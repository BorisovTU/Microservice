/*
$Name:        dvoposrp.mac
$Module:      ФИСС и КО
$Description: Отчет " Сделки по открытым позициям"
*/

import "dvrepfun.mac", "dvRepFun2.mac", RsbDataSet, "or_rep_h.mac", "dv_categ.mac", "dv_fun.mac", "dvserv.mac", "dldlngfun.mac";

const FontBold =  "ex_FS(b)";
const FontTitle = "ex_FS(b):ex_FN(Arial):ex_FZ(12)";

const NOTRETIRE = 0;
const RETIRE = 1;


/*
Филиал: <H0.1>
                                                         Отчет о требованиях/обязательствах на внебалансовом учете
Дата отчета:    <H0.2>

вид исходного актива   <H1.1>
исходный актив  <H2.1> <H2.2>
Вид контракта:  <H3.1>   
Контракт:       <H4.1> <H4.2>      
Базисный актив: <H4.3> <H4.4>, количество <H4.5>
Эмитент:        <H4.6> Дата исполнения:   <H4.7>       <H4.8>   <H4.9>  <H4.10>  <H4.11>   

*/


private var Table = "┌────────────────┬───────────────────┬────────────────┬────────┬────────────┬───────────────┬────────┬────────────────┬──────────────┬──────────────────────┐\n" +
                    "│  Номер сделки  │  Дата заключения  │    Контракт    │  Вид   │ Количество │ Цена сделки в │ Валюта │  Стоимость, в  │Расчетная цена│       Стоимость      │\n" +
                    "│                │                   │                │ сделки │            │  валюте цены  │  цены  │   валюте цены  │              │   по расчетной цене  │\n" +
                    "├────────────────┼───────────────────┼────────────────┼────────┼────────────┼───────────────┼────────┼────────────────┼──────────────┼──────────────────────┤";

private var TableErrors = "┌────────────────────────────────────────────────────────────────────────────────────────────────────┐\n" +
                          "│                                          Ошибки выполнения                                         │\n" +
                          "├────────────────────────────────────────────────────────────────────────────────────────────────────┤";



/* для обработки ошибок */
private var ErrorStr = 0;
private var Errors = TArray();
private var ErrorsReportLog = TRUE;
private var ReportData;
private var Dv_Deal;
private var Rep = CMakeReport(Table);

var IsID = -1;

CLASS SourceData( _Dprt:integer,
                  _DateRep:date,
                  _BaseFIKind:integer,
                  _BaseFIID:integer,
                  _DVFIKind:integer,
                  _IssuerID:integer,
                  _DVFIID:integer,
                  _IsApp:bool,
                  _IsExcel:bool )
   /* данные для отчета */
   var dateRep          = _DateRep,
       Dprt             = _Dprt,

       BaseFIKind       = _BaseFIKind,
       BaseFIID         = _BaseFIID,
       DVFIKind         = _DVFIKind,
       DVFIID           = _DVFIID,

       IssuerID         = _IssuerID,
       
       IsExcel          = _IsExcel,
       IsApp            = _IsApp,
       ReportRun        = false;

   var FirstSheet       = NULL;
   var Fltr             = "";

   if( BaseFIKind > 0 )
      Fltr = Fltr + " and t_BaseFIKind = " + String(BaseFIKind);
   end;

   if( IssuerID > 0 )
        IsID = IssuerID;
   else 
      IssuerID = -1;
   end;

   if( BaseFIID != ALLFININSTR )
      Fltr = Fltr + " and t_BaseFIID = " + String(BaseFIID);
   end;

   if( DVFIKind > 0 )
      Fltr = Fltr + " and t_FI_Kind = " + String(DVFIKind);
   end;

   if( DVFIID > 0 )
      Fltr = Fltr + " and t_FIID = " + String(DVFIID);
   end;

END;

/*------------------------------------------------------------------------------------------------------------------*/
macro ПИ_СИстекшимСроком()
  var sqlQuery;
   sqlQuery = "select t_FIID from dfininstr_dbt" +
              " where t_DrawingDate <= " + GetSQLDate( ReportData.dateRep ) + " and " +
              "       t_FI_Kind = " + String(FIKIND_DERIVATIVE);
   return sqlQuery;
end;

macro ПолучитьБиржевыеСделки( WhereCond:string )
  var sqlQuery;
   sqlQuery = "select t_Type, t_Date, t_Time, t_Code, t_FIID, t_Broker, t_Department," + 
              "       t_Amount, t_Position, t_Price, t_Bonus " +
              " from ddvdeal_dbt " +
              "where t_Client = " + String( -1 ) + " and " +
              //"      t_State = " + String(DVDEAL_CLOSE) + " and " +/*PNV 512793 непогашенные сделки не закрываются*/
              "      t_Hedge != '" + String(SET_CHAR) + "' and " +
              "      t_Date <= " + GetSQLDate( ReportData.dateRep );
   if( WhereCond )
      sqlQuery = sqlQuery + " and " + WhereCond;
   end;
   sqlQuery = sqlQuery + " order by t_Date, t_Time desc;";
  return TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
end;

macro ПолучитьПозиции( WhereCond:string )
  var sqlQuery;
   sqlQuery = "select t_ID, t_FIID, t_Department, t_Broker" + 
              " from ddvfipos_dbt "+
              "where t_Client = " + String( -1 );
   if( WhereCond )
      sqlQuery = sqlQuery + " and " + WhereCond;
   end;
   sqlQuery = sqlQuery + " order by t_Department, t_Broker, t_ID";
  return TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
end;

macro ПолучитьОстатокПоПозиции( FIID:integer, Broker:integer, Dprt:integer )
  var sqlQuery;
  var retVal;
   sqlQuery = RSDCommand("select t_LongPosition, t_ShortPosition" + 
              " from ddvfiturn_dbt " +
              "where t_Client = ? and " +
              "      t_Department = ? and " +
              "      t_Broker = ? and " +
              "      t_FIID = ? and " +
              "      t_Date <= ? " +
              " order by t_Date desc;");

  sqlQuery.addParam( "", RSDBP_IN, -1 );
  sqlQuery.addParam( "", RSDBP_IN, Dprt );
  sqlQuery.addParam( "", RSDBP_IN, Broker );
  sqlQuery.addParam( "", RSDBP_IN, FIID );
  sqlQuery.addParam( "", RSDBP_IN, ReportData.dateRep );
  sqlQuery.execute();
  
  retVal = TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
  retVal.MoveFirst();

  if( retVal.GetRecCount() > 0 )
     return retVal.ShortPosition - retVal.LongPosition;
  end;

  return 0.0;
end;

/* Добовление ошибки с массив*/
macro ErrorLog( msg:string )
   Errors[ErrorStr]  = msg;
   ErrorStr = ErrorStr + 1;
end;

/* печать */

macro PrintLog()
  var count = 0;
   if( ErrorsReportLog )
      Rep.AddNewSheetBreak("Ошибки", TableErrors);
      while( count < ErrorStr )

         Rep.AddPrintCell( Errors[count] , 0, 0, "l");
         Rep.AddStr();

         count = count + 1;
      end;
   end;
end;

/*------------------------------------------------------------------------------------------------------------------*/
/*
Филиал: <H0.1>
                                                     Сделки по открытым позициям
Дата отчета:    <H0.2>
*/
macro PrintHead0( Dprt, DateRep )

   if( ReportData.FirstSheet != NULL )
      Rep.AddNewSheetBreak( Dprt, Table );
   else
      ReportData.FirstSheet = Dprt;
   end;

   Rep.AddPrintCell( "Филиал:",                    10,                         0,  "l",            REP_ELEM_STR);
   Rep.AddPrintCell( Dprt,                         Rep.GetHeaderWidth() - 10 , 0,  "l:"+ FontBold, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddEmptyStr();
   Rep.AddPrintCell( "Сделки по открытым позициям",Rep.GetHeaderWidth(), 0, "c:" + FontTitle, REP_ELEM_STR);
   Rep.AddEmptyStr();
   Rep.AddStr();
   Rep.AddPrintCell( "Дата отчета:", 24, 0, "l", REP_ELEM_STR);
   Rep.AddPrintCell( DateRep,       Rep.GetHeaderWidth() - 24, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
end;
/*
вид исходного актива   <H1.1>
*/
macro PrintHead1( FI_KindID )
  var FI_Kind;
   if( FI_KindID == FIKIND_AVOIRISS )
      FI_Kind = "Ценная бумага";
   elif( FI_KindID == FIKIND_CURRENCY )
      FI_Kind = "Валюта";
   elif( FI_KindID == FIKIND_INDEX )
      FI_Kind = "Индекс";
   end;
   Rep.AddPrintCell( "вид исходного актива:", 24,                        0, "l",             REP_ELEM_STR);
   Rep.AddPrintCell( FI_Kind,                Rep.GetHeaderWidth() - 24, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
end;
/*
исходный актив  <H2.1> <H2.2>
*/
macro PrintHead2( FICode, FIName )
   Rep.AddPrintCell( "исходный актив:", 24,                                        0, "l",             REP_ELEM_STR);
   Rep.AddPrintCell( FICode,           StrLen(FICode) + 2,                         0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddPrintCell( FIName,           Rep.GetHeaderWidth() - StrLen(FICode) - 29, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
end;
/*
Вид контракта:  <H3.1>   
*/
macro PrintHead3( FI_Kind )
   Rep.AddPrintCell( "вид контракта:", 24,                        0, "l",             REP_ELEM_STR);
   Rep.AddPrintCell( FI_Kind,          Rep.GetHeaderWidth() - 24, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
end;
/*
Контракт:       <H4.1> <H4.2>      
Базисный актив: <H4.3> <H4.4>, количество <H4.5>
Эмитент:        <H4.6> Дата исполнения:   <H4.7>       <H4.8>   <H4.9>  <H4.10>  <H4.11>   
*/
macro PrintHead4( FI:ПроизводныйИнструмент )
   Rep.AddPrintCell( "контракт:",           24,                                             0, "l",             REP_ELEM_STR);
   Rep.AddPrintCell( FI.FI.FI_Code,            StrLen(FI.FI.FI_Code) + 2,                         0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddPrintCell( FI.FI.Name   ,            Rep.GetHeaderWidth() - StrLen(FI.FI.FI_Code) - 26, 0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();

   Rep.AddPrintCell( "Базисный актив:",           24,                                                                0, "l",             REP_ELEM_STR);
   Rep.AddPrintCell( FI.BasisFI.FI_Code,          StrLen(FI.BasisFI.FI_Code) + 2,                                        0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddPrintCell( FI.BasisFI.Name,             StrLen(FI.BasisFI.Name) + 2,                                        0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddPrintCell( ", количество",              16,                                                                0, "l",             REP_ELEM_STR);
   Rep.AddPrintCell( Double(FI.FI.FaceValue),     Rep.GetHeaderWidth() - StrLen(FI.BasisFI.Name+FI.BasisFI.FI_Code) - 44, SetPrecisionByFractPart(Double(FI.FI.FaceValue), FI.BasisFI.SumPrecision, 0), "l:" + FontBold, REP_ELEM_STR);
   Rep.AddStr();
   Rep.AddPrintCell( "Эмитент:",                  24,                                                  0, "l",             REP_ELEM_STR);
   Rep.AddPrintCell( FI.IssuerCode(),             StrLen(FI.IssuerCode()) + 2,                         0, "l:" + FontBold, REP_ELEM_STR);
   Rep.AddPrintCell( "Дата исполнения:",          16,                                                  0, "l",             REP_ELEM_STR);
   Rep.AddPrintCell( Date(FI.FI.DrawingDate),     16,                                                  0, "l:" + FontBold, REP_ELEM_STR);

   if( FI.FI.AvoirKind == DERIVATIVE_OPTION )
      Rep.AddPrintCell( "Тип опциона",              12,                          0, "l",             REP_ELEM_STR);
      Rep.AddPrintCell( FI.OptionType(),            StrLen(FI.OptionType())+5 ,  0, "l:" + FontBold, REP_ELEM_STR);
      Rep.AddPrintCell( "Страйк",                   12,                          0, "l",             REP_ELEM_STR);
      Rep.AddPrintCell( FI.DV.Strike,               16,                          2, "r:" + FontBold, REP_ELEM_STR);
      Rep.AddPrintCell( FI.StrikeCCY(),             5,                           0, "c:" + FontBold, REP_ELEM_STR);
   else
      Rep.AddPrintCell( "",                      Rep.GetHeaderWidth() - StrLen(FI.IssuerCode()) - 58, 0, "l", REP_ELEM_STR);
   end;

   Rep.AddStr();

end;
/*
┌────────────────┬───────────────────┬────────────────┬────────┬────────────┬───────────────┬────────┬────────────────┬──────────────┬──────────────────────┐
│  Номер сделки  │  Дата заключения  │    Контракт    │  Вид   │ Количество │ Цена сделки в │ Валюта │  Стоимость, в  │Расчетная цена│       Стоимость      │
│                │                   │                │ сделки │            │  валюте цены  │  цены  │   валюте цены  │              │   по расчетной цене  │
├────────────────┼───────────────────┼────────────────┼────────┼────────────┼───────────────┼────────┼────────────────┼──────────────┼──────────────────────┤
*/
macro PrintLine( DealCode, DealDate, FICode, DealType, Amount, Price, PriceFI, CalcPrice )

   Rep.AddPrintCell( DealCode,                0, 0, "c:" + FontBold);
   Rep.AddPrintCell( DealDate,                0, 0, "c");
   Rep.AddPrintCell( FICode,                  0, 0, "c");
   Rep.AddPrintCell( DealType,                0, 0, "c");

   if( ReportData.IsApp )
       Rep.AddPrintCell( Double(Amount),      0, 0, "r:a");
       Rep.AddPrintCell( Price,               0, 0, "r:a");
   else
       Rep.AddPrintCell( Double(Amount),      0, 0, "r");
       Rep.AddPrintCell( Price,               0, 0, "r");
   end;
   
   Rep.AddPrintCell( PriceFI,                 0, 0, "c");

   if( ReportData.IsApp )
       Rep.AddPrintCell( Money(Amount*Price), 0, 0, "r:a");
       Rep.AddPrintCell( CalcPrice,           0, 0, "r:a");
       Rep.AddPrintCell( Money(Amount*CalcPrice), 0, 0, "r:a");
   else
       Rep.AddPrintCell( Money(Amount*Price), 0, 0, "r");
       Rep.AddPrintCell( CalcPrice,           0, 0, "r");
       Rep.AddPrintCell( Money(Amount*CalcPrice), 0, 0, "r");
   end;
   Rep.AddStr();
end;

/*------------------------------------------------------------------------------------------------------------------*/
macro СохранитьСделку( Dprt, PosID, FIID, Contractor, DealCode, DealDate, DealTime, State, IsBuy, 
                                                        Amount, Price, Bonus )
  var Cost_NC:money;
  var Price_NC:money;
  var Bonus_NC:money;
  var MinPrice = 0.0, MaxPrice = 0.0, CalcPrice = 0.0, SinceDate;
  var CorrSumm = 0.0, PMargin = 0.0, GMargin = 0.0;
  var FI = ПроизводныйИнструмент( FIID );

   if( NOT ПолучитьКурсЗаданногоTипа( @CalcPrice, FIID, FI.PriceFIID(), ПИ_ПолучитьВидКурса(RATETYPE_CALC_PRICE), Date(DealDate), false, @SinceDate ) )
      ErrorLog( "Невозможно получить курс вида 'Расчетная цена' для FIID = " + String(FIID) + " на дату " + String(DealDate) );
      CalcPrice = 0.0;
   end;

/*Цена сделки в валюте цены (для опционов - премия за 1 контракт). Для операций исполнения опционов не заполняется*/
   if( ПолучитьТипПИ( FIID ) == DERIVATIVE_OPTION )
      Price = Bonus;
   end;

   Dv_Deal.Clear();
   Dv_Deal.rec.BaseFIKind    = FI.InitialFI.FI_Kind;
   Dv_Deal.rec.BaseFIID      = FI.InitialFI.FIID;
   Dv_Deal.rec.FI_Kind       = FI.FI.AvoirKind;
   Dv_Deal.rec.FIID          = FIID;
   Dv_Deal.rec.Contractor    = Contractor;
   Dv_Deal.rec.Department    = Dprt;
   Dv_Deal.rec.Position      = PosID;
   Dv_Deal.rec.DealCode      = DealCode;
   Dv_Deal.rec.DealDate      = Date(DealDate);
   Dv_Deal.rec.DealTime      = Time(DealTime);
   Dv_Deal.rec.State         = State;
   if( IsBuy )
      Dv_Deal.rec.IsBuy      = 1;
   else
      Dv_Deal.rec.IsBuy      = 0;
   end;
   Dv_Deal.rec.Amount        = Amount;
   Dv_Deal.rec.Price         = Price;
/*   Dv_Deal.rec.PriceFIID     = FI.PriceFIID();*/
   Dv_Deal.rec.Bonus         = Bonus;
   Dv_Deal.rec.BonusFIID     = FI.BonusFIID();
   Dv_Deal.rec.Cost          = Amount * Price;

   Dv_Deal.rec.MinPrice      = MinPrice;
   Dv_Deal.rec.MaxPrice      = MaxPrice;
   Dv_Deal.rec.CalcPrice     = CalcPrice;

   Dv_Deal.rec.CorrSumm      = CorrSumm;
   Dv_Deal.rec.PMargin       = PMargin;
   Dv_Deal.rec.GMargin       = GMargin;
   
   if( not Dv_Deal.Insert() )
      RunError( "Ошибка при вставке данных во временный файл отчета.");
   end;

   ReportData.ReportRun = true;

end;


macro ЗаполнитьВременныйФайлСделок()

  private macro IsBuy( Type, Pos )
     if( Type == "B" )
        return true;
     elif( Type == "S" )
        return false;
     elif( Type == "E" )
          if( Pos == 1 )
             return true;
          elif( Pos == 2 )
             return false;
          end;
     end;
     return false;
  end;                           

  var DVDeal = NULL, DVPos = NULL;
  var NotEofPos, NotEofDeal, Contractor, NettoPos;
  var Progress = TProgressBar;
  var Amount, State;

/*Разбираемся с оставшимися сделками*/
   DVPos = ПолучитьПозиции("t_FIID not in (" + ПИ_СИстекшимСроком() + ") and (t_State = " + String(DVPOS_OPEN) + " or t_CloseDate > " + GetSQLDate( ReportData.dateRep ) + " )" );
   DVPos.MoveLast();

   if( DVPos.GetRecCount() > 0 )
      Progress.Init( DVPos.GetRecCount(), "Отбор биржевых сделок", "Отбор непогашеннных биржевых сделок" );
      NotEofPos = DVPos.MoveFirst();
 
      while( NotEofPos )
        /*получаем место учета ПИ*/
        if( DVPos.Broker > 0 )
           Contractor = DVPos.Broker;
        else
           Contractor = ПолучитьЭмитентаПИ( DVPos.FIID );
        end;

        DVDeal = ПолучитьБиржевыеСделки( "t_FIID = " + String(DVPos.FIID) +
                                         " and t_Department = " + String(DVpos.Department) +
                                         " and t_Broker = " + String(DVPos.Broker) );
        DVDeal.MoveLast();
        if( DVDeal.GetRecCount() > 0 )

           NettoPos = ПолучитьОстатокПоПозиции( DVPos.FIID, DVPos.Broker, DVPos.Department );

           NotEofDeal = DVDeal.MoveFirst();
           while( NotEofDeal )

              if( ( (ПолучитьЭмитентаПИ(DVDeal.FIID) == IsID) AND (IsID > 0) ) OR (IsID < 0))
                 if( NettoPos < 0 )
                    /* Если длинные позиции то отбираем покупки */
                    if( ( (Abs(NettoPos)-DVDeal.Amount) >= 0 ) and (IsBuy(DVDeal.Type, DVDeal.Position)==true) )
                       State = NOTRETIRE;
                       Amount = DVDeal.Amount;
                       NettoPos = NettoPos + Amount;
                    elif( ( (Abs(NettoPos)-DVDeal.Amount) < 0 ) and (IsBuy(DVDeal.Type, DVDeal.Position)==true) )
                       /*плохой случай приходится бить сделку на две*/
                       /*Здесь будет сохранена виртуальная сделка*/
                       СохранитьСделку( DVDeal.Department, DVPos.ID, DVDeal.FIID, Contractor,
                                        DVDeal.Code, Date(DVDeal.Date), Time(DVDeal.Time),
                                        NOTRETIRE,
                                        IsBuy(DVDeal.Type, DVDeal.Position),
                                        Abs(NettoPos), DVDeal.Price, DVDeal.Bonus );
                       State = RETIRE;
                       Amount = DVDeal.Amount - Abs(NettoPos);
                       NettoPos = 0;
                    else
                       State = RETIRE;
                       Amount = DVDeal.Amount;
                    end;
                 elif( NettoPos > 0 )
                    /* Если короткие позиции то отбираем покупки */
                    if( ( (Abs(NettoPos)-DVDeal.Amount) >= 0 ) and ((not(IsBuy(DVDeal.Type, DVDeal.Position)))==true) )
                       State = NOTRETIRE;
                       Amount = DVDeal.Amount;
                       NettoPos = NettoPos - Amount;
                    elif( ( (Abs(NettoPos)-DVDeal.Amount) < 0 ) and ((not(IsBuy(DVDeal.Type, DVDeal.Position)))==true) )
                       /*плохой случай приходится бить сделку на две*/
                       /*Здесь будет сохранена виртуальная сделка*/
                       СохранитьСделку( DVDeal.Department, DVPos.ID, DVDeal.FIID, Contractor,
                                        DVDeal.Code, Date(DVDeal.Date), Time(DVDeal.Time),
                                        NOTRETIRE,
                                        IsBuy(DVDeal.Type, DVDeal.Position),
                                        Abs(NettoPos), DVDeal.Price, 
                                        DVDeal.Bonus );
                       State = RETIRE;
                       Amount = DVDeal.Amount - Abs(NettoPos);
                       NettoPos = 0;
                    else
                       State = RETIRE;
                       Amount = DVDeal.Amount;
                    end;
                 elif( NettoPos == 0 )
                      State = RETIRE;
                      Amount = DVDeal.Amount;
                 end;
                 if( State == NOTRETIRE )
                    СохранитьСделку( DVDeal.Department, DVPos.ID, DVDeal.FIID, Contractor,
                                     DVDeal.Code, Date(DVDeal.Date), Time(DVDeal.Time),
                                     State,
                                     IsBuy(DVDeal.Type, DVDeal.Position),
                                     Amount, DVDeal.Price,
                                     DVDeal.Bonus );
                 end;
              end;
              NotEofDeal = DVDeal.MoveNext();
           end;
        end;

        NotEofPos = DVPos.MoveNext();
        Progress.Use();
      end;
      Progress.Remove();
   end;
end;
/*------------------------------------------------------------------------------------------------------------------*/
macro ПечататьФилиала()

  var FI = ПроизводныйИнструмент();
  var NotEof;
  var FD_Pos:DVFirstDocPos;
  var PrintFIID, PrintFIKind;
  var PrintBaseFIID, PrintBaseFIKind;
  var PosID, RestAcc, Account, Netto, Demand;


   Dv_Deal.Clear();
   NotEof = Dv_Deal.GetGE();

   PrintHead0( ПолучитьКодФилиала( Dv_Deal.rec.Department ), ReportData.DateRep ); 

   while( NotEof )
     FI.Init( Dv_Deal.rec.FIID );
     if( PrintBaseFIKind != Dv_Deal.rec.BaseFIKind )
        PrintHead1( FI.InitialFI.FI_Kind );
        PrintBaseFIKind = Dv_Deal.rec.BaseFIKind;
        PrintBaseFIID = NULL;
        PrintFIKind = NULL;
        PrintFIID = NULL;
     end;
     if( PrintBaseFIID != Dv_Deal.rec.BaseFIID )
        PrintHead2( FI.InitialFI.FI_Code, FI.InitialFI.Name );
        PrintBaseFIID = Dv_Deal.rec.BaseFIID;
        PrintFIKind = NULL;
        PrintFIID = NULL;
     end;
     if( PrintFIKind != ПолучитьТипПИ( Dv_Deal.rec.FIID ) )
        PrintHead3( FI.AvoirKind() );
        PrintFIKind = FI.FI.AvoirKind;
        PrintFIID = NULL;
     end;
     if( PrintFIID != Dv_Deal.rec.FIID )
        PrintHead4( FI );
        PrintFIID = Dv_Deal.rec.FIID;
     end;
     if( PosID != Dv_Deal.rec.Position )
        PosID = Dv_Deal.rec.Position;
     end;

     PrintLine( Dv_Deal.rec.DealCode,
                Dv_Deal.rec.DealDate,
                FI.FI.FI_Code,
                IIF(Dv_Deal.rec.IsBuy==1, "покупка", "продажа"), 
                Dv_Deal.rec.Amount,
                Dv_Deal.rec.Price,
                GetCCY( FI.PriceFIID() ),
                Dv_Deal.rec.CalcPrice );


     NotEof = Dv_Deal.Next();
   end;
end;

macro PrintRep()
  var sqlQuery, Dprt;
  var NotEof;

   sqlQuery = "select distinct( t_Department ) as Dprt from ddvtaxreg_tmp";

   if( ReportData.Dprt > 0 )
      Dv_Deal.AddFilter( "t_Department = " + String(ReportData.Dprt) + ReportData.Fltr );
        if( Dv_Deal.NRecords() > 0 )
         ПечататьФилиала();
         after_44_footer( Rep );
      else
         MsgBox("\nНет данных, удовлетворяющих параметрам отчета.");
      end;
      Dv_Deal.DropFilter();
      PrintLog();
   else
      Dprt = TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
      Dprt.MoveLast();

      if( Dprt.GetRecCount() > 0 )
         NotEof = Dprt.MoveFirst();
         while( NotEof )

           Dv_Deal.AddFilter( "t_Department = " + String(Dprt.Dprt) + ReportData.Fltr );
             if( Dv_Deal.NRecords() > 0 )
              ПечататьФилиала();
              after_44_footer( Rep );
           else
              MsgBox("\nНет данных, удовлетворяющих параметрам отчета.");
           end;
           Dv_Deal.DropFilter();

           NotEof = Dprt.MoveNext();
         end;
         PrintLog();
      end;
   end;
end;



macro ReportRun( _Dprt:integer,
                 _DateRep:date,
                 _BaseFIKind:integer,
                 _BaseFIID:integer,
                 _DVFIKind:integer,
                 _IssuerID:integer,
                 _DVFIID:integer,
                 _IsApp:bool,
                 _IsExcel:bool )

     Dl_CallInsertStat( IIF( _IsExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
     ReportData = SourceData(_Dprt, _DateRep, _BaseFIKind, _BaseFIID, _DVFIKind, _IssuerID,
                             _DVFIID, _IsApp, _IsExcel);

     Dv_Deal = TBFile( "dvtaxreg.tmp", "W", 1 );

     ClearGlobalTmp( "ddvtaxreg_tmp" );
     ЗаполнитьВременныйФайлСделок();
     

   if( ReportData.ReportRun == false )
      MsgBox("\nНет данных, удовлетворяющих параметрам отчета.");
   else
     PrintRep();
     if( ReportData.FirstSheet != NULL )
        if( _IsExcel )
           Rep.PrintWinRep(ReportData.FirstSheet);
           Rep.ShowWinRep();
        else
           Rep.PrintRep();
        end;
     end;
   end;
end;
