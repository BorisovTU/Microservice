/*
$Name:        dvfx005.mac
$Module:      Производные инструменты
$Description: Конверсионная операция. Шаг "Настройка операции"
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac";

macro executeParamStep( kind_oper, BOf_kind, FDoc )
  record deal(dvndeal);
  var    FD, FD2;

  SetBuff(deal, FDoc);
  FD = DVFirstDocFXDeal(DL_DVFXDEAL, deal);


  DL_CalendDefineOprDate(FD, DV_FXDEAL_OPRDATE_BEGIN_OP, FD.ДатаСделки());
  if ((FD.ДатаТребования() - FD.ДатаОбязательства()) < 0)
      DL_CalendDefineOprDate(FD, DV_FXDEAL_OPRDATE_EXEC,     FD.ДатаТребования());
  elif ((FD.ДатаТребования() - FD.ДатаОбязательства()) > 0) 
      DL_CalendDefineOprDate(FD, DV_FXDEAL_OPRDATE_EXEC,     FD.ДатаОбязательства());
  elif ((FD.ДатаТребования() - FD.ДатаОбязательства()) == 0)
      DL_CalendDefineOprDate(FD, DV_FXDEAL_OPRDATE_EXEC,     FD.ДатаИсполнения());
  end;
  DL_CalendDefineOprDate(FD, DV_FXDEAL_OPRDATE_REQ,      FD.ДатаТребования());
  // KS 25.04.2019 Если ДатаОбязательства выпадает на выходной, то поставить в дату шагов предыдущий рабочий день
  var isEastCur = 0;
  if (not GetMainObjAttr( null, 2, strLpad(string(FD.nfi.rec.fiid), 10, "0"), 101, isEastCur))
    isEastCur = 0;
  end;
  if (((isHoliday(FD.ДатаОбязательства()))or(isEastCur == 1)) and (GetDateAfterWorkDays(FD.ДатаОбязательства(),-1) >= FD.ДатаСделки()))
    DL_CalendDefineOprDate(FD, DV_FXDEAL_OPRDATE_OBL,      GetDateAfterWorkDays(FD.ДатаОбязательства(),-1));
  else // иначе - старый алгоритм
    DL_CalendDefineOprDate(FD, DV_FXDEAL_OPRDATE_OBL,      FD.ДатаОбязательства());
  end;
  DL_CalendDefineOprDate(FD, DV_FXDEAL_OPRDATE_COMISS,   DL_GetMinComPlanDateByOper(FD.Kind, FD.ID));

  if( FD.IsSWAP() )
     FD2 = DVFirstDocFXDeal(DL_DVFXDEAL, deal, 2);

     DL_CalendDefineOprDate(FD, DV_FXDEAL_OPRDATE_EXEC_2, FD2.ДатаИсполнения());
     DL_CalendDefineOprDate(FD, DV_FXDEAL_OPRDATE_REQ_2,  FD2.ДатаТребования());
     DL_CalendDefineOprDate(FD, DV_FXDEAL_OPRDATE_OBL_2,  FD2.ДатаОбязательства());
     DL_CalendDefineOprDate(FD, DV_FXDEAL_OPRDATE_CLOSE,  FD2.ДатаЗавершения());
  else
     DL_CalendDefineOprDate(FD, DV_FXDEAL_OPRDATE_CLOSE,  FD.ДатаЗавершения());
  end;

  if( ПИ_ИнтегрРежимРаботы() and FD.IsBNote() )
     if( not УстановитьКатегориюПрихКассСимвол(FD, OBJTYPE_FXOPER_DV, FD.ДатаСделки()) )
        return 1;
     end;
     if( not УстановитьКатегориюРасхКассСимвол(FD, OBJTYPE_FXOPER_DV, FD.ДатаСделки()) )
        return 1;
     end;
  end;

  return 0;
end;
