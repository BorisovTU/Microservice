/*
$Name:        dv_period.mac
$Module:      Производные инструменты
$Description: Процедуры для выполнения переносов по срокам
*/
IMPORT CTInter, "dv_categ.mac", "dv_ground.mac", "dv_car.mac", "dltransf.mac";

private record СчетУчета(account);
private record СчетУчетаТреб(account);
private record СчетУчетаОбяз(account);
private record СчетУчетаНов(account);
private record СчетУчетаТребНов(account);
private record СчетУчетаОбязНов(account);
PRIVATE RECORD AccDocNew(mcaccdoc);

CONST TRANSFERINEXECDATE_PARM = "COMMON\\КАТЕГОРИИ СРЕДСТВ\\ПЕРЕНОС ПО СРОКАМ В ДАТУ ТО";
// dan>
PRIVATE MACRO IsCategExist(Категория)
  var cmd, n_rec;
  cmd= DL_RsdCommand("Select t_id from dmccateg_dbt where t_code like ?");
  cmd.AddParam (Категория);
  n_rec = cmd.GetCount();
  return n_rec;
END;
// <dan

private MACRO GetTransferInExecDate()
  var transferInExecDate, ErrCode;

  GetRegistryValue( TRANSFERINEXECDATE_PARM, V_BOOL, transferInExecDate, ErrCode );
  transferInExecDate = IIF(ErrCode != 0, false, transferInExecDate);

  return transferInExecDate;
END;

private macro ПИ_ПереносПоСрокамВДатуТО(TransfDate,ExecDate)
  if( TransfDate >= ExecDate )
     return IIF(GetTransferInExecDate(),ExecDate,date(0,0,0));
  end;
  return TransfDate;
end;

private macro FindPrevPeriod( ID, FindPrevPeriod )
  file Period( mcperiod ) key 0;
  file PrevPeriod( mcperiod ) key 3;

  ClearRecord( Period );
  Period.ID = ID;
  if( GetEQ( Period ) )     
     copy( PrevPeriod, Period );
     PrevPeriod.Number = PrevPeriod.Number - 1;
     if( GetEQ(PrevPeriod) )
        Copy( FindPrevPeriod, PrevPeriod );
        return 0;
     end;
  end; 
  return 8400; 
end;

private macro GetPidByAcc(acc):integer
    var pid = "",sql,rs;
      pid = SubStr(acc,4,2);
    if (pid != "")
      sql = "SELECT p.t_id FROM dmcperiod_dbt p WHERE p.t_id BETWEEN 43 AND 53 AND p.t_periodcode = "+pid;
      rs = ExecSqlSelect(sql);
      if(rs.MoveNext())
        return rs.value(0);
      end;
    end;
    return 0;
  end;

/* OprDate  - Дата операции, шага
   DateFin  - базовая дата (дата исполнения фьючерса)  (параметр MC_TYPE_PARAMETR_FINDATE для роли FIROLE_FIREQ/FIROLE_FICOM)*/ 
MACRO ПИ_НужноПереноситьПоСрокам( FD, Account, OprDate, DateBegin, DocID, FIID, FIRole, ActivateDate, Department, pid, ODate:date, ActionDate_ )
  record Period(mcperiod);
  var    stat, NeedTransf = false;
  var    ActionDate:date;

  ActivateDate = ActionDate = Date(0,0,0);

  if( pid ) 
     stat = FindPrevPeriod( pid, Period );
  else 
     stat = MC_GetPeriodForCurrAcc( Account, FD.Kind, DocID, FD.GetBasisFIRole(FIRole), FIID, MC_PERIOD_PREV, false, Period, Department ); 
  end;

  if( stat == 0 )

     ActionDate   = GetTransferActionDate( Period, OprDate, DateBegin );
     ActivateDate = GetTransferActivateDate( Period, ActionDate );

     if( (ActivateDate != Date(0,0,0)) and ((ODate == null) or (ActivateDate <= ODate)) )
        NeedTransf = true;
     end;

  elif (stat == 8400)
     stat = 0;
  end;

  SetParm( 7, ActivateDate );
  SetParm( 11, ActionDate );

  return NeedTransf;
END;

/*
Получить даты переноса по срокам.
  OprDate   - дата операции
  DateBeginPlus - Базовая дата требований (параметр MC_TYPE_PARAMETR_FINDATE для роли FIROLE_FIREQ)
  DateBeginMinus- Базовая дата обязательств (параметр MC_TYPE_PARAMETR_FINDATE для роли FIROLE_FICOM)
*/
PRIVATE MACRO GetTransferDates( FD:VARIANT, OprDate:DATE, КатУчетаТреб:STRING, КатУчетаОбяз:STRING, DateBeginPlus:@DATE, DateBeginMinus:@DATE )
   DateBeginPlus  = FD.GetParametr(MC_TYPE_PARAMETR_FINDATE, OprDate, КатУчетаТреб, FIROLE_FIREQ);
   DateBeginMinus = FD.GetParametr(MC_TYPE_PARAMETR_FINDATE, OprDate, КатУчетаОбяз, FIROLE_FICOM);
END;

PRIVATE MACRO minDate( D1, D2 )
   if( D1 < D2 ) return D1;
   else return D2;
   end;
END;

MACRO ПеренестиСчетПоСрокамБирж( Code, FD, Dat:DATE, Doc:MEMADDR, NoErr:BOOL, КатУчета:STRING, AccFIID:INTEGER, FIRoleFI:INTEGER, IsReq:bool ):BOOL

  var TransfDatePM:date, BegDate:date, FinDate:date;

// dan> Провереим факт существования переданной КУ
  if(not IsCategExist(КатУчета))
    return true;
  end;
// <dan
  if( not FD.IsExistAccount( КатУчета, null, NoErr, FIRoleFI, СчетУчета, null, Dat, AccFIID ) )
     return true; /*перенос не нужен*/
  end;

  BegDate = FD.GetParametr(MC_TYPE_PARAMETR_BEGDATE, Dat, КатУчета, FD.GetBasisFiRole(FIROLE_UNDEF));
  FinDate = FD.GetParametr(MC_TYPE_PARAMETR_FINDATE, Dat, КатУчета, FD.GetBasisFiRole(FIROLE_UNDEF));

  if( ПИ_НужноПереноситьПоСрокам( FD, КатУчета, BegDate, FinDate, FD.ID, AccFIID, FIRoleFI, TransfDatePM, FD.Department(), null, Dat ) )
     if( not FD.ActualCheckPeriod( КатУчета, null, null, FIRoleFI, СчетУчетаНов, TransfDatePM, AccFIID, null, null, TransfDatePM ) )
        return false;
     end;  
  else
     return true;
  end;

  if( СчетУчета.Account == СчетУчетаНов.Account )
     if( (NoErr != null) AND (NoErr == true) )
        return true;
     else     
        MsgBox("На дату " + string(TransfDatePM) + " перенос не требуется.");
        return false;
     end; 
  else

     if( КОРРЕСП_ГЛАВА_Г() == true )

        if( not ПроводкаПоКатегориямУчета( FD, 
                                           IIF(IsReq, СчетУчетаНов, СчетУчета), /* Деб */
                                           IIF(IsReq, СчетУчета, СчетУчетаНов), /* Кредит */
                                           TransfDatePM,                                         /* Дата */
                                           СчетУчета.Chapter,                                    /* Глава */
                                           СчетУчета.Code_Currency,                              /* Валюта */
                                           ABS(DL_GetRestAccount(СчетУчета, TransfDatePM)),         /* Сумма */
                                           Doc,                                     /* Документ */
                                           INPCARRY,                                /* Result_Carry */
                                           "",                                      /* Kind_Oper */
                                           Code,                                    /* Numb_Document */
                                           FD.ОснованиеПереносаПоСрокам(IsReq)  /* Ground */
                                         )
          )
            return false;
        end;

     else

        var RestTrAcc = ABS(DL_GetRestAccount(СчетУчета, TransfDatePM)), Sum1, Sum2, DebFIID, CredFIID, DebFiRole, CredFiRole;

        /*здесь параметры для СчКорреспГлаваГ одинаковы для двух проводок*/
        if( IsReq )
           Sum1       = null;
           DebFIID    = null;
           Sum2       = RestTrAcc;
           CredFIID   = СчетУчета.Code_Currency;
           DebFiRole  = FIROLE_CORACC_ACTIVE;
           CredFiRole = null;
        else
           Sum1       = RestTrAcc;
           DebFIID    = СчетУчета.Code_Currency;
           Sum2       = null;
           CredFIID   = null;;
           DebFiRole  = null;
           CredFiRole = FIROLE_CORACC_PASSIVE;
        end;

        FD.SetFIIDForCorAccNum(СчетУчета.Code_Currency);
        if( not ПроводкаПоКатегориямУчета( FD, 
                                           IIF(IsReq, "СчКорреспГлаваГ", СчетУчета), /* Деб */
                                           IIF(IsReq, СчетУчета, "СчКорреспГлаваГ"), /* Кредит */
                                           TransfDatePM,                                         /* Дата */
                                           СчетУчета.Chapter,                                    /* Глава */
                                           DebFIID,                              /* Валюта */
                                           Sum1,         /* Сумма */
                                           Doc,                                     /* Документ */
                                           INPCARRY,                                /* Result_Carry */
                                           "",                                      /* Kind_Oper */
                                           Code,                                    /* Numb_Document */
                                           FD.ОснованиеПереносаПоСрокам(IsReq),  /* Ground */
                                           null, DebFiRole, CredFiRole, null, null,
                                           CredFIID, Sum2
                                         )
          )
            return false;
        end;

        if( IsReq )
           Sum1       = RestTrAcc;
           DebFIID    = СчетУчета.Code_Currency;
           Sum2       = null;
           CredFIID   = null;
           DebFiRole  = null;
           CredFiRole = FIROLE_CORACC_ACTIVE;
        else
           Sum1       = null;
           DebFIID    = null;
           Sum2       = RestTrAcc;
           CredFIID   = СчетУчета.Code_Currency;
           DebFiRole  = FIROLE_CORACC_PASSIVE;
           CredFiRole = null;
        end;         

        FD.SetFIIDForCorAccNum(СчетУчета.Code_Currency);
        if( not ПроводкаПоКатегориямУчета( FD, 
                                           IIF(IsReq, СчетУчетаНов, "СчКорреспГлаваГ"), /* Деб */
                                           IIF(IsReq, "СчКорреспГлаваГ", СчетУчетаНов), /* Кредит */
                                           TransfDatePM,                                         /* Дата */
                                           СчетУчета.Chapter,                                    /* Глава */
                                           DebFIID,                              /* Валюта */
                                           Sum1,         /* Сумма */
                                           Doc,                                     /* Документ */
                                           INPCARRY,                                /* Result_Carry */
                                           "",                                      /* Kind_Oper */
                                           Code,                                    /* Numb_Document */
                                           FD.ОснованиеПереносаПоСрокам(IsReq),  /* Ground */
                                           null, DebFiRole, CredFiRole, null, null,
                                           CredFIID, Sum2
                                         )
          )
            return false;
        end;

     end;
  end;

  return true;
END;

PRIVATE MACRO ПеренестиСчетПоСрокамВнебирж( FD, Dat:DATE, Doc:MEMADDR, NoErr:BOOL, КатУчета:STRING, FIRole:INTEGER,
                                            AccFIID:INTEGER, TransfDate:@DATE, FIRoleFI:INTEGER, ExecDate:DATE, Change:BOOL, NewAccount:@STRING, SuplDate:DATE ):BOOL
  var DealFD, IsPmGr:bool = false;
  var TransfDatePM, DateBeginPM, ActionDate;
  var pid;

  if( FD.Kind == DL_DVNDEALPMGR )
     DealFD = FD.DealFD;
     IsPmGr = true;
     NewAccount = "";
  else
     DealFD = FD;
  end;

  TransfDate = date(0,0,0);

  if( IsPmGr )
     if( not DV_GetAccount(4, AccFIID, IIF(FIRole == FIROLE_FIREQ, FD.PmGr.rec.DemandAccount, FD.PmGr.rec.LiabilityAccount), СчетУчета, NoErr) )
        return true; /*перенос не нужен*/
     end;
  else
     if( not FD.IsExistAccount( КатУчета, null, NoErr, FIRoleFI, СчетУчета, null, ExecDate, AccFIID ) )
        return true; /*перенос не нужен*/
     end;
  end;

  DateBeginPM = FD.GetParametr(MC_TYPE_PARAMETR_FINDATE, ExecDate, КатУчета, FD.GetBasisFiRole(FIRole));

  if (DEALFD.IsMigrDeal())
    pid = GetPidByAcc(СчетУчета.Account);
  else
    pid = null;
  end;

  if( ПИ_НужноПереноситьПоСрокам(FD, КатУчета, Dat, DateBeginPM, FD.ID, AccFIID, FIRoleFI, TransfDatePM, FD.Department(), null, ExecDate, ActionDate) or 
      (GetTransferInExecDate() and (ExecDate == SuplDate)))
     if (ActionDate == "") /*PNV ошибка "Не задан параметр дата действия"*/
        ActionDate = ExecDate;
    end;
     if( not FD.ActualCheckPeriod(КатУчета, null, null, FIRoleFI, СчетУчетаНов, IIF(Change, ExecDate, ActionDate), AccFIID, null, AccDocNew, ExecDate) )
        return false;
     end;
  else
     TransfDate = TransfDatePM;
     return true;
  end;

  if( СчетУчета.Account == СчетУчетаНов.Account )
     if( (NoErr != null) AND (NoErr == true) )
        return true;
     else
        MsgBox("На дату " + string(TransfDatePM) + " перенос не требуется.");
        return false;
     end;
  else
     if( IsPmGr )
        NewAccount = СчетУчетаНов.Account;
     end;

     var Sum:money = $0.0;
     if( (not IsPmGr) and FD.OpenAccByGenAgr() )
        Sum = IIF(FIRole == FIROLE_FIREQ, FD.ReqSum(), FD.ComSum());

        var НКР = DL_ПолучитьПоследнююСумму(FD.Kind, FD.ID, DLSUM_KIND_SUM_DV_NKR);
        if( FD.IsForward() or FD.IsOption() )
           if( FD.IsSale_BuyPutOrSaleCall() )
              if( FIRole == FIROLE_FICOM )
                 Sum = Sum + НКР;
              end;
           else
              if( FIRole == FIROLE_FIREQ )
                 Sum = Sum + НКР;
              end;
           end;
        end;
     else
        if( DealFD.IsPctSWAP() )
           Sum = IIF(FIRole == FIROLE_FIREQ, FD.ReqSum(), FD.ComSum());
        else
           Sum = DL_GetRestAccount(СчетУчета, ExecDate);
        end;

        /*PNV 509505*/
        if (Sum > DL_GetRestAccount(СчетУчета, ExecDate))
           Sum = DL_GetRestAccount(СчетУчета, ExecDate); 
        end;
        /*PNV 509505*/

     end;

     if( КОРРЕСП_ГЛАВА_Г() == true )
        if( not ПроводкаПоКатегориямУчета( DealFD,
                                           IIF(FIRole == FIROLE_FIREQ, СчетУчетаНов, СчетУчета), /* Деб */
                                           IIF(FIRole == FIROLE_FIREQ, СчетУчета, СчетУчетаНов), /* Кредит */
                                           ExecDate,                                /* Дата */
                                           СчетУчета.Chapter,                       /* Глава */
                                           СчетУчета.Code_Currency,                 /* Валюта */
                                           ABS(Sum), /* Сумма */
                                           Doc,                                     /* Документ */
                                           IIF(FIRole == FIROLE_FIREQ, INPCARRY, DVTRANS_L), /* Result_Carry */
                                           "",                                      /* Kind_Oper */
                                           "",                                      /* Numb_Document */
                                           FD.ОснованиеПереносаПоСрокам(FIRole == FIROLE_FIREQ) /* Ground */
                                         )
          )
            return false;
        end;
     else
        DealFD.SetFIIDForCorAccNum(СчетУчета.Code_Currency);
        if( not ПроводкаПоКатегориямУчета( DealFD, 
                                           IIF(FIRole == FIROLE_FIREQ, "СчКорреспГлаваГ", СчетУчета), /* Деб */
                                           IIF(FIRole == FIROLE_FIREQ, СчетУчета, "СчКорреспГлаваГ"), /* Кредит */
                                           ExecDate,                                /* Дата */
                                           СчетУчета.Chapter,                       /* Глава */
                                           IIF(FIRole == FIROLE_FIREQ, null, СчетУчета.Code_Currency ),                 /* Валюта */
                                           IIF(FIRole == FIROLE_FIREQ, null, ABS(Sum) ),/* Сумма */
                                           Doc,                                     /* Документ */
                                           IIF(FIRole == FIROLE_FIREQ, INPCARRY, DVTRANS_L), /* Result_Carry */
                                           "",                                      /* Kind_Oper */
                                           "",                                      /* Numb_Document */
                                           FD.ОснованиеПереносаПоСрокам(FIRole == FIROLE_FIREQ), /* Ground */
                                           null, IIF(FIRole == FIROLE_FIREQ, DealFD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), null), IIF(FIRole == FIROLE_FIREQ, null, DealFD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE) ), null, null,
                                           IIF(FIRole == FIROLE_FIREQ, СчетУчета.Code_Currency, null ), IIF(FIRole == FIROLE_FIREQ, ABS(Sum), null)
                                         )
          )
            return false;
        end;

        DealFD.SetFIIDForCorAccNum(СчетУчета.Code_Currency);
        if( not ПроводкаПоКатегориямУчета( DealFD, 
                                           IIF(FIRole == FIROLE_FIREQ, СчетУчетаНов, "СчКорреспГлаваГ"), /* Деб */
                                           IIF(FIRole == FIROLE_FIREQ, "СчКорреспГлаваГ", СчетУчетаНов), /* Кредит */
                                           ExecDate,                                /* Дата */
                                           СчетУчета.Chapter,                       /* Глава */
                                           IIF(FIRole == FIROLE_FIREQ, СчетУчета.Code_Currency, null ),                 /* Валюта */
                                           IIF(FIRole == FIROLE_FIREQ, ABS(Sum), null ),/* Сумма */
                                           Doc,                                     /* Документ */
                                           IIF(FIRole == FIROLE_FIREQ, INPCARRY, DVTRANS_L), /* Result_Carry */
                                           "",                                      /* Kind_Oper */
                                           "",                                      /* Numb_Document */
                                           FD.ОснованиеПереносаПоСрокам(FIRole == FIROLE_FIREQ), /* Ground */
                                           null, IIF(FIRole == FIROLE_FIREQ, null, DealFD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE) ), IIF(FIRole == FIROLE_FIREQ, DealFD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), null), null, null,
                                           IIF(FIRole == FIROLE_FIREQ, null, СчетУчета.Code_Currency ), IIF(FIRole == FIROLE_FIREQ, null, ABS(Sum))
                                         )
          )
            return false;
        end;
     end;
  end;

  /*получить новую дату переноса по сроку*/
  if( ПИ_НужноПереноситьПоСрокам(FD, КатУчета, Dat, DateBeginPM, FD.ID, AccFIID, FIRoleFI, TransfDatePM, FD.Department(), AccDocNew.PeriodID) )
     TransfDate = TransfDatePM;
  end;

  return true;
END;

PRIVATE MACRO СВОП_ПеренестиСчетПоСрокамВнебирж( FD, Dat:DATE, Doc:MEMADDR, NoErr:BOOL, КатУчета:STRING, FIRole:INTEGER,
                                            AccFIID:INTEGER, TransfDate:@DATE, FIRoleFI:INTEGER, ExecDate:DATE, Change:BOOL, NewAccount:@STRING, SuplDate:Date ):BOOL
  var TransfDatePM, DateBeginPM, ActionDate;

  TransfDate = date(0,0,0);

  if( not FD.IsExistAccount( КатУчета, null, NoErr, FIRoleFI, СчетУчета, null, ExecDate, AccFIID ) )
     return true; /*перенос не нужен*/
  end;

  DateBeginPM = FD.GetParametr(MC_TYPE_PARAMETR_FINDATE, ExecDate, КатУчета, FD.GetBasisFiRole(FIRole));

  if( ПИ_НужноПереноситьПоСрокам(FD, КатУчета, Dat, DateBeginPM, FD.ID, AccFIID, FIRoleFI, TransfDatePM, FD.Department(), null, ExecDate, ActionDate) )
     if( not FD.ActualCheckPeriod(КатУчета, null, null, FIRoleFI, СчетУчетаНов, IIF(Change, ExecDate, ActionDate), AccFIID, null, AccDocNew, ExecDate) )
        return false;
     end;
  else
     TransfDate = TransfDatePM;
     return true;
  end;

  if( СчетУчета.Account == СчетУчетаНов.Account )
     if( (NoErr != null) AND (NoErr == true) )
        return true;
     else
        MsgBox("На дату " + string(TransfDatePM) + " перенос не требуется.");
        return false;
     end;
  else
     var Sum:money = IIF(FIRole == FIROLE_FIREQ, FD.СуммаТребования(), FD.СуммаОбязательства());

     if( КОРРЕСП_ГЛАВА_Г() == true )
        if( not ПроводкаПоКатегориямУчета( FD,
                                           IIF(FIRole == FIROLE_FIREQ, СчетУчетаНов, СчетУчета), /* Деб */
                                           IIF(FIRole == FIROLE_FIREQ, СчетУчета, СчетУчетаНов), /* Кредит */
                                           ExecDate,                                /* Дата */
                                           СчетУчета.Chapter,                       /* Глава */
                                           СчетУчета.Code_Currency,                 /* Валюта */
                                           ABS(Sum), /* Сумма */
                                           Doc,                                     /* Документ */
                                           IIF(FIRole == FIROLE_FIREQ, INPCARRY, DVTRANS_L), /* Result_Carry */
                                           "",                                      /* Kind_Oper */
                                           "",                                      /* Numb_Document */
                                           FD.ОснованиеПереносаПоСрокам(FIRole == FIROLE_FIREQ) /* Ground */
                                         )
          )
            return false;
        end;
     else
        FD.SetFIIDForCorAccNum(СчетУчета.Code_Currency);
        if( not ПроводкаПоКатегориямУчета( FD, 
                                           IIF(FIRole == FIROLE_FIREQ, "СчКорреспГлаваГ", СчетУчета), /* Деб */
                                           IIF(FIRole == FIROLE_FIREQ, СчетУчета, "СчКорреспГлаваГ"), /* Кредит */
                                           ExecDate,                                /* Дата */
                                           СчетУчета.Chapter,                       /* Глава */
                                           IIF(FIRole == FIROLE_FIREQ, null, СчетУчета.Code_Currency ),                 /* Валюта */
                                           IIF(FIRole == FIROLE_FIREQ, null, ABS(Sum) ),/* Сумма */
                                           Doc,                                     /* Документ */
                                           IIF(FIRole == FIROLE_FIREQ, INPCARRY, DVTRANS_L), /* Result_Carry */
                                           "",                                      /* Kind_Oper */
                                           "",                                      /* Numb_Document */
                                           FD.ОснованиеПереносаПоСрокам(FIRole == FIROLE_FIREQ), /* Ground */
                                           null, IIF(FIRole == FIROLE_FIREQ, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), null), IIF(FIRole == FIROLE_FIREQ, null, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE) ), null, null,
                                           IIF(FIRole == FIROLE_FIREQ, СчетУчета.Code_Currency, null ), IIF(FIRole == FIROLE_FIREQ, ABS(Sum), null)
                                         )
          )
            return false;
        end;

        FD.SetFIIDForCorAccNum(СчетУчета.Code_Currency);
        if( not ПроводкаПоКатегориямУчета( FD, 
                                           IIF(FIRole == FIROLE_FIREQ, СчетУчетаНов, "СчКорреспГлаваГ"), /* Деб */
                                           IIF(FIRole == FIROLE_FIREQ, "СчКорреспГлаваГ", СчетУчетаНов), /* Кредит */
                                           ExecDate,                                /* Дата */
                                           СчетУчета.Chapter,                       /* Глава */
                                           IIF(FIRole == FIROLE_FIREQ, СчетУчета.Code_Currency, null ),                 /* Валюта */
                                           IIF(FIRole == FIROLE_FIREQ, ABS(Sum), null ),/* Сумма */
                                           Doc,                                     /* Документ */
                                           IIF(FIRole == FIROLE_FIREQ, INPCARRY, DVTRANS_L), /* Result_Carry */
                                           "",                                      /* Kind_Oper */
                                           "",                                      /* Numb_Document */
                                           FD.ОснованиеПереносаПоСрокам(FIRole == FIROLE_FIREQ), /* Ground */
                                           null, IIF(FIRole == FIROLE_FIREQ, null, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE) ), IIF(FIRole == FIROLE_FIREQ, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), null), null, null,
                                           IIF(FIRole == FIROLE_FIREQ, null, СчетУчета.Code_Currency ), IIF(FIRole == FIROLE_FIREQ, null, ABS(Sum))
                                         )
          )
            return false;
        end;
     end;
  end;

  /*получить новую дату переноса по сроку*/
  if( ПИ_НужноПереноситьПоСрокам(FD, КатУчета, Dat, DateBeginPM, FD.ID, AccFIID, FIRoleFI, TransfDatePM, FD.Department(), AccDocNew.PeriodID) )
     TransfDate = TransfDatePM;
  end;

  return true;
END;

MACRO ПереноситьПоСрокам( FD, Dat:DATE, TransfDate:@DATE, NoErr:BOOL, pid_plus:INTEGER, pid_minus:INTEGER,
                          КатУчетаТреб:STRING, КатУчетаОбяз:STRING, ВалютаТреб:INTEGER, ВалютаОб:INTEGER,
                          РольТреб:INTEGER, РольОб:INTEGER, DealExecDate:date )
   var TransfDateMinus, TransfDatePlus, DateBeginPlus, DateBeginMinus;
   var Переносить:bool = false,
       Переносить1:bool = false,
       Переносить2:bool = false;

   GetTransferDates( FD, Dat, КатУчетаТреб, КатУчетаОбяз, @DateBeginPlus, @DateBeginMinus );

   if( КатУчетаТреб != "" )
      Переносить1 = ПИ_НужноПереноситьПоСрокам(FD, КатУчетаТреб, Dat, DateBeginPlus, FD.ID, ВалютаТреб, РольТреб, TransfDatePlus, FD.Department(), pid_plus);
      //для того чтобы перенос по сделке проходил после праздников, и сделка исполнялась с 01 , нужно убирать равно
      //if( (DealExecDate != null) and (TransfDatePlus >= DealExecDate) )
      if( (DealExecDate != null) and (TransfDatePlus > DealExecDate) )
         Переносить1 = false;
      end;
   end;

   if( КатУчетаОбяз != "" )
      Переносить2 = ПИ_НужноПереноситьПоСрокам(FD, КатУчетаОбяз, Dat, DateBeginMinus, FD.ID, ВалютаОб, РольОб, TransfDateMinus, FD.Department(), pid_minus);
      //для того чтобы перенос по сделке проходил после праздников, и сделка исполнялась с 01 , нужно убирать равно
      //if( (DealExecDate != null) and (TransfDateMinus >= DealExecDate) )
      if( (DealExecDate != null) and (TransfDateMinus > DealExecDate) )
         Переносить2 = false;
      end;
   end;

   if( Переносить1 and Переносить2 )
      TransfDate = minDate(TransfDatePlus, TransfDateMinus);
      Переносить = true;
   elif( Переносить1 )
      TransfDate = TransfDatePlus;
      Переносить = true;
   elif( Переносить2 )
      Переносить = true;
      TransfDate = TransfDateMinus;
   end;

   return Переносить;
END;

MACRO ВыполнитьПереносПоСрокам( doc:MEMADDR, FD:DVFirstDocNDeal, _ExecDate:date ):INTEGER
  VAR TransfDateDT, TransfDateCT, TransfDate = DATE(0,0,0);

  if (FD.ОтказОтИсполнения())
      if( not InsertOprStatus( IIF(FD.DealPart == 1, FD.DV_OPERSTATUS_COTERMS(), FD.DV_OPERSTATUS_COTERMS_2()), DV_OPERSTATUS_COTERMS_NOTRANSFER) ) 
          MsgBox("Ошибка при установке статуса <Перенос по срокам> для операции.");
          return 1;
       end;
     return 0;
  end;

  if( FD.IsClient() == false )

    var ExecDate:date = date(0,0,0);
    var Change:bool = false;
    if( (_ExecDate != NULL) and (_ExecDate != date(0,0,0)) )
       ExecDate = _ExecDate;
       Change = true;
    else
       GetOprDate( IIF( FD.DealPart == 1, DV_NDEAL_OPRDATE_COTERMS, DV_NDEAL_OPRDATE_COTERMS_2), ExecDate );
    end;

    var ReqAcc:string = ""; var ReqAccFIID:integer = ALLFININSTR; var ReqFiRole = FIROLE_UNDEF;
    var ComAcc:string = ""; var ComAccFIID:integer = ALLFININSTR; var ComFiRole = FIROLE_UNDEF;
    ПолучитьСрочныеСчетаВнб( FD, @ReqAcc, @ReqAccFIID, @ReqFiRole, @ComAcc, @ComAccFIID, @ComFiRole );

    if( ПеренестиСчетПоСрокамВнебирж(FD, FD.ДатаСделки(), doc, true, ReqAcc, FIROLE_FIREQ, ReqAccFIID, @TransfDateDT, ReqFiRole, ExecDate, Change, NULL, FD.ДатаИсполнения() ) == false )
       return 1;
    end;

    if( ПеренестиСчетПоСрокамВнебирж(FD, FD.ДатаСделки(), doc, true, ComAcc, FIROLE_FICOM, ComAccFIID, @TransfDateCT, ComFiRole, ExecDate, Change, NULL, FD.ДатаИсполнения() ) == false )
       return 1;
    end;

    TransfDateDT = ПИ_ПереносПоСрокамВДатуТО(TransfDateDT,FD.ДатаИсполнения());

    TransfDateCT = ПИ_ПереносПоСрокамВДатуТО(TransfDateCT,FD.ДатаИсполнения());

    if( (TransfDateDT != DATE(0,0,0)) AND (TransfDateCT != DATE(0,0,0)) )
       TransfDate = min( TransfDateDT, TransfDateCT );
    elif( TransfDateDT != DATE(0,0,0)  )
       TransfDate = TransfDateDT;
    elif( TransfDateCT != DATE(0,0,0)  )
       TransfDate = TransfDateCT;
    end;

    if( TransfDate != DATE(0,0,0) )
       if( not InsertOprStatus( IIF(FD.DealPart == 1, FD.DV_OPERSTATUS_COTERMS(), FD.DV_OPERSTATUS_COTERMS_2()), DV_OPERSTATUS_COTERMS_TRANSFER) )
          MsgBox("Ошибка при установке статуса <Перенос по срокам> для операции.");
          return 1;
       end;
       DL_CalendDefineOprDate(FD, IIF( FD.DealPart == 1, DV_NDEAL_OPRDATE_COTERMS, DV_NDEAL_OPRDATE_COTERMS_2), TransfDate );
    else
       if( not InsertOprStatus( IIF(FD.DealPart == 1, FD.DV_OPERSTATUS_COTERMS(), FD.DV_OPERSTATUS_COTERMS_2()), DV_OPERSTATUS_COTERMS_NOTRANSFER) ) 
          MsgBox("Ошибка при установке статуса <Перенос по срокам> для операции.");
          return 1;
       end;
    end;
  end;

  return 0;
END;

/*Специально для возможности проведения и закрытия старых сделок короткого свопа закрытой операции 2740*/
MACRO СВОП_ВыполнитьПереносПоСрокам( doc:MEMADDR, FD:DVFirstDocFXDeal, _ExecDate:date ):INTEGER
  VAR TransfDateDT, TransfDateCT, TransfDate = DATE(0,0,0);

  if( (FD.IsClient() == false) and FD.IsSwap() )

    var ExecDate:date = date(0,0,0);
    var Change:bool = false;
    if( (_ExecDate != NULL) and (_ExecDate != date(0,0,0)) )
       ExecDate = _ExecDate;
       Change = true;
    else
       GetOprDate( IIF( FD.DealPart == 1, DV_FXDEAL_OPRDATE_COTERMS, DV_FXDEAL_OPRDATE_COTERMS_2), ExecDate );
    end;

    var ReqAcc:string = "+Форвард, дрейф внебирж"; var ReqAccFIID:integer = IIF((FD.DealPart == 1), FD.ВалютаТребования(), FD.ВалютаОбязательства()); var ReqFiRole = FIROLE_FIREQ;
    var ComAcc:string = "-Форвард, дрейф внебирж"; var ComAccFIID:integer = IIF((FD.DealPart == 1), FD.ВалютаОбязательства(), FD.ВалютаТребования()); var ComFiRole = FIROLE_FICOM;

    if( СВОП_ПеренестиСчетПоСрокамВнебирж(FD, FD.ДатаСделки(), doc, true, ReqAcc, FIROLE_FIREQ, ReqAccFIID, @TransfDateDT, ReqFiRole, ExecDate, Change, NULL, FD.ДатаИсполнения() ) == false )
       return 1;
    end;

    if( СВОП_ПеренестиСчетПоСрокамВнебирж(FD, FD.ДатаСделки(), doc, true, ComAcc, FIROLE_FICOM, ComAccFIID, @TransfDateCT, ComFiRole, ExecDate, Change, NULL, FD.ДатаИсполнения() ) == false )
       return 1;
    end;

    TransfDateDT = ПИ_ПереносПоСрокамВДатуТО(TransfDateDT,FD.ДатаИсполнения());

    TransfDateCT = ПИ_ПереносПоСрокамВДатуТО(TransfDateCT,FD.ДатаИсполнения());

    if( (TransfDateDT != DATE(0,0,0)) AND (TransfDateCT != DATE(0,0,0)) )
       TransfDate = min( TransfDateDT, TransfDateCT );
    elif( TransfDateDT != DATE(0,0,0)  )
       TransfDate = TransfDateDT;
    elif( TransfDateCT != DATE(0,0,0)  )
       TransfDate = TransfDateCT;
    end;

    if( TransfDate != DATE(0,0,0) )
       if( not InsertOprStatus( IIF(FD.DealPart == 1, DV_FX_OPERSTATUS_KIND_COTERMS, DV_FX_OPERSTATUS_KIND_COTERMS_2 ), DV_OPERSTATUS_COTERMS_TRANSFER) )
          MsgBox("Ошибка при установке статуса <Перенос по срокам> для операции.");
          return 1;
       end;
       DL_CalendDefineOprDate(FD, IIF( FD.DealPart == 1, DV_FXDEAL_OPRDATE_COTERMS, DV_FXDEAL_OPRDATE_COTERMS_2 ), TransfDate );
    else
       if( not InsertOprStatus( IIF(FD.DealPart == 1, DV_FX_OPERSTATUS_KIND_COTERMS, DV_FX_OPERSTATUS_KIND_COTERMS_2 ), DV_OPERSTATUS_COTERMS_NOTRANSFER) ) 
          MsgBox("Ошибка при установке статуса <Перенос по срокам> для операции.");
          return 1;
       end;
       if( (FD.DealPart == 1) and (GetOprStatus(DV_FX_OPERSTATUS_KIND_BALANCE) != DV_FX_OPERSTATUS_DL_RUN) )
          if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_BALANCE, DV_FX_OPERSTATUS_DL_RUN) )
             MsgBox("Ошибка при установке статуса <Баланс> по операции.");
             return false;
          end;
       end;
       if( (FD.DealPart == 2) and (GetOprStatus(DV_FX_OPERSTATUS_KIND_BALANCE_2) != DV_FX_OPERSTATUS_DL_RUN) )
          if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_BALANCE_2, DV_FX_OPERSTATUS_DL_RUN) )
             MsgBox("Ошибка при установке статуса <Баланс 2ч> по операции.");
             return 1;
          end;
       end;
    end;
  end;

  return 0;
END;

MACRO ВыполнитьПереносПоСрокамПП( doc:MEMADDR, FD:DVFirstDocNDeal ):INTEGER

 if( not FD.IsClient() )
     if (FD.ОтказОтИсполнения())
        if( not InsertOprStatus(DV_OPERSTATUS_KIND_COTERMS_IP, DV_OPERSTATUS_COTERMS_NOTRANSFER) )
           MsgBox("Ошибка при установке статуса <Перенос по срокам ПП> для операции.");
           return 1;
        end;
        return 0;
     end;
     var TransfDateDT = date(0,0,0), TransfDateCT = date(0,0,0), TransferDate = date(0,0,0);
     var ExecDate:date = date(0,0,0);
     var ReqAcc:string = ""; var ReqAccFIID:integer = ALLFININSTR; var ReqFiRole = FIROLE_UNDEF;
     var ComAcc:string = ""; var ComAccFIID:integer = ALLFININSTR; var ComFiRole = FIROLE_UNDEF;
     var Query, Data, Sql;
     var DebAccount:string = "", CredAccount:string = "";
     var MinTransferDate:date = date(31,12,9999);
     /*PNV*/
     if( FD.IsPctSWAP() )
         ReqAcc = "+Форвард, дрейф Проц.СВОП";
         ComAcc = "-Форвард, дрейф Проц.СВОП";
         ReqFiRole  = FIROLE_FIREQ;
         ComFiRole  = FIROLE_FICOM;
     else
    //ПолучитьСрочныеСчетаВнб( FD, @ReqAcc, @ReqAccFIID, @ReqFiRole, @ComAcc, @ComAccFIID, @ComFiRole );
     ReqAcc = "+Форвард,дрейф внебирж ПП";
     ComAcc = "-Форвард,дрейф внебирж ПП";
     ReqFiRole  = FIROLE_FIREQ;
     ComFiRole  = FIROLE_FICOM;
     end;
     /*PNV*/

     GetOprDate(DV_NDEAL_OPRDATE_COTERMS_IP, ExecDate);

     Sql = " SELECT * " +
           "   FROM ddvnpmgr_dbt " +
           "  WHERE t_DealID  = ? " +
           "    AND t_TransferDate = ? ";

     Query = RSDCommand( Sql );
     Query.NullConversion = true;
     Query.addParam("", RSDBP_IN, FD.Deal.rec.ID );
     Query.addParam("", RSDBP_IN, ExecDate );
     Query.execute();

     Data = TRsbDataSet(Query);
     while( Data.MoveNext() )
        var GrFD = DVFirstDocPmGr(DL_DVNDEALPMGR, SQL_ConvTypeInteger(Data.rec.ID));

        if( GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_LIABILITY ) /* требование или не задано */
           if( ПеренестиСчетПоСрокамВнебирж(GrFD, GrFD.DealFD.ДатаСделки(), doc, true, ReqAcc, FIROLE_FIREQ, FD.ВалютаТребования(), @TransfDateDT, ReqFiRole, ExecDate, false, @DebAccount) == false )
              return 1;
           end;
        end;

        if( GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_DEMAND ) /* обязательство или не задано */
           if( ПеренестиСчетПоСрокамВнебирж(GrFD, GrFD.DealFD.ДатаСделки(), doc, true, ComAcc, FIROLE_FICOM, FD.ВалютаОбязательства(), @TransfDateCT, ComFiRole, ExecDate, false, @CredAccount) == false )
              return 1;
           end;
        end;

        if( (TransfDateDT != date(0,0,0)) AND (TransfDateCT != date(0,0,0)) )
           TransferDate = min( TransfDateDT, TransfDateCT );
        elif( TransfDateDT != date(0,0,0) )
           TransferDate = TransfDateDT;
        elif( TransfDateCT != date(0,0,0) )
           TransferDate = TransfDateCT;
        end;

        if ( (null==DebAccount) OR (""==DebAccount) )
            DebAccount =  GrFD.PmGr.rec.DemandAccount;   
        end;

        if( (null==CredAccount) OR (""==CredAccount) )
            CredAccount = GrFD.PmGr.rec.LiabilityAccount;
        end;

        MinTransferDate = min(MinTransferDate, IIF(TransferDate != date(0,0,0), TransferDate, date(31,12,9999)));
        if( DV_TransferPm(GrFD.PmGr.rec.ID, ExecDate, TransferDate, DebAccount, CredAccount) )
           return 1;
        end;
     end;

     Sql = " SELECT * " +
           "   FROM ddvnpmgr_dbt " +
           "  WHERE t_DealID  = ? " +
           "    AND t_TransferDate != ? " +
           "    AND t_TransferDate != to_date('01.01.0001', 'dd.mm.yyyy') ";

     Query = RSDCommand( Sql );
     Query.NullConversion = true;
     Query.addParam("", RSDBP_IN, FD.Deal.rec.ID );
     Query.addParam("", RSDBP_IN, ExecDate );
     Query.execute();

     Data = TRsbDataSet(Query);
     while( Data.MoveNext() )
        MinTransferDate = min(MinTransferDate, SQL_ConvTypeDate(Data.TransferDate));
     end;

     if( MinTransferDate != date(31,12,9999) )
        DL_CalendDefineOprDate(FD,DV_NDEAL_OPRDATE_COTERMS_IP, MinTransferDate);

        if( not InsertOprStatus(DV_OPERSTATUS_KIND_COTERMS_IP, DV_OPERSTATUS_COTERMS_TRANSFER) )
           MsgBox("Ошибка при установке статуса <Перенос по срокам ПП> для операции.");
           return 1;
        end;
     else
        if( not InsertOprStatus(DV_OPERSTATUS_KIND_COTERMS_IP, DV_OPERSTATUS_COTERMS_NOTRANSFER) )
           MsgBox("Ошибка при установке статуса <Перенос по срокам ПП> для операции.");
           return 1;
        end;
     end;
  end;

  return 0;
END;

/*зовется из сишника в операции переоценки для проверки необходимости вставлять цепочку с шагом переноса по срокам в сделку с БА артикул*/
/*немного дублируем код макроса шага, который зовется позже этой проверки для того, чтобы определить вид цепочки переоценки для вставки*/
/*теперь цепочка переоценки с переносом по срокам не нужна, поэтому здесь всегда возвращаем false*/
MACRO DVIsArticleNeedReMove(FirstDoc, OperDate, NeedTransfer)
  return false;
END;
