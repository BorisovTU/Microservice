/*
$Name:         dvndeal_funcobj.mac
$Module:       ФИССиКО
$Description:  Выполнение сделки валютного рынка через funcobj
*/

import FIInter, DVInter, InsCarryDoc, "dlmisc.mac", "FuncObj.mac";

private macro GetErrDescr(er: Variant): String
  var errStr = "", i = 0;

  if(IsEqClass("TrslError", er))
    errStr = er.module + " "+er.line + " " + er.message;

    if(IsEqClass("TDbError", er.err))
      errStr = errStr + "\n:|" + er.err.Stat + "-" + er.err.Oper + "-" + er.err.Table + "-" + er.err.Message;
    elif(isEqClass("TRsdError", er.err))
      while(i < er.err.environment.ErrorCount)
        errStr = errStr + "\n" + er.err.environment.Error(i).Descr;
        i = i + 1;
      end;
    end;
  end;

  return errStr;
end;

/**
  @brief     Выполнение задания funcobj
  @param[in] DocKind вид сделки = ddvndeal_dbt.t_DocKind
  @param[in] СlientСontr контрагент сделки = ddvndeal_dbt.t_СlientСontr
  @param[in] param  дата сделки в виде строки = ddvndeal_dbt.t_Date
  @return    Объект результата выполнения задания funcobj
*/
macro ExecuteNDealCur(DocKind:integer, СlientСontr:integer, param:string):FuncObjResult
  var errCode:integer = 0;
  var ReturnObj = FuncObjResult(FUNCOBJ_RESULT_OK, "", 0);
  var clients = TArray();
  var errStr, oper_err_idx;  
  var countSkipDeal:integer = 0;
  var clientСontrStr = "", dateStr = "", sql, SkipDealSql, NotSkipDealSql;

  if((DocKind == 0) or (СlientСontr == 0) or (param == ""))
      ReturnObj.state = FUNCOBJ_RESULT_ERROR;
      ReturnObj.errorText = "Переданы неверные параметры";
      ReturnObj.errorCode = 0;
  else
    clients = SplitString(param, ";"); 
  end;
  
  if(clients.size() == 3)
    countSkipDeal = int(clients[1]);
    clientСontrStr = clients[2];

    sql = "SELECT t.t_ID" +
          "  FROM ddvndeal_dbt t " +
          " WHERE t.t_Client > 0 " +
          "   AND t.t_Sector = chr(88) " +
          "   AND t.t_MarketKind = " + DV_MARKETKIND_CURRENCY +
          "   AND t.t_ClientContr IN (" + clientСontrStr + ")" +
          "   AND t.t_DocKind = " + String(DocKind);
      // дополнительное условие если нет пропускаемых сделок 
    NotSkipDealSql = " AND t.t_State IN (0, 1) ";
    SkipDealSql =    
          "   AND (t.t_State = 1 OR "+
                " (t.t_State = 0 AND NOT EXISTS (SELECT 1 "+
                                                " FROM ddlcontrmp_dbt contrmp, ddlcontr_dbt dlcontr "+
                                                " WHERE contrmp.t_SfContrID = t.t_ClientContr "+
                                                  " AND dlcontr.t_DlContrID = contrmp.t_DlContrID "+
                                                  " AND dlcontr.t_IIS = 'X' "+
                                                  " AND rsb_secur.GetMainObjAttr(207, LPAD(dlcontr.t_DlContrID, 34, '0'), 121/*Сведения о наличии или отсутствии ИИС у другого ПУ*/, t.t_Date) = 1 "+
                                                  " AND rsb_secur.GetMainObjAttr(207, LPAD(dlcontr.t_DlContrID, 34, '0'), 122/*Предоставлены подтвержд. документы о расторжении ИИС у другого ПУ*/, t.t_Date) <> 1 "+
                                                ") "+
                "))";
    if(countSkipDeal > 0) 
      sql = sql + SkipDealSql;
    else 
      sql = sql + NotSkipDealSql;
    end; 

    var rs = TRsbDataSet(sql);
    while( rs.MoveNext() and (not errCode))     
      errCode = DV_ExecDeal(int(rs.t_ID), false, -1, Date(clients[0]));

      if((errCode != 0) AND (errCode != 1336))
        ReturnObj.state = FUNCOBJ_RESULT_ERROR;
        errStr = GetErrMsg();
        if( errStr != "" )
          oper_err_idx = Index(errStr, "В операции есть неисполненные платежи с ID:");  // в строке 43 символа
          if(ReturnObj.errorText != "")
            ReturnObj.errorText = ReturnObj.errorText + ", ";
            if( (Index(ReturnObj.errorText, "В операции есть неисполненные платежи с ID:")>0) AND (oper_err_idx>0) )
              errStr = SubStr(errStr, 44);
            end;
          end;
          ReturnObj.errorText = ReturnObj.errorText + errStr;
        end;
        ReturnObj.errorCode = errCode;
      end;
    end;
  else
    ReturnObj.state = FUNCOBJ_RESULT_ERROR;
    ReturnObj.errorText = "Переданы неверные параметры";
    ReturnObj.errorCode = 0;
  end;

  return ReturnObj;

onError(err)
  ReturnObj.state = FUNCOBJ_RESULT_ERROR;
  ReturnObj.errorText = GetErrDescr(err);
  ReturnObj.errorCode = err.code;
  
  return ReturnObj;
end;

//var dat = date("15.04.2025");
//var res = DV_ExecDeal(2241764, false, -1, date("15.04.2025"));
//println(res);