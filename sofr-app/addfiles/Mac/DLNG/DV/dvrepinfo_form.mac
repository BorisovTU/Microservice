/*
$Name:        dvrepinfo_form.mac
$Module:      Производные инструменты
$Description: Отчет "Сведения о ПФИ  "- форма ввода данных
*/
import "DlRepForm_h.mac", DVInter;

CONST 
      PNFLD_DVREPINFO_DEPARTMENT       = 0,  
      PNFLD_DVREPINFO_DEPARTMENT_NAME  = 1,
      PNFLD_DVREPINFO_PERIOD           = 2,
      PNFLD_DVREPINFO_YEAR             = 3,
      PNFLD_DVREPINFO_BEG_DATE         = 4,
      PNFLD_DVREPINFO_END_DATE         = 5,
      PNFLD_DVREPINFO_FOR_EVERY_DAY    = 6,
      PNFLD_DVREPINFO_BYFISSKO         = 7,
      PNFLD_DVREPINFO_BYVA             = 8,
      PNFLD_DVREPINFO_EXCEL            = 9,
      PNFLD_DVREPINFO_KLIKO            = 10;

PRIVATE CONST ПЕРИОД = 0;

PRIVATE MACRO DL_FldProc_Period( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, BegD, EndD;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = ПЕРИОД;
     end;
     FldShowValue = DL_GetNameAlg( ALG_DV_REP_PERIOD, FldRealValue );
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( FldRealValue == ПЕРИОД )/*разрешаем редактировать только для вида периода "период"*/
        EnableFields( pThis.Data(), PNFLD_DVREPINFO_BEG_DATE );
        EnableFields( pThis.Data(), PNFLD_DVREPINFO_END_DATE );
     else
        DisableFields( pThis.Data(), PNFLD_DVREPINFO_BEG_DATE );
        DisableFields( pThis.Data(), PNFLD_DVREPINFO_END_DATE );

        Year = pThis.GetLinkedValue(DL_PNFLD_YEAR);
       
        Period_GetInterval( FldRealValue, Year, @BegD, @EndD );
        pThis.SetLinkedValue( DL_PNFLD_BEGINDATE, BegD );
        pThis.SetLinkedValue( DL_PNFLD_ENDDATE, EndD );

     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( ALG_DV_REP_PERIOD, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  end;
  return CM_DEFAULT;
END;

/*Начальная дата выпуска отчета*/
PRIVATE MACRO DL_FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, Period, BegYear, EndDate;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     Period = pThis.GetLinkedValue(DL_PNFLD_PERIOD);
     if( (Period == ПЕРИОД) and (FldRealValue != FldShowValue) )
        FldRealValue = FldShowValue;

        DateSplit( FldRealValue, null, null, BegYear);
        Year = pThis.GetLinkedValue(DL_PNFLD_YEAR);
        EndDate = pThis.GetLinkedValue(DL_PNFLD_ENDDATE);

        if( (Year != null) and (BegYear != Year) )
           MsgBox( "Дата начала не входит в указанный год.");
           return CM_IGNORE;
        end;

        if( (EndDate != null) and (EndDate < FldRealValue) )
           MsgBox( "Дата начала не может быть больше даты окончания периода.");
           return CM_IGNORE;
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
     Period = pThis.GetLinkedValue(DL_PNFLD_PERIOD);
     if( Period == ПЕРИОД )
        DateSplit( FldRealValue, null, null, BegYear);
        Year = pThis.GetLinkedValue(DL_PNFLD_YEAR);
        EndDate = pThis.GetLinkedValue(DL_PNFLD_ENDDATE);
   
        if( (Year != null) and (BegYear != Year) )
           MsgBox( "Дата начала не входит в указанный год.");
           return CM_IGNORE;
        end;
   
        if( (EndDate != null) and (EndDate < FldRealValue) )
           MsgBox( "Дата начала не может быть больше даты окончания периода.");
           return CM_IGNORE;
        end;
     end;
   
  end;
  return CM_DEFAULT;
END;

/*Конечная дата выпуска отчета*/
PRIVATE MACRO DL_FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, Period, EndYear, BegDate;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     Period = pThis.GetLinkedValue(DL_PNFLD_PERIOD);
     if( (Period == ПЕРИОД) and (FldRealValue != FldShowValue) )
        FldRealValue = FldShowValue;
        DateSplit( FldRealValue, null, null, EndYear);
        Year = pThis.GetLinkedValue(DL_PNFLD_YEAR);
        BegDate = pThis.GetLinkedValue(DL_PNFLD_BEGINDATE);

        if( (Year != null) and (EndYear != Year) )
           MsgBox( "Дата окончания не входит в указанный год.");
           return CM_IGNORE;
        end;
        if( (BegDate != null) and (FldRealValue < BegDate) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
     Period = pThis.GetLinkedValue(DL_PNFLD_PERIOD);
     if( Period == ПЕРИОД )
        DateSplit( FldRealValue, null, null, EndYear);
        Year = pThis.GetLinkedValue(DL_PNFLD_YEAR);
        BegDate = pThis.GetLinkedValue(DL_PNFLD_BEGINDATE);
     
        if( (Year != null) and (EndYear != Year) )
           MsgBox( "Дата окончания не входит в указанный год.");
           return CM_IGNORE;
        end;
        if( (BegDate != null) and (FldRealValue < BegDate) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
  end;
  return CM_DEFAULT;
END;

/* Код филиала
*/
PRIVATE MACRO DLUSR_FldProc_Department( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
file   Department( dp_dep );
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( DL_PNFLD_DEPARTMENT, STR_ALLVALUE);
     else
        FldShowValue = FldRealValue;
        pThis.SetLinkedValue( DL_PNFLD_DEPARTMENT, DL_GetDepartmentNameByCode(FldRealValue));
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     return pThis.SetLinkedValue( DL_PNFLD_DEPARTMENT, STR_ALLVALUE );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) 
     if( ListDepartment( Department ) )
        FldRealValue = Department.Code;
        FldShowValue = FldRealValue;
        pThis.SetLinkedValue( DL_PNFLD_DEPARTMENT, DL_GetDepartmentNameByCode(FldRealValue)); 
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) DV_DVREPINFO_Panel()

  PRIVATE MACRO Init()                                                                                      

     AddFieldProc( 0, PNFLD_DVREPINFO_DEPARTMENT,      DL_PNFLD_DEPARTCODE, @DLUSR_FldProc_Department, -1);
     AddFieldProc( 0, PNFLD_DVREPINFO_DEPARTMENT_NAME, DL_PNFLD_DEPARTMENT, @Default, "");

     AddFieldProc( 0, PNFLD_DVREPINFO_PERIOD,          DL_PNFLD_PERIOD,     @DL_FldProc_Period, null, 27, 6 );
     AddFieldProc( 0, PNFLD_DVREPINFO_YEAR,            DL_PNFLD_YEAR,       @DL_FldProc_Year );

     AddFieldProc( 0, PNFLD_DVREPINFO_BEG_DATE,        DL_PNFLD_BEGINDATE,  @DL_FldProc_BeginDate, {curdate});
     AddFieldProc( 0, PNFLD_DVREPINFO_END_DATE,        DL_PNFLD_ENDDATE,    @DL_FldProc_EndDate,   {curdate} );                 

     AddFieldProc( 0, PNFLD_DVREPINFO_FOR_EVERY_DAY,   DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox, "" );                 

     AddFieldProc( 0, PNFLD_DVREPINFO_BYFISSKO,        DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox, "X" );
     AddFieldProc( 0, PNFLD_DVREPINFO_BYVA,            DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox, "X" );      

     AddFieldProc( 0, PNFLD_DVREPINFO_EXCEL,           DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox , "X");                 
     AddFieldProc( 0, PNFLD_DVREPINFO_KLIKO,           DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox, "" );                 
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "dv.lbr", "dvinfo" );
END;
