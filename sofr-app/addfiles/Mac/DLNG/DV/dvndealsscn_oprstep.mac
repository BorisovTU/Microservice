/*
 $Name: dvndealsscn_oprstep.mac
 $Module: ФИССиКО
 $Description: Массовое выполнение действий над внебиржевыми сделками из скроллинга по шагам
*/

import DealsInter, dvinter;

PRIVATE RECORD step( oprstep );
PRIVATE RECORD deal( dvndeal );

PRIVATE CONST ERROR_EXEC = 0,
              PRINT_HEADER = 1,
              PRINT_FOOTER = 2;

PRIVATE MACRO PrintHead( Head:STRING )
[
                   #
┌──────────────────────────┬──────┬───────────────────────────────────────────────────────────────────────────────────────────────────┐
│            Код           │  №   │                      Сообщение                                                                    │
│         операции         │ошибки│                                                                                                   │
├──────────────────────────┼──────┼───────────────────────────────────────────────────────────────────────────────────────────────────┤](Head);
END;


PRIVATE MACRO PrintFooter()
[└──────────────────────────┴──────┴───────────────────────────────────────────────────────────────────────────────────────────────────┘
];

END;

PRIVATE MACRO PrintLine( ErrStatus:INTEGER, ErrMessage:STRING )
[│##########################│######│###################################################################################################│]
( deal.Code:w, ErrStatus, ErrMessage:w );
END;

PRIVATE MACRO GetStepState( ID_Operation:INTEGER, ID_Step:INTEGER ):STRING
  PRIVATE VAR StepByID = TBFile( "oprstep.dbt" );

  StepByID.Clear();
  StepByID.rec.ID_Operation = ID_Operation;
  StepByID.rec.ID_Step = ID_Step;
  if( StepByID.GetEQ() )
     return StepByID.rec.IsExecute;
  end;

  return "-";
END;

PRIVATE MACRO ExecuteOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом выполнении шагов." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 )
        var State = GetStepState( step.ID_Operation, step.ID_Step );

        if( State == "X" )
           ErrMessage = "Шаг выполнен успешно.";
        else
           ErrMessage = "Ошибка при выполнении.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

PRIVATE MACRO MoveToPrepOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом откате шагов." );
  elif( NumExec == PRINT_FOOTER )
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 )
        var State = GetStepState( step.ID_Operation, step.ID_Step );

        if( State == "R" )
           ErrMessage = "Откат шага успешно выполнен";
        else
           ErrMessage = "Ошибка при откате шага.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

PRIVATE MACRO InitDealRecord()
  var operation = TBFile("oproper.dbt");
  operation.rec.ID_Operation = step.ID_Operation;
  
  if( operation.GetEq )
    var dealFile = TBFile("dvndeal.dbt");
    dealFile.rec.ID = operation.rec.DocumentID;
    if( dealFile.GetEq )
      Copy(deal, dealFile);
    end;
  end;
END;

/* Action = 1 - начало/продолжение  (F2 из скроллинга )
   Action = 2 - перевод в отложенные (Alt+F8 из скроллинга )
   Action = 3 - удаление (F8 из скроллинга)
*/
MACRO Печать_ОтчетСканирования( NumExec:INTEGER, FDoc:VARIANT, ErrStatus:INTEGER, ErrMessage:STRING, Action:INTEGER )

  SetBuff( step, FDoc );
  InitDealRecord();

  if( Action == 1 )
     ExecuteOperation( NumExec, ErrStatus, ErrMessage );
  elif( Action == 2 )
     MoveToPrepOperation( NumExec, ErrStatus, ErrMessage );
  end;

END;
