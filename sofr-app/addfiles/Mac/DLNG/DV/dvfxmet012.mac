/*
$Name:        dvfxmet012.mac
$Module:      Производные инструменты
$Description: Покупка/Продажа слитков. Шаг: Квитовка подтверждения
*/

import InsCarryDoc, "dv_categ.mac", "dv_car.mac", "dv_fun.mac";

private macro DL_GetMinComCalcDateByOper( pDocKind:integer, pDocID:integer, pDate:date ):date
  var RetVal = date(0,0,0);
  var SQL, Query, Data;

  SQL = " SELECT NVL(MIN(Com.t_Date), TO_DATE('01.01.0001', 'dd.mm.yyyy')) pDate " +
        "   FROM DDLCOMIS_DBT Com " +
        "  WHERE Com.t_DocKind = ? " +
        "    AND Com.t_DocID   = ? " +
        "    AND Com.t_FactPayDate = TO_DATE('01.01.0001', 'dd.mm.yyyy') ";

  if( pDate != NULL )
     SQL = SQL + " AND Com.t_Date > ? ";
  end;

  Query = RSDCommand(SQL);
  Query.NullConversion = true;
  Query.addParam("", RSDBP_IN, pDocKind);
  Query.addParam("", RSDBP_IN, pDocID);
  if( pDate != NULL )
     Query.addParam("", RSDBP_IN, pDate);
  end;
  Query.execute();
  Data = TRsbDataSet(Query);
  if( Data.MoveNext() )
     RetVal = SQL_ConvTypeDate(Data.rec.pDate);
  end;

  return RetVal;
end;

macro ExecuteStep( doc, FDoc)
  record deal(dvndeal);
  var    FD, FD2;

  SetBuff(deal, FDoc);
  FD = DVFirstDocFXDeal(DL_DVFXDEAL, deal);
  /* 2-я очередь: Квитовка*/
  if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_CONFIRM, DV_FX_OPERSTATUS_DL_COMPLETE) )
    MsgBox("Ошибка при установке статуса <Подтверждение> по операции.");
    return 1;
  end;

  var Дрк = DL_GetMinComCalcDateByOper(FD.Kind, FD.ID);
  if(Дрк != date(0,0,0))
    DL_CalendDefineOprDate(FD,DV_FXDEAL_OPRDATE_CALCCOMISS, Дрк);
    if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_COMISSACC, DV_FX_OPERSTATUS_PAYM_EX) )
      MsgBox("Ошибка при установке статуса <Начисление комиссии> по операции.");
      return 1;
    end;
  else
    DL_CalendDefineOprDate(FD,DV_FXDEAL_OPRDATE_CALCCOMISS, Дрк);
    if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_COMISSACC, DV_FX_OPERSTATUS_PAYM_NOTEX) )
      MsgBox("Ошибка при установке статуса <Начисление комиссии> по операции.");
      return 1;
    end;
    if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_COMISS, DV_FX_OPERSTATUS_PAYM_NOTEX) )
      MsgBox("Ошибка при установке статуса <Оплата комиссий> по операции.");
      return 1;
    end;
  end;

  return 0;
end;
