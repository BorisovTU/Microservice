/*
$Name:        dvntaxreg_form.mac
$Module:      Производные инструменты
$Description: Форма ввода данных для отчета "Налоговый регистр по сделкам КО и внебиржевым ПИ"
*/
import "DlRepForm_h.mac", "globals.mac", DVInter;

CONST PNFLD_NTAXREG_DEPARTMENT      = 0,
      PNFLD_NTAXREG_DEPARTMENT_NAME = 1,
      PNFLD_NTAXREG_PERIOD          = 2,
      PNFLD_NTAXREG_YEAR            = 3,
      PNFLD_NTAXREG_GROWTH          = 4,
      PNFLD_NTAXREG_BEGINDATE       = 5,
      PNFLD_NTAXREG_ENDDATE         = 6,
      PNFLD_NTAXREG_DIRECTION       = 7,
      PNFLD_NTAXREG_DVKIND          = 8,
      PNFLD_NTAXREG_KINDINITIALFI   = 9,
      PNFLD_NTAXREG_FISS            = 10,
      PNFLD_NTAXREG_NOT_FISS        = 11,
      PNFLD_NTAXREG_TO_EXEL         = 12;
                             
const DV_DVKIND_CURSWAP_COMBO       = -1; // DV_DVKIND_CURSWAP || DV_DVKIND_CURSWAP_FX
const CURSWAP_COMBO_Name = "Валютный СВОП, СВОП";


CLASS DVNTaxRegFormData()
  VAR DepartmentID:integer  = -1, /*все*/
      Period:integer        = 0,
      Year:integer          = 0,
      GrowingItog:string    = UNSET_CHAR,
      BegDate:date          = {curdate},
      EndDate:date          = {curdate},
      Direction:integer     = 0,
      DVKind:integer        = 0,
      KindInitialFI:integer = 0,
      FISS:string           = SET_CHAR,
      NotFISS:string        = SET_CHAR,
      IsExportToExel:string = SET_CHAR;
END;

VAR DVNTaxRegRepFormData = DVNTaxRegFormData();

PRIVATE CONST H_DEPARTCODE    = 100,
              H_DEPARTNAME    = 101,
              H_DIRECTION     = 102,
              H_DVKIND        = 103,
              H_KINDINITIALFI = 104;


private MACRO DL_ListDVKind_Custom( AlgName:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, AddFilter:string )

   if( AddFilter == NULL )
      AddFilter = "";
   end;
   AlgName = ALG_DV_DVKIND;
   var q = "select t_szNameAlg, t_iNumberAlg from dnamealg_dbt where t_itypealg = " + string(AlgName) + " " + AddFilter;
   q = q + " union all ";
   q = q + " select '" + CURSWAP_COMBO_Name + "' t_szNameAlg, " + string(DV_DVKIND_CURSWAP_COMBO) +" t_iNumberAlg from dual";

   VAR Choice = DL_Scroll( q,
                           " ", X, Y,
                           "t_szNameAlg",""
                           );
   if( Choice != NULL )
      FldRealValue = Choice.Value(1); 
      FldShowValue = Choice.Value(0);
   end;

   return (Choice != NULL);

END;

private MACRO DV_UserFldProc_DVKind_Custom( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, NameAlg:integer, AddFilter:string ):INTEGER

   macro Name( NameAlg, FldRealValue)
      if(FldRealValue == DV_DVKIND_CURSWAP_COMBO)
         return CURSWAP_COMBO_Name;
      end;
      return DL_GetNameAlg( NameAlg, FldRealValue );
   end;

   NameAlg = ALG_DV_DVKIND;
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = 0;
     end;
     FldShowValue = Name( NameAlg, FldRealValue );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = 0;
     FldShowValue = Name( NameAlg, FldRealValue );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListDVKind_Custom( NameAlg, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), AddFilter );
  end;

  return CM_DEFAULT;
END;



MACRO DV_UserFldProc_NameAlg( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, NameAlg:integer, AddFilter:string ):INTEGER

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = 0;
     end;
     FldShowValue = DL_GetNameAlg( NameAlg, FldRealValue );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = 0;
     FldShowValue = DL_GetNameAlg( NameAlg, FldRealValue );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( NameAlg, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), AddFilter );
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DV_UserFldProc_Department( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
file Department( dp_dep );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = {OperDprt};
     end;
     if( FldRealValue < 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_DEPARTNAME, STR_ALLVALUE);
     else
        ClearRecord( Department );
        Department.Code = FldRealValue;
        GetGE(Department);
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPARTNAME, DL_GetDepartmentNameByCode(FldRealValue));
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     return pThis.SetLinkedValue( H_DEPARTNAME, STR_ALLVALUE );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) 
     if( ListDepartment( Department ) )
        FldRealValue = Department.Code;
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPARTNAME, DL_GetDepartmentNameByCode(FldRealValue)); 
     end;
  end;

  return CM_DEFAULT;
END;

MACRO DV_UserFldProc_Direction( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  return DV_UserFldProc_NameAlg( pThis, Cmd, Key, @FldShowValue, @FldRealValue, ALG_DV_DIRECTION );
END;

MACRO DV_UserFldProc_DVKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var OldFldRealValue = FldRealValue;

  DV_UserFldProc_DVKind_Custom( pThis, Cmd, Key, @FldShowValue, @FldRealValue, ALG_DV_DVKIND );

  if( OldFldRealValue != FldRealValue )
     if( (FldRealValue == DV_DVKIND_CURSWAP) or (FldRealValue == DV_DVKIND_PCTSWAP) or (FldRealValue == DV_DVKIND_CURSWAP_FX) or (FldRealValue == DV_DVKIND_CURSWAP_COMBO)  )
        pThis.SetLinkedValue(H_DIRECTION, 0/*Все*/);
        pThis.SetLinkedValue(H_KINDINITIALFI, ALG_DV_FIKIND_PRRATE_CURRENCY);
        DisableFields(pThis.Data(), PNFLD_NTAXREG_DIRECTION);
        if( FldRealValue == DV_DVKIND_PCTSWAP )
           DisableFields(pThis.Data(), PNFLD_NTAXREG_KINDINITIALFI);
        else
           EnableFields(pThis.Data(), PNFLD_NTAXREG_KINDINITIALFI);
        end;
     else
        EnableFields(pThis.Data(), PNFLD_NTAXREG_DIRECTION);
        EnableFields(pThis.Data(), PNFLD_NTAXREG_KINDINITIALFI);
     end;
  end;

  return CM_DEFAULT;
END;

MACRO DV_UserFldProc_KindinitialFI( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var AddFilter:string = "";
  var DvKind:integer = pThis.GetLinkedValue(H_DVKIND);
  if( ( DvKind == DV_DVKIND_CURSWAP ) or ( DvKind == DV_DVKIND_CURSWAP_FX) or ( DvKind == DV_DVKIND_CURSWAP_COMBO) )
     AddFilter = " and t_INumberAlg in( " + string(ALG_DV_FIKIND_PRRATE_CURRENCY) + ", " + string(ALG_DV_FIKIND_PRRATE_METAL) + ", " + string(ALG_DV_FIKIND_PRRATE_UNDEF) + " ) ";
  end;
  return DV_UserFldProc_NameAlg( pThis, Cmd, Key, @FldShowValue, @FldRealValue, ALG_DV_FIKIND_PRRATE, AddFilter );
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) DV_NTaxReg_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth;
     DateSplit( {curdate}, null, CurMonth, DVNTaxRegRepFormData.Year );
     DVNTaxRegRepFormData.Period = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( DVNTaxRegRepFormData.Period == 0 )
        DVNTaxRegRepFormData.Period = 4;
        DVNTaxRegRepFormData.Year   = DVNTaxRegRepFormData.Year - 1;
     end;
     /*Получим отчетную дату - последнюю дату из отчетного периода.*/
     Period_GetInterval( DVNTaxRegRepFormData.Period + 12, DVNTaxRegRepFormData.Year, @DVNTaxRegRepFormData.BegDate, @DVNTaxRegRepFormData.EndDate );

     AddFieldProc( 0, PNFLD_NTAXREG_DEPARTMENT,      H_DEPARTCODE,        @DV_UserFldProc_Department,    DVNTaxRegRepFormData.DepartmentID );
     AddFieldProc( 0, PNFLD_NTAXREG_DEPARTMENT_NAME, H_DEPARTNAME,        @Default,                      "" );
     AddFieldProc( 0, PNFLD_NTAXREG_PERIOD,          DL_PNFLD_PERIOD,     @DL_FldProc_Period,            DVNTaxRegRepFormData.Period + 12, 14, 8 );
     AddFieldProc( 0, PNFLD_NTAXREG_YEAR,            DL_PNFLD_YEAR,       @DL_FldProc_Year,              DVNTaxRegRepFormData.Year );
     AddFieldProc( 0, PNFLD_NTAXREG_GROWTH,          DL_PNFLD_GROWTH,     @DL_FldProc_Growth,            DVNTaxRegRepFormData.GrowingItog );
     AddFieldProc( 0, PNFLD_NTAXREG_BEGINDATE,       DL_PNFLD_BEGINDATE,  @DL_FldProc_BeginDate,         DVNTaxRegRepFormData.BegDate );
     AddFieldProc( 0, PNFLD_NTAXREG_ENDDATE,         DL_PNFLD_ENDDATE,    @DL_FldProc_EndDate,           DVNTaxRegRepFormData.EndDate );
     AddFieldProc( 0, PNFLD_NTAXREG_DIRECTION,       H_DIRECTION,         @DV_UserFldProc_Direction,     DVNTaxRegRepFormData.Direction, 28, 12 );
     AddFieldProc( 0, PNFLD_NTAXREG_DVKIND,          H_DVKIND,            @DV_UserFldProc_DVKind,        DVNTaxRegRepFormData.DVKind, 28, 14 );
     AddFieldProc( 0, PNFLD_NTAXREG_KINDINITIALFI,   H_KINDINITIALFI,     @DV_UserFldProc_KindinitialFI, DVNTaxRegRepFormData.KindInitialFI, 28, 16 );
     AddFieldProc( 0, PNFLD_NTAXREG_FISS,            DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,          DVNTaxRegRepFormData.FISS );
     AddFieldProc( 0, PNFLD_NTAXREG_NOT_FISS,        DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,          DVNTaxRegRepFormData.NotFISS );
     AddFieldProc( 0, PNFLD_NTAXREG_TO_EXEL,         DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,          DVNTaxRegRepFormData.IsExportToExel );
  END;

  PRIVATE MACRO Check():BOOL
     if( (this.GetFieldValue(PNFLD_NTAXREG_FISS) == UNSET_CHAR) and (this.GetFieldValue(PNFLD_NTAXREG_NOT_FISS) == UNSET_CHAR) )
        msgbox("Не установлены признаки сделок для включения в отчет");
        return false;
     end;

     return true;
  END;

  initDL_CPanel( "dv.lbr", "PNTAXREG" );
END;
