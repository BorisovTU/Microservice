/*
$Name:        dvoptransferrep.mac
$Module:      Производные инструменты
$Description: Отчет по операции "Перенос по срокам на внебалансе"
*/
import "dv_categ.mac";

PRIVATE VAR DvOper = TrecHandler( "dvoper" );

PRIVATE MACRO FindDVOPER( OperID:INTEGER ):BOOL
  var   Data = TRsbDataSet( "SELECT * FROM ddvoper_dbt WHERE t_ID = " + string(OperID) );

  if( Data.MoveNext() )
     Data.GetRecord().CopyTo( DvOper.rec );
     return true;
  end;
  return false;
END;

PRIVATE MACRO ОтчетПоШагу( oper, oprstep )

  record doc( acctrn );
  var FDoc = TRecHandler( "dvndeal" );

  if( ПолучитьПервичныйДокументСервОперПИ( oper, FDoc ) == 0 )
     var FD = DVFirstDocNDeal( oper.DocKind, FDoc );

     while( GetDocsByOperStep( doc, oper.ID_Operation, oprstep.ID_Step ) )
[│###############│##################│#######│#########################│#########################│####################│
]( FD.Deal.rec.Code,
   FD.DvKindName(),
   IIF(doc.Result_Carry == INPCARRY, "Т", "О"),
   doc.Account_Payer,
   doc.Account_Receiver,
   Abs(doc.Sum_Payer)
);
     end;
  end;

END;

PRIVATE MACRO Report( ErrorStr:string ):BOOL

  file oper( oproper );
  var oprstep = TBFile( "oprstep", "R", 9 ),
      CurNum:integer = 0;

[                        ПЕРЕНОС ПО СЧЕТАМ СРОЧНОСТИ НА ВНЕБАЛАНСЕ ВНЕБИРЖЕВЫХ СДЕЛОК
┌───────────────┬──────────────────┬───────┬─────────────────────────┬─────────────────────────┬────────────────────┐
│  Код сделки   │     Вид ПИ       │Вид Т/О│        Счет Дт          │        Счет Кт          │       Сумма        │
├───────────────┼──────────────────┼───────┼─────────────────────────┼─────────────────────────┼────────────────────┤
];

  oprstep.Clear;
  oprstep.AddFilter( "t_ServDocKind = " + string(DvOper.rec.DocKind)
                   + " and t_ServDocID = " + string(DvOper.rec.ID) );

  InitProgress( oprstep.NRecords, "Подготовка отчета ...", "Отчет по операции переноса по срокам на внебалансе" );

  var IsContinue = true;
  while( IsContinue and oprstep.Next )
     if( oprstep.rec.IsRemoveStep != "" )
        ClearRecord( oper );
        oper.ID_Operation = oprstep.rec.ID_Operation;
        if( GetEQ(oper) )
           ОтчетПоШагу( oper, oprstep.rec );
        end;
     end;

     UseProgress( CurNum = CurNum + 1 );
  end;

[└───────────────┴──────────────────┴───────┴─────────────────────────┴─────────────────────────┴────────────────────┘];

  RemProgress();
  if (ErrorStr != "")
     ErrorStr = "Ошибки при выполнении:\n" + ErrorStr;
     Print(ErrorStr);
  end;
  var Executer = DepoExecuter( DvOper.rec.oper );
  Print( "\nСоставил ( " + Executer.Post + " ):\n" +
         string( Executer.Name ) + "                 " + String( DvOper.rec.Date ) + "\n" +
         DL_StrCut( "", IIF(strlen(Executer.Name) <= 10, 10, strlen(Executer.Name) ), "─" ) + " ───────────────   дата" + "\n" +
         DL_StrCut("", IIF(strlen(Executer.Name) <= 10, 0, (strlen(Executer.Name) - 10) / 2 ), " ") + "   ФИО    " + DL_StrCut("", IIF(strlen(Executer.Name) <= 10, 0, (strlen(Executer.Name) - 10) / 2), " " ) + "    подпись\n\n\n" );

  return true;
END;

MACRO PrintReport( OperID:INTEGER, ErrorStr:string )
  VAR ReportFileName, errtext, errcode;
  FILE ReportOutFile() txt write; /*файл вывода для отчета*/

  if( not FindDVOPER( OperID ) )
     MsgBox( "Не найдена сервисная операция переноса по срокам на внебалансе (ID="+string(OperID)+")" );
     return false;
  end;

  ReportFileName = GetTxtFileName( "dvtransfer_" + DvOper.rec.Code );
  if( not Open( ReportOutFile, ReportFileName ) )
     errcode = status(errtext);
     RunError( "Ошибка при создании файла|"+ReportFileName+"|(" + errcode + ") " + errtext );
  end;
  SetOutput( ReportFileName );

  Report(ErrorStr);

  SetOutput( NULL, true );
  close( ReportOutFile );
  ViewFile( ReportOutFile );
  return true;
END;
