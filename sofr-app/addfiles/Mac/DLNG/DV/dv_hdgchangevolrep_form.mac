/*
$Name:         dv_hdgchangevolrep_form.mac
$Module:       ФИСС и КО
$Description:  Отчет "Изменение объёма объектов хеджирования" - форма ввода данных
*/

IMPORT "DlRepForm_h.mac";

// Номера полей в форме отчета
CONST
    PNFLD_HDGCHV_DEPARTCODE   = 0, // код филиала
    PNFLD_HDGCHV_DEPARTMENT   = 1, // наименование филиала
    PNFLD_HDGCHV_DATE         = 2, // дата начала
    PNFLD_HDGCHV_CB_ACQUIRED  = 3, // Ц/б приобретённые
    PNFLD_HDGCHV_CB_EMISSED   = 4, // Ц/б выпущенные
    PNFLD_HDGCHV_BN_ACCOUNTED = 5, // Учтённые векселя
    PNFLD_HDGCHV_BN_EMISSED   = 6, // Выпущенные векселя
    PNFLD_HDGCHV_MBK          = 7, // Сделки МБК
    PNFLD_HDGCHV_ISAPOSTR     = 8, // С апострофами
    PNFLD_HDGCHV_EXCEL        = 9; // Выпуск отчета в Mi-crosoft Excel 


PRIVATE MACRO SetCheckBoxByBackOffice(BOChar)
  if ((strfor(GetIdentProgram()) == BOChar) OR (strfor(GetIdentProgram()) == "Ю"))
    return SET_CHAR;
  end;

  return UNSET_CHAR;
end;


CLASS ( DL_CPanel ) HDGChangeVolume_Panel()
    PRIVATE MACRO Init()

        AddFieldProc( 0, PNFLD_HDGCHV_DEPARTCODE,   DL_PNFLD_DEPARTCODE, @DL_FldProc_DepartCode, -1);
        AddFieldProc( 0, PNFLD_HDGCHV_DEPARTMENT,   DL_PNFLD_DEPARTMENT, @DL_FldProc_Department, -1);
        AddFieldProc( 0, PNFLD_HDGCHV_DATE,         DL_PNFLD_BEGINDATE,  @DL_FldProc_BeginDate, {curdate} );
        AddFieldProc( 0, PNFLD_HDGCHV_CB_ACQUIRED,  DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox, SetCheckBoxByBackOffice("S"));
        AddFieldProc( 0, PNFLD_HDGCHV_CB_EMISSED,   DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox, SetCheckBoxByBackOffice("S"));
        AddFieldProc( 0, PNFLD_HDGCHV_BN_ACCOUNTED, DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox, SetCheckBoxByBackOffice("N"));
        AddFieldProc( 0, PNFLD_HDGCHV_BN_EMISSED,   DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox, SetCheckBoxByBackOffice("Y"));
        AddFieldProc( 0, PNFLD_HDGCHV_MBK,          DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox, SetCheckBoxByBackOffice("J"));
        AddFieldProc( 0, PNFLD_HDGCHV_ISAPOSTR,     DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox, SET_CHAR);
        AddFieldProc( 0, PNFLD_HDGCHV_EXCEL,        DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox, SET_CHAR);

        if (strfor(GetIdentProgram()) != "Ю")
          if(strfor(GetIdentProgram()) != "S")
            DisableFields(This.Data(), PNFLD_HDGCHV_CB_ACQUIRED);
            DisableFields(This.Data(), PNFLD_HDGCHV_CB_EMISSED);
          end;
          if(strfor(GetIdentProgram()) != "N")
            DisableFields(This.Data(), PNFLD_HDGCHV_BN_ACCOUNTED);
          end;
          if(strfor(GetIdentProgram()) != "Y")
            DisableFields(This.Data(), PNFLD_HDGCHV_BN_EMISSED);
          end;
          if(strfor(GetIdentProgram()) != "J")
            DisableFields(This.Data(), PNFLD_HDGCHV_MBK);
          end;
        end;
    END;

    PRIVATE MACRO Check():BOOL
      if ((this.GetFieldValue( PNFLD_HDGCHV_CB_ACQUIRED ) == UNSET_CHAR) and
         (this.GetFieldValue( PNFLD_HDGCHV_CB_EMISSED ) == UNSET_CHAR) and 
         (this.GetFieldValue( PNFLD_HDGCHV_BN_ACCOUNTED ) == UNSET_CHAR) and 
         (this.GetFieldValue( PNFLD_HDGCHV_BN_EMISSED ) == UNSET_CHAR) and 
         (this.GetFieldValue( PNFLD_HDGCHV_MBK ) == UNSET_CHAR))
        msgbox("Необходимо выбрать хотя бы один параметр для выпуска отчета.");
        return false;
      end;
      return true;
    END;

    initDL_CPanel( "dv.lbr", "HDGCHVOL" );
END;
