/*
$Name:        dvsw120.mac
$Module:      Производные инструменты
$Description: Валютный СВОП. Шаг "Исполнение требований"
*/
import "dv_car.mac";

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  VAR FD;
  VAR ДатаИсполненияШага;

  SetBuff( ndeal, FDoc );
  FD = DVFirstDocNDeal( DocKind, ndeal );
  if (FD.ОтказОтИсполнения())
     return 0;
  end;
  var ПлатежТреб = RsbPayment( FD.ПлатежТреб.rec.PaymentID );

  if (FD.Netting())//TEG 08.02.19
      if (ПлатежТреб.PaymStatus != PM_FINISHED) 
          if ( (FD.ВалютныйРынок()) and (not FD.IsClient()) )
                if( ПИ_УстановитьСтатусПлатежа(ПлатежТреб, PM_CLOSED_W_M_MOVEMENT) )
                    MsgBox("Ошибка при установке платежу статуса \"Исполнен\"");
                    return 1;
                end; 
          end;
      end;

      if( not InsertOprStatus(FD.DV_OPERSTATUS_DEMAND(), DV_OPERSTATUS_DL_COMPLETE) )
          MsgBox("Ошибка при установке статуса <Требования> по операции.");
          return 1;
      end;
      return 0;
  end;
  ДатаИсполненияШага = DV_GetPlanDateStep(ID_Operation, ID_Step);
  if( ДатаИсполненияШага == date(0,0,0) )
     ДатаИсполненияШага = FD.ДатаТребования();
  end;


/*>>BPV*/
  if (FD.ВалютныйРынок() and FD.IsClient())
     var nacdl = TRecHandler("dvnacdl.dbt");
     var Sql = " SELECT dlcomis.t_ID,  dlcomis.t_PlanPayDate, dlcomis.t_Sum, dlcomis.t_NDS, dlcomis.t_IsBankExpenses, comis.t_ReceiverID, comis.t_FIID_Comm " +
               "   FROM ddlcomis_dbt dlcomis, dsfcomiss_dbt comis  " +
               "  WHERE dlcomis.t_DocKind     = ? " +
               "    AND dlcomis.t_DocID       = ? " +
              // "    AND dlcomis.t_FactPayDate = TO_DATE('01.01.0001', 'dd.mm.yyyy') " +/*bpv комиссия уже помечена как оплаченная, нам нужна сумма*/
               "    AND dlcomis.t_PlanPayDate <= ? " +
               "    AND dlcomis.t_FeeType     = comis.t_FeeType " +
               "    AND dlcomis.t_ComNumber   = comis.t_Number " +
               " order by dlcomis.t_PlanPayDate ";

     var Query = RSDCommand(Sql);
     Query.NullConversion = true;
     Query.addParam("", RSDBP_IN, FD.Kind);
     Query.addParam("", RSDBP_IN, FD.ID);
     Query.addParam("", RSDBP_IN, FD.deal.rec.Date);/*Дата комиссии, пока считаем, что начало сделки*/
     Query.execute();

     var Data = TRsbDataSet(Query);
     while( Data.MoveNext() )
        nacdl.clear();
        nacdl.rec.Date    = ДатаИсполненияШага/*Data.rec.PlanPayDate*/;
        nacdl.rec.Sum     = Data.rec.Sum;
        nacdl.rec.AccFIID = Data.rec.FIID_Comm;
        nacdl.rec.TaxSum  = Data.rec.NDS;
        nacdl.rec.PaySum  = Data.rec.Sum - Data.rec.NDS;

        if (money(nacdl.rec.Sum) != 0.0)
           if(FD.IsClient() AND FD.КонтрагентНКЦ() AND Data.rec.IsBankExpenses == SET_CHAR) //обработка комиссий ОРБ в функции ОплатаКомиссииПосреднику
           else
              if(not ПроводкаПоКатегориямУчета( FD, /*FIROLE_COMISS_CLIENT*/
                                                "+РасчетыКомисс1", IIF(( Data.rec.ReceiverID == {OurBank} ), "+КВ, ц/б","+Биржа"), /* КатегДеб , КатегКредит*/
                                                nacdl.rec.Date,        /* Дата */
                                                1,                     /* Глава */
                                                nacdl.rec.AccFIID,     /* Валюта */
                                                nacdl.rec.Sum,         /* Сумма  */
                                                null,
                                                INPCARRY,             /* Result_Carry */
                                                " 1",                 /* Kind_Oper */
                                                "",                   /* Numb_Document */
                                                "Комиссия " + IIF(( Data.rec.ReceiverID == {OurBank} ), "брокера","биржи")+ ". Сделка СВОП N " + FD.deal.rec.code + " от " + FD.deal.rec.date + " по брокерскому договору N " + FD.ClientDBOContrName() + " от " + FD.ClientDBOContrDate() + ". НДС не облагается",
                                                0,null,FIROLE_COMISS_CLIENT/*null*/,
                                                nacdl.rec.AccFIID, 0     /* валюты счетов: Дт - Кт */ 
                                              )
                 )
                    return 1;
              end;    
           end;

           if( Data.rec.ReceiverID == {OurBank} )
              nacdl.rec.Direction = DV_CALCOP_DIRECTION_RECEPTION;
              if( ОплатаКомиссииБанку(FD, doc, nacdl) != 0 )
                 return 1;
              end;
           else
              nacdl.rec.Direction = DV_CALCOP_DIRECTION_PAYMENT;
              if( ОплатаКомиссииПосреднику(FD, doc, nacdl, Data.IsBankExpenses == SET_CHAR) != 0 )
                 return 1;
              end;
           end;
        end;
     end;
   end;

/*<<BPV*/ 

  if (ПлатежТреб.PaymStatus == PM_OVERDUE)
   var stat = true;
   var findAccount = "";
      stat = FD.FindNumberAccount( "Треб. с н.с.",  FindAccount );
      ПлатежТреб.FutureReceiverAccount = FindAccount;
  end;

  if( ИсполнитьТребование(FD, ДатаИсполненияШага, doc) != 0 )
     return 1;
  end;

  if( not InsertOprStatus(FD.DV_OPERSTATUS_DEMAND(), DV_OPERSTATUS_DL_COMPLETE) )
     MsgBox("Ошибка при установке статуса <Требования> по операции.");
     return 1;
  end;

  var Дк = DL_GetMinComPlanDateByOper(DocKind, FD.Deal.rec.ID);

  if( Дк != date(0,0,0) )
     if( not InsertOprStatus(FD.DV_OPERSTATUS_COMISS(), DV_OPERSTATUS_PAYM_EX) )
        MsgBox("Ошибка при установке статуса <Оплата комиссий> по операции.");
        return 1;
     end;
     DL_CalendDefineOprDate(FD,DV_NDEAL_OPRDATE_COMISS, Дк);
  else
     if( not InsertOprStatus(FD.DV_OPERSTATUS_COMISS(), DV_OPERSTATUS_PAYM_NOTEX) )
        MsgBox("Ошибка при установке статуса <Оплата комиссий> по операции.");
        return 1;
     end;
  end;

  return 0;
end;
