/*
$Name:        dvprconf.mac
$Module:      ФИСС и КО
$Description: Печать подверждения сделки ФИСС и КО
*/
import "dv_fun.mac", "dvrepfun.mac", "dv_categ.mac";

private const ALG_FX_LINK_CHANNEL = 2118,
      CODE_BIK = 3,
      buy = "покупаем",
      sale = "продаем";
PRIVATE VAR nDeal = TBfile("dvndeal"),
            FD_ND;

// Данные для формирования подтверждения
PRIVATE CLASS TConfData
  VAR BofficeKind,
      DealID,
      DealType,
      TraderID,
      DealCodePS,
      PartyID,
      DealCode,
      DealDate,
      LinkChannel,
      Comment,

      Price,
      Point,
      Principal,
      PFI,
      maturity,
      Cost,
      CFI,
      expiry;

  macro Construct()
    TraderID = -1;/*UnknownParty*/
    DealCodePS = "";
    LinkChannel = -1;
  end;

  this.Construct();
END;
PRIVATE VAR ConfData = TConfData();

PRIVATE MACRO NameAlg(type, num)
  var alg = TBfile("namealg");
  alg.KeyNum = 0;
  alg.rec.iTypeAlg = type;
  alg.rec.iNumberAlg = num;
  if( not GetEq(alg) )
    return "";
  else
    return alg.rec.szNameAlg;
  end;
END;

PRIVATE MACRO PartyName(ID)
  if (ID == 0)
    return {Name_Bank};
  end;
  var party = TBfile("party");
  party.KeyNum = 0;
  party.rec.PartyID = ID;
  if( not GetEq(party) )
    return ID;
  else
    return party.rec.Name;
  end;
END;

PRIVATE MACRO limiter (pm)
    return ((pm.DocKind==ConfData.BofficeKind) and
            (pm.DocumentID==ConfData.DealID) and
            ((pm.Purpose == BAi) or (pm.Purpose == CAi) or (pm.Purpose == BRi) or (pm.Purpose == CRi)));
END;

PRIVATE MACRO PartyCode(ID, type)
  var pcode = TBfile("partcode"),
      saveID = 0;
  if (ID == 0)
    if (type == CODE_BIK)
      return {MFO_Bank};
    else
      pcode.KeyNum = 1;
      pcode.rec.CodeKind = CODE_BIK;
      pcode.rec.Code = {MFO_Bank};
      if (not GetEQ(pcode))
        return "";
      else
        saveID = pcode.rec.PartyID;
        pcode.KeyNum = 0;
        pcode.rec.PartyID = saveID;
        pcode.rec.CodeKind = type;
        if (not GetEQ(pcode))
          return "";
        else
          return pcode.rec.Code;
        end;
      end;
    end;
  end;
  pcode.KeyNum = 0;
  pcode.rec.PartyID = ID;
  pcode.rec.CodeKind = type;
  if( not GetEq(pcode) )
    return "";
  else
    return pcode.rec.Code;
  end;
  
END;

PRIVATE MACRO Direction()
  if (ConfData.DealType == 2)
    return sale;
  else
    return buy;
  end;
END;

PRIVATE MACRO Currency(PFI)
  var fininstr = TBfile("fininstr");
  fininstr.KeyNum = 0;
  fininstr.rec.FIID = PFI;
  if( not GetEq(fininstr) )
    return "___";
  else
    return fininstr.rec.Ccy;
  end;
END;

PRIVATE MACRO Rate()
  var Rate, RateStr;

  Rate = ConfData.Cost / ConfData.Principal;
  RateStr = ЧислоCЗаданнойТочностью(Rate, ConfData.Point) + " " + Currency(ConfData.PFI) + "/ " + Currency(ConfData.CFI);

  return RateStr;
END;

PRIVATE MACRO TraderFIO()
  var persn = TBfile("persn");
  persn.KeyNum = 0;
  persn.rec.PersonID = ConfData.TraderID;
  if(not GetEq(persn))
    return "______________________________";
  else
    return persn.rec.Name1 +" "+ persn.rec.Name2 +" "+ persn.rec.Name3;
  end;
END;

PRIVATE MACRO GetBossBO()
  var officer = TBfile("officer"),
      ptoffice = TBfile("ptoffice"),
      persn = TBfile("persn"),
      OfficeID,
      stat = 0;

  GetRegistryValue("ПРОИЗВОДНЫЕ ИНСТРУМЕНТЫ\\ОТДЕЛ КОНВЕРСИОННЫХ ОПЕРАЦИЙ", V_INTEGER, OfficeID, stat);
  if (stat == 0)
    ptoffice.KeyNum = 0;
    ptoffice.rec.PartyID = {OurBank};
    ptoffice.rec.OfficeID = OfficeID;
    if (GetEq(ptoffice))
      officer.KeyNum = 5;
      officer.rec.PartyID = {OurBank};
      officer.rec.OfficeID = ptoffice.rec.OfficeID;
      officer.rec.IsFirstOfficePerson = "X";
      if (GetEq(officer))
        persn.KeyNum = 0;
        persn.rec.PersonID = officer.rec.PersonID;
        if (GetEq(persn))
          return persn.rec.Name1 + " " + persn.rec.Name2 + " " + persn.rec.Name3;
        end;
      end;
    end;
  end;
  return "______________________________";  
END;

PRIVATE MACRO GetBoss()
  if ({FIO_Boss} == "")
    return "______________________________";
  else
    return {FIO_Boss};
  end;
END;

PRIVATE MACRO PurposeName(Purpose)
  var pm_purp = TBfile("pmpurp");
  pm_purp.KeyNum = 0;
  pm_purp.rec.Purpose = Purpose;
  if( not GetEq(pm_purp) )
    return ")";
  else
    return pm_purp.rec.ShortName + ")";
  end;
END;

PRIVATE MACRO PaymNetting(Netting)
  if (Netting == "X")
    return "б/д";
  else
    return "д";
  end;
END;

PRIVATE MACRO PrintHeader()
  ARRAY TmpName;
/*0         1         2         3         4         5         6         7         8*/
 [                                                                              ];
 [                                                                              ];
 [                                                                              ]; 
   strsplit({Name_Bank}, TmpName, 50);
 [           ################################################## ########## #####] (TmpName(0), {CurDate}, time:5:t);
   if (valType(TmpName(1)) == V_STRING)   
 [           ##################################################                 ] (TmpName(1));
   end;
 [направляет Подтверждение № ________________   ################  от  ##########] (ConfData.DealCodePS, {CurDate});
  strsplit({Name_Bank}, TmpName, 50);
 [в          ##################################################                 ] (PartyName(ConfData.PartyID));
   if (valType(TmpName(1)) == V_STRING)   
 [           ##################################################                 ] (TmpName(1));
   end;
END;

PRIVATE MACRO PrintDeal()
  var i = 0;
  ARRAY TmpComment; 
  var TraderCode= "";
  if (ConfData.TraderID > 0)
    TraderCode = TraderCode + ПолучитьКодСубъекта (ConfData.TraderID, PTCK_CONTR );
  end;
/*0         1         2         3         4         5         6         7         8*/
 [                                                                              ];
 [Сделка № ################ заключена ##########         канал связи: ##########] (ConfData.DealCode, ConfData.DealDate:f, NameAlg(ALG_FX_LINK_CHANNEL, ConfData.LinkChannel));
 [трейдер: #####################                                                ] (TraderCode);
 [Мы ##########:############################# ### датой ##########              ] (Direction(), ConfData.Principal:21:2, Currency(ConfData.PFI), ConfData.maturity:f);
 [         за  :############################# ### датой ##########              ] (ConfData.Cost:21:2, Currency(ConfData.CFI), ConfData.expiry:f);
 [по курсу #############################################                        ] (Rate():l);
 [                                                                              ];
 strsplit(ConfData.Comment, TmpComment, 78, 62, 3);
 [Особые условия: ##############################################################] (TmpComment(i));
 i = i + 1;
 while (valType(TmpComment(i)) == V_STRING)
 [##############################################################################](TmpComment(i));
   i = i + 1;
 end;
END;

PRIVATE MACRO PrintPayments()
  var payment = TBfile("pmpaym"),
      Об = TArray, i = 0, /*обязательства*/
      Тр = TArray, j = 0; /*требования*/
  macro filter
    return true;
  end;

  macro definePaym(paym)
    if( ((Direction() == buy) and ((paym.rec.Purpose == CAi) or (paym.rec.Purpose == BRi))) or
         ((Direction() == sale) and ((paym.rec.Purpose == BAi) or (paym.rec.Purpose == CRi))) )
      Об(i) = TrecHandler("pmpaym");
      copy(Об(i), paym);
      i = i + 1;
    else
      Тр(j) = TrecHandler("pmpaym");
      copy(Тр(j), paym);
      j = j + 1;
    end;
  end;

  macro printPaym(pm, dirr, DebetCredit)
 if ( ( (pm.rec.payeraccount != "") and (pm.rec.payerbankid == -1) ) or ( (pm.rec.receiveraccount != "") and (pm.rec.receiverbankid == -1) ) )
     dirr = dirr + " наличными";
 end;
 [########## ################## ##################### ### (#                           ###](pm.rec.ValueDate:f, dirr, pm.rec.Amount:21:2, Currency(pm.rec.PayFIID), PurposeName(pm.rec.Purpose), PaymNetting(pm.rec.Netting));
    ARRAY TmpName;
    ARRAY TmpBankName;
    ARRAY TmpCorrName;
    var i = 0,
      pmprop = TBfile("pmprop"),
      pmrmprop = TBfile("pmrmprop"),
      cs = TBfile("corschem");
    pmrmprop.KeyNum = 0;
    pmrmprop.rec.PaymentID = pm.rec.PaymentID;
    if (not GetEQ(pmrmprop))
      pmrmprop.rec.PayerName = "                   ";
      pmrmprop.rec.ReceiverName = "                   ";
      pmrmprop.rec.PayerBankName = "                   ";
      pmrmprop.rec.ReceiverBankName = "                   ";
      pmrmprop.rec.PayerCorrBankName = "                   ";
      pmrmprop.rec.ReceiverCorrBankName = "                   ";
      pmrmprop.rec.PayerINN = "                   ";
      pmrmprop.rec.ReceiverINN = "                   ";
    end;
     strsplit(pmrmprop.rec.ReceiverName, TmpName, 20, 20, 1);
     strsplit(pmrmprop.rec.ReceiverBankName, TmpBankName, 20, 19, 1);
     strsplit(pmrmprop.rec.ReceiverCorrBankName, TmpCorrName, 20, 19, 1);
 var BIK = ПолучитьКодСубъекта (pm.rec.Receiver, PTCK_BIC );
 if (BIK != "")
   [в пользу #################### ########### ############# ИНН ##################](TmpName(i), DL_GetCodeKindName( PTCK_BIC ), BIK, pmrmprop.rec.ReceiverINN);
 else
   [в пользу #################### ########### ############# ИНН ##################](TmpName(i), DL_GetCodeKindName( pm.rec.ReceiverCodeKind ), pm.rec.ReceiverCode , pmrmprop.rec.ReceiverINN);
 end;
    i = i + 1;
    while (valType(TmpName(i)) == V_STRING)
 [         ####################                                                 ](TmpName(i));
      i = i + 1;
    end;
     i = 0;
    pmprop.KeyNum = 0;
    pmprop.rec.PaymentID = pm.rec.PaymentID;
    pmprop.rec.DebetCredit = 1;
    if (pmprop.GetEQ() and (pmprop.rec.Group == PAYMENTS_GROUP_EXTERNAL))
 [на счет #################### в банке ################### ########### #########](pm.rec.ReceiverAccount, TmpBankName(i), DL_GetCodeKindName(PTCK_BIC), pmprop.rec.BankCode);
      i = i + 1;
      while (valType(TmpBankName(i)) == V_STRING)
 [                                     ####################                     ](TmpBankName(i));
        i = i + 1;
      end;
      
      i = 0;
 [корсчет #################### в банке ################### ########### #########](pmprop.rec.CorrAcc, TmpCorrName(i), DL_GetCodeKindName(PTCK_BIC), pmprop.rec.CorrCode);
      i = i + 1;
      while (valType(TmpCorrName(i)) == V_STRING)
 [                                     ####################                     ](TmpCorrName(i));
        i = i + 1;
      end;
    else /*кредит - внутреннее свойство - значит это платеж к нам в банк*/
      i = 0;
 [на счет #################### в банке ################### ########### #########](pm.rec.ReceiverAccount, TmpBankName(i), DL_GetCodeKindName(PTCK_BIC), pmprop.rec.BankCode);
      i = i + 1;
      while (valType(TmpBankName(i)) == V_STRING)
 [                                     ####################                     ](TmpBankName(i));
        i = i + 1;
      end;
      /*можно попробовать найти дебет - восстановить корсчет через корсхему*/
      pmprop.KeyNum = 0;
      pmprop.rec.PaymentID = pm.rec.PaymentID;
      pmprop.rec.DebetCredit = 0;
      if (pmprop.GetEQ())
        cs.KeyNum = 0;
        cs.rec.Number = pmprop.rec.Corschem;
        cs.rec.FI_Kind = 1;
        cs.rec.FIID = pm.rec.PayFIID;
        if (cs.GetEQ())
          asize(TmpCorrName, 0);
          i = 0;
          strsplit(PartyName(cs.rec.CorrID), TmpCorrName, 20, 19);

 [корсчет #################### в банке ################### ########### #########](cs.rec.CorAccount, TmpCorrName(i), DL_GetCodeKindName(PTCK_BIC), PartyCode(cs.rec.CorrID, PTCK_BIC));
          i = i + 1;
          while (valType(TmpCorrName(i)) == V_STRING)
 [                                     ####################                     ](TmpCorrName(i));
            i = i + 1;
          end;
        end;

      end;
    end;
  end;
/*Для платежей по БА, контрактиву - печать */
  payment.KeyNum = 1;
  payment.rec.DocKind = ConfData.BofficeKind;
  payment.rec.DocumentID = ConfData.DealID;
  payment.rec.Purpose = 0;
  payment.rec.SubPurpose = 0;
  if (GetGE(payment) and limiter(payment.rec))
    if (filter)
      definePaym(payment);
    end;
    while (Next(payment) and limiter(payment.rec))
      if (filter)
        definePaym(payment);
      end;
    end;
  end;
/*0         1         2         3         4         5         6         7         8*/
 [                                                                              ];
 [------------------ ДВИЖЕНИЕ СРЕДСТВ и ПЛАТЕЖНЫЕ ИНСТРУКЦИИ -------------------];
 if (Об.size > 0)
    i = 0;
 [ОБЯЗАТЕЛЬСТВА                                                                 ];
    while (i < Об.size)
      printPaym(Об(i), "платим", 0);
      i = i + 1;
    end;
  end;
  if (Тр.size > 0)
    j = 0;
 [ТРЕБОВАНИЯ                                                                    ];
    while (j < Тр.size)
      printPaym(Тр(j), "получаем", 1);
      j = j + 1;
    end;
  end;

 [------------------------------------------------------------------------------];
END;

PRIVATE MACRO PrintFooter()
/*0         1         2         3         4         5         6         7         8*/
 [                                                                              ];
 [Трейдер                _________________  ##############################      ](TraderFIO());
 [                           (Подпись)      Ф.И.О.                              ];
 [                                                                              ];
 [Начальник БО           _________________  ##############################      ](GetBossBO());
 [                           (Подпись)      Ф.И.О.                              ];
 [                                                                              ];
 [Председатель Правления _________________  ##############################      ](GetBoss():30:t);
 [                           (Подпись)      Ф.И.О.                              ];
 [                                                                              ];
END;

PRIVATE MACRO CreateFileName()
    var hour,min,sec, Path,
        txtRegistryPath = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR";

    if(DV_SetRegistryPath(txtRegistryPath, Path))
       return "";
    end;

    // получить полный путь
    Path = DVCMT_processPath(Path) + "\\";
    TimeSplit (Time,hour,min,sec);
    return String(Path,{oper},hour,min,sec,".txt");
END;

PRIVATE MACRO FillConfData()

  ConfData.BofficeKind = FD_ND.Kind;
  ConfData.DealID = FD_ND.Deal.rec.ID;
  ConfData.DealType = FD_ND.Deal.rec.Type;
  ConfData.TraderID = FD_ND.Deal.rec.TraderID;
  ConfData.DealCodePS = FD_ND.Deal.rec.DealCodePS;
  ConfData.PartyID = FD_ND.Deal.rec.Contractor;
  ConfData.DealCode = FD_ND.Deal.rec.Code;
  ConfData.DealDate = FD_ND.Deal.rec.Date;
  ConfData.LinkChannel = -1;
  ConfData.Comment = FD_ND.Deal.rec.Comment;

  if( FD_ND.Kind == DL_DVFXDEAL )
    ConfData.Price = FD_ND.nFi().rec.Price;
    ConfData.Point = FD_ND.nFi().rec.Point;
    ConfData.Principal = FD_ND.nFi().rec.Amount;
    ConfData.PFI = FD_ND.nFI().rec.Fiid;
    ConfData.maturity = FD_ND.nFI().rec.SuplDate;
    ConfData.Cost = FD_ND.nFI().rec.Cost;
    ConfData.CFI = FD_ND.nFI().rec.PriceFIID;
    ConfData.expiry = FD_ND.nFI().rec.PayDate;
  else                                                                 
    ConfData.Price = FD_ND.nFI_BaseActiv.rec.Price;
    ConfData.Point = FD_ND.nFI_BaseActiv.rec.Point;
    ConfData.Principal = FD_ND.nFI_BaseActiv.rec.Amount;
    ConfData.PFI = FD_ND.nFI_BaseActiv.rec.Fiid;
    ConfData.maturity = FD_ND.nFI_BaseActiv.rec.SuplDate;
    ConfData.Cost = FD_ND.nFI_BaseActiv.rec.Cost;
    ConfData.CFI = FD_ND.nFI_BaseActiv.rec.PriceFIID;
    ConfData.expiry = FD_ND.nFI_BaseActiv.rec.PayDate;
  end;

END;

MACRO PrintConfirmation (DealID, OutFile, first)
  file out() txt;
  var oldOutFile = "";

  if (OutFile != "")
    oldOutFile = SetOutput(OutFile, TRUE);
  else
    OutFile = SetOutput(NULL, TRUE);
    SetOutput(OutFile, FALSE);
  end;

  nDeal.KeyNum = 0;
  nDeal.rec.ID = DealID;
  if( not GetEq(nDeal) )
    dv_error( "Не найдена сделка ", DealID );
    return 1;
  else
    if( nDeal.rec.DocKind == DL_DVFXDEAL )
       FD_ND = DVFirstDocFXDeal(nDeal.rec.DocKind, DealID);
    else
       FD_ND = DVFirstDocNDeal(nDeal.rec.DocKind, DealID);
    end;
  end;

  FillConfData();

  if (first == false)
    println("\f");
  end;
  PrintHeader();
  PrintDeal();
  PrintPayments();
  PrintFooter();
  if (oldOutFile != "")
    SetOutput(oldOutFile, TRUE);
  else
    SetOutput(OutFile);
  end;

  if (not isOprMultiExec)
    SetOutput(CreateFileName(), FALSE);
    open(out, OutFile);
    ViewFile(out);
  end;

END;

