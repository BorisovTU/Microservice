/*
$Name:        dvAlgCalcFrVal.mac
$Module:      Производные инструменты
$Description: Общие функции для алгоритмов расчета справедливой стоимости
*/
IMPORT "dvserv.mac", "DvRepFun2.mac", "cb_sql.mac";

CLASS ALG_Dn_i( Period_:integer, FiKind_:integer, DvKind_:integer, Basis_:integer, RateID_:integer )
  var Period:integer = Period_,
      FiKind:integer = FiKind_,
      DvKind:integer = DvKind_,
      Basis:integer  = Basis_,
      RateID:integer = RateID_;
END;

PRIVATE MACRO ALG_Rate( X:ALG_Dn_i, pDate:date ):double

  var RetVal:double = 0.0;

  if( not ПолучитьКурсЗаданногоTипа(@RetVal, X.RateID, FI_PRICE_POINT, 0, pDate, false) )
     RetVal = 0.0;
  end;

  return RetVal;
END;

PRIVATE MACRO ALG_GetRateAndBasis( Dn:ALG_Dn_i, pDate:date, v_R:@double, v_b:@integer )
  v_R = ALG_Rate(Dn, pDate);
  v_b = Dn.Basis;
END;

/* Расчет ставки */
MACRO ALG_RF(pF:integer, pDate:date, pDe:date, pFI:TRecHandler, pDVKind:integer, pFIKind:integer, pBasis:@integer, pStat:@integer ):double

  var RetVal:double = 0.0;
  var v_Dn = TArray();
  var Query, RS;
  var v_Is:integer = pDe - pDate;

  if( pStat != NULL )
     pStat = 0;
  end;

  Query = RSDCommand( " SELECT RSB_Derivatives.DV_DaysInPeriodByBasis(DR.t_Basis, ?, DR.t_Period, DR.t_PeriodKind) Period, " +
                      "        DR.t_FiKind, DR.t_DvKind, DR.t_Number, DR.t_Basis, DR.t_RateID " +
                      "   FROM ddvprrate_dbt DR                     " +
                      "  WHERE (DR.t_DvKind = ? or DR.t_DvKind = ?) " +
                      "    AND (? = ? or DR.t_FiKind = ? or DR.t_FiKind = ?) " +
                      "    AND DR.t_FIID = ? " +
                      " ORDER BY Period, DR.t_FiKind desc, DR.t_DvKind desc, DR.t_Number " );

  Query.addParam( "", RSDBP_IN, pDate );
  Query.addParam( "", RSDBP_IN, pDVKind );
  Query.addParam( "", RSDBP_IN, ALG_DV_KIND_PRRATE_UNDEF );
  Query.addParam( "", RSDBP_IN, pFIKind );
  Query.addParam( "", RSDBP_IN, ALG_DV_FIKIND_PRRATE_UNDEF );
  Query.addParam( "", RSDBP_IN, pFIKind );
  Query.addParam( "", RSDBP_IN, ALG_DV_FIKIND_PRRATE_UNDEF );
  Query.addParam( "", RSDBP_IN, pF );
  RS = TRsbDataSet( Query );
  while( RS.MoveNext() )
     if( (v_Dn.Size == 0) or (v_Dn[v_Dn.Size-1].Period != RS.Period) )
        v_Dn[v_Dn.Size] = ALG_Dn_i( RS.Period, RS.FiKind, RS.DvKind, RS.Basis, RS.RateID );
     end;
  end;

  if( v_Dn.Size > 0 )
     if( v_Is <= v_Dn[0].Period )
        ALG_GetRateAndBasis(v_Dn[0], pDate, @RetVal, @pBasis);
     elif( v_Is >= v_Dn[v_Dn.Size-1].Period )
        ALG_GetRateAndBasis(v_Dn[v_Dn.Size-1], pDate, @RetVal, @pBasis);
     else
        var i:integer = 0;
        while( i < (v_Dn.Size-1) )
           if( (v_Is >= v_Dn[i].Period) and (v_Is <= v_Dn[i+1].Period) )
              if( v_Is == v_Dn[i].Period )
                 ALG_GetRateAndBasis(v_Dn[i], pDate, @RetVal, @pBasis);
              elif( v_Is == v_Dn[i+1].Period )
                 ALG_GetRateAndBasis(v_Dn[i+1], pDate, @RetVal, @pBasis);
              else
                 var v_R1:double = 0.0, v_R2:double = 0.0, v_b2:integer = 0;
                 ALG_GetRateAndBasis(v_Dn[i], pDate, @v_R1, @pBasis); /*pBasis возьмём от первой ставки*/
                 ALG_GetRateAndBasis(v_Dn[i+1], pDate, @v_R2, @v_b2);
                 RetVal = v_R1 + (v_R2 - v_R1) * (v_Is - v_Dn[i].Period) / (v_Dn[i+1].Period - v_Dn[i].Period);
              end;
              break;
           end;
           i = i + 1;
        end;
     end;
  else
     if( pStat != NULL )
        pStat = 1;
     end;
     MsgBox("Не найдена процентная ставка для валюты <" + GetCCY(pF) + "> на дату <" + string(pDate) + ">");
  end;

  return RetVal;
END;

/* Расчет коэффициента */
MACRO ALG_DF(pF:integer, pDate:date, pFI:TRecHandler, pDVKind:integer, pFIKind:integer, De:date, pStat_:@integer):double
  var RetVal:double = 1.0;
  var v_R:double = 0.0;
  var v_Basis:integer = 4;
  var v_De:date = pFI.rec.ExecDate;
  var pStat:integer = 0;

  if( (De != null) and (De != date(0,0,0)) )
     v_De = De;
  end;

  if( pStat_ != NULL )
     pStat_ = 0;
  end;

  if( pDate <= v_De )
     v_R = ALG_RF(pF, pDate, v_De, pFI, pDVKind, pFIKind, @v_Basis, @pStat);

     if( pStat == 0 )
        var cmd = RsdCommand(RslDefCon, "begin\n ? := RSB_Derivatives.DV_Years(?, ?, ?);\n end; ");
        cmd.addParam("ret_val",  RSDBP_RETVAL, V_DOUBLE );
        cmd.addParam("start_dt", RSDBP_IN,     pDate    );
        cmd.addParam("end_dt",   RSDBP_IN,     v_De     );
        cmd.addParam("basis",    RSDBP_IN,     v_Basis  );
        cmd.execute();
       
        RetVal = (1.0 / (1.0 + (v_R / 100.0) * SQL_ConvTypeSum(cmd.Value(0))))
     else
        pStat_ = pStat;
     end;
  end;

  return RetVal;
END;

/* Расчет СС требования и обязательства по части сделки */
MACRO ALG_FairValue(pDate:date, pFI:TRecHandler, pFt:integer, pFo:integer, pNt:money, pNo:money, pSt:double, pSo:double,
                    pDVKind:integer, pFIKind:integer, poD:@double, poL:@double, poFV:@double, De:date)

  var v_Dft = 0,        /* Коэффициент для валюты требований */
      v_Dfo = 0;        /* Коэффициент для валюты обязательств */
  var stat:integer = 0;

  v_Dft = ALG_DF(pFt, pDate, pFI, pDVKind, pFIKind, De, @stat);
  if( stat == 0 )
     v_Dfo = ALG_DF(pFo, pDate, pFI, pDVKind, pFIKind, De, @stat);
     if( stat == 0 )
        var pD = pNt * pSt * v_Dft;
        var pL = pNo * pSo * v_Dfo;
        if( poD != null )
           poD = pD;
        end;
        if( poL != null )
           poL = pL;
        end;
        poFV = pD - pL;
     end;
  end;

  return stat;
END;

MACRO ALG_GetCouponSumByPeriod( FIID, DateBeg, DateEnd )
  var cmd, rsd;
  var RetVal = 0.0;

  cmd = RSDCommand( " SELECT warnt.t_DrawingDate " +
                    "   FROM dfiwarnts_dbt warnt " +
                    "  WHERE warnt.t_FIID = ? " +
                    "    AND warnt.t_IsPartial = chr(0) " +
                    "    AND warnt.t_DrawingDate <= ? " +
                    "    AND warnt.t_DrawingDate >= ? " +
                    " ORDER BY warnt.t_DrawingDate ASC " );

  cmd.addParam( "", RSDBP_IN, FIID );
  cmd.addParam( "", RSDBP_IN, DateEnd );
  cmd.addParam( "", RSDBP_IN, DateBeg );
  cmd.execute();

  rsd = TRsbDataSet(cmd);
  while( rsd.MoveNext() )
    RetVal = RetVal + СуммаКупона(FIID, 1, SQL_ConvTypeDate(rsd.DrawingDate));
  end;

  return RetVal;
END;

MACRO ALG_GetSinceDate( DealID:integer, Днач2:date, Доконч2:@date ):bool
  var cmd, rsd;
  var RetVal:bool = false;

  Доконч2 = date(0,0,0);

  cmd = RSDCommand( " SELECT ratevalue.t_SinceDate " +
                    "   FROM ddvratevalue_dbt ratevalue " +
                    "  WHERE ratevalue.t_DealID = ? " +
                    "    AND ratevalue.t_SinceDate  > ? " +
                    "    AND ratevalue.t_SinceDate = ( SELECT min(rv.t_SinceDate) " +
                    "                                    FROM ddvratevalue_dbt rv " +
                    "                                   WHERE rv.t_DealID = ? " +
                    "                                     AND rv.t_SinceDate > ? ) " );

  cmd.addParam( "", RSDBP_IN, DealID );
  cmd.addParam( "", RSDBP_IN, Днач2 );
  cmd.addParam( "", RSDBP_IN, DealID );
  cmd.addParam( "", RSDBP_IN, Днач2 );
  cmd.execute();

  rsd = TRsbDataSet(cmd);
  if( rsd.MoveNext() )
     Доконч2 = SQL_ConvTypeDate(rsd.SinceDate) - 1;
     RetVal = true;
  end;

  return RetVal;
END;

MACRO ALG_GetRATE( DealID:integer, Днач2:date, Rk:@money ):bool
  var cmd, rsd;
  var RetVal:bool = false;

  Rk = 0.0;

  cmd = RSDCommand( " SELECT ratevalue.t_Rate " +
                    "   FROM ddvratevalue_dbt ratevalue " +
                    "  WHERE ratevalue.t_DealID = ? " +
                    "    AND ratevalue.t_SinceDate <= ? " +
                    "    AND ratevalue.t_SinceDate = ( SELECT max(rv.t_SinceDate) " +
                    "                                    FROM ddvratevalue_dbt rv " +
                    "                                   WHERE rv.t_DealID = ? " +
                    "                                     AND rv.t_SinceDate <= ? ) " );

  cmd.addParam( "", RSDBP_IN, DealID );
  cmd.addParam( "", RSDBP_IN, Днач2 );
  cmd.addParam( "", RSDBP_IN, DealID );
  cmd.addParam( "", RSDBP_IN, Днач2 );
  cmd.execute();

  rsd = TRsbDataSet(cmd);
  if( rsd.MoveNext() )
     Rk = SQL_ConvTypeSum(rsd.Rate);
     RetVal = true;
  end;

  return RetVal;
END;

MACRO ALG_GetIndexQuerySelect()
   return " SELECT fin.T_FIID ";
END;

MACRO ALG_GetIndexQueryFromWhere()
   return "   FROM dfininstr_dbt fin " +
          "  WHERE fin.t_fi_kind = ? " +
          "    and fin.t_Settlement_Code = ? " +
          "    and fin.t_FaceValueFI = ? " +
          "    and fin.t_ParentFI = ? ";
END;

MACRO ALG_GetIndexQueryTermInDays()
   return " rsb_secur.GetTermInDaysByTypeDuration(fin.t_Duration, fin.t_TypeDuration, 1/*cnst.BASIS_30360*/) ";
END;

PRIVATE MACRO ЗаданКурсПоложитОтрицатСВОПразницы(FIID:integer,PriceFIID:integer,pDate:Date,pCr:@double)
   pCr = 0.0;

   if( not ПолучитьКурсЗаданногоTипа(@pCr, SQL_ConvTypeInteger(FIID), PriceFIID, RATETYPE_PLUS_SWAP_DIFFERENCE, pDate, false) or (pCr == 0.0) )
      if( not ПолучитьКурсЗаданногоTипа(@pCr, SQL_ConvTypeInteger(FIID), PriceFIID, RATETYPE_MINUS_SWAP_DIFFERENCE, pDate, false) or (pCr == 0.0) )
         return false;
      else
         pCr = -pCr;/*Отрицательная своп-разница с минусом*/
      end;
   end;

   return true;
END;

/* pRateType - тип курса (если не задан, то значит будем искать <Положительная своп-разница> или <Отрицательная своп-разница>)*/
MACRO ALG_GetIndexRateByTermInDays_NotEq(pFI:TRecHandler,pDate:Date,pRateType:integer,pK:integer,pKind:integer,pCr:@double,pSK:@integer)
   var v_ExistIndex = false;
   var RsdDVQuery, DVDealData, DVQuery;

   DVQuery = ALG_GetIndexQuerySelect() + ", " + ALG_GetIndexQueryTermInDays() + " SK " +
             ALG_GetIndexQueryFromWhere() +
             " and "+ALG_GetIndexQueryTermInDays();

   if( pKind == 1 )
      DVQuery = DVQuery + " < ? "; 
   else
      DVQuery = DVQuery + " > ? "; 
   end;

   RsdDVQuery = DL_RSDCommand(DVQuery);
   
   RsdDVQuery.addParam(FIKIND_INDEX);/*индекс*/
   RsdDVQuery.addParam(FI_INDEX_PAIR);/*на валютную пару*/
   RsdDVQuery.addParam(pFI.rec.FIID);/*базовый*/
   RsdDVQuery.addParam(pFI.rec.PriceFIID);/*котируемый*/
   RsdDVQuery.addParam(pK);
   
   DVDealData = RsdDVQuery.execute();
   
   while( DVDealData.MoveNext() )
      if( pRateType != null )
         if( ПолучитьКурсЗаданногоTипа(@pCr, SQL_ConvTypeInteger(DVDealData.FIID), pFI.rec.PriceFIID, pRateType, pDate, false) and (pCr != 0.0) )
            pSK = SQL_ConvTypeInteger(DVDealData.SK);
            v_ExistIndex = true;
            break;
         end;
      else
         /*Ищем по индексу курс вида <Положительная своп-разница> или <Отрицательная своп-разница> со значением на дату pDate*/
         if( ЗаданКурсПоложитОтрицатСВОПразницы(SQL_ConvTypeInteger(DVDealData.FIID), pFI.rec.PriceFIID, pDate, @pCr) )
            pSK = SQL_ConvTypeInteger(DVDealData.SK);
            v_ExistIndex = true;
            break;
         end;
      end;
   end;

   return v_ExistIndex;
END;

/* pRateType - тип курса (если не задан, то значит будем искать <Положительная своп-разница> или <Отрицательная своп-разница>)*/
MACRO ALG_GetIndexRateByTermInDays_Eq(pFI:TRecHandler,pDate:Date,pRateType:integer,pK:integer,pCr:@double)
   var v_ExistIndex = false;

   pCr = 0.0;

   var RsdDVQuery, DVDealData,
       DVQuery = ALG_GetIndexQuerySelect() +
                 ALG_GetIndexQueryFromWhere() + 
                 " and " + ALG_GetIndexQueryTermInDays() + " = ? ";
                                                                                       
   RsdDVQuery = DL_RSDCommand(DVQuery);

   RsdDVQuery.addParam(FIKIND_INDEX);/*индекс*/
   RsdDVQuery.addParam(FI_INDEX_PAIR);/*на валютную пару*/
   RsdDVQuery.addParam(pFI.rec.FIID);/*базовый*/
   RsdDVQuery.addParam(pFI.rec.PriceFIID);/*котируемый*/
   RsdDVQuery.addParam(pK);

   DVDealData = RsdDVQuery.execute();
   while( DVDealData.MoveNext() )
      if( pRateType != null )
         if( ПолучитьКурсЗаданногоTипа(@pCr, SQL_ConvTypeInteger(DVDealData.FIID), pFI.rec.PriceFIID, pRateType, pDate, false) and (pCr != 0.0) )
            v_ExistIndex = true;
            break;
         end;
      else
         /*Ищем по индексу курс вида <Положительная своп-разница> или <Отрицательная своп-разница> со значением на дату pDate*/
         if( ЗаданКурсПоложитОтрицатСВОПразницы(SQL_ConvTypeInteger(DVDealData.FIID), pFI.rec.PriceFIID, pDate, @pCr) )
            v_ExistIndex = true;
            break;
         end;
      end;
   end;

   return v_ExistIndex;
END;
