/*                           
$Name:        dvopamortrhdprep.mac
$Module:      Производные инструменты
$Description: Печать протокола операции амортизации РХДП
*/
import BankInter, XmlRpcInter, "dllog_xml.mac", "DlReport_h.mac", "dldlngfun.mac";

private var N1RepHeaderStr = " Дата расчета: ##########\n";

PRIVATE VAR DvOper = TrecHandler( "dvoper" );

PRIVATE MACRO FindDVOPER( OperID:INTEGER ):BOOL
  var   Data = TRsbDataSet( "SELECT * FROM ddvoper_dbt WHERE t_ID = " + string(OperID) );

  if( Data.MoveNext() )
     Data.GetRecord().CopyTo( DvOper.rec );
     return true;
  end;
  return false;
END;

//класс с данными для строки протокола
class AmortRHPD_DataLogParm(_IDObj, _ObjDocKind,_TypeObj, _DateFirst, _IDInstr, _DataInstr, _IDRel, _CodePortf, _CodeSubPortf, _DateEndPlan, _DateEndFact, _SumRHDPEndR, _SumRHDPOp, _SumSa, _TxtLine, _ErrorLine)
  var IDObj = _IDObj,
  ObjDocKind = _ObjDocKind,
  TypeObj = _TypeObj,
  DateFirst = _DateFirst,
  IDInstr = _IDInstr,
  DataInstr = _DataInstr,
  IDRel = _IDRel,
  CodePortf = _CodePortf,
  CodeSubPortf = _CodeSubPortf,
  DateEndPlan = _DateEndPlan,
  DateEndFact = _DateEndFact,
  SumRHDPEndR = _SumRHDPEndR,
  SumRHDPOp = _SumRHDPOp,
  SumSa = _SumSa,
  TxtLine = _TxtLine,
  ErrorLine = _ErrorLine;
end;

/*лог данных*/
PRIVATE CLASS (DL_LOG_FROM_XML) AMORTRHDP_LOG_DATA_FROM_XML(pDocKind:integer, pID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()
     var ProtocolData:AmortRHPD_DataLogParm;

     if( m_ReportVersion == 1 )
        if(DL_ReadTagAttribut(m_child_ReportNumber, "IDOBJ", V_INTEGER, ProtocolData.IDObj) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "TYPEOBJ", V_STRING, ProtocolData.TypeObj) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "DATEFIRST", V_DATE, ProtocolData.DateFirst) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "IDINSTR", V_INTEGER, ProtocolData.IDInstr) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "DATAINSTR", V_DATE, ProtocolData.DataInstr) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "IDREL", V_INTEGER, ProtocolData.IDRel) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CODEPORTF", V_STRING, ProtocolData.CodePortf) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CODESUBPORTF", V_STRING, ProtocolData.CodeSubPortf) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "DATEENDPLAN", V_DATE, ProtocolData.DateEndPlan) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "DATEENDFACT", V_DATE, ProtocolData.DateEndFact) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "SUMRHDPENDR", V_MONEY, ProtocolData.SumRHDPEndR) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "SUMRHDPOP", V_MONEY, ProtocolData.SumRHDPOp) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "SUMSA", V_MONEY, ProtocolData.SumSa) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "TXTLINE", V_STRING, ProtocolData.TxtLine) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "ERRORLINE", V_STRING, ProtocolData.ErrorLine) == false)
        end;
        
        return ProtocolData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pID, pType, pOnlyLastReport);

END;

PRIVATE MACRO GetLogDataXML( DocKind:integer, ID:integer, Type:integer, OnlyLastReport:bool )
  var xml_obj = null;

  if( Type == LOG_TYPE_DATA )
     xml_obj = AMORTRHDP_LOG_DATA_FROM_XML(DocKind, ID, Type, OnlyLastReport);
  end;

  return xml_obj;
END;

PRIVATE MACRO GetTypeObj(TypeObj)
  var NA = TBFile( "namealg.dbt",  "R", 0 );
  NA.Clear();
  NA.rec.ITypeAlg  = 7053;
  NA.rec.INumberAlg = TypeObj;
  if(NA.GetEQ())
    return NA.rec.szNameAlg;
  end;
  return "";
END;

PRIVATE CLASS (DL_CReportTemplate) AmortRHDP_Protocol(Mode)
  PRIVATE VAR m_PrintMode;
  PRIVATE VAR SheetName = "Протокол";

  PRIVATE MACRO PrintMode():INTEGER
     return m_PrintMode;
  END;
  
  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
    CreateTotalBook();
  END;

  PRIVATE MACRO EndReport()
    SaveTotalBook();
  END;
  
  private macro PrintProtocolLines (ReportLineData_XML:variant)
    var ProtocolData:AmortRHPD_DataLogParm;
    var IsFirst = true;
    while ( ReportLineData_XML.Next )
        ProtocolData = ReportLineData_XML.GetReportLineData();
        if ( IsFirst )
          CopyAllSheetInTotalBook(SheetName, false, "N1_ProtocolTableHead", 1, SheetName);

          RegisterTable( "N1_ProtocolTableData", null,
                          "N1_A1",
                          "N1_A2",
                          "N1_A3",
                          "N1_A4",
                          "N1_A5",
                          "N1_A6",
                          "N1_A7",
                          "N1_A8",
                          "N1_A9",
                          "N1_A10",
                          "N1_A11",
                          "N1_A12",
                          "N1_A13",
                          "N1_A14",
                          "N1_A15"
                          );            

          IsFirst = false;
        end;
        
        PrintTableLine(   "N1_A1",  ProtocolData.IDObj,               null,
                          "N1_A2",  GetTypeObj(ProtocolData.TypeObj), null,
                          "N1_A3",  ProtocolData.DateFirst,           null,
                          "N1_A4",  ProtocolData.IDInstr,             null,
                          "N1_A5",  ProtocolData.DataInstr,           null,
                          "N1_A6",  ProtocolData.IDRel,               null,
                          "N1_A7",  ProtocolData.CodePortf,           null,
                          "N1_A8",  ProtocolData.CodeSubPortf,        null,
                          "N1_A9",  ProtocolData.DateEndPlan,         null,
                          "N1_A10", ProtocolData.DateEndFact,         null,
                          "N1_A11", ProtocolData.SumRHDPEndR,         null,
                          "N1_A12", ProtocolData.SumRHDPOp,           null,
                          "N1_A13", ProtocolData.SumSa,               null,
                          "N1_A14", ProtocolData.TxtLine,             null,
                          "N1_A15", ProtocolData.ErrorLine,           null
                        );
    end;
    
    if ( not IsFirst )
       EndTable();
       CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);
    end;

    return (not IsFirst);
  end;

  PRIVATE MACRO Create()
    var ProtocolLineData_XML:variant;

    SetActiveSheet(SheetName);
    PrintFormatString(null,
                     "N1_DateOp",     "Дата расчета " + DvOper.rec.Date
                     );
    CopyAllSheetInTotalBook(SheetName, false, "N1_ProtocolHeader", 0, SheetName);

    ProtocolLineData_XML = GetLogDataXML(DvOper.rec.DocKind, DvOper.rec.ID, LOG_TYPE_DATA, false);
    ProtocolLineData_XML.SetReportNumber(0);
    if(not PrintProtocolLines(ProtocolLineData_XML))
      PrintFormatString(null,
                     "N1_NoData",     "Нет данных для операции"
                     );
      CopyAllSheetInTotalBook(SheetName, false, "N1_NoData", 0, SheetName);
    end;
  END;

  m_PrintMode = Mode;
    
  initDL_CReportTemplate( NULL, "Protocol_AmortRHDP.xlsx", true, null, null, false);
END;

MACRO AmortRHDPPrintProtocol(OperID)
  if( not FindDVOPER( OperID ) )
     MsgBox( "Не найдена сервисная операция амортизации РХДП (ID="+string(OperID)+")" );
     return false;
  end;
    
  AmortRHDP_Protocol(DL_OUTREPORT_EXCEL).Run();
  return true;

END;
