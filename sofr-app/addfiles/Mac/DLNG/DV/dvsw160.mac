/*
$Name:        dvsw160.mac
$Module:      Производные инструменты
$Description: Валютный СВОП. Шаг "Исполнение обязательств  2ч"
*/
import "dv_car.mac";
import dv_carchng;
import "usr_connect_attr.mac"; //Simanov

//Simanov. i-support 507260
//По валютным платежам с филиалами, должен документ формироваться с кодом 06.
private macro SetCodeDocument (dealid, dockind)
  var query = "select t_paymentid from dpmpaym_dbt pmpaym "
            + "where     t_dockind = :dockind "
            + "      and t_documentid = :dealid "
            + "      and exists (select 1 from dpmprop_dbt where t_paymentid = pmpaym.t_paymentid and t_debetcredit = 1 and t_corschem > -1) " //Есть исходящая корсхема
            + "      and t_receiverbankid in (select t_partyid from ddp_dep_dbt) "
            + "      and t_receiverbankid <> 1 ";
  var sql = execSqlSelect(query, makeArray(sqlParam("dockind", dockind), sqlParam("dealid", dealid)));

  while (sql.moveNext( ))
    Connect_attr_to_paym (sql.value("t_paymentid"), ATTR_GROUP_DOCUMENT_BIS, "06");
  end;

End;


macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  VAR FD;
  VAR ДатаИсполненияШага;
  var ДатаПланируемаяШага = DV_GetPlanDateStep(ID_Operation, ID_Step);

  SetBuff( ndeal, FDoc );
  FD = DVFirstDocNDeal( DocKind, ndeal, 2 );
  if (FD.ОтказОтИсполнения())
     return 0;
  end;
  var ПлатежОб = RsbPayment( FD.ПлатежОб.rec.PaymentID );

  SetCodeDocument(ndeal.id, ndeal.dockind);

  ДатаИсполненияШага = ДатаПланируемаяШага;
  if( ДатаИсполненияШага == date(0,0,0) )
     ДатаИсполненияШага = FD.ДатаОбязательства();
  end;

  if (FD.Netting2())//TEG 08.02.19
      if( not InsertOprStatus(FD.DV_OPERSTATUS_LIABILITY_2(), DV_OPERSTATUS_DL_COMPLETE) )
          MsgBox("Ошибка при установке статуса <Требования> по операции.");
          return 1;
      end;
      if (ПлатежОб.PaymStatus != PM_FINISHED) 
          if ( (FD.ВалютныйРынок()) and (not FD.IsClient()) )
                if( ПИ_УстановитьСтатусПлатежа(ПлатежОб, PM_CLOSED_W_M_MOVEMENT) )
                    MsgBox("Ошибка при установке платежу статуса \"Исполнен\"");
                    return 1;
                end; 
          end;
      end;
      return 0;
  end;

  

  if (ПлатежОб.PaymStatus == PM_OVERDUE)

   var stat = true;
   var findAccount = "";

      stat = FD.FindNumberAccount( "Обяз. с н.с.",  FindAccount );
      ПлатежОб.FuturePayerAccount = FindAccount;

  end;

  if (ПлатежОб.PaymStatus != PM_FINISHED) 
      if ( (FD.ВалютныйРынок()) and (not FD.IsClient()) )
            if( ПИ_УстановитьСтатусПлатежа(ПлатежОб, PM_CLOSED_W_M_MOVEMENT) )
                MsgBox("Ошибка при установке платежу статуса \"Исполнен\"");
                return 1;
            end; 
      else

          if( ИсполнитьОбязательство(FD, ДатаИсполненияШага, doc) != 0 )
              return 1;
          end;
      end;
   end;

  if ( (not FD.IsClient()) and (FD.ВидБазовогоАктива() == FIKIND_METAL) and FD.IsSale() )
     if (ОтнесениеФинРезультатаДМ( FD, doc, ДатаИсполненияШага ) != 0 )
        return 1;
     end;
  end;

  if( not InsertOprStatus(FD.DV_OPERSTATUS_LIABILITY_2(), DV_OPERSTATUS_DL_COMPLETE) )
     MsgBox("Ошибка при установке статуса <Обязательства 2ч> по операции.");
     return 1;
  end;
  
  if(((FD.IsSWAPNDeal()) or (FD.IsPctSWAP())) and (РАБОТА_С_РЕПОЗИТАРИЕМ()) and (ДатаПланируемаяШага > {curdate}))
    if(ИзменениеСостоянияОбязательств_ДосрочноеИсполнениеCПлатежами(FD, ДатаИсполненияШага, DocKind) )
       return 1;
    end;
  end;

  return 0;
end;
