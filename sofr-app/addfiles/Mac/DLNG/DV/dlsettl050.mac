/*
$Name:        dlsettl050.mac
$Module:      Ядро Securities
$Description: Операция "Расчеты с биржей", Шаг Перевод средств с торгового счёта на клиринговый
*/

import InsCarryDoc, "dv_categ.mac", "dv_car.mac", "dv_carop.mac";

PRIVATE VAR Dl_ValueData = TRecHandler( "dl_value.dbt");

MACRO ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step )

  record comm( dl_comm );
  var    FD; 

  SetBuff( comm, FirstDoc );
  FD = DVFirstDocDLCOMM( comm );

  var OurAcc = TRecHandler("account"),
      ClirAcc = TRecHandler("account"),
      PaymentObj:RsbPayment;

  if( (FD.ВидБиржевогоРынка() == DV_MARKETKIND_ALL) AND (Dl_ValueData.rec.SumFIID == NATCUR) AND (FD.comm.rec.OperSubKind == ALG_SP_MONEY_SOURCE_OWN) ) // Golovkin с счета 30424 ЕП 
     if( not FD.IsExistAccount("+Обеспечение", null, true, null, ClirAcc, null, Dl_ValueData.rec.Date, Dl_ValueData.rec.SumFIID) )
        MsgBox("Ошибка: не открыт счет категории \"+Обеспечение\" в валюте FIID = " + String(Dl_ValueData.rec.SumFIID));
        return 1;
     end;
  else
    if( not FD.IsExistAccount("Клиринговый счет", null, true, DV_GetSettlFiRoleByOperSubKind(comm.OperSubKind,false), ClirAcc, null, Dl_ValueData.rec.Date, Dl_ValueData.rec.SumFIID) )
       MsgBox("Ошибка: не открыт счет категории \"Клиринговый счет\" в валюте FIID = " + String(Dl_ValueData.rec.SumFIID));
       return 1;
    end;
  end;

  if( not FD.IsExistAccount("Торговый счет", null, true, null, OurAcc, null, Dl_ValueData.rec.Date, Dl_ValueData.rec.SumFIID) )
     MsgBox("Ошибка: не открыт счет категории \"Торговый счет\" в валюте FIID = " + String(Dl_ValueData.rec.SumFIID));
     return 1;
  end;

  if( DVND_CreatePayment( FD, PM_PURP_ORDER, FD.Kind, FD.ID,
                          Dl_ValueData.rec.Date,
                          Comm.PartyID, {OurBank},
                          Comm.PartyID, {OurBank},
                          Dl_ValueData.rec.Sum, Dl_ValueData.rec.SumFIID,
                          "Перевод средств с торгового счёта" + IIF(GetCodeParty("АО СПВБ",PTCK_CONTR) == FD.comm.rec.PartyID, " " + GetCodeParty(FD.comm.rec.PartyID,PTCK_CONTR), "") + 
                          " на клиринговый счёт" + IIF(GetCodeParty("АО СПВБ",PTCK_CONTR) == FD.comm.rec.PartyID, " " + GetCodeParty(FD.comm.rec.PartyID,PTCK_CONTR), ""),
                          ClirAcc, OurAcc,
                          comm.Division, NULL, NULL,
                          @PaymentObj
                        ) != 0 )
     return 1;
  end;

  if( not ПИ_ПроводкаПоПлатежу(FD, PaymentObj) )
     return 1;
  end;

  if( ПИ_ИсполнитьПлатеж(PaymentObj) )
     return 1;
  end;

  return 0;
end;
