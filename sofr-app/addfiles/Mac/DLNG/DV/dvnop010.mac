/*
$Name:        dvnop010.mac
$Module:      Производные инструменты
$Description: Внебиржевая операция. Шаг: Инициализация
*/
import InsCarryDoc, "dv_categ.mac", "dv_voop.mac";
import "dv_car.mac", "dv_period.mac";

macro executeParamStep( Kind_Operation, DocKind, FDoc )

  record ndeal(dvndeal);
  var    FD;
  var ExecDate2 = date(0,0,0);
  VAR FD_2;

  SetBuff( ndeal, FDoc );
  FD = DVFirstDocNDeal(DocKind, ndeal);

  if( DV_SetPaymentGround(FD.pm_BA(), -1, FD) == false )
     return 1;
  end;

  if( DV_SetPaymentGround(FD.pm_CA(), -1, FD) == false )
     return 1;
  end;

  DL_SetBalanceDateEQMinTODate(DV_NDEAL_OPRDATE_EXEC, DV_NDEAL_OPRDATE_REQ, DV_NDEAL_OPRDATE_OBL);
  DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_ES, FD.ДатаСделки() );
  DL_SetBalanceDateEQMinTODate(DV_NDEAL_OPRDATE_ES, DV_NDEAL_OPRDATE_REQ, DV_NDEAL_OPRDATE_OBL, true);

  DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_DEALDATE, FD.ДатаСделки() );
  DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_BEGIN,    FD.ДатаНачала() );

  if( FD.IsPctSWAP() == false )
     DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_REQ,  FD.ДатаТребования() );
     DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_OBL,  FD.ДатаОбязательства() );
     DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_EXEC, FD.ДатаИсполнения(true) );
  else
     FD_2 = DVFirstDocNDeal(DocKind, ndeal, 2);
     DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_EXEC,   FD.ДатаНачала() );
     ExecDate2 = readNoteForObject(OBJTYPE_OUTOPER_DV, UniID( FD.deal, OBJTYPE_OUTOPER_DV), 110);
     if ((ValType(ExecDate2) == V_DATE) AND (ExecDate2 != date(0,0,0)))
       DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_EXEC_2, ExecDate2 );

     else
       ExecDate2 = date(0,0,0);
       DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_EXEC_2, FD.ДатаИсполнения() );
     end;

     if( FD.БазовыеВалютыРавны() == false )
        if( not InsertOprStatus(FD.DV_OPERSTATUS_EXEC(), DV_OPERSTATUS_EXEC_EXEC) )
           MsgBox("Ошибка при установке статуса <Исполнение> по операции.");
           return 1;
        end;
     end;

     if( not InsertOprStatus(FD.DV_OPERSTATUS_EXEC_2(), DV_OPERSTATUS_PAYM_EX) )
        MsgBox("Ошибка при установке статуса <Исполнение 2ч> по операции.");
        return 1;
     end;
  end;

  if( FD.IsSWAP() )
     FD_2 = DVFirstDocNDeal(DocKind, ndeal, 2);

     DL_SetBalanceDateEQMinTODate(DV_NDEAL_OPRDATE_EXEC_2, DV_NDEAL_OPRDATE_REQ_2, DV_NDEAL_OPRDATE_OBL_2);
     DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_ES_2, FD.ДатаСделки() );
     DL_SetBalanceDateEQMinTODate(DV_NDEAL_OPRDATE_ES_2, DV_NDEAL_OPRDATE_REQ_2, DV_NDEAL_OPRDATE_OBL_2, true);

     DL_CalendDefineOprDate(FD_2, DV_NDEAL_OPRDATE_EXEC_2, FD_2.ДатаИсполнения() );
     DL_CalendDefineOprDate(FD_2, DV_NDEAL_OPRDATE_REQ_2,  FD_2.ДатаТребования() );
     DL_CalendDefineOprDate(FD_2, DV_NDEAL_OPRDATE_OBL_2,  FD_2.ДатаОбязательства() );
     DL_CalendDefineOprDate(FD_2, DV_NDEAL_OPRDATE_CLOSE,  MAX( FD_2.ДатаОбязательства(), FD_2.ДатаТребования() ) );

     if( DV_SetPaymentGround(FD_2.pm_BA(), -1, FD_2) == false )
        return 1;
     end;

     if( DV_SetPaymentGround(FD_2.pm_CA(), -1, FD_2) == false )
        return 1;
     end;
  end;

  /*PNV*/
  if ( АктуализироватьПлатежи(FD, FD.ДатаСделки()) != 0 )
     return 1;
  end;

  if(( FD.IsSWAP() and ((FD.ВидБазовогоАктива() != FIKIND_METAL) or FD.Netting2()) ) OR (FD.IsPctSWAP() and (ExecDate2 != date(0,0,0))))
      if ( АктуализироватьПлатежи(FD_2, FD.ДатаИсполнения()) != 0 )
           return 1;
      end;
  end;
  
  var ПлатежТреб = RsbPayment( FD.ПлатежТреб.rec.PaymentID );
  var ПлатежОб   = RsbPayment( FD.ПлатежОб.rec.PaymentID );
  /* устанавливаем признак участия в неттинге */
  if( (FD.IsClient() == false) and (FD.deal.rec.Netting == SET_CHAR))
     if( FD.ПлатежТреб.rec.PaymentID > 0 )
        ПлатежТреб.Netting = SET_CHAR;
     end;

     if( FD.ПлатежОб.rec.PaymentID > 0 )
        ПлатежОб.Netting = SET_CHAR;
     end;
  end;
  /*PNV*/

  if (FD.IsOption())
    DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_EXPIRY, FD.ДатаИстеченияСрокаДействия() );
   //mda 24.02.19 Добавлена дата экспирации , тут мы её обработаем и инициализируем!
    if( not InsertOprStatus(19950, 3) )
        MsgBox("Ошибка при установке статуса <Экспирация> по операции.");
        return 1;
    end;
  end;

  if( not InsertOprStatus( FD.DV_OPERSTATUS_DOC(), DV_OPERSTATUS_DOC_OPEN) )
     MsgBox("Ошибка при установке статуса <Документооборот> по операции.");
     return 1;
  end;

  return 0;
end;
