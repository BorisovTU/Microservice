/*
$Name:        dv_carsf.mac
$Module:      Производные инструменты
$Description: Дополнительные функции для выполнения сводных проводок
*/
IMPORT "dv_categ.mac", "dlconst.mac";

PRIVATE VAR TableAccountSumCarry = TArray();
PRIVATE MACRO InitTableAccountSumCarry()
  var val;
  while( GetParm(TableAccountSumCarry.Size, val) )
     TableAccountSumCarry[TableAccountSumCarry.Size] = val;
  end;
END;

/*Настройка в регистре*/
CONST DV_MAKE_CARRY_SUMMARY      = "ПРОИЗВОДНЫЕ ИНСТРУМЕНТЫ\\СВОДНЫЕ ПРОВОДКИ НА ВАЛ. РЫНКЕ";
CONST DV_MAKE_CARRY_SUMMARY_CLNT = "ПРОИЗВОДНЫЕ ИНСТРУМЕНТЫ\\СВОД. ПРОВОДКИ НА ВАЛ.РЫНКЕ КЛ.";

/*Проводки, которые могут быть сводными. 
  При использовании парных счетов вместо указанной категории счета может быть
  использована парная ей, т.е. например строка
  "+Форвард, расчеты" ,   "-Биржа"
  при использовании парных счетов, подразумевает 4 разных варианта проводок
  "+Форвард, расчеты" ,   "-Биржа"
  "+Форвард, расчеты" ,   "+Биржа"
  "-Форвард, расчеты" ,   "-Биржа"
  "-Форвард, расчеты" ,   "+Биржа"*/

/* ""-основание у тех пар счетов, у кот. может быть несколько оснований (для таких ищем основание по номеру)*/
InitTableAccountSumCarry(
  /*КатегорияСчетаДебета,  КатегорияСчетаКредита,  ОснованиеСводнойПроводки, НеИспользоватьПарныйСчет */

  ПИ_КатегорияМнФорвардРПИ(),  "-Расчеты",                  "", false,
  "+Расчеты",                  ПИ_КатегорияПлФорвардРПИ(),  "", false,
  "-Расчеты",                  "-Биржа",                    "", false,
  "+Биржа",                    "+Расчеты",                  "", false,
  ПИ_КатегорияМнФорвардРПИ(),  "-Биржа",                    "", false,
  "+Биржа",                    ПИ_КатегорияПлФорвардРПИ(),  "", false,
  ПИ_КатегорияПлФорвардРПИ(),  ПИ_КатегорияМнФорвардРПИ(),  "Постановка сделок на баланс", false
);

PRIVATE CONST Ground_101 = 101,
              Ground_102 = 102,
              Ground_103 = 103,
              Ground_104 = 104,
              Ground_105 = 105,
              Ground_106 = 106,
              Ground_107 = 107,
              Ground_108 = 108,
              Ground_109 = 109,
              Ground_110 = 110;

PRIVATE MACRO ПолучитьТекстОснования( НомерОснования:integer, ClientContrID:integer )

  var RetVal:string = "";

  if( (НомерОснования == Ground_101) or (НомерОснования == Ground_105) )
     RetVal = "Получение вар. маржи";
  elif( (НомерОснования == Ground_102) or (НомерОснования == Ground_107) )
     RetVal = "Оплата вар. маржи";
  elif( (НомерОснования == Ground_103) or (НомерОснования == Ground_108) )
     RetVal = "Исполнение требований";
  elif( (НомерОснования == Ground_104) or (НомерОснования == Ground_106) )
     RetVal = "Исполнение обязательств";
  elif( НомерОснования == Ground_109 )
     RetVal = "Перенос суммы обязательств на транзитный счет";
  elif( НомерОснования == Ground_110 )
     RetVal = "Перенос суммы требований на транзитный счет";
  end;

  if( ClientContrID > 0 )
     var sfcontr = GetSfContr(ClientContrID);
     RetVal = RetVal + " по договору клиента \"" + sfcontr.rec.number + "\"";
  end;

  return RetVal;
END;

MACRO DV_ИспользоватьСводныеПроводки( IsClientAcc:BOOL ):BOOL
  var Enable, ErrCode;

  if( IsClientAcc )
     GetRegistryValue(DV_MAKE_CARRY_SUMMARY_CLNT, V_BOOL, Enable, ErrCode);
  else /*собственные сделки*/
     GetRegistryValue(DV_MAKE_CARRY_SUMMARY, V_BOOL, Enable, ErrCode);
  end;

  if( (ErrCode == 0) AND (Enable != 0) )
     return true;
  end;

  return false;
END;

PRIVATE VAR _ДелатьСводныеПроводкиНаРынкеСПФИ = NULL;

/*Режим формирования сводных проводок*/
PRIVATE MACRO ФормироватьСводныеПроводки( FD:DVFirst, СчетДебет:VARIANT, СчетКредит:VARIANT, DebMcAccDoc, CredMcAccDoc ):bool

  var RetVal:bool = false;
  var CatDebet = "", CatCred = "";
  /*сводные больше не формируем*/
  return RetVal;
END;

/*Найти в массиве TableAccountSumCarry*/
PRIVATE MACRO НомерВТаблицеСводныхПроводок( СчетДебет:VARIANT, СчетКредит:VARIANT, DebMcAccDoc, CredMcAccDoc ) 
  var i = 0, CatDebet = "", CatDebetPair = "", CatCred = "", CatCredPair = "";

  if( DL_ПолучитьКатегориюСчетаПроводки(СчетДебет,  @CatDebet, @CatDebetPair, DebMcAccDoc) AND
      DL_ПолучитьКатегориюСчетаПроводки(СчетКредит, @CatCred,  @CatCredPair,  CredMcAccDoc)
    )
     while( i < TableAccountSumCarry.Size )
        if( ((TableAccountSumCarry[i] == CatDebet) OR (TableAccountSumCarry[i] == CatDebetPair)) AND
            ((TableAccountSumCarry[i+1] == CatCred) OR (TableAccountSumCarry[i+1] == CatCredPair))
          )
           return i;
        end;
        i = i + 4;
     end;
  end;

  return -1;
END;

MACRO DV_ПолучитьНомерОснованияСводнПроводки( FD:DVFirst, СчетДебет:VARIANT, СчетКредит:VARIANT, Result_Carry, Ground_Carry:STRING )
  var CarryIndex = НомерВТаблицеСводныхПроводок(СчетДебет, СчетКредит);

  if( FD.IsClient() )

     // обработать когда появятся ОснованиеПереносОбВариационнойМаржи, ОснованиеПереносТрВариационнойМаржи

     if( Ground_Carry == ОснованиеПолучениеВариационнойМаржи )
        return Ground_101;
     elif( Ground_Carry == ОснованиеСписаниеВариационнойМаржи )
        return Ground_102;
     else
        var CatDebet = "", CatDebetPair = "", CatCred = "", CatCredPair = "";
        if( DL_ПолучитьКатегориюСчетаПроводки(СчетДебет, @CatDebet, @CatDebetPair) or
            DL_ПолучитьКатегориюСчетаПроводки(СчетКредит, @CatCred, @CatCredPair)
          )
           if( (CatDebet == "+Биржа") or (CatDebetPair == "+Биржа") )
              return Ground_103;
           elif( (CatCred == "-Биржа") or (CatCredPair == "-Биржа") )
              return Ground_104;
           end;
        end;
     end;

  elif( (CarryIndex >= 0) and (TableAccountSumCarry[CarryIndex+2] == "") )

     // обработать когда появятся ОснованиеПереносОбВариационнойМаржи, ОснованиеПереносТрВариационнойМаржи

     if( Ground_Carry == ОснованиеПолучениеВариационнойМаржи )
        return Ground_105;
     elif( Ground_Carry == ОснованиеСписаниеВариационнойМаржи )
        return Ground_107;
     elif( (CarryIndex == НомерВТаблицеСводныхПроводок(ПИ_КатегорияМнФорвардРПИ(), "-Биржа")) or
           (CarryIndex == НомерВТаблицеСводныхПроводок("-Расчеты", "-Биржа"))
         )
        return Ground_106;
     elif( (CarryIndex == НомерВТаблицеСводныхПроводок("+Биржа", ПИ_КатегорияПлФорвардРПИ())) or
           (CarryIndex == НомерВТаблицеСводныхПроводок("+Биржа", "+Расчеты"))
         )
        return Ground_108;
     elif( CarryIndex == НомерВТаблицеСводныхПроводок(ПИ_КатегорияМнФорвардРПИ(), "-Расчеты") )
        return Ground_109;
     elif( CarryIndex == НомерВТаблицеСводныхПроводок("+Расчеты", ПИ_КатегорияПлФорвардРПИ()) )
        return Ground_110;
     end;

  end;

  return 0;
END;

MACRO DV_ПолучитьПараметрыСводнойПроводки( СчетДебет:variant, СчетКредит:variant, НомерОснования:integer, retDtCateg, retCtCateg, retGround, ClientContrID:integer )
  var CarryIndex = НомерВТаблицеСводныхПроводок(СчетДебет, СчетКредит);
  var Ground = "";
  if( CarryIndex >= 0 )

     Ground = TableAccountSumCarry[CarryIndex+2];

     if( Ground == "" )
        Ground = ПолучитьТекстОснования(НомерОснования, ClientContrID);
     end;

     SetParm( 3, TableAccountSumCarry[CarryIndex] );
     SetParm( 4, TableAccountSumCarry[CarryIndex+1] );
     SetParm( 5, Ground );

     return true;
  elif( (НомерОснования != null) AND (НомерОснования > 0) )/*ветка для случая клиентских сделок (заполняется по оплате с\без НКД)*/
     Ground = ПолучитьТекстОснования(НомерОснования, ClientContrID);
     SetParm( 5, Ground );
     return true;
  end;

  return false;
END;

MACRO DV_НеИспользоватьПарныеСчета( СчетДебет:VARIANT, СчетКредит:VARIANT )
  var i = 0, CatDebet = "", CatCred = "";

  if( DL_ПолучитьКатегориюСчетаПроводки(СчетДебет, @CatDebet, NULL) AND
      DL_ПолучитьКатегориюСчетаПроводки(СчетКредит, @CatCred, NULL)
    )
     while( i < TableAccountSumCarry.Size )
        if( (TableAccountSumCarry[i] == CatDebet) AND (TableAccountSumCarry[i+1] == CatCred) )
           return TableAccountSumCarry[i+3];
        end;
        i = i + 4;
     end;
  end;

  return false;
END;

PRIVATE MACRO НеДолжнаБытьСводной( FD:DVFirst, СчетДебет:VARIANT, СчетКредит:VARIANT, Purpose:INTEGER )

  // исключения

  var RetVal:bool = false;

  if( (FD.IsClient() and (Purpose == PM_PURP_GARANT)) or // по клиентским учет ГО
      (Purpose == PM_PURP_COMMARKET) or // комиссии бирже
      (Purpose == PM_PURP_COMBROKER) or // комиссии брокеру
      (Purpose == PM_PURP_COMMBANK) // комиссии банку
    )
     RetVal = true;
  end;

  return RetVal;
END;

MACRO DV_НужнаСводнаяПроводка( FD:DVFirst, СчетДебет:VARIANT, СчетКредит:VARIANT, Chapter:INTEGER, DebMcAccDoc, CredMcAccDoc, Purpose:INTEGER ):bool

  if( (Chapter != 5) AND (Chapter != 21) AND (Chapter != 22) AND
      ФормироватьСводныеПроводки(FD, СчетДебет, СчетКредит, DebMcAccDoc, CredMcAccDoc) AND
      (not НеДолжнаБытьСводной(FD, СчетДебет, СчетКредит, Purpose)) AND
      (
        (FD.IsClient()) OR /*Если сделка клиентская, то Да*/
        (НомерВТаблицеСводныхПроводок(СчетДебет, СчетКредит, DebMcAccDoc, CredMcAccDoc) >= 0)
      )
    )
     return true;
  end;

  return false;
END;

MACRO DV_ДобавитьСводнуюПроводку( FD:DVFirst, AccountPayer:VARIANT, AccountReceiver:VARIANT, DateCarry:DATE, SummaDebet:MONEY, SummaCredit:MONEY, Result_Carry, Ground:STRING )

  record PayerAcc(account);
  record ReceiverAcc(account);
  file spdocoff(spdocoff) key 1;

  ClearRecord(spdocoff);

  copy(PayerAcc,    AccountPayer);
  copy(ReceiverAcc, AccountReceiver);

  spdocoff.MarketKind        = FD.Deal.rec.MarketKind;  /* Вид рынка*/

  /* Биржа, по которой затем проводим операцию расчетов на валютном рынке или рынке СПФИ */
  if( FD.КонтрагентБиржа() )
     spdocoff.PartyID        = FD.GetParametr(MC_TYPE_PARAMETR_CONTRACTOR, DateCarry);          
  else
     /*Подразумеваем, что если контрагент не биржа, значит контрагент - это центральный контрагент, а значит, в качестве биржи выступает ММВБ, поэтому ее и записываем в PartyID.*/
     spdocoff.PartyID        = DV_GetPartyIDMMVB();
     if( spdocoff.PartyID <= 0 )
        msgbox("Не найден субъект с кодом \"ММВБ\", соотвествующий ОАО Московская Биржа");
        return false;
     end;
  end;

  spdocoff.OfficeID          = FD.GetParametr(MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE, DateCarry); /* Сектор */
  spdocoff.Chapter           = PayerAcc.Chapter;                                                /* Глава */
  spdocoff.Date_Carry        = DateCarry;                                                       /* Дата проводки */
  spdocoff.Currency_Payer    = PayerAcc.Code_Currency;                                          /* Валюта счета плательщика */
  spdocoff.Account_Payer     = PayerAcc.Account;                                                /* Счет плательщика */
  spdocoff.Sum_Payer         = SummaDebet;                                                      /* Сумма по счету плательщика */
  spdocoff.Currency_Receiver = ReceiverAcc.Code_Currency;                                       /* Валюта счета получателя */
  spdocoff.Account_Receiver  = ReceiverAcc.Account;                                             /* Счет получателя */
  spdocoff.Sum_Receiver      = SummaCredit;                                                     /* Сумма по счету получателя */
  spdocoff.State             = SPDOCOFF_STATE_PREPARED;                                         /* Статус */
  //spdocoff.MarketSchemeID  =                                                                  /* Схема расчетов с биржей */
  spdocoff.GroundKind        = DV_ПолучитьНомерОснованияСводнПроводки(FD, AccountPayer, AccountReceiver, Result_Carry, Ground);

  /*на рынке СПФИ только собственные (нет клиентских проводок)*/
  if( FD.IsClient() and (not FD.РынокСПФИ()) )
     spdocoff.IsClientCarry  = SET_CHAR;
     spdocoff.ClientContrID  = FD.ClientContrID();
  else
     spdocoff.IsClientCarry  = UNSET_CHAR;
  end;

  return OprInsertSPDOCOFF(spdocoff);
END;
