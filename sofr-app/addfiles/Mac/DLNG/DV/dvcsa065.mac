/*
$Name:        dvcsa065.mac
$Module:      ФИССиКО
$Description: Сопровождение CSA: Бух. учет начисления
*/
import "dv_car.mac";

PRIVATE VAR SvOpDvOper = TRecHandler("dvoper.dbt");

MACRO ExecuteStep( doc, Fdoc, DocKind, ID_Operation, ID_Step )
    record csaBuf(dvcsa);
    SetBuff(csaBuf, FDoc);
    var FD = DVFirstCSA(DocKind, csaBuf.CSAID);
    var d, m, y; /*PNV i-support 509879*/

    var query = "SELECT NVL(SUM(CASE WHEN t_SumProcCalc > 0 THEN t_SumProcCalc ELSE 0 END), 0) plusProcSum, "
               +"       NVL(SUM(CASE WHEN t_SumProcCalc < 0 THEN t_SumProcCalc ELSE 0 END), 0) minusProcSum, "
               +"       t_Cur currency "
               +"  FROM DDVCSASH_DBT "
               +" WHERE     t_CSAID = " + FD.CSA.rec.CSAID
               +"       AND t_DMain <= " + GetSQLDate(SvOpDvOper.rec.EndDate)
               +"       AND (    TO_DATE('01.01.0001', 'DD.MM.YYYY') = " + GetSQLDate(SvOpDvOper.rec.BegDate)
               +"             OR t_DMain >= "+ GetSQLDate(SvOpDvOper.rec.BegDate) +" ) "
               +" GROUP BY t_Cur";
    
    var cmd = RsdCommand(query);
    cmd.execute;
    var DataSet = TRsbDataSet(cmd);
    
    if (DataSet)
        while (DataSet.moveNext)
            DateSplit (Date(SvOpDvOper.rec.BegDate), d, m, y); /*PNV i-support 509879*/

            if (DataSet.plusProcSum != 0) // Начисление доходов
                if(FD.CSA.rec.BaseCurrency == 8) /* по евро ставка отрицательная, требования по процентам специфические */
                  ПроводкаПоКатегориямУчета(FD,
                                            "+Треб. по%, отриц. ставка", "+%ПкрCSA",
                                            SvOpDvOper.rec.Date,
                                            1,
                                            DataSet.currency, DataSet.plusProcSum,
                                            doc, INPCARRY, "", "",
                                            "Начисление процентов по договору " + FD.CSA.rec.Code + " за " +  StrLwr(MonName(m)) + " " + y + "г.",/*PNV i-support 509879*/
                                            null, null ,null,
                                            DataSet.currency, NATCUR);
                else
                  ПроводкаПоКатегориямУчета(FD,
                                            "+%МС", "+%ПкрCSA",
                                            SvOpDvOper.rec.Date,
                                            1,
                                            DataSet.currency, DataSet.plusProcSum,
                                            doc, INPCARRY, "", "",
                                            "Начисление процентов по договору " + FD.CSA.rec.Code + " за " +  StrLwr(MonName(m)) + " " + y + "г.",/*PNV i-support 509879*/
                                            null, null ,null,
                                            DataSet.currency, NATCUR);
                end;
            end;
            
            if (DataSet.minusProcSum != 0) // Начисление расходов
                ПроводкаПоКатегориямУчета(FD,
                                          "-%ПкрCSA", "-%МС",
                                          SvOpDvOper.rec.Date,
                                          1,
                                          DataSet.currency, abs(DataSet.minusProcSum),
                                          doc, INPCARRY, "", "",
                                          "Начисление процентов по договору " + FD.CSA.rec.Code + " за " +  StrLwr(MonName(m)) + " " + y + "г.",/*PNV i-support 509879*/
                                          null, null ,null,
                                          NATCUR, DataSet.currency);
            end;
            
        end;
    end;
    
    DV_AccountCalcPrc(FD.CSA.rec.CSAID, 0);
    
    return 0;
    
OnError(err)
    println(err.message);
    if( cmd )
        var i = 0;
        while( i < cmd.connection.environment.ErrorCount - 1 )
            println(cmd.connection.environment.Error(i).Descr);
            msgbox(cmd.connection.environment.Error(i).Descr);
            i = i + 1;
        end;
    end;
    return 1;
END;