/*
$Name:        dvbycntr706.mac
$Module:      Производ. инструм.
$Description: Отчет "Сведения об объемах внебиржевых сделок" - часть ПИ
*/
IMPORT "DvRepFun2.mac", "dv_categ.mac";

PRIVATE VAR    FDlByCntrTmp = TBFile( "dlbycntr.tmp", "W", 0 );
PRIVATE CONST  DEAL_TYPE_SALE = 1,       /*продажи\погашения*/
               DEAL_TYPE_BUY  = 2;       /*покупки*/

PRIVATE MACRO ReportRun706( Report:variant )
   RECORD REmi( party );
   RECORD DEmi( party );
   var Fininstr = TRecHandler("fininstr.dbt");
   var AV = Trechandler( "avoiriss.dbt" );
   var NRec = 0, FD, Count = 0; 
   var EmiID, CFI, Summa = $0, NumInList = "";

   /*отбираем поставочные сделки форвард, в которых НЕ выставлен признак ПФИ и которые в БОЦБ еще не пришли*/
   var RsdDVQuery, DVDealData,
       DVQuery = " SELECT DVDeal.T_ID, DVDeal.t_DocKind " +
                 "   FROM ddvndeal_dbt DVDeal, dfininstr_dbt fin, ddvnfi_dbt nfi " +
                 "  WHERE nfi.T_DealID = DVDeal.T_ID " +
                 "    and nfi.T_TYPE = 0 " +
                 "    and fin.t_fiid = nfi.T_fiid " +
                 "    and fin.t_FI_KIND = ? " +/* отбираем сделки, кот. совершены с исходным активом - ценной бумагой */
                 "    and DVDeal.T_IsTrust = chr(0) " +
                 "    and DVDeal.T_DVKIND in (?, ?, ?) "+
                 "    and DVDeal.T_State > 0 " +
                 "    and DVDeal.T_Date >= ? " +
                 "    and DVDeal.T_Date <= ? " +
                 "    and nFI.t_ExecType = ? " +
                 "    and not exists (select 1 " +
                 "                      from DPARTYOWN_DBT PARTYOWN " +
                 "                     where PARTYOWN.T_PARTYID = DVDeal.T_Contractor " +
                 "                       and PARTYOWN.T_PARTYKIND = ? " +/*Сделка считается внебиржевой, если контрагент по сделке не равен бирже*/
                 "                   ) ";
                                                                                       
   RsdDVQuery = DL_RSDCommand(DVQuery);

   RsdDVQuery.addParam(FIKIND_AVOIRISS);
   
   RsdDVQuery.addParam(DV_FORWARD);
   RsdDVQuery.addParam(DV_FORWARD_T3);
   RsdDVQuery.addParam(DV_OPTION);
   
   RsdDVQuery.addParam(Report.GetBegDate());
   RsdDVQuery.addParam(Report.GetEndDate());
   RsdDVQuery.addParam(DVSETTLEMET_STATE);
   RsdDVQuery.addParam(PTK_MARKETPLASE);

   NRec = RsdDVQuery.GetCount();

   DVDealData = RsdDVQuery.execute();
                                                                                                                
   InitProgress( NRec, "Отчет \"Сведения об объемах внебиржевых сделок\"", "Просмотр внебиржевых сделок с ПФИ" );
   while( DVDealData.MoveNext() )
      FD = DVFirstDocNDeal(DVDealData.DocKind, DVDealData.ID);

      ClearRecord( FDlByCntrTmp );
      ClearRecord( REmi );
      
      /*Замена эмитента возможна только для облигаций, в этом случае получим его из истории*/
      if( FI_IsBond(FD.BaseFI()))
         EmiID = FI_GetIssuerOnDate( FD.BaseFI(), Report.GetEndDate());   
      else
         EmiID = FD.BaseFI().rec.Issuer;
      end;
    
      if( ПолучитьСубъекта( EmiID, REmi ) != 0 ) /*эмитент*/
         Report.PrintMsgbox( "Не найден эмитент для ц/б " + FD.BaseFI().rec.FI_Code );
         UseProgress( Count = Count + 1 );
         continue;
      end; 
    
      if( FI_IsShare(FD.BaseFI(), false) )
         FDlByCntrTmp.rec.AvrType  = AVOIRISSKIND_SHARE;
      elif( FI_IsBond(FD.BaseFI()) )
         FDlByCntrTmp.rec.AvrType  = AVOIRISSKIND_BOND;
      elif( FI_IsInvestmentShare(FD.BaseFI()) )
         FDlByCntrTmp.rec.AvrType  = AVOIRISSKIND_INVESTMENT_SHARE;
      elif( FI_IsDeposReceipt(FD.BaseFI()) and (FD.BaseFI().rec.AvoirKind == AVOIRISSKIND_RUSSIAN_DEPREC) )
         FDlByCntrTmp.rec.AvrType  = AVOIRISSKIND_DEPOSITORY_RECEIPT;
      else
         UseProgress( Count = Count + 1 );
         continue;
      end;
    
      FDlByCntrTmp.rec.NotResident = REmi.NotResident;/*резидентность эмитента*/
    
      if( ПолучитьФинИн( FD.BaseFI().rec.FIID, null, AV ) != 0 ) 
         Report.PrintMsgbox( "Не найдена ценная бумага " + string(FD.BaseFI().rec.FIID) );
         UseProgress( Count = Count + 1 );
         continue;
      end;
    
      if( FDlByCntrTmp.rec.NotResident == SET_CHAR )
         if( DL_GetObjAttrName( OBJTYPE_AVOIRISS, 28/*Квалификация в качестве ценной бумаги*/, DL_GetMainObjAttr(AV,28/*Квалификация в качестве ценной бумаги*/,OBJTYPE_AVOIRISS) ) == "Нет" )
            UseProgress( Count = Count + 1 );
            continue;
         end;
      end;
             
      FDlByCntrTmp.rec.FI_Kind  = FD.BaseFI().rec.FI_Kind;
      FDlByCntrTmp.rec.DealDate = FD.ДатаСделки();
    
      CFI  = FD.pm_CA().rec.OrderFIID;
      Summa = FD.pm_CA().rec.OrderAmount;
    
      if(CFI != NATCUR)
         if( NOT DV_SmartConvertSum( Summa, Summa, FD.ДатаСделки(), CFI, NATCUR, true ) )
            Report.PrintMsgBox( GetCurrencyConvertErrorMsg(CFI, NATCUR, FD.ДатаСделки()) );
            UseProgress( Count = Count + 1 );
            continue;
         end;
      end;
    
      FDlByCntrTmp.rec.Summa = Summa;

      FDlByCntrTmp.rec.FIID  = FD.BaseFI().rec.FIID;
      FDlByCntrTmp.rec.AvrName    = DL_GetAvrKindName(FD.BaseFI().rec.FI_KIND, FD.BaseFI().rec.AvoirKind);/*здесь сохраняем наименование вида ц\б*/

      if( FDlByCntrTmp.rec.AvrType == AVOIRISSKIND_DEPOSITORY_RECEIPT )
         // Для РДР запишем в виде
         // <"Сок. наименование эмитента РДР"> на <Вид баз. актива> <"Сок. наименование эмитента баз. актива">
     
         if( (FD.BaseFI().rec.ParentFI > 0) and ПолучитьФинИн(FD.BaseFI().rec.ParentFI, Fininstr) )
            Report.PrintMsgbox("Не найдена ц/б (FIID = " + FD.BaseFI().rec.ParentFI + ")");
         else
            if( FI_IsBond(Fininstr))
               EmiID = FI_GetIssuerOnDate(Fininstr, Report.GetEndDate());
            else
               EmiID = Fininstr.rec.Issuer;
            end;

            if( ПолучитьСубъекта(EmiID, DEmi) != 0 ) /*эмитент*/
               Report.PrintMsgbox("Не найден эмитент для ц/б " + Fininstr.rec.FI_Code);
            end;
         end;

         FDlByCntrTmp.rec.IssuerName = REmi.ShortName + " на " + DL_GetAvrKindName(Fininstr.rec.FI_KIND, Fininstr.rec.AvoirKind) + " " + DEmi.ShortName;
      else
         FDlByCntrTmp.rec.IssuerName = REmi.Name;/*полное наименование эмитента*/
      end;

    
      FDlByCntrTmp.rec.ContrKind = -1;
      FDlByCntrTmp.rec.Department = FD.Deal.rec.Department;
    
      if( FD.IsClient() )
         FDlByCntrTmp.rec.TypeDeal = "111";
      else
         FDlByCntrTmp.rec.TypeDeal = "110";
      end;
    
      if( FD.IsBuy() )
         FDlByCntrTmp.rec.BuySale = DEAL_TYPE_BUY;
      else
         FDlByCntrTmp.rec.BuySale = DEAL_TYPE_SALE;
      end;

       if( not Insert( FDlByCntrTmp ) )
          RunError( "Ошибка при вставке данных во временный файл отчета.");
       end;

      UseProgress( Count = Count + 1 );
   end;
   RemProgress;
END;
