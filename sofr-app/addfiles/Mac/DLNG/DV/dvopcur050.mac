/*
$Name:        dvopcur050.mac
$Module:      Производные инструменты
$Description: Расчеты по производным инструментам на валютном рынке/рынке СПФИ. Шаг "Закрытие счетов расчетов с биржей"
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac";

CLASS (TArray) DocOffAccSumCollector

  PRIVATE CLASS DocOffAccSum( Chapter_:integer, Cat_:string, PairCat_:string, Sum_:money, FIID_:integer )
    var Chapter:integer = Chapter_,
        Cat:string      = Cat_,
        PairCat:string  = PairCat_,
        Sum:money       = Sum_,
        FIID:integer    = FIID_;
  END;

  MACRO ClearArray()
     this.Size = 0;
  END;

  MACRO AddData( Chapter_:integer, Cat_:string, PairCat_:string, Sum_:money, FIID_:integer )
     var Ex:bool = false;
     var i:integer = 0;

     while( i < this.Size )
        if( (this.(i).Chapter == Chapter_) and (this.(i).FIID == FIID_) and ((this.(i).Cat == Cat_) or (this.(i).PairCat == Cat_)) )
           this.(i).Sum = this.(i).Sum + Sum_;
           Ex = true;
           break;
        end;
        i = i + 1;
     end;

     if( Ex == false )
        this.(this.size) = DocOffAccSum(Chapter_, Cat_, PairCat_, Sum_, FIID_);
     end;
  END;
END;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record oper(dvoper);
  var Query, Data;
  var СчетДебет = TRecHandler("account"), СчетКредит = TRecHandler("account");
  var ClAcc  = TRecHandler("account");
  var DocOffDebAccSum  = DocOffAccSumCollector();
  var DocOffCredAccSum = DocOffAccSumCollector();

  SetBuff(oper, FDoc);

  var FD_Oper = DVFirstDocOper(DL_DVOPER, oper);

  Query = RSDCommand( " SELECT docoff.* " +
                      "   FROM dspdocoff_dbt docoff " +
                      "  WHERE docoff.t_MarketKind = ? " +
                      "    AND docoff.t_PartyID    = ? " +
                      "    AND docoff.t_OfficeID   = ? " +
                      "    AND docoff.t_State      = ? " +
                      "    AND docoff.t_Date_Carry = ? " +
                      "    AND docoff.t_Chapter = 1 " +
                      "    AND docoff.t_IsClientCarry " + IIF(FD_Oper.IsClient(), "=", "<>") + "chr(88)"

                    );
  Query.NullConversion = true;

  Query.addParam("", RSDBP_IN, FD_Oper.GetMarketKind());
  Query.addParam("", RSDBP_IN, FD_Oper.GetContractorID());
  Query.addParam("", RSDBP_IN, FD_Oper.DVOper.rec.PartyOffice);
  Query.addParam("", RSDBP_IN, SPDOCOFF_STATE_EXECUTED);
  Query.addParam("", RSDBP_IN, FD_Oper.DVOper.rec.Date);
  Query.execute();
  Data = TRsbDataSet(Query);
  while( Data.MoveNext() )

     ClearRecord(СчетДебет);
     ClearRecord(СчетКредит);

     if( DL_GetAccount(SQL_ConvTypeInteger(Data.Chapter), SQL_ConvTypeInteger(Data.Currency_Payer),    SQL_ConvTypeStr(Data.Account_Payer), СчетДебет) and
         DL_GetAccount(SQL_ConvTypeInteger(Data.Chapter), SQL_ConvTypeInteger(Data.Currency_Receiver), SQL_ConvTypeStr(Data.Account_Receiver), СчетКредит)
       )
        var CatDebet = "", CatDebetPair = "";
        if( DL_ПолучитьКатегориюСчетаПроводки(СчетДебет, @CatDebet, @CatDebetPair) )
           if( (CatDebet == "+Биржа") or (CatDebet == "-Биржа") )
              DocOffDebAccSum.AddData(SQL_ConvTypeInteger(Data.Chapter), CatDebet, CatDebetPair, SQL_ConvTypeSum(Data.Sum_Payer), SQL_ConvTypeInteger(Data.Currency_Payer));
           end;
        end;

        var CatCred = "", CatCredPair = "";
        if( DL_ПолучитьКатегориюСчетаПроводки(СчетКредит, @CatCred, @CatCredPair) )
           if( (CatCred == "+Биржа") or (CatCred == "-Биржа") )
              DocOffCredAccSum.AddData(SQL_ConvTypeInteger(Data.Chapter), CatCred, CatCredPair, SQL_ConvTypeSum(Data.Sum_Receiver), SQL_ConvTypeInteger(Data.Currency_Receiver));
           end;
        end;
     end;
  end;

  var i:integer = 0;
  while( i < DocOffDebAccSum.Size )

     if( not FD_Oper.IsExistAccount("Клиринговый счет", null, true, null, ClAcc, null, FD_Oper.DVOper.rec.Date, DocOffDebAccSum(i).FIID) )
        MsgBox("Ошибка: не открыт счет категории \"Клиринговый счет\" в валюте FIID = " + String(DocOffDebAccSum(i).FIID));
        return 1;
     end;

     if( not ПроводкаПоКатегориямУчета( FD_Oper,
                                        ClAcc, "+Биржа",
                                        FD_Oper.DVOper.rec.Date,
                                        1,
                                        DocOffDebAccSum(i).FIID, DocOffDebAccSum(i).Sum,
                                        doc,
                                        INPCARRY, "", "",
                                        "Закрытие задолженностей по расчетам с биржей",
                                        null, null, null,
                                        DocOffDebAccSum(i).FIID, DocOffDebAccSum(i).FIID
                                      )
       )
         return 1;
     end;

     i = i + 1;
  end;

  i = 0;
  while( i < DocOffCredAccSum.Size )

     if( not FD_Oper.IsExistAccount("Клиринговый счет", null, true, null, ClAcc, null, FD_Oper.DVOper.rec.Date, DocOffCredAccSum(i).FIID) )
        MsgBox("Ошибка: не открыт счет категории \"Клиринговый счет\" в валюте FIID = " + String(DocOffCredAccSum(i).FIID));
        return 1;
     end;

     if( not ПроводкаПоКатегориямУчета( FD_Oper,
                                        "-Биржа", ClAcc,
                                        FD_Oper.DVOper.rec.Date,
                                        1,
                                        DocOffCredAccSum(i).FIID, DocOffCredAccSum(i).Sum,
                                        doc,
                                        INPCARRY, "", "",
                                        "Закрытие задолженностей по расчетам с биржей",
                                        null, null, null,
                                        DocOffCredAccSum(i).FIID, DocOffCredAccSum(i).FIID
                                      )
       )
         return 1;
     end;

     i = i + 1;
  end;

  return 0;
end;
