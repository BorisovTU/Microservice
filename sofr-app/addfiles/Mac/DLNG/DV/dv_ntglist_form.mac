/*
$Name:         dv_ntglist_form.mac
$Module:       ФИСС и КО
$Description:  Форма отчета "Лист неттинга"
PROGRAMMED BY: Dubovoy A.A.
CREATION DATE: 20.02.16
*/
import "DlRepForm_h.mac", "secinter.mac";

// Номера полей в форме отчета
CONST
   PNFLD_NTGLISTREP_IsPFIOp     = 0,
   PNFLD_NTGLISTREP_IsConvOp    = 1,
   PNFLD_NTGLISTREP_IsFastOp    = 2,
   PNFLD_NTGLISTREP_IsExchCurOp = 3,
   PNFLD_NTGLISTREP_IsExchange  = 4,
   PNFLD_NTGLISTREP_IsOTC       = 5,
   PNFLD_NTGLISTREP_Contractor  = 6,
   PNFLD_NTGLISTREP_ContrName   = 7,
   PNFLD_NTGLISTREP_OpKind      = 8,
   PNFLD_NTGLISTREP_FIKind      = 9,
   PNFLD_NTGLISTREP_FI          = 10,
   PNFLD_NTGLISTREP_FIName      = 11,
   PNFLD_NTGLISTREP_DateBegin   = 12,
   PNFLD_NTGLISTREP_DateEnd     = 13,
   PNFLD_NTGLISTREP_IsSplit     = 14;

// Обработчики для нестандарных контролов
PRIVATE CONST
   H_IsPFIOp     = 101,
   H_IsConvOp    = 102,
   H_IsFastOp    = 103,
   H_IsExchCurOp = 104,
   H_IsExchange  = 105,
   H_IsOTC       = 106,
   H_Contractor  = 107,
   H_ContrName   = 108,
   H_OpKind      = 109,
   H_FIKind      = 110,
   H_FI          = 111,
   H_FIName      = 112,
   H_DateBegin   = 113,
   H_DateEnd     = 114,
   H_IsSplit     = 115;

// Значения полей в форме отчета
CLASS NtgListRepPanelData()
   var                     
   IsPFIOp       = "X",
   IsConvOp      = "X",
   IsFastOp      = "X",
   IsExchCurOp   = "X",
   IsExchange    = "",
   IsOTC         = "X",
   Contractor    = -1/*UnknownParty*/,
   OpKind        = -1,
   FIKind        = -1,
   FI            = ALLFININSTR,
   DateBegin     = {curdate},
   DateEnd       = Date(0,0,0),
   IsSplit       = "",

   IsExcel       = "X",
   IsSetEndDate  = false;
END;
VAR NtgRepFormData = NtgListRepPanelData();

PRIVATE CONST SelCodeKind = 1; // Общий параметр для листалок Бирж,Субьектов,Брокеров,Клиентов


PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

/*Начальная дата*/
PRIVATE MACRO DLUSR_FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;

     if( pThis.ExistField(H_DateEnd) )
       if( FldRealValue > pThis.GetLinkedValue(H_DateEnd) )
         if( pThis.GetLinkedValue(H_DateEnd) != Date(0,0,0) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
         end;
       end;
     end; 
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

/*Конечная дата*/
PRIVATE MACRO DLUSR_FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     /*инициализация по умолчанию*/
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;
     if( pThis.ExistField(H_DateBegin) )
        if( FldRealValue != Date(0,0,0) )
           if( FldRealValue < pThis.GetLinkedValue(H_DateBegin) )
              MsgBox( "Дата окончания не может быть меньше даты начала периода.");
              return CM_IGNORE;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

// Обработчик поля контрагентов
private MACRO DLUSR_FldProc_Contractor( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode  = TBFile( "partcode.dbt",  "R", 0 );
  var whereCond:string  = "";
  var sql1 = PTK_MARKETPLASE + "," + PTK_BROKER;
  var sql2 = PTK_PACT + "," + PTK_BROKER;

  if( Cmd == DLG_INIT )
     if( (FldRealValue == null) or (FldRealValue <= 0) )
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_ContrName, "");
     else
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
          pThis.SetLinkedValue( H_ContrName, Party.rec.ShortName);
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_ContrName, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     var IsExchange:Bool = false;
     var s, r;
     pThis.GetLinkedValue( H_IsExchange, @s, @r);
     if( s == "X" )
        IsExchange = true;
     end;
     whereCond = " EXISTS( SELECT d2.T_PARTYID " +                             
                       "     FROM DPARTYOWN_DBT d2 " +                                 
                       "    WHERE t.T_PARTYID = d2.T_PARTYID " +                       
                       "      AND d2.T_PARTYKIND IN (" + IIF(IsExchange == false, sql2, sql1) + " ) " +
                       " ) ";
     if( ListPText( Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( H_ContrName, Party.rec.ShortName);
     end;
  end;

  return CM_DEFAULT;
END;

private MACRO GetOpKindWhereCond(panel)
   var whereCond = "",
       L1, L2, L12, r1, r2, r12, sIsExchange, sIsOTC, r3, r4;
   panel.GetLinkedValue( H_IsPFIOp, @L1, @r1);   
   panel.GetLinkedValue( H_IsConvOp, @L2, @r2);
   panel.GetLinkedValue( H_IsExchCurOp, @L12, @r12);
   panel.GetLinkedValue( H_IsExchange, @sIsExchange, @r3);   
   panel.GetLinkedValue( H_IsOTC, @sIsOTC, @r4);

   if( L1 == "X" )
      if( sIsExchange == "X" )
         whereCond = whereCond + " AND ( (opr.T_SYSTYPES IN( 'BU', 'BO', 'SU', 'SO', 'EU', 'EO' ) AND opr.T_DocKind = 192) "; // список биржевых операций ПФИ
      end;
      if( sIsOTC == "X" )
         if( whereCond != "" ) whereCond = whereCond + " OR "; else whereCond = " AND ( " end;
         whereCond = whereCond + " ((opr.T_SYSTYPES IN( 'NU', 'NO', 'NS', 'NH', 'NP' ) AND opr.T_DocKind = 199) or (opr.T_SYSTYPES = 'U' AND opr.T_DocKind = 4815)) "; // список внебиржевых операций ПФИ
      end;
   end;
   if( L2 == "X" )
        whereCond = whereCond + " AND ( (opr.T_SYSTYPES IN( 'D' ) AND opr.T_DocKind = 4813) "; // список конверсионных операций: Покупка/Продажа безналичная
   end;
   if( L12 == "X" )
        whereCond = whereCond + " AND ( (opr.T_SYSTYPES IN( 'D' ) AND opr.T_DocKind = 4813) or ((opr.T_SYSTYPES IN( 'NU', 'NS', 'NH' ) AND opr.T_DocKind = 199) or (opr.T_SYSTYPES = 'U' AND opr.T_DocKind = 4815)) ";
   end;

   if( whereCond != "")
      whereCond = whereCond + ") AND opr.T_NOTINUSE != chr(88) ";
   end;

   return whereCond;
end;

// Листалка видов операций
private MACRO ListOpKind( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, OperName:@VARIANT, WhereCond:String ):BOOL
  var select = " SELECT opr.t_Kind_Operation Oper, opr.t_Name NameOper " +
               " FROM doprkoper_dbt opr "  +
               " WHERE 1 = 1 " + IIF(WhereCond != null, WhereCond, "") +
               " ORDER BY opr.t_Kind_Operation";
  var Choice = DL_Scroll(select,
                         "Виды операций", X, Y,
                         "Oper","Номер", "NameOper","Название"
                        );
  if( Choice != NULL )
     FldRealValue = Choice.Value("Oper"); 
     FldShowValue = Choice.Value("NameOper");
     OperName = Choice.Value("NameOper");
  end;

  return (Choice != NULL);
END;

// Обработчик поля виды операций
private MACRO DLUSR_FldProc_OpKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
 var OperName = "";

 if( Cmd == DLG_INIT )
     if( (FldRealValue == null) or (FldRealValue <= 0) )
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     if(ListOpKind( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), @OperName, GetOpKindWhereCond(pThis)) == true)
     end;
  end;

  return CM_DEFAULT;
END;

private MACRO GetFIKindWhereCond(panel)
   var whereCond = "",
       L1, L2, L3, L12, r1, r2, r3, r12;

   panel.GetLinkedValue( H_IsPFIOp, @L1, @r1);   
   panel.GetLinkedValue( H_IsConvOp, @L2, @r2);
   panel.GetLinkedValue( H_IsFastOp, @L3, @r3);
   panel.GetLinkedValue( H_IsExchCurOp, @L12, @r12);
   
   if( L1 == "X" )
      whereCond = FIKIND_CURRENCY + "," + FIKIND_AVOIRISS + "," + FIKIND_INDEX + "," + FIKIND_DERIVATIVE + "," + FIKIND_METAL + "," + FIKIND_ARTICLE;
   else
      if( ((L2 == "X") or (L12 == "X")) or (((L2 == "X") or (L12 == "X")) and (L3 == "X")) )
         whereCond = FIKIND_CURRENCY + "," + FIKIND_METAL;
      end;
   end;

   if( whereCond != "")
      whereCond = " AND fk.t_FI_Kind IN( " + whereCond + " ) ";
   end;

   return whereCond;
end;

// Листалка видов ФИ
private MACRO ListFIKind( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, FIKindName:@VARIANT, WhereCond:String ):BOOL
  var select = " SELECT fk.t_FI_Kind FIKind, fk.t_Name NameFIKind " +
               " FROM dFiKinds_dbt fk "  +
               " WHERE 1 = 1 " + IIF(WhereCond != null, WhereCond, "") +
               " ORDER BY fk.t_FI_Kind";
  var Choice = DL_Scroll(select,
                         "Виды финансовых инструментов", X, Y,
                         "FIKind","Номер", "NameFIKind","Название"
                        );
  if( Choice != NULL )
     FldRealValue = Choice.Value("FIKind"); 
     FldShowValue = Choice.Value("NameFIKind");
     FIKindName = Choice.Value("NameFIKind");
  end;

  return (Choice != NULL);
END;

// Обработчик поля виды ФИ
private MACRO DLUSR_FldProc_FIKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
 var FIKindName = "";
 var fikinds  = TBFile( "fikinds.dbt",  "R", 0 );

 if( Cmd == DLG_INIT )
     if( (FldRealValue == null) or (FldRealValue <= 0) )
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        DisableFields( pThis.Data(), PNFLD_NTGLISTREP_FI );
     elif( FldRealValue == FIKIND_AVOIRISS )
        fikinds.Clear();
        fikinds.rec.FI_Kind  = FldRealValue;
        if (fikinds.GetEQ())
          FldShowValue = fikinds.rec.Name;
        end;
        EnableFields( pThis.Data(), PNFLD_NTGLISTREP_FI );
        pThis.SetLinkedValue( H_FI, ALLFININSTR);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     DisableFields( pThis.Data(), PNFLD_NTGLISTREP_FI );
     pThis.SetLinkedValue( H_FI, FldRealValue);
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     if(ListFIKind( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), @FIKindName, GetFIKindWhereCond(pThis)) == true)
        pThis.SetLinkedValue( H_FI, ALLFININSTR);
        EnableFields( pThis.Data(), PNFLD_NTGLISTREP_FI );
     elif( FldRealValue == ALLFININSTR )
        DisableFields( pThis.Data(), PNFLD_NTGLISTREP_FI );
     end;
  end;

  return CM_DEFAULT;
END;

private MACRO GetFIWhereCond(panel)
   var whereCond = "",
       s, FIKind;

   panel.GetLinkedValue( H_FIKind, @s, @FIKind);   

   if( FIKind != ALLFININSTR )
      whereCond = FIKind;
   end;

   if( whereCond != "")
      whereCond = " AND fi.t_FI_Kind IN( " + whereCond + " ) ";
   end;

   return whereCond;
end;

// Листалка ФИ
private MACRO ListFI( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, FIName:@VARIANT, WhereCond:String ):BOOL
  var select = " SELECT fi.t_FI_Code FICode, fi.t_Name NameFI, fi.t_FIID FI " +
               " FROM dfininstr_dbt fi "  +
               " WHERE fi.t_IsClosed = chr(0) " + IIF(WhereCond != null, WhereCond, "") +
               " ORDER BY fi.t_FI_Kind, fi.t_FI_Code";
  var Choice = DL_Scroll(select,
                         "Список финансовых инструментов", X, Y,
                         "FICode","Код анкеты ФИ", "NameFI","Название", "FI","ID инструмента"
                        );
  if( Choice != NULL )
     FldRealValue = Choice.Value("FI"); 
     FldShowValue = Choice.Value("FICode");
     FIName = Choice.Value("NameFI");
  end;

  return (Choice != NULL);
END;

// Обработчик поля ФИ
private MACRO DLUSR_FldProc_FI( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
 var FIKindName = "";

 if( Cmd == DLG_INIT )
     if( (FldRealValue == null) or (FldRealValue < 0) )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_FIName, "");
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = ALLFININSTR;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_FIName, "");
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     if(ListFI( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), @FIKindName, GetFIWhereCond(pThis)) == true)
        pThis.SetLinkedValue( H_FIName, FIKindName);
     end;
  end;

  return CM_DEFAULT;
END;

private macro ConfigFldOpKind(panel, rIsPFI,rIsConv,rIsFast,rIsExCur,rIsEx,rIsOTC)
    var rFIKind, s;
    panel.GetLinkedValue( H_FIKind, @s, @rFIKind);

   if( (rIsConv == "X") and (rIsPFI == "") and (rIsFast == "") and (rIsExCur == "") )  // признаки рынка для L2
      panel.SetLinkedValue( H_IsExchange, "");
      panel.SetLinkedValue( H_IsOTC, "X");
      DisableFields( panel.Data(), PNFLD_NTGLISTREP_IsExchange );
      DisableFields( panel.Data(), PNFLD_NTGLISTREP_IsOTC );
   else
      EnableFields( panel.Data(), PNFLD_NTGLISTREP_IsExchange );
      EnableFields( panel.Data(), PNFLD_NTGLISTREP_IsOTC );
   end; 

   if( ((rIsFast == "")) and
       ( ((rIsPFI == "X") and (rIsConv == "") and (rIsExCur == "")) or ((rIsConv == "X") and (rIsPFI == "") and (rIsExCur == "")) or ((rIsExCur == "X") and (rIsPFI == "") and (rIsConv == "")) ) )
      if( (rIsEx == "X") or (rIsOTC == "X") or (rIsExCur == "X") )
         EnableFields( panel.Data(), PNFLD_NTGLISTREP_OpKind );
      else
         DisableFields( panel.Data(), PNFLD_NTGLISTREP_OpKind );
         panel.SetLinkedValue( H_OpKind, -1);
      end;
   else
      DisableFields( panel.Data(), PNFLD_NTGLISTREP_OpKind );
      panel.SetLinkedValue( H_OpKind, -1);
   end;

   if( (rIsFast == "X") and (rIsPFI == "") and (rIsConv == "") and (rIsExCur == "") )  // "ценные бумаги" для L3
      panel.SetLinkedValue( H_FIKind, FIKIND_AVOIRISS);
      DisableFields( panel.Data(), PNFLD_NTGLISTREP_FIKind );
   else 
      EnableFields( panel.Data(), PNFLD_NTGLISTREP_FIKind ); 
      if( (rFIKind == FIKIND_AVOIRISS) and (((rIsConv == "X") or (rIsExCur == "X")) or (((rIsConv == "X") or (rIsExCur == "X")) and (rIsFast == "X"))) )
         panel.SetLinkedValue( H_FIKind, FIKIND_ALLFI);
      end;
   end;
end;

PRIVATE MACRO DLUSR_FldProc_IsExchange( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
   var rIsPFI,rIsConv,rIsFast,rIsExCur,rIsOTC, s1,s2,s3,s4,s6;
   pThis.GetLinkedValue( H_IsPFIOp, @s1, @rIsPFI);
   pThis.GetLinkedValue( H_IsConvOp, @s2, @rIsConv);
   pThis.GetLinkedValue( H_IsFastOp, @s3, @rIsFast);
   pThis.GetLinkedValue( H_IsExchCurOp, @s4, @rIsExCur);
   pThis.GetLinkedValue( H_IsOTC, @s6, @rIsOTC);

  if( Cmd == DLG_INIT )
     if( ((FldRealValue == "") and (rIsOTC == "") and not ((rIsExCur == "X") and (rIsConv == "") and (rIsPFI == "") and (rIsFast == ""))) or
         ((FldRealValue == "X") and (rIsOTC == "X") and not ((rIsExCur == "X") and (rIsConv == "") and (rIsPFI == "") and (rIsFast == ""))) ) /*инициализация по умолчанию*/
        DisableFields( pThis.Data(), PNFLD_NTGLISTREP_Contractor );
        pThis.SetLinkedValue( H_Contractor, -1);
     else
        EnableFields( pThis.Data(), PNFLD_NTGLISTREP_Contractor );
     end;
     FldShowValue = FldRealValue;
     ConfigFldOpKind(pThis, rIsPFI,rIsConv,rIsFast,rIsExCur,FldRealValue,rIsOTC);
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
     else
        FldShowValue = FldRealValue = "";
     end;
     if( ((FldRealValue == "") and (rIsOTC == "") and not ((rIsExCur == "X") and (rIsConv == "") and (rIsPFI == "") and (rIsFast == ""))) or
         ((FldRealValue == "X") and (rIsOTC == "X") and not ((rIsExCur == "X") and (rIsConv == "") and (rIsPFI == "") and (rIsFast == ""))) )
        DisableFields( pThis.Data(), PNFLD_NTGLISTREP_Contractor );
        pThis.SetLinkedValue( H_Contractor, -1);
     else
        EnableFields( pThis.Data(), PNFLD_NTGLISTREP_Contractor );
     end;
     ConfigFldOpKind(pThis, rIsPFI,rIsConv,rIsFast,rIsExCur,FldRealValue,rIsOTC);
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_IsOTC( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
   var rIsPFI,rIsConv,rIsFast,rIsExCur,rIsEx, s1,s2,s3,s4,s5;
   pThis.GetLinkedValue( H_IsPFIOp, @s1, @rIsPFI);
   pThis.GetLinkedValue( H_IsConvOp, @s2, @rIsConv);
   pThis.GetLinkedValue( H_IsFastOp, @s3, @rIsFast);
   pThis.GetLinkedValue( H_IsExchCurOp, @s4, @rIsExCur);
   pThis.GetLinkedValue( H_IsExchange, @s5, @rIsEx);

  if( Cmd == DLG_INIT )
     if( ((FldRealValue == "") and (rIsEx == "") and not ((rIsExCur == "X") and (rIsConv == "") and (rIsPFI == "") and (rIsFast == ""))) or
         ((FldRealValue == "X") and (rIsEx == "X") and not ((rIsExCur == "X") and (rIsConv == "") and (rIsPFI == "") and (rIsFast == ""))) ) /*инициализация по умолчанию*/
        DisableFields( pThis.Data(), PNFLD_NTGLISTREP_Contractor );
        pThis.SetLinkedValue( H_Contractor, -1);
     else
        EnableFields( pThis.Data(), PNFLD_NTGLISTREP_Contractor );
     end;
     FldShowValue = FldRealValue;
     ConfigFldOpKind(pThis, rIsPFI,rIsConv,rIsFast,rIsExCur,rIsEx,FldRealValue);
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
     else
        FldShowValue = FldRealValue = "";
     end;
     if( ((FldRealValue == "") and (rIsEx == "") and not ((rIsExCur == "X") and (rIsConv == "") and (rIsPFI == "") and (rIsFast == ""))) or
         ((FldRealValue == "X") and (rIsEx == "X") and not ((rIsExCur == "X") and (rIsConv == "") and (rIsPFI == "") and (rIsFast == ""))) )
        DisableFields( pThis.Data(), PNFLD_NTGLISTREP_Contractor );
        pThis.SetLinkedValue( H_Contractor, -1);
     else
        EnableFields( pThis.Data(), PNFLD_NTGLISTREP_Contractor );
     end;
     ConfigFldOpKind(pThis, rIsPFI,rIsConv,rIsFast,rIsExCur,rIsEx,FldRealValue);
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_IsFastOp( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
   var rIsPFI,rIsConv,rIsExCur,rIsEx,rIsOTC, s1,s2,s4,s5,s6;
   pThis.GetLinkedValue( H_IsPFIOp, @s1, @rIsPFI);
   pThis.GetLinkedValue( H_IsConvOp, @s2, @rIsConv);
   pThis.GetLinkedValue( H_IsExchCurOp, @s4, @rIsExCur);
   pThis.GetLinkedValue( H_IsExchange, @s5, @rIsEx);
   pThis.GetLinkedValue( H_IsOTC, @s6, @rIsOTC);

  if( Cmd == DLG_INIT )
     if( (FldRealValue == "X") and (rIsConv == "") ) /*инициализация по умолчанию*/
        DisableFields( pThis.Data(), PNFLD_NTGLISTREP_FIKind );
     else
        EnableFields( pThis.Data(), PNFLD_NTGLISTREP_FIKind );
     end;
     FldShowValue = FldRealValue;
     ConfigFldOpKind(pThis, rIsPFI,rIsConv,FldRealValue,rIsExCur,rIsEx,rIsOTC);
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
     else
        FldShowValue = FldRealValue = "";
     end;
     ConfigFldOpKind(pThis, rIsPFI,rIsConv,FldRealValue,rIsExCur,rIsEx,rIsOTC);
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_IsPFIOp( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
   var rIsConv,rIsFast,rIsExCur,rIsEx,rIsOTC, s2,s3,s4,s5,s6;
   pThis.GetLinkedValue( H_IsConvOp, @s2, @rIsConv);
   pThis.GetLinkedValue( H_IsFastOp, @s3, @rIsFast);
   pThis.GetLinkedValue( H_IsExchCurOp, @s4, @rIsExCur);
   pThis.GetLinkedValue( H_IsExchange, @s5, @rIsEx);
   pThis.GetLinkedValue( H_IsOTC, @s6, @rIsOTC);

  if( Cmd == DLG_INIT )
     FldShowValue = FldRealValue;
     ConfigFldOpKind(pThis, FldRealValue,rIsConv,rIsFast,rIsExCur,rIsEx,rIsOTC);
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
     else
        FldShowValue = FldRealValue = "";
     end;
     ConfigFldOpKind(pThis, FldRealValue,rIsConv,rIsFast,rIsExCur,rIsEx,rIsOTC);
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_IsConvOp( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
   var rIsPFI,rIsFast,rIsExCur,rIsEx,rIsOTC, s1,s3,s4,s5,s6;
   pThis.GetLinkedValue( H_IsPFIOp, @s1, @rIsPFI);
   pThis.GetLinkedValue( H_IsFastOp, @s3, @rIsFast);
   pThis.GetLinkedValue( H_IsExchCurOp, @s4, @rIsExCur);
   pThis.GetLinkedValue( H_IsExchange, @s5, @rIsEx);
   pThis.GetLinkedValue( H_IsOTC, @s6, @rIsOTC);

  if( Cmd == DLG_INIT )
     FldShowValue = FldRealValue;
     ConfigFldOpKind(pThis, rIsPFI,FldRealValue,rIsFast,rIsExCur,rIsEx,rIsOTC);
     if( (rIsFast == "X") and (FldRealValue == "") )
        DisableFields( pThis.Data(), PNFLD_NTGLISTREP_FIKind );
     else
        EnableFields( pThis.Data(), PNFLD_NTGLISTREP_FIKind );
     end;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
     else
        FldShowValue = FldRealValue = "";
     end;
     ConfigFldOpKind(pThis, rIsPFI,FldRealValue,rIsFast,rIsExCur,rIsEx,rIsOTC);
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_IsExchCurOp( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
   var rIsPFI,rIsConv,rIsFast,rIsEx,rIsOTC, s1,s2,s3,s5,s6;
   pThis.GetLinkedValue( H_IsPFIOp, @s1, @rIsPFI);
   pThis.GetLinkedValue( H_IsConvOp, @s2, @rIsConv);
   pThis.GetLinkedValue( H_IsFastOp, @s3, @rIsFast);
   pThis.GetLinkedValue( H_IsExchange, @s5, @rIsEx);
   pThis.GetLinkedValue( H_IsOTC, @s6, @rIsOTC);

  if( Cmd == DLG_INIT )
     FldShowValue = FldRealValue;
     ConfigFldOpKind(pThis, rIsPFI,rIsConv,rIsFast,FldRealValue,rIsEx,rIsOTC);
     if( (rIsFast == "X") and (FldRealValue == "") )
           DisableFields( pThis.Data(), PNFLD_NTGLISTREP_FIKind );
        else
           EnableFields( pThis.Data(), PNFLD_NTGLISTREP_FIKind );
        end;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
     else
        FldShowValue = FldRealValue = "";
     end;
     ConfigFldOpKind(pThis, rIsPFI,rIsConv,rIsFast,FldRealValue,rIsEx,rIsOTC);
  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) DV_NtgListRep_Panel()
  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_NTGLISTREP_IsPFIOp,     H_IsPFIOp,     @DLUSR_FldProc_IsPFIOp,     NtgRepFormData.IsPFIOp);
     AddFieldProc( 0, PNFLD_NTGLISTREP_IsConvOp,    H_IsConvOp,    @DLUSR_FldProc_IsConvOp,    NtgRepFormData.IsConvOp);
     AddFieldProc( 0, PNFLD_NTGLISTREP_IsFastOp,    H_IsFastOp,    @DLUSR_FldProc_IsFastOp,    NtgRepFormData.IsFastOp );
     AddFieldProc( 0, PNFLD_NTGLISTREP_IsExchCurOp, H_IsExchCurOp, @DLUSR_FldProc_IsExchCurOp, NtgRepFormData.IsExchCurOp );
     AddFieldProc( 0, PNFLD_NTGLISTREP_IsExchange,  H_IsExchange,  @DLUSR_FldProc_IsExchange,  NtgRepFormData.IsExchange );
     AddFieldProc( 0, PNFLD_NTGLISTREP_IsOTC,       H_IsOTC,       @DLUSR_FldProc_IsOTC,       NtgRepFormData.IsOTC );
     AddFieldProc( 0, PNFLD_NTGLISTREP_Contractor,  H_Contractor,  @DLUSR_FldProc_Contractor,  NtgRepFormData.Contractor );
     AddFieldProc( 0, PNFLD_NTGLISTREP_ContrName,   H_ContrName,   @Default,                   "" );
     AddFieldProc( 0, PNFLD_NTGLISTREP_OpKind,      H_OpKind,      @DLUSR_FldProc_OpKind,      NtgRepFormData.OpKind );
     AddFieldProc( 0, PNFLD_NTGLISTREP_FIKind,      H_FIKind,      @DLUSR_FldProc_FIKind,      NtgRepFormData.FIKind );
     AddFieldProc( 0, PNFLD_NTGLISTREP_FI,          H_FI,          @DLUSR_FldProc_FI,          NtgRepFormData.FI );
     AddFieldProc( 0, PNFLD_NTGLISTREP_FIName,      H_FIName,      @Default,                   "" );
     AddFieldProc( 0, PNFLD_NTGLISTREP_DateBegin,   H_DateBegin,   @DLUSR_FldProc_BeginDate,   NtgRepFormData.DateBegin );
     AddFieldProc( 0, PNFLD_NTGLISTREP_DateEnd,     H_DateEnd,     @DLUSR_FldProc_EndDate,     NtgRepFormData.DateEnd );
     AddFieldProc( 0, PNFLD_NTGLISTREP_IsSplit,     H_IsSplit,     @DL_FldProc_CheckBox,       NtgRepFormData.IsSplit );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "deals.lbr", "DV_NTG_P" ); 
END;