/*
$Name:        dvcsa055.mac
$Module:      ФИССиКО
$Description: Сопровождение CSA: Обработка входящих платежей по процентам
*/
import "dv_car.mac";

PRIVATE VAR CSAPayment = TRecHandler("dvcsapm.dbt");

MACRO ExecuteStep( doc, Fdoc, DocKind, ID_Operation, ID_Step )
    record csaBuf(dvcsa);
    SetBuff(csaBuf, FDoc);
    var FD = DVFirstCSA(DocKind, csaBuf.CSAID);
    var PaymentObj:RsbPayment;
    var inGround = "Перечисление процентов контрагентом по договору " + FD.CSA.rec.Code;
    
    var query = "SELECT t_PaymentID ID "
               +"  FROM DPMPAYM_DBT "
               +" WHERE     t_DocKind = 4627 " /*Платеж по CSA*/
               +"       AND t_DocumentID = " + CSAPayment.rec.PMID
               +"       AND t_PaymStatus <> " + PM_FINISHED;
    
    var cmd = RsdCommand(query);
    cmd.execute;
    var StepPayments = TRsbDataSet(cmd);
    
    if (StepPayments)
        var bankAccount = TRecHandler("account");
        
        while (StepPayments.moveNext)
            PaymentObj = RsbPayment(StepPayments.ID);
            
            if(ПИ_ЭтоВнешнийВходящийПлатеж(PaymentObj) and ПИ_ПлатежКвитуетсяПоВидуФИ(PaymentObj) )
                if( PaymentObj.FuturePayerAmount != 0 )
                    if( not GetTrue(true, "Плановый платеж "+PaymentObj.PaymentID+" не сквитован полностью. Исполнить требование?") )
                        return 1;
                    end;
                end;
            end;
            
            if (/*( PaymentObj.Ground == inGround )*/((PaymentObj.ReceiverBankID != {OurBank}) or (PaymentObj.PayerBankID != {OurBank})) and (not ПИ_СтутусПлатежИсполнен(PaymentObj.PaymStatus))) /*Входящий несквитованный платеж*//*PNV 511078*/
                
                if (CSAPayment.rec.IsNetting == SET_CHAR)
                    if(not FD.OpenAccount("+Расчеты_CSA", null, false, null, bankAccount, CSAPayment.rec.PlanPayDate, PaymentObj.BaseFIID, null, null, MC_PAIRACCMODE_MANUAL)) /*PNV i-support 508247*/
                        return 1;
                    end;
                else
                   /*CHVA*/
                    if (FD.CSA.rec.BaseCurrency == 8)/*для договоров в евро категория д.б. "+Треб. по%, отриц. ставка"*/
                             if(not FD.OpenAccount( "+Треб. по%, отриц. ставка", null, false, null, bankAccount, CSAPayment.rec.PlanPayDate, PaymentObj.BaseFIID, null, null, MC_PAIRACCMODE_MANUAL))
                                 return 1;
                           end;
                    else
                            if(not FD.OpenAccount("+%МС", null, false, null, bankAccount, CSAPayment.rec.PlanPayDate, PaymentObj.BaseFIID, null, null, MC_PAIRACCMODE_MANUAL))
                                return 1;
                           end;
                    end;
                  /*CHVA*/  
                end;
                
                PaymentObj.SetReceiverAccount(bankAccount.rec.Code_Currency, bankAccount.rec.Chapter, bankAccount.rec.Account);
            
                PaymentObj.Actuate();
                
                var payerAccount = TRecHandler("account");
            
                if( not DL_GetAccount(1, PaymentObj.FuturePayerFIID, PaymentObj.FuturePayerAccount, payerAccount) )
                    MsgBox("В АБС не найден счет "+PaymentObj.FuturePayerAccount);
                    return 1;
                end;
                
                ПроводкаПоКатегориямУчета( FD, 
                                           payerAccount, bankAccount,         /* КатегДеб , КатегКредит*/
                                           GetDateAfterWorkDays(PaymentObj.ValueDate,0,FD.ПолучитьКалендСвязанный()),/* Дата */
                                           1,                                 /* Глава */
                                           PaymentObj.BaseFIID,               /* Валюта */
                                           PaymentObj.BaseAmount,             /* Сумма  */
                                           null,                              /* Документ */
                                           INPCARRY,                          /* Result_Carry */
                                           "",                                /* Kind_Oper */
                                           "",                                /* Numb_Document */
                                           PaymentObj.Ground, PaymentObj.PaymentID
                                         );
                
                PaymentObj.PaymStatus = PM_FINISHED;
                PaymentObj.Actuate();
                
            else /*Исходящий платеж или уже сквитованный*/
                
                continue;
            
            end;
            
        end;
    end;
    
    return 0;
END;