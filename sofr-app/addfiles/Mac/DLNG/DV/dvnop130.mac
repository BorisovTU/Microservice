/*
$Name:        dvnop130.mac
$Module:      Производные инструменты
$Description: Внебиржевая операция. Шаг: Исполнение требований
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac";
import dv_carchng;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  SetBuff( ndeal, FDoc );
  var FD = DVFirstDocNDeal(DocKind, ndeal);
  if (FD.ОтказОтИсполнения())
     return 0;
  end;
  var dat;
  GetOprDate( DV_NDEAL_OPRDATE_REQ, dat );

  if(((FD.IsOption()) or (FD.IsForwardNDeal())) and (РАБОТА_С_РЕПОЗИТАРИЕМ()))
    if(ИзменениеСостоянияОбязательств_ДосрочноеИсполнение(FD, dat, DocKind) )
       return 1;
   end;
  end;

  var ДатаИсполненияШага:date;
  var ПлатежТреб = RsbPayment( FD.ПлатежТреб.rec.PaymentID );
  ДатаИсполненияШага = FD.ДатаИсполнения();

  if (ПлатежТреб.PaymStatus == PM_OVERDUE)
   var stat = true;
   var findAccount = "";
      stat = FD.FindNumberAccount( "Треб. с н.с.",  FindAccount );
      ПлатежТреб.FutureReceiverAccount = FindAccount;
  end;
  if (ndeal.netting == "X") 
      if (not FD.ВалютныйРынок())
          if ((ПлатежТреб.PaymStatus != PM_FINISHED ) and (ПлатежТреб.PaymStatus != PM_CLOSED_W_M_MOVEMENT )) 
               MsgBox("Даная операция должна выполняться в рамках неттинга.");
               return 1;
          end;
      end;
  else
       if( ИсполнитьТребование(FD, dat, doc) != 0 )
           return 1;
       end;
  end; 

  if( not InsertOprStatus(FD.DV_OPERSTATUS_DEMAND(), DV_OPERSTATUS_DL_COMPLETE ) )
     MsgBox("Ошибка при установке статуса <Требования> по операции.");
     return 1;
  end;

  if( GetOprStatus(FD.DV_OPERSTATUS_LIABILITY()) == DV_OPERSTATUS_DL_COMPLETE )

     if( ВозвратПолучениеДепозитнойМаржи(FD, doc, dat) != 0 )
        return 1;
     end;

     if( FD.IsForward() )
        var Дк = DL_GetMinComPlanDateByOper(DocKind, FD.Deal.rec.ID);

        if( Дк != date(0,0,0) )
           if( not InsertOprStatus(FD.DV_OPERSTATUS_COMISS(), DV_OPERSTATUS_PAYM_EX) )
              MsgBox("Ошибка при установке статуса <Оплата комиссий> по операции.");
              return 1;
           end;
           DL_CalendDefineOprDate(FD,DV_NDEAL_OPRDATE_COMISS, Дк);
        else
           if( not InsertOprStatus(FD.DV_OPERSTATUS_COMISS(), DV_OPERSTATUS_PAYM_NOTEX) )
              MsgBox("Ошибка при установке статуса <Оплата комиссий> по операции.");
              return 1;
           end;

           if( not InsertOprStatus(FD.DV_OPERSTATUS_DOC(), DV_OPERSTATUS_DOC_CLOSE) )
              MsgBox("Ошибка при установке статуса <Документооборот> по операции.");
              return 1;
           end;
        end;
     else
        if( not InsertOprStatus(FD.DV_OPERSTATUS_DOC(), DV_OPERSTATUS_DOC_CLOSE) )
           MsgBox("Ошибка при установке статуса <Документооборот> по операции.");
           return 1;
        end;
     end;
  end;

  return 0;
end;
