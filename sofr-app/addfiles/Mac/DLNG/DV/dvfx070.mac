/*
$Name:        dvfx070.mac
$Module:      Производные инструменты
$Description: Конверсионная операция. Шаг "Исполнение обязательств"
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac", dv_lib, dv_dm;
import "dl_check_mt300.mac", "usr_connect_attr.mac"; //Simanov

//Simanov. i-support 507260
//По валютным платежам с филиалами, должен документ формироваться с кодом 06.
private macro SetCodeDocument (dealid, dockind)
  var query = "select t_paymentid from dpmpaym_dbt pmpaym "
            + "where     t_dockind = :dockind "
            + "      and t_documentid = :dealid "
            + "      and exists (select 1 from dpmprop_dbt where t_paymentid = pmpaym.t_paymentid and t_debetcredit = 1 and t_corschem > -1) " //Есть исходящая корсхема
            + "      and t_receiverbankid in (select t_partyid from ddp_dep_dbt) "
            + "      and t_receiverbankid <> 1 ";
  var sql = execSqlSelect(query, makeArray(sqlParam("dockind", dockind), sqlParam("dealid", dealid)));

  while (sql.moveNext( ))
    Connect_attr_to_paym (sql.value("t_paymentid"), ATTR_GROUP_DOCUMENT_BIS, "06");
  end;

End;

private macro DV_ПроверитьСуществованиеШага(ID_Operation, Symb, IsExecute)
  var cmd = DL_RSDCommand();
   var query = " SELECT COUNT(1) AS CountStep " +
    "   FROM doprstep_dbt step" +
    "  WHERE step.t_ID_Operation = ? AND " +
    "        step.t_Symbol       = ? AND " +
    "        step.t_IsExecute    = ? ";
  cmd.AddParam(ID_Operation);
  cmd.AddParam(Symb);
  cmd.AddParam(IsExecute);
  var DataSet = cmd.Execute(query);
  DataSet.moveNext();
  return DataSet.CountStep;
end;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record deal(dvndeal);
  var FD;
  var sta, bs;
  var ДатаИсполненияШага:date;

  SetBuff(deal, FDoc);
  FD = DVFirstDocFXDeal(DL_DVFXDEAL, deal);

  GetOprDate(DV_FXDEAL_OPRDATE_OBL, ДатаИсполненияШага);

  if(FD.IsDealMet())
   if( DV_ПроверитьСуществованиеШага(ID_Operation, "Е", "R") OR DV_ПроверитьСуществованиеШага(ID_Operation, "е", "R"))
      MsgBox("Исполнения Т/О возможно только после выполнения существующих шагов переноса на просрочку");
      return 1;
   end;
  end;

  SetCodeDocument(deal.id, deal.dockind);

 // ДатаИсполненияШага = FD.ДатаОбязательства();/*PNV 500390*/
  if (deal.netting == "X") 
    if ( (DL_ЕдиныйПулОбеспеченияСобств == 1) and (FD.Биржевая()) and (not FD.IsClient()) )
        if( ПИКО_ИсполнитьОбязательствоЧастиСделки(FD, ДатаИсполненияШага, doc) != 0 )
            return 1;
        end;
   end;
    if ( deal.type == 2 )
       bs = 1;
    else
       bs = 2;
    end;

    sta = plat_dil5(deal.id, DL_DVFXDEAL, bs, "paymstatus");
   if (not FD.ВалютныйРынок())
       if ((sta != PM_FINISHED ) and (sta != PM_CLOSED_W_M_MOVEMENT )) 
            MsgBox("Даная операция должна выполняться в рамках неттинга.");
            return 1;
       end;
   else
       if ((sta != PM_FINISHED ) and (not FD.IsClient()) )
            var Payment = RsbPayment(FD.ПлатежОб.rec.PaymentID);
            if( ПИ_УстановитьСтатусПлатежа(Payment, PM_CLOSED_W_M_MOVEMENT) )
                MsgBox("Ошибка при установке платежу статуса \"Исполнен\"");
                return 1;
            end; 
       end;
   end;
  else
    //Simanov. 13.01.2020 Проверка сквитованных МТ300 для сделок, заключенных по телефону
    if (not DV_check_kvit_mt(deal.dockind, deal.id, deal.code) )
      return 1;
    end;

    if( ПИКО_ИсполнитьОбязательствоЧастиСделки(FD, ДатаИсполненияШага, doc) != 0 )
        return 1;
    end;
    /*PNV - редкий случай. Сделка у которой дата оплаты и поставки отличаются, и дата оплаты = дате заключения
            такая сделка на внебаланс не ставится, поэтому переоценка не выполняется, а исполнение обязательств идет в дату поставки, т.е. уже по другому курсу.
            Разницу в курсах нужно списать на фин.рез*/
    if ((FD.ВидБазовогоАктива() == FIKIND_METAL) and (not FD.IsDealMet()))/*PNV 517767*/
        if ( /*(FD.СрокСделки() != 0) and*/ (FD.датаТребования() != FD.ДатаОбязательства()) and ((FD.ДатаСделки() == FD.ДатаОбязательства()) or (FD.ДатаСделки() == FD.датаТребования())) and (FD.ВалютаОбязательства() == FD.ВалютаБазовогоАктива()) )/*нужно списать фин.рез*/
              if( ПИКО_СписатьПереоценкуЧастиСделкиФинрез(FD, ДатаИсполненияШага, doc) != 0 )
                  return 1;
              end;
        end;
    end;
  end;

  /* в проекте через платеж, здесь через статусы операции */
  if( GetOprStatus(DV_FX_OPERSTATUS_KIND_DEMAND) == DV_FX_OPERSTATUS_DL_COMPLETE )
     if (GetOprStatus(DV_FX_OPERSTATUS_KIND_BALANCE) == DV_FX_OPERSTATUS_DL_COMPLETE)
     if( not DV_ОбновитьСтатусЧастиСделки(FD.nFI, DL_LEG_FORM) )
        MsgBox("Ошибка при изменении статуса сделки");
        return 1;
     end;
     end;

     if( FD.IsSWAP() and (not FD.IsClient()))
        if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_BALANCE_2, DV_FX_OPERSTATUS_DL_RUN) )
           MsgBox("Ошибка при установке статуса <Баланс 2ч> по операции.");
           return 1;
        end;
     end;
  end;

  if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_LIABILITY, DV_FX_OPERSTATUS_DL_COMPLETE) )
     MsgBox("Ошибка при установке статуса <Обязательства> по операции.");
     return 1;
  end;

  if( FD.IsClient() )
     if( not InsertOprStatus(DV_FX_OPERSTATUS_KIND_LIABILITY_2, DV_FX_OPERSTATUS_DL_RUN) )
        MsgBox("Ошибка при установке статуса <Обязательства 2ч> по операции.");
        return 1;
     end;
  end;

  return 0;
end;
