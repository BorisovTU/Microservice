/*
$Name:        dvAlgCalcFrValOFCur.mac
$Module:      Производные инструменты
$Description: Алгоритм расчета справедливой стоимости форвардного контракта с открытой датой на валюту
*/
IMPORT "dvAlgCalcFrVal.mac";

MACRO DVCalcFrVal( pFrVal:TRecHandler, pDEAL:TRecHandler, pFI_1:TRecHandler, pFI_2:TRecHandler ):integer

  var Fininstr = TRecHandler( "fininstr.dbt" );

  if( pFrVal.rec.Date == date(0,0,0) )
     MsgBox("Не задана дата расчёта.");
  elif( (pDEAL.rec.DVKind != DV_FORWARD) and (pDEAL.rec.DVKind != DV_FORWARD_T3) )
     MsgBox("В операции некорректно указан алгоритм расчета. ПФИ не является форвардом на валюту.");
  elif( ПолучитьФинИн(pFI_1.rec.FIID, Fininstr) )
     MsgBox("Не найден ФИ c ID = " + pFI_1.rec.FIID + ".");
  elif( Fininstr.rec.FI_Kind != FIKIND_CURRENCY )
     MsgBox("В операции некорректно указан алгоритм расчета. ПФИ не является форвардом на валюту.");
  elif( pFI_1.rec.OpenDate == UNSET_CHAR )
     MsgBox("В операции некорректно указан алгоритм расчета. ПФИ не является форвардом с открытой датой.");
  else
     var F:money = 0.0, AccFIID:integer = IIF(pFI_1.rec.AccFIID != ALLFININSTR, pFI_1.rec.AccFIID, pFI_1.rec.PriceFIID),
         Sвба:double = 0.0, Sвр:double = 0.0, S1:money = 0.0, S2:money = 0.0;

     if( pFrVal.rec.Date <= pDEAL.rec.BeginDate )
        F = pFI_1.rec.Price;
     else
        var S:money = pFI_1.rec.Price;
        var Stat:integer = 0;
        var Днач:date = pDEAL.rec.BeginDate;
        var F0:money = 0;

        while( Stat == 0 )
           var Доконч:date = date(0,0,0);
           if( (ALG_GetSinceDate(pDEAL.rec.ID, Днач, @Доконч) == false) or (Доконч > pFrVal.rec.Date) )
              Stat = 1;
              Доконч = pFrVal.rec.Date;
           end;

           var Rk:money = 0.0;
           ALG_GetRATE(pDEAL.rec.ID, Днач, @Rk);

           var Dk:integer = Доконч - Днач + 1;

           var cmd = RsdCommand(RslDefCon, "begin\n ? := RSB_Derivatives.DV_DaysInYearByBasis(?, ?);\n end; ");
           cmd.addParam("ret_val", RSDBP_RETVAL, V_INTEGER );
           cmd.addParam("basis",   RSDBP_IN,     pFI_1.rec.Basis );
           cmd.addParam("d",       RSDBP_IN,     Днач );
           cmd.execute();

           F0 = F0 + (Rk / 100.0 * Dk / SQL_ConvTypeSum(cmd.Value(0)));

           Днач = Доконч + 1;
        end;
        
        F = S + (S * F0);
     end;

     if( pFrVal.rec.ID == -100 ) /* используется в качестве флага */
        pFrVal.rec.FairValue = F;
     else
        if( ПолучитьКурсЗаданногоTипа(@Sвба, pFI_1.rec.FIID, NATCUR, DV_RATETYPE_CBR, pFrVal.rec.Date, true ) == false )
           MsgBox("Невозможно получить курс вида 'ЦБ РФ' для FIID = " + String(pFI_1.rec.FIID) + " на дату " + String(pFrVal.rec.Date));
           return 1;
        end;

        if( ПолучитьКурсЗаданногоTипа(@Sвр, AccFIID, NATCUR, DV_RATETYPE_CBR, pFrVal.rec.Date, true ) == false )
           MsgBox("Невозможно получить курс вида 'ЦБ РФ' для FIID = " + String(AccFIID) + " на дату " + String(pFrVal.rec.Date));
           return 1;
        end;

        S1 = Sвба;
        S2 = F * Sвр;

        if( pDEAL.rec.Type == ALG_DV_BUY )
           pFrVal.rec.FairValue = money(round((S1 - S2) * pFI_1.rec.Amount, 2));
        else
           pFrVal.rec.FairValue = money(round((S2 - S1) * pFI_1.rec.Amount, 2));
        end;

        pFrVal.rec.SetDemand = pFrVal.rec.SetLiability = pFrVal.rec.SetDemand2 = pFrVal.rec.SetLiability2 = UNSET_CHAR;
     end;

     return 0; /*рассчитали значение без ошибок*/
  end;

  return 1; /*по умолчанию ошибка*/
END;
