//DerivativeCommission.mac
import oralib, likepy, BankInter, DVInter, SFInter, CTInter, "dlquery.mac";

PRIVATE CONST SET_CHAR   = "X";
PRIVATE CONST UNSET_CHAR = "";
PRIVATE CONST TP_INDIVIDUAL = 0; //индивидуальный тарифный план

class DerivativeCommission(_clientContr:integer, _commissDate:date)
  private var clientContr:integer;
  private var commissDate:date;

  private macro Init(_clientContr:integer, _commissDate:date)
     clientContr = IfThenElse(_clientContr == null, 0, _clientContr);
     commissDate = _commissDate;
  end;

  macro RG_GetComSumNDS(TypeNDS:integer,ComSum:money,p_nds_rate:@double)
    var nds_rate:double = 0;

     if( (TypeNDS == SF_ADD_TAX) OR (TypeNDS == SF_NOT_ADD_TAX) )
       GetNDSRateByDate(nds_rate);
       if(p_nds_rate!=null)
          p_nds_rate = nds_rate;
       end;
       return money(ComSum*(nds_rate)/(100+nds_rate));     
     end;
     return $0;
  end;

  /*проинитить структуру комиссий
    Code - код комиссии , по котрой получаем номер комиссии
  */
  macro InitDlComis( Code:string, rDLComis, ErrMsg:@string, pFeeType:integer, ComSum:money, pFIID_Comm:integer, IsPrimaryPlacement:bool )
    var query, cmd, DataSet, cmd1, DataSet1, cmdTP, DataSetTP, TypeNDS = -1;
    var TPlanID:integer = 0;
    var IsPrimaryPlacementComBank:integer = 0;

    ErrMsg = "";

    if(IsPrimaryPlacement == null)
       IsPrimaryPlacement = false;
    end;

    rDLComis.Clear();

    rDLComis.rec.ID       = 0;
    rDLComis.rec.DOCKIND  = 0;/*заполнение в сишнике*/
    rDLComis.rec.DOCID    = 0;/*заполнение в сишнике*/   
    
    rDLComis.rec.CONTRACT = 0;/*заполнение в сишнике - заполняем основной договор по сделке или ДО Банка с получателем комиссии (биржей) в зависимости от значения ISBANKEXPENSES*/
    rDLComis.rec.CLIENTCONTRID = 0;/*заполнение в сишнике - заполняем основной договор по сделке*/

    cmd = DL_RSDCommand();

    query = " select COMISS.* "+
            "   from DSFCOMISS_DBT COMISS "+
            "  where LOWER(COMISS.t_Code) = LOWER('"+Code+"')";

    if( pFeeType != NULL )
       query = query + " and COMISS.t_FeeType = ? ";
       cmd.addParam(pFeeType);
    end;

    if( pFIID_Comm != NULL )
       query = query + " and COMISS.t_FIID_Comm = ? ";
       cmd.addParam(pFIID_Comm);
    end;

    DataSet = cmd.execute(query);

    if( DataSet.MoveNext() )
       if(clientContr > 0)
          //Поиск тарифного плана
          cmdTP = DL_RSDCommand(); 
          query = " select plan.T_SFPLANID "+
                  "   from DSFCONTRPLAN_DBT plan "+
                  "  where plan.T_SFCONTRID  = ? "+
                  "    and plan.T_BEGIN    <= ? "+
                  "    and (plan.T_END = to_date('01.01.0001','DD.MM.YYYY') or plan.T_END >= ?) ";
          cmdTP.addParam(clientContr);
          cmdTP.addParam(commissDate);
          cmdTP.addParam(commissDate);
          DataSetTP = cmdTP.execute(query);
          if(DataSetTP.MoveNext())
             TPlanID = DataSetTP.rec.SFPLANID;
          else
             TPlanID = TP_INDIVIDUAL;
          end;

          //ZMV BOSS-7162 По сделкам первичного размещения признак "отнесение на расходы банка" на комиссии может быть изменен в зависимости от значения категории "Отнесение комиссии биржи на расходы банка по сделкам размещения"
          if(IsPrimaryPlacement and (TPlanID != TP_INDIVIDUAL) and GetMainObjAttr(null, OBJTYPE_SFPLAN, String(TPlanID:o:10), 103, IsPrimaryPlacementComBank, null, null, commissDate)) 
             if(IsPrimaryPlacementComBank == 1/*Да*/)
                rDLComis.rec.ISBANKEXPENSES = SET_CHAR;
             elif(IsPrimaryPlacementComBank == 2/*Нет*/)
                rDLComis.rec.ISBANKEXPENSES = UNSET_CHAR;
             end;
          else  
             //Поиск признака ОРБ
             cmd1 = DL_RSDCommand();
             query = " select concom.t_IsBankExpenses "+
                     "   from DSFCONCOM_DBT concom "+
                     "  where concom.t_ObjectId   = ? "+
                     "    and concom.t_ObjectType = ? "+
                     "    and concom.t_FeeType    = ? "+
                     "    and concom.T_COMMNUMBER = ? "+
                     "    and concom.T_DATEBEGIN <= ? "+
                     "    and (concom.T_DATEEND = to_date('01.01.0001','DD.MM.YYYY') or concom.T_DATEEND >= ?) ";
             cmd1.addParam(IfThenElse(TPlanID == TP_INDIVIDUAL, clientContr, TPlanID));
             cmd1.addParam(IfThenElse(TPlanID == TP_INDIVIDUAL, OBJTYPE_SFCONTR, OBJTYPE_SFPLAN));
             cmd1.addParam(DataSet.FeeType);
             cmd1.addParam(DataSet.Number);
             cmd1.addParam(commissDate);
             cmd1.addParam(commissDate);
       
             DataSet1 = cmd1.execute(query);
       
             if(DataSet1.MoveNext())
                rDLComis.rec.ISBANKEXPENSES = DataSet1.rec.ISBANKEXPENSES;
             else
                rDLComis.rec.ISBANKEXPENSES = UNSET_CHAR;
             end;
          end;
       else
          rDLComis.rec.ISBANKEXPENSES = UNSET_CHAR;
       end;

       rDLComis.rec.FEETYPE   = DataSet.FeeType;
       rDLComis.rec.COMNUMBER = DataSet.Number;
       TypeNDS                = int(DataSet.PayNDS);
       /*проверить наличие получателя: если получатель не задан - не сможем сформировать пл реквизиты и ТО по комиссии (при этом конкретная ошибка выдана не будет)*/
       if( DataSet.ReceiverID <= 0 )
          ErrMsg = "В анкете комиссии с кодом \""+Code+"\" не задан получатель комиссии. Необходимо указать получателя комиссии.";
          return false;
       end;
    else
       ErrMsg = "Не найдена анкета комиссии с кодом \""+Code+"\".";
       return false;
    end;

    rDLComis.rec.SUM         = $0;          
    rDLComis.rec.NDS         = $0;     

    if( (ComSum!= null) and (ComSum > $0.0) )
       rDLComis.rec.SUM = ComSum;
       rDLComis.rec.NDS = RG_GetComSumNDS(TypeNDS,ComSum);     
    end;

    rDLComis.rec.DATE        =     
    rDLComis.rec.PLANPAYDATE = 
    rDLComis.rec.FACTPAYDATE = Date(0,0,0);
    rDLComis.rec.INPUTMEDIUM = "И";
    rDLComis.rec.ServOperID  = 0;
    rDLComis.rec.Immaterial  = /*существенность определим в сишнике*/
    rDLComis.rec.IsBack      = UNSET_CHAR;

    return true;
  end;

  macro GetIDByCode(code:string):integer
    var query, cmd, DataSet;
    var ComissID = 0;

    cmd = DL_RSDCommand();                                                                                              
    
    query = " select COMISS.* "+
            "   from DSFCOMISS_DBT COMISS "+
            "  where COMISS.t_Code = ? "+
            "    and COMISS.t_FeeType = ? ";

    cmd.addParam(code);
    cmd.addParam(SF_FEE_TYPE_PERIOD);

    DataSet = cmd.execute( query );

    if( DataSet.MoveNext() )
       ComissID = DataSet.ComissID;
    end;

    return ComissID;
  end;

  macro InitDvDlCom(code:string, ComSum:money, rDvDlCom, ErrMsg:@string):bool
    var query, cmd, DataSet;
    var dlcomis_market = TRecHandler("dlcomis.dbt");

    rDvDlCom.Clear();

    rDvDlCom.rec.ID     = 0;
    rDvDlCom.rec.DealID = 0;/*заполнение в сишнике*/
    rDvDlCom.rec.SUM    = $0;          
    rDvDlCom.rec.NDS    = $0;

    rDvDlCom.rec.ComissID = GetIDByCode(code);
    if (rDvDlCom.rec.ComissID == 0) 
      ErrMsg = "Не найдена анкета комиссии с кодом \""+Code+"\".";
      return false;
    end;

    if( not InitDlComis(Code, dlcomis_market, @ErrMsg, SF_FEE_TYPE_PERIOD, ComSum, null) )
      return false;
    end;

    rDvDlCom.rec.SUM  = dlcomis_market.rec.SUM;
    rDvDlCom.rec.NDS = dlcomis_market.rec.NDS;
    rDvDlCom.rec.isBankExpenses = dlcomis_market.rec.isBankExpenses;

    return true;
  end;

  macro InitDvDlComMust( code:string, ComSum:money, rDvDlCom)
    var ErrMsg:string = "";

    if (not InitDvDlCom(code, ComSum, rDvDlCom, @ErrMsg)) 
      RunError( "", ErrMsg);
    end;
  end;

  Init(_clientContr, _commissDate)
end;