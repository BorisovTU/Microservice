/*
$Name:        dlsettl090.mac
$Module:      Ядро Securities
$Description: Операция "Расчеты с биржей", Шаг Вывод средств с торгового счета на корсчет
*/

import InsCarryDoc, "dv_categ.mac", "dv_car.mac", "dv_carop.mac";

PRIVATE VAR Dl_ValueData = TRecHandler( "dl_value.dbt");

MACRO ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step )

  record comm( dl_comm );
  var    FD; 

  SetBuff( comm, FirstDoc );
  FD = DVFirstDocDLCOMM( comm );

  var OurAcc = TRecHandler("account"),
      CorrAcc = TRecHandler("account"),
      FI = TRecHandler("fininstr.dbt"), 
      PaymentObj:RsbPayment;

  if( ПолучитьФинИн( Dl_ValueData.rec.SumFIID, FI ) != 0 )
     MsgBox("Не найден финансовый инструмент FIID = " + String(Dl_ValueData.rec.SumFIID));
     return 1;
  end;

  if( FI.rec.FI_KIND == FIKIND_METAL )
      if( not FD.IsExistAccount("КлиринговыеСчетаДМ", null, true, null, OurAcc, null, Dl_ValueData.rec.Date, Dl_ValueData.rec.SumFIID) )
          MsgBox("Ошибка: не открыт счет категории \"КлиринговыеСчетаДМ\" в валюте FIID = " + String(Dl_ValueData.rec.SumFIID));
          return 1;
      end;
      if( not FD.IsExistAccount("Клиринговый счетA", null, true, null, CorrAcc, null, Dl_ValueData.rec.Date, Dl_ValueData.rec.SumFIID) )
          MsgBox("Ошибка: не открыт счет категории \"Клиринговый счетA\" в валюте FIID = " + String(Dl_ValueData.rec.SumFIID));
          return 1;
      end;
  else
      if( not FD.IsExistAccount("Торговый счет", null, true, null, OurAcc, null, Dl_ValueData.rec.Date, Dl_ValueData.rec.SumFIID) )
          MsgBox("Ошибка: не открыт счет категории \"Торговый счет\" в валюте FIID = " + String(Dl_ValueData.rec.SumFIID));
          return 1;
      end;
  end;

  if( DVND_CreatePayment( FD, PM_PURP_REPAYMENT, FD.Kind, FD.ID,
                          Dl_ValueData.rec.Date,
                          Comm.PartyID, -1,
                          {ourbank}, {ourbank},
                          Dl_ValueData.rec.Sum, Dl_ValueData.rec.SumFIID,
                          "Вывод средств с торгового счета" + IIF(GetCodeParty("АО СПВБ",PTCK_CONTR) == FD.comm.rec.PartyID, " " + GetCodeParty(FD.comm.rec.PartyID,PTCK_CONTR), "") + " на корсчет",
                          IIF(( FI.rec.FI_KIND == FIKIND_METAL ), CorrAcc, NULL), OurAcc,
                          comm.Division, NULL, NULL,
                          @PaymentObj
                        ) != 0 )
     return 1;
  end;

  if( not(ПИ_ИнтегрРежимРаботы() and ПИ_ЭтоВнешнийВходящийПлатеж(PaymentObj)) or (FI.rec.FI_Kind == FIKIND_METAL) )
     if( not ПИ_ПроводкаПоПлатежу(FD, PaymentObj) )
        return 1;
     end;
   
     if( ПИ_ИсполнитьПлатеж(PaymentObj) )
        return 1;
     end;
  end;

  if( FD.IsClient() and ПИ_ВестиВнутреннийУчет() and
      ((ВУ_ПроводкиВводаВыводаДСНаОРЦБ() == 1) or (ВУ_ПроводкиВводаВыводаДСНаОРЦБ() == 3))
    )
     if( not ПИ_ВУ_РасчетыВыводСКлирСчетаКлиента(FD, Doc, Dl_ValueData.rec.SumFIID) )
        return 1;
     end;
  end;

  return 0;
end;
