/*
$Name:        dvop030.mac
$Module:      Производные инструменты
$Description: Операция расчетов. Шаг "Корректировка внебалансовых счетов"
*/
import InsCarryDoc, "dvserv.mac", "dv_fun.mac", "dv_carop.mac", "dv_comiss.mac";
private var FI = TRecHandler( "fininstr.dbt");

macro ExecuteStep(doc, FDoc, DocKind, ID_Operation, ID_Step)

   record oper(dvoper);
   SetBuff( oper, FDoc );

   var FD;
   var Query:string = "", Data:variant;
   var cmd, DataSet;
   var Count:integer = 0;
   var NN:money = $0, ON:money = $0;
   var NS:money = $0, OS:money = $0;
   var /*Ki:money = $0,*/ DeltaKi:money = $0;
   var AddWhere:string = "";
   accDvdealTrnArr.size = 0;

   if( УчетБиржевыхКонтрактов() == DV_ACCEXCONTR_DEAL )
      Query = " SELECT DEAL.t_ID, TURN.t_Execution, TURN.t_DealCost " +
              "   FROM ddvdlturn_dbt TURN, ddvdeal_dbt DEAL " +
              "  WHERE " + DLTurnByOperationDate(oper) +
              "    AND DEAL.t_Type in('B', 'S', 'D', 'G') " +
              "    AND DEAL.t_Client = -1 ";

      Data = TRsbDataSet(Query);
      if( Data )
         Count = 0;
         InitProgress( SQL_GetNRecs(Query), "Выполнение проводок корректировки внебалансовых счетов", "Выполнение..." );
         while( Data.MoveNext() )

            FD = DVFirstDocDeal( DL_DVDEAL, SQL_ConvTypeInteger(Data.ID) );

               cmd = RsdCommand( " SELECT TURN.t_Execution, TURN.t_DealCost " +
                                 "   FROM ddvdlturn_dbt TURN " +
                                 "  WHERE TURN.t_DealID = " + string(Data.ID) +
                                 "    AND TURN.t_Date < " + GetSQLDate(oper.Date) +
                                 " ORDER BY TURN.T_DATE DESC " );
               cmd.execute();
               DataSet = TRsbDataSet(cmd);
               if( DataSet.MoveNext() )
                  //Ki      = SQL_ConvTypeSum(DataSet.Execution);
                  DeltaKi = SQL_ConvTypeSum(Data.Execution) - SQL_ConvTypeSum(DataSet.Execution);
               else
                  //Ki      = 0;
                  DeltaKi = SQL_ConvTypeSum(Data.Execution);
               end;

               /* Сделка полностью исполнена */
               if( FD.Deal.rec.Amount == FD.Deal.rec.Execution )
                  if( ВыполнитьПроводкиПоСнятиюСВнебаланса( doc, oper, FD ) == false )
                     RemProgress();
                     return 1;
                  end;
               end;

               /* Сделка заключена в текущую дату */
               if( FD.Deal.rec.Date_CLR == oper.Date )
                  if( ВыполнитьПроводкиПоПостановкеНаВнебаланс( doc, oper, FD, FD.Deal.rec.Amount, FD.Deal.rec.PositionCost ) == false )
                     RemProgress();
                     return 1;
                  end;
               end;

               /* Часть контрактов по сделке исполнено */
               if( (FD.Deal.rec.Amount != FD.Deal.rec.Execution) and (DeltaKi > 0) )
                  if( ВыполнитьПроводкиПоКорректировкеВнебаланса( doc, oper, FD,
                                                                  FD.Deal.rec.Amount - SQL_ConvTypeSum(DataSet.Execution),
                                                                  FD.Deal.rec.Amount - SQL_ConvTypeSum(Data.Execution),
                                                                  SQL_ConvTypeSum(DataSet.DealCost),
                                                                  SQL_ConvTypeSum(Data.DealCost) ) == false )
                     RemProgress();
                     return 1;
                  end;
               end;

            UseProgress(Count = Count + 1);
         end;
         RemProgress();
      end;

      if( oper.Flag1 == ALG_SP_MONEY_SOURCE_TRUST )
         AddWhere = " DEAL.t_IsTrust = chr(88) ";
      else
         AddWhere = " DEAL.t_Client " + IIF(oper.Flag1 == ALG_SP_MONEY_SOURCE_OWN, "<= 0 ", "> 0 ");
      end;

      /*закрытие без исполнения*/
      Query = " SELECT DEAL.t_ID, TURN.t_Execution, TURN.t_DealCost " +
              "   FROM ddvdlturn_dbt TURN, ddvdeal_dbt DEAL " +
              "  WHERE DEAL.t_IsTrust = RSB_Derivatives.DV_Setting_RunFromTrust " +
              "    AND DEAL.t_Department = " + string(oper.Department) + " " +
              "    AND DEAL.t_ID = TURN.t_DealID " +
              "    AND DEAL.T_FIID IN (SELECT FI.t_FIID FROM dfininstr_dbt FI WHERE fi.t_Avoirkind = 2 AND fi.t_drawingdate = " + GetSQLDate(oper.Date) + ")" +
              "    AND DEAL.t_Type in('B', 'S', 'D', 'G') " +
              "    AND " + AddWhere +
              "    AND DEAL.t_Client = -1 ";

      Data = TRsbDataSet(Query);
      if( Data )
         Count = 0;
         InitProgress( SQL_GetNRecs(Query), "Выполнение проводок корректировки внебалансовых счетов", "Выполнение..." );
         while( Data.MoveNext() )

            FD = DVFirstDocDeal( DL_DVDEAL, SQL_ConvTypeInteger(Data.ID) );

               cmd = RsdCommand( " SELECT TURN.t_Execution, TURN.t_DealCost " +
                                 "   FROM ddvdlturn_dbt TURN " +
                                 "  WHERE TURN.t_DealID = " + string(Data.ID) +
                                 "    AND TURN.t_Date < " + GetSQLDate(oper.Date) +
                                 " ORDER BY TURN.T_DATE DESC " );
               cmd.execute();
               DataSet = TRsbDataSet(cmd);
               if( DataSet.MoveNext() )
                  if( ВыполнитьПроводкиПоСнятиюСВнебаланса( doc, oper, FD ) == false )
                     RemProgress();
                     return 1;
                  end;
               end;

            UseProgress(Count = Count + 1);
         end;
         RemProgress();
      end;

   else
      /*Для всех позиций DDVFIPOS, таких что PositionByOperationDate (DDVFIPOS) и T_CLIENT не задан  
        и ПИ - опцион или поставочный фьючерс,  выполнить проводки по корректировке внебалансовых счетов. 
        Счета актуализируются по документу "Позиция по ПИ". Проводки на нулевую сумму не выполняются.*/

      Query = " SELECT POS.* " +
              "   FROM ddvfipos_dbt POS " +
              "  WHERE " + PositionByOperationDate(oper) +
              "    AND POS.t_Client = -1 ";
     
      Data = TRsbDataSet(Query);
      if( Data )
         Count = 0;
         InitProgress( SQL_GetNRecs(Query), "Выполнение проводок корректировки внебалансовых счетов", "Выполнение..." );
         while( Data.MoveNext() )

            FD = DVFirstDocPos( DL_DVFIPOS, SQL_ConvTypeInteger(Data.ID) );

               NN = НоваяНеттоПозиция( FD.DVPos.rec, oper.Date, @NS );
               ON = СтараяНеттоПозиция( FD.DVPos.rec, oper.Date, @OS );

               /* Если старая нетто-позиция ON != 0 и "  Новая нетто-позиция NN равна 0 или "  Старая ON и новая NN нетто-позиции имеют разные знаки */
               if( ( (ON != $0) AND (NN == $0) ) OR
                   ( (ON != $0) AND (NN != $0) AND (ON/NN < 0) )
                 )
                  if( ВыполнитьПроводкиПоСнятиюСВнебаланса( doc, oper, FD ) == false )
                     RemProgress();
                     return 1;
                  end;
               end;

               /* Если новая нетто-позиция NN не равна 0 и Старая нетто-позиция ON равна 0 или */
               /* Старая ON и новая NN нетто-позиции имеют разные знаки то выполнить проводки по */
               /* постановке на внебаланс. Счета актуализировать по указанным ро-лям.            */
               if( ( (NN != $0) AND (ON == $0)) OR
                   ( (ON != $0) AND (NN != $0) AND (ON/NN < 0) )
                 )
                   if( ВыполнитьПроводкиПоПостановкеНаВнебаланс( doc, oper, FD, NN, NS ) == false )
                      RemProgress();
                      return 1;
                   end;
               end;

               /* Если и новая NN и старая ON нетто-позиции не равны 0 и  их знак совпадает,     */
               /* а величина -нет, выполнить проводки по корректировке счетов на внебалансе. Счета */
               /* актуализировать по указан-ным ролям без открытия                                 */
               if( (NN != $0) AND (ON != $0) AND (ON != NN ) AND (ON/NN > 0) ) 
                  if( ВыполнитьПроводкиПоКорректировкеВнебаланса( doc, oper, FD, ON, NN, OS, NS ) == false )
                     RemProgress();
                     return 1;
                  end;
               end;

            UseProgress(Count = Count + 1);
         end;
         RemProgress();
      end;

      if( oper.Flag1 == ALG_SP_MONEY_SOURCE_TRUST )
         AddWhere = " POS.t_IsTrust = chr(88) ";
      else
         AddWhere = " POS.t_Client " + IIF(oper.Flag1 == ALG_SP_MONEY_SOURCE_OWN, "<= 0 ", "> 0 ");
      end;

      /*закрытие без исполнения*/
      Query = " SELECT POS.* " +
              "   FROM ddvfipos_dbt POS " +
              "  WHERE POS.T_IsTrust = RSB_Derivatives.DV_Setting_RunFromTrust " + 
              "    AND POS.t_Department = " + string(Oper.Department) +
              "    AND POS.T_FIID IN (SELECT FI.t_FIID FROM dfininstr_dbt FI WHERE fi.t_Avoirkind = 2 AND fi.t_drawingdate = " + GetSQLDate(oper.Date) + ")" +
              "    AND " + GetSQLDate(oper.Date) + " IN( SELECT TURN.T_DATE " +
              "                                            FROM ddvfiturn_dbt TURN " +
              "                                           WHERE TURN.T_DEPARTMENT = POS.T_DEPARTMENT " +
              "                                             AND TURN.T_FIID = POS.T_FIID " +
              "                                             AND TURN.T_BROKER = POS.T_BROKER " +
              "                                             AND TURN.T_CLIENTCONTR = POS.T_CLIENTCONTR " +
              "                                             AND TURN.t_GenAgrID = POS.t_GenAgrID ) " +
              "    AND " + AddWhere +
              "    AND POS.t_Client = -1 ";

      Data = TRsbDataSet(Query);
      if( Data )
         Count = 0;
         InitProgress( SQL_GetNRecs(Query), "Выполнение проводок закрытие без исполнения внебалансовых счетов", "Выполнение..." );
         while( Data.MoveNext() )

            FD = DVFirstDocPos( DL_DVFIPOS, SQL_ConvTypeInteger(Data.ID) );

               /* Если старая нетто-позиция ON != 0 и "  Новая нетто-позиция NN равна 0 или "  Старая ON и новая NN нетто-позиции имеют разные знаки */
               if( ВыполнитьПроводкиПоСнятиюСВнебаланса( doc, oper, FD ) == false )
                  RemProgress();
                  return 1;
               end;

            UseProgress(Count = Count + 1);
         end;
         RemProgress();
      end;
   end;

   if ( not InsertDvdealTrnLink(ID_Operation, ID_Step) )
     MsgBox("Ошибка при сохранении информации о связи проводок со сделками срочного рынка");
     return 1;
   end;

   return 0;
end;
