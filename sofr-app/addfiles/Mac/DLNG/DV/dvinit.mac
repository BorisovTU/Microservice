/*
$Name:        dvinit.mac
$Module:      Производные инструменты
$Description: Макрос инициализации операций конверсионных сделок
PNV 17.10.19 перенесена верификация с соответствующего шага
*/

IMPORT rsd;
IMPORT OprInter, DVInter, "dl_grfun.mac", "dv_grfun.mac";

record deal     (dvndeal);
record panel    (reentry2, "mbk_oper.lbr" ) dialog;
record errpanel (err2    , "mbk_oper.lbr" ) dialog;
record panel2    (reentry3, "mbk_oper.lbr" ) dialog;
record errpanel2 (err3    , "mbk_oper.lbr" ) dialog;
record panel2_1    (reentr31, "mbk_oper.lbr" ) dialog;
record errpanel2_1 (err3_1, "mbk_oper.lbr" ) dialog;

VAR _code = 0, _dealId = 0, _dealtype = 0; 
VAR Deal_main, Deal_dlg;
VAR Deal_buy, Deal_sale, Deal_buy_dlg, Deal_sale_dlg;

CLASS cDeal (id, type)
      var dealId, dealtype, dealDate, dealWay;
      var summ_BA, cur_BA, date_BA, price;
      var /*summ_K*/,  cur_K,  date_K;

      MACRO InitFields ()
            this.dealId   = 0;
            this.dealtype = -1;
            this.dealDate = date(0,0,0);
            this.dealWay  = "";
            this.summ_BA  = 0;
            this.cur_BA   = "";
            this.price    = 0;
            this.date_BA  = date(0,0,0);
            this.cur_K    = "";
            this.date_K   = date(0,0,0);
      END;

      MACRO getDealInfo (id, type)
            var sql, rs;
            sql = ( " select dv.t_date,                                                       "+
                    "        decode (dv.t_type, 1, 'Покупка', 2, 'Продажа',                   "+
                    "                           5, 'Покупка/продажа',6, 'Продажа/покупка',    "+
                    "                           chr(0)) as deal_way,                          "+
                    "        vn.t_amount as summ_BA,                                          "+
                    "        (select fi.t_iso_number                                          "+
                    "           from dfininstr_dbt fi                                         "+
                    "          where fi.t_fiid = vn.t_fiid                                    "+
                    "            and (fi.t_fi_kind = 1 or  fi.t_fi_kind = 6 )                 "+
                    "            and fi.t_isclosed = chr(0)) as cur_BA,                       "+
                    "        vn.t_supldate as date_BA,                                        "+
                    "        vn.t_cost as summ_K,                                             "+
                    "        (select fi.t_iso_number                                          "+
                    "           from dfininstr_dbt fi                                         "+
                    "          where fi.t_fiid = vn.t_pricefiid                               "+
                    "            and (fi.t_fi_kind = 1 or  fi.t_fi_kind = 6 )                 "+
                    "            and fi.t_isclosed = chr(0)) as cur_K,                        "+
                    "        vn.t_paydate as date_K,                                          "+
                    "        round(vn.t_price,vn.t_point)  t_price                            "+
                    "   from DDVNDEAL_DBT dv                                                  "+
                    "   join ddvnfi_dbt vn                                                    "+
                    "     on vn.t_dealid = dv.t_id                                            "+
                    "    and vn.t_type = :1                                                   "+
                    "  where dv.t_id = :2 ");
      
            sql = rsdcommand (sql);
            sql.addParam(":1", RSDBP_IN, type);
            sql.addParam(":2", RSDBP_IN, id);
            rs  = rsdrecordset (sql);

            return rs;
            OnError(er)
            MsgBox (er.Message,"| Модуль: ",er.Module,"| строка: ",er.Line );
            return false;
      END;

      MACRO loadDealInfo (id, type)
            private var m_rs = getDealInfo (id, type);  
              
            if (m_rs.movenext () )
                 this.dealId   = id;
                 this.dealtype = type;
                 this.dealDate = m_rs.value("t_date");
                 this.dealWay  = m_rs.value("deal_way");
                 this.summ_BA  = m_rs.value("summ_BA");
                 this.cur_BA   = m_rs.value("cur_BA");
                 this.price    = m_rs.value("t_price");
                 this.date_BA  = m_rs.value("date_BA");
                 this.cur_K    = m_rs.value("cur_K");
                 this.date_K   = m_rs.value("date_K");
            else
                 InitFields();   
            end;
      END;

      if (id != null )
         loadDealInfo (id, type);
      else
         InitFields();    
      end;   
END;

Macro GetConversFromDeal (id_deal) //Вывод примечания 300 на панель верификации сделки
	var sql_conv,  rs_conv, conv;
	
	  sql_conv = "select nvl(rsb_struct.getstring (t_text),' ') text from  dnotetext_dbt "+
		     "where t_DOCUMENTID = LPAD (?, 34, 0) and t_notekind=300";
	  sql_conv = rsdcommand (sql_conv);
          sql_conv.addParam(":1", RSDBP_IN, id_deal);
          rs_conv  = rsdrecordset (sql_conv);
            if (rs_conv.movenext () )
                 conv = rs_conv.value("text");
            end;
	Return nvl(conv,"");
End;

MACRO PanelErr (dlg, cmd, id, key ) 
   if (cmd == DLG_INIT)

      dlg.date1   = Deal_dlg.dealDate;  
      dlg.way     = Deal_dlg.dealWay;    
      dlg.summ_BA = Deal_dlg.summ_BA;
      dlg.cur_BA  = Deal_dlg.cur_BA; 
      dlg.price   = Deal_dlg.price;
      dlg.date_BA = Deal_dlg.date_BA;
      dlg.cur_K   = Deal_dlg.cur_K;  
      dlg.date_K  = Deal_dlg.date_K; 
   
      dlg.xDate1   = Deal_main.dealDate;   
      dlg.xWay     = Deal_main.dealWay;
      dlg.xSumm_BA = Deal_main.summ_BA; 
      dlg.xCur_BA  = Deal_main.cur_BA; 

      dlg.xPrice   = Deal_main.price;    
      dlg.xDate_BA = Deal_main.date_BA;   
      dlg.xCur_K   = Deal_main.cur_K;
      dlg.xDate_K  = Deal_main.date_K;
      
      updateFields (dlg);
   
   end;
   if ( cmd == DLG_KEY )
      if ( key == 27 ) 
         return CM_CANCEL;
      else
         return CM_IGNORE;
      end;
   end;
END;

MACRO PanelErr2 (dlg, cmd, id, key ) 
   if (cmd == DLG_INIT)
      //покупка
      //--введено
      dlg.date1   = Deal_buy_dlg.dealDate;  
      dlg.way     = Deal_buy_dlg.dealWay;    
      dlg.summ_BA = Deal_buy_dlg.summ_BA;
      dlg.cur_BA  = Deal_buy_dlg.cur_BA; 
      dlg.price   = Deal_buy_dlg.price; 
      dlg.date_BA = Deal_buy_dlg.date_BA;
      dlg.cur_K   = Deal_buy_dlg.cur_K;  
      dlg.date_K  = Deal_buy_dlg.date_K; 
      //--в сделке
      dlg.xDate1   = Deal_buy.dealDate;   
      dlg.xWay     = Deal_buy.dealWay;
      dlg.xSumm_BA = Deal_buy.summ_BA; 
      dlg.xCur_BA  = Deal_buy.cur_BA; 
      dlg.xPrice   = Deal_buy.price;   
      dlg.xDate_BA = Deal_buy.date_BA;   
      dlg.xCur_K   = Deal_buy.cur_K;
      dlg.xDate_K  = Deal_buy.date_K;
      //продажа
      //--введено
      dlg.sell_summ_BA = Deal_sale_dlg.summ_BA;
      dlg.sell_cur_BA  = Deal_sale_dlg.cur_BA; 
      dlg.sell_price   = Deal_sale_dlg.price; 
      dlg.sell_date_BA = Deal_sale_dlg.date_BA;
      dlg.sell_cur_K   = Deal_sale_dlg.cur_K;  
      dlg.sell_date_K  = Deal_sale_dlg.date_K; 
      //--в сделке
      dlg.xSell_Summ_BA = Deal_sale.summ_BA; 
      dlg.xSell_Cur_BA  = Deal_sale.cur_BA;   
      dlg.xSell_price   = Deal_sale.price;    
      dlg.xSell_Date_BA = Deal_sale.date_BA;
      dlg.xSell_Cur_K   = Deal_sale.cur_K;
      dlg.xSell_Date_K  = Deal_sale.date_K;
      updateFields (dlg);   
   end;
   
   if ( cmd == DLG_KEY )
      if ( key == 27 ) 
         return CM_CANCEL;
      else
         return CM_IGNORE;
      end;
   end;
END;

MACRO ScrollWAY ()
   var res;
   var way = Tarray ();
   way [0] = "Покупка";   
   way [1] = "Продажа";    
   way [2] = "Покупка/продажа";
   way [3] = "Продажа/покупка";
   
   var tway = Menu(way, "Выберите направление сделки", "Выберите направление сделки", 27, 11);
   if (tway >= 0)
      res = way[tway];
   else
      res = "Нет";
   end;
   return res;
END;

MACRO AddCol (ar,ind, fld, head, width, fldType,decPoint)
   ar.value (ind * 6)      = fld;
   ar.value (ind * 6 + 1)  = head;
   ar.value (ind * 6 + 2)  = width;
   ar.value (ind * 6 + 3 ) = fldType; 
   ar.value (ind * 6 + 4 ) = decPoint;
   ar.value (ind * 6 + 5 ) = 0; 
END;

PRIVATE MACRO ЧислоСЗаданнойТочностью( _var, _point:integer, _parm:string ) 
    var TmpStr:string;
    if( (valtype (_var) == V_DOUBLE) OR (valtype (_var) == V_MONEY) )
      if((valtype (_parm) == V_UNDEF) OR (_parm == ""))
        TmpStr = "String(_var:0" + ":" + string(_point) + ")";
      else
        TmpStr = "String(_var:0" + ":" + string(_point) + ":" + _parm + ")";
      end;
      return ExecExp(TmpStr);
    else
      return String(_var);
    end;
END;

MACRO ScrollFIID (head, xo, yo, zo)
    var cur_code = "", xsel, sel, col;
      
    MACRO EvList0 (xsel, cmd, id, key)
         if(cmd == DLG_KEY)
            if(key == 13) 
               cur_code = xsel.value ("t_iso_number");
               return CM_CANCEL;
            elif (key == 27) 
               cur_code = "";
               return false;
            elif ((key == 322) or (key == 323))
               return CM_IGNORE;
            end;
         end;
    END;
      
    sel = ("select t.t_iso_number, t.t_ccy, t.t_name from dfininstr_dbt t where t.t_isclosed = chr(0) and  ( t.t_fi_kind = 1  or  t.t_fi_kind = 6 ) ");
    sel = RSDCommand (sel);
      
    xsel = RSDRecordset(sel, RSDVAL_CLIENT, RSDVAL_STATIC );
     
    col = TArray;
      
    AddCol (col , 0, "t_iso_number", "ISO Код",      8,  2, null);
    AddCol (col , 1, "t_ccy",        "Код",          8,  2, null);
    AddCol (col , 2, "t_name",       "Наименование", 10, 2, null);
    if (RunScroll (xsel, 3, col, null, @EvList0, head, "ENTER-Выбор ESC-Отмена", false, xo, yo, 40, zo))
        return cur_code;
    end;
    return cur_code;
END;

MACRO compareDeals (deal1, deal2)
    if ((deal1.dealDate != deal2.dealDate) or
        (deal1.dealWay  != deal2.dealWay) or
        (deal1.summ_BA  != deal2.summ_BA  ) or
        (deal1.cur_BA   != deal2.cur_BA) or
        (Round(deal1.price,9) != Round(deal2.price,9) )or/*i-support 487442 PNV*/ /*SVE доработана работа с курсом с точностью до 9 знаков, поэтому сменил 8 на 9*/
        (deal1.date_BA  != deal2.date_BA)  or
        (deal1.cur_K    != deal2.cur_K ) or
        (deal1.date_K   != deal2.date_K ) 
        ) 
        return false;
    else 
        return true;
    end;
END;

MACRO mainPanel2 (dlg, cmd, id, key ) 
      
      if (cmd == DLG_INIT)
          if (_dealtype == 5) //покупка-продажа
            Deal_buy = cDeal (_dealId, 0); //0-своп-покупка
            Deal_sale = cDeal (_dealId, 2); //2-своп-продажа
          else //6 - продажа/покупка
            Deal_buy = cDeal (_dealId, 2); //2-своп-покупка
            Deal_sale = cDeal (_dealId, 0); //0-своп-продажа
          end;
          Deal_buy_dlg  = cDeal ();
          Deal_sale_dlg  = cDeal ();

          dlg.convers = GetConversFromDeal (_dealId);//примечание 300
          updateFields(dlg); 

      elif (cmd == DLG_KEY)
            if (key == 323)  //F9
               if (dlg.cur_BA == "810")
                   dlg.cur_BA = "643"; 
               end; 
               if (dlg.cur_K == "810")
                   dlg.cur_K = "643"; 
               end; 
               if (dlg.sell_cur_BA == "810")
                   dlg.sell_cur_BA = "643"; 
               end; 
               if (dlg.sell_cur_K == "810")
                   dlg.sell_cur_K = "643"; 
               end; 
               Deal_buy_dlg.dealDate = dlg.date1;
               Deal_buy_dlg.dealWay  = dlg.way;  
               Deal_buy_dlg.summ_BA  = dlg.summ_BA;
               Deal_buy_dlg.cur_BA   = dlg.cur_BA;
               Deal_buy_dlg.date_BA  = dlg.date_BA;
               Deal_buy_dlg.price    = dlg.price;
               Deal_buy_dlg.cur_K    = dlg.cur_K;
               Deal_buy_dlg.date_K   = dlg.date_K;

               Deal_sale_dlg.dealDate = dlg.date1;
               Deal_sale_dlg.dealWay  = dlg.way;  
               Deal_sale_dlg.summ_BA  = dlg.sell_summ_BA;
               Deal_sale_dlg.cur_BA   = dlg.sell_cur_BA;
               Deal_sale_dlg.price    = dlg.sell_price;
               Deal_sale_dlg.date_BA  = dlg.sell_date_BA;
               Deal_sale_dlg.cur_K    = dlg.sell_cur_K;
               Deal_sale_dlg.date_K   = dlg.sell_date_K;

               if ( (not compareDeals (Deal_buy, Deal_buy_dlg )) or
                    (not compareDeals (Deal_sale, Deal_sale_dlg )) 
                   )
                  msgbox ("Верификация не выполнена !");
                  _code = 4256;
                   if (_dealtype == 5)
                     rundialog ( errpanel2, "PanelErr2" );
                   else
                     rundialog ( errpanel2_1, "PanelErr2" );
                   end; 
               else 
                  msgbox ("Верификация выполнена !");
                  _code = 0;
               end; 
               return CM_CANCEL;
        
            elif (key == 317) //F3
                if (id == FldIndex(dlg, "way"))
                    dlg.way = ScrollWAY();
                    updateFields (dlg);  
                elif (id == FldIndex(dlg, "cur_BA"))    
                    dlg.cur_BA = ScrollFIID ("Валюта",27,12,15);
                    updateFields (dlg);
                elif (id == FldIndex(dlg, "cur_K"))    
                    dlg.cur_K = ScrollFIID ("Валюта",27,12,15);
                    updateFields (dlg);    
                elif (id == FldIndex(dlg, "sell_cur_BA"))    
                    dlg.sell_cur_BA = ScrollFIID ("Валюта",27,12,15);
                    updateFields (dlg);
                elif (id == FldIndex(dlg, "sell_cur_K"))    
                    dlg.sell_cur_K = ScrollFIID ("Валюта",27,12,15);
                    updateFields (dlg);      
                end;     
            elif (key == 27) //Esc
               msgbox ("Верификация не выполнена !");
               _code = 4256;
               return CM_CANCEL;
            elif (key == 13);
               return CM_IGNORE;   
            end;
      elif (cmd == DLG_REMFOCUS)
            if (id == FldIndex(dlg, "price"))      
            elif (id == FldIndex(dlg, "sell_price")) 
            end;
      end;
END;

MACRO mainPanel (dlg, cmd, id, key ) 
      
      if (cmd == DLG_INIT)
          Deal_main = cDeal (_dealId, 0); //0-покупка/продажа

          Deal_dlg  = cDeal ();

          dlg.convers = GetConversFromDeal (_dealId);//примечание 300
          updateFields(dlg); 

      elif (cmd == DLG_KEY)
            if (key == 323)  //F9
               if (dlg.cur_BA == "810")
                   dlg.cur_BA = "643"; 
               end; 
               if (dlg.cur_K == "810")
                   dlg.cur_K = "643"; 
               end;  
               Deal_dlg.dealDate = dlg.date1;
               Deal_dlg.dealWay  = dlg.way;  
               Deal_dlg.summ_BA  = dlg.summ_BA;
               Deal_dlg.cur_BA   = dlg.cur_BA;
               Deal_dlg.price    = dlg.price;
               Deal_dlg.date_BA  = dlg.date_BA;
               Deal_dlg.cur_K    = dlg.cur_K;
               Deal_dlg.date_K   = dlg.date_K;

               if (not compareDeals (Deal_main, Deal_dlg ))
                  msgbox ("Верификация не выполнена !");
                  _code = 4256;
                  rundialog ( errpanel, "PanelErr" );
               else 
                  msgbox ("Верификация выполнена !");
                  _code = 0;
               end; 
               return CM_CANCEL;
            elif (key == 317) //F3
                if (id == FldIndex(dlg, "way"))
                    dlg.way = ScrollWAY();
                    updateFields (dlg);  
                elif (id == FldIndex(dlg, "cur_BA"))    
                    dlg.cur_BA = ScrollFIID ("Валюта",27,12,15);
                    updateFields (dlg);
                elif (id == FldIndex(dlg, "cur_K"))    
                    dlg.cur_K = ScrollFIID ("Валюта",27,12,15);
                    updateFields (dlg);    
                end;
            elif (key == 27) //Esc
               msgbox ("Верификация не выполнена !");
               _code = 4256;
               return CM_CANCEL;
            elif (key == 13);
               return CM_IGNORE;   
            end;
      end;
END;

MACRO InitOperation( KindOp, KindDoc, addr)
  /*CHVA */
  record deal(dvndeal);
  SetBuff(deal, addr);
 
  if ((deal.kind == 12735) and (deal.conftpid == 0))
     msgbox("Не заполнено поле \"Транспорт подтверждения \", поле будет заполнено автоматически ");
     var cmd = "UPDATE ddvndeal_dbt t SET T.T_CONFTPID = 2 WHERE T.T_CONFTPID = 0 AND T.T_ID =" + deal.ID;
     RsdCommand(cmd).Execute;
  end;  
  /*CHVA */      

  /*PNV i-support 486998*/
  if ( (deal.genagrid == -1) and (deal.SECTOR == UNSET_CHAR))
      msgbox("В сделке не указано ген.соглашение!");
      return 4256;
  end;
  /*PNV i-support 486998*/

  /*ZMV BIQ-8258 блокировка исполнения внебиржевых сделок/сделок валютного рынка для договоров, 
  у которых категория "Сведения о наличии или отсутствии ИИС у другого ПУ" = "Да" И категория "Предоставлены подтвержд. документы о расторжении ИИС у другого ПУ" на дату операции равно "Нет" или не заполнено*/ 
  if( ДоговорИИСуДругогоПУ(deal.ClientContr, deal.Date) )
     return 4256;
  end;

  if (deal.sector == SET_CHAR ) //TEG 27.05.19 для биржи не производим двойного ввода
      return 0;
  end;

  if (index(deal.comment, "Bloomberg") != 0)  //SVE для сделок заключенных по Bloomberg верификация не нужна
     return 0;
  end;

  if (deal.kind == 22730)/*для биржи*/
      return 0;
  end;

    _dealId = deal.id;
   _dealtype =  deal.type;
   if ({oper} == deal.oper)
      msgbox ("Пользователь № "+ deal.oper+", вводивший сделку, не может ее верифицировать");
   end;

   if ((deal.kind == 12730) or (deal.kind == 12745) or (deal.kind == 12735)) //покупка-продажа
      rundialog ( panel, "mainPanel" );
   elif ( //своп
         (deal.kind == 22710) or
         (deal.kind == 12715)
        )
      if (_dealtype == 5) //покупка-продажа
          rundialog ( panel2, "mainPanel2" );
      else //6 - продажа/покупка
          rundialog ( panel2_1, "mainPanel2" );
      end;
   end;   

   return _code;

  return 0;
END;
