/*
$Name:        dvprcsw450.mac
$Module:      Производные инструменты
$Description: Процентный СВОП. Шаг: Корректировка РХДП
*/
IMPORT InsCarryDoc, RsbDataSet, "dv_car.mac", dvinter,rsd;

PRIVATE VAR rNDeal = TRecHandler( "dvndeal" );
VAR SvOpDL_COMM = TRecHandler("dl_comm");
PRIVATE VAR rDocKind;

Private MACRO ПроводкаПоКатегориямУчетаWithTxt( FD,
                                              СчетДебет, СчетКредит,
                                              ДатаВалютирования, Chapter,
                                              FIID, СуммаОперации,
                                              docum,
                                              Result_Carry, Kind_Oper,
                                              Numb_Document,
                                              Ground, TxtLinesRHDP
                                            )

  return ПроводкаПоКатегориямУчета( FD, СчетДебет, СчетКредит, ДатаВалютирования,
                                 Chapter ,FIID, СуммаОперации, docum,
                                 Result_Carry, Kind_Oper,Numb_Document,Ground,
                                 null, null, null,
                                 null, null, null, null,
                                 null, null, null, null,
                                 null, null, null, null,
                                 null, null, null, null, null,
                                 null, 
                                 null, null, null, TxtLinesRHDP
                               );
end;

PRIVATE MACRO GetLastCorrTrn (DealID, OperDate, AccPlus, AccMinus, CorrSum)
  var query = "  SELECT CASE " +
              "            WHEN    TRN.t_account_receiver LIKE '10620%' " +
              "                 OR TRN.t_account_payer LIKE '10620%' " +
              "            THEN " +
              "               '+' " +
              "            WHEN    TRN.t_account_receiver LIKE '10619%' " +
              "                 OR TRN.t_account_payer LIKE '10619%' " +
              "            THEN " +
              "               '-' " +
              "            ELSE " +
              "               '' " +
              "         END " +
              "            LastCor " +
              "    FROM (SELECT * " +
              "            FROM (  SELECT STEP.T_ID_OPERATION, step.t_id_step " +
              "                      FROM ddvndeal_dbt NDeal, " +
              "                           doprstep_dbt step, " +
              "                           doproper_dbt oper " +
              "                     WHERE     oper.t_DocKind = NDeal.t_DocKind " +
              "                           AND oper.t_DocumentID = LPAD (NDeal.t_ID, 34, '0') " +
              "                           AND step.t_ID_Operation = oper.t_ID_Operation " +
              "                           AND step.t_ServDocKind = 4841 /*DV_SRVOP_COR_RHDP*/ " + 
              "                           AND NDeal.t_ID = ? " +
              "                           AND step.t_plan_date <= ? " +
              "                  ORDER BY step.t_plan_date DESC) tmp " +
              "           WHERE ROWNUM = 1) prev_op, " +
              "         doprdocs_dbt doc, " +
              "         dacctrn_dbt trn " +
              "   WHERE     doc.T_ID_OPERATION = prev_op.T_ID_OPERATION " +
              "         AND doc.t_id_step = prev_op.t_id_step " +
              "         AND TRN.T_ACCTRNID = DOC.T_ACCTRNID " +
              "         AND (          TRN.t_account_payer IN (?,?) " +
              "                 AND     TRN.t_account_receiver NOT IN (?,?) " +
              "              OR        TRN.t_account_receiver IN (?,?) " + 
              "                 AND     TRN.t_account_payer NOT IN (?,?)) " +
              "         AND TRN.t_sum_natcur = ? " +
              "ORDER BY TRN.T_ACCTRNID DESC ";
  var  cmd = DL_RSDCommand(query);

  cmd.AddParam(DealID);
  cmd.AddParam(OperDate);
  cmd.AddParam(AccPlus);
  cmd.AddParam(AccMinus);
  cmd.AddParam(AccPlus);
  cmd.AddParam(AccMinus);
  cmd.AddParam(AccPlus);
  cmd.AddParam(AccMinus);
  cmd.AddParam(AccPlus);
  cmd.AddParam(AccMinus);
  cmd.AddParam(CorrSum);

  var DataSet = cmd.Execute();
  if (DataSet.MoveNext())
    if (DataSet.LastCor == "+")
      return 1;
    elif (DataSet.LastCor == "-")
      return 0;
    end;
  end;
  return -1;
END;

MACRO ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step )
  record rFirstDoc( dvndeal );
  var СчетPlus = TRecHandler("account");
  var СчетMinus = TRecHandler("account");
  var Sкорр_рхдп = 0, Delta = 0;
  var RestDPPlus = 0, RestDPMinus = 0;
  var LastCorr = -1;
  
  SetBuff( rFirstDoc, FirstDoc );
  copy( rNDeal, rFirstDoc );
  rDocKind = DocKind;
  
  if (SvOpDL_COMM.rec.DocumentID <= 0)
    msgbox("Шаг должен выполняться только из соответствующей сервисной операции." );
    return 1;
  end;

  var FD = DVFirstDocNDeal(DocKind, rFirstDoc);


  if((not FD.IsExistAccount("+Корр,Хедж_ДП",  null, true, FIROLE_UNDEF, СчетPlus, null, SvOpDL_COMM.rec.CommDate)) and
    (not FD.OpenAccount("+Корр,Хедж_ДП", null, false, FIROLE_UNDEF, СчетPlus, SvOpDL_COMM.rec.CommDate, NATCUR)))
    msgbox("Ошибка открытия/поиска счета с КУ '+Корр,Хедж_ДП' для сделки с id " + FD.deal.rec.ID );
    return 1;
  end;

  if((not FD.IsExistAccount("-Корр,Хедж_ДП",  null, true, FIROLE_UNDEF, СчетMinus, null, SvOpDL_COMM.rec.CommDate)) and
    (not FD.OpenAccount("-Корр,Хедж_ДП", null, false, FIROLE_UNDEF, СчетMinus, SvOpDL_COMM.rec.CommDate, NATCUR)))
    msgbox("Ошибка открытия/поиска счета с КУ '+Корр,Хедж_ДП' для сделки с id " + FD.deal.rec.ID );
    return 1;
  end;
  
  var query =   " select hdr.t_sumcor, hdr.t_delta "
          + "   from ddv_hdgcorrhdp_dbt hdr "
          + "  where hdr.t_SvOpID = ? "
          + "    and hdr.t_InstrID = ?";

  var  cmd = DL_RSDCommand(query);         

  cmd.AddParam(SvOpDL_COMM.rec.DocumentID);
  cmd.AddParam(FD.deal.rec.ID);

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    Sкорр_рхдп  = DataSet.SumCor;
    Delta       = round(DataSet.delta,2);
  end;

  if (Sкорр_рхдп > 0)
        LastCorr = GetLastCorrTrn(FD.deal.rec.ID, SvOpDL_COMM.rec.CommDate, СчетPlus.rec.Account, СчетMinus.rec.Account,Sкорр_рхдп);
        if (LastCorr == 1)
          if( not ПроводкаПоКатегориямУчетаWithTxt( FD,
                                              СчетPlus, "Расходы_хедж, ПФИ",
                                              SvOpDL_COMM.rec.CommDate, СчетPlus.rec.Chapter,
                                              СчетPlus.rec.Code_Currency, Abs(Sкорр_рхдп),
                                              Doc,
                                              INPCARRY, "",
                                              SvOpDL_COMM.rec.CommCode,
                                              "Восстановление  положительной разницы", "Неэффективное хеджирование"
                                            )
            )
              msgbox("Ошибка при выполнении проводки для сделки с id " + FD.deal.rec.ID );
              return 1;
          end;
        elif(LastCorr == 0)
          if( not ПроводкаПоКатегориямУчетаWithTxt( FD,
                                                "Доходы_хедж, ПФИ", СчетMinus,
                                                SvOpDL_COMM.rec.CommDate, СчетMinus.rec.Chapter,
                                                СчетMinus.rec.Code_Currency, Abs(Sкорр_рхдп),
                                                Doc,
                                                INPCARRY, "",
                                                SvOpDL_COMM.rec.CommCode,
                                                "Восстановление  положительной разницы", "Неэффективное хеджирование"
                                              )
              )
                msgbox("Ошибка при выполнении проводки для сделки с id " + FD.deal.rec.ID );
                return 1;
          end;
        else
          msgbox("Ошибка при выполнении проводки для сделки с id " + FD.deal.rec.ID + ". Не удалось найти предыдущую проводку по корректировке РХДП для восстановления положительной разницы");
          return 1;
        end;

        RestDPMinus = GetRestAccountTRH(СчетMinus, SvOpDL_COMM.rec.CommDate);
        RestDPPlus  = GetRestAccountTRH(СчетPlus, SvOpDL_COMM.rec.CommDate);

        if((RestDPMinus != 0) AND (RestDPPlus != 0))
          if( not ПроводкаПоКатегориямУчета( FD,
                                             СчетMinus,СчетPlus, 
                                             SvOpDL_COMM.rec.CommDate, 1,
                                             NATCUR, abs(IIF(abs(RestDPMinus) <= abs(RestDPPlus), RestDPMinus, RestDPPlus)),
                                             doc,
                                             INPCARRY,"", "",
                                             "Урегулирование счетов по учету РХДП"
                                          )
            )
               msgbox("Ошибка при выполнении проводки для сделки с id " + FD.deal.rec.ID );
              return 1;
          end
        end;
  end;
  if (Delta > 0)
        RestDPMinus = GetRestAccountTRH(СчетMinus, SvOpDL_COMM.rec.CommDate);
        RestDPPlus  = GetRestAccountTRH(СчетPlus, SvOpDL_COMM.rec.CommDate);
        if(RestDPPlus != 0)
          if( not ПроводкаПоКатегориямУчетаWithTxt( FD,
                                                  "Расходы_хедж, ПФИ", СчетPlus,
                                                SvOpDL_COMM.rec.CommDate, СчетPlus.rec.Chapter,
                                                СчетPlus.rec.Code_Currency, Abs(Delta),
                                                Doc,
                                                INPCARRY, "",
                                                SvOpDL_COMM.rec.CommCode,
                                                "Отражение положительной разницы", "Неэффективное хеджирование"
                                              )
              )
                msgbox("Ошибка при выполнении проводки для сделки с id " + FD.deal.rec.ID );
                return 1;
            end;
        elif(RestDPMinus != 0)
          if( not ПроводкаПоКатегориямУчетаWithTxt( FD,
                                                СчетMinus, "Доходы_хедж, ПФИ",
                                                SvOpDL_COMM.rec.CommDate, СчетMinus.rec.Chapter,
                                                СчетMinus.rec.Code_Currency, Abs(Delta),
                                                Doc,
                                                INPCARRY, "",
                                                SvOpDL_COMM.rec.CommCode,
                                                "Отражение положительной разницы", "Неэффективное хеджирование"
                                              )
              )
                msgbox("Ошибка при выполнении проводки для сделки с id " + FD.deal.rec.ID );
                return 1;
          end;
        end;
        query =   " select frval.t_id"
          + "   from ddv_hdgcorrhdp_dbt hdr, DDLHDGRFAIRVAL_DBT frval "
          + "  where hdr.t_SvOpID = ? "
          + "    and hdr.t_InstrID = ? "
          + "    and frval.t_RelationID = hdr.t_RelationID "
          + "    and frval.t_Date = ?";

        cmd = DL_RSDCommand(query);         

        cmd.AddParam(SvOpDL_COMM.rec.DocumentID);
        cmd.AddParam(FD.deal.rec.ID);
        cmd.AddParam(SvOpDL_COMM.rec.CommDate);

        DataSet = cmd.Execute();
        if(DataSet.moveNext())
          if(SetHDGFrCorReserv(DataSet.ID, Delta))
            msgbox("Ошибка при установке 'Корректировка резерва хеджирования' на ОХ для сделки " + FD.deal.rec.ID );
            return 1;
          end;
        else  
          msgbox("Ошибка при установке 'Корректировка резерва хеджирования' на ОХ для сделки " + FD.deal.rec.ID + ". Не найдена строка изменения СС на дату " + SvOpDL_COMM.rec.CommDate);
          return 1;
        end;
  end;
  
  return 0;
END;

MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */
    // Произошла ошибка или происходит откат
    record rFirstDoc( dvndeal );
    SetBuff( rFirstDoc, FirstDoc );
    copy( rNDeal, rFirstDoc );
    if (((( errTrn > 0 ) and ( CommitOrRollback == 1 ))  OR (CommitOrRollback == 2)) AND (KindDoc != DV_SRVOP_COR_RHDP))
      if ((CommitOrRollback == 2) AND (SvOpDL_COMM.rec.DocumentID <= 0))
      msgbox("Шаг можно откатывать только в сервисной операции" );
      return 0;
    end;
  
    var FD = DVFirstDocNDeal(rNDeal.rec.DocKind, rFirstDoc);
    var stat = 0;
    var query =   " SELECT frval.t_id " +
                  "  FROM ddv_hdgcorrhdp_dbt hdr, " +
                  "       DDLHDGRFAIRVAL_DBT frval " +
                  " WHERE  hdr.t_SvOpID = ? " +
                  "       AND hdr.t_InstrID = ? " +
                  "       AND frval.t_RelationID = hdr.t_RelationID " +
                  "       AND frval.t_Date = hdr.t_Date ";

    var  cmd = DL_RSDCommand(query);

    cmd.AddParam(SvOpDL_COMM.rec.DocumentID);
    cmd.AddParam(FD.deal.rec.ID);

    var DataSet = cmd.Execute();
    if(DataSet.moveNext())
        SetHDGFrCorReserv(DataSet.ID, $0);
      end;
      return;
   end;
END;
