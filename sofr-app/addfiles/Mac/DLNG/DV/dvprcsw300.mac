/*
$Name:        dvprcsw300.mac
$Module:      Производные инструменты
$Description: Процентный своп. Шаг: Изменение условий сделки
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac", "dv_period.mac",PaymInter,dvinter, RsbDataSet, "dvservop.mac", "dvinterimpm.mac";

PRIVATE VAR Dold = TRecHandler( "dvndeal.dbt" ); /**/
PRIVATE VAR Dnew = TRecHandler( "dvndeal.dbt" ); /**/
PRIVATE VAR Fold = TRecHandler( "dvnfi.dbt" );   /* Заполняется программой при выполнении шага */
PRIVATE VAR Fnew = TRecHandler( "dvnfi.dbt" );   /**/
PRIVATE VAR Fold_2 = TRecHandler( "dvnfi.dbt" );   /**/
PRIVATE VAR Fnew_2 = TRecHandler( "dvnfi.dbt" );   /**/

PRIVATE VAR rNDeal = TRecHandler( "dvndeal" );
PRIVATE VAR rDocKind;

PRIVATE MACRO SayError( StrErr:STRING, NumErr:INTEGER ):INTEGER
  if( NumErr == null )
     NumErr = 1;
  end;
  SvOpInsertError( rDocKind, rNDeal, NumErr, StrErr );

  return 1; /*выход по ошибке*/
END;

private macro ПроверитьНеобходимостьРепортингаВРепозитарии( DealID:integer )
  
  var    result=false;
  var cmd = DL_RSDCommand();
  var query = " SELECT t_docid"
             +" FROM ddl_repozdeal_dbt"
             +" WHERE t_docid = ?"
             +" AND (t_dockind = 199 OR t_dockind = 4815)";
  
  cmd.AddParam(DealID);
  
  var DataSet = cmd.Execute(query);
  if(DataSet.moveNext())
      result=true;
  end;
  return result;
end;

/* Поиск первичных сообщений для сделки, имеющих статус "Подготовлено" либо "Ошибка выгрузки" */
private macro FindUnloadedMessages(DealID:integer)
  var cmd = DL_RSDCommand();
  var query = "select 1 from dir_generalinf_dbt "
            + "  where t_internalmessageid "
            + "  in ( select t_docid from ddlgrdoc_dbt "
            + "       where t_grdealid in ( select t_id from ddlgrdeal_dbt "
            + "                             where t_docid = ? )"
            + "       and t_dockind = ? )" 
            + "  and T_RSTATUS in (1, 4)"; // 1 - подготовлено, 4 - ошибка выгрузки

  cmd.AddParam(DealID);
  cmd.AddParam(4853/*REPOS_GENERALINF*/);
  var DataSet = cmd.Execute(query);
  return DataSet.moveNext()
end;

/* Поиск сводных (cm083) сообщений для сделки, имеющих статус "Подготовлено" либо "Ошибка выгрузки" */
private macro FindUnloadedBulkMessages(DealID:integer)
  var cmd = DL_RSDCommand();
  var query = "select 1 from dir_generalinf_dbt "
            + "  where t_internalmessageid "
            + "  in ( select t_internalmessageid from dir_cm083_dbt "
            + "       where t_id in ( select t_docid from ddlgrdoc_dbt "
            + "                       where t_grdealid in ( select t_id from ddlgrdeal_dbt "
            + "                                             where t_docid = ? ) "
            + "                       and t_dockind = ? ) )"
            + "  and T_RSTATUS in (1, 4)"; // 1 - подготовлено, 4 - ошибка выгрузки

  cmd.AddParam(DealID);
  cmd.AddParam(4857/*REPOS_CM083*/);
  var DataSet = cmd.Execute(query);
  return DataSet.moveNext();
end;

private macro ПроверитьИзменениеПараметровДляРепозУчета()
  var haveChange = false;
  haveChange = haveChange or (Fnew.rec.Cost != Fold.rec.Cost); //стоимость
  haveChange = haveChange or (Fnew.rec.Price != Fold.rec.Price); //цена
  haveChange = haveChange or (Fnew.rec.PayDate != Fold.rec.PayDate); //дата поставки
  haveChange = haveChange or (Fnew.rec.SuplDate != Fold.rec.SuplDate); //дата оплаты
  haveChange = haveChange or (Fnew.rec.ExecDate != Fold.rec.ExecDate); //дата исполнения
  haveChange = haveChange or (Fnew_2.rec.Cost != Fold_2.rec.Cost); //стоимость
  haveChange = haveChange or (Fnew_2.rec.Price != Fold_2.rec.Price); //цена
  haveChange = haveChange or (Fnew_2.rec.PayDate != Fold_2.rec.PayDate); //дата поставки
  haveChange = haveChange or (Fnew_2.rec.SuplDate != Fold_2.rec.SuplDate); //дата оплаты
  haveChange = haveChange or (Fnew_2.rec.ExecDate != Fold_2.rec.ExecDate); //дата исполнения
  return haveChange;
end;

private macro DV_ПроверитьСуществованиеШага(ID_Operation, Symb, IsExecute)
  var cmd = DL_RSDCommand();
   var query = " SELECT COUNT(1) AS CountStep " +
    "   FROM doprstep_dbt step" +
    "  WHERE step.t_ID_Operation = ? AND " +
    "        step.t_Symbol       = ? AND " +
    "        step.t_IsExecute    = ? ";
  cmd.AddParam(ID_Operation);
  cmd.AddParam(Symb);
  cmd.AddParam(IsExecute);
  var DataSet = cmd.Execute(query);
  DataSet.moveNext();
  return DataSet.CountStep;
end;

/* Проверка, является ли слелка с ожиданием запроса: параметры "Способ подтверждения" = "Комбинированный/Последовательный" и  */
/* "Инициирующая сторона" = "Контрагент"                                                                                      */
private macro isPendingRequestDeal(DealID:integer)
  var cmd = DL_RSDCommand();
  var query = " SELECT T_INITIATOR, T_METHOD_WAYTWOWAY FROM DDL_REPOZDEAL_DBT "
            + "   WHERE T_DOCID = ? ";
  cmd.AddParam(DealID);
  var DataSet = cmd.Execute(query);
  var stat = false;
  if (DataSet.moveNext())
    stat = ( DataSet.rec.Initiator == 2 ) and ( DataSet.rec.Method_Waytwoway == 1 )
  end;
  return stat;
end;

PRIVATE MACRO КорректировкаСуммыСделки(doc, FD, КсТреб, КсОбяз, ДатаИсполненияШага)
  var DebAccBuf  = TRecHandler("account");
  var CredAccBuf = TRecHandler("account");
  var DebA  = TRecHandler("account");
  var CredA = TRecHandler("account");
  var DebAcc:string  = ""; var DebFIID:integer  = ALLFININSTR;  var DebFiRole  = FIROLE_UNDEF;
  var CredAcc:string = ""; var CredFIID:integer = ALLFININSTR;  var CredFiRole = FIROLE_UNDEF;
  var DebF:integer  = ALLFININSTR; var CredF:integer  = ALLFININSTR;
  var corr = TRecHandler("account.dbt");
  var СчКоррРоль;

   ПолучитьСрочныеСчетаВнб( FD, @DebAcc, @DebFIID, @DebFiRole, @CredAcc, @CredFIID, @CredFiRole );

    if( (not FD.IsExistAccount( DebAcc,  null, false, DebFiRole,  DebAccBuf, null, ДатаИсполненияШага, DebFIID)) or
      (not FD.IsExistAccount( CredAcc, null, false, CredFiRole, CredAccBuf, null, ДатаИсполненияШага, CredFIID)) )
        return 1;
    end;

   if (КсТреб < 0)
      FD.SetFIIDForCorAccNum(CredFIID);
      if( not FD.IsExistAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), corr, null, ДатаИсполненияШага ) )
         if( (not FD.OpenAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), corr, ДатаИсполненияШага) ) )
            return 1;
         end;
      end;
      if( not ПроводкаПоКатегориямУчета( FD,
                                          CredAccBuf, corr,
                                          ДатаИсполненияШага, 4,
                                          CredFIID, abs(КсТреб),
                                          doc,
                                          INPCARRY, "", "",
                                          "Корректировка обязательств по" + IIF( FD.DealPart == 1," сделке "," второй части сделки ") + FD.DealCode(),
                                          null, null
                                       )
         )
         return 1;
      end;
   else
      FD.SetFIIDForCorAccNum(CredFIID);
      if( not FD.IsExistAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), corr, null, ДатаИсполненияШага ) )
         if( (not FD.OpenAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), corr, ДатаИсполненияШага) ) )
            return 1;
         end;
      end;
      if( not ПроводкаПоКатегориямУчета( FD,
                                          corr, CredAccBuf,
                                          ДатаИсполненияШага, 4,
                                          null, null,
                                          doc,
                                          INPCARRY, "", "",
                                          "Корректировка обязательств по" + IIF( FD.DealPart == 1," сделке "," второй части сделки ") + FD.DealCode(),
                                          null, null, null, null, null,
                                          CredFIID, abs(КсТреб)
                                       )
         )
         return 1;
      end;
   end;
   
   if (КсОбяз < 0)
      FD.SetFIIDForCorAccNum(DebFIID);
      if( not FD.IsExistAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), corr, null, ДатаИсполненияШага ) )
         if( (not FD.OpenAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), corr, ДатаИсполненияШага) ) )
            return 1;
         end;
      end;
         if( not ПроводкаПоКатегориямУчета( FD,
                                             corr, DebAccBuf,
                                             ДатаИсполненияШага, 4,
                                             null, null,
                                             doc,
                                             INPCARRY, "", "",
                                             "Корректировка требований по" + IIF( FD.DealPart == 1," сделке "," второй части сделки ") + FD.DealCode(),
                                             null, null, null, null, null,
                                             DebFIID, abs(КсОбяз)
                                          )
            )
            return 1;
         end;
   else
      FD.SetFIIDForCorAccNum(DebFIID);
      if( not FD.IsExistAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), corr, null, ДатаИсполненияШага ) )
         if( (not FD.OpenAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), corr, ДатаИсполненияШага) ) )
            return 1;
         end;
      end;
      if( not ПроводкаПоКатегориямУчета( FD,
                                          DebAccBuf, corr,
                                          ДатаИсполненияШага, 4,
                                          DebFIID, abs(КсОбяз),
                                          doc,
                                          INPCARRY, "", "",
                                          "Корректировка требований по" + IIF( FD.DealPart == 1," сделке "," второй части сделки ") + FD.DealCode(),
                                          null, null
                                       )
         )
         return 1;
      end;
   end;
   return 0;
   
END;

PRIVATE MACRO ПлатежиПоИзменениям(doc, FD, ВидПроводки, Delta, ВалютаПлатежа, ДатаИсполненияШага, ReturnAccBuf:@TRecHandler)
  var AccBuf = TRecHandler("account");
  if (ВидПроводки == "ХА")
    if( not FD.OpenAccount( "-Форвард, РПИ", null, null, FIROLE_FUTURES, AccBuf, ДатаИсполненияШага, ВалютаПлатежа, null, null, MC_PAIRACCMODE_MANUAL ) )
      return false;
    end;
    if( not ПроводкаПоКатегориямУчета(  FD, 
                                        "Выбытие ПФИ", AccBuf, 
                                        ДатаИсполненияШага, 1, 
                                        ВалютаПлатежа, Abs(Delta), 
                                        doc,
                                        INPCARRY, "","",
                                        "Отражение суммы обязательства по уплате денежных средств",
                                        null, null, FIROLE_FUTURES,
                                        NATCUR, ВалютаПлатежа,
                                        null, null, null, null, null, null, null, null,
                                        null, MC_PAIRACCMODE_MANUAL
                                      ) 
      )
        return 1;        
    end;
  elif (ВидПроводки == "ХБ")
    if( not FD.OpenAccount( "+Форвард, РПИ", null, null, FIROLE_FUTURES, AccBuf, ДатаИсполненияШага, ВалютаПлатежа, null, null, MC_PAIRACCMODE_MANUAL ) )
      return false;
    end;
    if( not ПроводкаПоКатегориямУчета(  FD, 
                                        AccBuf, "Выбытие ПФИ", 
                                        ДатаИсполненияШага, 1, 
                                        ВалютаПлатежа, Abs(Delta), 
                                        doc,
                                        INPCARRY, "","",
                                        "Отражение суммы требований по получению денежных средств",
                                        null, FIROLE_FUTURES, null,
                                        ВалютаПлатежа, NATCUR,
                                        null, null, null, null, null, null, null, null,
                                        MC_PAIRACCMODE_MANUAL, null 
                                      ) 
      )
        return 1;        
    end;
  end;
  ReturnAccBuf = AccBuf;
  return 0;
END;

PRIVATE MACRO ПроводкиПоПереоценке(FD, Doc, PmGrAccSum, Sum:money, OldSum:money, ДатаИсполненияШага):integer

  record CorrAcc(account);
  var StrGround:string = "";
  var DebAcc:string  = "", CredAcc:string = "";

  if( Sum != $0.0 )
      if( PmGrAccSum.Side == ALG_DV_PMGR_SIDE_DEMAND )
         StrGround = ОснованиеПроводкиПИ(DV_GRNUM_OVERVALUE_REQ_IP); /*Переоценка требований по промежуточным платежам*/
      else
         StrGround = ОснованиеПроводкиПИ(DV_GRNUM_OVERVALUE_COM_IP); /*Переоценка обязательств по промежуточным платежам*/
      end;
  
      FD.SetFIIDForCorAccNum(PmGrAccSum.Account.rec.Code_Currency);
      if( not FD.OpenAccount("СчКорреспГлаваГ", null, false, IIF(PmGrAccSum.Side == ALG_DV_PMGR_SIDE_DEMAND,
                                                                 FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE),
                                                                 FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE)),
                             CorrAcc, ДатаИсполненияШага) )
         return 1;
      end;
 
      if( ((PmGrAccSum.Side == ALG_DV_PMGR_SIDE_DEMAND) AND (Sum > $0.0)) OR
          ((PmGrAccSum.Side == ALG_DV_PMGR_SIDE_LIABILITY) AND (Sum < $0.0)) )
         if( not ПроводкаПоКатегориямУчета( FD,
                                            PmGrAccSum.Account, CorrAcc,
                                            ДатаИсполненияШага, PmGrAccSum.Account.rec.Chapter,
                                            PmGrAccSum.Account.rec.Code_Currency, Abs(Sum),
                                            Doc,
                                            INPCARRY, "",
                                            "",
                                            StrGround
                                          )
           )
            return SayError(DV_GetCarryError());
         end;
 
         DebAcc  = PmGrAccSum.Account.rec.Account;
         CredAcc = CorrAcc.Account;
      else
       
         if( not ПроводкаПоКатегориямУчета( FD,
                                            CorrAcc, PmGrAccSum.Account,
                                            ДатаИсполненияШага, PmGrAccSum.Account.rec.Chapter,
                                            null, null,
                                            Doc,
                                            INPCARRY, "",
                                            "",
                                            StrGround,
                                            0, null, null, null, null,
                                            PmGrAccSum.Account.rec.Code_Currency, Abs(Sum)
                                          )
           )
            return SayError( DV_GetCarryError() );
         end;
         DebAcc  = CorrAcc.Account;
         CredAcc = PmGrAccSum.Account.rec.Account;
      end;
  end;
 
  return 0;
END;

PRIVATE MACRO ВыполнитьПереоценкуПоИзменениямПараметров( FD:DVFirstDocNDeal, FD_2:DVFirstDocNDeal, Doc, ДатаИсполненияШага)

  var GrSum = TRecHandler("dvnpmgr");
  var DebAccBuf  = TRecHandler("account");
  var CredAccBuf = TRecHandler("account");
  var PmGrAccSum = DV_PmGrAccSumCollector();
  var DebAcc:string  = ""; var DebFIID:integer  = ALLFININSTR; var DebFiRole  = FIROLE_UNDEF;
  var CredAcc:string = ""; var CredFIID:integer = ALLFININSTR; var CredFiRole = FIROLE_UNDEF;
  var Query, RS;
  var isFixRate:bool = false;
  ПолучитьСрочныеСчетаВнб( FD, @DebAcc, @DebFIID, @DebFiRole, @CredAcc, @CredFIID, @CredFiRole );

  var i = 0;
  var GrFD;

    Query = RSDCommand( " SELECT * " +
                        "   FROM DV_DVNPMGR " +
                        "  WHERE t_Type   = 3 " + /* секция по таблице */
                        "    AND t_Status = 1 " + /* ожидается */
                        "    AND t_DealID = ? " +
                        " ORDER BY t_Side, t_BegDate " );
    Query.addParam( "", RSDBP_IN, FD.Deal.rec.ID );

    RS = TRsbDataSet( Query );
    while( RS.MoveNext() )
       GrFD = DVFirstDocPmGr(DL_DVNDEALPMGR, SQL_ConvTypeInteger(RS.rec.ID));
       /* пересчёт сумм */
       GrSum.Clear();
        DVCalcInterimPm(GrFD.PmGr.rec.ID, GrFD.PmGr.rec.PayDate, GrFD.PmGr.rec.Side, FD.DealRecHandler(), FD.nFI_BaseActiv, FD_2.nFI_BaseActiv, GrSum, ДатаИсполненияШага);

       if( (GrFD.PmGr.rec.DemandSum      != GrSum.rec.DemandSum) or
           (GrFD.PmGr.rec.LiabilitySum   != GrSum.rec.LiabilitySum) or
           (GrFD.PmGr.rec.PaymentSum     != GrSum.rec.PaymentSum) or
           (GrFD.PmGr.rec.FloatRateValue != GrSum.rec.FloatRateValue ) )

          if ( (GrFD.PmGr.rec.FloatRateValue != GrSum.rec.FloatRateValue) )
             isFixRate = true;   
          end;
          
          /* обновление строки графика */
          if( DV_OvervaluePm(GrFD.PmGr.rec.ID, ДатаИсполненияШага, GrSum.rec.DemandSum, GrSum.rec.LiabilitySum, GrSum.rec.PaymentSum, GrSum.rec.FloatRateValue, 6))
             return 1;
          end;

          DebAccBuf.Clear();
          if( GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_LIABILITY ) /* требование или не задано */
             if( not DV_GetAccount(4, FD.ВалютаТребования(), GrFD.PmGr.rec.DemandAccount, DebAccBuf, false) )
                return 1;
             end;
          end;

          CredAccBuf.Clear();
          if( GrFD.PmGr.rec.Side != ALG_DV_PMGR_SIDE_DEMAND ) /* обязательство или не задано */
             if( not DV_GetAccount(4, FD.ВалютаОбязательства(), GrFD.PmGr.rec.LiabilityAccount, CredAccBuf, false) )
                return 1;
             end;
          end;
          PmGrAccSum.AddData(DebAccBuf, CredAccBuf, GrSum.rec.DemandSum, GrSum.rec.LiabilitySum, GrFD.PmGr.rec.Side, GrFD.PmGr.rec.DemandSum, GrFD.PmGr.rec.LiabilitySum );
       end;
    end;

  i = 0;
  while( i < PmGrAccSum.Size )
     if( ПроводкиПоПереоценке(FD, Doc, PmGrAccSum(i), PmGrAccSum(i).Sum - PmGrAccSum(i).OldSum, PmGrAccSum(i).OldSum, ДатаИсполненияШага) != 0 )
        return 1;
     end;
     i = i + 1;
  end;
  return 0;
END;

PRIVATE MACRO ПроверкаИзмененияНеТолькоВремени()
  var change = false;

  if (Fnew.rec.Amount     != Fold.rec.Amount) change = true; end;
  if (Fnew_2.rec.Amount   != Fold_2.rec.Amount) change = true; end;
  if (Fnew_2.rec.Course   != Fold_2.rec.Course) change = true; end;

  return change;
END;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  var ДатаИсполненияШага = {curdate};
  var ReCalcPayments = true;
  var PaymentRq_1part, PaymentOb_1part, PaymentRq_2part, PaymentOb_2part;
  var ActuatePaymentRq = false, ActuatePaymentOb = false;
  var SubPurpose = 1;
  var TmpPaymentID = -1;
  var AccBufForwardRPI = TRecHandler("account");
  var DublPayment_ob:RsbPayment = null,  DublPayment_rq:RsbPayment = null;

  SetBuff( ndeal, FDoc );
  var FD = DVFirstDocNDeal(DocKind, ndeal, null, Fnew), Ground;
  var oldFD = DVFirstDocNDeal(DocKind, ndeal, null, Fold);
  var FD_2 = DVFirstDocNDeal(DocKind, ndeal, 2, Fnew_2);
  var oldFD_2 = DVFirstDocNDeal(DocKind, ndeal, 2, Fold_2);
  if (FD.ОтказОтИсполнения() or FD_2.ОтказОтИсполнения())
     return 0;
  end;
  var ПризнаниеПФИ = 0, ПостНаБаланс = 0, Исполнение1ч_done = 0, Исполнение1ч_undone = 0;
  var КсТреб:money = 0.0;
  var КсОбяз:money = 0.0;

  КсОбяз = FD.СуммаОбязательства() - oldFD.СуммаОбязательства();
  КсТреб = FD_2.СуммаТребования() - oldFD_2.СуммаТребования();

  if( Dold.rec.ID <= 0 )
     MsgBox("Вставка шага \"Изменение условий\" должна выполняться из скроллинга сделок");
     return 1;
  end;  

  if(ПроверкаИзмененияНеТолькоВремени())
    if( (not FD.IsClient()) and (not FD.БиржеваяСделкаКромеВнебиржСПФИ()) and (not FD.ВыполненаПереоценкаСС(ДатаИсполненияШага)) )
      if( MsgBoxEx( "Не выполнена переоценка справедливой стоимости!|Продолжить?", MB_YES+MB_NO, IND_NO ) == IND_NO )
          return 1;
      end;
    end;

    if( MsgBoxEx( "Выполнить пересчет промежуточных платежей по изменениям?", MB_YES+MB_NO, IND_NO) != IND_YES )
      ReCalcPayments = false;
    end;

    Исполнение1ч_done = DV_ПроверитьСуществованиеШага(ID_Operation, "и", "X");
    Исполнение1ч_undone = DV_ПроверитьСуществованиеШага(ID_Operation, "и", "R");


    if( FD.IsClient() == false )
      if ((КсОбяз != 0) OR (КсТреб != 0))
        if (DV_SaveHistoryFaceValue(Dold, Fold, Fnew, 1, ID_Operation, ID_Step, ДатаИсполненияШага) != 0 )
          MsgBox("Ошибка при сохранении изменения номиналов.");
          return 1;
        end;
        if (DV_SaveHistoryFaceValue(Dold, Fold_2, Fnew_2, 2, ID_Operation, ID_Step, ДатаИсполненияШага) != 0 )
          MsgBox("Ошибка при сохранении изменения номиналов.");
          return 1;
        end;
        if ((Исполнение1ч_done == 0) AND ((Исполнение1ч_undone == 1) OR (Исполнение1ч_undone == 0)))
          if(FD.ПоставочныйКонтракт())
            if (КсОбяз != 0)
              if( FD.ПлатежТреб.rec.PaymentID > 0 )
                PaymentRq_1part = RsbPayment(FD.ПлатежТреб.rec.PaymentID);
              end;
              if( ActuatePaymentRq = ((FD.ПлатежТреб.rec.PaymentID > 0) and (PaymentRq_1part.PaymStatus != PM_CLOSED_W_M_MOVEMENT) and (FD.ДатаНачала() == PaymentRq_1part.ValueDate) and (PaymentRq_1part.ValueDate >= ДатаИсполненияШага)))
                PaymentRq_1part.BaseAmount = FD.СуммаОбязательства();
                PaymentRq_1part.OrderAmount = FD.СуммаОбязательства();
                PaymentRq_1part.ReceiverAmount = FD.СуммаОбязательства();
                PaymentRq_1part.Actuate();
              end;
            end;

            if (КсТреб != 0)
              if( FD.ПлатежОб.rec.PaymentID > 0 )
                PaymentOb_1part = RsbPayment(FD.ПлатежОб.rec.PaymentID);
              end;

              if( ActuatePaymentOb = ((FD.ПлатежОб.rec.PaymentID > 0) and (PaymentOb_1part.PaymStatus != PM_CLOSED_W_M_MOVEMENT) and (FD.ДатаНачала() == PaymentOb_1part.ValueDate) and (PaymentOb_1part.ValueDate >= ДатаИсполненияШага)))
                PaymentOb_1part.BaseAmount = FD_2.СуммаТребования();
                PaymentOb_1part.OrderAmount = FD_2.СуммаТребования();
                PaymentOb_1part.PayerAmount = FD_2.СуммаТребования();
                PaymentOb_1part.Actuate();
              end;
            end;

            if (ActuatePaymentRq OR ActuatePaymentOb)
              if ( АктуализироватьПлатежи(FD, ДатаИсполненияШага) != 0 )
                return 1;
              end;
            end;
          end;
          if (FD.ПоставочныйКонтракт() and (not ActuatePaymentRq) and (not ActuatePaymentOb))
            if( MsgBoxEx( "Не найдено платежей по первоначальному обмену активами для корректировки! Продолжить?", MB_YES+MB_NO, IND_NO) != IND_YES )
              return 1;
            end;
            if ( КорректировкаСуммыСделки(doc, FD, КсТреб, КсОбяз, ДатаИсполненияШага) != 0 )
              return 1;
            end;
          else
            if ( КорректировкаСуммыСделки(doc, FD, КсТреб, КсОбяз, ДатаИсполненияШага) != 0 )
              return 1;
            end;
          end;
        end;

        ActuatePaymentRq = ActuatePaymentOb = false;
        if( FD_2.ПлатежТреб.rec.PaymentID > 0 )
          PaymentOb_2part = RsbPayment(FD_2.ПлатежТреб.rec.PaymentID);
        end;
        if( FD_2.ПлатежОб.rec.PaymentID > 0 )
          PaymentRq_2part = RsbPayment(FD_2.ПлатежОб.rec.PaymentID);
        end;
        if (КсОбяз != 0)
          if (FD.ПоставочныйКонтракт())
            if ((Исполнение1ч_done == 1) AND (Исполнение1ч_undone == 0))
              if(КсОбяз > 0) 
                if ( ПлатежиПоИзменениям(doc, FD_2, "ХА", КсОбяз, FD_2.ВалютаОбязательства_(), ДатаИсполненияШага, @AccBufForwardRPI) )
                  return 1;
                end;
                if (FD_2.ПлатежТреб.rec.PaymentID > 0)
                  if( DVND_CreatePayment(FD_2,
                            PaymentRq_2part.Purpose, PaymentRq_2part.DocKind, PaymentRq_2part.DocumentID,
                            ДатаИсполненияШага,
                            PaymentRq_2part.Payer, PaymentRq_2part.PayerBankID,
                            PaymentRq_2part.Receiver, PaymentRq_2part.ReceiverBankID,
                            abs(КсОбяз), FD_2.ВалютаОбязательства_(),
                            "Изменение номинала БА",
                            AccBufForwardRPI, 
                            null,
                            PaymentRq_2part.Department, 1,
                            PM_READIED,
                            @DublPayment_ob, TmpPaymentID) != 0)
                        MsgBox("Ошибка при создании платежа по измнению номинала БА");
                        return 1;
                  end;
                    
                  DublPayment_ob.Netting = PaymentRq_2part.Netting;
                end;
              else
                if( FD.ПлатежТреб.rec.PaymentID > 0 )
                  PaymentOb_1part = RsbPayment(FD.ПлатежТреб.rec.PaymentID);
                end;
                if ( ПлатежиПоИзменениям(doc, FD, "ХБ", КсОбяз, PaymentOb_1part.BaseFIID, ДатаИсполненияШага, @AccBufForwardRPI) )
                  return 1;
                end;
                if (FD.ПлатежТреб.rec.PaymentID > 0)
                  if( DVND_CreatePayment(FD,
                            PaymentOb_1part.Purpose, PaymentOb_1part.DocKind, PaymentOb_1part.DocumentID,
                            ДатаИсполненияШага,
                            PaymentOb_1part.Payer, PaymentOb_1part.PayerBankID,
                            PaymentOb_1part.Receiver, PaymentOb_1part.ReceiverBankID,
                            abs(КсОбяз), PaymentOb_1part.BaseFIID,
                            "Изменение номинала БА",
                            null, 
                            AccBufForwardRPI,
                            PaymentOb_1part.Department, 1,
                            PM_READIED,
                            @DublPayment_ob, TmpPaymentID) != 0)
                        MsgBox("Ошибка при создании платежа по измнению номинала БА");
                        return 1;
                  end;
                    
                  DublPayment_ob.Netting = PaymentOb_1part.Netting;
                end;
              end;
              TmpPaymentID = TmpPaymentID - 1;
            end;

            if( ActuatePaymentRq = ((FD_2.ПлатежТреб.rec.PaymentID > 0) and (PaymentOb_2part.PaymStatus != PM_CLOSED_W_M_MOVEMENT) and (PaymentOb_2part.ValueDate >= ДатаИсполненияШага)))
              PaymentOb_2part.BaseAmount = FD_2.СуммаТребования();
              PaymentOb_2part.OrderAmount = FD_2.СуммаТребования();
              PaymentOb_2part.ReceiverAmount = FD_2.СуммаТребования();
              PaymentOb_2part.Actuate();
            end;
          end;
        end;

        if (КсТреб != 0)
          if (FD_2.ПоставочныйКонтракт())
            if ((Исполнение1ч_done == 1) AND (Исполнение1ч_undone == 0))
              if(КсТреб > 0)
                if ( ПлатежиПоИзменениям(doc, FD_2, "ХБ", КсТреб, FD_2.ВалютаТребования_(), ДатаИсполненияШага, @AccBufForwardRPI) )
                  return 1;
                end;
                if (FD_2.ПлатежТреб.rec.PaymentID > 0)
                  if( DVND_CreatePayment(FD_2,
                            PaymentOb_2part.Purpose, PaymentOb_2part.DocKind, PaymentOb_2part.DocumentID,
                            ДатаИсполненияШага,
                            PaymentOb_2part.Payer, PaymentOb_2part.PayerBankID,
                            PaymentOb_2part.Receiver, PaymentOb_2part.ReceiverBankID,
                            abs(КсТреб), FD_2.ВалютаТребования_(),
                            "Изменение номинала КА",
                            null, 
                            AccBufForwardRPI,
                            PaymentOb_2part.Department, 1,
                            PM_READIED,
                            @DublPayment_rq, TmpPaymentID) != 0)
                        MsgBox("Ошибка при создании платежа по измнению номинала КА");
                        return 1;
                  end;
                    
                  DublPayment_rq.Netting = PaymentOb_2part.Netting;
                end;
              else
                if( FD.ПлатежТреб.rec.PaymentID > 0 )
                  PaymentRq_1part = RsbPayment(FD.ПлатежОб.rec.PaymentID);
                end;
                if ( ПлатежиПоИзменениям(doc, FD, "ХА", КсТреб, PaymentRq_1part.BaseFIID, ДатаИсполненияШага, @AccBufForwardRPI) )
                  return 1;
                end;
                if (FD.ПлатежТреб.rec.PaymentID > 0)
                  if( DVND_CreatePayment(FD,
                            PaymentRq_1part.Purpose, PaymentRq_1part.DocKind, PaymentRq_1part.DocumentID,
                            ДатаИсполненияШага,
                            PaymentRq_1part.Payer, PaymentRq_1part.PayerBankID,
                            PaymentRq_1part.Receiver, PaymentRq_1part.ReceiverBankID,
                            abs(КсТреб), PaymentRq_1part.BaseFIID,
                            "Изменение номинала КА",
                            AccBufForwardRPI, 
                            null,
                            PaymentRq_1part.Department, 1,
                            PM_READIED,
                            @DublPayment_rq, TmpPaymentID) != 0)
                        MsgBox("Ошибка при создании платежа по измнению номинала КА");
                        return 1;
                  end;
                    
                  DublPayment_rq.Netting = PaymentRq_1part.Netting;
                end;
              end;
              TmpPaymentID = TmpPaymentID - 1;
            end;
              

            if( ActuatePaymentOb = ((FD_2.ПлатежОб.rec.PaymentID > 0) and (PaymentRq_2part.PaymStatus != PM_CLOSED_W_M_MOVEMENT) and (PaymentRq_2part.ValueDate >= ДатаИсполненияШага)))
              PaymentRq_2part.BaseAmount = FD.СуммаОбязательства();
              PaymentRq_2part.OrderAmount = FD.СуммаОбязательства();
              PaymentRq_2part.PayerAmount = FD.СуммаОбязательства();
              PaymentRq_2part.Actuate();
            end;

            if (ActuatePaymentRq OR ActuatePaymentOb)
              if ( АктуализироватьПлатежи(FD_2, ДатаИсполненияШага) != 0 )
                return 1;
              end;
            end;
            if ((Исполнение1ч_done == 1))
              DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_CHANGE, ДатаИсполненияШага);
              if( not InsertOprStatus(DV_OPERSTATUS_EXEC_CHANGE_PMGR, DV_OPERSTATUS_EXEC_CH_PMGR_EXEC) )
                return SayError("Ошибка при установке статуса <Исполнение платежей по изменениям> для операции");
              end;
            else
              if( not InsertOprStatus(DV_OPERSTATUS_EXEC_CHANGE_PMGR, DV_OPERSTATUS_EXEC_CH_PMGR_NOTEXEC) )
                return SayError("Ошибка при установке статуса <Исполнение платежей по изменениям> для операции");
              end;
            end;
          end;

          if ( КорректировкаСуммыСделки(doc, FD_2, КсОбяз, КсТреб, ДатаИсполненияШага) != 0 )
            return 1;
          end;
        end;
        if(ReCalcPayments)
          ВыполнитьПереоценкуПоИзменениямПараметров( FD, FD_2, doc, ДатаИсполненияШага);
        end;
      end;
    end;
  end;

  if( DV_ChangeDVNDEAL(Dold, Dnew, Fold, Fnew, Fold_2, Fnew_2, ALG_DV_CHANGEKIND_CHANGE, ДатаИсполненияШага) != 0 )
     MsgBox("Ошибка при изменении условий сделки");
     return 1;
  end;

  if( ПроверитьНеобходимостьРепортингаВРепозитарии( FD.ID ) and ПроверитьИзменениеПараметровДляРепозУчета() )
    if ((FindUnloadedMessages(FD.ID)) or (FindUnloadedBulkMessages(FD.ID)))
      MsgBox("По сделке есть подготовленные, но не сгенерированные сообщения. Необходимо либо выполнить откат подготовки сообщения, либо выполнить для него генерацию");
      return 1;
    else
      var requestTimeout = 0;
      GetRegistryValue("РЕПОЗИТАРИЙ\\СРОК ОЖИД.ЗАПР. ПРИ КОМБ.ПОДТВ.", V_INTEGER, requestTimeout, null);
      var planDate = ДатаИсполненияШага;
      if( isPendingRequestDeal(FD.ID) and ( requestTimeout > 0 ) )
        planDate = GetDateAfterWorkDays(planDate, requestTimeout);
        if( DL_InsertGrDeal( DocKind, FD.ID, DLGR_TEMPL_CHANGEMSGWTHREQUEST, planDate, time(0,0,0), -1) )
          return 1;
        end;
      elif( DL_InsertGrDeal( DocKind, FD.ID, DLGR_TEMPL_CHANGEMSG, planDate, time(0,0,0), -1) )
        return 1;
      end;  
    end;
  end;

  return 0;
end;
