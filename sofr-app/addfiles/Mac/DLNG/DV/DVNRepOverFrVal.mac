/*
$Name:             DVNRepOverFrVal.mac
$Module:           Производные инструменты
$Description:      Отчет "Сделки, подлежащие переоценке ТСС"
*/
IMPORT "DVNRepOverFrVal_form.mac", "DvRepFun2.mac";

PRIVATE MACRO GetFactDate( DocKind:integer, DealID:integer, Symbol:string ):date
  var Sql, SqlQuery;

  /* найдём фактическую дату исполнения шага "Постановка на баланс ?ч" */
  /* шаг пользовательский и кроме как по символу найти не получается */
  var FactDate:date = date(0,0,0);
  Sql = RSDCommand( " SELECT step.t_Fact_Date " +
                    "   FROM doprstep_dbt step, doproper_dbt oper " +
                    "  WHERE oper.t_DocKind      = ? " +
                    "    AND oper.t_DocumentID   = lpad(?, 34, '0') " +
                    "    AND step.t_ID_Operation = oper.t_ID_Operation " +
                    "    AND step.t_Symbol       = ? " );
  Sql.NullConversion = true;
  Sql.addParam("", RSDBP_IN, DocKind);
  Sql.addParam("", RSDBP_IN, DealID);
  Sql.addParam("", RSDBP_IN, Symbol);
  Sql.execute();
  SqlQuery = TRsbDataSet(Sql);
  if( SqlQuery.MoveNext() and (SQL_ConvTypeDate(SqlQuery.Fact_Date) != null) )
     FactDate = SQL_ConvTypeDate(SqlQuery.Fact_Date);
  end;

  return FactDate;
END;

PRIVATE MACRO GetFactDate_2( DocKind:integer, DealID:integer, Symbol:string ):date
  var Sql, SqlQuery;

  var FactDate:date = date(0,0,0);
  Sql = RSDCommand( " SELECT step.t_Fact_Date  " +
                    "   FROM doprstep_dbt step, doproper_dbt oper " +
                    "  WHERE oper.t_DocKind      = ? " +
                    "    AND oper.t_DocumentID   = lpad(?, 34, '0') " +
                    "    AND step.t_ID_Operation = oper.t_ID_Operation " +
                    "    AND step.t_Symbol       = ? " +
                    "    AND exists( select 1 from doprstep_dbt next_step " +
                    "                 where next_step.t_ID_Operation = step.t_ID_Operation " +
                    "                   and next_step.t_Previous_Step = step.t_ID_STEP " +
                    "                   and next_step.t_Symbol in('Т','О') " +
                    "              ) "
                  );
  Sql.NullConversion = true;
  Sql.addParam("", RSDBP_IN, DocKind);
  Sql.addParam("", RSDBP_IN, DealID);
  Sql.addParam("", RSDBP_IN, Symbol);
  Sql.execute();
  SqlQuery = TRsbDataSet(Sql);
  if( SqlQuery.MoveNext() and (SQL_ConvTypeDate(SqlQuery.Fact_Date) != null) )
     FactDate = SQL_ConvTypeDate(SqlQuery.Fact_Date);
  end;

  return FactDate;
END;

PRIVATE VAR ReportHeader = "          Сделки, подлежащие переоценке ТСС на ##########\n";

PRIVATE VAR TableHeader =
"┌──────────────────┬────────────────┬──────────────┬──────────────┬─────────────────┬───────────────┬──────────────┐\n"+
"│ Номер по порядку │ Номер операции │ Вид операции │ Часть сделки │ Дата заключения │ Отчетная дата │ Дата расчета │\n"+
"├──────────────────┼────────────────┼──────────────┼──────────────┼─────────────────┼───────────────┼──────────────┤";

PRIVATE CLASS (CB_CReportTemplate) DV_NTaxReg_Report

  PRIVATE VAR NumReportLine:integer = 1;

  PRIVATE MACRO PrintMode():INTEGER
     if( m_Form.RepData.IsExportToExel == "X" )
        return DL_OUTREPORT_EXCEL;
     else
        return DL_OUTREPORT_STD;
     end;
  END;

  PRIVATE MACRO BeginReport()
     NumReportLine = 1;
     SetUseApp(m_Form.RepData.IsUseApp == "X");
     DL_CallInsertStat(PrintMode());
  END;

  PRIVATE MACRO EndReport()
     if( NumReportLine > 1 )
        SaveTotalBook();
     else
        MsgBox( "Нет данных, удовлетворяющих параметрам отчета." );
     end;
  END;

  PRIVATE MACRO CompDate( dat1:date, dat2:date ):string
     return IIF(dat1 == dat2, "+", "");
  END;

  PRIVATE MACRO DealSQLCondition()
    return " select ndeal.t_DocKind, ndeal.t_ID, ndeal.t_DVKind, ndeal.t_Date, ndeal.t_Code, nfi.t_FIID, nfi_2.t_FIID t_FIID_2, " +
           "        RSI_RsbCalendar.GetDateAfterWorkDay(nfi.t_ExecDate, 0) t_ExecDate, RSI_RsbCalendar.GetDateAfterWorkDay(nfi_2.t_ExecDate, 0) t_ExecDate_2 " +
           "   from ddvndeal_dbt ndeal, ddvnfi_dbt nfi, ddvnfi_dbt nfi_2 " +
           "  where ndeal.t_Date <= ? " +
           "    and ndeal.t_DocKind in(?,?) " +
           "    and ndeal.t_IsPFI = 'X' " +
           "    and nfi.t_DealID = ndeal.t_ID " +
           "    and nfi.t_Type   = ? " +
           "    and nfi.t_FairValueAlg > 0 " +
           "    and nfi_2.t_DealID(+) = ndeal.t_ID " +
           "    and nfi_2.t_Type(+)   = ? " +
           "    and (case when (ndeal.t_DVKind = ? or ndeal.t_DVKind = ?) then RSI_RsbCalendar.GetDateAfterWorkDay(GREATEST(nfi_2.t_PayDate, nfi_2.t_SuplDate),0) " +
           "              when ndeal.t_DVKind = ? then ( SELECT nvl(max(gr.t_PayDate), RSI_RsbCalendar.GetDateAfterWorkDay(nfi.t_ExecDate,0)) " +
           "                                               FROM DDVNPMGR_DBT gr " +
           "                                              WHERE gr.t_DealID = ndeal.t_ID " +
           "                                           ) " +
           "              else RSI_RsbCalendar.GetDateAfterWorkDay(nfi.t_ExecDate,0) end) >= ? " +
           " order by ndeal.t_ID ";
  END;

  PRIVATE MACRO Create()
     var NRec:integer = 0, Select, DataSet, Num:integer = 0;

     Select = DL_RSDCommand( DealSQLCondition() );
     Select.addParam(m_Form.RepData.RepDate);
     Select.addParam(DL_DVNDEAL);
     Select.addParam(DL_DVDEALT3);
     Select.addParam(DV_NFIType_BaseActiv);
     Select.addParam(DV_NFIType_BaseActiv2);
     Select.addParam(DV_CURSWAP);
     Select.addParam(DV_CURSWAP_FX);
     Select.addParam(DV_PCTSWAP);
     Select.addParam(m_Form.RepData.RepDate);

     NRec = Select.GetCount();
     if( NRec > 0 )
        DataSet = Select.execute();

        InitProgress(NRec, "Отчет \"Сделки, подлежащие переоценке ТСС\"", "Просмотр сделок");
        while( DataSet.MoveNext() )
           var Sql, SqlQuery;
           var Part:string = "", KindOper:string = "";

           /* определим дату расчёта */
           var CalcDate:date = date(0,0,0);
           if( (SQL_ConvTypeInteger(DataSet.DVKind) == DV_OPTION) or (SQL_ConvTypeInteger(DataSet.DVKind) == DV_FORWARD) or (SQL_ConvTypeInteger(DataSet.DVKind) == DV_FORWARD_T3) )
              CalcDate = SQL_ConvTypeDate(DataSet.ExecDate);
              KindOper = IIF(SQL_ConvTypeInteger(DataSet.DVKind) == DV_OPTION, "OPT", "FW");
           elif( (SQL_ConvTypeInteger(DataSet.DVKind) == DV_CURSWAP) or (SQL_ConvTypeInteger(DataSet.DVKind) == DV_CURSWAP_FX) )
              /* дата расчета по 1 части */
              var FactDate:date = GetFactDate( SQL_ConvTypeInteger(DataSet.DocKind), SQL_ConvTypeInteger(DataSet.ID), "и");

              if( (FactDate == date(0,0,0)) or (FactDate >= m_Form.RepData.RepDate) )
                 CalcDate = SQL_ConvTypeDate(DataSet.ExecDate);
                 Part = "1 часть";
              end;

              if( CalcDate == date(0,0,0) ) /* по первой части переоценки нет */
                 /* дата расчета по 2 части */
                 FactDate = GetFactDate_2(SQL_ConvTypeInteger(DataSet.DocKind), SQL_ConvTypeInteger(DataSet.ID), "И");

                 if( (FactDate == date(0,0,0)) or (FactDate >= m_Form.RepData.RepDate) )
                    CalcDate = SQL_ConvTypeDate(DataSet.ExecDate_2);
                    Part = "2 часть";
                 end;
              end;

              KindOper = IIF(SQL_ConvTypeInteger(DataSet.DVKind) == DV_CURSWAP,"FXSWAP","SWAP");
           elif( SQL_ConvTypeInteger(DataSet.DVKind) == DV_PCTSWAP )
              Sql = RSDCommand( " SELECT min(t_PayDate) MinPayDate " +
                                "   FROM DV_DVNPMGR " +
                                "  WHERE t_DealID  = ? " +
                                "    AND t_PayDate >= ? " );
              Sql.NullConversion = true;
              Sql.addParam("", RSDBP_IN, SQL_ConvTypeInteger(DataSet.ID));
              Sql.addParam("", RSDBP_IN, m_Form.RepData.RepDate);
              SqlQuery = TRsbDataSet( Sql );
              if( SqlQuery.MoveNext() and (SQL_ConvTypeDate(SqlQuery.MinPayDate) != null) )
                 CalcDate = SQL_ConvTypeDate(SqlQuery.MinPayDate);
              end;

              KindOper = IIF(SQL_ConvTypeInteger(DataSet.FIID) == SQL_ConvTypeInteger(DataSet.FIID_2), "IRS", "XCCY");
           end;

           /* дата заключения - SQL_ConvTypeDate(DataSet.Date) */

           /* определим отчётную дату */
           var RpDate = DV_GetLastDayInMonth(m_Form.RepData.RepDate);

           if((CalcDate == m_Form.RepData.RepDate) or (SQL_ConvTypeDate(DataSet.Date) == m_Form.RepData.RepDate) or (RpDate == m_Form.RepData.RepDate))

              if( NumReportLine == 1 ) /* печатаем первую строку */
                 CreateTotalBook();

                 PrintFormatString(ReportHeader, "H0_1", m_Form.RepData.RepDate);
                 CopyAllSheetInTotalBook(null, false, "N1_TableHeader", 0, null);

                 RegisterTable( "N1_TableLine", TableHeader,
                                "N1_А1",
                                "N1_А2",
                                "N1_А3",
                                "N1_А4",
                                "N1_А5",
                                "N1_А6",
                                "N1_А7" );
              end;

              PrintTableLine( "N1_А1", NumReportLine,                                                    null,
                              "N1_А2", SQL_ConvTypeStr(DataSet.Code),                                    null,
                              "N1_А3", KindOper,                                                         null,
                              "N1_А4", Part,                                                             null,
                              "N1_А5", CompDate(SQL_ConvTypeDate(DataSet.Date), m_Form.RepData.RepDate), null,
                              "N1_А6", CompDate(RpDate, m_Form.RepData.RepDate),                         null,
                              "N1_А7", CompDate(CalcDate, m_Form.RepData.RepDate),                       null );

              NumReportLine = NumReportLine + 1;
           end;

           Num = Num + 1;
           UseProgress(Num);
        end;

        RemProgress();

        if( NumReportLine > 1 )
           EndTable();
           CopyAllSheetInTotalBook(null, false, GetTableRange(), 0, null);
        end;
     end;
  END;

  initCB_CReportTemplate(DV_NOverFrValRep_Panel(), "Report_DVNOverFrVal.xls");
END;

DV_NTaxReg_Report().run();
exit(1);
