/*
$Name:        dvdealsload.mac
$Module:      Производные инструменты
$Description: Генерация сделок ПИ для НТ
*/
import SfInter, DVInter, "DlRepForm_h.mac", "globals.mac", "dlquery.mac", "dlerr_log.mac";

PRIVATE VAR ErrLog = ErrorLoger();
PRIVATE VAR Rep;
PRIVATE VAR StartTime;

/*Номера полей в форме*/
PRIVATE CONST PNFLD_DEALSLOAD_DATE         = 0,
              PNFLD_DEALSLOAD_KINDCALC     = 1,
              PNFLD_DEALSLOAD_EXCHANGECODE = 2,
              PNFLD_DEALSLOAD_EXCHANGENAME = 3,
              PNFLD_DEALSLOAD_BROKERCODE   = 4,
              PNFLD_DEALSLOAD_BROKERNAME   = 5,
              PNFLD_DEALSLOAD_BROKERCONTR  = 6,
              PNFLD_DEALSLOAD_GENOURDEALS  = 7,
              PNFLD_DEALSLOAD_BLOCKCOUNT   = 8;

PRIVATE CONST H_KINDCALC      = 101,
              H_EXCHANGE_CODE = 102,
              H_EXCHANGE_NAME = 103,
              H_BROKER_CODE   = 104,
              H_BROKER_NAME   = 105,
              H_BROKER_CONTR  = 106,
              H_BLOCKCOUNT    = 107;

PRIVATE CONST DV_CONTRACTOR_MARKET = 1, // Биржа
              DV_CONTRACTOR_BROKER = 2; // Брокер

PRIVATE MACRO IIF( condition, value_true, value_false )
  if( condition )
    return value_true;
  else 
    return value_false;
  end;
END;

PRIVATE MACRO GetPartCode( PartyID:integer )
  var sqlPartcode, PartCode;

  sqlPartcode = RSDCommand("select t_Code from dpartcode_dbt " +
                           " where t_PartyID = ? " +
                           "   and t_CodeKind = 1");

  sqlPartcode.addParam("", RSDBP_IN, PartyID);
  sqlPartcode.execute();

  PartCode = TRsbDataSet(sqlPartcode);
  if( PartCode.MoveNext() )
     return PartCode.Code;
  end;

  return "";
END;

PRIVATE MACRO FldProc_KindCalc( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )
     if( Id == DV_CONTRACTOR_MARKET )
        return "Биржа";
     elif( Id == DV_CONTRACTOR_BROKER )
        return "Брокер";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DV_CONTRACTOR_MARKET;
     end;
     FldShowValue = Name(FldRealValue);
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( FldRealValue != DV_CONTRACTOR_BROKER )
        pThis.SetLinkedValue(H_BROKER_CODE, -1);
        DisableFields(pThis.Data(), PNFLD_DEALSLOAD_BROKERCODE);
        pThis.SetLinkedValue(H_BROKER_CONTR, -1);
     else
        EnableFields(pThis.Data(), PNFLD_DEALSLOAD_BROKERCODE);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, 32, 9, @FldShowValue, @FldRealValue,
                    Name(DV_CONTRACTOR_MARKET), DV_CONTRACTOR_MARKET,
                    Name(DV_CONTRACTOR_BROKER), DV_CONTRACTOR_BROKER
                  );
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_Exchange( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party    = TRecHandler("party.dbt");
  var partcode = TBFile("partcode.dbt",  "R", 0);
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = "";
        pThis.SetLinkedValue(H_EXCHANGE_NAME, "");
     else
        if( not ПолучитьСубъекта(FldRealValue, Party) )
           FldShowValue = GetPartCode(FldRealValue);
           pThis.SetLinkedValue(H_EXCHANGE_NAME, Party.rec.ShortName);
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = "";
     pThis.SetLinkedValue(H_EXCHANGE_NAME, "");
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     Party = TRecHandler("party.dbt");
     if( ListPT(Party, 1, "", PTLIST_MARKETPLASE, PTCK_CONTR) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = 1;
        if( partcode.GetEQ() )
           FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue(H_EXCHANGE_NAME, Party.rec.Name);
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_Broker( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party    = TRecHandler("party.dbt");
  var partcode = TBFile("partcode.dbt",  "R", 0);
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = "";
        pThis.SetLinkedValue(H_BROKER_NAME, "");
        DisableFields(pThis.Data(), PNFLD_DEALSLOAD_BROKERCONTR);
     else
        if( not ПолучитьСубъекта(FldRealValue, Party) )
           FldShowValue = GetPartCode(FldRealValue);
           pThis.SetLinkedValue(H_BROKER_NAME, Party.rec.ShortName);
           EnableFields(pThis.Data(), PNFLD_DEALSLOAD_BROKERCONTR);
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = "";
     pThis.SetLinkedValue(H_BROKER_NAME, "");
     pThis.SetLinkedValue(H_BROKER_CONTR, -1);
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     pThis.SetLinkedValue(H_BROKER_CONTR, -1);
     if( FldRealValue > 0 )
        EnableFields(pThis.Data(), PNFLD_DEALSLOAD_BROKERCONTR);
     else
        DisableFields(pThis.Data(), PNFLD_DEALSLOAD_BROKERCONTR);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     Party = TRecHandler("party.dbt");
     if( ListPT(Party, 1, "", PTLIST_BROKER, PTCK_CONTR) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = 1;
        if( partcode.GetEQ() )
           FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue(H_BROKER_NAME, Party.rec.Name);
        pThis.SetLinkedValue(H_BROKER_CONTR, -1);
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_BrokerContr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var SfContr  = TRecHandler( "sfcontr.dbt" );
  var BrokerID =  -1;
  var ServKind = TArray();

  BrokerID = pThis.GetFieldValue(PNFLD_DEALSLOAD_BROKERCODE);

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = "";
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = "";
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     ServKind[ServKind.Size] = PTSK_DV;
     DL_ListSfContrByServKind(@FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), {OurBank}, ServKind, BrokerID);
  end;

  return CM_DEFAULT;
END;

MACRO FldProc_BlockCount( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     FldRealValue = 1;
     FldShowValue = 1;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( (FldShowValue == null) OR (FldShowValue < 1 ) )
        MsgBox( "Неверное значение поля");
        return CM_IGNORE;
     else
        FldRealValue = FldShowValue;
     end;
  end;

  return CM_DEFAULT;
END;


PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  end; 
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) DV_DealsLoad_Panel()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_DEALSLOAD_DATE,         DL_PNFLD_ENDDATE,  @DL_FldProc_EndDate,  {curdate} );
     AddFieldProc( 0, PNFLD_DEALSLOAD_KINDCALC,     H_KINDCALC,        @FldProc_KindCalc);
     AddFieldProc( 0, PNFLD_DEALSLOAD_EXCHANGECODE, H_EXCHANGE_CODE,   @FldProc_Exchange);
     AddFieldProc( 0, PNFLD_DEALSLOAD_EXCHANGENAME, H_EXCHANGE_NAME,   @Default, "");
     AddFieldProc( 0, PNFLD_DEALSLOAD_BROKERCODE,   H_BROKER_CODE,     @FldProc_Broker);
     AddFieldProc( 0, PNFLD_DEALSLOAD_BROKERNAME,   H_BROKER_NAME,     @Default, "");
     AddFieldProc( 0, PNFLD_DEALSLOAD_BROKERCONTR,  H_BROKER_CONTR,    @FldProc_BrokerContr);
     AddFieldProc( 0, PNFLD_DEALSLOAD_GENOURDEALS,  DL_PNFLD_CHECKBOX, @DL_FldProc_CheckBox, SET_CHAR );
     AddFieldProc( 0, PNFLD_DEALSLOAD_BLOCKCOUNT,   H_BLOCKCOUNT,      @FldProc_BlockCount, 1 );
  END;

  PRIVATE MACRO Check():BOOL
     
    if( this.GetFieldValue(PNFLD_DEALSLOAD_EXCHANGECODE) == -1 )
       msgbox("Необходимо указать биржу");
       return false;
    end;

    if( this.GetFieldValue(PNFLD_DEALSLOAD_KINDCALC) == DV_CONTRACTOR_BROKER )
       if( this.GetFieldValue(PNFLD_DEALSLOAD_BROKERCODE) == -1 )
          msgbox("Необходимо указать контрагента");
          return false;
       end;

       if( this.GetFieldValue(PNFLD_DEALSLOAD_BROKERCONTR) == -1 )
          msgbox("Необходимо указать договор с брокером");
          return false;
       end;
    end;
     
    return true;
  END;

  initDL_CPanel("dv.lbr", "DVDLOAD");
END;

private var Form = DV_DealsLoad_Panel();
private var FirstDate = date(0,0,0), GenOurDeals = "", BlockCount = 1, CalcOperCode:string = "";
private var Exchange = -1, Broker = -1, BrokerContr = -1;
private var BuyDealTypeF = 0, BuyDealTypeO = 0, SaleDealTypeF = 0, SaleDealTypeO = 0, РасчетыПоПИ = 0;
private var MarketSchemeID, МскБиржПИ:integer = 0, МскБиржКлирПИ:integer = 0, МскБиржВнеПИ:integer = 0, БрокерПИ:integer = 0;

private const countsteps = 4;
private var countdeals = 0;

private macro GetComissID( Code:string, ComissID:@integer )
  var query, cmd, DataSet;

  ComissID = 0;

  cmd = DL_RSDCommand();                                                                                              
  query = " select COMISS.* "+
          "   from DSFCOMISS_DBT COMISS "+
          "  where COMISS.t_Code = '"+Code+"'"+
          "    and COMISS.t_FeeType = ? ";
  cmd.addParam(SF_FEE_TYPE_PERIOD);
  DataSet = cmd.execute( query );
  if( DataSet.MoveNext() )
     ComissID = DataSet.ComissID;
  else
     ErrLog.LogError("Не найдена анкета комиссии с кодом \""+Code+"\".");
     return false;
  end;

  return true;
end;

private macro GenerateDeal( IsBuy,
                            FIID,          //ц/б
                            AvoirKind,
                            ClientID,      //идентификатор клиента
                            ClientContrID, //договор с клиентом
                            Date1,         //дата 1ч
                            Time0,         //время сделки
                            Amount,        //количество ц/б
                            Price,         //цена
                            TickCost,
                            Tick,
                            PriceMode
                          ):integer
  var stat:integer = 0;
  var dvdeal = TRecHandler("dvdeal.dbt");
  var ArrComSums = TArray();
  var dlcom = TRecHandler( "dvdlcom.dbt"  );

  dvdeal.rec.ID              = 0;
  dvdeal.rec.Kind            = IIF(AvoirKind == DERIVATIVE_OPTION, IIF(IsBuy, BuyDealTypeO, SaleDealTypeO), IIF(IsBuy, BuyDealTypeF, SaleDealTypeF));
  dvdeal.rec.Type            = IIF(IsBuy, "B", "S");
  dvdeal.rec.Date            = Date1;
  dvdeal.rec.Time            = Time0;
  dvdeal.rec.Date_CLR        = IIF(Time0 > Time(19,0,0), GetDateAfterWorkDays(Date1, 1), Date1); 
  dvdeal.rec.Code            = "";
  dvdeal.rec.ExtCode         = "";
  dvdeal.rec.FIID            = FIID;
  dvdeal.rec.Broker          = Broker;
  dvdeal.rec.Contractor      = -1;
  dvdeal.rec.Client          = ClientID;
  dvdeal.rec.Amount          = Amount;
  dvdeal.rec.Position        = 0;
  dvdeal.rec.Price           = Price; /*возможно тут надо сначала рассчитывать???*/
  dvdeal.rec.Point           = -1/*4*/;
  dvdeal.rec.Cost            = dvdeal.rec.Price * TickCost / IIF(Tick != 0, Tick, $1);
  dvdeal.rec.Bonus           = IIF(AvoirKind == DERIVATIVE_OPTION, 0.01 * dvdeal.rec.Cost * dvdeal.rec.Amount, 0);
  dvdeal.rec.Hedge           = "";
  dvdeal.rec.Department      = {OperDprt};
  dvdeal.rec.Oper            = {oper};
  dvdeal.rec.State           = 0;
  dvdeal.rec.PositionCost    = dvdeal.rec.Cost * dvdeal.rec.Amount;
  dvdeal.rec.PositionBonus   = 0/*dvdeal.rec.Bonus * TickCost * dvdeal.rec.Amount / IIF(Tick != 0, Tick, $1)*/;
  dvdeal.rec.Turn            = 0;
  dvdeal.rec.TurnAmount      = 0;
  dvdeal.rec.TurnCost        = 0;
  dvdeal.rec.IsTrust         = "";
  dvdeal.rec.OFBU            = "";
  dvdeal.rec.BrokerContr     = BrokerContr;
  dvdeal.rec.ClientContr     = ClientContrID;

  ArrComSums.Size = 0;
  var ArrComSums_i = 0, ComSum = $0;

  if( Broker > 0 )
     if( БрокерПИ > 0 )
       dlcom.Clear();
       dlcom.rec.ComissID = БрокерПИ;
       dlcom.rec.Sum = 0.01 * dvdeal.rec.Cost * dvdeal.rec.Amount;
       ArrComSums_i = ArrComSums.Size;
       ArrComSums(ArrComSums_i) = TRecHandler("dvdlcom");
       copy(ArrComSums(ArrComSums_i), dlcom);
     end;
  else
     if( МскБиржПИ > 0 )
       dlcom.Clear();
       dlcom.rec.ComissID = МскБиржПИ;
       dlcom.rec.Sum = 0.015 * dvdeal.rec.Cost * dvdeal.rec.Amount;
       ArrComSums_i = ArrComSums.Size;
       ArrComSums(ArrComSums_i) = TRecHandler("dvdlcom");
       copy(ArrComSums(ArrComSums_i), dlcom);
     end;

     if( МскБиржКлирПИ > 0 )
       dlcom.Clear();
       dlcom.rec.ComissID = МскБиржКлирПИ;
       dlcom.rec.Sum = 0.01 * dvdeal.rec.Cost * dvdeal.rec.Amount;
       ArrComSums_i = ArrComSums.Size;
       ArrComSums(ArrComSums_i) = TRecHandler("dvdlcom");
       copy(ArrComSums(ArrComSums_i), dlcom);
     end;

     if( МскБиржВнеПИ > 0 )
       dlcom.Clear();
       dlcom.rec.ComissID = МскБиржВнеПИ;
       dlcom.rec.Sum = 0.005 * dvdeal.rec.Cost * dvdeal.rec.Amount;
       ArrComSums_i = ArrComSums.Size;
       ArrComSums(ArrComSums_i) = TRecHandler("dvdlcom");
       copy(ArrComSums(ArrComSums_i), dlcom);
     end;
  end;

  stat = DVDealGateCreateNew(dvdeal, 0, РасчетыПоПИ, IIF(ClientID > 0, CalcOperCode + "/К", CalcOperCode + "/С"), IIF(Broker > 0, Broker, Exchange), IIF(Broker > 0, BrokerContr, MarketSchemeID), 0, 0, ArrComSums);

  if( stat )
     ErrLog.LogError("Ошибка создания сделки. " + GetErrMsg());
  end;

  return stat;
end;

//генерировать сделки по одной ц/б и клиенту
private macro GenerateDealsByFI( FIID:integer, AvoirKind:integer, TickCost:money, Tick:money, PriceMode:integer, ClientID:integer, ClientContrID:integer )

  //1.
  if( GenerateDeal( true,
                    FIID,          //код ц/б
                    AvoirKind,
                    ClientID,      //идентификатор клиента
                    ClientContrID, //договор с клиентом
                    FirstDate,     //дата 1ч
                    StartTime,     //время сделки
                    10000,         //количество ц/б
                    102.0,         //
                    TickCost,
                    Tick,
                    PriceMode
                  ) == 0 )
     countdeals = countdeals + 1;
  end;
  StartTime = StartTime + time(0,0,1);

  //2.
  if( GenerateDeal( true,
                    FIID,          //код ц/б
                    AvoirKind,
                    ClientID,      //идентификатор клиента
                    ClientContrID, //договор с клиентом
                    FirstDate,     //дата 1ч
                    StartTime,     //время сделки
                    12000,         //количество ц/б
                    101.5,         //
                    TickCost,
                    Tick,
                    PriceMode
                  ) == 0 )
     countdeals = countdeals + 1;
  end;
  StartTime = StartTime + time(0,0,1);

  //3.
  if( GenerateDeal( true,
                    FIID,          //код ц/б
                    AvoirKind,
                    ClientID,      //идентификатор клиента
                    ClientContrID, //договор с клиентом
                    FirstDate,     //дата 1ч
                    StartTime,     //время сделки
                    15000,         //количество ц/б
                    101.73,        //
                    TickCost,
                    Tick,
                    PriceMode
                  ) == 0 )
     countdeals = countdeals + 1;
  end;
  StartTime = StartTime + time(0,0,1);

  //4.
  if( GenerateDeal( false,
                    FIID,          //код ц/б
                    AvoirKind,
                    ClientID,      //идентификатор клиента
                    ClientContrID, //договор с клиентом
                    FirstDate,     //дата 1ч
                    StartTime,     //время сделки
                    20000,         //количество ц/б
                    102.0,         //
                    TickCost,
                    Tick,
                    PriceMode
                   ) == 0 )
     countdeals = countdeals + 1;
  end;
  StartTime = StartTime + time(0,0,1);

end;

private macro LoadDealParms():integer
  var query, DataSet;

  query =   " select m.t_ID "
          + "   from ddlmarket_dbt m "
          + "  where m.t_Market = " + Exchange
          + "    and m.t_ORCB = 'X' "
          + "    and m.t_IsTrust = CHR(0) "
          + "    and m.t_FI_Kind = " + FIKIND_DERIVATIVE;

  DataSet = TRsbDataSet(query);
  if(DataSet.moveNext())
    MarketSchemeID = DataSet.ID;
  else
    ErrLog.LogError("Не найдена схема расчетов с биржей");
    return 1;
  end;

  //Покупка фьючерсов
  query =   " select t_Kind_Operation "
          + "   from doprkoper_dbt "
          + "  where t_DocKind = " + DL_DVDEAL
          + "    and t_NotInUse = CHR(0) "
          + "    and instr(t_SysTypes, 'B') > 0 "
          + "    and instr(t_SysTypes, 'D') <= 0 "
          + "    and instr(t_SysTypes, 'U') > 0 ";
  DataSet = TRsbDataSet(query);
  if(DataSet.moveNext())
    BuyDealTypeF = DataSet.Kind_Operation;
  end;

  //Покупка опционов
  query =   " select t_Kind_Operation "
          + "   from doprkoper_dbt "
          + "  where t_DocKind = " + DL_DVDEAL
          + "    and t_NotInUse = CHR(0) "
          + "    and instr(t_SysTypes, 'B') > 0 "
          + "    and instr(t_SysTypes, 'D') <= 0 "
          + "    and instr(t_SysTypes, 'O') > 0 ";
  DataSet = TRsbDataSet(query);
  if(DataSet.moveNext())
    BuyDealTypeO = DataSet.Kind_Operation;
  end;

  //Продажа фьючерсов
  query =   " select t_Kind_Operation "
          + "   from doprkoper_dbt "
          + "  where t_DocKind = " + DL_DVDEAL
          + "    and t_NotInUse = CHR(0)"
          + "    and instr(t_SysTypes, 'S') > 0 "
          + "    and instr(t_SysTypes, 'D') <= 0 "
          + "    and instr(t_SysTypes, 'U') > 0 ";
  DataSet = TRsbDataSet(query);
  if(DataSet.moveNext())
    SaleDealTypeF = DataSet.Kind_Operation;
  end;

  //Продажа фьючерсов
  query =   " select t_Kind_Operation "
          + "   from doprkoper_dbt "
          + "  where t_DocKind = " + DL_DVDEAL
          + "    and t_NotInUse = CHR(0)"
          + "    and instr(t_SysTypes, 'S') > 0 "
          + "    and instr(t_SysTypes, 'D') <= 0 "
          + "    and instr(t_SysTypes, 'O') > 0 ";
  DataSet = TRsbDataSet(query);
  if(DataSet.moveNext())
    SaleDealTypeO = DataSet.Kind_Operation;
  end;

  //Расчеты по ПИ
  query =   " select t_Kind_Operation "
          + "   from doprkoper_dbt "
          + "  where t_DocKind = " + DL_DVOPER
          + "    and t_NotInUse = CHR(0)";
  DataSet = TRsbDataSet(query);
  if(DataSet.moveNext())
    РасчетыПоПИ = DataSet.Kind_Operation;
  end;

  if( Broker > 0 )
     GetComissID("БрокерПИ", @БрокерПИ);
  else
     GetComissID("МскБиржПИ", @МскБиржПИ);
     GetComissID("МскБиржКлирПИ", @МскБиржКлирПИ);
     GetComissID("МскБиржВнеПИ", @МскБиржВнеПИ);
  end;

  return 0;
end;

//генерировать сделки по всем ц/б 
private macro GenerateDeals()
  var query, cmd, DataSet;
  var count = 0, i = 0, j = 0;

  countdeals = 0;

  //в этом запросе специально есть несвязанные таблицы, чтобы перемножились выборки ц/б и клиентов
  query =   " select q.FIID, q.AvoirKind, q.TickCost, q.Tick, q.PriceMode, q.ClientID, q.ClientContrID "
          + "   from ( select fin.t_FIID as FIID, fin.t_AvoirKind as AvoirKind, Rsb_Derivatives.DV_TickCost(fin.t_FIID, ?) as TickCost, deriv.t_Tick as Tick, deriv.t_PriceMode as PriceMode, cl.t_PartyID as ClientID, "
          + "                 NVL((select c.t_ID "
          + "                        from dsfcontr_dbt c "
          + "                       where c.t_ServKind = " + PTSK_DV
          + "                         and c.t_PartyID = cl.t_PartyID "
          + "                         and c.t_DateBegin <= ? "
          + "                         and ROWNUM = 1"
          + "                     ), 0) as ClientContrID "
          + "            from dfininstr_dbt fin, dfideriv_dbt deriv, dclient_dbt cl "
          + "           where fin.t_FI_Kind = " + FIKIND_DERIVATIVE
          + "             and fin.t_IsClosed = CHR(0) "
          + "             and fin.t_DrawingDate > ? "
          + "             and fin.t_Issuer = " + Exchange
          + "             and deriv.t_FIID = fin.t_FIID "
          + "             and deriv.t_NotMarket = chr(0) "
          + "             and cl.t_ServiceKind = " + PTSK_DV
          + "             and cl.t_StartDate <= ? "
          + "             and cl.t_PartyID <> " + {OurBank}
          + "             and cl.t_Department = " + {OperDprt};

  if(GenOurDeals == SET_CHAR)
    query = query + "          UNION ALL"
          + "          select fin.t_FIID as FIID, fin.t_AvoirKind as AvoirKind, Rsb_Derivatives.DV_TickCost(fin.t_FIID, ?) as TickCost, deriv.t_Tick as Tick, deriv.t_PriceMode as PriceMode, -1 as ClientID, 0 as ClientContrID "
          + "            from dfininstr_dbt fin, dfideriv_dbt deriv "
          + "           where fin.t_FI_Kind = " + FIKIND_DERIVATIVE
          + "             and fin.t_IsClosed = CHR(0) "
          + "             and fin.t_DrawingDate > ? "
          + "             and fin.t_Issuer = " + Exchange
          + "             and deriv.t_FIID = fin.t_FIID "
          + "             and deriv.t_NotMarket = chr(0) ";
  end;

  query = query + ") q order by q.ClientID, q.FIID";
  
  cmd = DL_RSDCommand(query);

  cmd.AddParam(FirstDate);
  cmd.AddParam(FirstDate);
  cmd.AddParam(FirstDate);
  cmd.AddParam(FirstDate);
  cmd.AddParam(FirstDate);
  cmd.AddParam(FirstDate);

  count = cmd.GetCount();
  if( count > 0 )
    if( LoadDealParms() == 0 )
       InitProgress( count * countsteps * BlockCount, "Идет генерация сделок ПИ", "Генерация сделок ПИ" );

       DataSet = cmd.Execute();
       while( DataSet.moveNext() )
         StartTime = time(9,0,0);

         if( (int(DataSet.ClientID) != -1) and (DataSet.ClientContrID == 0) )
           ErrLog.LogError("Не найден договор обслуживания с клиентом PartyID = " + int(DataSet.ClientID));
         else
           j = 0;
           while( j < BlockCount )
             GenerateDealsByFI(DataSet.FIID, DataSet.AvoirKind, DataSet.TickCost, DataSet.Tick, DataSet.PriceMode, DataSet.ClientID, DataSet.ClientContrID); //для клиента по текущей ц/б

             UseProgress(i = i + countsteps);
             j = j + 1;
           end;
         end;
       end;

       RemProgress();

       ErrLog.LogError("В процессе выполнения создано " + countdeals + " сделок");
    end;
  else
    ErrLog.LogError("Не удалось отобрать данные для генерации сделок");
  end;

end;

while( Form.Run() )
  var OpDay, OpMon, OpYear, ShowValueExchange, ShowValueBroker;
  Rep = CMakeReport();
  ErrLog.InitErrLog(Rep, true);

  Rep.GetCurSheet().SheetName = "Сообщения";
  ErrLog.StartErrLog();

  FirstDate     = date(Form.GetFieldValue(PNFLD_DEALSLOAD_DATE));
  Exchange      = Form.GetFieldValue(PNFLD_DEALSLOAD_EXCHANGECODE, @ShowValueExchange);
  Broker        = Form.GetFieldValue(PNFLD_DEALSLOAD_BROKERCODE, @ShowValueBroker);
  BrokerContr   = Form.GetFieldValue(PNFLD_DEALSLOAD_BROKERCONTR);
  GenOurDeals   = Form.GetFieldValue(PNFLD_DEALSLOAD_GENOURDEALS);
  BlockCount    = Form.GetFieldValue(PNFLD_DEALSLOAD_BLOCKCOUNT);

  DateSplit(FirstDate, OpDay, OpMon, OpYear);

  if( Broker > 0 )
     CalcOperCode = ShowValueBroker + string(OpDay:2:o) + string(OpMon:2:o) + string(OpYear:4:o);
  else
     CalcOperCode = ShowValueExchange + string(OpDay:2:o) + string(OpMon:2:o) + string(OpYear:4:o);
  end;

  GenerateDeals();

  ErrLog.FinishErrLog();

  //Напечатать протокол
  ErrLog.ShowErrLog(true);
  Rep.SetZoomType( ZOOM_TYPE_A4L );// альбомная ориентация
  Rep.PrintWinRep();
  Rep.ShowWinRep();
end;

exit(1);
