/*
$Name:        dvnpmgr.mac
$Module:      Производные инструменты
$Description: Функции скроллинга графика платежей
*/
import oralib, likepy, BankInter;

private const SIDE_DEMAND = 2, //Требование
              SIDE_LIABILITY = 1, //Обязательство
              SIDE_NETTING = 0;


private macro Get_dvnpmgr (id, dvnpmgr, dvnpmgr_old)
  dvnpmgr.KeyNum = 0;
  dvnpmgr.rec.ID = ID;
  if (not dvnpmgr.GetEQ() )
    MsgBox("Не найден промежуточный платёж с ИД = " + ID);
    return false;
  end;

  copy(dvnpmgr_old, dvnpmgr);

  return true;
onerror()
  return false;
End;

macro ИзменитьСчетПП (ID:integer )
  file dvnpmgr_old ("dvnpmgr.dbt");
  var dvnpmgr = TbFile("dvnpmgr.dbt", "W");
  var account:string = "";
  var button = -1;

  if (not Get_dvnpmgr(ID, dvnpmgr, dvnpmgr_old) )
    return;
  end;

  if (not GetString(account, "Номер счёта для ПП c ID = " + ID, 20) )
    return;
  end;

  if (strlen(account) != 20)
    MsgBox("Длина счета должна быть 20 символов");
    return;
  end;

  if (IsDigitalNumber(account) != 0)
    MsgBox("Счет должен состоять только из цифр");
    return;
  end;

  if (dvnpmgr.rec.side == SIDE_NETTING)
    button = Confwin( MakeArray("Требование или обязательство?"), MakeArray("Требование", "Обязательство"));
  end;

  if ( (button == 0) or (dvnpmgr.rec.side == SIDE_DEMAND) ) //Требование
    dvnpmgr.rec.DemandAccount = account;
  elif ( (button == 1) or (dvnpmgr.rec.side == SIDE_LIABILITY) ) //Обязательство
    dvnpmgr.rec.LiabilityAccount = account;
  end;

  if ( ((dvnpmgr.rec.DemandAccount != dvnpmgr_old.DemandAccount) or (dvnpmgr.rec.LiabilityAccount != dvnpmgr_old.LiabilityAccount)) and dvnpmgr.Update() )
    WriteFiscLog ( OLupdate, dvnpmgr_old, dvnpmgr_old, dvnpmgr );
  /*Simanov. 14.11.2019. Не нашёл актуальных счетов для t_dockind = 4811. Закомментарил, т.к. это должно быть не актуально
    ExecSql("update dmcaccdoc_dbt set t_account = :newacc where t_dockind = 4811 and t_docid = :docid and t_account = :oldacc",
            MakeArray(SqlParam("newacc", account), SqlParam("docid", ID), SqlParam("oldacc", oldaccount)));
  */
  end;
End;

macro ИзменитьСуммуПлатежа (ID:integer)
  file dvnpmgr_old ("dvnpmgr.dbt");
  var dvnpmgr = TbFile("dvnpmgr.dbt", "W");
  var sum:money = $0;
  var button:integer = -1;

  if (not Get_dvnpmgr(ID, dvnpmgr, dvnpmgr_old) )
    return;
  end;

  if (dvnpmgr.rec.side == SIDE_NETTING)
    button = Confwin( MakeArray("Выберите направление"), MakeArray("Сумма требования", "Сумма обязательства"));
  end;

  if (not GetMoney(Sum, "Сумма платежа для ПП с ID = " + ID))
    return;
  end;

  if (dvnpmgr.rec.side == SIDE_NETTING)
    if (button == 0) //Требование
      dvnpmgr.rec.DemandSum = sum;
    elif (button == 1) //Обязательство
      dvnpmgr.rec.LiabilitySum = sum;
    end;
    dvnpmgr.rec.PaymentSum = dvnpmgr.rec.DemandSum - dvnpmgr.rec.LiabilitySum;
  elif (dvnpmgr.rec.side == SIDE_LIABILITY)
    dvnpmgr.rec.LiabilitySum = sum;
    dvnpmgr.rec.PaymentSum   = sum;
  elif (dvnpmgr.rec.side == SIDE_DEMAND)
    dvnpmgr.rec.DemandSum  = sum;
    dvnpmgr.rec.PaymentSum = sum;
  end;

  if (dvnpmgr.Update() )
    WriteFiscLog ( OLupdate, dvnpmgr_old, dvnpmgr_old, dvnpmgr );
  end;

onerror()
  MsgBox("Ошибка при выполнении действия");
  return;
End;

macro ИзменитьДатуПлатежа (ID:integer)
  file dvnpmgr_old ("dvnpmgr.dbt");
  var dvnpmgr = TbFile("dvnpmgr.dbt", "W");

  if (not Get_dvnpmgr(ID, dvnpmgr, dvnpmgr_old) )
    return;
  end;

  if (not GetDate(dvnpmgr.rec.paydate))
    return;
  end;

  if ( (dvnpmgr.rec.paydate != dvnpmgr_old.paydate) and dvnpmgr.Update() )
    WriteFiscLog ( OLupdate, dvnpmgr_old, dvnpmgr_old, dvnpmgr );
  end;

onerror()
  MsgBox("Ошибка при выполнении действия");
  return;
End;

macro ИзменитьДатуПереноса (ID:integer)
  file dvnpmgr_old ("dvnpmgr.dbt");
  var dvnpmgr = TbFile("dvnpmgr.dbt", "W");

  if (not Get_dvnpmgr(ID, dvnpmgr, dvnpmgr_old) )
    return;
  end;

  if (not GetDate(dvnpmgr.rec.transferdate)) 
    return;
  end;

  if ( (dvnpmgr.rec.transferdate != dvnpmgr_old.transferdate) and dvnpmgr.Update() )
    WriteFiscLog ( OLupdate, dvnpmgr_old, dvnpmgr_old, dvnpmgr );
  end;

onerror()
  MsgBox("Ошибка при выполнении действия");
  return;
End;

macro ИзменитьДатуФиксинга (ID:integer)
  file dvnpmgr_old ("dvnpmgr.dbt");
  var dvnpmgr = TbFile("dvnpmgr.dbt", "W");

  if (not Get_dvnpmgr(ID, dvnpmgr, dvnpmgr_old) )
    return;
  end;

  if (not GetDate(dvnpmgr.rec.fixdate)) 
    return;
  end;

  if ( (dvnpmgr.rec.fixdate != dvnpmgr_old.fixdate) and dvnpmgr.Update() )
    WriteFiscLog ( OLupdate, dvnpmgr_old, dvnpmgr_old, dvnpmgr );
  end;

onerror()
  MsgBox("Ошибка при выполнении действия");
  return;
End;

MACRO Функция_Пользователя( DealID:integer, Type:integer, ID:integer )
  var mnu = tarray(), imnu = -2;

  mnu(mnu.size) = "Изменение счета ПП";
  mnu(mnu.size) = "Изменение сумм платежа";
  mnu(mnu.size) = "Изменение даты платежа";
  mnu(mnu.size) = "Изменение даты переноса";
  mnu(mnu.size) = "Изменение даты фиксинга";

  if(mnu.size > 0)
    imnu = menu(mnu);
    if(imnu >= 0)
      if(mnu(imnu) == "Изменение счета ПП")
        ИзменитьСчетПП(ID);
      elif(mnu(imnu) == "Изменение сумм платежа")
        ИзменитьСуммуПлатежа(ID);
      elif(mnu(imnu) == "Изменение даты платежа")
        ИзменитьДатуПлатежа(ID);
      elif(mnu(imnu) == "Изменение даты переноса")
        ИзменитьДатуПереноса(ID);
      elif(mnu(imnu) == "Изменение даты фиксинга")
        ИзменитьДатуфиксинга(ID);
      end;
    end;
  end;

  return 0;
END;
