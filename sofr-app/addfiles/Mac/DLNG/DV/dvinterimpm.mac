/*
$Name:        dvinterimpm.mac
$Module:       
$Description:  
*/
/*Макрос может вызываться в транзакции !!!*/
import "dv_fun.mac";

PRIVATE CLASS ClFV( Дата_:Date, Val_:money )
  VAR Дата:date = Дата_,
      Val:money = Val_;
END;

PRIVATE MACRO GetFvPrev( Dat:date, pSide:integer, ClcSide:integer, pDVNDeal:TRecHandler, pDVNFI:TRecHandler, pDVNFI_2:TRecHandler ):money
  var RetVal:money = $0.0;
  var Query, RS;

  Query = RSDCommand( " SELECT t_Date, t_Sum " +
                      "   FROM ddvnfvh_dbt   " +
                      "  WHERE t_DealID  = ? " +
                      "    AND t_Side    = ? " +
                      "    AND t_Date    < ? " +
                      " ORDER BY t_Date desc " );
  Query.addParam( "", RSDBP_IN, pDVNDeal.rec.ID );
  Query.addParam( "", RSDBP_IN, pSide );
  Query.addParam( "", RSDBP_IN, Dat );
  RS = TRsbDataSet( Query );

  if( RS.MoveNext() )
     RetVal = SQL_ConvTypeSum(RS.Sum);
  else
     if( ClcSide == ALG_DV_PMGR_SIDE_LIABILITY )
        RetVal = pDVNFI.rec.Amount;
     else
        RetVal = pDVNFI_2.rec.Amount;
     end;
  end;

  return RetVal;
END;

private macro ПИ_ДнейВГоду_сУчетомБазисаРасчета(pDate:date, ClcSide:integer, pDVNFI:TRecHandler, pDVNFI_2:TRecHandler):INTEGER
  /* количество дней в году с учетом базиса */
  MACRO DaysInYearByBasis( year:INTEGER, Basis:INTEGER ):INTEGER
    VAR NumDays;

    if( Basis == DV_BASIS_ACTACT )
       NumDays = DATE(1,1,year+1) - DATE(1,1,year);
    elif( (Basis == DV_BASIS_30360) OR
          (Basis == DV_BASIS_31360) OR
          (Basis == DV_BASIS_ACT360)
        )
       NumDays = 360;
    elif( Basis == DV_BASIS_ACT365 )
       NumDays = 365;
    else
       MsgBox( "Не определен базис для расчета количества дней в году." );
       NumDays = 365;
    end;
    return NumDays;
  END;

  Var YYYY :Integer;
  DateSplit(pDate, NULL, NULL, YYYY);

  return DaysInYearByBasis(YYYY, IIF(ClcSide == ALG_DV_PMGR_SIDE_LIABILITY, pDVNFI.rec.Basis, pDVNFI_2.rec.Basis));
end;

private MACRO isLeapYear(dateOrYear) // Высокосный год
  var y: integer = 0;
  if ( V_INTEGER == ValType(dateOrYear) )
    y = dateOrYear;
  elif ( V_DATE == ValType(dateOrYear) )
    DateSplit(dateOrYear, NULL, NULL, y);
  else
    RunError ("isLeapYear() - invalid parametr type!");
  end;

  // m_GenAgr.Duration = (m_GenAgr.Start.year % 4 == 0 && m_GenAgr.Start.year % 100 != 0 || m_GenAgr.Start.year % 400 == 0) ? 366 : 365;
  return ( (mod(y,4) == 0) and (mod(y,100) != 0)  ) or
         ( mod(y,400) == 0 );
END;

private MACRO logicXOR(b1: bool, b2: bool) : bool
  if (
        ( (b1 == true) and (b2 == false) )  OR 
        ( (b1 == false) and (b2 == true)  )
  )
    return true;
  else
    return false;
  end;
END;

private MACRO calcP(Basis:Integer, d1:date, d2:date, G: Integer) : double
  var p: double = 0;
  var bLeapD1: bool = isLeapYear(d1);
  var bLeapD2: bool = isLeapYear(d2);
  // Если базис расчета Act/Act, и i-й период начисления попадает и на обычный, и на високосный годы
  // P = (T1/365 + T2/366), 
  if( (Basis == DV_BASIS_ACTACT) and logicXOR(bLeapD1, bLeapD2) )
    var T1: double = 0, T2: double = 0; var yyyy=0;
    DateSplit(d1, NULL, NULL, yyyy);
    T1 = (Date(1,1,yyyy+1) - d1)  / double(IIF(bLeapD1, 366, 365));
    DateSplit(d2, NULL, NULL, yyyy);
    T2 = (d2 - Date(1,1,yyyy))    / double(IIF(bLeapD2, 366, 365));
    p = T1 + T2;
  else
    p = (d2-d1)/double(G);
  end;

  return p;
END;

PRIVATE MACRO GetSumSideInterimPm( ID:integer, pDate:date, pSide:integer, ClcSide:integer, pDVNDeal:TRecHandler, pDVNFI:TRecHandler, pDVNFI_2:TRecHandler, FloatRateValue:@money, SrvOpDate:date, PMGRtmp:bool ):money

  var RetVal:money = $0.0;
  var Query, RS, Sql;
  var tmp: bool = false;

  if ( (PMGRtmp != NULL) and PMGRtmp )
     tmp = true;
  end;


  /* Определим запись из графика платежа */
  /* Так как сейчас можно указать период, например, один день, то записей может быть несколько */
  SQL = " SELECT Min(t_BegDate) as BegDate, Max(t_EndDate) as EndDate, Max(t_FixDate) as FixDate " +
        "   FROM " + IIF(tmp, " ddvnpmgr_tmp ", " ddvnpmgr_dbt ");

  if( ID > 0 )
     SQL = SQL + "  WHERE t_ID = ? ";
  else
     SQL = SQL + "  WHERE t_DealID  = ? " +
                 "    AND t_Side    = ? " +
                 "    AND t_PayDate = ? ";
  end;

  Query = RSDCommand(SQL);

  if( ID > 0 )
     Query.addParam( "", RSDBP_IN, ID );
  else
     Query.addParam( "", RSDBP_IN, pDVNDeal.rec.ID );
     Query.addParam( "", RSDBP_IN, pSide );
     Query.addParam( "", RSDBP_IN, pDate );
  end;
  RS = TRsbDataSet( Query );
  if( RS.MoveNext() )
     var Query2, RS2;
     var BegDate:date = SQL_ConvTypeDate(RS.BegDate),
         EndDate:date = SQL_ConvTypeDate(RS.EndDate),
         FixDate:date = SQL_ConvTypeDate(RS.FixDate);
     Query2 = RSDCommand( " SELECT t_Date, t_Sum " +
                          "   FROM ddvnfvh_dbt   " +
                          "  WHERE t_DealID  = ? " +
                          "    AND t_Side    = ? " +
                          "    AND t_Date   >= ? " +
                          "    AND t_Date    < ? " );
     Query2.addParam( "", RSDBP_IN, pDVNDeal.rec.ID );
     Query2.addParam( "", RSDBP_IN, pSide );
     Query2.addParam( "", RSDBP_IN, BegDate );
     Query2.addParam( "", RSDBP_IN, EndDate );
     RS2 = TRsbDataSet( Query2 );

     /*собираем всё в массив*/
     var FV = TArray(); FV.size = 0;
     while( RS2.MoveNext() )
        var Дата:date = SQL_ConvTypeDate(RS2.Date);
        if( (FV.Size == 0) and (Дата != BegDate) )
           FV[FV.Size] = ClFV(BegDate, GetFvPrev(BegDate, pSide, ClcSide, pDVNDeal, pDVNFI, pDVNFI_2));
        else
           FV[FV.Size] = ClFV(Дата, SQL_ConvTypeSum(RS2.Sum));
        end;
     end;

     if( FV.Size == 0 ) /* записей в истории изменения номинала не было */
        FV[FV.Size] = ClFV(BegDate, GetFvPrev(BegDate, pSide, ClcSide, pDVNDeal, pDVNFI, pDVNFI_2));
     end;

     FV[FV.Size] = ClFV(EndDate, 0);

     var FixSide:bool = DV_NDealSideFix(ClcSide, pDVNDeal);
     var RDate:date = FixDate;

     if( not FixSide )
        if( pDvnDeal.rec.State == DVDEAL_PREP )
           var Query3 = RSDCommand( " SELECT min(t_FixDate) FixD " +
                                    "   FROM " +IIF(tmp," ddvnpmgr_tmp "," ddvnpmgr_dbt ")+
                                    "  WHERE t_DealID = ? " );
           Query3.addParam("", RSDBP_IN, pDVNDeal.rec.ID);
           var RS3 = TRsbDataSet( Query3 );
           if( RS3.movenext() and (SQL_ConvTypeDate(RS3.FixD) < pDVNDeal.rec.Date) )
              RDate = SQL_ConvTypeDate(RS3.FixD);
           else
              RDate = pDVNDeal.rec.Date;
           end;
        else
           if( (SrvOpDate != NULL) and (SrvOpDate < FixDate) )
              RDate = SrvOpDate;
           end;
        end;
     end;

     var R:double = GetDvNRate(RDate, ClcSide, pDVNDeal, pDVNFI, pDVNFI_2);
     var G:integer = ПИ_ДнейВГоду_сУчетомБазисаРасчета(pDate, ClcSide, pDVNFI, pDVNFI_2); /*SCR #186581*/
     var Basis = IIF(ClcSide == ALG_DV_PMGR_SIDE_LIABILITY, pDVNFI.rec.Basis, pDVNFI_2.rec.Basis);

     var i = 0;
     while(i < FV.Size-1)
        //RetVal = RetVal + (FV[i].Val * (FV[i+1].Дата - FV[i].Дата) / G * R);
        RetVal = RetVal + ( FV[i].Val * calcP(Basis, FV[i].Дата, FV[i+1].Дата, G) * R ); 
        i = i + 1;
     end;

     if( not FixSide ) /* возвращаем плавающую ставку по которой был расчёт */
        FloatRateValue = R;
     end;
  end;

  return round(RetVal, 2);
END;

MACRO DVCalcInterimPm( pID:integer, pDate:date, pSide:integer, pDVNDeal:TRecHandler, pDVNFI:TRecHandler, pDVNFI_2:TRecHandler, SumPm:TRecHandler, SrvOpDate:date, PMGRtmp:bool ):integer
  var Сумма:money = $0.0, СуммаОб:money = $0.0, СуммаТр:money = $0.0;
  var FloatRateValue:money = $0.0;

  if( (pSide == ALG_DV_PMGR_SIDE_UNDEF) or (pSide == ALG_DV_PMGR_SIDE_LIABILITY) )
     СуммаОб = GetSumSideInterimPm(pID, pDate, pSide, ALG_DV_PMGR_SIDE_LIABILITY, pDVNDeal, pDVNFI, pDVNFI_2, @FloatRateValue, SrvOpDate, PMGRtmp);
  end;

  if( (pSide == ALG_DV_PMGR_SIDE_UNDEF) or (pSide == ALG_DV_PMGR_SIDE_DEMAND) )
     СуммаТр = GetSumSideInterimPm(pID, pDate, pSide, ALG_DV_PMGR_SIDE_DEMAND, pDVNDeal, pDVNFI, pDVNFI_2, @FloatRateValue, SrvOpDate, PMGRtmp);
  end;

  if( pSide == ALG_DV_PMGR_SIDE_UNDEF )
     Сумма = СуммаТр - СуммаОб;
  elif( pSide == ALG_DV_PMGR_SIDE_LIABILITY )
     Сумма = СуммаОб;
  elif( pSide == ALG_DV_PMGR_SIDE_DEMAND )
     Сумма = СуммаТр;
  end;

  SumPm.rec.DemandSum      = СуммаТр;
  SumPm.rec.LiabilitySum   = СуммаОб;
  SumPm.rec.PaymentSum     = Сумма;
  SumPm.rec.FloatRateValue = (FloatRateValue * 100.0); /* в % */

  return 0;
END;
