/*
$Name:        dlsettl020.mac
$Module:      Ядро Securities
$Description: Операция "Расчеты с биржей", Шаг Перевод на клиринговый счет с корсчета
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac", "dv_carop.mac";

PRIVATE VAR Dl_ValueData = TRecHandler( "dl_value.dbt");

MACRO ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step )

  record comm( dl_comm );
  var    FD; 

  SetBuff( comm, FirstDoc );
  FD = DVFirstDocDLCOMM( comm );

  var OurAcc = TRecHandler("account"),
      DebProp = TRecHandler("pmprop.dbt"),
      FI = TRecHandler("fininstr.dbt"), 
      PaymentObj:RsbPayment;

  if( ПолучитьФинИн( Dl_ValueData.rec.SumFIID, FI ) != 0 )
     MsgBox("Не найден финансовый инструмент FIID = " + String(Dl_ValueData.rec.SumFIID));
     return 1;
  end;

  if( (FD.ВидБиржевогоРынка() == DV_MARKETKIND_ALL) AND (Dl_ValueData.rec.SumFIID == NATCUR) AND (FD.comm.rec.OperSubKind == ALG_SP_MONEY_SOURCE_OWN) ) // Golovkin на счет 30424 ЕП 
     if( not FD.IsExistAccount("+Обеспечение", null, true, null, OurAcc, null, Dl_ValueData.rec.Date, Dl_ValueData.rec.SumFIID) )
        MsgBox("Ошибка: не открыт счет категории \"+Обеспечение\" в валюте FIID = " + String(Dl_ValueData.rec.SumFIID));
        return 1;
     end;
  elif (FD.comm.rec.partyofficeid == 9)
     if( not FD.IsExistAccount( "Клиринговый счет", null, null, null, OurAcc, MC_PAIRACCMODE_AUTO, Dl_ValueData.rec.Date, Dl_ValueData.rec.SumFIID ) )
        MsgBox("Ошибка: ! не открыт счет категории \"Клиринговый счет\" в валюте FIID = " + String(Dl_ValueData.rec.SumFIID));
     end;
  elif(Dl_ValueData.rec.SumFIID == NATCUR)
     if( not FD.IsExistAccount("Клиринговый счет", null, true, DV_GetSettlFiRoleByOperSubKind(comm.OperSubKind,false), OurAcc, null, Dl_ValueData.rec.Date, Dl_ValueData.rec.SumFIID) )
        MsgBox("Ошибка: не открыт счет категории \"Клиринговый счет\" в валюте FIID = " + String(Dl_ValueData.rec.SumFIID));
        return 1;
     end;
  else
     if( not FD.IsExistAccount("+Биржа", null, true, null, OurAcc, null, Dl_ValueData.rec.Date, Dl_ValueData.rec.SumFIID) )
        MsgBox("Ошибка: не открыт счет категории \"+Биржа\" в валюте FIID = " + String(Dl_ValueData.rec.SumFIID));
        return 1;
     end;
  end;


  if( DVND_CreatePayment( FD, PM_PURP_ORDER, FD.Kind, FD.ID,
                          Dl_ValueData.rec.Date,
                          {ourbank}, {ourbank},
                          Comm.PartyID, -1,
                          Dl_ValueData.rec.Sum, Dl_ValueData.rec.SumFIID,
                          //"Перевод средств на клиринговый счет с корсчета",
                          ОснованиеПроводкиПИ(DV_GRNUM_TRANS_IN_CLIR) + IIF(GetCodeParty("АО СПВБ",PTCK_CONTR) == FD.comm.rec.PartyID, " " + GetCodeParty(FD.comm.rec.PartyID,PTCK_CONTR), ""),
                          OurAcc, NULL,
                          comm.Division, NULL, NULL,
                          @PaymentObj, NULL,
                          @DebProp
                        ) != 0 )
     return 1;
  end;

  if( not(ПИ_ИнтегрРежимРаботы() and ПИ_ЭтоВнешнийВходящийПлатеж(PaymentObj)) or (FI.rec.FI_Kind == FIKIND_METAL) )
     if( not ПИ_ПроводкаПоПлатежу(FD, PaymentObj) )
        return 1;
     end;
   
     if( ПИ_ИсполнитьПлатеж(PaymentObj) )
        return 1;
     end;
  end;

  if( FD.IsClient() and ПИ_ВестиВнутреннийУчет() and
      ((ВУ_ПроводкиВводаВыводаДСНаОРЦБ() == 1) or (ВУ_ПроводкиВводаВыводаДСНаОРЦБ() == 3))
    )
     if( not ПИ_ВУ_РасчетыПереводНаКлирСчетКлиенту(FD, Doc, Dl_ValueData.rec.SumFIID) )
        return 1;
     end;
  else
    if( Dl_ValueData.rec.SumFIID != NATCUR )
       FD.SetBankID(GetCorrID(DebProp.rec.CorSchem, Dl_ValueData.rec.SumFIID, FIKIND_CURRENCY));
    end;
    FD.SetParametr(MC_TYPE_PARAMETR_FIID, Dl_ValueData.rec.SumFIID);
    if( not ПроводкаПоКатегориямВнутреннегоУчета( 2,
                                                      FD,
                                                      doc,
                                                      Dl_ValueData.rec.Date,
                                                      Dl_ValueData.rec.SumFIID,
                                                      Dl_ValueData.rec.Sum
                                                    )
          )
           return 1;
        end;
  end;

  return 0;
end;
