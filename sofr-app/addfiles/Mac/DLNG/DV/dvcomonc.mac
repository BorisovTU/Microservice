/*
$Name:        dvcomonc.mac
$Module:      Производные инструменты
$Description: Определение базовых величин для расчета единовременных комиссий в ПИ
*/
import "dvcomalg.mac", SfInter;

var MacroError:integer = 0;

/* Получить базовую сумму для расчета единовременной комиссии
       docKind - Тип перв.документа
           Doc - Буфер перв.документа
     sfalg_adr - Алгоритм расчета
   sfcontr_adr - Договор обслуживания*/
macro CalcCommissionSum( docKind, Doc, sfalg_adr, sfcontr_adr )

   file   fAlgBase( "sfcalcal.dbt" );
   record rAlgBase( "sfcalcal.dbt" );
   record rAlg( "sfclusr.str" );
   record rBaseSum( "sfbassum.str" );
   record rDeal( dvdeal );
   var    AlgName;          

   /*прочитаем переменную часть алгоритма*/
   SetBuff( rAlgBase, sfalg_adr );
   ClearRecord(fAlgBase);
   fAlgBase.FeeType     = rAlgBase.FeeType;
   fAlgBase.CommNumber  = rAlgBase.CommNumber;
   fAlgBase.Kind        = rAlgBase.Kind;
   fAlgBase.Number      = rAlgBase.Number;
   if( GetEQ(fAlgBase) == false )
      MacroError = 1;
      return 0;  /* алгоритм в файле не найден */
   end;
   SetRecordAddr(rAlg, fAlgBase);

   SetBuff(rDeal, Doc);

   ClearRecord(rBaseSum);

   AlgName = АлгЕдиноврКомиссии(rDeal.ID);

   if( AlgName == COMM_ALG_ERROR )
      MacroError = 1;
      return;
   end;

   if( (AlgName == rAlg.Name) OR
       ((AlgName == COMM_ALG_AB) AND ((rAlg.Name == COMM_ALG_A) OR (rAlg.Name == COMM_ALG_B))) OR
       ((AlgName == COMM_ALG_ABC) AND ((rAlg.Name == COMM_ALG_A) OR (rAlg.Name == COMM_ALG_B) OR (rAlg.Name == COMM_ALG_C)))
     )
      rBaseSum.baseType   = SF_BASETYPE_QUONT;
      rBaseSum.baseType2  = SF_BASETYPE_QUONT;
      rBaseSum.baseQuont  = rDeal.Amount;
      rBaseSum.baseQuont2 = rDeal.Amount;
   end;

   if( InsertSumList(rBaseSum) )
      MacroError = 1;
      MsgBox("Ошибка при вставке базовой суммы");
   end;

   return;
end;

macro CalcCommissionSumBatch( CalCalID:integer ):bool
   var stat = true;

   var cmd = RSDCommand(" INSERT INTO dsfbasesum_tmp ( t_feetype, t_sfdefid, t_calcalid, t_concomid, t_basetype, " +
                        "                              t_basesum, t_basequont, t_fiid_basesum, t_basetype2, t_basesum2, " +
                        "                              t_basequont2, t_fiid_basesum2, t_baseobjecttype, t_baseobjectid, " +
                        "                              t_commsum, t_docnum, t_docdate, t_docsum, t_docfiid, " +
                        "                              t_docpayeraccount, t_docreceiveraccount ) " +
                        " SELECT ?, parms.t_sfdefid, parms.t_calcalid, parms.t_concomid, ?, " +
                        "        0, RSB_Derivatives.DV_GetBaseQuont(TO_NUMBER(rule.t_DocumentID), parms.t_CalCalID), 0, ?, 0, " +
                        "        RSB_Derivatives.DV_GetBaseQuont(TO_NUMBER(rule.t_DocumentID), parms.t_CalCalID), 0, 0, chr(1), " +
                        "        0, '0', TO_DATE ('01010001', 'DDMMYYYY'), 0, 0, CHR(1), CHR(1) " +
                        "   FROM dsfbasesumparms_tmp parms, druledefobjcom_tmp rule " +
                        "  WHERE rule.t_ID_Operation = parms.t_ID_Operation " +
                        "    AND rule.t_ID_Step      = parms.t_ID_Step "// +
                        //"    AND parms.t_CalCalID    = ? "
                       );

   cmd.addParam("", RSDBP_IN, SF_FEE_TYPE_SINGLE);
   cmd.addParam("", RSDBP_IN, SF_BASETYPE_QUONT);
   cmd.addParam("", RSDBP_IN, SF_BASETYPE_QUONT);
   //cmd.addParam("", RSDBP_IN, CalCalID);

   cmd.execute();

   return stat;
end;
