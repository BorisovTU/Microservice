/*
$Name:        dvnop170.mac
$Module:      Производные инструменты
$Description: Внебиржевая операция. Шаг: Промежуточный платеж расч.
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac";

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  SetBuff( ndeal, FDoc );
  var FD = DVFirstDocNDeal(DocKind, ndeal);
  if (FD.ОтказОтИсполнения())
     return 0;
  end;
  var ДатаИсполненияШага:date, KvitNetting:bool = false;
  GetOprDate( DV_NDEAL_OPRDATE_PAYM, ДатаИсполненияШага );

  if( not FD.ВыполненаПереоценкаПоИзменениям(ДатаИсполненияШага) )
     if( MsgBoxEx( "Не выполнена переоценка по изменениям!|Продолжить?", MB_YES+MB_NO, IND_NO ) == IND_NO )
        return 1;
     end;
  end;

  if( ПромежуточныйПлатеж( FD, doc, ДатаИсполненияШага, DV_CALCOP_DIRECTION_PAYMENT, @KvitNetting ) != 0 )
     return 1;
  end;

  if( KvitNetting )
     if( not InsertOprStatus(DV_OPERSTATUS_KIND_NETTING_PAYM, DV_OPERSTATUS_PAYM_EX) )
        MsgBox("Ошибка при установке статуса <Квитовка/неттинг промежуточного платежа> для операции.");
        return 1;
     end;

     if( not InsertOprStatus(FD.DV_OPERSTATUS_PAYM(), DV_OPERSTATUS_PAYM_NOTEX) )
        MsgBox("Ошибка при установке статуса <Платеж> для операции.");
        return 1;
     end;
  else
     if( not InsertOprStatus(DV_OPERSTATUS_KIND_NETTING_PAYM, DV_OPERSTATUS_PAYM_NOTEX) )
        MsgBox("Ошибка при установке статуса <Квитовка/неттинг промежуточного платежа> для операции.");
        return 1;
     end;

     var DatePmGr:date = FD.GetDatePmGr(ALG_DV_PMGR_SIDE_UNDEF, ДатаИсполненияШага);
     if( DatePmGr == date(0,0,0) )
        if( not InsertOprStatus(FD.DV_OPERSTATUS_PAYM(), DV_OPERSTATUS_PAYM_NOTEX) )
           MsgBox("Ошибка при установке статуса <Платеж> для операции.");
           return 1;
        end;
     else
        DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_PAYM, DatePmGr );
        if( not InsertOprStatus(FD.DV_OPERSTATUS_PAYM(), DV_OPERSTATUS_PAYM_EX) )
           MsgBox("Ошибка при установке статуса <Платеж> для операции.");
           return 1;
        end;
     end;
  end;

  return 0;
end;

private macro GetPaymentInterim(DocKind:integer, DealID:integer, StepDate:date):RSBPayment
   var Payment;
   var query, cmd, DataSet;

   query = " SELECT T_PAYMENTID "
         +"   FROM DPMPAYM_DBT paym "
         +"  WHERE     paym.T_DOCKIND = ? "
         +"        AND paym.T_DOCUMENTID = ? "
         +"        AND paym.T_PURPOSE = ? "
         +"        AND paym.T_SUBPURPOSE = ? "
         +"        AND paym.T_VALUEDATE = ? ";
   cmd=DL_RSDCommand(query);
   cmd.addParam(DocKind);
   cmd.addParam(DealID);
   cmd.addParam(PM_PURP_INTERIM);
   cmd.addParam(1); // Оплата
   cmd.addParam(StepDate);
   DataSet = cmd.Execute();
   if (DataSet.MoveNext())
      Payment = RsbPayment(DataSet.PaymentId);
   end;

   return Payment;
end;

MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

   if (errTrn OR (CommitOrRollback==2)) 
         /* Произошла ошибка или происходит откат */
         return;
   end;

   var ДатаШага = DV_GetPlanDateStep(ID_Operation, ID_Step);
   
   record deal(dvndeal);

   SetBuff(deal, FirstDoc);

   var Payment = GetPaymentInterim(KindDoc, deal.Id, ДатаШага);
   if (Payment)
      Payment.Number = String(Payment.PaymentID);
      Payment.Update();
   end;

  return 1;
END;

