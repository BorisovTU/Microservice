
/* Алгоритм расчета справедливой стоимости для процентного СВОПа */

IMPORT "dvAlgCalcFrVal.mac";

PRIVATE MACRO GetPrRate(pRateID:integer, pDate:date, pDVKind:integer, pFIKind:integer, pPeriod:@integer, pPeriodKind:@integer ):bool

  var RetVal:bool = false;
  var Query, RS;

  pPeriod = pPeriodKind = 0;

  Query = RSDCommand( " SELECT DR.t_FiKind, DR.t_DvKind, DR.t_Number, DR.t_RateID, DR.t_Basis, DR.t_Period, DR.t_PeriodKind " +
                      "   FROM ddvprrate_dbt DR                     " +
                      "  WHERE (DR.t_DvKind = ? or DR.t_DvKind = ?) " +
                      "    AND (? = ? or DR.t_FiKind = ? or DR.t_FiKind = ?) " +
                      "    AND DR.t_RateID = ? " +
                      " ORDER BY DR.t_FiKind desc, DR.t_DvKind desc, DR.t_Number " );

  Query.addParam( "", RSDBP_IN, pDVKind );
  Query.addParam( "", RSDBP_IN, ALG_DV_KIND_PRRATE_UNDEF );
  Query.addParam( "", RSDBP_IN, pFIKind );
  Query.addParam( "", RSDBP_IN, ALG_DV_FIKIND_PRRATE_UNDEF );
  Query.addParam( "", RSDBP_IN, pFIKind );
  Query.addParam( "", RSDBP_IN, ALG_DV_FIKIND_PRRATE_UNDEF );
  Query.addParam( "", RSDBP_IN, pRateID );
  RS = TRsbDataSet( Query );
  if( RS.MoveNext() )
     pPeriod     = RS.Period;
     pPeriodKind = RS.PeriodKind;
     RetVal = true;
  end;

  return RetVal;
END;

/* Расчет СС требования и обязательства по графику платежей Процентного СВОП */
PRIVATE MACRO CalcPaynSum(pDEAL:TRecHandler, pSide:integer, pDate:date, pFix:bool, pFI:TRecHandler, CCxT:@double):double

  var CC:double = 0.0, T:double = 0.0;
  CCxT = 0;

  if( ПолучитьКурсЗаданногоTипа(@T, pFI.rec.FIID, NATCUR, DV_RATETYPE_CBR, pDate, true ) == false )
     MsgBox("Невозможно получить курс вида 'ЦБ РФ' для FIID = " + String(pFI.rec.FIID) + " на дату " + String(pDate));
     return 1;
  end;

  if( T != 0.0 )
     var pPeriod:integer = 0, pPeriodKind:integer = 0;
     var pStat:integer = 0;

     if( (pFix == true) or ((pFix == false) and (GetPrRate(pFI.rec.RateID, pDate, ALG_DV_KIND_PRRATE_PCTSWAP, ALG_DV_FIKIND_PRRATE_UNDEF, @pPeriod, @pPeriodKind) == true)) )

        var v_R:double, Dfp:double, R:double, RB:double, RE:double, v_Basis:integer = 4;
        var Query, RS;

        Query = RSDCommand( " SELECT t_BegDate, t_EndDate, t_FixDate " +
                            "   FROM ddvnpmgr_dbt  " +
                            "  WHERE t_DealID  = ? " +
                            "    AND (t_Side = ? OR t_Side = ?) " +
                            "    AND t_BegDate >= ? " +
                            " ORDER BY t_BegDate " );
        Query.addParam( "", RSDBP_IN, pDEAL.rec.ID );
        Query.addParam( "", RSDBP_IN, pSide );
        Query.addParam( "", RSDBP_IN, ALG_DV_PMGR_SIDE_UNDEF );
        Query.addParam( "", RSDBP_IN, pDate );
        RS = TRsbDataSet( Query );
        while( RS.MoveNext() )

           v_R = ALG_RF(pFI.rec.FIID, pDate, SQL_ConvTypeDate(RS.EndDate), pFI, ALG_DV_KIND_PRRATE_PCTSWAP, ALG_DV_FIKIND_PRRATE_CURRENCY, @v_Basis, @pStat);

           if( pStat != 0 )
              return 1;
           end;

           var cmd1 = RsdCommand(RslDefCon, "begin\n ? := RSB_Derivatives.DV_Years(?, ?, ?);\n end; ");
           cmd1.addParam("ret_val",  RSDBP_RETVAL, V_DOUBLE );
           cmd1.addParam("start_dt", RSDBP_IN,     pDate );
           cmd1.addParam("end_dt",   RSDBP_IN,     SQL_ConvTypeDate(RS.EndDate) );
           cmd1.addParam("basis",    RSDBP_IN,     v_Basis );
           cmd1.execute();
           
           Dfp = 1.0 / (1.0 + v_R/100.0 * SQL_ConvTypeSum(cmd1.Value(0)));
           
           var cmd2 = RsdCommand(RslDefCon, "begin\n ? := RSB_Derivatives.DV_Years(?, ?, ?);\n end; ");
           cmd2.addParam("ret_val",  RSDBP_RETVAL, V_DOUBLE );
           cmd2.addParam("start_dt", RSDBP_IN,     SQL_ConvTypeDate(RS.BegDate) );
           cmd2.addParam("end_dt",   RSDBP_IN,     SQL_ConvTypeDate(RS.EndDate) );
           cmd2.addParam("basis",    RSDBP_IN,     pFI.rec.Basis );
           cmd2.execute();
       
           if( pFix )
              R  = pFI.rec.Rate;
              CC = CC + pFI.rec.Amount * R/100.0 * SQL_ConvTypeSum(cmd2.Value(0)) * Dfp;
           else
              if( SQL_ConvTypeDate(RS.FixDate) == pDate )
                 if( not ПолучитьКурсЗаданногоTипа(@R, pFI.rec.RateID, FI_PRICE_POINT, 0, SQL_ConvTypeDate(RS.FixDate), true) )
                    MsgBox("Невозможно получить курс индекса с FIID = " + String(pFI.rec.RateID) + " на дату " + String(SQL_ConvTypeDate(RS.FixDate)));
                    return 1;
                 end;
                 R = R/100.0;
              else
                 /* определяем значение плавающей ставки на дату начала периода начисленияфиксации данного периода */
                 RB = ALG_RF(pFI.rec.FIID, pDate, SQL_ConvTypeDate(RS.FixDate), pFI, ALG_DV_KIND_PRRATE_PCTSWAP, ALG_DV_FIKIND_PRRATE_CURRENCY, null, @pStat);

                 if( pStat != 0 )
                    return 1;
                 end;

                 /* определяем значение плавающей ставки на дату окончания фиксации плюс периодичность */
                 var EndFDate:date = date(0,0,0);
                 if( pPeriodKind == ALG_DV_PERIODKIND_PRRATE_DAY )
                    EndFDate = DateAfterCalenDays(SQL_ConvTypeDate(RS.FixDate), pPeriod);
                 elif( pPeriodKind == ALG_DV_PERIODKIND_PRRATE_MONTH )
                    EndFDate = DateAfterCalenMonths(SQL_ConvTypeDate(RS.FixDate), pPeriod);
                 elif( pPeriodKind == ALG_DV_PERIODKIND_PRRATE_YEAR )
                    EndFDate = DateAfterCalenMonths(SQL_ConvTypeDate(RS.FixDate), 12*pPeriod);
                 else
                    MsgBox("Неизвестный период процентной ставки");
                    return 1;
                 end;
                 if( EndFDate == pDate )
                    if( not ПолучитьКурсЗаданногоTипа(@RE, pFI.rec.RateID, FI_PRICE_POINT, 0, EndFDate, true) )
                       MsgBox("Невозможно получить курс индекса с FIID = " + String(pFI.rec.RateID) + " на дату " + String(EndFDate));
                       return 1;
                    end;
                 else
                    RE = ALG_RF(pFI.rec.FIID, pDate, EndFDate, pFI, ALG_DV_KIND_PRRATE_PCTSWAP, ALG_DV_FIKIND_PRRATE_CURRENCY, null, @pStat);
                    if( pStat != 0 )
                       return 1;
                    end;
                 end;

                 /* определяем значение форвардной ставки */
                 var cmd3 = RsdCommand(RslDefCon, "begin\n ? := RSB_Derivatives.DV_Years(?, ?, ?);\n end; ");
                 cmd3.addParam("ret_val",  RSDBP_RETVAL, V_DOUBLE );
                 cmd3.addParam("start_dt", RSDBP_IN,     pDate );
                 cmd3.addParam("end_dt",   RSDBP_IN,     SQL_ConvTypeDate(RS.FixDate) );
                 cmd3.addParam("basis",    RSDBP_IN,     v_Basis );
                 cmd3.execute();
                
                 var cmd4 = RsdCommand(RslDefCon, "begin\n ? := RSB_Derivatives.DV_Years(?, ?, ?);\n end; ");
                 cmd4.addParam("ret_val",  RSDBP_RETVAL, V_DOUBLE );
                 cmd4.addParam("start_dt", RSDBP_IN,     pDate );
                 cmd4.addParam("end_dt",   RSDBP_IN,     EndFDate );
                 cmd4.addParam("basis",    RSDBP_IN,     v_Basis );
                 cmd4.execute();

                 var cmd5 = RsdCommand(RslDefCon, "begin\n ? := RSB_Derivatives.DV_Years(?, ?, ?);\n end; ");
                 cmd5.addParam("ret_val",  RSDBP_RETVAL, V_DOUBLE );
                 cmd5.addParam("start_dt", RSDBP_IN,     SQL_ConvTypeDate(RS.FixDate) );
                 cmd5.addParam("end_dt",   RSDBP_IN,     EndFDate );
                 cmd5.addParam("basis",    RSDBP_IN,     v_Basis );
                 cmd5.execute();

                 R  = ((1.0 + (RE/100.0) * SQL_ConvTypeSum(cmd4.Value(0))) / (1.0 + (RB/100.0) * SQL_ConvTypeSum(cmd3.Value(0))) - 1.0) / SQL_ConvTypeSum(cmd5.Value(0));
              end;

              var cmd6 = RsdCommand(RslDefCon, "begin\n ? := RSB_Derivatives.DV_Years(?, ?, ?);\n end; ");
              cmd6.addParam("ret_val",  RSDBP_RETVAL, V_DOUBLE );
              cmd6.addParam("start_dt", RSDBP_IN,     SQL_ConvTypeDate(RS.BegDate) );
              cmd6.addParam("end_dt",   RSDBP_IN,     SQL_ConvTypeDate(RS.EndDate) );
              cmd6.addParam("basis",    RSDBP_IN,     pFI.rec.Basis );
              cmd6.execute();

              CC = CC + pFI.rec.Amount * (R+ (pFI.rec.Spread/100.0)) * SQL_ConvTypeSum(cmd6.Value(0)) * Dfp;
           end;
        end;
       
        CCxT = CC * T;
     else
        MsgBox("Не найдена процентная ставка в справочнике видов процентных ставок");
        return 1;
     end;
  end;

  return 0;
END;

/* Расчет СС требования и обязательства по части сделки процентного СВОП */
PRIVATE MACRO FairValueCS( pDEAL:TRecHandler, pDate:date, n:integer, pFI_1:TRecHandler, pFI_2:TRecHandler, pB1:double, pB2:double, poFV:@double )

  var v_Ft:integer = ALLFININSTR, /* Валюта требования */
      v_Fo:integer = ALLFININSTR, /* Валюта обязательства */
      v_St:double  = 0.0,         /* Курс валюты требования */
      v_So:double  = 0.0,         /* Курс валюты обязательства */
      v_Nt:money   = $0.0,        /* Сумма требований */
      v_No:money   = $0.0;        /* Сумма обязательств */

  if( n == 1 )
     v_Ft = pFI_1.rec.FIID;
     v_Fo = pFI_2.rec.FIID;
     v_St = pB1;
     v_So = pB2;
     v_Nt = pFI_1.rec.Amount;
     v_No = pFI_2.rec.Amount;
  else
     v_Ft = pFI_2.rec.FIID;
     v_Fo = pFI_1.rec.FIID;
     v_St = pB2;
     v_So = pB1;
     v_Nt = pFI_2.rec.Amount;
     v_No = pFI_1.rec.Amount;
  end;

  if( ALG_FairValue(pDate, IIF(n == 2, pFI_2, pFI_1), v_Ft, v_Fo, v_Nt, v_No, v_St, v_So, ALG_DV_KIND_PRRATE_PCTSWAP, ALG_DV_FIKIND_PRRATE_CURRENCY, null, null, @poFV, IIF(n == 2, pFI_1.rec.ExecDate, pDEAL.rec.BeginDate)) != 0 )
     return 1;
  end;

  return 0;
END;

MACRO DVCalcFrVal( pFrVal:TRecHandler, pDEAL:TRecHandler, pFI_1:TRecHandler, pFI_2:TRecHandler ):integer

  var Fininstr = TRecHandler( "fininstr.dbt" );

  if( pDEAL.rec.DVKind != DV_PCTSWAP )
     MsgBox("Алгоритм применим только для операции \"Процентный СВОП\".");
  elif( pFrVal.rec.Date == date(0,0,0) )
     MsgBox("Не задана дата расчёта.");
  elif( ПолучитьФинИн(pFI_1.rec.FIID, Fininstr) )
     MsgBox("Не найден ФИ c ID = " + pFI_1.rec.FIID + ".");
  else
     var CCb:double = 0.0, CCk:double = 0.0;
     var СС_1:double = $0.0, СС_2:double = $0.0;

     if( (CalcPaynSum(pDEAL, ALG_DV_PMGR_SIDE_LIABILITY, pFrVal.rec.Date, ((pDEAL.rec.Type == ALG_DV_FIX_FIX) or (pDEAL.rec.Type == ALG_DV_FIX_FLOAT)), pFI_1, @CCb) != 0) OR
         (CalcPaynSum(pDEAL, ALG_DV_PMGR_SIDE_DEMAND, pFrVal.rec.Date, ((pDEAL.rec.Type == ALG_DV_FIX_FIX) or (pDEAL.rec.Type == ALG_DV_FLOAT_FIX)), pFI_2, @CCk) != 0) )
        return 1;
     end;

     if( pFI_1.rec.FIID != pFI_2.rec.FIID )
        var v_B1:double = 0.0,
            v_B2:double = 0.0;
        var v_B1SinceDate:date = date(0,0,0),
            v_B2SinceDate:date = date(0,0,0);
        var ToContinue:bool = true;
        if( not ПолучитьКурсЗаданногоTипа(@v_B1, pFI_1.rec.FIID, NATCUR, DV_RATETYPE_CBR, pFrVal.rec.Date, true, @v_B1SinceDate) or
            ((v_B1SinceDate != pFrVal.rec.Date) and (v_B1 == 0.0)) )
           MsgBox("Невозможно получить курс вида 'ЦБ РФ' для FIID = " + String(pFI_1.rec.FIID) + " на дату " + String(pFrVal.rec.Date));
        else
           if( v_B1SinceDate != pFrVal.rec.Date )
              ToContinue = false;
              if( MsgBoxEx("На дату операции не найден курс вида 'ЦБ РФ'.| Выполнить расчет по курсу за " + String(v_B1SinceDate) + "?", MB_YES+MB_NO, IND_YES) == IND_YES )
                 ToContinue = true;
              end;
           end;

           if( ToContinue == true )
              if( not ПолучитьКурсЗаданногоTипа(@v_B2, pFI_2.rec.FIID, NATCUR, DV_RATETYPE_CBR, pFrVal.rec.Date, true, @v_B2SinceDate) or
                  ((v_B2SinceDate != pFrVal.rec.Date) and (v_B2 == 0.0)) )
                 MsgBox("Невозможно получить курс вида 'ЦБ РФ' для FIID = " + String(pFI_2.rec.FIID) + " на дату " + String(pFrVal.rec.Date));
              else
                 if( v_B2SinceDate != pFrVal.rec.Date )
                    ToContinue = false;
                    if( MsgBoxEx("На дату операции не найден курс вида 'ЦБ РФ'.| Выполнить расчет по курсу за " + String(v_B2SinceDate) + "?", MB_YES+MB_NO, IND_YES) == IND_YES )
                       ToContinue = true;
                    end;
                 end;

                 if( ToContinue == true )
                    if( FairValueCS(pDEAL, pFrVal.rec.Date, 2, pFI_1, pFI_2, v_B1, v_B2, @СС_2) != 0 )
                       return 1;
                    end;
       
                    if( pDEAL.rec.BeginDate > pFrVal.rec.Date )
                       if( FairValueCS(pDEAL, pFrVal.rec.Date, 1, pFI_1, pFI_2, v_B1, v_B2, @СС_1) != 0 )
                          return 1;
                       end;
                    end;
                 else
                    return 1;
                 end;
              end;
           else
              return 1;
           end;
        end;
     end;

     pFrVal.rec.FairValue  = money(round(CCk - CCb + СС_1 + СС_2, 2));
     pFrVal.rec.Demand     = money(round(CCk, 2));
     pFrVal.rec.Liability  = money(round(CCb, 2));
     pFrVal.rec.SetDemand  = pFrVal.rec.SetLiability  = SET_CHAR;
     pFrVal.rec.SetDemand2 = pFrVal.rec.SetLiability2 = UNSET_CHAR;

     return 0; /*рассчитали значение без ошибок*/
  end;

  return 1; /*по умолчанию ошибка*/
END;
