/** 
 @file  dvop040.mac
 @brief Операция расчетов на срочном рынке. Шаг "Перенос по срокам"

 Исполнение шага "Перенос по срокам" 

 # tag
 - functional_block:Лимиты
 - code_type:BP_Step

 # changeLog 
 |date       |author         |tasks            |note                            
 |-----------|---------------|-----------------|--------------------------------------------
 |2023.09.19 |Швецов  Я. А.  |CCBO-7598        | Добавлены комментарии, отформатирован текст          
 | ?         |Азарцов В. В.  |?                | Первоначальная реализация                 

*/

import InsCarryDoc, "dvserv.mac", "dv_fun.mac", "dv_carop.mac", "dv_comiss.mac", "dv_period.mac";

/**
 @brief Исполнение шага
 @param[in] Doc  Буфер временного файла операций
 @param[in] FDoc Первичный документ операции (dvoper.dbt)
 @return         Флаг исполнения шага (0 - исполнен без ошибок, 1 - с ошибками)
*/
macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )
  record oper(dvoper);
  SetBuff( oper, FDoc );

  var FD;
  var Query:string = "", Data:variant;
  var Count:integer = 0;
   accDvdealTrnArr.size = 0;

  if( УчетБиржевыхКонтрактов() == DV_ACCEXCONTR_DEAL )
    Query = " SELECT DEAL.t_ID " +
            "   FROM ddvdeal_dbt DEAL " +
            "  WHERE " + DealOpenOnOperationDate(oper) +
            "    AND DEAL.t_Type in('B', 'S', 'D', 'G') " +
            "    AND DEAL.t_State < " + string(DVDEAL_CLOSE) +
            "    AND DEAL.t_Client = -1 ";
  else //Для всех позиций DDVFIPOS, таких что PositionByOperationDate (DDVFIPOS) и T_CLIENT не задан выполнить перенос по срокам
    Query = " SELECT POS.t_ID " +
            "   FROM ddvfipos_dbt POS " +
            "  WHERE " + PositionOpenOnOperationDate(oper) +
            "    AND POS.T_CLIENT = -1";
  end;
  Data = TRsbDataSet(Query);
  if( Data )
    Count = 0;
    InitProgress( SQL_GetNRecs(Query), "Выполнение переноса по срокам", "Выполнение..." );
    while( Data.MoveNext() )

      if( УчетБиржевыхКонтрактов() == DV_ACCEXCONTR_DEAL )
        FD = DVFirstDocDeal( DL_DVDEAL, SQL_ConvTypeInteger(Data.ID) );
      else
        FD = DVFirstDocPos( DL_DVFIPOS, SQL_ConvTypeInteger(Data.ID) );
      end;

      if( ИспользуемСрочныеСчета(FD.GetDoc()) == true )
        var ReqAcc:string = ""; var ReqAccFIID:integer = ALLFININSTR; var ReqFiRole:integer = FIROLE_UNDEF;
        var ComAcc:string = ""; var ComAccFIID:integer = ALLFININSTR; var ComFiRole:integer = FIROLE_UNDEF;

        ПолучитьВнебалансовыеСчетаБирж( FD, @ReqAcc, @ReqAccFIID, @ReqFiRole, @ComAcc, @ComAccFIID, @ComFiRole, false, false ); /*DEF-34994*/
        if (ReqAcc != "")
          ReqAcc = ReqAcc+"1";
        end;
        if (ComAcc != "")
          ComAcc = ComAcc+"1";
        end;
        if( ПеренестиСчетПоСрокамБирж("", FD, oper.Date, doc, true, ReqAcc, ReqAccFIID, ReqFiRole, true ) == false )
          RemProgress();
          return 1;
        end;

        if( ПеренестиСчетПоСрокамБирж("", FD, oper.Date, doc, true, ComAcc, ComAccFIID, ComFiRole, false ) == false )
          RemProgress();
          return 1;
        end;
      end;
      UseProgress(Count = Count + 1);
    end;      
    RemProgress();
  end;

  if ( not InsertDvdealTrnLink(ID_Operation, ID_Step) )
    MsgBox("Ошибка при сохранении информации о связи проводок со сделками срочного рынка");
    return 1;
  end;

  return 0;
end;
