/*
$Name:        dvdl010.mac
$Module:      Производные инструменты
$Description: Биржевая операция c ПИ. Шаг настройки операции
*/
import CTInter, "dvserv.mac", "dv_categ.mac";

macro __ExecuteStep( FD )

  if( FD != NULL )
     // расчетная на валюту ИЛИ
     // НеРанееТретьегоДня поставочная сделка на валюту (т.е. поставка валюты должна быть осуществлена не ранее третьего дня после даты заключения)
     if( (FD.BaseFI().rec.FI_Kind == FIKIND_CURRENCY) AND
         (
            ( (FD.FI().rec.Settlement_Code == DVSETTLEMET_CALC) OR
              ( (FD.FI().rec.Settlement_Code == DVSETTLEMET_STATE) AND ИспользуемСрочныеСчета(FD.Deal)
              )
            )
         )
       )
        if( not УстановитьКатегориюФИСС(FD.Deal, OBJTYPE_OPER_DV, FD.Deal.rec.Date) )
           return 1;
        end;
     end;
  else
     /*Сделано, как и в единичном режиме без отката, хотя и там и там по-хорошему должен быть откат*/
     var cmd = RSDCommand( " INSERT INTO dobjatcor_dbt (t_objecttype, t_groupid, t_attrid, t_object, t_general, t_validfromdate, t_oper, t_validtodate, t_sysdate, t_systime, t_IsAuto ) "
                           " SELECT DISTINCT " + OBJTYPE_OPER_DV + ", 1, 1, lpad(deal.t_ID, 34, '0'), chr(88), deal.t_Date, " + {oper} + ", TO_DATE('31-12-9999','DD-MM-YYYY'), TRUNC(SYSDATE), TO_DATE('01.01.0001:' || TO_CHAR(SYSDATE, 'HH24:MI:SS'), 'DD.MM.YYYY:HH24:MI:SS'), chr(88) "
                           "   FROM doprtemp_view opr, ddvdeal_dbt deal, dfininstr_dbt FI, dfininstr_dbt BaseFI " +
                           "  WHERE deal.t_ID = TO_NUMBER(opr.t_DocumentID) " +
                           "    AND opr.t_DocKind = " + DL_DVDEAL +
                           "    AND FI.t_FIID = deal.t_FIID " +
                           "    AND BaseFI.t_FIID = FI.t_FaceValueFI " +
                           "    AND BaseFI.t_FI_Kind = " + FIKIND_CURRENCY +
                           "    AND ((FI.t_Settlement_Code = " + DVSETTLEMET_CALC + ") OR ((FI.t_Settlement_Code = " + DVSETTLEMET_STATE + ") AND (RSB_Derivatives.DV_UseUrgentAccount(deal.t_ID) != 0))) " +
                           "    AND not exists( select * " +
                           "                      from dobjatcor_dbt atcor " +
                           "                     where atcor.t_ObjectType = " + OBJTYPE_OPER_DV +
                           "                       and TO_NUMBER(atcor.t_Object) = deal.t_ID " +
                           "                       and atcor.t_GroupID = 1 ) "
                         );
     cmd.Execute();
  end;

  return 0;
end;

macro ExecuteStep( doc, FDoc )

  VAR FD, deal = TrecHandler("dvdeal");
  deal.SetRecordAddr(FDoc);

  FD = DVFirstDocDeal(DL_DVDEAL, deal);

  return __ExecuteStep(FD);
end;

macro MassExecuteStep()
  return __ExecuteStep(NULL);
end;
