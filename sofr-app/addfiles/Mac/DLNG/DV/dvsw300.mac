/*
$Name:        dvsw300.mac
$Module:      Производные инструменты
$Description: Внебиржевая операция. Шаг: Изменение условий сделки
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac", "dv_period.mac";

PRIVATE VAR Dold = TRecHandler( "dvndeal.dbt" ); /**/
PRIVATE VAR Dnew = TRecHandler( "dvndeal.dbt" ); /**/
PRIVATE VAR Fold = TRecHandler( "dvnfi.dbt" );   /* Заполняется программой при выполнении шага */
PRIVATE VAR Fnew = TRecHandler( "dvnfi.dbt" );   /**/
PRIVATE VAR Fold_2 = TRecHandler( "dvnfi.dbt" );   /**/
PRIVATE VAR Fnew_2 = TRecHandler( "dvnfi.dbt" );   /**/

private const КорректировакаСуммы_Все = 0;
private const КорректировакаСуммы_Треб = 1;
private const КорректировакаСуммы_Обяз = 2;

private macro ПроверитьНеобходимостьРепортингаВРепозитарии( DealID:integer )
  
  var    result=false;
  var cmd = DL_RSDCommand();
  var query = " SELECT t_docid"
             +" FROM ddl_repozdeal_dbt"
             +" WHERE t_docid = ?"
             +" AND (t_dockind = 199 OR t_dockind = 4815)";
  
  cmd.AddParam(DealID);
  
  var DataSet = cmd.Execute(query);
  if(DataSet.moveNext())
      result=true;
  end;
  return result;
end;

/* Поиск первичных сообщений для сделки, имеющих статус "Подготовлено" либо "Ошибка выгрузки" */
private macro FindUnloadedMessages(DealID:integer)
  var cmd = DL_RSDCommand();
  var query = "select 1 from dir_generalinf_dbt "
            + "  where t_internalmessageid "
            + "  in ( select t_docid from ddlgrdoc_dbt "
            + "       where t_grdealid in ( select t_id from ddlgrdeal_dbt "
            + "                             where t_docid = ? )"
            + "       and t_dockind = ? )" 
            + "  and T_RSTATUS in (1, 4)"; // 1 - подготовлено, 4 - ошибка выгрузки

  cmd.AddParam(DealID);
  cmd.AddParam(4853/*REPOS_GENERALINF*/);
  var DataSet = cmd.Execute(query);
  return DataSet.moveNext()
end;

/* Поиск сводных (cm083) сообщений для сделки, имеющих статус "Подготовлено" либо "Ошибка выгрузки" */
private macro FindUnloadedBulkMessages(DealID:integer)
  var cmd = DL_RSDCommand();
  var query = "select 1 from dir_generalinf_dbt "
            + "  where t_internalmessageid "
            + "  in ( select t_internalmessageid from dir_cm083_dbt "
            + "       where t_id in ( select t_docid from ddlgrdoc_dbt "
            + "                       where t_grdealid in ( select t_id from ddlgrdeal_dbt "
            + "                                             where t_docid = ? ) "
            + "                       and t_dockind = ? ) )"
            + "  and T_RSTATUS in (1, 4)"; // 1 - подготовлено, 4 - ошибка выгрузки

  cmd.AddParam(DealID);
  cmd.AddParam(4857/*REPOS_CM083*/);
  var DataSet = cmd.Execute(query);
  return DataSet.moveNext();
end;

private macro СрокОжиданияЗапроса(GAID:integer)
   var    result=0;
  var cmd = DL_RSDCommand();
  var query = " SELECT t_WAITINGPERIOD"
             +" FROM ddl_genagr_dbt"
             +" WHERE t_genagrid = ?";
  
  cmd.AddParam(GAID);
  
  var DataSet = cmd.Execute(query);
  if(DataSet.moveNext())
      result=DataSet.WAITINGPERIOD;
  end;
  return result;
end;

private macro ПроверитьИзменениеПараметровДляРепозУчета()
  var haveChange = false;
  haveChange = haveChange or (Fnew.rec.Cost != Fold.rec.Cost); //стоимость
  haveChange = haveChange or (Fnew.rec.Price != Fold.rec.Price); //цена
  haveChange = haveChange or (Fnew.rec.PayDate != Fold.rec.PayDate); //дата поставки
  haveChange = haveChange or (Fnew.rec.SuplDate != Fold.rec.SuplDate); //дата оплаты
  haveChange = haveChange or (Fnew.rec.ExecDate != Fold.rec.ExecDate); //дата исполнения
  haveChange = haveChange or (Fnew_2.rec.Cost != Fold_2.rec.Cost); //стоимость
  haveChange = haveChange or (Fnew_2.rec.Price != Fold_2.rec.Price); //цена
  haveChange = haveChange or (Fnew_2.rec.PayDate != Fold_2.rec.PayDate); //дата поставки
  haveChange = haveChange or (Fnew_2.rec.SuplDate != Fold_2.rec.SuplDate); //дата оплаты
  haveChange = haveChange or (Fnew_2.rec.ExecDate != Fold_2.rec.ExecDate); //дата исполнения
  return haveChange;
end;

private macro DV_ПроверитьСуществованиеШага(ID_Operation, Symb, IsExecute)
  var cmd = DL_RSDCommand();
   var query = " SELECT COUNT(1) AS CountStep " +
    "   FROM doprstep_dbt step" +
    "  WHERE step.t_ID_Operation = ? AND " +
    "        step.t_Symbol       = ? AND " +
    "        step.t_IsExecute    = ? ";
  cmd.AddParam(ID_Operation);
  cmd.AddParam(Symb);
  cmd.AddParam(IsExecute);
  var DataSet = cmd.Execute(query);
  DataSet.moveNext();
  return DataSet.CountStep;
end;

/* Проверка, является ли слелка с ожиданием запроса: параметры "Способ подтверждения" = "Комбинированный/Последовательный" и  */
/* "Инициирующая сторона" = "Контрагент"                                                                                      */
private macro isPendingRequestDeal(DealID:integer)
  var cmd = DL_RSDCommand();
  var query = " SELECT T_INITIATOR, T_METHOD_WAYTWOWAY FROM DDL_REPOZDEAL_DBT "
            + "   WHERE T_DOCID = ? ";
  cmd.AddParam(DealID);
  var DataSet = cmd.Execute(query);
  var stat = false;
  if (DataSet.moveNext())
    stat = ( DataSet.rec.Initiator == 2 ) and ( DataSet.rec.Method_Waytwoway == 1 )
  end;
  return stat;
end;


private macro ПроводкаТреббованийВнб(doc, FD:DVFirstDocNDeal, ДатаИсполненияШага, DebFIID, DebAccBuf  ):bool
   var corr = TRecHandler("account.dbt");
   FD.SetFIIDForCorAccNum(DebFIID);
   if( not FD.IsExistAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб( FIROLE_CORACC_ACTIVE ), corr, null, ДатаИсполненияШага ) )
      if( (not FD.OpenAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), corr, ДатаИсполненияШага) ) )
         return false;
      end;
   end;
   if( not ПроводкаПоКатегориямУчета( FD,
                                       DebAccBuf, corr,
                                       ДатаИсполненияШага, 4,
                                       DebFIID, FD.ReqSum(),
                                       doc,
                                       INPCARRY, "", "",
                                       "Постановка требований на внебалансовый учет по" + IIF( FD.DealPart == 1," сделке "," второй части сделки ") + FD.DealCode(),
                                       null, null, null, null, null, null, null, null, null, null, null, null, null, null, null, null, null, null, 0, true
                                    )
      )
      return false;
   end;
   return true;

end;

private macro ПроводкаОбязательствВнб(doc, FD:DVFirstDocNDeal, ДатаИсполненияШага, CredFIID, CredAccBuf):bool
   var corr_2 = TRecHandler("account.dbt");
   FD.SetFIIDForCorAccNum(CredFIID);
   if( not FD.IsExistAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), corr_2, null, ДатаИсполненияШага ) )
      if( (not FD.OpenAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), corr_2, ДатаИсполненияШага) ) )
         return false;
      end;
   end;
   if( not ПроводкаПоКатегориямУчета( FD,
                                       corr_2, CredAccBuf,
                                       ДатаИсполненияШага, 4,
                                       null, null,
                                       doc,
                                       INPCARRY, "", "",
                                       "Постановка обязательств на внебалансовый учет по" + IIF( FD.DealPart == 1," сделке "," второй части сделки ") + FD.DealCode(),
                                       null, null, null, null, null,
                                       CredFIID, FD.ComSum(),
                                       null, null, null, null, null, null, null, null, null, null, null, 0, false
                                    )
      )
      return false;
   end;
   return true;

end;

private macro ПоставитьНаВнебалансТО(doc, FD:DVFirstDocNDeal, ДатаИсполненияШага ):INTEGER
   var DebAccBuf  = TRecHandler("account");
   var CredAccBuf = TRecHandler("account");
   var DebAcc:string  = ""; var DebFIID:integer  = ALLFININSTR;  var DebFiRole  = FIROLE_UNDEF;
   var CredAcc:string = ""; var CredFIID:integer = ALLFININSTR;  var CredFiRole = FIROLE_UNDEF;

   ПолучитьСрочныеСчетаВнб( FD, @DebAcc, @DebFIID, @DebFiRole, @CredAcc, @CredFIID, @CredFiRole );

   if( (not FD.OpenAccount( DebAcc,  null, null, DebFiRole,  DebAccBuf,  ДатаИсполненияШага, DebFIID) ) OR
         (not FD.OpenAccount( CredAcc, null, null, CredFiRole, CredAccBuf, ДатаИсполненияШага, CredFIID) ) )
      return 1;
   end;

   if(not ПроводкаТреббованийВнб(doc, FD, ДатаИсполненияШага, DebFIID, DebAccBuf))
      return 1;
   end;
   if(not ПроводкаОбязательствВнб(doc, FD, ДатаИсполненияШага, CredFIID, CredAccBuf))
      return 1;
   end;
   return 0;
end;

PRIVATE MACRO ПоставитьНаВнебаланс(doc, FD:DVFirstDocNDeal, ДатаИсполненияШага ):INTEGER
   var DebAccBuf  = TRecHandler("account");
   var CredAccBuf = TRecHandler("account");
   var corr = TRecHandler("account.dbt");
   var corr_2 = TRecHandler("account.dbt");
   var DebAcc:string  = ""; var DebFIID:integer  = ALLFININSTR;  var DebFiRole  = FIROLE_UNDEF;
   var CredAcc:string = ""; var CredFIID:integer = ALLFININSTR;  var CredFiRole = FIROLE_UNDEF;

   ПолучитьСрочныеСчетаВнб( FD, @DebAcc, @DebFIID, @DebFiRole, @CredAcc, @CredFIID, @CredFiRole );

   if( (not FD.OpenAccount( DebAcc,  null, null, DebFiRole,  DebAccBuf,  ДатаИсполненияШага, DebFIID) ) OR
         (not FD.OpenAccount( CredAcc, null, null, CredFiRole, CredAccBuf, ДатаИсполненияШага, CredFIID) ) )
      return 1;
   end;

   if (FD.IsSale())
      /*проверяем, существует ли счет для того, чтобы не лезла ошибка, что такой счет уже существует #210153*/
      FD.SetFIIDForCorAccNum(DebFIID);
      if( not FD.IsExistAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб( FIROLE_CORACC_ACTIVE ), corr, null, ДатаИсполненияШага ) )
         if( (not FD.OpenAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), corr, ДатаИсполненияШага) ) )
            return 1;
         end;
      end;
      if( not ПроводкаПоКатегориямУчета( FD,
                                          DebAccBuf, corr,
                                          ДатаИсполненияШага, 4,
                                          DebFIID, FD.ReqSum(),
                                          doc,
                                          INPCARRY, "", "",
                                          "Постановка требований на внебалансовый учет по" + IIF( FD.DealPart == 1," сделке "," второй части сделки ") + FD.DealCode(),
                                          null, null, null, null, null, null, null, null, null, null, null, null, null, null, null, null, null, null, 0, true
                                       )
         )
         return 1;
      end;
   end;

   if(FD.IsBuy())
      FD.SetFIIDForCorAccNum(CredFIID);
      if( not FD.IsExistAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), corr_2, null, ДатаИсполненияШага ) )
         if( (not FD.OpenAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), corr_2, ДатаИсполненияШага) ) )
            return 1;
         end;
      end;
      if( not ПроводкаПоКатегориямУчета( FD,
                                          corr_2, CredAccBuf,
                                          ДатаИсполненияШага, 4,
                                          null, null,
                                          doc,
                                          INPCARRY, "", "",
                                          "Постановка обязательств на внебалансовый учет по" + IIF( FD.DealPart == 1," сделке "," второй части сделки ") + FD.DealCode(),
                                          null, null, null, null, null,
                                          CredFIID, FD.ComSum(),
                                          null, null, null, null, null, null, null, null, null, null, null, 0, false
                                       )
         )
         return 1;
      end;
   end;
   return 0;
END;

PRIVATE MACRO КорректировкаСуммыСделки(doc, FD, КсОбяз, КсТреб, ДатаИсполненияШага, ЧастьДляКорректировки)
  var DebAccBuf  = TRecHandler("account");
  var CredAccBuf = TRecHandler("account");
  var DebA  = TRecHandler("account");
  var CredA = TRecHandler("account");
  var DebAcc:string  = ""; var DebFIID:integer  = ALLFININSTR;  var DebFiRole  = FIROLE_UNDEF;
  var CredAcc:string = ""; var CredFIID:integer = ALLFININSTR;  var CredFiRole = FIROLE_UNDEF;
  var DebF:integer  = ALLFININSTR; var CredF:integer  = ALLFININSTR;
  var corr = TRecHandler("account.dbt");
  var СчКоррРоль;
  var КорректироватьОбязательства:bool = ((ValType(ЧастьДляКорректировки)== V_UNDEF) 
         OR (ЧастьДляКорректировки == КорректировакаСуммы_Обяз) OR (ЧастьДляКорректировки == КорректировакаСуммы_Все));
   
  var КорректироватьТребования:bool = ((ValType(ЧастьДляКорректировки)== V_UNDEF) 
         OR (ЧастьДляКорректировки == КорректировакаСуммы_Треб) OR (ЧастьДляКорректировки == КорректировакаСуммы_Все));

   ПолучитьСрочныеСчетаВнб( FD, @DebAcc, @DebFIID, @DebFiRole, @CredAcc, @CredFIID, @CredFiRole );

   if( (not FD.IsExistAccount( DebAcc,  null, true, DebFiRole,  DebAccBuf, null, ДатаИсполненияШага, DebFIID)) or
       (not FD.IsExistAccount( CredAcc, null, true, CredFiRole, CredAccBuf, null, ДатаИсполненияШага, CredFIID)) )
         return 1;
   end;

  if (КорректироватьОбязательства) 
   if (КсОбяз < 0)
      FD.SetFIIDForCorAccNum(CredFIID);
      if( not FD.IsExistAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), corr, null, ДатаИсполненияШага ) )
         if( (not FD.OpenAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), corr, ДатаИсполненияШага) ) )
            return 1;
         end;
      end;
      if( not ПроводкаПоКатегориямУчета( FD,
                                          CredAccBuf, corr,
                                          ДатаИсполненияШага, 4,
                                          CredFIID, abs(КсОбяз),
                                          doc,
                                          INPCARRY, "", "",
                                          "Корректировка обязательств по" + IIF( FD.DealPart == 1," сделке "," второй части сделки ") + FD.DealCode(),
                                          null, null
                                       )
         )
         return 1;
      end;
   else
      FD.SetFIIDForCorAccNum(CredFIID);
      if( not FD.IsExistAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), corr, null, ДатаИсполненияШага ) )
         if( (not FD.OpenAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), corr, ДатаИсполненияШага) ) )
            return 1;
         end;
      end;
      if( not ПроводкаПоКатегориямУчета( FD,
                                          corr, CredAccBuf,
                                          ДатаИсполненияШага, 4,
                                          null, null,
                                          doc,
                                          INPCARRY, "", "",
                                          "Корректировка обязательств по" + IIF( FD.DealPart == 1," сделке "," второй части сделки ") + FD.DealCode(),
                                          null, null, null, null, null,
                                          CredFIID, abs(КсОбяз)
                                       )
         )
         return 1;
      end;
   end;
  end; 
  
  if (КорректироватьТребования) 
   if (КсТреб < 0)
      FD.SetFIIDForCorAccNum(DebFIID);
      if( not FD.IsExistAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), corr, null, ДатаИсполненияШага ) )
         if( (not FD.OpenAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE), corr, ДатаИсполненияШага) ) )
            return 1;
         end;
      end;
         if( not ПроводкаПоКатегориямУчета( FD,
                                             corr, DebAccBuf,
                                             ДатаИсполненияШага, 4,
                                             null, null,
                                             doc,
                                             INPCARRY, "", "",
                                             "Корректировка требований по" + IIF( FD.DealPart == 1," сделке "," второй части сделки ") + FD.DealCode(),
                                             null, null, null, null, null,
                                             DebFIID, abs(КсТреб)
                                          )
            )
            return 1;
         end;
   else
      FD.SetFIIDForCorAccNum(DebFIID);
      if( not FD.IsExistAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), corr, null, ДатаИсполненияШага ) )
         if( (not FD.OpenAccount( "СчКорреспГлаваГ", null, false, FD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE), corr, ДатаИсполненияШага) ) )
            return 1;
         end;
      end;
      if( not ПроводкаПоКатегориямУчета( FD,
                                          DebAccBuf, corr,
                                          ДатаИсполненияШага, 4,
                                          DebFIID, abs(КсТреб),
                                          doc,
                                          INPCARRY, "", "",
                                          "Корректировка требований по" + IIF( FD.DealPart == 1," сделке "," второй части сделки ") + FD.DealCode(),
                                          null, null
                                       )
         )
         return 1;
      end;
   end;
  end; 
   return 0;
   
END;

PRIVATE MACRO СрочныеСчетаВнбСуществуют (FD, ДатаИсполненияШага):bool
  var DebAcc:string  = ""; var DebFIID:integer  = ALLFININSTR;  var DebFiRole  = FIROLE_UNDEF;
  var CredAcc:string = ""; var CredFIID:integer = ALLFININSTR;  var CredFiRole = FIROLE_UNDEF;

   ПолучитьСрочныеСчетаВнб( FD, @DebAcc, @DebFIID, @DebFiRole, @CredAcc, @CredFIID, @CredFiRole );

   if( (not FD.IsExistAccount( DebAcc,  null, true, DebFiRole,  null, null, ДатаИсполненияШага, DebFIID)) or
       (not FD.IsExistAccount( CredAcc, null, true, CredFiRole, null, null, ДатаИсполненияШага, CredFIID)) )
         return false;
   end;
   return true;
END;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  var ДатаИсполненияШага = {curdate};

  SetBuff( ndeal, FDoc );
  var FD = DVFirstDocNDeal(DocKind, ndeal, null, Fnew), Ground;
  var oldFD = DVFirstDocNDeal(DocKind, ndeal, null, Fold);
  var FD_2 = DVFirstDocNDeal(DocKind, ndeal, 2, Fnew_2);
  var oldFD_2 = DVFirstDocNDeal(DocKind, ndeal, 2, Fold_2);
  if (FD.ОтказОтИсполнения() or FD_2.ОтказОтИсполнения())
     return 0;
  end;
  
  //if( FD.IsClient() == false )
    
      var DebAccBuf  = TRecHandler("account");
      var CredAccBuf = TRecHandler("account");
      var DebAccBuf_2  = TRecHandler("account");
      var CredAccBuf_2 = TRecHandler("account");
      var DebA  = TRecHandler("account");
      var CredA = TRecHandler("account");
      var DebA_2  = TRecHandler("account");
      var CredA_2 = TRecHandler("account");
      var DebAcc:string  = ""; var DebFIID:integer  = ALLFININSTR;  var DebFiRole  = FIROLE_UNDEF;
      var CredAcc:string = ""; var CredFIID:integer = ALLFININSTR;  var CredFiRole = FIROLE_UNDEF;
      var DebAcc_2:string  = ""; var DebFIID_2:integer  = ALLFININSTR;  var DebFiRole_2  = FIROLE_UNDEF;
      var CredAcc_2:string = ""; var CredFIID_2:integer = ALLFININSTR;  var CredFiRole_2 = FIROLE_UNDEF;
      var DebF:integer  = ALLFININSTR; var CredF:integer  = ALLFININSTR;
      var КсБА:money = 0.0, КсБА_2:money = 0.0, СчКоррРоль;
      var КсКА:money = 0.0, КсКА_2:money = 0.0;
      var hasCurChange = false;
      var корТреб = TRecHandler("account.dbt"), корОбяз = TRecHandler("account.dbt");
      var корТреб_2 = TRecHandler("account.dbt"), корОбяз_2 = TRecHandler("account.dbt");
      var ПризнаниеПФИ = 0, ПостНаБаланс = 0, ИспБезПост = 0;
      var счета1:bool;
      var счета2:bool;
      var oldСчета1:bool;
      var oldСчета2:bool;
      var СтавитьНаВнебаланс1:bool = (FD.ДатаИсполнения() != ДатаИсполненияШага);
      var СтавитьНаВнебаланс2:bool = (FD_2.ДатаИсполнения() != ДатаИсполненияШага);
	  var УжеПоставилиНаВнебаланс1 = false;
	  var УжеПоставилиНаВнебаланс2 = false;

      КсКА = FD.СуммаОбязательства() - oldFD.СуммаОбязательства(); 
      КсБА = FD.СуммаТребования() - oldFD.СуммаТребования();
      КсКА_2 = FD_2.СуммаОбязательства() - oldFD_2.СуммаОбязательства(); 
      КсБА_2 = FD_2.СуммаТребования() - oldFD_2.СуммаТребования();
      hasCurChange = FD.ВалютаЦены() != oldFD.ВалютаЦены();  
      ПризнаниеПФИ = DV_ПроверитьСуществованиеШага(ID_Operation, "Ф", "X"); //Была ли постановка на внебаланс
      ИспБезПост = DV_ПроверитьСуществованиеШага(ID_Operation, "и", "X"); 
      ПостНаБаланс = DV_ПроверитьСуществованиеШага(ID_Operation, "И", "X"); // Подсчет: 1 - Инициализация, 2 - Инициализация и Исполненение с поставкой (или без поставкой 2ч), 3 - Инициализация и Исполненение с поставкой и Исполненение с поставкой 2ч

      if ((FD.IsClient() == false) and hasCurChange and (ПризнаниеПФИ) and (((ИспБезПост == 0) and (ПостНаБаланс == 1)) OR ((ИспБезПост == 1) and (ПостНаБаланс == 1)) OR ((ИспБезПост == 0) and (ПостНаБаланс == 2))))
         ПолучитьСрочныеСчетаВнб( oldFD, @DebAcc, @DebFIID, @DebFiRole, @CredAcc, @CredFIID, @CredFiRole );

         if( (not oldFD.IsExistAccount( DebAcc,  null, true, DebFiRole,  DebAccBuf, null, ДатаИсполненияШага, DebFIID )) or
               (not oldFD.IsExistAccount( CredAcc, null, true, CredFiRole, CredAccBuf, null, ДатаИсполненияШага, CredFIID )) )
            oldСчета1 = false;
         else
            oldСчета1 = true;
         end;

         ПолучитьСрочныеСчетаВнб( oldFD_2, @DebAcc_2, @DebFIID_2, @DebFiRole_2, @CredAcc_2, @CredFIID_2, @CredFiRole_2 );

         if( (not oldFD_2.IsExistAccount( DebAcc_2,  null, true, DebFiRole_2,  DebAccBuf_2, null, ДатаИсполненияШага, DebFIID_2 )) or
               (not oldFD_2.IsExistAccount( CredAcc_2, null, true, CredFiRole_2, CredAccBuf_2, null, ДатаИсполненияШага, CredFIID_2 )) )
            oldСчета2 = false;
         else
            oldСчета2 = true;
         end;

         if (oldСчета1)
         if (oldFD.IsSale())//если сделка - покупка, значит неправильный внебаланс только по требованию
            oldFD.SetFIIDForCorAccNum(oldFD.ВалютаТребования());
            if( not ПроводкаПоКатегориямУчета( oldFD,
                                                   "СчКорреспГлаваГ", DebAccBuf,
                                                   ДатаИсполненияШага,
                                                   4,
                                                   oldFD.ВалютаТребования(),
                                                   oldFD.СуммаТребования(),
                                                   doc,
                                                   INPCARRY, "", "",
                                                   "Снятие требований с внебалансового учета по сделке " + oldFD.DealCode(),
                                                   null, oldFD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE),
                                                   null
                                                   )
                  )
                     return 1;
               end;
            elif (oldFD.IsBuy()) //если сделка - покупка, значит неправильный внебаланс только по обязательству
               oldFD.SetFIIDForCorAccNum(oldFD.ВалютаОбязательства());
               if( not ПроводкаПоКатегориямУчета( oldFD,
                                                   CredAccBuf, "СчКорреспГлаваГ",
                                                   ДатаИсполненияШага,
                                                   4,
                                                   oldFD.ВалютаОбязательства(),
                                                   oldFD.СуммаОбязательства(),
                                                   doc,
                                                   INPCARRY, "", "",
                                                   "Снятие обязательств с внебалансового учета по сделке " + oldFD.DealCode(),
                                                   null,
                                                   null, oldFD.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE) 
                                                   )
                  )
                     return 1;
               end;
            end;
            if(ПоставитьНаВнебаланс(doc, FD, ДатаИсполненияШага))
               return 1;
            end;
         else
            // счета внебаланса не открыты: шаг признание ПФИ выполнен, но постановки на внебаланс не было
            // ставим и Т и О на внебаланс
            if (СтавитьНаВнебаланс1)
               if(ПоставитьНаВнебалансТО(doc, FD, ДатаИсполненияШага))
                  return 1;
               end;
			   УжеПоставилиНаВнебаланс1 = true;
            end;
         end;
//вторая часть
         if (oldСчета2)
         if (oldFD_2.IsSale())//если сделка - покупка, значит неправильный внебаланс только по требованию
            oldFD_2.SetFIIDForCorAccNum(oldFD_2.ВалютаТребования());
            if( not ПроводкаПоКатегориямУчета( oldFD_2,
                                                "СчКорреспГлаваГ", DebAccBuf_2,
                                                ДатаИсполненияШага,
                                                4,
                                                oldFD_2.ВалютаТребования(),
                                                oldFD_2.СуммаТребования(),
                                                doc,
                                                INPCARRY, "", "",
                                                "Снятие требований с внебалансового учета по второй части сделки " + oldFD_2.DealCode(),
                                                null,oldFD_2.РольСчКорреспГлаваГВнб(FIROLE_CORACC_PASSIVE),
                                                 null
                                                )
               )
                  return 1;
            end;
         elif (oldFD_2.IsBuy())//если сделка - покупка, значит неправильный внебаланс только по требованию
            oldFD_2.SetFIIDForCorAccNum(oldFD_2.ВалютаОбязательства());
            if( not ПроводкаПоКатегориямУчета( oldFD_2,
                                                CredAccBuf_2, "СчКорреспГлаваГ",
                                                ДатаИсполненияШага,
                                                4,
                                                oldFD_2.ВалютаОбязательства(),
                                                oldFD_2.СуммаОбязательства(),
                                                doc,
                                                INPCARRY, "", "",
                                                "Снятие обязательств с внебалансового учета по второй части сделки " + oldFD_2.DealCode(),
                                                null,
                                                null,oldFD_2.РольСчКорреспГлаваГВнб(FIROLE_CORACC_ACTIVE)
                                                )
               )
                  return 1;
            end;
         end;
         if(ПоставитьНаВнебаланс(doc, FD_2, ДатаИсполненияШага))
            return 1;
         end;
     else
            // счета внебаланса не открыты: шаг признание ПФИ выполнен, но постановки на внебаланс не было
            // ставим и Т и О на внебаланс
            if (СтавитьНаВнебаланс2)
               if(ПоставитьНаВнебалансТО(doc, FD_2, ДатаИсполненияШага))
                  return 1;
               end;
			   УжеПоставилиНаВнебаланс2 = true;
            end;
         end;
     end;

      счета1 = СрочныеСчетаВнбСуществуют(oldFD, ДатаИсполненияШага);
      счета2 = СрочныеСчетаВнбСуществуют(oldFD_2, ДатаИсполненияШага);
      var нужнаКорректировкаСуммы:bool = 
            (((FD.IsBuy() and (((not hasCurChange) and ((КсКА != 0.0) OR (КсБА_2 != 0.0))) OR (КсБА != 0.0) OR (КсКА_2 != 0.0)))  
            OR (FD.IsSale() and (((not hasCurChange) and ((КсБА != 0.0) OR (КсКА_2 != 0.0))) OR (КсКА != 0.0) OR (КсБА_2 != 0.0))) ) 
            and (ПризнаниеПФИ) 
            and (   ((ИспБезПост == 0) and (ПостНаБаланс == 1)) 
                     OR ((ИспБезПост == 1) and (ПостНаБаланс == 1)) 
                     OR ((ИспБезПост == 0) and (ПостНаБаланс == 2)))
            and (not FD.IsClient()));


      if(((Fnew.rec.ExecDate != Fold.rec.ExecDate) OR (Fnew.rec.PayDate != Fold.rec.PayDate) OR (Fnew.rec.SuplDate != Fold.rec.SuplDate)) 
         AND ((ИспБезПост == 0) and (ПостНаБаланс == 1)))

         if ((not счета1) and нужнаКорректировкаСуммы and СтавитьНаВнебаланс1 and (not УжеПоставилиНаВнебаланс1))
            if(ПоставитьНаВнебалансТО(doc, FD, ДатаИсполненияШага))
               return 1;
            end;
         end;
         if(ВыполнитьПереносПоСрокам( doc, FD, ДатаИсполненияШага) ) 
            return 1;
         end;
         DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_REQ,  FD.ДатаТребования() );
         DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_OBL,  FD.ДатаОбязательства() );
         DL_CalendDefineOprDate(FD, DV_NDEAL_OPRDATE_EXEC, FD.ДатаИсполнения(true) );
		 
		 OprReplanStepPlanDates( DV_NDEAL_OPRDATE_REQ, FD.ДатаТребования(),false );
		 OprReplanStepPlanDates( DV_NDEAL_OPRDATE_OBL, FD.ДатаОбязательства(),false );
		 OprReplanStepPlanDates( DV_NDEAL_OPRDATE_EXEC, FD.ДатаИсполнения(true),false );
      end;


      if(((Fnew_2.rec.ExecDate != Fold_2.rec.ExecDate) OR (Fnew_2.rec.PayDate != Fold_2.rec.PayDate) OR (Fnew_2.rec.SuplDate != Fold_2.rec.SuplDate))
         AND ((ИспБезПост == 0) and (ПостНаБаланс == 1)))
         
         if ((not счета2) and нужнаКорректировкаСуммы and СтавитьНаВнебаланс2 and (not УжеПоставилиНаВнебаланс2))
            if(ПоставитьНаВнебалансТО(doc, FD_2, ДатаИсполненияШага))
               return 1;
            end;
         end;
         if( ВыполнитьПереносПоСрокам( doc, FD_2, ДатаИсполненияШага) )
            return 1;
         end;
         OprReplanStepPlanDates( DV_NDEAL_OPRDATE_EXEC_2, FD_2.ДатаИсполнения() );
         OprReplanStepPlanDates( DV_NDEAL_OPRDATE_REQ_2,  FD_2.ДатаТребования() );
         OprReplanStepPlanDates( DV_NDEAL_OPRDATE_OBL_2,  FD_2.ДатаОбязательства() );
         OprReplanStepPlanDates( DV_NDEAL_OPRDATE_CLOSE,  MAX( FD_2.ДатаОбязательства(), FD_2.ДатаТребования() ) );
      end;

      if(нужнаКорректировкаСуммы)
         var ЧастьДляКорректировкиСуммы;
         if(((КсКА != 0.0) OR (КсБА != 0.0)) and (счета1 or (not СтавитьНаВнебаланс1)))
            if (hasCurChange)
               ЧастьДляКорректировкиСуммы = IIF( oldFD.IsSale(), КорректировакаСуммы_Обяз, КорректировакаСуммы_Треб);
            else
               ЧастьДляКорректировкиСуммы = КорректировакаСуммы_Все;
            end;
            if( КорректировкаСуммыСделки(doc, oldFD, КсКА, КсБА, ДатаИсполненияШага, ЧастьДляКорректировкиСуммы) )
               return 1;
            end;
         end;

         if(((КсБА_2 != 0.0) OR (КсКА_2 != 0.0)) and (счета2 or (not СтавитьНаВнебаланс2)))
            if (hasCurChange)
               ЧастьДляКорректировкиСуммы = IIF( oldFD_2.IsBuy(), КорректировакаСуммы_Треб, КорректировакаСуммы_Обяз);
            else
               ЧастьДляКорректировкиСуммы = КорректировакаСуммы_Все;
            end;
            if (КорректировкаСуммыСделки(doc, oldFD_2, КсКА_2, КсБА_2, ДатаИсполненияШага, ЧастьДляКорректировкиСуммы) )
               return 1;
            end;
         end;
      end;

      if ( hasCurChange OR (КсКА != 0.0) OR (КсБА != 0.0) 
           OR (Fnew.rec.ExecDate != Fold.rec.ExecDate) 
           OR (Fnew_2.rec.ExecDate != Fold_2.rec.ExecDate) 
           OR (КсКА_2 != 0.0) OR (КсБА_2 != 0.0))

         DV_NDealUpdatePayment();
      end;
  //end;

  if( DV_ChangeDVNDEAL(Dold, Dnew, Fold, Fnew, Fold_2, Fnew_2, ALG_DV_CHANGEKIND_CHANGE, ДатаИсполненияШага) != 0 )
     MsgBox("Ошибка при изменении условий сделки");
     return 1;
  end;

  if( ПроверитьНеобходимостьРепортингаВРепозитарии( FD.ID ) and ПроверитьИзменениеПараметровДляРепозУчета() )
    if ((FindUnloadedMessages(FD.ID)) or (FindUnloadedBulkMessages(FD.ID)))
      MsgBox("По сделке есть подготовленные, но не сгенерированные сообщения. Необходимо либо выполнить откат подготовки сообщения, либо выполнить для него генерацию");
      return 1;
    else
      var requestTimeout = 0;
      GetRegistryValue("РЕПОЗИТАРИЙ\\СРОК ОЖИД.ЗАПР. ПРИ КОМБ.ПОДТВ.", V_INTEGER, requestTimeout, null);
      if(СрокОжиданияЗапроса(FD.Deal.rec.GenAgrID) > 0)
         requestTimeout = СрокОжиданияЗапроса(FD.Deal.rec.GenAgrID);
      end;
      var planDate = ДатаИсполненияШага;
      if( isPendingRequestDeal(FD.ID) and ( requestTimeout > 0 ) )
        planDate = GetDateAfterWorkDays(planDate, requestTimeout);
        if( DL_InsertGrDeal( DocKind, FD.ID, DLGR_TEMPL_CHANGEMSGWTHREQUEST, planDate, time(0,0,0), -1) )
          return 1;
        end;
        if( DL_SetDateGrDeal(FD.Deal.rec.DocKind, FD.Deal.rec.ID, DLGR_DATEKIND_CLOSEDEAL, GetDateAfterWorkDays(MAX(FD.ДатаТребования(),FD.ДатаОбязательства()), requestTimeout)) != 0 )
          return 1;
        end;
      else
        if( DL_InsertGrDeal( DocKind, FD.ID, DLGR_TEMPL_CHANGEMSG, planDate, time(0,0,0), -1) )
          return 1;
        end;
        if(FD.Deal.rec.AutoClose == UNSET_CHAR)
          if( DL_SetDateGrDeal(FD.Deal.rec.DocKind, FD.Deal.rec.ID, DLGR_DATEKIND_CLOSEDEAL, MAX(FD.ДатаТребования(),FD.ДатаОбязательства())) != 0 )
            return 1;
          end;
        end;
      end;  
    end;
  end;
  return 0;
end;
