/*
$Name:        dvcsa070.mac
$Module:      ФИССиКО
$Description: Сопровождение CSA: Корректировка процентов
*/
import "dv_car.mac";

PRIVATE VAR SvOpDvOper = TRecHandler("dvoper.dbt");

PRIVATE CLASS (DL_LOG_DATA_XML) DV_CSAPSO_LOG_DATA_XML(pDocKind:integer, pID:integer, pDataParms:TArray, pType:integer, pVersion:integer)

  PRIVATE MACRO СоздатьУзелОшибок(i)
     var УзелОшибок = null;

     if( m_Version == 1 )
        УзелОшибок = CreateElement("Property",
                                   "Message", m_DataParms[i].Message
                                  );
     else
        УзелОшибок = CreateElement("Property");
     end;
     
     return УзелОшибок;
  END;

  initDL_LOG_DATA_XML(pDocKind, pID, pDataParms, pType, pVersion);
END;

PRIVATE MACRO SaveLogDataXML(RepErrArr, ID:integer, DocKind:integer, ID_Operation:integer, ID_Step:integer)

  var RepXML = "";

  if( RepErrArr.Size > 0 )
     RepXML = DV_CSAPSO_LOG_DATA_XML(DocKind, ID, RepErrArr, LOG_TYPE_ERROR).GetDataXML();
  end;

  if( RepXML != "" )
     var Header = DL_LOG_HEADER_XML(DocKind).GetHeaderXML();

     DL_InsertHeaderLog(ID, DocKind, {Oper}, Header);
     DL_InsertLogXMLData(ID, DocKind, LOG_TYPE_ERROR, {Oper}, RepXML, ID_Operation, ID_Step);
  end;

END;

PRIVATE MACRO GetLogDataXML(DocKind:integer, ID:integer, OnlyLastReport:bool)
    return DV_CSAPSO_LOG_XML(DocKind, ID, LOG_TYPE_ERROR, OnlyLastReport);
END;

PRIVATE MACRO LoadLogArray(DocKind:integer, ID:integer, OnlyLastReport:bool)
    var logArray = TArray();
    logArray.size = 0;
    
    var xmlLog = GetLogDataXML(DocKind, ID, OnlyLastReport);
    xmlLog.SetReportNumber(0);
    
    var logLine;
    while( xmlLog.Next )
        logLine = xmlLog.GetReportLineData();
        logArray[logArray.size] = CDV_CSAPSO_ErrorReport(logLine.Message);
    end;
    
    return logArray;
END;

PRIVATE MACRO DL_DeleteDataByOper_XML(DocKind:integer, ID:integer, Type:integer)
    var qvery = "DELETE FROM DDL_LOGDATA_DBT "
               +" WHERE     t_Type = " + Type
               +"       AND t_LogID = (SELECT t_ID "
               +"                        FROM DDL_LOG_DBT "
               +"                       WHERE     t_DocKind = " + DocKind
               +"                             AND t_DocID = "+ ID +")";

    var cmd = RSDCommand(qvery);
    cmd.Execute();
END;

PRIVATE MACRO AddLogLine(DocKind:integer, ID:integer, OnlyLastReport:bool, Message:string, ID_Operation:integer, ID_Step:integer)
    var log = LoadLogArray(DocKind, ID, OnlyLastReport);
    log[log.size] = CDV_CSAPSO_ErrorReport(Message);
    DL_DeleteDataByOper_XML(DocKind, ID, LOG_TYPE_ERROR);
    SaveLogDataXML(log, ID, DocKind, ID_Operation, ID_Step);
END;

PRIVATE MACRO GetCcyCode(FIID)
    record fi( fininstr );
    var r = 0;
    
    if( ПолучитьФинИн(FIID, fi) == 0 )
        r = fi.Ccy;
    end;

   return r;
END;

PRIVATE MACRO GetFI_Code(FIID)
    record fi( fininstr );
    var r = 0;
    
    if( ПолучитьФинИн(FIID, fi) == 0 )
        r = fi.FI_Code;
    end;

   return r;
END;

PRIVATE MACRO GetValueLLCode( List:INTEGER, ElementOrCode:VARIANT ):string
    var ll = TRecHandler("llvalues.dbt");

    LL_FindLLVALUES( List, ElementOrCode, ll );

    return ll.rec.Code;
END;

MACRO ExecuteStep( doc, Fdoc, DocKind, ID_Operation, ID_Step )
    record csaBuf(dvcsa);
    SetBuff(csaBuf, FDoc);
    var FD = DVFirstCSA(DocKind, csaBuf.CSAID);
    
    var query = "BEGIN "
               +"    RSB_DVCSA.CalcCSAPrcTMP(?, ?, ?, ?, 1);"
               +"END;";
    var cmd = RsdCommand(query);
    cmd.NullConversion = true;
    cmd.addParam("", RSDBP_IN, FD.CSA.rec.CSAID);
    cmd.addParam("", RSDBP_IN, SvOpDvOper.rec.FIID);
    cmd.addParam("", RSDBP_IN, SvOpDvOper.rec.BegDate);
    cmd.addParam("", RSDBP_IN, SvOpDvOper.rec.EndDate);
    cmd.execute;
    
    DV_SaveCSAPrcCalc(0);
    
    return 0;
OnError
    cmd = RsdCommand("BEGIN "
                    +"     ? := RSB_DVCSA.DVCSA_LAST_ERROR; "
                    +"     ? := RSB_DVCSA.DVCSA_INDERR_FIID; "
                    +"     ? := RSB_DVCSA.DVCSA_INDERR_INDTYPE; "
                    +"END;");
    cmd.addParam("pErr",     RSDBP_OUT, V_INTEGER);
    cmd.addParam("pIndFIID", RSDBP_OUT, V_INTEGER);
    cmd.addParam("pIndType", RSDBP_OUT, V_INTEGER);
    cmd.execute;
    
    if( SQL_ConvTypeInteger(cmd.value(0)) == 20996 )
        AddLogLine(SvOpDvOper.rec.DocKind, SvOpDvOper.rec.ID, false, "По CSA "+ FD.CSA.rec.Code +" не найден кредитный индекс вида "+ GetValueLLCode(3511, SQL_ConvTypeInteger(cmd.value(2))) +" по  валюте "+ GetCcyCode(SQL_ConvTypeInteger(cmd.value(1))), ID_Operation, ID_Step);
    elif( SQL_ConvTypeInteger(cmd.value(0)) == 20995 )
        AddLogLine(SvOpDvOper.rec.DocKind, SvOpDvOper.rec.ID, false, "CSA "+ FD.CSA.rec.Code +" : Ошибка определения курса индекса "+ GetFI_Code(SQL_ConvTypeInteger(cmd.value(1))), ID_Operation, ID_Step);
    else
        
    end;
    
    return 1;
END;