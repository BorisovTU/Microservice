/*
$Name:        dvnop110.mac
$Module:      Производные инструменты
$Description: Внебиржевая операция. Шаг: Исполнение с поставкой ц/б.
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac";

var EDate:date;
var SDate:date;
var PDate:date;

Private Macro GetBODealID(DealCode)
    var  Change = 0;
    var Select, DataSet, Query;
    Query = "select * from ddl_tick_dbt where "
          + " t_bofficekind = 101 and t_dealcode = ? and t_marketid > 0";
    Select = DL_RSDCommand(Query);
    Select.AddParam(DealCode);
    DataSet = Select.Execute(Query);
    if(DataSet.MoveNext())
       return DataSet.dealid;
    else
       return 0;
    end;
End;
macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record ndeal(dvndeal);
  var DebAccBuf  = TRecHandler("account");
  var CredAccBuf = TRecHandler("account");
  var ДатаИсполненияШага:date;
  SetBuff( ndeal, FDoc );
  var FD = DVFirstDocNDeal(DocKind, ndeal);
  if (FD.ОтказОтИсполнения())
     return 0;
  end;
  var ПлатежТреб, ПлатежОб;
  var MSSCost = 0, BODealID = 0;
  if( FD.ПлатежТреб.rec.PaymentID > 0 )
     ПлатежТреб = RsbPayment(FD.ПлатежТреб.rec.PaymentID);
  end;

  if( FD.ПлатежОб.rec.PaymentID > 0 )
     ПлатежОб = RsbPayment(FD.ПлатежОб.rec.PaymentID);
  end;

  if( (FD.IsOptionAmerican() or FD.IsForwardOpenDate()) and ((EDate == NULL) or (EDate == date(0,0,0))) )
     MsgBox("Вставка шага должна выполняться из скроллинга операций по Ctrl-E.");
     return 1;
  end;

  if( (EDate != NULL) and (EDate != date(0,0,0)) )
     ДатаИсполненияШага = EDate;
  else
     ДатаИсполненияШага = FD.ДатаИсполненияОпцион();   
  end;

  if( (not FD.IsClient()) and (not FD.БиржеваяСделкаКромеВнебиржСПФИ()) )
     if( not FD.ВыполненаПереоценкаСС( ДатаИсполненияШага ) )
        if( MsgBoxEx( "Не выполнена переоценка справедливой стоимости!|Продолжить?", MB_YES+MB_NO, IND_NO ) == IND_NO )
           return 1;
        end;
     end;
  end;

  if( (FD.IsOptionAmerican() or FD.IsForwardOpenDate()) )
     /*Даты передаются шагу и заносятся в платежи*/
     if( FD.ПлатежТреб.rec.PaymentID > 0 )
        ПлатежТреб.ValueDate = FD.ДатаТребования(PDate, SDate);
     end;
     if( FD.ПлатежОб.rec.PaymentID > 0 )
        ПлатежОб.ValueDate   = FD.ДатаОбязательства(PDate, SDate);
     end;
  end;

  if( СписаниеСправедливойСтоимости( FD, doc, ДатаИсполненияШага ) != 0 )
     return 1;
  end;

  if( СписаниеТребованияИОбязательства( FD, doc, ДатаИсполненияШага ) != 0 )
     return 1;
  end;

  if( FD.ПлатежТреб.rec.PaymentID > 0 )
     if( ПИ_УстановитьСтатусПлатежа( ПлатежТреб, PM_CLOSED_W_M_MOVEMENT ) )
        MsgBox( "Ошибка при утановке платежу статуса \"Закрыт без движения\"" );
        return 1;
     end;
  end;

  if( FD.ПлатежОб.rec.PaymentID > 0 )
     if( ПИ_УстановитьСтатусПлатежа( ПлатежОб, PM_CLOSED_W_M_MOVEMENT ) )
        MsgBox( "Ошибка при утановке платежу статуса \"Закрыт без движения\"" );
        return 1;
     end;
  end;
  if( (FD.ВидБазовогоАктива() == FIKIND_AVOIRISS) and ГенерацияСделокБОЦБ_ВНЕБирж() and (FD.deal.rec.marketkind !=1))
     var ДатаОб:date = date(0,0,0), ДатаТреб:date = date(0,0,0);

     if( FD.ПлатежТреб.rec.PaymentID > 0 )
        ДатаТреб = ПлатежТреб.ValueDate;
     end;
    
     if( FD.ПлатежОб.rec.PaymentID > 0 )
        ДатаОб = ПлатежОб.ValueDate;
     end;

     if( ВставитьСделкуБОЦБ_Внебирж( FD, ДатаИсполненияШага, IIF(FD.IsSale_BuyPutOrSaleCall(), ДатаОб, ДатаТреб),
                                                             IIF(FD.IsSale_BuyPutOrSaleCall(), ДатаТреб, ДатаОб) ) == false )
        return 1;
     end;
  end;
/*КД Биржевые*/
  if( (FD.ВидБазовогоАктива() == FIKIND_AVOIRISS) and (FD.deal.rec.marketkind ==1))
    MSSCost = GetSetDVSSCost(FD.Deal.rec.ID, DEALKIND_MRK_OUT, ДатаИсполненияШага);
    BODealID = GetBODealID(FD.Deal.rec.CODE);
    IF(BODealID != 0)
          addNoteForObject( 101, 
                                 L_Z(BODealID, 34 ),
                                 21, 
                                 MSSCost, 
                                 ДатаИсполненияШага )
     end;
  end;

  if( FD.ВидБазовогоАктива() == FIKIND_DERIVATIVE )
     if( ВставитьСделкуПИ_Внебирж( FD, ДатаИсполненияШага ) == false )
        return 1;
     end;
  end;

  if( FD.IsOptionAmerican() )
     if( ДатаИсполненияШага < FD.ДатаИсполнения() )
        /* вставить документ "Уведомление об исполнении" и распечатать */
     end;
  end;

  if( not InsertOprStatus(FD.DV_OPERSTATUS_DEMAND(), DV_OPERSTATUS_DL_COMPLETE) )
     MsgBox("Ошибка при установке статуса <Требования> по операции.");
     return 1;
  end;

  if( not InsertOprStatus(FD.DV_OPERSTATUS_LIABILITY(), DV_OPERSTATUS_DL_COMPLETE) )
     MsgBox("Ошибка при установке статуса <Обязательства> по операции.");
     return 1;
  end;

  if( FD.IsForward() )
     var Дк = DL_GetMinComPlanDateByOper(DocKind, FD.Deal.rec.ID);

     if( Дк != date(0,0,0) )
        if( not InsertOprStatus(FD.DV_OPERSTATUS_COMISS(), DV_OPERSTATUS_PAYM_EX) )
           MsgBox("Ошибка при установке статуса <Оплата комиссий> по операции.");
           return 1;
        end;
        DL_CalendDefineOprDate(FD,DV_NDEAL_OPRDATE_COMISS, Дк);
     else
        if( not InsertOprStatus(FD.DV_OPERSTATUS_COMISS(), DV_OPERSTATUS_PAYM_NOTEX) )
           MsgBox("Ошибка при установке статуса <Оплата комиссий> по операции.");
           return 1;
        end;

        if( not InsertOprStatus(FD.DV_OPERSTATUS_DOC(), DV_OPERSTATUS_DOC_CLOSE) )
           MsgBox("Ошибка при установке статуса <Документооборот> по операции.");
           return 1;
        end;
     end;
  else
     if( not InsertOprStatus(FD.DV_OPERSTATUS_DOC(), DV_OPERSTATUS_DOC_CLOSE) )
        MsgBox("Ошибка при установке статуса <Документооборот> по операции.");
        return 1;
     end;
  end;

  return 0;
end;
