/*
$Name:        dvAlgCalcFrValCurSw.mac
$Module:      Производные инструменты
$Description: Алгоритм расчета справедливой стоимости для валютного СВОПа
*/
IMPORT "dvAlgCalcFrVal.mac";

/* Расчет СС требования и обязательства по части сделки валютного СВОП */
PRIVATE MACRO FairValueCS( pDEAL:TRecHandler, pDate:date, n:integer, pFI:TRecHandler, pBP:double, pBB:double,
                           poD:@double, poL:@double, poFV:@double )

  var v_Ft:integer = ALLFININSTR, /* Валюта требования */
      v_Fo:integer = ALLFININSTR, /* Валюта обязательства */
      v_St:double  = 0.0,         /* Курс валюты требования */
      v_So:double  = 0.0,         /* Курс валюты обязательства */
      v_Nt:money   = $0.0,        /* Сумма требований */
      v_No:money   = $0.0;        /* Сумма обязательств */

  if( ((pDEAL.rec.Type == ALG_DV_SB) and (n == 1)) or ((pDEAL.rec.Type == ALG_DV_BS) and (n == 2)) )
     v_Ft = pFI.rec.PriceFIID;
     v_Fo = pFI.rec.FIID;
     v_St = pBP;
     v_So = pBB;
     v_Nt = pFI.rec.Cost;
     v_No = pFI.rec.Amount;
  else
     v_Ft = pFI.rec.FIID;
     v_Fo = pFI.rec.PriceFIID;
     v_St = pBB;
     v_So = pBP;
     v_Nt = pFI.rec.Amount;
     v_No = pFI.rec.Cost;
  end;

  if( ALG_FairValue(pDate, pFI, v_Ft, v_Fo, v_Nt, v_No, v_St, v_So, ALG_DV_KIND_PRRATE_CURSWAP, ALG_DV_FIKIND_PRRATE_CURRENCY, @poD, @poL, @poFV) != 0 )
     return 1;
  end;

  return 0;
END;

MACRO DVCalcFrVal( pFrVal:TRecHandler, pDEAL:TRecHandler, pFI_1:TRecHandler, pFI_2:TRecHandler ):integer

  var Fininstr = TRecHandler( "fininstr.dbt" );

  if( (pDEAL.rec.DVKind != DV_CURSWAP) and (pDEAL.rec.DVKind != DV_CURSWAP_FX) )
     MsgBox("Алгоритм применим только для операций вида СВОП");
  elif( pFrVal.rec.Date == date(0,0,0) )
     MsgBox("Не задана дата расчёта.");
  elif( ПолучитьФинИн(pFI_1.rec.FIID, Fininstr) )
     MsgBox("Не найден ФИ c ID = " + pFI_1.rec.FIID + ".");
  else
     var v_BP:double = 0.0,
         v_BB:double = 0.0;
     var v_BPSinceDate:date = date(0,0,0),
         v_BBSinceDate:date = date(0,0,0);
     var ToContinue:bool = true;
     if( not ПолучитьКурсЗаданногоTипа(@v_BP, pFI_1.rec.PriceFIID, NATCUR, DV_RATETYPE_CBR, pFrVal.rec.Date, true, @v_BPSinceDate) or
         ((v_BPSinceDate != pFrVal.rec.Date) and (v_BP == 0.0)) )
        MsgBox("Невозможно получить курс вида 'ЦБ РФ' для FIID = " + String(pFI_1.rec.PriceFIID) + " на дату " + String(pFrVal.rec.Date));
     else
        if( v_BPSinceDate != pFrVal.rec.Date )
           ToContinue = false;
           if( MsgBoxEx("На дату операции не найден курс вида 'ЦБ РФ'.| Выполнить расчет по курсу за " + String(v_BPSinceDate) + "?", MB_YES+MB_NO, IND_YES) == IND_YES )
              ToContinue = true;
           end;
        end;

        if( ToContinue == true )
           if( not ПолучитьКурсЗаданногоTипа(@v_BB, pFI_1.rec.FIID, NATCUR, DV_RATETYPE_CBR, pFrVal.rec.Date, true, @v_BBSinceDate) or
               ((v_BBSinceDate != pFrVal.rec.Date) and (v_BB == 0.0)) )
              MsgBox("Невозможно получить курс вида 'ЦБ РФ' для FIID = " + String(pFI_1.rec.FIID) + " на дату " + String(pFrVal.rec.Date));
           else
              if( v_BBSinceDate != pFrVal.rec.Date )
                 ToContinue = false;
                 if( MsgBoxEx("На дату операции не найден курс вида 'ЦБ РФ'.| Выполнить расчет по курсу за " + String(v_BBSinceDate) + "?", MB_YES+MB_NO, IND_YES) == IND_YES )
                    ToContinue = true;
                 end;
              end;

              if( ToContinue == true )
                 var ССт_1:double = $0.0, ССо_1:double = $0.0, СС_1:double = $0.0,
                     ССт_2:double = $0.0, ССо_2:double = $0.0, СС_2:double = $0.0;

                 if( FairValueCS(pDEAL, pFrVal.rec.Date, 2, pFI_2, v_BP, v_BB, @ССт_2, @ССо_2, @СС_2) != 0 )
                    return 1;
                 end;

                 if( pFI_1.rec.ExecDate > pFrVal.rec.Date )
                    if( FairValueCS(pDEAL, pFrVal.rec.Date, 1, pFI_1, v_BP, v_BB, @ССт_1, @ССо_1, @СС_1) != 0 )
                       return 1;
                    end;
                 end;

                 pFrVal.rec.FairValue  = round(money(СС_1 + СС_2), 2);
                 pFrVal.rec.Demand     = round(money(ССт_1), 2);
                 pFrVal.rec.Liability  = round(money(ССо_1), 2);
                 pFrVal.rec.Demand2    = round(money(ССт_2), 2);
                 pFrVal.rec.Liability2 = round(money(ССо_2), 2);
                 pFrVal.rec.SetDemand = pFrVal.rec.SetLiability = pFrVal.rec.SetDemand2 = pFrVal.rec.SetLiability2 = SET_CHAR;

                 return 0; /*рассчитали значение без ошибок*/
              end;
           end;
        end;
     end;
  end;

  return 1; /*по умолчанию ошибка*/
END;
