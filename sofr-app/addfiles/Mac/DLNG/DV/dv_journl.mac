/***************************************************************************************************************
 FILE         : dv_journl.mac
 DESCRIPTION  : Отчет "журнал регистрации поручений клиентов на сделки с производными финансовыми инструментами"
 PROGRAMMED BY: Chernov Anton 21-10-2005
***************************************************************************************************************/

import RsbDataSet, PTInter, CTInter;
import "dvrepfun.mac";
import "globals.mac";
import "or_rep_h.mac";
import "cb_sql.mac";

private const UNDF      =  -1;
const FontStyleTitel =  "ex_FS(b)";
const FontStyleHead0 =  "ex_FS(b):ex_FZ(12)";
const FontStyleHead1 =  "ex_FS(b):ex_FZ(11)";

const W = 5000;
const I = 6000;

var DV_Orders;
var DateStart, DateEnd, IsMarket, IsRecept, IsApp;
var IsNotEof;
var Operator;

private macro ПечатьЗаголовка()
[ 
                                     ЖУРНАЛ РЕГИСТРАЦИИ ПОРУЧЕНИЙ КЛИЕНТОВ
                               на сделки с производными финансовыми инструментами
];
end;

var Table = "┌───────────┬─────────┬─────────────┬─────────────────────┬────────────┬───────────┬──────────────────────────┬─────────────────────┬─────────────────────┐\n" +
            "│   Дата    │  Время  │             │                     │            │   Дата    │                          │                     │    Уполномоченный   │\n" +
            "│ поручения │  приема │ № поручения │    Клиент           │ № договора │ договора  │      Вид операции        │ Отметки об отказах  │      сотрудник      │\n" +
            "│           │поручения│             │                     │            │           │                          │                     │       брокера       │\n" +
            "├───────────┼─────────┼─────────────┼─────────────────────┼────────────┼───────────┼──────────────────────────┼─────────────────────┼─────────────────────┤";

var Rep = CMakeReport(Table);

macro PrintHead( OrderDate )
   Rep.AddPrintCell( "ЖУРНАЛ РЕГИСТРАЦИИ ПОРУЧЕНИЙ КЛИЕНТОВ", Rep.GetHeaderWidth(), 0, "c:" + FontStyleHead0, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddPrintCell( "на сделки с производными финансовыми инструментами", Rep.GetHeaderWidth(), 0, "c:" + FontStyleHead1, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddPrintCell( "за " + OrderDate, Rep.GetHeaderWidth(), 0, "c:" + FontStyleHead1, REP_ELEM_STR );
   Rep.AddStr();
end;

macro GetOperationName( Kind )
   if( DV_Orders.isOrder == 1 )
     if( ( DV_Orders.OrderKind == 2610 ) or ( DV_Orders.OrderKind == 2615 ) )
        return "Покупка";
     elif( ( DV_Orders.OrderKind == 2620 ) or ( DV_Orders.OrderKind == 2625 ) )
        return "Продажа";
     elif( ( DV_Orders.OrderKind == 2630 ) or ( DV_Orders.OrderKind == 2640 ) )
        if( DV_Orders.Position = 1 )
          return "Исполнение, короткие позиции";
        else
          return "Исполнение, длинные позиции";
        end;
     end;
  else
    if( Kind == W )
      return "Вывод средств со счета";
    elif( Kind == I )
      return "Зачисление средств на счет";
    end;
  end;

  return "";
end;

macro PrintLineTable()
   Rep.AddPrintCell(Date(DV_Orders.OrderDate), 0, 0, "l");

   if( (ValType(DV_Orders.OrderTime) != V_UNDEF) and (Time(DV_Orders.OrderTime) != time(0,0,0)) )
     Rep.AddPrintCell(Time(DV_Orders.OrderTime), 0, 0, "l");
   else
     Rep.AddPrintCell("12:00", 0, 0, "l");
   end;

   Rep.AddPrintCell(DV_Orders.OrderNum, 0, 0, "c");

   if( ValType(DV_Orders.ClientName) != V_UNDEF )
     Rep.AddPrintCell(DV_Orders.ClientName, 0, 0, "l");
   else
     Rep.AddPrintCell(" ", 0, 0, "l");
   end;

   if( ValType(DV_Orders.SfContrName) != V_UNDEF )
     Rep.AddPrintCell(DV_Orders.SfContrName, 0, 0, "l");
   else
     Rep.AddPrintCell(" ", 0, 0, "l");
   end;

   if( ValType(DV_Orders.SfContrDate) != V_UNDEF )
     Rep.AddPrintCell(Date(DV_Orders.SfContrDate), 0, 0, "c");
   else
     Rep.AddPrintCell(" ", 0, 0, "c");
   end;

   Rep.AddPrintCell(GetOperationName( DV_Orders.OrderKind ), 0, 0, "c");
   Rep.AddPrintCell(" ", 0, 0, "l");

   if( ValType(DV_Orders.ProxyAgent) != V_UNDEF )
     Rep.AddPrintCell(DV_Orders.ProxyAgent, 0, 0, "l");
   else
     Rep.AddPrintCell(" ", 0, 0, "l");
   end;

   Rep.AddStr();
end;

macro FormQuery( Client, SfContr )
  var sqlQuery;

  sqlQuery = " SELECT 0 isOrder," +
             "        ( SELECT Party.T_NAME FROM dparty_dbt Party WHERE Party.T_PARTYID = SfContr.T_PARTYID ) ClientName," +
             "        SfContr.T_NUMBER SfContrName," +
             "        SfContr.T_DATEBEGIN SfContrDate," +
             "        (CASE WHEN instr(acc.T_USERTYPEDOCUMENT,'W') <> 0 THEN " + string(W) +
             "              WHEN instr(acc.T_USERTYPEDOCUMENT,'I') <> 0 THEN " + string(I) +
             "              ELSE " + string(W+I) + " END) OrderKind," +
             "        0 POSITION," +
             "        acc.T_DATE_CARRY OrderDate," +
             "        TO_DATE('01.01.0001','DD.MM.YYYY') OrderTime," +
             "        (to_char(acc.T_DATE_CARRY,'DDMM')||'/'||acc.t_Numb_Document) OrderNum," +
             "        ( SELECT Person.T_NAME1||' '||SUBSTR(Person.T_NAME2, 1, 1)||'.'||SUBSTR(Person.T_NAME3, 1, 1)||'.'" +
             "            FROM dofficer_dbt Officer, dpersn_dbt Person" +
             "           WHERE Officer.T_ISFIRSTPERSON = 'X' and Officer.T_PARTYID = SfContr.T_PARTYID and " +
             "                Person.T_PERSONID = Officer.T_PERSONID ) ProxyAgent" +
             "   FROM dsfcontr_dbt SfContr, dsfssi_dbt sfsi, dsettacc_dbt sett, dacctrn_dbt acc " +
             "  WHERE SfContr.T_PartyID != " + string({OurBank}) + " and " +
             "        SfContr.T_SERVKIND = " + String(PTSK_DV) + " and " +
             " sfsi.t_ObjectType = " + string(OBJTYPE_SFCONTR) + " and " +
             " sfsi.t_ObjectID = TO_CHAR( SfContr.T_ID, 'FM0999999999') and " +
             " sett.t_SettAccID = sfsi.t_SetAccID and " +
             " (acc.T_ACCOUNT_PAYER = sett.t_Account or T_ACCOUNT_RECEIVER = sett.t_Account) and " +
             " acc.T_Date_Carry >= " + GetSQLDate( DateStart ) +
             " and acc.T_Date_Carry <= " + GetSQLDate( DateEnd ) +
             " and acc.T_State = 1";

  if( IsMarket and IsRecept )
    sqlQuery = sqlQuery +
             " and (instr(acc.T_USERTYPEDOCUMENT, 'W') <> 0 or instr(acc.T_USERTYPEDOCUMENT, 'I') <> 0) ";
  elif( IsMarket )
    sqlQuery = sqlQuery +
             " and instr(acc.T_USERTYPEDOCUMENT, 'W') <> 0 ";
  elif( IsRecept )
    sqlQuery = sqlQuery + 
             " and instr(acc.T_USERTYPEDOCUMENT, 'I') <> 0 ";
  end;

  if( Client and ( Client != UNDF ) )
      sqlQuery = sqlQuery + " and SfContr.T_PARTYID = " + String( Client );
  end;

  if( SfContr and ( SfContr != UNDF ) )
      sqlQuery = sqlQuery + " and SfContr.T_ID = " + String( SfContr );
  end;

  return sqlQuery;
end;

macro ПолучитьПоручения( Client, SfContr )
  var sqlQueryOrder = "", 
      sqlQueryCarry = "";

  sqlQueryOrder = " SELECT 1 isOrder," +
                  "        ( SELECT Party.T_NAME FROM dparty_dbt Party WHERE Party.T_PARTYID = DVDeal.T_CLIENT ) ClientName," +
                  "        ( SELECT SfContr.T_NUMBER FROM dsfcontr_dbt SfContr WHERE SfContr.T_ID = DVPos.T_CLIENTCONTR ) SfContrName," +
                  "        ( SELECT SfContr.T_DATEBEGIN FROM dsfcontr_dbt SfContr WHERE SfContr.T_ID = DVPos.T_CLIENTCONTR ) SfContrDate," +
                  "        DVDeal.T_KIND OrderKind," +
                  "        DVDeal.T_POSITION POSITION," +
                  "        SpGround.T_REGISTRDATE OrderDate," +
                  "        SpGround.T_REGISTRTIME OrderTime," +
                  "        SpGround.T_XLD OrderNum," +
                  "        ( SELECT Person.T_NAME" +
                  "            FROM dperson_dbt Person" +
                  "           WHERE Person.T_Oper = DVDeal.T_Oper ) ProxyAgent " +
                  "   FROM ddvdeal_dbt DVDeal, ddvfipos_dbt DVPos, dfininstr_dbt ContrFI, dfideriv_dbt ContrDV, dspgrdoc_dbt SpGrDoc, dspground_dbt SpGround" +
                  "  WHERE (DVDeal.T_State = 1 or DVDeal.T_State = 2) and " +
                  "        ContrFI.T_FIID = DVDeal.T_FIID and " +
                  "        ContrDV.T_FIID = DVDeal.T_FIID and" +
                  "        DVPos.T_FIID = DVDeal.T_FIID and " +
                  "        DVPos.T_DEPARTMENT = DVDeal.T_DEPARTMENT and " +
                  "        DVPos.T_CLIENT = DVDeal.T_CLIENT and " +
                  "        DVPos.T_BROKER = DVDeal.T_BROKER and " + 
                  "        SpGrDoc.T_SOURCEDOCKIND = 192 and" +
                  "        SpGrDoc.T_SOURCEDOCID = DVDeal.T_ID and " +
                  "        SpGround.T_SPGROUNDID = SpGrDoc.T_SPGROUNDID and " +
                  "        SpGround.T_KIND = 251 " +
                  /*условия по датам*/
                  "        and SpGround.T_RegistrDate >= " + GetSQLDate( DateStart ) +
                  "        and SpGround.T_RegistrDate <= " + GetSQLDate( DateEnd );

   if( Client and ( Client != UNDF ) )
       sqlQueryOrder = sqlQueryOrder + " and DVDeal.T_CLIENT = " + String( Client );
   end;

   if( SfContr and ( SfContr != UNDF ) )
       sqlQueryOrder = sqlQueryOrder + " and DVPos.T_CLIENTCONTR = " + String( SfContr );
   end;

   if( IsMarket or IsRecept ) 
     sqlQueryCarry = FormQuery( Client, SfContr );
   end;

   if( IsMarket or IsRecept ) 
     sqlQueryOrder = "( " + sqlQueryOrder  + " ) union all " + 
                     "( " + sqlQueryCarry + " )";
   end;

   sqlQueryOrder = sqlQueryOrder +
                   " ORDER BY OrderDate, OrderTime, SfContrName, ClientName, SfContrDate, OrderKind;";

   DV_Orders = TRsbDataSet( sqlQueryOrder, RSDVAL_CLIENT, RSDVAL_STATIC );
   DV_Orders.MoveLast();
   return DV_Orders.GetRecCount();
return 0;
end;

macro Печать( OrderDate )
   Rep.AddNewSheetBreak( "за дату "+String(Date(DV_Orders.OrderDate)), Table );
   PrintHead( Date(DV_Orders.OrderDate) );

   while( IsNotEof and (OrderDate == Date(DV_Orders.OrderDate) ) )
      PrintLineTable();
      IsNotEof = DV_Orders.MoveNext();
   end;

   after_44_footer( Rep );
end;

MACRO ReportRun( _ClientID:integer,
                 _SfContr:integer,
                 _IsMarket:bool,
                 _IsRecept:bool,
                 _dateMin:date,
                 _dateMax:date,
                 _IsApp:bool,
                 _IsExcel:bool )

   var Count = 0;

   DateStart = _dateMin;
   DateEnd   = _dateMax;
   IsMarket  = _IsMarket;
   IsRecept  = _IsRecept;
   IsApp     = _IsApp;

   if((_dateMin == Date(0,0,0)) and (_dateMax == Date(0,0,0)) )
      return;
   end;

   if( ПолучитьПоручения( _ClientID, _SfContr ) !=0 )
      IsNotEof = DV_Orders.MoveFirst();
      while( IsNotEof )
         Печать( date(DV_Orders.OrderDate) );
      end;

      if( _IsExcel )
         Rep.PrintWinRep();
         Rep.ShowWinRep();
      else
         Rep.PrintRep();
      end;
   else
     if ( not _IsExcel )
       ПечатьЗаголовка();
       PrintLn("\nНе найдено данных для формирования отчета.");
     else
       MsgBox( "Не найдено данных для формирования отчета.");
     end;
   end;
END;
