/*
$Name:        dv_order.mac
$Module:      Производные инструменты
$Description: Отчет "Поручение клиента на операции с ПИ"
*/
import RsbDataSet;
import "or_rep_h.mac";
import "cb_sql.mac","dvRepFun2.mac","dlmisc.mac","dv_categ.mac";

private const UNDF      =  -1;
const FontStyleTitel =  "ex_FS(b)";
const FontStyleHead0 =  "ex_FS(b):ex_FZ(12)";
const FontStyleHead1 =  "ex_FS(b):ex_FZ(11)";

var DV_Orders;
var DateStart, DateEnd, IsApp;
var IsNotEof;
var Operator;

var IsMarket = -1;
var IsOutMarket = -1;

private macro ПечатьЗаголовка()
                       
[                                          
                                              ПОРУЧЕНИЕ КЛИЕНТА 
           НА СОВЕРШЕНИЕ ОПЕРАЦИЙ С ПРОИЗВОДНЫМИ ФИНАНСОВЫМИ ИНСТРУМЕНТАМИ
];
end;

/*           Клиент: <H1>
           Код клиента: <H2>           Договор № <H3> от <H4>
┌───────────┬─────────┬──────────────┬──────────────┬────────────────────┬─────────────────┬────────────────┬────────────────┬─────────────┬──────────────────┬───────────┬───────────────┐
│           │  Время  │     Вид      │     Вид      │                    │                 │     Валюта     │                │             │                  │    Срок   │    Отметки    │
│№ поручения│  приема │   операции   │  контракта   │   Код контракта    │  Цена исполнеия │ цены исполнеия │      Цена      │ Валюта цена │    Количество    │  действия │   об отказе   │
│           │поручения│              │              │                    │                 │                │                │             │                  │ поручения │               │
├───────────┼─────────┼──────────────┼──────────────┼────────────────────┼─────────────────┼────────────────┼────────────────┼─────────────┼──────────────────┼───────────┼───────────────┤
│           │         │              │              │                    │                 │                │                │             │                  │           │               │
│   A1      │  T1     │      A2      │      A3      │       A4           │       N1        │       A5       │       N2       │     A6      │       N3         │   D1      │     A7  D2    │
└───────────┴─────────┴──────────────┴──────────────┴────────────────────┴─────────────────┴────────────────┴────────────────┴─────────────┴──────────────────┴───────────┴───────────────┘


                                                                                                                                             "___"________ 20__г.
           Клиент/Уполномоченный представитель Клиента  ________________ <F1>
                                                                          М.П.

   


           Поручения получены от Клиента <F2> <F3> <F4> г.
  
           Уполномоченный сотрудник Брокера            _________________ <F5>
                                                                         М.П.

                                                                           
*/

var Table = "┌───────────┬─────────┬──────────────┬──────────────┬────────────────────┬─────────────────┬────────────────┬────────────────┬─────────────┬──────────────────┬───────────┬───────────────┐\n" +
            "│           │  Время  │     Вид      │     Вид      │                    │                 │     Валюта     │                │             │                  │    Срок   │    Отметки    │\n" +
            "│№ поручения│  приема │   операции   │  контракта   │   Код контракта    │  Цена исполнеия │ цены исполнеия │      Цена      │ Валюта цены │    Количество    │  действия │   об отказе   │\n" +
            "│           │поручения│              │              │                    │                 │                │                │             │                  │ поручения │               │\n" +
            "├───────────┼─────────┼──────────────┼──────────────┼────────────────────┼─────────────────┼────────────────┼────────────────┼─────────────┼──────────────────┼───────────┼───────────────┤";



var Rep = CMakeReport(Table);

macro PrintHead( Client, ClientCode, SfContr, SfContrDate );
   Rep.AddPrintCell( "ПОРУЧЕНИЕ КЛИЕНТА", Rep.GetHeaderWidth(), 0, "c:" + FontStyleHead0, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddPrintCell( "на совершение операций с производными финансовыми инструментами", Rep.GetHeaderWidth(), 0, "c:" + FontStyleHead1, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddPrintCell( "Клиент:", 15, 0, "l" , REP_ELEM_STR );
   Rep.AddPrintCell( Client, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddPrintCell( "Код клиента:", 15, 0, "l" , REP_ELEM_STR );
   Rep.AddPrintCell( ClientCode, 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddPrintCell( "Договор №:", 12, 0, "l" , REP_ELEM_STR );
   Rep.AddPrintCell( SfContr, 25, 0, "l:z:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddPrintCell( " от ", 5, 0, "l" , REP_ELEM_STR );
   Rep.AddPrintCell( Date(SfContrDate), 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddStr();
end;

macro PrintLineTable()
  var OperName, Cost, PriceFIID, FD, ExecDate = date(0,0,0), OutDate = " ";
  var FI = ПроизводныйИнструмент( DV_Orders.FIID );

  if( DV_Orders.IsOutMarkTable )
     FD = DVFirstDocNDeal(IIF(DV_Orders.DVKind == DV_FORWARD_T3, DL_DVDEALT3, DL_DVNDEAL), DV_Orders.DealID);
  else
     FD = DVFirstDocDeal(DL_DVDEAL, DV_Orders.DealID);
  end;

   Rep.AddPrintCell(DV_Orders.XLD, 0, 0, "c:" + FontStyleTitel);
   Rep.AddPrintCell(Time(DV_Orders.RegistrTime), 0, 0, "c");
/*----Поле A2 - Вид операции --------------------------------------------------------------------------*/
   if(DV_Orders.IsOutMarkTable == 0)
      if( DV_Orders.Type == "B" )
         OperName = "Покупка";
      elif( DV_Orders.Type == "S" )
         OperName = "Продажа";
      elif(  DV_Orders.Type == "E" )
         if( DV_Orders.Position == 1 )
            OperName = "Исполнение, короткие позиции";
         else
           OperName = "Исполнение, длинные позиции";
         end;
      end;
      Rep.AddPrintCell(OperName, 0, 0, "c");
   end;

   if(DV_Orders.IsOutMarkTable == 1)
  /* Так как учет по сделке на внебирже, то позиций нет.*/
     if( DV_Orders.Type == 1 )
        OperName = "Покупка";
     elif( DV_Orders.Type == 2 )
        OperName = "Продажа";
     end;
     Rep.AddPrintCell(OperName, 0, 0, "c");
   end;

/* Поле A3  Вид контракта
   Вид производного инструмента (для опционов выводится также тип опциона) - "фьючерс" или "опцион put " или "опцион call".
   Для внебиржевых неопционных сделок выводится "форвард", "опцион put " или "опцион call". 
   Для биржевых сделок Т+3 и более (не ПФИ) вида <Покупка/продажа> выводится "фьючерс". 
   Для внебиржевых сделок Т+3 и более (не ПФИ) вида <Покупка/продажа> выводится "форвард".*/
   if(DV_Orders.IsOutMarkTable == 0)
      if(DV_Orders.DVFIKind=="Фьючерс")
         Rep.AddPrintCell(DV_Orders.DVFIKind, 0, 0, "c");
      else
         Rep.AddPrintCell("Опцион  " + FI.OptionType(), 0 , 0, "c");
      end;
   end;

   if(DV_Orders.DVKind == DV_FORWARD_T3)
     if(DV_Orders.IsOutMark == 0)
        Rep.AddPrintCell("Фьючерс ", 0, 0, "c");
     else
        Rep.AddPrintCell("Форвард ", 0, 0, "c");
     end;
   elif((DV_Orders.IsOutMarkTable == 1) and (DV_Orders.IsOutMark == 1))
     if(DV_Orders.DVKind == 2) /* Опцион*/
        if(DV_Orders.OPTIONTYPE == 1)
           Rep.AddPrintCell("Опцион  " + " put ", 0 , 0, "c");
        elif(DV_Orders.OPTIONTYPE == 2)
           Rep.AddPrintCell("Опцион  " + " call ", 0 , 0, "c");
        end;  
     else
        Rep.AddPrintCell("Форвард ", 0, 0, "c");
     end;
   end;

/*
 A4   Код контракта  Код производного инструмента на бирже.
 Для внебиржевых сделок и сделок Т+3 и более (не ПФИ) вида <Покупка/продажа> - вид базисного актива + код базисного актива + дата поставки в  формате dd.mm.уу + текст "расчетный" или "поставочный"
 + (только для внебиржевых опционных сделок) стиль опциона: "European" или "American"
*/
   if(DV_Orders.IsOutMarkTable == 0)
      Rep.AddPrintCell(DV_Orders.FI_Code, 0, 0, "l");
   end;                                                      

   if(DV_Orders.IsOutMarkTable == 1)
      Rep.AddPrintCell(DV_Orders.DVFIKind +" "+ DV_Orders.FI_Code, 0, 0, "l");  
   end;
/*
 N1   Цена исполнения   Для сделок с опционами - цена исполнения (страйк) опциона. В остальных случаях не заполняется.
 A5   Валюта цены Выводится буквенный код (по ISO) валюты цены страйка 
 */ 
   if( DV_Orders.Strike )
      Rep.AddPrintCell(DV_Orders.Strike, Rep.GetColWidthTable(5), 0, "r");
      Rep.AddPrintCell(DV_Orders.StrikeFI, Rep.GetColWidthTable(6), 0, "c");
   else
      Rep.AddPrintCell(" ", Rep.GetColWidthTable(5), 0, "r");
      Rep.AddPrintCell(" ", Rep.GetColWidthTable(6), 0, "r");
   end;
/*
 N2   Цена  Для сделок покупки/продажи фьючерсов, форвардов, сделок Т+3 и более (не ПФИ) вида <Покупка/продажа> - цена сделки,
 Для сделок покупки/продажи опционов - указывается размер премии по одному контракту.
 Для операций исполнения фьючерсов - цена исполнения из операции исполнения
 Для операций исполнения опционов - цена страйк (цена исполнения опциона из справочника ПИ)

 A6   Валюта цены Выводится буквенный код валюты цены по ISO или пункты
*/
   if(DV_Orders.IsOutMarkTable == 0)
   if(DV_Orders.DVFIKind=="Фьючерс")
     Cost = DV_Orders.CostDeal;
   else
      if( (DV_Orders.Type == "B") or (DV_Orders.Type == "S") ) 
         Cost = DV_Orders.BonusDeal;
      else
         Cost = DV_Orders.Strike;
      end;
   end;
   Rep.AddPrintCell(Money(Cost), Rep.GetColWidthTable(7), 0, "r");
   Rep.AddPrintCell(FI.PriceCCY(), Rep.GetColWidthTable(8), 0, "c");
   end;
 
   if(DV_Orders.IsOutMarkTable == 1)
     Cost = DV_Orders.CostDeal; /*Цена исполнения есть выше, тут только премия*/
     if( Cost > $0. )
       PriceFIID = GetCCY(DV_Orders.T_BONUSFIID);
     else
        PriceFIID = "";
     end;
     Rep.AddPrintCell(Money(Cost), Rep.GetColWidthTable(7), 0, "r");
     Rep.AddPrintCell(PriceFIID, Rep.GetColWidthTable(8), 0, "c");
   end;
/*
 N3   Кол-во   Количество контрактов в сделке  (операции)
*/     
   Rep.AddPrintCell(DV_Orders.Amount, Rep.GetColWidthTable(9), 0, "r");
/*
 D1   Срок действия поручения Дата заключения сделки или выполнения операции.
*/
   Rep.AddPrintCell(Date(DV_Orders.RegistrDate), Rep.GetColWidthTable(10), 0, "r");
/*
 А7   Отметки об отказе Если был выполнен отказ сделки, то выводится текст "отк. от сделки"
*/
   if( DV_Orders.IsOutMarkTable and (FD.IsRejectDealExcecute(null,@ExecDate)) )
      OutDate = "отк. от сделки " + date_as_string( 1, ExecDate );
   end;
   Rep.AddPrintCell(OutDate, Rep.GetColWidthTable(11), 0, "r");

   Rep.AddStr();

end;


macro PrintAfterFooter();
 var ProxyAgent, OperName,Dat;

   Dat = String(Date(DV_Orders.Date):m);
   Rep.AddPrintCell( " ", Rep.GetHeaderWidth(), 0, "c", REP_ELEM_STR );
   Rep.AddStr();

   if(DV_Orders.ProxyAgent)
      ProxyAgent = DV_Orders.ProxyAgent;
      var pos:integer = StrBrk(ProxyAgent, StrFor(1));
      if (pos != 0)
        ProxyAgent = substr(ProxyAgent,1,pos-1)+" "+substr(ProxyAgent,pos+1);
      end;

      pos = StrBrk(ProxyAgent, StrFor(1));
      if (pos != 0)
        ProxyAgent = substr(ProxyAgent,1,pos-1)+" "+substr(ProxyAgent,pos+1);
      end;

   else
      ProxyAgent = "  ";                                                       
   end;

   Rep.AddPrintCell( "Клиент/Уполномоченный представитель Клиента _____________________________" + ProxyAgent, Rep.GetHeaderWidth(), 0, "l:z:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddPrintCell( "                                                                             М.П.", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddPrintCell( "Поручения получены от Клиента " + Dat , Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddPrintCell( " ", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddStr();

   if(DV_Orders.OperName)
      OperName = DV_Orders.OperName;
   else
      OperName = "  ";
   end;

   Rep.AddPrintCell( "Уполномоченный сотрудник Брокера            _____________________________" + OperName, Rep.GetHeaderWidth(), 0, "l:z:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddPrintCell( "                                                                             М.П.", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   Rep.AddStr();
end;

macro ПолучитьПоручения( Client, IsFormID, FormSubID, IsVidID, VidSubID, NoResident, SfContr, OperationID, BaseKind, BaseFIID, DVKind, DVFIID, Issuer )

  var sqlQuery = "";

  private macro ПолучитьБиржевыеСделки( Client, IsFormID, FormSubID, IsVidID, VidSubID, NoResident, SfContr, OperationID, BaseKind, BaseFIID, DVKind, DVFIID, Issuer )
      sqlQuery = " SELECT ( SELECT Party.T_NAME FROM dparty_dbt Party WHERE Party.T_PARTYID = DVDeal.T_CLIENT ) ClientName," +
                 "        ( select t_Code from dobjcode_dbt where t_codekind = 1 and t_objectid = DVDeal.T_CLIENT and t_State = 0 ) ClientCode," +
                 "        ( SELECT SfContr.T_NUMBER FROM dsfcontr_dbt SfContr WHERE SfContr.T_ID = DVPos.T_CLIENTCONTR ) SfContrName," +                                               
                 "        ( SELECT SfContr.T_DATEBEGIN FROM dsfcontr_dbt SfContr WHERE SfContr.T_ID = DVPos.T_CLIENTCONTR ) SfContrDate," +                                            
                 "        0 IsOutMarkTable, " +
                 "        0 IsOutMark,  " +
                 "        DVDeal.T_KIND," +
                 "        DVDeal.T_TYPE," +
                 "        -1 T_BONUSFIID,  " +
                 "        -1 T_DVKIND,  " +
                 "        0 T_OPTIONTYPE,  " +
                 "        0 T_OPTIONSTYLE,  " +
                 "        SpGround.T_REGISTRTIME," +
                 "        SpGround.T_REGISTRDATE," +
                 "        SpGround.T_XLD," +
                 "        ( SELECT AvrKinds.T_NAME FROM davrkinds_dbt AvrKinds WHERE AvrKinds.T_FI_KIND = ContrFI.T_FI_KIND and AvrKinds.T_AVOIRKIND = ContrFI.T_AVOIRKIND) DVFIKind," +
                 "        ContrFI.T_FI_CODE," +
                 "        ContrDV.T_STRIKE," +                                                                                                                                          
                 "        ( SELECT FI.T_CCY FROM dfininstr_dbt FI WHERE FI.T_FIID = DECODE( ContrDV.T_STRIKE, 0.0, NULL, ContrDV.T_STRIKEFIID ) ) StrikeFI," +
                 "        ( DVDeal.T_PRICE ) CostDeal," +
                 "        ( DVDeal.T_BONUS ) BonusDeal," +
                 "        (DVDeal.T_FIID) FIID," +
                 "        DVDeal.T_POSITION," +
                 "        DVDeal.T_AMOUNT," +                                                               
                 "        DVDeal.T_DATE," +
                 "        DVDeal.T_CLIENT," +
                 "        DVPos.T_CLIENTCONTR," +
                 "        ( SELECT Person.T_NAME1||' '||SUBSTR(Person.T_NAME2, 1, 1)||'.'||SUBSTR(Person.T_NAME3, 1, 1)||'.'" +
                 "            FROM dofficer_dbt Officer, dpersn_dbt Person" +
                 "           WHERE Officer.T_ISFIRSTPERSON = 'X' and Officer.T_PARTYID = DVDeal.T_CLIENT and " +
                 "                Person.T_PERSONID = Officer.T_PERSONID and ROWNUM = 1) ProxyAgent," +
                 "        ( SELECT t_name FROM dperson_dbt person WHERE person.t_oper=" + string(Operator) + ") OperName," +
                 "        DVDEAL.T_ID T_DealID" +
                 "   FROM ddvdeal_dbt DVDeal, ddvfipos_dbt DVPos, dfininstr_dbt ContrFI, dfideriv_dbt ContrDV, dspgrdoc_dbt SpGrDoc, dspground_dbt SpGround," +
                 "        dfininstr_dbt BaseFI1, dfininstr_dbt BaseFI2, dparty_dbt Pt " +
                 "  WHERE ContrFI.T_FIID = DVDeal.T_FIID and " +
                 "        ContrDV.T_FIID = DVDeal.T_FIID and" +
                 "        Pt.T_PARTYID = DVDeal.T_CLIENT and" +
                 "        DVPos.T_FIID = DVDeal.T_FIID and " +
                 "        DVPos.T_DEPARTMENT = DVDeal.T_DEPARTMENT and " +
                 "        DVPos.T_CLIENT = DVDeal.T_CLIENT and " +
                 "        DVPos.T_BROKER = DVDeal.T_BROKER and " + 
                 "        SpGrDoc.T_SOURCEDOCKIND = 192 and" +                                                                                                                         
                 "        SpGrDoc.T_SOURCEDOCID = DVDeal.T_ID and " +
                 "        SpGround.T_SPGROUNDID = SpGrDoc.T_SPGROUNDID and " +
                 "        SpGround.T_KIND = 251 and " +
                 "        BaseFI1.T_FIID = ContrFI.T_FACEVALUEFI and " +
                 "        BaseFI2.T_FIID = DECODE(BaseFI1.T_FI_KIND, 4, BaseFI1.T_FACEVALUEFI, ContrFI.T_FACEVALUEFI)" +
                 /*условия по датам*/
                 "        and DVdeal.T_DATE >= " + GetSQLDate( DateStart ) +
                 "        and DVdeal.T_DATE <= " + GetSQLDate( DateEnd ); 

   /* Если надо уставливаем условие по клиенту*/
   if( Client and ( Client != UNDF ) )
       sqlQuery = sqlQuery + " and DVDeal.T_CLIENT = " + String( Client );
   else
       sqlQuery = sqlQuery + " and DVDeal.T_CLIENT <> -1 ";
   end;

   if( (IsFormID > 0) and (FormSubID != 0) )
      if(IsFormID == 1)
        sqlQuery = sqlQuery + "    and   Pt.T_LEGALFORM =   " + String (FormSubID);
      end;
      if(IsFormID == 2)            	
        sqlQuery = sqlQuery + "    and   Pt.T_LEGALFORM !=   " + String (FormSubID);
      end;
   end;

   if( (IsVidID > 0) and (VidSubID != 0) )            
      if(IsVidID == 1)
        sqlQuery = sqlQuery + "   and    EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (VidSubID) +" and Pt.T_PARTYID = Pto.T_PARTYID)  " ;
      end;
      if(IsVidID == 2)            	
        sqlQuery = sqlQuery + "    and   NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (VidSubID)+ " and Pt.T_PARTYID = Pto.T_PARTYID)  " ;
      end;
   end;

   if(NoResident)
      sqlQuery = sqlQuery + " and  Pt.T_NOTRESIDENT = 'X'  ";
   end;

   /* Если надо уставливаем условие по клиенту*/
   if( SfContr and ( SfContr != UNDF ) )
       sqlQuery = sqlQuery + " and DVPos.T_CLIENTCONTR = " + String( SfContr );
   else
   //       sqlQuery = sqlQuery + " and DVPos.T_CLIENTCONTR <> -1 ";
   end;
   /* Если надо устанавливаем фильтр по типу операции*/
   if( OperationID and ( OperationID != UNDF ) )
       sqlQuery = sqlQuery + " and DVDeal.T_KIND = " + String( OperationID );
   end;
   /* Если надо уставливаем условие типу базового актива*/
   if( BaseKind and (BaseKind != UNDF ) )
       sqlQuery = sqlQuery + " and BaseFI2.T_FI_KIND = " + String( BaseKind );
   end;
   /* Если надо уставливаем условие базому активу*/
   if( BaseFIID and (BaseFIID != UNDF ) )
       sqlQuery = sqlQuery + " and DECODE(BaseFI1.T_FI_KIND, 4, BaseFI1.T_FACEVALUEFI, ContrFI.T_FACEVALUEFI) = " + String( BaseFIID );
   end;
   /* Если надо уставливаем условие типу ПИ*/
   if( DVKind and ( DVKind != UNDF ) )
       sqlQuery = sqlQuery + " and ContrFI.T_AVOIRKIND = " + String( DVKind );
   end;
   /* Если надо уставливаем условие по ПИ*/
   if( DVFIID and ( DVFIID != UNDF ) )
       sqlQuery = sqlQuery + " and DVDeal.T_FIID = " + String( DVFIID );
   end;
   /* Если надо уставливаем условие по эмитенту*/
   if( Issuer and ( Issuer != UNDF ) )
       sqlQuery = sqlQuery + " and ContrFI.T_ISSUER = " + String( Issuer );
   end;

  end;

  private macro ПолучитьВнеБиржевыеСделки( Client, IsFormID, FormSubID, IsVidID, VidSubID, NoResident, SfContr, OperationID, BaseKind, BaseFIID, DVKind, DVFIID, Issuer )

  sqlQuery = sqlQuery +  "   SELECT (SELECT Party.T_NAME "+
          "             FROM dparty_dbt Party   "+
          "            WHERE Party.T_PARTYID = DVDeal.T_CLIENT)   "+
          "             ClientName, "+
          "          (SELECT t_Code "+
          "             FROM dobjcode_dbt "+
          "            WHERE t_codekind = 1 and t_objectid = DVDeal.T_CLIENT and t_State = 0 ) "+
          "             ClientCode, "+
          "          (SELECT SfContr.T_NUMBER   "+
          "             FROM dsfcontr_dbt SfContr  "+
          "            WHERE SfContr.T_ID = DVDeal.T_CLIENTCONTR) "+
          "             SfContrName,   "+
          "          (SELECT SfContr.T_DATEBEGIN   "+
          "             FROM dsfcontr_dbt SfContr  "+
          "            WHERE SfContr.T_ID = DVDeal.T_CLIENTCONTR) "+
          "             SfContrDate,   "+
          "          1 IsOutMarkTable, " +
          "          case when ((DVDeal.t_DVKind = " + DV_FORWARD_T3 + ") and ( EXISTS ( SELECT * " +
          "                                                                                FROM DPARTYOWN_DBT " +
          "                                                                               WHERE T_PARTYID   = DVDeal.t_Contractor " +
          "                                                                                 AND T_PARTYKIND = " + PTK_MARKETPLASE +
          "                                                                            ) " +
          "                                                                   ) ) then 0 else 1 end IsOutMark," +
          "          DVDeal.T_KIND, "+
          "          TO_CHAR (DVDeal.T_TYPE) T_TYPE,  "+
          "          DVDeal.T_BONUSFIID,  "+
          "          DVDeal.T_DVKIND,  "+
          "          DVDeal.T_OPTIONTYPE, "+
          "          DVDeal.T_OPTIONSTYLE,   "+
          "          SpGround.T_REGISTRTIME, "+
          "          SpGround.T_REGISTRDATE, "+
          "          SpGround.T_XLD,   "+
          "          DECODE (    "+
          "           ContrFI.T_FI_KIND,  "+
          "           2                      "+                           /*FIKIND_AVOIRISS*/
          "           , (SELECT avr.t_Name      "+
          "                FROM davrkinds_dbt avr  "+
          "               WHERE avr.t_fi_kind = ContrFI.t_fi_kind "+
          "                     AND avr.t_avoirkind =          "+
          "                            RSB_FIInstr.               "+
          "                             FI_AvrKindsGetRoot (      "+
          "                               ContrFI.t_fi_kind,      "+
          "                               ContrFI.t_avoirkind)),  "+
          "           'Индекс')                                      "+
          "             DVFIKind,   "+
          "             CONTRFI.T_FI_CODE "+
          "          || ' '   "+
          "          || TO_CHAR (nfi.T_SuplDate, 'DD.MM.YY')   "+
          "          || '  '  "+
          "          || CASE nfi.T_EXECTYPE  "+
          "                WHEN 0 THEN 'Расчетный' "+
          "                WHEN 1 THEN 'Поставочный'  "+
          "             END   "+
          "          || '  '  "+
          "          || CASE DVDeal.T_OPTIONSTYLE  "+
          "                WHEN 1 THEN 'American'  "+
          "                WHEN 2 THEN 'Europian'  "+
          "             END   "+
          "             AS T_FI_CODE,                                "+
          "          CASE  "+
          "             WHEN DVDeal.T_DVKIND = 2 THEN nfi.T_Price * nfi.T_Amount "+
          "             ELSE 0   "+
          "          END   "+
          "             T_STRIKE,                                    "+
          "          (SELECT FI.T_CCY  "+
          "             FROM dfininstr_dbt FI   "+
          "            WHERE FI.T_FIID = DECODE (T_STRIKE, 0.0, NULL, NFI.T_PRICEFIID))   "+
          "             StrikeFI,   "+
          "          CASE  "+
          "             WHEN DVDeal.T_DVKIND = 2 THEN DVDeal.T_BONUSPRICE  "+
          "             ELSE (nfi.T_PRICE * nfi.T_Amount)   "+
          "          END   "+
          "             CostDeal,                                             "+
          "          (DVDeal.T_BONUS) BonusDeal,   "+
          "          (nfi.T_FIID) FIID,   "+
          "          0 T_POSITION,  "+
          "          nfi.T_CONTRAMOUNT T_AMOUNT,   "+
          "          DVDeal.T_DATE, "+
          "          DVDeal.T_CLIENT,  "+
          "          DVDeal.T_CLIENTCONTR,   "+
          "          (SELECT    Person.T_NAME1  "+
          "                  || ' ' "+
          "                  || SUBSTR (Person.T_NAME2, 1, 1)  "+
          "                  || '.' "+
          "                  || SUBSTR (Person.T_NAME3, 1, 1)  "+
          "                  || '.' "+
          "             FROM dofficer_dbt Officer, dpersn_dbt Person "+
          "            WHERE     Officer.T_ISFIRSTPERSON = 'X' "+
          "                  AND Officer.T_PARTYID = DVDeal.T_CLIENT "+
          "                  AND Person.T_PERSONID = Officer.T_PERSONID "+
          "                  AND ROWNUM = 1) "+
          "             ProxyAgent, "+
          "          (SELECT t_name "+
          "             FROM dperson_dbt person "+
               "             WHERE person.t_oper = " + string(Operator) +   "   )                     "+
          "             OperName,   "+
          "          DVDEAL.T_ID T_DealID" +
          "     FROM ddvndeal_dbt DVDeal, "+
          "          dfininstr_dbt ContrFI,  "+
          "          dfideriv_dbt ContrDV,   "+
          "          dspgrdoc_dbt SpGrDoc,   "+
          "          dspground_dbt SpGround, "+
          "          ddvnfi_dbt nfi,   "+
          "          ddvnfi_dbt nfi2, dparty_dbt Pt   "+
          "    WHERE     nfi.T_DealID = DVDeal.T_ID   "+
          "          AND ContrFI.T_FIID = nfi.T_FIID         /*здесь берем базовый актив*/   "+
          "          AND ContrDV.T_FIID(+) = nfi.T_FIID  "+
          "          AND  Pt.T_PARTYID = DVDeal.T_CLIENT " +
          "          AND SpGrDoc.T_SOURCEDOCKIND IN (" + DL_DVNDEAL + "," + DL_DVDEALT3 + ") "+
          "          AND SpGrDoc.T_SOURCEDOCID = DVDeal.T_ID   "+
          "          AND SpGround.T_SPGROUNDID = SpGrDoc.T_SPGROUNDID   "+
          "          AND SpGround.T_KIND = 251  "+
          "          AND nfi2.T_DealID = DVDeal.T_ID  "+
          "          AND nfi2.T_TYPE = 0                               /*актив по сделке*/   "+
          "          AND nfi.T_TYPE = CASE WHEN DVDeal.T_FORVARD = 'X' THEN 1 ELSE 0 END /*актив по форварду или по сделке  если  nfi.T_TYPE ==1 тогда  BaseFI2 = nfi.T_FIID*/   "+
         /*условия по датам*/
          "          AND DVdeal.T_DATE >= " + GetSQLDate( DateStart ) +
          "          AND DVdeal.T_DATE <= " + GetSQLDate( DateEnd ) ;

    /* Если надо уставливаем условие по клиенту*/
     if( Client and ( Client != UNDF ) )
        sqlQuery = sqlQuery + " and DVDeal.T_CLIENT = " + String( Client );
     else
        sqlQuery = sqlQuery + " and DVDeal.T_CLIENT <> -1 ";
     end;

     if( (IsFormID > 0) and (FormSubID != 0) )
        if(IsFormID == 1)
          sqlQuery = sqlQuery + "    and   Pt.T_LEGALFORM =   " + String (FormSubID);
        end;
        if(IsFormID == 2)            	
          sqlQuery = sqlQuery + "    and   Pt.T_LEGALFORM !=   " + String (FormSubID);
        end;
     end;

     if( (IsVidID > 0) and (VidSubID != 0) )            
        if(IsVidID == 1)
          sqlQuery = sqlQuery + "   and    EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (VidSubID) +" and Pt.T_PARTYID = Pto.T_PARTYID)  " ;
        end;
        if(IsVidID == 2)            	
          sqlQuery = sqlQuery + "    and   NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (VidSubID ) + " and Pt.T_PARTYID = Pto.T_PARTYID)  " ;
        end;
     end;

   if(NoResident)
       sqlQuery = sqlQuery + " and  Pt.T_NOTRESIDENT = 'X'  ";
     end;

    /* Если надо уставливаем условие по клиенту*/
     if( SfContr and ( SfContr != UNDF ) )
        sqlQuery = sqlQuery + " and  DVDeal.T_CLIENTCONTR  = " + String( SfContr );
     else
 //       sqlQuery = sqlQuery + " and DVDeal.T_CLIENTCONTR <> -1 ";
     end;
   /* Если надо устанавливаем фильтр по типу операции*/
     if( OperationID and ( OperationID != UNDF ) )
        sqlQuery = sqlQuery + " and DVDeal.T_KIND = " + String( OperationID );
     end;
   /* Если надо уставливаем условие типу базового актива*/
     if( BaseKind and (BaseKind != UNDF ) )
        sqlQuery = sqlQuery + " and ContrFI.T_FI_KIND = " + String( BaseKind );
     else/*"ценная бумага" или "индекс"*/
        sqlQuery = sqlQuery + " and ContrFI.T_FI_KIND in ("+FIKIND_AVOIRISS+","+FIKIND_INDEX+")";
     end;

   /* Если надо уставливаем условие базому активу*/  
     if( BaseFIID and (BaseFIID != UNDF ) )
         sqlQuery = sqlQuery + " and nfi.T_FIID = " + String( BaseFIID );
     end;

   /* Если надо уставливаем условие типу ПИ*/
     if( DVKind and ( DVKind != UNDF ) )/*в базе: 1,5 - Форвард, 2 - Опцион*/
     /*из панели: 1 - Фьючерс, 2 - Опцион, 3 - Форвард*/
         if( DVKind == 1 )
            sqlQuery = sqlQuery + " and DVDeal.T_DVKIND = " + DV_FORWARD_T3/*5*/;
         elif( DVKind == 2 )
            sqlQuery = sqlQuery + " and DVDeal.T_DVKIND = 2 ";
         elif( DVKind == 3 )
            sqlQuery = sqlQuery + " and DVDeal.T_DVKIND IN (1,5) ";
         end;
     else
        sqlQuery = sqlQuery + " and DVDeal.T_DVKIND in (1,2,5) "; /*1,5 - Форвард, 2 - Опцион*/
     end;

   /* Если надо уставливаем условие по ПИ*/
     if( DVFIID and ( DVFIID != UNDF ) )
        sqlQuery = sqlQuery + " and nfi.T_FIID  = " + String( DVFIID );
     end;
   /* Если надо уставливаем условие по эмитенту*/
     if( Issuer and ( Issuer != UNDF ) )
        sqlQuery = sqlQuery + " and ContrFI.T_ISSUER = " + String( Issuer );
     end;

   /* Биржевые/Внебиржевые/Все*/
     if( not (IsOutMarket and IsMarket) ) // если взведен только один флаг
        var flag = IIF(IsMarket, "", " not "); // биржевые или внебиржевые

        sqlQuery = sqlQuery + 
          "     and (case when ((DVDeal.t_DVKind = " + DV_FORWARD_T3 + ") and ("+flag+" EXISTS ( SELECT * " +
          "                                                                                FROM DPARTYOWN_DBT " +
          "                                                                               WHERE T_PARTYID   = DVDeal.t_Contractor " +
          "                                                                                 AND T_PARTYKIND = " + PTK_MARKETPLASE +
          "                                                                            ) " +
          "                                                                   ) ) then 0 else 1 end) = 0 ";
     end;

  end; /*macro ПолучитьВнеБиржевыеСделки()*/


  if( IsMarket )
      ПолучитьБиржевыеСделки( Client, IsFormID, FormSubID, IsVidID, VidSubID, NoResident, SfContr, OperationID, BaseKind, BaseFIID, DVKind, DVFIID, Issuer );
  end;

  if( IsOutMarket or IsMarket ) /*во внебиржевых таблицах живут биржевые сделки Т+3*/
     if(sqlQuery  != "" )
        sqlQuery = sqlQuery + "  union all  ";
     end;
     ПолучитьВнеБиржевыеСделки( Client, IsFormID, FormSubID, IsVidID, VidSubID, NoResident, SfContr, OperationID, BaseKind, BaseFIID, DVKind, DVFIID, Issuer );
  end;

  /* Уставливаем Нужную сортировку для последующего вывода*/
  if( sqlQuery  != "" )
      sqlQuery = sqlQuery + " ORDER BY T_CLIENT, T_CLIENTCONTR, T_DATE ";
  end;

  DV_Orders = TRsbDataSet( sqlQuery, RSDVAL_CLIENT, RSDVAL_STATIC );
  DV_Orders.MoveLast();

  return DV_Orders.GetRecCount();

end; /*macro ПолучитьПоручения*/

macro Печать( OrderDate, Client, SfContr )
   Rep.AddNewSheetBreak( DV_Orders.ClientName+"_"+DV_Orders.SfContrName+"_"+String(Date(DV_Orders.Date))  , Table );
   PrintHead( DV_Orders.ClientName, DV_Orders.ClientCode, DV_Orders.SfContrName, DV_Orders.SfContrDate );
   while( IsNotEof and ( ( OrderDate == Date(DV_Orders.Date) ) and 
                         ( SfContr == DV_Orders.ClientContr ) and
                         ( Client == DV_Orders.Client ) ) )
      PrintLineTable();
      IsNotEof = DV_Orders.MoveNext();
   end;
   PrintAfterFooter();
end;

MACRO ReportRun(  _ClientID:integer,
                  _IsFormID:integer,
                  _FormSubID:integer,
                  _IsVidID:integer,
                  _VidSubID:integer,
                  _NoResident:bool,
                  _SfContr:integer,
                  _IsMarket:bool,
                  _ContrByDeal:integer,
                  _IsOutMarket:bool,
                  _OperationID:integer, 
                  _BaseKind:integer,
                  _BaseFIID:integer,
                  _DVKind:integer,
                  _IssuerID:integer,
                  _DVFIID:integer,
                  _dateMin:date,
                  _dateMax:date,
                  _ProxyAgent:integer,
                  _IsApp:bool,
                  _IsExcel:bool )


   var Count = 0;

   DateStart = _dateMin;
   DateEnd   = _dateMax;
   IsApp = _IsApp;
   Operator = _ProxyAgent;

   IsMarket =  _IsMarket;              
   IsOutMarket = _IsOutMarket;
         
   Dl_CallInsertStat( IIF( _IsExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
   if( (DateStart == Date(0,0,0)) and (DateEnd == Date(0,0,0)) )
     return;
   end;

   if( IsMarket or IsOutMarket )
      if( ПолучитьПоручения( _ClientID, _IsFormID, _FormSubID, _IsVidID, _VidSubID, _NoResident, _SfContr, _OperationID, _BaseKind, _BaseFIID, _DVKind, _DVFIID, _IssuerID ) !=0 )
         IsNotEof = DV_Orders.MoveFirst();
         while( IsNotEof )
            Печать( DV_Orders.Date, DV_Orders.Client, DV_Orders.ClientContr );
         end;
   
         if( _IsExcel )
            Rep.PrintWinRep();
            Rep.ShowWinRep();
         else
            Rep.PrintRep();
         end;
      else
        if ( not _IsExcel )
         ПечатьЗаголовка();
         PrintLn("\nНе найдено данных для формирования отчета.");
        else
          MsgBox( "Не найдено данных для формирования отчета.");
        end;
      end;
   end;
END;
