/*
$Name:        dvop050.mac
$Module:      Производные инструменты
$Description: Операция расчетов по ПИ. Шаг - Исполнение поставочных контрактов
*/

import InsCarryDoc, FIInter, "dvserv.mac", "dv_fun.mac", "dv_carop.mac";

private var FI          = TRecHandler( "fininstr.dbt");
private var Deriv       = TRecHandler( "fideriv.dbt");
private var FinInstr    = TRecHandler( "fininstr.dbt");
private var FinInstrG   = TRecHandler( "fininstr.dbt");
private var Party       = TRecHandler( "party.dbt" );
private var DLMarket    = TRecHandler( "dlmarket.dbt" );
/*Настраиваемые пользоватьелем константы операций в БО ЦБ*/
private const BuyMarket  = 2144; /* Покупка ц/б биржевая today */
private const SaleMarket = 2154; /* Продажа ц/б биржевая today */
private const BuyBroker  = 2162;
private const SaleBroker = 2172;

/*виды секций бирж*/
/*ММВБ*/
private const СекторГосЦБ = 1; /*секция гос.бумаг*/
private const СекторФонд  = 2; /*секция фондового рынка*/
/*РТС*/
private const СекторСГК      = 1; /*система гарантированных котировок*/
private const СекторТрадРТС  = 2; /*традиционная РТС*/

/* Схема расчетов с биржей */
const MICEX_DEPSET    = 6; /*ММВБ*/
const MICEX_FB_DEPSET = 7; /*ФБ ММВБ*/
const RTS_DEPSET      = 8; /*РТС*/

private const IsMacroTuning = true;

private macro GetDealType( IsBuy:bool, IsBroker:bool )
  if( IsMacroTuning )
     if( IsBuy )
        if( IsBroker ) 
           return BuyBroker;
        else
           return BuyMarket;
        end;
     else
        if( IsBroker ) 
           return SaleBroker;
        else
           return SaleMarket;
        end;
     end;
 else
 /* Реализовать отдельную функцию выбора на SELECT`е */
 end;
end;

macro ExecuteStep( doc, FDoc)
  var Count, CountDeal;
  var FD_Oper;
  var Query, PosByOper, DealByPos;
  var IsBuy = true, IsBroker = false, ExitFlag:bool = false;
  var SuplyTime, DeliveringFIID, FIID = -1, SSCost = 0., NKD_CFI = $0;
  var AccBuf = TRecHandler("account");

  record oper(dvoper);

  record tick(dl_tick);
  record leg(dl_leg);

  SetBuff( oper , FDoc );

  FD_Oper = DVFirstDocOper( DL_DVOPER, oper );

  if( not ГенерацияСделокБОЦБ_Бирж() )
     return 0;
  end;

  Query = "SELECT * FROM ddvfipos_dbt POS WHERE " + PositionByOperationDate( oper ) + ";";
  PosByOper  = TRsbDataSet(Query);

  if( PosByOper )           
     Count = 0;
     InitProgress( PosByOper.GetRecCount(), "Перебор позиций по операции расчетов", "Выполнение..." );

     while( PosByOper.MoveNext() AND (ExitFlag == false) )

        if( (ExitFlag == false) AND (not ПолучитьПИ( PosByOper.FIID, @FI, @Deriv )) )
           ExitFlag = true;
        end;
        
        if( (ExitFlag == false) AND ПолучитьФинИн( FI.FaceValueFI, FinInstr ) )
           ExitFlag = true;
        end;

        if ((ExitFlag == false) AND (FinInstr.rec.FI_Kind == FIKIND_AVOIRISS)
            AND (FinInstr.rec.AvoirKind != AVOIRISSKIND_BASKET)) 

           Query = "SELECT * FROM ddvdeal_dbt DEAL WHERE " + DealByPosition( PosByOper ) + " AND DEAL.T_DATE_CLR = " + GetSQLDate(oper.Date) + ";";
           DealByPos = TRsbDataSet(Query);
           if( DealByPos )
              CountDeal = 0;
              InitProgress( DealByPos.GetRecCount(), "Перебор сделок по позиции", "Выполнение..." );

              while( DealByPos.MoveNext() AND (ExitFlag == false) )
                 /* сделки БОЦБ вставляем только для исполнения "поставочного" фьючерса */ 
                 if( (DealByPos.Type == "E") AND (FI.Settlement_Code == DVSETTLEMET_STATE) )

                   if( (Deriv != null) and (Deriv.OptionType == DVTYPE_PUT) )
                     if( DealByPos.Position == 1 )  /*короткие позиции*/
                        IsBuy = true;
                     else
                        IsBuy = false;                   
                     end;
                   else
                     if( DealByPos.Position == 1 )  /*короткие позиции*/
                        IsBuy = false;
                     else
                        IsBuy = true;                   
                     end;
                   end;

                   if( DealByPos.Broker > 0 )
                      IsBroker = true;
                   else
                      IsBroker = false;
                   end;

                   DV_InitSPDeal( tick, DL_SECURITYDOC, GetDealType( IsBuy, IsBroker ), true );

                   tick.DealDate = tick.CommDate = Date(oper.Date);
                   tick.DealTime = Time(DealByPos.Time);

                   tick.MarketID = FI.Issuer;
                   /*Сектор биржи*/
                   if( not FindDLMARKETbyMarket(FI.Issuer, true, false, FIKIND_AVOIRISS, DLMarket) )
                      tick.MarketSchemeID = DLMarket.rec.ID;
                      tick.MarketOfficeID = DLMarket.rec.CentrOffice;
                      tick.TraderID       = DLMarket.rec.Centr;
                      tick.DepSetID       = DLMarket.rec.DepSetID;
                      tick.DepositID      = DV_GetDepositID(tick.DepSetID); /* в сишнике перенесется в leg */
                   else
                      tick.MarketSchemeID = 0;
                      tick.DepSetID       = 0;
                      tick.TraderID       = -1;
                      tick.MarketOfficeID = -1;
                   end;

                   if( DealByPos.Broker > 0 )
                      tick.BrokerID = DealByPos.Broker;
                      tick.PartyID  = DealByPos.Broker;
                      tick.BrokerContrID = GetContrID(PTSK_STOCKDL, DealByPos.Broker, {OurBank});
                   end;

                   tick.ClientID = DealByPos.Client;
                   tick.Flag1    = SET_CHAR;
                   tick.Department = DealByPos.Department;
                   tick.Oper     = DealByPos.Oper;
                   tick.Points   = 4;
                   tick.TypeDoc  = StrFor(SP_TYPEDOC_SYSTEM);

                   if( DealByPos.Client > 0 )
                      tick.PortfolioID = KINDPORT_CLIENT;
                      tick.ClientContrID = GetContrID(PTSK_STOCKDL, {OurBank}, DealByPos.Client);
                   else
                      if( (DV_ExistNOSS(FD_Oper.DVOper.rec.FIID, tick.DealDate)) == true )
                         tick.PortfolioID = KINDPORT_TRADE;
                      else
                         tick.PortfolioID = KINDPORT_SALE;
                      end;
                   end;

                   if ( oper.PartyKind != PTK_MARKETPLASE )
                      tick.Flag3 = SET_CHAR;
                   end;

                   SuplyTime = Time(DealByPos.Time);

                   if( Fininstr.rec.GeneralizedFIID > 0 ) 
                      DeliveringFIID = -1;
                   else
                      DeliveringFIID = Fininstr.rec.FIID;
                   end;

                   if(Fininstr.rec.IsGeneralized == SET_CHAR) /* если выпуск обобщённый */
                      FIID = Fininstr.rec.FIID;
                   elif( not ПолучитьФинИн( Fininstr.rec.GeneralizedFIID, FininstrG ) ) /* если фактический и нашли обобщённый */
                      FIID = Fininstr.rec.GeneralizedFIID;
                   else
                      FIID = Fininstr.rec.FIID;
                   end;

                   tick.OriginID = GetIdentProgram();
                   tick.ParentID = oper.ID;

                   ClearRecord(leg);

                   leg.PFI            = FIID;
                   leg.CFI            = FI.ParentFI;
                   leg.Principal      = Money(FI.FaceValue * DealByPos.Amount);
                   leg.Cost           = Money(DealByPos.Price * DealByPos.Amount);
                   leg.Price          = DealByPos.Price;
                   leg.PayFIID        = FI.ParentFI;
                   leg.DeliveringFIID = DeliveringFIID;
                   leg.SupplyTime     = SuplyTime;
                   leg.DepositID      = tick.DepositID;
                   leg.Point          = tick.Points;
                   leg.MaturityIsPrincipal = UNSET_CHAR;
                   leg.Expiry         = tick.DealDate;
                   leg.Maturity       = tick.DealDate;
                 
                   NKD_CFI = $0;
                   if( FI_IsBond(leg.PFI) )
                      leg.NKD = НКД(leg.PFI, leg.Principal, tick.DealDate);
                      leg.NKDFIID = Fininstr.rec.FaceValueFI;
                      if( not DV_SmartConvertSum( NKD_CFI, leg.NKD, tick.DealDate, leg.NKDFIID, leg.CFI, true ) )
                         return false;
                      end;
                   end;

                   leg.TotalCost      = leg.Cost + NKD_CFI;

                   if( Money(FI.FaceValue * DealByPos.Amount) != $0 )
                      tick.PreOutlayFIID = NATCUR;
                      SSCost = GetSetDVSSCost(DealByPos.ID,DEALKIND_MRK_IN,Date(oper.Date));
                      AccBuf.clear();

                      if( SSCost < 0 )
                         var FD_Pos = DVFirstDocPos( DL_DVFIPOS, PosByOper.ID );
                         if( not FD_Pos.OpenAccount("-ПФИ", null, null, FIROLE_UNDEF, AccBuf, oper.Date, NATCUR, null, null) )
                            ExitFlag = true;
                         end;
                      end;

                      if( not ExitFlag )
                         if( OprInsertSecDeal( tick,
                                               leg,
                                               SSCost,
                                               AccBuf.rec.Account,
                                               AccBuf.rec.Chapter,
                                               AccBuf.rec.Code_Currency,
                                               DL_DVOPER ) == false )
                            msgbox("Ошибка при вставке сделки БОЦБ");
                            ExitFlag = true;
                         end;
                      end;
                   end;
                                   
                 end;

                 UseProgress( CountDeal = CountDeal + 1 );
              end;
              RemProgress();
           end;
        end;
        UseProgress( Count = Count + 1 );
     end;

     RemProgress();
  end;

  if(ExitFlag == true) 
     return 1;
  end;

  return 0;
end;
