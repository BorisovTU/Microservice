/*
$Name:        dvopcur040.mac
$Module:      Производные инструменты
$Description: Расчеты по производным инструментам на валютном рынке/рынке СПФИ. Шаг "Учет гарантийного обеспечения"
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac";

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )

  record oper(dvoper);
  var Query, Data;

  SetBuff(oper, FDoc);

  var FD_Oper = DVFirstDocOper(DL_DVOPER, oper);

  if( FD_Oper.IsClient() )
     return 0;
  end;

  Query = RSDCommand( " SELECT dlvalue.* " +
                      "   FROM ddl_value_dbt dlvalue " +
                      "  WHERE dlvalue.t_DocKind = ? " +
                      "    AND dlvalue.t_DocID   = ? " +
                      "    AND dlvalue.t_Kind in(?, ?) " +
                      "    AND dlvalue.t_ID_Operation = 0 " +
                      "    AND dlvalue.t_ID_Step      = 0 " +
                      " ORDER BY dlvalue.t_Kind "
                    );
  Query.NullConversion = true;
  Query.addParam("", RSDBP_IN, FD_Oper.Kind);
  Query.addParam("", RSDBP_IN, FD_Oper.ID);
  Query.addParam("", RSDBP_IN, ALG_DV_TYPE_CALC_PAYMENT_GUARANTEE);
  Query.addParam("", RSDBP_IN, ALG_DV_TYPE_CALC_REFUND_GUARANTEE);
  Query.execute();
  Data = TRsbDataSet(Query);
  while( Data.MoveNext() )

     VAR OurAcc  = TRecHandler("account"),
         PaymentObj:RsbPayment;

     if( not FD_Oper.IsExistAccount("Гарантийный счет", null, true, null, OurAcc, null, FD_Oper.DVOper.rec.Date, SQL_ConvTypeInteger(Data.SumFIID)) )
        MsgBox("Ошибка: не открыт счет категории \"Гарантийный счет\" в валюте FIID = " + String(SQL_ConvTypeInteger(Data.SumFIID)));
        return 1;
     end;

     if( SQL_ConvTypeInteger(Data.Kind) == ALG_DV_TYPE_CALC_PAYMENT_GUARANTEE ) // оплата

        if( DVND_CreatePayment( FD_Oper, PM_PURP_GARANT, FD_Oper.Kind, FD_Oper.ID,
                                FD_Oper.DVOper.rec.Date,
                                {ourbank}, {ourbank},
                                FD_Oper.GetContractorID(), -1,
                                SQL_ConvTypeInteger(Data.Sum), SQL_ConvTypeInteger(Data.SumFIID),
                                "Оплата гарантийного обеспечения",
                                OurAcc, NULL,
                                FD_Oper.DVOper.rec.Department, NULL, NULL,
                                @PaymentObj
                              ) != 0 )
           return 1;
        end;
     else // получение
   
        if( DVND_CreatePayment( FD_Oper, PM_PURP_GARANT, FD_Oper.Kind, FD_Oper.ID, 
                                FD_Oper.DVOper.rec.Date,
                                FD_Oper.GetContractorID(), -1,
                                {ourbank}, {ourbank},
                                SQL_ConvTypeInteger(Data.Sum), SQL_ConvTypeInteger(Data.SumFIID),
                                "Получение гарантийного обеспечения",
                                NULL, OurAcc,
                                FD_Oper.DVOper.rec.Department, NULL, NULL,
                                @PaymentObj
                              ) != 0 )
           return 1;
        end;
     end;

     if( not ПИ_ПроводкаПоПлатежу(FD_Oper, PaymentObj) )
        return 1;
     end;

     if( ПИ_ИсполнитьПлатеж(PaymentObj) )
        return 1;
     end;
  end;

  if( not InsertOprStatus(DV_OPCUR_OPERSTATUS_KIND_GUARANTEE, DV_OPCUR_OPERSTATUS_COMPLETE) )
     MsgBox("Ошибка при установке статуса <Гарантийное обеспечение> по операции.");
     return 1;
  end;

  if( FD_Oper.ЕстьСуммыПереводаНаКлиринговыйСчет() )
     if( not InsertOprStatus(DV_OPCUR_OPERSTATUS_KIND_CLEARING, DV_OPCUR_OPERSTATUS_RUN) )
        MsgBox("Ошибка при установке статуса <Перевод средств на клиринговый счёт> по операции.");
        return 1;
     end;
  else
     if( not InsertOprStatus(DV_OPCUR_OPERSTATUS_KIND_CLEARING, DV_OPCUR_OPERSTATUS_COMPLETE) )
        MsgBox("Ошибка при установке статуса <Перевод средств на клиринговый счёт> по операции.");
        return 1;
     end;

     if( not InsertOprStatus(DV_OPCUR_OPERSTATUS_KIND_SUM_CARRY, DV_OPCUR_OPERSTATUS_RUN) )
        MsgBox("Ошибка при установке статуса <Сводные проводки> по операции.");
        return 1;
     end;
  end;

  return 0;
end;
