/*
$Name:        dvcuritog060.mac
$Module:      ФИССИКО
$Description: Операция "Обработка итогов биржевых валютных торгов", Шаг Оплата комиссий бирже
*/
import InsCarryDoc, "dv_categ.mac", "dv_car.mac", "dv_carop.mac";

MACRO ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step )

  record Comm( dl_comm );
  var FD; 

  SetBuff( Comm, FirstDoc );
  FD = DVFirstDocDLCOMM( Comm );
  var ДатаОперации = Comm.CommDate;

  var СчетДебет  = TRecHandler( "account" ),
      СчетКредит = TRecHandler( "account" ),
      PaymentObj:RsbPayment;
  var IsBank:bool = false;
  var Ground;

  var Sql = " SELECT dlcomis.t_ID,  dlcomis.t_PlanPayDate, dlcomis.t_Sum, dlcomis.t_NDS, dlcomis.t_Ground, comis.t_ReceiverID, comis.t_FIID_Comm, comis.t_number, comis.t_code, " +
            IIF(FD.comm.rec.PartyID == GetCodeParty("АО СПВБ",1),
            "        NVL((SELECT distinct  ExtCode.t_MarketSchemeID " +
            "             FROM DDL_EXTSETTLECODE_DBT ExtCode " + 
            "              WHERE ExtCode.t_marketid = " + GetCodeParty("АО СПВБ",1) + " and  ExtCode.t_marketkind = ? and dlcomis.t_SettleCode = 'СПВБ' " +
            "           ), 0) MarketSchemeID  ",
            "        NVL((SELECT NVL(Analytics.t_MarketSchemeID, ExtCode.t_MarketSchemeID) " +
            "               FROM DDL_EXTSETTLECODE_DBT ExtCode LEFT JOIN DDL_EXTSCANALYTICS_DBT Analytics " +
            "                 ON     ExtCode.t_InPool = CHR(88) " +
            "                    AND Analytics.t_CodeID = case when ExtCode.t_ParentID = 0 then ExtCode.t_ID else ExtCode.t_ParentID end " +
            "                    AND Analytics.t_MarketKind = ? " +
            "              WHERE ExtCode.t_SettleCode = dlcomis.t_SettleCode " +
            "            ), 0) MarketSchemeID ") +
            "   FROM ddlcomis_dbt dlcomis, dsfcomiss_dbt comis  " +
            "  WHERE dlcomis.t_DocKind     = ? " +
            "    AND dlcomis.t_DocID       = ? " +
            "    AND dlcomis.t_FactPayDate = TO_DATE('01.01.0001', 'dd.mm.yyyy') " +
            "    AND dlcomis.t_PlanPayDate = ? " +
            "    AND dlcomis.t_FeeType     = comis.t_FeeType " +
            "    AND dlcomis.t_ComNumber   = comis.t_Number " +
            " order by dlcomis.t_PlanPayDate ";

  var Query = RSDCommand( Sql );
  Query.NullConversion = true;
  Query.addParam( "", RSDBP_IN, DL_MARKETKIND_SETTLE_CURRENCY );     
  Query.addParam( "", RSDBP_IN, FD.Kind );
  Query.addParam( "", RSDBP_IN, FD.ID );
  Query.addParam( "", RSDBP_IN, ДатаОперации );
  Query.execute();

  var Data = TRsbDataSet(Query);
  while( Data.MoveNext() )
     IsBank = ((FD.Comm.rec.OperSubKind == ALG_SP_MONEY_SOURCE_CLIENT) or (ВидСубъекта( Data.rec.ReceiverID, PTK_BANK ))); //принадлежность "Банки"
     FD.SetDVMarketSchemeByTotalOfFI( SQL_ConvTypeInteger(Data.rec.MarketSchemeID) );

     if( not FD.IsExistAccount(IIF (Comm.OPERSUBKIND == 1, "+Обеспечение", "Клиринговый счет"), null, true, DV_GetSettlFiRoleByOperSubKind(FD.Comm.rec.OperSubKind,false), СчетКредит, null, ДатаОперации, Data.rec.FIID_Comm) )/*PNV*/
        MsgBox( "Ошибка: не открыт счет категории \"Клиринговый счет\" в валюте FIID = " + String(Data.rec.FIID_Comm) );
        return 1;
     end;

     FD.sETFI_KIND(FIKIND_CURRENCY);
     if (FD.Comm.rec.OperSubKind == DL_EXTCODETYPE_CLIENT)
         if( not FD.OpenAccount("-КВ, КОV", null, null, FIROLE_UNDEF, СчетДебет, ДатаОперации, NATCUR) )
             return 1;
         end;         
         if( not FD.OpenAccount("ОбязатПрочие", null, null, FIROLE_UNDEF, СчетКредит, ДатаОперации, NATCUR) )
             return 1;
         end;         
         if( not ПроводкаПоКатегориямУчета( FD,
                                        СчетДебет, СчетКредит,               
                                        ДатаОперации, 1,          
                                        Data.rec.FIID_Comm, money(Data.rec.Sum-Data.rec.NDS),          
                                        Doc,                      
                                        INPCARRY, "", "",                
                                        Data.rec.Ground,
                                        NULL,
                                        FIROLE_UNDEF, FIROLE_UNDEF,
                                        NATCUR, Data.rec.FIID_Comm
                                      )
             )
             return 1;
         end;
         if( not FD.IsExistAccount( "Клиринговый счет", null, true, DV_GetSettlFiRoleByOperSubKind(FD.Comm.rec.OperSubKind,false), СчетКредит, null, ДатаОперации, Data.rec.FIID_Comm) )/*PNV*/
             MsgBox( "Ошибка: не открыт счет категории \"Клиринговый счет\" в валюте FIID = " + String(Data.rec.FIID_Comm) );
             return 1;
         end;
         if( not FD.OpenAccount("ОбязатПрочие", null, null, FIROLE_UNDEF, СчетДебет, ДатаОперации, NATCUR) )
             return 1;
         end;         

         if( not ПроводкаПоКатегориямУчета( FD,
                                        СчетДебет, СчетКредит,               
                                        ДатаОперации, 1,          
                                        Data.rec.FIID_Comm, money(Data.rec.Sum-Data.rec.NDS),          
                                        Doc,                      
                                        INPCARRY, "", "",                
                                        Data.rec.Ground,
                                        NULL,
                                        FIROLE_UNDEF, FIROLE_UNDEF,
                                        NATCUR, Data.rec.FIID_Comm
                                      )
             )
             return 1;
         end;

     else
         if( not FD.OpenAccount(IIF(( Data.rec.number == 81) or (Data.rec.code == "СПВББиржКлирВ"), "-КВ, КОV", "-КВ, КОV5"), null, null, FIROLE_UNDEF, СчетДебет, ДатаОперации, NATCUR) )
             return 1;
         end;
         
         if(GetCodeParty("АО СПВБ",PTCK_CONTR) == FD.comm.rec.PartyID)
           Ground = "Валютный рынок. Комиссионное вознаграждение за ";
           if (Data.rec.code == "СПВББиржВ")
             Ground = Ground + "организацию торгов ";
           else
             Ground = Ground + "клиринговое обслуживание ";
           end;
           Ground = Ground + "за " + string(SQL_ConvTypeDate(Data.rec.PlanPayDate):f);
         else
           Ground = Data.rec.Ground;
         end;
 
         if( not ПроводкаПоКатегориямУчета( FD,
                                        СчетДебет, СчетКредит,               
                                        ДатаОперации, 1,          
                                        Data.rec.FIID_Comm, money(Data.rec.Sum-Data.rec.NDS),          
                                        Doc,                      
                                        INPCARRY, "", "",                
                                        Ground,
                                        NULL,
                                        FIROLE_UNDEF, FIROLE_UNDEF,
                                        NATCUR, Data.rec.FIID_Comm
                                      )
             )
             return 1;
         end;
     end;

     if( Data.rec.NDS > 0 )
        if( not FD.OpenAccount("+НДС", null, null, FIROLE_UNDEF, СчетДебет, ДатаОперации, NATCUR) )
           return 1;
        end;

        if( not ПроводкаПоКатегориямУчета( FD,
                                           СчетДебет,                 /* Деб */
                                           СчетКредит,                /* Кредит */
                                           ДатаОперации,              /* Дата */
                                           1,                         /* Глава */
                                           Data.rec.FIID_Comm,        /* Валюта */
                                           Data.rec.NDS,              /* Сумма */
                                           Doc,                       /* Документ */
                                           INPCARRY,                  /* Result_Carry */
                                           "",                        /* Kind_Oper */
                                           "",                        /* Numb_Document */
                                           "Удержание НДС"            /* Ground */
                                         )
          )
           return 1;
        end;
     end; 

     if( DL_SetComIsPaid(Data.rec.ID, ДатаОперации) != 0 )
        MsgBox( "Ошибка при установке признака оплаты комиссии" );
        return 1;
     end;
    
  end;

  if( not InsertOprStatus(463302, 2) )
     MsgBox("Ошибка при установке статуса <Оплата комиссий> по операции.");
     return 1;
  end;
 
  return 0;
end;
