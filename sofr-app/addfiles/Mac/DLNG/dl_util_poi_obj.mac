/*
$Name:        dl_util_poi_obj.mac
$Module:      Ядро Securities
$Description: Утилиты для формирования отчетов посредством POI, но аналогично dl_util_xml_obj.mac
*/
import "dl_util_xml_obj.mac", "dlquery.mac", "CbRepFun.mac", "dlmisc.mac", "jvmutils.mac", "dlutils.mac";

PRIVATE CONST TWIP_TO_MM = 0.0176388889;
PRIVATE CONST INCH_TO_MM = 25.4;
PRIVATE CONST A4_LANDSCAPE_WIDTH_MM = 297.0;
PRIVATE CONST A4_LANDSCAPE_HEIGHT_MM = 210.0;


PRIVATE CLASS RegexPattern( _outputType, _pattern, _format )
   var OutputType = _outputType,
       Pattern = _pattern,
       Format = _format;
END;

PRIVATE CLASS DateRegexParser( _jvm )
   private var jvm = _jvm;
   private var Patterns;
   var DateSeparators = ".\\-,/";
   var TimeSeparators = ":./";
   var DateTimeSeparators = "\\s";

   var RealFormat = "";

   private macro D( name, cnt )
      return "\\s*(?<"+name+">\\d{"+cnt+"})";
   end;

   private macro _D( name, cnt )
      return "\\s*(?<"+name+">\\d{1,"+cnt+"})";
   end;

   private macro DS( name )
      return "(?<"+name+">["+DateSeparators+"])";
   end;

   private macro TS( name )
      return "(?<"+name+">["+TimeSeparators+"])";
   end;

   private macro DTS( )
      return "(?<dts>["+DateTimeSeparators+"]+)";
   end;

   private macro opt( ptr )
      return "(?:"+ptr+")?";
   end;

   macro Init()
      Patterns = TArray();
      this.AddPattern( V_DTTM,  String("^\\s*", _D("DD",2), DS("ds1"), _D("MM",2), opt(DS("ds2")+D("YY",4)), DTS(), "\\(?", _D("hh",2), TS("ts1"), _D("mm",2), opt(TS("ts2")+_D("ss",2)), opt(TS("ts3") + _D("ms",5) ), "\\)?\\s*$"), "dd\\.mm\\.yyyy hh:mm:ss" );
      this.AddPattern( V_DTTM,  String("^\\s*", _D("DD",2), DS("ds1"), _D("MM",2),  DS("ds2"), D("YY",2), DTS(), _D("hh",2), TS("ts1"), _D("mm",2), opt(TS("ts2")+_D("ss",2)), "\\s*$"),    "dd\\.mm\\.yy hh:mm:ss" );
      this.AddPattern( V_DTTM,  String("^\\s*", D("YY",4), DS("ds1"), _D("MM",2), DS("ds2"), _D("DD",2), DTS(), _D("hh",2), TS("ts1"), _D("mm",2), opt(TS("ts2")+_D("ss",2)), "\\s*$"),     "yyyy\\.mm\\.dd hh:mm:ss" );
      this.AddPattern( V_DATE,  String("^\\s*", _D("DD",2), DS("ds1"), _D("MM", 2), opt(DS("ds2")+D("YY",4)), "\\s*$"),                                                                     "dd\\.mm\\.yyyy" );
      this.AddPattern( V_DATE,  String("^\\s*", _D("DD",2), DS("ds1"), _D("MM", 2), opt(DS("ds2")+D("YY",2)), "\\s*$"),                                                                     "dd\\.mm\\.yy" );
      this.AddPattern( V_DATE,  String("^\\s*", D("YY",4), DS("ds1"), _D("MM", 2), DS("ds2"), _D("DD",2), "\\s*$"),                                                                         "yyyy\\.mm\\.dd" );
      this.AddPattern( V_DATE,  String("^\\s*", _D("YY",4), "\\s*$"),                                                                                                                       "yyyy" );
      this.AddPattern( V_TIME,  String("^\\s*", _D("hh",2), TS("ts1"), _D("mm",2), opt(TS("ts2")+_D("ss",2)), "\\s*$"),                                                                     "hh:mm:ss" );
   end;

   macro Destructor()
      for( var i, 0, Patterns.Size - 1 )
         Patterns[i].Pattern = null;
         Patterns[i] = null;
      end;;
      Patterns = null;
   end;

   macro AddPattern( outputType, pattern:string, format )
      this.InsertPattern( Patterns.Size, outputType, pattern:string, format );
   end;

   macro PatternCount()
      return Patterns.Size;
   end;

   macro InsertPattern(id:integer, outputType, pattern:string, format )
      var i = Patterns.Size;
      if( ( id < 0 ) or (id > Patterns.Size ) )
         RunError("Индекс вне границ массива");
      end;

      while( i > id )
         Patterns[i] = Patterns[i-1];
         i = i - 1;
      end;

      if( ValType( outputType ) == V_UNDEF )
         outputType = V_DTTM;
      end;

      if( ValType( format ) == V_UNDEF )
         if( outputType == V_DTTM )
            format = "dd\\.mm\\.yyyy hh:mm:ss";
         elif( outputType == V_DATE )
            format = "dd\\.mm\\.yyyy";
         elif( outputType == V_TIME )
            format = "hh:mm:ss";
         end;
      end;

      Patterns[id] = RegexPattern( outputType, jvm.callStaticMethod("java.util.regex.Pattern", "compile", pattern), format );
   onError(err)
      RunError("Некорректное регулярное выражение \"" + pattern + "\"|" + GetFullErrorMessage(err,4000) );
   end;

   macro Parse( str:string, type, throw_err )
      var matcher, pattern, found = false, val;
      var day, month, year, hour, minute, second, ds1, ds2, ts1, ts2, _dts;

      macro TryGetGroup( groupName )
         if( Index( pattern.Pattern.pattern(), "<"+groupName+">" ) > 0 )
            return matcher.{group@(Ljava/lang/String;)Ljava/lang/String;}( groupName );
         else
            return null;
         end;
      end;

      macro MakeRealFormat( )
         var format = pattern.Format;
         var f_ptrn = jvm.callStaticMethod("java.util.regex.Pattern", "compile", "(\\w+)|(\\W+)");
         var f_matcher = f_ptrn.matcher( format );
         var is_date = true, item, i_s = 1, val;

         RealFormat = "";

         while( f_matcher.find() )
            item = f_matcher.group();
            val = null;

            if( ( StrUpr( item ) == "DD" )  or ( is_date and ( StrUpr( item ) == "MM" ) ) or ( StrUpr( item ) == "YY" ) or ( StrUpr( item ) == "YYYY" ) )
               if( not is_date )
                  i_s = 1;
                  is_date = true;
               end;

               if( StrUpr( item ) == "YYYY" )
                  val = TryGetGroup( "YY" );
               else
                  val = TryGetGroup( StrUpr( item ) );
               end;
            elif( ( StrLwr( item ) == "hh" ) or ( not is_date and ( StrLwr( item ) == "mm" ) ) or ( StrLwr( item ) == "ss" ) )
               if( is_date )
                  i_s = 1;
                  is_date = false;
               end;

               val = TryGetGroup( StrLwr( item ) );
            elif( ( item == "." ) or ( item == "\\." ) or ( item == ":" ) )
               if( is_date )
                  if( i_s == 1 )
                     if( ds1 == "." )
                        RealFormat = RealFormat + "\\.";
                     else
                        RealFormat = RealFormat + NVL( ds1, "" );
                     end;
                  else
                     if( ds2 == "." )
                        RealFormat = RealFormat + "\\.";
                     else
                        RealFormat = RealFormat + NVL( ds2, "" );
                     end;
                  end;
               else
                  if( i_s == 1 )
                     RealFormat = RealFormat + NVL( ts1, "" );
                  else
                     RealFormat = RealFormat + NVL( ts2, "" );
                  end;
               end;
               i_s = i_s + 1;
            elif( ( item == " " ) )
               RealFormat = RealFormat + _dts;
            end;

            if( val )
               RealFormat = RealFormat + item;
            end;
         end;
      end;

      for( pattern, Patterns )
         if( ( ValType( type ) == V_INTEGER ) and ( pattern.OutputType != type ) )
            continue;
         end;

         matcher = pattern.Pattern.matcher( str );
         if( matcher.matches() )
            found = true;
            break;
         end;
      end;

      if( not found )
         if( throw_err )
            RunError();
         else
            return str;
         end;
      else
         day =    TryGetGroup("DD");
         month =  TryGetGroup("MM");
         year =   TryGetGroup("YY");
         hour =   TryGetGroup("hh");
         minute = TryGetGroup("mm");
         second = TryGetGroup("ss");
         ds1 =    TryGetGroup("ds1");
         ds2 =    TryGetGroup("ds2");
         ts1 =    TryGetGroup("ts1");
         ts2 =    TryGetGroup("ts2");
         _dts =   TryGetGroup("dts");

         if( ValType( year ) == V_UNDEF )
            DateSplit( Date(), null, null, year );
         elif( StrLen(year) == 2 )
            year = 2000 + Int(Trim(year));
         else
            year = Int(Trim(year));
         end;

         if( ValType( day ) == V_UNDEF )
            day = 1;
         else
            day = Int(Trim(day));
         end;

         if( ValType( month ) == V_UNDEF )
            month = 1;
         else
            month = Int(Trim(month));
         end;

         if( pattern.OutputType == V_DTTM )
            val = DtTm( Date(day, month, year), Time(Int(Trim(hour)),Int(Trim(minute)),Int(Trim(second))) );
         elif( pattern.OutputType == V_DATE )
            val = Date(day, month, year);
         elif( pattern.OutputType == V_TIME )
            val = Time(Int(Trim(hour)),Int(Trim(minute)),Int(Trim(second)));
         end;

         MakeRealFormat();
      end;

      return val;
   onError(err)
      if( throw_err )
         RunError( "Не удалось преобразовать строковое значение в дату|" + GetFullErrorMessage(err,4000) );
      else
         return str;
      end;
   end;

   if( ValType( jvm ) == V_UNDEF )
      jvm = CreateObject ("rsjvm", "TJavaHost", "GlobalJavaHost");
   end;

   Init();
END;

class NamedAddr(SI, Nm, pcolId, prowId)
   var SheetIndex = SI;
   var Name = Nm;
   var ColId = pcolId;
   var RowId = prowId;
end;


CLASS (T_Xml_Rep) T_Poi_Rep()
   var jvm = null;
   var poiRepCreator = null; //общий класс по созданию отчётов в библиотеке PoiReports.jar
   var poiBook = null; //класс книги в рамках PoiReports.jar
   var poiWorkBook = null; //текущая книга из ApachePOI
   var poiCurrentSheet = null; //текущий лист из ApachePOI
   var poiCurrentRow = null; //текущая строка из ApachePOI
   var poiCurrentCell = null; //текущая ячейка из ApachePOI
   var poiStyleMap = null;
   var newRowStyleMap = null;
   var sheetIdx = -1;
   var nameAddrArr = TArray();
   var DateParser: DateRegexParser;
   var useXLSFormat = false;
   var mergedRegions = null;

   InitT_Xml_Rep();

   MACRO Set_Filter
      RunError("Not implemented yet");
   END;  // Set_Filter

   MACRO Set_Name(p_Encode)
   END;
 
   //───────────────────────────────────────────────────────────────────────────
   // Макрос инициализации.
   //───────────────────────────────────────────────────────────────────────────
   MACRO Init(p_Encode, p_UseApostr, p_PrintScale, p_UseXLSFormat)
      jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
      poiRepCreator = jvm.callStaticMethod("ru.softlab.rsbank.poireports.ReportCreator", "getInstance");

      if((ValType(p_UseXLSFormat) == V_BOOL) and p_UseXLSFormat)
         poiBook = poiRepCreator.addWorkbookXLSNoDefSheet();
         useXLSFormat = true;
      else
         poiBook = poiRepCreator.addWorkbookXLSXNoDefSheet();
         useXLSFormat = false;
      end;

      poiWorkBook = poiBook.getCurrentWorkbook();
      poiStyleMap = jvm.createJavaObject("java.util.HashMap", "<init>");
      newRowStyleMap = jvm.createJavaObject("java.util.HashMap", "<init>");
      DateParser = DateRegexParser(jvm);
      Init(p_Encode, p_UseApostr, p_PrintScale);
   END;  // Init;

   MACRO Add_PageBreak(Row_Num)
      RunError("Not implemented yet");
   END;

   //───────────────────────────────────────────────────────────────────────────
   MACRO Add_Style()
      private var p_Name, p_DataType, p_Brl, p_Brr, p_Brt, p_Brb, p_Ha, p_Va, p_FnName, p_FnSize, p_FnBld, p_FnItl, p_FnUnd, p_Point, p_Color, p_Indent, p_InteriorColor, p_WrapText, p_SignPrefix, p_Format;
      GetParm(1,  p_DataType); _Nvl(p_DataType, "String"); 
      GetParm(2,  p_Brl);      _Nvl(p_Brl,      FALSE);
      GetParm(3,  p_Brr);      _Nvl(p_Brr,      FALSE);
      GetParm(4,  p_Brt);      _Nvl(p_Brt,      FALSE);
      GetParm(5,  p_Brb);      _Nvl(p_Brb,      FALSE);
      GetParm(6,  p_Ha);       
      GetParm(7,  p_Va);       _Nvl(p_Va,       "Top");
      GetParm(8,  p_FnName);   _Nvl(p_FnName,   Rep_Font_Name);
      GetParm(9,  p_FnSize);   _Nvl(p_FnSize,   Rep_Font_Size);
      GetParm(10, p_FnBld);    _Nvl(p_FnBld,    FALSE);
      GetParm(11, p_FnItl);    _Nvl(p_FnItl,    FALSE);
      GetParm(12, p_FnUnd);    _Nvl(p_FnUnd,    FALSE);
      if (p_DataType == "String")
         _Nvl(p_Ha, "Left");
      elif ((p_DataType == "Procent")
      or    (p_DataType == "Money")
      or    (Substr(p_DataType, 1, 6) == "Number")
           )
         _Nvl(p_Ha, "Right");
      else 
         _Nvl(p_Ha, "Center");
      end;
      GetParm(13, p_Point);
      if ((p_DataType == "Money")
      or  (p_DataType == "Procent")
         ) 
         _Nvl(p_Point, 2);
      else
         _Nvl(p_Point, 0);
      end;
      GetParm(14, p_Color);  _Nvl(p_Color, "");
      GetParm(15, p_Indent); _Nvl(p_Indent, Indent_Value);
      GetParm(16, p_InteriorColor); _Nvl(p_InteriorColor, "");
      GetParm(17, p_WrapText); _Nvl(p_WrapText, WrapText_Value);
      GetParm(18, p_SignPrefix); _Nvl(p_SignPrefix, "");
      GetParm(19, p_Format);
      
      if   (Substr(p_Va, 1, 1) == "T") p_Va = "Top"; 
      elif (Substr(p_Va, 1, 1) == "C") p_Va = "Center"; 
      elif (Substr(p_Va, 1, 1) == "B") p_Va = "Bottom"; 
      elif (Substr(p_Va, 1, 1) == "J") p_Va = "Justify"; 
      end;
      if   (Substr(p_Ha, 1, 1) == "L") p_Ha = "Left"; 
      elif (Substr(p_Ha, 1, 1) == "C") p_Ha = "Center"; 
      elif (Substr(p_Ha, 1, 1) == "R") p_Ha = "Right"; 
      elif (Substr(p_Ha, 1, 1) == "J") p_Ha = "Justify"; 
      end;

      var styleInd = 
         string(p_DataType) + "::" +
         string(p_Brl) + "::" +
         string(p_Brr) + "::" +
         string(p_Brt) + "::" +
         string(p_Brb) + "::" +
         string(p_Ha) + "::" +
         string(p_Va) + "::" +
         string(p_FnName) + "::" +
         string(p_FnSize) + "::" +
         string(p_FnBld) + "::" +
         string(p_FnItl) + "::" +
         string(p_FnUnd) + "::" +
         string(p_Point) + "::" +
         string(p_Color) + "::" +
         string(p_Indent) + "::" +
         string(p_InteriorColor) + "::" +
         string(p_WrapText) + "::" +
         string(p_SignPrefix);
      var resStyle = poiStyleMap.get(styleInd);
      if (ValType(resStyle) != V_UNDEF)
         return resStyle;
      else
         var cellStyle = poiWorkBook.{createCellStyle@()Lorg/apache/poi/ss/usermodel/CellStyle;}();

         if (p_Brl or p_Brr or p_Brt or p_Brb)

            var borderStyle = GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.BorderStyle", "THIN");
            var borderColor = GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.IndexedColors", "BLACK");

            if (p_Brr)
               cellStyle.setBorderRight(borderStyle);
               cellStyle.setRightBorderColor(borderColor.getIndex());
            end;
            if (p_Brl)
               cellStyle.setBorderLeft(borderStyle);
               cellStyle.setLeftBorderColor(borderColor.getIndex());
            end;
            if (p_Brb)
               cellStyle.setBorderBottom(borderStyle);
               cellStyle.setBottomBorderColor(borderColor.getIndex());
            end;
            if (p_Brt)
               cellStyle.setBorderTop(borderStyle);
               cellStyle.setTopBorderColor(borderColor.getIndex());
            end;
         end;

         var cellFont = poiWorkBook.{createFont@()Lorg/apache/poi/ss/usermodel/Font;}(); //xssf->ss
         var enumName = "";
         if   (p_Color == "R")
            enumName = "RED";
         elif (p_Color == "G")
            enumName = "GREEN";
         elif (p_Color == "B")
            enumName = "BLUE";
         elif (p_Color == "W")
            enumName = "WHITE";
         else
            enumName = "BLACK";
         end;
         cellFont.setColor(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.IndexedColors", enumName).getIndex());
         if (p_FnUnd)
            if (not useXLSFormat)
               cellFont.{setUnderline@(Lorg/apache/poi/ss/usermodel/FontUnderline;)V}(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.FontUnderline", "SINGLE"));
            end;
         end;
         if (p_FnItl)
            cellFont.setItalic(true);
         end;
         if (p_FnBld)
            cellFont.setBold(true);
         end;
         if (not useXLSFormat)
            cellFont.setCharSet(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.FontCharset", "RUSSIAN"));
            cellFont.setFamily(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.FontFamily", "ROMAN"));
         end;
         cellFont.setFontHeightInPoints(p_FnSize);
         cellFont.setFontName(p_FnName);

         cellStyle.{setFont@(Lorg/apache/poi/ss/usermodel/Font;)V}(cellFont);

         enumName = "";
         if (p_Ha == "Left")
            enumName = "LEFT";
         elif (p_Ha == "Center")
            enumName = "CENTER";
         elif (p_Ha == "Right")
            enumName = "RIGHT";
         elif (p_Ha == "Justify")
            enumName = "JUSTIFY";
         end;
         if (enumName != "")
            cellStyle.setAlignment(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.HorizontalAlignment", enumName));
         end;

         enumName = "";
         if (p_Va == "Top")
            enumName = "TOP";
         elif (p_Va == "Center")
            enumName = "CENTER";
         elif (p_Va == "Bottom")
            enumName = "BOTTOM";
         elif (p_Va == "Justify")
            enumName = "JUSTIFY";
         end;
         if (enumName != "")
            cellStyle.setVerticalAlignment(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.VerticalAlignment", enumName));
         end;

         cellStyle.setIndention(p_Indent);

         cellStyle.setWrapText(p_WrapText == 1);

         if (p_InteriorColor != "")
            var newColor;
            if (useXLSFormat)
               newColor = jvm.callStaticMethod("java.awt.Color", "decode", "#"+SubStr(p_InteriorColor,StrLen(p_InteriorColor)-6+1)); // alpha-канал не используется
               newColor = poiWorkBook.getCustomPalette().findSimilarColor(newColor.getRed(), newColor.getGreen(), newColor.getBlue());
               cellStyle.{setFillForegroundColor@(S)V}(newColor.getIndex());
            else
               newColor = jvm.createJavaObject("org.apache.poi.xssf.usermodel.XSSFColor",
                                             "<init>@(Lorg/apache/poi/xssf/usermodel/IndexedColorMap;)V",
                                             poiWorkBook.getStylesSource().getIndexedColors());
               newColor.setARGBHex(p_InteriorColor);
               cellStyle.{setFillForegroundColor@(Lorg/apache/poi/xssf/usermodel/XSSFColor;)V}(newColor);
            end;
            cellStyle.setFillPattern(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.FillPatternType", "SOLID_FOREGROUND"));
         end;

         var formatString = "";
         if( ( ValType( p_Format ) == V_STRING ) and ( StrLen( p_Format ) > 0 ) )
            formatString = IIF( p_Point == 1, "dd\\.mm\\.yyyy", p_Format );
         else
            if   (p_DataType == "String")
               formatString = "@";
            elif   (p_DataType == "Account")
               formatString = "##### ### # #### #######";
            elif   (p_DataType == "Time")
               formatString = "hh:mm:ss";
            elif   (p_DataType == "Date")
               formatString = "dd\\.mm\\.yyyy";
            elif (p_DataType == "Money")
               formatString = p_SignPrefix +"#"+IIF(m_UseApostr, ",", "")+"##0" +
                              IIF(p_Point > 0, ".", "") + MkStr("0", p_Point) +
                              ";\\-#"+IIF(m_UseApostr, ",", "")+"##0" +
                              IIF(p_Point > 0, ".", "") + MkStr("0", p_Point);
            elif (p_DataType == "DateTime")
               formatString = IIF(p_Point == 1, "dd\\.mm\\.yyyy", "dd\\.mm\\.yyyy hh:mm:ss");
            elif (p_DataType == "Procent")
               formatString = "Percent";
            elif (p_DataType == "Number")
               formatString = "0" + IIF(p_Point > 0, ".", "") + MkStr("0", p_Point);
            end;
         end;
         if (formatString != "")
            var poiFormat = poiWorkBook
                               .{getCreationHelper@()Lorg/apache/poi/ss/usermodel/CreationHelper;}() //xssf->ss
                               .{createDataFormat@()Lorg/apache/poi/ss/usermodel/DataFormat;}() //xssf->ss
                               .getFormat(formatString);
            cellStyle.setDataFormat(poiFormat);
         end;

         poiStyleMap.put(styleInd, cellStyle);
         return cellStyle;
      end;

   END;

   private macro calculateColWidthForPoi(width)
      const coef = Numeric(0.1548);
      if (width > 254)
         return 65280;
      end;
      if (width > 1)
         var floor = int(double(Numeric(width)/Numeric(5)));
         var factor = 30 * floor;
         var value = 450 + factor + int((numeric(width) - numeric(1)) * numeric(250) * coef);
         return value;
      else
         return 450;
      end;
   end;
   //──────────────────────────────────────────────────────────────────────────┐
   //                               MACRO                                      │
   // Печать заголовка XML файла и стилей.                                     │
   // 0 - наименование отчета (листа)                                          │
   // 1 - наименование шрифта                                                  │
   // 2 - размер шрифта                                                        │
   // 3 - FALSE - новый файл, TRUE - новый лист                                │
   // 4 - диапазон для фильтра, если пусто, то без фильтра. Пример "R8C1:R8C12"│
   //──────────────────────────────────────────────────────────────────────────┘
   MACRO Print_Header()
      private var i=0, v_Name, v_New_Sheet=NULL, l_Filter_Range;
      GetParm(1, v_Name);         _Nvl(v_Name,      "Лист 666");
      GetParm(2, Rep_Font_Name);  _Nvl(Rep_Font_Name, "Calibri");
      GetParm(3, Rep_Font_Size);  _Nvl(Rep_Font_Size, 11);
      GetParm(4, v_New_Sheet);    _Nvl(v_New_Sheet, FALSE);
      GetParm(5, l_Filter_Range); _Nvl(l_Filter_Range, "");
      u_Rep_Sheet_Name = v_Name;

      poiCurrentSheet = poiWorkBook.{createSheet@(Ljava/lang/String;)Lorg/apache/poi/ss/usermodel/Sheet;}(v_Name); //xssf->ss
      sheetIdx = sheetIdx + 1;
      var printSetup = poiCurrentSheet.{getPrintSetup@()Lorg/apache/poi/ss/usermodel/PrintSetup;}();
      printSetup.setLandscape(l_Orientation);
      While (i < col_with.Size)
         poiCurrentSheet.setColumnWidth(i, calculateColWidthForPoi(col_with(i)));
         i = i + 1;
      End;
      printSetup.setFitHeight(9999);
      printSetup.setScale(l_PrintScale);
      poiCurrentSheet.setFitToPage(true);

      if(useXLSFormat)
         printSetup.setFitWidth(1);
         poiCurrentSheet.setAutobreaks(true);
         var cls = jvm.callStaticMethod("ru.softlab.tools.RsJvm","findJavaClass2@(Ljava/lang/String;)Ljava/lang/Class;", "org.apache.poi.hssf.usermodel.HSSFPrintSetup");
         printSetup.{setPaperSize@(S)V}(cls.getField("A4_PAPERSIZE").get(cls));
      else
         printSetup.{setPaperSize@(Lorg/apache/poi/ss/usermodel/PaperSize;)V}(GetJavaEnumConstByName(jvm,"org.apache.poi.ss.usermodel.PaperSize","A4_PAPER"));
      end;
      u_Current_Row = 0;
   END;  // Print_Header() 
   //───────────────────────────────────────────────────────────────────────────
   MACRO New_Line()
      var styleUniqId = "new_row:::" + string(Rep_Font_Name) + ":::" +string(Rep_Font_Size);
      private var row_height;
      GetParm(1, row_height);
      poiCurrentRow = poiCurrentSheet.{createRow@(I)Lorg/apache/poi/ss/usermodel/Row;}(u_Current_Row); //xssf->ss
      if (ValType(row_height) != V_UNDEF)
         poiCurrentRow.setHeightInPoints(row_height);
      else
         poiCurrentRow.setHeight(-1);
      end;
      var currentStyle = newRowStyleMap.get(styleUniqId);

      if (ValType(currentStyle) == V_UNDEF)
         currentStyle = poiWorkBook.{createCellStyle@()Lorg/apache/poi/ss/usermodel/CellStyle;}();
         var rowFont = poiWorkBook.{createFont@()Lorg/apache/poi/ss/usermodel/Font;}(); //xssf->ss

         if(not useXLSFormat)
            rowFont.setCharSet(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.FontCharset", "RUSSIAN"));
            rowFont.setFamily(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.FontFamily", "ROMAN"));
         end;

         rowFont.setFontHeightInPoints(Rep_Font_Size);
         rowFont.setFontName(Rep_Font_Name);
         currentStyle.{setFont@(Lorg/apache/poi/ss/usermodel/Font;)V}(rowFont);
         newRowStyleMap.put(styleUniqId, currentStyle);
      end;

      poiCurrentRow.{setRowStyle@(Lorg/apache/poi/ss/usermodel/CellStyle;)V}(currentStyle);

      Row_Count = Row_Count + 1;
      u_Current_Column = 0;
   END;  // New_Line()  
   //───────────────────────────────────────────────────────────────────────────
   MACRO End_Line()
      u_Current_Row = u_Current_Row + 1;
   END;  // End_Line()  
   //──────────────────────────────────────────────────────────────────────────┐
   //
   // 1 - значение выводимой переменной                                        │  
   // 2 - строка параметров                                                    │  
   // 3 - если начало строки, то можно указать ее высоту                       │  
   // 4 - разделитель (по-умолчанию ";")                                       │  
   //                                                                          │ 
   // Вывод переменной любого типа с параметрами, описанными в одной строке.   │
   // Ключевые слова:                                                          │ 
   // START - переменная выводится с новой строки                              │ 
   // END   - после вывода переменной вставляется тэг окончания строки         │
   // STEN  - тэг начала и окончания строки, значение выводится в одну строку  │
   // BOR   - рисуется рамка                                                   │
   // BRL   - рисуется граница слева                                           │
   // BRT   - рисуется граница сверху                                          │
   // BRR   - рисуется граница справа                                          │
   // BRB   - рисуется граница внизу                                           │
   // I=X   - данные выводятся в колонке X                                     │ 
   // R=X   - ячейки объединяются, справа будет захвачено еще Х ячеек          │ 
   // D=X   - ячейки объединяются, снизу будет захвачено еще Х ячеек           │ 
   // F=X   - выводится формула, текст в Х                                     │ 
   // WT    - переносить по словам                                             │
   // POIH=X- высота строки чисто для POI                                      │
   //         (плохо работает автовысота для переносов)                        │
   //                                                                          │ 
   // T=XXX - определяется тип переменной. Возможные значения XXX              │ 
   //     D  - дата                                                            │ 
   //     MX - Money                                                           │ 
   //     NX - Number                                                          │ 
   //     PX - Procent                                                         │
   //        (X - кол-во знаков после зяпятой, может отсутствовать, тогда для  │
   //         N X=0, для M и P X=2)                                            │
   //     в остальных случаях - String                                         │ 
   //                                                                          │ 
   // Выравнивание                                                             │ 
   //   P=C - центр по горизонтали и вертикали                                 │ 
   //   HA=X - выравнивание по горизонтали                                     │ 
   //     Значения Х:                                                          │
   //       С - по центру                                                      │ 
   //       L - по левому краю                                                 │ 
   //       R - по правому краю                                                │ 
   //       J - по ширине                                                      │ 
   //   VA=X - выравнивание по вертикали                                       │ 
   //     Значения Х:                                                          │ 
   //       С - по центру                                                      │ 
   //       T - по верхнему краю                                               │ 
   //       B - по нижнему краю                                                │ 
   //   O=X - X-отступ                                                         │
   //                                                                          │
   // Префикс знака                                                            │
   //   + - выводить префикс "+" для неотрицательных значений типа Money       │
   //                                                                          │ 
   // Разделитель ";". Если ключевое слово одно, то разделитель можно не указыв│
   // Если в параметрах присутствует символ "|", то он используется в качестве │
   // разделителя (в формулах иногда используется ";")                         │
   //                                                                          │ 
   // Шрифт.                                                                   │ 
   //                                                                          │ 
   // FN=X - X - наименование шрифта, например "FN=Times New Roman;"           │ 
   // FS=X - X - размер шрифта, например "FS=10;"                              │ 
   // FB   - жирный шрифт                                                      │ 
   // FI   - наклонный шрифт                                                   │ 
   // FU   - подчеркнутый шрифт                                                │ 
   // FC=X - X-цвет шрифта, варианты: R-красный, G-зеленый, B-синий.           │ 
   //     Пример: "FB;FI;FS=12;FC=R;"                                          │ 
   //                                                                          │ 
   // Примеры: "R=2;START;END"                                                 │ 
   //          "T=M;END"                                                       │ 
   //          "R=3;START;T=N2;"                                               │ 
   //──────────────────────────────────────────────────────────────────────────┘
   MACRO Put_Val()
      private var l_Value, l_Index=NULL, l_Point=NULL, l_Formula=NULL, l_Cell_Name=NULL;
      private var l_Right=NULL, l_Down=NULL;
      private var l_Type=NULL, l_String, l_Heig, l_UpString;
      private var l_Style, l_Filter=FALSE;
      private var l_DataType, l_Brl, l_Brr, l_Brt, l_Brb, l_Ha, l_Va, l_FnName, l_FnSize, l_FnBld, l_FnItl, l_FnUnd, l_Color, l_Indent, l_WrapText;
      private var l_str:String=""; 
      private var Day, Month, Year, Hour, Min, Sec, l_Time;
      private var l_InteriorColor;
      private var SignPrefix;
      private var l_poiParamType = "Ljava/lang/String;";
      private var l_poiValue = null;
      private var l_poiHeig = null;
      private var l_format;
      
      GetParm(1, l_Value);    // Значение переменной.
      GetParm(2, l_String);   // Параметры, описанные в строке.
      _Nvl(l_String, "");
      GetParm(3, l_Heig); 
      l_UpString = StrUpr(l_String);
      l_Delim = NULL;
      GetParm(4, l_Delim);
      if (ValType(l_Delim) == V_UNDEF)
         if (Index(l_UpString, "|") > 0)
            l_Delim = "|";
         else
            l_Delim = ";";
         end;
      end;
      l_poiValue = l_Value;
      l_UpString = StrSubst(l_UpString, "P=C", "HA=C;VA=C");
      l_UpString = StrSubst(l_UpString, "STEN", "START;END;");
      if (Value_From_String(l_UpString, "POIH=",   l_poiHeig))  l_Heig  = double(l_poiHeig);  end;
      // Если есть ключевое слово, то начинаем новую строку
      if (Index(l_UpString, "START") > 0) this.New_Line(l_Heig); end;
      // Разбор строки.
      Value_From_String(l_UpString, "T=", l_Type);
      _Nvl(l_Type, "S");
      if (Index(l_UpString, "+") > 0)
        SignPrefix = "+";
      else 
        SignPrefix = ""; 
      end;

      if (Substr(l_Type, 1, 1) == "M")
         l_DataType = "Money";
         l_poiParamType = "D";
         if (StrLen(l_Type) == 1)
            l_Point = 2;
         else
            l_Point = Int(SubStr(l_Type, 2, 1));
         end;
         if(valtype(l_Value) == v_undef)
           l_poiValue = 0.0;
           l_Value = "";
         else
           l_poiValue = Numeric(l_Value);
           l_Value = ExecExp( "String(Sql_Money("+string(double(l_Value)) + "):0:"+string(l_Point)+")");
         end;
      elif (Substr(l_Type, 1, 1) == "A")
         l_DataType = "Account";
         l_Value = String(l_Value);
      elif (Substr(l_Type, 1, 1) == "T")
         l_DataType = "Time";
         l_Value = String(l_Value);
         l_poiValue = DtTm(date(0,0,0),time(l_Value));
         l_poiParamType = "Ljava/util/Date;";
      elif (Substr(l_Type, 1, 1) == "N")
         l_poiParamType = "D";
         l_DataType = "Number";
         l_poiValue = Numeric(l_Value);
         if (StrLen(l_Type) == 1)
            l_Point = 0;
         else
            l_Point = Int(SubStr(l_Type, 2, 1));
         end;
           l_Value = ExecExp( "String(Sql_Num("+string(l_Value) + ","+l_Point+"):0:"+string(l_Point)+")");
      elif (Substr(l_Type, 1, 1) == "P")
         l_DataType = "Procent";
         if (StrLen(l_Type) == 1)
            l_Point = 2;
         else
            l_Point = Int(SubStr(l_Type, 2, 1));
         end;
         l_Point = l_Point + 2;
         l_Value = ExecExp("String((Sql_Num("+l_Value+","+ l_Point+")/100):0:"+l_Point+")");
         l_Point = l_Point - 2;
      elif (Substr(l_Type, 1, 1) == "D")
         l_poiParamType = "Ljava/util/Date;";
         l_DataType = "DateTime";
         if (ValType( l_Value ) == V_DATE)
            l_poiValue = DtTm(date(l_Value),time(0,0,0));
            l_DataType = "Date";
         elif (ValType( l_Value ) == V_TIME)
            l_poiValue = DtTm(date(0,0,0),time(l_Value));
            l_DataType = "Time";
         elif (ValType( l_Value ) == V_DTTM)
            l_poiValue = l_Value;
            l_DataType = "DateTime";
         elif (ValType( l_Value ) == V_STRING)
            l_poiValue = DateParser.Parse( l_Value );
            if (ValType( l_poiValue ) == V_DATE)
               l_DataType = "Date";
            elif (ValType( l_poiValue ) == V_TIME)
               l_DataType = "Time";
            elif (ValType( l_poiValue ) == V_DTTM)
               l_DataType = "DateTime";
            elif (ValType( l_poiValue ) == V_STRING)
               l_poiParamType = "Ljava/lang/String";
               l_DataType = "String";
            end;
            l_format = DateParser.RealFormat;
         end;
         l_Value = Sql_Date(l_Value);
         DateSplit(l_Value, Day, Month, Year);
         DtTmSplit(l_Value, NULL, l_Time);
         TimeSplit(l_Time, Hour, Min, Sec);         
         if (Year > 1)
            l_Value = String(Year, "-", lPad(String(Month), 2, "0"), "-", lPad(String(Day), 2, "0"), "T", lPad(String(Hour), 2, "0"), ":", lPad(String(Min), 2, "0"), ":", lPad(String(Sec), 2, "0"), ".000");
         else
            l_Value = "";
         end;
/*ak
         l_Point = 0;*/
         if (StrLen(l_Type) == 1)
           l_Point = 0
         else
           l_Point = Int(SubStr(l_Type, 2, 1));
         end;
/*~ak*/
      else
         l_DataType = "String";
         l_Value = Sql_String(l_Value);
         l_Value = StrSubst(l_Value, "<", "'");
         l_Value = StrSubst(l_Value, ">", "'");
         l_Point = 0;
      end;

      if ((Substr(l_DataType, 1, 1) == "N")
      or  (Substr(l_DataType, 1, 1) == "M")
      or  (Substr(l_DataType, 1, 1) == "P")
      or  (Substr(l_DataType, 1, 1) == "A")
         )
         l_Type = "Number";
      elif (Substr(l_DataType, 1, 1) == "T")
         l_Type = "DateTime"; 
      else
         l_Type = l_DataType;
      end;

      if (Index(l_UpString, "BOR") > 0)
         l_Brl = TRUE;
         l_Brr = TRUE;
         l_Brt = TRUE;
         l_Brb = TRUE;
      else
         if (Index(l_UpString, "BRL") > 0) l_Brl = TRUE; end;   
         if (Index(l_UpString, "BRR") > 0) l_Brr = TRUE; end;   
         if (Index(l_UpString, "BRT") > 0) l_Brt = TRUE; end;   
         if (Index(l_UpString, "BRB") > 0) l_Brb = TRUE; end;   
      end;   
      Value_From_String(l_String, "FN=", l_FnName);
      if (Value_From_String(l_UpString, "FS=", l_FnSize)) l_FnSize = Int(l_FnSize); end;
      if (Index(l_UpString, "FB") > 0) l_FnBld = TRUE; end;
      if (Index(l_UpString, "FI") > 0) l_FnItl = TRUE; end;
      if (Index(l_UpString, "FU") > 0) l_FnUnd = TRUE; end;
      if (Value_From_String(l_UpString, "I=",   l_Index))  l_Index  = Int(l_Index);  end; _Nvl(l_Index, -1);
      if (Value_From_String(l_UpString, "O=",   l_Indent)) l_Indent = Int(l_Indent); end; _Nvl(l_Indent, Indent_Value);
      if (Value_From_String(l_UpString, "R=",   l_Right))  l_Right  = Int(l_Right);  end; _Nvl(l_Right, 0);
      if (Value_From_String(l_UpString, "D=",   l_Down))   l_Down   = Int(l_Down);   end; _Nvl(l_Down, 0);

      // Шабашов. 29.10.2019 добавил обработку имени ячейки
      Value_From_String(l_UpString, "CN=",  l_Cell_Name);  

      Value_From_String(l_String, "F=",   l_Formula);     _Nvl(l_Formula, "");
      Value_From_String(l_UpString, "HA=",  l_Ha);  
      Value_From_String(l_UpString, "VA=",  l_Va);  
      Value_From_String(l_UpString, "FC=",  l_Color);
      Value_From_String(l_UpString, "IC=",  l_InteriorColor);  

      if(Index(l_UpString, "WT") > 0)
        l_WrapText = 1;
      end;

      // Находим стиль в таблице стилей (или создаем новый).
      l_Style = Add_Style(l_DataType, l_Brl, l_Brr, l_Brt, l_Brb, l_Ha, l_Va, l_FnName, l_FnSize, l_FnBld, l_FnItl, l_FnUnd, l_Point, l_Color, l_Indent, l_InteriorColor, l_WrapText, SignPrefix, l_format);
      
      if (l_Index != -1)
         u_Current_Column = l_Index;
      end;

      poiCurrentCell = poiCurrentRow.{createCell@(I)Lorg/apache/poi/ss/usermodel/Cell;}(u_Current_Column); //xssf->ss
      poiCurrentCell.{setCellStyle@(Lorg/apache/poi/ss/usermodel/CellStyle;)V}(l_Style);
      if ((ValType(l_poiValue) != V_UNDEF) and (Trim(string(l_poiValue)) != ""))
         jvm.callObjectMethod(poiCurrentCell,"setCellValue@("+l_poiParamType+")V", l_poiValue);
      end;
      IF (l_Cell_Name != NULL)
         var newName = poiWorkBook.{createName@()Lorg/apache/poi/ss/usermodel/Name;}(); //xssf->ss
         newName.setNameName(l_Cell_Name);
         var regExObj = RsRegex("[A-Za-z]+");
         if ((not regExObj.match(poiCurrentCell.getAddress().formatAsString())) or (not regExObj.first()))
            RunError("Ошибка распознавания адреса ячейки");
         end;
         newName.setRefersToFormula("'"+u_Rep_Sheet_Name+"'!$"+regExObj.cap(0) + "$" + string(poiCurrentCell.getAddress().getRow()+1));
         nameAddrArr[nameAddrArr.size] = NamedAddr(sheetIdx,l_Cell_Name,u_Current_Column,u_Current_Row);
      END;
      if ((l_Down > 0) or (l_Right > 0))
         var mergeAddress = jvm.createJavaObject("org.apache.poi.ss.util.CellRangeAddress",
                                                 "<init>@(IIII)V", u_Current_Row, u_Current_Row + l_Down, u_Current_Column, u_Current_Column + l_Right);
         poiCurrentSheet.addMergedRegion(mergeAddress);
         if (l_Right > 0)
            var lastCol = u_Current_Column + l_Right;
            var curCol = u_Current_Column + 1;
            while (curCol <= lastCol)
               poiCurrentCell = poiCurrentRow.{createCell@(I)Lorg/apache/poi/ss/usermodel/Cell;}(curCol); //xssf->ss
               poiCurrentCell.{setCellStyle@(Lorg/apache/poi/ss/usermodel/CellStyle;)V}(l_Style);
               curCol = curCol + 1;
            end;
            u_Current_Column = u_Current_Column + l_Right;
         end;
      end;
      if (l_Index == -1)
         u_Current_Column = u_Current_Column + 1;
      end;

      // Если есть ключевое слово, то добавляем тэг конца строки
      if (Index(l_UpString, "END") > 0) this.End_Line(); end;
   END;  // Put_Val() 

   MACRO Add_Format(p_Range, p_Qualif, p_Value, p_Color, p_Pattern)
      RunError("Not implemented yet");
   END;  // Add_Format()
   //───────────────────────────────────────────────────────────────────────────

   MACRO Save(p_Name)
      if (not poiBook.saveAs(p_Name))
         RunError("Ошибка сохранения файла по пути " + p_Name);
      end;
   end;
   
   MACRO Move(p_Name)
      //Ничего не делаем
   END;

   MACRO Print_Bottom()
      //Ничего не делаем
   END;

   MACRO Delete()
      //Ничего не делаем
   END;

   MACRO ExistDataPrint()
      //если текущая ячейка пуста, значит мы никаких данных не заполняли
     return poiCurrentCell != null;
   END;

   PRIVATE MACRO GetColumnSpan(cell)
      if(ValType(mergedRegions) == V_UNDEF)
         mergedRegions = poiCurrentSheet.getMergedRegions().toArray();
      end;

      for(var mr, mergedRegions)
         if(mr.isInRange(cell))
            if((cell.getRowIndex() == mr.getFirstRow()) and (cell.getColumnIndex() == mr.getFirstColumn()))
               return mr.getLastColumn() - mr.getFirstColumn();
            else
               return 0;
            end;
         end;
      end;

      return 1;
   END;

   PRIVATE MACRO calculateRowHeight(p_row)
      var TextAttribute = jvm.callStaticMethod ("ru.softlab.tools.RsJvm","findJavaClass2@(Ljava/lang/String;)Ljava/lang/Class;", "java.awt.font.TextAttribute");
      var ci = p_row.cellIterator();
      var defCharWidth =  jvm.callStaticMethod("org.apache.poi.ss.util.SheetUtil", "getDefaultCharWidth@(Lorg/apache/poi/ss/usermodel/Workbook;)I", poiWorkBook);
      var defRowHeight = poiCurrentSheet.getDefaultRowHeight();
      var maxHeight = defRowHeight;

      while(ci.hasNext())
         var cell;
         if(useXLSFormat)
            cell = ci.{next@()Lorg/apache/poi/ss/usermodel/Cell;}();
         else
            cell = ci.next();
         end;
         var style = cell.{getCellStyle@()Lorg/apache/poi/ss/usermodel/CellStyle;}();
         var font = poiWorkBook.{getFontAt@(I)Lorg/apache/poi/ss/usermodel/Font;}(style.{getFontIndex@()I}());
         var value = jvm.callStaticMethod("ru.softlab.rsbank.poireports.CellUtils", "getCellStringValue@(Lorg/apache/poi/ss/usermodel/Cell;)Ljava/lang/String;", cell);
         var colSpan = GetColumnSpan(cell);

         if((StrLen(value) > 0) and (colSpan > 0) and style.getWrapText())
            var attributedString = jvm.createJavaObject("java.text.AttributedString", "<init>", value);
            attributedString.{addAttribute@(Ljava/text/AttributedCharacterIterator$Attribute;Ljava/lang/Object;II)V}(TextAttribute.getField("FAMILY").get(TextAttribute),
                                                                                                                     font.getFontName(),
                                                                                                                     0,
                                                                                                                     StrLen(value));
            attributedString.{addAttribute@(Ljava/text/AttributedCharacterIterator$Attribute;Ljava/lang/Object;)V}(TextAttribute.getField("SIZE").get(TextAttribute),
                                                                                                                   font.getFontHeightInPoints());
            if (font.getBold()) 
               attributedString.{addAttribute@(Ljava/text/AttributedCharacterIterator$Attribute;Ljava/lang/Object;II)V}(TextAttribute.getField("WEIGHT").get(TextAttribute),
                                                                                                                        TextAttribute.getField("WEIGHT_BOLD").get(TextAttribute),
                                                                                                                        0,
                                                                                                                        StrLen(value));
            end;

            if (font.getItalic())
               attributedString.{addAttribute@(Ljava/text/AttributedCharacterIterator$Attribute;Ljava/lang/Object;II)V}(TextAttribute.getField("POSTURE").get(TextAttribute),
                                                                                                                        TextAttribute.getField("POSTURE_OBLIQUE").get(TextAttribute),
                                                                                                                        0,
                                                                                                                        StrLen(value));
            end;

            var fontRenderContext = jvm.createJavaObject("java.awt.font.FontRenderContext", "<init>", null, true, true);
            var lineBreakMeasurer = jvm.createJavaObject("java.awt.font.LineBreakMeasurer", "<init>", attributedString.getIterator(), fontRenderContext);
            var lineCount = 0, cellWidth = 0;

            for(var i, cell.getColumnIndex(), cell.getColumnIndex() + colSpan - 1)
               cellWidth = cellWidth + poiCurrentSheet.getColumnWidth(i);
            end;

            while(lineBreakMeasurer.getPosition() < StrLen(value))
               var pos = lineBreakMeasurer.nextOffset(cellWidth / 256.0 * defCharWidth);
               lineCount = lineCount + 1;
               lineBreakMeasurer.setPosition(pos);
            end;

            var height = lineCount * defRowHeight;

            if( maxHeight < height )
               maxHeight = height;
            end;
         end;
      end;

      return maxHeight;
   END;

   PRIVATE MACRO GetSheetWidth()
      var totalWidth = 0, lastCol = 0;
      var defCharWidth = jvm.callStaticMethod("org.apache.poi.ss.util.SheetUtil", "getDefaultCharWidth@(Lorg/apache/poi/ss/usermodel/Workbook;)I", poiWorkBook);
      var irow = poiCurrentSheet.rowIterator();

      while(irow.hasNext())
         var row = iRow.next();
         if(lastCol < row.getLastCellNum())
            lastCol = row.getLastCellNum();
         end;
      end;
     
      for(var i, 0, lastCol)
         totalWidth = totalWidth + poiCurrentSheet.getColumnWidth(i);
      end;

      return totalWidth / 256.0 * defCharWidth;
   END;

   // Рассчитывает высоты строк (с высотой по-умолчанию) и устанавливает разрывы строк (перед этим рассчитывается масштаб и отключается автоматическое вписывание листа и авторазрывы), возвращает массив установленных разрывов строк
   MACRO FixRowHeightsAndRowBreaks()
      var rowBreaks = TArray();
      var topMargin_mm, bottomMargin_mm, leftMargin_mm, rightMargin_mm;
      var printSetup = poiCurrentSheet.{getPrintSetup@()Lorg/apache/poi/ss/usermodel/PrintSetup;}();

      if(useXLSFormat)
         topMargin_mm = poiCurrentSheet.getMargin(poiCurrentSheet.{class}.getField("TopMargin").get(poiCurrentSheet.{class})) * INCH_TO_MM;
         bottomMargin_mm = poiCurrentSheet.getMargin(poiCurrentSheet.{class}.getField("BottomMargin").get(poiCurrentSheet.{class})) * INCH_TO_MM;
         leftMargin_mm = poiCurrentSheet.getMargin(poiCurrentSheet.{class}.getField("LeftMargin").get(poiCurrentSheet.{class})) * INCH_TO_MM;
         rightMargin_mm = poiCurrentSheet.getMargin(poiCurrentSheet.{class}.getField("RightMargin").get(poiCurrentSheet.{class})) * INCH_TO_MM;
      else
         topMargin_mm = printSetup.getTopMargin() * INCH_TO_MM;
         bottomMargin_mm = printSetup.getBottomMargin() * INCH_TO_MM;
         leftMargin_mm = printSetup.getLeftMargin() * INCH_TO_MM;
         rightMargin_mm = printSetup.getRightMargin() * INCH_TO_MM;
      end;

      var sheetWidth_mm = GetSheetWidth() * 0.3262; //
      var scale = (A4_LANDSCAPE_WIDTH_MM - leftMargin_mm - rightMargin_mm) / sheetWidth_mm;
      poiCurrentSheet.setFitToPage(false);
      poiCurrentSheet.setAutobreaks(false);
      printSetup.setScale(Int(scale * 100.0));

      var defH = poiCurrentSheet.getDefaultRowHeight(), h, ph = 0;

      for(var i, 0, poiCurrentSheet.getLastRowNum())
         var r = poiCurrentSheet.{getRow@(I)Lorg/apache/poi/ss/usermodel/Row;}(i);
         if(r)
            if(useXLSFormat and (not poiCurrentSheet.getSheet().getRow(i).getBadFontHeight())
               or (not useXLSFormat) and (not r.getCTRow().getCustomHeight())
            )
               h = calculateRowHeight(r);
               if(h > 0)
                  r.setHeight(h);
               else
                  h = defH;
               end;
            else
               h = r.getHeight();
            end;
         else
            h = defH;
         end;

         ph = ph + h;

         if( ph * TWIP_TO_MM * scale + topMargin_mm + bottomMargin_mm > A4_LANDSCAPE_HEIGHT_MM )
            poiCurrentSheet.setRowBreak(i);
            rowBreaks[rowBreaks.Size] = i;
            ph = h;
         end;
      end;

      return rowBreaks;
   END;

END;
