/* dlcalc_in.mac
   макрос инициализации расчетной операций ВУ
*/
import oralib, likepy, globals, lib_lang;

private macro isEdpAttr(sfCOntrID:integer):bool
  var rows = execSqlSelect("SELECT 1 from dual where rshb_rsi_sclimit.SfcontrIsEDP(p_SfcontrID => :id) = 1", MakeArray(SqlParam("id", sfCOntrID)));
  return rows.MoveNext();
end;

private macro GetEDPMode():bool
  var EdpDate = "";
  var result = GetRegistryValue ("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ДАТА ВКЛЮЧЕНИЯ ЕДП", V_STRING, EdpDate );
  if (result == null)
    return true;
  end;
  return (EdpDate != "00.00.0000");
end;

private macro GetEDPModeLegalEntity():bool
  var EdpUL = false;
  var result = GetRegistryValue ("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ВЕДЕНИE ЕДП ЮЛ", V_BOOL, EdpUL );
  if (result == null)
    return false;
  end;
  return EdpUL;
end;

/**
  @brief IsSubjectLegalEntity - проверка что это ЮЛ
  @param[in] partyid - код субъекта экономики
  @return логическое значение
*/
private macro IsSubjectLegalEntity(partyid:integer):bool
  var query = "select 1 "
            + "  from dparty_dbt pt"
            + "  left join dpersn_dbt pr on pr.t_personid = pt.t_partyid "
            + " where pt.t_partyid = :id "
            + "   and (   pt.t_legalform = 1 "
            + "        or pr.t_isemployer = chr(88))";

  var rows = execSqlSelect(query, MakeArray(SqlParam("id", partyid)));
  return rows.MoveNext();
end;

private macro isEdpDoc(sfCOntrID:integer, partyid:integer):bool
  return (GetEDPMode() and isEdpAttr(sfCOntrID) and (GetEDPModeLegalEntity() or (not IsSubjectLegalEntity(partyid))));
end;

//just template
private class c_vldIntarface
  macro Check():string
  end;
end;

// Вид переводов на и с СПБ для договоров не ЕДП не поддерживается. Для ЕДП следует выбирать Фондовый рынок МБ.
class(c_vldIntarface) c_vldCorrectMarket(_sfContrID:integer)
  private const SPB_MARKET_ID = 151337;
  private var sfContrID:integer = _sfContrID;

  private macro GetMarketID(sfContrID:integer):integer
    var rows = execSqlSelect("select t_marketid from ddlcontrmp_dbt where t_sfcontrid = :id", MakeArray(SqlParam("id", sfContrID)));
    if (rows.MoveNext()) 
      return rows.value("t_marketid");
    end;
    return 0;
  end;

  macro Check():string
    if (GetMarketID(sfContrID) == SPB_MARKET_ID)
      return "Вид переводов на и с СПБ для договоров не ЕДП не поддерживается. Для ЕДП следует выбирать Фондовый рынок МБ.";
    end;
    return "";
  end;
END;

  /* subject - Место хранения - t_newplace или t_oldplace
   * subjectKind - вид субъекта - t_newplacekind или t_oldplacekind
   * marketPlace - по факту, торговая площадка. Но в интерфейсе это "сектор" - t_newcentroffice или t_oldcentroffice
   * TODO - путает ФР СПБ и ФР МБ - как поправить?
   */
class(c_vldIntarface) c_vldSfContrAndPlaceMatching(_servKind:integer, _servKindSub:integer, _subject:integer, _subjectKind:integer, _marketPlace:integer, _directionName)
  //Servkind
  private const EXCHANGE_SERVKINDSUB = 8;
  private const STOCK_MARKET_SERVKIND = 1;
  private const CURRENCY_MARKET_SERVKIND = 21;
  private const DERIVATIVE_MARKET_SERVKIND = 15;

  //Subjects
  private const NRD_PARTY_ID = 4;
  private const EXCHANGE_SETTLEMENT_CENTER_KIND = 46;
  private const BANK_KIND = 2;

  //market place
  private const MAIN_MARKET_PLACE = 2;
  private const CURRENCY_MARKET_PLACE = 9;
  private const DERIVATIVE_MARKET_PLACE = 8;

  private var servKind:integer    = _servKind;
  private var servKindSub:integer = _servKindSub;
  private var subject:integer     = _subject;
  private var subjectKind:integer = _subjectKind;
  private var marketPlace:integer = _marketPlace;
  private var directionName:string = _directionName;

  private macro IsExchange(servKindSub:integer)
    return servKindSub == EXCHANGE_SERVKINDSUB;
  end;

  private macro getSubjectName(partyID:integer):string
    var rows = execSqlSelect("select t_name from dparty_dbt where t_partyid = :id", MakeArray(SqlParam("id", partyID)));
    if (rows.MoveNext()) 
      return rows.value("t_name");
    end;
    return "Undefined";
  end;

  private macro getSubjectKindName(partyKind:integer):string
    var rows = execSqlSelect("select t_name2 from dptkind_dbt where t_partykind = :id", MakeArray(SqlParam("id", partyKind)));
    if (rows.MoveNext()) 
      return rows.value("t_name2");
    end;
    return "Undefined";
  end;

  private macro getMarketPlaceName(partyID:integer, placeID:integer):string
    var rows = execSqlSelect("select t_officename from dptoffice_dbt where t_partyid = :partyid and t_officeid = :placeid",
                              MakeArray(SqlParam("partyid", partyID),
                                        SqlParam("placeid", placeID)));
    if (rows.MoveNext()) 
      return rows.value("t_officename");
    end;
    return "Undefined";
  end;

  macro Check():string

    var expectedSubject:integer;
    var expectedSubjectKind:integer;
    var expectedMarketPlace:integer;

    if (not IsExchange(servKindSub))
      expectedSubject     = {OurBank};
      expectedSubjectKind = BANK_KIND;
      expectedMarketPlace = marketPlace; //Если в ui выбрать "Банк", то поле "сектор" затрётся и не будет выводится, но в БД останется прежнее значение
    elif (servKind == STOCK_MARKET_SERVKIND)
      expectedSubject     = NRD_PARTY_ID;
      expectedSubjectKind = EXCHANGE_SETTLEMENT_CENTER_KIND;
      expectedMarketPlace = MAIN_MARKET_PLACE;
    elif (servKind == CURRENCY_MARKET_SERVKIND)
      expectedSubject     = NRD_PARTY_ID;
      expectedSubjectKind = EXCHANGE_SETTLEMENT_CENTER_KIND;
      expectedMarketPlace = CURRENCY_MARKET_PLACE;
    elif (servKind == DERIVATIVE_MARKET_SERVKIND)
      expectedSubject     = NRD_PARTY_ID;
      expectedSubjectKind = EXCHANGE_SETTLEMENT_CENTER_KIND;
      expectedMarketPlace = DERIVATIVE_MARKET_PLACE;
    end;

    if (expectedSubjectKind != subjectKind)
      return "Место учета актива " + directionName + " не соответствует Торговой площадке субдоговора. Ожидается: " + getSubjectKindName(expectedSubjectKind) + ", получено: " + getSubjectKindName(subjectKind);
    end;

    if (expectedSubject != subject)
      return "Место учета актива " + directionName + " не соответствует Торговой площадке субдоговора. Ожидается: " + getSubjectName(expectedSubject) + ", установлено: " + getSubjectName(subject);
    end;

    if (expectedMarketPlace != marketPlace)
      return "Место учета актива " + directionName + " не соответствует Торговой площадке субдоговора. Ожидается: " + getMarketPlaceName(expectedSubject, expectedMarketPlace) + ", получено: " + getMarketPlaceName(subject, marketPlace);
    end;

    return "";
  end;
END;

class (c_vldIntarface) c_SfContrTransaction(_isEDP:bool, _oldServKindSub:integer, _newServKindSub:integer)
  private const EXCHANGE_SERVKINDSUB = 8;

  private var isEDP:bool = _isEDP;
  private var oldServKindSub:integer = _oldServKindSub;
  private var newServKindSub:integer = _newServKindSub;

  private macro IsExchange(servKindSub:integer)
    return servKindSub == EXCHANGE_SERVKINDSUB;
  end;

  macro Check():string
    if (isEDP and (IsExchange(oldServKindSub) == IsExchange(newServKindSub)))
      return "По договорам ЕДП допустимы переводы только с/на Внебиржевой рынок";
    end;
    return "";
  end;
end;

class (c_vldIntarface) c_DifferendOldNewPlaces(_oldSubject:integer, _oldCentrOffice:integer, _newSubject:integer, _newCentrOffice:integer)
  private var oldSubject:integer     = _oldSubject;
  private var oldCentrOffice:integer = _oldCentrOffice;
  private var newSubject:integer     = _newSubject;
  private var newCentrOffice:integer = _newCentrOffice;

  macro Check():string
    if ((oldSubject == newSubject) and (oldCentrOffice == newCentrOffice))
      return "Место учета актива списания не должно совпадать с местом учета актива зачисления";
    end;
    return "";
  end;

end;

class c_validator()
  var validations = TArray(); //c_vldIntarface

  macro Add(vldClass:c_vldIntarface)
    validations[validations.size] = vldClass;
  end;

  macro Run():string
    var vld;
    var err:string;
    for(vld, validations)
      err = vld.Check();
      if (err != "")
        return err;
      end;
    end;

    return "";
  end;
end;

MACRO InitOperation( Oper, dockind, Adr )

  record dl_acc( dl_acc );
  SetBuff ( dl_acc, adr );
  private var oldSfContr = TBfile("sfcontr.dbt");
  private var newSfContr = TBfile("sfcontr.dbt");

  var validator = c_validator();
  var checkResult:string = "";

  if (not oldSfContr.GetEQ(dl_acc.clientContr))
    MsgBox("Договор обслуживания для места учета списания не найден. ID = " + dl_acc.clientContr);
    return 1;
  end;


  if ((dl_acc.operType == 1) or (dl_acc.operType == 48))
    // Вид переводов на и с СПБ для договоров не ЕДП не поддерживается. Для ЕДП следует выбирать Фондовый рынок МБ.
    //    Проще говоря, получается СПБ вообще запрещен?
    validator.Add(c_vldCorrectMarket(oldSfContr.rec.id));

    // Соответствие Мест учета актива и Торговых площадок (субдоговоров) для места списания
    // TODO данная проверка путает ФР СПБ и ФР МБ  
    validator.Add(c_vldSfContrAndPlaceMatching(oldSfContr.rec.servKind, oldSfContr.rec.servKindSub, dl_acc.oldPlace, dl_acc.oldPlaceKind, dl_acc.oldCentrOffice, " списания "));
  end;  

  checkResult = validator.Run();
  if (checkResult != "")
    MsgBox(checkResult);
    return 1;
  end;

  validator = c_validator();

  if (dl_acc.operType == 1)

    // Место учета актива списания не должно совпадать с местом учета актива зачисления
    validator.Add(c_DifferendOldNewPlaces(dl_acc.oldPlace, dl_acc.oldCentrOffice, dl_acc.newPlace, dl_acc.newCentrOffice));

    checkResult = validator.Run();
    if (checkResult != "")
      MsgBox(checkResult);
      return 1;
    end;

    validator = c_validator();

    newSfContr.rec.id = nvl(execStoredFunc( "sec_rovu_read.get_second_sfcontr", V_INTEGER, MakeArray(SqlParam("p_dl_acc_id", dl_acc.ID))),
                               -1,true);
    if (newSfContr.rec.id==-1)
      MsgBox("Договор обслуживания для места учета актива зачисления не найден. РОВУ = " + dl_acc.code);
      return 1;
    end; 
    if (not newSfContr.GetEQ())
      MsgBox("Договор обслуживания для места учета актива зачисления не найден. РОВУ = " + dl_acc.code);
      return 1;
    end;

    // Соответствие Мест учета актива и Торговых площадок (субдоговоров) для места зачисления
    // TODO данная проверка путает ФР СПБ и ФР МБ  
    validator.Add(c_vldSfContrAndPlaceMatching(newSfContr.rec.servKind, newSfContr.rec.servKindSub, dl_acc.newPlace, dl_acc.newPlaceKind, dl_acc.newCentrOffice, " зачисления "));

    var isEDP:bool = (isEdpDoc(oldSfContr.rec.id, oldSfContr.rec.partyid) or isEdpDoc(newSfContr.rec.id, newSfContr.rec.partyid));

    // По договорам ЕДП допустимы переводы только с/на Внебиржевой рынок
    validator.Add(c_SfContrTransaction(isEDP, oldSfContr.rec.servKindSub, newSfContr.rec.servKindSub));

  end;

  checkResult = validator.Run();
  if (checkResult != "")
    MsgBox(checkResult);
    return 1;
  end;

  return 0;
END;
