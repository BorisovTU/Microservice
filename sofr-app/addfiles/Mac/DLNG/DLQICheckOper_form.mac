
import "DlRepForm_h.mac";

PRIVATE CONST PNFLD_REASON       = 0,
              PNFLD_OTHERREASON  = 1;

PRIVATE CONST H_REASON       = 100,
              H_OTHERREASON  = 101;

CLASS DL_QIOperData()
  VAR Reason       = "Зарубежные рынки",
      OtherReason  = "";
END;

PRIVATE MACRO DL_FldProc_Reason( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var MenuValues   = TArray,
      Choice, X:INTEGER,Y:INTEGER;

  if( Cmd == DLG_INIT )
     FldRealValue = FldShowValue = "Зарубежные рынки";

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     MenuValues[0]  = "Зарубежные рынки";
     MenuValues[1]  = "Реорганизация эмитента (лица, обязанного по ценной бумаге)";
     MenuValues[2]  = "Осуществление прав, закрепленных депозитарными расписками";
     MenuValues[3]  = "Реализация преимущественного права приобретения ц/б";
     MenuValues[4]  = "Распределение имущества ликвидируемого юр. лица";
     MenuValues[5]  = "Приобретение ц/б эмитентом";
     MenuValues[6]  = "Приобретение на основании трудового договора";
     MenuValues[7]  = "Приобретение в связи с членством физического лица в совете директоров (наблюдательном совете) юридического лица";
     MenuValues[8]  = "Приобретение через доверительного управляющего";
     MenuValues[9]  = "Приобретение на основании заявки";
     MenuValues[10] = "Приобретение через брокера";
     MenuValues[11] = "Универсальное правопреемство";
     MenuValues[12] = "Конвертация (обмен) других ценных бумаг того же эмитента (по решению эмитента)";
     MenuValues[13] = "Распределение дополнительных ценных бумаг среди владельцев";
     MenuValues[14] = "Приобретение/отчуждение иностранным юридическим или физическим лицом";
     MenuValues[15] = "Приобретение основным обществом, владеющим более 50% акций эмитента, в результате размещения дополнительных акций";
     MenuValues[16] = "Иное";

     X = pThis.CurrentFldX();
     Y = pThis.CurrentFldY();

     Choice = menu( MenuValues, null, "Причины продолжения", X, Y);
     if( Choice >= 0 )
        FldRealValue = FldShowValue = MenuValues[Choice]; 
        if(FldRealValue != "Иное")
           pThis.SetLinkedValue( H_OTHERREASON, "" );
           DisableFields( pThis.Data(), PNFLD_OTHERREASON );
        else
           EnableFields( pThis.Data(), PNFLD_OTHERREASON );
        end
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldRealValue = FldShowValue = "";
  elif (Cmd == DLG_CHANGEVALUE)
    FldRealValue = FldShowValue;
  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) DL_QIOper_Panel()

  private var DL_QIOperFormData:DL_QIOperData;

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_REASON,       H_REASON,       @DL_FldProc_Reason,  DL_QIOperFormData.Reason      );
     AddFieldProc( 0, PNFLD_OTHERREASON,  H_OTHERREASON,  @Default,            DL_QIOperFormData.OtherReason );
     DisableFields( Data(), PNFLD_OTHERREASON );
  END;

  MACRO GetFormFields()
     DL_QIOperFormData.Reason       = GetFieldValue( PNFLD_REASON      );
     DL_QIOperFormData.OtherReason  = GetFieldValue( PNFLD_OTHERREASON );
  END;

  MACRO ReturnFormFields():DL_QIOperData
     GetFormFields();
     return DL_QIOperFormData;
  END;

  PRIVATE MACRO Check():BOOL
     var RetVal:bool = true;
     GetFormFields();
     if((DL_QIOperFormData.Reason == "Иное") and (DL_QIOperFormData.OtherReason == ""))
        MsgBox("Необходимо указать причину.");
        RetVal = false;
     end;
     return RetVal;
  END;

  initDL_CPanel( "deals.lbr", "QIOPER" );
END;
