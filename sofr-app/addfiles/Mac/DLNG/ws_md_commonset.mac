/*
$Name:             ws_md_commonset.mac
$Module:           Монитор дилера
$Description:      WEB. Монитор дилера. Макрос классов и сервисов скроллингов и панелей общих настроек монитора дилера
*/
import "ws_md.mac";
import "ws_md_rates.mac";
import "ws_common.mac";
import "secinter.mac";
import "ws_namealg.mac";
import "ws_typeac.mac";
import "ws_datafilter.mac";
import "ws_person.mac";
import "ws_file.mac";
import "ws_fiunilist.mac";
import "acsgroup_widgetclass.mac";

PRIVATE const MacroName = "ws_md_commonset.mac";
PRIVATE CONST EQUAL_FI_ERR = 1483;
PRIVATE CONST DUPLICATE_CURPAIR_ERR = 4675;

// Класс CMdSettings общие настройки модуля.
class CMdSettings
  var IsStopTrade:Bool;      // Режим торги/стоп торги
  var Muclt:Double;          // Сумма штрафа за отказ клиента (%)
  var Group:TWebAcsGroup;    // Номер группы пользователей
  var Preptime:Integer;      // Время, отводимое на подготовку сделки клиентом по вы-бранному курсу (сек.)
  var MinDealSum:Money;      // Минимальная сумма сделки для расширенной конвертации
  var MinDealSumCrn;         // TWsFinInstr Валюта суммы сделки
  var EndTradeTime:Time;     // Время ограничения торгов
  var CurRestLimit:Double;   // Лимит текущих остатков (%)
  var TermId:Integer;        // Идентификатор терминала
  var TimeUpdateScroll:Time; // Промежуток времени, через который должен обновляться скроллинг курсов
  var Range11:Double;        // Диапазон 1. Мин
  var Range12:Double;        // Диапазон 1. Макс
  var Range21:Double;        // Диапазон 2. Мин
  var Range22:Double;        // Диапазон 2. Макс
  var Range31:Double;        // Диапазон 3. Мин
  var Range32:Double;        // Диапазон 3. Макс
  var GrantEdit;             // Разрешение на редактирование (пользователь находится в группе редактирования)
  var IsExistUnComplDeals;   // Есть ли незавершенные сделки
  var NeedUpdateTradeState:bool;  //Флаг, нужно ли обновлять статус кнопки по таймеру.
  var TimeUpdateTradeState:time;  //Промежуток времени, через который нужно обновлять статус кнопки по таймеру.
end;

// Класс CMdCurPair Валютные пары системы. 
class CMdCurPair
  var ID:Integer;       // Идентификатор
  var PFI;              // TWsFinInstr fiid базовой валюты
  var CFI;              // TWsFinInstr fiid валюты
  var IsShow:Bool;      // Отображать
  var IsLimit:Bool;     // Является валютой ограничения
  var LimitAmount:Money;// Ограничение суммы сделки в базовой валюте
end;


private macro IsExistUnComplDeals()
  var IsExistDeals = false;
  var query = "SELECT t_noticeval FROM DRKNOTICE_DBT WHERE T_NOTICEKIND = " + string (UNCOMPLETED_DEALS); 
  var ds = TRsbDataSet(query);   
  if( ds.MoveNext() and (int(ds.noticeval) > 0) )
     IsExistDeals = true;
  end;

  return IsExistDeals;
end;



// Возвращает общие настройки модуля
macro MdGetSettings() : CMdSettings
  var result : CMdSettings;
  var stat:integer = -1;

  var query = "select * from V_RKSETTINGS";

  var ds = TRsbDataSet(query);

  if( ds.MoveNext() )
    result.IsStopTrade = IIF(SQL_ConvTypeStr(ds.IsStopTrade) == "X", true, false);
    result.Muclt         = ds.Muclt;
    result.Group         = WebCore_CreateTWebAcsGroupFromBD(ds.Group);
    result.Preptime      = ds.Preptime;
    result.MinDealSum    = ds.MinDealSum;
    result.MinDealSumCrn = GetTWsFinInstrByID(SQL_ConvTypeInteger(ds.MinDealSumCrn), CODE_Ccy);
    result.EndTradeTime  = Time(ds.EndTradeTime);
    result.CurRestLimit  = ds.CurRestLimit;
    result.TermId        = ds.TermId;
    result.TimeUpdateScroll = TimeUpdateScroll;
    result.Range11       = RANGE11;
    result.Range12       = RANGE12;
    result.Range21       = RANGE21;
    result.Range22       = RANGE22;
    result.Range31       = RANGE31;
    result.Range32       = RANGE32;
    result.GrantEdit     = MdExistsUserInEditGroup();
    result.IsExistUnComplDeals = IsExistUnComplDeals();
    result.NeedUpdateTradeState = UPDATE_TRADESTATE_ON_TIMER;
    result.TimeUpdateTradeState = TimeUpdateTradeState;
  end;

  return result;

OnError( err )
  WSRunError( MacroName, err, stat );
end;

private macro SetStopTradeTime(EndTradeTime, ErrMsg)
   var stat = 0;
   var sheduler_id = 0;

   GetRegistryValue("МОНИТОР ДИЛЕРА\\ID ПЛАНИРОВЩИКА", V_INTEGER, sheduler_id, stat);

   var old_id = sheduler_id;

   if (stat == 0)
      stat = Web_MdUpdateShedulerTask(sheduler_id, EndTradeTime, STOPTRADE_SHEDULE_EXEC_KEY, STOPTRADE_SHEDULE_TASK_NAME);
   end;                                    

   if(not stat and (sheduler_id != old_id) )
      if(NOT SetDefaultRegistryValue( "МОНИТОР ДИЛЕРА\\ID ПЛАНИРОВЩИКА",  sheduler_id ))
        stat = 1; 

        if(ErrMsg != "")
          ErrMsg = ErrMsg + ", ";
        end;                            
        ErrMsg = "\"ID ПЛАНИРОВЩИКА\" ";

        Web_MdDeleteShedulerTask(sheduler_id);
      end;    
   end;

   return stat == 0;
end;

// Сохраняет общие настройки
macro MdSetSettings(Params):WebServiceResponse
  var stat = 0;
  var res = true;
  var tmp = 0;
  var ErrMsg = "";
  var OldParams = CMdSettings();
  var ResponseBuilder:WebResponseBuilder = WebResponseBuilder();

  InitError();

  if( ValType( Params ) == V_UNDEF )
     stat = 1;
     RunError("Не задан обязательный параметр функции: Сохранение настроек налогового учета!");
  end;

  if(IsStopTrade() )   
    OldParams = MdGetSettings();

    if(OldParams.MinDealSumCrn.Fiid != Params.MinDealSumCrn.Fiid)
      if(NOT SetDefaultRegistryValue( "МОНИТОР ДИЛЕРА\\ВАЛЮТА СУММЫ СДЕЛКИ",  Params.MinDealSumCrn.Fiid ))
        ErrMsg = "\"ВАЛЮТА СУММЫ СДЕЛКИ\" ";
      end;
    end;

    if(OldParams.MinDealSum != Params.MinDealSum)
      if(NOT SetDefaultRegistryValue( "МОНИТОР ДИЛЕРА\\МИНИМАЛЬНАЯ СУММА СДЕЛКИ",  Params.MinDealSum ))
        if(ErrMsg != "")
          ErrMsg = ErrMsg + ", ";
        end;  
        ErrMsg = ErrMsg + "\"МИНИМАЛЬНАЯ СУММА СДЕЛКИ\" ";
      end;
    end;

    if(OldParams.EndTradeTime != Params.EndTradeTime)
      if( NOT SetStopTradeTime(Params.EndTradeTime, @ErrMsg) )
        if(ErrMsg != "")
          ErrMsg = ErrMsg + ", ";
        end;  
        ErrMsg = ErrMsg + "\"ВРЕМЯ ОКОНЧАНИЯ ТОРГОВ\" ";
      end;
    end;

    if(OldParams.Preptime != Params.Preptime)
      if(NOT SetDefaultRegistryValue( "МОНИТОР ДИЛЕРА\\ВРЕМЯ ПОДГОТОВКИ СДЕЛКИ",  Params.Preptime ))
        if(ErrMsg != "")
          ErrMsg = ErrMsg + ", ";
        end;  
        ErrMsg = ErrMsg + "\"ВРЕМЯ ПОДГОТОВКИ СДЕЛКИ\" ";
      end;
    end;

    var GroupID = 0;
    if(ValType( Params.Group ) != V_UNDEF)
      GroupID = Params.Group.GroupID;
    end;
    if(OldParams.Group != Params.Group)
      if(NOT SetDefaultRegistryValue( "МОНИТОР ДИЛЕРА\\ГРУППА ДЛЯ РЕДАКТИРОВАНИЯ", GroupID ))
        if(ErrMsg != "")
          ErrMsg = ErrMsg + ", ";
        end;  
        ErrMsg = ErrMsg + "\"ГРУППА ДЛЯ РЕДАКТИРОВАНИЯ\" ";
      end;
    end;

    if(OldParams.CurRestLimit != Params.CurRestLimit)
      if(NOT SetDefaultRegistryValue( "МОНИТОР ДИЛЕРА\\ЛИМИТ ТЕКУЩИХ ОСТАТКОВ",  Params.CurRestLimit ))
        if(ErrMsg != "")
          ErrMsg = ErrMsg + ", ";
        end;  
        ErrMsg = ErrMsg + "\"ЛИМИТ ТЕКУЩИХ ОСТАТКОВ\" ";
      end;
    end;

    if(OldParams.Muclt != Params.Muclt)
      if(NOT SetDefaultRegistryValue( "МОНИТОР ДИЛЕРА\\СУММА ШТРАФА",  Params.Muclt ))
        if(ErrMsg != "")
          ErrMsg = ErrMsg + ", ";
        end;  
        ErrMsg = ErrMsg + "\"СУММА ШТРАФА\" ";
      end;
    end;

  else 
    ErrMsg = string(IS_STOP_TRADE_ERR_MSG);
  end;  

  if(ErrMsg == "")
     ResponseBuilder.AddErrorEx(stat, MessageSeverity.RuntimeError, "Параметры успешно сохранены!");
  else
     stat = 1;
    ResponseBuilder.AddErrorEx(stat, MessageSeverity.RuntimeError, ErrMsg );
  end;

  return ResponseBuilder.Result;

OnError( err )
  WSRunError( MacroName, err, stat );
end;

// Возвращает валютные пары системы
macro MdGetCurPair()
  var CurPair : CMdCurPair;
  var result = TArray();
  var stat:integer = -1;

  var query = "select T_ID, T_PFI, RSB_SECUR.GetDealFICcy( T_PFI ) T_PCY, T_CFI, RSB_SECUR.GetDealFICcy( T_CFI ) T_CCY, T_ISSHOW, T_ISLIMIT, T_LIMITAMOUNT from V_RKCURPAIR";

  var ds = TRsbDataSet(query);

  while( ds.MoveNext() )
    CurPair             = CMdCurPair();

    CurPair.ID          = ds.ID;
    CurPair.PFI         = GetTWsFinInstrByID(SQL_ConvTypeInteger(ds.PFI), CODE_Ccy);
    CurPair.CFI         = GetTWsFinInstrByID(SQL_ConvTypeInteger(ds.CFI), CODE_Ccy);
    CurPair.IsShow      = IIF(SQL_ConvTypeStr(ds.IsShow) == "X", true, false);
    CurPair.IsLimit     = IIF(SQL_ConvTypeStr(ds.IsLimit) == "X", true, false);
    CurPair.LimitAmount = ds.LimitAmount;

    result.value(result.Size) = CurPair;
  end;

  return result;

OnError( err )
  WSRunError( MacroName, err, stat );
end;

// дублирование записи
private macro IsExistCurPair(pfi: integer, cfi: integer)
  var isExist = false;

  var query = RSDCommand( "SELECT CURPAIR.t_id FROM V_RKCURPAIR CURPAIR WHERE CURPAIR.T_CFI = ? AND CURPAIR.T_PFI = ? "  );
  query.addParam( "", RSDBP_IN, cfi );
  query.addParam( "", RSDBP_IN, pfi );
  query.execute();

  var ds = TRsbDataSet(query);

  if( ds.MoveNext() )
    isExist = true;
  end;

  return isExist;
end;


// проверить валютную пару перед сохранением
private macro CheckCurPair(pfi: integer, cfi: integer)
  var stat = 0;
  if(pfi == cfi)
    stat = EQUAL_FI_ERR;
  elif (IsExistCurPair(pfi, cfi))
    stat = DUPLICATE_CURPAIR_ERR;
  end;
  return stat;    
end;


//Сохраняет валютную пару системы
macro MdSetCurPair( Buf /* :CMdCurPair Буфер записи */):WebServiceResponse
  var stat:integer = 0;
  var sql, query, ds, Action;
  var ResponseBuilder:WebResponseBuilder = WebResponseBuilder();
  var ErrMsg = "";

  InitError();

  if(IsStopTrade() )   
    if( (Buf.ID == null) OR (Buf.ID == 0) )
      stat = CheckCurPair(Buf.Pfi.Fiid, Buf.Cfi.Fiid);
      Action = OperationKind_Insert;
    else
      Action = OperationKind_Update;
    end;
  else 
    stat = 1;
    ErrMsg = string(IS_STOP_TRADE_ERR_MSG);
  end;  

  if (stat == 0)
    if( Action == OperationKind_Insert )

      sql = RSDCommand("INSERT INTO DRKCURPAIR_DBT (T_ID, T_PFI, T_CFI, T_ISSHOW, T_LIMITAMOUNT, T_ISLIMIT, T_PAMOUNT11, T_PAMOUNT12, T_PAMOUNT21, T_PAMOUNT22, T_PAMOUNT31, T_PAMOUNT32 ) " +
                       "VALUES (?, ?, ?, CHR(?), ?, CHR(?), ?, ?, ?, ?, ?, ?)");

      sql.addParam( "", RSDBP_IN, Buf.ID );
      sql.addParam( "", RSDBP_IN, Buf.Pfi.Fiid );
      sql.addParam( "", RSDBP_IN, Buf.Cfi.Fiid );
      sql.addParam( "", RSDBP_IN, IIF( Buf.IsShow , 88, 0) );
      sql.addParam( "", RSDBP_IN, Buf.LimitAmount);

      query = "select T_PAMOUNT11, T_PAMOUNT12, T_PAMOUNT21, T_PAMOUNT22, T_PAMOUNT31, T_PAMOUNT32 from DRKCURPAIR_DBT WHERE T_ISLIMIT = CHR(88)";
      ds = TRsbDataSet(query);
      if( ds.MoveNext() )
        sql.addParam( "", RSDBP_IN, 0);
        sql.addParam( "", RSDBP_IN, ds.Pamount11);
        sql.addParam( "", RSDBP_IN, ds.Pamount12);
        sql.addParam( "", RSDBP_IN, ds.Pamount21);
        sql.addParam( "", RSDBP_IN, ds.Pamount22);
        sql.addParam( "", RSDBP_IN, ds.Pamount31);
        sql.addParam( "", RSDBP_IN, ds.Pamount32);
      else
        sql.addParam( "", RSDBP_IN, 88);
        sql.addParam( "", RSDBP_IN, RANGE11);
        sql.addParam( "", RSDBP_IN, RANGE12);
        sql.addParam( "", RSDBP_IN, RANGE21);
        sql.addParam( "", RSDBP_IN, RANGE22);
        sql.addParam( "", RSDBP_IN, RANGE31);
        sql.addParam( "", RSDBP_IN, RANGE32);
      end;

      sql.execute();

      query = "select T_ID from V_RKCURPAIR WHERE T_PFI = " + String(Buf.Pfi.Fiid) + " AND T_CFI = " + String(Buf.Cfi.Fiid);
      ds = TRsbDataSet(query);
      if( ds.MoveNext() )
        sql = RSDCommand("INSERT INTO DRKPARAMS_DBT (T_DOCKIND, T_DOCID, T_CURPAIRID, T_SPRED1, T_SPRED2, T_SPRED3, T_MARGINBUY, T_MARGINSALE, T_MARGINCOVERBUY, T_MARGINCOVERSALE, "+
                         " T_MARGINTOMBUY, T_MARGINTOMSALE, T_MARGINSPOTBUY, T_MARGINSPOTSALE ) VALUES (?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?)");
        sql.addParam( "", RSDBP_IN, 0 );
        sql.addParam( "", RSDBP_IN, 0 );
        sql.addParam( "", RSDBP_IN, ds.ID );
        sql.addParam( "", RSDBP_IN, 0);
        sql.addParam( "", RSDBP_IN, 0);
        sql.addParam( "", RSDBP_IN, 0);
        sql.addParam( "", RSDBP_IN, 0);
        sql.addParam( "", RSDBP_IN, 0);
        sql.addParam( "", RSDBP_IN, 0);
        sql.addParam( "", RSDBP_IN, 0);
        sql.addParam( "", RSDBP_IN, 0);
        sql.addParam( "", RSDBP_IN, 0);
        sql.addParam( "", RSDBP_IN, 0);
        sql.addParam( "", RSDBP_IN, 0);
        sql.execute();
      end;

    elif( Action == OperationKind_Update )
      query = "select T_ISLIMIT from V_RKCURPAIR WHERE T_PFI = " + String(Buf.Pfi.Fiid) + " AND T_CFI = " + String(Buf.Cfi.Fiid);
      ds = TRsbDataSet(query);
      if( ds.MoveNext() )
        if(Buf.IsLimit != ds.IsLimit)
          sql = RSDCommand("UPDATE V_RKCURPAIR SET T_ISLIMIT = CHR(?) WHERE T_ID = ?");
          sql.addParam( "", RSDBP_IN, IIF( Buf.IsLimit , 88, 0) );
          sql.addParam( "", RSDBP_IN, Buf.ID );
        else
          sql = RSDCommand("UPDATE V_RKCURPAIR SET T_ISSHOW = CHR(?), T_LIMITAMOUNT = ? WHERE T_ID = ?");
          sql.addParam( "", RSDBP_IN, IIF( Buf.IsShow , 88, 0) );
          sql.addParam( "", RSDBP_IN, Buf.LimitAmount );
          sql.addParam( "", RSDBP_IN, Buf.ID );
        end;
        sql.execute();
      end;
    end;
  else
    if(ErrMsg == "")
      stat = -1;
      ErrMsg = GetErrMsg();
      if( ErrMsg != "" )
         ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, ErrMsg );
      else
         ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, "Ошибка сохранения параметров." );
      end;
    else
      ResponseBuilder.AddErrorEx(stat, MessageSeverity.RuntimeError,  ErrMsg );
    end;
  end;

  return ResponseBuilder.Result;

OnError( err )
  WSRunError( MacroName, err, stat );
end;

// Возвращает код нац. валюты
macro MdGetNatCur()
  return NatCur;
end;


// Возвращает статус торгов
macro MdGetTradeState() : bool
  var result : bool;
  var stat:integer = -1;

  var query = "SELECT q.t_State "+
              "   FROM (  SELECT T_State "+
              "             FROM DRKTRADEHIST_DBT "+
              "         ORDER BY T_SYSDATE DESC, T_CHANGETIME DESC, T_ID DESC) q "+
              "  WHERE ROWNUM < 2 ";

  var ds = TRsbDataSet(query);

  if( ds.MoveNext() )
    result = IIF(SQL_ConvTypeInteger(ds.State) == START, false, true); /* если true - значит торги остановлены*/
  end;

  return result;

OnError( err )
  WSRunError( MacroName, err, stat );
end;



