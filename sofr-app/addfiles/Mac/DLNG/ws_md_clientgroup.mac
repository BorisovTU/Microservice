/*
$Name:        ws_md_clientgroup.mac
$Module:      Монитор дилера
$Description: Макрос виджета групп клиентов класс
*/

import "ws_md_commonset.mac";

class TWebClientGroup
  var GroupID;
  var Name;
  var AttrID;
  var AttrName;
end;

class TWebClientGroupListerItem
  var GroupID;
  var Name;
  var AttrID;
  var AttrName;
end;

////////////////////////////////////////////////////////////////////////////////////////////////////////////
// class ClientGroupList
////////////////////////////////////////////////////////////////////////////////////////////////////////////

class ClientGroupList()

  macro GetList(sub, listLen, kind_client)
          
    var cmd, rs, GroupID = 0;

    var Arr = TArray;

    var query = "SELECT ATTR.T_OBJECTTYPE, ATTR.T_GROUPID, GRP.T_NAME, ATTR.T_ATTRID, ATTR.T_NAME T_ATTRNAME " +
                "FROM DOBJATTR_DBT ATTR, DOBJGROUP_DBT GRP " + 
                "WHERE ATTR.T_OBJECTTYPE = GRP.T_OBJECTTYPE AND ATTR.T_GROUPID = GRP.T_GROUPID AND GRP.T_OBJECTTYPE = 3 ";
    
    if( ValType( kind_client ) != V_UNDEF )
      query = query + "AND GRP.T_GROUPID = ? ";
    else
      query = query + "AND GRP.T_GROUPID IN (?,?) "; 
    end;

    if( listLen != 0 )

      query = query + " AND ROWNUM < " + string(listLen);

    end;
    
    query = query + " ORDER BY T_GROUPID ";

    cmd = RsdCommand(query);
    
    cmd.NullConversion = true;

    if( ValType( kind_client ) != V_UNDEF )
      if(kind_client == 1)
        GroupID = группа_ЮЛ;
      elif(kind_client == 2)
        GroupID = группа_ФЛ;
      end;
      cmd.addParam( "", RSDBP_IN, GroupID );
    else
      cmd.addParam( "", RSDBP_IN, группа_ЮЛ );
      cmd.addParam( "", RSDBP_IN, группа_ФЛ );
    end;

    rs = RsdRecordset(cmd);
   
    while(rs.movenext)

      var obj = TWebClientGroupListerItem;
      
      obj.GroupID       = SQL_ConvType(rs.value("t_GroupID"));
      obj.Name          = SQL_ConvType(rs.value("t_Name"));
      obj.AttrID        = SQL_ConvType(rs.value("t_AttrID"));
      obj.AttrName      = SQL_ConvType(rs.value("t_AttrName"));

      Arr[Arr.size] = obj;

    end;

    return Arr;

  end;

end;

macro WebCore_CreateTWebClientGroupFromBD(GroupID, AttrID)

  var obj;

  var cmd, rs;

  var query = "SELECT ATTR.T_OBJECTTYPE, ATTR.T_GROUPID, GRP.T_NAME, ATTR.T_ATTRID, ATTR.T_NAME T_ATTRNAME " +
              "FROM DOBJATTR_DBT ATTR, DOBJGROUP_DBT GRP " + 
              "WHERE ATTR.T_OBJECTTYPE = GRP.T_OBJECTTYPE AND ATTR.T_GROUPID = GRP.T_GROUPID AND GRP.T_OBJECTTYPE = 3 AND GRP.T_GROUPID = ? AND ATTR.T_ATTRID = ? ";

  cmd = RsdCommand(query);
  
  cmd.addParam( "", RSDBP_IN, GroupID );
  cmd.addParam( "", RSDBP_IN, AttrID );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    obj = TWebClientGroup;

    obj.GroupID    = SQL_ConvType(rs.value("t_GroupID"));
    obj.Name       = SQL_ConvType(rs.value("t_Name"));
    obj.AttrID     = SQL_ConvType(rs.value("t_AttrID"));
    obj.AttrName   = SQL_ConvType(rs.value("t_AttrName"));

  end;

  return obj;

end;

macro WebCore_CreateTWebClientGroup(GroupID, GroupName, AttrId, AttrName)

  var obj = TWebClientGroup;
  
  obj.GroupID    = GroupID;
  obj.Name       = GroupName;
  obj.AttrID     = AttrID;
  obj.AttrName   = AttrName;

  return obj;

end;

// Получить список групп 

macro WebCore_GetClientGroupListerItemList(sub, listLen, param):TArray
    
  var obj = ClientGroupList();

  return obj.GetList( sub, listLen, param );
  
end;