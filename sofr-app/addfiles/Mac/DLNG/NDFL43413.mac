/*
$Name:         NDFL43413.mac
$Module:       Ценные бумаги
$Description:  Генерация операций удержания НДФЛ
*/
IMPORT  globals,"nptxcalcfun.mac";
IMPORT RsbDataSet, cb_sql;

import "sp_categ.mac";

private macro GetNOB(Client, Year)
  var result = 0;
  var DataSet, query;

  query = RSDCommand(" SELECT  SUM (NVL(obj.t_Sum0,0)) as Sum"
                   + " FROM dnptxobj_dbt obj, dparty_dbt pt "
                   + "WHERE     obj.t_client = ? "
                   + "      AND obj.t_date >= TO_DATE ('01.01.' || ?, 'DD.MM.YYYY') "
                   + "      AND obj.t_date <= TO_DATE ('31.12.' || ?, 'DD.MM.YYYY') "
                   + "      AND pt.t_partyid = obj.t_client "
                   + "      AND obj.t_kind = (CASE WHEN pt.t_notresident = CHR (88) THEN 840 ELSE 1153 END) "
                   + " GROUP BY obj.t_client  "
                   );

  query.addParam( "", RSDBP_IN, Client );
  query.addParam( "", RSDBP_IN, Year );
  query.addParam( "", RSDBP_IN, Year );
  query.execute();

  DataSet = TRsbDataSet( query );
  if( (DataSet.MoveNext()))
    result  = DataSet.Sum;
  end;
  return result;
end;

private macro CalcNdfl(sum)
  var result = 0;
  if(sum> 5000000)
    result = 650000 + ((sum-5000000)*0.15);
  else
    result = sum*0.13;
  end;
  return result;
end;

MACRO insert_ndfl_oper( nptxop, OperCode:@variant, Name, Code, rest, year, Old_SumNob, NewNob )
  var cmd, rs, nobnum;
  var yyyy;
  var RefID, Number, op_id = 0, op_account = "";   // параметры референса
  var query;
  var IsOut = false;
  var OutRest = $0;
  var OutAcc = "";
  var ByKBK = false, DgKBK1 = $0, DgKBK2 = $0, taxeKBK1 = $0, taxeKBK2 = $0;
  var SumNdfl = 0.0;
  sumNdfl = round((CalcNdfl(NewNob) - CalcNdfl(Old_SumNob)),0);

  DateSplit( nptxop.operdate, null, null, yyyy);
  //------------------------------------------------------------------------------------------------------
  // изначально ищем для фондового рынка, если не находим счет, то ищем по срочному и валютному.
  // редкий вариант

  if((nptxop.account == "")or(valtype(nptxop.account)==V_UNDEF))
    query = " SELECT rd.* , rsb_account.restac( rd.t_account, 0, SYSDATE, 1, null ) rest FROM  " +
             "(SELECT t_account " +
             "          FROM (  SELECT sett.t_account, " +
             "                         ABS ( " +
             "                           NVL ( " +
             "                             (SELECT t_Rest " +
             "                                FROM drestdate_dbt rd " +
             "                               WHERE     rd.t_AccountID = acnt.t_AccountID " +
             "                                     AND rd.t_RestCurrency = 0 " +
             "                                     AND rd.t_RestDate = " +
             "                                           (SELECT MAX (t.t_RestDate) " +
             "                                              FROM drestdate_dbt t " +
             "                                             WHERE     t.t_AccountID = " +
             "                                                         rd.t_accountID " +
             "                                                   AND t.t_RestCurrency = 0 " +
             "                                                   AND t.t_RestDate <= SYSDATE)), " +
             "                             0)) " +
             "                           RestForOrder " +
             "                    FROM dsettacc_dbt sett, " +
             "                         dsfcontr_dbt sf, " +
             "                         ddlcontr_dbt dl, " +
             "                         ddlcontrmp_dbt mp, " +
             "                         dsfssi_dbt si, " +
             "                         daccount_dbt acnt " +
             "                   WHERE " + 
             "                         sf.t_partyID = ? " +
             "                         AND sf.t_servkind IN (?,?) " +
             "                         AND sf.t_servkindsub = 8  " +
             "                         AND si.t_objectType = 659  " +
             "                         AND si.t_objectid = LPAD (sf.t_id, 10, '0') " +
             "                         AND sett.t_settaccID = si.t_setaccid " +
             "                         AND sett.t_fiid = 0  " +
             "                         AND sett.t_bankid = 1 " +
             "                         AND sett.t_account LIKE '306%' " +
             "                         AND SUBSTR (sett.t_account, 14, 1) = '0' " +
             "                         AND acnt.t_Chapter = 1  " +
             "                         AND acnt.t_Code_Currency = 0 " +
             "                         AND acnt.t_Account = sett.t_account " +
             "                         AND mp.t_SfContrID = sf.t_ID " + 
             "                         AND dl.t_DlContrID = mp.t_DlContrID " +
             "                         AND sf.t_dateClose = to_date('01.01.0001','DD.MM.YYYY') " +
             "                         AND sf.t_datebegin < ? " +
             "                         AND dl.t_iis = CHR(0) " +
             "                ORDER BY RestForOrder DESC) " +
             "         WHERE ROWNUM = 1 )rd";
    cmd = RSDCommand(query);
    cmd.addParam( "", RSDBP_IN, nptxop.client ); 
    cmd.addParam( "", RSDBP_IN, PTSK_DV ); 
    cmd.addParam( "", RSDBP_IN, PTSK_CM ); 
    cmd.addParam( "", RSDBP_IN, date(1, 1, yyyy+1)   );
    cmd.execute();
    rs  = TRsbDataSet(cmd);
    rs.movenext();
    nptxop.account = rs.account;
  end;
  // считаем сумму налога, который нужно заплатить

  var Bg, Bm, Bs, Bo, Bd, Bp;
  var Dg = $0, Dm = $0, Ds = $0, Dp = $0, Po = $0;
  var Tax = $0, taxe = $0, taxe15 = $0;
  var TaxToPayNow = $0;

  CalcPaidSum(nptxop.client, nptxop.Contract, DL_HOLDNDFL, DL_TXHOLD_OPTYPE_ENDYEAR, date(1,1,yyyy), date(31,12,yyyy), 0, 0, 0, @Bg, @Bm, @Bs, @Dm, @Ds, @Dg, UNSET_CHAR, @Po, @Bd, @Bp, @Dp,  null, null, null, null, null, null, null, null, @ByKBK, @DgKBK1, @DgKBK2 );
  TaxToPayNow = max(Ds + Dg, 0) + max(Dp, 0);

  if((ByKBK == true) and (TaxToPayNow > 0) and (Dg > 0))
    taxeKBK1 = min(max(Ds+DgKBK1, 0), TaxToPayNow);
    taxeKBK2 = min(max(Ds+DgKBK2, 0), TaxToPayNow);
    taxe     = taxeKBK1 + taxeKBK2;
  else 
    taxe   = min(max(Ds+Dg, 0), TaxToPayNow);
  end;

  taxe15 = min(max(Dp, 0), TaxToPayNow-taxe);

  Tax = taxe + taxe15;

  if((nptxop.account == "")or(valtype(nptxop.account)==V_UNDEF))
    [│ ############################# │#####################│###################│######################│#########################│##############│##################│##################│##################################│]
     ( Name:w, Code:w, year:w, Old_SumNob:w, NewNob:w ,sumNdfl:w, Tax:w, Rest:w, "не найден открытый счет" );
    return;
  end;

  if(Tax < 0)
    [│ ############################# │#####################│###################│######################│#########################│##############│##################│##################│##################################│]
     ( Name:w, Code:w, year:w, Old_SumNob:w, NewNob:w ,sumNdfl:w, Tax:w, Rest:w, "Отрицательная сумма налога" );
    return;
  end;
  if(Tax == 0)
    [│ ############################# │#####################│###################│######################│#########################│##############│##################│##################│##################################│]
     ( Name:w, Code:w, year:w, Old_SumNob:w, NewNob:w ,sumNdfl:w, Tax:w, Rest:w, "Нулевая сумма налога" );
    return;
  end;

  if((Rest < Tax) AND (Tax > 0))//на бирже не хватает средств, ищем на внебирже 
    query = " SELECT rd.* , rsb_account.restac( rd.t_account, 0, SYSDATE, 1, null ) rest FROM  " +
             "(SELECT t_account " +
             "          FROM (  SELECT sett.t_account, " +
             "                         ABS ( " +
             "                           NVL ( " +
             "                             (SELECT t_Rest " +
             "                                FROM drestdate_dbt rd " +
             "                               WHERE     rd.t_AccountID = acnt.t_AccountID " +
             "                                     AND rd.t_RestCurrency = 0 " +
             "                                     AND rd.t_RestDate = " +
             "                                           (SELECT MAX (t.t_RestDate) " +
             "                                              FROM drestdate_dbt t " +
             "                                             WHERE     t.t_AccountID = " +
             "                                                         rd.t_accountID " +
             "                                                   AND t.t_RestCurrency = 0 " +
             "                                                   AND t.t_RestDate <= SYSDATE)), " +
             "                             0)) " +
             "                           RestForOrder " +
             "                    FROM dsettacc_dbt sett, " +
             "                         dsfcontr_dbt sf, " +
             "                         ddlcontr_dbt dl, " +
             "                         ddlcontrmp_dbt mp, " +
             "                         dsfssi_dbt si, " +
             "                         daccount_dbt acnt " +
             "                   WHERE " + 
             "                         sf.T_PArtyID = ? " +
             "                         AND sf.t_servkind IN (?,?,?) " +
             "                         AND sf.t_servkindsub = 9  " +
             "                         AND si.t_objectType = 659  " +
             "                         AND si.t_objectid = LPAD (sf.t_id, 10, '0') " +
             "                         AND sett.t_settaccID = SI.t_setaccid " +
             "                         AND sett.t_fiid = 0  " +
             "                         AND sett.t_bankid = 1 " +
             "                         AND sett.t_account LIKE '306%' " +
             "                         AND SUBSTR (sett.t_account, 14, 1) = '0' " +
             "                         AND acnt.t_Chapter = 1  " +
             "                         AND acnt.t_Code_Currency = 0 " +
             "                         AND acnt.t_Account = sett.t_account " +
             "                         AND mp.t_SfContrID = sf.t_ID " + 
             "                         AND dl.t_DlContrID = mp.t_DlContrID " +
             "                         AND sf.t_dateClose = to_date('01.01.0001','DD.MM.YYYY') " +
             "                         AND sf.t_datebegin < ? " +
             "                         AND dl.t_iis = CHR(0) " +
             "                ORDER BY RestForOrder DESC) " +
             "         WHERE ROWNUM = 1 )rd";
    cmd = RSDCommand(query);
    cmd.addParam( "", RSDBP_IN, nptxop.client ); 
    cmd.addParam( "", RSDBP_IN, PTSK_STOCKDL ); 
    cmd.addParam( "", RSDBP_IN, PTSK_DV ); 
    cmd.addParam( "", RSDBP_IN, PTSK_CM ); 
    cmd.addParam( "", RSDBP_IN, date(1, 1, yyyy+1)   );
    cmd.execute();
    rs  = TRsbDataSet(cmd);
    if(rs.movenext())
       IsOut = true;
       OutRest = rs.Rest;
       OutAcc  = rs.account;
    end;
  end;
  if( (Rest + OutRest) <= 0 )//на бирже и внебирже не нет средств
    [│ ############################# │#####################│###################│######################│#########################│##############│##################│##################│##################################│]
     ( Name:w, Code:w, year:w, Old_SumNob:w, NewNob:w ,sumNdfl:w, Tax:w, Rest:w, "Недостаточно средств для списания" );
    return;
  end;
  RslDefCon.BeginTrans();

  if (Rest > 0)
    cmd = RSDCommand(" DECLARE ID NUMBER(10); " +
    " BEGIN " +
    " INSERT INTO dnptxop_dbt (T_ID,T_DOCKIND,T_KIND_OPERATION,T_SUBKIND_OPERATION,T_CODE,T_OPERDATE,T_CLIENT,T_CONTRACT,T_PREVDATE,T_PLACEKIND,T_PLACE,T_TAXBASE,T_OUTSUM,T_OUTCOST,T_TOUT,T_TOTALTAXSUM,T_PREVTAXSUM,T_TAXSUM,T_TAX,T_METHOD,T_ACCOUNT,T_CURRENCY,T_STATUS,T_OPER,T_DEPARTMENT,T_IIS) "+
    " VALUES (to_char(DNPTXOP_DBT_SEQ.NextVal),?,?,?,?,?,?,?,?,0,0,0,0,0,0,0,0,0,0,10,?,0,0,?,?,?) RETURNING T_ID INTO ID; " +
    " END; ");
  
    //cmd.addParam( "", RSDBP_IN, op_id );  // T_ID
    cmd.addParam( "", RSDBP_IN, DL_HOLDNDFL );  // T_DOCKIND
    cmd.addParam( "", RSDBP_IN, 2038 );  // T_KIND_OPERATION
    cmd.addParam( "", RSDBP_IN, DL_TXHOLD_OPTYPE_ENDYEAR );  // T_SUBKIND_OPERATION
    cmd.addParam( "", RSDBP_IN, OperCode ); // T_CODE
    cmd.addParam( "", RSDBP_IN, /*date(nptxop.operdate) */{Curdate}); // T_OPERDATE
    cmd.addParam( "", RSDBP_IN, nptxop.client );         // T_CLIENT
    cmd.addParam( "", RSDBP_IN, nptxop.contract ); // T_CONTRACT
    cmd.addParam( "", RSDBP_IN, date(1,1,yyyy) );         // T_PREVDATE
    cmd.addParam( "", RSDBP_IN, nptxop.account );   // MZ  op_account );          // T_OPER
    cmd.addParam( "", RSDBP_IN, {oper} );          // T_OPER
    cmd.addParam( "", RSDBP_IN, nptxop.department);       // T_DEPARTMENT
    cmd.addParam( "", RSDBP_IN, nptxop.iis);       // T_DEPARTMENT
    cmd.addParam( "", RSDBP_IN, DL_HOLDNDFL );  // T_DOCKIND
    cmd.execute();
    [│ ############################# │#####################│###################│######################│#########################│##############│##################│##################│##################################│]
     ( Name:w, Code:w, year:w, Old_SumNob:w, NewNob:w ,sumNdfl:w, Tax:w, Rest:w, "Операция создана" );
  end;
  if( (Rest < Tax) AND (Tax > 0) AND (OutRest > 0))  // для внебиржи только если нет хватает на бирже и на внебирже есть остаток
    OperCode = OperCode +IIF((Rest > 0),1,0);   //если не создавали на бирже, то не увеличиваем
    cmd = RSDCommand(" DECLARE ID NUMBER(10); " +
    " BEGIN " +
    " INSERT INTO dnptxop_dbt (T_ID,T_DOCKIND,T_KIND_OPERATION,T_SUBKIND_OPERATION,T_CODE,T_OPERDATE,T_CLIENT,T_CONTRACT,T_PREVDATE,T_PLACEKIND,T_PLACE,T_TAXBASE,T_OUTSUM,T_OUTCOST,T_TOUT,T_TOTALTAXSUM,T_PREVTAXSUM,T_TAXSUM,T_TAX,T_METHOD,T_ACCOUNT,T_CURRENCY,T_STATUS,T_OPER,T_DEPARTMENT,T_IIS) "+
    " VALUES (to_char(DNPTXOP_DBT_SEQ.NextVal),?,?,?,?,?,?,?,?,0,0,0,0,0,0,0,0,0,0,10,?,0,0,?,?,?) RETURNING T_ID INTO ID; " +
    " END; ");

    cmd.addParam( "", RSDBP_IN, DL_HOLDNDFL );  // T_DOCKIND
    cmd.addParam( "", RSDBP_IN, 2038 );  // T_KIND_OPERATION
    cmd.addParam( "", RSDBP_IN, DL_TXHOLD_OPTYPE_ENDYEAR );  // T_SUBKIND_OPERATION
    cmd.addParam( "", RSDBP_IN, OperCode ); // T_CODE
    cmd.addParam( "", RSDBP_IN, /*date(nptxop.operdate) */{Curdate}); // T_OPERDATE
    cmd.addParam( "", RSDBP_IN, nptxop.client );         // T_CLIENT
    cmd.addParam( "", RSDBP_IN, nptxop.contract ); // T_CONTRACT
    cmd.addParam( "", RSDBP_IN, date(1,1,yyyy) );         // T_PREVDATE
    cmd.addParam( "", RSDBP_IN, OutAcc );   
    cmd.addParam( "", RSDBP_IN, {oper} );          // T_OPER
    cmd.addParam( "", RSDBP_IN, nptxop.department);       // T_DEPARTMENT
    cmd.addParam( "", RSDBP_IN, nptxop.iis);       // T_DEPARTMENT
    cmd.addParam( "", RSDBP_IN, DL_HOLDNDFL );  // T_DOCKIND
    cmd.execute();
    [│ ############################# │#####################│###################│######################│#########################│##############│##################│##################│##################################│]
     ( Name:w, Code:w, year:w, Old_SumNob:w, NewNob:w ,sumNdfl:w, Tax:w, OutRest:w, "Операция создана" );
  end;
  RslDefCon.CommitTrans(); /*Завершить транзакцию*/

OnError(er)
    [│ ############################# │#####################│###################│######################│#########################│##############│##################│##################│##################################│]
     ( Name:w, Code:w, year:w, Old_SumNob:w, NewNob:w ,sumNdfl:w, Tax:w, OutRest:w, "Операция не создана" );
  RslDefCon.RollbackTrans();

END;

PRIVATE MACRO Execute()

   var Query, cmd, rsd, dateop, count;
   var yyyy, FirstNum = 1, FlagCorrectNumber;
   record nptxop("nptxop");
   var isFirst = true;
   var NewNob= 0.0;
   if (MsgBoxEx( "Генерация НДФЛ по концу года.|Продолжить?", MB_YES+MB_NO, IND_NO,  "Предупреждение", "") != IND_YES ) 
      return 1;
   end;
   Query = " select NVL(max(to_number(t_code)),0) num " +
           " from (select t_code, rownum from dnptxop_Dbt where t_dockind = ? and regexp_like(t_code, '^[0-9]+$')) " +
           " where to_number(t_code) < 4000000"; 
   cmd = RSDCommand(Query);
   cmd.addParam( "", RSDBP_IN, DL_HOLDNDFL );  // T_DOCKIND
   rsd = TRsbDataSet(cmd);
   rsd.movenext();
   if ( rsd.num >= FirstNum ) 
        FirstNum = Int(rsd.num) + 1;
   end;


  Query = " SELECT DISTINCT rd.* , rsb_account.restac( rd.t_account, 0, SYSDATE, 1, null ) rest FROM  " +
          "(SELECT NVL (t_department, 1) AS t_department, " +
          "       t_operdate, " +
          "       def.t_client, " +
          "       t_contract, " +
          "       (SELECT pt.t_ShortName FROM dparty_dbt pt WHERE pt.t_partyID = def.t_client) Name, " +
          "       (SELECT oc.t_Code " +
          "          FROM dobjcode_dbt oc  " +
          "         WHERE     oc.t_ObjectType = 3 " +
          "               AND oc.t_CodeKind = 1 " +
          "               AND oc.t_state = 0  " +   
          "               AND oc.t_objectid = def.t_client) code, " +
          "       (SELECT t_account " +
          "          FROM (  SELECT sett.t_account, " +
          "                         ABS ( " +
          "                           NVL ( " +
          "                             (SELECT t_Rest " +
          "                                FROM drestdate_dbt rd " +
          "                               WHERE     rd.t_AccountID = acnt.t_AccountID " +
          "                                     AND rd.t_RestCurrency = 0 " +
          "                                     AND rd.t_RestDate = " +
          "                                           (SELECT MAX (t.t_RestDate) " +
          "                                              FROM drestdate_dbt t " +
          "                                             WHERE     t.t_AccountID = " +
          "                                                         rd.t_accountID " +
          "                                                   AND t.t_RestCurrency = 0 " +
          "                                                   AND t.t_RestDate <= SYSDATE)), " +
          "                             0)) " +
          "                           RestForOrder " +
          "                    FROM dsettacc_dbt sett, " +
          "                         dsfcontr_dbt sf, " +
          "                         ddlcontr_dbt dl, " +
          "                         ddlcontrmp_dbt mp, " +
          "                         dsfssi_dbt si, " +
          "                         daccount_dbt acnt " +
          "                   WHERE " + 
          "                         sf.T_PArtyID = def.t_client " +
          "                         AND sf.t_servkind IN (1) " +
          "                         AND sf.t_servkindsub = 8  " +
          "                         AND si.t_objectType = 659  " +
          "                         AND si.t_objectid = LPAD (sf.T_ID, 10, '0') " +
          "                         AND sett.t_settaccID = SI.T_SETACCID " +
          "                         AND sett.t_fiid = 0  " +
          "                         AND sett.t_bankid = 1 " +
          "                         AND sett.t_account LIKE '306%' " +
          "                         AND SUBSTR (sett.t_account, 14, 1) = '0' " +
          "                         AND acnt.t_Chapter = 1  " +
          "                         AND acnt.t_Code_Currency = 0 " +
          "                         AND acnt.t_Account = sett.t_account " +
          "                         AND mp.t_SfContrID = sf.t_ID " + 
          "                         AND dl.t_DlContrID = mp.t_DlContrID " +
          "                         AND sf.t_dateClose = to_date('01.01.0001','DD.MM.YYYY') " +
          "                         AND sf.t_datebegin < ? " +
          "                         AND dl.t_iis = CHR(0) " +
          "                ORDER BY RestForOrder DESC) " +
          "         WHERE ROWNUM = 1) " +
          "         t_account, " +
          "         def.t_year, " +
          "         def.t_Old_SumNob " +
          "  FROM dnptxop_Dbt dnptxop, def43413_dbt def " +
          " WHERE  def.t_op_created = CHR(88)  " + 
          "     AND dnptxop.t_status = 2" +
          "     AND dnptxop.t_code LIKE ('RC43413%') " +
          "     AND EXTRACT ( YEAR FROM NVL (dnptxop.t_operdate, TO_DATE ('01.01.0001', 'DD.MM.YYYY'))) = def.t_year " +
          "     AND dnptxop.t_client = def.t_client) rd " 
          ;
  cmd = RSDCommand(Query);
  cmd.addParam( "", RSDBP_IN, {curdate});  // T_OPERDATE
  cmd.execute; 
  rsd = TRsbDataSet(cmd);
  count = 0;
  var ReportFileName;
  var hour, min, sec;
  
  TimeSplit( Time(), hour, min, sec);
  ReportFileName = GetTxtFileName( "NDFL43413_" + hour + min + sec);
  SetOutput( ReportFileName, TRUE ); //Пишем лог в файл. Начало

  while(rsd.moveNext())
     if ( count == 0 ) 		 
        InitProgress( -1, "Генерация операций", "Генерация операций" );
     end;
     nptxop.operdate = rsd.t_operdate;
     nptxop.client = rsd.t_client;
     nptxop.contract = rsd.t_contract;
     nptxop.department = rsd.t_department;
     nptxop.iis = UNSET_CHAR;// не ИИС
     nptxop.account = IIF((valtype(rsd.t_account)==V_UNDEF), "", rsd.t_account);
     count = count + 1;
     NewNob = GetNOB(rsd.t_client,rsd.t_year);
     if((NewNob - rsd.t_Old_SumNob) > 0 )
       if(isFirst)
         println("┌───────────────────────────────┬─────────────────────┬───────────────────┬──────────────────────┬─────────────────────────┬──────────────┬──────────────────┬──────────────────┬──────────────────────────────────┐\n"+
                 "│          ФИО инвестора        │     Код инвестора   │  Налоговый период │Сумма НОБ до пересчета│Сумма НОБ после пересчета│ Сумма налога │Рассчитанный налог│ Остаток на счете │            Комментарий           │\n"+
                 "├───────────────────────────────┼─────────────────────┼───────────────────┼──────────────────────┼─────────────────────────┼──────────────┼──────────────────┼──────────────────┼──────────────────────────────────┤"
              );
         isFirst = false;
       end;

       insert_ndfl_oper( nptxop, @FirstNum, rsd.Name, rsd.Code, rsd.Rest, rsd.year, rsd.Old_SumNob, NewNob );
       FirstNum = FirstNum + 1;  // номера операций удержания НДФЛ
     end;
  end;
  if(not isFirst)
    println( "└───────────────────────────────┴─────────────────────┴───────────────────┴──────────────────────┴─────────────────────────┴──────────────┴──────────────────┴──────────────────┴──────────────────────────────────┘" );
  end;
  RemProgress(); 
  SetOutput( NULL, TRUE ); //Пишем лог в файл. Конец

  ViewFile( ReportFileName );
  return 0;
END;
execute();