/* 
$Name:        dlcontrmsg.mac
$Module:      Ядро Securities
$Description: Формирование сообщений ДБО
*/
import "nptxmfns_report.mac", "dlcontrmsg_mmvb.mac", "dlcontrmsg_spb.mac";

private macro GetIISCorrectNum(DlContrID) :integer
   var num:integer = 1, query, cmd, ds;

   query =  " SELECT Count(1) cnt " +
              " FROM ddlcontrmsg_dbt " +
             " WHERE t_DlContrID = ? " +
               " AND t_Kind = " + OBJTYPE_DLCONTRMSG_IISCORRECT;

   cmd = RsdCommand(query);
   cmd.NullConversion = true;
   cmd.AddParam("", RSDBP_IN, DlContrID);

   ds = TRsbDataSet(cmd);
   if(ds.MoveNext())
      num = ds.cnt;
   end;
   return num;
end;

/*private*/ macro GetFNSData(Kind, DlContr, SfContr)
   var mFNS = CNptxMesFnsData();

   if(Kind == OBJTYPE_DLCONTRMSG_IISOPEN)
      mFNS.EndDate = SfContr.rec.DateBegin;
      if(DlContr.rec.IISTransfer == SET_CHAR)
         mFNS.MessKind = MES_KIND_IISOPENMOVE; // Открытие ИИС с переводом активов
      else
         mFNS.MessKind = MES_KIND_IISOPEN; // Открытие ИИС
      end;
   elif(Kind == OBJTYPE_DLCONTRMSG_IISCLOSE)
      mFNS.EndDate = SfContr.rec.DateClose;
      if(DlContr.rec.IISCloseByTransf == SET_CHAR)
         mFNS.MessKind = MES_KIND_IISCLOSEMOVE; // Закрытие ИИС с переводом активов
      else
         mFNS.MessKind = MES_KIND_IISCLOSE; // Закрытие ИИС
      end;
   elif(Kind == OBJTYPE_DLCONTRMSG_IISCORRECT)
      mFNS.EndDate = SfContr.rec.DateBegin;
      mFNS.MessKind = MES_KIND_IISCORRECT; // Корректировка ИИС
      mFNS.CorrectNum = GetIISCorrectNum(DlContr.rec.DlContrID);
   end;

   mFNS.InvestorID = SfContr.rec.PartyID;
   mFNS.ContrID = SfContr.rec.ID;

/*ak
   var RefID;
   GenerateNumberByReference( 207, 5, @RefID, @mFNS.MessNum );*/

   var rs, cmd;
   SfContrID = mFNS.ContrID;
   PartyID = mFNS.InvestorID;
   /*Номер сообщения*/
   mFNS.MessNum = GetGuid();
   mFNS.MessNum = StrSubst ( mFNS.MessNum, StrFor(1), " " );
   /*Номер договора*/
   StartCaptureOutput();
   [ 
     select sfcontr.t_number t_number 
     from dsfcontr_dbt sfcontr 
     where sfcontr.t_id = ?
   ];
   cmd = RsdCommand(EndCaptureOutput());
   cmd.AddParam("sfcontrid", RSDBP_IN, mFNS.ContrID);
   rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
   if(rs.movenext())
     mFNS.ContrNum = rs.value("t_number");
   else
     mFNS.ContrNum = 0;
   end;
   /*Подписант*/
   StartCaptureOutput();
   [ 
     select officer.t_personid t_partyid
     from dofficer_dbt officer
     join dlegalproxy_dbt legalproxy on
         legalproxy.t_partyid = officer.t_partyid and
         legalproxy.t_proxyid = officer.t_personid and
         legalproxy.t_begindate <= ? and
         legalproxy.t_enddate >= ?
     left join dperson_dbt person on person.t_partyid = officer.t_personid 
     /*where officer.t_isfirstperson = chr(88) or officer.t_issecondperson = chr(88)*/
     /*order by case when person.t_oper = 1010 then 0 else 1 end*/
     ORDER BY CASE WHEN person.t_oper = 1010 AND (officer.t_isfirstperson = CHR (88) OR officer.t_issecondperson = CHR (88))  THEN 0
                   WHEN person.t_oper = 1010 AND (officer.t_isfirstperson != CHR (88) AND officer.t_issecondperson != CHR (88)) THEN 2
     ELSE 1 END     
   ];
   cmd = RsdCommand(EndCaptureOutput());
   //cmd.AddParam("dt1", RSDBP_IN, mFNS.EndDate);
   //cmd.AddParam("dt2", RSDBP_IN, mFNS.EndDate);
   cmd.AddParam("dt1", RSDBP_IN, date());//Chesnokov D.S. I-Support 503927, доверенность ищем на календарную дату.
   cmd.AddParam("dt2", RSDBP_IN, date());
   cmd.AddParam("sfcontrid", RSDBP_IN, mFNS.ContrID);
   rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
   if(rs.movenext())
     mFNS.SignerID = rs.value("t_partyid");
   else
     mFNS.SignerID = 0;
   end;
/*~ak*/

   return mFNS;
end;


private macro СоздатьСообщениеДБО_ФНС(Kind:integer, MarketID:integer, DlContr:TRecHandler, SfContr:TRecHandler) :integer
  var stat:integer = 0;
  var query, cmd, ds, fullPathName = "", erMsg = "";
  var dlcontrmsg = Tbfile("dlcontrmsg.dbt", "W", 0);

  erMsg = ПроверитьПутьДБО_ФНС();
  if(erMsg != "")
    stat = 1;
    msgErrAdd(V_DLCONTR_ERR_CODE,erMsg);
  end;

  if(stat == 0)
    if(Kind == OBJTYPE_DLCONTRMSG_IISCORRECT)
      stat = DL_ОчиститьНеотправленныеСообщения(Kind, DlContr.rec.DlContrID, MarketID);
    end;
  end;

  // Создать запись о сообщении ДБО
  if(stat == 0)
    dlcontrmsg.clear();
    dlcontrmsg.rec.DlContrID = DlContr.rec.DlContrID;
    dlcontrmsg.rec.Kind = Kind;
    dlcontrmsg.rec.CreateDate = {curdate};
    dlcontrmsg.rec.CreateTime = Time();
    dlcontrmsg.rec.IsOnlineSendMes = UNSET_CHAR;
    dlcontrmsg.rec.MarketID = MarketID;
    if(not dlcontrmsg.insert())
      stat = 1;
	  msgErrAdd(V_DLCONTR_ERR_CODE,"Непредвиденная ошибка при вставке записи в DDLCONTRMSG_DBT");
    end;
  end;

  if(stat == 0)
    fullPathName = NPTX_MFNS_Report_XML(GetFNSData(Kind, DlContr, SfContr)).Run();

    if(fullPathName == "")
      stat = 1;
      msgErrAdd(V_DLCONTR_ERR_CODE,"Для клиента \"" + ПолучитьКороткоеИмяСубъекта(SfContr.rec.PartyID) + "\" не удалось сформировать файл сообщения");
    end;
  end;

  if(stat == 0)
    if(not LoadImageObj(fullPathName, OBJTYPE_BSCMSG_DL, UniID(dlcontrmsg, OBJTYPE_BSCMSG_DL), 1));
      stat = 1;
      msgErrAdd(V_DLCONTR_ERR_CODE,"К записи о сообщении не удалось прикрепить файл сообщения");
    end;
  end;

  return stat;
end;

/*private*/ macro СоздатьСообщениеДБО(Kind:integer, MarketID:integer, DlContr:TRecHandler, SfContr:TRecHandler, MpID:integer) :integer
  var stat = 0;

  // Сообщение ФНС
  if((Kind == OBJTYPE_DLCONTRMSG_IISOPEN) or
     (Kind == OBJTYPE_DLCONTRMSG_IISCLOSE) or
     (Kind == OBJTYPE_DLCONTRMSG_IISCORRECT)
    )
    stat = СоздатьСообщениеДБО_ФНС(Kind, MarketID, DlContr, SfContr);

  elif(MarketID > 0)
    var CodeKind = ПолучитьВидКодаДБОДляБиржи(MarketID);

    if((CodeKind <= 0) or 
       ((CodeKind != DLCCK_SPB_BIDCODE) and (DL_GetDlObjCodeOnDate(OBJTYPE_BROKERCONTR_DL, CodeKind, DlContr.rec.DlContrID/*, SfContr.rec.DateBegin*/) == "")))
      stat = 1;
      msgErrAdd(V_DLCONTR_ERR_CODE,"Для клиента \"" + ПолучитьКороткоеИмяСубъекта(SfContr.rec.PartyID) + "\" на ДБО не задано значение единого биржевого кода");
    end;

    if(stat == 0)
      // Сообщение ММВБ
      if((CodeKind == DLCCK_USC) and
           ((Kind == OBJTYPE_DLCONTRMSG_MARKETREG) or
            (Kind == OBJTYPE_DLCONTRMSG_MPREG) or
            (Kind == OBJTYPE_DLCONTRMSG_MPCLOSE) or
            (Kind == OBJTYPE_DLCONTRMSG_MARKETCLOSE) or
            (Kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA))
          )
        stat = СоздатьСообщениеДБО_ММВБ(Kind, MarketID, DlContr, SfContr);

      // Сообщение СПБ
      elif((CodeKind == DLCCK_SPB_BIDCODE) and 
           ((Kind == OBJTYPE_DLCONTRMSG_BIDCODE) or
            (Kind == OBJTYPE_DLCONTRMSG_MPREG) or
            (Kind == OBJTYPE_DLCONTRMSG_MPCLOSE) or
            (Kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA) or
            (Kind == OBJTYPE_DLCONTRMSG_FATCAA) or
            (Kind == OBJTYPE_DLCONTRMSG_FATCAU) /*or
            (Kind == OBJTYPE_DLCONTRMSG_FATCAD) dan Отключил формирование сообщения FATCA при закрытии площадки pvcs 296009*/)
          )
        stat = СоздатьСообщениеДБО_СПБ(Kind, MarketID, DlContr, SfContr, null, MpID);
      end;
    end;
  end;


//очищается в фнукциях СоздатьСообщениеДБО_ММВБ и СоздатьСообщениеДБО_СПБ
//  if(stat == 0)
//    SQL_Execute("DELETE FROM DDLCONTRMSG_TMP");
//  end;

  return stat;
end;

macro СформироватьВсеСообщенияДБО(dlContrID:integer) :integer
  var stat:integer = 0, MmvbID:integer = MMVB_ID();
  var sql, cmd, ds, query4, cmd4, ds4;
  var dlcontr = TRecHandler("dlcontr.dbt"),
      sfcontr = TRecHandler("sfcontr.dbt"),
      party   = TRecHandler("party.dbt");

  if (DL_GetRecHandler(dlcontr, "SELECT DLCONTR.* FROM DDLCONTR_DBT DLCONTR WHERE DLCONTR.T_DLCONTRID = ?", dlContrID) and
      DL_GetRecHandler(sfcontr, "SELECT SFCONTR.* FROM DSFCONTR_DBT SFCONTR WHERE SFCONTR.T_ID = ?", dlcontr.rec.SfContrID))

      ПолучитьСубъекта(sfcontr.rec.partyID, party);
      sql = "SELECT DISTINCT GENMSG.* "+
             " FROM DDLCONTRGENMSG_TMP GENMSG " +
            " WHERE (CASE WHEN GENMSG.T_KIND = 2 AND GENMSG.T_MARKETID = ? THEN (CASE WHEN EXISTS(select 1 from DDLCONTRGENMSG_TMP WHERE T_KIND = 1 AND T_MARKETID = GENMSG.T_MARKETID) THEN 0 ELSE 1 END) " +
                        " WHEN GENMSG.T_KIND = 3 AND GENMSG.T_MARKETID = ? THEN (CASE WHEN EXISTS(select 1 from DDLCONTRGENMSG_TMP WHERE T_KIND = 4 AND T_MARKETID = GENMSG.T_MARKETID) THEN 0 ELSE 1 END) " +
                        " ELSE 1 END) = 1 "+
            " ORDER BY GENMSG.T_ID ";

      cmd = RSDCommand(sql);
      cmd.addParam("", RSDBP_IN, MmvbID);
      cmd.addParam("", RSDBP_IN, MmvbID);

      ds = TRsbDataSet(cmd);
      while(ds.moveNext() and (stat == 0))
        // BIQ-9185 
        if ((ds.Kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA) and (party.rec.legalForm == PTLEGF_PERSN))
          InstLoadModule("dbo_qi_func.mac");
          if (ExecMacro2("FindQIInclusionMessage", sfcontr.rec.partyid, {curdate})==1 )
            ExecMacro("InsEvent_QI", sfcontr.rec.partyid, 1); 
          end;
        end;

        if(((ds.Kind == OBJTYPE_DLCONTRMSG_MARKETREG) OR
            (ds.Kind == OBJTYPE_DLCONTRMSG_MPREG) OR
            (ds.Kind == OBJTYPE_DLCONTRMSG_MPCLOSE) OR
            (ds.Kind == OBJTYPE_DLCONTRMSG_MARKETCLOSE) OR
            (ds.Kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA) OR
            (ds.Kind == OBJTYPE_DLCONTRMSG_BIDCODE) or
            (ds.Kind == OBJTYPE_DLCONTRMSG_FATCAA) or
            (ds.Kind == OBJTYPE_DLCONTRMSG_FATCAU) or
            (ds.Kind == OBJTYPE_DLCONTRMSG_FATCAD)) AND
           (not ОтправлятьСообщенияНаБиржу()))
          continue;
        end;

        /*BPV чтобы не создавать по старым договорам*/
        query4 = cmd4 = ds4 = null;
        query4 = 
        " SELECT 1                                            " +
        "   FROM DDLCONTRMSG_DBT                              " +
        "  WHERE     DBMS_LOB.GETLENGTH (T_XML) = 1           " +
        "        AND T_DLCONTRID = " + dlcontrid                +
        "        AND T_KIND != 5                              " + /** Golovkin 29.10.2021 ID : 535112 */ 
        "        AND t_marketid = " + ds.MarketID; 

        cmd4 = DL_RSDCommand(query4);
        ds4  = cmd4.execute();
        if ((ds4.movenext() ) and (ds.kind == OBJTYPE_DLCONTRMSG_MARKETREG))
          continue;
        end;
        // DEF-43990 для "доверительный управляющий" не регистрируем на бирже
        query4 = cmd4 = ds4 = null;
        query4 = 
        " SELECT 1               " +
        "   FROM dpartyown_dbt   " +
        "  WHERE t_partyid =     " +sfcontr.rec.partyid +
        "    AND t_partykind = 65 "; 

        cmd4 = DL_RSDCommand(query4);
        ds4  = cmd4.execute();
        if ( ds4.movenext() ) 
          continue;
        end;

        /*>>*/
        stat = СоздатьСообщениеДБО(ds.Kind, ds.MarketID, dlcontr, sfcontr, ds.MpID);
      end;
  end;
  
  if(stat == 1)
   var cmd5, cnt:Integer;
   cmd5 = DL_RSDCommand( "select * from dlcontrerrmsg_tmp" );
   cnt = cmd5.GetCount();
   if(cnt ==0)
     msgErrAdd(V_DLCONTR_ERR_CODE,"Непредвиденная ошибка")
   end;
   stat = V_DLCONTR_ERR_CODE; 
  end;

  return stat;
OnError(er)
  return DL_IIF(er.Code != 0, er.Code, 1);
end;


// Повторно добавить сообщение в очередь для отправки
macro DlRepeatSendMes(dlContrMsgID) :integer
  var dlcontrmsg = Tbfile("dlcontrmsg", "W", 0);
  var stat:integer = 0;

  dlContrMsg.KeyNum = 0;
  dlContrMsg.rec.ID = dlContrMsgID;
  if(not GetEQ(dlContrMsg))
    stat = 1;
    MsgBox("Не удалось найти сообщение с ID = " + dlContrMsgID);
  end;

  if(stat == 0)
    var CodeKind = ПолучитьВидКодаДБОДляБиржи(dlContrMsg.rec.MarketID);

    if((CodeKind == DLCCK_USC) and (dlContrMsg.rec.IsOnlineSendMes == SET_CHAR)) // ММВБ
      stat = DlRepeatSendMes_MMVB(dlContrMsg);
    elif((CodeKind == DLCCK_SPB_BIDCODE) and (dlContrMsg.rec.ID > 0)) // СПБ dan   
      //stat = DlRepeatSendMes_SPB(dlContrMsg);

      //stat = СоздатьНовоеСообщ(OBJTYPE_DLCONTRMSG_FATCAA, dlContr, sfContr, dlContrMp);
      var dlContrF = TRecHandler("dlcontr.dbt"), sfContrF = TRecHandler("sfcontr.dbt"), dlContrMpF = TRecHandler("dlcontrmp.dbt");
      var queryF = " SELECT DLCONTR.* FROM DDLCONTR_DBT DLCONTR WHERE DLCONTR.T_DLCONTRID = " + dlContrMsg.rec.DlContrID;
      var cmdF = DL_RSDCommand(queryF);
      var dsF = cmdF.Execute();
      while(dsF.moveNext())
         dlcontrF.Clear();
         dsF.GetRecord().CopyTo( dlcontrF.rec);
         queryF = " SELECT DLCONTRMP.* FROM DDLCONTRMP_DBT DLCONTRMP WHERE DLCONTRMP.T_DLCONTRID = " + dlContrMsg.rec.DlContrID + " AND DLCONTRMP.T_MARKETID = " + dlContrMsg.rec.MarketID;
         cmdF = DL_RSDCommand(queryF);
         dsF = cmdF.Execute();
         while(dsF.moveNext())
            dlcontrMpF.Clear();
            dsF.GetRecord().CopyTo( dlcontrMpF.rec);   
            queryF = " SELECT DSFCONTR.* FROM DSFCONTR_DBT DSFCONTR WHERE DSFCONTR.T_ID = " + dlContrMpF.rec.SFContrID ;
            cmdF = DL_RSDCommand(queryF);
            dsF = cmdF.Execute();
            while(dsF.moveNext())
               sfContrF.Clear();
               dsF.GetRecord().CopyTo( sfContrF.rec);   
            end;
         end;
      end;
      if(dlContrMsg.rec.SendMesState == DLSENDMESSTATE_CONFIRM)
          MsgBox("Данное сообщение уже успешно подтверждено биржей.");
      else
        var msgdt = TRsbDataSet("select t.t_id from ddlcontrmsg_dbt t where t.t_kind in (" + dlContrMsg.rec.Kind  + ") and t.t_sendmesstate = 305 and t.t_dlcontrid = " + dlContrMsg.rec.dlcontrid + " and not exists (select 1 from ddlcontrmsg_dbt t1 where t1.t_dlcontrid = t.t_dlcontrid and t1.t_kind in (" + dlContrMsg.rec.Kind  + ") and t1.t_sendmesstate in (300,302) ) ");  
        if(msgdt.movenext)
          stat = DL_AddDlContrGenMes(dlContrMsg.rec.Kind, dlContrMpF.rec.ID, dlContrMpF.rec.MarketID);
          if(stat == 0)
            stat = СоздатьСообщениеДБО_СПБ(dlContrMsg.rec.Kind, dlContrMpF.rec.MarketID, DlContrF, SfContrF, null, dlContrMpF.rec.ID);
          end;
        else
          var ListName = GetLLVALname (1143/*OBJTYPE_DLCONTRMSG_VALUES*/, dlContrMsg.rec.Kind);
          msgbox("Повторное формирование невозможно.\n Повторное формирование "+ ListName + " невозможно при наличии для ДБО неподтвержденных сообщений.");
        end;
      end;
      SQL_Execute("DELETE FROM DDLCONTRGENMSG_TMP");
    end;
  end;

  return stat;
end;

// Печать результата отправленного сообщения 
macro DlPrintSendMesResp(dlContrMsgAddr)
   var dlContrMsg = TRecHandler("dlcontrmsg.dbt");
   SetBuff(dlContrMsg, dlContrMsgAddr);
   var respData = DL_GetClobData("t_RespData", " from DDLCONTRMSG_DBT where t_ID = "+Int(dlContrMsg.rec.ID));
   PrintSendMesResp(dlContrMsg.rec.Id, respData);
end;

// Печать отправленного сообщения 
macro DlPrintSendMes(dlContrMsgAddr)
   var dlContrMsg = TRecHandler("dlcontrmsg.dbt");
   SetBuff(dlContrMsg, dlContrMsgAddr);
   var respData = DL_GetClobData("t_XML", " from DDLCONTRMSG_DBT where t_ID = "+Int(dlContrMsg.rec.ID));
   PrintSendMesResp(dlContrMsg.rec.Id, respData);
end;