/**
 @file      dlvaprds.mac
 @brief     Функции получения сумм ПДД для УВ

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |07.04.2025 |Велигжанин А.В.|DEF-85905                                       |поправка СущественностьОтклонения()
 |           |               |                                                |функция возвращает true, если вызов функции проверки существенности был успешным,
 |           |               |                                                |а не тогда, когда проверка показывает несущественность отклонения,
 |           |               |                                                |существенность/не существенность возвращается в параметре
*/
import cb_sql, CTInter, VSInter, DealsInter, PaymInter, BankInter, globals, OprInter, "dlquery.mac", "dlcnst.inc";
import RsbDataSet, FIInter, dlmisc;

//собираемые параметры для  VACollectIncome
CLASS VACollectPrm(_BCID, _Disc, _Perc, _Quality, _AccPerc, _AccDisc, _Bonus, _AccBonus, _AccBalOver, _AccBonusOver, _AccPercOver, _SumEIR, _AccSumEir, _WrittenBonus, _WrittenAccBonus)
  var BCID, Disc, Perc, Quality, AccPerc, AccDisc, Bonus, AccBonus, AccBalOver, AccBonusOver, AccPercOver, SumEIR, AccSumEir, WrittenBonus, WrittenAccBonus;

  BCID    = _BCID;
  Disc    = _Disc;
  Perc    = _Perc;
  Quality = _Quality;
  AccPerc = _AccPerc;
  AccDisc = _AccDisc;
  Bonus   = _Bonus;
  AccBonus= _AccBonus;

  AccBalOver   = _AccBalOver;
  AccBonusOver = _AccBonusOver;

  AccPercOver  = _AccPercOver;

  SumEIR    = _SumEIR;
  AccSumEir = _AccSumEir;

  WrittenBonus = _WrittenBonus;
  WrittenAccBonus = _WrittenAccBonus;
END;

//класс для сбора начисленных процентов и дисконта по векселям.
//необходим, чтобы запомнить суммы, когда на одном шаге с доначислением выполняемся перевод или списание.
//так как суммы в базу будут вставлены только после выполнения шага, то необходимо помнить и прибавлять при списании доначисленные суммы
CLASS VACollectIncome(_Дата)
  var Дата = _Дата;
  var prm = TArray();
  prm.size = 0;

  PRIVATE MACRO newInc(BCID, Disc, Perc, Quality, AccPerc, AccDisc, Bonus, AccBonus, AccBalOver, AccBonusOver, AccPercOver, SumEIR, AccSumEir, WrittenBonus, WrittenAccBonus)
    prm[prm.size] = VACollectPrm(BCID, Disc, Perc, Quality, AccPerc, AccDisc, Bonus, AccBonus, AccBalOver, AccBonusOver, AccPercOver, SumEIR, AccSumEir, WrittenBonus, WrittenAccBonus);
  END;

  PRIVATE MACRO find(BCID)
    var i = 0;
    while (i < prm.size)
      if(prm[i].BCID == BCID)
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  END;

  MACRO add (BCID, Disc, Perc, Quality, AccPerc, AccDisc, Bonus, AccBonus, AccBalOver, AccBonusOver, AccPercOver, SumEIR, AccSumEir, WrittenBonus, WrittenAccBonus)
     var i = find(BCID);

     if(ValType(AccBalOver) == V_UNDEF)
       AccBalOver = $0;
     end;

     if(ValType(AccBonusOver) == V_UNDEF)
       AccBonusOver = $0;
     end;

     if(ValType(AccPercOver) == V_UNDEF)
       AccPercOver = $0;
     end;

     if(ValType(SumEIR) == V_UNDEF)
       SumEIR = $0;
     end;

     if(ValType(AccSumEir) == V_UNDEF)
       AccSumEir = $0;
     end;

     if(i > -1)
       prm[i].Perc     = prm[i].Perc + Perc;
       prm[i].Disc     = prm[i].Disc + Disc;
       prm[i].AccPerc  = prm[i].AccPerc + AccPerc;
       prm[i].AccDisc  = prm[i].AccDisc + AccDisc;
       prm[i].Bonus    = prm[i].Bonus + Bonus;
       prm[i].AccBonus = prm[i].AccBonus + AccBonus;
       prm[i].AccBalOver  = prm[i].AccBalOver + AccBalOver;
       prm[i].AccBonusOver= prm[i].AccBonusOver + AccBonusOver;
       prm[i].AccPercOver = prm[i].AccPercOver + AccPercOver;
       prm[i].SumEIR      = prm[i].SumEIR + SumEIR;
       prm[i].AccSumEir   = prm[i].AccSumEir + AccSumEir;
       prm[i].WrittenBonus = prm[i].WrittenBonus + WrittenBonus;
       prm[i].WrittenAccBonus = prm[i].WrittenAccBonus + WrittenAccBonus;
       
       if((prm[i].Quality == UNSET_CHAR) and (Quality == SET_CHAR))
         prm[i].Quality = Quality;
       end;
     else
       newInc(BCID, Disc, Perc, Quality, AccPerc, AccDisc, Bonus, AccBonus, AccBalOver, AccBonusOver, AccPercOver, SumEIR, AccSumEir, WrittenBonus, WrittenAccBonus);
     end;
  END;

  MACRO GetIncSumm(BCID, FiRole)
    var i = find(BCID);
    var Summ = 0.0;
    if(i > -1)
      if(FiRole == FIROLE_PERCENT)
        Summ = prm[i].Perc;
      elif(FiRole == FIROLE_DISCOUNT)
        Summ = prm[i].Disc;
      elif(FiRole == FIROLE_BONUS)
        Summ = prm[i].Bonus;
      elif(FiRole == FIROLE_WRITTENBONUS)
        Summ = prm[i].WrittenBonus;
      end;
    end;
    return Summ;
  END;

  MACRO GetIncSummAcc(BCID, FiRole)
    var i = find(BCID);
    var Summ = 0.0;
    if(i > -1)
      if(FiRole == FIROLE_PERCENT)
        Summ = prm[i].AccPerc;
      elif(FiRole == FIROLE_DISCOUNT)
        Summ = prm[i].AccDisc;
      elif(FiRole == FIROLE_BONUS)
        Summ = prm[i].AccBonus;
      elif(FiRole == FIROLE_WRITTENBONUS)
        Summ = prm[i].WrittenAccBonus;
      end;
    end;
    return Summ;
  END;

  MACRO GetIncSummBalOverAcc(BCID)
    var i = find(BCID);
    var Summ = 0.0;
    if(i > -1)
      Summ = prm[i].AccBalOver;
    end;
    return Summ;
  END;

  MACRO GetIncSummBonusOverAcc(BCID)
    var i = find(BCID);
    var Summ = 0.0;
    if(i > -1)
      Summ = prm[i].AccBonusOver;
    end;
    return Summ;
  END;

  MACRO GetIncSummPercOverAcc(BCID)
    var i = find(BCID);
    var Summ = 0.0;
    if(i > -1)
      Summ = prm[i].AccPercOver;
    end;
    return Summ;
  END;

  MACRO GetIncQuality(BCID)
    var i = find(BCID);
    var Quality = -1;
    if(i > -1)
      Quality = prm[i].Quality;
    end;
    return Quality;
  END;

  MACRO GetSumEIR(BCID)
    var i = find(BCID);
    var Summ = 0.0;
    if(i > -1)
      Summ = prm[i].SumEIR;
    end;
    return Summ;
  END;
  
  MACRO GetSumEIRAcc(BCID)
    var i = find(BCID);
    var Summ = 0.0;
    if(i > -1)
      Summ = prm[i].AccSumEIR;
    end;
    return Summ;
  END;

END;

CLASS VAPrDsParm(_IncGroup:@VACollectIncome, _ID_Operation:integer, _ID_Step:integer, _NeedWriteBonus:bool)
  var IncGroup:VACollectIncome = _IncGroup;
  var ID_Operation:integer = _ID_Operation;
  var ID_Step:integer = _ID_Step;
  var NeedWriteBonus:bool = _NeedWriteBonus;
END;

MACRO VA_GetLastBalanceDate(BCID, Дата)

  var query, DataSet, RSD = DL_RSDCommand();
  var BalancaDate = date(0,0,0);

  if(ValType(Дата) == V_UNDEF)
    Дата = date(0,0,0);
  end;

  query = "select rsb_bill.GetVABnrLastBalanceDate(?, ?) as LastChangeDate from dual ";

  RSD.AddParam(BCID);  /*1*/
  RSD.AddParam(Дата);  /*2*/

  DataSet = RSD.Execute(query);

  if((DataSet.moveNext()) and (ValType(DataSet.LastChangeDate) != V_UNDEF) and (DataSet.LastChangeDate > date(0,0,0)))
    BalancaDate = date(DataSet.LastChangeDate);
  end;

  return BalancaDate;

END;

MACRO VA_GetLastAccountedEnrolmentID(BCID, ChangeDate, BalanceDealID, BalanceDocKind)
  var Enrolment_ID = 0;
  var query, DataSet;
  var Дата = date(0,0,0);
  var RSD = DL_RSDCommand();

  if((ValType(ChangeDate) == V_UNDEF) or (ChangeDate == date(0,0,0)))
    Дата = VA_GetLastBalanceDate(BCID);
  else
    Дата = ChangeDate;
  end;

  if(ValType(BalanceDealID) == V_UNDEF)
    BalanceDealID = 0;
  end;

  if(ValType(BalanceDocKind) == V_UNDEF)
    BalanceDocKind = 0;
  end;

  query = "select rsb_bill.GetVABnrLastAccountedEnrolID(?, ?, ?, ?) as ID from dual ";
  RSD.AddParam(BCID); /*1*/
  RSD.AddParam(Дата); /*2*/
  RSD.AddParam(BalanceDealID);
  RSD.AddParam(BalanceDocKind);

  DataSet = RSD.Execute(query);

  if(DataSet.moveNext())
    Enrolment_ID = DataSet.ID;
  end;

  return int(Enrolment_ID);
END;

MACRO VS_GetVSBnrLastPayedDate(BCID, Дата)

  var query, DataSet, RSD = DL_RSDCommand();
  var BalancaDate = date(0,0,0);

  if(ValType(Дата) == V_UNDEF)
    Дата = date(0,0,0);
  end;

  query = "select rsb_bill.GetVSBnrLastPayedDate(?, ?) as LastChangeDate from dual ";

  RSD.AddParam(BCID);  /*1*/
  RSD.AddParam(Дата);  /*2*/

  DataSet = RSD.Execute(query);

  if((DataSet.moveNext()) and (ValType(DataSet.LastChangeDate) != V_UNDEF) and (DataSet.LastChangeDate > date(0,0,0)))
    BalancaDate = date(DataSet.LastChangeDate);
  end;

  return BalancaDate;

END;

MACRO VS_GetLastPayedEnrolmentID(BCID, ChangeDate, BalanceDealID, BalanceDocKind)
  var Enrolment_ID = 0;
  var query, DataSet;
  var Дата = date(0,0,0);
  var RSD = DL_RSDCommand();

  if((ValType(ChangeDate) == V_UNDEF) or (ChangeDate == date(0,0,0)))
    Дата = VS_GetVSBnrLastPayedDate(BCID);
  else
    Дата = ChangeDate;
  end;

  if(ValType(BalanceDealID) == V_UNDEF)
    BalanceDealID = 0;
  end;

  if(ValType(BalanceDocKind) == V_UNDEF)
    BalanceDocKind = 0;
  end;

  query = "select rsb_bill.GetVSBnrLastPayedEnrolID(?, ?, ?, ?) as ID from dual ";
  RSD.AddParam(BCID); /*1*/
  RSD.AddParam(Дата); /*2*/
  RSD.AddParam(BalanceDocKind);
  RSD.AddParam(BalanceDealID);

  DataSet = RSD.Execute(query);

  if(DataSet.moveNext())
    Enrolment_ID = DataSet.ID;
  end;

  return int(Enrolment_ID);
END;

//получить надежность предыдущего начисления
MACRO НадежностьПредыдущегоНачисления(BCID)
  var income;
  var Надежность = true; //по умолчанию, считаем, что предыдущее начисление было надежным

  //найдем последнюю запись в dvsincome_dbt
  var Select = DL_RSDCommand(  " select t_Quality from dvsincome_dbt"
                             + "  where t_BCID = ? /*1*/ "
                             + "    and t_Enrolment_ID = ? /*2*/ "
                             + "    and t_IncomeType = " + VSINCOMETYPE_CHARGE
                             + "  order by t_EndDate desc" );

  Select.AddParam(BCID); /*1*/
  Select.AddParam(VA_GetLastAccountedEnrolmentID(BCID)); /*2*/
  income = Select.Execute();

  if(income.moveNext())
    if(income.Quality == "")
      Надежность = false;
    end;
  end;

  return Надежность;
END;

//Универсальная функция получения неотнесенного дохода
PRIVATE MACRO ПолучитьСуммуПДДНеотнесУнив(BCID, FIRole, Дата)
  var query, income;
  var Сумма = 0.0, Сумма1 = 0.0;
  var CurrEnrolID = 0;
  var RSD = DL_RSDCommand();

  CurrEnrolID = VA_GetLastAccountedEnrolmentID(BCID);

  if(FIRole == FIROLE_PERCENT)
    query =   " select inc.t_Perc ";
  elif(FIRole == FIROLE_DISCOUNT)
    query =   " select inc.t_Disc ";
  else
    query =   " select (inc.t_Perc+inc.t_Disc) ";
  end;

  query =  query + "  as Summ, inc.t_Quality, inc.t_IncomeType "
          + " from dvsincome_dbt inc"
          + " where inc.t_BCID = ? /*1*/ "
          + "   and inc.t_Enrolment_ID = ? /*2*/ "
          + "   and inc.t_IncomeType = ? /*3*/ ";

  RSD.AddParam(BCID);         /*1*/
  RSD.AddParam(CurrEnrolID);  /*2*/
  RSD.AddParam(VSINCOMETYPE_CHARGE);  /*3*/

  if((ValType(Дата) != V_UNDEF) and (Дата > date(0,0,0)))
    query = query + " and inc.t_EndDate <= ? /*4*/ ";
    RSD.AddParam(date(Дата));  /*4*/
  end;

  query = query + " order by inc.t_EndDate desc";

  income = RSD.Execute(query);

  while((income.moveNext()) and ((income.Quality == "") or (income.IncomeType != VSINCOMETYPE_CHARGE)) and (ValType(income.Summ) != V_UNDEF))
    if(income.IncomeType == VSINCOMETYPE_CHARGE) //начисление ПДД
      Сумма = Сумма + income.Summ;
    elif(income.Quality == "")
      Сумма1 = Сумма1 + income.Summ;
    end;
  end;

  if(Сумма)
    Сумма = Сумма + Сумма1;
  end;

  return Сумма;

END;

//получить сумму ПДД (на период обращения)
MACRO ПолучитьСуммуПДД(BCID, Enrolment_ID, FIRole, Дата)
  var query, DataSet;
  var Сумма = 0.0;
  var CurrEnrolID = 0;
  var RSD = DL_RSDCommand();

  if((ValType(Enrolment_ID) == V_UNDEF) or (Enrolment_ID == 0))
    CurrEnrolID = VA_GetLastAccountedEnrolmentID(BCID);
  else
    CurrEnrolID = Enrolment_ID;
  end;

  if(FIRole == FIROLE_PERCENT)
    query =   " select sum(inc.t_Perc) ";
  elif(FIRole == FIROLE_DISCOUNT)
    query =   " select sum(inc.t_Disc) ";
  elif(FIRole == FIROLE_BONUS)
    query =   " select sum(inc.t_Bonus) ";
  else
    query =   " select sum(inc.t_Perc+inc.t_Disc+inc.t_Bonus) ";
  end;

  query =  query + " as Summ"
                 + " from dvsincome_dbt inc "
                 + " where inc.t_BCID = ? /*1*/ AND inc.t_Enrolment_ID = ? /*2*/ AND inc.t_IncomeType IN (?, ?)";

  RSD.AddParam(BCID);         /*1*/
  RSD.AddParam(CurrEnrolID);  /*2*/
  RSD.AddParam(VSINCOMETYPE_CHARGE);  /*3*/
  RSD.AddParam(VSINCOMETYPE_OVERVALUE);  /*4*/

  if((ValType(Дата) != V_UNDEF) and (Дата > date(0,0,0)))
    query =  query + " and inc.t_EndDate <= ? /*3*/";
    RSD.AddParam(date(Дата));  /*3*/
  end;

  DataSet = RSD.Execute(query);

  if((DataSet.moveNext()) and (ValType(DataSet.Summ) != V_UNDEF))
    Сумма = DataSet.Summ;
  end;

  return Сумма;

END;

//Универсальная функция получения отнесенного дохода
PRIVATE MACRO ПолучитьСуммуПДДДоходУнив(BCID, FIRole)
  var query, DataSet;
  var Сумма = 0.0;
  var CurrEnrolID = 0;
  var RSD = DL_RSDCommand();

  CurrEnrolID = VA_GetLastAccountedEnrolmentID(BCID);

  //измнчально считаем, что у нас вся сумма на доходе
  Сумма = ПолучитьСуммуПДД(BCID, CurrEnrolID, FIRole);
  if(НадежностьПредыдущегоНачисления(BCID) == false)
    //последний раз мы начисляли по ненадежной категории, но до этого часть суммы могла быть на доходе
    Сумма = Сумма - ПолучитьСуммуПДДНеотнесУнив(BCID, FIRole);
  end;

  return Сумма;
END;

MACRO ПолучитьСуммуПДДНаДоходе(BCID, Сумма:@variant)

  Сумма = ПолучитьСуммуПДДДоходУнив(BCID, 0);
END;

//сумма дисконта, отнесенного на доход
MACRO ПолучитьСуммуДисконтаНаДоходе(BCID)

  return ПолучитьСуммуПДДДоходУнив(BCID, FIROLE_DISCOUNT);
END;

//сумма процентов, отнесенных на доход
MACRO ПолучитьСуммуПроцентовНаДоходе(BCID)

  return ПолучитьСуммуПДДДоходУнив(BCID, FIROLE_PERCENT);
END;

MACRO ПолучитьСуммуНачисленнойПремии(BCID) //премия не бывает на дохоже или неотнесенная - она всегда вся

  return ПолучитьСуммуПДД(BCID, null, FIROLE_BONUS);
END;

MACRO ПолучитьСуммуНеотнесенногоДохода(BCID, Дата)

  return ПолучитьСуммуПДДНеотнесУнив(BCID, 0, Дата);
END;

MACRO ПолучитьСуммуНеотнесенныхПроцентов(BCID)

  return ПолучитьСуммуПДДНеотнесУнив(BCID, FIROLE_PERCENT);
END;

MACRO ПолучитьСуммуНеотнесенногоДисконта(BCID)

  return ПолучитьСуммуПДДНеотнесУнив(BCID, FIROLE_DISCOUNT);
END;

//сумма начисленного ПДД на дату
MACRO ПолучитьСуммуПДДНаДату(BCID, Дата, FIRole, BalanceDate, BalanceDealID, BalanceDocKind)
  var Сумма = 0.0;

  if(ValType(BalanceDate) == V_UNDEF)
    BalanceDate = VA_GetLastBalanceDate(BCID, Дата);
  end;

  Сумма = ПолучитьСуммуПДД(BCID, VA_GetLastAccountedEnrolmentID(BCID, BalanceDate, BalanceDealID, BalanceDocKind), FIRole, Дата);

  return Сумма;
END;

//сумма за период
MACRO ПолучитьСуммуПДДЗаПериод(BCID, DateB, DateE, FIRole, BalanceDate)
  var SumE = 0.0, SumB = 0.0;
  var CurrEnrolID = 0;

  if(ValType(BalanceDate) == V_UNDEF)
    BalanceDate = VA_GetLastBalanceDate(BCID, DateE);
  end;

  CurrEnrolID = VA_GetLastAccountedEnrolmentID(BCID, BalanceDate);

  //только в рамках текущего CurrEnrolID
  SumE = ПолучитьСуммуПДД(BCID, CurrEnrolID, FIRole, date(DateB-1));
  SumB = ПолучитьСуммуПДД(BCID, CurrEnrolID, FIRole, DateE);

  return SumB-SumE;
END;

//Получить дату последнего начисления процентов по векселю
MACRO ПолучитьДатуПоследПДДнаДату(BCID, Дата, BalanceDate)
  var query, DataSet;
  var ДатаПДД = date(0,0,0);
  var CurrEnrolID = 0;
  var RSD = DL_RSDCommand();

  CurrEnrolID = VA_GetLastAccountedEnrolmentID(BCID, BalanceDate);

  query =   " select max(inc.t_EndDate) as EndDate "
          + " from dvsincome_dbt inc "
          + " where inc.t_BCID = ? /*1*/ AND inc.t_Enrolment_ID = ? /*2*/ AND inc.t_IncomeType = ? /*3*/";

  RSD.AddParam(BCID);         /*1*/
  RSD.AddParam(CurrEnrolID);  /*2*/
  RSD.AddParam(VSINCOMETYPE_CHARGE);  /*3*/

  if((ValType(Дата) != V_UNDEF) and (Дата > date(0,0,0)))
    query =  query + " and inc.t_EndDate <= ? /*4*/";
    RSD.AddParam(date(Дата));  /*4*/
  end;

  DataSet = RSD.Execute(query);

  if(DataSet.moveNext())
    if(ValType(DataSet.EndDate) != V_UNDEF)
      ДатаПДД = date(DataSet.EndDate);
    end;
  end;

  return ДатаПДД;
END;

//Получить сумму ПДД, начисленную на шаге операции
MACRO ПолучитьСуммуПДДШага(BCID, FIRole, ID_Operation, ID_Step)
  var Сумма = 0.0;
  var query, DataSet;
  var RSD = DL_RSDCommand();

  if(FIRole == FIROLE_PERCENT)
    query =   " select sum(inc.t_Perc) ";
  elif(FIRole == FIROLE_DISCOUNT)
    query =   " select sum(inc.t_Disc) ";
  elif(FIRole == FIROLE_BONUS)
    query =   " select sum(inc.t_Bonus) ";
  else
    query =   " select sum(inc.t_Perc+inc.t_Disc+inc.t_Bonus) ";
  end;

  query = query + " as Summ "
                + "  from dvsincome_dbt inc, doprdocs_dbt od "
                + " where od.t_ID_Operation = ? /*1*/ "
                + "   and od.t_ID_Step = ? /*2*/ "
                + "   and od.t_DocKind = 4702 " //начисление ПДД
                + "   and inc.t_AutoKey = TO_NUMBER(od.t_DocumentID) "
                + "   and inc.t_BCID = ? /*3*/ "
                + "   and inc.t_IncomeType IN (?, ?)";

  RSD.AddParam(ID_Operation);  /*1*/
  RSD.AddParam(ID_Step);      /*2*/
  RSD.AddParam(BCID);         /*3*/
  RSD.AddParam(VSINCOMETYPE_CHARGE);    /*4*/
  RSD.AddParam(VSINCOMETYPE_OVERVALUE); /*5*/

  DataSet = RSD.Execute(query);

  if((DataSet.moveNext()) and (ValType(DataSet.Summ) != V_UNDEF))
    Сумма = DataSet.Summ;
  end;

  return Сумма;
END;

MACRO ПолучитьСуммуПремииНеОтнесеннойНаРасходы(BCID, Дата, Enrolment_ID)
  var Сумма = 0.0;
  var query, DataSet;
  var RSD = DL_RSDCommand();

  query =   " select NVL(sum(inc.t_Perc), 0) as Summ"
          + " from dvsincome_dbt inc "
          + " where inc.t_BCID = ? /*1*/ AND inc.t_Enrolment_ID = ? /*2*/ AND inc.t_IncomeType = ?";

  RSD.AddParam(BCID);         /*1*/
  RSD.AddParam(Enrolment_ID);  /*2*/
  RSD.AddParam(VSINCOMETYPE_WRITENBONUS);  /*3*/

  if((ValType(Дата) != V_UNDEF) and (Дата > date(0,0,0)))
    query =  query + " and inc.t_EndDate <= ? /*3*/";
    RSD.AddParam(date(Дата));  /*3*/
  end;

  DataSet = RSD.Execute(query);

  if(DataSet.moveNext())
    Сумма = ПолучитьСуммуПДД(BCID, Enrolment_ID, FIROLE_BONUS, Дата) - DataSet.Summ;
  end;

  return Сумма;
END;

MACRO ПолучитьСуммуПремииОтнесеннуюНаРасходы(BCID, Дата, Enrolment_ID)
  var Сумма = 0.0;
  var query, DataSet;
  var RSD = DL_RSDCommand();
  var CurrEnrolID;

  if((ValType(Enrolment_ID) == V_UNDEF) or (Enrolment_ID == 0))
    CurrEnrolID = VA_GetLastAccountedEnrolmentID(BCID);
  else
    CurrEnrolID = Enrolment_ID;
  end;

  query =   " select NVL(sum(inc.t_Perc), 0) as Summ"
          + " from dvsincome_dbt inc "
          + " where inc.t_BCID = ? /*1*/ AND inc.t_Enrolment_ID = ? /*2*/ AND inc.t_IncomeType = ?";

  RSD.AddParam(BCID);         /*1*/
  RSD.AddParam(CurrEnrolID);  /*2*/
  RSD.AddParam(VSINCOMETYPE_WRITENBONUS);  /*3*/

  if((ValType(Дата) != V_UNDEF) and (Дата > date(0,0,0)))
    query =  query + " and inc.t_EndDate <= ? /*3*/";
    RSD.AddParam(date(Дата));  /*3*/
  end;

  DataSet = RSD.Execute(query);

  if(DataSet.moveNext())
    Сумма = DataSet.Summ;
  end;

  return Сумма;
END;

//////////////////////////////////////////////////////////////////////
//Функции получения сумм ПДД в валюте учета
//////////////////////////////////////////////////////////////////////

//Универсальная функция получения неотнесенного дохода
PRIVATE MACRO ПолучитьСуммуПДДНеотнесУнивВУ(BCID, FIRole, Дата)
  var query, income;
  var Сумма = 0.0, Сумма1 = 0.0;
  var CurrEnrolID = 0;
  var RSD = DL_RSDCommand();

  CurrEnrolID = VA_GetLastAccountedEnrolmentID(BCID);

  if(FIRole == FIROLE_PERCENT)
    query =   " select (inc.t_AccPerc) ";
  elif(FIRole == FIROLE_DISCOUNT)
    query =   " select inc.t_AccDisc ";
  else
    query =   " select (inc.t_AccPerc+inc.t_AccDisc) ";
  end;

  query =  query + "  as Summ, inc.t_Quality, inc.t_IncomeType"
          + " from dvsincome_dbt inc"
          + " where inc.t_BCID = ? /*1*/ "
          + "   and inc.t_Enrolment_ID = ? /*2*/ "
          + "   and inc.t_IncomeType IN (? /*3*/, ? /*4*/) ";

  RSD.AddParam(BCID);         /*1*/
  RSD.AddParam(CurrEnrolID);  /*2*/
  RSD.AddParam(VSINCOMETYPE_CHARGE);    /*3*/
  RSD.AddParam(VSINCOMETYPE_OVERVALUE); /*4*/

  if((ValType(Дата) != V_UNDEF) and (Дата > date(0,0,0)))
    query = query + " and inc.t_EndDate <= ? /*5*/";
    RSD.AddParam(date(Дата));  /*3*/
  end;

  query = query + " order by inc.t_EndDate desc";

  income = RSD.Execute(query);

  while((income.moveNext()) and ((income.Quality == "") or (income.IncomeType != VSINCOMETYPE_CHARGE)) and (ValType(income.Summ) != V_UNDEF))
    if(income.IncomeType == VSINCOMETYPE_CHARGE) //начисление ПДД
      Сумма = Сумма + income.Summ;
    elif(income.Quality == "")
      Сумма1 = Сумма1 + income.Summ;
    end;
  end;

  if(Сумма)
    Сумма = Сумма + Сумма1;
  end;

  return Сумма;

END;

//получить сумму ПДД (на период обращения)
MACRO ПолучитьСуммуПДДВУ(BCID, Enrolment_ID, FIRole, Дата)
  var query, DataSet;
  var Сумма = 0.0;
  var CurrEnrolID = 0;
  var RSD = DL_RSDCommand();

  if((ValType(Enrolment_ID) == V_UNDEF) or (Enrolment_ID == 0))
    CurrEnrolID = VA_GetLastAccountedEnrolmentID(BCID);
  else
    CurrEnrolID = Enrolment_ID;
  end;

  if(FIRole == FIROLE_PERCENT)
    query =   " select sum(inc.t_AccPerc) ";
  elif(FIRole == FIROLE_DISCOUNT)
    query =   " select sum(inc.t_AccDisc) ";
  elif(FIRole == FIROLE_BONUS)
    query =   " select sum(inc.t_AccBonus) ";
  else
    query =   " select sum(inc.t_AccPerc+inc.t_AccDisc+inc.t_AccBonus) ";
  end;

  query =  query + " as Summ"
                 + " from dvsincome_dbt inc "
                 + " where inc.t_BCID = ? /*1*/ AND inc.t_Enrolment_ID = ? /*2*/"
                 + "   and inc.t_IncomeType IN (? /*3*/, ? /*4*/)";

  RSD.AddParam(BCID);         /*1*/
  RSD.AddParam(CurrEnrolID);  /*2*/
  RSD.AddParam(VSINCOMETYPE_CHARGE);    /*3*/
  RSD.AddParam(VSINCOMETYPE_OVERVALUE); /*4*/

  if((ValType(Дата) != V_UNDEF) and (Дата > date(0,0,0)))
    query =  query + " and inc.t_EndDate <= ? /*5*/ ";
    RSD.AddParam(date(Дата));  /*3*/
  end;

  DataSet = RSD.Execute(query);

  if((DataSet.moveNext()) and (ValType(DataSet.Summ) != V_UNDEF))
    Сумма = DataSet.Summ;
  end;

  return Сумма;

END;

PRIVATE MACRO ПолучитьСуммуПДДДоходУнивВУ(BCID, FIRole)
  var query, DataSet;
  var Сумма = 0.0;
  var CurrEnrolID = 0;
  var RSD = DL_RSDCommand();

  CurrEnrolID = VA_GetLastAccountedEnrolmentID(BCID);

  //измнчально считаем, что у нас вся сумма на доходе
  Сумма = ПолучитьСуммуПДДВУ(BCID, CurrEnrolID, FIRole);
  if(НадежностьПредыдущегоНачисления(BCID) == false)
    //последний раз мы начисляли по ненадежной категории, но до этого часть суммы могла быть на доходе
    Сумма = Сумма - ПолучитьСуммуПДДНеотнесУнивВУ(BCID, FIRole);
  end;

  return Сумма;

END;

MACRO ПолучитьСуммуПДДНаДоходеВУ(BCID, Сумма:@variant)

  Сумма = ПолучитьСуммуПДДДоходУнивВУ(BCID, 0);
END;

//сумма дисконта, отнесенного на доход
MACRO ПолучитьСуммуДисконтаНаДоходеВУ(BCID)

  return ПолучитьСуммуПДДДоходУнивВУ(BCID, FIROLE_DISCOUNT);
END;

//сумма процентов, отнесенных на доход
MACRO ПолучитьСуммуПроцентовНаДоходеВУ(BCID)

  return ПолучитьСуммуПДДДоходУнивВУ(BCID, FIROLE_PERCENT);
END;

MACRO ПолучитьСуммуНачисленнойПремииВУ(BCID)

  return ПолучитьСуммуПДДВУ(BCID, null, FIROLE_BONUS);
END;

MACRO ПолучитьСуммуНеотнесенногоДоходаВУ(BCID, Дата)

  return ПолучитьСуммуПДДНеотнесУнивВУ(BCID, 0, Дата);
END;

MACRO ПолучитьСуммуНеотнесенныхПроцентовВУ(BCID)

  return ПолучитьСуммуПДДНеотнесУнивВУ(BCID, FIROLE_PERCENT);
END;

MACRO ПолучитьСуммуНеотнесенногоДисконтаВУ(BCID)

  return ПолучитьСуммуПДДНеотнесУнивВУ(BCID, FIROLE_DISCOUNT);
END;

//сумма начисленного ПДД на дату
MACRO ПолучитьСуммуПДДНаДатуВУ(BCID, Дата, FIRole, BalanceDate, BalanceDealID, BalanceDocKind)
  var Сумма = 0.0;

  if(ValType(BalanceDate) == V_UNDEF)
    BalanceDate = VA_GetLastBalanceDate(BCID, Дата);
  end;

  Сумма = ПолучитьСуммуПДДВУ(BCID, VA_GetLastAccountedEnrolmentID(BCID, BalanceDate, BalanceDealID, BalanceDocKind), FIRole, Дата);

  return Сумма;
END;

//сумма за период
MACRO ПолучитьСуммуПДДЗаПериодВУ(BCID, DateB, DateE, FIRole, BalanceDate)
  var SumE = 0.0, SumB = 0.0;
  var CurrEnrolID = 0;

  if(ValType(BalanceDate) == V_UNDEF)
    BalanceDate = VA_GetLastBalanceDate(BCID, DateE);
  end;

  CurrEnrolID = VA_GetLastAccountedEnrolmentID(BCID, BalanceDate);

  //только в рамках текущего CurrEnrolID
  SumE = ПолучитьСуммуПДДВУ(BCID, CurrEnrolID, FIRole, date(DateB-1));
  SumB = ПолучитьСуммуПДДВУ(BCID, CurrEnrolID, FIRole, DateE);

  return SumB-SumE;
END;

//Получить сумму ПДД, начисленную на шаге операции
MACRO ПолучитьСуммуПДДШагаВУ(BCID, FIRole, ID_Operation, ID_Step)
  var Сумма = 0.0;
  var query, DataSet;
  var RSD = DL_RSDCommand();

  if(FIRole == FIROLE_PERCENT)
    query =   " select sum(inc.t_AccPerc) ";
  elif(FIRole == FIROLE_DISCOUNT)
    query =   " select sum(inc.t_AccDisc) ";
  elif(FIRole == FIROLE_BONUS)
    query =   " select sum(inc.t_AccBonus) ";
  else
    query =   " select sum(inc.t_AccPerc+inc.t_AccDisc+inc.t_AccBonus) ";
  end;

  query = query + " as Summ "
                + "  from dvsincome_dbt inc, doprdocs_dbt od "
                + " where od.t_ID_Operation = ? /*1*/ "
                + "   and od.t_ID_Step = ? /*2*/ "
                + "   and od.t_DocKind = 4702 " //начисление ПДД
                + "   and inc.t_AutoKey = TO_NUMBER(od.t_DocumentID) "
                + "   and inc.t_BCID = ? /*3*/ "
                + "   and inc.t_IncomeType IN (?, ?)";

  RSD.AddParam(ID_Operation);  /*1*/
  RSD.AddParam(ID_Step);      /*2*/
  RSD.AddParam(BCID);         /*3*/
  RSD.AddParam(VSINCOMETYPE_CHARGE);    /*4*/
  RSD.AddParam(VSINCOMETYPE_OVERVALUE); /*5*/

  DataSet = RSD.Execute(query);

  if((DataSet.moveNext()) and (ValType(DataSet.Summ) != V_UNDEF))
    Сумма = DataSet.Summ;
  end;

  return Сумма;
END;

MACRO ПолучитьСуммуПремииНеОтнесеннойНаРасходыВУ(BCID, Дата, Enrolment_ID)
  var Сумма = 0.0;
  var query, DataSet;
  var RSD = DL_RSDCommand();

  query =   " select NVL(sum(inc.t_AccPerc), 0) as Summ"
          + " from dvsincome_dbt inc "
          + " where inc.t_BCID = ? /*1*/ AND inc.t_Enrolment_ID = ? /*2*/ AND inc.t_IncomeType = ?";

  RSD.AddParam(BCID);         /*1*/
  RSD.AddParam(Enrolment_ID);  /*2*/
  RSD.AddParam(VSINCOMETYPE_WRITENBONUS);  /*3*/

  if((ValType(Дата) != V_UNDEF) and (Дата > date(0,0,0)))
    query =  query + " and inc.t_EndDate <= ? /*3*/";
    RSD.AddParam(date(Дата));  /*3*/
  end;

  DataSet = RSD.Execute(query);

  if(DataSet.moveNext())
    Сумма = ПолучитьСуммуПДДВУ(BCID, Enrolment_ID, FIROLE_BONUS, Дата) - DataSet.Summ;
  end;

  return Сумма;
END;

MACRO ПолучитьСуммуПремииОтнесеннуюНаРасходыВУ(BCID, Дата, Enrolment_ID)
  var Сумма = 0.0;
  var query, DataSet;
  var RSD = DL_RSDCommand();
  var CurrEnrolID;

  if((ValType(Enrolment_ID) == V_UNDEF) or (Enrolment_ID == 0))
    CurrEnrolID = VA_GetLastAccountedEnrolmentID(BCID);
  else
    CurrEnrolID = Enrolment_ID;
  end;

  query =   " select NVL(sum(inc.t_AccPerc), 0) as Summ"
          + " from dvsincome_dbt inc "
          + " where inc.t_BCID = ? /*1*/ AND inc.t_Enrolment_ID = ? /*2*/ AND inc.t_IncomeType = ?";

  RSD.AddParam(BCID);         /*1*/
  RSD.AddParam(CurrEnrolID);  /*2*/
  RSD.AddParam(VSINCOMETYPE_WRITENBONUS);  /*3*/

  if((ValType(Дата) != V_UNDEF) and (Дата > date(0,0,0)))
    query =  query + " and inc.t_EndDate <= ? /*3*/";
    RSD.AddParam(date(Дата));  /*3*/
  end;

  DataSet = RSD.Execute(query);

  if(DataSet.moveNext())
    Сумма = DataSet.Summ;
  end;

  return Сумма;
END;

//получить дату последней переоценки векселя
MACRO ДатаПоследнейПереоценкиВекселя(BCID, Дата)
  var Select = DL_RSDCommand(), DataSet, query;
  var OverDate = date(0,0,0);

  query =   " select max(t_EndDate) as EndDate from dvsincome_dbt "
          + "  where t_BCID = ? /*1*/ "
          + "    and t_Enrolment_ID = ? /*2*/ "
          + "    and t_IncomeType = " + VSINCOMETYPE_OVERVALUE;

  Select.AddParam(BCID); /*1*/
  Select.AddParam(VA_GetLastAccountedEnrolmentID(BCID)); /*2*/

  if(ValType(Дата) == V_DATE)
    query = query + " and t_EndDate <= ? /*3*/ ";
    Select.AddParam(Дата); /*3*/
  end;

  DataSet = Select.Execute(query);
  if(DataSet.moveNext())
    if(ValType(DataSet.EndDate) != V_UNDEF)
      OverDate = date(DataSet.EndDate);
    end;
  end;

  return OverDate;
END;

//Получить сумму ПДД, начисленную на шаге операции
MACRO ПолучитьСуммуПереоценкиШагаВУ(BCID, FIRole, ID_Operation, ID_Step)
  var Сумма = 0.0;
  var query, DataSet;
  var RSD = DL_RSDCommand();

  if(FIRole == FIROLE_PERCENT)
    query =   " select sum(inc.t_AccPerc) ";
  elif(FIRole == FIROLE_DISCOUNT)
    query =   " select sum(inc.t_AccDisc) ";
  elif(FIRole == FIROLE_BONUS)
    query =   " select sum(inc.t_AccBonus) ";
  else
    query =   " select sum(inc.t_AccPerc+inc.t_AccDisc+inc.t_AccBonus) ";
  end;

  query = query + " as Summ "
                + "  from dvsincome_dbt inc, doprdocs_dbt od "
                + " where od.t_ID_Operation = ? /*1*/ "
                + "   and od.t_ID_Step = ? /*2*/ "
                + "   and od.t_DocKind = 4702 " //начисление ПДД
                + "   and inc.t_AutoKey = TO_NUMBER(od.t_DocumentID) "
                + "   and inc.t_BCID = ? /*3*/ "
                + "   and inc.t_IncomeType = ?";

  RSD.AddParam(ID_Operation);  /*1*/
  RSD.AddParam(ID_Step);      /*2*/
  RSD.AddParam(BCID);         /*3*/
  RSD.AddParam(VSINCOMETYPE_OVERVALUE); /*4*/

  DataSet = RSD.Execute(query);

  if((DataSet.moveNext()) and (ValType(DataSet.Summ) != V_UNDEF))
    Сумма = DataSet.Summ;
  end;

  return Сумма;
END;

// Получить дату постановки векселя на баланс, ID сделки, цену векселя и валюту цены по этой сделке
//    (дату поставки из посл. покупки),
// не раньше BoundDate
MACRO DL_VA_GetBalanceDate(BCID, BoundDate, BalanceDate:@date, DealID:@variant, Cost:@money, CostFI:@variant, DealType:@variant, DealCode:@variant, ClientContrID, SetDealID, BOfficeKind:@variant, ClientID, SaleID )
  var stat = false;
  var cmd;

  BalanceDate = date(0,0,0);
  Cost = $0.0;

  if(ValType(BoundDate) == V_UNDEF)
    BoundDate = {curdate};
  end;

  if(ValType(SetDealID) == V_UNDEF)
    SetDealID = 0;
  end;

  if(ValType(ClientID) == V_UNDEF)
    ClientID = -1;
  end;

  if(ValType(SaleID) == V_UNDEF)
    SaleID = -1;
  end;

  cmd = RsdCommand("begin\n rsb_bill.GetVABnrBalancePrm(?,?,?,?,?,?,?,?,?,?,?,?,?);\n end; ");

  cmd.addParam("p_BCID",          RSDBP_IN, BCID          );
  cmd.addParam("p_BoundDate",     RSDBP_IN, BoundDate     );
  cmd.addParam("p_ClientContrID", RSDBP_IN, ClientContrID );
  cmd.addParam("p_SetDealID",     RSDBP_IN, SetDealID     );

  cmd.addParam("p_BalanceDate",   RSDBP_OUT, V_DATE    );
  cmd.addParam("p_DealID",        RSDBP_OUT, V_INTEGER );
  cmd.addParam("p_Cost",          RSDBP_OUT, V_DOUBLE  );
  cmd.addParam("p_CostFI",        RSDBP_OUT, V_INTEGER );
  cmd.addParam("p_DealType",      RSDBP_OUT, V_INTEGER );
  cmd.addParam("p_DealCode",      RSDBP_OUT, V_STRING  );
  cmd.addParam("p_BOfficeKind",   RSDBP_OUT, V_INTEGER );

  cmd.addParam("p_ClientID",   RSDBP_IN, ClientID );
  cmd.addParam("p_SaleID",     RSDBP_IN, SaleID );

  cmd.execute();

  BalanceDate = SQL_ConvTypeDate(cmd.value("p_BalanceDate"));
  DealID      = SQL_ConvTypeInteger(cmd.value("p_DealID"));
  Cost        = SQL_ConvTypeSum(cmd.value("p_Cost"));
  CostFI      = SQL_ConvTypeInteger(cmd.value("p_CostFI"));
  DealType    = SQL_ConvTypeInteger(cmd.value("p_DealType"));
  DealCode    = SQL_ConvTypeDate(cmd.value("p_DealCode"));
  BOfficeKind = SQL_ConvTypeInteger(cmd.value("p_BOfficeKind"));

  if(DealID > 0)
    stat = true;
  end;

  return stat;
END;

MACRO DL_IsOurBanner(IssuerID)
  var DataSet, Select;

  Select = DL_RSDCommand("SELECT t_PartyID FROM ddp_dep_dbt WHERE t_PartyID = ?");
  Select.AddParam(IssuerID);
  DataSet = Select.execute();

  return DataSet.MoveNext();
END;

/* Определить плановую дату погашения
*/
MACRO DL_VS_GetPlanExpryDate(bnr, leg)
  VAR prd = date(0,0,0),
      ttype = ValType(bnr),
      BCTermFormula,
      Maturity,
      Expiry;

  if(ttype == V_GENOBJ)
     BCTermFormula = bnr.rec.BCTermFormula;
     Maturity      = leg.rec.Maturity;
     Expiry        = leg.rec.Expiry;
  else
     BCTermFormula = bnr.BCTermFormula;
     Maturity      = leg.Maturity;
     Expiry        = leg.Expiry;
  end;

  if((BCTermFormula == VS_TERMF_FIXEDDAY) or (BCTermFormula == VS_TERMF_INATIME))
    prd = Expiry;
  elif( (BCTermFormula == VS_TERMF_ATSIGHT) or (BCTermFormula == VS_TERMF_DURING) )
    prd = Maturity;
  end;

  return prd;
END;

/* Получить цену последней продажи векселя
*/
MACRO DL_VS_GetLastBnrSalePrice( BCID:Integer, OnDate:Date, LastSalePrice:@Money, LastSaleFI:@Integer )
  var sql = "", cmd = null, rsd = null;

  sql = " SELECT "
          + " lnk.t_BCCost "
         + " ,lnk.t_BCCFI "
      + " FROM "
          + " dvsbnrbck_dbt bck "
         + " ,doprdocs_dbt docs "
         + " ,dvsordlnk_dbt lnk "
         + " ,doproper_dbt oper "
      + " WHERE "
          + " lnk.t_DocKind = oper.t_DocKind "
      + " AND LPAD(lnk.t_ContractID, 10, '0') = oper.t_DocumentID "
      + " AND lnk.t_BCID = ? "
      + " AND oper.t_DocKind IN (" + DL_VEKSELORDER + ", " + DL_VSBARTERORDER + ", " + DL_VSSALE + ") "
      + " AND oper.t_ID_Operation = docs.t_ID_Operation "
      + " AND docs.t_DocKind = " + DL_VSBNRBCK + " "
      + " AND docs.t_DocumentID = LPAD(bck.t_ID, 10, '0') "
      + " AND bck.t_BCID = ? ";

  if( OnDate != ZeroDate )
    sql = sql +
        " AND bck.t_ChangeDate <= ? ";
  end;

  sql = sql +
        " AND bck.t_BCStatus = 'X' "
      + " AND bck.t_NewABCStatus = " + VSBANNER_STATUS_SENDED + " "
      + " ORDER BY "
          + " bck.t_ID DESC ";

  cmd = RSDCommand( sql );
  cmd.NullConversion = true;
  cmd.AddParam( "", RSDBP_IN, BCID );
  cmd.AddParam( "", RSDBP_IN, BCID );
  if( OnDate != ZeroDate )
    cmd.AddParam( "", RSDBP_IN, OnDate );
  end;
  cmd.Execute();

  rsd = TRsbDataSet(cmd);
  if( rsd.MoveNext() )
    LastSalePrice = SQL_ConvTypeSum( rsd.BCCost );
    LastSaleFI = SQL_ConvTypeInteger( rsd.BCCFI );
  else
    LastSalePrice = $0;
    LastSaleFI = ALLFININSTR;
  end;
END;

/* Получить сумму последнего размещения векселя,
   актуально для дисконтного векселя
*/
MACRO DL_VS_GetReceiptAmount( leg, BCCFI:@variant )
  var ReceiptAmount = $0,
      sReceiptAmount,
      PFI,
      DealID,
      Start_Date = ZeroDate,
      sql, cmd, rsd,
      ttype = ValType( leg );

  var bnr = TBFile( "vsbanner.dbt" );
  var isOurBan = true;

  if( ttype == V_GENOBJ )
    sReceiptAmount = leg.rec.ReceiptAmount;
    DealID         = leg.rec.DealID;
    PFI            = leg.rec.PFI;
  else
    sReceiptAmount = leg.ReceiptAmount;
    DealID         = leg.DealID;
    PFI            = leg.PFI;
  end;

  bnr.rec.BCID = DealID;
  if( bnr.GetEQ() )
    isOurBan = DL_IsOurBanner( bnr.rec.Issuer );
  end;

  if( isOurBan )
    DL_VS_GetLastBnrSalePrice( DealID, ZeroDate, @ReceiptAmount, @BCCFI );
  else
    sql = " SELECT "
            + " subq.t_BCCost "
           + " ,subq.t_BCCFI "
           + " ,subq.t_Start_Date "
        + " FROM "
            + " (SELECT "
                 + " lnk.t_BCCost "
                + " ,lnk.t_BCCFI "
                + " ,oper.t_Start_Date "
             + " FROM "
                 + " doproper_dbt oper "
                + " ,doprkoper_dbt koper "
                + " ,ddl_tick_dbt tick "
                + " ,dvsordlnk_dbt lnk "
             + " WHERE "
                 + " oper.t_DocKind IN (" + DL_VEKSELACCOUNTED + ", " + DL_VATRUST + ", " + DL_VAENWR + ") "
             + " AND oper.t_DocumentID = LPAD(tick.t_DealID, 34, '0') "
             + " AND tick.t_DealID = lnk.t_ContractID "
             + " AND koper.t_Kind_Operation = tick.t_DealType "
             + " AND (INSTR(koper.t_SysTypes, 'B') != 0 OR INSTR(koper.t_SysTypes, 'X') != 0) "
             + " AND lnk.t_BCID = ? "
             + " AND lnk.t_LinkKind = " + VSORDLNK_K_BUY + " "
             + " ORDER BY "
                 + " oper.t_Start_Date DESC "
                + " ,tick.t_DealID DESC) subq "
        + " WHERE ROWNUM = 1 ";

    cmd = RSDCommand( sql );
    cmd.NullConversion = true;
    cmd.AddParam( "", RSDBP_IN, DealID );
    cmd.Execute();

    rsd = TRsbDataSet( cmd );
    if( rsd.MoveNext() )
      ReceiptAmount = SQL_ConvTypeSum( rsd.BCCost );
      Start_Date = SQL_ConvTypeDate( rsd.Start_Date );
      BCCFI = SQL_ConvTypeInteger( rsd.BCCFI );
    end;

    //возможно были операции ВВК
    sql = " SELECT "
            + " subq.t_BCCost "
           + " ,subq.t_BCCFI "
           + " ,subq.t_Start_Date "
        + " FROM "
            + " (SELECT "
                 + " lnk.t_BCCost "
                + " ,lnk.t_BCCFI "
                + " ,tick.t_DealDate as t_Start_Date "
             + " FROM "
                 + " dvsordlnk_dbt lnk "
                + " ,doprstep_dbt ostep "
                + " ,ddl_tick_dbt tick "
             + " WHERE "
                 + " ostep.t_ServDocKind = " + TS_DOC_DECLAR + " "
             + " AND tick.t_RequestID = ostep.t_ServDocID "
             + " AND lnk.t_ContractID = tick.t_DealID "
             + " AND lnk.t_DocKind = tick.t_BOfficeKind "
             + " AND lnk.t_BCID = ? "
             + " AND lnk.t_LinkKind = " + VSORDLNK_K_BUY + " "
             + " ORDER BY "
                 + " tick.t_DealDate DESC "
                + " ,tick.t_DealID DESC) subq "
        + " WHERE ROWNUM = 1 ";

    cmd = RSDCommand( sql );
    cmd.NullConversion = true;
    cmd.AddParam( "", RSDBP_IN, DealID );
    cmd.Execute();

    rsd = TRsbDataSet( cmd );
    if( rsd.MoveNext() and ( SQL_ConvTypeDate( rsd.Start_Date ) > Start_Date ) )
      ReceiptAmount = SQL_ConvTypeSum( rsd.BCCost );
      start_date = SQL_ConvTypeDate( rsd.start_date );
      BCCFI = SQL_ConvTypeInteger( rsd.BCCFI );
    end;
  end;

  if( ReceiptAmount == $0 )
    ReceiptAmount = sReceiptAmount;
    BCCFI = PFI;
  end;

  return ReceiptAmount;
END;

/* Получить дату повторного размещения (продажи) векселя
*/
MACRO DL_VS_GetStartDate(bnr, leg)
  var sStart = ZeroDate,
      Start = ZeroDate,
      BCID,
      sql,
      rsd,
      ttype = ValType(leg), isOurBan = true;

  if(ttype == V_GENOBJ)
     BCID   = leg.rec.DealID;
     sStart = leg.rec.Start;
  else
     BCID   = leg.DealID;
     sStart = leg.Start;
  end;

  if(ValType(bnr) == V_UNDEF)
    bnr = TBFile("vsbanner.dbt");
    bnr.rec.BCID = BCID;
    bnr.GetEQ();
  end;

  if(ValType(bnr) == V_GENOBJ)
     isOurBan = DL_IsOurBanner(bnr.rec.Issuer);
  else
     isOurBan = DL_IsOurBanner(bnr.Issuer);
  end;

  if(isOurBan)
    sql = " SELECT lnk.t_InterestChargeDate t_Start_Date " +
          "   FROM dvsordlnk_dbt lnk, " +
          "        doproper_dbt op, " +
          "        doprdocs_dbt opd, " +
          "        (  SELECT LPAD (hist.t_ID, 10, '0') t_ID " +
          "             FROM dvsbnrbck_dbt hist " +
          "            WHERE hist.t_BCID = " + BCID +
          "              AND hist.t_BCStatus = 'X' " +
          "              AND hist.t_NewABCStatus = 20 " +
          "         ORDER BY hist.t_ChangeDate DESC, hist.t_ID DESC) tmp " +
          "  WHERE ROWNUM = 1 " +
          "    AND opd.t_DOCKind = " + DL_VSBNRBCK +
          "    AND opd.t_DocumentID = tmp.t_ID " +
          "    AND op.t_ID_Operation = opd.t_ID_Operation " +
          "    AND op.t_DocKind IN ( " + DL_VSSALE + ", " + DL_VSBARTERORDER + ") " +
          "    AND LPAD (lnk.t_contractid, 10, '0') = op.t_DocumentID " +
          "    AND lnk.t_DocKind = op.t_DocKind " +
          "    AND lnk.t_BCID = " + BCID;

        rsd = TRsbDataSet(sql);
        if( rsd.MoveNext() )
          Start = date(rsd.Start_Date);
        end;
    //end;
  else
    Start = date(VA_GetLastBalanceDate(BCID));
  end;

  if( Start == date(0,0,0) )
    Start = sStart;
  end;

  return Start;
END;

//Сумма, которая находится на счете "Учтенные векселя", но в ВН
macro БалансоваяСтоимостьВекселяВН(BCID, OnDate)
  var BalanceCost = $0;
  var Select, DataSet;

  Select = DL_RSDCommand("select cast(rsb_bill.GetVABnrAccountedCostPFI(?, ?) as NUMBER(32,12)) as BalanceCost from dual");
  Select.AddParam(BCID);
  Select.AddParam(OnDate);

  DataSet = Select.Execute();
  if(DataSet.moveNext())
    BalanceCost = DataSet.BalanceCost;
  end;

  return BalanceCost;
end;

//Сумма, которая находится на счете "Учтенные векселя"
macro БалансоваяСтоимостьВекселяВУ(BCID, OnDate)
  var BalanceCost = $0;
  var Select, DataSet;

  Select = DL_RSDCommand("select cast(rsb_bill.GetVABnrAccountedCostAcc(?, ?) as NUMBER(32,12)) as BalanceCost from dual");
  Select.AddParam(BCID);
  Select.AddParam(OnDate);

  DataSet = Select.Execute();
  if(DataSet.moveNext())
    BalanceCost = DataSet.BalanceCost;
  end;

  return BalanceCost;
end;

//Сумма, которую заплатили за вексель в ВР, переведенная в ВН на дату поставки, если поставка позже оплаты
//или цена в ВЦ, переведенная в ВН на дату поставки, если поставка раньше оплаты
macro СтоимостьВекселяВН(BCID, OnDate)
  var BalanceCost = $0;
  var Select, DataSet;

  Select = DL_RSDCommand("select cast(rsb_bill.GetVABnrCostPFI(?, ?) as NUMBER(32,12)) as BalanceCost from dual");
  Select.AddParam(BCID);
  Select.AddParam(OnDate);

  DataSet = Select.Execute();
  if(DataSet.moveNext())
    BalanceCost = DataSet.BalanceCost;
  end;

  return BalanceCost;
end;

/* Получить сумму начального дисконта
*/

MACRO УВ_ПолучитьСуммуНачальногоДисконта( bnr, leg, Дата )
  var N, R, Днач, Principal, BalanceDate, BCID;
  var Series = "", Number = "";

  if(ValType(leg) == V_GENOBJ)
    Principal = leg.rec.Principal;
    BCID      = leg.rec.DealID;
  else
    Principal = leg.Principal;
    BCID      = leg.DealID;
  end;

  if(ValType(bnr) == V_GENOBJ)
    Series = bnr.rec.BCSeries;
    Number = bnr.rec.BCNumber;
  else
    Series = bnr.BCSeries;
    Number = bnr.BCNumber;
  end;

  BalanceDate = VA_GetLastBalanceDate(BCID, Дата);

  N = Principal; /* номинал  */
  R = СтоимостьВекселяВН(BCID, BalanceDate);

  if (ValType(R) == V_UNDEF)
    RunError("Не удалость найти стоимость векселя cер. " + Series + " N " + Trim(Number) + " на дату " + BalanceDate);
  else
    Днач = N - R;
  end;

  return Днач;
END;

/* ИСТИНа, если вексель дисконтный
*/
MACRO DL_VS_IsVsDiscount(leg, bnr)
var
   Ok = false,
   PFI, Principal, Formula,
   BCCFI, BCCost, BCID, ttype = ValType(leg), isOurBan = true;

   if(ttype == V_GENOBJ)
      PFI       = leg.rec.PFI;
      Principal = leg.rec.Principal;
      Formula   = leg.rec.Formula;
      BCID      = leg.rec.DealID;
   else
      PFI       = leg.PFI;
      Principal = leg.Principal;
      Formula   = leg.Formula;
      BCID      = leg.DealID;
   end;

   if(ValType(bnr) == V_UNDEF)
     bnr = TBFile("vsbanner.dbt");
     bnr.rec.BCID = BCID;
     bnr.GetEQ();
   end;

   if(ValType(bnr) == V_GENOBJ)
      isOurBan = DL_IsOurBanner(bnr.rec.Issuer);
   else
      isOurBan = DL_IsOurBanner(bnr.Issuer);
   end;

   if((Formula == VS_IN_DISCONT) OR (Formula == VS_IN_DISC_PC)) // дисконтный или процентно-дисконтный
     if(isOurBan)
       if(ttype != V_INTEGER)
         BCCost = DL_VS_GetReceiptAmount(leg, @BCCFI);
         if((BCCFI == PFI) OR (ConvSum(BCCost, BCCost, DL_VS_GetStartDate(bnr, leg), BCCFI, PFI, 0) == 0))
           if( BCCost < Principal )
             Ok = true;
           end;
         end;
       end;
     else
       if(УВ_ПолучитьСуммуНачальногоДисконта(bnr, leg, DL_VS_GetStartDate(bnr, leg)) > $0)
         Ok = true;
       end;
     end;
   end;

   return Ok;
END;

/* ИСТИНа, если вексель с начальной премией
*/
MACRO DL_VS_IsVsBonus(leg, bnr)
var
   Ok = false,
   PFI, Principal, Formula,
   BCCFI, BCCost, BCID, ttype = ValType(leg), isOurBan = true;

   if(ttype == V_GENOBJ)
      PFI       = leg.rec.PFI;
      Principal = leg.rec.Principal;
      Formula   = leg.rec.Formula;
      BCID      = leg.rec.DealID;
   else
      PFI       = leg.PFI;
      Principal = leg.Principal;
      Formula   = leg.Formula;
      BCID      = leg.DealID;
   end;

   if(ValType(bnr) == V_UNDEF)
     bnr = TBFile("vsbanner.dbt");
     bnr.rec.BCID = BCID;
     bnr.GetEQ();
   end;

   if(ValType(bnr) == V_GENOBJ)
      isOurBan = DL_IsOurBanner(bnr.rec.Issuer);
   else
      isOurBan = DL_IsOurBanner(bnr.Issuer);
   end;
   if(isOurBan)
     if(ttype != V_INTEGER)
       BCCost = DL_VS_GetReceiptAmount(leg, @BCCFI);
       if((BCCFI == PFI) OR (ConvSum(BCCost, BCCost, DL_VS_GetStartDate(bnr, leg), BCCFI, PFI, 0) == 0))
         if( BCCost > Principal )
           Ok = true;
         end;
       end;
     end;
   else
     if(УВ_ПолучитьСуммуНачальногоДисконта(bnr, leg, DL_VS_GetStartDate(bnr, leg)) < $0)
       Ok = true;
     end;
   end;

   return Ok;
END;

MACRO DL_GetBnrPlanRepayDate(bnr, leg)
  var PlanRepayDate = ZeroDate;
  var BCTermFormula;
  var BCFormKind;
  var Start;
  var Maturity;
  var Expiry;
  var Basis;
  var Diff;

  if(ValType(bnr) == V_GENOBJ)
     BCTermFormula = bnr.rec.BCTermFormula;
     BCFormKind    = bnr.rec.BCFormKind;
     Start         = leg.rec.Start;
     Maturity      = leg.rec.Maturity;
     Expiry        = leg.rec.Expiry;
     Basis         = leg.rec.Basis;
     Diff          = leg.rec.Diff;
  else
     BCTermFormula = bnr.BCTermFormula;
     BCFormKind    = bnr.BCFormKind;
     Start         = leg.Start;
     Maturity      = leg.Maturity;
     Expiry        = leg.Expiry;
     Basis         = leg.Basis;
     Diff          = leg.Diff;
  end;

  if(BCFormKind == VSBANNER_FORMKIND_SIMPLE) //вексель
    if(BCTermFormula == VS_TERMF_FIXEDDAY)
      //для векселя на определенный день  - дата погашения, указанная в векселе
      PlanRepayDate = Maturity;
    elif(BCTermFormula == VS_TERMF_INATIME)
      //для векселя <во столько-то времени от составления> - дата погашения, указанная в векселе
      PlanRepayDate = Maturity;
    elif(BCTermFormula == VS_TERMF_DURING)
      //для векселей "во столько-то времени от предъявления" полный срок обращения равен 1 году + количество дней, определяемое формулировкой "во столько-то"
      PlanRepayDate = Start + Diff + DaysInYearByBasis(Basis, Start, true);
    elif(BCTermFormula == VS_TERMF_ATSIGHT)
      if((Maturity >= Start) AND (Expiry >= Start))
        //для векселя <по предъявлении, но не ранее и не позднее> - дата <не позднее>,
        PlanRepayDate = Expiry;
      elif(Maturity >= Start)
        //для векселя <по предъявлении, но не ранее> - дата <не ранее> + 1 год
        PlanRepayDate = Maturity + DaysInYearByBasis(Basis, Maturity, true);
      elif(Expiry >= Start)
        //для векселя <по предъявлении, но не позднее> - дата <не позднее>,
        PlanRepayDate = Expiry;
      else
        //для векселей со сроком "по предъявлении" полный срок обращения равен 1 году (согласно базису расчета) от даты составления
        PlanRepayDate = Start + DaysInYearByBasis(Basis, Start, true);
      end;
    end;
  else //депозитный сертификат
    //депозитных сертификатов - дата востребования
    PlanRepayDate = Maturity;
  end;

  return PlanRepayDate;
END;

/* Получить срок обращения векселя
*/
MACRO DL_VS_GetTermCircDate(bnr, leg, P:integer)
  var T = 0;
  var ttype = ValType(bnr);
  var isOurBan = true;
  var err = 0, DiscKind = 0;
  var Start:date, Maturity:date, Expiry:date;
  var BCTermFormula;
  var Basis;
  var BnrStartDate = DL_VS_GetStartDate(bnr, leg);
  var BnrBackOffice;
  var BnrBCFormKind;
  var BCPresentationDate;
  var Diff = 0;
  var day, month, year;

  if(ttype == V_GENOBJ)
     isOurBan = DL_IsOurBanner(bnr.rec.Issuer);
     BCTermFormula = bnr.rec.BCTermFormula;
     BnrBackOffice = bnr.rec.BackOffice;
     BnrBCFormKind = bnr.rec.BCFormKind;
     BCPresentationDate = bnr.rec.BCPresentationDate;
     Start    = leg.rec.Start;
     Maturity = leg.rec.Maturity;
     Expiry   = leg.rec.Expiry;
     Basis    = leg.rec.Basis;
     Diff     = leg.rec.Diff;
  else
     isOurBan = DL_IsOurBanner(bnr.Issuer);
     BCTermFormula = bnr.BCTermFormula;
     BnrBackOffice = bnr.BackOffice;
     BnrBCFormKind = bnr.BCFormKind;
     BCPresentationDate = bnr.BCPresentationDate;
     Start    = leg.Start;
     Maturity = leg.Maturity;
     Expiry   = leg.Expiry;
     Basis    = leg.Basis;
     Diff     = leg.Diff;
  end;

  GetRegistryValue( "ДОВЕРИТЕЛЬНОЕ УПРАВЛЕНИЕ\\РАБОТА С НЕЭМИССИОННЫМИ ЦБ\\НАЧИСЛЕНИЕ ДИСКОНТА", V_INTEGER, DiscKind, err );

  if(not isOurBan and (BnrBackOffice == "А"/*ДУ*/) and not err and DiscKind)
    //если это УВ в ДУ и в настройках стоит начислять дисконт по максимальному сроку
    if( DL_VS_IsVsDiscount(leg) )

      if( ((BCTermFormula == VS_TERMF_ATSIGHT) AND (Maturity < Start) AND (Expiry < Start)))  // просто по предъявлении
        //для векселя "по предъявлении" - 1 год
        DateSplit(BnrStartDate, day, month, year);
        T = date(day, month, year+1) - date(BnrStartDate);
      elif(BCTermFormula == VS_TERMF_DURING) //Во столько-то времени  предъявления
        //для векселя "во столько-то времени от предъявления" - 1 год + количество дней, определяемое формулировкой "во столько-то"
        DateSplit(BnrStartDate, day, month, year);
        T = date(day, month, year+1) - date(BnrStartDate) + Diff;
      elif((BCTermFormula == VS_TERMF_ATSIGHT) AND (Expiry >= Start) ) // по предъявлении, но не позднее
        //для векселя "по предъявлении, но не позднее" - до даты "не позднее"
        T = date(Expiry) - date(BnrStartDate);
      elif((BCTermFormula == VS_TERMF_ATSIGHT) AND (Expiry < Start) and (Maturity >= Start)) // по предъявлении, но не ранее
        //для векселя "по предъявлении, но не ранее" - 1 год с даты "не ранее"
        DateSplit(Maturity, day, month, year);
        T = date(day, month, year+1) - date(BnrStartDate); //От постановки на баланс до даты "не ранее"+1 год
      else
        T = DL_VS_GetPlanExpryDate(bnr, leg) - BnrStartDate;
      end;
    else
      T = DL_VS_GetPlanExpryDate(bnr, leg) - BnrStartDate;
    end;
  elif(not isOurBan and (BnrBackOffice == "А"/*ДУ*/) and not err and not DiscKind)
    if( (BnrBCFormKind == VSBANNER_FORMKIND_CERT) OR                                            // депозитный сертификат
        ((BCTermFormula == VS_TERMF_ATSIGHT) AND (Maturity < Start) AND (Expiry < Start)) OR    // просто по предъявлении
        ((((BCTermFormula == VS_TERMF_ATSIGHT) AND (Expiry < Start) and (Maturity >= Start)) OR // по предъявлении, но не ранее
          ((BCTermFormula == VS_TERMF_ATSIGHT) AND (Expiry >= Start))                           // по предъявлении, но не позднее и не ранее
         ) AND (date(BnrStartDate) >= Maturity)
        )
      )
      if( P != NULL )
         T = P;
      else // оставим как было
         T = DL_VS_GetPlanExpryDate(bnr, leg) - BnrStartDate;
      end;
    elif( (((BCTermFormula == VS_TERMF_ATSIGHT) AND (Expiry < Start) and (Maturity >= Start)) OR // по предъявлении, но не ранее
           ((BCTermFormula == VS_TERMF_ATSIGHT) AND (Expiry >= Start))                           // по предъявлении, но не позднее и не ранее
          ) AND (date(BnrStartDate) <= Maturity)
        )
      T = date(Maturity) - date(BnrStartDate);
    elif( BCTermFormula == VS_TERMF_DURING ) // Во столько-то времени  предъявления
      if(BCPresentationDate > date(0,0,0))
        T = Diff;
      end;
    else // срочные...
      T = DL_VS_GetPlanExpryDate(bnr, leg) - BnrStartDate;
    end;
/* old:
    if(BCTermFormula == VS_TERMF_DURING) //Во столько-то времени  предъявления
      T = Diff;
    else
      T = DL_VS_GetPlanExpryDate(bnr, leg) - BnrStartDate;
    end;
*/
  else // УВ или СВ
    T = DL_GetBnrPlanRepayDate(bnr, leg) - BnrStartDate;
  end;

  return T;
END;

/* Вычисление максимальных и минимальных сроков до погашения */
MACRO СрокДоПогашения(bnr, leg, dateB, p:@variant)
   /* Смотрим на формулировку срока */
   if(bnr.rec.BCFormKind == VSBANNER_FORMKIND_CERT)
      p = leg.rec.Maturity - dateB;
   elif((bnr.rec.BCTermFormula == VS_TERMF_FIXEDDAY) OR (bnr.rec.BCTermFormula == VS_TERMF_INATIME))
      /* На опред. день или во столько-то времени от составления */
      p = leg.rec.Maturity - dateB;//(leg.rec.Start + int(leg.rec.Duration) - 1) - dateB;
   elif(bnr.rec.BCTermFormula == VS_TERMF_DURING)/* Во столько-то времени от предъявления */
      if(bnr.rec.BCPresentationDate != ZeroDate)
         p = (bnr.rec.BCPresentationDate + leg.rec.Diff) - dateB;
      else
         p = leg.rec.Start + DaysInYearByBasis(leg.rec.Basis, leg.rec.Start, true) + leg.rec.Diff - dateB
      end;
   elif(bnr.rec.BCTermFormula == VS_TERMF_ATSIGHT)/* По предъявлении */
     // Т.к векселя с формулировками "По предъявлении, но не ранее опред. срока от составления"
     // и "По предъявлении, но не ранее даты ..." у нас в системе идентичны, обрабатываем только второй случай.
     if(leg.rec.Maturity != ZeroDate) // По предъявлении, но не ранее
        if(dateB < leg.rec.Maturity)
           p = leg.rec.Maturity - dateB;
        else
           if(leg.rec.Expiry == ZeroDate)
              p = leg.rec.Maturity + DaysInYearByBasis(leg.rec.Basis, leg.rec.Maturity, true) - dateB;
           else
              p = Max( (leg.rec.Expiry - dateB), (leg.rec.Maturity + DaysInYearByBasis(leg.rec.Basis, leg.rec.Maturity, true) - dateB) );
           end;
        end;
     else  // По предъявлении
        p = 0;
        if(leg.rec.Expiry == ZeroDate)
           p = leg.rec.Start + DaysInYearByBasis(leg.rec.Basis, leg.rec.Start, true) - dateB;
        else
           p = Max( (leg.rec.Expiry - dateB), (leg.rec.Start + DaysInYearByBasis(leg.rec.Basis, leg.rec.Start, true) - dateB) );
        end;
     end;
   end;
END;

//получить идентификатор док.ц/б для векселя (DVSBANNER_DBT.T_BCID)
macro DL_GetBnrFiCertID(BCID:integer)
  var query, cmd, DataSet;
  var FiCertID = 0;

  query =   " select vficert.t_FiCertID "
          + "   from dv_ficert_dbt vficert, dfininstr_dbt fin, dvsbanner_dbt bnr "
          + "  where bnr.t_BCID = ? "
          + "    and fin.t_FIID = bnr.t_FIID "
          + "    and vficert.t_CertID = bnr.t_BCID "
          + "    and vficert.t_AvoirKind = fin.t_AvoirKind";
  cmd = Dl_RSDCommand(query);
  cmd.AddParam(BCID);

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    FiCertID = DataSet.FiCertID;
  end;

  return FiCertID;
end;


macro СущественностьОтклонения(Дата, LevelEssent, PortfolioId, DoCompare, Compare1, Compare2, ObjKind, ObjId, IsEssential, RateKind, RateVal)
  if (ValType(ObjKind)==V_UNDEF)
    ObjKind = 0;
  end;
  if (ValType(ObjId)==V_UNDEF)
    ObjId = 0;
  end;
  var cmd = RsdCommand("begin ?:= rsb_secur.GetEssentialDevInt(?,?,?,?,?,?,?,?,?,?,?);\n end; ");
  cmd.addParam("pReturn",          RSDBP_OUT, V_INTEGER                   );
  cmd.addParam("p_LevelEssential", RSDBP_IN, LevelEssent                  );
  cmd.addParam("p_Portfolio",      RSDBP_IN, PortfolioId                  );
  cmd.addParam("p_CalcDate",       RSDBP_IN, Дата                         ); 
  cmd.addParam("p_DoCompare",      RSDBP_IN, DoCompare                    );
  cmd.addParam("p_ToCompare1",     RSDBP_IN, Compare1                     );
  cmd.addParam("p_ToCompare2",     RSDBP_IN, Compare2                     );
  cmd.addParam("p_ObjectKind",     RSDBP_IN, ObjKind                      );
  cmd.addParam("p_ObjectID",       RSDBP_IN, ObjId                        );
  cmd.addParam("p_IsEssential",    RSDBP_OUT, V_BOOL                      );
  cmd.addParam("p_RateKind",       RSDBP_OUT, V_DOUBLE                    );
  cmd.addParam("p_RateVal",        RSDBP_OUT, V_DOUBLE                    );
  cmd.execute();
  SetParm(8,SQL_ConvTypeBool(cmd.value(9)));
  SetParm(9,SQL_ConvTypeInteger(cmd.value(10)));
  SetParm(10,SQL_ConvTypeSum(cmd.value(11)));
  // DEF-85905 функция возвращает true, если вызов функции проверки существенности был успешным,
  //           а не тогда, когда проверка показывает несущественность отклонения,
  //           существенность/не существенность возвращается в параметре
  return true;
OnError(e)
  return false;
end;

macro ОпределениеРПС(bnr, leg, ServiceKind, ProductKind, Дата, MinRPS, MaxRPS, SourceDataLayer)
  var year, month, day;

  var fullTerm = DL_VS_GetTermCircDate(bnr,leg);
  year = int(fullTerm / 360);
  fullTerm = mod(fullTerm,360);
  month = int(fullTerm / 30);
  fullTerm = mod(fullTerm,30);
  day = fullTerm;

  var cmd = RsdCommand("begin ?:= RSI_MARKETRATE.SelectRangeMarketRate(?,?,?,?,?,?,?,?,?,?,?,?,?,?);\n end; ");
  cmd.addParam("pReturn",          RSDBP_OUT, V_INTEGER        );
  cmd.addParam("pServiseKind",     RSDBP_IN, ServiceKind       );
  cmd.addParam("pBranchID",        RSDBP_IN, {NumDprt}         );
  cmd.addParam("pProductKindID",   RSDBP_IN, ProductKind       ); 
  cmd.addParam("pProductID",       RSDBP_IN, 0                 );
  cmd.addParam("pContractYear",    RSDBP_IN, year              );
  cmd.addParam("pContractMonths",  RSDBP_IN, month             );
  cmd.addParam("pContractDays",    RSDBP_IN, day               );
  cmd.addParam("pContractSum",     RSDBP_IN, leg.rec.Principal );
  cmd.addParam("pFiID",            RSDBP_IN, leg.rec.PFI       );
  cmd.addParam("pClientType",      RSDBP_IN, 0                 );
  cmd.addParam("pDate",            RSDBP_IN, Дата              );
  cmd.addParam("pMinValue",        RSDBP_OUT, V_DOUBLE         );
  cmd.addParam("pMaxValue",        RSDBP_OUT, V_DOUBLE         );
  cmd.addParam("pSourceDataLayer", RSDBP_OUT, V_DOUBLE         );
  cmd.execute();

  SetParm(5,SQL_ConvTypeSum(cmd.value(12)));
  SetParm(6,SQL_ConvTypeSum(cmd.value(13)));
  SetParm(7,SQL_ConvTypeSum(cmd.value(14)));
  return (SQL_ConvTypeInteger(cmd.value(0)) == 0);
OnError(e)
  return false;
END;


macro РасчетЭПС(CalcKind,DealId,BCID,CalcDate)
  var cmd = RsdCommand("begin ?:= rsb_secur.CalcEPS(?,?,?,?);\n end; ");
  cmd.addParam("pReturn",            RSDBP_OUT,  V_NUMERIC                         );
  cmd.addParam("p_CalcKind",         RSDBP_IN,   CalcKind                          );
  cmd.addParam("p_DocID",            RSDBP_IN,   DealId                            );
  cmd.addParam("p_SumID",            RSDBP_IN,   BCID                              );
  cmd.addParam("p_CalcDate",         RSDBP_IN,   CalcDate                          );
  cmd.execute();
  return (SQL_ConvTypeSum(cmd.value(0)));
OnError(e)
  return null;
end;

macro СВ_РасчетЭПС(DealId,BCID,CalcDate)
  return РасчетЭПС(4,DealId,BCID,CalcDate);
end;

macro УВ_РасчетЭПС(DealId,BCID,CalcDate)
  return РасчетЭПС(5,DealId,BCID,CalcDate);
end;

macro РасчетКорректировкиПроцентаПоЭПС(CalcKind,DealId,BCID,CalcDate,InterestIncomeAdd,BonusAdd,DiscountIncomeAdd,FairValue,EPS)
  var cmd = RsdCommand("begin ?:= rsb_secur.CalcCorrectPersentEPS(?,?,?,?,?,?,?,?,?,?,?);\n end; ");
  cmd.addParam("pReturn",            RSDBP_OUT,  V_NUMERIC                         );
  cmd.addParam("p_CalcKind",         RSDBP_IN,   CalcKind                          );
  cmd.addParam("p_DocID",            RSDBP_IN,   DealId                            );
  cmd.addParam("p_SumID",            RSDBP_IN,   BCID                              );
  cmd.addParam("p_CalcDate",         RSDBP_IN,   CalcDate                          );
  cmd.addParam("p_PrevCalcDate",     RSDBP_IN,   NULL                              );
  cmd.addParam("p_InterestIncomeAdd",RSDBP_IN,   InterestIncomeAdd                 );
  cmd.addParam("p_BonusAdd",         RSDBP_IN,   BonusAdd                          );
  cmd.addParam("p_DiscountIncomeAdd",RSDBP_IN,   DiscountIncomeAdd                 );
  cmd.addParam("p_OutlayAdd",        RSDBP_IN,   0                                 );
  cmd.addParam("p_FairValue",        RSDBP_IN,   FairValue                         );
  cmd.addParam("p_EPS",              RSDBP_IN,   EPS                               );
  cmd.execute();
  return (Round(SQL_ConvTypeSum(cmd.value(0)),2));
OnError(e)
  return null;
end;

macro РасчетКорректировкиПроцентаПоЭПССВ(DealId,BCID,CalcDate,InterestIncomeAdd,BonusAdd,DiscountIncomeAdd,FairValue,EPS)
  return РасчетКорректировкиПроцентаПоЭПС(4,DealId,BCID,CalcDate,InterestIncomeAdd,BonusAdd,DiscountIncomeAdd,FairValue,EPS);
end;

macro РасчетКорректировкиПроцентаПоЭПСУВ(DealId,BCID,CalcDate,InterestIncomeAdd,BonusAdd,DiscountIncomeAdd,FairValue,EPS)
  return РасчетКорректировкиПроцентаПоЭПС(5,DealId,BCID,CalcDate,InterestIncomeAdd,BonusAdd,DiscountIncomeAdd,FairValue,EPS);
end;

macro РасчетАСпоЭПС(CalcKind,DealId,BCID,CalcDate,EPS)
  var cmd = RsdCommand("begin ?:= rsb_secur.CalcAS_EPS(?,?,?,?,?);\n end; ");
  cmd.addParam("pReturn",            RSDBP_OUT,  V_NUMERIC                         );
  cmd.addParam("p_CalcKind",         RSDBP_IN,   CalcKind                          );
  cmd.addParam("p_DocID",            RSDBP_IN,   DealId                            );
  cmd.addParam("p_SumID",            RSDBP_IN,   BCID                              );
  cmd.addParam("p_CalcDate",         RSDBP_IN,   CalcDate                          );
  cmd.addParam("p_EPS",              RSDBP_IN,   EPS                               );
  cmd.execute();
  return (SQL_ConvTypeSum(cmd.value(0)));
OnError(e)
  return null;
end;

macro РасчетАСпоЭПС_СВ(DealId,BCID,CalcDate)
  return РасчетАСпоЭПС(4,DealId,BCID,CalcDate);
end;

macro РасчетАСпоЭПС_УВ(DealId,BCID,CalcDate)
  return РасчетАСпоЭПС(5,DealId,BCID,CalcDate);
end;


macro РасчетАСЛН(CalcKind,DealId,BCID,CalcDate)
  var cmd = RsdCommand("begin ?:= rsb_secur.CalcAS_Line(?,?,?,?);\n end; ");
  cmd.addParam("pReturn",            RSDBP_OUT,  V_NUMERIC                         );
  cmd.addParam("p_CalcKind",         RSDBP_IN,   CalcKind                          );
  cmd.addParam("p_DocID",            RSDBP_IN,   DealId                            );
  cmd.addParam("p_SumID",            RSDBP_IN,   BCID                              );
  cmd.addParam("p_CalcDate",         RSDBP_IN,   CalcDate                          );
  cmd.execute();
  return (SQL_ConvTypeSum(cmd.value(0)));
OnError(e)
  return null;
end;

macro РасчетАСЛН_СВ(DealId,BCID,CalcDate)
  return РасчетАСЛН(4,DealId,BCID,CalcDate);
end;

macro РасчетАСЛН_УВ(DealId,BCID,CalcDate)
  return РасчетАСЛН(5,DealId,BCID,CalcDate);
end;

macro УВ_ПолучитьДатуПоследнегоРасчетаКорр(BCID, VDate, LimitSearch)
  var cmd, DataSet, query;
  var retVal = null;
  var isLimitSearch = false;
  if ((ValType(LimitSearch) == V_BOOL) and (LimitSearch))
    isLimitSearch = true;
  end;
  query =  "SELECT NVL(MAX(T_OPERDATE),TO_DATE('01.01.0001','DD.MM.YYYY')) as LastDate "
          +"  FROM DVSINCOME_DBT "
          +" WHERE     T_INCOMETYPE = ? "
          +"       AND T_BCID = ? "
          +"       AND T_ENROLMENT_ID = "
          +"              RSB_BILL.GetVABnrLastAccountedEnrolID (?, ?) ";
  if (isLimitSearch)
    query = query + " AND T_OPERDATE <= ?";
  end;
  cmd = DL_RSDCommand(query);
  cmd.AddParam(VSINCOMETYPE_EPRPERC);
  cmd.AddParam(BCID);
  cmd.AddParam(BCID);
  cmd.AddParam(VDate);
  if (isLimitSearch)
    cmd.AddParam(VDate);
  end;

  DataSet = cmd.Execute();
  if (DataSet.MoveNext())
    retVal = SQL_ConvTypeDate(DataSet.LastDate);
  end;
  return retVal;
end;


macro РасчетПроцентовХран(LegID, VDate, CalcPercent)
  var Select;
  Select = RsdCommand("begin ?:= rsb_bill.GetPrecentOnDate(?, ?, ?);\n end;");
  Select.addParam("pReturn",             RSDBP_OUT, V_INTEGER );  
  Select.AddParam("p_LegId",             RSDBP_IN,  LegID      );
  Select.AddParam("p_CalcDate",          RSDBP_IN,  VDate     );
  Select.AddParam("p_CalcPrecent",       RSDBP_OUT, V_NUMERIC );

  Select.Execute();
  SetParm(2,SQL_ConvTypeSum(Select.value(3)));
  return (SQL_ConvTypeInteger(Select.value(0)) == 0);  
Onerror(e)
  return false;
end;

macro РасчетПроцентовХранЗаПериод(LegID, StartDate, EndDate, CalcPercent )
  var CalcPercentBeg,CalcPercentEnd;
  if (РасчетПроцентовХран(LegID, StartDate, CalcPercentBeg) 
    and РасчетПроцентовХран(LegID, EndDate, CalcPercentEnd))
    SetParm(3,CalcPercentEnd-CalcPercentBeg);
    return true;
  end;
  return false;
Onerror(e)
  return false;
end;

MACRO ПолучитьОтраженнуюОтсроченнуюРазницу(BCID, Дата, Enrolment_ID)
  var Сумма = 0.0;
  var query, DataSet;
  var RSD = DL_RSDCommand();
  var CurrEnrolID;

  if((ValType(Enrolment_ID) == V_UNDEF) or (Enrolment_ID == 0))
    CurrEnrolID = VA_GetLastAccountedEnrolmentID(BCID);
  else
    CurrEnrolID = Enrolment_ID;
  end;

  query =   " select NVL(sum(inc.t_DefDif), 0) as Summ"
          + " from dvsincome_dbt inc "
          + " where inc.t_BCID = ? /*1*/ AND inc.t_Enrolment_ID = ? /*2*/ AND (inc.t_IncomeType = ? /*3*/ OR inc.t_IncomeType = ? /*4*/) ";

  RSD.AddParam(BCID);         /*1*/
  RSD.AddParam(CurrEnrolID);  /*2*/
  RSD.AddParam(VSINCOMETYPE_CHARGE);  /*3*/
  RSD.AddParam(VSINCOMETYPE_OVERVALUE);  /*4*/

  if((ValType(Дата) != V_UNDEF) and (Дата > date(0,0,0)))
    query =  query + " and inc.t_EndDate <= ? /*5*/";
    RSD.AddParam(date(Дата));  /*5*/
  end;

  DataSet = RSD.Execute(query);

  if(DataSet.moveNext())
    Сумма = DataSet.Summ;
  end;

  return Сумма;
END;

macro УВ_ПолучитьИдСделки(BCID,CalcDate,DealId)
  var Select;
  Select = RsdCommand("begin ?:= rsb_bill.GetVADealId(?, ?, ?);\n end;");
  Select.addParam("pReturn",            RSDBP_OUT, V_INTEGER );  
  Select.AddParam("p_BCID",             RSDBP_IN,  BCID      );
  Select.AddParam("p_CalcDate",         RSDBP_IN,  CalcDate     );
  Select.AddParam("p_DealId",           RSDBP_OUT, V_NUMERIC );

  Select.Execute();
  SetParm(2,SQL_ConvTypeInteger(Select.value(3)));
  return (SQL_ConvTypeInteger(Select.value(0)) == 0);  
Onerror(e)
  return false;
end;

macro СВ_ПолучитьИдСделки(BCID,CalcDate,DealId)
  var Select;
  Select = RsdCommand("begin ?:= rsb_bill.GetVSDealId(?, ?, ?);\n end;");
  Select.addParam("pReturn",            RSDBP_OUT, V_INTEGER );  
  Select.AddParam("p_BCID",             RSDBP_IN,  BCID      );
  Select.AddParam("p_CalcDate",         RSDBP_IN,  CalcDate     );
  Select.AddParam("p_DealId",           RSDBP_OUT, V_NUMERIC );

  Select.Execute();
  SetParm(2,SQL_ConvTypeInteger(Select.value(3)));
  return (SQL_ConvTypeInteger(Select.value(0)) == 0);  
Onerror(e)
  return false;
end;

macro УВ_РасчетСС(FIID,BCID,DealId,CalcDate,pFairValue,pMsg,pRateID,pCat55,pCat66,PAlgUsed,pEir,pAmortCalcKind)
  var cmd = RsdCommand("begin ?:= rsb_secur.SC_CalcFairValue(?,?,?,?,?,?,?,?,?,?,?,?,?,?);\n end; ");
  cmd.addParam("pReturn",               RSDBP_OUT,  V_NUMERIC         );
  cmd.addParam("PFIID",                 RSDBP_IN,   FIID              );
  cmd.addParam("PCALCDATE",             RSDBP_IN,   CalcDate          );
  cmd.addParam("PCALCKIND",             RSDBP_IN,   5                 );
  cmd.addParam("PDEALID",               RSDBP_IN,   DealId            );
  cmd.addParam("PSUMID",                RSDBP_IN,   BCID              );
  cmd.addParam("PENDCOUPDATE",          RSDBP_IN,   ZeroDate          );
  cmd.addParam("PFAIRVALUE",            RSDBP_OUT,  V_NUMERIC         );
  cmd.addParam("PMSG",                  RSDBP_OUT,  V_STRING,    10000);
  cmd.addParam("PRATEID",               RSDBP_OUT,  V_NUMERIC         );
  cmd.addParam("PCAT55",                RSDBP_OUT,  V_NUMERIC         );
  cmd.addParam("PCAT60",                RSDBP_OUT,  V_NUMERIC         );
  cmd.addParam("PALGUSED",              RSDBP_OUT,  V_NUMERIC         );
  cmd.addParam("PEIR",                  RSDBP_OUT,  V_NUMERIC         );
  cmd.addParam("PAMORTCALCKIND",        RSDBP_OUT,  V_NUMERIC         );
  cmd.execute();
  SetParm(4,SQL_ConvTypeSum(cmd.value(7)));
  SetParm(5,SQL_ConvTypeSum(cmd.value(8)));
  SetParm(6,SQL_ConvTypeSum(cmd.value(9)));
  SetParm(7,SQL_ConvTypeSum(cmd.value(10)));
  SetParm(8,SQL_ConvTypeSum(cmd.value(11)));
  SetParm(9,SQL_ConvTypeSum(cmd.value(12)));
  SetParm(10,SQL_ConvTypeSum(cmd.value(13)));
  SetParm(11,SQL_ConvTypeSum(cmd.value(14)));
  return (SQL_ConvTypeSum(cmd.value(0))==0);
OnError(e)
  return false;
end;

macro СВ_РасчетСС(FIID,BCID,DealId,CalcDate,pFairValue,pMsg,pRateID,pCat55,pCat66,PAlgUsed,pEir,pAmortCalcKind)
  var cmd = RsdCommand("begin ?:= RSB_SECUR.SC_CalcFairValue(?,?,?,?,?,?,?,?,?,?,?,?,?,?);\n end; ");
  cmd.addParam("pReturn",               RSDBP_OUT,  V_NUMERIC         );
  cmd.addParam("PFIID",                 RSDBP_IN,   FIID              );
  cmd.addParam("PCALCDATE",             RSDBP_IN,   CalcDate          );
  cmd.addParam("PCALCKIND",             RSDBP_IN,   4                 );
  cmd.addParam("PDEALID",               RSDBP_IN,   DealId            );
  cmd.addParam("PSUMID",                RSDBP_IN,   BCID              );
  cmd.addParam("PENDCOUPDATE",          RSDBP_IN,   ZeroDate          );
  cmd.addParam("PFAIRVALUE",            RSDBP_OUT,  V_NUMERIC         );
  cmd.addParam("PMSG",                  RSDBP_OUT,  V_STRING,    10000);
  cmd.addParam("PRATEID",               RSDBP_OUT,  V_NUMERIC         );
  cmd.addParam("PCAT55",                RSDBP_OUT,  V_NUMERIC         );
  cmd.addParam("PCAT60",                RSDBP_OUT,  V_NUMERIC         );
  cmd.addParam("PALGUSED",              RSDBP_OUT,  V_NUMERIC         );
  cmd.addParam("PEIR",                  RSDBP_OUT,  V_NUMERIC         );
  cmd.addParam("PAMORTCALCKIND",        RSDBP_OUT,  V_NUMERIC         );
  cmd.execute();
  SetParm(4,SQL_ConvTypeSum(cmd.value(7)));
  SetParm(5,SQL_ConvTypeSum(cmd.value(8)));
  SetParm(6,SQL_ConvTypeSum(cmd.value(9)));
  SetParm(7,SQL_ConvTypeSum(cmd.value(10)));
  SetParm(8,SQL_ConvTypeSum(cmd.value(11)));
  SetParm(9,SQL_ConvTypeSum(cmd.value(12)));
  SetParm(10,SQL_ConvTypeSum(cmd.value(13)));
  SetParm(11,SQL_ConvTypeSum(cmd.value(14)));
  return (SQL_ConvTypeSum(cmd.value(0))==0);
OnError(e)
  return false;
end;

MACRO ПолучитьСуммуПДДНеотнесУнивНаДату(BCID, FIRole, Дата)
  return ПолучитьСуммуПДДНеотнесУнив(BCID, FIRole, Дата);
END;


macro ПолучитьКорректировкуПДДПоЭПСВУ(BCID, Enrolment_ID, Дата, SubSystem)
  var query, DataSet;
  var Сумма = 0.0;
  var CurrEnrolID = 0;
  var RSD = DL_RSDCommand();

  if((ValType(Enrolment_ID) == V_UNDEF) or (Enrolment_ID == 0))
    if (SubSystem == "N")
      CurrEnrolID = VA_GetLastAccountedEnrolmentID(BCID, Дата);
    elif (SubSystem == "Y")
      CurrEnrolID = VS_GetLastPayedEnrolmentID(BCID, Дата);
    end;
  else
    CurrEnrolID = Enrolment_ID;
  end;

  query =   " select sum(inc.t_AccPerc) as Summ" +
            " from dvsincome_dbt inc " + 
            " where inc.t_BCID = ? /*1*/ AND inc.t_Enrolment_ID = ? /*2*/ AND inc.t_IncomeType = ?";

  RSD.AddParam(BCID);         /*1*/
  RSD.AddParam(CurrEnrolID);  /*2*/
  RSD.AddParam(VSINCOMETYPE_EPRPERC);  /*3*/

  if((ValType(Дата) != V_UNDEF) and (Дата > date(0,0,0)))
    query =  query + " and inc.t_EndDate <= ? /*3*/";
    RSD.AddParam(date(Дата));  /*3*/
  end;

  query = query + " order by inc.t_EndDate DESC";

  DataSet = RSD.Execute(query);

  if((DataSet.moveNext()) and (ValType(DataSet.Summ) != V_UNDEF))
    Сумма = DataSet.Summ;
  end;

  return Сумма;
end;