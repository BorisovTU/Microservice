/*
$Name:         dl_risklimit.mac
$Module:       ГКБО
$Description:  Операции по лимитам риска и утилизациям лимитов
*/
import cb_sql, DealsInter;

private record LUOperation("DLLUOperation");
private file UtilRegister("DLUtilRegister.dbt") write;

private var RegisterOpGroup: integer;   // Группа операции-регистратора: LUTIL_REGGROUP_SECUR, LUTIL_REGGROUP_BILL или LUTIL_REGGROUP_LUOPER
private var RegisterOpKind: integer;    // Вид операции-регистратора (справочник OPRKOPER.dbt)
private var RegisterOpId: integer;      // Id операции-регистратора
private var RegisterOpCode: string;     // Код операции-регистратора
private var RegisterOpStepId: integer;    // Шаг операции-регистратора (справочник OPRKSTEP.dbt)
private var {curdate};
private var {oper};

const LUOPERATION_STATUS_INIT = 0;      // "Отложенная" операция
const LUOPERATION_STATUS_EXEC = 1;      // "Выполненная" операция

const LUTIL_REGGROUP_SECUR  = 1;        // Группа операции-регистратора "Ценные бумаги"
const LUTIL_REGGROUP_BILL   = 2;        // Группа операции-регистратора "Учтенные векселя"
const LUTIL_REGGROUP_LUOPER = 3;        // Группа операции-регистратора "Операции лимитов риска"

private macro GetStringParm( p_parm )
    var res = "";
    if( p_parm != null )
        res = p_parm;
    end;
    return res;
end;

private macro GetIntegerParm( p_parm )
    var res = 0;
    if( p_parm != null )
        if( p_parm > -1 )
            res = p_parm;
        end;
    end;
    return res;
end;

private macro GetSaveForOperationSign( limitKindId: integer )
    var cmd = RsdCommand("SELECT T_SAVEFOROPERATION FROM DDLLIMITKIND_DBT WHERE T_ID = ?");
    cmd.addParam("", RSDBP_IN, limitKindId);
    cmd.execute();
    var rs = TRsbDataSet(cmd);
    var IsSaveForOperation = "";
    if( rs.MoveNext() )
        IsSaveForOperation = rs.rec.SaveForOperation;
    end;
    return IsSaveForOperation;
end; /* GetSaveForOperationSign */

// Транзакция для ввода установленных лимитов
private macro SetRLimitsTrnProc
    var cmdstr, cmd;
    var st = true;
    file opf("DLLUOperation.dbt") write;

    if( LUOperation.id > 0 )
        opf.id = LUOperation.id;
        st = GetEQ( opf );
    end;

    opf.id = LUOperation.id;
    opf.kind = LUOperation.kind;
    opf.code = LUOperation.code;
    opf.LimitKindId = LUOperation.LimitKindId;
    opf.Analitic1 = LUOperation.Analitic1;
    opf.Analitic2 = LUOperation.Analitic2;
    opf.Date = LUOperation.Date;
    opf.Time = LUOperation.Time;
    opf.StartDate = LUOperation.StartDate;
    opf.StartTime = LUOperation.StartTime;
    opf.Oper = LUOperation.Oper;
    opf.State = LUOperation.State;

    if( LUOperation.id == 0 )
        st = Insert(opf);
        if( st == true )
            cmdstr = "INSERT INTO DDLLIMITREGISTER_DBT t ( "
                       "t.T_OPERATIONID, t.T_LIMITKINDID, "
                       "t.T_ANALITIC1, t.T_ANALITIC2, "
                       "t.T_FIID, t.T_VALUE, "
                       "t.T_STARTDATE, t.T_STARTTIME) "
                       "SELECT ?, ?, "
                              "tmp.T_ANALITIC1, tmp.T_ANALITIC2, "
                              "tmp.T_FIID, tmp.T_LIMITVALUE, "
                              "?, ? "
                         "FROM DDLLUOPERPARM_TMP tmp";
            cmd = RsdCommand(cmdstr);
            cmd.addParam("", RSDBP_IN, opf.id);
            cmd.addParam("", RSDBP_IN, opf.LimitKindId);
            cmd.addParam("", RSDBP_IN, opf.StartDate);
            cmd.addParam("", RSDBP_IN, opf.StartTime);
            cmd.execute();
        end;
    else
        if( st == true )
            st = Update(opf);
        end;
        if( st == true )
            cmdstr = "DELETE FROM DDLLIMITREGISTER_DBT t "
                           "WHERE t.t_operationid = ? "
                             "AND t.t_id NOT IN (SELECT tmp.T_LIMITREGID FROM DDLLUOPERPARM_TMP tmp)";
            cmd = RsdCommand(cmdstr);
            cmd.addParam("", RSDBP_IN, opf.id);
            cmd.execute();

            cmdstr = 
                "MERGE INTO DDLLIMITREGISTER_DBT t "
                     "USING (SELECT * FROM DDLLUOPERPARM_TMP) tmp "
                        "ON (t.t_id = tmp.T_LIMITREGID) "
                "WHEN MATCHED THEN "
                   "UPDATE SET t.T_LIMITKINDID = tmp.T_LIMITKINDID, "
                              "t.T_ANALITIC1 = tmp.T_ANALITIC1, "
                              "t.T_ANALITIC2 = tmp.T_ANALITIC2, "
                              "t.T_FIID = tmp.T_FIID, "
                              "t.T_VALUE = tmp.T_LIMITVALUE, "
                              "t.T_STARTDATE = tmp.T_STARTDATE, "
                              "t.T_STARTTIME = tmp.T_STARTTIME "
                "WHEN NOT MATCHED THEN "
                       "INSERT (t.T_OPERATIONID, t.T_LIMITKINDID, "
                               "t.T_ANALITIC1, t.T_ANALITIC2, "
                               "t.T_FIID, t.T_VALUE, "
                               "t.T_STARTDATE, t.T_STARTTIME) "
                       "VALUES (?, tmp.T_LIMITKINDID, "
                               "tmp.T_ANALITIC1, tmp.T_ANALITIC2, "
                               "tmp.T_FIID, tmp.T_LIMITVALUE, "
                               "tmp.T_STARTDATE, tmp.T_STARTTIME)";
            cmd = RsdCommand(cmdstr);
            cmd.addParam("", RSDBP_IN, opf.id);
            cmd.execute();
        end;
    end;
    
    if( st == false )
        AbortTrn();
    end;
end; /* SetRLimitsTrnProc */

// Транзакция для переоценки утилизации
private macro SetUtilizationTrnProc
    file opf("DLLUOperation.dbt") write;

    opf.Kind = LUOperation.Kind;
    opf.Code = LUOperation.Code;
    opf.LimitKindId = LUOperation.LimitKindId;
    opf.Analitic1 = LUOperation.Analitic1;
    opf.Analitic2 = LUOperation.Analitic2;
    opf.Date = LUOperation.Date;
    opf.Time = LUOperation.Time;
    opf.StartDate = LUOperation.StartDate;
    opf.StartTime = LUOperation.StartTime;
    opf.Oper = LUOperation.Oper;
    opf.State = LUOPERATION_STATUS_EXEC;
    var st = Insert(opf);
    
    if( st == true )
        var cmdstr = 
            "MERGE INTO DDLUTILREGISTER_DBT t "
                 "USING (SELECT * FROM DDLLUOPERPARM_TMP) tmp "
                    "ON (t.t_id = tmp.T_UTILREGID) "
            "WHEN MATCHED THEN "
               "UPDATE SET t.T_VALUE = tmp.T_UTILVALUE, " 
                          "t.T_REGISTEROPGROUP = ?, "
                          "t.T_REGISTEROPKIND = ?, "
                          "t.T_REGISTEROPID = ?, "
                          "t.T_REGISTEROPCODE = ? ";
        var cmd = RsdCommand(cmdstr);
        cmd.addParam("", RSDBP_IN, LUTIL_REGGROUP_LUOPER);
        cmd.addParam("", RSDBP_IN, opf.Kind);
        cmd.addParam("", RSDBP_IN, opf.id);
        cmd.addParam("", RSDBP_IN, opf.Code);
        cmd.execute();
    end;

    if( st == false )
        AbortTrn();
    end;
end; /* SetUtilizationTrnProc */

// Транзакция для вставки записи утилизации
private macro InsertUtilizationTrnProc
    var cmdstr = 
        "DELETE FROM DDLUTILREGISTER_DBT "
              "WHERE T_REGISTEROPGROUP = ? "
                "AND T_REGISTEROPKIND = ? "
                "AND T_REGISTEROPID = ? "
                "AND T_REGISTEROPCODE = ? ";
    if( RegisterOpStepId )
        cmdstr = cmdstr + "AND T_REGISTEROPSTEP = ? ";
    end;
                       
    var cmd = RsdCommand(cmdstr);
    cmd.addParam("", RSDBP_IN, RegisterOpGroup);
    cmd.addParam("", RSDBP_IN, RegisterOpKind);
    cmd.addParam("", RSDBP_IN, RegisterOpId);
    cmd.addParam("", RSDBP_IN, RegisterOpCode);
    if( RegisterOpStepId )
        cmd.addParam("", RSDBP_IN, RegisterOpStepId);
    end;
    cmd.execute();

    var st = Insert(UtilRegister);

    if( st == false )
        AbortTrn();
    end;
end; /* InsertUtilizationTrnProc */

// Определение активных лимитов
macro GetActiveLimits( p_limitKind, p_analitic1, p_analitic2, p_date, p_time )
    var startdate = {curdate};
    var starttime = time();
    var limitKind = 0, analitic1 = 0, analitic2 = 0;
    if( p_date != null )
        if( p_date != date(0,0,0) )
            startdate = p_date;
        end;
    end;
    if( p_time != null )
        if( p_time != time(0) )
            starttime = p_time;
        end;
    end;
    if( p_limitKind != null )
        limitKind = p_limitKind;
    end;
    if( p_analitic1 != null )
        analitic1 = p_analitic1;
    end;
    if( p_analitic2 != null )
        analitic1 = p_analitic2;
    end;

    SQL_Truncate( "DDLLUOPERPARM_TMP" );

    var cmdstr =
        "INSERT INTO DDLLUOPERPARM_TMP tmp ( "
                    "tmp.T_LIMITKINDID, tmp.T_ANALITIC1, tmp.T_ANALITIC2, tmp.T_LIMITREGID ) "
             "SELECT DISTINCT t.t_limitKindId, t.t_analitic1, t.t_analitic2, "
                    "FIRST_VALUE (t.t_id) "
                    "OVER (PARTITION BY t.t_limitKindId, t.t_analitic1, t.t_analitic2 "
                              "ORDER BY t.t_startdate DESC, t.t_starttime DESC "
                          "RANGE BETWEEN UNBOUNDED PRECEDING AND UNBOUNDED FOLLOWING) "
               "FROM DDLLIMITREGISTER_DBT t "
                   " INNER JOIN DDLLUOPERATION_DBT op ON op.T_ID = t.T_OPERATIONID "
              "WHERE ( t.t_startdate < ? OR ( t.t_startdate = ? AND t.t_starttime <= ? ) ) "
                "AND op.T_STATE = 1";

    if( limitKind > 0 )
        cmdstr = cmdstr + "AND t_limitKindId = ? ";
    end;
    if( analitic1 > 0 )
        cmdstr = cmdstr + "AND t_analitic1 = ? ";
    end;
    if( analitic2 > 0 )
        cmdstr = cmdstr + "AND t_analitic2 = ? ";
    end;

    var cmd = RsdCommand(cmdstr);
    cmd.addParam("", RSDBP_IN, startdate);
    cmd.addParam("", RSDBP_IN, startdate);
    cmd.addParam("", RSDBP_IN, starttime);
    if( limitKind > 0 )
        cmd.addParam("", RSDBP_IN, limitKind);
    end;
    if( analitic1 > 0 )
        cmd.addParam("", RSDBP_IN, analitic1);
    end;
    if( analitic2 > 0 )
        cmd.addParam("", RSDBP_IN, analitic2);
    end;
    cmd.execute();
end; /* GetActiveLimits */

// Определение активного лимита заданного вида
macro GetActiveLimitId( p_limitKind: integer, p_analitic1: integer, p_analitic2: integer, p_date, p_time )
    var startdate = {curdate};
    var starttime = time();
    if( p_date != null )
        if( p_date != date(0,0,0) )
            startdate = p_date;
        end;
    end;
    if( p_time != null )
        if( p_time != time(0) )
            starttime = p_time;
        end;
    end;

    var cmdstr =
        "SELECT t_id FROM dDLLimitRegister_dbt "
         "WHERE t_kindId = ? "
           "AND ( t_startdate < ? OR ( t_startdate = ? AND t_starttime <= ? ) ) ";

    if( p_analitic1 > 0 )
        cmdstr = cmdstr + "AND t_analitic1 = ? ";
    end;
    if( p_analitic2 > 0 )
        cmdstr = cmdstr + "AND t_analitic2 = ? ";
    end;

    cmdstr = cmdstr + "ORDER BY t_startdate DESC, t_starttime DESC";
      
    var cmd = RsdCommand(cmdstr);
    cmd.addParam("", RSDBP_IN, p_limitKind);
    cmd.addParam("", RSDBP_IN, startdate);
    cmd.addParam("", RSDBP_IN, startdate);
    cmd.addParam("", RSDBP_IN, starttime);
    if( p_analitic1 > 0 )
        cmd.addParam("", RSDBP_IN, p_analitic1);
    end;
    if( p_analitic2 > 0 )
        cmd.addParam("", RSDBP_IN, p_analitic2);
    end;
    cmd.execute();
    var rs = TRsbDataSet(cmd);

    var regId = 0;
    if( rs.MoveNext() )
        regId = rs.rec.id;
    end;

    return regId;
end; /* GetActiveLimitId */

// Процедура вставки записи утилизации
macro InsertUtilization( p_limitKind: integer,  // Вид лимита
                         p_value: numeric,      // Величина утилизации
                         p_analitic1: integer,  // Аналитический признак 1
                         p_analitic2: integer,  // Аналитический признак 2
                         p_date, p_time,        // Дата и время начала действия утилизации
                         p_fiid: integer,       // Валюта, по умолчанию 0 (RUR)
                         p_operationKind: integer,  // Вид операции, для которой задается утилизация
                         p_operationId: integer,    // Id операции, для которой задается утилизация
                         p_operationCode: string,   // Код операции, для которой задается утилизация
                         p_registerGroup: integer,  // Группа операции-регистратора: LUTIL_REGGROUP_SECUR, LUTIL_REGGROUP_BILL или LUTIL_REGGROUP_LUOPER
                         p_registerKind: integer,   // Вид операции-регистратора (справочник OPRKOPER.dbt)
                         p_registerId: integer,     // Id операции-регистратора
                         p_registerCode: string,    // Код операции-регистратора
                         p_registerStepId: integer )// Шаг операции-регистратора
    var startdate = {curdate};
    var starttime = time();
    var fiid = GetIntegerParm( p_fiid );
    var limitKind = GetIntegerParm( p_limitKind );
    var analitic1 = GetIntegerParm( p_analitic1 );
    var analitic2 = GetIntegerParm( p_analitic2 );
    RegisterOpGroup = GetIntegerParm( p_registerGroup );
    RegisterOpKind = GetIntegerParm( p_registerKind );
    RegisterOpId = GetIntegerParm( p_registerId );
    RegisterOpCode = GetStringParm( p_registerCode );
    RegisterOpStepId = GetIntegerParm( p_registerStepId );

    if( p_date != null )
        if( p_date != date(0,0,0) )
            startdate = p_date;
        end;
    end;
    if( p_time != null )
        if( p_time != time(0) )
            starttime = p_time;
        end;
    end;

    ClearRecord( UtilRegister );
    UtilRegister.RegisterOpGroup = GetIntegerParm( p_registerGroup );
    UtilRegister.RegisterOpKind = GetIntegerParm( p_registerKind );
    UtilRegister.RegisterOpId = GetIntegerParm( p_registerId );
    UtilRegister.RegisterOpCode = GetStringParm( p_registerCode );
    UtilRegister.RegisterOpStep = GetIntegerParm( p_registerStepId );
    UtilRegister.LimitKindId = limitKind;
    UtilRegister.Analitic1 = analitic1;
    UtilRegister.Analitic2 = analitic2;
    UtilRegister.Fiid = fiid;
    UtilRegister.StartDate = startdate;
    UtilRegister.StartTime = starttime;
    UtilRegister.Value = p_value;

    if( limitKind > 0 )
        if( GetSaveForOperationSign( limitKind ) != "" )
            UtilRegister.OperationKind = GetIntegerParm( p_operationKind );
            UtilRegister.OperationId = GetIntegerParm( p_operationId );
            UtilRegister.OperationCode = GetStringParm( p_operationCode );
        end;
    end;

    return ProcessTrn( 1, "InsertUtilizationTrnProc" );
end; /* InsertUtilization */

// Предоставление данных по текущей утилизации и суммам установленных лимитов
macro GetLimitState( p_limitKind: integer, p_analitic1: integer, p_analitic2: integer, p_date, p_time )
    GetActiveLimits( p_limitKind, p_analitic1, p_analitic2, p_date, p_time );
    var cmdstr = 
        "UPDATE DDLLUOPERPARM_TMP t SET t.T_UTILVALUE = ("
             "SELECT SUM( NVL(ut.T_VALUE, 0) ) "
               "FROM DDLUTILREGISTER_DBT ut "
              "WHERE ut.t_limitKindId = t.t_limitKindId "
                "AND ut.t_analitic1 = t.t_analitic1 "
                "AND ut.t_analitic2 = t.t_analitic2 "
                "AND ut.t_fiid = t.t_fiid)";
    var cmd = RsdCommand(cmdstr);
    cmd.execute();
end; /* GetLimitState */

// Ввод установленных лимитов
macro SetRLimits( p_recbuf )
    SetBuff( LUOperation, p_recbuf );
    var res = ProcessTrn( 1, "SetRLimitsTrnProc" );

    if( res )
        msgbox( "Установка лимитов выполнена успешно" );
    end;
    return res;
end; /* SetRLimits */

// Переоценка утилизации
macro SetUtilization( p_recbuf )
    SetBuff( LUOperation, p_recbuf );
    SQL_Truncate( "DDLLUOPERPARM_TMP" );

    var cmdstr = "INSERT INTO DDLLUOPERPARM_TMP tmp ( "
               "tmp.T_UTILREGID, "
               "tmp.T_OPERATIONID, tmp.T_LIMITKINDID, "
               "tmp.T_ANALITIC1, tmp.T_ANALITIC2, "
               "tmp.T_FIID, tmp.T_UTILOLDVALUE, "
               "tmp.T_STARTDATE, tmp.T_STARTTIME) "
               "SELECT t.T_ID, "
                      "t.T_OPERATIONID, t.T_LIMITKINDID, "
                      "t.T_ANALITIC1, t.T_ANALITIC2, "
                      "t.T_FIID, t.T_VALUE, "
                      "t.T_STARTDATE, t.T_STARTTIME "
                 "FROM DDLUTILREGISTER_DBT t";
    var cmd = RsdCommand(cmdstr);
    cmd.execute();
    /*
    Расчет значения атрибутов utilNewValue, utilValue и задания их в DDLLUOPERPARM_TMP
    */
    var res = ProcessTrn( 1, "SetUtilizationTrnProc" );

    if( res )
        msgbox( "Переоценка утилизации выполнена успешно" );
    end;
    return res;
end; /* SetUtilization */

// Заполнение временной таблицы для накладного скроллинга
macro SetLimRegScrol( p_recbuf )
    SetBuff( LUOperation, p_recbuf );
    SQL_Truncate( "DDLLUOPERPARM_TMP" );

    var cmdstr = "INSERT INTO DDLLUOPERPARM_TMP tmp ( "
                 "tmp.T_LIMITREGID, "
                 "tmp.T_OPERATIONID, tmp.T_LIMITKINDID, "
                 "tmp.T_ANALITIC1, tmp.T_ANALITIC2, "
                 "tmp.T_FIID, tmp.T_LIMITVALUE, "
                 "tmp.T_STARTDATE, tmp.T_STARTTIME) "
                 "SELECT t.T_ID, "
                        "t.T_OPERATIONID, t.T_LIMITKINDID, "
                        "t.T_ANALITIC1, t.T_ANALITIC2, "
                        "t.T_FIID, t.T_VALUE, "
                        "t.T_STARTDATE, t.T_STARTTIME "
                   "FROM DDLLIMITREGISTER_DBT t "
                  "WHERE t.t_OPERATIONID = ?";
    var cmd = RsdCommand(cmdstr);
    cmd.addParam("", RSDBP_IN, LUOperation.id);
    cmd.execute();
end;

macro UpdateState(p_recbuf, state)
  SetBuff( LUOperation, p_recbuf );
  var stat = false;
  file opf("DLLUOperation.dbt") write;
  opf.id = LUOperation.id;
  stat = GetEQ( opf );
  if( stat == true )
    opf.State = state;
    stat = Update(opf);
  end;
  return stat;
end;

macro DeleteFromLimitRegTrnProc
  file opf("DLLUOperation.dbt") write;
  var st = true;
  var cmdstr = 
      "DELETE FROM DDLLIMITREGISTER_DBT "
            "WHERE T_OPERATIONID = ? ";
  var cmd = RsdCommand(cmdstr);
  cmd.addParam("", RSDBP_IN, LUOperation.ID);
  cmd.execute();

  opf.id = LUOperation.id;
  st = GetEQ( opf );
  if( st = true )
    st = Delete( opf );
  end;

  if( st == false )
      AbortTrn();
  end;
end;

macro DeleteFromLimitReg( p_recbuf )
  SetBuff( LUOperation, p_recbuf );
  var res = ProcessTrn( 1, "DeleteFromLimitRegTrnProc" );
  if( not res )
    msgbox( "Произошла ошибка при удалении" );
  end;
  return res;
end;

// Выполнение процедуры со статусом <Отложенная> по F2
macro ExecRegLimits( p_recbuf )
  /**
    TO DO
  **/
  var res = UpdateState(p_recbuf, LUOPERATION_STATUS_EXEC);
  if( res )
    msgbox( "Процедура выполнена успешно" );
  end;
end;

// Откат процедуры со статусом <Выполненная> по ALT-F8
macro RollbackRegLimits( p_recbuf )
  /**
    TO DO
  **/
  var res = UpdateState(p_recbuf, LUOPERATION_STATUS_INIT);
  if( res )
    msgbox( "Откат процедуры выполнен успешно" );
  end;
end;