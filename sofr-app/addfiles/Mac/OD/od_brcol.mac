
/*
**  Опердневник ф.24. Сбор данных для сводного дневника
**
*/

import od_com, od_total;

class ( TDataCollector ) TBrigadeDC

  macro processDataRec
  end;

  macro processDataRecs( type, subType, currCode, processing )

    var
      recFound, 
      minProc = 0, maxProc = 32767; 

    if ( currCode == null )
      currCode = NATIONAL_CURR;
    end;

    if ( processing != null )
      minProc = maxProc = processing;
    end;

    odData.keyNum = 1;
    odData.clear;
    odData.rec.Date = {curdate};
    odData.rec.Brigade = operBrigade;
    odData.rec.Mode = mode;
    odData.rec.Type = type;
    odData.rec.CurrCode = currCode;
    odData.rec.SubType = subType;
    odData.rec.Processing = minProc;

    odData.addFilter( "t.t_Date = " + sqlDateToStr( {curdate} ) +
                      " and t.t_Brigade = " + string( operBrigade ) +
                      " and t.t_Mode = " + string( mode ) +
                      " and t.t_Type = " + string( type ) +
                      " and t.t_CurrCode = " + string( currCode ) +
                      " and t.t_SubType = " + string( subType ) +
                      " and t.t_Processing between  " + string( minProc ) + " and " + string( maxProc ) +
                      " and ( t.t_Oper <= " + string( maxOper ) + " or t.t_Oper = " + string( noOper ) + " )" );

    recFound = odData.getGE;
    while ( recFound  
      and ( odData.rec.Date == {curdate} )
      and ( odData.rec.Brigade == operBrigade )
      and ( odData.rec.Mode == mode )
      and ( odData.rec.Type == type ) 
      and ( odData.rec.CurrCode == currCode )
      and ( odData.rec.SubType == subType )
      and ( odData.rec.Processing <= maxProc ) )
      if ( ( odData.rec.oper <= maxOper ) or ( odData.rec.oper == noOper ) )
        processDataRec;
      end;
      recFound = odData.next;
    end;

    odData.dropFilter;
  end;

  initTDataCollector( 0 );
end;

class ( TBrigadeDC ) TBrigadeDC_ValTurns

  var
    credit, debet;

  macro processDataRec 

    var
      v = TRecHandler( "od_data.3" );

    v.setRecordAddr( odData );
    credit = credit + v.rec.Credit; 
    debet = debet + v.rec.Debet; 
  end;

  macro processSubType( subType )

    var
      v = TRecHandler( "od_data.3" );

    credit = debet = $0;
    processDataRecs( ODT_VAL_TOTAL, subType );
    
    v.setRecordAddr( odData );
 
    initDataRec( ODT_VAL_TOTAL, subType, 0 );

    v.clear;
    v.rec.Credit = credit;
    v.rec.Debet = debet;

    odData.insert( v.recSize );
  end;

  macro proceed

    message( "Сбор данных: Обороты кассы - ценности" );
    processSubType( ODSTT_TURNS_TOTAL );
    processSubType( ODSTT_TURNS_DEP );
    processSubType( ODSTT_TURNS_OTHER );
    processSubType( ODSTT_TURNS_EXCHANGE );
    processSubType( ODSTT_TURNS_CONV );
    message( "" );
  end;

  initTBrigadeDC;
end;

class ( TBrigadeDC ) TBrigadeDC_MoneyTurns

  var
    credit, debet, memCredit, memDebet, 
    creditR, debetR, memCreditR, memDebetR;

  macro processDataRec 

    var
      v = TRecHandler( "od_data.2" );

    v.setRecordAddr( odData );
    credit = credit + v.rec.Credit; 
    debet = debet + v.rec.Debet; 
    memCredit = memCredit + v.rec.MemCredit; 
    memDebet = memDebet + v.rec.MemDebet; 
    creditR = creditR + v.rec.CreditR; 
    debetR = debetR + v.rec.DebetR; 
    memCreditR = memCreditR + v.rec.MemCreditR; 
    memDebetR = memDebetR + v.rec.MemDebetR; 
  end;

  macro processSubType( subType )

    var
      v = TRecHandler( "od_data.2" );

    credit = debet = memCredit = memDebet = 
      creditR = debetR = memCreditR = memDebetR = $0;
    processDataRecs( ODT_TOTAL, subType, currency.rec.Code_Currency );
    
    v.setRecordAddr( odData );
 
    initDataRec( ODT_TOTAL, subType, currency.rec.Code_Currency );

    v.clear;
    v.rec.Credit = credit;
    v.rec.Debet = debet;
    v.rec.MemCredit = memCredit;
    v.rec.MemDebet = memDebet;
    v.rec.CreditR = creditR;
    v.rec.DebetR = debetR;
    v.rec.MemCreditR = memCreditR;
    v.rec.MemDebetR = memDebetR;

    odData.insert( v.recSize );
  end;

  macro processSubTypes( offs )

    processSubType( offs + ODSTT_TURNS_TOTAL );
    processSubType( offs + ODSTT_TURNS_DEP );
    processSubType( offs + ODSTT_TURNS_OTHER );
    processSubType( offs + ODSTT_TURNS_EXCHANGE );
    processSubType( offs + ODSTT_TURNS_CONV );
  end;

  macro proceed
    
    message( "Сбор данных: Обороты кассы - деньги" );
    processSubTypes( 0 );
    processSubTypes( ODSTT_UNPAY_OFFS );
    message( "" );
  end;

  initTBrigadeDC;
end;

class ( TBrigadeDC ) TBrigadeDC_Operations( _opSubType, _totSubType, _desc )

  var
    desc = _desc, 
    opSubType = _opSubType, 
    totSubType = _totSubType, 
    totals = TSubTotals; 

  macro processDataRec

    var
      v = TRecHandler( "od_data.1" );

    v.setRecordAddr( odData );

    odData.getPos;
    odData.rec.SubType = opSubType;
    v.rec.Ground = "Итого по операционисту " + odData.rec.Oper;
    odData.rec.Oper = 0;
    odData.insert( v.recSize );
    odData.getDirect; 

    totals.credit = totals.credit + v.rec.Credit;
    totals.debet = totals.debet + v.rec.Debet;
    totals.memCredit = totals.memCredit + v.rec.MemCredit;
    totals.memDebet = totals.memDebet + v.rec.MemDebet;
    totals.creditR = totals.creditR + v.rec.CreditR;
    totals.debetR = totals.debetR + v.rec.DebetR;
    totals.memCreditR = totals.memCreditR + v.rec.MemCreditR;
    totals.memDebetR = totals.memDebetR + v.rec.MemDebetR;
    totals.valCredit = totals.valCredit + v.rec.ValCredit;
    totals.valDebet = totals.valDebet + v.rec.ValDebet;
    totals.valCreditCV = totals.valCreditCV + v.rec.ValCreditCV;
    totals.valDebetCV = totals.valDebetCV + v.rec.ValDebetCV;
  end;

  macro proceed

    message( "Cбор данных: " + desc );
    totals.reset;
    processDataRecs( ODT_OPERATION, totSubType, currency.rec.Code_Currency );
    totals.write( totSubType );
  end;

  initTBrigadeDC;
  if ( desc == null )
    desc = "";
  end;
end;

class ( TBrigadeDC_Operations ) TBrigadeDC_Dep

  initTBrigadeDC_Operations( ODSTO_DEP_OP, ODSTO_DEP_TOT, "Вклады" )
end;

class ( TBrigadeDC_Operations ) TBrigadeDC_Conv

  initTBrigadeDC_Operations( ODSTO_CONV_OP, ODSTO_CONV_TOT, "Конверсионные операции" )
end;

class ( TBrigadeDC ) TBrigadeDC_Exch                  

  var
    totals = TSubTotals;

  macro processDataRec

    var
      v = TRecHandler( "od_data.1" );

    v.setRecordAddr( odData );

    odData.getPos;
    v.rec.Ground = v.rec.Ground + " операционист " + odData.rec.Oper;
    odData.rec.Oper = 0;
    odData.insert( v.recSize );
    odData.getDirect; 

    totals.credit = totals.credit + v.rec.Credit/* SCR9795 + v.CreditRDiff*/;
    totals.debet = totals.debet + v.rec.Debet/* SCR9795 + v.DebetRDiff*/;
    totals.memCredit = totals.memCredit + v.rec.MemCredit/* SCR9795 + v.MemCreditRDiff*/;
    totals.memDebet = totals.memDebet + v.rec.MemDebet/* SCR9795 + v.MemDebetRDiff*/;
    totals.creditR = totals.creditR + v.rec.CreditR;
    totals.debetR = totals.debetR + v.rec.DebetR;
    totals.memCreditR = totals.memCreditR + v.rec.MemCreditR;
    totals.memDebetR = totals.memDebetR + v.rec.MemDebetR;
    totals.valCredit = totals.valCredit + v.rec.ValCredit;
    totals.valDebet = totals.valDebet + v.rec.ValDebet;
    totals.valCreditCV = totals.valCreditCV + v.rec.ValCreditCV;
    totals.valDebetCV = totals.valDebetCV + v.rec.ValDebetCV;
  end;

  macro proceed

    message( "Сбор данных: Валютообменные операции" );
    totals.reset;
    processDataRecs( ODT_OPERATION, ODSTO_EXCHANGE_OP, currency.rec.Code_Currency );
    totals.write( ODSTO_EXCHANGE_TOT );
    message( "" );
  end;

  initTBrigadeDC;
end;

class ( TBrigadeDC ) TBrigadeLabeledDC( _opSubType, _totSubType )

  var
    opSubType = _opSubType, totSubType = _totSubType, 
    vr = TRecHandler( "od_data.1" ), 
    prevLabel = "", prevOper = -1,  
    totals = TSubTotals;

  macro processDataRec_subTotal

    var
      v = TRecHandler( "od_data.1" );

    v.setRecordAddr( odData );

    totals.credit = totals.credit + v.rec.Credit;
    totals.debet = totals.debet + v.rec.Debet;
    totals.memCredit = totals.memCredit + v.rec.MemCredit;
    totals.memDebet = totals.memDebet + v.rec.MemDebet;
    totals.creditR = totals.creditR + v.rec.CreditR;
    totals.debetR = totals.debetR + v.rec.DebetR;
    totals.memCreditR = totals.memCreditR + v.rec.MemCreditR;
    totals.memDebetR = totals.memDebetR + v.rec.MemDebetR;
    totals.valCredit = totals.valCredit + v.rec.ValCredit;
    totals.valDebet = totals.valDebet + v.rec.ValDebet;
    totals.valCreditCV = totals.valCreditCV + v.rec.ValCreditCV;
    totals.valDebetCV = totals.valDebetCV + v.rec.ValDebetCV;
  end;

  macro processDataRec_transfer

    var
      v = TRecHandler( "od_data.1" );

    odData.getPos;

    v.setRecordAddr( odData );
    v.rec.Ground = v.rec.Ground + " операционист " + odData.rec.Oper;
    odData.rec.Oper = 0;
    odData.rec.Time = time( 0, 0, 0 );

    odData.insert( v.recSize );

    odData.getDirect;
  end;

  macro makeGround( label, oper )

    return "Итого по операционисту " + oper;    
  end;

  macro savePrevRec

    var
      v = TRecHandler( "od_data.1" );

    odData.getPos;

    initDataRec( 
      ODT_OPERATION, 
      opSubType, 
      currency.rec.Code_Currency );

    odData.rec.Label = prevLabel;

    v.setRecordAddr( odData );
    copy( v, vr );
    v.rec.ground = makeGround( prevLabel, prevOper );
    odData.insert( v.recSize );
    odData.getDirect;
  end;

  macro processDataRec_sumUp

    var
      v = TRecHandler( "od_data.1" );

    if ( ( odData.rec.Label != prevLabel ) or ( odData.rec.Oper != prevOper ) )
      if ( prevLabel != "" )
        savePrevRec;
      end;
      prevLabel = odData.rec.Label;
      prevOper = odData.rec.Oper;
      vr.clear;
    end;

    v.setRecordAddr( odData );
    vr.rec.Credit = vr.rec.Credit + v.rec.Credit;
    vr.rec.Debet = vr.rec.Debet + v.rec.Debet;
    vr.rec.MemCredit = vr.rec.MemCredit + v.rec.MemCredit;
    vr.rec.MemDebet = vr.rec.MemDebet + v.rec.MemDebet;
    vr.rec.CreditR = vr.rec.CreditR + v.rec.CreditR;
    vr.rec.DebetR = vr.rec.DebetR + v.rec.DebetR;
    vr.rec.MemCreditR = vr.rec.MemCreditR + v.rec.MemCreditR;
    vr.rec.MemDebetR = vr.rec.MemDebetR + v.rec.MemDebetR;
    vr.rec.ValCredit = vr.rec.ValCredit + v.rec.ValCredit;
    vr.rec.ValDebet = vr.rec.ValDebet + v.rec.ValDebet;
    vr.rec.ValCreditCV = vr.rec.ValCreditCV + v.rec.ValCreditCV;
    vr.rec.ValDebetCV = vr.rec.ValDebetCV + v.rec.ValDebetCV;
  end;

  macro processDataRec
  
    if ( odData.rec.SubType == totSubType )
      processDataRec_subTotal;
    else
      if ( odData.rec.Processing == PROC_TRANSFER )
        processDataRec_transfer;
      elif ( odData.rec.Processing == PROC_SUM_UP )
        processDataRec_sumUp;
      end;
    end;
  end;

  macro proceed

    prevLabel = "";
    prevOper = 0; 
    vr.clear;
    totals.reset;
    processDataRecs( ODT_OPERATION, totSubType, currency.rec.Code_Currency );
    totals.write( totSubType );
    processDataRecs( ODT_OPERATION, opSubType, currency.rec.Code_Currency, PROC_TRANSFER );
    processDataRecs( ODT_OPERATION, opSubType, currency.rec.Code_Currency, PROC_SUM_UP );
    if ( prevLabel != "" )
      savePrevRec;
    end;
  end;

  initTBrigadeDC;
end;

class ( TBrigadeLabeledDC ) TBrigadeDC_Other                  

  macro makeGround( label, oper )

    var
      lType = getLabelField( label, 0 ), 
      lSubType, 
      kkmNo, 
      op, 
      ciType, 
      ground = "";

    if ( lType == "oth" )
      ground = "Прочие операции";
      kkmNo = getLabelField( label, 1 );
      if ( kkmNo != "" )  
        ground = ground + " ( ККМ № " + kkmNo + ")";
      else
        ground = ground + " (без ККМ)";
      end;
    elif ( lType == "cis" )
      ciType = getLabelField( label, 1 );
      lSubType = getLabelField( label, 2 );
      op = getLabelField( label, 3 );

      if ( findCIOperation( ciType, op ) )
        ground = ciOper.rec.Name + " ";
      else
        ground = "* Неизвестная операция * ";
      end;

      if ( ciType == CI_OGSZ )
        if ( findOGSZIssue( ciType, lSubType ) )
          ground = ground + "ОГСЗ " + lSubType + " серии, номинал " + ogszIssue.rec.Par;
        end;
      elif ( ciType == CI_CERT )
        ground = ground + "Сбер. сертификат " + lSubType + "г.";
      else
        if ( findCIMisc( ciType, lSubType ) )
          ground = ground + ciMisc.rec.Name;
        end;
      end;  

    end;                         
    ground = ground + " операционист " + oper;

    return ground;
  end;

  macro proceed

    message( "Сбор данных: Прочие операции" ); 
    proceed;
    message( "" );
  end;

  initTBrigadeLabeledDC( ODSTO_OTHER_OP, ODSTO_OTHER_TOT );
end;

class ( TBrigadeLabeledDC ) TBrigadeDC_FBDep

  var
    prevBranch;

  macro saveBranchSubTotal

    var
      v = TRecHandler( "od_data.1" );

    var
      branchName;

    v.setRecordAddr( odData );

    odData.getPos;

    initDataRec( ODT_OPERATION, 
                 ODSTO_FBDEP_BR_TOT_BASE + prevBranch, 
                 currency.rec.Code_Currency );
    getBranchParams( prevBranch, branchName );
    copy( v, vr );
    v.rec.Ground = "  - по филиалу " + branchName + ":";
    odData.insert( v.recSize );
    odData.getDirect; 
  end;

  macro processBranchSubTotal

    var
      v = TRecHandler( "od_data.1" );

    v.setRecordAddr( odData );

    if ( odData.rec.SubType != ODSTO_FBDEP_BR_TOT_BASE + prevBranch )
      if ( prevBranch != -1 )
        saveBranchSubTotal;
      end;
      prevBranch = odData.rec.SubType - ODSTO_FBDEP_BR_TOT_BASE;
      vr.clear;
    end;

    vr.rec.Credit = vr.rec.Credit + v.rec.Credit;
    vr.rec.Debet = vr.rec.Debet + v.rec.Debet;
    vr.rec.MemCredit = vr.rec.MemCredit + v.rec.MemCredit;
    vr.rec.MemDebet = vr.rec.MemDebet + v.rec.MemDebet;
    vr.rec.CreditR = vr.rec.CreditR + v.rec.CreditR;
    vr.rec.DebetR = vr.rec.DebetR + v.rec.DebetR;
    vr.rec.MemCreditR = vr.rec.MemCreditR + v.rec.MemCreditR;
    vr.rec.MemDebetR = vr.rec.MemDebetR + v.rec.MemDebetR;
    vr.rec.ValCredit = vr.rec.ValCredit + v.rec.ValCredit;
    vr.rec.ValDebet = vr.rec.ValDebet + v.rec.ValDebet;
    vr.rec.ValCreditCV = vr.rec.ValCreditCV + v.rec.ValCreditCV;
    vr.rec.ValDebetCV = vr.rec.ValDebetCV + v.rec.ValDebetCV;
  end;

  macro processBranchSubTotals

    var
      recFound;

    vr.clear;
    prevBranch = -1;

    odData.keyNum = 1;
    odData.clear;
    odData.rec.Date = {curdate};
    odData.rec.Brigade = operBrigade;
    odData.rec.Mode = mode;
    odData.rec.Type = ODT_OPERATION;
    odData.rec.CurrCode = currency.rec.Code_Currency;
    odData.rec.SubType = ODSTO_FBDEP_BR_TOT_BASE;

    odData.addFilter( "t.t_Date = " + sqlDateToStr( {curdate} ) +
                      " and t.t_Brigade = " + string( operBrigade ) +
                      " and t.t_Mode = " + string( mode ) +
                      " and t.t_Type = " + string( ODT_OPERATION ) +
                      " and t.t_CurrCode = " + string( currency.rec.Code_Currency ) +
                      " and t.t_SubType between " + string( ODSTO_FBDEP_BR_TOT_BASE ) + " and " + string( ODSTO_FBDEP_BR_TOT_BASE + 999 ) +
                      " and t.t_Oper <= " + string( maxOper ) );

    recFound = odData.getGE;
    while ( recFound  
      and ( odData.rec.Date == {curdate} )
      and ( odData.rec.Brigade == operBrigade )
      and ( odData.rec.Mode == mode )
      and ( odData.rec.Type == ODT_OPERATION ) 
      and ( odData.rec.CurrCode == currency.rec.Code_Currency )
      and ( odData.rec.SubType <= ODSTO_FBDEP_BR_TOT_BASE + 999 ) )
      if( odData.rec.oper <= maxOper )
        processBranchSubTotal;
      end;
      recFound = odData.next;
    end;

    odData.dropFilter;

    if ( prevBranch != -1 )
      saveBranchSubTotal;
    end;
  end;

  macro makeGround( label, oper )

    var
      lType = getLabelField( label, 0 ), 
      destBr, destBrName, 
      ground = ""; 

    if ( lType == "fbd" )
      destBr = int( getLabelField( label, 1 ) );
      getBranchParams( destBr, destBrName );
      ground = "За филиал " + destBrName;
    end;                         
    ground = ground + " операционист " + oper;

    return ground;
  end;

  macro proceed

    message( "Сбор данных: Вклады. Операции за другие филиалы" ); 
    proceed;
    processBranchSubTotals;

    message( "" );
  end;

  initTBrigadeLabeledDC( ODSTO_FBDEP_OP, ODSTO_FBDEP_TOT );
end;