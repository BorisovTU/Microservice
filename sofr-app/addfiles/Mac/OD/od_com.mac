
/*
**  Опердневник ф.24. Общие функции и определения
**
**
*/

import alg_vars, CommonInter, BankInter, contain, cur_rate, brParm, sqlConv, rsd;

var 
  odData;

/* Включить индивидуальные опердневники в сводный */

const 
  EXPANDED_BRIGADE_REPORT = true;

/* Не бить на листы */

const
  noPages = false;

/* Строк на листе */

const 
  LINES_PER_PAGE = 60;

const
  N_SYS_LINES = 5;

/* Предопределенные коды валют */

const
  ANY_CURR  = -1,     /* Любая валюта */
  NATIONAL_CURR = 0;    /* Национальная валюта */

/* Виды записей ОД */

const
  ODT_OPERATION = 1,    /* Операция, сводная запись, подытог, итог */
  ODT_TOTAL     = 2,    /* Итоги - деньги */
  ODT_VAL_TOTAL = 3,    /* Итоги - ценности */
  ODT_ON_ACC  = 4;    /* Подотчетная ценность */

/* Подвиды записей ОД */

const
  /* Для ODT_OPERATION */
  ODSTO_DEP_OP    = 1,    /* Операция/сводная запись по вкладам */
  ODSTO_DEP_TOT   = 100,    /* Итого по вкладам */
  ODSTO_OTHER_OP  = 2,    /* Операция/сводная запись по прочим операциям */
  ODSTO_OTHER_TOT = 200,    /* Итого по прочим */
  ODSTO_CI_TOT_BASE     = 200,    /* + capIssue.Type - итого по виду ЦБ */
  ODSTO_OTHEROTHER_TOT  = 290,    /* Итого по прочим операциям */  
  ODSTO_EXCHANGE_OP = 3,    /* Операция/сводная запись ОП */
  ODSTO_EXCHANGE_TOT  = 300,    /* Итого по ОП */  
  ODSTO_CONV_OP   = 4,    /* Операция/сводная запись по конверсиям */
  ODSTO_CONV_TOT  = 400,    /* Итого по конверсиям */  
  ODSTO_FBDEP_OP  = 5,    /* Операция/сводная запись по вкладной операции по другому филиалу */
  ODSTO_FBDEP_TOT = 500,    /* Итого по вкладной операциям по другому филиалу */  
  ODSTO_FBID_OP   = 6,    /* Операция/сводная запись по вкладной операции, выполненной другим филиалам */
  ODSTO_FBID_TOT  = 600,    /* Итого по вкладной операциям, выполненным другими филиалами  */
  ODSTO_FBID_BR_TOT_BASE  = 1000,   /* + branch - итого выполненно данным филиалом по счетам нашего */
  ODSTO_FBDEP_BR_TOT_BASE = 2000,   /* + branch - итого выполненно по данному филиалу */
  ODSTO_CI_OP_TOT_BASE  = 20000,  /* + 100 * capIssue.Type + operation - итого по виду операции по ЦБ */

  /* Для ODT_[VAL_]TOTAL */
  ODSTT_PREV_REST       = 1,    /* Остаток на начало дня */
  ODSTT_PREV_COIN_REST  = 2,    /* Остаток на начало дня - монеты */
  ODSTT_TURNS_TOTAL = 11,     /* Обороты по вкладам */
  ODSTT_TURNS_DEP = 12,     /* Обороты по вкладам */
  ODSTT_TURNS_OTHER = 13,     /* Обороты по вкладам */
  ODSTT_TURNS_EXCHANGE  = 14,     /* Обороты по вкладам */
  ODSTT_TURNS_CONV  = 15,     /* Обороты по вкладам */
  ODSTT_TURNS_FBDEP = 16,     /* Обороты по вкладным операциям по другим филиалам */
  ODSTT_REST      = 21,     /* Остаток на начало дня */
  ODSTT_COIN_REST = 22,     /* Остаток на начало дня - монеты */
  ODSTT_UNPAY_OFFS      = 100;    /* Для неплатежки добавляется к вышеперечисленным */

/* Виды обработки записи дет. дневника при формировании сводного */

const
  PROC_SKIP     = 0, 
  PROC_TRANSFER = 1, 
  PROC_SUM_UP   = 2; 

/* Дебет/кредит в прочих операциях */

const
  CI_CREDIT = 1,
  CI_DEBET  = 2;

/* Режимы работы группы */

const
  GRM_HAS_SAFE  = 0,     /* Имеет собственный сейф */
  GRM_IMMEDIATE = 1;     /* Ресурсы выдаются непосредственно из хранилища КБ */

/* Типы ресурсов */

const
  TYPERES_CURRENCY    =   1, /* Деньги (из справочника валют)           */
  TYPERES_VALFORM     =   2, /* Ценные бланки                           */
  TYPERES_VALUABLES   =   3, /* Др. ценности (из справочника ценностей) */ 
  TYPERES_PAYDOC      =   4, /* Платежные документы ОП                  */
  TYPERES_CARD        =  10, /* Пластиковые карточки                    */
  TYPERES_MINCAPISSUE = 500; /* Минимально возможный код ЦБ             */

/* Состояния ресурсов */

const
  TYPE_RES_PAY    = 0, /* Платежеспособные ресурсы                     */
  TYPE_RES_NOPAY  = 1, /* Неплатежеспособные ресурсы                   */
  TYPE_RES_DOUBT  = 2, /* Сомнительные ресурсы                         */
  TYPE_RES_SAVE   = 3, /* На хранении                                  */
  TYPE_RES_EXP    = 4, /* На экспертизу                                */
  TYPE_RES_INCASS = 5, /* На инкассо                                   */
  TYPE_RES_PDNAT  = 20,/* ПД, оплаченные рублями(национальной валютой) */
  TYPE_RES_PDCUR  = 21,/* ПД, оплаченные валютой                       */
  TYPE_RES_PD     = 22;/* оплаченные ПД                                */

/* Единицы измерения ресурсов */

const
  RESMESUNIT_POINT = 1,  /* Поштучно   */
  RESMESUNIT_MONEY = 2,  /* Стоимость  */
  RESMESUNIT_MIXED = 3;  /* Cмешанный учет  */

/* Состояния кассовых документов */

const
  TYPEDOC_CARRYOFF = 1,  /* Отложен (не подтвержден) */
  TYPEDOC_CARRYON  = 2;  /* Проведен (подтвержден) */

/* Операции Оперкассы */

const
  CASHOP_REINFORCE  = 1,  /* Подкрепление кассира */
  CASHOP_RETURN     = 2,  /* Возврат средств в кладовую */
  CASHOP_COLLECT  = 3,  /* Инкассация */
  CASHOP_ADVANCE    = 4,  /* Аванс */
  CASHOP_DEBIT      = 5,  /* Расход */
  CASHOP_CREDIT     = 6,  /* Приход */
  CASHOP_REMOVE     = 7,  /* Внутреннее подкрепление (Расход) */
  CASHOP_REMOVE_1   = 8,  /* Внутреннее подкрепление (Приход) */
  CASHOP_CHVALUE    = 9,  /* Перевод на другой ресурс той же группы (Расход) */
  CASHOP_CHVALUE_1  = 10, /* Перевод на другой ресурс той же группы (Приход) */
  CASHOP_REMOVE_SAFE    = 11, /* Внутреннее подкрепление сейфа (Расход) */
  CASHOP_REMOVE_SAFE_1  = 12, /* Внутреннее подкрепление сейфа (Приход) */
  CASHOP_DEBET_SAFE   = 13, /* Расход из сейфа */
  CASHOP_CREDIT_SAFE  = 14, /* Приход в сейф */
  CASHOP_GIVEA    = 15, /* Выдача под отчет (со стороны Оперкассы) */
  CASHOP_RETA     = 16, /* Возврат подотчетной суммы (со стороны оперкассы) */
  CASHOP_REVAL_POS  = 18, /* Переоценка (+) */
  CASHOP_REVAL_NEG  = 19; /* Переоценка (-) */

/* Подвиды операций Оперкассы */

const 
  CASHOP_DEBET_SHORTAGE       = 2,      /* Отражение недостачи */
  CASHOP_CREDIT_OVERPLUS      = 1,      /* Отражение излишков  */
  CASHOP_DEBET_SAFE_SHORTAGE  = 2,      /* Отражение недостачи */
  CASHOP_CREDIT_SAFE_OVERPLUS = 1;      /* Отражение излишков  */

/* Признак "приход/расход" кассового документа */

const
  FLAGDEBCRED_IN   = 1,  /* Кредит */
  FLAGDEBCRED_OUT  = 2;  /* Дебет */

const
  CP_FLAG = 2;  /* Флаг варианта группировки отчета по коммунальным платежам     */
                /* CP_FLAG = 0 - по получателям без группировки (старый вариант) */
                /* CP_FLAG = 1 - группировка по получателям                      */
                /* CP_FLAG = 2 - группировка по видам платежей                   */

/* Виды прочих кассовых операций */

const
  CO_COMMPAY     =   1, /* Коммунальные платежи */
  CO_NALPER      =  11, /* Наличный перевод */
  CO_COMMISSION  =  21, /* Комиссия */
  CO_CONVERSION  =  31, /* Конверсия */
  CO_PAYED       =  41, /* Выплата */
  CO_BANKPAYED   =  51, /* Банковские расчеты */
  CO_OVERCRED    =  61, /* Документ овердрафтного кредитования */
  CO_OTHERBRANCH =  71, /* Документ по другому филиалу */
  CO_METALACCOUNT = 81, /* Операция по металлическому счету */
  CO_TRN         = 100, /* Безналичные платежи */
  CO_TREASURE    = 101; /* Ценности */

/* Виды ЦБ */

const
  CI_OGSZ          = 1,
  CI_LOTTERY       = 2,
  CI_CERT          = 3,
  CI_COIN          = 4,
  CI_INGOT         = 5,
  CI_ORVVZ         = 6,
  CI_MISC          = 10;

/* Виды монет */

const
  CK_NEW          = 0, 
  CK_OLD          = 1;

/* Операции по ценным бумагам и ценностям */

const
  O_OGSZ_SELL      = 1,
  O_OGSZ_BUY       = 2,
  O_OGSZ_PAYWARR   = 3,
  O_OGSZ_KEEP      = 4,
  O_OGSZ_RETURN    = 5,
  O_OGSZ_SELL_ACC  = 6,

  O_LOTTERY_SELL   = 1,
  O_LOTTERY_PAYWIN = 2,
  O_LOTTERY_BUY    = 3,
  O_LOTTERY_KEEP   = 4,
  O_LOTTERY_RETURN = 5,
  O_LOTTERY_SELL_ACC = 6,

  O_CERT_SELL      = 1,
  O_CERT_PAY       = 2,
  O_CERT_PAYALN    = 3,
  O_CERT_KEEP      = 4,
  O_CERT_RETURN    = 5,
  O_CERT_SELL_ACC  = 6,

  O_MISC_SELL      = 1,         /* Для монет и прочих */
  O_MISC_BUY       = 2,
  O_MISC_KEEP      = 3,
  O_MISC_RETURN    = 4,
  O_MISC_SELL_ACC  = 5,

  O_INGOT_SELL     = 1,
  O_INGOT_BUY      = 2,
  O_INGOT_KEEP     = 3,
  O_INGOT_RETURN   = 4,
  O_INGOT_SELL_ACC = 5;

/* Действия над записью */

const
  D_INSERT  = 0,     /* Вставка      */
  D_UPDATE  = 1,     /* Обновить     */
  D_DELETE  = 2,     /* Удалить      */
  D_STORN   = 11;    /* Сторнировать */

/* Флаги вкладного документа */

const
  BIT_FLAG_UNCASH = 31;

/* Виды документов */

const
  SB_INCASH        = 100,
  SB_EXTCASH       = 150, 
  SB_CASHOPERT     = 200;

/* Кодификатор ОП */

const
 CDF_Oper                   = 11,      /* Операции ОП */
   CDF_OperPayDoc           = 2,     /* Операции покупки/продажи платежных документов */
     CDF_OperPayDocBuy      = 3,       /* Покупка платежного документа за рубли */
     CDF_OperPayDocSale     = 4,       /* Продажа платежного документа за рубли */
     CDF_OperPayDocBuyCurr  = 20,      /* Покупка платежного документа за валюту */
     CDF_OperPayDocSaleCurr = 21,      /* Продажа платежного документа за валюту */
   CDF_OperCurrChange       = 5,     /* Операции обмена валюты */
     CDF_OperUnpayCurrBuy   = 1,       /* Покупка неплатежеспособной валюты за рубли */
     CDF_OperCurrBuy        = 6,       /* Покупка платежеспособной валюты за рубли */
     CDF_OperCurrSale       = 7,       /* Продажа платежеспособной валюты за рубли */
     CDF_OperCurrConversion = 18,      /* Конверсия */
   CDF_OperTreasure         = 8,     /* Операции передачи ценностей */
     CDF_OperTransfer       = 9,       /* Передача ценности между кассирами */
     CDF_OperAvance         = 10,      /* Получение аванса */
     CDF_OperCashOut        = 11,      /* Сдача кассы */
     CDF_OperEvenRests      = 36,      /* Выравнивание остатков с Кассой Retail */
   CDF_OperCard             = 12,    /* Операции по картам */
     CDF_OperCardTake       = 13,       /* Начисление на карту */
     CDF_OperCardGive       = 14,       /* Снятие с карты */
   CDF_OperIncassoExpert    = 15,    /* Операции приема на инкассо или экспертизу */
     CDF_OperIncasso        = 16,       /* Прием на инкассо */
     CDF_OperExpert         = 17,       /* Прием на экспертизу */
     CDF_OperReturnIncasso  = 24,       /* Возврат с инкассо */
     CDF_OperReturnExpert   = 25,       /* Возврат с экспертизы */
     CDF_OperPayDocIncasso       = 27,  /* Прием ПД на инкассо     RA 11-06-99 */
     CDF_OperPayDocExpert        = 28,  /* Прием ПД на экспертизу  RA 11-06-99 */
     CDF_OperPayDocReturnIncasso = 29,  /* Возврат ПД с инкассо    RA 11-06-99 */
     CDF_OperPayDocReturnExpert  = 30,  /* Возврат ПД с экспертизы RA 11-06-99 */

     CDF_OperCheck        = 26,         /* Проверка на подлинность RA 30-07-99 */

   CDF_OperSplitChange   = 19,       /* Операции размена или замены */
     CDF_OperSplit        =  22,        /* Размен валюты */
     CDF_OperChange       =  23,        /* Замена неплатежеспособной валюты */

   CDF_OperDeficitOverplus  = 31,    /* Операции учета недостачи и излишков */
     CDF_OperDetectOverplus    = 32,    /* Отражение излишков               */
     CDF_OperGiveOutOverplus   = 33,    /* Выдача излишков                  */
     CDF_OperDetectDeficit     = 34,    /* Отражение недостачи              */
     CDF_OperCompensateDeficit = 35,    /* Возмещение недостачи             */

   CDF_OperPayDocAcc        = 37,    /* Операции покупки/продажи ПД через счет */
     CDF_OperPayDocBuyAcc      = 38,    /* Покупка ПД за рубли через счет   */
     CDF_OperPayDocSaleAcc     = 39,    /* Продажа ПД за рубли через счет   */
     CDF_OperPayDocBuyCurrAcc  = 40,    /* Покупка ПД за валюту через счет  */
     CDF_OperPayDocSaleCurrAcc = 41,    /* Продажа ПД за валюту через счет  */

    /* RA 23-12-99 "операции" - комиссии (нужны для контировки в Retail'е)  */
   CDF_OperComiss           = 90,
     CDF_OperFixCom            = 98,    /* Фиксированная комиссия           */
     CDF_OperPercCom           = 99,    /* Процентная комиссия              */


/* состояние справки БСО */
  CDF_SprvState   = 5,      /* Состояние справки БСО */
     CDF_SprvGetOut = 1,    /* Выдана     */
     CDF_SprvKill   = 2,    /* Уничтожена */
     CDF_SprvIncome = 3,    /* Получена   */

  CDF_SprvPackState = 22,    /* Состояние пачки справок БСО */
     CDF_SprvPackCashOffice = 1,    /* Касса                */
     CDF_SprvPackAvance     = 2,    /* Аванс                */
     CDF_SprvPackReturn     = 3,    /* Возврат\Передача     */

/* типы оборотов */
  CDF_Turn = 23,  /* Типы оборотов по операциям */
    CDF_TurnIncome        = 1, /* Полученная от клиента сумма  */
    CDF_TurnOut           = 2, /* Выданная клиенту сумма       */
    CDF_TurnCom           = 3, /* Общая комиссия               */
    CDF_TurnFix           = 4, /* Фиксированная комиссия       */
    CDF_TurnTaxWithCom    = 5, /* Налог с учетом комиссии      */
    CDF_TurnTaxWithoutCom = 6, /* Налог без учета комиссии     */
    CDF_TurnPComInc       = 7, /* Сумма процентной комиссии от полученной суммы    */
    CDF_TurnPComOut       = 8, /* Сумма процентной комиссии от выданной суммы      */
    CDF_TurnCnvCoins      = 9, /* Сумма от безналичной конвертации валютной мелочи */

  /*18-02-98  документы физического лица - кодификатор */
  CDF_PersonDoc = 10, /*группа в кодификаторе*/
     CDF_Residence      = 1,  /* Вид на жительство               */
     CDF_WarTicket      = 2,  /* Военный билет                   */
     CDF_PersDocOfficer = 3,  /* Удостоверение личности офицера  */
     CDF_ForeignPass    = 4,  /* Заграничный паспорт             */
     CDF_Passport       = 5,  /* Паспорт РФ                      */
     CDF_PassportUSSR   = 6,  /* Паспорт СССР                    */

  CDF_ReestrState = 13,  /* Состояние реестра                    */
     CDF_ReestrOpen     = 1,  /* Открыт                          */
     CDF_ReestrClosed   = 2,  /* Закрыт                          */
     CDF_ReestrStorno   = 3,  /* Сторнирован                     */

  /*AV 24-04-98*/
  CDF_ComType  = 17,  /* Типы комиссии */
     CDF_ComPercLTFix  = 1,   /* %, но не более фиксированной суммы   */
     CDF_ComPercGTFix  = 2,   /*   %, но не менее фиксированной суммы */
     CDF_ComPercAndFix = 3,   /*   Сумма % и фиксированной суммы      */
     CDF_ComFix        = 4,   /*   Фиксированная сумма                */
     CDF_ComNotUsed    = 5,   /*   Комиссии нет                       */
  /*AV 27-05-98*/
  CDF_TypeCalcRate = 25,  /* Тип пересчета сумм */
     CDF_CalcRateOper  = 0, /* По курсу операции */
     CDF_CalcRateCB    = 1, /* По курсу ЦБ       */
     CDF_CalcRateBuy   = 2, /* По курсу покупки  */
     CDF_CalcRateSale  = 3, /* По курсу продажи  */
  /*AV 19-06-98 */
  CDF_RateChngState    = 14,/* Состояние смены */
     CDF_CHANGE_ACTIVE  = 1,  /* Смена активна          */
     CDF_CHANGE_LOCKED  = 2,  /* Смена блокирована      */
     CDF_CHANGE_STOPED  = 3,  /* Смена завершила работу */
     CDF_CHANGE_NOBEG   = 4,  /* Смена не началась      */
  /*RA 18-12-98 */
  CDF_OperState         = 12, /* Состояние операции, проведенной в ОП */
     CDF_OperCompleted  =  1, /*   Завершена            */
     CDF_OperStarted    =  2, /*   Начата               */
     CDF_OperStorn      =  3, /*   Откатана             */
     CDF_OperCancel     =  4, /*   Отменена             */
     CDF_OperReestr     =  5, /*   В реестре            */
        CDF_OperInReestr=  6, /*     В реестре          */
        CDF_OperDeleted =  7; /*     Удалена            */

/* Виды плат и налогов */

const
  COMM_COMMISSION = 1;

const
  maxOper = 32760, 
  noOper  = maxOper + 1,  /* Псевдо-опер для операций, не отнесенных к конкретному оперу (аванс, инкассация и т.п.) */
  noOper2 = maxOper + 2;        /* Псевдо-опер для операций других филиалов */

var
  {MakeDeskDocs}, 
  {CNum},  
  mode = getCurrentMode, 
  branch = numFNCash, 
  isCur, 
  bankStd, 
  operBrigade = getOperBrigade,  
  dbDir = "", //getIniString( "DATABASEDIR" ), 
  workDir = getRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\WORKDIR", true );

var
  ci            = TBFile( "capissue.dbt", "r", 0 ), 
  ciOper        = TBFile( "ci_oper.dbt",  "r", 0 ), 
  ogszIssue     = TBFile( "ogsz_iss",     "r", 0 ), 
  ciMisc        = TBFile( "ci_misc.dbt",  "r", 0 ), 
  pd            = TBFile( "paydoc.dbt",   "r", 0, null, "exchange.def" ), 
  currency      = TBFile( "currency.dbt", "r", 0 ), 
  group         = TBFile( "rtgroup.dbt",  "w", 0 ), 
  person        = TBFile( "person.dbt",   "r", 0, null, "bank.def" ), 
  alg           = TBFile( "namealg.dbt",  "r", 0, null, "bank.def" ), 
  dep           = TBFile( "depositr.dbt", "r", 0 ), 
  depOpTypes    = TBFile( "sb_typop.dbt", "r", 0 ),
  depSubOpTypes = TBFile( "suboper.dbt",  "r", 0 ), 
  grpMember     = TBFile( "groupmem.dbt", "w", 0 );

var
  cbRate, buyRate, sellRate, rateScale;

var
  newDataFile = false, 
  opsForCurrReported,
  opsForOperReported;

var
  maxTime = time( 23, 59, 59 );

var
  pageCounter = 0, 
  lineCounter = 0, 
  linesToPrint;

var
  currencyList = TArray,       // Список валют, по которым идет обработка
  currencyListCreated = false;

/* Базовый класс сборщика данных */

class TDataCollector( _oper )

  var
    oper = _oper;

  macro proceed
  end;
end;

/* Выход по ошибке */

macro exitOnError( msg )

  msgBox( msg );
  exit( 1 );
end;

/* Выход по ошибке Btrieve */

macro exitOnBtError( msg )

  var
    st;

  status( st );
  msg = msg + "|" + st;
  exitOnError( msg );
end;

/* Число в n-значную строку */

macro num2string( m, n )

  var
    s = string( m );

  while ( strLen( s ) < n )
    s = "0" + s;
  end;
  return s;
end;

/* Открытие или создание файла данных */

macro openDataFile

  var
    pathname = dbDir + "od_data." + num2string( branch, 3 );

  if ( odData == null )
    odData = TBFile( "od_data.dbt", "wp", 0, pathname );
  end;
end;

macro copyArrayToTArray( a, ta )

  var
    i = 0;

  while ( i < aSize( a ) )
    ta[i] = a[i];                  
    i = i + 1;
  end;
end;

/* Найти группу пользователей */

macro findGroup( brigade )

  if ( (group.rec.branch!=branch) or 
       (group.rec.type!=1) or
       (group.rec.Date!={curdate}) or
       (group.rec.ID!=brigade) )

     group.rec.branch = branch;
     group.rec.type   = 1;     /* GT_BRIGADE */
     group.rec.Date   = {curdate};
     group.rec.ID     = brigade;
     return group.getEQ; 
  else
      return true;
  end;
end;

macro findCIMisc( type, issueID )

  ciMisc.rec.Type = type;
  ciMisc.rec.IssueID = issueID;
  return ciMisc.getEQ;
end;

macro findOGSZIssue( type, issueID )

  ogszIssue.rec.Type = type;
  ogszIssue.rec.IssueID = issueID;
  return ogszIssue.getEQ;
end;

macro isCoinOld( issue )

 findCIMisc( CI_COIN, issue );
 return ciMisc.rec.KindOfCoin == CK_OLD; 
end;

macro cur2rub( sum )

  return Sumconv( sum, cbRate );
end;

macro getRate

  getAllCurrRate( {curdate}, currency.rec.Code_Currency, cbRate, buyRate, sellRate );
  if ( currency.rec.ScaleOfc )
    rateScale = currency.rec.ScaleOfc;
  else 
    rateScale = 1;
  end;
end;

macro findDep( ref )

  var 
    saveKey = dep.KeyNum,
    stat;

  dep.keyNum = 8;
  dep.clear;
  dep.rec.Referenc = ref;
  stat = dep.getEQ;    
  dep.KeyNum = saveKey;
  return stat;
end;

/* Очистка данных */

macro cleanupDataFile( oper )

  var
    recFound;

  var cmd, fName=string( "d", StrSubst(odData.FileName, ".", "_") );

  message( "Очистка данных" );

  cmd = RsdCommand( NULL, string("delete from ", fName, 
                                 " t where t.t_Date=",sqlDateToStr({curdate}),
                                 " and t.t_brigade=", operBrigade,
                                 " and t.t_Mode=", mode,
                                 " and t.t_Oper=", Oper) );
  cmd.execute();


  message( "" );
end;

/* Инициализация данных */

macro initDataRec( type, subType, currCode, oper, time )

  odData.clear;
  odData.rec.Date = {curdate};
  odData.rec.Brigade = operBrigade;
  odData.rec.mode = mode;
  if ( oper != null )
    odData.rec.oper = oper;
  end;
  odData.rec.Type = type;
  odData.rec.SubType = subType;
  if ( currCode != null )
    odData.rec.CurrCode = currCode;
  end;
  if ( time != null )
    odData.rec.Time = time;
  end;
end;

/* Получить запись */

macro getDataRec( type, subType, currCode, oper )

  odData.keyNum = 0;
  odData.clear;
  odData.rec.Date = {curdate};
  odData.rec.Brigade = operBrigade;
  odData.rec.Mode = mode;
  if ( oper != null )
    odData.rec.oper = oper;
  end;
  odData.rec.Type = type;
  if ( currCode != null )
    odData.rec.CurrCode = currCode;
  end;
  odData.rec.SubType = subType;
  return odData.getEQ;
end;

/* Получить номер опердневника */

macro getDiaryNumber

  var cmd, rs;

  cmd = RsdCommand( NULL, String("select t.t_NumOperDy as NumOperDy from ddepparm_dbt t ",
                                 "where t.t_FNCash=:FNCash and t.t_FlagCur=:FlagCur") );

  rs = RsdRecordset( cmd );

  cmd.addParam( "FNCash", RSDBP_IN );
  cmd.addParam( "FlagCur", RSDBP_IN );

  cmd.value( "FNCash" ) = branch;
  cmd.value( "FlagCur" ) = 0;

  cmd.execute;

  if ( not rs.moveNext() )
      exitOnBtError( "Ошибка чтения параметров филиала" );
  else
    return rs.value("NumOperDy");
  end;
end;

/* Номер опердневника */

var
  diaryNumber = getDiaryNumber;

/* Переход на новую страницу */

macro newPage

  if ( noPages )
    return;
  end; 

  if ( lineCounter )
    printLn( StrFor( 12 ) );    /* На предыдущей странице было что-то напечатано - переводим */
  end;

  if ( ( lineCounter != 0 ) or ( pageCounter == 0 ) )
    pageCounter = pageCounter + 1; 
    lineCounter = 0;   
  
    [##############################################################################################################################]
    ( ( "-- " + pageCounter + " --" ):c );
    [ ];
  end;
end;

/*  checkNewPage() - 
**    Проверка "заполненности" страницы, и, если надо, переход на новую страницу 
**
**  incLineCounter() - 
**    Увеличение текущего значения счетчика строк 
**
**  Эти две процедуры используются так:
**
**  checkNewPage( 2 ); /* Влезут ли еще 2 строки? Если нет - перевести страницу */
**
**  [ Это заголовок ;) ];
**  [ ================ ];
**
**  incLineCounter;
**
*/

macro checkNewPage( nLinesToPrint, autoFF )

  if ( noPages )
    return;
  end; 

  linesToPrint = nLinesToPrint;
  if ( lineCounter + nLinesToPrint >= LINES_PER_PAGE - N_SYS_LINES ) 
    if ( ( autoFF == null ) or autoFF )  
      newPage;
    end;
    return true; 
  else    
    return false; 
  end;  
end;

macro incLineCounter( n )

  if ( noPages )
    return;
  end; 

  if ( n == null )
    n = linesToPrint;
  end;

  lineCounter = lineCounter + n;
end;

macro getSublines( str, segLen, a )

  array
    arr; 
  
  strSplit( str, arr, segLen );
  a = TArray;
  copyArrayToTArray( arr, a );
  setParm( 2, a );
  return aSize( arr ); 
end;

/* Извлечение времени из applicationKey */

macro getDocumentTime( appKey )

  return time( int( subStr( appKey, 11, 2 ) ), 
               int( subStr( appKey, 13, 2 ) ), 
               int( subStr( appKey, 15, 2 ) ) );
end;

/* Проверка подтвержденности документа */

macro isConfirmed( isConfirmed )

  return ( not {MakeDeskDocs} ) or isConfirmed;
end;

macro findPerson( oper )

  person.rec.Oper = oper;
  return person.getEQ;
end;

macro makeFio( surname, name, patr )

 var
   fio;

  surname = trim( surname );
  name = trim( name );
  patr = trim( patr );

  fio = surname;
  if ( name != "" )
    fio = fio + " " + subStr( name, 1, 1 ) + ".";
  end;
  if ( patr != "" )
    if ( name != "" )
      fio = fio + " ";
    end;
    fio = fio + subStr( patr, 1, 1 ) + ".";
  end;

  return fio;
end;

macro getAlgName( type, number )

  alg.rec.iTypeAlg = type;
  alg.rec.iNumberAlg = number;
  if ( alg.getEQ )
    return alg.rec.szNameAlg;
  end;
  return "";
end;

macro getWorkDeskName( oper, brigade )

  var
    operStr = String( oper ),
    brigadeStr = String( brigade );

  return mkStr( "0", 4 - strLen( operStr ) ) + operStr + "/" +
    mkStr( "0", 2 - strLen( brigadeStr ) ) + brigadeStr;
end;

macro findCapIssueType( type )

  ci.rec.Type = type;
  return ci.getEQ;
end;

macro findCIOperation( type, op )

  ciOper.rec.Type = type;
  ciOper.rec.Number = op;
  return ciOper.getEQ;
end;

macro findGroupMember( oper )

  if ( (grpMember.rec.Branch!=branch) or
       (grpMember.rec.Date!={curdate}) or
       (grpMember.rec.Group!=operBrigade) or
       (grpMember.rec.Oper!=oper) )
     grpMember.rec.Branch = branch;
     grpMember.rec.Date = {curdate};
     grpMember.rec.Group = operBrigade;
     grpMember.rec.Oper = oper; 
     if ( not grpMember.getEQ )
       exitOnBtError( "В смене не найден пользователь " + oper );
     end;
  end;
end;

macro getLabelField( label, n )

  var
    s = label, 
    len, 
    i = 0;

  while ( ( s != "" ) and ( i < n ) )
    s = subStr( s, index( s, ":" ) + 1 );
    i = i + 1;      
  end;
  len = index( s, ":" ) - 1;
  if ( len < 0 )
    len = null;
  end;
  return subStr( s, 1, len );
end;

macro getRegSetting( type, relPath )

  var
    regPath = "RS-RETAIL\\СЕРВИСНЫЕ\\" + relPath,
    value;

  if ( getRegistryValue( regPath, type, value, null ) == Type )
    return value;
  end;
  exitOnError( "Не найдена настройка АБС|", regPath );
end;

macro boolToInt(val)

  if (val)
    return 1;
  end;
  return 0;
end;

macro __init

  bankStd = GetBankStandart( );
end;

__init;
