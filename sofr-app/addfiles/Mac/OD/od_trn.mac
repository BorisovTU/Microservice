
/*
**  Опердневник ф.24. Безналичные зачисления
**
 KIN 09.01.04 дописан рублевый эквивалент зачислений/списаний 
*/

import od_com, od_total, operCode, sqlconv;

class ( TDataCollector ) TOperDC_Trn( _oper, _collectUnpaid )

  var  
    collectUnpaid = _collectUnpaid,
    payf = TBFile( "trn_payf.dbt", "r", 5 ),
    trnTotals = TSubTotals;

  macro filter

    if ( payf.rec.Oper != oper )
      return false;
    end;

    if ( payf.rec.Code_Currency != currency.rec.Code_Currency )
      return false;
    end;

    if ( ( payf.rec.RealFNCash != 0 ) and ( payf.rec.RealFNCash != branch ) )
      return false;
    end; 

    return true; 
  end;

  macro proceedForQF( qf )

    var
      v = TRecHandler( "od_data.1" );

    var
      processing = PROC_SKIP, 
      subType, 
      sum,
      ground,  
      recFound;

    v.setRecordAddr( odData );

    payf.addFilter("t.t_FlagCur = " + string( isCur ) +
		   " and t.t_SignKvitFilial = " + sqlChar( qf ) +
		   " and t.t_DateDocumentFilial = " + sqlDateToStr({curdate}) +
		   " and t.t_FNcash = " + string( branch ) +
		   " and t.t_Oper = " + string( oper ) +
		   " and t.t_Code_Currency = " + string( currency.rec.Code_Currency ) );
    payf.clear;
    payf.rec.FlagCur = isCur;
    payf.rec.SignKvitFilial = qf;
    payf.rec.DateDocumentFilial = {curdate};
    payf.rec.FNcash = branch;
    recFound = payf.getGE;
    while ( recFound 
      and ( payf.rec.FlagCur == isCur )
      and ( payf.rec.SignKvitFilial == qf )
      and ( payf.rec.DateDocumentFilial == {curdate} )
      and ( payf.rec.FNcash == branch ) )
      if ( filter )
        if ( collectUnpaid )
          sum = payf.rec.GlobalSumFilial - payf.rec.GlobalSumFactFilial;
        else
          sum = payf.rec.GlobalSumFactFilial;
        end;

        if ( sum != $0 )
          if ( payf.rec.ground != "" )
            ground = payf.rec.ground;
          else
            ground = "Безнал. зачисление";
          end;

          ground = ground + " контракт № " + payf.rec.NumberContract + ", п/п № " + payf.rec.Num_PayMessage;

          if ( collectUnpaid )
            subType = ODSTO_OTHER_OP;
            processing = PROC_SUM_UP;
            ground = ground + " (незачисл.)";
          else
            subType = ODSTO_DEP_OP;
          end;  

          initDataRec( 
            ODT_OPERATION, 
            subType, 
            currency.rec.Code_Currency, 
            oper, 
            maxTime );

          odData.rec.Processing = processing;
          odData.rec.Label = "oth:"; 

          v.clear;
          v.rec.Ground = ground;
          if ( payf.rec.TypeOper == Зачисление_В_Пак_Режиме )
            v.rec.memCredit = sum;
            trnTotals.memCredit = trnTotals.memCredit + sum;

 /* KIN 09.01.04 считаем рублевый эквивалент зачислений */
            
            if( payf.rec.Code_Currency != NATIONAL_CURR )
              v.rec.MemCreditR = cur2rub( sum );
     	      trnTotals.memCreditR = trnTotals.memCreditR + v.rec.MemCreditR;
            end;

          else
            v.rec.memDebet = sum;
            trnTotals.memDebet = trnTotals.memDebet + sum;

 /* KIN 09.01.04 считаем рублевый эквивалент списаний */            
            
            if( payf.rec.Code_Currency != NATIONAL_CURR )
              v.rec.memDebetR = cur2rub( sum );
	      trnTotals.memDebetR = trnTotals.memDebetR + v.rec.MemDebetR;
            end;
          end;                        
          odData.insert( v.recSize ); 
        end; 
      end;
      recFound = payf.next;
    end;
    payf.dropFilter();
  end;

  macro proceed

    message( string( oper ) + ": Сбор данных: Безналичные платежи" );
    trnTotals.reset;
    proceedForQF( "" ); 
    proceedForQF( "X" ); 
    message( "" );
  end;

  InitTDataCollector( _oper );
  if ( collectUnpaid == null )
    collectUnpaid = false;
  end;
end;
