/*-RSL---------------------------------------- R-Style Software Lab --
               RS-Retail 

               Импорт курсов валют ЦБ РФ.

               RA 08-06-03 Создан на основе imp_curr.mac
*/

/*                             Настройка                                    */

/* Если флаг ПоВсемВалютам установлен в TRUE, то импорт будет произведен
   для всех валют, записи по которым имеются в currency.dbt, иначе -
   для валют, заданных в нижеследующем списке */
const ПоВсемВалютам = TRUE;

var ПутьК_DBF = ""; /* Директория, где находится DBF файл. Если не задана,
                       поиск производится в каталоге Dbfile */

/* Список кодов ISO валют, курсы по которым будут импортированы */
const СписокВалют = TArray;
СписокВалют(0)  = "840";
СписокВалют(1)  = "978";

/* ------------------------------------------------------------------------ */

IMPORT DeprIntr, "globals.mac", "cur_rate.mac";

FILE src() DBF;
FILE currency( "currency.dbt",   "sbbank.def" ) key 1;
FILE cur_rate( "cur_rate.dbt", "exchange.def" ) key 0 write;
FILE ratekind( "ratekind.dbt", "exchange.def" ) key 0;

/* ------------------------------------------------------------------------ */

const not_found = "NOT FOUND";

/* ------------------------------------------------------------------------ */

/* Надо ли импортировать валюту с заданным кодом */
MACRO NeedImportRate( code )

 var i = 0;
 const n = СписокВалют.size;

 while( i < n )
  if( СписокВалют(i) == code )
   return TRUE;
  end;
  i = i+1;
 end;
 return FALSE;

END;

/* ......................................................................... */

/* Получить внутренний код валюты по коду ISO */
MACRO getCodeByISO( ISOCode )
 currency.ExternalCode = ISOCode;
 if( not getEQ( currency ) )

  if( ISOCode == "810" )
   println( "!!! Ошибка: в справочнике не найдена базовая валюта (код ISO = 810)" );
   exit;
  end;

  return not_found;
 end;
 return currency.Code_Currency;
END;

/* ......................................................................... */

/* Установить курс в смену курсов данного вида */
MACRO processRateKind()

 var CodeCurrency = getCodeByISO( src.ISO_DIG );
 var err_str = "";

 /* Получить курс валюты */
 /* (спозиционируется на RateChange) */
 if( not FindCurrRate( CodeCurrency, ratekind.ID, {curdate}, null, null, null ) )
  println( "................................................................" );
  println;
  println( "!!! Вид курсов ", RateChange.Kind_Ref, " ", RateChange.Descr );
  println( "!!! Ошибка получения данных по курсу валюты ",
           src.ISO_DIG, "  ", trim( src.NAME_RUSH ) );
  println;
  return;
 end;

 /* проверка на уже заданный курс */
 if( RateChange.StartDate == src.DAT )

  if( not getTrue( TRUE, "Вид курсов " + RateChange.Kind_Ref + "  " + RateChange.Descr + "\n \n" +
                         "По валюте " + trim( src.ISO_DIG ) + "  " +
                         trim( src.NAME_RUSH ) + "\n" +
                         " уже задан курс за текущий день. Обновить?" ) )
   return;
  end;

 elif( RateChange.StartDate > src.DAT )
  return;
 end;

 /* проверка на соотвествие даты */
 if( src.DAT > {curdate} )
   
  if( not getTrue( FALSE, "Вид курсов " + RateChange.Kind_Ref + "  " + RateChange.Descr + "\n \n" +
                          "По валюте " + trim( src.ISO_DIG ) + "  " +
                          trim( src.NAME_RUSH ) + "\n" +
                          "дата устанавливаемого курса больше текущего\n" +
                          "операционного дня. Установить?" ) )
   return;
  end;
 
 end;

/*
 while( src.DAT > {curdate} )
   
  src.DAT = {curdate};
  if( not getTrue( FALSE, "Вид курсов " + RateChange.Kind_Ref + "  " + RateChange.Descr + "\n \n" +
                          "По валюте " + trim( src.ISO_DIG ) + "  " +
                          trim( src.NAME_RUSH ) + "\n" +
                          "дата устанавливаемого курса больше текущего\n" +
                          "операционного дня. Установить?" ) )
   return;
  elif( not getDate( src.DAT, "Введите дату начала действия курса" ) )
   return;
  end;
 
 end;
*/

 copy( cur_rate, CurrRate );
 GetEQ( cur_rate );

 cur_rate.CBRate = src.CURSE;

 if( ratekind.ID == 1 )
  println( "................................................................" );
  println;
  println( ">> Валюта ", src.ISO_DIG, "  ", trim( src.NAME_RUSH ) );
  println;
 end;

 if( Update( cur_rate ) != true )
  print( "> !!! Ошибка обновления курса: " );
  println( " Ошибка Btrieve № ", status( err_str ), " ", err_str );
  return;
 end;

 if( ratekind.ID == 1 )
  println( "> Курс валюты был обновлен. Установлены следующие значения:" );
  println;
// println( "> Дата курса       ", RateChange.StartDate );
  println( "> Текущий курс     ", cur_rate.CBRate:0:4, ", масштаб ", cur_rate.ChangeNom );
  println;
 end;

END;

/* ......................................................................... */

/* Установить курс по текущей валюте */
MACRO processCurrency()

 var CodeCurrency = getCodeByISO( src.ISO_DIG );
 
 if( CodeCurrency == not_found )
  return;
 elif( ( not ПоВсемВалютам ) and
       ( not NeedImportRate( src.ISO_DIG ) ) )
  return;
 end;

 rewind( ratekind );
 
 /* перебор видов курсов */
 while( next( ratekind ) )

  if(     ( ratekind.State == 0 )
      and ( ratekind.Base_Code_Currency != 5 )
    )
   processRateKind();
  end;

 end;

END;

/* ---------------------------- Entry point -------------------------------- */

if( trim( ПутьК_DBF ) == "" )
 ПутьК_DBF = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\DISTDBFILE" );
end;

if( not open( src,  ПутьК_DBF + "\\curcours.dbf" ) )
 msgbox( "Невозможно открыть файл с курсами валют:|",
         ПутьК_DBF + "\\curcours.dbf" );
 exit(1);
end;

println;
println( "Протокол импорта валют ", date, "  ", time );
println;

while( next( src ) )
 processCurrency();
end;
