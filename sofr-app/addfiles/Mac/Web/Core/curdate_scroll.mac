 /*
 $Name: curdate_scroll.mac
 $Module: Главная книга
 $Description: Макрос Списока операционных дней в филиале пользователя
 */
 
import "ws_partycommon.mac";
import "ws_dprt.mac";
import "ws_datafilter.mac";
import "pr_party_lib.mac";

class TWebCurdateGridObject
  var Department;
	var OperCanSetMain;
	var OperCanCloseDate;
	var IsReview;
end;

class TWebCurdateGridItem
  var CurDate;
  var IsClosed;
  var MinPhase;
  var MaxPhase;
  var IsBalance;
  var Branch;
end;

macro WebCore_InitCurdateGrid(DprtID)

  var CurdateGridObject = TWebCurdateGridObject();
  
  var cmd, rs;

  var query = " SELECT dp.t_Code As Code, "
              "  dp.t_Name As Name_, "
              "  pt.t_Name As PartyName "
              " FROM ddp_dep_dbt dp, dparty_dbt pt "
              "  WHERE "
              "   dp.t_Code = " + string(DprtID) +
              "   AND pt.t_PartyID = dp.t_PartyID ";

  cmd = RsdCommand(query);
  
  rs = RsdRecordset(cmd);

  if(rs.movenext)

    var obj = TWsDepartment();

    obj.Code        = SQL_ConvType(rs.value("Code"));
    obj.Name_       = SQL_ConvType(rs.value("Name_"));
    obj.PartyName   = SQL_ConvType(rs.value("PartyName"));
    
    CurdateGridObject.Department = obj;

  end;

  if(CheckActionRestriction(144) == true)

    CurdateGridObject.OperCanSetMain = true;
    
  else
    
    CurdateGridObject.OperCanSetMain = false;

  end;
    
  if(CheckActionRestriction(145) == true)

    CurdateGridObject.OperCanCloseDate = true;
    
  else
    
    CurdateGridObject.OperCanCloseDate = false;

  end;
  
  if({cTypePerson} == TP_BOSS)

    CurdateGridObject.IsReview = true;
    
  else
    
    CurdateGridObject.IsReview = false;

  end;
  
  return CurdateGridObject;

end;

/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else 
      return value_false;
   end;
end;

private macro GetSortStr( OrderItems,            /* Параметры сортировки */
                          DefaultSortStr:string, /* Строка сортировки по умолчанию (первичному ключу) */
                          Table:string           /* Название таблицы c . */
                        ):String
                        
  var SortStr:string = "";

  if( (OrderItems == null) or (OrderItems.Size() <= 0) )
  
    SortStr = " " + DefaultSortStr + " ";
     
  else
    
    var i:integer = 0;
     
    while( i < OrderItems.size() )
      if ((OrderItems[i].Column == "CurDate") OR 
          (OrderItems[i].Column == "MinPhase") OR
          (OrderItems[i].Column == "MaxPhase") OR
          (OrderItems[i].Column == "IsBalance"))
          OrderItems[i].Column = "curdate.T_" + OrderItems[i].Column;
      else
        if(OrderItems[i].Column == "State")
          OrderItems[i].Column = "curdate.T_ISCLOSED";
        end;
      end;
        
      if (i!=0) 
        SortStr = SortStr + ", " + OrderItems[i].Column + " " + IIF(OrderItems[i].Direction == 0, "asc", "desc");
      else 
        SortStr = SortStr + "  " + OrderItems[i].Column + " " + IIF(OrderItems[i].Direction == 0, "asc", "desc");
      end;
       
      i = i + 1;
    end;
     
    SortStr = SortStr + " ";
     
  end;

  return SortStr;
  
end;

private macro GetQuery()


  var query = " SELECT curdate.T_CURDATE, "
              "        curdate.T_BRANCH, "
              "        curdate.T_MINPHASE, "
              "        curdate.T_MAXPHASE, "
              "        curdate.T_ISCLOSED, "
              "        curdate.T_ISBALANCE "
              " FROM dcurdate_dbt curdate "
              "  WHERE curdate.T_BRANCH = ? AND curdate.T_ISCLOSED != 'Z' ";

  return query;

end;

private const FILTERITEM_STATE    = 0; // Состояние
private const FILTERITEM_OPERDATE = 1; // Дата операционного дня

private macro GetAddWhereCondition(FilterItems)

  var condition = "";

  var FilterItemsCount = 0;
  var ValueCount = 0;

  while( FilterItemsCount < FilterItems.Size )

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_STATE ) // Состояние

      if(FilterItems[FilterItemsCount].Value.Size > 0)

        ValueCount = 0;

        condition = condition + " AND ( 1!=1 ";

        while( ValueCount < FilterItems[FilterItemsCount].Value.Size )

          if(FilterItems[FilterItemsCount].Value[ValueCount] == 0);

            condition = condition + " OR curdate.t_isclosed = CHR(0) ";

          end;

          if(FilterItems[FilterItemsCount].Value[ValueCount] == 1);

            condition = condition + " OR curdate.t_isclosed = 'X' ";

          end;

          ValueCount = ValueCount + 1;

        end;
        
        condition = condition + " ) ";

      end;

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_OPERDATE ) // Дата операционного дня

	  
	
	  if( FilterItems[FilterItemsCount].IsNot )
	 
        condition = condition + "AND NOT ";
		
	    condition = condition + " ( 1=1 ";
		
      end;
	  
	  
	  
      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Null )
        
        condition = condition + " AND curdate.t_curdate = NULL ";

      end;
      
      if( FilterItems[FilterItemsCount].Condition == FilterCondition_From )
        
        condition = condition + " AND curdate.t_curdate ";
        condition = condition + " >= ";
        condition = condition + To_SQLData(FilterItems[FilterItemsCount].Value[0]);

      end;

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_To )

        condition = condition + " AND curdate.t_curdate ";
        condition = condition + " <= ";
        condition = condition + To_SQLData(FilterItems[FilterItemsCount].Value[0]);

      end;

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Between )

        condition = condition + " AND curdate.t_curdate ";
        condition = condition + " Between ";
        condition = condition + To_SQLData(FilterItems[FilterItemsCount].Value[0]);
        condition = condition + " AND ";
        condition = condition + To_SQLData(FilterItems[FilterItemsCount].Value[1]);

      end;

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Equal )

        condition = condition + " AND curdate.t_curdate ";
        condition = condition + " = ";
        condition = condition + To_SQLData(FilterItems[FilterItemsCount].Value[0]);

      end;

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_CurrentDate )

        condition = condition + " AND curdate.t_curdate = " + To_SQLData({curdate});

      end;
	  
	  if( FilterItems[FilterItemsCount].IsNot )
	   
	    condition = condition + ") ";
		
	  end;

    end;
    FilterItemsCount = FilterItemsCount + 1;

  end;

  return condition;

end;

macro WebCore_GetCurdateGridItemCount(filteritems, DprtID)

  var res : integer = 0;

  var cmd, rs;

  var query = " SELECT count(*) c FROM ( ";

  query = query + GetQuery();

  var strAddWhere = GetAddWhereCondition( filteritems );

  if( strAddWhere != "" )
    query = query + strAddWhere;
  end;

  query = query + " ) ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, DprtID );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    res = rs.value("c");

  end;

  return res;

end;

macro WebCore_GetCurdateGridItemList(PageSize, PageNum, filteritems, orderitems, DprtID)

  var cmd, rs, p;

  var Arr = TArray;

  var query = " SELECT t.* FROM ( SELECT rownum RN, tt.* FROM ( ";

  query = query + GetQuery();

  var strAddWhere = GetAddWhereCondition( filteritems );

  if( strAddWhere != "" )

     query = query + strAddWhere;

  end;

  query = query + " ORDER BY " + GetSortStr(orderitems,"curdate.t_curdate DESC","");

  if( PageSize > 0 )
    query = query + " ) tt WHERE ROWNUM <= " + string(PageSize*(PageNum + 1)) + " ) t WHERE t.RN >= " + string(PageSize*(PageNum) + 1) + " ";
  else
    query = query + " ) tt ) t ";
  end;

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, DprtID );
  
  rs = RsdRecordset(cmd);

  while(rs.movenext)

    p = TWebCurdateGridItem();

    p.CurDate   = SQL_ConvType(rs.value("T_CURDATE"));
    p.IsClosed  = SQL_ConvTypeBool(rs.value("T_ISCLOSED"));
    p.MinPhase  = SQL_ConvType(rs.value("T_MINPHASE"));
    p.MaxPhase  = SQL_ConvType(rs.value("T_MAXPHASE"));
    p.IsBalance = SQL_ConvTypeBool(rs.value("T_ISBALANCE"));
    p.Branch    = SQL_ConvType(rs.value("T_BRANCH"));

    Arr[Arr.size] = p;

  end;

  return Arr;
  
end;