/*
$Name:        docgrnd_scroll.mac
$Module:      Главная книга
$Description: Макрос содержит реализацию сервисов для скроллинга оснований документов
*/

import "ws_partycommon.mac";
import "ws_person.mac";
import "ws_validation.mac";

class TWebDocGroundGridItem
  var RecID;
  var DocKind;
  var SubDocKind;
  var GroundCode;
  var Oper;
  var GroundText;
end;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro dg_GetSortStr( OrderItems,            /* Параметры сортировки */
                             Table:string           /* Название таблицы c . */
                           ):String

  var SortStr:string = "";

   SortStr = " " + Table + "t_" + OrderItems[0].Column + " " + PT_IIF(OrderItems[0].Direction == 0, "asc", "desc");
   var i:integer = 1;
   while( i < OrderItems.size() )
      SortStr = SortStr + ", " + Table + "t_" + OrderItems[i].Column + " " + PT_IIF(OrderItems[i].Direction == 0, "asc", "desc");
      i = i + 1;
   end;
   SortStr = SortStr + " ";

  return SortStr;

end;

private macro GetAddWhereCondition(
  FilterItems/*:TArray of FilterCondItem*/
):String

  var condition = "";
  var conditionNOT = "";

  var FilterItemsCount = 0;
  var ValueCount = 0;

  while( FilterItemsCount < FilterItems.Size )

    if( FilterItems[FilterItemsCount].Id == 0 ) // Операционист

      if( FilterItems[FilterItemsCount].IsNot )
        conditionNOT = " NOT ";
      else
        conditionNOT = " ";
      end; 
    
      if((FilterItems[FilterItemsCount].Value != null) AND (FilterItems[FilterItemsCount].Value.Size > 0))
      
        condition = condition + " AND T_OPER " + conditionNOT + " in ( ";
        
        ValueCount = 0;

        while( ValueCount < FilterItems[FilterItemsCount].Value.Size )

          condition = condition + string(FilterItems[FilterItemsCount].Value[ValueCount]);

          if(ValueCount != FilterItems[FilterItemsCount].Value.Size - 1)

            condition = condition + ", ";

          end;

          ValueCount = ValueCount + 1;

        end;

        condition = condition + " ) ";         
      
      end;      

    end;

    FilterItemsCount = FilterItemsCount + 1;

  end;

  return condition;
  
end;

macro WebCore_GetDocGroundGridItemList(
                                        PageSize,
                                        PageNum,
                                        DocKind,
                                        SubDocKind,
                                        AllGrounds,
                                        FilterItems,
                                        Orderitems)

  var cmd, rs;

  var Arr = TArray;

  var query = " SELECT t.* FROM ( SELECT rownum RN, tt.* FROM ( ";

  query = query + " SELECT T_RecID, T_DocKind, T_SubDocKind, T_GROUNDCODE, T_OPER, T_GROUNDTEXT "
                  "   FROM DDOCGRND_DBT WHERE T_DOCKIND	= " + string(DocKind);

  if((AllGrounds == false) AND (SubDocKind != 0))

    query = query + " AND T_SUBDOCKIND	= " + string(SubDocKind);

  end;

  if(({cTypePerson} != TP_ADM) AND ({cTypePerson} != TP_BOOK))

      query = query + " AND T_OPER	= " + string({oper});

  end;
  
  var strAddWhere = GetAddWhereCondition( FilterItems );

  if( strAddWhere != "" )
    
    query = query + strAddWhere;
    
  end;

  if( (Orderitems != null) AND (Orderitems.Size() > 0) )

    query = query + " ORDER BY " + dg_GetSortStr(Orderitems, "");
    
  else
    
    query = query + " ORDER BY T_GROUNDCODE ";
  
  end;

  if( PageSize > 0 )
    query = query + " ) tt WHERE ROWNUM <= " + string(PageSize*(PageNum + 1)) + " ) t WHERE t.RN >= " + string(PageSize*(PageNum) + 1) + " ";
  else
    query = query + " ) tt ) t ";
  end;

  cmd = RsdCommand(query);

  rs = RsdRecordset(cmd);

  while(rs.movenext)

    var obj = TWebDocGroundGridItem;

    obj.RecID       = SQL_ConvType(rs.value("T_RECID"));;
    obj.DocKind     = SQL_ConvType(rs.value("T_DOCKIND"));;
    obj.SubDocKind  = SQL_ConvType(rs.value("T_SUBDOCKIND"));;

    obj.GroundCode  = SQL_ConvType(rs.value("T_GROUNDCODE"));

    var OperNum     = SQL_ConvType(rs.value("T_OPER"));

    if(OperNum != 0)
      obj.Oper      = FindPersonByNumber(OperNum);
    else
      obj.Oper = TWsPerson();
      obj.Oper.Number = 0;
    end;

    obj.GroundText  = SQL_ConvType(rs.value("T_GROUNDTEXT"));

    Arr[Arr.size] = obj;

  end;

  return Arr;

end;

macro WebCore_GetDocGroundGridItemCount(DocKind, SubDocKind, AllGrounds, FilterItems)

  var cmd, rs;

  var res : integer;
  res = 0;

  var query = " SELECT count(1) c "
              "   FROM DDOCGRND_DBT WHERE T_DOCKIND	= " + string(DocKind);

  if((AllGrounds == false) AND (SubDocKind != 0))

    query = query + " AND T_SUBDOCKIND	= " + string(SubDocKind);

  end;

  if(({cTypePerson} != TP_ADM) AND ({cTypePerson} != TP_BOOK))

      query = query + " AND T_OPER	= " + string({oper});

  end;
  
  var strAddWhere = GetAddWhereCondition( FilterItems );

  if( strAddWhere != "" )
    
    query = query + strAddWhere;
    
  end;

  cmd = RsdCommand(query);

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    res  = rs.value("c");

  end;

  return res;

end;

macro WebCore_InsertUpdateDocGroundGridItem(DocGroundGridItem, AllGrounds)

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  file docgrnd("docgrnd.dbt");

  ClearRecord(docgrnd);

  docgrnd.RecID    = DocGroundGridItem.RecID;
  docgrnd.DocKind    = DocGroundGridItem.DocKind;
  docgrnd.SubDocKind = DocGroundGridItem.SubDocKind;
  docgrnd.GroundCode = DocGroundGridItem.GroundCode;
  docgrnd.Oper       = DocGroundGridItem.Oper.Number;
  docgrnd.GroundText = DocGroundGridItem.GroundText;

  res = CB_InsertUpdateDocGrnd(docgrnd, AllGrounds);

  if(res != True)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_DeleteDocGroundGridItem(RecID)

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = CB_DeleteDocGrnd(RecID);

  if(res != True)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;