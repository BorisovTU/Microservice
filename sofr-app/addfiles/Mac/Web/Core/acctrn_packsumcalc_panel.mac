/*
$Name:        acctrn_packsumcalc_panel.mac
$Module:      Главная книга
$Description: Подсчет суммы в пачке
*/

import RSD;
import "rescarry_widget.mac";
import "ws_ficlass.mac";
import "ws_fiunilist.mac";
import "acctrn_scroll.mac";

class TWebAccTrnPackSumCalcOutParm
  var Sum      : Money  ;
  var PackDoc  : integer;
  var TotalDoc : integer;    
end;

class TWebAccTrnPackSumCalcPanelObject
  var ResCarry                ; // TWebResCarry
  var AllSelResCarry          ; // bool
  var IsCalcByDebet : bool    ;
  var IsCalcByCredit: bool    ;
  var FI                      ; // TWsFinInstr
  var PackNumber    : integer ;
  var OutParm                 ; // TWebAccTrnPackSumCalcOutParm
end;


macro WebCore_CalcPackSum ( ResultCarry, CalcByDebet, CalcByCredit, FIID, PackNumb, Chapter, IsCash, State, FilterItems, FromDate, ToDate ) : TWebAccTrnPackSumCalcOutParm

  var OutParm = TWebAccTrnPackSumCalcOutParm(); 
  var StrQuery = "";
  var cmd, rs;

  cmd = RsdCommand();
  cmd.NullConversion = true;

  if(CalcByDebet == TRUE)
    StrQuery = "select COUNT(1) AS COUNT, NVL(SUM(acctrn.t_Sum_Payer), 0) AS SUM from dacctrn_dbt acctrn where ";
  else
    StrQuery = "select COUNT(1) AS COUNT, NVL(SUM(acctrn.t_Sum_Receiver), 0) AS SUM from dacctrn_dbt acctrn where ";
  end;


  StrQuery = StrQuery + " acctrn.t_Chapter = ? AND acctrn.t_State = ? ";
  cmd.addParam( "", RSDBP_IN, Chapter );
  cmd.addParam( "", RSDBP_IN, State   );

  if(IsCash == true)
    StrQuery = StrQuery + " AND acctrn.t_Kind_Oper = ' 3' ";
  end;

  if(ResultCarry > 0)
    StrQuery = StrQuery + " AND acctrn.t_Result_Carry = ? ";
    cmd.addParam( "", RSDBP_IN, ResultCarry   );
  end;

  if(FIID >= 0)
    if(CalcByDebet == TRUE)
      StrQuery = StrQuery +" AND acctrn.t_FIID_Payer = ? ";
    else
      StrQuery = StrQuery +" AND acctrn.t_FIID_Receiver = ? ";
    end;
    cmd.addParam( "", RSDBP_IN, FIID );
  end;

  if(PackNumb > 0)
    StrQuery = StrQuery +" AND acctrn.t_Number_Pack = ? ";
    cmd.addParam( "", RSDBP_IN, PackNumb );
  end;

  if((FromDate != null) AND (FromDate != PT_ZERODATE))
    
    StrQuery = StrQuery + " AND acctrn.t_date_carry >= " + To_SQLData(FromDate);

  end;  
  
  if((ToDate != null) AND (ToDate != PT_ZERODATE))
    
    StrQuery = StrQuery + " AND acctrn.t_date_carry <= " + To_SQLData(ToDate);

  end;   

  var strAddWhere = GetAddWhereCondition( FilterItems );

  if( strAddWhere != "" )
     StrQuery = StrQuery + strAddWhere;
  end;

  cmd.cmdText  = StrQuery;
  rs = RsdRecordset(cmd);
  if(rs.movenext)
    OutParm.PackDoc = rs.value("COUNT");
    OutParm.Sum     = rs.value("SUM");

    // OutParm.TotalDoc = OutParm.PackDoc

  end;

  // Count TotalDoc

  cmd = RsdCommand();
  cmd.NullConversion = true;

  StrQuery = "select COUNT(1) AS COUNT from dacctrn_dbt acctrn where ";
  StrQuery = StrQuery + " acctrn.t_Chapter = ? AND acctrn.t_State = ? ";
  cmd.addParam( "", RSDBP_IN, Chapter );
  cmd.addParam( "", RSDBP_IN, State   );

  if(IsCash == true)
    StrQuery = StrQuery + " AND acctrn.t_Kind_Oper = ' 3' ";
  end;

  if( strAddWhere != "" )
     StrQuery = StrQuery + strAddWhere;
  end;

  cmd.cmdText  = StrQuery;
  rs = RsdRecordset(cmd);
  if(rs.movenext)
    OutParm.TotalDoc = rs.value("COUNT");
  end;

  return OutParm;
end;


macro WebCore_InitCalcPackSumPanel () : TWebAccTrnPackSumCalcPanelObject
  var panelObj = TWebAccTrnPackSumCalcPanelObject();
  
  panelObj.ResCarry         = null ;       // TWebResCarry();
  panelObj.AllSelResCarry   = true ;
  panelObj.IsCalcByDebet    = true ;                                
  panelObj.IsCalcByCredit   = false;                                
  panelObj.FI               = GetTWsFinInstrByID(NATCUR);
  panelObj.PackNumber       = 0    ;  

  panelObj.OutParm   = TWebAccTrnPackSumCalcOutParm();
  panelObj.OutParm.Sum       = $0;
  panelObj.OutParm.PackDoc   = 0;
  panelObj.OutParm.TotalDoc  = 0;

  return panelObj;
end;