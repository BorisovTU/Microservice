/*
$Name:        calendar_panel.mac
$Module:      Главная книга
$Description: Макрос видов календаря
*/


IMPORT BankInter, RSD, Календарь, "cb_sql.mac", "globals.mac";
IMPORT "ws_validation.mac";

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

const  DrawMonthFields = 37 ; // Количество полей, представляющих день месяца в панели "Календарь"
     
class TWebCalendarKind
  var ID                :integer;
  var KindName          :string;
  var Balance           :integer;
  var RetailInSaturday  :bool;
  var RetailInSunday    :bool;
  var RetailInHolyday   :bool;
  var ParentID          :integer;
  var BalanceRule       :string;
end;

class  TWebCalendarDay
  var DayNum      ; // int   порядковый номер дня в месяце 
  var YearDayNum  ; // int   порядковый номер дня в году
  var BalanceFlag ; // int значение байта "Баланс", соответствующего дню DayNumStr и полученного из calendar.CalenDays флаг, определяющий наличие баланса в дне: 0 - нет баланса, 1 - есть баланс
  var ServiceKind ; // int значение байта "Вид обслуживание", соответствующего дню DayNumStr и полученного из calendar.CalenDays Вид обслуживания: 
                    //   0 - нет обслуживания,
                    //   1 - банковское обслуживание,
                    //   2 - розничное обслуживание
  var DayNumStr   ; // string    Выводимая информация о дне месяца, содержащая номер дня и символ вида обслуживания
end;

class TWebCalendarMonth
  var Month           ; // int   порядковый номер месяца
  var Year            ; // int   год, к которому относится месяц Month
  var Days            ; // TWebCalendarDay[]   Массив дней - объектов класса. Размер массива определяется значением CalendarConstants. DrawMonthFields.
  var CalendarDayNum  ; // int   Количество календарных дней в месяце
  var WorkDayNum      ; // int   Количество рабочих дней в месяце
end;                  

class TWebCalendarYear
  var CalendarID           ; // int calendar.ID идентификатор записи
  var Year                 ; // int calendar.Year год
  var DaysInYear           ; // int calendar.DaysInYear количество дней в году
  var Months               ; // TWebCalendarMonth[]   массив месяцев - объектов класса TWebCalendarMonth. Размер массива равен 12 (по количеству месяцев в году).
end;

class TWebCalendarPanelObject
  var CanUserEditCalendar  ; //   bool Признак возможности редактирования календаря текущим пользователем в соответствии с привилегией {140} "Редактировать календарь".  
  var CalendarYearList     ; //   List<TWebCalendarYear> Список календарей по годам - список объектов класса TWebCalendarYear  
  var CurrentYear          ; //   TWebCalendarYear    Информация о годе, к которому относится отображаемый в календаре месяц  
  var CurrentMonth         ; //   TWebCalendarMonth   Информация об отображаемом в календаре месяце 
  var CurrentMonthTitle    ; //   string    Название текущего месяца CurrentMonth в формате "<наименование месяця> <год>"   
  var QuartCalDayNum       ; //   int Выводимое значение, равное количеству календарных дней в квартале, к которому относится текущий месяц CurrentMonth  
  var QuartWorkDayNum      ; //   int Выводимое значение, равное количеству рабочих дней в квартале, к которому относится текущий месяц CurrentMonth  
  var YearWorkDayNum       ; //   int Выводимое значение, равное количеству рабочих дней в году 
  var IsVSP                ; //   bool Признак, определяющий привязку календаря к узлу ТС типа "ВСП" 
  var CalendarKind         ; //   TWebCalendarKind Информация о виде календаря
end;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;


private macro GetCalendarKind( CalcKindID : integer )
  
  file calkind("calkind.dbt") key 0;

  var CalendarKind = TWebCalendarKind();

  calkind.ID = CalcKindID;
  
  if( GetEQ(calkind) )

    CalendarKind.ID                = calkind.ID;
    CalendarKind.KindName          = calkind.Name;
    CalendarKind.Balance           = calkind.Balance;
    CalendarKind.RetailInSaturday  = calkind.RetailInSaturday;
    CalendarKind.RetailInSunday    = calkind.RetailInSunday;
    CalendarKind.RetailInHolyday   = calkind.RetailInHolyday;
    CalendarKind.ParentID          = calkind.ParentID;

  end;

  return CalendarKind;

end;

private macro GetCalendarYearParm(calendarObj:TRsbCalendarYear)
  var yearObj = TWebCalendarYear();

  yearObj.CalendarID = calendarObj.ID;
  yearObj.Year       = calendarObj.Year;
  yearObj.DaysInYear = calendarObj.DaysInYear;

  yearObj.Months = TArray(12, 1);

  var it = 0;
  while(it<12)
    var i = it;
    var calMount = TWebCalendarMonth(); 

    calMount.Month          = i+1;
    calMount.Year           = calendarObj.Year;  // ??
    calMount.CalendarDayNum = 0;
    calMount.WorkDayNum     = 0;

    calendarObj.GetDrawMonth(i+1, calMount.CalendarDayNum, calMount.WorkDayNum, calMount.Days); 

    
    yearObj.Months[i] = calMount;
    it = it + 1;
  end;
    
  return yearObj;
end;

macro WebCore_GetCalendar (ID, Branch) : TWebCalendarPanelObject
  var panelObject = TWebCalendarPanelObject;
  var calenID = ID;
  var currentYear = 0 ;
  DateSplit ({curdate}, null, null, currentYear );
  var review = false;

  ResponseBuilder = WebResponseBuilder();

  if((calenID == null) AND (Branch != null))
      CB_GetDprtCalendarParam(Branch, calenID, currentYear, review, panelObject.IsVSP );
  end;

  if(review == false)
    if((calenID == 0) and ({OperDprtNode} != {NumDprt}))
      review = true;
    end;
  end;

  if(review == false)
    review = not CheckActionRestriction(140) ;
  end;

  if(calenID != null)
    var query = " select 1 cnt from dcalendar_dbt where t_ID = :calenID and t_Year = :Year ";
    var cmd = RsdCommand(query);
    cmd.addParam( "", RSDBP_IN, calenID );
    cmd.addParam( "", RSDBP_IN, currentYear );
    var rs = RsdRecordset(cmd);

    panelObject.CalendarYearList = TArray();
    if(rs.movenext())
      var calendarObj = TRsbCalendarYear(calenID, currentYear);
      panelObject.CurrentYear = GetCalendarYearParm(calendarObj); //TWebCalendarYear
 
      var lsize = panelObject.CalendarYearList.size();
      panelObject.CalendarYearList[lsize] = panelObject.CurrentYear;

      if(review)
        panelObject.CanUserEditCalendar = false;
      else
        panelObject.CanUserEditCalendar = true;
      end;
    end;

    panelObject.CalendarKind = GetCalendarKind( calenID );

  end;

  responseBuilder.SetUserObject(panelObject);

  return responseBuilder.Result;
end;


macro WebCore_LoadYear (ID, year, branch) //: TWebCalendarYear
  ResponseBuilder = WebResponseBuilder();

  var calendarObj = TRsbCalendarYear(ID,  year);

  var res = calendarObj.LoadYear(branch);

  if(res != True)
    var ErrorMessage : WsErrorMessage = WsErrorMessage();
    AddErrorMessage(@ErrorMessage);
  end;

  var CurrentYear = GetCalendarYearParm(calendarObj); //TWebCalendarYear
  ResponseBuilder.SetUserObject(CurrentYear);

  return ResponseBuilder.Result; 
end;



macro WebCore_SaveDayParam (calenDay, calenYear, BalanceFlag : integer, ServiceKind : integer ) : TWebCalendarYear

  ResponseBuilder = WebResponseBuilder();

  var calendarObj = TRsbCalendarYear(calenYear.CalendarID, calenYear.Year);

  var Day = TRsbCalendarDay();

  Day.DayNum      = calenDay.DayNum     ; 
  Day.YearDayNum  = calenDay.YearDayNum ;
  Day.BalanceFlag = BalanceFlag;
  Day.ServiceKind = ServiceKind;
  // Day.ChangeFlag  = calenDay.ChangeFlag ;

  var res = calendarObj.SetDayParam(Day);

  if(res != True)
    var ErrorMessage : WsErrorMessage = WsErrorMessage();
    AddErrorMessage(@ErrorMessage);
  end;

  var CurrentYear = GetCalendarYearParm(calendarObj); //TWebCalendarYear

  responseBuilder.SetUserObject(CurrentYear);

  return responseBuilder.Result;

end;

macro WebCore_GetWorkDaysNum (ID, firstDate, lastDate) : integer

  if (firstDate == null)
    firstDate = date(0,0,0);
  end;

  if (lastDate == null)
    lastDate = date(0,0,0);
  end;
  
  var num = GetWorkDaysNumExt(ID, firstDate, lastDate);

  return num;
end;


macro WebCore_GetDateAfterWorkDays (ID, firstDate, WorkDays) : date

  if (firstDate == null)
    firstDate = date(0,0,0);
  end;
  
  var Result = GetDateAfterWorkDays(firstDate, WorkDays, ID);

  return Result;
end;

