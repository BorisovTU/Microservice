/*
$Name:        partblnc_scroll.mac
$Module:      Главная книга
$Description: Макрос содержит реализацию сервисов для скроллинга "Разделы балансовых счетов"
*/

import "ws_partycommon.mac";
import "ws_person.mac";
import "ws_validation.mac";

class TWebPartBalAccGridItem
  var Part;
  var iNumPlan;
  var Chapter;
  var Name_Part;
end;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

macro WebCore_GetPartBalAccountGridItemList(Chapter, iNumPlan)

  var cmd, rs;

  var Arr = TArray;

  var query = " SELECT T_INUMPLAN, T_PART, T_NAME_PART, T_CHAPTER "
              "  FROM DPARTBLNC_DBT WHERE T_INUMPLAN = ? ";

  if(Chapter != NULL)

    query = query + " AND T_CHAPTER = ? ";

  end;

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, iNumPlan );

  if(Chapter != NULL)

    cmd.addParam( "", RSDBP_IN, Chapter );

  end;

  cmd.NullConversion = true;

  cmd.Execute();

  rs = RsdRecordset(cmd);

  while(rs.movenext)

    var obj = TWebPartBalAccGridItem;

    obj.Part       = SQL_ConvType(rs.value("T_PART"));;
    obj.iNumPlan   = SQL_ConvType(rs.value("T_INUMPLAN"));;
    obj.Chapter    = SQL_ConvType(rs.value("T_CHAPTER"));;
    obj.Name_Part  = SQL_ConvType(rs.value("T_NAME_PART"));

    Arr[Arr.size] = obj;

  end;

  return Arr;

end;

macro WebCore_InsertUpdatePartBalAccount(Action, PartBalAcc, savePartBalAcc)

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  file partblnc("partblnc.dbt") key 0 write;

  ClearRecord(partblnc);

  if(Action == 1) /*Create*/

    partblnc.Chapter   = PartBalAcc.Chapter;
    partblnc.iNumPlan  = PartBalAcc.iNumPlan;
    partblnc.Part      = PartBalAcc.Part;

    res = GetEQ( partblnc );

    if(res == true)

      res = false;

      AddErrorMessageExt(@ErrorMessage, 11, "Уже есть такой раздел");

    else

      partblnc.Name_Part = PartBalAcc.Name_Part;

      res = Insert(partblnc);

    end;

  else

    if(Action == 2) /*Modify*/
    
      var n1;
    
      partblnc.Chapter   = PartBalAcc.Chapter;
      partblnc.iNumPlan  = PartBalAcc.iNumPlan;
      partblnc.Part      = PartBalAcc.Part;

      res = GetEQ( partblnc );
      
      if(res == true)
      
        n1 = getpos(partblnc);
      
      else
        
        n1 = null;
      
      end;

      partblnc.Chapter   = savePartBalAcc.Chapter;
      partblnc.iNumPlan  = savePartBalAcc.iNumPlan;
      partblnc.Part      = savePartBalAcc.Part;

      res = GetEQ( partblnc );
      
      if(res == true)
      
        var n2 = getpos(partblnc);

        if((n1 == null) OR (n1 == n2))

          partblnc.Part      = PartBalAcc.Part;
          partblnc.Name_Part = PartBalAcc.Name_Part;

          res = UpDate(partblnc);
          
        else
          
          res = false;
          
          AddErrorMessageExt(@ErrorMessage, 11, "Уже есть такой раздел");

        end;
      
      end;

    end;

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_DeletePartBalAccount (Chapter, iNumPlan, Part)

  file partblnc("partblnc.dbt") key 0 write;

  var res = True;

  partblnc.Chapter  = Chapter;
  partblnc.iNumPlan = iNumPlan;
  partblnc.Part     = Part;

  res = GetEQ( partblnc );

  if(res == true)

    res = Delete(partblnc);

  end;

  return res;

end;