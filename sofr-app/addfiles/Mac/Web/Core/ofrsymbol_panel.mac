/*
$Name:        ofrsymbol_panel.mac
$Module:      Web - Главная книга
$Description: Функции для работы панели добавления\редактирования символов ОФР
*/

import BankInter,RSD;
import "globals.mac","ws_validation.mac";

class TWebOFRSymbolHist
  var OFRRecID  :integer;
  var Name      :string;
  var StartDate :Date;
end;

class TWebOFRSymbolObject
  var RecID       :integer;
  var Code        :string;
  var Balance     :string;
  var Principle   :bool;
  var OpenDate    :Date;
  var CloseDate   :Date;
  var NotUpgrade  :bool;
  var Version     :integer;
  var SymHistList ; // List< TWebOFRSymbolHist >
end;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;
  
macro GetSymHistList(OFRRecID)
  var cmd, rs;
  
  var query = "SELECT t_ofrrecid,t_name,t_startdate "
              "   FROM dopusymhist_dbt WHERE t_ofrrecid = ? "
              "   ORDER BY  t_startdate ";
  var Result = TArray();
  cmd = RsdCommand(query);
  
  cmd.addParam( "", RSDBP_IN, OFRRecID );
  
  rs = RsdRecordset(cmd);
  
  var i=0;
  while(rs.movenext)

    var obj = TWebOFRSymbolHist;

    obj.OFRRecID   = rs.value("T_OFRRECID");
    obj.Name       = rs.value("T_NAME");
    obj.StartDate  = rs.value("T_STARTDATE");
    
    Result.Value(i) = obj;
    i = i + 1;
  end;
  
  return Result;
end;
private macro ConvTypeBool(boolVal)
  if(boolVal != NULL)
    if(boolVal == true)
      return "X";
    else
      return "";
    end;
  else
    return ""
  end;
end;

macro WebCore_GetOFRSymbolObject(OFRRecID)
  var cmd, rs;
  
  var query = " SELECT * "
              "   FROM DOPUSYMB_DBT WHERE T_RECID = ? ";

  cmd = RsdCommand(query);
  
  cmd.NullConversion = true;
  
  cmd.addParam( "", RSDBP_IN, OFRRecID );
  
  rs = RsdRecordset(cmd);
  var obj = TWebOFRSymbolObject;
  
  if(rs.movenext)

    obj.RecID      = SQL_ConvType(rs.value("T_RecID"));
    obj.Code       = SQL_ConvType(rs.value("T_CODE"));
    obj.Balance    = SQL_ConvType(rs.value("T_BALANCE"));
    obj.Principle  = SQL_ConvTypeBool(rs.value("T_PRINCIPLE"));
    obj.OpenDate   = SQL_ConvType(rs.value("T_OPENDATE"));
    obj.CloseDate  = SQL_ConvType(rs.value("T_CLOSEDATE"));
    obj.NotUpgrade = SQL_ConvType(rs.value("T_NOTUPGRADE"));
    obj.Version    = SQL_ConvType(rs.value("T_VERSION"));
    
    obj.SymHistList = GetSymHistList(OFRRecID);
    
  end;
  return obj;
end;

macro WebCore_InsertUpdateOFRSymbolObject(OFRSymbolObject, IsNewRec)

  var opusymbRec = TRecHandler("opusymb.dbt");
  var opusymhistArray = TArray();
  
  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  if(OFRSymbolObject.RecID      != NULL) opusymbRec.rec.RecID       =  OFRSymbolObject.RecID                     ; end;
  if(OFRSymbolObject.Code       != NULL) opusymbRec.rec.Code        =  OFRSymbolObject.Code                      ; end;
  if(OFRSymbolObject.Balance    != NULL) opusymbRec.rec.Balance     =  OFRSymbolObject.Balance                   ; end;
  if(OFRSymbolObject.Principle  != NULL) opusymbRec.rec.Principle   =  ConvTypeBool(OFRSymbolObject.Principle)   ; end;
  if(OFRSymbolObject.OpenDate   != NULL) opusymbRec.rec.OpenDate    =  OFRSymbolObject.OpenDate                  ; end;
  if(OFRSymbolObject.CloseDate  != NULL) opusymbRec.rec.CloseDate   =  OFRSymbolObject.CloseDate                 ; end;
  if(OFRSymbolObject.NotUpgrade != NULL) opusymbRec.rec.NotUpgrade  =  ConvTypeBool(OFRSymbolObject.NotUpgrade)  ; end;
  if(OFRSymbolObject.Version    != NULL) opusymbRec.rec.Version     =  OFRSymbolObject.Version                   ; end;
  
  var i=0;
  var Count = OFRSymbolObject.SymHistList.size;

  while (i<Count)
    if(OFRSymbolObject.SymHistList[i] != NULL) 
      
      var opusymhistRec = TRecHandler("opusymhist.dbt");

      opusymhistRec.rec.OFRRecId    = OFRSymbolObject.SymHistList[i].OFRRecId;
      opusymhistRec.rec.Name        = OFRSymbolObject.SymHistList[i].Name;
      opusymhistRec.rec.StartDate   = OFRSymbolObject.SymHistList[i].StartDate;
      
      opusymhistArray[i] = opusymhistRec;
      
      i=i+1;
    end;
  end;
  
  var stat = CB_InsertUpdateOPUSYMB( opusymbRec, opusymhistArray, IsNewRec );
  
  if(stat != 0)

    res = False;

  end;

  if(res != True)

    AddErrorMessage(@ErrorMessage);

  end;
  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;
 
end;