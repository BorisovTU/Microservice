/*
$Name:        curdatealldprt_scroll.mac
$Module:      Главная книга
$Description: Макрос Список операционных дней во всех филиалах
*/

import "ws_partycommon.mac";
import "ws_datafilter.mac";
import "pr_party_lib.mac";

class TWebCurdateAllDprtGridItem
		var CurDate;
		var OpenedInDprt;
		var ClosedInDprt;
end;

class TWebCurdateAllDprtDetailGridItem
  var DprtName;
  var DprtPartyName;
  var IsClosed;
  var IsBalance;
end;

/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else
      return value_false;
   end;
end;

private macro GetSortStr( OrderItems,            /* Параметры сортировки */
                          DefaultSortStr:string, /* Строка сортировки по умолчанию (первичному ключу) */
                          Table:string           /* Название таблицы c . */
                        ):String

  var SortStr:string = "";

  if( (OrderItems == null) or (OrderItems.Size() <= 0) )

    SortStr = " " + DefaultSortStr + " ";

  else

    var i:integer = 0;

    while( i < OrderItems.size() )
      if (i!=0)
        SortStr = SortStr + ", " + OrderItems[i].Column + " " + IIF(OrderItems[i].Direction == 0, "asc", "desc");
      else
        SortStr = SortStr + "  " + OrderItems[i].Column + " " + IIF(OrderItems[i].Direction == 0, "asc", "desc");
      end;

      i = i + 1;
    end;

    SortStr = SortStr + " ";

  end;

  return SortStr;

end;

private macro GetQuery()

  var query = " SELECT DISTINCT "
              "        curdate.T_CURDATE AS CurDate, "
              "        (SELECT ListAgg(t1.t_Name, ', ') within group(order by t1.t_Name) "
              "           FROM ddp_dep_dbt t1 "
              "          WHERE t1.t_Code IN "
              "                   (SELECT t2.t_Branch "
              "                      FROM dcurdate_dbt t2 "
              "                     WHERE     t2.t_Curdate = curdate.T_CURDATE "
              "                           AND t2.t_IsClosed = CHR (0))) "
              "           AS OpenedInDprt, "
              "        (SELECT ListAgg(t1.t_Name, ', ') within group(order by t1.t_Name) "
              "           FROM ddp_dep_dbt t1 "
              "          WHERE t1.t_Code IN "
              "                   (SELECT t2.t_Branch "
              "                      FROM dcurdate_dbt t2 "
              "                     WHERE     t2.t_Curdate = curdate.T_CURDATE "
              "                           AND t2.t_IsClosed = CHR (88))) "
              "           AS ClosedInDprt "
              "   FROM dcurdate_dbt curdate "
              "  WHERE curdate.T_ISCLOSED != 'Z' ";

  return query;

end;

private const FILTERITEM_STATE    = 0; // Состояние
private const FILTERITEM_OPERDATE = 1; // Дата операционного дня
private const FILTERITEM_FILIAL   = 2; // Филиал

private macro GetAddWhereCondition(FilterItems)

  var condition = "";

  var FilterItemsCount = 0;
  var ValueCount = 0;

  while( FilterItemsCount < FilterItems.Size )

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_STATE ) // Состояние

      if(FilterItems[FilterItemsCount].Value.Size > 0)

        ValueCount = 0;

        condition = condition + " AND ( 1!=1 ";

        while( ValueCount < FilterItems[FilterItemsCount].Value.Size )

          if(FilterItems[FilterItemsCount].Value[ValueCount] == 0);

            condition = condition + " OR curdate.t_isclosed = CHR(0) ";

          end;

          if(FilterItems[FilterItemsCount].Value[ValueCount] == 1);

            condition = condition + " OR curdate.t_isclosed = 'X' ";

          end;

          ValueCount = ValueCount + 1;

        end;

        condition = condition + " ) ";

      end;

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_OPERDATE ) // Дата операционного дня

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Null )

        condition = condition + " AND curdate.t_curdate = NULL ";

      end;

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_From )

        condition = condition + " AND curdate.t_curdate ";
        condition = condition + " >= ";
        condition = condition + To_SQLData(FilterItems[FilterItemsCount].Value[0]);

      end;

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_To )

        condition = condition + " AND curdate.t_curdate ";
        condition = condition + " <= ";
        condition = condition + To_SQLData(FilterItems[FilterItemsCount].Value[0]);

      end;

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Between )

        condition = condition + " AND curdate.t_curdate ";
        condition = condition + " Between ";
        condition = condition + To_SQLData(FilterItems[FilterItemsCount].Value[0]);
        condition = condition + " AND ";
        condition = condition + To_SQLData(FilterItems[FilterItemsCount].Value[1]);

      end;

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Equal )

        condition = condition + " AND curdate.t_curdate ";
        condition = condition + " = ";
        condition = condition + To_SQLData(FilterItems[FilterItemsCount].Value[0]);

      end;

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_CurrentDate )

        condition = condition + " AND curdate.t_curdate = " + To_SQLData({curdate});

      end;

    end;
    
    if( FilterItems[FilterItemsCount].Id == FILTERITEM_FILIAL ) // Филиал

      if( (FilterItems[FilterItemsCount].Condition == FilterCondition_Equal) AND (FilterItems[FilterItemsCount].Value != null) AND (FilterItems[FilterItemsCount].Value.Size > 0) )

        ValueCount = 0;

        condition = condition + " and curdate.t_branch in (";

        while( ValueCount < FilterItems[FilterItemsCount].Value.Size )

          condition = condition + string(FilterItems[FilterItemsCount].Value[ValueCount].Code);

          if(ValueCount != FilterItems[FilterItemsCount].Value.Size - 1)

            condition = condition + ", ";

          end;

          ValueCount = ValueCount + 1;

        end;

        condition = condition + ") ";          

      end;

    end;    

    FilterItemsCount = FilterItemsCount + 1;

  end;

  return condition;

end;

macro WebCore_GetCurdateAllDprtGridItemCount(filteritems)

  var res : integer = 0;

  var cmd, rs;

  var query = " SELECT count(*) c FROM ( ";

  query = query + GetQuery();

  var strAddWhere = GetAddWhereCondition( filteritems );

  if( strAddWhere != "" )
    query = query + strAddWhere;
  end;

  query = query + " ) ";

  cmd = RsdCommand(query);

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    res = rs.value("c");

  end;

  return res;

end;

macro WebCore_GetCurdateAllDprtGridItemList(PageSize, PageNum, filteritems, orderitems)

  var cmd, rs, p;

  var Arr = TArray;

  var query = " SELECT t.* FROM ( SELECT rownum RN, tt.* FROM ( ";

  query = query + GetQuery();

  var strAddWhere = GetAddWhereCondition( filteritems );

  if( strAddWhere != "" )

     query = query + strAddWhere;

  end;

  query = query + " ORDER BY " + GetSortStr(orderitems,"curdate.t_curdate DESC","");

  if( PageSize > 0 )
    query = query + " ) tt WHERE ROWNUM <= " + string(PageSize*(PageNum + 1)) + " ) t WHERE t.RN >= " + string(PageSize*(PageNum) + 1) + " ";
  else
    query = query + " ) tt ) t ";
  end;

  cmd = RsdCommand(query);

  rs = RsdRecordset(cmd);

  while(rs.movenext)

    p = TWebCurdateAllDprtGridItem();

    p.CurDate      = SQL_ConvType(rs.value("CurDate"));
    p.OpenedInDprt = SQL_ConvType(rs.value("OpenedInDprt"));
    p.ClosedInDprt = SQL_ConvType(rs.value("ClosedInDprt"));

    Arr[Arr.size] = p;

  end;

  return Arr;

end;

macro WebCore_GetCurdateAllDprtDetailGridItemList(CurDate)

  var cmd, rs, p;

  var Arr = TArray;

  var query = " 	SELECT dp_dep.t_Name AS DprtName, "
              "        party.t_Name AS DprtPartyName, "
              "        curdate.t_IsClosed, "
              "        curdate.t_IsBalance "
              "   FROM dcurdate_dbt curdate "
              "        JOIN ddp_dep_dbt dp_dep "
              "           ON (dp_dep.t_Code = curdate.t_Branch) "
              "        JOIN dparty_dbt party "
              "           ON (party.t_PartyID = dp_dep.t_PartyID) "
              "  WHERE curdate.t_IsClosed <> 'Z' "
              "        AND curdate.t_CurDate = ? "
              "  ORDER BY dp_dep.t_Name ";

  cmd = RsdCommand(query);
  
  cmd.addParam( "", RSDBP_IN, CurDate );

  rs = RsdRecordset(cmd);

  while(rs.movenext)

    p = TWebCurdateAllDprtDetailGridItem();

    p.DprtName      = SQL_ConvType(rs.value("DprtName"));
    p.DprtPartyName = SQL_ConvType(rs.value("DprtPartyName"));
    p.IsClosed      = SQL_ConvTypeBool(rs.value("t_IsClosed"));
    p.IsBalance     = SQL_ConvTypeBool(rs.value("t_IsBalance"));
    
    Arr[Arr.size] = p;

  end;

  return Arr;
  
end;