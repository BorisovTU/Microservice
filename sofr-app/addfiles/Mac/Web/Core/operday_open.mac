 /*
 $Name: operday_open.mac
 $Module: Главная книга
 $Description: Макрос Списока операционных дней в филиале пользователя
 */

import globals;
import "ws_validation.mac";
import "ws_dprt.mac";
import "ws_file.mac";

class TWebOperdayOpenDateObject
  var LastCurDate;
  var OpenCurDate;
  var Department;
  var OperCanExecServProcHead;
  var IsAllDprtSelected;
end;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

macro WebCore_BeforeShowOperdayOpenDatePanel()

  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  if(({cTypePerson} != TP_BOSS) AND (CheckActionRestriction(143) == true))

    res = true;

  end;

  if(res != true)

    CreateErrMsg(22164);
    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_InitOperdayOpenDatePanel()

  var res;

  var OperdayOpenDateObj = TWebOperdayOpenDateObject();

  OperdayOpenDateObj.Department = GetTWsDepartmentByCode({OperDprt});

  var err;

  GetRegistryValue( "COMMON\\СЕРВИСНЫЕ ПРОЦЕДУРЫ\\ЗАПУСК СЕРВИСНЫХ ПРОЦЕДУР", V_BOOL, res, err );

  if(err == 0)

    if((res == true) AND ({OperDprt} == {NumDprt}))
      OperdayOpenDateObj.OperCanExecServProcHead = true;
    else
      OperdayOpenDateObj.OperCanExecServProcHead = false;
    end;

  else
    RunError("Ошибка при чтении настройки реестра!");
  end;

  var LastCurDate = date(0,0,0);
  var OpenCurDate = date(0,0,0);

  CB_GetLastOpenCurDateForDprt({OperDprt}, LastCurDate, OpenCurDate);

  if(LastCurDate == date(0,0,0))

    LastCurDate = null;

  end;

  OperdayOpenDateObj.LastCurDate = LastCurDate;
  OperdayOpenDateObj.OpenCurDate = OpenCurDate;

  return OperdayOpenDateObj;

end;

macro WebCore_GetOperdayOpenDatesPanel(Department, IsAllDprtSelected)

  var OperdayOpenDateObj = TWebOperdayOpenDateObject();

  if(IsAllDprtSelected == true)

    CB_GetLastOpenCurDateForDprt(0, OperdayOpenDateObj.LastCurDate, OperdayOpenDateObj.OpenCurDate);

  else

    if(Department != null)

      CB_GetLastOpenCurDateForDprt(Department.Code, OperdayOpenDateObj.LastCurDate, OperdayOpenDateObj.OpenCurDate);

    end;

  end;

  return OperdayOpenDateObj;

end;

macro WebCore_ExecOperdayOpenDate(DprtID, OpenDate)

  var fc = null;

  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  if((DprtID !=0) AND (DprtID != {OperDprt}))

    var err;

    GetRegistryValue( "COMMON\\СЕРВИСНЫЕ ПРОЦЕДУРЫ\\ЗАПУСК СЕРВИСНЫХ ПРОЦЕДУР", V_BOOL, res, err );

    if(err == 0)

      if((res != true) OR ({OperDprt} != {NumDprt}))

        AddErrorMessageExt(@ErrorMessage, GetErrorMsg( 22164 ));

        res = false;

      end;

    else
      RunError("Ошибка при чтении настройки реестра!");
    end;

  end;

  var LogName : string = "";

  res = CB_OpenDate(DprtID, OpenDate, @LogName);

  if(LogName != "")

    fc = GetTxtFileName(LogName);
    
    fc = ConvertTxt2Html(fc); 
    
    fc = GetTextFile(fc);

  end;

  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(fc);

  return responseBuilder.Result;

end;