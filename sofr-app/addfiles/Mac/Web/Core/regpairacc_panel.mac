 /*
 $Name: regpairacc_panel.mac
 $Module: Главная книга
 $Description: Макрос Выведение единого результата
 */

import globals;
import "ws_validation.mac";
import "ws_dprt.mac";
import "ws_file.mac";
import serviceaction;
import cb_sql;
import rsd, oralib, likepy;
import SPInter;
import BankInter;
import PTInter;
import regpair;

class TWebFIKind
  var ID   /*: integer*/;
  var Name /*: string*/;
end;

class TWebRegPairAccPrm
  var ParamID : integer  /*: integer*/;
  var RecNumber : integer /*: integer*/;
end;

class TWebRegPairAccObject
  var OperCanExecServProcHead /*: bool*/;
  var RegulateDate            /*: date*/;
  var Department              /*: TWsDepartment*/;
  var IsAllDprtsSelected      /*: bool*/;
  var ChapterListSelected     /*: Array<TWebChapter>*/;
  var IsAllChaptersSelected   /*: bool*/;
  var FIKind                  /*: TWebFIKind/integer*/;
  var FIKindList              /*: Array<TWebFIKind>*/;
  var IsAllFIKindsSelected    /*: bool*/;
  var FIListSelected          /*: Array<TWsFinInstr>*/;
  var IsAllFIsSelected        /*: bool*/;
  var Account                 /*: TWebAccount*/;
  var IsAllAccountsSelected   /*: bool*/;
  var NumberPack              /*: integer*/;
  var NumberDoc               /*: integer*/;
  var PrintRegistry           /*: bool*/;
  var Backout                 /*: bool*/;
  var FullAccess              /*: bool*/;
end;

private var _countErrors;

private var _responsebuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)
  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  _responsebuilder.AddMessage( ErrorMessage );
end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)
  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  _responsebuilder.AddMessage( ErrorMessage );
end;

macro WebCore_BeforeShowRegPairAccPanel()
  var res = true;
  var err;
  var regVal;

  _responsebuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  if(res and ({cTypePerson} == TP_BOSS))
    res = false;
    AddErrorMessageExt(@ErrorMessage, GetErrorMsg(548));
  end;


  if(res)
    GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\БЕЗОПАСНОСТЬ\\ACCESS_REGPAIR", V_STRING, regVal, err);
    if(err == 0)
      if(regVal == "NONE")
        res = false;
        AddErrorMessageExt(@ErrorMessage, GetErrorMsg(256));
      end;
    else
      RunError("Ошибка при чтении настройки реестра!");
    end; 
  end;

  _responseBuilder.SetUserObject(res);

  return _responseBuilder.Result;
end;

macro WebCore_InitRegPairAccPanel(Backout)
  var RegPairAccObj  = TWebRegPairAccObject();
  var FirstDate = date(0, 0, 0);
  var err;
  var res = CB_GetFirstOpenDate({OperDprt}, @FirstDate);
  
  if(res == true)
    RegPairAccObj.RegulateDate = FirstDate;
  end;
  
  RegPairAccObj.Department = GetTWsDepartmentByCode({OperDprt});
  
  GetRegistryValue("COMMON\\СЕРВИСНЫЕ ПРОЦЕДУРЫ\\ЗАПУСК СЕРВИСНЫХ ПРОЦЕДУР", V_BOOL, res, err);

  if(err == 0)
    if((res == true) and ({OperDprt} == {NumDprt}))
      RegPairAccObj.OperCanExecServProcHead = true;
    else
      RegPairAccObj.OperCanExecServProcHead = false;
    end;
  else
    RunError("Ошибка при чтении настройки реестра!");
  end; 

  RegPairAccObj.FIKindList = TArray();
  RegPairAccObj.FIKindList[0] = TWebFIKind();
  RegPairAccObj.FIKindList[0].ID   = 1;
  RegPairAccObj.FIKindList[0].Name = "Валюты";
  RegPairAccObj.FIKindList[1] = TWebFIKind();
  RegPairAccObj.FIKindList[1].ID   = -1;
  RegPairAccObj.FIKindList[1].Name = "Валюты, кроме национальной";
  RegPairAccObj.FIKindList[2] = TWebFIKind();
  RegPairAccObj.FIKindList[2].ID   = 2;
  RegPairAccObj.FIKindList[2].Name = "Ценные бумаги";
  RegPairAccObj.FIKindList[3] = TWebFIKind();
  RegPairAccObj.FIKindList[3].ID   = 6;
  RegPairAccObj.FIKindList[3].Name = "Драгметаллы";

  GetRegistryValue("CB\\REGPAIRACC\\NUMBER_PACK", V_INTEGER, res, err);

  if(err == 0)
    RegPairAccObj.NumberPack = res;
  else
    RunError("Ошибка при чтении настройки реестра!");
  end;

  RegPairAccObj.NumberDoc = 1; 
  RegPairAccObj.PrintRegistry = true; 
  RegPairAccObj.Backout = Backout; 

  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\БЕЗОПАСНОСТЬ\\ACCESS_REGPAIR", V_STRING, res, err);

  if(err == 0)
    if(res == "FULL")
      RegPairAccObj.FullAccess = true;
    else
      RegPairAccObj.FullAccess = false;
    end;
  else
    RunError("Ошибка при чтении настройки реестра!");
  end;

  RegPairAccObj.IsAllChaptersSelected = true;
  RegPairAccObj.IsAllFIKindsSelected = true;
  RegPairAccObj.IsAllFIsSelected = true;
  RegPairAccObj.IsAllAccountsSelected = true;

  return RegPairAccObj;
end;

macro WebCore_GetRegPairAccPanel(Department, IsAllDprtSelected)
  var res;
  var RegulateDate;
  if(IsAllDprtSelected == true)
    res = CB_GetFirstOpenDate(0, @RegulateDate);
  else
    if(Department != null)
      res = CB_GetFirstOpenDate(Department.Code, @RegulateDate);
    end;
  end;
  return RegulateDate;
end;

private macro _InsertParamXML(XML : string, NumberDoc)
  var query = "";  
  var cmd;
  var ID;
  
  query = query + "INSERT INTO dregpairaccprm_dbt ( t_ParamXML, t_SysDate, t_SysTime, t_NextNumberDoc ) ";
  query = query + "VALUES ( :XML, TRUNC(SYSDATE), TO_DATE('01-01-0001:' || TO_CHAR(SYSDATE, 'HH24:MI:SS'), 'DD-MM-YYYY:HH24:MI:SS'), :NumberDoc ) ";
  query = query + "RETURNING t_ParamID INTO :ID ";
  cmd = RsdCommand(query);
  cmd.addParam("XML", RSDBP_IN, XML);
  cmd.addParam("NumberDoc", RSDBP_IN, NumberDoc);
  cmd.addParam("ID", RSDBP_OUT, V_INTEGER);
  cmd.execute();
  
  ID = cmd.value("ID");
  return ID;
end;

private macro _GetParamXML(ID : integer)
  var query = "";
  var ParamXML;

  query = query + "SELECT ";
  query = query + "  t_ParamID, t_ParamXML "; /*!!! CLOB-поле не должно быть первым*/
  query = query + "  FROM dregpairaccprm_dbt ";
  query = query + " WHERE t_ParamID = " + ID;
  /*!!!важно!!! инструмент требует, чтобы CLOB-поле не было первым в SELECT'е, иначе ошибка выполнения*/
  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if (ds.MoveNext())
    ParamXML = ds.ParamXML;
  end;
  return ParamXML;
end;

private macro _GetCountAccounts(ID : integer)
  var Count;

  var query = "";
  var cmd, rs;

  query = query + "SELECT COUNT(*) ";
  query = query + "  FROM dregpairaccsel_dbt ";
  query = query + " WHERE t_ParamID = " + ID;
  
  cmd = RsdCommand(query);

  rs = RsdRecordset(cmd);

  if(rs.movenext)
    Count = rs.value(0);
  end;

  return Count;
end;

private macro _FormFisclogInfo(RegPairAccObj)
  var info = "";
  var i, k;
  if(RegPairAccObj.Backout)
    info = info + "Откат выведения единого результата.\n";
  else
    info = info + "Выведение единого результата.\n";
  end;

  if(RegPairAccObj.IsAllDprtsSelected == true)
    info = info + "Узел ТС: Все филиалы\n";
  else
    info = info + "Узел ТС: " + RegPairAccObj.Department.Name_ + " " + RegPairAccObj.Department.PartyName +"\n";
  end;

  if(RegPairAccObj.IsAllChaptersSelected)
    info = info + "Глава счетов: Все главы\n";
  elif((RegPairAccObj.ChapterListSelected != null) and (RegPairAccObj.ChapterListSelected.size() > 0))
    i = 0;
    k = RegPairAccObj.ChapterListSelected.size();
    while(i < k)
      if(i == 0)
        info = info + "Глава счетов: " + RegPairAccObj.ChapterListSelected[i].Chapter + " (" + RegPairAccObj.ChapterListSelected[i].Name + ")";
      else
        info = info + "              " + RegPairAccObj.ChapterListSelected[i].Chapter + " (" + RegPairAccObj.ChapterListSelected[i].Name + ")";
      end;
      i = i + 1;
      if(i < k)
        info = info + ",\n";
      else
        info = info + "\n";
      end;
    end;
  end;

  if((RegPairAccObj.Account != null) and (RegPairAccObj.Account.Account != null))
    info = info + "Номер счета: " + RegPairAccObj.Account.Account + "\n";
  else
    info = info + "Номер счета:\n";
  end;

  info = info + "Дата урегулирования: " + string(RegPairAccObj.RegulateDate:f) + "\n";
  
  if(RegPairAccObj.IsAllFIKindsSelected)
    info = info + "Вид Финансового инструмента: Все\n";
  elif(RegPairAccObj.FIKind != null)
    info = info + "Вид Финансового инструмента: " + RegPairAccObj.FIKind.Name + "\n";
  end;

  if(RegPairAccObj.IsAllFIsSelected)
    info = info + "Код Финансового инструмента: Все\n";
  elif((RegPairAccObj.FIListSelected != null) and (RegPairAccObj.FIListSelected.size() > 0))
    i = 0;
    k = RegPairAccObj.FIListSelected.size();
    while(i < k)
      if(i == 0)
        info = info + "Код Финансового инструмента: " + RegPairAccObj.FIListSelected[i].CodeWidget + " (" + RegPairAccObj.FIListSelected[i].Name + ")";
      else
        info = info + "                             " + RegPairAccObj.FIListSelected[i].CodeWidget + " (" + RegPairAccObj.FIListSelected[i].Name + ")";
      end;
      i = i + 1;
      if(i < k)
        info = info + ",\n";
      else
        info = info + "\n";
      end;
    end;
  end;

  info = info + "Номер пачки: " + RegPairAccObj.NumberPack + "\n";
  
  info = info + "Номер первой проводки урегулирования: " + RegPairAccObj.NumberDoc + "\n";

  if(RegPairAccObj.PrintRegistry)
    info = info + "Печать реестра: Да\n";
  else
    info = info + "Печать реестра: Нет\n";
  end;

  return info;
end;

macro WebCore_MakeRegPairAccList(RegPairAccObj)
  var retObj = TWebRegPairAccPrm;
  var xml;
  var err;
  var stmt = "";
  var i;
  var cmd;
  var info;
  
  err = ConvertToXML(RegPairAccObj, null, xml);
  if(not err)
    retObj.ParamID = _InsertParamXML(xml, RegPairAccObj.NumberDoc);
  end;

  stmt = stmt + "INSERT INTO dregpairaccsel_dbt ";
  stmt = stmt + "( t_ParamID ";
  stmt = stmt + " ,t_RecID ";
  stmt = stmt + " ,t_Account ";
  stmt = stmt + " ,t_Chapter ";
  stmt = stmt + " ,t_Code_Currency ";
  stmt = stmt + " ,t_Department ";
  stmt = stmt + " ,t_Branch ";
  stmt = stmt + " ,t_Error ";
  stmt = stmt + " ,t_ErrMsg ";
  stmt = stmt + ") ";
  stmt = stmt + "SELECT ";
  stmt = stmt + " " + retObj.ParamID;
  stmt = stmt + " ,ROWNUM ";
  stmt = stmt + " ,acc.t_Account ";
  stmt = stmt + " ,acc.t_Chapter ";
  stmt = stmt + " ,acc.t_Code_Currency ";
  stmt = stmt + " ,acc.t_Department ";
  stmt = stmt + " ,acc.t_Branch ";
  stmt = stmt + " ,0 ";
  stmt = stmt + " ,CHR(1) ";
  stmt = stmt + "  FROM daccount_dbt acc ";
  stmt = stmt + " WHERE acc.t_Open_Close = CHR(0) ";
  stmt = stmt + "   AND (   acc.t_Type_Account LIKE '%Ш%'  /*TA_PAIR - Парный счет*/ ";
  stmt = stmt + "        OR acc.t_Type_Account LIKE '%N%') /*TA_ROLLUP - Сворачиваемый счет*/ ";
  stmt = stmt + "   AND NOT acc.t_Type_Account LIKE '%Ц%' /*TA_OVESTIM - Переоценка валюты*/";

  if(RegPairAccObj.IsAllDprtsSelected == true)
    stmt = stmt + "   AND acc.t_Department IN ";
    stmt = stmt + "          (    SELECT t_Code ";
    stmt = stmt + "                 FROM ddp_dep_dbt ";
    stmt = stmt + "                WHERE     t_Status = 2 /* DEPARTMENT_STATUS_ACTIVE Активное */ ";
    stmt = stmt + "                      AND t_AccessMode = 1 /* DEPARTMENT_ACCESS_ONLINE Доступ On-Line */ ";
    stmt = stmt + "           START WITH t_Code = " + {NumDprt} + " /* Ид. головного подразделения */ ";
    stmt = stmt + "           CONNECT BY     t_ParentCode = PRIOR t_Code "; 
    stmt = stmt + "                      AND t_NodeType = 1 /* DEPARTMENT_TYPE_FILIAL Филиал */) ";
  else
    if(RegPairAccObj.Department.NodeType == 1)
      stmt = stmt + "   AND acc.t_Department = " + RegPairAccObj.Department.Code;
    else
      stmt = stmt + "   AND acc.t_Branch = " + RegPairAccObj.Department.Code;
    end;
  end;

  if(RegPairAccObj.FullAccess == false)
    if({cTypePerson} == TP_OPER)
      stmt = stmt + "   AND acc.t_Oper = " + {oper} + " ";
    elif({cTypePerson} ==  TP_DEP)
      stmt = stmt + "   AND (acc.t_Oper = " + {oper} + " OR (acc.t_Oper >= " + {GroupOperF} + " AND acc.t_Oper <= " + {GroupOperL} + "))";
    end;
  end;

  if((RegPairAccObj.ChapterListSelected != null) and (RegPairAccObj.ChapterListSelected.size() > 0))
    stmt = stmt + "   AND ( ";
    i = 0;
    while(i < RegPairAccObj.ChapterListSelected.size())
      if(i > 0)
        stmt = stmt + " OR ";
      end;
      stmt = stmt + " acc.t_Chapter = " + RegPairAccObj.ChapterListSelected[i].Chapter;
      i = i + 1;
    end;
    stmt = stmt + "       ) ";
  end;

  if((RegPairAccObj.Account != null) and (RegPairAccObj.Account.Account != null))
    if(Web_StringIsMaskEx(RegPairAccObj.Account.Account, CSMFL_Mask ))
      stmt = stmt + "   AND " + ConvertMaskToSQLFormat(RegPairAccObj.Account.Account, "acc.t_Account", CSMFL_Mask);
    else
      stmt = stmt + "   AND acc.t_Account = '" + RegPairAccObj.Account.Account + "' ";
      stmt = stmt + "   AND acc.t_Chapter = " + RegPairAccObj.Account.Chapter + " ";
      stmt = stmt + "   AND acc.t_Code_Currency = " + RegPairAccObj.Account.Currency + " ";
    end;
  end;

  if(RegPairAccObj.FIKind != null)
    if  (RegPairAccObj.FIKind.ID == 1)
      stmt = stmt + "   AND acc.t_Code_Currency IN (SELECT t_FIID FROM dfininstr_dbt WHERE t_FI_Kind = 1) ";
    elif(RegPairAccObj.FIKind.ID == -1)
      stmt = stmt + "   AND acc.t_Code_Currency IN (SELECT t_FIID FROM dfininstr_dbt WHERE t_FI_Kind = 1) AND acc.t_Code_Currency != 0 ";
    elif(RegPairAccObj.FIKind.ID == 2)
      stmt = stmt + "   AND acc.t_Code_Currency IN (SELECT t_FIID FROM dfininstr_dbt WHERE t_FI_Kind = 2) ";
    elif(RegPairAccObj.FIKind.ID == 6)
      stmt = stmt + "   AND acc.t_Code_Currency IN (SELECT t_FIID FROM dfininstr_dbt WHERE t_FI_Kind = 6) ";
    end;
  end;

  if((RegPairAccObj.FIListSelected != null) and (RegPairAccObj.FIListSelected.size() > 0))
    stmt = stmt + "   AND ( ";
    i = 0;
    while(i < RegPairAccObj.FIListSelected.size())
      if(i > 0)
        stmt = stmt + " OR ";
      end;
      stmt = stmt + " acc.t_Code_Currency = " + RegPairAccObj.FIListSelected[i].FIID;
      i = i + 1;
    end;
    stmt = stmt + "       ) ";
  end;

  cmd = RsdCommand(stmt);
  cmd.execute();

  retObj.RecNumber = _GetCountAccounts(retObj.ParamID);

  info = _FormFisclogInfo(RegPairAccObj);
  
  if(RegPairAccObj.Backout)
    WriteFiscLog(UOL_B_RBUCPAIR_BACK_PROC_FISCLOG, info);
  else
    WriteFiscLog(UOL_B_RBUCPAIR_PROC_FISCLOG, info);
  end;

  return retObj;
end;

macro WebCore_ExecRegPairAcc
(
  ParamID
 ,RecID
 ,PrintRegistry
 ,RegulateDate
 ,Backout
 ,NumberDoc
 ,NumberPack
)
  file regpairaccsel("regpairaccsel.dbt") key 0;

  regpairaccsel.ParamID = ParamID;
  regpairaccsel.RecID = RecID;
  if(GetEQ(regpairaccsel))
    CB_RegulatePairAccount
    (
      RegulateDate     
     ,regpairaccsel.Chapter          
     ,regpairaccsel.Code_Currency    
     ,regpairaccsel.Account          
     ,null    
     ,Backout          
     ,ParamID
     ,RecID  
     ,PrintRegistry    
     ,NumberDoc        
     ,NumberPack       
    );
  end;
  
  _responsebuilder = WebResponseBuilder();

  _responsebuilder.SetUserObject(true);
  
  return _responsebuilder.Result;
end;

private macro _TruncateTable(TableName) : bool
  var cmd : RsdCommand;
  cmd = RsdCommand("TRUNCATE TABLE " + TableName);
  cmd.execute();
end;

/* Вставить во временную таблицу данные для построения Реестра проводок.
Используется существующая реализация построения Реестра.*/
private macro _InsertIntoCarryLog(ParamID, DprtID)
  var stmt = "";
  var cmd;

  stmt = stmt + "INSERT INTO drp_carry_log_tmp ";
  stmt = stmt + "( ";
  stmt = stmt + "  t_CarryID ";
  stmt = stmt + " ,t_Date_Carry ";
  stmt = stmt + " ,t_Chapter ";
  stmt = stmt + " ,t_Currency ";
  stmt = stmt + " ,t_Account_Payer ";
  stmt = stmt + " ,t_Account_Receiver ";
  stmt = stmt + " ,t_Numb_Document ";
  stmt = stmt + " ,t_Sum ";
  stmt = stmt + " ,t_SumNatCur ";
  stmt = stmt + " ,t_Number_Pack ";
  stmt = stmt + " ,t_DprtID ";
  stmt = stmt + ") ";
  stmt = stmt + "SELECT  ";
  stmt = stmt + "  t_CarryID ";
  stmt = stmt + " ,t_Date_Carry ";
  stmt = stmt + " ,t_Chapter ";
  stmt = stmt + " ,t_Currency ";
  stmt = stmt + " ,t_Account_Payer ";
  stmt = stmt + " ,t_Account_Receiver ";
  stmt = stmt + " ,t_Numb_Document ";
  stmt = stmt + " ,t_Sum ";
  stmt = stmt + " ,t_SumNatCur ";
  stmt = stmt + " ,t_Number_Pack ";
  stmt = stmt + " ,t_DprtID ";
  stmt = stmt + "  FROM dregpairacctrn_dbt ";
  stmt = stmt + " WHERE t_ParamID = " + ParamID;
  stmt = stmt + "   AND t_DprtID = " + DprtID;
  
  cmd = RsdCommand(stmt);
  cmd.execute();
end;

private macro _PrintRegistryToFile(ParamID, RegPairAccObj, ToFileName)
  var query = "";
  var cmd, rs;

  query = query + "    SELECT t_Code ";
  query = query + "      FROM ddp_dep_dbt ";

  if(RegPairAccObj.IsAllDprtsSelected == true)
    query = query + "     WHERE     t_Status = 2 /* DEPARTMENT_STATUS_ACTIVE Активное */ ";
    query = query + "           AND t_AccessMode = 1 /* DEPARTMENT_ACCESS_ONLINE Доступ On-Line */ ";
    query = query + "START WITH t_Code = " + {NumDprt} + " /* Ид. головного подразделения */ ";
    query = query + "CONNECT BY     t_ParentCode = PRIOR t_Code "; 
    query = query + "           AND t_NodeType = 1 /* DEPARTMENT_TYPE_FILIAL Филиал */ ";
  else
    query = query + "     WHERE t_Code = " + RegPairAccObj.Department.Code;
  end;

  cmd = RsdCommand(query);

  rs = RsdRecordset(cmd);

  setoutput(ToFileName, true);
  while(rs.movenext)
    _TruncateTable("drp_carry_log_tmp");
    _InsertIntoCarryLog(ParamID, rs.value(0));
    PrintCarryLogExtPair(rs.value(0), RegPairAccObj.Backout);
  end;
  setoutput(null, true);
end;

private macro _PrintReportTop(RegPairAccObj)
  var info = "";
  var i, k;

  if(RegPairAccObj.Backout)
    info = info + "Протокол удаления проводок выведения единого результата на " + string({curdate}:f) + "\n";
  else
    info = info + "Протокол выполнения процедуры выведения единого результата на " + string({curdate}:f) + "\n";
  end;
  info = info + "\n";
  info = info + "Дата формирования: " + string(date():f) + "\n";
  info = info + "Время формирования: " + string(time():f) + "\n";
  info = info + "Операционист: " + {oper} + " " + {Name_Oper} + "\n";
  info = info + "\n";
  info = info + "Параметры запуска:\n";
  if(RegPairAccObj.IsAllDprtsSelected == true)
    info = info + "  Узел ТС: Все филиалы\n";
  else
    info = info + "  Узел ТС: " + RegPairAccObj.Department.Name_ + " " + RegPairAccObj.Department.PartyName + "\n";
  end;
  info = info + "  Дата выведения единого результата: " + string(RegPairAccObj.RegulateDate:f) + "\n";
  if(RegPairAccObj.IsAllChaptersSelected)
    info = info + "  Глава счетов: Все главы\n";
  elif((RegPairAccObj.ChapterListSelected != null) and (RegPairAccObj.ChapterListSelected.size() > 0))
    i = 0;
    k = RegPairAccObj.ChapterListSelected.size();
    while(i < k)
      if(i == 0)
        info = info + "  Глава счетов: " + RegPairAccObj.ChapterListSelected[i].Chapter + " (" + RegPairAccObj.ChapterListSelected[i].Name + ")";
      else
        info = info + "                " + RegPairAccObj.ChapterListSelected[i].Chapter + " (" + RegPairAccObj.ChapterListSelected[i].Name + ")";
      end;
      i = i + 1;
      if(i < k)
        info = info + ",\n";
      else
        info = info + "\n";
      end;
    end;
  end;

  if(RegPairAccObj.IsAllFIKindsSelected)
    info = info + "  Вид Финансового инструмента: Все\n";
  elif(RegPairAccObj.FIKind != null)
    info = info + "  Вид Финансового инструмента: " + RegPairAccObj.FIKind.Name + "\n";
  end;

  if(RegPairAccObj.IsAllFIsSelected)
    info = info + "  Финансовый инструмент: Все\n";
  elif((RegPairAccObj.FIListSelected != null) and (RegPairAccObj.FIListSelected.size() > 0))
    i = 0;
    k = RegPairAccObj.FIListSelected.size();
    while(i < k)
      if(i == 0)
        info = info + "  Финансовый инструмент: " + RegPairAccObj.FIListSelected[i].CodeWidget + " (" + RegPairAccObj.FIListSelected[i].Name + ")";
      else
        info = info + "                         " + RegPairAccObj.FIListSelected[i].CodeWidget + " (" + RegPairAccObj.FIListSelected[i].Name + ")";
      end;
      i = i + 1;
      if(i < k)
        info = info + ",\n";
      else
        info = info + "\n";
      end;
    end;
  end;

  info = info + "  Номер пачки: " + RegPairAccObj.NumberPack + "\n";
  
  info = info + "  Номер первой проводки урегулирования: " + RegPairAccObj.NumberDoc + "\n";

  info = info + "\n";

  print(info);
end;

private macro _GetDprtPartyName( DprtID : integer )
  record party("party.dbt");
  file dp_dep("dp_dep.dbt") key 0;
  var DprtPartyName = "";
  dp_dep.Code = DprtID;
  if( GetEQ(dp_dep) )
    if( ПолучитьСубъекта(dp_dep.PartyID, party) == 0 )
      DprtPartyName = party.Name;
    end;
  end;
  return DprtPartyName;
end;

private macro _GetDprt( DprtID : integer, dprt )
  file dp_dep("dp_dep.dbt") key 0;
  dp_dep.Code = DprtID;
  if( GetEQ( dp_dep ))
    copy( dprt, dp_dep );
  end;
end;

private macro _WereAccountsSelected(ParamID, DprtID, RegPairAccObj)
  var query = "";
  var cmd, rs;
  if(RegPairAccObj.IsAllDprtsSelected == true)
    query = query + "SELECT COUNT(*) FROM dregpairaccsel_dbt WHERE t_Department = " + DprtID + " AND t_ParamID = " + ParamID;
  else
    query = query + "SELECT COUNT(*) FROM dregpairaccsel_dbt WHERE (t_Branch = " + DprtID + " OR t_Department = " + DprtID + ") AND t_ParamID = " + ParamID;
  end;
  cmd = RsdCommand(query);
  rs = RsdRecordset(cmd);
  if(rs.movenext and (rs.value(0) > 0))
    return true;
  end;
  return false;
end;

private macro _GetErrorAccounts(ParamID, DprtID, RegPairAccObj)
  var query = "";
  var cmd, rs;
  if(RegPairAccObj.IsAllDprtsSelected == true)
    query = query + "SELECT t_ErrMsg FROM dregpairaccsel_dbt WHERE t_Error <> 0 AND t_Department = " + DprtID + " AND t_ParamID = " + ParamID;
  else
    query = query + "SELECT t_ErrMsg FROM dregpairaccsel_dbt WHERE t_Error <> 0 AND (t_Branch = " + DprtID + " OR t_Department = " + DprtID + ") AND t_ParamID = " + ParamID;
  end;
  cmd = RsdCommand(query);
  rs = RsdRecordset(cmd);
  return rs;
end;

private macro _PrintReportDprt(ParamID, DprtID, RegPairAccObj)
  var info = "";
  var noAccounts = false;
  var rs;
  var i;

  if(RegPairAccObj.IsAllDprtsSelected == true)
    info = info + "Узел ТС - " + _GetDprtPartyName(DprtID) + "\n";
  end;

  info = info + "\n";
  info = info + "Шаги выполнения:\n";

  info = info + "  1. Отбор парных счетов:\n";

  if(_WereAccountsSelected(ParamID, DprtID, RegPairAccObj))
    info = info + "     Отбор парных счетов выполнен успешно.\n";
  else
    info = info + "     Не найдены счета для урегулирования.\n";
    noAccounts = true;
    _countErrors = _countErrors + 1;
  end;

  info = info + "  2. Выполнение проводок по урегулированию:\n";
  i = 0;
  if(noAccounts)
    info = info + "     Невозможно выполнить проводки по урегулированию.\n";
  else
    rs = _GetErrorAccounts(ParamID, DprtID, RegPairAccObj);
    while(rs.movenext)
      if(i == 0)
        info = info + "     Во время выполнения процедуры были обнаружены ошибки:\n";
      end;
      info = info + "     " + rs.value(0);
      if(index(rs.value(0), "\n") != strlen(rs.value(0)))
        info = info + "\n";
      end;
      i = i + 1;
      _countErrors = _countErrors + 1;
    end;
    if(i == 0)
      info = info + "     Проводки по урегулированию выполнены успешно.\n";
    end;
  end;

  if((not noAccounts) and (i == 0))
    info = info + "Процедура выполнена успешно!\n";
  else
    info = info + "Во время выполнения процедуры были обнаружены ошибки.\n";
  end;

  info = info + "\n";

  print(info);
end;

private macro _PrintReportBottom(RegPairAccObj)
  var info = "";
  if(RegPairAccObj.IsAllDprtsSelected == true)
    if(_countErrors > 0)
      info = info + "Процедура выполнена не во всех филиалах.\n";
    else
      info = info + "Процедура выполнена во всех филиалах.\n";
    end;
    print(info);
  end;
end;

private macro _PrintReportToFile(ParamID, RegPairAccObj, ToFileName)
  var query = "";
  var cmd, rs;

  query = query + "    SELECT t_Code ";
  query = query + "      FROM ddp_dep_dbt ";

  if(RegPairAccObj.IsAllDprtsSelected == true)
    query = query + "     WHERE     t_Status = 2 /* DEPARTMENT_STATUS_ACTIVE Активное */ ";
    query = query + "           AND t_AccessMode = 1 /* DEPARTMENT_ACCESS_ONLINE Доступ On-Line */ ";
    query = query + "START WITH t_Code = " + {NumDprt} + " /* Ид. головного подразделения */ ";
    query = query + "CONNECT BY     t_ParentCode = PRIOR t_Code "; 
    query = query + "           AND t_NodeType = 1 /* DEPARTMENT_TYPE_FILIAL Филиал */ ";
  else
    query = query + "     WHERE t_Code = " + RegPairAccObj.Department.Code;
  end;

  cmd = RsdCommand(query);

  rs = RsdRecordset(cmd);

  _countErrors = 0;

  setoutput(ToFileName, true);
  _PrintReportTop(RegPairAccObj);

  while(rs.movenext)
    _PrintReportDprt(ParamID, rs.value(0), RegPairAccObj);
  end;

  _PrintReportBottom(RegPairAccObj);
  setoutput(null, true);
end;

private macro _GetReportFileName(ParamID)
  var ToFileName = GetTxtFileName("regpairacc_" + ParamID);
  return ToFileName;
end;

macro WebCore_GetRegPairAccReport(ParamID)
  var ToFileName = _GetReportFileName(ParamID);

  var info;

  setoutput(ToFileName);
  setoutput(null, true);
  var RegPairAccObj;
  ConvertToRSL(_GetParamXML(ParamID), @RegPairAccObj);
  info = _FormFisclogInfo(RegPairAccObj);
  
  if(RegPairAccObj.Backout)
    WriteFiscLog(UOL_E_RBUCPAIR_BACK_PROC_FISCLOG, info);
  else
    WriteFiscLog(UOL_E_RBUCPAIR_PROC_FISCLOG, info);
  end;
  _PrintReportToFile(ParamID, RegPairAccObj, ToFileName);
  if(RegPairAccObj.PrintRegistry)
    _PrintRegistryToFile(ParamID, RegPairAccObj, ToFileName);
  end;
  /*
  return ToFileName;
  */
  var fc = null;
  
  if(ToFileName != "")

    fc = ConvertTxt2Html(ToFileName);
    
    fc = GetTextFile(fc);

  end;

  _responsebuilder = WebResponseBuilder();

  _responsebuilder.SetUserObject(fc);
  
  return _responsebuilder.Result;
end;