/*
$Name:             ofrsymbol_scroll.mac
$Module:           Отчеты ЦБ
$Description:      Сервисы символов ОФР.
*/
import rsd,globals;
import BankInter;
import "ws_validation.mac";
import "ws_datafilter.mac";

class TWebOFRSymbolGridItem
  var RecID     :integer;
  var Code      :string;
  var Balance   :string;
  var Name      :string;
  var Principle :bool;
  var OpenDate  :Date;
  var CloseDate :Date;
end;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else 
      return value_false;
   end;
end;

/* Идентификаторы условий фильтрации */
private const FILTERITEM_NUMBER               = 0;   // Номер символа ОФР.
private const FILTERITEM_STRDATE              = 1;   // Дата открытия символа.
private const FILTERITEM_ENDDATE              = 2;   // Дата закрытия символа.
private const FILTERITEM_NUMBERACC            = 3;   // Номер балансового счета.

private macro GetSortStr( OrderItems,            /* Параметры сортировки */
                          DefaultSortStr:string, /* Строка сортировки по умолчанию (первичному ключу) */
                          Table:string           /* Название таблицы c . */
                        ):String
                        
  var SortStr:string = "";

  if( (OrderItems == null) or (OrderItems.Size() <= 0) )
  
     SortStr = " " + DefaultSortStr + " ";
     
  else
     if (OrderItems[0].Column == "Name")
        SortStr = " t_" + OrderItems[0].Column + " " + IIF(OrderItems[0].Direction == 0, "asc", "desc");
     else
        SortStr = " s.t_" + OrderItems[0].Column + " " + IIF(OrderItems[0].Direction == 0, "asc", "desc");
     end;
     var i:integer = 1;
     while( i < OrderItems.size() )
        SortStr = SortStr + ", t_" + OrderItems[i].Column + " " + IIF(OrderItems[i].Direction == 0, "asc", "desc");
        i = i + 1;
     end;
     SortStr = SortStr + " ";
     
  end;

  return SortStr;
  
end;

macro GetAddWhereCondition(FilterItems, where, cmd)

  var condition = "";

  var FilterItemsCount = 0;
  
  while( FilterItemsCount < FilterItems.Size )

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_NUMBER ) // Номер символа ОФР.

      condition = condition + " AND " + GetFilterStringSQLCondition( FilterItems[FilterItemsCount], "s.T_CODE", true, false );

    end;
    
    if( FilterItems[FilterItemsCount].Id == FILTERITEM_STRDATE ) // Дата открытия символа.

      condition = condition + " AND " + GetFilterDateSQLCondition( FilterItems[FilterItemsCount], "s.T_OPENDATE" );

    end;
    
    if( FilterItems[FilterItemsCount].Id == FILTERITEM_ENDDATE ) // Дата закрытия символа.

      condition = condition + " AND " + GetFilterDateSQLCondition( FilterItems[FilterItemsCount], "s.T_CLOSEDATE" );

    end;    
    
    if( FilterItems[FilterItemsCount].Id == FILTERITEM_NUMBERACC ) // Номер балансового счета.

      condition = condition + " AND " + GetFilterStringSQLCondition( FilterItems[FilterItemsCount], "s.T_BALANCE", true, false );

    end;
    
    FilterItemsCount = FilterItemsCount + 1;
    
  end;

  return condition;
  
end;

macro SetOFRSymbolQuery()
  
    var query = " SELECT s.*, "
                "   (SELECT sh.t_name "
                "    FROM DOPUSYMHIST_DBT sh "
                "    WHERE sh.t_ofrrecid = s.t_recid AND sh.t_startdate = h.startdate) "
                "     t_name "
                "     FROM dopusymb_dbt s, "
                "     (  SELECT MAX (sh.t_startdate) startdate, s.t_code "
                "               FROM dopusymb_dbt s, DOPUSYMHIST_DBT sh "
                "                WHERE sh.t_ofrrecid = s.t_recid AND sh.t_startdate <=  ? "
                "                GROUP BY s.t_code "
                "      ) h "
                " WHERE s.t_code = h.t_code (+) ";
  
  return query;
  
end;

macro WebCore_GetOFRSymbolGridItemList(pagesize: integer,pagenum: integer,FilterItems,orderitems)

  var cmd = RSDCommand();
  
  var query = " SELECT t.* FROM ( SELECT rownum RN, tt.* FROM ( ";
  query = query + SetOFRSymbolQuery();
  //
  var where = GetAddWhereCondition(FilterItems, where, cmd); 
  
  if(where != "")
    query = query + where;    
  end;
              
  query = query + " ORDER BY " + GetSortStr(orderitems, "s.T_CODE", "");
  
  if( PageSize > 0 )
     query = query + " ) tt WHERE ROWNUM <= " + string(PageSize*(PageNum + 1)) + " ) t WHERE t.RN >= " + string(PageSize*(PageNum) + 1) + " ";
  else
     query = query + " ) tt ) t ";
  end;
  
  cmd.CmdText = query;
  
  cmd.addParam( "", RSDBP_IN, {curdate} );

  cmd.nullConversion = true;
   
  var rs = rsdRecordSet( cmd );
  
  var result = TArray();
  while( rs.moveNext() )
     var ofrsobj = TWebOFRSymbolGridItem;
     
     ofrsobj.RecID   = SQL_ConvType(rs.value("T_RECID"));
     ofrsobj.Code    = SQL_ConvType(rs.value("T_CODE"));
     ofrsobj.Balance = SQL_ConvType(rs.value("T_BALANCE"));
     if ((SQL_ConvType(rs.value("T_NAME"))!=null) AND (SQL_ConvType(rs.value("T_NAME"))!=nullval))
      ofrsobj.Name = SQL_ConvType(rs.value("T_NAME"));
     end;
     ofrsobj.Principle = SQL_ConvTypeBool(rs.value("T_PRINCIPLE"));
     ofrsobj.OpenDate  = SQL_ConvType(rs.value("T_OPENDATE"));
     ofrsobj.CloseDate = SQL_ConvType(rs.value("T_CLOSEDATE"));

     result.value(result.Size) = ofrsobj;
  end;
  
  return result;
end;
//WebCore_GetOFRSymbolGridItemList(30,0,null,null);
macro WebCore_GetOFRSymbolGridItemCount(FilterItems)

  var res : integer = 0;
  var cmd = RSDCommand();
  
  var query = " SELECT count(*) c FROM ( ";
  query = query + SetOFRSymbolQuery();
  //
  var where = GetAddWhereCondition(FilterItems, where, cmd); 
  
  if(where != "")
    query = query + where;    
  end;
  
  query = query + " ) ";
  
  cmd.CmdText = query;
  
  cmd.addParam( "", RSDBP_IN, {curdate} );
   
  var rs = rsdRecordSet( cmd );
  
  var result = TArray();
   
  if(rs.movenext)

    res = rs.value("c");

  end;
  return res;
end;

macro WebCore_DeleteOFRSymbol( RecID )

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  var stat = CB_DeleteOPUSYMB( RecID );

  if(stat != 0)

    res = False;

  end;

  if(res != True)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

//WebCore_DeleteOFRSymbol(11119);