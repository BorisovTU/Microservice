/*
$Name:        acctrn_scroll.mac
$Module:      Главная книга
$Description: Макрос списка проводок
*/

IMPORT BankInter;
import "ws_partycommon.mac";
import "ws_ficlass.mac";
import "ws_dprt.mac";
import "ws_person.mac";
import "ws_partyclasses.mac";
import "ws_fiunilist.mac";
import "ws_validation.mac";
import "ws_datafilter.mac";
import "pr_party_lib.mac";
import "ws_file.mac";
import "acctrn_scroll_protocol.mac";
import "acctrn_web_common.mac";

/* Идентификаторы сортировки */
private const SORT_FI_PAY      =  0;     /* Валюта счета плательщика */
private const SORT_FI_PAY_EQ   =  1;     /* Валюта эквивалента счета плательщика */
private const SORT_FI_REC      =  2;     /* Валюта счета получателя */
private const SORT_FI_REC_EQ   =  3;     /* Валюта эквивалента счета получателя */
private const SORT_DEPART      =  4;     /* Филиал проводки */
private const SORT_BRANCH      =  5;     /* Подразделение проводки */
private const SORT_OPER        =  6;     /* Операционист */
private const SORT_SYMB_DEBET  =  7;     /* Приходный кассовый символ */
private const SORT_SYMB_CREDIT =  8;     /* Расходный кассовый символ */
private const SORT_SYMB_NOTBAL =  8;     /* Забалансовый кассовый символ */

private const SORT_COUNT_ALL = 7;     /* Общее количество сортировок по связанным таблицам */


/* Псевдонимы для связанных таблиц */

private const ALIAS_FI_PAY    = "FI_PAY";      /* Валюта счета плательщика */
private const ALIAS_FI_PAY_EQ = "FI_PAY_EQ";   /* Валюта эквивалента счета плательщика */
private const ALIAS_FI_REC    = "FI_REC";      /* Валюта счета получателя */
private const ALIAS_FI_REC_EQ = "FI_REC_EQ";   /* Валюта эквивалента счета получателя */
private const ALIAS_DEPART    = "DEPART";      /* Филиал проводки */
private const ALIAS_BRANCH    = "BRANCH";      /* Подразделение счета */
private const ALIAS_OPER      = "OPER";        /* Операционист */

/* Идентификаторы условий фильтрации */
private const FILTERITEM_NUMBER               = 0;   // Номер
private const FILTERITEM_OPER                 = 1;   // Операционист
private const FILTERITEM_CIPHER               = 2;   // Шифр
private const FILTERITEM_FI_PAYER             = 3;   // Валюта плательщика
private const FILTERITEM_FI_RECEIVER          = 4;   // Валюта получателя
private const FILTERITEM_ACC_PAYER            = 5;   // Счет плательщика
private const FILTERITEM_ACC_RECEIVER         = 6;   // Счет получателя
private const FILTERITEM_FIEQ_PAYER           = 7;   // Код ВЭ дебета
private const FILTERITEM_FIEQ_RECEIVER        = 8;   // Код ВЭ кредита
private const FILTERITEM_SUMEQ_PAYER          = 9;   // Сумма в ВЭ счета дебета
private const FILTERITEM_SUMEQ_RECEIVER       = 10;  // Сумма в ВЭ счета кредита
private const FILTERITEM_SUM_PAYER            = 11;  // Сумма дебета
private const FILTERITEM_SUM_RECEIVER         = 12;  // Сумма кредита
private const FILTERITEM_SUM_NATCUR           = 13;  // Сумма эквивалента в национальной валюте
private const FILTERITEM_PACK                 = 14;  // Пачка
private const FILTERITEM_RESCARRY             = 15;  // Проводка
private const FILTERITEM_DEPARTMENT           = 16;  // Филиал
private const FILTERITEM_BRANCH               = 17;  // Подразделение ТС
private const FILTERITEM_SYMBCASH_BALANCE     = 18;  // Балансовый символ
private const FILTERITEM_SYMBCASH_NOT_BALANCE = 19;  // Забалансовый символ
private const FILTERITEM_DATE_CARRY           = 20;  // Дата проводки
private const FILTERITEM_TYPE_DOCUMENT        = 21;  // Тип проводки
private const FILTERITEM_USERTYPE_DOCUMENT    = 22;  // Пользовательский тип
private const FILTERITEM_OFRSYMBOL            = 23;  // Символ ОФР

class TWebAccRestDate
  var RestDate;
  var Kind;
  var NatCurName;
  var NatCurRest : money = $0;
  var FIID_Name;
  var FIIDRest : money = $0;
end;

class TWebAccountPeriodTurn
  var NatCurDebet : money = $0;
  var NatCurCredit : money = $0;
  var NatCurName;
  var FIID_Debet : money = $0;
  var FIID_Credit : money = $0;
  var FIID_Name;
end;

class TWebAcctrnGridForAccPanelObject
  var RestIn;
  var Turn;
  var RestOut;
end;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro GetErrorMessage(ErrorCode : @integer, ErrorMessage : @string)

  var objErr = AL_GetErrorInfo();

  ErrorCode     = objErr.stat;
  ErrorMessage  = objErr.message;

  InitError();

end;


private macro GetSortStr( OrderItems,            /* Параметры сортировки */
                          DefaultSortStr:string  /* Строка сортировки по умолчанию (первичному ключу) */
                        ) : string

  /* Аналог оператора ?: в си. */
  private macro IIF( condition, value_true, value_false )
    if( condition )
      return value_true;
    else 
      return value_false;
    end;
  end;


  var SortStr:string = "";

   if( (OrderItems == null) or (OrderItems.Size() <= 0) )
  
     SortStr = " " + DefaultSortStr + " ";
     
   else
   
     var i:integer = 0;
     while( i < OrderItems.size() )
        
          if( OrderItems[i].Column == "numb_document"    ) OrderItems[i].Column = "T_NUMB_DOCUMENT";
        elif( OrderItems[i].Column == "date_carry"       ) OrderItems[i].Column = "T_DATE_CARRY";
        elif( OrderItems[i].Column == "account_payer"    ) OrderItems[i].Column = "T_ACCOUNT_PAYER";
        elif( OrderItems[i].Column == "sum_payer"        ) OrderItems[i].Column = "T_SUM_PAYER";
        elif( OrderItems[i].Column == "account_receiver" ) OrderItems[i].Column = "T_ACCOUNT_RECEIVER";
        elif( OrderItems[i].Column == "sum_receiver"     ) OrderItems[i].Column = "T_SUM_RECEIVER";
        elif( OrderItems[i].Column == "sum_natcur"       ) OrderItems[i].Column = "T_SUM_NATCUR";
        elif( OrderItems[i].Column == "department_name" ) OrderItems[i].Column = WebDprt_GetSortParam( ALIAS_DEPART );
        elif( OrderItems[i].Column == "branch_name"     ) OrderItems[i].Column = WebDprt_GetSortParam( ALIAS_BRANCH );
        elif (OrderItems[i].Column == "fi_payer"        ) OrderItems[i].Column = WebFI_GetSortParam( ALIAS_FI_PAY    );
        elif (OrderItems[i].Column == "fieq_payer"      ) OrderItems[i].Column = WebFI_GetSortParam( ALIAS_FI_PAY_EQ );
        elif (OrderItems[i].Column == "fi_receiver"     ) OrderItems[i].Column = WebFI_GetSortParam( ALIAS_FI_REC    );
        elif (OrderItems[i].Column == "fieq_receiver"   ) OrderItems[i].Column = WebFI_GetSortParam( ALIAS_FI_REC_EQ );
        elif (OrderItems[i].Column == "oper_number"     ) OrderItems[i].Column = WebPerson_GetSortParam( ALIAS_OPER );
        elif (OrderItems[i].Column == "Symbol_Debet"  ) OrderItems[i].Column = "Symbol_Debet";
        elif (OrderItems[i].Column == "Symbol_Credit" ) OrderItems[i].Column = "Symbol_Credit";
        elif (OrderItems[i].Column == "Symbol_NotB"   ) OrderItems[i].Column = "Symbol_NotB";
        elif (OrderItems[i].Column == "OFRSymbol"     ) OrderItems[i].Column = "T_OFRRECID";
        end;
        
        if( i != 0 )
          SortStr = SortStr + ", " + OrderItems[i].Column + " " + IIF(OrderItems[i].Direction == 0, "asc", "desc");
        else 
          SortStr = SortStr + "  " + OrderItems[i].Column + " " + IIF(OrderItems[i].Direction == 0, "asc", "desc");
        end;

        i = i + 1;
     end;
     SortStr = SortStr + ", " + DefaultSortStr + " ";

   end;

  return SortStr;

end;

private macro GetSymbol( alias : string, str_kind : string, field_name : string )

  var query = "CASE (SELECT COUNT (0) FROM dsymbcash_dbt s "
              "      WHERE s.t_AccTrnID = " + alias + ".t_AccTrnID AND s.t_Kind = " + str_kind + ")"
              "  WHEN 0 THEN NULL"
              "  WHEN 1 THEN (SELECT t_Symbol FROM dsymbcash_dbt s "
              "      WHERE s.t_AccTrnID = " + alias + ".t_AccTrnID AND s.t_Kind = " + str_kind + ")"
              "  ELSE '0' "
              "END " + field_name;

  return query;

end;

private macro GetAccountType( alias : string, acc_field_name : string, field_name : string )

  var query = "(SELECT acc.t_Type_Account FROM daccount_dbt acc "
              "WHERE acc.t_AccountID = " + alias + "." + acc_field_name + ") " + field_name;

  return query;

end;

private macro GetQuery( orderitems, SortArray )

  var where_str : string, from_str : string;

  var query = " SELECT acctrn.T_ACCTRNID, "
              "        acctrn.T_DEPARTMENT, "
              "        acctrn.T_BRANCH, "
              "        acctrn.T_NUMB_DOCUMENT, "
              "        acctrn.T_DATE_CARRY, "
              "        acctrn.T_ACCOUNTID_PAYER, "
              "        acctrn.T_ACCOUNT_PAYER, "
              "        acctrn.T_FIID_PAYER, "
              "        acctrn.T_SUM_PAYER, "
              "        acctrn.T_ACCOUNTID_RECEIVER, "
              "        acctrn.T_ACCOUNT_RECEIVER, "
              "        acctrn.T_FIID_RECEIVER, "
              "        acctrn.T_SUM_RECEIVER, "
              "        acctrn.T_SUM_NATCUR, "
              "        acctrn.T_OPER, "
              "        acctrn.T_GROUND, "
              "        acctrn.T_SUMEQ_PAYER, "
              "        acctrn.T_FIIDEQ_PAYER, "
              "        acctrn.T_SUMEQ_RECEIVER, "
              "        acctrn.T_FIIDEQ_RECEIVER, "
              "        acctrn.T_RESULT_CARRY, "
              "        acctrn.T_NUMBER_PACK, "
              "        acctrn.T_SHIFR_OPER, "
              "        acctrn.T_KIND_OPER, "
              "        acctrn.T_EXRATEACCTRNID, "
              "        acctrn.T_OFRRECID ";

  var sql_FROM = "FROM dacctrn_dbt acctrn ";

  var sql_WHERE = "WHERE acctrn.t_chapter = ? AND acctrn.t_state = ? ";

  if(orderitems != null)

    var i:integer = 0;

    while( i < orderitems.size() )

      if(OrderItems[i].Column == "department_name")

        WebDprt_GetQueryParam( @query, @sql_FROM, @sql_WHERE, ALIAS_DEPART, "acctrn.T_DEPARTMENT" );

        SortArray[SORT_DEPART] = true;

      end;

      if(OrderItems[i].Column == "branch_name")

        WebDprt_GetQueryParam( @query, @sql_FROM, @sql_WHERE, ALIAS_BRANCH, "acctrn.T_BRANCH" );

        SortArray[SORT_BRANCH] = true;

      end;

      if(OrderItems[i].Column == "fi_payer")

        WebFI_GetQueryParam( @query, @sql_FROM, @sql_WHERE, ALIAS_FI_PAY, "acctrn.T_FIID_PAYER" );

        SortArray[SORT_FI_PAY] = true;

      end;

      if(OrderItems[i].Column == "fieq_payer")

        WebFI_GetQueryParam( @query, @sql_FROM, @sql_WHERE, ALIAS_FI_PAY_EQ, "acctrn.T_FIIDEQ_PAYER" );

        SortArray[SORT_FI_PAY_EQ] = true;

      end;

      if(OrderItems[i].Column == "fi_receiver")

        WebFI_GetQueryParam( @query, @sql_FROM, @sql_WHERE, ALIAS_FI_REC, "acctrn.T_FIID_RECEIVER" );

        SortArray[SORT_FI_REC] = true;

      end;

      if(OrderItems[i].Column == "fieq_receiver")

        WebFI_GetQueryParam( @query, @sql_FROM, @sql_WHERE, ALIAS_FI_REC_EQ, "acctrn.T_FIIDEQ_RECEIVER" );

        SortArray[SORT_FI_REC_EQ] = true;

      end;

      if(OrderItems[i].Column == "oper_number")

        WebPerson_GetQueryParam( @query, @sql_FROM, @sql_WHERE, ALIAS_OPER, "acctrn.T_OPER" );

        SortArray[SORT_OPER] = true;

      end;

      if( orderitems[i].column == "Symbol_Debet" )

        WebScroll_AddSelect( @query, GetSymbol("acctrn", "1", "Symbol_Debet") );

        SortArray[SORT_SYMB_DEBET] = true;

      end;

      if( orderitems[i].column == "Symbol_Credit" )

        WebScroll_AddSelect( @query, GetSymbol("acctrn", "2", "Symbol_Credit") );

        SortArray[SORT_SYMB_DEBET] = true;

      end;

      if( orderitems[i].column == "Symbol_NotB" )

        WebScroll_AddSelect( @query, GetSymbol("acctrn", "3", "Symbol_NotB") );

        SortArray[SORT_SYMB_DEBET] = true;

      end;

      i = i + 1;

    end;

  end;

  query = query + " " + sql_FROM + " " + sql_WHERE;

  return query;

end;

private macro StringCondition(filteritems, value, FlagUpper) : string

  var condition = "";
  var conditionNOT = "";
  var condB = "";
  var condE = "";

  if( filteritems.IsNot )
    conditionNOT = " NOT ";
  else
    conditionNOT = " ";
  end;

  if( filteritems.Condition == FilterCondition_Null )
    condB = " = CHR(1) ";
  end;

  if( filteritems.Condition == FilterCondition_Equal )
    condB = " LIKE ";
    condE = " ";
  end;

  if( filteritems.Condition == FilterCondition_StartWith )
    condB = " LIKE ";
    condE = " || '%'";
  end;

  if( filteritems.Condition == FilterCondition_Include )
    condB = " LIKE '%' || ";
    condE = " || '%'";
  end;

  condition = condition + conditionNOT;

  condition = condition + condB;

  if( filteritems.Condition != FilterCondition_Null )

    if(FlagUpper)

      condition = condition + " UPPER ('" + string(value) + "') ";

    else

      condition = condition + "'" + string(value) + "'";

    end;
  end;

  condition = condition + condE;

  return condition;

end;

macro GetAddWhereCondition(
  FilterItems/*:TArray of FilterCondItem*/
):String

  var condition = "";
  var condition_temp = "";
  var conditionNOT;

  var ConvertMask = "";

  var FilterItemsCount = 0;
  var ValueCount = 0;

  while( FilterItemsCount < FilterItems.Size )

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_NUMBER ) // Номер

      condition = condition + " AND " + GetFilterStringSQLCondition( FilterItems[FilterItemsCount], "acctrn.T_NUMB_DOCUMENT", true, false );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_OPER ) // Операционист

      condition = condition + GetFilterOperSQLCondition( FilterItems[FilterItemsCount], "acctrn.T_OPER" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_CIPHER ) // Шифр

      condition = condition + GetFilterCipherSQLCondition( FilterItems[FilterItemsCount], "acctrn.T_SHIFR_OPER" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_FI_PAYER ) // Валюта плательщика

      condition = condition + GetFilterFISQLCondition( FilterItems[FilterItemsCount], "acctrn.T_FIID_PAYER" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_FI_RECEIVER ) // Валюта получателя

      condition = condition + GetFilterFISQLCondition( FilterItems[FilterItemsCount], "acctrn.T_FIID_RECEIVER" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_ACC_PAYER ) // Счет плательщика

      condition = condition + " AND " + GetFilterStringSQLCondition( FilterItems[FilterItemsCount], "acctrn.T_ACCOUNT_PAYER", false, true );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_ACC_RECEIVER ) // Счет получателя

      condition = condition + " AND " + GetFilterStringSQLCondition( FilterItems[FilterItemsCount], "acctrn.T_ACCOUNT_RECEIVER", false, true );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_FIEQ_PAYER ) // Код ВЭ дебета

      condition = condition + GetFilterFISQLCondition( FilterItems[FilterItemsCount], "acctrn.T_FIIDEQ_PAYER" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_FIEQ_RECEIVER ) // Код ВЭ кредита

      condition = condition + GetFilterFISQLCondition( FilterItems[FilterItemsCount], "acctrn.T_FIIDEQ_RECEIVER" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_SUMEQ_PAYER ) // Сумма в ВЭ счета дебета

      condition = condition + " AND " + GetFilterMoneySQLCondition( FilterItems[FilterItemsCount], "T_SUMEQ_PAYER" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_SUMEQ_RECEIVER ) // Сумма в ВЭ счета кредита

      condition = condition + " AND " + GetFilterMoneySQLCondition( FilterItems[FilterItemsCount], "T_SUMEQ_RECEIVER" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_SUM_PAYER ) // Сумма дебета

      condition = condition + " AND " + GetFilterMoneySQLCondition( FilterItems[FilterItemsCount], "T_SUM_PAYER" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_SUM_RECEIVER ) // Сумма кредита

      condition = condition + " AND " + GetFilterMoneySQLCondition( FilterItems[FilterItemsCount], "T_SUM_RECEIVER" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_SUM_NATCUR ) // Сумма эквивалента в национальной валюте

      condition = condition + " AND " + GetFilterMoneySQLCondition( FilterItems[FilterItemsCount], "T_SUM_NATCUR" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_PACK ) // Пачка

      condition = condition + " AND " + GetFilterStringSQLCondition( FilterItems[FilterItemsCount], "TO_CHAR(acctrn.T_NUMBER_PACK)", false, false );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_RESCARRY ) // Проводка

      condition = condition + GetFilterResCarrySQLCondition( FilterItems[FilterItemsCount], "acctrn.T_RESULT_CARRY" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_DEPARTMENT ) // Филиал

      condition = condition + GetFilterDprtSQLCondition( FilterItems[FilterItemsCount], "acctrn.T_DEPARTMENT" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_BRANCH ) // Подразделение ТС

      condition = condition + GetFilterDprtSQLCondition( FilterItems[FilterItemsCount], "acctrn.T_BRANCH" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_SYMBCASH_BALANCE ) // Балансовый символ

      if( FilterItems[FilterItemsCount].IsNot )
        conditionNOT = " NOT ";
      else
        conditionNOT = " ";
      end;

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Equal )

        condition = condition + " AND " + conditionNOT + " EXISTS (SELECT * FROM dsymbcash_dbt sc WHERE acctrn.T_ACCTRNID = sc.T_ACCTRNID AND sc.t_Kind >= 1 and sc.t_Kind <= 2 AND sc.T_DOCKIND IN (1,7) ";

        if(FilterItems[FilterItemsCount].Value.Size > 0)

          condition = condition + " AND sc.t_Symbol IN ( ";

          condition_temp = "";

          ValueCount = 0;

          while( ValueCount < FilterItems[FilterItemsCount].Value.Size )

            condition_temp = condition_temp + string(FilterItems[FilterItemsCount].Value[ValueCount].SymbCash);

            if(ValueCount != FilterItems[FilterItemsCount].Value.Size - 1)

              condition_temp = condition_temp + ", ";

            end;

            ValueCount = ValueCount + 1;

          end;

          condition = condition + condition_temp + " ) ";

        end;

        condition = condition + " ) ";

      end;

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Include )

        condition = condition + " AND " + conditionNOT + " EXISTS (SELECT * FROM dsymbcash_dbt sc WHERE acctrn.T_ACCTRNID = sc.T_ACCTRNID AND sc.t_Kind >= 1 and sc.t_Kind <= 2 AND sc.T_DOCKIND IN (1,7) ";

        if(FilterItems[FilterItemsCount].Value.Size > 0)

          condition = condition + " AND sc.t_Symbol IN ( ";

          condition_temp = "";

          ValueCount = 0;

          while( ValueCount < FilterItems[FilterItemsCount].Value.Size )

            condition_temp = condition_temp + string(FilterItems[FilterItemsCount].Value[ValueCount].SymbCash);

            if(ValueCount != FilterItems[FilterItemsCount].Value.Size - 1)

              condition_temp = condition_temp + ", ";

            end;

            ValueCount = ValueCount + 1;

          end;

          condition = condition + condition_temp + " ) ";

        end;

        condition = condition + " ) ";

      end;

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_SYMBCASH_NOT_BALANCE ) // Забалансовый символ

      if( FilterItems[FilterItemsCount].IsNot )
        conditionNOT = " NOT ";
      else
        conditionNOT = " ";
      end;

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Equal )

        condition = condition + " AND " + conditionNOT + " EXISTS (SELECT * FROM dsymbcash_dbt sc WHERE acctrn.T_ACCTRNID = sc.T_ACCTRNID AND sc.t_Kind = 3 AND sc.T_DOCKIND IN (1,7) ";

        if(FilterItems[FilterItemsCount].Value.Size > 0)

          condition = condition + " AND sc.t_Symbol IN ( ";

          condition_temp = "";

          ValueCount = 0;

          while( ValueCount < FilterItems[FilterItemsCount].Value.Size )

            condition_temp = condition_temp + string(FilterItems[FilterItemsCount].Value[ValueCount].SymbCash);

            if(ValueCount != FilterItems[FilterItemsCount].Value.Size - 1)

              condition_temp = condition_temp + ", ";

            end;

            ValueCount = ValueCount + 1;

          end;

          condition = condition + condition_temp + " ) ";

        end;

        condition = condition + " ) ";

      end;

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Include )

        condition = condition + " AND " + conditionNOT + " EXISTS (SELECT * FROM dsymbcash_dbt sc WHERE acctrn.T_ACCTRNID = sc.T_ACCTRNID AND sc.t_Kind = 3 AND sc.T_DOCKIND IN (1,7) ";

        if(FilterItems[FilterItemsCount].Value.Size > 0)

          condition = condition + " AND sc.t_Symbol IN ( ";

          condition_temp = "";

          ValueCount = 0;

          while( ValueCount < FilterItems[FilterItemsCount].Value.Size )

            condition_temp = condition_temp + string(FilterItems[FilterItemsCount].Value[ValueCount].SymbCash);

            if(ValueCount != FilterItems[FilterItemsCount].Value.Size - 1)

              condition_temp = condition_temp + ", ";

            end;

            ValueCount = ValueCount + 1;

          end;

          condition = condition + condition_temp + " ) ";

        end;

        condition = condition + " ) ";

      end;

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_DATE_CARRY ) // Дата проводки

      condition = condition + " AND " + GetFilterDateSQLCondition( FilterItems[FilterItemsCount], "acctrn.T_DATE_CARRY" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_TYPE_DOCUMENT ) // Тип проводки

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Equal )

        if( FilterItems[FilterItemsCount].IsNot )

          condition = condition + " AND NOT( 1!=1 ";

          ValueCount = 1;

          while( ValueCount <= StrLen(FilterItems[FilterItemsCount].Value[0]) )

            condition = condition + " OR acctrn.T_TYPEDOCUMENT LIKE '%'||'" + SubStr(FilterItems[FilterItemsCount].Value[0], ValueCount, 1) + "'||'%' ";

            ValueCount = ValueCount + 1;

          end;

          condition = condition + " ) ";

        else

          ValueCount = 1;

          while( ValueCount <= StrLen(FilterItems[FilterItemsCount].Value[0]) )

            condition = condition + " AND acctrn.T_TYPEDOCUMENT LIKE '%'||'" + SubStr(FilterItems[FilterItemsCount].Value[0], ValueCount, 1) + "'||'%' ";

            ValueCount = ValueCount + 1;

          end;

        end;

      end;

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Include )

        if( FilterItems[FilterItemsCount].IsNot )

          condition = condition + " AND NOT( 1=1 ";

          ValueCount = 1;

          while( ValueCount <= StrLen(FilterItems[FilterItemsCount].Value[0]) )

            condition = condition + " AND acctrn.T_TYPEDOCUMENT LIKE '%'||'" + SubStr(FilterItems[FilterItemsCount].Value[0], ValueCount, 1) + "'||'%' ";

            ValueCount = ValueCount + 1;

          end;

          condition = condition + " ) ";

        else

          condition = condition + " AND ( 1!=1 ";

          ValueCount = 1;

          while( ValueCount <= StrLen(FilterItems[FilterItemsCount].Value[0]) )

            condition = condition + " OR acctrn.T_TYPEDOCUMENT LIKE '%'||'" + SubStr(FilterItems[FilterItemsCount].Value[0], ValueCount, 1) + "'||'%' ";

            ValueCount = ValueCount + 1;

          end;

          condition = condition + " ) ";

        end;

      end;

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_USERTYPE_DOCUMENT ) // Пользовательский тип

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Equal )

        if( FilterItems[FilterItemsCount].IsNot )

          condition = condition + " AND NOT( 1!=1 ";

          ValueCount = 1;

          while( ValueCount <= StrLen(FilterItems[FilterItemsCount].Value[0]) )

            condition = condition + " OR acctrn.T_USERTYPEDOCUMENT LIKE '%'||'" + SubStr(FilterItems[FilterItemsCount].Value[0], ValueCount, 1) + "'||'%' ";

            ValueCount = ValueCount + 1;

          end;

        else

          ValueCount = 1;

          while( ValueCount <= StrLen(FilterItems[FilterItemsCount].Value[0]) )

            condition = condition + " AND acctrn.T_USERTYPEDOCUMENT LIKE '%'||'" + SubStr(FilterItems[FilterItemsCount].Value[0], ValueCount, 1) + "'||'%' ";

            ValueCount = ValueCount + 1;

          end;

        end;

      end;

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Include )

        if( FilterItems[FilterItemsCount].IsNot )

          condition = condition + " AND NOT( 1=1 ";

          ValueCount = 1;

          while( ValueCount <= StrLen(FilterItems[FilterItemsCount].Value[0]) )

            condition = condition + " AND acctrn.T_USERTYPEDOCUMENT LIKE '%'||'" + SubStr(FilterItems[FilterItemsCount].Value[0], ValueCount, 1) + "'||'%' ";

            ValueCount = ValueCount + 1;

          end;

          condition = condition + " ) ";

        else

          condition = condition + " AND ( 1!=1 ";

          ValueCount = 1;

          while( ValueCount <= StrLen(FilterItems[FilterItemsCount].Value[0]) )

            condition = condition + " OR acctrn.T_USERTYPEDOCUMENT LIKE '%'||'" + SubStr(FilterItems[FilterItemsCount].Value[0], ValueCount, 1) + "'||'%' ";

            ValueCount = ValueCount + 1;

          end;

          condition = condition + " ) ";

        end;

      end;

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_OFRSYMBOL ) // Символ ОФР
    
      if( FilterItems[FilterItemsCount].IsNot )
        conditionNOT = " NOT ";
      else
        conditionNOT = " ";
      end;

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Equal )

        if(FilterItems[FilterItemsCount].Value.Size > 0)

          condition = condition + " AND acctrn.T_OFRRECID " + conditionNOT + " IN ( ";

          condition_temp = "";

          ValueCount = 0;

          while( ValueCount < FilterItems[FilterItemsCount].Value.Size )

            condition_temp = condition_temp + string(FilterItems[FilterItemsCount].Value[ValueCount].RecID);

            if(ValueCount != FilterItems[FilterItemsCount].Value.Size - 1)

              condition_temp = condition_temp + ", ";

            end;

            ValueCount = ValueCount + 1;

          end;

          condition = condition + condition_temp + " ) ";

        end;

      end;
      
    end;

    FilterItemsCount = FilterItemsCount + 1;

  end;

  return condition;

end;

/****************************************************************************************************
  Сервис получения списка проводок
*****************************************************************************************************/
macro WebCore_GetAcctrnGridItemList(PageSize, PageNum, Chapter, IsCash, State, AccountID, filteritems, orderitems, FromDate, ToDate)

  var cmd, rs, p;

  var query_select = "", query_from = "", query_where = "";

  var Arr = TArray;

  var SortArray = TArray(SORT_COUNT_ALL);

  var FullQuery : string;

  var Query = GetQuery( orderitems, SortArray );

  var strAddWhere = GetAddWhereCondition( filteritems );

  if( strAddWhere != "" )

     Query = Query + strAddWhere;

  end;

  if((FromDate != null) AND (FromDate != PT_ZERODATE))
    
    Query = Query + " AND acctrn.t_date_carry >= " + To_SQLData(FromDate);

  end;  
  
  if((ToDate != null) AND (ToDate != PT_ZERODATE))
    
    Query = Query + " AND acctrn.t_date_carry <= " + To_SQLData(ToDate);

  end;  
    
  if(IsCash == True)

    Query = Query + " AND acctrn.t_kind_oper = ' 3' ";

  end;

  if(AccountID != null)

    Query = Query + " AND (acctrn.t_accountid_payer = ? OR acctrn.t_accountid_receiver = ?) ";

  end;

  if( (SortArray[SORT_DEPART] == null) or (SortArray[SORT_DEPART] == false) )
    
    WebDprt_GetQueryParam( @query_select, @query_from, @query_where, ALIAS_DEPART, "t.T_DEPARTMENT" );

  end;

  if( (SortArray[SORT_BRANCH] == null) or (SortArray[SORT_BRANCH] == false) )
    
    WebDprt_GetQueryParam( @query_select, @query_from, @query_where, ALIAS_BRANCH, "t.T_BRANCH" );

  end;

  if( (SortArray[SORT_FI_PAY] == null) or (SortArray[SORT_FI_PAY] == false) )
    
    WebFI_GetQueryParam( @query_select, @query_from, @query_where, ALIAS_FI_PAY, "t.T_FIID_PAYER" );

  end;

  if( (SortArray[SORT_FI_PAY_EQ] == null) or (SortArray[SORT_FI_PAY_EQ] == false) )
    
    WebFI_GetQueryParam( @query_select, @query_from, @query_where, ALIAS_FI_PAY_EQ, "t.T_FIIDEQ_PAYER" );

  end;

  if( (SortArray[SORT_FI_REC] == null) or (SortArray[SORT_FI_REC] == false) )
    
    WebFI_GetQueryParam( @query_select, @query_from, @query_where, ALIAS_FI_REC, "t.T_FIID_RECEIVER" );

  end;

  if( (SortArray[SORT_FI_REC_EQ] == null) or (SortArray[SORT_FI_REC_EQ] == false) )
    
    WebFI_GetQueryParam( @query_select, @query_from, @query_where, ALIAS_FI_REC_EQ, "t.T_FIIDEQ_RECEIVER" );

  end;

  if( (SortArray[SORT_OPER] == null) or (SortArray[SORT_OPER] == false) )
    
    WebPerson_GetQueryParam( @query_select, @query_from, @query_where, ALIAS_OPER, "t.T_OPER" );

  end;

  if( IsCash == True )

    if( (SortArray[SORT_SYMB_DEBET] == null) or (SortArray[SORT_SYMB_DEBET] == false) )
    
      WebScroll_AddSelect( @query_select, GetSymbol("t", "1", "Symbol_Debet") );

    end;

    if( (SortArray[SORT_SYMB_CREDIT] == null) or (SortArray[SORT_SYMB_CREDIT] == false) )
    
      WebScroll_AddSelect( @query_select, GetSymbol("t", "2", "Symbol_Credit") );

    end;

    if( (SortArray[SORT_SYMB_NOTBAL] == null) or (SortArray[SORT_SYMB_NOTBAL] == false) )
    
      WebScroll_AddSelect( @query_select, GetSymbol("t", "3", "Symbol_NotB") );

    end;

    WebScroll_AddSelect( @query_select, GetAccountType("t", "t_AccountID_Payer", "AccTypePayer") );

    WebScroll_AddSelect( @query_select, GetAccountType("t", "t_AccountID_Receiver", "AccTypeReceiver") );

  end;

  Query = Query + " ORDER BY " + GetSortStr(orderitems, "acctrn.t_acctrnid");

  FullQuery = WebScroll_CreatePageSQLString( PageSize, PageNum, Query, query_select, query_from, query_where );

  cmd = RsdCommand(FullQuery);

  cmd.addParam( "", RSDBP_IN, Chapter );
  cmd.addParam( "", RSDBP_IN, State );

  if(AccountID != null)
    cmd.addParam( "", RSDBP_IN, AccountID );
    cmd.addParam( "", RSDBP_IN, AccountID );
  end;

  rs = RsdRecordset(cmd);

  while(rs.movenext)

    p = TWebAcctrnGridItem();

    p.AcctrnID          = SQL_ConvType(rs.value("T_ACCTRNID"));
    p.Numb_Document     = SQL_ConvType(rs.value("T_NUMB_DOCUMENT"));
    p.Date_Carry        = SQL_ConvType(rs.value("T_DATE_CARRY"));
    
    p.Account_Payer     = SQL_ConvType(rs.value("T_ACCOUNT_PAYER"   ));
    p.Account_Receiver  = SQL_ConvType(rs.value("T_ACCOUNT_RECEIVER"));

    p.Sum_Payer         = SQL_ConvType(rs.value("T_SUM_PAYER"));
    p.Sum_Receiver      = SQL_ConvType(rs.value("T_SUM_RECEIVER"));
    p.Sum_NatCur        = SQL_ConvType(rs.value("T_SUM_NATCUR"));
    p.Ground            = SQL_ConvType(rs.value("T_GROUND"));
    
    p.Result_Carry      = SQL_ConvType(rs.value("T_RESULT_CARRY"));
    p.Number_Pack       = SQL_ConvType(rs.value("T_NUMBER_PACK"));
    p.Shifr_Oper        = SQL_ConvType(rs.value("T_SHIFR_OPER"));
    p.Kind_Oper         = SQL_ConvType(rs.value("T_KIND_OPER"));

    p.Department = WebDprt_GetObjectFromRecordset( ALIAS_DEPART, rs );
    p.Branch     = WebDprt_GetObjectFromRecordset( ALIAS_BRANCH, rs );

    p.Oper = WebPerson_GetObjectFromRecordset( ALIAS_OPER, rs );

    p.FI_Payer   = WebFI_GetObjectFromRecordset( ALIAS_FI_PAY   , rs );
    p.FIEq_Payer = WebFI_GetObjectFromRecordset( ALIAS_FI_PAY_EQ, rs );

    p.FI_Receiver   = WebFI_GetObjectFromRecordset( ALIAS_FI_REC   , rs );
    p.FIEq_Receiver = WebFI_GetObjectFromRecordset( ALIAS_FI_REC_EQ, rs );

    if( p.FIEq_Payer == null )
      p.SumEq_Payer = null;
    else
      p.SumEq_Payer = SQL_ConvType(rs.value("T_SUMEQ_PAYER"));
    end;

    if( p.FIEq_Receiver == null )
      p.SumEq_Receiver = null;
    else
      p.SumEq_Receiver = SQL_ConvType(rs.value("T_SUMEQ_RECEIVER"));
    end;

    if( IsCash == True )

      p.Symbol_Debet  = SQL_ConvType(rs.value("Symbol_Debet" ));
      p.Symbol_Credit = SQL_ConvType(rs.value("Symbol_Credit"));
      p.Symbol_NotB   = SQL_ConvType(rs.value("Symbol_NotB"  ));

      p.IsCashAccPayer    = Index(SQL_ConvType(rs.value("AccTypePayer"    )), "А") > 0;
      p.IsCashAccReceiver = Index(SQL_ConvType(rs.value("AccTypeReceiver" )), "А") > 0;

    end;

    p.IsMultyCarry = GetIsMultyCarryAcctrn( SQL_ConvType(rs.value("T_FIID_PAYER")), SQL_ConvType(rs.value("T_FIID_RECEIVER")), p.Result_Carry );

    if( p.IsMultyCarry == false ) p.IsExistExRate = false;
    else                          p.IsExistExRate = GetIsExistExRate( SQL_ConvType(rs.value("T_EXRATEACCTRNID")), p.Result_Carry );
    end;

    p.OFRSymbol = GetOFRSymbol(SQL_ConvType(rs.value("T_OFRRecID")));

    Arr[Arr.size] = p;

  end;

  return Arr;

end;

macro WebCore_GetAcctrnGridItemCount(Chapter, IsCash, State, AccountID, filteritems, FromDate, ToDate)

  var res : integer = 0;

  var cmd, rs;

  var query = " SELECT count(*) c FROM ( ";

  query = query + GetQuery(null);

  var strAddWhere = GetAddWhereCondition( filteritems );

  if( strAddWhere != "" )

    query = query + strAddWhere;

  end;

  if(IsCash == True)

    query = query + " AND acctrn.t_kind_oper = ' 3' ";

  end;

  if(AccountID != null)

    query = query + " AND (acctrn.t_accountid_payer = ? OR acctrn.t_accountid_receiver = ?) ";

  end;

  if((FromDate != null) AND (FromDate != PT_ZERODATE))
    
    query = query + " AND acctrn.t_date_carry >= " + To_SQLData(FromDate);

  end;  
  
  if((ToDate != null) AND (ToDate != PT_ZERODATE))
    
    query = query + " AND acctrn.t_date_carry <= " + To_SQLData(ToDate);

  end;   

  query = query + " ) ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, Chapter );
  cmd.addParam( "", RSDBP_IN, State );

  if(AccountID != null)
    cmd.addParam( "", RSDBP_IN, AccountID );
    cmd.addParam( "", RSDBP_IN, AccountID );
  end;

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    res = rs.value("c");

  end;

  return res;

end;

macro WebCore_DeleteAcctrn(AcctrnID)

  var res = False;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  var accTrn : RsbAccTransaction;

  accTrn = RsbAccTransaction;

  var stat = accTrn.Find(AcctrnID);

  if(stat == 0)

    stat = accTrn.Delete();

    res = stat;

  end;

  if(res != True)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_DeleteAcctrnList( AccTrnIDList : TArray, ReportName : string )

  var i : integer;
  
  var AccTrnCount = AccTrnIDList.Size;

  var FullFilePath = GetTxtFileName(ReportName);

  SetOutput(FullFilePath);

  PrintProtocolHeader_acctrn( "Протокол удаления проводок" );

  i = 0;

  while( i < AccTrnCount )

    var stat = 0;

    var ResultMessage = "";

    var accTrn : RsbAccTransaction;

    accTrn = RsbAccTransaction;

    stat = accTrn.Find(AccTrnIDList[i]);

    if( stat == 0 )

      if( accTrn.Delete() )
        ResultMessage = "Проводка удалена успешно";
      else
        stat = 1;
      end;

    end;

    if( stat != 0 )
      GetErrorMessage( @stat, @ResultMessage );
    end;

    PrintProtocolLine_acctrn( accTrn.AccTrnID, accTrn.Numb_Document, stat, ResultMessage );

    accTrn = null;

    i = i + 1;

  end;

  PrintProtocolFooter_acctrn();

  SetOutput();

  FullFilePath = ConvertTxt2Html(FullFilePath);

  return GetTextFile(FullFilePath);

end;


macro WebCore_CarryDateAcctrn(AcctrnID, DateCarry, MassLogName)

  var res = False;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  var accTrn : RsbAccTransaction;

  accTrn = RsbAccTransaction;

  var stat = accTrn.Find(AcctrnID);

  if(stat == 0)

    accTrn.Date_Carry   = DateCarry;
    accTrn.Status_After = ACCTRN_STATUS_DATE_CARRY;

    stat = accTrn.Update();

    res = stat;

  end;

  if(res != True)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_CarryDateAcctrnList( AccTrnIDList : TArray, DateCarry : date, ReportName : string )

  var i : integer;
  
  var AccTrnCount = AccTrnIDList.Size;

  var FullFilePath = GetTxtFileName(ReportName);

  SetOutput(FullFilePath);

  PrintProtocolHeader_acctrn( "Протокол выполнения проводок" );

  i = 0;

  while( i < AccTrnCount )

    var stat = 0;

    var ResultMessage = "";

    var accTrn : RsbAccTransaction;

    accTrn = RsbAccTransaction;

    stat = accTrn.Find(AccTrnIDList[i]);

    if( stat == 0 )

      if( (DateCarry != null) and (DateCarry != Date(0, 0, 0)) ) accTrn.Date_Carry = DateCarry; end;
      
      accTrn.Status_After = ACCTRN_STATUS_DATE_CARRY;

      if( accTrn.Update() )
        ResultMessage = "Проводка выполнена успешно";
      else
        stat = 1;
      end;

    end;

    if( stat != 0 )
      GetErrorMessage( @stat, @ResultMessage );
    end;

    PrintProtocolLine_acctrn( accTrn.AccTrnID, accTrn.Numb_Document, stat, ResultMessage );

    accTrn = null;

    i = i + 1;

  end;

  PrintProtocolFooter_acctrn();

  SetOutput();

  FullFilePath = ConvertTxt2Html(FullFilePath);

  return GetTextFile(FullFilePath);

end;

macro WebCore_InDefferedAcctrn( AccTrnID )

  var res = False;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  var accTrn : RsbAccTransaction;

  accTrn = RsbAccTransaction;

  var stat = accTrn.Find(AccTrnID);

  if(stat == 0)

    accTrn.Status_After = ACCTRN_STATUS_POST;

    stat = accTrn.Update();

    res = stat;

  end;

  if(res != True)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;


macro WebCore_MassLogAcctrn(MassLogName, IsBeginMass)

  var fc = NULL;

  if(MassLogName != V_UNDEF)
    if(IsBeginMass == true)
      
      setOutput("..\\txtfile\\" + MassLogName);
      
[Отчет о групповом выполнении.];
[+-----------+-----------+--------+---------------------------------------------------------+];
[|    ID     |     №     |   №    |                            Сообщение                    |];
[|           | документа | ошибки |                                                         |];
[+-----------+-----------+--------+---------------------------------------------------------+];

    else
      
      setOutput("..\\txtfile\\" + MassLogName, True);
      
[+-----------+-----------+--------+---------------------------------------------------------+];

      setOutput(NULL, True);
      
      fc = GetTxtFileName("..\\txtfile\\" + MassLogName);
      
      fc = ConvertTxt2Html(fc);

      fc = GetTextFile(fc);
      
    end;
  end;
  
  return fc;

end;

macro WebCore_CheckCurdateIsOpened(ChkCurdate, ChkOperDprt)

  ResponseBuilder = WebResponseBuilder();
  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  var res = false;
  var ErrMsg = "";

  var cmd, rs, query;

  query = " SELECT * "
          "  FROM dcurdate_dbt "
          " WHERE t_Curdate = ? AND t_Branch = ? AND t_IsClosed = CHR (0) ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, ChkCurdate );
  cmd.addParam( "", RSDBP_IN, ChkOperDprt );

  rs = RsdRecordset(cmd);

  if(rs.movenext)
    res = true;
  end;

  if(res == false)

    ErrorMessage.Code     = 22125 ;
    ErrorMessage.Message  = "Операционный день в филиале не открыт";
    ErrorMessage.Severity = MessageSeverity.RuntimeError;

    ResponseBuilder.AddMessage( ErrorMessage );

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_GetChapter(Chapter)

  var cmd, rs;

  var Rec = TRecHandler("obchaptr");
  var p;

  var query = " SELECT * FROM DOBCHAPTR_DBT WHERE T_CHAPTER = ? ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, Chapter );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    _CopyRecordsetToRecHandler(rs, Rec);

    p = TRecHandler("obchaptr");
    copy(p, Rec);

  end;

  return p.rec;

end;

private macro GetCurName(Code_Currency)

  var cmd, rs, p;

  var query = " SELECT t_CCY "
              "        FROM dfininstr_dbt "
              "        WHERE t_FIID = ? ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, Code_Currency );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    p = rs.value("t_CCY");

  end;

  return p;

end;

private macro GetDebeta_Full(Chapter, Account, FIid, RestInDate, RestOutDate, Code_Currency)

  var Debeta_Full;

  var cmd;
  cmd = RSDCommand( "select rsi_rsb_account.debeta_full(?, ?, ?, ?, ?, ?) as Debeta_Full from dual;" );
  cmd.addParam( "", RSDBP_IN, Account );
  cmd.addParam( "", RSDBP_IN, Chapter );
  cmd.addParam( "", RSDBP_IN, RestInDate );
  cmd.addParam( "", RSDBP_IN, RestOutDate );
  cmd.addParam( "", RSDBP_IN, FIid );
  cmd.addParam( "", RSDBP_IN, Code_Currency );

  cmd.NullConversion = true;

  var rs = RsdRecordset(cmd);

  if(rs.movenext)
    Debeta_Full = rs.value("Debeta_Full");
  end;

  return Debeta_Full;

end;

private macro GetKredita_Full(Chapter, Account, FIid, RestInDate, RestOutDate, Code_Currency)

  var Kredita_Full;

  var cmd;
  cmd = RSDCommand( "select rsi_rsb_account.kredita_full(?, ?, ?, ?, ?, ?) as Kredita_Full from dual;" );
  cmd.addParam( "", RSDBP_IN, Account );
  cmd.addParam( "", RSDBP_IN, Chapter );
  cmd.addParam( "", RSDBP_IN, RestInDate );
  cmd.addParam( "", RSDBP_IN, RestOutDate );
  cmd.addParam( "", RSDBP_IN, FIid );
  cmd.addParam( "", RSDBP_IN, Code_Currency );

  cmd.NullConversion = true;

  var rs = RsdRecordset(cmd);

  if(rs.movenext)
    Kredita_Full = rs.value("Kredita_Full");
  end;

  return Kredita_Full;

end;

macro WebCore_GetAcctrnGridForAccPanelObject(Chapter, Account, Code_Currency, RestInDate, RestOutDate)

  var ZeroDate = date( 0, 0, 0 );
  
  if(RestOutDate == ZeroDate)
    
    RestOutDate = date( 31, 12, 9999 );
    
  end;

  var NatCurName = GetCurName(NATCUR);
  var NatCurRest;
  var FIID_Name = GetCurName(Code_Currency);

  var accPanelObject = TWebAcctrnGridForAccPanelObject();

  accPanelObject.RestOut = TWebAccRestDate();

  var FIIDRest = RestA(Account, null, RestOutDate, RestOutDate, Code_Currency, Code_Currency);

  if(Code_Currency != NATCUR)
    NatCurRest = RestA(Account, null, RestOutDate, RestOutDate, Code_Currency, NATCUR);
  else
    NatCurRest = FIIDRest;
  end;

  accPanelObject.RestOut.RestDate   = RestOutDate;

  if(FIIDRest < 0 )
    accPanelObject.RestOut.Kind     = "Актив";
  else
    accPanelObject.RestOut.Kind     = "Пассив";
  end;

  accPanelObject.RestOut.NatCurName = NatCurName;
  accPanelObject.RestOut.NatCurRest = NatCurRest;
  accPanelObject.RestOut.FIID_Name  = FIID_Name;
  accPanelObject.RestOut.FIIDRest   = FIIDRest;

  accPanelObject.Turn = TWebAccountPeriodTurn();

  accPanelObject.Turn.FIID_Debet    = GetDebeta_Full(Chapter, Account, Code_Currency, RestInDate, RestOutDate, Code_Currency);
  accPanelObject.Turn.FIID_Credit   = GetKredita_Full(Chapter, Account, Code_Currency, RestInDate, RestOutDate, Code_Currency);
  accPanelObject.Turn.FIID_Name     = FIID_Name;

  if(Code_Currency != NATCUR)
    accPanelObject.Turn.NatCurDebet   = GetDebeta_Full(Chapter, Account, Code_Currency, RestInDate, RestOutDate, NATCUR);
  else
    accPanelObject.Turn.NatCurDebet   = accPanelObject.Turn.FIID_Debet;
  end;
  if(Code_Currency != NATCUR)
    accPanelObject.Turn.NatCurCredit   = GetKredita_Full(Chapter, Account, Code_Currency, RestInDate, RestOutDate, NATCUR);
  else
    accPanelObject.Turn.NatCurCredit   = accPanelObject.Turn.FIID_Credit;
  end;
  accPanelObject.Turn.NatCurName    = NatCurName;

  accPanelObject.RestIn = TWebAccRestDate();

  accPanelObject.RestIn.RestDate   = RestInDate;

  accPanelObject.RestIn.NatCurName = NatCurName;
  accPanelObject.RestIn.NatCurRest = accPanelObject.RestOut.NatCurRest - accPanelObject.Turn.NatCurCredit + accPanelObject.Turn.NatCurDebet;
  accPanelObject.RestIn.FIID_Name  = FIID_Name;
  accPanelObject.RestIn.FIIDRest   = accPanelObject.RestOut.FIIDRest - accPanelObject.Turn.FIID_Credit + accPanelObject.Turn.FIID_Debet;
  
  if(accPanelObject.RestIn.FIIDRest < 0 )
    accPanelObject.RestIn.Kind     = "Актив";
  else
    accPanelObject.RestIn.Kind     = "Пассив";
  end;

  return accPanelObject;

end;

macro WebCore_PaymentFromAcctrn(AcctrnID)

  var paymentid = 0;

  var cmd, rs;

  var query = 	" SELECT pmdocs.T_PAYMENTID  	"+
				" FROM dpmdocs_dbt pmdocs		"+
				" WHERE pmdocs.T_ACCTRNID = ?	";

  cmd = RsdCommand(query);
 
  cmd.addParam( "", RSDBP_IN, AcctrnID);
  
  rs = RsdRecordset(cmd);

  if (rs.movenext)
  
    paymentid = rs.value("T_PAYMENTID"); 
  
  end;

  return paymentid;

end;