 /*
 $Name: opdphaseaccgroup_scroll.mac
 $Module: Главная книга
 $Description: Макрос получения списка групп счетов для переключения фазы
 */
 
IMPORT FIInter, CurrInter, BankInter, PTInter;
import globals;
import "ws_validation.mac";
import "switch_phase.mac";
import "ws_dprt.mac";
import "ws_file.mac";
import "ws_datafilter.mac";

class TWebOpdPhaseAccGroupGridItem
  var GroupID          : integer;   
  var GroupName        : string ;
  var Phase            ; //TWebPhase
  var Department	  : integer; 
  var DprtNodeName     : string ;
  var OwnerList        ;//List <int>
  var SwitchBySheduler : bool ;
  var SwitchTime       :time ;  
end;


PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

macro WebCore_DeleteOpdPhaseAccGroup (GroupID):WebResponseBuilder


  responseBuilder =WebResponseBuilder();
    
  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  var res = CB_DeleteOPDPhaseGroup(GroupID);
     
  if(res != true)
   
     AddErrorMessage(@ErrorMessage);
  
  end;
    
  responseBuilder.SetUserObject(res);
  
  return responseBuilder.Result;

end;



/*
     Фильтрация по владельцу
 */
macro GetFilterOWNERSQLCondition( FilterItem) : string

  var SQLString = "";

  if( FilterItem.Condition == FilterCondition_Equal )

    if( FilterItem.Value.Size > 0 )

      var i = 0;

      SQLString = SQLString + " AND";

      if( FilterItem.IsNot )
        SQLString = SQLString + " NOT";
      end;

      SQLString = SQLString + " (";

      while( i < FilterItem.Value.Size )

        if( i > 0 )
          SQLString = SQLString + " OR ";
        end;
        
        SQLString = SQLString +"opdgroup.T_GROUPID";

        SQLString = SQLString + " IN ";

        SQLString = SQLString + ExecMacro2( "getOWNERString", FilterItem.Value[i] );

        i = i + 1;

      end;

      SQLString = SQLString + ") ";

    end;

  end;

  return SQLString;

end;

private macro getOWNERString( OWNERObject )
  return "( SELECT opdowner.T_GROUPID  FROM DOPDOWNER_DBT opdowner WHERE opdowner.T_OPER = " +string(OWNERObject) + ")";
end;


private const DPRTNODEDGTC = 0;    // Узел ТС

private const PHASEDGTC = 1;    // Фаза

private const OWNERDGTC = 2; // Владелец группы


private macro GetAddWhereCondition(FilterItems,  Phase): string

  var condition = "";

  if( Phase==0 )
  
    condition = " WHERE 1 = 1";
   	
  end;
  
  var FilterItemsCount = 0;
  
  var ValueCount = 0;

  while( FilterItemsCount < FilterItems.Size )

    if( FilterItems[FilterItemsCount].Id == DPRTNODEDGTC ) // Узел ТС
		
    condition = condition + GetFilterDprtSQLCondition( FilterItems[FilterItemsCount], "opdgroup.T_DEPARTMENT" );
	
    end;

    if( FilterItems[FilterItemsCount].Id == PHASEDGTC )// Фаза 

	  condition = condition + GetFilterPhaseSQLCondition( FilterItems[FilterItemsCount], "opdgroup.T_PHASE" );	

    end;
	
	if( FilterItems[FilterItemsCount].Id == OWNERDGTC ) // Владелец группы

      condition = condition + GetFilterOWNERSQLCondition( FilterItems[FilterItemsCount] );	
          
    end;

    FilterItemsCount = FilterItemsCount + 1;

  end;

  return condition;

end;


private macro GetQuery(Phase)
		   
  var query = 	" SELECT opdgroup.T_GROUPID, opdgroup.T_NAME, opdgroup.T_PHASE, opdgroup.T_DEPARTMENT, opdgroup.T_SWITCHBYSHEDULER, "
  
				" TO_CHAR( opdgroup.T_SWITCHTIME,'HH24:MI:SS') as T_SWITCHTIME "
				
				" FROM dopdgroup_dbt opdgroup "  ;
	  
  if((Phase!=null) and( Phase>0))
				   
    query = query + " WHERE opdgroup.T_PHASE = ?" ;
				  
  end;				  
				
  return query;

end;





macro WebCore_GetODPhaseAccGroupGridItemCount(filteritems, Phase)

  var res : integer = 0;

  var cmd, rs;

  var query = " SELECT count(*) c FROM ( ";

  query = query + GetQuery(Phase);

  var strAddWhere = GetAddWhereCondition( filteritems,  Phase );

  if( strAddWhere != "" )
  
    query = query + strAddWhere;
	
  end;

  query = query + " ) ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, Phase );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    res = rs.value("c");

  end;

  return res;

end;


macro WebCore_GetODPhaseAccGroupGridItemList(Phase , PageSize, PageNum, FilterItems)

  var result = TArray();
  
  var cmd, rs;
	
  var query = GetQuery(Phase);
 
  var strAddWhere = GetAddWhereCondition(filteritems, Phase );

  if( strAddWhere != "" )
  
    query = query + strAddWhere;
	
  end;

  query = query + " ORDER BY T_DEPARTMENT, T_PHASE, T_GROUPID ;";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, Phase );
  
  rs = RsdRecordset(cmd);

  while(rs.movenext)
  
    var OperDayPhaseGridItem = TWebOpdPhaseAccGroupGridItem ();
	
	OperDayPhaseGridItem.GroupID    = SQL_ConvType(rs.value("T_GROUPID" ));
	
	OperDayPhaseGridItem.GroupName    = SQL_ConvType(rs.value("T_NAME" ));
	
	OperDayPhaseGridItem.Phase = TWebPhase(); 
	
	OperDayPhaseGridItem.Phase.Phase = SQL_ConvType(rs.value("T_PHASE" ));
	
	OperDayPhaseGridItem.Department    = SQL_ConvType(rs.value("T_DEPARTMENT" ));	
	
	OperDayPhaseGridItem.SwitchBySheduler    = SQL_ConvType(rs.value("T_SWITCHBYSHEDULER" ));
	
	OperDayPhaseGridItem.SwitchTime    = Time ( SQL_ConvType(rs.value("T_SWITCHTIME" ))); 
	
	OperDayPhaseGridItem.OwnerList=TArray();
	
    var query_inside1 = "SELECT T_OPER FROM DOPDOWNER_DBT opdowner WHERE opdowner.t_GroupID = " + OperDayPhaseGridItem.GroupID;
	
	var  cmd_inside1 = RsdCommand(query_inside1);
  
    var rs_inside1 = RsdRecordset(cmd_inside1);
	
	while(rs_inside1.movenext)
	
	  OperDayPhaseGridItem.OwnerList[OperDayPhaseGridItem.OwnerList.size]=SQL_ConvType(rs_inside1.value("T_OPER" ));
	
	end;
	
       var query_inside2 = "SELECT T_SHORTNAME FROM DPARTY_DBT dp_dep WHERE T_PARTYID= (SELECT T_PARTYID FROM ddp_dep_dbt dp_dep WHERE T_CODE= " + OperDayPhaseGridItem.Department+")";
	
	var  cmd_inside2 = RsdCommand(query_inside2);
  
    var rs_inside2 = RsdRecordset(cmd_inside2);
	
	if(rs_inside2.movenext)
	
	  OperDayPhaseGridItem.DprtNodeName    = SQL_ConvType(rs_inside2.value("T_SHORTNAME" )); 
	
	end;
		
	result[result.size] = OperDayPhaseGridItem;
     
    OperDayPhaseGridItem = null;
  
  end;
  
  return result;

end;


