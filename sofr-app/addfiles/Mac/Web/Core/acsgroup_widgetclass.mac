/*
$Name:        acsgroup_widgetclass.mac
$Module:      Главная книга
$Description: Макрос виджета групп пользователей класс
*/

import "ws_partycommon.mac";
import "ws_dprt.mac";

class TWebAcsGroup
  var GroupID;
  var Name;
  var Department;
  var IsNotLocal;
end;

class TWebAcsGroupListerItem
  var GroupID;
  var Name;
  var DepartmentObj;
  var IsNotLocal;
end;

////////////////////////////////////////////////////////////////////////////////////////////////////////////
// class AcsGroupList
////////////////////////////////////////////////////////////////////////////////////////////////////////////

class AcsGroupList()

  macro GetList(sub, listLen)
          
    var cmd, rs;

    var Arr = TArray;

    var query = 
                " SELECT t.* "
                "   FROM DACSGROUP_DBT t "
                "  WHERE (   (    t.t_Department IN (    SELECT t_ParentCode "
                "                                          FROM ddp_dep_dbt "
                "                                    START WITH t_Code = " + {OperDprt} + " "
                "                                    CONNECT BY t_Code = PRIOR t_ParentCode) "
                "             AND t.t_IsNotLocal = 'X') "
                "         OR t.t_Department = " + {OperDprt} + ") ";
                
    if(sub != "")
    
      query = query + " AND LOWER(T_GROUPID) LIKE LOWER ('%' || ? || '%') ";
    
    end;

    if( listLen != 0 )

      query = query + " AND ROWNUM < " + string(listLen);

    end;
    
    query = query + " ORDER BY T_GROUPID ";

    cmd = RsdCommand(query);
    
    cmd.NullConversion = true;
    
    if(sub != "")
    
      cmd.addParam( "", RSDBP_IN, sub );
    
    end;
    
    rs = RsdRecordset(cmd);
    
    while(rs.movenext)

      var obj = TWebAcsGroupListerItem;
      
      obj.GroupID       = SQL_ConvType(rs.value("t_GroupID"));
      obj.Name          = SQL_ConvType(rs.value("t_Name"));
      obj.DepartmentObj = GetTWsDepartmentByCode(SQL_ConvType(rs.value("t_Department")));
      obj.IsNotLocal    = SQL_ConvTypeBool(rs.value("t_IsNotLocal"));

      Arr[Arr.size] = obj;

    end;

    return Arr;

  end;

end;

macro WebCore_CreateTWebAcsGroupFromBD(GroupID)

  var obj;

  var cmd, rs;

  var query = " SELECT * FROM DACSGROUP_DBT WHERE t_GroupID = ? ";

  cmd = RsdCommand(query);
  
  cmd.addParam( "", RSDBP_IN, GroupID );
  
  rs = RsdRecordset(cmd);

  if(rs.movenext)

    obj = TWebAcsGroup;

    obj.GroupID    = SQL_ConvType(rs.value("t_GroupID"));
    obj.Name       = SQL_ConvType(rs.value("t_Name"));
    obj.Department = SQL_ConvType(rs.value("t_Department"));
    obj.IsNotLocal = SQL_ConvTypeBool(rs.value("t_IsNotLocal"));

  end;

  return obj;

end;

macro WebCore_CreateTWebAcsGroup(GroupID, Name, Department, IsNotLocal)

  var obj = TWebAcsGroup;
  
  obj.GroupID    = GroupID;
  obj.Name       = Name;
  obj.Department = Department;
  obj.IsNotLocal = IsNotLocal;

  return obj;

end;