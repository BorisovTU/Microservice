 /*
 $Name: bankrupthistory.mac
 $Module: Главная книга
 $Description: Макрос истории банкротства
 */


IMPORT BankInter;
IMPORT PTInter;
import "ws_file.mac";
import "ws_validation.mac";
import "cb_sql.mac";
import "ws_person.mac";
import "ws_llvalues.mac";


class TWebPTBankRuptHistGridItem 
 
  var RecID          	 	: integer ; 
  
  var EventDate       		: Date ; 
  
  var RecType         		: integer ; 
  
  var RecTypeName       	: String ; 
  
  var BankruptStatus  		: integer ; 
  
  var BankruptStatusName    : String ; 
       
end;


class TWebPTBankRuptHist 
 
  var RecID          	 	: integer ; 
  
  var EventDate       		: Date ; 
  
  var RecTypeList         	; //List<TWsLlvalues>
  
  var BankruptStatusList    ; //List<TWsLlvalues>
  
  var CourtName  			: string ; 
  
  var CaseNumber    		: string ; 
  
  var PerformDate          	: Date ; 
  
  var ClaimantName       	: string ; 
  
  var ClaimantID         	: integer ; 
  
  var BankruptTypeList      ; //List<TWsLlvalues>
  
  var CourtDecision			: string ; 
  
  var SysDate  				: Date ; 
  
  var Oper    				; //TWsPerson
       
end;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro GetErrorMessage(ErrorCode : @integer, ErrorMessage : @string)

  var objErr = AL_GetErrorInfo();

  ErrorCode     = objErr.stat;
  ErrorMessage  = objErr.message;

  InitError();

end;


macro WebCore_GetWebPTBankRuptHistGridItemList(PartyID)

  var result = TArray();
  
  var query, cmd, rs, odj;
  
  query = 		"	SELECT BR.T_RECID RECID,													"+
				"	BR.T_EVENTDATE EVENTDATE,													"+
				"	BR.T_RECTYPE  RECTYPE,														"+
				"	(	SELECT LV.T_NAME														"+
				"		FROM dllvalues_dbt lv													"+
				"		WHERE LV.T_LIST = 3563 AND LV.T_ELEMENT = BR.T_RECTYPE					"+
				"	) RECTYPENAME,																"+
				"	BR.T_BANKRUPTSTATUS BANKRUPTSTATUS,											"+
				"	(	SELECT LV.T_NAME														"+
				"		FROM dllvalues_dbt lv 													"+
				"		WHERE LV.T_LIST = 3564 AND LV.T_ELEMENT = BR.T_BANKRUPTSTATUS			"+
				"	) 	BANKRUPTSTATUSNAME 														"+
				"	FROM DPTBANKRUPTHIST_DBT BR 												"+
				"	WHERE BR.T_PARTYID = ?  													"+
				"	ORDER BY BR.T_EVENTDATE DESC, BR.T_BANKRUPTSTATUS DESC, BR.T_RECTYPE DESC 	";
		  
  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, PartyID );
  
  cmd.NullConversion = true;
  
  rs = RsdRecordset(cmd);

  while(rs.MoveNext())
    
	odj = TWebPTBankRuptHistGridItem();
		
	odj.RecID = SQL_ConvType(rs.value("RECID"));
	
	odj.EventDate = SQL_ConvType(rs.value("EVENTDATE"));
	
	odj.RecType = SQL_ConvType(rs.value("RECTYPE"));
	
	odj.RecTypeName = SQL_ConvType(rs.value("RECTYPENAME"));
	
	odj.BankruptStatus = SQL_ConvType(rs.value("BANKRUPTSTATUS"));
	
	odj.BankruptStatusName = SQL_ConvType(rs.value("BANKRUPTSTATUSNAME"));
	   
    result[result.size] = odj;

  end;
  
  return result;

end;

macro WebCore_GetWebPTBankRuptHist(PartyID, RecID)

  var odj = TWebPTBankRuptHist();
  
  var query, cmd, rs;
  
  query = 		"	SELECT BR.T_RECID RECID,													"+
				"	BR.T_EVENTDATE EVENTDATE,													"+
				"	BR.T_RECTYPE  RECTYPE,														"+
				"	BR.T_BANKRUPTSTATUS BANKRUPTSTATUS,											"+				
				"	BR.T_COURTNAME COURTNAME,													"+
				"	BR.T_CASENUMBER CASENUMBER,													"+
				"	BR.T_PERFORMDATE PERFORMDATE,												"+
				"	BR.T_CLAIMANTNAME CLAIMANTNAME,												"+
				"	BR.T_CLAIMANTID CLAIMANTID,													"+
				"	BR.T_BANKRUPTTYPE BANKRUPTTYPE,												"+
				"	BR.T_COURTDECISION COURTDECISION,											"+
				"	BR.T_SYSDATE SYSDATEt,														"+
				"	BR.T_OPER OPER																"+
				"	FROM DPTBANKRUPTHIST_DBT BR 												"+
				"	WHERE BR.T_PARTYID = ?  AND BR.T_RECID  = ?									";
		  
  cmd = RsdCommand(query);

  cmd.addParam( "PartyID", RSDBP_IN, PartyID );
  
  cmd.addParam( "RecID", RSDBP_IN, RecID );
  
  cmd.NullConversion = true;

  rs = RsdRecordset(cmd);
  
  while(rs.MoveNext())
    		
	odj.RecID = SQL_ConvType(rs.value("RECID"));
	
	if( RecID!= 0)
	
	  odj.EventDate = SQL_ConvType(rs.value("EVENTDATE"));
	  
	end; 
	
	odj.RecTypeList = TArray();
	
	odj.RecTypeList[odj.RecTypeList.size] = CreateLLValueFromAppServ(3563,SQL_ConvType(rs.value("RECTYPE"))); 
	
	odj.BankruptStatusList = TArray();
	
	odj.BankruptStatusList[odj.BankruptStatusList.size] = CreateLLValueFromAppServ(3564, SQL_ConvType(rs.value("BANKRUPTSTATUS")));  
	
	odj.CourtName = SQL_ConvType(rs.value("COURTNAME"));
	
	odj.CaseNumber = SQL_ConvType(rs.value("CASENUMBER"));
	
	odj.PerformDate = SQL_ConvType(rs.value("PERFORMDATE"));
	
	odj.ClaimantName = SQL_ConvType(rs.value("CLAIMANTNAME"));
		
	odj.ClaimantID = SQL_ConvType(rs.value("CLAIMANTID"));
	
	odj.BankruptTypeList = TArray();
	
	odj.BankruptTypeList[odj.BankruptTypeList.size] = CreateLLValueFromAppServ(2758, SQL_ConvType(rs.value("BANKRUPTTYPE")));
	
	odj.CourtDecision = SQL_ConvType(rs.value("COURTDECISION"));
	 
  end;
  
  if( RecID == 0)
	
	  odj.EventDate = date;
	  
  end; 
  
  odj.SysDate = date;
	
  odj.Oper = FindPersonByNumber({oper});

 return odj;

end;

macro WebCore_PT_DeletePTBANKRUPTHIST_Mas(RecIDList):WebResponseBuilder

  var res = 0;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  var i =0;

  while ( i < RecIDList.size)
 
    res = PT_DeletePTBANKRUPTHIST(RecIDList[i]);
	
	i= i + 1;
	
	if(res != 0)

      AddErrorMessage(@ErrorMessage);

    end;
	
	responseBuilder.SetUserObject(res);
  
  end;

  return responseBuilder.Result;

end;

macro WebCore_PT_InsUpdPTBANKRUPTHIST(PTBankRuptHist,PartyID):WebResponseBuilder

  Record  PtBankruptHistRec  ("ptbankrupthist.dbt"); 

  PtBankruptHistRec.RECID = PTBankRuptHist.RecID;
  
  PtBankruptHistRec.PARTYID = PartyID;

  if(PTBankRuptHist.EventDate != null)
  
    PtBankruptHistRec.EVENTDATE = PTBankRuptHist.EventDate;
  
  end;
  
  if(PTBankRuptHist.RecTypeList != null)
   
    PtBankruptHistRec.RECTYPE = PTBankRuptHist.RecTypeList[PTBankRuptHist.RecTypeList.size-1].Element;
  
  end;
  
  if(PTBankRuptHist.BankruptStatusList != null)
  
    PtBankruptHistRec.BANKRUPTSTATUS = PTBankRuptHist.BankruptStatusList[PTBankRuptHist.BankruptStatusList.size-1].Element;
  
  end;
  
  if(PTBankRuptHist.SysDate != null)
    
    PtBankruptHistRec.SYSDATE = PTBankRuptHist.SysDate;
  
  end;
  if(PTBankRuptHist.Oper != null)
  
    PtBankruptHistRec.OPER = PTBankRuptHist.Oper.Number;
  
  end;
  
  if(PTBankRuptHist.CourtName != null)
  
    PtBankruptHistRec.COURTNAME = PTBankRuptHist.CourtName;
  
  end;
  
  if(PTBankRuptHist.CaseNumber != null)
  
    PtBankruptHistRec.CASENUMBER = PTBankRuptHist.CaseNumber;
  
  end;
  
  if(PTBankRuptHist.PerformDate != null)
  
    PtBankruptHistRec.PERFORMDATE = PTBankRuptHist.PerformDate;
  
  end;
  
  if(PTBankRuptHist.ClaimantName != null)
  
    PtBankruptHistRec.CLAIMANTNAME = PTBankRuptHist.ClaimantName;
  
  end;
  
  PtBankruptHistRec.CLAIMANTID = PTBankRuptHist.ClaimantID;
  
  if(PTBankRuptHist.BankruptTypeList != null)
  
    PtBankruptHistRec.BANKRUPTTYPE = PTBankRuptHist.BankruptTypeList[PTBankRuptHist.BankruptTypeList.size-1].Element;
  
  end;
  
  if(PTBankRuptHist.CourtDecision != null)
  
    PtBankruptHistRec.COURTDECISION = PTBankRuptHist.CourtDecision;
  
  end;

  var res = 0;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = PT_InsUpdPTBANKRUPTHIST(PtBankruptHistRec);

  if(res != 0)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;
  
end;

