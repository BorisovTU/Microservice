/*
$Name:        balaccount_widget.mac
$Module:      Главная книга
$Description: Макрос виджета балансовых счетов
*/

import "ws_partycommon.mac";
import "web_scroll_lib.mac";

class TWebBalAccount
  var ID;
  var iNumPlan;
  var Balance;
  var Chapter;
  var Name_Part;
end;

class TWebBalAccountListerItem
  var ID;
  var iNumPlan;
  var Balance;
  var Kind_Account;
  var Name_Part;
  var Chapter;
end;

macro WebCore_GetBalAccountListerItemList(iNumPlan, Chapter, Kind_Account, sub, listLen,PrivID)

  var cmd, rs;
  
  var where_str : string, from_str : string;

  var Arr = TArray;
  
  
  var query = " SELECT T_ID, T_INUMPLAN, T_BALANCE, T_KIND_ACCOUNT, T_NAME_PART, T_CHAPTER "
              "   FROM DBALANCE_DBT balance";
			  
  var query_where = " WHERE 1 = 1 ";
  
  if(iNumPlan >= 0)
  
    query_where = query_where + " AND T_INUMPLAN    = " + string(iNumPlan);
    
  end;

  if(Chapter != 0)
  
    query_where = query_where + " AND T_CHAPTER     = " + string(Chapter);
    
  end;
  
  if( (Kind_Account != null) AND (Kind_Account != "") )
  
    query_where = query_where + " AND T_KIND_ACCOUNT = ? ";
    
  end;
  
  if(sub != "")
  
    query_where = query_where + " AND LOWER(T_BALANCE) LIKE LOWER ('%' || ? || '%') ";
  
  end;

  if( listLen != 0 )

    query_where = query_where + " AND ROWNUM < " + string(listLen);

  end;
  
  if( PrivID != 0 )

    if( GetObjectRestriction(where_str, PrivID, NULL, "balance", NULL, "balance.dbt", @from_str, true) == true )

      if((from_str != "") AND (from_str!= null) )
	
        query = query + ", " + from_str + " ";
	  
      end;

      if(where_str != "")
	
        query_where = query_where + "AND " + where_str + " ";
	  
      end;

    end;

  end;
  
  query_where = query_where + " ORDER BY T_BALANCE ";

  query = query + query_where;

  cmd = RsdCommand(query);
  
  if( (Kind_Account != null) AND (Kind_Account != "") )
  
    cmd.addParam( "", RSDBP_IN, Kind_Account );
    
  end;
  
  if(sub != "")
  
    cmd.addParam( "", RSDBP_IN, sub );
  
  end;
  
  rs = RsdRecordset(cmd);

  while(rs.movenext)

    var obj = TWebBalAccountListerItem;

    obj.ID            = SQL_ConvType(rs.value("T_ID"));
    obj.iNumPlan      = SQL_ConvType(rs.value("T_INUMPLAN"));
    obj.Balance       = SQL_ConvType(rs.value("T_BALANCE"));
    obj.Kind_Account  = SQL_ConvType(rs.value("T_KIND_ACCOUNT"));
    obj.Name_Part     = SQL_ConvType(rs.value("T_NAME_PART"));
    obj.Chapter       = SQL_ConvType(rs.value("T_CHAPTER"));

    Arr[Arr.size] = obj;

  end;

  return Arr;

end;

macro WebCore_CreateTWebBalAccountFromBD(Balance, Chapter, NumPlan )

  var obj;

  var cmd, rs;

  var query = " SELECT T_ID, T_INUMPLAN, T_BALANCE, T_CHAPTER, T_NAME_PART "
              "   FROM DBALANCE_DBT WHERE T_BALANCE = ? ";

  cmd = RsdCommand(query);
  
  cmd.NullConversion = true;
  
  if(Chapter == NULL)
    Chapter = 1;
  end;

  if(NumPlan == NULL)
    NumPlan = 0;
  end;
  
  cmd.addParam( "", RSDBP_IN, Balance );
  cmd.addParam( "", RSDBP_IN, Chapter );
  cmd.addParam( "", RSDBP_IN, NumPlan );
  
  rs = RsdRecordset(cmd);

  if(rs.movenext)

    obj = TWebBalAccount;

    obj.ID       = rs.value("T_ID");
    obj.iNumPlan = rs.value("T_INUMPLAN");
    obj.Balance  = rs.value("T_BALANCE");
    obj.Chapter  = rs.value("T_CHAPTER");
    obj.Name_Part  = rs.value("T_NAME_PART");

  end;

  return obj;

end;

macro WebCore_CreateTWebBalAccount(ID, iNumPlan, Balance, Chapter, Name_Part)

  var obj = TWebBalAccount;
  
  obj.ID       = ID;
  obj.iNumPlan = iNumPlan;
  obj.Balance  = Balance;
  obj.Chapter  = Chapter;
  obj.Name_Part  = Name_Part;

  return obj;

end;


/* Получить условия для встраивания в SQL-запрос */
macro WebBalance_GetQueryParam( select : @string, from : @string, where : @string, alias : string, Balance : string, Chapter : string, iNumPlan : string )

  WebScroll_AddSelect( @select, string(alias, ".t_ID"       , " ", alias, "_ID"       ) );
  WebScroll_AddSelect( @select, string(alias, ".t_iNumPlan" , " ", alias, "_iNumPlan" ) );
  WebScroll_AddSelect( @select, string(alias, ".t_Chapter"  , " ", alias, "_Chapter"  ) );
  WebScroll_AddSelect( @select, string(alias, ".t_Balance"  , " ", alias, "_Balance"  ) );
  WebScroll_AddSelect( @select, string(alias, ".t_Name_Part", " ", alias, "_Name_Part") );

  WebScroll_AddFrom( @from, string("dbalance_dbt ", alias) );

  WebScroll_AddWhere( @where, string(Chapter , " = ", alias, ".t_Chapter(+)" ) );
  WebScroll_AddWhere( @where, string(iNumPlan, " = ", alias, ".t_iNumPlan(+)") );
  WebScroll_AddWhere( @where, string(Balance , " = ", alias, ".t_Balance(+)" ) );

end;


/* Получить условие сортировки для встраивания в SQL-запрос */
macro WebBalance_GetSortParam( alias : string )

  return string(alias, "_Balance");

end;

/* Создать объект TWsDepartment по результатам выполнения SQL-запроса */
macro WebBalance_GetObjectFromRecordset( alias : string, rs : RsdRecordset )

  var p = null;

  var IsBalanceNull : bool;

  var Balance = rs.value( string(alias, "_Balance"), IsBalanceNull );

  if( IsBalanceNull == false )

    p = TWebBalAccount();

    p.Balance   = Balance;
    p.ID        = SQL_ConvType( rs.value(string(alias, "_ID"       )) );
    p.iNumPlan  = SQL_ConvType( rs.value(string(alias, "_iNumPlan" )) );
    p.Chapter   = SQL_ConvType( rs.value(string(alias, "_Chapter"  )) );
    p.Name_Part = SQL_ConvType( rs.value(string(alias, "_Name_Part")) );

  end;

  return p;

end;
