/*
$Name:        calkind_scroll.mac
$Module:      Главная книга
$Description: Макрос видов календаря
*/

IMPORT BankInter, RSD, Календарь, "cb_sql.mac", "globals.mac";
IMPORT "ws_validation.mac";

class TWebCalendarKindGridItem
  var ID                :integer;
  var KindName          :string;
  var Balance           :integer;
  var RetailInSaturday  :bool;
  var RetailInSunday    :bool;
  var RetailInHolyday   :bool;
  var ParentID          :integer;
end;

class TWebCalendarKindGridParam
  var Review    :bool;
end;


PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

macro WebCore_GetCalendarKindGridParam()
  
  var obj = TWebCalendarKindGridParam();
  
  obj.Review = not CheckActionRestriction(140);

  return obj;

end;

macro WebCore_GetCalendarKindGridItemList(ID) : TArray
  var cmd, rs, query;

  var Arr = TArray;

  query = " SELECT * FROM dcalkind_dbt WHERE ";
  
  if(ID != null)
    query = query + " t_id = " + string(ID);
  else
    query = query + " t_id >= 0 ";
  end;
  
  query = query + " ORDER BY t_id ";

  cmd = RsdCommand(query);
  rs = RsdRecordset(cmd);

  while(rs.movenext)
    var obj = TWebCalendarKindGridItem();
    obj.ID                = SQL_ConvType(rs.value("t_ID"));     
    obj.KindName          = SQL_ConvType(rs.value("t_Name"));
    obj.Balance           = SQL_ConvType(rs.value("t_Balance"));
    obj.RetailInSaturday  = SQL_ConvType(rs.value("t_RetailInSaturday"));
    obj.RetailInSunday    = SQL_ConvType(rs.value("t_RetailInSunday"));
    obj.RetailInHolyday   = SQL_ConvType(rs.value("t_RetailInHolyday"));
    obj.ParentID          = SQL_ConvType(rs.value("t_ParentID"));

    Arr[Arr.size] = obj;
  end;

  return Arr;
end;

macro WebCore_GetCalendarKind (ID) //: TWebCalendarKind
  ResponseBuilder = WebResponseBuilder();
  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  var List = WebCore_GetCalendarKindGridItemList(ID);

  if(List.size() == 0)
    ErrorMessage.Code     = 1;
    ErrorMessage.Message  = "Не найден вид календаря";
    ErrorMessage.Severity = MessageSeverity.RuntimeError;

    ResponseBuilder.AddMessage( ErrorMessage );
  else
    responseBuilder.SetUserObject(List[0]);
  end;

  return responseBuilder.Result;
end;

private macro ConvTypeBool(boolVal)
  if(boolVal != NULL)
    if(boolVal == true)
      return "X";
    else
      return "";
    end;
  else
    return "";
  end;
end;

private macro ConvTypeToBool(boolVal)
  if(boolVal != NULL)
    if(boolVal == "X")
      return true;
    else
      return false;
    end;
  else
    return false;
  end;
end;
      
private macro SetCalendarKindByObject(p, CalendarKindObject)
  if(CalendarKindObject.ID                != NULL) p.ID                           = CalendarKindObject.ID               ;  end;
  if(CalendarKindObject.KindName          != NULL) p.Name                         = CalendarKindObject.KindName         ;  end;
  if(CalendarKindObject.Balance           != NULL) p.Balance                      = CalendarKindObject.Balance          ;  end;
  if(CalendarKindObject.RetailInSaturday  != NULL) p.RetailInSaturday             = ConvTypeBool(CalendarKindObject.RetailInSaturday) ;  end;
  if(CalendarKindObject.RetailInSunday    != NULL) p.RetailInSunday               = ConvTypeBool(CalendarKindObject.RetailInSunday  ) ;  end;
  if(CalendarKindObject.RetailInHolyday   != NULL) p.RetailInHolyday              = ConvTypeBool(CalendarKindObject.RetailInHolyday ) ;  end;
  if(CalendarKindObject.ParentID          != NULL) p.ParentID                     = CalendarKindObject.ParentID         ;  end;
end;

private macro SetObjectByCalendarKind(CalendarKindObject, p)
  CalendarKindObject.ID               = p.ID              ;
  CalendarKindObject.KindName         = p.Name            ;
  CalendarKindObject.Balance          = p.Balance         ;
  CalendarKindObject.RetailInSaturday = ConvTypeToBool(p.RetailInSaturday);
  CalendarKindObject.RetailInSunday   = ConvTypeToBool(p.RetailInSunday  );
  CalendarKindObject.RetailInHolyday  = ConvTypeToBool(p.RetailInHolyday );
  CalendarKindObject.ParentID         = p.ParentID        ;
end;                                                                                                                   


macro WebCore_InsertCalendarKind(CalendarKindObject)
  file p("calkind.dbt");
  ClearRecord(p);
  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  SetCalendarKindByObject(p, CalendarKindObject);

  res = CB_InsertCalendarKind(p);

  if(res != True)
    AddErrorMessage(@ErrorMessage);
  else
    SetObjectByCalendarKind(CalendarKindObject, p);
  end;

  responseBuilder.SetUserObject(CalendarKindObject);

  return responseBuilder.Result;
end;

macro WebCore_UpdateCalendarKind(CalendarKindObject, oldCalendarKindObject)
  file p("calkind.dbt");
  ClearRecord(p);

  file oldp("calkind.dbt");
  ClearRecord(oldp);

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  SetCalendarKindByObject(p, CalendarKindObject);
  SetCalendarKindByObject(oldp, oldCalendarKindObject);

  res = CB_UpdateCalendarKind(p, oldp);

  if(res != True)
    AddErrorMessage(@ErrorMessage);
  else
    SetObjectByCalendarKind(CalendarKindObject, p);
  end;

  responseBuilder.SetUserObject(CalendarKindObject);

  return responseBuilder.Result;
end;


macro WebCore_DeleteCalendarKind(CalendarKindID)
  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = CB_DeleteCalendarKind(CalendarKindID);

  if(res != True)
    AddErrorMessage(@ErrorMessage);
  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;
end;


// Сервис проверки существования календаря по годам
macro WebCore_ExistCalendar (ID, Branch)    // Вид календаря. Узел ТС имеет смысл и обязателен при незаданном значении ID.

  ResponseBuilder = WebResponseBuilder();
    
  var calenID = ID;
  var count = 0;
  if((calenID == null) AND (Branch != null))
    calenID = -1;
    GetCalendarForBranch(Branch, calenID);
  end;              

  if(calenID >= 0)   
    var query = " select count(1) cnt from dcalendar_dbt where t_ID = :calenID ";
    var cmd = RsdCommand(query);
    cmd.addParam( "", RSDBP_IN, calenID );
    var rs = RsdRecordset(cmd);

    if(rs.movenext())
      count  = SQL_ConvType(rs.value("cnt"));     
    end;
  end;

  if(count > 0)
    responseBuilder.SetUserObject(true);
  else
    responseBuilder.SetUserObject(false);
  end;

  return responseBuilder.Result;
end;

// Сервис проверки существования календаря на текущий год.
macro WebCore_ExistCalendarToDate (ID, Branch)    // Вид календаря. Узел ТС имеет смысл и обязателен при незаданном значении ID.

  ResponseBuilder = WebResponseBuilder();  
  var calenID = ID;
  var currentYear = 0 ;
  DateSplit ({curdate}, null, null, currentYear );
  var count = 0;
  if((calenID == null) AND (Branch != null))
    calenID = -1;
    GetCalendarForBranch(Branch, calenID);
  end;              
  if(calenID >= 0)   
    var query = " select count(1) cnt from dcalendar_dbt where t_ID = :calenID and t_Year = :Year ";
    var cmd = RsdCommand(query);
    cmd.addParam( "", RSDBP_IN, calenID );
	cmd.addParam( "", RSDBP_IN, currentYear );
    var rs = RsdRecordset(cmd);

    if(rs.movenext())
      count  = SQL_ConvType(rs.value("cnt"));     
    end;
  end;

  if(count > 0)
    responseBuilder.SetUserObject(true);
  else
    responseBuilder.SetUserObject(false);
  end;

  return responseBuilder.Result;
end;


macro WebCore_GenerateCalendar(СalendarKind, FromDate) : WebResponseBuilder
  file p("calkind.dbt");
  ClearRecord(p);

  file oldp("calkind.dbt") key 0;
  ClearRecord(oldp);

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  SetCalendarKindByObject(p, СalendarKind);

  oldp.ID = p.ID;

  if( GetEQ(oldp) )
    res = CB_UpdateCalendarKind(p, oldp);

    if(res != True)
      AddErrorMessage(@ErrorMessage);
    end;
  else
     res = false;
     // AddErrorMessage(@ErrorMessage);  
  end;
  
  if(res == True)
    res = GenerateCalendar( p.ID, FromDate );
  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;
end;

