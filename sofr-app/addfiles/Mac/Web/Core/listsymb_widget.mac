/*
$Name:        listsymb_widget.mac
$Module:      Главная книга
$Description: Макрос виджета выбора кассовых символов
*/

import "ws_partycommon.mac";

class TWebListSymb
  var DocKind;
  var SymbCash;
  var Name;
  var SymbolType;
end;

const TS_BAL = -1, TS_ALL = 0, TS_DEBET = 1, TS_KREDIT = 2, TS_NOTB = 3;

macro WebCore_GetListSymbList(SymbolType, DocKind, sub, listLen)

  var cmd, rs;

  var Arr = TArray;

  var query = " SELECT * "
              "   FROM DLISTSYMB_DBT WHERE 1 = 1 ";
  
  
  if( (DocKind != null) and (DocKind != 0) )

    query = query + " AND T_DocKind = " + string(DocKind);

  end;
  
  
  if(SymbolType == TS_BAL)
  
    query = query + " AND ( T_Type_Symbol = " + string(TS_DEBET) + " OR T_Type_Symbol = " + string(TS_KREDIT) + " ) ";
    
  else
  
    if(SymbolType == TS_ALL)
    
      query = query + " AND 1 = 1 ";
      
    else
    
      query = query + " AND T_Type_Symbol = " + string(SymbolType);
    
    end;
    
  end;
  
  if(sub != "")
  
    query = query + " AND LOWER(T_SYMB_CASH) LIKE LOWER ('%' || ? || '%') ";
  
  end;

  if( listLen != 0 )

    query = query + " AND ROWNUM < " + string(listLen);

  end;
  
  query = query + " ORDER BY t_Symb_Cash ";

  cmd = RsdCommand(query);
  
  if(sub != "")
  
    cmd.addParam( "", RSDBP_IN, sub );
  
  end;
  
  rs = RsdRecordset(cmd);

  while(rs.movenext)

    var obj = TWebListSymb;
    
    obj.DocKind    = rs.value("T_DOCKIND");
    obj.SymbCash   = rs.value("T_SYMB_CASH");
    obj.Name       = rs.value("T_NAME");
    obj.SymbolType = rs.value("T_TYPE_SYMBOL");

    Arr[Arr.size] = obj;

  end;

  return Arr;
  
end;

macro WebCore_CreateTWebListSymbFromBD(DocKind)

  var obj;

  var cmd, rs;

  var query = " SELECT * FROM DLISTSYMB_DBT WHERE T_DOCKIND = ? ";

  cmd = RsdCommand(query);
  
  cmd.addParam( "", RSDBP_IN, DocKind );
  
  rs = RsdRecordset(cmd);

  if(rs.movenext)

    obj = TWebListSymb;
    
    obj.DocKind    = rs.value("T_DOCKIND");
    obj.SymbCash   = rs.value("T_SYMB_CASH");
    obj.Name       = rs.value("T_NAME");
    obj.SymbolType = rs.value("T_TYPE_SYMBOL");

  end;

  return obj;

end;

macro WebCore_CreateTWebMCCateg(DocKind, SymbCash, Name, SymbolType)

  var obj = TWebListSymb;
  
  obj.DocKind    = DocKind;
  obj.SymbCash   = SymbCash;
  obj.Name       = Name;
  obj.SymbolType = SymbolType;

  return obj;

end;