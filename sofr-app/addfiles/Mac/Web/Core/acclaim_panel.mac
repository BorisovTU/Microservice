 /*
 $Name: acclaim_panel.mac
 $Module: Главная книга
 $Description: Макрос Панели претензий
 */
 
import "acclaim_scroll.mac";
import "ws_person.mac";

class TWebAcClaimListerItem
  var ClaimID;
  var DocNumber;
  var Initiator;
  var CurrentAmount;
  var Priority;
  var Comment;
end;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
  if( condition )
    return value_true;
  else
    return value_false;
  end;
end;

private macro GetCurrentAmount(claimid, ondate);

  var p;

  var query = " SELECT "
              "   acst.t_CurrentAmount "
              " FROM dacclaimstate_dbt acst "
              " WHERE acst.t_ClaimID = " + string(claimid) +
              " AND acst.t_StateDate <= " + To_SQLData(ondate) +
              " ORDER BY acst.t_StateDate DESC ";

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if (ds.MoveNext())

    p = ds.CurrentAmount;

  else

    p = null;

  end;

  return p;

end;

private macro GetCurrentPriority(claimid, ondate);

  var p;

  var query = " SELECT "
              "   acst.t_Priority "
              " FROM dacclaimstate_dbt acst "
              " WHERE acst.t_ClaimID = " + string(claimid) +
              " AND acst.t_StateDate <= " + To_SQLData(ondate) +
              " ORDER BY acst.t_StateDate DESC ";

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if (ds.MoveNext())

    p = ds.Priority;

  end;

  return p;

end;

macro WebCore_GetAcClaimListerItemList(Account, FIID, Chapter, ClaimKind, OnDate, ExcludeClaimID, sub, listLen)

  var cmd, rs;

  var Arr = TArray;

  var query = " SELECT cl.* "
              "  FROM DACCLAIM_DBT cl WHERE 1 = 1 ";

  if(Account != null)

    query = query + " AND T_ACCOUNT	= '" + string(Account) + "' ";

  end;

  if(FIID != -1)

    query = query + " AND T_FIID	= " + string(FIID);

  end;

  if(Chapter != 0)

    query = query + " AND T_CHAPTER	= " + string(Chapter);

  end;

  if(ClaimKind != 0)

    query = query + " AND T_CLAIMKIND	= " + string(ClaimKind);

  end;

  if(ExcludeClaimID != 0)

    query = query + " AND T_CLAIMID	!= " + string(ExcludeClaimID);

  end;

  if(OnDate != null)

    query = query + " AND EXISTS "
                    "       (SELECT 1 "
                    "          FROM dacclaimstate_dbt acs "
                    "         WHERE acs.t_ClaimID = cl.t_ClaimID AND acs.t_State IN (1, 2) "
                    "           AND acs.t_StateDate = "
                    "                 (SELECT MAX (acs2.t_StateDate) "
                    "                    FROM dacclaimstate_dbt acs2 "
                    "                   WHERE acs2.t_ClaimID = acs.t_ClaimID "
                    "                     AND acs2.t_StateDate <= " + To_SQLData(OnDate) + " )) "


  end;

  if(sub != "")

    query = query + " AND LOWER(T_DOCNUMBER) LIKE LOWER ('%' || ? || '%') ";

  end;

  if( listLen != 0 )

    query = query + " AND ROWNUM < " + string(listLen);

  end;

  query = query + " ORDER BY T_SYSDATE ";

  cmd = RsdCommand(query);

  cmd.NullConversion = true;

  if(sub != "")

    cmd.addParam( "", RSDBP_IN, sub );

  end;

  rs = RsdRecordset(cmd);

  while(rs.movenext)

    var obj = TWebAcClaimListerItem;

    obj.ClaimID       = SQL_ConvType(rs.value("T_CLAIMID"));
    obj.DocNumber     = SQL_ConvType(rs.value("T_DOCNUMBER"));
    obj.Initiator     = GetInitiator_TWebNameKind(SQL_ConvType(rs.value("T_INITIATOR")));
    obj.CurrentAmount = GetCurrentAmount(obj.ClaimID, OnDate);
    obj.Priority      = GetCurrentPriority(obj.ClaimID, OnDate);
    obj.Comment       = SQL_ConvType(rs.value("T_COMMENT"));

    Arr[Arr.size] = obj;

  end;

  return Arr;

end;

macro WebCore_CreateTWebAcClaimObjFromBD(ClaimID)

  var obj;

  var cmd, rs;

  var query = " SELECT * FROM DACCLAIM_DBT WHERE T_CLAIMID = ? ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, ClaimID );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    obj = TWebAcClaimObj;

    obj.ClaimID   = rs.value("T_CLAIMID");
    obj.DocNumber = rs.value("T_DOCNUMBER");

  end;

  return obj;

end;

macro WebCore_CreateTWebAcClaimObj(ClaimID, DocNumber)

  var obj = TWebAcClaimObj;

  obj.ClaimID   = ClaimID;
  obj.DocNumber = DocNumber;

  return obj;

end;

macro WebCore_GetAcClaimInitListItem()

  var cmd, rs;

  var Arr = TArray;

  var query = " SELECT * "
              "  FROM DACCLAIMINIT_DBT ";

  query = query + " ORDER BY T_INITIATORID  ";

  cmd = RsdCommand(query);

  cmd.NullConversion = true;

  rs = RsdRecordset(cmd);

  while(rs.movenext)

    var obj = TWebAcClaimInit;
    
    obj.InitiatorID = SQL_ConvType(rs.value("T_INITIATORID"));
    obj.Group       = SQL_ConvType(rs.value("T_GROUP"));
    obj.Name        = SQL_ConvType(rs.value("T_NAME"));

    Arr[Arr.size] = obj;

  end;

  return Arr;
  
end;

macro WebCore_GetNewAcClaimObject(Chapter, FIID, Account)

  var obj = TWebAcClaimObject();

  obj.Chapter          = Chapter;
  obj.FIID             = FIID;
  obj.Account          = Account;
  obj.Oper             = FindPersonByNumber({oper});
  obj.SysDate          = {curdate};
  obj.DocDate          = {curdate};
  obj.StartDate        = {curdate};
  obj.MaxPriority      = GetMaxPriority();  
  obj.Comment          = "";
  obj.EditingIsEnabled = true;

  return obj;

end;

macro WebCore_RestoreAcClaimState(ClaimID)

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = CB_RestoreAcClaimState(ClaimID);

  if(res != True)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_CheckAcClaimIncomplete(Account, Code_Currency, Chapter)

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = AccClaimIncomplete(Account, Chapter, Code_Currency);

  if(res == True)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

private macro SetAcClaim(AcClaim, AcClaim_)

  AcClaim.ClaimID      = AcClaim_.ClaimID;
  AcClaim.Chapter      = AcClaim_.Chapter;
  AcClaim.Account      = AcClaim_.Account;
  AcClaim.FIID         = AcClaim_.FIID;
  
  if(AcClaim_.ClaimKind != null)
    AcClaim.ClaimKind    = AcClaim_.ClaimKind.Element;
  end;
  
  if(AcClaim_.RestKind != null)
    AcClaim.RestKind     = AcClaim_.RestKind.Element;
  end;
  
  if(AcClaim_.Initiator != null)
    AcClaim.Initiator    = AcClaim_.Initiator.InitiatorID;
  end;
  
  AcClaim.DocNumber    = AcClaim_.DocNumber;
  AcClaim.DocDate      = AcClaim_.DocDate;
  AcClaim.SysDate      = AcClaim_.SysDate;
  AcClaim.StartDate    = AcClaim_.StartDate;
  AcClaim.FinishDate   = AcClaim_.FinishDate;
  AcClaim.StartAmount  = AcClaim_.StartAmount;
  AcClaim.Priority     = AcClaim_.Priority;
  AcClaim.Comment      = AcClaim_.Comment;
  AcClaim.FiscOrgCode  = AcClaim_.FiscOrgCode;
  AcClaim.FiscOrgName  = AcClaim_.FiscOrgName;
  AcClaim.FiscOrgINN   = AcClaim_.FiscOrgINN;
  AcClaim.FiscOrderNum = AcClaim_.FiscOrderNum;
  
  if(AcClaim_.Oper != null)
    AcClaim.Oper         = AcClaim_.Oper.Number;
  end;
  
  AcClaim.Incremental  = IIF(AcClaim_.IsIncremental == true, "X", "");
  
  if(AcClaim_.ClaimCancelObj != null)
    AcClaim.Claim_Cancel = AcClaim_.ClaimCancelObj.ClaimID;
  end;
  
  if(AcClaim_.FiscOrgParty != null)
    AcClaim.FiscOrgCode  = AcClaim_.FiscOrgParty.code;
  end;

  AcClaim.Version      = AcClaim_.Version;
  
end;

macro WebCore_InsUpdAcClaim(AcClaimOld, AcClaimNew)

  file acclaimoldf("acclaim.dbt");
  file acclaimf("acclaim.dbt");

  var ClaimID = 0;
  
  var acclaimoldTb = Tbfile ("acclaim", "W", 0);
  
  acclaimoldTb.Clear();
  
  SetAcClaim(acclaimoldTb.rec, AcClaimOld);
  
  Copy(acclaimoldf, acclaimoldTb);

  var acclaimTb    = Tbfile ("acclaim", "W", 0);
  
  acclaimTb.Clear();
  
  SetAcClaim(acclaimTb.rec, AcClaimNew);
  
  Copy(acclaimf, acclaimTb);

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = CB_InsUpdAcClaim(acclaimoldf, acclaimf, @ClaimID);

  if(res != True)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(ClaimID);

  return responseBuilder.Result;
  
end;
