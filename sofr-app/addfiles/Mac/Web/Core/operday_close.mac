 /*
 $Name: operday_close.mac
 $Module: Главная книга
 $Description: Макрос Завершение операционного дня
 */

import globals;
import "ws_validation.mac";
import "ws_dprt.mac";
import "ws_file.mac";

class TWebOperdayCloseDateObject
  var FirstOpenDate;
  var Department;
  var OperCanExecServProcHead;
  var IsAllDprtSelected;
end;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

macro WebCore_BeforeShowCloseDatePanel()

  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  if(({cTypePerson} != TP_BOSS) AND (CheckActionRestriction(145) == true))

    res = true;

  end;

  if(res != true)

    CreateErrMsg(22169);
    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;
  
end;

macro WebCore_InitCloseDatePanel()

  var CloseDateObj  = TWebOperdayCloseDateObject();
  
  var FirstDate = date(0,0,0);
  
  var res = CB_GetFirstOpenDate({OperDprt}, @FirstDate);
  
  if(res == true)
    
    CloseDateObj.FirstOpenDate         = FirstDate;
    
  end;
  
  CloseDateObj.Department              = GetTWsDepartmentByCode({OperDprt});
  
  var err;

  GetRegistryValue( "COMMON\\СЕРВИСНЫЕ ПРОЦЕДУРЫ\\ЗАПУСК СЕРВИСНЫХ ПРОЦЕДУР", V_BOOL, res, err );

  if(err == 0)
    
    if((res == true) AND ({OperDprt} == {NumDprt}))
      CloseDateObj.OperCanExecServProcHead = true;
    else
      CloseDateObj.OperCanExecServProcHead = false;
    end;
    
  else
    RunError("Ошибка при чтении настройки реестра!");
  end; 

  return CloseDateObj;

end;

macro WebCore_GetCloseDatePanel(Department, IsAllDprtSelected)

  var res;

  var FirstOpenDate;

  if(IsAllDprtSelected == true)
    
    res = CB_GetFirstOpenDate(0, @FirstOpenDate);
    
  else
    
    if(Department != null)
      
      res = CB_GetFirstOpenDate(Department.Code, @FirstOpenDate);
      
    end;
  
  end;
  
  return FirstOpenDate;

end;

macro WebCore_ExecCloseDate(DprtID, CloseDate)

  var fc = null;
  
  var res = true;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  var LogName : string = "";

  if(DprtID !=0)
    
    res = CB_CheckCloseDay(DprtID, CloseDate);
    
  end;
  
  if(res == true)
    
    if((DprtID !=0) AND (DprtID != {OperDprt}))
      
      var err;

      GetRegistryValue( "COMMON\\СЕРВИСНЫЕ ПРОЦЕДУРЫ\\ЗАПУСК СЕРВИСНЫХ ПРОЦЕДУР", V_BOOL, res, err );

      if(err == 0)
        
        if((res != true) OR ({OperDprt} != {NumDprt}))
          
          AddErrorMessageExt(@ErrorMessage, GetErrorMsg( 22110 ));
          
          res = false;
          
        end;
        
      else
        RunError("Ошибка при чтении настройки реестра!");
      end; 
      
    end;
    
  end;
  
  if(res == true)

  res = CB_CloseDate(DprtID, CloseDate, @LogName);
  
  if(LogName != "")
  
    fc = GetTxtFileName(LogName);
    
    fc = ConvertTxt2Html(fc);
  
    fc = GetTextFile(fc);
  
  end;

  end;

  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(fc);

  return responseBuilder.Result;
  
end;