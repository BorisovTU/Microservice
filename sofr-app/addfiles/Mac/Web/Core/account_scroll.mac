/*
$Name:        account_scroll.mac
$Module:      Главная книга
$Description: Макрос списка личевых счетов
*/

IMPORT FIInter, CurrInter, BankInter, PTInter, Календарь;
import "ws_partycommon.mac";
import "ws_ficlass.mac";
import "ws_fiunilist.mac";
import "ws_dprt.mac";
import "ws_partyclasses.mac";
import "ws_partylist.mac";
import "balaccount_widget.mac";
import "ws_validation.mac";
import "ws_datafilter.mac";
import "pr_party_lib.mac";
import "ws_file.mac";
import "account_scroll_protocol.mac";



/* Идентификаторы сортировки */
private const SORT_FI        = 0;     /* Валюта счета */
private const SORT_FI_EQ     = 1;     /* Валюта эквиалента счета */
private const SORT_DEPART    = 2;     /* Филиал счета */
private const SORT_BRANCH    = 3;     /* Подразделение счета */
private const SORT_CLIENT    = 4;     /* Клиент счета */
private const SORT_BALANCE   = 5;     /* Балансовый счет */
private const SORT_REST_NC   = 6;     /* Остаток в национальный валюте */
private const SORT_REST_FIID = 7;     /* Остаток в валюте счета */
private const SORT_REST_EQV  = 8;     /* Остаток в валюте эквивалента */

private const SORT_COUNT_ALL = 9;     /* Общее количество сортировок по связанным таблицам */


/* Псевдонимы для связанных таблиц */

private const ALIAS_FI      = "FI";         /* Валюта счета */
private const ALIAS_FI_EQ   = "FI_EQ";      /* Валюта эквиалента счета */
private const ALIAS_DEPART  = "DEPART";     /* Филиал счета */
private const ALIAS_BRANCH  = "BRANCH";     /* Подразделение счета */
private const ALIAS_CLIENT  = "CLIENT";     /* Клиент счета */
private const ALIAS_BALANCE = "BALANCE";    /* Балансовый счет */

private const PRIV_ACCOUNT_SCROL = 6;


class TWebAccStatementPeriodPanelObject

  var BeginPeriodDate : date;
  
  var EndPeriodDate   : date;
  
  var ListRepFormats  ;//List<TWbNameKind>
  
  var SelectedRepFormat : integer;

end;

class TWbNameKind

  var ID    : integer ;
  
  var Name  : string ;
  
end;


class TWebAccountGridItem
  var AccountID;
  var ChapterSymbol	: string;
  var IsClosed		: bool;
  var Open_Close;
  var Account;
  var Currency;
  var Chapter;
  var Department;
  var Branch;
  var NameAccount;
  var RestNat : money   = $0;
  var RestCur : money   = $0;
  var Client;
  var Balance;
  var Kind_Account;
  var CurrencyEQ;
  var RestEqv : money   = $0;
  var Open_Date;
  var Close_Date;
end;

class TWebAccRestTurnGridItem
  var AccountID;
  var RestCurrency : money   = $0;
  var RestDate;
  var Month;
  var Rest         : money   = $0;
  var PlanRest     : money   = $0;
  var Debet        : money   = $0;
  var Credit       : money   = $0;
  var DebetSPOD    : money   = $0;
  var CreditSPOD   : money   = $0;
end;

class TWebAnlAccSubGridItem

  var AccanaliticsID    : integer ;
  
  var SubAccountID      : integer ;
  
  var AnlNameAccSubCode : string ;
  
  var Status                    : string ;
  
  var Rest                              : money ;
  
  var Analiticsid     : integer; 
  
  var Children          ;//List<TWebAnlAccSubGridItem>
 
end;


PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else 
      return value_false;
   end;
end;

private macro SettingParm(): integer

  var err=0;
  
  var ProcValue;
  
  GetRegistryValue( "REPORT\\ВЫПИСКА\\ФОРМАТ ВЫПИСКИ", V_INTEGER, ProcValue, err);

  if(err == 0)
    
    return ProcValue;
         
  else

    return 0;
        
  end;
 
end;

private macro ConvertCharToBool(CurValue :string ) : bool

  if(CurValue == "X")
    
	return true;
	
  else
  
    return false;
	
  end;
  
end;

/*
 * Работа с виджетом выбора финансового инструмента
 */
macro GetCurrency(Code_Currency)

  var cmd, rs, p;

  var query = " SELECT t_FIID As Fiid, "
              "        t_FI_Code As CodeWidget, "
              "        t_FI_Code As CodeList, "
              "        t_FI_Kind As Kind, "
              "        t_Name As Name "
              "        FROM dfininstr_dbt "
              "        WHERE t_FIID = ? ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, Code_Currency );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    p = TWsFinInstr();

    p.Fiid          = SQL_ConvType(rs.value("Fiid"));
    p.CodeWidget    = SQL_ConvType(rs.value("CodeWidget"));
    p.CodeList      = SQL_ConvType(rs.value("CodeList"));
    p.Kind          = SQL_ConvType(rs.value("Kind"));
    p.Name          = SQL_ConvType(rs.value("Name"));

  end;

  return p;

end;

/*
 * Определение остатка на счете
 */
private macro GetRest( alias : string, str_fiid : string, field_name : string, field_rest : string )

  if( field_rest == null ) field_rest = "t_Rest"; end;
  
  
  var query = "NVL( (SELECT rtbl." + field_rest +
              "      FROM drestdate_dbt rtbl "
              "      WHERE rtbl.T_ACCOUNTID = " + alias + " .T_ACCOUNTID "
              "        AND rtbl.T_RESTCURRENCY = " + str_fiid +
              "        AND rtbl.T_RESTDATE = "
              "        (SELECT max(m.T_RESTDATE) FROM drestdate_dbt m "
              "         WHERE m.T_ACCOUNTID = rtbl.T_ACCOUNTID AND m.T_RESTCURRENCY = " + str_fiid +
              "         AND m.T_RESTDATE <= to_date('"+ {curdate} + "','dd.mm.yyyy'))), 0) " + field_name + "";

  return query;

end;

/*
 * Определение остатка на счете в валюте эквивалента
 */
private macro GetRestEqv( alias : string, field_name : string )

  var query = "NVL( (SELECT rtbl.t_Rest "
              "      FROM dresteqv_dbt rtbl "
              "      WHERE rtbl.T_ACCOUNTID = " + alias + " .T_ACCOUNTID "
              "        AND rtbl.T_RESTDATE = "
              "        (SELECT max(m.T_RESTDATE) FROM dresteqv_dbt m "
              "         WHERE m.T_ACCOUNTID = rtbl.T_ACCOUNTID "
              "         AND m.T_RESTDATE <= to_date('"+ {curdate} + "','dd.mm.yyyy'))), 0) " + field_name;

  return query;

end;

macro GetClient(Client)

  var cmd, rs, p;

  var query = " SELECT pt.t_PartyID, pt.t_LegalForm, pt.t_Name, pt.t_ShortName, pcode.t_Code "
              "   FROM dparty_dbt pt, dpartcode_dbt pcode "
              " WHERE pt.t_PartyID = ? "
              "   AND pt.t_PartyID = pcode.t_PartyID "
              "   AND pcode.t_CodeKind = 1 ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, Client );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    p = TWsPartyEx();

    p.partyid   = SQL_ConvType(rs.value("t_PartyID"));
    p.legalform = SQL_ConvType(rs.value("t_LegalForm"));
    p.name      = SQL_ConvType(rs.value("t_Name"));
    p.partyname = SQL_ConvType(rs.value("t_Name"));
    p.shortname = SQL_ConvType(rs.value("t_ShortName"));
    p.code      = SQL_ConvType(rs.value("t_Code"));

  end;

  return p;

end;

private macro GetRestNVPI(AccountID)

  var cmd, rs, p;

  var query = " SELECT rtbl.t_Rest "
              "   FROM dresteqv_dbt rtbl "
              " WHERE rtbl.t_AccountID = ? "
              "   AND rtbl.t_RestDate = "
              "   (SELECT max(t.t_RestDate) FROM dresteqv_dbt t "
              "   WHERE t.t_AccountID = ? AND t.t_RestDate <= ?) ";

  p = $0;

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, AccountID );
  cmd.addParam( "", RSDBP_IN, AccountID );
  cmd.addParam( "", RSDBP_IN, {curdate} );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    p = rs.value("t_Rest");

  end;

  return p;

end;

private macro GetSortStr( OrderItems,            /* Параметры сортировки */
                          DefaultSortStr:string, /* Строка сортировки по умолчанию (первичному ключу) */
                          Table:string           /* Название таблицы c . */
                        ):String
                        
  var SortStr:string = "";

  if( (OrderItems == null) or (OrderItems.Size() <= 0) )
  
     SortStr = " " + DefaultSortStr + " ";
     
  else
    
     var i:integer = 0;
     
     while( i < OrderItems.size() )
        if ((OrderItems[i].Column == "NameAccount") OR (OrderItems[i].Column == "Kind_Account") OR (OrderItems[i].Column == "Open_Date") OR (OrderItems[i].Column == "Close_Date"))
            OrderItems[i].Column = "account.T_" + OrderItems[i].Column;
        elif (OrderItems[i].Column == "Account")               OrderItems[i].Column = "account.T_ACCOUNT" ;
        elif (OrderItems[i].Column == "Department")            OrderItems[i].Column = WebDprt_GetSortParam( ALIAS_DEPART );
        elif (OrderItems[i].Column == "Branch")                OrderItems[i].Column = WebDprt_GetSortParam( ALIAS_BRANCH );
        elif (OrderItems[i].Column == "Currency")              OrderItems[i].Column = WebFI_GetSortParam( ALIAS_FI );
        elif (OrderItems[i].Column == "Client")                OrderItems[i].Column = WebParty_GetSortParam( ALIAS_CLIENT );
        elif (OrderItems[i].Column == "Balance")               OrderItems[i].Column = WebBalance_GetSortParam( ALIAS_BALANCE );
        elif (OrderItems[i].Column == "CurrencyEQ")            OrderItems[i].Column = WebFI_GetSortParam( ALIAS_FI_EQ );
        elif (OrderItems[i].Column == "RestNat")               OrderItems[i].Column = "RESTNAT";
        elif (OrderItems[i].Column == "RestCur")               OrderItems[i].Column = "RESTCUR";
        elif (OrderItems[i].Column == "RestEqv")               OrderItems[i].Column = "RESTEQV";
        end;
        
        if (i!=0) 
          SortStr = SortStr + ", " + OrderItems[i].Column + " " + IIF(OrderItems[i].Direction == 0, "asc", "desc");
        else 
          SortStr = SortStr + "  " + OrderItems[i].Column + " " + IIF(OrderItems[i].Direction == 0, "asc", "desc");
        end;
        
        i = i + 1;
     end;
     
     SortStr = SortStr + ", " + DefaultSortStr;
     
  end;

  return SortStr;
  
end;

private macro IntCondition(filteritems, value) : string

  var condition = "";
  var conditionNOT = "";
  var condB = "";
  var condE = "";

  if( filteritems.IsNot )
    conditionNOT = " NOT ";
  else
    conditionNOT = " ";
  end;

  if( filteritems.Condition == FilterCondition_Null )
    condB = " = 0 ";
  end;

  if( filteritems.Condition == FilterCondition_Equal )
    condB = " LIKE ";
    condE = " ";
  end;

  if( filteritems.Condition == FilterCondition_StartWith )
    condB = " LIKE ";
    condE = " || '%'";
  end;

  if( filteritems.Condition == FilterCondition_Include )
    condB = " LIKE '%' || ";
    condE = " || '%'";
  end;

  condition = condition + conditionNOT;

  condition = condition + condB;

  if( filteritems.Condition != FilterCondition_Null )

    condition = condition + string(value);

  end;

  condition = condition + condE;

  return condition;

end;

private macro TypeAccountToString( TypeAccount : string ) : string

  if( TypeAccount == "'" )
    TypeAccount = "''";
  end;

  return TypeAccount;

end;


private macro TypeAccountFilterCondition( FilterItem, SQLField : string ) : string
  
  var SQLString = "";

  if( FilterItem.Value[0] != null )

    var i : integer;

    SQLString = SQLString + " AND ";

    var Length = StrLen(FilterItem.Value[0]);
   
    if( FilterItem.Condition == FilterCondition_Equal )

      if( FilterItem.IsNot ) SQLString = SQLString + " NOT "; end;

      SQLString = SQLString + "(";

      SQLString = SQLString + " LENGTH(" + SQLField + ") = " + string(Length);
      
      i = 1;

      while( i <= Length )

        SQLString = SQLString + " AND " + SQLField + " LIKE '%'||'" + TypeAccountToString(SubStr(FilterItem.Value[0], i, 1)) + "'||'%'";

        i = i + 1;

      end;

      SQLString = SQLString + ")";

    elif( FilterItem.Condition == FilterCondition_Include )

      if( Length > 0 )
      
        SQLString = SQLString + "(";

        i = 1;

        while( i <= Length )

          if( i > 1 )
            SQLString = SQLString + " OR ";
          end;

          if( FilterItem.IsNot ) SQLString = SQLString + " NOT "; end;

          SQLString = SQLString + SQLField + " LIKE '%'||'" + TypeAccountToString(SubStr(FilterItem.Value[0], i, 1)) + "'||'%'";

          i = i + 1;

        end;

        SQLString = SQLString + ")";

      end;

    end;

  end;

  return SQLString;

end;

private const FILTERITEM_OPEN_CLOSE      = 0;   // Статус
private const FILTERITEM_ACCOUNT         = 1;   // Лицевой счет
private const FILTERITEM_CLIENT          = 2;   // Клиент
private const FILTERITEM_CURRENCY_CODE   = 3;   // Валюта
private const FILTERITEM_OPER            = 4;   // Операционист
private const FILTERITEM_BALANCE         = 5;   // Балансовый
private const FILTERITEM_PHASE           = 6;   // Фазы счета
private const FILTERITEM_CLAIMKIND       = 7;   // Вид претензии
private const FILTERITEM_CLAIMSTATE      = 8;   // Тип ареста
private const FILTERITEM_RESTNATCUR      = 9;   // Остаток в национальной валюте
private const FILTERITEM_PLANREST        = 10;  // Планируемый остаток
private const FILTERITEM_TYPE_ACCOUNT    = 11;  // Тип счета
private const FILTERITEM_USERTYPEACCOUNT = 12;  // Пользовательский тип счета
private const FILTERITEM_DEPARTMENT      = 13;  // Филиал
private const FILTERITEM_BRANCH          = 14;  // Подразделение ТС
private const FILTERITEM_OPEN_DATE       = 15;  // Дата открытия счета
private const FILTERITEM_CLOSE_DATE      = 16;  // Дата закрытия счета
private const FILTERITEM_SYMBOL          = 17;  // Символ отчетности
private const FILTERITEM_DATE_CARRY      = 18;  // Дата последней проводки
private const FILTERITEM_RESTFIID        = 19;  // Остаток в валюте счета
private const FILTERITEM_CHAPTERSYMBOL   = 20;  // Символ главы счета.
private const FILTERITEM_BANKRUPT_STATUS = 21;  // Статус банкротства физлица

macro GetAddWhereCondition(
  FilterItems/*:TArray of FilterCondItem*/
):String

  var condition = "";
  var condition_temp = "";
  var ConvertMask = "";
  var conditionNOT;

  var FilterItemsCount = 0;
  var ValueCount = 0;

  while( FilterItemsCount < FilterItems.Size )

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_OPEN_CLOSE ) // Статус

      if(FilterItems[FilterItemsCount].Value.Size > 0)

        condition = condition + " AND ";

        if( FilterItems[FilterItemsCount].IsNot == true ) condition = condition + "NOT "; end;

        if( FilterItems[FilterItemsCount].Value.Size == 0 )

          condition = condition + "1 <> 1";

        else

          condition = condition + "(";

          ValueCount = 0;
          while( ValueCount < FilterItems[FilterItemsCount].Value.Size )

            if( ValueCount > 0 )
              condition = condition + " OR ";
            end;

            condition = condition + "account.T_OPEN_CLOSE = ";
            
            
            if(FilterItems[FilterItemsCount].Value[ValueCount] == 0);

              condition = condition + "CHR(0)";

            end;

            if(FilterItems[FilterItemsCount].Value[ValueCount] == 1);

              condition = condition + "'З'";

            end;

            ValueCount = ValueCount + 1;

          end;
        
          condition = condition + ") ";

        end;

      end;

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_ACCOUNT ) // Лицевой счет

      if( IsAccountWithMask(FilterItems[FilterItemsCount].Value[0]) )

        ConvertMask = "";

        ConvertMask = ConvertMaskToSQLFormat( FilterItems[FilterItemsCount].Value[0], "account.T_ACCOUNT" );

        if( ConvertMask == "" )
          ConvertMask = "1 = 1";
        end;

        condition = condition + " AND (" + ConvertMask + ") ";

      else

        condition = condition + " AND " + GetFilterStringSQLCondition( FilterItems[FilterItemsCount], "account.T_ACCOUNT", true, false );

      end;

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_CLIENT ) // Клиент

      condition = condition + GetFilterPartySQLCondition( FilterItems[FilterItemsCount], "account.T_CLIENT" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_CURRENCY_CODE ) // Валюта

      condition = condition + GetFilterFISQLCondition( FilterItems[FilterItemsCount], "account.T_CODE_CURRENCY" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_OPER ) // Операционист

      condition = condition + GetFilterOperSQLCondition( FilterItems[FilterItemsCount], "account.T_OPER" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_BALANCE ) // Балансовый

      if( IsAccountWithMask(FilterItems[FilterItemsCount].Value[0]) )

        ConvertMask = "";

        ConvertMask = ConvertMaskToSQLFormat( FilterItems[FilterItemsCount].Value[0], "account.T_BALANCE" );

        if( ConvertMask == "" )
          ConvertMask = "1 = 1";
        end;

        condition = condition + " AND (" + ConvertMask + ") ";

      else

        condition = condition + " AND " + GetFilterStringSQLCondition( FilterItems[FilterItemsCount], "account.T_BALANCE", true, false );

      end;

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_PHASE ) // Фазы счета

        var strCurDate = "TO_DATE('" + string({curdate}:f) + "','DD.MM.YYYY')";
        
        var PhaseString = " NVL( (SELECT t_Phase t FROM dopdphase_dbt t WHERE t.t_AccountID = account.t_AccountID AND t.t_PhaseDate = " + strCurDate + " ), "
                          "      (SELECT t_Phase t FROM dcurdate_dbt t WHERE t.t_Branch = account.t_Department AND t.t_CurDate = " + strCurDate + " ) )";

        
        condition = condition + " AND " + GetFilterIntegerSQLCondition( FilterItems[FilterItemsCount], PhaseString );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_CLAIMKIND ) // Вид претензии

      if(FilterItems[FilterItemsCount].Value.Size > 0)

        var ClaimKindStr = "";

        conditionNOT = "";

        if( FilterItems[FilterItemsCount].IsNot == true ) conditionNOT = "NOT "; end;

        ValueCount = 0;

        while( ValueCount < FilterItems[FilterItemsCount].Value.Size )

          if(FilterItems[FilterItemsCount].Value[ValueCount] == 0);

          ClaimKindStr = ClaimKindStr + "1,"; //ACCLAIM_KIND_ARREST

          end;

          if(FilterItems[FilterItemsCount].Value[ValueCount] == 1);

          ClaimKindStr = ClaimKindStr + "2,"; //ACCLAIM_KIND_SPECIAL

        end;

          if(FilterItems[FilterItemsCount].Value[ValueCount] == 2);

          ClaimKindStr = ClaimKindStr + "3,"; //ACCLAIM_KIND_RESERVE

          end;

          ValueCount = ValueCount + 1;

        end;

        ClaimKindStr = SubStr(ClaimKindStr, 1, StrLen(ClaimKindStr) - 1);

        if(ClaimKindStr != "")

          condition = condition + " AND " + conditionNOT
                                + "EXISTS ( "
                                  "  SELECT 1 "
                                  "    FROM dacclaim_dbt ac, dacclaimstate_dbt acs "
                                  "   WHERE ac.t_Chapter = account.t_Chapter "
                                  "   AND ac.t_Account = account.t_Account "
                                  "   AND ac.t_FIID = account.t_Code_Currency "
                                  "   AND (ac.t_FinishDate = TO_DATE ('01010001', 'ddmmyyyy') "
                                  "    OR ac.t_FinishDate > to_date('"+ {curdate} + "','dd.mm.yyyy')) "
                                  "   AND ac.t_ClaimKind IN ( " + string(ClaimKindStr) + " ) "
                                  "   AND acs.t_ClaimID = ac.t_ClaimID "
                                  "   AND (acs.t_State = 1 "
                                  "    OR acs.t_State = 2) "
                                  "   AND acs.t_StateDate = "
                                  "   (SELECT MAX (tmp.t_StateDate) "
                                  "     FROM dacclaimstate_dbt tmp "
                                  "    WHERE tmp.t_ClaimID = ac.t_ClaimID "
                                  "      AND tmp.t_StateDate <= to_date('"+ {curdate} + "','dd.mm.yyyy')) "
                                  ") ";

        end;

      end;

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_CLAIMSTATE ) // Тип ареста

      if(FilterItems[FilterItemsCount].Value.Size > 0)
      
        conditionNOT = "";

        if( FilterItems[FilterItemsCount].IsNot == true ) conditionNOT = "NOT "; end;

        if(ClaimKindStr == null)

          ClaimKindStr = "1"; //ACCLAIM_KIND_ARREST

        end;

        var ClaimTypeStr = "";

        ValueCount = 0;

        while( ValueCount < FilterItems[FilterItemsCount].Value.Size )

          if(FilterItems[FilterItemsCount].Value[ValueCount] == 0);

            ClaimTypeStr = ClaimTypeStr + "1,"; //ACCLAIM_TYPE_FULL

          end;

          if(FilterItems[FilterItemsCount].Value[ValueCount] == 1);

            ClaimTypeStr = ClaimTypeStr + "2,"; //ACCLAIM_TYPE_PARTIAL

          end;

          if(FilterItems[FilterItemsCount].Value[ValueCount] == 2);

            ClaimTypeStr = ClaimTypeStr + "3,"; //ACCLAIM_TYPE_AMOUNT

          end;

          if(FilterItems[FilterItemsCount].Value[ValueCount] == 3);

            ClaimTypeStr = ClaimTypeStr + "4,"; //ACCLAIM_TYPE_SECUR

          end;

          ValueCount = ValueCount + 1;

        end;

        ClaimTypeStr = SubStr(ClaimTypeStr, 1, StrLen(ClaimTypeStr) - 1);

        if(ClaimTypeStr != "")

          condition = condition + " AND " + conditionNOT
                                + "EXISTS ( "
                                  "  SELECT 1 "
                                  "    FROM dacclaim_dbt ac, dacclaimstate_dbt acs "
                                  "   WHERE ac.t_Chapter = account.t_Chapter "
                                  "   AND ac.t_Account = account.t_Account "
                                  "   AND ac.t_FIID = account.t_Code_Currency "
                                  "   AND (ac.t_FinishDate = TO_DATE ('01010001', 'ddmmyyyy') "
                                  "    OR ac.t_FinishDate > to_date('"+ {curdate} + "','dd.mm.yyyy')) "
                                  "   AND ac.t_RestKind IN ( " + string(ClaimTypeStr) + " ) "
                                  "   AND ac.t_ClaimKind IN ( " + string(ClaimKindStr) + " ) "
                                  "   AND acs.t_ClaimID = ac.t_ClaimID "
                                  "   AND (acs.t_State = 1 "
                                  "    OR acs.t_State = 2) "
                                  "   AND acs.t_StateDate = "
                                  "   (SELECT MAX (tmp.t_StateDate) "
                                  "     FROM dacclaimstate_dbt tmp "
                                  "    WHERE tmp.t_ClaimID = ac.t_ClaimID "
                                  "      AND tmp.t_StateDate <= to_date('"+ {curdate} + "','dd.mm.yyyy')) "
                                  ") " ;

        end;

      end;

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_RESTFIID ) // Остаток на счете

      var RestFIIDField = GetRest( "ACCOUNT", "ACCOUNT.T_CODE_CURRENCY", "" );

      condition = condition + " AND " + GetFilterMoneySQLCondition( FilterItems[FilterItemsCount], RestFIIDField );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_RESTNATCUR ) // Остаток на счете

      var RestNatCurField = GetRest( "ACCOUNT", string(NATCUR), "" );

      condition = condition + " AND " + GetFilterMoneySQLCondition( FilterItems[FilterItemsCount], RestNatCurField );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_PLANREST ) // Планируемый остаток

      var PlanRestField = GetRest( "ACCOUNT", "ACCOUNT.T_CODE_CURRENCY", "", "t_PlanRest" );

      condition = condition + " AND " + GetFilterMoneySQLCondition( FilterItems[FilterItemsCount], PlanRestField );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_TYPE_ACCOUNT ) // Тип счета

      condition = condition + TypeAccountFilterCondition( FilterItems[FilterItemsCount], "account.T_TYPE_ACCOUNT" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_USERTYPEACCOUNT ) // Пользовательский тип счета

      condition = condition + TypeAccountFilterCondition( FilterItems[FilterItemsCount], "account.T_USERTYPEACCOUNT" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_DEPARTMENT ) // Филиал

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Equal )

        if( FilterItems[FilterItemsCount].IsNot )
          conditionNOT = " NOT ";
        else
          conditionNOT = " ";
        end;

        ValueCount = 1;

        condition_temp = " account.T_DEPARTMENT = " + string(FilterItems[FilterItemsCount].Value[0].Code);

        while( ValueCount < FilterItems[FilterItemsCount].Value.Size )

          condition_temp = condition_temp + " OR account.T_DEPARTMENT = " + string(FilterItems[FilterItemsCount].Value[ValueCount].Code);

          ValueCount = ValueCount + 1;

        end;

        condition = condition + " AND " + conditionNOT + " ( " + condition_temp + " ) ";

      end;

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_BRANCH ) // Подразделение ТС

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Equal )

        if( FilterItems[FilterItemsCount].IsNot )
          conditionNOT = " NOT ";
        else
          conditionNOT = " ";
        end;

        ValueCount = 1;

        condition_temp = " account.T_BRANCH = " + string(FilterItems[FilterItemsCount].Value[0].Code);

        while( ValueCount < FilterItems[FilterItemsCount].Value.Size )

          condition_temp = condition_temp + " OR account.T_BRANCH = " + string(FilterItems[FilterItemsCount].Value[ValueCount].Code);

          ValueCount = ValueCount + 1;

        end;

        condition = condition + " AND " + conditionNOT + " ( " + condition_temp + " ) ";

      end;

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_OPEN_DATE ) // Дата открытия счета

      condition = condition + " AND " + GetFilterDateSQLCondition( FilterItems[FilterItemsCount], "account.T_OPEN_DATE" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_CLOSE_DATE ) // Дата закрытия счета

      condition = condition + " AND " + GetFilterDateSQLCondition( FilterItems[FilterItemsCount], "account.T_CLOSE_DATE" );

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_SYMBOL ) // Символ отчетности

        condition = condition + " AND " + GetFilterStringSQLCondition( FilterItems[FilterItemsCount], "account.T_SYMBOL", true, false );

    end;


    if( FilterItems[FilterItemsCount].Id == FILTERITEM_DATE_CARRY ) // Дата последней проводки

      var LastDateCarryField = "";

      LastDateCarryField = LastDateCarryField + "(";
      LastDateCarryField = LastDateCarryField + "SELECT NVL(MAX(acctrn.t_date_carry), ";
      LastDateCarryField = LastDateCarryField + GetSQLDate( date(0, 0, 0) ) + ") ";
      LastDateCarryField = LastDateCarryField + "FROM dacctrn_dbt acctrn ";
      LastDateCarryField = LastDateCarryField + "WHERE acctrn.t_accountid_payer = account.t_accountid OR acctrn.t_accountid_receiver = account.t_accountid";
      LastDateCarryField = LastDateCarryField + ")";

      condition = condition + " AND " + GetFilterDateSQLCondition( FilterItems[FilterItemsCount], LastDateCarryField );

    end;

	if( FilterItems[FilterItemsCount].Id == FILTERITEM_CHAPTERSYMBOL ) // Символ главы счета
	
      if((FilterItems[FilterItemsCount].Value != null) AND (FilterItems[FilterItemsCount].Value.Size > 0))
	  
	    if( FilterItems[FilterItemsCount].IsNot )
		
          condition = condition + " AND account.T_CHAPTER IN (  ";
		  
		  condition = condition + " SELECT OBCHAPTR.T_CHAPTER  FROM DOBCHAPTR_DBT OBCHAPTR WHERE OBCHAPTR.T_CHAPTER NOT IN ('";
		  
		else
		
	      condition = condition + " AND account.T_CHAPTER IN (  ";
		  
		  condition = condition + " SELECT OBCHAPTR.T_CHAPTER  FROM DOBCHAPTR_DBT OBCHAPTR WHERE OBCHAPTR.T_CHAPTER  IN ('";
		
		end;

        ValueCount = 0;
		
        while( ValueCount < FilterItems[FilterItemsCount].Value.Size )

	      condition = condition + FilterItems[FilterItemsCount].Value[ValueCount].Chapter;
		  
          ValueCount = ValueCount + 1;	
		  
	      if((ValueCount > 0) AND (ValueCount < FilterItems[FilterItemsCount].Value.Size))
			
		    condition = condition + "' , '";
			  
	      end;

        end;
		
		condition = condition + "' )) ";

      end;
 	  
	end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_BANKRUPT_STATUS ) // Статус банкротства физлица
      
      var f = FilterConditionItem();
      
      f.id        = FilterItems[FilterItemsCount].Id;
      f.Condition = FilterItems[FilterItemsCount].Condition;
      f.IsNot     = FilterItems[FilterItemsCount].IsNot;
      f.ValueType = FilterItems[FilterItemsCount].ValueType;      
      
      f.Value     = TArray();
      f.Value[0]  = FilterItems[FilterItemsCount].Value[0].Element;
      
      condition = condition + " AND " + GetFilterIntegerSQLCondition(
                                            f, 
                                            " RSI_RSBPARTY.GetPartyBankruptStatus( account.T_CLIENT,  (SELECT T_LEGALFORM FROM dparty_dbt WHERE T_PARTYID = account.T_CLIENT), "+GetSQLDate(Date)+")" );
    end;
    
    FilterItemsCount = FilterItemsCount + 1;

  end;

  return condition;

end;

private macro GetQuery( filteritems, orderitems, SortArray, Chapter )

  var where_str : string, from_str : string;

  var query = " SELECT account.T_ACCOUNTID, "
			  " (      	SELECT T_SYMBOL 	"
			  "	   		FROM DOBCHAPTR_DBT 	"
			  "	   		WHERE T_CHAPTER = 	"
			  "			account.T_CHAPTER	"
			  " ) CHAPTERSYMBOL, 			"
			  " CASE  						"
			  "   		WHEN account.T_OPEN_CLOSE = 'З' THEN 'X'"
			  "   		ELSE ''									"
			  "	END	ISCLOSED,									"
              "        account.T_OPEN_CLOSE, "
              "        account.T_ACCOUNT, "
              "        account.T_CODE_CURRENCY, "
              "        account.T_CHAPTER, "
              "        account.T_DEPARTMENT, "
              "        account.T_BRANCH, "
              "        account.T_NAMEACCOUNT, "
              "        account.T_CLIENT, "
              "        account.T_BALANCE, "
              "        account.T_KIND_ACCOUNT, "
              "        account.T_CURRENCYEQ, "
              "        account.T_TYPE_ACCOUNT, "
              "        account.T_OPEN_DATE, "
              "        account.T_CLOSE_DATE ";

  var sql_FROM = "FROM daccount_dbt account ";
  
  var sql_WHERE = "WHERE 1=1";

  if(Chapter!=0)
  
    sql_WHERE = sql_WHERE + " AND account.T_CHAPTER =  " + Chapter +" ";
	
  end;

  if( orderitems != null )
    
    var i : integer = 0;
    
    while( i < orderitems.size() )
      
      if( orderitems[i].column == "Department" )

        WebDprt_GetQueryParam( @query, @sql_FROM, @sql_WHERE, ALIAS_DEPART, "account.T_DEPARTMENT" );

        SortArray[SORT_DEPART] = true;

      end;

      if( orderitems[i].column == "Branch" )

        WebDprt_GetQueryParam( @query, @sql_FROM, @sql_WHERE, ALIAS_BRANCH, "account.T_BRANCH" );

        SortArray[SORT_BRANCH] = true;

      end;

      if( orderitems[i].column == "Currency" )

        WebFI_GetQueryParam( @query, @sql_FROM, @sql_WHERE, ALIAS_FI, "account.T_CODE_CURRENCY" );

        SortArray[SORT_FI] = true;

      end;

      if( orderitems[i].column == "CurrencyEQ" )

        WebFI_GetQueryParam( @query, @sql_FROM, @sql_WHERE, ALIAS_FI_EQ, "account.T_CURRENCYEQ" );

        SortArray[SORT_FI_EQ] = true;

      end;

      if( orderitems[i].column == "Client" )
    
        WebParty_GetQueryParam( @query, @sql_FROM, @sql_WHERE, ALIAS_CLIENT, "account.T_CLIENT", PTCK_CONTR );

        SortArray[SORT_CLIENT] = true;

      end;

      if( orderitems[i].column == "Balance" )
    
        WebBalance_GetQueryParam( @query, @sql_FROM, @sql_WHERE, ALIAS_BALANCE, "account.T_BALANCE", "account.T_CHAPTER", "0" );

        SortArray[SORT_BALANCE] = true;

      end;

      if( orderitems[i].column == "RestNat" )

        WebScroll_AddSelect( @query, GetRest("account", string(NATCUR), "RESTNAT") );

        SortArray[SORT_REST_NC] = true;

      end;

      if( orderitems[i].column == "RestCur" )

        WebScroll_AddSelect( @query, GetRest("account", "account.T_CODE_CURRENCY", "RESTCUR") );

        SortArray[SORT_REST_FIID] = true;

      end;

      if( orderitems[i].column == "RestEqv")

        WebScroll_AddSelect( @query, GetRestEqv("account", "RESTEQV") );

        SortArray[SORT_REST_EQV] = true;
      
      end;           
      i = i + 1;
    end;
  end;

  if( GetObjectRestriction(where_str, PRIV_ACCOUNT_SCROL, null, "account", null, "account.dbt", @from_str, true) == true )

    if(from_str != "")
      sql_FROM  = sql_FROM + ", " + from_str;
    end;

    if(where_str != "")
      sql_WHERE = sql_WHERE + " AND " + where_str;
    end;

  end;

  var strFilterWhere = GetAddWhereCondition( filteritems );

  if( strFilterWhere != "" )
    sql_WHERE = sql_WHERE + " " + strFilterWhere;
  end;

  query = query + " " + sql_FROM + " " + sql_WHERE;
  
  return query;

end;

/****************************************************************************************************
  Сервис получения списка лицевых счетов
*****************************************************************************************************/
macro WebCore_GetAccountGridItemList(PageSize, PageNum, Chapter, filteritems, orderitems)

  var cmd, rs, p;

  var query_select = "", query_from = "", query_where = "";

  var TypeAccount : string;

  var Arr = TArray;

  var SortArray = TArray(SORT_COUNT_ALL);

  var FullQuery : string;

  var Query = GetQuery( filteritems, orderitems, SortArray, Chapter);

  Query = Query + " ORDER BY " + GetSortStr(orderitems,"account.T_SORT, account.T_ACCOUNTID","");

  if( (SortArray[SORT_DEPART] == null) or (SortArray[SORT_DEPART] == false) )
    
    WebDprt_GetQueryParam( @query_select, @query_from, @query_where, ALIAS_DEPART, "t.T_DEPARTMENT" );

  end;

  if( (SortArray[SORT_BRANCH] == null) or (SortArray[SORT_BRANCH] == false) )
    
    WebDprt_GetQueryParam( @query_select, @query_from, @query_where, ALIAS_BRANCH, "t.T_BRANCH" );

  end;

  if( (SortArray[SORT_FI] == null) or (SortArray[SORT_FI] == false) )
    
    WebFI_GetQueryParam( @query_select, @query_from, @query_where, ALIAS_FI, "t.T_CODE_CURRENCY" );

  end;

  if( (SortArray[SORT_FI_EQ] == null) or (SortArray[SORT_FI_EQ] == false) )
    
    WebFI_GetQueryParam( @query_select, @query_from, @query_where, ALIAS_FI_EQ, "t.T_CURRENCYEQ" );

  end;

  if( (SortArray[SORT_CLIENT] == null) or (SortArray[SORT_CLIENT] == false) )
    
    WebParty_GetQueryParam( @query_select, @query_from, @query_where, ALIAS_CLIENT, "t.T_CLIENT", PTCK_CONTR );

  end;

  if( (SortArray[SORT_BALANCE] == null) or (SortArray[SORT_BALANCE] == false) )
    
    WebBalance_GetQueryParam( @query_select, @query_from, @query_where, ALIAS_BALANCE, "t.T_BALANCE", "t.T_CHAPTER", "0" );

  end;

  if( (SortArray[SORT_REST_NC] == null) or (SortArray[SORT_REST_NC] == false) )

    WebScroll_AddSelect( @query_select, GetRest("t", string(NATCUR), "RESTNAT") );

  end;

  if( (SortArray[SORT_REST_FIID] == null) or (SortArray[SORT_REST_FIID] == false) )

    WebScroll_AddSelect( @query_select, GetRest("t", "t.T_CODE_CURRENCY", "RESTCUR") );

  end;

  if( (SortArray[SORT_REST_EQV] == null) or (SortArray[SORT_REST_EQV] == false) )

    WebScroll_AddSelect( @query_select, GetRestEqv("t", "RESTEQV") );

  end;

  FullQuery = WebScroll_CreatePageSQLString( PageSize, PageNum, Query, query_select, query_from, query_where );

  cmd = RsdCommand(FullQuery);
  
  rs = RsdRecordset(cmd);

  while(rs.movenext)

    p = TWebAccountGridItem();

    p.AccountID    = SQL_ConvType(rs.value("T_ACCOUNTID"   ));
	p.ChapterSymbol= SQL_ConvType(rs.value("CHAPTERSYMBOL"  ));
	p.IsClosed     = ConvertCharToBool(SQL_ConvType(rs.value("ISCLOSED" )));
    p.Open_Close   = SQL_ConvType(rs.value("T_OPEN_CLOSE"  ));
    p.Account      = SQL_ConvType(rs.value("T_ACCOUNT"     ));
    p.Chapter      = SQL_ConvType(rs.value("T_CHAPTER"     ));
    p.NameAccount  = SQL_ConvType(rs.value("T_NAMEACCOUNT" ));
    p.Kind_Account = SQL_ConvType(rs.value("T_KIND_ACCOUNT"));
    p.Open_Date    = SQL_ConvType(rs.value("T_OPEN_DATE"   ));
    p.Close_Date   = SQL_ConvType(rs.value("T_CLOSE_DATE"  ));

    p.Department = WebDprt_GetObjectFromRecordset( ALIAS_DEPART, rs );
    p.Branch     = WebDprt_GetObjectFromRecordset( ALIAS_BRANCH, rs );

    p.Currency   = WebFI_GetObjectFromRecordset( ALIAS_FI, rs );

    p.Client     = WebParty_GetObjectFromRecordset( ALIAS_CLIENT, rs );

    p.Balance    = WebBalance_GetObjectFromRecordset( ALIAS_BALANCE, rs );

    p.RestNat    = SQL_ConvType(rs.value("RESTNAT"));
    p.RestCur    = SQL_ConvType(rs.value("RESTCUR"));

    TypeAccount = SQL_ConvType(rs.value("T_TYPE_ACCOUNT"));

    if( Index(TypeAccount, "В") == 0 )

      p.CurrencyEQ = null;
      p.RestEqv    = null;

    else

      p.CurrencyEQ = WebFI_GetObjectFromRecordset( ALIAS_FI_EQ, rs );
      p.RestEqv    = SQL_ConvType(rs.value("RESTEQV"));

    end;

    Arr[Arr.size] = p;

    p = null;

  end;

  return Arr;

end;

/****************************************************************************************************
  Сервис получения количества записей в списке лицевых счетов
*****************************************************************************************************/
macro WebCore_GetAccountGridItemCount(Chapter, filteritems)

  var res : integer = 0;

  var cmd, rs;

  var query = " SELECT count(*) c FROM ( ";

  query = query + GetQuery(filteritems, null, null, Chapter);

  query = query + " ) ";

  cmd = RsdCommand(query);

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    res = rs.value("c");

  end;

  return res;

end;

macro WebCore_GetChapter(Chapter)

  var cmd, rs;

  var Rec = TRecHandler("obchaptr");
  var p;

  var query = " SELECT * FROM DOBCHAPTR_DBT WHERE T_CHAPTER = ? ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, Chapter );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    _CopyRecordsetToRecHandler(rs, Rec);

    p = TRecHandler("obchaptr");
    copy(p, Rec);

  end;

  return p.rec;

end;

macro WebCore_CloseAccount(Chapter, Currency, Account, CloseDate)

  var res = True;

  var Error = "";

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  var stat = CB_CloseAccount( Chapter, Currency, Account, CloseDate, Error);

  if(stat != 0)

    res = False;

  end;

  if(res != True)

    AddErrorMessageExt(@ErrorMessage, stat, Error);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_CloseAccountList( AccountIDList : TArray, CloseDate : date, ReportName : string )

  file account("account.dbt") key 1;

  var i : integer;
  
  var AccCount = AccountIDList.Size;

  var FullFilePath = GetTxtFileName(ReportName);

  SetOutput(FullFilePath);

  PrintProtocolHeader( "Протокол закрытия лицевых счетов" );

  i = 0;

  while( i < AccCount )

    var stat = 0;

    var ResultMessage = "";

    ClearRecord(account);

    account.AccountID = AccountIDList[i];

    if( GetEQ(account) == false )

      ResultMessage = "Счет не найден";

    else

      stat = CB_CloseAccount( account.Chapter, account.Code_Currency, account.Account, CloseDate, ResultMessage );

      if( stat == 0 ) ResultMessage = "Счет закрыт успешно"; end;

    end;

    PrintProtocolLine( account.AccountID, account.Account, stat, ResultMessage );

    i = i + 1;

  end;

  PrintProtocolFooter();

  SetOutput();

  FullFilePath = ConvertTxt2Html(FullFilePath);

  return GetTextFile(FullFilePath);

end;


macro WebCore_OpenClosedAccount(Chapter, Currency, Account)

  var res = True;

  var Error = "";

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  var stat = CB_OpenClosedAccount( Chapter, Currency, Account, Error);

  if(stat != 0)

    res = False;

  end;

  if(res != True)

    AddErrorMessageExt(@ErrorMessage, stat, Error);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_OpenClosedAccountList( AccountIDList : TArray, ReportName : string )

  file account("account.dbt") key 1;

  var i : integer;
  
  var AccCount = AccountIDList.Size;

  var FullFilePath = GetTxtFileName(ReportName);

  SetOutput(FullFilePath);

  PrintProtocolHeader( "Протокол открытия закрытых лицевых счетов" );

  i = 0;

  while( i < AccCount )

    var stat = 0;

    var ResultMessage = "";

    ClearRecord(account);

    account.AccountID = AccountIDList[i];

    if( GetEQ(account) == false )

      ResultMessage = "Счет не найден";

    else

      stat = CB_OpenClosedAccount( account.Chapter, account.Code_Currency, account.Account, ResultMessage );

      if( stat == 0 ) ResultMessage = "Закрытый счет открыт успешно"; end;

    end;

    PrintProtocolLine( account.AccountID, account.Account, stat, ResultMessage );

    i = i + 1;

  end;

  PrintProtocolFooter();

  SetOutput();

  FullFilePath = ConvertTxt2Html(FullFilePath);

  return GetTextFile(FullFilePath);

end;

macro WebCore_DeleteAccount( AccountID )

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  var stat = Delete_Account( AccountID );

  if(stat != 0)

    res = False;

  end;

  if(res != True)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_DeleteAccountList( AccountIDList : TArray, ReportName : string )

  file account("account.dbt") key 1;

  var i : integer;
  
  var AccCount = AccountIDList.Size;

  var FullFilePath = GetTxtFileName(ReportName);

  SetOutput(FullFilePath);

  PrintProtocolHeader( "Протокол удаления закрытых лицевых счетов" );

  i = 0;

  while( i < AccCount )

    var stat = 0;

    var ResultMessage = "";

    ClearRecord(account);

    account.AccountID = AccountIDList[i];

    if( GetEQ(account) == false )

      ResultMessage = "Счет не найден";

    else

      if( account.Open_Close != "З" )

        ResultMessage = "Нельзя удалять открытый лицевой счет";

      else
      
        stat = Delete_Account( account.AccountID );

        if( stat == 0 )
          ResultMessage = "Закрытый счет удален успешно";
        else
          ResultMessage = GetErrMsg();
        end;

      end;

    end;

    PrintProtocolLine( account.AccountID, account.Account, stat, ResultMessage );

    i = i + 1;

  end;

  PrintProtocolFooter();

  SetOutput();

  FullFilePath = ConvertTxt2Html(FullFilePath);

  return GetTextFile(FullFilePath);

end;

private macro GetStrPeriod(month, year)

  var StrPeriod = "";

  var cmd, rs;

  var query = " SELECT T_SZNAMEALG "
              "  FROM DNAMEALG_DBT "
              " WHERE T_ITYPEALG = 3440 AND T_INUMBERALG = ? ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, month );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    StrPeriod = rs.value("T_SZNAMEALG") + " " + string(year);

  end;

  return StrPeriod;

end;

private macro GetRestTurnMonthGridItemList(AccountID, Chapter, Account, FIid, RDate)

  var obj;

  var cmd, rs;

  var month, year;

  DateSplit(RDate, NULL, month, year);

  var query =  " SELECT "
               "  NVL(SUM(t_debet), 0) SD"
               " ,NVL(SUM(t_credit), 0) SC"
               " ,NVL(SUM(t_debetspod), 0) SDS"
               " ,NVL(SUM(t_creditspod), 0) SCS"
               "  FROM drestdate_dbt "
               " WHERE t_AccountID    = ? "
               "   AND t_RestCurrency = ? "
               "   AND EXTRACT(MONTH FROM t_RestDate) = ? "
               "   AND EXTRACT(YEAR  FROM t_RestDate) = ? ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, AccountID );
  cmd.addParam( "", RSDBP_IN, FIid );
  cmd.addParam( "", RSDBP_IN, month );
  cmd.addParam( "", RSDBP_IN, year );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    var RestDate = date(GetDaysInMonth(RDate), month, year);

    obj = TWebAccRestTurnGridItem;

    obj.RestDate   = RestDate;
    obj.Month      = GetStrPeriod(month, year);
    obj.Rest       = RestA(Account, RestDate, NULL, Chapter, FIid, FIid);
    obj.Debet      = rs.value("SD");
    obj.Credit     = rs.value("SC");
    obj.DebetSPOD  = rs.value("SDS");
    obj.CreditSPOD = rs.value("SCS");

  end;

  return obj;

end;

macro WebCore_GetRestTurnMonthGridItemList(PageSize, PageNum, AccountID, Chapter, Account, FIid, BeginYear, EndYear)

  var cmd, rs;

  var Arr = TArray;

  var BeginDate = date(1, 1, BeginYear);
  var EndDate   = date(31, 12, EndYear);

  var query = " SELECT t.* FROM ( SELECT rownum RN, tt.* FROM ( ";

  query = query + " SELECT DISTINCT TO_DATE('01.' || TO_CHAR(t_RestDate, 'MM.YYYY'), 'DD.MM.YYYY') RDATE "
              "  FROM drestdate_dbt "
              " WHERE t_AccountID = ? "
              "   AND t_RestCurrency = ? "
              "   AND t_RestDate BETWEEN ? AND ? "
              " ORDER BY TO_DATE('01.' || TO_CHAR(t_RestDate, 'MM.YYYY'), 'DD.MM.YYYY') ";

  if( PageSize > 0 )
    query = query + " ) tt WHERE ROWNUM <= " + string(PageSize*(PageNum + 1)) + " ) t WHERE t.RN >= " + string(PageSize*(PageNum) + 1) + " ";
  else
    query = query + " ) tt ) t ";
  end;              

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, AccountID );
  cmd.addParam( "", RSDBP_IN, FIid );
  cmd.addParam( "", RSDBP_IN, BeginDate );
  cmd.addParam( "", RSDBP_IN, EndDate );

  rs = RsdRecordset(cmd);

  while(rs.movenext)

    var RestDate : Date = rs.value("RDATE");

    Arr[Arr.size] = GetRestTurnMonthGridItemList(AccountID, Chapter, Account, FIid, RestDate);

  end;

  return Arr;

end;

macro WebCore_GetRestTurnMonthGridItemCount(AccountID, FIid, BeginYear, EndYear)

  var cmd, rs;

  var Count = 0;

  var BeginDate = date(1, 1, BeginYear);
  var EndDate   = date(31, 12, EndYear);

  var query = " SELECT DISTINCT TO_DATE('01.' || TO_CHAR(t_RestDate, 'MM.YYYY'), 'DD.MM.YYYY') RDATE "
              "  FROM drestdate_dbt "
              " WHERE t_AccountID = ? "
              "   AND t_RestCurrency = ? "
              "   AND t_RestDate BETWEEN ? AND ? "
              " ORDER BY TO_DATE('01.' || TO_CHAR(t_RestDate, 'MM.YYYY'), 'DD.MM.YYYY') ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, AccountID );
  cmd.addParam( "", RSDBP_IN, FIid );
  cmd.addParam( "", RSDBP_IN, BeginDate );
  cmd.addParam( "", RSDBP_IN, EndDate );

  rs = RsdRecordset(cmd);

  while(rs.movenext)

    Count = Count + 1;

  end;

  return Count;

end;

/*
 * Сформировать заголовок списка остатков и оборотов
 *
 */
macro WebCore_GetRestTurnTitle(AccountID, RestFIID, IsRestEqv)

  file account("account.dbt") key 1;
  
  var Title = " Лицевой счет ";

  ClearRecord(account);
  account.AccountID = AccountID;

  if( GetEQ(account) )
    Title = Title + account.Account;
  end;

  Title = Title + ". Остатки и обороты в ";

  Title = Title + GetFIName(RestFIID);

  Title = Title + ".";

  return Title;

end;

macro WebCore_GetRestTurnGridItemList(PageSize, PageNum, AccountID, Account, IsRestEqv, Code_Currency, filteritems, orderitems)
  var cmd,rs;
  /*PageSize, PageNum, filteritems, orderitems are not used, should be implemented later*/
  var Arr = TArray;
  var query;
  
  if (IsRestEqv == false)
    query =     " SELECT  restdate.T_ACCOUNTID,     "
                "         restdate.T_RESTCURRENCY,  "
                "         restdate.T_RESTDATE,      "
                "         restdate.T_REST,          "
                "         restdate.T_PLANREST,      "
                "         restdate.T_DEBET,         "
                "         restdate.T_CREDIT,        "
                "         restdate.T_DEBETSPOD,     "
                "         restdate.T_CREDITSPOD     "
                " FROM drestdate_dbt restdate       "
                " WHERE restdate.T_ACCOUNTID = ?    "
                "   AND restdate.T_RESTCURRENCY = ? "
                " ORDER BY restdate.T_RESTDATE      ";
    
    cmd = RsdCommand(query);
    cmd.addParam( "", RSDBP_IN, AccountID );
    cmd.addParam( "", RSDBP_IN, Code_Currency );
  
    rs = RsdRecordset(cmd);
  
    while(rs.movenext)
      var item = TWebAccRestTurnGridItem;
      item.AccountID = rs.value("T_ACCOUNTID");
      item.RestCurrency = rs.value("T_RESTCURRENCY");
    /*item.Month = Doesn't fills with anything;*/
      item.RestDate = rs.value("T_RESTDATE");
      item.Rest = rs.value("T_REST");
      item.PlanRest = rs.value("T_PLANREST");
      item.Debet = rs.value("T_DEBET");
      item.Credit = rs.value("T_CREDIT");
      item.DebetSPOD = rs.value("T_DEBETSPOD");
      item.CreditSPOD = rs.value("T_CREDITSPOD");
      Arr[Arr.size] = item;

    end;  
    
  elif (IsRestEqv == true)
    query =     " SELECT  resteqv.T_ACCOUNTID,   "
                "         resteqv.T_RESTDATE,    " 
                "         resteqv.T_REST,        "
                "         resteqv.T_PLANREST,    "
                "         resteqv.T_DEBET,       "
                "         resteqv.T_CREDIT       "
                " FROM dresteqv_dbt resteqv      "
                " WHERE resteqv.T_ACCOUNTID = ?  "
                " ORDER BY resteqv.T_RESTDATE    ";
                
    cmd = RsdCommand(query);
    cmd.addParam( "", RSDBP_IN, AccountID );
  
    rs = RsdRecordset(cmd);
  
    while(rs.movenext)
      var items = TWebAccRestTurnGridItem;
      items.AccountID = rs.value("T_ACCOUNTID");
    /*items.RestCurrency = Doesn't fills with anything;*/
      items.RestDate = rs.value("T_RESTDATE");
    /*items.Month = Doesn't fills with anything;*/
      items.Rest = rs.value("T_REST");
      items.PlanRest = rs.value("T_PLANREST");
      items.Debet = rs.value("T_DEBET");
      items.Credit = rs.value("T_CREDIT");
    /*items.DebetSPOD = Doesn't fills with anything;*/
    /*items.CreditSPOD = Doesn't fills with anything;*/
      Arr[Arr.size] = items;

    end;              
  end;
  
  return Arr;
  
end;

macro WebCore_GetRestTurnGridItemCount(AccountID, Account, IsRestEqv, Code_Currency,filteritems)
  /*filteritems are not used, should be implemented later*/

  var res : integer = 0;

  var cmd, rs;

  var query = " SELECT count(*) c FROM ( ";

  if (IsRestEqv == false)
    query = query+" SELECT  restdate.*                "
                  " FROM drestdate_dbt restdate       "
                  " WHERE restdate.T_ACCOUNTID = ?    "
                  "   AND restdate.T_RESTCURRENCY = ? "
                  " ORDER BY restdate.T_RESTDATE      ";
    query = query + " ) ";
    cmd = RsdCommand(query);
    cmd.addParam( "", RSDBP_IN, AccountID );
    cmd.addParam( "", RSDBP_IN, Code_Currency );
    
    
  elif (IsRestEqv == true)
    query = query+" SELECT  resteqv.*             "
                  " FROM dresteqv_dbt resteqv     "
                  " WHERE resteqv.T_ACCOUNTID = ? "
                  " ORDER BY resteqv.T_RESTDATE   ";
    query = query + " ) ";           
    cmd = RsdCommand(query);
    cmd.addParam( "", RSDBP_IN, AccountID );
  end;

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    res = rs.value("c");

  end;

  return res;
end;/*
macro SetAccRestTurnObject(restDateRec,RestDateItem,IsRestEqv)
  if (RestDateItem.AccountID != NULL)     restDateRec.AccountID = RestDateItem.AccountID;       end;
  if (RestDateItem.RestDate != NULL)      restDateRec.RestDate = RestDateItem.RestDate;         end;
//if (RestDateItem.Month != NULL)         restDateRec.Month = RestDateItem.Month;               end;
  if (RestDateItem.Rest != NULL)          restDateRec.Rest = RestDateItem.Rest;                 end;
  if (RestDateItem.PlanRest != NULL)      restDateRec.PlanRest = RestDateItem.PlanRest;         end;
  if (RestDateItem.Debet != NULL)         restDateRec.Debet = RestDateItem.Debet;               end;
  if (IsRestEqv == false)
    if (RestDateItem.RestCurrency != NULL)  restDateRec.RestCurrency = RestDateItem.RestCurrency; end;
    if (RestDateItem.Credit != NULL)        restDateRec.Credit = RestDateItem.Credit;             end;
    if (RestDateItem.DebetSPOD != NULL)     restDateRec.DebetSPOD = RestDateItem.DebetSPOD;       end;
    if (RestDateItem.CreditSPOD != NULL)    restDateRec.CreditSPOD = RestDateItem.CreditSPOD;     end;
  end;
  
end;
macro WebCore_InsertAccRestTurn (RestDateItem:TWebAccRestTurnGridItem, IsRestEqv:bool)
  file AccRestTurn("restdate.dbt");
  var res = True;
  
  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  var restDateRec = TWebAccRestTurnGridItem;  
  SetAccRestTurnObject(restDateRec,RestDateItem);
  
  var ErrMsg = "";
  
  res = CB_InsertAccRest_RSL(RestDateItem, IsRestEqv);
  
  if((res != 0))
    ErrorMessage.Code     = res;
    ErrorMessage.Message  = ErrMsg;
    ErrorMessage.Severity = MessageSeverity.RuntimeError;
    ResponseBuilder.AddMessage( ErrorMessage );
  end;
  
  return responseBuilder.Result;
end;*/

macro AccountScroll_IsStandalone( )
   return true; // в WEB нет 3х звенки
end;


macro WebCore_CreateAccountStatement(AccountID, BeginPeriodDate, EndPeriodDate, RepFormatID ) 
  var fc = null;
  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  ReplaceMacro ( "IsStandalone", "AccountScroll_IsStandalone" );

  var ReportFileName =  GetTxtFileName("\ordacc_"+AccountID);
  var Status=createAccountStatement(AccountID, BeginPeriodDate, EndPeriodDate, ReportFileName, RepFormatID);
  if(Status!=0)
    AddErrorMessageExt(@ErrorMessage, GetErrMsg());
    res = false;
  end;

  ReplaceMacro ( "IsStandalone", NULL );

  if( ReportFileName != "" )
  
    if( RepFormatID == 0 )
      fc = ConvertTxt2Html(ReportFileName);
      fc = GetTextFile(fc);
    else
      fc = CreateFileContainer(ReportFileName);
    end;

  end;

  if(res != true)
    AddErrorMessage(@ErrorMessage);
  end;
  responseBuilder.SetUserObject(fc);
  return responseBuilder.Result;
end;





macro WebCore_Get_AnlAccSubGridItemList(Chapter, Code_Currency, Account):TArray

  var result = TArray();
   
  var query = " SELECT *  FROM DACCVANL_DBT  accvanl "
                          "     WHERE accvanl.T_CHAPTER = ? AND      "
                          "     accvanl.T_FIID = ? AND                           "
                          "     accvanl.T_ACCOUNT = ?                            ";
    
  var  cmd = RsdCommand(query);
  
  cmd.addParam( "", RSDBP_IN, Chapter );
  
  cmd.addParam( "", RSDBP_IN, Code_Currency );
  
  cmd.addParam( "", RSDBP_IN, Account );
  
  var rs = RsdRecordset(cmd);

  while(rs.movenext)
  
    result[result.size]=rs.value("T_AccanaliticsID"); 
  
  end;
  
  var AnlAccSubGridItemArray = TArray();
  
  var i =0;

  while (i < result.size())
  
    var AnlAccSubGridItem = TWebAnlAccSubGridItem();
                        
        var query_inside2 = " SELECT anl.T_SHORTNAME, vanl.T_AccanaliticsID, vanl.T_ANALITICSID "
                                                " FROM DACCANL_DBT anl, DACCVANL_DBT vanl               "
                                                " WHERE anl.T_ANALITICSID = vanl.T_ANALITICSID  "
                                                " AND vanl.T_CHAPTER = ?                                    "
                                                " AND vanl.T_FIID = ?                               "
                                                " AND vanl.T_ACCOUNT = ?                                "
                                                " AND vanl.T_AccanaliticsID = ?                     ";
    
    var  cmd_inside2 = RsdCommand(query_inside2);
  
    cmd_inside2.addParam( "", RSDBP_IN, Chapter );
        
        cmd_inside2.addParam( "", RSDBP_IN, Code_Currency );
        
        cmd_inside2.addParam( "", RSDBP_IN, Account );
        
        cmd_inside2.addParam( "", RSDBP_IN, result[i] );
         
    var rs_inside2 = RsdRecordset(cmd_inside2);
        
        if(rs_inside2.movenext)
        
          AnlAccSubGridItem.AnlNameAccSubCode = rs_inside2.value("T_SHORTNAME");
          
          AnlAccSubGridItem.AccanaliticsID = rs_inside2.value("T_AccanaliticsID");
          
          AnlAccSubGridItem.Analiticsid = rs_inside2.value("T_ANALITICSID");
                
        end;
        
        AnlAccSubGridItem.Children = TArray();
                
        var query_inside1 = " SELECT *  FROM DACCSUB_DBT  accsub   "
                                            " WHERE accsub.T_ACCANALITICSID = ?    ";
    
    var  cmd_inside1 = RsdCommand(query_inside1);
  
    cmd_inside1.addParam( "", RSDBP_IN, result[i]);
   
    var rs_inside1 = RsdRecordset(cmd_inside1);
        
        while(rs_inside1.movenext)
        
          var AnlAccSubGridItemInside = TWebAnlAccSubGridItem();
          
          AnlAccSubGridItemInside.AccanaliticsID = rs_inside1.value("T_ACCANALITICSID");
        
          AnlAccSubGridItemInside.SubAccountID = rs_inside1.value("T_SUBACCOUNTID");
          
          AnlAccSubGridItemInside.AnlNameAccSubCode = rs_inside1.value("T_SUBACCOUNTCODE");  
          
          AnlAccSubGridItemInside.Analiticsid = rs_inside1.value("T_ANALITICSID");  
                                
          var query_inside3 = " SELECT T_SZNAMEALG Status                        "
                                                " FROM DNAMEALG_DBT nl, DACCSUB_DBT accs "
                                                " WHERE     nl.T_ITYPEALG = 2102                 "
                                                " AND nl.T_INUMBERALG = accs.T_STATUS    "
                                                " AND accs.T_ACCANALITICSID = ?          "
                                                " AND accs.T_SUBACCOUNTID = ?                    ";
    
      var  cmd_inside3 = RsdCommand(query_inside3);
  
      cmd_inside3.addParam( "", RSDBP_IN, result[i] );
        
          cmd_inside3.addParam( "", RSDBP_IN, rs_inside1.value("T_SUBACCOUNTID") );     
         
      var rs_inside3 = RsdRecordset(cmd_inside3);
        
          if(rs_inside3.movenext)
        
            AnlAccSubGridItemInside.Status = rs_inside3.value("Status");
                
          end;
        
          var query_inside4 = " SELECT acsrd.T_REST  Rest         "
                                                " FROM DACCSUBRD_DBT acsrd      "
                                                " WHERE acsrd.T_DATE_CARRY = ?  "
                                                " AND acsrd.T_ACCANALITICSID = ?"
                                                " AND acsrd.T_SUBACCOUNTID = ?  ";
    
      var  cmd_inside4 = RsdCommand(query_inside4);
  
      cmd_inside4.addParam( "", RSDBP_IN, {CurDate} );
        
          cmd_inside4.addParam( "", RSDBP_IN, result[i]);       
        
          cmd_inside4.addParam( "", RSDBP_IN,  rs_inside1.value("T_SUBACCOUNTID"));     
         
      var rs_inside4 = RsdRecordset(cmd_inside4);
        
          if(rs_inside4.movenext)
        
            AnlAccSubGridItemInside.Rest = rs_inside4.value("Rest");
                
          end;

          var query_inside5 = " SELECT *                                                                                           "
                                                " FROM DACCSUB_DBT asb                                                             "
                                                " WHERE ASB.T_ACCANALITICSID = ?                                           "
                                                " CONNECT BY PRIOR ASB.T_SUBACCOUNTID = ASB.T_PARENTID "
                                                " START WITH ASB.T_HAVECHILD = 'X'                                         ";
    
      var  cmd_inside5 = RsdCommand(query_inside5);
  
      cmd_inside5.addParam( "", RSDBP_IN, result[i]);
                 
      var rs_inside5 = RsdRecordset(cmd_inside5);
        
          /*while(rs_inside5.movenext)
        
            var AnlAccSubGridItemInside3 = TWebAnlAccSubGridItem();

            AnlAccSubGridItemInside3.AnlNameAccSubCode = rs_inside5.value("T_SUBACCOUNTCODE");
          
            AnlAccSubGridItemInside.Children = TArray();
          
            AnlAccSubGridItemInside.Children[AnlAccSubGridItemInside.Children.size] = AnlAccSubGridItemInside3;
                
                AnlAccSubGridItemInside3 = null;
                
          end;*/
                        
          AnlAccSubGridItem.Children[AnlAccSubGridItem.Children.size]=AnlAccSubGridItemInside;
          
          AnlAccSubGridItemInside = null;
          
        end;
        
        AnlAccSubGridItemArray[AnlAccSubGridItemArray.size]=AnlAccSubGridItem;
        
        i=i+1;
          
  end;
  
  return AnlAccSubGridItemArray;

end;

macro WebCore_AccANalitics (AccAnaliticsID): WebResponseBuilder

  responseBuilder =WebResponseBuilder();
    
  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  var res = CB_DeleteAcc_Analitic(AccAnaliticsID);
     
  if(res != 0)
   
     AddErrorMessage(@ErrorMessage);
  
  end;
    
  responseBuilder.SetUserObject(res);
  
  return responseBuilder.Result;

end;


macro WebCore_SubAccount(AccANaliticsID, SubAccountID): WebResponseBuilder

  Record  accsubRec ("accsub.dbt");
  
  responseBuilder =WebResponseBuilder();
    
  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  var res;
  
  var query_inside1 = " SELECT *  FROM DACCSUB_DBT  accsub   "
                                          " WHERE accsub.T_ACCANALITICSID = ? AND T_SUBACCOUNTID = ? ";
    
  var  cmd = RsdCommand(query_inside1);
  
  cmd.addParam( "", RSDBP_IN, AccANaliticsID);
  
  cmd.addParam( "", RSDBP_IN, SubAccountID);
   
  var rs = RsdRecordset(cmd);
        
  if(rs.movenext)
  
    accsubRec.AccAnaliticsID = rs.value("T_ACCANALITICSID");
        
        accsubRec.SubAccountID = rs.value("T_SUBACCOUNTID");
        
        accsubRec.SubAccountCode = rs.value("T_SUBACCOUNTCODE");
        
        accsubRec.ParentID = rs.value("T_PARENTID");
        
        accsubRec.HaveChild = rs.value("T_HAVECHILD");
        
        accsubRec.SpecialType = rs.value("T_SPECIALTYPE");
        
        accsubRec.Status = rs.value("T_STATUS");
       
        accsubRec.AnaliticsID = rs.value("T_ANALITICSID");

  end;

  if(accsubRec != null)
  
    res = CB_DeleteSubAcc(accsubRec);
  
  end;
     
  if(res != 0)
   
     AddErrorMessage(@ErrorMessage);
  
  end;
    
  responseBuilder.SetUserObject(res);
  
  return responseBuilder.Result;

end;

macro WebCore_InitAccStatementPeriodPanelObject() : TWebAccStatementPeriodPanelObject

  var result = TWebAccStatementPeriodPanelObject();
  
  var NameKindObj;
  
  result.BeginPeriodDate = {curdate};
  
  result.EndPeriodDate = {curdate};
  
  result.ListRepFormats = TArray();
  
  NameKindObj = TWbNameKind();
  
  NameKindObj.ID = 0;
  
  NameKindObj.Name = "Текстовый файл";
  
  result.ListRepFormats[0] = NameKindObj;
  
  NameKindObj = TWbNameKind();
  
  NameKindObj.ID = 1;
  
  NameKindObj.Name = "Таблица MS Excel";
  
  result.ListRepFormats[1] = NameKindObj;
  
  NameKindObj = TWbNameKind();
  
  NameKindObj.ID = 2;
  
  NameKindObj.Name = "Документ MS Word";
  
  result.ListRepFormats[2] = NameKindObj;
  
  result.SelectedRepFormat = SettingParm();
  
  return result;

end;







