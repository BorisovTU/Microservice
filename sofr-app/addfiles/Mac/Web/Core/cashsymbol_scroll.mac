/*
$Name:        cashsymbol_scroll.mac
$Module:      Главная книга
$Description: Макрос скроллинга распределения суммы проводки по кассовым символам
*/

IMPORT BankInter;
IMPORT "ws_validation.mac";
IMPORT "listsymb_widget.mac";
import "ws_datafilter.mac";

class TWebCashSymbolGridItem
  var RecID    : integer     ;    // Идентификатор записи     
  var Sum      : Money   = $0;    // Сумма                    
  var AcctrnID : integer     ;    // Идентификатор проводки   
  var Date     : date        ;    // Дата
  var ListSymb ;                  // TWebListSymb
  var Version  : integer     ;
  
  var SymbolType : integer;       // Вид символа
  var DocKind    : integer;       // Вид первичного документа
end;

class TWebCashSymbolGridParam
  var Review   : bool        ;    // Идентификатор записи     
  var Sum      : Money   = $0;    // Сумма                    
  var SymbDate : date        ;    // Дата символа
  var DocKind  : integer     ;    // Вид первичного документа
end;


PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;


macro WebCore_GetCashSymbolGridItemList(DocKind, SymbolKind, AcctrnID) : TArray
  var cmd, rs, query;

  var Arr = TArray;

  query = " SELECT * FROM dsymbcash_dbt WHERE t_DocKind = ? AND t_Kind = ? AND t_AcctrnID = ? ORDER BY t_DocKind, t_AcctrnID, t_Kind, t_RecID ";
  cmd = RsdCommand(query);
  cmd.addParam( "", RSDBP_IN, DocKind );
  cmd.addParam( "", RSDBP_IN, SymbolKind );
  cmd.addParam( "", RSDBP_IN, AcctrnID );
  rs = RsdRecordset(cmd);

  while(rs.movenext)
    var obj = TWebCashSymbolGridItem();
    obj.RecID     = SQL_ConvType(rs.value("t_RecID"));     
    obj.Sum       = SQL_ConvType(rs.value("t_Sum"));
    obj.AcctrnID  = SQL_ConvType(rs.value("t_AcctrnID"));
    obj.Date      = SQL_ConvType(rs.value("t_Date"));
    obj.ListSymb  = WebCore_CreateTWebMCCateg(SQL_ConvType(rs.value("t_DocKind")), SQL_ConvType(rs.value("t_Symbol")), NULL/*Name*/, SQL_ConvType(rs.value("t_Kind")));
    obj.Version   = SQL_ConvType(rs.value("t_Version"));

    obj.DocKind    = DocKind;
    obj.SymbolType = SymbolKind;

    Arr[Arr.size] = obj;
  end;

  return Arr;

end;


macro WebCore_GetCashSymbolGridParam(AcctrnID, SymbolKind) : TWebCashSymbolGridParam
  var cmd, rs, query;
  var Obj = TWebCashSymbolGridParam();

  var DocKind = 0;
  var review = false;
  var Sum = $0;
  var SymbDate = date(0, 0, 0);

  var stat = CB_GetSymbCashParam(AccTrnID, SymbolKind, @DocKind, @review, @Sum, @SymbDate);

  if(stat)
    Obj.Review   = Review  ;
    Obj.Sum      = Sum     ;
    Obj.DocKind  = DocKind ;
    Obj.SymbDate = SymbDate;
  end;

  return obj;
end;


private macro SetCashSymbolByObject(p, CashSymbolObject)
  p.RecID                = CashSymbolObject.RecID   ;
  p.Sum                  = CashSymbolObject.Sum     ;
  p.AcctrnID             = CashSymbolObject.AcctrnID;
  p.Date                 = CashSymbolObject.Date    ;
  p.Version              = CashSymbolObject.Version ;
  
  if(CashSymbolObject.ListSymb != null)
    p.DocKind              = CashSymbolObject.ListSymb.DocKind    ;
    p.Symbol               = CashSymbolObject.ListSymb.SymbCash   ;
    p.Kind                 = CashSymbolObject.ListSymb.SymbolType ;
  end;
end;


private macro SetObjectByCashSymbol(CashSymbolObject, p)
  CashSymbolObject.RecID                 = p.RecID   ;
  CashSymbolObject.Sum                   = p.Sum     ;
  CashSymbolObject.AcctrnID              = p.AcctrnID;
  CashSymbolObject.Date                  = p.Date    ;
  CashSymbolObject.Version               = p.Version ;

  CashSymbolObject.ListSymb = WebCore_CreateTWebMCCateg(p.DocKind, p.Symbol, NULL/*Name*/, p.Kind);
end;


macro WebCore_InsertCashSymbolGridItem(CashSymbolObject, AcctrnID, SymbolKind)   // TWebCashSymbolGridItem

  file p("symbcash.dbt");
  ClearRecord(p);
  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  SetCashSymbolByObject(p, CashSymbolObject);

  res = CB_InsertSYMBCASH(AcctrnID, SymbolKind, p);

  if(res != True)

    AddErrorMessage(@ErrorMessage);
  else
    SetObjectByCashSymbol(CashSymbolObject, p)
  end;

  responseBuilder.SetUserObject(CashSymbolObject);

  return responseBuilder.Result;

end;



macro WebCore_UpdateCashSymbolGridItem(CashSymbolObject, AcctrnID, SymbolKind)   // TWebCashSymbolGridItem

  file p("symbcash.dbt") key 2;
  ClearRecord(p);

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  p.RecID = CashSymbolObject.RecID;
  GetEQ(p);

  SetCashSymbolByObject(p, CashSymbolObject);

  res = CB_UpdateSYMBCASH(AcctrnID, SymbolKind, p);

  if(res != True)
    AddErrorMessage(@ErrorMessage);
  else
    SetObjectByCashSymbol(CashSymbolObject, p)
  end;

  responseBuilder.SetUserObject(CashSymbolObject);

  return responseBuilder.Result;

end;

macro WebCore_DeleteCashSymbolGridItem(RecID)

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = CB_DeleteSYMBCASH(RecID);

  if(res != True)
    AddErrorMessage(@ErrorMessage);
  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

private const SymbolTypeDGTC = 0;    // Тип кассового символа.

/*
     Фильтрация по типу кассового символа
 */
macro GetFilterOWNERSQLCondition( FilterItem) : string

  var SQLString = "";

  if( FilterItem.Condition == FilterCondition_Equal )

    if( FilterItem.Value.Size > 0 )

      var i = 0;

      SQLString = SQLString + " AND";

      if( FilterItem.IsNot )
        SQLString = SQLString + " NOT";
      end;

      SQLString = SQLString + " (";

      while( i < FilterItem.Value.Size )

        if( i > 0 )
          SQLString = SQLString + " OR ";
        end;
        
        SQLString = SQLString +"listsymb.T_TYPE_SYMBOL";

        SQLString = SQLString + " = ";

        SQLString = SQLString + string(FilterItem.Value[i]);
		

        i = i + 1;

      end;

      SQLString = SQLString + ") ";

    end;

  end;

  return SQLString;

end;


private macro GetAddWhereCondition(FilterItems,  DocKind): string

  var condition = "";

  if( DocKind==0 )
  
    condition = " WHERE 1 = 1";
   	
  end;
  
  var FilterItemsCount = 0;
  
  var ValueCount = 0;

  while( FilterItemsCount < FilterItems.Size )
   
	if( FilterItems[FilterItemsCount].Id == SymbolTypeDGTC ) // Владелец группы

      condition = condition + GetFilterOWNERSQLCondition( FilterItems[FilterItemsCount] );	
          
    end;

    FilterItemsCount = FilterItemsCount + 1;

  end;

  return condition;

end;

private macro GetQuery(DocKind)
		   
  var query = 	"  SELECT * FROM dlistsymb_dbt listsymb";
	  
  if((DocKind!=null) and( DocKind>0))
				   
    query = query + " WHERE t_DocKind = ? " ;
				  
  end;				  
				
  return query;

end;

macro WebCore_GetListSymbolGridItemList(PageSize, PageNum, FilterItems,DocKind) : TArray

  var cmd, rs, query;

  var Arr = TArray;

  query = GetQuery(DocKind);
  
  var strAddWhere = GetAddWhereCondition(filteritems, DocKind );
  
  if( strAddWhere != "" )
  
    query = query + strAddWhere;
	
  end;
  
  if(pagesize != 0)
    
    query = SQL_WrapSelect( query, pagenum, pagesize);
      
  end; 

  cmd = RsdCommand(query);
  
  cmd.addParam( "", RSDBP_IN, DocKind );

  rs = RsdRecordset(cmd);

  while(rs.movenext)
    var obj = TWebListSymb();
    obj.DocKind        = DocKind;     
    obj.SymbCash       = SQL_ConvType(rs.value("T_SYMB_CASH"));
    obj.Name  = SQL_ConvType(rs.value("T_NAME"));
    obj.SymbolType      = SQL_ConvType(rs.value("T_TYPE_SYMBOL"));
    Arr[Arr.size] = obj;
  end;

  return Arr;

end;

macro WebCore_GetListSymbolGridItemListCount(filteritems, DocKind)

  var res : integer = 0;

  var cmd, rs;

  var query = " SELECT count(*) c FROM ( ";

  query = query + GetQuery(DocKind);

  var strAddWhere = GetAddWhereCondition( filteritems,  DocKind );

  if( strAddWhere != "" )
  
    query = query + strAddWhere;
	
  end;

  query = query + " ) ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, DocKind );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    res = rs.value("c");

  end;

  return res;

end;

macro WebCore_UpdateListSymbol( IsInsert,  ListSymbObj, InitListSymbObj )  

  responseBuilder =WebResponseBuilder();
    
  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  Record  ListSymbRec ("listsymb.dbt");
  
  ListSymbRec.DocKind =ListSymbObj.DocKind;
  
  ListSymbRec.SYMB_CASH=ListSymbObj.SymbCash;
  
  ListSymbRec.Name=ListSymbObj.Name;
  
  ListSymbRec.TYPE_SYMBOL=ListSymbObj.SymbolType;

  Record  InitListSymbObjRec ("listsymb.dbt");

  if(InitListSymbObj!=null)
  
    InitListSymbObjRec.DocKind =InitListSymbObj.DocKind;
  
    InitListSymbObjRec.SYMB_CASH=InitListSymbObj.SymbCash;
  
    InitListSymbObjRec.Name=InitListSymbObj.Name;
  
    InitListSymbObjRec.TYPE_SYMBOL=InitListSymbObj.SymbolType;
  
  end;
  

  var res = CB_UpdateListSymbol(IsInsert, ListSymbRec,InitListSymbObjRec);
     
  if(res != true)
   
     AddErrorMessage(@ErrorMessage);
  
  end;
    
  responseBuilder.SetUserObject(res);
  
  return responseBuilder.Result;
 
end;

macro WebCore_DeleteListSymbol(  SymbCash, DocKind)  

  responseBuilder =WebResponseBuilder();
    
  var ErrorMessage : WsErrorMessage = WsErrorMessage();
   
  var res = CB_DeleteListSymbol(SymbCash, DocKind);
     
  if(res != true)
   
     AddErrorMessage(@ErrorMessage);
  
  end;
    
  responseBuilder.SetUserObject(res);
  
  return responseBuilder.Result;
 
end;

// Test 
/*
var ls = WebCore_GetCashSymbolGridItemList(1, 3, 4);

var ob = WebCore_GetCashSymbolGridParam(1, 3);

ls[0].RecID = 0;
WebCore_InsertCashSymbolGridItem(ls[0],ls[0].AcctrnID, ls[0].ListSymb.SymbolType);
var a = 1;
*/