/*
$Name:        attached_obj_scroll.mac
$Module:      Главная книга
$Description: Макрос WEB. Список прикрепленных объектов
*/

import RsbDataSet, globals, oralib, likepy;
import BankInter;
import "ws_partycommon.mac";
import "ws_validation.mac";
import "imgtype_widget.mac";
import "ws_imgdata.mac";
import "ws_file.mac";

class TWebAttachedObjGridObject
  var IsPrivReadOnly;
  var RegUploadDir;
end;

class TWebAttachedObjGridItem
  var IsImage;
  var ImageType;
  var Name;
  var IsMain;
  var IsClosed;
  var ImageID;
  var PreviewBase64Data;
  var OpenDateTime;
  var OpenOper;
  var CloseDateTime;
  var CloseOper;
  var IsAttachedObj;
  var Children;
end;

class TWebAttachedObjObject
  var ImageID;
  var ImageType;
  var FileName;
  var Base64Data;
  var PreviewBase64Data = "";
  var OpenDateTime;
  var IsClosed;
  var Comment;
end;

/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else
      return value_false;
   end;
end;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro GetRecAccountByID(AccountID : integer);
  var cmd, rs;

  var Rec = TRecHandler("account");

  var query = "select * from daccount_dbt where t_accountid = " + string(AccountID);

  cmd = RsdCommand(query);
  rs = RsdRecordset(cmd);

  if(rs.movenext)

    _CopyRecordsetToRecHandler(rs, Rec);

  end;

  return Rec;
end;

private macro MakeAttachedObjectID(ObjectType, ObjectID)

  var RecParty;
  var RecAccount;

  var ObjectIDStr = ObjectID;
  
  if(ObjectType == OBJTYPE_PARTY)
    
    RecParty = GetRecPartyByID(ObjectID);

    ObjectIDStr = UniID(RecParty, ObjectType);  
    
  else

    if(ObjectType == OBJTYPE_ACCOUNT)
      
      RecAccount = GetRecAccountByID(ObjectID);

      ObjectIDStr = UniID(RecAccount, ObjectType);  
    
    end;
    
  end;
  
  return ObjectIDStr;

end;

macro WebCore_BeforeShowAttachedObjGrid()

  var obj = TWebAttachedObjGridObject();

  if(CheckActionRestriction(161) == true)

    obj.IsPrivReadOnly = false;

  else

    obj.IsPrivReadOnly = true;

  end;

  var res = "";

  var err;

  GetRegistryValue( "CB\\PARTY\\IMAGEDATA\\UPLOADDIR", V_STRING, res, err );

  if(err == 0)

    if(SubStr(res, 0, 1) == "$")

      obj.RegUploadDir = SubStr(res, 1);

    end;

  else
    RunError("Ошибка при чтении настройки реестра!");
  end;

  return obj;

end;

private macro WebCore_GetPreviewBase64Data(ImageID)

  var query = "";
  var ds;
  
  var Previewbase64Data = "";

  query = "	SELECT IT.T_IMAGEID, RSB_PRDCLIENT.base64encode(it.T_FMTBLOBDATA_XXXX) as PreviewBase64Data "
          "  FROM dimgthumbn_dbt it "
            " where "
            "  IT.T_IMAGEID = " + string(ImageID);

  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if(ds.MoveNext())
    
    var phd = ds.Previewbase64Data;
    
    if(phd != "\"\"")
      Previewbase64Data  = phd;
    end;
    
  end;
  
  return Previewbase64Data;

end;

macro WebCore_GetAttachedObjGridItemList(IsHistory, ObjectType, ObjectID)

  var ObjectID_Str = MakeAttachedObjectID(ObjectType, ObjectID);

  var result = TArray();
  var typeObj;
  var openedObj;
  var closedObj;
  var firstObj;
  var secondObj;

  var cmd1, rs1;
  var cmd2, rs2;
  var cmd3, rs3;
  var cmd4, rs4;

  var query = "	SELECT * "
              "   FROM dimgtype_dbt "
              " WHERE (t_ObjectType, t_ImageType) IN "
              "    ( "
              "               SELECT t_ObjectType, t_ImageType "
              "                 FROM dimgdata_dbt "
              "                WHERE t_ObjectType = " + string(ObjectType) +
              "                  AND t_ObjectID = " + string(ObjectID_Str);

  if(IsHistory == false)

    query = query + " AND t_CloseOper = 0 ";

  end;

  query = query + " ) ";

  cmd1 = RsdCommand(query);
  
  cmd1.NullConversion = true;
  
  rs1 = RsdRecordset(cmd1);

  while(rs1.movenext)

    typeObj = TWebAttachedObjGridItem();

    typeObj.IsImage   = false;
    typeObj.ImageID   = -1;
    typeObj.ImageType = SQL_ConvType(rs1.value("T_IMAGETYPE"));
    typeObj.Name      = SQL_ConvType(rs1.value("T_NAME"));

    query = "	SELECT * "
            "   FROM dimgdata_dbt "
            "  WHERE t_ObjectType = " + string(ObjectType) +
            "    AND t_ObjectID = " + string(ObjectID_Str) +
            "    AND t_ImageType = " + string(typeObj.ImageType) +
            "    AND t_CloseOper = 0 ";
         
    if(IsHistory)
    
      query =  
" SELECT * "+
"   FROM (  SELECT * "+
"             FROM dimgdata_dbt d "+
"            WHERE d.t_imageid IN ( "+
"                       SELECT MAX (t_imageid) as closedMain_id  "+
"                          FROM dimgdata_dbt "+
"                         WHERE     t_ObjectType =  "+string(ObjectType) +
"                               AND t_ObjectID =  "+ string(ObjectID_Str) +
"                               AND t_ImageType =  "+ string(typeObj.ImageType) +
"                               AND t_closeoper <> 0 "+
"                               AND t_filename NOT IN "+
"                                      (SELECT t_filename             "+
"                                         FROM dimgdata_dbt "+
"                                        WHERE     t_ObjectType =  "+string(ObjectType) +
"                                              AND t_ObjectID =  "+ string(ObjectID_Str) +
"                                              AND t_ImageType = "+ string(typeObj.ImageType) +
"                                              AND t_closeoper = 0) "+
"                      GROUP BY t_filename) "+
"         ORDER BY t_opendate DESC, t_opentime DESC) "+
"   UNION ALL "+ query;

    end;
    cmd2 = RsdCommand(query);
    
    cmd2.NullConversion = true;
    
    rs2 = RsdRecordset(cmd2);

    typeObj.Children = TArray();

    while(rs2.movenext)

      openedObj = TWebAttachedObjGridItem();

      openedObj.IsImage        = true;
      openedObj.IsAttachedObj  = true;
      openedObj.ImageType      = SQL_ConvType(rs2.value("T_IMAGETYPE"));
      openedObj.Name           = SQL_ConvType(rs2.value("T_FILENAME"));
      openedObj.IsMain         = SQL_ConvTypeBool(rs2.value("T_ISMAIN"));

      if(SQL_ConvType(rs2.value("T_CLOSEOPER")) != 0)
        openedObj.IsClosed     = true;
      else
        openedObj.IsClosed     = false;
      end;

      openedObj.ImageID        = SQL_ConvType(rs2.value("T_IMAGEID"));
      openedObj.PreviewBase64Data = WebCore_GetPreviewBase64Data(openedObj.ImageID);
      openedObj.OpenDateTime   = SQL_ConvTypeDate(rs2.value("T_OPENDATE")) + " " + SQL_ConvTypeTime(rs2.value("T_OPENTIME"));
      openedObj.OpenOper       = SQL_ConvType(rs2.value("T_OPENOPER"));
      openedObj.CloseDateTime  = SQL_ConvTypeDate(rs2.value("T_CLOSEDATE")) + " " + IIF(SQL_ConvTypeTime(rs2.value("T_CLOSETIME")) != time(0,0,0), SQL_ConvTypeTime(rs2.value("T_CLOSETIME")), "");
      openedObj.CloseOper      = SQL_ConvType(rs2.value("T_CLOSEOPER"));

      if(IsHistory == true)

        query = " SELECT * "
                "   FROM dimgdata_dbt "
                "  WHERE t_ObjectType = " + string(ObjectType) +
                "    AND t_ObjectID = " + string(ObjectID_Str) +
                "    AND t_ImageType = " + string(typeObj.ImageType) +
                "  AND t_CloseOper <> 0 " +
                "        AND t_filename = '"+string(openedObj.Name)+"'  "+
                "        AND t_imageid <> "+string(openedObj.ImageID)+ // сам себя в свою историю не добавлять
                " ORDER BY t_OpenDate DESC, t_OpenTime DESC ";

        cmd3 = RsdCommand(query);
        
        cmd3.NullConversion = true;
        
        rs3 = RsdRecordset(cmd3);

        openedObj.Children = TArray();

        while(rs3.movenext)

          closedObj = TWebAttachedObjGridItem();

          closedObj.IsImage = true;
          closedObj.IsAttachedObj  = true;
          closedObj.ImageType      = SQL_ConvType(rs3.value("T_IMAGETYPE"));
          closedObj.Name           = SQL_ConvType(rs3.value("T_FILENAME"));
          closedObj.IsMain         = SQL_ConvTypeBool(rs3.value("T_ISMAIN"));

          if(SQL_ConvType(rs3.value("T_CLOSEOPER")) != 0)
            closedObj.IsClosed     = true;
          else
            closedObj.IsClosed     = false;
          end;

          closedObj.ImageID        = SQL_ConvType(rs3.value("T_IMAGEID"));
          closedObj.PreviewBase64Data = WebCore_GetPreviewBase64Data(closedObj.ImageID);
          closedObj.OpenDateTime   = SQL_ConvTypeDate(rs3.value("T_OPENDATE")) + " " + SQL_ConvTypeTime(rs3.value("T_OPENTIME"));
          closedObj.OpenOper       = SQL_ConvType(rs3.value("T_OPENOPER"));
          closedObj.CloseDateTime  = SQL_ConvTypeDate(rs3.value("T_CLOSEDATE")) + " " + IIF(SQL_ConvTypeTime(rs3.value("T_CLOSETIME")) != time(0,0,0), SQL_ConvTypeTime(rs3.value("T_CLOSETIME")), "");
          closedObj.CloseOper      = SQL_ConvType(rs3.value("T_CLOSEOPER"));

          openedObj.Children[openedObj.Children.size] = closedObj;

        end;

      end;

      typeObj.Children[typeObj.Children.size] = openedObj;

    end;



    result[result.size] = typeObj;

  end;

  return result;

end;

macro WebCore_SetIsMainImgData(ImageID)

  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = CB_SetMainImage(ImageID);

  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_CloseImgData(ImageID)

  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = CB_CloseImage(ImageID);

  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_InsertImgData(
  ObjectType,
  ObjectID,
  ImageType,
  ImageStr,
  ThumbnStr,
  FileName,
  Comment,
  ChangeImage,
  ImgChunkID
 )
  var res = false;

  var ImageID = -1;

  var query, cmd, rs;

  var ObjectID_Str = MakeAttachedObjectID(ObjectType, ObjectID);

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = WEB_CheckImage(ObjectType, ObjectID_Str, ImageType, ImageStr, FileName, ImgChunkID);

  if( res == true )

    query = "	SELECT * "
            "   FROM dimgdata_dbt "
            "  WHERE t_ObjectType = " + string(ObjectType) +
            "    AND t_ObjectID = " + string(ObjectID_Str) +
            "    AND t_ImageType = " + string(ImageType) +
            "    AND t_FileName = '" + string(FileName) + "' "
            "    AND t_CloseOper = 0 ";

    cmd = RsdCommand(query);
    rs = RsdRecordset(cmd);

    if(rs.movenext)

      if(ChangeImage != null)

        res = WEB_LoadImageObjFromStrExt(
                                          ObjectType,
                                          ObjectID_Str,
                                          ImageType,
                                          ImageStr,
                                          FileName,
                                          ChangeImage,
                                          SQL_ConvType(rs.value("T_IMAGEID")),
                                          Comment,
                                          ThumbnStr,
                                          ImgChunkID
                                        );

      else

        res = NULL;

      end;
      
    else
      
        res = WEB_LoadImageObjFromStrExt(
                                          ObjectType,
                                          ObjectID_Str,
                                          ImageType,
                                          ImageStr,
                                          FileName,
                                          false,
                                          ImageID,
                                          Comment,
                                          ThumbnStr,
                                          ImgChunkID
                                        );    

        if(res == true)
          
          res = ImageID;
          
        end;

    end;

  end;

  if(res == false)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_UpdateImage(
  ImageID,
  ObjectType,
  ObjectID,
  ImageType,
  ImageStr,
  ThumbnStr,
  FileName,
  Comment,
  ChangeImage,
  ImgChunkID
 )

  var res = false;

  var query, cmd, rs;

  var ObjectID_Str = MakeAttachedObjectID(ObjectType, ObjectID);

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = WEB_CheckImage(ObjectType, ObjectID_Str, ImageType, ImageStr, FileName, ImgChunkID);

  var objErr = AL_GetErrorInfo();
  
  if((res == false) AND (objErr.stat == 29043)) //IMAGEOBJECT_ALR_EXIST
  
    res = WEB_UpdateImageFromStrExt(
                                      ImageID,
                                      "",
                                      FileName,
                                      Comment,
                                      "",
                                      -1
                                    );
  
  else

  if( res == true )

    if(ChangeImage == true)

        res = WEB_LoadImageObjFromStrExt(
                                          ObjectType,
                                          ObjectID_Str,
                                          ImageType,
                                          ImageStr,
                                          FileName,
                                          ChangeImage,
                                          ImageID,
                                          Comment,
                                          ThumbnStr,
                                          ImgChunkID
                                        );

    else

        res = WEB_UpdateImageFromStrExt(
                                              ImageID,
                                              ImageStr,
                                              FileName,
                                              Comment,
                                              ThumbnStr,
                                              ImgChunkID
                                            );

    end;

  end;

  end;


  if(res == false)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

private macro GetImageType(objecttype, imagetype)

  var query, cmd, rs;

  var p = TWebImageType();

  query = "	SELECT T_IMAGETYPE, T_NAME "
          "   FROM dimgtype_dbt "
          "  WHERE t_ObjectType = " + string(objecttype) + " AND t_ImageType = " + string(imagetype);

  cmd = RsdCommand(query);
  rs = RsdRecordset(cmd);

  if(rs.movenext)
    p.ImageType = SQL_ConvType(rs.value("T_IMAGETYPE"));
    p.Name      = SQL_ConvType(rs.value("T_NAME"));
  end;
  
  return p;

end;

macro WebCore_GetAttachedObjObject(ImageID)

  var query = "";
  var ds;

  var p = TWebAttachedObjObject();

  query = "	SELECT d.*, RSB_PRDCLIENT.base64encode(i.T_FMTBLOBDATA_XXXX) as Base64Data, RSB_PRDCLIENT.base64encode(it.T_FMTBLOBDATA_XXXX) as PreviewBase64Data "
          "  FROM dimage_dbt i,dimgdata_dbt d, dimgthumbn_dbt it "
            " where "
            "  I.T_IMAGEID = " + string(ImageID) +
            "  and I.T_IMAGEID = D.T_IMAGEID "
            "  and I.T_IMAGEID = IT.T_IMAGEID(+) ";

  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if(ds.MoveNext())
    p.ImageID         = ImageID;
    p.ImageType       = GetImageType(SQL_ConvType(ds.objecttype), SQL_ConvType(ds.imagetype));
    p.FileName        = SQL_ConvType(ds.filename);
    
    var bd = ds.Base64data;
    
    if(bd != "\"\"")
    
      var i = index(bd, "\r\n");
    
      while(i > 0)
        
        bd = SubStr(bd, 1, i - 1) + SubStr(bd, i + 2);
        
        i = index(bd, "\r\n");
        
      end;
      
      p.Base64Data  = bd;
    
    end; 
    
    var pbd = ds.previewBase64data;
    
    if(pbd != "\"\"")
      
      var ii = index(pbd, "\r\n");
    
      while(ii > 0)
        
        pbd = SubStr(pbd, 1, ii - 1) + SubStr(pbd, ii + 2);
        
        ii = index(pbd, "\r\n");
        
      end;    
    
      p.PreviewBase64Data  = pbd;
    end;
    
    p.OpenDateTime    = SQL_ConvTypeDate(ds.opendate) + " " + SQL_ConvTypeTime(ds.opentime);

    if(SQL_ConvType(ds.closeoper) != 0)
      p.IsClosed = true;
    else
      p.IsClosed = false;
    end;

    p.Comment = SQL_ConvType(ds.comment);
  end;
  
  return p;

end;

macro WebCore_DeleteImage(ImageID)

  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = WEB_DeleteImage(ImageID);

  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_GetAttachedObject(ObjectType, ObjectID, ImageType, ImageID)

  var ObjectID_Str = MakeAttachedObjectID(ObjectType, ObjectID);

  var fc = null;
  
  var res = true;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  var query = "";
  
  var ds;

  if ((ValType(ImageID) == V_UNDEF) or (ImageID <= 0))
    
    if ((ValType(ImageType) == V_UNDEF) or (ImageType <= 0))

      query = " select t.t_ImageType "
              "  from dimgtype_dbt t "
              " where "
              "  t.t_objecttype = " + string(ObjectType) +
              "  and t.t_isdefault = 'X' ";

      ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

      if( ds.MoveNext() )
        
        ImageType = ds.ImageType;
        
      end;

    end;

    query = " select d.t_imageid "
            "  from dimgdata_dbt d "
            " where "
            "  d.t_objecttype = " + string(ObjectType) +
            "  and d.t_imagetype = " + ImageType +
            "  and d.t_objectid = " + string(ObjectID_Str) +
            "  and d.t_closedate = TO_DATE ('01010001', 'DDMMYYYY') ORDER BY d.t_IsMain desc ";

    ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

    if( ds.MoveNext() )

      ImageID = ds.ImageID;

    end;

  end;
  
  var fulltmpname : string = "";

  res = WEB_ShowImageByID(ImageID, @fulltmpname);
  
  if(fulltmpname != "")
  
    fc = GetTextFile(fulltmpname);    
  
  end;

  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(fc);

  return responseBuilder.Result;

end;
