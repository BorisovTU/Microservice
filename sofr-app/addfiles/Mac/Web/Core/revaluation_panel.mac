 /*
 $Name: revaluation_panel.mac
 $Module: Главная книга
 $Description: Макрос Переоценки
 */

import globals;
import "ws_validation.mac";
import "ws_dprt.mac";
import "ws_file.mac";
import serviceaction;
import cb_sql;
import rsd, oralib, likepy;
import SPInter;
import BankInter;
import PTInter;
import "consrep.mac";
import "ovest_summ_order.mac";

class TWebNameKind
  var ID   /*: integer*/;
  var Name /*: string*/;
end;

class TWebReValueAccPack
  var ParamID /*: integer*/; //ид. записи параметров
  var DprtID /*: integer*/; //код подразделения
  var StartRecID /*: integer*/; //стартовый номер записи внутри группы счетов
  var PackSize /*: integer*/; //количестов записей в пачке
end;

class TWebReValueAccPrm
  var ParamID : integer /*: integer*/;
  var RecNumber : integer /*: integer*/;
  var Packs /*: List<TWebReValueAccPack>*/;
end;

class TWebReValuationPanelObject
  var OperCanExecServProcHead /*: bool*/;
  var RestKindList            /*: List<TWebNameKind>*/;
  var RestKind                /*: TWebNameKind/integer*/;
  var Department              /*: TWsDepartment*/;
  var IsAllDprtsSelected      /*: bool*/;
  var ChapterListSelected     /*: List<TWebChapter>*/;
  var IsAllChaptersSelected   /*: bool*/;
  var AccKindList             /*: List<TWebNameKind>*/;
  var AccKind                 /*: TWebNameKind/integer*/;
  var IsAllAccKindsSelected   /*: bool*/;
  var FIKindList              /*: List<TWebNameKind>*/;
  var FIKind                  /*: TWebNameKind/integer*/;
  var IsAllFIKindsSelected    /*: bool*/;
  var FIListSelected          /*: List<TWsFinInstr>*/;
  var IsAllFIsSelected        /*: bool*/;
  var Account                 /*: TWebAccount*/;
  var IsAllAccountsSelected   /*: bool*/;
  var RegulateDate            /*: date*/;
  var ActuateAccOvervalue     /*: bool*/;
  var RegCarry                /*: bool*/;
  var AllDays                 /*: bool*/;
  var FirstCarryNumber        /*: integer*/;
  var PrintReport             /*: bool*/;
  var PrintOrders             /*: bool*/;
  var FullAccess              /*: bool*/;
  var Backout                 /*: bool*/;
end;

private var _countErrors;

private var _responsebuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)
  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  _responsebuilder.AddMessage( ErrorMessage );
end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)
  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  _responsebuilder.AddMessage( ErrorMessage );
end;

macro WebCore_CheckReValuationPanelCallRights()
  var res = true;
  var err;
  var regVal;

  _responsebuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  if(res and ({cTypePerson} == TP_BOSS))
    res = false;
    AddErrorMessageExt(@ErrorMessage, GetErrorMsg(548));
  end;


  if(res)
    GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\БЕЗОПАСНОСТЬ\\ACCESS_REGRUB", V_STRING, regVal, err);
    if(err == 0)
      if(regVal == "NONE")
        res = false;
        AddErrorMessageExt(@ErrorMessage, GetErrorMsg(256));
      end;
    else
      RunError("Ошибка при чтении настройки реестра!");
    end; 
  end;

  _responseBuilder.SetUserObject(res);

  return _responseBuilder.Result;
end;

macro WebCore_InitReValuationPanel(Backout)
  var panelObj  = TWebReValuationPanelObject();
  var FirstDate = date(0, 0, 0);
  var err;
  var res = CB_GetFirstOpenDate({OperDprt}, @FirstDate);
  
  if(res == true)
    panelObj.RegulateDate = FirstDate;
  end;
  
  panelObj.Department = GetTWsDepartmentByCode({OperDprt});
  panelObj.IsAllDprtsSelected = false;
  
  GetRegistryValue("COMMON\\СЕРВИСНЫЕ ПРОЦЕДУРЫ\\ЗАПУСК СЕРВИСНЫХ ПРОЦЕДУР", V_BOOL, res, err);

  if(err == 0)
    if((res == true) and ({OperDprt} == {NumDprt}))
      panelObj.OperCanExecServProcHead = true;
    else
      panelObj.OperCanExecServProcHead = false;
    end;
  else
    RunError("Ошибка при чтении настройки реестра!");
  end; 
  
  panelObj.RestKindList = TArray();
  panelObj.RestKindList[0] = TWebNameKind();
  panelObj.RestKindList[0].ID   = 1;
  panelObj.RestKindList[0].Name = "Входящие";
  panelObj.RestKindList[1] = TWebNameKind();
  panelObj.RestKindList[1].ID   = 2;
  panelObj.RestKindList[1].Name = "Исходящие";
  panelObj.RestKindList[2] = TWebNameKind();
  panelObj.RestKindList[2].ID   = 3;
  panelObj.RestKindList[2].Name = "Исходящие, равные нулю";

  GetRegistryValue("CB\\CONSENTACNT\\DEFAULT_REST_IN", V_BOOL, res, err);
  
  if(err == 0)
    if(res == true)
      panelObj.RestKind = panelObj.RestKindList[0];
    else
      panelObj.RestKind = panelObj.RestKindList[1];
    end;
  else
    RunError("Ошибка при чтении настройки реестра!");
  end; 

  panelObj.IsAllChaptersSelected = true;

  panelObj.AccKindList = TArray();
  panelObj.AccKindList[0] = TWebNameKind();
  panelObj.AccKindList[0].ID   = 1;
  panelObj.AccKindList[0].Name = "активные";
  panelObj.AccKindList[1] = TWebNameKind();
  panelObj.AccKindList[1].ID   = 2;
  panelObj.AccKindList[1].Name = "пассивные";
  panelObj.AccKindList[2] = TWebNameKind();
  panelObj.AccKindList[2].ID   = 3;
  panelObj.AccKindList[2].Name = "без остатка";

  panelObj.IsAllAccKindsSelected = true;

  panelObj.FIKindList = TArray();
  panelObj.FIKindList[0] = TWebNameKind();
  panelObj.FIKindList[0].ID   = 1;
  panelObj.FIKindList[0].Name = "Валюты";
  panelObj.FIKindList[1] = TWebNameKind();
  panelObj.FIKindList[1].ID   = 6;
  panelObj.FIKindList[1].Name = "Драгметаллы";

  panelObj.IsAllFIKindsSelected = true;
  
  panelObj.IsAllFIsSelected = true;
  
  panelObj.IsAllAccountsSelected = true;
  
  panelObj.ActuateAccOvervalue = false;
  
  panelObj.RegCarry = false;
  
  panelObj.AllDays = false;
  
  panelObj.FirstCarryNumber = 1;
  
  panelObj.PrintReport = true; 
  
  if(Backout == true)
    panelObj.PrintOrders = false; 
  else
    panelObj.PrintOrders = true; 
  end;
  
  panelObj.Backout = Backout; 

  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\БЕЗОПАСНОСТЬ\\ACCESS_REGRUB", V_STRING, res, err);

  if(err == 0)
    if(res == "FULL")
      panelObj.FullAccess = true;
    else
      panelObj.FullAccess = false;
    end;
  else
    RunError("Ошибка при чтении настройки реестра!");
  end;

  return panelObj;
end;

macro WebCore_GetReValuationPanel(Department, IsAllDprtSelected)
  var res;
  var RegulateDate;
  if(IsAllDprtSelected == true)
    res = CB_GetFirstOpenDate(0, @RegulateDate);
  else
    if(Department != null)
      res = CB_GetFirstOpenDate(Department.Code, @RegulateDate);
    end;
  end;
  return RegulateDate;
end;

private macro _InsertParamXML(XML : string, NumberDoc)
  var query = "";  
  var cmd;
  var ID;
  
  query = query + "INSERT INTO drevalueaccprm_dbt ( t_ParamXML, t_SysDate, t_SysTime, t_NextNumberDoc ) ";
  query = query + "VALUES ( :XML, TRUNC(SYSDATE), TO_DATE('01-01-0001:' || TO_CHAR(SYSDATE, 'HH24:MI:SS'), 'DD-MM-YYYY:HH24:MI:SS'), :NumberDoc ) ";
  query = query + "RETURNING t_ParamID INTO :ID ";
  cmd = RsdCommand(query);
  cmd.addParam("XML", RSDBP_IN, XML);
  cmd.addParam("NumberDoc", RSDBP_IN, NumberDoc);
  cmd.addParam("ID", RSDBP_OUT, V_INTEGER);
  cmd.execute();
  
  ID = cmd.value("ID");
  return ID;
end;

private macro _GetParamXML(ID : integer)
  var query = "";
  var ParamXML;

  query = query + "SELECT ";
  query = query + "  t_ParamID, t_ParamXML "; /*!!! CLOB-поле не должно быть первым*/
  query = query + "  FROM drevalueaccprm_dbt ";
  query = query + " WHERE t_ParamID = " + ID;
  /*!!!важно!!! инструмент требует, чтобы CLOB-поле не было первым в SELECT'е, иначе ошибка выполнения*/
  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if (ds.MoveNext())
    ParamXML = ds.ParamXML;
  end;
  return ParamXML;
end;

private macro _GetCountAccounts(ID : integer)
  var Count;

  var query = "";
  var cmd, rs;

  query = query + "SELECT COUNT(*) ";
  query = query + "  FROM drevalueaccsel_dbt ";
  query = query + " WHERE t_ParamID = " + ID;
  
  cmd = RsdCommand(query);

  rs = RsdRecordset(cmd);

  if(rs.movenext)
    Count = rs.value(0);
  end;

  return Count;
end;

private macro _FormFisclogInfo(PanelObj)
  var info = "";
  var i, k;

  if(PanelObj.Backout)
    info = info + "Откат урегулирования покрытий.\n";
  else
    info = info + "Урегулирование покрытий.\n";
  end;

  if(PanelObj.AccKind != null)
    info = info + "Переоценка остатка:" + PanelObj.AccKind.Name + "\n";
  end;
  
  if(PanelObj.IsAllDprtsSelected == true)
    info = info + "Узел ТС: Все филиалы\n";
  else
    info = info + "Узел ТС: " + PanelObj.Department.Name_ + " " + PanelObj.Department.PartyName +"\n";
  end;

  if(PanelObj.IsAllChaptersSelected)
    info = info + "Глава счетов: Все главы\n";
  elif((PanelObj.ChapterListSelected != null) and (PanelObj.ChapterListSelected.size() > 0))
    i = 0;
    k = PanelObj.ChapterListSelected.size();
    while(i < k)
      if(i == 0)
        info = info + "Глава счетов: " + PanelObj.ChapterListSelected[i].Chapter + " (" + PanelObj.ChapterListSelected[i].ChapterName + ")";
      else
        info = info + "              " + PanelObj.ChapterListSelected[i].Chapter + " (" + PanelObj.ChapterListSelected[i].ChapterName + ")";
      end;
      i = i + 1;
      if(i < k)
        info = info + ",\n";
      else
        info = info + "\n";
      end;
    end;
  end;

  if(PanelObj.IsAllAccKindsSelected)
    info = info + "Вид счетов: Все\n";
  elif(PanelObj.AccKind != null)
    info = info + "Вид счетов: " + PanelObj.AccKind.Name + "\n";
  end;

  if(PanelObj.IsAllFIKindsSelected)
    info = info + "Вид Финансового инструмента: Все\n";
  elif(PanelObj.FIKind != null)
    info = info + "Вид Финансового инструмента: " + PanelObj.FIKind.Name + "\n";
  end;

  if(PanelObj.IsAllFIsSelected)
    info = info + "ФИ: Все\n";
  elif((PanelObj.FIListSelected != null) and (PanelObj.FIListSelected.size() > 0))
    i = 0;
    k = PanelObj.FIListSelected.size();
    while(i < k)
      if(i == 0)
        info = info + "ФИ: " + PanelObj.FIListSelected[i].CodeWidget + " (" + PanelObj.FIListSelected[i].Name + ")";
      else
        info = info + "    " + PanelObj.FIListSelected[i].CodeWidget + " (" + PanelObj.FIListSelected[i].Name + ")";
      end;
      i = i + 1;
      if(i < k)
        info = info + ",\n";
      else
        info = info + "\n";
      end;
    end;
  end;

  if((PanelObj.Account != null) and (PanelObj.Account.Account != null))
    info = info + "Номер счета: " + PanelObj.Account.Account + "\n";
  else
    info = info + "Номер счета:\n";
  end;

  info = info + "Дата урегулирования: " + string(PanelObj.RegulateDate:f) + "\n";
  
  if(PanelObj.RegCarry)
    info = info + "Урегулирование проводок: Да\n";
  else
    info = info + "Урегулирование проводок: Нет\n";
  end;

  if(PanelObj.AllDays)
    info = info + "Урегулирование в следующих днях: Да\n";
  else
    info = info + "Урегулирование в следующих днях: Нет\n";
  end;

  info = info + "Номер первого документа: " + PanelObj.FirstCarryNumber + "\n";

  if(PanelObj.PrintReport)
    info = info + "Печать реестра: Да\n";
  else
    info = info + "Печать реестра: Нет\n";
  end;

  if(PanelObj.PrintOrders)
    info = info + "Печать сводных ордеров: Да\n";
  else
    info = info + "Печать сводных ордеров: Нет\n";
  end;

  if(PanelObj.ActuateAccOvervalue)
    info = info + "Актуализировать счета курсовых разниц: Да\n";
  else
    info = info + "Актуализировать счета курсовых разниц: Нет\n";
  end;

  return info;
end;

private macro _GetSelectedDprtsRS(PanelObj)
  var query = "";
  var cmd, rs;

  query = query + "    SELECT t_Code ";
  query = query + "      FROM ddp_dep_dbt ";

  if(PanelObj.IsAllDprtsSelected == true)
    query = query + "     WHERE     t_Status = 2 /* DEPARTMENT_STATUS_ACTIVE Активное */ ";
    query = query + "           AND t_AccessMode = 1 /* DEPARTMENT_ACCESS_ONLINE Доступ On-Line */ ";
    query = query + "START WITH t_Code = " + {NumDprt} + " /* Ид. головного подразделения */ ";
    query = query + "CONNECT BY     t_ParentCode = PRIOR t_Code "; 
    query = query + "           AND t_NodeType = 1 /* DEPARTMENT_TYPE_FILIAL Филиал */ ";
  else
    query = query + "     WHERE t_Code = " + PanelObj.Department.Code;
  end;
  query = query + "  ORDER BY t_Code ";

  cmd = RsdCommand(query);

  rs = RsdRecordset(cmd);

  return rs;
end;

private macro _GetAccRecsForDprtsRS(ParamID)
  var query = "";
  var cmd, rs;

  query = query + "SELECT ";
  query = query + "  t_Branch ";
  query = query + " ,MIN (t_RecID) ";
  query = query + " ,MAX (t_RecID) ";
  query = query + "  FROM drevalueaccsel_dbt ";
  query = query + " WHERE t_ParamID = " + ParamID;
  query = query + " GROUP BY t_Branch ";
  query = query + " ORDER BY t_Branch ";

  cmd = RsdCommand(query);

  rs = RsdRecordset(cmd);

  return rs;
end;

private macro _FillRevalueAccPacks(ReValueAccPrm)
  var err;
  var regVal = 1000;
  var AccRecsForDprtsRS = _GetAccRecsForDprtsRS(ReValueAccPrm.ParamID);
  var i;
  var MinRecID, MaxRecID, PackSize, DprtID;
  var ReValueAccPack;
  
  GetRegistryValue("CB\\CONSENTACNT\\ACCOUNT_PACKAGE_SIZE", V_INTEGER, regVal, err);

  ReValueAccPrm.Packs = TArray();
  i = 0;
  while(AccRecsForDprtsRS.movenext)
    DprtID = int(AccRecsForDprtsRS.value(0));
    MinRecID = int(AccRecsForDprtsRS.value(1));
    MaxRecID = int(AccRecsForDprtsRS.value(2));

    while(MinRecID <= MaxRecID)
      ReValueAccPack = TWebReValueAccPack();
      ReValueAccPack.ParamID = ReValueAccPrm.ParamID;
      ReValueAccPack.DprtID = DprtID;
      ReValueAccPack.StartRecID = MinRecID;
      if((MinRecID + regVal) > MaxRecID)
        PackSize = MaxRecID - MinRecID + 1;
      else
        PackSize = regVal;
      end;
      ReValueAccPack.PackSize = PackSize;

      ReValueAccPrm.Packs[i] = ReValueAccPack;
      i = i + 1;
      MinRecID = MinRecID + regVal;
    end;
  end;
end;

macro WebCore_MakeReValuationAccList(PanelObj)
  var retObj = TWebReValueAccPrm;
  var xml;
  var err;
  var stmtPrt1 = "";
  var stmtPrt2 = "";
  var stmtRNum = "";
  var stmtDprt = "";
  var stmtRslt = "";
  var i;
  var cmd;
  var info;
  var DprtsRS = _GetSelectedDprtsRS(PanelObj);
  
  err = ConvertToXML(PanelObj, null, xml);
  if(not err)
    retObj.ParamID = _InsertParamXML(xml, PanelObj.FirstCarryNumber);
  end;

  stmtPrt1 = stmtPrt1 + "INSERT INTO drevalueaccsel_dbt ";
  stmtPrt1 = stmtPrt1 + "( t_ParamID ";
  stmtPrt1 = stmtPrt1 + " ,t_RecID ";
  stmtPrt1 = stmtPrt1 + " ,t_AccountID ";
  stmtPrt1 = stmtPrt1 + " ,t_Account ";
  stmtPrt1 = stmtPrt1 + " ,t_Chapter ";
  stmtPrt1 = stmtPrt1 + " ,t_Code_Currency ";
  stmtPrt1 = stmtPrt1 + " ,t_Branch ";
  stmtPrt1 = stmtPrt1 + ") ";
  stmtPrt1 = stmtPrt1 + "SELECT ";
  stmtPrt1 = stmtPrt1 + " " + retObj.ParamID;
  stmtRNum = " ,ROWNUM ";
  stmtPrt2 = stmtPrt2 + " ,acc.t_AccountID ";
  stmtPrt2 = stmtPrt2 + " ,acc.t_Account ";
  stmtPrt2 = stmtPrt2 + " ,acc.t_Chapter ";
  stmtPrt2 = stmtPrt2 + " ,acc.t_Code_Currency ";
  
  if(PanelObj.IsAllDprtsSelected == true)
    stmtPrt2 = stmtPrt2 + " ,acc.t_Department ";
  else
    if(PanelObj.Department.NodeType == 1)
      stmtPrt2 = stmtPrt2 + " ,acc.t_Department ";
    else
      stmtPrt2 = stmtPrt2 + " ,acc.t_Branch ";
    end;
  end;

  stmtPrt2 = stmtPrt2 + "  FROM daccount_dbt acc ";
  stmtPrt2 = stmtPrt2 + " WHERE acc.t_Open_Close = CHR(0) ";
  stmtPrt2 = stmtPrt2 + "   AND acc.t_Code_Currency <> 0 ";
  stmtPrt2 = stmtPrt2 + "   AND NOT acc.t_Type_Account LIKE '%Н%' /*TA_ESTIM - Непереоц.покрытие*/";
  stmtPrt2 = stmtPrt2 + "   AND acc.T_Chapter IN ";
  stmtPrt2 = stmtPrt2 + "          (SELECT T_CHAPTER ";
  stmtPrt2 = stmtPrt2 + "             FROM DOBCHAPTR_DBT ";
  stmtPrt2 = stmtPrt2 + "            WHERE     T_UNREALIZEDCATNUMPLUS <> 0 ";
  stmtPrt2 = stmtPrt2 + "                  AND T_UNREALIZEDCATNUMMINUS <> 0) ";

  if(PanelObj.FullAccess == false)
    if({cTypePerson} == TP_OPER)
      stmtPrt2 = stmtPrt2 + "   AND acc.t_Oper = " + {oper} + " ";
    elif({cTypePerson} ==  TP_DEP)
      stmtPrt2 = stmtPrt2 + "   AND (acc.t_Oper = " + {oper} + " OR (acc.t_Oper >= " + {GroupOperF} + " AND acc.t_Oper <= " + {GroupOperL} + "))";
    end;
  end;

  if((PanelObj.ChapterListSelected != null) and (PanelObj.ChapterListSelected.size() > 0))
    stmtPrt2 = stmtPrt2 + "   AND ( ";
    i = 0;
    while(i < PanelObj.ChapterListSelected.size())
      if(i > 0)
        stmtPrt2 = stmtPrt2 + " OR ";
      end;
      stmtPrt2 = stmtPrt2 + " acc.t_Chapter = " + PanelObj.ChapterListSelected[i].Chapter;
      i = i + 1;
    end;
    stmtPrt2 = stmtPrt2 + "       ) ";
  end;

  if((PanelObj.AccKind != null) and (PanelObj.AccKind.ID != 0))
    if  (PanelObj.AccKind.ID == 1)
      stmtPrt2 = stmtPrt2 + "   AND acc.t_Kind_Account = 'А' ";
    elif(PanelObj.AccKind.ID == 2)
      stmtPrt2 = stmtPrt2 + "   AND acc.t_Kind_Account = 'П' ";
    elif(PanelObj.AccKind.ID == 3)
      stmtPrt2 = stmtPrt2 + "   AND acc.t_Kind_Account = '0' ";
    end;
  end;

  if((PanelObj.Account != null) and (PanelObj.Account.Account != null))
    if(Web_StringIsMaskEx(PanelObj.Account.Account, CSMFL_Mask ))
      stmtPrt2 = stmtPrt2 + "   AND " + ConvertMaskToSQLFormat(PanelObj.Account.Account, "acc.t_Account", CSMFL_Mask);
    else
      stmtPrt2 = stmtPrt2 + "   AND acc.t_Account = '" + PanelObj.Account.Account + "' ";
      stmtPrt2 = stmtPrt2 + "   AND acc.t_Chapter = " + PanelObj.Account.Chapter + " ";
      stmtPrt2 = stmtPrt2 + "   AND acc.t_Code_Currency = " + PanelObj.Account.Currency + " ";
    end;
  end;

  if(PanelObj.FIKind != null)
    if  (PanelObj.FIKind.ID == 1)
      stmtPrt2 = stmtPrt2 + "   AND acc.t_Code_Currency IN (SELECT t_FIID FROM dfininstr_dbt WHERE t_FI_Kind = 1) ";
    elif(PanelObj.FIKind.ID == 2)
      stmtPrt2 = stmtPrt2 + "   AND acc.t_Code_Currency IN (SELECT t_FIID FROM dfininstr_dbt WHERE t_FI_Kind = 6) ";
    end;
  end;

  if((PanelObj.FIListSelected != null) and (PanelObj.FIListSelected.size() > 0))
    stmtPrt2 = stmtPrt2 + "   AND ( ";
    i = 0;
    while(i < PanelObj.FIListSelected.size())
      if(i > 0)
        stmtPrt2 = stmtPrt2 + " OR ";
      end;
      stmtPrt2 = stmtPrt2 + " acc.t_Code_Currency = " + PanelObj.FIListSelected[i].FIID;
      i = i + 1;
    end;
    stmtPrt2 = stmtPrt2 + "       ) ";
  end;

  if(PanelObj.IsAllDprtsSelected == true)
    while(DprtsRS.movenext)
      retObj.RecNumber = _GetCountAccounts(retObj.ParamID);
      stmtRNum = " ,ROWNUM + " + retObj.RecNumber;
      stmtDprt = "   AND acc.t_Department = " + DprtsRS.value(0);
      stmtRslt = stmtPrt1 + stmtRNum + stmtPrt2 + stmtDprt;
      cmd = RsdCommand(stmtRslt);
      cmd.execute();
    end;
  else
    if(PanelObj.Department.NodeType == 1)
      stmtDprt = "   AND acc.t_Department = " + PanelObj.Department.Code;
    else
      stmtDprt = "   AND acc.t_Branch = " + PanelObj.Department.Code;
    end;
    stmtRslt = stmtPrt1 + stmtRNum + stmtPrt2 + stmtDprt;
    cmd = RsdCommand(stmtRslt);
    cmd.execute();
  end;

  retObj.RecNumber = _GetCountAccounts(retObj.ParamID);
  _FillRevalueAccPacks(retObj);

  info = _FormFisclogInfo(PanelObj);
  
  if(PanelObj.Backout)
    WriteFiscLog(UOL_B_COCUNS_BACK_PROC_FISCLOG, info);
  else
    WriteFiscLog(UOL_B_COCUNS_PROC_FISCLOG, info);
  end;

  return retObj;
end;

private macro _TruncateTable(TableName) : bool
  var cmd : RsdCommand;
  cmd = RsdCommand("TRUNCATE TABLE " + TableName);
  cmd.execute();
end;

private macro _InsertAccountsIntoAccOvervalue(RevalueAccPack)
  var stmt = "";
  var cmd;
  
  _TruncateTable("daccovervalue_tmp");

  stmt = stmt + "INSERT INTO daccovervalue_tmp ";
  stmt = stmt + "( t_AccountID ";
  stmt = stmt + " ,t_Chapter ";
  stmt = stmt + " ,t_Code_Currency ";
  stmt = stmt + " ,t_Account ";
  stmt = stmt + ") ";
  stmt = stmt + "SELECT ";
  stmt = stmt + "  t_AccountID ";
  stmt = stmt + " ,t_Chapter ";
  stmt = stmt + " ,t_Code_Currency ";
  stmt = stmt + " ,t_Account ";
  stmt = stmt + "  FROM drevalueaccsel_dbt ";
  stmt = stmt + " WHERE     t_ParamID = " + RevalueAccPack.ParamID;
  stmt = stmt + "       AND t_Branch = " + RevalueAccPack.DprtID;
  stmt = stmt + "       AND t_RecID BETWEEN " + RevalueAccPack.StartRecID + " AND " + (RevalueAccPack.StartRecID + RevalueAccPack.PackSize - 1);

  cmd = RsdCommand(stmt);
  cmd.execute();
end;

macro WebCore_ExecReValuation(RevalueAccPack)
  var PanelObj;
  var accKind = 0;
  var restKind = 0;
  ConvertToRSL(_GetParamXML(RevalueAccPack.ParamID), @PanelObj);

  if(PanelObj.RestKind != null)
    restKind = PanelObj.RestKind.ID;
  end;

  if(PanelObj.IsAllAccKindsSelected == true)
    if(PanelObj.AccKind != null)
      accKind = PanelObj.AccKind.ID;
    end;
  end;

  _InsertAccountsIntoAccOvervalue(RevalueAccPack);

  CB_OverEstimateAccount
  (
    RevalueAccPack.ParamID  
   ,RevalueAccPack.StartRecID    
   ,restKind
   ,RevalueAccPack.DprtID             
   ,0            
   ,""            
   ,0      
   ,PanelObj.RegulateDate       
   ,accKind
   ,PanelObj.RegCarry           
   ,PanelObj.ActuateAccOvervalue
   ,PanelObj.PrintReport        
   ,PanelObj.PrintOrders        
   ,PanelObj.AllDays            
   ,PanelObj.Backout            
   ,PanelObj.FirstCarryNumber          
  );
  
  _responsebuilder = WebResponseBuilder();

  _responsebuilder.SetUserObject(true);
  
  return _responsebuilder.Result;
end;

/* Вставить во временную таблицу данные для построения Реестра проводок.
Используется существующая реализация построения Реестра.*/
private macro _InsertIntoCarryLog(ParamID, DprtID)
  var stmt = "";
  var cmd;

  stmt = stmt + "INSERT INTO dovest_carry_log_tmp ";
  stmt = stmt + "( ";
  stmt = stmt + "  t_CarryID ";
  stmt = stmt + " ,t_Result_Carry ";
  stmt = stmt + " ,t_Date_Carry ";
  stmt = stmt + " ,t_Chapter ";
  stmt = stmt + " ,t_Currency ";
  stmt = stmt + " ,t_ExpAccount ";
  stmt = stmt + " ,t_Account_Payer ";
  stmt = stmt + " ,t_Account_Receiver ";
  stmt = stmt + " ,t_Numb_Document ";
  stmt = stmt + " ,t_Sum ";
  stmt = stmt + " ,t_DprtID ";
  stmt = stmt + ") ";
  stmt = stmt + "SELECT  ";
  stmt = stmt + "  t_CarryID ";
  stmt = stmt + " ,t_Result_Carry ";
  stmt = stmt + " ,t_Date_Carry ";
  stmt = stmt + " ,t_Chapter ";
  stmt = stmt + " ,t_Currency ";
  stmt = stmt + " ,t_ExpAccount ";
  stmt = stmt + " ,t_Account_Payer ";
  stmt = stmt + " ,t_Account_Receiver ";
  stmt = stmt + " ,t_Numb_Document ";
  stmt = stmt + " ,t_Sum ";
  stmt = stmt + " ,t_DprtID ";
  stmt = stmt + "  FROM drevalueacctrn_dbt ";
  stmt = stmt + " WHERE t_ParamID = " + ParamID;
  stmt = stmt + "   AND t_DprtID = " + DprtID;
  
  cmd = RsdCommand(stmt);
  cmd.execute();
end;

private macro _PrintRegistryToFile(ParamID, PanelObj, ToFileName)
  var RestIn = false;
  var RestOut = false;
  var DprtsRS = _GetSelectedDprtsRS(PanelObj);

  if(PanelObj.RestKind.ID == 1)
    RestIn = true;
  else
    RestOut = true;
  end;
  setoutput(ToFileName, true);
  while(DprtsRS.movenext)
    _TruncateTable("dovest_carry_log_tmp");
    _InsertIntoCarryLog(ParamID, DprtsRS.value(0));
    PrintCarryLogExt(DprtsRS.value(0), RestIn, RestOut, PanelObj.Backout);
  end;
  setoutput(null, true);
end;

private macro _PrintOrdersToFile(ParamID, PanelObj, ToFileName)
  var Chapter = 0;
  var RestIn = false;
  var RestOut = false;
  var RegDate = PanelObj.RegulateDate;
  var DprtsRS = _GetSelectedDprtsRS(PanelObj);
  var i;

  setoutput(ToFileName, true);
  if (GenClassName (PanelObj.RestKind) == "TArray")
    i = 0;
    while (i < PanelObj.RestKind.size)
      if(PanelObj.RestKind[i].ID == 1)
        RestIn = true;
      end;

      if((PanelObj.RestKind[i].ID == 2) or (PanelObj.RestKind[i].ID == 3))
        RestOut = true;
      end;
      i = i + 1;
    end;
  else
    if(PanelObj.RestKind.ID == 1)
      RestIn = true;
    else
      RestOut = true;
    end;
  end;

  while(DprtsRS.movenext)
    _TruncateTable("dovest_carry_log_tmp");
    _InsertIntoCarryLog(ParamID, DprtsRS.value(0));
    if(PanelObj.IsAllChaptersSelected)
      ReportMO_SQL(true, Chapter, RegDate, RestIn, RestOut, DprtsRS.value(0));
    else
      i = 0;
      while(i < PanelObj.ChapterListSelected.size())
        Chapter = PanelObj.ChapterListSelected[i].Chapter;
        ReportMO_SQL(true, Chapter, RegDate, RestIn, RestOut, DprtsRS.value(0));
        i = i + 1;
      end;
    end;
  end;
  setoutput(null, true);
end;

private macro _PrintReportTop(PanelObj)
  var info = "";
  var i, k;

  if(PanelObj.Backout)
    info = info + "Протокол удаления проводок переоценки\n";
  else
    info = info + "Протокол выполнения процедуры переоценки\n";
  end;
  info = info + "\n";
  info = info + "Дата формирования: " + string(date():f) + "\n";
  info = info + "Время формирования: " + string(time():f) + "\n";
  info = info + "Операционист: " + {oper} + " " + {Name_Oper} + "\n";
  info = info + "\n";
  info = info + "Параметры запуска:\n";
  info = info + "  Дата операционного дня: " + string(PanelObj.RegulateDate:f) + "\n"; 
  if(PanelObj.IsAllDprtsSelected == true)
    info = info + "  Узел ТС: Все филиалы\n";
  else
    info = info + "  Узел ТС: " + PanelObj.Department.Name_ + " " + PanelObj.Department.PartyName + "\n";
  end;
  if(PanelObj.IsAllChaptersSelected)
    info = info + "  Глава счетов: Все главы\n";
  elif((PanelObj.ChapterListSelected != null) and (PanelObj.ChapterListSelected.size() > 0))
    i = 0;
    k = PanelObj.ChapterListSelected.size();
    while(i < k)
      if(i == 0)
        info = info + "  Глава счетов: " + PanelObj.ChapterListSelected[i].Chapter + " (" + PanelObj.ChapterListSelected[i].ChapterName + ")";
      else
        info = info + "                " + PanelObj.ChapterListSelected[i].Chapter + " (" + PanelObj.ChapterListSelected[i].ChapterName + ")";
      end;
      i = i + 1;
      if(i < k)
        info = info + ",\n";
      else
        info = info + "\n";
      end;
    end;
  end;

  if(PanelObj.IsAllFIKindsSelected)
    info = info + "  Вид ФИ: Все\n";
  elif(PanelObj.FIKind != null)
    info = info + "  Вид ФИ: " + PanelObj.FIKind.Name + "\n";
  end;

  if(PanelObj.IsAllAccKindsSelected)
    info = info + "  Вид счетов: Все\n";
  elif(PanelObj.AccKind != null)
    info = info + "  Вид счетов: " + PanelObj.AccKind.Name + "\n";
  end;

  if(PanelObj.IsAllFIsSelected)
    info = info + "  Финансовый инструмент: Все\n";
  elif((PanelObj.FIListSelected != null) and (PanelObj.FIListSelected.size() > 0))
    i = 0;
    k = PanelObj.FIListSelected.size();
    while(i < k)
      if(i == 0)
        info = info + "  Финансовый инструмент: " + PanelObj.FIListSelected[i].CodeWidget + " (" + PanelObj.FIListSelected[i].Name + ")";
      else
        info = info + "                         " + PanelObj.FIListSelected[i].CodeWidget + " (" + PanelObj.FIListSelected[i].Name + ")";
      end;
      i = i + 1;
      if(i < k)
        info = info + ",\n";
      else
        info = info + "\n";
      end;
    end;
  end;

  if((PanelObj.Account != null) and (PanelObj.Account.Account != null))
    info = info + "  Переоцениваемый счет: " + PanelObj.Account.Account + "\n";
  else
    info = info + "  Переоцениваемый счет:\n";
  end;

  if(PanelObj.RegCarry)
    info = info + "  Урегулирование проводок: Да\n";
  else
    info = info + "  Урегулирование проводок: Нет\n";
  end;

  info = info + "  Номер первого документа: " + PanelObj.FirstCarryNumber + "\n";

  if(PanelObj.PrintReport)
    info = info + "  Печать реестра: Да\n";
  else
    info = info + "  Печать реестра: Нет\n";
  end;

  if(PanelObj.PrintOrders)
    info = info + "  Печать сводных ордеров: Да\n";
  else
    info = info + "  Печать сводных ордеров: Нет\n";
  end;

  info = info + "\n";

  print(info);
end;

private macro _GetDprtPartyName( DprtID : integer )
  record party("party.dbt");
  file dp_dep("dp_dep.dbt") key 0;
  var DprtPartyName = "";
  dp_dep.Code = DprtID;
  if( GetEQ(dp_dep) )
    if( ПолучитьСубъекта(dp_dep.PartyID, party) == 0 )
      DprtPartyName = party.Name;
    end;
  end;
  return DprtPartyName;
end;

private macro _GetDprt( DprtID : integer, dprt )
  file dp_dep("dp_dep.dbt") key 0;
  dp_dep.Code = DprtID;
  if( GetEQ( dp_dep ))
    copy( dprt, dp_dep );
  end;
end;

private macro _GetErrorLogRS(ParamID, DprtID, RegulateDate)
  var query = "";
  var cmd, rs;
  query = query + "SELECT t_Error, t_Message ";
  query = query + "  FROM drevalueacclog_dbt ";
  query = query + " WHERE t_ParamID = :ParamID ";
  query = query + "   AND t_DprtID = :DprtID ";
  query = query + "   AND t_RegulateDate = :RegulateDate ";
  query = query + " ORDER BY t_ParamID, t_RecID, t_LogID ";
  cmd = RsdCommand(query);
  cmd.addParam("ParamID", RSDBP_IN, ParamID);
  cmd.addParam("DprtID", RSDBP_IN, DprtID);
  cmd.addParam("RegulateDate", RSDBP_IN, RegulateDate);
  rs = RsdRecordset(cmd);
  return rs;
end;

private macro _PrintReportDprt(ParamID, DprtID, PanelObj)
  var info = "";
  var ErrorLogRS;
  var i;
  var LastCurDate = date(0,0,0);
  var FirstOpenDate = date(0,0,0);
  var RegDate = PanelObj.RegulateDate;
  var localErrors;
  var dateErrors;
  var ProcName = IfThenElse(PanelObj.Backout, "удаления проводок", "выполнения");
  var RestKind = "";

  if (GenClassName (PanelObj.RestKind) == "TArray") 
    var j = 0;
    while (j < PanelObj.RestKind.size)
      if (RestKind != "")
        RestKind = RestKind + ", ";
      end;
      RestKind = RestKind + IfThenElse((PanelObj.RestKind[j].ID == 1), "входящего", "");

      if ((PanelObj.RestKind[j].ID == 2) or (PanelObj.RestKind[j].ID == 3))
        RestKind = RestKind + "исходящего";
      end;
      j = j + 1;
    end;
  else
    RestKind = IfThenElse((PanelObj.RestKind.ID == 1), "входящего", "исходящего");
  end;

  if(PanelObj.IsAllDprtsSelected == true)
    info = info + "Узел ТС - " + _GetDprtPartyName(DprtID) + "\n";
  end;

  CB_GetLastOpenCurDateForDprt(DprtID, @LastCurDate, null);
  CB_GetFirstOpenDate(DprtID, @FirstOpenDate);

  RegDate = IfThenElse((RegDate < FirstOpenDate), FirstOpenDate, RegDate);
  
  localErrors = 0;
  while(RegDate <= LastCurDate)
    
    info = info + "\n  Результаты " + ProcName + " переоценки " + RestKind + " остатка за " + string(RegDate:f) + ":" + "\n";
    
    ErrorLogRS = _GetErrorLogRS(ParamID, DprtID, RegDate);
    dateErrors = 0;
    while(ErrorLogRS.movenext)
      if(ErrorLogRS.value(0) > 0)
        localErrors = localErrors + 1; 
        dateErrors = dateErrors + 1; 
        _countErrors = _countErrors + 1;

        info = info + "    " + dateErrors + ". ";
        info = info + ErrorLogRS.value(1) + "\n";
      else
        info = info + ErrorLogRS.value(1);
      end;
    end;

    if(dateErrors > 0)
      info = info + "  Не выполнено из-за ошибок\n";
    else
      info = info + "  Выполнено успешно\n";
    end;

    if(PanelObj.AllDays)
      RegDate = RegDate + 1;
    else
      RegDate = LastCurDate + 1;
    end;
  end;

  info = info + "Ошибок переоценки: " + localErrors + ".\n";

  info = info + "\n";

  print(info);
end;

private macro _PrintReportBottom(PanelObj)
  var info = "";
  info = info + "Итоговый результат:\n";
  if(PanelObj.IsAllDprtsSelected == true)
    if(_countErrors > 0)
      info = info + "Процедура выполнена не во всех филиалах.\n";
    else
      info = info + "Процедура выполнена во всех филиалах.\n";
    end;
    print(info);
  end;
end;

private macro _PrintReportToFile(ParamID, PanelObj, ToFileName)
  var DprtsRS = _GetSelectedDprtsRS(PanelObj);

  _countErrors = 0;

  setoutput(ToFileName, true);
  _PrintReportTop(PanelObj);

  while(DprtsRS.movenext)
    _PrintReportDprt(ParamID, DprtsRS.value(0), PanelObj);
  end;

  _PrintReportBottom(PanelObj);
  setoutput(null, true);
end;

private macro _GetReportFileName(ParamID)
  var ToFileName = GetTxtFileName("revalueacc_" + ParamID);
  return ToFileName;
end;

macro WebCore_GetReValuationReport(ParamID)
  var ToFileName = _GetReportFileName(ParamID);

  var info;

  setoutput(ToFileName);
  setoutput(null, true);
  var PanelObj;
  ConvertToRSL(_GetParamXML(ParamID), @PanelObj);
  info = _FormFisclogInfo(PanelObj);
  
  if(PanelObj.Backout)
    WriteFiscLog(UOL_E_COCUNS_BACK_PROC_FISCLOG, info);
  else
    WriteFiscLog(UOL_E_COCUNS_PROC_FISCLOG, info);
  end;
  _PrintReportToFile(ParamID, PanelObj, ToFileName);
  if(PanelObj.PrintReport)
    _PrintRegistryToFile(ParamID, PanelObj, ToFileName);
  end;
  if(PanelObj.PrintOrders)
    _PrintOrdersToFile(ParamID, PanelObj, ToFileName);
  end;
  /*
  return ToFileName;
  */
  var fc = null;
  
  if(ToFileName != "")

    fc = ConvertTxt2Html(ToFileName);
    
    fc = GetTextFile(fc);

  end;

  _responsebuilder = WebResponseBuilder();

  _responsebuilder.SetUserObject(fc);
  
  return _responsebuilder.Result;
end;

macro GetReValuationReport(ParamID)
  var ToFileName = _GetReportFileName(ParamID);

  var info;
  
  var PanelObj;
  ConvertToRSL(_GetParamXML(ParamID), @PanelObj);
  info = _FormFisclogInfo(PanelObj);
  
  if(PanelObj.Backout)
    WriteFiscLog(UOL_E_COCUNS_BACK_PROC_FISCLOG, info);
  else
    WriteFiscLog(UOL_E_COCUNS_PROC_FISCLOG, info);
  end;
  _PrintReportToFile(ParamID, PanelObj, ToFileName);
  if(PanelObj.PrintReport)
    _PrintRegistryToFile(ParamID, PanelObj, ToFileName);
  end;
  if(PanelObj.PrintOrders)
    _PrintOrdersToFile(ParamID, PanelObj, ToFileName);
  end;
  
 
  return ToFileName;

end;
