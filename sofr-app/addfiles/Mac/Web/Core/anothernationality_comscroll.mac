 /*
 $Name: anothernationality_comscroll.mac
 $Module: Главная книга
 $Description: Макрос совмещенный скроллинг иных гражданств физлица
 */
 
import oralib;
import PTInter;
import "commonutil.mac";
import "ws_validation.mac";
import "ws_llvalues.mac";
import "cb_sql.mac";
import "ws_getparty.mac";
import "ws_partycommon.mac";
import likepy;


class TWebAnotherNationalityFlCsPanelGridItem
  var CountryCodeLat3 : string;
  var CountryName     : string;
  var TerritoryName       : string;
  var IsOffshore      : bool;
end;

class TWebAnotherNationalityFlCsPanel
  var Country;       // TWsCountry 
  var OffshoreName ; // TWsPartyCountry
  var PersonIdc; // 
  var PaperKindNameStr : string = "";
end;

private macro LoadRsbErrors(responseBuilder : WebResponseBuilder, stat:integer)
  var objErr = AL_GetErrorInfo();
  if( (objErr.stat == 0) AND (stat != 0) )
    CreateErrMsg(stat);
    objErr = AL_GetErrorInfo();
  end;
  responseBuilder.AddErrorEx(objErr.stat, MessageSeverity.RuntimeError, objErr.Message);
end;

macro WebCore_GetAnotherNationalityFlGridItemsList(partyId : integer)

  CaptureOutput();
[
SELECT ct2.t_codelat3 CountryCodeLat3,
       ct2.t_name countryName,
       terr.t_name territoryName,
       case ct2.t_territoryid 
        when 0 then NVL2(ct2.t_offshoregroup, 'X', chr(0))  /*check is offshore when no subterritory*/
        else NVL2(terrO.t_offshoregroup, 'X', chr(0))       /*check is offshore for subterritory*/
       end IsOffshore       
  FROM (SELECT P.T_TERRITORYID,
               CT.T_COUNTRYID,
               ct.t_codelat3,
               ct.t_name,
               O.T_OFFSHOREGROUP
          FROM (SELECT T_COUNTRYCODELAT3, T_TERRITORYID
                  FROM dpersncit_dbt
                 WHERE t_personid = :pid) p
               INNER JOIN dcountry_dbt ct ON CT.T_CODELAT3 = P.T_COUNTRYCODELAT3
               LEFT OUTER JOIN DOFSHHIST_DBT O ON O.T_COUNTRYID = CT.T_COUNTRYID
       ) ct2
       LEFT JOIN dcountry_dbt terr ON (    terr.t_parentcountryid = ct2.t_countryid
                                            AND TERR.T_COUNTRYID = ct2.T_TERRITORYID)
       LEFT OUTER JOIN DOFSHHIST_DBT terrO ON terrO.T_COUNTRYID = terr.T_COUNTRYID
];
  var sql = StopCaptureOutput();

  var cmd = execSQL(sql, MakeArray(SQLParam("pid", partyid)));
  var ds = TRsbDataSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  var arr = TArray();
  
  while (ds.MoveNext())
    var p = TWebAnotherNationalityFlCsPanelGridItem();
    p.CountryCodeLat3 = ds.CountryCodeLat3;
    p.CountryName     = ds.CountryName;     
    p.TerritoryName   = ds.TerritoryName;       
    p.IsOffshore      = ds.IsOffshore;
    
    arr[arr.size] = p;
 end;

  return arr;  
end;


macro WebCore_GetTWebAnotherNationalityFlCsPanel(partyId : integer, countryCodeLat3:string )  // TWebAnotherNationalityFlCsPanel

  var panelobj = TWebAnotherNationalityFlCsPanel();
  var party = RsbParty(partyid);
  var pcit  = party.PersonCitizen();
  var stat:integer = pcit.GetByCountryLat3(countryCodeLat3);    

  if(stat == 0)
    // COUNTRY & OFFSHORE
    CaptureOutput();
    [
      SELECT c.*,
             O.T_OFFSHOREGROUP,
             (SELECT T_COUNTRYID
                FROM dcountry_dbt t
               WHERE     t.t_parentcountryid = c.t_countryid
                     AND t.T_COUNTRYID = :terrid)
                offshoreid
        FROM dcountry_dbt c
             LEFT OUTER JOIN DOFSHHIST_DBT O ON O.T_COUNTRYID = C.T_COUNTRYID
       WHERE c.T_CODELAT3 = :code
    ];
    var sql = StopCaptureOutput();
    var cmd = execSQL(sql, MakeArray(  SQLParam("terrid", pcit.TERRITORYID)
                                     , SQLParam("code", countryCodeLat3)));
    var ds = TRsbDataSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);

    if (ds.MoveNext())    // --если задан T_TERRITORYID, вычитываем оффшорную зону. Если не задан, вычитываем просто страну
      var obj = CreateCountryFromParam(
         ds.CountryId    
        ,ds.ParentCountryId     
        ,ds.Name         
        ,ds.FullName     
        ,ds.CodeCyr3     
        ,ds.CodeLat3     
        ,ds.CodeLat2     
        ,ds.CodeNum3      
        ,ds.OffshoreGroup
        ,TArray()     
      );
      panelobj.Country = obj;
      panelobj.OffshoreName = CU_IIF(ds.offshoreid == null,
                                        null,
                                        CreateCountryFromParam(ds.offshoreid)); // ТК приведет TWsCountry к TWsPartyCountry.
    end;
    
    
    // PERSON IDC
    CaptureOutput();
    [
      SELECT *
        FROM dpersnidc_dbt i
             INNER JOIN dpaprkind_dbt k ON I.T_PAPERKIND = K.T_PAPERKIND
       WHERE I.T_PERSONID = :pid AND K.T_PAPERKIND = :ppkid
    ];
    sql = StopCaptureOutput();
    var rs = execSQLSelect(sql, MakeArray(  SQLParam("pid", partyid)
                                          , SQLParam("ppkid", pcit.PAPERKIND)));

    var pidc = TRecHandler("persnidc");
    pidc.rec.PAPERKIND = pcit.PAPERKIND;
    if (rs.MoveNext())   
      _CopyRecordsetToRecHandler(rs, pidc);
    end;
    panelobj.PersonIdc = pidc.rec;
    
    
    
    // PaperKindNameStr
    if(pcit.PAPERKIND > 0)
      var rs2 = execSQLSelect("SELECT t_name   FROM dpaprkind_dbt WHERE t_paperkind = :ppkid"
                              , MakeArray(SQLParam("ppkid", pcit.PAPERKIND)));
      if(rs2.MoveNext())
        panelobj.PaperKindNameStr = SQL_ConvTypeStr(rs2.value("t_name"));
      end;
    end;
  
  end; // pcit != null 
  
  return panelobj;
  
end;

macro WebCore_PT_DeleteAnotherNationality(partyId : integer, countryCodeLat3:string) : WebServiceResponse

  var responseBuilder = WebResponseBuilder();
  var stat :integer= 0;
  var party = RsbParty(PartyId);
  var pcit = party.PersonCitizen();
  
  stat = pcit.GetByCountryLat3(countryCodeLat3);
  
  if(stat == 0)
    if( (stat = pcit.Delete()) == 0);
      var stat2 = party.Update();
      if(not stat2)
        stat = -1;
      end;
    end;
  end;
  
  if(stat != 0)
    LoadRsbErrors(responseBuilder, stat);
  end;
  
  responseBuilder.SetUserObject(stat);
  
  return responseBuilder.Result();
end;

private macro IsCountryExists(party, codeLat3 : string):bool
  
  var pcit = party.PersonCitizen();
  var stat : integer = pcit.GetByCountryLat3(codeLat3);
    
  if(stat == 0) // same country exists
    return true;
  else
    return false;
  end;
  
end;

private macro SavePersonNationality(party, model, oldCodeLat3:string, responseBuilder : WebResponseBuilder):integer
  
  var stat:integer = 0;
                       
                                
  var pcit = party.PersonCitizen();

  var pcitrec = TRecHandler("persncit");
  
  pcitrec.rec.COUNTRYCODELAT3 = model.Country.CodeLat3;
  if(Model.OffshoreName == null)
    pcitrec.rec.TERRITORYID = 0;
  else 
    pcitrec.rec.TERRITORYID = Model.OffshoreName.countryid;
  end;
  
  if(Model.PersonIdc == null)
    pcitrec.rec.PAPERKIND = -1;
  else 
    pcitrec.rec.PAPERKIND = Model.PersonIdc.paperkind;
  end;
  
  if(oldCodeLat3 == null)
    /* insert */
    if(IsCountryExists(party, pcitrec.rec.COUNTRYCODELAT3)) 
      stat = 1008; // #define COUNTRY_ERR 1008
    else
      stat = pcit.Add(pcitrec.rec.COUNTRYCODELAT3,
                      pcitrec.rec.TERRITORYID,
                      pcitrec.rec.PAPERKIND);
    end;
    
  else
    /* update */    
    if(oldCodeLat3 != model.Country.CodeLat3) // поменяли страну в режиме редактирования
      stat = pcit.GetByCountryLat3(oldCodeLat3);
      
      if(stat == 0)
        stat = pcit.Delete();
      end;
      
      if(stat == 0)
        stat = pcit.Add(pcitrec.rec.COUNTRYCODELAT3,
                        pcitrec.rec.TERRITORYID,
                        pcitrec.rec.PAPERKIND);
      end;
    else
      stat = pcit.GetByCountryLat3(oldCodeLat3);
      if(stat == 0)
        pcit.TERRITORYID = pcitrec.rec.TERRITORYID;
        pcit.PAPERKIND = pcitrec.rec.PAPERKIND
      end;
      
    end;
  
  end;
  

  return stat;
end;



private macro SavePersonPaper(party, wPersonIDC, responseBuilder : WebResponseBuilder):integer
  record persnidcRecord("persnidc.dbt");

  clearRecord(persnidcRecord);
  
  var stat:integer = 0;
  var PersonPaper = Party.PersonPaper(wPersonIDC.paperkind);


  if(wPersonIDC.action != PT_REMOVE)

    PersonPaper.IsMain     = wPersonIDC.ismain;
    PersonPaper.IssuedDate = wPersonIDC.paperissueddate;
    PersonPaper.Issuer     = wPersonIDC.paperissuer;
    PersonPaper.IssuerCode = wPersonIDC.paperissuercode;
    PersonPaper.Number     = wPersonIDC.papernumber;
    // PersonPaper.PaperKind  = wPersonIDC.paperkind;
    PersonPaper.Series     = wPersonIDC.paperseries;


    if((wPersonIDC.action != PT_REMOVE) )

      persnidcRecord.personid        = wPersonIDC.personid;
      persnidcRecord.paperkind       = wPersonIDC.paperkind;
      persnidcRecord.ismain          = wPersonIDC.ismain;
      persnidcRecord.paperissueddate = wPersonIDC.paperissueddate;
      persnidcRecord.paperissuer     = wPersonIDC.paperissuer;
      persnidcRecord.paperissuercode = wPersonIDC.paperissuercode;
      persnidcRecord.papernumber     = wPersonIDC.papernumber;
      persnidcRecord.paperseries     = wPersonIDC.paperseries;


      var stat2 = PT_CheckPartyPapers(persnidcRecord, null);

      if(stat2 != true)
        stat = -1;
      end;
    end;
    
  end;

  return stat;
end;

macro WebCore_PT_InsUpdAnotherNationality(partyId :integer, model, oldCodeLat3:string, papers) : WebServiceResponse
  

  var responseBuilder = WebResponseBuilder();
  var stat :integer= 0;
  var party = RsbParty(PartyId);

  if( stat == 0 )
    stat = SavePersonPaper(party, papers, responseBuilder);
  end;  
  
  if( stat == 0 )
    stat = SavePersonNationality(party, model, oldCodeLat3, responseBuilder)
  end;
  
  if(stat != 0)
    LoadRsbErrors(responseBuilder, stat);
  else
    var stat2 = party.Update();
    if(not stat2)
      LoadRsbErrors(responseBuilder);
    end;
  
  end;

  responseBuilder.SetUserObject(stat);
  
  return responseBuilder.Result();
end;



// var arr = WebCore_GetTWebAnotherNationalityFlCsPanel(13, "MYS");
// var c = 5;


  // var party = RsbParty(13);
  // var pcit = party.PersonCitizen();
  // var stat = pcit.GetByCountryLat3();
  
  // var c = 6;
