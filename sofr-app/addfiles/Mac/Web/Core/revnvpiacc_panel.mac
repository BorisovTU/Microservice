 /*
 $Name: revnvpiacc_panel.mac
 $Module: Главная книга
 $Description: Макрос Переоценки НВПИ
 */

import globals;
import "ws_validation.mac";
import "ws_dprt.mac";
import "ws_file.mac";
import serviceaction;
import cb_sql;
import rsd, oralib, likepy;
import SPInter;
import BankInter;
import PTInter;
import "nvpirep.mac";

class TWebNameKind
  var ID   /*: integer*/;
  var Name /*: string*/;
end;

class TWebReVNVPIAccPrm
  var ParamID : integer  /*: integer*/;
  var RecNumber : integer /*: integer*/;
end;

class TWebReVNVPIAccObject
  var OperCanExecServProcHead /*: bool*/;
  var RegulateDate            /*: date*/;
  var Department              /*: TWsDepartment*/;
  var IsAllDprtsSelected      /*: bool*/;
  var ChapterListSelected     /*: Array<TWebChapter>*/;
  var IsAllChaptersSelected   /*: bool*/;
  var AccKindList             /*: List<TWebNameKind>*/;
  var AccKind                 /*: TWebNameKind/integer*/;
  var IsAllAccKindsSelected   /*: bool*/;
  var IsAllObligFIsSelected    /*: bool*/;
  var ObligFIListSelected          /*: Array<TWsFinInstr>*/;
  var IsAllEqvFIsSelected    /*: bool*/;
  var EqvFIListSelected          /*: Array<TWsFinInstr>*/;
  var Account                 /*: TWebAccount*/;
  var IsAllAccountsSelected   /*: bool*/;
  var FirstDocNumber          /*: integer*/;
  var PrintReport             /*: bool*/;
  var Backout                 /*: bool*/;
end;

private var _countErrors;

private var _responsebuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)
  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  _responsebuilder.AddMessage( ErrorMessage );
end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)
  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  _responsebuilder.AddMessage( ErrorMessage );
end;

macro WebCore_CheckNVPI_ReValuationPanelCallRights()
  var res = true;
  var err;
  var regVal;

  _responsebuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  if(res and ({cTypePerson} == TP_BOSS))
    res = false;
    AddErrorMessageExt(@ErrorMessage, GetErrorMsg(548));
  end;

  _responseBuilder.SetUserObject(res);

  return _responseBuilder.Result;
end;

macro WebCore_InitNVPI_ReValuationPanel(Backout)
  var panelObj  = TWebReVNVPIAccObject();
  var FirstDate = date(0, 0, 0);
  var err;
  var res = CB_GetFirstOpenDate({OperDprt}, @FirstDate);
  
  if(res == true)
    panelObj.RegulateDate = FirstDate;
  end;
  
  panelObj.Department = GetTWsDepartmentByCode({OperDprt});
  panelObj.IsAllDprtsSelected = false;

  GetRegistryValue("COMMON\\СЕРВИСНЫЕ ПРОЦЕДУРЫ\\ЗАПУСК СЕРВИСНЫХ ПРОЦЕДУР", V_BOOL, res, err);

  if(err == 0)
    if((res == true) and ({OperDprt} == {NumDprt}))
      panelObj.OperCanExecServProcHead = true;
    else
      panelObj.OperCanExecServProcHead = false;
    end;
  else
    RunError("Ошибка при чтении настройки реестра!");
  end; 

  panelObj.IsAllChaptersSelected = true;

  panelObj.AccKindList = TArray();
  panelObj.AccKindList[0] = TWebNameKind();
  panelObj.AccKindList[0].ID   = 1;
  panelObj.AccKindList[0].Name = "активные";
  panelObj.AccKindList[1] = TWebNameKind();
  panelObj.AccKindList[1].ID   = 2;
  panelObj.AccKindList[1].Name = "пассивные";

  panelObj.IsAllAccKindsSelected = true;

  panelObj.IsAllObligFIsSelected = true;
  panelObj.IsAllEqvFIsSelected = true;
  panelObj.IsAllAccountsSelected = true;

  panelObj.FirstDocNumber = 1; 
  panelObj.Backout = Backout; 

  panelObj.PrintReport = true; 

  return panelObj;
end;

macro WebCore_GetReVNVPIAccPanel(Department, IsAllDprtSelected)
  var res;
  var RegulateDate;
  if(IsAllDprtSelected == true)
    res = CB_GetFirstOpenDate(0, @RegulateDate);
  else
    if(Department != null)
      res = CB_GetFirstOpenDate(Department.Code, @RegulateDate);
    end;
  end;
  return RegulateDate;
end;

private macro _InsertParamXML(XML : string, NumberDoc)
  var query = "";  
  var cmd;
  var ID;
  
  query = query + "INSERT INTO drevnvpiaccprm_dbt ( t_ParamXML, t_SysDate, t_SysTime, t_NextNumberDoc ) ";
  query = query + "VALUES ( :XML, TRUNC(SYSDATE), TO_DATE('01-01-0001:' || TO_CHAR(SYSDATE, 'HH24:MI:SS'), 'DD-MM-YYYY:HH24:MI:SS'), :NumberDoc ) ";
  query = query + "RETURNING t_ParamID INTO :ID ";
  cmd = RsdCommand(query);
  cmd.addParam("XML", RSDBP_IN, XML);
  cmd.addParam("NumberDoc", RSDBP_IN, NumberDoc);
  cmd.addParam("ID", RSDBP_OUT, V_INTEGER);
  cmd.execute();
  
  ID = cmd.value("ID");
  return ID;
end;

private macro _GetParamXML(ID : integer)
  var query = "";
  var ParamXML;

  query = query + "SELECT ";
  query = query + "  t_ParamID, t_ParamXML "; /*!!! CLOB-поле не должно быть первым*/
  query = query + "  FROM drevnvpiaccprm_dbt ";
  query = query + " WHERE t_ParamID = " + ID;
  /*!!!важно!!! инструмент требует, чтобы CLOB-поле не было первым в SELECT'е, иначе ошибка выполнения*/
  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if (ds.MoveNext())
    ParamXML = ds.ParamXML;
  end;
  return ParamXML;
end;

private macro _GetCountAccounts(ID : integer)
  var Count;

  var query = "";
  var cmd, rs;

  query = query + "SELECT COUNT(*) ";
  query = query + "  FROM drevnvpiaccsel_dbt ";
  query = query + " WHERE t_ParamID = " + ID;
  
  cmd = RsdCommand(query);

  rs = RsdRecordset(cmd);

  if(rs.movenext)
    Count = rs.value(0);
  end;

  return Count;
end;

private macro _FormFisclogInfo(PanelObj)
  var info = "";
  var i, k;

  if(PanelObj.Backout)
    info = info + "Откат переоценки НВПИ.\n";
  else
    info = info + "Переоценка НВПИ.\n";
  end;

  if(PanelObj.IsAllDprtsSelected == true)
    info = info + "Узел ТС: Все филиалы\n";
  else
    info = info + "Узел ТС: " + PanelObj.Department.Name_ + " " + PanelObj.Department.PartyName +"\n";
  end;

  info = info + "Дата переоценки: " + string(PanelObj.RegulateDate:f) + "\n";
  
  if(PanelObj.IsAllChaptersSelected)
    info = info + "Глава счетов: Все главы\n";
  elif((PanelObj.ChapterListSelected != null) and (PanelObj.ChapterListSelected.size() > 0))
    i = 0;
    k = PanelObj.ChapterListSelected.size();
    while(i < k)
      if(i == 0)
        info = info + "Глава счетов: " + PanelObj.ChapterListSelected[i].Chapter + " (" + PanelObj.ChapterListSelected[i].Name + ")";
      else
        info = info + "              " + PanelObj.ChapterListSelected[i].Chapter + " (" + PanelObj.ChapterListSelected[i].Name + ")";
      end;
      i = i + 1;
      if(i < k)
        info = info + ",\n";
      else
        info = info + "\n";
      end;
    end;
  end;

  if(PanelObj.IsAllAccKindsSelected)
    info = info + "Вид счетов: Все\n";
  elif(PanelObj.AccKind != null)
    info = info + "Вид счетов: " + PanelObj.AccKind.Name + "\n";
  end;

  if(PanelObj.IsAllObligFIsSelected)
    info = info + "Код валюты обязательства: Все\n";
  elif((PanelObj.ObligFIListSelected != null) and (PanelObj.ObligFIListSelected.size() > 0))
    i = 0;
    k = PanelObj.ObligFIListSelected.size();
    while(i < k)
      if(i == 0)
        info = info + "Код валюты обязательства: " + PanelObj.ObligFIListSelected[i].CodeWidget + " (" + PanelObj.ObligFIListSelected[i].Name + ")";
      else
        info = info + "                          " + PanelObj.ObligFIListSelected[i].CodeWidget + " (" + PanelObj.ObligFIListSelected[i].Name + ")";
      end;
      i = i + 1;
      if(i < k)
        info = info + ",\n";
      else
        info = info + "\n";
      end;
    end;
  end;

  if(PanelObj.IsAllEqvFIsSelected)
    info = info + "Код валюты-эквивалента: Все\n";
  elif((PanelObj.EqvFIListSelected != null) and (PanelObj.EqvFIListSelected.size() > 0))
    i = 0;
    k = PanelObj.EqvFIListSelected.size();
    while(i < k)
      if(i == 0)
        info = info + "Код валюты-эквивалента: " + PanelObj.EqvFIListSelected[i].CodeWidget + " (" + PanelObj.EqvFIListSelected[i].Name + ")";
      else
        info = info + "                        " + PanelObj.EqvFIListSelected[i].CodeWidget + " (" + PanelObj.EqvFIListSelected[i].Name + ")";
      end;
      i = i + 1;
      if(i < k)
        info = info + ",\n";
      else
        info = info + "\n";
      end;
    end;
  end;

  if((PanelObj.Account != null) and (PanelObj.Account.Account != null))
    info = info + "Переоцениваемый счет: " + PanelObj.Account.Account + "\n";
  else
    info = info + "Переоцениваемый счет:\n";
  end;

  info = info + "Номер первого документа: " + PanelObj.FirstDocNumber + "\n";

  if(PanelObj.PrintReport)
    info = info + "Печать реестра: Да\n";
  else
    info = info + "Печать реестра: Нет\n";
  end;

  return info;
end;

macro WebCore_MakeNVPI_ReValuationAccList(PanelObj)
  var retObj = TWebReVNVPIAccPrm;
  var xml;
  var err;
  var stmt = "";
  var i;
  var cmd;
  var info;
  
  err = ConvertToXML(PanelObj, null, xml);
  if(not err)
    retObj.ParamID = _InsertParamXML(xml, PanelObj.FirstDocNumber);
  end;

  stmt = stmt + "INSERT INTO drevnvpiaccsel_dbt ";
  stmt = stmt + "( t_ParamID ";
  stmt = stmt + " ,t_RecID ";
  stmt = stmt + " ,t_Account ";
  stmt = stmt + " ,t_Chapter ";
  stmt = stmt + " ,t_Code_Currency ";
  stmt = stmt + " ,t_Department ";
  stmt = stmt + " ,t_Branch ";
  stmt = stmt + " ,t_Error ";
  stmt = stmt + " ,t_ErrMsg ";
  stmt = stmt + ") ";
  stmt = stmt + "SELECT ";
  stmt = stmt + " " + retObj.ParamID;
  stmt = stmt + " ,ROWNUM ";
  stmt = stmt + " ,acc.t_Account ";
  stmt = stmt + " ,acc.t_Chapter ";
  stmt = stmt + " ,acc.t_Code_Currency ";
  stmt = stmt + " ,acc.t_Department ";
  stmt = stmt + " ,acc.t_Branch ";
  stmt = stmt + " ,0 ";
  stmt = stmt + " ,CHR(1) ";
  stmt = stmt + "  FROM daccount_dbt acc ";
  stmt = stmt + " WHERE acc.t_Open_Date <= TO_DATE('" + string(PanelObj.RegulateDate:f) + "', 'DD.MM.YYYY') ";
  stmt = stmt + "   AND acc.t_Close_Date = TO_DATE('01.01.0001', 'DD.MM.YYYY') ";
  stmt = stmt + "   AND acc.t_Type_Account LIKE '%В%' /*TA_NVPI - Счет с НВПИ*/";

  if((PanelObj.Account != null) and (PanelObj.Account.Account != null))
    if(Web_StringIsMaskEx(PanelObj.Account.Account, CSMFL_Mask ))
      stmt = stmt + "   AND " + ConvertMaskToSQLFormat(PanelObj.Account.Account, "acc.t_Account", CSMFL_Mask);
    else
      stmt = stmt + "   AND acc.t_Account = '" + PanelObj.Account.Account + "' ";
      stmt = stmt + "   AND acc.t_Chapter = " + PanelObj.Account.Chapter + " ";
      stmt = stmt + "   AND acc.t_Code_Currency = " + PanelObj.Account.Currency + " ";
    end;
  end;

  if(not PanelObj.IsAllDprtsSelected)
    if(PanelObj.Department.NodeType == 1)
      stmt = stmt + "   AND acc.t_Department = " + PanelObj.Department.Code;
    else
      stmt = stmt + "   AND acc.t_Branch = " + PanelObj.Department.Code;
    end;
  end;

  if((PanelObj.ChapterListSelected != null) and (PanelObj.ChapterListSelected.size() > 0))
    stmt = stmt + "   AND ( ";
    i = 0;
    while(i < PanelObj.ChapterListSelected.size())
      if(i > 0)
        stmt = stmt + " OR ";
      end;
      stmt = stmt + " acc.t_Chapter = " + PanelObj.ChapterListSelected[i].Chapter;
      i = i + 1;
    end;
    stmt = stmt + "       ) ";
  end;

  if((PanelObj.AccKind != null) and (PanelObj.AccKind.ID != 0))
    if  (PanelObj.AccKind.ID == 1)
      stmt = stmt + "   AND acc.t_Kind_Account = 'А' ";
    elif(PanelObj.AccKind.ID == 2)
      stmt = stmt + "   AND acc.t_Kind_Account = 'П' ";
    end;
  end;

  if((PanelObj.ObligFIListSelected != null) and (PanelObj.ObligFIListSelected.size() > 0))
    stmt = stmt + "   AND ( ";
    i = 0;
    while(i < PanelObj.ObligFIListSelected.size())
      if(i > 0)
        stmt = stmt + " OR ";
      end;
      stmt = stmt + " acc.t_Code_Currency = " + PanelObj.ObligFIListSelected[i].FIID;
      i = i + 1;
    end;
    stmt = stmt + "       ) ";
  end;

  if((PanelObj.EqvFIListSelected != null) and (PanelObj.EqvFIListSelected.size() > 0))
    stmt = stmt + "   AND ( ";
    i = 0;
    while(i < PanelObj.EqvFIListSelected.size())
      if(i > 0)
        stmt = stmt + " OR ";
      end;
      stmt = stmt + " acc.t_CurrencyEq = " + PanelObj.EqvFIListSelected[i].FIID;
      i = i + 1;
    end;
    stmt = stmt + "       ) ";
  end;

  cmd = RsdCommand(stmt);
  cmd.execute();

  retObj.RecNumber = _GetCountAccounts(retObj.ParamID);

  info = _FormFisclogInfo(PanelObj);
  
  if(PanelObj.Backout)
    WriteFiscLog(UOL_B_CUNVPIRV_BACK_PROC_FISCLOG, info);
  else
    WriteFiscLog(UOL_B_CUNVPIRV_PROC_FISCLOG, info);
  end;

  return retObj;
end;

macro WebCore_ExecNVPI_ReValuation
(
  ParamID
 ,RecID
)
  file revnvpiaccsel("revnvpiaccsel.dbt") key 0;
  var panelObj;
  var AccKind;
  ConvertToRSL(_GetParamXML(ParamID), @panelObj);

  revnvpiaccsel.ParamID = ParamID;
  revnvpiaccsel.RecID = RecID;
  if(GetEQ(revnvpiaccsel))
    if(panelObj.IsAllAccKindsSelected)
      AccKind = 0;
    else
      AccKind = panelObj.AccKind.ID;
    end;
    
    CB_OverstimNVPIAccount
    (
      revnvpiaccsel.ParamID          
     ,revnvpiaccsel.RecID    
     ,revnvpiaccsel.Department          
     ,revnvpiaccsel.Chapter          
     ,revnvpiaccsel.Account          
     ,revnvpiaccsel.Code_Currency          
     ,panelObj.RegulateDate     
     ,AccKind     
     ,panelObj.PrintReport     
     ,panelObj.Backout     
    );
  end;
  
  _responsebuilder = WebResponseBuilder();

  _responsebuilder.SetUserObject(true);
  
  return _responsebuilder.Result;
end;

private macro _TruncateTable(TableName) : bool
  var cmd : RsdCommand;
  cmd = RsdCommand("TRUNCATE TABLE " + TableName);
  cmd.execute();
end;

/* Вставить во временную таблицу данные для построения Реестра проводок.
Используется существующая реализация построения Реестра.*/
private macro _InsertIntoCarryLog(ParamID)
  var stmt = "";
  var cmd;

  stmt = stmt + "INSERT INTO dnvpi_log_tmp ";
  stmt = stmt + "( ";
  stmt = stmt + "  t_Date_Carry ";
  stmt = stmt + " ,t_Dprt ";
  stmt = stmt + " ,t_Chapter ";
  stmt = stmt + " ,t_Currency ";
  stmt = stmt + " ,t_CurrencyEq ";
  stmt = stmt + " ,t_Account_Payer ";
  stmt = stmt + " ,t_Account_Receiver ";
  stmt = stmt + " ,t_Account_Exp ";
  stmt = stmt + " ,t_Numb_Document ";
  stmt = stmt + " ,t_Sum ";
  stmt = stmt + " ,t_SumNat ";
  stmt = stmt + ") ";
  stmt = stmt + "SELECT  ";
  stmt = stmt + "  t_Date_Carry ";
  stmt = stmt + " ,t_DprtID ";
  stmt = stmt + " ,t_Chapter ";
  stmt = stmt + " ,t_Currency ";
  stmt = stmt + " ,t_CurrencyEq ";
  stmt = stmt + " ,t_Account_Payer ";
  stmt = stmt + " ,t_Account_Receiver ";
  stmt = stmt + " ,t_Account_Exp ";
  stmt = stmt + " ,t_Numb_Document ";
  stmt = stmt + " ,t_Sum ";
  stmt = stmt + " ,t_SumNat ";
  stmt = stmt + "  FROM drevnvpiacctrn_dbt ";
  stmt = stmt + " WHERE t_ParamID = " + ParamID;
  
  cmd = RsdCommand(stmt);
  cmd.execute();
end;

private macro _PrintRegistryToFile(ParamID, ReVNVPIAccObj, ToFileName)
  setoutput(ToFileName, true);
  _TruncateTable("dnvpi_log_tmp");
  _InsertIntoCarryLog(ParamID);
  PrintCarryLogExtNVPI(ReVNVPIAccObj.Backout);
  setoutput(null, true);
end;

private macro _PrintReportTop(PanelObj)
  var info = "";
  var i, k;

  if(PanelObj.Backout)
    info = info + "Протокол удаления проводок переоценки НВПИ\n";
  else
    info = info + "Протокол выполнения процедуры переоценки НВПИ\n";
  end;
  info = info + "\n";
  info = info + "Дата формирования: " + string(date():f) + "\n";
  info = info + "Время формирования: " + string(time():f) + "\n";
  info = info + "Операционист: " + {oper} + " " + {Name_Oper} + "\n";
  info = info + "\n";
  info = info + "Параметры запуска:\n";

  if(PanelObj.IsAllDprtsSelected == true)
    info = info + "  Узел ТС: Все филиалы\n";
  else
    info = info + "  Узел ТС: " + PanelObj.Department.Name_ + " " + PanelObj.Department.PartyName +"\n";
  end;

  info = info + "  Дата переоценки: " + string(PanelObj.RegulateDate:f) + "\n";
  
  if(PanelObj.IsAllChaptersSelected)
    info = info + "  Глава счетов: Все главы\n";
  elif((PanelObj.ChapterListSelected != null) and (PanelObj.ChapterListSelected.size() > 0))
    i = 0;
    k = PanelObj.ChapterListSelected.size();
    while(i < k)
      if(i == 0)
        info = info + "  Глава счетов: " + PanelObj.ChapterListSelected[i].Chapter + " (" + PanelObj.ChapterListSelected[i].Name + ")";
      else
        info = info + "                " + PanelObj.ChapterListSelected[i].Chapter + " (" + PanelObj.ChapterListSelected[i].Name + ")";
      end;
      i = i + 1;
      if(i < k)
        info = info + ",\n";
      else
        info = info + "\n";
      end;
    end;
  end;

  if(PanelObj.AccKind != null)
    info = info + "  Вид счетов: " + PanelObj.AccKind.Name + "\n";
  elif(PanelObj.IsAllAccKindsSelected)
    info = info + "  Вид счетов: Все\n";
  end;

  if(PanelObj.IsAllObligFIsSelected)
    info = info + "  Код валюты обязательства: Все\n";
  elif((PanelObj.ObligFIListSelected != null) and (PanelObj.ObligFIListSelected.size() > 0))
    i = 0;
    k = PanelObj.ObligFIListSelected.size();
    while(i < k)
      if(i == 0)
        info = info + "  Код валюты обязательства: " + PanelObj.ObligFIListSelected[i].CodeWidget + " (" + PanelObj.ObligFIListSelected[i].Name + ")";
      else
        info = info + "                            " + PanelObj.ObligFIListSelected[i].CodeWidget + " (" + PanelObj.ObligFIListSelected[i].Name + ")";
      end;
      i = i + 1;
      if(i < k)
        info = info + ",\n";
      else
        info = info + "\n";
      end;
    end;
  end;

  if(PanelObj.IsAllEqvFIsSelected)
    info = info + "  Код валюты-эквивалента: Все\n";
  elif((PanelObj.EqvFIListSelected != null) and (PanelObj.EqvFIListSelected.size() > 0))
    i = 0;
    k = PanelObj.EqvFIListSelected.size();
    while(i < k)
      if(i == 0)
        info = info + "  Код валюты-эквивалента: " + PanelObj.EqvFIListSelected[i].CodeWidget + " (" + PanelObj.EqvFIListSelected[i].Name + ")";
      else
        info = info + "                          " + PanelObj.EqvFIListSelected[i].CodeWidget + " (" + PanelObj.EqvFIListSelected[i].Name + ")";
      end;
      i = i + 1;
      if(i < k)
        info = info + ",\n";
      else
        info = info + "\n";
      end;
    end;
  end;

  if((PanelObj.Account != null) and (PanelObj.Account.Account != null))
    info = info + "  Переоцениваемый счет: " + PanelObj.Account.Account + "\n";
  else
    info = info + "  Переоцениваемый счет:\n";
  end;

  info = info + "  Номер первого документа: " + PanelObj.FirstDocNumber + "\n";

  if(PanelObj.PrintReport)
    info = info + "  Печать реестра: Да\n";
  else
    info = info + "  Печать реестра: Нет\n";
  end;
  
  info = info + "\n";

  print(info);
end;

private macro _GetDprtPartyName( DprtID : integer )
  record party("party.dbt");
  file dp_dep("dp_dep.dbt") key 0;
  var DprtPartyName = "";
  dp_dep.Code = DprtID;
  if( GetEQ(dp_dep) )
    if( ПолучитьСубъекта(dp_dep.PartyID, party) == 0 )
      DprtPartyName = party.Name;
    end;
  end;
  return DprtPartyName;
end;

private macro _GetDprt( DprtID : integer, dprt )
  file dp_dep("dp_dep.dbt") key 0;
  dp_dep.Code = DprtID;
  if( GetEQ( dp_dep ))
    copy( dprt, dp_dep );
  end;
end;

private macro _GetErrorAccounts(ParamID, DprtID, ReVNVPIAccObj)
  var query = "";
  var cmd, rs;
  if(ReVNVPIAccObj.IsAllDprtsSelected == true)
    query = query + "SELECT t_ErrMsg FROM drevnvpiaccsel_dbt WHERE t_Error <> 0 AND t_Department = " + DprtID + " AND t_ParamID = " + ParamID;
  else
    query = query + "SELECT t_ErrMsg FROM drevnvpiaccsel_dbt WHERE t_Error <> 0 AND (t_Branch = " + DprtID + " OR t_Department = " + DprtID + ") AND t_ParamID = " + ParamID;
  end;
  cmd = RsdCommand(query);
  rs = RsdRecordset(cmd);
  return rs;
end;

private macro _PrintReportDprt(ParamID, DprtID, PanelObj)
  var info = "";
  var noAccounts = false;
  var rs;
  var i;

  if(PanelObj.IsAllDprtsSelected == true)
    info = info + "Узел ТС - " + _GetDprtPartyName(DprtID) + "\n";
  end;

  info = info + "\n";

  i = 0;
  rs = _GetErrorAccounts(ParamID, DprtID, PanelObj);
  while(rs.movenext)
    if(i == 0)
      info = info + "  Во время выполнения процедуры были обнаружены ошибки:\n\n";
    end;
    info = info + "  " + rs.value(0);
    if(index(rs.value(0), "\n") != strlen(rs.value(0)))
      info = info + "\n";
    end;
    info = info + "\n";
    i = i + 1;
    _countErrors = _countErrors + 1;
  end;

  if(i == 0)
    info = info + "Процедура выполнена успешно!\n";
  end;

  info = info + "\n";

  print(info);
end;

private macro _PrintReportBottom(PanelObj)
  var info = "";
  if(PanelObj.IsAllDprtsSelected == true)
    if(_countErrors > 0)
      info = info + "Процедура выполнена не во всех филиалах.\n";
    else
      info = info + "Процедура выполнена во всех филиалах.\n";
    end;
    print(info);
  end;
end;

private macro _PrintReportToFile(ParamID, PanelObj, ToFileName)
  var query = "";
  var cmd, rs;

  query = query + "    SELECT t_Code ";
  query = query + "      FROM ddp_dep_dbt ";

  if(PanelObj.IsAllDprtsSelected == true)
    query = query + "     WHERE     t_Status = 2 /* DEPARTMENT_STATUS_ACTIVE Активное */ ";
    query = query + "           AND t_AccessMode = 1 /* DEPARTMENT_ACCESS_ONLINE Доступ On-Line */ ";
    query = query + "START WITH t_Code = " + {NumDprt} + " /* Ид. головного подразделения */ ";
    query = query + "CONNECT BY     t_ParentCode = PRIOR t_Code "; 
    query = query + "           AND t_NodeType = 1 /* DEPARTMENT_TYPE_FILIAL Филиал */ ";
  else
    query = query + "     WHERE t_Code = " + PanelObj.Department.Code;
  end;

  cmd = RsdCommand(query);

  rs = RsdRecordset(cmd);

  _countErrors = 0;

  setoutput(ToFileName, true);
  _PrintReportTop(PanelObj);

  while(rs.movenext)
    _PrintReportDprt(ParamID, rs.value(0), PanelObj);
  end;

  _PrintReportBottom(PanelObj);
  setoutput(null, true);
end;

private macro _GetReportFileName(ParamID)
  var ToFileName = GetTxtFileName("revnvpiacc_" + ParamID);
  return ToFileName;
end;

macro WebCore_GetReValueAccReport(ParamID)
  var ToFileName = _GetReportFileName(ParamID);

  var info;

  setoutput(ToFileName);
  setoutput(null, true);
  var ReVNVPIAccObj;
  ConvertToRSL(_GetParamXML(ParamID), @ReVNVPIAccObj);
  info = _FormFisclogInfo(ReVNVPIAccObj);
  
  if(ReVNVPIAccObj.Backout)
    WriteFiscLog(UOL_E_CUNVPIRV_BACK_PROC_FISCLOG, info);
  else
    WriteFiscLog(UOL_E_CUNVPIRV_PROC_FISCLOG, info);
  end;
  _PrintReportToFile(ParamID, ReVNVPIAccObj, ToFileName);
  if(ReVNVPIAccObj.PrintReport)
    _PrintRegistryToFile(ParamID, ReVNVPIAccObj, ToFileName);
  end;
  /*
  return ToFileName;
  */
  var fc = null;
  
  if(ToFileName != "")

    fc = ConvertTxt2Html(ToFileName);
    
    fc = GetTextFile(fc);

  end;

  _responsebuilder = WebResponseBuilder();

  _responsebuilder.SetUserObject(fc);
  
  return _responsebuilder.Result;
end;