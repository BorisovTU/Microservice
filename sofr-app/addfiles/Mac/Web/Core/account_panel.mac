/*
$Name:        account_panel.mac
$Module:      ƒ« ¢­ ï ª­¨£ 
$Description: Œ ªà®á ¯ ­¥«¨ «¨æ¥¢ëå áç¥â®¢
*/
import FMInter, BankInter;
import "globals.mac";
import "account_widget.mac", "ws_ficlass.mac", "ws_fiunilist.mac", "balaccount_widget.mac","ws_partyclasses.mac",
       "ws_partylist.mac" ,"ws_person.mac", "ws_dprt.mac", "ws_llvalues.mac", "ws_firate.mac", "ofrsymbol_widget.mac", "acctrn_panel.mac";


class TWebAccountBalAccGridItem
  var iNumPlan      : integer;   
  var NamePlan      : string ;   
  var Chapter       : integer;     
  var Balance       ;  // TWebBalAccount   
  var IsReadOnlyRec : bool   ;     
end;

class TWebAccLimit
  var Account;
  var Chapter;
  var Currency;
  var LimitDate : Date               ;
  var Limit     : Money = $0         ;
end;


class TWebAccountObject
  var AccountID       : Integer            ;
  var Account         : String             ;
  var Chapter         ; // TWebChapter
  var Currency        ; // TWsFinInstr
  var CurrencyNat     ; // TWsFinInstr
  var CurrencyEq      ; // TWsFinInstr
  var PairAccount     ; // TWebAccount
  var Balance         ; // TWebBalAccount
  var Kind_Account    : String             ;
  var Client          ; // TWsPartyEx
  var ClientInfo      : String             ;
  var Beneficiary     ; // TWsPartyEx
  var Open_Date       : Date               ;
  var Close_Date      : Date               ;
  var Type_Account    : String             ;
  var UserTypeAccount : String             ;
  var OFRSymbol       ; // TWebOFRSymbol
  var Symbol          : String             ;
  var Oper            ; // TWsPerson
  var Department      ; // TWsDepartment
  var Branch          ; // TWsDepartment
  var RestNat         : Money = $0         ;
  var RestCur         : Money = $0         ;
  var RestEqv         : Money = $0         ;
  var Limit           : Money = $0         ;
  var ORScheme        ; // TWsLlvalues
  var HaveSubAccounts : Bool               ;
  var FinalDate       : Date               ;
  var DateNoChange    : Date               ;
  var NameAccount     : String             ;
  var UserField1      : String             ;
  var UserField2      : String             ;
  var UserField3      : String             ;
  var UserField4      : String             ;
  var EqvRateIsDominant    : Bool               ;
  var EqvRateIsNotDominant : Bool               ; 
  var EqvRateType     ; // TWsFIRateType
  var EqvRateDate     : Integer            ;
  var EqvRateExtra    : double             ;
  var EqvNoteText     : String             ;
  var OperationDate   : Date               ;
  var DaysToEnd       : Integer            ;
  var AccBalanceList  ; // List< TWebBalAccountGridItem >
  var Version         : Integer            ;
  var AtributesRestriction  ; // List< bool >
end;

class TWebOpdPhasesGridItem
  var Phase;
  var PhaseName;
  var BeginPhaseDate;
  var EndPhaseDate;
end;

class TWebAvailableRest
  var Rest;
  var MinRest;
  var ClaimSum;
  var CalcPriority;
  var CalcFreeAmount;
  var CalcFreeLimitAmount;
end;

class TWebAvailableRestGridItem
  var ClaimID;
  var ClaimKindID;
  var ClaimKindName;
  var ClaimSum;
  var FreeAmount;
  var BeneficiaryName;
  var ClaimNumber;
  var Children;
end;

const ACC_FR_AM_GROUPS_FULL_AREST = 0;
const ACC_FR_AM_GROUPS_SPECIAL    = 1;
const ACC_FR_AM_GROUPS_RESERVE    = 2;
const ACC_FR_AM_GROUPS_AREST_SUM  = 3;

const ACCLAIM_STATUS_ACTIVE   = 1;
const ACCLAIM_STATUS_MODIFIED = 2;
const ACCLAIM_STATUS_STOPED   = 3;
const ACCLAIM_STATUS_CANCELED = 4;
const ACCLAIM_STATUS_CLOSED   = 5;

private Record RecordAcc      ( account );

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;


macro WebCore_GetAccountClientInfo(ClientID)
  var RetVal = NULL;

  if( ClientID > 0 )
  
    var  PtFrzngRec = TBFile("ptfrzng.dbt", "R", 0 );
  
    PtFrzngRec.Clear();
    PtFrzngRec.rec.PartyID = ClientID;

    if( PtFrzngRec.GetEQ() )
       var zdate = date(0,0,0);

       if(zdate != PtFrzngRec.rec.FreezingDate)
         RetVal = "[‡€ŒŽŽ†…]";
       else
         var stat = ClientRejectCheck(ClientID);
         if(stat == 1)
           RetVal = "[…ŠŽŒ…„“…’‘Ÿ €‘’Žƒ“’œ „ŽƒŽ‚Ž]";
         elif(stat == 2)
           RetVal = "[„ŽƒŽ‚Ž ‘—…’€ €‘’Žƒ“’]";
         end;
       end;
    end;

  end;

  return RetVal;
end;

private macro GetRecAccountByID(AccountID : integer);
  var cmd, rs;

  var Rec = TRecHandler("account");

  var query = "select * from daccount_dbt where t_accountid = " + string(AccountID);

  cmd = RsdCommand(query);
  rs = RsdRecordset(cmd);

  if(rs.movenext)

    _CopyRecordsetToRecHandler(rs, Rec);

  end;

  return Rec;
end;

private macro GetChapter(Chapter : integer);

  var cmd, rs, query;

  query = "SELECT t_Chapter, t_Name, t_SHortName, t_Symbol FROM dobchaptr_dbt WHERE t_Chapter = ? ";
  
  var obj = TWebChapter();
  
  cmd = RsdCommand(query);
	  
  cmd.addParam( "", RSDBP_IN, Chapter );
	  
  rs = RsdRecordset(cmd);

  if(rs.movenext)
  
	obj = TWebChapter;
	
	obj.Chapter   = SQL_ConvType(rs.value("t_Chapter")); 
	
	obj.Name      = SQL_ConvType(rs.value("t_Name"));
	
	obj.ShortName = SQL_ConvType(rs.value("t_ShortName"));
	
	obj.Symbol    = SQL_ConvType(rs.value("t_Symbol"));
	
  end;
  
  return obj;
	  
end;



private macro GetAccountObjectByRec(AccountRec, AccBlncRec) : TWebAccountObject
  var cmd, rs;

  var obj = TWebAccountObject;
  
  obj.AccountID       = AccountRec.AccountID        ;
  obj.Account         = AccountRec.Account          ;
  obj.Chapter       = GetChapter(AccountRec.Chapter);
  obj.Kind_Account    = AccountRec.Kind_Account     ;
  obj.Open_Date       = AccountRec.Open_Date        ;
  obj.Close_Date      = AccountRec.Close_Date       ;
  obj.Type_Account    = AccountRec.Type_Account     ;
  obj.UserTypeAccount = AccountRec.UserTypeAccount  ;
  obj.Symbol          = AccountRec.Symbol           ;

  obj.HaveSubAccounts = AccountRec.HaveSubAccounts  ;
  obj.DateNoChange    = AccountRec.DateNoChange     ;
  obj.NameAccount     = AccountRec.NameAccount      ;
  obj.UserField1      = AccountRec.UserField1       ;
  obj.UserField2      = AccountRec.UserField2       ;
  obj.UserField3      = AccountRec.UserField3       ;
  obj.UserField4      = AccountRec.UserField4       ;
  
  if(AccountRec.CurrencyEq_RateType == 0)
    obj.EqvRateIsDominant = true;
  else
    obj.EqvRateIsNotDominant = false;  
  end;
  
  obj.EqvRateDate     = AccountRec.CurrencyEq_RateDate ;
  obj.EqvRateExtra    = AccountRec.CurrencyEq_RateExtra ;

  obj.OperationDate   = AccountRec.OperationDate    ;
  obj.DaysToEnd       = AccountRec.DaysToEnd        ;
  obj.Version         = AccountRec.Version          ;
  
  if(AccountRec.Balance != "")
    obj.Balance = WebCore_CreateTWebBalAccountFromBD(AccountRec.Balance, AccountRec.Chapter, 0 );
  end;

  if(AccountRec.Code_Currency >=0)
    obj.Currency = GetTWsFinInstrByID(AccountRec.Code_Currency);
  end;

  obj.CurrencyNat = GetTWsFinInstrByID(NATCUR);

  if(AccountRec.CurrencyEq >=0)
    obj.CurrencyEq = GetTWsFinInstrByID(AccountRec.CurrencyEq);
  end;

  obj.PairAccount =  WebCore_CreateTWebAccountFromBD0(AccountRec.Chapter, AccountRec.PairAccount, AccountRec.Code_Currency);

  obj.Client = GetTWsPartyExByID(AccountRec.Client);
  
  obj.ClientInfo = WebCore_GetAccountClientInfo(AccountRec.Client) ;
 
  if(AccountRec.BeneficiaryID > 0 )
    obj.Beneficiary = GetTWsPartyExByID(AccountRec.BeneficiaryID);
  end;

  obj.Oper = FindPersonByNumber(AccountRec.Oper);
  
  if (AccountRec.OFRRecID != "")
    obj.OFRSymbol = WebCore_CreateTWebOFRSymbolFromBD(AccountRec.OFRRecID);
  end;
  
  obj.Department = GetTWsDepartmentByCode(AccountRec.Department);
      
  obj.Branch = GetTWsDepartmentByCode(AccountRec.Branch);

  obj.ORScheme = CreateLLValueFromAppServ(1693, AccountRec.ORScheme);

  obj.EqvRateType =  FI_GetRateType(AccountRec.CurrencyEq_RateType);  

  //
  // „ â  ¯®á«¥¤­¥© ¯à®¢®¤ª¨
  //
  GetLastCarryDate( AccountRec.Chapter, AccountRec.Code_Currency, AccountRec.Account, obj.FinalDate );

  // ‡­ ç¥­¨¥ ¯à¨¬¥ç ­¨ï 31 ¤«ï «¨æ¥¢®£® áç¥â .
  
  var RecAccount = GetRecAccountByID(AccountRec.AccountID);

  var AccountIDStr = UniID(RecAccount, OBJTYPE_ACCOUNT);
  
  obj.EqvNoteText = ReadNoteForObject(OBJTYPE_ACCOUNT, AccountIDStr, 31);
  
  obj.RestNat = RestA( AccountRec.Account, {curdate}, null, AccountRec.Chapter, AccountRec.Code_Currency, NATCUR );

  obj.RestCur = RestA( AccountRec.Account, {curdate}, null, AccountRec.Chapter, AccountRec.Code_Currency, AccountRec.Code_Currency );

  obj.RestEqv = GetRestAEQ( AccountRec.AccountID, {curdate});
  
  obj.AtributesRestriction = CB_GetAccAttribRestriction();
  
    
  GetAccOverdaftLimit( AccountRec.Account, AccountRec.Chapter, AccountRec.Code_Currency, {curdate}, obj.Limit );

  obj.AccBalanceList = TArray();


  var query = "SELECT t_iNumPlan, t_NamePlan FROM dnameplan_dbt ORDER BY t_iNumPlan";

  cmd = RsdCommand(query);
  rs = RsdRecordset(cmd);

  while(rs.movenext)
    var NumPlan = SQL_ConvType(rs.value("t_iNumPlan"));

    obj.AccBalanceList.value(NumPlan) = TWebAccountBalAccGridItem;

    obj.AccBalanceList.value(NumPlan).iNumPlan = NumPlan;
    obj.AccBalanceList.value(NumPlan).NamePlan = SQL_ConvType(rs.value("t_NamePlan"));
    obj.AccBalanceList.value(NumPlan).Chapter  = AccountRec.Chapter;

    var Balance = "";
    if(NumPlan == 0)
      Balance = AccBlncRec.Balance0;
    elif(NumPlan == 1)
      Balance = AccBlncRec.Balance1;
    elif(NumPlan == 2)
      Balance = AccBlncRec.Balance2;
    elif(NumPlan == 3)
      Balance = AccBlncRec.Balance3;
    elif(NumPlan == 4)
      Balance = AccBlncRec.Balance4;
    elif(NumPlan == 5)
      Balance = AccBlncRec.Balance5;
    elif(NumPlan == 6)
      Balance = AccBlncRec.Balance6;
    elif(NumPlan == 7)
      Balance = AccBlncRec.Balance7;
    elif(NumPlan == 8)
      Balance = AccBlncRec.Balance8;
    elif(NumPlan == 9)
      Balance = AccBlncRec.Balance9;
    elif(NumPlan == 10)
      Balance = AccBlncRec.Balance10;
    elif(NumPlan == 11)
      Balance = AccBlncRec.Balance11;
    end;

    if(Balance != "")
      obj.AccBalanceList.value(NumPlan).Balance = WebCore_CreateTWebBalAccountFromBD(Balance, AccountRec.Chapter, NumPlan );
    end;

    if((obj.AccBalanceList.value(NumPlan).iNumPlan == 0) OR (obj.AccBalanceList.value(NumPlan).NamePlan == ""))
      obj.AccBalanceList.value(NumPlan).IsReadOnlyRec = True;
    else
      obj.AccBalanceList.value(NumPlan).IsReadOnlyRec = False;
    end;

  end;

  return obj;
end;


macro WebCore_GetAccountObject(AccountID) : TWebAccountObject
  var cmd, rs;

  var obj = TWebAccountObject;

  var  AccountRec = TBFile("account.dbt", "R", 1 );

  AccountRec.Clear();
  AccountRec.rec.AccountID = AccountID;

  if( AccountRec.GetEQ() )
    var AccBlncRec = TBFile("accblnc.dbt", "R", 0 );
    AccBlncRec.Clear();
    AccBlncRec.rec.AccountID = AccountID;
    AccBlncRec.GetEQ();

    obj = GetAccountObjectByRec(AccountRec.rec, AccBlncRec.rec);
  end;

  return obj;
end;

macro WebCore_GetNewAccountObject(Chapter) : TWebAccountObject
  var cmd, rs;

  var obj = TWebAccountObject;

  var query = "SELECT t_iNumPlan, t_NamePlan FROM dnameplan_dbt ORDER BY t_iNumPlan";

  cmd = RsdCommand(query);
  rs = RsdRecordset(cmd);

  obj.Chapter = GetChapter(Chapter);
  obj.Oper = FindPersonByNumber({oper});
  obj.Department = GetTWsDepartmentByCode({OperDprt});
  obj.Branch = GetTWsDepartmentByCode({OperDprtNode});
  obj.Open_Date = {curdate};
  obj.AtributesRestriction = CB_GetAccAttribRestriction();
  obj.AccBalanceList = TArray();

  obj.EqvRateIsDominant = true;

  while(rs.movenext)
    var NumPlan = SQL_ConvType(rs.value("t_iNumPlan"));

    obj.AccBalanceList.value(NumPlan) = TWebAccountBalAccGridItem;
    obj.AccBalanceList.value(NumPlan).iNumPlan = NumPlan;
    obj.AccBalanceList.value(NumPlan).NamePlan = SQL_ConvType(rs.value("t_NamePlan"));
    obj.AccBalanceList.value(NumPlan).Chapter  = Chapter;
  end;

  return obj;
end;

private macro ConvTypeBool(boolVal)
  if(boolVal != NULL)
    if(boolVal == true)
      return "X";
    else
      return "";
    end;
  else
    return ""
  end;
end;

private macro SetAccountObject(Account, AccountObject)
  ClearRecord(Account);

  Account.CurrencyEq = ALLFININSTR;

  if(AccountObject.AccountID       != NULL) Account.AccountID             =  AccountObject.AccountID                    ; end;
  if(AccountObject.Account         != NULL) Account.Account               =  AccountObject.Account                      ; end;
  if(AccountObject.Chapter         != NULL) Account.Chapter               =  AccountObject.Chapter.Chapter              ; end;
  if(AccountObject.Currency        != NULL) Account.Code_Currency         =  AccountObject.Currency.Fiid                ; end;
  if(AccountObject.CurrencyEq      != NULL) Account.CurrencyEq            =  AccountObject.CurrencyEq.Fiid              ; end;
  if(AccountObject.PairAccount     != NULL) Account.PairAccount           =  AccountObject.PairAccount.Account          ; end;
  if(AccountObject.Balance         != NULL) Account.Balance               =  AccountObject.Balance.Balance              ; end;
  if(AccountObject.Kind_Account    != NULL) Account.Kind_Account          =  AccountObject.Kind_Account                 ; end;
  if(AccountObject.Client          != NULL) Account.Client                =  AccountObject.Client.partyid               ; end;
  //if(AccountObject.ClientInfo      != NULL) Account.ClientInfo            =  AccountObject.ClientInfo                   ; end;
  if(AccountObject.Beneficiary     != NULL) Account.BeneficiaryID         =  AccountObject.Beneficiary.partyid          ; end;
  if(AccountObject.Open_Date       != NULL) Account.Open_Date             =  AccountObject.Open_Date                    ; end;
  if(AccountObject.Close_Date      != NULL) Account.Close_Date            =  AccountObject.Close_Date                   ; end;
  if(AccountObject.Type_Account    != NULL) Account.Type_Account          =  AccountObject.Type_Account                 ; end;
  if(AccountObject.UserTypeAccount != NULL) Account.UserTypeAccount       =  AccountObject.UserTypeAccount              ; end;
  if(AccountObject.OFRSymbol       != NULL)
                                             Account.OFRRecID              =  AccountObject.OFRSymbol.RecID              ;
                                             Account.OPUCode               =  AccountObject.OFRSymbol.Code               ;
  end;
  if(AccountObject.Symbol          != NULL) Account.Symbol                =  AccountObject.Symbol                       ; end;
  if(AccountObject.Oper            != NULL) Account.Oper                  =  AccountObject.Oper.Number                  ; end;
  if(AccountObject.Department      != NULL) Account.Department            =  AccountObject.Department.Code              ; end;
  if(AccountObject.Branch          != NULL) Account.Branch                =  AccountObject.Branch.Code                  ; end;
//  if(AccountObject.RestNat         != NULL) Account.RestNat               =  AccountObject.RestNat                      ; end;
//  if(AccountObject.RestCur         != NULL) Account.RestCur               =  AccountObject.RestCur                      ; end;
//  if(AccountObject.RestEqv         != NULL) Account.RestEqv               =  AccountObject.RestEqv                      ; end;
//  if(AccountObject.Limit           != NULL) Account.Limit                 =  AccountObject.Limit                        ; end;
  if(AccountObject.ORScheme        != NULL) Account.ORScheme              =  AccountObject.ORScheme.Element             ; end;
  if(AccountObject.HaveSubAccounts != NULL) Account.HaveSubAccounts       =  ConvTypeBool(AccountObject.HaveSubAccounts); end;
  if(AccountObject.DateNoChange    != NULL) Account.DateNoChange          =  AccountObject.DateNoChange                 ; end;
  if(AccountObject.NameAccount     != NULL) Account.NameAccount           =  AccountObject.NameAccount                  ; end;
  if(AccountObject.UserField1      != NULL) Account.UserField1            =  AccountObject.UserField1                   ; end;
  if(AccountObject.UserField2      != NULL) Account.UserField2            =  AccountObject.UserField2                   ; end;
  if(AccountObject.UserField3      != NULL) Account.UserField3            =  AccountObject.UserField3                   ; end;
  if(AccountObject.UserField4      != NULL) Account.UserField4            =  AccountObject.UserField4                   ; end;
  if(AccountObject.EqvRateType     != NULL) Account.CurrencyEq_RateType   =  AccountObject.EqvRateType.TypeID           ; end;
  if(AccountObject.EqvRateDate     != NULL) Account.CurrencyEq_RateDate   =  AccountObject.EqvRateDate                  ; end;
  if(AccountObject.EqvRateExtra    != NULL) Account.CurrencyEq_RateExtra  =  AccountObject.EqvRateExtra                 ; end;
//  if(AccountObject.EqvNoteText     != NULL) Account.EqvNoteText           =  AccountObject.EqvNoteText                  ; end; 
  if(AccountObject.OperationDate   != NULL) Account.OperationDate         =  AccountObject.OperationDate                ; end;
  if(AccountObject.DaysToEnd       != NULL) Account.DaysToEnd             =  AccountObject.DaysToEnd                    ; end;
  if(AccountObject.Version         != NULL) Account.Version               =  AccountObject.Version                      ; end;
end;

private macro GetBalanceByNumPlan(AccBalanceList, NumPlan)
   var Balance = "";
   var SizeList = AccBalanceList.size();

   var i = 0;
   while(i<SizeList)
     if(AccBalanceList[i] != NULL)  
       if(AccBalanceList[i].iNumPlan == NumPlan)
         if(AccBalanceList[i].Balance != NULL)
           if(AccBalanceList[i].Balance.Balance != NULL)
             Balance = AccBalanceList[i].Balance.Balance;
           end;
         end;
       end;
     end;
     i = i + 1;
   end;

   return Balance;
end;

private macro SetAccBlncObject(AccBlnc, AccountObject)
  ClearRecord(AccBlnc);

  AccBlnc.AccountID     = AccountObject.AccountID;
  AccBlnc.Account       = AccountObject.Account;
  AccBlnc.Code_Currency = AccountObject.Currency.FIID;
  AccBlnc.Chapter       = AccountObject.Chapter.Chapter;
                    
  AccBlnc.Balance0  = GetBalanceByNumPlan(AccountObject.AccBalanceList, 0 );
  AccBlnc.Balance1  = GetBalanceByNumPlan(AccountObject.AccBalanceList, 1 );
  AccBlnc.Balance2  = GetBalanceByNumPlan(AccountObject.AccBalanceList, 2 );
  AccBlnc.Balance3  = GetBalanceByNumPlan(AccountObject.AccBalanceList, 3 );
  AccBlnc.Balance4  = GetBalanceByNumPlan(AccountObject.AccBalanceList, 4 );
  AccBlnc.Balance5  = GetBalanceByNumPlan(AccountObject.AccBalanceList, 5 );
  AccBlnc.Balance6  = GetBalanceByNumPlan(AccountObject.AccBalanceList, 6 );
  AccBlnc.Balance7  = GetBalanceByNumPlan(AccountObject.AccBalanceList, 7 );
  AccBlnc.Balance8  = GetBalanceByNumPlan(AccountObject.AccBalanceList, 8 );
  AccBlnc.Balance9  = GetBalanceByNumPlan(AccountObject.AccBalanceList, 9 );
  AccBlnc.Balance10 = GetBalanceByNumPlan(AccountObject.AccBalanceList, 10);
  AccBlnc.Balance11 = GetBalanceByNumPlan(AccountObject.AccBalanceList, 11);

end;

macro WebCore_InsertAccountObject(AccountObject)
  file Account("account.dbt");
  file AccBlnc("accblnc.dbt");
  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  SetAccountObject(Account, AccountObject);
  SetAccBlncObject(AccBlnc, AccountObject);

  var ErrMsg = "";
  res = Create_Account(Account, AccBlnc, ErrMsg, AccountObject.Limit, PRIV_OPEN_ACCOUNT);

  GetEQ(Account);

  if((res != 0))
    ErrorMessage.Code     = res;
    ErrorMessage.Message  = ErrMsg;
    ErrorMessage.Severity = MessageSeverity.RuntimeError;
    ResponseBuilder.AddMessage( ErrorMessage );
  else
    if((AccountObject.EqvNoteText != NULL) AND (AccountObject.EqvNoteText != ""))
      copy (RecordAcc, Account);
      res = AddNoteForObject(OBJTYPE_ACCOUNT, makeObjectID(OBJTYPE_ACCOUNT,null,RecordAcc), 31, AccountObject.EqvNoteText);

      if(res != 0)
        AddErrorMessage(@ErrorMessage);
      end;
    end;
  end;

  var NewAccountObject = GetAccountObjectByRec(Account, AccBlnc);
  responseBuilder.SetUserObject(NewAccountObject);

  return responseBuilder.Result;
end;


macro WebCore_UpdateAccountObject(AccountObject)
  file Account("account.dbt");
  file AccBlnc("accblnc.dbt");
  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  SetAccountObject(Account, AccountObject);
  SetAccBlncObject(AccBlnc, AccountObject);

  var stat = 0;
  res = Update_AccountEx(Account, AccBlnc);

  if((res == 0))  

    var RecAccount = GetRecAccountByID(Account.AccountID);

    var AccountIDStr = UniID(RecAccount, OBJTYPE_ACCOUNT);
    
    var EqvNoteText = ReadNoteForObject(OBJTYPE_ACCOUNT, AccountIDStr, 31);

    if(AccountObject.EqvNoteText == NULL)
      AccountObject.EqvNoteText = "";
    end;

    if((EqvNoteText != AccountObject.EqvNoteText))
      res = AddNoteForObject(OBJTYPE_ACCOUNT, AccountIDStr, 31, AccountObject.EqvNoteText);
    end;
/*
      var sqlString, cmd;
      cmd = RSDCommand( "DELETE FROM DNOTETEXT_DBT WHERE T_OBJECTTYPE = ? AND T_DOCUMENTID = ? AND T_NOTEKIND = ?" );
      cmd.addParam( "", RSDBP_IN, OBJTYPE_ACCOUNT );
      cmd.addParam( "", RSDBP_IN, AccObjID );
      cmd.addParam( "", RSDBP_IN, 31 );
      cmd.Execute();
*/
  end;

  if(res != 0)
    AddErrorMessage(@ErrorMessage);
  end;

  var NewAccountObject = GetAccountObjectByRec(Account, AccBlnc);
  responseBuilder.SetUserObject(NewAccountObject);

  return responseBuilder.Result;
end;

macro WebCore_GetAccountKeyByDprtCode(Account, DprtCode)
  var RetAccount;

  var sqlString, cmd;
  cmd = RSDCommand( "select rsi_rsb_account.GetAccountKeyByDprtCode(?, ?) as newaccount from dual;" );
  cmd.addParam( "", RSDBP_IN, Account );
  cmd.addParam( "", RSDBP_IN, DprtCode );

  var rs = RsdRecordset(cmd);

  if(rs.movenext)
    RetAccount = rs.value("newaccount");
  end;

  return RetAccount;
end;

/*
var o = WebCore_GetAccountObject(1);
var co = WebCore_UpdateAccountObject(o);
var ci = WebCore_InsertAccountObject(o);
var n = WebCore_GetNewAccountObject(1);
var a= 1;
*/
macro WebCore_GetAccLimitList(Account, Code_Currency, Chapter)

  var cmd, rs;

  var Arr = TArray;

  var query = " SELECT * "
              "  FROM DACCLIMIT_DBT WHERE 1 = 1 ";

  if(Account != "")

    query = query + " AND T_ACCOUNT = ? ";

  end;

  if(Code_Currency != null)

    query = query + " AND T_CODE_CURRENCY = ? ";

  end;

  if(Chapter != null)

    query = query + " AND T_CHAPTER = ? ";

  end;

  cmd = RsdCommand(query);

  if(Account != "")

    cmd.addParam( "", RSDBP_IN, Account );

  end;

  if(Code_Currency != null)

    cmd.addParam( "", RSDBP_IN, Code_Currency );

  end;

  if(Chapter != null)

    cmd.addParam( "", RSDBP_IN, Chapter );

  end;

  rs = RsdRecordset(cmd);

  while(rs.movenext)

    var obj = TWebAccLimit;

    obj.Account   = rs.value("T_ACCOUNT");
    obj.Chapter   = rs.value("T_CHAPTER");
    obj.Currency  = rs.value("T_CODE_CURRENCY");
    obj.LimitDate = rs.value("T_LIMITDATE");
    obj.Limit     = rs.value("T_LIMIT");

    Arr[Arr.size] = obj;

  end;

  return Arr;

end;

private macro SetAccLimitObject(p, AccLimitObject)

  p.account       = AccLimitObject.Account;
  p.chapter       = AccLimitObject.Chapter;
  p.code_currency = AccLimitObject.Currency;
  p.limit         = AccLimitObject.Limit;
  p.limitdate     = AccLimitObject.LimitDate;
  
end;

macro WebCore_InsertAccLimit(AccLimitObject, OpenDate)

  file p("acclimit.dbt");

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  SetAccLimitObject(p, AccLimitObject);

  res = CB_InsertAccLimit(p, OpenDate);

  if(res != True)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_UpdateAccLimit(SaveAccLimitObject, AccLimitObject, OpenDate)

  file savep("acclimit.dbt");
  file p("acclimit.dbt");

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  SetAccLimitObject(savep, SaveAccLimitObject);
  SetAccLimitObject(p, AccLimitObject);

  res = CB_UpdateAccLimit(savep, p, OpenDate);

  if(res != True)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_DeleteAccLimit(AccLimitObject)

  file p("acclimit.dbt");

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  SetAccLimitObject(p, AccLimitObject);

  res = CB_DeleteAccLimit(p);

  if(res != True)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;
macro WebCore_onAccountClientChanged(AccountObject)
  
  var ClientID = 0;

  if( AccountObject.Client )
    ClientID = AccountObject.Client.PartyID;
  end;
  
  AccountObject.ClientInfo = WebCore_GetAccountClientInfo(ClientID);
  
  return AccountObject.ClientInfo;
  
end;

macro WebCore_GetOpdPhasesGridItemList(Account, Code_Currency, Chapter)

  var Arr = TArray;

  file acc_table("account.dbt");

  ClearRecord(account);

  acc_table.Chapter       = Chapter;
  acc_table.Account       = Account;
  acc_table.Code_Currency = Code_Currency;

  if( GetEQ(acc_table) )
  
    var stat = CB_FillOPDPHASE( acc_table.AccountID );

    if( stat != 0 )

      RunError( GetErrMsg() );

    else

      var cmd, rs, p;

      var query = " SELECT t1.t_Phase, "
                  "        t3.t_Name AS t_PhaseName, "
                  "        t1.t_DateBegin AS t_BeginPhaseDate, "
                  "        t1.t_DateEnd AS  t_EndPhaseDate "
                  "    FROM dopdphase_tmp t1, dphases_dbt t3 "
                  "   WHERE     t1.t_AccountID = ? "
                  "         AND t3.t_Phase = t1.t_Phase "
                  "  ORDER BY t1.t_DateBegin DESC ";

      cmd = RsdCommand(query);

      cmd.addParam( "", RSDBP_IN, acc_table.AccountID );
  
      rs = RsdRecordset(cmd);

      while(rs.movenext)

        p = TWebOpdPhasesGridItem();

        p.Phase           = SQL_ConvType(rs.value("t_Phase"));
        p.PhaseName       = SQL_ConvType(rs.value("t_PhaseName"));
        p.BeginPhaseDate  = SQL_ConvType(rs.value("t_BeginPhaseDate"));
        p.EndPhaseDate    = SQL_ConvType(rs.value("t_EndPhaseDate"));

        Arr[Arr.size] = p;

      end;

    end;

  end;

  return Arr;
  
end;

macro WebCore_GetTWebAvailableRest (AccountID)

  var BeneficiaryName;

  var ClaimKindIDs;
  var ClaimKindNames;
  var ClaimSums;
  var FreeAmounts;

  var AvailableRest = TWebAvailableRest();

  AvailableRest.CalcPriority = 0;

  WEB_CB_CalcFreeAmount(
    AccountID,
    {curdate},
    @(AvailableRest.Rest),
    @(AvailableRest.MinRest),
    @(AvailableRest.CalcPriority),
    @(AvailableRest.CalcFreeAmount),
    @(AvailableRest.CalcFreeLimitAmount)
  );

  WEB_CB_FillAcClaimData(
    AccountID,
    {curdate},
    AvailableRest.Rest,
    @(AvailableRest.ClaimSum),
    @ClaimKindIDs,
    @ClaimKindNames,
    @ClaimSums,
    @FreeAmounts,
    @BeneficiaryName
  );

  return AvailableRest;

end;

private macro GetRestKind_Name(RestKindID)

  var p;
  
  var query = " SELECT "
              "    llv.t_Name AS t_Name "
              "    FROM dllvalues_dbt llv "
              "  WHERE llv.t_Element = " + string(RestKindID) +
              "  AND llv.t_List = 2522 "; /*OBJTYPE_ACCLAIM_TYPE*/

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if (ds.MoveNext())
    
    p = ds.NAME;

  end;

  return p;

end;

private macro Get_ACC_FR_AM_GROUPS_FULL_AREST(Chapter, Account, Code_Currency, BeneficiaryName)

  var cmd, rs, p;

  var Arr = TArray;

  var query = " SELECT claim.t_docnumber DocNumber, claim.t_ClaimID "
              "   FROM dacclaim_dbt claim, dacclaimstate_dbt acclaimstate "
              "  WHERE     claim.t_Chapter = ? "
              "        AND claim.t_Account = ? "
              "        AND claim.t_FIID = ? "
              "        AND claim.t_ClaimKind = ? "
              "        AND claim.t_RestKind = ? "
              "        AND (claim.t_FinishDate = ? OR claim.t_FinishDate > ?) "
              "        AND claim.t_ClaimID = acclaimstate.t_ClaimID "
              "        AND (   acclaimstate.t_State = ? "
              "             OR acclaimstate.t_State = ?) "
              "        AND acclaimstate.t_StateDate = "
              "               (SELECT MAX (t.t_StateDate) "
              "                  FROM dacclaimstate_dbt t "
              "                 WHERE     t.t_ClaimID = claim.t_ClaimID "
              "                       AND t.t_StateDate <= ?) ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, Chapter );
  cmd.addParam( "", RSDBP_IN, Account );
  cmd.addParam( "", RSDBP_IN, Code_Currency );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_KIND_ARREST );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_TYPE_FULL );
  cmd.addParam( "", RSDBP_IN, date(0,0,0) );
  cmd.addParam( "", RSDBP_IN, {curdate} );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_STATUS_ACTIVE );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_STATUS_MODIFIED );
  cmd.addParam( "", RSDBP_IN, {curdate} );
  
  cmd.NullConversion = true;

  rs = RsdRecordset(cmd);

  while(rs.movenext)

    p = TWebAvailableRestGridItem();

    p.ClaimID         = SQL_ConvType(rs.value("t_ClaimID"));
    p.ClaimKindName   = GetRestKind_Name(ACCLAIM_TYPE_FULL);
    p.ClaimSum        = $0;
    p.ClaimNumber     = SQL_ConvType(rs.value("DocNumber"));
    p.FreeAmount      = $0;
    p.BeneficiaryName = BeneficiaryName;

    Arr[Arr.size] = p;

  end;

  return Arr;

end;

private macro Get_ACC_FR_AM_GROUPS_SPECIAL(
                                            Chapter,
                                            Account,
                                            Code_Currency,
                                            BeneficiaryName,
                                            FreeAmount_claimkindid_1,
                                            Rest
                                          )

  var cmd, rs, p;

  var Arr = TArray;

  var query = " SELECT claim.t_docnumber DocNumber, acclaimstate.t_CurrentAmount, claim.t_ClaimID "
              "   FROM dacclaim_dbt claim, dacclaimstate_dbt acclaimstate "
              "  WHERE     claim.t_Chapter = ? "
              "        AND claim.t_Account = ? "
              "        AND claim.t_FIID = ? "
              "        AND claim.t_ClaimKind = ?"
              "        AND claim.t_RestKind = ?"
              "        AND (claim.t_FinishDate = ? OR claim.t_FinishDate > ?) "
              "        AND claim.t_ClaimID = acclaimstate.t_ClaimID "
              "        AND (   acclaimstate.t_State = ?"
              "             OR acclaimstate.t_State = ?) "
              "        AND acclaimstate.t_StateDate = "
              "               (SELECT MAX (t.t_StateDate) "
              "                  FROM dacclaimstate_dbt t "
              "                 WHERE     t.t_ClaimID = claim.t_ClaimID "
              "                       AND t.t_StateDate <= ?) ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, Chapter );
  cmd.addParam( "", RSDBP_IN, Account );
  cmd.addParam( "", RSDBP_IN, Code_Currency );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_KIND_SPECIAL );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_TYPE_AMOUNT );
  cmd.addParam( "", RSDBP_IN, date(0,0,0) );
  cmd.addParam( "", RSDBP_IN, {curdate} );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_STATUS_ACTIVE );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_STATUS_MODIFIED );
  cmd.addParam( "", RSDBP_IN, {curdate} );
  
  cmd.NullConversion = true;

  rs = RsdRecordset(cmd);
  
  var i = 0;
  
  var FreeAmount_1 = $0;

  while(rs.movenext)

    p = TWebAvailableRestGridItem();

    p.ClaimID         = SQL_ConvType(rs.value("t_ClaimID"));
    p.ClaimKindName   = GetRestKind_Name(ACCLAIM_TYPE_AMOUNT);
    p.ClaimSum        = $0;
    p.ClaimNumber     = SQL_ConvType(rs.value("DocNumber"));

    if(FreeAmount_claimkindid_1 == $0)
      p.FreeAmount    = $0;
    else
      if(FreeAmount_claimkindid_1 == -1)
        p.FreeAmount = Rest - rs.value("t_CurrentAmount");
      else
        if(i == 0)
          p.FreeAmount = FreeAmount_claimkindid_1 - rs.value("t_CurrentAmount");
        else
          p.FreeAmount = FreeAmount_1 - rs.value("t_CurrentAmount");
        end;
      end;
    end;
    
    FreeAmount_1 = p.FreeAmount;

    p.BeneficiaryName = BeneficiaryName;

    Arr[Arr.size] = p;
    
    i = i + 1;

  end;

  return Arr;

end;

private macro Get_ACC_FR_AM_GROUPS_RESERVE(
                                            Chapter,
                                            Account,
                                            Code_Currency,
                                            BeneficiaryName,
                                            FreeAmount_claimkindid_1,
                                            Rest
                                          )

  var cmd, rs, p;

  var Arr = TArray;

  var query = " SELECT claim.t_docnumber DocNumber, acclaimstate.t_CurrentAmount, claim.t_ClaimID "
              "   FROM dacclaim_dbt claim, dacclaimstate_dbt acclaimstate "
              "  WHERE     claim.t_Chapter = ? "
              "        AND claim.t_Account = ? "
              "        AND claim.t_FIID = ? "
              "        AND claim.t_ClaimKind = ? "
              "        AND claim.t_RestKind = ? "
              "        AND (claim.t_FinishDate = ? OR claim.t_FinishDate > ?) "
              "        AND claim.t_ClaimID = acclaimstate.t_ClaimID "
              "        AND (   acclaimstate.t_State = ? "
              "             OR acclaimstate.t_State = ?) "
              "        AND acclaimstate.t_StateDate = "
              "               (SELECT MAX (t.t_StateDate) "
              "                  FROM dacclaimstate_dbt t "
              "                 WHERE     t.t_ClaimID = claim.t_ClaimID "
              "                       AND t.t_StateDate <= ?) ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, Chapter );
  cmd.addParam( "", RSDBP_IN, Account );
  cmd.addParam( "", RSDBP_IN, Code_Currency );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_KIND_RESERVE );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_TYPE_AMOUNT );
  cmd.addParam( "", RSDBP_IN, date(0,0,0) );
  cmd.addParam( "", RSDBP_IN, {curdate} );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_STATUS_ACTIVE );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_STATUS_MODIFIED );
  cmd.addParam( "", RSDBP_IN, {curdate} );
  
  cmd.NullConversion = true;

  rs = RsdRecordset(cmd);
  
  var i = 0;
  
  var FreeAmount_1 = $0;

  while(rs.movenext)

    p = TWebAvailableRestGridItem();

    p.ClaimID         = SQL_ConvType(rs.value("t_ClaimID"));
    p.ClaimKindName   = GetRestKind_Name(ACCLAIM_TYPE_AMOUNT);
    p.ClaimSum        = $0;
    p.ClaimNumber     = SQL_ConvType(rs.value("DocNumber"));

    if(FreeAmount_claimkindid_1 == $0)
      p.FreeAmount    = $0;
    else
      if(FreeAmount_claimkindid_1 == -1)
        p.FreeAmount = Rest - rs.value("t_CurrentAmount");
      else
        if(i == 0)
          p.FreeAmount = FreeAmount_claimkindid_1 - rs.value("t_CurrentAmount");
        else
          p.FreeAmount = FreeAmount_1 - rs.value("t_CurrentAmount");
        end;
      end;
    end;
    
    FreeAmount_1 = p.FreeAmount;

    p.BeneficiaryName = BeneficiaryName;

    Arr[Arr.size] = p;
    
    i = i + 1;

  end;

  return Arr;

end;

private macro Get_ACC_FR_AM_GROUPS_AREST_SUM(
                                              Chapter,
                                              Account,
                                              Code_Currency,
                                              BeneficiaryName,
                                              FreeAmount_claimkindid_1,
                                              Rest,
                                              Priority
                                            )

  var cmd, rs, p;

  var Arr = TArray;

  var query = " SELECT claim.t_DocNumber DocNumber, "
              "        claim.t_RestKind RestKind, "
              "        acclaimstate.t_CurrentAmount ClaimSum, claim.t_ClaimID "
              "   FROM dacclaim_dbt claim, dacclaimstate_dbt acclaimstate "
              "  WHERE     claim.t_Chapter = ? "
              "        AND claim.t_Account = ? "
              "        AND claim.t_FIID = ? "
              "        AND claim.t_ClaimKind = ? "
              "        AND (   claim.t_RestKind = ? "
              "             OR claim.t_RestKind = ?) "
              "        AND (claim.t_FinishDate = ? OR claim.t_FinishDate > ?) "
              "        AND claim.t_ClaimID = acclaimstate.t_ClaimID "
              "        AND acclaimstate.t_Priority = ? "
              "        AND (   acclaimstate.t_State = ? "
              "             OR acclaimstate.t_State = ?) "
              "        AND acclaimstate.t_StateDate = "
              "               (SELECT MAX (t.t_StateDate) "
              "                  FROM dacclaimstate_dbt t "
              "                 WHERE     t.t_ClaimID = claim.t_ClaimID "
              "                       AND t.t_StateDate <= ?) "
              " UNION ALL "
              " SELECT claim.t_DocNumber DocNumber, "
              "        claim.t_RestKind RestKind, "
              "        acclaimstate.t_CurrentAmount ClaimSum, claim.t_ClaimID "
              "   FROM dacclaim_dbt claim, dacclaimstate_dbt acclaimstate "
              "  WHERE     claim.t_Chapter = ? "
              "        AND claim.t_Account = ? "
              "        AND claim.t_FIID = ? "
              "        AND claim.t_ClaimKind = ? "
              "        AND claim.t_RestKind = ? "
              "        AND (claim.t_FinishDate = ? OR claim.t_FinishDate > ?) "
              "        AND claim.t_ClaimID = acclaimstate.t_ClaimID "
              "        AND acclaimstate.t_Priority = ? "
              "        AND (   acclaimstate.t_State = ? "
              "             OR acclaimstate.t_State = ?) "
              "        AND acclaimstate.t_StateDate = "
              "               (SELECT MAX (t.t_StateDate) "
              "                  FROM dacclaimstate_dbt t "
              "                 WHERE     t.t_ClaimID = claim.t_ClaimID "
              "                       AND t.t_StateDate <= ?) ";

  cmd = RsdCommand(query);
  
  cmd.addParam( "", RSDBP_IN, Chapter );
  cmd.addParam( "", RSDBP_IN, Account );
  cmd.addParam( "", RSDBP_IN, Code_Currency );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_KIND_ARREST );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_TYPE_AMOUNT );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_TYPE_SECUR );
  cmd.addParam( "", RSDBP_IN, date(0,0,0) );
  cmd.addParam( "", RSDBP_IN, {curdate} );
  cmd.addParam( "", RSDBP_IN, Priority);
  cmd.addParam( "", RSDBP_IN, ACCLAIM_STATUS_ACTIVE );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_STATUS_MODIFIED );
  cmd.addParam( "", RSDBP_IN, {curdate} );

  cmd.addParam( "", RSDBP_IN, Chapter );
  cmd.addParam( "", RSDBP_IN, Account );
  cmd.addParam( "", RSDBP_IN, Code_Currency );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_KIND_ARREST );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_TYPE_PARTIAL );
  cmd.addParam( "", RSDBP_IN, date(0,0,0) );
  cmd.addParam( "", RSDBP_IN, {curdate} );
  cmd.addParam( "", RSDBP_IN, Priority);
  cmd.addParam( "", RSDBP_IN, ACCLAIM_STATUS_ACTIVE );
  cmd.addParam( "", RSDBP_IN, ACCLAIM_STATUS_MODIFIED );
  cmd.addParam( "", RSDBP_IN, {curdate} );
  
  cmd.NullConversion = true;

  rs = RsdRecordset(cmd);
  
  var i = 0;
  
  var FreeAmount_1 = $0;

  while(rs.movenext)

    p = TWebAvailableRestGridItem();

    p.ClaimID         = SQL_ConvType(rs.value("t_ClaimID"));
    p.ClaimKindName   = GetRestKind_Name(SQL_ConvType(rs.value("RestKind")));
    p.ClaimSum        = SQL_ConvType(rs.value("ClaimSum"));
    p.ClaimNumber     = SQL_ConvType(rs.value("DocNumber"));

    if(FreeAmount_claimkindid_1 == $0)
      p.FreeAmount    = $0;
    else
      if(FreeAmount_claimkindid_1 == -1)
        p.FreeAmount = Rest - rs.value("ClaimSum");
      else
        if(i == 0)
          p.FreeAmount = FreeAmount_claimkindid_1 - rs.value("ClaimSum");
        else
          p.FreeAmount = FreeAmount_1 - rs.value("ClaimSum");
        end;
      end;
    end;
    
    FreeAmount_1 = p.FreeAmount;

    p.BeneficiaryName = BeneficiaryName;

    Arr[Arr.size] = p;
    
    i = i + 1;

  end;

  return Arr;

end;

macro WebCore_Get_AvailableRestGridItemList (AccountID, Rest)

  var cmd, rs, p, pp;

  var ClaimSum;

  var ClaimKindIDs;
  var ClaimKindNames;
  var ClaimSums;
  var FreeAmounts;

  var BeneficiaryName;

  var Arr = TArray;
  
  var AccountRec = TBFile("account.dbt", "R", 1 );

  AccountRec.Clear();
  AccountRec.rec.AccountID = AccountID;

  AccountRec.GetEQ();

  WEB_CB_FillAcClaimData(
    AccountID,
    {curdate},
    Rest,
    @ClaimSum,
    @ClaimKindIDs,
    @ClaimKindNames,
    @ClaimSums,
    @FreeAmounts,
    @BeneficiaryName
  );

  var i = 0;
  
  var FreeAmount_claimkindid_1 = -1;

  while(i < ClaimKindIDs.size)

    p = TWebAvailableRestGridItem();
        
    p.ClaimKindID     = ClaimKindIDs[i];
    p.ClaimKindName   = ClaimKindNames[i];
    p.ClaimSum        = ClaimSums[i];
    p.BeneficiaryName = BeneficiaryName;    

    if(p.ClaimKindID == ACC_FR_AM_GROUPS_FULL_AREST)
      p.Children = Get_ACC_FR_AM_GROUPS_FULL_AREST(
                                                    AccountRec.rec.Chapter,
                                                    AccountRec.rec.Account,
                                                    AccountRec.rec.Code_Currency,
                                                    BeneficiaryName
                                                  );
    end;

    if(p.ClaimKindID == ACC_FR_AM_GROUPS_SPECIAL)
      p.Children = Get_ACC_FR_AM_GROUPS_SPECIAL(
                                                  AccountRec.rec.Chapter,
                                                  AccountRec.rec.Account,
                                                  AccountRec.rec.Code_Currency,
                                                  BeneficiaryName,
                                                  FreeAmount_claimkindid_1,
                                                  Rest
                                                );
    end;
    
    if(p.ClaimKindID == ACC_FR_AM_GROUPS_RESERVE)
      p.Children = Get_ACC_FR_AM_GROUPS_RESERVE(
                                                  AccountRec.rec.Chapter,
                                                  AccountRec.rec.Account,
                                                  AccountRec.rec.Code_Currency,
                                                  BeneficiaryName,
                                                  FreeAmount_claimkindid_1,
                                                  Rest
                                                );
    end;

    if(p.ClaimKindID == ACC_FR_AM_GROUPS_AREST_SUM)
      p.Children = Get_ACC_FR_AM_GROUPS_AREST_SUM(
                                                    AccountRec.rec.Chapter,
                                                    AccountRec.rec.Account,
                                                    AccountRec.rec.Code_Currency,
                                                    BeneficiaryName,
                                                    FreeAmount_claimkindid_1,
                                                    Rest,
                                                    p.ClaimKindID - ACC_FR_AM_GROUPS_AREST_SUM
                                                  );
    end;

    p.FreeAmount      = FreeAmounts[i];
    
    FreeAmount_claimkindid_1 = p.FreeAmount;
    
    Arr[Arr.size] = p;

    i = i + 1;

  end;

  return Arr;

end;

macro WebCore_CalcFreeAmount(AccountID, CalcPriority)

  var AvailableRest = TWebAvailableRest();

  AvailableRest.CalcPriority = CalcPriority;

  WEB_CB_CalcFreeAmount(
    AccountID,
    {curdate},
    @(AvailableRest.Rest),
    @(AvailableRest.MinRest),
    @(AvailableRest.CalcPriority),
    @(AvailableRest.CalcFreeAmount),
    @(AvailableRest.CalcFreeLimitAmount)
  );

  return AvailableRest;

end;