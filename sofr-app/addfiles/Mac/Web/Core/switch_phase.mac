 /*
 $Name: switch_phase.mac
 $Module: Главная книга
 $Description: Макрос Переключение фазы операционного дня
 */

import globals;
import "ws_validation.mac";
import "ws_dprt.mac";
import "ws_file.mac";
import "ws_datafilter.mac";
import "pr_party_lib.mac";

class TWebPhase
  var Phase;
end;

class TWebSwitchPhaseObject
  var Operday;
  var Department;
  var OperCanExecServProcHead;
  var IsAllDprtSelected;
  var Phase;
  var SwitchInPhase;
  var BackoutInPhase;
end;

class TWebSwitchPhaseGroupGridItem
  var Switched;
  var Group;
  var GroupName;
  var Phase;
  var Oper;
end;

class TWebSwitchPhaseGroupPanelObject
  var Department              ; //TWsDepartment
  var OperDay                 : date;
  var IsReadOnly              : bool ;
  var OperCanExecServProcHead : bool ;
end;



PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

macro WebCore_BeforeShowSwitchPhasePanel()

  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  if({cTypePerson} != TP_BOSS)

    res = true;

  end;

  if(res != true)

    AddErrorMessageExt(@ErrorMessage, GetErrorMsg( 548 ));

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_InitSwitchPhasePanel()

  var res;

  var SwitchPhaseObj = TWebSwitchPhaseObject();

  SwitchPhaseObj.Operday = {curdate};
  SwitchPhaseObj.Department = GetTWsDepartmentByCode({OperDprt});

  var err;

  GetRegistryValue( "COMMON\\СЕРВИСНЫЕ ПРОЦЕДУРЫ\\ЗАПУСК СЕРВИСНЫХ ПРОЦЕДУР", V_BOOL, res, err );

  if(err == 0)

    if((res == true) AND ({OperDprt} == {NumDprt}))
      SwitchPhaseObj.OperCanExecServProcHead = true;
    else
      SwitchPhaseObj.OperCanExecServProcHead = false;
    end;

  else
    RunError("Ошибка при чтении настройки реестра!");
  end;

  SwitchPhaseObj.SwitchInPhase  = true;
  SwitchPhaseObj.BackoutInPhase = false;

  return SwitchPhaseObj;

end;

macro WebCore_ExecSwitchPhase(DprtID, Operday, Phase, IsBackout)

  var fc = null;

  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  var LogName : string = "";

  res = CB_RunSwitchPhase(DprtID, Operday, Phase, IsBackout, @LogName);

  if((DprtID == 0) AND (LogName != ""))

    fc = GetTextFile(LogName);

  end;

  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(fc);

  return responseBuilder.Result;

end;

macro WebCore_BeforeShowSwitchPhaseGroupPanel()

  var res = false;

  if({cTypePerson} == TP_BOSS)

    res = true;

  end;

  return res;

end;

/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else
      return value_false;
   end;
end;

private macro GetSortStr( OrderItems,            /* Параметры сортировки */
                          DefaultSortStr:string, /* Строка сортировки по умолчанию (первичному ключу) */
                          Table:string           /* Название таблицы c . */
                        ):String

  var SortStr:string = "";

  if( (OrderItems == null) or (OrderItems.Size() <= 0) )

    SortStr = " " + DefaultSortStr + " ";

  else

    var i:integer = 0;

    while( i < OrderItems.size() )
      if (OrderItems[i].Column == "Phase.Phase")
          OrderItems[i].Column = "t0.t_Phase";
      else
        if(OrderItems[i].Column == "Group")
          OrderItems[i].Column = "t0.t_GroupID";
        else
          if(OrderItems[i].Column == "GroupName")
            OrderItems[i].Column = "t0.t_Name";
          else
            if(OrderItems[i].Column == "Switched")
              OrderItems[i].Column = "t4.t_State";
            else
              if(OrderItems[i].Column == "Oper")
                OrderItems[i].Column = "t4.t_Oper";
              end;
            end;
          end;
        end;
      end;

      if (i!=0)
        SortStr = SortStr + ", " + OrderItems[i].Column + " " + IIF(OrderItems[i].Direction == 0, "asc", "desc");
      else
        SortStr = SortStr + "  " + OrderItems[i].Column + " " + IIF(OrderItems[i].Direction == 0, "asc", "desc");
      end;

      i = i + 1;
    end;

    SortStr = SortStr + " ";

  end;

  return SortStr;

end;

private macro GetQuery()

  var query = " SELECT t0.t_GroupID, "
              "        t0.t_Name, "
              "        t0.t_Phase, "
              "        t4.t_State, "
              "        t4.t_Oper "
              "   FROM dopdgroup_dbt t0, "
              "        (  SELECT t3.t_GroupID, MIN (t3.t_BankDate) t_BankDate "
              "             FROM dopdacgrp_dbt t3 "
              "            WHERE t3.t_BankDate >= ? "
              "         GROUP BY t3.t_GroupID) t3, "
              "        dopdacgrp_dbt t4 "
              "  WHERE     (   EXISTS "
              "                   (SELECT 1 "
              "                      FROM dopdowner_dbt t1 "
              "                     WHERE t1.t_GroupID = t0.t_GroupID AND t1.t_Oper = ? ) "
              "             OR EXISTS "
              "                   (SELECT 1 "
              "                      FROM dopdphaseowner_dbt t2 "
              "                     WHERE t2.t_Phase = t0.t_Phase AND t2.t_Oper = ? )) "
              "        AND t4.t_GroupID(+) = t3.t_GroupID "
              "        AND t4.t_BankDate(+) = t3.t_BankDate "
              "        AND t3.t_GroupID(+) = t0.t_GroupID "
              "        AND t0.t_Department = ? ";

  return query;

end;

private const FILTERITEM_PHASE = 0; // Фаза
private const FILTERITEM_STATE = 1; // Состояние группы

private macro GetAddWhereCondition(FilterItems)

  var condition = "";

  var FilterItemsCount = 0;
  var ValueCount = 0;

  while( FilterItemsCount < FilterItems.Size )

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_PHASE ) // Фаза

      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Equal )
        
        if((FilterItems[FilterItemsCount].Value.Size > 0) AND
           (FilterItems[FilterItemsCount].Value[0] != null))
           
          condition = condition + " AND t0.t_Phase ";
          condition = condition + " = ";
          condition = condition + (FilterItems[FilterItemsCount].Value[0].Phase);
          
        end;

      end;

    end;

    if( FilterItems[FilterItemsCount].Id == FILTERITEM_STATE ) // Состояние группы

      if(FilterItems[FilterItemsCount].Value.Size > 0)

        ValueCount = 0;

        condition = condition + " AND ( 1!=1 ";

        while( ValueCount < FilterItems[FilterItemsCount].Value.Size )

          if(FilterItems[FilterItemsCount].Value[ValueCount] == 0);

            condition = condition + " OR t4.t_State = CHR(0) ";

          end;

          if(FilterItems[FilterItemsCount].Value[ValueCount] == 1);

            condition = condition + " OR t4.t_State = 'X' ";

          end;

          ValueCount = ValueCount + 1;

        end;

        condition = condition + " ) ";

      end;

    end;

    FilterItemsCount = FilterItemsCount + 1;

  end;

  return condition;

end;

macro WebCore_GetSwitchPhaseGroupGridItemCount(filteritems, DprtID, Operday)

  var res : integer = 0;

  var cmd, rs;

  var query = " SELECT count(*) c FROM ( ";

  query = query + GetQuery();

  var strAddWhere = GetAddWhereCondition( filteritems );

  if( strAddWhere != "" )
    query = query + strAddWhere;
  end;

  query = query + " ) ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, Operday );
  cmd.addParam( "", RSDBP_IN, {oper} );
  cmd.addParam( "", RSDBP_IN, {oper} );
  cmd.addParam( "", RSDBP_IN, DprtID );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    res = rs.value("c");

  end;

  return res;

end;

macro WebCore_GetSwitchPhaseGroupGridItemList(PageSize, PageNum, filteritems, orderitems, DprtID, Operday)

  var cmd, rs, p;

  var Arr = TArray;

  var query = " SELECT t.* FROM ( SELECT rownum RN, tt.* FROM ( ";

  query = query + GetQuery();

  var strAddWhere = GetAddWhereCondition( filteritems );

  if( strAddWhere != "" )

     query = query + strAddWhere;

  end;

  query = query + " ORDER BY " + GetSortStr(orderitems,"t0.t_GroupID DESC","");

  if( PageSize > 0 )
    query = query + " ) tt WHERE ROWNUM <= " + string(PageSize*(PageNum + 1)) + " ) t WHERE t.RN >= " + string(PageSize*(PageNum) + 1) + " ";
  else
    query = query + " ) tt ) t ";
  end;

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, Operday );
  cmd.addParam( "", RSDBP_IN, {oper} );
  cmd.addParam( "", RSDBP_IN, {oper} );
  cmd.addParam( "", RSDBP_IN, DprtID );

  rs = RsdRecordset(cmd);

  while(rs.movenext)

    p = TWebSwitchPhaseGroupGridItem();

    p.Switched  = SQL_ConvTypeBool(rs.value("t_State"));
    p.Group     = SQL_ConvType(rs.value("t_GroupID"));
    p.GroupName = SQL_ConvType(rs.value("t_Name"));
    
    p.Phase = TWebPhase();
    p.Phase.Phase = SQL_ConvType(rs.value("t_Phase"));
    
    p.Oper      = SQL_ConvType(rs.value("t_Oper"));

    Arr[Arr.size] = p;

  end;

  return Arr;

end;

macro WebCore_ExecSwitchPhaseGroup(GroupID, Operday)

  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = CB_SwitchPhaseGroup(GroupID, Operday);

  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_ExecBackoutPhaseGroup(GroupID, Operday, Phase)

  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = CB_BackoutPhaseGroup(GroupID, Operday, Phase);

  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_InitSwitchPhaseGroupPanel(Department, OperDay:Date):TWebSwitchPhaseGroupPanelObject 
  
  var PanelObj  = TWebSwitchPhaseGroupPanelObject();
   
  if(Department!=null)
   
    PanelObj.Department=Department;
	 
  else
   
    PanelObj.Department=GetTWsDepartmentByCode({OperDprt});
	  
  end;

  if(OperDay!=null)
   
   if(OperDay!=Date(0,0,0))
   
     PanelObj.OperDay=OperDay;
	 
	else
	
	  PanelObj.OperDay={curdate};
	  
	end;
	 
  else
   
    PanelObj.OperDay={curdate};
   
  end;
   
  if( (OperDay!=null) or (Department!=null) or (OperDay!=Date(0,0,0)))
   
    PanelObj.IsReadOnly=true;
	 
  else
   
    PanelObj.IsReadOnly=false;
   
  end;
   
  var err=0;
	 
  GetRegistryValue( "COMMON\\СЕРВИСНЫЕ ПРОЦЕДУРЫ\\ЗАПУСК СЕРВИСНЫХ ПРОЦЕДУР", V_BOOL, true, err );
	 
  if(err == 0)
    
    if( {OperDprt} == {NumDprt})
		
      PanelObj.OperCanExecServProcHead=true;
		
	else
	  
	  PanelObj.OperCanExecServProcHead=false;
		
	end;
	 
  else

    PanelObj.OperCanExecServProcHead=false; 	
	  
  end;

   return PanelObj;

end;