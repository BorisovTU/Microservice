/*
$Name:        operday_set.mac
$Module:      Главная книга
$Description: Макрос Установка текущего дня филиала
*/

import globals;
import "ws_validation.mac";
import "ws_dprt.mac";
import "ws_file.mac";

class TWebSwitchOperdayObject
  var CurDate;
  var NewCurDate;
  var Department;
  var OperCanExecServProcHead;
  var IsAllDprtSelected;
end;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

macro WebCore_BeforeShowSwitchOperdayPanel()

  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  if(({cTypePerson} != TP_BOSS) AND (CheckActionRestriction(144) == true))

    res = true;

  end;

  if(res != true)

    CreateErrMsg(22167);
    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;
  
end;

macro WebCore_InitSwitchOperdayPanel()

  var res;

  var SwitchOperdayObj = TWebSwitchOperdayObject();
  
  SwitchOperdayObj.Department = GetTWsDepartmentByCode({OperDprt});

  var err;

  GetRegistryValue( "COMMON\\СЕРВИСНЫЕ ПРОЦЕДУРЫ\\ЗАПУСК СЕРВИСНЫХ ПРОЦЕДУР", V_BOOL, res, err );

  if(err == 0)
    
    if((res == true) AND ({OperDprt} == {NumDprt}))
      SwitchOperdayObj.OperCanExecServProcHead = true;
    else
      SwitchOperdayObj.OperCanExecServProcHead = false;
    end;
    
  else
    RunError("Ошибка при чтении настройки реестра!");
  end;   
  
  var CurDate = date(0,0,0);
  
  CurDate = CB_GetCurDate({OperDprt});
  
  SwitchOperdayObj.CurDate = CurDate;
  
  var NewCurDate = date(0,0,0);
  
  NewCurDate = CB_GetNewCurDate({OperDprt}, SwitchOperdayObj.CurDate);
  
  SwitchOperdayObj.NewCurDate = NewCurDate;
    
  return SwitchOperdayObj;

end;

macro WebCore_ExecSwitchOperday(DprtID, CurDate)

  var fc = null;
  
  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  if((DprtID !=0) AND (DprtID != {OperDprt}))
    
    var err;

    GetRegistryValue( "COMMON\\СЕРВИСНЫЕ ПРОЦЕДУРЫ\\ЗАПУСК СЕРВИСНЫХ ПРОЦЕДУР", V_BOOL, res, err );

    if(err == 0)
      
      if((res != true) OR ({OperDprt} != {NumDprt}))
        
        AddErrorMessageExt(@ErrorMessage, GetErrorMsg( 22006 ));
        
        res = false;
        
      end;
      
    else
      RunError("Ошибка при чтении настройки реестра!");
    end; 
    
  end;  
    
  var LogName : string = "";

  res = ChooseDprtDay(DprtID, CurDate, @LogName);
  
  if(LogName != "")
  
    fc = GetTxtFileName(LogName);
    
    fc = ConvertTxt2Html(fc); 
    
    fc = GetTextFile(fc);
  
  end;

  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(fc);

  return responseBuilder.Result;
  
end;

macro WebCore_GetSwitchOperdayDatesPanel(Department, IsAllDprtSelected)

  var SwitchOperdayObj = TWebSwitchOperdayObject();

  if(IsAllDprtSelected == true)
    
    SwitchOperdayObj.CurDate    = CB_GetCurDate(0);
    SwitchOperdayObj.NewCurDate = CB_GetNewCurDate(0, SwitchOperdayObj.CurDate);
    
  else
    
    if(Department != null)
    
      SwitchOperdayObj.CurDate    = CB_GetCurDate(Department.Code);
      SwitchOperdayObj.NewCurDate = CB_GetNewCurDate(Department.Code, SwitchOperdayObj.CurDate);
    
    end;
  
  end;
  
  return SwitchOperdayObj;

end;
