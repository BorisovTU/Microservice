/*
$Name:        rescarry_widget.mac
$Module:      Главная книга
$Description: Макрос виджета выбора вида проводки
*/

Import RSD;

class TWebResCarry
  var ResultCarry;
  var ShortName;
end;

class TWebResCarryListerItem
  var ResultCarry;
  var ShortName;
end;

macro WebCore_GetResCarryListerItemList(sub, listLen, PageSize, PageNum) // ?? PageSize, PageNum
  
  var cmd,rs;

  if(PageSize == null)
    PageSize = 0;
  end;

  if(PageNum == null)
    PageNum = 0;
  end;

  var Arr = TArray;

  var query = " SELECT t.* FROM ( SELECT rownum RN, tt.* FROM ( ";
  
  query = query + "SELECT rsc.T_RESULT_CARRY, rsc.T_SHORT_NAME "
                      "FROM DRESCARRY_DBT rsc "
                      "WHERE 1=1";
  
  if ((sub != "") AND (sub != null))
    query = query + " AND LOWER(T_RESULT_CARRY) LIKE LOWER ('%' || ? || '%') " ;
  end;

  if ((listLen != 0) AND (listLen != null))
    query = query + " AND ROWNUM < " + string(listLen);
  end;

  query = query + " ORDER BY rsc.T_RESULT_CARRY";

  if( PageSize > 0 )
    query = query + " ) tt WHERE ROWNUM <= " + string(PageSize*(PageNum + 1)) + " ) t WHERE t.RN >= " + string(PageSize*(PageNum) + 1) + " ";
  else
    query = query + " ) tt ) t ";
  end;
  
  cmd = RsdCommand(query);

  if ((sub != "") AND (sub != null))
     cmd.addParam( "", RSDBP_IN, sub );
  end;

  rs = RsdRecordset(cmd);

  while (rs.movenext)
      
      var obj = TWebResCarryListerItem;
      
      obj.ResultCarry  = rs.value("T_RESULT_CARRY");
      obj.ShortName    = rs.value("T_SHORT_NAME");
      
      Arr[Arr.size] = obj;

  end;

  return Arr;

end;

macro WebCore_GetResCarryListerItemByNum(ResultCarry)

  var obj;

  var cmd, rs;

  var query = " SELECT rsc.* FROM DRESCARRY_DBT rsc WHERE rsc.T_RESULT_CARRY =  " + string(ResultCarry) ;

  cmd = RsdCommand(query);

  rs = RsdRecordset(cmd);

  if (rs.movenext)

     obj =  TWebResCarry;

     obj.ResultCarry  = rs.value("T_RESULT_CARRY");
     obj.ShortName = rs.value("T_SHORT_NAME");

  end;

  return obj;

end;  

macro WebCore_CreateTWebResCarry(ResultCarry, ShortName)

  var obj = TWebResCarry;

  obj.ResultCarry   = ResultCarry;
  obj.ShortName     = ShortName;
    
  return obj;

end;