/*
$Name:        grpblnc_scroll.mac
$Module:      Главная книга
$Description: Макрос содержит реализацию сервисов для скроллинга групп балансовых счетов
*/

import "ws_partycommon.mac";
import "ws_person.mac";
import "ws_validation.mac";

class TWebGroupBalAccGridItem
  var Chapter;
  var GroupID;
  var Name;
end;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

macro WebCore_GetGroupBalAccountGridItemList(Chapter)

  var cmd, rs;

  var Arr = TArray;

  var query = " SELECT T_CHAPTER, T_CBLNCGROUP, T_SZNAME "
              "  FROM DBGROUP_DBT WHERE 1=1 ";

  if(Chapter != NULL)

    query = query + " AND T_CHAPTER = ? ";

  end;

  cmd = RsdCommand(query);

  if(Chapter != NULL)

    cmd.addParam( "", RSDBP_IN, Chapter );

  end;

  cmd.NullConversion = true;

  cmd.Execute();

  rs = RsdRecordset(cmd);

  while(rs.movenext)

    var obj = TWebGroupBalAccGridItem;

    obj.Chapter = SQL_ConvType(rs.value("T_CHAPTER"));;
    obj.GroupID = SQL_ConvType(rs.value("T_CBLNCGROUP"));
    obj.Name    = SQL_ConvType(rs.value("T_SZNAME"));

    Arr[Arr.size] = obj;

  end;

  return Arr;

end;

macro WebCore_InsertUpdateGroupBalAccount(Action, GroupBalAcc, saveGroupBalAcc)

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  file bgroup("bgroup.dbt") key 0 write;

  ClearRecord(bgroup);

  if(Action == 1) /*Create*/

    bgroup.Chapter    = GroupBalAcc.Chapter;
    bgroup.cBlncGroup = GroupBalAcc.GroupID;

    res = GetEQ( bgroup );
    
    if(res == true)

      res = false;

      AddErrorMessageExt(@ErrorMessage, 11, "Уже есть такая группа");

    else

      bgroup.szname = GroupBalAcc.Name;

      res = Insert(bgroup);

    end;    

  else

    if(Action == 2) /*Modify*/
    
      var n1;
    
      bgroup.Chapter    = GroupBalAcc.Chapter;
      bgroup.cBlncGroup = GroupBalAcc.GroupID;

      res = GetEQ( bgroup );
      
      if(res == true)
      
        n1 = getpos(bgroup);
      
      else
        
        n1 = null;
      
      end;

      bgroup.Chapter    = saveGroupBalAcc.Chapter;
      bgroup.cBlncGroup = saveGroupBalAcc.GroupID;

      res = GetEQ( bgroup );
      
      if(res == true)
      
        var n2 = getpos(bgroup);

        if((n1 == null) OR (n1 == n2))

          bgroup.cBlncGroup = GroupBalAcc.GroupID;
          bgroup.szname     = GroupBalAcc.Name;

          res = UpDate(bgroup);
          
        else
          
          res = false;
          
          AddErrorMessageExt(@ErrorMessage, 11, "Уже есть такая группа");

        end;
      
      end;

    end;

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_DeleteGroupBalAccount (Chapter, GroupId)

  file bgroup("bgroup.dbt") key 0 write;

  var res = True;

  bgroup.Chapter    = Chapter;
  bgroup.cBlncGroup = GroupId;

  res = GetEQ( bgroup );

  if(res == true)

    res = Delete(bgroup);

  end;

  return res;

end;