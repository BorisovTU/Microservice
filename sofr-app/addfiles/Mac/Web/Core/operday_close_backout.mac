 /*
 $Name: operday_close_backout.mac
 $Module: Главная книга
 $Description: Макрос Отмена завершения операционного дня
 */

import globals;
import "ws_validation.mac";
import "ws_dprt.mac";
import "ws_file.mac";

class TWebPhase
  var Phase : integer;
end;

class TWebOperdayCloseDateBackoutObject
    var LastClosedDate;
    var Department;
    var OperCanExecServProcHead;
    var IsAllDprtSelected;
    var Phase;
end;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

macro WebCore_BeforeShowCloseDateBackoutPanel(DprtID, CloseDate)

  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  if(({cTypePerson} != TP_BOSS) AND (CheckActionRestriction(145) == true))

    res = true;

  end;

  if(res != true)

    CreateErrMsg(22170);
    AddErrorMessage(@ErrorMessage);

  end;
  
  if((DprtID != 0) AND (CloseDate != null) AND (res == true))
  
    var LastClosedDate = date(0,0,0);
    
    res = CB_GetLastDate(DprtID, @LastClosedDate);
    
    if((res == true) AND (CloseDate != LastClosedDate))
      
      AddErrorMessageExt(@ErrorMessage, GetErrorMsg( 22112 ));
      
      res = false;
      
    end;

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;
  
end;

macro WebCore_InitCloseDateBackoutPanel(DprtID)

  var res;

  var CloseDateBackoutObj = TWebOperdayCloseDateBackoutObject();
  
  CloseDateBackoutObj.Department = GetTWsDepartmentByCode(DprtID);

  var err;

  GetRegistryValue( "COMMON\\СЕРВИСНЫЕ ПРОЦЕДУРЫ\\ЗАПУСК СЕРВИСНЫХ ПРОЦЕДУР", V_BOOL, res, err );

  if(err == 0)
    
    if((res == true) AND ({OperDprt} == {NumDprt}))
      CloseDateBackoutObj.OperCanExecServProcHead = true;
    else
      CloseDateBackoutObj.OperCanExecServProcHead = false;
    end;
    
  else
    RunError("Ошибка при чтении настройки реестра!");
  end;   
  
  var LastClosedDate = date(0,0,0);
  
  res = CB_GetLastDate(DprtID, @LastClosedDate);
  
  if(res == true)
    
    CloseDateBackoutObj.LastClosedDate = LastClosedDate;
    
  end;
  
  CloseDateBackoutObj.Phase = TWebPhase();

  return CloseDateBackoutObj;

end;

macro WebCore_GetCloseDateBackoutPanel(Department, IsAllDprtSelected)

  var res;

  var LastClosedDate;

  if(IsAllDprtSelected == true)
    
    res = CB_GetLastDate(0, @LastClosedDate);
    
  else
    
    if(Department != null)
      
      res = CB_GetLastDate(Department.Code, @LastClosedDate);
      
    end;
  
  end;
  
  return LastClosedDate;

end;

macro WebCore_ExecCloseDateBackout(DprtID, CloseDate, Phase)

  var fc = null;
  
  var res = true;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  var LogName : string = "";

  if(DprtID != 0)
    
    res = CB_CheckBackoutCloseDay(DprtID, CloseDate);
    
  end;

  if(res == true)
    
    if((DprtID !=0) AND (DprtID != {OperDprt}))
      
      var err;

      GetRegistryValue( "COMMON\\СЕРВИСНЫЕ ПРОЦЕДУРЫ\\ЗАПУСК СЕРВИСНЫХ ПРОЦЕДУР", V_BOOL, res, err );

      if(err == 0)
        
        if((res != true) OR ({OperDprt} != {NumDprt}))
          
          AddErrorMessageExt(@ErrorMessage, GetErrorMsg( 22111 ));
          
          res = false;
          
        end;
        
      else
        RunError("Ошибка при чтении настройки реестра!");
      end; 
      
    end;  
    
  end;
  
  if(res == true)
    
  res = CB_CloseDate_BackOut(DprtID, CloseDate, Phase, @LogName);
  
  if(LogName != "")
  
    fc = GetTxtFileName(LogName);
    
    fc = ConvertTxt2Html(fc); 
    
    fc = GetTextFile(fc);
  
  end;

  end;

  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(fc);

  return responseBuilder.Result;
  
end;