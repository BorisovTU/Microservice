/*
$Name:        DocKind_Widget.mac
$Module:      Главная книга
$Description: сервисы для виджета выбора вида первичного документа.  
*/

IMPORT BankInter;

import "ws_file.mac";
import "ws_validation.mac";

class TWebOprKDoc 
 
  var Name          : String ; 
  
  var DocKind       : integer ; 
       
end;

class TWebOprKDocListerItem 
 
  var Name     : String ;

  var DocKind  : integer ;  
  
  var Children ;//List<TWebOprKDocListerItem>
    
end;



PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro GetErrorMessage(ErrorCode : @integer, ErrorMessage : @string)

  var objErr = AL_GetErrorInfo();

  ErrorCode     = objErr.stat;
  ErrorMessage  = objErr.message;

  InitError();

end;

private macro ConvertBool(CurValue :bool ) : string

  if(CurValue==true)

	return "CHR (88)";
	
  else
  
    return "CHR (0)";
	
  end;
  
end;

macro WebCore_GetOprKDocListerItem(Primary, Str)

  var query_cond="";

  if(Str != null)
  
    query_cond =  " AND  okd.T_DOCKIND = " + Str ;
  
  end;
  
  var innerquery  = " SELECT LEVEL,													"
                    "        XMLELEMENT (OPRKDOC,									"
                    "        XMLELEMENT (DOCKIND, okd.T_DOCKIND),					"
                    "        XMLELEMENT (NAME, okd.T_NAME))							"
					"	FROM DOPRKDOC_DBT okd										"					 
                    "	WHERE okd.T_PRIMARY = " +ConvertBool (Primary) + query_cond +
					"        AND REGEXP_LIKE (okd.T_NAME,''\\D+'') 					"
				    "        CONNECT BY PRIOR okd.T_DOCKIND = okd.T_PARENTDOCKIND	"
					"		 ORDER SIBLINGS BY okd.T_DOCKIND					    ";

  var outerquery=   " SELECT 1, "
					" XMLELEMENT(OPRKDOC_LIST, " 
					"(SELECT DBMS_XMLGEN.GETXMLTYPE "
					"(DBMS_XMLGEN.NEWCONTEXTFROMHIERARCHY  ( '"+innerquery +"') )"
					" FROM DUAL)).GETCLOBVAL() XMLDOC  FROM DUAL " ;
					
  var ds = TRsbDataSet(outerquery );
  
  var result;

  if( ds.MoveNext() )
  
    result = ds.XMLDOC;
	
  else
	
	result="";
	
  end;

  return result;
  
end;

macro GetTWebOprKDocFromBD( DocKind ) 

  var obj = TWebOprKDoc();

  var cmd, rs;

  var query = " SELECT * FROM DOPRKDOC_DBT okd WHERE  okd.T_DOCKIND = ? ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, DocKind );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    obj.Name   = rs.value("T_NAME");
    obj.DocKind = rs.value("T_DOCKIND");

  end;

  return obj;

end;

