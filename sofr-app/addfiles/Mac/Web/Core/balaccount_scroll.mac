/*
$Name:             balaccount_scroll.mac
$Module:           Ядро ГКБО 
$Description:      Сервисы балансовых счетов
*/


import rsd, cb_sql;
import RsbDataSet;
import "ws_validation.mac";
import "ws_datafilter.mac";

const BalanceFNum      = 1;
const Kind_AccountFNum = 2; 
const Type_BalanceFNum = 3; 
const BlncUsedBWPFNum  = 4;


class TWebBalAccountGridItem
  var ID            : integer;
  var iNumPlan      : integer;   
  var Balance       : string ;     
  var Chapter       : integer;     
  var Kind_Account  : string ;
  var Name_Part     : string ;   
  var Part          : integer;       
  var Type_Balance  : string ;
  var ExtraType     : string ;   
  var cBlncUsedBWP  : bool   ;
end;
/*
class BalAccountListInfo
  var BalAccountListCollection;
  var BalAccountCount;
end;
*/

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else 
      return value_false;
   end;
end;

private macro GetSortStr( OrderItems,            /* Параметры сортировки */
                          DefaultSortStr:string, /* Строка сортировки по умолчанию (первичному ключу) */
                          Table:string           /* Название таблицы c . */
                        ):String
                        
  var SortStr:string = "";

  if( (OrderItems == null) or (OrderItems.Size() <= 0) )
  
     SortStr = " " + DefaultSortStr + " ";
     
  else
  
     SortStr = " " + OrderItems[0].Column + " " + IIF(OrderItems[0].Direction == 0, "asc", "desc");
     var i:integer = 1;
     while( i < OrderItems.size() )
        SortStr = SortStr + ", " + OrderItems[i].Column + " " + IIF(OrderItems[i].Direction == 0, "asc", "desc");
        i = i + 1;
     end;
     SortStr = SortStr + " ";
     
  end;

  return SortStr;
  
end;


private macro StringCondition(filteritems, value, FlagUpper) : string

  var condition = "";
  var conditionNOT = "";
  var condB = "";
  var condE = "";

  if( filteritems.IsNot )
    conditionNOT = " NOT ";
  else
    conditionNOT = " ";
  end;

  if( filteritems.Condition == FilterCondition_Null )
    condB = " = CHR(1) ";
  end;

  if( filteritems.Condition == FilterCondition_Equal )
    condB = " LIKE ";
    condE = " ";
  end;

  if( filteritems.Condition == FilterCondition_StartWith )
    condB = " LIKE ";
    condE = " || '%'";
  end;

  if( filteritems.Condition == FilterCondition_Include )
    condB = " LIKE '%' || ";
    condE = " || '%'";
  end;

  condition = condition + conditionNOT;

  condition = condition + condB;

  if( filteritems.Condition != FilterCondition_Null )

    if(FlagUpper)

      condition = condition + " UPPER ('" + string(value) + "') ";

    else

      condition = condition + "'" + string(value) + "'";

    end;
  end;

  condition = condition + condE;

  return condition;

end;


private macro GetAddWhereCondition(
  FilterItems,/*:TArray of FilterCondItem*/
  where,
  cmd
):String

  var conditionNOT = "";

  var condition = where;

  var FilterItemsCount = 0;
  var ValueCount = 0;

  while( FilterItemsCount < FilterItems.Size )
    var FilterItemsCountI = FilterItemsCount;
    if( FilterItems[FilterItemsCountI].Id == BalanceFNum ) // Номер

      if(condition != "")
        condition = condition + " AND ";
      end;

      condition = condition + " UPPER(t_Balance) ";
//      println("Condition = ", FilterItems[FilterItemsCount].Condition);
//      println("Value[0] = ", FilterItems[FilterItemsCount].Value[0]);

      condition = condition + StringCondition(FilterItems[FilterItemsCount], FilterItems[FilterItemsCount].Value[0], true);

      // cmd.addParam( "", RSDBP_IN, FilterItems[FilterItemsCount].Value[0] );
    end;

    if( FilterItems[FilterItemsCountI].Id == Kind_AccountFNum ) // Вид
    
      if(FilterItems[FilterItemsCount].Value.Size > 0)
    
        if( FilterItems[FilterItemsCount].IsNot )
          conditionNOT = " AND NOT (1 = 1 ";
        else
          conditionNOT = " ";
      end;

        condition = condition + conditionNOT;      
      
        ValueCount = 0;

        while( ValueCount < FilterItems[FilterItemsCount].Value.Size )

          if(FilterItems[FilterItemsCount].Value[ValueCount] == 0);

            condition = condition + " AND UPPER(t_Kind_Account) = 'А' ";

          end;

          if(FilterItems[FilterItemsCount].Value[ValueCount] == 1);

            condition = condition + " AND UPPER(t_Kind_Account) = 'П' ";

          end;

          if(FilterItems[FilterItemsCount].Value[ValueCount] == 2);

            condition = condition + " AND UPPER(t_Kind_Account) = '0' ";

          end;
          
          if(FilterItems[FilterItemsCount].Value[ValueCount] == 3);

            condition = condition + " AND UPPER(t_Kind_Account) = 'АП' ";

          end;

          ValueCount = ValueCount + 1;

        end;
        
        if( FilterItems[FilterItemsCount].IsNot )
          condition = condition + " ) ";
        end; 
      
      end;

    end;


    if( FilterItems[FilterItemsCountI].Id == Type_BalanceFNum ) // Группа
      if(condition != "")
        condition = condition + " AND ";
      end;

      condition = condition + " UPPER(t_Type_Balance) ";

      condition = condition + StringCondition(FilterItems[FilterItemsCount], FilterItems[FilterItemsCount].Value[0], true);
    end;

    if( FilterItems[FilterItemsCountI].Id == BlncUsedBWPFNum ) // Принадлежность к РПС
      if( FilterItems[FilterItemsCount].Condition == FilterCondition_Equal )

        if(condition != "")
          condition = condition + " AND ";
        end;

        if(FilterItems[FilterItemsCount].Value[0])
          condition = condition + " t_cBlncUsedBWP = 'X'";
        else
          condition = condition + " t_cBlncUsedBWP = CHR(0)";
        end;
      end;
    end;

    FilterItemsCount = FilterItemsCount + 1;
  end;

  return condition;
end;



macro SetBalAccountQuery
(
  pagesize      : integer,
  pagenum       : integer,
  Chapter,
  NumPlan,
  FilterItems,
  orderitems,
  cmd
)
  var query = " SELECT t.* FROM ( SELECT rownum RN, tt.* FROM (SELECT t_ID as ID, t_iNumPlan as iNumPlan, t_Balance as Balance, t_Chapter as Chapter, t_Kind_Account as Kind_Account, t_Name_Part as Name_Part, t_Part as Part, t_Type_Balance as Type_Balance, t_ExtraType as ExtraType, t_cBlncUsedBWP as cBlncUsedBWP FROM dbalance_dbt ";
  var where = "";

  if((Chapter != NULL) AND (Chapter != 0))
    where = where + " t_Chapter = ? ";
    cmd.addParam( "", RSDBP_IN, Chapter );
  end;          

  if(NumPlan != NULL)
    if(where != "")
      where = where + " AND ";
    end;
    where = where + " t_iNumPlan = ? ";
    cmd.addParam( "", RSDBP_IN, NumPlan );
  end;          

  where = GetAddWhereCondition(FilterItems, where, cmd); 

  if(where != "")
    query = query + " WHERE ";    
    query = query + where;    
  end;
              
  query = query + " ORDER BY " + GetSortStr(orderitems, "Balance", "");

  if( PageSize > 0 )
     query = query + " ) tt WHERE ROWNUM <= " + string(PageSize*(PageNum + 1)) + " ) t WHERE t.RN >= " + string(PageSize*(PageNum) + 1) + " ";
  else
     query = query + " ) tt ) t ";
  end;
  
  return query;
end;

macro WebCore_GetBalAccountGridItemList
(
  pagesize      : integer,
  pagenum       : integer,
  Chapter,
  NumPlan,
  FilterItems,
  orderitems
)

  var cmd = RSDCommand();
  
  var query = SetBalAccountQuery(pagesize,
                              pagenum,
                              Chapter,
                              NumPlan,
                              FilterItems,
                              orderitems,
                              cmd);
 
  cmd.CmdText = query;
  cmd.NullConversion = true;

  println("*** GetBalAccountList ***");
  println( cmd.CmdText );

  var rs = rsdRecordSet( cmd );

  var result = TArray();

  while( rs.moveNext() )
    var balance = TWebBalAccountGridItem;
    
    balance.ID            = rs.value("ID");
    balance.iNumPlan      = rs.value("iNumPlan");
    balance.Balance       = rs.value("Balance");
    balance.Chapter       = rs.value("Chapter");
    balance.Kind_Account  = rs.value("Kind_Account");
    balance.Name_Part     = rs.value("Name_Part");
    balance.Part          = rs.value("Part");
    balance.Type_Balance  = rs.value("Type_Balance");
    balance.ExtraType     = rs.value("ExtraType");
    balance.cBlncUsedBWP  = SQL_ConvTypeBool(rs.value("cBlncUsedBWP"));

    result.value(result.Size) = balance;
  end;

  return result;
end;
      
macro WebCore_GetBalAccountGridItemCount
(
  Chapter,   
  NumPlan,
  FilterItems,
  orderitems
):integer

  var result : integer;
  
  var cmd = RSDCommand();
  
  var query = "SELECT COUNT(*) C FROM ( SELECT * FROM ( ";


  query = query + SetBalAccountQuery(0,
                                     0,
                                     Chapter,
                                     NumPlan,
                                     FilterItems,
                                     orderitems,
                                     cmd);
  
  query = query + "))";
 
  cmd.CmdText = query;
  cmd.NullConversion = true;

  println("*** GetBalAccountCount ***");
  println( cmd.CmdText );

  var rs = rsdRecordSet( cmd );  

  if (rs.moveNext())

    result = rs.value("c");

  end;

  return result;

end;

macro WebCore_DeleteBalAccount (Chapter, iNumPlan, Balance)

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = CB_DeleteBalAccount(Chapter, iNumPlan, Balance);
  
  if(res != True)

    AddErrorMessage(@ErrorMessage);

  end;

  ResponseBuilder.SetUserObject(res);

  return ResponseBuilder.Result;  
end;


macro WebCore_GetBalAccountTitle
(
  Chapter,   
  NumPlan
):string

  var Title : string;
  
  var cmd = RSDCommand();
  
  var query = "SELECT T_NAME FROM DOBCHAPTR_DBT WHERE T_CHAPTER = ?";

  cmd.CmdText = query;
  cmd.NullConversion = true;
  cmd.addParam( "", RSDBP_IN, Chapter );

  var rs = rsdRecordSet( cmd );  

  if (rs.moveNext())
     Title = rs.value("T_NAME");
     Title = Title + ".";
  end;


  cmd = RSDCommand();
  query = "SELECT T_NAMEPLAN FROM dnameplan_dbt WHERE T_INUMPLAN = ?";
 
  cmd.CmdText = query;
  cmd.NullConversion = true;
  cmd.addParam( "", RSDBP_IN, NumPlan );

  rs = rsdRecordSet( cmd );  

  if (rs.moveNext())
     Title = Title + rs.value("T_NAMEPLAN");
  end;

  return Title;
end;
