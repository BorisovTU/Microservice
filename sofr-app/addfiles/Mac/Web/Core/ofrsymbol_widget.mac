/*
$Name:        ofrsymbol_widget.mac
$Module:      Главная книга
$Description: Макрос виджета выбора символа ОФР
*/

import globals, "ws_partycommon.mac";

class TWebOFRSymbol
  var RecID;
  var Code;
  var Name;
end;

class TWebOFRSymbolListerItem
  var RecID;
  var Code;
  var Balance;
  var Name;
  var Principle;
  var OpenDate;
  var CloseDate;
end;

private macro GetQuery()

  var query = " SELECT * "
              "   FROM (  SELECT s.*, "
              "                  (SELECT sh.t_name "
              "                     FROM DOPUSYMHIST_DBT sh "
              "                    WHERE sh.t_ofrrecid = s.t_recid AND sh.t_startdate = t.startdate) "
              "                     name "
              "             FROM dopusymb_dbt s, "
              "                  (  SELECT MAX (sh.t_startdate) startdate, s.t_code "
              "                       FROM dopusymb_dbt s, DOPUSYMHIST_DBT sh "
              "                      WHERE sh.t_ofrrecid = s.t_recid AND sh.t_startdate <= ? "
              "                   GROUP BY s.t_code "
              "                  ) t "
              "            WHERE s.t_code = t.t_code (+) "
              "         ) tt"
              "  WHERE 1 = 1 ";
              
  return query;
end;

macro WebCore_GetOFRSymbolListerItemList(CodeMask, sub, listLen)

  var cmd, rs;

  var Arr = TArray;

  var ConvertMask : string;

  var query = GetQuery();
  
  if( (CodeMask != NULL) AND (CodeMask != "") )

    ConvertMask = "";

    ConvertMask = ConvertMaskToSQLFormat( CodeMask, "tt.t_code" );

    if( ConvertMask == "" )
      ConvertMask = "1 = 1";
    end;

    query = query + " AND (" + ConvertMask + ") ";

  end;
  
  if(sub != "")
  
    query = query + " AND LOWER(tt.t_code) LIKE LOWER ('%' || ? || '%') ";
  
  end;

  if( listLen != 0 )

    query = query + " AND ROWNUM < " + string(listLen);

  end;
  
  query = query + " ORDER BY tt.t_code ";

  cmd = RsdCommand(query);
  
  cmd.addParam( "", RSDBP_IN, {curdate} );
  
  if(sub != "")
  
    cmd.addParam( "", RSDBP_IN, sub );
  
  end;
  
  rs = RsdRecordset(cmd);

  while(rs.movenext)

    var obj = TWebOFRSymbolListerItem;

    obj.RecID     = rs.value("t_recid");
    obj.Code      = rs.value("t_code");
    obj.Balance   = rs.value("t_balance");
    obj.Name      = rs.value("name");
    obj.Principle = rs.value("t_principle");
    obj.OpenDate  = rs.value("t_opendate");
    obj.CloseDate = rs.value("t_closedate");

    Arr[Arr.size] = obj;

  end;

  return Arr;
  
end;

macro WebCore_CreateTWebOFRSymbolFromBD(RecID)

  var obj;

  var cmd, rs;

  var query = GetQuery();
  
  if( RecID != NULL)

    query = query + " AND tt.t_RecID = ?";

  end;

  cmd = RsdCommand(query);
  
  cmd.addParam( "", RSDBP_IN, {curdate} );
  cmd.addParam( "", RSDBP_IN, RecID     );
  
  rs = RsdRecordset(cmd);

  if(rs.movenext)

    obj = TWebOFRSymbol;

    obj.RecID = rs.value("t_recid");
    obj.Code = rs.value("t_code");
    obj.Name = rs.value("Name");

  end;

  return obj;

end;

macro WebCore_CreateTWebOFRSymbol(Code, Name, RecID)

  var obj = TWebOFRSymbol;
  
  obj.RecID = RecID;
  obj.Code = Code;
  obj.Name = Name;

  return obj;

end;