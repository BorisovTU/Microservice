/*
$Name:        operation_steps.mac
$Module:      Главная книга
$Description: Cервисы  панели экземпляра операции
*/

IMPORT BankInter;
IMPORT OprInter;

import "ws_file.mac";
import "ws_validation.mac";
import "cb_sql.mac";

class TWebOprOper 
 
  var DocKind          	   : integer ; 
  
  var DocumentID       	   : String ; 
  
  var ID_Operation         : integer ; 
  
  var Kind_Operation       : integer ; 
  
  var Kind_Operation_Name  : integer ; 
       
end;

class TWebOprStepGridItem 
 
  var ID_Operation     	: integer ;

  var ID_Step  			: integer ;  
  
  var IsExecute  		: bool ;  
  
  var CountNum  		: integer ;  
  
  var IsCase  			: bool ;  
  
  var IsRemoveStep  	: bool ;  
  
  var Symbol  			: String ;  
  
  var Name          	: String ; 
   
  var Status          	: String ; 
	
  var Number_Step       : integer ; 
	 
  var BlockID          	: integer ; 
	  
  var Plan_Date         : Date ;
	   
  var Fact_Date         : Date ; 
		
  var AccountDate       : Date ; 
		 
  var Syst_Date         : Date ; 
		  
  var Syst_Time         : Date ; 
  
  var Group          	: bool ; 
  
  var Oper          	: integer ; 
     
end;

class TWebOprDatesGridItem 
 
  var DateOpr          	: Date ; 
  
  var DateKindID       	: integer ; 
  
  var Operation         : integer ; 
  
  var NameDate       	: String ; 
       
end;

class TWebOprStKndGridItem 
 
  var Code          	: String ;
  
  var Name       		: String ;
  
  var NumValue      	: integer ;
  
  var ValueName     	: String ;
  
  var StatusKindID      : integer ;
       
end;

class TWebOprDocsGridItem  
 
  var ID_Operation  : integer ;
  
  var ID_Step       : integer ; 
  
  var DocKind       : integer ;
  
  var DocKindName   : String ;
  
  var DocumentID  	: String ; 
  
  var Children 		;//List <TWebOprDocsGridItem> 
       
end;


PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro GetErrorMessage(ErrorCode : @integer, ErrorMessage : @string)

  var objErr = AL_GetErrorInfo();

  ErrorCode     = objErr.stat;
  ErrorMessage  = objErr.message;

  InitError();

end;

private macro ConvertCharToBool(CurValue :string ) : bool

  if((CurValue == "X") OR (CurValue == "I"))
    
	return true;
	
  else
  
    return false;
	
  end;
  
end;


macro WebCore_InitOprStep(DocKind, DocumentID)

  var query, cmd, rs;
  
  var obj = TWebOprOper();

  query = 	" SELECT oproper.T_DOCKIND, oproper.T_DOCUMENTID,		 	"+
				" oproper.T_ID_OPERATION, oproper.T_KIND_OPERATION,		 	"+
				" (														 	"+
				"	SELECT OK.T_NAME									 	"+
				"	FROM DOPRKOPER_DBT OK 								 	"+
				"	WHERE OK.T_KIND_OPERATION = oproper.T_KIND_OPERATION 	"+
				" )	Kind_Operation_Name										"+
				" FROM doproper_dbt oproper								 	"+
				" WHERE   oproper.T_DOCUMENTID = ? AND oproper.T_DOCKIND  = ?	";

  cmd = RsdCommand(query);
  cmd.addParam( "DocumentID", RSDBP_IN, DocumentID);
  
  cmd.addParam( "DocKind", RSDBP_IN, DocKind);

  rs = RsdRecordset(cmd);

  if (rs.movenext)
  
    obj.DocKind = rs.value("T_DOCKIND"); 
	
	obj.DocumentID = rs.value("T_DOCUMENTID"); 
	
	obj.ID_Operation = rs.value("T_ID_OPERATION"); 
	
	obj.Kind_Operation = rs.value("T_KIND_OPERATION"); 
	
	obj.Kind_Operation_Name = rs.value("Kind_Operation_Name"); 
  
  end;

  return obj;

end;

macro WebCore_GetWebOprStepGridItemList(ID_Operation)

  var result = TArray();
  
  var query, cmd, rs, odj;
  
  query = " SELECT OPRSTEP.T_ID_OPERATION ID_OPERATION, 	"+
		  " OPRSTEP.T_ID_STEP ID_STEP,						"+
		  " OPRSTEP.T_ISEXECUTE ISEXECUTE,					"+
		  " OPRSTEP.T_COUNTNUM COUNTNUM,					"+
		  " CASE											"+
		  "	  WHEN OPRSTEP.T_KIND_ACTION=2 THEN 'X'			"+
		  "	  ELSE CHR (0)									"+
		  " END IsCase,										"+
		  " OPRSTEP.T_ISREMOVESTEP ISREMOVESTEP,			"+		  
		  " OPRSTEP.T_SYMBOL SYMBOL,						"+
		  "(												"+
		  " SELECT OS.T_NAME 								"+
		  " FROM DOPROSTEP_DBT OS 							"+
		  " WHERE OS.T_BLOCKID = OPRSTEP.T_BLOCKID  		"+
		  " AND OS.T_NUMBER_STEP = OPRSTEP.T_NUMBER_STEP	"+
		  ") Name,											"+
		   " CASE											"+
		  "	  WHEN OPRSTEP.T_ISEXECUTE = 'R' THEN 'Готов к выполнению'	"+
		  "	  WHEN OPRSTEP.T_ISEXECUTE = chr(0) THEN 'Запланирован'		"+
		  "	  WHEN OPRSTEP.T_ISEXECUTE = 'X' THEN 'Выполнен'			"+
		  "	  WHEN OPRSTEP.T_ISEXECUTE = 'W' THEN 'Отвергнут'			"+
		  " END Status,										"+
		  " OPRSTEP.T_NUMBER_STEP NUMBER_STEP,				"+
		  " OPRSTEP.T_BLOCKID BLOCKID,						"+
		  " OPRSTEP.T_PLAN_DATE PLAN_DATE,					"+
		  " OPRSTEP.T_FACT_DATE FACT_DATE,					"+
		  " OPRSTEP.T_ACCOUNTDATE ACCOUNTDATE,				"+
		  " OPRSTEP.T_SYST_DATE SYST_DATE,					"+
		  " OPRSTEP.T_SYST_TIME SYST_TIME,					"+
		  " CASE											"+
		  "	  WHEN OPRSTEP.T_ISEXECUTE='X' OR OPRSTEP.T_ISEXECUTE='W' THEN OPRSTEP.T_OPERORGROUP "+
		  "	  ELSE( 													"+ 
		  "         SELECT OS.T_OPERORGROUP 							"+ 
		  "			FROM DOPROSTEP_DBT OS								"+ 
		  "			WHERE  OS.T_BLOCKID = OPRSTEP.T_BLOCKID 		"+ 
		  "			AND OS.T_NUMBER_STEP = OPRSTEP.T_NUMBER_STEP	"+
		  "		  )														"+
		  " END First_ArgGroup,											"+
		  " CASE														"+
		  "	  WHEN OPRSTEP.T_ISEXECUTE='X' OR OPRSTEP.T_ISEXECUTE='W' THEN OPRSTEP.T_OPER "+
		  "	  ELSE( 													"+
		  "         SELECT OS.T_ISALLOWFOROPER 							"+
		  "			FROM DOPROSTEP_DBT OS								"+ 
		  "			WHERE  OS.T_BLOCKID = OPRSTEP.T_BLOCKID 		"+ 
		  "			AND OS.T_NUMBER_STEP = OPRSTEP.T_NUMBER_STEP	"+
		  "		  )														"+
		  " END Second_ArgGroup											"+
          " FROM DOPRSTEP_DBT OPRSTEP			"+
		  " WHERE OPRSTEP.T_ID_OPERATION = ?	"+
		  " AND OPRSTEP.T_ISEXECUTE != 'Z' ";
		  
  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, ID_Operation );
  cmd.NullConversion = true;
  
  rs = RsdRecordset(cmd);

  while(rs.MoveNext())
    
	odj = TWebOprStepGridItem();
		
	odj.ID_Operation = SQL_ConvType(rs.value("ID_OPERATION"));
	
	odj.ID_Step = SQL_ConvType(rs.value("ID_STEP"));
	
	odj.IsExecute = ConvertCharToBool(SQL_ConvType(rs.value("ISEXECUTE")));
	
	odj.CountNum = SQL_ConvType(rs.value("COUNTNUM"));

	odj.IsCase = ConvertCharToBool(SQL_ConvType(rs.value("IsCase")));	  
	
	odj.IsRemoveStep = ConvertCharToBool(SQL_ConvType(rs.value("ISREMOVESTEP")));
	
	odj.Symbol = SQL_ConvType(rs.value("SYMBOL"));
	
	odj.Name = SQL_ConvType(rs.value("Name"));
	
	odj.Status = SQL_ConvType(rs.value("Status"));
	
	odj.Number_Step = SQL_ConvType(rs.value("Number_Step"));
	
	odj.BlockID = SQL_ConvType(rs.value("BlockID"));
	
	odj.Plan_Date = SQL_ConvType(rs.value("Plan_Date"));
	
	odj.Fact_Date = SQL_ConvType(rs.value("Fact_Date"));
	
	odj.AccountDate = SQL_ConvType(rs.value("AccountDate"));
	
	odj.Syst_Date = SQL_ConvType(rs.value("Syst_Date"));
	
	odj.Syst_Time = SQL_ConvType(rs.value("Syst_Time"));
	
	var GroupFlag="";

	var First_ArgGroup = rs.value("First_ArgGroup");
	
	var Second_ArgGroup = rs.value("Second_ArgGroup");
	
	UpdateOperFields( 0, First_ArgGroup, Second_ArgGroup, @GroupFlag, "" ) ;
	
    odj.Group = ConvertCharToBool(GroupFlag);
	
	odj.Oper =  SQL_ConvType(rs.value("Second_ArgGroup"));
	   
    result[result.size] = odj;

  end;

  return result;

end;

macro WebCore_GetWebOprDatesGridItemList(ID_Operation)

  var result = TArray();
  
  var query, cmd, rs, odj;
  
  query = " SELECT OPRDATES.T_DATE DATEt, 											"+
		  " OPRDATES.T_DATEKINDID DATEKINDID, OPRDATES.T_ID_OPERATION ID_OPERATION,	"+
		  " (														 				"+
		  "	  SELECT OKD.T_NAMEDATE 									 			"+
		  "	  FROM DOPRKDATE_DBT OKD 								 				"+
		  "	  WHERE OKD.T_DATEKINDID = OPRDATES.T_DATEKINDID						"+
		  " )	NameDate															"+
          " FROM DOPRDATES_DBT OPRDATES												"+
		  " WHERE OPRDATES.T_ID_OPERATION = ?										";
		  
  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, ID_Operation );
  
  rs = RsdRecordset(cmd);
  
  while(rs.MoveNext())
    
	odj = TWebOprDatesGridItem();
		
	odj.DateOpr = SQL_ConvType(rs.value("DATEt"));
	
	odj.DateKindID = SQL_ConvType(rs.value("DATEKINDID"));
	
	odj.Operation = SQL_ConvType(rs.value("ID_OPERATION"));
	
	odj.NameDate = SQL_ConvType(rs.value("NameDate"));
	   
    result[result.size] = odj;

  end;
  
  return result;

end;

macro WebCore_Opr_UpdateOprDates(ID_Operation, DateKindID, DateOpr ):WebResponseBuilder
  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  file oprdatesFile("oprdates.dbt") key 0 write;
  
  var stat : bool;

  oprdatesFile.ID_Operation = ID_Operation;
  
  oprdatesFile.DateKindID = DateKindID;

  stat = GetEQ( oprdatesFile);
		  
  if(stat != false)
		  
    oprdatesFile.Date = DateOpr;
	
	stat = UpDate(oprdatesFile);
	
  end;
  
  if(stat == false)
  
    AddErrorMessageExt(@ErrorMessage,0,"Запись не найдена или обновление прошло с ошибкой");
	
  end;
			
  responseBuilder.SetUserObject(stat);

  return responseBuilder.Result;

end;

macro WebCore_Opr_ExecuteStep(ID_Operation, ID_Step):WebResponseBuilder

  var res = 0;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  res = Opr_ExecuteStep(ID_Operation, ID_Step);

  if(res != 0)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;


macro WebCore_Opr_BackoutStep (ID_Operation, ID_Step, IsBackToStep):WebResponseBuilder

  var res = 0;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  if(ID_Step!=0)
  
    res = Opr_BackoutStep(ID_Operation, ID_Step,IsBackToStep);
	
  else
  
    res = Opr_BackoutStep(ID_Operation, null, IsBackToStep);
	
  end;

  if(res != 0)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;


/*
 *  Сервис печати документов шага
 */
macro WebCore_Opr_PrintStepDocs( ID_Operation, ID_Step ) : WebResponseBuilder

  var    RepFileName : string;
  var AddRepFileName : string;

  var FileContainer : FileContainer;

  ResponseBuilder = WebResponseBuilder();

  var stat = Opr_PrintStepDocuments( ID_Operation, ID_Step, RepFileName, AddRepFileName );

  if( stat == 0 )

    if( (AddRepFileName != null) and (AddRepFileName != "") )
      
      FileContainer = CreateFileContainer(AddRepFileName);
    
    elif( (RepFileName != null) and (RepFileName != "") )
      
      RepFileName = ConvertTxt2Html(RepFileName);

      FileContainer = GetTextFile(RepFileName);
    
    end;

    responseBuilder.SetUserObject(FileContainer);

  end;

  if( stat != 0 )

    CreateErrMsg(stat);

    var objErr = AL_GetErrorInfo();

    var ErrorMessage = WsErrorMessage();

    ErrorMessage.Code     = objErr.stat;
    ErrorMessage.Message  = objErr.message;
    ErrorMessage.Severity = MessageSeverity.RuntimeError;

    ResponseBuilder.AddMessage( ErrorMessage );

  end;

  return responseBuilder.Result;

end;


macro WebCore_GetWebOprStKndGridItemList(DocKind, IDOperation)

  var result = TArray();
  
  var query, cmd, rs, odj;
 
  query =   " SELECT t.T_CODE CODE,														"+
			"        t.T_NAME NAME,														"+
			"        t.T_STATUSKINDID STATUSKINDID,										"+
			"        TT.T_NUMVALUE NumValue												"+
			" FROM    DOPRSTKND_DBT t													"+
			"         LEFT JOIN															"+
			"         DOPRCURST_dbt tt													"+
			"         ON     t.T_STATUSKINDID = TT.T_STATUSKINDID						"+
			"         AND TT.T_ID_OPERATION = " + IDOperation +
			" WHERE t.t_DocKind IN (    SELECT t_DocKind								"+
			"                           FROM doprkdoc_dbt	oprkdoc						"+
			"                      	  START WITH oprkdoc.t_DocKind = " + DocKind +
			"                     	  CONNECT BY PRIOR  								"+
			"							  oprkdoc.t_DocKind =  oprkdoc.t_ParentDocKind  "+
			"					 	 )													"+
			" ORDER BY t.t_sort															";
	
  cmd = RsdCommand(query);
 
  rs = RsdRecordset(cmd);

  while(rs.MoveNext())
	odj = TWebOprStKndGridItem();

	odj.Code = SQL_ConvType(rs.value("CODE"));	
	
	odj.Name = SQL_ConvType(rs.value("NAME"));	

	odj.NumValue = SQL_ConvType(rs.value("NumValue"));
	
	var insideQuery, insideCmd, insideRs;
	
	insideQuery =	" SELECT OSV.T_NAME OSV_NAME		"
					" FROM DOPRSTVAL_DBT OSV		"
					" WHERE  OSV.T_STATUSKINDID = ?	"
					" AND OSV.T_NUMVALUE = ?		";
					
    insideCmd = RsdCommand(insideQuery);
    insideCmd.addParam( "STATUSKINDID", RSDBP_IN, rs.value("STATUSKINDID") );
  
    insideCmd.addParam( "NumValue", RSDBP_IN, odj.NumValue );
  
    insideRs = RsdRecordset(insideCmd);

    if(insideRs.MoveNext())
  
      odj.ValueName = SQL_ConvType(insideRs.value("OSV_NAME"));
  
    end;
			   
    result[result.size] = odj;

  end;
  
  return result;

end;


private macro GetChildrenOprDocs(DocKind, DocumentID):TWebOprDocsGridItem
  
  var query, cmd, rs;
  
  var ID_Operation = 0;
  
  query = " 	SELECT OPROPER.T_ID_OPERATION 	id_operation	"+
		  "		FROM DOPROPER_DBT OPROPER 						"+
		  "		WHERE  OPROPER.T_DOCKIND = ? 					"+
		  "     AND OPROPER.T_DOCUMENTID  = ?					";
		  
  cmd = RsdCommand(query);

  cmd.addParam( "DocKind", RSDBP_IN, DocKind );
  
  cmd.addParam( "DocumentID", RSDBP_IN, DocumentID );
  
  cmd.NullConversion = true;
  
  rs = RsdRecordset(cmd);
  
  if(rs.MoveNext())
  
	ID_Operation = SQL_ConvType(rs.value("id_operation"));
  
  end;
		  
  query = " SELECT											 	"+
		  " OPRDOCS.T_DOCKIND	dockind,						"+
		  " ( 	SELECT OKD.T_NAME 								"+
		  "		FROM DOPRKDOC_DBT OKD 							"+
		  "		WHERE  OKD.T_DOCKIND = OPRDOCS.T_DOCKIND		"+
		  "	) name,												"+
		  "	OPRDOCS.T_DOCUMENTID								"+
		  " FROM DOPRDOCS_DBT OPRDOCS							"+
		  " WHERE OPRDOCS.T_ID_OPERATION = ?					"+
		  " AND OPRDOCS.T_ID_STEP = 0							";
			  
  cmd = RsdCommand(query);
  
  cmd.addParam( "ID_Operation", RSDBP_IN, ID_Operation );
  
  cmd.NullConversion = true;
  
  rs = RsdRecordset(cmd);
  
  while(rs.MoveNext())
    
	var odj = TWebOprDocsGridItem ();
	
	odj.ID_Operation = ID_Operation;
	
	odj.ID_Step = 0;
	
	odj.DocKind = SQL_ConvType(rs.value("dockind"));
	
	odj.DocKindName = SQL_ConvType(rs.value("name"));
	
	odj.DocumentID = SQL_ConvType(rs.value("documentid"));
	
	odj.Children = GetChildrenOprDocs(odj.DocKind, odj.DocumentID); 

  end;
  
  return odj;
  
end;

macro WebCore_GetWebOprDocsGridItemList(ID_Operation, ID_Step)

  var result = TArray();
  
  var query, cmd, rs;
  
  query = " SELECT OPRDOCS.T_ID_OPERATION id_operation, 		"+
		  " OPRDOCS.T_ID_STEP id_step, 							"+
		  " OPRDOCS.T_DOCKIND	dockind,						"+
		  " ( 	SELECT OKD.T_NAME 								"+
		  "		FROM DOPRKDOC_DBT OKD 							"+
		  "		WHERE  OKD.T_DOCKIND = OPRDOCS.T_DOCKIND		"+
		  "	) name,												"+
		  " OPRDOCS.T_DOCUMENTID documentid 					"+
          " FROM DOPRDOCS_DBT OPRDOCS							"+
		  " WHERE OPRDOCS.T_ID_OPERATION = ?  AND  EXISTS 		"+
          " (													"+
          "     SELECT OKD.T_NAME        						"+                       
          "     FROM DOPRKDOC_DBT OKD    						"+                        
          "     WHERE  OKD.T_DOCKIND = OPRDOCS.T_DOCKIND 		"+        
          " )													";
		  
  if(ID_Step != 0)	
		  
    query = query + " AND OPRDOCS.T_ID_STEP = " + ID_Step;
	
  end;
   
  cmd = RsdCommand(query);

  cmd.addParam( "ID_Operation", RSDBP_IN, ID_Operation );
  
  cmd.NullConversion = true;

  rs = RsdRecordset(cmd);
  
  while(rs.MoveNext())
    
	var odj = TWebOprDocsGridItem ();
		
	odj.ID_Operation = SQL_ConvType(rs.value("id_operation"));
	
	odj.ID_Step = SQL_ConvType(rs.value("id_step"));
	
	odj.DocKind = SQL_ConvType(rs.value("dockind"));
	
	odj.DocKindName = SQL_ConvType(rs.value("name"));
	
	odj.DocumentID = SQL_ConvType(rs.value("documentid"));

	if((odj.DocKind!=0) AND (odj.DocumentID!=""))
	
	  odj.Children = GetChildrenOprDocs(odj.DocKind, odj.DocumentID);
	  
	end;

      result[result.size] = odj;	  
  end;

  return result;

end;
