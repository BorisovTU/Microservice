/*
$Name:        mccateg_widget.mac
$Module:      Главная книга
$Description: Макрос виджета категорий учета
*/

import "ws_partycommon.mac";

class TWebMCCateg
  var ID;
  var Number;
  var Code;
  var LevelType;
end;

class TWebMCCategListerItem
  var ID;
  var Number;
  var Code;
  var LevelType;
  var GroupID;
  var GroupCode;
end;

macro WebCore_GetMCCategListerItemList(LevelType, sub, listLen)

  var cmd, rs;

  var Arr = TArray;

  var query = " SELECT mc.*, "
              "      (SELECT t_Code "
              "         FROM dmccateg_dbt "
              "        WHERE t_ID = mc.t_GroupID) "
              "         GroupCode "
              " FROM DMCCATEG_DBT mc WHERE 1 = 1 ";
  
  if(LevelType != 0)
  
    query = query + " AND T_LEVELTYPE	= " + string(LevelType);
    
  end;
  
  if(sub != "")
  
    query = query + " AND LOWER(t_Code) LIKE LOWER ('%' || ? || '%') ";
  
  end;

  if( listLen != 0 )

    query = query + " AND ROWNUM < " + string(listLen);

  end;
  
  query = query + " ORDER BY t_number ";

  cmd = RsdCommand(query);
  
  cmd.NullConversion = true;
  
  if(sub != "")
  
    cmd.addParam( "", RSDBP_IN, sub );
  
  end;
  
  rs = RsdRecordset(cmd);
  
  while(rs.movenext)

    var obj = TWebMCCategListerItem;

    obj.ID        = rs.value("t_id");
    obj.Number    = rs.value("t_number");
    obj.Code      = SQL_ConvTypeStr( rs.value("t_code") );
    obj.LevelType = rs.value("t_leveltype");
    obj.GroupID   = rs.value("t_groupid");
    obj.GroupCode = rs.value("groupcode");

    Arr[Arr.size] = obj;

  end;

  return Arr;
  
end;

macro WebCore_CreateTWebMCCategFromBD(Number)

  var obj;

  var cmd, rs;

  var query = " SELECT * FROM DMCCATEG_DBT WHERE T_NUMBER = ? ";

  cmd = RsdCommand(query);
  
  cmd.addParam( "", RSDBP_IN, Number );
  
  rs = RsdRecordset(cmd);

  if(rs.movenext)

    obj = TWebMCCateg;

    obj.ID        = rs.value("t_id");
    obj.Number    = rs.value("t_number");
    obj.Code      = rs.value("t_code");
    obj.LevelType = rs.value("t_leveltype");

  end;

  return obj;

end;

macro WebCore_CreateTWebMCCateg(ID, Number, Code, LevelType)

  var obj = TWebMCCateg;
  
  obj.ID        = ID;
  obj.Number    = Number;
  obj.Code      = Code;
  obj.LevelType = LevelType;

  return obj;

end;