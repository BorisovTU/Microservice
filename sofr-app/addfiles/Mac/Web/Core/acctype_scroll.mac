/*
$Name:        acctype_scroll.mac
$Module:      Главная книга
$Description: Макрос скроллинга типов лицевых счетов
*/

IMPORT  BankInter;
import "ws_file.mac";
import "ws_validation.mac";

class TWebAccTypeGridItem

  var OldTypeAccount	: string ;
  
  var TypeAccount		: string ;
  
  var IsSystem			: bool ;
  
  var TypeName			: string ;
  
  var Description		: string ;
  
  var NatCurUse			: bool; 
  
  var ForeignCurUse		: bool; 
  
  var Version			: integer;   
 
end;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro CovertBool(CurValue :bool ) : string

  if(CurValue==true)
    
	return "X";
	
  else
  
    return "";
	
  end;
  
end;

private macro CovertCharToBool(CurValue : string ) : bool

  if(CurValue == "X")
    
	return true;
	
  else
  
    return false;
	
  end;
  
end;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

macro WebCore_GetAccTypeGridItemList(PageSize, PageNum) : TArray

  var result = TArray();
  
  var query, cmd, rs, odj;
  
  query = " SELECT *							"
          " FROM dacctype_dbt acctype			"
          " WHERE acctype.T_ISSYSTEM = CHR(0)	"
		  " ORDER BY acctype.T_TYPE_ACCOUNT		";
		  
  if(PageSize != 0)
  
    query = SQL_WrapSelect( query, PageNum, PageSize);
    
  end;

  cmd = RsdCommand(query);
  
  rs = RsdRecordset(cmd);
  
  while(rs.MoveNext())
    
	odj = TWebAccTypeGridItem();
	
	odj.OldTypeAccount = rs.value("T_TYPE_ACCOUNT");
	
	odj.TypeAccount = rs.value("T_TYPE_ACCOUNT");
	
	odj.IsSystem = CovertCharToBool(rs.value("T_ISSYSTEM"));
	
	odj.TypeName = rs.value("T_TYPE_NAME");
	
	odj.Description = rs.value("T_DESCRIPTION");
	
	odj.NatCurUse = CovertCharToBool(rs.value("T_NATCURUSE"));
	
	odj.ForeignCurUse = CovertCharToBool(rs.value("T_FOREIGNCURUSE"));
	
	odj.Version = rs.value("T_VERSION");
    
    result[result.size] = odj;

  end;

  return result;

end;

macro WebCore_GetAccTypeGridItemCount() : integer

  var count : integer = 0;

  var cmd, rs, query;

  query = " SELECT count(*)	c					"
          " FROM dacctype_dbt acctype			"
          " WHERE acctype.T_ISSYSTEM = CHR(0)	";

  cmd = RsdCommand(query);

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    count = rs.value("c");

  end;

  return count;

end;

macro WebCore_DeleteAccType(OldTypeAccount) : WebResponseBuilder

  responseBuilder =WebResponseBuilder();
    
  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  var res = CB_DeleteACCTYPE(OldTypeAccount);
     
  if(res != true)
   
     AddErrorMessage(@ErrorMessage);
  
  end;
    
  responseBuilder.SetUserObject(res);
  
  return responseBuilder.Result;

end;

macro WebCore_InsertUpdateAccType(AccType, IsNewRec)

  Record  acctypeRec ("acctype.dbt");
  
  acctypeRec.TYPE_ACCOUNT = AccType.TypeAccount;
	
  acctypeRec.ISSYSTEM = CovertBool(AccType.IsSystem);

  acctypeRec.TYPE_NAME = AccType.TypeName;

  acctypeRec.DESCRIPTION = AccType.Description;

  acctypeRec.NATCURUSE =  CovertBool(AccType.NatCurUse);

  acctypeRec.FOREIGNCURUSE = CovertBool(AccType.ForeignCurUse);

  acctypeRec.VERSION = AccType.Version;

  responseBuilder =WebResponseBuilder();
    
  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  var res = CB_InsertUpdateACCTYPE(AccType.TypeAccount, acctypeRec, IsNewRec);
     
  if(res != true)
   
     AddErrorMessage(@ErrorMessage);
  
  end;
    
  responseBuilder.SetUserObject(res);
  
  return responseBuilder.Result;

end;