/*
$Name:        operdaychange_panel.mac
$Module:      Главная книга
$Description: Макрос Выбор операционного дня пользователя.
*/

import globals;
import "ws_validation.mac";
import "curdate_scroll.mac";

class TWebOperDayChangePanelObject
  var IsPriv144;
  var IsWork;
  var IsFixed;
  var FullListOperDay;
  var Group;
  var SelectedDate;
end;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro GetIsFixed(oper)

  var IsFixed;

  var cmd, rs;

  var query = " SELECT * FROM DPERSON_DBT WHERE T_OPER = ? ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, oper );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    if(SQL_ConvType(rs.value("T_BANKDATE")) != date(0,0,0))
      
      IsFixed = true;
      
    else
      
      IsFixed = false;
      
    end;

  end;

  return IsFixed;

end;

private macro GetFullListOperDay(DprtID)

  var cmd, rs, p;

  var Arr = TArray;

  var query = " SELECT curdate.T_CURDATE, "
              "        curdate.T_BRANCH, "
              "        curdate.T_MINPHASE, "
              "        curdate.T_MAXPHASE, "
              "        curdate.T_ISCLOSED, "
              "        curdate.T_ISBALANCE "
              " FROM dcurdate_dbt curdate "
              "  WHERE curdate.T_BRANCH = ? AND curdate.T_ISCLOSED != 'Z' ";

  query = query + " ORDER BY curdate.T_CURDATE ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, DprtID );
  
  rs = RsdRecordset(cmd);

  while(rs.movenext)

    p = TWebCurdateGridItem();

    p.CurDate   = SQL_ConvType(rs.value("T_CURDATE"));
    p.IsClosed  = SQL_ConvTypeBool(rs.value("T_ISCLOSED"));
    p.MinPhase  = SQL_ConvType(rs.value("T_MINPHASE"));
    p.MaxPhase  = SQL_ConvType(rs.value("T_MAXPHASE"));
    p.IsBalance = SQL_ConvTypeBool(rs.value("T_ISBALANCE"));
    p.Branch    = SQL_ConvType(rs.value("T_BRANCH"));

    Arr[Arr.size] = p;

  end;

  return Arr;

end;

private macro GetAcsGroupOpers(GroupId)

  var cmd, rs, p;

  var Arr = TArray;

  var query = " SELECT t_oper "
              " FROM dacsgroupoper_dbt "
              "  WHERE t_groupid = ? ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, GroupId );
  
  rs = RsdRecordset(cmd);

  while(rs.movenext)
    
    Arr[Arr.size] = SQL_ConvType(rs.value("t_oper"));

  end;

  return Arr;

end;

macro WebCore_InitOperDayChangePanel()

  var res = false;
  
  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  var OperDayChangePanelObject = TWebOperDayChangePanelObject();
  
  OperDayChangePanelObject.IsPriv144       = CheckActionRestriction(144);
  
  res = WEB_CB_GetIsWorkNotCurDepOperDay(OperDayChangePanelObject.IsWork);
  
  OperDayChangePanelObject.IsFixed         = GetIsFixed({oper});
  OperDayChangePanelObject.FullListOperDay = GetFullListOperDay({OperDprt});
  
  OperDayChangePanelObject.SelectedDate    = {curdate};
    
  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  ResponseBuilder.SetUserObject(OperDayChangePanelObject);

  return ResponseBuilder.Result;

end;

macro WebCore_SetCurDate(SelectedDate, GroupId, IsFixed)

  var res = false;
  
  var Opers;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  res = WEB_CB_SetCurDate({OperDprt}, SelectedDate, GroupId, IsFixed);
  
  if(res == true)
    
    Opers = GetAcsGroupOpers(GroupId);
  
  end;

  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  ResponseBuilder.SetUserObject(Opers);

  return ResponseBuilder.Result;

end;

