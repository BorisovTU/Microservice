/*
$Name:        acctype_widget.mac
$Module:      Главная книга
$Description: Макрос виджета выбора типов лицевых счетов
*/

import "ws_partycommon.mac";

class TWebAccType
  var Type_Account;
  var Type_Name;
end;

macro WebCore_GetAccTypeList(IsSystem, IsNatCurUse, Types_Account)

  var cmd, rs;

  var Arr = TArray;

  var query = " SELECT T_TYPE_ACCOUNT, T_TYPE_NAME "
              " FROM DACCTYPE_DBT t WHERE 1 = 1 ";

  if( Types_Account != NULL )

    query = query + " AND T_TYPE_ACCOUNT IN ( " + string(Types_Account) + " ) ";

  end;
  
  if( IsSystem != NULL )

    if( IsSystem == true )

      query = query + " AND T_ISSYSTEM = 'X' ";

    else

      query = query + " AND T_ISSYSTEM = CHR(0) ";

    end;

  end;

  if( IsNatCurUse != NULL )

    if( IsNatCurUse == true )

      query = query + " AND T_NatCurUse = 'X' ";

    else

      query = query + " AND T_ForeignCurUse = 'X' ";

    end;

  end;

  query = query + " ORDER BY t_IsSystem, t_Type_Account";

  cmd = RsdCommand(query);

  rs = RsdRecordset(cmd);

  while(rs.movenext)

    var obj = TWebAccType;

    obj.Type_Account = rs.value("T_TYPE_ACCOUNT");
    obj.Type_Name    = rs.value("T_TYPE_NAME");

    Arr[Arr.size] = obj;

  end;

  return Arr;

end;

macro WebCore_CreateTWebAccTypeFromBD(Type_Account)

  var obj;

  var cmd, rs;

  var query = " SELECT T_TYPE_ACCOUNT, T_TYPE_NAME "
              " FROM DACCTYPE_DBT t WHERE T_TYPE_ACCOUNT = ? ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, Type_Account );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    obj = TWebAccType;

    obj.Type_Account = rs.value("T_TYPE_ACCOUNT");
    obj.Type_Name    = rs.value("T_TYPE_NAME");

  end;

  return obj;

end;

macro WebCore_CreateTWebAccType(Type_Account, Type_Name)

  var obj = TWebAccType;

  obj.Type_Account = Type_Account;
  obj.Type_Name    = Type_Name;

  return obj;

end;