/*
$Name:        acctrn_panel.mac
$Module:      Главная книга
$Description: Макрос панели проводок
*/

IMPORT BankInter;
IMPORT FIInter;
import "account_widget.mac";
import "ws_partycommon.mac";
import "ws_ficlass.mac";
import "ws_dprt.mac";
import "ws_person.mac";
import "ws_partyclasses.mac";
import "ws_fiunilist.mac";
import "ws_validation.mac";
import "ws_datafilter.mac";
import "pr_party_lib.mac";
import "multymtd_widget.mac";
import "cipher_widget.mac";
import "acctrn_web_common.mac";
import "ws_file.mac";
import "acctrn_scroll_protocol.mac";
import "ws_progress.mac";
import "acctrn_print.mac";

class TWebTrnAccRest
  var RestNat : money = $0;
  var RestCur : money = $0;
  var RestEqv : money = $0;
end;

class TWebAccTrnSide
  var FI      ; // TWsFinInstr        
  var AccObj  ; // TWebAccount    
  var Sum     : money = $0;
  var FIEq    ; // TWsFinInstr      
  var SumEq   : money = $0;
  var AccRest ; // TWebTrnAccRest 
end;

class TWebChapter // удалить после исправления виджета
  var Chapter   : integer;   
  var Name      : string ;
  var ShortName : string ;
  var Symbol    : string ;
end;

class TWebAccTrnInfo
  
  var SumNatCur     : money;
  var ISOCodeNatCur : string;

  var AccountPayer : string;
  var SumPayer     : money;
  var ISOCodePayer : string;
  var NamePayer    : string;

  var AccountReceiver : string;
  var SumReceiver     : money;
  var ISOCodeReceiver : string;
  var NameReceiver    : string;

end;


class TWebAccTrnObject
  var AcctrnID           : integer          ;
  var State              : integer          ;
  var Department         ; // TWsDepartment
  var Branch             ; // TWsDepartment    
  var Numb_Document      : string           ;
  var Date_Carry         : Date             ;
  var Chapter            ; // TWebChapter      
  var NatcurCode         : string           ;
  var Debet              ; // TWebAccTrnSide     
  var Credit             ; // TWebAccTrnSide   
  var Sum_NatCur         : Money            ;
  var SkipRecalcSumNatCur: bool             ;
  var SkipRestEqChange   : bool             ;
  var SideTransaction    : integer          ;
  var SideTrnName        : string           ;
  var ExRateExtra        : Money            ;
  var Ground             : string           ;
  var Cipher             ; // TWebCipher     
  var MultyMtd           ; // TWebMultyMtd    
  var Kind_Oper          : string           ;
  var Number_Pack        : integer          ;
  var TypeAc             : string           ;
  var UserTypeAc         : string           ;
  var UserField1         : string           ;
  var UserField2         : string           ;
  var UserField3         : string           ;
  var UserField4         : string           ;
  var RateObj               ;//TWebAccTrnRate  
  
  var IsMultyCarry       : bool             ;

  var MC_Numb_Document : string;
  var MC_Date_Carry    : Date  ;

  var MultyCarryParmBase;
  var MultyCarryParmExRate;

  var MC_Ground           : string;
  var MC_Shifr_Oper       : string;
  var MC_Kind_Oper        : string;
  var MC_Number_Pack      : integer;
  var MC_TypeDocument     : string;
  var MC_UserTypeDocument : string;
  var MC_Department       : string;
  var MC_Branch           : string;  
  
  var OFRSymbol           ; //TWebOFRSymbol    
  var OFRSymbolCodeMask   : string;    

end;                     

class TWebAccTrnPanAccParm
  var AccountID       : Integer            ;
  var CurrencyEq      ; // TWsFinInstr
  var RestNat         : Money = $0         ;
  var RestCur         : Money = $0         ;
  var RestEqv         : Money = $0         ;
  var IsNVPI          : bool               ;
  var IsCash          : bool               ;
end;

class TWebAccTrnRate
  var Rate             : Double;
  var Rate_FI_From_Code: string;
  var Scale            :Integer;
  var Rate_FI_To_Code  : string;
  var Point            :Integer;
  var Rate_IsDtKt      :   bool;
  var Rate_IsKtDt      :   bool;
end;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro GetErrorMessage(ErrorCode : @integer, ErrorMessage : @string)

  var objErr = AL_GetErrorInfo();

  ErrorCode     = objErr.stat;
  ErrorMessage  = objErr.message;

  InitError();

end;

private macro GetWebAccTrnSide(Side:@TWebAccTrnSide, FIID, AccountID, Sum, FIIDEq, SumEq, Date_Carry, Account, Chapter)
  Side = TWebAccTrnSide;
  Side.FI      = GetTWsFinInstrByID(FIID);
  Side.AccObj  = WebCore_CreateTWebAccountFromBD1(AccountID);
  Side.Sum     = Sum;
  Side.FIEq    = GetTWsFinInstrByID(FIIDEq);                       
  Side.SumEq   = SumEq;                       
  Side.AccRest = TWebTrnAccRest;
  Side.AccRest.RestNat = GetRestAByID(AccountID, Date_Carry, NATCUR);

  if( FIID == NATCUR )
    Side.AccRest.RestCur = $0;
  else
    Side.AccRest.RestCur = GetRestAByID(AccountID, Date_Carry, FIID);
  end;

  Side.AccRest.RestEqv = GetRestAEQ(AccountID, Date_Carry);

  if( (Side.AccObj == null) and (Account != "") )
    Side.AccObj = WebCore_CreateTWebAccount( 0, Account, "", FIID, Chapter, 0 );
  end;

end;

macro WebCore_GetRate(FIID_Receiver, FIID_Receiver_Code, Sum_Receiver, FIID_Payer, FIID_Payer_Code, Sum_Payer, OnDate): TWebAccTrnRate
        var rateObj=TWebAccTrnRate;
    var IsInverse;
        FI_GetAccTrnRate( FIID_Payer,FIID_Receiver,Sum_Payer,Sum_Receiver,OnDate, @RateObj.Rate,
                                        @RateObj.Scale,@RateObj.Point,@IsInverse);
        if(IsInverse == false)
                rateObj.Rate_IsDtKt=true;
            rateObj.Rate_IsKtDt=false;
                RateObj.Rate_FI_From_Code =FIID_Payer_Code;
                RateObj.Rate_FI_To_Code =FIID_Receiver_Code;
        else    
                rateObj.Rate_IsDtKt=false;
            rateObj.Rate_IsKtDt=true;
                RateObj.Rate_FI_From_Code =FIID_Receiver_Code;
                RateObj.Rate_FI_To_Code =FIID_Payer_Code;
        end;
        RateObj.Rate=RateObj.Rate/Pow ( 10, RateObj.Point );
        return rateObj;
end;

/* Получить информацию о проводке курсовой разницы  */
private macro GetExRateAccTrn( AccTrnID : integer ) : TBFile

  var ExRateAccTrnFile = TBFile("acctrn.dbt", "R", 0);

  ClearRecord(ExRateAccTrnFile);

  if( AccTrnID > 0 )
  
    ExRateAccTrnFile.rec.AccTrnID = AccTrnID;
 
    if( not ExRateAccTrnFile.getEQ() )
      ClearRecord(ExRateAccTrnFile);
    end;

  end;

  return ExRateAccTrnFile;

end;

/* Получить информацию об основной проводке для проводки курсовой разницы */
private macro GetMainMultyCarryAccTrn( ExRateAccTrnID : integer ) : TBFile

  var MainAccTrnFile = TBFile("acctrn.dbt", "R", 9);

  ClearRecord(MainAccTrnFile);
  
  if( ExRateAccTrnID > 0 )

    MainAccTrnFile.rec.ExRateAccTrnID = ExRateAccTrnID;
 
    if( not MainAccTrnFile.getEQ() )
      ClearRecord(MainAccTrnFile);
    end;

  end;

  return MainAccTrnFile;

end;

/* Получить название счета */
private macro GetAccountName( AccountID : integer ) : string

  var AccountName = "";

  var cmd, rs;

  var query = " SELECT T_NAMEACCOUNT FROM DACCOUNT_DBT WHERE T_ACCOUNTID = ? ";
  
  cmd = RsdCommand(query);

  cmd.nullConversion = true;
  
  cmd.addParam( "", RSDBP_IN, AccountID );
  
  rs = RsdRecordset(cmd);

  if( rs.MoveNext() )

    AccountName = rs.value("T_NAMEACCOUNT");

  end;

  return AccountName;

end;

/* Заполнить объект RsbAccTransactionData */
private macro FillAccTransactionData( accTrn : RsbAccTransactionData, AcctrnObject )

  accTrn.Chapter             = AcctrnObject.Chapter.Chapter;
  accTrn.Date_Carry          = AcctrnObject.Date_Carry;
  if(AcctrnObject.Debet.FI != null)
    accTrn.FIIDPayer           = AcctrnObject.Debet.FI.FIID;
  end;

  if(AcctrnObject.Debet.AccObj != null)
    accTrn.AccountPayer        = AcctrnObject.Debet.AccObj.Account;
  end;

  accTrn.SumPayer            = AcctrnObject.Debet.Sum;

  if(AcctrnObject.Credit.FI != null)
    accTrn.FIIDReceiver        = AcctrnObject.Credit.FI.FIID;
  end;

  if(AcctrnObject.Credit.AccObj != null)
    accTrn.AccountReceiver     = AcctrnObject.Credit.AccObj.Account;
  end;

  accTrn.SumReceiver         = AcctrnObject.Credit.Sum;

  if( (AcctrnObject.Sum_NatCur != null) and (AcctrnObject.Sum_NatCur != 0) )
    accTrn.SumEquivalentCarry  = AcctrnObject.Sum_NatCur;
  end;

  if(AcctrnObject.Ground != null)
    accTrn.Ground              = AcctrnObject.Ground;
  end;

  accTrn.Numb_Document       = AcctrnObject.Numb_Document;
  accTrn.Number_Pack         = AcctrnObject.Number_Pack;

  if(AcctrnObject.Cipher != null)
    accTrn.Shifr_Oper          = AcctrnObject.Cipher.Shifr_Oper;
  end;

  if(AcctrnObject.Kind_Oper != null)
    accTrn.Kind_Oper           = AcctrnObject.Kind_Oper;
  end;

  if(AcctrnObject.Department != null)
    accTrn.Department          = AcctrnObject.Department.Code;
  end;

  if(AcctrnObject.Branch != null)
    accTrn.Branch              = AcctrnObject.Branch.Code;
  end;

  if(AcctrnObject.TypeAc != null)
    accTrn.TypeDocument        = AcctrnObject.TypeAc;
  end;

  if(AcctrnObject.UserTypeAc != null)
    accTrn.UserTypeDocument    = AcctrnObject.UserTypeAc;
  end;

  accTrn.SkipRestEqChange    = AcctrnObject.SkipRestEqChange;
  accTrn.SumEqPayer          = AcctrnObject.Debet.SumEq;
  accTrn.SumEqReceiver       = AcctrnObject.Credit.SumEq;

  if(AcctrnObject.MultyMtd != null)
    accTrn.MCMethod            = AcctrnObject.MultyMtd.MethodID;
  end;

  accTrn.Rate                = AcctrnObject.RateObj.Rate;
  accTrn.Scale               = AcctrnObject.RateObj.Scale;
  accTrn.Point               = AcctrnObject.RateObj.Point;

  if(AcctrnObject.RateObj.Rate_IsKtDt == true)
    accTrn.IsInverse = false;
  else
    accTrn.IsInverse = true;
  end;

  accTrn.UserField1          = AcctrnObject.UserField1;
  accTrn.UserField2          = AcctrnObject.UserField2;
  accTrn.UserField3          = AcctrnObject.UserField3;
  accTrn.UserField4          = AcctrnObject.UserField4;
  accTrn.SideTransaction     = AcctrnObject.SideTransaction;
  accTrn.ExRateExtra         = AcctrnObject.ExRateExtra;

end;


macro GetAccTrnObj(  AccTrn )

  var cmd, rs, query;

  var obj = TWebAccTrnObject;
    
	obj.AcctrnID      = AccTrn.AcctrnID;
	obj.State         = AccTrn.State;
	obj.Department    = GetTWsDepartmentByCode(AccTrn.Department);
	obj.Branch        = GetTWsDepartmentByCode(AccTrn.Branch);
	obj.Numb_Document = AccTrn.Numb_Document;
	obj.Date_Carry    = AccTrn.Date_Carry;

	query = "SELECT t_Chapter, t_Name, t_SHortName, t_Symbol FROM dobchaptr_dbt WHERE t_Chapter = ? ";
	cmd = RsdCommand(query);
	cmd.addParam( "", RSDBP_IN, AccTrn.Chapter );
	rs = RsdRecordset(cmd);

	if(rs.movenext)
	  obj.Chapter = TWebChapter;
	  obj.Chapter.Chapter   = SQL_ConvType(rs.value("t_Chapter"));     
	  obj.Chapter.Name      = SQL_ConvType(rs.value("t_Name"));
	  obj.Chapter.ShortName = SQL_ConvType(rs.value("t_ShortName"));
	  obj.Chapter.Symbol    = SQL_ConvType(rs.value("t_Symbol"));
	end;

	query = "SELECT t_CCY FROM dfininstr_dbt WHERE t_FIID = ? ";
	cmd = RsdCommand(query);
	cmd.addParam( "", RSDBP_IN, NATCUR );
	rs = RsdRecordset(cmd);

	if(rs.movenext)
	  obj.NatcurCode = SQL_ConvType(rs.value("t_CCY"));     
	end;

	// Поля группы "Дебет"
	GetWebAccTrnSide(@obj.Debet, AccTrn.FIID_Payer, AccTrn.AccountID_Payer, AccTrn.Sum_Payer, AccTrn.FIIDEq_Payer,
					 AccTrn.SumEq_Payer, AccTrn.Date_Carry, AccTrn.Account_Payer, AccTrn.Chapter);

	// Поля группы "Кредит"
	GetWebAccTrnSide(@obj.Credit, AccTrn.FIID_Receiver, AccTrn.AccountID_Receiver, AccTrn.Sum_Receiver, AccTrn.FIIDEq_Receiver,
					 AccTrn.SumEq_Receiver, AccTrn.Date_Carry, AccTrn.Account_Receiver, AccTrn.Chapter);
		obj.RateObj = TWebAccTrnRate;
	obj.Sum_NatCur          = AccTrn.Sum_NatCur          ;
	obj.SkipRecalcSumNatCur = AccTrn.SkipRecalcSumNatCur ;
	obj.SkipRestEqChange    = AccTrn.SkipRestEqChange    ;
	obj.RateObj.Rate                = AccTrn.Rate        ;
		
	if(AccTrn.IsInverse == "X")
	  obj.RateObj.Rate_FI_From_Code = ПолучитьКодФинИн(AccTrn.FIID_Receiver);
	  obj.RateObj.Rate_FI_To_Code   = ПолучитьКодФинИн(AccTrn.FIID_Payer);
	  obj.RateObj.Rate_IsDtKt = false;
	  obj.RateObj.Rate_IsKtDt = true;
	else
	  obj.RateObj.Rate_FI_From_Code = ПолучитьКодФинИн(AccTrn.FIID_Payer);
	  obj.RateObj.Rate_FI_To_Code   = ПолучитьКодФинИн(AccTrn.FIID_Receiver);
	  obj.RateObj.Rate_IsDtKt = true;
	  obj.RateObj.Rate_IsKtDt = false;
	end;

	obj.RateObj.Scale = AccTrn.Scale;
	obj.RateObj.Point = AccTrn.Point;
	obj.MultyMtd = WebCore_GetMultyMtdItemByID(AccTrn.MethodID);
		obj.RateObj.Rate=obj.RateObj.Rate/Pow ( 10, obj.RateObj.Point );

	if(AccTrn.SideTransaction == ACCTRN_SIDE_DEBET)
	  obj.SideTrnName = "Дебет";
	elif(AccTrn.SideTransaction == ACCTRN_SIDE_CREDIT)
	  obj.SideTrnName = "Кредит";
	else
	  obj.SideTrnName = ""; // ?? NULL
	end;

	obj.ExRateExtra = AccTrn.ExRateExtra;
	obj.Ground      = AccTrn.Ground;
	obj.Cipher      = WebCore_GetCipherItemByShifr(AccTrn.Shifr_Oper);

	obj.Kind_Oper   = AccTrn.Kind_Oper  ;
	obj.Number_Pack = AccTrn.Number_Pack;

	obj.TypeAc      = AccTrn.TypeDocument;
	obj.UserTypeAc  = AccTrn.UserTypeDocument;

	obj.UserField1  = AccTrn.UserField1;
	obj.UserField2  = AccTrn.UserField2;
	obj.UserField3  = AccTrn.UserField3;
	obj.UserField4  = AccTrn.UserField4;

	obj.OFRSymbol = GetOFRSymbol(AccTrn.OFRRecID);

	var err;
	var regVal;

	GetRegistryValue("COMMON\\WILDCARD_SEARCH\\ONE_CHAR", V_STRING, regVal, err);

	if(err == 0)
	  
	  if(regVal == "NONE")
		
		regVal = "?";
		
	  end;
	  
	else
	  
	  RunError("Ошибка при чтении настройки реестра!");
	  
	end; 

	obj.OFRSymbolCodeMask = "7" + regVal + regVal + regVal + regVal;

  return obj;
end;

/*
 *  Сервис получения параметров панели просмотра/редактирования проводки
 */
macro WebCore_GetAccTrnObject( AcctrnID, IsShowExRate )
  var cmd, rs, query;

  var obj = TWebAccTrnObject;

  var AccTrnFile = TBFile("acctrn.dbt", "R", 0);

  ClearRecord (AccTrnFile);
  AccTrnFile.rec.AcctrnID = AcctrnID;
 
  if( AccTrnFile.getEQ () ) 
    
    if( IsShowExRate == true )
     
      /* Ищем связанную проводку */
      if( AccTrnFile.rec.Result_Carry == DELTARATE_MCD )

        AccTrnFile = GetMainMultyCarryAccTrn( AccTrnFile.rec.AcctrnID );

      else

        AccTrnFile = GetExRateAccTrn( AccTrnFile.rec.ExRateAccTrnID );

      end;
    
    end;
    
    obj.AcctrnID      = AccTrnFile.rec.AcctrnID;
    obj.State         = AccTrnFile.rec.State;
    obj.Department    = GetTWsDepartmentByCode(AccTrnFile.rec.Department);
    obj.Branch        = GetTWsDepartmentByCode(AccTrnFile.rec.Branch);
    obj.Numb_Document = AccTrnFile.rec.Numb_Document;
    obj.Date_Carry    = AccTrnFile.rec.Date_Carry;

    query = "SELECT t_Chapter, t_Name, t_SHortName, t_Symbol FROM dobchaptr_dbt WHERE t_Chapter = ? ";
    cmd = RsdCommand(query);
    cmd.addParam( "", RSDBP_IN, AccTrnFile.rec.Chapter );
    rs = RsdRecordset(cmd);

    if(rs.movenext)
      obj.Chapter = TWebChapter;
      obj.Chapter.Chapter   = SQL_ConvType(rs.value("t_Chapter"));     
      obj.Chapter.Name      = SQL_ConvType(rs.value("t_Name"));
      obj.Chapter.ShortName = SQL_ConvType(rs.value("t_ShortName"));
      obj.Chapter.Symbol    = SQL_ConvType(rs.value("t_Symbol"));
    end;

    query = "SELECT t_CCY FROM dfininstr_dbt WHERE t_FIID = ? ";
    cmd = RsdCommand(query);
    cmd.addParam( "", RSDBP_IN, NATCUR );
    rs = RsdRecordset(cmd);

    if(rs.movenext)
      obj.NatcurCode = SQL_ConvType(rs.value("t_CCY"));     
    end;

    // Поля группы "Дебет"
    GetWebAccTrnSide(@obj.Debet, AccTrnFile.rec.FIID_Payer, AccTrnFile.rec.AccountID_Payer, AccTrnFile.rec.Sum_Payer, AccTrnFile.rec.FIIDEq_Payer,
                     AccTrnFile.rec.SumEq_Payer, AccTrnFile.rec.Date_Carry, AccTrnFile.rec.Account_Payer, AccTrnFile.rec.Chapter);

    // Поля группы "Кредит"
    GetWebAccTrnSide(@obj.Credit, AccTrnFile.rec.FIID_Receiver, AccTrnFile.rec.AccountID_Receiver, AccTrnFile.rec.Sum_Receiver, AccTrnFile.rec.FIIDEq_Receiver,
                     AccTrnFile.rec.SumEq_Receiver, AccTrnFile.rec.Date_Carry, AccTrnFile.rec.Account_Receiver, AccTrnFile.rec.Chapter);
        obj.RateObj = TWebAccTrnRate;
    obj.Sum_NatCur          = AccTrnFile.rec.Sum_NatCur          ;
    obj.SkipRecalcSumNatCur = AccTrnFile.rec.SkipRecalcSumNatCur ;
    obj.SkipRestEqChange    = AccTrnFile.rec.SkipRestEqChange    ;
    obj.RateObj.Rate                = AccTrnFile.rec.Rate        ;
        
    if(AccTrnFile.rec.IsInverse == "X")
      obj.RateObj.Rate_FI_From_Code = ПолучитьКодФинИн(AccTrnFile.rec.FIID_Receiver);
      obj.RateObj.Rate_FI_To_Code   = ПолучитьКодФинИн(AccTrnFile.rec.FIID_Payer);
      obj.RateObj.Rate_IsDtKt = false;
      obj.RateObj.Rate_IsKtDt = true;
    else
      obj.RateObj.Rate_FI_From_Code = ПолучитьКодФинИн(AccTrnFile.rec.FIID_Payer);
      obj.RateObj.Rate_FI_To_Code   = ПолучитьКодФинИн(AccTrnFile.rec.FIID_Receiver);
      obj.RateObj.Rate_IsDtKt = true;
      obj.RateObj.Rate_IsKtDt = false;
    end;
  
    obj.RateObj.Scale = AccTrnFile.rec.Scale;
    obj.RateObj.Point = AccTrnFile.rec.Point;
    obj.MultyMtd = WebCore_GetMultyMtdItemByID(AccTrnFile.rec.MethodID);
        obj.RateObj.Rate=obj.RateObj.Rate/Pow ( 10, obj.RateObj.Point );

    if(AccTrnFile.rec.SideTransaction == ACCTRN_SIDE_DEBET)
      obj.SideTrnName = "Дебет";
    elif(AccTrnFile.rec.SideTransaction == ACCTRN_SIDE_CREDIT)
      obj.SideTrnName = "Кредит";
    else
      obj.SideTrnName = ""; // ?? NULL
    end;

    obj.ExRateExtra = AccTrnFile.rec.ExRateExtra;
    obj.Ground      = AccTrnFile.rec.Ground;
	if(AccTrnFile.rec.Shifr_Oper!= "")
      obj.Cipher      = WebCore_GetCipherItemByShifr(AccTrnFile.rec.Shifr_Oper);
	end;

    obj.Kind_Oper   = AccTrnFile.rec.Kind_Oper  ;
    obj.Number_Pack = AccTrnFile.rec.Number_Pack;

    obj.TypeAc      = AccTrnFile.rec.TypeDocument;
    obj.UserTypeAc  = AccTrnFile.rec.UserTypeDocument;

    obj.UserField1  = AccTrnFile.rec.UserField1;
    obj.UserField2  = AccTrnFile.rec.UserField2;
    obj.UserField3  = AccTrnFile.rec.UserField3;
    obj.UserField4  = AccTrnFile.rec.UserField4;

    /* Параметры мультивалютной проводки */
    obj.IsMultyCarry = GetIsMultyCarryAcctrn( AccTrnFile.rec.FIID_Payer, AccTrnFile.rec.FIID_Receiver, AccTrnFile.rec.Result_Carry );

    if( obj.IsMultyCarry == true )

      var MainAccTrnFile : TBFile;

      obj.MultyCarryParmBase = TWebAccTrnInfo;

      if( AccTrnFile.rec.Result_Carry != DELTARATE_MCD )

        MainAccTrnFile = AccTrnFile;

      else
          
        MainAccTrnFile = GetMainMultyCarryAccTrn( AccTrnFile.rec.AccTrnID );

      end;

      obj.MC_Numb_Document = MainAccTrnFile.rec.Numb_Document;
      obj.MC_Date_Carry    = MainAccTrnFile.rec.Date_Carry;

      obj.MC_Ground           = MainAccTrnFile.rec.Ground;
      obj.MC_Shifr_Oper       = MainAccTrnFile.rec.Shifr_Oper;
      obj.MC_Kind_Oper        = MainAccTrnFile.rec.Kind_Oper;
      obj.MC_TypeDocument     = MainAccTrnFile.rec.TypeDocument;
      obj.MC_UserTypeDocument = MainAccTrnFile.rec.UserTypeDocument;
      obj.MC_Number_Pack      = MainAccTrnFile.rec.Number_Pack;

      obj.MultyCarryParmBase.SumNatCur     = MainAccTrnFile.rec.Sum_NatCur;
      obj.MultyCarryParmBase.ISOCodeNatCur = ПолучитьКодФинИн(NATCUR, null, FICK_ISOSTRING);

      obj.MultyCarryParmBase.AccountPayer = MainAccTrnFile.rec.Account_Payer;
      obj.MultyCarryParmBase.SumPayer     = MainAccTrnFile.rec.Sum_Payer;
      obj.MultyCarryParmBase.ISOCodePayer = ПолучитьКодФинИн(MainAccTrnFile.rec.FIID_Payer, null, FICK_ISOSTRING);
      obj.MultyCarryParmBase.NamePayer    = GetAccountName( MainAccTrnFile.rec.AccountID_Payer );

      obj.MultyCarryParmBase.AccountReceiver = MainAccTrnFile.rec.Account_Receiver;
      obj.MultyCarryParmBase.SumReceiver     = MainAccTrnFile.rec.Sum_Receiver;
      obj.MultyCarryParmBase.ISOCodeReceiver = ПолучитьКодФинИн(MainAccTrnFile.rec.FIID_Receiver, null, FICK_ISOSTRING);
      obj.MultyCarryParmBase.NameReceiver    = GetAccountName( MainAccTrnFile.rec.AccountID_Receiver );
      
      if( (AccTrnFile.rec.ExRateAccTrnID > 0) or (AccTrnFile.rec.Result_Carry == DELTARATE_MCD) )

        var ExRateAccTrnFile : TBFile;
        
        obj.MultyCarryParmExRate = TWebAccTrnInfo;

        if( AccTrnFile.rec.Result_Carry == DELTARATE_MCD )

          ExRateAccTrnFile = AccTrnFile;

        else
          
          ExRateAccTrnFile = GetExRateAccTrn( AccTrnFile.rec.ExRateAccTrnID );

        end;

        obj.MultyCarryParmExRate.SumNatCur     = ExRateAccTrnFile.rec.Sum_NatCur;
        obj.MultyCarryParmExRate.ISOCodeNatCur = ПолучитьКодФинИн(NATCUR, null, FICK_ISOSTRING);

        obj.MultyCarryParmExRate.AccountPayer = ExRateAccTrnFile.rec.Account_Payer;
        obj.MultyCarryParmExRate.NamePayer    = GetAccountName( ExRateAccTrnFile.rec.AccountID_Payer );

        obj.MultyCarryParmExRate.AccountReceiver = ExRateAccTrnFile.rec.Account_Receiver;
        obj.MultyCarryParmExRate.NameReceiver    = GetAccountName( ExRateAccTrnFile.rec.AccountID_Receiver );

      end;

    end;

    obj.OFRSymbol = GetOFRSymbol(AccTrnFile.rec.OFRRecID);
    
    var err;
    var regVal;

    GetRegistryValue("COMMON\\WILDCARD_SEARCH\\ONE_CHAR", V_STRING, regVal, err);
    
    if(err == 0)
      
      if(regVal == "NONE")
        
        regVal = "?";
        
      end;
      
    else
      
      RunError("Ошибка при чтении настройки реестра!");
      
    end; 
    
    obj.OFRSymbolCodeMask = "7" + regVal + regVal + regVal + regVal;

  end;

  return obj;
end;

macro WebCore_InsertAcctrnObject(AcctrnObject/*:TWebAcctrnObject*/, State : integer) // : WebResponseBuilder 
  ResponseBuilder = WebResponseBuilder();
  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  var res = true;
  var ErrMsg = "";

  var accTrn = RsbAccTransaction;

  if(AcctrnObject.AccTrnID > 0)
    var stat = accTrn.Find(AcctrnObject.AccTrnID); 

    if(stat != 0)
      CreateErrMsg(1); 
      AddErrorMessage(@ErrorMessage);
      res = false;
    end;
  end;

  if(res)
    //accTrn.AccTrnID            = AcctrnObject.AcctrnID

    FillAccTransactionData( accTrn, AcctrnObject );

    if(AcctrnObject.OFRSymbol != null)
      accTrn.AddOFRSymbol(AcctrnObject.OFRSymbol.RecID);
    else
      accTrn.AddOFRSymbol(0);
    end;

    if(AcctrnObject.AccTrnID > 0)
      res = accTrn.Update(@ErrMsg);

      if(res == false)
        ErrorMessage.Code     = 1;
        ErrorMessage.Message  = ErrMsg;
        ErrorMessage.Severity = MessageSeverity.RuntimeError;

        ResponseBuilder.AddMessage( ErrorMessage );
      end;
    else
      var Status_After = 0;

      if(State == 1) // TRN_STATE_FACT
        Status_After = 3; // ACCTRN_STATUS_FACT
      elif(State == 2) // TRN_STATE_PLAN
        Status_After = 4; // ACCTRN_STATUS_PLAN
      elif(State == 3) // TRN_STATE_POST
        Status_After = 5; // ACCTRN_STATUS_POST
      end;

      res = accTrn.Carry(Status_After, @ErrMsg);

      if(res == false)
        ErrorMessage.Code     = 1;
        ErrorMessage.Message  = ErrMsg;
        ErrorMessage.Severity = MessageSeverity.RuntimeError;

        ResponseBuilder.AddMessage( ErrorMessage );
      else
        AcctrnObject.AccTrnID = accTrn.AccTrnID;
      end;
    end;
  end;

  responseBuilder.SetUserObject(AcctrnObject);

  return responseBuilder.Result;
end;

macro WebCore_GetNewAccTrnObject(Chapter, DebetAccountID, CreditAccountID) : TWebAcctrnObject
  var cmd, rs, query;

  var obj = TWebAccTrnObject();

  var accTrn = RsbAccTransaction;

  obj.Date_Carry    = {curdate};
  obj.Department    = GetTWsDepartmentByCode({OperDprt});
  obj.Branch        = GetTWsDepartmentByCode({OperDprtNode});    

  query = "SELECT t_Chapter, t_Name, t_SHortName, t_Symbol FROM dobchaptr_dbt WHERE t_Chapter = ? ";
  cmd = RsdCommand(query);
  cmd.addParam( "", RSDBP_IN, Chapter );
  rs = RsdRecordset(cmd);

  if(rs.movenext)
    obj.Chapter = TWebChapter;
    obj.Chapter.Chapter   = SQL_ConvType(rs.value("t_Chapter"));     
    obj.Chapter.Name      = SQL_ConvType(rs.value("t_Name"));
    obj.Chapter.ShortName = SQL_ConvType(rs.value("t_ShortName"));
    obj.Chapter.Symbol    = SQL_ConvType(rs.value("t_Symbol"));
  end;

  obj.Debet          = TWebAccTrnSide();
  obj.Debet.AccRest  = TWebTrnAccRest();
  obj.Debet.FI       = GetTWsFinInstrByID(NATCUR);

  if(DebetAccountID != 0)
    obj.Debet.AccObj  = WebCore_CreateTWebAccountFromBD1(DebetAccountID);
        obj.Debet.FI      = GetTWsFinInstrByID(obj.Debet.AccObj.Currency);
  end;
  
  obj.Credit         = TWebAccTrnSide();
  obj.Credit.AccRest = TWebTrnAccRest();
  obj.Credit.FI      = GetTWsFinInstrByID(NATCUR);
  
  if(CreditAccountID != 0)
    obj.Credit.AccObj  = WebCore_CreateTWebAccountFromBD1(CreditAccountID);
        obj.Credit.FI      = GetTWsFinInstrByID(obj.Credit.AccObj.Currency);
  end;

  obj.Cipher      = WebCore_GetCipherItemByShifr(accTrn.Shifr_Oper);

//  obj.Kind_Oper   = accTrn.Kind_Oper  ;
  obj.Kind_Oper   = " 1";
  obj.Number_Pack = accTrn.Number_Pack;
  obj.MultyMtd = WebCore_GetMultyMtdItemByID(-1);

  obj.RateObj        =  TWebAccTrnRate;
  WebCore_GetRate(obj.Credit.FI.FIID, obj.Credit.FI.CodeWidget, obj.Credit.Sum, 
                                         obj.Debet.FI.FIID, obj.Debet.FI.CodeWidget, obj.Debet.Sum, obj.Date_Carry);
  
  obj.IsMultyCarry = false;

  return obj;
end;

macro WebCore_ConvertSum(Sum, Rate, Scale, Point, OutRate): money
        var _Rate=Rate*Pow ( 10, Point );
    var obj = ConvertSum (Sum, _Rate, Scale, Point, OutRate);
        return obj;
end;


macro WebCore_GetAccTrnPanParmAcc(AccountID) : TWebAccTrnPanAccParm
  var cmd, rs;
  var obj;

  var  AccountFile = TBFile("account.dbt", "R", 1 );

  AccountFile.Clear();
  AccountFile.rec.AccountID = AccountID;

  if( AccountFile.GetEQ() )
    obj = TWebAccTrnPanAccParm;
    var AccountRec = AccountFile.rec;

    obj.AccountID       = AccountRec.AccountID        ;
    var Type_Account    = AccountRec.Type_Account     ;

    if(Index(Type_Account, "В") > 0)
      obj.IsNVPI = true;
    else
      obj.IsNVPI = false;
    end;

    if(Index(Type_Account, "А") > 0)
      obj.IsCash = true;
    else
      obj.IsCash = false;
    end;
   
    if(AccountRec.CurrencyEq >=0)
      obj.CurrencyEq = GetTWsFinInstrByID(AccountRec.CurrencyEq);
    end;


    obj.RestNat = RestA( AccountRec.Account, {curdate}, null, AccountRec.Chapter, AccountRec.Code_Currency, NATCUR );

    if( AccountRec.Code_Currency == NATCUR )
      obj.RestCur = $0;
    else
      obj.RestCur = RestA( AccountRec.Account, {curdate}, null, AccountRec.Chapter, AccountRec.Code_Currency, AccountRec.Code_Currency );
    end;

    obj.RestEqv = GetRestAEQ( AccountRec.AccountID, {curdate});
  end;

  return obj;
end;

macro WebCore_InitSetOFRSymbolPanel()

  var resChar;

  var err;
  var regVal;

  GetRegistryValue("COMMON\\WILDCARD_SEARCH\\ONE_CHAR", V_STRING, regVal, err);
  
  if(err == 0)
    
    if(regVal == "NONE")
      
      regVal = "?";
      
    end;
    
  else
    
    RunError("Ошибка при чтении настройки реестра!");
    
  end; 
  
  resChar = "7" + regVal + regVal + regVal + regVal;
  
  return resChar;

end;

macro WebCore_SetAccTrnOFRRecID(OFRRecID, AccTrnList, ReportName)

  var ProgressIndicator = CreateProgressIndicator(true);

  ResponseBuilder = WebResponseBuilder();
  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  var ErrMsg = "";

  var i : integer;
  
  var AccTrnCount = AccTrnList.Size;
  
  var RepFile;
  
  if(ReportName != null)

    var FullFilePath = GetTxtFileName(ReportName);

    SetOutput(FullFilePath);

    PrintProtocolHeader_acctrn( "Протокол назначения проводке символа ОФР" );
  
  end;

  i = 0;
  
  ProgressIndicator.Start(AccTrnCount, "Выполнение...");      

  while( i < AccTrnCount )

    var stat = 0;

    var ResultMessage = "";

    var accTrn : RsbAccTransaction;

    accTrn = RsbAccTransaction;

    stat = accTrn.Find(AccTrnList[i].AcctrnID);
    
    if(stat != 0)
    
      CreateErrMsg(1); 
      AddErrorMessage(@ErrorMessage);
    
    end;

    if( stat == 0 )

      if(accTrn.AddOFRSymbol(OFRRecID))
        
        if(accTrn.Update(@ErrMsg) == false)
          ErrorMessage.Code     = 1;
          ErrorMessage.Message  = ErrMsg;
          ErrorMessage.Severity = MessageSeverity.RuntimeError;

          ResponseBuilder.AddMessage( ErrorMessage );
          
          stat = 1;
        end;
      
        if(( stat == 0 ) AND (ReportName != null))        
          ResultMessage = "Назначение проводке символа ОФР выполнено успешно";
        end;
        
      else
        
        stat = 1;
        
      end;

    end;

    if( stat != 0 )
      GetErrorMessage( @stat, @ResultMessage );
    end;

    if(ReportName != null)
      
      PrintProtocolLine_acctrn( accTrn.AccTrnID, accTrn.Numb_Document, stat, ResultMessage );
      
    end;
    
    accTrn = null;
    
    i = i + 1;
    
    ProgressIndicator.Update(i);

  end;
  
  if(ReportName != null)

    PrintProtocolFooter_acctrn();

    SetOutput();

    FullFilePath = ConvertTxt2Html(FullFilePath);
    
    RepFile = GetTextFile(FullFilePath);
  
  end;

  responseBuilder.SetUserObject(RepFile);
  
  ProgressIndicator.Stop();

  return responseBuilder.Result;  
  
end;

/*
 * Печатает проводку из панели ввода/редактирования.
 */
macro WebCore_PrintAcctrnPanel( AcctrnObject, FileName : string )

  record acctrn("acctrn.dbt");

  var accTrnData : RsbAccTransactionData;

  ClearRecord( acctrn );

  accTrnData = RsbAccTransactionData;

  FillAccTransactionData( accTrnData, AcctrnObject );

  accTrnData.GetAccTrnRecord( acctrn );

  return _WebCore_PrintAcctrn( acctrn, FileName);

end;

// var ob0 = WebCore_GetAccTrnPanParmAcc(16);

// var ob1 = WebCore_GetNewAccTrnObject(1);
// var ob = WebCore_GetAccTrnObject( 1 );
// var res = WebCore_InsertAcctrnObject(ob, 1);
// ob = ob;
