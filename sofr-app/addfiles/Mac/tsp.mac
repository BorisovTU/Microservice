
import rsexts;
var {oper};



private CLASS UserTerminal()

  CLASS EH(p1, p2)
    var Name  = p1,
        isDir = p2;
  END;
 
  const Доступен     = 0,
        Недоступен   = 1,
        Несуществует = 2;

  // Пути относительно терминала  
  // var TempRootDir = "$C:\\SOFR_TEMP", // Не используется
  var RootDir     = "$F:\\client\\term",
      ImportDir   = RootDir + "\\import",
      ExportDir   = RootDir + "\\export",
      РаспорDir   = ExportDir + "\\Распоряжения",
      TerminalDir = RootDir + "\\terminal",
      UpdateDir   = TerminalDir + "\\update",
      DataDir     = UpdateDir + "\\data",
      StatusDir   = UpdateDir + "\\status",
      LogDir      = UpdateDir + "\\logs",            //@set LogUpdate=%LogDir%sofrupdate.log
      UpdateFile  = RootDir + "\\update-sofr.sh";
      
  // Пути относительно сервера приложений
  var EtalonDir         = "S:\\sofr_terminal_for_users",
      EtalonTerminalDir = EtalonDir + "\\term_for_users",
      EtalonUpdateFile  = EtalonDir + "\\update-sofr.sh_",
      EtalonStatusFile  = EtalonDir + "\\_COPY_FILES_FROM_ETALON.z";
      // FirstTermArhiv    = "SOFR.7z"; // Не используется

  Macro WriteLog(logstr)
    TStream("..\\Logs\\TermDir.log", "A").write(logstr);
  end;

  Macro SetZayavka(Zayavka)
    return CopyFile (EtalonStatusFile, StatusDir + "\\" + Zayavka , True, "Подача заявки");
  End;

  Macro DelZayavka(Zayavka)
    return RemoveFile(StatusDir + "\\" + Zayavka);
  End;

  Macro CheckZayavka(Zayavka)
    var x = TDirList(StatusDir+"\\*","F"),
        xi = 0;
    if (x.Name(xi) != Zayavka)
      return false;
    end;
    return true;
  end;

  Macro Run(ShortUpdate)
    var ReadyToUpdate = false;
    var sep = "\t\t";
    var YYYY, MM, DD, H, M, S;
    DateSplit(date(),DD,MM,YYYY);
    TimeSplit(time(),H,M,S);

    var Zayavka = YYYY +
                  substr("0"+string(MM),strlen("0"+string(MM))-1) +
                  substr("0"+string(DD),strlen("0"+string(DD))-1) +
                  "_" +
                  substr("0"+string(H),strlen("0"+string(H))-1) +
                  substr("0"+string(M),strlen("0"+string(M))-1) +
                  substr("0"+string(S),strlen("0"+string(S))-1) +
                  "_COPY_FILES_FROM_ETALON.z";

    if (ExistDir(StatusDir) == Доступен)
      //msgbox("Подаём заявку ", Zayavka);
      if (SetZayavka(Zayavka)) 
        if (CheckZayavka(Zayavka))
          ReadyToUpdate = true;
        else
          //msgbox("Заявка ОТКЛОНЕНА, т.е. исполняется ранее поданная (другая) заявка.");
          //msgbox("Снимаем заявку");
          DelZayavka(Zayavka);
        end;
      end;
      //msgbox("Заявка ПРИНЯТА к исполнению");
    else
      WriteLog(" "+ date() +"\t"+ time() +"\t"+ {oper} +":\tТерминал имеет ограниченное наполнение. Update level 0\n");
      //msgbox("Терминал запущен из неизвестного места. Возможна работа на сервере. Заявку не подаём.");
    end;



    if (ExistDir(RootDir) == Доступен)
      if ( ( (ExistDir(UpdateFile) == Доступен) and (ExistDir(EtalonUpdateFile) == Доступен)
           ) or
           ( ((ExistDir(UpdateFile) == Доступен) or (ExistDir(UpdateFile) == Несуществует)) and (ExistDir(EtalonUpdateFile) == Доступен)
           )
         )
         var x1 = CopyFile (EtalonUpdateFile, UpdateFile , True, "Обязательное обновление");
         println(x1);
      end;

      if ((ExistDir(TerminalDir) == Доступен) and (ShortUpdate != "OnlyMain") and (ReadyToUpdate))
        this.Compare(EtalonTerminalDir,TerminalDir,DataDir);
      end;
    end;

//    println(RootDir,sep,ExistDir(RootDir));
//    println(ImportDir,sep,ExistDir(ImportDir));
//    println(ExportDir,sep,ExistDir(ExportDir));
//    println(TerminalDir,sep,ExistDir(TerminalDir));
//    println;
//    println(UpdateFile,sep,ExistFile(UpdateFile));

    if (ReadyToUpdate)
      //msgbox("Снимаем заявку");
      DelZayavka(Zayavka)
    end;
  end;

  Macro Compare(spath, dpath, tmppath)

    private macro GetList(root, list)
      var x = TDirList(root+"\\*","F"),
          xi = 0;
      while (xi < x.Count)
        list[list.Size] = EH(root+"\\"+x.Name(xi), False);
        xi = xi + 1;
      end;
      x = TDirList(root+"\\*","D");
      xi = 0;
      while(xi < x.Count)
        if ((substr(x.Name(xi),1,1) != ".") and ((root+"\\"+x.Name(xi)) != UpdateDir))
          list[list.Size] = EH(root+"\\"+x.Name(xi), True);
          GetList(root+"\\"+x.Name(xi),list);
        end;       
        xi = xi + 1;
      end;
    end;

    var src = TDirList(spath + "\\*.*", "DF"),
        dst = TDirList(dpath + "\\*.*", "DF"), 
        si, di, Find, sstr, 
        sl = strlen(spath)+2,
        dl = strlen(dpath)+2,
        tl = strlen(tmppath)+2,
        sdt, stm, ssize,
        ddt, dtm, dsize;

    var sList   = TArray(),
        dList   = TArray(),
        tmpList = TArray();

    GetList(spath,sList);
    GetList(dpath,dList);
    GetList(tmppath,tmpList);

    //msgbox("Составим список эталонных версий файлов");
    for (si,0,sList.Size-1)
      sstr = substr(sList[si].Name,sl);
      for (di,0,dList.Size-1)
        if ( (sstr == substr(dList[di].Name,dl)) and (sList[si].isDir == dList[di].isDir) )
          GetFileInfo (sList[si].Name, sdt, stm, ssize);
          GetFileInfo (dList[di].Name, ddt, dtm, dsize);
          if ( ((sdt == ddt) and (stm == dtm) and (ssize == dsize)) or 
               (sList[si].isDir == True) )
            sList[si].Name = "";
          end;
          dList[di].Name = "";
          break;
        end;
      end;
    end;

    //msgbox("Исключим ранее скопированные, но ещё не установленные");

    for (si,0,sList.Size-1)
      sstr = substr(sList[si].Name,sl);
      if ( (sList[si].Name != "") and (tmpList.Size) )
        for (di,0,tmpList.Size-1)
          if ( (sstr == substr(tmpList[di].Name,tl)) and (sList[si].isDir == tmpList[di].isDir) )
            GetFileInfo (sList[si].Name, sdt, stm, ssize);
            GetFileInfo (tmpList[di].Name, ddt, dtm, dsize);
            if ( ((sdt == ddt) and (stm == dtm) and (ssize == dsize)) or 
                 (sList[si].isDir == True) )
              sList[si].Name = "";
            end;
            tmpList[di].Name = "";
            break;
          end;
        end;
      end;
    end;



    var rez;

    InitProgress(tmpList.Size*2,"","Очистка каталога ранее скопированных файлов для терминала, от лишних, уже не входящих в эталон, файлов и каталогов");
    if (tmpList.Size)
      for (di,0,tmpList.Size-1)
        if ( (tmpList[di].Name != "") and not tmpList[di].isDir)
          rez = RemoveFile (tmpList[di].Name);
          println("D  must del    ", tmpList[di].isDir, "   ", tmpList[di].Name,  "   ", rez);
        end;
        UseProgress(di);
      end;
      for (di,tmpList.Size-1,0,-1)
        if ( (tmpList[di].Name != "") and tmpList[di].isDir)
          rez = RemoveDir (tmpList[di].Name);
          println("D  must delD   ", tmpList[di].isDir, "   ", tmpList[di].Name,  "   ", rez);
          tmpList[di].Name = "";
        end;
        UseProgress(tmpList.Size+di);
      end;
    end;
    RemProgress();                                      	


    InitProgress(sList.Size,"","Копирование файлов для обновления терминала");
    for (si,0,sList.Size-1)
      if ( sList[si].Name != "")
        if (sList[si].isDir)
          rez = MakeDir(StrSubst(sList[si].Name,EtalonTerminalDir,DataDir));
          println("S  must MD     ", sList[si].isDir, "   ", StrSubst(sList[si].Name,EtalonTerminalDir,TerminalDir), "   ", rez);
        else
          rez = CopyFile (sList[si].Name, StrSubst(sList[si].Name,EtalonTerminalDir,DataDir));
          println("S  must copy   ", sList[si].isDir, "   ", sList[si].Name,  "   ", rez);
        end;
      end;
      UseProgress(si);
    end;              
    RemProgress();                                      	

    InitProgress(dList.Size*2,"","Очистка каталога терминала от лишних, не входящих в эталон, файлов и каталогов");
    for (di,0,dList.Size-1)
      if ( (dList[di].Name != "") and not dList[di].isDir)
        rez = RemoveFile (dList[di].Name);
        println("D  must del    ", dList[di].isDir, "   ", dList[di].Name,  "   ", rez);
      end;
      UseProgress(di);
    end;
    for (di,dList.Size-1,0,-1)
      if ( (dList[di].Name != "") and dList[di].isDir)
        rez = RemoveDir (dList[di].Name);
        println("D  must delD   ", dList[di].isDir, "   ", dList[di].Name,  "   ", rez);
        dList[di].Name = "";
      end;
      UseProgress(dList.Size+di);
    end;
    RemProgress();


  End;

  // Macro FirstTerm()
  //   var rez = true;
  //   if (ExistDir(TempRootDir) == Несуществует)
  //       MakeDir(TempRootDir);
  //   end;
  //   if (ExistDir(TempRootDir) == Доступен)
  //     InitProgress(-1,"","Копирование архива обновления терминала");
  //     rez = CopyFile (EtalonDir+"\\"+FirstTermArhiv, TempRootDir+"\\"+FirstTermArhiv);
  //     RemProgress();
  //     msgbox("Результат: ",rez, "|",EtalonDir+"\\"+FirstTermArhiv, "|", TempRootDir+"\\"+FirstTermArhiv);
  //   end;
  // end;


  Macro LogInfo()
    var logstr = " "+ date() +"\t"+ time() +"\t"+ {oper} +":\t";
        logstr = logstr + ExistDir(RootDir)         +"";
        logstr = logstr +"-"+ ExistDir(РаспорDir)   +"-";
        logstr = logstr +     ExistDir(ImportDir)   +"";
        logstr = logstr +     ExistDir(ExportDir)   +"";
        logstr = logstr +     ExistDir(TerminalDir) +"";
        logstr = logstr +     ExistDir(UpdateDir)   +"";
        logstr = logstr +     ExistDir(DataDir)     +""; 
        logstr = logstr +     ExistDir(StatusDir)   +""; 
        logstr = logstr +     ExistDir(LogDir)      +""; 
        logstr = logstr +     ExistDir(UpdateFile)  +" (0)\t";
        logstr = logstr +     GetCurDir(true)       +"\n";

    WriteLog(logstr);
    //TStream("..\\Logs\\TermDir.log", "A").write(logstr);
  end;

  Macro CheckDirs()
    var rez = ExistDir(RootDir) + 
              ExistDir(РаспорDir) +
              ExistDir(ImportDir) + 
              ExistDir(ExportDir) + 
              ExistDir(TerminalDir) + 
              ExistDir(UpdateDir) + 
              ExistDir(DataDir) +
              ExistDir(StatusDir) +
              ExistDir(LogDir) +
              ExistDir(UpdateFile);
    return rez;
  End;

  Macro CheckMainDirs()
    var rez = ExistDir(RootDir) + 
              ExistDir(TerminalDir);
    if (("$"+GetCurDir(true)) != TerminalDir)
//      msgbox("_"+GetCurDir(true)+"_|_"+TerminalDir+"_");
      rez = rez + 100;
    end;
    return rez;
  end;

END;




/****************
  ТОЧКА ВХОДА
*****************/

macro RunCopyUpdateTerminalFiles()
  var x = UserTerminal();
  x.run();
end;

macro RunCopyUpdateTerminalMainFiles()
  var x = UserTerminal();
  x.run("OnlyMain");
end;

// macro GetFirstTerm()
//   var x = UserTerminal();
//   x.FirstTerm();
// end;


macro LogInfo()
  var x = UserTerminal();
  x.LogInfo();
end;

macro isDirsExist()
  var x = UserTerminal();
  return x.CheckDirs();
end;

macro isMainDirsExist()
  var x = UserTerminal();
  return x.CheckMainDirs();
end;