/*----------------------------------------------------------*/
/* vtb_MarkClientsAndContract.mac
   Простановка кода субъекта 109 Код ВТБ 
   и примечаний 130, 131 (номер и дата договора ВТБ) для 
   договора                                                 */
/*                                                          */
/* Автор: Гераськина Т.                                     */
/*----------------------------------------------------------*/
import rsd;
import BankInter;

// получение sql-ошибки. 
// использовать в onError(e) так:  RunError(e.message + getFullCmdErrorText(cmd));
private macro getFullCmdErrorText(_cmd:RSDCommand)
   var cmd = _cmd;
   var err_str = "", i = 0;

   while( i < cmd.connection.environment.ErrorCount)
      err_str = err_str + cmd.connection.environment.Error(i).Descr + " ";
      i = i+1;
   end;

   return trim(err_str);
end;

private class Logger()

    macro RepHeader(dt, tm)
       var str;
       str =     "\n                             ОТЧЕТ о создании примечаний  \n";
       str = str+"\n";
       str = str+"Дата загрузки  : "+dt+"\n";
       str = str+"Время загрузки : "+tm+"\n";
       str = str+"┌─┬──────────────────┬───────────────────┬───┬────────────────────┬───────────────┬─────────────────────────────────────────────────────────────────────────────────────────┐\n";
       str = str+"│ │Код ВТБ           │Соглашение         │ИИС│ Дата договора      │Статус загрузки│Комментарий                                                                              │\n";
       str = str+"├─┼──────────────────┼───────────────────┼───┼────────────────────┼───────────────┼─────────────────────────────────────────────────────────────────────────────────────────┤";
       println(str);
    end;

    macro RepFooter(suc, er, tot)
       var str = "└─┴──────────────────┴───────────────────┴───┴────────────────────┴───────────────┴─────────────────────────────────────────────────────────────────────────────────────────┘\n";
       str = str+"Загружено успешно: "+suc+"\n";
       str = str+"Не загружено     : "+er+"\n";
       str = str+"Всего            : "+tot+"\n";
      println(str);
    end;

    macro RepStr(_err,_code, _ca, _iis, _dt, _stat,_comm)
        var status;
        if (_stat == false)
            status = "Загружено";
        else
            status = "Не загружено";
        end;
        var str = "│"+string(_err  :1)   +"│"+
                  string(_code     :18:l)+"│"+
                  string(_ca       :19:l)+"│"+
                  string(_iis      :3:l) +"│"+
                  string(_dt       :20:l)+"│"+
                  string(status    :15:l)+"│"+
                  string(_comm          );
       return println(str);
    end;
end;


macro MarkClients
   var sql_str, cmd, rs;
   var cnt_code = 0;
   var cnt_partyid = 0;
   
   println("Дата начала обработки кодов: "+date());
   println("Время начала обработки кодов: "+time());
       
   BegAction(1, "Обработка кодов клиентов. Ждите...");
   
   sql_str = 
"begin "+
"    execute immediate 'alter trigger DOBJCODE_DBT_TCA disable'; "+
"    execute immediate 'alter trigger DOBJCODE_DBT_TCB disable'; "+
"    execute immediate 'alter trigger DOBJCODE_DBT_TRGGT_A_EACH_ROW disable'; "+
"    execute immediate 'alter trigger DOBJCODE_DBT_TRGGT_A_ALL disable'; "+
"end; ";
   cmd = RSDCommand(sql_str);
   cmd.execute;
   cmd.close;

   sql_Str = 
"declare "+
"   cnt_code number(10); "+
"   cnt_partyid number(10); "+
"begin "+
"   merge into DOBJCODE_DBT objc "+
"   using "+
"     (select obj.t_objectid, cm.t_code_vtb "+
"        from "+
"           DOBJCODE_DBT obj, "+
"           usr_code_match cm "+
"       where obj.t_objecttype = 3 and obj.t_codekind = 101 and obj.t_state = 0 "+
"         and cm.t_code_abs = obj.t_code ) n "+
"      on (objc.t_objecttype = 3 and objc.t_codekind = 109 and n.t_objectid = objc.t_objectid) "+
"   when not matched then "+
"        insert ( objc.T_OBJECTTYPE, "+
"                 objc.T_CODEKIND, objc.T_OBJECTID, objc.T_CODE, objc.T_STATE, objc.T_BANKDATE, "+
"                 objc.T_SYSDATE, objc.T_SYSTIME, objc.T_USERID, objc.T_BRANCH, objc.T_NUMSESSION, "+
"                 objc.T_UNIQUE, objc.T_AUTOKEY, objc.T_BANKCLOSEDATE, objc.T_NORMCODE) "+
"        values (3, 109, n.t_objectid, n.t_code_vtb, 0, to_date('01.01.0001', 'dd.mm.yyyy'), "+
"                trunc(sysdate), to_date('01.01.0001 '||to_char(sysdate, 'hh24.mi.ss'),'dd.mm.yyyy hh24:mi:ss'), 9997, 1, 0, "+
"                chr(88), 0, to_date('01.01.0001', 'dd.mm.yyyy'), chr(1)); "+
"   cnt_code := sql%rowcount; "+
"   cnt_partyid := 0; "+
"   for c in (select obj.t_objectid, cm.t_code_vtb, cm.rowid "+
"               from dobjcode_dbt obj, usr_code_match cm "+
"              where obj.t_objecttype = 3 and obj.t_codekind = 109 and obj.t_state = 0 "+
"                and obj.t_code = cm.t_code_vtb and cm.t_partyid = 0 "+
"             ) loop "+
"     update usr_code_match "+
"        set t_partyid = c.t_objectid "+
"      where rowid = c.rowid; "+
"     cnt_partyid := cnt_partyid+1; "+
"   end loop; "+
"   :p_cnt_code := cnt_code; "+
"   :p_cnt_partyid := cnt_partyid; "+
"end; ";

   cmd = RSDCommand(sql_str);

   cmd.AddParam("p_cnt_code", RSDBP_OUT, v_integer);
   cmd.AddParam("p_cnt_partyid", RSDBP_OUT, v_integer);
   cmd.execute;
   if ((cmd.Value("p_cnt_code") != null) and (cmd.Value("p_cnt_code") != nullval))
      cnt_code = cmd.Value("p_cnt_code");
   end;
   if ((cmd.Value("p_cnt_partyid") != null) and (cmd.Value("p_cnt_partyid") != nullval))
      cnt_partyid = cmd.Value("p_cnt_partyid");
   end;
   cmd.close;
   
   sql_str = 
"begin "+
"    execute immediate 'alter trigger DOBJCODE_DBT_TCA enable'; "+
"    execute immediate 'alter trigger DOBJCODE_DBT_TCB enable'; "+
"    execute immediate 'alter trigger DOBJCODE_DBT_TRGGT_A_EACH_ROW enable'; "+
"    execute immediate 'alter trigger DOBJCODE_DBT_TRGGT_A_ALL enable'; "+
"end; ";
   cmd = RSDCommand(sql_str);
   cmd.execute;
   cmd.close;

   
   EndAction(1);
   println ("Создано кодов клиентов (код ВТБ): "+cnt_code);
   println ("Обновлено записей usr_code_match: "+cnt_partyid);
   
   cnt_code = 0;
   sql_str = "select count(*) from dobjcode_dbt where t_objecttype = 3 and t_codekind = 109 and t_state = 0";
   rs = RSDRecordSet(sql_str);
   if (rs.movenext)
      cnt_code = Int(rs.value(0));
   end;
   println ("Всего существует кодов клиентов (код ВТБ): "+cnt_code);
   
   println("Время окончания обработки кодов: "+time());
   
   return "OK";
onError(e)
   return e.message + getFullCmdErrorText(cmd);
end;

macro MarkContract(log)
   var sql_str, rs, cmd;
   var ErrStr = "",ErrStat = false;
   var objectid, objecttype = 207;
   var stat = 0, err_txt = "";
   var cn, cn_vtb, cd_vtb;
   var err=0, suc=0;
   const CONTRACT_NOTEKIND_CN = 130;
   const CONTRACT_NOTEKIND_CD = 131;
   
   var sql_str_upd, cmd_upd;
   var cnt_notes=0;
   
   log.RepHeader(date(),time());
    
   sql_str ="select contract.t_code_vtb, contract.t_iis, contract.t_contract_number_vtb, "+
            "        to_date(substr(contract.t_contract_date,1,10),'yyyy-mm-dd') dt, dl.t_dlcontrid, sf.t_number, "+
            "        contract.rowid "+
            "  from "+
            "     usr_contract_match contract, "+
            "     usr_code_match codes, "+
            "     dsfcontr_dbt sf, "+
            "     ddlcontr_dbt dl "+
            "where codes.t_code_vtb = contract.t_code_vtb and codes.t_partyid > 0 "+
            "  and sf.t_partyid = codes.t_partyid and sf.t_dateclose = to_date('01.01.0001','dd.mm.yyyy') "+
            "  and dl.t_sfcontrid = sf.t_id "+
            "  and dl.t_iis = decode(contract.t_iis,1,chr(88),chr(0)) "+
            "  and contract.t_dlid = 0";
   rs = RSDRecordSet(sql_str);
   while (rs.movenext)
      objectid = String(rs.value("t_dlcontrid"):o:34);
      cn = rs.value("t_number");
      cn_vtb = rs.value("t_contract_number_vtb");
      cd_vtb = date(rs.value("dt"));
      stat = addnoteforobject(objecttype,objectid,CONTRACT_NOTEKIND_CN,cn_vtb);
      if (stat)
         ErrStat = true;
         ErrStr = "Ошибка добавления примечания "+CONTRACT_NOTEKIND_CN+"="+cn_vtb+" к договору "+cn;
         log.RepStr("X",rs.value("t_code_vtb"),rs.value("t_contract_number_vtb"),rs.value("t_iis"),cd_vtb,ErrStat,ErrStr);
         err = err + 1;
         continue;
      end;

      stat = addnoteforobject(objecttype,objectid,CONTRACT_NOTEKIND_CD,cd_vtb);
      if (stat)
         ErrStat = true;
         ErrStr = "Ошибка добавления примечания "+CONTRACT_NOTEKIND_CD+"="+cd_vtb+" к договору "+cn;
         log.RepStr("X",rs.value("t_code_vtb"),rs.value("t_contract_number_vtb"),rs.value("t_iis"),cd_vtb,ErrStat,ErrStr);
         err = err + 1;
         continue;
      end;
      
      sql_str_upd = "update usr_contract_match set t_dlid = :dlid where rowid = :r";
      cmd_upd = RSDCommand(sql_str_upd);
      cmd_upd.AddParam("dlid", RSDBP_IN, rs.value("t_dlcontrid"));
      cmd_upd.AddParam("r", RSDBP_IN, rs.value("rowid"));
      cmd_upd.execute;
      
      ErrStat = false;
      ErrStr = "";
      log.RepStr("",rs.value("t_code_vtb"),rs.value("t_contract_number_vtb"),rs.value("t_iis"),cd_vtb,ErrStat,ErrStr);
      suc = suc + 1;
   end;

   log.RepFooter(suc,err,suc+err);
   
   sql_str = "select count(*) from dnotetext_dbt where t_objecttype = 207 and t_notekind = :nk ";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("nk", RSDBP_IN, CONTRACT_NOTEKIND_CN);
   rs = RSDRecordSet(cmd);
   if (rs.movenext)
      cnt_notes = Int(rs.value(0));
   end;
   println ("Всего существует примечаний "+CONTRACT_NOTEKIND_CN+" у договоров (номер договора ВТБ): "+cnt_notes);
   
   sql_str = "select count(*) from dnotetext_dbt where t_objecttype = 207 and t_notekind = :nk ";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("nk", RSDBP_IN, CONTRACT_NOTEKIND_CD);
   rs = RSDRecordSet(cmd);
   if (rs.movenext)
      cnt_notes = Int(rs.value(0));
   end;
   println ("Всего существует примечаний "+CONTRACT_NOTEKIND_CD+" у договоров (дата договора ВТБ): "+cnt_notes);
   
   println("Время окончания обработки примечаний: "+time());
   
   return "OK";
end;


macro runMarkClientsAndContract(errp:@string, result_protocol:@variant)
   var state;
   var log = Logger();
   var dt = StrSubSt(string(Date()),".","")+StrSubSt(string(Time()),":","");
   var InfoLogFileName = GetTxtFileName( "mark_"+dt);
   SetOutput( InfoLogFileName, true ); //Пишем лог в файл. Начало
   
   state = MarkClients();
   if (state != "OK")
      errp = state;
      SetOutput( NULL, true ); //Пишем лог в файл. Конец
msgbox(state);
      return false;
   end;
   
   state = MarkContract(@log);
   if (state != "OK")
      errp = state;
msgbox(state);
      SetOutput( NULL, true ); //Пишем лог в файл. Конец
      return false;
   end;

   SetOutput( NULL, true ); //Пишем лог в файл. Конец
   ViewFile( InfoLogFileName );   


   return true;
end;

//runMarkClientsAndContract;