/**                                                                                                             
 @file CloseBillOldAcc.mac                                                                                           
 @brief Устранение дефекта DEF-66119 <Не закрываются счета по собственным векселям при погашении>
*/

IMPORT Vsbnrfd, vs4each, vsmulty, vslib;
import oralib, likepy, BankInter;

private VAR tableHeader = 
 "┌───────────────────┬─────────────────────┬───────────────────────────────────┬────────────┬───────────────────┬───────────────────┬──────────────────────────────────────────┬──────────────────────────────┐\n"+
 "│   Номер векселя   │   Категория учета   │            Номер счета            │   Валюта   │   Дата открытия   │   Дата закрытия   │            Наименование счета            │            Ошибка            │\n" +
 "├───────────────────┼─────────────────────┼───────────────────────────────────┼────────────┼───────────────────┼───────────────────┼──────────────────────────────────────────┼──────────────────────────────┤";

PRIVATE VAR CountClosed = 0, CountError = 0;
FILE ReportOutFile() txt write; /*Файл вывода*/

private macro PrintHeader()
  println("Протокол закрытия счетов по собственным векселям при погашении");
  println (tableHeader);
end;

private macro GetCCY(FIID)
  var cmd = DL_RSDCommand("select t_CCY from dfininstr_dbt where t_FIID = ?");
  var ccy = "";

  cmd.AddParam(FIID);

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    ccy = DataSet.CCY;
  end;

  return ccy;
end;

private macro PrintRepLine(BillNumber, Category, NumberAcc, Currency, OpenDate, CloseDate, NameAcc, Error);
  [│ ################# │ ################### │ ################################# │ ########## │ ################# │ ################# │ ######################################## │ ############################ │]  
  (BillNumber:w, Category:w, NumberAcc:w, GetCCY(Currency):w, OpenDate:w, CloseDate:w, NameAcc:w, Error:w);
end;

private macro PrintEnd()
 [└───────────────────┴─────────────────────┴───────────────────────────────────┴────────────┴───────────────────┴───────────────────┴──────────────────────────────────────────┴──────────────────────────────┘];
end;

PRIVATE MACRO GetAccountRecord(rec_acc, Account, Code_Currency )
  file Acc("account.dbt") key 0;
  Acc.Chapter       = 1;
  Acc.Account       = Account;
  Acc.Code_Currency = Code_Currency;
  if( getEQ( Acc ) )
    Copy(rec_acc, Acc);
    return Acc;  
  end;
end;

private macro Rest_account_natcur (account, code_currency, chapter, restdate):money
  var rest:money = $0;
  var sql = ExecSqlSelect("select rsb_account.restall ( :acc, :chapter, :code_currency, :dt, 0) t_rest from dual",
                          MakeArray(SqlParam("acc", account), SqlParam("chapter", chapter), SqlParam("code_currency", code_currency), SqlParam("dt", restdate)));
  if (sql.MoveNext() ) 
    rest = sql.value("t_rest");
  end;
  return rest;
End;


PRIVATE MACRO open_accounts(nameacc,account)
   var open_acc="";
   var cmd =DL_RSDCommand("select t_account, t_open_date from daccount_dbt where t_account like ? and t_balance like '523%' and t_account <> ? and t_nameaccount like ? order by t_balance asc");
   cmd.AddParam("%"+substr(account,15,20));
   cmd.AddParam(account);
   cmd.AddParam(nameacc + "%");

   var DataSet = cmd.execute();
   while (DataSet.moveNext())
      open_acc = DataSet.t_account; 
   end;
   return open_acc;
END;

private macro ВыполнитьПереоценкуПоСчету (Account, currency, closedate)
  var rest = $0;
  if (currency != NATCUR)
    rest = Rest_account_natcur(account, currency, 1, closedate);
    if (rest != $0)
      OverValueAccount(Account, currency, 1, closedate);
    end;
  end;
End;

PRIVATE MACRO DoCloseAcc(fd, categ, fi, closeDate, firole)
  var exists_open_acc = "";
  var err="";
  var nameaccount ="";

  var acc;
  record rec_acc("account.dbt");
  record rec_acc_exist("account.dbt");

  if ((acc = MC_GetExistAccount(categ, fd.kind, fd.id, FIROLE_UNDEF, fi, closeDate)) != "")
      GetAccountRecord(rec_acc, acc, fi);
      if(rec_acc.Close_Date == date(0,0,0))
      if (RestAMulty(acc, fi) == $0)
        ВыполнитьПереоценкуПоСчету(acc, fi, closeDate); 
        
        if (MC_FindAndCloseAccount(categ, fd, closeDate, firole, fi, MC_ACC_CLOSE_INDIVIDUAL) != 0)
          PrintRepLine(trim(fd.GetBnr().rec.BcNumber), categ, rec_acc.Account, fi, rec_acc.Open_Date, rec_acc.Close_Date, rec_acc.NameAccount, "Ошибка при закрытии");
          CountError = CountError + 1;
        else
          PrintRepLine(trim(fd.GetBnr().rec.BcNumber), categ, rec_acc.Account, fi, rec_acc.Open_Date,closeDate, rec_acc.NameAccount, "");
          CountClosed = CountClosed + 1;
        end;   
        if ((categ == "Наш вексель" ) or (categ == "Дисконт, Свексель"))     //при погашении для категорий Наш вексель и Дисконт, Свексель необходимо закрывать все счета открытые до счета "До восстребования" 52307-52301
          if (categ == "Наш вексель" )
            nameaccount = "Вексель";
          else
            nameaccount = "Дисконт. Вексель";
          end;
          exists_open_acc = open_accounts(nameaccount,acc);
          if ((exists_open_acc != "") and (RestAMulty(exists_open_acc, fi) == $0))
            GetAccountRecord(rec_acc_exist, exists_open_acc, fi);
            if(rec_acc_exist.Close_Date == date(0,0,0))
            ВыполнитьПереоценкуПоСчету(exists_open_acc, fi,GetDateAfterWorkDays(closeDate,-1)); 
            if(CB_CloseAccount (1, fi, exists_open_acc, closeDate,err));
              PrintRepLine(trim(fd.GetBnr().rec.BcNumber), categ, rec_acc_exist.Account, fi, rec_acc_exist.Open_Date,rec_acc_exist.Close_Date, rec_acc_exist.NameAccount, "Ошибка при закрытии");
              CountError = CountError + 1;
            else
              PrintRepLine(trim(fd.GetBnr().rec.BcNumber), categ, rec_acc_exist.Account, fi, rec_acc_exist.Open_Date, closeDate, rec_acc_exist.NameAccount, "");
              CountClosed = CountClosed + 1;
            end;
            end;
          end;
        end;
      end;
      end;
  end;
END;

PRIVATE MACRO CloseBillAccount(BCID, closeDate)
  var fd;

  fd  = VSBannerFD( DL_VSBANNER, BCID );;
  DoCloseAcc(fd, "Наш вексель",              ОпределитьВалютуУчетаВекселя(fd.GetBnr().rec.PayFIID, fd.GetLeg().rec.PFI), closeDate);
  DoCloseAcc(fd, "СВексель к исполнению",    ОпределитьВалютуУчетаВекселя(fd.GetBnr().rec.PayFIID, fd.GetLeg().rec.PFI), closeDate);

  DoCloseAcc(fd, "Дисконт, СВексель к исп.", ОпределитьВалютуУчетаВекселя(fd.GetBnr().rec.PayFIID, fd.GetLeg().rec.PFI), closeDate);
  DoCloseAcc(fd, "Обяз%, СВексель к исп.",   ОпределитьВалютуУчетаВекселя(fd.GetBnr().rec.PayFIID, fd.GetLeg().rec.PFI), closeDate);

  DoCloseAcc(fd, "К погашению, Сц/б",        ОпределитьВалютуУчетаВекселя(fd.GetBnr().rec.PayFIID, fd.GetLeg().rec.PFI), closeDate);
  DoCloseAcc(fd, "-Корр, Свексель",          NATCUR, closeDate);
  DoCloseAcc(fd, "+Корр, Свексель",          NATCUR, closeDate);
  DoCloseAcc(fd, "Дисконт, Свексель",        ОпределитьВалютуУчетаВекселя(fd.GetBnr().rec.PayFIID, fd.GetLeg().rec.PFI), closeDate, 43);

  DoCloseAcc(fd, "Обяз%,СВексель",           ОпределитьВалютуУчетаВекселя(fd.GetBnr().rec.PayFIID, fd.GetLeg().rec.PFI), closeDate);
END;

private macro DDMMYYYY( dt ):string
  var day, mon, year, str;
  DateSplit( date(dt), day, mon, year );
  str = string(day:2, mon:2, string(year));
  str = StrSubst ( str, " ", "0" );
  return str;
end;


var cnt = 0, i = 0,  query = "";
if (not (GetTrue(true, "Выполнить закрытие счетов по собственным векселям при погашении?")))
  return;
end;

var ReportFileName = GetTxtFileName("CloseBillOldAcc_" + DDMMYYYY({curdate}) + ".txt");
if( Open(ReportOutFile, ReportFileName) == false )
  MsgBox("Ошибка при открытии файла |"+ ReportFileName);
  return;
end;
SetOutput(ReportFileName);
PrintHeader();
query = "SELECT T_BCID BCID FROM DVSBANNER_DBT WHERE t_Issuer = " + {OurBank} + " AND t_bcstatus = " + VSBANNER_STATUS_ENDED + " ORDER BY T_BCID";
var cmd = RSDCommand(query);

var rs = RSDRecordset (cmd, rsdval_client, rsdval_static);
cnt = SQL_GetNRecs(query);
InitProgress( cnt, "Идет закрытие счетов по собственным векселям при погашениив", "Выполняется закрытие счетов по собственным векселям при погашении" );
while (rs.movenext)
  CloseBillAccount(rs.value("bcid"), {curdate});
  i = i + 1;
  UseProgress(i);
end;
RemProgress();
PrintEnd();
println("Закрыто счетов: " + CountClosed);
println("Количество ошибок: " + CountError);
