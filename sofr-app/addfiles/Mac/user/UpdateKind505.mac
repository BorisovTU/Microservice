import rsd, secinter, "or_exl_h.mac";

PRIVATE CONST OBJTYPE_DLCONTRMSG_NPTXDUEINFO     = 505,   // Значение справочника "Вид сообщения ДБО"(1143)->"Сообщение клиенту о недоплате/переплате НДФЛ"
              OBJTYPE_DLCONTRMSG_NPTXDUEINFO_BCK = 99505; // Значение справочника "Вид сообщения ДБО"(1143)->"Сообщение клиенту о недоплате/переплате НДФЛ_отмена"

private const firstrow = 2;

private var outdir, repfilename, repfile, r = firstrow-1, succnt = 0, miscnt = 0, errcnt = 0, totalcnt = 0;
private var y, mon, d, h, m, s;

file protocol() txt write;


GetRegistryValue( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\WORKDIR", V_STRING, outdir );
DateSplit(Date(), d, mon, y);
TimeSplit(Time(), h, m, s); 

Open(protocol, outdir + "\\" + "КорректНДФЛ_Переотправка_" +string(y:4:o,".",mon:2:o,".",d:2:o) + "_" + string(h:2:o,".",m:2:o,".",s:2:o) + ".log");

private macro ProcessRow()
  var clientID, clientName, contrNumber;
  var cmd, rs;

  clientId    = repfile.GetVariant(r,0);
  clientName  = repfile.GetValue(r,1);
  contrNumber = repfile.GetValue(r,3);

  if((ValType(contrNumber) != V_STRING) OR (contrNumber == ""))
    return false;
  end;

  totalcnt = totalcnt + 1;

  cmd = RsdCommand("SELECT dlcontr.t_DlContrId "
                   "  FROM dsfcontr_dbt sfcontr, ddlcontr_dbt dlcontr "
                   " WHERE sfcontr.t_Number = ? "
                   "   AND dlcontr.t_SfContrId = sfcontr.t_Id ");
  cmd.AddParam("", RSDBP_IN, contrNumber);
  rs = RsdRecordset(cmd);

  if (not rs.MoveNext())
    Insert(protocol, "Не найден договор обслуживания " + contrNumber);
    miscnt = miscnt + 1;
    return true;
  end;

  cmd = RsdCommand("SELECT t_Id FROM ddlcontrmsg_dbt WHERE t_DlContrId = ? AND t_Kind = ?");
  cmd.AddParam("", RSDBP_IN, rs.Value("t_DlContrId"));
  cmd.AddParam("", RSDBP_IN, OBJTYPE_DLCONTRMSG_NPTXDUEINFO);
  rs = RsdRecordset(cmd);

  if (rs.MoveNext())
    execSQL("UPDATE ddlcontrmsg_dbt SET t_Kind = " + OBJTYPE_DLCONTRMSG_NPTXDUEINFO_BCK + " WHERE t_Id = " + rs.value("t_Id"));
    succnt = succnt + 1;
  else
    miscnt = miscnt + 1;
    return true;
  end;

  Insert(protocol, clientId + ";" + clientName + ";" + contrNumber);
  return true;
OnError(e)
  errcnt = errcnt + 1;
  Insert(protocol, e.Message,"(Модуль:",e.Module,", строка:",e.Line,")");
  return true;
end;

if (SelectFile(repfilename, "..\\Import\\*.xls*"))
  repfile = CDAOMSExcel();

  repfile.Open(repfilename);
  repfile.SheetChange(1);

  while (ProcessRow())
    r = r + 1;
  end;

  Insert(protocol);
  Insert(protocol, "количество успешно обработанных записей (отправленных сообщений): " + succnt);
  Insert(protocol, "количество ошибок обработки: " + errcnt);
  Insert(protocol, "количество пропущенных (необработанных) записей: " + miscnt);
  Insert(protocol, "всего записей: " + totalcnt);
end;

Close(protocol);
ViewFile(protocol);
exit(1);