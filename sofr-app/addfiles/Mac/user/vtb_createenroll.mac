/*
$Name:         vtb_createenroll.mac
$Module:       Ценные бумаги
$Description:  макрос создания операций BIQ-12207
*/

import globals, dealsinter, spInter, cb_sql, likepy, oralib, spserv;

Private var rsdErrorCount = 0, rsdErrText, rsdOraString, errStr;


PRIVATE CONST C_OPER = 1;   /*Операционист для создания операции*/
PRIVATE CONST C_TRANSF_DATE = date(24,3,2022); /*дата перевод цб */

private macro IIF(cond, val1, val2)
   if (cond) 
      return val1; 
   else 
      return val2; 
   end;
end;

private macro ByDefault(variable, value) 
   if (ValType(variable) == V_UNDEF) 
      variable = value;
   end;
   return variable;
end;

private Macro getLastRSDError ()
    return rsdErrText;
End;

private Macro myExecSQL ( sqltext, params )
    rsdErrText = "";
    if ( sqltext == NULL ) sqltext = rsdOraString; end;
    var cmd:RsdCommand = RsdCommand (sqltext), i = 0;
    cmd.NullConversion = false;

    while ( params and (i < params.size) )
        cmd.addParam (params [i].name, params [i].type, params [i].value);
        i = i + 1;
    end;

    cmd.execute ();
    return cmd;
OnError 
    if ( rsdErrorCount >= cmd.connection.environment.ErrorCount ) rsdErrorCount = 0; end;
    while ( rsdErrorCount < cmd.connection.environment.ErrorCount )
        rsdErrText = rsdErrText + IIF (rsdErrText, "\n", "") + cmd.connection.environment.error (rsdErrorCount).descr;
        rsdErrorCount = rsdErrorCount + 1;
    end;
End;


private macro GetFiidByISIN(Isin, Fiid:@integer)
   var cmd = RsdCommand("select t_fiid from davoiriss_dbt where t_isin = ?");
   cmd.AddParam("", RSDBP_IN, Isin);
   var rs = RsdRecordSet(cmd);
   if (rs.movenext())
      fiid = rs.Value(0);
      return 0;
   else
      fiid = -1;
      return 1;
   end;
end;

private macro GetCurrByISO(IsoCode, Fiid:@integer)
   var cmd = RsdCommand("select t_fiid from dfininstr_dbt where t_Iso_Number = ?");
   if(IsoCode == "810") 
      IsoCode = "643";
   end;
   cmd.AddParam("", RSDBP_IN, IsoCode);
   var rs = RsdRecordSet(cmd);
   if (rs.movenext())
      fiid = rs.Value(0);
      return 0;
   else
      fiid = -1;
      return 1;
   end;
end;

 macro UpdateRest ( BoardID, OldAmount, Amount, Scale, Point,
                    msg:@string
                    )
   private macro CheckDealForUpdate(BoardID, msg)
      return true;
   end;

   private macro UpdateLeg(BoardID)
//       if  (myExecSql ("UPDATE ddl_leg_dbt SET t_principal = :1, t_scale = :2, t_point = :3 where t_dealid = :4",
//                       makeArray (sqlParam ("1", Amount/Scale*pow(10,point)), sqlParam ("2", Scale), sqlParam ("3", Point), sqlParam ("4", BoardId))) == NULL) 
       if  (myExecSql ("UPDATE ddl_leg_dbt SET t_principal = :1 where t_dealid = :2",
                       makeArray (sqlParam ("1", Amount), sqlParam ("2", BoardID))) == NULL) 
           runError ("-");
       end;
   end;

   private macro UpdateRq(BoardID)
       if  (myExecSql ("UPDATE ddlrq_dbt SET t_amount = :1 where t_dockind = 127 and t_type = 8 and t_dealpart = 1 and t_docid = :2",
                       makeArray (sqlParam ("1", Amount),sqlParam ("2", BoardId))) == NULL) 
           runError ("-");
       end;
   end;

   private macro UpdateTrn(BoardID)
      var sql =  "  SELECT * " +
                 "    FROM dacctrn_dbt t " +
                 "   WHERE t.t_AccTrnID IN " +
                 "                  (SELECT grdoc.t_DocID " +
                 "                     FROM ddlgrdeal_dbt grdeal, ddlgrdoc_dbt grdoc " +
                 "                    WHERE     grdeal.t_DocKind = 127 " +
                 "                          AND grdeal.t_DocID = ? " +
                 "                          AND grdoc.t_GrDealID = grdeal.t_ID " +
                 "                          AND grdoc.t_DocKind = 1) " +
                 "ORDER BY t.t_acctrnid " ;
      var cmd = RsdCommand(sql);
      cmd.addParam("", RSDBP_IN, BoardID);
      var Dt = RsdRecordSet(cmd);
      if (Dt.movenext())
         var trn = RsbAccTransaction(); 
         var er =  trn.Find(Dt.Value("t_acctrnid"));
         if (er == 0)
            trn.SumPayer = Amount;
            trn.SumReceiver = Amount;
            if (not trn.Update())
               msg = "Не удалось обновить проводку";
               RunError("-"); 
            end;
         else
            msg = "Не удалось получить проводку";
            RunError("-"); 
         end;
      end;
   end;

   private macro UpdateWrtCl(BoardID)
      var sql = 
               " UPDATE dpmwrtcl_dbt set t_amount = t_amount + ? where t_id = ( "
               " SELECT c.t_id " +
               "  FROM dpmwrtcl_dbt c, ddl_tick_dbt t " +
               " WHERE     c.t_party = T.T_CLIENTID " +
               "       AND c.t_contract = T.T_CLIENTCONTRID " +
               "       AND c.t_fiid = t.t_pfi " +
               "       AND t.t_dealid = ? " +
               "       AND c.t_enddate = TO_DATE ('31129999', 'ddmmyyyy')) " ;

       if  (myExecSql (sql, makeArray (sqlParam ("1", (Amount - OldAmount) ),sqlParam ("2", BoardId))) == NULL) 
           runError ("-");
       end;
   end;

   private macro UpdateWrtSum(BoardID)
      var sql = "SELECT rq.t_id, t.t_dealid, s.* " +
                "  FROM ddlrq_dbt rq, " +
                "       ddl_tick_dbt t, " +
                "          dpmwrtsum_dbt s         " +
                " WHERE     t.t_dealid = rq.t_docid " +
                "       AND rq.t_dockind = t.t_bofficekind " +
                "       AND t.t_dealid = ? " +
                "       AND S.T_DOCID = rq.t_id " +
                "       AND s.t_dockind = 29 " +
                "       AND s.t_fiid = t.t_pfi " ;

      var cmd = RsdCommand(sql);
      cmd.addParam("", RSDBP_IN, BoardID);
      var Dt = RsdRecordSet(cmd);
      if (Dt.movenext())
         if( Dt.value("t_instance") > 0)
            msg = "По лоту уже выполнялись операции";
            RunError("-"); 
         else
            if(Dt.value("t_cost") > 0)            
               msg = "Лот с заданной стоимостью. Нельзя обновлять количество";
               RunError("-"); 
            else
               if  (myExecSql ("UPDATE dpmwrtsum_dbt SET t_amount = :1 where t_sumid = :2",
                               makeArray (sqlParam ("1", Amount),sqlParam ("2", Dt.value("t_sumid") ))) == NULL) 
                   runError ("-");
               else
                  UpdateWrtCl(BoardID);
               end;
            end;
         end;
      else
         //нечего обновлять
      end;
   end;


   private macro InTrn()
      
      UpdateLeg(BoardID);
      UpdateRq(BoardID);
      UpdateTrn(BoardID);
      UpdateWrtSum(BoardID);

   OnError ( err )

      if ( substr (err.message, strlen (err.message)) == "+" )
         msg = msg+strsubst (GetErrMsg (), "|", "\n");
      else
         msg = msg+getLastRSDError ();
      end;
      AbortTrn();
   end;

   LoopInTrn(true);
   if (CheckDealForUpdate(BoardID))  
      if (ProcessTrn(0, "InTrn"))
         return 0;
      else
         return 1;
      end;
   end;
End;


macro vtb_CreateNPTXOP (ServKind, Partyid, ContrID, ContrAccID, Sum, AcctSend, BIC, Dat, Tim, NptxOpID:@integer)
 var         stat = CreateNPTXOP( ServKind, // вид обслуживания субдоговора 1-фондовый, 21 - валютныйб 15-срочный
                               10,        //TYPE     //пока всегда 10-зачисление
                               PartyId,          //Клиент
                               ContrId,      //договор
                               ContrAccId,  //айди счета клиента
                               Sum,  
                               AcctSend,   //номер счет откуда зачисляем  
                               BIC,      //бакн откуда зачисляем
                               Dat,          //DATE
                               Tim,        //TIME
                               Null,          // RUN
                               Null,          //SPGROUND_ID
                               Null,          //SPGROUND_KIND
                               Null,          //SPGROUND_XLD
                               Null,          //SPGROUND_DATE
                               Null,          //KIND OPERATION

                               Null,          //PLACE
                               Null,          //MARKET_PLACE
                               Null,          //MARKET_SECTOR
                               Null,          //PLACE2
                               Null,          //MARKET_PLACE2
                               Null,          //MARKET_SECTOR2

                               NptxOpid      //RETURN ID
                               ); 
end;



private macro _CreateBoard (
                    DealCode,
                    DealCodeEts,
                    DealType, 
                    ClientID,
                    ContractID,
                    Amount,
                    Price,
                    Cost,
                    Nkd,
                    Cms,
                    DealDate,
                    DealTime,
                    DealDateBegin,
                    DealTimeBegin,
                    FIID,
                    DeliveringFIID,
                    PriceCur,
                    Custody,
                    Department,
                    Oper,
                    Point,
                    Portfolio,
                    ContragentID,
                    UseInTax,
                    msg:@string,
                    RetID:@integer,
                    NotCountedOnIIS
                    )
   var BoardID = 0, stat = 0;
    record BoardPrm ("BoardPrm.rec");

    private macro InTrn()
       if ( not SP_InsertBoardLot (BoardPrm, BoardId, true) )
          runError ("+");
       end;

       if ( (dealcodeEts) and (myExecSql ("update ddl_tick_dbt set t_dealcodets = :1, t_TaxOwnBegDate = :2 where t_dealid = :3", makeArray (sqlParam ("1", dealcodeEts), sqlParam ("2", DealDateBegin), sqlParam ("3", BoardId))) == NULL) )
           runError ("-");
       end;


       if ( (Price) and (myExecSql ("UPDATE ddlsum_dbt SET t_date = :1, t_sum = :2,  t_currency = :3 where t_dockind = 127 and  t_docid = :4 and t_kind = :5",
                              makeArray (sqlParam ("1", DealDateBegin), sqlParam ("2", Price), sqlParam ("3", PriceCur), sqlParam ("4", BoardId), sqlParam ("5", 1220) )) == NULL) )
           runError ("-");
       end;

       if ( (Cost) and (myExecSql ("UPDATE  ddlsum_dbt SET t_date = :1, t_sum = :2,  t_currency = :3 where t_dockind = 127 and  t_docid = :4 and t_kind = :5",
                              makeArray (sqlParam ("1", DealDateBegin), sqlParam ("2", Cost), sqlParam ("3", PriceCur), sqlParam ("4", BoardId), sqlParam ("5", 1230) )) == NULL) )
           runError ("-");
       end;

       if ( (NotCountedOnIIS) and (myExecSql ("UPDATE  ddlsum_dbt SET T_NOTCOUNTEDONIIS = :1 where t_dockind = 127 and  t_docid = :2 and t_kind = :3",
                              makeArray (sqlParam ("1", NotCountedOnIIS), sqlParam ("2", BoardId), sqlParam ("3", 1230) )) == NULL) )
           runError ("-");
       end;

       if ( (nkd) and (myExecSql ("UPDATE  ddlsum_dbt SET t_date = :1, t_sum = :2,  t_currency = :3 where t_dockind = 127 and  t_docid = :4 and t_kind = :5",
                              makeArray (sqlParam ("1", DealDateBegin), sqlParam ("2", nkd), sqlParam ("3", PriceCur), sqlParam ("4", BoardId), sqlParam ("5", 1240) )) == NULL) )
           runError ("-");
       end;

       if ( (Cms) and (myExecSql ("UPDATE  ddlsum_dbt SET t_date = :1, t_sum = :2,  t_currency = :3 where t_dockind = 127 and  t_docid = :4 and t_kind = :5",
                              makeArray (sqlParam ("1", DealDateBegin), sqlParam ("2", Cms), sqlParam ("3", PriceCur), sqlParam ("4", BoardId), sqlParam ("5", 1250) )) == NULL) )
           runError ("-");
       end;

       if ( (Cms) and (Index(dealcode, "NDFL")>0) and (myExecSql ("UPDATE  ddlsum_dbt SET t_date = :1, t_sum = :2,  t_currency = :3 where t_dockind = 127 and  t_docid = :4 and t_kind = :5",
                              makeArray (sqlParam ("1", DealDateBegin), sqlParam ("2", Cms), sqlParam ("3", 0), sqlParam ("4", BoardId), sqlParam ("5", 1250) )) == NULL) )
           runError ("-");
       end;

       if ( (PriceCur) and (myExecSql ("UPDATE  ddlsum_dbt SET t_currency = :1 where t_dockind = 127 and  t_docid = :2 and t_kind = :3",
                              makeArray ( sqlParam ("1", PriceCur), sqlParam ("2", BoardId), sqlParam ("3", 1100) )) == NULL) )
           runError ("-");
       end;

       if ( (PriceCur) and (Index(dealcode, "NDFL")>0) and (myExecSql ("UPDATE  ddlsum_dbt SET t_currency = :1 where t_dockind = 127 and  t_docid = :2 and t_kind = :3",
                              makeArray ( sqlParam ("1", 0), sqlParam ("2", BoardId), sqlParam ("3", 1100) )) == NULL) )
           runError ("-");
       end;


       if ( (Index(dealcode, "NDFL")>0) and (myExecSql ("update ddl_leg_dbt set t_supplytime = :1 where t_legkind = 0 and t_dealid = :2", makeArray (sqlParam ("1", DealTimeBegin), sqlParam ("2", BoardId))) == NULL) )
           runError ("-");
       end;

       /*
       if ( (PriceCur) and (myExecSql ("UPDATE ddl_tick_dbt SET T_PREOUTLAYFIID=:1 WHERE T_DEALID=:2",
                              makeArray ( sqlParam ("1", PriceCur), sqlParam ("2", BoardId) )) == NULL) )
           runError ("-");
       end;
       */

       /*	
       if ( outlayoth > 0 )
       	myExecSql ("insert into ddlcomis_Dbt( T_DOCKIND, T_DOCID, T_CONTRACT, T_FEETYPE, T_COMNUMBER, T_SUM, T_NDS, T_DATE, T_PLANPAYDATE, T_FACTPAYDATE, T_INPUTMEDIUM, T_SERVOPERID, 	T_VERSION, T_RECEIVERID, T_PARENT) " +
       		   " values( 101, :1 , -1, 1 , 60, :2 , 0, :3 , :4 , :5 , 'П', 0, 0, -1, 0) ",
       	makeArray (sqlParam ("1", BoardId), sqlParam ("2", outlayoth), sqlParam ("3", dealDate), sqlParam ("4", dealDate), sqlParam ("5", dealDate)));
       end;                                       
       */
       msg = string("ID операции: "+BoardID);
       RetID = BoardID;

   OnError ( err )

       if ( substr (err.message, strlen (err.message)) == "+" )
           msg = strsubst (GetErrMsg (), "|", "\n");
       else
           msg = getLastRSDError ();
       end;
       AbortTrn();
    end;

    BoardPrm.DealType           = DealType; //2011;
    BoardPrm.DealCode           = DealCode;
    BoardPrm.Client             = IIF (ClientID > 0, ClientID, {OurBank});
    BoardPrm.ClientContr        = IIF (ContractId > 0, ContractId, 0);
    BoardPrm.Amount             = Amount;
    BoardPrm.Price              = price;
    BoardPrm.Cost               = Cost;
    BoardPrm.Nkd                = nkd;
    BoardPrm.DealDate           = dealDate;

    if ( ClientID <= 0 )
        BoardPrm.Begbonusdate       = dealdate;
    end;
    if ( valType (dealTime) == V_TIME )
        BoardPrm.DealTime           = dealTime;
    end;
    BoardPrm.PreOutlay          = 0;
    BoardPrm.Fiid               = FIID;
    BoardPrm.DeliveringFIID     = DeliveringFIID;
    BoardPrm.priceFi            = priceCur;
    If(Index(BoardPrm.DealCode, "NDFL") == 0)
    BoardPrm.PreOutlayFiid      = priceCur;
    else
       BoardPrm.PreOutlayFiid      = 0;
    end;
    BoardPrm.Custody            = Custody;
    BoardPrm.DepoClientCustody  = {ourBank};
    BoardPrm.DepoContrCustody   = Custody;
    BoardPrm.Department         = 1;
    BoardPrm.Oper               = Oper;
    BoardPrm.DealState          = DL_PREPARING;
    if ( (BoardPrm.DealType == 32011) or (BoardPrm.DealType == 2011))
        BoardPrm.Outlay = Cms;
        BoardPrm.LastOverDate =  dealdate;
        BoardPrm.BegDate = dealdateBegin;
    end;
    BoardPrm.Point              = point;
    BoardPrm.Portfolio          = portfolio;
    BoardPrm.Contragent         = ContragentID;

//    BoardPrm.UseInTax = "X";
    If(Index(BoardPrm.DealCode, "NDFL") == 0)
       BoardPrm.UseInTax = UseInTax;
    else
       BoardPrm.UseInTax = "X";
    end;

/*    BoardPrm.TaxBegDate = DealDate;
    BoardPrm.TaxBegTime = DealTime;
    BoardPrm.TaxPrice = Price;
    BoardPrm.TaxCost = Amount;
    BoardPrm.Nkd = Nkd;
    BoardPrm.TaxOutLay = Cms;*/
/*
     BoardPrm.OverAmount         = overAmount;
     BoardPrm.ReservAmount	 = reservAmount;
     BoardPrm.BegDiscount	 = begDiscount;
     BoardPrm.BegBonus		 = begBonus;
     BoardPrm.BegDiscountDate	 = begDiscountDate;
     BoardPrm.BegBonusDate	 = begBonusDate;
     BoardPrm.InterestIncome	 = InterestIncome;
     BoardPrm.DiscountIncome	 = DiscountIncome;
     BoardPrm.RecalcDate         = recalcDate;
     BoardPrm.OldBegDiscount	 = OldBegDiscount;
     BoardPrm.NotCarryDiscount	 = NotCarryDiscount;
     BoardPrm.OldBegBonus	 = OldBegBonus;
     BoardPrm.NotWrtBonus	 = NotWrtBonus;
 BoardPrm.BegDate            = OwnerBegDate;
*/


     
   if (ProcessTrn(0, "InTrn"))
      return 0;
   else
      return 1;
   end;

End;



macro VTB_CreateBoardByRec ( RecID /*ID записи из u_vtb_lots_dbt*/, IsSingle, msg:@string)

   var sql = "";
   var cmd, stat = 0;

   var 
      DealCode    = "",    
      DealCodeEts = "", 
      DealType    = 2011,
      ClientID    = -1,
      ContractID  = -1,
      Amount      = $0.0,
      Price       = $0.0,
      Cost        = $0.0,
      nkd         = $0.0,
      Cms         = $0.0,
      DealDate    = date(0,0,0),
      DealTime    = time(0,0,0),
      DealDateBegin= date(0,0,0),
      DealTimeBegin= time(0,0,0),
      FIID        = -1,
      DeliveringFIID = -1,
      PriceCur    = 0,       
      Custody     = 4,
      Department  = 1,
      Oper        = 1,   
      Point       = 6,  
      Portfolio   = -1,
      ContragentID= -1,
      UseInTax    = "";

   msg  = "";

   sql = " select t.*,   " +
         " c.t_code_vtb, sf.t_id t_clientcontrid, sf.t_partyid clientid, sf.t_datebegin, " +
         " t_state, t_comment                                                                     " +
         " from u_vtb_lots_dbt t                                                                  " +
         "      left join USR_CONTRACT_MATCH c on t.t_agrno = c.t_contract_number_vtb             " +
         "      left join ddlcontr_dbt d on d.t_dlcontrid = c.t_dlid                                " +
         "           join ddlcontrmp_dbt mp on d.t_dlcontrid = mp.t_dlcontrid and mp.t_marketid =  " +
         "                case when t.t_stor = 'НРД' then 2 when t.t_stor = 'БЭБ' then 151337 else -99 end  "
         "           join dsfcontr_dbt sf on sf.t_id = mp.t_sfcontrid and sf.t_servkind = 1 and sf.t_servkindsub = 8 " +
         " where t.t_id = " + string(RecID);
      

   var Dt = TrsbDataSet(sql);

   if (Dt.movenext())

      DealCode    = dt.operno;
      DealCodeEts = dt.operno;   
      DealType    = 2011;  
      ClientID    = Dt.ClientID;  
      ContractID  = Dt.ClientContrID;
      Amount      = Dt.Qty;  
      Price       = Dt.Price;  
      Cost        = Dt.Amount;  
      nkd         = Dt.Aci;
      Cms         = Dt.Cms;
      DealDate    = IIF(sql_ConvTypeDate(Dt.datebegin) > C_TRANSF_DATE, sql_ConvTypeDate(Dt.datebegin), C_TRANSF_DATE);
      DealTime    = time(Dt.DatePos);
      DealDateBegin = date(Dt.DatePos);
      DealTimeBegin = time(Dt.DatePos);
      if ( GetFiidByISIN(Dt.Isin, @Fiid) != 0 )
         msg = "|Не удалось получить бумагу";
         stat = 1;
      end;
      DeliveringFIID = Fiid;
      if ( GetCurrByISO(Dt.Curr, @PriceCur ) != 0)
         msg = msg+"|Не удалось получить валюту";
         stat = 1;
      end;

      if (Dt.Stor == "БЭБ")
         Custody = GetCodeParty("00#Б#239654",1); //1450;
      elif (Dt.Stor == "НРД")
         Custody = GetCodeParty("НРД",1); //4;
      else
         msg = msg+"|Неизвестное место хранения: " + Dt.Stor;
         stat = 1;
      end;
      Department = 1;    
      Oper = C_OPER;          
      Point = 6;        
      Portfolio = 0;    
      ContragentID = -1;  

      if (not stat)
         if (IsSingle)
            SP_BeginInsertBoardLot ();
         end;

         stat = _CreateBoard (
                          DealCode,
                          DealCodeEts,
                          DealType, 
                          ClientID,
                          ContractID,
                          Amount,
                          Price,
                          Cost,
                          nkd,
                          Cms,
                          DealDate,
                          DealTime,
                          DealDateBegin,
                          DealTimeBegin,
                          FIID,
                          DeliveringFIID,
                          PriceCur,
                          Custody,
                          Department,
                          Oper,
                          Point,
                          Portfolio,
                          ContragentID,
                          UseInTax,
                          @msg
                          );
         if (IsSingle)
            SP_EndInsertBoardLot ();
         end;
      end;
   else
      msg  = "Не найден соответствующий субдоговор для t_id = " + RecID;
      stat = 1;
   end;
   return stat;
end;

macro VTB_CreateBoardByDlcontr(dlid, log:@string)
   private macro SetState(Id, State, Comment)
     var rs = RsdCommand    ( " BEGIN update u_vtb_lots_dbt         " +
                                               "set t_state = ?, t_comment = ? "+
                                               " where T_ID = ?; COMMIT; END; ");
     rs.AddParam ("", RSDBP_IN, State);          
     rs.AddParam ("", RSDBP_IN, Comment);          
     rs.AddParam ("", RSDBP_IN, Id); 
     rs.Execute;
   end;

   var msg = "", flag = 0;
   var sql = " select t.*,   " +
         " c.t_code_vtb, sf.t_id t_clientcontrid, sf.t_partyid clientid, sf.t_datebegin, " +
         " t_state, t_comment                                                                     " +
         " from u_vtb_lots_dbt t                                                                  " +
         "      left join USR_CONTRACT_MATCH c on t.t_agrno = c.t_contract_number_vtb             " +
         "      left join ddlcontr_dbt d on d.t_dlcontrid = c.t_dlid                                " +
         "           join ddlcontrmp_dbt mp on d.t_dlcontrid = mp.t_dlcontrid and mp.t_marketid =  " +
         "                case when t.t_stor = 'НРД' then 2 when t.t_stor = 'БЭБ' then 151337 else -99 end  "
         "           join dsfcontr_dbt sf on sf.t_id = mp.t_sfcontrid and sf.t_servkind = 1 and sf.t_servkindsub = 8 " +
         " where t.t_state != 'Обработано' and c.t_dlid = " + string(dlid);

   var Dt = TrsbDataSet(sql);
   log = "";
   while (Dt.movenext())
      if (flag == 0)
         SP_BeginInsertBoardLot ();
         flag = 1;
      end;
      msg = "";
      if ( VTB_CreateBoardByRec ( dt.id, false, @msg) != 0 )
         log = log+"Не создана "+Dt.operno+": " + msg+"|";
         if (index(msg, "же существует операция") > 0)
            SetState(dt.id, "Обработано", msg);
         else
            SetState(dt.id, "Ошибка", msg);
         end;
      else
         log = log+"Cоздана "+Dt.operno+": " + msg+"|";
         SetState(dt.id, "Обработано", msg);
      end;
   end;
   if (flag == 1)
      SP_EndInsertBoardLot ();
   end;

   if (log == "")
      log = "Нет остатков ВТБ для обработки";
   end;
//   msgbox(log);
end;

macro _CreateRet(
                    DealCode,
                    DealCodeEts,
                    DealType, 
                    ClientID,
                    ContractID,
                    Amount,
                    Price,
                    Cost,
                    Nkd,
                    Cms,
                    DealDate,
                    DealTime,
                    DealDateBegin,
                    DealTimeBegin,
                    FIID,
                    DeliveringFIID,
                    PriceCur,
                    Custody,
                    Department,
                    Oper,
                    Point,
                    Portfolio,
                    ContragentID,
                    msg:@string,
                    RetID:@integer
)

   var tick = TRecHandler( "dl_tick" );
   var leg = TRecHandler( "dl_leg" );

  InitSPDeal(tick, DL_RETIREMENT , DealType, true);

  tick.rec.DealType = DealType;

  tick.rec.PartyID         = GetCodeParty("НРД",1); //4;; 
  tick.rec.marketid        = GetCodeParty("НРД",1); //4;;
  leg.rec.registrar        = GetCodeParty("НРД",1); //4;; 
  tick.rec.PortfolioID     = 0;
  tick.rec.ClientID        = ClientID;
  tick.rec.Department      = Department;     
  tick.rec.Oper            = Oper;
  tick.rec.DealDate        = DealDate;
  tick.rec.DealTime        = DealTime;
  tick.rec.DealCode        = DealCode;
  tick.rec.DealCodeTS      = DealCodeETS;
  tick.rec.ClientContrID   = ContractID;

  leg.rec.PFI = leg.rec.DeliveringFIID = FIID;

  leg.rec.Principal    = Amount;
  leg.rec.Cost         = Cost;

    leg.rec.Expiry = DealDate;
    leg.rec.Maturity = DealDate;
    leg.rec.Start = DealDate;

  leg.rec.SupplyTime     = DealTime;
  leg.rec.CFI            = PriceCur;
  leg.rec.PayFIID        = PriceCur;
  leg.rec.Scale          = 1;          
  leg.rec.Point          = 6;          
  leg.rec.TotalCost = Cost + NKD;
  leg.rec.price          = price;


   var stat = sp_createdeal(tick, leg, null,null,null,null,null,true);
   if (not stat)
      RetID =  tick.rec.dealid;
      msg = "";
      return 0;
   else
      msg = geterrmsg();
      return 1;
   end;
end;

macro CreateDepoBoard (_DealCode,
                       InDate,
                       InTime,
                       Client,
                       ContrID,
                       _Fiid,
                       _Amount,
                       Prec,
                       Custod,
                       IsSingle,
                       OperType,
                       _UseInTax,
                       _price,
                       _priceCur,
                       _nkd,
                       _cms,
                       msg:@string,
                       BoardID:@integer
                        )


   var sql = "";
   var cmd, stat = 0;

   var 
      DealCode    = _DealCode,    
      DealCodeEts = "", 
      DealType    = 2011,
      ClientID    = Client,
      ContractID  = ContrID,
      Amount      = _Amount,
      Price       = ByDefault(_price, $0.0),
      Cost        = $0.0,
      nkd         = ByDefault(_nkd, $0.0),
      Cms         = ByDefault(_cms, $0.0),
      DealDate    = InDate,
      DealTime    = InTime,
      DealDateBegin= date(0,0,0),
      DealTimeBegin= time(0,0,0),
      FIID        = _Fiid,
      DeliveringFIID = _Fiid,
      PriceCur    = ByDefault(_priceCur, -1),       
      Custody     = Custod,
      Department  = 1,
      Oper        = 1,   
      Point       = 6,  
      Portfolio   = -1,
      ContragentID= -1,
      UseInTax    = _UseInTax;

   msg  = "";

   if (PriceCur == -1)
      cmd = TRsbDataSet(" select s.t_fiid from DSETTACC_DBT s, dsfssi_dbt f where F.T_SETACCID = S.T_SETTACCID and F.T_OBJECTID = lpad("+string(ContrID)+",10,'0') and F.T_OBJECTTYPE = 659 order by t_fiid");
      if (cmd.movenext())
         PriceCur = cmd.Fiid;
      else
         PriceCur = 0;
      end;
   end;

   DealCodeEts = DealCode;   
   DealType    = 2011;  
   DealDateBegin = DealDate;
   DealTimeBegin = DealTime;
   Department = 1;    
   Oper = C_OPER;          
   Point = Prec;        
   Portfolio = 0;    
   ContragentID = -1;  

   If(OperType == 2)
      DealType = 2010;
   elif(OperType == 3) 
      DealType = 12021;

      cmd = RsdCommand(" select RSB_FIINSTR.FI_GETNOMINALONDATE(? , ? ) nom   from dual ") ;
      cmd.addParam("",RSDBP_IN, FIID);
      cmd.addParam("",RSDBP_IN, DealDate);
      cmd = RsdRecordSet(cmd);
      if (cmd.movenext())
         Cost = Amount * cmd.Value("nom");
      end;
   else
      DealType = 2011;
   end;

   if (not stat)
      if (OperType == 3)
         stat = _CreateRet (
                          DealCode,
                          DealCodeEts,
                          DealType, 
                          ClientID,
                          ContractID,
                          Amount,
                          Price,
                          Cost,
                          nkd,
                          Cms,
                          DealDate,
                          DealTime,
                          DealDateBegin,
                          DealTimeBegin,
                          FIID,
                          DeliveringFIID,
                          PriceCur,
                          Custody,
                          Department,
                          Oper,
                          Point,
                          Portfolio,
                          ContragentID,
                          @msg, @BoardID
                          );
      else
         if (IsSingle)
            SP_BeginInsertBoardLot ();
         end;

         stat = _CreateBoard (
                          DealCode,
                          DealCodeEts,
                          DealType, 
                          ClientID,
                          ContractID,
                          Amount,
                          Price,
                          Cost,
                          nkd,
                          Cms,
                          DealDate,
                          DealTime,
                          DealDateBegin,
                          DealTimeBegin,
                          FIID,
                          DeliveringFIID,
                          PriceCur,
                          Custody,
                          Department,
                          Oper,
                          Point,
                          Portfolio,
                          ContragentID,
                          UseInTax,
                          @msg, @BoardID
                          );
         if (IsSingle)
            SP_EndInsertBoardLot ();
         end;
      end;
   end;

   return stat;
end;

macro CreateDepoBoardWithPrice (
                                   _DealCode, InDate, InTime,
                                   Client,
                                   ContrID,
                                   _Fiid,
                                   _Amount, Prec,
                                   Custod,
                                   IsSingle, OperType,  CurFiid, _Price, _Cost, ACI, CmsRur, BegDealDate,
                                   msg:@string, BoardID:@integer, NotCountedOnIIS
                                    )


   var sql = "";
   var cmd, stat = 0;
   var 
      DealCode    = _DealCode,    
      DealCodeEts = "", 
      DealType    = 0,
      ClientID    = Client,
      ContractID  = ContrID,
      Amount      = _Amount,
      Price       = _Price,
      Cost        = _Cost,
      nkd         = ACI,
      Cms         = CmsRur,
      DealDate    = InDate,
      DealTime    = InTime,
      DealDateBegin= BegDealDate,
      DealTimeBegin= time(0,0,0),
      FIID        = _Fiid,
      DeliveringFIID = _Fiid,
      PriceCur    = CurFiid,       
      Custody     = Custod,
      Department  = 1,
      Oper        = 1,   
      Point       = 6,  
      Portfolio   = -1,
      ContragentID= -1,
      UseInTax    = "X";

   msg  = "";

//   PriceCur = 0;

   DealCodeEts = DealCode;   
//   DealDateBegin = DealDate;
   DealTimeBegin = DealTime;
   Department = 1;    
   Oper = C_OPER;          
   Point = Prec;        
   Portfolio = 0;    
   ContragentID = -1;  

   If(OperType == 2)
      DealType = 2010;
   else
      DealType = 32011;
   end;

   if (not stat)
      if (IsSingle)
         SP_BeginInsertBoardLot ();
      end;

      stat = _CreateBoard (
                       DealCode,
                       DealCodeEts,
                       DealType, 
                       ClientID,
                       ContractID,
                       Amount,
                       Price,
                       Cost,
                       nkd,
                       Cms,
                       DealDate,
                       DealTime,
                       DealDateBegin,
                       DealTimeBegin,
                       FIID,
                       DeliveringFIID,
                       PriceCur,
                       Custody,
                       Department,
                       Oper,
                       Point,
                       Portfolio,
                       ContragentID,
                       UseInTax,
                       @msg, @BoardID,
                       NotCountedOnIIS
                       );
      if (IsSingle)
         SP_EndInsertBoardLot ();
      end;
   end;

   return stat;
end;



//VTB_CreateBoardByRec(24, true);