/*----------------------------------------------------------*/
/* vtb_LoadContract.mac
   Соответствие кодов ВТБ и ЦФТ - загрузка из *.csv         
   Формат данных:
   gkk|agreement|client_id|party_name|party_name_long|lastName|firstName|middleName|nature|iia|firstIIAAgreementDate|firstIIAAgreement|iiaMoneyAmount|iiaCloseDate|contract_date|
   (первые 15 столбцов)                                     */
/*                                                          */
/* Автор: Гераськина Т.                                     */
/*----------------------------------------------------------*/
import rsd, rsexts;
import "usr_open_files.mac";
import BankInter;

private class Logger()

    macro RepHeader(dt, tm)
       var str;
       str =     "                             ОТЧЕТ о загрузке соглашений \n";
       str = str+"\n";
       str = str+"Дата загрузки  : "+dt+"\n";
       str = str+"Время загрузки : "+tm+"\n";
       str = str+"┌─┬──────────────────┬───────────────────┬───┬────────────────────┬───────────────┬─────────────────────────────────────────────────────────────────────────────────────────┐\n";
       str = str+"│ │Код ВТБ           │Соглашение         │ИИС│ Дата договора      │Статус загрузки│Комментарий                                                                              │\n";
       str = str+"├─┼──────────────────┼───────────────────┼───┼────────────────────┼───────────────┼─────────────────────────────────────────────────────────────────────────────────────────┤";
       println(str);
    end;

    macro RepFooter(suc, er, tot)
       var str = "└─┴──────────────────┴───────────────────┴───┴────────────────────┴───────────────┴─────────────────────────────────────────────────────────────────────────────────────────┘\n";
       str = str+"Загружено успешно: "+suc+"\n";
       str = str+"Не загружено     : "+er+"\n";
       str = str+"Всего            : "+tot+"\n";
      println(str);
    end;

    macro RepStr(_err,_code, _ca, _iis, _dt, _stat,_comm)
        var status;
        if (_stat == false)
            status = "Загружено";
        else
            status = "Не загружено";
        end;
        var str = "│"+string(_err  :1)   +"│"+
                  string(_code     :18:l)+"│"+
                  string(_ca       :19:l)+"│"+
                  string(_iis      :3:l) +"│"+
                  string(_dt       :20:l)+"│"+
                  string(status    :15:l)+"│"+
                  string(_comm          );
       return println(str);
    end;
end;

// получение sql-ошибки. 
// использовать в onError(e) так:  RunError(e.message + getFullCmdErrorText(cmd));
private macro getFullCmdErrorText(_cmd:RSDCommand)
   var cmd = _cmd;
   var err_str = "", i = 0;

   while( i < cmd.connection.environment.ErrorCount)
      err_str = err_str + cmd.connection.environment.Error(i).Descr + " ";
      i = i+1;
   end;

   return trim(err_str);
end;

private macro InsRec(_code_abs, _contract_number, _iis, _contract_date)
   var sql_str, cmd, rs;
   var res = "OK";

   sql_str = "declare "+
             "    v_Err varchar2(2000);"+
             " begin "+
             "    begin "+
             "       insert into usr_contract_match (t_code_vtb, t_iis, t_contract_number_vtb,t_contract_date,t_dlid) "+
             "       values (:code_abs, :iis, :contract_number, :contract_date, 0);"+
             "       v_Err := 'OK'; "+
             "    exception "+
             "       when DUP_VAL_ON_INDEX then v_Err := 'Дублирование: код АБС и признак ИИС '; "+
             "       when OTHERS then v_Err := 'Ошибка вставки: '|| SQLERRM; "+
             "    end; "+
             "    :p_Err := v_Err; "+
             "end; ";
             
   cmd = RSDCommand(sql_str);
   cmd.AddParam("code_abs", RSDBP_IN, _code_abs);
   cmd.AddParam("iis", RSDBP_IN, _iis);
   cmd.AddParam("contract_number", RSDBP_IN, _contract_number);
   cmd.AddParam("contract_date", RSDBP_IN, _contract_date);
   cmd.AddParam("p_Err", RSDBP_OUT, v_string, 2000);
   cmd.execute;
   if ((cmd.Value("p_Err") != null) and (cmd.Value("p_Err") != nullval))
      res = cmd.Value("p_Err");
   end;

   return res;
onError(e)
   return e.message + getFullCmdErrorText(cmd);
end;

macro runLoadContract(errp:@string, result_protocol:@variant)
   var ErrStr = "",ErrStat = false;
   var row = 0;
   var suc = 0, err = 0;
   var log = Logger();
   var dt = StrSubSt(string(Date()),".","")+StrSubSt(string(Time()),":","");
   var InfoLogFileName = GetTxtFileName( "load_contract_"+dt);

   var cur_file_mask = "*.csv";
   file datafile() txt;
   SetDelim("|");
      
   var c_file = c_txt_file();
   var FullFileName;
   var stat;
   
   var sql_str, cmd, rs;
   var code_abs,iis,contract_number, contract_date;
   var index_code, index_number, index_iis, index_date;

   stat = c_file.openFile(datafile, cur_file_mask, true, false, FullFileName, "rsansi");

   if ( (valtype(stat) == V_UNDEF) or (stat == false) )
      errp = "Ошибка открытия файла";
      return false;
   end;

   /*Открываем файл*/
   BegAction(1, "Обработка файла \"" + c_file.localFileName + "\". Ждите...");

   /* формат файла, первая строка - всегда заголовок, данные со второй строки
1	ГКК Глобальный код клиента"	gkk	BIGINT 	Да	253629
2	Номер соглашения (БО и ИИС)	agreement	VARCHAR (18)	Да	104VNY
3	"Код субпозиции Номера субпозиций / субсчетов клиентов в формате CHAR(8) через запятую"	client_id	VARCHAR (500)	Да	23174007,25802000,35802000
4	ФИО(сокр)	party_name	VARCHAR (255)	Да	Сидоров И.П.
5	ФИО полностью	party_name_long	VARCHAR (255)	Да	Сидоров Иван Петрович
6	Фамилия 	lastName	VARCHAR (50)	Да	Сидоров
7	Имя 	firstName	VARCHAR (50)	Да	Иван
8	Отчество	middleName	VARCHAR (50)	Да	Петрович
9	Тип субъекта (физ.лицо/юр. лицо и тд) физ.лицо=1 юр. лицо=2	nature	INT	Да	1
10	Признак договора БО и ИИС - 0- БО, 1 - ИИС	iia	BIT	Да	0
11	Дата заключения первого договора на ведение ИИС, если ИИС был переведён к контрагенту	firstIIAAgreementDate	DATE	Да (для тех, кто с переходом)	2019-09-03
12	Номер первого договора на ведение ИИС, если ИИС был переведён к контрагенту	firstIIAAgreement	VARCHAR (100)	Да (для тех, кто с переходом)	659080/19-м-иис
13	Сумма внесённых на ИИС денежных средств в 2022 году	iiaMoneyAmount	money	Да (для тех, кто с переходом)	30000,0245
14	Плановая дата закрытия ИИС у контагента	iiaCloseDate			
15	Дата начала договора	contract_date	DATETIME	Да	2019-03-29 00:00:00.000
...
     */

   // заголовка нет
   /*next(datafile);

   if (  (StrUpr(datafile(0)) == StrUpr("gkk")) and
         (StrUpr(datafile(1)) == StrUpr("agreement"))  )
   else 
      EndAction(1);
      errp = "Файл имеет некорректный заголовок, корректный: gkk|agreement|client_id|party_name|...";
      return false;
   end;              */

   SetOutput( InfoLogFileName, true ); //Пишем лог в файл. Начало
   log.RepHeader(date(),time());
   
   // очищать таблицу не будем, так как возможны частичные загрузки
   /*sql_str = "truncate table usr_contract_match";
   cmd = RSDCommand(sql_str);
   cmd.execute;
   cmd.close;
   cmd = null;*/

   row = 1;
   index_code = 0;
   index_number = 1;
   index_iis = 13;
   index_date = 17;

   //Бежим по файлу
   InitProgress(-1);
   while (next(datafile))
      if (StrLen(trim(datafile.str)) == 0)
         continue;
      end;

      code_abs = StrSubst(datafile(index_code),"\"","");
      contract_number = StrSubst(datafile(index_number),"\"","");
      iis = StrSubst(datafile(index_iis),"\"","");
      contract_date = StrSubst(datafile(index_date),"\"","");

      
      if (StrLen(code_abs) == 0)
         ErrStat = true;
         ErrStr = "Не задан код клиента, строка "+row;
         log.RepStr("X",code_abs,contract_number,iis,contract_date,ErrStat,ErrStr);
         row = row + 1;
         err = err + 1;
         continue;
      end;
      
      if (StrLen(contract_number) == 0)
         ErrStat = true;
         ErrStr = "Не задан номер договора, строка "+row;
         log.RepStr("X",code_abs,contract_number,iis,contract_date,ErrStat,ErrStr);
         row = row + 1;
         err = err + 1;
         continue;
      end;
      
      if (StrLen(iis) == 0)
         ErrStat = true;
         ErrStr = "Не задан признак ИИС, строка "+row;
         log.RepStr("X",code_abs,contract_number,iis,contract_date,ErrStat,ErrStr);
         row = row + 1;
         err = err + 1;
         continue;
      end;
      
      if (StrLen(contract_date) == 0)
         ErrStat = true;
         ErrStr = "Не задана дата договора, строка "+row;
         log.RepStr("X",code_abs,contract_number,iis,contract_date,ErrStat,ErrStr);
         row = row + 1;
         err = err + 1;
         continue;
      end;
      
      stat = InsRec(code_abs,contract_number,iis,contract_date);

      if (stat == "OK")
         ErrStat = false;
         ErrStr = "";
         log.RepStr("",code_abs,contract_number,iis,contract_date,ErrStat,ErrStr);
         suc = suc + 1;
      else 
         ErrStat = true;
         ErrStr = stat;
         log.RepStr("X",code_abs,contract_number,iis,contract_date,ErrStat,ErrStr);
         row = row + 1;
         err = err + 1;
         continue;
      end;

      row = row+1;
      UseProgress(row);
      ErrStr = "";
      ErrStat = false;
   end;
   RemProgress();

   log.RepFooter(suc,err,suc+err);
   SetOutput( NULL, true ); //Пишем лог в файл. Конец

   c_file.closeFile(datafile);
   ViewFile( InfoLogFileName );

   errp = "";
   result_protocol.addString("Обработано " + row + " строк " );
   return true;


end;

//runLoadContract();