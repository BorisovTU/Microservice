// CCBO-722 Решение проблемы с дублями договоров
// CloseExtraContracts.mac
// Гераськина Т.В.
import rsd, globals, DealsInter, XmlRpcInter;
import dl_activex;
import "func_lib.mac";

private const COL_NUMBER      = 3,
              COL_PARTYID     = 1,
              COL_PARTYCODE   = 2,
              COL_DATE        = 4,
              COL_EKK         = 5,
              COL_STATE       = 7;
var gg =  CreateGUID();

// получение sql-ошибки. 
// использовать в onError(e) так:  RunError(e.message + getFullCmdErrorText(cmd));
private macro getFullCmdErrorText(_cmd:RSDCommand)
   var cmd = _cmd;
   var err_str = "", i = 0;

   while( i < cmd.connection.environment.ErrorCount)
      err_str = err_str + cmd.connection.environment.Error(i).Descr + " ";
      i = i+1;
   end;

   return trim(err_str);
end;


class Logger()
    macro RepHeader(dt, tm)
       var str;
       str =     "                             ОТЧЕТ о техническом закрытии договоров \n";
       str = str+"\n";
       str = str+"Дата обработки  : "+dt+"\n";
       str = str+"Время обработки : "+tm+"\n";
       str = str+"┌─┬──────────────────┬───────────────┬──────────────┬─────────────────────────────────────────────────────────────────────────────────────────┐\n";
       str = str+"│ │Договор           │Клиент         │Дата открытия │Комментарий                                                                              │\n";
       str = str+"├─┼──────────────────┼───────────────┼──────────────┼─────────────────────────────────────────────────────────────────────────────────────────┤";
       println(str);
    end;

    macro RepFooter(suc, er, tot)
       var str = "└─┴──────────────────┴───────────────┴──────────────┴─────────────────────────────────────────────────────────────────────────────────────────┘\n";
       str = str+time()+"\n";
       str = str+"Обработано успешно: "+suc+"\n";
       str = str+"Не обработано     : "+er+"\n";
       str = str+"Всего             : "+tot+"\n";
      println(str);
    end;

    macro RepStr(_err,_num, _cl, _dt,_comm)
        var str = "│"+string(_err :1)   +"│"+
                      string(_num :18:l)+"│"+
                      string(_cl  :15:l)+"│"+
                      string(_dt  :14:l)+"│"+
                      string(_comm   ) ;
       return println(str);
    end;

end;

private macro CheckCreateTable()
   var sql = "declare " +
        " retval number := 0; " +
        " sql_crt varchar2(4000); " +
        "begin " +
        "sql_crt := ' create table usr_extra_contracts ( "+
        " t_dlcontrid number(10), "+
        " t_partyid number(10), "+
        " t_partycode varchar2(35 char), "+
        " t_contractnumber varchar2(20 char), "+
        " t_contractdate date, "+
        " t_ekk  varchar2(35 char), "+
        " t_state varchar2(30 char), "+
        " t_errortext varchar2(200), "+
        " t_guid varchar2(200) "+
        " )'; "+
        "select count(1) into retval from user_tables where upper (table_name) = upper('usr_extra_contracts'); " +
        "if retval = 0 then " +
        "  execute immediate (sql_crt); " +
        "  execute immediate 'CREATE INDEX usr_extra_contracts_IDX0 ON usr_extra_contracts (t_guid) '; "+
        "end if;  " +
        "end; " ;
   var cmd = RSDCommand(sql);
   cmd.execute;
   cmd.close;
onError(err)
      var ErrText = err.message + getFullCmdErrorText(cmd);
      RunError(ErrText);

end;

private macro FindContract(_PartyId, _ContractNumber)
   var res = -1;
   var sql_str, rs, cmd;
   
   sql_str = "select dl.t_dlcontrid "+
             "  from ddlcontr_dbt dl, dsfcontr_dbt sf "+
             " where dl.t_sfcontrid = sf.t_id "+
             "   and sf.t_number = :n "+
             "   and sf.t_partyid = :p ";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("n", RSDBP_IN, _ContractNumber);
   cmd.AddParam("p", RSDBP_IN, _PartyId);
   rs = RSDRecordSet(cmd);
   if (rs.movenext)
      res = rs.value("t_dlcontrid");
   end;
   
   return res;
onError(err)
      var ErrText = err.message + getFullCmdErrorText(cmd);
      RunError(ErrText);

end;

//Заполняется буферная таблица данными из Excel
macro FillServTable()
   var DlContrID,
       PartyId,
       PartyCode,
       ContractNumber,
       ContractDate,
       EKK,
       State,
       ErrorText = "";
   
   var FullNameFile = "";
   var FileName = "";
   var TermPath = GetCurDir(true) + "\\TxtFile";
   var DirName = "";
   var ExtName = "";
   var frow = 1;
   var row = 1;
   var sheet, v_state=true;
   
   var sql_str, cmd, rs;

   /*Выбираем файл для обработки*/
   if(not SelectFile(FullNameFile, "$C:\\*.xls*", "Выберите файл для загрузки", 0, true))
      MsgBox("Отказ от загрузки");
      return 0;
   end;

   //Отделяем путь файла от имени
   splitfile(FullNameFile, FileName, ExtName);
   FileName = FileName + ExtName;

   DirName = substr(FullNameFile, 1, index(FullNameFile, FileName) - 1);

   if (not copyfile("$" + DirName + FileName, "$" + TermPath + "\\" + FileName) )
      MsgBox("Ошибка при передаче файла на терминал");
   end;

   /*Открываем файл*/
   BegAction(1, "Обработка файла \"" + TermPath + "\\" + FileName + "\". Ждите...");

   if(not OpenExcelFile(FileName, false, true, TermPath))
      MsgBox("Ошибка открытия файла \"" + TermPath + "\\" + FileName + "\"");
      EndAction(1);
      return 0;
   end;

   sheet = ExcelApplication.Sheets(1);

   /*Ищем первую строку*/
   while(frow <= 10)
      if(valtype(sheet.cells(frow, COL_NUMBER).value) != V_UNDEF)
         break;
      end;
      frow = frow + 1;
   end;

   row = frow+1;
   //Бежим по файлу
   InitProgress(-1);
   while (v_state) 
      if (valtype(sheet.cells(row, COL_NUMBER).value) == V_UNDEF)
         v_state = false;
         continue;
      else
         PartyId = int(sheet.cells(row, COL_PARTYID).value);
         PartyCode = sheet.cells(row, COL_PARTYCODE).value;
         ContractNumber = String(sheet.cells(row, COL_NUMBER).value);
         ContractDate = sheet.cells(row, COL_DATE).value;
         EKK = sheet.cells(row, COL_EKK).value;
         State = sheet.cells(row, COL_STATE).value;
         UseProgress(row);
      end;

      DlContrID = FindContract(PartyId, ContractNumber);

      if (DlContrID == -1)
         ErrorText = "Не найден договор "+ContractNumber+" по клиенту "+PartyCode;
      end;
      
      sql_str = "insert into usr_extra_contracts "+
                " (t_dlcontrid, t_partyid, t_partycode, t_contractnumber, t_contractdate, "+
                " t_ekk, t_state, t_errortext, t_guid ) "+
                " values (:d, :p, :pc, :cn, :cd, :e, :s, :er, :g) ";
      cmd = RSDCommand(sql_str);
      cmd.AddParam("d", RSDBP_IN, DlContrID);
      cmd.AddParam("p", RSDBP_IN, PartyId);
      cmd.AddParam("pc", RSDBP_IN, PartyCode);
      cmd.AddParam("cn", RSDBP_IN, ContractNumber);
      cmd.AddParam("cd", RSDBP_IN, ContractDate);
      cmd.AddParam("e", RSDBP_IN, EKK);
      cmd.AddParam("s", RSDBP_IN, State);
      cmd.AddParam("er", RSDBP_IN, ErrorText);
      cmd.AddParam("g", RSDBP_IN, gg);
      cmd.execute;
      cmd.close;
      
      row = row + 1;
  end;
  RemProgress();

  ExcelApplication.ActiveWorkbook.Close;
  
   if (not RemoveFile ("$" + TermPath + "\\" + FileName))
      MsgBox("Ошибка удаления файла " + TermPath + "\\" + FileName);
   end;

onerror()
  if (valtype(ExcelApplication) != V_UNDEF)
    ExcelApplication.ActiveWorkbook.Close;
  end;
  EndAction(1);
End;

var all = 0,suc = 0, err = 0;
var dt = StrSubst(StrSubSt(string(Date(),Time()),".",""),":","");
var InfoLogFileName = GetTxtFileName( "extradbo_"+dt);
   
var logg = Logger();
SetOutput( InfoLogFileName, true ); //Пишем лог в файл. Начало
logg.RepHeader(date(),time());

CheckCreateTable();
FillServTable();

var sql_str = "select t_dlcontrid, t_partyid, t_partycode, t_contractnumber, to_char(t_contractdate,'dd.mm.yyyy') dt, "+
              "   t_ekk, t_state, t_errortext, t_guid, rowid "+
              " from usr_extra_contracts where lower(t_state) = 'дубль' and t_guid = :g";
var cmd = RSDCommand(sql_str);
cmd.AddParam("g", RSDBP_IN, gg);
var rs = RSDRecordSet(cmd);

var errcode = 0, ErrStr = ""; 
var DlContrID, CloseDate = {curdate}, IisCloseByTransf = null;
var sql_str2, cmd2;

var sql_checkQ, cmd_Q, rs_Q, dl_partyid;

InitProgress(-1);

while (rs.movenext)
   DlContrID = rs.value("t_dlcontrid");
   ErrStr = "";
   dl_partyid = Int(rs.value("t_partyid"));
   
   if (rs.value("t_errortext"))
      logg.RepStr("X",rs.value("t_contractnumber"),rs.value("t_partycode"), rs.value("dt"), rs.value("t_errortext"));
      err = err + 1;
   else 
SetDialogFlag(0);
      errcode = DL_CloseDLCONTR(DlContrID, CloseDate, IisCloseByTransf, ErrStr);
SetDialogFlag(1);
      if (errcode == 0 )
         suc = suc + 1;
         ErrStr = "Закрыт успешно";
         
         sql_checkQ = "SELECT * FROM Dobjcode_Dbt o "+
                      " WHERE o.t_Objecttype = 3  AND o.t_Codekind = 105  AND o.t_Objectid = :v_Prt and t_bankclosedate = to_date('01010001','ddmmyyyy') ";
         cmd_Q = RSDCommand(sql_checkQ);
         cmd_Q.AddParam("v_Prt", RSDBP_IN, dl_partyid);
         rs_Q = RSDRecordSet(cmd_Q);
         if (rs_Q.movenext)
            /* обновление аккаунта Quik при закрытиии договора*/
            execMacrofile("global_utils_intgr.mac","m_make_event_to_process",5025, dl_partyid);
            /*обновление РСХБ-Брокер*/
            execMacrofile("global_utils_intgr.mac","m_make_event_to_process",5026, dl_partyid);
         end;
         InsEvent_UpdateContract(DlContrID, 3); 
         addNoteForObject( 207, String(DlContrID:o:34), 135, "Техническое закрытие. Дубль по проекту ВТБ", CloseDate);
      else 
         err = err + 1;
      end;
      logg.RepStr("X",rs.value("t_contractnumber"),rs.value("t_partycode"), rs.value("dt"), ErrStr);
      
      sql_str2 = "update usr_extra_contracts set t_errortext = :e where rowid = :r";
      cmd = RSDCommand(sql_str2);
      cmd.AddParam("e", RSDBP_IN, ErrStr);
      cmd.AddParam("r", RSDBP_IN, rs.value("rowid"));
      cmd.execute();
   end;
   
   all=all+1;
   UseProgress(all);

end;
RemProgress();


logg.RepFooter(suc,err,all);
SetOutput( NULL, true ); //Пишем лог в файл. Конец

ViewFile( InfoLogFileName );

exit(1);