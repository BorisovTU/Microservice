import rsexts;

private macro IIF(condition, valueOnTrue, valueOnFalse)
  if (condition == true)
    return valueOnTrue;
  end;
  
  return valueOnFalse;
end;

/*----------------------*/
/* Параметры архиватора */
/*----------------------*/
class c_archiver_tool()
   /* private позволяет обращаться к элементам из класса-потомка */
   private var zip7_util_path_term : string = "$C:\\Program Files\\7-Zip\\7z.exe";
   private var zip7_util_path_sp : string = "C:\\Program Files\\7-Zip\\7z.exe";
   private var arj_util_path : string = "C:\\Program Files\\ARJ\\arj.exe";
   private var state_message : string;

   macro get_zip7_util_path_term()
      return zip7_util_path_term;
   end;

   macro get_zip7_util_path_sp()
      return zip7_util_path_sp;
   end;

   macro get_arj_util_path()
      return arj_util_path;
   end;

   macro set_arj_util_path(p_new_val)
      arj_util_path = p_new_val;
   end;

   macro get_state_message()
      return state_message;
   end;

   private macro zip7_return_analize(p_proc_ret:integer)
      var ret = false;

      if(p_proc_ret == 0)
         state_message = "No error";
         ret = true;

      else
         if(p_proc_ret == 1)
            state_message = "Warning";
         elif(p_proc_ret == 2)
            state_message = "Fatal error";
         elif(p_proc_ret == 7)
            state_message = "Command line error";
         elif(p_proc_ret == 8)
            state_message = "Not enough memory for operation";
         elif(p_proc_ret == 255)
            state_message = "User stopped the process";
         else
            state_message = string("Неизвестный код ошибки (", p_proc_ret, ")");
         end;
      end; 

      return ret;
   end;

   private macro arj_return_analize(p_proc_ret:integer)
      var ret = false;

      if(p_proc_ret == 0)
         state_message = "Success";
         ret = true;

      else
         if(p_proc_ret == 1)
            state_message = "Warning";
         elif(p_proc_ret == 2)
            state_message = "Fatal error";
         elif(p_proc_ret == 3)
            state_message = "CRC error (header or file CRC error or bad password)";
         elif(p_proc_ret == 4)
            state_message = "ARJ-SECURITY error or attempt to update an ARJ-SECURED archive";
         elif(p_proc_ret == 5)
            state_message = "Disk full or write error";
         elif(p_proc_ret == 6)
            state_message = "Cannot open archive or file";
         elif(p_proc_ret == 7)
            state_message = "Simple user error (bad parameters)";
         elif(p_proc_ret == 8)
            state_message = "Not enough memory";
         elif(p_proc_ret == 9)
            state_message = "Not an ARJ archive";
         elif(p_proc_ret == 10)
            state_message = "[DOS] XMS memory error (read or write)";
         elif(p_proc_ret == 11)
            state_message = "User control break";
         elif(p_proc_ret == 12)
            state_message = "Too many chapters (over 250)";
         else
            state_message = string("Неизвестный код ошибки (", p_proc_ret, ")");
         end;
      end;

      return ret;
   end;
end; /* class c_archiver_tool */

/*------------------------------*/
/* Инструмент распаковки архива */
/*------------------------------*/
class (c_archiver_tool) c_unarch_kit(i_archive_path:string, i_extract_files_path:string)
   private var archiver_util_path : string = ""; /* Путь к архиватору, для которого сформированы параметры */
   private var archive_kind       : string = ""; /* Тип архива, для которого сформированы параметры */
   private var extract_param_str  : string = ""; /* Параметры для процедуры распаковки */

   var archive_path       : string = ""; /* Путь к архиву */
   var extract_files_path : string = ""; /* Путь к директории, в которую требуется распаковать файлы */

   private macro init_toolkit(i_archive_path, i_extract_files_path)
      if(ValType(i_archive_path) != V_UNDEF)
         archive_path = i_archive_path;
      end;

      if(ValType(i_extract_files_path) != V_UNDEF)
         extract_files_path = i_extract_files_path;
      end;
   end;

   /* Сборка строки параметров процедуры распаковки */
   private macro make_param_str(p_arch_kind:string)
      var ret = false;
      extract_param_str = "";
      archiver_util_path = "";

      if(ValType(p_arch_kind) != V_UNDEF)
         if(    (strlen(p_arch_kind) != 0)
            and (strlen(archive_path) != 0)
            and (strlen(extract_files_path) != 0))

            p_arch_kind = strlwr(p_arch_kind);

            if(p_arch_kind == "zip7")
               extract_param_str = string("x ",
                                         archive_path,
                                         " -o",
                                         extract_files_path);
               archiver_util_path = zip7_util_path_sp;
               ret = true;

            elif(p_arch_kind == "arj")
               extract_param_str = string("x ",
                                         archive_path,
                                         " ",
                                         extract_files_path,
                                         "\\");
               archiver_util_path = arj_util_path;
               ret = true;
            end;

            if(ret)
               archive_kind = p_arch_kind;
            end;
         end;
      end;

      return ret;
   end;

   /* Распаковка файлов из архива */
   macro extract_files(p_arch_kind:string)
      var ret = false;
      var aux_buff;

      if(make_param_str(p_arch_kind))

         aux_buff = startprog(archiver_util_path, extract_param_str,false);
         if(aux_buff == -1)
            state_message = string("Архиватор по заданному пути (",
                                   archiver_util_path,
                                   ") не найден");
         else
            if(archive_kind == "zip7")
               if(zip7_return_analize(aux_buff))
                  ret = true;
               end;

            elif(archive_kind == "arj")
               if(arj_return_analize(aux_buff))
                  ret = true;
               end;
            end;
         end;

      else
         state_message = "Не заданы основные параметры";
      end;

      return ret;
   end;

   Initc_archiver_tool();

   init_toolkit(i_archive_path, i_extract_files_path);
end; /* class (c_archiver_tool) c_unarch_kit */

/*--------------------------------*/
/* Инструмент формирования архива */
/*--------------------------------*/
class (c_archiver_tool) c_inarch_kit(i_archive_path:string, i_packing_files_path:string, i_is_on_term: bool)
   private var is_on_term : bool = true; // Запуск архватора на терминале
   var archiver_util_path : string = ""; /* Путь к архиватору, для которого сформированы параметры */
   var archive_kind       : string = ""; /* Тип архива, для которого сформированы параметры */
   var packing_param_str  : string = ""; /* Параметры для процедуры упаковки */

   var archive_path       : string = ""; /* Путь к архиву */
   var packing_files_path : string = ""; /* Путь к директории, в которой находятся файлы */
   var packing_files_list = TArray;      /* Список имен файлов для упаковки в архив */

   private macro init_toolkit(i_archive_path, i_packing_files_path, i_is_on_term)
      packing_files_list.size = 0;

      if(ValType(i_archive_path) != V_UNDEF)
         archive_path = i_archive_path;
      end;

      if(ValType(i_packing_files_path) != V_UNDEF)
         packing_files_path = i_packing_files_path;
      end;
      
      if (ValType(i_is_on_term) == V_BOOL)
        is_on_term = i_is_on_term;
      end;
   end;

   /* Сборка строки параметров процедуры упаковки */
  private macro make_param_str(p_arch_kind:string, p_file_path:string)
      var ret = false;
      var extract_param_str = "";
      archiver_util_path = "";
      if((ValType(p_arch_kind) != V_UNDEF) and (ValType(p_file_path) != V_UNDEF))
         if(    (strlen(p_arch_kind) != 0)
            and (strlen(p_file_path) != 0)
            and (strlen(archive_path) != 0)
            and (strlen(packing_files_path) != 0))

            p_arch_kind = strlwr(p_arch_kind);

            if(p_arch_kind == "zip7")  
               packing_param_str = string("a ",
                                          archive_path,
                                          " ",
                                          p_file_path,packing_files_list[0]);
               archiver_util_path = IIF(is_on_term, zip7_util_path_term, zip7_util_path_sp);
               ret = true;

            elif(p_arch_kind == "arj")
               packing_param_str = string("a ",
                                          archive_path,
                                          " ",
                                          p_file_path,
                                          packing_files_list[0]);
               archiver_util_path = arj_util_path;
               ret = true;

            end;

            if(ret)
               archive_kind = p_arch_kind;
            end;
         end;
      end;

      return ret;
   end;
   
   /* Сборка строки параметров процедуры упаковки с передачей пакуемого файла*/
  private macro make_param_str_filename(p_arch_kind:string, p_file_path:string, filename)
      
      var ret = false;
      var extract_param_str = "";
      archiver_util_path = "";

      if((ValType(p_arch_kind) != V_UNDEF) and (ValType(p_file_path) != V_UNDEF))
         if(    (strlen(p_arch_kind) != 0)
            and (strlen(p_file_path) != 0)
            and (strlen(archive_path) != 0)
            and (strlen(packing_files_path) != 0))

            p_arch_kind = strlwr(p_arch_kind);

            if(p_arch_kind == "zip7")
               packing_param_str = string("a ",
                                          archive_path,
                                          " ",
                                          filename);
               archiver_util_path = IIF(is_on_term, zip7_util_path_term, zip7_util_path_sp);
               ret = true;

            elif(p_arch_kind == "arj")
               packing_param_str = string("a ",
                                          archive_path,
                                          " ",
                                          p_file_path,
                                          filename);
               archiver_util_path = arj_util_path;
               ret = true;

            end;

            if(ret)
               archive_kind = p_arch_kind;
            end;
         end;
      end;

      return ret;
   end;
   /* Добавление имени файла в список файлов для упаковки в архив */
   macro add_file_name(p_file_name:string)
      if(ValType(p_file_name) != V_UNDEF)
         if(strlen(p_file_name) != 0)
            packing_files_list.value(packing_files_list.size) = p_file_name;
         end;
      end;
   end;
   /*DAN*/
   /*перепаковка для создания непрерывного архива(уменьшения количества блоков) в целях уменьшения итогового размера*/
   macro Repack_Archive(p_arch_kind:string)
      var ret = false;
      var aux_buff;
      var arch_util_path;
      var unpack_param_str;
      var repack_param_str;
      var Arch_name;
      if(ValType(p_arch_kind) != V_UNDEF)
        if(p_arch_kind == "zip7")
           arch_util_path = IIF(is_on_term, zip7_util_path_term, zip7_util_path_sp);
           Arch_name = archive_path + ".7z";
           unpack_param_str = string ("x  ",
                                      archive_path,".7z",
                                      " -o",
                                      archive_path);
           repack_param_str =  string("a -t7z -mx5 -r0 ",
                                     "-sdel ",
                                     archive_path,".7z",
                                     " ",
                                     archive_path);  
           ret = true
        end;
        if(ret)
          MakeDir(archive_path);
          aux_buff = startprog(arch_util_path, unpack_param_str,false);
          RemoveFile(Arch_name);
          aux_buff = startprog(arch_util_path, repack_param_str,false);
        end;

      end;
   end;
  /*DAN*/


   /* Упаковка файлов в архив */
   macro Add_to_Archive(p_arch_kind:string)
      var ret = false;
      var aux_buff;

      if(make_param_str(p_arch_kind,packing_files_path))

         aux_buff = startprog(archiver_util_path, packing_param_str ,false);
         if(aux_buff == -1)
            state_message = string("Архиватор по заданному пути (",
                                   archiver_util_path,
                                   ") не найден");
         else
            if(archive_kind == "zip7")
               if(zip7_return_analize(aux_buff))
                  ret = true;
               end;

            elif(archive_kind == "arj")
               if(arj_return_analize(aux_buff))
                  ret = true;
               end;
            end;
         end;

      else
         state_message = "Не заданы основные параметры";
      end;

      return ret;
   end;

   /* Упаковка файлов в архив с передачей пакуемого файла*/
   macro Add_to_Archive_Filename(p_arch_kind:string, fileName)
      var ret = false;
      var aux_buff;

      if(make_param_str_filename(p_arch_kind,packing_files_path, packing_files_path + filename))

         aux_buff = startprog(archiver_util_path, packing_param_str ,false);
         if(aux_buff == -1)
            state_message = string("Архиватор по заданному пути (",
                                   archiver_util_path,
                                   ") не найден");
         else
            if(archive_kind == "zip7")
               if(zip7_return_analize(aux_buff))
                  ret = true;
               end;

            elif(archive_kind == "arj")
               if(arj_return_analize(aux_buff))
                  ret = true;
               end;
            end;
         end;

      else
         state_message = "Не заданы основные параметры";
      end;

      return ret;
   end;

   /* Упаковка каталога распакованного ворда обратно в архив *.docx */
   macro Add_to_Archive_Word(p_arch_kind:string)
      var ret = false;
      var aux_buff;

      var pack_param_str = String(" a -tzip ",  //тип zip
                                  " -sdel ",    //удалить файлы после упаковки
                                  archive_path,".docx",
                                  " -r ",       //рекурсивно для всех папок
                                  archive_path,"\\*.*"); 
      archive_kind = p_arch_kind;
      if(archive_kind == "zip7")
         archiver_util_path = zip7_util_path_sp;
      elif(archive_kind == "arj")
         archiver_util_path = arj_util_path;
      end;
 
      aux_buff = startprog(archiver_util_path, pack_param_str, false);
      if(aux_buff == -1)
         state_message = string("Архиватор по заданному пути (",
                                archiver_util_path,
                                ") не найден");
      else
         if(archive_kind == "zip7")
            if(zip7_return_analize(aux_buff))
               ret = true;
            end;

         elif(archive_kind == "arj")
            if(arj_return_analize(aux_buff))
               ret = true;
            end;
         end;
      end;

      return ret;
   end;

   Initc_archiver_tool();

   init_toolkit(i_archive_path, i_packing_files_path, i_is_on_term);
end; /* class (c_archiver_tool) c_inarch_kit */

/**
   @brief Удаление вложенных папок внутри целевой директории
   @param[in] pdirname Имя целевой директории

   Предполагается, что папки уже пустые
*/
macro RecursiveRemoveInDir(pdirname:string)
   var namedir = pdirname;
   var dl = TDirList(namedir+"\\*","d");
   var dl2;
   var i = 0;
   var inner_name;
   
   while ( i < dl.count())
      inner_name = dl.name(i);
      if ( dl.isdir(i) and (inner_name!=".") and (inner_name!=".."))
         dl2 = TDirList(namedir+"\\"+inner_name+"\\*", "d");
         if (dl2.count() > 0 )
            RecursiveRemoveInDir(namedir+"\\"+inner_name);
         end;
         RemoveDir(namedir+"\\"+inner_name);
      end;
      i = i+1;
   end;

end;


/*
var zip_arch_path = "D:\\RS\\shishkin\\7-Zip\\7z.exe";//"C:\\Program Files (x86)\\WinRAR\\WinRAR.exe";

var b_arch_dir_path = "\\\\vboxsrv\\VM_Common\\sandbox\\3";
var b_inar_dir_path = "\\\\vboxsrv\\VM_Common\\sandbox\\1";
var b_unar_dir_path = "\\\\vboxsrv\\VM_Common\\sandbox\\2";

var arch_dir_path = "D:\\RSHB_SOFR_TEST\\TxtFile\\sent_reports\\";
var file_dir_path = "C:\\1\\send\\pdfBROK\\RS\\51-05-272786.pdf";//string(b_inar_dir_path, "\\*");

D:\RS\shishkin\7-Zip\7z.exe
a C:\1\send\pdfBROK\RS\ 51-05-272786.pdf

var ret;
ret = run(zip_arch_path, string("a -tzip ", arch_dir_path, " ", file_dir_path));
ret = run(zip_arch_path, string("e ", arch_dir_path, " -o", b_unar_dir_path));

msgbox("Done!");
*/