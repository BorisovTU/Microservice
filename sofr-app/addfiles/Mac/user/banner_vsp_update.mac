/**
 @file pawn_acc_import.mac
 @brief Первоначальная загрузка счетов в примечание векселей, находящихся в залоге на момент вывода задачи в релиз

  # tag
 - functional_block:Векселя
 - code_type:One_time
 - Векселя банка
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |14.03.2025 |Топорков Д.В.|BOSS-7047 BOSS-7103                                       | Создание
 */
import "StorageInfo.mac", "logger.mac", "TxtTable.mac";
import "cblogger2.mac";
import bnk_common;
import rsd;

private const REGPATH_VSPCODE_ENABLED = "ВЕКСЕЛЯ БАНКА\\РСХБ\\ИСПОЛЬЗОВАТЬ КОД ВСП";

private class CSpecial(pBCNumber, pDepartment, pBranch)
  var bcNumber = pBCNumber;
  var department = pDepartment;
  var branch = pBranch;
end;

private class CSpecials()
  private var specials = TArray();
  
  private macro Add(pBCNumber, pDepartment, pBranch)
    specials(specials.size) = CSpecial(pBCNumber, pDepartment, pBranch);
  end;
  
  macro ExtractByBCNumber(bcNumber)
    var i = 0;
    var special = null;
    
    while ((i < specials.size) and (ValType(special) == V_UNDEF))
      if (specials(i).bcNumber == bcNumber)
        special = specials(i);
      end;
      
      i = i + 1;
    end;
    
    while (i < specials.size)
      specials(i - 1) = specials(i);
      i = i + 1;
    end;
    
    if (ValType(special) != V_UNDEF)
      specials.size = specials.size - 1;
    end;
    
    return special;
  end;
    
  
  private macro Init()
    Add("69787", 1, 5);
    Add("69674", 1, 0);
    Add("69796", 1, 3);
    Add("57649", 7, 0);
    Add("68532", 7, 0);
    Add("68603", 7, 0);
    Add("68638", 7, 0);
    Add("68643", 7, 0);
    Add("68661", 7, 0);
    Add("68671", 7, 0);
    Add("68559", 7, 22);
    Add("66401", 30, 22);
    Add("66402", 30, 22);
    Add("66432", 30, 29);
    Add("66433", 30, 29);
    Add("66434", 30, 29);
    Add("63378", 35, 0);
    Add("63169", 51, 0);
  end;
  
  Init();
end;

var specials = CSpecials();

// Данные по филиалам
private class CDepartment(pCode, pName)
  var code = pCode;
  var name = pName;
end;

private class CDepartmentInfo()
  private var departments = TArray();
  
  private macro LoadDepartmentInfo(code, name)
    var param, value;
    var sql = "SELECT t_code, t_name " +
                "FROM ddp_dep_dbt " +
               "WHERE ";
    if ((ValType(name) == V_STRING) and (StrLen(name) > 0))
      sql = sql + "t_name = :name";
      param = "name";
      value = name;
    else
      sql = sql + "t_code = :code";
      param = "code";
      value = code;
    end;
    
    var cmd = RsdCommand(sql);
    cmd.AddParam(param, RSDBP_IN, value);
    
    var rs = RsdRecordSet(cmd);
    if (rs.MoveNext())
      departments(departments.size) = CDepartment(rs.Value("t_code"), rs.Value("t_name"));
    end;
  end;
  
  macro GetName(code)
    var idx = 0;
    while (idx < departments.size)
      if (departments(idx).code == code)
        return departments(idx).name;
      end;
      
      idx = idx + 1;
    end;
    
    idx = departments.size;
    LoadDepartmentInfo(code, null);
    
    if (idx < departments.size)
      return departments(idx).name;
    end;
    
    return null;
  end;
  
  macro GetCode(name)
    var idx = 0;
    while (idx < departments.size)
      if (departments(idx).name == name)
        return departments(idx).code;
      end;
      
      idx = idx + 1;
    end;
    
    idx = departments.size;
    LoadDepartmentInfo(null, name);
    
    if (idx < departments.size)
      return departments(idx).code;
    end;
    
    return null;
  end;
  
  private macro Init()
    LoadDepartmentInfo(null, "0000");
  end;
  
  Init();
end;

var departments = CDepartmentInfo();

// Получить настройки для временных файлов
private macro GetStorageInfo(): CStorageInfo
  var storageInfo = CStorageInfo("VspUpdateLog", "txt");
  var logfile: CFile = storageInfo.GetFile(TXTTAG, TXTTAG);
  
  return storageInfo;
end;

// Извлечь информацию о филиале и подразделении из серии векселя
private macro GetVSPInfoFromBCSeries(bcSeries: String, department: @Integer, branch: @Integer, error: @String)
  var seriesDepartmentCode;
  var seriesBranch;
  var seriesCode;
  
  var separatorIndex = Index(bcSeries, "-");
  error = "Серия векселя отличается от ожидаемого формата ФФФ-ВВВ";
  department = -1;
  branch = -1;
  
  if ((separatorIndex == 0) and (StrLen(bcSeries) >= 3))
    seriesDepartmentCode = SubStr(bcSeries, 1, 3);
    seriesCode = SubStr(bcSeries, 4);
    seriesBranch = "000";
  elif (separatorIndex == 4)
    seriesDepartmentCode = SubStr(bcSeries, 1, 3);
    seriesBranch = SubStr(bcSeries, 5, 3);
    seriesCode = SubStr(bcSeries, 8);
  else
    return false;
  end;
  
  if ((not StrIsNumber(seriesDepartmentCode)) or 
      (not StrIsNumber(seriesBranch)) or 
      (StrLen(seriesBranch) != 3) or
      (SubStr(seriesDepartmentCode, 1, 1) != "0") or
      ((StrLen(seriesCode) > 0) and StrIsNumber(SubStr(seriesCode, 1, 1))))
    return false;
  end;
  
  var departmentName = SubStr(seriesDepartmentCode, 2) + "00";
  department = departments.GetCode(departmentName);
  
  if (ValType(department) == V_UNDEF)
    department = -1;
    error = "Не найден филиал " + departmentName;
    return false;
  end;
  
  branch = Int(seriesBranch);
  error = "";
  
  return true;
end;

// Процедура обновления кодов
private macro UpdateVSPCodes()
  var department, branch, result, update;
  var storage = GetStorageInfo();
  var logFilename: String = storage.FilePath(TXTTAG, TXTTAG);
  var log = CTxtLogger(logFilename);
  var table = CTxtTable(MakeArray(6, 10, 10, 80));
  var recordsTotal = 0;
  var recordsUpdated = 0;
  var special;
  var specialTag;
  
  table.SetAligns(MakeArray(ALIGN_RIGHT, ALIGN_LEFT, ALIGN_LEFT, ALIGN_LEFT));
  table.SetStyle(BORDER_STYLE_NONE);
  table.SetMultiline(false);
  
  log.UseTimestamp(false);
  log.Info("ПРОТОКОЛ ОБНОВЛЕНИЯ КОДОВ ВСП НА ВЕКСЕЛЯХ");
  log.Info("=================================================================");
  log.Info(table.GetFormattedData(MakeArray("ID векселя", "Серия", "Номер", "Результат")).Value(0));
  log.Info("=================================================================");

  var cmd = RsdCommand("select t_bcid, t_bcseries, t_bcnumber, t_department, t_branch from dvsbanner_dbt where t_issuer in (select t_partyid from ddp_dep_dbt) order by t_bcid");
  var rs = RsdRecordset(cmd);
  
  InitProgress(-1, "Обновление данных...", "Обновление данных...");
  
  while (rs.MoveNext())
    update = "";
    result = "";
    specialTag = "";
    
    if (rs.Value("t_bcid") >= 5738)
      special = specials.ExtractByBCNumber(Trim(rs.Value("t_bcnumber")));
    else
      special = null;
    end;
    
    if (ValType(special) != V_UNDEF)
      department = special.department;
      branch = special.branch;
      specialTag = "(ПО СПИСКУ) ";
    end;
    
    if ((ValType(special) == V_UNDEF) and (not GetVSPInfoFromBCSeries(rs.Value("t_bcseries"), @department, @branch, @result)))
      result = "Ошибка: " + result;
    else
      if (department != rs.Value("t_department"))
        result = String("t_department ", rs.Value("t_department"), " => ", department);
        update = String("t_department = ", department);
      end;
      
      if (branch != rs.Value("t_branch"))
        if (StrLen(update) > 0)
          update = update + ", ";
          result = result + "; ";
        end;
        
        update = String(update, "t_branch = ", branch);
        result = String(result, "t_branch ", rs.Value("t_branch"), " => ", branch);
      end;
      
      if (StrLen(update) == 0)
        result = "не требуется";
      else
        update = "update dvsbanner_dbt set " + update + " where t_bcid = " + String(rs.Value("t_bcid"));
        recordsUpdated = recordsUpdated + 1;
        
        RsdCommand(update).Execute();
      end;
      
      result = "Обновление " + specialTag + result;
    end;
    
    log.Info(table.GetFormattedData(MakeArray(String(rs.Value("t_bcid")), rs.Value("t_bcseries"), Trim(rs.Value("t_bcnumber")), result)).Value(0));
    recordsTotal = recordsTotal + 1;
    UseProgress(recordsTotal);
  end;
  
  log.Info("=================================================================");
  log.Info("   Всего векселей: " + String(recordsTotal));
  log.Info("        обновлено: " + String(recordsUpdated));
  
  log.Close();
  ViewFile(logFilename);
  RemProgress();

OnError(err)
  log.Error("ОШИБКА ВЫПОЛНЕНИЯ: " + GetFullErrMsg(err));
  RemProgress();
end;

private macro IsFeatureEnable()
   var isEnable = false;
   var error: integer;
   
   GetRegistryValue(REGPATH_VSPCODE_ENABLED, V_BOOL, isEnable, error);
   
   if (error > 0)
      isEnable = false;
   end;

   return isEnable;
end;

// ======== Точка входа ========
if (not IsFeatureEnable())
  msgbox("Не включена настройка " + REGPATH_VSPCODE_ENABLED);
  Exit(1);
end;

UpdateVSPCodes();
Exit(1);