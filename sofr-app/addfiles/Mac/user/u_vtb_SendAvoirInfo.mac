/*
$Name:        u_vtb_SendAvoirInfo.mac
$Module:      Ценные бумаги
$Description: Формирования справки о ценах приобретения по ценным бумагам, принятым РСХБ от стороннего брокера
*/
import "DlRepForm_h.mac", "DLReport_h.mac", "or_exl_h.mac", "oralib.mac", "dlmisc.mac", "u_common_email_utils.mac", "dlwreps.mac";

PRIVATE CONST POI_MODE = TRUE;

/*Номера полей в форме отчета*/
PRIVATE CONST PNFLD_VTBAINFO_REPDATE = 0,
              PNFLD_VTBAINFO_DLCONTR = 1;

PRIVATE CONST DLCONTR_ALL      = -1,
              DLCONTR_FROMLIST = -2;

PRIVATE CONST STR_FROMLIST = "По списку";

PRIVATE CONST PARTY_CODEKIND_VTB = 109;         // код клиента ВТБ

PRIVATE CONST MESSAGE_SENT_ATTR_GRP = 195;

PRIVATE CONST CNTT_DBO = 1005;

PRIVATE CONST TMP_DIR = "VTBAINFO";
PRIVATE CONST MAIL_TMPL_NAME = "VTBAvoirInfoMailTempl.dotx";
PRIVATE CONST MAIL_HEAD = "Формирование справки о ценах приобретения по иностранным ценным бумагам, переведенным в Россельхозбанк";

PRIVATE CONST VTB_AVOIR_INFO_EMAIL_GRP = 88; //группа рассылки

PRIVATE MACRO FldProc_RepDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Year, Month;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_DlContr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Choice;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DLCONTR_ALL;
     end;
     FldShowValue = STR_ALLVALUE;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     Choice = DL_Scroll("SELECT sf.t_number, dlcontr.t_dlcontrid t_id FROM ddlcontr_dbt dlcontr, dsfcontr_dbt sf "
                        "WHERE sf.t_id = dlcontr.t_sfcontrid ",
                        "Список ДБО", pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_number", "Номер");
     if(Choice != NULL)
        FldRealValue = Choice.Value("t_Id");
        FldShowValue = Choice.Value("t_Number");
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if ( (ValType(FldRealValue) == V_INTEGER) AND (FldRealValue == DLCONTR_ALL) )
        FldRealValue = DLCONTR_FROMLIST;
        FldShowValue = STR_FROMLIST;
     else
        FldRealValue = DLCONTR_ALL;
        FldShowValue = STR_ALLVALUE;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE CLASS (DL_CPanel) VTBAvoirInfoPanel()
  var RepDate, DlContr;

  PRIVATE MACRO Init()
    AddFieldProc( 0, PNFLD_VTBAINFO_REPDATE, DL_USERFIELD, @FldProc_RepDate);
    AddFieldProc( 0, PNFLD_VTBAINFO_DLCONTR, DL_USERFIELD, @FldProc_DlContr);
  END;

  PRIVATE MACRO GetFieldValue(fldNum:integer):variant
    var showValue:variant, fldValue:variant;

    GetFieldValue(fldNum, @showValue, @fldValue);
    return fldValue;
  END;

  MACRO Run()
    var stat = Run();

    RepDate = GetFieldValue(PNFLD_VTBAINFO_REPDATE);
    DlContr = GetFieldValue(PNFLD_VTBAINFO_DLCONTR);

    return stat;
  END;

  InitDL_CPanel("VTB.lbr", "VTBAINFO");
END;

PRIVATE CLASS (DL_CReportTemplate) VTBAvoirInfoReport
  var TmpDir = GetSysDir(0) + "\\" + TMP_DIR;

  PRIVATE MACRO LoadDlContr (DlContrId)
    var cmd, cmd1, rs;

    execSQL("TRUNCATE TABLE u_vtb_dlcontr_tmp;");

    cmd = RsdCommand("BEGIN "
                   + " INSERT INTO U_VTB_DLCONTR_TMP ( T_DLCONTRID,"
                   + "                                 T_SFCONTRID,"
                   + "                                 T_NUMBER,"
                   + "                                 T_EKK,"
                   + "                                 T_DATEBEGIN,"
                   + "                                 T_DATECLOSE,"
                   + "                                 T_PARTYID,"
                   + "                                 T_CLIENTNAME,"
                   + "                                 T_EMAIL)"
                   + " SELECT dlcontr.t_DlContrId, sf.t_Id, sf.t_number, "
                   + "        NVL( (select RSB_SECUR.SC_GetDlObjCodeOnDate(" + OBJTYPE_BROKERCONTR_DL + ", 1, dlcontr.t_DlContrId, ?) FROM DUAL), CHR(1)), "
                   + "        sf.t_DateBegin, sf.t_DateClose, "
                   + "        pt.t_PartyId, pt.t_Name, dlcontr.t_Email "
                   + "   FROM ddlcontr_dbt dlcontr, dsfcontr_dbt sf, dparty_dbt pt "
                   + "  WHERE "
                   + iif (((ValType(DlContrId) == V_INTEGER) AND (DlContrId > 0)),
                     "        dlcontr.t_DlContrId = " + string(DlContrId)
                   + "    AND ", "")
                   + "        sf.t_Id = dlcontr.t_SfContrId "
                   + "    AND pt.t_PartyId = sf.t_PartyId "
                   + "    AND RSI_RSBPARTY.GetPartyCode(pt.t_PartyId, " + PARTY_CODEKIND_VTB + ") != CHR(1) "
                   + "    AND RSB_SECUR.GetObjAttrName( " + OBJTYPE_BROKERCONTR_DL + ", " + MESSAGE_SENT_ATTR_GRP + ", "
                   + "                                  RSB_SECUR.GetMainObjAttrNoDate( " + OBJTYPE_BROKERCONTR_DL + ", LPAD(dlcontr.t_DlContrId, 34, '0'), " + MESSAGE_SENT_ATTR_GRP + ")) != 'Да'; "
                   + " ?:=sql%rowcount; "
                   + "END; ");

    cmd.AddParam("", RSDBP_IN, m_Form.RepDate);
    cmd.addParam("p_out", RSDBP_OUT,V_INTEGER);
    cmd.Execute();

    if ((ValType(DlContrId) == V_INTEGER) AND (DlContrId > 0))
      if ( cmd.value("p_out") == 0 )
        cmd1 = RsdCommand("SELECT t_SysDate, t_SysTime "
                        + "  FROM dobjatcor_dbt "
                        + " WHERE     t_ObjectType = " + OBJTYPE_BROKERCONTR_DL
                        + "       AND t_Object = LPAD (?, 34, '0') "
                        + "       AND t_GroupId = " +  MESSAGE_SENT_ATTR_GRP
                        + "       AND t_ValidToDate = TO_DATE ('31129999', 'ddmmyyyy') "
                        + "       AND t_AttrId = 1 ");
        cmd1.AddParam("", RSDBP_IN, DlContrId);
        rs = RsdRecordSet(cmd1);

        if(rs.MoveNext())
          msgbox("Справка сформирована ранее и отправлена клиенту по электронной почте " + date(rs.Value("t_SysDate")) + " " + time(rs.Value("t_SysTime")));
        end;
      end;
    else
      msgbox("Отобрано договоров: " + cmd.value("p_out"));
    end;

    execSQL("COMMIT;");

  END;

  PRIVATE MACRO LoadDlContrList
    var fname = "", file_xlsx, range, i = 1, cnt = 0, EKK = "", number = "";
    var cmd;

    if ( SelectFile(fname, "*.xlsx") )
      file_xlsx = CDAOMSExcel(fname);

      execSQL("TRUNCATE TABLE u_vtb_dlcontr_tmp;");

      while (ValType(file_xlsx.Sheet.Cells(i, 1).Value) != V_UNDEF)
        EKK = string(file_xlsx.Sheet.Cells(i, 1).Value:0:0);
        number = file_xlsx.Sheet.Cells(i, 2).Value;

        cmd = RsdCommand("BEGIN "
                       + " INSERT INTO U_VTB_DLCONTR_TMP ( T_DLCONTRID,"
                       + "                                 T_SFCONTRID,"
                       + "                                 T_NUMBER,"
                       + "                                 T_EKK,"
                       + "                                 T_DATEBEGIN,"
                       + "                                 T_DATECLOSE,"
                       + "                                 T_PARTYID,"
                       + "                                 T_CLIENTNAME,"
                       + "                                 T_EMAIL)"
                       + " SELECT dlcontr.t_DlContrId, sf.t_Id, sf.t_number, ?, sf.t_DateBegin, sf.t_DateClose, "
                       + "         pt.t_PartyId, pt.t_Name, dlcontr.t_Email "
                       + "   FROM ddlcontr_dbt dlcontr, dsfcontr_dbt sf, dparty_dbt pt "
                       + "  WHERE sf.t_Id = dlcontr.t_SfContrId "
                       + "    AND sf.t_Number = ? "
                       + "    AND RSB_SECUR.SC_GetDlObjCodeOnDate( " + OBJTYPE_BROKERCONTR_DL + ", 1, dlcontr.t_DlContrId, ?) = ? "
                       + "    AND RSB_SECUR.GetObjAttrName( " + OBJTYPE_BROKERCONTR_DL + ", " + MESSAGE_SENT_ATTR_GRP + ", "
                       + "                                  RSB_SECUR.GetMainObjAttrNoDate( " + OBJTYPE_BROKERCONTR_DL + ", LPAD(dlcontr.t_DlContrId, 34, '0'), " + MESSAGE_SENT_ATTR_GRP + ")) != 'Да' "
                       + "    AND pt.t_PartyId = sf.t_PartyId; "
                       + " ?:=sql%rowcount; "
                       + "END; ");
        cmd.AddParam("", RSDBP_IN, EKK);
        cmd.AddParam("", RSDBP_IN, number);
        cmd.AddParam("", RSDBP_IN, m_Form.RepDate);
        cmd.AddParam("", RSDBP_IN, EKK);
        cmd.addParam("p_out", RSDBP_OUT,V_INTEGER);
        cmd.Execute();

        if( cmd.value("p_out") < 1 )
          msgbox("Не найден ДБО №" + number + " (ЕКК = \"" + EKK + "\"), либо сообщение по данному ДБО уже отправлено");
        else
          cnt = cnt + 1;
        end;

        i = i + 1;
      end;

      execSQL("COMMIT;");

      msgbox("Отобрано договоров: " + cnt);
    end;

  OnError(_err)
    msgbox("Загрузка списка ДБО из файла \"" + fname + "\" не выполнена " );
  END;

  PRIVATE MACRO SendMessage(ClientName, ContrNum, ContrBegDate, RepFilePath, RepFileName, _email, PartyID)
    var email = _email;
    var cmd, rs;
    var htm = "", DirName_Template = "", TempStr, tmpdoc_name, doc:object;
    var v_email_processor = c_email_proc_env();

    macro read_tmp
      File f() txt;
      Open(f, tmpdoc_name, "rsansi");
      while (Next(f,1024))
        htm = htm + f.str;
      end;
      Close(f);
      return true;
    OnError (_err)
      return false;
    end;

    if ((email == NULL) OR (email == ""))
      cmd = RsdCommand(" WITH email as (SELECT t_ContactType, t_Value FROM dcontact_dbt WHERE t_PartyId = ? AND t_ContactKind = " + CNTK_EMAIL + " ) "
                       " SELECT NVL((SELECT t_Value FROM email WHERE t_ContactType = " + CNTT_TOSENDREPORTS + " AND rownum = 1 ), "
                       "            NVL((SELECT t_Value FROM email WHERE t_ContactType = " + CNTT_DBO + " AND rownum = 1 ), "
                       "                (SELECT t_Value FROM email WHERE t_ContactType = 0 AND rownum = 1 )) "
                       "           ) t_Email"
                       "  FROM DUAL "  );
      cmd.AddParam("",RSDBP_IN, PartyID);
      rs = RsdRecordset(cmd);

      if (rs.MoveNext())
        email = rs.Value("t_Email");

        if ((email == NULL) OR (email == ""))
          msgbox(RepFileName + ";Не найден E-mail");
          return false;
        end;
      end;
    end;

    GetRegistryValue( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEMPLSDIR", V_STRING, DirName_Template );

    TempStr = FindPath(MAIL_TMPL_NAME, DirName_Template);

    if( not TempStr )
      MsgBox(RepFileName + ";Сообщение не сформировано: Не найден шаблон уведомления");
      return false;
    end;

    println(MAIL_TMPL_NAME);

    println(RepFileName);

    [~ClientName];
    println(ClientName);

    [~Contract];
    println(ContrNum);

    [~ContrBegDate];
    println(ContrBegDate);

    var OutFileName = "";
    var HtmlFileName = RepFileName + ".html";
    debugbreak;
    DLWREPS_PrintReportFromTagFile(SetOutput (GetTxtFileName ("SendAvoirInfo")), @OutFileName, null, false, true, null, HtmlFileName, false);

    SetOutput(NULL, false);

    tmpdoc_name = SplitFile(OutFileName, NULL, NULL) + "\\" + HtmlFileName;

    if(not read_tmp()) // содержимое временного "*.htm"-файла считывается в переменную
      msgbox(RepFileName + ";Сообщение не сформировано: Не удалось загрузить файл уведомления " + tmpdoc_name);
      return false;
    end;

    DelFile(OutFileName);
    DelFile(tmpdoc_name);

    v_email_processor.m_add_email_to_list(email);
    v_email_processor.m_get_email_group(VTB_AVOIR_INFO_EMAIL_GRP);
    v_email_processor.m_set_msg_head(MAIL_HEAD);
    v_email_processor.m_add_attach_to_list(RepFilePath);
    v_email_processor.m_add_row_to_msg_text(htm);
    v_email_processor.m_save_to_submit_synch(true);
    v_email_processor.m_submit_email_synch(); // отправляем синхронно в формате html

    if(v_email_processor.m_get_error_status())
      msgbox( RepFileName + ";Сообщение сформировано, но не отправлено: " + v_email_processor.get_service_msg());
      return false;
    else
      msgbox( RepFileName + ";Сообщение отправлено" );
    end;

    return true;
  OnError(_err)
    msgbox(RepFileName + ";Ошибка при отправке: " + _err.Message);
    return false;
  END;

  PRIVATE MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  MACRO BeginReport()
    if( ValType(m_Form.DlContr) == V_INTEGER )
      if (m_Form.DlContr == DLCONTR_FROMLIST )
        LoadDlContrList();
      else
        LoadDlContr(m_Form.DlContr);
      end;
    end;

    CreateTotalBook();
  END;

  MACRO Create()
    var cmd, nrec, rs, next = true, i = 1, stat = false, msg = "";
    var cur_dlcontr = -1, DirName = "", RepFilePath = "", RepFileName = "", _ext = "", Number, ContrBegDate, ClientName, EKK, Email, PartyID;

    MakeDir(TmpDir);

    GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\СПРАВКА_ЦЕНЫ_ВТБ", V_STRING, DirName);

    var query =         " WITH op "
                      + "        AS (SELECT DISTINCT op1.t_dlcontrid, op1.t_dealid_ndfl t_dealid "
                      + "              FROM u_vtb_dlcontr_tmp dlcontr, uvtb_checkop_dbt op1 "
                      + "             WHERE op1.t_clientid = dlcontr.t_partyid "
                      + "                   AND NOT EXISTS "
                      + "                              (SELECT 1 "
                      + "                                 FROM uVTB_checkOp_dbt op2, ddl_tick_dbt tick "
                      + "                                WHERE     op2.t_clientid = op1.t_clientid "
                      + "                                      AND op2.t_dlcontrid = op1.t_dlcontrid "
                      + "                                      AND op2.t_dealid_ndfl = tick.t_dealid "
                      + "                                      AND tick.t_dealstatus != 20) "
                      + "                   AND op1.t_dlcontrid = dlcontr.t_dlcontrid "
                      + "                   AND op1.t_depo_sum = op1.t_ndfl_sum) "
                      + "  SELECT t.t_DlContrID, dlcontr.t_Number, dlcontr.t_DateBegin, dlcontr.t_ClientName, dlcontr.t_EKK, fi.t_Name t_AvrName, "
                      + "         (SELECT CASE WHEN t_ISIN != CHR(1) THEN t_ISIN ELSE t_LSIN END "
                      + "            FROM davoiriss_dbt avr "
                      + "           WHERE avr.t_FIID = fi.t_FIID) "
                      + "            t_ISIN, "
                      + "         t.t_taxownbegdate t_DateBuy, "
                      + "         RSB_SPREPFUN.GetRateOnDateCrossDbl_Ex (t.t_taxownbegdate, t.t_CFI, " + NATCUR + ", 0) t_rate, "
                      + "         t.t_Principal, (SELECT t_ISO_Number FROM dfininstr_dbt WHERE t_FIID = t.t_CFI) t_currency, t.t_Price, "
                      + "         RSI_RSB_FIINSTR.FI_GetNominalOnDate (t.t_PFI, t.t_taxownbegdate) t_Nominal, t.t_Cost, t.t_NKD, "
                      + "         RSB_SPREPFUN.SmartConvertSum_Ex (t.t_TotalCost, t.t_taxownbegdate, t.t_CFI, " + NATCUR + ") t_TotalCost, "
                      + "         NVL ( "
                      + "            (SELECT SUM (t_Sum) "
                      + "               FROM ddlsum_dbt "
                      + "              WHERE t_DocKind = " + DL_AVRWRT + " AND t_DocId = t.t_DealId AND t_Kind = " + DLSUM_KIND_OUTLAY + "), "
                      + "            0) "
                      + "            t_comis, "
                      + "         dlcontr.t_PartyId, dlcontr.t_Email "
                      + "    FROM (SELECT op.t_dlcontrid, tick.t_DealId, tick.t_taxownbegdate, leg.t_pfi, leg.t_cfi, "
                      + "                 leg.t_principal, leg.t_price, leg.t_cost, leg.t_nkd,leg.t_totalcost "
                      + "            FROM op, ddl_tick_dbt tick, ddl_leg_dbt leg "
                      + "           WHERE tick.t_dealid = op.t_dealid AND leg.t_LegKind = " + LEG_KIND_DL_TICK + " AND leg.t_dealId = op.t_dealid) t, "
                      + "         u_vtb_dlcontr_tmp dlcontr, "
                      + "         dfininstr_dbt fi "
                      + "   WHERE dlcontr.t_dlcontrid = t.t_dlcontrid AND fi.t_FIID = t.t_PFI "
                      + "ORDER BY t.t_dlcontrid, t.t_pfi, t.t_taxownbegdate ";

    cmd = DL_RsdCommand( "SELECT COUNT(DISTINCT t_DLContrID) t_cnt FROM ( " + query + " ) ");

    rs = cmd.Execute();

    if(rs.MoveNext())
      nrec = int(rs.cnt);
    end;

    if(nrec == 0)
      msgbox("Нет данных для отчета");
      return;
    else
      msgbox("Количество ДБО с данными для отчета: " + nrec);
    end;

    m_ProgressIndicator.Start(nrec, "Печать отчетов", "Печать отчетов");

    cmd = RsdCommand(query);
    cmd.AddParam("", RSDBP_IN, m_Form.RepDate);
    cmd.AddParam("", RSDBP_IN, m_Form.RepDate);
    cmd.AddParam("", RSDBP_IN, m_Form.RepDate);
    cmd.NullConversion = true;
    rs = RsdRecordSet(cmd);

    next = rs.MoveNext();
    while(next)
      cur_dlcontr = int(rs.Value("t_DlContrId"));

      SetActiveSheet("Справка");

      stat = false;
      msg = "";

      PrintFormatString("",
                        "N1_Contract",     rs.Value("t_Number"),
                        "N1_ContrBegDate", rs.Value("t_DateBegin"),
                        "N1_ClientName",   rs.Value("t_ClientName"),
                        "N1_EKK",          "(" + rs.Value("t_EKK") + ")",
                        "N1_RepDate",      m_Form.RepDate);

      CopyAllSheetInTotalBook("Справка",false, "N1_ReportHead", 0, "Справка");
      CopyAllSheetInTotalBook("Справка",false, "N1_TableHead", 0, "Справка");

      RegisterTable("N1_TableLine", "", "N1_AvrName", "N1_ISIN", "N1_DateBuy", "N1_Date", "N1_Rate", "N1_Qnty", "N1_Currency", "N1_Price", "N1_Nominal", "N1_Cost", "N1_NKD", "N1_CostNKD", "N1_Comis");

      while( next AND (cur_dlcontr == rs.Value("t_DlContrId")) )
        PrintTableLine("N1_AvrName", rs.Value("t_AvrName"),         null,
                       "N1_ISIN",    rs.Value("t_ISIN"),            null,
                       "N1_DateBuy", date(rs.Value("t_DateBuy")),   null,
                       "N1_Date",    date(rs.Value("t_DateBuy")),   null,
                       "N1_Rate",    rs.Value("t_Rate"),            null,
                       "N1_Qnty",    rs.Value("t_Principal"),       null,
                       "N1_Currency",rs.Value("t_Currency"),        null,
                       "N1_Price",   rs.Value("t_Price"),           null,
                       "N1_Nominal", rs.Value("t_Nominal"),         null,
                       "N1_Cost",    rs.Value("t_Cost"),            null,
                       "N1_NKD",     rs.Value("t_NKD"),             null,
                       "N1_CostNKD", rs.Value("t_TotalCost"),       null,
                       "N1_Comis",   rs.Value("t_Comis"),           null);

        Number       = rs.Value("t_Number");
        ContrBegDate = date(rs.Value("t_DateBegin"));
        ClientName   = rs.Value("t_ClientName");
        EKK          = rs.Value("t_EKK");
        Email        = rs.Value("t_Email");
        PartyID      = rs.Value("t_PartyId");

        next = rs.MoveNext();
      end;

      EndTable();
      CopyAllSheetInTotalBook("Справка",false, GetTableRange(), 0, "Справка");
      SaveSubTotalBook(TMP_DIR + "\\" + ClientName + " " + string(EKK), false);
      CreateTotalBook();

      RepFilePath = GetFullReportFileNames()(GetFullReportFileNames().Size-1);
      SplitFile(RepFilePath, RepFileName, _ext);

      msgbox(RepFileName + _ext + ";Файл отчета создан");

      stat = CopyFile(RepFilePath, DirName + "\\" + RepFileName + _ext);
      if(not stat)
        RepFilePath = "$" + RepFilePath;
        stat = CopyFile(RepFilePath, DirName + "\\" + RepFileName + _ext);
      end;

      if(stat)
        msgbox(RepFileName + _ext + ";Файл отчета перемещен в конечную директорию");
      else
        msgbox(RepFileName + _ext + ";Не удалось переместить файл отчета в конечную директорию");
        i = i + 1;
        continue;
      end;

      if (SendMessage(ClientName, Number, ContrBegDate, /*RepFilePath*/ (DirName + "\\" + RepFileName + _ext), RepFileName, Email, PartyID))
         if( ( not DisconnectObjAttr(OBJTYPE_BROKERCONTR_DL, string(cur_dlcontr:o:34), MESSAGE_SENT_ATTR_GRP) ) OR
            ( not ConnectObjAttr(stat, OBJTYPE_BROKERCONTR_DL, string(cur_dlcontr:o:34), MESSAGE_SENT_ATTR_GRP, 1/*Да*/, null, null, {curdate}) ))
             msgbox( RepFileName + ";Ошибка при установке Категории для ДБО " + Number );
         end;
      end;

      RemoveFile(RepFilePath);

      i = i + 1;
      m_ProgressIndicator.Update(i, nrec);
    end;

   m_ProgressIndicator.Stop();
  END;

  MACRO EndReport
    SaveTotalBook();
  END;

  InitDL_CReportTemplate( VTBAvoirInfoPanel(), "Report_VTBAvoirInfo.xlsx", TRUE, FALSE, NULL, POI_MODE );
END;


VTBAvoirInfoReport().Run();
exit(1);
