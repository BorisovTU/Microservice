/**
 @file dbo_qi_ProlongNotification.mac
 @brief Функционал отправки уведомления о пролонгации статуса КИ в реестре квалифицированных инвесторов
 
 Файл содержит функционал для формирования уведомления о пролонгации статуса КИ субъекта в реестре
 квалифицированных инвесторов и его отправки по e-mail.
 
  # tag
 - functional_block:Отчетность_Клиент
 - code_type:API
 - code_type:Report
 - Квалификация
 - КИ
 - Квалифицированный инвестор
 - Пролонгация КИ
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |25.01.2024 |Топорков Д.В.|BIQ-16875 BOSS-194 BOSS-1775 BOSS-2093                    | Создание
 */
 
import rsd;
import "dbo_qi_func.mac"; // CSigner, CClient
import "dlcnst.inc";
import "logger.mac";
import "dlutils.mac";

var logger;

/**
@brief Функция выбора значения в зависимости от условия
@param[in] condition Условие для выбора (true/false !0/0)
@param[in] valueTrue Значение которое вернет функция при condition = true (тип Variant)
@param[in] valueFalse Значение которое вернет функция при condition = false (тип Variant)
@return valueTrue при condition=true, valueFalse при condition=false (тип Variant)
*/
private macro IIF(condition, resultOnTrue, resultOnFalse)
   if (condition)
      return resultOnTrue;
   else
      return resultOnFalse;
   end;
end;

/**
@brief Класс с параметрами уведомлений по пролонгации КИ
*/
class (CQINotificationParams) CQIProlongParams()
  var prolongDate: Date;      ///< Дата пролонгации статуса субъекта КИ
  var controlDate: Date;      ///< Дата проверки статуса субъекта КИ
  
  /**
  @brief Процедура начальной инициализации класса
  */
  private macro Init()
    prolongDate = Date();
    controlDate = Date();
  end;
  //--- конструктор ---
  InitCQINotificationParams();
  Init();
end; // CQIProlongParams

/**
@brief Функция записи сообщения в логгер
@param[in] msg - Сообщение
@param[in] logType - Тип сообщения (если не указано, то будет интерпритировано как LOGTYPE_INFO
@return Переданное значение msg
*/
private macro LogMessage(msg, logType)
  logger.log(msg, logType);
  return msg;
end;

/**
@brief Процедура записи в логгер параметров Уведомления о пролонгации
@param[in] prolongParams Параметры уведомления о пролонгации
*/
private macro LogQIProlongParams(prolongParams: CQIProlongParams)
  LogMessage("Формирование уведомления для клиента " + prolongParams.client.shortName + ", договор №" + prolongParams.client.contractNumber);
  LogMessage("Дата уведомления: " + prolongParams.notificationDate);
  LogMessage("Дата пролонгации КИ: " + prolongParams.prolongDate);
  LogMessage("Дата проверки КИ: " + prolongParams.controlDate);
end;

/**
@brief Функция проверки параметров Уведомления о пролонгации
@param[in] prolongParams Параметры уведомления о пролонгации
@param[out] error Текст ошибки
@return true В случае корректных параметров
@return false Параметры некорректны (см. error)
*/
private macro CheckQIProlongConditions(prolongParams: CQIProlongParams, error: @String): Bool
  error = "";
  /// Общие проверки
  if (StrLen(error) == 0)
    if ((ValType(prolongParams.dlContrID) == V_UNDEF) or (prolongParams.dlContrID <= 0))
      error = "Не задан договор обслуживания";
    elif ((ValType(prolongParams.client) == V_UNDEF) or (ValType(prolongParams.client.emails) == V_UNDEF) or (prolongParams.client.emails.size == 0))
      error = "У клиента отсутствует электронная почта для рассылки";
    elif ((ValType(prolongParams.prolongDate) == V_UNDEF) or (prolongParams.prolongDate == ZeroDate))
      error = "Не задана дата пролонгации";
    end;
  end;

  /// Проверка наличия договора в реестре квалифицированных инвесторов
  if (StrLen(error) == 0)
    var sql;
    if(not GetRegValueSwitchQI()) //NEW PARAM
      sql = "SELECT * FROM dScQinv_dbt WHERE t_sfContrID = (SELECT t_sfContrID FROM dDlContr_dbt WHERE t_dlContrID = " + prolongParams.dlContrID + ") ORDER BY t_regDate DESC"
    else
      sql =   " SELECT * " +
              "  FROM dScQinv_dbt   " +
              " WHERE t_partyid  = (SELECT sf.t_partyid   " +
              "                      FROM dDlContr_dbt d JOIN dsfcontr_dbt sf ON d.t_sfcontrid = sf.t_id   " +
              "                      WHERE t_dlContrID = " + prolongParams.dlContrID + ") " +
              " ORDER BY t_regDate DESC ";
    end;

    var qInvRec = TRsbDataSet(sql);

    if(not GetRegValueSwitchQI()) //NEW PARAM
      if (qInvRec.MoveNext())
        if (qInvRec.state == 0)
          error = "Договор исключен из реестра КИ";
        elif (qInvRec.state != 1)
          error = "Неизвестный статус договора в реестре квалифицированных инвесторов";
        elif (qInvRec.changeDate != prolongParams.prolongDate)
          error = "Заданная дата пролонгации (" + String(prolongParams.prolongDate:f) + ") не соответствует дате изменения статуса в реестре КИ (" + String(Date(qInvRec.changeDate):f) + ")";
        elif (qInvRec.controlDate != prolongParams.controlDate)
          error = "Заданная дата проверки (" + String(prolongParams.controlDate:f) + ") не соответствует дате проверки в реестре КИ (" + String(Date(qInvRec.controlDate):f) + ")";
        end;
      else
        error = "Договор отсутствует в реестре квалифицированных инвесторов";
      end;
    else
      if (qInvRec.MoveNext())
        if (qInvRec.state == 0)
          error = "Клиент исключен из реестра КИ";
        elif (qInvRec.state != 1)
          error = "Неизвестный статус клиента в реестре квалифицированных инвесторов";
        elif (qInvRec.changeDate != prolongParams.prolongDate)
          error = "Заданная дата пролонгации (" + String(prolongParams.prolongDate:f) + ") не соответствует дате изменения статуса в реестре КИ (" + String(Date(qInvRec.changeDate):f) + ")";
        elif (qInvRec.controlDate != prolongParams.controlDate)
          error = "Заданная дата проверки (" + String(prolongParams.controlDate:f) + ") не соответствует дате проверки в реестре КИ (" + String(Date(qInvRec.controlDate):f) + ")";
        end;
      else
        error = "Клиент отсутствует в реестре квалифицированных инвесторов";
      end;
    end;

    
  end;

  if (StrLen(error) > 0)
    return false;
  end;

  return true;
end;

/**
@brief Функция получения объекта класса CStorageInfo (данные об используемых директориях и файлах в них)
@return Настроенный объекта класса CStorageInfo

# Предполагается следующее использование директорий:
- WORKTAG   Директория WorkFile сервера приложений (установлена в конструкторе CStorageInfo)
- TXTTAG    Директория TxtFile сервера приложений (установлена в конструкторе CStorageInfo)
# Предполагается следующее использование файлов:
- TXTTAG/LOGTAG       Файл для записи лога (возможно удаляется в случае отсутствия ошибок)
*/
private macro GetQIStorageInfo(): CStorageInfo
  var storageInfo: CStorageInfo = CStorageInfo("Уведомление_квал_инвест_");

  /// Добавим информацию для файла логгирования
  storageInfo.AddFile(TXTTAG, LOGTAG, "QiProlong_" + GetDateTimeMark(Date(), Time()) + "_" + String(Random(999)), "txt", false);
  return storageInfo;
end;

/**
@brief Функция создания сообщения для ДБО
@param[in] prolongParams Параметры уведомления о пролонгации
@param[out] error Текст ошибки
@return Индекс записи в таблице dDlContrMsg_dbt
@return null В случае ошибки записи

Предполагается, что файл уведомления RESULTTAG/PDFTAG уже создан
*/
private macro CreateQIProlongDBOMessage(prolongParams: CQIProlongParams, error: @String): Integer
  var dlContrMsg = Tbfile("dlcontrmsg.dbt", "W", 0);
  
  dlContrMsg.clear();
  dlContrMsg.rec.DlContrID = prolongParams.dlContrID;
  dlContrMsg.rec.Kind = PROLONG_QI;
  dlContrMsg.rec.CreateDate = Date();
  dlContrMsg.rec.CreateTime = Time();
  dlContrMsg.rec.SendDate = ZeroDate;
  dlContrMsg.rec.SendTime = ZeroTime;
  dlContrMsg.rec.IsOnlineSendMes = UNSET_CHAR;
  dlContrMsg.rec.MarketID = -1;
  dlContrMsg.rec.SendMesState = 0;
  
  if(not dlContrMsg.Insert())
    error = "Ошибка создания сообщения для ДБО";
    return 0;
  end;
  
  LogMessage("Создано сообщение к ДБО с ID = " + dlContrMsg.rec.ID);

  return dlContrMsg.rec.ID;
end;

/**
@brief Процедура отправки e-mail
@param[in] prolongParams Параметры уведомления о пролонгации
@param[out] error Текст ошибки
*/
private macro SendQIProlongEmail(prolongParams: CQIProlongParams, error: @String)
  var emailProcessor = c_email_proc_env();
  var isLegalPerson: Bool = prolongParams.client.legalForm == PTLEGF_INST;
  var registryPath: String = GetHiddenRecipientsRegistryPath(isLegalPerson);
  var hiddenRecipients: TArray = GetHiddenRecipients(registryPath);
  
  if (hiddenRecipients.size == 0)
    error = "Не заданы системные настройки процесса: Направлять скрытую копию по " + IIF(isLegalPerson, "ЮЛ", "ФЛ");
    return;
  end;
  
  /// Формируем письмо
  RunProcByParamArray(R2M(emailProcessor, "m_add_email_to_list"), prolongParams.client.emails); ///< Список получателей
  RunProcByParamArray(R2M(emailProcessor, "m_add_email_to_bcc_list"), hiddenRecipients);        ///< Список получателей скрытой копии
  
  var emailSubject: String = "Уведомление об успешной пролонгации до " + String(prolongParams.controlDate:f) + " статуса Квалифицированный инвестор " + prolongParams.client.shortName; 
  emailProcessor.m_set_msg_head(emailSubject);                                                  ///< Тема письма
  
  var emailBody: String = 
    "Уважаемый Клиент!\n" +
    "\n" +
    "АО \x22Россельхозбанк\x22 уведомляет Вас, что " + String(prolongParams.prolongDate:f) + " был успешно продлен до " + String(prolongParams.controlDate:f) + " статус квалифицированного инвестора " + prolongParams.client.fullName +
    "\n";
  emailProcessor.m_add_row_to_msg_text(emailBody);                                              ///< Текст 

  /// Сохраняем для отправки плановой процедурой 
  emailProcessor.m_save_to_submit_synch();
  /// Отправляем
  emailProcessor.m_submit_email_synch();

  if(emailProcessor.m_get_error_status())
    if (ValType(error) == V_STRING)
      error = "Сообщение не отправлено: " + emailProcessor.get_service_msg();
    end;
  else
    LogMessage("Письмо отправлено");
  end;
end;

/**
@brief Функция формирования и отправки уведомления о пролонгации статуса квалифицированного инвестора
@param[in] prolongParams Параметры уведомления о пролонгации
@return V_STRING Текст ошибки
*/
macro QIProlongNotification(prolongParams: CQIProlongParams): String
  logger = CTxtLogger();
  
  var storage: CStorageInfo = GetQIStorageInfo();
  var error: String = "";
  var dlContrMsgID: Integer = 0;

  logger.InitFile(storage.FilePath(TXTTAG, LOGTAG));
  LogQIProlongParams(prolongParams);
  
  /// Проверка параметров операции
  if (not CheckQIProlongConditions(prolongParams, @error))
    error = "Ошибка проверки параметров:|" + error;
  end;
  
  /// Создание сообщения dDlContrMsg
  if (StrLen(error) == 0)
    dlContrMsgID = CreateQIProlongDBOMessage(prolongParams, @error);
  end;
  
  /// Отправка e-mail
  if (StrLen(error) == 0)
    SendQIProlongEmail(prolongParams, @error);
  end;
  
  if (StrLen(error) == 0)
    // Обновим дату отправки сообщения
    ExecSql("UPDATE dDlContrMsg_dbt SET t_sendDate = ?, t_sendTime = ? WHERE t_ID = ? ", makeArray(SQLParam("", Date()), SQLParam("", Time()), SQLParam("", dlContrMsgID)));
  end;
  
  if (StrLen(error) > 0)
    LogMessage(error, LOGTYPE_ERROR);
  end;
  
  logger.close();
  // Если нет ошибок, то отметим файл лога на удаление
  if ((logger.GetMessagesCount(LOGTYPE_WARNING) == 0) and (logger.GetMessagesCount(LOGTYPE_ERROR) == 0))
    storage.GetFile(TXTTAG, LOGTAG).IsTemporary = true;
  end;
  
  return error;
  
onError (err)
  error = LogMessage("Ошибка выполнения:|" + err.Module + " строка " + err.Line + "|" + err.message, LOGTYPE_ERROR);
  LogMessage(GetFullErrorMessage(err), LOGTYPE_ERROR);
  logger.close();
  
  return error;
end;