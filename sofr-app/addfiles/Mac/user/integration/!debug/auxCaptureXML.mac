/*--------------------------------------------------------------*/
/* Макрос с инструментарием для отладки интеграционных сервисов */
/*                                                              */
/* Автор: Карпов В., Мамичев А.                                 */
/*--------------------------------------------------------------*/

import serviceaction, rsd;

/* Функции для работы со стандартным потоком вывода - begin */
private var outFluxStringBuff : string;

macro uOutFluxCapture(strVal)
   outFluxStringBuff = String(outFluxStringBuff, " ", trim(StrSubst(StrSubst(StrSubst(strVal, "\n", ""), "\r", ""), "\t", "")));
end;

macro uStartOutFluxCapture()
   outFluxStringBuff = "";
   SetOutHandler("uOutFluxCapture");
end;

macro uEndOutFluxCapture();
   SetOutHandler();
   return trim(outFluxStringBuff);
end;
/* Функции для работы со стандартным потоком вывода - end */


/* Процедура для извлечения названия макроса и функции из сообщения запроса */
macro u_get_req_msg_prm(v_req_msg : @string,
                        v_macro_name : @string,
                        v_func_name : @string)
   var ret = true;
   var start_word = "RunMacro.";
   var start_sym;
   var end_sym;

   start_sym = index(v_req_msg, start_word);
   if(start_sym != 0)
      start_sym = start_sym + strlen(start_word);
      end_sym = index(v_req_msg, ".", start_sym) - 1;
      if(end_sym != -1)
         v_macro_name = trim(substr(v_req_msg, start_sym, (end_sym - start_sym + 1)));
      else
         ret = false;
      end;
   else
      ret = false;
   end;

   if(ret)
      start_sym = end_sym + 2;
      end_sym = index(v_req_msg, "<", start_sym) - 1;
      if(end_sym != -1)
         v_func_name = trim(substr(v_req_msg, start_sym, (end_sym - start_sym + 1)));
      else
         ret = false;
      end;
   end;

   return ret;
end;


/*-------------------------------------------------------------------------------*/
/* Функция извлекает текст сообщения из таблицы dxr_log_dbt                      */
/* На вход необходимо передать id входящего сообщения                            */
/* На выходе функции - извлеченное сообщение                                     */
/* Во входящие параметры macroName и serviceName функция отдает t_macro и t_func */
/*-------------------------------------------------------------------------------*/

macro uGetXrLogMsg(log_id, macroName, serviceName)
   var sql_s, cmd_s, rs_s;
   var sql, cmd;
   var in_msg_id;
   var aux_buff_m, aux_buff_f;
   var was_found_prm = false;
   var msg_str = "";       /* Буффер сообщения */
   var read_len = 1000;    /* Ограничение на количество символов, извлекаемых за одну итерацию */
   var clob_offset = 1;    /* Начальное смещение в clob'е - начинаем с первого символа */
   var read_status = true; /* Статус окончания сообщения в clob'е */

   sql_s =         "   SELECT log_m.t_id, log_ex.t_macro, log_ex.t_func ";
   sql_s = sql_s + "     FROM dxr_log_dbt log_m ";
   sql_s = sql_s + "LEFT JOIN dxr_log_dbt log_ex ON log_ex.t_reqid = log_m.t_reqid AND log_ex.t_kind = :v_kind_mac ";
   sql_s = sql_s + "    WHERE log_m.t_kind = :v_kind_in ";
   sql_s = sql_s + "      AND log_m.t_reqid = (SELECT log_q.t_reqid ";
   sql_s = sql_s + "                             FROM dxr_log_dbt log_q ";
   sql_s = sql_s + "                            WHERE log_q.t_id = :v_id) ";

   cmd_s = RSDCommand(sql_s);
   cmd_s.addParam("v_kind_mac", RSDBP_IN, 2);
   cmd_s.addParam("v_kind_in", RSDBP_IN, 1);
   cmd_s.addParam("v_id", RSDBP_IN, log_id);
   rs_s = RSDRecordSet(cmd_s);

   if(rs_s.movenext())
      in_msg_id = rs_s.value("t_id");
      aux_buff_m = ValType(rs_s.value("t_macro"));
      aux_buff_f = ValType(rs_s.value("t_func"));
      if(    (aux_buff_m != V_UNDEF) and (aux_buff_m != 26)
         and (aux_buff_f != V_UNDEF) and (aux_buff_f != 26))

         macroName = rs_s.value("t_macro");
         serviceName = rs_s.value("t_func");
         SetParm(1, macroName);
         SetParm(2, serviceName);

         was_found_prm = true;
      end;
   else
      msgbox(string("Сообщение с id=", log_id, " не найдено в журнале"));
      Exit(1);
   end;

   rs_s.close(); cmd_s.close(); rs_s = null; cmd_s = null;

   sql =       "DECLARE                                         ";
   sql = sql + "   xrlog_msg_loc      CLOB;                     ";
   sql = sql + "   xrlog_msg_id       INTEGER := :p_msg_id;     ";
   sql = sql + "   xrlog_msg_offset   INTEGER := :p_msg_offset; ";
   sql = sql + "   xrlog_msg_read     BINARY_INTEGER := 1000;   ";
   sql = sql + "   xrlog_msg_part     VARCHAR2 (2000);          ";
   sql = sql + "   ret_length         BINARY_INTEGER;           ";
   sql = sql + "   ret_msg_part       VARCHAR2 (2000);          ";
   sql = sql + "BEGIN                                           ";
   sql = sql + "   SELECT t_message                             ";
   sql = sql + "     INTO xrlog_msg_loc                         ";
   sql = sql + "     FROM dxr_log_dbt                           ";
   sql = sql + "    WHERE t_id = xrlog_msg_id;                  ";
   sql = sql + "   DBMS_LOB.read (xrlog_msg_loc,                ";
   sql = sql + "                  xrlog_msg_read,               ";
   sql = sql + "                  xrlog_msg_offset,             ";
   sql = sql + "                  xrlog_msg_part);              ";
   sql = sql + "   :ret_length_s := xrlog_msg_read;             ";
   sql = sql + "   :ret_msg_part := xrlog_msg_part;             ";
   sql = sql + "EXCEPTION                                       ";
   sql = sql + "   WHEN NO_DATA_FOUND                           ";
   sql = sql + "   THEN                                         ";
   sql = sql + "      :ret_length_e := 0;                       ";
   sql = sql + "END;                                            ";

   while(read_status)
      cmd = RSDCommand(sql);
      cmd.addParam("p_msg_id", RSDBP_IN, in_msg_id);
      cmd.addParam("p_msg_offset", RSDBP_IN, clob_offset);
      cmd.addParam("ret_length_s", RSDBP_OUT, V_INTEGER);
      cmd.addParam("ret_msg_part", RSDBP_OUT, V_STRING, 2000);
      cmd.addParam("ret_length_e", RSDBP_OUT, V_INTEGER);
      cmd.execute();

      read_len = cmd.value("ret_length_s");
      if(ValType(read_len) == V_UNDEF)
         read_len = cmd.value("ret_length_e");
      end;
      if(read_len > 0)
         msg_str = string(msg_str, cmd.value("ret_msg_part"));
         clob_offset = clob_offset + read_len;
         read_status = true;

         cmd.close(); cmd = null;
      else
         read_status = false;

         cmd.close(); cmd = null;
      end;
   end;

   /* Если до этого не удалось получить название макроса и функции */
   if(not was_found_prm)
      if(u_get_req_msg_prm(@msg_str, @macroName, @serviceName))
         SetParm(1, macroName);
         SetParm(2, serviceName);
      else
         msgbox(string("Не найдены наименования макроса и функции"));
         Exit(1);
      end;
   end;

   return msg_str;
end;


/* Функция для преобразования текста XML-сообщения в объект */

macro uConvertXMLtoRSL(xml_msg : string) : object
   var res_obj = null;
   var stat;

/* Данную строку стоит раскомментировать, если работа идет с исходными (не xmlrpc) сообщениями */
/* В этом случае стоит также указать актуальный путь к преобразованиям                         */
/*   var res_xml;                                                                              */
/*   res_xml = ApplyXSLT(FRONTMessage, ".//xsl//Interbank_to_RSBank_0_5.xsl");                 */

   stat = ConvertToRSL(xml_msg, res_obj);

   return res_obj;
end;


/* Функция получения контекстного значения (maa) */

macro uGetContextVal()
   var cmd, rs;
   var ret;
   var auxBuff;
   cmd = RSDCommand(RSLDEFCON, "SELECT SYS_CONTEXT('CLIENTCONTEXT', 'usname') FROM DUAL");
   cmd.execute();
   rs = RSDRecordSet(cmd);
   if(rs.movenext())
      auxBuff = ValType(rs.value(0));
      if((auxBuff == 26) or (auxBuff == V_UNDEF))
         ret = 0;
      else
         ret = rs.value(0);
      end;
   else
      ret = 0;
   end;
   rs.close();
   cmd.close();
   rs = null;
   cmd = null;

   return ret;
end;


/* Функция сохранения контекстного значения (maa) */

macro uSetContextVal(cntVal)
   var cmd;
   cmd = RSDCommand(RSLDEFCON, "begin DBMS_SESSION.SET_CONTEXT('CLIENTCONTEXT', 'usname', TO_CHAR(?)); end;");
   cmd.addparam("nameValue", RSDBP_IN, cntVal);
   cmd.execute();
   cmd.close();
   cmd = null;
end;