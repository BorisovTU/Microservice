/**
 @file 		load_entcompare.mac
 @brief 	Загрузка файла проводок, загруженных в БИСКВИТ из СОФР

 # tag
 - functional_block: Отчетность_внутренняя
 - code_type: Report
 - code_type: GUI

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |20.11.2024 |Велигжанин А.В.|DEF-76687                                       |LoadEntryForCompare(..., p_Format)
 |           |               |                                                |Доработка загрузка с учетом формата файла, в связи с 
 |           |               |                                                |появлением полей наименования и счета плательщика и получателя
 |20.05.2018 |А.Киселев      |                                                |Создан
*/
import oralib, likepy, globals, rsexts, "dlutils.mac";

private var File_Name,
            Load_Path,
            File_Name_CtrlFile = "", // Имя управляющего файла для SQL*Loader
            Log_File_Name = "",
            Bad_File_Name = "",
            Dsc_File_Name = "";



// Определение имени файла из его ПОЛНОГО имени
private macro ExtractShortName(NameOperFile)
 var Flag = True;

 NameOperFile = Trim(NameOperFile);
 while (Flag)
   if(Index(NameOperFile,"\\") != 0)
     Flag = TRUE
   else
     Flag = FALSE
   end;
   if(Flag)
     NameOperFile = SubStr(NameOperFile,Index(NameOperFile,"\\")+1)
   end
 end;
 return NameOperFile
end;

//отключаем индексы для ускорения загрузки
private macro index_off()
  execSQL("ALTER INDEX ULOADENTCOMPARE_IDX1 UNUSABLE;",null,false);
  execSQL("ALTER INDEX ULOADENTFORCOMPARE_IDX2 UNUSABLE;",null,false);
end;

//включаем индексы
private macro index_on()
  execSQL("ALTER INDEX ULOADENTCOMPARE_IDX1 REBUILD;",null,false);
  execSQL("ALTER INDEX ULOADENTFORCOMPARE_IDX2 REBUILD;",null,false);
end;

/*удаление вспомогательных файлов*/
private macro DeleteFile
  //RemoveFile(File_Name);
  RemoveFile(Log_File_Name);
  RemoveFile(Bad_File_Name);
  RemoveFile(File_Name_CtrlFile);
end;

private macro CopyDestFiles(datafilepath_orig, datafilemask, datafilepath:@string, datafilename:@string)

  datafilepath = datafilepath_orig+"\\";
  var List = TDirList(datafilepath + datafilemask, "f");
  var txtpath = GetAbsoluteTxtPath()+"\\";

  List.Sort(0);
  if( List.Count == 0 )
    datafilepath = "$"+datafilepath;
    List = TDirList(datafilepath + datafilemask, "f");
    List.Sort(0);
  end;

  if( List.Count == 0 )
    datafilepath = PrcPath(datafilepath_orig);
    List = TDirList(datafilepath + datafilemask, "f");
    List.Sort(0);
  end;

  if( List.Count == 0 )
    datafilename = "";
    RunError( "Ошибка открытия файла данных. Не найден файл по маске " + datafilepath_orig + datafilemask);
  else
    datafilename = List.name(0);

    if((txtpath != "") and (txtpath != datafilepath))
      var stat = CopyFile(datafilepath+datafilename, txtpath+datafilename);
      datafilepath = txtpath;
      datafilename = datafilepath+datafilename;
    end;

  end;

end;

/* DEF-76687, Загрузка по формату 0 (как обычно, без счетов и наименований плательщика и получателя) */
private macro Load_Format_0()
  SetOutput(File_Name_CtrlFile); //Создаем CONTROL-file для загрузки файла проводок, загруженных в БИСКВИТ из СОФР
  [LOAD DATA CHARACTERSET CL8MSWIN1251
   INFILE *
   APPEND
   INTO TABLE uloadentforcompare_dbt
   Fields terminated by "|"
   (
    T_COUNTER,
    T_USERID,
    T_OPDATE DATE "DD/MM/YY",
    T_DOCDATE DATE "DD/MM/YY",
    T_ACCT_DB,
    T_ACCT_CR,
    T_CURRENCY,
    T_AMTCUR "REPLACE(:T_AMTCUR, '.', ',')",
    T_AMTRUB "REPLACE(:T_AMTRUB, '.', ',')",
    T_DETAILS CHAR(600),
    T_DOCNUM,
    T_ACCTRNID "CASE WHEN INSTR( :T_ACCTRNID, 'paym') = 1 THEN REPLACE(:T_ACCTRNID, 'paym', '-') WHEN INSTR( :T_ACCTRNID, 'trnpm') = 1 THEN REPLACE(:T_ACCTRNID, 'trnpm', '') WHEN INSTR( :T_ACCTRNID, 'nptxop') = 1 THEN REPLACE(:T_ACCTRNID, 'nptxop', '-') WHEN INSTR( :T_ACCTRNID, 'inpaym') = 1 THEN REPLACE(:T_ACCTRNID, 'inpaym', '-') WHEN INSTR( :T_ACCTRNID, 'depcom') = 1 THEN REPLACE(:T_ACCTRNID, 'depcom', '-')  ELSE :T_ACCTRNID END",
    T_IDBISCOTTO,
    T_REQID,
    NOT_LOADED
   )
   ];
  SetOutput(Null, True);
  Close(File_Name_CtrlFile);
end;

/* DEF-76687, Загрузка по формату 1 (со счетами и наименованиями плательщика и получателя) */
private macro Load_Format_1()
  SetOutput(File_Name_CtrlFile); //Создаем CONTROL-file для загрузки файла проводок, загруженных в БИСКВИТ из СОФР
  [LOAD DATA CHARACTERSET CL8MSWIN1251
   INFILE *
   APPEND
   INTO TABLE uloadentforcompare_dbt
   Fields terminated by "|"
   (
    T_COUNTER,
    T_USERID,
    T_OPDATE DATE "DD/MM/YY",
    T_DOCDATE DATE "DD/MM/YY",
    T_ACCT_DB,
    T_ACCT_CR,
    T_CURRENCY,
    T_AMTCUR "REPLACE(:T_AMTCUR, '.', ',')",
    T_AMTRUB "REPLACE(:T_AMTRUB, '.', ',')",
    T_DETAILS CHAR(600),
    T_DOCNUM,
    T_ACCTRNID "CASE WHEN INSTR( :T_ACCTRNID, 'paym') = 1 THEN REPLACE(:T_ACCTRNID, 'paym', '-') WHEN INSTR( :T_ACCTRNID, 'trnpm') = 1 THEN REPLACE(:T_ACCTRNID, 'trnpm', '') WHEN INSTR( :T_ACCTRNID, 'nptxop') = 1 THEN REPLACE(:T_ACCTRNID, 'nptxop', '-') WHEN INSTR( :T_ACCTRNID, 'inpaym') = 1 THEN REPLACE(:T_ACCTRNID, 'inpaym', '-') WHEN INSTR( :T_ACCTRNID, 'depcom') = 1 THEN REPLACE(:T_ACCTRNID, 'depcom', '-')  ELSE :T_ACCTRNID END",
    T_IDBISCOTTO,
    T_REQID,
    T_PAYERNAME,
    T_RECEIVERNAME,
    T_PAYERACCOUNT,
    T_RECEIVERACCOUNT,
    NOT_LOADED
   )
   ];
  SetOutput(Null, True);
  Close(File_Name_CtrlFile);
end;


//Загрузка файла проводок, загруженных в БИСКВИТ из СОФР
macro LoadEntryForCompare( File_Name :string, ReqId :string, Fl_SetDialog :integer, p_Format :integer ) :bool
var DSN :string = "",
    USER_ID :string = "",
    PASSWORD :string = "",
    CONNSTRING :string = GetIniString("CONNSTRING", "rsreq.ini"),
    String_sqlldr :string = "",
    state :integer = 0,
    ldr_param :string = "bindsize=100000000 readsize=1000000 columnarrayrows=10000000 rows=100000 errors=100000 ",
    Query :string = "",
    x_Ok: BOOL,
    Params :TArray = TArray();

  var ShortFileName = "";
  var PathFileName = "", PathFileName_reg = "";

  //создаём блокировщик по процессу, чтобы лишний раз не делать выключение и включение индексов
  var _locker = UniConcurrentLocker("LoadEntryForCompare", 3600, true);

  if( ( ValType(Fl_SetDialog) == V_UNDEF ) or ( ValType(Fl_SetDialog) == 26 ) )
   Fl_SetDialog = 0;
  end;

  ShortFileName = ExtractShortName(File_Name);
  PathFileName_reg = StrSubst( File_Name, "\\" + ShortFileName, "" );
  if( ( ValType(File_Name) == V_UNDEF ) or  ( ValType(File_Name) == 26 ) or ( File_Name == "" ) or ( ShortFileName == "" )
        or ( FindPath( ShortFileName, PathFileName_reg ) == "" ) )
    return false;
  end;

  if( Fl_SetDialog == 1 )
    CopyDestFiles(PathFileName_reg, ShortFileName, @PathFileName, @File_Name);
  else
    PathFileName = PathFileName_reg;
  end;

  if( ( ValType(ReqId) != V_UNDEF ) and  ( ValType(ReqId) != 26 ) and ( ReqId != "" ) )
    Query = " DELETE uloadentforcompare_dbt WHERE T_REQID = :p_ReqId";
    Params = makeArray( SQLParam("p_ReqId", ReqId) );
    execSQL( Query, Params );
  end;

  DSN = SubStr(CONNSTRING, 5, index(CONNSTRING,";") - 5);
  CONNSTRING = SubStr(CONNSTRING, index(CONNSTRING,";") + 1);
  USER_ID = SubStr(CONNSTRING, index(CONNSTRING,"user id=") + 8, index(CONNSTRING,";") - 9);
  CONNSTRING = SubStr(CONNSTRING, index(CONNSTRING,";") + 1);
  PASSWORD = SubStr(CONNSTRING, index(CONNSTRING,"password=") + 9);

  String_sqlldr = "userid=" + USER_ID + "/" + PASSWORD + "@" + DSN;
  File_Name_CtrlFile = PathFileName;

  Log_File_Name = File_Name_CtrlFile + "\\CTL_" + string(UserNumber) + ".log";
  Bad_File_Name = File_Name_CtrlFile + "\\CTL_" + string(UserNumber) + ".bad";
  Dsc_File_Name = File_Name_CtrlFile + "\\CTL_" + string(UserNumber) + ".dsc";
  File_Name_CtrlFile = File_Name_CtrlFile + "\\CTL_" + string(UserNumber) + ".ctl";

  // DEF-76687, загрузка с учетом формата в связи с появлением полей наименования и счета плательщика и получателя
  if(p_Format == 1)
     Load_Format_1();
  else
     Load_Format_0();
  end;

  BegAction(Null, "Отключаем индескы", false);
  //если получается заблокировать монопольно, то значит других процессов нет, и тольк тогда отключаем индексы
  //иначе они уже отключены
  var idxOffLocker = UniConcurrentLocker("LoadEntryForCompare");
  if (idxOffLocker.Lock())
    index_off();
  end;
  idxOffLocker = null;
  if (not _locker.Lock())
    RunError("Не удалось заблокировать процесс по идентификатору "+ _locker.GetLockId())
  end;
  EndAction(10);

  BegAction(Null, "Загружаем файл в RS-Bank...", False);

  state = StartProg("run_sqlldr.cmd", String_sqlldr + " control=" + File_Name_CtrlFile +
                                              " data="    + File_Name          +
                                              " log="     + Log_File_Name      +
                                              " bad="     + Bad_File_Name      +
                                              " discard=" + Dsc_File_Name      +
                                              " parallel=true " +
                                              " SKIP_UNUSABLE_INDEXES=true"    +
                                              " " + ldr_param, false);

  EndAction(10);
  BegAction(Null, "Включаем индескы", false);
  _locker = null;
  //включаем индексы, только если получается заблокировать монопольно
  var idxOnLocker = UniConcurrentLocker("LoadEntryForCompare");
  if (idxOnLocker.Lock())
    index_on();
  end;
  idxOnLocker = null;
  EndAction(10);

  x_Ok = ((state == 0) or (state == 2)); // DEF-67238, Если отработало без ошибок (stat == 0) или с предупреждениями (stat == 2)

  if (not x_Ok)
    RunError("Ошибка выполнения программы в StartProg. Код возврата: " + state);
  else
    CopyFile(Log_File_Name, PathFileName_reg+"\\CTL_" + string(UserNumber) + ".log");
  end;
  return x_Ok;
onError(err)
  AddDBLogWithErrorObj("LoadEntryForCompare",err);
  return false;
end;