/*---------------------------------------------*/
/* Адаптер инициализации процесса отправки СМС */
/* через СМС-центр (Процессинговый центр)      */
/* Поток "A5"                                  */
/*---------------------------------------------*/
/* РСХБ                                        */
/* |- БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ                  */
/*    |- ВЗАИМОДЕЙСТВИЕ С ДБО ФЛ               */
/*       |- ОТВЕТЫ                             */
/*          |- ТЕЛЕФОН ДЛЯ СПРАВОК             */
/* |- ИНТЕГРАЦИЯ                               */
/*    |- INTEGR_SERV_PRM                       */
/*       |- EXT_SERVICES                       */
/*          |- SMS_SUBMIT_INIT                 */
/*             |- TYPE_0                       */
/*                |- MSG_TEXT                  */
/*                |- ТЕЛЕФОН ДЛЯ СПРАВОК       */
/*                |- TRANSLIT_SIGN             */
/*             |- TYPE_1                       */
/*                |- MSG_TEXT                  */
/*                |- ТЕЛЕФОН ДЛЯ СПРАВОК       */
/*                |- TRANSLIT_SIGN             */
/*             |- TYPE_2                       */
/*                |- MSG_TEXT                  */
/*                |- ТЕЛЕФОН ДЛЯ СПРАВОК       */
/*                |- TRANSLIT_SIGN             */
/*---------------------------------------------*/
/* Вменяемый текст ошибки на данный момент     */
/* остается на уровне объектов (поскольку      */
/* Клиенту, предположительно, транслируется    */
/* текст сообщения из прикладного кода)        */
/*---------------------------------------------*/


import "SendSMS_lib.mac"; /* Типы объектов для данного функционала */
import "SendSMSRequest.mac"; /* Функционал инициализации процесса отправки СМС */
import BankInter; /* GetRegistryValue() */


private const CV_ERROR_TECH = -10001;
private const CV_ERR_TECH = "Техническая проблема при выполнении запроса";



class (c_IntegrSymId) c_adp_MessageId(p_MessageId)

   private macro m_init_adp_MessageId(i_MessageId)
      ObjectId = i_MessageId;
   end; /* macro m_init_adp_MessageId() */

   Initc_IntegrSymId();

   m_init_adp_MessageId(p_MessageId);
end;


class (c_DboNPMessage) c_adp_DboNPMessage(p_income_data)
   /*-------------*/
   /* Конструктор */
   /*-------------*/
   /* Считаем, что вся обработка была выполнена ранее и сюда приходит готовый объект */
   private macro m_init_adp_DboNPMessage(i_income_data)
      SmsSendPhoneNum = i_income_data.SmsSendPhoneNum;
      TransliterationSign = i_income_data.TransliterationSign;
      MessageText = i_income_data.MessageText;

      MessageId = i_income_data.MessageId;
      Error = i_income_data.Error;
   end; /* macro m_init_adp_DboNPMessage() */

   Initc_DboNPMessage();

   m_init_adp_DboNPMessage(p_income_data);
end; /* class c_adp_DboNPMessage() */


class (c_DboNPMessage) c_init_DboNPMessage(p_MessageId, p_ClientLogin, p_SmsSendPhoneNum, p_Type, p_tmp_pwd)
   private const CV_TRADE_TERM_TMP_PWD = 0; /* Тип "отправка временного пароля для входа в торговые терминалы" */
   private const CV_TRADE_TERM_ENTRY = 1; /* Тип "запрос на вход в торговые терминалы" */
   private const CV_SET_CODE_WORD = 2; /* Тип "запрос на установку/смену Кодового слова" */

   private const CV_ERR_CODE_DEFAULT = 1;

   private var v_service_code = 0;
   private var v_service_msg = "";

   private var v_client_login;
   private var v_code_type;
   private var v_code_val;
   private var v_code_id; /* Идентификатор записи с кодом для дальнейшего изменения статуса */


   /**/
   macro m_get_service_code()
      return v_service_code;
   end;


   /**/
   macro m_get_service_msg()
      return v_service_msg;
   end;


   /**/
   macro m_get_code_id()
      return v_code_id;
   end;

   /**/
   macro m_get_code_type()
      return v_code_type;
   end;


   /*----------------*/
   /* Генерация кода */
   /*----------------*/
   private macro m_make_code_value()
      const CV_NUM_OF_TRY = 10; /* Если учитывается уникальность кода в течение суток, то используется количество попыток генерации */
      var v_ret = true;
      var v_sql, v_cmd;

      v_sql =         "BEGIN ";
      v_sql = v_sql + "    :p_code_id := USR_PKG_IMPORT_SOFR.f_make_only_one_code(:p_type, :p_client_id, :p_num_of_try, :p_sms_code); ";
      v_sql = v_sql + "END; ";

      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_code_id", RSDBP_RETVAL, V_INTEGER);
      v_cmd.addParam("p_type", RSDBP_IN, v_code_type);
      v_cmd.addParam("p_client_id", RSDBP_IN, v_client_login);
      v_cmd.addParam("p_num_of_try", RSDBP_IN, CV_NUM_OF_TRY);
      v_cmd.addParam("p_sms_code", RSDBP_OUT, V_STRING, 20);
      v_cmd.execute();

      v_code_id = v_cmd.value("p_code_id");
      if(v_code_id != 0)
         v_code_val = v_cmd.value("p_sms_code");
      else
         v_service_code = CV_ERR_CODE_DEFAULT;
         v_service_msg = "Код не был сгенерирован";
         v_ret = false;
      end;

      v_cmd.close(); v_cmd = NULL; v_sql = NULL;

      return v_ret;

   onError(err)
      v_service_code = CV_ERR_CODE_DEFAULT;
      v_service_msg = "Возникли проблемы при генерации кода";
      v_ret = false;
      return v_ret;
   end; /* macro m_make_code_value() */


   /*-----------------------------------*/
   /* Установка признака транслитерации */
   /*-----------------------------------*/
   private macro m_set_TransliterationSign()
      const CV_TRANSLIT_SIGN_PATH_0 = "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\SMS_SUBMIT_INIT\\TYPE_0\\TRANSLIT_SIGN";
      const CV_TRANSLIT_SIGN_PATH_1 = "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\SMS_SUBMIT_INIT\\TYPE_1\\TRANSLIT_SIGN";
      const CV_TRANSLIT_SIGN_PATH_2 = "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\SMS_SUBMIT_INIT\\TYPE_2\\TRANSLIT_SIGN";

      var v_ret = true;

      var v_stat;
      var v_translit_sign;

      /* Получаем признак кодификации для текста сообщения */
      v_stat = 0;

      if(v_code_type == CV_TRADE_TERM_TMP_PWD)
         GetRegistryValue(CV_TRANSLIT_SIGN_PATH_0, V_INTEGER, v_translit_sign, v_stat);
      elif(v_code_type == CV_TRADE_TERM_ENTRY)
         GetRegistryValue(CV_TRANSLIT_SIGN_PATH_1, V_INTEGER, v_translit_sign, v_stat);
      elif(v_code_type == CV_SET_CODE_WORD)
         GetRegistryValue(CV_TRANSLIT_SIGN_PATH_2, V_INTEGER, v_translit_sign, v_stat);
      else
         v_service_code = CV_ERR_CODE_DEFAULT;
         v_service_msg = string("Заданный тип кода (", v_code_type, ") не поддерживается");
         v_ret = false;
      end;

      if(v_ret)
         if(v_stat > 0)
            /* Значение по умолчанию */
            v_translit_sign = 1;
         end;

         TransliterationSign = v_translit_sign;
      end;

      return v_ret;

   onError(err)
      v_service_code = CV_ERR_CODE_DEFAULT;
      v_service_msg = "Возникли проблемы при установке признака транслитерации";
      v_ret = false;
      return v_ret;
   end; /* macro m_set_TransliterationSign() */


   /*-----------------------------*/
   /* Подготовка текста сообщения */
   /*-----------------------------*/
   private macro m_set_MessageText()
      const CV_SMS_MSG_PHONE_NUM = "8 (800) 100-40-40"; /* Для замены %SupPhoneNum% */

      const CV_TEXT_TO_CHNG_PHONE = "%SupPhoneNum%";
      const CV_TEXT_TO_CHNG_CODE = "%v_code_val%";
      /*
      В соответствии с ТЗ должен использоваться следующий текст:
         - (Тип 0): !!! - в ТЗ не указан текст - !!!
         - (Тип 1): "Код подтверждения генерации временного пароля для первого входа в торговые терминалы- %v_code_val%. Никому не сообщайте код. Если Вы не совершали данную операцию, обратитесь в службу поддержки Банка по телефону %SupPhoneNum%"
         - (ТИП 2): "%v_code_val%.Код подтверждения для установки/изменения кодового слова для подачи поручений по телефону в рамках Регламента оказания брокерских услуг. Никому не сообщайте код. Если Вы не совершали данную операцию, позвоните по номеру %SupPhoneNum%"
      */
      const CV_MSG_TEXT_STUB_PATH_0 = "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\SMS_SUBMIT_INIT\\TYPE_0\\MSG_TEXT";
      const CV_MSG_TEXT_STUB_PATH_1 = "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\SMS_SUBMIT_INIT\\TYPE_1\\MSG_TEXT";
      const CV_MSG_TEXT_STUB_PATH_2 = "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\SMS_SUBMIT_INIT\\TYPE_2\\MSG_TEXT";

      const CV_INFO_PHONE_NUM_PATH_0 = "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\SMS_SUBMIT_INIT\\TYPE_0\\ТЕЛЕФОН ДЛЯ СПРАВОК";
      const CV_INFO_PHONE_NUM_PATH_1 = "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\SMS_SUBMIT_INIT\\TYPE_1\\ТЕЛЕФОН ДЛЯ СПРАВОК";
      const CV_INFO_PHONE_NUM_PATH_2 = "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\SMS_SUBMIT_INIT\\TYPE_2\\ТЕЛЕФОН ДЛЯ СПРАВОК";

      var v_ret = true;

      var v_msg_text_stub;
      var v_stat;
      var v_sms_msg_phone_num;

      /* Получаем текст сообщения */
      v_stat = 0;

      if(v_code_type == CV_TRADE_TERM_TMP_PWD)
         GetRegistryValue(CV_MSG_TEXT_STUB_PATH_0, V_STRING, v_msg_text_stub, v_stat);
      elif(v_code_type == CV_TRADE_TERM_ENTRY)
         GetRegistryValue(CV_MSG_TEXT_STUB_PATH_1, V_STRING, v_msg_text_stub, v_stat);
      elif(v_code_type == CV_SET_CODE_WORD)
         GetRegistryValue(CV_MSG_TEXT_STUB_PATH_2, V_STRING, v_msg_text_stub, v_stat);
      else
         v_service_code = CV_ERR_CODE_DEFAULT;
         v_service_msg = string("Заданный тип кода (", v_code_type, ") не поддерживается");
         v_ret = false;
      end;

      if(v_ret)
         if(v_stat > 0)
            /* Значение по умолчанию */
            v_msg_text_stub = "";

         else /* Номер телефона ищем в реестре если успешно получен шаблон сообщения */
            if(v_code_type == CV_TRADE_TERM_TMP_PWD)
               GetRegistryValue(CV_INFO_PHONE_NUM_PATH_0, V_STRING, v_sms_msg_phone_num, v_stat);
            elif(v_code_type == CV_TRADE_TERM_ENTRY)
               GetRegistryValue(CV_INFO_PHONE_NUM_PATH_1, V_STRING, v_sms_msg_phone_num, v_stat);
            elif(v_code_type == CV_SET_CODE_WORD)
               GetRegistryValue(CV_INFO_PHONE_NUM_PATH_2, V_STRING, v_sms_msg_phone_num, v_stat);
            /*else*/ /* Считаем, что сюда попасть не можем (выше уже обработано) */
            end;

            if(v_stat > 0)
               /* Значение по умолчанию */
               v_sms_msg_phone_num = CV_SMS_MSG_PHONE_NUM;
            end;
         end;

         /* Код должен быть сформирован заранее */
         if(index(v_msg_text_stub, CV_TEXT_TO_CHNG_CODE) > 0)
            if(v_code_type == CV_TRADE_TERM_TMP_PWD)
               MessageText = strsubst(strsubst(v_msg_text_stub, CV_TEXT_TO_CHNG_CODE, string(v_code_val)), CV_TEXT_TO_CHNG_PHONE, v_sms_msg_phone_num);
            elif(v_code_type == CV_TRADE_TERM_ENTRY)
               MessageText = strsubst(strsubst(v_msg_text_stub, CV_TEXT_TO_CHNG_CODE, string(v_code_val)), CV_TEXT_TO_CHNG_PHONE, v_sms_msg_phone_num);
            elif(v_code_type == CV_SET_CODE_WORD)
               MessageText = strsubst(strsubst(v_msg_text_stub, CV_TEXT_TO_CHNG_CODE, string(v_code_val)), CV_TEXT_TO_CHNG_PHONE, v_sms_msg_phone_num);
            end;
         else
            MessageText = string(v_code_val, " ", v_msg_text_stub);
            MessageText = trim(MessageText);
         end;
      end;

      return v_ret;

   onError(err)
      v_service_code = CV_ERR_CODE_DEFAULT;
      v_service_msg = "Возникли проблемы при подготовке текста сообщения";
      v_ret = false;
      return v_ret;
   end; /* macro m_set_MessageText() */


   /*-------------*/
   /* Конструктор */
   /*-------------*/
   private macro m_init_init_DboNPMessage(i_MessageId, i_ClientLogin, i_SmsSendPhoneNum, i_Type, i_tmp_pwd)
      var v_stat = true;

      if(ValType(i_ClientLogin) != V_UNDEF)
         v_client_login = i_ClientLogin;
      else
         v_service_msg = "Не задан параметр ClientLogin";
         RunError(CV_ERR_TECH, CV_ERROR_TECH);
      end;
      if(ValType(i_Type) != V_UNDEF)
         v_code_type = i_Type;
      else
         v_service_msg = "Не задан параметр Type";
         RunError(CV_ERR_TECH, CV_ERROR_TECH);
      end;

      if(ValType(i_SmsSendPhoneNum) != V_UNDEF)
         SmsSendPhoneNum = i_SmsSendPhoneNum;
      else
         v_service_msg = "Не задан параметр SmsSendPhoneNum";
         RunError(CV_ERR_TECH, CV_ERROR_TECH);
      end;
      if(ValType(i_MessageId) != V_UNDEF)
         MessageId = c_adp_MessageId(i_MessageId);
      else
         v_service_msg = "Не задан параметр MessageId";
         RunError(CV_ERR_TECH, CV_ERROR_TECH);
      end;

      if(v_code_type == CV_TRADE_TERM_TMP_PWD)
         if(ValType(i_tmp_pwd) != V_UNDEF)
            v_code_val = i_tmp_pwd;
         else
            v_service_msg = "Не указано значение временного пароля";
            RunError(CV_ERR_TECH, CV_ERROR_TECH);
         end;
      end;

      if(v_stat and (v_code_type != CV_TRADE_TERM_TMP_PWD))
         v_stat = m_make_code_value();
      end;
      if(v_stat)
         v_stat = m_set_TransliterationSign();
      end;
      if(v_stat)
         v_stat = m_set_MessageText();
      end;

   onError(err)
      v_service_code = CV_ERR_CODE_DEFAULT;
   end;

   Initc_DboNPMessage();

   m_init_init_DboNPMessage(p_MessageId, p_ClientLogin, p_SmsSendPhoneNum, p_Type, p_tmp_pwd);
end; /* class (c_DboNPMessage) c_init_DboNPMessage() */


class (c_SendSMSRequest) c_adp_SendSMSRequest(p_preinit_obj)
   private macro m_init_adp_SendSMSRequest(i_preinit_obj)
      DboNPMessage = c_adp_DboNPMessage(i_preinit_obj);
   end;

   Initc_SendSMSRequest();

   m_init_adp_SendSMSRequest(p_preinit_obj);
end; /* class (c_SendSMSRequest) c_adp_SendSMSRequest() */


/*--------------------------------------------*/
/* Обертка для формирования сообщения запроса */
/*--------------------------------------------*/
private class c_adp_SendSMS_env()
   var SendSMSRequest;
end; /* class c_adp_SendSMS_env() */


/*----------------------------------------------------------*/
/* Основной класс обработки процесса инициации отправки СМС */
/*----------------------------------------------------------*/
private class (c_pc_sms_submit_general) c_sms_submit_init_proc(p_preinit_obj)
   private const CV_INFO_PHONE_NUM_PATH = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ВЗАИМОДЕЙСТВИЕ С ДБО ФЛ\\ОТВЕТЫ\\ТЕЛЕФОН ДЛЯ СПРАВОК";
   private const CV_TRADE_TERM_TMP_PWD = 0; /* Тип "отправка временного пароля для входа в торговые терминалы" */
   private const CV_ERROR_SIGN = 1; /* Значение для ErrorCode, говорящее о наличии ошибки */
   private const CV_ERR_SMS_SEND_FAIL = "Ошибка отправки кода подтверждения, попробуйте снова или обратитесь в службу поддержки Банка по телефону %SupPhoneNum%";
   private const CV_TEXT_TO_CHNG_PHONE = "%SupPhoneNum%";
   private const CV_SUP_PHONE_NUM = "8 (800) 100-40-40"; /* Для замены %SupPhoneNum% */
   private const CV_ERR_SMS_SCHED_FAIL = "Ошибка отправки кода подтверждения";

   private var v_result_list;
   private var v_is_send_succesful = false;
   private var v_code_id = 0;
   private var v_sup_phone_num; /* Телефон Банка для справок по Брокерскому обслуживанию (для замены %SupPhoneNum%) */


   /**/
   macro m_get_result_list()
      return v_result_list;
   end;


   /*---------------------------------------------*/
   /*        Метод обновления статуса кода        */
   /*---------------------------------------------*/
   /* Метод вызывается ПОСЛЕ разбора ответа от ПЦ */
   /*---------------------------------------------*/
   macro m_update_code_status()
      const CV_STAT_SEND_FAILED = 5;
      const CV_STAT_WAS_SENT = 6;
      var v_ret = true;
      var v_sql, v_cmd;

      v_sql =         "UPDATE usr_clnt_sms_code_dbt ";
      v_sql = v_sql + "   SET t_status = :p_status ";
      v_sql = v_sql + " WHERE t_id = :p_id ";
      v_cmd = RSDCommand(v_sql);
      if(v_is_send_succesful)
         v_cmd.addParam("p_status", RSDBP_IN, CV_STAT_WAS_SENT);
      else
         v_cmd.addParam("p_status", RSDBP_IN, CV_STAT_SEND_FAILED);
      end;
      v_cmd.addParam("p_id", RSDBP_IN, v_code_id);
      v_cmd.execute();

      v_cmd.close(); v_cmd = null; v_sql = null;

      return v_ret;

   onError(err)
      v_ret = false;
      return v_ret;
   end; /* macro m_update_code_status() */


   /*-------------------------------------*/
   /* Метод формирования списка с ошибкой */
   /*-------------------------------------*/
   macro m_make_error_list(p_preinit_obj)
      var v_aux_cntr = v_result_list.size;
      if(v_aux_cntr == 0)
         v_result_list.value(v_aux_cntr) = c_Error;
         v_result_list.value(v_aux_cntr).ErrorCode = CV_ERROR_SIGN;
         /* Особая обработка для запросов из планировщика */
         if(ValType(p_preinit_obj) != V_UNDEF)
            if(p_preinit_obj.m_get_code_type() == CV_TRADE_TERM_TMP_PWD)
               v_result_list.value(v_aux_cntr).ErrorDesc = CV_ERR_SMS_SCHED_FAIL;
            else
               v_result_list.value(v_aux_cntr).ErrorDesc = strsubst(CV_ERR_SMS_SEND_FAIL, CV_TEXT_TO_CHNG_PHONE, CV_SUP_PHONE_NUM);
            end;
         else
            v_result_list.value(v_aux_cntr).ErrorDesc = strsubst(CV_ERR_SMS_SEND_FAIL, CV_TEXT_TO_CHNG_PHONE, CV_SUP_PHONE_NUM);
         end;
      end;

      return v_result_list;
   end; /* macro m_make_error_list() */


   /*---------------------------------------*/
   /* Метод формирования списка результатов */
   /*---------------------------------------*/
   macro m_make_result_list()
      var v_ret = true;
      var v_aux_buf;
      var v_aux_cntr;

      if(ValType(uTryToGetPropS(v_resp_obj, "DboNPMessage")) == V_GENOBJ)
         v_aux_buf = ValType(uTryToGetPropS(v_resp_obj.DboNPMessage, "Error"));
         if(v_aux_buf == V_GENOBJ)
            v_aux_cntr = 0;
            v_is_send_succesful = true;
            while((v_is_send_succesful) and (v_aux_cntr < v_resp_obj.DboNPMessage.Error.size))
               v_aux_buf = ValType(v_resp_obj.DboNPMessage.Error.value(v_aux_cntr));
               if(v_aux_buf == V_GENOBJ)
                  v_aux_buf = v_resp_obj.DboNPMessage.Error.value(v_aux_cntr).ErrorCode;
                  if(ValType(v_aux_buf) == V_UNDEF)
                     v_is_send_succesful = true;
                  else
                     if(v_aux_buf == 0)
                        v_is_send_succesful = true;
                     else
                        v_is_send_succesful = false;
                     end;
                  end;
               elif(v_aux_buf == V_UNDEF)
                  v_is_send_succesful = true;
               else
                  v_is_send_succesful = false;
               end;

               v_aux_cntr = v_aux_cntr + 1;
            end;

         elif(v_aux_buf == V_UNDEF)
            v_is_send_succesful = true;
         else
            v_is_send_succesful = false;
         end;
      else
         v_is_send_succesful = false;
      end;

      /* Обновление статуса кода */
      m_update_code_status();

      if(not v_is_send_succesful)
         v_aux_cntr = v_result_list.size;
         if(v_aux_cntr == 0)
            v_result_list.value(v_aux_cntr) = c_Error;
            v_result_list.value(v_aux_cntr).ErrorCode = CV_ERROR_SIGN;
            v_result_list.value(v_aux_cntr).ErrorDesc = strsubst(CV_ERR_SMS_SEND_FAIL, CV_TEXT_TO_CHNG_PHONE, CV_SUP_PHONE_NUM);
         end;
      end;

      return v_ret;

   onError(err)
      v_ret = false;
      return v_ret;
   end; /* macro m_make_result_list() */


   /*-------------*/
   /* Конструктор */
   /*-------------*/
   private macro m_init_sms_submit_init_req(i_preinit_obj)
      var v_stat;
      v_result_list = TArray;

      GetRegistryValue(CV_INFO_PHONE_NUM_PATH, V_INTEGER, v_sup_phone_num, v_stat);
      if(v_stat > 0)
         /* Значение по умолчанию */
         v_sup_phone_num = CV_SUP_PHONE_NUM;
      end;

      v_req_obj = c_adp_SendSMS_env();

      if(ValType(i_preinit_obj) != V_UNDEF)
         v_code_id = i_preinit_obj.m_get_code_id();

         v_req_obj.SendSMSRequest = c_adp_SendSMSRequest(i_preinit_obj);
      end;
   end; /* macro m_init_sms_submit_init_req() */

   Initc_pc_sms_submit_general();

   m_init_sms_submit_init_req(p_preinit_obj);
end; /* class c_sms_submit_init_proc() */


/*-----------------------------------------------------*/
/* Точка входа в функционал инициализации отправки СМС */
/*-----------------------------------------------------*/
macro adp_sms_submit_init(p_MessageId, p_ClientLogin, p_SmsSendPhoneNum, p_Type, p_tmp_pwd, p_synch_timeout)
   var v_result_obj;
   var v_submit_obj;
   var v_preinit_obj;
   var v_stat = true;

   /* Идентификатор сообщения обязателен во всех сообщениях - в данном случае должен быть получен от инициатора */
   if(ValType(p_MessageId) == V_UNDEF)
      v_stat = false;
   end;
   /* Без номера телефона СМС, очевидно, отправить не получится */
   if(ValType(p_SmsSendPhoneNum) == V_UNDEF)
      v_stat = false;
   end;

   if(ValType(p_Type) == V_UNDEF)
      v_stat = false;
   end;

   if(v_stat)
      /* Подготовка объекта для сообщения запроса */
      v_preinit_obj = c_init_DboNPMessage(p_MessageId, p_ClientLogin, p_SmsSendPhoneNum, p_Type, p_tmp_pwd);
      v_submit_obj = c_sms_submit_init_proc(v_preinit_obj);

      if(v_preinit_obj.m_get_service_code() == 0)
         /* Отправка запроса */
         if(v_submit_obj.m_submit_sms_req(p_synch_timeout))
            if(v_submit_obj.m_make_result_list())
               v_result_obj = v_submit_obj.m_get_result_list();
            else
               v_result_obj = v_submit_obj.m_make_error_list(v_preinit_obj);
            end;
         else
            v_result_obj = v_submit_obj.m_make_error_list(v_preinit_obj);
         end;
      else
         v_result_obj = v_submit_obj.m_make_error_list(v_preinit_obj);
      end;
   else
      v_submit_obj = c_sms_submit_init_proc(); /* Создаем пустой объект */
      v_result_obj = v_submit_obj.m_make_error_list(v_preinit_obj); /* Централизованно формируем список с ошибкой */
   end;

   return v_result_obj;

onError(err)
   v_submit_obj = null;
   v_result_obj = null;
   v_submit_obj = c_sms_submit_init_proc(); /* Создаем пустой объект */
   v_result_obj = v_submit_obj.m_make_error_list(v_preinit_obj); /* Централизованно формируем список с ошибкой */
   return v_result_obj;
end; /* macro adp_sms_submit_init() */