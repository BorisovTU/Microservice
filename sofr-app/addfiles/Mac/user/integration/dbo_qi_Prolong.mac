/**
 @file dbo_qi_Prolong.mac
 @brief Функционал пролонгирования статуса квалифицированного инвестора.
 
 Файл содержит функционал пролонгирования статуса квалифицированного инвестора.
 
  # tag
 - functional_block:Отчетность_Клиент
 - code_type:API
 - Квалификация
 - КИ
 - Квалифицированный инвестор
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |01.12.2023 |Топорков Д.В.|BIQ-16875 BOSS-194 BOSS-1415                              | Создание
 |25.01.2024 |Топорков Д.В.|BIQ-16875 BOSS-194 BOSS-1775 BOSS-2093                    | Отправка уведомления при пролонгации
 */
 
import rsd, RsbFormsInter, globals;
import "oralib.mac";
import RsbDataSet;
import "dbo_qi_ProlongNotification.mac";

/**
@brief Функция проверки договора обслуживания и  условиям пролонгации
@param[in] sfContrID ID Договора обслуживания (таблица dSfContr_dbt)
@param[out] error Текст ошибки
@return Bool Статус проверки (True - договор удовлетворяет условиям пролонгации)
*/
macro CheckProlongConditions(sfContrID: Integer, prolongDate: Date,  error: @String): Bool
  var rec = TRsbDataSet("SELECT sf.t_dateClose, party.t_legalForm, qi.t_state, GREATEST(qi.t_regDate, qi.t_changeDate) AS t_lastDate " +
                          "FROM dSfContr_dbt sf, dParty_dbt party, dScQinv_dbt qi " +
                         "WHERE sf.t_ID = " + sfContrID + " AND party.t_partyID = sf.t_partyID AND qi.t_sfContrID(+) = sf.t_ID");
  if (rec.MoveNext())
    if (rec.dateClose > Date(0, 0, 0))
      error = "Невозможно сформировать уведомление по закрытому договору";
    elif (ValType(rec.state) == V_UNDEF)
      error = "Договор не включен в реестр Квалицированных инвесторов";
    elif (rec.state != 1)
      error = "Нельзя выполнить пролонгацию для исключенного из реестра договора";
    elif (rec.legalForm != 1)
      error = "Выполнение пролонгации доступно только для юридических лиц";
    elif (rec.lastDate >= prolongDate)
      error = "Неверная дата выполнения пролонгации";
    else
      return true;
    end;
  else
    error = "Данные по договору с ID = '" + sfContrID + "' не найдены";
  end;
  
  return false;
  
onError(err)
  error = "Ошибка выполнения:|" + err.Module + " строка " + err.Line + "|" + err.message;
  return false;
end;

/**
@brief Функция проверки договора обслуживания и  условиям пролонгации
@param[in] PartyID ID Клиента (dParty_dbt)
@param[out] error Текст ошибки
@return Bool Статус проверки (True - договор удовлетворяет условиям пролонгации)
*/
macro CheckProlongConditionsParty(PartyID: Integer, prolongDate: Date,  error: @String): Bool
  
   var sql = " SELECT sf.t_dateClose,  "   +
             "       party.t_legalForm,   "   +
             "       qi.t_state,   "   +
             "       GREATEST(qi.t_regDate, qi.t_changeDate) AS t_lastDate "   +
             "   FROM dSfContr_dbt sf JOIN  dParty_dbt party on  party.t_partyID = sf.t_partyID  "   +
             "                   LEFT JOIN dScQinv_dbt qi  on  qi.t_partyid = sf.t_partyid  "   +
             "   WHERE party.t_partyid = " + PartyID + " AND sf.t_servkind = 0 AND sf.t_dateclose = to_date('01.01.0001','dd.mm.yyyy') " ;
  
  var rec = TRsbDataSet(sql);
  if (rec.MoveNext())
    if (ValType(rec.state) == V_UNDEF)
      error = "Клиент не включен в реестр Квалицированных инвесторов";
    elif (rec.state != 1)
      error = "Нельзя выполнить пролонгацию для исключенного из реестра клиента";
    elif (rec.legalForm != 1)
      error = "Выполнение пролонгации доступно только для юридических лиц";
    elif (rec.lastDate >= prolongDate)
      error = "Неверная дата выполнения пролонгации";
    else
      return true;
    end;
  else
    error = "Данные в реестре для клиента с ID = '" + PartyID + "' не найдены";
  end;
  
  return false;
  
onError(err)
  error = "Ошибка выполнения:|" + err.Module + " строка " + err.Line + "|" + err.message;
  return false;
end;

/**
@brief Функция создания и отправки уведомления о пролонгации
@param[in] sfContrID ID Договора обслуживания (таблица dSfContr_dbt)
@param[in] prolongDate Дата пролонгации статуса КИ
@param[in] controlDate Дата следующей проверки статуса КИ
@return Integer Статус выполнения (0 - успешное выполнение)
*/
macro QIProlongNotifyBySfContr(sfContrID: Integer, prolongDate: Date, controlDate: Date, error: @String): Integer
  var rs = ExecSQLSelect("SELECT t_dlContrID FROM dDlContr_dbt WHERE t_sfContrID = " + sfContrID);
  var params: CQIProlongParams = CQIProlongParams();

  error = "";
  
  if (not rs.movenext())
    error = "Ошибка формирования уведомления:|Не найден договор брокерского обслуживания для договора обслуживания с ID = " + sfContrID;
    return 1;
  end;
  
  params.dlContrID = rs.value("t_dlContrID");
  params.client = GetClient(params.dlContrID);
  params.signer = GetQIDefaultSigner();
  params.notificationDate = Date();
  params.prolongDate = prolongDate;
  params.controlDate = controlDate;

  error = QIProlongNotification(params);
  
  if (StrLen(error) > 0)
    error = "Ошибка формирования уведомления:|" + error;
    return 1;
  end;
  
  return 0;
  
onError(err)
  error = "Ошибка выполнения при формировании уведомления:|" + err.Module + " строка " + err.Line + "|" + err.message;
  return 1;
end;

/**
@brief Функция создания и отправки уведомления о пролонгации
@param[in] PartyID ID Клиента (dParty_dbt)
@param[in] prolongDate Дата пролонгации статуса КИ
@param[in] controlDate Дата следующей проверки статуса КИ
@return Integer Статус выполнения (0 - успешное выполнение)
*/
macro QIProlongNotifyByParty(PartyID: Integer, prolongDate: Date, controlDate: Date, error: @String): Integer
  var sql = " SELECT t_dlContrID  as t_dlContrID "   +
            "   FROM dDlContr_dbt  dl join dsfcontr_dbt sf on dl.t_sfcontrid = sf.t_id and sf.t_servkind = 0 "   +
            "  WHERE sf.t_dateclose = to_date('01.01.0001','dd.mm.yyyy') AND sf.t_partyid = " + PartyID;
  var cmd = DL_RSDCommand();
  var cnt = cmd.GetCount(sql);
  var params: CQIProlongParams = CQIProlongParams();
  var DataSet;
  error = "";

  if(cnt > 0)
    DataSet = cmd.Execute(sql);

    while(DataSet.moveNext())
      params.dlContrID = DataSet.value("t_dlContrID");
      params.client = GetClient(params.dlContrID);
      params.signer = GetQIDefaultSigner();
      params.notificationDate = Date();
      params.prolongDate = prolongDate;
      params.controlDate = controlDate;

      error = QIProlongNotification(params);
      
      if (StrLen(error) > 0)
        error = "Ошибка формирования уведомления:|" + error;
        return 1;
      end;
    end;
  else 
    error = "Ошибка формирования уведомления:|Не найден договор брокерского обслуживания для клиента с ID = " + PartyID;
    return 1;
  end;
  
  return 0;
  
onError(err)
  error = "Ошибка выполнения при формировании уведомления:|" + err.Module + " строка " + err.Line + "|" + err.message;
  return 1;
end;