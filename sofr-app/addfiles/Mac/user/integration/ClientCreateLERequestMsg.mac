/**
 @file ClientCreateLERequestMsg.mac
 @brief Интеграция с внешними системами. Адаптер для загрузки клиентов - юр.лиц

 # tag
 - functional_block: Клиенты_ЮЛ
 - code_type: API

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |00.00.2019 |Гераськина Т.В.|                                                |Создано
 |20.11.2023 |Гераськина Т.В.|BIQ-10268                                       |При обработке запроса по результатам запроса CdiGetOrgXrefReq сохраняется код субъекта 110
 |           |               |                                                |При обновлении субъекта, если код задан, то ожидается, что поступит запрос CdiPublishOrgSOFRReq, 
 |           |               |                                                |поэтому обновление не делается 
 |29.11.2023 |Гераськина Т.В.|DEF-56111                                       |Отключено создание контактов и адресов при вставке субъекта
 |09.01.2024 |Фаршатов Г.К.  |DEF-58463                                       |Добавление заполнения категории Тип субъекта для CDI

*/

import "ws_msg_transform_sec.mac"; /* Инструментарий для преобразования сообщений */
import "ws_party.mac";         /* Сервис */
import rcw;
import "func_lib.mac";
import "PersonGetXRefLE.mac";
import "CdiGetOrgXrefReq.mac"; // BIQ-9555 Замена на CDI
import "global_classes.mac";
import "CDIOrg_lib.mac";

private const USE_CDI = "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\CDI\\ИСПОЛЬЗОВАТЬ CDIGETORGXREF";

private const SERV_CDI_FIND_ORG   = 5053,
              SERV_CDI_GET_ORG    = 5054,
              SERV_CDI_UPDATE_ORG = 5055,
              SERV_CDI_CREATE_ORG = 5056;

/**
@brief Класс адреса
@param[in] Incomedata входящий объект XMLRPC
@param[in] _nametag_up имя верхнего тега для генерации ошибок
*/
private class adp_Address(IncomeData, _nametag_up)
   var IsPrimaryMVG  :bool;      // Признак основного адреса   Да
   var FullAddress   : String;   // Адрес одной строкой        Нет
   var PostalCode    : String;   // Почтовый индекс            Нет
   var District      : String;   // Район                      Нет
   var City          : String;   // Город                      Нет
   var Locality      : String;   // Населенный пункт           Нет
   var Street        : String;   // Улица                      Нет
   var House         : String;   // Дом (владение)             Нет
   var Building      : String;   // Корпус                     Нет
   var Apartment     : String;   // Квартира/офис              Нет
   var Structure     : String;   // Строение                   Нет
   
   // справочники
   var AddressType   ;           // Тип адреса                 Да    Справочник     АдрЮр АдрПочт
   var CountryCode   ;           // Код страны                 Нет   Справочник
   var RegionGNI     ;           // Код региона ГНИ            Нет   Справочник
   
   var mAddressType;

   /* Кратность параметров в соответствии со спецификацией */
   private macro uInitPrime(IncomeData, _nametag_up)
      var err_txt;
      var nametag_up = "Address";
      if (ValType(_nametag_up) != V_UNDEF)
         nametag_up = _nametag_up+"."+nametag_up;
      end;
      
      IsPrimaryMVG   = getValueFromProperty(IncomeData, "IsPrimaryMVG"  , nametag_up, true);
      FullAddress    = getValueFromProperty(IncomeData, "FullAddress"   , nametag_up);
      PostalCode     = getValueFromProperty(IncomeData, "PostalCode"    , nametag_up);
      District       = getValueFromProperty(IncomeData, "District"      , nametag_up);
      City           = getValueFromProperty(IncomeData, "City"          , nametag_up);
      Locality       = getValueFromProperty(IncomeData, "Locality"      , nametag_up);
      Street         = getValueFromProperty(IncomeData, "Street"        , nametag_up);
      House          = getValueFromProperty(IncomeData, "House"         , nametag_up);
      Building       = getValueFromProperty(IncomeData, "Building"      , nametag_up);
      Apartment      = getValueFromProperty(IncomeData, "Apartment"     , nametag_up);
      Structure      = getValueFromProperty(IncomeData, "Structure"     , nametag_up);

      AddressType = getDictionaryFromProperty(IncomeData, "AddressType"   , nametag_up, true);
      CountryCode = getDictionaryFromProperty(IncomeData, "CountryCode"   , nametag_up);
      RegionGNI   = getDictionaryFromProperty(IncomeData, "RegionGNI"     , nametag_up);
      
      if (IsPrimaryMVG)
         mAddressType = 1; // юридический
      else
         if ( StrUpr(AddressType.RecordCode) == StrUpr("АдрЮр") )
            mAddressType = 1; // юридический
         elif ( StrUpr(AddressType.RecordCode) == StrUpr("АдрФакт") )
            mAddressType = 2; // физический
         elif ( StrUpr(AddressType.RecordCode) == StrUpr("АдрПочт") )
            mAddressType = 3; // почтовый
         else
            err_txt = string("Недопустимое значение "+nametag_up+".AddressType.RecordCode=",AddressType.RecordCode);
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR)); 
         end;
      end;
   end;

   uInitPrime(IncomeData, _nametag_up);
end;

/**
@brief ГВК
@param[in] Incomedata входящий объект XMLRPC
@param[in] _nametag_up имя верхнего тега для генерации ошибок
*/
private class adp_GVK(IncomeData, _nametag_up)
   var GVKCode    ;  // Код группы                  Да    Справочник

   /* Кратность параметров в соответствии со спецификацией */
   private macro uInitPrime(IncomeData, _nametag_up)
      var err_txt;
      var nametag_up = "GVK";
      if (ValType(_nametag_up) != V_UNDEF)
         nametag_up = _nametag_up+"."+nametag_up;
      end;
      
      GVKCode = getDictionaryFromProperty(IncomeData, "GVKCode", nametag_up, true);
   end;

   uInitPrime(IncomeData, _nametag_up);
end;

/**
@brief ОКВЭД
@param[in] Incomedata входящий объект XMLRPC
@param[in] _nametag_up имя верхнего тега для генерации ошибок
*/
private class adp_OKVED(IncomeData, _nametag_up)
   var OKVEDType  ; // Тип классификатора ОКВЭД   Да     Справочник
   var OKVEDCode  ; // Код ОКВЭД                  Да     Справочник
   
   /* Кратность параметров в соответствии со спецификацией */
   private macro uInitPrime(IncomeData, _nametag_up)
      var err_txt;
      var nametag_up = "OKVED";
      if (ValType(_nametag_up) != V_UNDEF)
         nametag_up = _nametag_up+"."+nametag_up;
      end;
      
      OKVEDCode = getDictionaryFromProperty(IncomeData, "OKVEDCode", nametag_up, true);
      OKVEDType = getDictionaryFromProperty(IncomeData, "OKVEDType", nametag_up, true);
   end;

   uInitPrime(IncomeData, _nametag_up);
end;

/**
@brief данные, подготовленные для вставки/обновления
@param[in] Incomedata входящий объект XMLRPC
@param[in] _code_value_number "очищенный" код АБС
@param[in] _partyid ID субъекта, заполнено при обновлении
*/
private class adp_Party(IncomeData, _code_value_number, _partyid)
   var PersnParam /*: TWsPersn*/;    // Атрибуты физического лица 
   var InstParam  /*: TWsInst*/;     // Атрибуты юридического лица 
   var ClientParam /*: TArray*/;     // Атрибуты клиента по виду обслуживния
   var Codes /*: TArray*/;           // Атрибуты кодов субъекта 
   var Addresses/* : TArray*/;       // Список адресов субъекта экономики
   var Contacts /*: TArray*/;        // Список контактной информация субъекта экономики
   var PersnPapers /*: TArray*/;     // Список документов, удостоверяющих личность субъекта экономики
   var RegDocs /*: TArray*/;         // Список регистрационных документов субъекта экономики
   var PartyNotes /*: TArray*/;      // Список примечаний субъекта экономики
   var PersnNotes /*: TArray*/;      // Список примечаний субъекта - физического лица

   var Names : TArray;           // Список наименований субъекта
   var Categories: TArray;       // Список категорий субъекта экономики
   var Employees: TArray;        // Список сотрудников организации

   var PartyCode; /*: TWsPartyCode*/  /* Код субъекта для поиска. */   
   
   private var Code_temp : TWsPartyCode;  
   private var Name_temp, Category_temp, Contacts_temp, address_temp, GVK, OKVED;  
   private var paper_temp;
   
   var pos, tmp;
   var aux_buff;
   var err_txt;
   
   var code_value, num_filial;
   var pp;
   var Structure_building, NumCopr_building;  
   var TaxInstitution;

   if (ValType(IncomeData.Kind) != V_UNDEF)
      if ( (IncomeData.Kind.RecordCode == 1) or (IncomeData.Kind.RecordCode == 2) ) // ЮЛ или банк
         // юрик
         PersnParam = NULL;
         InstParam = TWsInst;
            
         // заполнение параметров юрика
         InstParam.ShortName         = IncomeData.ShortName;
         InstParam.FullName          = IncomeData.Name; 
         if (ValType(IncomeData.Resident) != V_UNDEF)
            if (IncomeData.Resident.RecordCode == 1)    // "1 = Резидент 2 = Не резидент"
               InstParam.IsNotResident = false;
            elif
               (IncomeData.Resident.RecordCode == 2)
               InstParam.IsNotResident = true;
            end;
         end;
      
         if (ValType(IncomeData.Country) != V_UNDEF)
            InstParam.NRCountry  = StrUpr(IncomeData.Country.RecordCode);  //Справочник ?
         end;

         if ( ( not InstParam.IsNotResident ) and (InstParam.NRCountry != "RUS") )
            err_txt = string("Страна не соответствует резиденту: "+InstParam.NRCountry);
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR)); 
         elif ( InstParam.IsNotResident  and (InstParam.NRCountry == "RUS") )
            err_txt = string("Страна не соответствует нерезиденту: "+InstParam.NRCountry);
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR)); 
         end; 

         InstParam.OKPO       =  IncomeData.OKPO;
         
      elif (IncomeData.Kind.RecordCode == 3)
         // физик ИП
         PersnParam = TWsPersn;
         InstParam = NULL;
         
         PersnParam.LastName = trim(IncomeData.Name);
         pos = Index(PersnParam.LastName," ");
         if (pos > 0)
            tmp = PersnParam.LastName;
            PersnParam.LastName = SubStr(tmp, 1, pos-1);
            tmp = Substr(tmp, pos+1);
            pos = Index(tmp, " ");
            if (pos > 0)
               PersnParam.FirstName = SubStr(tmp, 1, pos-1);
               PersnParam.Patronymic = SubStr(tmp, pos+1);
            else
               PersnParam.FirstName = tmp;
            end;
         end;
         
         PersnParam.IsEmployer = true;
         PersnParam.OKPO       =  IncomeData.OKPO;

         if (ValType(IncomeData.Resident) != V_UNDEF)
            if (IncomeData.Resident.RecordCode == 1)    // "1 = Резидент 2 = Не резидент"
               PersnParam.IsNotResident = false;
            elif
               (IncomeData.Resident.RecordCode == 2)
               PersnParam.IsNotResident = true;
            end;
         end;
         
         PersnParam.NRCountry  = StrUpr(IncomeData.Country.RecordCode);  //Справочник ?
         
         if ( ( not PersnParam.IsNotResident ) and (PersnParam.NRCountry != "RUS") )
            err_txt = string("Страна не соответствует резиденту: "+PersnParam.NRCountry);
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR)); 
         elif ( PersnParam.IsNotResident  and (PersnParam.NRCountry == "RUS") )
            err_txt = string("Страна не соответствует нерезиденту: "+PersnParam.NRCountry);
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR)); 
         end; 

         // ДУЛ
         if ( ValType(IncomeData.TypeDocument) != V_UNDEF )
            PersnPapers = TArray;
            aux_buff = 0;
            paper_temp = TWsPaper;
            paper_temp.Kind = IncomeData.TypeDocument.RecordCode;  //???
            //paper_temp.Series = ;
            paper_temp.Number = IncomeData.Document;
            paper_temp.IssuedDate = IncomeData.DateVid;
            paper_temp.Issuer = IncomeData.IssueName;
            paper_temp.IsMain = true;
               
            PersnPapers.value(PersnPapers.size) = paper_temp;
         end;
         
         PersnParam.BirthDate = date(IncomeData.Birthday);  //????
         PersnParam.BirthPlace = IncomeData.BirthPlace;
         
      end;
   end;
   
   // коды субъекта
   Codes = TArray;
   
   if (ValType(IncomeData.INN) != V_UNDEF)
      Code_temp = TWsPartyCode;
      Code_temp.CodeKind = PTCK_INN;
      Code_temp.Code = IncomeData.INN;
      if (ValType(IncomeData.KPP) != V_UNDEF)
         Code_temp.Code = Code_temp.Code + "/" + IncomeData.KPP;
      end;

      pp = GetPartyID_byPartyCode(PTCK_MNS, substr(IncomeData.INN,1,4));
      if (pp > 0)
         if ( (IncomeData.Kind.RecordCode == 1) or (IncomeData.Kind.RecordCode == 2) ) // ЮЛ или банк
            InstParam.TaxInstitution = pp;
         else
            PersnParam.TaxInstitution = pp;
         end;
      end;

      Codes.value(Codes.size) = Code_temp;
   end;
   
   if (ValType(IncomeData.KIO) != V_UNDEF)   
      Code_temp = TWsPartyCode;
      Code_temp.CodeKind = PTCK_FOREIGN_INN;
      Code_temp.Code = IncomeData.KIO;
      Codes.value(Codes.size) = Code_temp;
   end;
   
   /* код CIF не сохраняем
   if (ValType(IncomeData.CIFId) != V_UNDEF)
      Code_temp = TWsPartyCode;
      Code_temp.CodeKind = PARTY_CODEKIND_CIF;
      Code_temp.Code = IncomeData.CIFId.ObjectId;
      Codes.value(Codes.size) = Code_temp;
   end;   */
   
   // код Бисквит
   if (ValType(_code_value_number) != V_UNDEF)
      Code_temp = TWsPartyCode;
      Code_temp.CodeKind = PARTY_CODEKIND_ABS;
      if (ValType(IncomeData.FilialId) != V_UNDEF)
         num_filial = IncomeData.FilialId.ObjectId;
      else
         num_filial = "0000";
      end;
// 2021-01-15 Cherednichenko is521858 В коде 101 ЦФТ нет префксов, как было в Бисквит
//      code_value = MakeCodeABS(_code_value_number, num_filial, IncomeData.Kind.RecordCode);
        code_value = _code_value_number;
      Code_temp.Code = code_value;
      
      Codes.value(Codes.size) = Code_temp;
   end;
  
   if ( (IncomeData.Kind.RecordCode == 1) or (IncomeData.Kind.RecordCode == 2) ) // ЮЛ или ИП
      // если указан код Бисквит, значит, это и код 1 тоже
      if (code_value)
      else
         // если не указан - последовательность
         if (GenerateReference( UREFERENCE_PARTY_CODE, code_value ) != 0)
            code_value = StrFor(1);
         end;
      end;
      
      Code_temp = TWsPartyCode;
      Code_temp.CodeKind = PTCK_CONTR;
      Code_temp.Code = code_value;
      Codes.value(Codes.size) = Code_temp;
   
   elif (IncomeData.Kind.RecordCode == 2) // банк
      if (ValType(IncomeData.BIK) != V_UNDEF)
         Code_temp = TWsPartyCode;
         Code_temp.CodeKind = PTCK_BIC;
         Code_temp.Code = IncomeData.BIK;
         Codes.value(Codes.size) = Code_temp;
      end;

      if (ValType(IncomeData.SWIFT) != V_UNDEF)
         Code_temp = TWsPartyCode;
         Code_temp.CodeKind = PTCK_SWIFT;
         Code_temp.Code = IncomeData.SWIFT;
         Codes.value(Codes.size) = Code_temp;
         code_value = IncomeData.SWIFT;
      else
         code_value = StrFor(1);
      end;
      // для банков - код 1 - свифт
      Code_temp = TWsPartyCode;
      Code_temp.CodeKind = PTCK_CONTR;
      Code_temp.Code = code_value;
      Codes.value(Codes.size) = Code_temp;

      if (ValType(IncomeData.CBRLicense) != V_UNDEF)
         Code_temp = TWsPartyCode;
         Code_temp.CodeKind = PTCK_ORCBLICENSE; 
         Code_temp.Code = IncomeData.CBRLicense;
         Codes.value(Codes.size) = Code_temp;
      end;
   end;
   
   if (ValType(IncomeData.OGRN) != V_UNDEF)
      Code_temp = TWsPartyCode;
      Code_temp.CodeKind = 27;
      Code_temp.Code = IncomeData.OGRN;
      Codes.value(Codes.size) = Code_temp;
   end;

   if (_partyid > 0)
      code_value = GetPartyCode_ByNumber(_partyid, PTCK_ACC);
   else
      code_value = "";
   end;

   if (not code_value)
      GenerateReference( UREFERENCE_ACC_NUM, code_value );
      var pf = GetPartyID_byPartyCode(PTCK_ACC, code_value);
      while (pf > 0)  // существует такой код
         GenerateReference( UREFERENCE_ACC_NUM, code_value );
         pf = GetPartyID_byPartyCode(PTCK_ACC, code_value); 
      end;
   end;

   //код в номере счета
   if (code_value)
      Code_temp = TWsPartyCode;
      Code_temp.CodeKind = PTCK_ACC;
      Code_temp.Code = code_value;
      Codes.value(Codes.size) = Code_temp;
   end;

   // наименования
   if ( (ValType(IncomeData.LatinName) != V_UNDEF) or (ValType(IncomeData.ShortLatinName) != V_UNDEF) )
      Names = TArray;
      
      if (ValType(IncomeData.LatinName) != V_UNDEF)
         Name_temp = adp_Names;
         Name_temp.PartyNameID = 3;
         Name_temp.Name = IncomeData.LatinName;
         Names.value(Names.size) = Name_temp;
      end;

      if (ValType(IncomeData.ShortLatinName) != V_UNDEF)
         Name_temp = adp_Names;
         Name_temp.PartyNameID = 8;
         Name_temp.Name = IncomeData.ShortLatinName;
         Names.value(Names.size) = Name_temp;
      end;
   end;
   
   // категории
   Categories = TArray;
   
   //ОПФ
   if (ValType(IncomeData.OPF) != V_UNDEF)
      Category_temp = adp_Categories;
      Category_temp.GroupID = 4;
      Category_temp.Value = IncomeData.OPF.RecordCode;
      Category_temp.IsSingle = GetSingleSign(OBJTYPE_PARTY, 4);
      Categories.value(Categories.size) = Category_temp;
   end;
   
   // ОКАТО
   if (ValType(IncomeData.OKATO) != V_UNDEF)
      Category_temp = adp_Categories;
      Category_temp.GroupID = 12;
      Category_temp.Value = IncomeData.OKATO;
      Category_temp.IsSingle = GetSingleSign(OBJTYPE_PARTY, 12);
      Categories.value(Categories.size) = Category_temp;   
   end;
   
   // Актуальная зона ответственности
   if (ValType(IncomeData.ResponseZone) != V_UNDEF)
      Category_temp = adp_Categories;
      Category_temp.GroupID = 116;
      Category_temp.Value =  GetObjAttrNameObject_byName(OBJTYPE_PARTY, 116, IncomeData.ResponseZone.RecordCode);
      if (ValType(Category_temp.Value) != V_UNDEF)
        Category_temp.IsSingle = GetSingleSign(OBJTYPE_PARTY, 116);
        Categories.value(Categories.size) = Category_temp;
      end;        
   end;
   
   // FATCA
   if (ValType(IncomeData.FATCA) != V_UNDEF)
      Category_temp = adp_Categories;
      Category_temp.GroupID = 108;
      if (StrUpr(IncomeData.FATCA.RecordCode) == "ДА")
         Category_temp.Value = 1;
      else
         Category_temp.Value = 2;
      end;
      Category_temp.IsSingle = GetSingleSign(OBJTYPE_PARTY, 108);
      Categories.value(Categories.size) = Category_temp;   
   end;
   
   // CRS
   if (ValType(IncomeData.CRS) != V_UNDEF)
      Category_temp = adp_Categories;
      Category_temp.GroupID = 109;
      if (StrUpr(IncomeData.CRS.RecordCode) == "ДА")
         Category_temp.Value = 1;
      else
         Category_temp.Value = 2;
      end;
      Category_temp.IsSingle = GetSingleSign(OBJTYPE_PARTY, 109);
      Categories.value(Categories.size) = Category_temp;   
   end;   

   // Группа взаимосвязанных субъектов
   if (ValType(IncomeData.GVKList) != V_UNDEF)
      for (GVK, IncomeData.GVKList)
         Category_temp = adp_Categories;
         Category_temp.GroupID = 20;
         Category_temp.Value = GVK.GVKCode.RecordCode;
         Category_temp.IsSingle = GetSingleSign(OBJTYPE_PARTY, 20);
         Categories.value(Categories.size) = Category_temp;
      end;
   end;
         
   // ОКВЭД
   if (ValType(IncomeData.OKVEDList) != V_UNDEF)
      for (OKVED, IncomeData.OKVEDList)
         Category_temp = adp_Categories;
         Category_temp.GroupID = 64;
         Category_temp.Value = OKVED.OKVEDCode.RecordCode;
         Category_temp.IsSingle = GetSingleSign(OBJTYPE_PARTY, 64);
         Categories.value(Categories.size) = Category_temp;
      end;
   end;
   
   // контакты
   if ( (ValType(IncomeData.Phone) != V_UNDEF) or (ValType(IncomeData.Fax) != V_UNDEF) or (ValType(IncomeData.Email) != V_UNDEF) )
      Contacts = TArray;
      
      if (ValType(IncomeData.Phone) != V_UNDEF)
         Contacts_temp = TWsContactInfo;
         Contacts_temp.Kind = 1;       // телефон
         if (ValType(IncomeData.Fax) != V_UNDEF)
            Contacts_temp.PhoneNumber2 = IncomeData.Fax;
         end;
         Contacts_temp.Category = 3;   // по адресу
         Contacts_temp.Value = IncomeData.Phone;
         Contacts_temp.AdresType = 1;  // юридический
         
         Contacts.value(Contacts.size) = Contacts_temp;
      end;

      if (ValType(IncomeData.Email) != V_UNDEF)
         Contacts_temp = TWsContactInfo;
         Contacts_temp.Kind = 5;       // адрес электронной почты
         Contacts_temp.Category = 8;   // по адресу
         Contacts_temp.Value = IncomeData.Email;
         Contacts_temp.AdresType = 1;  // юридический
         
         Contacts.value(Contacts.size) = Contacts_temp;
      end;
   end;

   // адреса
   aux_buff = ValType(IncomeData.AddressList);  // тип элемента 
   if(aux_buff == V_GENOBJ)
      Addresses = TArray;
      aux_buff = 0;
      while(IncomeData.AddressList.size > aux_buff)
         address_temp = TWsAdres;
         address_temp.Type = TArray;
         address_temp.Type.value(address_temp.Type.size) = IncomeData.AddressList.value(aux_buff).mAddressType; 
         if (ValType(IncomeData.AddressList.value(aux_buff).CountryCode) != V_UNDEF)
            address_temp.Country = IncomeData.AddressList.value(aux_buff).CountryCode.RecordCode;
         else
            address_temp.Country = "RUS";
         end;
         address_temp.PostIndex = IncomeData.AddressList.value(aux_buff).PostalCode;
         // область
         if (ValType(IncomeData.AddressList.value(aux_buff).RegionGNI) != V_UNDEF)
            address_temp.Region = GetRegionCode_ByCode(IncomeData.AddressList.value(aux_buff).RegionGNI.RecordCode, address_temp.RegionNumber);
            address_temp.CodeRegion = GetCodeAdmUnit(address_temp.Region, "REGION");

            if ( (IncomeData.Kind.RecordCode == 1) or (IncomeData.Kind.RecordCode == 2) ) // ЮЛ или банк
               TaxInstitution = InstParam.TaxInstitution;
            else
               TaxInstitution = PersnParam.TaxInstitution;
            end;
            if (not TaxInstitution) 
               pp = GetPartyID_byPartyCode(PTCK_MNS, IncomeData.AddressList.value(aux_buff).RegionGNI.RecordCode+"00");
               if (pp > 0)
                  if ( (IncomeData.Kind.RecordCode == 1) or (IncomeData.Kind.RecordCode == 2) ) // ЮЛ или банк
                     InstParam.TaxInstitution = pp;
                  else
                     PersnParam.TaxInstitution = pp;
                  end;
               end;
            end;
         end;
         if (Valtype(IncomeData.AddressList.value(aux_buff).District) != V_UNDEF)
            address_temp.Province = IncomeData.AddressList.value(aux_buff).District;   // район
            address_temp.CodeProvince = GetCodeAdmUnit(address_temp.Province, "PROVINCE");
         end;
         if (Valtype(IncomeData.AddressList.value(aux_buff).Locality) != V_UNDEF)
            address_temp.Place = IncomeData.AddressList.value(aux_buff).Locality;      // населенный пункт
            address_temp.CodePlace = GetCodeAdmUnit(address_temp.Place, "PLACE");
         end;
         if (Valtype(IncomeData.AddressList.value(aux_buff).City) != V_UNDEF)
            address_temp.District = IncomeData.AddressList.value(aux_buff).City;       // город
            address_temp.CodeDistrict = GetCodeAdmUnit(address_temp.District, "CITY");
         end;
         if (Valtype(IncomeData.AddressList.value(aux_buff).Street) != V_UNDEF)
            address_temp.Street = IncomeData.AddressList.value(aux_buff).Street;       // улица
            address_temp.CodeStreet = GetCodeAdmUnit(address_temp.Street, "STREET");
         end;

         address_temp.House = IncomeData.AddressList.value(aux_buff).House;
         address_temp.NumCorps = IncomeData.AddressList.value(aux_buff).Building;
         address_temp.Flat = IncomeData.AddressList.value(aux_buff).Apartment;
         
         // строение
         if (ValType(IncomeData.AddressList.value(aux_buff).Structure) != V_UNDEF)
            Structure_building = Trim(IncomeData.AddressList.value(aux_buff).Structure);
            if (ValType(IncomeData.AddressList.value(aux_buff).Building) == V_UNDEF)
               address_temp.NumCorps = IncomeData.AddressList.value(aux_buff).Structure;
            else
               NumCopr_building = Trim(IncomeData.AddressList.value(aux_buff).Building);
               address_temp.NumCorps = NumCopr_building + " стр " + Structure_building;
               if ( StrLen(address_temp.NumCorps) > 10 )
                  if ( StrLen(NumCopr_building) + StrLen(Structure_building) > 5) 
                     address_temp.NumCorps = NumCopr_building + " " + Structure_building;
                  end;
               end;
            end;
         end;

         address_temp.Adress = IncomeData.AddressList.value(aux_buff).FullAddress;
         if (ValType(address_temp.Adress) == V_UNDEF)
             address_temp.Adress = usr_GenResultAddressUser(address_temp);
         end;
         
         Addresses.value(Addresses.size) = address_temp;
         aux_buff = aux_buff + 1;
      end;
   end;  
  
   // сотрудники ????
/*2 директор  var HeadFIO             : String;   // ФИО руководителя                       Нет
4 глав бух   var AccountantFIO       : String;   // ФИО главного бухгалтера                Нет
   */
      // сотрудники
/*         Employees = TArray;
         Employees.value(Employees.size) = adp_Employees();*/
         
/* ???
   var CorrAccount         : String;   // Корреспондентский счет                 Нет
   var RegDate             : Date;     // Дата гос.регистрации                   Нет
   var RegBy               : String;   // Орган гос.регистрации                  Нет
   var HeadFIO             : String;   // ФИО руководителя                       Нет
   var AccountantFIO       : String;   // ФИО главного бухгалтера                Нет
   var CategoryDiasoft     : String;   // Категория клиента относительно Диасофт Нет
   var NumContract         : String;   // Номер согласшения в ДИАСОФТ            Нет
   var UNKg                : String;   // Уникальный номер клиента глобальный    Нет
   var Consent             : String;   // Согласие на передачу сведений в БКИ    Нет
   var SubjectCode         : string;   // Код субъекта кредитной истории         Нет
   var OKOGU               : string;   // Российская классификация государственных, правительственных и административных подразделений Нет
   var Town                : string;   // Наименование населенного пункта        Нет
   var TownType            : string;   // Тип населенного пункта                 Нет
   var CustStat            : string;   // Статус организации                     Нет
   var OrgVid              : string;   // Орган, выдавший свидетельство          Нет
   var RegPlace            : string;   // Место регистрации                      Нет
   var NumberPFR           : string;   // Страховой номер ПФР                    Нет
   
  
   // справочники
   var Ownership           ;           // Форма собственности                    Нет   Справочник
   var ResponseZone        ;           // Зона ответственности бизнес-ССП ГО     Нет   Справочник
   var IndividualRevenue   ;           // Сегмент по индивидуальной выручке      Нет   Справочник
   var ProjectOrganization ;           // Принадлежность к проектным компаниям   Нет   Справочник
   var GKSSegment          ;           // Ответственная служба                   Нет   Справочник
   var FinalSegment        ;           // Итоговый сегмент клиента               Нет   Справочник
   var SeparateEntity      ;           // Отдельные сегменты хозяйствования      Нет   Справочник
   var ResponseService     ;           // Ответственная служба                   Нет   Справочник
   var SMBusinesses        ;           // Субъект МСП                            Нет   Справочник
   var Entity              ;           // Вид субъекта                           Нет   Справочник
   var Client              ;           // Вид отношений с банком                 Нет   Справочник
   var CountryReg          ;           // Страна регистрации компании, для обработки по Положению 161-П     Нет
   var RatingF             ;           // Рейтинг Fitch Raitings                                            Нет
   var RatingM             ;           // Рейтинг Moody`s                                                   Нет
   var RatingSP            ;           // Рейтинг S and P                                                   Нет
   
   // символьные
   var FilialId            ;           // Код филиала                                           Да
   var BranchId            ;           // Код подразделения                                     Да
??? */
   
end;

/**
@brief класс параметров из входящего запроса
@param[in] p_req_data_obj входящий объект XMLRPC
*/
private class c_PartyParm(p_req_data_obj)
   var LegalEntity;
   var Name                : String;   // Полное наименование                    Да
   var ShortName           : String;   // Краткое наименование                   Нет
   var INN                 : String;   // ИНН                                    Нет
   var KPP                 : String;   // КПП                                    Нет
   var KIO                 : String;   // КИО                                    Нет
   var LatinName           : String;   // Полное латинское наименование          Нет
   var ShortLatinName      : String;   // Краткое латинское наименование         Нет
   var OKPO                : String;   // ОКПО                                   Нет
   var BIK                 : String;   // БИК                                    Нет
   var CorrAccount         : String;   // Корреспондентский счет                 Нет
   var SWIFT               : String;   // SWIFT                                  Нет
   var CBRLicense          : String;   // Лицензия ЦБ РФ                         Нет
   var OKATO               : String;   // ОКАТО                                  Нет
   var OGRN                : String;   // ОГРН/ОРГНИП                            Нет
   var RegDate             : Date;     // Дата гос.регистрации                   Нет
   var RegBy               : String;   // Орган гос.регистрации                  Нет
   var HeadFIO             : String;   // ФИО руководителя                       Нет
   var AccountantFIO       : String;   // ФИО главного бухгалтера                Нет
   var Phone               : String;   // Телефон                                Нет
   var Fax                 : String;   // ФАКС                                   Нет
   var Email               : String;   // Электронная почта                      Нет
   var CategoryDiasoft     : String;   // Категория клиента относительно Диасофт Нет
   var NumContract         : String;   // Номер согласшения в ДИАСОФТ            Нет
   var UNKg                : String;   // Уникальный номер клиента глобальный    Нет
   var Consent             : String;   // Согласие на передачу сведений в БКИ    Нет
   var SubjectCode         : string;   // Код субъекта кредитной истории         Нет
   var OKOGU               : string;   // Российская классификация государственных, правительственных и административных подразделений Нет
   var Town                : string;   // Наименование населенного пункта        Нет
   var TownType            : string;   // Тип населенного пункта                 Нет
   var CustStat            : string;   // Статус организации                     Нет
   var Document            : string;   // Номер документа                        Нет
   var Birthday            : date;     // Дата рождения                          Нет
   var OrgVid              : string;   // Орган, выдавший свидетельство          Нет
   var IssueName           : string;   // Кем выдан ДУЛ                          Нет
   var DateVid             : date;     // Дата выдачи документа                  Нет
   var RegPlace            : string;   // Место регистрации                      Нет
   var BirthPlace          : string;   // Место рождения                         Нет
   var NumberPFR           : string;   // Страховой номер ПФР                    Нет
   
   // списки
   var AddressList;                    // Список адресов                         Нет
   var GVKList;                        // Группы взаимосвязанных клиентов        Нет
   var OKVEDList;                      // Список ОКВЭД                           Нет
   
   // справочники
   var OPF                 ;           // Организационно-правовая форма          Нет   Справочник
   var Kind                ;           // Тип субъекта                           Да    Справочник  "1 = ЮЛ 2 = Банк 3 = ИП"
   var Resident            ;           // Резидентность                          Нет   Справочник  "1 = Резидент 2 = Не резидент"
   var Country             ;           // Страна резидентности                   Нет   Справочник
   var Ownership           ;           // Форма собственности                    Нет   Справочник
   var ResponseZone        ;           // Зона ответственности бизнес-ССП ГО     Нет   Справочник
   var IndividualRevenue   ;           // Сегмент по индивидуальной выручке      Нет   Справочник
   var ProjectOrganization ;           // Принадлежность к проектным компаниям   Нет   Справочник
   var GKSSegment          ;           // Ответственная служба                   Нет   Справочник
   var FinalSegment        ;           // Итоговый сегмент клиента               Нет   Справочник
   var SeparateEntity      ;           // Отдельные сегменты хозяйствования      Нет   Справочник
   var ResponseService     ;           // Ответственная служба                   Нет   Справочник
   var SMBusinesses        ;           // Субъект МСП                            Нет   Справочник
   var Entity              ;           // Вид субъекта                           Нет   Справочник
   var Client              ;           // Вид отношений с банком                 Нет   Справочник
   var FATCA               ;           // FATCA-статус Возможные значения Да/Нет                            Да
   var CRS                 ;           // Статус клиента в целях CRS. Допустимые значения: Да/Нет           Да
   var CountryReg          ;           // Страна регистрации компании, для обработки по Положению 161-П     Нет
   var RatingF             ;           // Рейтинг Fitch Raitings                                            Нет
   var RatingM             ;           // Рейтинг Moody`s                                                   Нет
   var RatingSP            ;           // Рейтинг S and P                                                   Нет
   var TypeDocument        ;           // Тип документа                                                     Нет
   
   // символьные
   var FilialId            ;           // Код филиала                                           Да
   var BranchId            ;           // Код подразделения                                     Да
   var OperationalRecordId ;           // Идентификатор карточки клиента (операционные данные)  Да
   var SOFRId              ;           // Идентификатор карточки клиента в СОФР                 Нет
   var CIFId               ;           // Идентификатор клиента в CIF                           Да

   /* Кратность параметров в соответствии со спецификацией */
   private macro uInitPrime(p_req_data_obj)
      private var aux_buff;
      private var err_txt;
      private var nametag_up = "ClientCreateLERequestMsg.ClientCreateLERequest.LegalEntity";
      
      // верхний уровень
      LegalEntity = getValueFromProperty(p_req_data_obj, "LegalEntity", nametag_up, true);
      
      Name           = getValueFromProperty(LegalEntity, "Name"            , nametag_up, true);
      ShortName      = getValueFromProperty(LegalEntity, "ShortName"       , nametag_up);
      INN            = getValueFromProperty(LegalEntity, "INN"             , nametag_up);
      KPP            = getValueFromProperty(LegalEntity, "KPP"             , nametag_up);
      KIO            = getValueFromProperty(LegalEntity, "KIO"             , nametag_up);
      LatinName      = getValueFromProperty(LegalEntity, "LatinName"       , nametag_up);
      ShortLatinName = getValueFromProperty(LegalEntity, "ShortLatinName"  , nametag_up);
      OKPO           = getValueFromProperty(LegalEntity, "OKPO"            , nametag_up);
      BIK            = getValueFromProperty(LegalEntity, "BIK"             , nametag_up);
      CorrAccount    = getValueFromProperty(LegalEntity, "CorrAccount"     , nametag_up);
      SWIFT          = getValueFromProperty(LegalEntity, "SWIFT"           , nametag_up);
      CBRLicense     = getValueFromProperty(LegalEntity, "CBRLicense"      , nametag_up);
      OKATO          = getValueFromProperty(LegalEntity, "OKATO"           , nametag_up);
      OGRN           = getValueFromProperty(LegalEntity, "OGRN"            , nametag_up);
      RegDate        = getValueFromProperty(LegalEntity, "RegDate"         , nametag_up);
      RegBy          = getValueFromProperty(LegalEntity, "RegBy"           , nametag_up);
      HeadFIO        = getValueFromProperty(LegalEntity, "HeadFIO"         , nametag_up);
      AccountantFIO  = getValueFromProperty(LegalEntity, "AccountantFIO"   , nametag_up);
      Phone          = getValueFromProperty(LegalEntity, "Phone"           , nametag_up);
      Fax            = getValueFromProperty(LegalEntity, "Fax"             , nametag_up);
      Email          = getValueFromProperty(LegalEntity, "Email"           , nametag_up);
      CategoryDiasoft= getValueFromProperty(LegalEntity, "CategoryDiasoft" , nametag_up);
      NumContract    = getValueFromProperty(LegalEntity, "NumContract"     , nametag_up);
      UNKg           = getValueFromProperty(LegalEntity, "UNKg"            , nametag_up);
      Consent        = getValueFromProperty(LegalEntity, "Consent"         , nametag_up);
      SubjectCode    = getValueFromProperty(LegalEntity, "SubjectCode"     , nametag_up);
      OKOGU          = getValueFromProperty(LegalEntity, "OKOGU"           , nametag_up);
      Town           = getValueFromProperty(LegalEntity, "Town"            , nametag_up);
      TownType       = getValueFromProperty(LegalEntity, "TownType"        , nametag_up);
      CustStat       = getValueFromProperty(LegalEntity, "CustStat"        , nametag_up);
      Document       = getValueFromProperty(LegalEntity, "Document"        , nametag_up);
      Birthday       = getValueFromProperty(LegalEntity, "Birthday"        , nametag_up);
      OrgVid         = getValueFromProperty(LegalEntity, "OrgVid"          , nametag_up);
      IssueName      = getValueFromProperty(LegalEntity, "IssueName"       , nametag_up);
      DateVid        = getValueFromProperty(LegalEntity, "DateVid"         , nametag_up);
      RegPlace       = getValueFromProperty(LegalEntity, "RegPlace"        , nametag_up);
      BirthPlace     = getValueFromProperty(LegalEntity, "BirthPlace"      , nametag_up);
      NumberPFR      = getValueFromProperty(LegalEntity, "NumberPFR"       , nametag_up);
      
      OPF                  = getDictionaryFromProperty(LegalEntity, "OPF"                 , nametag_up);
      Kind                 = getDictionaryFromProperty(LegalEntity, "Kind"                , nametag_up, true);
      Resident             = getDictionaryFromProperty(LegalEntity, "Resident"            , nametag_up);
      Country              = getDictionaryFromProperty(LegalEntity, "Country"             , nametag_up);
      Ownership            = getDictionaryFromProperty(LegalEntity, "Ownership"           , nametag_up);
      ResponseZone         = getDictionaryFromProperty(LegalEntity, "ResponseZone"        , nametag_up);
      IndividualRevenue    = getDictionaryFromProperty(LegalEntity, "IndividualRevenue"   , nametag_up);
      ProjectOrganization  = getDictionaryFromProperty(LegalEntity, "ProjectOrganization" , nametag_up);
      GKSSegment           = getDictionaryFromProperty(LegalEntity, "GKSSegment"          , nametag_up);
      FinalSegment         = getDictionaryFromProperty(LegalEntity, "FinalSegment"        , nametag_up);
      SeparateEntity       = getDictionaryFromProperty(LegalEntity, "SeparateEntity"      , nametag_up);
      ResponseService      = getDictionaryFromProperty(LegalEntity, "ResponseService"     , nametag_up);
      SMBusinesses         = getDictionaryFromProperty(LegalEntity, "SMBusinesses"        , nametag_up);
      Entity               = getDictionaryFromProperty(LegalEntity, "Entity"              , nametag_up);
      Client               = getDictionaryFromProperty(LegalEntity, "Client"              , nametag_up);
      FATCA                = getDictionaryFromProperty(LegalEntity, "FATCA"               , nametag_up, true);
      CRS                  = getDictionaryFromProperty(LegalEntity, "CRS"                 , nametag_up, true);
      CountryReg           = getDictionaryFromProperty(LegalEntity, "CountryReg"          , nametag_up);
      RatingF              = getDictionaryFromProperty(LegalEntity, "RatingF"             , nametag_up);
      RatingM              = getDictionaryFromProperty(LegalEntity, "RatingM"             , nametag_up);
      RatingSP             = getDictionaryFromProperty(LegalEntity, "RatingSP"            , nametag_up);
      TypeDocument         = getDictionaryFromProperty(LegalEntity, "TypeDocument"        , nametag_up);
      
      FilialId             = getSymbolFromProperty(LegalEntity, "FilialId"             , nametag_up, true);
      BranchId             = getSymbolFromProperty(LegalEntity, "BranchId"             , nametag_up, true);
      OperationalRecordId  = getSymbolFromProperty(LegalEntity, "OperationalRecordId"  , nametag_up, true);
      SOFRId               = getSymbolFromProperty(LegalEntity, "SOFRId"               , nametag_up);
      CIFId                = getSymbolFromProperty(LegalEntity, "CIFId"                , nametag_up, true);
      
      // проверка по списку значений
      if(ValType(Kind) != V_UNDEF)
         if ( (Kind.RecordCode == "1") or (Kind.RecordCode == "2") or (Kind.RecordCode == "3") )
         else
            err_txt = string("Недопустимое значение "+nametag_up+".Kind.RecordCode =",Kind.RecordCode);
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR)); 
         end;
      end;
      
      if(ValType(Resident) != V_UNDEF)
         if ( (Resident.RecordCode == "1") or (Resident.RecordCode == "2") )
         else
            err_txt = string("Недопустимое значение "+nametag_up+".Resident.RecordCode=",Resident.RecordCode);
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR)); 
         end;
      end;

      aux_buff = uTryToGetPropS(LegalEntity, "AddressList");
      if(ValType(aux_buff) == V_GENOBJ)
         AddressList = TArray;
         aux_buff = 0;
         while(LegalEntity.AddressList.size > aux_buff)
            AddressList.value(AddressList.size) = adp_Address(LegalEntity.AddressList.value(aux_buff), nametag_up+".AddressList");
            aux_buff = aux_buff + 1;
         end;
      elif(ValType(aux_buff) == V_UNDEF)
      else
         err_txt = string(InErrNotObj, "LegalEntity.AddressList");
         RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
      end;  
      
      aux_buff = uTryToGetPropS(LegalEntity, "GVKList");
      if(ValType(aux_buff) == V_GENOBJ)
         GVKList = TArray;
         aux_buff = 0;
         while(LegalEntity.GVKList.size > aux_buff)
            GVKList.value(GVKList.size) = adp_GVK(LegalEntity.GVKList.value(aux_buff), nametag_up+".GVKList");
            aux_buff = aux_buff + 1;
         end;
      elif(ValType(aux_buff) == V_UNDEF)
      else
         err_txt = string(InErrNotObj, "LegalEntity.GVKList");
         RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
      end;        

      aux_buff = uTryToGetPropS(LegalEntity, "OKVEDList");
      if(ValType(aux_buff) == V_GENOBJ)
         OKVEDList = TArray;
         aux_buff = 0;
         while(LegalEntity.OKVEDList.size > aux_buff)
            OKVEDList.value(OKVEDList.size) = adp_OKVED(LegalEntity.OKVEDList.value(aux_buff), nametag_up+".OKVEDList");
            aux_buff = aux_buff + 1;
         end;
      elif(ValType(aux_buff) == V_UNDEF)
      else
         err_txt = string(InErrNotObj, "LegalEntity.OKVEDList");
         RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
      end;  

   end; /* macro uInitPrime() */

   uInitPrime(p_req_data_obj);      
end;

/**
@brief непосредственная обработка единичной записи
@param[in] p_req_data_obj входящий объект XMLRPC
@param[out] p_ClientId Id созданного/обновленного субъекта
@param[out] p_CodeValue код АБС созданного/обновленного субъекта
@param[in] p_transparent_id сквозной id XML-запроса
@param[out] out_WillMakeCIFCode признак необходимости создания ссылки в CIF (не используется)
@param[out] out_MasterExternalId класс для создания ссылки в CIF (не используется)
@return ErrorAction класс ошибок по единичной записи 
*/
macro InsertClient_single(p_req_data_obj, p_ClientId, p_CodeValue, p_transparent_id, out_WillMakeCIFCode:@bool, out_MasterExternalId:@c_IntegrationSymbolicIdentifierXType) 
   var ErrorAction;
   
   var req_obj_serv = NULL;
   var pparty = NULL;
   var resp_obj_serv = NULL;

   private var resp_resultCode, resp_resultText;
   
   private var party_name;
   
   private var PartyID_found = -1;
   
   private var SOFRId = NULL;
   private var vINN;
   private var vSize = 0;
   
   private var code_value_number = NULL, code_value, pos, type_code_found;
   private var code_number = PARTY_CODEKIND_ABS, num_filial;

   private var PersonGetXrefLE_req = NULL;
   private var PersonGetXrefLE_resp = NULL;
// BIQ-9555 замена на CDI
   private var CdiGetOrgXrefReq_resp = NULL;
   private var Id_CDI, CodeFromCDI;   
   
   private var CodeFromCIF;

   //BIQ-10268
   private var cdi_isactive, needcdi=false;
   cdi_isactive = uGetRegistryParam(REG_CDI_ACTIVE, V_BOOL);

   // входящие параметры
   req_obj_serv = c_PartyParm(p_req_data_obj);
   
   //поиск Субъекта в БД СОФР по переданным идентификаторам: ИД в АБС, ИД в СОФР (если передан);
   // SOFRID (partyid)
   if (Valtype(req_obj_serv.SOFRId) != V_UNDEF)
      PartyID_found = GetPartyID_byStringPartyID(req_obj_serv.SOFRId.ObjectID); // поиск по коду в СОФР
   end;

/*      if (ValType(req_obj_serv.FilialId) != V_UNDEF)
         num_filial = req_obj_serv.FilialId.ObjectId;
      else
         num_filial = "0000";
      end;*/
   num_filial = "0000";

   // код АБС
   code_value = req_obj_serv.OperationalRecordId.ObjectID;
   pos = 0;
   pos = Index(code_value,"cust-corp");
   if (pos > 0)
      code_value = Substr(code_value,pos+9);
      code_value_number = code_value;
      code_number = PARTY_CODEKIND_ABS;
      type_code_found = true;
   end;
   if (not type_code_found)
      pos = Index(code_value,"banks");
      if (pos > 0)
         code_value = Substr(code_value,pos+6);
         code_value_number = code_value;
         code_number = PARTY_CODEKIND_ABS;
         type_code_found = true;
      end;
   end;
   
   // OperationalRecordId (код АБС)
   if ( (PartyID_found <= 0) // субъект не найден
      and (Valtype(req_obj_serv.OperationalRecordId) != V_UNDEF) )

      PartyID_found = GetPartyID_byPartyCode(code_number, code_value); 
   end;
   
   if (PartyID_found <= 0) // субъект не найден ни по коду АБС, ни по SOFRId
      // переключатель BIQ-9555
      var v_use_cdi = uGetRegistryParam(USE_CDI, V_BOOL);
      // BIQ-10268 еще одна настройка для обмена с CDI с более широким набором запросов
      if (not v_use_cdi)
         v_use_cdi = cdi_isactive;
      end;

      if (not v_use_cdi)
         // Если Субъект не найден, то производится поиск Клиента в CIF	
         // Производится Запрос ссылок (идентификаторов) по Клиенту из CIF ЮЛ 
         if (Valtype(req_obj_serv.OperationalRecordId) != V_UNDEF) 
            PersonGetXrefLE_req = c_PersonGetXRefLEReq;
            PersonGetXrefLE_req.MasterDataId.ObjectId = req_obj_serv.OperationalRecordId.ObjectID;
            PersonGetXrefLE_req.MasterDataId.SystemId = "CFT";
            PersonGetXrefLE_req.MasterDataId.SystemNodeId = "CFT";
         end;
         if (ValType(PersonGetXrefLE_req) != V_UNDEF)
            PersonGetXrefLE_resp = PersonGetXRefLEReq(PersonGetXrefLE_req, NULL, p_transparent_id);

            if (ValType(PersonGetXrefLE_resp) != V_UNDEF)  // ответ получен
               CodeFromCIF = PersonGetXrefLE_resp.GetSystemXref("SOFR");
               if (CodeFromCIF.ObjectId != "-1")
                  PartyID_found = GetPartyID_byStringPartyID(CodeFromCIF.ObjectId);  // здесь должен быть PartyID
               end;
               if (PartyID_found < 0)
                  out_WillMakeCIFCode = true;
               end;
            else
               out_WillMakeCIFCode = true;
            end;
         end;
      end;
   end;

   //CCBO-10808 не все субъекты выгружаются в CDI
   var not_send_cdi = false;
   if (PartyID_found > 0)
      if (CheckBlockUnloadCDI(PartyID_found)) 
          //субъект заблокирован для выгрузки в CDI, запросы на поиск и создание/обновление не отправляются
          not_send_cdi = true;
      end;
   end;

   if (not_send_cdi)
      //ничего не делаем
   else 
      // BIQ-10268 поиск в CDI производится всегда
        // BIQ-9555 замена на CDI
      if (Valtype(req_obj_serv.OperationalRecordId) != V_UNDEF) 
         if (code_value != V_UNDEF)
            var code_value_obj = req_obj_serv.OperationalRecordId; // тип c_IntegrationSymbolicIdentifierXType
            code_value_obj.ObjectID = code_value;
            CdiGetOrgXrefReq_resp = CdiGetOrgXrefReq (code_value_obj, p_transparent_id);
            if (ValType(CdiGetOrgXrefReq_resp) != V_UNDEF)  // ответ получен
               if (ValType(CdiGetOrgXrefReq_resp.ExtIdList) != V_UNDEF)
                  if (PartyID_found <= 0) 
                     CodeFromCDI = CdiGetOrgXrefReq_resp.GetSystemXref("SOFR");
                     if (CodeFromCDI.ExtIdPartySourceId.ObjectId != "-1")
                        PartyID_found = GetPartyID_byStringPartyID(CodeFromCDI.ExtIdPartySourceId.ObjectId);  // здесь должен быть PartyID
                     end;
                  end;
                  if (PartyID_found <= 0) 
                     CodeFromCDI = CdiGetOrgXrefReq_resp.GetSystemXref("SOFR_UL");
                     if (CodeFromCDI.ExtIdPartySourceId.ObjectId != "-1")
                        PartyID_found = GetPartyID_byStringPartyID(CodeFromCDI.ExtIdPartySourceId.ObjectId);  // здесь должен быть PartyID
                     end;
                  end;
                  CodeFromCDI = CdiGetOrgXrefReq_resp.GetSystemXref("CFT");
                  Id_CDI = CodeFromCDI.PartyId;  // код CDI, в виде объекта
               end;
            end;
         end;
      end;
   end;
         
   if (out_WillMakeCIFCode)
      // BIQ-9555 Замена на CDI
      // Сохраним ID CDI, считаем, что ссылка на АБС уже есть, поэтому этот ID тоже в наличии
      if (ValType(Id_CDI) != V_UNDEF)
         out_MasterExternalId = Id_CDI;
      end;
   end;

   if (PartyID_found <= 0)    // все еще не нашли субъекта
   // Если кросс-ссылки СОФР нет, то производится поиск Клиента в БД СОФР: 
      if ( (req_obj_serv.Kind.RecordCode == 1) or (req_obj_serv.Kind.RecordCode == 3) )
         // h.	Если Тип переданного Клиента = ЮЛ или =ИП, то производится поиск Субъекта по параметрам:
         // i.	ИНН;
         //shev 030620 если не передали ни одного id, реджектим
         if ((Valtype(req_obj_serv.SOFRId) == V_UNDEF) and (Valtype(req_obj_serv.OperationalRecordId) == V_UNDEF))
            resp_resultCode = ERR_CODE_TECH_ERROR;
            resp_resultText = "Не передан один из обязательных параметров BisquitId или SofrId";
         end;

         vINN = req_obj_serv.INN;
         /*if (ValType(req_obj_serv.KPP) != V_UNDEF)
            vINN = vINN + "/" + req_obj_serv.KPP;
         end;*/
         PartyID_found = GetPartyID_byPartyCode(PTCK_INN, vINN);

         //shev 030620 если нашли по ИНН, но не совпал бисквит id, реджектим
         if (PartyID_found >= 0) 
            var sql = " select t_code from dobjcode_dbt where t_objecttype =3";
                sql = sql + " and t_codekind = 101 and t_objectid = "+PartyID_found;
            var cmd = RSDCommand(sql);
            var rs = RSDRecordSet(cmd);
            if(rs.movenext)
               if(code_value != rs.value("t_code"))
                 resp_resultCode = ERR_CODE_TECH_ERROR;
                 resp_resultText = "Переданный BisquitId не совпадает с найденным по ИНН";
               end;
            end;
         end;

         if (PartyID_found <= 0)
            PartyID_found = GetPartyID_byPartyCode(27, req_obj_serv.OGRN);
         end;
      elif (req_obj_serv.Kind.RecordCode == 2)
         // i.	Если Тип переданного Клиента = кредитная организация, то производится поиск Субъекта по параметрам:
         //  i.	БИК;
         PartyID_found = GetPartyID_byPartyCode(PTCK_BIC, req_obj_serv.BIK);
      end;
   end;
               
   if (PartyID_found > 0) // субъект найден в АБС
      //	Производится обновление данных по Субъекту, в соответствии с полученными значениями, обработка данных прекращается, формируется ответное сообщение, которое возвращается в Адаптер;
      //         j.	Если Субъект найден, - производится обновление данных по Субъекту (только для ЮЛ и ИП). 

      resp_obj_serv = PartyID_found;
      resp_resultCode = 0;
      resp_resultText = "OK";

      //BIQ-10268 при работе с CDI, если клиент уже есть и у него есть код CDI, то клиента должен обновлять запрос Publish из CDI, а не запрос из ЦФТ
      if (cdi_isactive)
         if (not_send_cdi)
            needcdi = false;
         else 
            needcdi = NeedSendCDI(PartyID_found, date());  // для юр.лиц или физ.лиц с признаками. Теоретически нет других объектов, которые могут прийти в запросе ClientCreate, проверка избыточна
         end;
         if (needcdi)
            var stat = true;
            var code_for_check = GetPartyCode_ByNumber(PartyID_found, PARTY_CODEKIND_CDI);
            var need_update_code = false;
            if (code_for_check) // код CDI в наличии, проверим на совпадение
               if (ValType(Id_CDI) != V_UNDEF) // код должен быть заполнен
                  if (code_for_check != Id_CDI.ObjectId) // так быть не должно, откуда взялся некорректный код?
                     need_update_code = true;
                  end;
               end;
            else 
               need_update_code = true;
            end;
            if (need_update_code and (ValType(Id_CDI) == V_GENOBJ)) // только если есть, на что обновлять
               var PartyforUpdateCode = RSBParty(PartyID_found); 
               stat = PartyforUpdateCode.Code.SetCode(PARTY_CODEKIND_CDI, Id_CDI.ObjectId);
               if(not stat)
                  resp_resultCode = ERR_CODE_TECH_ERROR;
                  resp_resultText = "Ошибка установки кода вида " + string(PARTY_CODEKIND_CDI);
               else 
                  stat = PartyforUpdateCode.Update();
                  if(not stat)
                     resp_resultCode = ERR_CODE_TECH_ERROR;
                     resp_resultText = "Ошибка сохранения субъекта ";
                  end;
               end;
               if (stat)
                  PutInProcess(SERV_CDI_GET_ORG, PartyID_found, 1, "4,5,6", Id_CDI.ObjectId); // создать событие GetOrg
               end;
            else // код 110 был, совпал, должен быть запрос CdiPublish, больше ничего не делаем
            end;
         end;
      end;

      pparty = adp_Party(req_obj_serv, code_value_number, PartyID_found); 
      if (not needcdi) // при включенном обмене с CDI в эту ветку не должно заходить никогда, так как нет других субъектов
         UpdateParty(
                      PartyID_found
                     ,pparty.PartyCode
                     ,pparty.PersnParam
                     ,pparty.InstParam
                     ,pparty.ClientParam
                     ,pparty.Codes
                     ,pparty.Addresses
                     ,pparty.Contacts
                     ,pparty.PersnPapers
                     ,pparty.RegDocs
                     ,pparty.PartyNotes
                     ,pparty.PersnNotes
                    );
         // процедура ничего не возвращает, поэтому если не выпало в ошибку, считаем, что все нормально
      end;

      SetParm(1,resp_obj_serv);
      SetParm(2,code_value_number);
      
   else  // субъект не найден
      //            k.	Если Субъект не найден, то производится создание нового Субъекта в СОФР. При этом: 
      //               i.	В параметрах Субъекта сохраняются: Идентификатор Клиента в АБС, и Глобальный идентификатор, полученный из CIF. 
      //               ii.	Формируется запись в Таблицу Событий по Типу Объекта = <Кросс-ссылка Субъекта>.
      pparty = adp_Party(req_obj_serv, code_value_number, -1);

      if (cdi_isactive)
         // если код CDI в наличии, добавим к кодам
         if (ValType(Id_CDI) == V_GENOBJ)   
            var Code_temp_cdi = TWsPartyCode;
            Code_temp_cdi.CodeKind = PARTY_CODEKIND_CDI;
            Code_temp_cdi.Code = Id_CDI.ObjectId;
            pparty.Codes.value(pparty.Codes.size) = Code_temp_cdi;
         end;
         // DEF-56111 не передаем контакты
         pparty.Addresses = NULL;
         pparty.Contacts = NULL;
      end;

      resp_obj_serv = InsertParty(
                                  pparty.PersnParam
                                 ,pparty.InstParam
                                 ,pparty.ClientParam
                                 ,pparty.Codes
                                 ,pparty.Addresses
                                 ,pparty.Contacts
                                 ,pparty.PersnPapers
                                 ,pparty.RegDocs
                                 ,pparty.PartyNotes
                                 ,pparty.PersnNotes
                                );
      if (ValType(resp_obj_serv) == V_UNDEF)
         resp_resultCode = ERR_CODE_TECH_ERROR;
         resp_resultText = getErrMsg();
      else
         resp_resultCode = 0;
         resp_resultText = "OK";
         SetParm(1,resp_obj_serv);
         SetParm(2,code_value_number);

         if (cdi_isactive)
            needcdi = NeedSendCDI(resp_obj_serv, date());  // для юр.лиц или физ.лиц с признаками. Теоретически нет других объектов, которые могут прийти в запросе ClientCreate, проверка избыточна
            if (needcdi)
               if (ValType(Id_CDI) == V_GENOBJ) // код должен быть заполнен
                  PutInProcess(SERV_CDI_GET_ORG, resp_obj_serv, 1, "4,5,6", Id_CDI.ObjectId); // создать событие GetOrg
               else // пойдем по более длинному пути через FindOrg
                  PutInProcess(SERV_CDI_FIND_ORG, resp_obj_serv, 1, "4,5,6", "-1"); // создать событие FindOrg
               end;
            end;
            var СубъектCDI = RsbParty(resp_obj_serv);
            var PartyCateg = RsbObjCategories( 3, String(СубъектCDI.partyid:o:10) );
            if(СубъектCDI.legalform == 1)
              var OGRN = ПолучитьКодСубъекта(СубъектCDI.partyid, 27, null, 0);
              var INN = ПолучитьКодСубъекта(СубъектCDI.partyid, PTCK_INN);
              if( (StrLen(INN) > 10) and (StrLen(OGRN) > 13) )
                   PartyCateg.ConnectAttr( 150, 3, null, null,{curdate});
                   PartyCateg.Save( String(СубъектCDI.partyid:o:10) );
              else 
                if( (StrLen(INN) > 10) and (StrLen(OGRN) == 0) )
                   PartyCateg.ConnectAttr( 150, 4, null, null,{curdate});
                   PartyCateg.Save( String(СубъектCDI.partyid:o:10) );
                else
                  PartyCateg.ConnectAttr( 150, 2, null, null,{curdate});
                  PartyCateg.Save( String(СубъектCDI.partyid:o:10) );
                end;
              end;
            end;

            if(СубъектCDI.legalform == 2)
              var sqlIE = "SELECT * " +
                          "  FROM (SELECT COUNT (1)     AS cat " +
                          "          FROM dobjatcor_dbt  c " +
                          "               INNER JOIN DOBJATTR_DBT a " +
                          "                   ON     a.t_groupid = c.t_groupid " +
                          "                      AND a.t_attrid = c.t_attrid " +
                          "                      AND a.t_objecttype = c.t_objecttype " +
                          "         WHERE     LPAD (" + СубъектCDI.partyid + ", 10, '0') = c.t_object " +
                          "               AND a.t_objecttype = 3 " +
                          "               AND a.t_groupid = 53 " +
                          "               AND a.t_nameobject LIKE '5 01%'), " +
                          "       (SELECT COUNT (1)     AS employer " +
                          "          FROM dpersn_dbt pers " +
                          "         WHERE pers.t_personid = " + СубъектCDI.partyid + " AND pers.t_isemployer = 'X')";

              var sqlPP = "SELECT COUNT (1) AS cat " +
                          "  FROM dobjatcor_dbt  c " +
                          "       INNER JOIN DOBJATTR_DBT a " +
                          "           ON     a.t_groupid = c.t_groupid " +
                          "              AND a.t_attrid = c.t_attrid " +
                          "              AND a.t_objecttype = c.t_objecttype " +
                          " WHERE     LPAD (" + СубъектCDI.partyid + ", 10, '0') = c.t_object " +
                          "       AND a.t_objecttype = 3 " +
                          "       AND a.t_groupid = 53 " +
                          "       AND a.t_nameobject LIKE '5 02%' ";
              var setCat = false;
              var cmdС = rsdCommand( sqlIE );
              var data = TRsbDataSet(cmdС);
              if (data.moveNext())
                if ((data.cat > 0) or (data.employer > 0))
                  PartyCateg.ConnectAttr( 150, 3, null, null,{curdate});
                  PartyCateg.Save( String(СубъектCDI.partyid:o:10) );
                  setCat = true;
                end;    
              end;
              
              cmdС = rsdCommand( sqlPP );
              data = TRsbDataSet(cmdС);
              if (data.moveNext())
                if (data.cat > 0)
                  PartyCateg.ConnectAttr( 150, 4, null, null,{curdate});
                  PartyCateg.Save( String(СубъектCDI.partyid:o:10) );
                  setCat = true;
                end;    
              end;
              if (setCat == false)
                PartyCateg.ConnectAttr( 150, 1, null, null,{curdate});
                PartyCateg.Save( String(СубъектCDI.partyid:o:10) );
              end;
            end;
         end;
      end;

   end;
   
   // Если Субъект в СОФР был успешно создан или обновлен, то:
       //  a.	Формируется ответное сообщение в АБС;
       //  b.	Формируется, при необходимости (определяется Настройками СОФР), соответствующее e-mail сообщение, которое отправляется ответственному сотруднику Банка.  
   // Если Субъект успешно создан, - то он становится доступным для использования в бизнес-процессах СОФР. Дальнейшие действия Пользователя определяются внутренними регламентами бизнес-процессов СОФР по работе с Субъектами, как если бы Субъект был введен непосредственно Пользователем в СОФР (см. документацию по Системе и ЧТЗ по бизнес-направлениям).
   // Пользователь в Системе, через соответствующий интерфейс, по каждому Субъекту, при необходимости, может видеть логи обмена данными с АС Банка.   
   
   if ( (resp_resultCode == 0) and (not needcdi))
      // наименования
      if (ValType(/*pparty.Names*/ uTryToGetPropS(pparty, "Names")) != V_UNDEF) //А.Киселев 23.10.202 падаем если нет ни одного наименования
         for (party_name, pparty.Names)
            SetPartyNames(resp_obj_serv, party_name.PartyNameID, party_name.Name);
         end;
      end;

      // принадлежности
      if (StrUpr(req_obj_serv.CategoryDiasoft) == "ЭМИТЕНТ")
         SetPartyOwn(resp_obj_serv, PTK_ISSUER); // контрагент по сделкам
      else
         SetPartyOwn(resp_obj_serv, PTK_CONTR); // контрагент по сделкам
         SetPartyOwn(resp_obj_serv, PTK_PACT ); // контрагент по договорам
      end;
      
      // категории, передаем массив
      SetCategories(resp_obj_serv, pparty.Categories);
      
      // сотрудники, передаем массив
      //SetEmployees(resp_obj_serv, pparty.Employees);
      
      ErrorAction = NULL;
   else 
      ErrorAction = TArray;
      vSize = ErrorAction.size;
      ErrorAction(vSize) = c_Error;
      ErrorAction(vSize).ErrorCode = resp_resultCode;
      ErrorAction(vSize).ErrorDesc = resp_resultText;
   end;

   return ErrorAction;
      
onError(err)
   /* Получаем объектный вид ответа */
   ErrorAction = TArray;
   vSize = ErrorAction.size;
   ErrorAction(vSize) = c_Error;
   ErrorAction(vSize).ErrorCode = c_err_obj.obtain_err_code(err);
   ErrorAction(vSize).ErrorDesc = c_err_obj.obtain_err_msg(err);   
   
   return ErrorAction;
end;


/**
@brief Обработка субъекта
@param[in] IncomeData входящий объект XMLRPC
@param[out] p_log_id сквозной log_id
@param[in] p_transparent_id сквозной id XML-запроса
@param[out] out_WillMakeCIFCode признак необходимости создания ссылки в CIF (не используется)
@param[out] out_MasterExternalId класс для создания ссылки в CIF (не используется)
@return объект класса c_ClientCreateLE_outObj
*/
macro adp_ClientCreateLE(IncomeData, p_log_id, p_transparent_id, out_WillMakeCIFCode:@bool, out_MasterExternalId:@c_IntegrationSymbolicIdentifierXType)
   var req_data = NULL;
   var resp_data_obj = NULL;
   
   var err_txt="", out_code = 0;   
   var ErrorAction_arr = TArray;
   
   var ClientId = NULL;
   var CodeValue = NULL;
   
   if(ValType(IncomeData) == V_UNDEF)
      RunError(ERR_TEXT_NO_IN_MSG, c_err_obj(ERR_TEXT_NO_IN_MSG,
                                             ERR_CODE_NO_IN_PRM)); 
   end;   
       
   req_data = getValueFromProperty(IncomeData, "ClientCreateLERequest", "ClientCreateLERequestMsg", true);
   
   // единичная обработка
   ErrorAction_arr = InsertClient_single(req_data, ClientId, CodeValue, p_transparent_id, @out_WillMakeCIFCode, @out_MasterExternalId); /* 20190103 - kva - протаскиваем id для логирования */
   
   // итоговый класс 
   resp_data_obj = c_ClientCreateLE_outObj;
   if ( (ErrorAction_arr) and (ErrorAction_arr.size > 0) )
      if (ErrorAction_arr(0).ErrorCode != 0)
         resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.ResultCode = 1;
      else
         resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.ResultCode = 0;
      end;
   else
      resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.ResultCode = 0;
   end;   
   if (ValType(ClientId) != V_UNDEF )
      resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.MasterRecordId = c_IntegrationSymbolicIdentifierXType;
      resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.MasterRecordId.Objectid = ClientId;
      resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.MasterRecordId.SystemId = "SOFR";
      resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.MasterRecordId.SystemNodeId = "0000";
   end;
   if (ValType(CodeValue) == V_UNDEF)
      if (ValType(req_data.LegalEntity.OperationalRecordId) != V_UNDEF )
         CodeValue = req_data.LegalEntity.OperationalRecordId.Objectid;
      end;
   end;
   if (ValType(CodeValue) != V_UNDEF )
      resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.OperationalRecordId = c_IntegrationSymbolicIdentifierXType;
      resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.OperationalRecordId.Objectid = CodeValue;
      resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.OperationalRecordId.SystemId = req_data.LegalEntity.OperationalRecordId.SystemId;
      resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.OperationalRecordId.SystemNodeId = req_data.LegalEntity.OperationalRecordId.SystemNodeId;
   end;
   
   resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.ErrorList = ErrorAction_arr;

   return resp_data_obj;
   
onError(err)
   out_code = c_err_obj.obtain_err_code(err);
   err_txt = c_err_obj.obtain_err_msg(err);

   resp_data_obj = c_ClientCreateLE_outObj;
   resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.ResultCode = 1;
   if (ValType(ClientId) != V_UNDEF )
      resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.MasterRecordId = c_IntegrationSymbolicIdentifierXType;
      resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.MasterRecordId.Objectid = ClientId;
      resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.MasterRecordId.SystemId = "SOFR";
      resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.MasterRecordId.SystemNodeId = "0000";
   end;
   if (ValType(CodeValue) == V_UNDEF)
      if (ValType(req_data.LegalEntity.OperationalRecordId) != V_UNDEF )
         CodeValue = req_data.LegalEntity.OperationalRecordId.Objectid;
      end;
   end;
   if (ValType(CodeValue) != V_UNDEF )
      resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.OperationalRecordId = c_IntegrationSymbolicIdentifierXType;
      resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.OperationalRecordId.Objectid = CodeValue;
      resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.OperationalRecordId.SystemId = req_data.LegalEntity.OperationalRecordId.SystemId;
      resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.OperationalRecordId.SystemNodeId = req_data.LegalEntity.OperationalRecordId.SystemNodeId;
   end;
   resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.ErrorList = TArray;
   resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.ErrorList(0) = c_Error;
   resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.ErrorList(0).ErrorCode = out_code;
   resp_data_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.ErrorList(0).ErrorDesc = err_txt;

   return resp_data_obj;
end;


/**
@brief Точка входа обработки запроса ClientCreateLERequestMsg
@param[in] IncomeData входящий объект XMLRPC
@param[out] in_log_id сквозной log_id
@param[in] in_transparent_id сквозной id XML-запроса
@param[out] out_WillMakeCIFCode признак необходимости создания ссылки в CIF (не используется)
@param[out] out_MasterExternalId класс для создания ссылки в CIF (не используется)
@return объект класса c_ClientCreateLE_outObj
*/
macro ClientCreateLERequestMsg(IncomeData, in_log_id, in_transparent_id, out_WillMakeCIFCode:@bool, out_MasterExternalId:@c_IntegrationSymbolicIdentifierXType)
   var out_str = "";
 
//SetRSTrace(true);  //19-04-2022 Cherednichenko
   out_str = adp_ClientCreateLE(IncomeData, in_log_id, in_transparent_id, @out_WillMakeCIFCode, @out_MasterExternalId);
//SetRSTrace(false);  //19-04-2022 Cherednichenko
   return out_str;
end;
