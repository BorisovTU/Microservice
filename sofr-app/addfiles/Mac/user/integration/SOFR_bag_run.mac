/**
 @file SOFR_bag_run.mac
 @brief Интеграция с внешними системами. Интерфейс для запуска SOFRbag

 # tag
 - functional_block: Отчетность_Внутренняя
 - code_type: GUI

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |00.00.2021 |Гераськина Т.В.|                                                |Интерфейс для запуска SOFRbag

*/

import RSD, cb_sql;
import "int_usr_globals.mac";
import "usr_key_const.mac";
import RtRegEx;    /* регулярные выражения */
import "SOFR_RSHBbag";
import "global_utils_intgr.mac";
import activex, oralib, likepy, globals;
import "usr_table_int.mac";

//Вынесено для дальнейшего удаления файла
private var TermPath = GetCurDir(true) + "\\Import";
private var FileName;

/*Диалоговое окно*/
private var dlgPanel_Parm = TRecHandler ("SOFRBag", "..\\mac\\USER\\Integration\\Lbr\\Integr.lbr", true);
private var dlgPanel_Parm_Auto = TRecHandler ("Bagauto", "..\\mac\\USER\\Integration\\Lbr\\Integr.lbr", true);


private var Branch        = 1;
private var RepDate       = Date();

private var Global_InputTemplate   = "^(\\d|\\*|;)+$";
private var Global_InputTemplate_str   = "Цифры, * - любой символ, ; - разделитель";
private var Global_InputExample    = "474278* или 505* или 523*;526*";

private const V_SPECVAL = 26;

/*Переменные, для построения скролингов в диалоговых окнах*/
private var str_AddScroll_1, rs_AddScroll_1;
private var str_AddScroll_2, rs_AddScroll_2;
private var Columns_Include = TArray(), NumColumns_Include = 0;
private var Columns_Exclude = TArray(), NumColumns_Exclude = 0;
private var str_AddScroll_1_auto, rs_AddScroll_1_auto;
private var str_AddScroll_2_auto, rs_AddScroll_2_auto;
private var Columns_Include_auto = TArray(), NumColumns_Include_auto = 0;
private var Columns_Exclude_auto = TArray(), NumColumns_Exclude_auto = 0;

/**
@brief Класс для работы с таблицей uTableProcessEvent_dbt

*/
private class (c_usr_tbl_base) c_usr_tbl_uTableProcessEvent()
   private class gen_uTableProcessEvent()
      var T_RECID :integer,
          T_TIMESTAMP :date,
          T_OBJECTTYPE :integer,
          T_OBJECTID :integer,
          T_TYPE :integer,
          T_STATUS :integer,
          T_NOTE :string,
          T_MESSAGEID :integer,
          T_RESULTCODE :string,
          T_RESULTTEXT :string;
   end;

   private macro init_table_obj()
      new_val_obj = gen_uTableProcessEvent();
      where_val_obj = gen_uTableProcessEvent();
   end;

   Initc_usr_tbl_base("uTableProcessEvent_dbt");

   init_table_obj();
end;

/**
@brief Проверить значение через регулярные выражения
@param[in] mask Маска
@param[in] value_ Проверяемое значение
@param[in] show_err Признак показа ошибок

@return Если значение удовлетворяет маске True
*/
macro Check_value_by_reg_expression (mask, value_, show_err)
   private var maskObj;
   private var ret_val;
   ret_val=false;
   if ((valtype (mask)!=V_UNDEF) and (valtype (value_)!=V_UNDEF))
      maskObj = TRegEx(string (mask)); 
      if (maskObj)
         ret_val = maskObj.match (string (value_));
      else
         if (show_err)
            println ("Ошибка выполнения функции Check_value_by_reg_expression\n Маска=", mask, "\n Значение=",value_);
            msgbox  ("Ошибка выполнения функции Check_value_by_reg_expression||Маска=", mask, "||Значение="  ,value_);
         end;
      end;
  end;
  return ret_val;
end;

/**
@brief Получить и проверить маску
@param[in] _mask Маска
@return Если маска в порядке, то маска, если нет - пустое значение

Задаваемая маска должна соответствовать формату "474278* или 505* или 523*;526*", только цифры, звездочка как любой символ и точка с запятой как разделитель
*/
macro CheckMask(_mask)
   var ParmValue = _mask;
   if (GetString(ParmValue, "Введите маску в соответствии с форматом: " + Global_InputExample, 20))
      if (not Check_value_by_reg_expression (Global_InputTemplate, ParmValue, 1))
         msgbox ("Значение маски должно соответствовать формату: |" + Global_InputTemplate_str + "|Пример: " + Global_InputExample);
         return "";
      else
         return ParmValue;
      end;
   end;
   return "";
end;

/**
@brief  Показать список объектов для выгрузки в скроллинге
@param[in] _guid Уникальный идентификатор

Перед формированием отчета пользователю показывается предварительный расчет размеров выгрузки по маскам (из-за ограничения на размер XML в ИП-онлайн)
*/
macro ShowSelection(_guid)
   private var gv_column_list_scr = TArray(); /* Список колонок скроллинга */
   private var gv_num_columns_scr = 0;        /* Количество колонок скроллинга */
   private var gv_scr_focus = 0;              /* Номер позиции скроллинга */

   var sql_scr, cmd_scr, rs_scr, cnt;
   sql_scr = "select mask_include, count_records, count_records*700*1.3 size_about from SOFRBag_launch_log where RequestID = :vguid order by mask_include";

   m_add_column(gv_column_list_scr, gv_num_columns_scr, "mask_include",    "Маска",                  15, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "count_records",   "Количество счетов",      17, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "size_about",      "Приблизительный размер", 20, 2, 0); gv_num_columns_scr = gv_num_columns_scr + 1;

   cmd_scr = RSDCommand(sql_scr);
   cmd_scr.Addparam("vguid", RSDBP_IN, _guid);
   cmd_scr.NullConversion = true;
   
   rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   rs_scr.NullConversion = true;
   rs_scr.pagesize = 100;

   while(RunScroll(rs_scr, gv_num_columns_scr, gv_column_list_scr, "SelectionRecords", "", "Список объектов для выгрузки", "Внимание! Цифры расчета предварительные ", false, 5))
      rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   end;
   rs_scr.close; cmd_scr.close; rs_scr = null; cmd_scr = null;

end;

/**
@brief  Удалить список объектов к выгрузке
@param[in] _guid Уникальный идентификатор

*/
macro DeleteSelection(_guid)
   var sql_scr, cmd_scr, rs_scr, cnt;
   sql_scr = "delete from SOFRBag_launch_log where RequestID = :vguid";
   cmd_scr = RSDCommand(sql_scr);
   cmd_scr.Addparam("vguid", RSDBP_IN, _guid);
   cmd_scr.execute;
   
   cmd_scr.close; cmd_scr = null;
end;

/**
@brief  Запуск отчета
@param[in] _repdate Дата отчета
@param[in] _branch Подразделение
@param[in] _FlagAuto Признак автоматической выгрузки (по планировщику)

*/
macro RunReport(_repdate, _branch, _FlagAuto)
   var vguid = CreateGUID();
   var vguid_single;
   var maskexclude = "", maskexclude_str = "";
   var ProcessEventTbl;
   
   var sql_str, cmd, rs;

   var count = 0;
   var fltr_add = "";
   
   var long_text;
   var cnt_prepare = 0;
   var txt;
   
   var resp_obj;
   var Recid;
   var flag_run;
   
   private var gv_column_list_scr = TArray(); 
   private var gv_num_columns_scr = 0;
   
   maskexclude = GetExcludeFilter(_FlagAuto, maskexclude_str);
   SetIncludeFilter(maskexclude, maskexclude_str, _repdate, _branch, vguid, _FlagAuto); // вставка сразу в таблицу
   
   sql_str = "select count(mask_include) cnt from SOFRBag_launch_log where RequestID = :vguid";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("vguid", RSDBP_IN, vguid);
   rs = RSDRecordSet(cmd);
   cnt_prepare = 0;
   if (rs.movenext)
      cnt_prepare = rs.value("cnt");
   end;
   rs.close; rs = null; cmd.close; cmd = null;

   count = 0;
   if (cnt_prepare == 0)
      msgbox("Нет данных");
   else
      ShowSelection(vguid);
      if (msgboxex("Запустить?", MB_YES+MB_NO, IND_NO) == IND_YES)
         flag_run = true;
         // теперь запуск через таблицу событий
         sql_str = "select count(*) cnt from utableprocessevent_dbt where t_objecttype = 5020 and trunc(t_timestamp) = trunc(sysdate) and t_status = 1";
         cmd = RSDCommand(sql_str);
         cmd.AddParam("vguid", RSDBP_IN, vguid);
         rs = RSDRecordSet(cmd);
         if (rs.movenext)
            if (rs.value(0) > 0)
               if (msgboxex("Есть назвершенное событие за текущую дату по выгрузке отчета. |Запустить еще раз?", MB_YES+MB_NO, IND_NO) == IND_YES)
                  flag_run = true;
               else
                  flag_run = false;
               end;
             end;
         end;

         if (flag_run)
            //установка статуса в uTableProcessEvent_dbt
            ProcessEventTbl = c_usr_tbl_uTableProcessEvent();
            ProcessEventTbl.New_Val_Obj.T_OBJECTTYPE = 5020;
            ProcessEventTbl.New_Val_Obj.T_OBJECTID = 1;
            ProcessEventTbl.New_Val_Obj.T_TYPE = 2;
            ProcessEventTbl.New_Val_Obj.T_STATUS = 1;
            ProcessEventTbl.New_Val_Obj.T_TIMESTAMP = dttm(date(), time());

            ProcessEventTbl.add_returning_prm("T_RECID");
            if( not ProcessEventTbl.Insert() ) //инструмент простых действий с таблицами из RSL
               var state = 1;
            else
               Recid = ProcessEventTbl.New_Val_Obj.T_RECID;
               sql_str = "update SOFRBag_launch_log set recid = :recid where RequestID = :vguid";
               cmd = RSDCommand(sql_str);
               cmd.AddParam("recid", RSDBP_IN, RecID);
               cmd.AddParam("vguid", RSDBP_IN, vguid);
               cmd.execute;
               cmd.close; cmd = null;
               msgbox("Отчет добавлен в очередь выгрузок. |Состояние выгрузки проверяйте через журнал");
            end;
            ProcessEventTbl = Null;
            //установка статуса в uTableProcessEvent_dbt
         end;

/*         InitProgress (cnt_prepare);
         sql_str = "select l.*, rowid from SOFRBag_launch_log l where RequestID = :vguid";
         cmd = RSDCommand(sql_str);
         cmd.AddParam("vguid", RSDBP_IN, vguid);
         rs = RSDRecordSet(cmd);
         while (rs.movenext)
            count = count+1;
            vguid_single = CreateGUID();
            resp_obj = run_SOFRBag(_repDate, _branch, vguid_single, SQL_ConvTypeStr(rs.value("mask_include_pure")), SQL_ConvTypeStr(rs.value("mask_include")), SQL_ConvTypeStr(rs.value("mask_exclude_pure")));
            if (ValType(resp_obj) == V_GENOBJ)
               UpdateStatusLog(resp_obj, vguid_single, rs.value("rowid"));
            end;
            UseProgress (count);
         end;
         RemProgress; 
         rs.close; rs = null; cmd.close; cmd = null;*/
      else
         DeleteSelection(vguid);
      end;  
      
/*      sql_str = "select to_char(processtime,'dd.mm.yyyy hh24:mi:ss') processtime, mask_include, "+
                "       count_records, count_real_records,length_xml, send_resultcode, send_err, RequestID_single from SOFRBag_launch_log l where RequestID = :vguid";
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "mask_include",    "Маска"              ,17, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "count_records",   "Кол-во"             ,10, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "count_real_records","Выгр.кол-во"      ,10, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "length_xml",      "Длина без конв."    ,10, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "send_resultcode", "Код"                , 3, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "send_err",        "Результат отправки" ,50, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "processtime",     ""                   ,20, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "RequestID_single",""                   ,20, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      
      cmd = RSDCommand(sql_str);
      cmd.AddParam("vguid", RSDBP_IN, vguid);
      cmd.NullConversion = true;
      rs = RSDRecordSet(cmd, RSDVAL_CLIENT, RSDVAl_STATIC);
      rs.NullConversion = true;
      while(RunScroll(rs, gv_num_columns_scr, gv_column_list_scr, "ResultRecords", "", "Результаты выгрузки", "ESC выход", true, 5, NULL, NULL, 40))
         rs = RSDRecordSet(cmd, RSDVAL_CLIENT, RSDVAl_STATIC);
      end;
      rs.close; cmd.close; rs = null; cmd = null;
      */
   end;
   
OnError(err)
  RemProgress;
   
end;

/**
@brief  Обработчик событий при работе со скролингом
@param[in] rs_attr RSDRecordSet
@param[in] cmd команда
@param[in] id id поля скроллинга
@param[in] key код клавиши

Для масок, которые необходимо включить в отчет
*/
macro EvProc_Include (rs_attr, cmd, id, key )
   var CM_FLAG = CM_DEFAULT;
   var ParmValue = "";
   var ParmIsInclude = StrFor(88);
   var ParmIsExlude  = StrFor(0); 

   if  (cmd == DLG_KEY)
      if(key == K_ENTER)
         /*проверяем валидность данных*/
         ParmValue = CheckMask(rs_attr.value ("balance_mask"));
         if (ParmValue)
            rs_attr.value ("balance_mask") = ParmValue;
            rs_attr.Update();
            UpdateScroll(rs_attr, 2);
         end;
         CM_FLAG = CM_IGNORE;
      elif(key == K_F8)
         CM_FLAG = CM_DEFAULT;
      elif(key == K_F9)
         ParmValue = CheckMask("");
         if (ParmValue)
            rs_attr.AddNew ();

            rs_attr.value ("balance_mask")   = ParmValue;
            rs_attr.value ("IsInclude")      = ParmIsInclude;
            rs_attr.value ("IsExclude")      = ParmIsExlude;
            rs_attr.value ("IsAuto")         = StrFor(0);

            rs_attr.Update ();
            GoTOScroll (rs_attr);
            UpdateScroll (rs_attr);
         end;

         CM_FLAG = CM_IGNORE;
      end;
   end;

   return CM_FLAG;
end;

/**
@brief  Обработчик событий при работе со скролингом
@param[in] rs RSDRecordSet
@param[in] cmd команда
@param[in] id id поля скроллинга
@param[in] key код клавиши

Дубль EvProc_Include, только зеркальные параметры
Для масок, которые необходимо исключить из отчета
*/
macro EvProc_Exclude (rs, cmd, id, key )
   var CM_FLAG = CM_DEFAULT;
   var ParmValue = "";
   var ParmIsInclude = StrFor(0);
   var ParmIsExlude  = StrFor(88); 

   if  (cmd == DLG_KEY)
      if(key == K_ENTER)
         /*проверяем валидность данных*/
         ParmValue = CheckMask(rs.value ("balance_mask"));
         if (ParmValue)
            rs.value ("balance_mask") = ParmValue;
            rs.Update();
            UpdateScroll(rs, 2);
         end;
         CM_FLAG = CM_IGNORE;
      elif(key == K_F8)
         CM_FLAG = CM_DEFAULT;
      elif(key == K_F9)
         ParmValue = CheckMask("");
         if (ParmValue)
            rs.AddNew ();

            rs.value ("balance_mask")   = ParmValue;
            rs.value ("IsInclude")      = ParmIsInclude;
            rs.value ("IsExclude")      = ParmIsExlude;
            rs.value ("IsAuto")         = strFor(0);

            rs.Update ();
            GoTOScroll (rs);
            UpdateScroll (rs);
         end;

         CM_FLAG = CM_IGNORE;
      end;
   end;

   return CM_FLAG;
end;


/**
@brief  Обработчик событий при работе со скролингом
@param[in] rs_attr RSDRecordSet
@param[in] cmd команда
@param[in] id id поля скроллинга
@param[in] key код клавиши

Для масок, которые необходимо включить в отчет, который формируется автоматически
*/
macro EvProc_Include_auto (rs_attr, cmd, id, key )
   var CM_FLAG = CM_DEFAULT;
   var ParmValue = "";
   var ParmIsInclude = StrFor(88);
   var ParmIsExlude  = StrFor(0); 

   if  (cmd == DLG_KEY)
      if(key == K_ENTER)
         /*проверяем валидность данных*/
         ParmValue = CheckMask(rs_attr.value ("balance_mask"));
         if (ParmValue)
            rs_attr.value ("balance_mask") = ParmValue;
            rs_attr.Update();
            UpdateScroll(rs_attr, 2);
         end;
         CM_FLAG = CM_IGNORE;
      elif(key == K_F8)
         CM_FLAG = CM_DEFAULT;
      elif(key == K_F9)
         ParmValue = CheckMask("");
         if (ParmValue)
            rs_attr.AddNew ();

            rs_attr.value ("balance_mask")   = ParmValue;
            rs_attr.value ("IsInclude")      = ParmIsInclude;
            rs_attr.value ("IsExclude")      = ParmIsExlude;
            rs_attr.value ("IsAuto")         = "X";

            rs_attr.Update ();
            GoTOScroll (rs_attr);
            UpdateScroll (rs_attr);
         end;

        CM_FLAG = CM_IGNORE;
      end;
   end;

   return CM_FLAG;
end;

/**
@brief  Обработчик событий при работе со скролингом
@param[in] rs RSDRecordSet
@param[in] cmd команда
@param[in] id id поля скроллинга
@param[in] key код клавиши

Дубль EvProc_Include_auto, только зеркальные параметры 
Для масок, которые необходимо исключить из отчета, который формируется автоматически
*/
macro EvProc_Exclude_auto (rs, cmd, id, key )
   var CM_FLAG = CM_DEFAULT;
   var ParmValue = "";
   var ParmIsInclude = StrFor(0);
   var ParmIsExlude  = StrFor(88); 

   if  (cmd == DLG_KEY)
      if(key == K_ENTER)
         /*проверяем валидность данных*/
         ParmValue = CheckMask(rs.value ("balance_mask"));
         if (ParmValue)
            rs.value ("balance_mask") = ParmValue;
            rs.Update();
            UpdateScroll(rs, 2);
         end;
         CM_FLAG = CM_IGNORE;
      elif(key == K_F8)
         CM_FLAG = CM_DEFAULT;
      elif(key == K_F9)
         ParmValue = CheckMask("");
         if (ParmValue)
            rs.AddNew ();

            rs.value ("balance_mask")   = ParmValue;
            rs.value ("IsInclude")      = ParmIsInclude;
            rs.value ("IsExclude")      = ParmIsExlude;
            rs.value ("IsAuto")         = "X";

            rs.Update ();
            GoTOScroll (rs);
            UpdateScroll (rs);
         end;

        CM_FLAG = CM_IGNORE;
      end;
   end;

   return CM_FLAG;
end;

/**
@brief  Очистить список масок
@param[in] _isinclude Признак включения
@param[in] _isexclude Признак исключения
@param[in] _isauto Признак формирования автоматически

*/
macro ClearMask(_isinclude, _isexclude, _isauto)
   var sql_str, cmd;
   var str_in, str_ex, str_auto;
   var msg_txt;

   if (_isinclude)
      str_in = "isinclude = 'X'";
      msg_txt = "включать";
   else
      str_in = "isinclude = chr(0)";
   end;
   if (_isexclude)
      str_ex = "isexclude = 'X'";
      msg_txt = "исключать";
   else
      str_ex = "isexclude = chr(0)";
   end;

   str_auto = "isauto = chr("+_isauto+")";


   if (GetTrue(false, "Очистить данные <"+msg_txt+"> ?"))
      sql_str = "delete from SOFRBag_param where "+str_in+" and "+str_ex+" and "+str_auto;
      cmd = RSDCommand(sql_str);
      cmd.execute;
      cmd.close;
   end;
onerror(e)
   RunError(e.message + getFullCmdErrorText(cmd));   
end;

/**
@brief Выбрать и открыть файл Excel для загрузки списка масок

*/
private macro SelectAndOpenFile ()
   var FullNameFile, ExtName, DirName;

   /*Выбираем файл для обработки*/
   if(not SelectFile(FullNameFile, "$\\..\\..\\import\\*.xls*", "Выберите файл для загрузки", 0, true))
      MsgBox("Отказ от загрузки");
      return null;
   end;

   //Отделяем путь файла от имени
   splitfile(FullNameFile, FileName, ExtName);
   FileName = FileName + ExtName;
   DirName = substr(FullNameFile, 1, index(FullNameFile, FileName) - 1);

   TermPath = DirName;

   BegAction(1, "Обработка файла \\" + TermPath + "\\" + FileName + "\". Ждите...");

   if(not OpenExcelFile(FileName, false, true, TermPath))
      MsgBox("Ошибка открытия файла \"" + TermPath + "\\" + FileName + "\"");
      EndAction(1);
      return null;
   end;

   return ExcelApplication.Sheets(1);
End;

/**
@brief Загрузить список масок из файла в параметры 

*/
macro LoadFile(_isinclude, _isexclude, _isauto)
   var sheet;
   var col = 1;
   var row = 1;
   var sql_str, cmd;
   var temp_mask = "";
   var str_in, str_ex, str_auto;

   if (_isinclude)
      str_in = "chr(88)";
   else
      str_in = "chr(0)";
   end;
   if (_isexclude)
      str_ex = "chr(88)";
   else
      str_ex = "chr(0)";
   end;
  
   str_auto = "chr("+_isauto+")";

   sheet = SelectAndOpenFile();
   if (valtype(sheet) == V_UNDEF) 
      return;
   end;
   
   sql_str = " insert into SOFRBag_param (balance_mask, isinclude, isexclude, isauto) "+
             " values (:balance_mask, "+str_in+", "+str_ex+", "+str_auto+")";

   rslDefCon.beginTrans();
   while (valtype(sheet.cells(row, col).value) != V_UNDEF)
      temp_mask = sheet.cells(row, col).value;
      cmd = RSDCommand(sql_str);
      cmd.AddParam("balance_mask", RSDBP_IN, temp_mask);
      cmd.execute();
      cmd.close;
      temp_mask = "";

      row = row + 1;
   end;

   rslDefCon.commitTrans();
   ExcelApplication.ActiveWorkbook.Close;

/*   if (not RemoveFile ("$" + TermPath + "\\" + FileName))
     MsgBox("Ошибка удаления файла " + TermPath + "\\" + FileName);
   end; */
   EndAction(1);

onerror()
   if (valtype(ExcelApplication) != V_UNDEF)
     ExcelApplication.ActiveWorkbook.Close;
   end;
   if ( rslDefCon.isInTrans() )
     rslDefCon.rollbackTrans();
   end;
   EndAction(1);
End;


/**
@brief  Обработка нажатия клавиш в диалоговом окне
@param[in] dlgPanel Диалоговая панель
@param[in] cmd команда
@param[in] id id поля панели
@param[in] key код клавиши

Панель масок для автозагрузки
*/
macro MsgProc_Parm_Auto(dlgPanel, cmd, id, key)
   var CM_FLAG = CM_DEFAULT;  /*возращаемое значение по умолчанию*/

   if  (cmd == DLG_PREINIT)

      // "включать"
      str_AddScroll_1_auto  =                        "  SELECT balance_mask, IsInclude, IsExclude, IsAuto, rowid ";
      str_AddScroll_1_auto  = str_AddScroll_1_auto + "    FROM SOFRBag_param ";
      str_AddScroll_1_auto  = str_AddScroll_1_auto + "   WHERE IsInclude = chr(88)";         /* включать в отчет */ 
      str_AddScroll_1_auto  = str_AddScroll_1_auto + "     AND IsAuto = chr(88)";    
      str_AddScroll_1_auto  = str_AddScroll_1_auto + "ORDER BY balance_mask";
      
      Columns_Include_auto.size = 0;
      NumColumns_Include_auto = 0;
      m_add_column(Columns_Include_auto, NumColumns_Include_auto, "balance_mask", "Включать по маске", 20, 2); NumColumns_Include_auto = NumColumns_Include_auto + 1;

      rs_AddScroll_1_auto = RsdRecordSet(str_AddScroll_1_auto, RSDVAL_CLIENT, RSDVAL_STATIC );
      AddScroll (dlgPanel, rs_AddScroll_1_auto, NumColumns_Include_auto, Columns_Include_auto, "EvProc_Include_auto", False, False, 0);

      // "исключать"
      str_AddScroll_2_auto  =                   "  SELECT balance_mask, IsInclude, IsExclude, IsAuto, rowid ";
      str_AddScroll_2_auto  = str_AddScroll_2_auto + "    FROM SOFRBag_param ";
      str_AddScroll_2_auto  = str_AddScroll_2_auto + "   WHERE IsExclude = chr(88)";         /* исключать из отчета */
      str_AddScroll_2_auto  = str_AddScroll_2_auto + "     AND IsAuto = chr(88)";    
      str_AddScroll_2_auto  = str_AddScroll_2_auto + "ORDER BY balance_mask";

      Columns_Exclude_auto.size = 0;
      NumColumns_Exclude_auto = 0;
      m_add_column(Columns_Exclude_auto, NumColumns_Exclude_auto, "balance_mask", "Исключать по маске", 20, 2); NumColumns_Exclude_auto = NumColumns_Exclude_auto + 1;

      rs_AddScroll_2_auto = RsdRecordSet(str_AddScroll_2_auto, RSDVAL_CLIENT, RSDVAL_STATIC );
      AddScroll (dlgPanel, rs_AddScroll_2_auto, NumColumns_Exclude_auto, Columns_Exclude_auto, "EvProc_Exclude_auto", False, False, 1);
      message("Esc Выход  F8 Удалить  F9 Добавить  Enter Корректировка");

   elif(cmd == DLG_INIT)

   elif(cmd == DLG_BUTTON)   /*Обрабатываем, нажатие кнопок в диалоге*/
      if  (fldname(dlgPanel, id) == "Button_Clear_In")
         ClearMask(true, false, 88);
         CM_FLAG = CM_UPDATE_ADDSCROLL;
      elif (fldname(dlgPanel, id) == "Button_Clear_Ex")
         ClearMask(false, true, 88);
         CM_FLAG = CM_UPDATE_ADDSCROLL;
      elif (fldname(dlgPanel, id) == "Button_Load_In")
         LoadFile(true, false, 88);
         CM_FLAG = CM_UPDATE_ADDSCROLL;
      elif (fldname(dlgPanel, id) == "Button_Load_Ex")
         LoadFile(false, true, 88);
         CM_FLAG = CM_UPDATE_ADDSCROLL;
      end;
   elif(cmd == DLG_REMFOCUS)
   elif(cmd == DLG_KEY)

      if (key == K_ESCAPE)
         CM_FLAG = CM_CANCEL;
      elif(key == K_SPACE)
         CM_FLAG = CM_IGNORE;
      elif(key == K_ENTER)
         CM_FLAG = CM_IGNORE;
      elif(key == K_F3)
         CM_FLAG = CM_IGNORE;
      elif(key == K_F8)
         CM_FLAG = CM_IGNORE;
      elif(key == K_F9)
         CM_FLAG = CM_IGNORE;
      end;

   end;

  return CM_FLAG;
end;

/**
@brief  Обработка нажатия клавиш в диалоговом окне
@param[in] dlgPanel Диалоговая панель
@param[in] cmd команда
@param[in] id id поля панели
@param[in] key код клавиши

Панель масок для ручной загрузки
*/
macro MsgProc_Parm (dlgPanel, cmd, id, key)
   var CM_FLAG = CM_DEFAULT;  /*возращаемое значение по умолчанию*/

   if  (cmd == DLG_PREINIT)

      // "включать"
      str_AddScroll_1  =                   "  SELECT balance_mask, IsInclude, IsExclude, IsAuto, rowid ";
      str_AddScroll_1  = str_AddScroll_1 + "    FROM SOFRBag_param ";
      str_AddScroll_1  = str_AddScroll_1 + "   WHERE IsInclude = chr(88)";         /* включать в отчет */ 
      str_AddScroll_1  = str_AddScroll_1 + "     AND IsAuto = chr(0)";    
      str_AddScroll_1  = str_AddScroll_1 + "ORDER BY balance_mask";
      
      Columns_Include.size = 0;
      NumColumns_Include = 0;
      m_add_column(Columns_Include, NumColumns_Include, "balance_mask", "Включать по маске", 20, 2); NumColumns_Include = NumColumns_Include + 1;

      rs_AddScroll_1 = RsdRecordSet(str_AddScroll_1, RSDVAL_CLIENT, RSDVAL_STATIC );
      AddScroll (dlgPanel, rs_AddScroll_1, NumColumns_Include, Columns_Include, "EvProc_Include", False, False, 0);

      // "исключать"
      str_AddScroll_2  =                   "  SELECT balance_mask, IsInclude, IsExclude, IsAuto, rowid ";
      str_AddScroll_2  = str_AddScroll_2 + "    FROM SOFRBag_param ";
      str_AddScroll_2  = str_AddScroll_2 + "   WHERE IsExclude = chr(88)";         /* исключать из отчета */
      str_AddScroll_2  = str_AddScroll_2 + "     AND IsAuto = chr(0)";    
      str_AddScroll_2  = str_AddScroll_2 + "ORDER BY balance_mask";

      Columns_Exclude.size = 0;
      NumColumns_Exclude = 0;
      m_add_column(Columns_Exclude, NumColumns_Exclude, "balance_mask", "Исключать по маске", 20, 2); NumColumns_Exclude = NumColumns_Exclude + 1;

      rs_AddScroll_2 = RsdRecordSet(str_AddScroll_2, RSDVAL_CLIENT, RSDVAL_STATIC );
      AddScroll (dlgPanel, rs_AddScroll_2, NumColumns_Exclude, Columns_Exclude, "EvProc_Exclude", False, False, 1);

   elif(cmd == DLG_INIT)

      DisableFields ( dlgPanel, fldIndex(dlgPanel, "Branch") );

      dlgPanel.rec.RepDate        = RepDate;
      dlgPanel.rec.Branch          = Branch;
      UpdateFields (dlgPanel);

   elif(cmd == DLG_BUTTON)   /*Обрабатываем, нажатие кнопок в диалоге*/
      if  (fldname(dlgPanel, id) == "Button_Run")
         RunReport(dlgPanel.rec.RepDate, dlgPanel.rec.Branch, 0);
      elif (fldname(dlgPanel, id) == "Button_Clear_In")
         ClearMask(true, false, 0);
         CM_FLAG = CM_UPDATE_ADDSCROLL;
      elif (fldname(dlgPanel, id) == "Button_Clear_Ex")
         ClearMask(false, true, 0);
         CM_FLAG = CM_UPDATE_ADDSCROLL;
      elif (fldname(dlgPanel, id) == "Button_Load_In")
         LoadFile(true, false, 0);
         CM_FLAG = CM_UPDATE_ADDSCROLL;
      elif (fldname(dlgPanel, id) == "Button_Load_Ex")
         LoadFile(false, true, 0);
         CM_FLAG = CM_UPDATE_ADDSCROLL;
      elif (fldname(dlgPanel, id) == "Button_Auto")
         RunDialog (dlgPanel_Parm_Auto, @MsgProc_Parm_Auto);
         CM_FLAG = CM_IGNORE;
      elif  (fldname(dlgPanel, id) == "Button_launch")
         ExecMacroFile("int_bag_launch");
      end;
   elif(cmd == DLG_REMFOCUS)
   elif(cmd == DLG_KEY)

      if (key == K_ESCAPE)
         CM_FLAG = CM_CANCEL;
      elif(key == K_SPACE)
         CM_FLAG = CM_IGNORE;
      elif(key == K_ENTER)
         CM_FLAG = CM_IGNORE;
      elif(key == K_F3)
         CM_FLAG = CM_IGNORE;
      elif(key == K_F8)
         CM_FLAG = CM_IGNORE;
      elif(key == K_F9)
         CM_FLAG = CM_IGNORE;
      end;

   end;

  return CM_FLAG;
end;

/*Инициализация переменных, для диалоговой панели*/
Branch        = 1;
RepDate       = Date();

/*Диалоговое окно*/
if (RunDialog (dlgPanel_Parm, @MsgProc_Parm))
end;

exit(1);