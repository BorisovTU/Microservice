/*----------------------------------------------------------*/
/* LoadCSAAgreement.mac
   BIQ-5049. Начальное решение по CSA - загрузка из *.csv   */
/*                                                          */
/* Автор: Гераськина Т.                                     */
/*----------------------------------------------------------*/
import rsd, DVInter;
import func_lib;
import LoadCSA_func;
import "usr_open_files.mac";


class Logger()

    macro RepHeader(dt, tm)
       var str;
       str =     "                             ОТЧЕТ о загрузке CSA \n";
       str = str+"\n";
       str = str+"Дата загрузки  : "+dt+"\n";
       str = str+"Время загрузки : "+tm+"\n";
       str = str+"┌─┬──────────────────┬──────────────────────────────┬───────────────┬─────────────────────────────────────────────────────────────────────────────────────────┐\n";
       str = str+"│ │Код               │Контрагент                    │Статус загрузки│Комментарий                                                                              │\n";
       str = str+"├─┼──────────────────┼──────────────────────────────┼───────────────┼─────────────────────────────────────────────────────────────────────────────────────────┤";
       println(str);
    end;

    macro RepFooter(suc, er, tot)
       var str = "└─┴──────────────────┴──────────────────────────────┴───────────────┴─────────────────────────────────────────────────────────────────────────────────────────┘\n";
       str = str+"Загружено успешно: "+suc+"\n";
       str = str+"Не загружено     : "+er+"\n";
       str = str+"Всего            : "+tot+"\n";
      println(str);
    end;

    macro RepStr(_err,_code, _ca, _stat,_comm)
        var status;
        if (_stat == false)
            status = "Загружено";
        else
            status = "Не загружено";
        end;
        var str = "│"+string(_err   :1)   +"│"+
                  string(_code    :18:l)+"│"+
                  string(_ca    :30:l)+"│"+
                  string(status    :15:l)+"│"+
                  string(_comm:89:0   ) +"│";
       return println(str);
    end;
end;


macro runLoadCSAAgreement
   var ErrStr = "",ErrStat = false;
   var row = 0;
   var all = 0,suc = 0, err = 0;
   var log = Logger();
   var dt = StrSubSt(string(Date()),".","");
   var InfoLogFileName = GetTxtFileName( "load_csa_"+dt);

   var cur_file_mask = "*.csv";
   file datafile() txt;
   SetDelim(";");
   
   var CSAFromFile;
   
   var state;

   var c_file = c_txt_file();
   var FullFileName;
   var stat;

   stat = c_file.openFile(datafile, cur_file_mask, true, false, FullFileName, "rsansi");

   if ( (valtype(stat) == V_UNDEF) or (stat == false) )
      msgbox("Ошибка открытия файла");
      return 0;
   end;

   /*Открываем файл*/
   BegAction(1, "Обработка файла \"" + c_file.localFileName + "\". Ждите...");

   /* формат файла, первая строка - всегда заголовок, данные со второй строки
   Agreement_Id;Agreement_Code;Agreement_Name;Agreement_Parent;Agreement_Name_Parent;Cpty;ValidFrom;ValidTo;Currency;MarginAllowed;Status;MarginTerms;MarginPayer;MaturityClass_RP;MinThAmount;MinPayAmount;MaturityClass_VD
   
1	(Agreement_Id) Уникальный идентификатор соглашения CSA в АСУДР	число целое	 
2	(Agreement_Code) Код соглашения CSA	строка	 
3	(Agreement_Name) Наименование соглашения	строка	 
4	(Agreement_Parent) Код основного генерального соглашения	строка	 
5	(Agreement_Name_Parent) Наименование основного генерального соглашения	строка	 
6	(Cpty) Код контрагента в нотации АСУДР	строка	 
7	(ValidFrom) Дата заключения соглашения CSA	дата	формат ГГГГ-ММ-ДД
8	(ValidTo) Дата окончания соглашения CSA	дата	формат ГГГГ-ММ-ДД
9	(Currency) Базовая валюта соглашения CSA 	строка	трёхзначный латинский буквенный код
10	(MarginAllowed) Признак маржинального договора	строка	может принимать одно из значений: Y/N
11	(Status) Статус соглашения	строка	может принимать одно из значений: Действует; Завершен; Расторгнут
12	(MarginTerms) Вид маржирования	строка	может принимать одно из значений: Одностороннее/Двустороннее
13	(MarginPayer) Плательщик маржи	строка	может принимать одно из значений: Оба/Контрагент/Мы
14	(MaturityClass_RP) Периодичность маржирования	строка	 
15	(MinThAmount) Маржевая пороговая сумма	число дробное	 
16	(MinPayAmount) Минимальная сумма платежа при превышении маржевой пороговой суммы 	число дробное	 
17	(MaturityClass_VD) Фактическое количество рабочих дней между перечислением маржи и переоценкой обеспечения	строка	 
     */

   next(datafile);

   if (  (StrUpr(datafile(0)) == StrUpr("Agreement_Id")) and
         (StrUpr(datafile(1)) == StrUpr("Agreement_Code")) and
         (StrUpr(datafile(2)) == StrUpr("Agreement_Name")) )
   else 
      MsgBox("Файл имеет некорректный заголовок");
      EndAction(1);
      return 0;
   end;
   SetOutput( InfoLogFileName, true ); //Пишем лог в файл. Начало
   log.RepHeader(date(),time());

   //Бежим по файлу
   InitProgress(-1);
   while (next(datafile))
      if (StrLen(trim(datafile.str)) == 0)
         continue;
      end;
      
      CSAFromFile = c_CSAFromFile;
      CSAFromFile.fAgreement_Id = datafile(0);
      CSAFromFile.fAgreement_Code = datafile(1);
      CSAFromFile.fAgreement_Name = datafile(2);
      CSAFromFile.fAgreement_Parent = datafile(3);
      CSAFromFile.fAgreement_Name_Parent = datafile(4);
      CSAFromFile.fCpty = datafile(5);
      CSAFromFile.fValidFrom = datafile(6);
      CSAFromFile.fValidTo = datafile(7);
      CSAFromFile.fCurrency = datafile(8);
      CSAFromFile.fMarginAllowed = datafile(9);
      CSAFromFile.fStatus = datafile(10);
      CSAFromFile.fMarginTerms = datafile(11);
      CSAFromFile.fMarginPayer = datafile(12);
      CSAFromFile.fMaturityClass_RP = datafile(13);
      CSAFromFile.fMinThAmount = datafile(14);
      CSAFromFile.fMinPayAmount = datafile(15);
      CSAFromFile.fMaturityClass_VD = datafile(16);
      
      CSAFromFile.TransformFields;

      CSAFromFile.vPartyid = GetPartyID_byPartyCode(103, CSAFromFile.Cpty);
      if (CSAFromFile.vPartyid == -1)
         ErrStat = true;
         ErrStr = "В СОФР отсутствует контрагент с кодом 103 = "+CSAFromFile.Cpty;
         log.RepStr("X",CSAFromFile.Agreement_Code,CSAFromFile.Cpty, ErrStat, ErrStr);
         row = row + 1;
         err = err + 1;
         continue;
      end;
      
      CSAFromFile.vGenAgr = GetGenAgr(CSAFromFile.Agreement_Parent);
      if (CSAFromFile.vGenAgr.AgrId == -1)
/*         ErrStat = true;
         ErrStr = "В СОФР отсутствует ген.соглашение с кодом = "+CSAFromFile.Agreement_Parent;
         log.RepStr("X",CSAFromFile.Agreement_Code,CSAFromFile.Cpty, ErrStat, ErrStr);
         row = row + 1;
         err = err + 1;
         continue;        */
        CSAFromFile.vGenAgr.AgrId = 0;
      end;
      
      CSAFromFile.vCur = GetFiidCodeByName(CSAFromFile.Currency);
      
      CSAFromFile.FindCSA();
      
      if (CSAFromFile.vCSAid > 0) // существующее
         if (not CSAFromFile.vstate)
            ErrStat = true;
            ErrStr = CSAFromFile.vErrText;
            log.RepStr("X",CSAFromFile.Agreement_Code,CSAFromFile.Cpty, ErrStat, ErrStr);
            row = row + 1;
            err = err + 1;
            continue;
         end;
         CSAFromFile.UpdateCSA();
         if (not CSAFromFile.vstate)
            ErrStat = true;
            ErrStr = CSAFromFile.vErrText;
            log.RepStr("X",CSAFromFile.Agreement_Code,CSAFromFile.Cpty, ErrStat, ErrStr);
            row = row + 1;
            err = err + 1;
            continue;
         else
            CSAFromFile.UpdateCSAAdd();
            if (not CSAFromFile.vstate)
               ErrStat = true;
               ErrStr = CSAFromFile.vErrText;
               log.RepStr("X",CSAFromFile.Agreement_Code,CSAFromFile.Cpty, ErrStat, ErrStr);
               row = row + 1;
               err = err + 1;
               continue;
            else
               ErrStat = false;
               ErrStr = "";
               log.RepStr("",CSAFromFile.Agreement_Code,CSAFromFile.Cpty, ErrStat, ErrStr);
               suc = suc + 1;
            end;
         end;
      else // новое
         ErrStat = true;
         ErrStr = "Не найдено CSA";
         log.RepStr("X",CSAFromFile.Agreement_Code,CSAFromFile.Cpty, ErrStat, ErrStr);
         row = row + 1;
         err = err + 1;
         continue;
      end;
       
      CSAFromFile = NULL;

      row = row+1;
      UseProgress(row);
      all = all + 1;
      ErrStr = "";
      ErrStat = false;
    end;
   RemProgress();

   log.RepFooter(suc,err,all);
   SetOutput( NULL, true ); //Пишем лог в файл. Конец

   c_file.closeFile(datafile);
   ViewFile( InfoLogFileName );

end;

runLoadCSAAgreement();