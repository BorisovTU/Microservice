/** 
 @file GetAmountSNOBReq.mac
 @brief Интеграция с внешними системами. Запрос на получение суммы СНОБ
  
 # tag 
 - functional_block:Налоги_Отчеты
 - code_type:API

   
 # changeLog 
 |date       |author         |tasks            |note                                                         
 |-----------|---------------|-----------------|-------------------------------------------------------------------------------------------------------------
 |15.11.2022 |Гераськина Т.В.|BIQ-7294         | Создание
 |27.11.2023 |Гераськина Т.В.|DEF-56827        | Добавлена обработка тегов EventDescription и RecordDateTime

*/ 

import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import "RequestWS.mac";                   /* Функция передачи запроса в Web Sphere */
import "uws_exchng_log.mac";              /* Функционал логирования */
import func_lib;

/**
 @brief Запрос на получение суммы СНОБ

*/
class c_GetAmountSNOBReq
   var StartTaxPeriodDate  : date;     // Начало налогового периода                                         Нет
   var EndTaxPeriodDate    : date;     // Окончание налогового периода                                      Нет
   var IsSNOB              : bool;     // Получать сумму СНОБ                                               Нет
   var IsActivRecord       : bool;     // Данные по активным записям                                        Да
   var ClientId;                       // ID клиента клиента в системе-источнике                            Да    Идентификатор
   var TaxYear             : integer;  // Налоговый период, к которому относится событие (налоговый год)    Да
   var TaxBaseType;                    // Вид налоговой базы по НДФЛ
end;

/**
 @brief Основной класс работы с сервисом

*/
class c_GetAmountSNOBRequest()
   var GetAmountSNOBReq; /* !!! По названию корневого тега запроса !!! */
   GetAmountSNOBReq = c_GetAmountSNOBReq;
end;

/**
 @brief Информация по записи

*/
class c_RecordionInfo
   var TaxYear                : integer;  // Налоговый период, к которому относится событие (налоговый год)    Да
   var IncomeRegionDateTime   : datetime; // Дата и время получения дохода (региональное время)                Да
   var IncomeDateTime         : datetime; // Дата и время получения дохода (московское время)                  Да
   var NOBAmount              : double;   // Сумма НОБ по текущей выплате                                      Нет
   var NDFLCalculateAmount    : double;   // Сумма исчисленного НДФЛ по выполненной операции                   Нет
   var NDFLTaxRate            : double;  // Налоговая ставка, примененная при исчислении НДФЛ                 Нет
   var NDFLKeepAmount         : double;   // Сумма  НДФЛ удержанного по выполненной операции                   Нет
   var NDFLKeepRate           : double;   // Налоговая ставка удержанного НДФЛ по выплаченному доходу          Нет
   var KBKCalculate           : string;   // Код КБК для исчисленного к удержанию НДФЛ                         Нет
   var KBKKeep                : string;   // Код КБК удержанного НДФЛ                                          Да
   var SNOBPayAmount          : double;   // Сумма СНОБ с учетом НОБ по текущей выплаты                        Нет
   var SNOBAmount             : double;   // Сумма СНОБ без учета НОБ по текущей выплате                       Нет
   var EventDescription       : string;   // Описание события
   var RecordDateTime         : datetime; // Дата и время создания записи в Хранилище СНОБ
   var RecordId;                          // Id записи в хранилище СНОБ                                        Нет   Идентификатор
   var EventType;                         // Тип события                                                       Да    Справочник
   var EventId;                           // ID события                                                        Да    Идентификатор
   var RecordStatus;                      // Статус записи                                                     Да    Справочник
   var OperationId;                       // ID операции                                                       Да    Идентификатор
   var TaxBaseType;                       // Вид налоговой базы по НДФЛ, к которой относится событие           Нет   Справочник
   var TaxPayerStatus;                    // Статус налогоплательщика ФЛ                                       Нет   Справочник
   var SystemCode;                        // Код системы, в которой осуществляется выполнение операции         Да    Справочник
end;

class c_GetAmount
   var AmountSNOB : double;   // Сумма СНОБ                                            Да
   var NOBTypeAmount;         // Сумма НОБ по типу дохода
   var ClientId;              // ID клиента в системе-источнике                        Да    Идентификатор
   var RecordInfoList;        // Список записей, по которым передается информация      Нет   Список
end;

/**
 @brief Ответ на запрос получения суммы СНОБ

*/
class adp_GetAmountSNOBResponse(IncomeData)
   var GetAmount;    // Блок данных    Нет
   var ErrorList;    // Список ошибок  Да

   /* Кратность параметров в соответствии со схемой/спецификацией */
   private macro uInitPrime(IncomeData)
      var err_txt;
      var aux_buff, aux_buff2, ai, aux_buff3, arr_size;
      var temp, temp2;
      var temp_error;
      
      var nametag_up = "GetAmountSNOBResp";
      
      if(ValType(IncomeData) != V_UNDEF)
         aux_buff = getValueFromProperty(IncomeData, "GetAmount", nametag_up);
         if (ValType(aux_buff) == V_GENOBJ)
            GetAmount = c_GetAmount;
            nametag_up = nametag_up+".GetAmount";

            GetAmount.AmountSNOB   = getValueFromProperty(aux_buff, "AmountSNOB" , nametag_up, true);
            GetAmount.NOBTypeAmount = getValueFromProperty(aux_buff, "NOBTypeAmount", nametag_up);
            GetAmount.ClientId     = getSymbolFromProperty(aux_buff, "ClientId"  , nametag_up, true);
            
            aux_buff2 = uTryToGetPropS(aux_buff, "RecordInfoList");
            if(ValType(aux_buff2) == V_GENOBJ)
               GetAmount.RecordInfoList = TArray;
               ai = 0;
               nametag_up = nametag_up+".RecordInfoList";
               var secondaryArr = TArray(aux_buff.RecordInfoList);
               arr_size = secondaryArr.size;
       
               while(arr_size > ai)
                  aux_buff3 = secondaryArr.value(ai);
                  temp = c_RecordionInfo;
                  temp.TaxYear                = getValueFromProperty(aux_buff3,     "TaxYear"               , nametag_up, true);
                  temp.IncomeRegionDateTime   = getValueFromProperty(aux_buff3,     "IncomeRegionDateTime"  , nametag_up, true);
                  temp.IncomeDateTime         = getValueFromProperty(aux_buff3,     "IncomeDateTime"        , nametag_up, true);
                  temp.NOBAmount              = getValueFromProperty(aux_buff3,     "NOBAmount"             , nametag_up);
                  temp.NDFLCalculateAmount    = getValueFromProperty(aux_buff3,     "NDFLCalculateAmount"   , nametag_up);
                  temp.NDFLTaxRate            = getValueFromProperty(aux_buff3,     "NDFLTaxRate"           , nametag_up);
                  temp.NDFLKeepAmount         = getValueFromProperty(aux_buff3,     "NDFLKeepAmount"        , nametag_up);
                  temp.NDFLKeepRate           = getValueFromProperty(aux_buff3,     "NDFLKeepRate"          , nametag_up);
                  temp.KBKCalculate           = getValueFromProperty(aux_buff3,     "KBKCalculate"          , nametag_up);
                  temp.KBKKeep                = getValueFromProperty(aux_buff3,     "KBKKeep"               , nametag_up, true);
                  temp.SNOBPayAmount          = getValueFromProperty(aux_buff3,     "SNOBPayAmount"         , nametag_up);
                  temp.SNOBAmount             = getValueFromProperty(aux_buff3,     "SNOBAmount"            , nametag_up);
                  temp.EventDescription       = getValueFromProperty(aux_buff3,     "EventDescription"      , nametag_up);
                  temp.RecordDateTime         = getValueFromProperty(aux_buff3,     "RecordDateTime"        , nametag_up);
                  temp.RecordId               = getSymbolFromProperty(aux_buff3,    "RecordId"              , nametag_up);
                  temp.EventType              = getDictionaryFromProperty(aux_buff3,"EventType"             , nametag_up, true);
                  temp.EventId                = getSymbolFromProperty(aux_buff3,    "EventId"               , nametag_up, true);
                  temp.RecordStatus           = getDictionaryFromProperty(aux_buff3,"RecordStatus"          , nametag_up, true);
                  temp.OperationId            = getSymbolFromProperty(aux_buff3,    "OperationId"           , nametag_up, true);
                  temp.TaxBaseType            = getDictionaryFromProperty(aux_buff3,"TaxBaseType"           , nametag_up);
                  temp.TaxPayerStatus         = getDictionaryFromProperty(aux_buff3,"TaxPayerStatus"        , nametag_up);
                  temp.SystemCode             = getDictionaryFromProperty(aux_buff3,"SystemCode"            , nametag_up, true);

                  GetAmount.RecordInfoList.value(ai) = temp;
                  ai = ai + 1;
                  temp = NULL;
                  aux_buff3 = NULL;
               end;
               secondaryArr = null;
            elif(ValType(aux_buff2) == V_UNDEF)
            else
               err_txt = string(InErrNotObj, nametag_up+".RecordInfoList");
               RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
            end;
         end;

         nametag_up = "GetAmountSNOBResp";
         aux_buff = uTryToGetPropS(IncomeData, "ErrorList");
         if(ValType(aux_buff) == V_GENOBJ)
            ErrorList = TArray;
            aux_buff = 0;
            nametag_up = nametag_up+".ErrorList";
            while(IncomeData.ErrorList.size > aux_buff)
               temp_Error = c_Error;
               temp_Error.ErrorCode = getValueFromProperty(IncomeData.ErrorList.value(aux_buff), "ErrorCode", nametag_up);
               temp_Error.ErrorDesc = getValueFromProperty(IncomeData.ErrorList.value(aux_buff), "ErrorDesc", nametag_up);
               
               ErrorList.value(aux_buff) = temp_Error;
               aux_buff = aux_buff + 1;
               temp_Error = NULL;
            end;
         elif(ValType(aux_buff) == V_UNDEF)
         else
            err_txt = string(InErrNotObj, nametag_up+".ErrorList");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
         end;
      end;
   end;
   
   uInitPrime(IncomeData);
end;

private macro MakeErrorAnswer(pcode, ptext)
   var res = adp_GetAmountSNOBResponse;
   res.ErrorList = TArray;
   res.ErrorList.value(0) = c_Error;
   res.ErrorList.value(0).ErrorCode = pcode;
   res.ErrorList.value(0).ErrorDesc = ptext;

   return res;
end;


/**
 @brief Адаптер для вызова сервиса "Запрос на получение суммы СНОБ"

 Считаем, что ReqData заполняется вовне
*/
macro run_GetAmountSNOB(ReqData)
   var RespData = NULL;
   var err_txt = NULL;
   
   private var RequestText;
   private var v_transformator;
   private var v_answer;
   private var v_answer_data;
   private var v_logger_obj;
   private var v_log_id;
   private var xml_str;
   private var v_guid = CreateGUID();
   private var v_xml_rpc_reqid = XmlRpcRequestId(); 
   
   private var V_WS_TYPE = GetNumDataType("GetAmountSNOB");

   var v_timeout;
   var stat;
   GetRegistryValue(REG_TIMEOUT_SNOB, V_INTEGER, v_timeout, stat);
   if (stat>0)
      v_timeout = 15;
   end;

   v_transformator = c_secur_msg_converter();

   // заполнение класса информацией 
   RequestText = v_transformator.m_secur_internal_resp(ReqData);
   
   v_logger_obj = uws_exchng_logger(null, v_guid, v_xml_rpc_reqid, "GetAmountSNOBReq", modulename(), null, RequestText);
   v_log_id = v_logger_obj.get_log_id();


   if (err_txt == NULL)
      v_answer = SubmitRequestToWS( C_QUEUE_OUT_ID,  // ИД очереди.
                                    "",              // ИД сообщения.
                                    "",              // Идентификатор Бэк Офиса. (не обрабатывается).
                                    true,            // Признак синхронной обработки. Если не указан, = False
                                    "SOFR",          // Подразделение системы-отпр
                                    V_WS_TYPE,       // ИД типа данных
                                    RequestText,     // Бизнес данные в виде XML
                                    v_log_id,        // ID usr_exchng_log
                                    "EPRSHB2", 
                                    v_timeout
                                       );
      if (v_answer.ResultCode == 0)
         xml_str = v_answer.responsetext;
         v_logger_obj.add_response(xml_str); 

         v_transformator = NULL;
         if (xml_str)
            /* Преобразуем запрос в объектный вид - begin */
            v_transformator = c_secur_msg_converter();
            if(not v_transformator.m_decode_escaped_char(xml_str))
               RunError(v_transformator.get_service_msg(), c_usr_err_obj(v_transformator.get_service_msg()));
            end;
            v_answer_data = v_transformator.m_secur_wol_internal_req(v_transformator.get_pure_msg());
            /* Преобразуем запрос в объектный вид - end */

            if(ValType(v_answer_data) == V_UNDEF)
               err_txt = "Не удалось преобразовать XML в объект";
               RunError(err_txt, c_usr_err_obj(err_txt)); 
            end;
            
            RespData = adp_GetAmountSNOBResponse(v_answer_data);
         end;
      else 
         v_logger_obj.add_error_info(v_answer.ResultCode, v_answer.ResultText);
         RespData = MakeErrorAnswer(v_answer.ResultCode, v_answer.ResultText);
      end;

   else
      v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, err_txt);
      RespData = MakeErrorAnswer(UNIVERSAL_ERROR_CODE, err_txt);
   end;

   return RespData;

onError(err)
   stat = c_err_obj.obtain_err_code(err);
   err_txt = c_err_obj.obtain_err_msg(err);

   RespData = MakeErrorAnswer(stat, err_txt);
   return RespData;
end;


/**
 @brief Точка входа
 @param[in] ReqData - объект класса c_GetAmountSNOBRequest
 @return объект класса adp_GetAmountSNOBResponse
*/
macro GetAmountSNOBReq (ReqData)
   return run_GetAmountSNOB(ReqData);
end;