/*---------------------------------*/
/* Интерфейс адреса групп рассылки */
/*                                 */
/* Автор: Карпов В.                */
/*---------------------------------*/

import rsd;
import RsbFormsInter;
import "usr_key_const.mac";
import "int_usr_globals.mac";


/*-----------------------------------*/
/* Глобальные переменные и константы */
/*-----------------------------------*/
private const CV_EMAIL_GROUP_DICT = 5009;                  /* Номер справочника групп рассылки */
private const CV_EMAIL_CAT_0 = "Основные получатели";      /* Категория 0 получателя */
private const CV_EMAIL_CAT_1 = "Получатели копии";         /* Категория 1 получателя */
private const CV_EMAIL_CAT_2 = "Получатели скрытой копии"; /* Категория 2 получателя */

private var gv_column_list_scr_main = TArray(); /* Список колонок основного скроллинга */
private var gv_num_columns_scr_main = 0;        /* Количество колонок основного скроллинга */
private var gv_scr_focus_main = 0;              /* Номер позиции основного скроллинга */

private var gv_email_group_id = 0;              /* Идентификатор редактируемой группы рассылки */

private var gv_column_list_scr_addr = TArray(); /* Список колонок скроллинга списка адресов */
private var gv_num_columns_scr_addr = 0;        /* Количество колонок скроллинга списка адресов */
private var gv_scr_focus_addr = 0;              /* Номер позиции скроллинга списка адресов */


/*-------------------------------*/
/* Параметры получателя рассылки */
/*-------------------------------*/
private class c_email_prm(p_id, p_group, p_email, p_place, p_comment)
   var v_id;
   var v_group;
   var v_email;
   var v_place;
   var v_place_bd;
   var v_comment;


   private macro m_sv0_decode(p_value, p_type)
      private var v_ret;

      if(ValType(p_value) == 26)
         if(p_type == V_STRING)
            v_ret = "";
         elif(p_type == V_INTEGER)
            v_ret = 0;

         /* Предполагается, что все возможные варианты должны быть явно учтены */
//         else
         end;
      else
         v_ret = p_value;
      end;

      return v_ret;
   end;


   private macro m_init_email_prm(i_id, i_group, i_email, i_place, i_comment)
      private var v_aux_buf = ValType(i_id);
      if((v_aux_buf != V_UNDEF) and (v_aux_buf != 26))
         v_id = i_id;
         v_group = i_group;
         v_email = m_sv0_decode(i_email, V_STRING); /* Проверка пригодиться не должна, но пусть будет */
         v_comment = m_sv0_decode(i_comment, V_STRING);
         v_place_bd = i_place;
         if(v_place_bd == 0)
            v_place = CV_EMAIL_CAT_0;
         elif(v_place_bd == 1)
            v_place = CV_EMAIL_CAT_1;
         elif(v_place_bd == 2)
            v_place = CV_EMAIL_CAT_2;
         else
            v_place = CV_EMAIL_CAT_0;
         end;
      else
         v_id = 0;
         v_group = gv_email_group_id;
         v_email = "";
         v_comment = "";
         v_place_bd = 0;
         v_place = CV_EMAIL_CAT_0;
      end;
   end;


   m_init_email_prm(p_id, p_group, p_email, p_place, p_comment);
end; /* class c_evnt_prm() */


/*----------------------------------*/
/* Сохранение параметров получателя */
/*----------------------------------*/
private macro m_save_email_prm(p_email_prm : @object)
   private var v_ret = true;
   private var v_sql, v_cmd;

   if((ValType(p_email_prm.v_email) == V_STRING) and (strlen(p_email_prm.v_email) > 0))
      v_sql =    "MERGE INTO usr_email_addr_dbt tbl_mail ";
      v_sql = v_sql + "USING (SELECT :p_id p_id, :p_group p_group, :p_email p_email, :p_comment p_comment, ";
      v_sql = v_sql + "              DECODE(:p_place, 0, 'R', 1, 'C', 2, 'H', 'R') p_place FROM DUAL) src_data ";
      v_sql = v_sql + "   ON (tbl_mail.t_id = src_data.p_id) ";
      v_sql = v_sql + "WHEN MATCHED THEN ";
      v_sql = v_sql + "     UPDATE SET tbl_mail.t_email = src_data.p_email, ";
      v_sql = v_sql + "                tbl_mail.t_place = src_data.p_place, ";
      v_sql = v_sql + "                tbl_mail.t_comment = src_data.p_comment ";
      v_sql = v_sql + "WHEN NOT MATCHED THEN ";
      v_sql = v_sql + "     INSERT (tbl_mail.t_group, tbl_mail.t_email, tbl_mail.t_place, tbl_mail.t_comment) ";
      v_sql = v_sql + "     VALUES (src_data.p_group, src_data.p_email, src_data.p_place, src_data.p_comment) ";

      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_id", RSDBP_IN, p_email_prm.v_id);
      v_cmd.addParam("p_group", RSDBP_IN, p_email_prm.v_group);
      v_cmd.addParam("p_email", RSDBP_IN, p_email_prm.v_email);
      v_cmd.addParam("p_comment", RSDBP_IN, p_email_prm.v_comment);
      v_cmd.addParam("p_place", RSDBP_IN, p_email_prm.v_place_bd);
      v_cmd.execute();

      v_cmd.close(); v_cmd = NULL; v_sql = NULL;
   else
      v_ret = false;
   end;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end; /* macro m_save_email_prm() */


/*-----------------------------------*/
/* Класс формы параметров получателя */
/*-----------------------------------*/
class (TRsbPanel) c_email_prm_form(p_id, p_group, p_email, p_place, p_comment)
   const CV_POS_X = 3;
   const CV_POS_Y = 1;
   private var v_msg_to_save;

   var v_email_prm;
   var v_id_fld;
   var v_group_fld;
   var v_email_fld;
   var v_place_fld;
   var v_comment_fld;


   /* Обработчик событий нажатия кнопок */
   macro m_email_on_key_pressed(p_rsb_evnt)
      var v_aux_buf;

      if(p_rsb_evnt.KeyCode == K_F2)
//         v_email_prm.v_id
//         v_email_prm.v_group
         v_email_prm.v_email = v_email_fld.value;
//         v_email_prm.v_place
//         v_email_prm.v_place_bd
         v_email_prm.v_comment = v_comment_fld.value;

         if(not m_save_email_prm(@v_email_prm))
            msgbox("Не удалось сохранить параметры получателя!");
         else
            msgbox("Параметры сохранены");
         end;
      end;

      return true;
   end; /* m_email_on_key_pressed() */


   /* Заполнение списка выбора ComboBox place */
   private macro m_fill_place_cb()
      v_place_fld.addChoice(0, CV_EMAIL_CAT_0);
      v_place_fld.addChoice(1, CV_EMAIL_CAT_1);
      v_place_fld.addChoice(2, CV_EMAIL_CAT_2);
   end; /* macro m_fill_place_cb() */


   /* Обработка выбора значения в ComboBox place */
   macro m_evnt_place_cb_select(p_rsb_evnt)
//      v_place_fld.value = v_email_prm.v_place;
      v_place_fld.value = p_rsb_evnt.source.value;
      v_email_prm.v_place = p_rsb_evnt.source.value;
      v_email_prm.v_place_bd = p_rsb_evnt.source.currentChoice;
   end; /* macro m_evnt_place_cb_select(p_rsb_evnt) */


   private macro m_init_email_prm_form(i_this_obj, i_id, i_group, i_email, i_place, i_comment)
      Caption = "Параметры получателя рассылки";
      Status = "[Esc] Выход  [F2] Сохранение  [F3] Список";
//      v_evnt_prm = c_evnt_prm(i_recid, i_objecttype, i_objectid);
      v_email_prm = c_email_prm(i_id, i_group, i_email, i_place, i_comment);

      /* ID получателя */
      AddLabel(TRsbLabel(CV_POS_X, CV_POS_Y, "ID получателя"));
      v_id_fld = TRsbEditField(FT_INT);
      v_id_fld.bindValue(v_email_prm, "v_id", 25);
      v_id_fld.setPosition(CV_POS_X, (CV_POS_Y + 1));
      v_id_fld.setSize(11, 1);
      v_id_fld.focusable = true;
      v_id_fld.enabled = true;
      v_id_fld.editable = false;
      AddControl(v_id_fld);

      /* ID группы рассылки */
      AddLabel(TRsbLabel((CV_POS_X + 22), CV_POS_Y, "ID группы рассылки"));
      v_group_fld = TRsbEditField(FT_INT);
      v_group_fld.bindValue(v_email_prm, "v_group", 25);
      v_group_fld.setPosition((CV_POS_X + 22), (CV_POS_Y + 1));
      v_group_fld.setSize(11, 1);
      v_group_fld.focusable = true;
      v_group_fld.enabled = true;
      v_group_fld.editable = false;
      AddControl(v_group_fld);

      /* E-mail получателя */
      AddLabel(TRsbLabel(CV_POS_X, (CV_POS_Y + 3), "E-mail получателя"));
      v_email_fld = TRsbEditField(FT_STRING);
      v_email_fld.bindValue(v_email_prm, "v_email", 2000);
      v_email_fld.setPosition(CV_POS_X, (CV_POS_Y + 4));
      v_email_fld.setSize(20, 1);
      v_email_fld.focusable = true;
      v_email_fld.enabled = true;
      v_email_fld.editable = true;
      AddControl(v_email_fld);

      /* Категория получателя */
      AddLabel(TRsbLabel((CV_POS_X + 22), (CV_POS_Y + 3), "Категория получателя"));
      v_place_fld = TRsbComboBox();
//      v_place_fld.dataType = FT_INT;
      m_fill_place_cb();
      v_place_fld.bindValue(v_email_prm, "v_place");
      v_place_fld.value = v_email_prm.v_place;
      v_place_fld.setPosition((CV_POS_X + 22), (CV_POS_Y + 4));
      v_place_fld.setSize(20, 1);

      v_place_fld.OnItemSelected(R2M(i_this_obj, "m_evnt_place_cb_select"));
      AddControl(v_place_fld);


      /* Комментарий */
      AddLabel(TRsbLabel(CV_POS_X, (CV_POS_Y + 6), "Комментарий"));
      v_comment_fld = TRsbEditField(FT_STRING);
      v_comment_fld.bindValue(v_email_prm, "v_comment", 2000);
      v_comment_fld.setPosition(CV_POS_X, (CV_POS_Y + 7));
      v_comment_fld.setSize(42, 3);
      v_comment_fld.focusable = true;
      v_comment_fld.enabled = true;
      v_comment_fld.editable = true;
      AddControl(v_comment_fld);

      AddEventHandler(RSB_EV_KEY_PRESSED, R2M(i_this_obj, "m_email_on_key_pressed"));

      setSize(47, 12);
      setPosition(20, 6);
   end; /* macro m_init_email_prm_form() */


   InitTRsbPanel();

   m_init_email_prm_form(this, p_id, p_group, p_email, p_place, p_comment);
end; /* class (TRsbPanel) c_email_prm_form() */


/*----------------------------------------------*/
/* Удаление заданной позиции из группы рассылки */
/*----------------------------------------------*/
private macro m_delete_email(p_email_id)
   private var v_ret = true;
   private var v_sql, v_cmd;

   v_sql =         "DELETE FROM usr_email_addr_dbt ";
   v_sql = v_sql + " WHERE t_id = :p_email_id ";

   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_email_id", RSDBP_IN, p_email_id);
   v_cmd.execute();

   v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end; /* macro m_delete_email() */


/*--------------------------------------------*/
/* Метод обработки событий скроллинга адресов */
/*--------------------------------------------*/
macro m_addr_proc_scr(p_rs, p_cmd, p_id, p_key)
   var CM_FLAG = CM_DEFAULT;
   var gv_email_prm_form;

   if(p_cmd == DLG_INIT)
      while((gv_scr_focus_addr != 0) and p_rs.movenext())
         if(p_rs.value("t_id") == gv_scr_focus_addr)
            gv_scr_focus_addr = 0;
            GoToScroll(p_rs);
         end;
      end;

   elif(p_cmd == DLG_KEY)
      if(p_key == K_ENTER)
         if(ValType(p_rs.value("t_id")) != 26)
            gv_scr_focus_addr = p_rs.value("t_id");

            gv_email_prm_form = c_email_prm_form(p_rs.value("t_id"),
                                                 p_rs.value("t_group"),
                                                 p_rs.value("t_email"),
                                                 p_rs.value("t_place"),
                                                 p_rs.value("t_comment"));
            gv_email_prm_form.run;

            CM_FLAG = CM_SELECT;
         else
            CM_FLAG = CM_IGNORE;
         end;

      elif(p_key == K_F8)
         gv_scr_focus_addr = 0;
         CM_FLAG = CM_IGNORE;

         if(ValType(p_rs.value("t_id")) != 26)
            if(gettrue(true, "Удалить запись?"))
               /* Удаление заданной позиции из группы рассылки */
               if(not m_delete_email(p_rs.value("t_id")))
                  msgbox(string("Не удалось удалить адрес ",
                                p_rs.value("t_email"),
                                " (",
                                p_rs.value("t_id"),
                                ") ",
                                "из группы рассылки"));
               else
                  CM_FLAG = CM_SELECT;
               end;
            end;
         end;


      elif(p_key == K_F9)

         gv_email_prm_form = c_email_prm_form();
         gv_email_prm_form.run;

         CM_FLAG = CM_SELECT;

      elif(p_key == K_ESCAPE)
         if(not gettrue(true, "Выйти из списка адресов группы рассылки?"))
            CM_FLAG = CM_IGNORE;
         end;
      end;

   end;

   return CM_FLAG;
end; /* macro m_addr_proc_scr() */


/*-------------------------------------------------------------*/
/* Метод, запускающий скроллинг списка адресов группы рассылки */
/*-------------------------------------------------------------*/
private macro m_run_addr_scroll(p_group_id)
   private var sql_scr, cmd_scr, rs_scr;
   private var sql_main = "";
   private var sql_filtr = "";
   private var sql_order = "";

   gv_email_group_id = p_group_id;

   gv_column_list_scr_addr.size = 0;
   gv_num_columns_scr_addr = 0;
   gv_scr_focus_addr = 0;

   sql_main =            "  SELECT tbl_addr.t_id, tbl_addr.t_group, tbl_addr.t_email, tbl_addr.t_comment, ";
   sql_main = sql_main + "         DECODE(tbl_addr.t_place, 'R', 0, 'C', 1, 'H', 2, 0) t_place, ";
   sql_main = sql_main + "         DECODE(tbl_addr.t_place, 'R', 'Основные получатели', 'C', 'Получатели копии', 'H', 'Получатели скрытой копии', tbl_addr.t_place) t_place_char ";
   sql_main = sql_main + "    FROM usr_email_addr_dbt tbl_addr ";
   sql_main = sql_main + "   WHERE tbl_addr.t_group = :p_group_id ";
   sql_order = sql_order + "ORDER BY tbl_addr.t_id ASC ";

   sql_scr = sql_main + sql_filtr + sql_order;

   cmd_scr = RSDCommand(sql_scr);
   cmd_scr.NullConversion = true;
   cmd_scr.addParam("p_group_id", RSDBP_IN, p_group_id);

   rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   rs_scr.NullConversion = true;

   m_add_column(gv_column_list_scr_addr, gv_num_columns_scr_addr, "T_EMAIL",      "E-mail", 10); gv_num_columns_scr_addr = gv_num_columns_scr_addr + 1;
   m_add_column(gv_column_list_scr_addr, gv_num_columns_scr_addr, "T_PLACE_CHAR", "Категория получателя", 15); gv_num_columns_scr_addr = gv_num_columns_scr_addr + 1;
   m_add_column(gv_column_list_scr_addr, gv_num_columns_scr_addr, "T_COMMENT",    "Текстовое описание", 15); gv_num_columns_scr_addr = gv_num_columns_scr_addr + 1;

   while(RunScroll(rs_scr, gv_num_columns_scr_addr, gv_column_list_scr_addr, "addr_list", "m_addr_proc_scr", "Список адресов группы рассылки", "ESC - выход  ENTER - редактирование  F8 - удалить  F9 - добавить", true, 7, 5, NULL, 40))
      /* Обновляем скроллинг */
      cmd_scr.close(); cmd_scr = NULL;
      rs_scr.close(); rs_scr = NULL;

      sql_filtr = "";

      sql_scr = sql_main + sql_filtr + sql_order;

      cmd_scr = RSDCommand(sql_scr);
      cmd_scr.NullConversion = true;
      cmd_scr.addParam("p_group_id", RSDBP_IN, p_group_id);

      rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   end;

   rs_scr.close();

end; /* macro m_run_addr_scroll() */


/*-----------------------------------------*/
/* Изменение активности рассылки по группе */
/*-----------------------------------------*/
private macro m_switch_post_state(p_group_id, p_is_active)
   private var v_ret = true;
   private var v_sql, v_cmd;

   v_sql =         "UPDATE dllvalues_dbt ";
   v_sql = v_sql + "   SET t_flag = :p_is_active ";
   v_sql = v_sql + " WHERE t_list = :p_email_group_dict ";
   v_sql = v_sql + "   AND t_element = :p_group_id ";

   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_is_active", RSDBP_IN, abs(p_is_active - 1));
   v_cmd.addParam("p_email_group_dict", RSDBP_IN, CV_EMAIL_GROUP_DICT);
   v_cmd.addParam("p_group_id", RSDBP_IN, p_group_id);
   v_cmd.execute();

   v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end; /* macro m_switch_post_state() */


/*----------------------------------------------*/
/* Метод обработки событий основного скроллинга */
/*----------------------------------------------*/
macro m_post_proc_scr(p_rs, p_cmd, p_id, p_key)
   var CM_FLAG = CM_DEFAULT;

   if(p_cmd == DLG_INIT)
      while((gv_scr_focus_main != 0) and p_rs.movenext())
         if(p_rs.value("t_element") == gv_scr_focus_main)
            gv_scr_focus_main = 0;
            GoToScroll(p_rs);
         end;
      end;

   elif(p_cmd == DLG_KEY)
      if(p_key == K_ENTER)

         /* Отображение списка адресов заданной группы рассылки */
         m_run_addr_scroll(p_rs.value("t_element"));

         CM_FLAG = CM_IGNORE;

      elif(p_key == K_F5)
         gv_scr_focus_main = p_rs.value("t_element");

         if(ValType(p_rs.value("t_element")) != 26)
            /* Переключение работы рассылки писем по заданной группе */
            if(not m_switch_post_state(p_rs.value("t_element"), p_rs.value("t_flag")))
               msgbox(string("Не удалось изменить состояние рассылки по группе \"",
                             p_rs.value("t_name"),
                             "\" (",
                             p_rs.value("t_element"),
                             ")"));
            end;
         end;

         CM_FLAG = CM_SELECT;

      elif(p_key == K_ESCAPE)
         if(not gettrue(true, "Выйти из списка групп рассылки?"))
            CM_FLAG = CM_IGNORE;
         end;
      end;

   end;

   return CM_FLAG;
end; /* macro m_post_proc_scr() */


/*---------------------------------------*/
/* Метод, запускающий основной скроллинг */
/*---------------------------------------*/
private macro m_run_main_scroll()
   private var sql_scr, cmd_scr, rs_scr;
   private var sql_main = "";
   private var sql_filtr = "";
   private var sql_order = "";

   sql_main =            "  SELECT tbl_dict.t_element, tbl_dict.t_name, ";
   sql_main = sql_main + "         DECODE(tbl_dict.t_flag, 1, 'ВКЛ.', 'ВЫКЛ.') t_is_active, ";
   sql_main = sql_main + "         tbl_dict.t_flag, tbl_dict.t_note ";
   sql_main = sql_main + "    FROM dllvalues_dbt tbl_dict ";
   sql_main = sql_main + "   WHERE tbl_dict.t_list = :p_dict_num ";
   sql_order = sql_order + "ORDER BY tbl_dict.t_element ASC ";

   sql_scr = sql_main + sql_filtr + sql_order;

   cmd_scr = RSDCommand(sql_scr);
   cmd_scr.NullConversion = true;
   cmd_scr.addParam("p_dict_num", RSDBP_IN, CV_EMAIL_GROUP_DICT);

   rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   rs_scr.NullConversion = true;

   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_ELEMENT",   "ID группы", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_NAME",      "Название группы", 15); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_IS_ACTIVE", "Состояние рассылки по группе", 15); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_NOTE",      "Описание", 15); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;

   while(RunScroll(rs_scr, gv_num_columns_scr_main, gv_column_list_scr_main, "post_list", "m_post_proc_scr", "Список групп рассылки", "ESC - выход  F5 - ВКЛ./ВЫКЛ.  ENTER - список адресов", true, 5, NULL, NULL, 40))
      /* Обновляем скроллинг */
      cmd_scr.close(); cmd_scr = NULL;
      rs_scr.close(); rs_scr = NULL;

      sql_filtr = "";

      sql_scr = sql_main + sql_filtr + sql_order;

      cmd_scr = RSDCommand(sql_scr);
      cmd_scr.NullConversion = true;
      cmd_scr.addParam("p_dict_num", RSDBP_IN, CV_EMAIL_GROUP_DICT);

      rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   end;

   rs_scr.close();

end; /* macro m_run_main_scroll() */


/* Запуск основного скроллинга */
m_run_main_scroll();
Exit(1);