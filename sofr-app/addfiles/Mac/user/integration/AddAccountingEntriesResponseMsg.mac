/*-------------------------------------------*/
/* Cоздание проводок/платежей (ответ в СОФР) */
/* Автор: Горленко, Карпов                   */
/*-------------------------------------------*/

//import "global_utils_intgr.mac";
import "u_common_email_utils.mac";
import "uws_exchng_log.mac";


private const CV_EMAIL_HEAD = "Ошибка. Ответ - Создание проводок/платежей";
private const CV_EMAIL_ROW_HEAD = "AddAccountingEntriesResponse: ";
private const CV_EMAIL_GRP_ENTRY_RESP = 2;

//BIQ-13363 доп.логирование
private const CV_REG_LOG_ON = "РСХБ\\ИНТЕГРАЦИЯ\\ВКЛЮЧИТЬ_ЛОГИ_ВЫГРУЗКИ_ПЛАТЕЖЕЙ";
private var g_log_isactive;
private var stat_regval;
GetRegistryValue(CV_REG_LOG_ON, V_BOOL, g_log_isactive, stat_regval);
if (stat_regval>0)
   g_log_isactive = false;
end;


/**
@brief   Логирование сообщения
@param[in] p_text       Сообщение для записи в лог
*/
private macro AddLog_it(p_text)
   if (g_log_isactive)
      var cmd = RSDCommand("begin it_log.log(:p_text,it_log.C_MSG_TYPE__DEBUG); end;");
      cmd.AddParam("p_text", RSDBP_IN, p_text);
      cmd.execute;
      cmd.close;
   end;
end;


/*----------------------------------------------------*/
/* Сохранение идентификатора проводки внешней системы */
/*----------------------------------------------------*/
private macro m_save_entry_ref_id(p_trn_id, p_ref_id, p_jmsmessageid)
   var v_ret = true;
   var v_aux_buf = 0;
   var v_sql, v_cmd;
   var v_ref_id;
   var v_trn_id;


   //maa040219 - для обычных проводок сами клеим op.entry - всегда #1
   if ( ( index (p_trn_id, "," ) > 0) or ( index (p_trn_id, "#" ) > 0) )
   else
       v_trn_id = p_trn_id + "#1"
   end ;


   if(substr(p_ref_id, 1, 4) != "paym") 
      /*----------*/
      /* Проводки */
      /*----------*/

      if(substr(p_ref_id, 1, 5) != "trnpm")
         v_ref_id = p_ref_id;
      else
         v_ref_id = Int(substr(p_ref_id, 6));
      end;

      v_sql =         "DECLARE ";
      v_sql = v_sql + "   v_ret NUMBER(5) := 0; ";
      v_sql = v_sql + "   v_ref_id NUMBER(10); ";
      v_sql = v_sql + "BEGIN ";
      v_sql = v_sql + "    BEGIN ";
      v_sql = v_sql + "        SELECT t_acctrnid INTO v_ref_id ";
      v_sql = v_sql + "          FROM dacctrn_dbt ";
      v_sql = v_sql + "         WHERE t_acctrnid = :p_ref_id; ";

      v_sql = v_sql + "        UPDATE dacctrn_dbt ";
      v_sql = v_sql + "           SET t_userfield4 = :p_trn_id ";
      v_sql = v_sql + "         WHERE t_acctrnid = v_ref_id; ";

//   v_sql = v_sql + "        COMMIT; ";

      v_sql = v_sql + "    EXCEPTION ";
      v_sql = v_sql + "        WHEN OTHERS ";
      v_sql = v_sql + "        THEN ";
      v_sql = v_sql + "            v_ret := 1; ";
      v_sql = v_sql + "    END; ";

      v_sql = v_sql + "    :p_ret := v_ret; ";
      v_sql = v_sql + "END; ";

      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_ref_id", RSDBP_IN, v_ref_id);
      v_cmd.addParam("p_trn_id", RSDBP_IN, v_trn_id);
      v_cmd.addParam("p_ret", RSDBP_OUT, V_INTEGER);
      v_cmd.execute();

      if(v_cmd.value("p_ret") == 0)
         v_ret = true;
      else
         v_ret = false;
      end;

      v_cmd.close(); v_cmd = NULL; v_sql = NULL;

//   end;
//shev trnpm может быть и платежом и проводкой
// 2020-07-10 Cherednichenko - trnpm - это проводка, порожденная операцией по платежу, т.е.в составе операции; => возвращаю ветку к исходной
   else

//   if((substr(p_ref_id, 1, 4) == "paym") or (substr(p_ref_id, 1, 5) == "trnpm"))


      /*---------*/
      /* Платежи */
      /*---------*/
      if(substr(p_ref_id, 1, 4) == "paym")  
         v_ref_id = substr(p_ref_id, 5);
      else
         v_ref_id = substr(p_ref_id, 1, 6);
      end;

      AddLog_it("AddAcc~"+p_jmsmessageid+"~1. Обновление dpmpaym_dbt.t_userfield4, "+"v_ref_id="+v_ref_id+", p_ref_id="+p_ref_id+", p_trn_id="+p_trn_id+", v_trn_id="+v_trn_id);

      v_sql =         "DECLARE ";
      v_sql = v_sql + "   v_ret NUMBER(5) := 0; ";
      v_sql = v_sql + "   v_ref_id NUMBER(10); ";
      v_sql = v_sql + "BEGIN ";
      v_sql = v_sql + "    BEGIN ";
      v_sql = v_sql + "        SELECT t_paymentid INTO v_ref_id ";
      v_sql = v_sql + "          FROM dpmpaym_dbt ";
      v_sql = v_sql + "         WHERE t_paymentid = :p_ref_id; ";

      v_sql = v_sql + "        UPDATE dpmpaym_dbt ";
      v_sql = v_sql + "           SET t_userfield4 = :p_trn_id ";
      v_sql = v_sql + "         WHERE t_paymentid = v_ref_id; ";

//   v_sql = v_sql + "        COMMIT; ";

      v_sql = v_sql + "    EXCEPTION ";
      v_sql = v_sql + "        WHEN OTHERS ";
      v_sql = v_sql + "        THEN ";
      v_sql = v_sql + "            v_ret := 1; ";
      v_sql = v_sql + "            it_log.log('AddAcc~Обновление dpmpaym_dbt.t_userfield4 '||sqlerrm,it_log.C_MSG_TYPE__DEBUG); ";
      v_sql = v_sql + "    END; ";

      v_sql = v_sql + "    :p_ret := v_ret; ";
      v_sql = v_sql + "END; ";

      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_ref_id", RSDBP_IN, v_ref_id);
      v_cmd.addParam("p_trn_id", RSDBP_IN, v_trn_id);
      v_cmd.addParam("p_ret", RSDBP_OUT, V_INTEGER);
      v_cmd.execute();

      if(v_cmd.value("p_ret") == 0)
         v_ret = true;
      else
         v_ret = false;
      end;

      v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   end;

   return v_ret;

onError(err)
   AddLog_it("AddAcc~"+p_jmsmessageid+"~1.0. "+err.message+"~"+getFullCmdErrorText(v_cmd));
   v_ret = false;
   return v_ret;
end;


private class c_IntegrSymId(p_income_data)
   var ObjectId     : string
      ,SystemId     : string
      ,SystemNodeId : string;

   private macro m_init_IntegrSymId(i_income_data)
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         ObjectId = uTryToGetPropS(i_income_data, "ObjectId");
         if(ValType(ObjectId) == V_UNDEF)
            v_service_msg = string(InErrComPart, "ObjectId");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;

         SystemId = uTryToGetPropS(i_income_data, "SystemId");
         SystemNodeId = uTryToGetPropS(i_income_data, "SystemNodeId");
      end;
   end;

   m_init_IntegrSymId(p_income_data);
end;

private class с_Error(p_income_data)
   var ErrorCode : string
      ,ErrorDesc : string;

   private macro m_init_Error(i_income_data)
      if(ValType(i_income_data) != V_UNDEF)
         ErrorCode = uTryToGetPropS(i_income_data, "ErrorCode");
         ErrorDesc = uTryToGetPropS(i_income_data, "ErrorDesc");
      end;
   end;

   m_init_Error(p_income_data);
end;

private class с_EntryResult(p_income_data)
   var ResultCode           : string
      ,DocumentNumberInSOFR : string
      ,DocumentNumberInABS  : string
      ,EntryId              : object  /* c_IntegrSymId */
      ,ReferenceId          : object  /* c_IntegrSymId */
      ,ErrorList            : object; /* TArray */

   private macro m_init_EntryResult(i_income_data)
      private var v_aux_buf;
      private var v_aux_cntr;
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         ResultCode = uTryToGetPropS(i_income_data, "ResultCode");
         if(ValType(ResultCode) == V_UNDEF)
            v_service_msg = string(InErrComPart, "ResultCode");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;

         DocumentNumberInSOFR = uTryToGetPropS(i_income_data, "DocumentNumberInSOFR");
         DocumentNumberInABS = uTryToGetPropS(i_income_data, "DocumentNumberInABS");

         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "EntryId"));
         if(v_aux_buf != V_UNDEF)
            if(v_aux_buf == V_GENOBJ)
               EntryId = c_IntegrSymId(i_income_data.EntryId);
            else
               v_service_msg = string(InErrNotObj, "EntryId");
               RunError(v_service_msg, c_usr_err_obj(v_service_msg));
            end;
         else
            EntryId = NULL;
         end;

         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "ReferenceId"));
         if(v_aux_buf != V_UNDEF)
            if(v_aux_buf == V_GENOBJ)
               ReferenceId = c_IntegrSymId(i_income_data.ReferenceId);
            else
               v_service_msg = string(InErrNotObj, "ReferenceId");
               RunError(v_service_msg, c_usr_err_obj(v_service_msg));
            end;
         else
            ReferenceId = NULL;
         end;

         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "ErrorList"));
         if(v_aux_buf != V_UNDEF)
            if(v_aux_buf == V_GENOBJ)
               v_aux_cntr = 0;
               ErrorList = TArray;
               while(i_income_data.ErrorList.size > v_aux_cntr)
                  ErrorList.value(v_aux_cntr) = с_Error(i_income_data.ErrorList.value(v_aux_cntr));
                  v_aux_cntr = v_aux_cntr + 1;
               end;
            else
               v_service_msg = string(InErrNotObj, "ErrorList");
               RunError(v_service_msg, c_usr_err_obj(v_service_msg));
            end;
         else
            ErrorList = NULL;
         end;
      end;
   end;

   m_init_EntryResult(p_income_data);
end;

private class с_TransactionResult(p_income_data)
   var TransactionId   : object  /* c_IntegrSymId */
      ,EntryResultList : object; /* TArray */

   private macro m_init_TransactionResult(i_income_data)
      private var v_aux_buf;
      private var v_aux_cntr;
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "TransactionId"));
         if(v_aux_buf != V_UNDEF)
            if(v_aux_buf == V_GENOBJ)
               TransactionId = c_IntegrSymId(i_income_data.TransactionId);
            else
               v_service_msg = string(InErrNotObj, "TransactionId");
               RunError(v_service_msg, c_usr_err_obj(v_service_msg));
            end;
         else
            v_service_msg = string(InErrComPart, "TransactionId");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;

         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "EntryResultList"));
         if(v_aux_buf != V_UNDEF)
            if(v_aux_buf == V_GENOBJ)
               v_aux_cntr = 0;
               EntryResultList = TArray;
               while(i_income_data.EntryResultList.size > v_aux_cntr)
                  EntryResultList.value(v_aux_cntr) = с_EntryResult(i_income_data.EntryResultList.value(v_aux_cntr));
                  v_aux_cntr = v_aux_cntr + 1;
               end;
            else
               v_service_msg = string(InErrNotObj, "EntryResultList");
               RunError(v_service_msg, c_usr_err_obj(v_service_msg));
            end;
         else
            v_service_msg = string(InErrComPart, "EntryResultList");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;
      end;
   end;

   m_init_TransactionResult(p_income_data);
end;

private class c_AddAccountingEntriesResponse(p_income_data)
   var TransactionResultList : object; /* TArray */

   private macro m_init_AddAccountingEntriesResponse(i_income_data)
      private var v_aux_buf;
      private var v_aux_cntr;
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "TransactionResultList"));
         if(v_aux_buf != V_UNDEF)
            if(v_aux_buf == V_GENOBJ)
               v_aux_cntr = 0;
               TransactionResultList = TArray;
               while(i_income_data.TransactionResultList.size > v_aux_cntr)
                  TransactionResultList.value(v_aux_cntr) = с_TransactionResult(i_income_data.TransactionResultList.value(v_aux_cntr));
                  v_aux_cntr = v_aux_cntr + 1;
               end;
            else
               v_service_msg = string(InErrNotObj, "TransactionResultList");
               RunError(v_service_msg, c_usr_err_obj(v_service_msg));
            end;
         else
            v_service_msg = string(InErrComPart, "TransactionResultList");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;
      end;
   end;

   m_init_AddAccountingEntriesResponse(p_income_data);
end;

private class c_AddAccountingEntriesResponseMsg(p_income_data)
   var AddAccountingEntriesResponse : object; /* c_AddAccountingEntriesResponse */

   private macro m_init_AddAccountingEntriesResponseMsg(i_income_data)
      private var v_aux_buf;
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "AddAccountingEntriesResponse"));
         if(v_aux_buf != V_UNDEF)
            if(v_aux_buf == V_GENOBJ)
               AddAccountingEntriesResponse = c_AddAccountingEntriesResponse(i_income_data.AddAccountingEntriesResponse);
            else
               v_service_msg = string(InErrNotObj, "AddAccountingEntriesResponse");
               RunError(v_service_msg, c_usr_err_obj(v_service_msg));
            end;
         else
            v_service_msg = string(InErrComPart, "AddAccountingEntriesResponse");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;
      end;
   end;

   m_init_AddAccountingEntriesResponseMsg(p_income_data);
end;

/*----------------------------------------------*/
/* Собрать строку сообщения об ошибке по списку */
/*----------------------------------------------*/
private macro m_make_err_str(p_err_list)
   var v_ret;
   var v_cntr_err = 0;

   if(ValType(p_err_list) != V_UNDEF)
      while(p_err_list.size > v_cntr_err)
         v_ret = string("(", v_cntr_err, "): ",
                        p_err_list.value(v_cntr_err).ErrorCode, " ",
                        p_err_list.value(v_cntr_err).ErrorDesc);

         v_cntr_err = v_cntr_err + 1;
      end;
   else
      v_ret = "";
   end;

   return v_ret;
end;

// 2020-04-21 Cherednichenko пользовательская ф-я изменения статуса события с добавлением кода и описания ошибки из бискваита
private macro m_upd_table_proc_evnt_stat_w_err(p_objectid, p_objecttype, p_status, p_msg_id, p_status_old, p_res_code, p_res_text)

   var v_ret = true;
   var v_obj_tp_type, v_msg_id_type, v_stat_old_type;
   var v_res_code, v_res_text;
   var restest:integer = p_res_code;
   var v_sql, v_cmd;

   v_obj_tp_type = ValType(p_objecttype);
   v_msg_id_type = ValType(p_msg_id);
   v_stat_old_type = ValType(p_status_old);
   v_res_code = ValType(p_res_code);

   v_sql =         "UPDATE utableprocessevent_dbt ";
   v_sql = v_sql + "   SET t_status = :p_status ";
   if(v_msg_id_type != V_UNDEF)
      v_sql = v_sql + "   ,t_messageid = :p_msg_id ";
   end;

   if(v_res_code != V_UNDEF)
      v_sql = v_sql + "   ,t_resultcode = :p_res_code ";
     if(ValType(p_res_text) != V_UNDEF)
        v_sql = v_sql + " , t_resulttext = :p_res_text ";
     end;
   end;

   if(v_obj_tp_type != V_UNDEF)
      v_sql = v_sql + " WHERE t_objecttype = :p_objecttype ";
      v_sql = v_sql + "   AND t_objectid = :p_objectid ";
   else
      v_sql = v_sql + " WHERE t_recid = :p_recid ";
   end;
   if(v_stat_old_type != V_UNDEF)
      v_sql = v_sql + "AND t_status = :p_status_old ";
   end;

   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_status", RSDBP_IN, p_status);
   if(v_msg_id_type != V_UNDEF)
      v_cmd.addParam("p_msg_id", RSDBP_IN, p_msg_id);
   end;
   if(v_res_code != V_UNDEF)
      v_cmd.addParam("p_res_code", RSDBP_IN, p_res_code);
     if(ValType(p_res_text) != V_UNDEF)
//shev обрезаем ошибку под длину поля
//        v_cmd.addParam("p_res_text", RSDBP_IN, p_res_text);
        v_cmd.addParam("p_res_text", RSDBP_IN, substr(p_res_text,1,999));
      end;
   end;
   if(v_obj_tp_type != V_UNDEF)
      v_cmd.addParam("p_objecttype", RSDBP_IN, p_objecttype);
      v_cmd.addParam("p_objectid", RSDBP_IN, p_objectid);
   else
      v_cmd.addParam("p_recid", RSDBP_IN, p_objectid);
   end;
   if(v_stat_old_type != V_UNDEF)
      v_cmd.addParam("p_status_old", RSDBP_IN, p_status_old);
   end;

   v_cmd.execute();

   v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end;



/*--------------------------------------*/
/* Обновление статуса исходного события */
/*--------------------------------------*/
// 2020-04-21 Cherednichenko Для присвоения событию ошибки, вернувшейся из бисквит, добавляем параметр p_result_desc в ф-юю
private macro m_save_src_event_status(p_ref_id, p_result_code, p_log_id, p_result_desc, p_jmsmessageid)
   const CV_OBJTYPE_ENTRY = 1;
   const CV_OBJTYPE_PAYM = 501;
   const CV_STATUS_OLD = 2;
   var v_ret = true;
   var v_sql, v_cmd, v_rs;
   var v_is_paym;
   var v_objectid;
   var v_status;

   if(substr(p_ref_id, 1, 4) == "paym")
      v_objectid = substr(p_ref_id, 5);
      v_is_paym = true;
   else
      if(substr(p_ref_id, 1, 5) != "trnpm")
         v_objectid = p_ref_id;
         v_is_paym = false;
      else
         v_objectid = substr(p_ref_id, 6);
         v_is_paym = false;
      end;
   end;

   if(p_result_code == "0")
      v_status = 4;
   else
      v_status = 5;
   end;

   v_sql =         "SELECT t_objecttype, t_objectid, t_status ";
   v_sql = v_sql + "  FROM utableprocessevent_dbt ";
   v_sql = v_sql + " WHERE t_objecttype = :p_objecttype ";
   v_sql = v_sql + "   AND t_objectid = :p_objectid ";
   v_sql = v_sql + "   AND t_status = :p_status_old ";
   v_cmd = RSDCommand(v_sql);
   if(v_is_paym)
      v_cmd.addParam(":p_objecttype", RSDBP_IN, CV_OBJTYPE_PAYM);
   else
      v_cmd.addParam(":p_objecttype", RSDBP_IN, CV_OBJTYPE_ENTRY);
   end;
   v_cmd.addParam("p_objectid", RSDBP_IN, v_objectid);
   v_cmd.addParam("p_status_old", RSDBP_IN, CV_STATUS_OLD);
   v_rs = RSDRecordSet(v_cmd);
   if(v_rs.movenext())
// 2020-04-21 Cherednichenko Добавляем в таблицу событий ошибки, вернувшеся и бисквит
      //v_ret = m_upd_table_proc_evnt_status(v_rs.value("t_objectid"), v_rs.value("t_objecttype"), v_status, p_log_id, CV_STATUS_OLD);
      v_ret = m_upd_table_proc_evnt_stat_w_err(v_rs.value("t_objectid"), v_rs.value("t_objecttype"), v_status, p_log_id, CV_STATUS_OLD, 
                                               p_result_code, p_result_desc);
   else
      v_ret = false;
   end;

   v_rs.close(); v_cmd.close(); v_rs = NULL; v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   AddLog_it("AddAcc~"+p_jmsmessageid+"~6. "+err.message);
   v_ret = false;
   return v_ret;
end;

// DEF-82482 статус отправляется только для проводок, только для зачислений
// BOSS-1585 Отправка стутуса об исполнениии платежа
private macro send_order_status_done( p_ref_id)

   var v_sql, v_cmd;
   var v_ref_id;

   AddLog_it("send_order_status_done:Start p_ref_id="+p_ref_id);

   if(substr(p_ref_id, 1, 4) != "paym") 
      /*----------*/
      /* Проводки */
      /*----------*/

      if(substr(p_ref_id, 1, 5) != "trnpm")
         v_ref_id = p_ref_id;
      else
         v_ref_id = Int(substr(p_ref_id, 6));
      end;

      AddLog_it("send_order_status_done:Проводки v_ref_id="+v_ref_id);

      v_sql =         "declare ";
      v_sql = v_sql + "  v_nptxop_id dnptxop_dbt.t_id%type; ";
      v_sql = v_sql + "  v_acctrnid  dacctrn_dbt.t_acctrnid%type := :p_ref_id; ";
      v_sql = v_sql + "begin ";
      v_sql = v_sql + "  select max(n.t_id) into v_nptxop_id ";
      v_sql = v_sql + "    from dacctrn_dbt t ";
      v_sql = v_sql + "    join doprdocs_dbt d on t.t_acctrnid = d.t_acctrnid ";
      v_sql = v_sql + "    join doproper_dbt o on d.t_id_operation = o.t_id_operation ";
      v_sql = v_sql + "    join dnptxop_dbt n on n.t_id = to_number(o.t_documentid default null on conversion error) ";
      v_sql = v_sql + "         and n.t_dockind = o.t_dockind ";
      v_sql = v_sql + "   where d.t_acctrnid = v_acctrnid and n.t_dockind = 4607 and n.t_subkind_operation = 20 ";
      v_sql = v_sql + "     and t.t_account_payer like '306%' and t.t_account_receiver not like '603%'; ";
      v_sql = v_sql + "  if v_nptxop_id is not null then ";
      v_sql = v_sql + "     nontrading_orders_utils.send_order_status(p_operid  => v_nptxop_id,p_status => nontrading_orders_read.buf_status_done ); ";
      v_sql = v_sql + "  end if; ";
      v_sql = v_sql + "exception ";
      v_sql = v_sql + "  when others then ";
      v_sql = v_sql + "    it_log.log_error(p_object => 'nptx_money-> macro send_order_status_done' ";
      v_sql = v_sql + "                    ,p_msg => 'Ошибка при установке статуса Исполнено для проводки ' || v_acctrnid); ";
      v_sql = v_sql + "end; ";

      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_ref_id", RSDBP_IN, v_ref_id);
      v_cmd.execute();

      v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   end;

onError(err)
   AddLog_it("Ошибка при установке статуса Исполнено для ref_id="+p_ref_id+": "+err.message+"~"+getFullCmdErrorText(v_cmd));
end;

/*---------------------------*/
/* Обработка списка проводок */
/*---------------------------*/
private macro m_entry_list_proc(p_entry_list, p_email_processor : @object, p_log_err : @string, p_log_id, p_jmsmessageid)
   var v_cntr_entry = 0;
   var v_cntr_err = 0;
   var v_aux_buf;

   while(p_entry_list.size > v_cntr_entry)
      /* Если вернулся код "0" */
      if(p_entry_list.value(v_cntr_entry).ResultCode == "0")
         if((ValType(p_entry_list.value(v_cntr_entry).EntryId.ObjectId) != V_UNDEF) and
            (ValType(p_entry_list.value(v_cntr_entry).ReferenceId.ObjectId) != V_UNDEF))

            if(not m_save_entry_ref_id(p_entry_list.value(v_cntr_entry).EntryId.ObjectId,
                                       p_entry_list.value(v_cntr_entry).ReferenceId.ObjectId, 
                                       p_jmsmessageid))

               p_email_processor.m_add_row_to_msg_text(string(CV_EMAIL_ROW_HEAD,
                                                              "EntryId: ", p_entry_list.value(v_cntr_entry).EntryId.ObjectId, " - ",
                                                              "ReferenceId: ", p_entry_list.value(v_cntr_entry).ReferenceId.ObjectId, " - ",
                                                              "Не удалось сохранить ReferenceId"));

               p_log_err = m_add_note_to_str(string("Не удалось сохранить ReferenceId (",
                                                    p_entry_list.value(v_cntr_entry).EntryId.ObjectId,
                                                    ";",
                                                    p_entry_list.value(v_cntr_entry).ReferenceId.ObjectId,
                                                    ")"),
                                             p_log_err);
               AddLog_it("AddAcc~"+p_jmsmessageid+"~2. "+p_log_err);
            else
              // BOSS-1585
               send_order_status_done(p_entry_list.value(v_cntr_entry).ReferenceId.ObjectId);
            end;

         else
            v_aux_buf = m_make_err_str(p_entry_list.value(v_cntr_entry).ErrorList);

            p_email_processor.m_add_row_to_msg_text(string(CV_EMAIL_ROW_HEAD,
                                                           v_aux_buf, " - ",
                                                           "Передано не достаточно информации: ",
                                                           "EntryId: ", p_entry_list.value(v_cntr_entry).EntryId.ObjectId, " - ",
                                                           "ReferenceId: ", p_entry_list.value(v_cntr_entry).ReferenceId.ObjectId));

            p_log_err = m_add_note_to_str(string("Передано не достаточно информации (",
                                                 p_entry_list.value(v_cntr_entry).EntryId.ObjectId,
                                                 ";",
                                                 p_entry_list.value(v_cntr_entry).ReferenceId.ObjectId,
                                                 ")"),
                                          p_log_err);
            AddLog_it("AddAcc~"+p_jmsmessageid+"~3. "+v_aux_buf+"~"+p_log_err);
         end;

      /* В остальных случаях считаем, что произошла ошибка */
      else
         v_aux_buf = m_make_err_str(p_entry_list.value(v_cntr_entry).ErrorList);

         p_email_processor.m_add_row_to_msg_text(string(CV_EMAIL_ROW_HEAD,
                                                        v_aux_buf, " - ",
                                                        "Получена ошибка: ",
                                                        p_entry_list.value(v_cntr_entry).ResultCode));

         p_log_err = m_add_note_to_str(string("Получена ошибка (",
                                              p_entry_list.value(v_cntr_entry).ResultCode,
                                              ")"),
                                              p_log_err);
         AddLog_it("AddAcc~"+p_jmsmessageid+"~4. "+v_aux_buf+"~"+p_log_err);

         if(ValType(p_entry_list.value(v_cntr_entry).ErrorList) != V_UNDEF)
            v_cntr_err = 0;
            while(p_entry_list.value(v_cntr_entry).ErrorList.size > v_cntr_err)
               p_log_err = m_add_note_to_str(string(p_entry_list.value(v_cntr_entry).ErrorList.value(v_cntr_err).ErrorCode,
                                                    ";",
                                                    p_entry_list.value(v_cntr_entry).ErrorList.value(v_cntr_err).ErrorDesc),
                                             p_log_err);

               v_cntr_err = v_cntr_err + 1;
               AddLog_it("AddAcc~"+p_jmsmessageid+"~5. "+p_log_err);
            end;
         end;
      end;

      if(ValType(p_entry_list.value(v_cntr_entry).ReferenceId) != V_UNDEF)
// 2020-04-21 Cherednichenko Для присвоения событию ошибки, вернувшейся из бисквит, передаем еще и описание ошибки в ф-ю
         if(not m_save_src_event_status(p_entry_list.value(v_cntr_entry).ReferenceId.ObjectId, p_entry_list.value(v_cntr_entry).ResultCode, p_log_id, p_log_err, p_jmsmessageid))
            p_log_err = m_add_note_to_str(string("Не удалось обновить статус исходного события (",
                                                 p_entry_list.value(v_cntr_entry).EntryId.ObjectId,
                                                 ";",
                                                 p_entry_list.value(v_cntr_entry).ReferenceId.ObjectId,
                                                 ")"),
                                          p_log_err);
            AddLog_it("AddAcc~"+p_jmsmessageid+"~7. "+p_log_err);
         end;
      else
         //DEF-30750 Не обновляется статус события выгрузки проводки
         AddLog_it("AddAcc~"+p_jmsmessageid+"~8. "+"Не найден ReferenceId");
         // попытаемся найти исходный запрос
         var parent_eventid = 0, parent_ulog_id = 0, temp_str = "", pos = 0; 
         var sql_str, cmd, rs;
         var NotFound_event = false;

         sql_str = "select l_in.t_jmsmessageid in_jmsmessageid, l_out.t_jmsmessageid out_jmsmessageid, l_out.t_jmscorrelationid user_id"+
                   "  from dqueue_log_dbt l_in, "+
                   "       dqueue_log_dbt l_out "+
                   " where "+
                   "     l_in.t_jmsmessageid = :p_jmsmessageid "+
                   " and l_in.t_qname = 'ESB_TO_SOFR' "+
                   " and l_out.t_jmsmessageid = l_in.t_jmscorrelationid "+
                   " and rownum = 1; ";
         cmd = RSDCommand(sql_str);
         cmd.AddParam("p_jmsmessageid", RSDBP_IN, p_jmsmessageid);
         rs = RSDRecordSet(cmd);
         if (rs.movenext)
            temp_str = rs.value("user_id");
         else 
            temp_str = "";
         end;
         pos = Index(temp_str, "-");
         if (pos > 0)
            parent_eventid = Int(substr(temp_str,1,pos-1));
            parent_ulog_id = Int(substr(temp_str, pos+1));
         end;
         rs.close; cmd.close; rs = null; cmd = null;
         
         if (parent_eventid > 0)
            p_log_err = m_add_note_to_str("Не заполнен ReferenceID в ответе. Поиск по транспортному конверту",
                                          p_log_err);
            AddLog_it("AddAcc~"+p_jmsmessageid+"~9. "+p_log_err);
            sql_str = "select count(1) cnt from utableprocessevent_dbt "+
                      " where t_recid = :p_recid ";
            cmd = RSDCommand(sql_str);
            cmd.Addparam("p_recid", RSDBP_IN, parent_eventid);
            rs = RSDRecordSet(cmd);
            if (rs.movenext)
               if (rs.value("cnt") == 0)
                  NotFound_event = true;
               else 
                  if(not m_upd_table_proc_evnt_stat_w_err(parent_eventid, null, 5, p_log_id, 2, 
                                                     p_entry_list.value(v_cntr_entry).ResultCode, p_log_err));
                     p_log_err = m_add_note_to_str(string("Не удалось обновить статус исходного события (",
                                                          parent_eventid,
                                                          ";",
                                                          parent_ulog_id,
                                                          ")"),
                                                   p_log_err);
                     AddLog_it("AddAcc~"+p_jmsmessageid+"~10. "+p_log_err);
                  end;
               end;
            end;
         else 
            NotFound_event = true;
         end;

         if (NotFound_event)
            // попробуем отправить еще одно письмо с ошибкой
            var v_email_processor_extra = c_email_proc_env();
            var v_email_head = "СОФР. Ошибка установки статуса таблицы событий для проводки";
            var v_email_body = String("Для входящего ответа сервиса AddAccountingEntries с ID = ", p_jmsmessageid,
                                      " не могу найти исходное сообщение. Обновить статус события выгрузки проводки невозможно");
            v_email_processor_extra.m_set_msg_head(v_email_head);
            v_email_processor_extra.m_add_row_to_msg_text(v_email_body);
            v_email_processor_extra.m_save_to_submit_grp(16, true);    // использовать группу рассылки BrokerContract 
            v_email_processor_extra.m_submit_email_synch();

            p_log_err = m_add_note_to_str("Невозможно найти исходное событие ",
                                          p_log_err);
            AddLog_it("AddAcc~"+p_jmsmessageid+"~11. "+v_email_body);
         end;
      end;

      v_cntr_entry = v_cntr_entry + 1;

   end;
end;

/*-------------*/
/* ТОЧКА ВХОДА */
/*-------------*/
// DEF-30750 Не обновляется статус события выгрузки проводки
// параметр p_jmsmessageid и так передается из QueueBO
macro AddAccountingEntriesResponseMsg(p_income_data, p_log_id, p_jmsmessageid)
   var v_ret = true;
   var v_income_data;
   var v_cntr_trn;//, v_cntr_entry;
   var v_logger_obj;
   var v_log_err = "";
   var v_aux_buf;
   var v_email_processor = c_email_proc_env();

   v_email_processor.m_set_msg_head(CV_EMAIL_HEAD);

   v_logger_obj = uws_exchng_logger();
   v_logger_obj.set_log_id(p_log_id);

   /* Получаем проверенный объект со входными данными */
   v_income_data = c_AddAccountingEntriesResponseMsg(p_income_data);


   /* Цикл по списку транзакций */
   v_cntr_trn = 0;
   while(v_income_data.AddAccountingEntriesResponse.TransactionResultList.size > v_cntr_trn)

      /* Цикл по списку проводок */
      m_entry_list_proc(v_income_data.AddAccountingEntriesResponse.TransactionResultList.value(v_cntr_trn).EntryResultList,
                        @v_email_processor,
                        @v_log_err,
                        p_log_id,
                        p_jmsmessageid);

      v_cntr_trn = v_cntr_trn + 1;
   end;

   /* Если были какие-то ошибки - сохраняем их в лог */
   v_aux_buf = strlen(v_log_err);
   if(v_aux_buf > 0)
      if(v_aux_buf >= 2000)
         v_log_err = substr(v_log_err, 1, 1999);
      end;
      v_logger_obj.add_error_info(GC_ADD_DEV_ERR_CODE, v_log_err);
   end;
   AddLog_it("AddAcc~"+p_jmsmessageid+"~11. "+v_log_err);

   /* Сохраняем текст письма для отправки плановой процедурой */
   v_email_processor.m_save_to_submit_grp(CV_EMAIL_GRP_ENTRY_RESP, true);
   /* Отправляем все не отправленные письма */
   v_email_processor.m_submit_email_synch();

   v_logger_obj = NULL;
   v_email_processor = NULL;

   return v_ret;
end;