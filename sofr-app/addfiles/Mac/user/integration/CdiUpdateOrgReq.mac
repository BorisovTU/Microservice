/** 
 @file CdiUpdateOrgReq.mac
 @brief Cервис обновления карточки организации в CDI
  
 # tag 
 - functional_block:Клиенты_Регистрация_на_бирже
 - code_type:API
 - CDI

   
 # changeLog 
 |date       |author         |tasks            |note                                                         
 |-----------|---------------|-----------------|--------------------------------
 |20.09.2023 |Гераськина Т.В.|BIQ-10268        | Создание
 |31.10.2023 |Гераськина Т.В.|DEF-54193        | Если входящий класс не создан, то возвращается ошибка, а не RunError
 |07.11.2023 |Гераськина Т.В.|DEF-55175        | Корректно обрабатывается блок ErrorList ответа
 |09.11.2023 |Гераськина Т.В.|DEF-55343        | Для ExtId добавлен SourceSystemId
 |13.11.2023 |Гераськина Т.В.|DEF-55749        | Исправлено деление ИНН на ИНН и КПП
 |17.11.2023 |Гераськина Т.В.|DEF-55758        | Добавлена начальная инициализация всем переменных в верхнем классе и Party
 |28.11.2023 |Гераськина Т.В.|DEF-55464        | При ошибках отправки запроса (запрос сформирован, но не получен ответ) устанавливается статус 1005 (повторить)
 |04.12.2023 |Гераськина Т.В.|DEF-55758        | Добавлена начальная инициализация параметрам PODFTInformation, изменен код страны
 |20.12.2023 |Гераськина Т.В.|DEF-58099        | Маппинг пустых полей для CDIUpdate
 |26.12.2023 |Гераськина Т.В.|DEF-59120        | Приведены в соответствие CdiCreate и CdiUpdate
*/ 
import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import "RequestWS.mac";                   /* Функция передачи запроса в Web Sphere */
import "uws_exchng_log.mac";              /* Функционал логирования */
import func_lib;
import global_classes;
import "CDIOrg_lib.mac";

/**
@brief ОКВЭД
*/
class c_RelOkved_UpdateOrg
   var MainOkved   : string;
   var ValidFrom   : datetime;
   var ValidTo     ;
   var Active      :string;
   var LastUpdateDate :datetime;
   var SourceSystemCode;
   var SourceSystemId;
   var PartyId;
   var OKVEDCode;
   var RelOkvedId;
   var SourceSystemPartyId;
end;

/**
@brief Кросс-ссылка
*/
class c_ExtId_UpdateOrg()
   var Active            : string;     // Признак активности записи                 
   var LastUpdateDate      : datetime; // Дата и время последнего изменения записи  Да
   var IsExtIdActive       : bool;     // Признак активной                          Нет
   var StartDate           : datetime; // Дата начала действия кросс-ссылки         Нет
   var EndDate             : datetime; // Дата окончания действия кросс-ссылки      Нет
   var ExtIdSourceSystemName  :string; // Наименование системы источника            Нет
   var ExtIdId             ;           // Идентификатор в CDI                       Нет    Идентификатор
   var ExtIdPartyId        ;           // Идентификатор субъекта в CDI              Нет    Идентификатор
   var ExtIdPartySourceId  ;           // Идентификатор субъекта в источнике        Да    Идентификатор
   var ExtIdSourceSystem   ;           // Код системы источника                     Да    Справочник
   var SourceSystemCode;
   var SourceSystemId;
   var SourceSystemPartyId;
end;

/**
@brief Субъект 

Отличается от Party в GetOrg - все Is.. bool заменены на string
*/
private class c_Party_UpdateOrg(IncomeData)
   var Active                       : string    = "YES";   // Признак активности записи                       Да 
   var LastUpdateDate               : datetime    = dttm(date(),time()); // Дата и время последнего изменения записи        Да 
   var Vip                          : string    = "NULL";   // Признак вип-клиента                             Нет 
   var ShortNameOrg                 : string    = "";   // Краткое наименование                            Нет 
   var FullNameOrg                  : string    = "";   // Полное наименование                             Нет 
   var ShortNameOrgLat              : string    = "";   // Краткое латинское наименование                  Нет 
   var FullNameOrgLat               : string    = "";   // Полное латинское наименование                   Нет 
   var ResidentCurrency             : string    = "NULL";   // Признак резидента по валютному законодательству Нет 
   var ResidentTax                  : string    = "";   // Признак налогового резидента                    Нет 
   var ProblemFactor                : string    = "NULL";   // Есть ФП (фактор проблемности)                   Нет 
   var EssentialProblemFactor       : string    = "NULL";   // Есть СФП (существенный фактор проблемности)     Нет 
   var DealProblemFactor            : string    = "NULL";   // Есть ФПЗ (фактор проблемной задолженности)      Нет 
   var Problem                      : string    = "NULL";   // Признак проблемного                             Нет 
   var EmployeesCount               : string    = "NULL";   // Численность работников                          Нет 
   var INN                          : string    = "";   // ИНН                                             Нет 
   var KPPRegistrationPlace         : string    = "";   // КПП по месту регистрации                        Нет 
   var KPPMajorTaxpayer             : string    = "NULL";   // КПП крупнейшего налогоплательщика               Нет 
   var OGRN                         : string    = "";   // ОГРН/ОГРНИП                                     Нет 
   var KIO                          : string    = "NULL";   // КИО                                             Нет 
   var OKPO                         : string    = "";   // ОКПО                                            Нет 
   var RegistrationDate             : date    = date(11,11,1911);     // Дата регистрации (до ОГРН/ОГРНИП)               Нет 
   var CmoLiquidationDate           : date    = date(11,11,1911);     // Дата ликвидации                                 Нет 
   var RegistrationNumber           : string    = "NULL";   // Регистрационный номер (до ОГРН/ОГРНИП)          Нет 
   var OGRNRegistrationDate         : date    = date(11,11,1911);     // Дата регистрации ОГРН                           Нет 
   var FLChPRegistrationNumber      : string    = "NULL";   // Регистрационный номер ФЛЧП                      Нет 
   var FLChPRegistrationDate        : date    = date(11,11,1911);     // Дата регистрации ФЛЧП                           Нет 
   var FLChPRegistrationStructure   : string    = "NULL";   // Регистрирующий орган ФЛЧП                       Нет 
   var NonResidentRegistrationNumber: string    = "";   // Регистрационный номер нерезидента               Нет 
   var NonResidentRegistrationDate  : date    = date(11,11,1911);     // Дата регистрации нерезидента                    Нет 
   var StrategicOrg               : string    = "NULL";     // Признак стратегического общества                Нет 
   var GozOrg                     : string    = "NULL";     // Признак клиент ГОЗ                              Нет 
   var SelfRegulating             : string    = "NULL";     // Признак СРО (саморегулируемой организации)      Нет 
   var ApkExporter                : string    = "NULL";     // Признак экспортер АПК (агропромышленного комплекса)      Нет 
   var VedMember                  : string    = "NULL";     // Признак участник ВЭД (внешнеэкономической деятельности)  Нет 
   var Exporter                   : string    = "NULL";     // Признак экспортера                              Нет 
   var Importer                   : string    = "NULL";     // Признак импортера                               Нет 
   var Mfh                        : string    = "NULL";     // Признак МФХ (малой формы хозяйствования)        Нет 
   var CreditHistory              : string    = "NULL";     // Код субъекта кредитной истории                  Нет 
   var SavingAccount              : string    = "NULL";     // Признак наличия категории накопительный счет    Нет 
   var SourceSystemCode;
   var SourceSystemPartyId;
   var PartyId                   = c_IntegrSymId;              // Идентификатор субъекта в CDI                    Да   Идентификатор
   var PartyType                 = c_IntegrDictRec;              // Тип субъекта                                    Нет  Справочник
   PartyType.RecordCode = "C";
   var PartyStatus               = c_IntegrDictRec;              // Статус субъекта                                 Нет  Справочник
   PartyStatus.RecordCode = "LEAD";
   var OrgType                   = c_IntegrDictRec;              // Вид предприятия                                 Нет  Справочник
   OrgType.RecordCode = "NULL";
   var FinalSegment              = c_IntegrDictRec;              // Итоговый сегмент                                Нет  Справочник
   FinalSegment.RecordCode = "NULL";
   var GksSegment                = c_IntegrDictRec;              // Сегмент по ГКС                                  Нет  Справочник
   GksSegment.RecordCode = "NULL";
   var IndividualSegment         = c_IntegrDictRec;              // Индивидуальный сегмент                          Нет  Справочник
   IndividualSegment.RecordCode = "NULL";
   var ResponsibilityZone        = c_IntegrDictRec;              // Зона ответственности бизнес-ССП ГО              Нет  Справочник
   ResponsibilityZone.RecordCode = "";
   var SeparatedSubjects         = c_IntegrDictRec;              // Отдельные субъекты хозяйствования               Нет  Справочник
   SeparatedSubjects.RecordCode = "NULL";
   var AffiliationToProjectsOrg  = c_IntegrDictRec;              // Принадлежность к проектным компаниям            Нет  Справочник
   AffiliationToProjectsOrg.RecordCode = "NULL";
   var SmallMiddleOrg            = c_IntegrDictRec;              // Реестр субъектов малого и среднего предпринимательства    Нет Справочник
   SmallMiddleOrg.RecordCode = "NULL";
   var ResponsibleService        = c_IntegrDictRec;              // Ответственная служба                            Нет  Справочник
   ResponsibleService.RecordCode = "NULL";
   var OKFS                      = c_IntegrDictRec;              // ОКФС                                            Нет  Справочник
   OKFS.RecordCode = "";
   var OKOGU                     = c_IntegrDictRec;              // ОКОГУ                                           Нет  Справочник
   OKOGU.RecordCode = "";
   var OKTMO                     = c_IntegrDictRec;              // ОКТМО                                           Нет  Справочник
   OKTMO.RecordCode = "";
   var OKATO                     = c_IntegrDictRec;              // ОКАТО                                           Нет  Справочник
   OKATO.RecordCode = "NULL";
   var OKOPF                     = c_IntegrDictRec;              // ОКОПФ                                           Нет  Справочник
   OKOPF.RecordCode = "";
   var DiasoftCategory           = c_IntegrDictRec;              // Категория Диасофт                               Нет  Справочник
   DiasoftCategory.RecordCode = "NULL";
   var ResidenceCountry          = c_IntegrDictRec;              // Юрисдикция                                      Нет  Справочник
   ResidenceCountry.RecordCode = "NULL";
   var AuthorizedCapital         ;              // Уставной капитал                                Нет 
   var PODFTInformation          = c_PODFTInformation;              // Информация ПОД/ФТ                               Нет 
   PODFTInformation.PODFTFormCreateDate = date(11,11,1911);     // Дата формирования анкеты по ПОД/ФТ  Нет
   PODFTInformation.PODFTFormUpdateDate = date(11,11,1911);     // Дата обновления анкеты по ПОД/ФТ    Нет
   PODFTInformation.FATCAStatusDate     = date(11,11,1911);     // Дата статуса FATCA                  Нет
   PODFTInformation.CRSStatusDate       = date(11,11,1911);     // Дата статуса CRS                    Нет
   PODFTInformation.FATCAStatus      = c_IntegrDictRec;              // Статус FATCA                        Нет      Справочник
   PODFTInformation.FATCAStatus.RecordCode = "NULL";
   PODFTInformation.CRSStatus        = c_IntegrDictRec;              // Статус CRS                          Нет      Справочник
   PODFTInformation.CRSStatus.RecordCode = "NULL";
   var MainSolveOption           = c_IntegrDictRec;              // Основной вариант урегулирования                 Нет  Справочник
   MainSolveOption.RecordCode = "NULL";
end;


/**
@brief Класс для сервиса CdiUpdateOrgReq
@param[in] p_partyid dparty_dbt.t_partyid

Через uInitPrime заполняются данные в соответствии с маппингом значений для XML CdiUpdateOrgReq
*/
class c_CdiUpdateOrgReq(p_partyid, p_IsPublishResponse, p_notCheckCDICode) 
   var IsPublishResponse: bool;  // Изменения, которые произошли в системе после обработки запроса сервиса публикации 
   var Party;                    // Субъект                                   Контейнер   Нет
   var RelBranchToPartyList;     // Список связей субъект-подразделение       Контейнер   Нет
   var RelGroupList        ;             // Список связи субъектов и группы компаний  Контейнер   Нет
   var RelOkvedList        ;             // Список связи субъектов и ОКВЭД            Контейнер   Нет
   var RelLicenseList      ;           // Список связи субъектов и лицензий         Контейнер   Нет
   var BankRelList         ;              // Список характеры отношений с Банком       Контейнер   Нет
   var ContactList         ;              // Список контактов                          Контейнер   Нет
   var AddressList         ;              // Список адресов                            Контейнер   Нет
   var VerificationList    ;         // Список проверок                           Контейнер   Нет
   var StateList           ;                // Список состояний и стадий                 Контейнер   Нет
   var ExtIdList           ;                // Список кросс-ссылок                       Контейнер   Нет
   var RelPartyToPartyList ;      // Список связанных лиц                      Контейнер   Нет
   
   macro uInitPrime(p_partyid, p_IsPublishResponse, p_notCheckCDICode)
      var vParty : RsbParty;
      var temp_value;
      record fininstr(fininstr);
      var sql_str, cmd, rs;
      var pos = 0;
      var Code_ABS, Code_CDI;
      var vPartyType;
      var IsClient, error;
      var vPartyName;
      
      if (ValType(p_IsPublishResponse) == V_BOOL)
         IsPublishResponse = p_IsPublishResponse;
      else 
         IsPublishResponse = false;
      end;

      if (ValType(p_notCheckCDICode) == V_BOOL)
      else 
         p_notCheckCDICode = false;
      end;

      vParty = RsbParty(p_partyid);
      
      Party = c_Party_UpdateOrg;

      //CCBO-10808 не все субъекты выгружаются
      if (CheckBlockUnloadCDI(p_partyid)) 
         error = ERROR_DESC_BLOCK_UNLOAD;
         RunError(error, c_err_obj(error,ERROR_CODE_BLOCK_UNLOAD));
      end;
      
      Party.LastUpdateDate = dttm(date(), time());
      Party.FullNameOrg = vParty.FullName;
      Party.ShortNameOrg = vParty.ShortName;
      if (vParty.IsNotResident)
         Party.ResidentTax = "NO";
         Party.ResidenceCountry.RecordCode = vParty.NRCountry;
      else
         Party.ResidentTax = "YES";
         Party.ResidenceCountry.RecordCode = "RUS";
      end;
      // не буквенный, а числовой
      sql_str = "select t_codenum3 from dcountry_dbt where t_CodeLat3 = '"+Party.ResidenceCountry.RecordCode+"'";
      rs = RSDRecordSet(sql_str);
      if (rs.movenext)
         Party.ResidenceCountry.RecordCode = rs.value("t_codenum3");
      end;

      vPartyName = GetPartyName(p_partyid, PTKN_LATSHORTNAME);
      if (vPartyName != "-1");
         Party.ShortNameOrgLat = vPartyName;
      end;
      vPartyName = GetPartyName(p_partyid, PTKN_LATFULLNAME);
      if (vPartyName == "-1");
         vPartyName = GetPartyName(p_partyid, PTKN_FOREIGNNAME);
      end;
      if (vPartyName != "-1");
         Party.FullNameOrgLat = vPartyName;
      end;

      Party.OKPO = vParty.OKPO;

      if (vParty.LegalForm == 1) // для ФЛ ИП капитал не задан
         if (vParty.RealCapital > 0)
            Party.AuthorizedCapital = c_AuthorizedCapital_FOR_CDI;
            Party.AuthorizedCapital.Value = vParty.RealCapital;
            Party.AuthorizedCapital.Currency = c_IntegrDictRec;
            Party.AuthorizedCapital.Currency.RecordCode = "";
            if (ПолучитьФинИн(vParty.CapitalFI, fininstr) == 0)
               Party.AuthorizedCapital.Currency.RecordCode = fininstr.iso_number;
            end;
         end;
      end;

      // коды
      sql_str = "select t_codekind, t_code, t_name, t_begindate from dRepPartyCodes_vw where t_partyid = :partyid ";
      cmd = RSDCommand(sql_str);
      cmd.AddParam("partyid", RSDBP_IN, p_partyid);
      rs = RSDRecordSet(cmd);
      while (rs.movenext)
         if (rs.value("t_codekind") == PTCK_INN)
            // DEF-55749 
            temp_value = rs.value("t_code");
            SplitFullINN (temp_value, Party.INN, Party.KPPRegistrationPlace); 
         /*elif (rs.value("t_codekind") == PTCK_FOREIGN_INN)
            Party.KIO = rs.value("t_code");*/
         elif (rs.value("t_codekind") == 27)
            Party.OGRN = rs.value("t_code");
            //Party.OGRNRegistrationDate = rs.value("t_begindate");
         elif (rs.value("t_codekind") == 33)
            Party.NonResidentRegistrationNumber = rs.value("t_code");
         elif (rs.value("t_codekind") == PARTY_CODEKIND_CDI)
            Code_CDI = rs.value("t_code");
            Party.PartyId = c_IntegrSymId;
            Party.PartyId.ObjectId = Code_CDI;
         elif (rs.value("t_codekind") == PARTY_CODEKIND_ABS)
            Code_ABS = rs.value("t_code");
         end;
      end;

      if ( (Valtype(Code_CDI) == V_UNDEF) and (not p_notCheckCDICode) )
         error = "Не задан код CDI у субъекта "+p_PartyId;
         RunError(error, c_err_obj(error,ERR_CODE_TECH_ERROR));
      end;

      RelOkvedList = TArray;
      // категории
      sql_str = "select c.t_groupid, c.t_attrid, c.t_general, a.t_numinlist, a.t_nameobject, a.t_name, a.t_fullname, c.t_validfromdate, c.t_id "+
                "  from dobjatcor_dbt c "+
                "  inner join DOBJATTR_DBT a "+
                "     on a.t_groupid = c.t_groupid and  a.t_attrid = c.t_attrid  and  a.t_objecttype = c.t_objecttype "+
                "  where lpad(:partyid, 10, '0')=c.t_object and a.t_objecttype = 3 "+
                "    and c.t_validtodate = to_date('31129999','ddmmyyyy')";
      cmd = RSDCommand(sql_str);
      cmd.AddParam("partyid", RSDBP_IN, p_partyid);
      rs = RSDRecordSet(cmd);
      while (rs.movenext)
         if (rs.value("t_groupid") == CONTRACT_CATEGORY_CDI_PARTYTYPE)
            Party.PartyType = c_IntegrDictRec;
            Party.PartyType.RecordCode = rs.value("t_numinlist");
         elif (rs.value("t_groupid") == 27)
            Party.OKFS = c_IntegrDictRec;
            Party.OKFS.RecordCode = rs.value("t_numinlist");
         elif (rs.value("t_groupid") == 48)
            Party.OKOGU = c_IntegrDictRec;
            Party.OKOGU.RecordCode = rs.value("t_numinlist");
         elif (rs.value("t_groupid") == 55)
            Party.OKTMO = c_IntegrDictRec;
            Party.OKTMO.RecordCode = rs.value("t_numinlist");
         elif (rs.value("t_groupid") == 12)
            Party.OKATO = c_IntegrDictRec;
            Party.OKATO.RecordCode = "NULL"; //= rs.value("t_numinlist"); DEF-82296
         elif (rs.value("t_groupid") == 53)
            Party.OKOPF = c_IntegrDictRec;
            Party.OKOPF.RecordCode = StrSubst(rs.value("t_nameobject")," ","");
            if (Substr(rs.value("t_nameobject"),1,4) == "5 01")
               vPartyType = "IE";
            elif (Substr(rs.value("t_nameobject"),1,4) == "5 02")
               vPartyType = "PP";
            else 
               vPartyType = "P";
            end;
         elif (rs.value("t_groupid") == 64)
            temp_value = c_RelOkved_updateOrg;
            if (rs.value("t_general") == "X")
               temp_value.MainOkved = "YES";
            else 
               temp_value.MainOkved = "NO";
            end;
            temp_value.ValidFrom = dttm(rs.value("t_validfromdate"));
            temp_value.ValidTo = dttm(date(11,11,1911), time(11,11,11));
            temp_value.Active        = "YES";
            temp_value.LastUpdateDate  = dttm(date(),time());
            temp_value.SourceSystemCode = c_IntegrDictRec;
            temp_value.SourceSystemCode.RecordCode = NAME_SYSTEM;
            temp_value.SourceSystemId = c_IntegrSymId;
            temp_value.SourceSystemId.Objectid = rs.value("t_id");
            temp_value.PartyId = c_IntegrSymId;
            temp_value.PartyId.ObjectId = Code_CDI;
            temp_value.OKVEDCode = c_IntegrDictRec;
            temp_value.OKVEDCode.RecordCode = rs.value("t_numinlist");
            temp_value.RelOkvedId = c_IntegrSymId;
            temp_value.RelOkvedId.Objectid = "NULL";
            temp_value.OKVEDCode.RecordTitle = rs.value("t_fullname");
            temp_value.SourceSystemPartyId = c_IntegrSymId;
            temp_value.SourceSystemPartyId.Objectid = p_PartyID;

            RelOkvedList.value(RelOkvedList.size) = temp_value;
            temp_value = NULL;
         elif (rs.value("t_groupid") == 116)
            Party.ResponsibilityZone = c_IntegrDictRec;
            Party.ResponsibilityZone.RecordCode = rs.value("t_numinlist");
         end;
      end;
      
      if (RelOkvedList.size == 0)
         RelOkvedList = NULL;
      end;
      
      Party.SourceSystemCode = c_IntegrDictRec;
      Party.SourceSystemCode.RecordCode = NAME_SYSTEM;
      Party.SourceSystemPartyId = c_IntegrSymId;
      Party.SourceSystemPartyId.Objectid = p_PartyID;
      Party.PartyId = c_IntegrSymId;
      Party.PartyId.ObjectId = Code_CDI;
      
      if ( Party.PartyType.RecordCode == "NULL") 
         Party.PartyType = c_IntegrDictRec;
         if (vParty.LegalForm == 1)
            Party.PartyType.RecordCode = "C";
         else 
            Party.PartyType.RecordCode = vPartyType;
         end;
      end;
      
      // признак клиента
      IsClient = ВидСубъекта(p_PartyID, PTK_CLIENT, error, {OperDprt}); // является ли субъект клиентом 
      Party.PartyStatus = c_IntegrDictRec;
      if (IsClient)
         Party.PartyStatus.RecordCode = "Client";
      else
         Party.PartyStatus.RecordCode = "Lead";
      end;

      AddressList = TArray;
      temp_value = FormAddressObj(vParty, 1, Code_CDI, "UPDATE"); // юридический
      if (ValType(temp_value) == V_GENOBJ)
         AddressList.value(AddressList.size) = temp_value;
      end;
      temp_value = FormAddressObj(vParty, 2, Code_CDI, "UPDATE"); // фактический
      if (ValType(temp_value) == V_GENOBJ)
         AddressList.value(AddressList.size) = temp_value;
      end;
      temp_value = FormAddressObj(vParty, 3, Code_CDI, "UPDATE"); // почтовый
      if (ValType(temp_value) == V_GENOBJ)
         AddressList.value(AddressList.size) = temp_value;
      end;
      temp_value = FormAddressObj(vParty, 5, Code_CDI, "UPDATE"); // Место нахождения (по учр.документам)
      if (ValType(temp_value) == V_GENOBJ)
         AddressList.value(AddressList.size) = temp_value;
      end;
      
      if (AddressList.size == 0)
         AddressList = NULL;
      end;

      ContactList = TArray;  
      var PartyContact = vParty.PartyContact();
      if(PartyContact.First())
        temp_value = CDI_FillContact( PartyContact, Code_CDI, p_PartyID, "UPDATE");
        if (ValType(temp_value) == V_GENOBJ)
           ContactList.value(ContactList.size) = temp_value;
        end;
        while(PartyContact.Next())
          temp_value = CDI_FillContact( PartyContact, Code_CDI, p_PartyID, "UPDATE" );
          if (ValType(temp_value) == V_GENOBJ)
             ContactList.value(ContactList.size) = temp_value;
          end;
        end;
      end;
      if (ContactList.size == 0)
         ContactList = NULL;
      end;

    onError(err)
       var err_code, err_txt;  
       // ошибки могут быть при создании, будет передавать через Party.PartyId 
       err_code = c_err_obj.obtain_err_code(err);
       err_txt = c_err_obj.obtain_err_msg(err);

       Party.PartyId = c_IntegrSymId;
       Party.PartyId.ObjectId = "--1";
       Party.PartyId.SystemId = err_code;
       Party.PartyId.SystemNodeId = err_txt;
   end;
   
   uInitPrime(p_partyid, p_IsPublishResponse, p_notCheckCDICode)
end;


/**
@brief Элемент обработки отдельной записи исходного сообщения
*/
class c_Record_CdiUpdateOrgResp
   var Entity        : string;   // Сущность, к которой относится элемент исходного сообщения      Да
   var RecordChange  : string;   // Тип изменения                                                  Да
   var SourceId      ;           // Идентификатор на источнике записи из исходного сообщения       Да    Идентификатор
   var SourceSystem  ;           // Код системы источника                                          Да    Справочник
   var Id            ;           // Идентификатор, присвоенный записи в CDI                        Да    Идентификатор
end;


/**
@brief Класс ответа CdiUpdateOrgResp
@param[in] IncomeData входящий ответ

В текущей реализации ответ только сохраняется в логах, но никак не используется
*/
private class adp_CdiUpdateOrgResp(IncomeData)
   var RecordList;   // Список статусов обработки исходных записей
   var ErrorList;    // Блок ошибок обработки
   
   /* Кратность параметров в соответствии со схемой/спецификацией */
   private macro uInitPrime(IncomeData)
      var err_txt;
      var aux_buff, aux_buff2, ai;
      var temp_val;
            
      var nametag_up = "CdiUpdateOrgResp";
      var nametag_up2 = "";
      
      if(ValType(IncomeData) != V_UNDEF)
         aux_buff = uTryToGetPropS(IncomeData, "RecordList");
         if(ValType(aux_buff) == V_GENOBJ)
            RecordList = TArray;
            ai = 0;
            nametag_up2 = nametag_up+".RecordList";
            while(IncomeData.RecordList.size > ai)
               temp_val = c_Record_CdiUpdateOrgResp;
               temp_val.Entity        = getValueFromProperty(aux_buff.value(ai), "Entity", nametag_up2, true);
               temp_val.RecordChange  = getValueFromProperty(aux_buff.value(ai), "RecordChange", nametag_up2, true);
               
               temp_val.SourceId      = getSymbolFromProperty(aux_buff.value(ai), "SourceId", nametag_up2, true);
               temp_val.Id            = getSymbolFromProperty(aux_buff.value(ai), "Id", nametag_up2, true);
               
               temp_val.SourceSystem  = getDictionaryFromProperty(aux_buff.value(ai), "SourceSystem", nametag_up2, true);
               
               RecordList.value(ai) = temp_val;
               ai = ai + 1;
               temp_val = NULL;
            end;
         elif(ValType(aux_buff) == V_UNDEF)
         else
            err_txt = string(InErrNotObj, nametag_up2);
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
         end;

         aux_buff = uTryToGetPropS(IncomeData, "ErrorList");
         if(ValType(aux_buff) == V_GENOBJ)
            ErrorList = TArray;
            ai = 0;
            nametag_up2 = nametag_up+".ErrorList";
            while(IncomeData.ErrorList.size > ai)
               temp_val = c_Error;
               temp_val.ErrorCode = getValueFromProperty(aux_buff.value(ai), "ErrorCode", nametag_up2);
               temp_val.ErrorDesc = getValueFromProperty(aux_buff.value(ai), "ErrorDesc", nametag_up2);
               
               ErrorList.value(ai) = temp_val;
               ai = ai + 1;
               temp_val = NULL;
            end;
         elif(ValType(aux_buff) == V_UNDEF)
         else
            err_txt = string(InErrNotObj, nametag_up2);
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
         end;
      end;
   end;
   
   uInitPrime(IncomeData);
end; 


/**
@brief Основной класс работы с сервисом CdiUpdateOrgReq
@param[in] p_PartyID dparty_dbt.y_partyid
@return Объект со структурой, совпадающей с CdiUpdateOrgReq в XSD

Верхний тег класса должен быть строго один и совпадать с корневым тегом в XSD, иначе невозможно преобразование в XML
*/
class c_CdiUpdateOrgReqProcessor(p_partyid, p_IsPublishResponse)
   var CdiUpdateOrgReq = c_CdiUpdateOrgReq(p_partyid, p_IsPublishResponse);
end; 


/**
@brief Адаптер для вызова сервиса "Запрос сервиса обновления карточки организации"
@param[in] ReqData Сформированный снаружи объект класса c_CdiUpdateOrgReqProcessor
@param[in] p_recid utableprocessevent.t_recid для связки в "Интерфейс таблицы событий"
@return Объект класса c_Error

Считаем, что на вход приходит корректный c_CdiUpdateOrgReqProcessor
*/
macro run_CdiUpdateOrgReq(ReqData, p_recid)
   var RespData = NULL;
   var err_txt = NULL, err_code = 0;
   var res = c_Error;
   
   private var RequestText;
   private var v_transformator;
   private var v_answer;
   private var v_answer_data;
   private var v_logger_obj;
   private var v_log_id;
   private var xml_str;
   private var v_xml_rpc_reqid = XmlRpcRequestId();
   private var temp_error;

   private var V_WS_TYPE = GetNumDataType("CdiUpdateOrg");
   res = c_Error;
   res.ErrorCode = -1;
   res.ErrorDesc = "Неизвестная ошибка";

   if ( (ReqData == NULL) or (ValType(ReqData) == V_UNDEF) )
      return null;
   end;

   v_transformator = c_secur_msg_converter();

   RequestText = v_transformator.m_secur_internal_resp(ReqData);
   
   v_logger_obj = uws_exchng_logger(null, null, v_xml_rpc_reqid, "CdiUpdateOrgReq", modulename(), p_recid, RequestText);
   v_log_id = v_logger_obj.get_log_id();

   if (err_txt == NULL)
      v_answer = SubmitRequestToWS( 2,          // ИД очереди.
                                    "",         // ИД сообщения.
                                    "",         // Идентификатор Бэк Офиса. (не обрабатывается).
                                    true,       // Признак синхронной обработки. Если не указан, = False
                                    "SOFR",     // Подразделение системы-отпр
                                    V_WS_TYPE,  // ИД типа данных 
                                    RequestText,// Бизнес данные в виде XML
                                    v_log_id,   // ID usr_exchng_log
                                    "CDI"
                                   );

      if (v_answer.ResultCode == 0)
         xml_str = v_answer.responsetext;
         v_logger_obj.add_response(xml_str); 
         
         v_transformator = NULL;
         if (xml_str)
            /* Преобразуем запрос в объектный вид - begin */
            v_transformator = c_secur_msg_converter();
            if(not v_transformator.m_decode_escaped_char(xml_str))
               RunError(v_transformator.get_service_msg(), c_usr_err_obj(v_transformator.get_service_msg()));
            end;
            v_answer_data = v_transformator.m_secur_wol_internal_req(v_transformator.get_pure_msg());
            /* Преобразуем запрос в объектный вид - end */

            if(ValType(v_answer_data) == V_UNDEF)
               err_txt = "Не удалось преобразовать XML в объект";
               RunError(err_txt, c_usr_err_obj(err_txt)); 
            end;
            
            /* Преобразование объекта к виду, ожидаемому Системой */
            RespData = adp_CdiUpdateOrgResp(v_answer_data);

            res.ErrorCode = 0;
            res.ErrorDesc = "OK";

            for (temp_error, RespData.ErrorList)
               if (temp_error.ErrorCode > 0)  
                  res.ErrorCode = temp_error.ErrorCode;
                  res.ErrorDesc = temp_Error.ErrorDesc;
                  break;
               end;
            end;
            
         end;
      else 
         v_logger_obj.add_error_info(v_answer.ResultCode, v_answer.ResultText);
         res.ErrorCode = 1005; // будем считать, что в любом случае, когда не получен ответ, стоит повторить  
         res.ErrorDesc = v_answer.ResultText;
      end;

   else
      v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, err_txt);
      res.ErrorCode = UNIVERSAL_ERROR_CODE; // если не смогли сформировать запрос, повторять не стоит
      res.ErrorDesc = err_txt;
   end;

   return res;

onError(err)
   err_code = c_err_obj.obtain_err_code(err);
   err_txt = c_err_obj.obtain_err_msg(err);

   v_logger_obj.add_error_info(err_code, err_txt);

   res = c_Error;
   res.ErrorCode = err_code;  // здесь глобальная ошибка, повторять не будем
   res.ErrorDesc = err_txt;
   return res;
end; 


/**
@brief Точка входа "Запрос сервиса обновления карточки организации"
@param[in] ReqData Сформированный снаружи объект класса c_CdiUpdateOrgReqProcessor
@param[in] in_recid utableprocessevent.t_recid для связки в "Интерфейс таблицы событий"
@return Объект класса c_Error

Считаем, что на вход приходит корректный c_CdiUpdateOrgReqProcessor
*/
macro CdiUpdateOrgReq (ReqData, in_recid) 
   return run_CdiUpdateOrgReq(ReqData, in_recid); 
end;

