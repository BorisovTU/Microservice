/*---------------------------------------------------------------------------------------------*/
/*  ManualPushUnload.mac  - Интерфейс для выгрузки объектов по пачке, отбираются:  
                             - новые/изменившиеся счета, которые участвуют в       
                               проводке за даты по пачке, когда было изменение счета - неважно
                             - проводки с указанной пачкой за даты
                             - платежи с указанной пачкой за даты                              */
/*                                                                                             */
/* Автор: Гераськина Т.                                                                        */
/*---------------------------------------------------------------------------------------------*/
import RsbFormsInter, CTInter;
import "usr_key_const.mac";
import globals, rsd;
import "global_utils_intgr.mac";
import "int_usr_globals.mac";

private var gv_evnt_sel_prm;               /* Параметры выборки событий */
private var gv_com_unload_form;
private var gv_result_prm;
private var gv_com_result_form;


private macro FillTempTable(_objecttype)
   var sql_str, rs, cmd;
   var vguid, vcnt;
   var vbegdate, venddate;

   if (gv_evnt_sel_prm.v_com_date_switch)
      vbegdate = gv_evnt_sel_prm.v_com_beg_date;
      venddate = gv_evnt_sel_prm.v_com_end_date;
   else
      vbegdate = gv_evnt_sel_prm.v_com_date;
      venddate = gv_evnt_sel_prm.v_com_date;
   end;

   if (_objecttype == OBJTYPE_ACCOUNT)
      sql_str = "begin :res := u_manual_drive.prepare_accounts(:p_begdate, :p_enddate, :p_numberpack, :p_oper, :p_mac, :vcnt); end;";
   elif (_objecttype == OBJTYPE_DOCUMENT)
      sql_str = "begin :res := u_manual_drive.prepare_trans(:p_begdate, :p_enddate, :p_numberpack, :p_oper, :p_mac, :vcnt); end;";
   elif (_objecttype == OBJTYPE_PAYMENT)
      sql_str = "begin :res := u_manual_drive.prepare_payments(:p_begdate, :p_enddate, :p_numberpack, :p_oper, :p_mac, :vcnt); end;";
   end;

   cmd = RSDCommand(sql_str);
   cmd.AddParam("res", RSDBP_RETVAL, V_STRING);
   cmd.AddParam("p_begdate", RSDBP_IN, vbegdate);
   cmd.AddParam("p_enddate", RSDBP_IN, venddate);
   cmd.AddParam("p_numberpack", RSDBP_IN, gv_evnt_sel_prm.v_com_number_pack);
   cmd.AddParam("p_oper", RSDBP_IN, {oper});
   cmd.AddParam("p_mac", RSDBP_IN, "");
   cmd.AddParam("vcnt", RSDBP_OUT, V_INTEGER);
   cmd.execute();

   gv_evnt_sel_prm.v_guid = cmd.value("res");
   gv_evnt_sel_prm.v_cnt = cmd.value("vcnt");

   cmd.close;
   cmd = null; sql_str = null;
onerror(e)
   RunError(e.message + getFullCmdErrorText(cmd));
end;


private macro RunTempTable()
   var sql_str, rs, cmd;
   var vguid, vcnt;

   sql_str = "begin u_manual_drive.push_records_from_temptable; end;";
   cmd = RSDCommand(sql_str);
   cmd.execute();

   cmd.close;
   cmd = null; sql_str = null;

onerror(e)
   RunError(e.message + getFullCmdErrorText(cmd));
end;


macro MarkAllRecordsOK(_objecttype, _guid)
   var sql_str, cmd;
   sql_str = "update u_unload_bisquit "+
             "   set resultcode = 0, err = 'OK' "+
             " where  t_objecttype = :pobjecttype and SESSION_GUID = :pguid ";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("pobjecttype", RSDBP_IN, _objecttype);
   cmd.AddParam("pguid", RSDBP_IN, _guid);
   cmd.execute();
   cmd.close; cmd = null;
onerror(e)
   RunError(e.message + getFullCmdErrorText(cmd));
end;


private macro RunCheckResult(_objecttype)
   private var gv_column_list_scr = TArray(); /* Список колонок скроллинга */
   private var gv_num_columns_scr = 0;        /* Количество колонок скроллинга */
   private var gv_scr_focus = 0;              /* Номер позиции скроллинга */

   var sql_str, rs, cmd;
   var sql_include_error = "";
   var sql_sort_error = "";

   var sql_scr, rs_scr, cnt;

   BegAction(2000, "Сбор информации");

   sql_str = "begin u_manual_drive.check_results(:p_guid, :p_objecttype); end;";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("p_guid", RSDBP_IN, gv_evnt_sel_prm.v_guid);
   cmd.AddParam("p_objecttype", RSDBP_IN, _objecttype);
   cmd.execute();

   cmd.close;
   cmd = null; sql_str = null;

   if (gv_result_prm.v_com_onlyerror)
      sql_include_error = " and u.resultcode != '0'";
   else
      sql_include_error = "";
      if (gv_result_prm.v_com_errorfirst_switch)
         sql_sort_error = " order by decode(resultcode,'0',1,-1)";
      end;
   end;

   if (_objecttype == OBJTYPE_ACCOUNT)
      sql_scr = "select a.t_account, a.t_chapter, a.t_nameaccount, "+
                "       u.resultcode, u.err, u.t_number_pack       "+
                "  from u_unload_bisquit u,                        "+
                "       daccount_dbt a                             "+
                " where u.session_guid = :p_guid                   "+
                "   and u.t_objecttype = :p_objecttype             "+
                "   and u.t_objectid = a.t_accountid               ";

      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_account",     "Номер счета",        17, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_chapter",     "Гл.",                3 , 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_nameaccount", "Наименование счета", 20, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "resultcode",    "",                    3, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "err",           "Результат",          90, 2); gv_num_columns_scr = gv_num_columns_scr + 1;

   elif (_objecttype == OBJTYPE_DOCUMENT)
      sql_scr = "select a.t_account_payer, a.t_account_receiver, a.t_sum_payer, a.t_sum_receiver, a.t_numb_document, "+
                "       u.resultcode, u.err, u.t_number_pack       "+
                "  from u_unload_bisquit u,                        "+
                "       dacctrn_dbt a                              "+
                " where u.session_guid = :p_guid                   "+
                "   and u.t_objecttype = :p_objecttype             "+
                "   and a.t_state = 1                              "+
                "   and u.t_objectid = a.t_acctrnid                ";

      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_numb_document",    "Номер",        10, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_account_payer",    "Счет дебета",  17, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_account_receiver", "Счет кредита", 17, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_sum_payer",        "Сумма дебета", 16, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_sum_receiver",     "Сумма кредита",16, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "resultcode",         "",              3, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "err",                "Результат",    90, 2); gv_num_columns_scr = gv_num_columns_scr + 1;

   elif (_objecttype == OBJTYPE_PAYMENT)
      sql_scr = "select p.t_payeraccount, p.t_receiveraccount, p.t_amount, p.t_payamount, "+
                "       u.resultcode, u.err, u.t_number_pack                              "+                     
                "  from u_unload_bisquit u,                                               "+                     
                "       dpmpaym_dbt p                                                     "+                     
                " where u.session_guid = :p_guid                                          "+                     
                "   and u.t_objecttype = :p_objecttype                                    "+                       
                "   and u.t_objectid = p.t_paymentid                                      ";

      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_payeraccount",    "Счет дебета",  17, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_receiveraccount", "Счет кредита", 17, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_amount",          "Сумма дебета", 16, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_payamount",       "Сумма кредита",16, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "resultcode",        "",              3, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "err",               "Результат",    90, 2); gv_num_columns_scr = gv_num_columns_scr + 1;

   end;

   EndAction();

   if (sql_scr)
      sql_scr = sql_scr + sql_include_error + sql_sort_error;

      cmd = RSDCommand(sql_scr);
      cmd.AddParam("p_guid", RSDBP_IN, gv_evnt_sel_prm.v_guid);
      cmd.AddParam("objecttype", RSDBP_IN, _objecttype);
      cmd.NullConversion = true;
      rs_scr = RSDRecordSet(cmd, RSDVAL_CLIENT, RSDVAl_STATIC);
      rs_scr.NullConversion = true;
      rs_scr.pagesize = 100;

      while(RunScroll(rs_scr, gv_num_columns_scr, gv_column_list_scr, "ResultRecords", "m_evnt_proc_scr", "Результаты выгрузки", "[ESC] Выход  [Ctrl+Shift+F11] в Excel [Ctrl+Shift+F7] в Текст", true, 5, NULL, NULL, 40))
         rs_scr = RSDRecordSet(cmd, RSDVAL_CLIENT, RSDVAl_STATIC);
      end;
      rs_scr.close; cmd.close; rs_scr = null; cmd = null;
   end;

onerror(e)
   RunError(e.message + getFullCmdErrorText(cmd));
end;


/*------------------------------*/
/* Параметры отбора результатов */
/*------------------------------*/
private class c_result_prm()
   var v_com_errorfirst_switch;

   var v_com_onlyerror;
   var v_com_all;

   /* Значения по умолчанию */
   private macro m_init_result_prm()
      v_com_errorfirst_switch = true;

      v_com_onlyerror = true;
      v_com_all = false;
   end;

   m_init_result_prm();
end; /* class c_result_prm() */


/*----------------------------------------*/
/* Класс формы вывода результатов         */
/*----------------------------------------*/
class (TRsbPanel) c_com_result_form(_longtext, _objecttype)
   const CV_POS_X = 2;
   const CV_POS_Y = 2;
   var SizeLabelx, SizeLabely;

   var v_com_longlabel;
   var v_com_errorfirst_switch_fld;

   var v_com_result_btn;

   var v_com_r_onlyerror;
   var v_com_r_all;

   var v_objecttype = _objecttype;

   macro m_evnt_on_key_pressed(p_rsb_evnt)
      if(p_rsb_evnt.KeyCode == K_F2)
         close(CV_CLOSE_FORM_OK);
      end;

      return true;
   end;


   macro m_on_switch_errorfirst(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_CONTROL_CHECKED)
         gv_result_prm.v_com_errorfirst_switch = v_com_errorfirst_switch_fld.checked;
      end;

      redraw();

      return true;
   end;

   macro m_on_checked_r(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_CONTROL_CHECKED)
         if (v_com_r_onlyerror.checked)
            gv_result_prm.v_com_onlyerror = true;
            gv_result_prm.v_com_all = false;
         elif (v_com_r_all.checked)
            gv_result_prm.v_com_onlyerror = false;
            gv_result_prm.v_com_all = true;
         end;
      end;

      return true;
   end;

   macro m_evnt_result_btn(p_rsb_evnt)

      if(p_rsb_evnt.id == RSB_EV_BUTTON_CLICKED)
         RunCheckResult(v_objecttype);
      end;

      return true;
   end;


   private macro m_init_com_result_form(i_this_obj, _longtext)
      var ai = 0, make_visible = false;
      Caption = "Просмотр результатов";
      Status = "[Esc] Выход ";


      /* Предварительные результаты */
      SizeLabely = 0;
      while (ai < _longtext.size)
         v_com_longlabel = TRsbLabel(CV_POS_X + 3, CV_POS_Y+ai, _longtext.value(ai));
         AddLabel(v_com_longlabel);
         SizeLabely = SizeLabely+ 1;
         ai = ai+1;
      end;

      if (gv_evnt_sel_prm.v_flag_notallsent) 
         make_visible = false;
      else
         make_visible = true;
      end;

      AddLabel(TRsbLabel(CV_POS_X + 4, (CV_POS_Y + SizeLabely + 1), "Параметры вывода отчета о выгрузке"));
      AddLabel(TRsbLabel(CV_POS_X, (CV_POS_Y + SizeLabely + 2), "(просмотр возможен только когда все объекты отправлены)"));


      /* Фильтр по наличию ошибок */
      AddLabel(TRsbLabel(CV_POS_X + 4, (CV_POS_Y + SizeLabely + 3), "только ошибки"));
      v_com_r_onlyerror = TRsbRadioButton();
      v_com_r_onlyerror.setPosition(CV_POS_X+1, (CV_POS_Y + SizeLabely + 3));
      v_com_r_onlyerror.onChecked(R2M(i_this_obj, "m_on_checked_r"));
      v_com_r_onlyerror.checked = true;
      v_com_r_onlyerror.enabled = make_visible;
      AddControl(v_com_r_onlyerror);
      AddLabel(TRsbLabel(CV_POS_X + 4, (CV_POS_Y + SizeLabely + 4), "все результаты"));
      v_com_r_all = TRsbRadioButton();
      v_com_r_all.setPosition(CV_POS_X+1, (CV_POS_Y + SizeLabely + 4));
      v_com_r_all.onChecked(R2M(i_this_obj, "m_on_checked_r"));
      v_com_r_all.enabled = make_visible;
      AddControl(v_com_r_all);

      AddLabel(TRsbLabel((CV_POS_X + 4), (CV_POS_Y + SizeLabely + 5), "ошибки в начале списка"));
      v_com_errorfirst_switch_fld = TRsbCheckBox();
      v_com_errorfirst_switch_fld.setPosition((CV_POS_X + 1), (CV_POS_Y + SizeLabely + 5));
      v_com_errorfirst_switch_fld.onChecked(R2M(i_this_obj, "m_on_switch_errorfirst"));
      v_com_errorfirst_switch_fld.checked = gv_result_prm.v_com_errorfirst_switch;
      v_com_errorfirst_switch_fld.enabled = make_visible;
      AddControl(v_com_errorfirst_switch_fld);


      /* Кнопка выгрузки */
      v_com_result_btn = TRsbPushButton("Показать");
      v_com_result_btn.setPosition(CV_POS_X + 4, (CV_POS_Y + SizeLabely + 7));
      v_com_result_btn.setSize(12, 1);
      v_com_result_btn.onClicked(R2M(i_this_obj, "m_evnt_result_btn"));
      v_com_result_btn.enabled = make_visible;
      AddControl(v_com_result_btn);

      AddEventHandler(RSB_EV_KEY_PRESSED, R2M(i_this_obj, "m_evnt_on_key_pressed"));

      setSize(42, CV_POS_Y + SizeLabely + 9);
      setPosition(25, 6);

   end;

   InitTRsbPanel();

   m_init_com_result_form(this, _longtext);
end; /* class (TRsbPanel) c_com_result_form() */


private macro GetResult(_objecttype)
   var sql_str, rs, cmd;
   var long_text = TArray;
   var cnt_result = 0;
   var cnt_result_OK = 0;
   var cnt_result_all = 0;
   var txt;

   sql_str = "select  cnt, t_status,   "+ 
             "        case t_status             "+                            
             "            when 1 then 'В очереди к отправке, пока не отправлен'     "+
//             "            when 2 then 'Отправлено в Бисквит, ответ пока не получен' "+
             "            when 2 then 'Отправлено в ЦФТ, ответ пока не получен' "+
             "            when 4 then 'Успешно'                                "+
             "            when 5 then 'Ошибка'                                 "+
             "            else to_char(t_status)                               "+
             "        end status_txt                                           "+   
             " from (                                                          "+
             "   select  count(*) cnt, t_status   "+ 
             "     from utableprocessevent_dbt where t_objecttype = :pobjecttype and t_recid in  "+
             "       (select t_recid from u_unload_bisquit where t_objecttype = :pobjecttype and session_guid = :vguid) "+
             "    group by t_status )";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("pobjecttype1", RSDBP_IN, _objecttype);        
   cmd.AddParam("pobjecttype2", RSDBP_IN, _objecttype);        
   cmd.AddParam("vguid", RSDBP_IN, gv_evnt_sel_prm.v_guid);        
   rs = RSDRecordSet(cmd);
   gv_evnt_sel_prm.v_flag_notallsent = false;
   long_text.value(long_text.size) = "Результаты отправки: ";
   while (rs.movenext)
      cnt_result = cnt_result + 1;
      if ( rs.value("t_status") == 1 ) 
         gv_evnt_sel_prm.v_flag_notallsent = true;
      end;
      txt = rs.value("status_txt");
      txt = txt +":" + MkStr(" ",43-Strlen(txt)-1);
      long_text.value(long_text.size) = txt + Int(rs.value("cnt"));
      if (rs.value("t_status") == 4)
         cnt_result_OK = Int(rs.value("cnt"));
      end;
      cnt_result_all = cnt_result_all + Int(rs.value("cnt"));
   end;
   if (cnt_result_all == cnt_result_OK)
      MarkAllRecordsOK(_objecttype, gv_evnt_sel_prm.v_guid);
   end;

   if (cnt_result == 0)
      msgbox("Нет данных");
   else
      gv_result_prm = c_result_prm();
      gv_com_result_form = c_com_result_form(long_text, _objecttype);
      gv_com_result_form.run;
   end;

   cmd.close;
   cmd = null; sql_str = null;

onerror(e)
   RunError(e.message + getFullCmdErrorText(cmd));
end; 

macro m_evnt_proc_scr(p_rs, p_cmd, p_id, p_key)
   var CM_FLAG = CM_DEFAULT;
   var ss;

   if(p_cmd == DLG_INIT)

   elif(p_cmd == DLG_KEY)
      if(p_key == K_ENTER)
         CM_FLAG = CM_DEFAULT;
      elif(p_key == K_F5)
         CM_FLAG = CM_CANCEL;
      elif(p_key == K_F8)
         CM_FLAG = CM_IGNORE;
      elif(p_key == K_F9)
         CM_FLAG = CM_CANCEL;
      elif(p_key == K_F2)
         CM_FLAG = CM_CANCEL;
      elif(p_key == K_SPACE)
         ss = CodeFor(p_rs.value("t_execute"));
         p_rs.edit;
         p_rs.value("t_execute") = StrFor(88-ss);
         p_rs.Update();
         UpdateScroll(p_rs,2);

      end;
   end;

   return CM_FLAG;
end; /* macro m_evnt_proc_scr() */
        

macro ShowSelection(_objecttype)
   private var gv_column_list_scr = TArray(); /* Список колонок скроллинга */
   private var gv_num_columns_scr = 0;        /* Количество колонок скроллинга */
   private var gv_scr_focus = 0;              /* Номер позиции скроллинга */

   var sql_scr, cmd_scr, rs_scr, cnt;
   if (_objecttype == OBJTYPE_ACCOUNT)
      sql_scr = "SELECT t_execute, t_account_payer, t_chapter, t_nameaccount, t_recid  FROM u_Selection_obj_Process ";

      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_execute",       "",                   3 , 1); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_account_payer", "Номер счета",        17, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_chapter",       "Гл.",                3 , 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_nameaccount",   "Наименование счета", 90, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_recid",         "",                    1, 2); gv_num_columns_scr = gv_num_columns_scr + 1;

   elif (_objecttype == OBJTYPE_DOCUMENT)
      sql_scr = "SELECT t_execute, t_numb_document, t_account_payer, t_account_receiver, t_sum_payer, t_sum_receiver, t_nameaccount, t_recid  FROM u_Selection_obj_Process ";

      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_execute",          "",              3, 1); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_numb_document",    "Номер",        10, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_account_payer",    "Счет дебета",  20, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_account_receiver", "Счет кредита", 20, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_sum_payer",        "Сумма дебета", 16, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_sum_receiver",     "Сумма кредита",16, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_nameaccount",      "",             45, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_recid",            "",              1, 2); gv_num_columns_scr = gv_num_columns_scr + 1;

   elif (_objecttype == OBJTYPE_PAYMENT)
      sql_scr = "SELECT t_execute, t_account_payer, t_account_receiver, t_sum_payer, t_sum_receiver, t_recid  FROM u_Selection_obj_Process ";

      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_execute",          "",              3, 1); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_account_payer",    "Счет дебета",  20, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_account_receiver", "Счет кредита", 20, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_sum_payer",        "Сумма дебета", 16, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_sum_receiver",     "Сумма кредита",16, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
      m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_recid",            "",              1, 2); gv_num_columns_scr = gv_num_columns_scr + 1;

   end; 

   if (sql_scr)
      cmd_scr = RSDCommand(sql_scr);
      cmd_scr.NullConversion = true;
      rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
      rs_scr.NullConversion = true;
      rs_scr.pagesize = 100;

      while(RunScroll(rs_scr, gv_num_columns_scr, gv_column_list_scr, "SelectionRecords", "m_evnt_proc_scr", "Список объектов для выгрузки", "ESC выход ~ПРОБЕЛ~ Отметить/Снять отметку ", false, 5, NULL, NULL, 40))
         rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
      end;
      rs_scr.close; cmd_scr.close; rs_scr = null; cmd_scr = null;

      sql_scr = "SELECT count(t_execute) cnt FROM u_Selection_obj_Process WHERE t_execute = 'X'";
      rs_scr = RSDRecordSet(sql_scr);
      if (rs_scr.movenext)
         cnt = Int(rs_scr.value("cnt"));
      end;
      rs_scr.close; rs_scr = null; 

      gv_evnt_sel_prm.v_cnt = cnt;
   end;

onError(e)
   RunError(e.message + getFullCmdErrorText(cmd_scr));
end;


/*---------------------------*/
/* Параметры отбора объектов */
/*---------------------------*/
private class c_evnt_selection_prm()
   var v_com_date;
   var v_com_beg_date;
   var v_com_end_date;
   var v_com_date_switch;

   var v_com_accounts;
   var v_com_trans;
   var v_com_payments;

   var v_com_number_pack;
   var v_guid:string;
   var v_cnt;
   var v_flag_notallsent;
   var v_current_date;
   var v_period_txt;

   /* Значения по умолчанию */
   private macro m_init_evnt_selection_prm()
      v_com_date = date();
      v_com_beg_date = date();
      v_com_end_date = date();
      v_com_date_switch = false;

      v_com_accounts = true;
      v_com_trans = false;
      v_com_payments = false;

      v_com_number_pack = -1;
      v_guid = "";
      v_cnt = 0;
      v_flag_notallsent = false;

      v_current_date = date();
      v_period_txt = "";
   end;

   m_init_evnt_selection_prm();
end; /* class c_evnt_selection_prm() */


/*----------------------------------------*/
/* Класс основной формы                   */
/*----------------------------------------*/
class (TRsbPanel) c_com_unload_form()
   const CV_POS_X = 3;
   const CV_POS_Y = 2;
   var v_com_date_fld;
   var v_com_beg_date_fld;
   var v_com_end_date_fld;
   var v_com_date_switch_fld;
   var v_com_number_pack_fld;

   var v_com_null_btn;
   var v_com_unload_btn;
   var v_com_result_btn;
   var v_com_new_btn;

   var v_com_r_accounts;
   var v_com_r_trans;
   var v_com_r_payments;

   var v_com_date;
   var v_com_beg_date;
   var v_com_end_date;
   var v_com_number_pack;

   macro EnableDisableFields(_sign)
      v_com_date_fld.enabled = _sign;
      v_com_beg_date_fld.enabled = _sign;
      v_com_end_date_fld.enabled = _sign;

      v_com_date_switch_fld.enabled = _sign;

      v_com_number_pack_fld.enabled = _sign;

      v_com_r_accounts.enabled = _sign;
      v_com_r_trans.enabled = _sign;
      v_com_r_payments.enabled = _sign;

      v_com_null_btn.enabled = _sign;
      v_com_unload_btn.enabled = _sign;
      v_com_new_btn.enabled = (not _sign);

      redraw();
   end;


   macro m_evnt_on_key_pressed(p_rsb_evnt)
      if(p_rsb_evnt.KeyCode == K_F2)
         close(CV_CLOSE_FORM_OK);
      end;

      return true;
   end;


   macro m_on_switch_date(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_CONTROL_CHECKED)
         gv_evnt_sel_prm.v_com_date_switch = v_com_date_switch_fld.checked;
      end;

      if(gv_evnt_sel_prm.v_com_date_switch)
         v_com_date_fld.focusable = false;
         v_com_date_fld.enabled = false;
         v_com_date_fld.editable = false;

         v_com_beg_date_fld.focusable = true;
         v_com_beg_date_fld.enabled = true;
         v_com_beg_date_fld.editable = true;

         v_com_end_date_fld.focusable = true;
         v_com_end_date_fld.enabled = true;
         v_com_end_date_fld.editable = true;
      else
         v_com_date_fld.focusable = true;
         v_com_date_fld.enabled = true;
         v_com_date_fld.editable = true;

         v_com_beg_date_fld.focusable = false;
         v_com_beg_date_fld.enabled = false;
         v_com_beg_date_fld.editable = false;

         v_com_end_date_fld.focusable = false;
         v_com_end_date_fld.enabled = false;
         v_com_end_date_fld.editable = false;
      end;

      redraw();

      return true;
   end;

   macro m_on_checked_r(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_CONTROL_CHECKED)
         if (v_com_r_accounts.checked)
            gv_evnt_sel_prm.v_com_accounts = true;
            gv_evnt_sel_prm.v_com_trans = false;
            gv_evnt_sel_prm.v_com_payments = false;
         elif (v_com_r_trans.checked)
            gv_evnt_sel_prm.v_com_accounts = false;
            gv_evnt_sel_prm.v_com_trans = true;
            gv_evnt_sel_prm.v_com_payments = false;
         elif (v_com_r_payments.checked)
            gv_evnt_sel_prm.v_com_accounts = false;
            gv_evnt_sel_prm.v_com_trans = false;
            gv_evnt_sel_prm.v_com_payments = true;
         end;

      end;

      return true;
   end;

   macro MakeNull()
     v_com_date_fld.focusable = true;
     v_com_date_fld.enabled = true;
     v_com_date_fld.editable = true;

     v_com_beg_date_fld.focusable = false;
     v_com_beg_date_fld.enabled = false;
     v_com_beg_date_fld.editable = false;

     v_com_end_date_fld.focusable = false;
     v_com_end_date_fld.enabled = false;
     v_com_end_date_fld.editable = false;

     v_com_r_accounts.checked = true;
     gv_evnt_sel_prm.v_com_accounts = true;
     v_com_r_trans.checked = false;
     gv_evnt_sel_prm.v_com_trans = false;
     v_com_r_payments.checked = false;
     gv_evnt_sel_prm.v_com_payments = false;

     v_com_number_pack = -1;
     gv_evnt_sel_prm.v_com_number_pack = -1;
     v_com_number_pack_fld.value = v_com_number_pack;

     gv_evnt_sel_prm.v_guid = "";
     gv_evnt_sel_prm.v_cnt = 0;
     gv_evnt_sel_prm.v_flag_notallsent = false;
     gv_evnt_sel_prm.v_current_date = date();
     gv_evnt_sel_prm.v_period_txt = "";

     redraw();
   end;


   macro m_evnt_null_btn(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_BUTTON_CLICKED)
         MakeNull();

         gv_evnt_sel_prm.v_com_date = date("00.00.0000");
         v_com_date_fld.value = date("00.00.0000");

         gv_evnt_sel_prm.v_com_beg_date = date("00.00.0000");
         v_com_beg_date_fld.value = date("00.00.0000");

         gv_evnt_sel_prm.v_com_end_date = date("00.00.0000");
         v_com_end_date_fld.value = date("00.00.0000");

         v_com_date_switch_fld.checked = false;
         gv_evnt_sel_prm.v_com_date_switch = false;

         redraw();
      end;

      return true;
   end;


   macro check_params()
      if ( (not v_com_date_switch_fld.checked) and (gv_evnt_sel_prm.v_com_date == date(0,0,0)) )
         msgbox("Не указана дата");
         return false;
      end;

      if ( (v_com_date_switch_fld.checked) and ( (gv_evnt_sel_prm.v_com_beg_date == date(0,0,0)) or 
                                                 (gv_evnt_sel_prm.v_com_end_date == date(0,0,0))  ) )
         msgbox("Не указана дата из диапазона");
         return false;
      end;

      if (gv_evnt_sel_prm.v_com_number_pack < 0)
         msgbox("Не указан номер пачки");
         return false;
      else
         if ({oper} == 1)
         else
           if ( 
               ( gv_evnt_sel_prm.v_com_number_pack == 10 ) or 
               ( gv_evnt_sel_prm.v_com_number_pack == 11 ) or //shev 03.10.2019 для вывода денежных средств клиента
               ( gv_evnt_sel_prm.v_com_number_pack == 30 ) or 
               ( gv_evnt_sel_prm.v_com_number_pack == 35 ) or 
               ( gv_evnt_sel_prm.v_com_number_pack == 40 ) or 
               ( gv_evnt_sel_prm.v_com_number_pack == 45 ) or 
               ( gv_evnt_sel_prm.v_com_number_pack == 50 ) or  /*maa080519-автом. не выгрузились проводки по векселям, пришлось добавить на ручную выгрузку*/
               ( gv_evnt_sel_prm.v_com_number_pack == 55 ) or  /*maa130519-автом. не выгрузились проводки по векселям, пришлось добавить на ручную выгрузку*/
               ( gv_evnt_sel_prm.v_com_number_pack == 60 ) or 
               ( gv_evnt_sel_prm.v_com_number_pack == 63 ) or
               ( gv_evnt_sel_prm.v_com_number_pack == 70 ) or 
               ( gv_evnt_sel_prm.v_com_number_pack == 80 ) or 
               ( gv_evnt_sel_prm.v_com_number_pack == 90 ) or 
               ( gv_evnt_sel_prm.v_com_number_pack == 160 ) or
	       ( gv_evnt_sel_prm.v_com_number_pack == 170 ) 

              )
           else
              msgbox("Вам запрещено выгружать эту пачку "+gv_evnt_sel_prm.v_com_number_pack);
              return false;
           end;
         end;
      end;


      if ( (not gv_evnt_sel_prm.v_com_accounts) and (not gv_evnt_sel_prm.v_com_trans) and (not gv_evnt_sel_prm.v_com_payments) )
         msgbox("Необходимо отметить хотя бы один объект для выгрузки: счета, проводки или платежи");
         return false;
      end;

      return true;

   end;

   macro GetNameAmount(_val, _num1, _num2, _num10) 
      var res = _num10; 

      if ( (mod(_val,100) >= 11) and (mod(_val,100) <= 20) )
         res = _num10;
      elif (mod(_val,10) == 1) 
         res = _num1;
      elif ( (mod(_val,10) >= 2) and (mod(_val,10) <= 4) )
         res = _num2;
      else
         res = _num10;
      end;
      return res;
   end;

   macro m_evnt_unload_btn(p_rsb_evnt)
      var num_acc, num_acc_str;

      if(p_rsb_evnt.id == RSB_EV_BUTTON_CLICKED)
         if (check_params)
            if (not v_com_date_switch_fld.checked)
               gv_evnt_sel_prm.v_period_txt = " за дату "+gv_evnt_sel_prm.v_com_date;
            elif (v_com_date_switch_fld.checked)
               gv_evnt_sel_prm.v_period_txt = " за период с "+gv_evnt_sel_prm.v_com_beg_date+" по "+ gv_evnt_sel_prm.v_com_end_date;
            end;

            if (gv_evnt_sel_prm.v_com_accounts)
               FillTempTable(OBJTYPE_ACCOUNT);
               if (gv_evnt_sel_prm.v_cnt > 0)
                  num_acc_str = GetNameAmount(gv_evnt_sel_prm.v_cnt,"счет","счета","счетов");

                  if (msgboxex("К выгрузке "+gv_evnt_sel_prm.v_cnt+" "+num_acc_str+" по пачке "+gv_evnt_sel_prm.v_com_number_pack+gv_evnt_sel_prm.v_period_txt+
                               ".| Просмотреть?", MB_YES+MB_NO, IND_NO) == IND_YES)
                     ShowSelection(OBJTYPE_ACCOUNT);
                  end;
                  if (gv_evnt_sel_prm.v_cnt == 0)
                     msgbox("Нет счетов для выгрузки");
                  else
                     num_acc_str = GetNameAmount(gv_evnt_sel_prm.v_cnt,"счет","счета","счетов");
                     if (msgboxex("Отмечено к выгрузке "+gv_evnt_sel_prm.v_cnt+" "+num_acc_str+" по пачке "+gv_evnt_sel_prm.v_com_number_pack+gv_evnt_sel_prm.v_period_txt+"|Вы уверены, что хотите их выгрузить?", MB_NO+MB_YES, IND_NO) == IND_YES)
                        RunTempTable();
                        EnableDisableFields(false);
                     end;
                  end;
               else
                  msgbox("Нет счетов для выгрузки");
               end;

            elif (gv_evnt_sel_prm.v_com_trans)
               FillTempTable(OBJTYPE_DOCUMENT);
               if (gv_evnt_sel_prm.v_cnt > 0)
                  num_acc_str = GetNameAmount(gv_evnt_sel_prm.v_cnt,"проводка","проводки","проводок");

                  if (msgboxex("К выгрузке "+gv_evnt_sel_prm.v_cnt+" "+num_acc_str+" по пачке "+gv_evnt_sel_prm.v_com_number_pack+gv_evnt_sel_prm.v_period_txt+
                               ".| Просмотреть?", MB_YES+MB_NO, IND_NO) == IND_YES)
                     ShowSelection(OBJTYPE_DOCUMENT);
                  end;
                  if (gv_evnt_sel_prm.v_cnt == 0)
                     msgbox("Нет проводок для выгрузки");
                  else
                     num_acc_str = GetNameAmount(gv_evnt_sel_prm.v_cnt,"проводка","проводки","проводок");
                     if (msgboxex("Отмечено к выгрузке "+gv_evnt_sel_prm.v_cnt+" "+num_acc_str+" по пачке "+gv_evnt_sel_prm.v_com_number_pack+gv_evnt_sel_prm.v_period_txt+"|Вы уверены, что хотите их выгрузить?", MB_NO+MB_YES, IND_NO) == IND_YES)
                        RunTempTable();
                        EnableDisableFields(false);
                     end;
                  end;
               else
                  msgbox("Нет проводок для выгрузки");
               end;

            elif (gv_evnt_sel_prm.v_com_payments)
               FillTempTable(OBJTYPE_PAYMENT);
               if (gv_evnt_sel_prm.v_cnt > 0)
                  num_acc_str = GetNameAmount(gv_evnt_sel_prm.v_cnt,"платеж","платежа","платежей");

                  if (msgboxex("К выгрузке "+gv_evnt_sel_prm.v_cnt+" "+num_acc_str+" по пачке "+gv_evnt_sel_prm.v_com_number_pack+gv_evnt_sel_prm.v_period_txt+
                               ".| Просмотреть?", MB_YES+MB_NO, IND_NO) == IND_YES)
                     ShowSelection(OBJTYPE_PAYMENT);
                  end;
                  if (gv_evnt_sel_prm.v_cnt == 0)
                     msgbox("Нет платежей для выгрузки");
                  else
                     num_acc_str = GetNameAmount(gv_evnt_sel_prm.v_cnt,"платеж","платежа","платежей");
                     if (msgboxex("Отмечено к выгрузке "+gv_evnt_sel_prm.v_cnt+" "+num_acc_str+" по пачке "+gv_evnt_sel_prm.v_com_number_pack+gv_evnt_sel_prm.v_period_txt+"|Вы уверены, что хотите их выгрузить?", MB_NO+MB_YES, IND_NO) == IND_YES)
                        RunTempTable();
                        EnableDisableFields(false);
                     end;
                  end;
               else
                  msgbox("Нет платежей для выгрузки");
               end;

            end;
         end;
      end;

      return true;
   end;


   macro m_evnt_startnew_btn(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_BUTTON_CLICKED)
         if (msgboxex("Вы получили результаты выгрузки и хотите начать новую? ", MB_YES+MB_NO, IND_NO) == IND_YES)
            EnableDisableFields(true);
            MakeNull();
         end;
      end;
      return true;
   end;


   macro m_evnt_getresult_btn(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_BUTTON_CLICKED)
         if (gv_evnt_sel_prm.v_com_accounts)
            GetResult(OBJTYPE_ACCOUNT);
         elif (gv_evnt_sel_prm.v_com_trans)
            GetResult(OBJTYPE_DOCUMENT);
         elif (gv_evnt_sel_prm.v_com_payments)
            GetResult(OBJTYPE_PAYMENT);
         end;
      end;
      return true;
   end;


   private macro m_init_com_unload_form(i_this_obj)
//      Caption = "Выгрузка в Бисквит";
      Caption = "Выгрузка в ЦФТ";

      Status = "[Esc] Выход ";

      /* Фильтр по дате */
      AddLabel(TRsbLabel(CV_POS_X, CV_POS_Y, "Дата выборки"));
      v_com_date_fld = TRsbEditField(FT_DATE);
      v_com_date_fld.bindValue(gv_evnt_sel_prm, "v_com_date", 10);
      v_com_date_fld.setPosition(CV_POS_X, (CV_POS_Y + 1));
      v_com_date_fld.setSize(10, 1);
      if(gv_evnt_sel_prm.v_com_date_switch)
         v_com_date_fld.focusable = false;
         v_com_date_fld.enabled = false;
         v_com_date_fld.editable = false;
      else
         v_com_date_fld.focusable = true;
         v_com_date_fld.enabled = true;
         v_com_date_fld.editable = true;
      end;
      AddControl(v_com_date_fld);

      AddLabel(TRsbLabel((CV_POS_X + 14), (CV_POS_Y + 1), "Диапазон"));
      v_com_date_switch_fld = TRsbCheckBox();
      v_com_date_switch_fld.setPosition((CV_POS_X + 12), (CV_POS_Y + 1));
      v_com_date_switch_fld.onChecked(R2M(i_this_obj, "m_on_switch_date"));
      v_com_date_switch_fld.checked = gv_evnt_sel_prm.v_com_date_switch;
      AddControl(v_com_date_switch_fld);

      AddLabel(TRsbLabel((CV_POS_X + 22), CV_POS_Y, "Диапазон дат"));
      v_com_beg_date_fld = TRsbEditField(FT_DATE);
      v_com_beg_date_fld.bindValue(gv_evnt_sel_prm, "v_com_beg_date", 10);
      v_com_beg_date_fld.setPosition((CV_POS_X + 22), (CV_POS_Y + 1));
      v_com_beg_date_fld.setSize(10, 1);
      if(gv_evnt_sel_prm.v_com_date_switch)
         v_com_beg_date_fld.focusable = true;
         v_com_beg_date_fld.enabled = true;
         v_com_beg_date_fld.editable = true;
      else
         v_com_beg_date_fld.focusable = false;
         v_com_beg_date_fld.enabled = false;
         v_com_beg_date_fld.editable = false;
      end;
      AddControl(v_com_beg_date_fld);
      v_com_end_date_fld = TRsbEditField(FT_DATE);
      v_com_end_date_fld.bindValue(gv_evnt_sel_prm, "v_com_end_date", 10);
      v_com_end_date_fld.setPosition((CV_POS_X + 22), (CV_POS_Y + 2));
      v_com_end_date_fld.setSize(10, 1);
      if(gv_evnt_sel_prm.v_com_date_switch)
         v_com_end_date_fld.focusable = true;
         v_com_end_date_fld.enabled = true;
         v_com_end_date_fld.editable = true;
      else
         v_com_end_date_fld.focusable = false;
         v_com_end_date_fld.enabled = false;
         v_com_end_date_fld.editable = false;
      end;
      AddControl(v_com_end_date_fld);

      /* Фильтр по пачке */
      AddLabel(TRsbLabel(CV_POS_X, CV_POS_Y + 2, "Номер пачки проводок"));
      v_com_number_pack_fld = TRsbEditField(FT_INT);
      v_com_number_pack_fld.bindValue(gv_evnt_sel_prm, "v_com_number_pack", 5);
      v_com_number_pack_fld.setPosition(CV_POS_X + 2, CV_POS_Y + 3);
      v_com_number_pack_fld.setSize(5, 1);
      AddControl(v_com_number_pack_fld);

      /* Фильтр по объектам */
      AddLabel(TRsbLabel(CV_POS_X + 4, (CV_POS_Y + 4), "Счета"));
      v_com_r_accounts = TRsbRadioButton();
      v_com_r_accounts.setPosition(CV_POS_X+1, (CV_POS_Y + 4));
      v_com_r_accounts.onChecked(R2M(i_this_obj, "m_on_checked_r"));
      v_com_r_accounts.checked = true;
      AddControl(v_com_r_accounts);
      AddLabel(TRsbLabel(CV_POS_X + 4, (CV_POS_Y + 5), "Проводки"));
      v_com_r_trans = TRsbRadioButton();
      v_com_r_trans.setPosition(CV_POS_X+1, (CV_POS_Y + 5));
      v_com_r_trans.onChecked(R2M(i_this_obj, "m_on_checked_r"));
      AddControl(v_com_r_trans);
      AddLabel(TRsbLabel(CV_POS_X + 4, (CV_POS_Y + 6), "Платежи"));
      v_com_r_payments = TRsbRadioButton();
      v_com_r_payments.setPosition(CV_POS_X+1, (CV_POS_Y + 6));
      v_com_r_payments.onChecked(R2M(i_this_obj, "m_on_checked_r"));
      AddControl(v_com_r_payments);



      /* Кнопка выгрузки */
      v_com_unload_btn = TRsbPushButton("Выгрузить");
      v_com_unload_btn.setPosition((CV_POS_X), (CV_POS_Y + 8));
      v_com_unload_btn.setSize(12, 1);
      v_com_unload_btn.onClicked(R2M(i_this_obj, "m_evnt_unload_btn"));
      AddControl(v_com_unload_btn);

      /* Кнопка сброса */
      v_com_null_btn = TRsbPushButton("Сброс");
      v_com_null_btn.setPosition((CV_POS_X + 14), (CV_POS_Y + 8));
      v_com_null_btn.setSize(6, 1);
      v_com_null_btn.onClicked(R2M(i_this_obj, "m_evnt_null_btn"));
      AddControl(v_com_null_btn);

      v_com_result_btn = TRsbPushButton("Посмотреть результат");
      v_com_result_btn.setPosition((CV_POS_X + 22), (CV_POS_Y + 8));
      v_com_result_btn.setSize(15, 1);
      v_com_result_btn.onClicked(R2M(i_this_obj, "m_evnt_getresult_btn"));
      AddControl(v_com_result_btn);

      v_com_new_btn = TRsbPushButton("Начать новую выгрузку");
      v_com_new_btn.setPosition(CV_POS_X , (CV_POS_Y + 10));
      v_com_new_btn.setSize(17, 1);
      v_com_new_btn.enabled = false;
      v_com_new_btn.onClicked(R2M(i_this_obj, "m_evnt_startnew_btn"));
      AddControl(v_com_new_btn);


      AddEventHandler(RSB_EV_KEY_PRESSED, R2M(i_this_obj, "m_evnt_on_key_pressed"));

      setSize(45, 14);
      setPosition(20, 5);

   end;

   InitTRsbPanel();

   m_init_com_unload_form(this);
end; /* class (TRsbPanel) c_com_unload_form() */

//msgbox("Выгрузка из интерфейса временно недоступна");
//exit(1);

   gv_evnt_sel_prm = c_evnt_selection_prm();

         gv_com_unload_form = c_com_unload_form();
         gv_com_unload_form.run;

exit(1);
