/*
$Name:        SendTaxRecalculationSOFRReq.mac
$Module:      Интеграция с внешними системами
$Description: Входящий сервис передачи данных для пересчета НОБ 
*/

/*-------------------------------------------------------------------------------*/
/* BIQ-7294                                                                      */
/* Автор: Гераськина Т.                                                          */
/*-------------------------------------------------------------------------------*/
import "func_lib.mac";
import "global_classes.mac";


// Операция
class c_Operation(IncomeData, nametag_up)
   var IsSNOBChange           : bool;     // Признак изменения СНОБ                       Да
   var IsResidentChange       : bool;     // Признак изменения резидентности              Да
   var AmountSNOB             : money;    // Актуальная сумма СНОБ                        Да
   var NOBTypeAmount          : money;    // Актуальная сумма НОБ по типу дохода          Нет
   var TaxResidentFlag        : string;   // Актуальный налоговый статус                  Нет
   var ClientId               ;           // Идентификатор клиента в системе-источнике    Да
   var OperationId            ;           // Идентификатор операции                       Да
   var EventId                ;           // Идентификатор события                        Да
   var SNOBRecordId           ;           // Идентификатор записи в Хранилище СНОБ        Да

   private macro uInitPrime(IncomeData, nametag_up)
      IsSNOBChange      = getValueFromProperty(IncomeData, "IsSNOBChange",          nametag_up, true);
      IsResidentChange  = getValueFromProperty(IncomeData, "IsResidentChange",      nametag_up, true);
      AmountSNOB        = getValueFromProperty(IncomeData, "AmountSNOB",      nametag_up, true);
      NOBTypeAmount     = getValueFromProperty(IncomeData, "NOBTypeAmount",   nametag_up);
      if (ValType(NOBTypeAmount) == V_UNDEF)
         NOBTypeAmount = 0;
      end;
      TaxResidentFlag   = getValueFromProperty(IncomeData, "TaxResidentFlag", nametag_up);
      if (ValType(TaxResidentFlag) == V_UNDEF)
         TaxResidentFlag = "0";
      end;

      ClientId          = getSymbolFromProperty(IncomeData, "ClientId"     , nametag_up, true);
      OperationId       = getSymbolFromProperty(IncomeData, "OperationId"  , nametag_up, true);
      EventId           = getSymbolFromProperty(IncomeData, "EventId"      , nametag_up, true);
      SNOBRecordId      = getSymbolFromProperty(IncomeData, "SNOBRecordId" , nametag_up, true);
   end; 

   uInitPrime(IncomeData, nametag_up);
end;


// непосредственная обработка единичной записи
macro ProcessRecalculation(p_Operation) 
   var out_obj = c_Error;
   var out_code, err_txt;
   var ReqID = "";

   if(ValType(TrnPrm) != V_UNDEF) //Глобализм с информацией о входящем запросе
     ReqID = strsubst(strsubst(TrnPrm.ReqIDMes, "{", ""), "}", "");
   end;

   out_obj.ErrorCode = 0;
   out_obj.ErrorDesc = "OK";
   ExecMacroFile("nptxstbfun.mac", "STB_AddRecToTBBuf", p_Operation, out_obj, ReqID);

   return out_obj;
   
onError(err)
   out_code = c_err_obj.obtain_err_code(err);
   err_txt = c_err_obj.obtain_err_msg(err);

   out_obj = c_Error;
   out_obj.ErrorCode = out_code;
   out_obj.ErrorDesc = err_txt;
   
   return out_obj;
end;

/*--------------------------------------------*/
/* Запрос по статусу прохождения аккредитации */
/*--------------------------------------------*/
macro adp_SendTaxRecalculationSOFR(IncomeData, p_log_id, p_transparent_id)
   var OperationList = NULL;              // Список операций  Контейнер      Нет
   
   var resp_data_obj = NULL;
   var err_txt="", out_code = 0;
   var aux_buff;

   var temp_Operation = NULL;
   var res_arr = TArray;
   var res = NULL, ai = 0;
   
   if(ValType(IncomeData) == V_UNDEF)
      RunError(ERR_TEXT_NO_IN_MSG, c_err_obj(ERR_TEXT_NO_IN_MSG,
                                             ERR_CODE_NO_IN_PRM));
   end;

   aux_buff = getValueFromProperty(IncomeData, "OperationList", "SendTaxRecalculationSOFRReq", true);
   if(ValType(aux_buff) == V_GENOBJ)
      OperationList = TArray;
      ai = 0;
      while(IncomeData.OperationList.size > ai)
         temp_Operation = c_Operation(IncomeData.OperationList.value(ai), "SendTaxRecalculationSOFRReq.OperationList.Operation");
         OperationList.value(ai) = temp_Operation;
         ai = ai + 1;
         temp_Operation = null;
      end;
   elif(ValType(aux_buff) == V_UNDEF)
      err_txt = string(InErrComPart, "SendTaxRecalculationSOFRReq.OperationList");
      RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
   else
      err_txt = string(InErrNotObj, "SendTaxRecalculationSOFRReq.OperationList");
      RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
   end;

   ai = 0;
   for (temp_Operation, OperationList)
      res = ProcessRecalculation(temp_Operation);
      res_arr.value(ai) = res;
      res = NULL;
      ai = ai+1;
   end;

   // итоговый класс 
   resp_data_obj = c_SendTaxRecalculationSOFRResp_outObj();
   resp_data_obj.SendTaxRecalculationSOFRResp.ErrorList = TArray;
   if (res_arr.size > 0)
      resp_data_obj.SendTaxRecalculationSOFRResp.ErrorList = res_arr;
   else
      resp_data_obj.SendTaxRecalculationSOFRResp.ErrorList.value(0) = c_Error;
      resp_data_obj.SendTaxRecalculationSOFRResp.ErrorList.value(0).ErrorCode = "-1";
      resp_data_obj.SendTaxRecalculationSOFRResp.ErrorList.value(0).ErrorDesc = "Нет данных"; // О_о
   end;
   res_arr = NULL;
   
   return resp_data_obj;
   
onError(err)
   out_code = c_err_obj.obtain_err_code(err);
   err_txt = c_err_obj.obtain_err_msg(err);

   resp_data_obj = c_SendTaxRecalculationSOFRResp_outObj;

   resp_data_obj.SendTaxRecalculationSOFRResp.ErrorList = TArray;
   resp_data_obj.SendTaxRecalculationSOFRResp.ErrorList.value(0) = c_Error;
   resp_data_obj.SendTaxRecalculationSOFRResp.ErrorList.value(0).ErrorCode = out_code;
   resp_data_obj.SendTaxRecalculationSOFRResp.ErrorList.value(0).ErrorDesc = err_txt;
   
   return resp_data_obj;
end;

/*---------------------------------------------------------*/
/* Запрос с контейнером записей, подлежащих пересчету с СИ */
/*---------------------------------------------------------*/
macro SendTaxRecalculationSOFRReq(IncomeData, in_log_id, in_transparent_id) 
   var out_obj;
   out_obj = adp_SendTaxRecalculationSOFR(IncomeData, in_log_id, in_transparent_id); 
   return out_obj;
end;

