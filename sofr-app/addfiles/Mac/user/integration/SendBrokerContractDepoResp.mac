/*-----------------------------------------------------------------------------------------
   Обработка ответа на передачу информации по брокерскому обслуживанию в систему Депозитарий
   BIQ-7382
   Гераськина Т.В.
-----------------------------------------------------------------------------------------*/
import global_classes, func_lib;
import "u_common_email_utils.mac";
import "usr_table_int.mac";

private class (c_usr_tbl_base) c_usr_tbl_uTableProcessEvent()
   private class gen_uTableProcessEvent()
      var T_RECID :integer,
          T_TIMESTAMP :date,
          T_OBJECTTYPE :integer,
          T_OBJECTID :integer,
          T_TYPE :integer,
          T_STATUS :integer,
          T_NOTE :string,
          T_MESSAGEID :integer,
          T_RESULTCODE :string,
          T_RESULTTEXT :string,
          T_ATTEMPTS :integer;
   end;

   private macro init_table_obj()
      new_val_obj = gen_uTableProcessEvent();
      where_val_obj = gen_uTableProcessEvent();
   end;

   Initc_usr_tbl_base("uTableProcessEvent_dbt");

   init_table_obj();
end;


private class (c_Error) c_Error_Updatebrok(IncomeData)
   Initc_Error;

   private macro m_init_Error(IncomeData)
      private var nametag_up = "SendBrokerContractDepoResp.ErrorList.Error";
      ErrorCode = getValueFromProperty(IncomeData, "ErrorCode", nametag_up, true);
      ErrorDesc = getValueFromProperty(IncomeData, "ErrorDesc", nametag_up);
   end;

   m_init_Error(IncomeData);
end;

private class c_SendBrokerContractDepoResult()
   var ResultCode                   : String;   //Код результата              Да    =0, если успех
   var ContractNumber               : String;   //Номер брокерского договора  Да
   var DepoNumber                   : String;   //Номер договора депо         Да
   var DepoDate                     : date;     //Дата заключения договора депо  Да
   var DepoAccountNumber            : String;   //Счёт депо владельца            Да
   var DepoAccountPartition         : String;   //Раздел счёта депо владельца    Да
   var DepoTradingAccountNumber     : String;   //Торговый счёт депо             Нет
   var DepoTradingAccountPartition  : String;   //Раздел торгового счёта депо    Нет
   var DepoTradingAccountPartitionSPBEX: String;   //Раздел торгового счёта депо СПБ Нет
end;

private class c_SendBrokerContractDepoResp(IncomeData)
   var SendBrokerContractDepoResult;   //Контейнер    Нет
   var ErrorList;                      //Контейнер    Да          ErrorCode=0, если нет ошибок (видимо, в первом элементе)
   
   private macro m_init_SendBrokerContractDepoResp(IncomeData)
      private var err_txt, aux_buffResp, aux_buffR, aux_buff;
      private var nametag_up = "SendBrokerContractDepoResp.SendBrokerContractDepoResult";

      if(ValType(IncomeData) != V_UNDEF)
         aux_buffResp = getValueFromProperty(IncomeData, "SendBrokerContractDepoResult", "SendBrokerContractDepoResp");
         if (ValType(aux_buffResp) == V_GENOBJ)
            SendBrokerContractDepoResult = c_SendBrokerContractDepoResult();
            // очень длинные наименования, но разложим класс здесь, чтобы не усложнять
            SendBrokerContractDepoResult.ResultCode                 = getValueFromProperty(aux_buffResp, "ResultCode"                 , nametag_up, true);
            SendBrokerContractDepoResult.ContractNumber             = getValueFromProperty(aux_buffResp, "ContractNumber"             , nametag_up, true);
            SendBrokerContractDepoResult.DepoNumber                 = getValueFromProperty(aux_buffResp, "DepoNumber"                 , nametag_up, true);
            SendBrokerContractDepoResult.DepoDate                   = getValueFromProperty(aux_buffResp, "DepoDate"                   , nametag_up, true);
            SendBrokerContractDepoResult.DepoAccountNumber          = getValueFromProperty(aux_buffResp, "DepoAccountNumber"          , nametag_up, true);
            SendBrokerContractDepoResult.DepoAccountPartition       = getValueFromProperty(aux_buffResp, "DepoAccountPartition"       , nametag_up, true);
            SendBrokerContractDepoResult.DepoTradingAccountNumber   = getValueFromProperty(aux_buffResp, "DepoTradingAccountNumber"   , nametag_up);
            SendBrokerContractDepoResult.DepoTradingAccountPartition= getValueFromProperty(aux_buffResp, "DepoTradingAccountPartition", nametag_up);
            SendBrokerContractDepoResult.DepoTradingAccountPartitionSPBEX= getValueFromProperty(aux_buffResp, "DepoTradingAccountPartitionSPBEX", nametag_up);
         elif(ValType(aux_buffResp) == V_UNDEF)    // не обязательный
         else
            err_txt = string(InErrNotObj, "SendBrokerContractDepoResp.SendBrokerContractDepoResult");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
         end;
         aux_buffResp = NULL;
         
         aux_buffResp = getValueFromProperty(IncomeData, "ErrorList", "SendBrokerContractDepoResp");
         if (ValType(aux_buffResp) == V_GENOBJ)
            ErrorList = TArray;
            aux_buff = 0;
            while(IncomeData.ErrorList.size > aux_buff)
               ErrorList.value(aux_buff) = c_Error_Updatebrok(IncomeData.ErrorList.value(aux_buff));
               aux_buff = aux_buff + 1;
            end;
         else
            err_txt = string(InErrNotObj, "SendBrokerContractDepoResp.ErrorList");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
         end;
      else
         err_txt = string(InErrNotObj, "SendBrokerContractDepoResp");
         RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
      end;
   end;

   m_init_SendBrokerContractDepoResp(IncomeData);
end;

macro AddNotekind_depo(_objecttype, _objectid, _notekind, _notetext, _contractnumber)
   var stat = 0, err_txt = "";
   stat = addnoteforobject(_objecttype,_objectid,_notekind,_notetext);
   if (stat)
      err_txt = "Ошибка добавления примечания "+_notekind+"="+_notetext+" к договору "+_contractnumber;
      RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
   end;
end;

macro RunDepoInfo(IncomeData, err_txt:@string)
   var sql_str, cmd, rs;
   var new_status;
   var objecttype = 207, objectid, dlcontrid;
      
   sql_str = "select ddlcontr.t_dlcontrid, sfcontr.t_number, sfcontr.t_datebegin, part.t_shortname "+
             "  from ddlcontr_dbt ddlcontr, "+
             "       dsfcontr_dbt sfcontr, "+
             "       dparty_dbt part "+
             " where sfcontr.t_id = ddlcontr.T_SFCONTRID and sfcontr.T_PARTYID = part.T_PARTYID and sfcontr.t_number = :inparam ";

   cmd = rsdcommand(sql_str);
   cmd.AddParam("inparam", RSDBP_IN, IncomeData.ContractNumber);
   rs = RsdRecordset(cmd);
   if (rs.movenext) // договор должен быть один
      dlcontrid = rs.value("t_dlcontrid");
      objectid = String(dlcontrid:o:34);

      // перенесено в формирование запроса
      //SendShortNotification(dlcontrid, objecttype, objectid, rs.value("t_number"), rs.value("t_datebegin"), rs.value("t_shortname"));
      
      // Номер договора Депо
      AddNotekind_depo(objecttype, objectid, CONTRACT_NOTEKIND_Depo_ContractNumber, IncomeData.DepoNumber, IncomeData.ContractNumber); 
      // Дата заключения договора Депо
      AddNotekind_depo(objecttype, objectid, CONTRACT_NOTEKIND_Depo_ContractDate, IncomeData.DepoDate, IncomeData.ContractNumber); 
      // Счет Депо Владельца
      AddNotekind_depo(objecttype, objectid, CONTRACT_NOTEKIND_Depo_Account, IncomeData.DepoAccountNumber, IncomeData.ContractNumber); 
      //Раздел счёта депо владельца
      AddNotekind_depo(objecttype, objectid, CONTRACT_NOTEKIND_Depo_AccountPart, IncomeData.DepoAccountPartition, IncomeData.ContractNumber); 

      // необязательные параметры
      // Торговый счёт депо
      if (IncomeData.DepoTradingAccountNumber)
         AddNotekind_depo(objecttype, objectid, CONTRACT_NOTEKIND_Depo_TradeAccount, IncomeData.DepoTradingAccountNumber, IncomeData.ContractNumber); 
      end;
      // Раздел торгового счёта депо
      if (IncomeData.DepoTradingAccountPartition)
         AddNotekind_depo(objecttype, objectid, CONTRACT_NOTEKIND_Depo_TradeAccountPart, IncomeData.DepoTradingAccountPartition, IncomeData.ContractNumber); 
      end;

      // Раздел торгового счёта депо Спб
      if (IncomeData.DepoTradingAccountPartitionSPBEX)
         AddNotekind_depo(objecttype, objectid, CONTRACT_NOTEKIND_Depo_TradeAccountPart_Spb, IncomeData.DepoTradingAccountPartitionSPBEX, IncomeData.ContractNumber); 
      end;

      // статусная модель 
      UpdateStatusContractFromDepo(objectid);

   else 
      err_txt = "Не найден Договор БО: "+IncomeData.ContractNumber;
      return false;
   end;
   return true;

onError(err)
   err_txt = String(c_err_obj.obtain_err_msg(err)," Модуль: ", err.Module, " строка: ", err.Line, ". ",getFullCmdErrorText(cmd));
   return false;
end;


/*-------------*/
/* ТОЧКА ВХОДА */
/*-------------*/
macro SendBrokerContractDepoResp(p_income_data, p_log_id, p_jmsmessageid)
   var v_logger_obj;
   var v_income_data;
   var v_aux_buf;
   var temp_error;
   var v_cnt_error, ai=0;
   
   var vresulttext= "", vresultcode; 

   var v_email_processor;
   var v_email_head;
   var v_email_body;

   var parent_eventid = 0, parent_ulog_id = 0, temp_str = "", pos = 0; 
   var sql_str, cmd, rs;
   var attempts = 0, attempts_def = 3, stat;
   var cntr_num;
   
   
   /* Получаем проверенный объект со входными данными */
   v_income_data = c_SendBrokerContractDepoResp(p_income_data);

   v_logger_obj = uws_exchng_logger();
   v_logger_obj.set_log_id(p_log_id);
   
   vresultcode = v_income_data.ErrorList.value(0).ErrorCode;   // возьмем из первого элемента
   
   if (vresultcode == "0")  // успешно
      vresulttext = "OK";
      
      if (not RunDepoInfo(v_income_data.SendBrokerContractDepoResult, @vresulttext))
         vresultcode = "-1";
      end;
   else
      /* Описание ошибок */
      temp_error = v_income_data.ErrorList.value(0); 
      vresulttext = String("Получены ошибки: ",temp_error.ErrorCode,"-",temp_error.ErrorDesc);

      v_cnt_error = v_income_data.ErrorList.size();
      if (v_cnt_error > 1) // если ошибок больше одной
         for (ai, 1, v_cnt_error-1)
            temp_error = v_income_data.ErrorList.value(ai);
            vresulttext = String(vresulttext,"|",temp_error.ErrorCode,"-",temp_error.ErrorDesc);
         end;
      end;
   end;

   // найдем исходное событие
   sql_str = "select l_in.t_jmsmessageid in_jmsmessageid, l_out.t_jmsmessageid out_jmsmessageid, l_out.t_jmscorrelationid user_id"+
             "  from dqueue_log_dbt l_in, "+
             "       dqueue_log_dbt l_out "+
             " where "+
             "     l_in.t_jmsmessageid = :p_jmsmessageid "+
             " and l_in.t_qname = 'ESB_TO_SOFR' "+
             " and l_out.t_jmsmessageid = l_in.t_jmscorrelationid "+
             " and rownum = 1; ";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("p_jmsmessageid", RSDBP_IN, p_jmsmessageid);
   rs = RSDRecordSet(cmd);
   if (rs.movenext)
      temp_str = rs.value("user_id");
   else 
      temp_str = "";
   end;
   pos = Index(temp_str, "-");
   if (pos > 0)
      parent_eventid = substr(temp_str,1,pos-1);
      parent_ulog_id = substr(temp_str, pos+1);
   end;
   rs.close; cmd.close; rs = null; cmd = null;
   
   /* Сохраняем в логи */
   // текущий запрос
   v_aux_buf = strlen(vresulttext);
   if(v_aux_buf > 0)
      if(v_aux_buf >= 2000)
         vresulttext = substr(vresulttext, 1, 1999);
      end;
      v_logger_obj.add_error_info(vresultcode, vresulttext);
   end;

   v_logger_obj = NULL;

   // исходный запрос
/*   if (parent_ulog_id > 0)
      v_logger_obj = uws_exchng_logger();
      v_logger_obj.set_log_id(parent_ulog_id);
      v_logger_obj.add_response(v_result_obj.ResponseText, null, null, parent_ulog_id, v_module_name);
      v_logger_obj = NULL;
   end; */

   // исходное событие
   if (parent_eventid > 0)
      var ProcessEventTbl = c_usr_tbl_uTableProcessEvent();
      ProcessEventTbl.New_Val_Obj.T_RESULTCODE = vresultcode;
      v_aux_buf = strlen(vresulttext);
      if(v_aux_buf > 0)
         if(v_aux_buf >= 1000)
            vresulttext = substr(vresulttext, 1, 999);
         end;
      end;
      ProcessEventTbl.New_Val_Obj.T_RESULTTEXT = vresulttext;
      ProcessEventTbl.Where_Val_Obj.T_RECID = parent_eventid;

      if (vresultcode != "0")
         // количество попыток
         sql_str = " SELECT nvl(t_attempts, 1) attempt, nvl(sf.t_number,u.t_objectid) cntr_num "+      
                   "   FROM utableprocessevent_dbt u,                                          "+
                   "        ddlcontr_dbt dl,                                                   "+
                   "        dsfcontr_dbt sf                                                    "+
                   "  WHERE T_RECID = :r                                                       "+
                   "    AND u.t_objectid = dl.t_dlcontrid (+)                                  "+
                   "    AND sf.t_id (+)= dl.t_sfcontrid                                        ";
         cmd = RSDCommand(sql_str);
         cmd.addParam("r", RSDBP_IN, parent_eventid);
         rs = RSDRecordSet(cmd);
         if (rs.movenext());
            attempts = Int(rs.value("attempt"));
            cntr_num = rs.value("cntr_num");
         end;
                                                                                                            
         GetRegistryValue("РСХБ\\ИНТЕГРАЦИЯ\\CONTR_REPEAT", V_INTEGER, attempts_def, stat);
         if (stat>0)
            attempts_def = 3;
         end;

         if (attempts <= attempts_def)
            ProcessEventTbl.New_Val_Obj.T_STATUS = 1;  // повторяем обработку
            ProcessEventTbl.New_Val_Obj.T_ATTEMPTS = attempts+1;
         else
            ProcessEventTbl.New_Val_Obj.T_STATUS = 5;        

            // попробуем  отправить почту с ошибкой
            v_email_processor = c_email_proc_env();
            v_email_head = "СОФР. Ошибка обновления информации в Депозитарии";
            // откуда бы взять номер договора, если тег необязательный
            v_email_body = String("По договору "+cntr_num+" произошла ошибка обновления информации в Депозитарии: ",
                                  vresulttext );
            v_email_processor.m_set_msg_head(v_email_head);
            v_email_processor.m_add_row_to_msg_text(v_email_body);
            v_email_processor.m_save_to_submit_grp(CONTRACT_EMAIL_GRP, true);    // использовать группу рассылки BrokerContract 
            v_email_processor.m_submit_email_synch();
         end;
      else
         ProcessEventTbl.New_Val_Obj.T_STATUS = 4; // ошибок нет       
      end;
      ProcessEventTbl.Update();

   end;

   return true;

onError(err)
   v_logger_obj.add_error_info(c_usr_err_obj.obtain_err_code(err), c_usr_err_obj.obtain_err_msg(err));

   return false;
end;