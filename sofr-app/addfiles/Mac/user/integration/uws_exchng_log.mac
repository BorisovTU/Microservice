/*-------------------------------------------------------*/
/* Инструментарий для логирования сообщений Веб-сервисов */
/*                                                       */
/* Автор: Карпов В.                                      */
/*-------------------------------------------------------*/

import rsd;

class uws_exchng_logger(v_is_short_log,   /* Признак короткого лога */
                        v_reqid,          /* Идентификатор запроса */
                        v_reqid_linklog,  /* Идентификатор запроса в связанном журнале */
                        v_module,         /* Название модуля, от имени которого осуществляется взаимодействие */
                        v_by_macro,       /* Название макроса, который выполняет сохранение сообщений */
                        v_transparent_id, /* Сквозной идентификатор сообщений */
                        v_req_message,    /* Сообщение запроса */
                        v_resp_message,   /* Сообщение ответа */
                        v_error_code,     /* Код ошибки */
                        v_error_msg)      /* Текст ошибки */

   private const MSG_LINE_LENGTH = 1990; /* Длина части строки */
   private const MSG_EMPTY_TEXT = "Изначально пустой текст"; /* Сообщение для записи в лог для случая передачи пустой строки */

   private const REQ_LINE_NAME = "req_line";   /* Основная часть названия параметров отдельных частей сообщения запроса */
   private const RESP_LINE_NAME = "resp_line"; /* Основная часть названия параметров отдельных частей сообщения ответа */

   private var service_msg;   /* Текст служебного сообщения */
   private var req_str_list;  /* Массив строк сообщения запроса */
   private var resp_str_list; /* Массив строк сообщения ответа */

   private var p_is_short_log;    /* Признак короткого лога */
   private var P_IS_PRIME_BEFORE; /* Признак инициализации записи журнала */

   private var p_id;           /* Уникальный идентификатор записи */
   private var p_req_message;  /* Запрос */
   private var p_resp_message; /* Ответ */

   var p_reqid;          /* Идентификатор запроса */
   var p_module;         /* Название модуля, от имени которого осуществляется взаимодействие */
   var p_by_macro;       /* Название макроса, который выполняет сохранение сообщений */
   var p_transparent_id; /* Сквозной идентификатор сообщений */
   var p_error_code;     /* Код ошибки */
   var p_error_msg;      /* Текст ошибки */
   var p_reqid_linklog;  /* Ид запроса в связанном журнале */

   /*-----------------------------------------------*/
   /* Метод обработки входного текстового параметра */
   /*-----------------------------------------------*/
   private macro proc_in_text_prm(text_prm)
      var ret;
      var data_type = ValType(text_prm);

      if(data_type != V_UNDEF)
         if(data_type == V_STRING)
            ret = text_prm;
         else
            ret = string(text_prm);
         end;
      else
         ret = "";
      end;

      return ret;
   end; /* macro proc_in_text_prm() */

   /*---------------------------------------------*/
   /* Метод получения текста сервисного сообщения */
   /*---------------------------------------------*/
   macro get_service_msg()
      return service_msg;
   end;

   /*-----------------------------------------*/
   /* Метод получения признака короткого лога */
   /*-----------------------------------------*/
   macro get_is_short_log()
      return p_is_short_log;
   end;

   /*---------------------------------------------------*/
   /* Метод получения идентификатора сохраненной строки */
   /*---------------------------------------------------*/
   macro get_log_id()
      return p_id;
   end;

   /*-------------------------------------------------*/
   /* Метод получения массива строк сообщения запроса */
   /*-------------------------------------------------*/
   macro get_req_str_array()
      return req_str_list;
   end;

   /*------------------------------------------------*/
   /* Метод получения массива строк сообщения ответа */
   /*------------------------------------------------*/
   macro get_resp_str_array()
      return resp_str_list;
   end;

   /*-----------------------------------------*/
   /* Метод установки признака короткого лога */
   /*-----------------------------------------*/
   macro set_is_short_log(v_is_short_log)
      if(ValType(v_is_short_log) != V_UNDEF)
         p_is_short_log = v_is_short_log;
      end;
   end;

   /*-------------------------------------------------------------*/
   /* Метод установки значения идентификатора сообщения в журнале */
   /*-------------------------------------------------------------*/
   macro set_log_id(v_log_id)
      var ret = false;
      var sql, cmd, rs;
      service_msg = "";

      if(ValType(v_log_id) != V_UNDEF)
         sql = "SELECT t_id FROM usr_exchng_log_dbt WHERE t_id = :v_log_id ";
         cmd = RSDCommand(sql);
         cmd.addParam("v_log_id", RSDBP_IN, v_log_id);
         rs = RSDRecordSet(cmd);
         if(rs.movenext())
            p_id = v_log_id;
            ret = true;
         else
            service_msg = string("Запись с идентификатором ", v_log_id, " не найдена");
         end;

         rs.close(); cmd.close(); rs = null; cmd = null;
      else
         service_msg = "Идентификатор записи не задан";
      end;

      return ret;
   end; /* macro set_log_id() */

   /*---------------------------------------------------------*/
   /* Проверка наличия информации об идентификаторе сообщения */
   /*---------------------------------------------------------*/
   private macro check_log_id(v_log_id)
      var ret;

      if(ValType(v_log_id) != V_UNDEF)
         if(set_log_id(v_log_id))
            ret = true;
         else
            ret = false;
         end;
      else
         if(ValType(p_id) != V_UNDEF)
            ret = true;
         else
            ret = false;
         end;
      end;

      return ret;
   end; /* macro check_log_id() */

   /*-----------------------------------------------------------------------------------*/
   /* Преобразовать строку запроса в массив строк фиксированной (MSG_LINE_LENGTH) длины */
   /*-----------------------------------------------------------------------------------*/
   macro make_req_str_array(v_req_message)
      if(ValType(v_req_message) != V_UNDEF)
         p_req_message = v_req_message;
      end;

      if(strlen(p_req_message) != 0)
         req_str_list = TArray(StrSplit2(p_req_message, MSG_LINE_LENGTH, true));
      end;
   end; /* macro make_req_str_array() */

   /*----------------------------------------------------------------------------------*/
   /* Преобразовать строку ответа в массив строк фиксированной (MSG_LINE_LENGTH) длины */
   /*----------------------------------------------------------------------------------*/
   macro make_resp_str_array(v_resp_message)
      if(ValType(v_resp_message) != V_UNDEF)
         p_resp_message = v_resp_message;
      end;

      if(strlen(p_resp_message) != 0)
         resp_str_list = TArray(StrSplit2(p_resp_message, MSG_LINE_LENGTH, true));
      end;
   end; /* macro make_resp_str_array() */

   /*--------------------------------------*/
   /* Обработка полученного текста запроса */
   /*--------------------------------------*/
   private macro proc_src_req(v_req_message)
      if(ValType(v_req_message) != V_UNDEF)
         p_req_message = v_req_message;
         make_req_str_array();
      else
         if((req_str_list.size == 0) and (strlen(p_req_message) != 0))
            make_req_str_array();
         end;
      end;
   end; /* macro proc_src_req() */

   /*-------------------------------------*/
   /* Обработка полученного текста ответа */
   /*-------------------------------------*/
   private macro proc_src_resp(v_resp_message)
      if(ValType(v_resp_message) != V_UNDEF)
         p_resp_message = v_resp_message;
         make_resp_str_array();
      else
         if((resp_str_list.size == 0) and (strlen(p_resp_message) != 0))
            make_resp_str_array();
         end;
      end;
   end; /* macro proc_src_resp() */

   /*-----------------------------------------------------------------*/
   /* Метод получения текста анонимного блока вставки сообщения в лог */
   /*-----------------------------------------------------------------*/
   private macro make_insert_sql_block()
      var ret;
      var aux_buff;
      var line_cntr;

      ret =       "DECLARE ";
      ret = ret + "    PRAGMA autonomous_transaction; "; /* Для записи лога внутри транзакции */
      ret = ret + "    TYPE MSG_LINE IS TABLE OF VARCHAR2(2000 char); ";
      ret = ret + "    raw_req_str MSG_LINE; ";
      ret = ret + "    temp_req_clob CLOB; ";

      if(resp_str_list.size != 0)
         ret = ret + " raw_resp_str MSG_LINE; ";
         ret = ret + " temp_resp_clob CLOB; ";
      end;

      ret = ret + "    EXCEPTION_CODE NUMBER DEFAULT 0; ";
      ret = ret + "BEGIN ";
      ret = ret + "    raw_req_str := MSG_LINE(";

      line_cntr = 0;

      if(req_str_list.size == 0)
         ret = ret + string(":", REQ_LINE_NAME, line_cntr);
      else
         while(line_cntr < req_str_list.size)
            if(line_cntr > 0)
               ret = ret + ",";
            end;

            ret = ret + string(":", REQ_LINE_NAME, line_cntr);

            line_cntr = line_cntr + 1;
         end;
      end;
      ret = ret + "); ";

      if(resp_str_list.size != 0)
         ret = ret + " raw_resp_str := MSG_LINE(";
         line_cntr = 0;
         while(line_cntr < resp_str_list.size)
            if(line_cntr > 0)
               ret = ret + ",";
            end;

            ret = ret + string(":", RESP_LINE_NAME, line_cntr);

            line_cntr = line_cntr + 1;
         end;
         ret = ret + "); ";
      end;

      ret = ret + "    DBMS_LOB.CREATETEMPORARY(lob_loc => temp_req_clob, ";
      ret = ret + "                             cache => true, ";
      ret = ret + "                             dur => DBMS_LOB.SESSION); ";

      ret = ret + "    IF DBMS_LOB.ISOPEN(temp_req_clob)=0 ";
      ret = ret + "    THEN ";
      ret = ret + "        DBMS_LOB.OPEN(temp_req_clob, DBMS_LOB.LOB_READWRITE); ";
      ret = ret + "    END IF; ";

      ret = ret + "    FOR i IN raw_req_str.FIRST.. raw_req_str.LAST ";
      ret = ret + "    LOOP ";
      ret = ret + "        DBMS_LOB.WRITEAPPEND(temp_req_clob, DBMS_LOB.GETLENGTH(raw_req_str(i)), raw_req_str(i)); ";
      ret = ret + "    END LOOP; ";

      if(resp_str_list.size != 0)
         ret = ret + " DBMS_LOB.CREATETEMPORARY(lob_loc => temp_resp_clob, ";
         ret = ret + "                          cache => true, ";
         ret = ret + "                          dur => DBMS_LOB.SESSION); ";

         ret = ret + " IF DBMS_LOB.ISOPEN(temp_resp_clob)=0 ";
         ret = ret + " THEN ";
         ret = ret + "     DBMS_LOB.OPEN(temp_resp_clob, DBMS_LOB.LOB_READWRITE); ";
         ret = ret + " END IF; ";

         ret = ret + " FOR i IN raw_resp_str.FIRST.. raw_req_str.LAST ";
         ret = ret + " LOOP ";
         ret = ret + "     DBMS_LOB.WRITEAPPEND(temp_resp_clob, DBMS_LOB.GETLENGTH(raw_resp_str(i)), raw_resp_str(i)); ";
         ret = ret + " END LOOP; ";
      end;

      ret = ret + "    INSERT INTO USR_EXCHNG_LOG_DBT(T_ID";
      aux_buff = "          VALUES (0";
      if(strlen(p_reqid) != 0)
         ret = ret + ", T_REQID";
         aux_buff = aux_buff + ", :p_reqid";
      end;
      if(strlen(p_module) != 0)
         ret = ret + ", T_MODULE";
         aux_buff = aux_buff + ", :p_module";
      end;
      if(strlen(p_by_macro) != 0)
         ret = ret + ", T_BY_MACRO";
         aux_buff = aux_buff + ", :p_by_macro";
      end;
      if(strlen(p_transparent_id) != 0)
         ret = ret + ", T_TRANSPARENT_ID";
         aux_buff = aux_buff + ", :p_transparent_id";
      end;
      ret = ret + ", T_ERROR_CODE";
      if(strlen(p_error_code) != 0)
         aux_buff = aux_buff + ", :p_error_code";
      else
         aux_buff = aux_buff + ", '0'";
      end;
      if(strlen(p_error_msg) != 0)
         ret = ret + ", T_ERROR_MSG";
         aux_buff = aux_buff + ", :p_error_msg";
      end;

      if((strlen(p_reqid_linklog) != 0) and (strupr(p_reqid_linklog) != "UNDEFINED"))
        ret = ret + ", T_REQID_LINKED";
        aux_buff = aux_buff + ", :p_reqid_linklog";
      end;

      ret = ret + ", T_REQ_DTTM, T_REQ_MESSAGE";
      aux_buff = aux_buff + ", systimestamp, temp_req_clob";

      if(resp_str_list.size != 0)
         ret = ret + ", T_RESP_MESSAGE) ";
         aux_buff = aux_buff + ", temp_resp_clob) ";
      else
         ret = ret + ") ";
         aux_buff = aux_buff + ") ";
      end;

      ret = ret + aux_buff;
      ret = ret + " RETURNING T_ID INTO :p_id; ";

      ret = ret + "    IF DBMS_LOB.ISOPEN(temp_req_clob)=1 ";
      ret = ret + "    THEN ";
      ret = ret + "        DBMS_LOB.CLOSE(temp_req_clob); ";
      ret = ret + "    END IF; ";

      ret = ret + "    DBMS_LOB.FREETEMPORARY(lob_loc => temp_req_clob); ";

      if(resp_str_list.size != 0)
         ret = ret + " IF DBMS_LOB.ISOPEN(temp_resp_clob)=1 ";
         ret = ret + " THEN ";
         ret = ret + "     DBMS_LOB.CLOSE(temp_resp_clob); ";
         ret = ret + " END IF; ";

         ret = ret + " DBMS_LOB.FREETEMPORARY(lob_loc => temp_resp_clob); ";
      end;

      ret = ret + "    COMMIT; "; /* Выполняется фиксация данных только для изменений данного анонимного блока */

      ret = ret + "EXCEPTION ";
      ret = ret + "    WHEN OTHERS ";
      ret = ret + "    THEN ";
      ret = ret + "        EXCEPTION_CODE := SQLCODE; ";
      ret = ret + "        :p_result := EXCEPTION_CODE; ";
      ret = ret + "END; ";

      return ret;
   end; /* macro make_insert_sql_block() */

   /*-------------------------*/
   /* Сохранение записи в лог */
   /*-------------------------*/
   macro save_row_to_log(v_req_message, v_resp_message)
      var ret = true;
      var sql, cmd;
      var line_cntr;
      service_msg = "";

      proc_src_req(v_req_message);

      proc_src_resp(v_resp_message);

      sql = make_insert_sql_block();
      cmd = RSDCommand(sql);

      line_cntr = 0;

      if(req_str_list.size == 0)
         cmd.addParam(string(REQ_LINE_NAME, line_cntr), RSDBP_IN, MSG_EMPTY_TEXT);
      else
         while(line_cntr < req_str_list.size)
            cmd.addParam(string(REQ_LINE_NAME, line_cntr), RSDBP_IN, req_str_list.value(line_cntr));

            line_cntr = line_cntr + 1;
         end;
      end;

      line_cntr = 0;

      while(line_cntr < resp_str_list.size)
         cmd.addParam(string(RESP_LINE_NAME, line_cntr), RSDBP_IN, resp_str_list.value(line_cntr));

         line_cntr = line_cntr + 1;
      end;

      if(strlen(p_reqid) != 0)
         cmd.addParam("p_reqid", RSDBP_IN, p_reqid);
      end;
      if(strlen(p_module) != 0)
         cmd.addParam("p_module", RSDBP_IN, p_module);
      end;
      if(strlen(p_by_macro) != 0)
         cmd.addParam("p_by_macro", RSDBP_IN, p_by_macro);
      end;
      if(strlen(p_transparent_id) != 0)
         cmd.addParam("p_transparent_id", RSDBP_IN, p_transparent_id);
      end;
      if(strlen(p_error_code) != 0)
         cmd.addParam("p_error_code", RSDBP_IN, p_error_code);
      end;
      if(strlen(p_error_msg) != 0)
         cmd.addParam("p_error_msg", RSDBP_IN, p_error_msg);
      end;
      if((strlen(p_reqid_linklog) != 0) and (strupr(p_reqid_linklog) != "UNDEFINED"))
        cmd.addParam("p_reqid_linklog", RSDBP_IN, p_reqid_linklog);
      end;

      cmd.addParam("p_id", RSDBP_OUT, V_INTEGER);
      cmd.addParam("p_result", RSDBP_OUT, V_INTEGER);

      cmd.execute();

      if(cmd.value("p_result") == 0)
         p_id = cmd.value("p_id");
      else
         service_msg = string("При вставке записи произошла ошибка: SQLCODE=", cmd.value("p_result"));
         ret = false;
      end;

      cmd.close(); cmd = null; sql = null;

      return ret;

      onError(err)      
         service_msg = "Произошла ошибка при записи в журнал";
         ret = false;
         return ret;
   end; /* macro save_row_to_log() */

   /*----------------------------------------------------------------------------------------*/
   /* Метод получения текста анонимного блока добавления текста ответного сообщения к записи */
   /*----------------------------------------------------------------------------------------*/
   private macro make_add_sql_block()
      var ret;
      var line_cntr;

      ret =       "DECLARE ";
      ret = ret + "    PRAGMA autonomous_transaction; "; /* Для записи лога внутри транзакции */
      ret = ret + "    TYPE MSG_LINE IS TABLE OF VARCHAR2(2000 char); ";
      ret = ret + "    raw_resp_str MSG_LINE; ";
      ret = ret + "    temp_resp_clob CLOB; ";
      ret = ret + "    EXCEPTION_CODE NUMBER DEFAULT 0; ";
      ret = ret + "    v_id USR_EXCHNG_LOG_DBT.T_ID%TYPE := :p_id; ";
      ret = ret + "    v_error_code USR_EXCHNG_LOG_DBT.T_ERROR_CODE%TYPE; ";
      ret = ret + "BEGIN ";
      ret = ret + "    raw_resp_str := MSG_LINE(";

      line_cntr = 0;

      if(resp_str_list.size == 0)
         ret = ret + string(":", RESP_LINE_NAME, line_cntr);
      else
         while(line_cntr < resp_str_list.size)
            if(line_cntr > 0)
               ret = ret + ",";
            end;

            ret = ret + string(":", RESP_LINE_NAME, line_cntr);

            line_cntr = line_cntr + 1;
         end;
      end;
      ret = ret + "); ";

      ret = ret + "    DBMS_LOB.CREATETEMPORARY(lob_loc => temp_resp_clob, ";
      ret = ret + "                             cache => true, ";
      ret = ret + "                             dur => DBMS_LOB.SESSION); ";

      ret = ret + "    IF DBMS_LOB.ISOPEN(temp_resp_clob)=0 ";
      ret = ret + "    THEN ";
      ret = ret + "        DBMS_LOB.OPEN(temp_resp_clob, DBMS_LOB.LOB_READWRITE); ";
      ret = ret + "    END IF; ";

      ret = ret + "    FOR i IN raw_resp_str.FIRST.. raw_resp_str.LAST ";
      ret = ret + "    LOOP ";
      ret = ret + "        DBMS_LOB.WRITEAPPEND(temp_resp_clob, DBMS_LOB.GETLENGTH(raw_resp_str(i)), raw_resp_str(i)); ";
      ret = ret + "    END LOOP; ";

      ret = ret + "    SELECT T_ID, DECODE(T_ERROR_CODE, CHR(1), '0', T_ERROR_CODE) ";
      ret = ret + "      INTO v_id, v_error_code ";
      ret = ret + "      FROM USR_EXCHNG_LOG_DBT WHERE T_ID = v_id; ";

      ret = ret + "    UPDATE USR_EXCHNG_LOG_DBT ";
      ret = ret + "       SET T_RESP_MESSAGE = temp_resp_clob ";
      ret = ret + "          ,T_RESP_DTTM = systimestamp ";
      if(strlen(p_error_code) != 0)
         ret = ret + "       ,T_ERROR_CODE = :p_error_code ";
      else
         ret = ret + "       ,T_ERROR_CODE = v_error_code ";
      end;
      if(strlen(p_error_msg) != 0)
         ret = ret + "       ,T_ERROR_MSG = :p_error_msg ";
      end;
      if(strlen(p_module) != 0)
         ret = ret + "       ,T_MODULE = :p_module ";
      end;
      if(strlen(p_reqid) != 0)
         ret = ret + "       ,T_REQID = :p_reqid ";
      end;
      ret = ret + "     WHERE T_ID = v_id; ";

      ret = ret + "    IF DBMS_LOB.ISOPEN(temp_resp_clob)=1 ";
      ret = ret + "    THEN ";
      ret = ret + "        DBMS_LOB.CLOSE(temp_resp_clob); ";
      ret = ret + "    END IF; ";

      ret = ret + "    DBMS_LOB.FREETEMPORARY(lob_loc => temp_resp_clob); ";

      ret = ret + "    COMMIT; "; /* Выполняется фиксация данных только для изменений данного анонимного блока */

      ret = ret + "EXCEPTION ";
      ret = ret + "    WHEN OTHERS ";
      ret = ret + "    THEN ";
      ret = ret + "        EXCEPTION_CODE := SQLCODE; ";
      ret = ret + "        :p_result := EXCEPTION_CODE; ";
      ret = ret + "END; ";

      return ret;
   end; /* macro make_add_sql_block() */

   /*------------------------------------------------*/
   /* Добавление текста ответного сообщения к записи */
   /*------------------------------------------------*/
   macro add_response(v_resp_message, v_error_code, v_error_msg, v_log_id, v_module, v_reqid)
      var ret = true;
      var sql, cmd;
      var line_cntr;
      var valid_log_id;
      service_msg = "";

      valid_log_id = check_log_id(v_log_id);

      if(valid_log_id)
         proc_src_resp(v_resp_message);

         p_error_code = proc_in_text_prm(v_error_code);

         p_error_msg = proc_in_text_prm(v_error_msg);

         p_module = proc_in_text_prm(v_module);

         p_reqid = proc_in_text_prm(v_reqid);

         sql = make_add_sql_block();
         cmd = RSDCommand(sql);

         line_cntr = 0;

         cmd.addParam("p_id", RSDBP_IN, p_id);

         if(resp_str_list.size == 0)
            cmd.addParam(string(RESP_LINE_NAME, line_cntr), RSDBP_IN, MSG_EMPTY_TEXT);
         else
            while(line_cntr < resp_str_list.size)
               cmd.addParam(string(RESP_LINE_NAME, line_cntr), RSDBP_IN, resp_str_list.value(line_cntr));

               line_cntr = line_cntr + 1;
            end;
         end;
         if(strlen(p_error_code) != 0)
            cmd.addParam("p_error_code", RSDBP_IN, p_error_code);
         end;
         if(strlen(p_error_msg) != 0)
            cmd.addParam("p_error_msg", RSDBP_IN, p_error_msg);
         end;
         if(strlen(p_module) != 0)
            cmd.addParam("p_module", RSDBP_IN, p_module);
         end;
         if(strlen(p_reqid) != 0)
            cmd.addParam("p_reqid", RSDBP_IN, p_reqid);
         end;

         cmd.addParam("p_result", RSDBP_OUT, V_INTEGER);

         cmd.execute();

         if(cmd.value("p_result") != 0)
            service_msg = string("При добавлении ответного сообщения к записи произошла ошибка: SQLCODE=", cmd.value("p_result"));
            ret = false;
         end;

         cmd.close(); cmd = null; sql = null;
      else
         if(strlen(service_msg) == 0)
            service_msg = "Идентификатор записи не задан";
         end;
         ret = false;
      end;

      return ret;

      onError(err)
         service_msg = "Произошла ошибка при добавлении ответного сообщения к записи";
         ret = false;
         return ret;
   end; /* macro add_response() */

   /*------------------------------------------*/
   /* Добавление информации об ошибке к записи */
   /*------------------------------------------*/
   macro add_error_info(v_error_code, v_error_msg, v_log_id, v_module)
      var ret = true;
      var sql, cmd;
      var valid_log_id;
      service_msg = "";

      valid_log_id = check_log_id(v_log_id);

      if(valid_log_id)
         p_error_code = proc_in_text_prm(v_error_code);

         p_error_msg = proc_in_text_prm(v_error_msg);

         p_module = proc_in_text_prm(v_module);

         if((strlen(p_error_code) != 0) or (strlen(p_error_msg) != 0))

            sql =       "DECLARE ";
            sql = sql + "    PRAGMA autonomous_transaction; "; /* Для записи лога внутри транзакции */
            sql = sql + "    EXCEPTION_CODE NUMBER DEFAULT 0; ";
            sql = sql + "BEGIN ";
            sql = sql + "    UPDATE USR_EXCHNG_LOG_DBT ";
            if(strlen(p_error_code) != 0)
               sql = sql + "    SET T_ERROR_CODE = :p_error_code ";
            else
               sql = sql + "    SET T_ERROR_CODE = '0' ";
            end;
            if(strlen(p_error_msg) != 0)
               sql = sql + "       ,T_ERROR_MSG = :p_error_msg ";
            end;
            if(strlen(p_module) != 0)
               sql = sql + "       ,T_MODULE = :p_module ";
            end;
            if(p_is_short_log)
               if(P_IS_PRIME_BEFORE) /* Если предыдущей операцией была инициализация записи журнала */
                  sql = sql + "    ,T_RESP_DTTM = systimestamp ";
                  P_IS_PRIME_BEFORE = false; /* Сбрасываем признак, чтобы при сохранении информации об ошибке не перетерлось время получения ответа */
               end;
            end;
            sql = sql + "     WHERE T_ID = :p_id; ";

            sql = sql + "    COMMIT; "; /* Выполняется фиксация данных только для изменений данного анонимного блока */

            sql = sql + "EXCEPTION ";
            sql = sql + "    WHEN OTHERS ";
            sql = sql + "    THEN ";
            sql = sql + "        EXCEPTION_CODE := SQLCODE; ";
            sql = sql + "        :p_result := EXCEPTION_CODE; ";
            sql = sql + "END; ";
            cmd = RSDCommand(sql);

            if(strlen(p_error_code) != 0)
               cmd.addParam("p_error_code", RSDBP_IN, p_error_code);
            end;
            if(strlen(p_error_msg) != 0)
               cmd.addParam("p_error_msg", RSDBP_IN, p_error_msg);
            end;
            if(strlen(p_module) != 0)
               cmd.addParam("p_module", RSDBP_IN, p_module);
            end;

            cmd.addParam("p_id", RSDBP_IN, p_id);
            cmd.addParam("p_result", RSDBP_OUT, V_INTEGER);
            cmd.execute();

            if(cmd.value("p_result") != 0)
               service_msg = string("При добавлении информации о коде возврата к записи произошла ошибка: SQLCODE=", cmd.value("p_result"));
               ret = false;
            end;

            cmd.close(); cmd = null; sql = null;
         else
            service_msg = "Отсутствуют данные для сохранения";
         end;
      else
         if(strlen(service_msg) == 0)
            service_msg = "Идентификатор записи не задан";
         end;
         ret = false;
      end;

      return ret;

      onError(err)
         service_msg = "Произошла ошибка при добавлении информации о коде возврата";
         ret = false;
         return ret;
   end; /* macro add_error_info() */

   /*--------------------------------------------------------*/
   /* Формирование запроса для вставки короткой записи в лог */
   /*--------------------------------------------------------*/
   private macro make_insert_short_sql_block()
      var ret;
      var aux_buff;

      ret =       "DECLARE ";
      ret = ret + "    PRAGMA autonomous_transaction; "; /* Для записи лога внутри транзакции */
      ret = ret + "    EXCEPTION_CODE NUMBER DEFAULT 0; ";
      ret = ret + "BEGIN ";
      ret = ret + "    INSERT INTO USR_EXCHNG_LOG_DBT(T_ID, T_REQ_DTTM";
      aux_buff =  "         VALUES (0, systimestamp";

      if(strlen(p_reqid) != 0)
         ret = ret + ", T_REQID";
         aux_buff = aux_buff + ", :p_reqid";
      end;
      if(strlen(p_module) != 0)
         ret = ret + ", T_MODULE";
         aux_buff = aux_buff + ", :p_module";
      end;
      if(strlen(p_by_macro) != 0)
         ret = ret + ", T_BY_MACRO";
         aux_buff = aux_buff + ", :p_by_macro";
      end;
      if(strlen(p_transparent_id) != 0)
         ret = ret + ", T_TRANSPARENT_ID";
         aux_buff = aux_buff + ", :p_transparent_id";
      end;
      ret = ret + ", T_ERROR_CODE";
      if(strlen(p_error_code) != 0)
         aux_buff = aux_buff + ", :p_error_code";
      else
         aux_buff = aux_buff + ", '0'";
      end;
      if(strlen(p_error_msg) != 0)
         ret = ret + ", T_ERROR_MSG";
         aux_buff = aux_buff + ", :p_error_msg";
      end;
      if(strlen(p_reqid_linklog) != 0)
         ret = ret + ", T_REQID_LINKED";
         aux_buff = aux_buff + ", :p_reqid_linklog";
      end;
      ret = ret + ") ";
      aux_buff = aux_buff + ") ";

      ret = ret + aux_buff;
      ret = ret + " RETURNING T_ID INTO :p_id; ";
      ret = ret + " COMMIT; "; /* Выполняется фиксация данных только для изменений данного анонимного блока */

      ret = ret + "EXCEPTION ";
      ret = ret + "    WHEN OTHERS ";
      ret = ret + "    THEN ";
      ret = ret + "        EXCEPTION_CODE := SQLCODE; ";
      ret = ret + "        :p_result := EXCEPTION_CODE; ";
      ret = ret + "END; ";

      return ret;
   end; /* macro make_insert_short_sql_block() */

   /*----------------------------------*/
   /* Сохранение короткой записи в лог */
   /*----------------------------------*/
   macro save_short_row_to_log()
      var ret = true;
      var sql, cmd;
      service_msg = "";

      sql = make_insert_short_sql_block();
      cmd = RSDCommand(sql);

      if(strlen(p_reqid) != 0)
         cmd.addParam("p_reqid", RSDBP_IN, p_reqid);
      end;
      if(strlen(p_module) != 0)
         cmd.addParam("p_module", RSDBP_IN, p_module);
      end;
      if(strlen(p_by_macro) != 0)
         cmd.addParam("p_by_macro", RSDBP_IN, p_by_macro);
      end;
      if(strlen(p_transparent_id) != 0)
         cmd.addParam("p_transparent_id", RSDBP_IN, p_transparent_id);
      end;
      if(strlen(p_error_code) != 0)
         cmd.addParam("p_error_code", RSDBP_IN, p_error_code);
      end;
      if(strlen(p_error_msg) != 0)
         cmd.addParam("p_error_msg", RSDBP_IN, p_error_msg);
      end;
      if(strlen(p_reqid_linklog) != 0)
         cmd.addParam("p_reqid_linklog", RSDBP_IN, p_reqid_linklog);
      end;

      cmd.addParam("p_id", RSDBP_OUT, V_INTEGER);
      cmd.addParam("p_result", RSDBP_OUT, V_INTEGER);

      cmd.execute();

      if(cmd.value("p_result") == 0)
         p_id = cmd.value("p_id");
         P_IS_PRIME_BEFORE = true; /* Устанавливаем признак инициализации записи журнала */
      else
         service_msg = string("При вставке записи произошла ошибка: SQLCODE=", cmd.value("p_result"));
         ret = false;
      end;

      cmd.close(); cmd = null; sql = null;

      return ret;

      onError(err)
         service_msg = "Произошла ошибка при записи в журнал";
         ret = false;
         return ret;
   end; /* macro save_short_row_to_log() */

   /*-------------*/
   /* Конструктор */
   /*-------------*/
   private macro init_exchng_logger_params(i_is_short_log,
                                           i_reqid,
                                           i_reqid_linklog,
                                           i_module,
                                           i_by_macro,
                                           i_transparent_id,
                                           i_req_message,
                                           i_resp_message,
                                           i_error_code,
                                           i_error_msg)
      p_is_short_log = false;

      if(ValType(i_is_short_log) != V_UNDEF)
         p_is_short_log = i_is_short_log;
      end;

      service_msg = "";
      req_str_list = TArray;
      resp_str_list = TArray;
      p_id = null;

      p_reqid = proc_in_text_prm(i_reqid);

      p_module = proc_in_text_prm(i_module);

      p_by_macro = proc_in_text_prm(i_by_macro);

      p_transparent_id = proc_in_text_prm(i_transparent_id);

      p_error_code = proc_in_text_prm(i_error_code);

      p_error_msg = proc_in_text_prm(i_error_msg);

      p_resp_message = proc_in_text_prm(i_resp_message);

      p_req_message = proc_in_text_prm(i_req_message);

      p_reqid_linklog  = proc_in_text_prm(i_reqid_linklog);

      /* Подробный лог содержит сообщения; короткая версия - нет */
      if(not p_is_short_log)
         if(ValType(i_req_message) != V_UNDEF)
            save_row_to_log(); /* Если задан текст сообщения запроса, то выполняем запись сразу */
         end;
      else
         if(ValType(i_reqid) != V_UNDEF)
            save_short_row_to_log(); /* Если задан по крайней мере идентификатор запроса, то выполняем запись сразу */
         end;
      end;
   end; /* macro init_exchng_logger_params() */

   init_exchng_logger_params(v_is_short_log,
                             v_reqid,
                             v_reqid_linklog,
                             v_module,
                             v_by_macro,
                             v_transparent_id,
                             v_req_message,
                             v_resp_message,
                             v_error_code,
                             v_error_msg);

end; /* class uws_exchng_logger() */