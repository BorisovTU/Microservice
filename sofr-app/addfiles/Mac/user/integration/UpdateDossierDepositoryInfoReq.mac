/*---------------------------------------------------------*/
/* Обновление досье по договору на брокерское обслуживание */
/*                                                         */
/* Автор: Гераськина Т.                                    */
/*
*/
/*----------------------------------------------*/
import func_lib;
import "global_utils_intgr.mac";
import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import "uws_exchng_log.mac";              /* Функционал логирования */
import "RequestWS.mac";                   /* Функция передачи запроса в Web Sphere */


private file attr("objattr.dbt") key 0;

class c_RespData
   var ResultCode: integer;
   var ResultText: string;
end;


class c_UpdateDossier_Error()
  var ErrorCode: integer = 0;
  var ErrorDesc: string;
end;

private class c_Account
  var AccountNumber: string;
  var CurrencyCode: string = NULL;
end;

class c_UpdateDossierDepositoryInfoReq(p_contrid) 
  var ContractNumber            : string;   // Номер договора                                 Да
  var DepositoryContractNumber  : string;   // Номер договора на депозитарное обслуживание    Да
  var FilialId                  : c_IntegrationSymbolicIdentifierXType;   // Код РФ                                         Да
  var DossierKind               : c_IntegrationDictionaryRecordXType;   // Тип досье                                      Да
  var DepositoryAccountNumberList = TArray; // Список номеров счетов депо                     Да
   
  private macro uInitPrime(p_contrid)
    var sql_str, cmd, rs;
    var ObjType = 207;  // договор брокерского обслуживания
    var ObjID;
    var err_txt;
    var notekind, temp_acc, temp_acc_c;
    var ObjCat;
    
    if(ValType(p_contrid) != V_UNDEF)
      sql_str = "SELECT sfcontr.* FROM "+
                "  ddlcontr_dbt ddlcontr, "+
                "  dsfcontr_dbt sfcontr "+
                " WHERE sfcontr.t_id = ddlcontr.t_sfcontrid "+
                "   AND ddlcontr.t_dlcontrid = :dlid ";
      cmd = RSDCommand(sql_str);
      cmd.AddParam("dlid", RSDBP_IN, p_contrid);
      rs = RSDRecordSet(cmd);
      if (rs.movenext)
        ContractNumber = rs.value("t_number");
      end;
      if (not ContractNumber)
        err_txt = "Не найден договор БО ID="+p_contrid;
        RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
      end;
      
      ObjID = String(p_contrid:o:34);
      
      notekind = CONTRACT_NOTEKIND_CodeRF;
      FilialId.Objectid = readNoteForObject( ObjType, ObjID, notekind );  
      if (not FilialId.Objectid)
        //err_txt = "Не задан код РФ в примечании "+notekind;
        //RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
        FilialId.Objectid = "";
      end;

      ObjCat = RsbObjCategories(ObjType, ObjID);
      if (ObjCat.GetMainAttr( CONTRACT_CATEGORY_TYPEDOSSIER, date(), attr ) == 0)
        DossierKind.RecordCode = attr.Numinlist;
      end;
      if (not DossierKind.RecordCode)
        err_txt = "Не задан тип досье в категории "+CONTRACT_CATEGORY_TYPEDOSSIER;
        RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
      end;
      
      notekind = 102;
      DepositoryContractNumber = readNoteForObject( ObjType, ObjID, notekind );  // номер договора ДЕПО
      if (not DepositoryContractNumber)
        err_txt = "Не задан номер договора ДЕПО в примечании "+notekind;
        RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
      end;
      
      notekind = 101;
      temp_acc = readNoteForObject( ObjType, ObjID, notekind );  // счет ДЕПО
      if (temp_acc)
        temp_acc_c = c_Account;
        temp_acc_c.AccountNumber = temp_acc;
        DepositoryAccountNumberList.value(DepositoryAccountNumberList.size) = temp_acc_c;
      end;

      notekind = 104;
      temp_acc = readNoteForObject( ObjType, ObjID, notekind );  // торговый счет ДЕПО
      if (temp_acc)
        temp_acc_c = c_Account;
        temp_acc_c.AccountNumber = temp_acc;
        DepositoryAccountNumberList.value(DepositoryAccountNumberList.size) = temp_acc_c;
      end;

      notekind = 107;
      temp_acc = readNoteForObject( ObjType, ObjID, notekind );  // счет ДЕПО для распоряжений
      if (temp_acc)
        temp_acc_c = c_Account;
        temp_acc_c.AccountNumber = temp_acc;
        DepositoryAccountNumberList.value(DepositoryAccountNumberList.size) = temp_acc_c;
      end;
      
    end;
  end;
   
  uInitPrime(p_contrid); 
      
end;

/* Основной класс работы с сервисом */
private class c_UpdateDossierProcessor(p_contrid)
   var UpdateDossierDepositoryInfoReq; /* !!! По названию корневого тега запроса !!! */
   UpdateDossierDepositoryInfoReq = c_UpdateDossierDepositoryInfoReq(p_contrid);
end; 

//отправка запроса в очередь
macro run_queueDossier(_req_obj)
   var RespData = c_RespData;
   var err_txt = NULL;
   
   private var v_out_obj;
   private var RequestText;
   private var v_transformator;
   private var v_answer;
   private var v_answer_data;
   private var v_logger_obj;
   private var v_log_id;
   private var p_transparent_id = NULL;

   var vguid = CreateGUID();
  BegAction(1, "Выгрузка досье в АСОА");

   v_transformator = c_secur_msg_converter();

   // заполнение класса информацией
   v_out_obj = _req_obj;

   if (ValType(v_out_obj) == V_UNDEF)
      RespData.ResultCode = -1000;
      RespData.ResultText = "Пустой объект";
   else
      RequestText = v_transformator.m_secur_internal_resp(v_out_obj);
      //RequestText = StrSubst(RequestText, "xmlns=\"http","xmlns:ns1=\"http");
      //RequestText = StrSubst(RequestText, "UpdateDossierDepositoryInfoReq","ns1:UpdateDossierDepositoryInfoReq");
     
      v_logger_obj = uws_exchng_logger(null, vguid, null, "UpdateDossierDepository", modulename(), p_transparent_id, RequestText);
      v_log_id = v_logger_obj.get_log_id();

        
      //err_txt = v_transformator.m_validate_out_xml(RequestText); gtv: отключена проверка по схеме из-за мерцающей ошибки
      v_transformator = NULL;
      
      if (err_txt == NULL)
         v_answer = SubmitRequestToWS( 2,              // ИД очереди.
                                      "",                // ИД сообщения.
                                      "",             // Идентификатор Бэк Офиса. (не обрабатывается).
                                      false,           // Признак синхронной обработки. Если не указан, = False
                                      "SOFR",         // Подразделение системы-назначения.
                                      15,              // ИД типа данных
                                      RequestText,    // Бизнес данные в виде XML.
                                      v_log_id,
                                      "ASOA"
                                      );
                                      
         if (v_answer.ResultCode == 0)
            RespData.ResultCode = 0;
            RespData.ResultText = "OK";
         else 
            v_logger_obj.add_error_info(v_answer.ResultCode, v_answer.ResultText);
            RespData.ResultCode = v_answer.ResultCode;
            RespData.ResultText = v_answer.ResultText;
         end;
      else
         v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, err_txt);
         RespData.ResultCode = UNIVERSAL_ERROR_CODE;
         RespData.ResultText = err_txt;
      end;
           
   end;

  EndAction(1);
   return RespData;

onError(err)
   RespData.ResultCode = err.code;
   RespData.ResultText = err.message;
  EndAction(1);
   return RespData;
end; 


/*-------------------------------------------------*/
/*              Точка входа                        */
/*-------------------------------------------------*/
/* _contrid - ID договора брокерского обслуживания */
/*-------------------------------------------------*/
macro UpdateDossierDepositoryInfoReq (_contrid, p_error:@object) 
  // заполнение класса информацией
  BegAction(1, "Выгрузка досье в АСОА");
  var v_out_obj = c_UpdateDossierProcessor(_contrid);
  EndAction(1);
  p_error = c_UpdateDossier_Error;

  return v_out_obj;
onError(err)
  v_out_obj = NULL;
  p_error = c_UpdateDossier_Error;
  p_error.ErrorCode = c_err_obj.obtain_err_code(err);
  p_error.ErrorDesc = c_err_obj.obtain_err_msg(err);
  EndAction(1);
  
  return v_out_obj;  
end;




