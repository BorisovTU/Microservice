/*-----------------------------------------------------*/
/* Запрос ссылок по Субъекту ЮЛ                        */
/*                                                     */
/* Автор: Гераськина Т.                                */
/*
*/
/*----------------------------------------------*/
import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import "RequestWS.mac";                   /* Функция передачи запроса в Web Sphere */
import "uws_exchng_log.mac";              /* Функционал логирования */
import func_lib;

class c_PersonGetXRefLEReq(Incomedata) 
   var MasterDataId: c_IntegrationSymbolicIdentifierXType;    //Ссылка

   private macro uInitPrime(IncomeData)
      var aux_buf;
      if(ValType(IncomeData) != V_UNDEF)
         aux_buf = uTryToGetPropS(IncomeData, "MasterDataId");
         if (ValType(aux_buf) != V_UNDEF)
            MasterDataId.ObjectId = uTryToGetPropS(aux_buf, "ObjectId");;
            MasterDataId.SystemId = uTryToGetPropS(aux_buf, "SystemId");
            MasterDataId.SystemNodeId = uTryToGetPropS(aux_buf, "SystemNodeId");;
         end;
      end;
   end;
   
   uInitPrime(IncomeData); 
      
end;

private class c_ErrorData
   var ErrorCode :String;  // Код результата       Нет      Идентификатор
   var ErrorDesc :String;  // Поясняющий текст     Нет
end;

private class c_ResultData
   var ResultCode:String;
   var ErrorData :c_ErrorData;
end;

private class c_LEXRef(IncomeData)
   var OperationalDataId;

   private macro uInitPrime(IncomeData)
      var aux_buff;
      aux_buff = uTryToGetPropS(IncomeData, "OperationalDataId");
      if (ValType(aux_buff) != V_UNDEF)
         OperationalDataId = c_IntegrationSymbolicIdentifierXType;
         OperationalDataId.ObjectId = uTryToGetPropS(aux_buff, "ObjectId");
         OperationalDataId.SystemId = uTryToGetPropS(aux_buff, "SystemId");
         OperationalDataId.SystemNodeId = uTryToGetPropS(aux_buff, "SystemNodeId");
      end;
   end;
   
   uInitPrime(IncomeData);
end;


/* Корневой элемент ответа - AS ID - как пришел */
private class adp_PersonGetXRefLEResp(IncomeData)

   
   var MasterDataId : c_IntegrationSymbolicIdentifierXType;
   var ResultData : c_ResultData;
   var LEXRefList = TArray;         // Список: Ссылки

   var aux_buff, ai, aux_buff2;

   /* Кратность параметров в соответствии со схемой/спецификацией */
   private macro uInitPrime(IncomeData)
      var err_txt;

      if(ValType(IncomeData) != V_UNDEF)
         aux_buff = uTryToGetPropS(IncomeData, "MasterDataId");
         if (ValType(aux_buff) != V_UNDEF)
            MasterDataId.SystemId       = uTryToGetPropS(aux_buff, "SystemId");
            MasterDataId.SystemNodeId   = uTryToGetPropS(aux_buff, "SystemNodeId");
            MasterDataId.ObjectId       = uTryToGetPropS(aux_buff, "ObjectId");
         end;

         aux_buff = uTryToGetPropS(IncomeData, "ResultData");
         if (ValType(aux_buff) != V_UNDEF)
            ResultData.ResultCode = uTryToGetPropS(aux_buff, "ResultCode");
            aux_buff2 = uTryToGetPropS(aux_buff, "ErrorData");
            if (ValType(aux_buff2) != V_UNDEF)
               ResultData.ErrorData.ErrorCode = uTryToGetPropS(aux_buff2, "ErrorCode");
               ResultData.ErrorData.ErrorDesc = uTryToGetPropS(aux_buff2, "ErrorDesc");
            end;
         end;
         
         aux_buff = uTryToGetPropS(IncomeData, "LEXRefList");
         if(ValType(aux_buff) == V_GENOBJ)
            aux_buff = 0;
            while(IncomeData.LEXRefList.size > aux_buff)
               ai = LEXRefList.size;
               LEXRefList.value(ai) = c_LEXRef(IncomeData.LEXRefList.value(aux_buff));
               aux_buff = aux_buff + 1;
            end;
         end;

      end;
   end;

   macro GetSystemXref(_SystemId)
      var res = c_IntegrationSymbolicIdentifierXType;
      res.ObjectId = "-1";
      res.SystemId = "-1";
      res.SystemNodeId = "-1";
      
      var LEXRef;
      
      for (LEXRef, LEXRefList)
         if ( Valtype(LEXRef.OperationalDataId) != V_UNDEF )
            if ( (Valtype(LEXRef.OperationalDataId.SystemId) != V_UNDEF) and (LEXRef.OperationalDataId.SystemId == _SystemId) )
               res.ObjectId = LEXRef.OperationalDataId.ObjectId;
               res.SystemId = LEXRef.OperationalDataId.SystemId;
               res.SystemNodeId = LEXRef.OperationalDataId.SystemNodeId;
            end;
         end;
      end;
      return res;
   end;


   uInitPrime(IncomeData);
end; /* class adp_ExportDealResponse(IncomeData) */


/* Основной класс работы с сервисом */
private class c_PersonGetXRefLEProcessor(IncomeData)
   var PersonGetXRefLEReq;    /* !!! По названию корневого тега запроса !!! */
   PersonGetXRefLEReq = c_PersonGetXRefLEReq(IncomeData);
end; /* private class c_ExportStatusDealProcessor() */


/*-------------------------------------------------------------------*/
/* Адаптер для вызова сервиса "Запрос ссылок по Субъекту ЮЛ"         */
/*-------------------------------------------------------------------*/
macro run_PersonGetXRefLEReq(ReqData, p_log_id, p_transparent_id)
   var RespData = NULL;
   var err_txt = NULL;
   
   private var v_out_obj;
   private var RequestText;
   private var v_transformator;
   private var v_answer;
   private var v_answer_data;
   private var v_logger_obj;
   private var v_log_id;
   private var xml_str;
   private var v_xml_rpc_reqid = XmlRpcRequestId(); /* 20181229 - kva - чтобы дважды не вызывать функцию */

   if ( (ReqData == NULL) or (ValType(ReqData) == V_UNDEF) )
      return null;
   end;

   v_transformator = c_secur_msg_converter();

   // заполнение класса информацией 
   v_out_obj = c_PersonGetXRefLEProcessor(ReqData);
   RequestText = v_transformator.m_secur_internal_resp(v_out_obj);
   
   v_logger_obj = uws_exchng_logger(null, p_transparent_id, v_xml_rpc_reqid, "PersonGetXRefLEReq", modulename(), null, RequestText);
   v_log_id = v_logger_obj.get_log_id();

      
   err_txt = v_transformator.m_validate_out_xml(RequestText);
   if (err_txt == NULL)
      v_answer = SubmitRequestToWS( 2,              // ИД очереди.
                                    "",             // ИД сообщения.
                                    "",             // Идентификатор Бэк Офиса. (не обрабатывается).
                                    true,           // Признак синхронной обработки. Если не указан, = False
                                    "SOFR",         // Подразделение системы-отправителя.
                                    11,             // ИД типа данных
                                    RequestText,    // Бизнес данные в виде XML.
                                    v_log_id,       // ID usr_exchng_log
                                    "CIFLE"         // Подразделение системы-получателя.
                                    );

      if (v_answer.ResultCode == 0)
         xml_str = v_answer.responsetext;
         v_logger_obj.add_response(xml_str); 

         v_transformator = NULL;
         
         /* Преобразуем запрос в объектный вид - begin */
         v_transformator = c_secur_msg_converter();
         if(not v_transformator.m_decode_escaped_char(xml_str))
            RunError(v_transformator.get_service_msg(), c_usr_err_obj(v_transformator.get_service_msg()));
         end;
         v_answer_data = v_transformator.m_secur_wol_internal_req(v_transformator.get_pure_msg());
         /* Преобразуем запрос в объектный вид - end */

         if(ValType(v_answer_data) == V_UNDEF)
            err_txt = "Не удалось преобразовать XML в объект";
            RunError(err_txt, c_usr_err_obj(err_txt)); 
         end;
         
         RespData = adp_PersonGetXRefLEResp(v_answer_data);
      /* Преобразование объекта к виду, ожидаемому Системой */
      else 
         v_logger_obj.add_error_info(v_answer.ResultCode, v_answer.ResultText);
      end;

   else
      v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, err_txt);
   end;
         
   return RespData;

onError(err)
/*   RespErr = RSCFaultResponse;
   RespErr.ReqID = REQUEST_ID;
   RespErr.faultType = UNIVERSAL_ERROR_TYPE;
   RespErr.faultCode = UNIVERSAL_ERROR_CODE;
   RespErr.faultString = string("Выгрузка статуса сделок: ", uTryToGetPropS(err, "message")); 

   return RespErr;*/
end; /* macro ExportStatusDeal(ReqData) */


/*---------------------------------------*/
/*              Точка входа              */
/*---------------------------------------*/
/* ReqData - данные для отправки         */
/*---------------------------------------*/
macro PersonGetXRefLEReq(ReqData, in_log_id, in_transparent_id) /* 20190103 - kva - добавлены параметры логирования */
   return run_PersonGetXRefLEReq(ReqData, in_log_id, in_transparent_id); /* 20190103 - kva - добавлены параметры логирования */
end;

