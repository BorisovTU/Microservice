import "KondorRunDealCnv.mac";

/**
@brief Процедура запуска импортированных из АСУДР-КОНДОР сделок на исполнение в последовательном режиме
*/
private macro RunKondorDeals()
  var resexec;
  var errMessage;
  var dealsSelectQuery: String = KondorDealsGetQuery();
  var cmd = RSDCommand(dealsSelectQuery);
  KondorDealsAddParams(cmd);
  var rs = RSDRecordSet(cmd);

  while (rs.MoveNext())
    OutputIntrace("Exec_deal_begin "+rs.value("t_code")+" curdate="+{curdate}); 
    
    resexec = ExecOneKondorDeal(rs.Value("t_ID"), rs.Value("t_DocKind"), true, @errMessage);

    msgbox({curdate} + " - " + rs.value("t_id") + " - " + rs.value("t_code") + " - " + resexec + " - " + errMessage);
    println({curdate} + " - " + rs.value("t_id") + " - " + rs.value("t_code") + " - " + resexec + " - " + errMessage);
    OutputIntrace("Exec_deal_end "+rs.value("t_code")+" result="+resexec);
  end;
end;

/**
@brief Процедура запуска импортированных из АСУДР-КОНДОР сделок на исполнение через конвейер
*/
private macro RunKondorDealsConveyer()
  var cnv = RsbConveyerExec(KONDORDEAL_TASKID);
  var params = KondorDealsCnvParams();
  params.conveyerID = KONDORDEAL_CONVEYERID;

  cnv.AddParamsForConveyer(KONDORDEAL_CONVEYERID, params, 0);
  cnv.AddParamsForOperation(KONDORDEAL_CONVEYERID, KONDORDEAL_OPERATIONID, params, "", 0);
  cnv.ShowPanelMonitoring(0);
  cnv.Run();
end;

/**
@brief Процедура запуска импортированных из АСУДР-КОНДОР сделок на исполнение

В зависимости от настроек банка исполнение сделок будет выполнено последовательно, либо через конвейер
*/
private macro RunDeals()
  var useConveyer = (Bnk_GetRegistryValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ИМПОРТ СДЕЛОК ИЗ АСУДР-КОНДОР\\ИСПОЛНЯТЬ СДЕЛКИ В КОНВЕЙЕРЕ", V_BOOL, false) and
                     SecurUseConveyer(KONDORDEAL_CONVEYERID, KONDORDEAL_OPERATIONID));
                     
  if (useConveyer)
    RunKondorDealsConveyer();
  else
    RunKondorDeals();
  end;
end;

RunDeals();

