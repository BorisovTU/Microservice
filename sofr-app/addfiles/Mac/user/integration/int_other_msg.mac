/*------------------------------*/
/* Интерфейс "прочие сообщения" */
/*                              */
/* Автор: Карпов В.             */
/*------------------------------*/

import RsbFormsInter;
import "int_usr_globals.mac";
//import rcw; // Для отладки


/*-----------------------*/
/* Глобальные переменные */
/*-----------------------*/
private var gv_msg_sel_prm;                /* Параметры выборки событий */
private var gv_msg_chain_info;             /* Информация о цепочке событий */

private var gv_column_list_scr = TArray(); /* Список колонок скроллинга */
private var gv_num_columns_scr = 0;        /* Количество колонок скроллинга */
private var gv_scr_focus = 0;              /* Номер позиции скроллинга */

/* Список сообщений, которые будут исключены из списка */
private var gv_exclude_msg = TArray();
gv_exclude_msg.value(gv_exclude_msg.size) = "WS_PROCESSDEALS";
gv_exclude_msg.value(gv_exclude_msg.size) = "ws_SOFR_addAccountingEntries";
gv_exclude_msg.value(gv_exclude_msg.size) = "AddAccountingEntriesResponseMsg";
gv_exclude_msg.value(gv_exclude_msg.size) = "ws_SOFR_approveAccountingEntries";
gv_exclude_msg.value(gv_exclude_msg.size) = "ApproveAccountingEntriesResponseMsg";
gv_exclude_msg.value(gv_exclude_msg.size) = "ws_SOFR_OpenAccount";
gv_exclude_msg.value(gv_exclude_msg.size) = "OpenAccountsResponseMsg";
gv_exclude_msg.value(gv_exclude_msg.size) = "ws_SOFR_GetCurrencyRate";

/* Список исходящих сообщений */
private var gv_outgoing_msg = TArray();
gv_outgoing_msg.value(gv_outgoing_msg.size) = "SendRSHBBagRequestMsg";
gv_outgoing_msg.value(gv_outgoing_msg.size) = "PersonGetXrefRequest";
gv_outgoing_msg.value(gv_outgoing_msg.size) = "PersonGetXrefLEReq";
gv_outgoing_msg.value(gv_outgoing_msg.size) = "PersonCreateXref";
gv_outgoing_msg.value(gv_outgoing_msg.size) = "PersonCreateXrefLE";


/*---------------------------*/
/* Параметры выборки событий */
/*---------------------------*/
private class c_msg_selection_prm()
   var v_com_date;
   var v_com_beg_date;
   var v_com_end_date;
   var v_com_date_switch;

   var v_com_is_err_filtr;
   var v_com_err_code;
   var v_com_err_inv;

   var v_com_is_substr_search;
   var v_com_substr_to_search;

   var v_fl_last_name;
   var v_fl_first_name;
   var v_fl_mid_name;
   var v_fl_bth_date;

   var v_le_name;
   var v_le_inn;
   var v_le_kpp;


   /* Значения по умолчанию */
   private macro m_init_msg_selection_prm()
      v_com_date = date();
      v_com_beg_date = date();
      v_com_end_date = date();
      v_com_date_switch = false;

      v_com_is_err_filtr = false;
      v_com_err_code = "0";
      v_com_err_inv = false;

      v_com_is_substr_search = false;
      v_com_substr_to_search = "";

      v_fl_last_name = "";
      v_fl_first_name = "";
      v_fl_mid_name = "";
      v_fl_bth_date = date("00.00.0000");

      v_le_name = "";
      v_le_inn = "";
      v_le_kpp = "";

   end; /* macro m_init_msg_selection_prm() */

   m_init_msg_selection_prm();
end; /* class c_msg_selection_prm() */


/*-------------------------------------*/
/* Класс формы поисковых параметров ЮЛ */
/*-------------------------------------*/
class (TRsbPanel) c_le_search_form()
   const CV_POS_X = 5;
   const CV_POS_Y = 2;

   var v_le_name_fld;
   var v_le_inn_fld;
   var v_le_kpp_fld;

   var v_com_null_btn;


   macro m_evnt_on_key_pressed(p_rsb_evnt)
      if(p_rsb_evnt.KeyCode == K_F2)
         close(CV_CLOSE_FORM_OK);
      elif(p_rsb_evnt.KeyCode == K_ESCAPE)
         if(not gettrue(true, "Выйти из панели?\nБудет применен выбранный фильтр."))
            p_rsb_evnt.keyCode = K_F2;
         else
            close(CV_CLOSE_FORM_OK);
         end;
      end;

      return true;
   end; /* macro m_evnt_on_key_pressed(p_rsb_evnt) */


   macro m_msg_null_le_btn(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_BUTTON_CLICKED)

         gv_msg_sel_prm.v_le_name = "";
         v_le_name_fld.value = "";

         gv_msg_sel_prm.v_le_inn = "";
         v_le_inn_fld.value = "";

         gv_msg_sel_prm.v_le_kpp = "";
         v_le_kpp_fld.value = "";

         redraw();
      end;

      return true;
   end; /* macro m_msg_null_le_btn() */


   private macro m_init_le_search_form(i_this_obj)
      Caption = "Фильтры ЮЛ";
//      Status = "[Esc] Выход  [F2] Применить";
      Status = "[F2] Применить";

      AddLabel(TRsbLabel(CV_POS_X, CV_POS_Y, "Наименование"));
      v_le_name_fld = TRsbEditField(FT_STRING);
      v_le_name_fld.bindValue(gv_msg_sel_prm, "v_le_name", 320);
      v_le_name_fld.setPosition(CV_POS_X, (CV_POS_Y + 1));
      v_le_name_fld.setSize(30, 1);
      AddControl(v_le_name_fld);

      AddLabel(TRsbLabel(CV_POS_X, (CV_POS_Y + 3), "ИНН"));
      v_le_inn_fld = TRsbEditField(FT_STRING);
      v_le_inn_fld.bindValue(gv_msg_sel_prm, "v_le_inn", 35);
      v_le_inn_fld.setPosition(CV_POS_X, (CV_POS_Y + 4));
      v_le_inn_fld.setSize(30, 1);
      AddControl(v_le_inn_fld);

      AddLabel(TRsbLabel(CV_POS_X, (CV_POS_Y + 6), "КПП"));
      v_le_kpp_fld = TRsbEditField(FT_STRING);
      v_le_kpp_fld.bindValue(gv_msg_sel_prm, "v_le_kpp", 35);
      v_le_kpp_fld.setPosition(CV_POS_X, (CV_POS_Y + 7));
      v_le_kpp_fld.setSize(30, 1);
      AddControl(v_le_kpp_fld);

      /* Кнопка сброса */
      v_com_null_btn = TRsbPushButton("Сброс");
      v_com_null_btn.setPosition((CV_POS_X + 31), (CV_POS_Y + 10));
      v_com_null_btn.setSize(6, 1);

      v_com_null_btn.onClicked(R2M(i_this_obj, "m_msg_null_le_btn"));
      AddControl(v_com_null_btn);

      AddEventHandler(RSB_EV_KEY_PRESSED, R2M(i_this_obj, "m_evnt_on_key_pressed"));

      setSize(45, 14);
      setPosition(20, 5);

   end; /* macro m_init_le_search_form() */

   InitTRsbPanel();

   m_init_le_search_form(this);
end; /* class (TRsbPanel) c_le_search_form() */


/*-------------------------------------*/
/* Класс формы поисковых параметров ФЛ */
/*-------------------------------------*/
class (TRsbPanel) c_fl_search_form()
   const CV_POS_X = 5;
   const CV_POS_Y = 2;

   var v_fl_last_name_fld;
   var v_fl_first_name_fld;
   var v_fl_mid_name_fld;
   var v_fl_bth_date_fld;

   var v_com_null_btn;


   macro m_evnt_on_key_pressed(p_rsb_evnt)
      if(p_rsb_evnt.KeyCode == K_F2)
         close(CV_CLOSE_FORM_OK);
      elif(p_rsb_evnt.KeyCode == K_ESCAPE)
         if(not gettrue(true, "Выйти из панели?\nБудет применен выбранный фильтр."))
            p_rsb_evnt.keyCode = K_F2;
         else
            close(CV_CLOSE_FORM_OK);
         end;
      end;

      return true;
   end; /* macro m_evnt_on_key_pressed(p_rsb_evnt) */


   macro m_msg_null_fl_btn(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_BUTTON_CLICKED)

         gv_msg_sel_prm.v_fl_last_name = "";
         v_fl_last_name_fld.value = "";

         gv_msg_sel_prm.v_fl_first_name = "";
         v_fl_first_name_fld.value = "";

         gv_msg_sel_prm.v_fl_mid_name = "";
         v_fl_mid_name_fld.value = "";

         gv_msg_sel_prm.v_fl_bth_date = date("00.00.0000");
         v_fl_bth_date_fld.value = date("00.00.0000");

         redraw();
      end;

      return true;
   end; /* macro m_msg_null_fl_btn() */


   private macro m_init_fl_search_form(i_this_obj)
      Caption = "Фильтры ФЛ";
//      Status = "[Esc] Выход  [F2] Применить";
      Status = "[F2] Применить";

      AddLabel(TRsbLabel(CV_POS_X, CV_POS_Y, "Фамилия"));
      v_fl_last_name_fld = TRsbEditField(FT_STRING);
      v_fl_last_name_fld.bindValue(gv_msg_sel_prm, "v_fl_last_name", 25);
      v_fl_last_name_fld.setPosition(CV_POS_X, (CV_POS_Y + 1));
      v_fl_last_name_fld.setSize(10, 1);
      AddControl(v_fl_last_name_fld);

      AddLabel(TRsbLabel((CV_POS_X + 13), CV_POS_Y, "Имя"));
      v_fl_first_name_fld = TRsbEditField(FT_STRING);
      v_fl_first_name_fld.bindValue(gv_msg_sel_prm, "v_fl_first_name", 25);
      v_fl_first_name_fld.setPosition((CV_POS_X + 13), (CV_POS_Y + 1));
      v_fl_first_name_fld.setSize(10, 1);
      AddControl(v_fl_first_name_fld);

      AddLabel(TRsbLabel((CV_POS_X + 26), CV_POS_Y, "Отчество"));
      v_fl_mid_name_fld = TRsbEditField(FT_STRING);
      v_fl_mid_name_fld.bindValue(gv_msg_sel_prm, "v_fl_mid_name", 25);
      v_fl_mid_name_fld.setPosition((CV_POS_X + 26), (CV_POS_Y + 1));
      v_fl_mid_name_fld.setSize(10, 1);
      AddControl(v_fl_mid_name_fld);

      AddLabel(TRsbLabel(CV_POS_X, (CV_POS_Y + 3), "Дата рождения"));
      v_fl_bth_date_fld = TRsbEditField(FT_DATE);
      v_fl_bth_date_fld.bindValue(gv_msg_sel_prm, "v_fl_bth_date", 10);
      v_fl_bth_date_fld.setPosition(CV_POS_X, (CV_POS_Y + 4));
      v_fl_bth_date_fld.setSize(10, 1);
      AddControl(v_fl_bth_date_fld);

      /* Кнопка сброса */
      v_com_null_btn = TRsbPushButton("Сброс");
      v_com_null_btn.setPosition((CV_POS_X + 31), (CV_POS_Y + 10));
      v_com_null_btn.setSize(6, 1);

      v_com_null_btn.onClicked(R2M(i_this_obj, "m_msg_null_fl_btn"));
      AddControl(v_com_null_btn);

      AddEventHandler(RSB_EV_KEY_PRESSED, R2M(i_this_obj, "m_evnt_on_key_pressed"));

      setSize(45, 14);
      setPosition(20, 5);

   end; /* macro m_init_fl_search_form() */

   InitTRsbPanel();

   m_init_fl_search_form(this);
end; /* class (TRsbPanel) c_fl_search_form() */


/*----------------------------------------*/
/* Класс формы общих поисковых параметров */
/*----------------------------------------*/
class (TRsbPanel) c_com_search_form()
   const CV_POS_X = 5;
   const CV_POS_Y = 2;
   var v_com_date_fld;
   var v_com_beg_date_fld;
   var v_com_end_date_fld;
   var v_com_date_switch_fld;

   var v_com_is_err_filtr_fld;
   var v_com_err_code_fld;
   var v_com_err_inv_fld;

   var v_com_is_substr_search_fld;
   var v_com_substr_to_search_fld;

   var v_com_null_btn;


   macro m_evnt_on_key_pressed(p_rsb_evnt)
      if(p_rsb_evnt.KeyCode == K_F2)
         if(gv_msg_sel_prm.v_com_is_substr_search)
            if(not gettrue(true, "Поиск по подстроке исходного сообщения может занять значительное время:\nВсе равно применить выбранный фильтр?"))
               p_rsb_evnt.keyCode = K_F2;
            else
               close(CV_CLOSE_FORM_OK);
            end;
         else
            close(CV_CLOSE_FORM_OK);
         end;
      elif(p_rsb_evnt.KeyCode == K_ESCAPE)
         if(not gettrue(true, "Выйти из панели?\nБудет применен выбранный фильтр."))
            p_rsb_evnt.keyCode = K_F2;
         else
            if(gv_msg_sel_prm.v_com_is_substr_search)
               if(not gettrue(true, "Поиск по подстроке исходного сообщения может занять значительное время:\nВсе равно применить выбранный фильтр?"))
                  p_rsb_evnt.keyCode = K_F2;
               else
                  close(CV_CLOSE_FORM_OK);
               end;
            else
               close(CV_CLOSE_FORM_OK);
            end;
         end;
      end;

      return true;
   end; /* macro m_evnt_on_key_pressed(p_rsb_evnt) */


   macro m_on_switch_date(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_CONTROL_CHECKED)
         gv_msg_sel_prm.v_com_date_switch = v_com_date_switch_fld.checked;
      end;

      if(gv_msg_sel_prm.v_com_date_switch)
         v_com_date_fld.focusable = false;
         v_com_date_fld.enabled = false;
         v_com_date_fld.editable = false;

         v_com_beg_date_fld.focusable = true;
         v_com_beg_date_fld.enabled = true;
         v_com_beg_date_fld.editable = true;

         v_com_end_date_fld.focusable = true;
         v_com_end_date_fld.enabled = true;
         v_com_end_date_fld.editable = true;
      else
         v_com_date_fld.focusable = true;
         v_com_date_fld.enabled = true;
         v_com_date_fld.editable = true;

         v_com_beg_date_fld.focusable = false;
         v_com_beg_date_fld.enabled = false;
         v_com_beg_date_fld.editable = false;

         v_com_end_date_fld.focusable = false;
         v_com_end_date_fld.enabled = false;
         v_com_end_date_fld.editable = false;
      end;

      redraw();

      return true;
   end; /* macro m_on_switch_date() */


   macro m_on_checked_is_err_filtr(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_CONTROL_CHECKED)
         gv_msg_sel_prm.v_com_is_err_filtr = v_com_is_err_filtr_fld.checked;

         if(gv_msg_sel_prm.v_com_is_err_filtr)
            v_com_err_code_fld.focusable = true;
            v_com_err_code_fld.enabled = true;
            v_com_err_code_fld.editable = true;
         else
            v_com_err_code_fld.focusable = false;
            v_com_err_code_fld.enabled = false;
            v_com_err_code_fld.editable = false;
         end;

         redraw();
      end;

      return true;
   end; /* macro m_on_checked_is_err_filtr() */


   macro m_on_checked_err_inv(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_CONTROL_CHECKED)
         gv_msg_sel_prm.v_com_err_inv = v_com_err_inv_fld.checked;
      end;

      return true;
   end; /* macro m_on_checked_err_inv() */


   macro m_on_checked_is_substr_search(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_CONTROL_CHECKED)
         gv_msg_sel_prm.v_com_is_substr_search = v_com_is_substr_search_fld.checked;

         if(gv_msg_sel_prm.v_com_is_substr_search)
            v_com_substr_to_search_fld.focusable = true;
            v_com_substr_to_search_fld.enabled = true;
            v_com_substr_to_search_fld.editable = true;
         else
            v_com_substr_to_search_fld.focusable = false;
            v_com_substr_to_search_fld.enabled = false;
            v_com_substr_to_search_fld.editable = false;
         end;

         redraw();
      end;

      return true;
   end;


   macro m_msg_null_com_btn(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_BUTTON_CLICKED)
         gv_msg_sel_prm.v_com_date = date("00.00.0000");
         v_com_date_fld.value = date("00.00.0000");

         gv_msg_sel_prm.v_com_beg_date = date("00.00.0000");
         v_com_beg_date_fld.value = date("00.00.0000");

         gv_msg_sel_prm.v_com_end_date = date("00.00.0000");
         v_com_end_date_fld.value = date("00.00.0000");

         v_com_date_fld.focusable = true;
         v_com_date_fld.enabled = true;
         v_com_date_fld.editable = true;

         v_com_beg_date_fld.focusable = false;
         v_com_beg_date_fld.enabled = false;
         v_com_beg_date_fld.editable = false;

         v_com_end_date_fld.focusable = false;
         v_com_end_date_fld.enabled = false;
         v_com_end_date_fld.editable = false;

         v_com_date_switch_fld.checked = false;
         gv_msg_sel_prm.v_com_date_switch = false;

         v_com_is_err_filtr_fld.checked = false;
         gv_msg_sel_prm.v_com_is_err_filtr = false;
         gv_msg_sel_prm.v_com_err_code = "0";
         v_com_err_code_fld.value = "0";

         v_com_err_code_fld.focusable = false;
         v_com_err_code_fld.enabled = false;
         v_com_err_code_fld.editable = false;

         v_com_err_inv_fld.checked = false;
         gv_msg_sel_prm.v_com_err_inv = false;

         v_com_is_substr_search_fld.checked = false;
         gv_msg_sel_prm.v_com_is_substr_search = false;
         gv_msg_sel_prm.v_com_substr_to_search = "";
         v_com_substr_to_search_fld.value = "";

         v_com_substr_to_search_fld.focusable = false;
         v_com_substr_to_search_fld.enabled = false;
         v_com_substr_to_search_fld.editable = false;

         redraw();
      end;

      return true;
   end; /* macro m_msg_null_com_btn() */


   private macro m_init_com_search_form(i_this_obj)
      Caption = "Общие фильтры";
//      Status = "[Esc] Выход  [F2] Применить";
      Status = "[F2] Применить";

      /* Фильтр по дате */
      AddLabel(TRsbLabel(CV_POS_X, CV_POS_Y, "Дата выборки"));
      v_com_date_fld = TRsbEditField(FT_DATE);
      v_com_date_fld.bindValue(gv_msg_sel_prm, "v_com_date", 10);
      v_com_date_fld.setPosition(CV_POS_X, (CV_POS_Y + 1));
      v_com_date_fld.setSize(10, 1);
      if(gv_msg_sel_prm.v_com_date_switch)
         v_com_date_fld.focusable = false;
         v_com_date_fld.enabled = false;
         v_com_date_fld.editable = false;
      else
         v_com_date_fld.focusable = true;
         v_com_date_fld.enabled = true;
         v_com_date_fld.editable = true;
      end;
      AddControl(v_com_date_fld);
      AddLabel(TRsbLabel((CV_POS_X + 27), CV_POS_Y, "Диапазон дат"));
      v_com_beg_date_fld = TRsbEditField(FT_DATE);
      v_com_beg_date_fld.bindValue(gv_msg_sel_prm, "v_com_beg_date", 10);
      v_com_beg_date_fld.setPosition((CV_POS_X + 27), (CV_POS_Y + 1));
      v_com_beg_date_fld.setSize(10, 1);
      if(gv_msg_sel_prm.v_com_date_switch)
         v_com_beg_date_fld.focusable = true;
         v_com_beg_date_fld.enabled = true;
         v_com_beg_date_fld.editable = true;
      else
         v_com_beg_date_fld.focusable = false;
         v_com_beg_date_fld.enabled = false;
         v_com_beg_date_fld.editable = false;
      end;
      AddControl(v_com_beg_date_fld);
      v_com_end_date_fld = TRsbEditField(FT_DATE);
      v_com_end_date_fld.bindValue(gv_msg_sel_prm, "v_com_end_date", 10);
      v_com_end_date_fld.setPosition((CV_POS_X + 27), (CV_POS_Y + 2));
      v_com_end_date_fld.setSize(10, 1);
      if(gv_msg_sel_prm.v_com_date_switch)
         v_com_end_date_fld.focusable = true;
         v_com_end_date_fld.enabled = true;
         v_com_end_date_fld.editable = true;
      else
         v_com_end_date_fld.focusable = false;
         v_com_end_date_fld.enabled = false;
         v_com_end_date_fld.editable = false;
      end;
      AddControl(v_com_end_date_fld);

      AddLabel(TRsbLabel((CV_POS_X + 14), CV_POS_Y, "Исп. диапазон"));
      v_com_date_switch_fld = TRsbCheckBox();
      v_com_date_switch_fld.setPosition((CV_POS_X + 18), (CV_POS_Y + 1));
      v_com_date_switch_fld.onChecked(R2M(i_this_obj, "m_on_switch_date"));
      v_com_date_switch_fld.checked = gv_msg_sel_prm.v_com_date_switch;
      AddControl(v_com_date_switch_fld);

      /* Фильтр по коду ошибки */
      v_com_is_err_filtr_fld = TRsbCheckBox();
//      v_com_is_err_filtr_fld.bindValue(gv_msg_sel_prm, "v_com_is_err_filtr");
      v_com_is_err_filtr_fld.setPosition(CV_POS_X, (CV_POS_Y + 4));
      v_com_is_err_filtr_fld.onChecked(R2M(i_this_obj, "m_on_checked_is_err_filtr"));
      v_com_is_err_filtr_fld.checked = gv_msg_sel_prm.v_com_is_err_filtr;
      AddControl(v_com_is_err_filtr_fld);
      AddLabel(TRsbLabel((CV_POS_X + 3), (CV_POS_Y + 4), "Фильтр по коду ошибки"));

      AddLabel(TRsbLabel(CV_POS_X, (CV_POS_Y + 5), "Код ошибки"));
      v_com_err_code_fld = TRsbEditField(FT_STRING);
      v_com_err_code_fld.bindValue(gv_msg_sel_prm, "v_com_err_code", 10);
      v_com_err_code_fld.setPosition(CV_POS_X, (CV_POS_Y + 6));
      v_com_err_code_fld.setSize(10, 1);
      if(gv_msg_sel_prm.v_com_is_err_filtr)
         v_com_err_code_fld.focusable = true;
         v_com_err_code_fld.enabled = true;
         v_com_err_code_fld.editable = true;
      else
         v_com_err_code_fld.focusable = false;
         v_com_err_code_fld.enabled = false;
         v_com_err_code_fld.editable = false;
      end;
      AddControl(v_com_err_code_fld);

      v_com_err_inv_fld = TRsbCheckBox();
//      v_com_err_inv_fld.bindValue(gv_msg_sel_prm, "v_com_err_inv");
      v_com_err_inv_fld.setPosition((CV_POS_X + 15), (CV_POS_Y + 6));
      v_com_err_inv_fld.onChecked(R2M(i_this_obj, "m_on_checked_err_inv"));
      v_com_err_inv_fld.checked = gv_msg_sel_prm.v_com_err_inv;
      AddControl(v_com_err_inv_fld);
      AddLabel(TRsbLabel((CV_POS_X + 18), (CV_POS_Y + 6), "Инверсия условия"));

      /* Фильтр по подстроке в исходном сообщении */
      v_com_is_substr_search_fld = TRsbCheckBox();
      v_com_is_substr_search_fld.setPosition(CV_POS_X, (CV_POS_Y + 8));
      v_com_is_substr_search_fld.onChecked(R2M(i_this_obj, "m_on_checked_is_substr_search"));
      v_com_is_substr_search_fld.checked = gv_msg_sel_prm.v_com_is_substr_search;
      AddControl(v_com_is_substr_search_fld);
      AddLabel(TRsbLabel((CV_POS_X + 3), (CV_POS_Y + 8), "Фильтр по подстроке в исходном сообщении"));

      AddLabel(TRsbLabel(CV_POS_X, (CV_POS_Y + 9), "Подстрока для поиска"));
      v_com_substr_to_search_fld = TRsbEditField(FT_STRING);
      v_com_substr_to_search_fld.bindValue(gv_msg_sel_prm, "v_com_substr_to_search", 50);
      v_com_substr_to_search_fld.setPosition(CV_POS_X, (CV_POS_Y + 10));
      v_com_substr_to_search_fld.setSize(25, 1);
      if(gv_msg_sel_prm.v_com_is_substr_search)
         v_com_substr_to_search_fld.focusable = true;
         v_com_substr_to_search_fld.enabled = true;
         v_com_substr_to_search_fld.editable = true;
      else
         v_com_substr_to_search_fld.focusable = false;
         v_com_substr_to_search_fld.enabled = false;
         v_com_substr_to_search_fld.editable = false;
      end;
      AddControl(v_com_substr_to_search_fld);

      /* Кнопка сброса */
      v_com_null_btn = TRsbPushButton("Сброс");
      v_com_null_btn.setPosition((CV_POS_X + 31), (CV_POS_Y + 10));
      v_com_null_btn.setSize(6, 1);

      v_com_null_btn.onClicked(R2M(i_this_obj, "m_msg_null_com_btn"));
      AddControl(v_com_null_btn);

      AddEventHandler(RSB_EV_KEY_PRESSED, R2M(i_this_obj, "m_evnt_on_key_pressed"));

      setSize(45, 14);
      setPosition(20, 5);

   end; /* macro m_init_com_search_form() */

   InitTRsbPanel();

   m_init_com_search_form(this);
end; /* class (TRsbPanel) c_com_search_form() */


class (TRsbTabbedWindow) c_main_search_form()
/*
   const CV_POS_X = 5;
   const CV_POS_Y = 2;
*/

   private macro m_init_main_search_form(i_this_obj)
//      Status = "[Esc] Выход";
   end;

   InitTRsbTabbedWindow();

   m_init_main_search_form(this);
end;


/*-------------------------------------------------*/
/* Проверка наличия информации о цепочке сообщений */
/*-------------------------------------------------*/
macro m_check_chain_info(p_rs)
   if(ValType(gv_msg_chain_info) != V_GENOBJ)
      /* Первичная инициализация объекта */
      gv_msg_chain_info = c_log_chain_msg(p_rs.value("t_id"), p_rs.value("t_module"));

   else
      if(    (gv_msg_chain_info.m_get_log_id_req() != p_rs.value("t_id"))
         and (gv_msg_chain_info.m_get_log_id_resp() != p_rs.value("t_id")))

         /* Переинициализация объекта */
         gv_msg_chain_info.m_reinit_log_chain_seg(p_rs.value("t_id"), p_rs.value("t_module"));
      end;
   end;
end; /* macro m_check_chain_info() */


/*---------------------------------------------------------------*/
/* Проверка наличия проблем при восстановлении цепочки сообщений */
/*---------------------------------------------------------------*/
macro m_check_chain_err()
   var v_msg_text;

   if(gv_msg_chain_info.m_get_service_code() != 0)
      v_msg_text = string("При восстановлении цепочки сообщений возникла следующая проблема:",
                          "\n(", gv_msg_chain_info.m_get_service_code(), ") ",
                          gv_msg_chain_info.m_get_service_msg(),
                          "\nПри необходимости обратитесь к разработчику.");
      msgbox(v_msg_text);
   end;
end; /* macro m_check_chain_err() */


/*------------------------------------*/
/* Метод обработки событий скроллинга */
/*------------------------------------*/
macro m_msg_proc_scr(p_rs, p_cmd, p_id, p_key)
   var CM_FLAG = CM_DEFAULT;

   var gv_com_search_form;
   var gv_fl_search_form;
   var gv_le_search_form;
   var gv_main_search_form;
   var v_msg_menu = TArray;
   var v_choice;
   var v_msg_str = "";
   var v_file_path = "";


   if(p_cmd == DLG_INIT)
      while((gv_scr_focus != 0) and p_rs.movenext())
         if(p_rs.value("t_id") == gv_scr_focus)
            gv_scr_focus = 0;
            GoToScroll(p_rs);
         end;
      end;

   elif(p_cmd == DLG_KEY)
      if(p_key == K_ENTER)
         v_msg_menu.value(0) = "Описание ошибки";
         v_msg_menu.value(1) = "Исходный запрос с оберткой";
         v_msg_menu.value(2) = "Запрос без обертки";
         v_msg_menu.value(3) = "Исходный ответ";
         v_msg_menu.value(4) = "Ответ с оберткой";

         v_choice = menu(v_msg_menu, "Выберите сообщение", "Выберите сообщение");

         if(v_choice == 0)
            msgbox(p_rs.value("t_error_msg"));

         elif(v_choice == 1)
            m_check_chain_info(p_rs);
            m_make_queue_log_msg(gv_msg_chain_info.m_get_log_id_req_env());
            m_check_chain_err();

         elif(v_choice == 2)
            m_check_chain_info(p_rs);
            m_make_usr_log_msg(gv_msg_chain_info.m_get_log_id_req(), 1);
            m_check_chain_err();

         elif(v_choice == 3)
            m_check_chain_info(p_rs);
            if(gv_msg_chain_info.m_get_sep_flag())
               m_make_usr_log_msg(gv_msg_chain_info.m_get_log_id_resp(), 1);
            else
               /* Сообщения запроса и ответа находятся в одной записи */
               m_make_usr_log_msg(gv_msg_chain_info.m_get_log_id_resp(), 3);
            end;
            m_check_chain_err();

         elif(v_choice == 4)
            m_check_chain_info(p_rs);
            m_make_queue_log_msg(gv_msg_chain_info.m_get_log_id_resp_env());
            m_check_chain_err();

         else
            msgbox("Выбор не подтвержден");
         end;

         CM_FLAG = CM_IGNORE;

      elif(p_key == K_F5)
         CM_FLAG = CM_SELECT;


         gv_com_search_form = c_com_search_form();
         gv_fl_search_form = c_fl_search_form();
         gv_le_search_form = c_le_search_form();
//         gv_com_search_form.run;
         gv_main_search_form = c_main_search_form();
         gv_main_search_form.addForm(gv_com_search_form);
         gv_main_search_form.addForm(gv_fl_search_form);
         gv_main_search_form.addForm(gv_le_search_form);
         gv_main_search_form.run;

      elif(p_key == K_ESCAPE)
         if(not gettrue(true, "Выйти из списка сообщений?"))
            CM_FLAG = CM_IGNORE;
         end;
      end;

   end;

   return CM_FLAG;
end; /* macro m_evnt_proc_scr() */


/*---------------------------------------*/
/* Метод, запускающий основной скроллинг */
/*---------------------------------------*/
private macro m_run_main_scroll()

   var sql_scr, cmd_scr, rs_scr;
   var sql_main = "";
   var sql_filtr = "";
   var sql_order = "";
   var v_aux_buf = false;
   var v_aux_cntr;
   var v_mod_set_pers = "SetPersonRequestMsg";
   var v_mod_create_le = "ClientCreateLERequestMsg";

   gv_msg_sel_prm = c_msg_selection_prm();

   sql_main =            "  SELECT u_log.t_id, u_log.t_reqid, u_log.t_module, ";
// 31-01-2020 - Cherednichenko - ускоряем выборку
//   sql_main = sql_main + "         DECODE(u_log.t_by_macro, 'QUEUEBO', 'IN', 'OUT') t_direction, ";
   sql_main = sql_main + "         CASE u_log.t_by_macro WHEN 'QUEUEBO' THEN 'IN' ELSE 'OUT' END t_direction, ";
   sql_main = sql_main + "         TO_CHAR(u_log.t_req_dttm, 'DD.MM.YYYY HH24:MI:SS') t_req_dttm, ";
   sql_main = sql_main + "         TO_CHAR(u_log.t_resp_dttm, 'DD.MM.YYYY HH24:MI:SS') t_resp_dttm, ";
   sql_main = sql_main + "         u_log.t_error_code, u_log.t_error_msg, u_log.t_reqid_linked ";
   sql_main = sql_main + "    FROM usr_exchng_log_dbt u_log ";
   sql_main = sql_main + "   WHERE 1=1 ";

   if(gv_msg_sel_prm.v_com_date != date("00.00.0000"))
// 31-01-2020 - Cherednichenko - ускоряем выборку
//      sql_filtr =        "     AND TRUNC(u_log.t_req_dttm) = :p_msg_on_date ";
      sql_filtr =        "     AND u_log.t_req_dttm BETWEEN :p_msg_on_date AND :p_msg_on_date + 1";
   end;

   v_aux_cntr = 0;
   sql_order =             "     AND u_log.t_module NOT IN ('" + gv_exclude_msg.value(v_aux_cntr) + "'";
   v_aux_cntr = v_aux_cntr + 1;
   while(gv_exclude_msg.size > v_aux_cntr)
      sql_order = sql_order + ", '" + gv_exclude_msg.value(v_aux_cntr) + "'";
      v_aux_cntr = v_aux_cntr + 1;
   end;
   sql_order = sql_order + ") ";
//   sql_order = sql_order + "     AND u_log.t_by_macro = 'WS_PROCESSDEALS' ";
   sql_order = sql_order + "ORDER BY u_log.t_id DESC ";

   sql_scr = sql_main + sql_filtr + sql_order;

   cmd_scr = RSDCommand(sql_scr);
   cmd_scr.NullConversion = true;

   if(gv_msg_sel_prm.v_com_date != date("00.00.0000"))
      cmd_scr.addParam("p_msg_on_date", RSDBP_IN, gv_msg_sel_prm.v_com_date);
   end;

   rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   rs_scr.NullConversion = true;

   m_add_column(gv_column_list_scr, gv_num_columns_scr, "T_ID",           "ID сообщения", 10); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "T_DIRECTION",    "Направление", 10); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "T_MODULE",       "Модуль", 20); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "T_REQ_DTTM",     "Время запроса", 15); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "T_RESP_DTTM",    "Время ответа", 15); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "T_ERROR_CODE",   "Код ошибки", 5); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "T_ERROR_MSG",    "Описание ошибки", 15); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "T_REQID",        "ID сообщения", 35); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "T_REQID_LINKED", "REQID", 35); gv_num_columns_scr = gv_num_columns_scr + 1;

   while(RunScroll(rs_scr, gv_num_columns_scr, gv_column_list_scr, "msg_list", "m_msg_proc_scr", "Список сделок", "ESC - выход  F5 - фильтр  ENTER - подробности", true, 5, NULL, NULL, 40))
      /* Обновляем скроллинг */
      cmd_scr.close(); cmd_scr = NULL;
      rs_scr.close(); rs_scr = NULL;

      sql_filtr = "";

      /*----------------*/
      /* Фильтр по дате */
      /*----------------*/
      if(gv_msg_sel_prm.v_com_date_switch)
         if(gv_msg_sel_prm.v_com_beg_date != date("00.00.0000"))
            sql_filtr = sql_filtr + " AND TRUNC(u_log.t_req_dttm) >= :p_msg_beg_date ";
         end;
         if(gv_msg_sel_prm.v_com_end_date != date("00.00.0000"))
            sql_filtr = sql_filtr + " AND TRUNC(u_log.t_req_dttm) <= :p_msg_end_date ";
         end;
      else
         if(gv_msg_sel_prm.v_com_date != date("00.00.0000"))
            sql_filtr = sql_filtr + " AND TRUNC(u_log.t_req_dttm) = :p_msg_on_date ";
         end;
      end;

      /*-----------------------*/
      /* Фильтр по коду ошибки */
      /*-----------------------*/
      if(gv_msg_sel_prm.v_com_is_err_filtr)
         if(gv_msg_sel_prm.v_com_err_inv)
            sql_filtr = sql_filtr + " AND u_log.t_error_code <> :p_error_code ";
         else
            sql_filtr = sql_filtr + " AND u_log.t_error_code = :p_error_code ";
         end;
      end;

      /*-----------------------------------------*/
      /* Фильтр по подстроке исходного сообщения */
      /*-----------------------------------------*/
      if(gv_msg_sel_prm.v_com_is_substr_search)
         sql_filtr = sql_filtr + string(" AND u_log.t_req_message LIKE '%", gv_msg_sel_prm.v_com_substr_to_search, "%' ");
      end;

      /*--------------*/
      /* Фильтр по ФЛ */
      /*--------------*/
      /*SetPersonRequestMsg*/
      /*<LastName>Колошеин</LastName><FirstName>Юрий</FirstName><MiddleName>Александрович</MiddleName><BirthDate>18.06.1982</BirthDate>*/
      if(   (gv_msg_sel_prm.v_fl_last_name != "")
         or (gv_msg_sel_prm.v_fl_first_name != "")
         or (gv_msg_sel_prm.v_fl_mid_name != "")
         or (gv_msg_sel_prm.v_fl_bth_date != date("00.00.0000")))

         sql_filtr = sql_filtr + " AND (u_log.t_module = :p_mod_set_pers AND (";

         if(gv_msg_sel_prm.v_fl_last_name != "")
            v_aux_buf = true;
            sql_filtr = sql_filtr + "u_log.t_req_message LIKE \'%<ns0:LastName>" + gv_msg_sel_prm.v_fl_last_name + "</ns0:LastName>%\' ";  //shev isup 521681 изменился формат тэга, добавился ns0:
         end;

         if(gv_msg_sel_prm.v_fl_first_name != "")
            if(v_aux_buf)
               sql_filtr = sql_filtr + "AND ";
            else
               v_aux_buf = true;
            end;
            sql_filtr = sql_filtr + "u_log.t_req_message LIKE \'%<ns0:FirstName>" + gv_msg_sel_prm.v_fl_first_name + "</ns0:FirstName>%\' "; //shev isup 521681 изменился формат тэга, добавился ns0:
         end;

         if(gv_msg_sel_prm.v_fl_mid_name != "")
            if(v_aux_buf)
               sql_filtr = sql_filtr + "AND ";
            else
               v_aux_buf = true;
            end;
            sql_filtr = sql_filtr + "u_log.t_req_message LIKE \'%<ns0:MiddleName>" + gv_msg_sel_prm.v_fl_mid_name + "</ns0:MiddleName>%\' "; //shev isup 521681 изменился формат тэга, добавился ns0:
         end;

         sql_filtr = sql_filtr + ")) ";
         v_aux_buf = false;
      end;
	
      /*--------------*/
      /* Фильтр по ЮЛ */
      /*--------------*/
      if(   (gv_msg_sel_prm.v_le_name != "")
         or (gv_msg_sel_prm.v_le_inn != "")
         or (gv_msg_sel_prm.v_le_kpp != ""))

         sql_filtr = sql_filtr + " AND (u_log.t_module = :p_mod_create_le AND ";

         if(gv_msg_sel_prm.v_le_name != "")
            v_aux_buf = true;
            sql_filtr = sql_filtr + "u_log.t_req_message LIKE \'%" + gv_msg_sel_prm.v_le_name + "%\' ";
         end;

         if(gv_msg_sel_prm.v_le_inn != "")
            if(v_aux_buf)
               sql_filtr = sql_filtr + "AND ";
            else
               v_aux_buf = true;
            end;
            sql_filtr = sql_filtr + "u_log.t_req_message LIKE \'%<ns0:INN>" + gv_msg_sel_prm.v_le_inn + "</ns0:INN>%\' ";
         end;

         if(gv_msg_sel_prm.v_le_kpp != "")
            if(v_aux_buf)
               sql_filtr = sql_filtr + "AND ";
            else
               v_aux_buf = true;
            end;
            sql_filtr = sql_filtr + "u_log.t_req_message LIKE \'%<ns0:KPP>" + gv_msg_sel_prm.v_le_kpp + "</ns0:KPP>%\' ";
         end;

// Cherednichenko 02.12.2019 - Лишняя скобка
         //sql_filtr = sql_filtr + ")) ";
         sql_filtr = sql_filtr + ") ";
// /Cherednichenko 02.12.2019
         v_aux_buf = false;
      end;

      sql_scr = sql_main + sql_filtr + sql_order;

      /*-----------*/
      /* Параметры */
      /*-----------*/
      cmd_scr = RSDCommand(sql_scr);
      cmd_scr.NullConversion = true;
      if(gv_msg_sel_prm.v_com_date_switch)
         if(gv_msg_sel_prm.v_com_beg_date != date("00.00.0000"))
            cmd_scr.addParam("p_msg_beg_date", RSDBP_IN, gv_msg_sel_prm.v_com_beg_date)
         end;
         if(gv_msg_sel_prm.v_com_end_date != date("00.00.0000"))
            cmd_scr.addParam("p_msg_end_date", RSDBP_IN, gv_msg_sel_prm.v_com_end_date);
         end;
      else
         if(gv_msg_sel_prm.v_com_date != date("00.00.0000"))
            cmd_scr.addParam("p_msg_on_date", RSDBP_IN, gv_msg_sel_prm.v_com_date);
         end;
      end;

      if(gv_msg_sel_prm.v_com_is_err_filtr)
         cmd_scr.addParam("p_error_code", RSDBP_IN, gv_msg_sel_prm.v_com_err_code);
      end;

      if(   (gv_msg_sel_prm.v_le_name != "")
         or (gv_msg_sel_prm.v_le_inn != "")
         or (gv_msg_sel_prm.v_le_kpp != ""))

         cmd_scr.addParam("p_mod_create_le", RSDBP_IN, v_mod_create_le);
      end;

// Cherednichenko 02.11.2019 - При фильтрации по ФЛ не подставлялся p_mod_set_pers
      if(   (gv_msg_sel_prm.v_fl_last_name != "")
         or (gv_msg_sel_prm.v_fl_first_name != "")
         or (gv_msg_sel_prm.v_fl_mid_name != "")
         or (gv_msg_sel_prm.v_fl_bth_date != date("00.00.0000")))
           cmd_scr.addParam("p_mod_set_pers", RSDBP_IN, v_mod_set_pers);
      end;
// /Cherednichenko 02.11.2019


      rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   end;

   rs_scr.close();

end; /* macro m_run_main_scroll() */


/* Запуск основного скроллинга */
m_run_main_scroll();
Exit(1);