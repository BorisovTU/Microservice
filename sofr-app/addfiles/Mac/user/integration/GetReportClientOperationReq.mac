/*
$Name:        GetReportClientOperationReq.mac
$Module:      Интеграция с внешними системами
$Description: Запрос на получение отчета об операциях клиента в СНОБ
*/
/* BIQ-7294 28.08.20023 Гераськина Т.*/

import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import "RequestWS.mac";                   /* Функция передачи запроса в Web Sphere */
import "uws_exchng_log.mac";              /* Функционал логирования */
import func_lib;

CONST OUTSYS_SYSTEMCODE_ASUP    = "SAP";
CONST OUTSYS_SYSTEMCODE_DIASOFT = "DIASOFT";
CONST OUTSYS_SYSTEMCODE_SOFR    = "SOFR";
CONST OUTSYS_SYSTEMCODE_CFT_HOZ = "CFT_HOZ";
CONST OUTSYS_SYSTEMCODE_CFT_UNI = "CFT_UNI";
CONST OUTSYS_SYSTEMCODE_CFT_ROZ = "CFT_ROZ";

//Отладочный режим GetReportClientOperationReq
//В коммитах никогда не оставлять включенным
//Нужен для того чтобы удобнее было отлаживать запрос с отсутствующей интеграцией
//Вместо отправки запроса будет предложено выбрать файл ответа
PRIVATE CONST DEBUGMODE_GETREPORTCLIENTOPERATIONREQ = false;

// Информация по клиентам
class c_ClientInfo_GetReportClientOperation
   var ClientId;     // ID клиента клиента в системе-источнике                            Да    Идентификатор
end;

// Запрос на получение отчета об операциях клиента
class c_GetReportClientOperationReq
   var TaxYear          : integer;  // Налоговый период, к которому относится событие (налоговый год)    Да
   var ClientInfoList;              // Список информации по клиентам                                     Да    Список
end;

/* Основной класс работы с сервисом */
class c_GetReportClientOperationRequest()
   var GetReportClientOperationReq; /* !!! По названию корневого тега запроса !!! */
   GetReportClientOperationReq = c_GetReportClientOperationReq;
end;


// Информация по записи
class c_ReportRecord
   var IncomeRegionDateTime   : datetime; // Дата и время получения дохода (региональное время)                Да
   var IncomeDateTime         : datetime; // Дата и время получения дохода (московское время)                  Да
   var NOBAmount              : money;    // Сумма НОБ по текущей выплате                                      Нет
   var NDFLCalculateAmount    : money;    // Сумма исчисленного НДФЛ по выполненной операции                   Нет
   var NDFLTaxRate            : integer;  // Налоговая ставка, примененная при исчислении НДФЛ                 Нет
   var NDFLKeepAmount         : money;    // Сумма  НДФЛ удержанного по выполненной операции                   Нет
   var NDFLKeepRate           : integer;  // Налоговая ставка удержанного НДФЛ по выплаченному доходу          Нет
   var KBKCalculate           : string;   // Код КБК для исчисленного к удержанию НДФЛ                         Нет
   var KBKKeep                : string;   // Код КБК удержанного НДФЛ                                          Да
   var TaxBaseType;                       // Вид налоговой базы по НДФЛ, к которой относится событие           Нет   Справочник
   var TaxPayerStatus;                    // Статус налогоплательщика ФЛ                                       Нет   Справочник
   var SystemCode;                        // Код системы, в которой осуществляется выполнение операции         Да    Справочник
   var ClientId;                          // Идентификатор клиента в системе-источнике                         Да    Идентификатор
   var RecordId;                          // Идентификатор записи в хранилище СНОБ                             Да    Идентификатор
   var OperationId;                       // Идентификатор операции                                            Да    Идентификатор
   var EventId;                           // Идентификатор события                                             Да    Идентификатор
   var EventType;                         // Тип события                                                       Да    Справочник
end;


// Информация по записи
class adp_ReportRecordClient(IncomeData)
   var AmountSNOB : double;   // Сумма СНОБ                                                                             Да
   var ClientId;              // Идентификатор клиента в СИ из запроса, по которому выбраны записи Хранилища для отчёта Да    Идентификатор
   var ReportRecordList;      // Список записей операций клиента для формирования отчета                                Да   Список
   
   private macro uInitPrime(IncomeData)
      var err_txt;
      var aux_buff, aux_buff2, ai;
      var temp;
      var temp_error;
      
      var nametag_up = "GetReportClientOperationResp.ReportRecordClientList.ReportRecordClient";
      
      if(ValType(IncomeData) != V_UNDEF)
         AmountSNOB  = getValueFromProperty(IncomeData,  "AmountSNOB", nametag_up, true);
         ClientId    = getSymbolFromProperty(IncomeData, "ClientId"  , nametag_up, true);
      
         aux_buff = uTryToGetPropS(IncomeData, "ReportRecordList");
         if (ValType(aux_buff) == V_GENOBJ)
            ReportRecordList = TArray;
            
            ai = 0;
            nametag_up = nametag_up+".ReportRecordList.ReportRecord";
            while(aux_buff.size > ai)
               temp = c_ReportRecord();
// DEF-52591 не будем проверять ответ, потому что как оказалось, он может не соответствовать схеме, но повлиять на это мы не можем               
               temp.IncomeRegionDateTime   = getValueFromProperty(aux_buff.value(ai),     "IncomeRegionDateTime"  , nametag_up);
               temp.IncomeDateTime         = getValueFromProperty(aux_buff.value(ai),     "IncomeDateTime"        , nametag_up);
               temp.NOBAmount              = getValueFromProperty(aux_buff.value(ai),     "NOBAmount"             , nametag_up);
               temp.NDFLCalculateAmount    = getValueFromProperty(aux_buff.value(ai),     "NDFLCalculateAmount"   , nametag_up);
               temp.NDFLTaxRate            = getValueFromProperty(aux_buff.value(ai),     "NDFLTaxRate"           , nametag_up);
               temp.NDFLKeepAmount         = getValueFromProperty(aux_buff.value(ai),     "NDFLKeepAmount"        , nametag_up);
               temp.NDFLKeepRate           = getValueFromProperty(aux_buff.value(ai),     "NDFLKeepRate"          , nametag_up);
               temp.KBKCalculate           = getValueFromProperty(aux_buff.value(ai),     "KBKCalculate"          , nametag_up);
               temp.KBKKeep                = getValueFromProperty(aux_buff.value(ai),     "KBKKeep"               , nametag_up);
               temp.TaxBaseType            = getDictionaryFromProperty(aux_buff.value(ai),"TaxBaseType"           , nametag_up);
               temp.TaxPayerStatus         = getDictionaryFromProperty(aux_buff.value(ai),"TaxPayerStatus"        , nametag_up);
               temp.SystemCode             = getDictionaryFromProperty(aux_buff.value(ai),"SystemCode"            , nametag_up);
               temp.ClientId               = getSymbolFromProperty(aux_buff.value(ai),    "ClientId"              , nametag_up);
               temp.RecordId               = getSymbolFromProperty(aux_buff.value(ai),    "RecordId"              , nametag_up);
               temp.OperationId            = getSymbolFromProperty(aux_buff.value(ai),    "OperationId"           , nametag_up);
               temp.EventId                = getSymbolFromProperty(aux_buff.value(ai),    "EventId"               , nametag_up);
               temp.EventType              = getDictionaryFromProperty(aux_buff.value(ai),"EventType"             , nametag_up);
               
               ReportRecordList.value(ai) = temp;
               ai = ai + 1;
               temp = NULL;
            end;
         elif(ValType(aux_buff) == V_UNDEF)
// DEF-52591 не будем проверять ответ, потому что как оказалось, он может не соответствовать схеме, но повлиять на это мы не можем
/*            err_txt = string(InErrComPart, nametag_up+".ReportRecordList");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));  */
         else
            err_txt = string(InErrNotObj, nametag_up+".ReportRecordList");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
         end;

      end;
   end;
   
   uInitPrime(IncomeData);   
end;


/* Ответ на получение отчета об операциях клиента */
class adp_GetReportClientOperationResp(IncomeData)
   var ReportRecordClientList;   // Блок данных    Нет
   var ErrorList;                // Список ошибок  Да

   /* Кратность параметров в соответствии со схемой/спецификацией */
   /* В ответе нет смысла проверять обязательность параметров, так как мы можем только залогировать ошибку и больше ничего */
   private macro uInitPrime(IncomeData)
      var err_txt;
      var aux_buff, aux_buff2, ai;
      var temp;
      var temp_error;
      
      var nametag_up = "GetReportClientOperationResp";
      
      if(ValType(IncomeData) != V_UNDEF)
         aux_buff = uTryToGetPropS(IncomeData, "ReportRecordClientList");
         if (ValType(aux_buff) == V_GENOBJ)
            ReportRecordClientList = TArray;
            
            ai = 0;
            while(aux_buff.size > ai)
               temp = adp_ReportRecordClient(aux_buff.value(ai));
               ReportRecordClientList.value(ai) = temp;
               ai = ai + 1;
               temp = NULL;
            end;
         elif(ValType(aux_buff) == V_UNDEF)
         else
            err_txt = string(InErrNotObj, nametag_up+".ReportRecordClientList");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
         end;

         nametag_up = "GetReportClientOperationResp";
         aux_buff = uTryToGetPropS(IncomeData, "ErrorList");
         if(ValType(aux_buff) == V_GENOBJ)
            ErrorList = TArray;
            aux_buff = 0;
            nametag_up = nametag_up+".ErrorList";
            while(IncomeData.ErrorList.size > aux_buff)
               temp_Error = c_Error;
               temp_Error.ErrorCode = getValueFromProperty(IncomeData.ErrorList.value(aux_buff), "ErrorCode", nametag_up);
               temp_Error.ErrorDesc = getValueFromProperty(IncomeData.ErrorList.value(aux_buff), "ErrorDesc", nametag_up);
               
               ErrorList.value(aux_buff) = temp_Error;
               aux_buff = aux_buff + 1;
               temp_Error = NULL;
            end;
         elif(ValType(aux_buff) == V_UNDEF)
         else
            err_txt = string(InErrNotObj, nametag_up+".ErrorList");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
         end;
      end;
   end;
   
   uInitPrime(IncomeData);
end;


private macro MakeErrorAnswer(pcode, ptext)
   var res = adp_GetReportClientOperationResp;
   res.ErrorList = TArray;
   res.ErrorList.value(0) = c_Error;
   res.ErrorList.value(0).ErrorCode = pcode;
   res.ErrorList.value(0).ErrorDesc = ptext;

   return res;
end;


macro GetReportClientOperationFromFile()
  var RespData = NULL;
  var FilePath = "";

  var isTerm = false;
  var choice = 1;
  if (not isStandalone())
    choice = DL_Menu(makeArray("Терминал","Сервер"),"Где ищем файл?");
  end;
  if (choice < 0)
    RunError("Отказались от выбора");
  end;
  if (choice == 0)
    isTerm = true;
  end;
  if (not SelectFile ( FilePath, "*.*", "Выберите файл", 0, isTerm ))
    RunError("Отказались от выбора");
  end;

  var arMenu = makeArray(
    "utf8",
    "rsoem",
    "rsansi"
  );
  choice = DL_Menu(arMenu,"Выберите кодировку файла");
  if (choice < 0)
    RunError("Отказались от выбора");
  end;

  var v_transformator = c_secur_msg_converter();

  var respText = "";
  var fl = TStreamDoc(FilePath, "R", arMenu[choice]);
  var bufLine;
  while (fl.ReadLine(@bufLine))
    respText = respText + bufLine;
  end;

  if (respText)
    if(not v_transformator.m_decode_escaped_char(respText))
      RunError(v_transformator.get_service_msg(), c_usr_err_obj(v_transformator.get_service_msg()));
    end;
    var v_answer_data = v_transformator.m_secur_wol_internal_req(v_transformator.get_pure_msg());
    /* Преобразуем запрос в объектный вид - end */
    if(ValType(v_answer_data) == V_UNDEF)
      RunError("Не удалось преобразовать XML в объект"); 
    end;
    
    RespData = adp_GetReportClientOperationResp(v_answer_data);
  end;

  return RespData;

onError(err)
  return MakeErrorAnswer( c_err_obj.obtain_err_code(err), c_err_obj.obtain_err_msg(err));
end;

/*-------------------------------------------------------------*/
/* Адаптер для вызова сервиса "Запрос на получение отчета об операциях клиента" */
/*  считаем, что ReqData заполняется вовне                     */
/*-------------------------------------------------------------*/
macro run_GetReportClientOperation(ReqData)
   var RespData = NULL;
   var err_txt = NULL;
   
   private var RequestText;
   private var v_transformator;
   private var v_answer;
   private var v_answer_data;
   private var v_logger_obj;
   private var v_log_id;
   private var xml_str;

   if (DEBUGMODE_GETREPORTCLIENTOPERATIONREQ == true)
      return GetReportClientOperationFromFile();
   end;

   private var v_guid = CreateGUID();
   private var v_xml_rpc_reqid = XmlRpcRequestId(); 
   
   private var V_WS_TYPE = GetNumDataType("GetReportClientOperation");

   var v_timeout;
   var stat;
   GetRegistryValue(REG_TIMEOUT_SNOB, V_INTEGER, v_timeout, stat);
   if (stat>0)
      v_timeout = 15;
   end;

   v_transformator = c_secur_msg_converter();

   // заполнение класса информацией 
   RequestText = v_transformator.m_secur_internal_resp(ReqData);
   
   v_logger_obj = uws_exchng_logger(null, v_guid, v_xml_rpc_reqid, "GetReportClientOperationReq", modulename(), null, RequestText);
   v_log_id = v_logger_obj.get_log_id();


   if (err_txt == NULL)
      v_answer = SubmitRequestToWS( C_QUEUE_OUT_ID,  // ИД очереди.
                                    "",              // ИД сообщения.
                                    "",              // Идентификатор Бэк Офиса. (не обрабатывается).
                                    true,            // Признак синхронной обработки. Если не указан, = False
                                    "SOFR",          // Подразделение системы-отпр
                                    V_WS_TYPE,       // ИД типа данных
                                    RequestText,     // Бизнес данные в виде XML
                                    v_log_id,        // ID usr_exchng_log
                                    "EPRSHB2", 
                                    v_timeout
                                       );


      if (v_answer.ResultCode == 0)
         xml_str = v_answer.responsetext;
         v_logger_obj.add_response(xml_str); 

         v_transformator = NULL;
         if (xml_str)
            /* Преобразуем запрос в объектный вид - begin */
            v_transformator = c_secur_msg_converter();
            if(not v_transformator.m_decode_escaped_char(xml_str))
               RunError(v_transformator.get_service_msg(), c_usr_err_obj(v_transformator.get_service_msg()));
            end;
            v_answer_data = v_transformator.m_secur_wol_internal_req(v_transformator.get_pure_msg());
            /* Преобразуем запрос в объектный вид - end */

            if(ValType(v_answer_data) == V_UNDEF)
               err_txt = "Не удалось преобразовать XML в объект";
               RunError(err_txt, c_usr_err_obj(err_txt)); 
            end;
            
            RespData = adp_GetReportClientOperationResp(v_answer_data);
         end;
      else 
         v_logger_obj.add_error_info(v_answer.ResultCode, v_answer.ResultText);
         RespData = MakeErrorAnswer(v_answer.ResultCode, v_answer.ResultText);
      end;

   else
      v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, err_txt);
      RespData = MakeErrorAnswer(UNIVERSAL_ERROR_CODE, err_txt);
   end;

   return RespData;

onError(err)
   stat = c_err_obj.obtain_err_code(err);
   err_txt = c_err_obj.obtain_err_msg(err);

   v_logger_obj.add_error_info(stat, err_txt);
   RespData = MakeErrorAnswer(stat, err_txt);
   return RespData;
end;


/*---------------------------------------*/
/*              Точка входа              */
/*---------------------------------------*/
/* ReqData - данные для отправки         */
/*---------------------------------------*/
macro GetReportClientOperationReq (ReqData)
   return run_GetReportClientOperation(ReqData);
end;