/*-----------------------------------------------------------------------------------------
   UndoNOBFromDepoToSOFR.mac
   Загрузка файла отката НОБ из Депозитария и удаление по нему НОБ
   BIQ-7335
   Гераськина Т.В.
-----------------------------------------------------------------------------------------*/
import rsexts;
import "global_classes.mac";           /* класс ошибок */
import "ws_msg_transform_sec.mac";     /* для обработки ошибок */
import dlmisc;                         /* SplitString*/
import "loadusernptxobj.mac";          /* оббъекты НРД */
import "func_lib";

macro DeleteNPTXObjects(_FullFileName, _flag_error:@bool)
   var FS;
   var file_line:string, file_line_fields = TArray;
   var DBONUMBER, DATEOPEN, DATECLOSE, FIO, CLIENTID,
       NDRTYPE, NDRDIRECTION, NDRLEVEL, NDRSUM, SECNUM, 
       IIS, NDRID, NOBID, RESULTCODE, RESULTTEXT, T_TIMESTAMP;
   var sql_str, cmd;
   var ErrText, err;
   var res_obj, res_obj_arr = TArray, ai = 0;
   var objkind = -1;
   
   /* Пример файла Откат_НОБ_Депозитарий_20211231_104237.txt
   11-111111;01.01.2021;01.03.2021;Иванов Иван Иванович;2010005007544;ProcSec_TS;доход;2;6,000,000.00;RU000A102Y4;;200005007855*/

   _flag_error = false;
   FS = TStreamDoc(_FullFileName, "R", "rsansi"); // только на чтение
   while (FS.readLine(@file_line))
      file_line_fields = SplitString(file_line, ";");  // получим массив полей
      DBONUMBER      = file_line_fields(0);  // 1 Номер ДБО                   Строка   Да   Номер договора БО
      DATEOPEN       = file_line_fields(1);  // 2 Дата открытия               Дата     Да   Дата открытия договора БО формате ДД.ММ.ГГГГ
      DATECLOSE      = file_line_fields(2);  // 3 Дата закрытия               Дата     Да   Дата закрытия договора БО в формате ДД.ММ.ГГГГ
      FIO            = file_line_fields(3);  // 4 ФИО клиента                 Строка   Да   ФИО клиента полностью
      CLIENTID       = file_line_fields(4);  // 5 ID клиента в системе ЦФТ    Строка   Да   
      NDRTYPE        = file_line_fields(5);  // 6 Вид объекта НДР             Строка   Да   Вид НДР для объекта НДР, формируемого в Депозитарии. 
      NDRDIRECTION   = file_line_fields(6);  // 7 Направление                 Строка   Да   Направление объекта НДР, формируемого в Депозитарии. Возможные значения: доход/расход
      NDRLEVEL       = file_line_fields(7);  // 8 Уровень объекта НДР         Строка   Да   Уровень объекта НДР, формируемого в Депозитарии. 
      NDRSUM         = file_line_fields(8);  // 9 Сумма объекта НДР в руб     Число    Да   Сумма выплаты по объекту НДР, приведенная в рубли. Разделитель разрядов-запятая, разделитель рублей и копеек -точка
      SECNUM         = file_line_fields(9);  // 10 ISIN или Госрег№/инф о ПФИ Строка   Да   ISIN ила гос. регистрационный номер ЦБ в разрезе выпусков (если есть) или информация о ПФИ
      IIS            = file_line_fields(10); // 11 Признак ИИС                Строка   Да   Признак наличия ИИС. Возможные значения: Да или пусто (не ИИС)
      NDRID          = file_line_fields(11); // 12 ID объекта НДР             Число    Да   Целочисленный идентификатор объекта НДР (выплаты). Формируется в Diasoft FA#  
      
      NDRSUM = StrSubst(NDRSUM, ",","");
      NDRSUM = Double(NDRSUM);
      
      res_obj = c_Error;

/*      ObjKind = FindObjectKindNRD(NDRTYPE);
      if (ObjKind == -1)
         ErrText = "Вид объекта НДР "+NDRTYPE+" не найден";
         RunError(ErrText);
      end;*/

      err = DeleteUserNPTXOBJ( "DEPO", NDRID, @ErrText);
      if(err)
         res_obj.ErrorCode = 5;  // ошибка
         res_obj.ErrorDesc = ErrText;
         _flag_error = true;  // если хотя бы одна ошибка, то помечаем
      else
         res_obj.ErrorCode = 0;
         res_obj.ErrorDesc = "OK";
      end;
      
      res_obj_arr.value(ai) = res_obj;
      ai = ai+1;
      res_obj = null;
      ObjKind = -1;
   end;
   
   return res_obj_arr;
      
onError(err)
   ErrText = err.message + getFullCmdErrorText(cmd);
   RunError(ErrText, c_err_obj(ErrText,UNIVERSAL_ERROR_CODE));
end;

macro UndoNOBFromDepoToSOFR(del_array:@TArray)
   const REG_IMPORT_PATH = "РСХБ\\ДИРЕКТОРИИ\\IN\\НОБ_ДЕПО_ОТКАТ"; //путь для загрузки файла
   const REG_IMPORT_ARCH_PATH = "РСХБ\\ДИРЕКТОРИИ\\IN\\НОБ_ДЕПО_ОТКАТ_АРХ"; 
   var File_mask = "Откат_НОБ_Депозитарий_*.txt"; // Откат_НОБ_Депозитарий_ГГГГММДД_ЧЧMMCC.txt, пример: Откат_НОБ_Депозитарий_20211231_104237.txt
   var Src_Path;
   var Arch_Path;
   var Dest_Path = "..\\TxtFile"; 
   var FileName = "", FullFileName = "", FS;
   var ErrCode, ErrText;
   var DL;
   var out_obj = c_Error;
   var flag_error = false;

   //получим значение пути загрузки из реестра
   GetRegistryValue(REG_IMPORT_PATH, V_STRING, Src_Path, ErrCode);
   if( ErrCode > 0 )
      Src_Path = "..\\Import\\In";
   end;
   GetRegistryValue(REG_IMPORT_ARCH_PATH, V_STRING, Arch_Path, ErrCode);
   if( ErrCode > 0 )
      Arch_Path = "..\\Import\\Archive";
   end;
   
   // в директории уже должен находится файл, поэтому считаем, что директория существует
   DL = TDirList; 
   DL.List(Src_Path+"\\"+File_mask, "F");
   DL.Sort(2); // сортировка по дате модификации, хотя файл должен быть один
   if (DL.Count == 0)               // файлов нет
      ErrText = "Файлы "+Src_Path+"\\"+File_mask+" отсутствуют";
      RunError(ErrText, c_err_obj(ErrText,UNIVERSAL_ERROR_CODE));
   else 
      FileName = DL.name(0); // первый по порядку сортировки, если вдруг несколько
      DL.Copy(Src_Path+"\\"+FileName, "", Dest_Path+"\\", false, false, ""); //скопируем к себе без индикатора
      if (DL.IsCopy(0))
         FullFileName = Dest_Path+"\\"+FileName;
      else
         ErrText = "Файл "+Src_Path+"\\"+FileName+" не удалось скопировать";
         RunError(ErrText, c_err_obj(ErrText,UNIVERSAL_ERROR_CODE));
      end;
   end;
   
   del_array = DeleteNPTXObjects(FullFileName, @flag_error);
   
   // переносим в архив исходный файл
   DL.Copy(Src_Path+"\\"+FileName, "", Arch_Path+"\\", true, false, ""); 
   if (DL.IsCopy(0))
   else
      ErrText = "Файл "+Src_Path+"\\"+FileName+" не удалось перенести в архив";
      RunError(ErrText, c_err_obj(ErrText,UNIVERSAL_ERROR_CODE));
   end;
   // удаляем у себя
   DL.Remove(FullFileName, "F"); 
   
   if (flag_error)   // были ошибки
      out_obj.ErrorCode = 5;     // список ошибок - в del_array, необходима доп.проверка
      out_obj.ErrorDesc = "Ошибка!";
   else
      out_obj.ErrorCode = 0;
      out_obj.ErrorDesc = "OK";
   end;

   return out_obj;

onError(err)
   out_obj = c_Error;
   out_obj.ErrorCode = c_err_obj.obtain_err_code(err);
   out_obj.ErrorDesc = c_err_obj.obtain_err_msg(err);

   return out_obj;
end;

/*var del_array = TArray;
var rr = UndoNOBFromDepoToSOFR(del_array);  */