/**                                                                                                             
 @file  ChangeFaceFIID_Cl.mac                                                                                           
 @brief Интеграция с внешними системами. Изменение валюты номинала ценной бумаги
                                                                                                                
 #tag                                                                                                          
 - functional_block: Интеграция_RUData
 - code_type: GUI 
                                                                                                                                                                                                                                                                                                             
 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |17.05.2023 |Гераськина Т.В.|DEF-35684                                       |Неверная валюта номинала
 |12.10.2023 |Гераськина Т.В.|DEF-51470                                       |SD9896878||РСХБ Брокер Некорректно указана валюта ценной 
 |           |               |                                                |бумаги ISIN=US63253R2013	
 |07.12.2023 |Жиц М.В.       |DEF-51470                                       |Реализация массового режима   
 |18.01.2024 |Жиц М.В.       |DEF-51470                                       |Не проверяем собственные лоты со статусом "Аннулирован"                                                                                                                                                                                                                                                                                           
*/ 

import RsbFormsInter, FiInter, SecInter;
import "usr_key_const.mac";
import globals, rsd;
import "int_usr_globals.mac";
import "global_utils_intgr.mac";

private var gv_sel_prm = TArray();               /* Параметры */
private var gv_security_form;

private var fi = TRecHandler("fininstr.dbt");;
private var av = TRecHandler("avoiriss.dbt");

FILE output_file() txt write;

private const MAX_SECURITY_COUNT = 10000; //Максимальное кол-во обрабатываемых бумаг

/**
 @brief Класс логирования
*/
private class Logger()
   // заголовок итогового протокола
   macro RepHeader(dt, tm)
      var str;
      if(gv_sel_prm[0].v_ismass == true)
        str = "              Смена номинала по всем бумагам \n";
      else
        str = "              Смена номинала ценной бумаги "+gv_sel_prm[0].v_security+" \n";
      end;
      str = str+"\n";
      str = str+"Дата   : "+string(dt:f)+"\n";
      str = str+"Время  : "+tm+"\n";
      output_file.str = str;
      insert(output_file);
   end;

   //строка протокола
   macro RepStr(_comm)
      var str = string(_comm );
      output_file.str = str;
      insert(output_file);
   end;
end;

/**
 @brief Формирование протокола и обновление валюты номинала по всем бумагам
 Параметры новой валюты берутся из панели, где заполняются глобальные переменные
*/
private macro RunRenew()
   var cur_dt = StrSubSt(string(Date():f),".","")+"_"+StrSubSt(string(Time()),":","");
   private var InfoLogFileName = "..\\TxtFile\\СменаНоминала_"+ iif(gv_sel_prm[0].v_ismass == true, "ВсеБумаги", gv_sel_prm[0].v_security) +"_"+ cur_dt + ".txt" ;
   private var log = Logger();
   var i:integer = 0;
   var errCnt:integer = 0, lotsCnt:integer = 0;
   var Progress = TProgressBar;

   /**
    @brief Запуск обновления валюты номинала по конретной бумаге
    @param[in] prm класс параметров бумаги
    @return true, если смена номинала и обновление лотов прошли успешно пройдены
    @return false, если в процессе выполнения ображуены ошибки
   */
   macro Renew(prm)
      var mess;
      var sql_str, rs, cmd;
      var sql_str_in, rs_in, cmd_in;
      var sql_str_upd, cmd_upd;

      var new_Cost;
      var new_BalanceCost;
      var new_AccBalanceCost;
      var cnt = 0;
   
      var new_NKD;
      var new_NKDFIID;
      var new_Price;
      var new_RelativePrice;

      var isErr:bool = false; //Признак наличия критических ошибок

      RslDefCon.BeginTrans();

      sql_str = " SELECT DISTINCT s.t_sumid "+
                "   FROM dpmwrtsum_dbt s "+
                "  WHERE s.t_fiid = :pfiid AND s.t_cost <> 0 "+
                "    AND s.t_portfolio <> 0 "+
                "    AND s.t_State <> 2 "; //Не проверяем лоты со статусом "Аннулирован"    
      cmd = RSDCommand(sql_str);
      cmd.Addparam("pfiid", RSDBP_IN, prm.v_avoirid);
      rs = RSDRecordSet(cmd);
      while (rs.movenext)
         mess = "!!! Внимание. Для бумаги "+prm.v_security+" обнаружен неклиентский лот "+rs.value("t_sumid")+" с ненулевой стоимостью. Обработка лота выполнена не будет. Обратитесь к аналитикам СОФР";
         log.RepStr(mess);
         isErr = true;
      end;
      cmd.close; 

      if (isErr) //Прервем дальнейшее выполнение при наличии неклиентских лотов
         if (RslDefCon.IsInTrans())
            RslDefCon.RollbackTrans();
         end; 
         return false;
      end;

      sql_str = "UPDATE dfininstr_dbt "+
                "   SET t_facevaluefi = :pfaceid"+
                " WHERE t_fiid = :pfiid ";
      cmd = RSDCommand(sql_str);
      cmd.Addparam("pfaceid", RSDBP_IN, prm.v_faceid_new );
      cmd.Addparam("pfiid", RSDBP_IN, prm.v_avoirid );
      cmd.execute;
    
      mess = "Бумага "+prm.v_security+": выполнена замена валюты номинала в справочнике бумаг. \n"+
             "Старый номинал - "+prm.v_facevaluefi_exist+", новый номинал - "+prm.v_currency;
      log.RepStr(mess);
      
      cmd.close; 
      
      // нужно зафиксировать все-превсе изменения, поэтому обновляем самым неоптимальным образом
      sql_str = " SELECT s.t_sumid,  s.t_cost, s.t_portfolio, s.t_BalanceCost, s.t_AccBalanceCost, s.t_DealID, s.t_date, s.t_currency, l.t_totalcost leg_totalcost, l.t_cost leg_cost, "+
                "        RSI_RSB_FIInstr.ConvSum(l.t_totalcost, s.t_currency, :newfaceid, s.t_date) new_cost, "+
                "        RSI_RSB_FIInstr.ConvSum(l.t_totalcost-l.t_cost, s.t_currency, :newfaceid2, s.t_date) new_nkd, "+
                "        RSI_RSB_FIInstr.ConvSum(1, :newfaceid3, 0, s.t_date) CB, "+
                "        l.t_NKD, l.t_NKDFIID, l.t_Price, l.t_RelativePrice, l.t_Principal, l.t_dealid leg_dealid, l.t_id leg_id "+
                "   FROM dpmwrtsum_dbt s, "+
                "        ddl_leg_dbt l "+
                "  WHERE s.t_fiid = :pfiid AND s.t_cost <> 0 "+
                "    AND s.t_dealid = l.t_dealid";
    
      cmd = RSDCommand(sql_str);
      cmd.Addparam("newfaceid", RSDBP_IN, prm.v_faceid_new);
      cmd.Addparam("newfaceid2", RSDBP_IN, prm.v_faceid_new);
      cmd.Addparam("newfaceid3", RSDBP_IN, prm.v_faceid_new);
      cmd.Addparam("pfiid", RSDBP_IN, prm.v_avoirid);
      rs = RSDRecordSet(cmd);
      cnt = 0;
      while (rs.movenext)
         if (rs.value("t_portfolio") == 0)            
            // dpmwrtsum_dbt
            new_BalanceCost = 0;
            new_AccBalanceCost = 0;
            new_Cost = 0;
            
            if (rs.value("t_currency") == prm.v_faceid_new)
               new_Cost = rs.value("leg_totalcost");
            else 
               new_Cost = rs.value("new_cost");
            end;
            new_BalanceCost = new_Cost;
            if (prm.v_faceid_new != 0)
               new_AccBalanceCost = new_Cost*rs.value("CB");
            else 
               new_AccBalanceCost = rs.value("t_AccBalanceCost");
            end;
            
            sql_str_upd = " UPDATE dpmwrtsum_dbt "+
                          "    SET t_cost = :new_cost, t_BalanceCost = :new_BalanceCost, t_AccBalanceCost = :new_AccBalanceCost "+
                          "  WHERE t_sumid = :psumid";
            cmd_upd = RSDCommand(sql_str_upd);
            cmd_upd.Addparam("new_cost", RSDBP_IN, new_cost);
            cmd_upd.Addparam("new_BalanceCost", RSDBP_IN, new_BalanceCost);
            cmd_upd.Addparam("new_AccBalanceCost", RSDBP_IN, new_AccBalanceCost);
            cmd_upd.Addparam("psumid", RSDBP_IN, rs.value("t_sumid"));
            cmd_upd.execute;
            cmd_upd.close;
    
            mess = "Бумага "+prm.v_security+": выполнена замена валюты номинала в клиентском лоте dpmwrtsum_dbt.t_sumid = "+rs.value("t_sumid")+":";
            log.RepStr(mess);
            mess = "   Cost            Старое значение: "+rs.value("t_cost")+ " Новое значение: "+new_cost;
            log.RepStr(mess);         
            mess = "   BalanceCost     Старое значение: "+rs.value("t_BalanceCost")+ " Новое значение: "+new_BalanceCost;
            log.RepStr(mess);         
            mess = "   AccBalanceCost  Старое значение: "+rs.value("t_AccBalanceCost")+ " Новое значение: "+new_AccBalanceCost;
            log.RepStr(mess);
            
            // dpmwrtbc_dbt
            new_cost = 0;
            new_BalanceCost = 0;
            new_AccBalanceCost = 0;
            sql_str_in = "SELECT b.t_bcid, b.t_sumid, b.t_cost, b.t_balanceCost, b.t_AccBalanceCost, "+
                         "       RSI_RSB_FIInstr.ConvSum( b.t_cost, b.t_currency, :newfaceid, b.t_date) new_cost, "+
                         "       RSI_RSB_FIInstr.ConvSum(1, :newfaceid2, 0, b.t_date) CB "+
                         "  FROM dpmwrtbc_dbt b "+
                         " WHERE b.t_sumid = :psumid ";
            cmd_in = RSDCommand(sql_str_in);
            cmd_in.Addparam("newfaceid", RSDBP_IN, prm.v_faceid_new);
            cmd_in.Addparam("newfaceid2", RSDBP_IN, prm.v_faceid_new);
            cmd_in.Addparam("psumid", RSDBP_IN, rs.value("t_sumid"));
            rs_in = RSDRecordSet(cmd_in);
            while (rs_in.movenext)
               new_cost = rs.value("new_cost");
               new_BalanceCost = new_Cost;
               if (prm.v_faceid_new != 0)
                  new_AccBalanceCost = new_Cost*rs_in.value("CB");
               else 
                  new_AccBalanceCost = rs_in.value("t_AccBalanceCost");
               end;
               sql_str_upd = " UPDATE dpmwrtbc_dbt "+
                             "    SET t_cost = :new_cost, t_BalanceCost = :new_BalanceCost, t_AccBalanceCost = :new_AccBalanceCost "+
                             "  WHERE t_bcid = :pbcid";
               cmd_upd = RSDCommand(sql_str_upd);
               cmd_upd.Addparam("new_cost", RSDBP_IN, new_cost);
               cmd_upd.Addparam("new_BalanceCost", RSDBP_IN, new_BalanceCost);
               cmd_upd.Addparam("new_AccBalanceCost", RSDBP_IN, new_AccBalanceCost);
               cmd_upd.Addparam("pbcid", RSDBP_IN, rs_in.value("t_bcid"));
               cmd_upd.execute;
               cmd_upd.close;
               
               mess = "Бумага "+prm.v_security+": выполнена замена валюты номинала в истории клиентского лота dpmwrtbc_dbt.t_bcid = "+rs_in.value("t_bcid")+":";
               log.RepStr(mess);
               mess = "   Cost            Старое значение: "+rs_in.value("t_cost")+ " Новое значение: "+new_cost;
               log.RepStr(mess);         
               mess = "   BalanceCost     Старое значение: "+rs_in.value("t_BalanceCost")+ " Новое значение: "+new_BalanceCost;
               log.RepStr(mess);         
               mess = "   AccBalanceCost  Старое значение: "+rs_in.value("t_AccBalanceCost")+ " Новое значение: "+new_AccBalanceCost;
               log.RepStr(mess);
            end;
            
            // ddl_leg_dbt
            if (prm.v_isbond)
               if (rs.value("t_currency") == prm.v_faceid_new)
                  new_NKD = rs.value("leg_TotalCost") - rs.value("leg_cost");
               else 
                  new_NKD = rs.value("new_nkd");
               end;
               new_NKDFIID = prm.v_faceid_new;
               if (rs.value("t_RelativePrice") != "X")
                  new_Price = rs.value("leg_Cost")/ rs.value("t_Principal") / prm.v_facevalue * 100;
               else 
                  new_Price = rs.value("t_Price");
               end;
               new_RelativePrice = "X";

               sql_str_upd = " UPDATE ddl_leg_dbt "+
                             "    SET t_NKD = :new_NKD, t_NKDFIID = :new_NKDFIID, t_Price = :new_Price, t_RelativePrice = :new_RelativePrice "+
                             "  WHERE t_id = :leg_id";
               cmd_upd = RSDCommand(sql_str_upd);
               cmd_upd.Addparam("new_NKD", RSDBP_IN, new_NKD);
               cmd_upd.Addparam("new_NKDFIID", RSDBP_IN, new_NKDFIID);
               cmd_upd.Addparam("new_Price", RSDBP_IN, new_Price);
               cmd_upd.Addparam("new_RelativePrice", RSDBP_IN, new_RelativePrice);
               cmd_upd.Addparam("leg_id", RSDBP_IN, rs.value("leg_id"));
               cmd_upd.execute;
               cmd_upd.close;
            
               mess = "Бумага "+prm.v_security+": выполнена замена валюты номинала в сделке ddl_leg_dbt.t_id = "+rs.value("leg_id")+":";
               log.RepStr(mess);
            
               mess = "   NKD             Старое значение: "+rs.value("t_NKD")+ " Новое значение: "+new_NKD;
               log.RepStr(mess);
               mess = "   NKDFIID         Старое значение: "+rs.value("t_NKDFIID")+ " Новое значение: "+new_NKDFIID;
               log.RepStr(mess);
               mess = "   Price           Старое значение: "+rs.value("t_Price")+ " Новое значение: "+new_Price;
               log.RepStr(mess);
               mess = "   RelativePrice   Старое значение: "+rs.value("t_RelativePrice")+ " Новое значение: "+new_RelativePrice;
               log.RepStr(mess);         
            else 
               new_Price = rs.value("leg_Cost") / rs.value("t_Principal");
               new_RelativePrice = UNSET_CHAR;

               sql_str_upd = " UPDATE ddl_leg_dbt "+
                             "    SET t_Price = :new_Price, t_RelativePrice = :new_RelativePrice "+
                             "  WHERE t_id = :leg_id";
               cmd_upd = RSDCommand(sql_str_upd);
               cmd_upd.Addparam("new_Price", RSDBP_IN, new_Price);
               cmd_upd.Addparam("new_RelativePrice", RSDBP_IN, new_RelativePrice);
               cmd_upd.Addparam("leg_id", RSDBP_IN, rs.value("leg_id"));
               cmd_upd.execute;
               cmd_upd.close;
            
               mess = "Бумага "+prm.v_security+": выполнена замена валюты номинала в сделке ddl_leg_dbt.t_id = "+rs.value("leg_id")+":";
               log.RepStr(mess);
            
               mess = "   Price           Старое значение: "+rs.value("t_Price")+ " Новое значение: "+new_Price;
               log.RepStr(mess);
               mess = "   RelativePrice   Старое значение: "+rs.value("t_RelativePrice")+ " Новое значение: "+new_RelativePrice;
               log.RepStr(mess);         
            end;            
         end;
         cnt = cnt + 1
      end;
      if (cnt == 0)
         mess = "Для бумаги "+prm.v_security+" не обнаружено клиентских лотов с ненулевой стоимостью";
         log.RepStr(mess);
      end;
      lotsCnt = lotsCnt + cnt; //Добавим кол-во обработанных лотов по бумаге к общему количеству
    
      if (RslDefCon.IsInTrans())
         RslDefCon.CommitTrans();
      end;

      return true;

   onerror(e)
      if (RslDefCon.IsInTrans())
         RslDefCon.RollbackTrans();
      end; 

      mess = e.message + getFullCmdErrorText(cmd);
      log.RepStr(mess);

      return false;
   end;

   Open(output_file, InfoLogFileName/*, "rsansi"*/); // для показа на экране нужен 866, а для сохранения - лучше 1251, выберем DOS
   log.RepHeader(date(),time());

   Progress.Init(gv_sel_prm.size(), "Изменение валют номинала ценных бумаг", "Просмотр ценных бумаг...");

   while(i < gv_sel_prm.size())
      if(not Renew(gv_sel_prm[i])) //Смена номинала по i-ой бумаге
         errCnt = errCnt + 1; //В случае ошибки добавим ее к счетчику
      end;
      log.RepStr(""); //Напечатаем пустую строчку, чтобы отделить бумаги
      i = i + 1;
      Progress.Use();
   end;
   Progress.Remove(); 

   if(gv_sel_prm[0].v_ismass == true) //В массовом режиме дополнительно печатаем "подвал" протокола
      log.RepStr("Обработано бумаг: " + gv_sel_prm.size());
      log.RepStr("Обработано лотов: " + lotsCnt);
      log.RepStr("Количество ошибок: " + errCnt);
   end;
      
   Close(output_file);
   ViewFile(output_file);
end;

/**
@brief Параметры отбора объектов
*/
private class c_selection_prm()
   var v_security;
   var v_currency;

   var v_avoirid;
   var v_faceid_new;
   var v_facevaluefi_exist;
   var v_facevaluefiid_exist;
   var v_isbond;
   var v_facevalue;
   var v_ismass:bool; //Признак массового режима

   /* Значения по умолчанию */
   macro m_init_selection_prm()
      v_security = "";
      v_currency = "";

      v_avoirid = -1;
      v_faceid_new = -1;
      v_facevaluefi_exist = "";
      v_facevaluefiid_exist = -1;
      v_isbond = false;
      v_facevalue = 0;
      v_ismass = false;
   end;

   m_init_selection_prm();
end; /* class c_selection_prm() */


/**
@brief Класс основной формы
*/
class (TRsbPanel) c_security_form()
   const CV_POS_X = 3;
   const CV_POS_Y = 2;
   var v_security_fld;
   var v_currency_fld;

   var v_run_btn;

   // найдем валюту номинала в данных Рудаты
   macro FindFaceValueFI()
      var sql_str, cmd, rs;
      
      gv_sel_prm[0].v_security = getControl("v_security_fld").value;
      sql_str = " with bb as ( "+
                "    SELECT  f.t_fi_code, f.t_fiid, f.t_name, a.t_isin, a.t_lsin, o.t_code, f.t_avoirkind, "+
                "            f.t_drawingdate, f.t_facevalue, f.t_facevaluefi, c.t_ccy, nvl(r.faceftname_nrd, r.faceftname) faceftname, "+
                "            RSB_FIINSTR.FI_AVRKINDSGETROOT (f.t_fi_kind, f.t_avoirkind) fikindroot "+
                "      FROM "+
                "         dfininstr_dbt f, "+
                "         davoiriss_dbt a, "+
                "         dobjcode_dbt o, "+
                "         dfininstr_dbt c, "+
                "         SOFR_INFO_FINTOOLREFERENCEDATA r "+
                "     WHERE f.t_fiid = a.t_fiid "+
                "       AND o.t_objectid = f.t_fiid "+
                "       AND o.t_objecttype = 9 "+
                "       AND o.t_codekind = 104 "+
                "       AND c.t_fiid = f.t_facevaluefi "+
                "       AND to_char(r.fintoolid) = o.t_code) "+
                "  SELECT * FROM bb WHERE bb.t_isin = :pcode1 "+
                "     union "+
                "  SELECT * FROM bb WHERE bb.t_lsin = :pcode2 "+
                "     union "+
                "  SELECT * FROM bb WHERE bb.t_code = :pcode3 ";
      cmd = RSDCommand(sql_str);
      cmd.Addparam("pcode1", RSDBP_IN, gv_sel_prm[0].v_security);
      cmd.Addparam("pcode2", RSDBP_IN, gv_sel_prm[0].v_security);
      cmd.Addparam("pcode3", RSDBP_IN, gv_sel_prm[0].v_security);
      rs = RSDRecordSet(cmd);
      if (rs.movenext)
         gv_sel_prm[0].v_avoirid = rs.value("t_fiid");
         gv_sel_prm[0].v_currency = rs.value("faceftname");
         gv_sel_prm[0].v_facevaluefi_exist = rs.value("t_ccy");
         gv_sel_prm[0].v_facevaluefiid_exist = rs.value("t_facevaluefi");
         gv_sel_prm[0].v_facevalue = rs.value("t_facevalue");
         if (rs.value("fikindroot") == 17)
            gv_sel_prm[0].v_isbond = true;
         end;
      end;
      rs.close; cmd.close;
      
      if (gv_sel_prm[0].v_currency == gv_sel_prm[0].v_facevaluefi_exist) /* совпадают */
         gv_sel_prm[0].v_faceid_new = -2;
      else /* отличаются */
         sql_str = "SELECT t_fiid FROM dfininstr_dbt WHERE t_ccy = :pcode";
         cmd = RSDCommand(sql_str);
         cmd.Addparam("pcode", RSDBP_IN, gv_sel_prm[0].v_currency);
         rs = RSDRecordSet(cmd);
         if (rs.movenext)
            gv_sel_prm[0].v_faceid_new = rs.value("t_fiid");
         end;
         rs.close;
         cmd.close;
      end;
   end;

   // найдем валюту номинала в данных Рудаты для каждой бумаги, где требуется смена номинала и компишем информацию в массив
   macro MassFindFaceValueFI()
      var sql_str, cmd, rs;
      var i:integer = 0;

      gv_sel_prm.size = 0; //Очистка массива
      
      sql_str = "SELECT f.t_fiid, f.t_name, a.t_isin, f.t_facevalue, f.t_facevaluefi, c.t_ccy, "+
                "       nvl(r.faceftname_nrd, r.faceftname) faceftname, n.t_fiid faceid_new, "+
                "       RSB_FIINSTR.FI_AVRKINDSGETROOT (f.t_fi_kind, f.t_avoirkind) fikindroot "+ 
                "  FROM dfininstr_dbt f, "+
                "       davoiriss_dbt a, "+
                "       dobjcode_dbt o, "+
                "       dfininstr_dbt c, "+
                "       SOFR_INFO_FINTOOLREFERENCEDATA r, "+
                "       dfininstr_dbt n "+
                " WHERE f.t_fiid = a.t_fiid "+
                "   AND o.t_objectid = f.t_fiid "+
                "   AND o.t_objecttype = 9 "+
                "   AND o.t_codekind = 104 "+
                "   AND c.t_fiid = f.t_facevaluefi "+
                "   AND to_char(r.fintoolid) = o.t_code "+
                "   AND c.t_ccy <> nvl(r.faceftname_nrd, r.faceftname) "+
                "   AND n.t_ccy = nvl(r.faceftname_nrd, r.faceftname) "+
                "   and rownum <= "+MAX_SECURITY_COUNT; 
      cmd = RSDCommand(sql_str);
      rs = RSDRecordSet(cmd);
      while (rs.movenext)
         gv_sel_prm[i] = c_selection_prm();
         gv_sel_prm[i].m_init_selection_prm;
         gv_sel_prm[i].v_avoirid = rs.value("t_fiid");
         gv_sel_prm[i].v_currency = rs.value("faceftname");
         gv_sel_prm[i].v_facevaluefi_exist = rs.value("t_ccy");
         gv_sel_prm[i].v_facevaluefiid_exist = rs.value("t_facevaluefi");
         gv_sel_prm[i].v_facevalue = rs.value("t_facevalue");
         if (rs.value("fikindroot") == 17)
            gv_sel_prm[i].v_isbond = true;
         end;
         gv_sel_prm[i].v_faceid_new = rs.value("faceid_new");
         gv_sel_prm[i].v_security = rs.value("t_isin");
         gv_sel_prm[i].v_ismass = true;
         i = i + 1;
      end;
      rs.close; cmd.close;
   end;

   // проверка корректности параметров перед обновлением
   macro check_params()
      if ( gv_sel_prm[0].v_security == "" )
         if(GetTrue(false, "Вы действительно желаете выполнить замену номинала по всем бумагам с неверным номиналом?")) /*DEF-51470*/
            MassFindFaceValueFI(); //Заполнение массива параметров по каждой бумаге, где требуется смена номинала
            if(gv_sel_prm.size != 0)
               return true;
            else
               msgbox("Для всех бумаг валюта номинала задана верно. Изменения валюты номинала не требуется");
               return false;
            end;
         else
            return false;
         end;
      end;

      if ( gv_sel_prm[0].v_currency == "" )
         msgbox("Не указана валюта номинала");
         return false;
      end;
      
      if ( gv_sel_prm[0].v_avoirid == -1 )
         msgbox("Не найдена ценная бумага с указанным идентификатором");
         return false;
      end;

      if ( gv_sel_prm[0].v_faceid_new == -1 )
         msgbox("Не найдена валюта номинала с указанным кодом");
         return false;
      end;
      
      if ( gv_sel_prm[0].v_faceid_new == -2 )
         msgbox("Для бумаги "+gv_sel_prm[0].v_security+" валюта номинала задана верно. Изменения валюты номинала не требуется");
         return false;
      end;
      
      return true;
   end;
   
   /* Обработчик клавиш */
   macro m_evnt_on_key_pressed(p_rsb_evnt)
      if(p_rsb_evnt.KeyCode == K_F2)
         if (check_params)
            RunRenew();
         end;
         gv_sel_prm.size = 0;
         gv_sel_prm[0] = c_selection_prm();
         gv_sel_prm[0].m_init_selection_prm;
         v_security_fld.value = "";
         v_currency_fld.value = "";
         redraw();
      elif(p_rsb_evnt.KeyCode == K_F3)
         if( this.focusedControl.Name == "v_security_fld" )
            if (ListAvoiriss (FIKIND_ALLAVOIRISS, fi, av, true)==true)
               gv_sel_prm[0].v_avoirid = fi.rec.fiid;
               gv_sel_prm[0].v_security = av.rec.ISIN;
               v_security_fld.value = av.rec.ISIN;
               FindFaceValueFI();
               v_currency_fld.value = gv_sel_prm[0].v_currency;
               redraw();
            end;
         end;
      end;
   end;
   
    /*Обработчик смены фокуса*/
    macro m_evnt_on_remove_focus(p_rsb_evnt)
      if(this.focusedControl.Name == "v_security_fld")
         FindFaceValueFI();
         v_currency_fld.value = gv_sel_prm[0].v_currency;
         redraw();
      end;
    end;

   /*Обработчик нажатия кнопок*/
   macro m_evnt_run_btn(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_BUTTON_CLICKED)
         if (check_params)
            RunRenew();
         end;
         gv_sel_prm.size = 0;
         gv_sel_prm[0] = c_selection_prm();
         gv_sel_prm[0].m_init_selection_prm;
         v_security_fld.value = "";
         v_currency_fld.value = "";
         redraw();
      end;
   end;

   /* Экранная форма */
   private macro m_init_security_form(i_this_obj)
      Caption = "Смена номинала бумаги";

      Status = "[F2] Выполнить [Esc] Выход ";

      /* Бумага */
      AddLabel(TRsbLabel(CV_POS_X, CV_POS_Y, "Идентификатор бумаги"));
      v_security_fld = TRsbEditField(FT_STRING);
      v_security_fld.Name = "v_security_fld";
      v_security_fld.bindValue(gv_sel_prm[0], "v_security", 20);
      v_security_fld.setPosition(CV_POS_X+20, CV_POS_Y);
      v_security_fld.setSize(20, 1);
      v_security_fld.focusable = true;
      v_security_fld.enabled = true;
      v_security_fld.editable = true;

      AddControl(v_security_fld);

      /* Номинал */
      AddLabel(TRsbLabel(CV_POS_X, CV_POS_Y+1, "Новый код валюты"));
      v_currency_fld = TRsbEditField(FT_STRING);
      v_currency_fld.Name = "v_currency_fld";
      v_currency_fld.bindValue(gv_sel_prm[0], "v_currency", 10);
      v_currency_fld.setPosition(CV_POS_X+20, CV_POS_Y+1);
      v_currency_fld.setSize(10, 1);
      v_currency_fld.focusable = true;
      v_currency_fld.enabled = true;
      v_currency_fld.editable = false;

      AddControl(v_currency_fld);

      /* Кнопка выгрузки */
      v_run_btn = TRsbPushButton("Запустить");
      v_run_btn.setPosition(CV_POS_X, CV_POS_Y+3);
      v_run_btn.setSize(12, 1);
      v_run_btn.onClicked(R2M(i_this_obj, "m_evnt_run_btn"));
      AddControl(v_run_btn);

      AddEventHandler(RSB_EV_KEY_PRESSED, R2M(i_this_obj, "m_evnt_on_key_pressed"));
      AddEventHandler(RSB_EV_REMOVE_FOCUS, R2M(i_this_obj, "m_evnt_on_remove_focus"));

      setSize(45, 7);
      setPosition(20, 5);

   end;

   InitTRsbPanel();

   m_init_security_form(this);
end; /* class (TRsbPanel) c_security_form() */

   gv_sel_prm[0] = c_selection_prm();

   gv_security_form = c_security_form();
   gv_security_form.run;

exit(1);