/** 
 @file CdiGetOrgReq.mac
 @brief Cервис получения из CDI карточки организации по идентификатору
  
 # tag 
 - functional_block:Клиенты_Регистрация_на_бирже
 - code_type:API
 - CDI

   
 # changeLog 
 |date       |author         |tasks            |note                                                         
 |-----------|---------------|-----------------|--------------------------------
 |05.09.2023 |Гераськина Т.В.|BIQ-10268        | Создание
 |31.10.2023 |Гераськина Т.В.|DEF-54193        | Исправлена ошибка тестовых данных                  
 |28.11.2023 |Гераськина Т.В.|DEF-55464        | При ошибках отправки запроса (запрос сформирован, но не получен ответ) устанавливается статус 1005 (повторить)

*/ 
import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import "RequestWS.mac";                   /* Функция передачи запроса в Web Sphere */
import "uws_exchng_log.mac";              /* Функционал логирования */
import func_lib;
import global_classes;
import "CDIOrg_lib.mac";

class c_CdiGetOrgResult 
   var ErrorCode : string;
   var ErrorDesc : string;
   var IsPublishResponse : bool;
   var NeedUpdate : bool;
   IsPublishResponse = false;
end;

/**
 @brief Класс запроса
 @param[in] _ABS_Id Идентификатор субъекта код АБС - код 101
 @param[in] _ABS_Id Идентификатор субъекта код CDI - код 110

 Код CDI должен быть заполнен обязательно
 Может быть заполнены SourceId + SourceSystem или PartyId (код CDI)
*/
class c_CdiGetOrgReq(_ABS_Id, _CDI_Id) 
   var SourceId      ;  // ID субъекта в источнике    Идентификатор 
   var SourceSystem  ;  // Код системы источника      Справочник
   var PartyId       ;  // ID клиента в CDI           Идентификатор
   
   macro uInitPrime(_ABS_Id, _CDI_Id)
      SourceId = NULL;
      SourceSystem = NULL;
      if (Valtype(_ABS_Id) != V_UNDEF)
         if (Strlen(_ABS_Id) > 1)
            SourceId = c_IntegrationSymbolicIdentifierXType();
            SourceId.ObjectId = _ABS_Id;
            
            SourceSystem = c_IntegrationDictionaryRecordXType();
            SourceSystem.RecordCode = "CFT";
         end;
      end;
      if (Valtype(_CDI_Id) != V_UNDEF)
         if (Strlen(_CDI_Id) > 1)
            PartyId = c_IntegrationSymbolicIdentifierXType();
            PartyId.ObjectId = _CDI_Id;
         end;
      end;
   end;
end;


/**
 @brief Корневой элемент ответа - AS ID - как пришел
 @param[in] Входящий объект XMLRPC
*/
private class adp_CdiGetOrgResp(IncomeData)
   var CdiGetOrg;    // Блок ответа сервиса получения карточки организации по идентификатору
   var ErrorList;    // Блок ошибок обработки
   
   /* Кратность параметров в соответствии со схемой/спецификацией */
   private macro uInitPrime(IncomeData)
      var err_txt;
      var aux_buff, aux_buff2, ai;
      var temp_Error;
            
      var nametag_up = "CdiGetOrgResp";
      
      if(ValType(IncomeData) != V_UNDEF)
         aux_buff = uTryToGetPropS(IncomeData, "CdiGetOrg");
         if(ValType(aux_buff) == V_GENOBJ)
            CdiGetOrg = adp_CdiGetOrg(aux_buff, nametag_up+".CdiGetOrg");
         elif(ValType(aux_buff) == V_UNDEF)
         else
            err_txt = string(InErrNotObj, nametag_up+".CdiGetOrg");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
         end;

         aux_buff = uTryToGetPropS(IncomeData, "ErrorList");
         if(ValType(aux_buff) == V_GENOBJ)
            ErrorList = TArray;
            aux_buff = 0;
            nametag_up = nametag_up+".ErrorList";
            while(IncomeData.ErrorList.size > aux_buff)
               temp_Error = c_Error;
               temp_Error.ErrorCode = getValueFromProperty(IncomeData.ErrorList.value(aux_buff), "ErrorCode", nametag_up);
               temp_Error.ErrorDesc = getValueFromProperty(IncomeData.ErrorList.value(aux_buff), "ErrorDesc", nametag_up);
               
               ErrorList.value(aux_buff) = temp_Error;
               aux_buff = aux_buff + 1;
               temp_Error = NULL;
            end;
         elif(ValType(aux_buff) == V_UNDEF)
         else
            err_txt = string(InErrNotObj, nametag_up+".ErrorList");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
         end;
      end;
   end;
   
   uInitPrime(IncomeData);
end; 


/**
 @brief Основной класс работы с сервисом
 @param[in] _ABS_Id Идентификатор субъекта код АБС - код 101
 @param[in] _ABS_Id Идентификатор субъекта код CDI - код 110
*/
class c_CdiGetOrgReqProcessor(_ABS_Id, _CDI_Id)
   var CdiGetOrgReq; /* !!! По названию корневого тега запроса !!! */
   CdiGetOrgReq = c_CdiGetOrgReq;
   CdiGetOrgReq.uInitPrime(_ABS_Id, _CDI_Id);
end; 


/**
 @brief Адаптер для вызова сервиса "Запрос сервиса получения карточки организации по идентификатору"
 @param[in] ReqData объект c_CdiGetOrgReqProcessor
 @param[in] p_PartyID Идентификатор СОФР
 @param[in] p_recid utableprocessevent.t_recid для связки в "Интерфейс таблицы событий"

 Считаем, что на вход приходит корректный c_CdiGetOrgReqProcessor
*/
macro run_CdiGetOrgReq(ReqData, p_PartyID, p_recid)
   var RespData = NULL;
   var err_txt = NULL, err_code = 0;
   var res = c_Error;
   
   private var v_out_obj;
   private var RequestText;
   private var v_transformator;
   private var v_answer;
   private var v_answer_data;
   private var v_logger_obj;
   private var v_log_id;
   private var xml_str;
   private var v_xml_rpc_reqid = XmlRpcRequestId(); 

   private var V_WS_TYPE = GetNumDataType("CdiGetOrg");
   res = c_CdiGetOrgResult;
   res.ErrorCode = -1;
   res.ErrorDesc = "Неизвестная ошибка";
   res.NeedUpdate = false;
   var res_updateorg;


   //CCBO-10808 не все субъекты выгружаются
   if (CheckBlockUnloadCDI(p_PartyID)) 
      res.ErrorCode = ERROR_CODE_BLOCK_UNLOAD;
      res.ErrorDesc = ERROR_DESC_BLOCK_UNLOAD;
      res.NeedUpdate = false;
      return res;
   end;

   if ( (ReqData == NULL) or (ValType(ReqData) == V_UNDEF) )
      return null;
   end;

   v_transformator = c_secur_msg_converter();

   RequestText = v_transformator.m_secur_internal_resp(ReqData);
   
   v_logger_obj = uws_exchng_logger(null, null, v_xml_rpc_reqid, "CdiGetOrgReq", modulename(), p_recid, RequestText);
   v_log_id = v_logger_obj.get_log_id();

   if (err_txt == NULL)
      v_answer = SubmitRequestToWS( 2,          // ИД очереди.
                                    "",         // ИД сообщения.
                                    "",         // Идентификатор Бэк Офиса. (не обрабатывается).
                                    true,       // Признак синхронной обработки. Если не указан, = False
                                    "SOFR",     // Подразделение системы-отпр
                                    V_WS_TYPE,  // ИД типа данных !!!!
                                    RequestText,// Бизнес данные в виде XML
                                    v_log_id,   // ID usr_exchng_log
                                    "CDI"
                                   );

      if (v_answer.ResultCode == 0)
         xml_str = v_answer.responsetext;
         v_logger_obj.add_response(xml_str); 
         
         v_transformator = NULL;
         if (xml_str)
            /* Преобразуем запрос в объектный вид - begin */
            v_transformator = c_secur_msg_converter();
            if(not v_transformator.m_decode_escaped_char(xml_str))
               RunError(v_transformator.get_service_msg(), c_usr_err_obj(v_transformator.get_service_msg()));
            end;
            v_answer_data = v_transformator.m_secur_wol_internal_req(v_transformator.get_pure_msg());
            /* Преобразуем запрос в объектный вид - end */

            if(ValType(v_answer_data) == V_UNDEF)
               err_txt = "Не удалось преобразовать XML в объект";
               RunError(err_txt, c_usr_err_obj(err_txt)); 
            end;
            
            RespData = adp_CdiGetOrgResp(v_answer_data);
            /* Преобразование объекта к виду, ожидаемому Системой */
            
            res_updateorg = UpdateParty_ByCDI(RespData.CdiGetOrg, p_PartyID, true);
            res.ErrorCode = res_updateorg.ErrorCode;
            res.ErrorDesc = res_updateorg.ErrorDesc;
            res.NeedUpdate = res_updateorg.NeedUpdate;
         end;
      else 
         v_logger_obj.add_error_info(v_answer.ResultCode, v_answer.ResultText);
         res.ErrorCode = 1005; // будем считать, что в любом случае, когда не получен ответ, стоит повторить  
         res.ErrorDesc = v_answer.ResultText;
      end;

   else
      v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, err_txt);
      res.ErrorCode = UNIVERSAL_ERROR_CODE;  // будем считать, что в любом случае, когда не получен ответ, стоит повторить  
      res.ErrorDesc = err_txt;
   end;

   return res;

onError(err)
   err_code = c_err_obj.obtain_err_code(err);
   err_txt = c_err_obj.obtain_err_msg(err);

   v_logger_obj.add_error_info(err_code, err_txt);

   res = c_CdiGetOrgResult;
   res.ErrorCode = err_code;  // здесь глобальная неконтролируемая ошибка, повторять не будем
   res.ErrorDesc = err_txt;
   return res;
end; 


/**
 @brief Точка входа
 @param[in] ReqData объект c_CdiGetOrgReqProcessor
 @param[in] in_partyid Идентификатор СОФР
 @param[in] in_recid utableprocessevent.t_recid для связки в "Интерфейс таблицы событий"

 Считаем, что на вход приходит корректный c_CdiGetOrgReqProcessor
*/
macro CdiGetOrgReq (ReqData, in_partyid, in_recid) 
   return run_CdiGetOrgReq(ReqData, in_partyid, in_recid); 
end;

