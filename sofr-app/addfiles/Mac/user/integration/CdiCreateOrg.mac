/** 
 @file CdiCreateOrg.mac
 @brief Сервис создания карточек клиентов ЮЛ в CDI 
  
 Сервис создания карточек клиентов ЮЛ в CDI. 
 По входному PartyID собирает данные и делает запрос на создание карточки клиента в CDI.

 # tag 
 - functional_block:Клиенты_Регистрация_на_бирже
 - code_type:API
 - CDI


 # changeLog 
 |date       |author         |tasks            |note                            
 |-----------|---------------|-----------------|--------------------------------
 |2023.09.22 |Фаршатов Г. К. |CCBO-6568        | Реализация сервиса             
 |2023.11.20 |Фаршатов Г. К. |DEF-56224        | Доработка согласно новой ревизии ТЗ  
 |2023.12.22 |Фаршатов Г. К. |DEF-58322        | Доработка согласно новой ревизии ТЗ   
 |2023.12.26 |Гераськина Т.В.|DEF-59120        | Приведены в соответствие CdiCreate и CdiUpdate                  

*/ 
import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import "RequestWS.mac";                   /* Функция передачи запроса в Web Sphere */
import "uws_exchng_log.mac";              /* Функционал логирования */
import func_lib;
import global_classes;
import dlquery;
import "CDIOrg_lib.mac";

CONST DataTimeNullCDI = DtTm(date(11,11,1911), time(11,11,11));
CONST DataNullCDI = date(11,11,1911);

/**
 @brief Класс соответствующий типу данных IntegrationDictionaryRecordXType
 @note Заполнен специальными значениями
 
 Тип используемый для передачи значений справочника, которые подлежат трансформации
*/
class c_IntegrationDictionaryRecordXTypeNull()
   var RecordCode       : string = "NULL"; //Поле типа используемого для передачи значений справочника
   var RecordTitle      : string; //Поле типа используемого для передачи значений справочника
   var Note             : string; //Поле типа используемого для передачи значений справочника
   var SystemId         : string; //Поле типа используемого для передачи значений справочника
   var SystemNodeId     : string; //Идентификатор узла/экземпляра системы/приложения, в контексте которой существует идентификатор объекта
   var Desc             : string; //Поле типа используемого для передачи значений справочника
   var DictionaryCode   : string; //Поле типа используемого для передачи значений справочника
end;

/**
 @brief Класс соответствующий типу данных IntegrationSymbolicIdentifierXType
 @note Заполнен специальными значениями
 
 Интеграционный символьный идентификатор с поддержкой распределенной
   системы (может хранить информацию об системе/приложении, а также об ее
   узле/экземпляре, в контексте которого существует идентификатор)
*/
class c_IntegrationSymbolicIdentifierXTypeNull()
   var ObjectId     : string = "NULL"; //Символьный идентификатор объекта
   var SystemId     : string; //Идентификатор системы/приложения, в контексте которой
                                       // существует идентификатор объекта
   var SystemNodeId : string; //Идентификатор узла/экземпляра системы/приложения, в контексте
                                       // которой существует идентификатор объекта
end;

/**
 @brief Класс соответствующий типу данных Уставной капитал
 @note Заполнен специальными значениями
*/
class c_AuthorizedCapitalNull()
   var Value      ;      // Значение уставного капитала
   var Currency   : c_IntegrationDictionaryRecordXTypeNull; // Валюта уставного капитала
end;

/**
 @brief Класс соответствующий типу данных Информация ПОД/ФТ
 @note Заполнен специальными значениями
*/
class c_PODFTInformationNull()
   var PODFTFormCreateDate : date = DataNullCDI;     // Дата формирования анкеты по ПОД/ФТ 
   var PODFTFormUpdateDate : date = DataNullCDI;     // Дата обновления анкеты по ПОД/ФТ   
   var FATCAStatusDate     : date = DataNullCDI;     // Дата статуса FATCA                  
   var CRSStatusDate       : date = DataNullCDI;     // Дата статуса CRS                    
   var FATCAStatus         : c_IntegrationDictionaryRecordXTypeNull;              // Статус FATCA  
   var CRSStatus           : c_IntegrationDictionaryRecordXTypeNull;              // Статус CRS   
end;

/**
 @brief Класс соответствующий типу данных PartyType в CdiCreateOrgReq
 @note Заполнен специальными значениями
 
 Класс необходим для создания объекта xml PartyType в CdiCreateOrgReq
*/
class c_CdiCreateOrgReq_PartyType()
   var Active                       : string   = "YES";       // Признак активности записи                       
   var LastUpdateDate               : datetime = DataTimeNullCDI; // Дата и время последнего изменения записи       
   var Vip                          : string   = "NULL";      // Признак вип-клиента                              
   var ShortNameOrg                 : string   = "";      // Краткое наименование                             
   var FullNameOrg                  : string   = "";      // Полное наименование                              
   var ShortNameOrgLat              : string   = "";      // Краткое латинское наименование                   
   var FullNameOrgLat               : string   = "";      // Полное латинское наименование                    
   var ResidentCurrency             : string   = "NULL";      // Признак резидента по валютному законодательству  
   var ResidentTax                  : string   = "";      // Признак налогового резидента                     
   var ProblemFactor                : string   = "NULL";      // Есть ФП (фактор проблемности)                    
   var EssentialProblemFactor       : string   = "NULL";      // Есть СФП (существенный фактор проблемности)      
   var DealProblemFactor            : string   = "NULL";      // Есть ФПЗ (фактор проблемной задолженности)       
   var Problem                      : string   = "NULL";      // Признак проблемного                              
   var EmployeesCount               : string   = "NULL";      // Численность работников                           
   var INN                          : string   = "";          // ИНН                                              
   var KPPRegistrationPlace         : string   = "";          // КПП по месту регистрации                         
   var KPPMajorTaxpayer             : string   = "NULL";      // КПП крупнейшего налогоплательщика                
   var OGRN                         : string   = "";          // ОГРН/ОГРНИП                                      
   var KIO                          : string   = "NULL";      // КИО                                              
   var OKPO                         : string   = "";          // ОКПО                                             
   var RegistrationDate             : date     = DataNullCDI; // Дата регистрации (до ОГРН/ОГРНИП)                
   var CmoLiquidationDate           : date     = DataNullCDI; // Дата ликвидации                                  
   var RegistrationNumber           : string   = "NULL";      // Регистрационный номер (до ОГРН/ОГРНИП)           
   var OGRNRegistrationDate         : date     = DataNullCDI; // Дата регистрации ОГРН                            
   var FLChPRegistrationNumber      : string   = "NULL";      // Регистрационный номер ФЛЧП                       
   var FLChPRegistrationDate        : date     = DataNullCDI; // Дата регистрации ФЛЧП                            
   var FLChPRegistrationStructure   : string   = "NULL";      // Регистрирующий орган ФЛЧП                        
   var NonResidentRegistrationNumber: string   = "";      // Регистрационный номер нерезидента                
   var NonResidentRegistrationDate  : date     = DataNullCDI; // Дата регистрации нерезидента                     
   var StrategicOrg                 : string   = "NULL";      // Признак стратегического общества                 
   var GozOrg                       : string   = "NULL";      // Признак клиент ГОЗ                               
   var SelfRegulating               : string   = "NULL";      // Признак СРО (саморегулируемой организации)       
   var ApkExporter                  : string   = "NULL";      // Признак экспортер АПК (агропромышленного комплекса)       
   var VedMember                    : string   = "NULL";      // Признак участник ВЭД (внешнеэкономической деятельности)   
   var Exporter                     : string   = "NULL";      // Признак экспортера                               
   var Importer                     : string   = "NULL";      // Признак импортера                                
   var Mfh                          : string   = "NULL";      // Признак МФХ (малой формы хозяйствования)         
   var CreditHistory                : string   = "NULL";      // Код субъекта кредитной истории                   
   var SavingAccount                : string   = "NULL";      // Признак наличия категории накопительный счет     
   var SourceSystemCode             : c_IntegrationDictionaryRecordXTypeNull;              // Система источник
   var SourceSystemPartyId          : c_IntegrationSymbolicIdentifierXTypeNull;            // Идентификатор субъекта в системе источнике
   var PartyType                    : c_IntegrationDictionaryRecordXTypeNull;              // Тип субъекта                                      
   var PartyStatus                  : c_IntegrationDictionaryRecordXTypeNull;              // Статус субъекта                                   
   var OrgType                      : c_IntegrationDictionaryRecordXTypeNull;              // Вид предприятия                                   
   var FinalSegment                 : c_IntegrationDictionaryRecordXTypeNull;              // Итоговый сегмент                                  
   var GksSegment                   : c_IntegrationDictionaryRecordXTypeNull;              // Сегмент по ГКС                                    
   var IndividualSegment            : c_IntegrationDictionaryRecordXTypeNull;              // Индивидуальный сегмент                            
   var ResponsibilityZone           : c_IntegrationDictionaryRecordXTypeNull;              // Зона ответственности бизнес-ССП ГО                
   var SeparatedSubjects            : c_IntegrationDictionaryRecordXTypeNull;              // Отдельные субъекты хозяйствования                 
   var AffiliationToProjectsOrg     : c_IntegrationDictionaryRecordXTypeNull;              // Принадлежность к проектным компаниям              
   var SmallMiddleOrg               : c_IntegrationDictionaryRecordXTypeNull;              // Реестр субъектов малого и среднего предпринимательства     
   var ResponsibleService           : c_IntegrationDictionaryRecordXTypeNull;              // Ответственная служба                              
   var OKFS                         : c_IntegrationDictionaryRecordXTypeNull;              // ОКФС                                              
   OKFS.RecordCode = "";
   var OKOGU                        : c_IntegrationDictionaryRecordXTypeNull;              // ОКОГУ                                             
   OKOGU.RecordCode = "";
   var OKTMO                        : c_IntegrationDictionaryRecordXTypeNull;              // ОКТМО                                             
   OKTMO.RecordCode = "";
   var OKATO                        : c_IntegrationDictionaryRecordXTypeNull;              // ОКАТО                                             
   OKATO.RecordCode = "";
   var OKOPF                        : c_IntegrationDictionaryRecordXTypeNull;              // ОКОПФ                                             
   OKOPF.RecordCode = "";
   var DiasoftCategory              : c_IntegrationDictionaryRecordXTypeNull;              // Категория Диасофт                                 
   var ResidenceCountry             : c_IntegrationDictionaryRecordXTypeNull;              // Юрисдикция                                        
   ResidenceCountry.RecordCode = "";
   var AuthorizedCapital            ;                             // Уставной капитал                                 
   var PODFTInformation             : c_PODFTInformationNull;                              // Информация ПОД/ФТ                                
   var MainSolveOption              : c_IntegrationDictionaryRecordXTypeNull;;             // Основной вариант урегулирования                   
end;

/**
 @brief Класс соответствующий типу данных Связь субъект-подразделение
 @note Заполнен специальными значениями
*/
class c_RelBranchToParty()
   var Active                       : string   = "NULL";          // Признак активности записи
   var LastUpdateDate               : datetime = DataTimeNullCDI; // Дата и время последнего изменения записи
   var SourceSystemCode             : c_IntegrationDictionaryRecordXTypeNull;   //Система источник
   var SourceSystemPartyId          : c_IntegrationSymbolicIdentifierXTypeNull; //Идентификатор субъекта в системе источнике
   var BranchCode                   : c_IntegrationDictionaryRecordXTypeNull;   //Код подразделения
   var SourceSystemId               : c_IntegrationSymbolicIdentifierXTypeNull; //Идентификатор в системе источнике
end;


/**
 @brief Класс соответствующий типу данных Связь субъект-группа
 @note Заполнен специальными значениями
*/
class c_RelGroup()
   var ValidFrom                    : datetime = DataTimeNullCDI; // Дата и время начала действия связи
   var ValidTo                      : datetime = DataTimeNullCDI;  // Дата и время окончания действия связи
   var Active                       : string   = "NULL";           // Признак активности записи
   var LastUpdateDate               : datetime = DataTimeNullCDI;  // Дата и время последнего изменения записи
   var SourceSystemCode             : c_IntegrationDictionaryRecordXTypeNull;   //Система источник
   var SourceSystemPartyId          : c_IntegrationSymbolicIdentifierXTypeNull; //Идентификатор субъекта в системе источнике
   var GroupCode                    : c_IntegrationDictionaryRecordXTypeNull;   //Группа
   var SourceSystemId               : c_IntegrationSymbolicIdentifierXTypeNull; //Идентификатор в системе источнике
end;


/**
 @brief Класс соответствующий типу данных Связь субъект-ОКВЭД
 @note Заполнен специальными значениями
*/
class c_RelOkved()
   var MainOkved                    : string   = "NULL";          // Признак основного ОКВЭД
   var ValidFrom                    : datetime = DataTimeNullCDI; // Дата и время начала действия
   var ValidTo                      : datetime = DataTimeNullCDI; // Дата и время окончания действия
   var Active                       : string   = "NULL";          // Признак активности записи
   var LastUpdateDate               : datetime = DataTimeNullCDI; // Дата и время последнего изменения записи
   var SourceSystemCode             : c_IntegrationDictionaryRecordXTypeNull;   //Система источник
   var SourceSystemPartyId          : c_IntegrationSymbolicIdentifierXTypeNull; //Идентификатор субъекта в системе источнике
   var OKVEDCode                    : c_IntegrationDictionaryRecordXTypeNull;   //ОКВЭД
   var SourceSystemId               : c_IntegrationSymbolicIdentifierXTypeNull; //Идентификатор в системе источнике
end;


/**
 @brief Класс соответствующий типу данных Связь субъект-лицензия
 @note Заполнен специальными значениями
*/
class c_RelLicense()
   var LicenseNumber                : string   = "NULL";   // Номер лицензии
   var LicenseDescription           : string   = "NULL";   // Описание лицензии
   var LicensesIssuedBy             : string   = "NULL";   // Орган, выдавший лицензию
   var LicenseStartDate             : date = DataNullCDI; // Дата выдачи лицензии
   var LicenseEndDate               : date = DataNullCDI; // Дата отзыва лицензии
   var Active                       : string   = "NULL";   // Признак активности записи
   var LastUpdateDate               : datetime = DataTimeNullCDI; // Дата и время последнего изменения записи
   var SourceSystemCode             : c_IntegrationDictionaryRecordXTypeNull;   //Система источник
   var SourceSystemPartyId          : c_IntegrationSymbolicIdentifierXTypeNull; //Идентификатор субъекта в системе источнике
   var LicenseCode                  : c_IntegrationDictionaryRecordXTypeNull;   //Вид лицензии
   var SourceSystemId               : c_IntegrationSymbolicIdentifierXTypeNull; //Идентификатор в системе источнике
end;

/**
 @brief Класс соответствующий типу данных Список связи субъектов и лицензий
 @note Заполнен специальными значениями
*/
class c_CdiCreateOrgReq_RelLicenseList()
   var RelLicense : c_RelLicense; // Связь субъект-лицензия
end;

/**
 @brief Класс соответствующий типу данных Характер отношений с Банком
 @note Заполнен специальными значениями
*/
class c_BankRel()
   var Active                       : string   = "NULL";          // Признак активности записи
   var LastUpdateDate               : datetime = DataTimeNullCDI; // Дата и время последнего изменения записи
   var SourceSystemCode             : c_IntegrationDictionaryRecordXTypeNull;   //Система источник
   var SourceSystemPartyId          : c_IntegrationSymbolicIdentifierXTypeNull; //Идентификатор субъекта в системе источнике
   var BankRelCode                  : c_IntegrationDictionaryRecordXTypeNull;   //Вид отношений субъекта с Банком
   var SourceSystemId               : c_IntegrationSymbolicIdentifierXTypeNull; //Идентификатор в системе источнике
end;

/**
 @brief Класс соответствующий типу данных Список характеры отношений с Банком
 @note Заполнен специальными значениями
*/
class c_CdiCreateOrgReq_BankRelList()
   var BankRel : c_BankRel; // Характер отношений с Банком
end;


/**
 @brief Класс соответствующий типу данных Проверки
 @note Заполнен специальными значениями
*/
class c_Verification_CDI()
   var CheckDate                    : date     = DataNullCDI;     // Дата проверки
   var CheckVal                     : string   = "NULL";          // Результат проверки
   var Active                       : string   = "NULL";          // Признак активности записи
   var LastUpdateDate               : datetime = DataTimeNullCDI; // Дата и время последнего изменения записи
   var SourceSystemCode             : c_IntegrationDictionaryRecordXTypeNull;   //Система источник
   var SourceSystemPartyId          : c_IntegrationSymbolicIdentifierXTypeNull; //Идентификатор субъекта в системе источнике
   var CheckCode                    : c_IntegrationDictionaryRecordXTypeNull;   //Вид проверки
   var SourceSystemId               : c_IntegrationSymbolicIdentifierXTypeNull; //Идентификатор в системе источнике
end;

/**
 @brief Класс соответствующий типу данных Список проверок
 @note Заполнен специальными значениями
*/
class c_CdiCreateOrgReq_VerificationList()
   var Verification : c_Verification_CDI; // Проверки
end;

/**
 @brief Класс соответствующий типу данных Состояния и стадии
 @note Заполнен специальными значениями
*/
class c_State_CDI()
   var ReorganizationDate           : date     = DataNullCDI;     // Дата реорганизации
   var StartDate                    : date     = DataNullCDI;     // Дата начала действия стадии
   var EndDate                      : date     = DataNullCDI;     // Дата окончания действия стадии
   var Active                       : string   = "NULL";          // Признак активности записи
   var LastUpdateDate               : datetime = DataTimeNullCDI; // Дата и время последнего изменения записи
   var SourceSystemCode             : c_IntegrationDictionaryRecordXTypeNull;   //Система источник
   var SourceSystemPartyId          : c_IntegrationSymbolicIdentifierXTypeNull; //Идентификатор субъекта в системе источнике
   var StateCode                    : c_IntegrationDictionaryRecordXTypeNull;   //Вид стадии или ограничения
   var SourceSystemId               : c_IntegrationSymbolicIdentifierXTypeNull; //Идентификатор в системе источнике
end;

/**
 @brief Класс соответствующий типу данных Список состояний и стадий
 @note Заполнен специальными значениями
*/
class c_CdiCreateOrgReq_StateList()
   var State : c_State_CDI; // Состояния и стадии
end;

/**
@brief Кросс-ссылка
*/
class c_ExtId_CreateOrg()
   var Active              : string;   // Признак активности записи                 
   var LastUpdateDate      : datetime; // Дата и время последнего изменения записи  
   var IsExtIdActive       : bool;     // Признак активной                          
   var StartDate           : datetime; // Дата начала действия кросс-ссылки         
   var EndDate             : datetime; // Дата окончания действия кросс-ссылки     
   var ExtIdSourceSystemName :string;  // Наименование системы источника           
   var ExtIdPartySourceId  : c_IntegrationSymbolicIdentifierXTypeNull; // Идентификатор субъекта в источнике
   var ExtIdSourceSystem   : c_IntegrationDictionaryRecordXTypeNull;   // Код системы источника
   var SourceSystemCode    : c_IntegrationDictionaryRecordXTypeNull;   // Система источник
   var SourceSystemPartyId : c_IntegrationSymbolicIdentifierXTypeNull; // Идентификатор субъекта в системе источнике
   var SourceSystemId      : c_IntegrationSymbolicIdentifierXTypeNull; // Идентификатор в системе источнике
end;


/**
 @brief Класс соответствующий типу данных Связанные лица
 @note Заполнен специальными значениями
*/
class c_RelPartyToParty_CDI()
   var ValidFrom               : datetime = DataTimeNullCDI; // Дата и время начала действия связи
   var ValidTo                 : datetime = DataTimeNullCDI; // Дата и время окончания действия связи
   var BaseDocumentNumber      : string   = "NULL";   // Номер документа основания
   var AuthorizedCapitalShare  : string   = "NULL";   // Доля в уставном капитале
   var RelatedPartyId          : c_IntegrationSymbolicIdentifierXTypeNull; // Идентификатор связанного лица в CDI
   var RelatedPartyCFTId       : c_IntegrationSymbolicIdentifierXTypeNull; // Идентификатор связанного лица в ЦФТ
   var PartyRelationType       : c_IntegrationDictionaryRecordXTypeNull;   // Вид связи
   var BaseDocumentType        : c_IntegrationDictionaryRecordXTypeNull;   // Вид документа основания
   var SourceSystemCode        : c_IntegrationDictionaryRecordXTypeNull;   // Система источник
   var SourceSystemPartyId     : c_IntegrationSymbolicIdentifierXTypeNull; // Идентификатор субъекта в системе источнике
   var SourceSystemId          : c_IntegrationSymbolicIdentifierXTypeNull; // Идентификатор в системе источнике
   var ChangedInAnotherSubject : c_IntegrationSymbolicIdentifierXTypeNull; // Идентификатор субъекта, в рамках которого 
                                                                           //  произошло обновление в системе-источнике
end;

/**
 @brief Класс соответствующий типу данных Список связанных лиц
 @note Заполнен специальными значениями
*/
class c_CdiCreateOrgReq_RelPartyToPartyList()
   var RelPartyToParty : c_RelPartyToParty_CDI; // Связанные лица
end;


/**
@brief Получить массив адресов субъекта
@param[in] partyID Идентификатор субъекта
@return Сформированный массив адресов субъекта
*/
private macro getAddress(partyID)
  var AddressList = TArray;
  var vParty = RsbParty(partyID);
  var temp_value = FormAddressObj(vParty, 1, null, "CREATE"); // юридический
  if (ValType(temp_value) == V_GENOBJ)
    AddressList.value(AddressList.size) = temp_value;
  end;
  temp_value = FormAddressObj(vParty, 2, null, "CREATE"); // фактический
  if (ValType(temp_value) == V_GENOBJ)
    AddressList.value(AddressList.size) = temp_value;
  end;
  temp_value = FormAddressObj(vParty, 3, null, "CREATE"); // почтовый
  if (ValType(temp_value) == V_GENOBJ)
    AddressList.value(AddressList.size) = temp_value;
  end;
  temp_value = FormAddressObj(vParty, 5, null, "CREATE"); // Место нахождения (по учр.документам)
  if (ValType(temp_value) == V_GENOBJ)
    AddressList.value(AddressList.size) = temp_value;
  end;
    
  if (AddressList.size == 0)
    AddressList = NULL;
  end; 
  return AddressList
end;


/**
@brief Получить массив контактов субъекта
@param[in] partyID Идентификатор субъекта
@return Сформированный массив контактов субъекта
*/
private macro getContacts(partyID)
  var ContactList = TArray;  
  var vParty = RsbParty(partyID);
  var PartyContact = vParty.PartyContact();
  if(PartyContact.First())
    var temp_value = CDI_FillContact( PartyContact, null, partyID, "CREATE" );
    if (ValType(temp_value) == V_GENOBJ)
      ContactList.value(ContactList.size) = temp_value;
    end;
    while(PartyContact.Next())
      temp_value = CDI_FillContact( PartyContact, null, partyID, "CREATE" );
      if (ValType(temp_value) == V_GENOBJ)
        ContactList.value(ContactList.size) = temp_value;
      end;
    end;
  end;

  if (ContactList.size == 0)
    ContactList = NULL;
  end; 
  return ContactList
end;

/**
@brief Получить массив ОКВЭД субъекта
@param[in] partyID Идентификатор субъекта
@return Сформированный массив ОКВЭД субъекта
*/
private macro getRelOkveds(partyID)
  var temp_value;
  var RelOkvedList = TArray;
  var sql_str = "select c.t_groupid, c.t_attrid, c.t_general, a.t_numinlist, a.t_nameobject, a.t_name, a.t_fullname, c.t_validfromdate, c.t_id "+
            "  from dobjatcor_dbt c "+
            "  inner join DOBJATTR_DBT a "+
            "     on a.t_groupid = c.t_groupid and  a.t_attrid = c.t_attrid  and  a.t_objecttype = c.t_objecttype "+
            "  where lpad(:partyid, 10, '0')=c.t_object and a.t_objecttype = 3 "+
            "    and c.t_validtodate = to_date('31129999','ddmmyyyy') and c.t_groupid = 64 ";
  var cmd = RSDCommand(sql_str);
  cmd.AddParam("partyid", RSDBP_IN, partyID);
  var rs = RSDRecordSet(cmd);
  while (rs.movenext)
    temp_value = c_RelOkved;
    if (rs.value("t_general") == "X")
       temp_value.MainOkved = "YES";
    else 
       temp_value.MainOkved = "NO";
    end;
    temp_value.ValidFrom = dttm(rs.value("t_validfromdate"));
    temp_value.Active        = "YES";
    temp_value.LastUpdateDate  = dttm(date(),time());
    temp_value.SourceSystemCode = c_IntegrationDictionaryRecordXTypeNull;
    temp_value.SourceSystemCode.RecordCode = NAME_SYSTEM;
    temp_value.SourceSystemId = c_IntegrationSymbolicIdentifierXTypeNull;
    temp_value.SourceSystemId.Objectid = rs.value("t_id");
    temp_value.OKVEDCode = c_IntegrationDictionaryRecordXTypeNull;
    temp_value.OKVEDCode.RecordCode = rs.value("t_numinlist");
    temp_value.OKVEDCode.RecordTitle = rs.value("t_fullname");
    temp_value.SourceSystemPartyId = c_IntegrationSymbolicIdentifierXTypeNull;
    temp_value.SourceSystemPartyId.Objectid = partyID;

    RelOkvedList.value(RelOkvedList.size) = temp_value;
    temp_value = NULL;
  end;

  if (RelOkvedList.size == 0)
    RelOkvedList = NULL;
  end;
  return RelOkvedList
end;

/**
 @brief Класс соответствующий типу данных CdiCreateOrgReqType
 
 Класс необходим для создания объекта xml CdiCreateOrgReq
*/
class c_CdiCreateOrgReqType (PartyData)
  var  Party                : c_CdiCreateOrgReq_PartyType; // Субъект
  var  RelBranchToPartyList ; // Список связей субъект-подразделение
  var  RelGroupList         ; // Список связи субъектов и группы компаний
  var  RelOkvedList         ; // Список связи субъектов и ОКВЭД
  var  RelLicenseList       ; //Список связи субъектов и лицензий
  var  BankRelList          ; // Список характеры отношений с Банком
  var  ContactList          ; // Список контактов
  var  AddressList          ; // Список адресов
  var  VerificationList     ; //Список проверок
  var  StateList            ; //Список состояний и стадий
  var  ExtIdList; //Список кросс-ссылок
  var  RelPartyToPartyList  ; //Список связанных лиц

  if ((PartyData != NULL) and (ValType(PartyData) != V_UNDEF))
    Party = PartyData;
    AddressList = getAddress(PartyData.SourceSystemPartyId.ObjectId);
    ContactList = getContacts(PartyData.SourceSystemPartyId.ObjectId);
    RelOkvedList = getRelOkveds(PartyData.SourceSystemPartyId.ObjectId);
  else
    Party = c_CdiCreateOrgReq_PartyType();
  end;

end;

/**
 @brief Класс хранения объекта CdiCreateOrgReq
 
 Класс необходим для создания объекта xml CdiCreateOrgReq
*/
private class c_CdiCreateOrgReqProcessor(PartyData)
   var CdiCreateOrgReq;
   CdiCreateOrgReq = c_CdiCreateOrgReqType(PartyData);
end; 

/**
 @brief Поиск данных в SOFR
 @param[in] PartyId Идентификатор субъекта в SOFR
 @return заполненный  класс c_CdiCreateOrgReq_PartyType данными из SOFR

 Процедура осуществляет поиск данных в SOFR 
 и заполняет ими класс c_CdiCreateOrgReq_PartyType
*/
private macro FindDataParty (PartyId)
  var Query, cmd, data;
  var Party = c_CdiCreateOrgReq_PartyType();
  var PartyName = "";
  record fininstr(fininstr);
  var sql_str = "";
  var rs;

  var vParty = RsbParty(PartyId);
  
  Party.FullNameOrg = vParty.FullName;
  Party.ShortNameOrg = vParty.ShortName;
  if (vParty.IsNotResident == "X")
    Party.ResidentTax = "NO";
    Party.ResidenceCountry.RecordCode = vParty.NRCountry;
  else
    Party.ResidentTax = "YES";
    Party.ResidenceCountry.RecordCode = "RUS";
  end;

  Query = "select t_codenum3 as COUNTRY from dcountry_dbt where t_CodeLat3 = '"+Party.ResidenceCountry.RecordCode+"'";

  cmd = rsdCommand( Query);

  data = TRsbDataSet(cmd);

  // не буквенный, а числовой
  if (data.moveNext())
    Party.ResidenceCountry.RecordCode = data.COUNTRY;
  else 
    return NULL;
  end;

  PartyName = GetPartyName(PartyId, PTKN_LATSHORTNAME);
  if (PartyName != "-1");
    Party.ShortNameOrgLat = PartyName;
  end;
  PartyName = GetPartyName(PartyId, PTKN_LATFULLNAME);
  if (PartyName == "-1");
    PartyName = GetPartyName(PartyId, PTKN_FOREIGNNAME);
  end;
  if (PartyName != "-1");
    Party.FullNameOrgLat = PartyName;
  end;

  Party.SourceSystemCode.RecordCode = NAME_SYSTEM;
  Party.LastUpdateDate = dttm(date(), time());
  Party.SourceSystemPartyId.ObjectId = PartyId;
  Party.OKPO = vParty.OKPO;

  if ((vParty.LegalForm == 1) and (vParty.RealCapital > 0))
     Party.AuthorizedCapital = c_AuthorizedCapitalNull;
     Party.AuthorizedCapital.Currency.RecordCode = "";
     Party.AuthorizedCapital.Value = vParty.RealCapital;
     if (ПолучитьФинИн(vParty.CapitalFI, fininstr) == 0)
        Party.AuthorizedCapital.Currency.RecordCode = fininstr.iso_number;
     end;
  end;

  // Тип субъекта
  cmd = rsdCommand( " SELECT CASE WHEN t_legalform = 1 " +
                    "             THEN 'C' " +
                    "             WHEN (SELECT pers.t_isemployer " +
                    "                     FROM dpersn_dbt pers " +
                    "                    WHERE pers.t_personid = prt.t_partyid) = 'X' then 'IE' " +
                    "             ELSE NVL ( " +
                    "                    (SELECT CASE WHEN    a.t_nameobject LIKE '5 01%' " +
                    "                                 THEN 'IE' " +
                    "                                 WHEN a.t_nameobject LIKE '5 02%' " +
                    "                                 THEN 'PP' " +
                    "                            END " +
                    "                       FROM dobjatcor_dbt  c " +
                    "                            INNER JOIN DOBJATTR_DBT a " +
                    "                                ON     a.t_groupid = c.t_groupid " +
                    "                                   AND a.t_attrid = c.t_attrid " +
                    "                                   AND a.t_objecttype = c.t_objecttype " +
                    "                      WHERE     LPAD (prt.T_PARTYID, 10, '0') = c.t_object " +
                    "                            AND a.t_objecttype = 3 " +
                    "                            AND a.t_groupid = 53 " +
                    "                            AND a.t_nameobject LIKE '5%'), 'P') " +
                    "        END as Type" +
                    "   FROM dparty_dbt prt " +
                    "  WHERE prt.t_PartyID = :p_partyid "
                   );

  cmd.AddParam("p_partyid", RSDBP_IN, PartyId);

  data = TRsbDataSet(cmd);

  if (data.moveNext())
    Party.PartyType.RecordCode = data.Type;
  else
    Party.PartyType.RecordCode = "";
  end;

  sql_str = "select c.t_groupid, c.t_attrid, c.t_general, a.t_numinlist, a.t_nameobject, a.t_name, a.t_fullname, c.t_validfromdate, c.t_id "+
            "  from dobjatcor_dbt c "+
            "  inner join DOBJATTR_DBT a "+
            "     on a.t_groupid = c.t_groupid and  a.t_attrid = c.t_attrid  and  a.t_objecttype = c.t_objecttype "+
            "  where lpad(:partyid, 10, '0')=c.t_object and a.t_objecttype = 3 "+
            "    and c.t_validtodate = to_date('31129999','ddmmyyyy')";
  cmd = RSDCommand(sql_str);
  cmd.AddParam("partyid", RSDBP_IN, PartyId);
  rs = RSDRecordSet(cmd);
  while (rs.movenext)
     if (rs.value("t_groupid") == 27)
        Party.OKFS.RecordCode = rs.value("t_numinlist");
     elif (rs.value("t_groupid") == 48)
        Party.OKOGU.RecordCode = rs.value("t_numinlist");
     elif (rs.value("t_groupid") == 55)
        Party.OKTMO.RecordCode = rs.value("t_numinlist");
     elif (rs.value("t_groupid") == 12)
        Party.OKATO.RecordCode = rs.value("t_numinlist");
     elif (rs.value("t_groupid") == 53)
        Party.OKOPF.RecordCode = StrSubst(rs.value("t_nameobject")," ","");
     end;
  end;

  // Статус субъекта
  cmd = rsdCommand( " SELECT CASE WHEN COUNT (DECODE (t_partykind, 1, t_partykind, NULL)) > 0 "
                    "             THEN 'Client' "
                    "             ELSE 'Lead' "
                    "        END as Status "
                    "   FROM DPARTYOWN_DBT po "
                    "  WHERE po.t_partyid = :p_partyid "
                   );

  cmd.AddParam("p_partyid", RSDBP_IN, PartyId);

  data = TRsbDataSet(cmd);

  if (data.moveNext())
    Party.PartyStatus.RecordCode = data.Status;
  end;

  sql_str = "select t_codekind, t_code, t_name, t_begindate from dRepPartyCodes_vw where t_partyid = :partyid ";
  cmd = RSDCommand(sql_str);
  cmd.AddParam("partyid", RSDBP_IN, PartyId);
  rs = RSDRecordSet(cmd);
  while (rs.movenext)
     if (rs.value("t_codekind") == PTCK_INN)
        var INN = rs.value("t_code");
        SplitFullINN (INN, Party.INN, Party.KPPRegistrationPlace); 
     /*elif (rs.value("t_codekind") == PTCK_FOREIGN_INN)
        Party.KIO = rs.value("t_code");*/
     elif (rs.value("t_codekind") == 27)
        Party.OGRN = rs.value("t_code");
        //Party.OGRNRegistrationDate = rs.value("t_begindate");
     elif (rs.value("t_codekind") == 33)
        Party.NonResidentRegistrationNumber = rs.value("t_code");
     end;
  end;


  return Party;
end;

/**
 @brief Получение идентификатора субъекта в CDI из ответа 
 @param[in] RespData Объект данных из CDI
 @param[out] ErrorList Массив ошибок CDI 
 @return Идентификатор субъекта в CDI
 @return При ошибке 0
*/
private macro parseRespDate (RespData, ErrorList)
  var cdiID = 0;
  var err_txt = "";
  var ErrorData;
  
  if(ValType(RespData) != V_UNDEF)
      var RecordList = uTryToGetPropS(RespData,"RecordList");
      var ErrorListResp = uTryToGetPropS(RespData,"ErrorList");

      if(ValType(RecordList) == V_GENOBJ)
        var ir = 0;
        while(ir < RecordList.size)
          var Entity = getValueFromProperty(RecordList(ir),"Entity");
          if (Entity == "party_org")
            var SourceId = uTryToGetPropS(RecordList(ir),"Id");
            if(ValType(SourceId) == V_GENOBJ)
              cdiID = getValueFromProperty(SourceId,"ObjectId");
            end;
          end;
          ir = ir + 1;
        end;
      else
        err_txt = string(InErrNotObj,"Ошибка парсинга объекта 'CdiCreateOrg'");
        RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
      end;

      if( (ValType(ErrorListResp) == V_GENOBJ) and 
          (ValType(ErrorList) == V_GENOBJ) )
        var i = 0;
        while(ErrorListResp.size > i)
          ErrorData = c_ErrorDataCDI();
          ErrorData.ErrorCode = getValueFromProperty(ErrorListResp(i),"ErrorCode");
          ErrorData.ErrorDesc = getValueFromProperty(ErrorListResp(i),"ErrorDesc");
            
          ErrorList(i) = ErrorData;
          i = i + 1;
          ErrorData = NULL;
        end;
      end;
  else
      err_txt = string(InErrNotObj,"Получен некорректный объект");
      RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
  end;

  return cdiID;
end;

/**
 @brief Выполнение запроса на создание субъекта в CDI
 @param[in] DataPartySOFR Объект для создания данных в CDI c_CdiCreateOrgReq_PartyType
 @param[out] ErrorList Массив ошибок CDI 
 @param[in] in_recid utableprocessevent.t_recid для связки в "Интерфейс таблицы событий"
 @return Идентификатор субъекта в CDI

 Выполнение запроса на создание субъекта в CDI по буферу DataPartySOFR
*/
private macro makeCdiCreateOrgReq(DataPartySOFR, ErrorList, in_recid)
   const QUEUE_OUT = 2;
   const QUEUE_CdiCreateOrgReq = GetNumDataType("CdiCreateOrg");

   var cdiID = 0;
   var err_txt  = NULL;

   private var v_out_obj;
   private var RequestText;
   private var v_transformator;
   private var v_answer;
   private var v_answer_data;
   private var v_logger_obj;
   private var v_log_id;
   private var xml_str;
   private var v_xml_rpc_reqid = XmlRpcRequestId(); 

   v_transformator = c_secur_msg_converter();

   v_out_obj = c_CdiCreateOrgReqProcessor(DataPartySOFR);
   RequestText = v_transformator.m_secur_internal_resp(v_out_obj);

   v_logger_obj = uws_exchng_logger(null, null, v_xml_rpc_reqid, "CdiCreateOrgReq", modulename(), in_recid, RequestText);
   v_log_id = v_logger_obj.get_log_id();

   err_txt = v_transformator.m_validate_out_xml(RequestText);
   if (err_txt == NULL)
        v_answer  =  SubmitRequestToWS( QUEUE_OUT,  // ИД очереди.
                                       "",          // ИД сообщения.
                                       "",          // Идентификатор Бэк Офиса. (не обрабатывается).
                                       true,        // Признак синхронной обработки. Если не указан, = False
                                       "SOFR",      // Подразделение системы-отпр
                                       QUEUE_CdiCreateOrgReq,          // ИД типа данных  
                                       RequestText, // Бизнес данные в виде XML
                                       v_log_id,    // ID usr_exchng_log
                                       "CDI",
                                       30
                                       );

        if (v_answer.ResultCode == 0)
          xml_str = v_answer.responsetext;
          v_logger_obj.add_response(xml_str); 

          v_transformator = NULL;
          if (xml_str)
               /* Преобразуем запрос в объектный вид - begin */
            v_transformator = c_secur_msg_converter();
            if(not v_transformator.m_decode_escaped_char(xml_str))
              RunError(v_transformator.get_service_msg(), c_usr_err_obj(v_transformator.get_service_msg()));
            end;
            v_answer_data = v_transformator.m_secur_wol_internal_req(xml_str);
            /* Преобразуем запрос в объектный вид - end */

            if(ValType(v_answer_data) == V_UNDEF)
              err_txt = "Не удалось преобразовать XML в объект";
              RunError(err_txt, c_usr_err_obj(err_txt)); 
            end;
               
            cdiID = parseRespDate(v_answer_data,ErrorList); 
          end;  
        else 
          v_logger_obj.add_error_info(v_answer.ResultCode, v_answer.ResultText);
        end;

   else
      v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, err_txt);
   end;

   return cdiID;

onError(err)
   return 0;

end;

/**
 @brief Выполнение обработки сервиса CdiCreateOrg
 @param[in] PartyObjId Идентификатор субъекта в SOFR
 @param[in] in_recid utableprocessevent.t_recid для связки в "Интерфейс таблицы событий"
 @return В случаии успешного карточки клиента возвращает ID субъекта в CDI, иначе NULL

 Создание карточек клиентов ЮЛ в CDI по данным из SOFR
*/
macro run_CdiCreateOrgReq (PartyObjId, in_recid)
  var DataPartySOFR;
  var ErrorList = TArray;
  var cdi_result  = c_ErrorDataCDI_ID();
  cdi_result.cdiID = 0;
  cdi_result.ErrorCode = "-1";
  cdi_result.ErrorDesc = "";

  var tryNum = 0;
  var error  = 0;
  var i = 0;

  //CCBO-10808 не все субъекты выгружаются
  if (CheckBlockUnloadCDI(PartyObjId)) 
     cdi_result.ErrorCode = ERROR_CODE_BLOCK_UNLOAD;
     cdi_result.ErrorDesc = ERROR_DESC_BLOCK_UNLOAD;
     return cdi_result;
  end;

  DataPartySOFR = FindDataParty(PartyObjId);

  GetRegistryValue( "РСХБ\\ИНТЕГРАЦИЯ\\ВЗАИМОДЕЙСТВИЕ С AC CDI\\КОЛИЧЕСТВО ПОПЫТОК", V_INTEGER, tryNum, error );

  if (error !=0 ) tryNum = 1; end;//если не смогли получить считаем, что попытка 1

  while ( (tryNum > i) and (cdi_result.cdiID == 0) ) //выполняем пока не получим положительного ответа или не закончатся попытки.
    cdi_result.cdiID = makeCdiCreateOrgReq(DataPartySOFR, ErrorList, in_recid);
    if (cdi_result.cdiID > 0)
      cdi_result.ErrorCode = "0";
      cdi_result.ErrorDesc = "";
      return cdi_result;
    end;
    i = i + 1;
  end;
  
  return cdi_result;
end;

/**
 @brief Точка входа обработки сервиса CdiCreateOrg
 @param[in] PartyObjId Идентификатор субъекта в SOFR
 @param[in] in_recid utableprocessevent.t_recid для связки в "Интерфейс таблицы событий"
 @return В случаии успешного карточки клиента возвращает ID субъекта в CDI, иначе NULL
*/
macro CdiCreateOrg (PartyObjId, in_recid) 
   return run_CdiCreateOrgReq(PartyObjId, in_recid); 
end;