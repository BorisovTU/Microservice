/**
  @file dbo_message_close.mac 
  @brief Макрос, запускаемый из скроллинга ДБО(dlcontrsc.mac), при закрытие договора БО.

 #tag                                                                                                          
 - functional_block:Уведомление клиентам                                                                             
 - code_type:GUI 
 - code_type:API                                                                                                                                                                                               
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.01.15 |Шишкин Е.В.    |BOSS-1802           |Автоматическое уведомление клиентов о закрытии договора БО/ИИС                                                                                                                                                 

*/

Import rsd, RsbDataSet;
import "u_common_email_utils.mac";
import "dlQuery.mac";
import "dlwreps.mac";

CONST CONTR_MESS_DELETE = 508;
CONST CONTR_MESS_NOTIFICATION = 5020;
CONST CONTR_MESS_NOTIFICATION_IIS = 5021;

/**
 @brief Запись ошибок в лог
 @param[Contr_number] Номер договора БО
 @param[errordesc] Описание ошибки
*/                    
private macro AddLog_it(p_text)
   var cmd;
   cmd = RSDCommand("begin it_log.log_handle('dbo_message_close',:p_text,it_log.C_MSG_TYPE__DEBUG); end;");
   cmd.AddParam("p_text", RSDBP_IN, p_text);
   cmd.execute;
   cmd.close;
end;

/**
 @brief пользовательская обработка NULL
 @param[_value] обрабатываемое значение
*/                    
private macro usr_nvlx(_value)
   var res;
   if ( (ValType(_value) == V_UNDEF) or (ValType(_value) == 26) or (_value == NULL) or (_value == StrFor(1)) or (_value == StrFor(0)) )
      res = 0;
   else
      res = _value;
   end;
   return res;
end;

/**
 @brief Класс формирования уведомления клиенту о закрытие договора БО.
 @param[in] _dlcontrid id договора БО
*/                    
class dboMessageClose(_dlcontrid)
   const wdFormatPDF = 17;

   private var oWrd, oDoc;

   var ddl_id = _dlcontrid; 
   var Contr_number, 
       Party_name, 
       DateBegin, 
       DateEnd, 
       attrid,
       is_IIS, 
       Bank_init, 
       Client_init, 
       Close_contract_sheduler, 
       email,
       TextBody,  
       SubjectMail;   

   private var PATH_OUT,bcc,notification;
   private var FileDirForm;
   var Mess = "";

   var TemplName_DBO_Bank = "";
   var TemplName_DBO_Client = "";
   var TemplName_IIS_Client = "";
   var TemplName_IIS_Bank = "";

   var Templ_Dir = "";

   var statMes = "";
                      
   var FormFilename = "";
   var w_ext = ".docx";

   var NumberSend;

   /**
    @brief Сбор данных по договору
   */                    
   macro constructor()           
      var sqls, rsdc, rsds, StrErr;
      statMes = "";

      sqls = String( 
                     "SELECT sf.t_number,                                                             \n"
                     ,"       sf.t_datebegin,                                                         \n"
                     ,"       sf.t_dateclose,                                                         \n"
                     ,"       decode(dd.t_iis,chr(88),1,2) as iis,                                    \n"
                     ,"       party.t_name,                                                           \n"
                     ,"       dd.t_Email,                                                             \n"
                     ,"       (SELECT t_attrid                                                        \n"
                     ,"                FROM dobjatcor_dbt obj                                         \n"
                     ,"               WHERE     t_objecttype = 207                                    \n"
                     ,"                     AND t_groupid = 198                                       \n"
                     ,"                     AND t_object = LPAD (dd.t_dlcontrid, 34, '0')             \n"
                     ,"                     AND t_general = 'X'                                       \n"
                     ,"                     AND t_validtodate = TO_DATE ('31129999', 'ddmmyyyy'))     \n"
                     ,"                AS attrid                                                      \n"
                     ,"  FROM ddlcontr_dbt dd, dsfcontr_dbt sf, dparty_dbt party                      \n"
                     ," WHERE     dd.t_dlcontrid = :ddl_id                                            \n"
                     ,"       AND dd.t_sfcontrid = sf.t_id                                            \n"
                     ,"       AND sf.t_partyid = party.t_partyid                                      \n"
                                        );
      rsdc = rsdcommand(sqls);
      rsdc.AddParam("ddl_id", RSDBP_IN, ddl_id);                                            
      rsds = rsdrecordset(rsdc, RSDVAL_CLIENT_IF_NEEDED, RSDVAL_FORWARD_ONLY);
      if (rsds.moveNext())
          Contr_number = rsds.value("t_number");
          Party_Name   = rsds.value("t_name");
          DateBegin    = date(rsds.value("t_datebegin"));
          DateEnd      = date(rsds.value("t_dateclose"));
          is_IIS       = rsds.value("iis");
          email        = rsds.value("t_email");
          attrid       = usr_nvlx(rsds.value("attrid"));
          if(attrid == 1)
             Bank_init = true;
             Client_init = false;
          else
             Bank_init = false;
             Client_init = true;
          end;
      else
          runerror("Ошибка получения данных по договору","Ошибка получения данных по договору "+ddl_id);
      end;        

      PATH_OUT = "";
      
      GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\OUT\\ГО", V_STRING, PATH_OUT, StrErr);
      GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEMPLSDIR", V_STRING, Templ_Dir, StrErr);
      FileDirForm = "..\\txtFile\\";

      FormFilename = "Уведомление о расторжении соглашения "+ Contr_number;
      FormFilename = strSubst(FormFilename,"\"","");
      FormFilename = strSubst(FormFilename,"\\","-");
      FormFilename = strSubst(FormFilename,"/","-");
      FormFilename    = FormFilename + w_ext;

      rsdc = null;rsds=null;
   onerror(e);
      rsdc = null;rsds=null;       
      if ((e.Err != null) or (e.Err != nullval))
         statMes = e.Err;
      else 
         statMes = e.Message;
      end;
   end;

   /**
    @brief функция заполнения шаблона word
   */                    
   macro fill_bmk(p_Templ)
      var txtfile = "..\\txtfile\\bmk_dbo_mess_file.txt";
      
      SetOutPut(txtfile, false);
      println(p_Templ); //Имя файла-шаблона
      println( substr(MESS, 1, index(MESS, ".docx") - 1) );

      [~Дата_открытия];
      println(DateBegin);

      [~Дата_открытия2];
      println(DateBegin);

      [~Номер_соглашения];
      println(Contr_Number);

      [~Номер_соглашения2];
      println(Contr_Number);

      [~Дата_закрытия];
      println(DateEnd);

      txtfile = SetOutput(null, false);

      return DLWREPS_PrintReportFromTagFile(txtfile, @MESS, null, null, null, null, null, false);
   end;

   /**
    @brief формирование текста уведомления
    @param[in] p_Templ путь к шаблону
    в случае не успешного формирования взводится ошибка
   */                    
   macro Head_Form(p_Templ)
   var count = 0, str = "", i = 0;
   var tpl_nam = "";

   tpl_nam = FindPath(toANSI(p_Templ), Templ_Dir);
   if (strlen(trim(tpl_nam)) == 0)
      runerror("Ошибка поиска шаблона", "Ошибка поиска шаблона " + p_Templ);
   end;  
   
   if (NOT fill_bmk(p_Templ))
      str = "Error";
   end;

   return "";       
   onerror(e)
      if (e.Err)
         if (IsEqClass ("TRsComErr", e.err))
            count = e.err.Count;
            i = 0;
            while (i < count)
               str = string(str, e.err.Message(i), " (Code = ",e.err.Code(i), ", Level = ",e.err.Level(i), ")", strfor(10));
               i = i + 1
            end;
            str = str + " " + e.Module + " " + e.Line + " " + e.Message;
         elif (valtype(e.err) == v_string)
            str = e.err;
         else
            str = e.Module + " " + e.Line + " " + e.Message;
         end;  
      else
         str = e.Module + " " + e.Line + " " + e.Message;
      end;    
      return str;
   end;

   /**
    @brief Выбор шаблона, в зависимости от договора и инициатора закрытия
   */                    
   macro CreateNotification()
      FileDirForm = "..\\txtFile\\";

      Mess = string(/*FileDirForm,*/ FormFilename);
      if(Close_contract_sheduler)
          return Head_Form(TemplName_IIS_Bank);
      elif((is_IIS == 2) and (Bank_init == true) and (Client_init == false))
          return Head_Form(TemplName_DBO_Bank);
      elif((is_IIS == 2) and (Bank_init == false) and (Client_init == true))
          return Head_Form(TemplName_DBO_Client);
      elif((is_IIS == 1) and (Bank_init == false) and (Client_init == true))
          return Head_Form(TemplName_IIS_Client);
      elif((is_IIS == 1) and (Bank_init == true) and (Client_init == false))
          return Head_Form(TemplName_IIS_Bank);
      end;
      statMes = "Не удалось подобрать шаблон уведомления";
      return "";       
   end;

   /**
    @brief Отправка уведомления
   */                    
   macro SendNotification(Close_contract_sheduler) 
 
      //получить полный путь
      private macro GetFullPath(Path)
         var fullpath;
         if(substr(Path, 1, 2) == "..") /* ссылка на вышестоящий каталог */
            Path = substr(Path, 3);
         end;
         if(substr(Path, 1, 1) == "\\") /* ссылка на вышестоящий каталог */
            Path = substr(Path, 2);
         end;
         fullpath = strsubst(GetCurDir(false), "\\Obj","") + "\\" + Path ; 

         return fullpath;
      end;

      var emailText, Subject, v_email_processor;
  
      if(Close_contract_sheduler)

        if(SubjectMail == "")
           SUBJECT = "О расторжении договора ИИС "+Contr_number+" "+Party_name+" в связи с непредоставлением документов.";
        else
           Subject = SubjectMail;
        end;
      
        if(TextBody == "")
           EMAILTEXT = string(" Уважаемый Клиент! ",
                              "\n",
                              "\n  Информируем, что Ваш договор ИИС "+Contr_number+" от "+DateBegin+", заключенный в АО \x22Россельхозбанк\x22,", 
                              "\n  расторгнут в связи с непредоставлением подтверждающих документов о расторжении договора ИИС,", 
                              "\n  заключенного с другим профессиональным участником в соответствии с данными, указанными при заключении договора ИИС в АО \x22Россельхозбанк\x22.", 
                              "\n",
                              "\n",
        			"\n						С уважением,           ",
           			"\n						АО \x22Россельхозбанк\x22.   \n\n");
        else
           emailtext = TextBody;
        end;
      else
      if(SubjectMail == "")
         Subject = "Уведомление о расторжении Соглашения!!! "+Contr_number+" "+Party_name;
      else
         Subject = SubjectMail;
      end;

      if(TextBody == "")
         emailText = "Уважаемый клиент!\n" +
                     "\n" +
                     "В соответствии с условиями Регламента оказания брокерских услуг АО \x22Россельхозбанк\x22 направляем Вам Уведомление о расторжении Соглашения. \n" +
                     "\n" +
                     "При возникновении вопросов Вы можете связаться с клиентским менеджером по бесплатной линии 8-800-100-40-40.\n" +
                     "\n" +
                     "\n" +
                     "С уважением,\n" +
                     "\nАО \x22Россельхозбанк\x22"; 
      else
         emailtext = TextBody;
      end;
      end;

      //создадим сообщение на договоре и отправим клиенту
      var dlcontrmsg = Tbfile("dlcontrmsg.dbt", "W", 0);
      var contrmsg;  
   
      if(Close_contract_sheduler)
         dlcontrmsg.rec.DlContrID = ddl_id;
         dlcontrmsg.rec.Kind = CONTR_MESS_DELETE;
         dlcontrmsg.rec.CreateDate = {curdate};
         dlcontrmsg.rec.CreateTime = Time();
         dlcontrmsg.rec.MarketID = -1;
         if(dlcontrmsg.insert())                                                                          
            contrmsg = true;
         end;
      else
         //создадим сообщение на договоре об отправке клиенту уведомления. если договор закрыт из интерфейса
         if(is_IIS == 1)
            dlcontrmsg.rec.DlContrID = ddl_id;
            dlcontrmsg.rec.Kind = CONTR_MESS_NOTIFICATION_IIS;
            dlcontrmsg.rec.CreateDate = {curdate};
            dlcontrmsg.rec.CreateTime = Time();
            dlcontrmsg.rec.MarketID = -1;
            if(dlcontrmsg.insert())                                                                          
               contrmsg = true;
            end;
         else
            dlcontrmsg.rec.DlContrID = ddl_id;
            dlcontrmsg.rec.Kind = CONTR_MESS_NOTIFICATION;
            dlcontrmsg.rec.CreateDate = {curdate};
            dlcontrmsg.rec.CreateTime = Time();
            dlcontrmsg.rec.MarketID = -1;
            if(dlcontrmsg.insert())                                                                          
               contrmsg = true;
            end;
         end;
      end;

      GetRegistryValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\УВЕДОМЛЕНИЯ КЛИЕНТУ\\ДОБ.ОД В СК.КОПИЮ ПРИ ЗАКР.ДБО", V_INTEGER, notification);
      GetRegistryValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\УВЕДОМЛЕНИЯ КЛИЕНТУ\\АДРЕС ОТП.В СК ОД О ЗАК.ДБО", V_STRING, bcc);

      if(contrmsg)
         if((ValType(email) == V_UNDEF) or (ValType(email) == 26) or (trim(email) == StrFor(1)) or (trim(email) == "") or (trim(email) == "-"))
            statMes = "Сообщение сформировано, но не отправлено. У клиента отсутствует электронная почта для рассылки";
         else
            v_email_processor = c_email_proc_env();
            v_email_processor.m_add_email_to_list(email);
            if(notification == 1)
               v_email_processor.m_add_email_to_bcc_list(bcc);
            end;
            v_email_processor.m_set_msg_head(Subject);
            v_email_processor.m_add_attach_to_list(Mess);
            v_email_processor.m_add_row_to_msg_text(emailText);
            v_email_processor.m_save_to_submit_synch();
            v_email_processor.m_submit_email_synch();
         
            if(v_email_processor.m_get_error_status())
               statMes = "Сообщение сформировано, но не отправлено: " + v_email_processor.get_service_msg();
            else
               //время и дата отправки клиенту уведомления
               var upd = DL_RSDCommand("UPDATE DDLCONTRMSG_DBT SET T_SENDDATE = ?, T_SENDTIME = ? WHERE T_ID = ?");
               upd.AddParam({curdate});
               upd.AddParam(Time());
               upd.AddParam(dlcontrmsg.rec.ID);
               upd.ExecuteCMD();
            end;
         end;
      
         //сохранить сообщение в образах
         if(not LoadImageObj(Mess, OBJTYPE_BSCMSG_DL, UniID(dlcontrmsg, OBJTYPE_BSCMSG_DL), 1))
            statMes = "К записи о сообщении не удалось прикрепить файл сообщения";
         end;
      end;
   end;

   /**
    @brief Создание уведомления
   */                    
   macro CreateMessage(CloseAnotherContract, TextEmail, SubjectText)
      var FileFrom, FileFromDOC; 
      Close_contract_sheduler = CloseAnotherContract;

      TextBody = TextEmail;  
      SubjectMail = SubjectText;

      macro remove_tmp()
         RemoveFile(FileFromDOC);
      end;
   
      TemplName_DBO_Bank   = "Уведомление о закрытии ДБО_Банк" + w_ext;
      TemplName_DBO_Client = "Уведомление о закрытии ДБО_Клиент" + w_ext;
      TemplName_IIS_Client = "Уведомление о закрытии ИИС_Клиент" + w_ext;
      TemplName_IIS_Bank   = "Уведомление о закрытии ИИС_Банк" + w_ext;

      statMes = CreateNotification();

      if(strlen(trim(statMes)) > 0)
         runerror("Ошибка формирования уведомления", "Ошибка формирования уведомления " + FormFilename + " " + statMes);
      end;

      //локальный путь для трехзвенки
      FileFromDOC = Mess;
      
      FileFrom = FileFromDOC;
      Mess = string(PATH_OUT, FormFilename);
      
      //перенос файла
      if(not CopyFile(FileFrom, Mess))
         runerror("Ошибка формирования уведомления", "Не удалось скопировать файл " + FormFilename + " в директорию " + string(PATH_OUT, FormFilename));
      end;            

      //удаление временных
      remove_tmp();

      SendNotification(Close_contract_sheduler);

      if(strlen(trim(statMes)) > 0)
         runerror("Ошибка формирования уведомления", "Ошибка формирования уведомления " + FormFilename + " " + statMes);
      end;
 
      return 0;

   onerror(e)   

      remove_tmp();
      //все равно возвращаем ноль, так как процесс закрытия не прерывается, но записываем в лог ошибку
      AddLog_it("Непредвиденная ошибка: договор "+Contr_number+": "+e.Err+", "+e.Message);

      return 0;
   end;

   constructor();
end;

          