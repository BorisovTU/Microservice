/*---------------------------------------------------------------------------------------------*/
/*  ManualPushDepo.mac  - Интерфейс для повторной выгрузки договор в Депозитарий, 
                           отбираются незавершенные события 8207 из таблицы событий:
                           статус не равен 1 и 4                                               */
/* BIQ-7382                                                                                    */
/* Автор: Гераськина Т.                                                                        */
/*---------------------------------------------------------------------------------------------*/
import RsbFormsInter;
import "usr_key_const.mac";
import globals, rsd;
import "global_utils_intgr.mac";
import "int_usr_globals.mac";
import "func_lib.mac";

private var gv_evnt_sel_prm;               /* Парамеытры выборки событий */
private var gv_com_unload_form;
private var gv_result_prm;
private var gv_com_result_form;


private macro FillTempTable(_objecttype)
   var sql_str, rs, cmd;
   var vbegdate, venddate;

   if (gv_evnt_sel_prm.v_com_date_switch)
      vbegdate = gv_evnt_sel_prm.v_com_beg_date;
      venddate = gv_evnt_sel_prm.v_com_end_date+1;
   else
      vbegdate = gv_evnt_sel_prm.v_com_date;
      venddate = gv_evnt_sel_prm.v_com_date+1;
   end;

   sql_str = " declare "+
             "      vguid varchar2(200 char); "+
             "      rec_u_Selection_Process U_SELECTION_OBJ_PROCESS%rowtype; "+
             "      vcnt number := 0; "+
             " begin "+
             "   vguid := sys_guid(); "+
             "   delete from U_SELECTION_OBJ_PROCESS; "+
             "   for datax in ( select u.t_recid, u.t_timestamp, u.t_objecttype, u.t_objectid, u.t_type, u.t_status, "+
             "                         sf.t_number, sf.t_partyid, sf.t_datebegin, "+
             "                         p.t_name, "+
             "                         DECODE(u.t_status, 1, 'Ожидание выгрузки', "+
             "                                            2, 'Выгружено', "+
             "                                            3, 'Обработка', "+
             "                                            4, 'Успех', "+
             "                                            5, 'Ошибка обработки', TO_CHAR(u.t_status)) t_status_str "+
             "                    from "+
             "                         ( select d.* from utableprocessevent_dbt d "+
             "                            where d.t_objecttype = :p_objecttype and d.t_timestamp between :pd1 and :pd2 "+
             "                              and d.t_recid = (select max(t_recid) from utableprocessevent_dbt d2 "+
             "                                                where d2.t_objecttype = d.t_objecttype and d2.t_objectid = d.t_objectid)) u, "+
             "                         ddlcontr_dbt dl, "+
             "                         dsfcontr_dbt sf, "+
             "                         dparty_dbt p "+
             "                   where u.t_objectid = dl.t_dlcontrid "+
             "                     and dl.t_sfcontrid = sf.t_id "+
             "                     and p.t_partyid = sf.t_partyid "+
             "                     and t_status not in (1, 4) "+
             "                order by t_timestamp desc) loop "+
             "      rec_u_Selection_Process.t_Execute          := 'X';     /* признак 'выгружать'*/ "+
             "      rec_u_Selection_Process.T_OBJECTTYPE       := datax.t_objecttype; "+
             "      rec_u_Selection_Process.T_OBJECTID         := datax.t_objectid; "+
             "      rec_u_Selection_Process.T_TYPE             := datax.t_type; "+
             "      rec_u_Selection_Process.T_DATE             := datax.t_timestamp; "+
             "      rec_u_Selection_Process.T_NUMB_DOCUMENT    := ''; "+
             "      rec_u_Selection_Process.T_NUMBER_PACK      := datax.t_status; "+
             "      rec_u_Selection_Process.T_USERFIELD4       := datax.t_status_str; "+
             "      rec_u_Selection_Process.T_USERFIELD2       := datax.t_name; "+
             "      rec_u_Selection_Process.t_account_payer    := datax.t_number; "+
             "      rec_u_Selection_Process.t_account_receiver := to_char(datax.t_datebegin,'dd.mm.yyyy'); "+
             "      rec_u_Selection_Process.t_sum_payer        := 0; "+
             "      rec_u_Selection_Process.t_sum_receiver     := 0; "+
             "      rec_u_Selection_Process.SESSION_GUID       := vguid; "+ 
             "      rec_u_Selection_Process.PROCESSTIME        := sysdate; "+
             "      rec_u_Selection_Process.T_RECID            := datax.t_recid; "+
             "      rec_u_Selection_Process.t_mac              := ''; "+
             "      rec_u_Selection_Process.t_oper             := :p_oper; "+
             "      insert into U_SELECTION_OBJ_PROCESS "+
             "      values rec_u_Selection_Process; "+
             "      vcnt := vcnt+1; "+
             "   end loop; "+
             "   :p_cnt := vcnt; "+
             "   :p_guid := vguid; "+
             " end; ";
 
   cmd = RSDCommand(sql_str);

   cmd.AddParam("p_objecttype", RSDBP_IN, _objecttype);
   cmd.AddParam("p_begdate", RSDBP_IN, vbegdate);
   cmd.AddParam("p_enddate", RSDBP_IN, venddate);
   cmd.AddParam("p_oper", RSDBP_IN, {oper});
   cmd.AddParam("p_cnt", RSDBP_OUT, V_INTEGER);
   cmd.AddParam("p_guid", RSDBP_OUT, V_STRING);
   cmd.execute();

   gv_evnt_sel_prm.v_guid = cmd.value("p_guid");
   gv_evnt_sel_prm.v_cnt = cmd.value("p_cnt");

   cmd.close;
   cmd = null; sql_str = null;
onerror(e)
   RunError(e.message + getFullCmdErrorText(cmd));
end;


private macro RunTempTable()
   var sql_str, rs, cmd;

   sql_str = " declare "+
             "     vresultcode            u_unload_bisquit.resultcode%type; "+
             "     verr                   u_unload_bisquit.err%type; "+
             " begin "+
             "    for datax in (select sp.t_objecttype, sp.t_objectid, sp.t_type, sp.t_date, sp.session_guid, sp.processtime, "+
             "                         sp.t_recid, sp.t_mac, sp.t_number_pack, sp.t_oper, "+
             "                         ev.t_status "+
             "                    from U_SELECTION_OBJ_PROCESS sp, "+
             "                         utableprocessevent_dbt ev "+
             "                   where sp.t_recid = ev.t_recid "+
             "                     and sp.t_execute = 'X') loop "+
             "       vresultcode := -1; "+
             "       verr := ''; "+
             "       if datax.t_status = 4 then  /* уже успешно выгружено */"+
             "          vresultcode := 0; "+
             "          verr := 'Уже выгружен'; "+
             "       elsif datax.t_status = 1 then  /* уже обрабатывается */"+
             "          vresultcode := -1420; "+
             "          verr := 'Обрабатывается в другой сессии'; "+
             "       end if; "+
             "       if  vresultcode = -1 then "+
             "          /* смена статуса в таблице событий */ "+
             "          UPDATE utableprocessevent_dbt ex1 "+
             "             SET ex1.t_status = 1, "+
             "                 ex1.T_RESULTTEXT = SYSTIMESTAMP, "+
             "                 ex1.t_timestamp = sysdate, "+
             "                 ex1.t_lastupdate = sysdate, "+
             "                 ex1.T_RESULtCODE = '0', "+
             "                 ex1.t_attempts = 1 "+  //отсчет попыток заново
             "           WHERE  ex1.t_recid = datax.t_recid; "+
             "       end if; "+ // ошибки фиксировать некуда
             "    end loop; "+
             " end; ";
   cmd = RSDCommand(sql_str);
   cmd.execute();

   cmd.close;
   cmd = null; sql_str = null;

onerror(e)
   RunError(e.message + getFullCmdErrorText(cmd));
end;


macro m_evnt_proc_scr(p_rs, p_cmd, p_id, p_key)
   var CM_FLAG = CM_DEFAULT;
   var ss;

   if(p_cmd == DLG_INIT)

   elif(p_cmd == DLG_KEY)
      if(p_key == K_ENTER)
         CM_FLAG = CM_DEFAULT;
      elif(p_key == K_F5)
         CM_FLAG = CM_CANCEL;
      elif(p_key == K_F8)
         CM_FLAG = CM_IGNORE;
      elif(p_key == K_F9)
         CM_FLAG = CM_CANCEL;
      elif(p_key == K_F2)
         CM_FLAG = CM_CANCEL;
      elif(p_key == K_SPACE)
         ss = CodeFor(p_rs.value("t_execute"));
         p_rs.edit;
         p_rs.value("t_execute") = StrFor(88-ss);
         p_rs.Update();
         UpdateScroll(p_rs,2);

      end;
   end;

   return CM_FLAG;
end; /* macro m_evnt_proc_scr() */
        

macro ShowSelection(_objecttype)
   private var gv_column_list_scr = TArray(); /* Список колонок скроллинга */
   private var gv_num_columns_scr = 0;        /* Количество колонок скроллинга */
   private var gv_scr_focus = 0;              /* Номер позиции скроллинга */

   var sql_scr, cmd_scr, rs_scr, cnt;
   sql_scr = "SELECT t_execute, t_account_payer, t_account_receiver, T_USERFIELD2, to_char(t_date,'dd.mm.yyyy hh24:mi:ss') t_date, "+
             "       T_USERFIELD4, t_recid  FROM u_Selection_obj_Process ";
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_execute",          "",                3, 1); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_account_payer",    "Номер договора", 20, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_account_receiver", "Дата открытия",  10, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "T_USERFIELD2",       "Клиент",         30, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_date",             "Дата выгрузки",  20, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_status",           "Статус",         10, 2); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_recid",            "",                1, 2); gv_num_columns_scr = gv_num_columns_scr + 1;

   if (sql_scr)
      cmd_scr = RSDCommand(sql_scr);
      cmd_scr.NullConversion = true;
      rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
      rs_scr.NullConversion = true;
      rs_scr.pagesize = 100;

      while(RunScroll(rs_scr, gv_num_columns_scr, gv_column_list_scr, "SelectionRecords", "m_evnt_proc_scr", "Список объектов для выгрузки", "ESC выход ~ПРОБЕЛ~ Отметить/Снять отметку ", false, 5, NULL, NULL, 40))
         rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
      end;
      rs_scr.close; cmd_scr.close; rs_scr = null; cmd_scr = null;

      sql_scr = "SELECT count(t_execute) cnt FROM u_Selection_obj_Process WHERE t_execute = 'X'";
      rs_scr = RSDRecordSet(sql_scr);
      if (rs_scr.movenext)
         cnt = Int(rs_scr.value("cnt"));
      end;
      rs_scr.close; rs_scr = null; 

      gv_evnt_sel_prm.v_cnt = cnt;
   end;

onError(e)
   RunError(e.message + getFullCmdErrorText(cmd_scr));
end;


/*---------------------------*/
/* Параметры отбора объектов */
/*---------------------------*/
private class c_evnt_selection_prm()
   var v_com_date;
   var v_com_beg_date;
   var v_com_end_date;
   var v_com_date_switch;

   var v_guid:string;
   var v_cnt;
   var v_flag_notallsent;
   var v_current_date;
   var v_period_txt;

   /* Значения по умолчанию */
   private macro m_init_evnt_selection_prm()
      v_com_date = date();
      v_com_beg_date = date();
      v_com_end_date = date();
      v_com_date_switch = false;

      v_guid = "";
      v_cnt = 0;
      v_flag_notallsent = false;

      v_current_date = date();
      v_period_txt = "";
   end;

   m_init_evnt_selection_prm();
end; /* class c_evnt_selection_prm() */


/*----------------------------------------*/
/* Класс основной формы                   */
/*----------------------------------------*/
class (TRsbPanel) c_com_unload_form()
   const CV_POS_X = 3;
   const CV_POS_Y = 2;
   var v_com_date_fld;
   var v_com_beg_date_fld;
   var v_com_end_date_fld;
   var v_com_date_switch_fld;

   var v_com_unload_btn;
   var v_com_result_btn;

   var v_com_date;
   var v_com_beg_date;
   var v_com_end_date;

   macro EnableDisableFields(_sign)
      v_com_date_fld.enabled = _sign;
      v_com_beg_date_fld.enabled = _sign;
      v_com_end_date_fld.enabled = _sign;

      v_com_date_switch_fld.enabled = _sign;

      v_com_unload_btn.enabled = _sign;

      redraw();
   end;

   macro m_evnt_on_key_pressed(p_rsb_evnt)
      if(p_rsb_evnt.KeyCode == K_F2)
         close(CV_CLOSE_FORM_OK);
      end;
      return true;
   end;

   macro m_on_switch_date(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_CONTROL_CHECKED)
         gv_evnt_sel_prm.v_com_date_switch = v_com_date_switch_fld.checked;
      end;

      if(gv_evnt_sel_prm.v_com_date_switch)
         v_com_date_fld.focusable = false;
         v_com_date_fld.enabled = false;
         v_com_date_fld.editable = false;

         v_com_beg_date_fld.focusable = true;
         v_com_beg_date_fld.enabled = true;
         v_com_beg_date_fld.editable = true;

         v_com_end_date_fld.focusable = true;
         v_com_end_date_fld.enabled = true;
         v_com_end_date_fld.editable = true;
      else
         v_com_date_fld.focusable = true;
         v_com_date_fld.enabled = true;
         v_com_date_fld.editable = true;

         v_com_beg_date_fld.focusable = false;
         v_com_beg_date_fld.enabled = false;
         v_com_beg_date_fld.editable = false;

         v_com_end_date_fld.focusable = false;
         v_com_end_date_fld.enabled = false;
         v_com_end_date_fld.editable = false;
      end;

      redraw();

      return true;
   end;

   macro check_params()
      if ( (not v_com_date_switch_fld.checked) and (gv_evnt_sel_prm.v_com_date == date(0,0,0)) )
         msgbox("Не указана дата");
         return false;
      end;

      if ( (v_com_date_switch_fld.checked) and ( (gv_evnt_sel_prm.v_com_beg_date == date(0,0,0)) or 
                                                 (gv_evnt_sel_prm.v_com_end_date == date(0,0,0))  ) )
         msgbox("Не указана дата из диапазона");
         return false;
      end;

      return true;

   end;

   macro GetNameAmount(_val, _num1, _num2, _num10) 
      var res = _num10; 

      if ( (mod(_val,100) >= 11) and (mod(_val,100) <= 20) )
         res = _num10;
      elif (mod(_val,10) == 1) 
         res = _num1;
      elif ( (mod(_val,10) >= 2) and (mod(_val,10) <= 4) )
         res = _num2;
      else
         res = _num10;
      end;
      return res;
   end;

   macro m_evnt_unload_btn(p_rsb_evnt)
      var num_acc, num_acc_str;

      if(p_rsb_evnt.id == RSB_EV_BUTTON_CLICKED)
         if (check_params)
            if (not v_com_date_switch_fld.checked)
               gv_evnt_sel_prm.v_period_txt = " за дату "+gv_evnt_sel_prm.v_com_date;
            elif (v_com_date_switch_fld.checked)
               gv_evnt_sel_prm.v_period_txt = " за период с "+gv_evnt_sel_prm.v_com_beg_date+" по "+ gv_evnt_sel_prm.v_com_end_date;
            end;

            FillTempTable(8207);
            if (gv_evnt_sel_prm.v_cnt > 0)
               num_acc_str = GetNameAmount(gv_evnt_sel_prm.v_cnt,"договор","договора","договоров");

               if (msgboxex("К выгрузке "+gv_evnt_sel_prm.v_cnt+" "+num_acc_str+gv_evnt_sel_prm.v_period_txt+
                            ".| Просмотреть?", MB_YES+MB_NO, IND_NO) == IND_YES)
                  ShowSelection(8207);
               end;
               if (gv_evnt_sel_prm.v_cnt == 0)
                  msgbox("Нет договоров для выгрузки");
               else
                  num_acc_str = GetNameAmount(gv_evnt_sel_prm.v_cnt,"договор","договора","договоров");
                  if (msgboxex("Отмечено к выгрузке "+gv_evnt_sel_prm.v_cnt+" "+num_acc_str+gv_evnt_sel_prm.v_period_txt+"|Вы уверены, что хотите их выгрузить?", MB_NO+MB_YES, IND_NO) == IND_YES)
                     RunTempTable();
                     msgbox("Выгрузка запущена, отслеживайте результаты в журнале| \"Интерфейс таблицы событий\", событие с типом 8207");
                     //EnableDisableFields(false);
                  end;
               end;
            else
               msgbox("Нет договоров для выгрузки");
            end;

         end;
      end;

      return true;
   end;


   private macro m_init_com_unload_form(i_this_obj)
      Caption = "Выгрузка в Депозитарий";

      Status = "[Esc] Выход ";

      /* Фильтр по дате */
      AddLabel(TRsbLabel(CV_POS_X, CV_POS_Y, "Дата выборки"));
      v_com_date_fld = TRsbEditField(FT_DATE);
      v_com_date_fld.bindValue(gv_evnt_sel_prm, "v_com_date", 10);
      v_com_date_fld.setPosition(CV_POS_X, (CV_POS_Y + 1));
      v_com_date_fld.setSize(10, 1);
      if(gv_evnt_sel_prm.v_com_date_switch)
         v_com_date_fld.focusable = false;
         v_com_date_fld.enabled = false;
         v_com_date_fld.editable = false;
      else
         v_com_date_fld.focusable = true;
         v_com_date_fld.enabled = true;
         v_com_date_fld.editable = true;
      end;
      AddControl(v_com_date_fld);

      AddLabel(TRsbLabel((CV_POS_X + 14), (CV_POS_Y + 1), "Диапазон"));
      v_com_date_switch_fld = TRsbCheckBox();
      v_com_date_switch_fld.setPosition((CV_POS_X + 12), (CV_POS_Y + 1));
      v_com_date_switch_fld.onChecked(R2M(i_this_obj, "m_on_switch_date"));
      v_com_date_switch_fld.checked = gv_evnt_sel_prm.v_com_date_switch;
      AddControl(v_com_date_switch_fld);

      AddLabel(TRsbLabel((CV_POS_X + 22), CV_POS_Y, "Диапазон дат"));
      v_com_beg_date_fld = TRsbEditField(FT_DATE);
      v_com_beg_date_fld.bindValue(gv_evnt_sel_prm, "v_com_beg_date", 10);
      v_com_beg_date_fld.setPosition((CV_POS_X + 22), (CV_POS_Y + 1));
      v_com_beg_date_fld.setSize(10, 1);
      if(gv_evnt_sel_prm.v_com_date_switch)
         v_com_beg_date_fld.focusable = true;
         v_com_beg_date_fld.enabled = true;
         v_com_beg_date_fld.editable = true;
      else
         v_com_beg_date_fld.focusable = false;
         v_com_beg_date_fld.enabled = false;
         v_com_beg_date_fld.editable = false;
      end;
      AddControl(v_com_beg_date_fld);
      v_com_end_date_fld = TRsbEditField(FT_DATE);
      v_com_end_date_fld.bindValue(gv_evnt_sel_prm, "v_com_end_date", 10);
      v_com_end_date_fld.setPosition((CV_POS_X + 22), (CV_POS_Y + 2));
      v_com_end_date_fld.setSize(10, 1);
      if(gv_evnt_sel_prm.v_com_date_switch)
         v_com_end_date_fld.focusable = true;
         v_com_end_date_fld.enabled = true;
         v_com_end_date_fld.editable = true;
      else
         v_com_end_date_fld.focusable = false;
         v_com_end_date_fld.enabled = false;
         v_com_end_date_fld.editable = false;
      end;
      AddControl(v_com_end_date_fld);

      /* Кнопка выгрузки */
      v_com_unload_btn = TRsbPushButton("Выгрузить");
      v_com_unload_btn.setPosition((CV_POS_X), (CV_POS_Y + 4));
      v_com_unload_btn.setSize(12, 1);
      v_com_unload_btn.onClicked(R2M(i_this_obj, "m_evnt_unload_btn"));
      AddControl(v_com_unload_btn);

      AddEventHandler(RSB_EV_KEY_PRESSED, R2M(i_this_obj, "m_evnt_on_key_pressed"));

      setSize(45, 8);
      setPosition(20, 5);

   end;

   InitTRsbPanel();

   m_init_com_unload_form(this);
end; /* class (TRsbPanel) c_com_unload_form() */

   var v_etl = true, stat;

   GetRegistryValue(DEPO_UNLOAD_TYPE, V_BOOL, v_etl, stat);
   if (stat>0)
      v_etl = true; // не найдено - значит, ETL
   end;

   if (v_etl)
      msgbox("Повторная выгрузка договоров из интерфейса не доступна.| Включена настройка "+DEPO_UNLOAD_TYPE);
      exit(1);
   end;

   gv_evnt_sel_prm = c_evnt_selection_prm();

         gv_com_unload_form = c_com_unload_form();
         gv_com_unload_form.run;

exit(1);
