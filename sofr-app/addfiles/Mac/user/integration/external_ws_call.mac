/*---------------------------------------------------------*/
/* Инструментарий для работы с объектом WinHttpRequest.5.1 */
/*                                                         */
/* Автор: Федюшин П., Мамичев А., Карпов В.                */
/*---------------------------------------------------------*/

/*import rslxml;*/
/*import rcw;*/ /* Для использования CreateObject() */
import rslx; /* Для использования ActiveX() */
import "global_utils_intgr.mac";

/*--------------------------------------------------*/
/* Класс для выполнения вызова внешнего Web-сервиса */
/*--------------------------------------------------*/
class c_external_ws_caller()
   /* Тип объектов для хранения данных элемента заголовка http */
   private class c_http_header_type
      var name
         ,value;
   end;

   /* Тип объектов для хранения данных опций http */
   private class c_http_option_type
      var name /* Пока не используется, чтобы не держать в памяти структуру с именами */
         ,value;
   end;
/*
   private var option_enum = TArray;
   option_enum.value(0) = "WinHttpRequestOption_UserAgentString";
   option_enum.value(1) = "WinHttpRequestOption_URL";
   option_enum.value(2) = "WinHttpRequestOption_URLCodePage";
   option_enum.value(3) = "WinHttpRequestOption_EscapePercentInURL";
   option_enum.value(4) = "WinHttpRequestOption_SslErrorIgnoreFlags";
   option_enum.value(5) = "WinHttpRequestOption_SelectCertificate";
   option_enum.value(6) = "WinHttpRequestOption_EnableRedirects";
   option_enum.value(7) = "WinHttpRequestOption_UrlEscapeDisable";
   option_enum.value(8) = "WinHttpRequestOption_UrlEscapeDisableQuery";
   option_enum.value(9) = "WinHttpRequestOption_SecureProtocols";
   option_enum.value(10) = "WinHttpRequestOption_EnableTracing";
   option_enum.value(11) = "WinHttpRequestOption_RevertImpersonationOverSsl";
   option_enum.value(12) = "WinHttpRequestOption_EnableHttpsToHttpRedirects";
   option_enum.value(13) = "WinHttpRequestOption_EnablePassportAuthentication";
   option_enum.value(14) = "WinHttpRequestOption_MaxAutomaticRedirects";
   option_enum.value(15) = "WinHttpRequestOption_MaxResponseHeaderSize";
   option_enum.value(16) = "WinHttpRequestOption_MaxResponseDrainSize";
   option_enum.value(17) = "WinHttpRequestOption_EnableHttp1_1";
   option_enum.value(18) = "WinHttpRequestOption_EnableCertificateRevocationCheck";
*/
   private var option_max_num;   /* Максимальный допустимый номер опции (допустимы номера от 0 до этого значения) */
   private var service_msg;      /* Текст служебного сообщения */
   private var http_request;     /* Объект WinHttpRequest.5.1 */
   private var http_header_list; /* Список заголовков http-пакета */
   private var http_option_list; /* Список опций */
   private var return_code;      /* Возвращаемый код */
   private var status_text;      /* Текст статуса */
   private var response_text;    /* Текст ответа */

   /* SetTimeouts(timeout_resolve, timeout_connect, timeout_send, timeout_receive) */
   var timeout_resolve; /* Время (мс) получения ip адреса для имени хоста. 0 (по умолчанию) - "бесконечное" время ожидания (нет таймаута). */
   var timeout_connect; /* Время (мс) установки соединения сокета с сервером. Значение по умолчанию: 60000 мс. */
   var timeout_send;    /* Время (мс) отправки одного пакета данных запроса на сервер посредством сокета. Значение по умолчанию: 30000 мс. */
   var timeout_receive; /* Время (мс) получения одного пакета данных ответа от сервера. Значение по умолчанию: 30000 мс. */

   var client_https_certificate; /* Место хранения клиентского сертификата в хранилище */
   var http_method_name; /* Имя используемого метода http */
   var server_url;       /* Ссылка на сервер, обрабатывающий запрос */
   var async_mode;       /* Признак использования асинхронного соединения */

   /* Конструктор */
   private macro init_ws_caller_params()
      option_max_num = 18;
      service_msg = "";
      http_method_name = "POST";
      async_mode = false; /* По умолчанию используем синхронное соединение */
      http_header_list = TArray;
      http_option_list = TArray;

      /* Значения по умолчанию с MSDN */
      timeout_resolve = 0;
      timeout_connect = 60000;
      timeout_send    = 30000;
      timeout_receive = 30000;
   end;

   /* Метод установка таймаутов */
   private macro set_timeouts_range()
      var v_ret = true;

      http_request.SetTimeOuts(timeout_resolve,
                               timeout_connect,
                               timeout_send,
                               timeout_receive);

      return v_ret;

      onError(err)
         service_msg = "Произошла ошибка при установке значений таймаутов";
         v_ret = false;
         return v_ret;
   end;

   /* Метод открытия http-соединения с заданным адресом */
   private macro open_connection()
      var v_ret = true;

      http_request.Open(http_method_name, server_url, async_mode);

      return v_ret;

      onError(err)
         service_msg = "Произошла ошибка при открытии соединения";
         v_ret = false;
         return v_ret;
   end;

   /* Метод установки значений заголовков запроса */
   private macro set_request_headers()
      var v_ret = true;
      var v_row_num = 0;

      while(v_row_num < http_header_list.size)
         http_request.SetRequestHeader(http_header_list.value(v_row_num).name,
                                       http_header_list.value(v_row_num).value);
         v_row_num = v_row_num + 1;
      end;

      return v_ret;

      onError(err)
         service_msg = "Произошла ошибка при установке значений заголовков";
         v_ret = false;
         return v_ret;
   end;

   /* Метод установки значений опций запроса */
   private macro set_request_options()
      var v_ret = true;
      var v_row_num = 0;

      while(v_row_num < http_option_list.size)
         if(ValType(http_option_list.value(v_row_num)) != V_UNDEF)
            http_request.Option(v_row_num) = http_option_list.value(v_row_num).value;
         end;

         v_row_num = v_row_num + 1;
      end;

      return v_ret;

      onError(err)
         service_msg = "Произошла ошибка при установке значений опций";
         v_ret = false;
         return v_ret;
   end;

   /* Метод выбора клиентского сертификата, используемого при взаимодействии */
   private macro set_client_certificate()
      var v_ret = true;

      if(ValType(client_https_certificate) != V_UNDEF)
         if(strlen(client_https_certificate) != 0)
            http_request.SetClientCertificate(client_https_certificate);
         end;
      end;

      return v_ret;

      onError(err)
         service_msg = "Произошла ошибка при выборе клиентского сертификата";
         v_ret = false;
         return v_ret;
   end;

   /* Метод отправки запроса */
   private macro send_request(p_req)
      var v_ret = true;

      http_request.send(p_req);

      return_code =   http_request.Status;
      status_text =   http_request.StatusText;
      response_text = http_request.ResponseText;

      if(return_code != 200)
         service_msg = "Возникли проблемы при отправке запроса";
         v_ret = false;
      else /* Status = 200 - The request completed successfully */
         v_ret = true;
      end;

      return v_ret;

      onError(err)

         return_code = c_usr_err_obj.obtain_err_code(err);
         status_text = c_usr_err_obj.obtain_err_message_exactly(err);
         service_msg = c_usr_err_obj.obtain_err_axmes_exactly(err);

         if(ValType(service_msg) == V_UNDEF)
            service_msg = "Произошла ошибка при отправке запроса";
         else
            service_msg = string("Произошла ошибка при отправке запроса: ", service_msg);
         end;

         v_ret = false;
         return v_ret;
   end;

   /* Метод получения текста сервисного сообщения */
   macro get_service_msg()
      return service_msg;
   end;

   /* Метод получения возвращаемого кода */
   macro get_return_code()
      return return_code;
   end;

   /* Метод получения текстового описания статуса */
   macro get_status_text()
      return status_text;
   end;

   /* Метод получения ответного сообщения */
   macro get_response_text()
      return response_text;
   end;

   /* Метод получения списка заголовков (list = TArray(obj.get_http_header_list())) */
   macro get_http_header_list()
      return http_header_list;
   end;

   /* Метод очистки списка заголовков */
   macro erase_http_header_list()
      http_header_list.size = 0;
   end;

   /* Метод добавления заголовка к списку используемых в запросе */
   macro add_http_header(p_header_name, p_header_value)
      var v_row_num = http_header_list.size;
      http_header_list.value(v_row_num) = c_http_header_type();
      http_header_list.value(v_row_num).name = p_header_name;
      http_header_list.value(v_row_num).value = p_header_value;
   end;

   /* Метод очистки списка опций запроса */
   macro erase_http_option_list()
      http_option_list.size = 0;
   end;

   /* Метод удаления значения заданной опции запроса */
   macro delete_option_value(p_option_num)
      if(http_option_list.size >= (p_option_num + 1))
         http_option_list.value(p_option_num) = null;
      end;
   end;

   /* Сохранить значение заданной опции запроса */
   macro set_option_value(p_option_num, p_option_val)
      if(p_option_num <= option_max_num)
         http_option_list.value(p_option_num) = null;
         http_option_list.value(p_option_num) = c_http_option_type();
         http_option_list.value(p_option_num).value = p_option_val;
      end;
   end;

   /* Получить значение заданной опции запроса */
   macro get_option_value(p_option_num)
      var v_ret = null;

      if(http_option_list.size >= (p_option_num + 1))
         v_ret = http_option_list.value(p_option_num).value;
      end;

      return v_ret;
   end;

   /* Метод отправки запроса */
   macro submit_request(p_req_text)
      var v_ret = true;

      http_request = ActiveX("WinHttp.WinHttpRequest.5.1");
      /* Инициализация значений таймаутов */
      if((v_ret) and (not set_timeouts_range()))
         v_ret = false;
      end;
      /* Открытие соединения */
      if((v_ret) and (not open_connection()))
         v_ret = false;
      end;
      /* Инициализация значений заголовков запроса */
      if((v_ret) and (not set_request_headers()))
         v_ret = false;
      end;
      /* Инициализация значений опций запроса */
      if((v_ret) and (not set_request_options()))
         v_ret = false;
      end;
      /* Инициализация пути к сертификату */
      if((v_ret) and (not set_client_certificate()))
         v_ret = false;
      end;
      /* Отправка запроса */
      if((v_ret) and (not send_request(p_req_text)))
         v_ret = false;
      end;

      return v_ret;

      onError(err)
         service_msg = "Произошла ошибка во время отправки запроса";
         v_ret = false;
         return v_ret;
   end;

   init_ws_caller_params();
end; /* class external_ws_caller() */