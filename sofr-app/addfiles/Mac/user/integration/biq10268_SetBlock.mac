/** 
 @file 		BIQ10268_SetBlock_exl.mac
 @brief 	Начальное решение по BIQ-10268. Загрузка признака блокировки обмена с CDI (из Excel-файла или csv-файла)
  
 # tag 
 - CDI
   
 # changeLog 
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |27.11.2024 |Велигжанин А.В.|CCBO-10808_CCBO-10849                           |Создание 

*/ 
IMPORT BankInter, CTInter, rcw, globals, "usr_open_files.mac";


VAR g_PathIn: string = "..\\import\\biq10268\\";     		// Путь к каталогу с файлом данных с признаком блокировки
   

/**
  @brief    True, если полученная ячейка содержит значение
  @param[in]   val         значение ячейки
*/
PRIVATE MACRO NotNull ( val )
    if ( val == NULL ) return false; end;
    if ( ValType (val) == 26 ) return false; end;
    return true;
END; // NotNull


Macro NormValue (vl)
  if (valtype (vl) == V_UNDEF)
     return "";
  end;
  return vl;
End;

/**
  @brief    	Открытие Excel
*/
Private macro OpenExcel
    return CreateObject ("rsax", "TRsAxServer", "RsBankAxServer", isStandAlone ()).CreateComObject ("Excel.Application");
OnError
    return NULL;
End;


/**
  @brief    	Открытие Excel-файла
*/
Private macro OpenExcelFile ( fName )
    Var ex = OpenExcel ();
    if ( ex == NULL )
        MsgBox ("Ошибка открытия MS-Excel");
        return NULL;
    end;
    ex.Workbooks.Open (fName);
    return ex;
OnError
    ex = NULL;
    MsgBox ("Ошибка открытия файла ", fName);
    return NULL;
End;


/* @brief 	Установка признака блокировки для полученного субъекта.
                Возвращает TRUE, если признак блокировки установлен.
                Возвращает FALSE при ошибке установки.
*/
PRIVATE MACRO SetBlockParty( p_PartyID )

  Print ( "Установка признака блокировки субъекта: " + string(p_PartyID) );

  var party = TRecHandler("party.dbt");
  var stat: INTEGER = 0;
  var ObjID: STRING;
  var ObjType : INTEGER = 3;
  var GroupID: INTEGER = 175;  // Блокировать обмен с CDI
  var AttrID: INTEGER = 1;  // блокировать
  var Ok: BOOL = FALSE;

  party.Clear();
  party.rec.partyID = p_PartyID;

  ObjID = UniID(party, ObjType); // Объявлено в CTInter

  if( ObjID == "" )
      Println(" Ошибка поиска идентификатора субъекта");
  elif( not DisconnectObjAttr(ObjType, ObjID, GroupID) )
      Println(" Ошибка сброса категории");
  elif( not ConnectObjAttr(stat, ObjType, ObjID, GroupID, AttrID, null, null, {curdate}) )
      Println(" Ошибка установки категории");
  elif( stat != 0 )
      Println(" Ошибка установки категории");
  else 
      Println(" Ok");
      Ok = TRUE;
  end;

  RETURN Ok;
END; // SetBlockParty


Private macro LoadXLS  (ex, EndLine)
   
    Var str = "", error = FALSE, sql, cnt = 0;
    Var Line = 1, Col = 0, EndCol = 0;
  var x_BlockFlag :integer;
  var x_PartyID :string;

  var 
    x_AllCount :integer = 0
    , x_OkCount :integer = 0
    , x_ErrCount :integer = 0
    , x_ErrStr :string
  ;

   
    EndLine = EndLine + Line;
    InitProgress (EndLine - Line, NULL, "Чтение файла");

    while ( (Line=Line+1) < EndLine )
        UseProgress (cnt = cnt+1);

        x_BlockFlag = int(NormValue(trim (string(ex.Cells (Line, Col+1 ).Value))));
        x_PartyID = string(int(NormValue (trim (string(ex.Cells (Line, Col+2 ).Value))))); 

        x_AllCount = x_AllCount + 1;
        if (x_BlockFlag == 1)
          x_OkCount = x_OkCount + 1;
          if( SetBlockParty(x_PartyID) == false )
            x_ErrCount = x_ErrCount + 1;
          end;
        end;
    end;
    RemProgress ();

    ex.Quit ();
    ex = NULL;

    Println ( "Обработка завершена" );
    Println ( "Общее количество записей в файле: " + string(x_AllCount) );
    Println ( "Число субъектов, отмеченных признаком блокировки: " + string(x_OkCount) );
    Println ( "Количество ошибок: " + string(x_ErrCount) );
End;

/**
  @brief    Процедура запуска процеса
*/
PRIVATE MACRO RepStartProcess
  Var 
    x_FileName, ex, cnt = 0, x_EndLine
    , x_FileID : integer
  ;

  Println ( "Загрузка признака блокировки обмена с CDI..." );
  Println ();

  if ( not SelectFile (x_FileName, (g_PathIn+"*.*"), NULL, NULL, TRUE) )
      Println ( "Файл не выбран" );
      return FALSE;
  elif ( (ex = OpenExcelFile (x_FileName)) == NULL )
      Println ( "Ошибка открытия файла "+string(x_FileName) );
      return FALSE;
  end;

  x_EndLine = ex.ActiveSheet.UsedRange.Rows.Count;

  return LoadXLS  (ex, x_EndLine);
END; // StartProcess


/**
  @brief    Обработать файлы
*/
macro StartBlockProcess()

  SetOutPut(gettxtfilename("biq10268_setblock_"+StrSubst(string(date()),".","")+StrSubst(string(time()),":","")),false);

  var stat = RepStartProcess(); // обработка файлов с формированием протокола

  ViewFile(SetOutPut(null, true));
  SetOutPut(null, true)
END;

