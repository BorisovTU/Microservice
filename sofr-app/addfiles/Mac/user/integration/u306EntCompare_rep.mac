/**
 @file 		u306EntCompare_rep.mac
 @brief 	Интеграция с внешними системами. Отчет-сверка зачислений и списаний ДС СОФР_ЦФТ

 # tag
 - functional_block: Отчетность_внутренняя
 - code_type: Report
 - code_type: GUI

 #menu 
  - БО ЦБ\ Отчеты \ РСХБ \ [13408] Сверка зачислений списаний ДС СОФР_ЦФТ

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |16.12.2024 |Велигжанин А.В.|DEF-76827                                       |Поля <s06.1>, <s08.1>, <r4.1>
 |14.10.2024 |Велигжанин А.В.|BOSS-1266_BOSS-2664                             |Создание (скопировано из u306AccCompare_Rep, только там 
 |           |               |                                                |была реализация для другого отчета, с 13-ю полями, 
 |           |               |                                                |здесь полей больше и состав другой)
*/
IMPORT globals, ActiveX, oralib, "global_utils_intgr.mac", "u_common_email_utils.mac", "ucompare_func.mac", "dlutils.mac";

PRIVATE CONST 
  EXPORT_XML_FILENAME_TXT = "for_Ent306compare"
  , CV_EMAIL_GRP_ETCOMPARE = 30 				//группа сверки зачислений загруженных из ЦФТ в СОФР 
  ;

PRIVATE VAR 
  g_CNum = string(UserNumber)
  ;

/*  Получение курсора
*/
PRIVATE MACRO GetDataSet( p_DataSet :@RsdRecordset, p_ReqId :string, p_ParamProcess :object, p_DialogFlag :integer, p_ErrString :@string ) :bool
  VAR 
    x_Sql :string = "", x_Cmd
  ;

  IF( p_DialogFlag == 1 )
    InitProgress(-1, "Обработка данных.", "Обработка данных.");
  END;

  // Вызов процедуры формирования данных сверки
  x_Sql =  
      "DECLARE "
    + "  x_rows number; "
    + "BEGIN "
    + "  x_rows := cft_utils.CFT_CreateCompare306(:x_ReqID, :InDate, :OutDate); "
    + "END; "
  ;
  x_Cmd = RSDCommand(x_Sql);
  x_Cmd.AddParam("ReqID", RSDBP_IN, p_ReqID);
  x_Cmd.addParam("InDate", RSDBP_IN, p_ParamProcess.DateIn);
  x_Cmd.addParam("OutDate", RSDBP_IN, p_ParamProcess.DateOut);
  x_Cmd.execute();

  // Запрос сверки
  x_Sql = "SELECT "
     + "r.t_cft_date_carry, r.t_cft_doc_number, r.t_cft_sum, r.t_cft_dbt_acc, r.t_cft_dbt_cur "
     + ", r.t_cft_crd_acc, r.t_cft_crd_cur, r.t_cft_payer_acc, r.t_cft_receiver_acc "
     + ", r.t_cft_payer_name, r.t_cft_receiver_name, r.t_cft_pay_purpose "
     + ", r.t_sofr_date_carry, r.t_sofr_doc_number, r.t_sofr_sum, r.t_sofr_dbt_acc, r.t_sofr_dbt_cur "
     + ", r.t_sofr_crd_acc, r.t_sofr_crd_cur, r.t_sofr_payer_name, r.t_sofr_receiver_name, r.t_sofr_ground "
     + ", r.t_sofr_receiver_acc, r.t_sofr_receiver_payname "
     + ", case when r.t_check_pay = 1 then 'успешно' ELSE 'неуспешно' END AS t_check_pay "
     + ", case when r.t_check_cur = 1 then 'успешно' ELSE 'неуспешно' END AS t_check_cur "
     + ", case when r.t_check_name = 1 then 'успешно' ELSE 'неуспешно' END AS t_check_name "
     + ", case when r.t_check_acc = 1 then 'успешно' ELSE 'неуспешно' END AS t_check_acc "
     + ", case when r.t_check_sum = 1 then 'успешно' ELSE 'неуспешно' END AS t_check_sum "
     + ", case when r.t_check_all = 1 then 'успешно' ELSE 'неуспешно' END AS t_check_all "
     + " FROM UENT306COMPARE_TMP r "
  ;
  p_DataSet = execSQLselect( x_Sql, null );

  IF( p_DialogFlag == 1 )
    RemProgress;
  END;

  RETURN true;
onError(err)
  p_ErrString = "Ошибка получения данных";
  IF( p_DialogFlag == 1 )
    RemProgress;
  END;
  RETURN false;
END; // GetDataSet

/*  Процедура экспорта файла-сверки
*/
PRIVATE MACRO ExportEntFile( p_SourceFile :string, p_Date: date, p_ErrString :@string ) :bool
  // DEF-71583 Возвращает имя файла в виде: Каталог(по параметру) дата + count(utableprocessevent_dbt) для ObjType 
  VAR 
    x_ExportPath = GetExportPath( "РСХБ\\ИНТЕГРАЦИЯ\\ДИРЕКТОРИИ\\EXPORT\\306ACCCOMPAREDIR", p_Date, 5030 )
    , x_eMailProcessor
  ;
  
  //копирование в ресурс из реестра
  RemoveFile(x_ExportPath);

  IF( not CopyFile( p_SourceFile, x_ExportPath ) )
    p_ErrString = "Ошибка копирования файла  " + x_ExportPath;
  ELSE
    //отправляем в почту
    x_eMailProcessor = Null;
    x_eMailProcessor = c_email_proc_env();
    x_eMailProcessor.m_set_msg_head("СОФР - Отчет результат сверки зачислений загруженных из ЦФТ в СОФР.");
    x_eMailProcessor.m_add_row_to_msg_text("Результат сверки зачислений загруженных из ЦФТ в СОФР в файле " + x_ExportPath + ". Приложен.");
    x_eMailProcessor.m_add_attach_to_list( x_ExportPath );
    x_eMailProcessor.m_save_to_submit_grp(CV_EMAIL_GRP_ETCOMPARE, true);
    x_eMailProcessor.m_submit_email_synch();
    x_eMailProcessor = Null;
    //отправляем в почту
  END;

  RETURN true;
OnError(er)
  RETURN false;
END; // ExportEntFile

/*  Отображение Excel-файла отчета-сверки
*/
PRIVATE MACRO ShowExcelBook( p_SourceFile :string, p_ErrString :@string ) :bool
  VAR 
     x_termFilePath = PathCombine2(GetCurDir(true),"txtfile", EXPORT_XML_FILENAME_TXT + g_CNum + "306" + ".xlsx")
     , x_Ok :bool = false
  ;

  IF( not CopyFile( p_SourceFile, "$" + x_termFilePath) )
    p_ErrString = "Ошибка копирования файла  " + p_SourceFile;
  ELIF( StartShellProgram("$" + x_termFilePath) )
    p_ErrString = "Ошибка отображения excel-файла";
  ELSE
    x_Ok = true;
  END;
  RETURN x_Ok;
OnError(er)
  RETURN false;
END; // ShowExcelBook

/*  Формирование отчета, возвращает количество записей в отчете
*/
PRIVATE MACRO MakeReport( 
   p_DataSet :RsdRecordset, p_ReqID :string, p_ParamProcess: object, p_DialogFlag :integer, p_fileSavePathExcel :@string, p_ErrString :@string 
) :integer

  VAR x_RepTmpl, x_TableRowCount = 0, x_TableObj;

  IF( p_DialogFlag == 1 )
    RemProgress;                                                    
    InitProgress(-1, "Формирование отчета.", "Формирование отчета");
  END;

  // шаблон в режиме POI
  x_RepTmpl = CTemplateSXLSX("Report_UEnt306Compare.xlsx");
  p_fileSavePathExcel = PathCombine(GetAbsoluteTxtPath(), EXPORT_XML_FILENAME_TXT + g_CNum + ".xlsx");

  x_RepTmpl.SetValue_NameCell("header_text","Отчет о расхождениях ПО НЕ ТОРГОВЫМ ПОРУЧЕНИЯМ МЕЖДУ ЦФТ и СОФР (зачисления на 306, выводы с 306, перевод между 306 счетами)");
  x_RepTmpl.SetValue_NameCell("req_text", "В ответ на запрос СОФР: " + p_ReqID);
  x_RepTmpl.SetValue_NameCell("period_text", 
                            "Период: с " + StrSubst( string(date(p_ParamProcess.DateIn):10), " ", "0" ) 
                            + " по " + StrSubst( string(date(p_ParamProcess.DateOut):10), " ", "0" )
  );

  x_TableObj = x_RepTmpl.RegisterTable("bodyline");

  WHILE (p_DataSet.MoveNext())
    // Данные ЦФТ
    x_TableObj.SetValueCell("bodyline_F01", SQL_ConvTypeDate(p_DataSet.value("t_cft_date_carry")));  // Дата проводки
    x_TableObj.SetValueCell("bodyline_F02", SQL_ConvTypeStr(p_DataSet.value("t_cft_doc_number")));	// Номер документа
    x_TableObj.SetValueCell("bodyline_F03", SQL_ConvTypeSum(p_DataSet.value("t_cft_sum")));		// Сумма в валюте счета
    x_TableObj.SetValueCell("bodyline_F04", SQL_ConvTypeStr(p_DataSet.value("t_cft_dbt_acc")));	// Счет дебета
    x_TableObj.SetValueCell("bodyline_F05", SQL_ConvTypeStr(p_DataSet.value("t_cft_dbt_cur")));	// Валюта счета дебета
    x_TableObj.SetValueCell("bodyline_F06", SQL_ConvTypeStr(p_DataSet.value("t_cft_crd_acc")));	// Счет кредита
    x_TableObj.SetValueCell("bodyline_F07", SQL_ConvTypeStr(p_DataSet.value("t_cft_crd_cur")));	// Валюта счета кредита
    x_TableObj.SetValueCell("bodyline_F08", SQL_ConvTypeStr(p_DataSet.value("t_cft_payer_acc")));	// Счет плательщика
    x_TableObj.SetValueCell("bodyline_F09", SQL_ConvTypeStr(p_DataSet.value("t_cft_receiver_acc")));	// Счет получателя
    x_TableObj.SetValueCell("bodyline_F10", SQL_ConvTypeStr(p_DataSet.value("t_cft_payer_name")));	// Наименование плательщика
    x_TableObj.SetValueCell("bodyline_F11", SQL_ConvTypeStr(p_DataSet.value("t_cft_receiver_name")));// Наименование получателя
    x_TableObj.SetValueCell("bodyline_F12", SQL_ConvTypeStr(p_DataSet.value("t_cft_pay_purpose")));	// Назначение платежа
    // Данные СОФР
    x_TableObj.SetValueCell("bodyline_S01", SQL_ConvTypeDate(p_DataSet.value("t_sofr_date_carry")));  // Дата проводки
    x_TableObj.SetValueCell("bodyline_S02", SQL_ConvTypeStr(p_DataSet.value("t_sofr_doc_number")));	// Номер документа
    x_TableObj.SetValueCell("bodyline_S03", SQL_ConvTypeSum(p_DataSet.value("t_sofr_sum")));		// Сумма в валюте счета
    x_TableObj.SetValueCell("bodyline_S04", SQL_ConvTypeStr(p_DataSet.value("t_sofr_dbt_acc")));	// Счет дебета
    x_TableObj.SetValueCell("bodyline_S05", SQL_ConvTypeStr(p_DataSet.value("t_sofr_dbt_cur")));	// Валюта счета дебета
    x_TableObj.SetValueCell("bodyline_S06", SQL_ConvTypeStr(p_DataSet.value("t_sofr_crd_acc")));	// Счет кредита
    x_TableObj.SetValueCell("bodyline_S06.1", SQL_ConvTypeStr(p_DataSet.value("t_sofr_receiver_acc")));	// Счет получателя платежа
    x_TableObj.SetValueCell("bodyline_S07", SQL_ConvTypeStr(p_DataSet.value("t_sofr_crd_cur")));	// Валюта счета кредита
    x_TableObj.SetValueCell("bodyline_S08", SQL_ConvTypeStr(p_DataSet.value("t_sofr_payer_name")));	// Наименование плательщика
    x_TableObj.SetValueCell("bodyline_S08.1", SQL_ConvTypeStr(p_DataSet.value("t_sofr_receiver_payname")));	// Наименование получателя платежа
    x_TableObj.SetValueCell("bodyline_S09", SQL_ConvTypeStr(p_DataSet.value("t_sofr_receiver_name")));// Наименование получателя
    x_TableObj.SetValueCell("bodyline_S10", SQL_ConvTypeStr(p_DataSet.value("t_sofr_ground")));	// Основание
    // Сверка
    x_TableObj.SetValueCell("bodyline_R1", SQL_ConvTypeStr(p_DataSet.value("t_check_pay")));		// Квитовка платежей
    x_TableObj.SetValueCell("bodyline_R2", SQL_ConvTypeStr(p_DataSet.value("t_check_cur")));		// Проверка соответствия валюты
    x_TableObj.SetValueCell("bodyline_R3", SQL_ConvTypeStr(p_DataSet.value("t_check_name")));	// Проверка соответствия ФИО в СОФР и ЦФТ
    x_TableObj.SetValueCell("bodyline_R4", SQL_ConvTypeStr(p_DataSet.value("t_check_acc")));		// Проверка корректности корреспонденции
    x_TableObj.SetValueCell("bodyline_R4.1", SQL_ConvTypeStr(p_DataSet.value("t_check_sum")));		// Проверка суммы проводки
    x_TableObj.SetValueCell("bodyline_R5", SQL_ConvTypeStr(p_DataSet.value("t_check_all")));		// Итог сверки
    x_TableObj.AddStr();
    x_TableRowCount = x_TableRowCount + 1;
    IF( p_DialogFlag == 1 )
      UseProgress(x_TableRowCount);
    END;
  END;
  x_TableObj.EndTable();
  x_RepTmpl.SetValue_NameCell("count_text", string(x_TableRowCount));

  IF (not x_RepTmpl.GetWorkbook().saveAs(p_fileSavePathExcel))
    p_ErrString = "Не удалось создать файл " + p_fileSavePathExcel;
  END;

  IF( p_DialogFlag == 1 )
    RemProgress;
  END;

  RETURN x_TableRowCount;

OnError(er)
  RETURN 0;
END; // MakeReport


/*  Отчет-сверка зачислений выгруженных из ЦФТ в СОФР 
*/
MACRO Ent306CompareRep( p_ReqID :string, p_ParamProcess: object, p_DialogFlag :integer ) :bool

  VAR x_DataSet :RsdRecordset, x_TableRowCount :integer = 0, x_ErrStr :string = "", x_FileSavePathExcel;

  IF( ( ValType(p_DialogFlag) == V_UNDEF ) or ( ValType(p_DialogFlag) == 26 ) )
    p_DialogFlag = 0;
  END;

  // получим курсор для отчета
  GetDataSet( @x_DataSet, p_ReqID, p_ParamProcess, p_DialogFlag, @x_ErrStr ); 

  // Формируем отчет
  IF( x_ErrStr == "" )
    x_TableRowCount = MakeReport(x_DataSet, p_ReqID, p_ParamProcess, p_DialogFlag, @x_FileSavePathExcel, @x_ErrStr); 
  END;

  // Производим выгрузку, если флаг выгрузки взведен
  IF( (x_ErrStr == "") AND (x_TableRowCount > 0) )
    ExportEntFile(x_FileSavePathExcel, p_ParamProcess.DateOut, @x_ErrStr); 
    IF( x_ErrStr != "" )
      // если есть ошибка в выгрузке, залогируем и продолжим
      AddDBLogDebug("Ent306CompareRep", x_ErrStr);
      x_ErrStr = "";
    END;
  END;

  // открываем пользователю локальную книгу
  IF( (x_ErrStr == "") AND (p_DialogFlag == 1) )
    ShowExcelBook( x_FileSavePathExcel, @x_ErrStr );
  END;

  // ошибку логируем
  IF( x_ErrStr != "" )
    AddDBLogDebug("Ent306CompareRep", x_ErrStr);
    RETURN false;
  END;

  RETURN true;

onError(err)
  VAR errortext = GetFullErrorMessage(err, 1000);
  IF (p_DialogFlag == 1)
    MsgBox(errortext);
  END;
  AddDBLogWithErrorObj("Ent306CompareRep", err);
  RETURN false;
END; // Ent306CompareRep
