/*
$Name:        LoadFVDeals.mac
$Module:      Интеграция с внешними системами
$Description: Загрузка в СОФР данных по Справедливой Стоимости по Сделкам
*/

/*-------------------------------------------*/
/* Автор: Гераськина Т.В.                    */
/*-------------------------------------------*/
import "func_lib.mac";
import "uws_exchng_log.mac";
import "u_common_email_utils.mac";
import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import rsd, DVInter;

{OperDprt} = 1;
FILE output_file() txt write;

private class c_TccParm_req
   var DealId     : string   /* (  1)  Номер сделки */
      ,Cpty       : string   /* (0-1)  CPTY контрагента */
      ,TradeDate  : date     /* (  1)  Дата сделки */
      ,ValueDate  : date     /* (0-1)  Дата валютирования сделки */
      ,Maturity   : date     /* (0-1)  Дата исполнения сделки */
      ,Settlement : date     /* (0-1)  Дата расчетов сделки */
      ,MarketValue: double;  /* (  1)  Значение ТСС */
end;

private class с_IdentityTcc_req
   var ActionType : string   /* (0-1) Тип действия */
      ,RateDateTcc: date;    /* (  1) Дата формирования ТСС*/
end;

private class c_TccParmAll_req
   var IdentityTcc: object /* (  1) Параметры с_IdentityTcc_req */
      ,TccList : object;   /* (1-n) Список СС TArray c_TccParm_req */
end;

private class c_LoadFVDeals_req()
   var ReqID    : string  /* (  1) ИД запроса */
      ,SenderId : string  /* (0-1) Код АС-источника сведений */
      ,TccParm  : object; /* (  1) Параметры СС c_TccParmAll_req */
end;

private class c_ResultData()
   var Code : String; 
   var Text;  
end;

private class c_Result_tcc
   var DealId : string /*(  1) Номер сделки*/ 
      ,Result : object; /* (1) c_ResultData */   
end;

private class c_LoadFVDeals_resp
   var ReqID    : string  /* (  1) ИД исходного запроса */
      ,SenderId : string  /* (0-1) Код АС-получателя ответа */
      ,Result   : object  /* (  1) Результат обработки запроса c_ResultData */
      ,TccList  : object; /* (0-n) Результат загрузки CC TArray c_Result_Tcc */
end;

private class c_out_obj
   var LoadFVDeals_resp = c_LoadFVDeals_resp;
end;

private class Logger()
   macro RepHeader(dt, tm)
      var str;
      str =     "                      ОТЧЕТ о загрузке СС для сделок ПФИ (LoadFVDeals) \n";
      str = str+"\n";
      str = str+"Дата загрузки  : "+dt+"\n";
      str = str+"Время загрузки : "+tm+"\n";
      str = str+"┌─┬──────────────────┬──────────────────┬────────────┬─────────────────────────────────────────────────────────────────────────────────────────┐\n";
      str = str+"│ │Сделка            │Справ. стоимость  │Дата СС     │Комментарий                                                                              │\n";
      str = str+"├─┼──────────────────┼──────────────────┼────────────┼─────────────────────────────────────────────────────────────────────────────────────────┤";
      //println(ToAnsi(str));
      output_file.str = str;
      insert(output_file);
   end;

   macro RepFooter(suc, er, tot)
      var str = "└─┴──────────────────┴──────────────────┴────────────┴─────────────────────────────────────────────────────────────────────────────────────────┘\n";
      str = str+"Загружено успешно: "+suc+"\n";
      str = str+"Не загружено     : "+er+"\n";
      str = str+"Всего            : "+tot+"\n";
      ///println(ToAnsi(str));
      output_file.str = str;
      insert(output_file);
   end;

   macro RepStr(_err,_code, _ss,_datess,_comm)
      var str = "│"+string(_err   :1)   +"│"+
                  string(_code    :18:l)+"│"+
                  string(_ss      :18:l)+"│"+
                  string(_datess  :12:l)+"│"+
                  string(_comm   ) ;
      //return println(ToAnsi(str));
      output_file.str = str;
      insert(output_file);
   end;
end;


/*-----------------------------*/
/* Адаптер типа c_TccParm_req */
/*-----------------------------*/
private class (c_TccParm_req) c_adp_Tccparm_req(p_income_data)
   private var vg_is_error;
   private var vg_error_code;
   private var vg_error_text;

   macro m_get_error_status()
      return vg_is_error;
   end;

   macro m_get_error_code()
      return vg_error_code;
   end;

   macro m_get_error_text()
      return vg_error_text;
   end;

   private macro m_add_text_to_err(p_src_text, p_add_text)
      private var v_ret = "";

      if(strlen(p_src_text) != 0)
         v_ret = string(p_src_text, "|", p_add_text);
      else
         v_ret = p_add_text;
      end;

      return v_ret;
   end; /* macro m_add_text_to_err() */

   private macro m_init_prime(p_income_data)
      vg_is_error = false;
      private var err_txt;
      private var nametag_up = "LoadFVDeals_req.TccList.Tcc";

      if(p_income_data != V_UNDEF)
         DealId      = getValueFromProperty(p_income_data, "DealId"     , nametag_up, true);
         Cpty        = getValueFromProperty(p_income_data, "Cpty"       , nametag_up);
         TradeDate   = getValueFromProperty(p_income_data, "TradeDate"  , nametag_up, true);
         ValueDate   = getValueFromProperty(p_income_data, "ValueDate"  , nametag_up);
         Maturity    = getValueFromProperty(p_income_data, "Maturity"   , nametag_up);
         Settlement  = getValueFromProperty(p_income_data, "Settlement" , nametag_up);
         MarketValue = getValueFromProperty(p_income_data, "MarketValue", nametag_up, true);
      end;

   onError(err)
      vg_is_error = true;
      vg_error_code = -1001;
      vg_error_text = m_add_text_to_err(vg_error_text,
                                        string("При обработке параметров котировки возникла ошибка: ",
                                               c_usr_err_obj.obtain_err_msg(err)));
   end;

   Initc_TccParm_req();

   m_init_prime(p_income_data);
end; /* class c_adp_Tccparm_req() */


/*---------------------------------*/
/* Функционал сохранения СС        */
/*---------------------------------*/
private class save_tcc(p_income_data, p_set_date)
   private var vg_is_error;
   private var vg_service_msg;
   private var vg_service_code;
   private var vg_dealid;
   private var vg_dealkind;
   private var vg_stepdate;
   private var vg_sumtcc;
   private var vg_cur;
   private var vg_fair;
   private var vg_stat;
   
   macro m_get_error_status()
      return vg_is_error;
   end;

   /* Получение сервисного сообщения */
   macro m_get_service_msg()
      return vg_service_msg;
   end;

   macro m_get_service_code()
      return vg_service_code;
   end;


   private macro CheckFrValOnDate(p_DealId, p_dockind, p_set_date):money
     var sql_str, rs, cmd;
     //var ret:money = 0; //нулевая СС тоже может быть
     var ret:money;

     sql_str = "SELECT f.t_fairvalue FROM ddvnfrval_dbt f "+
               " WHERE f.t_dealid = :dealid  AND f.t_date = :dt AND f.t_dockind = :dockind";
     cmd = RSDCommand(sql_str);
     cmd.AddParam("dealid", RSDBP_IN, p_DealId);
     cmd.AddParam("dt", RSDBP_IN, p_set_date);
     cmd.AddParam("dockind", RSDBP_IN, p_dockind);
     rs = RSDRecordSet(cmd);
     if (rs.MoveNext())
       ret = money(rs.value("t_fairvalue"));
     end;
     rs.close; cmd.close; rs = null; cmd = null;

     return ret;
   end;

   /* Подготовка данных для сохранения в БД */
   private macro m_prep_data(p_income_data, p_set_date)
      var sql_str, cmd, rs;
      
      sql_str = "select t_id, t_dockind, t_state from ddvndeal_dbt t where t_code=:dealcode";
      cmd = RSDCommand(sql_str);
      cmd.AddParam("dealcode", RSDBP_IN, p_income_data.DealId);
      rs = RSDRecordSet(cmd);
      if (rs.movenext)
         vg_dealid = rs.value("t_id");
         vg_dealkind = rs.value("t_dockind");
         vg_stat = rs.value("t_state");
      end;
      
      if (vg_dealid < 0) 
         vg_is_error = true;
         vg_service_msg = "Сделка не найдена: "+p_income_data.DealId;
         vg_service_code = -1001;
         RunError(vg_service_msg, c_err_obj(vg_service_msg,vg_service_code));
      end;

      if (vg_stat == 2) 
         vg_is_error = true;
         vg_service_msg = "Сделка закрыта: "+p_income_data.DealId;
         vg_service_code = -1001;
         RunError(vg_service_msg, c_err_obj(vg_service_msg,vg_service_code));
      end;

      vg_stepdate = p_set_date;
      vg_sumtcc = round(Money(p_income_data.MarketValue),2); // округление???
      vg_cur = 0; // только нац.валюта
      
      vg_fair = CheckFrValOnDate(vg_dealid, vg_dealkind, p_set_date);
      //if (vg_fair != 0) // нулевая СС тоже может быть
      if (ValType(vg_fair) != V_UNDEF) // если СС найдена
         vg_is_error = true;
         vg_service_msg = "У cделки "+p_income_data.DealId+" на "+p_set_date+" СС = "+vg_fair+". Передано СС = "+vg_sumtcc;
         vg_service_code = -1001;
         RunError(vg_service_msg, c_err_obj(vg_service_msg,vg_service_code));
      end;
     
   end; /* macro m_prep_data */
   
   /* Сохранение данных */
   private macro m_save_data()
      var stat, errmsg;
   
/*:для окончательной картины. Вам потом вызывать функцию, сделанную по запросу 266221:
Engineering Note   [19.02.2019 10:35:13(Anchugov)]: 
  
  	Функция в DVInter: DV_InsertFairValue
Параметры:
   - ID сделки
   - дата шага
   - сумма СС
   - валюта (0 - NATCUR - для рубля, попытка вызова с другой валютой приведет к ошибке)

Метод у класса первичного документа сделки (DVFirstDocNDeal): InsertFairValue
Параметры:
   - дата шага
   - сумма СС
   - валюта (0 - NATCUR - для рубля, попытка вызова с другой валютой приведет к ошибке)

Функция интера и метод ПД возвращают True в случае успеха, и False при ошибке. 

Запрос 291229, включён в 70-й патч.

Engineering Note   [07.04.2021 9:28:06(Allenov)]: 
  
  	Добавил новый параметр(TRUE/FALSE), включить безынтерфейсный режим:
DV_InsertFairValue(25, 199, date(16,12,2022), Money(123) ,0, TRUE);
по умолчанию FALSE

294351   
Engineering Note   [05.08.2021 14:20:48(KuznetsovP)]:  
   
   изменения:
возвращаемое значение 
  0 - успех
  код ошибки при неудаче

добавил 7ой параметр (номер 6 если с 0)
  в него возвращается сообщение обошибке 

var errmsg;
var stat;
stat = DV_InsertFairValue(25, 199, date(16,12,2022), Money(123) ,0, TRUE, errmsg);  


*/   
   
      stat = DV_InsertFairValue(vg_dealid, vg_dealkind, vg_stepdate, vg_sumtcc, vg_cur, false, errmsg);
      if (stat > 0)
         // текст ошибки ???
         vg_is_error = true;
         vg_service_msg = errmsg/*GetErrMessage(stat)*/;
         vg_service_code = -1*stat;
         RunError(vg_service_msg, c_err_obj(vg_service_msg,vg_service_code));
      end;
   end;

   /* Конструктор */
   private macro m_init_prime(p_income_data, p_set_date)
      vg_is_error = false;
      vg_service_msg = "";
      vg_dealid = -1;
      vg_dealkind = -1;
      vg_fair = $0;

      if(ValType(p_income_data) != V_UNDEF)
         m_prep_data(p_income_data, p_set_date);

         m_save_data();
      end;

   onError(err)
      vg_is_error = true;
      vg_service_msg = c_usr_err_obj.obtain_err_msg(err);
      vg_service_code = c_usr_err_obj.obtain_err_code(err);
   end; /* macro m_init_prime() */

   m_init_prime(p_income_data, p_set_date);
end; 


/*---------------------------------------------------*/
/* Класс с реализацией основного функционала сервиса */
/*---------------------------------------------------*/
private class (c_LoadFVDeals_req) c_LoadFVDeals_req_proc(p_income_data)
   /* Признак наличия ошибок */
   private var vg_is_error;
   /* Статусы для структуры верхнего уровня */
   private var vg_hl_error;
   /* Список зарегистрированных статусов для структур нижнего уровня */
   private var vg_ll_error_list;

   /* Метод получения признака наличия ошибок */
   macro m_get_error_status()
      return vg_is_error;
   end;

   /* Метод получения статуса для структуры верхнего уровня */
   macro m_get_hl_error()
      return vg_hl_error;
   end;
   
   /* Метод получения кода статуса для структур нижнего уровня */
   macro m_get_ll_error_code(p_index)
      return vg_ll_error_list.value(p_index).Code;
   end;

   /* Метод получения текста статуса для структур нижнего уровня */
   macro m_get_ll_error_text(p_index)
      return vg_ll_error_list.value(p_index).Text;
   end;   

   private macro m_add_text_to_err(p_src_text, p_add_text)
      private var v_ret = "";

      if(strlen(p_src_text) != 0)
         v_ret = string(p_src_text, "|", p_add_text);
      else
         v_ret = p_add_text;
      end;

      return v_ret;
   end; /* macro m_add_text_to_err() */

   /* Конструктор */
   private macro m_init_prime(p_income_data)
      private var v_aux_buff, v_aux_buff2, v_aux_buff3;
      private var v_aux_cntr;

      private var err_txt;
      private var nametag_up = "LoadFVDeals_req";
      private var v_size = 0;
      private var temp_tss;

      vg_is_error = false;
      vg_hl_error = c_ResultData;
      vg_hl_error.Code = 0; /* Значение по умолчанию - ошибок нет */
      vg_hl_error.Text = "OK"; 
      vg_ll_error_list = TArray;

      if(p_income_data != V_UNDEF)
         ReqID    = getValueFromProperty(p_income_data, "ReqID" , nametag_up);
         SenderId = getValueFromProperty(p_income_data, "SenderId" , nametag_up);
         v_aux_buff = getValueFromProperty(p_income_data, "TccParm" , nametag_up, true);
         if(ValType(v_aux_buff) == V_GENOBJ)
             TccParm  = c_TccParmAll_req;
             nametag_up = nametag_up+".TccParm";
             v_aux_buff2 = getValueFromProperty(v_aux_buff, "IdentityTcc" , nametag_up, true);
             if(ValType(v_aux_buff2) == V_GENOBJ)
                TccParm.IdentityTcc = с_IdentityTcc_req;
                nametag_up = nametag_up+".IdentityTcc";
                TccParm.IdentityTcc.ActionType = getValueFromProperty(v_aux_buff2, "ActionType" , nametag_up);
                TccParm.IdentityTcc.RateDateTcc = getValueFromProperty(v_aux_buff2, "RateDateTcc" , nametag_up, true);
             elif(ValType(v_aux_buff2) == V_UNDEF)
                vg_is_error = true;
                vg_hl_error.Code = -1001;
                vg_hl_error.Text = m_add_text_to_err(vg_hl_error.Text, string(InErrComPart, "IdentityTcc"));
             else
                vg_is_error = true;
                vg_hl_error.Code = -1001;
                vg_hl_error.Text = m_add_text_to_err(vg_hl_error.Text, string(InErrNotObj, "IdentityTcc"));
             end;
             v_aux_buff2 = null;

             nametag_up = "LoadFVDeals_req.TccParm";
             v_aux_buff2 = getValueFromProperty(v_aux_buff, "TccList" , nametag_up, true);

             if(ValType(v_aux_buff2) == V_GENOBJ)
                TccParm.TccList = TArray;
                v_aux_cntr = 0;

                v_size = v_aux_buff2.size;
                temp_tss = null;
                while(v_size > v_aux_cntr)
                   vg_ll_error_list.value(v_aux_cntr) = c_ResultData;
                   vg_ll_error_list.value(v_aux_cntr).Code = 0;
                   
                   v_aux_buff3 = ValType(v_aux_buff2.value(v_aux_cntr));
                   if(v_aux_buff3 == V_GENOBJ)
                      v_aux_buff3 = v_aux_buff2.value(v_aux_cntr);
                      temp_tss = c_adp_Tccparm_req(v_aux_buff3);
                      TccParm.TccList.value(v_aux_cntr) = temp_tss;
                      /* Сохраняем ошибки, зафиксированные при обработке параметров котировки */
                      if(temp_tss.m_get_error_status())
                         vg_ll_error_list.value(v_aux_cntr).Code = TccParm.TccList.value(v_aux_cntr).m_get_error_code();
                         vg_ll_error_list.value(v_aux_cntr).Text = TccParm.TccList.value(v_aux_cntr).m_get_error_text();
                      end;     
                      temp_tss = null;
                   else
                      vg_ll_error_list.value(v_aux_cntr).Code = -1001;
                      vg_ll_error_list.value(v_aux_cntr).Text = string(InErrNotObj, "элемент списка TccList");
                   end;       
                   v_aux_buff3 = null;

                   v_aux_cntr = v_aux_cntr + 1;
                end;

             elif(ValType(v_aux_buff2) == V_UNDEF)
                vg_is_error = true;
                vg_hl_error.Code = -1001;
                vg_hl_error.Text = m_add_text_to_err(vg_hl_error.Text, string(InErrComPart, "TccList"));

             else
                vg_is_error = true;
                vg_hl_error.Code = -1001;
                vg_hl_error.Text = m_add_text_to_err(vg_hl_error.Text, string(InErrNotObj, "TccList"));
             end;
             v_aux_buff2 = null;
          elif(ValType(v_aux_buff) == V_UNDEF)
             vg_is_error = true;
             vg_hl_error.Code = -1001;
             vg_hl_error.Text = m_add_text_to_err(vg_hl_error.Text, string(InErrComPart, "TccParm"));

          else
             vg_is_error = true;
             vg_hl_error.Code = -1001;
             vg_hl_error.Text = m_add_text_to_err(vg_hl_error.Text, string(InErrNotObj, "TccParm"));
          end;
          v_aux_buff = null;
      end;

   onError(err)
      /* Записываем в ошибку структуры верхнего уровня структуры */
      vg_is_error = true;
      vg_hl_error.Code = -1001;
      vg_hl_error.Text = m_add_text_to_err(vg_hl_error.Text,
                                           string("При обработке входных параметров возникла ошибка: ",
                                                  c_usr_err_obj.obtain_err_msg(err)));
   end;

   Initc_LoadFVDeals_req();

   m_init_prime(p_income_data);
end; /* class c_LoadFVDeals_req_proc() */


/*-------------*/
/* ТОЧКА ВХОДА */
/*-------------*/
macro LoadFVDeals(p_income_data)
   private var v_aux_cntr;
   private var v_income_data = NULL;
   var v_email_processor = NULL;
   var v_transformator = NULL;
   var v_service_processor = NULL;
   var v_save_processor = NULL;
   var v_resp_obj = NULL;
   var v_resp_doc;
   var cur_dt = StrSubSt(string(Date()),".","")+"_"+StrSubSt(string(Time()),":","");
   
   var CV_EMAIL_HEAD = "ОТЧЕТ о загрузке СС для сделок ПФИ (LoadFVDeals) "+cur_dt;
   var CV_EMAIL_RECEIVER = "";
   var CV_CONTRACT_EMAIL_GRP = 35;
   var CV_EMAIL_ROW_HEAD = "";
   var all_tss = 0,suc_tss = 0, err_tss = 0;

   private var v_logger_obj;    /* Объект для работы с журналом */
   private var v_log_id;        /* Идентификатор записи в журнале (если требуется что-то добавить) */

   private var InfoLogFileName = "..\\TxtFile\\load_ss_" + cur_dt + ".txt" ;
   private var log = Logger();
   
   var PATH_LOADFVDEALS;
   var REG_PATH_LOADFVDEALS = "РСХБ\\ДИРЕКТОРИИ\\ОТЧЕТ_LOADFVDEALS";
   var ErrCode;
   GetRegistryValue(REG_PATH_LOADFVDEALS, V_STRING, PATH_LOADFVDEALS, ErrCode);
   if( ( ErrCode > 0 ) or (not PATH_LOADFVDEALS) )
      PATH_LOADFVDEALS = "..\\TxtFile\\";
   end;
        
   Open(output_file, InfoLogFileName, "rsansi");
   
   log.RepHeader(date(),time());
          
   v_resp_obj = c_out_obj;

   /* Инициализация письма */
   v_email_processor = c_email_proc_env;
   v_email_processor.m_set_msg_head(CV_EMAIL_HEAD);
   /* !!! - Необходимо где-то брать список адресов для отправки уведомлений - !!! */
   //v_email_processor.m_add_email_to_list(CV_EMAIL_RECEIVER);
   v_email_processor.m_add_row_to_msg_text(CV_EMAIL_HEAD);

   if(ValType(p_income_data) != V_UNDEF)
      /* Преобразуем запрос в объектный вид - begin */
      v_transformator = c_secur_msg_converter();
      if(not v_transformator.m_decode_escaped_char(p_income_data))
         RunError(v_transformator.get_service_msg(), c_usr_err_obj(v_transformator.get_service_msg()));
      end;
      if(not v_transformator.m_make_pure_msg_from_ifx(v_transformator.get_pure_msg()))
         RunError(v_transformator.get_service_msg(), c_usr_err_obj(v_transformator.get_service_msg()));
      end;
      v_logger_obj = uws_exchng_logger(null, null, XmlRpcRequestId(), "LoadFVDeals", modulename(), NULL, v_transformator.get_pure_msg());
      v_log_id = v_logger_obj.get_log_id();

      v_income_data = v_transformator.m_secur_internal_req(v_transformator.get_pure_msg());
      /* Преобразуем запрос в объектный вид - end */

      v_service_processor = c_LoadFVDeals_req_proc(v_income_data);

      /* Общие параметры структуры верхнего уровня */
      v_resp_obj.LoadFVDeals_resp.ReqID    = v_service_processor.ReqID;
      v_resp_obj.LoadFVDeals_resp.SenderId = v_service_processor.SenderId;
      v_resp_obj.LoadFVDeals_resp.Result   = v_service_processor.m_get_hl_error();

      if(not v_service_processor.m_get_error_status())
         v_resp_obj.LoadFVDeals_resp.TccList = TArray;

         v_aux_cntr = 0;
         /* Обработка параметров котировок */
         while(v_service_processor.TccParm.TccList.size > v_aux_cntr)
            /* Общие параметры для СС */
            v_resp_obj.LoadFVDeals_resp.TccList.value(v_aux_cntr) = c_Result_Tcc;
            v_resp_obj.LoadFVDeals_resp.TccList.value(v_aux_cntr).DealId    = v_service_processor.TccParm.TccList.value(v_aux_cntr).DealId;
            v_resp_obj.LoadFVDeals_resp.TccList.value(v_aux_cntr).Result    = c_ResultData;
         
            /* Проверка наличия ошибок на этапе получения параметров СС */
            if(v_service_processor.m_get_ll_error_code(v_aux_cntr) == 0)
               v_save_processor = save_tcc(v_service_processor.TccParm.TccList.value(v_aux_cntr),
                                           v_service_processor.TccParm.IdentityTcc.RateDateTcc);

               if(not v_save_processor.m_get_error_status())
                  /* СС сохранена успешно */
                  v_resp_obj.LoadFVDeals_resp.TccList.value(v_aux_cntr).Result.Code = 0;
                  v_resp_obj.LoadFVDeals_resp.TccList.value(v_aux_cntr).Result.Text = "OK";
                  suc_tss = suc_tss + 1;
               else
                  /* При сохранении СС возникли проблемы */
                  v_resp_obj.LoadFVDeals_resp.TccList.value(v_aux_cntr).Result.Code = v_save_processor.m_get_service_code();
                  v_resp_obj.LoadFVDeals_resp.TccList.value(v_aux_cntr).Result.Text = v_save_processor.m_get_service_msg();
                  
                  /*v_email_processor.m_add_row_to_msg_text(string(CV_EMAIL_ROW_HEAD,
                                                                 "(", (v_aux_cntr + 1), ") ",
                                                                 v_resp_obj.LoadFVDeals_resp.TccList.value(v_aux_cntr).DealId, " - ",
                                                                 v_resp_obj.LoadFVDeals_resp.TccList.value(v_aux_cntr).Result.Text));*/
                  log.RepStr("X",v_service_processor.TccParm.TccList.value(v_aux_cntr).DealId, 
                                 v_service_processor.TccParm.TccList.value(v_aux_cntr).MarketValue, 
                                 v_service_processor.TccParm.IdentityTcc.RateDateTcc,
                                 v_resp_obj.LoadFVDeals_resp.TccList.value(v_aux_cntr).Result.Text);
                  err_tss = err_tss + 1;
               end;

            else
               /* При получении параметров возникли проблемы */
               v_resp_obj.LoadFVDeals_resp.TccList.value(v_aux_cntr).Result.Code = v_service_processor.m_get_ll_error_code(v_aux_cntr);
               v_resp_obj.LoadFVDeals_resp.TccList.value(v_aux_cntr).Result.Text = v_service_processor.m_get_ll_error_text(v_aux_cntr);
                                 
               /*v_email_processor.m_add_row_to_msg_text(string(CV_EMAIL_ROW_HEAD,
                                                              "(", (v_aux_cntr + 1), ") ",
                                                              v_resp_obj.LoadFVDeals_resp.ReqID, " - ",
                                                              v_resp_obj.LoadFVDeals_resp.TccList.value(v_aux_cntr).Result.Text));*/
               log.RepStr("X",v_resp_obj.LoadFVDeals_resp.ReqID, 
                              " - ", 
                              v_service_processor.TccParm.IdentityTcc.RateDateTcc,
                              v_resp_obj.LoadFVDeals_resp.TccList.value(v_aux_cntr).Result.Text);
               err_tss = err_tss + 1;
            end;

            v_aux_cntr = v_aux_cntr + 1;
            all_tss = all_tss + 1;
         end;
      else
         /* Зафиксированы ошибки на верхнем уровне структуры */
         v_resp_obj.LoadFVDeals_resp.TccList = TArray;
      end;

   else
      /* Ошибка в структуру верхнего уровня - не переданы входные данные */
      v_resp_obj.LoadFVDeals_resp.ReqID       = XmlRpcRequestId(); /* Поскольку не можем взять информацию из входящего объекта */
      v_resp_obj.LoadFVDeals_resp.Result      = c_ResultData;
      v_resp_obj.LoadFVDeals_resp.Result.Code = -1001;
      v_resp_obj.LoadFVDeals_resp.Result.Text = "Не переданы данные СС";
      v_resp_obj.LoadFVDeals_resp.TccList     = TArray;
      /*v_email_processor.m_add_row_to_msg_text(string(CV_EMAIL_ROW_HEAD,
                                              v_resp_obj.LoadFVDeals_resp.ReqID, " - ",
                                              v_resp_obj.LoadFVDeals_resp.Result.Text));*/
   end;

   log.RepFooter(suc_tss,err_tss,all_tss);
   Close(output_file);
  
   /* Сохраняем текст письма для отправки плановой процедурой */
   //v_email_processor.m_save_to_submit();
   /* Отправляем все не отправленные письма */
//   v_email_processor.m_submit_email();

   v_email_processor.m_add_attach_to_list(InfoLogFileName);
   v_email_processor.m_save_to_submit_grp(CV_CONTRACT_EMAIL_GRP, true);
   v_email_processor.m_submit_email_synch();

   var DL = TDirList();
   DL.Copy(InfoLogFileName, "", PATH_LOADFVDEALS, true, false, ""); 

   /* Преобразуем объектный вид ответа в документ требуемого формата - begin */
   v_resp_doc = v_transformator.m_secur_internal_resp(v_resp_obj);
   /* Преобразуем объектный вид ответа в документ требуемого формата - end */

   v_logger_obj.add_response(v_resp_doc, null, null, null, modulename, null);
   v_logger_obj.add_error_info(v_resp_obj.LoadFVDeals_resp.Result.Code, v_resp_obj.LoadFVDeals_resp.Result.Text);

   return v_resp_doc;

onError(err)
   if(ValType(v_resp_obj.LoadFVDeals_resp.ReqID) == V_UNDEF)
      v_resp_obj.LoadFVDeals_resp.ReqID = XmlRpcRequestId();
   end;
   v_resp_obj.LoadFVDeals_resp.Result      = NULL;
   v_resp_obj.LoadFVDeals_resp.Result      = c_ResultData;
   v_resp_obj.LoadFVDeals_resp.Result.Code = c_usr_err_obj.obtain_err_code(err);
   v_resp_obj.LoadFVDeals_resp.Result.Text = c_usr_err_obj.obtain_err_msg(err);

   /* Преобразуем объектный вид ответа в документ требуемого формата - begin */
   v_resp_doc = v_transformator.m_secur_internal_resp(v_resp_obj);
   /* Преобразуем объектный вид ответа в документ требуемого формата - end */

   v_logger_obj.add_response(v_resp_doc, null, null, null, modulename, null);
   v_logger_obj.add_error_info(v_resp_obj.LoadFVDeals_resp.Result.Code, v_resp_obj.LoadFVDeals_resp.Result.Text);

   return v_resp_doc;   

end;