/*---------------------------------------------------------*/
/* Инструментарий для работы с пользовательскими таблицами */
/*                                                         */
/* Автор: Карпов В.                                        */
/*---------------------------------------------------------*/

import rsd;
//import serviceaction;
import "global_utils_intgr.mac";


class c_usr_tbl_base(p_table_name)
   var new_val_obj;
   var where_val_obj;
   private var returning_prm_list;
   private var table_name;

   private var name_list_main;
   private var value_list_main;
   private var name_list_where;
   private var value_list_where;

   private var sttmnt_ins_names;
   private var sttmnt_ins_params;
   private var sttmnt_upd_set;
   private var sttmnt_where;
   private var sttmnt_ret_names;
   private var sttmnt_ret_params;

   private var service_msg;

   private const C_PRM_ALIAS = "p_val_";
   private const C_PRM_DELIM = ", ";
   private const C_STTMNT_INSERT = 1;
   private const C_STTMNT_UPDATE = 2;
   private const C_STTMNT_DELETE = 3;
   private const C_STTMNT_WHERE = 4;

   private macro init_base(p_table_name)
      service_msg = "";
      returning_prm_list = TArray();
      if(ValType(p_table_name) != V_UNDEF)
         table_name = string(p_table_name);
      end;
   end;

   macro add_returning_prm(p_fld_name)
      var ret = true;
      var before_size = returning_prm_list.size;

      if(GenPropID(new_val_obj, p_fld_name) != (-1))
         returning_prm_list.value(before_size) = p_fld_name;

         if(before_size != 0)
            sttmnt_ret_names = sttmnt_ret_names + C_PRM_DELIM;
            sttmnt_ret_params = sttmnt_ret_params + C_PRM_DELIM;
         else
            sttmnt_ret_names = "";
            sttmnt_ret_params = "";
         end;
         sttmnt_ret_names = sttmnt_ret_names + p_fld_name;
         sttmnt_ret_params = string(sttmnt_ret_params, ":", C_PRM_ALIAS, "3_", before_size);
      else
         ret = false;
         service_msg = "Ошибка при добавлении возвращаемого значения";
      end;

      return ret;

      onError(err)
         ret = false;
         service_msg = c_usr_err_obj.obtain_err_msg(err);
         return ret;
   end;

   macro reset_returning_prm()
      returning_prm_list.size = 0;
   end;

   private macro make_sql_with_returning(p_sql_str)
      var ret = "";
      var sql_ret1 = " RETURNING ";
      var sql_ret2 = " INTO ";

      if(returning_prm_list.size != 0)
         ret = p_sql_str + sql_ret1 + sttmnt_ret_names + sql_ret2 + sttmnt_ret_params;
      else
         ret = p_sql_str;
      end;

      return ret;
   end;

   macro get_service_msg()
      return service_msg;
   end;

   private macro get_table_name()
      return table_name;
   end;

   private macro get_obj_type(p_fld_name)
      var ret;

      ret = ValType(new_val_obj(GenPropID(new_val_obj, p_fld_name)));

      return ret;
   end;

   private macro set_returned_value(p_fld_name, p_ret_val)
      new_val_obj(GenPropID(new_val_obj, p_fld_name)) = p_ret_val;
   end;

   private macro get_obj_value(p_fld_name, p_sttmnt_type)
      var ret;

      if(ValType(p_sttmnt_type) == V_UNDEF)
         p_sttmnt_type = 0;
      end;

      if(p_sttmnt_type == C_STTMNT_WHERE)
         ret = where_val_obj(GenPropID(where_val_obj, p_fld_name));
      else
         ret = new_val_obj(GenPropID(new_val_obj, p_fld_name));
      end;

      return ret;
   end;

   private macro make_name_list()
      var ret = true;

      name_list_main = TArray(GetObjProps(new_val_obj));
      name_list_where = TArray(GetObjProps(where_val_obj));

      return ret;

      onError(err)
         ret = false;
         service_msg = c_usr_err_obj.obtain_err_msg(err);
         return ret;
   end;

   private macro make_prm_list(p_sttmnt_type)
      var ret = true;
      var cntr_of_list;
      if(not make_name_list())
         RunError("Формирование списков полей",
                  c_usr_err_obj("Ошибка при формировании списка полей таблицы"));
      end;

      sttmnt_ins_names = "";
      sttmnt_ins_params = "";
      sttmnt_upd_set = "";
      sttmnt_where = "";
      value_list_main = TArray;
      value_list_where = TArray;
      if(p_sttmnt_type != C_STTMNT_DELETE)
         cntr_of_list = 0;
         while(name_list_main.size > cntr_of_list)
            if(ValType(get_obj_value(name_list_main.value(cntr_of_list))) != V_UNDEF)
               value_list_main.value(value_list_main.size) = get_obj_value(name_list_main.value(cntr_of_list));
               if(value_list_main.size != 1)
                  if(p_sttmnt_type == C_STTMNT_INSERT)
                     sttmnt_ins_names = sttmnt_ins_names + C_PRM_DELIM;
                     sttmnt_ins_params = sttmnt_ins_params + C_PRM_DELIM;
                  elif(p_sttmnt_type == C_STTMNT_UPDATE)
                     sttmnt_upd_set = sttmnt_upd_set + C_PRM_DELIM;
                  end;
               end;

               if(p_sttmnt_type == C_STTMNT_INSERT)
                  sttmnt_ins_names = sttmnt_ins_names + name_list_main.value(cntr_of_list);
                  sttmnt_ins_params = sttmnt_ins_params + string(":", C_PRM_ALIAS, "1_", cntr_of_list);
               elif(p_sttmnt_type == C_STTMNT_UPDATE)
                  sttmnt_upd_set = sttmnt_upd_set + string(name_list_main.value(cntr_of_list),
                                                           " = :",
                                                           C_PRM_ALIAS,
                                                           "1_",
                                                           cntr_of_list);
               end;
            end;

            cntr_of_list = cntr_of_list + 1;
         end;
      end;

      if(p_sttmnt_type != C_STTMNT_INSERT)
         cntr_of_list = 0;
         while(name_list_where.size > cntr_of_list)
            if(ValType(get_obj_value(name_list_where.value(cntr_of_list), C_STTMNT_WHERE)) != V_UNDEF)
               value_list_where.value(value_list_where.size) = get_obj_value(name_list_where.value(cntr_of_list), C_STTMNT_WHERE);
               if(value_list_where.size != 1)
                  sttmnt_where = sttmnt_where + " AND ";
               end;

               sttmnt_where = sttmnt_where + string(name_list_where.value(cntr_of_list),
                                                    "= :",
                                                    C_PRM_ALIAS,
                                                    "2_",
                                                    cntr_of_list);
            end;

            cntr_of_list = cntr_of_list + 1;
         end;
      end;

      return ret;

      onError(err)
         ret = false;
         service_msg = c_usr_err_obj.obtain_err_msg(err);
         return ret;
   end;

   private macro where_prm_check()
      var ret = true;

      if(value_list_where.size == 0)
         ret = false;
      end;

      return ret;
   end;

   /* Вставка записи в таблицу */
   macro insert()
      var ret = true;
      var sql_beg1 = "INSERT INTO " + table_name + " (";
      var sql_beg2 = ")";
      var sql_end1 = " VALUES (";
      var sql_end2 = ")";
      var sql, cmd;
      var cntr_of_list;

      if(not make_prm_list(C_STTMNT_INSERT))
         RunError("Формирование запроса на вставку",
                  c_usr_err_obj("Ошибка при формировании запроса на вставку записи"));
      end;
      sql = string(sql_beg1, sttmnt_ins_names, sql_end2,
                   sql_end1, sttmnt_ins_params, sql_end2);
      sql = make_sql_with_returning(sql);

      cmd = RSDCommand(sql);

      cntr_of_list = 0;
      while(value_list_main.size > cntr_of_list)
         cmd.addParam(string(C_PRM_ALIAS, "1_", cntr_of_list), RSDBP_IN, value_list_main.value(cntr_of_list));
         cntr_of_list = cntr_of_list + 1;
      end;

      cntr_of_list = 0;
      while(returning_prm_list.size > cntr_of_list)
         cmd.addParam(string(C_PRM_ALIAS, "3_", cntr_of_list), RSDBP_OUT, get_obj_type(returning_prm_list.value(cntr_of_list)));
         cntr_of_list = cntr_of_list + 1;
      end;
      cmd.execute();

      cntr_of_list = 0;
      while(returning_prm_list.size > cntr_of_list)
         set_returned_value(returning_prm_list.value(cntr_of_list), cmd.value(string(C_PRM_ALIAS, "3_", cntr_of_list)));
         cntr_of_list = cntr_of_list + 1;
      end;

      cmd.close(); cmd = NULL;

      return ret;

      onError(err)
         ret = false;
         service_msg = c_usr_err_obj.obtain_err_msg(err);
         return ret;
   end;

   /* Обновление записи в таблице */
   macro update()
      var ret = true;
      var sql_beg = "UPDATE " + table_name + " SET ";
      var sql_end = " WHERE ";
      var sql, cmd;
      var cntr_of_list;

      if(not make_prm_list(C_STTMNT_UPDATE))
         RunError("Формирование запроса на обновление",
                  c_usr_err_obj("Ошибка при формировании запроса на обновление записи"));
      end;

      if(not where_prm_check())
         RunError("Формирование запроса на обновление",
                  c_usr_err_obj("Ошибка при формировании запроса на обновление записи: отсутствует условие отбора"));
      end;

      sql = string(sql_beg, sttmnt_upd_set, sql_end, sttmnt_where);
      sql = make_sql_with_returning(sql);

      cmd = RSDCommand(sql);

      cntr_of_list = 0;
      while(value_list_main.size > cntr_of_list)
         cmd.addParam(string(C_PRM_ALIAS, "1_", cntr_of_list), RSDBP_IN, value_list_main.value(cntr_of_list));
         cntr_of_list = cntr_of_list + 1;
      end;

      cntr_of_list = 0;
      while(value_list_where.size > cntr_of_list)
         cmd.addParam(string(C_PRM_ALIAS, "2_", cntr_of_list), RSDBP_IN, value_list_where.value(cntr_of_list));
         cntr_of_list = cntr_of_list + 1;
      end;

      cntr_of_list = 0;
      while(returning_prm_list.size > cntr_of_list)
         cmd.addParam(string(C_PRM_ALIAS, "3_", cntr_of_list), RSDBP_OUT, get_obj_type(returning_prm_list.value(cntr_of_list)));
         cntr_of_list = cntr_of_list + 1;
      end;
      cmd.execute();

      cntr_of_list = 0;
      while(returning_prm_list.size > cntr_of_list)
         set_returned_value(returning_prm_list.value(cntr_of_list), cmd.value(string(C_PRM_ALIAS, "3_", cntr_of_list)));
         cntr_of_list = cntr_of_list + 1;
      end;

      cmd.close(); cmd = NULL;

      return ret;

      onError(err)
         ret = false;
         service_msg = c_usr_err_obj.obtain_err_msg(err);
         return ret;
   end;

   /* Удаление записи из таблицы */
   macro delete()
      var ret = true;
      var sql_beg = "DELETE FROM " + table_name + " WHERE ";
      var sql, cmd;
      var cntr_of_list;

      if(not make_prm_list(C_STTMNT_DELETE))
         RunError("Формирование запроса на удаление",
                  c_usr_err_obj("Ошибка при формировании запроса на удаление записи"));
      end;

      if(not where_prm_check())
         RunError("Формирование запроса на удаление",
                  c_usr_err_obj("Ошибка при формировании запроса на удаление записи: отсутствует условие отбора"));
      end;

      sql = string(sql_beg, sttmnt_where);
      sql = make_sql_with_returning(sql);

      cmd = RSDCommand(sql);

      cntr_of_list = 0;
      while(value_list_where.size > cntr_of_list)
         cmd.addParam(string(C_PRM_ALIAS, "2_", cntr_of_list), RSDBP_IN, value_list_where.value(cntr_of_list));
         cntr_of_list = cntr_of_list + 1;
      end;

      cntr_of_list = 0;
      while(returning_prm_list.size > cntr_of_list)
         cmd.addParam(string(C_PRM_ALIAS, "3_", cntr_of_list), RSDBP_OUT, get_obj_type(returning_prm_list.value(cntr_of_list)));
         cntr_of_list = cntr_of_list + 1;
      end;
      cmd.execute();

      cntr_of_list = 0;
      while(returning_prm_list.size > cntr_of_list)
         set_returned_value(returning_prm_list.value(cntr_of_list), cmd.value(string(C_PRM_ALIAS, "3_", cntr_of_list)));
         cntr_of_list = cntr_of_list + 1;
      end;

      cmd.close(); cmd = NULL;

      return ret;

      onError(err)
         ret = false;
         service_msg = c_usr_err_obj.obtain_err_msg(err);
         return ret;
   end;

   init_base(p_table_name);
end;



/* Класс для работы с таблицей UREQFROMRETAIL_DBT */
/*для примера использования
class (c_usr_tbl_base) c_usr_tbl_ureqfromretail()
   private class gen_ureqfromretail()
      var T_RECORDID     : integer;
      var T_TIMESHIFT    : DateTime;
      var T_REQID        : string;
      var T_ORIGINALID   : string;
      var T_TYPEMESSAGE  : integer;
      var T_ORIGINALTEXT : string;
      var T_STATUS       : integer;
      var T_ERRORTEXT    : string;
      var T_SENDERID     : string;
      var T_OBJECTID     : string;
   end;

   private macro init_table_obj()
      new_val_obj = gen_ureqfromretail();
      where_val_obj = gen_ureqfromretail();
   end;

   Initc_usr_tbl_base("UREQFROMRETAIL_DBT");

   init_table_obj();
end;
*/