/*------------------------------------------------------------------------------------------------------
   MassUnloadInvestments.mac
   Формирование json сообщения для передачи в Свои Инвестии через kafka всех клиентов/клиентов из файла  
   Шишкин Е.В.
--------------------------------------------------------------------------------------------------------*/

/**
 @file dbo_qi_ExclusionNotification.mac
 @brief    Формирование json сообщения для передачи в Свои Инвестии через kafka  
 
 Выгрузка возможна либо всех клиентов, либо клиентов загруженных из файла
 
  # tag
 - functional_block:Клиенты_выгрузка
 - code_type:API
 - Свои инвестиции
 - kafka
 - карточка клиента

  # changeLog
 |date       |author         |tasks                                                     |note                                                        
 |-----------|---------------|----------------------------------------------------------|-------------------------------------------------------------
 |04.06.2024 |Шишкин Е.В.    |                   BOSS-1642                              | Создание
 */


import oralib, likepy, globals, rsexts,dlquery;

var IsNtr = isStandalone();

/*
@brief Функция получения имени файла из полного пути
@param[in] полный путь к файлу, включающий имя
@return имя файла
*/
private macro ExtractPath(NameFile)
  var sql;

  sql = execSqlSelect(" select substr(:p_NameFile,1,instr(:p_NameFile2,'\\',-1)) from dual",
                      makeArray (sqlParam ("p_NameFile", NameFile), sqlParam ("p_NameFile2", NameFile)));

  if ( sql.moveNext () )
     return string (sql.value (0));
  else
     return NameFile;
  end;

end;

/**
 @brief коррекция терминального имени для копирования
 @param[in] путь к фалу
 @return скорректированный путь к фалу
*/    
macro fnam4copy(p)
   if (not isNtr)
      if (substr(p, 1, 1) == "$")
         return p;
      end;
      return string("$", p);
   end;
   return p;
end;


/*
@brief Функция формирования шапки протокола
*/
private macro CreateHeader()

  [ ];
  [         Протокол ; ];
  [  +------------------------------------------+------------------------------------------------------------------------+];
  [  |      СОФР ID        |   ФИО клиента      |                                Ошибка                                  |];
  [  +------------------------------------------+------------------------------------------------------------------------+];

end;

/*
@brief Функция печати строки в протокол
@param[in] Partyid идентификатор клиента
@param[in] PartyName ФИО клиента
@param[in] ErrDesc текст ошибки выполнения процедуры
*/
private macro AddRow(Partyid, PartyName, ErrDesc)

      if((valtype(Partyid) == V_UNDEF) or (Partyid == null))
         Partyid = "";
      end;

      if((valtype(PartyName) == V_UNDEF) or (PartyName == null))
         PartyName = "";
      end;

      [  | ################### |################### |##############################################################          |](Partyid, PartyName, ErrDesc);
      [  |---------------------+--------------------+------------------------------------------------------------------------|];
end;

/*
@brief Запуск хранимой процедуры it_broker.Client_info, которая выгружает карточку клиента в свои инвестиции
@param[in] полный путь к файлу, включающий имя
@param[out] ErrCode код ошибки выполнения процедуры
@param[out] ErrDesc текст ошибки выполнения процедуры
*/

macro StartProc(p_Partyid,ErrDesc, ErrCode)

   var sql,rs,cmd;

   sql = "DECLARE "+
         "   v_ErrorCode number; "+
         "   v_ErrorDesc varchar2(2000); "+
         "   XML varchar(100) := '<XML action = \"UPD\" tab = \"DPARTY_DBT\" pkcol = \"T_PARTYID\" pk = \""+p_Partyid+"\"/>'; "+
         "BEGIN                       "+
         "   it_broker.Client_info(XML, v_ErrorCode, v_ErrorDesc); "+
         "   :p_ErrorCode := v_ErrorCode; "+
         "   :p_ErrorDesc := v_ErrorDesc; "+
         "END;  ";
    
   cmd = RSDCommand(sql);
   cmd.addParam("p_ErrorCode", RSDBP_OUT, V_INTEGER);
   cmd.addParam("p_ErrorDesc", RSDBP_OUT, V_STRING);
   cmd.execute();
  
   ErrDesc = cmd.value("p_ErrorDesc");
   ErrCode = cmd.value("p_ErrorCode");

   SetParm(1,ErrDesc);
   SetParm(2,ErrCode);

   cmd.close(); cmd = NULL; sql = NULL;

end;

/*
@brief Запуск sql loader для загрузки содержимого выбранного файла во временную таблицу uloadlistidinvest_dbt
@param[in] File_Name путь к выбранному файлу
@return bool результат выполнения функции true/false
*/

private macro LoadIdForListUnloadInvest( File_Name :string ) :bool
var DSN :string = "",
    USER_ID :string = "",
    PASSWORD :string = "",
    CONNSTRING :string = GetIniString("CONNSTRING", "rsreq.ini"),
    String_sqlldr :string = "",
    state :integer = 0,
    ldr_param :string = "bindsize=100000000 readsize=1000000 columnarrayrows=10000000 rows=100000 errors=100000 ",
    Query :string = "",
    Params :TArray = TArray();

private var 
            Load_Path,
            File_Name_CtrlFile = "", 
            Log_File_Name = "",
            Bad_File_Name = "",
            Dsc_File_Name = "";


  if( ( ValType(File_Name) == V_UNDEF ) or  ( ValType(File_Name) == 26 ) or ( File_Name == "" ) )
   return false;
  end;

  Query = " truncate table uloadlistidinvest_dbt";
  execSQL( Query );
  
  DSN = SubStr(CONNSTRING, 5, index(CONNSTRING,";") - 5);
  CONNSTRING = SubStr(CONNSTRING, index(CONNSTRING,";") + 1);
  USER_ID = SubStr(CONNSTRING, index(CONNSTRING,"user id=") + 8, index(CONNSTRING,";") - 9);
  CONNSTRING = SubStr(CONNSTRING, index(CONNSTRING,";") + 1);
  PASSWORD = SubStr(CONNSTRING, index(CONNSTRING,"password=") + 9);

  String_sqlldr = "userid=" + USER_ID + "/" + PASSWORD + "@" + DSN;

  File_Name_CtrlFile = ExtractPath(File_Name);

  Log_File_Name = File_Name_CtrlFile + "\CTL_" + string(UserNumber) + ".log";
  Bad_File_Name = File_Name_CtrlFile + "\CTL_" + string(UserNumber) + ".bad";
  Dsc_File_Name = File_Name_CtrlFile + "\CTL_" + string(UserNumber) + ".dsc";
  File_Name_CtrlFile = File_Name_CtrlFile + "\CTL_" + string(UserNumber) + ".ctl";
  
  SetOutput(File_Name_CtrlFile); 
  [LOAD DATA CHARACTERSET CL8MSWIN1251
   INFILE *
   APPEND
   INTO TABLE uloadlistidinvest_dbt
   Fields terminated by "|" 
   (
    T_PARTYID
   )
   ];
  SetOutput(Null, True); 
  Close(File_Name_CtrlFile);

  BegAction(Null, "Загружаем файл в СОФР...", False);
  
  state = Run("run_sqlldr.cmd", String_sqlldr + " control=" + File_Name_CtrlFile +
                                        " data="    + File_Name          +
                                        " log="     + Log_File_Name      +
                                        " bad="     + Bad_File_Name      +
                                        " discard=" + Dsc_File_Name      +
                                        " SKIP_UNUSABLE_INDEXES=true"    +
                                        " " + ldr_param);

  EndAction(10);
  
  if(state != -1)
    return true;
  else
    return false;
  end;

onError(err)
   return false;
end;

/*
@brief Функция копирования файлов с сервера на терминал/с терминала на сервер
@param[in] filepath путь к выбранному файлу
@param[in] copyServ True - копирование на сервер приложений, False - копирование на териминальник
@return string актуальный путь после копирования
*/
private macro SaveFile(filepath:string, copyServ:bool):string
    
    if(not IsNtr) 
      var file_name, file_ext, path_work;
      SplitFile(filepath, file_name, file_ext);  
      file_name = MergeFile("", file_name, + file_ext);
      
      if(copyServ) // когда мы на терминале, копируем файл на СП
         path_work = "..\\workfile\\" + file_name;
         if(not CopyFile(fnam4copy(filepath), path_work, true, "Копируется файл на сервер приложений..."))
            runerror("Ошибка копирования файла с терминала на сервер", "Ошибка копирования файла с терминала на сервер " + toOem(filepath));
         end;
         filepath = path_work;

      else // когда мы на терминале, копируем файл c севера на терминал
         path_work = GetCurDir(true) + "\\txtfile\\" + file_name;
         if(not CopyFile(filepath,fnam4copy(path_work), true, "Копируется файл на терминал..."))
            runerror("Ошибка копирования файла с сервера на терминал", "Ошибка копирования файла с сервера на терминал " + toOem(filepath));
         end;
         filepath = path_work;
      end;      
    end;
    
    return filepath;
end;


/*
@brief Запуск sql loader для загрузки содержимого выбранного файла во временную таблицу uloadlistidinvest_dbt
@param[in] File_Name путь к выбранному файлу
@return bool результат выполнения функции true/false
*/

private macro SelectAndOpenFile()

  var sql_u,sql,rs_u,rs,cmd_u,cmd;
  var Partyid, PartyName, stat, ErrDesc, ErrCode, dataExists:bool = False, errorExists:bool = False;
  FILE output_file() txt write;
  var filenameOutput:string, FullNameFile:string;

   
  /*Выбираем файл для обработки*/
  if(not SelectFile(FullNameFile, fnam4copy("\\..\\..\\import\\*.csv*"), "Выберите файл для загрузки", 0, true))
    MsgBox("Отказ от загрузки");
    return;
  end;
 

   //доставим загружаемый файл на сервер
  FullNameFile = SaveFile(FullNameFile, True);

  //загрузка partyid из файла в таблицу
  stat = LoadIdForListUnloadInvest(FullNameFile,1);

  filenameOutput = "Протокол_выгрузки_всех_клиентов_в_СИ_" + string(UserNumber) + ".txt";
  FullNameFile = "..\\txtfile\\"+filenameOutput;

  if(not Open(output_file, FullNameFile))
    var errmsg = "Ошибка при открытии файла протокола|" + FullNameFile;
    runerror(errmsg);
  end;
  
  SetOutput(FullNameFile); 
  CreateHeader();


  if(stat)
      //Ограничить вывод ID, тк int имеет пределы по значениям
     sql_u = " select t_partyid  from uloadlistidinvest_dbt where t_partyid < 2147483647 ";

     cmd_u = RSDCommand(sql_u);
     rs_u = RSDRecordSet(cmd_u);
      
     while (rs_u.moveNext())

        dataExists = True;
        Partyid = int(rs_u.value ("t_partyid"));
        StartProc(Partyid, @ErrDesc, @ErrCode);
                 
        if(ErrCode != 0)
           errorExists = True;
           sql = "select t_name from dparty_dbt where t_partyid = :p_partyid";
        
           cmd = RSDCommand(sql);
           cmd.addParam("p_Partyid", RSDBP_IN, Partyid);
           rs = RSDRecordSet(cmd);
        
           if(rs.movenext())
              PartyName = rs.value("t_name");
           end;  

            AddRow(Partyid, PartyName, ErrDesc);
        end;
     end;


  end;

  if(not dataExists)
      ErrDesc = "Нет данных, удовлетворяющих параметрам отчета";
      AddRow(Partyid,PartyName,ErrDesc);
  end;

  if(dataExists and not errorExists)
      Partyid = PartyName = null;
      ErrDesc = "Клиенты успешно выгружены";
      AddRow(Partyid,PartyName,ErrDesc);
  end;

  SetOutput(null, true);
  Close(output_file);

  //доставим загружаемый файл на терминал
  FullNameFile = SaveFile(FullNameFile, False);
  ViewFile(output_file); //Покажем файл отчёта на экран


end;


 
/*
@brief Основная функция загрузки данных из файла, запуск процедуры выгрузки карточки клиента, печать протокола выгрузки 
*/

private macro execMassUnloadInvest()

  var sql,rs,cmd,sql_p,rs_p,cmd_p,i=0,j=0,k=0;
  var ErrCode = 0, ErrDesc = "", PartyName = "", partyid;
  FILE output_file() txt write;
  var filename:string, FullNameFile:string;

  sql = " SELECT party.t_partyid FROM dparty_dbt party, dpartyown_dbt part "
        " WHERE party.t_partyid= part.t_partyid and part.t_partykind = 1 "+
        "     AND EXISTS (SELECT 1 FROM dpersn_dbt WHERE party.t_partyid = T_PERSONID and t_isemployer <> chr(88))";
  
  cmd = DL_RSDCommand(sql);

  var cnt = cmd.GetCount(sql);
  var DataSet = cmd.Execute();

  filename = "Протокол_выгрузки_всех_клиентов_в СИ.txt";
  FullNameFile = GetCurDir(true)+"\\txtfile\\"+filename;
  SetOutput(FullNameFile);

  if(not Open(output_file, FullNameFile))
    var errmsg = "Ошибка при открытии файла протокола|" + FullNameFile;
  end;

  CreateHeader();

  if(cnt >0)
    InitProgress(cnt, "Идет выгрузка всех клиентов в свои инвестиции", "Идет выгрузка всех клиентов в свои инвестиции");
  
    while(DataSet.movenext())
  
      Partyid = DataSet.value ("t_partyid");
      StartProc(Partyid, @ErrDesc, @ErrCode);
  
      if(ErrCode != 0)
  
        sql_p = "select t_name from dparty_dbt where t_partyid = :p_partyid";
      
        cmd_p = RSDCommand(sql_p);
        cmd_p.addParam("p_Partyid", RSDBP_IN, Partyid);
        rs_p = RSDRecordSet(cmd_p);
      
        if(rs_p.movenext())
           PartyName = rs_p.value("t_name");
        end;      
          j = j + 1;

          AddRow(Partyid, PartyName, ErrDesc);

      else
        k = k + 1;
      end;     
      UseProgress(i = i + 1);
    end;      
    RemProgress();

    SetOutput(null, true);
    Close(output_file);
    ViewFile(output_file); //Покажем файл отчёта на экран

  end;

  msgbox("Количество клиентов, выгруженных в топик кафки = "+k+" Количество клиентов с ошибкой выгрузки = "+j);

end;


/*
@brief Выбор способа выгрузки
*/

private macro execListUnloadInvest()

  var sheet = SelectAndOpenFile();

 if (valtype(sheet) == V_UNDEF) 
    return;
 end;

end;

if (Gettrue(false, "Необходимо ли загрузить файл со списком СОФР ID?"))
   execListUnloadInvest();
else 
   if (Gettrue(false, "Вы уверены, что хотите перевыгрузить всех клиентов из СОФР в ИС ""Свои инвестиции""?"))
      execMassUnloadInvest();     
   end;
end;

