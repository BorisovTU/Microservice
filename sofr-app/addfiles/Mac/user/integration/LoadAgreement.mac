/*----------------------------------------------------------*/
/* LoadAgreement.mac
/*                                                          */
   BIQ-5049. Загрузка CSA                                   */
/* Автор: Гераськина Т.                                     */
/*----------------------------------------------------------*/
import "func_lib.mac";
import "global_classes.mac";
import "secur_prc_msg_transform.mac";
import LoadCSA_func;
import "ws_impdeals.mac"; // для единого поиска ген.соглашений

var vg_error = "", vg_state = 0;

private class c_MainResult
   var Agreement_Id  : integer;
   var Agreement_Code: string;
   var Error = c_Error;
end;

// параметры ответа
private class c_CSAResponse
   var AgreementList = TArray;
end;

// параметры ответа
private class c_outObj()
   var LoadAgreement_resp = c_CSAResponse; // имя строго по названию тега
end;

private class c_AgreementRate(IncomeData)
   var Rate          : double;
   var CashRate_Code :string;
   var Basis         :string;
   var StartDate     :date;
   
   /* Кратность параметров в соответствии со спецификацией */
   private macro uInitPrime(IncomeData)   
      private var err_txt;
      var nametag_up = "AgreementList.Agreement.AgreementRate";
      
      Rate           = getValueFromProperty(IncomeData, "Rate"          , nametag_up);
      CashRate_Code  = getValueFromProperty(IncomeData, "CashRate_Code" , nametag_up);
      Basis          = getValueFromProperty(IncomeData, "Basis"         , nametag_up);
      StartDate      = getValueFromProperty(IncomeData, "StartDate"     , nametag_up);
   end;
   
   uInitPrime(IncomeData);
end;


private class (c_CSAFromFile) adp_CSAParm(IncomeData)

   Initc_CSAFromFile;

   /* Кратность параметров в соответствии со спецификацией */
   private macro uInitPrime(IncomeData)   
      private var err_txt;
      var nametag_up = "AgreementList.Agreement";
      var aux_buff;

      if(ValType(IncomeData) != V_UNDEF)
         Agreement_Id           = getValueFromProperty(IncomeData, "Agreement_Id"         , nametag_up, true);
         Agreement_Code         = getValueFromProperty(IncomeData, "Agreement_Code"       , nametag_up, true);
         Agreement_Name         = getValueFromProperty(IncomeData, "Agreement_Name"       , nametag_up, true);
         Agreement_Parent       = getValueFromProperty(IncomeData, "Agreement_Parent"     , nametag_up, true);
         Agreement_Name_Parent  = getValueFromProperty(IncomeData, "Agreement_Name_Parent", nametag_up);
         Cpty                   = getValueFromProperty(IncomeData, "Cpty"                 , nametag_up, true);
         ValidFrom              = getValueFromProperty(IncomeData, "ValidFrom"            , nametag_up);
         ValidTo                = getValueFromProperty(IncomeData, "ValidTo"              , nametag_up);
         Currency               = getValueFromProperty(IncomeData, "Currency"             , nametag_up, true);
         MarginAllowed          = getValueFromProperty(IncomeData, "MarginAllowed"        , nametag_up);
         Status                 = getValueFromProperty(IncomeData, "Status"               , nametag_up);
         
         aux_buff = getValueFromProperty(IncomeData, "Margin", nametag_up);
         if (ValType(aux_buff) == V_UNDEF)
         else
            Margin = c_Margin();
            Margin.uInitPrime(aux_buff);
            Margin.TransformFields();
         end;
         
         aux_buff = getValueFromProperty(IncomeData, "AgreementRate", nametag_up);
         if (ValType(aux_buff) == V_UNDEF)
         else
            AgreementRate = c_AgreementRate(aux_buff);
         end;

         if (ValType(ValidTo) == V_UNDEF)
            ValidTo = date(0,0,0);
         end;

      end;
   end;

   uInitPrime(IncomeData);
end;


// непосредственная обработка единичной записи
// исключения обрабатываются в основном классе 
macro RunProcessCSA(p_CSAParm) 
   var err_txt;
   var CSA_res = c_MainResult;
   var CSAParm = adp_CSAParm(p_CSAParm);
   var GenAgrClassic;
   var v_st;
      
   CSAParm.vPartyid = GetPartyID_byPartyCode(103, CSAParm.Cpty);
   if (CSAParm.vPartyid == -1)
      err_txt = "В СОФР отсутствует контрагент с кодом 103 = "+CSAParm.Cpty;
      RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
   end;
      
/*   CSAParm.vGenAgr = GetGenAgr(CSAParm.Agreement_Parent);
   if (CSAParm.vGenAgr.AgrId == -1)
      err_txt = "В СОФР отсутствует ген.соглашение с кодом = "+CSAParm.Agreement_Parent;
      RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));   
   end;*/

   GenAgrClassic = FindGenAgreementByParty(NULL, "", CSAParm.vPartyid, "PFI", 0);
   CSAParm.vGenAgr.AgrID = GenAgrClassic.AgrID;
   CSAParm.vGenAgr.Confirmmethod = GenAgrClassic.Confirmmethod;
   CSAParm.vGenAgr.Trans = GenAgrClassic.Trans;        

   CSAParm.vCur = GetFiidCodeByName(CSAParm.Currency);
      
   CSAParm.FindCSA();
      
   if (CSAParm.vCSAid > 0) // существующее
/*      err_txt = "CSA с кодом "+CSAParm.Agreement_Code+" уже существует";
      RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));*/
   else // новое
      v_st = CheckCSA_byPartyid(CSAParm.vPartyid, err_txt, CSAParm.Cpty);
      if (not v_st)
         RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
      end;

      v_st = CheckGenAgr(CSAParm.vGenAgr, err_txt, CSAParm.Cpty);
      if (not v_st)
         RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
      end;

      CSAParm.CreateCSA();
      if (not CSAParm.vstate)
         err_txt = CSAParm.vErrText;
         RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
      end;
      CSAParm.FindCSA(); // так как мы не знаем ID свеже созданного CSA
   end;
   CSAParm.UpdateCSA();
   if (not CSAParm.vstate)
      err_txt = CSAParm.vErrText;
      RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
   else
      CSAParm.UpdateCSAAdd();
      if (not CSAParm.vstate)
         err_txt = CSAParm.vErrText;
         RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
      end;
   end;
   
   CSA_res.Agreement_Id  = CSAParm.Agreement_Id;
   CSA_res.Agreement_Code= CSAParm.Agreement_Code;
   CSA_res.Error.ErrorCode = 0;
   CSA_res.Error.ErrorDesc = "OK";
   
   return CSA_res;
onError(err)
   vg_state = c_err_obj.obtain_err_code(err);
   vg_error = c_err_obj.obtain_err_msg(err);
   
   CSA_res.Agreement_Id  = CSAParm.Agreement_Id;
   CSA_res.Agreement_Code= CSAParm.Agreement_Code;
   CSA_res.Error.ErrorCode = vg_state;
   CSA_res.Error.ErrorDesc = vg_error;
   
   return CSA_res;
end;


macro adp_LoadAgreement(IncomeData)
   var out_str = "";
  
   var req_msg_obj = NULL;
   var req_data_obj = NULL;
   var resp_data_obj = NULL;
   var resp_data_obj_arr = TArray;
   
   var ProcessCSA_result = NULL;
   var CSAList = TArray();   
   var CSAParm = NULL;
   
   var v_result, v_xml;
   var err_txt, out_code;
   var v_aux_buff, nametag_up, v_aux_cntr;
   
   private var v_logger_obj = NULL;    /* Объект для работы с журналом */
   private var v_log_id = NULL;        /* Идентификатор записи в журнале */

   if(ValType(IncomeData) == V_UNDEF)
      RunError(ERR_TEXT_NO_IN_MSG, c_err_obj(ERR_TEXT_NO_IN_MSG,
                                             ERR_CODE_NO_IN_PRM));
   end;
         
   /* Преобразуем запрос в объектный вид - begin */
   req_msg_obj = c_secur_msg_converter();
   if(not req_msg_obj.m_decode_escaped_char(IncomeData))
      RunError(req_msg_obj.get_service_msg(), c_usr_err_obj(req_msg_obj.get_service_msg()));
   end;
   if(not req_msg_obj.m_make_pure_msg_from_ifx(req_msg_obj.get_pure_msg()))
      RunError(req_msg_obj.get_service_msg(), c_usr_err_obj(req_msg_obj.get_service_msg()));
   end;
   req_data_obj = req_msg_obj.m_secur_wol_internal_req(req_msg_obj.get_pure_msg());
   /* Преобразуем запрос в объектный вид - end */

   v_logger_obj = uws_exchng_logger(null, null, XmlRpcRequestId(), "LoadAgreement", modulename(), NULL, req_msg_obj.get_pure_msg());
   v_log_id = v_logger_obj.get_log_id();

   if(ValType(req_data_obj) == V_UNDEF)
      err_txt = "Не удалось преобразовать XML в объект";
      RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
   end;
   
   // обработка
   CSAList = uTryToGetPropS(req_data_obj, "AgreementList");
   
   nametag_up = "LoadAgreement_req";
   v_aux_buff = getValueFromProperty(req_data_obj, "AgreementList" , nametag_up, true);
   
   resp_data_obj_arr.Size = 0;
   var resp_data_obj_arr_i = 0;
   v_aux_cntr = 0;

   if(ValType(v_aux_buff) == V_GENOBJ)
      CSAList = TArray;
      while(v_aux_buff.size > v_aux_cntr)
         CSAParm = v_aux_buff.value(v_aux_cntr);
         if(ValType(CSAParm) == V_GENOBJ)
            // единичная обработка
            ProcessCSA_result = RunProcessCSA(CSAParm);
         else
            ProcessCSA_result = c_MainResult;
            ProcessCSA_result.Agreement_Id  = 0;
            ProcessCSA_result.Agreement_Code= "";
            ProcessCSA_result.Error.ErrorCode = -1001;
            ProcessCSA_result.Error.ErrorDesc = string(InErrNotObj, "элемент списка AgreementList");
         end;
         // массив результатов обработки
         resp_data_obj_arr(resp_data_obj_arr_i) = ProcessCSA_result;
   
         resp_data_obj_arr_i = resp_data_obj_arr_i+1;
         CSAParm = NULL;
         ProcessCSA_result = NULL;

         v_aux_cntr = v_aux_cntr + 1;
      end;

   elif(ValType(v_aux_buff) == V_UNDEF)
      err_txt = string(InErrComPart, "AgreementList");
      RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
   else
      err_txt = string(InErrNotObj, "AgreementList");
      RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
   end;
   
   // итоговый класс 
   resp_data_obj = c_outObj;
   resp_data_obj.LoadAgreement_resp.AgreementList = resp_data_obj_arr;

   /* Преобразуем объектный вид ответа в документ требуемого формата - begin */
   out_str = req_msg_obj.m_secur_internal_resp(resp_data_obj);
   /* Преобразуем объектный вид ответа в документ требуемого формата - end */

   v_logger_obj.add_response(out_str);
   v_logger_obj.add_error_info(vg_state, vg_error);
  
   resp_data_obj = NULL;
   resp_data_obj_arr = NULL;
   req_msg_obj = NULL;
   req_data_obj = NULL;
   v_logger_obj = NULL;
   v_log_id = NULL;
   
   vg_state = 0;
   vg_error = "";

   return out_str;
   
onError(err)
   vg_state = c_err_obj.obtain_err_code(err);
   vg_error = c_err_obj.obtain_err_msg(err);

   resp_data_obj = c_outObj;
   resp_data_obj.LoadAgreement_resp.AgreementList = TArray;
   resp_data_obj.LoadAgreement_resp.AgreementList.value(0) = c_MainResult;
   resp_data_obj.LoadAgreement_resp.AgreementList.value(0).Agreement_Id = 0;
   resp_data_obj.LoadAgreement_resp.AgreementList.value(0).Agreement_Code = "";
   resp_data_obj.LoadAgreement_resp.AgreementList.value(0).Error.ErrorCode = vg_state;
   resp_data_obj.LoadAgreement_resp.AgreementList.value(0).Error.ErrorDesc = vg_error;

   /* Преобразуем объектный вид ответа в документ требуемого формата - begin */
   out_str = req_msg_obj.m_secur_internal_resp(resp_data_obj);
   /* Преобразуем объектный вид ответа в документ требуемого формата - end */   

   v_logger_obj.add_response(out_str);
   v_logger_obj.add_error_info(vg_state, vg_error);

   resp_data_obj = NULL;
   resp_data_obj_arr = NULL;
   req_msg_obj = NULL;
   req_data_obj = NULL;
   v_logger_obj = NULL;
   v_log_id = NULL;
   
   vg_state = 0;
   vg_error = "";

   return out_str;
end;


macro LoadAgreement(IncomeData) 
//setrstrace(true);
   vg_state = 0;
   vg_error = "";
   var out_obj = "";
   out_obj = adp_LoadAgreement(IncomeData); 
//setrstrace(false);
   return out_obj;
end;

