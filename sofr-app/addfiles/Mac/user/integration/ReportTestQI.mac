/* BIQ-9185 Отчет по уведомлениям о тестировании НКИ
   Гераськина Т.В.

*/
import "DlRepForm_h.mac", Календарь, "or_tpl_h.mac";
import RsbFormsInter, "scincfun.mac";
import "dbo_qi_func.mac";

var  isAuto = false;

macro RunReportTestQI(DateBeg : date, DateEnd : date)
   var Rep;
   var sheet = 1;
   var errorcount = 0;
   var filePath ;
   var saveDirPath ;
   var email = tarray();
   var i =0;
   var head_format="c:ex_FS(b):ex_B(tblr)";

   Rep = CMakeReport("┌┬┬┬┬┬┬┬┬┬┬┬┬┐");
   Rep.SetDefaultFontName("Times New Roman");
   Rep.SetDefaultFontSize(12);
   Rep.SetFlagShowGrid(true);
   Rep.AddPrintCell("Отчет по уведомлениям о тестировании НКИ за " + IIF(DateBeg == DateEnd, string(DateBeg), "период с "+ string(DateBeg) + " по " + string(DateEnd)) , 97, null, "c:ex_FS(b):ex_B()");
   Rep.AddStr();

   Rep.AddEmptyStr();
   Rep.AddPrintCell("№ п/п", 3, null, head_format);
   Rep.AddPrintCell("Код клиента", 10.14, null, head_format);
   Rep.AddPrintCell("Дата тестирования", 16.14, null, head_format);
   Rep.AddPrintCell("Номер категории", 10.14, null, head_format);
   Rep.AddPrintCell("Результат", 10.14, null, head_format);
   Rep.AddPrintCell("Дата отправки уведомления", 16.14, null, head_format);
   Rep.AddPrintCell("Результат отправки", 26.14, null, head_format);
   Rep.AddStr();
   
   var cmd = DL_RSDCommand("SELECT t_id, t_timestamp, t_partyid, t_kind, t_ekk, to_char(t_accred_date, 'dd.mm.yyyy hh24:mi:ss') as t_accred_date, "+
                           "       t_accred_code, t_accred_result, to_char(t_send_date,'dd.mm.yyyy') as t_send_date, t_send_status, t_send_result, "+
                           "       t_dlcontrid, t_msgid, t_jmsmessageid "+
                           "  FROM uqiaccreditation_dbt "+
                           " WHERE t_timestamp between :p_datebegin and :p_dateend+1 "+
                           "   AND t_kind = :kind " );
   cmd.AddParam(DateBeg);
   cmd.AddParam(DateEnd);
   cmd.AddParam(TEST_QI);
   var DataSet = cmd.execute();
    
   var n = 1;
   var normal_format = "r:ex_FS():ex_B(tblr)";
   var signal_format = "r:ex_FS():ex_B(tblr):ex_BC(255)";
   var comment_format = normal_format;

   while (DataSet.movenext())
      Rep.AddPrintCell(n                           ,     3, null, Comment_format);
      Rep.AddPrintCell(DataSet.rec.t_ekk           , 10.14, null, Comment_format);
      Rep.AddPrintCell(DataSet.rec.t_accred_date   , 16.14, null, Comment_format);
      Rep.AddPrintCell(DataSet.rec.t_accred_code   , 10.14, null, Comment_format);
      Rep.AddPrintCell(DataSet.rec.t_accred_result , 10.14, null, Comment_format);
      Rep.AddPrintCell(DataSet.rec.t_send_date     , 16.14, null, Comment_format);
      Rep.AddPrintCell(DataSet.rec.t_send_result   , 26.14, null, "l:ex_FS():ex_B(tblr)");
      Rep.AddStr();
      n = n+1;
   end;

   var CreateDate = date();
   var CreateTime = time();
   var day, mon, year;
   var hour, min, sec;
   DateSplit(CreateDate, day, mon, year);
   TimeSplit(CreateTime, hour, min, sec);
   
   Rep.PrintWinRep("Отчет");
   Rep.SetZoomType(ZOOM_TYPE_A4L);
  
   var fileNameAfter = "ReportTestQI" +"_"+string(year)+string(mon:f)+string(day:f)
                                      +"_"+string(hour:f)+string(min:f)+string(sec:f)+SubStr(Rep.Exlusivefilename, StrLen(Rep.Exlusivefilename) - 3);
   fileNameAfter = strSubst(fileNameAfter,".htm",".xls");
   Rep.SetWinRepOutput(1,WINREP_FORMAT_XLS);
 
   if( Not IsStandAlone() )
      // Если в строке пути есть знак ":" или "\\", то это абсолютный путь
      if( Index(toAnsi(NameDirTerm), ":") OR Index(toAnsi(NameDirTerm), "\\\\") )
         filePath  = SubStr(toAnsi(NameDirTerm), 2);
      else
         filePath = GetCurDir(TRUE)+"\\"+SubStr(toAnsi(NameDirTerm), 2);
      end;
   else
      filePath = Rep.WorkDir + "\\";  
   end;
  
   var ob, mainbook, mainsize, childbook, ind = 1;
   ob = CDAOMSExcel(null, true);
   ob.Create();
   mainbook = ob.Application.Workbooks.Open(string(filePath+Rep.Exlusivefilename), null, false);

   /*Сохраняем книгу*/
   if(mainbook.SaveAs(filePath + fileNameAfter, WINREP_FORMAT_XLS) == false)
      msgbox("Ошибка при сохранении файла отчета");
   end;

   /*Закрываем книгу*/
   mainbook.Close();
   var saveFilePath = filePath + fileNameAfter;

   if (isAuto == false)
      Rep.ShowWinRep();/*Нужен для выпуска пользователем напрямую, чтобы отображался на экране*/
      /* Если работаем в трехзвенке - то запускаем отчет на терминале */
   end;
end; 

class (TRsbPanel) PanelRunReportTestQI
   initTRsbPanel();

   const K_Enter = 13;
   const K_F3    = 317;
   const K_F2    = 316;

   const BeginCalcDate_ID = 1;

   var curstr = 1;
   Caption = "Отчет по уведомлениям о тестировании НКИ";
   Status = "~ESC~ Выход ~F2~ Выполнить";
   setSize(40,4);
   setPosition(30,10);
   var BeginCalcDate, EndCalcDate, dx;

   curstr = curstr+1;
   BeginCalcDate = TRsbEditField(V_DATE);
   BeginCalcDate.setPosition(14,curstr);
   BeginCalcDate.setSize(10,1);
   BeginCalcDate.name = "begincalcdate";
   BeginCalcDate.value = date();
   addControl(BeginCalcDate);

   EndCalcDate = TRsbEditField(V_DATE);
   EndCalcDate.setPosition(27,curstr);
   EndCalcDate.setSize(10,1);
   EndCalcDate.name = "endincalcdate";
   EndCalcDate.value = date() ;
   addControl(EndCalcDate);

   var m_LabelCD:TrsbLabel = TRsbLabel(2,curstr,"Даты периода : с");
   addLabel (m_LabelCD);

   var m_LabelT:TrsbLabel = TRsbLabel(25,curstr,"по");
   addLabel (m_LabelT);

   addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "onKeyPressed"));

   macro onKeyPressed(ev)
      if (ev.KeyCode == K_F3)
         if (ev.id == BeginCalcDate_ID)
            if (this.focusedControl.name == "begincalcdate")
               BeginCalcDate.value = GetDateByCalendar(date());
               this.redraw; 
            elif  (this.focusedControl.name == "endincalcdate")
               EndCalcDate.value = GetDateByCalendar(date());
               this.redraw; 
            end;
         end;
      elif (ev.KeyCode == K_Enter)
         this.redraw; 
      elif(ev.keyCode == K_F2)
         RunReportTestQI(BeginCalcDate.value, EndCalcDate.value);
      end;
   end; 
end;

var myPanel:TRsbPanel = PanelRunReportTestQI;

GetCmdLineParm("isAuto",isAuto);
if (isAuto == false)
  myPanel.run;
  exit(1);
else
  RunReportTestQI(date(), date());
end;
