/*-----------------------------------------------------*/
/* CdiGetXref.mac  Запрос ссылок по Субъекту ФЛ     */
/*                                                     */
/* Автор: Гераськина Т.                                */
/* 16.07.2021 BIQ-7006 Замена PersonGetXref на сервис CdiGetXrefReq
*/
/*----------------------------------------------*/
import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import "RequestWS.mac";                   /* Функция передачи запроса в Web Sphere */
import "uws_exchng_log.mac";              /* Функционал логирования */
import func_lib;
import global_classes;

// входящий параметр - код АБС
class c_CdiGetXrefReq(Incomedata) 
   var ExtIdSourceSystem   ;  // Код системы источника      Справочник
   var ExtIdPartySourceId;    // ID клиента на источнике    Идентификатор
   var PartyId;               // ID клиента в CDI           Идентификатор
   
   macro uInitPrime(IncomeData)
      ExtIdSourceSystem = c_IntegrationDictionaryRecordXType();
      ExtIdSourceSystem.RecordCode = "CFT";
      
      ExtIdPartySourceId = c_IntegrationSymbolicIdentifierXType();
      ExtIdPartySourceId = Incomedata;
   end;
end;

/* Корневой элемент ответа - AS ID - как пришел */
private class adp_CdiGetXrefResponse(IncomeData)
   var ExtIdList;    // Список кросс-ссылок
   var ErrorList;    // Блок ошибок обработки
  
   /* Кратность параметров в соответствии со схемой/спецификацией */
   private macro uInitPrime(IncomeData)
      var err_txt;
      var aux_buff, aux_buff2, ai;
      var temp_ExtId;
      var temp_Error;
            
      var nametag_up = "CdiGetXrefResp";
      
      if(ValType(IncomeData) != V_UNDEF)
         aux_buff = uTryToGetPropS(IncomeData, "ExtIdList");
         if(ValType(aux_buff) == V_GENOBJ)
            ExtIdList = TArray;
            aux_buff = 0;
            nametag_up = nametag_up+".ExtIdList";
            while(IncomeData.ExtIdList.size > aux_buff)
               temp_ExtId = c_CdiGetXrefReq;
               temp_ExtId.ExtIdSourceSystem = getDictionaryFromProperty(IncomeData.ExtIdList.value(aux_buff), "ExtIdSourceSystem", nametag_up);
               temp_ExtId.ExtIdPartySourceId = getSymbolFromProperty(IncomeData.ExtIdList.value(aux_buff), "ExtIdPartySourceId", nametag_up);
               temp_ExtId.PartyId = getSymbolFromProperty(IncomeData.ExtIdList.value(aux_buff), "PartyId", nametag_up);
               
               ExtIdList.value(aux_buff) = temp_ExtId;
               aux_buff = aux_buff + 1;
               temp_ExtId = NULL;
            end;
         elif(ValType(aux_buff) == V_UNDEF)
         else
            err_txt = string(InErrNotObj, nametag_up+".ExtIdList");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
         end;  

         aux_buff = uTryToGetPropS(IncomeData, "ErrorList");
         if(ValType(aux_buff) == V_GENOBJ)
            ErrorList = TArray;
            aux_buff = 0;
            nametag_up = nametag_up+".ErrorList";
            while(IncomeData.ErrorList.size > aux_buff)
               temp_Error = c_Error;
               temp_Error.ErrorCode = getValueFromProperty(IncomeData.ErrorList.value(aux_buff), "ErrorCode", nametag_up);
               temp_Error.ErrorDesc = getValueFromProperty(IncomeData.ErrorList.value(aux_buff), "ErrorDesc", nametag_up);
               
               ErrorList.value(aux_buff) = temp_Error;
               aux_buff = aux_buff + 1;
               temp_Error = NULL;
            end;
         elif(ValType(aux_buff) == V_UNDEF)
         else
            err_txt = string(InErrNotObj, nametag_up+".ErrorList");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
         end;  
      end;
   end;
   
   macro GetSystemXref(_SystemId)
      var res = c_CdiGetXrefReq;
      res.PartyId = c_IntegrationSymbolicIdentifierXType();
      res.PartyId.ObjectId = "-1"; 
      res.ExtIdPartySourceId = c_IntegrationSymbolicIdentifierXType();
      res.ExtIdPartySourceId.ObjectId = "-1";
      res.ExtIdSourceSystem = c_IntegrationDictionaryRecordXType();
      res.ExtIdSourceSystem.RecordCode = "-1";

      var ExtId;
      
      for (ExtId, ExtIdList)
         if ( Valtype(ExtId.ExtIdSourceSystem) != V_UNDEF )
            if ( (Valtype(ExtId.ExtIdSourceSystem.RecordCode) != V_UNDEF) and (ExtId.ExtIdSourceSystem.RecordCode == _SystemId) )
               res = ExtId;
            end;
         end;
      end;
      return res;
   end;

   uInitPrime(IncomeData);
end; 


/* Основной класс работы с сервисом */
private class c_CdiGetXrefProcessor(IncomeData)
   var CdiGetXrefReq; /* !!! По названию корневого тега запроса !!! */
   CdiGetXrefReq = c_CdiGetXrefReq;
   CdiGetXrefReq.uInitPrime(IncomeData);
end; 

/*-------------------------------------------------------------------*/
/* Адаптер для вызова сервиса "Запрос ссылок по Субъекту ФЛ"         */
/*-------------------------------------------------------------------*/
macro run_CdiGetXrefReq(ReqData, p_transparent_id)
   var RespData = NULL;
   var err_txt = NULL;
   
   private var v_out_obj;
   private var RequestText;
   private var v_transformator;
   private var v_answer;
   private var v_answer_data;
   private var v_logger_obj;
   private var v_log_id;
   private var xml_str;
   private var v_xml_rpc_reqid = XmlRpcRequestId(); 

   if ( (ReqData == NULL) or (ValType(ReqData) == V_UNDEF) )
      return null;
   end;

   v_transformator = c_secur_msg_converter();

   // заполнение класса информацией 
   v_out_obj = c_CdiGetXrefProcessor(ReqData);
   RequestText = v_transformator.m_secur_internal_resp(v_out_obj);
   
   v_logger_obj = uws_exchng_logger(null, p_transparent_id, v_xml_rpc_reqid, "CdiGetXrefReq", modulename(), null, RequestText);
   v_log_id = v_logger_obj.get_log_id();

      
   //err_txt = v_transformator.m_validate_out_xml(RequestText);
   if (err_txt == NULL)
        v_answer = SubmitRequestToWS( 2,            // ИД очереди.
                                       "",          // ИД сообщения.
                                       "",          // Идентификатор Бэк Офиса. (не обрабатывается).
                                       true,        // Признак синхронной обработки. Если не указан, = False
                                       "SOFR",      // Подразделение системы-отпр
                                       25,          // ИД типа данных !!!!
                                       RequestText, // Бизнес данные в виде XML
                                       v_log_id,    // ID usr_exchng_log
                                       "CDI"
                                       );
         if (v_answer.ResultCode == 0)
            xml_str = v_answer.responsetext;
            v_logger_obj.add_response(xml_str); 
            
            v_transformator = NULL;
            if (xml_str)
               /* Преобразуем запрос в объектный вид - begin */
               v_transformator = c_secur_msg_converter();
               if(not v_transformator.m_decode_escaped_char(xml_str))
                  RunError(v_transformator.get_service_msg(), c_usr_err_obj(v_transformator.get_service_msg()));
               end;
               v_answer_data = v_transformator.m_secur_wol_internal_req(v_transformator.get_pure_msg());
               /* Преобразуем запрос в объектный вид - end */

               if(ValType(v_answer_data) == V_UNDEF)
                  err_txt = "Не удалось преобразовать XML в объект";
                  RunError(err_txt, c_usr_err_obj(err_txt)); 
               end;
               
               RespData = adp_CdiGetXrefResponse(v_answer_data);
               /* Преобразование объекта к виду, ожидаемому Системой */
            end;
         else 
            v_logger_obj.add_error_info(v_answer.ResultCode, v_answer.ResultText);
         end;

   else
      v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, err_txt);
   end;
         
   return RespData;

onError(err)
   return NULL;
end; 


/*---------------------------------------*/
/*              Точка входа              */
/*---------------------------------------*/
/* ReqData - данные для отправки         */
/*---------------------------------------*/
macro CdiGetXrefReq (ReqData, in_transparent_id) 
   return run_CdiGetXrefReq(ReqData, in_transparent_id); 
end;

