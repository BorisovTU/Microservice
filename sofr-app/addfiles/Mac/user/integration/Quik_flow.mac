/*
$Name:        Quik_flow.mac
$Module:      Интеграция со внешними системами
$Description: Формирование файлов создания/изменения клиентов для системы QUIK
*/

import oralib, likepy, globals, CTInter;
import "global_utils_intgr.mac"; //maa241019 - этот нельзя так как идет ошибка переопределения c_email_proc_env
import func_lib;
import dlcontrfunc; // BIQ-7041 для кода биржи Спб

private Const  CategNumber = 171;
private Const QuikCode = 105;
private Const REG_EXPORT_PATH = "РСХБ\\интеграция\\директории\\Export\\Quik_folder";
private Const REG_CLASSES = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\QUIK\\CLASSES";


//Проверка корректности загрузки клиента в Quik
macro Check_Correct_Response(ClientId, EventId)
private var XML, name, StrPath, ErrCode, sql, dt;

   sql = ExecSqlSelect("select trunc(t_timestamp) from utableprocessevent_dbt where t_objecttype = 5025 and t_status = 1 and t_recid = :EventId", makeArray(SqlParam("EventId", EventId)));
   sql.movenext();
   dt = StrSubst(string(date(sql.value(0))), " ", "0");
   dt = StrSubst(dt, ".", "");

   GetRegistryValue(REG_EXPORT_PATH, V_STRING, StrPath, ErrCode);
   if( ErrCode > 0 )
      StrPath = "d:\\rshb_sofr\\export\\";
   end;

   xml = ActiveX("Microsoft.XMLDOM");

   XML.load( StrPath + "Out\\in_"+ClientId+"_" + dt + ".xml");
   if (xml.url !="")
       name = XML.getElementsByTagName("Result").item(0).getAttributeNode("UID").text;
   else
      name  = -1;
   end;
   return(name);
end;

//Проверка некорректности загрузки клиента в Quik
macro Check_Error_Response(ClientId, EventId)
private var XML, name, StrPath, ErrCode, sql, dt, contact, EMail;

   sql = ExecSqlSelect("select trunc(t_timestamp) from utableprocessevent_dbt where t_objecttype = 5025 and t_status = 1 and t_recid = :EventId", makeArray(SqlParam("EventId", EventId)));
   sql.movenext();
   dt = StrSubst(string(date(sql.value(0))), " ", "0");
   dt = StrSubst(dt, ".", "");
   GetRegistryValue(REG_EXPORT_PATH, V_STRING, StrPath, ErrCode);
   if( ErrCode > 0 )
      StrPath = "d:\\rshb_sofr\\export\\";
   end;

   xml = ActiveX("Microsoft.XMLDOM");

   XML.load( StrPath + "Error\\in_"+ClientId+"_" + dt + ".xml");
   if (xml.url !="")
       name = 1;
      contact = ExecSqlSelect("SELECT  t_value  FROM dcontact_dbt  WHERE t_partyid = :partyid  AND t_addresstype in ( 0,1)  AND t_ismain = 'X' AND t_contactkind = 5 AND t_contacttype = 8", makeArray(SqlParam("partyid", ClientId )));
      if (contact.movenext());
         EMail  = contact.value(0);
      else
         EMail  = "-";
      end;
   else
      name  = 0;
   end;
   return(name);
end;

private
macro MakeOneElement (ParentElement, ElementName, ElementValue, xml)
   var Element;
   Element = xml.createElement(ElementName);
   Element.Text = ElementValue;
   ParentElement.appendChild(Element);
End;

//Загрузка клиента в Quik
macro Quik_User_Create (ClientId, OperKind, EventId)
   var sql, xml, tempXML, contact, err;
   var UserData, Command, UserInfo,Organization, ContactInfo, Permission, PermissionsOnMarket,Permission2,  GlobalRights, Module, Options, UserKey, Certificate, TFAuth, AssignToManagers,  RSAKey, Classes, FileName ;
   var Security,AuthByLogin, Login, Passwor, sqldate, StrPath, ErrCode, dt;
   var Name1, Name2, Name3, tmp, symnom, validdate,validto, SMSPhone, EMail, sqlpass, sqlUID, sqlcodes, codes;
//dan> BIQ-7571
   var pFirmID;
//<dan   

   // BIQ-7041 биржевые коды Спб
   var marketid, codes_spb;

   GetRegistryValue(REG_EXPORT_PATH, V_STRING, StrPath, ErrCode);
   if( ErrCode > 0 )
      StrPath = "d:\\rshb_sofr\\export\\";
   end;

   var getreg;
   // BIQ-7785, в настройке дата начала ЕПД
   var flag_edp = false;
   var EdpDate = "";
   getreg = GetRegistryValue ("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ДАТА ВКЛЮЧЕНИЯ ЕДП", V_STRING, EdpDate );
   if (ValType(getreg) != V_UNDEF)
      if ((EdpDate != "00.00.0000") and ({curdate} >= date(EdpDate)))
         flag_edp = true;
      end;
   end;

   // BIQ-7839 gtv: в настройке дата начала работы с модулем BookBuilding 
   var flag_bookbuilding = false;
   var BookbuildingDate = "";
   getreg = GetRegistryValue ("РСХБ\\ИНТЕГРАЦИЯ\\ДАТА ВКЛЮЧЕНИЯ BOOKBUILDING", V_STRING, BookbuildingDate );
   if ( (ValType(getreg) != V_UNDEF) and (StrLen(BookbuildingDate) == 10) )
      if ((BookbuildingDate != "00.00.0000") and ({curdate} >= date(BookbuildingDate)))
         flag_bookbuilding = true;
      end;
   end;

   sqldate = ExecSqlSelect("select trunc(t_timestamp) from utableprocessevent_dbt where t_objecttype = 5025 and t_status = 1 and t_recid = :EventId", makeArray(SqlParam("EventId", EventId)));
   sqldate.movenext();
   dt = StrSubst(string(date(sqldate.value(0))), " ", "0");
   dt = StrSubst(dt, ".", "");
   TempXML = StrPath + "in\\in_"+ClientId+"_" + dt + ".xml";
   
   xml = ActiveX("Microsoft.XMLDOM");
   xml.appendChild( xml.createProcessingInstruction("xml"," version='1.0' encoding='Windows-1251'") );
   UserData  = xml.createElement("UserData");

   command = xml.createElement("Command");
   
   if (OperKind == 1)
      command.setAttribute("Type", "AddUser");
   else 
      command.setAttribute("Type", "UpdateUser");
   end;
   
   if (OperKind == 2)
      sqlUID = ExecSqlSelect("select t_code from dobjcode_dbt where t_codekind = :codekind and t_objectid = :objid",
                    makeArray(SqlParam("codekind", QuikCode ), SqlParam("objid", ClientId )));
      sqlUID.movenext();
      MakeOneElement (command, "UID", sqlUID.value(0), xml);
   end;

   sql = ExecSqlSelect("select t_name, t_legalform  from dparty_dbt where t_partyid = :partyid", makeArray(SqlParam("partyid", ClientId )));
   sql.movenext();
   //Здесь сделать в зависимости от легалформ либо для физиков, либо для юриков.

   if (sql.value(1) == 1)
      Name1 = subStr(sql.value(0), 1, 150);
      Name2 = subStr(sql.value(0), 151, 300);
      Name2 = subStr(sql.value(0), 301, 450);
   else
      symnom = strbrk(sql.value(0), " ");
      Name1 = substr(sql.value(0), 1, symnom-1);
      tmp = substr(sql.value(0), symnom + 1);
      symnom = strbrk(tmp, " ");
      Name2 = substr(tmp,1, symnom-1);
      Name3 = substr(tmp, symnom + 1);      
   end;


   UserInfo = xml.createElement("UserInfo");
   MakeOneElement (UserInfo, "FirstName", Name2, xml);
   MakeOneElement (UserInfo, "MiddleName", Name3, xml);
   MakeOneElement (UserInfo, "LastName", Name1, xml);
   MakeOneElement (UserInfo, "Comment", null, xml);
   command.appendchild(Userinfo);

   Permission = xml.createElement("Permission");
   sqldate = ExecSqlSelect("select trunc(t_timestamp) from utableprocessevent_dbt where t_objecttype = 5025 and t_status = 1 and t_recid = :EventId", makeArray(SqlParam("EventId", EventId)));
   sqldate.movenext();
   dt = StrSubst(string(date(sqldate.value(0))), " ", "0");
   dt = StrSubst(dt, ".", "");
   validdate = substr(dt, 5) + "-" + substr(dt, 3, 2) + "-" + substr(dt, 1,2);
   dt = StrSubst(string(date(sqldate.value(0))+36524), " ", "0");
   dt = StrSubst(dt, ".", "");
   validto = substr(dt, 5) + "-" + substr(dt, 3, 2) + "-" + substr(dt, 1,2);
   MakeOneElement (Permission, "ValidFrom", validdate, xml);
   MakeOneElement (Permission, "ValidTo", "2038-01-18", xml);
   command.appendchild(Permission);

    // Для нового способа учета телефона
    //AND t_contactkind = CV_CONTACT_KIND_PHONE AND t_contacttype = CV_CONTACT_TYPE_MOBILE

   contactinfo = xml.createElement("ContactInfo");
   contact = ExecSqlSelect("SELECT  t_value  FROM dcontact_dbt  WHERE t_partyid = :partyid  AND t_addresstype in ( 0,1)  AND t_ismain = 'X' AND t_contactkind = :contactkind AND t_contacttype = :contacttype",
                            makeArray(SqlParam("partyid", ClientId ), SqlParam("contactkind", CV_CONTACT_KIND_PHONE ), SqlParam("contacttype", CV_CONTACT_TYPE_MOBILE )));
   if (contact.movenext());
      MakeOneElement (contactinfo, "Phone", contact.value(0), xml);
   else
      MakeOneElement (contactinfo, "Phone", "-", xml);
   end;

   contact = ExecSqlSelect("SELECT  t_value  FROM dcontact_dbt  WHERE t_partyid = :partyid  AND t_addresstype in ( 0,1)  AND t_ismain = 'X' AND t_contactkind = :contactkind AND t_contacttype = :contacttype",
                            makeArray(SqlParam("partyid", ClientId ), SqlParam("contactkind", CV_CONTACT_KIND_PHONE ), SqlParam("contacttype", CV_CONTACT_TYPE_MOBILE )));
   if (contact.movenext());
      SMSPhone = contact.value(0);
   else 
      SMSPhone = "-";
   end;
   MakeOneElement (contactinfo, "SMS", SMSPhone, xml);

   contact = ExecSqlSelect("SELECT  t_value  FROM dcontact_dbt  WHERE t_partyid = :partyid  AND t_addresstype in ( 0,1)  AND t_ismain = 'X' AND t_contactkind = :contactkind AND t_contacttype = :contacttype",
                            makeArray(SqlParam("partyid", ClientId ), SqlParam("contactkind", CV_CONTACT_KIND_EMAIL ), SqlParam("contacttype", CV_CONTACT_TYPE_REPORT )));
   if (contact.movenext());
      EMail  = contact.value(0);
   else
      EMail  = "-";
   end;
   MakeOneElement (contactinfo, "EMail", EMail, xml);
   command.appendchild(contactinfo);

   Security = xml.createElement("Security");
   MakeOneElement (Security, "CryptoType", "Unknown", xml);
   command.appendchild(Security);

   GlobalRights = xml.createElement("GlobalRights");
   MakeOneElement (GlobalRights, "Rights", "Phr", xml);
   sqlcodes = ExecSqlSelect("select mp.t_mpcode from dsfcontr_dbt sf join ddlcontrmp_dbt mp on sf.t_id = mp.t_sfcontrid "+
                                          "where  sf.t_partyid = :partyid and t_servkind in (1, 15) and t_servkindsub = 8   and t_dateclose =  to_date('01010001', 'ddmmyy')", makeArray(SqlParam("partyid", ClientId )));
   if (sqlcodes.movenext())
      MakeOneElement (GlobalRights, "Blocked", 0, xml);
   else
      MakeOneElement (GlobalRights, "Blocked", 1, xml);
   end;
   MakeOneElement (GlobalRights, "Archives", 1, xml);
   MakeOneElement (GlobalRights, "API", 1, xml);
   MakeOneElement (GlobalRights, "SeeNews", 0, xml);
   MakeOneElement (GlobalRights, "SendMessages", 0, xml);
   MakeOneElement (GlobalRights, "ReceiveMessages", 1, xml);
   MakeOneElement (GlobalRights, "NeedSign", 0, xml);
   command.appendchild(GlobalRights);

   MakeOneElement (command, "IPAddress", null, xml);
   MakeOneElement (command, "MsgGroup","CLNT", xml);
   MakeOneElement (command, "News", null, xml);
   MakeOneElement (command, "MessagesTo", null, xml);

   Organization = xml.createElement("Organization");   
   MakeOneElement (Organization, "OrgCode", 2, xml);
   MakeOneElement (Organization, "OrgName", "Клиенты ОАО &quot;Российский сельскохозяйственный банк&quot;", xml);      
   MakeOneElement (Organization, "Address", null, xml);
   MakeOneElement (Organization, "EMail", null, xml);
   command.appendchild(Organization);


   var FirmId_Edp = "MC0134700000"; // BIQ-7785 Гераськина Т.В. Единый код
   var codes_mmvb;

   // BIQ-7041 биржевые коды Спб
   marketid = ПолучитьБиржуПоВидуКодаДБО(DLCCK_SPB_BIDCODE); 

   sqlcodes = ExecSqlSelect("select LISTAGG(mp.t_mpcode,', ') within group(order by mp.t_marketid) mpcode, mp.t_marketid "+
                              "from dsfcontr_dbt sf join ddlcontrmp_dbt mp on sf.t_id = mp.t_sfcontrid "+
                             "where sf.t_partyid = :partyid and t_servkind = 1 and t_servkindsub = 8 and t_dateclose =  to_date('01010001', 'ddmmyy')"+
                          "group by mp.t_marketid",
                             makeArray(SqlParam("partyid", ClientId )));
   codes = null;

   if (sqlcodes.movenext());
      if (sqlcodes.value(1) == marketid)
         codes_spb = string(sqlcodes.value(0)); // такая строчка только одна
      else
         codes = string(sqlcodes.value(0));
      end;
      while (sqlcodes.movenext())
         if (sqlcodes.value(1) == marketid)
            codes_spb = string(sqlcodes.value(0));
         else
            codes = codes + ", " + sqlcodes.value(0);
         end;
      end;
   end;

   // BIQ-16465 shev: классы с правами вынесены в настройку 
   var CLASSES_TEXT = "", CLASSES_REG = "";
   getreg = GetRegistryValue (REG_CLASSES, V_STRING, CLASSES_REG );
   if ( (ValType(getreg) != V_UNDEF) and (StrLen(CLASSES_REG) > 0) )
         CLASSES_TEXT = CLASSES_REG;
//оставил старый варинат на случай нештатной работы
   else
         CLASSES_TEXT = "EQDB, EQEO, EQEU, EQGO, EQOB, EQTD, EQTU, EQYO, TQBD, TQBE, TQBR, TQCB, TQDD, TQDE, TQIF, TQOB, TQOD, TQTD, TQTE, TQTF, TQIR, TQOE, TQPI, TQOY, TQIY";
   end;

   if (codes != null)
      Classes = xml.createElement("Classes");
      Classes.setAttribute("ClientCodes", codes);
      Classes.setAttribute("FirmID", "MC0134700000");
      Classes.setAttribute("Rights", "BCGTbcoptvx");
      Classes.text = CLASSES_TEXT;
      command.appendchild(Classes);

      Classes = xml.createElement("Classes");
      Classes.setAttribute("ClientCodes", codes);
      Classes.setAttribute("FirmID", "MC0134700000");
      Classes.setAttribute("Rights", "Cbpv");
      Classes.text = "INDX, RTSIDX, RTSIND";
      command.appendchild(Classes);

      //BIQ-7839 классы для букбилдинга
      if (flag_bookbuilding)
         Classes = xml.createElement("Classes");
         Classes.setAttribute("ClientCodes", codes);
         Classes.setAttribute("FirmID", "MC0134700000");
         Classes.setAttribute("Rights", "Cbcoptvx");
         Classes.text = "PSAU_POS";
         command.appendchild(Classes);
      end;

      codes_mmvb = codes;
      codes = null; 
   end;

   if (codes_spb != null)
      Classes = xml.createElement("Classes");
      if (flag_edp)
         Classes.setAttribute("ClientCodes", codes_mmvb);
      else 
         Classes.setAttribute("ClientCodes", codes_spb);
      end;
      Classes.setAttribute("FirmID", "MC0134700000");
      Classes.setAttribute("Rights", "BCGTbcoptvx");
//      Classes.text = "SPBXM, SPBBND, SPBHKEX, SPBDE";
//      Classes.text = "SPBXM, SPBHKEX, SPBDE";  // BIQ-7041 исключение SPBBND
      Classes.text = "SPBXM, SPBHKEX, SPBDE, SPBEQRU";  // BIQ-15889 Переход на расчетный цикл Т+1 на рынке акций и облигаций ПАО Московская Биржа и Биржа СПБ
      command.appendchild(Classes);
      codes_spb = null;
   end;

   sqlcodes = null; 
              

   // BIQ-7785 Гераськина Т.В. Единый код
   sqlcodes = ExecSqlSelect("select mp.t_mpcode, mp.t_firmid, "+
                            "    ( select count(*) from dobjatcor_dbt o "+                                                       
                            "       where o.t_objecttype = 659 and o.t_groupid = :groupid "+                                 
                            "         and (o.t_validtodate >= :dt or o.t_validtodate = to_date('01.01.0001','dd.mm.yyyy')) "+
                            "         and o.t_object = lpad(sf.t_id,10,'0') and o.t_attrid = 1 ) edp "+                                               
                            "  from dsfcontr_dbt sf join ddlcontrmp_dbt mp on sf.t_id = mp.t_sfcontrid "+
                            " where  sf.t_partyid = :partyid and t_servkind = 15 and t_servkindsub = 8  and t_dateclose =  to_date('01010001', 'ddmmyy')",
                            makeArray(SqlParam("groupid", SUBCONTRACT_CATEGORY_EDP ),
                                      SqlParam("dt", date() ),
                                      SqlParam("partyid", ClientId )));
   if (sqlcodes.movenext());
      codes = "CL" + string(sqlcodes.value(0));
      if ( flag_edp and (sqlcodes.value("edp")>0) ) // если ЕДП
        pFirmID = FirmId_Edp;
      else
        if(sqlcodes.value("t_firmid") == "")
          pFirmID = string("SPBFUTF502");
        else
          pFirmID = string("SPBFUT",sqlcodes.value("t_firmid")); 
        end;  
      end;

      while (sqlcodes.movenext())
         if (substr(sqlcodes.value(0), 1,1) == "F")
            codes = codes + ", CL" + sqlcodes.value(0);
         else
            codes = codes + ", " + sqlcodes.value(0);
         end;
      end;   
   end;
   if (codes != null)
      Classes = xml.createElement("Classes");
      Classes.setAttribute("ClientCodes", codes);
      Classes.setAttribute("FirmID", pFirmID);
      Classes.setAttribute("Rights", "BCGTbcoptvx");
// 2020-09-25 Cherednichenko is-516011 Добавил классы SPBOPT,FUTSPREAD
// DEF-16422 при ЕДП исключается SPBOPT 
      if ( flag_edp)
         Classes.text = "SPBFUT, FUTSPREAD";
      else
         Classes.text = "SPBFUT, SPBOPT, FUTSPREAD";
      end;

      command.appendchild(Classes);
   end;

   sqlcodes = ExecSqlSelect("select mp.t_mpcode, "+
                            "    ( select count(*) from dobjatcor_dbt o "+                                                       
                            "       where o.t_objecttype = 659 and o.t_groupid = :groupid "+                                 
                            "         and (o.t_validtodate >= :dt or o.t_validtodate = to_date('01.01.0001','dd.mm.yyyy')) "+
                            "         and o.t_object = lpad(sf.t_id,10,'0') and o.t_attrid = 1 ) edp "+                                               
                            "  from dsfcontr_dbt sf join ddlcontrmp_dbt mp on sf.t_id = mp.t_sfcontrid "+
                            " where  sf.t_partyid = :partyid and t_servkind = 21 and t_servkindsub = 8   and t_dateclose =  to_date('01010001', 'ddmmyy')", 
                            makeArray(SqlParam("groupid", SUBCONTRACT_CATEGORY_EDP ),
                                      SqlParam("dt", date() ),
                                      SqlParam("partyid", ClientId )));
   codes = null;

   if (sqlcodes.movenext());
      codes = string(sqlcodes.value(0));
      if ( flag_edp and (sqlcodes.value("edp")>0) ) // если ЕДП
        pFirmID = FirmId_Edp;
      else
        pFirmID = "MB0134700000";
      end;
      if ( flag_edp and (sqlcodes.value("edp")>0) ) // если ЕДП
         codes = codes_mmvb;
      else 
         while (sqlcodes.movenext())
            codes = codes + ", " + sqlcodes.value(0);
         end;
      end;
   end;
   if (codes != null)
      Classes = xml.createElement("Classes");
      Classes.setAttribute("ClientCodes", codes);
      Classes.setAttribute("FirmID", pFirmID);     
      Classes.setAttribute("Rights", "BCGTbcoptvx");
      Classes.text = "CETS, CROSSRATE, CETS_SU";
      command.appendchild(Classes);

      sqlcodes = null; codes = null;
   end;


   AuthByLogin = xml.createElement("AuthByLogin");
   MakeOneElement (AuthByLogin, "WebQuik", 1, xml);
   MakeOneElement (AuthByLogin, "Desktop", 1, xml);
      login = xml.createElement("Login");
      login.Text = ClientId;//соответствует коду клиента в СОФР
      AuthByLogin.appendchild(login);
      
//   if (OperKind == 1)
      sqlpass = ExecSqlSelect("SELECT t_code_word FROM usr_clnt_cdwrd_buf_dbt  "+
                                            " WHERE t_cw_type = 1  AND t_cw_client = :p_cw_client", makeArray(SqlParam("p_cw_client", ClientId )));
      if(sqlpass.movenext())
         Passwor = xml.createElement("Password");
         Passwor.Text = sqlpass.value(0);
         Passwor.setAttribute("Type", "plain");
         Passwor.setAttribute("NeedChange", 1);
         Passwor.setAttribute("ValidityTerm", 365);
         AuthByLogin.appendchild(Passwor);  
         command.appendchild(AuthByLogin); 
      end;

   MakeOneElement (command, "TFAuthUse", 1, xml);

   if (OperKind == 1)
      TFAuth  = xml.createElement("TFAuth");
      TFAuth.setAttribute("Platform", -1);
      MakeOneElement (TFAuth, "AuthType", -1, xml);
      MakeOneElement (TFAuth, "Scenario", -1, xml);
      command.appendchild(TFAuth);
   end;

   TFAuth  = xml.createElement("TFAuth");
   TFAuth.setAttribute("Platform", 1);
   MakeOneElement (TFAuth, "AuthType", 2, xml);
   MakeOneElement (TFAuth, "Scenario", 1, xml);
   MakeOneElement (TFAuth, "ExpireDays", 0, xml);
   MakeOneElement (TFAuth, "SMSPhone", SMSPhone, xml);
   MakeOneElement (TFAuth, "CodeWord", null  , xml);
   MakeOneElement (TFAuth, "EMail", null, xml);
   command.appendchild(TFAuth);

   Userdata.appendChild(command);
   xml.appendChild(UserData);
   println(string(xml.xml));
   xml.Save(TempXML);
   DisconnectObjAttr(3, string(ClientId:10:o), CategNumber, 0);
   ConnectObjAttr(err, 3, string(ClientId:10:o), CategNumber, 3);
   Return (0);
   
   OnError(er)
      DisconnectObjAttr(3, string(ClientId:10:o), CategNumber, 0);
      ConnectObjAttr(err, 3, string(ClientId:10:o), CategNumber, 2);
      return 3;
End;

macro Quik_User_Block (ClientId, UID, EventId)
   var sql, xml, tempXML, contact, err;
   var UserData, Command, UserInfo,Organization, ContactInfo, Permission, PermissionsOnMarket,Permission2,  GlobalRights, Module, Options, UserKey, Certificate, TFAuth, AssignToManagers,  RSAKey, Classes, FileName ;
   var Security,AuthByLogin, Login, Passwor, sqldate, StrPath, ErrCode, dt;
   var Name1, Name2, Name3, tmp, symnom, validdate,validto, SMSPhone, EMail, sqlpass, sqlUID, sqlcodes, codes;


   GetRegistryValue(REG_EXPORT_PATH, V_STRING, StrPath, ErrCode);
   if( ErrCode > 0 )
      StrPath = "d:\\rshb_sofr\\export\\";
   end;

   sqldate = ExecSqlSelect("select trunc(t_timestamp) from utableprocessevent_dbt where t_objecttype = 5025 and t_status = 1 and t_recid = :EventId", makeArray(SqlParam("EventId", EventId)));
   sqldate.movenext();
   dt = StrSubst(string(date(sqldate.value(0))), " ", "0");
   dt = StrSubst(dt, ".", "");
   TempXML = StrPath + "in\\in_"+ClientId+"_" + dt + ".xml";
   
   xml = ActiveX("Microsoft.XMLDOM");
   xml.appendChild( xml.createProcessingInstruction("xml"," version='1.0' encoding='Windows-1251'") );
   UserData  = xml.createElement("UserData");

   command = xml.createElement("Command");
   command.setAttribute("Type", "UpdateUser");
   MakeOneElement (command, "UID", UID, xml);
   GlobalRights = xml.createElement("GlobalRights");
   GlobalRights.setAttribute("UpdateMode", "add");
   MakeOneElement (GlobalRights, "Blocked", 1, xml);
   command.appendchild(GlobalRights);
   Userdata.appendChild(command);
   xml.appendChild(UserData);
   println(string(xml.xml));
   xml.Save(TempXML);
   DisconnectObjAttr(3, string(ClientId:10:o), CategNumber, 0);
   ConnectObjAttr(err, 3, string(ClientId:10:o), CategNumber, 3);
   Return (0);
   
   OnError(er)
      DisconnectObjAttr(3, string(ClientId:10:o), CategNumber, 0);
      ConnectObjAttr(err, 3, string(ClientId:10:o), CategNumber, 2);
      return 3;
End;


macro SetPartyCodeQUIK(ClientId, _Rezult)
   var res = "OK";
   var cmd = "INSERT INTO dobjcode_dbt (T_OBJECTTYPE, T_CODEKIND, T_OBJECTID, T_CODE, T_STATE, "+
                                      " T_BANKDATE, T_SYSDATE, T_SYSTIME, T_USERID, T_BRANCH, "+ 
                                      " T_NUMSESSION, T_UNIQUE, T_BANKCLOSEDATE) "+
                          "   values (3, :codekind, :objid, :code, 0, "+
                          "           :pbankdate, :psysdate, :psystime, :poper, 0, "+
                          "           0, null, to_date('01/01/0001', 'dd/mm/yyyy') ) ";  
   cmd = RSDCommand(cmd);
   cmd.AddParam("codekind", RSDBP_IN, QuikCode);
   cmd.AddParam("objid", RSDBP_IN, ClientId);
   cmd.AddParam("code", RSDBP_IN, _Rezult);
   cmd.AddParam("pbankdate", RSDBP_IN, {curdate});
   cmd.AddParam("psysdate", RSDBP_IN, date());
   cmd.AddParam("psystime", RSDBP_IN, time());
   cmd.AddParam("poper", RSDBP_IN, {oper});
   cmd.execute;
   return res;
onError(err)
   return ("Не удалось установить код "+QuikCode+" "+err.message +" "+ getFullCmdErrorText(cmd));
end;


//Старт потока квик
macro Start_Quik_flow(ClientId, EventId, _manual, _add_note:@string)
private var isload;
private var v_manual;
if (ValType(_manual) == V_UNDEF)
  v_manual = false;
else
  v_manual = _manual;
end;
var AttrCode, BrokerCode, Rezult, Err, sqlcode, sqlContr, AttrSKEP, cmdDel;
   GetMainObjAttr(err, 3, string(ClientId:10:o), CategNumber, AttrCode);
   GetMainObjAttr(err, 3, string(ClientId:10:o), 170, BrokerCode);
   
   sqlcode = ExecSqlSelect("select * from dobjcode_dbt  where t_objecttype = 3 and T_CODEKIND = :codekind and "+
                                         "T_OBJECTID = :partyid and (T_BANKCLOSEDATE > sysdate or T_BANKCLOSEDATE = to_date('01010001', 'ddmmyyyy'))", 
                                         makeArray(SqlParam("codekind", QuikCode ),SqlParam("partyid", ClientId )));
   if (sqlcode.movenext())

      sqlContr = ExecSqlSelect("select t_dlcontrid from ddlcontr_dbt                                               "+
                               "    where t_sfcontrid in (select t_id from dsfcontr_dbt where t_partyid = :partyid) ", 
                                         makeArray(SqlParam("partyid", ClientId )));
      sqlContr.movenext();
      //Смотрим на договор, если аутентификация СКЭП - блокируем клиента
      GetMainObjAttr(err, 207, string(sqlContr.value(0):34:o), 170, AttrSKEP);
      if (AttrSKEP == 1)
         if (AttrCode == 2)
            DisconnectObjAttr(3, string(ClientId:10:o), CategNumber, 0);
            isload = Start_Quik_flow(ClientId, EventId);
         elif (AttrCode == 3)
            Rezult = Check_Correct_Response(ClientId, EventId);
            If (Rezult  !=  -1)
              DisconnectObjAttr(3, string(ClientId:10:o), CategNumber, 0);
              ConnectObjAttr(err, 3, string(ClientId:10:o), CategNumber, 1);
              return 1;
            else
               Err = Check_Error_Response(ClientId, EventId);
               if (Err == 0 )
                  return 0;
               else
                  DisconnectObjAttr(3, string(ClientId:10:o), CategNumber, 0);
                  ConnectObjAttr(err, 3, string(ClientId:10:o), CategNumber, 2);
                  return 2;
               end;
            end;
         else
            isload = Quik_User_Block(ClientId, sqlcode.value(3), EventId);
         end;
      end;

      if (AttrCode == 2)
         DisconnectObjAttr(3, string(ClientId:10:o), CategNumber, 0);
         isload = Start_Quik_flow(ClientId, EventId);
      elif (AttrCode == 3)
         Rezult = Check_Correct_Response(ClientId, EventId);
         If (Rezult  !=  -1)
           DisconnectObjAttr(3, string(ClientId:10:o), CategNumber, 0);
           ConnectObjAttr(err, 3, string(ClientId:10:o), CategNumber, 1);
           return 1;
         else
            Err = Check_Error_Response(ClientId, EventId);
            if (Err == 0 )
               return 0;
            else
               DisconnectObjAttr(3, string(ClientId:10:o), CategNumber, 0);
               ConnectObjAttr(err, 3, string(ClientId:10:o), CategNumber, 2);
               return 2;
            end;
         end;
      else
         isload = Quik_User_Create(ClientId, 2, EventId);
           //Дополнительно проверяем, а прошла ли загрузка к Брокер

               cmdDel =
                     " DECLARE "
                   + "   v_state   usr_clnt_cdwrd_buf_dbt.t_cw_status%TYPE := -1; "
                   + "   p_cw_client NUMBER(10) := :p_cw_client; "
                   + "BEGIN "
                   + "   BEGIN "
                   + "      SELECT t_cw_status "
                   + "        INTO v_state "
                   + "        FROM usr_clnt_cdwrd_buf_dbt "
                   + "       WHERE t_cw_type = 1 AND t_cw_client = p_cw_client; "
                   + "   EXCEPTION "
                   + "      WHEN NO_DATA_FOUND "
                   + "      THEN "
                   + "         v_state := -1; "
                   + "   END; "
                   + " "
                   + "   IF v_state = 6 "
                   + "   THEN "
                   + "      DELETE FROM usr_clnt_cdwrd_buf_dbt "
                   + "            WHERE t_cw_type = 1 AND t_cw_client = p_cw_client; "
                   + "   ELSIF v_state <> -1 "
                   + "   THEN "
                   + "      UPDATE usr_clnt_cdwrd_buf_dbt "
                   + "         SET t_cw_status = 6 "
                   + "       WHERE t_cw_type = 1 AND t_cw_client = p_cw_client; "
                   + "   END IF; "
                   + "END; ";
              cmdDel = RSDCommand(cmdDel);
              cmdDel.AddParam("p_cw_client", RSDBP_IN, ClientId);
              cmdDel.execute;

      end;
   else
      if (AttrCode == 3)
         Rezult = Check_Correct_Response(ClientId, EventId);
         If (Rezult  !=  -1)
           //DEF-72277 невозможно установить код 105, даже если такой же код уже закрыт
           Err = SetPartyCodeQUIK(ClientId, Rezult);
           if (Err != "OK")
             _add_note = Err;
             DisconnectObjAttr(3, string(ClientId:10:o), CategNumber, 0);
             ConnectObjAttr(err, 3, string(ClientId:10:o), CategNumber, 2);
             return 3;
           end;

           DisconnectObjAttr(3, string(ClientId:10:o), CategNumber, 0);
           ConnectObjAttr(err, 3, string(ClientId:10:o), CategNumber, 1);
   
           //Дополнительно проверяем, а прошла ли загрузка к Брокер
               cmdDel =
                     " DECLARE "
                   + "   v_state   usr_clnt_cdwrd_buf_dbt.t_cw_status%TYPE := -1; "
                   + "   p_cw_client NUMBER(10) := :p_cw_client; "
                   + "BEGIN "
                   + "   BEGIN "
                   + "      SELECT t_cw_status "
                   + "        INTO v_state "
                   + "        FROM usr_clnt_cdwrd_buf_dbt "
                   + "       WHERE t_cw_type = 1 AND t_cw_client = p_cw_client; "
                   + "   EXCEPTION "
                   + "      WHEN NO_DATA_FOUND "
                   + "      THEN "
                   + "         v_state := -1; "
                   + "   END; "
                   + " "
                   + "   IF v_state = 6 "
                   + "   THEN "
                   + "      DELETE FROM usr_clnt_cdwrd_buf_dbt "
                   + "            WHERE t_cw_type = 1 AND t_cw_client = p_cw_client; "
                   + "   ELSIF v_state <> -1 "
                   + "   THEN "
                   + "      UPDATE usr_clnt_cdwrd_buf_dbt "
                   + "         SET t_cw_status = 6 "
                   + "       WHERE t_cw_type = 1 AND t_cw_client = p_cw_client; "
                   + "   END IF; "
                   + "END; ";
              cmdDel = RSDCommand(cmdDel);
              cmdDel.AddParam("p_cw_client", RSDBP_IN, ClientId);
              cmdDel.execute;
           DisconnectObjAttr(3, string(ClientId:10:o), CategNumber, 0);
           ConnectObjAttr(err, 3, string(ClientId:10:o), CategNumber, 1);
   
           return 1;
   
         else
            Err = Check_Error_Response(ClientId, EventId);
            if (Err == 0 )
               return 0;
            else
               DisconnectObjAttr(3, string(ClientId:10:o), CategNumber, 0);
               ConnectObjAttr(err, 3, string(ClientId:10:o), CategNumber, 2);
               return 2;
            end;
         end;
      elif (AttrCode == 2)
         DisconnectObjAttr(3, string(ClientId:10:o), CategNumber, 0);
         isload = Start_Quik_flow(ClientId, EventId);
      else
         if (v_manual) // ничего не делаем
            isload = 1; // но пометим, что обработано
         else
            isload = Quik_User_Create(ClientId, 1, EventId);
         end;
       end;
   end;
return isload;
end;

//Start_Quik_flow(126708, 6127134);