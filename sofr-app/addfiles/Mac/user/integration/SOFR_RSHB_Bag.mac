import rsd, BankInter, XmlRpcInter;
import globals, cb_sql;
import func_lib;
import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import "uws_exchng_log.mac";              /* Функционал логирования */
import "RequestWS.mac";                   /* Функция передачи запроса в Web Sphere */

private file attr("objattr.dbt") key 0;

// График возврата в рублевом эквиваленте по отношению ко дню формирования отчета в соответствии со следующей разбивкой по срокам	
class c_ReturnSchedule()
  var PaymentTerm : String; // Срок Да
/* 	Возможные значения (сроки указаны включительно):
-	1 день
-	от 2 до 5 дней
-	от 6 до 7 дней
-	от 8 до 10 дней
-	от 11 до 20 дней
-	от 21 до 30 дней
-	от 31 до 90 дней
-	от 91 до 180 дней
-	от 181 до 270 дней
-	от 271 до 1 года (с учетом високосного года)
-	от 1 до 2 лет (с учетом високосного года)
-	от 2 до 3 лет (с учетом високосного года)
-	от 3 до 4 лет (с учетом високосного года)
-	от 4 до 5 лет (с учетом високосного года)
-	от 5 до 7 лет (с учетом високосного года)
-	от 7 до 10 лет (с учетом високосного года)
-	от 10 до 15 лет (с учетом високосного года)
-	от 15 до 20 лет (с учетом високосного года)
-	свыше 20 лет (с учетом високосного года) */
  var Value : string; // Сумма возврата в рублевом эквиваленте  Да
end;	

class c_Account
  var AccountNum      : String; // Номер лицевого счета        Да
  var AccountRole     : c_IntegrationDictionaryRecordXType; // Роль счета                  Да
  var AccountContract : c_IntegrationDictionaryRecordXType; // Назначение счета            Да
end;

// Набор данных о сделке для отчета rshb_bag	
class c_InformationDeal	
/*  var AccountNum      = "-";     
  var AccountRole     = c_IntegrationDictionaryRecordXType;
  var AccountContract = c_IntegrationDictionaryRecordXType;*/
  var ISIN            : String; // Код ISIN                    Усл
  var SecuritiesNum   : String; // Номер Ценной Бумаги         Усл
  var ContractNum     : String;	// Номер Договора              Да
  var TypeContract    : String; // Тип договора 	       Да
  var DateOpenContract: date;   // Дата открытия договора      Да	
  var DateEndContract : date;   // Дата по		       Да	
  var DateOpenTranche : date;   // Дата открытия транша	       Нет	
  var RatioReserve    : String; // Коэффициент резервирования  Усл	
  var QualityCategory : String; // Категория качества 	       Усл	
  var AccountList;
  var ReturnScheduleList;       // Список графиков возврата    Усл
end;

// Подразделение, из которого производится выгрузка
class c_Branch()
  var BranchId : c_IntegrationSymbolicIdentifierXType;    // Код подразделения
  var InformationDealList;  // Информация о сделках
end;

// Даты выгрузки
class c_UnloadDate()
  var DateUnload : date;  // Дата выгрузки
  var BranchList;   // Отчет в разрезе подразделений, по которым производится выгрузка
end;

// Основной класс
class c_Report                 
  var RequestId: c_IntegrationSymbolicIdentifierXType;  // Идентификатор запроса
  var UnloadDateList;     // Список дат, за которые производится выгрузка данных
end;

macro GetAccountList(_dockind, _docid)
  var sql_str, cmd, rs;
  var res = TArray;
  var cc;

  var accfile = TBFile("account.dbt", "r", 0);
  var ObjectType;
  var ObjectID;
  var NOTEKIND = 112;

  sql_str = 
    " select  "+
    "      mcacc.t_dockind, mcacc.t_docid, mcacc.t_activatedate, "+
    "      mcacc.t_account, mcacc.t_catid, mcacc.t_catnum, "+
    "      mccat.t_code, mccat.t_name, mccat.t_updatemode, "+
    "      mcacc.t_chapter, mcacc.t_currency "+
    "   from "+
    "      dmcaccdoc_dbt mcacc, "+
    "      dmccateg_dbt mccat "+
    "  where mcacc.t_dockind = :dockind and mcacc.t_docid = :docid "+
    "    and mccat.t_id = mcacc.t_catid and t_updatemode in (0,1) ";
  cmd = RSDCommand(sql_str);
  cmd.AddParam("dockind", RSDBP_IN, _dockind);
  cmd.AddParam("docid", RSDBP_IN, _docid);

  rs = RSDRecordSet(cmd);
  while (rs.movenext)
    accfile.rec.chapter = rs.value("t_chapter");
    accfile.rec.account = rs.value("t_account");
    accfile.rec.code_currency = rs.value("t_currency");
    ObjectType = OBJTYPE_ACCOUNT;
    ObjectID = UniID(accfile, ObjectType);

    cc = c_Account;
    cc.AccountNum      = rs.value("t_account");     

    cc.AccountRole     = c_IntegrationDictionaryRecordXType;
    cc.AccountRole.RecordCode     = rs.value("t_code");
    cc.AccountRole.RecordTitle    = "";
    cc.AccountRole.Note           = "";
    cc.AccountRole.SystemId       = "SOFR";
    cc.AccountRole.SystemNodeId   = "SOFR";
    cc.AccountRole.Desc           = "";
    cc.AccountRole.DictionaryCode = "";
    
    cc.AccountContract = c_IntegrationDictionaryRecordXType;
    cc.AccountContract.RecordCode     = readNoteForObject( ObjectType, ObjectID, NOTEKIND );
    if (not cc.AccountContract.RecordCode)
      cc.AccountContract.RecordCode = "не_заполнено";
    end;
    cc.AccountContract.RecordTitle    = "";
    cc.AccountContract.Note           = "";
    cc.AccountContract.SystemId       = "SOFR";
    cc.AccountContract.SystemNodeId   = "SOFR";
    cc.AccountContract.Desc           = "";
    cc.AccountContract.DictionaryCode = "";

    res.value(res.size) = cc;
  end;

  return res;
 
end;

macro GetRatioReserve(_num_acc, _accfiid, _fiid, _iss, prc:@variant, num_quality:@string)
   var accfile = TBFile("account.dbt", "r", 0);
   var ObjectType;
   var ObjectID;
   var NOTEKIND = 3;
   var CATEGORY = 13;
   var ObjCat;

   accfile.rec.chapter = 1;
   accfile.rec.account = _num_acc;
   accfile.rec.code_currency = _accfiid;
   ObjectType = OBJTYPE_ACCOUNT;
   ObjectID = UniID(accfile, ObjectType);

   if (accfile.getEQ())
      prc = readNoteForObject( ObjectType, ObjectID, NOTEKIND );

      ObjCat = RsbObjCategories( objecttype, ObjectID );
      if (ObjCat.GetMainAttr( CATEGORY, {curdate}, attr ) == 0)
         num_quality = attr.Numinlist;
      end;
   end;

   if (not prc)
      ObjectType = OBJTYPE_AVOIRISS;
      ObjectID = String(_fiid:o:10);
      prc = readNoteForObject( ObjectType, ObjectID, NOTEKIND );
   end;

   if (not num_quality)
      ObjectType = OBJTYPE_AVOIRISS;
      ObjectID = String(_fiid:o:10);

      ObjCat = RsbObjCategories( objecttype, ObjectID );
      if (ObjCat.GetMainAttr( CATEGORY, {curdate}, attr ) == 0)
         num_quality = attr.Numinlist;
      end;
   end;

   if (not prc)
      ObjectType = OBJTYPE_PARTY;
      ObjectID = String(_iss:o:10);
      prc = readNoteForObject( ObjectType, ObjectID, NOTEKIND );
   end;

   if (not num_quality)
      ObjectType = OBJTYPE_PARTY;
      ObjectID = String(_iss:o:10);

      ObjCat = RsbObjCategories( objecttype, ObjectID );
      if (ObjCat.GetMainAttr( CATEGORY, {curdate}, attr ) == 0)
         num_quality = attr.Numinlist;
      end;
   end;

end;



/* MAIN */
macro FillBranch(_dep, _repdate) 
  var sql_str, rs, cmd;
  var bag;
  var branch = c_Branch;
  var acc;
  var AccountList;
  var prc, num_quality;

  branch.BranchID = c_IntegrationSymbolicIdentifierXType;
  if (_dep == "1")
    branch.BranchID.ObjectId = "0000";
  else
    branch.BranchID.ObjectId = _dep;
  end;
  branch.BranchID.SystemId = "SOFR";
  branch.BranchID.SystemNodeId = "SOFR";

  branch.InformationDealList = TArray;

  sql_str = 
     "select                     "+
     "    tick.t_dealid dealid,  "+
     "    avoir.t_isin isin,     "+
     "    avoir.t_lsin lsin,     "+
     "    fi.t_fiid,             "+
     "    fi.t_issuer,           "+
     "    tick.t_dealcode dealcode,     "+
     "    tick.t_dealcodets dealcodets, "+
     "    opr.t_name  name,             "+
     "    tick.t_dealdate dealdate,     "+
     "    (select max(leg.t_maturity) from ddl_leg_dbt leg where leg.t_dealid = tick.t_dealid) maturity_date, "+
     "    tick.t_bofficekind dockind,   "+
     "    tick.t_dealid docid           "+
     " from                             "+
     "    ddl_tick_dbt tick             "+
     "    , DAVOIRISS_DBT avoir         "+
     "    , dfininstr_dbt fi            "+
     "    , DOPRKOPER_DBT opr           "+
     "where                             "+
     "    tick.t_department = :dep      "+
     "    and tick.t_dealdate = :dt     "+
     "    and tick.t_pfi = avoir.t_fiid(+)            "+
     "    and tick.t_dealtype = opr.t_kind_operation  "+
     "    and fi.t_fiid = avoir.t_fiid                "+
     "union all                                       "+
     "select                                  "+
     "    d.t_id,                             "+
     "    null,                               "+
     "    null,                               "+
     "    null,                               "+
     "    null,                               "+
     "    d.t_code,                           "+
     "    d.t_extcode,                        "+
     "    opr.t_name,                         "+
     "    d.t_date,                           "+
     "    (select max(t_execdate) from ddvnfi_dbt  dd where dd.t_dealid = d.t_id) maturity_date, "+
     "    d.t_dockind,                        "+
     "    d.t_id                              "+
     " from                                   "+
     "    ddvndeal_dbt d                      "+
     "    , DOPRKOPER_DBT opr                 "+
     "where                                   "+
     "   d.t_department = :dep                "+
     "   and d.t_date = :dt                   "+
     "   and d.t_kind = opr.t_kind_operation  ";
  cmd = RSdCommand(sql_str);
  cmd.AddParam("dep1", RSDBP_IN, _dep);
  cmd.AddParam("dt1",  RSDBP_IN, _repdate);
  cmd.AddParam("dep2", RSDBP_IN, _dep);
  cmd.AddParam("dt2",  RSDBP_IN, _repdate);

  rs = RSDRecordSet(cmd);
  while (rs.movenext)
    bag = c_InformationDeal;
    AccountList = GetAccountList(rs.value("dockind"), rs.value("docid"));
    if ( (ValType(AccountList) == V_GENOBJ) and (AccountList.size > 0) )
       /*for (acc, AccountList)
         bag.AccountNum      = acc.AccountNum;     
         bag.AccountRole     = acc.AccountRole;
         bag.AccountContract = acc.AccountContract;
       end;*/

       bag.ISIN            = SQL_ConvTypeStr(rs.value("isin"));
       if (not bag.ISIN)
          bag.ISIN = NULL;
       end;
       bag.SecuritiesNum   = SQL_ConvTypeStr(rs.value("lsin"));
       if (not bag.SecuritiesNum)
          bag.SecuritiesNum = NULL;
       end;
       bag.ContractNum     = SQL_ConvTypeStr(rs.value("dealcode"));
       bag.TypeContract    = SQL_ConvTypeStr(rs.value("name"));
       bag.DateOpenContract= SQL_ConvTypeDate(rs.value("dealdate"));
       bag.DateEndContract = SQL_ConvTypeDate(rs.value("maturity_date"));
       bag.DateOpenTranche = null;

       GetRatioReserve("", "", rs.value("t_fiid"), rs.value("t_issuer"), @prc, @num_quality);
       if (prc)
          bag.RatioReserve = prc;
       else
          bag.RatioReserve = null;
       end;
       if (num_quality)
          bag.QualityCategory = num_quality;
       else
          bag.QualityCategory = null;
       end;

       bag.AccountList     = AccountList;
       bag.ReturnScheduleList = null;

       branch.InformationDealList.value(branch.InformationDealList.size) = bag;

    end;
    bag = null;

  end;

  return branch;
end;


macro c_SOFRBag()
  var dep = "1";
  var repdate = {curdate};

  var rep = c_Report;
  var UnloadDate;

  rep.RequestID = c_IntegrationSymbolicIdentifierXType;
  rep.RequestID.ObjectId = CreateGUID();
  rep.RequestID.SystemId = "SOFR";
  rep.RequestID.SystemNodeId = "SOFR";

  rep.UnloadDateList = TArray; 
  rep.UnloadDateList.size = 0;

    UnloadDate = c_UnloadDate;
    UnloadDate.DateUnload = repdate;
    UnloadDate.BranchList = TArray;

      UnloadDate.BranchList.value(UnloadDate.BranchList.size) = FillBranch(dep, UnloadDate.DateUnload);

  rep.UnloadDateList.value(rep.UnloadDateList.size) = UnloadDate;
  return rep;

end;


private class c_SendRSHBBagRequest
   var SendRSHBBagRequest;
end;


/* Основной класс работы с сервисом */
private class c_SOFRRSHBBagRequestMsgProcessor()
   var SendRSHBBagRequestMsg; /* !!! По названию корневого тега запроса !!! */
   SendRSHBBagRequestMsg = c_SendRSHBBagRequest();
end; 


macro run_SOFRBag()
   var RespData = NULL;
   var err_txt = NULL;
   
   private var v_out_obj;
   private var RequestText;
   private var v_transformator;
   private var v_answer;
   private var v_answer_data;
   private var v_logger_obj;
   private var v_log_id;
   private var xml_str;
   private var v_xml_rpc_reqid = XmlRpcRequestId;
   private var p_transparent_id = NULL;


   v_transformator = c_secur_msg_converter();

   // заполнение класса информацией
   v_out_obj = c_SOFRRSHBBagRequestMsgProcessor;
   v_out_obj.SendRSHBBagRequestMsg.SendRSHBBagRequest = c_SOFRBag;
   RequestText = v_transformator.m_secur_internal_resp(v_out_obj);
   
   v_logger_obj = uws_exchng_logger(null, null, v_xml_rpc_reqid, "SendRSHBBagRequestMsg", modulename(), p_transparent_id, RequestText);
   v_log_id = v_logger_obj.get_log_id();

      
   err_txt = v_transformator.m_validate_out_xml(RequestText);
   v_transformator = NULL;
            
   if (err_txt == NULL)
      v_answer = SubmitRequestToWS( 2,              // ИД очереди.
                                    "",             // ИД сообщения.
                                    "",             // Идентификатор Бэк Офиса. (не обрабатывается).
                                    false,           // Признак синхронной обработки. Если не указан, = False
                                    "SOFR",         // Подразделение системы-назначения.
                                    1,              // ИД типа данных
                                    RequestText,    // Бизнес данные в виде XML.
                                    v_xml_rpc_reqid // 20181229 - kva - пока передаем reqid - больше нечего здесь взять
                                    );

         if (v_answer.ResultCode == 0)

         else 
            v_logger_obj.add_error_info(v_answer.ResultCode, v_answer.ResultText);
         end;
   else
      v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, err_txt);
   end;
         
   return RespData;

onError(err)

end; 

       
run_SOFRBag;   