//-----------------------------------------------------------------------------------------
// Получение ответа СОФР результата обработки данных по проводкам
// В.Горленко
//-----------------------------------------------------------------------------------------
import global_utils_intgr, email_notifyfun, likepy;

Private const EMAIL_SUBJECT = "Ошибка. Загрузка данных по проводкам";
Private var EMAIL_RECEIVER = "gorlenko@nsk.softlab.ru";
//-----------------------------------------------------------------------------------------

Private macro getProperty ( obj, property )
    if ( genPropId (obj, property) < 0 )
        return NULL;
    end;

    return uTryToGetPropS (obj, property);
onError
    return NULL;
End;
//-----------------------------------------------------------------------------------------

Private macro sendMail ( email, subject, body );
    AddNotifyToDbt(subject, body, email);
    SendNotyfyToEmail ();

    return TRUE;
OnError
    return FALSE;
End;
//-----------------------------------------------------------------------------------------

Macro addAccountingEntries_resp ( response )
    Var trnResult = getProperty (response, "TransactionResult"), transactionId, entryResult, responseCode, responseNote, mailBody = "";
    if ( (trnResult == NULL) or (valType (trnResult) != V_GENOBJ) )
        return false;
    end;

    if ( ((entryResult = getProperty (response, "EntryResultList")) == NULL) or (not isEqClass ("tarray", entryResult)) )
        return false;
    end;

    transactionId = getProperty (trnResult, "TransactionID");

    for (var entry, entryResult)
        responseCode = getProperty (entry, "responseCode");
        if ( (responseCode == NULL) or (int (responseCode) == 0) )
            continue;
        end;
        responseNote = getProperty (entry, "responseNote");
        if ( mailBody == "" )
            mailBody = "addAccountingEntries:\ntransactionId: "+transactionId;
        end;
        for ( Var prop, makeArray ("ReferenceId", "entryNumber", "entryId", "currencyStatus") )
            if ( getProperty (entry, prop) )
                mailBody = mailBody + "\n\t"+prop+": "+getProperty (entry, prop);
            end;
        end;
        mailBody = mailBody + "\n\tКод ошибки: "+responseCode+ifThenElse (responseNote, " ("+responseNote+")", "");
    end;

    if ( mailBody )
        if ( not (EMAIL_RECEIVER) )
            return false;
        end;

        return sendMail (EMAIL_RECEIVER, EMAIL_SUBJECT, mailBody);
    end;
    return true;
End;
//-----------------------------------------------------------------------------------------
