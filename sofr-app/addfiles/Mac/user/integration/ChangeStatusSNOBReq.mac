/*
$Name:        ChangeStatusSNOBReq.mac
$Module:      Интеграция с внешними системами
$Description: Запрос на изменение статуса записи в СНОБ
*/
/* BIQ-7294 16.11.2022 Гераськина Т.*/

import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import "RequestWS.mac";                   /* Функция передачи запроса в Web Sphere */
import "uws_exchng_log.mac";              /* Функционал логирования */
import func_lib;

// Информация о записи
class c_ChangeStatusInfo
   var RecordId;     // ID записи в хранилище СНОБ    Да    Идентификатор
   var RecordStatus; // Статус записи                 Да    Справочник
end;

// Запрос на изменения статуса записи в СНОБ
class c_ChangeStatusSNOBReq
   var ChangeStatusInfoList;  //    Информаия об изменении статуса записей в СНОБ   Да    Список
end;

/* Основной класс работы с сервисом */
class c_ChangeStatusSNOBRequest()
   var ChangeStatusSNOBReq; /* !!! По названию корневого тега запроса !!! */
   ChangeStatusSNOBReq = c_ChangeStatusSNOBReq;
end;


//Результат обработки статуса записи
class c_ChangeStatusInfoResp
   var RecordId;        // Id записи в хранилище СНОБ    Да   Идентификатор
   var RecordStatus;    // Статус записи                 Да   Справочник
   var Error;           // Ошибка                        Да
end;

/* Ответ на запрос изменения статуса записи в СНОБ */
class adp_ChangeStatusSNOBResponse(IncomeData)
   var ChangeStatusInfoList;  // Результаты обработки информации по измнению статусов записей    Нет
   var ErrorList;             // Список ошибок                                                   Да

   /* Кратность параметров в соответствии со схемой/спецификацией */
   private macro uInitPrime(IncomeData)
      var err_txt;
      var aux_buff, aux_buff2, ai;
      var temp;
      var temp_error;
      
      var nametag_up = "ChangeStatusSNOBResp";
      
      if(ValType(IncomeData) != V_UNDEF)
         aux_buff = getValueFromProperty(IncomeData, "ChangeStatusInfoList", nametag_up);
         if (ValType(aux_buff) == V_GENOBJ)
            nametag_up = nametag_up+".ChangeStatusInfoList";

            ChangeStatusInfoList = TArray;
            ai = 0;
            nametag_up = nametag_up+".ChangeStatusInfo";
            while(IncomeData.ChangeStatusInfoList.size > ai)
               temp = c_ChangeStatusInfoResp;
               // DEF-49746 не имеет смысла проверять обязательность полей ответа. Все равно мы с этим ничего уже не сделаем
               temp.RecordId     = getSymbolFromProperty(IncomeData.ChangeStatusInfoList.value(ai),      "RecordId"     , nametag_up);
               temp.RecordStatus = getDictionaryFromProperty(IncomeData.ChangeStatusInfoList.value(ai),  "RecordStatus" , nametag_up);
               temp_Error        = getValueFromProperty(IncomeData.ChangeStatusInfoList.value(ai),       "Error"        , nametag_up);
               temp.Error = c_Error;
               temp.Error.ErrorCode = getValueFromProperty(temp_Error, "ErrorCode", nametag_up);
               temp.Error.ErrorDesc = getValueFromProperty(temp_Error, "ErrorDesc", nametag_up);

               ChangeStatusInfoList.value(ai) = temp;
               ai = ai + 1;
               temp = NULL;
               temp_Error = NULL;
            end;
         elif(ValType(aux_buff2) == V_UNDEF)
         else
            err_txt = string(InErrNotObj, nametag_up+".ChangeStatusInfoList");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
         end;

         nametag_up = "ChangeStatusSNOBResp";
         aux_buff = uTryToGetPropS(IncomeData, "ErrorList");
         if(ValType(aux_buff) == V_GENOBJ)
            ErrorList = TArray;
            aux_buff = 0;
            nametag_up = nametag_up+".ErrorList";
            while(IncomeData.ErrorList.size > aux_buff)
               temp_Error = c_Error;
               temp_Error.ErrorCode = getValueFromProperty(IncomeData.ErrorList.value(aux_buff), "ErrorCode", nametag_up);
               temp_Error.ErrorDesc = getValueFromProperty(IncomeData.ErrorList.value(aux_buff), "ErrorDesc", nametag_up);
               
               ErrorList.value(aux_buff) = temp_Error;
               aux_buff = aux_buff + 1;
               temp_Error = NULL;
            end;
         elif(ValType(aux_buff) == V_UNDEF)
         else
            err_txt = string(InErrNotObj, nametag_up+".ErrorList");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
         end;
      end;
   end;
   
   uInitPrime(IncomeData);
end;

private macro MakeErrorAnswer(pcode, ptext)
   var res = adp_ChangeStatusSNOBResponse;
   res.ErrorList = TArray;
   res.ErrorList.value(0) = c_Error;
   res.ErrorList.value(0).ErrorCode = pcode;
   res.ErrorList.value(0).ErrorDesc = ptext;

   return res;
end;

/*------------------------------------------------------------------------*/
/* Адаптер для вызова сервиса "Запрос на изменение статуса записи в СНОБ" */
/*  считаем, что ReqData заполняется вовне                                */
/*------------------------------------------------------------------------*/
macro run_ChangeStatusSNOBReq(ReqData)
   var RespData = NULL;
   var err_txt = NULL;
   
   private var RequestText;
   private var v_transformator;
   private var v_answer;
   private var v_answer_data;
   private var v_logger_obj;
   private var v_log_id;
   private var xml_str;
   private var v_guid = CreateGUID();
   private var v_xml_rpc_reqid = XmlRpcRequestId(); 
   
   private var V_WS_TYPE = GetNumDataType("ChangeStatusSNOB");

   var v_timeout;
   var stat;
   GetRegistryValue(REG_TIMEOUT_SNOB, V_INTEGER, v_timeout, stat);
   if (stat>0)
      v_timeout = 15;
   end;

   v_transformator = c_secur_msg_converter();

   // заполнение класса информацией 
   RequestText = v_transformator.m_secur_internal_resp(ReqData);
   
   v_logger_obj = uws_exchng_logger(null, v_guid, v_xml_rpc_reqid, "ChangeStatusSNOBReq", modulename(), null, RequestText);
   v_log_id = v_logger_obj.get_log_id();


   if (err_txt == NULL)
      v_answer = SubmitRequestToWS( C_QUEUE_OUT_ID,  // ИД очереди.
                                    "",              // ИД сообщения.
                                    "",              // Идентификатор Бэк Офиса. (не обрабатывается).
                                    true,            // Признак синхронной обработки. Если не указан, = False
                                    "SOFR",          // Подразделение системы-отпр
                                    V_WS_TYPE,       // ИД типа данных
                                    RequestText,     // Бизнес данные в виде XML
                                    v_log_id,        // ID usr_exchng_log
                                    "EPRSHB2",
                                    v_timeout
                                       );
      if (v_answer.ResultCode == 0)
         xml_str = v_answer.responsetext;
         v_logger_obj.add_response(xml_str); 

         v_transformator = NULL;
         if (xml_str)
            /* Преобразуем запрос в объектный вид - begin */
            v_transformator = c_secur_msg_converter();
            if(not v_transformator.m_decode_escaped_char(xml_str))
               RunError(v_transformator.get_service_msg(), c_usr_err_obj(v_transformator.get_service_msg()));
            end;
            v_answer_data = v_transformator.m_secur_wol_internal_req(v_transformator.get_pure_msg());
            /* Преобразуем запрос в объектный вид - end */

            if(ValType(v_answer_data) == V_UNDEF)
               err_txt = "Не удалось преобразовать XML в объект";
               RunError(err_txt, c_usr_err_obj(err_txt)); 
            end;
            
            RespData = adp_ChangeStatusSNOBResponse(v_answer_data);
         end;
      else 
         v_logger_obj.add_error_info(v_answer.ResultCode, v_answer.ResultText);
         RespData = MakeErrorAnswer(v_answer.ResultCode, v_answer.ResultText);
      end;

   else
      v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, err_txt);
      RespData = MakeErrorAnswer(UNIVERSAL_ERROR_CODE, err_txt);
   end;

   return RespData;

onError(err)
   stat = c_err_obj.obtain_err_code(err);
   err_txt = c_err_obj.obtain_err_msg(err);

   RespData = MakeErrorAnswer(stat, err_txt);
   return RespData;
end;


/*---------------------------------------*/
/*              Точка входа              */
/*---------------------------------------*/
/* ReqData - данные для отправки         */
/*---------------------------------------*/
macro ChangeStatusSNOBReq (ReqData)
   return run_ChangeStatusSNOBReq(ReqData);
end;