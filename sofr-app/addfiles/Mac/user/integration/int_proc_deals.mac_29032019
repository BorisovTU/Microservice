/*-----------------------------*/
/* Интерфейс полученных сделок */
/*                             */
/* Автор: Карпов В.            */
/*-----------------------------*/

import RsbFormsInter;
import "int_usr_globals.mac";
//import rcw; // Для отладки


/*-----------------------*/
/* Глобальные переменные */
/*-----------------------*/
private var gv_deals_sel_prm;              /* Параметры выборки событий */

private var gv_column_list_scr = TArray(); /* Список колонок скроллинга */
private var gv_num_columns_scr = 0;        /* Количество колонок скроллинга */
private var gv_scr_focus = 0;              /* Номер позиции скроллинга */


/*-----------------------------------*/
/* Выгрузка сообщения из dxr_log_dbt */
/*-----------------------------------*/
macro m_make_xr_log_msg(p_reqid, p_kind)
   var v_aux_buf = "";
   var v_sql, v_cmd;
   var v_rd_stat = true;
   var v_clob_offset = 1; /* Начальное смещение в clob'е - начинаем с первого символа */
   var v_msg_str = "";    /* Сообщение */

   v_sql =         "DECLARE ";
   v_sql = v_sql + "    v_msg_loc    CLOB; ";
   v_sql = v_sql + "    v_msg_id     dxr_log_dbt.t_reqid%TYPE := :p_msg_id; ";
   v_sql = v_sql + "    v_kind       dxr_log_dbt.t_kind%TYPE := :p_kind; ";
   v_sql = v_sql + "    v_msg_offset INTEGER := :p_msg_offset; ";
   v_sql = v_sql + "    v_msg_read   BINARY_INTEGER := 1000; ";
   v_sql = v_sql + "    v_msg_part   VARCHAR2 (2000 Char); ";
   v_sql = v_sql + "    v_ret_length BINARY_INTEGER; ";
   v_sql = v_sql + "BEGIN ";
   v_sql = v_sql + "    BEGIN ";
   v_sql = v_sql + "        SELECT t_message ";
   v_sql = v_sql + "          INTO v_msg_loc ";
   v_sql = v_sql + "          FROM dxr_log_dbt ";
   v_sql = v_sql + "         WHERE t_reqid = v_msg_id ";
   v_sql = v_sql + "           AND t_kind = v_kind; ";

   v_sql = v_sql + "        DBMS_LOB.read (v_msg_loc, ";
   v_sql = v_sql + "                       v_msg_read, ";
   v_sql = v_sql + "                       v_msg_offset, ";
   v_sql = v_sql + "                       v_msg_part); ";
   v_sql = v_sql + "        v_ret_length := v_msg_read; ";

   v_sql = v_sql + "    EXCEPTION ";
   v_sql = v_sql + "        WHEN NO_DATA_FOUND ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            v_ret_length := 0; ";
   v_sql = v_sql + "        WHEN OTHERS ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            v_ret_length := 0; ";
   v_sql = v_sql + "    END; ";

   v_sql = v_sql + "    :p_ret_length := v_ret_length; ";
   v_sql = v_sql + "    :p_ret_msg_part := v_msg_part; ";
   v_sql = v_sql + "END; ";

   while(v_rd_stat)
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_msg_id", RSDBP_IN, p_reqid);
      v_cmd.addParam("p_kind", RSDBP_IN, p_kind);
      v_cmd.addParam("p_msg_offset", RSDBP_IN, v_clob_offset);
      v_cmd.addParam("p_ret_length", RSDBP_OUT, V_INTEGER);
      v_cmd.addParam("p_ret_msg_part", RSDBP_OUT, V_STRING, 2000);
      v_cmd.execute();

      if(v_cmd.value("p_ret_length") > 0)
         v_msg_str = string(v_msg_str, v_cmd.value("p_ret_msg_part"));
         v_clob_offset = v_clob_offset + v_cmd.value("p_ret_length");

         v_rd_stat = true;
         v_cmd.close(); v_cmd = null;
      else
         v_rd_stat = false;
         v_cmd.close(); v_cmd = null;
      end;
   end;

   v_aux_buf = m_make_file_with_msg(v_msg_str);
   if(v_aux_buf != "")
      msgbox(string("Создан файл: ", v_aux_buf));
   else
      msgbox("Ошибка: файл не создан");
   end;

onError(err)
   msgbox("Ошибка: файл не создан");
end; /* macro m_make_xr_log_msg() */


/*------------------------------------------*/
/* Выгрузка сообщения из usr_exchng_log_dbt */
/*------------------------------------------*/
macro m_make_usr_log_msg(p_log_id, p_kind)
   var v_aux_buf = "";
   var v_sql, v_cmd;
   var v_rd_stat = true;
   var v_clob_offset = 1; /* Начальное смещение в clob'е - начинаем с первого символа */
   var v_msg_str = "";    /* Сообщение */

   v_sql =         "DECLARE ";
   v_sql = v_sql + "    v_msg_loc    CLOB; ";
   v_sql = v_sql + "    v_msg_id     usr_exchng_log_dbt.t_id%TYPE := :p_msg_id; ";
   v_sql = v_sql + "    v_msg_offset INTEGER := :p_msg_offset; ";
   v_sql = v_sql + "    v_msg_read   BINARY_INTEGER := 1000; ";
   v_sql = v_sql + "    v_msg_part   VARCHAR2 (2000 Char); ";
   v_sql = v_sql + "    v_ret_length BINARY_INTEGER; ";
   v_sql = v_sql + "BEGIN ";
   v_sql = v_sql + "    BEGIN ";
   if(p_kind == 1) /* Запрос */
      v_sql = v_sql + "     SELECT t_req_message ";
   elif(p_kind == 3) /* Ответ */
      v_sql = v_sql + "     SELECT t_resp_message ";
   else
      RunError("Непредусмотренный тип сообщения", -10101);
   end;
   v_sql = v_sql + "          INTO v_msg_loc ";
   v_sql = v_sql + "          FROM usr_exchng_log_dbt ";
   v_sql = v_sql + "         WHERE t_id = v_msg_id; ";

   v_sql = v_sql + "        DBMS_LOB.read (v_msg_loc, ";
   v_sql = v_sql + "                       v_msg_read, ";
   v_sql = v_sql + "                       v_msg_offset, ";
   v_sql = v_sql + "                       v_msg_part); ";
   v_sql = v_sql + "        v_ret_length := v_msg_read; ";

   v_sql = v_sql + "    EXCEPTION ";
   v_sql = v_sql + "        WHEN NO_DATA_FOUND ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            v_ret_length := 0; ";
   v_sql = v_sql + "        WHEN OTHERS ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            v_ret_length := 0; ";
   v_sql = v_sql + "    END; ";

   v_sql = v_sql + "    :p_ret_length := v_ret_length; ";
   v_sql = v_sql + "    :p_ret_msg_part := v_msg_part; ";
   v_sql = v_sql + "END; ";

   while(v_rd_stat)
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_msg_id", RSDBP_IN, p_log_id);
      v_cmd.addParam("p_msg_offset", RSDBP_IN, v_clob_offset);
      v_cmd.addParam("p_ret_length", RSDBP_OUT, V_INTEGER);
      v_cmd.addParam("p_ret_msg_part", RSDBP_OUT, V_STRING, 2000);
      v_cmd.execute();

      if(v_cmd.value("p_ret_length") > 0)
         v_msg_str = string(v_msg_str, v_cmd.value("p_ret_msg_part"));
         v_clob_offset = v_clob_offset + v_cmd.value("p_ret_length");

         v_rd_stat = true;
         v_cmd.close(); v_cmd = null;
      else
         v_rd_stat = false;
         v_cmd.close(); v_cmd = null;
      end;
   end;

   v_aux_buf = m_make_file_with_msg(v_msg_str);
   if(v_aux_buf != "")
      msgbox(string("Создан файл: ", v_aux_buf));
   else
      msgbox("Ошибка: файл не создан");
   end;

onError(err)
   msgbox("Ошибка: файл не создан");
end; /* macro m_make_usr_log_msg() */


/*---------------------------*/
/* Параметры выборки событий */
/*---------------------------*/
private class c_deals_selection_prm()
   var v_com_date;
   var v_com_beg_date;
   var v_com_end_date;
   var v_com_date_switch;

   var v_com_is_err_filtr;
   var v_com_err_code;
   var v_com_err_inv;

   var v_com_deal_num;


   /* Значения по умолчанию */
   private macro m_init_deals_selection_prm()
      v_com_date = date();
      v_com_beg_date = date();
      v_com_end_date = date();
      v_com_date_switch = false;

      v_com_is_err_filtr = false;
      v_com_err_code = "0";
      v_com_err_inv = false;

      v_com_deal_num = "0";
   end; /* macro m_init_deals_selection_prm() */

   m_init_deals_selection_prm();
end; /* class c_deals_selection_prm() */


/*----------------------------------------*/
/* Класс формы общих поисковых параметров */
/*----------------------------------------*/
class (TRsbPanel) c_com_search_form()
   const CV_POS_X = 5;
   const CV_POS_Y = 2;
   var v_com_date_fld;
   var v_com_beg_date_fld;
   var v_com_end_date_fld;
   var v_com_date_switch_fld;

   var v_com_is_err_filtr_fld;
   var v_com_err_code_fld;
   var v_com_err_inv_fld;

   var v_com_deal_num_fld;

   var v_com_null_btn;


   macro m_evnt_on_key_pressed(p_rsb_evnt)
      if(p_rsb_evnt.KeyCode == K_F2)
         close(CV_CLOSE_FORM_OK);
      end;

      return true;
   end; /* macro m_evnt_on_key_pressed(p_rsb_evnt) */


   macro m_on_switch_date(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_CONTROL_CHECKED)
         gv_deals_sel_prm.v_com_date_switch = v_com_date_switch_fld.checked;
      end;

      if(gv_deals_sel_prm.v_com_date_switch)
         v_com_date_fld.focusable = false;
         v_com_date_fld.enabled = false;
         v_com_date_fld.editable = false;

         v_com_beg_date_fld.focusable = true;
         v_com_beg_date_fld.enabled = true;
         v_com_beg_date_fld.editable = true;

         v_com_end_date_fld.focusable = true;
         v_com_end_date_fld.enabled = true;
         v_com_end_date_fld.editable = true;
      else
         v_com_date_fld.focusable = true;
         v_com_date_fld.enabled = true;
         v_com_date_fld.editable = true;

         v_com_beg_date_fld.focusable = false;
         v_com_beg_date_fld.enabled = false;
         v_com_beg_date_fld.editable = false;

         v_com_end_date_fld.focusable = false;
         v_com_end_date_fld.enabled = false;
         v_com_end_date_fld.editable = false;
      end;

      redraw();

      return true;
   end; /* macro m_on_switch_date() */


   macro m_on_checked_is_err_filtr(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_CONTROL_CHECKED)
         gv_deals_sel_prm.v_com_is_err_filtr = v_com_is_err_filtr_fld.checked;

         if(gv_deals_sel_prm.v_com_is_err_filtr)
            v_com_err_code_fld.focusable = true;
            v_com_err_code_fld.enabled = true;
            v_com_err_code_fld.editable = true;
         else
            v_com_err_code_fld.focusable = false;
            v_com_err_code_fld.enabled = false;
            v_com_err_code_fld.editable = false;
         end;

         redraw();
      end;

      return true;
   end; /* macro m_on_checked_is_err_filtr() */


   macro m_on_checked_err_inv(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_CONTROL_CHECKED)
         gv_deals_sel_prm.v_com_err_inv = v_com_err_inv_fld.checked;
      end;

      return true;
   end; /* macro m_on_checked_err_inv() */


   macro m_deals_null_com_btn(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_BUTTON_CLICKED)
         gv_deals_sel_prm.v_com_date = date("00.00.0000");
         v_com_date_fld.value = date("00.00.0000");

         gv_deals_sel_prm.v_com_beg_date = date("00.00.0000");
         v_com_beg_date_fld.value = date("00.00.0000");

         gv_deals_sel_prm.v_com_end_date = date("00.00.0000");
         v_com_end_date_fld.value = date("00.00.0000");

         v_com_date_fld.focusable = true;
         v_com_date_fld.enabled = true;
         v_com_date_fld.editable = true;

         v_com_beg_date_fld.focusable = false;
         v_com_beg_date_fld.enabled = false;
         v_com_beg_date_fld.editable = false;

         v_com_end_date_fld.focusable = false;
         v_com_end_date_fld.enabled = false;
         v_com_end_date_fld.editable = false;

         v_com_date_switch_fld.checked = false;
         gv_deals_sel_prm.v_com_date_switch = false;

         v_com_is_err_filtr_fld.checked = false;
         gv_deals_sel_prm.v_com_is_err_filtr = false;
         gv_deals_sel_prm.v_com_err_code = "0";
         v_com_err_code_fld.value = "0";

         v_com_err_code_fld.focusable = false;
         v_com_err_code_fld.enabled = false;
         v_com_err_code_fld.editable = false;

         v_com_err_inv_fld.checked = false;
         gv_deals_sel_prm.v_com_err_inv = false;

         gv_deals_sel_prm.v_com_deal_num = "0";
         v_com_deal_num_fld.value = "0";

         redraw();
      end;

      return true;
   end; /* macro m_deals_null_com_btn() */


   private macro m_init_com_search_form(i_this_obj)
      Caption = "Общие фильтры";
      Status = "[Esc] Выход  [F2] Применить";

      /* Фильтр по дате */
      AddLabel(TRsbLabel(CV_POS_X, CV_POS_Y, "Дата выборки"));
      v_com_date_fld = TRsbEditField(FT_DATE);
      v_com_date_fld.bindValue(gv_deals_sel_prm, "v_com_date", 10);
      v_com_date_fld.setPosition(CV_POS_X, (CV_POS_Y + 1));
      v_com_date_fld.setSize(10, 1);
      if(gv_deals_sel_prm.v_com_date_switch)
         v_com_date_fld.focusable = false;
         v_com_date_fld.enabled = false;
         v_com_date_fld.editable = false;
      else
         v_com_date_fld.focusable = true;
         v_com_date_fld.enabled = true;
         v_com_date_fld.editable = true;
      end;
      AddControl(v_com_date_fld);
      AddLabel(TRsbLabel((CV_POS_X + 27), CV_POS_Y, "Диапазон дат"));
      v_com_beg_date_fld = TRsbEditField(FT_DATE);
      v_com_beg_date_fld.bindValue(gv_deals_sel_prm, "v_com_beg_date", 10);
      v_com_beg_date_fld.setPosition((CV_POS_X + 27), (CV_POS_Y + 1));
      v_com_beg_date_fld.setSize(10, 1);
      if(gv_deals_sel_prm.v_com_date_switch)
         v_com_beg_date_fld.focusable = true;
         v_com_beg_date_fld.enabled = true;
         v_com_beg_date_fld.editable = true;
      else
         v_com_beg_date_fld.focusable = false;
         v_com_beg_date_fld.enabled = false;
         v_com_beg_date_fld.editable = false;
      end;
      AddControl(v_com_beg_date_fld);
      v_com_end_date_fld = TRsbEditField(FT_DATE);
      v_com_end_date_fld.bindValue(gv_deals_sel_prm, "v_com_end_date", 10);
      v_com_end_date_fld.setPosition((CV_POS_X + 27), (CV_POS_Y + 2));
      v_com_end_date_fld.setSize(10, 1);
      if(gv_deals_sel_prm.v_com_date_switch)
         v_com_end_date_fld.focusable = true;
         v_com_end_date_fld.enabled = true;
         v_com_end_date_fld.editable = true;
      else
         v_com_end_date_fld.focusable = false;
         v_com_end_date_fld.enabled = false;
         v_com_end_date_fld.editable = false;
      end;
      AddControl(v_com_end_date_fld);

      AddLabel(TRsbLabel((CV_POS_X + 14), CV_POS_Y, "Исп. диапазон"));
      v_com_date_switch_fld = TRsbCheckBox();
      v_com_date_switch_fld.setPosition((CV_POS_X + 18), (CV_POS_Y + 1));
      v_com_date_switch_fld.onChecked(R2M(i_this_obj, "m_on_switch_date"));
      v_com_date_switch_fld.checked = gv_deals_sel_prm.v_com_date_switch;
      AddControl(v_com_date_switch_fld);

      /* Фильтр по коду ошибки */
      v_com_is_err_filtr_fld = TRsbCheckBox();
//      v_com_is_err_filtr_fld.bindValue(gv_deals_sel_prm, "v_com_is_err_filtr");
      v_com_is_err_filtr_fld.setPosition(CV_POS_X, (CV_POS_Y + 4));
      v_com_is_err_filtr_fld.onChecked(R2M(i_this_obj, "m_on_checked_is_err_filtr"));
      v_com_is_err_filtr_fld.checked = gv_deals_sel_prm.v_com_is_err_filtr;
      AddControl(v_com_is_err_filtr_fld);
      AddLabel(TRsbLabel((CV_POS_X + 3), (CV_POS_Y + 4), "Фильтр по коду ошибки"));

      AddLabel(TRsbLabel(CV_POS_X, (CV_POS_Y + 5), "Код ошибки"));
      v_com_err_code_fld = TRsbEditField(FT_STRING);
      v_com_err_code_fld.bindValue(gv_deals_sel_prm, "v_com_err_code", 10);
      v_com_err_code_fld.setPosition(CV_POS_X, (CV_POS_Y + 6));
      v_com_err_code_fld.setSize(10, 1);
      if(gv_deals_sel_prm.v_com_is_err_filtr)
         v_com_err_code_fld.focusable = true;
         v_com_err_code_fld.enabled = true;
         v_com_err_code_fld.editable = true;
      else
         v_com_err_code_fld.focusable = false;
         v_com_err_code_fld.enabled = false;
         v_com_err_code_fld.editable = false;
      end;
      AddControl(v_com_err_code_fld);

      v_com_err_inv_fld = TRsbCheckBox();
//      v_com_err_inv_fld.bindValue(gv_deals_sel_prm, "v_com_err_inv");
      v_com_err_inv_fld.setPosition((CV_POS_X + 15), (CV_POS_Y + 6));
      v_com_err_inv_fld.onChecked(R2M(i_this_obj, "m_on_checked_err_inv"));
      v_com_err_inv_fld.checked = gv_deals_sel_prm.v_com_err_inv;
      AddControl(v_com_err_inv_fld);
      AddLabel(TRsbLabel((CV_POS_X + 18), (CV_POS_Y + 6), "Инверсия условия"));

      /* Фильтр по номеру сделки */
      AddLabel(TRsbLabel(CV_POS_X, (CV_POS_Y + 8), "Номер сделки"));
      v_com_deal_num_fld = TRsbEditField(FT_STRING);
      v_com_deal_num_fld.bindValue(gv_deals_sel_prm, "v_com_deal_num", 25);
      v_com_deal_num_fld.setPosition(CV_POS_X, (CV_POS_Y + 9));
      v_com_deal_num_fld.setSize(10, 1);
      v_com_deal_num_fld.focusable = true;
      v_com_deal_num_fld.enabled = true;
      v_com_deal_num_fld.editable = true;
      AddControl(v_com_deal_num_fld);

      /* Кнопка сброса */
      v_com_null_btn = TRsbPushButton("Сброс");
      v_com_null_btn.setPosition((CV_POS_X + 31), (CV_POS_Y + 10));
      v_com_null_btn.setSize(6, 1);

      v_com_null_btn.onClicked(R2M(i_this_obj, "m_deals_null_com_btn"));
      AddControl(v_com_null_btn);

      AddEventHandler(RSB_EV_KEY_PRESSED, R2M(i_this_obj, "m_evnt_on_key_pressed"));

      setSize(45, 14);
      setPosition(20, 5);

   end; /* macro m_init_com_search_form() */

   InitTRsbPanel();

   m_init_com_search_form(this);
end; /* class (TRsbPanel) c_com_search_form() */


/*------------------------------------*/
/* Метод обработки событий скроллинга */
/*------------------------------------*/
macro m_deals_proc_scr(p_rs, p_cmd, p_id, p_key)
   var CM_FLAG = CM_DEFAULT;

   var gv_com_search_form;
   var v_msg_menu = TArray;
   var v_choice;
   var v_msg_str = "";
   var v_file_path = "";


   if(p_cmd == DLG_INIT)
      while((gv_scr_focus != 0) and p_rs.movenext())
         if(p_rs.value("t_id") == gv_scr_focus)
            gv_scr_focus = 0;
            GoToScroll(p_rs);
         end;
      end;

   elif(p_cmd == DLG_KEY)
      if(p_key == K_ENTER)
         v_msg_menu.value(0) = "Описание ошибки";
         v_msg_menu.value(1) = "Исходный запрос с оберткой";
         v_msg_menu.value(2) = "Запрос без обертки";
         v_msg_menu.value(3) = "Исходный ответ";
         v_msg_menu.value(4) = "Ответ с оберткой";

         v_choice = menu(v_msg_menu, "Выберите сообщение", "Выберите сообщение");

         if(v_choice == 0)
            msgbox(p_rs.value("t_error_msg"));
         elif(v_choice == 1)
            m_make_xr_log_msg(p_rs.value("t_reqid_linked"), 1);
         elif(v_choice == 2)
            m_make_usr_log_msg(p_rs.value("t_id"), 1);
         elif(v_choice == 3)
            m_make_usr_log_msg(p_rs.value("t_id"), 3); /* 3 просто для единообразия - никакого скрытого смысла */
         elif(v_choice == 4)
            m_make_xr_log_msg(p_rs.value("t_reqid_linked"), 3);
         else
            msgbox("Выбор не подтвержден");
         end;

         CM_FLAG = CM_IGNORE;

      elif(p_key == K_F5)
         CM_FLAG = CM_SELECT;

         gv_com_search_form = c_com_search_form();
         gv_com_search_form.run;
      end;

   end;

   return CM_FLAG;
end; /* macro m_evnt_proc_scr() */


/*---------------------------------------*/
/* Метод, запускающий основной скроллинг */
/*---------------------------------------*/
private macro m_run_main_scroll()
   var sql_scr, cmd_scr, rs_scr;
   var sql_main = "";
   var sql_filtr = "";
   var sql_order = "";

   gv_deals_sel_prm = c_deals_selection_prm();

   sql_main =            "  SELECT u_log.t_id, u_log.t_reqid, ";
   sql_main = sql_main + "         TO_CHAR(u_log.t_req_dttm, 'DD.MM.YYYY HH24:MI:SS') t_req_dttm, ";
   sql_main = sql_main + "         TO_CHAR(u_log.t_resp_dttm, 'DD.MM.YYYY HH24:MI:SS') t_resp_dttm, ";
   sql_main = sql_main + "         u_log.t_error_code, u_log.t_error_msg, u_log.t_reqid_linked ";
   sql_main = sql_main + "    FROM usr_exchng_log_dbt u_log ";
   sql_main = sql_main + "   WHERE 1=1 ";

   if(gv_deals_sel_prm.v_com_date != date("00.00.0000"))
      sql_filtr =        "     AND TRUNC(u_log.t_req_dttm) = :p_deals_on_date ";
   end;

   sql_order =             "     AND u_log.t_module = 'WS_PROCESSDEALS' ";
   sql_order = sql_order + "     AND u_log.t_by_macro = 'WS_PROCESSDEALS' ";
   sql_order = sql_order + "ORDER BY u_log.t_id DESC ";

   sql_scr = sql_main + sql_filtr + sql_order;

   cmd_scr = RSDCommand(sql_scr);
   cmd_scr.NullConversion = true;

   if(gv_deals_sel_prm.v_com_date != date("00.00.0000"))
      cmd_scr.addParam("p_deals_on_date", RSDBP_IN, gv_deals_sel_prm.v_com_date);
   end;

   rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   rs_scr.NullConversion = true;

   m_add_column(gv_column_list_scr, gv_num_columns_scr, "T_ID",           "ID сообщения", 10); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "T_REQID",        "Номер сделки", 15); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "T_REQ_DTTM",     "Время запроса", 15); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "T_RESP_DTTM",    "Время ответа", 15); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "T_ERROR_CODE",   "Код ошибки", 5); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "T_ERROR_MSG",    "Описание ошибки", 15); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "T_REQID_LINKED", "REQID", 35); gv_num_columns_scr = gv_num_columns_scr + 1;

   while(RunScroll(rs_scr, gv_num_columns_scr, gv_column_list_scr, "deals_list", "m_deals_proc_scr", "Список сделок", "ESC - выход  F5 - фильтр  ENTER - подробности", true, 5, NULL, NULL, 40))
      /* Обновляем скроллинг */
      cmd_scr.close(); cmd_scr = NULL;
      rs_scr.close(); rs_scr = NULL;

      sql_filtr = "";

      /*----------------*/
      /* Фильтр по дате */
      /*----------------*/
      if(gv_deals_sel_prm.v_com_date_switch)
         if(gv_deals_sel_prm.v_com_beg_date != date("00.00.0000"))
            sql_filtr = sql_filtr + " AND TRUNC(u_log.t_req_dttm) >= :p_deals_beg_date ";
         end;
         if(gv_deals_sel_prm.v_com_end_date != date("00.00.0000"))
            sql_filtr = sql_filtr + " AND TRUNC(u_log.t_req_dttm) <= :p_deals_end_date ";
         end;
      else
         if(gv_deals_sel_prm.v_com_date != date("00.00.0000"))
            sql_filtr = sql_filtr + " AND TRUNC(u_log.t_req_dttm) = :p_deals_on_date ";
         end;
      end;

      /*-----------------------*/
      /* Фильтр по коду ошибки */
      /*-----------------------*/
      if(gv_deals_sel_prm.v_com_is_err_filtr)
         if(gv_deals_sel_prm.v_com_err_inv)
            sql_filtr = sql_filtr + " AND u_log.t_error_code <> :p_error_code ";
         else
            sql_filtr = sql_filtr + " AND u_log.t_error_code = :p_error_code ";
         end;
      end;

      /*-------------------------*/
      /* Фильтр по номеру сделки */
      /*-------------------------*/
      if(gv_deals_sel_prm.v_com_deal_num != "0")
         sql_filtr = sql_filtr + " AND u_log.t_reqid = :p_deal_num ";
      end;

      sql_scr = sql_main + sql_filtr + sql_order;

      /*-----------*/
      /* Параметры */
      /*-----------*/
      cmd_scr = RSDCommand(sql_scr);
      cmd_scr.NullConversion = true;
      if(gv_deals_sel_prm.v_com_date_switch)
         if(gv_deals_sel_prm.v_com_beg_date != date("00.00.0000"))
            cmd_scr.addParam("p_deals_beg_date", RSDBP_IN, gv_deals_sel_prm.v_com_beg_date)
         end;
         if(gv_deals_sel_prm.v_com_end_date != date("00.00.0000"))
            cmd_scr.addParam("p_deals_end_date", RSDBP_IN, gv_deals_sel_prm.v_com_end_date);
         end;
      else
         if(gv_deals_sel_prm.v_com_date != date("00.00.0000"))
            cmd_scr.addParam("p_deals_on_date", RSDBP_IN, gv_deals_sel_prm.v_com_date);
         end;
      end;

      if(gv_deals_sel_prm.v_com_is_err_filtr)
         cmd_scr.addParam("p_error_code", RSDBP_IN, gv_deals_sel_prm.v_com_err_code);
      end;

      if(gv_deals_sel_prm.v_com_deal_num != "0")
         cmd_scr.addParam("p_deal_num", RSDBP_IN, gv_deals_sel_prm.v_com_deal_num);
      end;

      rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   end;

   rs_scr.close();

end; /* macro m_run_main_scroll() */


/* Запуск основного скроллинга */
m_run_main_scroll();
Exit(1);