/*-----------------------------------------------------------------------------------------
   PrepareNOBFromSOFRToDepo.mac
   Подготовка передачи файла НОБ для Депозитария
   BIQ-7335
   Гераськина Т.В.
   Предполагается, что сам файл создается планировщиком и уже находится в директории
-----------------------------------------------------------------------------------------*/
import rsexts;
import "global_classes.mac";           /* класс ошибок */
import "ws_msg_transform_sec.mac";     /* для обрботки ошибок */

private const REG_EXPORT_PATH = "РСХБ\\ДИРЕКТОРИИ\\OUT\\НОБ_ДЕПО"; //путь для выгрузки файла
private const File_mask = "Отчет_НОБ_СОФР_*.txt"; // Отчет_НОБ_СОФР_ГГГГММДД_ЧЧMMCC.txt, пример: Отчет_НОБ_СОФР_20211231_104237.txt
private const Src_Path = "..\\TxtFile";

macro PrepareNOBFromSOFRToDepo()
   var Dest_Path; 
   var ErrCode, ErrText;
   var DL;
   var out_obj = c_Error;
   
   //получим значение пути выгрузки из реестра
   GetRegistryValue(REG_EXPORT_PATH, V_STRING, Dest_Path, ErrCode);
   if( ErrCode > 0 )
      Dest_Path = "..\\Export\\out";
   end;
   
   // проверка/создание директории назначения
   DL = TDirList();
   DL.List(Dest_Path, "D");  // директория для выгрузки
   if (DL.Count == 0)        // директории нет
      if (not MakeDir (Dest_Path))
         ErrText = "Директория выгрузки "+Dest_Path+" отсутствует и не может быть создана";
         RunError(ErrText, c_err_obj(ErrText,UNIVERSAL_ERROR_CODE));
      end;
   end;
   DL = NULL;                                                                                                                                                         
   
   DL = TDirList; 
   DL.Copy (Src_Path+"\\"+File_mask, "", Dest_Path+"\\", true, false, "Копирование файла НОБ"); //без индикатора
   if (DL.count > 0 )
      out_obj.ErrorCode = 0;
      out_obj.ErrorDesc = "Скопировано файлов: "+DL.count;
   else
      ErrText = "Файлы "+Src_Path+"\\"+File_mask+" отсутствуют";
      RunError(ErrText, c_err_obj(ErrText,UNIVERSAL_ERROR_CODE));
   end;

   return out_obj;

onError(err)
   out_obj = c_Error;
   out_obj.ErrorCode = c_err_obj.obtain_err_code(err);
   out_obj.ErrorDesc = c_err_obj.obtain_err_msg(err);

   return out_obj;
end;

// проверяет размер и наличие файлов, если пустой или отсутствует - ошибка
// возвращает c_Error
macro CkeckNOBFromSOFRToDepo()
   var ErrCode, ErrText = "";
   var DL, Dl_Rem;
   var out_obj = c_Error;
   var cnt = 0, i;
   var flag_notzero = false;
   var FileName;

   
   Dl_Rem = TDirList;
   DL = TDirList(Src_Path+"\\"+File_mask, "F"); 
   cnt = DL.count;

   if (cnt > 0 )
      for (i,0,cnt-1)
         if (DL.size(i) > 0)
            flag_notzero = true; // если есть хоть один ненулевой
         else
            FileName = DL.name(i);
            Dl_Rem.Remove(Src_Path+"\\"+FileName);
            ErrText = ErrText+"Размер файла "+FileName+" 0 байт, файл удален \n";
         end;
      end;
      if (flag_notzero)
         out_obj.ErrorCode = 0;
         out_obj.ErrorDesc = "OK \n"+ErrText;
      else
         out_obj.ErrorCode = -1;
         out_obj.ErrorDesc = ErrText;
      end;
   else
      ErrText = "Файлы "+Src_Path+"\\"+File_mask+" отсутствуют";
      RunError(ErrText, c_err_obj(ErrText,UNIVERSAL_ERROR_CODE));
   end;

   return out_obj;

onError(err)
   out_obj = c_Error;
   out_obj.ErrorCode = c_err_obj.obtain_err_code(err);
   out_obj.ErrorDesc = c_err_obj.obtain_err_msg(err);

   return out_obj;
end;


//CkeckNOBFromSOFRToDepo();
//PrepareNOBFromSOFRToDepo();