/*---------------------------------------------------------------*/
/* Отправка в АСОА документа                                     */
/*                                                               */
/* Автор: Гераськина Т.                                          */
/*
*/
/*----------------------------------------------*/
import "ws_msg_transform_sec.mac"; /* Инструментарий для преобразования сообщений */
import "secur_prc_msg_transform.mac";
import "email_notifyfun.mac";
import "func_lib.mac";

/* Константы, определяемые на этапе тестирования - когда есть доступ к внешним сервисам */
private const REQ_ROOT_NS_ALIAS = "";
private const REQ_NS_LIST       = "xmlns:req=\"http://www.rshb.ru/csm/pro/asoa/upload_doc/201908/req\" xmlns:xsi=\"http://www.w3.org/2001/XMLSchema-instance\"";

/* Пользовательская настойка реестра  */
private const URL_LOC_REG_PATH      = "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\UPLOADDOC\\URL";
private const HOST_LOC_REG_PATH     = "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\UPLOADDOC\\HOST";
private const LOGIN_LOC_REG_PATH    = "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\UPLOADDOC\\LOGIN";
private const PASSWORD_LOC_REG_PATH = "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\UPLOADDOC\\PASSWORD";


private macro SaveXmlBuf(bufXml)
  file OutFile("") txt write;  

  var outStr = bufXml;

  var stat = Open( OutFile, ".\\0uploadDoc.xml");
  if((stat == true))
     OutFile.str = OutStr;
     insert(OutFile);

     close(OutFile);
  end;
end;


private class c_ClientNPInfo
   var DocTypeCode;
   var DocNumber;
   var FirstName; 
   var LastName;
   var BirthDate;
   var MiddleName;
   var DocSeries;
   var DocIssueDate;
   var DocIssuedBy;
end;

private class c_Client
   var ClientId;
   var Status;
   var ClientNPInfo;
end;

private class c_Field
   var Name;
   var Value;
   var Kind;
end;

private class c_Dossier
  var ProductId;
  var DossierId;
  var FieldsList;
end;

private class c_Source
   var Creator;
   var SourceID;
   var CodeRF;
end;

private class c_Document
   var Source;
   var DocType;
   var DocName;
   var Data;
   var FieldsList;
end;

// класс для заполнения извне
private class c_UploadDoc(p_req_data_obj)
   var Client;
   var Dossier;
   var Document;
end;


// заполнение класса параметрами объекта Клиент 
macro CreateObjectForExport(_dlcontrid)
   private var obj;
   private var sql_str, cmd, rs;
   private var temp_field;
   private var ContractNumber;
   private var notekind, FilialID;
   private var ObjType = 207;  // договор брокерского обслуживания
   private var ObjID;
   private var v_login;
   private var v_imageid;
   
   private macro u_base64encode(p_imageid)
      var sql_str, cmd, rs, ds;
      var encode_clob;
      sql_str = "with encode_clob as "+
                " (SELECT RSB_PRDCLIENT.base64encode(Dig.t_Fmtblobdata_Xxxx) as enc "+
                "   FROM Dimage_Dbt Dig "+
                "  WHERE Dig.t_Imageid = :pid) "+
                "  select enc, DBMS_LOB.getlength(enc) len from encode_clob ";
      cmd = RSDCommand(sql_str);
      cmd.AddParam("pid", RSDBP_IN, p_imageid);
      rs = RSDRecordSet(cmd,RSDVAL_CLIENT_IF_NEEDED,RSDVAL_FORWARD_ONLY);
      if (rs.MoveNext)
         rs.Fld("enc").Read(encode_clob, Int(rs.Value("len")));
      end;
      rs.close; rs = null;
      cmd.close; cmd = null;  
      
      return encode_clob;
   end;
 
   
   sql_str = "select dl.t_dlcontrid, sf.t_id, sf.t_partyid, sf.t_number, sf.t_datebegin, "+
             "       p.t_name1, p.t_name2, p.t_name3, p.t_born, "+
             "       idc.t_paperkind, idc.t_paperseries, idc.t_papernumber, idc.t_paperissuer, idc.t_paperissueddate, "+
             "       pk.t_name paperkind_name, "+
             "       d.t_filename, d.t_imageid "+
             "  from "+
             "   ddlcontr_dbt dl, "+
             "   dsfcontr_dbt sf, "+
             "   ddlcontrmsg_dbt m, "+
             "   dimgdata_dbt d, "+
             "   dpersn_dbt p, "+
             "   dpersnidc_dbt idc, "+  
             "   dpaprkind_dbt pk "+
             " where dl.t_dlcontrid = :dlcontrid  "+
             "   and m.t_dlcontrid = dl.t_dlcontrid "+
             "   and dl.t_sfcontrid = sf.t_id "+
             "   and m.t_kind = 500 "+
             "   and d.t_objecttype = 208 and d.t_objectid =  lpad(m.t_id,34,'0') "+
             "   and p.t_personid = sf.t_partyid "+
             "   and p.t_personid = idc.t_personid "+
             "   and pk.t_paperkind = idc.t_paperkind "+
             " order by d.t_imageid desc";
   cmd = RSDCommand(sql_str);
   cmd.Addparam("dlcontrid", RSDBP_IN, _dlcontrid);
   rs = RSDRecordSet(cmd);
   if (rs.movenext)
      obj = c_UploadDoc();
      obj.Client = c_Client;
      obj.Client.ClientId = c_IntegrationSymbolicIdentifierXType;
      obj.Client.ClientId.ObjectID = GetPartyCode_ByNumber(rs.value("t_partyid"), PARTY_CODEKIND_ABS);
      var pos = Index(obj.Client.ClientId.ObjectID,"#");
      if (pos > 0)
         obj.Client.ClientId.ObjectID = Substr(obj.Client.ClientId.ObjectID,6);
      end;
      obj.Client.ClientId.SystemId = "BISQUIT";
      obj.Client.ClientId.SystemNodeId = "0000";

      obj.Client.Status = "ACTIVE";
      // указано, что не нужно передавать
//DEF-25856 возвращаем назад
      obj.Client.ClientNPInfo = c_ClientNPInfo;
      //obj.Client.ClientNPInfo.DocTypeCode = "21";  // заменить на код
      obj.Client.ClientNPInfo.DocTypeCode = rs.value("paperkind_name");
      obj.Client.ClientNPInfo.DocNumber   = rs.value("t_papernumber");
      obj.Client.ClientNPInfo.FirstName   = rs.value("t_name1"); 
      obj.Client.ClientNPInfo.LastName    = rs.value("t_name3");
      obj.Client.ClientNPInfo.BirthDate   = rs.value("t_born");
      obj.Client.ClientNPInfo.MiddleName  = rs.value("t_name2");
      obj.Client.ClientNPInfo.DocSeries   = rs.value("t_paperseries");
      obj.Client.ClientNPInfo.DocIssueDate= rs.value("t_paperissueddate");
      obj.Client.ClientNPInfo.DocIssuedBy = rs.value("t_paperissuer");
      
      obj.Dossier = c_Dossier;
      obj.Dossier.ProductId  = "БрД01";
      //obj.Dossier.DossierId = "ID";  // заменить на примечание
      obj.Dossier.FieldsList = TArray;
      temp_field = c_Field;
      temp_field.Name   = "ContractNumber";
      ContractNumber = rs.value("t_number");
      temp_field.Value  = ContractNumber;
      temp_field.Kind   = "String";
      obj.Dossier.FieldsList.value(obj.Dossier.FieldsList.Size) = temp_field;
      temp_field = NULL;
      temp_field = c_Field;
      temp_field.Name   = "ContractOpenDate";
      temp_field.Value  = rs.value("t_datebegin");
      temp_field.Kind   = "Date";
      obj.Dossier.FieldsList.value(obj.Dossier.FieldsList.Size) = temp_field;
      temp_field = NULL;
      
      notekind = CONTRACT_NOTEKIND_CodeRF;
      ObjID = String(_dlcontrid:o:34);
      FilialId = readNoteForObject( ObjType, ObjID, notekind );  
      if (not FilialId)
        //err_txt = "Не задан код РФ в примечании "+notekind;
        //RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
        FilialId = "0000";
      end;

      temp_field = c_Field;
      temp_field.Name   = "DepartmentId";
      temp_field.Value  = FilialId;
      temp_field.Kind   = "String";
      obj.Dossier.FieldsList.value(obj.Dossier.FieldsList.Size) = temp_field;
      temp_field = NULL;
      temp_field = c_Field;
      temp_field.Name   = "CodeRF";
      temp_field.Value  = "00";//FilialId;
      temp_field.Kind   = "String";
      obj.Dossier.FieldsList.value(obj.Dossier.FieldsList.Size) = temp_field;  
      temp_field = NULL;
   
      GetRegistryValue(LOGIN_LOC_REG_PATH, V_STRING, v_login);
      v_imageid = rs.value("t_imageid");

      obj.Document = c_Document;
      obj.Document.Source = c_Source;
      obj.Document.Source.Creator = v_login;
      obj.Document.Source.SourceID = "SOFR";
      obj.Document.Source.CodeRF = "00";
      obj.Document.DocType = "item446#LbEpaDocDossierVerifieable";  // ????
      obj.Document.DocName = rs.value("t_filename");
      obj.Document.Data = u_base64encode(v_imageid);
      obj.Document.FieldsList = " "; // нужен тег
   end;
   
   return obj;
end;

class c_Document_Response
   var DocID;
   var DocURL;
end;

private class c_ErrorData
   var ErrorCode : integer;  
   var ErrorDesc : string;   
end;

class c_ResultData
   var ResultCode;
   var ErrorData;
end;

class c_UploadDocResponse
   var ClientID;
   var DossierID;
   var Document;
   var ResultData;
end;


/* Корневой элемент ответа - AS IS - как пришел */
private class adp_UploadDocResponse(IncomeData)
   var ClientID;
   var DossierID;
   var Document;
   var ResultData;

   var aux_buff, aux_buff2;

   /* Кратность параметров в соответствии со схемой/спецификацией */
   private macro uInitPrime(IncomeData)
      var err_txt;
      
      if(ValType(IncomeData) != V_UNDEF)
         ClientID = getSymbolFromProperty(IncomeData, "ClientID");
         DossierID = getSymbolFromProperty(IncomeData, "DossierID");
         
         aux_buff = getValueFromProperty(IncomeData, "Document");
         if (ValType(aux_buff) != V_UNDEF)
            Document = c_Document_Response;
            Document.DocID = getSymbolFromProperty(aux_buff, "DocID");
            Document.DocURL = getValueFromProperty(aux_buff, "DocURL");
         end;

         aux_buff = getValueFromProperty(IncomeData, "ResultData"); 
         if(ValType(aux_buff) != V_UNDEF)
            ResultData = c_ResultData;
            ResultData.ResultCode = getValueFromProperty(aux_buff, "ResultCode");
            aux_buff2 = getValueFromProperty(aux_buff, "ErrorData"); 
            if (ValType(aux_buff2) != V_UNDEF)
               ResultData.ErrorData = c_ErrorData;
               ResultData.ErrorData.ErrorCode = getSymbolFromProperty(aux_buff2, "ErrorCode");
               ResultData.ErrorData.ErrorDesc = getSymbolFromProperty(aux_buff2, "ErrorDesc");
            end;
         end;

      end;
   end;

   uInitPrime(IncomeData);
end; /* class adp_Netting(IncomeData) */


/* Основной класс работы с сервисом */
private class (c_secur_ext_ws_processor) c_ExportDocProcessor(IncomeData_main)

   private class cc_ExportUploadDoc(IncomeData)
      var UploadDocReq; /* !!! По названию корневого тега запроса !!! */

      private macro uInitPrime(IncomeData)
         UploadDocReq = IncomeData;
      end;

      uInitPrime(IncomeData);
   end; /* private class cc_ExportNetting(IncomeData) */

   private macro uInitPrime_main(IncomeData_main)
      // определимся с классом.
      var obj, DefineDealType; 
      var err_txt;
      if (Valtype(IncomeData_main) != V_GENOBJ)
        err_txt = "Неверный тип входящего параметра, ожидается class";
        RunError(err_txt, c_err_obj(err_txt,-101));
      end;
      
      obj = cc_ExportUploadDoc(IncomeData_main);
      
      set_req_obj(obj);
   end;


   /*----------------------------------------------*/
   /* Метод получения сообщения запроса из объекта */
   /*----------------------------------------------*/
   /*      Перегруженный метод базового класса     */
   /*----------------------------------------------*/
   macro m_make_req_msg(p_root_ns_alias, p_ns_list)
      private var v_ret = true;
      private var xmltxt;

      private var v_wrap_obj;

      if(v_ret)
         vg_req_msg = c_secur_msg_converter.m_secur_external_req(vg_req_obj);
         vg_req_msg = StrSubst(vg_req_msg, "<?xml version=\"1.0\" encoding=\"UTF-8\"?>", "");
         vg_req_msg = StrSubst(vg_req_msg, "UploadDocReq", "req:UploadDocReq");
         vg_req_msg = StrSubst(vg_req_msg, "<Client>", "<Client xsi:type=\"req:ClientNPType\">");

         v_wrap_obj = c_secur_msg_converter();

         if(v_wrap_obj.m_wrap_req_in_soap(vg_req_msg,
                                          p_root_ns_alias,
                                          p_ns_list))
            //vg_req_msg = OemToUtf8(v_wrap_obj.get_result_msg());
            vg_req_msg = v_wrap_obj.get_result_msg();

SaveXmlBuf(vg_req_msg);
         else
            vg_service_msg = vg_req_msg.get_service_msg();
            v_ret = false;
         end;

         v_wrap_obj = null;
      end;

      return v_ret;

   onError(err)
      v_ret = false;
      vg_service_msg = string("При формировании сообщения запроса возникла ошибка: ",
                              c_usr_err_obj.obtain_err_msg(err));

      return v_ret;
   end; /* macro m_make_req_msg() */

   macro m_make_resp_obj()
      private const CV_BORDER_1 = "<soapenv:Body>";
      private const CV_BORDER_2 = "</soapenv:Body>";
      private var v_ret = true;
      private var v_pos1, v_pos2;
      private var v_aux_buff, v_pure_msg;

      if(ValType(vg_resp_msg) == V_STRING)
         v_aux_buff = vg_resp_msg;
         v_pos1 = index(v_aux_buff, CV_BORDER_1);
         v_pos2 = index(v_aux_buff, CV_BORDER_2);

         if((v_pos1 == 0) or (v_pos2 == 0))
            vg_service_msg = "Полученное сообщение не соответствует заданному формату";
            v_ret = false;
         else
            v_pure_msg = substr(v_aux_buff, (v_pos1 + strlen(CV_BORDER_1)), (v_pos2 - v_pos1 - strlen(CV_BORDER_1)));
         end;

      else
         vg_service_msg = "Тип данных не соответствует ожидаемому";
         v_ret = false;
      end;

      vg_resp_obj = c_secur_msg_converter.m_secur_external_resp(v_pure_msg);

      return v_ret;

   onError(err)
      v_ret = false;
      vg_service_msg = string("В процессе преобразования ответа возникла ошибка: ",
                              c_usr_err_obj.obtain_err_msg(err));

      return v_ret;
   end; /* macro m_make_resp_obj() */


   Initc_secur_ext_ws_processor();

   uInitPrime_main(IncomeData_main);
end; /* private class c_ExportStatusDealProcessor() */


/*-------------------------------------------------------------------*/
/* Адаптер для вызова сервиса "Отправка документа в АСОА     "       */
/*-------------------------------------------------------------------*/
macro ExportDoc(ReqData)
   var service_processor = NULL;
   var RespData = NULL;
   var PrepObj = NULL;
   var RespObj = NULL;
   var RespErr = NULL;
   var aux_buff_a, aux_buff_i;

   var v_url, v_host, v_soapaction;
   var err_txt;

   if ( (ReqData == NULL) or (ValType(ReqData) == V_UNDEF) )
      return null;
   end;

   service_processor = c_ExportDocProcessor(ReqData);


   /* Подготовка сообщения для отправки */
   if(not service_processor.m_make_req_msg(REQ_ROOT_NS_ALIAS, REQ_NS_LIST))
      RunError(service_processor.get_service_msg());
   end;

   v_url        = uGetRegistryParam(URL_LOC_REG_PATH, V_STRING);
//   v_host       = uGetRegistryParam(HOST_LOC_REG_PATH, V_STRING);
   v_host = " ";
   v_soapaction = " ";

   // тип аутентификации
   if(not service_processor.m_set_auth_type_val(1, false))
      RunError(service_processor.get_service_msg());
   end;
   // логин
   if(not service_processor.m_set_auth_login_val(LOGIN_LOC_REG_PATH, true))
      RunError(service_processor.get_service_msg());
   end;
   // пароль
   if(not service_processor.m_set_auth_pwd_val(PASSWORD_LOC_REG_PATH, true))
      RunError(service_processor.get_service_msg());
   end;

   /* Отправка запроса */
   BegAction(2000,"Ожидание ответа от сервера. Запрос ...");
   if(not service_processor.uCallExtServiceViaWinHttp(v_url, modulename(), "UPLOADDOC", v_host, v_soapaction))
      err_txt = service_processor.get_service_msg();
      RunError(err_txt, c_err_obj(err_txt,service_processor.get_service_code()) );
   end; 
   EndAction();

   /* Получение объекта ответа в том виде, в котором он пришел */
   if(not service_processor.m_make_resp_obj())
      RunError(service_processor.get_service_msg());
   end;

   /* Преобразование объектов ответа к ожидаемому формату */
   RespData = adp_UploadDocResponse(service_processor.get_resp_obj());
   /* Преобразование объекта к виду, ожидаемому Системой */


   if(ValType(uTryToGetPropS(RespData, "ResultData")) != V_UNDEF)
      RespObj = RespData;
   else
      /* Сообщение с ошибкой обрабатывается выше */
      /* Неизвестный формат ответного сообщения */
      RespObj = c_UploadDocResponse();
      RespObj.ResultData = c_ResultData;
      RespObj.ResultData.ResultCode = 1;
      RespObj.ResultData.ErrorData = c_ErrorData;
      RespObj.ResultData.ErrorData.ErrorCode = -1001;
      RespObj.ResultData.ErrorData.ErrorDesc = "Неизвестный формат ответного сообщения";
   end;

//    msgbox("OK");

   return RespObj;

onError(err)
   EndAction();

   RespObj = c_UploadDocResponse();
   RespObj.ResultData = c_ResultData;
   RespObj.ResultData.ResultCode = 1;
   RespObj.ResultData.ErrorData = c_ErrorData;
   RespObj.ResultData.ErrorData.ErrorCode = c_err_obj.obtain_err_code(err);
   RespObj.ResultData.ErrorData.ErrorDesc = c_err_obj.obtain_err_msg(err);

   return RespObj;
end; /* macro ExportNetting(ReqData) */



/*---------------------------------------*/
/*              Точка входа              */
/*---------------------------------------*/
/* ReqData - данные для отправки         */
/*---------------------------------------*/
macro PushUploadDoc(_doit, _dlcontrid)
  var ReqData;
  var PushDoc;
  var res = false; // пессимистично

  if (_doit)
    ReqData = CreateObjectForExport( _dlcontrid );
    if (ValType(ReqData) == V_GenObj)
      res = ExportDoc(ReqData);
    else
      res = c_UploadDocResponse();

      res = c_UploadDocResponse();
      res.ResultData = c_ResultData;
      res.ResultData.ResultCode = 1;
      res.ResultData.ErrorData = c_ErrorData;
      res.ResultData.ErrorData.ErrorCode = -1001;
      res.ResultData.ErrorData.ErrorDesc = "Нет данных для выгрузки - не сформировано уведомление";
    end;
  end;
  return res;
end;


