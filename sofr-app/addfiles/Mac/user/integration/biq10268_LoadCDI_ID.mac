/** 
 @file 		BIQ10268_LoadCDI_ID.mac
 @brief 	Начальное решение по BIQ-10268. Загрузка идентификатора CDI субъектов СОФР
  
 # tag 
 - CDI
   
 # changeLog 
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |05.12.2024 |Велигжанин А.В.|CCBO-10808_CCBO-10921                           |Исправление ошибки поставки
 |05.12.2024 |Велигжанин А.В.|CCBO-10808_CCBO-10915                           |Создание 

*/ 
IMPORT BankInter, CTInter, PtInter, RSD, RsbDataSet, rcw, globals, "usr_open_files.mac";

VAR g_PathIn: string = "..\\import\\biq10268\\";     		// Путь к каталогу с файлом данных с признаком блокировки
   

/**
  @brief    True, если полученная ячейка содержит значение
  @param[in]   val         значение ячейки
*/
PRIVATE MACRO NotNull ( val )
    if ( val == NULL ) return false; end;
    if ( ValType (val) == 26 ) return false; end;
    return true;
END; // NotNull


Macro NormValue (vl)
  if (valtype (vl) == V_UNDEF)
     return "";
  end;
  return vl;
End; // NormValue

/**
  @brief    	Открытие Excel
*/
Private macro OpenExcel
    return CreateObject ("rsax", "TRsAxServer", "RsBankAxServer", isStandAlone ()).CreateComObject ("Excel.Application");
OnError
    return NULL;
End; // OpenExcel


/**
  @brief    	Открытие Excel-файла
*/
Private macro OpenExcelFile ( fName )
    Var ex = OpenExcel ();
    if ( ex == NULL )
        MsgBox ("Ошибка открытия MS-Excel");
        return NULL;
    end;
    ex.Workbooks.Open (fName);
    return ex;
OnError
    ex = NULL;
    MsgBox ("Ошибка открытия файла ", fName);
    return NULL;
End; // OpenExcelFile


/* @brief 	Загрузка p_CdiID для субъекта p_PartyID.
                Возвращает TRUE, если загрузка выполнена без ошибки.
                Возвращает FALSE при ошибке загрузки.
*/
PRIVATE MACRO LoadOneParty( p_PartyID, p_CdiID )

  Print ( "Загрузка CDI_ID: " + string(p_CdiID) + " для субъекта: " + string(p_PartyID) );

  var pt = rsbparty( int(p_PartyID) );

  Pt.Code.SetCode(110, p_CdiID);
  pt.update();

  Println(" Ok");

  RETURN TRUE;
OnError
  Println(" Err");
  return FALSE;
END; // LoadOneParty


/* @brief 	Поиск субъекта p_PartyID в таблице p_TableName.
                Возвращает TRUE, если запись найдена.
                Возвращает FALSE, если запись не найдена.
*/
private macro FindPartyInTable (p_TableName, p_PartyID)
  var cmd, rs;
  cmd = RsdCommand(
          "select 1 AS from " + p_TableName + " n "
          + " where n.t_partyID = " + string(p_PartyID)
  );
  cmd.Execute();
  rs = TRsbDataSet(cmd);
  return rs.MoveNext();
onerror()
  return false;
End;

/* @brief 	Проверка категории субъекта p_PartyID.
                Возвращает TRUE, если проверка успешная (значение категории 175 не равно 1).
                Возвращает FALSE, если проверка не успешная (значение категории 175 равно 1).
*/
private macro CheckCategory_175 (p_PartyID)
  var cmd, rs, Ok = true;
  cmd = RsdCommand(
          "SELECT 1 AS n FROM dparty_dbt p "
          + " JOIN dobjatcor_dbt r ON (r.t_objecttype = 3 and r.t_object = LPAD(p.t_partyid, 10, '0') and r.t_groupid = 175 and r.t_attrid = 1) "
          + " WHERE p.t_partyid = " + string(p_PartyID)
  );
  cmd.Execute();
  rs = TRsbDataSet(cmd);
  if(rs.MoveNext())
     Ok = false;
  end;  
  return Ok;
onerror()
  return Ok;
End;

/* @brief 	Проверка субъекта p_PartyID или p_CdiID.
                Возвращает TRUE, если прверка прошла.
                Возвращает FALSE, если прверка не прошла.
*/
Private macro CheckParty  (p_PartyID, p_CdiID)
  var Ok: BOOL = FALSE;

  if( FindPartyInTable("dparty_dbt", p_PartyID) == false )
      Println("Субъект "+p_PartyID+" не найден в таблице dparty_dbt");
  elif( CheckCategory_175(p_PartyID) == false)
      Println("Значение категории 175 субъекта "+p_PartyID+" равно 1");
  elif( FindPartyInTable("biq10268_unload", p_PartyID) == false )
      Println("Субъект "+p_PartyID+" не найден в таблице biq10268_unload");
  else 
      Ok = TRUE;
  end;

  RETURN Ok;

END; // CheckParty

/* @brief 	Парсер строки p_Row, возвращает p_CdiID и p_PartyID
*/
Private macro ParseRow  (p_Row, p_CdiID:@STRING, p_PartyID:@STRING)
  var x_Pos: INTEGER;

  x_Pos = Index(p_Row, "|");
  if( x_Pos > 0 )
     p_CdiID = SubStr ( p_Row, 1, x_Pos-1 );
     p_PartyID = strSubst(SubStr ( p_Row, x_Pos+1 ), "\"", "");
  end;

END; // ParseRow

/* @brief 	Обработка xls-файла
*/
Private macro LoadXLS  (ex, EndLine)
   
    Var str = "", error = FALSE, sql, cnt = 0;
    Var Line = 1, Col = 0, EndCol = 0;
  var x_CdiID :string;
  var x_PartyID :string;

  var 
    x_AllCount :integer = 0
    , x_OkCount :integer = 0
    , x_ErrCount :integer = 0
    , x_ErrStr :string
  ;

   
    EndLine = EndLine + Line;
    InitProgress (EndLine - Line, NULL, "Чтение файла");

    while ( (Line=Line+1) < EndLine )
        UseProgress (cnt = cnt+1);

        ParseRow(NormValue(trim (string(ex.Cells (Line, Col+1 ).Value))), @x_CdiID, @x_PartyID); 

        x_AllCount = x_AllCount + 1;
        if ( CheckParty (x_PartyID, x_CdiID) == true )
          x_OkCount = x_OkCount + 1;
          if( LoadOneParty(x_PartyID, x_CdiID) == false )
            x_ErrCount = x_ErrCount + 1;
          end;
        end;
    end;
    RemProgress ();

    ex.Quit ();
    ex = NULL;

    Println ( "Обработка завершена" );
    Println ( "Общее количество записей в файле: " + string(x_AllCount) );
    Println ( "Число субъектов с установленным кодом: " + string(x_OkCount) );
    Println ( "Количество ошибок: " + string(x_ErrCount) );
End;

/**
  @brief    Процедура запуска процеса
*/
PRIVATE MACRO RepStartProcess
  Var 
    x_FileName, ex, cnt = 0, x_EndLine
    , x_FileID : integer
  ;

  Println ( "Загрузка идентификатора CDI_ID для субъектов СОФР..." );
  Println ();

  if ( not SelectFile (x_FileName, (g_PathIn+"*.*"), NULL, NULL, TRUE) )
      Println ( "Файл не выбран" );
      return FALSE;
  elif ( (ex = OpenExcelFile (x_FileName)) == NULL )
      Println ( "Ошибка открытия файла "+string(x_FileName) );
      return FALSE;
  end;

  x_EndLine = ex.ActiveSheet.UsedRange.Rows.Count;

  return LoadXLS  (ex, x_EndLine);
END; // StartProcess


/**
  @brief    Обработать файлы
*/
macro StartLoadCdiIDProcess()

  SetOutPut(gettxtfilename("biq10268_setblock_"+StrSubst(string(date()),".","")+StrSubst(string(time()),":","")),false);

  var stat = RepStartProcess(); // обработка файлов с формированием протокола

  ViewFile(SetOutPut(null, true));
  SetOutPut(null, true)
END;

/**
  @brief    Точка входа. Запуск процесса.
*/
StartLoadCdiIDProcess();