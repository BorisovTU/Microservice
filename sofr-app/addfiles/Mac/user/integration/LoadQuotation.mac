/*-------------------------------------------*/
/* Сервис Загрузка данных по котировкам      */
/*                                           */
/* Автор: Карпов В.                          */
/*-------------------------------------------*/

import "global_utils_intgr.mac"; /* serviceaction */ /* email_notifyfun */
import "com_fin_instr_rates.mac";
//import email_notifyfun; /* Функционал работы с e-mail уведомлениями */
import "secur_prc_msg_transform.mac"; /* Функционал преобразования сообщений */


private const CV_RATE_DEFAULT_CODE = 0;
private const CV_RATE_ERROR_CODE = -1;
private const CV_EMAIL_HEAD = "Ошибка. Загрузка данных по котировкам";
private const CV_EMAIL_ROW_HEAD = "LoadQuotation: ";
/* !!! - Временно, до тех пор, пока не будет понимания где брать список адресов - !!! */
private const CV_EMAIL_RECEIVER = "";


/*-------------------*/
/* Описание объектов */
/*-------------------*/
private class c_RateParm_req
   var Type        : integer /* (  1) Тип курса в СОФР */
      ,Basefiid    : string  /* (  1) Код базового финансового инструмента */
      ,Otherfiid   : string  /* (  1) Код котируемого финансового инструмента */
      ,MarketPlace : string  /* (0-1) Код торговой площадки */
      ,Section     : string  /* (0-1) Код секции торговой площадки */
      ,Rate        : integer /* (  1) Значение курcа */
      ,Scale       : integer /* (  1) Масштаб */
      ,Point       : integer /* (  1) Округление (количество знаков после запятой) */
      ,IsRelative  : bool    /* (0-1) Признак относительной котировки */
      ,IsDominant  : bool    /* (0-1) Признак основного курса */
      ,IsInverse   : bool;   /* (0-1) Признак обратной котировки */
end;

/* Данные запроса сервиса Загрузки данных по котировкам */
private class c_LoadQuotation_req
   var ReqID    : string  /* (  1) ИД запроса */
      ,SenderId : string  /* (0-1) Код АС-источника сведений */
      ,Date     : date    /* (  1) Дата, с которой установлен курс */
      ,RateList : object; /* (1-n) Список котировок TArray c_RateParm_req */
end;

private class c_Result_Rate
   var Code : integer /* (  1) Код */
      ,Text : string; /* (0-1) Описание */
end;

private class c_RateParm_resp
   var Type      : string  /* (1) Тип курса в СОФР */
      ,Basefiid  : string  /* (1) Код базового финансового инструмента */
      ,Otherfiid : string  /* (1) Код котируемого финансового инструмента */
      ,Result    : object; /* (1) c_Result_Rate */
end;

/* Данные ответа сервиса Загрузки данных по котировкам */
private class c_LoadQuotation_resp
   var ReqID    : string  /* (  1) ИД исходного запроса */
      ,SenderId : string  /* (0-1) Код АС-получателя ответа */
      ,Result   : object  /* (  1) Результат обработки запроса c_Result_Rate */
      ,RateList : object; /* (0-n) Результат загрузки котировок TArray c_RateParm_resp */
end;

/* Ответ сервиса */
private class c_out_obj
   var LoadQuotation_resp = c_LoadQuotation_resp;
end;


/*---------------------------------*/
/* Функционал сохранения котировок */
/*---------------------------------*/
private class (c_save_rate) c_save_rate_quotation(p_income_data, p_set_date)
   private var vg_is_error;

   macro m_get_error_status()
      return vg_is_error;
   end;

   /* Получение идентификатора финансового инструмента по коду ценной бумаги */
   private macro m_get_fi_by_code_avoir(p_fi_code)
      private var v_ret;
      private var v_sql, v_cmd, v_rs;

      v_sql = "SELECT t_fiid, t_isin FROM davoiriss_dbt WHERE t_isin = :p_isin ";
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_isin", RSDBP_IN, p_fi_code);
      v_rs = RSDRecordSet(v_cmd);
      if(v_rs.movenext())
         v_ret = v_rs.value("t_fiid");
      end;

      v_rs.close(); v_cmd.close(); v_rs = null; v_cmd = null; v_sql = null;

      return v_ret;
   end; /* macro m_get_fi_by_code_avoir() */

   /* Получение идентификатора финансового инструмента по коду валюты */
   private macro m_get_fi_by_code_currency(p_fi_code)
      private var v_ret;
      private var v_sql, v_cmd, v_rs;

      v_sql = "SELECT t_fiid, t_fi_code FROM dfininstr_dbt WHERE t_fi_code = :p_fi_code ";
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_fi_code", RSDBP_IN, p_fi_code);
      v_rs = RSDRecordSet(v_cmd);
      if(v_rs.movenext())
         v_ret = v_rs.value("t_fiid");
      end;

      v_rs.close(); v_cmd.close(); v_rs = null; v_cmd = null; v_sql = null;

      return v_ret;
   end; /* macro m_get_fi_by_code_currency() */

   private macro m_get_extra_params()
      private var v_sql, v_cmd, v_rs;

      v_sql =         "  SELECT t_fiid, t_otherfi, t_type, ";
      v_sql = v_sql + "         t_market_place, t_section, ";
      v_sql = v_sql + "         DECODE(t_isrelative, chr(0), '0', 'X') t_isrelative, ";
      v_sql = v_sql + "         DECODE(t_isdominant, chr(0), '0', 'X') t_isdominant, ";
      v_sql = v_sql + "         DECODE(t_isinverse, chr(0), '0', 'X') t_isinverse ";
      v_sql = v_sql + "    FROM dratedef_dbt ";
      v_sql = v_sql + "   WHERE t_fiid = :p_base_fiid ";
      v_sql = v_sql + "     AND t_otherfi = :p_other_fiid ";
      v_sql = v_sql + "     AND t_type = :p_rate_kind ";
      v_sql = v_sql + "ORDER BY t_rateid ASC "; /* Если есть несколько записей, берем первую */
      v_cmd = RSDCommand(v_sql);
      /* Следствие того, что в пакете на момент написания параметры были взаимозаменены */
      v_cmd.addParam("p_base_fiid", RSDBP_IN, vg_other_fiid);
      v_cmd.addParam("p_other_fiid", RSDBP_IN, vg_base_fiid);
      v_cmd.addParam("p_rate_kind", RSDBP_IN, vg_rate_kind);
      v_rs = RSDRecordSet(v_cmd);
      if(v_rs.movenext())
         vg_market_place = v_rs.value("t_market_place");
         vg_market_section = v_rs.value("t_section");

         if(v_rs.value("t_isrelative") == "X")
            vg_is_relative = strfor(88);
         else
            vg_is_relative = strfor(0);
         end;
         if(v_rs.value("t_isdominant") == "X")
            vg_is_dominant = strfor(88);
         else
            vg_is_dominant = strfor(0);
         end;
         if(v_rs.value("t_isinverse") == "X")
            vg_is_inverse = strfor(88);
         else
            vg_is_inverse = strfor(0);
         end;
      else
         /* !!! - Необходимо подтверждение - !!! */
         /* Поиск по dratedef_dbt, если вводим новый курс, то 0 */
         vg_market_place = 0;
         /* Поиск по dratedef_dbt, если вводим новый курс, то 0 */
         vg_market_section = 0;
         /* Поиск по dratedef_dbt, если вводим новый курс, то strfor(0) */
         vg_is_relative = strfor(0);
         /* Поиск по dratedef_dbt, если вводим новый курс, то strfor(0) */
         vg_is_dominant = strfor(0);
         /* Поиск по dratedef_dbt, если вводим новый курс, то берем из dfininstr_dbt */
         vg_is_inverse = strfor(0);
      end;

      v_rs.close(); v_cmd.close(); v_rs = null; v_cmd = null; v_sql = null;

   onError(err)
      vg_is_error = true;
      vg_service_msg = "Возникла проблема при определении дополнительных параметров";
      RunError(vg_service_msg, c_usr_err_obj(vg_service_msg));
   end;

   /* Подготовка данных для сохранения в БД */
   private macro m_prep_data(p_income_data, p_set_date)
      /* В версии пакета на момент написания параметры были взаимозаменены */
      vg_base_fiid  = m_get_fi_by_code_avoir(p_income_data.Otherfiid);
      vg_other_fiid = m_get_fi_by_code_currency(p_income_data.Basefiid);
      vg_rate_kind  = p_income_data.Type;

      /* !!! - Необходимо подтверждение - !!! */
      /* Определение параметров (если они не заданы):
         - vg_market_place
         - vg_market_section
         - vg_is_relative
         - vg_is_dominant
         - vg_is_inverse
      */
      m_get_extra_params();

      vg_since_date = p_set_date;
      if(ValType(p_income_data.MarketPlace) != V_UNDEF)
         vg_market_place = p_income_data.MarketPlace;
      end;
      if(ValType(p_income_data.Section) != V_UNDEF)
         vg_market_section = p_income_data.Section;
      end;

      vg_rate = p_income_data.Rate;
      vg_scale = p_income_data.Scale;
      vg_point = p_income_data.Point;

      if(ValType(p_income_data.IsRelative) != V_UNDEF)
         vg_is_relative = p_income_data.IsRelative;
      end;
      if(ValType(p_income_data.IsDominant) != V_UNDEF)
         vg_is_dominant = p_income_data.IsDominant;
      end;
      if(ValType(p_income_data.IsInverse) != V_UNDEF)
         vg_is_inverse = p_income_data.IsInverse;
      end;
      vg_oper = {oper};
   end; /* macro m_prep_data */

   /* Конструктор */
   private macro m_init_prime(p_income_data, p_set_date)
      vg_is_error = false;
      vg_service_msg = "";

      if(ValType(p_income_data) != V_UNDEF)
         m_clear_buf();

         m_prep_data(p_income_data, p_set_date);

         m_save_rate();
      end;

   onError(err)
      vg_is_error = true;
      vg_service_msg = c_usr_err_obj.obtain_err_msg(err);
   end; /* macro m_init_prime() */

   Initc_save_rate();

   m_init_prime(p_income_data, p_set_date);
end; /* class c_save_rate_quotation() */


/*-----------------------------*/
/* Адаптер типа c_RateParm_req */
/*-----------------------------*/
private class (c_RateParm_req) c_adp_RateParm_req(p_income_data)
   private var vg_is_error;
   private var vg_error_code;
   private var vg_error_text;

   macro m_get_error_status()
      return vg_is_error;
   end;

   macro m_get_error_code()
      return vg_error_code;
   end;

   macro m_get_error_text()
      return vg_error_text;
   end;

   private macro m_add_text_to_err(p_src_text, p_add_text)
      private var v_ret = "";

      if(strlen(p_src_text) != 0)
         v_ret = string(p_src_text, "|", p_add_text);
      else
         v_ret = p_add_text;
      end;

      return v_ret;
   end; /* macro m_add_text_to_err() */

   private macro m_init_prime(p_income_data)
      vg_is_error = false;

      if(ValType(p_income_data) != V_UNDEF)
         Type = uTryToGetPropS(p_income_data, "Type");
         if(ValType(Type) == V_UNDEF)
            vg_is_error = true;
            vg_error_code = CV_RATE_ERROR_CODE;
            vg_error_text = m_add_text_to_err(vg_error_text, string(InErrComPart, "Type"));
         end;

         Basefiid = uTryToGetPropS(p_income_data, "Basefiid");
         if(ValType(Basefiid) == V_UNDEF)
            vg_is_error = true;
            vg_error_code = CV_RATE_ERROR_CODE;
            vg_error_text = m_add_text_to_err(vg_error_text, string(InErrComPart, "Basefiid"));
         end;

         Otherfiid = uTryToGetPropS(p_income_data, "Otherfiid");
         if(ValType(Otherfiid) == V_UNDEF)
            vg_is_error = true;
            vg_error_code = CV_RATE_ERROR_CODE;
            vg_error_text = m_add_text_to_err(vg_error_text, string(InErrComPart, "Otherfiid"));
         end;

         MarketPlace = uTryToGetPropS(p_income_data, "MarketPlace");
         Section = uTryToGetPropS(p_income_data, "Section");

         Rate = uTryToGetPropS(p_income_data, "Rate");
         if(ValType(Rate) == V_UNDEF)
            vg_is_error = true;
            vg_error_code = CV_RATE_ERROR_CODE;
            vg_error_text = m_add_text_to_err(vg_error_text, string(InErrComPart, "Rate"));
         end;

         Scale = uTryToGetPropS(p_income_data, "Scale");
         if(ValType(Scale) == V_UNDEF)
            vg_is_error = true;
            vg_error_code = CV_RATE_ERROR_CODE;
            vg_error_text = m_add_text_to_err(vg_error_text, string(InErrComPart, "Scale"));
         end;

         Point = uTryToGetPropS(p_income_data, "Point");
         if(ValType(Point) == V_UNDEF)
            vg_is_error = true;
            vg_error_code = CV_RATE_ERROR_CODE;
            vg_error_text = m_add_text_to_err(vg_error_text, string(InErrComPart, "Point"));
         end;

         IsRelative = uTryToGetPropS(p_income_data, "IsRelative");
         IsDominant = uTryToGetPropS(p_income_data, "IsDominant");
         IsInverse = uTryToGetPropS(p_income_data, "IsInverse");
      end;

   onError(err)
      vg_is_error = true;
      vg_error_code = CV_RATE_ERROR_CODE;
      vg_error_text = m_add_text_to_err(vg_error_text,
                                        string("При обработке параметров котировки возникла ошибка: ",
                                               c_usr_err_obj.obtain_err_msg(err)));
   end;

   Initc_RateParm_req();

   m_init_prime(p_income_data);
end; /* class c_adp_RateParm_req() */


/*---------------------------------------------------*/
/* Класс с реализацией основного функционала сервиса */
/*---------------------------------------------------*/
private class (c_LoadQuotation_req) c_LoadQuotation_req_proc(p_income_data)
   /* Признак наличия ошибок */
   private var vg_is_error;
   /* Статусы для структуры верхнего уровня */
   private var vg_hl_error;
   /* Список зарегистрированных статусов для структур нижнего уровня */
   private var vg_ll_error_list;

   /* Метод получения признака наличия ошибок */
   macro m_get_error_status()
      return vg_is_error;
   end;

   /* Метод получения статуса для структуры верхнего уровня */
   macro m_get_hl_error()
      return vg_hl_error;
   end;

   /* Метод получения кода статуса для структур нижнего уровня */
   macro m_get_ll_error_code(p_index)
      return vg_ll_error_list.value(p_index).Code;
   end;

   /* Метод получения текста статуса для структур нижнего уровня */
   macro m_get_ll_error_text(p_index)
      return vg_ll_error_list.value(p_index).Text;
   end;

   private macro m_add_text_to_err(p_src_text, p_add_text)
      private var v_ret = "";

      if(strlen(p_src_text) != 0)
         v_ret = string(p_src_text, "|", p_add_text);
      else
         v_ret = p_add_text;
      end;

      return v_ret;
   end; /* macro m_add_text_to_err() */

   /* Конструктор */
   private macro m_init_prime(p_income_data)
      private var v_aux_buff;
      private var v_aux_cntr;

      vg_is_error = false;
      vg_hl_error = c_Result_Rate;
      vg_hl_error.Code = 0; /* Значение по умолчанию - ошибок нет */
      vg_ll_error_list = TArray;

      if(p_income_data != V_UNDEF)
         ReqID = uTryToGetPropS(p_income_data, "ReqID");
         if(ValType(ReqID) == V_UNDEF)
            vg_is_error = true;
            vg_hl_error.Code = CV_RATE_ERROR_CODE;
            vg_hl_error.Text = m_add_text_to_err(vg_hl_error.Text, string(InErrComPart, "ReqID"));
            ReqID = XmlRpcRequestId();
         end;

         SenderId = uTryToGetPropS(p_income_data, "SenderId");

         Date = uTryToGetPropS(p_income_data, "Date");
         if(ValType(Date) == V_UNDEF)
            vg_is_error = true;
            vg_hl_error.Code = CV_RATE_ERROR_CODE;
            vg_hl_error.Text = m_add_text_to_err(vg_hl_error.Text, string(InErrComPart, "Date"));
         end;

         v_aux_buff = ValType(uTryToGetPropS(p_income_data, "RateList"));
         if(v_aux_buff == V_GENOBJ)
            RateList = TArray;
            v_aux_cntr = 0;

            while(p_income_data.RateList.size > v_aux_cntr)
               vg_ll_error_list.value(v_aux_cntr) = c_Result_Rate;
               vg_ll_error_list.value(v_aux_cntr).Code = 0;

               v_aux_buff = ValType(p_income_data.RateList.value(v_aux_cntr));
               if(v_aux_buff == V_GENOBJ)
                  RateList.value(v_aux_cntr) = c_adp_RateParm_req(p_income_data.RateList.value(v_aux_cntr));
                  /* Сохраняем ошибки, зафиксированные при обработке параметров котировки */
                  if(RateList.value(v_aux_cntr).m_get_error_status())
                     vg_ll_error_list.value(v_aux_cntr).Code = RateList.value(v_aux_cntr).m_get_error_code();
                     vg_ll_error_list.value(v_aux_cntr).Text = RateList.value(v_aux_cntr).m_get_error_text();
                  end;
               /*elif(v_aux_buff == V_UNDEF)*/ /* Пустой элемент массива - ошибка */
               else
                  vg_ll_error_list.value(v_aux_cntr).Code = CV_RATE_ERROR_CODE;
                  vg_ll_error_list.value(v_aux_cntr).Text = string(InErrNotObj, "элемент списка RateList");
               end;

               v_aux_cntr = v_aux_cntr + 1;
            end;

         elif(v_aux_buff == V_UNDEF)
            vg_is_error = true;
            vg_hl_error.Code = CV_RATE_ERROR_CODE;
            vg_hl_error.Text = m_add_text_to_err(vg_hl_error.Text, string(InErrComPart, "RateList"));

         else
            vg_is_error = true;
            vg_hl_error.Code = CV_RATE_ERROR_CODE;
            vg_hl_error.Text = m_add_text_to_err(vg_hl_error.Text, string(InErrNotObj, "RateList"));
         end;
      /*else*/ /* Сохраняем возможность создания объекта без передачи данных */
      end;

   onError(err)
      /* Записываем в ошибку структуры верхнего уровня структуры */
      vg_is_error = true;
      vg_hl_error.Code = CV_RATE_ERROR_CODE;
      vg_hl_error.Text = m_add_text_to_err(vg_hl_error.Text,
                                           string("При обработке входных параметров возникла ошибка: ",
                                                  c_usr_err_obj.obtain_err_msg(err)));
   end;

   Initc_LoadQuotation_req();

   m_init_prime(p_income_data);
end; /* class c_LoadQuotation_req_proc() */


/*---------------------*/
/* ТОЧКА ВХОДА СЕРВИСА */
/*---------------------*/
macro LoadQuotation(p_income_data)
   private var v_aux_cntr;
   private var v_income_data = NULL;
   var v_email_processor = NULL;
   var v_transformator = NULL;
   var v_service_processor = NULL;
   var v_save_processor = NULL;
   var v_resp_obj = NULL;
   var v_resp_doc;

   v_resp_obj = c_out_obj;

   /* Инициализация письма */
   v_email_processor = c_email_proc_env;
   v_email_processor.m_set_msg_head(CV_EMAIL_HEAD);
   /* !!! - Необходимо где-то брать список адресов для отправки уведомлений - !!! */
   v_email_processor.m_add_email_to_list(CV_EMAIL_RECEIVER);

   if(ValType(p_income_data) != V_UNDEF)
      /* Преобразуем запрос в объектный вид - begin */
      v_transformator = c_secur_msg_converter();
      if(not v_transformator.m_decode_escaped_char(p_income_data))
         RunError(v_transformator.get_service_msg(), c_usr_err_obj(v_transformator.get_service_msg()));
      end;
      if(not v_transformator.m_make_pure_msg_from_ifx(v_transformator.get_pure_msg()))
         RunError(v_transformator.get_service_msg(), c_usr_err_obj(v_transformator.get_service_msg()));
      end;
      v_income_data = v_transformator.m_secur_internal_req(v_transformator.get_pure_msg());
      /* Преобразуем запрос в объектный вид - end */

      v_service_processor = c_LoadQuotation_req_proc(v_income_data);

      /* Общие параметры структуры верхнего уровня */
      v_resp_obj.LoadQuotation_resp.ReqID    = v_service_processor.ReqID;
      v_resp_obj.LoadQuotation_resp.SenderId = v_service_processor.SenderId;
      v_resp_obj.LoadQuotation_resp.Result   = v_service_processor.m_get_hl_error();

      if(not v_service_processor.m_get_error_status())
         v_resp_obj.LoadQuotation_resp.RateList = TArray;

         v_aux_cntr = 0;
         /* Обработка параметров котировок */
         while(v_service_processor.RateList.size > v_aux_cntr)
            /* Общие параметры для котировки */
            v_resp_obj.LoadQuotation_resp.RateList.value(v_aux_cntr) = c_RateParm_resp;
            v_resp_obj.LoadQuotation_resp.RateList.value(v_aux_cntr).Type      = v_service_processor.RateList.value(v_aux_cntr).Type;
            v_resp_obj.LoadQuotation_resp.RateList.value(v_aux_cntr).Basefiid  = v_service_processor.RateList.value(v_aux_cntr).Basefiid;
            v_resp_obj.LoadQuotation_resp.RateList.value(v_aux_cntr).Otherfiid = v_service_processor.RateList.value(v_aux_cntr).Otherfiid;
            v_resp_obj.LoadQuotation_resp.RateList.value(v_aux_cntr).Result    = c_Result_Rate;

            /* Проверка наличия ошибок на этапе получения параметров котировки */
            if(v_service_processor.m_get_ll_error_code(v_aux_cntr) == 0)
               v_save_processor = c_save_rate_quotation(v_service_processor.RateList.value(v_aux_cntr),
                                                        v_service_processor.Date);

               if(not v_save_processor.m_get_error_status())
                  /* Котировка сохранена успешно */
                  v_resp_obj.LoadQuotation_resp.RateList.value(v_aux_cntr).Result.Code = CV_RATE_DEFAULT_CODE;
               else
                  /* При сохранении котировки возникли проблемы */
                  v_resp_obj.LoadQuotation_resp.RateList.value(v_aux_cntr).Result.Code = CV_RATE_ERROR_CODE;
                  v_resp_obj.LoadQuotation_resp.RateList.value(v_aux_cntr).Result.Text = v_save_processor.m_get_service_msg();

                  v_email_processor.m_add_row_to_msg_text(string(CV_EMAIL_ROW_HEAD,
                                                                 "(", (v_aux_cntr + 1), ") ",
                                                                 v_resp_obj.LoadQuotation_resp.ReqID, " - ",
                                                                 v_resp_obj.LoadQuotation_resp.RateList.value(v_aux_cntr).Result.Text));
               end;

            else
               /* При получении параметров котировки возникли проблемы */
               v_resp_obj.LoadQuotation_resp.RateList.value(v_aux_cntr).Result.Code = v_service_processor.m_get_ll_error_code(v_aux_cntr);
               v_resp_obj.LoadQuotation_resp.RateList.value(v_aux_cntr).Result.Text = v_service_processor.m_get_ll_error_text(v_aux_cntr);

               v_email_processor.m_add_row_to_msg_text(string(CV_EMAIL_ROW_HEAD,
                                                              "(", (v_aux_cntr + 1), ") ",
                                                              v_resp_obj.LoadQuotation_resp.ReqID, " - ",
                                                              v_resp_obj.LoadQuotation_resp.RateList.value(v_aux_cntr).Result.Text));
            end;

            v_aux_cntr = v_aux_cntr + 1;
         end;
      else
         /* Зафиксированы ошибки на верхнем уровне структуры */
         v_resp_obj.LoadQuotation_resp.RateList = TArray;
      end;

   else
      /* Ошибка в структуру верхнего уровня - не переданы входные данные */
      v_resp_obj.LoadQuotation_resp.ReqID       = XmlRpcRequestId(); /* Поскольку не можем взять информацию из входящего объекта */
      v_resp_obj.LoadQuotation_resp.Result      = c_Result_Rate;
      v_resp_obj.LoadQuotation_resp.Result.Code = CV_RATE_ERROR_CODE;
      v_resp_obj.LoadQuotation_resp.Result.Text = "Не переданы данные котировок";
      v_resp_obj.LoadQuotation_resp.RateList    = TArray;

      v_email_processor.m_add_row_to_msg_text(string(CV_EMAIL_ROW_HEAD,
                                              v_resp_obj.LoadQuotation_resp.ReqID, " - ",
                                              v_resp_obj.LoadQuotation_resp.Result.Text));
   end;

   /* Сохраняем текст письма для отправки плановой процедурой */
   v_email_processor.m_save_to_submit();
   /* Отправляем все не отправленные письма */
//   v_email_processor.m_submit_email();

   /* Преобразуем объектный вид ответа в документ требуемого формата - begin */
   v_resp_doc = v_transformator.m_secur_internal_resp(v_resp_obj);
   /* Преобразуем объектный вид ответа в документ требуемого формата - end */

   return v_resp_doc;

onError(err)
   if(ValType(v_resp_obj.LoadQuotation_resp.ReqID) == V_UNDEF)
      v_resp_obj.LoadQuotation_resp.ReqID = XmlRpcRequestId();
   end;
   v_resp_obj.LoadQuotation_resp.Result      = NULL;
   v_resp_obj.LoadQuotation_resp.Result      = c_Result_Rate;
   v_resp_obj.LoadQuotation_resp.Result.Code = CV_RATE_ERROR_CODE;
   v_resp_obj.LoadQuotation_resp.Result.Text = c_usr_err_obj.obtain_err_msg(err);

   /* Преобразуем объектный вид ответа в документ требуемого формата - begin */
   v_resp_doc = v_transformator.m_secur_internal_resp(v_resp_obj);
   /* Преобразуем объектный вид ответа в документ требуемого формата - end */

   return v_resp_doc;
end; /* macro LoadQuotation() */