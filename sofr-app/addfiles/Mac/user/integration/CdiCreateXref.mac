/*-----------------------------------------------------*/
/* CdiCreateXref.mac Создание ссылок по Субъекту ФЛ */
/*                                                     */
/* Автор: Гераськина Т.                                */
/* 16.07.2021 BIQ-7006 Замена PersonCreateXref на сервис CdiCreateXrefReq
*/
/*----------------------------------------------*/
import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import "RequestWS.mac";                   /* Функция передачи запроса в Web Sphere */
import "uws_exchng_log.mac";              /* Функционал логирования */
import func_lib;
import global_classes;

class c_ExtId
   var SourceTimestamp     : datetime;  // Дата изменения записи на источнике
   var ExtIdPartySourceId;              // ID клиента в источнике                    Идентификатор
   var ExtIdSourceSystem;               // Код системы источника                     Справочник
   var ExtIdPartyTargetId;              // ID клиента, на которого создается ссылка  Идентификатор
   var ExtIdTargetSystem;               // Код системы, на которую создается ссылка  Справочник
end;


class c_CdiCreateXrefReq(SourceId, TargetId) 
   var ExtIdList;    // Список кросс-ссылок
   
   macro uInitPrime(SourceId, TargetId)
      ExtIdList = TArray;
      var ExtId = c_ExtId;
      
      ExtId.SourceTimestamp = Dttm(Date(),time());
      // источник - СОФР
      ExtId.ExtIdPartySourceId = c_IntegrationSymbolicIdentifierXType();
      ExtId.ExtIdPartySourceId.ObjectId = SourceId;
      ExtId.ExtIdSourceSystem = c_IntegrationDictionaryRecordXType();
      ExtId.ExtIdSourceSystem.RecordCode = "SOFR";
      // Получатель - CDI
      if ((ValType(TargetId) != V_UNDEF) and (ValType(TargetId.ObjectId) != V_UNDEF))
         ExtId.ExtIdPartyTargetId = TargetId;
         ExtId.ExtIdTargetSystem = c_IntegrationDictionaryRecordXType();
         ExtId.ExtIdTargetSystem.RecordCode = "CDI";
      else 
         ExtId.ExtIdPartyTargetId = c_IntegrationSymbolicIdentifierXType();
         ExtId.ExtIdPartyTargetId.ObjectId = GetPartyCode_ByNumber(SourceId, PARTY_CODEKIND_ABS);
         ExtId.ExtIdTargetSystem = c_IntegrationDictionaryRecordXType();
         ExtId.ExtIdTargetSystem.RecordCode = "CFT";
      end;
      
      ExtIdList.value(0) = ExtId;
      ExtId = null;
   end;
end;


/* Основной класс работы с сервисом */
private class c_CdiCreateXrefProcessor(SourceId, TargetId)
   var CdiCreateXrefReq; /* !!! По названию корневого тега запроса !!! */
   CdiCreateXrefReq = c_CdiCreateXrefReq;
   CdiCreateXrefReq.uInitPrime(SourceId, TargetId);
end; 

/*-------------------------------------------------------------------------*/
/* Адаптер для вызова сервиса "Запрос на создание кросс-ссылки клиента ФЛ" */
/*-------------------------------------------------------------------------*/
macro run_CdiCreateXref(SourceId, TargetId, p_transparent_id)
   var RespData = NULL;
   var err_txt = NULL;
   
   private var v_out_obj;
   private var RequestText;
   private var v_transformator;
   private var v_answer;
   private var v_answer_data;
   private var v_logger_obj;
   private var v_log_id;
   private var xml_str;
   private var v_xml_rpc_reqid = XmlRpcRequestId(); 

   if ( (SourceId == NULL) or (ValType(SourceId) == V_UNDEF) )
      return null;
   end;

   v_transformator = c_secur_msg_converter();

   // заполнение класса информацией 
   v_out_obj = c_CdiCreateXrefProcessor(SourceId, TargetId);
   RequestText = v_transformator.m_secur_internal_resp(v_out_obj);
   
   v_logger_obj = uws_exchng_logger(null, p_transparent_id, v_xml_rpc_reqid, "CdiCreateXref", modulename(), null, RequestText);
   v_log_id = v_logger_obj.get_log_id();

      
   //err_txt = v_transformator.m_validate_out_xml(RequestText);
   v_transformator = NULL;
            
   if (err_txt == NULL)
      v_answer = SubmitRequestToWS( 2,              // ИД очереди.
                                    "",             // ИД сообщения.
                                    "",             // Идентификатор Бэк Офиса. (не обрабатывается).
                                    true,          // Признак синхронной обработки. Если не указан, = False
                                    "SOFR",         // Подразделение системы-назначения.
                                    26,             // ИД типа данных !!!
                                    RequestText,    // Бизнес данные в виде XML.
                                    v_log_id,       // ID usr_exchng_log
                                    "CDI"
                                    );

         if (v_answer.ResultCode == 0)
            xml_str = v_answer.responsetext;
            v_logger_obj.add_response(xml_str); 
         else 
            v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, v_answer.ResultText);
         end;
   else
      v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, err_txt);
   end;
         
   return RespData;

onError(err)

end; 

/*---------------------------------------*/
/*              Точка входа              */
/*---------------------------------------*/
/* ReqData - данные для отправки         */
/*---------------------------------------*/
macro CdiCreateXref (SourceId, TargetId, in_transparent_id) 
   return run_CdiCreateXref(SourceId, TargetId, in_transparent_id); 
end;

