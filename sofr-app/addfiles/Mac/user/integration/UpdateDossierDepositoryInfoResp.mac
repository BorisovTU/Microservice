import "func_lib.mac";
import "global_utils_intgr.mac";
import "uws_exchng_log.mac";

private class c_Error(IncomeData)
   var ErrorCode : string;
   var ErrorDesc : string;

   private macro m_init_Error(IncomeData)
      private var nametag_up = "UpdateDossierDepositoryInfoResp.ErrorData";
      ErrorCode = getValueFromProperty(IncomeData, "ErrorCode", nametag_up);
      ErrorDesc = getValueFromProperty(IncomeData, "ErrorDesc", nametag_up);
   end;

   m_init_Error(IncomeData);
end;

/*private class c_ResultData()
   var ResultCode : String; 
   var ErrorData;  
end;*/

private class c_UpdateDossierDepositoryInfoResp(IncomeData)
   //var ResultData; 
   var ResultCode : String; 
   var Error;  

   private macro m_init_UpdateDossierDepositoryInfoResp(IncomeData)
      private var err_txt, aux_buff;
      private var nametag_up = "UpdateDossierDepositoryInfoResp";

      if(ValType(IncomeData) != V_UNDEF)
            ResultCode  = getValueFromProperty(IncomeData, "ResultCode" , nametag_up, true);

            aux_buff    = getValueFromProperty(IncomeData, "Error"  , nametag_up);
            if(ValType(aux_buff) == V_GENOBJ)
               Error = c_Error(aux_buff);
            elif(ValType(aux_buff) == V_UNDEF)
            else
               err_txt = string(InErrNotObj, nametag_up+".Error");
               RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
            end;
      end;
   end;

   m_init_UpdateDossierDepositoryInfoResp(IncomeData);
end;


/*-------------*/
/* ТОЧКА ВХОДА */
/*-------------*/
macro UpdateDossierDepositoryInfoResp(p_income_data, p_log_id)
   var v_logger_obj;
   var v_income_data;
   var v_aux_buf;
   var v_cntr_err;
   
   var Reqid;
   var vresulttext= "", vresultcode; 
   var sql_str, cmd;
   
   /* Получаем проверенный объект со входными данными */
   v_income_data = c_UpdateDossierDepositoryInfoResp(p_income_data);

   v_logger_obj = uws_exchng_logger();
   v_logger_obj.set_log_id(p_log_id);

   if (StrUpr(v_income_data.ResultCode) != "ERROR")  // успешно
      vresulttext = "OK";
      vresultcode = 0;
   else
      /* Описание ошибок */
      vresulttext = string("Получена ошибка: ",
                            v_income_data.Error.ErrorCode,
                            ";",
                            v_income_data.Error.ErrorDesc
                           );
      vresultcode = v_income_data.Error.ErrorCode;
   end;

   /* Сохраняем в логи */
   v_aux_buf = strlen(vresulttext);
   if(v_aux_buf > 0)
      if(v_aux_buf >= 2000)
         vresulttext = substr(vresulttext, 1, 1999);
      end;
      v_logger_obj.add_error_info(vresultcode, vresulttext);
   end;

   v_logger_obj = NULL;

   return true;

onError(err)
   if (ValType(v_logger_obj) == V_UNDEF)
      v_logger_obj = uws_exchng_logger();
      v_logger_obj.set_log_id(p_log_id);
   end;

   v_logger_obj.add_error_info(c_usr_err_obj.obtain_err_code(err), c_usr_err_obj.obtain_err_msg(err));

   return false;
end;