/*
$Name:        SendOperationResultReq.mac
$Module:      Интеграция с внешними системами
$Description: Запрос на отправку результатов выполнения операции в системе
*/
/* BIQ-7294 14.11.2022 Гераськина Т.*/

import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import "RequestWS.mac";                   /* Функция передачи запроса в Web Sphere */
import "uws_exchng_log.mac";              /* Функционал логирования */
import func_lib;

// Информация по операции
class c_OperationInfo
   var EventDescription       : string;   // Описание события                                               Да
   var TaxYear                : integer;  // Налоговый период, к которому относится событие (налоговый год) Да
//   var IncomeRegionDateTime   : datetime; // Дата и время получения дохода (региональное время)             Да
//shev 20.12.2022  def-36575
   var IncomeRegionDateTime   : string; // Дата и время получения дохода (региональное время)             Да
   var IncomeDateTime         : datetime; // Дата и время получения дохода (московское время)               Да
   var NOBAmount              : money;    // Сумма НОБ по текущей выплате                                   Нет
   var NDFLCalculateAmount    : money;    // Сумма исчисленного НДФЛ по выполненной операции                Нет
   var NDFLTaxRate            : double;   // Налоговая ставка, примененная при исчислении НДФЛ              Нет
   var NDFLKeepAmount         : money;    // Сумма  НДФЛ удержанного по выполненной операции                Нет
   var NDFLKeepRate           : double;   // Налоговая ставка удержанного НДФЛ по выплаченному доходу       Нет
   var KBKCalculate           : string;   // Код КБК для исчисленного к удержанию НДФЛ                      Нет
   var KBKKeep                : string;   // Код КБК удержанного НДФЛ                                       Да
   var SNOBPayAmount          : money;    // Сумма СНОБ с учетом НОБ по текущей выплаты                     Нет
   var SNOBAmount             : money;    // Сумма СНОБ без учета НОБ по текущей выплате                    Нет
   var ClientId;                          // ID клиента в системе-источнике                                 Да    Идентификатор
   var EventType;                         // Тип события                                                    Да    Справочник
   var EventId;                           // ID события                                                     Да    Идентификатор
   var RecordStatus;                      // Статус записи                                                  Да    Справочник
   var OperationId;                       // ID операции                                                    Да    Идентификатор
   var TaxBaseType;                       // Вид налоговой базы по НДФЛ, к которой относится событие        Нет   Справочник
   var TaxPayerStatus;                    // Статус налогоплательщика ФЛ                                    Нет   Справочник
end;


// Запрос на отправку результатов выполнения операции в системе
class c_SendOperationResultReq
   var SystemCode;         // Код системы, в которой осуществляется выполнение операции   Да     Справочник
   var OperationInfoList;  // Список операций, по которым передается информация           Да     Список
end;

// Результат обработки операции
class c_OperationInfoResp
   var AmountSNOB    : money;   // Сумма СНОБ                       Нет
   var ClientId;                 // ID клиента в системе-источнике   Да    Идентификатор
   var RecordId;                 // ID записи в хранилище СНОБ       Нет   Идентификатор
   var OperationId;              // ID операции                      Да    Идентификатор
   var EventId;                  // ID события                       Да    Идентификатор
   var Error;                    // Ошибка                           Да
end;

class c_SendOperationResultResp
   var OperationInfoList;  // Результаты обработки информации по списку операций    Да
end;


private class adp_SendOperationResultResponse(IncomeData)
   var SendOperationResult;   // Блок данных    Нет
   var ErrorList;             // Список ошибок  Да

   /* Кратность параметров в соответствии со схемой/спецификацией */
   private macro uInitPrime(IncomeData)
      var err_txt;
      var aux_buff, aux_buff2, ai;
      var temp;
      var temp_error;
      
      var nametag_up = "SendOperationResultReq";
      
      if(ValType(IncomeData) != V_UNDEF)
         aux_buff = getValueFromProperty(IncomeData, "SendOperationResult", nametag_up);
         if (ValType(aux_buff) == V_GENOBJ)
            SendOperationResult = c_SendOperationResultResp;
            nametag_up = nametag_up+".SendOperationResult";
            aux_buff2 = uTryToGetPropS(aux_buff, "OperationInfoList");
            if(ValType(aux_buff2) == V_GENOBJ)
               SendOperationResult.OperationInfoList = TArray;
               ai = 0;
               nametag_up = nametag_up+".OperationInfoList";
               while(aux_buff2.size > ai)
                  temp = c_OperationInfoResp;
                  
                  temp.AmountSNOB   = getValueFromProperty(aux_buff2.value(ai), "AmountSNOB"   , nametag_up);
                  temp.ClientId     = getSymbolFromProperty(aux_buff2.value(ai), "ClientId"     , nametag_up, true);
                  temp.RecordId     = getSymbolFromProperty(aux_buff2.value(ai), "RecordId"     , nametag_up);
                  temp.OperationId  = getSymbolFromProperty(aux_buff2.value(ai), "OperationId"  , nametag_up, true);
                  temp.EventId      = getSymbolFromProperty(aux_buff2.value(ai), "EventId"      , nametag_up, true);
                  temp_Error        = getValueFromProperty(aux_buff2.value(ai), "Error"         , nametag_up, true);
                  temp.Error = c_Error;
                  temp.Error.ErrorCode = getValueFromProperty(temp_Error, "ErrorCode", nametag_up);
                  temp.Error.ErrorDesc = getValueFromProperty(temp_Error, "ErrorDesc", nametag_up);
                  
                  SendOperationResult.OperationInfoList.value(ai) = temp;
                  ai = ai + 1;
                  temp = NULL;
                  temp_Error = null;
               end;
            elif(ValType(aux_buff) == V_UNDEF)
               err_txt = string(InErrComPart, nametag_up+".OperationInfoList");
               RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
            else
               err_txt = string(InErrNotObj, nametag_up+".OperationInfoList");
               RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
            end;
         end;

         nametag_up = "SendOperationResultReq";
         aux_buff = uTryToGetPropS(IncomeData, "ErrorList");
         if(ValType(aux_buff) == V_GENOBJ)
            ErrorList = TArray;
            aux_buff = 0;
            nametag_up = nametag_up+".ErrorList";
            while(IncomeData.ErrorList.size > aux_buff)
               temp_Error = c_Error;
               temp_Error.ErrorCode = getValueFromProperty(IncomeData.ErrorList.value(aux_buff), "ErrorCode", nametag_up);
               temp_Error.ErrorDesc = getValueFromProperty(IncomeData.ErrorList.value(aux_buff), "ErrorDesc", nametag_up);
               
               ErrorList.value(aux_buff) = temp_Error;
               aux_buff = aux_buff + 1;
               temp_Error = NULL;
            end;
         elif(ValType(aux_buff) == V_UNDEF)
         else
            err_txt = string(InErrNotObj, nametag_up+".ErrorList");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
         end;  
      end;
   end;
   
   uInitPrime(IncomeData);
end;

private macro MakeErrorAnswer(pcode, ptext)
   var res = adp_SendOperationResultResponse;
   res.ErrorList = TArray;
   res.ErrorList.value(0) = c_Error;
   res.ErrorList.value(0).ErrorCode = pcode;
   res.ErrorList.value(0).ErrorDesc = ptext;

   return res;
end;

/* Основной класс работы с сервисом */
class c_SendOperationRequest()
   var SendOperationResultReq; /* !!! По названию корневого тега запроса !!! */
   SendOperationResultReq = c_SendOperationResultReq;
end; 


/*----------------------------------------------------------------------------------*/
/* Адаптер для вызова сервиса "Запрос на отправку результатов выполнения операции"  */
/*  считаем, что ReqData заполняется вовне                                          */
/*----------------------------------------------------------------------------------*/
macro run_SendOperationResult(ReqData)
   var RespData = NULL;
   var err_txt = NULL;
   
   private var RequestText;
   private var v_transformator;
   private var v_answer;
   private var v_answer_data;
   private var v_logger_obj;
   private var v_log_id;
   private var xml_str;
   private var v_guid = CreateGUID();
   private var v_xml_rpc_reqid = XmlRpcRequestId(); 
   
   private var V_WS_TYPE = GetNumDataType("SendOperationResult");

   var v_timeout;
   var stat;
   GetRegistryValue(REG_TIMEOUT_SNOB, V_INTEGER, v_timeout, stat);
   if (stat>0)
      v_timeout = 15;
   end;


   v_transformator = c_secur_msg_converter();

   // заполнение класса информацией 
   RequestText = v_transformator.m_secur_internal_resp(ReqData);
   
   v_logger_obj = uws_exchng_logger(null, v_guid, v_xml_rpc_reqid, "SendOperationResultReq", modulename(), null, RequestText);
   v_log_id = v_logger_obj.get_log_id();



   if (err_txt == NULL)
      v_answer = SubmitRequestToWS( C_QUEUE_OUT_ID,  // ИД очереди.
                                    "",              // ИД сообщения.
                                    "",              // Идентификатор Бэк Офиса. (не обрабатывается).
                                    true,            // Признак синхронной обработки. Если не указан, = False
                                    "SOFR",          // Подразделение системы-отпр
                                    V_WS_TYPE,       // ИД типа данных
                                    RequestText,     // Бизнес данные в виде XML
                                    v_log_id,        // ID usr_exchng_log
                                    "EPRSHB2",
                                    v_timeout
                                       );
      if (v_answer.ResultCode == 0)
         xml_str = v_answer.responsetext;
         v_logger_obj.add_response(xml_str); 

         v_transformator = NULL;
         if (xml_str)
               /* Преобразуем запрос в объектный вид - begin */
               v_transformator = c_secur_msg_converter();
               if(not v_transformator.m_decode_escaped_char(xml_str))
                  RunError(v_transformator.get_service_msg(), c_usr_err_obj(v_transformator.get_service_msg()));
               end;
               v_answer_data = v_transformator.m_secur_wol_internal_req(v_transformator.get_pure_msg());
               /* Преобразуем запрос в объектный вид - end */

               if(ValType(v_answer_data) == V_UNDEF)
                  err_txt = "Не удалось преобразовать XML в объект";
                  RunError(err_txt, c_usr_err_obj(err_txt)); 
               end;
               
               RespData = adp_SendOperationResultResponse(v_answer_data);
         end;
      else 
         v_logger_obj.add_error_info(v_answer.ResultCode, v_answer.ResultText);
         RespData = MakeErrorAnswer(v_answer.ResultCode, v_answer.ResultText);
      end;

   else
      v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, err_txt);
      RespData = MakeErrorAnswer(UNIVERSAL_ERROR_CODE, err_txt);
   end;

   return RespData;

onError(err)
   stat = c_err_obj.obtain_err_code(err);
   err_txt = c_err_obj.obtain_err_msg(err);

   if (valtype(v_logger_obj) == V_UNDEF)
      v_logger_obj = uws_exchng_logger(null, v_guid, v_xml_rpc_reqid, "SendOperationResultReq", modulename(), null, "");
   end;

   v_logger_obj.add_error_info(stat, err_txt);
   RespData = MakeErrorAnswer(stat, err_txt);
   return RespData;
end;


/*---------------------------------------*/
/*              Точка входа              */
/*---------------------------------------*/
/* ReqData - данные для отправки         */
/*---------------------------------------*/
macro SendOperationResultReq (ReqData)
   return run_SendOperationResult(ReqData);
end;
