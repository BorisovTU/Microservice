/* 47423_unload.mac
   Гераськина Т.В.
   BIQ-11207 Выгрузка счетов 47423 в текстовый файл с разделителями (;)
*/

import rsd;
import cb_sql;

  file datafile() txt write;  //csv файл
  var cur_dt = StrSubSt(string(Date()),".","")+"_"+StrSubSt(string(Time()),":","");
  var datafilename = "47423forABS_"+cur_dt+".csv";

/*
№ п/п	Тип	Описание поля
1	CHAR	Номер счета 47423 
2	CHAR	Номер договора обслуживания
3	CHAR	ФИО клиента
*/


/* Временная таблица */
macro CheckAndCreateTable()
   var sql_str, cmd;
   sql_str = 
      " declare "+
      "    cnt number(10);  "+
      " begin  "+
      "    select count(*) into cnt from user_tables  "+
      "    where table_name = upper('uaccount_BIQ_11207'); "+
      "    if cnt =  0 then "+
      "        execute immediate 'create global temporary table uaccount_BIQ_11207 ( "+
      "                              T_NUMBER     VARCHAR2(20 BYTE), "+
      "                              T_DLCONTRID  NUMBER(10), "+
      "                              T_PARTYID    NUMBER(10), "+
      "                              T_ACCOUNT    VARCHAR2(25 BYTE), "+
      "                              T_CURRENCY   NUMBER(10), "+
      "                              T_CHAPTER    NUMBER(5) "+
      "        ) "+
      "        on commit preserve rows'; "+
      "    end if; "+
      "end;";
   cmd = RSDCommand(sql_str);
   cmd.execute;
   cmd.close;
   cmd = null; sql_str = null;
end;

macro RunReport()
  var sql_str, rs, cmd;
  var data_str;
  var CountContr = 0;

println(time() + " Начало");
  CheckAndCreateTable();
println(time() + " Таблица проверена");

   sql_str = " truncate table uaccount_BIQ_11207 ";
   cmd = RSDCommand(sql_str);
   cmd.execute;
   cmd.close;
println(time() + " Таблица очищена");

BegAction("Отбор данных");
   sql_str = " insert into uaccount_BIQ_11207 "+
            "  SELECT s.t_number, "+
            "       d.t_dlcontrid, "+
            "       s.t_partyid, "+
            "       a47423.t_account, "+
            "       a47423.t_currency, "+
            "       a47423.t_chapter "+
            "  FROM ddlcontr_dbt d, "+
            "       dsfcontr_dbt s, "+
            "       dparty_dbt p, "+
            "       ddlcontrmp_dbt mf, "+
            "       dsfcontr_dbt sf, "+
            "       dmcaccdoc_dbt a47423 "+
            " WHERE d.t_sfcontrid = s.t_id "+
            "   AND s.t_servkind = 0 "+
            "   AND S.T_DATECLOSE = TO_DATE ('01010001', 'ddmmyyyy') "+
            "   AND p.t_partyid = s.t_partyid AND p.t_legalform = 2 "+
            "   AND d.t_dlcontrid = mf.t_dlcontrid "+
            "   AND sf.t_id = mf.t_sfcontrid "+
            "   AND sf.t_dateclose = TO_DATE ('01010001', 'ddmmyyyy') "+
            "   AND a47423.t_clientcontrid  = sf.t_id "+
            "   AND a47423.t_iscommon = CHR (88) "+
            "   AND a47423.t_catid = 818 "+
            "   AND a47423.t_owner = sf.t_partyid";
   cmd = RSDCommand(sql_str);
   cmd.execute;
   cmd.close;

println(time() + " Таблица заполнена");
   
   // замена distinct
   sql_str = " DELETE FROM uaccount_BIQ_11207 "+
             "  WHERE rowid not in "+
             "  (SELECT MIN(rowid) "+
             "     FROM uaccount_BIQ_11207 "+
             "  GROUP BY t_number, t_dlcontrid, t_partyid, t_account, t_currency, t_chapter)";
   cmd = RSDCommand(sql_str);
   cmd.execute;
   cmd.close;
EndAction;

println(time() + " Удалены дубли");

   sql_str = "select "+
             "    acc.t_account, a47423.t_number, pers.t_name1, pers.t_name2, pers.t_name3  "+
             " from "+
             "    dpersn_dbt pers, "+
             "    daccount_dbt acc, "+
             "    uaccount_BIQ_11207 a47423 "+
             " where pers.t_isemployer <> chr(88) "+
             "   and a47423.t_partyid = pers.t_personid "+
             "   and acc.t_client = pers.t_personid "+
             "   and acc.t_balance = '47423' "+
             "   and a47423.t_chapter = acc.t_chapter "+
             "   and a47423.t_currency = acc.t_code_currency "+
             "   and a47423.t_account = acc.t_account "+
             "   and acc.t_open_close <> 'З'";
   rs = RSDRecordSet(sql_str);
   
println(time() + " Выгрузка в файл");
InitProgress(-1);
   if ( Open( datafile, datafilename, "rsansi" ) )      // создается в TXTDIR
     while (rs.movenext())
       data_str = "";
       data_str = rs.value("t_account")+";"+rs.value("t_number")+";"+rs.value("t_name1")+" "+SQL_ConvTypeStr(rs.value("t_name2"))+" "+SQL_ConvTypeStr(rs.value("t_name3")); 
       Insert( datafile, data_str );
       CountContr = CountContr+1;
       UseProgress(CountContr);
     end;
     Close( datafile );
   else
RemProgress();
     msgbox("Ошибка создания файла выгрузки!");
   end;
   rs.close; rs = null;
   cmd.close; cmd = null;

RemProgress();

   println("Выгружено записей: "+CountContr);

println(time() + " Конец");
end;

/* Точка входа */
  if (Gettrue(true, "Выгрузить счета 47423 для АБС?"))
     RunReport();
  end;

end;