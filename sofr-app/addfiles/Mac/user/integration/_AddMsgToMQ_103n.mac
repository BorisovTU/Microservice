/*
$Name:        _AddMsgToMQ_103.mac
$Module:      Securities
$Description: Добавление сообщений в очередь отправки
*/
import "dlcontrmsg_mmvb.mac";

private macro GetAMQPMOEXClientParams(host_:@string, vhost_:@string, user_:@string, password_:@string, port_:@integer, queue_:@string):integer
  var stat:integer = 0;
  var host, vhost, user, password, port, queue;

  GetRegistryValue("SECUR\\AMQPMOEXCLIENT\\HOST", V_STRING, host, stat);
  if(stat) return 1; end;

  GetRegistryValue("SECUR\\AMQPMOEXCLIENT\\VHOST", V_STRING, vhost, stat);
  if(stat) return 1; end;

  GetRegistryValue("SECUR\\AMQPMOEXCLIENT\\USER", V_STRING, user, stat);
  if(stat) return 1; end;

  GetRegistryValue("SECUR\\AMQPMOEXCLIENT\\PASSWORD", V_STRING, password, stat);
  if(stat) return 1; end;

  GetRegistryValue("SECUR\\AMQPMOEXCLIENT\\QUEUE", V_STRING, queue, stat);
  if(stat) return 1; end;

  GetRegistryValue("SECUR\\AMQPMOEXCLIENT\\PORT", V_INTEGER, port, stat);
  if(stat) return 1; end;

  host_ = host;
  vhost_ = vhost;
  user_ = user;
  password_ = password;
  port_ = port;
  queue_ = queue;  

  return 0;
end;

private macro GetErrDescr(er:variant) :string
  var errStr = "", i = 0;

  if(IsEqClass("TrslError", er))
    errStr = er.module + " "+er.line + " " + er.message;

    if(IsEqClass("TDbError", er.err))
      errStr = errStr + "\n:|" + er.err.Stat + "-" + er.err.Oper + "-" + er.err.Table + "-" + er.err.Message;

    elif(isEqClass("TRsdError", er.err))
      while(i < er.err.environment.ErrorCount)
        errStr = errStr + "\n" + er.err.environment.Error(i).Descr;
        i = i + 1;
      end;
    end;
  end;

  return errStr;
end;

var MQClient;
private macro CreateMQClient()
  var host, vhost, user, password, port, queue;
  if(GetAMQPMOEXClientParams(@host, @vhost, @user, @password, @port, @queue) == 0)
    MQClient = AMQPMOEXClient(host, vhost, user, password, port, queue);
    return 0;
  else
    return 1;
  end;
onError(er)
  return 1;
end;

private macro AddMesToMQ(partyID, dlContrID, dlContrMsgID, dataXML:@string, errMsg:@string)
  MQClient.publish(string(partyID), string(dlContrID), string(dlContrMsgID), dataXML);
  return 0;
onError(er)
  errMsg = GetErrDescr(er);
  return 1;
end;

private macro ТипСообщения(kind)
  if(kind == OBJTYPE_DLCONTRMSG_MARKETREG)
     return "Регистрация клиента на бирже";
  elif(kind == OBJTYPE_DLCONTRMSG_MPREG)
     return "Регистрация клиента на биржевой площадке";
  elif(kind == OBJTYPE_DLCONTRMSG_MPCLOSE)
     return "Закрытие биржевой площадки";
  elif(kind == OBJTYPE_DLCONTRMSG_MARKETCLOSE)
     return "Закрытие ДБО";
  elif(kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA)
     return "Изменение информации о личных данных клиента";
  else
    return string(kind);
  end;
end;

private macro _ДобавитьСообщВОчередьОтправки(PartyID, dlContrMsg, dataXML:@string, errMsg:@string)
  var stat = 0, statAddToMQ = 0;
  var sendMesState = DL_IIF(dlContrMsg.rec.SendMesState == 0, DLSENDMESSTATE_INQUEUE, DLSENDMESSTATE_REPEATINQUEUE);
  var transactionStarted:bool = false;

  RslDefCon.BeginTrans();
  transactionStarted = true;

  var DlContrMsgToUpd = TBFile("dlcontrmsg.dbt", "W", 0);
  DlContrMsgToUpd.rec.ID = dlContrMsg.rec.ID;

  if(DlContrMsgToUpd.GetEQ() and
     (DlContrMsgToUpd.rec.Version == dlContrMsg.rec.Version) and 
     (DlContrMsgToUpd.rec.SendMesState != DLSENDMESSTATE_SUCCESS))

    DlContrMsgToUpd.rec.SendMesState = sendMesState;
    if(not DlContrMsgToUpd.Update())
      stat = 1;
      ErrMsg = "Не удалось обновить сообщение с DlContrMsgID=" + dlContrMsg.rec.DlContrMsgID + ", выставив на нем статус=" + sendMesState;
    end;
    if(not DlContrMsgToUpd.GetEQ())
      stat = 1;
      ErrMsg = "Не удалось получить обновленное состояние сообщения с DlContrMsgID=" + dlContrMsg.rec.DlContrMsgID;
    end;
    if(stat == 0)
      if(DlContrMsgToUpd.rec.Version != dlContrMsg.rec.Version+1) // запись уже где-то изменилась
        stat = 1;
        ErrMsg = "У обновленного сообщения отличается Version. Вероятно оно конкурентно изменено. Сообщение с DlContrMsgID=" + dlContrMsg.rec.DlContrMsgID;
      end;
    end;

    if(stat == 0)
      var erMsgMQ = "";
      statAddToMQ = AddMesToMQ(PartyID, dlContrMsg.rec.DlContrID, dlContrMsg.rec.ID, @dataXML, @erMsgMQ);
      if(statAddToMQ != 0)
        DlContrMsgToUpd.rec.SendMesState = DLSENDMESSTATE_ER_ADDINQUEUE;
        ErrMsg = erMsgMQ;

        if(not DlContrMsgToUpd.Update())
          stat = 1;
          ErrMsg = "Не удалось добавить сообщение в очередь. Сообщение с DlContrMsgID=" + dlContrMsg.rec.DlContrMsgID + "; " + erMsgMQ;
        end;
      end;
    end;
  end;

  if(stat == 0)
    RslDefCon.CommitTrans();
  else
    RslDefCon.RollbackTrans();
  end;
  transactionStarted = false;

  if((stat == 0) and (statAddToMQ != 0))
    stat = statAddToMQ;
  end;

  return stat;
OnError(er)
  if(transactionStarted)
    RslDefCon.RollbackTrans();
  end;
  ErrMsg = GetErrDescr(er);
  return 1;
end;

private macro AddMsgToMQ()
  var stat:integer=0, dataXML:string="", find=false, i=0, ErrMsg:string= "", respData:string="";
  var dlContrMsg = TRecHandler("dlcontrmsg.dbt"),
      dlContr = TRecHandler("dlcontr.dbt"), 
      sfContr = TRecHandler("sfcontr.dbt");

  if(CreateMQClient() == 0)
    var sql = "SELECT m.* "+
               " FROM DDLCONTRMSG_DBT m, dsfcontr_dbt s, ddlcontr_dbt l "+
               " LEFT JOIN dobjatcor_dbt AtCor "+
                 " ON AtCor.t_Object = LPAD(l.T_DLCONTRID, 34, '0') "+
                " AND AtCor.t_ObjectType = 207 "+
                " AND AtCor.t_GroupID = 101 "+
                " AND AtCor.t_ValidFromDate = "+
                       " (SELECT MAX (t.t_ValidFromDate) "+
                          " FROM dobjatcor_dbt t "+
                         " WHERE t.t_ObjectType = AtCor.t_ObjectType "+
                           " AND t.t_GroupID = AtCor.t_GroupID "+
                           " AND t.t_Object = AtCor.t_Object) "+
              " WHERE m.T_ISONLINESENDMES = 'X' "+
                " AND m.T_SENDMESSTATE = ? "+
                " AND m.T_DLCONTRID = L.T_DLCONTRID "+
                " AND s.T_ID = L.T_SFCONTRID "+
                " AND NVL(AtCor.t_AttrID, -1) <> 3 "+
              " ORDER BY m.t_CreateDate, m.t_CreateTime";

    var cmd = DL_RSDCommand(sql);
    cmd.addParam(DLSENDMESSTATE_ER_CALLSTATSERV);

    InitProgress(cmd.GetCount(), "Добавление сообщений в очередь отправки", "Добавление сообщений в очередь отправки");

  [
                          Добавление сообщений в очередь отправки 
                                  за ########## г.
   ┌──────────────────────────────┬───────────────────────┬───────────────────────────────┬──────┬────────────────────────────────────────────────────────────┐
   │           Код ДБО            │ Наименование клиента  │         Тип сообщения         │ stat │                         Msg                                │
  ]
     ({CurDate});

    var ds = cmd.Execute();
    while(ds.moveNext())
      find = true;
      dlContrMsg.clear();
      ds.GetRecord().CopyTo(dlContrMsg.rec);

      respData = StrUpr(trim(DL_GetClobData("t_RespData", " from DDLCONTRMSG_DBT where t_ID = "+Int(dlContrMsg.rec.ID))));
      if(respData == StrUpr("Ошибка получения статуса обработки заявки. null"))

        if(DL_GetRecHandler(dlContr, "SELECT * FROM DDLCONTR_DBT WHERE T_DLCONTRID = "+dlContrMsg.rec.DlContrID) and
           DL_GetRecHandler(sfContr, "SELECT * FROM DSFCONTR_DBT WHERE T_ID = "+dlContr.rec.SfContrID)
          )
          dataXML = DL_GetDlContrMsgXml(dlContrMsg.rec.ID);
          if(strlen(dataXML) > 0)
            stat = _ДобавитьСообщВОчередьОтправки(sfContr.rec.PartyID, dlContrMsg, @dataXML, @ErrMsg);
          else
            stat = 1;
            ErrMsg = "Отсутствует тело xml-сообщения.";
          end;

  [├──────────────────────────────┼───────────────────────┼───────────────────────────────┼──────┼────────────────────────────────────────────────────────────┤
   │##############################│#######################│###############################│######│############################################################│]
             ( sfContr.rec.Number:w,
               DL_GetPartyShortName(sfContr.rec.PartyID):w,
               ТипСообщения(dlContrMsg.rec.Kind):w,
               string(stat):w,
               ErrMsg:w );

        end;
        i = i + 1;
        UseProgress(i);
      end;

    end;
  [└──────────────────────────────┴───────────────────────┴───────────────────────────────┴──────┴────────────────────────────────────────────────────────────┘];
    RemProgress();
    if(not find)
      [Нет сообщений для добавления в очередь.];
    end;
    
  else
    MsgBox("Ошибка при создании клиента подключения к очереди");
  end;
end;

AddMsgToMQ();
