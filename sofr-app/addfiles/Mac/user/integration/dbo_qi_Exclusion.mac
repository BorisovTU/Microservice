/**
 @file dbo_qi_Exclusion.mac
 @brief Рассылка уведомлений клиентам ФЛ о возможности подать заявление на исключение из реестра квалифицированных инвесторов
 
  # tag
 - functional_block:Отчетность_Клиент
 - code_type:API
 - code_type:Report
 - Квалификация
 - КИ
 - Квалифицированный инвестор
 
  # changeLog
 |date       |author         |tasks                              |note                                                        
 |-----------|---------------|-----------------------------------|-------------------------------------------------------------
 |00.00.2022 |Гераськина Т.В.|BIQ-9185                           | Создание
 |01.12.2023 |Топорков Д.В.  |BOSS-194                           | Дополнительные процедуры и функции
 |02.02.2024 |Гераськина Т.В.|DEF-60925                          | Дата подписания уведомения об исключении

 */
import rsd;
import "dbo_qi_func.mac";
import global_classes;
import "dbo_qi_ExclusionNotification.mac";

/* Процедура отбирает клиентов ФЛ со значением поля "Дата отправки" у сообщений "Уведомление о возможном исключении из КИ" 
   меньше или равно текущей даты на 1 календарный год, если поле "дата отправки" заполнено, 
   или поле "Дата включения в реестр" в реестре КИ, если поле "Дата отправки" у сообщения на ДБО "Уведомление о возможном исключении из КИ" не заполнено 
   или такое сообщение не создавалось.*/
/* Отбор ведется не по сообщениям, так как договоров у клиента может быть 2 и сообщения формируются только под первым, а по примечению субъекта
   "Дата уведомления о возможном исключении из КИ", примечание заполняется при удачной отправке сообщения "Уведомление о возможном исключении из КИ" */

macro RunExclusionQI(_repdate, _ismanual)
   var sql_str, cmd, rs;
   var dlid, mess;
   var g_repdate;
   var stat = 0, err_txt = "";
   var partyid;
   var res = c_Error;
   var sql_str2, cnt = 0, ai = 0;

   if(ValType(_repdate) == V_UNDEF)
     g_repdate = date();
   else
     g_repdate = _repdate;
   end;

   if (not _ismanual) 
      _ismanual = false;
   end;

   sql_str = "select qi.t_state, qi.t_partyid, qi.t_regdate, qi.t_changedate from dscqinv_dbt qi "+
// действующий квал.инвестор
             " where qi.t_state = 1 "+
// дата включения меньше текущей на год             
             "   and add_months(qi.t_regdate,12) <= :dt1 "+  
// примечание отсутствует (сообщение не отправлялось) или в примечании дата меньше текущей текущей на год (за текущий год отсекаются)
             "   and not exists (select 1 from dnotetext_dbt n where n.t_objecttype = 3 "+
             "                      and n.t_documentid = lpad(qi.t_partyid,10,'0') and n.t_notekind = :notekind "+
             "                      and rsb_struct.getdate(n.t_text) > add_months(:dt2,-12)  )"+
// только с открытыи договорами, так как иначе не к чему привязать сообщение
             "   and exists (select 1 from dsfcontr_dbt sf where sf.t_partyid = qi.t_partyid and (sf.t_dateclose > :dt3 "+
             "                 OR sf.t_dateclose = to_date('01.01.0001','dd.mm.yyyy')) ) "; 
;
   // для тестов
   //sql_str = sql_str + " and rownum <= 2 ";
   
   if (_ismanual)
      sql_str2 = "select count(*) from ("+sql_str+")";
      cmd = RSDCommand(sql_str2);
      cmd.AddParam("dt1", RSDBP_IN, g_repdate);
      cmd.AddParam("notekind", RSDBP_IN, NOTEKIND_DATE_EXCLUSION_QI);
      cmd.AddParam("dt2", RSDBP_IN, g_repdate);
      cmd.AddParam("dt3", RSDBP_IN, g_repdate);
      rs = RSDRecordSet(cmd);
      if (rs.movenext)
        cnt = Int(rs.value(0));
      end;
      rs.close; cmd.close; rs = null; cmd = null;
      InitProgress(cnt, "Формирование и отправка уведомлений о праве исключения из реестра квал.инвесторов", "Формирование и отправка уведомлений");
   end;
    
   cmd = RSDCommand(sql_str);
   cmd.AddParam("dt1", RSDBP_IN, g_repdate);
   cmd.AddParam("notekind", RSDBP_IN, NOTEKIND_DATE_EXCLUSION_QI);
   cmd.AddParam("dt2", RSDBP_IN, g_repdate);
   cmd.AddParam("dt3", RSDBP_IN, g_repdate);
   rs = RSDRecordSet(cmd);


   while (rs.movenext)
      partyid = rs.value("t_partyid");
      dlid = FindFirstContract(partyid, g_repdate);
      mess = dboMessage_qi(dlid, EXCLUSIONRIGHT_QI);
      if (mess.statsend == 0) // сформировано без ошибок
         mess.CreateMessage(false, true);
         mess.FixUQIAccreditation();
         if (mess.statsend == 1) // сообщение отправлено
            stat = addnoteforobject(3, String(partyid:o:10),NOTEKIND_DATE_EXCLUSION_QI, g_repdate);
            if (stat)
               err_txt = "Ошибка добавления примечания "+NOTEKIND_DATE_EXCLUSION_QI+"="+g_repdate+" по клиенту "+partyid;
               //RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
            end;
         end;
      else
         mess.FixUQIAccreditation();
      end;
      if (_ismanual)
         ai = ai+1;
         UseProgress(ai);
      end;
   end;
   RemProgress();
   res.ErrorCode = 0;
   res.ErrorDesc = "OK";

   return res;
   
onError(err)
   RemProgress();
   res.ErrorCode = err.Code;
   res.ErrorDesc = err.message + getFullCmdErrorText(cmd);
   return res;
end;

/**
@brief Функция вызова процедуры создания и отправки уведомления об исключении из реестра квалифицированных инвесторов
@param[in] dlContrID ID Договора брокерского обслуживания (таблица dDlContr_dbt)
@param[in] exclusionDate Дата исключения из реестра КИ
@return V_STRING Сообщение об ошибке ("" в случае успеха)
*/
macro QIExclusionNotify(dlContrID: Integer, exclusionDate: Date): String
  var error: String = "";
  var params: CQINotificationParams = CQINotificationParams();
  var resultparams : CQINotificationResultParams = CQINotificationResultParams();

  params.dlContrID = dlContrID;
  params.client = GetClient(params.dlContrID);
  params.signer = GetQIDefaultSigner();
  // params.notificationDate = exclusionDate;
  // DEF-60925 в печатной форме должна проставляться системная дата
  params.notificationDate = date();
  
  error = QIExclusionNotification(params, resultparams);
  SetParm(2, resultparams);
  
  return error;
  
onError(err)
  error = "Ошибка выполнения:|" + err.Module + " строка " + err.Line + "|" + err.message;
  return error;
end;

/**
@brief Функция вызова процедуры создания и отправки уведомления об исключении из реестра квалифицированных инвесторов
@param[in] sfContrID ID Договора обслуживания (таблица dSfContr_dbt)
@param[in] exclusionDate Дата исключения из реестра КИ
@param[out] error Сообщение об ошибке
@return V_INTEGER Статус выполнения (0 - успешное выполнение)
*/
macro QIExclusionNotifyBySfContr(sfContrID: Integer, exclusionDate: Date, error: @String): Integer
  var rs = ExecSQLSelect("SELECT t_dlContrID FROM dDlContr_dbt WHERE t_sfContrID = " + sfContrID);

  error = "";

  if (not rs.movenext())
    error = "Ошибка формирования уведомления:|Не найден договор брокерского обслуживания для договора обслуживания с ID = " + sfContrID;
    return 1;
  end;
  
  error = QIExclusionNotify(rs.value("t_dlContrID"), exclusionDate);
  
  if (StrLen(error) > 0)
    error = "Ошибка формирования уведомления:|" + error;
    return 1;
  end;
  
  return 0;
  
onError(err)
  error = "Ошибка выполнения при формировании уведомления:|" + err.Module + " строка " + err.Line + "|" + err.message;
  return 1;
end;


/**
@brief Функция вызова процедуры создания и отправки уведомления об исключении из реестра квалифицированных инвесторов
@param[in] PartyID ID Клиента (dParty_dbt)
@param[in] exclusionDate Дата исключения из реестра КИ
@param[out] error Сообщение об ошибке
@return V_INTEGER Статус выполнения (0 - успешное выполнение)
*/
macro QIExclusionNotifyByParty(PartyID: Integer, exclusionDate: Date, error: @String): Integer
  var sql = " SELECT t_dlContrID  as t_dlContrID "   +
            "   FROM dDlContr_dbt  dl join dsfcontr_dbt sf on dl.t_sfcontrid = sf.t_id and sf.t_servkind = 0 "   +
            "  WHERE sf.t_dateclose = to_date('01.01.0001','dd.mm.yyyy') AND sf.t_partyid = " + PartyID;
  var cmd = DL_RSDCommand();
  var cnt = cmd.GetCount(sql);
  var DataSet;
  
  error = "";

  if(cnt > 0)
    DataSet = cmd.Execute(sql);

    while(DataSet.moveNext())
      error = QIExclusionNotify(DataSet.value("t_dlContrID"), exclusionDate);
      
      if (StrLen(error) > 0)
        error = "Ошибка формирования уведомления:|" + error;
        return 1;
      end;
    end;
  else 
    error = "Ошибка формирования уведомления:|Не найден договор брокерского обслуживания для клиента с ID = " + PartyID;
    return 1;
  end;
  
  return 0;          
  
onError(err)
  error = "Ошибка выполнения при формировании уведомления:|" + err.Module + " строка " + err.Line + "|" + err.message;
  return 1;
end;