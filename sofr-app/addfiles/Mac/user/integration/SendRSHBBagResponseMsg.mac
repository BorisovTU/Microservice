import "func_lib.mac";
import "global_utils_intgr.mac";
import "uws_exchng_log.mac";

private class c_Error(IncomeData)
   var ErrorCode : string;
   var ErrorDesc : string;

   private macro m_init_Error(IncomeData)
      private var err_txt;
      private var nametag_up = "SendRSHBBagResponseMsg.SendRSHBBagResponse.ErrorList.Error";

      ErrorCode = getValueFromProperty(IncomeData, "ErrorCode", nametag_up, true);
      ErrorDesc = getValueFromProperty(IncomeData, "ErrorDesc", nametag_up);
   end;

   m_init_Error(IncomeData);
end;

private class c_SendRSHBBagResponse(IncomeData)
   var ResultCode : String; 
   var RequestId;   // c_IntegrationSymbolicIdentifierXType
   var ErrorList;   // TArray

   private macro m_init_SendRSHBBagResponse(IncomeData)
      private var aux_buff;
      private var err_txt;
      private var nametag_up = "SendRSHBBagResponseMsg.SendRSHBBagResponse";

      if(ValType(IncomeData) != V_UNDEF)
         ResultCode  = getValueFromProperty(IncomeData, "ResultCode"    , nametag_up, true);
         RequestId   = getSymbolFromProperty(IncomeData, "RequestId" , nametag_up, true);
         
         aux_buff = uTryToGetPropS(IncomeData, "ErrorList");
         if(ValType(aux_buff) == V_GENOBJ)
            ErrorList = TArray;
            aux_buff = 0;
            while(IncomeData.ErrorList.size > aux_buff)
               ErrorList.value(ErrorList.size) = c_Error(IncomeData.ErrorList.value(aux_buff));
               aux_buff = aux_buff + 1;
            end;
         elif(ValType(aux_buff) == V_UNDEF)
         else
            err_txt = string(InErrNotObj, nametag_up+".ErrorList");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
         end;
            
      end;
   end;

   m_init_SendRSHBBagResponse(IncomeData);
end;

private class c_SendRSHBBagResponseMsg(p_income_data)
   var SendRSHBBagResponse : object; 

   private macro m_init_SendRSHBBagResponseMsg(i_income_data)
      private var v_aux_buf;
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "SendRSHBBagResponse"));
         if(v_aux_buf != V_UNDEF)
            if(v_aux_buf == V_GENOBJ)
               SendRSHBBagResponse = c_SendRSHBBagResponse(i_income_data.SendRSHBBagResponse);
            else
               v_service_msg = string(InErrNotObj, "SendRSHBBagResponse");
               RunError(v_service_msg, c_usr_err_obj(v_service_msg));
            end;
         else
            v_service_msg = string(InErrComPart, "SendRSHBBagResponse");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;
      end;
   end;

   m_init_SendRSHBBagResponseMsg(p_income_data);
end;


/*-------------*/
/* ТОЧКА ВХОДА */
/*-------------*/
macro SendRSHBBagResponseMsg(p_income_data, p_log_id)
   var v_logger_obj;
   var v_income_data;
   var v_aux_buf;
   var v_cntr_err;
   
   var Reqid;
   var vresulttext= "", vresultcode; 
   var sql_str, cmd;
   
   /* Получаем проверенный объект со входными данными */
   v_income_data = c_SendRSHBBagResponseMsg(p_income_data);

   v_logger_obj = uws_exchng_logger();
   v_logger_obj.set_log_id(p_log_id);

   if (v_income_data.SendRSHBBagResponse.ResultCode == "0")  // успешно
      vresulttext = "OK";
      vresultcode = 0;
   else
      vresulttext = "";
      v_cntr_err = 0;
      while(v_income_data.SendRSHBBagResponse.ErrorList.size > v_cntr_err)
         /* Описание ошибок */
         vresulttext = m_add_note_to_str(string("Получена ошибка: ",
                                              v_income_data.SendRSHBBagResponse.ErrorList.value(v_cntr_err).ErrorCode,
                                              ";",
                                              v_income_data.SendRSHBBagResponse.ErrorList.value(v_cntr_err).ErrorDesc,
                                              ")."),
                                          vresulttext);
         vresultcode = v_income_data.SendRSHBBagResponse.ErrorList.value(v_cntr_err).ErrorCode;

         v_cntr_err = v_cntr_err + 1;
      end;
   end;

   /* Сохраняем в логи */
   v_aux_buf = strlen(vresulttext);
   if(v_aux_buf > 0)
      if(v_aux_buf >= 2000)
         vresulttext = substr(vresulttext, 1, 1999);
      end;
      v_logger_obj.add_error_info(vresultcode, vresulttext);

      Reqid = v_income_data.SendRSHBBagResponse.RequestId.ObjectId;

      sql_str = " update SOFRBAG_LAUNCH_LOG "+
                "    set answer_resultcode = :answer_code, answer_err = :answer_err "+
                "  where requestid_single = :reqid";
      cmd = RSDCommand(sql_str);
      cmd.AddParam("answer_code", RSDBP_IN, vresultcode);
      cmd.AddParam("answer_err", RSDBP_IN, vresulttext);
      cmd.AddParam("reqid", RSDBP_IN, Reqid);
      cmd.execute;
      cmd.close;   cmd = null; sql_str = null;
   end;

   v_logger_obj = NULL;

   return true;

onError(err)
   v_logger_obj.add_error_info(c_usr_err_obj.obtain_err_code(err), c_usr_err_obj.obtain_err_msg(err));

   return false;
end;