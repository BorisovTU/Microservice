//DEF-34782
//SD8780641||--- Отсутствует ---||Требуется макрос проставляющий тарифы на субдоговорах
/* Отбирает договора, у которых отличаются действующие тарифные планы на основном договоре и на субдоговоре 
и проставляет ТП субдоговору такой же, как у основного в текущей календарной дате */
// Гераськина Т.В. 15.12.2022

import rsd, SfInter, BankInter;
var sfid = 0, tarifid = 0, partyid = 0;
var state;
var err_txt = "";
var sql_deals_a, cmd_deals, rs_deals;
var sql_deals_b;
var flag = false;
var cnt = 0;
var subdog_num;
var dd,mm,yy, tarif_date, tarif_name_main, tarif_name_sub; 

var dt = StrSubst(String(date()), ".", "");
var tm = StrSubst(String(time()), ":", "");
var InfoLogFileName = "..\\Export\\DEF34782_"+dt+"_"+tm+".log";

SetOutput( InfoLogFileName, false ); 

datesplit (date(), dd, mm, yy);
tarif_date = date(1,mm,yy);

private macro FindTarifName(_id)
   var sql, cmd, rs;
   var res = "Инд.условия";
   sql = "select t_name from dsfplan_dbt where t_sfplanid = :pid";
   cmd = RSDCommand(sql);
   cmd.AddParam("pid", RSDBP_IN, _id);
   rs = RSDRecordSet(cmd);
   if (rs.movenext)
      res = rs.value(0);
   end;
   return res;
end;

//текст ошибки RS Bank
private macro GetErrMessage(err_code)
  var err_msg = "";
  var cmd = RsdCommand(
      "select t_Contents from dbank_msg "
      "where t_Number = ? "
  );
  cmd.addParam("Number", RSDBP_IN, err_code);
  cmd.execute();

  var rs = RsdRecordset(cmd);
  if (rs.moveNext())
    err_msg = rs.value(0);
  end;

  return err_msg;
end;


  
var sql_str = "with bigdata as ( "+
              "   select  dl.t_dlcontrid, sf.t_number, sf.t_datebegin, sf.t_partyid, "+
              "           mp_sf.t_id, "+
              "           mp.t_marketid, mp_sf.t_number number_sub, mp_sf.t_datebegin begin_sub, mp_sf.t_dateclose, mp_sf.t_servkind, mp_sf.t_servkindsub, "+
              "           sf_p.t_sfplanid main_dog, sf_p.t_begin main_tarif_begin, mp_sf_p.t_sfplanid sub_dog, mp_sf_p.t_begin sub_tarif_begin "+
              "   from "+
              "      ddlcontr_dbt dl, "+
              "      dsfcontr_dbt sf, "+
              "      ddlcontrmp_dbt mp, "+
              "      dsfcontr_dbt mp_sf, "+
              "      dsfcontrplan_dbt sf_p, "+
              "      dsfcontrplan_dbt mp_sf_p, "+
              "      dparty_dbt p "+
              "   where dl.t_sfcontrid = sf.t_id and sf.t_dateclose = to_date('01010001','ddmmyyyy') "+
              "     and mp.t_dlcontrid = dl.t_dlcontrid  "+
              "     and mp_sf.t_id = mp.t_sfcontrid and mp_sf.t_dateclose = to_date('01010001','ddmmyyyy') "+
              "     and sf_p.t_sfcontrid = sf.t_id and sf_p.t_end = to_date('01010001','ddmmyyyy') "+
              "     and mp_sf_p.t_sfcontrid = mp_sf.t_id and mp_sf_p.t_end = to_date('01010001','ddmmyyyy') "+
              "     and p.t_partyid = sf.t_partyid and p.t_legalform = 2 ) "+
              " select  bigdata.t_partyid, bigdata.t_dlcontrid, bigdata.t_number num_dog, bigdata.t_datebegin date_dog, "+
              "         bigdata.t_id sfid, bigdata.number_sub, bigdata.begin_sub, cod.t_code party_code, "+
              "         bigdata.main_dog, bigdata.main_tarif_begin, bigdata.sub_dog, bigdata.sub_tarif_begin "+
              "   from  bigdata, "+
              "         dobjcode_dbt cod "+
              "  where cod.t_objecttype = 3 and cod.t_codekind = 101 and cod.t_objectid = bigdata.t_partyid "+
              "    and bigdata.main_dog <> bigdata.sub_dog"; 

// сделки ФИССиКО
sql_deals_a = "select t_code, to_char(t_date,'dd.mm.yyyy') t_date from ddvdeal_dbt d "+
              " where d.t_ClientContr = :sfid and d.t_client = :partyid and d.t_date >= to_date('26102022','ddmmyyyy') ";
// сделки по ценным бумагам
sql_deals_b = "select t_dealcode, to_char(t_dealdate,'dd.mm.yyyy') t_dealdate from ddl_tick_dbt d "+
              " where d.t_clientcontrid = :sfid and d.t_clientid = :partyid and d.t_bofficeKind = 101 and d.t_dealdate >= to_date('26102022','ddmmyyyy') ";

var rs = RSDRecordSet(sql_str);
InitProgress(-1);
while (rs.movenext)
   sfid = rs.value("sfid");
   partyid = rs.value("t_partyid");
   tarifid = rs.value("main_dog");
   subdog_num = String(rs.value("number_sub"):w:20);
   if (rs.value("sub_dog") != tarifid) // условие есть в самом селекте, но пусть будет
      flag = true;
      cmd_deals = RSDCommand(sql_deals_a);
      cmd_deals.AddParam("sfid", RSDBP_IN, sfid);
      cmd_deals.AddParam("partyid", RSDBP_IN, partyid);
      rs_deals = RSDRecordSet(cmd_deals);
      if (rs_deals.movenext) // достаточно одной сделки
         flag = false;
         err_txt = "Субдоговор " + subdog_num + ": обнаружены торговые операции после 26.10.2022, сделка ФИССиКО "+rs_deals.value("t_code") +" от "+ rs_deals.value("t_date")+". Требуется ручная обработка!";
      end;
      
      if (flag)
         cmd_deals = RSDCommand(sql_deals_b);
         cmd_deals.AddParam("sfid", RSDBP_IN, sfid);
         cmd_deals.AddParam("partyid", RSDBP_IN, partyid);
         rs_deals = RSDRecordSet(cmd_deals);
         if (rs_deals.movenext) // достаточно одной сделки
            flag = false;
            err_txt = "Субдоговор " + subdog_num + ": обнаружены торговые операции после 26.10.2022, сделка по ценным бумагам "+rs_deals.value("t_dealcode") +" от "+ rs_deals.value("t_dealdate")+". Требуется ручная обработка!";
         end;
      end;
      
      if (flag)
         if (tarif_date < rs.value("begin_sub"))
           tarif_date = date(rs.value("begin_sub"));
         end;

         state  = SfContrChangeSfPlan(sfid, tarifid, tarif_date, false);
         if(state != 0)
            err_txt = String("Субдоговор " + subdog_num + "Ошибка смены тарифа "+GetErrMessage(state) );
         else 
            tarif_name_main = FindTarifName(rs.value("main_dog"));
            tarif_name_sub = FindTarifName(rs.value("sub_dog"));;

            err_txt = "Субдоговор " + subdog_num + ": выполнена смена ТП со значения " + tarif_name_sub + " на значение " + tarif_name_main;
         end;
      end; 
   end;

   println(err_txt);

   cnt = cnt+1;
   UseProgress(cnt);

end;
RemProgress();

if (cnt == 0)
   println("Нет подходящих договоров");
end;

SetOutput(null, false);
ViewFile(InfoLogFileName);

exit(1);


