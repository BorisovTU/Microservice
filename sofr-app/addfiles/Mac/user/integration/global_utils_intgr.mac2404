/*------------------------------------------*/
/* Интеграция. Функционал общего назначения */
/*                                          */
/* Автор: Карпов В.                         */
/*------------------------------------------*/

import rsd;
import serviceaction; /* Для GetObjProps() */
import globals; /* Для {oper} */
//import BankInter; /* Для GetRegistryValue() */ /* Содержится в globals.mac */
import email_notifyfun; /* Функционал работы с e-mail уведомлениями */
import Календарь;


const UNIVERSAL_ERROR_TYPE = 5;
const UNIVERSAL_ERROR_CODE = -10001;
const GC_ADD_DEV_ERR_CODE  = -10101;
const InErrComPart = "Не задан обязательный параметр ";
const InErrNotObj = "Нарушена структура данных: ";


/*------------------------------------------------------------------------------*/
/* Базовый класс для инициализации свойств из строки с ; в качестве разделителя */
/*------------------------------------------------------------------------------*/
class с_init_obj_param(p_str_param : string)

   private macro m_init_с_init_obj_param(i_str_param)
      private const StrSeparator : string = ";";
      private var StrParamBuf : string = "",
                  i : integer = 0,
                  StrPos : integer = 0;

      if((Valtype(i_str_param) != V_UNDEF) and (Valtype(i_str_param) != 26) and (i_str_param != ""))
         StrParamBuf = i_str_param;

         i = 0;
         while((StrLen(StrParamBuf) > 0) and (GenNumProps(this) > i)) // Заполняем свойства
            StrPos = Index(StrParamBuf, StrSeparator);

            if(StrPos > 1)
               this[i] =  Substr(StrParamBuf, 1, StrPos - 1);
               StrParamBuf = Substr(StrParamBuf, StrPos + 1);
            elif((StrPos == 0) and (StrLen(StrParamBuf) > 0))
               this[i] = StrParamBuf;
               StrParamBuf = "";
            end;

            i = i + 1;
         end;
      end;

   onError(err)
      private var j = 0;
      while(GenNumProps(this) > j)
         this[j] = NULL;

         j = j + 1;
      end;
   end;

   m_init_с_init_obj_param(p_str_param);

end; /* class с_init_obj_param() */


/*---------------------------------------*/
/* Класс с основными параметрами события */
/*---------------------------------------*/
class c_event_main_prm()
   var v_func_res   /* Результат работы функции получения параметров события */
      ,v_func_msg   /* Описание кода возврата */
      ,v_recid
      ,v_objecttype
      ,v_objectid
      ,v_type
      ,v_status;
end; /* class c_event_main_prm() */


/*------------------------------------------------------*/
/* Функция получения значения свойства заданного класса */
/* Если передана пустая строка, то функция вернет NULL  */
/*------------------------------------------------------*/
macro uTryToGetPropS(obj:object, propName:string)
   var ret;
   ret = genGetProp(obj, propName);
   if(ValType(ret) == V_STRING)
      if(StrLen(ret) == 0)
         ret = NULL;
      end;
   elif(ValType(ret) == V_DATE)
      if(ret == date(0, 0, 0))
         ret = NULL;
      end;
   end;
   return ret;

   onError
      return NULL
end;


/*------------------------------------------*/
/* Класс для хранения полного текста ошибки */
/*------------------------------------------*/
class c_usr_err_obj(p_err_msg, p_err_code)
   private var v_prop_list = TArray();
   private var v_err_code = "";
   var v_err_msg = "";

   /* Конструктор */
   private macro init_err_obj_params(p_err_msg, p_err_code)
      if(ValType(p_err_msg) != V_UNDEF)
         v_err_msg = string(p_err_msg);
      end;
      if(ValType(p_err_code) != V_UNDEF)
         v_err_code = string(p_err_code);
      end;
   end;

   /* Поиск свойства с заданным именем в заданном объекте */
   private macro try_to_find_prop(p_err_obj, p_prop_name, p_use_exist)
      var v_ret = false;
      var v_cntr_list = 0;

      if((ValType(p_use_exist) == V_UNDEF))
         v_prop_list.size = 0;
         v_prop_list = TArray(GetObjProps(p_err_obj, true));
      else
         if(not p_use_exist)
            v_prop_list.size = 0;
            v_prop_list = TArray(GetObjProps(p_err_obj, true));
         end;
      end;

      while((not v_ret) and (v_prop_list.size > v_cntr_list))
         if(v_prop_list.value(v_cntr_list) == p_prop_name)
            v_ret = true;
         end;
         v_cntr_list = v_cntr_list + 1;
      end;

      if(ValType(p_use_exist) == V_UNDEF)
         v_prop_list.size = 0;
      end;

      return v_ret;
   end;

   /* Получить v_err_msg */
   macro get_err_msg()
      return v_err_msg;
   end;

   /* Получить v_err_code */
   macro get_err_code()
      return v_err_code;
   end;

   /* Получить служебное сообщение из объекта ошибки */
   macro obtain_err_msg(p_err_obj)
      private var actx_code, actx_text, actx_msg;
      private var v_err_obj = NULL;

      /* Первый вызов try_to_find_prop() собирает массив свойств */
      if(try_to_find_prop(p_err_obj, "AxCode", false))
         actx_code = uTryToGetPropS(p_err_obj, "AxCode");
      else
         actx_code = 0;
      end;
      if(try_to_find_prop(p_err_obj, "Message", true))
         actx_text = uTryToGetPropS(p_err_obj, "Message");
      else
         actx_text = "0";
      end;
      if(try_to_find_prop(p_err_obj, "AxMes", true))
         actx_msg = uTryToGetPropS(p_err_obj, "AxMes");
      else
         actx_msg = "0";
      end;
      if(actx_code == 0)
         if(try_to_find_prop(p_err_obj, "Err", true))
            v_err_obj = uTryToGetPropS(p_err_obj, "Err");
            if(ValType(v_err_obj) == V_GENOBJ)
               if(try_to_find_prop(v_err_obj, "v_err_msg"))
                  v_err_msg = uTryToGetPropS(v_err_obj, "v_err_msg");
                  if(ValType(v_err_msg) == V_UNDEF)
                     v_err_msg = actx_text;
                  end;
               else
                  v_err_msg = actx_text;
               end;
            else
               v_err_msg = actx_text;
            end;
         else
            v_err_msg = actx_text;
         end;
      else
         v_err_msg = string(actx_text, ": Code: ", actx_code, "; Text: ", actx_msg);
      end;

      return v_err_msg;
   end;

   /* Получить код ошибки */
   macro obtain_err_code(p_err_obj, p_def_code)
      private var v_err_obj = NULL;

      /* При каждом вызове try_to_find_prop() массив свойств пересобирается */
      /* Если обрабатывается объект этого класса */
      if(try_to_find_prop(p_err_obj, "Err"))
         v_err_obj = uTryToGetPropS(p_err_obj, "Err");
         if(ValType(v_err_obj) == V_GENOBJ)
            if(try_to_find_prop(v_err_obj, "v_err_msg"))
               v_err_code = p_err_obj.err.get_err_code();
            end;
         end;
      end;

      /* Остальные объекты */
      if(strlen(v_err_code) == 0)
         if(ValType(p_def_code) != V_UNDEF)
            v_err_code = p_def_code;
         else
            if(try_to_find_prop(p_err_obj, "Code"))
               v_err_code = uTryToGetPropS(p_err_obj, "Code");
               if(ValType(v_err_code) == V_UNDEF)
                  v_err_code = UNIVERSAL_ERROR_CODE;
               end;
            else
               v_err_code = UNIVERSAL_ERROR_CODE;
            end;
         end;
      end;

      return v_err_code;
   end;

   macro obtain_err_axcode_exactly(p_err_obj, p_def_val)
      private var v_err_axcode;

      if(try_to_find_prop(p_err_obj, "AxCode"))
         v_err_axcode = uTryToGetPropS(p_err_obj, "AxCode");
      else
         if(ValType(p_def_val) == V_UNDEF)
            v_err_axcode = 0;
         else
            v_err_axcode = p_def_val;
         end;
      end;

      return v_err_axcode;
   end;

   macro obtain_err_message_exactly(p_err_obj, p_def_val)
      private var v_err_message;

      if(try_to_find_prop(p_err_obj, "Message"))
         v_err_message = uTryToGetPropS(p_err_obj, "Message");
      else
         if(ValType(p_def_val) == V_UNDEF)
            v_err_message = "0";
         else
            v_err_message = p_def_val;
         end;
      end;

      return v_err_message;
   end;

   macro obtain_err_axmes_exactly(p_err_obj, p_def_val)
      private var v_err_axmes;

      if(try_to_find_prop(p_err_obj, "AxMes"))
         v_err_axmes = uTryToGetPropS(p_err_obj, "AxMes");
      else
         if(ValType(p_def_val) == V_UNDEF)
            v_err_axmes = "0";
         else
            v_err_axmes = p_def_val;
         end;
      end;

      return v_err_axmes;
   end;

   init_err_obj_params(p_err_msg, p_err_code);
end; /* class c_usr_err_obj() */


/*-------------------------------------------------------*/
/* Обертка для функционала работы с e-mail уведомлениями */
/*-------------------------------------------------------*/
class c_email_proc_env()
   private var vg_is_error;
   private var vg_service_msg;
   /* Чтобы лишний раз не вычислять длину строки */
   private var vg_is_msg_text; /* Признак не пустого текста */
   private var vg_is_email_list; /* Признак не пустого списка адресов */

   private var vg_msg_head;   /* Заголовок письма */
   private var vg_msg_text;   /* Текст письма */
   private var vg_email_list; /* Список адресов получателей */
   private var vg_attach_list; /* Список вложений */

   /* Метод получения текста сервисного сообщения */
   macro get_service_msg()
      return vg_service_msg;
   end;

   macro m_get_error_status()
      return vg_is_error;
   end;

   /* Задать заголовок письма */
   macro m_set_msg_head(p_msg_head)
      vg_msg_head = p_msg_head;
   end;

   /* Очистить текст письма */
   macro m_clear_msg_text()
      vg_msg_text = "";
      vg_is_msg_text = false;
   end;

   /* Очистить список адресатов */
   macro m_clear_email_list()
      vg_email_list = "";
      vg_is_email_list = false;
   end;

   /* Добавить новую строку к тексту письма */
   macro m_add_row_to_msg_text(p_add_text)
      if(strlen(p_add_text) > 0)
         if(vg_is_msg_text)
            vg_msg_text = string(vg_msg_text, "\n", p_add_text);
         else
            vg_msg_text = p_add_text;
            vg_is_msg_text = true;
         end;
      end;
   end;

   /* Добавить новый адрес к списку получателей письма */
   macro m_add_email_to_list(p_add_email)
      if(strlen(p_add_email) > 0)
         if(vg_is_email_list)
            vg_email_list = string(vg_email_list, ";", p_add_email);
         else
            vg_email_list = p_add_email;
            vg_is_email_list = true;
         end;
      end;
   end;

   /* Добавить путь к файлу, который нужно приаттачить к письму */
   macro m_add_attach_to_list(p_add_attach)
      if(strlen(p_add_attach) > 0)
         vg_attach_list(vg_attach_list.size) = p_add_attach;
      end;
   end;
   /*----------------------------------------------*/
   /* Метод сохранения сообщения в БД для отправки */
   /*     Письма будут отправлены планировщиком    */
   /*----------------------------------------------*/
   macro m_save_to_submit()
      vg_is_error = false;
      /* Сохранение в БД */
      AddNotifyToDbt(vg_msg_head, vg_msg_text, vg_email_list, vg_attach_list);

   onError(err)
      vg_is_error = true;
      vg_service_msg = c_usr_err_obj.obtain_err_msg(err);
   end;

   /*---------------------------------------------*/
   /*             Метод отправки писем            */
   /* Будут отправлены все не отправленные письма */
   /*---------------------------------------------*/
   macro m_submit_email()
      vg_is_error = false;
      /* Отправка писем */
      SendNotyfyToEmail(/*fromCnv:bool, interface_mode:bool*/);

   onError(err)
      vg_is_error = true;
      vg_service_msg = c_usr_err_obj.obtain_err_msg(err);
   end;

   /* Конструктор */
   private macro m_init_prime()
      vg_is_error = true;
      vg_service_msg = "";
      vg_msg_head = "";
      vg_msg_text = "";
      vg_is_msg_text = false;
      vg_email_list = "";
      vg_is_email_list = false;
      vg_attach_list = TArray();
   end;

   m_init_prime();
end; /* class c_email_proc_env() */


/*-------------------------------------------------*/
/* Вставка записи в таблицу utableprocessevent_dbt */
/*-------------------------------------------------*/
/* Для p_event_type и p_event_stat предусмотрены   */
/* значения по умолчанию                           */
/*-------------------------------------------------*/
macro m_make_event_to_process(p_objecttype, p_objectid, p_event_type, p_event_stat)
   const C_EVENT_TYPE = 1;
   const C_EVENT_STAT = 1;

   var v_ret = true;
   var v_sql, v_cmd;

   if((ValType(p_objecttype) == V_UNDEF) or (ValType(p_objectid) == V_UNDEF))
      v_ret = false;
   end;

   if(v_ret)
      if(ValType(p_event_type) == V_UNDEF)
         p_event_type = C_EVENT_TYPE;
      end;

      if(ValType(p_event_stat) == V_UNDEF)
         p_event_stat = C_EVENT_STAT;
      end;

      v_sql =         "INSERT INTO utableprocessevent_dbt ";
      v_sql = v_sql + "            (t_timestamp, t_objecttype, t_objectid, t_type, t_status) ";
      v_sql = v_sql + "     VALUES (SYSDATE, :p_objecttype, :p_objectid, :p_event_type, :p_event_stat) ";

      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_objecttype", RSDBP_IN, p_objecttype);
      v_cmd.addParam("p_objectid", RSDBP_IN, p_objectid);
      v_cmd.addParam("p_event_type", RSDBP_IN, p_event_type);
      v_cmd.addParam("p_event_stat", RSDBP_IN, p_event_stat);
      v_cmd.execute();

      v_cmd.close(); v_cmd = NULL; v_sql = NULL;
   end;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end; /* macro m_make_event_to_process() */


//текст ошибки RS Bank
macro GetErrMessage(err_code)
  var err_msg = "";
  
  var cmd = RsdCommand(
      "select t_Contents from dbank_msg "
      "where t_Number = ? "
  );
  cmd.addParam("Number", RSDBP_IN, err_code);
  cmd.execute();

  var rs = RsdRecordset(cmd);
  if (rs.moveNext())
    err_msg = rs.value(0);
  end;

  return err_msg;
end;


// получение sql-ошибки. 
// использовать в onError(e) так:  RunError(e.message + getFullCmdErrorText(cmd));
macro getFullCmdErrorText(_cmd:RSDCommand)
   var cmd = _cmd;
   var err_str = "", i = 0;

   while( i < cmd.connection.environment.ErrorCount)
      err_str = err_str + cmd.connection.environment.Error(i).Descr + " ";
      i = i+1;
   end;

   return trim(err_str);
end;


/*-------------------------------------------------*/
/* Обновление статуса записи в utableprocessin_dbt */
/*-------------------------------------------------*/
macro m_upd_table_proc_in_status(p_recid, p_status_new)
   var v_ret = true;
   var v_sql, v_cmd;

   v_sql =         "UPDATE utableprocessin_dbt ";
   v_sql = v_sql + "   SET t_status = :p_status_new ";
   v_sql = v_sql + " WHERE t_recid = :p_recid ";

   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_status_new", RSDBP_IN, p_status_new);
   v_cmd.addParam("p_recid", RSDBP_IN, p_recid);
   v_cmd.execute();

   v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end;


/*----------------------------------------------------*/
/* Обновление статуса записи в utableprocessevent_dbt */
/*----------------------------------------------------*/
macro m_upd_table_proc_evnt_status(p_objectid, p_objecttype, p_status, p_msg_id, p_status_old)
   var v_ret = true;
   var v_obj_tp_type, v_msg_id_type, v_stat_old_type;
   var v_sql, v_cmd;

   v_obj_tp_type = ValType(p_objecttype);
   v_msg_id_type = ValType(p_msg_id);
   v_stat_old_type = ValType(p_status_old);

   v_sql =         "UPDATE utableprocessevent_dbt ";
   v_sql = v_sql + "   SET t_status = :p_status ";
   if(v_msg_id_type != V_UNDEF)
      v_sql = v_sql + "   ,t_messageid = :p_msg_id ";
   end;
   if(v_obj_tp_type != V_UNDEF)
      v_sql = v_sql + " WHERE t_objecttype = :p_objecttype ";
      v_sql = v_sql + "   AND t_objectid = :p_objectid ";
   else
      v_sql = v_sql + " WHERE t_recid = :p_recid ";
   end;
   if(v_stat_old_type != V_UNDEF)
      v_sql = v_sql + "AND t_status = :p_status_old ";
   end;

   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_status", RSDBP_IN, p_status);
   if(v_msg_id_type != V_UNDEF)
      v_cmd.addParam("p_msg_id", RSDBP_IN, p_msg_id);
   end;
   if(v_obj_tp_type != V_UNDEF)
      v_cmd.addParam("p_objecttype", RSDBP_IN, p_objecttype);
      v_cmd.addParam("p_objectid", RSDBP_IN, p_objectid);
   else
      v_cmd.addParam("p_recid", RSDBP_IN, p_objectid);
   end;
   if(v_stat_old_type != V_UNDEF)
      v_cmd.addParam("p_status_old", RSDBP_IN, p_status_old);
   end;
   v_cmd.execute();

   v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end; /* m_upd_table_proc_evnt_status() */


/*------------------------------------------------*/
/* Добавление новой строки к строковой переменной */
/*------------------------------------------------*/
macro m_add_note_to_str(p_new_note, p_src_str)
   var v_ret;

   if(strlen(p_src_str) != 0)
      v_ret = string(p_src_str, "\n", p_new_note);
   else
      v_ret = p_new_note;
   end;

   return v_ret;
end;


/*--------------------------------------*/
/* Получить параметры исходного события */
/*--------------------------------------*/
macro m_get_source_event(p_jms_msg_id)
   var v_ret = c_event_main_prm();
   var v_sql, v_cmd;

   v_ret.v_func_res = 0;
   v_ret.v_func_msg = "";

   v_sql =         "DECLARE ";

   v_sql = v_sql + "    v_ret              NUMBER(10) := 0; ";
   v_sql = v_sql + "    v_ret_txt          usr_exchng_log_dbt.t_error_msg%TYPE := 'OK'; ";
   v_sql = v_sql + "    v_aux_buf          NUMBER(10); ";
   v_sql = v_sql + "    v_jmsmessageid     dqueue_log_dbt.t_jmsmessageid%TYPE := :p_jmsmessageid; ";
   v_sql = v_sql + "    v_jmsmessageguid   dqueue_log_dbt.t_jmsmessageguid%TYPE; ";
   v_sql = v_sql + "    v_jmscorrelationid dqueue_log_dbt.t_jmscorrelationid%TYPE; ";
   v_sql = v_sql + "    v_log_link_id      dqueue_log_dbt.t_jmscorrelationid%TYPE; ";
   v_sql = v_sql + "    v_recid_log        utableprocessevent_dbt.t_recid%TYPE; ";
   v_sql = v_sql + "    v_recid            utableprocessevent_dbt.t_recid%TYPE := 0; ";
   v_sql = v_sql + "    v_objecttype       utableprocessevent_dbt.t_objecttype%TYPE := 0; ";
   v_sql = v_sql + "    v_objectid         utableprocessevent_dbt.t_objectid%TYPE := 0; ";
   v_sql = v_sql + "    v_type             utableprocessevent_dbt.t_type%TYPE := 0; ";
   v_sql = v_sql + "    v_status           utableprocessevent_dbt.t_status%TYPE := 0; ";
   v_sql = v_sql + "BEGIN ";
   v_sql = v_sql + "    BEGIN ";
   v_sql = v_sql + "        v_ret := 1; ";
   v_sql = v_sql + "        SELECT t_jmsmessageguid INTO v_jmsmessageguid ";
   v_sql = v_sql + "          FROM dqueue_log_dbt ";
   v_sql = v_sql + "         WHERE t_jmsmessageid = v_jmsmessageid; ";

   v_sql = v_sql + "        v_ret := 2; ";
   v_sql = v_sql + "        SELECT t_jmscorrelationid INTO v_jmscorrelationid ";
   v_sql = v_sql + "          FROM dqueue_log_dbt ";
   v_sql = v_sql + "         WHERE t_jmsmessageguid = v_jmsmessageguid";
   v_sql = v_sql + "           AND t_jmscorrelationid <> chr(1) ";
   v_sql = v_sql + "           AND t_qname = 'ESB_TO_SOFR'; ";

   v_sql = v_sql + "        v_ret := 3; ";
   v_sql = v_sql + "        SELECT t_jmscorrelationid INTO v_log_link_id ";
   v_sql = v_sql + "          FROM dqueue_log_dbt ";
   v_sql = v_sql + "         WHERE t_jmsmessageid = v_jmscorrelationid; ";

   v_sql = v_sql + "        v_aux_buf := INSTR(v_log_link_id, '-'); ";
   v_sql = v_sql + "        IF v_aux_buf = 0 ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            v_recid_log := TO_NUMBER(v_aux_buf); ";
   v_sql = v_sql + "        ELSE ";
   v_sql = v_sql + "            v_recid_log := TO_NUMBER(SUBSTR(v_log_link_id, 1, (v_aux_buf - 1))); ";
   v_sql = v_sql + "        END IF; ";

   v_sql = v_sql + "        v_ret := 4; ";
   v_sql = v_sql + "        SELECT t_recid, t_objecttype, t_objectid, t_type, t_status ";
   v_sql = v_sql + "          INTO v_recid, v_objecttype, v_objectid, v_type, v_status ";
   v_sql = v_sql + "          FROM utableprocessevent_dbt ";
   v_sql = v_sql + "         WHERE t_recid = v_recid_log; ";

   v_sql = v_sql + "        v_ret := 0; ";

   v_sql = v_sql + "    EXCEPTION ";
   v_sql = v_sql + "        WHEN NO_DATA_FOUND ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            v_ret := v_ret; ";
   v_sql = v_sql + "            v_ret_txt := 'NO_DATA_FOUND'; ";
   v_sql = v_sql + "        WHEN OTHERS ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            v_ret := SQLCODE; ";
   v_sql = v_sql + "            v_ret_txt := SUBSTR(SQLERRM, 1, 1500); ";
   v_sql = v_sql + "    END; ";

   v_sql = v_sql + "    :p_recid := v_recid; ";
   v_sql = v_sql + "    :p_objecttype := v_objecttype; ";
   v_sql = v_sql + "    :p_objectid := v_objectid; ";
   v_sql = v_sql + "    :p_type := v_type; ";
   v_sql = v_sql + "    :p_status := v_status; ";
   v_sql = v_sql + "    :p_ret := v_ret; ";
   v_sql = v_sql + "    :p_ret_txt := v_ret_txt; ";
   v_sql = v_sql + "END; ";

   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_jmsmessageid", RSDBP_IN, p_jms_msg_id);
   v_cmd.addParam("p_recid", RSDBP_OUT, V_INTEGER);
   v_cmd.addParam("p_objecttype", RSDBP_OUT, V_INTEGER);
   v_cmd.addParam("p_objectid", RSDBP_OUT, V_INTEGER);
   v_cmd.addParam("p_type", RSDBP_OUT, V_INTEGER);
   v_cmd.addParam("p_status", RSDBP_OUT, V_INTEGER);
   v_cmd.addParam("p_ret", RSDBP_OUT, V_INTEGER);
   v_cmd.addParam("p_ret_txt", RSDBP_OUT, V_STRING, 2000);
   v_cmd.execute();

   v_ret.v_func_res   = v_cmd.value("p_ret");
   v_ret.v_func_msg   = v_cmd.value("p_ret_txt");
   v_ret.v_recid      = v_cmd.value("p_recid");
   v_ret.v_objecttype = v_cmd.value("p_objecttype");
   v_ret.v_objectid   = v_cmd.value("p_objectid");
   v_ret.v_type       = v_cmd.value("p_type");
   v_ret.v_status     = v_cmd.value("p_status");
   v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret.v_func_res = 7; /* Иные проблемы */
   return v_ret;
end; /* m_get_source_event() */


/* Получить дату первого рабочего дня, отстающего от даты на заданное количество рабочих дней */
macro m_get_work_date_delta_before(p_set_date, p_delta)
   var v_ret;

   v_ret = p_set_date - p_delta;
   while(p_set_date < DateAfterWorkDays(v_ret, p_delta))
      v_ret = v_ret - 1;
   end;

   return v_ret;
end; /* macro m_get_work_date_delta_before() */
