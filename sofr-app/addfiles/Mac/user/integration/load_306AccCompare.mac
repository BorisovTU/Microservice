/**
 @file 		mac\user\integration\load_306AccCompare.mac
 @brief 	Загрузка файла с зачислениями, загруженных в БИСКВИТ из СОФР

 # tag
 - functional_block: Отчетность_внутренняя
 - code_type: Report
 - code_type: GUI

 #menu 
  - БО ЦБ\ Отчеты \ РСХБ \ [13408] Сверка зачислений списаний ДС СОФР_ЦФТ

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |05.11.2024 |Велигжанин А.В.|DEF-75364                                       |Load306AccForCompare(), загрузка с учетом формата файла
 |           |Шишкин Е.В.    |                                                |Создан
*/
import oralib, likepy, globals, rsexts, "dlutils.mac";

private var File_Name,
            Load_Path,
            File_Name_CtrlFile = "", // Имя управляющего файла для SQL*Loader
            Log_File_Name = "",
            Bad_File_Name = "",
            Dsc_File_Name = "";



// Определение имени файла из его ПОЛНОГО имени
private macro ExtractShortName(NameOperFile)
 var Flag = True;

 NameOperFile = Trim(NameOperFile);
 while (Flag)
   if(Index(NameOperFile,"\\") != 0)
     Flag = TRUE
   else
     Flag = FALSE
   end;
   if(Flag)
     NameOperFile = SubStr(NameOperFile,Index(NameOperFile,"\\")+1)
   end
 end;
 return NameOperFile
end;

//отключаем индексы для ускорения загрузки
private macro index_off()
  execSQL("ALTER INDEX ULOAD306ACCCOMPARE_IDX1 UNUSABLE;",null,false);
  execSQL("ALTER INDEX ULOAD306ACCFORCOMPARE_IDX2 UNUSABLE;",null,false);
end;

//включаем индексы
private macro index_on()
  execSQL("ALTER INDEX ULOAD306ACCCOMPARE_IDX1 REBUILD;",null,false);
  execSQL("ALTER INDEX ULOAD306ACCFORCOMPARE_IDX2 REBUILD;",null,false);
end;

/*удаление вспомогательных файлов*/
private macro DeleteFile
  //RemoveFile(File_Name);
  RemoveFile(Log_File_Name);
  RemoveFile(Bad_File_Name);
  RemoveFile(File_Name_CtrlFile);
end;


/* Загрузка по формату 0 (как обычно, без счетов и наименований плательщика и получателя) */
private macro Load_Format_0()
  SetOutput(File_Name_CtrlFile); 
  [LOAD DATA CHARACTERSET CL8MSWIN1251
   INFILE *
   APPEND
   INTO TABLE uload306Accforcompare_dbt 
   Fields terminated by "|" 
   (
    T_COUNTER,
    T_USERID,
    T_OPDATE DATE "DD/MM/YY",
    T_DOCDATE DATE "DD/MM/YY",
    T_ACCT_DB,
    T_ACCT_CR,
    T_CURRENCY,
    T_AMTCUR "REPLACE(:T_AMTCUR, '.', ',')",
    T_AMTRUB "REPLACE(:T_AMTRUB, '.', ',')",
    T_DETAILS,
    T_DOCNUM,
    T_ACCTRNID "CASE WHEN INSTR( :T_ACCTRNID, 'nptxop') = 1 THEN REPLACE(:T_ACCTRNID, 'nptxop', '') WHEN INSTR( :T_ACCTRNID, '') = 1 THEN REPLACE(:T_ACCTRNID, '', '0')  ELSE '-1' END",
    T_IDBISCOTTO,
    T_REQID 
   )
   ];
  SetOutput(Null, True); 
  Close(File_Name_CtrlFile);
end;

/* Загрузка по формату 1 (со счетами и наименованиями плательщика и получателя) */
private macro Load_Format_1()
  SetOutput(File_Name_CtrlFile); 
  [LOAD DATA CHARACTERSET CL8MSWIN1251
   INFILE *
   APPEND
   INTO TABLE uload306Accforcompare_dbt 
   Fields terminated by "|" 
   (
    T_COUNTER,
    T_USERID,
    T_OPDATE DATE "DD/MM/YY",
    T_DOCDATE DATE "DD/MM/YY",
    T_ACCT_DB,
    T_ACCT_CR,
    T_CURRENCY,
    T_AMTCUR "REPLACE(:T_AMTCUR, '.', ',')",
    T_AMTRUB "REPLACE(:T_AMTRUB, '.', ',')",
    T_DETAILS,
    T_DOCNUM,
    T_ACCTRNID "CASE WHEN INSTR( :T_ACCTRNID, 'nptxop') = 1 THEN REPLACE(:T_ACCTRNID, 'nptxop', '') WHEN INSTR( :T_ACCTRNID, '') = 1 THEN REPLACE(:T_ACCTRNID, '', '0')  ELSE '-1' END",
    T_IDBISCOTTO,
    T_REQID, 
    T_PAYERNAME,
    T_RECEIVERNAME, 
    T_PAYERACCOUNT,
    T_RECEIVERACCOUNT
   )
   ];
  SetOutput(Null, True); 
  Close(File_Name_CtrlFile);
end;


//Загрузка файла зачислений, загруженных из БИСКВИТ в СОФР
macro Load306AccForCompare( File_Name :string, ReqId :string, Fl_SetDialog :integer, p_Format :integer ) :bool
var DSN :string = "",
    USER_ID :string = "",
    PASSWORD :string = "",
    CONNSTRING :string = GetIniString("CONNSTRING", "rsreq.ini"),
    String_sqlldr :string = "",
    state :integer = 0,
    ldr_param :string = "bindsize=100000000 readsize=1000000 columnarrayrows=10000000 rows=100000 errors=100000 ",
    Query :string = "",
    Params :TArray = TArray();

  var _locker = UniConcurrentLocker("Load306AccForCompare", 3600, true);


  if( ( ValType(Fl_SetDialog) == V_UNDEF ) or ( ValType(Fl_SetDialog) == 26 ) )
   Fl_SetDialog = 0;
  end;

  if( ( ValType(File_Name) == V_UNDEF ) or  ( ValType(File_Name) == 26 ) or ( File_Name == "" ) or ( FindPath( ExtractShortName( File_Name ), StrSubst( File_Name, "\\" + ExtractShortName(File_Name), "" ) ) == "" ) )
   return false;
  end;

  if( ( ValType(ReqId) != V_UNDEF ) and  ( ValType(ReqId) != 26 ) and ( ReqId != "" ) )
   Query = " DELETE ULOAD306ACCFORCOMPARE_DBT WHERE T_REQID = :p_ReqId";
   Params = makeArray( SQLParam("p_ReqId", ReqId) );
   execSQL( Query, Params );
  end;
  
  DSN = SubStr(CONNSTRING, 5, index(CONNSTRING,";") - 5);
  CONNSTRING = SubStr(CONNSTRING, index(CONNSTRING,";") + 1);
  USER_ID = SubStr(CONNSTRING, index(CONNSTRING,"user id=") + 8, index(CONNSTRING,";") - 9);
  CONNSTRING = SubStr(CONNSTRING, index(CONNSTRING,";") + 1);
  PASSWORD = SubStr(CONNSTRING, index(CONNSTRING,"password=") + 9);

  String_sqlldr = "userid=" + USER_ID + "/" + PASSWORD + "@" + DSN;
  GetRegistryValue("РСХБ\\ИНТЕГРАЦИЯ\\ДИРЕКТОРИИ\\IMPORT\\306ACCCOMPAREDIR", V_STRING, File_Name_CtrlFile);

  Log_File_Name = File_Name_CtrlFile + "\\CTL_" + string(UserNumber) + ".log";
  Bad_File_Name = File_Name_CtrlFile + "\\CTL_" + string(UserNumber) + ".bad";
  Dsc_File_Name = File_Name_CtrlFile + "\\CTL_" + string(UserNumber) + ".dsc";
  File_Name_CtrlFile = File_Name_CtrlFile + "\\CTL_" + string(UserNumber) + ".ctl";

  if(p_Format == 1)
     Load_Format_1();
  else
     Load_Format_0();
  end;

  BegAction(Null, "Отключаем индескы", false);
  //если получается заблокировать монопольно, то значит других процессов нет, и тольк тогда отключаем индексы
  //иначе они уже отключены
  var idxOffLocker = UniConcurrentLocker("Load306AccForCompare");
  if (idxOffLocker.Lock())
    index_off();
  end;
  idxOffLocker = null;
  if (not _locker.Lock())
    RunError("Не удалось заблокировать процесс по идентификатору "+ _locker.GetLockId())
  end;
  EndAction(10);

  BegAction(Null, "Загружаем файл в RS-Bank...", False);
  
  state = StartProg("run_sqlldr.cmd", String_sqlldr + " control=" + File_Name_CtrlFile +
                                              " data="    + File_Name          +
                                              " log="     + Log_File_Name      +
                                              " bad="     + Bad_File_Name      +
                                              " discard=" + Dsc_File_Name      +
                                              " parallel=true " +
                                              " SKIP_UNUSABLE_INDEXES=true"    +
                                              " " + ldr_param, false);

  EndAction(10);
  BegAction(Null, "Включаем индескы", false);
  _locker = null;
  //включаем индексы, только если получается заблокировать монопольно
  var idxOnLocker = UniConcurrentLocker("Load306AccForCompare");
  if (idxOnLocker.Lock())
    index_on();
  end;
  idxOnLocker = null;
  EndAction(10);
  if (state != 0)
    RunError("Ошибка выполнения программы в StartProg. Код возврата: " + state);
  end;
  return true;
onError(err)
  AddDBLogWithErrorObj("Load306AccForCompare",err);
  return false;
end;