/*---------------------------------*/
/* Сервис проверки данных Клиента  */
/* Поток "A3"                      */
/*---------------------------------*/
/* РСХБ                            */
/* |- БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ      */
/*    |- ВЗАИМОДЕЙСТВИЕ С ДБО ФЛ   */
/*       |- ОТВЕТЫ                 */
/*          |- ТЕЛЕФОН ДЛЯ СПРАВОК */
/*---------------------------------*/

import "SendCustBrReq_lib.mac";
import "global_classes_adp.mac";
import "global_utils_intgr.mac"; /* Функции общего назначения */
import BankInter; /* GetRegistryValue() */


private const CV_ERROR_TECH = -10001;
private const CV_ERR_TECH = "Техническая проблема при выполнении запроса";


/**/
private class (c_SendCustBrResp) c_adp_SendCustBrResp(p_MessageId, p_ErrorCode, p_ErrorDesc)
   /*----------------------------------*/
   /* Метод добавления ошибки к списку */
   /*----------------------------------*/
   macro m_add_error_to_list(p_ErrorCode, p_ErrorDesc)
      var v_aux_buf;

      /* Код ошибки считаем первичным параметром */
      if(ValType(p_ErrorCode) != V_UNDEF)
         v_aux_buf = ErrorList.size;
         ErrorList.value(v_aux_buf) = c_Error;

         ErrorList.value(v_aux_buf).ErrorCode = p_ErrorCode;

         if(ValType(p_ErrorDesc) != V_UNDEF)
            ErrorList.value(v_aux_buf).ErrorDesc = p_ErrorDesc;
         else
            ErrorList.value(v_aux_buf).ErrorDesc = "";
         end;
      end;
   end; /* macro m_add_error_to_list() */

   /*-------------*/
   /* Конструктор */
   /*-------------*/
   private macro m_init_adp_SendCustBrResp(i_MessageId, i_ErrorCode, i_ErrorDesc)
      ErrorList = TArray; /* В данном случае список ошибок - обязательный параметр */
      MassageId = c_IntegrSymId();

      if(ValType(i_MessageId) != V_UNDEF)
         MassageDate = dttm(date(), time());
         MassageId.ObjectId = i_MessageId;
      end;

      m_add_error_to_list(i_ErrorCode, i_ErrorDesc);
   end; /* macro m_init_adp_SendCustBrResp() */

   Initc_SendCustBrResp();

   m_init_adp_SendCustBrResp(p_MessageId, p_ErrorCode, p_ErrorDesc);
end; /* class c_adp_SendCustBrResp */


/* Исходящий объект */
private class c_out_obj()
   var SendCustBrResp : object; /* c_adp_SendCustBrResp */
end;


/**/
private class (c_SendCustBrReq) c_adp_SendCustBrReq(p_income_data)
   private const CV_INFO_PHONE_NUM_PATH = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ВЗАИМОДЕЙСТВИЕ С ДБО ФЛ\\ОТВЕТЫ\\ТЕЛЕФОН ДЛЯ СПРАВОК";

   private const CV_MSG_TO_BO_WAY_1 = "СКП ЭП";
   private const CV_MSG_TO_BO_WAY_2 = "Двухфакторная авторизация";
   private const CV_MSG_TO_BO_WAY_3 = "QUIK";

   private const CV_ECONOMIC_SUBJ = 3;
   private const CV_BROKER_SERV_CONTR = 207;

   private const CV_TRADE_TERM_ENTRY = 1; /* Тип "запрос на вход в торговые терминалы" */
   private const CV_SET_CODE_WORD = 2; /* Тип "запрос на установку/смену Кодового слова" */

   private const CV_SUP_PHONE_NUM = "8 (800) 100-40-40"; /* Значение по умолчанию для замены %SupPhoneNum% */
   private const CV_DOC_LINK = "https://www.rshb.ru/download-file/377531/"; /* Для замены %DocLink% */

   /* Код ошибки */
   private const CV_ERROR_CLEAR = 0; /* Значение для ErrorCode, говорящее об отсутствии ошибки */
   private const CV_ERROR_SIGN = 1;  /* Значение для ErrorCode, говорящее о наличии ошибки */
   private const CV_ERROR_FATAL = 2; /* Значение для ErrorCode - продолжение процедуры не возможно */

   /* Описание ошибки */
   private const CV_ERR_CLEAR = ""; /* Текст ошибки по умолчанию */
   private const CV_ERR_DEFAULT = ""; /* Если ничего конкретного нет, то будем возвращать этот текст */
   /* Для случаев, когда надо выдать ошибку, но в ТЗ случай не заложен */
   private const CV_ERR_COMMON = "Выполнение операции временно не возможно. Повторите попытку позже";
   /* "Клиент не найден": заменить %IDCust% и %SupPhoneNum% */
   private const CV_ERR_NO_CLIENT = "Клиент с номером %IDCust% не найден, попробуйте повторить попытку позже или обратитесь в службу поддержки Банка по телефону %SupPhoneNum%";
   /* "Договор не найден": заменить %SupPhoneNum% */
   private const CV_ERR_NO_CNTR = "Не найдено открытого договора на брокерское обслуживание, попробуйте повторить попытку позже или обратитесь в службу поддержки Банка по телефону %SupPhoneNum%";
   /* Обработка способа передачи поручений на БО: заменить %DocLink% */
   private const CV_ERR_MGS_TO_BO_WAY = "При оформлении документов для заключения соглашения об оказании брокерских услуг Вами был выбран вариант аутентификации с использованием ЭП. Инструкция по использованию ИТС QUIK с использованием ЭП размещена по ссылке: %DocLink%";
   /* "Не найден номер договора" */
   private const CV_ERR_NO_CNTR_NUM = "Неверно указан номер соглашения об оказании брокерских услуг";
   /* "Не соответствует ФИО Клиента" */
   private const CV_ERR_NO_CLIENT_FIO = "Неверно указано ФИО";
   /* "Не найден номер договора и не соответствует ФИО Клиента" */
   private const CV_ERR_NO_EXT_DATA = "Неверно указан номер соглашения об оказании брокерских услуг и ФИО";
   /* "Признак выгрузки данных в QUIK установлен": заменить %SupPhoneNum% */
   private const CV_ERR_WAS_SENT_TO_QUIK = "Регистрация была пройдена ранее, попробуйте повторить попытку позже или обратитесь в службу поддержки Банка по телефону %SupPhoneNum%";
   /* "Дата открытия/изменения договора БО/ИИС меньше CV_CNTR_OPEN_CHNG_DATE" */
   private const CV_ERR_CNTR_OPEN_CHNG = "Ошибка! Необходимо актуализировать Ваши контактные данные (номер мобильного телефона и адрес эл. почты) в Заявлении о присоединении к Регламенту оказания брокерских услуг в офисе Банка. Дополнительная информация по эл. почте (invest@rshb.ru) или по телефону %SupPhoneNum%.";
   /* Дополнительная проверка способа передачи поручений на БО для Клиентов без UID */
   private const CV_ERR_MSG_TO_BO_AFTER = "При оформлении документов для заключения соглашения об оказании брокерских услуг Вами был выбран вариант аутентификации с использованием ЭП. Инструкция по использованию ИТС QUIK с использованием ЭП размещена по ссылке: https://www.rshb.ru/download-file/377531/";
   /* Регистрация была выполнена ранее */
   private const CV_ERR_REGISTRED_ALREADY = "Регистрация в торговых терминалах была выполнена ранее";

   private const CV_TEXT_TO_CHNG_PHONE = "%SupPhoneNum%";
   private const CV_TEXT_TO_CHNG_CLNT_ID = "%IDCust%";
   private const CV_TEXT_TO_CHNG_DOC_LNK = "%DocLink%";

   private const CV_CNTR_OPEN_CHNG_DATE = date("24.10.2019");

   private var v_error_code;
   private var v_error_desc;

   private var v_sup_phone_num; /* Телефон Банка для справок по Брокерскому обслуживанию (для замены %SupPhoneNum%) */

   private var v_client_partyid; /* Внутренний id клиента в RS-Bank */
   private var v_client_uid; /* UID Клиента */
   private var v_client_fio; /* ФИО клиента */
   private var v_client_open_cntr_list; /* Список номеров открытых договоров */
   private var v_client_open_cntr_id_list; /* Список ID открытых договоров */
   private var v_client_msg_to_bo_way; /* Значение параметра "Способ передачи поручений на БО" */
   private var v_client_was_sent_to_quik; /* Значение параметра "Признак выгрузки данных в QUIK" (1 - "Успешно", 2 - "Ошибка") */

   private var v_cntr_date_chng; /* Дата последнего изменения договора */
   private var v_cntr_time_chng; /* Время последнего изменения договора */

   private var v_clnt_auth_date; /* Дата последней установки типа аутентификации */
   private var v_clnt_auth_time; /* Время последней установки типа аутентификации */

   private var v_clnt_quik_date; /* Дата последней выгрузки данных Клиента в QUIK */
   private var v_clnt_quik_time; /* Время последней выгрузки данных Клиента в QUIK */


   macro m_get_error_code()
      return v_error_code;
   end;

   macro m_get_error_desc()
      return v_error_desc;
   end;

   /*------------------------------------------*/
   /* Проверка существования Клиента в БД СОФР */
   /*------------------------------------------*/
   macro m_check_client()
      var v_ret = true;
      var v_sql, v_cmd, v_rs;

      v_sql =         "SELECT p_info.t_personid, ";
      v_sql = v_sql + "       TRIM(REPLACE(TRIM((p_info.t_name1) || ' ' || TRIM(p_info.t_name2) || ' ' || TRIM(p_info.t_name3)), CHR(1))) v_client_fio ";
      v_sql = v_sql + "  FROM dpersn_dbt p_info ";
      v_sql = v_sql + " WHERE p_info.t_personid = :p_personid ";
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_personid", RSDBP_IN, ClientLogin);
      v_rs = RSDRecordSet(v_cmd);

      if(v_rs.movenext())
         v_client_partyid = v_rs.value("t_personid");
         /* Сразу извлекаем ФИО */
         v_client_fio = v_rs.value("v_client_fio");
      else
         /* Клиент не найден */
         v_error_code = CV_ERROR_FATAL;
         v_error_desc = strsubst(strsubst(CV_ERR_NO_CLIENT, CV_TEXT_TO_CHNG_CLNT_ID, ClientLogin), CV_TEXT_TO_CHNG_PHONE, v_sup_phone_num);
         v_ret = false;
      end;

      v_rs.close(); v_cmd.close(); v_rs = NULL; v_cmd = NULL; v_sql = NULL;

      return v_ret;

   onError(err)
      /* Считаем, что это эквивалентно случаю "Клиент не найден" */
      v_error_code = CV_ERROR_FATAL;
      v_error_desc = strsubst(strsubst(CV_ERR_NO_CLIENT, CV_TEXT_TO_CHNG_CLNT_ID, ClientLogin), CV_TEXT_TO_CHNG_PHONE, v_sup_phone_num);
      v_ret = false;
      return v_ret;
   end; /* macro m_check_client() */


   /*----------------------------------*/
   /* Проверка открытых договоров (БО) */
   /*----------------------------------*/
   macro m_check_open_cntr()
      var v_ret = true;
      var v_sql, v_cmd, v_rs;
      var v_date_chng;

      v_sql =         "   SELECT sf_cntr.t_partyid, sf_cntr.t_number, sf_cntr.t_datebegin, ";
      v_sql = v_sql + "          sf_cntr.t_dateclose, dl_cntr.t_dlcontrid, dl_cntr.t_sfcontrid ";
      v_sql = v_sql + "     FROM dsfcontr_dbt sf_cntr ";
      v_sql = v_sql + "LEFT JOIN ddlcontr_dbt dl_cntr ON dl_cntr.t_sfcontrid = sf_cntr.t_id ";
      v_sql = v_sql + "    WHERE sf_cntr.t_partyid = :p_client_id ";
      v_sql = v_sql + "      AND (   sf_cntr.t_dateclose = TO_DATE('01.01.0001', 'dd.mm.yyyy') ";
      v_sql = v_sql + "           OR sf_cntr.t_dateclose > :p_req_date) ";
      v_sql = v_sql + "      AND dl_cntr.t_dlcontrid IS NOT NULL ";
      v_sql = v_sql + " ORDER BY sf_cntr.t_id DESC ";

      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_client_id", RSDBP_IN, v_client_partyid);
      v_cmd.addParam("p_req_date", RSDBP_IN, MassageDate);
      v_rs = RSDRecordSet(v_cmd);

      while(v_rs.movenext())
         v_client_open_cntr_list.value(v_client_open_cntr_list.size) = v_rs.value("t_number");
         v_client_open_cntr_id_list.value(v_client_open_cntr_id_list.size) = v_rs.value("t_dlcontrid");

         v_date_chng = readnoteforobject(CV_BROKER_SERV_CONTR,
                                         string(v_rs.value("t_dlcontrid"):o:34),
                                         CV_CONTRACT_NOTEKIND_UPDATE,
                                         date());

         /* Определяем максимальную дату открытия договора */
         if(ValType(v_cntr_date_chng) == V_UNDEF)
            v_cntr_date_chng = v_rs.value("t_datebegin");
         else
            if(v_cntr_date_chng < v_rs.value("t_datebegin"))
               v_cntr_date_chng = v_rs.value("t_datebegin");
            end;
         end;

         if((ValType(v_date_chng) != V_UNDEF) and (ValType(v_date_chng) != 26))
            if(v_cntr_date_chng < v_date_chng)
               v_cntr_date_chng = v_date_chng;
            end;
         end;
      end;

      v_cntr_time_chng = time("00:00:00"); /* Значение времени открытия договора не хранится в БД */

      if(v_client_open_cntr_list.size == 0)
         /* Договора не найдены */
         v_error_code = CV_ERROR_FATAL;
         v_error_desc = strsubst(CV_ERR_NO_CNTR, CV_TEXT_TO_CHNG_PHONE, v_sup_phone_num);
         v_ret = false;
      end;

      v_rs.close(); v_cmd.close(); v_rs = NULL; v_cmd = NULL; v_sql = NULL;

      return v_ret;

   onError(err)
      /* Считаем, что это эквивалентно случаю "Договор не найден" */
      v_error_code = CV_ERROR_FATAL;
      v_error_desc = strsubst(CV_ERR_NO_CNTR, CV_TEXT_TO_CHNG_PHONE, v_sup_phone_num);
      v_ret = false;
      return v_ret;
   end; /* macro m_check_open_cntr() */


   /*-------------------------------------------*/
   /* Проверка способа передачи поручений на БО */
   /*-------------------------------------------*/
   macro m_check_msg_to_bo_way()
      const CV_CATEG_TYPE_NAME = 2;
      const CV_MSG_TO_BO_WAY_CATEG = 170;

      var v_ret = true;

      /* Получение значения параметра "Способ передачи поручений на БО" (получаем name по dllcontr_dbt.t_dlcontrid) */
      v_client_msg_to_bo_way = m_get_obj_categ_value(CV_BROKER_SERV_CONTR,
                                                     v_client_open_cntr_id_list.value(0), /* Считается, что для всех договоров значение категории одинаково */
                                                     CV_MSG_TO_BO_WAY_CATEG,
                                                     CV_CATEG_TYPE_NAME,
                                                     v_clnt_auth_date,
                                                     v_clnt_auth_time);

      /* Согласно ТЗ для любого другого значения обработка идентичная - считается, что проверка пройдена */
      if(ValType(v_client_msg_to_bo_way) == V_UNDEF)
         v_clnt_auth_date = NULL;
         v_clnt_auth_time = NULL;
         /* Если значение категории не найдено на договоре, то считаем, что произошла ошибка (описания не указываем) */
         v_error_code = CV_ERROR_SIGN;
         v_error_desc = CV_ERR_COMMON;
         v_ret = false;

      elif(v_client_msg_to_bo_way == CV_MSG_TO_BO_WAY_1) /* Соответствие условию приводит к исключительной ситуации */
            v_error_code = CV_ERROR_FATAL;
            v_error_desc = strsubst(CV_ERR_MGS_TO_BO_WAY, CV_TEXT_TO_CHNG_DOC_LNK, CV_DOC_LINK);
            v_ret = false;

      elif(v_client_msg_to_bo_way == CV_MSG_TO_BO_WAY_3) /* По информации из письма 24.10.2019 18:11 */
         v_error_code = CV_ERROR_FATAL;
         v_error_desc = strsubst(CV_ERR_MGS_TO_BO_WAY, CV_TEXT_TO_CHNG_DOC_LNK, CV_DOC_LINK);
         v_ret = false;
      end;

      return v_ret;

   onError(err)
      /* Если что-то пошло не так, то описание ошибки не указываем */
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = string("(", err.Line, ") ", CV_ERR_COMMON);
      v_ret = false;
      return v_ret;
   end; /* macro m_check_msg_to_bo_way() */


   /*----------------------------------------------------------------*/
   /* Проверка способа передачи поручений на БО (если не найден UID) */
   /*----------------------------------------------------------------*/
   macro m_after_check_msg_to_bo_way()
      var v_ret = true;

      if(v_client_msg_to_bo_way == CV_MSG_TO_BO_WAY_1)
         v_error_code = CV_ERROR_SIGN;
         v_error_desc = CV_ERR_MSG_TO_BO_AFTER;
         v_ret = false;

      elif(v_client_msg_to_bo_way != CV_MSG_TO_BO_WAY_2)
         /* Ситуация не предусмотрена в ТЗ */
         RunError(CV_ERR_TECH, CV_ERR_COMMON);
      end;

      return v_ret;

   onError(err)
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = string("(", err.Line, ") ", CV_ERR_COMMON);
      v_ret = false;
      return v_ret;
   end; /* macro m_after_check_msg_to_bo_way() */


   /*----------------------------------------*/
   /* Проверка дополнительных данных Клиента */
   /*----------------------------------------*/
   macro m_check_client_ext_data()
      var v_ret = false;
      var v_aux_cntr;

      /* Данные для проверки уже должны быть собраны на предыдущих шагах */
      /* Номер договора */
      v_aux_cntr = 0;
      while((not v_ret) and (v_aux_cntr < v_client_open_cntr_list.size))
         if(ContractNum == v_client_open_cntr_list.value(v_aux_cntr))
            v_ret = true;
         end;

         v_aux_cntr = v_aux_cntr + 1;
      end;

      /* ФИО Клиента */
      if(NameCust == v_client_fio)
         if(not v_ret) /* Не найден ТОЛЬКО номер договора */
            v_error_code = CV_ERROR_SIGN;
            v_error_desc = CV_ERR_NO_CNTR_NUM;
            v_ret = false;
         end;
      else
         if(not v_ret) /* Не найден номер договора и не совпадает ФИО Клиента */
            v_error_code = CV_ERROR_SIGN;
            v_error_desc = CV_ERR_NO_EXT_DATA;
            v_ret = false;
         else /* Не совпадает ТОЛЬКО ФИО Клиента */
            v_error_code = CV_ERROR_SIGN;
            v_error_desc = CV_ERR_NO_CLIENT_FIO;
            v_ret = false;
         end;
      end;

      return v_ret;

   onError(err)
      v_ret = false;
      return v_ret;
   end; /* macro m_check_client_ext_data() */


   /*------------------------------------------*/
   /* Проверка признака выгрузки данных в QUIK */
   /*------------------------------------------*/
   macro m_get_info_sent_to_quik()
      const CV_SEND_TO_QUIK_YES = 1;
      const CV_SEND_TO_QUIK_NO = 2;
      const CV_WAS_SENT_TO_QUIK_CATEG = 171;

      var v_ret = true;

      /* Получение значения признака выгрузки данных в QUIK (получаем numinlist) */
      v_client_was_sent_to_quik = m_get_obj_categ_value(CV_ECONOMIC_SUBJ,
                                                        v_client_partyid,
                                                        CV_WAS_SENT_TO_QUIK_CATEG,
                                                        NULL,
                                                        v_clnt_quik_date,
                                                        v_clnt_quik_time);

      /* Если признак выгрузки данных в QUIK проставлен на Клиенте */
      if(ValType(v_client_was_sent_to_quik) == V_UNDEF)
         v_client_was_sent_to_quik = CV_SEND_TO_QUIK_NO;
         /* Возвращаются нулевые значения */
         v_clnt_quik_date = date("01.01.0001"); // shev isup 508318
         //v_clnt_quik_time = NULL;
      end;

      return v_ret;

   onError(err)
      /* Если что-то пошло не так, то описание ошибки не указываем */
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = string("(", err.Line, ") ", CV_ERR_COMMON);
      v_ret = false;
      return v_ret;
   end; /* macro m_check_was_sent_to_quik() */


   /*--------------------------------*/
   /* Проверка наличия UID у Клиента */
   /*--------------------------------*/
   macro m_check_client_uid()
      const CV_CODE_KIND_UID = 105;
      const CV_STATE_ACTIVE = 0;

      var v_ret = false;
      var v_sql, v_cmd, v_rs;

      v_sql =         "SELECT t_objecttype, t_codekind, t_objectid, t_state, t_code ";
      v_sql = v_sql + "  FROM dobjcode_dbt ";
      v_sql = v_sql + " WHERE t_objecttype = :p_objecttype ";
      v_sql = v_sql + "   AND t_codekind = :p_codekind ";
      v_sql = v_sql + "   AND t_objectid = :p_objectid ";
      v_sql = v_sql + "   AND t_state = :p_state ";
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_objecttype", RSDBP_IN, CV_ECONOMIC_SUBJ);
      v_cmd.addParam("p_codekind", RSDBP_IN, CV_CODE_KIND_UID);
      v_cmd.addParam("p_objectid", RSDBP_IN, v_client_partyid);
      v_cmd.addParam("p_state", RSDBP_IN, CV_STATE_ACTIVE);
      v_rs = RSDRecordSet(v_cmd);

      if(v_rs.movenext())
         v_client_uid = v_rs.value("t_code");
         v_ret = true;
      end;

      v_rs.close(); v_cmd.close(); v_rs = null; v_cmd = null; v_sql = null;

      return v_ret;

   onError(err)
      /* Если что-то пошло не так, то описание ошибки не указываем */
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = string("(", err.Line, ") ", CV_ERR_COMMON);
      v_ret = false;
      return v_ret;
   end; /* macro m_check_client_uid() */


   /*------------------------------------------------*/
   /* Проверка даты и времени выгрузки данных в quik */
   /*------------------------------------------------*/
   macro m_check_quik_upload_dttm()
      var v_ret = true;

      if((ValType(v_clnt_quik_date) == V_UNDEF) or (v_clnt_quik_date==date("01.01.0001"))) //shev isup 508318
         ; /* Проверка пройдена */
      elif(v_cntr_date_chng < v_clnt_quik_date)
         ; /* Проверка пройдена */
      elif(    (v_cntr_date_chng == v_clnt_quik_date)
           and (v_cntr_time_chng <= v_clnt_quik_time))
      else
         v_ret = false; /* Проверка не пройдена */
      end;

   return v_ret;

   onError(err)
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = string("(", err.Line, ") ", CV_ERR_COMMON);
      v_ret = false;
      return v_ret;
   end; /* macro m_check_quik_upload_dttm() */


   /*------------------------------------------------------------------------------------------------------*/
   /* Проверка даты/времени установки "Двухфакторной аутентификации" и даты/времени отправки данных в QUIK */
   /*------------------------------------------------------------------------------------------------------*/
   macro m_check_auth_first()
      var v_ret = true;

      if(ValType(v_clnt_quik_date) != V_UNDEF ) /* Если значения нет, то проверка также пройдена */
         if(v_clnt_auth_date > v_clnt_quik_date)
            ; /* Проверка пройдена */
         elif(    (v_clnt_auth_date == v_clnt_quik_date)
              and (v_clnt_auth_time > v_clnt_quik_time))
            ; /* Проверка пройдена */
         else
            v_error_code = CV_ERROR_SIGN;
            v_error_desc = CV_ERR_REGISTRED_ALREADY;
            v_ret = false;
         end;
      end;

      return v_ret;

   onError(err)
      v_ret = false;
      return v_ret;
   end; /* macro m_check_auth_first() */


   /*-------------------------------------------------------------------------------------------------------------*/
   /* Проверка даты/времени установки "Двухфакторной аутентификации" и даты открытия/внесения измениний в договор */
   /*-------------------------------------------------------------------------------------------------------------*/
   macro m_check_auth_sec()
      var v_ret = true;


      if(v_clnt_auth_date > v_cntr_date_chng)
         ; /* Проверка пройдена */
      elif(    (v_clnt_auth_date == v_cntr_date_chng)
           and (v_clnt_auth_time >= v_cntr_time_chng)) /* Условие по ТЗ */
         ; /* Проверка пройдена */
      else
         v_error_code = CV_ERROR_SIGN;
         v_error_desc = CV_ERR_REGISTRED_ALREADY;
         v_ret = false;
      end;

      return v_ret;

   onError(err)
      v_ret = false;
      return v_ret;
   end; /* macro m_check_auth_sec() */


   /*-------------------------------------------------------------*/
   /* Проверка даты открытия/внесения изменений в договора БО/ИИС */
   /*-------------------------------------------------------------*/
   macro m_cntr_open_chng_check()
      var v_ret = false;

      /* Максимальная дата открытия/внесения изменений в договора */
      if(v_cntr_date_chng < v_clnt_auth_date)
         v_cntr_date_chng = v_clnt_auth_date;

      elif(    (v_cntr_date_chng == v_clnt_auth_date)
           and (v_cntr_time_chng < v_clnt_auth_time))

         v_cntr_time_chng = v_clnt_auth_time;
      end;

      if(v_cntr_date_chng < v_clnt_quik_date)
         v_cntr_date_chng = v_clnt_quik_date;

      elif(    (v_cntr_date_chng == v_clnt_quik_date)
           and (v_cntr_time_chng < v_clnt_quik_time))

         v_cntr_time_chng = v_clnt_quik_time;
      end;

      /* Основная проверка */
      if(v_cntr_date_chng >= CV_CNTR_OPEN_CHNG_DATE)
         v_ret = true;
      else
         v_error_code = CV_ERROR_SIGN;
         v_error_desc = strsubst(CV_ERR_CNTR_OPEN_CHNG, CV_TEXT_TO_CHNG_PHONE, v_sup_phone_num);
         v_ret = false;
      end;

      return v_ret;

   onError(err)
      /* Если что-то пошло не так, то описание ошибки не указываем */
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = string("(", err.Line, ") ", CV_ERR_COMMON);
      v_ret = false;
      return v_ret;
   end; /* macro m_cntr_open_chng_check() */


   /*----------------------------------------*/
   /* Требуется ли сохранение кодового слова */
   /*----------------------------------------*/
   macro m_is_set_code_word()
      var v_ret = false;

      if(RequestType.RecordCode == CV_SET_CODE_WORD)
         v_ret = true;
      end;

      return v_ret;
   end;


   /*---------------------------*/
   /* Сохранение кодового слова */
   /*---------------------------*/
   macro m_save_code_word()
      const CV_CODE_WRD_TYPE = 2; /* Кодовое слово для Брокерского обслуживания */
      const CV_CODE_WRD_STAT = 1; /* Запись вставляется со статусом "Поступило из неавторизованной зоны ДБО ФЛ (новое)" */

      var v_ret = true;
      var v_sql, v_cmd;

      v_sql =         "BEGIN ";
      v_sql = v_sql + "    :p_result := USR_PKG_IMPORT_SOFR.f_save_code_word_buf(:p_cw_client, ";
      v_sql = v_sql + "                                                          :p_code_word, ";
      v_sql = v_sql + "                                                          :p_code_type, ";
      v_sql = v_sql + "                                                          :p_code_stat, ";
      v_sql = v_sql + "                                                          :p_ret_txt); ";
      v_sql = v_sql + "END; ";
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_result", RSDBP_RETVAL, V_INTEGER);
      v_cmd.addParam("p_cw_client", RSDBP_IN, v_client_partyid);
      v_cmd.addParam("p_code_word", RSDBP_IN, CodeWord);
      v_cmd.addParam("p_code_type", RSDBP_IN, CV_CODE_WRD_TYPE);
      v_cmd.addParam("p_code_stat", RSDBP_IN, CV_CODE_WRD_STAT);
      v_cmd.addParam("p_ret_txt", RSDBP_OUT, V_STRING, 2000);
      v_cmd.execute();

      if(v_cmd.value("p_result") != 0)
         /* Если что-то пошло не так, то описание ошибки не указываем */
         v_error_code = CV_ERROR_SIGN;
         v_error_desc = CV_ERR_COMMON;
         v_ret = false;
      end;

      v_cmd.close(); v_cmd = NULL; v_sql = NULL;

      return v_ret;

   onError(err)
      /* Если что-то пошло не так, то описание ошибки не указываем */
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = string("(", err.Line, ") ", CV_ERR_COMMON);
      v_ret = false;
      return v_ret;
   end; /* macro m_save_code_word() */


   /*-------------*/
   /* Конструктор */
   /*-------------*/
   private macro m_init_adp_SendCustBrReq(i_income_data)
      var v_aux_buf;
      var v_stat;
      v_client_open_cntr_list = TArray;
      v_client_open_cntr_id_list = TArray;
      v_error_code = CV_ERROR_CLEAR;
      v_error_desc = CV_ERR_CLEAR;
      v_cntr_date_chng = NULL;
      v_cntr_time_chng = NULL;

      GetRegistryValue(CV_INFO_PHONE_NUM_PATH, V_INTEGER, v_sup_phone_num, v_stat);
      if(v_stat > 0)
         /* Значение по умолчанию */
         v_sup_phone_num = CV_SUP_PHONE_NUM;
      end;

      if(ValType(i_income_data) != V_UNDEF)
         MassageDate = uTryToGetPropS(i_income_data, "MassageDate");
         if(ValType(MassageDate) == V_UNDEF)
            RunError(CV_ERR_TECH, CV_ERROR_TECH);
         end;

         NameCust = uTryToGetPropS(i_income_data, "NameCust");
         if(ValType(NameCust) == V_UNDEF)
            RunError(CV_ERR_TECH, CV_ERROR_TECH);
         else
            NameCust = trim(NameCust);
         end;

         ContractNum = uTryToGetPropS(i_income_data, "ContractNum");
         if(ValType(ContractNum) == V_UNDEF)
            RunError(CV_ERR_TECH, CV_ERROR_TECH);
         end;

         ClientLogin = uTryToGetPropS(i_income_data, "ClientLogin");
         if(ValType(ClientLogin) == V_UNDEF)
            RunError(CV_ERR_TECH, CV_ERROR_TECH);
         end;

         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "MassageId"));
         if(v_aux_buf == V_GENOBJ)
            MassageId = c_adp_IntegrSymId(i_income_data.MassageId);
         else
            RunError(CV_ERR_TECH, CV_ERROR_TECH);
         end;

         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "RequestType"));
         if(v_aux_buf == V_GENOBJ)
            RequestType = c_adp_IntegrDictRec(i_income_data.RequestType);
         else
            RunError(CV_ERR_TECH, CV_ERROR_TECH);
         end;

         CodeWord = uTryToGetPropS(i_income_data, "CodeWord");
         if(RequestType.RecordCode == CV_SET_CODE_WORD)
            /* Кодовое слово должно быть указано для операции установки/смены Кодового слова */
            if(ValType(CodeWord) == V_UNDEF)
               RunError(CV_ERR_TECH, CV_ERROR_TECH);
            end;
         else
            if(RequestType.RecordCode != CV_TRADE_TERM_ENTRY)
               RunError(CV_ERR_TECH, CV_ERROR_TECH); /* Неизвестное значение типа операции */
            end;
         end;
      end;

   onError(err);
      /* Что делать, если параметров не хватает в ТЗ не описано */
      /* В ТЗ есть указание, что состав параметров проверяется ДБО ФЛ */
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = string("(", err.Line, ") ", CV_ERR_COMMON);
   end; /* macro m_init_adp_SendCustBrReq() */

   Initc_SendCustBrReq();

   m_init_adp_SendCustBrReq(p_income_data);
end; /* class c_adp_SendCustBrReq() */


/*----------------------------------------------*/
/* Точка входа в Сервис проверки данных Клиента */
/*----------------------------------------------*/
macro SendCustBrReq(p_income_data)
   const CV_ERROR_SIGN = 1; /* Значение для ErrorCode, говорящее о наличии ошибки */
   const CV_ERR_DEFAULT = ""; /* Если ничего конкретного нет, то будем возвращать этот текст */
   private const CV_ERR_COMMON = "Выполнение операции временно не возможно. Повторите попытку позже";

   var gv_message_id;
   var v_in_data_proc;
   var v_result_obj = c_out_obj();
   var v_check_stat = true; /* Флаг состояния процесса */
   var v_code_word_proc;

   /* На всякий случай сохраняем для формирования ответа */
   if(ValType(uTryToGetPropS(p_income_data, "MassageId")) == V_GENOBJ)
      gv_message_id = uTryToGetPropS(p_income_data.MassageId, "ObjectId");
   else
      RunError(CV_ERR_TECH, CV_ERROR_TECH);
   end;

   /* Проверка входящего объекта */
   v_in_data_proc = c_adp_SendCustBrReq(p_income_data);
   if(v_in_data_proc.m_get_error_code() != 0)
      v_check_stat = false;
   end;

   /* Поиск Клиента */
   if(v_check_stat)
      v_check_stat = v_in_data_proc.m_check_client();
   end;

   /* Поиск открытых договоров (БО) Клиента */
   if(v_check_stat)
      v_check_stat = v_in_data_proc.m_check_open_cntr();
   end;

   v_code_word_proc = v_in_data_proc.m_is_set_code_word();

   /* Проверка способа передачи поручений на БО */
   /* При работе с кодовым словом проверка не выполняется (письмо) */
   if(v_check_stat and (not v_code_word_proc))
      v_check_stat = v_in_data_proc.m_check_msg_to_bo_way();
   end;

   /* Проверка дополнительных данных клиента */
   if(v_check_stat)
      v_check_stat = v_in_data_proc.m_check_client_ext_data();
   end;

   /* Получение данных по выгрузке клиента в КВИК */
   if (v_check_stat)
      v_check_stat = v_in_data_proc.m_get_info_sent_to_quik();
   end;

   /* Считается, что если до этих проверок дошло, то тип аутентификации = "Двухфакторная аутентификация" */
   if(v_check_stat and (not v_code_word_proc))
      /* Проверка даты открытия/внесения изменений в договор БО/ИИС */
      v_check_stat = v_in_data_proc.m_cntr_open_chng_check();
   end;

   /* Сохранение кодового слова */
   if(v_code_word_proc)
      if(v_check_stat)
         v_check_stat = v_in_data_proc.m_save_code_word();
      end;
   end;

   /* Формируем исходящий объект */
   v_result_obj.SendCustBrResp = c_adp_SendCustBrResp(gv_message_id,
                                                      v_in_data_proc.m_get_error_code(),
                                                      v_in_data_proc.m_get_error_desc());

   return v_result_obj;

onError(err)
   v_result_obj.SendCustBrResp = NULL;
//   v_result_obj.SendCustBrResp = c_adp_SendCustBrResp(gv_message_id, 1, c_usr_err_obj.obtain_err_msg(err));
   /* Есть подозрение, что текст будет транслироваться Клиенту как есть */
   v_result_obj.SendCustBrResp = c_adp_SendCustBrResp(gv_message_id,
                                                      CV_ERROR_SIGN,
                                                      string("(", err.Line, ") ", CV_ERR_COMMON));

   return v_result_obj;
end; /* macro SendCustBrReq() */

