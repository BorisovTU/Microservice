//import email_notifyfun; /* Функционал работы с e-mail уведомлениями */
//import "email_notifyfun.mac"; /* Дистрибутивный макрос с пользовательскими расширениями */
/*-------------------------------------------------------------------------------------*/
/*                                !!! - ВНИМАНИЕ - !!!                                 */
/* Функционал отправки сообщений временно разделен:                                    */
/* Сообщения e-mail могут находиться в таблицах demail_notify_dbt и demail_notify2_dbt */
/*-------------------------------------------------------------------------------------*/
//import "usr_email_notifyfun.mac"; /* Пользовательская редакция макроса */
import "email_notifyfun.mac"; /* Пользовательская редакция макроса */
import "global_utils_intgr.mac"; /* Функционал обработки ошибок */

/** 


# changeLog 
 |date       |author         |tasks            |note                            
 |-----------|---------------|-----------------|--------------------------------
 |19.03.2024 |Шестаков Д.В.  | DEF-53800       | Добавление методов для получения списков адресов
 
*/ 


/*-------------------------------------------------------*/
/* Обертка для функционала работы с e-mail уведомлениями */
/*-------------------------------------------------------*/
class c_email_proc_env()
   private const CV_EMAIL_GROUP_DICT = 5009;
   private const CV_STAT_ON = 1;
   private const CV_SYNCH_EMAIL_MARK = "R";

   private var vg_is_error;
   private var vg_service_msg;
   /* Чтобы лишний раз не вычислять длину строки */
   private var vg_is_msg_text; /* Признак не пустого текста */

   private var vg_msg_head;       /* Заголовок письма */
   private var vg_msg_text;       /* Текст письма */
   private var vg_email_list;     /* Список адресов получателей */
   private var vg_email_cc_list;  /* Список адресов получателей копии */
   private var vg_email_bcc_list; /* Список адресов получателей скрытой копии */
   private var vg_attach_list;    /* Список вложений */

   private var vg_saved_id_list;  /* Список сохраненных писем (id) */

   /* Метод получения текста сервисного сообщения */
   macro get_service_msg()
      return vg_service_msg;
   end;

   macro m_get_error_status()
      return vg_is_error;
   end;

   /* Метод получения списка сохраненных писем (id) */
   macro m_get_saved_msg_list()
      return vg_saved_id_list;
   end;
   
   
/* Метод получения списка адресов получателей */
   macro m_get_email_list()
      return vg_email_list;
   end;
   
   
    /* Метод получения списка адресов получателей копии  */
   macro m_get_email_cc_list()
      return vg_email_cc_list;
   end;
   
   
    /* Метод получения списка адресов получателей скрытой копии */
   macro m_get_email_bcc_list()
      return vg_email_bcc_list;
   end;


   /*-------------------------*/
   /* Задать заголовок письма */
   /*-------------------------*/
   macro m_set_msg_head(p_msg_head)
      vg_msg_head = p_msg_head;
   end;


   /*---------------------------*/
   /* Получить заголовок письма */
   /*---------------------------*/
   macro m_get_msg_head()
     return vg_msg_head;
   end;


   /*-----------------------*/
   /* Очистить текст письма */
   /*-----------------------*/
   macro m_clear_msg_text()
      vg_msg_text = "";
      vg_is_msg_text = false;
   end;


   /*--------------------------*/
   /* Очистить список вложений */
   /*--------------------------*/
   macro m_clear_attach_list()
      vg_attach_list.size = 0;
   end;


   /*---------------------------*/
   /* Очистить список адресатов */
   /*---------------------------*/
   macro m_clear_email_list()
      vg_email_list.size = 0;
   end;

   /*---------------------------------*/
   /* Очистить список адресатов копии */
   /*---------------------------------*/
   macro m_clear_email_cc_list()
      vg_email_cc_list.size = 0;
   end;

   /*-----------------------------------------*/
   /* Очистить список адресатов скрытой копии */
   /*-----------------------------------------*/
   macro m_clear_email_bcc_list()
      vg_email_bcc_list.size = 0;
   end;

   /*-----------------------------------*/
   /* Очистить список сохраненных писем */
   /*-----------------------------------*/
   macro m_clear_saved_id_list()
      if(ValType(vg_saved_id_list) != V_UNDEF)
         vg_saved_id_list.size = 0;
      end;
   end;


   /* Добавить id к списку сохраненных писем */
   macro m_add_saved_id_to_list(p_saved_id)
      if(ValType(p_saved_id) != V_UNDEF)
         if(ValType(vg_saved_id_list) == V_UNDEF)
            vg_saved_id_list = TArray;
         end;
         vg_saved_id_list.value(vg_saved_id_list.size) = p_saved_id;
      end;
   end;


   /*---------------------------------------*/
   /* Добавить новую строку к тексту письма */
   /*---------------------------------------*/
   macro m_add_row_to_msg_text(p_add_text)
      if(strlen(p_add_text) > 0)
         if(vg_is_msg_text)
            vg_msg_text = string(vg_msg_text, "\n", p_add_text);
         else
            vg_msg_text = p_add_text;
            vg_is_msg_text = true;
         end;
      end;
   end;


   /*--------------------------------------------------*/
   /* Добавить новый адрес к списку получателей письма */
   /*--------------------------------------------------*/
   macro m_add_email_to_list(p_add_email)
      if(strlen(p_add_email) > 0)
         vg_email_list(vg_email_list.size) = p_add_email;
      end;
   end;


   /*--------------------------------------------------------*/
   /* Добавить новый адрес к списку получателей копии письма */
   /*--------------------------------------------------------*/
   macro m_add_email_to_cc_list(p_add_email)
      if(strlen(p_add_email) > 0)
         vg_email_cc_list(vg_email_cc_list.size) = p_add_email;
      end;
   end;


   /*----------------------------------------------------------------*/
   /* Добавить новый адрес к списку получателей скрытой копии письма */
   /*----------------------------------------------------------------*/
   macro m_add_email_to_bcc_list(p_add_email)
      if(strlen(p_add_email) > 0)
         vg_email_bcc_list(vg_email_bcc_list.size) = p_add_email;
      end;
   end;


   /*-----------------------------------------------------------*/
   /* Добавить путь к файлу, который нужно приаттачить к письму */
   /*-----------------------------------------------------------*/
   macro m_add_attach_to_list(p_add_attach)
      if(strlen(p_add_attach) > 0)
         vg_attach_list(vg_attach_list.size) = p_add_attach;
      end;
   end;
   
   /*--------------------------------------------------*/
   /* Добавить брокерский почтовый адрес к списку получателей письма */
   /*--------------------------------------------------*/
   macro m_add_broker_email_to_list()
      vg_email_list(vg_email_list.size) = GetMainRSHBBrokerAddress();
   end;

   /*--------------------------------------------------------*/
   /* Добавить новый адрес к списку получателей копии письма */
   /*--------------------------------------------------------*/
   macro m_add_broker_email_to_cc_list()
      vg_email_cc_list(vg_email_cc_list.size) = GetMainRSHBBrokerAddress();
   end;

   /*----------------------------------------------------------------*/
   /* Добавить брокерский почтовый адрес к списку получателей скрытой копии письма */
   /*----------------------------------------------------------------*/
   macro m_add_broker_email_to_bcc_list()
      vg_email_bcc_list(vg_email_bcc_list.size) = GetMainRSHBBrokerAddress();
   end;



   /*-----------------*/
   /* Группа рассылки */
   /*-----------------*/
   macro m_get_email_group(p_email_grp)
      var v_ret = true;
      var v_sql, v_cmd, v_rs;

      v_sql =         "SELECT tbl_addr.t_group, tbl_addr.t_email, tbl_addr.t_place ";
      v_sql = v_sql + "  FROM usr_email_addr_dbt tbl_addr ";
      v_sql = v_sql + " WHERE tbl_addr.t_group = :p_email_grp ";
      /* Сразу выполняем проверку включена ли рассылка по заданной группе */
      v_sql = v_sql + "   AND (SELECT tbl_dval.t_flag ";
      v_sql = v_sql + "          FROM dllvalues_dbt tbl_dval ";
      v_sql = v_sql + "         WHERE tbl_dval.t_list = :p_email_dict ";
      v_sql = v_sql + "           AND tbl_dval.t_element = tbl_addr.t_group) = :p_stat_on ";

      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_email_grp", RSDBP_IN, p_email_grp);
      v_cmd.addParam("p_email_dict", RSDBP_IN, CV_EMAIL_GROUP_DICT);
      v_cmd.addParam("p_stat_on", RSDBP_IN, CV_STAT_ON);
      v_rs = RSDRecordSet(v_cmd);

      if(v_rs.movenext())
         if(v_rs.value("t_place") == "C")
            m_add_email_to_cc_list(v_rs.value("t_email"));
         elif(v_rs.value("t_place") == "H")
            m_add_email_to_bcc_list(v_rs.value("t_email"));
         else /* Считаем, что все остальное относится к "R" */
            m_add_email_to_list(v_rs.value("t_email"));
         end;

         while(v_rs.movenext())
            if(v_rs.value("t_place") == "C")
               m_add_email_to_cc_list(v_rs.value("t_email"));
            elif(v_rs.value("t_place") == "H")
               m_add_email_to_bcc_list(v_rs.value("t_email"));
            else /* Считаем, что все остальное относится к "R" */
               m_add_email_to_list(v_rs.value("t_email"));
            end;
         end;
      else
         v_ret = false;
      end;

      v_rs.close(); v_cmd.close(); v_rs = NULL; v_cmd = NULL; v_sql = NULL;

      return v_ret;

   onError(err)
      v_ret = false;
      return v_ret;
   end;


   /*----------------------------------------------*/
   /* Метод сохранения сообщения в БД для отправки */
   /*     Письма будут отправлены планировщиком    */
   /*----------------------------------------------*/
   macro m_save_to_submit(isHtml)
      vg_is_error = false;
      /* Очищаем список сохраненных писем */
      m_clear_saved_id_list();
      vg_saved_id_list = NULL;
      /* Сохранение в БД */
//      AddNotifyToDbt(vg_msg_head, vg_msg_text, vg_email_list, vg_attach_list);
      vg_saved_id_list = TArray(u_AddNotifyToDbt_w_mode(isHtml,
                                                        vg_msg_head,
                                                        vg_msg_text,
                                                        vg_email_list,
                                                        vg_attach_list,
                                                        vg_email_cc_list,
                                                        vg_email_bcc_list));

   onError(err)
      vg_is_error = true;
      vg_service_msg = c_usr_err_obj.obtain_err_msg(err);
   end;


   /*-----------------------------------------------------------*/
   /*       Метод сохранения сообщения в БД для отправки        */
   /*        Письма со специальной меткой - должны быть         */
   /* отправлены сразу при помощи метода m_submit_email_synch() */
   /*-----------------------------------------------------------*/
   macro m_save_to_submit_synch(isHtml)
      vg_is_error = false;
      /* Очищаем список сохраненных писем */
      m_clear_saved_id_list();
      vg_saved_id_list = NULL;
      /* Сохранение в БД */
      vg_saved_id_list = TArray(u_AddNotifyToDbt_w_mode(isHtml,
                                                        vg_msg_head,
                                                        vg_msg_text,
                                                        vg_email_list,
                                                        vg_attach_list,
                                                        vg_email_cc_list,
                                                        vg_email_bcc_list,
                                                        CV_SYNCH_EMAIL_MARK));

   onError(err)
      vg_is_error = true;
      vg_service_msg = c_usr_err_obj.obtain_err_msg(err);
   end;


   /*-----------------------------------------------*/
   /* Метод сохранения сообщения по группе рассылки */
   /*-----------------------------------------------*/
   macro m_save_to_submit_grp(p_email_grp, p_is_synch, isHtml)
      var v_is_synch;
      if(ValType(p_is_synch) != V_BOOL)
         v_is_synch = false;
      else
         v_is_synch = p_is_synch;
      end;

      vg_is_error = false;

      m_clear_email_list();
      m_clear_email_cc_list();
      m_clear_email_bcc_list();

      /* Группа рассылки */
      if(m_get_email_group(p_email_grp))
         /* Сохранение в БД */
         if(v_is_synch)
            m_save_to_submit_synch(isHtml);
         else
            m_save_to_submit(isHtml);
         end;
      else
         vg_is_error = true;
         vg_service_msg = string("Группа рассылки (", p_email_grp, "): письма не сохранены для отправки");
      end;


   onError(err)
      vg_is_error = true;
      vg_service_msg = c_usr_err_obj.obtain_err_msg(err);
   end;


   /*-----------------------------------------------*/
   /*              Метод отправки писем             */
   /*  Будут отправлены все не отправленные письма  */
   /*-----------------------------------------------*/
   /* !!!- УСТАРЕВШИЙ МЕТОД - НЕ ИСПОЛЬЗОВАТЬ - !!! */
   /*-----------------------------------------------*/
   macro m_submit_email()
      vg_is_error = false;
      /* Отправка писем */
//      SendNotyfyToEmail(/*fromCnv:bool, interface_mode:bool*/);
      u_SendNotyfyToEmail();

   onError(err)
      vg_is_error = true;
      vg_service_msg = c_usr_err_obj.obtain_err_msg(err);
   end;


   /*-----------------------------------------------------------------*/
   /*                       Метод отправки писем                      */
   /* Будут отправлены все не отправленные письма (асинхронный режим) */
   /*-----------------------------------------------------------------*/
   macro m_submit_email_asynch()
      private var v_email_submitter = c_email_submit_tp_sep();

      vg_is_error = false;
      v_email_submitter.m_send_asynch();

      v_email_submitter = NULL;

   onError(err)
      vg_is_error = true;
      vg_service_msg = c_usr_err_obj.obtain_err_msg(err);
   end;


   /*-----------------------------------------------------*/
   /*                 Метод отправки писем                */
   /* Будут отправлены письма из списка vg_saved_id_list */
   /*-----------------------------------------------------*/
   macro m_submit_email_synch()//shev
      private var v_email_submitter = c_email_submit_tp_sep();

      vg_is_error = false;
      /*u_SendNotyfyToEmail(null, null, vg_saved_id_list);*/
      v_email_submitter.m_send_synch(vg_saved_id_list);

      v_email_submitter = NULL;

   onError(err)
      vg_is_error = true;
      vg_service_msg = c_usr_err_obj.obtain_err_msg(err);
   end;


   /*-------------*/
   /* Конструктор */
   /*-------------*/
   private macro m_init_prime()
      vg_is_error = true;
      vg_service_msg = "";

      vg_msg_head = "";
      vg_msg_text = "";
      vg_is_msg_text = false;
      vg_email_list = TArray();
      vg_email_cc_list = TArray();
      vg_email_bcc_list = TArray();
      vg_attach_list = TArray();
   end;

   m_init_prime();
end; /* class c_email_proc_env() */

/**
@brief Функция отправки email с использованием класса c_email_proc_env
@param[in] pMessageHead V_STRING Заголовок письма
@param[in] pMessageText Текст письма. Варианты - массив строк письма; строка с полным текстом письма.
@param[in] pEmails Адрес(а) получателей. Варианты - массив адресов; строка с одним адресом; номер группы рассылки (V_INTEGER)
@param[in] pAttaches Файл(ы) для отправки. Варианты - массив путей к файлам; строка с путем к отдельному файлу. Необяз.
@param[in] pEmailsCC Адрес(а) получателей скрытой копии. Варианты - массив адресов; строка с одним адресом. Необяз.
@param[in] pEmailsBCC Адрес(а) получателей скрытой копии. Варианты - массив адресов; строка с одним адресом. Необяз.
@return Сформированный экземпляр класса c_email_proc_env
*/
macro SendEmailSynch(pMessageHead: String, pMessageText, pEmails, pAttaches, pEmailsCC, pEmailsBCC)
  macro IsTArray(parameter): Bool
    return ((ValType(parameter) == V_GENOBJ) and isEqClass("TArray", parameter));
  end;
  
  macro RunProcByParamArray(procPtr: Variant, paramArray: TArray)
    var i: Integer = 0;
    
    while (i < paramArray.size)
      CallR2M(procPtr, paramArray(i));
      i = i + 1;
    end;
  end;
  
  var emailProcessor = c_email_proc_env();
  
  if (ValType(pMessageHead) == V_STRING)
    emailProcessor.m_set_msg_head(pMessageHead);
  else
    RunError("Неверный тип параметра pMessageHead функции SendEmail");
  end;
  
  if (ValType(pMessageText) == V_STRING)
    emailProcessor.m_add_row_to_msg_text(pMessageText);
  elif (IsTArray(pMessageText))
    RunProcByParamArray(R2M(emailProcessor, "m_add_row_to_msg_text"), pMessageText);
  else
    RunError("Неверный тип параметра pMessageText функции SendEmail");
  end;
  
  if (ValType(pEmails) == V_STRING)
    emailProcessor.m_add_email_to_list(pEmails);
  elif (IsTArray(pEmails))
    RunProcByParamArray(R2M(emailProcessor, "m_add_email_to_list"), pEmails);
  elif (ValType(pEmails) != V_INTEGER)
    RunError("Неверный тип параметра pEmails функции SendEmail");
  end;
  
  if (ValType(pAttaches) == V_STRING)
    emailProcessor.m_add_attach_to_list(pAttaches);
  elif (IsTArray(pAttaches))
    RunProcByParamArray(R2M(emailProcessor, "m_add_attach_to_list"), pAttaches);
  end;
  
  if (ValType(pEmailsCC) == V_STRING)
    emailProcessor.m_add_email_to_cc_list(pEmailsCC);
  elif (IsTArray(pEmailsCC))
    RunProcByParamArray(R2M(emailProcessor, "m_add_email_to_cc_list"), pEmailsCC);
  end;
  
  if (ValType(pEmailsBCC) == V_STRING)
    emailProcessor.m_add_email_to_bcc_list(pEmailsBCC);
  elif (IsTArray(pEmailsBCC))
    RunProcByParamArray(R2M(emailProcessor, "m_add_email_to_bcc_list"), pEmailsBCC);
  end;
  
  if (ValType(pEmails) == V_INTEGER)
    emailProcessor.m_save_to_submit_grp(pEmails, true);
  else
    emailProcessor.m_save_to_submit_synch();
  end;
  
  emailProcessor.m_submit_email_synch();
  
  return emailProcessor;
end;