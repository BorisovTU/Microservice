/*----------------------------------------------*/
/* Открытие/закрытие счета в АБС (ответ в СОФР) */
/*----------------------------------------------*/

import "global_utils_intgr.mac";
import "uws_exchng_log.mac";

private macro TryGetOutcome (p_jmsmessageid)
   // попытаемся найти исходный запрос
   var parent_eventid = 0, parent_ulog_id = 0, temp_str = "", pos = 0; 
   var sql_str, cmd, rs;
   var NotFound_event = false;
   var res = 0;

   sql_str = "select l_in.t_jmsmessageid in_jmsmessageid, l_out.t_jmsmessageid out_jmsmessageid, l_out.t_jmscorrelationid user_id"+
             "  from dqueue_log_dbt l_in, "+
             "       dqueue_log_dbt l_out "+
             " where "+
             "     l_in.t_jmsmessageid = :p_jmsmessageid "+
             " and l_in.t_qname = 'ESB_TO_SOFR' "+
             " and l_out.t_jmsmessageid = l_in.t_jmscorrelationid "+
             " and rownum = 1; ";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("p_jmsmessageid", RSDBP_IN, p_jmsmessageid);
   rs = RSDRecordSet(cmd);
   if (rs.movenext)
      temp_str = rs.value("user_id");
   else 
      temp_str = "";
   end;
   pos = Index(temp_str, "-");
   if (pos > 0)
      parent_eventid = Int(substr(temp_str,1,pos-1));
      parent_ulog_id = Int(substr(temp_str, pos+1));
   end;
   rs.close; cmd.close; rs = null; cmd = null;
   
   if (parent_eventid > 0)
      sql_str = "select t_objectid from utableprocessevent_dbt "+
                " where t_recid = :p_recid ";
      cmd = RSDCommand(sql_str);
      cmd.Addparam("p_recid", RSDBP_IN, parent_eventid);
      rs = RSDRecordSet(cmd);
      if (rs.movenext)
         res = rs.value("t_objectid");
      else 
         NotFound_event = true;
      end;
   else 
      NotFound_event = true;
   end;
   return res;

end;


private class c_IntegrSymId(p_income_data, ObjFalse )
   var ObjectId     : string
      ,SystemId     : string
      ,SystemNodeId : string;

   private macro m_init_IntegrSymId(i_income_data, ObjFalse)  //18.01.2021 А.Киселев гвоздик для ЦФТ
      private var v_service_msg;

      if( ( Valtype(ObjFalse) == V_UNDEF ) or ( Valtype(ObjFalse) == 26 ) )
       ObjFalse = true;
      end;

      if(ValType(i_income_data) != V_UNDEF)
         ObjectId = uTryToGetPropS(i_income_data, "ObjectId");
         if( ( ValType(ObjectId) == V_UNDEF ) and (not ObjFalse) )
          ObjectId = "-1";
         elif( ValType(ObjectId) == V_UNDEF )
            v_service_msg = string(InErrComPart, "ObjectId");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;

         SystemId = uTryToGetPropS(i_income_data, "SystemId");
         SystemNodeId = uTryToGetPropS(i_income_data, "SystemNodeId");
      end;
   end;

   m_init_IntegrSymId(p_income_data, ObjFalse);
end;


private class c_Error(p_income_data)
   var ErrorCode : string
      ,ErrorDesc : string;

   private macro m_init_Error(i_income_data)
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         ErrorCode = uTryToGetPropS(i_income_data, "ErrorCode");
         if(ValType(ErrorCode) == V_UNDEF)
            v_service_msg = string(InErrComPart, "ErrorCode");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;

         ErrorDesc = uTryToGetPropS(i_income_data, "ErrorDesc");
      end;
   end;

   m_init_Error(p_income_data);
end;


private class c_OpenAccountResult_Result(p_income_data, p_jmsmessageid)
   var ResultCode    : string
      ,AccountNumber : string
      ,AccountIdSOFR : object  /* c_IntegrSymId */
      ,AccountIdBIS  : object  /* c_IntegrSymId */
      ,ErrorList     : object; /* TArray */


   private macro m_init_OpenAccountResult(i_income_data, p_jmsmessageid)
      private var v_aux_buf;
      private var v_aux_cntr;
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         ResultCode = uTryToGetPropS(i_income_data, "ResultCode");
         if(ValType(ResultCode) == V_UNDEF)
            v_service_msg = string(InErrComPart, "ResultCode");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;

         // дальнейшие поля являются обязательными, только если ResultCode = 0 
         AccountNumber = uTryToGetPropS(i_income_data, "AccountNumber");
         if( (ValType(AccountNumber) == V_UNDEF) and (ResultCode == 0) )
            v_service_msg = string(InErrComPart, "AccountNumber");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;

         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "AccountIdSOFR"));
         if(v_aux_buf != V_UNDEF)
            if(v_aux_buf == V_GENOBJ)
               AccountIdSOFR = c_IntegrSymId(i_income_data.AccountIdSOFR, (ResultCode == 0) );
               if (AccountIdSOFR.ObjectId == "-1")
                  AccountIdSOFR.ObjectId = TryGetOutCome(p_jmsmessageid);
               end;
            else
               if (ResultCode == 0)
                  v_service_msg = string(InErrNotObj, "AccountIdSOFR");
                  RunError(v_service_msg, c_usr_err_obj(v_service_msg));
               end;
            end;
         else
            v_service_msg = string(InErrComPart, "AccountIdSOFR");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;

         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "AccountIdBIS"));
         if(v_aux_buf != V_UNDEF)
            if(v_aux_buf == V_GENOBJ)

//!!!! А.Киселев 18.01.2021 гвоздик для ЦФТ
//              AccountIdBIS = c_IntegrSymId(i_income_data.AccountIdBIS);
               AccountIdBIS = c_IntegrSymId(i_income_data.AccountIdBIS, false);
            else
               if (ResultCode == 0)
                  v_service_msg = string(InErrNotObj, "AccountIdBIS");
                  RunError(v_service_msg, c_usr_err_obj(v_service_msg));
               end;
            end;
         else
            v_service_msg = string(InErrComPart, "AccountIdBIS");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;

         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "ErrorList"));
         if(v_aux_buf != V_UNDEF)
            if(v_aux_buf == V_GENOBJ)
               v_aux_cntr = 0;
               ErrorList = TArray;
               while(i_income_data.ErrorList.size > v_aux_cntr)
                  ErrorList.value(v_aux_cntr) = c_Error(i_income_data.ErrorList.value(v_aux_cntr));
                  v_aux_cntr = v_aux_cntr + 1;
               end;
            else
               v_service_msg = string(InErrNotObj, "ErrorList");
               RunError(v_service_msg, c_usr_err_obj(v_service_msg));
            end;
         end;
      end;
   end;

   m_init_OpenAccountResult(p_income_data, p_jmsmessageid);
end;


private class c_OpenAccountResult(p_income_data, p_jmsmessageid)
   var GeneralResultCode : string
      ,ResultList        : object; /* TArray - c_OpenAccountResult_Result */

   private macro m_init_OpenAccountResult(i_income_data, p_jmsmessageid)
      private var v_service_msg;
      private var v_aux_buf;
      private var v_aux_cntr;

      if(ValType(i_income_data) != V_UNDEF)
         GeneralResultCode = uTryToGetPropS(i_income_data, "GeneralResultCode");
         if(ValType(GeneralResultCode) == V_UNDEF)
            v_service_msg = string(InErrComPart, "GeneralResultCode");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;

         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "ResultList"));
         if(v_aux_buf != V_UNDEF)
            if(v_aux_buf == V_GENOBJ)
               v_aux_cntr = 0;
               ResultList = TArray;
               while(i_income_data.ResultList.size > v_aux_cntr)
                  ResultList.value(v_aux_cntr) = c_OpenAccountResult_Result(i_income_data.ResultList.value(v_aux_cntr), p_jmsmessageid);
                  v_aux_cntr = v_aux_cntr + 1;
               end;
            else
               v_service_msg = string(InErrNotObj, "ResultList");
               RunError(v_service_msg, c_usr_err_obj(v_service_msg));
            end;
         else
            v_service_msg = string(InErrComPart, "ResultList");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;
      end;
   end;

   m_init_OpenAccountResult(p_income_data, p_jmsmessageid);
end;


private class c_OpenAccountsResponse(p_income_data, p_jmsmessageid)
   var OpenAccountResult : object; /* c_OpenAccountResult */

   private macro m_init_OpenAccountsResponse(i_income_data, p_jmsmessageid)
      private var v_aux_buf;
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "OpenAccountResult"));
         if(v_aux_buf != V_UNDEF)
            if(v_aux_buf == V_GENOBJ)
               OpenAccountResult = c_OpenAccountResult(i_income_data.OpenAccountResult, p_jmsmessageid);
            else
               v_service_msg = string(InErrNotObj, "OpenAccountResult");
               RunError(v_service_msg, c_usr_err_obj(v_service_msg));
            end;
         else
            v_service_msg = string(InErrComPart, "OpenAccountResult");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;
      end;
   end;

   m_init_OpenAccountsResponse(p_income_data, p_jmsmessageid);
end;


private class c_OpenAccountsResponseMsg(p_income_data, p_jmsmessageid);
   var OpenAccountsResponse : object; /* c_OpenAccountsResponse */

   private macro m_init_OpenAccountsResponseMsg(i_income_data, p_jmsmessageid)
      private var v_aux_buf;
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "OpenAccountsResponse"));
         if(v_aux_buf != V_UNDEF)
            if(v_aux_buf == V_GENOBJ)
               OpenAccountsResponse = c_OpenAccountsResponse(i_income_data.OpenAccountsResponse, p_jmsmessageid);
            else
               v_service_msg = string(InErrNotObj, "OpenAccountsResponse");
               RunError(v_service_msg, c_usr_err_obj(v_service_msg));
            end;
         else
            v_service_msg = string(InErrComPart, "ApproveEntry");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;
      end;
   end;

   m_init_OpenAccountsResponseMsg(p_income_data, p_jmsmessageid);
end;

//shev def-42590
         
private macro SkipErr(ErrText, ErrCode)

 var sql, rs, cmd, text;


  text = StrSubst(ErrText,"'"," ");

 sql = " select trim(dl.t_note) t_note from dllvalues_dbt dl, dobjects_dbt ob where ob.T_OBJECTTYPE = DL.T_LIST and ob.t_code = 'OA_SkipEr'  "+
       " and dl.t_code = '"+ErrCode+"'";
 cmd = RSDCommand(sql);

 rs = RSDRecordSet(cmd);
 if(rs.movenext())
   if (StrLen(rs.value("t_note")) <=1)
      return true;
   elif (Index(Text,rs.value("t_note")) > 0)
      return true;
   end;
 end;

 return false;

end;


/*--------------------------------------*/
/* Обновление статуса исходного события */
/*--------------------------------------*/
private macro m_save_src_event_status(p_obj_id, p_result_code, v_stat_descr, p_ext_id, p_log_id, v_stat_code)
   const CV_OBJTYPE_ACC = 4;
   const CV_USR_TYPE_ACC = "Ы";
   const CV_STATUS_PRC = 2;
   var v_ret = 0;
   var v_aux_buf;
   var v_err_len;
   var v_sql, v_cmd, v_rs;
   var v_status_end;
   var v_status_descr;

   v_aux_buf = ValType(p_log_id);

   v_err_len = strlen(v_stat_descr);

   if(p_result_code == "0")
      v_status_end = 4;
   elif(SkipErr(v_stat_descr, v_stat_code))
      v_status_end = 6;
      v_status_descr = "Выгрузка прекращена. Полученная ошибка в списке исключений";
   else
      v_status_end = 5;
      v_status_descr = v_stat_descr;
   end;

   v_sql =         "DECLARE ";
   v_sql = v_sql + "    PRAGMA autonomous_transaction; "; /* Для корректной работы с ответами по парным счетам */
   v_sql = v_sql + "    v_ret             NUMBER(5) := 0; ";
   v_sql = v_sql + "    v_obj_type        utableprocessevent_dbt.t_objecttype%TYPE; ";
   v_sql = v_sql + "    v_type        utableprocessevent_dbt.t_type%TYPE; ";
   v_sql = v_sql + "    v_timestamp        utableprocessevent_dbt.t_timestamp%TYPE; ";
   v_sql = v_sql + "    v_status_prc      utableprocessevent_dbt.t_status%TYPE; ";
   v_sql = v_sql + "    v_usertypeaccount VARCHAR2(5) := :p_usertypeaccount; ";
   v_sql = v_sql + "    v_userfield4      daccount_dbt.t_userfield4%TYPE := :p_ext_id; ";
   v_sql = v_sql + "    v_objectid        daccount_dbt.t_accountid%TYPE := :p_objectid; ";
   v_sql = v_sql + "BEGIN ";
   v_sql = v_sql + "    BEGIN ";
   v_sql = v_sql + "        v_ret := 1; ";
   v_sql = v_sql + "        SELECT t_objecttype, t_status, t_timestamp, t_type ";
   v_sql = v_sql + "          INTO v_obj_type, v_status_prc,v_timestamp,v_type ";
   v_sql = v_sql + "          FROM utableprocessevent_dbt ";
   v_sql = v_sql + "         WHERE t_objecttype = :p_objecttype ";
   v_sql = v_sql + "           AND t_objectid = v_objectid ";
   v_sql = v_sql + "           AND t_status in ( :p_status_prc, -1420 ) "; //для парного счета событие помечено как невыгружаемое, ID будет отобран, но не обновлен
   v_sql = v_sql + "           AND ROWNUM = 1; "; /* На всякий случай - обновлять будем все найденные */

   v_sql = v_sql + "        v_ret := 2; ";
   v_sql = v_sql + "        if v_status_prc <> -1420 then ";
   v_sql = v_sql + "           UPDATE utableprocessevent_dbt ";
   v_sql = v_sql + "              SET t_status = :p_status_end ";
   if(v_err_len != 0)
      v_sql = v_sql + "              ,t_resulttext = :p_resulttext ";
   end;
   if(v_aux_buf != V_UNDEF)
      v_sql = v_sql + "              ,t_messageid = :p_log_id ";
   end;
   v_sql = v_sql + "            WHERE t_objecttype = v_obj_type ";
   v_sql = v_sql + "              AND t_objectid = v_objectid ";
   v_sql = v_sql + "              AND t_status = v_status_prc; ";
   v_sql = v_sql + "        end if; "; 

   if(v_status_end == 4)
      v_sql = v_sql + "     UPDATE daccount_dbt ";
      v_sql = v_sql + "        SET t_usertypeaccount = DECODE(t_usertypeaccount, chr(1), v_usertypeaccount, (t_usertypeaccount || v_usertypeaccount)) ";
      v_sql = v_sql + "      WHERE t_accountid = v_objectid ";
      v_sql = v_sql + "        AND INSTR(t_usertypeaccount, v_usertypeaccount) = 0; ";

      v_sql = v_sql + "     UPDATE daccount_dbt ";
      /* Если к одному счету каким-то образом возникнет два разных ext_id, то можно будет найти следы */
      v_sql = v_sql + "        SET t_userfield4 = DECODE(t_userfield4, chr(1), v_userfield4, (t_userfield4 || ';' || v_userfield4)) ";
      v_sql = v_sql + "      WHERE t_accountid = v_objectid ";
      v_sql = v_sql + "        AND INSTR(t_userfield4, v_userfield4) = 0; "; /* Если такой идентификатор уже сохранен, то ничего не трогаем */
   end;

//shev def-42590,53549
   if(v_status_end == 6)
      v_sql = v_sql + "        IF v_type = 3 THEN   ";
      v_sql = v_sql + "           update daccount_dbt set t_close_date = trunc(v_timestamp) where t_accountid = v_objectid and t_close_date != TRUNC (v_timestamp);     ";
      v_sql = v_sql + "        END IF;  ";
   end;

   v_sql = v_sql + "        v_ret := 0; ";
   v_sql = v_sql + "        COMMIT; ";

   v_sql = v_sql + "    EXCEPTION ";
   v_sql = v_sql + "        WHEN NO_DATA_FOUND ";
   v_sql = v_sql + "        THEN ";
//   v_sql = v_sql + "            ROLLBACK; ";
   v_sql = v_sql + "            v_ret := v_ret; ";
   v_sql = v_sql + "        WHEN OTHERS ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            ROLLBACK; ";
   v_sql = v_sql + "            v_ret := SQLCODE; ";
   v_sql = v_sql + "    END; ";

   v_sql = v_sql + "    :p_ret := v_ret; ";
   v_sql = v_sql + "END; ";

   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_usertypeaccount", RSDBP_IN, CV_USR_TYPE_ACC);
   v_cmd.addParam("p_ext_id", RSDBP_IN, p_ext_id);
   v_cmd.addParam("p_objectid", RSDBP_IN, p_obj_id);
   v_cmd.addParam("p_objecttype", RSDBP_IN, CV_OBJTYPE_ACC);
   v_cmd.addParam("p_status_prc", RSDBP_IN, CV_STATUS_PRC);
   v_cmd.addParam("p_status_end", RSDBP_IN, v_status_end);
   if(v_err_len != 0)
      v_cmd.addParam("p_resulttext", RSDBP_IN, v_status_descr);
   end;
   if(v_aux_buf != V_UNDEF)
      v_cmd.addParam("p_log_id", RSDBP_IN, p_log_id);
   end;
   v_cmd.addParam("p_ret", RSDBP_OUT, V_INTEGER);

   v_cmd.execute();

   v_ret = v_cmd.value("p_ret");

   v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret = 3;
   return v_ret;
end; /* macro m_save_src_event_status() */


/*-------------*/
/* ТОЧКА ВХОДА */
/*-------------*/
macro OpenAccountsResponseMsg(p_income_data, p_log_id, p_jmsmessageid)
   var v_logger_obj;
   var v_acc_err = "";
   var v_acc_code = "";
   var v_log_err = "";
   var v_income_data;
   var v_aux_buf;
   var v_cntr_res;
   var v_cntr_err;
   /* Получаем проверенный объект со входными данными */
   v_income_data = c_OpenAccountsResponseMsg(p_income_data, p_jmsmessageid);

   v_logger_obj = uws_exchng_logger();
   v_logger_obj.set_log_id(p_log_id);

   v_cntr_res = 0;
   while(v_income_data.OpenAccountsResponse.OpenAccountResult.ResultList.size > v_cntr_res)
      v_acc_err = "";
      v_acc_code = "0";

      /* Если верхнеуровневый код показывает, что вернулась ошибка, то собираем текст ошибки */
      if(v_income_data.OpenAccountsResponse.OpenAccountResult.ResultList.value(v_cntr_res).ResultCode != 0)
         /* Верхнеуровневый код */
         v_acc_err = m_add_note_to_str(string("Получена ошибка: ",
                                              v_income_data.OpenAccountsResponse.OpenAccountResult.ResultList.value(v_cntr_res).ResultCode,
                                              " (",
                                              v_income_data.OpenAccountsResponse.OpenAccountResult.ResultList.value(v_cntr_res).AccountIdSOFR.ObjectId,
                                              ")."),
                                       v_acc_err);

         v_cntr_err = 0;
         while(v_income_data.OpenAccountsResponse.OpenAccountResult.ResultList.value(v_cntr_res).ErrorList.size > v_cntr_err)
            /* Описание ошибок */
            v_acc_err = m_add_note_to_str(string("Получена ошибка: ",
                                                 v_income_data.OpenAccountsResponse.OpenAccountResult.ResultList.value(v_cntr_res).ErrorList.value(v_cntr_err).ErrorCode,
                                                 ";",
                                                 v_income_data.OpenAccountsResponse.OpenAccountResult.ResultList.value(v_cntr_res).ErrorList.value(v_cntr_err).ErrorDesc,
                                                 ")."),
                                          v_acc_err);
            v_acc_code = v_income_data.OpenAccountsResponse.OpenAccountResult.ResultList.value(v_cntr_res).ErrorList.value(v_cntr_err).ErrorCode;
            v_cntr_err = v_cntr_err + 1;
         end;
      end;

      v_aux_buf = m_save_src_event_status(v_income_data.OpenAccountsResponse.OpenAccountResult.ResultList.value(v_cntr_res).AccountIdSOFR.ObjectId,
                                          v_income_data.OpenAccountsResponse.OpenAccountResult.ResultList.value(v_cntr_res).ResultCode,
                                          substr(v_acc_err, 1, 999),
                                          v_income_data.OpenAccountsResponse.OpenAccountResult.ResultList.value(v_cntr_res).AccountIdBIS.ObjectId,
                                          p_log_id,
                                          v_acc_code );
      if(v_aux_buf != 0)
         v_log_err = m_add_note_to_str(string("Не удалось обновить статус исходного события: ",
                                              v_aux_buf,
                                              " (",
                                              v_income_data.OpenAccountsResponse.OpenAccountResult.ResultList.value(v_cntr_res).AccountIdSOFR.ObjectId,
                                              ")."),
                                       v_log_err);
      end;

      v_log_err = v_log_err + v_acc_err;

      v_cntr_res = v_cntr_res + 1;
   end;

   /* Если были какие-то ошибки - сохраняем их в лог */
   v_aux_buf = strlen(v_log_err);
   if(v_aux_buf > 0)
      if(v_aux_buf >= 2000)
         v_log_err = substr(v_log_err, 1, 1999);
      end;
      v_logger_obj.add_error_info(GC_ADD_DEV_ERR_CODE, v_log_err);
   end;

   v_logger_obj = NULL;

   return true;

onError(err)
   v_logger_obj.add_error_info(c_usr_err_obj.obtain_err_code(err), c_usr_err_obj.obtain_err_msg(err));

   return false;
end;