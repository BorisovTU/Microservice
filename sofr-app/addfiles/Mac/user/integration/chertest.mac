/*---------------------------------------------------------------*/
/* Запрос из СОФР в Кондор информации по неттингу по контрагенту */
/*                                                               */
/* Автор: Гераськина Т.                                          */
/*

*/
/*----------------------------------------------*/
import "ws_msg_transform_sec.mac"; /* Инструментарий для преобразования сообщений */
import "secur_prc_msg_transform.mac";
import "email_notifyfun.mac";
import "func_lib.mac";
import or_rep_h;

/* Константы, определяемые на этапе тестирования - когда есть доступ к внешним сервисам */
private const REQ_ROOT_NS_ALIAS = "";
private const REQ_NS_LIST       = "";

/* Пользовательская настойка реестра  */
// используются настройки от контроля лимитов
private const RUN_USERNAME =     "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\DEALCONTROLLIMIT\\USERNAME";
private const RUN_USERPASSWORD = "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\DEALCONTROLLIMIT\\USERPASSWORD";
private const RUN_RULENAME =     "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\DEALCONTROLLIMIT\\RULENAME";

private const URL_LOC_REG_PATH =        "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\DEALCONTROLLIMIT\\URL";
private const HOST_LOC_REG_PATH =       "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\DEALCONTROLLIMIT\\HOST";
private const SOAPACTION_LOC_REG_PATH = "РСХБ\\ИНТЕГРАЦИЯ\\INTEGR_SERV_PRM\\EXT_SERVICES\\DEALCONTROLLIMIT\\SOAPACTION";

private var ar_variance_s = TArray;
private var ar_variance_p = TArray;
private var Rep;


// класс для заполнения извне
private class c_Netting(p_req_data_obj)
   var PartyID       : string;    
   var PartyCode     : string;    
   var DealDate      : date;      
end;


// заполнение класса параметрами объекта Сделка 
macro CreateObjectForExport(_partyid, _repdate)
   private var obj;

   obj = c_Netting();   

   obj.PartyID = _partyid;
   obj.PartyCode = GetPartyCode_ByNumber(_partyid, 103);
   obj.DealDate = _repdate;
   
   return obj;

onerror(err)
   
end;

class c_NettingRequest(Incomedata) 
   var PartyCode     : string;    
   var DealDate     ; //: date;   
   
   private macro uInitPrime(IncomeData)
  
      if(ValType(IncomeData) != V_UNDEF)
         PartyCode = uTryToGetPropS(IncomeData, "PartyCode");
         DealDate = uTryToGetPropS(IncomeData, "DealDate");
      end;
      
   end;
   
   macro is_empty_obj()
      private var ret = false;
      private var empty_prm_list = TArray;

      if((ValType(PartyCode) == V_UNDEF) and
         (ValType(DealDate) == V_UNDEF))
         ret = true;
      end;

      if((not ret) and (empty_prm_list.size != 0))
         RunError("Пустой класс");
      end;

      return ret;
   end; /* macro is_empty_obj() */
   
   uInitPrime(IncomeData); 
      
end;


private class c_Result
   var Code : string;  // Поле Код, обязательное: Да
   var Text : string;   // Поле Текст, обязательное: Нет
end;

private class c_Sum(IncomeData)
   var Currency : string;  
   var Amount   : double;

   /* Кратность параметров в соответствии со спецификацией */
   private macro uInitPrime(IncomeData)
      if(ValType(IncomeData) != V_UNDEF)
         Currency = uTryToGetPropS(IncomeData, "Currency");
         Amount   = uTryToGetPropS(IncomeData, "Amount");
      end;
   end;

   uInitPrime(IncomeData);
   
end;

private class c_Payment(IncomeData)
   var Currency : string;  
   var Amount   : double;   
   var Direction: string;
   var DealCode : string;

   /* Кратность параметров в соответствии со спецификацией */
   private macro uInitPrime(IncomeData)
      if(ValType(IncomeData) != V_UNDEF)
         Currency  = uTryToGetPropS(IncomeData, "Currency");
         Amount    = uTryToGetPropS(IncomeData, "Amount");
         Direction = uTryToGetPropS(IncomeData, "Direction");
         DealCode  = uTryToGetPropS(IncomeData, "DealCode");
      end;
   end;

   uInitPrime(IncomeData);

end;

private class c_PaymentNew(Cur, Am, Dir, DC)
   var Currency : string = Cur;
   var Amount   : double = Am;
   var Direction: string = Dir;
   var DealCode : string = DC;
end;

class c_NettingResponse
   var PartyCode : string;
   var DealDate : date;  
   var Result   : c_Result;
   var SumList;
   var PaymentList;
end;


/* Корневой элемент ответа - AS ID - как пришел */
private class adp_Netting(IncomeData)
   var PartyCode : string;
   var DealDate : date;  
   var Result   : c_Result;
   var SumList;
   var PaymentList;
   var fastCount; // for faster insert in array
   var fCur, fAm, fDir, fDC;
   var tmpPaymentData;
   var fastTest;

   var aux_buff;

   /* Кратность параметров в соответствии со схемой/спецификацией */
   private macro uInitPrime(IncomeData)
      var err_txt;
      
      /* !!! Фактически, здесь могут быть также трансформации,                 !!! */
      /* !!! связанные с различием получаемой и ожидаемой инициатором структур !!! */
      if(ValType(IncomeData) != V_UNDEF)
         BegAction(2000,"Обработка данных...");
         PartyCode = uTryToGetPropS(IncomeData, "PartyCode");
         DealDate = uTryToGetPropS(IncomeData, "DealDate");
         
         aux_buff = uTryToGetPropS(IncomeData, "Result");
         if (ValType(IncomeData) != V_UNDEF)
            Result.Code = uTryToGetPropS(aux_buff, "Code");
            Result.Text = uTryToGetPropS(aux_buff, "Text");
         else
            err_txt = string(InErrComPart, "Result");
            RunError(err_txt, c_err_obj(err_txt,"No param")); 
         end;

         aux_buff = getValueFromProperty(IncomeData, "SumList"); 
         if(ValType(aux_buff) != V_UNDEF)
            SumList = TArray;
            aux_buff = 0;
            fastCount = IncomeData.SumList.size;
            for (aux_buff, 0, fastCount-1)
            //while(IncomeData.SumList.size > aux_buff)
               //SumList.value(SumList.size) = c_Sum(IncomeData.SumList.value(aux_buff));
               SumList.value(aux_buff) = c_Sum(IncomeData.SumList.value(aux_buff));
               //aux_buff = aux_buff + 1;
            end;
         end;

         aux_buff = getValueFromProperty(IncomeData, "PaymentList"); 
         if(ValType(aux_buff) != V_UNDEF)
            PaymentList = TArray;
            aux_buff = 0;
            fastCount = IncomeData.PaymentList.size;
            fastTest = IncomeData.PaymentList;
            for (aux_buff, 0, fastCount-1)
            //while(IncomeData.PaymentList.size > aux_buff)
               //PaymentList.value(PaymentList.size) = c_Payment(IncomeData.PaymentList.value(aux_buff));
 		 tmpPaymentData = fastTest.value(aux_buff);
/*
               fCur = uTryToGetPropS(tmpPaymentData, "Currency");
               fAm  = uTryToGetPropS(tmpPaymentData, "Amount");
               fDir = uTryToGetPropS(tmpPaymentData, "Direction");
               fDC  = uTryToGetPropS(tmpPaymentData, "DealCode");
*/

               //PaymentList.value(aux_buff) = c_PaymentNew(fCur, fAm, fDir, fDC);
               PaymentList.value(PaymentList.size) = c_Payment(fastTest.value(aux_buff));
               //aux_buff = aux_buff + 1;
            end;
         end;
         EndAction();
      end;
   end;

   uInitPrime(IncomeData);
end; /* class adp_Netting(IncomeData) */


/* Основной класс работы с сервисом */
private class (c_secur_ext_ws_processor) c_ExportNettingProcessor(IncomeData_main)

   private class cc_ExportNetting(IncomeData)
      var GetNetting_req; /* !!! По названию корневого тега запроса !!! */

      private macro uInitPrime(IncomeData)
         GetNetting_req = c_NettingRequest(IncomeData);
         if(GetNetting_req.is_empty_obj())
            RunError(string(InErrComPart, "GetNetting_req"));
         end;
      end;

      uInitPrime(IncomeData);
   end; /* private class cc_ExportNetting(IncomeData) */

   private macro uInitPrime_main(IncomeData_main)
      // определимся с классом.
      var obj, DefineDealType; 
      var err_txt;
      if (Valtype(IncomeData_main) != V_GENOBJ)
        err_txt = "Неверный тип входящего параметра, ожидается class";
        RunError(err_txt, c_err_obj(err_txt,-101));
      end;
      
      obj = cc_ExportNetting(IncomeData_main);
      
      set_req_obj(obj);
   end;


   /*----------------------------------------------*/
   /* Метод получения сообщения запроса из объекта */
   /*----------------------------------------------*/
   /*      Перегруженный метод базового класса     */
   /*----------------------------------------------*/
   macro m_make_req_msg(p_root_ns_alias, p_ns_list)
      private var v_ret = true;
      private var v_username = NULL;
      private var v_userpassword = NULL;
      private var v_ruleName = NULL;
      private var xmltxt;

      private var v_wrap_obj;

      if(v_ret)
         vg_req_msg = c_secur_msg_converter.m_secur_external_req(vg_req_obj);
         
         v_username     = uGetRegistryParam(RUN_USERNAME, V_STRING);
         v_userpassword = uGetRegistryParam(RUN_USERPASSWORD, V_STRING);
         v_ruleName     = uGetRegistryParam(RUN_RULENAME, V_STRING);

         CaptureOutput;
         println("<Run xmlns=\"http://systematica.ru/ModullarWebService\">");
         println("   <userName>"+v_username+"</userName>");
         println("   <userPassword>"+v_userpassword+"</userPassword>");
         println("   <ruleName>"+v_ruleName+"</ruleName>");
         println("   <message><![CDATA[ ");
         println("   "+vg_req_msg);
         println("     ]]></message> " );
         println("</Run>");

         xmltxt = StopCaptureOutput;
         
         v_wrap_obj = c_secur_msg_converter();

         if(v_wrap_obj.m_wrap_req_in_soap(xmltxt,
                                          p_root_ns_alias,
                                          p_ns_list))
            vg_req_msg = v_wrap_obj.get_result_msg();
         else
            vg_service_msg = vg_req_msg.get_service_msg();
            v_ret = false;
         end;

         v_wrap_obj = null;
      end;

      return v_ret;

   onError(err)
      v_ret = false;
      vg_service_msg = string("При формировании сообщения запроса возникла ошибка: ",
                              c_usr_err_obj.obtain_err_msg(err));

      return v_ret;
   end; /* macro m_make_req_msg() */

   macro m_make_resp_obj()
      private const CV_BORDER_1 = "<RunResult>";
      private const CV_BORDER_2 = "</RunResult>";
      private var v_ret = true;
      private var v_pos1, v_pos2;
      private var v_aux_buff, v_pure_msg;

      if(ValType(vg_resp_msg) == V_STRING)
         v_aux_buff = vg_resp_msg;
         v_pos1 = index(v_aux_buff, CV_BORDER_1);
         v_pos2 = index(v_aux_buff, CV_BORDER_2);

         if((v_pos1 == 0) or (v_pos2 == 0))
            vg_service_msg = "Полученное сообщение не соответствует заданному формату";
            v_ret = false;
         else
            v_pure_msg = substr(v_aux_buff, (v_pos1 + strlen(CV_BORDER_1)), (v_pos2 - v_pos1 - strlen(CV_BORDER_1)));
         end;

         /* Если вдруг символы '<' и '>' экранируются, то приводим строку к нормальному виду */
         v_aux_buff = StrSubst(v_pure_msg, "&lt;", "<");
         v_aux_buff = StrSubst(v_aux_buff, "&#60;", "<");
         v_aux_buff = StrSubst(v_aux_buff, "&gt;", ">");
         v_aux_buff = StrSubst(v_aux_buff, "&#62;", ">");
         v_aux_buff = StrSubst(v_aux_buff, "&#xD;", "");

         v_pure_msg = v_aux_buff;
      else
         vg_service_msg = "Тип данных не соответствует ожидаемому";
         v_ret = false;
      end;

      vg_resp_obj = c_secur_msg_converter.m_secur_external_resp(v_pure_msg);

      return v_ret;

   onError(err)
      v_ret = false;
      vg_service_msg = string("В процессе преобразования ответа возникла ошибка: ",
                              c_usr_err_obj.obtain_err_msg(err));

      return v_ret;
   end; /* macro m_make_resp_obj() */


   Initc_secur_ext_ws_processor();

   uInitPrime_main(IncomeData_main);
end; /* private class c_ExportStatusDealProcessor() */


/*-------------------------------------------------------------------*/
/* Адаптер для вызова сервиса "Запрос неттинга по контрагенту"       */
/*-------------------------------------------------------------------*/
macro ExportNetting(ReqData)
   var service_processor = NULL;
   var RespData = NULL;
   var PrepObj = NULL;
   var RespObj = NULL;
   var RespErr = NULL;
   var aux_buff_a, aux_buff_i;

   var v_url, v_host, v_soapaction;
   var err_txt;

   if ( (ReqData == NULL) or (ValType(ReqData) == V_UNDEF) )
      return null;
   end;

   service_processor = c_ExportNettingProcessor(ReqData);


   /* Подготовка сообщения для отправки */
   if(not service_processor.m_make_req_msg(REQ_ROOT_NS_ALIAS, REQ_NS_LIST))
      RunError(service_processor.get_service_msg());
   end;

   v_url        = uGetRegistryParam(URL_LOC_REG_PATH, V_STRING);
   v_host       = uGetRegistryParam(HOST_LOC_REG_PATH, V_STRING);
   v_soapaction = uGetRegistryParam(SOAPACTION_LOC_REG_PATH, V_STRING);

   /* Отправка запроса */
   BegAction(2000,"Ожидание ответа от сервера. Запрос ...");
   if(not service_processor.uCallExtServiceViaWinHttp(v_url, modulename(), "ContrGetNetting", v_host, v_soapaction))
      err_txt = service_processor.get_service_msg();
      RunError(err_txt, c_err_obj(err_txt,service_processor.get_service_code()) );
   end; 
   EndAction();

   /* Получение объекта ответа в том виде, в котором он пришел */
   if(not service_processor.m_make_resp_obj())
      RunError(service_processor.get_service_msg());
   end;

   /* Преобразование объектов ответа к ожидаемому формату */
   RespData = adp_Netting(service_processor.get_resp_obj());
   /* Преобразование объекта к виду, ожидаемому Системой */


   /* !!! Далее, в зависимости от способа вызова этого макроса, могут идти   !!! */
   /* !!! дальнейшие шаги для приведения полученного ответа к жалаемому виду !!! */

   /* Для понимания пришла ошибка или нормальный результат проверяем наличие параметра ReqID */
   if(ValType(uTryToGetPropS(RespData, "DealDate")) != V_UNDEF)
//      PrepObj = adp_RSCReqFindAccountsResponse(RespData);
      PrepObj = RespData;

      /*---------------------*/
      /* Anti-Zombie - begin */
      RespObj = c_NettingResponse();
      RespObj.PartyCode = PrepObj.PartyCode;
      RespObj.DealDate = PrepObj.DealDate;
      RespObj.Result.Code = PrepObj.Result.Code;
      RespObj.Result.Text = PrepObj.Result.Text;
      RespObj.SumList = TArray(PrepObj.SumList);
      RespObj.PaymentList = TArray(PrepObj.PaymentList);

      /* Anti-Zombie - end */
      /*-------------------*/
   else
      /* Сообщение с ошибкой обрабатывается выше */
      /* Неизвестный формат ответного сообщения */
      /*RespObj = RSCFaultResponse;
      RespObj.ReqID = REQUEST_ID;
      RespObj.faultType = UNIVERSAL_ERROR_TYPE;
      RespObj.faultCode = UNIVERSAL_ERROR_CODE;
      RespObj.faultString = "Запрос лимита сделок: неизвестный формат ответного сообщения";        */
   end;

//    msgbox("OK");

   return RespObj;

onError(err)
   EndAction();
   RespObj = c_NettingResponse();
   RespObj.PartyCode = NULL;
   RespObj.DealDate = NULL;
   RespObj.Result.Code = c_err_obj.obtain_err_code(err);
   RespObj.Result.Text = c_err_obj.obtain_err_msg(err);

   return RespObj
end; /* macro ExportNetting(ReqData) */

private class c_rec
   var ccy;
   var amount_sofr;
   var amount_kondor;
   var dealcode;
   var cutcode;
   var psign;
end;


macro CheckNetting(_Netting, _partyid, _repdate)
  var res = false;
  var err_txt;
  var temp_obj;
  var sql_str, rs, cmd;
  var vsign, vsum, vcur, visocur, vcode, vsign_dir, vdealcode;
  var sql_str_in, rs_in, cmd_in;
  var isfound;
  var rowid_str; 
  var cVariance;

  // перенос объекта во временную таблицу
  if (ValType(_Netting) != v_GenObj)
     err_txt = "Неверный тип входящего параметра, ожидается class";
     RunError(err_txt, c_err_obj(err_txt,-101));
  end;

  sql_str = "delete from ucheck_netting where t_contractor = :contractor and t_check_date = :check_date";
  cmd = RSDCommand(sql_str);
  cmd.Addparam("contractor", RSDBP_IN, _partyid);
  cmd.Addparam("check_date", RSDBP_IN, _repdate);
  cmd.execute;
  cmd.close;

  // список по суммам
  for (temp_obj,_Netting.sumlist)
    if (temp_obj.Amount < 0)
       vsign = "-";
       vsum = abs(temp_obj.Amount);
    else
       vsign = "+";
       vsum = temp_obj.Amount;
    end;

//для отладки                                        
//    if (temp_obj.Currency == "CHF") temp_obj.Currency = "SEK"; end;

    sql_str = "insert into ucheck_netting (t_check_date, t_contractor, t_ccy, t_psign, t_amount_sofr, t_amount_kondor, t_type) "+
              " values (:check_date, :contractor, :ccy, :psign, 0, :amount_kondor, :type)";
    cmd = RSDCommand(sql_str);
    cmd.Addparam("check_date", RSDBP_IN, _repdate);
    cmd.Addparam("contractor", RSDBP_IN, _partyid);
    cmd.Addparam("ccy", RSDBP_IN, temp_obj.Currency);
    cmd.Addparam("psign", RSDBP_IN, vsign);
    cmd.Addparam("amount_kondor", RSDBP_IN, vsum);
    cmd.Addparam("type", RSDBP_IN, "S");
    cmd.execute;
    cmd.close;
  end;

  // список по сделкам/платежам
  for (temp_obj,_Netting.PaymentList)
    if (StrUpr(temp_obj.Direction) == "BASE")
       vsign = "-";
    else
       vsign = "+";
    end;

//для отладки                                        
//    if (temp_obj.Currency == "CHF") temp_obj.Currency = "SEK"; end;

    sql_str = "insert into ucheck_netting (t_check_date, t_contractor, t_ccy, t_psign, t_amount_sofr, t_amount_kondor, t_sign_dir, t_cutcode, t_type) "+
              " values (:check_date, :contractor, :ccy, :psign, 0, :amount_kondor, :sign_dir, :cutcode, :type)";
    cmd = RSDCommand(sql_str);
    cmd.Addparam("check_date", RSDBP_IN, _repdate);
    cmd.Addparam("contractor", RSDBP_IN, _partyid);
    cmd.Addparam("ccy", RSDBP_IN, temp_obj.Currency);
    cmd.Addparam("psign", RSDBP_IN, vsign);
    cmd.Addparam("amount_kondor", RSDBP_IN, abs(temp_obj.Amount));
    cmd.Addparam("sign_dir", RSDBP_IN, temp_obj.Direction);
    cmd.Addparam("cutcode", RSDBP_IN, temp_obj.DealCode);
    cmd.Addparam("type", RSDBP_IN, "P");
    cmd.execute;
    cmd.close;
  end;

  //код валюты
  sql_str = "update ucheck_netting u "+
            "   set t_fiid = nvl((select t_fiid from dfininstr_dbt f "+
            "                      where f.t_ccy = u.t_ccy and f.t_fi_kind = 1),-1) "+
            " where t_fiid is null and t_ccy is not null ";
  cmd = RSDCommand(sql_str);
  cmd.execute;
  cmd.close;

  //заполнение данных СОФР
  // суммы
  sql_str = " select net.t_signingdate, net.t_dealnumber, net.t_nettingid, net.t_contractor, "+
            "          p.t_fiid, p.t_amount, p.t_payfiid, p.t_payamount, p.t_payer, p.t_receiver, "+
            "          decode(t_payer, t_contractor,'+', '-') psign,                              "+
            "          nvl((select t_ccy from dfininstr_dbt where t_fiid = p.t_fiid),'---') isocur "+
            "  from                                                                                "+
            "      ddl_nett_dbt net,                                                               "+
            "      dpmpaym_dbt p                                                                   "+
            "  where net.t_signingdate = :pdate and net.t_contractor = :party "+
            "      and p.t_dockind = net.t_dockind and p.t_documentid = net.t_nettingid and p.t_purpose = 39";
  cmd = RSDCommand(sql_str);
  cmd.AddParam("pdate", RSDBP_IN, _repdate);
  cmd.AddParam("party", RSDBP_IN, _partyid);
  rs = RSdRecordSet(cmd);
  while (rs.movenext)
    vcur = rs.value("t_fiid");
    vsum = rs.value("t_amount");
    visocur = rs.value("isocur");
    vsign = rs.value("psign");
    isfound = false;

    sql_str_in = "select rowid rowid_str from ucheck_netting "+
                 " where t_check_date = :dt and t_contractor = :party and t_fiid = :fiid and t_type = 'S'";
    cmd_in = RSDCommand(sql_str_in); 
    cmd_in.AddParam("dt", RSDBP_IN, _repdate);
    cmd_in.AddParam("party", RSDBP_IN, _partyid);
    cmd_in.AddParam("fiid", RSDBP_IN, vcur);
    rs_in = RSDRecordSet(cmd_in);
    if (rs_in.movenext)
      isfound = true;
      rowid_str = rs_in.value("rowid_str");
    end;
    rs_in.close; cmd_in.close; 
    if (isfound)
       sql_str_in = "update ucheck_netting set t_amount_sofr = :am where rowid = :r ";
       cmd_in = RSDCommand(sql_str_in);
       cmd_in.AddParam("am", RSDBP_IN, vsum);
       cmd_in.AddParam("r", RSDBP_IN, rowid_str);
       cmd_in.execute;
       cmd_in.close;
    else  // запись не найдена, добавляем новую
       sql_str_in = "insert into ucheck_netting (t_check_date, t_contractor, t_fiid, t_ccy, t_psign, t_amount_kondor, t_amount_sofr, t_type) "+
                    "values (:check_date, :contractor, :fiid, :ccy, :psign, 0, :amount_sofr, :type)";
       cmd_in = RSDCommand(sql_str_in);
       cmd_in.Addparam("check_date", RSDBP_IN, _repdate);
       cmd_in.Addparam("contractor", RSDBP_IN, _partyid);
       cmd_in.Addparam("fiid", RSDBP_IN, vcur);
       cmd_in.Addparam("ccy", RSDBP_IN, visocur);
       cmd_in.Addparam("psign", RSDBP_IN, vsign);
       cmd_in.Addparam("amount_sofr", RSDBP_IN, vsum);
       cmd_in.Addparam("type", RSDBP_IN, "S");
       cmd_in.execute;
       cmd_in.close;
    end;
  end;
  rs.close; cmd.close;

  // платежи
  sql_str = "select net.t_signingdate, net.t_dealnumber, net.t_nettingid, net.t_contractor, "+
            "       l.t_fiid, l.t_amount, l.t_initialpayment, l.t_ipsign, decode(l.t_ipsign,'-','Base','Contr') sign_dir, "+
            "       nvl((select t_ccy from dfininstr_dbt where t_fiid = p.t_fiid),'---') isocur, "+
            "       p.t_dockind, p.t_documentid, "+
            "       d.t_code, substr(d.t_code,instr(d.t_code,':')) cut_code "+
            "  from "+
            "      ddl_nett_dbt net, "+
            "      dpmlink_dbt l,    "+
            "      dpmpaym_dbt p,    "+
            "      ddvndeal_dbt d    "+
            " where net.t_signingdate = :pdate and net.t_contractor = :party         "+
            "   and l.t_dockind = net.t_dockind and l.t_documentid = net.t_nettingid "+
            "   and l.t_initialpayment = p.t_paymentid                               "+
            "   and d.t_dockind = p.t_dockind and d.t_id = p.t_documentid;           ";
  cmd = RSDCommand(sql_str);
  cmd.AddParam("pdate", RSDBP_IN, _repdate);
  cmd.AddParam("party", RSDBP_IN, _partyid);
  rs = RSdRecordSet(cmd);
  while (rs.movenext)
    isfound = false;
    vsign = rs.value("t_ipsign");
    vcode = rs.value("cut_code");
    vdealcode = rs.value("t_code");
    vcur = rs.value("t_fiid");
    visocur = rs.value("isocur");
    vsum = rs.value("t_amount");  
    vsign_dir = rs.value("sign_dir");  

    sql_str_in = "select rowid rowid_str from ucheck_netting "+
                 " where t_check_date = :dt and t_contractor = :party and t_fiid = :fiid and t_type = 'P' "+
                 "   and :code in (t_cutcode, t_dealcode) and t_psign = :psign";
    cmd_in = RSDCommand(sql_str_in); 
    cmd_in.AddParam("dt", RSDBP_IN, _repdate);
    cmd_in.AddParam("party", RSDBP_IN, _partyid);
    cmd_in.AddParam("fiid", RSDBP_IN, vcur);
    cmd_in.AddParam("code", RSDBP_IN, vdealcode);
    cmd_in.AddParam("psign", RSDBP_IN, vsign);

    rs_in = RSDRecordSet(cmd_in);
    if (rs_in.movenext)
      isfound = true;
      rowid_str = rs_in.value("rowid_str");
    end;
    rs_in.close; cmd_in.close; 
    if (isfound)
       sql_str_in = "update ucheck_netting set t_amount_sofr = :am, t_dealcode = :dealcode where rowid = :r ";
       cmd_in = RSDCommand(sql_str_in);
       cmd_in.AddParam("am", RSDBP_IN, vsum);
       cmd_in.AddParam("dealcode", RSDBP_IN, vdealcode);
       cmd_in.AddParam("r", RSDBP_IN, rowid_str);
       cmd_in.execute;
       cmd_in.close;
    else  // запись не найдена, добавляем новую
       sql_str_in = "insert into ucheck_netting (t_check_date, t_contractor, t_fiid, t_ccy, t_psign, "+
                    "                             t_amount_kondor, t_amount_sofr,  t_sign_dir, t_dealcode, t_cutcode, t_type) "+
                    " values (:check_date, :contractor, :fiid, :ccy, :psign, 0, :amount_sofr, :sign_dir, :dealcode, :cutcode, :type)";
       cmd_in = RSDCommand(sql_str_in);
       cmd_in.Addparam("check_date", RSDBP_IN, _repdate);
       cmd_in.Addparam("contractor", RSDBP_IN, _partyid);
       cmd_in.Addparam("fiid", RSDBP_IN, vcur);
       cmd_in.Addparam("ccy", RSDBP_IN, visocur);
       cmd_in.Addparam("psign", RSDBP_IN, vsign);
       cmd_in.Addparam("amount_sofr", RSDBP_IN, vsum);
       cmd_in.Addparam("sign_dir", RSDBP_IN, vsign_dir);
       cmd_in.Addparam("dealcode", RSDBP_IN, vdealcode);
       cmd_in.Addparam("cutcode", RSDBP_IN, vcode);
       cmd_in.Addparam("type", RSDBP_IN, "P");
       cmd_in.execute;
       cmd_in.close;
    end;
    
  end;
         
  // считаем расхождения
  ar_variance_s.size = 0;
  ar_variance_p.size = 0;
  cVariance = null;

  // сначала проверка сумм
  sql_str = "select * from ucheck_netting f "+
            " where t_check_date = :check_date and t_contractor = :contractor and t_type = 'S'"+
            "   and round(t_amount_sofr,4) - round(t_amount_kondor,4) <> 0 ";/*CHVA 502600*/
  cmd = RSDCommand(sql_str);
  cmd.AddParam("pdate", RSDBP_IN, _repdate);
  cmd.AddParam("party", RSDBP_IN, _partyid);
  rs = RSdRecordSet(cmd);
  while (rs.movenext)
    cVariance = c_rec;
    cVariance.ccy           = rs.value("t_ccy");
    cVariance.amount_sofr   = rs.value("t_amount_sofr");
    cVariance.amount_kondor = rs.value("t_amount_kondor");
    ar_variance_s.value(ar_variance_s.size) = cVariance;
    cVariance = NULL;
  end;
  rs.close; cmd.close;
  if (ar_variance_s.size > 0)  // расхождения найдены
     // проверка платежей
     sql_str = "select * from ucheck_netting f "+
               " where t_check_date = :check_date and t_contractor = :contractor and t_type = 'P' "+
               "   and round(t_amount_sofr,4) - round(t_amount_kondor,4) <> 0 ";/*CHVA 502600*/
     cmd = RSDCommand(sql_str);
     cmd.AddParam("pdate", RSDBP_IN, _repdate);
     cmd.AddParam("party", RSDBP_IN, _partyid);
     rs = RSdRecordSet(cmd);
     while (rs.movenext)
        cVariance = c_rec;
        cVariance.ccy           = rs.value("t_ccy");
        cVariance.amount_sofr   = rs.value("t_amount_sofr");
        cVariance.amount_kondor = rs.value("t_amount_kondor");
        cVariance.dealcode      = rs.value("t_dealcode");
        cVariance.cutcode       = rs.value("t_cutcode");
        cVariance.psign         = rs.value("t_psign");
        ar_variance_p.value(ar_variance_p.size) = cVariance;
        cVariance = NULL;
     end;
     rs.close; cmd.close;
     res = false;
  else
     res = true;
  end;
            
  return res;
end; /*CheckNetting*/

//вывод отчета
macro print_header(_type)
 
  var EXCEL_MONEY = "\"##0,0000;-##0,0000;##0,0000\"";
  Rep.SetExecute("iBook.Sheets(1).Columns(1).NumberFormat = "+EXCEL_MONEY+";");
  Rep.SetExecute("iBook.Sheets(1).Columns(2).NumberFormat = "+EXCEL_MONEY+";");
  Rep.SetExecute("iBook.Sheets(1).Columns(3).NumberFormat = "+EXCEL_MONEY+";");
 
  Rep.AddEmptyStr();

  Rep.AddPrintCell("СОФР",                  14, null, "c:ex_FS(b):ex_B(tblr)");
  Rep.AddPrintCell("АСУДР",                 14, null, "c:ex_FS(b):ex_B(tblr)");
  Rep.AddPrintCell("Расхождение",           14, null, "c:ex_FS(b):ex_B(tblr)");
  Rep.AddPrintCell("Валюта",                 3, null, "c:ex_FS(b):ex_B(tblr)");
  if (_type == "P")
     Rep.AddPrintCell("Сделка",     10, null, "c:ex_FS(b):ex_B(tblr)");
     Rep.AddPrintCell("Направление", 5, null, "c:ex_FS(b):ex_B(tblr)");
  end;
  
  Rep.AddStr();
end;

macro PrintStr(type, amount_sofr, amount_kondor, cur, deal, psign)
 
  Rep.AddPrintCell(amount_sofr,               14, null, "r:ex_FS():ex_B(tblr)");
  Rep.AddPrintCell(amount_kondor,             14, null, "r:ex_FS():ex_B(tblr)");
  Rep.AddPrintCell(amount_sofr-amount_kondor, 14, null, "r:ex_FS():ex_B(tblr)");
  Rep.AddPrintCell(cur,                        3, null, "c:ex_FS():ex_B(tblr)");
  if (type == "P")
    Rep.AddPrintCell(deal,       10, null, "c:ex_FS():ex_B(tblr)");
    Rep.AddPrintCell(psign,       5, null, "c:ex_FS():ex_B(tblr)");
  end;
  Rep.AddStr();
  
end;


macro PrintResultRep(_partyid, _repdate)

  var contr =  GetPartyShortName_integr( _partyid );
  var header_len = 55;
  var temp_v;
  var dcode;

  Rep = CMakeReport("┌┬┬┬┬┐");
  Rep.SetDefaultFontName("Times New Roman");
  Rep.SetDefaultFontSize(12);
  Rep.SetFlagShowGrid(true);

  Rep.SetExecute("iBook.Sheets(1).Rows(1).WrapText = false;");
  Rep.AddEmptyStr();
  Rep.AddPrintCell("Неттинг. Расхождения по контрагенту " + contr + " за дату " + _repdate, header_len, null, "c:ex_FS(b):ex_B()");
  Rep.AddStr();

  Rep.AddEmptyStr();
  Rep.AddPrintCell("Расхождения по суммам ", header_len, null, "c:ex_FS(b):ex_B()");
  Rep.AddStr();

  print_header("S");
  for (temp_v, ar_variance_s)
    PrintStr("S", temp_v.amount_sofr, temp_v.amount_kondor, temp_v.ccy);
  end;

  Rep.AddEmptyStr();
  Rep.AddPrintCell("Расхождения по платежам ", header_len, null, "c:ex_FS(b):ex_B()");
  Rep.AddStr();

  print_header("P");
  for (temp_v, ar_variance_p)
    if ( (ValType(temp_v.dealcode) != V_UNDEF) and (ValType(temp_v.dealcode) != 26) )
      dcode = temp_v.dealcode;
    elif ( (ValType(temp_v.cutcode) != V_UNDEF) and (ValType(temp_v.cutcode) != 26) )
      dcode = temp_v.cutcode;
    end;
    PrintStr("P", temp_v.amount_sofr, temp_v.amount_kondor, temp_v.ccy, dcode, temp_v.psign);
  end;

  Rep.PrintWinRep("Неттинг");
  Rep.SetZoomType(ZOOM_TYPE_A4L);
  Rep.SetWinRepOutput();
  Rep.ShowWinRep();

end;

macro MakeChoise_for_error(p_mess, p_text, p_res:@string, p_doit:@bool)
  private var v_acc_trn_menu = TArray;
  private var v_choice;
  private var v_res;
  private var v_doit;

  v_acc_trn_menu.value(0) = "Повторить запрос в АСУДР";
  v_acc_trn_menu.value(1) = "Приостановить выполнение текущего шага проверки неттинга";
  v_acc_trn_menu.value(2) = "Продолжить исполнение";

  v_choice = menu(v_acc_trn_menu, p_mess, p_text);
  if(v_choice == 0)
    v_res = "ERROR";      // ошибка
    v_doit = true; // повторить
  elif(v_choice == 1)
    v_res = "STOP";        // ошибка, но от продолжения отказались
    v_doit = false; // закончить
  elif (v_choice == 2)
    v_res = "IGNORE";        // игнорировать результаты
    v_doit = false; // закончить
  else
    v_res = "CANCEL";        // отказались от выбора
    v_doit = false; // закончить
  end;

  p_res = v_res;
  p_doit = v_doit;

end;


/*---------------------------------------*/
/*              Точка входа              */
/*---------------------------------------*/
/* ReqData - данные для отправки         */
/*---------------------------------------*/
macro GetNetting(_doit, _partyid, _repdate)
  var ReqData;
  var AskNetting;
  var stat = false;
  var res = "ERROR"; // пессимистично
  private var v_acc_trn_menu = TArray;
  private var v_choice;
  private var v_doit = _doit;

  v_acc_trn_menu.value(0) = "Повторить запрос в АСУДР";
  v_acc_trn_menu.value(1) = "Приостановить выполнение текущего шага проверки неттинга";
  v_acc_trn_menu.value(2) = "Продолжить исполнение";


  while (v_doit)
    ReqData = CreateObjectForExport( _partyid, _repdate );
    if (ValType(ReqData) == V_GenObj)
      AskNetting = ExportNetting(ReqData);
    end;
    if (Valtype(AskNetting) == V_GenObj)
      if (StrUpr(AskNetting.Result.Code) == "OK")
         stat = CheckNetting(AskNetting, _partyid, _repdate);
         if (stat) 
           v_doit = false; // закончить
           res = "OK";
         else   // есть расхождения
           PrintResultRep(_partyid, _repdate);
           MakeChoise_for_error("Найдены расхожения. Результаты в отдельном окне", "Найдены рахожения. Выберите действие", @res, @v_doit);
         end;
      else
         MakeChoise_for_error("Ответ получен", "Ответ от АСУДР: "+AskNetting.Result.Text+". Выберите действие", @res, @v_doit);
      end;
    else
      MakeChoise_for_error("Ответ не получен", "Произошла ошибка при разборе ответа. Выберите действие", @res, @v_doit);
    end;
  end;
  return res;
end;

//var ccf = GetNetting( true, 810, date(26,7,2019) );
var ccf = GetNetting( true, 1181, date(10,1,2020) );
msgbox(ccf);
