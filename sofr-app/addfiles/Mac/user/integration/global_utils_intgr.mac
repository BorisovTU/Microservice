/*------------------------------------------*/
/* Интеграция. Функционал общего назначения */
/*                                          */
/* Автор: Карпов В.                         */
/*------------------------------------------*/

import rsd;
import serviceaction; /* Для GetObjProps() */
import globals; /* Для {oper} */
//import BankInter; /* Для GetRegistryValue() */ /* Содержится в globals.mac */
//import email_notifyfun; /* Функционал работы с e-mail уведомлениями */
import Календарь;
import CTInter; /* kva: 20190927 - для работы с категориями */


const UNIVERSAL_ERROR_TYPE = 5;
const UNIVERSAL_ERROR_CODE = -10001;
const GC_ADD_DEV_ERR_CODE  = -10101;
const InErrComPart = "Не задан обязательный параметр ";
const InErrNotObj = "Нарушена структура данных: ";

const CV_TYPE_CODE_TMP_PWD = 1; /* Тип кода для хранения значения в таблице - временный пароль */

const CV_CATEGORY_UNLOAD_BROKER = 170;
const CV_CATEGORY_UNLOAD_QUIK = 171;  

const CV_CATEGORY_UNLOAD_OK = 1;
const CV_CATEGORY_UNLOAD_ERROR = 2;
const CV_CATEGORY_UNLOAD_CLOSE = 3;
const CV_CATEGORY_UNLOAD_CLOSE_ERROR = 4;

const CV_CONTACT_KIND_PHONE = 1;  // телефон
const CV_CONTACT_TYPE_MOBILE = 1004; // мобильный теперь Договор БО
const CV_CONTACT_KIND_EMAIL = 5;  // электронная почта
const CV_CONTACT_TYPE_REPORT = 1005; // "для отчетов" теперь Договор БО

const CV_CONTRACT_NOTEKIND_UPDATE = 170;

/*------------------------------------------------------------------------------*/
/* Базовый класс для инициализации свойств из строки с ; в качестве разделителя */
/*------------------------------------------------------------------------------*/
class с_init_obj_param(p_str_param : string)

   private macro m_init_с_init_obj_param(i_str_param)
      private const StrSeparator : string = ";";
      private var StrParamBuf : string = "",
                  i : integer = 0,
                  StrPos : integer = 0;

      if((Valtype(i_str_param) != V_UNDEF) and (Valtype(i_str_param) != 26) and (i_str_param != ""))
         StrParamBuf = i_str_param;

         i = 0;
         while((StrLen(StrParamBuf) > 0) and (GenNumProps(this) > i)) // Заполняем свойства
            StrPos = Index(StrParamBuf, StrSeparator);

            if(StrPos > 1)
               this[i] =  Substr(StrParamBuf, 1, StrPos - 1);
               StrParamBuf = Substr(StrParamBuf, StrPos + 1);
            elif((StrPos == 0) and (StrLen(StrParamBuf) > 0))
               this[i] = StrParamBuf;
               StrParamBuf = "";
            end;

            i = i + 1;
         end;
      end;

   onError(err)
      private var j = 0;
      while(GenNumProps(this) > j)
         this[j] = NULL;

         j = j + 1;
      end;
   end;

   m_init_с_init_obj_param(p_str_param);

end; /* class с_init_obj_param() */


/*---------------------------------------*/
/* Класс с основными параметрами события */
/*---------------------------------------*/
class c_event_main_prm()
   var v_func_res   /* Результат работы функции получения параметров события */
      ,v_func_msg   /* Описание кода возврата */
      ,v_recid
      ,v_objecttype
      ,v_objectid
      ,v_type
      ,v_status;
end; /* class c_event_main_prm() */


/*------------------------------------------------------*/
/* Функция получения значения свойства заданного класса */
/* Если передана пустая строка, то функция вернет NULL  */
/*------------------------------------------------------*/
macro uTryToGetPropS(obj:object, propName:string)
   var ret;
   if (GenPropID(obj,propName) == -1)
      return NULL;
   end;
   ret = genGetProp(obj, propName);
   if(ValType(ret) == V_STRING)
      if(StrLen(ret) == 0)
         ret = NULL;
      end;
   elif(ValType(ret) == V_DATE)
      if(ret == date(0, 0, 0))
         ret = NULL;
      end;
   end;
   return ret;

   onError
      return NULL
end;


/*------------------------------------------*/
/* Класс для хранения полного текста ошибки */
/*------------------------------------------*/
class c_usr_err_obj(p_err_msg, p_err_code)
   private var v_prop_list = TArray();
   private var v_err_code = "";
   var v_err_msg = "";

   /* Конструктор */
   private macro init_err_obj_params(p_err_msg, p_err_code)
      if(ValType(p_err_msg) != V_UNDEF)
         v_err_msg = string(p_err_msg);
      end;
      if(ValType(p_err_code) != V_UNDEF)
         v_err_code = string(p_err_code);
      end;
   end;

   /* Поиск свойства с заданным именем в заданном объекте */
   private macro try_to_find_prop(p_err_obj, p_prop_name, p_use_exist)
      var v_ret = false;
      var v_cntr_list = 0;

      if((ValType(p_use_exist) == V_UNDEF))
         v_prop_list.size = 0;
         v_prop_list = TArray(GetObjProps(p_err_obj, true));
      else
         if(not p_use_exist)
            v_prop_list.size = 0;
            v_prop_list = TArray(GetObjProps(p_err_obj, true));
         end;
      end;

      while((not v_ret) and (v_prop_list.size > v_cntr_list))
         if(v_prop_list.value(v_cntr_list) == p_prop_name)
            v_ret = true;
         end;
         v_cntr_list = v_cntr_list + 1;
      end;

      if(ValType(p_use_exist) == V_UNDEF)
         v_prop_list.size = 0;
      end;

      return v_ret;
   end;

   /* Получить v_err_msg */
   macro get_err_msg()
      return v_err_msg;
   end;

   /* Получить v_err_code */
   macro get_err_code()
      return v_err_code;
   end;

   /* Получить служебное сообщение из объекта ошибки */
   macro obtain_err_msg(p_err_obj)
      private var actx_code, actx_text, actx_msg;
      private var v_err_obj = NULL;

      /* Первый вызов try_to_find_prop() собирает массив свойств */
      if(try_to_find_prop(p_err_obj, "AxCode", false))
         actx_code = uTryToGetPropS(p_err_obj, "AxCode");
      else
         actx_code = 0;
      end;
      if(try_to_find_prop(p_err_obj, "Message", true))
         actx_text = uTryToGetPropS(p_err_obj, "Message");
      else
         actx_text = "0";
      end;
      if(try_to_find_prop(p_err_obj, "AxMes", true))
         actx_msg = uTryToGetPropS(p_err_obj, "AxMes");
      else
         actx_msg = "0";
      end;
      if(actx_code == 0)
         if(try_to_find_prop(p_err_obj, "Err", true))
            v_err_obj = uTryToGetPropS(p_err_obj, "Err");
            if(ValType(v_err_obj) == V_GENOBJ)
               if(try_to_find_prop(v_err_obj, "v_err_msg"))
                  v_err_msg = uTryToGetPropS(v_err_obj, "v_err_msg");
                  if(ValType(v_err_msg) == V_UNDEF)
                     v_err_msg = actx_text;
                  end;
               else
                  v_err_msg = actx_text;
               end;
            else
               v_err_msg = actx_text;
            end;
         else
            v_err_msg = actx_text;
         end;
      else
         v_err_msg = string(actx_text, ": Code: ", actx_code, "; Text: ", actx_msg);
      end;

      return v_err_msg;
   end;

   /* Получить код ошибки */
   macro obtain_err_code(p_err_obj, p_def_code)
      private var v_err_obj = NULL;

      /* При каждом вызове try_to_find_prop() массив свойств пересобирается */
      /* Если обрабатывается объект этого класса */
      if(try_to_find_prop(p_err_obj, "Err"))
         v_err_obj = uTryToGetPropS(p_err_obj, "Err");
         if(ValType(v_err_obj) == V_GENOBJ)
            if(try_to_find_prop(v_err_obj, "v_err_msg"))
               v_err_code = p_err_obj.err.get_err_code();
            end;
         end;
      end;

      /* Остальные объекты */
      if(strlen(v_err_code) == 0)
         if(ValType(p_def_code) != V_UNDEF)
            v_err_code = p_def_code;
         else
            if(try_to_find_prop(p_err_obj, "Code"))
               v_err_code = uTryToGetPropS(p_err_obj, "Code");
               if(ValType(v_err_code) == V_UNDEF)
                  v_err_code = UNIVERSAL_ERROR_CODE;
               end;
            else
               v_err_code = UNIVERSAL_ERROR_CODE;
            end;
         end;
      end;

      return v_err_code;
   end;

   macro obtain_err_axcode_exactly(p_err_obj, p_def_val)
      private var v_err_axcode;

      if(try_to_find_prop(p_err_obj, "AxCode"))
         v_err_axcode = uTryToGetPropS(p_err_obj, "AxCode");
      else
         if(ValType(p_def_val) == V_UNDEF)
            v_err_axcode = 0;
         else
            v_err_axcode = p_def_val;
         end;
      end;

      return v_err_axcode;
   end;

   macro obtain_err_message_exactly(p_err_obj, p_def_val)
      private var v_err_message;

      if(try_to_find_prop(p_err_obj, "Message"))
         v_err_message = uTryToGetPropS(p_err_obj, "Message");
      else
         if(ValType(p_def_val) == V_UNDEF)
            v_err_message = "0";
         else
            v_err_message = p_def_val;
         end;
      end;

      return v_err_message;
   end;

   macro obtain_err_axmes_exactly(p_err_obj, p_def_val)
      private var v_err_axmes;

      if(try_to_find_prop(p_err_obj, "AxMes"))
         v_err_axmes = uTryToGetPropS(p_err_obj, "AxMes");
      else
         if(ValType(p_def_val) == V_UNDEF)
            v_err_axmes = "0";
         else
            v_err_axmes = p_def_val;
         end;
      end;

      return v_err_axmes;
   end;

   init_err_obj_params(p_err_msg, p_err_code);
end; /* class c_usr_err_obj() */


/*-------------------------------------------------*/
/* Вставка записи в таблицу utableprocessevent_dbt */
/*-------------------------------------------------*/
/* Для p_event_type и p_event_stat предусмотрены   */
/* значения по умолчанию                           */
/*-------------------------------------------------*/
macro m_make_event_to_process(p_objecttype, p_objectid, p_event_type, p_event_stat, p_param)
   const C_EVENT_TYPE = 1;
   const C_EVENT_STAT = 1;

   var v_ret = true;
   var v_sql, v_cmd;

   if((ValType(p_objecttype) == V_UNDEF) or (ValType(p_objectid) == V_UNDEF))
      v_ret = false;
   end;

   if(v_ret)
      if(ValType(p_event_type) == V_UNDEF)
         p_event_type = C_EVENT_TYPE;
      end;

      if(ValType(p_event_stat) == V_UNDEF)
         p_event_stat = C_EVENT_STAT;
      end;

      if(ValType(p_param) == V_UNDEF)
         p_param = "";
      end;

      v_sql =         "INSERT INTO utableprocessevent_dbt ";
      v_sql = v_sql + "            (t_timestamp, t_objecttype, t_objectid, t_type, t_status, t_param) ";
      v_sql = v_sql + "     VALUES (SYSDATE, :p_objecttype, :p_objectid, :p_event_type, :p_event_stat, :p_param) ";

      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_objecttype", RSDBP_IN, p_objecttype);
      v_cmd.addParam("p_objectid", RSDBP_IN, p_objectid);
      v_cmd.addParam("p_event_type", RSDBP_IN, p_event_type);
      v_cmd.addParam("p_event_stat", RSDBP_IN, p_event_stat);
      v_cmd.addParam("p_param", RSDBP_IN, p_param);
      v_cmd.execute();

      v_cmd.close(); v_cmd = NULL; v_sql = NULL;
   end;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end; /* macro m_make_event_to_process() */


//текст ошибки RS Bank
macro GetErrMessage(err_code)
  var err_msg = "";
  
  var cmd = RsdCommand(
      "select t_Contents from dbank_msg "
      "where t_Number = ? "
  );
  cmd.addParam("Number", RSDBP_IN, err_code);
  cmd.execute();

  var rs = RsdRecordset(cmd);
  if (rs.moveNext())
    err_msg = rs.value(0);
  end;

  return err_msg;
end;


// получение sql-ошибки. 
// использовать в onError(e) так:  RunError(e.message + getFullCmdErrorText(cmd));
macro getFullCmdErrorText(_cmd:RSDCommand)
   var cmd = _cmd;
   var err_str = "", i = 0;

   while( i < cmd.connection.environment.ErrorCount)
      err_str = err_str + cmd.connection.environment.Error(i).Descr + " ";
      i = i+1;
   end;

   return trim(err_str);
end;


/*-------------------------------------------------*/
/* Обновление статуса записи в utableprocessin_dbt */
/*-------------------------------------------------*/
macro m_upd_table_proc_in_status(p_recid, p_status_new)
   var v_ret = true;
   var v_sql, v_cmd;

   v_sql =         "UPDATE utableprocessin_dbt ";
   v_sql = v_sql + "   SET t_status = :p_status_new ";
   v_sql = v_sql + " WHERE t_recid = :p_recid ";

   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_status_new", RSDBP_IN, p_status_new);
   v_cmd.addParam("p_recid", RSDBP_IN, p_recid);
   v_cmd.execute();

   v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end;


/*----------------------------------------------------*/
/* Обновление статуса записи в utableprocessevent_dbt */
/*----------------------------------------------------*/
macro m_upd_table_proc_evnt_status(p_objectid, p_objecttype, p_status, p_msg_id, p_status_old)
   var v_ret = true;
   var v_obj_tp_type, v_msg_id_type, v_stat_old_type;
   var v_sql, v_cmd;

   v_obj_tp_type = ValType(p_objecttype);
   v_msg_id_type = ValType(p_msg_id);
   v_stat_old_type = ValType(p_status_old);

   v_sql =         "UPDATE utableprocessevent_dbt ";
   v_sql = v_sql + "   SET t_status = :p_status ";
   if(v_msg_id_type != V_UNDEF)
      v_sql = v_sql + "   ,t_messageid = :p_msg_id ";
   end;
   if(v_obj_tp_type != V_UNDEF)
      v_sql = v_sql + " WHERE t_objecttype = :p_objecttype ";
      v_sql = v_sql + "   AND t_objectid = :p_objectid ";
   else
      v_sql = v_sql + " WHERE t_recid = :p_recid ";
   end;
   if(v_stat_old_type != V_UNDEF)
      v_sql = v_sql + "AND t_status = :p_status_old ";
   end;

   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_status", RSDBP_IN, p_status);
   if(v_msg_id_type != V_UNDEF)
      v_cmd.addParam("p_msg_id", RSDBP_IN, p_msg_id);
   end;
   if(v_obj_tp_type != V_UNDEF)
      v_cmd.addParam("p_objecttype", RSDBP_IN, p_objecttype);
      v_cmd.addParam("p_objectid", RSDBP_IN, p_objectid);
   else
      v_cmd.addParam("p_recid", RSDBP_IN, p_objectid);
   end;
   if(v_stat_old_type != V_UNDEF)
      v_cmd.addParam("p_status_old", RSDBP_IN, p_status_old);
   end;
   v_cmd.execute();

   v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end; /* m_upd_table_proc_evnt_status() */


/*------------------------------------------------*/
/* Добавление новой строки к строковой переменной */
/*------------------------------------------------*/
macro m_add_note_to_str(p_new_note, p_src_str)
   var v_ret;

   if(strlen(p_src_str) != 0)
      v_ret = string(p_src_str, "\n", p_new_note);
   else
      v_ret = p_new_note;
   end;

   return v_ret;
end;


/*--------------------------------------*/
/* Получить параметры исходного события */
/*--------------------------------------*/
macro m_get_source_event(p_jms_msg_id)
   const CV_QNAME_IN = "ESB_TO_SOFR";

   var v_ret = c_event_main_prm();
   var v_sql, v_cmd;

   v_ret.v_func_res = 0;
   v_ret.v_func_msg = "";

   v_sql =         "DECLARE ";

   v_sql = v_sql + "    v_ret              NUMBER(10) := 0; ";
   v_sql = v_sql + "    v_ret_txt          usr_exchng_log_dbt.t_error_msg%TYPE := 'OK'; ";
   v_sql = v_sql + "    v_queue_qname_in   dqueue_log_dbt.t_qname%TYPE := :p_qname_in; ";
   v_sql = v_sql + "    v_aux_buf          NUMBER(10); ";
   v_sql = v_sql + "    v_jmsmessageid     dqueue_log_dbt.t_jmsmessageid%TYPE := :p_jmsmessageid; ";
   v_sql = v_sql + "    v_jmsmessageguid   dqueue_log_dbt.t_jmsmessageguid%TYPE; ";
   v_sql = v_sql + "    v_jmscorrelationid dqueue_log_dbt.t_jmscorrelationid%TYPE; ";
   v_sql = v_sql + "    v_log_link_id      dqueue_log_dbt.t_jmscorrelationid%TYPE; ";
   v_sql = v_sql + "    v_recid_log        utableprocessevent_dbt.t_recid%TYPE; ";
   v_sql = v_sql + "    v_recid            utableprocessevent_dbt.t_recid%TYPE := 0; ";
   v_sql = v_sql + "    v_objecttype       utableprocessevent_dbt.t_objecttype%TYPE := 0; ";
   v_sql = v_sql + "    v_objectid         utableprocessevent_dbt.t_objectid%TYPE := 0; ";
   v_sql = v_sql + "    v_type             utableprocessevent_dbt.t_type%TYPE := 0; ";
   v_sql = v_sql + "    v_status           utableprocessevent_dbt.t_status%TYPE := 0; ";
   v_sql = v_sql + "BEGIN ";
   v_sql = v_sql + "    BEGIN ";
   v_sql = v_sql + "        v_ret := 1; ";
   v_sql = v_sql + "        SELECT t_jmscorrelationid, t_jmsmessageguid ";
   v_sql = v_sql + "          INTO v_jmscorrelationid, v_jmsmessageguid ";
   v_sql = v_sql + "          FROM dqueue_log_dbt ";
   v_sql = v_sql + "         WHERE t_jmsmessageid = v_jmsmessageid ";
   v_sql = v_sql + "           AND t_qname = v_queue_qname_in; ";
/*
   v_sql = v_sql + "        v_ret := 2; ";
   v_sql = v_sql + "        SELECT t_jmscorrelationid INTO v_jmscorrelationid ";
   v_sql = v_sql + "          FROM dqueue_log_dbt ";
   v_sql = v_sql + "         WHERE t_jmsmessageguid = v_jmsmessageguid";
   v_sql = v_sql + "           AND t_jmscorrelationid <> chr(1) ";
   v_sql = v_sql + "           AND t_qname = 'ESB_TO_SOFR'; ";
*/
   v_sql = v_sql + "        v_ret := 3; ";
   v_sql = v_sql + "        SELECT t_jmscorrelationid INTO v_log_link_id ";
   v_sql = v_sql + "          FROM dqueue_log_dbt ";
   v_sql = v_sql + "         WHERE t_jmsmessageid = v_jmscorrelationid; ";

   v_sql = v_sql + "        v_aux_buf := INSTR(v_log_link_id, '-'); ";
   v_sql = v_sql + "        IF v_aux_buf = 0 ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            v_recid_log := TO_NUMBER(v_aux_buf); ";
   v_sql = v_sql + "        ELSE ";
   v_sql = v_sql + "            v_recid_log := TO_NUMBER(SUBSTR(v_log_link_id, 1, (v_aux_buf - 1))); ";
   v_sql = v_sql + "        END IF; ";

   v_sql = v_sql + "        v_ret := 4; ";
   v_sql = v_sql + "        SELECT t_recid, t_objecttype, t_objectid, t_type, t_status ";
   v_sql = v_sql + "          INTO v_recid, v_objecttype, v_objectid, v_type, v_status ";
   v_sql = v_sql + "          FROM utableprocessevent_dbt ";
   v_sql = v_sql + "         WHERE t_recid = v_recid_log; ";

   v_sql = v_sql + "        v_ret := 0; ";

   v_sql = v_sql + "    EXCEPTION ";
   v_sql = v_sql + "        WHEN NO_DATA_FOUND ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            v_ret := v_ret; ";
   v_sql = v_sql + "            v_ret_txt := 'NO_DATA_FOUND'; ";
   v_sql = v_sql + "        WHEN OTHERS ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            v_ret := SQLCODE; ";
   v_sql = v_sql + "            v_ret_txt := SUBSTR(SQLERRM, 1, 1500); ";
   v_sql = v_sql + "    END; ";

   v_sql = v_sql + "    :p_recid := v_recid; ";
   v_sql = v_sql + "    :p_objecttype := v_objecttype; ";
   v_sql = v_sql + "    :p_objectid := v_objectid; ";
   v_sql = v_sql + "    :p_type := v_type; ";
   v_sql = v_sql + "    :p_status := v_status; ";
   v_sql = v_sql + "    :p_ret := v_ret; ";
   v_sql = v_sql + "    :p_ret_txt := v_ret_txt; ";
   v_sql = v_sql + "END; ";

   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_qname_in", RSDBP_IN, CV_QNAME_IN);
   v_cmd.addParam("p_jmsmessageid", RSDBP_IN, p_jms_msg_id);
   v_cmd.addParam("p_recid", RSDBP_OUT, V_INTEGER);
   v_cmd.addParam("p_objecttype", RSDBP_OUT, V_INTEGER);
   v_cmd.addParam("p_objectid", RSDBP_OUT, V_INTEGER);
   v_cmd.addParam("p_type", RSDBP_OUT, V_INTEGER);
   v_cmd.addParam("p_status", RSDBP_OUT, V_INTEGER);
   v_cmd.addParam("p_ret", RSDBP_OUT, V_INTEGER);
   v_cmd.addParam("p_ret_txt", RSDBP_OUT, V_STRING, 2000);
   v_cmd.execute();

   v_ret.v_func_res   = v_cmd.value("p_ret");
   v_ret.v_func_msg   = v_cmd.value("p_ret_txt");
   v_ret.v_recid      = v_cmd.value("p_recid");
   v_ret.v_objecttype = v_cmd.value("p_objecttype");
   v_ret.v_objectid   = v_cmd.value("p_objectid");
   v_ret.v_type       = v_cmd.value("p_type");
   v_ret.v_status     = v_cmd.value("p_status");
   v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret.v_func_res = 7; /* Иные проблемы */
   return v_ret;
end; /* m_get_source_event() */


/* Получить дату первого рабочего дня, отстающего от даты на заданное количество рабочих дней */
macro m_get_work_date_delta_before(p_set_date, p_delta)
   var v_ret;

   v_ret = p_set_date - p_delta;
   while(p_set_date < DateAfterWorkDays(v_ret, p_delta))
      v_ret = v_ret - 1;
   end;

   return v_ret;
end; /* macro m_get_work_date_delta_before() */


/* Получение кодового слова / временного пароля по partyid */
/* p_type: 1 - временный пароль, 2 - кодовое слово*/ 
macro m_GetPasswordForClient(p_client_id, p_type)
   var sql_str, rs, cmd;
   var res = "";
   
   sql_str = "select t_code_word "+
             "  FROM usr_clnt_cdwrd_buf_dbt "+
             " WHERE t_cw_client = :p_client_id "+
             "   AND t_cw_type = :p_type "; 
   cmd = RSDCommand(sql_str);
   cmd.AddParam("p_client_id", RSDBP_IN, p_client_id);
   cmd.AddParam("p_type", RSDBP_IN, p_type);
   rs = RSDRecordSet(cmd);

   if (rs.movenext)  
      res = rs.value(0);
   end;
   
   return res;
end;


/*-------------------------------------*/
/* Получить значение категории объекта */
/*-------------------------------------*/
macro m_get_obj_categ_value(p_object_type, p_object_id, p_categ_id, p_ret_type, p_date_categ, p_time_categ)
   private const CV_RET_TYPE_NUMINLIST = 1;
   private const CV_RET_TYPE_NAME = 2;
   private const CV_SUBJ_ECON = 3;
   private const CV_BROKER_SERV_CONTR = 207; /* в p_object_id должен быть передан dllcontr_dbt.t_dlcontrid */
   private const CV_NO_VALUE_WAS_FOUND = 4; /* Возвращаемое значение "Не найдено записи по заданным параметрам" */

   var v_ret;
   var v_ret_type;
   var v_err_code;
   var v_object_id;
   var v_obj_categ;
   var v_service_msg = "";
   var v_date_cat_dt, v_time_cat_dt;
   var v_date_cat_tm, v_time_cat_tm;

   private file fl_attr("objattr.dbt") key 0;
   private file fl_attrcor("objatcor.dbt") key 0;

   /* Определяем тип возвращаемого значения */
   if(ValType(p_ret_type) == V_UNDEF)
      v_ret_type = CV_RET_TYPE_NUMINLIST;
   else
      v_ret_type = p_ret_type;
   end;

   /* При необходимости преобразуем идентификатор объекта */
   if(p_object_type == CV_BROKER_SERV_CONTR)
      v_object_id = string(p_object_id:o:34); /* dllcontr_dbt.t_dlcontrid */
   elif(p_object_type == CV_SUBJ_ECON)
      v_object_id = string(p_object_id:o:10);
   else
      v_object_id = string(p_object_id);
   end;

   v_obj_categ = RsbObjCategories(p_object_type, v_object_id);
   v_err_code = v_obj_categ.GetMainAttr(p_categ_id, date(), fl_attr, fl_attrcor);
   if(v_err_code == 0)
      if(v_ret_type == CV_RET_TYPE_NUMINLIST)
         v_ret = fl_attr.numinlist;
      else
         v_ret = fl_attr.name;
      end;
      /* Если значение найдено, то возвращаем дату его установки */
      v_date_cat_dt = NULL; v_time_cat_dt = NULL;
      dttmsplit(fl_attrcor.sysdate, v_date_cat_dt, v_time_cat_dt);
      setparm(4, v_date_cat_dt);
      v_date_cat_tm = NULL; v_time_cat_tm = NULL;
      dttmsplit(fl_attrcor.systime, v_date_cat_tm, v_time_cat_tm);
      setparm(5, v_time_cat_tm);

   elif(v_err_code == CV_NO_VALUE_WAS_FOUND)
      v_date_cat_dt = date(1,1,2) - 365; /* 01.01.0001 */
      v_time_cat_tm = time(0,0,0); /* 00:00:00 */
      setparm(4, v_date_cat_dt);
      setparm(5, v_time_cat_tm);
      /* Если не найдено значение, то возвращаем NULL */
      v_ret = NULL;
   else
      v_service_msg = string("Не удалось получить значение категории ",
                             p_categ_id,
                             " по объекту ",
                             p_object_id,
                             " (", p_object_type, ")");
      RunError(v_service_msg, c_usr_err_obj(v_service_msg));
   end;

   return v_ret;

onError(err)
   v_service_msg = string("Ошибка при получении значения категории ",
                          p_categ_id,
                          " по объекту ",
                          p_object_id,
                          " (", p_object_type, ")");
   RunError(v_service_msg, c_usr_err_obj(v_service_msg));
end; /* macro m_get_obj_categ_value() */


// проверка на наличие хотя бы 1 договора СКП ЭП
macro CheckCategory_SKP(p_client_id)
   var sql_str, rs, cmd;
   var contr;
   var res = false;
   
   sql_str = "SELECT cntr.t_id cntr_id, dl.t_dlcontrid dl_id, cntr.t_number, cntr.t_name, dl.t_iis "+
             " FROM dsfcontr_dbt cntr, "+
             "      ddlcontr_dbt dl "+
             "WHERE dl.t_sfcontrid = cntr.t_id "+
             "  AND cntr.t_partyid = :p_client_id "+
             "  AND cntr.t_dateclose = to_date('01.01.0001','dd.mm.yyyy') "+
             "  AND exists (select 1 from dobjatcor_dbt o where o.t_objecttype = 207 and o.t_groupid = 170 "+
             "                and o.t_attrid = 1 and (o.t_validtodate >= :dt or o.t_validtodate = to_date('01.01.0001','dd.mm.yyyy')) "+
             "                and lpad(dl.t_dlcontrid,34,'0') = o.t_object ) ";            
   cmd = RSDCommand(sql_str);
   cmd.AddParam("p_client_id", RSDBP_IN, p_client_id);
   cmd.AddParam("dt", RSDBP_IN, date());
   rs = RSDRecordSet(cmd);
   if (rs.movenext)
      res = true;
   end;
   cmd.close; rs.close; cmd = null; rs = null;

   return res;
end;

/*
  @brief get_Defined_StringField - получение не нулевого значения для вставки в БД
  @param[in] param - поле объекта 
  @return значение поля или chr(1)
*/
macro get_Defined_StringField(param)
  if (ValType(param) != V_UNDEF)
    return param;
  else
    return StrFor(1);                                            
  end;
end;



