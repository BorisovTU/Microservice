/*--------------------------------------*/
/* Интерфейс журнала выгрузок rshb_bag  */
/*                                      */
/* Автор: Гераськина Т.В.               */
/*--------------------------------------*/
import RsbFormsInter;
import "int_usr_globals.mac";

/*-----------------------*/
/* Глобальные переменные */
/*-----------------------*/
private var gv_bag_sel_prm;              /* Параметры выборки событий */

private var gv_column_list_scr = TArray(); /* Список колонок скроллинга */
private var gv_num_columns_scr = 0;        /* Количество колонок скроллинга */
private var gv_scr_focus = 0;              /* Номер позиции скроллинга */

private var gv_column_list_scr_main = TArray(); /* Список колонок скроллинга */
private var gv_num_columns_scr_main = 0;        /* Количество колонок скроллинга */
private var gv_scr_focus_main = 0;              /* Номер позиции скроллинга */


/*---------------------------*/
/* Параметры выборки событий */
/*---------------------------*/
private class c_bag_selection_prm()
   var v_com_date;
   var v_com_beg_date;
   var v_com_end_date;
   var v_com_date_switch;

   /* Значения по умолчанию */
   private macro m_init_bag_selection_prm()
      v_com_date = date();
      v_com_beg_date = date();
      v_com_end_date = date();
      v_com_date_switch = false;

   end; /* macro m_init_bag_selection_prm() */

   m_init_bag_selection_prm();
end; /* class c_bag_selection_prm() */


/*----------------------------------------*/
/* Класс формы общих поисковых параметров */
/*----------------------------------------*/
class (TRsbPanel) c_com_search_form()
   const CV_POS_X = 3;
   const CV_POS_Y = 2;
   var v_com_date_fld;
   var v_com_beg_date_fld;
   var v_com_end_date_fld;
   var v_com_date_switch_fld;

   var v_com_null_btn;
   var v_com_use_btn;

   macro m_evnt_on_key_pressed(p_rsb_evnt)
      if(p_rsb_evnt.KeyCode == K_F2)
         close(CV_CLOSE_FORM_OK);
      end;

      return true;
   end; /* macro m_evnt_on_key_pressed(p_rsb_evnt) */


   macro m_on_switch_date(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_CONTROL_CHECKED)
         gv_bag_sel_prm.v_com_date_switch = v_com_date_switch_fld.checked;                                                                         
      end;

      if(gv_bag_sel_prm.v_com_date_switch)
         v_com_date_fld.focusable = false;
         v_com_date_fld.enabled = false;
         v_com_date_fld.editable = false;

         v_com_beg_date_fld.focusable = true;
         v_com_beg_date_fld.enabled = true;
         v_com_beg_date_fld.editable = true;

         v_com_end_date_fld.focusable = true;
         v_com_end_date_fld.enabled = true;
         v_com_end_date_fld.editable = true;
      else
         v_com_date_fld.focusable = true;
         v_com_date_fld.enabled = true;
         v_com_date_fld.editable = true;

         v_com_beg_date_fld.focusable = false;
         v_com_beg_date_fld.enabled = false;
         v_com_beg_date_fld.editable = false;

         v_com_end_date_fld.focusable = false;
         v_com_end_date_fld.enabled = false;
         v_com_end_date_fld.editable = false;
      end;

      redraw();

      return true;
   end; /* macro m_on_switch_date() */

   macro m_bag_null_com_btn(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_BUTTON_CLICKED)
         gv_bag_sel_prm.v_com_date = date("00.00.0000");
         v_com_date_fld.value = date("00.00.0000");

         gv_bag_sel_prm.v_com_beg_date = date("00.00.0000");
         v_com_beg_date_fld.value = date("00.00.0000");

         gv_bag_sel_prm.v_com_end_date = date("00.00.0000");
         v_com_end_date_fld.value = date("00.00.0000");

         v_com_date_fld.focusable = true;
         v_com_date_fld.enabled = true;
         v_com_date_fld.editable = true;

         v_com_beg_date_fld.focusable = false;
         v_com_beg_date_fld.enabled = false;
         v_com_beg_date_fld.editable = false;

         v_com_end_date_fld.focusable = false;
         v_com_end_date_fld.enabled = false;
         v_com_end_date_fld.editable = false;

         v_com_date_switch_fld.checked = false;
         gv_bag_sel_prm.v_com_date_switch = false;

         redraw();
      end;

      return true;
   end; /* macro m_bag_null_com_btn() */

   macro m_bag_run_com_btn(p_rsb_evnt)
      if(p_rsb_evnt.id == RSB_EV_BUTTON_CLICKED)
         close(CV_CLOSE_FORM_OK);
      end;

      return true;
   end; /* macro m_bag_run_com_btn() */


   private macro m_init_com_search_form(i_this_obj)
      Caption = "Общие фильтры";
      Status = "[Esc] Выход  [F2] Применить";

      /* Фильтр по дате */
      AddLabel(TRsbLabel(CV_POS_X, CV_POS_Y, "Дата выборки"));
      v_com_date_fld = TRsbEditField(FT_DATE);
      v_com_date_fld.bindValue(gv_bag_sel_prm, "v_com_date", 10);
      v_com_date_fld.setPosition(CV_POS_X, CV_POS_Y+1);
      v_com_date_fld.setSize(10, 1);
      if(gv_bag_sel_prm.v_com_date_switch)
         v_com_date_fld.focusable = false;
         v_com_date_fld.enabled = false;
         v_com_date_fld.editable = false;
      else
         v_com_date_fld.focusable = true;
         v_com_date_fld.enabled = true;
         v_com_date_fld.editable = true;
      end;
      AddControl(v_com_date_fld);

      AddLabel(TRsbLabel(CV_POS_X+14, CV_POS_Y+1, "Диапазон"));
      v_com_date_switch_fld = TRsbCheckBox();
      v_com_date_switch_fld.setPosition(CV_POS_X+12, CV_POS_Y+1);
      v_com_date_switch_fld.onChecked(R2M(i_this_obj, "m_on_switch_date"));
      v_com_date_switch_fld.checked = gv_bag_sel_prm.v_com_date_switch;
      AddControl(v_com_date_switch_fld);

      AddLabel(TRsbLabel(CV_POS_X+22, CV_POS_Y, "Диапазон дат"));
      v_com_beg_date_fld = TRsbEditField(FT_DATE);
      v_com_beg_date_fld.bindValue(gv_bag_sel_prm, "v_com_beg_date", 10);
      v_com_beg_date_fld.setPosition(CV_POS_X+22, CV_POS_Y+1);
      v_com_beg_date_fld.setSize(10, 1);
      if(gv_bag_sel_prm.v_com_date_switch)
         v_com_beg_date_fld.focusable = true;
         v_com_beg_date_fld.enabled = true;
         v_com_beg_date_fld.editable = true;
      else
         v_com_beg_date_fld.focusable = false;
         v_com_beg_date_fld.enabled = false;
         v_com_beg_date_fld.editable = false;
      end;
      AddControl(v_com_beg_date_fld);
      v_com_end_date_fld = TRsbEditField(FT_DATE);
      v_com_end_date_fld.bindValue(gv_bag_sel_prm, "v_com_end_date", 10);
      v_com_end_date_fld.setPosition(CV_POS_X+22, CV_POS_Y+2);
      v_com_end_date_fld.setSize(10, 1);
      if(gv_bag_sel_prm.v_com_date_switch)
         v_com_end_date_fld.focusable = true;
         v_com_end_date_fld.enabled = true;
         v_com_end_date_fld.editable = true;
      else
         v_com_end_date_fld.focusable = false;
         v_com_end_date_fld.enabled = false;
         v_com_end_date_fld.editable = false;
      end;
      AddControl(v_com_end_date_fld);

      /* Кнопка запуска */
      v_com_use_btn = TRsbPushButton("Применить");
      v_com_use_btn.setPosition(CV_POS_X, (CV_POS_Y + 4));
      v_com_use_btn.setSize(8, 1);
      v_com_use_btn.onClicked(R2M(i_this_obj, "m_bag_run_com_btn"));
      AddControl(v_com_use_btn);

      /* Кнопка сброса */
      v_com_null_btn = TRsbPushButton("Сброс");
      v_com_null_btn.setPosition((CV_POS_X + 22), (CV_POS_Y + 4));
      v_com_null_btn.setSize(6, 1);
      v_com_null_btn.onClicked(R2M(i_this_obj, "m_bag_null_com_btn"));
      AddControl(v_com_null_btn);

      AddEventHandler(RSB_EV_KEY_PRESSED, R2M(i_this_obj, "m_evnt_on_key_pressed"));

      setSize(38, 9);
      setPosition(20, 5);

   end; /* macro m_init_com_search_form() */

   InitTRsbPanel();

   m_init_com_search_form(this);
end; /* class (TRsbPanel) c_com_search_form() */


/*------------------------------------*/
/* Метод обработки событий скроллинга */
/*------------------------------------*/
macro m_bag_proc_scr(p_rs, p_cmd, p_id, p_key)
   var CM_FLAG = CM_DEFAULT;

   var gv_com_search_form;
   var v_msg_menu = TArray;
   var v_choice;
   var v_msg_str = "";
   var v_file_path = "";


   if(p_cmd == DLG_INIT)
      while((gv_scr_focus != 0) and p_rs.movenext())
         if(p_rs.value("t_id") == gv_scr_focus)
            gv_scr_focus = 0;
            GoToScroll(p_rs);
         end;
      end;

   elif(p_cmd == DLG_KEY)
      if(p_key == K_ENTER)
         v_msg_menu.value(0) = "Исходный запрос с оберткой";
         v_msg_menu.value(1) = "Запрос без обертки";
         v_msg_menu.value(2) = "Исходный ответ";
         v_msg_menu.value(3) = "Ответ с оберткой";

         v_choice = menu(v_msg_menu, "Выберите сообщение", "Выберите сообщение");

         if(v_choice == 0)
            m_make_xr_log_msg(p_rs.value("u_reqid_linked"), 1);
         elif(v_choice == 1)
            m_make_usr_log_msg(p_rs.value("u_id"), 1);
         elif(v_choice == 2)
            m_make_usr_log_msg(p_rs.value("u_id"), 3); /* 3 просто для единообразия - никакого скрытого смысла */
         elif(v_choice == 3)
            m_make_xr_log_msg(p_rs.value("u_reqid_linked"), 3);
         else
            msgbox("Выбор не подтвержден");
         end;

         CM_FLAG = CM_IGNORE;

      elif(p_key == K_F5)
         CM_FLAG = CM_SELECT;

         gv_com_search_form = c_com_search_form();
         gv_com_search_form.run;
      end;

   end;

   return CM_FLAG;
end; /* macro m_evnt_proc_scr() */


/*---------------------------------------*/
/* Метод, запускающий детальный скроллинг */
/*---------------------------------------*/
private macro m_run_detail_scroll(_recid, _begdate, _enddate)
   var sql_scr, cmd_scr, rs_scr;
   var sql_main = "";
   var sql_filtr = "";
   var sql_order = "";

   gv_bag_sel_prm = c_bag_selection_prm();

   sql_main =            "  SELECT to_char(l.processtime,'dd.mm.yyyy hh24:mi:ss') processtime, l.mask_include, l.mask_exclude, ";
   sql_main = sql_main + "         l.count_real_records, l.send_err, l.answer_err, l.requestid_single, nvl(u.t_id, -1) u_id,  nvl(u.t_reqid_linked,-1) u_reqid_linked, ";
   sql_main = sql_main + "         nvl(repdate, trunc(processtime)) repdate ";
   sql_main = sql_main + "    FROM SOFRBAG_LAUNCH_LOG l";
   sql_main = sql_main + " left join usr_exchng_log_dbt u on u.t_reqid = l.requestid_single ";
   sql_main = sql_main + "   WHERE count_records > 0 ";
   sql_main = sql_main + "     AND TRUNC(processtime) >= :pbegdate ";
   sql_main = sql_main + "     AND TRUNC(processtime) <= :penddate ";
   sql_main = sql_main + "     AND recid = :recid ";

   sql_order = sql_order + "ORDER BY processtime DESC ";

   sql_scr = sql_main + sql_filtr + sql_order;

   cmd_scr = RSDCommand(sql_scr);
   cmd_scr.NullConversion = true;

   cmd_scr.addParam("pbegdate",  RSDBP_IN, _begdate);
   cmd_scr.addParam("penddate",  RSDBP_IN, _enddate);
   cmd_scr.addParam("recid",     RSDBP_IN, _recid);

   rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   rs_scr.NullConversion = true;
   gv_num_columns_scr = 0;
   gv_column_list_scr.size = 0;

   m_add_column(gv_column_list_scr, gv_num_columns_scr, "processtime",        "Время"    , 14); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "repdate",            "Дата отчета", 10); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "mask_include",       "Включать" , 15); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "mask_exclude",       "Исключать", 15); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "count_real_records", "Выгр."    ,  8); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "send_err",           "Отправка" , 15); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "answer_err",         "Ответ"    , 15); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "requestid_single",   ""         , 30); gv_num_columns_scr = gv_num_columns_scr + 1;
   m_add_column(gv_column_list_scr, gv_num_columns_scr, "u_id",               ""         , 10,2,0); gv_num_columns_scr = gv_num_columns_scr + 1;

   while(RunScroll(rs_scr, gv_num_columns_scr, gv_column_list_scr, "bag_launch", "m_bag_proc_scr", "Выгрузки RSHB_Bag", "ESC - выход  F5 - фильтр  ENTER - подробности", true, 5, NULL, NULL, 40))
      /* Обновляем скроллинг */
      cmd_scr.close(); cmd_scr = NULL;
      rs_scr.close(); rs_scr = NULL;

      sql_filtr = "";

      /*----------------*/
      /* Фильтр по дате */
      /*----------------*/
      if(gv_bag_sel_prm.v_com_date_switch)
         if(gv_bag_sel_prm.v_com_beg_date != date("00.00.0000"))
            sql_filtr = sql_filtr + " AND TRUNC(processtime) >= :pbegdate ";
         end;
         if(gv_bag_sel_prm.v_com_end_date != date("00.00.0000"))
            sql_filtr = sql_filtr + " AND TRUNC(processtime) <= :penddate ";
         end;
      else
         if(gv_bag_sel_prm.v_com_date != date("00.00.0000"))
            sql_filtr = sql_filtr + " AND TRUNC(processtime) = :pdate ";
         end;
      end;

      sql_scr = sql_main + sql_filtr + sql_order;

      /*-----------*/
      /* Параметры */
      /*-----------*/
      cmd_scr = RSDCommand(sql_scr);
      cmd_scr.NullConversion = true;
      if(gv_bag_sel_prm.v_com_date_switch)
         if(gv_bag_sel_prm.v_com_beg_date != date("00.00.0000"))
            cmd_scr.addParam("pbegdate", RSDBP_IN, gv_bag_sel_prm.v_com_beg_date)
         end;
         if(gv_bag_sel_prm.v_com_end_date != date("00.00.0000"))
            cmd_scr.addParam("penddate", RSDBP_IN, gv_bag_sel_prm.v_com_end_date);
         end;
      else
         if(gv_bag_sel_prm.v_com_date != date("00.00.0000"))
            cmd_scr.addParam("pdate", RSDBP_IN, gv_bag_sel_prm.v_com_date);
         end;
      end;

      rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   end;

   rs_scr.close();

end; /* macro m_run_detail_scroll() */



/*------------------------------------*/
/* Метод обработки событий скроллинга */
/*------------------------------------*/
macro m_bag_proc_scr_main(p_rs, p_cmd, p_id, p_key)
   var CM_FLAG = CM_DEFAULT;

   var gv_com_search_form;
   var v_msg_menu = TArray;
   var v_choice;
   var v_msg_str = "";
   var v_file_path = "";


   if(p_cmd == DLG_INIT)
      while((gv_scr_focus != 0) and p_rs.movenext())
         if(p_rs.value("t_id") == gv_scr_focus)
            gv_scr_focus = 0;
            GoToScroll(p_rs);
         end;
      end;

   elif(p_cmd == DLG_KEY)
      if(p_key == K_ENTER)
         if (p_rs.value("t_type") == 1)
            m_run_detail_scroll(0, p_rs.value("tt"), p_rs.value("tt"));
         else
            m_run_detail_scroll(p_rs.value("t_recid"), p_rs.value("tt"), p_rs.value("tt"));
         end;
         CM_FLAG = CM_IGNORE;

      elif(p_key == K_F5)
         CM_FLAG = CM_SELECT;

         gv_com_search_form = c_com_search_form();
         gv_com_search_form.run;
      end;

   end;

   return CM_FLAG;
end; /* macro m_bag_proc_scr_main() */

/*---------------------------------------*/
/* Метод, запускающий основной скроллинг */
/*---------------------------------------*/
macro m_run_main_scroll()
   var sql_scr, cmd_scr, rs_scr;
   var sql_main = "";
   var sql_filtr = "";
   var sql_order = "";

   gv_bag_sel_prm = c_bag_selection_prm();

   sql_main =            " select t_recid, trunc(t_timestamp) tt, to_char(t_lastupdate,'dd.mm.yyyy hh24:mi:ss') t_timestamp, t_type, decode(t_type, 1,'Автозапуск','Ручной запуск') type, ";
   sql_main = sql_main + "        decode(t_status, 1, 'Задание в очереди', 7, 'Идет обработка',2,'Завершено', ' ') status ";
   sql_main = sql_main + "   from utableprocessevent_dbt where t_objecttype = 5020 ";

   if(gv_bag_sel_prm.v_com_date != date("00.00.0000"))
      sql_filtr =        "     AND TRUNC(t_timestamp) = :pdate ";
   end;

   sql_order = sql_order + "ORDER BY t_timestamp DESC ";

   sql_scr = sql_main + sql_filtr + sql_order;

   cmd_scr = RSDCommand(sql_scr);
   cmd_scr.NullConversion = true;

   if(gv_bag_sel_prm.v_com_date != date("00.00.0000"))
      cmd_scr.addParam("pdate", RSDBP_IN, gv_bag_sel_prm.v_com_date);
   end;

   rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   rs_scr.NullConversion = true;

   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "t_timestamp",        "Время"    , 20); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "type",               "Тип"      , 15); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "status",             "Статус"   , 20); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;

   while(RunScroll(rs_scr, gv_num_columns_scr_main, gv_column_list_scr_main, "bag_launch_main", "m_bag_proc_scr_main", "Выгрузки RSHB_Bag", "ESC - выход  F5 - фильтр  ENTER - подробности", true, 5, NULL, NULL, 40))
      /* Обновляем скроллинг */
      cmd_scr.close(); cmd_scr = NULL;
      rs_scr.close(); rs_scr = NULL;

      sql_filtr = "";

      /*----------------*/
      /* Фильтр по дате */
      /*----------------*/
      if(gv_bag_sel_prm.v_com_date_switch)
         if(gv_bag_sel_prm.v_com_beg_date != date("00.00.0000"))
            sql_filtr = sql_filtr + " AND TRUNC(t_timestamp) >= :pbegdate ";
         end;
         if(gv_bag_sel_prm.v_com_end_date != date("00.00.0000"))
            sql_filtr = sql_filtr + " AND TRUNC(t_timestamp) <= :penddate ";
         end;
      else
         if(gv_bag_sel_prm.v_com_date != date("00.00.0000"))
            sql_filtr = sql_filtr + " AND TRUNC(t_timestamp) = :pdate ";
         end;
      end;

      sql_scr = sql_main + sql_filtr + sql_order;

      /*-----------*/
      /* Параметры */
      /*-----------*/
      cmd_scr = RSDCommand(sql_scr);
      cmd_scr.NullConversion = true;
      if(gv_bag_sel_prm.v_com_date_switch)
         if(gv_bag_sel_prm.v_com_beg_date != date("00.00.0000"))
            cmd_scr.addParam("pbegdate", RSDBP_IN, gv_bag_sel_prm.v_com_beg_date)
         end;
         if(gv_bag_sel_prm.v_com_end_date != date("00.00.0000"))
            cmd_scr.addParam("penddate", RSDBP_IN, gv_bag_sel_prm.v_com_end_date);
         end;
      else
         if(gv_bag_sel_prm.v_com_date != date("00.00.0000"))
            cmd_scr.addParam("pdate", RSDBP_IN, gv_bag_sel_prm.v_com_date);
         end;
      end;

      rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   end;

   rs_scr.close();
end; /* macro m_run_main_scroll() */


/* Запуск основного скроллинга */
m_run_main_scroll();
//Exit(1);