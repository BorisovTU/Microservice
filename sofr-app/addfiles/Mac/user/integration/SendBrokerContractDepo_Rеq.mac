/*-----------------------------------------------------------------------------------------
   Передача информации по брокерскому обслуживанию в систему Депозитарий
   BIQ-7382
   Гераськина Т.В.
-----------------------------------------------------------------------------------------*/
import rsd, func_lib;
import global_classes;
import dlcontrfunc; // BIQ-7041 для кода биржи Спб

//Параметры счета 
class c_BrokerAccount
   var AccountNumber:string;  // Номер счёта    Да
   var AccountOpenDate:date;  // Дата открытия счета Да
   var AccountType:c_IntegrationDictionaryRecordXType;    // Тип счета    Да    Тип счета:1 - брокерский счет; 2- требования по уплате комиссий по БО: cчета категории учёта 5087 по договору
end;

// Параметры субдоговора (площадки)
class c_TradingMarket_Depo()
   var TradingMarketType:c_IntegrationDictionaryRecordXType; // Тип площадки   Да
                                    /*Возможные значения:
                                       1 - фондовая секция МБ;
                                       2 - внебиржевой рынок;
                                       3 - секция иностранных бумаг СПБ*/
   var AccountList;              // Счета клиента      Контейнер
end;

// Параметры договора БО 
class c_ContractParams_Depo()
   var ContractNumber:string;          // Номер договора    Да
   var ContractName:string;            // Название договора Да
   var ContractOpenDate:date;          // Дата открытия договора  Да
   var ClientCodeMOEX:string;          // Единый код клиента на МБ  Нет
   var ClientCodeSPBEX:string;         // Единый код клиента на СПБ  Нет
   var QualifiedInvestor:string;       // Признак квал.инвестора  Нет  0 - нет, 1 - да, 2 - отозван
   var QualifiedInvestorStartDate:date;// Дата признания квал. инвестором  Нет
   var QualifiedInvestorEndDate:date;  // Дата отозвания признака квал. инвестора  Нет   Проставляется в случае если поле QualifiedInvestor = 2. Может быть NULL, если признак квалифицированного инвестора не отозван.
   var CurrencyCodes:string;           // Валюты   Нет   Буквенное обозначение валют через запятую, в которых будут происходить расчеты по договору. Например: RUB, USD
   var ClientRegistrationCodeSPBEX:string; // Регистрационный код участника торгов СПБ Нет             
   var FilialId:c_IntegrationSymbolicIdentifierXType;                // Код филиала    Да Код филиала договора обслуживания: 4-x значный код 0000
   var ClientId:c_IntegrationSymbolicIdentifierXType;                // Код клиента в АБС       Код клиента вида 101
   var ClientType:c_IntegrationDictionaryRecordXType;              // Тип клиента  Да    Тип клиента: 1 - ФЛ, 2 - ЮЛ
   var ContractId:c_IntegrationSymbolicIdentifierXType;              // Id договора в СОФР   Да
end;

//Общий запрос
class c_BrokInfoRequest
   var ContractParameters;    // Параметры договора      Контейнер
   var TradingMarketList;     // Список площадок         Контейнер
end;

private macro FindInArray(str, arr)
   var res = -1;
   var i   = 0;
   while(i<arr.size)
      if ( arr(i) == str )
         return i;
      end;
      i = i + 1;
   end;
   return res;
end;

// квалифицированный инвестор
private class c_QI
   var state   : string;
   var begdate : date;
   var enddate : date;
end;

private macro QI(_partyid)
   var res;
   var sql_str, cmd, rs;
   sql_str = " select decode(t_state, 0, 2, 1, 1, 0) st, "+
             "        t_state, t_regdate, t_changedate "+
             "   from dscqinv_dbt where t_partyid = :partyid";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("partyid", RSDBP_IN, _partyid);
   rs = RSDRecordSet(cmd);
   if (rs.movenext)
      res = c_QI;
      res.state = String(Int(rs.value("st")));
      res.begdate = rs.value("t_regdate");
      if (rs.value("t_state") == 0)  // исключен
         res.enddate = rs.value("t_changedate");
      else
         res.enddate = NULL;
      end;
   else 
      res = NULL;
   end;

   return res;
end;


macro FillBrokInfo_Depo(p_dlcontrid)
   var outobj = c_BrokInfoRequest();
   var sql_str, rs, cmd;
   var err_txt;
   var ai, ai_acc;
   var sql_str_acc, rs_acc, cmd_acc;
   var p_sf_id;
   var temp_acc, temp_acc_list;

   // BIQ-7041 биржевые коды Спб
   var marketid = ПолучитьБиржуПоВидуКодаДБО(DLCCK_SPB_BIDCODE); 
   var temp_CurrencyCodes, arr_CurrencyCodes = TArray;
   var temp_qi;

   // договор БО
   outobj.ContractParameters = c_ContractParams_Depo();

   sql_str = "SELECT dl.t_dlcontrid, dl.t_sfcontrid, dl.t_IIS, sf.t_number, sf.t_name, sf.t_datebegin, sf.t_partyid, to_char(p.t_legalform) t_legalform"+
             "  FROM dsfcontr_dbt sf, "+
             "       ddlcontr_dbt dl, "+
             "       dparty_dbt p "+
             " WHERE sf.t_id = dl.t_sfcontrid  "+
             "   AND sf.t_partyid = p.t_partyid  "+
             "   AND dl.t_dlcontrid = :p_dlcontrid ";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("p_dlcontrid", RSDBP_IN, p_dlcontrid);
   rs = RSDRecordSet(cmd);
   if (rs.movenext)
      outobj.ContractParameters.ContractNumber   = rs.value("t_number");
      outobj.ContractParameters.ContractName     = rs.value("t_name");
      if (outobj.ContractParameters.ContractName == StrFor(1))
         err_txt = string("Не задано название договора ");
         RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR)); 
      end;
      outobj.ContractParameters.ContractOpenDate = rs.value("t_datebegin");
      outobj.ContractParameters.ClientCodeMOEX   = GetDLObjCode_ByObjectid(p_dlcontrid, 207, DLCCK_USC);
      outobj.ContractParameters.ClientRegistrationCodeSPBEX   = GetDLObjCode_ByObjectid(p_dlcontrid, 207, DLCCK_SPB_BIDCODE);
      if (not outobj.ContractParameters.ClientRegistrationCodeSPBEX)
         outobj.ContractParameters.ClientRegistrationCodeSPBEX = NULL;
      end;
      outobj.ContractParameters.QualifiedInvestor   = 0; 
      temp_qi = QI(rs.value("t_partyid"));
      if (ValType(temp_qi) != V_UNDEF)
         outobj.ContractParameters.QualifiedInvestor         = temp_qi.state;
         outobj.ContractParameters.QualifiedInvestorStartDate= temp_qi.begdate; 
         outobj.ContractParameters.QualifiedInvestorEndDate  = temp_qi.enddate; 
      end;

      outobj.ContractParameters.FilialId.ObjectId         = "0000"; 
      outobj.ContractParameters.ClientId.ObjectId         = GetPartyCode_ByNumber(rs.value("t_partyid"), PARTY_CODEKIND_ABS);
      if (rs.value("t_legalform") == 2)  // Тип клиента: 1 - ФЛ, 2 - ЮЛ
         outobj.ContractParameters.ClientType.RecordCode = 1;
      else 
         outobj.ContractParameters.ClientType.RecordCode = 2;
      end;
      outobj.ContractParameters.ContractId.ObjectId       = p_dlcontrid;
   else 
      err_txt = string("Договор ID="+p_dlcontrid+" не найден");
      RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR)); 
   end;
   rs.close; rs = null;
   cmd.close; cmd = null;
   
   // торговые площадки
   outobj.TradingMarketList = TArray;
   
   // только биржа/внебиржа
   sql_str = "SELECT cntr.t_number, cntr.t_datebegin, cntr.t_servkind, cntr.t_servkindsub, cntr.t_id, "+
             "         CASE "+
             "             when cntr.t_servkind = 1 and t_servkindsub = 8 then 1 "+
             "             when cntr.t_servkind = 1 and t_servkindsub = 9 then 2 "+
             "             else 0 "+
             "         END servkind, mp.t_marketid, mp.t_mpcode, "+
             "         (select t_legalform from dparty_dbt where t_partyid = cntr.t_partyid) as legalform   "+
             "  FROM dsfcontr_dbt cntr, "+
             "       ddlcontrmp_dbt mp "+
             " WHERE cntr.t_id = mp.t_sfcontrid "+
             "   AND mp.t_dlcontrid = :p_dlcontrid "+
//             "   AND mp.t_mpclosedate = to_date('01.01.0001','dd.mm.yyyy') "+
             "   AND cntr.t_dateclose = to_date('01.01.0001','dd.mm.yyyy') "+
             "   AND cntr.t_servkind = 1";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("p_dlcontrid", RSDBP_IN, p_dlcontrid);
   rs = RSDRecordSet(cmd);
   ai = 0;
   while (rs.movenext)
      // def-35817 если это старый договор, то Спб игнорируем
      if ( rs.value("t_marketid") == marketid )
         if ((outobj.ContractParameters.ContractOpenDate < date(15,2,2022)) and (rs.value("Legalform") == 2)) // проверяем дату только для физиков
            continue;
         end;
      end;

      outobj.TradingMarketList.value(ai) = c_TradingMarket_Depo();
      outobj.TradingMarketList.value(ai).TradingMarketType.RecordCode = String(Int(rs.value("servkind")));
      // BIQ-7041 биржа Спб
      if ( rs.value("t_marketid") == marketid )
         if (rs.value("servkind") == 1)
            outobj.TradingMarketList.value(ai).TradingMarketType.RecordCode = 3;
            outobj.ContractParameters.ClientCodeSPBEX  = rs.value("t_mpcode");
         end;
      end;

      p_sf_id = rs.value("t_id");
      
      // брокерские счета
      temp_acc_list = TArray;

      sql_str_acc = "SELECT sf.t_number, sf.t_id, acc.t_account, acc.t_open_date, fi.t_ccy, fi.t_iso_number "+
                    "  FROM dsfcontr_dbt sf, "+
                    "       dsfssi_dbt ssi, "+
                    "       dsettacc_dbt settacc, "+
                    "       daccount_dbt acc, "+
                    "       dfininstr_dbt fi "+
                    " WHERE ssi.t_objecttype = 659 AND t_objectid = lpad(sf.t_id, 10, chr(48)) "+
                    "   AND settacc.t_settaccid = ssi.t_setaccid "+
                    "   AND settacc.t_account = acc.t_account AND settacc.t_chapter = acc.t_chapter AND settacc.t_fiid = acc.t_code_currency "+
                    "   AND acc.t_close_date = to_date('01.01.0001','dd.mm.yyyy') "+
                    "   AND fi.t_fiid = acc.t_code_currency "+
                    "   AND sf.t_id = :p_sf_id";
      cmd_acc = RSDCommand(sql_str_acc);
      cmd_acc.AddParam("p_sf_id", RSDBP_IN, p_sf_id);
      rs_acc = RSDRecordSet(cmd_acc);
      ai_acc = 0;
      while (rs_acc.movenext)
         temp_acc = c_BrokerAccount();
         temp_acc.AccountNumber = rs_acc.value("t_account");
         temp_acc.AccountOpenDate = rs_acc.value("t_open_date");
         temp_acc.AccountType.RecordCode = 1;
         
         temp_acc_list.value(ai_acc) = temp_acc;
         temp_acc = null;
         ai_acc = ai_acc+1;

         if (FindInArray(rs_acc.value("t_ccy"), arr_CurrencyCodes) < 0 )
            arr_CurrencyCodes.value(arr_CurrencyCodes.size) = rs_acc.value("t_ccy");
         end;
      end;
      
      // счета требований, нумерация сквозная
      sql_str_acc = "SELECT acc.t_account, acc.t_open_date, fi.t_ccy, fi.t_iso_number "+
                    "  FROM dmcaccdoc_dbt mc, "+
                    "       daccount_dbt acc, "+
                    "       dfininstr_dbt fi "+
                    " WHERE  mc.t_account = acc.t_account AND mc.t_chapter = acc.t_chapter AND mc.t_currency = acc.t_code_currency "+
                    "   AND acc.t_close_date = to_date('01.01.0001','dd.mm.yyyy') "+
                    "   AND fi.t_fiid = acc.t_code_currency "+
                    "   AND mc.t_iscommon = 'X' and t_catnum = 5087 and mc.t_clientcontrid = :p_sf_id";
                   
      cmd_acc = RSDCommand(sql_str_acc);
      cmd_acc.AddParam("p_sf_id", RSDBP_IN, p_sf_id);
      rs_acc = RSDRecordSet(cmd_acc);
      while (rs_acc.movenext)
         temp_acc = c_BrokerAccount();
         temp_acc.AccountNumber = rs_acc.value("t_account");
         temp_acc.AccountOpenDate = rs_acc.value("t_open_date");
         temp_acc.AccountType.RecordCode = 2;
         
         temp_acc_list.value(ai_acc) = temp_acc;
         temp_acc = null;
         ai_acc = ai_acc+1;
      end;
      
      if (temp_acc_list.size > 0)
         outobj.TradingMarketList.value(ai).AccountList = temp_acc_list;
      else 
         outobj.TradingMarketList.value(ai).AccountList = null;
      end;
      temp_acc_list = null;

      ai = ai+1;
   end;
   rs.close; rs = null;
   cmd.close; cmd = null;
   
   if (arr_CurrencyCodes.size > 0)
      outobj.ContractParameters.CurrencyCodes = arr_CurrencyCodes.value(0);
      ai = 1;
      while (ai < arr_CurrencyCodes.size)
         outobj.ContractParameters.CurrencyCodes = outobj.ContractParameters.CurrencyCodes + "," + arr_CurrencyCodes.value(ai);
         ai = ai+1;
      end;
   end;
   ai = 0; arr_CurrencyCodes = NULL;

   return outobj;
end;

private class c_SendBrokerContractDepo_Rеq
   var SendBrokerContractDepoReq;
end;


macro SendBrokerContractDepo_Rеq ( dlcontrid, p_error:@object )
   var res = c_SendBrokerContractDepo_Rеq();

   if( valType (dlcontrid) == V_INTEGER )
      res.SendBrokerContractDepoReq = FillBrokInfo_Depo(dlcontrid);
   end;

   return res;
onError(err)
  res = NULL;
  p_error = c_Error;
  p_error.ErrorCode = c_err_obj.obtain_err_code(err);
  p_error.ErrorDesc = c_err_obj.obtain_err_msg(err);
  
  return res;  
end;

//SendBrokerContractDepo_Rеq(15332);