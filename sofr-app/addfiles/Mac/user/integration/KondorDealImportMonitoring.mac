import RSD, BankInter, RsbFormsInter;
import "KondorFunc.mac";

private const REGPATH_KONDORPROCESSING_STORAGELIMITDAYS = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ИМПОРТ СДЕЛОК ИЗ АСУДР-КОНДОР\\СРОК ХРАНЕНИЯ ДАННЫХ ОБ ИМПОРТЕ";
private const FIELD_TYPE_READONLY = 2;
private const K_SPACE = 32,
              K_F2    = 316,
              K_F5    = 319,
              K_F8    = 322;
private const READONLY = true;
private const FT_CHR = 12;

private const FILTER_PARAM_ERRSTATUS_ALL          = 0,
              FILTER_PARAM_ERRSTATUS_SUCCESS      = 1,
              FILTER_PARAM_ERRSTATUS_ERROR        = 2,
              FILTER_PARAM_PROCESSING_ALL         = 3,
              FILTER_PARAM_PROCESSING_COMPLETE    = 4,
              FILTER_PARAM_PROCESSING_NOTFINISHED = 5,
              FILTER_PARAM_EXCLUDE_MASS_ERRORS    = 6;

private macro IIF(condition, valueOnTrue, valueOnFalse)
  if (condition)
    return valueOnTrue;
  end;
  
  return valueOnFalse;
end;

/**
@brief Функция получения из настроек банка переменной "Срок хранения данных об импорте" (в днях)
@return V_INTEGER Значение переменной или 0 в случае ошибки или неверного значения
*/
private macro GetStoreLimitDays(): Integer
  var daysLimit;
  var stat = 0;

  GetRegistryValue(REGPATH_KONDORPROCESSING_STORAGELIMITDAYS, V_INTEGER, daysLimit, stat);
  if ((stat != 0) or (daysLimit < 0))
    daysLimit = 0;
  end;

  return daysLimit;
end;

/**
@brief Функция проверки наличия устаревших данных в таблице kondor_sofr_buffer_dbt
@param[in] daysLimit Количество дней (начиная с текущей даты), которое должно пройти, чтобы данные считались устаревшими
@return V_BOOL True - устаревшие данные есть, иначе false
*/
private macro IsOldRecordsExists(daysLimit): Bool
  var query = "SELECT 1 " +
              "FROM kondor_sofr_buffer_dbt " +
             "WHERE t_dateComplete != TO_DATE('01.01.0001', 'DD.MM.YYYY') " + 
               "AND NOT t_dateComplete IS NULL " +
               "AND t_dateComplete <= SYSDATE - :daysLimit " +
               "AND ROWNUM = 1";
  var cmd = RsdCommand(query);

  cmd.AddParam("daysLimit", RSDBP_IN, daysLimit);
  var rs = RSDRecordset(cmd);
  
  if (rs.MoveNext())
    return true;
  end;
  
  return false;
end;

/**
@brief Процедура удаления записей из таблицы kondor_sofr_buffer_dbt с t_dateComplete старше на daysLimit по сравнению с текущей датой
@param[in] daysLimit Количество дней
*/
private macro DeleteOldRecords(daysLimit)
  var query = "DELETE " +
              "FROM kondor_sofr_buffer_dbt " +
             "WHERE t_dateComplete > TO_DATE('01.01.0001','DD.MM.YYYY') " +
               "AND t_dateComplete <= SYSDATE - :daysLimit ";
  var cmd = RsdCommand(query);

  cmd.AddParam("daysLimit", RSDBP_IN, daysLimit);
  cmd.Execute();
  cmd.Close();
end;

/**
@brief Процедура удаления записей из таблицы kondor_sofr_buffer_dbt по ID
@param[in] id Строка с одним или несколькими t_recID разделенными запятыми
*/
private macro DeleteRecordsByID(id: String)
  var query = "DELETE " +
                "FROM kondor_sofr_buffer_dbt " +
               "WHERE t_recID in (" + id + ")";
  var cmd = RsdCommand(query);

  cmd.Execute();
  cmd.Close();
end;

private class CFilterParams()
  private var params: TArray;
  
  macro Set(index: Integer, value: Variant)
    params(index) = value;
  end;
  
  macro Get(index: Integer): Variant
    return params(index);
  end;
  
  macro GetFilter(): String
    var filter = "";

    if (params(FILTER_PARAM_ERRSTATUS_SUCCESS) == true)
      filter = "t_errorStatus = 0";
    else
      if (params(FILTER_PARAM_EXCLUDE_MASS_ERRORS) == true)
        var errorFilterRecs = GetErrorFilterRecs();
        var i = 0;
        
        while (i < errorFilterRecs.size)
          filter = String(filter, IIF(StrLen(filter) > 0, " OR ", ""), "(t_errorStatus = ", errorFilterRecs(i).errorCode, " AND t_errorMessage like '%", errorFilterRecs(i).errorMessagePart, "%')");
  
          i = i + 1;
        end;
        
        if (StrLen(filter) > 0)
          filter = String("NOT (", filter, ")");
        end;
      end;
      
      if (params(FILTER_PARAM_ERRSTATUS_ERROR) == true)
        filter = String("t_errorStatus != 0", IIF(StrLen(filter) > 0, " AND ", ""), filter);
      end;
    end;

    if (params(FILTER_PARAM_PROCESSING_ALL) == false)
      if (StrLen(filter) > 0)
        filter = filter + IIF((params(FILTER_PARAM_ERRSTATUS_ERROR) == true) and (params(FILTER_PARAM_PROCESSING_NOTFINISHED) == true), " OR ", " AND ");
      end;
      
      if (params(FILTER_PARAM_PROCESSING_COMPLETE) == true)
        filter = filter + "t_dateComplete > TO_DATE('01.01.0001', 'DD.MM.YYYY')";
      else
        filter = filter + "t_dateComplete IS NULL " +
                       "OR t_dateComplete = TO_DATE('01.01.0001', 'DD.MM.YYYY') ";
      end;
    end;

    return filter;
  end;
  
  //----- конструктор -------
  params = TArray();
  params(FILTER_PARAM_ERRSTATUS_ALL)          = false;
  params(FILTER_PARAM_ERRSTATUS_SUCCESS)      = false;
  params(FILTER_PARAM_ERRSTATUS_ERROR)        = true;
  params(FILTER_PARAM_PROCESSING_ALL)         = false;
  params(FILTER_PARAM_PROCESSING_COMPLETE)    = false;
  params(FILTER_PARAM_PROCESSING_NOTFINISHED) = true;
  params(FILTER_PARAM_EXCLUDE_MASS_ERRORS)    = true;
end; //CFilterParams

/**
@brief Фильтр скроллинга
*/
private class (TRsbPanel) CFilterPanel(filterParams: @CFilterParams)
  private var f;
  private var params;
  
  private macro NewLabel(x: Integer, y: Integer, caption: String)
    AddLabel(TRsbLabel(x, y, caption));
  end;
  
  private macro NewCheckbox(x: Integer, y: Integer, controlName: String, isChecked: Bool)
    var checkbox: Object;
    
    checkbox = TRsbCheckbox(1);
    checkbox.name = controlName;
    checkbox.dataType = FT_CHR;
    checkbox.SetPosition(x, y);
    checkbox.checked = isChecked;
    AddControl(checkbox);
  end;
  
  macro EventProc(event: Object)
    if (event.keyCode == K_ESC)
      this.Close( event.keyCode );
    elif (event.keyCode == K_SPACE)
      if (this.focusedControl.name != "ExcludeMassErrors")
        this.focusedControl.checked = true;
      end;
      
      if (this.focusedControl.name == "ErrStatusAll")
        this.GetControl("ErrStatusError").checked = false;
        this.GetControl("ErrStatusSuccess").checked = false;
      elif (this.focusedControl.name == "ErrStatusSuccess")
        this.GetControl("ErrStatusAll").checked = false;
        this.GetControl("ErrStatusError").checked = false;
      elif (this.focusedControl.name == "ErrStatusError")
        this.GetControl("ErrStatusAll").checked = false;
        this.GetControl("ErrStatusSuccess").checked = false;
      
      elif (this.focusedControl.name == "ProcessingAll")
        this.GetControl("ProcessingNotFinished").checked = false;
        this.GetControl("ProcessingComplete").checked = false;
      elif (this.focusedControl.name == "ProcessingComplete")
        this.GetControl("ProcessingAll").checked = false;
        this.GetControl("ProcessingNotFinished").checked = false;
      elif (this.focusedControl.name == "ProcessingNotFinished")
        this.GetControl("ProcessingAll").checked = false;
        this.GetControl("ProcessingComplete").checked = false;
      end;
      
      this.Redraw();
    elif (event.keyCode == K_F2)
      params.Set(FILTER_PARAM_ERRSTATUS_ALL, GetControl("ErrStatusAll").checked);
      params.Set(FILTER_PARAM_ERRSTATUS_SUCCESS, GetControl("ErrStatusSuccess").checked);
      params.Set(FILTER_PARAM_ERRSTATUS_ERROR, GetControl("ErrStatusError").checked);
      params.Set(FILTER_PARAM_PROCESSING_ALL, GetControl("ProcessingAll").checked);
      params.Set(FILTER_PARAM_PROCESSING_COMPLETE, GetControl("ProcessingComplete").checked);
      params.Set(FILTER_PARAM_PROCESSING_NOTFINISHED, GetControl("ProcessingNotFinished").checked);
      params.Set(FILTER_PARAM_EXCLUDE_MASS_ERRORS, GetControl("ExcludeMassErrors").checked);

      this.Close( event.keyCode );
    end;
  end;
  
  InitTRsbPanel();
  params = filterParams;

  SetCaption("Фильтр скроллинга");
  SetStatus("~Esc~ Выход   ~F2~ Применить");
  SetSize(36, 13);
  SetPosition(45,10);
  
  NewLabel(2, 1, "Код ошибки:");
  NewCheckbox(4, 2, "ErrStatusAll",     params.Get(FILTER_PARAM_ERRSTATUS_ALL));     NewLabel(7, 2, "все");
  NewCheckbox(4, 3, "ErrStatusSuccess", params.Get(FILTER_PARAM_ERRSTATUS_SUCCESS)); NewLabel(7, 3, "код ошибки равен 0");
  NewCheckbox(4, 4, "ErrStatusError",   params.Get(FILTER_PARAM_ERRSTATUS_ERROR));   NewLabel(7, 4, "код ошибки не равен 0");
  
  NewLabel(2, 6, "Дата обработки:");
  NewCheckbox(4, 7, "ProcessingAll",         params.Get(FILTER_PARAM_PROCESSING_ALL));         NewLabel(7, 7, "все");
  NewCheckbox(4, 8, "ProcessingComplete",    params.Get(FILTER_PARAM_PROCESSING_COMPLETE));    NewLabel(7, 8, "установлена");
  NewCheckbox(4, 9, "ProcessingNotFinished", params.Get(FILTER_PARAM_PROCESSING_NOTFINISHED)); NewLabel(7, 9, "не определена");
  
  NewCheckbox(2, 11, "ExcludeMassErrors", params.Get(FILTER_PARAM_EXCLUDE_MASS_ERRORS)); NewLabel(5, 11, "фильтр массовых ошибок");
  
  AddEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "EventProc"));
end;  //CFilterPanel

/**
@brief Базовый класс для создания скроллинга
*/
class CRslScroll
  private var columns;

  private macro GetColumnsCount(): Integer
    return columns.size / 6;
  end;

  private macro Add(Value)
    columns.Value(columns.Size) = Value;
  end;

  macro AddColumn (field: String, caption: String, width: Integer, type: Integer, decPoint: Integer)
    Add(field);
    Add(caption);
    Add(width);
    Add(type); 
    Add(decPoint);
    Add(0);  // Резерв
  end;

  macro EventProc (rs, cmd, id, key)
  end;  

  macro Run (rs: Object, scrollUniqueName: String, scrollCaption: String, statusLine: String, isReadOnly: Bool)
    return RunScroll (rs, GetColumnsCount(), columns, scrollUniqueName, R2M(this,"EventProc"), scrollCaption, statusLine, isReadOnly, -1, -1, -1, -1);
  end;

  //----- конструктор -------
  columns = TArray();
end; //CRslScroll

/**
@brief Класс скроллинга для таблицы kondor_sofr_buffer_dbt
*/
class (CRslScroll) CKondorDealImportMonitoring
  private var query: String = "";
  private var rs, cmd;
  private var selectedRecIDs: String;
  private var filterParams = CFilterParams();

  macro EventProc (rs, cmd, id, key)
    var CM_FLAG = CM_DEFAULT;

    if(cmd == DLG_INIT)
      AddMultiAction(rs, K_F8);
    elif (cmd == DLG_MSELSTART)
      if (not GetTrue(false, "Удалить выбранные записи?"))
        return CM_IGNORE;
      end;

      selectedRecIDs = "";
    elif (cmd == DLG_MSEL)
      selectedRecIDs = selectedRecIDs + "," + rs.Value("t_recID");
      return CM_MSEL_CONT_CLEAR;
    elif ((cmd == DLG_MSELEND) and (StrLen(selectedRecIDs) > 0))
      selectedRecIDs = SubStr(selectedRecIDs, 2);
      DeleteRecordsByID(selectedRecIDs);
    elif (cmd == DLG_KEY)
      if (key == K_F8)
        if (GetTrue(false, "Удалить выбранную запись?"))
          DeleteRecordsByID(String(rs.Value("t_recID")));
          CM_FLAG = CM_SELECT;
        end;
      elif (key == K_F5) 
        CM_FLAG = CM_IGNORE;
        if (CFilterPanel(@filterParams).Run() == K_F2)
          CM_FLAG = CM_SELECT;
        end;
      elif (key == 0)
        CM_FLAG = CM_SELECT;
      end;
    end;

    return CM_FLAG;
  end; 
  
  macro Run()
    query = "SELECT t_recID, t_requestID, t_dealCode, t_errorStatus, t_errorMessage, t_dateComplete, t_reqType, chr(1) as t_emptyField " +
              "FROM kondor_sofr_buffer_dbt ";
              
    var filter = filterParams.GetFilter();
    if (StrLen(filter) > 0)
      query = query + "WHERE " + filter;
    end;

    query = query + " ORDER BY t_recID DESC ";

    cmd = RsdCommand(query);
    rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
    return this.Run(rs, "kondorDealImport", "Мониторинг импорта сделок из Кондор/АСУДР", "~F5~ Фильтр    ~F8~ Удалить запись    ~Alt+Shift+F11~ Экспорт в Excel     ~Esc~ Выход", READONLY);
  end;
  
  //----- конструктор -------
  InitCRslScroll();
    
  AddColumn("t_requestID",    "Номер запроса из АСУДР",        15, FIELD_TYPE_READONLY, 0);
  AddColumn("t_dealCode",     "Код сделки (DealID из XML / поле Код сделки из таблиц)", 20, FIELD_TYPE_READONLY, 0);
  AddColumn("t_reqType",      "Тип запроса",                    5, FIELD_TYPE_READONLY, 0);
  AddColumn("t_errorStatus",  "Ошибка",                         5, FIELD_TYPE_READONLY, 0);
  AddColumn("t_errorMessage", "Результат обработки сообщений", 60, FIELD_TYPE_READONLY, 0);
  AddColumn("t_dateComplete", "Дата обработки",                15, FIELD_TYPE_READONLY, 0);
  AddColumn("t_emptyField",   "",                              80, FIELD_TYPE_READONLY, 0);
end; //CKondorDealImportMonitoring

//-------------------------------------------- Точка входа в макрос ----------------------------------
var daysLimit = GetStoreLimitDays();

if ((daysLimit > 0) and IsOldRecordsExists(daysLimit) and GetTrue(false, "Найдены устаревшие обработанные сделки, удалить их?"))
  DeleteOldRecords(daysLimit);
end;

var scroll = CKondorDealImportMonitoring();

while (scroll.Run())
end;

Exit(1);