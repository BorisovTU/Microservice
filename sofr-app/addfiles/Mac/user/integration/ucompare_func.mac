/**
 @file 		uentcompare_func.mac
 @brief 	Интеграция с внешними системами. 

 # tag
 - functional_block: Отчетность_внутренняя
 - code_type: Report

*/
IMPORT rsexts, rsd, "dlutils.mac";

/*  DEF-66506, открытие локального excel-я перенесено в отдельную процедуру
*/
macro GetBebecuporos ( p_PathS:string, p_PathL:string, p_SrcFileName:string, p_Suffix: string, p_CNum:string, p_BebeFile: @STRING, p_ErrText:@STRING );
  var 
    x_SrcFile :string = p_PathS + p_SrcFileName + p_CNum + ".xlsx"
    , x_PathBebeFile: string
    , x_I: integer = 0
    , x_Stop: BOOL = false
    , x_Ok: BOOL = false
    ;

  while( x_Stop == false )
    p_BebeFile = "bebecuporos_" + string(p_Suffix) + "_" + string(x_I) + "_" + p_CNum + ".xlsx";
    x_PathBebeFile = p_PathL + p_BebeFile;
    // если нет файла bebecupos.xlsx, или он есть, но нормально удаляется
    if((ExistFile(x_PathBebeFile) == false) or (RemoveFile(x_PathBebeFile) == true))
      // пробуем переименовать исходный файл в файл bebecupos.xlsx
      if(RenameFile(x_SrcFile, x_PathBebeFile))
         // получилось, выходим из цикла
         x_Stop = true;
         x_Ok = true;
      end;
    end;
    x_I = x_I + 1;
    if(x_Stop == false)
       // не получилось, попробуем еще (до 3х раз)
       x_Stop = (x_I < 2);
    end;
  end;

  if(x_Ok == false)
    p_ErrText = "Ошибка при открытии книги Excel " + x_PathBebeFile;
  end;

  return x_Ok;
end; // GetBebecuporos

/*  DEF-71583, получение имени Export-ного файла вынесено в отдельную процедуру
*/
macro GetExportPath ( p_RegDir:string, p_RestDate:date, p_ObjType:integer );

  var 
    x_XLSExport_Path :string, x_Sql :string, x_RefValue :string 
    , x_Count :integer, x_Dt :integer, x_Mn :integer, x_Year :integer
    , x_Cmd, x_Rs
    ;

  GetRegistryValue(p_RegDir, V_STRING, x_XLSExport_Path);

  DateSplit ( p_RestDate, x_Dt, x_Mn, x_Year );
  x_RefValue = StrSubst( string(x_Dt:2), " ", "0" ) + StrSubst( string(x_Mn:2), " ", "0" ) + SubStr(StrSubst( string(x_Year), " ", "0" ), 3);

  x_Sql = "select count(t_param) as count from utableprocessevent_dbt where t_objecttype = ?  "
         +"and to_date(substr(t_param,length(t_param)-9),'dd.mm.yyyy') = ? and t_status = 4 group by t_param "
  ;

  x_Cmd = RSDCommand(x_Sql);
  x_Cmd.AddParam( "", RSDBP_IN, p_ObjType );
  x_Cmd.AddParam( "", RSDBP_IN, p_RestDate );
  x_Rs = RSDRecordSet(x_Cmd);
  if(x_Rs.movenext())
     x_Count = x_Rs.value("count");
     x_RefValue = x_RefValue + "_" + x_Count;
  end;

  return PathCombine(x_XLSExport_Path, x_RefValue + ".xlsx");

end; // GetExportPath
