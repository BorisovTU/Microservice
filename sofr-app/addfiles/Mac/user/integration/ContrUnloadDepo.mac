/**
 @file ContrUnloadDepo.mac
 @brief Интеграция с внешними системами. Выгрузка данных по договору в Депозитарий 

 Инициирует создание события 8207 или 9 для планировщика Томката в зависимости от настройки ДЕПОЗИТАРИЙ_ETL вкл/выкл.
 8207 предназначено для формирования запроса SendBrokerContractDepo (ИП-онлайн), 9 - для отправки данных через таблицы (ETL).

 # tag
 - functional_block: Клиенты_ФЛ
 - functional_block: Депозитарий_Обмен
 - code_type: API

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |00.00.2021 |Гераськина Т.В.|BIQ-7382                                        | Выгрузка данных по договору в Депозитарий 
 |           |               |                                                | В зависимости от настройки, отправляется или через ETL, или через ИП

*/
import rsd;
import "global_utils_intgr.mac", "func_lib.mac";

/** 
@brief Поиск кросс-ссылки для переданной системы
@param[in] _dlcid Значение ddlcontr_dbt.t_dlcontrid
@param[in] SilentMode Безынтерфейсный режим: true - макрос запущен при обработке другого процесса (обработка ответа от биржи Спб),
                                             false - макрос запущен из интерфейса по Ctrl+Z из договора

Перед добавлением записи проверяется, что нет других еще не обработанных событий (включая те, на которые не получен ответ).
Если такие есть, то в интерфейсном режиме операционисту задается вопрос - хочет ли он повторно добавить событие. 
Предполагается, что пользователь знает, что он делает, и возможное пересечение обработок запрос/ответ на его совести.
*/
macro ContrUnloadDepo(_dlcid, SilentMode)
   var v_etl = true, stat;
   var cmd, rs;
   var objecttype;

   if((valtype(SilentMode) == v_undef) or (SilentMode == Null)) // если параметр не передан, значит, из интерфейса
      SilentMode = False;
   end;

   GetRegistryValue(DEPO_UNLOAD_TYPE, V_BOOL, v_etl, stat);  // тип выгрузки в Депозитарий: ДЕПОЗИТАРИЙ_ETL = YES/NO

   if(stat > 0)
      v_etl = true; // не найдено - значит, ETL
   end;

   if(v_etl)
      objecttype = 9;    // тип 9 использовался только для ETL, в dfuncobj такие задания не попадают
   else 
      objecttype = 8207;  // тип *207 (207, 5207, 6207, 8207 - все, что связано с договором БО) - см. справочник 5002 Техн.спр-к Объекты очереди заданий funcobj
   end;

   // существующие незавершенные события (статус 1 - не обработано, 2 - выгружено, но ответ не получен)
   cmd = RSDCommand(" SELECT *                                                               " +
                    "   FROM utableprocessevent_dbt                                          " +
                    "  WHERE t_objecttype = :objtype AND t_status < 4 AND t_objectid = :obid ");
   cmd.AddParam("objtype", RSDBP_IN, objecttype);
   cmd.AddParam("obid",    RSDBP_IN, _dlcid);
   
   rs = RsdRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
   if(rs.movenext)
      /* Думаю логично, что задавать вопрос пользователю нужно только не в тихом режиме */
      if(not SilentMode)
         var UploadCount = 0;
         var cmdUploadCount = RSDCommand(" SELECT COUNT (*)     cnt                              " +
                                         "   FROM utableprocessevent_dbt                         " +
                                         "  WHERE t_objecttype = :objtype AND t_objectid = :obid ");
         cmdUploadCount.AddParam("objtype", RSDBP_IN, objecttype);
         cmdUploadCount.AddParam("obid",    RSDBP_IN, _dlcid);
         
         var rsUploadCount = rsdRecordset(cmdUploadCount);
         if(rsUploadCount.movenext)
            UploadCount = int(rsUploadCount.value("cnt"));
         end;

         if(GetTRUE(False, "Уже было "+UploadCount+" запросов счетов депо. Последнее событие было в "+rs.value("T_TIMESTAMP")+" перевести его в ошибку (5) и выгрузить заново?"))
            rs.Edit();
            rs.value("t_status") = 5;
            rs.Update();

            m_make_event_to_process(objecttype, _dlcid);
            msgboxex("Событие создано", MB_YES, IND_YES);
         else
            msgboxex("Повторная выгрузка невозможна.\n По данному договору есть незаконченое событие.", MB_YES, IND_YES);
         end;
      end;
   else
      m_make_event_to_process(objecttype, _dlcid);  // создать запись в utableprocessevent_dbt со параметрами по умолчанию: статус = 1, тип = 1
      if(not SilentMode)
         msgboxex("Событие создано", MB_YES, IND_YES);
      end;
   end;
end;