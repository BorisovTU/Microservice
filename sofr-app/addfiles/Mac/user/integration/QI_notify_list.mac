/** 
 @file QI_notify_list.mac
 @brief Журнал уведомлений по КИ
  
 # tag 
 - functional_block:Клиенты_Регистрация_на_бирже
 - code_type:API
 - Квал.инвесторы

   
 # changeLog 
 |date       |author         |tasks            |note                             
 |-----------|---------------|-----------------|--------------------------------
 |01.12.2023 |Гераськина Т.В.|BOSS-194         | Создание
 |09.01.2024 |Гераськина Т.В.|BOSS-194         | Code review
 |09.01.2024 |Гераськина Т.В.|BOSS-194         | Панель фильтра
 |14.02.2024 |Гераськина Т.В.|DEF-61602        | Отображение пустого сообщения об ошибке по F7
 |14.02.2024 |Гераськина Т.В.|DEF-61605        | Исключение успешных записей из массовой обработки
 

*/ 
import QI_job;
import RsbFormsInter;
import "sud_utils.mac";

const C_USE_CODEKIND = 1;
const C_NUMBER_LIST = 1143;

var g_fill = false; // признак заполнения фильтра
var g_begin_date = {curdate};
var g_end_date = {curdate};
var g_checked_ready = true;
var g_checked_inprocessing = true;
var g_checked_errors = true;
var g_checked_success = false;


/**
@brief Панель фильтра
*/
private class (TRsbPanel) c_FilterPanel()

   /*Конструктор*/
   macro InitFilterPanel()
      var control, rows = 0;
      /*Параметры панели*/
      SetSize(29, 6); 
      SetPosition(10, 5);
      SetStatus("~F2~ Применить ~Esc~ Выход"); 
      SetCaption("Фильтр записей");

      addLabel(TRsbLabel(1, rows, "Дата:"));
      rows = rows + 1;
      addLabel(TRsbLabel(2, rows, "с"));
      control = TRsbEditField;
      control.name = "g_begin_date";
      control.dataType = 9/*FT_DATE*/;
      control.textLength = 10;
      control.setPosition(4, rows);
      control.setSize(10, 1);
      control.editable = true;
      control.enabled = true;
      control.value = g_begin_date;
      addControl(control);
      addLabel(TRsbLabel(15, rows, "по"));
      control = TRsbEditField;
      control.name = "g_end_date";
      control.dataType = 9/*FT_DATE*/;
      control.textLength = 10;
      control.setPosition(18, rows);
      control.setSize(10, 1);
      control.editable = true;
      control.enabled = true;
      control.value = g_end_date;
      addControl(control);
      rows = rows + 1;
      /*Состояния*/
      addLabel(TRsbLabel(1, rows, "Состояние:"));
      rows = rows + 1;
      /*Ожидание обработки*/
      control = TRsbCheckBox(1);
      control.name = "g_checked_ready";
      control.dataType = 12; /*FT_CHR*/
      control.setPosition(3, rows);
      control.checked = g_checked_ready;
      addControl(control);
      addLabel(TRsbLabel(5, rows, "ожидание обработки"));
      rows = rows + 1;
      /*В обработке*/
      control = TRsbCheckBox(1);
      control.name = "g_checked_inprocessing";
      control.dataType = 12; /*FT_CHR*/
      control.setPosition(3, rows);
      control.checked = g_checked_inprocessing;
      addControl(control);
      addLabel(TRsbLabel(5, rows, "в обработке"));
      rows = rows + 1;
      /*Ошибки*/
      control = TRsbCheckBox(1);
      control.name = "g_checked_errors";
      control.dataType = 12; /*FT_CHR*/
      control.setPosition(3, rows);
      control.checked = g_checked_errors;
      addControl(control);
      addLabel(TRsbLabel(5, rows, "с ошибкой"));
      rows = rows + 1;
      /*Успешные*/
      control = TRsbCheckBox(1);
      control.name = "g_checked_success";
      control.dataType = 12; /*FT_CHR*/
      control.setPosition(3, rows);
      control.checked = g_checked_success;
      addControl(control);
      addLabel(TRsbLabel(5, rows, "успешные"));
      rows = rows + 1;
   end;
    
   /*Обработчик смены фокуса*/
   macro eventHandlerRF(ev)
      if((this.focusedControl.name == "g_begin_date") or (this.focusedControl.name == "g_end_date"))
         if((getControl("g_begin_date").value != date(0,0,0)) and (getControl("g_end_date").value != date(0,0,0)) and (getControl("g_end_date").value < getControl("g_begin_date").value))
            msgboxex("Дата окончания периода не может быть меньше даты начала");
            if(this.focusedControl.name == "g_begin_date")
               getControl("g_begin_date").value = getControl("g_end_date").value;
            else
               getControl("g_end_date").value = getControl("g_begin_date").value;
            end;
         end;
      end;
      return true;
   end;
   
   /*Обработчик нажатия кнопок*/
   macro eventHandlerKP(ev)
      if(ev.keycode == 27/*Esc*/)
         g_fill = false;
         close(ev.keycode);
         return true;
      elif(ev.keycode == 316/*F2*/) 
         eventHandlerRF(ev);
         g_begin_date = getControl("g_begin_date").value;
         g_end_date = getControl("g_end_date").value;
         g_checked_ready = getControl("g_checked_ready").checked;
         g_checked_inprocessing = getControl("g_checked_inprocessing").checked;
         g_checked_errors = getControl("g_checked_errors").checked;
         g_checked_success = getControl("g_checked_success").checked;
         g_fill = true;
         close(ev.keycode);
         return true;
      end;
    end;
   
   /*Конструктор*/
   InitTRsbPanel();
   InitFilterPanel();
   
   /*Обработчики*/
   addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandlerKP"));
   addEventHandler(RSB_EV_REMOVE_FOCUS, r2m(this, "eventHandlerRF"));
end;


/**
@brief Класс скроллинга 
*/
private class TRslScroll ()
   var scr_name  = "";
   var scr_readonly = true;
   var colInfo;

   var title   = "";
   var status_line = "";

   macro numCol()
      if (colInfo)
         return colInfo.size / 6
      end
   end;

   macro IsOnPosition(sql)
      gotoscroll(sql);
      if ((sql.Value(0) != null) and (sql.Value(0) != nullval))
         return true;
      end;
      return false;
   end;

   macro AddCol(fld, head, width, fldtype, decpoint) 
      var ind = 0;
      if (not colInfo)
         colInfo = TArray;
      end;       
      ind = colInfo.Size/6;
      colInfo.value (ind * 6)      = fld;
      colInfo.value (ind * 6 + 1)  = head;
      colInfo.value (ind * 6 + 2)  = width;
      colInfo.value (ind * 6 + 3 ) = fldType;
      colInfo.value (ind * 6 + 4 ) = decPoint;
      colInfo.value (ind * 6 + 5 ) = 0;
   end;

   macro on_scr_key_13(sql, field, key_code)
   end;
   macro on_scr_key_32(sql, field, key_code)
   end;
   macro on_scr_key_PLUS(sql, field, key_code)
   end;
   macro on_scr_key_MINUS(sql, field, key_code)
   end;
   macro on_scr_key_F2(sql, field, key_code)
   end;
   macro on_scr_key_F3(sql, field, key_code)
   end;
   macro on_scr_key_F5(sql, field, key_code)
   end;
   macro on_scr_key_F6(sql, field, key_code)
   end;
   macro on_scr_key_F7(sql, field, key_code)
   end;
   macro on_scr_key_F8(sql, field, key_code)
   end;
   macro on_scr_key_F9(sql, field, key_code)
   end;
   macro on_scr_key_AltF6(sql, field, key_code)
   end;
   macro on_scr_key_AltF8(sql, field, key_code)
   end;
   macro on_scr_key_AltF9(sql, field, key_code)
   end;
   macro on_scr_key_CtrlF8(sql, field, key_code)
   end;
   macro on_scr_key_Esc(sql, field, key_code)
   end;
   macro on_scr_key_Ctrl_R(sql, field, key_code)
   end;
   macro on_scr_key_empty(sql, field, key_code)
   end;
   macro on_scr_init(sql, field, key_code)
   end;
   macro on_scr_mlt_beg(sql, field, key_code)
   end;
   macro on_scr_mlt_end(sql, field, key_code)
   end;
   macro on_scr_mlt_sel(sql, field, key_code)
   end;
   macro on_scr_preinit(sql, field, key_code)
   end;

   macro EvProc (sql, event, field, key_code)
      if (event == DLG_INIT)
         on_scr_init(sql, event, field, key_code);
      end;
      if (event == DLG_PREINIT)
         on_scr_preinit(sql, event, field, key_code);
      end;
      if (event == DLG_MSELSTART)
         return on_scr_mlt_beg(sql, field, key_code);
      end;
      if (event == DLG_MSEL)
         return on_scr_mlt_sel(sql, field, key_code);
      end;
      if (event == DLG_MSELEND)
         return on_scr_mlt_end(sql, field, key_code);
      end;
      if (event == DLG_KEY)
         if    (key_code==316)
               return on_scr_key_f2(sql, field, key_code);
         elif  (key_code==317)
               return on_scr_key_f3(sql, field, key_code);
         elif  (key_code==319)
               return on_scr_key_f5(sql, field, key_code);
         elif  (key_code==320)
               return on_scr_key_f6(sql, field, key_code);
         elif  (key_code==321)
               return on_scr_key_f7(sql, field, key_code);
         elif  (key_code==322)
               return on_scr_key_f8(sql, field, key_code);
         elif  (key_code==323)
               return on_scr_key_f9(sql, field, key_code);
         elif  (key_code==365) //ALT_F6
               return on_scr_key_altf6(sql, field, key_code);
         elif  (key_code==367) //ALT_F8
               return on_scr_key_altf8(sql, field, key_code);
         elif  (key_code==368) //ALT_F9
               return on_scr_key_altf9(sql, field, key_code);
         elif  (key_code==357) //CTRL_F8
               return on_scr_key_ctrlf8(sql, field, key_code);
         elif  (key_code==32)  //SPACE
               return on_scr_key_32(sql, field, key_code);
         elif  (key_code==13)  //ENTER)
               return on_scr_key_13(sql, field, key_code);
         elif  (key_code==217) //ESC
               return on_scr_key_Esc(sql, field, key_code);
         elif  (key_code == 43)  //PLUS серый
               return on_scr_key_PLUS(sql, field, key_code);
         elif  (key_code == 45)  //MINUS серый
               return on_scr_key_MINUS(sql, field, key_code);
         elif  (key_code == 0)
               return on_scr_key_empty(sql, field, key_code);
         elif  (key_code == 18) //CTRL_R
               return on_scr_key_Ctrl_R(sql, field, key_code);
         end;
      end; 
   
   end;

   macro Run (rs, X, Y, CX, CY)
      return RunScroll (rs, numCol, colInfo, scr_name, R2M(this,"EvProc"), title, status_line, scr_readonly, X, Y, CX, CY);
   end;

onerror(err);
   msgbox(err.Message);
   return false;
end;

/**
@brief Скроллинг уведомлений КИ
*/
class (TRslScroll) TMsgScroll
   var flag_end = false, v_LineId = 0;
   
   /**
      @brief Проверка наличия и создание таблицы текущей обработки 
   */
   macro CheckAndCreateTable()
      var sqls, rsdc;

      sqls = String(
            "DECLARE   \n"
           ,"   cnt INTEGER;   \n"
           ,"   str VARCHAR2(2000);   \n"
           ,"   err VARCHAR2(2000);   \n"
           ,"BEGIN   \n"
           ,"   BEGIN   \n"
           ,"      SELECT COUNT(1) INTO cnt FROM User_Tables t WHERE t.Table_Name = 'DSCQINV_NOTIFY_TMP';   \n"
           ,"      IF cnt = 0 THEN   \n"
           ,"         str := 'create table DSCQINV_NOTIFY_TMP   \n"
           ,"                 ( T_ID            NUMBER(10)            ,    \n"
           ,"                   T_PARTYID       NUMBER(10)            DEFAULT 0,   \n"
           ,"                   T_SFCONTRID     NUMBER(10)            DEFAULT 0,   \n"
           ,"                   T_DLCONTRID     NUMBER(10)            DEFAULT 0,   \n"
           ,"                   T_LASTREGDATE   DATE                  DEFAULT TO_DATE(''01.01.0001'',''DD.MM.YYYY''),   \n"
           ,"                   T_CONTROLDATE   DATE                  DEFAULT TO_DATE(''01.01.0001'',''DD.MM.YYYY''),   \n"
           ,"                   T_MESSAGETYPEID NUMBER(10)            DEFAULT 0,   \n"
           ,"                   T_MESSAGETYPE   VARCHAR2(70 BYTE)     DEFAULT chr(1),   \n"
           ,"                   T_SENDDATE      DATE                  DEFAULT TO_DATE(''01.01.0001'',''DD.MM.YYYY''),   \n"
           ,"                   T_FACTSENDDATE  DATE                  DEFAULT TO_DATE(''01.01.0001'',''DD.MM.YYYY''),   \n"
           ,"                   T_SENDSTATE     NUMBER(5)             DEFAULT 0,   \n"
           ,"                   T_SENDSTATE_TEXT VARCHAR2(15)         DEFAULT chr(1),   \n"
           ,"                   T_ERROR         VARCHAR2(2000 CHAR)   DEFAULT CHR(1),   \n"
           ,"                   T_PATH          VARCHAR2(2000 CHAR)   DEFAULT CHR(1),   \n"
           ,"                   t_countsend     NUMBER(10)            DEFAULT 0,   \n"
           ,"                   T_MESSAGEID     NUMBER(10)            DEFAULT 0,   \n"
           ,"                   T_MESSAGEEXIST  char(1)               DEFAULT chr(0),   \n"
           ,"                   t_shortname     varchar2(60 byte)     DEFAULT chr(1),   \n"
           ,"                   t_name          varchar2(320 byte)    DEFAULT chr(1),   \n"
           ,"                   t_number        varchar2(64 byte)     DEFAULT chr(1),   \n"
           ,"                   t_code          varchar2(35 byte)     DEFAULT chr(1)   \n"
           ,"                  )';     \n"
           ,"         EXECUTE IMMEDIATE str;   \n"
           ,"      END IF;   \n"
           ,"   EXCEPTION   \n"
           ,"      WHEN OTHERS THEN   \n"
           ,"         err := Nvl(err, Dbms_Utility.Format_Error_Stack || ' ' || Dbms_Utility.Format_Error_Backtrace);   \n"
           ,"   END;   \n"
           ,"   :err := err;   \n"
           ,"END;   \n"
          );
      rsdc = rsdcommand(sqls);
      rsdc.AddParam("err", RSDBP_OUT, v_string, 2000);
      rsdc.execute;
      rsdc = null;

   onerror(err);
   end;

   /**
   @brief Добавить в строку новый элемент. Элементы разделены запятой
   @return Новая строка
   
   Для формирования списков "строка1" -> "строка1,строка2" и т.д.
   Начальная строка должна быть равна ""
   */
   private macro AddToStringList(currentList: String, stringToAdd: String): String
     var result: String;
     
     if (StrLen(currentList) == 0)
       result = stringToAdd;
     else
       result = currentList + "," + stringToAdd;
     end;
     
     return result;
   end;

   /**
      @brief Отобрать подходящие записи и вставить их в таблицу
      
      @return true
      @return false
   */
   macro ChooseRecords()
      var sqls, rsdc;

      sqls = String( 
                     "DECLARE   \n"
                    ,"   v_Msg DSCQINV_NOTIFY_TMP.t_error%TYPE;   \n"
                    ,"   v_Err VARCHAR2(2000);   \n"
                    ,"BEGIN   \n"
                    ,"   DELETE FROM DSCQINV_NOTIFY_TMP;   \n"
                    ,"   INSERT INTO DSCQINV_NOTIFY_TMP   \n"
                    ,"      (T_ID   \n"
                    ,"      ,T_PARTYID   \n"
                    ,"      ,T_SFCONTRID   \n"
                    ,"      ,T_DLCONTRID   \n"
                    ,"      ,T_LASTREGDATE   \n"
                    ,"      ,T_CONTROLDATE   \n"
                    ,"      ,T_MESSAGETYPEID   \n"
                    ,"      ,T_MESSAGETYPE   \n"
                    ,"      ,T_SENDDATE   \n"
                    ,"      ,T_FACTSENDDATE   \n"
                    ,"      ,T_SENDSTATE   \n"
                    ,"      ,T_SENDSTATE_TEXT   \n"
                    ,"      ,T_ERROR   \n"
                    ,"      ,T_PATH   \n"
                    ,"      ,T_SHORTNAME   \n"
                    ,"      ,T_NAME   \n"
                    ,"      ,T_NUMBER   \n"
                    ,"      ,t_code   \n"
                    ,"      ,t_Countsend   \n"
                    ,"      ,T_MESSAGEID   \n"
                    ,"      ,T_MESSAGEEXIST)   \n"
                    ,"   SELECT n.t_id, n.t_partyid, n.t_sfcontrid, n.t_dlcontrid, n.t_lastregdate, n.t_controldate, n.t_messagetypeid,   \n"
                    ,"          (select t_name from dllvalues_dbt where t_list = :plist and t_element = n.t_messagetypeid) t_messagetype,   \n"
                    ,"          n.t_senddate, n.t_factsenddate, n.t_sendstate,    \n"
                    ,"          case n.t_sendstate   \n"
                    ,"              when 0 then 'Ожидает'   \n"
                    ,"              when 1 then 'Отправлено'   \n"
                    ,"              when 2 then 'В обработке'   \n"
                    ,"              when 100 then 'Ошибка'   \n"
                    ,"              else '-'   \n"
                    ,"          end t_sendstate_text, n.t_error,   \n"
                    ,"          n.t_path, p.t_shortname, p.t_name, sf.t_number, o.t_code, 0 t_Countsend, n.t_messageid,   \n"
                    ,"          case   \n"
                    ,"             when nvl(n.t_messageid,0) > 0 then 'X'   \n"
                    ,"              else chr(0)   \n"
                    ,"          end    \n");
         if(not GetRegValueSwitchQI())//NEW PARAM           
            sqls  = sqls + String(
                      "     FROM DSCQINV_NOTIFY_DBT n, dparty_dbt p, dsfcontr_dbt sf, dobjcode_dbt o   \n"
                     ,"    where p.t_partyid = n.t_partyid   \n"
                     ,"      and sf.t_id = n.t_sfcontrid    \n"
                     ,"      and o.t_objectid = p.t_partyid and o.t_codekind = :pcodekind and o.t_objecttype = :pobjecttype and t_bankclosedate = to_date('01010001', 'ddmmyyyy') \n");
         else
            sqls  = sqls + String(
                     " FROM DSCQINV_NOTIFY_DBT n \n"
                    ,"       JOIN dparty_dbt p   ON p.t_partyid = n.t_partyid      \n"
                    ,"       JOIN dobjcode_dbt o ON o.t_objectid = p.t_partyid     \n"
                    ,"                          AND o.t_codekind = :pcodekind      \n"
                    ,"                          AND o.t_objecttype = :pobjecttype  \n"  
                    ,"       LEFT JOIN dsfcontr_dbt sf ON sf.t_id = n.t_sfcontrid  \n"  
                    ,"    WHERE t_bankclosedate = to_date('01010001', 'ddmmyyyy')  \n")
         end;

      if (g_begin_date != date(0,0,0))
         sqls = String(sqls
                    ,"      and n.t_createdatetime > ", GetSQLDate(g_begin_date), " \n");
      end;
      if( g_end_date != date(0,0,0))
         sqls = String(sqls 
                    ,"      and n.t_createdatetime < ", GetSQLDate(g_end_date+1), " \n");
      end;

      if (g_checked_errors and g_checked_success and g_checked_ready and g_checked_inprocessing) // все состояния, фильтра нет
      elif ( (not g_checked_errors) and (not g_checked_success) and (not g_checked_ready) and (not g_checked_inprocessing)) // ни одного?
         sqls = String(sqls 
                    ,"      and n.t_sendstate not in (",C_READY_STATE,",",C_ERROR_STATE,",",C_SUCCESS_STATE,",",C_PROCESSING_STATE,") \n");
      else // перебор вариантов
         var stateFilter: String = "";
         
         if (g_checked_ready)
           stateFilter = AddToStringList(stateFilter, String(C_READY_STATE));
         end;
         
         if (g_checked_success)
           stateFilter = AddToStringList(stateFilter, String(C_SUCCESS_STATE));
         end;
         
         if (g_checked_inprocessing)
           stateFilter = AddToStringList(stateFilter, String(C_PROCESSING_STATE));
         end;
         
         if (g_checked_errors)
           stateFilter = AddToStringList(stateFilter, String(C_ERROR_STATE));
         end;
         
         if (Index(stateFilter, ",") == 0)
           sqls = sqls + 
                     "      and n.t_sendstate = " + stateFilter + " \n";
         else
           sqls = sqls + 
                     "      and n.t_sendstate in (" + stateFilter + ") \n";
         end;
      end; 
      
      sqls = String(sqls, "; "
                    ,"   COMMIT;   \n"
                    ,"EXCEPTION   \n"
                    ,"   WHEN OTHERS THEN   \n"
                    ,"      ROLLBACK;   \n"
                    ,"      :p_Err := v_Err;   \n"
                    ,"END;   \n"
                   );
      rsdc = rsdcommand(sqls);
      rsdc.AddParam("plist",        RSDBP_IN, C_NUMBER_LIST);
      rsdc.AddParam("pcodekind",    RSDBP_IN, C_USE_CODEKIND);
      rsdc.AddParam("pobjecttype",  RSDBP_IN, OBJTYPE_PARTY);
      rsdc.AddParam("p_Err",        RSDBP_OUT, v_string, 2000);
      rsdc.execute;
      if (not rsdc)
         runerror("Ошибка получения списка");
      end;
      sqls = rsdc.Value("p_Err");
      if ((sqls != null) and (sqls != nullval))
         runerror("Ошибка получения данных", sqls);
      end;    
      rsdc = null;

      return true;
   onerror(e)
      if ((e.Err != null) or (e.Err != nullval))
         msgbox(e.message + getFullCmdErrorText(rsdc));
      else 
         msgbox(e.Message + " " + e.Module + " " + e.Line);
      end;
      rsdc = null;

      return false;
   end;
   

   /**
      @brief Очистить выбранные записи
   */
   macro ClearChosen()
      var sqls, rsdc;

      sqls = String( 
         "DECLARE   \n"
        ,"   v_Ses NUMBER(10) := Sys_Context('Userenv', 'SESSIONID');   \n"
        ,"BEGIN   \n"
        ,"   UPDATE DSCQINV_NOTIFY_TMP t SET t.t_Countsend = 0 WHERE t.t_Countsend IN (v_Ses, -1 * v_Ses);   \n"
        ,"   COMMIT;   \n"
        ,"END;   \n"
       );
      rsdc = rsdcommand(sqls);
      rsdc.execute;
      rsdc = null;
   end;
   
   
   /**
      @brief Получить отобранные записи из временной таблицы
      
      @return RecordSet
      @return false
   */
   macro GetChosenData()
      var sqls, rsdc, rsds;

      sqls = String( "SELECT t_id   \n"
                    ,"      ,t_partyid   \n"
                    ,"      ,t_code   \n"
                    ,"      ,t_sfcontrid   \n"
                    ,"      ,t_dlcontrid   \n"
                    ,"      ,t_lastregdate   \n"
                    ,"      ,t_controldate   \n"
                    ,"      ,t_messagetype   \n"
                    ,"      ,t_senddate   \n"
                    ,"      ,to_char(t_factsenddate,'dd.mm.yyyy hh24:mi:ss') t_factsenddate  \n"
                    ,"      ,t_sendstate   \n"
                    ,"      ,t_sendstate_text   \n"
                    ,"      ,t_error   \n"
                    ,"      ,t_path   \n"
                    ,"      ,t_shortname   \n"
                    ,"      ,t_name   \n"
                    ,"      ,t_number   \n"
                    ,"      ,t_Countsend   \n"
                    ,"      ,t_messageid   \n"
                    ,"      ,ROWID rwd   \n"
                    ,"  FROM DSCQINV_NOTIFY_TMP   \n"
                    ," ORDER BY T_CONTROLDATE DESC, t_Partyid, t_dlcontrid   \n"
                   );
      rsdc = rsdcommand(sqls);
      rsds = rsdrecordset(rsdc, RSDVAL_CLIENT, RSDVAL_STATIC);
      rsds.Open;

      return rsds;
   onerror(e)
      rsdc = null;
      msgbox(e.Message + " " + e.Module + " " + e.Line);

      return null;
   end;
   

   /**
      @brief Обновить запись данными из БД
   */
   macro UpdateLine(p_Rwd, p_Msg)
      var sqls, rsdc;

      if ((p_Msg == null) or (p_Msg == nullval))
         p_Msg = "";
      end;
      sqls = String( 
            "DECLARE   \n"
           ,"   v_Rwd VARCHAR2(50) := :p_Rwd;   \n"
           ,"   v_state number(10) := :p_state;   \n"
           ,"BEGIN   \n"
           ,"   UPDATE DSCQINV_NOTIFY_TMP t   \n"
           ,"      SET (t_lastregdate   \n"
           ,"         ,t_senddate   \n"
           ,"         ,t_factsenddate   \n"
           ,"         ,t_sendstate   \n"
           ,"         ,t_sendstate_text   \n"
           ,"         ,t_error   \n"
           ,"         ,t_path   \n"
           ,"         ,t_messageid   \n"
           ,"         ,t_Countsend) =   \n"
           ,"          (SELECT n.t_lastregdate, n.t_senddate, n.t_factsenddate, n.t_sendstate, \n"
           ,"          case n.t_sendstate   \n"
           ,"              when 0 then 'Ожидает'   \n"
           ,"              when 1 then 'Отправлено'   \n"
           ,"              when 2 then 'В обработке'   \n"
           ,"              when 100 then 'Ошибка'   \n"
           ,"              else '-'   \n"
           ,"          end t_sendstate_text, n.t_error,   \n"
           ,"            n.t_path, n.t_messageid   \n"
           ,"                 ,CASE -- если успех - изменить признак на обработанный этой сессией   \n"
           ,"                     WHEN t_sendstate = v_state THEN   \n"
           ,"                      -1 * Abs(t.t_Countsend)   \n"
           ,"                     ELSE -- иначе освободить договор для других сессий   \n"
           ,"                      0   \n"
           ,"                  END t_Countsend   \n"
           ,"             FROM DSCQINV_NOTIFY_DBT n   \n"
           ,"            WHERE n.t_id = t.t_id)   \n"
           ,"    WHERE t.Rowid = v_Rwd;   \n"
           ,"   COMMIT;   \n"
           ,"EXCEPTION   \n"
           ,"   WHEN OTHERS THEN   \n"
           ,"      ROLLBACK;   \n"
           ,"END;   \n"
          );
      rsdc = rsdcommand(sqls);
      rsdc.AddParam("p_Rwd", RSDBP_IN, p_Rwd);
      rsdc.AddParam("p_state", RSDBP_IN, C_SUCCESS_STATE);
      rsdc.AddParam("p_Msg", RSDBP_IN, p_Msg);
      rsdc.execute;
      rsdc = null;

   onerror(e);
   end;

   /**
      @brief Запустить обработку записей
   */
   macro ProcessChosen()
      var sqls, rsdc, rsds;
      var v_result_msg = "";

      sqls = String( 
               "SELECT t.t_id, t.t_messagetypeid, t_sendstate, t.Rowid Rwd, COUNT(1) Over() r_All, Row_Number() Over(ORDER BY t.t_id) n_All   \n"
              ,"  FROM DSCQINV_NOTIFY_TMP t   \n"
              ," WHERE t.t_Countsend = Sys_Context('Userenv', 'SESSIONID')   \n"
              ," ORDER BY t.t_id   \n"
            );
      rsdc = rsdcommand(sqls);
      rsds = rsdrecordset(rsdc, RSDVAL_CLIENT_IF_NEEDED, RSDVAL_FORWARD_ONLY);
      rsds.Open;
      rsds.MoveNext();
      // если ничего не выбрано - никакой обработки
      if ((rsds.Value("r_All") == null) or (rsds.Value("r_All") == nullval) or (rsds.Value("r_All") == 0))
         rsds.Close;
         rsdc = rsds = null;
         
         return;
      end;
      
      Initprogress(int(rsds.Value("r_All")), "", "Обработка");
      SetDialogFlag(0);
      while (true)
         if ( (rsds.Value("t_sendstate") == C_READY_STATE) or (rsds.Value("t_sendstate") == C_ERROR_STATE) )
            if ( (rsds.Value("t_messagetypeid") == OVERDUE_QI) or (rsds.Value("t_messagetypeid") == CLAIM_QI) )
               v_result_msg = ProcessQISendNotify(rsds.Value("t_id"));  
            elif (rsds.Value("t_messagetypeid") == EXCLUSION_QI)
               v_result_msg = ProcessQIExclusion(rsds.Value("t_id"));  
            end;
            if (strlen(trim(v_result_msg)) == 0)
               UpdateLine(rsds.Value("rwd"), " Ок ");
            else
               UpdateLine(rsds.Value("rwd"), v_result_msg);
            end;
         end;

         if (not rsds.MoveNext())
            break;
         end;
         
         SetDialogFlag(1);
         Useprogress(int(rsds.Value("n_All")));
         SetDialogFlag(0);
      end;

      rsds.Close;
      rsdc = rsds = null;
      SetDialogFlag(1);
      remprogress();
      
   onerror(e)
      SetDialogFlag(1);
      remprogress();
      msgbox(e.Message);
   end;

   /**
      @brief Разметить отобранные записи
   */
   macro SaveProcessed(sql)
      var sqls, rsdc;
      sqls = String( 
         "DECLARE   \n"
        ,"   v_Rwd VARCHAR2(50) := :p_Rwd;   \n"
        ,"   v_Ses NUMBER(10) := Sys_Context('Userenv', 'SESSIONID');   \n"
        ,"   v_Err VARCHAR2(2000);   \n"
        ,"BEGIN   \n"
        ,"   BEGIN   \n"
        ,"      UPDATE DSCQINV_NOTIFY_TMP l   \n"
        ,"         SET l.t_Countsend = v_Ses   \n"
        ,"       WHERE l.Rowid = v_Rwd   \n"
        ,"         AND (Nvl(l.t_Countsend, 0) IN (0, v_Ses, -1 * v_Ses) OR NOT EXISTS   \n"
        ,"              (SELECT 1   \n"
        ,"                 FROM V$session v   \n"
        ,"                INNER JOIN Dregistry_Dbt r ON (r.t_Audsid = v.Audsid)   \n"
        ,"                WHERE v.Audsid = l.t_Countsend));   \n"
        ,"      IF SQL%ROWCOUNT = 0 THEN   \n"
        ,"         v_Err := 'Не найден или находится в обработке у другого пользователя';   \n"
        ,"         RAISE No_Data_Found;   \n"
        ,"      END IF;   \n"
        ,"      COMMIT;   \n"
        ,"   EXCEPTION   \n"
        ,"      WHEN OTHERS THEN   \n"
        ,"         ROLLBACK;   \n"
        ,"         v_Err := Nvl(v_Err, Dbms_Utility.Format_Error_Stack || ' ' || Dbms_Utility.Format_Error_Backtrace);   \n"
        ,"   END;   \n"
        ,"   :p_Err := v_Err;   \n"
        ,"END;   \n"
       );
      rsdc = rsdcommand(sqls);
      rsdc.AddParam("p_Rwd", RSDBP_IN, sql.Value("Rwd"));
      rsdc.AddParam("p_Err", RSDBP_OUT, v_string, 2000);
      rsdc.execute;
      sqls = rsdc.Value("p_Err");
      
      if ((sqls == null) or (sqls == nullval))
         sqls = "";
      end;
      rsdc = null;
      
      return sqls;
   onerror(e);
      sqls = e.Message;
      return sqls;
   end;
   
   /**
      @brief При инициализации скроллинга
   */
   macro on_scr_init(sql, field, key_code)
      AddMultiAction(sql, 316);
      while (sql.MoveNext())
         if (int(sql.Value("t_dlcontrid")) == v_LineId)
            gotoscroll(sql);
            break;
         end;
      end;
      if ((sql.Value("t_dlcontrid") != null) and (sql.Value("t_dlcontrid") != nullval) and (int(sql.Value("t_dlcontrid")) != v_LineId))
         sql.MoveFirst();
         gotoscroll(sql);
      end;
      v_LineId = -1;
   end;

   /**
      @brief Начало массового выделения записей
   */
   macro on_scr_mlt_beg(sql, field, key_code)
      flag_end = false;
      if (key_code == 316)
         v_LineId = -1;
         if (msgboxex("Отправить уведомления по выделенным записям? ", MB_YES + MB_NO, IND_YES) == IND_NO)
            return CM_MSEL_STOP_KEEP;
         end;
      end;
   end;

   /**
      @brief Конец массового выделения записей
   */
   macro on_scr_mlt_end(sql, field, key_code)  
      flag_end = true;
      // обработка отобранных
      if (key_code == 316)
         ProcessChosen();
      end;
   end;

   /**
      @brief Обработка выделенных записей
   */
   macro on_scr_mlt_sel(sql, field, key_code)
      var sel_rslt = "";
      
      if (key_code == 316)
         if (v_LineId < 0) 
            v_LineId = int(sql.Value("t_id"));
         end;
      end;
      sel_rslt = SaveProcessed(sql);
      
      return CM_MSEL_CONT_CLEAR;
   end;

   /**
      @brief Обработка клавищ
   */
   macro on_scr_key_empty(sql, field, key_code)
      if (flag_end)
         return cm_select;
      end;
   end;

   macro on_scr_key_F2(sql, field, key_code)
      var sel_rslt = "";
      
      if (not IsOnPosition(sql))
         return cm_ignore;
      end;
      if ( (sql.Value("t_sendstate") == C_READY_STATE) or (sql.Value("t_sendstate") == C_ERROR_STATE) )
         if (msgboxex(" Отправить уведомление? ", MB_YES + MB_NO, IND_YES) == IND_YES)
            v_LineId = int(sql.Value("t_id"));
            // расставить отметки для выбранных к обработке записей
            sel_rslt = SaveProcessed(sql);
            // обработка отобранных
            ProcessChosen();
            
            return cm_select;
         end;
      else 
         msgboxex("Невозможно переотправить в данном статусе");
         return cm_ignore;
      end;
      
      return cm_ignore;
   end;
   
   macro on_scr_key_F5(sql, field, key_code)
      var panel_filter = c_FilterPanel();
      panel_filter.run();
      if(g_fill)
         ChooseRecords();
         return CM_SELECT;
      end;
   end;

   macro on_scr_key_Ctrl_R(sql, field, key_code)
      if (not IsOnPosition(sql))
         v_LineId = -1;
      else 
         v_LineId = int(sql.Value("t_id"));
      end;
      if (not ChooseRecords())
         return cm_ignore;
      end;
      
      return cm_select;
   end;

   macro on_scr_key_F7(sql, field, key_code)
      file view () txt;
      var n_txt = GetTXTFileName("ERR_DETAIL." + UserNumber);
      
      if (not IsOnPosition(sql))
         return cm_ignore;
      end;
      setoutput(n_txt, false);
      println(SQL_ConvTypeStr(sql.Value("t_error")));
      setoutput(null, false);
      open(view,n_txt);
      viewfile(view);
      close(view);
   end;
   
   /**
      @brief Запуск скроллинга
   */
   macro run_list()
      var rsds;
      
      CheckAndCreateTable();
      if (not ChooseRecords())
         return false;
      end;
   
      AddCol ("t_code",          "Код субъекта",       12);
      AddCol ("T_shortname",     "Наименование",       25);
      if(not GetRegValueSwitchQI())//NEW PARAM
         AddCol ("t_number",        "Номер договора ",    13);
      end;      
      AddCol ("t_lastregdate",   "Посл.дата включения",12);
      AddCol ("t_controldate",   "Дата проверки",      12);
      AddCol ("t_messagetype",   "Тип уведомления",    20);
      AddCol ("t_senddate",      "Дата отправки",      12);
      AddCol ("t_factsenddate",  "Факт.дата отправки", 18);
      AddCol ("t_sendstate_text","Статус отправки",    10);
      AddCol ("t_error",         "Ошибка",             30);
      AddCol ("t_path",          "Путь",               30);
      AddCol ("t_messageid",     "Сообщение",          12);
   
      rsds = GetChosenData();
      if (rsds == null)
         return false;
      end;
      v_LineId = -1;
      while (Run(rsds))
         rsds = GetChosenData();
         if (rsds == null)
            return false;
         end;
      end;
      ClearChosen();
   end;

   InitTRslScroll();
   scr_name = "FILTR_CONTRACT";
   title    = "Журнал уведомлений по КИ";
   status_line  = "ESC-Выход  F2-Отправить F5-Фильтр F7-Вывести ошибку  Shift(Вниз/Вверх)-Выделить";

end;

if (not CheckUserRoles({oper}, РОЛЬ_12, РОЛЬ_14))
   MsgBox("Меню доступно только для ролей "+РОЛЬ_12+" и "+РОЛЬ_14);
   exit(1);
else
   TMsgScroll.run_list;
end;
exit(1);
