/*-----------------------------------------------------*/
/* Запроc активации Договора брокерского обслуживания  */
/*                                                     */
/* Автор: Гераськина Т.                                */
/*
*/
/*----------------------------------------------*/
import "func_lib.mac";
import "global_utils_intgr.mac";
import "uws_exchng_log.mac";
import "global_classes.mac";

private file attr("objattr.dbt") key 0;


// класс входящих параметров 
private class c_ActiveContractRequest(p_req_data_obj)
  var ContractNumber   : string;   // Номер Договора      Да
  var DossierURL       : string;   // Ссылка на досье     Да
  var DossierKind      ;   // Тип досье           Да
  var FilialId         ;   // Код РФ              Да
  var DossierId        ;   // Идентификатор обобщенного досье в АСОА Да

   
  /* Кратность параметров в соответствии со спецификацией */
  private macro uInitPrime(p_req_data_obj)
    private var err_txt;

    // проверка производится на этапе проверки XML, оставлено для контроля
    if(ValType(p_req_data_obj) != V_UNDEF)
      var nametag_up = "ActiveContractReq";
      
      ContractNumber  = getValueFromProperty(p_req_data_obj, "ContractNumber" , nametag_up, true);
      DossierURL      = getValueFromProperty(p_req_data_obj, "DossierURL"     , nametag_up, true);

      DossierKind     = getDictionaryFromProperty(p_req_data_obj, "DossierKind", nametag_up, true);

      FilialId        = getSymbolFromProperty(p_req_data_obj, "FilialId"       , nametag_up, true);
      DossierId       = getSymbolFromProperty(p_req_data_obj, "DossierId"      , nametag_up, true);
     
    else
      err_txt = string(InErrComPart, "ActiveContractReq");
      RunError(err_txt, c_err_obj(err_txt,ERR_CODE_NO_REQUIRED_PRM));
    end;
  end; /* macro uInitPrime() */

  uInitPrime(p_req_data_obj);
end;


macro RunActiveContract(req_obj_serv)
  var res = c_Error;
  var err_txt;
  
  var ObjCat;
  var dlcontr_id, stat;
  var ObjType = 207;  // договор брокерского обслуживания
  var ObjID;
  var cur_status = "0";
  var new_status = "1";

  dlcontr_id = FindDlContrByNumber(req_obj_serv.ContractNumber);
  if (dlcontr_id > 0)  // договор найден
    ObjID = String(dlcontr_id:o:34);

    ObjCat = RsbObjCategories(ObjType, ObjID);
    if (ObjCat.GetMainAttr( CONTRACT_CATEGORY_STATUS, {curdate}, attr ) == 0)
      cur_status = attr.Numinlist;
    end;
    
    if (cur_status == "0")  // категория пустая
      new_status = "2";     // АСОА подтверждено
    elif (cur_status == "1")  // получено подтверждение депозитария
      new_status = "3";     // обработка завершена
    elif (cur_status == "2") // получено подтверждение АСОА (второй раз пришел запрос?)
      new_status = "2";
    elif (cur_status == "3")  // обработка завершена (второй раз пришел запрос?)
      //err_txt = "Текущее состояние договора: <Обработка завершена>. Обновление невозможно";
      //RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
      new_status = "3";     // обработка завершена
    end;
    
    stat = ObjCat.ConnectAttr(CONTRACT_CATEGORY_STATUS, NULL, "", new_status, {curdate} );
    if (stat > 0)
       err_txt = String("Ошибка "+stat+" установки категории ",CONTRACT_CATEGORY_STATUS,", значение ",new_status);
       RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
    end;

    stat = ObjCat.ConnectAttr(CONTRACT_CATEGORY_TYPEDOSSIER, NULL, "", req_obj_serv.DossierKind.RecordCode, {curdate} );
    if (stat > 0)
       err_txt = String("Ошибка "+stat+" установки категории ",CONTRACT_CATEGORY_TYPEDOSSIER,", значение ",req_obj_serv.DossierKind.RecordCode);
       RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
    end;

 
    stat = addnoteforobject(ObjType, ObjID, CONTRACT_NOTEKIND_CodeRF, req_obj_serv.FilialID.Objectid);
    if (stat != 0)
      err_txt = String("Ошибка "+stat+" установки примечания "+CONTRACT_NOTEKIND_CodeRF);
      RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
    end;

    stat = addnoteforobject(ObjType, ObjID, CONTRACT_NOTEKIND_DossierURL, req_obj_serv.DossierURL);
    if (stat != 0)
      err_txt = String("Ошибка "+stat+" установки примечания "+CONTRACT_NOTEKIND_DossierURL);
      RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
    end;

    stat = addnoteforobject(ObjType, ObjID, CONTRACT_NOTEKIND_DossierID, req_obj_serv.DossierId.ObjectID);
    if (stat != 0)
      err_txt = String("Ошибка "+stat+" установки примечания "+CONTRACT_NOTEKIND_DossierID);
      RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
    end;

    // обновление категории только после установки примечаний
    if(stat == 0)   // избыточное условие, но пусть будет
      stat = ObjCat.Save(ObjID);
      if (stat > 0)
        err_txt = String("Ошибка "+stat+" установки категорий ");
        RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
      end;
    end;

    if ( new_status == "3")     // обработка завершена
      //BIQ-7089 при включении закоментировать следующую строку
      //PutInProcess(ObjType, dlcontr_id); // для UpdateDossier BIQ-7089 Гераськина Т.В. досье больше не отправляется в АСОА
      //BIQ-7089 конец комментирования
      PutInProcess(5207, dlcontr_id); // для отправки уведомления
      PutInProcess(6207, dlcontr_id); // для отправки уведомления
    end;
    
  else
    err_txt = "Договор "+req_obj_serv.ContractNumber+" не найден";
    RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
  end;
  
  res.ErrorCode = 0;
  res.ErrorDesc = "OK";

  return res;

onError(err)
  res.ErrorCode = c_err_obj.obtain_err_code(err);
  res.ErrorDesc = c_err_obj.obtain_err_msg(err);
  
  return res;
end;


// непосредственная обработка
// формирование класса ответа
macro ActiveContract_run(p_req_data_obj)
  var ResultAction = c_ActiveContract_outObj;

  var req_obj_serv = NULL;
  var Res = NULL;

  req_obj_serv = c_ActiveContractRequest(p_req_data_obj);

  /* Вызываем сервис */
  Res = RunActiveContract(req_obj_serv);

  /* Получаем объектный вид ответа */
  if (Res.ErrorCode == 0)  // успешно
    ResultAction.ActiveContractResp.ResultCode = 0;
    ResultAction.ActiveContractResp.Error = NULL;
    ResultAction.ActiveContractResp.DossierId = c_IntegrationSymbolicIdentifierXType;
    ResultAction.ActiveContractResp.DossierId = req_obj_serv.DossierId;

  else 
    ResultAction.ActiveContractResp.ResultCode = 1;
    ResultAction.ActiveContractResp.Error = c_Error;
    ResultAction.ActiveContractResp.Error.ErrorCode = Res.ErrorCode;
    ResultAction.ActiveContractResp.Error.ErrorDesc = Res.ErrorDesc;
    ResultAction.ActiveContractResp.DossierId = c_IntegrationSymbolicIdentifierXType;
    ResultAction.ActiveContractResp.DossierId = req_obj_serv.DossierId;
  end;
  
  return ResultAction;

onError(err)
  /* Получаем объектный вид ответа */
  ResultAction.ActiveContractResp.ResultCode = 1;
  ResultAction.ActiveContractResp.Error = c_Error;
  ResultAction.ActiveContractResp.Error.ErrorCode = c_err_obj.obtain_err_code(err);
  ResultAction.ActiveContractResp.Error.ErrorDesc = c_err_obj.obtain_err_msg(err);
  ResultAction.ActiveContractResp.DossierId = c_IntegrationSymbolicIdentifierXType;
  ResultAction.ActiveContractResp.DossierId = req_obj_serv.DossierId;

  return ResultAction;
end;

/*----------------------------------------------*/
/*  Активация Договора брокерского обслуживания */
/*----------------------------------------------*/
macro ActiveContractReq(p_income_data, p_log_id, p_transparent_id)
  var ActiveContract_result = NULL;

  var v_result, v_xml;
  var err_txt="", out_code = 0;

  // BIQ-7089 Гераськина Т.В. Сервис больше не используется
  // при включении РАСкомментировать
  err_txt = "Сервис больше не используется";
  RunError(err_txt, c_err_obj(err_txt,UNIVERSAL_ERROR_CODE));


  if(ValType(p_income_data) == V_UNDEF)
    RunError(ERR_TEXT_NO_IN_MSG, c_err_obj(ERR_TEXT_NO_IN_MSG,
                                           ERR_CODE_NO_IN_PRM));
  end; 

  //готовый объект, преобразования вовне
  ActiveContract_result = ActiveContract_run(p_income_data);

  // итоговый класс 
  return ActiveContract_result;

onError(err)
  out_code = c_err_obj.obtain_err_code(err);
  err_txt = c_err_obj.obtain_err_msg(err);
  
  ActiveContract_result = c_ActiveContractResp;
  ActiveContract_result.ResultCode = 1;
  ActiveContract_result.Error = c_Error;
  ActiveContract_result.Error.ErrorCode = out_code;
  ActiveContract_result.Error.ErrorDesc = err_txt;
  ActiveContract_result.DossierId = c_IntegrationSymbolicIdentifierXType;
  ActiveContract_result.DossierId.ObjectID = "";

  return ActiveContract_result;
end;

