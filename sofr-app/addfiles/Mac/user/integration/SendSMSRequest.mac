/*---------------------------------------------*/
/* Адаптер инициализации процесса отправки СМС */
/* через СМС-центр (Процессинговый центр)      */
/* Поток "A5"                                  */
/*---------------------------------------------*/

import globals;
import "RequestWS.mac"; /* Отправка сообщения в очередь (и получение "синхронного" ответа) */
import "secur_prc_msg_transform.mac"; /* Преобразования */
import "uws_exchng_log.mac"; /* Журнализация */



/*------------------------------------------------*/
/* Основной класс процесса инициации отправки СМС */
/*------------------------------------------------*/
class c_pc_sms_submit_general()
   private const CV_ERR_CODE_DEFAULT = 1;

   private var v_service_code = 0;
   private var v_service_msg = "";

   var v_req_obj : object;
   var v_resp_obj : object;


   /**/
   macro m_get_service_code()
      return v_service_code;
   end;


   /**/
   macro m_get_service_msg()
      return v_service_msg;
   end;


   /*----------------------------------------*/
   /* Метод отправки запроса на отправку СМС */
   /*----------------------------------------*/
   macro m_submit_sms_req(p_synch_timeout)
      const CV_SEND_SMS_DATA_TYPE = 22;
      const CV_SOURCE_BRANCH = "SOFR";
      const CV_DEST_BRANCH = "PC";
      var v_ret = true;
      var v_transformator;
      var v_logger_obj;
      var v_log_id;
      var v_req_msg;
      var v_resp_msg;
      var v_queue_resp;

      v_transformator = c_secur_msg_converter();
      v_req_msg = v_transformator.m_secur_external_req(v_req_obj);
      v_service_msg = v_transformator.get_service_msg();
      /* Добавляем алиасы пространств имен (пока так) - begin */
      v_req_msg = strsubst(v_req_msg, "<SendSMSRequest", "<ns0:SendSMSRequest");
      v_req_msg = strsubst(v_req_msg, "</SendSMSRequest>", "</ns0:SendSMSRequest>");
      v_req_msg = strsubst(v_req_msg, "xmlns=\"", "xmlns:ns0=\"");
      v_req_msg = strsubst(v_req_msg, "<DboNPMessage>", "<ns0:DboNPMessage>");
      v_req_msg = strsubst(v_req_msg, "</DboNPMessage>", "</ns0:DboNPMessage>");
      /* Добавляем алиасы пространств имен (пока так) - end */
      if(v_service_msg == "")
         v_logger_obj = uws_exchng_logger(null,
                                          null,
                                          XmlRpcRequestId(),
                                          "SOFR_SendSMS",
                                          modulename(),
                                          v_req_obj.SendSMSRequest.DboNPMessage.MessageId.ObjectId,
                                          v_req_msg);
         v_log_id = v_logger_obj.get_log_id();

         v_queue_resp = SubmitRequestToWS(2,                     // ИД очереди.
                                          "",                    // ИД сообщения.
                                          "",                    // Идентификатор Бэк Офиса. (не обрабатывается).
                                          true,                  // Признак синхронной обработки. Если не указан, = False
                                          CV_SOURCE_BRANCH,      // Подразделение системы-источника
                                          CV_SEND_SMS_DATA_TYPE, // ИД типа данных
                                          v_req_msg,             // Бизнес данные в виде XML.
                                          string(v_log_id),      // Сквозной идентификатор
                                          CV_DEST_BRANCH,        // Подразделение системы-назначения
                                          p_synch_timeout        // Значение времени ожидания
                                          );

         if (v_queue_resp.ResultCode == 0)
            v_resp_msg = v_queue_resp.responsetext;
            v_logger_obj.add_response(v_resp_msg); 
         else 
            v_logger_obj.add_error_info(v_queue_resp.ResultCode, v_queue_resp.ResultText);
            v_ret = false;
         end;

         v_transformator = NULL;
         if (v_resp_msg)
            /* Преобразуем ответ в объектный вид - begin */
            v_transformator = c_secur_msg_converter();
            if(not v_transformator.m_decode_escaped_char(v_resp_msg))
               v_service_msg = v_transformator.get_service_msg();
               RunError(v_service_msg, c_usr_err_obj(v_service_msg));
            end;
            //v_resp_obj = v_transformator.m_secur_external_resp(v_transformator.get_pure_msg());
            v_resp_obj = v_transformator.m_secur_wol_external_resp(v_transformator.get_pure_msg());
            /* Преобразуем ответ в объектный вид - end */

            if(ValType(v_resp_obj) == V_UNDEF)
               v_service_msg = "Не удалось преобразовать XML в объект";
               RunError(v_service_msg, c_usr_err_obj(v_service_msg)); 
            end;
         end;
      else
         /* Ошибка преобразования */
         RunError(v_service_msg, c_usr_err_obj(v_service_msg));
      end;

      return v_ret;

   onError(err)
      v_ret = false;
      v_service_code = CV_ERR_CODE_DEFAULT;
      v_service_msg = c_usr_err_obj.obtain_err_msg(err);

      return v_ret;
   end; /* macro m_submit_sms_req() */

end; /* class c_sms_submit_general() */