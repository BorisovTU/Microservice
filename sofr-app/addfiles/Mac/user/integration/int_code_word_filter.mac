import RsbFormsInter;

// Константы типа поля
private const FT_CHR    = 12;
private const FT_DATE   =  9;
private const FT_STRING =  7;

// Константы клавиш
private const K_ENTER =  13,
              K_F2    = 316,
              K_F3    = 317,
              K_ESC   =  27;

private macro IIF(condition, valueOnTrue, valueOnFalse)
  if (condition)
    return valueOnTrue;
  end;
  
  return valueOnFalse;
end;

private macro StrListAdd(currentStringList: String, delimiter: String, newListValue: String): String
  return currentStringList + IIF(StrLen(currentStringList) > 0, delimiter, "") + newListValue;
end;

class CCodewordFilterParams()
  var clientID: Integer = 0;
  var clientCode: String = "";
  var tradingCode: String = "";
  var codeWord: String = "";
  var clientName: String = "";
  var contractName: String = "";
  var clientEmail: String = "";
  var clientPhone: String = "";
  var isValidCodesOnly: Bool = false;
  
  macro IsHighLimiting()
    return ((clientID > 0) or
            (StrLen(clientCode) > 1) or
            (StrLen(tradingCode) > 1) or
            (StrLen(clientName) > 3) or
            (StrLen(contractName) > 3) or
            (StrLen(codeWord) > 3) or
            (StrLen(clientPhone) > 3) or
            (StrLen(clientEmail) > 3)); 
  end;
  
  macro GetFilter()
    var filter = "";
    var subQuery = "";
    
    if (clientID > 0)
      filter = "sf_cntr.t_partyid = " + clientID;
    end;
    
    if (StrLen(clientCode) > 0)
      filter = StrListAdd(filter, " AND ", 
               "dbo_cntr.t_dlcontrid in (SELECT t_objectid " +
                                          "FROM ddlobjcode_dbt code " +
                                         "WHERE code.t_objecttype = 207 " +
                                           "AND code.t_codekind = 1 " +
                                           "AND code.t_BankCloseDate = TO_DATE('01010001', 'DDMMYYYY') "
                                           "AND code.t_code LIKE '" + clientCode + "%')");
    end;
    
    if (StrLen(tradingCode) > 0)
      filter = StrListAdd(filter, " AND ",  "mp_cntr.t_mpCode LIKE '" + StrUpr(tradingCode) + "%'");
    end;
    
    if (StrLen(codeWord) > 0)
      filter = StrListAdd(filter, " AND ", "UPPER(cw_time.t_code_word) LIKE '" + StrUpr(codeWord) + "%'");
    end;
    
    if (StrLen(clientName) > 0)
      filter = StrListAdd(filter, " AND ", "cl_cntr.t_normalizedName LIKE '" + StrUpr(clientName) + "%'");
    end;
    
    if (StrLen(contractName) > 0)
      filter = StrListAdd(filter, " AND ", "sf_cntr.t_number like '" + contractName + "%'");
    end;
    
    if (StrLen(clientEmail) > 0)
      subQuery = "SELECT t_partyid " +
                   "FROM dcontact_dbt " +
                  "WHERE t_addresstype = 1 " +
                    "AND t_ismain = CHR(88) " +
                    "AND t_contactkind = 5 " +
                    "AND t_contacttype IN (8, 1005) " +
                    "AND LOWER(t_value) LIKE '%" + StrLwr(clientEmail) + "%'";
    end;
    
    if (StrLen(clientPhone) > 0)
      subQuery = StrListAdd(subQuery, " UNION ", "SELECT t_partyid " +
                                                   "FROM dcontact_dbt " +
                                                  "WHERE t_addresstype = 1 " +
                                                    "AND t_ismain = CHR(88) " +
                                                    "AND t_contactkind = 1 " +
                                                    "AND t_contacttype in (1, 1004) " +
                                                    "AND LOWER(t_value) LIKE '%" + clientPhone + "%'");
    end;
    
    if (StrLen(subQuery) > 0)
      filter = StrListAdd(filter, " AND ", "sf_cntr.t_partyid in (" + subQuery + ")");
    end;
    
    if (isValidCodesOnly)
      filter = StrListAdd(filter, " AND ", "cw_time.t_cw_is_active = 'X' AND cw_time.t_cw_compromised != 'X'", " AND ");
    end;
    
    return filter;
  end;
end; //CFilterParams

class(TRsbPanel) CCodewordFilterPanel(filterParams: CCodewordFilterParams)
  private var params;
  
  private macro NewLabel(x: Integer, y: Integer, caption: String)
    AddLabel(TRsbLabel(x, y, caption));
  end;
  
  private macro NewCheckbox(x: Integer, y: Integer, controlName: String, isChecked: Bool)
    var checkbox: Object;
    
    checkbox = TRsbCheckbox(1);
    checkbox.name = controlName;
    checkbox.dataType = FT_CHR;
    checkbox.SetPosition(x, y);
    checkbox.checked = isChecked;
    AddControl(checkbox);
  end;
  
  private macro NewEditField(x: Integer, y: Integer, w: Integer, h: Integer, controlName: String, type: Integer, length: Integer, editable: Bool, value: Variant)
    var edit: Object;
    
    edit = TRsbEditField(type);
    edit.name = controlName;
    edit.dataType = type;
    edit.SetPosition(x, y);
    edit.SetSize(w, h);
    edit.TextLength = length;
    edit.Value = value;
    edit.Editable = editable;
    edit.Focusable = true;
    edit.Enabled = true;
    AddControl(edit);
    
    return edit;
  end;
  
  macro EventProc(event: Object)
    if (event.keyCode == K_ESC)
      this.Close(event.keyCode);
    elif (event.keyCode == K_F2)
      var clientID = this.GetControl("clientID").value;
      
      if (StrLen(clientID) == 0)
        clientID = "0";
      elif (not StrIsNumber(clientID))
        MsgBox("Некорректное значение в поле ID клиента");
        return;
      end;
      
      params.clientID = Int(clientID);
      params.clientCode = this.GetControl("clientCode").value;
      params.tradingCode = this.GetControl("tradingCode").value;
      params.clientName = this.GetControl("clientName").value;
      params.contractName = this.GetControl("contractName").value;
      params.clientPhone = this.GetControl("clientPhone").value;
      params.clientEmail = this.GetControl("clientEmail").value;
      params.codeWord = this.GetControl("codeWord").value;
      params.isValidCodesOnly = this.GetControl("isValidCodesOnly").checked;
      
      this.Close(event.keyCode);
    end;
  end;
  
  InitTRsbPanel();
  params = filterParams;

  SetCaption("Фильтр скроллинга");
  SetStatus("~Esc~ Выход   ~F2~ Применить   ~F3~ Выбор");
  SetSize(44, 15);
  SetPosition(45,10);
  
  var idValue = String(params.clientID);
  if (idValue == "0")
    idValue = "";
  end;
  
  NewLabel(2,  2, "Уникальный код клиента:");   NewEditField(20,  2, 22, 1, "clientCode",   FT_STRING,  50, true, params.clientCode);
  NewLabel(2,  3, "Код на торговой площадке:"); NewEditField(20,  3, 22, 1, "tradingCode",  FT_STRING,  50, true, params.tradingCode);
  
  NewLabel(2,  5, "ФИО/Наименование:");         NewEditField(20,  5, 22, 1, "clientName",   FT_STRING, 320, true, params.clientName);
  NewLabel(2,  6, "Номер договора БО:");        NewEditField(20,  6, 22, 1, "contractName", FT_STRING,  64, true, params.contractName);
  NewLabel(2,  7, "ID клиента:");               NewEditField(20,  7, 22, 1, "clientID",     FT_STRING,  50, true, idValue);
  
  NewLabel(2,  9, "Телефон:");                  NewEditField(20,  9, 22, 1, "clientPhone",  FT_STRING, 100, true, params.clientPhone);
  NewLabel(2, 10, "E-mail:");                   NewEditField(20, 10, 22, 1, "clientEmail",  FT_STRING, 100, true, params.clientEmail);
  NewLabel(2, 11, "Кодовое слово:");            NewEditField(20, 11, 22, 1, "codeWord",     FT_STRING, 200, true, params.codeWord);
  NewCheckbox(2, 13, "isValidCodesOnly", params.isValidCodesOnly); NewLabel(20, 13, "только договоры с действующим кодовым словом");

  AddEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "EventProc"));
end;  //CFilterPanel