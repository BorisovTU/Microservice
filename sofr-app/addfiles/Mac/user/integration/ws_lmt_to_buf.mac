//А.Киселев 08.11.2018
import globals, oralib, likepy;
import "ulmt_to_file.mac", "usr_table_int.mac";

private const REG_EXPORT_PATH = "РСХБ\\ИНТЕГРАЦИЯ\\ДИРЕКТОРИИ\\EXPORT\\QUIK_ЛИМИТЫ"; //путь для выгрузки файла

private class (c_usr_tbl_base) c_usr_tbl_uTableProcessOut()
   private class gen_uTableProcessOut()
      var T_RECID :integer,
          T_OBJECTTYPE :integer,
          T_STATUS :integer,
          T_TIMESTAMP :date,
          T_RESULTCODE :string,
          T_RESULTTEXT :string;
   end;

   private macro init_table_obj()
      new_val_obj = gen_uTableProcessOut();
      where_val_obj = gen_uTableProcessOut();
   end;

   Initc_usr_tbl_base("uTableProcessOut_dbt");

   init_table_obj();
end;


//класс вызываемых параметров ProcessOut
private class TParamProcess( StrParam :string )
var RecId :integer = 0,
    ObjectType :integer = 0,
    ProcessType :integer = 0,
    ProcessStatus :integer = 0;

private var StrSeparator :string = ";";

 private macro InitParam( StrParam :string )
 var StrDigit :string = "0123456789",
     StrParamBuf :string = "",
     i :integer = 0,
     StrPos :integer = 0;

  if( (Valtype(StrParam) != V_UNDEF) and (Valtype(StrParam) != 26) and (StrParam != "") )
   i = 1;
   while( i <= StrLen(StrParam) ) //нормализация оставить, что только цифры или разделитель

    if( Index( (StrDigit + StrSeparator), Substr(StrParam, i, 1) ) >= 1 )
     StrParamBuf = StrParamBuf + Substr(StrParam, i, 1);

    end;
    i = i + 1;

   end;

   i = 0;
   while( (StrLen(StrParamBuf) > 0) and (GenNumProps(this) > i) ) //заполняем свойства
    StrPos = Index( StrParamBuf, StrSeparator );
    if( StrPos > 1 )
     this[i] =  int(Substr(StrParamBuf, 1, StrPos - 1));
     StrParamBuf = Substr(StrParamBuf, StrPos + 1);
    elif( (StrPos == 0) and (StrLen(StrParamBuf) > 0) )
     this[i] = int(StrParamBuf);
     StrParamBuf = "";
    end;
    i = i + 1;

   end;

  end;

 onError(err)

  RecId = 0;
  ObjectType = 0;
  ProcessStatus = 0;


 end;

 InitParam( StrParam );

end;


//сервис загрузки лимитов
macro ws_lmt_to_buf( Str_Param :string, HM :bool ) :integer
var state :integer = 0,
    errortext :string = "",
    Params :TArray,
    ParamProcess :TParamProcess,
    ProcessOutTbl :c_usr_tbl_uTableProcessOut,
    StrPath :string = "",
    ErrCode;



 if( not GetParm(2, Str_Param) )
  Str_Param = "";
 end;

 //загрузили буферные таблицы из дистрибутивных
 state = execStoredFunc( "USR_PKG_IMPORT_SOFR.AddInudl_lmtcashstock_exch", V_INTEGER );

 if( state != 0 )
  return state;
 end;

 state = execStoredFunc( "USR_PKG_IMPORT_SOFR.AddInudl_lmtsecuritest_exch", V_INTEGER );

 if( state != 0 )
  return state;
 end;

 state = execStoredFunc( "USR_PKG_IMPORT_SOFR.AddInudl_lmtfuturmark_exch", V_INTEGER );

 if( state != 0 )
  return state;
 end;
 //загрузили буферные таблицы из дистрибутивных


//формируем файл
 Params = Null;
 Params = makeArray( SQLParam("p_Mode", 0), //формируем файл по лимитам
                     SQLParam("p_IsDel", 1 ), //удалять ранее сформированный файл с таким именем
                     SQLParam("p_FileName", "bebe_cuporos") );
 state = execStoredFunc( "USR_PKG_IMPORT_SOFR.AddLOBToTMP", V_INTEGER, Params );
 Params = Null;
 if( state != 0 )
  return state;
 end;

//получим значение пути выгрузки из реестра
 GetRegistryValue(REG_EXPORT_PATH, V_STRING, StrPath, ErrCode);
 if( ErrCode > 0 )
  StrPath = "..\\QUIK_EXCHNG\\out";
 end;
//получим значение пути выгрузки из реестра


 state = CreateOutputFileLMT(/*"$C:\\RsBank60"*/ StrPath );
//формируем файл

 if( state != 0 )
  return state;
 end;

if (not HM)
 //установка статуса в ProcessOut
 ParamProcess = Null;
 ProcessOutTbl = Null;
 ParamProcess = TParamProcess( Str_Param + ";2"); //установить статусы 2
 ProcessOutTbl = c_usr_tbl_uTableProcessOut();
 ProcessOutTbl.New_Val_Obj.T_STATUS = ParamProcess.ProcessStatus;
 ProcessOutTbl.Where_Val_Obj.T_RECID = ParamProcess.RecId;
 ProcessOutTbl.Where_Val_Obj.T_OBJECTTYPE = ParamProcess.ObjectType;
 if( not ProcessOutTbl.Update() ) //инструмент простых действий с таблицами из RSL В.Карпов
  state = 1;
 end;
 ParamProcess = Null;
 ProcessOutTbl = Null;
 //установка статуса в ProcessOut
end;
 return state;

onError(err)
 Params = Null;
 ParamProcess = Null;
 ProcessOutTbl = Null;
 state = 1;
 return state;

end;
//сервис загрузки лимитов


//сервис загрузки корректировок лимитов
macro ws_lmtchange_to_buf( Str_Param :string ) :integer
var state :integer = 0,
    errortext :string = "",
    Params :TArray,
    ParamProcess :TParamProcess,
    ProcessOutTbl :c_usr_tbl_uTableProcessOut,
    StrPath :string = "",
    ErrCode;



 if( not GetParm(2, Str_Param) )
  Str_Param = "";
 end;

 //загрузили буферные таблицы из дистрибутивных
 state = execStoredFunc( "USR_PKG_IMPORT_SOFR.AddInudl_dl_lmtadjust_exch", V_INTEGER );

 if( state != 0 )
  return state;
 end;
 //загрузили буферные таблицы из дистрибутивных


//формируем файл
 Params = Null;
 Params = makeArray( SQLParam("p_Mode", 1), //формируем файл по корректировкам лимитов
                     SQLParam("p_IsDel", 1 ), //удалять ранее сформированный файл с таким именем
                     SQLParam("p_FileName", "bebe_cuporos_chng") );
 state = execStoredFunc( "USR_PKG_IMPORT_SOFR.AddLOBToTMP", V_INTEGER, Params );
 Params = Null;
 if( state != 0 )
  return state;
 end;


//получим значение пути выгрузки из реестра
 GetRegistryValue(REG_EXPORT_PATH, V_STRING, StrPath, ErrCode);
 if( ErrCode > 0 )
  StrPath = "..\\QUIK_EXCHNG\\out";
 end;
//получим значение пути выгрузки из реестра

 state = CreateOutputFileLMT(/*"$C:\\RsBank60"*/ StrPath );
//формируем файл

 if( state != 0 )
  return state;
 end;

 //установка статуса в ProcessOut
 ParamProcess = Null;
 ProcessOutTbl = Null;
 ParamProcess = TParamProcess( Str_Param + ";2"); //установить статусы 2
 ProcessOutTbl = c_usr_tbl_uTableProcessOut();
 ProcessOutTbl.New_Val_Obj.T_STATUS = ParamProcess.ProcessStatus;
 ProcessOutTbl.Where_Val_Obj.T_RECID = ParamProcess.RecId;
 ProcessOutTbl.Where_Val_Obj.T_OBJECTTYPE = ParamProcess.ObjectType;
 if( not ProcessOutTbl.Update() ) //инструмент простых действий с таблицами из RSL В.Карпов
  state = 1;
 end;
 ParamProcess = Null;
 ProcessOutTbl = Null;
 //установка статуса в ProcessOut

 return state;

onError(err)
 Params = Null;
 ParamProcess = Null;
 ProcessOutTbl = Null;
 state = 1;
 return state;

end;
//сервис загрузки корректировок лимитов