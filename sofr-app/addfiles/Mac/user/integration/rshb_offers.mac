/*
rshb_offers.mac
Загрузка оферт из буф. таблиц рудаты 
*/

import oralib, likepy, FIInter, globals, Календарь;

macro InsertIntoOffers (fiid:integer, offernum:integer, begindate:date, enddate:date, redempdate:date)
  var sql;
  sql = "select 1 from doffers_dbt where t_fiid = :fiid and t_offernum = :offernum";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("fiid", fiid),
                                     SqlParam("offernum", offernum)), false);
  if (sql.MoveNext() )
    return "У ценной бумаги " + ПолучитьИмяФинИн(fiid) + " уже есть оферта с номером " + offernum;
  end;

  sql = "insert into doffers_dbt (t_fiid, t_offernum, t_begindateoffer, t_endfateoffer, t_dateredemption) "
      + "values ( :fiid, "     //1. t_fiid
      + "         :offernum, " //2. t_offernum
      + "         :begin, "    //3. t_begindateoffer
      + "         :end, "      //4. t_endfateoffer
      + "         :datered "   //5. t_dateredemption
      + ") ";
  sql = ExecSql(sql, MakeArray(SqlParam("fiid",     fiid),
                               SqlParam("offernum", offernum),
                               SqlParam("begin",    begindate),
                               SqlParam("end",      enddate),
                               SqlParam("datered",  redempdate)), false);
  if (sql != null)
    return "";
  end;
onerror()
  return "Ошибка при добавлении оферты для ценноый бумаги " + ПолучитьИмяФинИн(fiid);
End;

macro ВставитьОфертуОблигации (fiid:integer, offernum:integer, begindate:date, enddate:date, redempdate:date)
  record fininstr ("fininstr");

  if (ПолучитьФинИн(fiid, fininstr) != 0 )
    return "Не найдена ценная бумага с fiid=" + fininstr.fiid;
  elif (not FI_IsBond(fininstr.fiid) or (fininstr.Issuer != {OurBank}))
    return "Ценная бумага с fiid=" + fininstr.fiid + " не является выпущенной облигацией Банка";
  end;

  return InsertIntoOffers(fiid, offernum, begindate, enddate, redempdate);
End;

//Найти данные по оферте в буфере
macro FindOfferInBuf (isin, offernum:@integer, begindate:@date, enddate:@date, redempdate:@date)
  var sql;
  sql = "select nvl(offer_Date, to_date('01.01.0001', 'dd.mm.yyyy')) offer_Date, "
      + "       nvl(end_offer_Date, to_date('01.01.0001', 'dd.mm.yyyy')) end_offer_Date, "
      + "       nvl(id_offer, 0) id_offer "
      + "from sofr_bond_offers "
      + "where id_fintool in (select fintoolid from sofr_info_fintoolreferencedata where isincode = :isin)";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("isin", isin)), false);
  if (sql.MoveNext() )
    offernum   = sql.value("id_offer");
    begindate  = Date(sql.value("offer_Date"));
    enddate    = Date(sql.value("end_offer_Date"));
    redempdate = GetNextWorkDate(enddate, 1);
    return true;
  end;
  return false;
End;

/*Найти и вставить оферту
mode - возвращать текст ошибки или нет
*/
macro FindAndInsertOffer (fiid, mode)
  var offernum, begindate, enddate, redempdate;
  var isin = ПолучитьКодФинИн(fiid, null, FICK_ISIN);
  var retval = "";

  if (FindOfferInBuf(isin, @offernum, @begindate, @enddate, @redempdate)) 
    retval = ВставитьОфертуОблигации(fiid, offernum, begindate, enddate, redempdate);
  else
    retval = "Ценная бумага с isin = " + isin + " не найдена в буфере";
  end;

  if ( (valtype(mode) != V_UNDEF) and (mode == false) )
    if (retval != "")
      retval = "Ошибка при добавлении оферты";
    end;
  end;

  return retval;
End;

//Пробегает по всем ценным бумагам, и вставляет офферты для собственных облигаций
macro InsertAllOwnOffer ()
  record fininstr ("fininstr");
  var sql, count = 0;
  var rezult = "";

  sql = "select t_fiid "
      + "from dfininstr_dbt "
      + "where     t_fi_kind = :fikind "
      + "      and t_issuer = :ourbank ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("fikind", FIKIND_AVOIRISS),
                                     SqlParam("ourbank", {OurBank})), false);

  InitProgress(-1, "подождите...", "Загрузка оферт выпущенных облигаций");
  while (sql.MoveNext() )
     
    if (ПолучитьФинИн(sql.value("t_fiid"), fininstr) != 0 )
      println("Не найдена ценная бумага с fiid=" + fininstr.fiid);
    elif (FI_IsBond(fininstr.fiid) )
      rezult = FindAndInsertOffer (fininstr.fiid);
      if (rezult == "")
        rezult = "Оферта для бумаги fiid = " + fininstr.fiid + " успешно добавлена";
      end;
      println(rezult);
    end;
    UseProgress(count = count + 1);
  end;
  RemProgress();

onerror()
  RemProgress();
End;