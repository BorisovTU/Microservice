/* ekk_unload.mac
   Гераськина Т.В.
   BIQ-7426 Выгрузка открытых договоров БО в текстовый файл с разделителями (;)

   16/05/2022: дополнение кодами площадки Спб

*/

import rsd;
import dlcontrfunc;
import cb_sql;

  file datafile() txt write;  //scv файл
  var datafilename = "EKKforABS.txt";

/*
№ п/п	Тип	Описание поля
1	CHAR	Номер договора обслуживания 
2	CHAR	Код клиента в АБС: значение кода клиента вида 101
3	CHAR	ЕКК: значение поля "Единый краткий код" договора обслуживания
4               Код СПБ
*/
  var ReportDate = date();

  var marketid = ПолучитьБиржуПоВидуКодаДБО(DLCCK_SPB_BIDCODE);

macro RunReport(repdate)
  var sql_str, rs, cmd;
  var data_str;
  var CountContr = 0;

  sql_str = "select "+
            "      dl.t_dlcontrid, dl.t_sfcontrid, dl.t_iis, "+
            "      sf.t_partyid, sf.t_number, sf.t_datebegin, "+
            "      code.t_code ekk, "+
            "      p.t_code partycode, "+
            "      mp.t_mpcode "+ 
            "  from "+
            "      ddlcontr_dbt dl, "+
            "      dsfcontr_dbt sf,"+
            "      ddlobjcode_dbt code,"+
            "      dobjcode_dbt p, "+
            "      ddlcontrmp_dbt mp "+
            " where dl.t_sfcontrid = sf.t_id "+
            "   and dl.t_dlcontrid = code.t_objectid and code.t_objecttype = 207 and code.t_codekind = 1 "+
            "   and code.t_BankDate <= :FromDate and :ToDate < DECODE(code.t_BankCloseDate, TO_DATE('01010001', 'DDMMYYYY'), TO_DATE('31129999', 'DDMMYYYY'), code.t_BankCloseDate) "+
            "   and p.t_objectid = sf.t_partyid and p.t_objecttype = 3 and p.t_codekind = 101 and p.t_state = 0 "+
            "   and (sf.t_dateclose = to_date('01.01.0001', 'dd.mm.yyyy') or sf.t_dateclose > :pd) "+
            "   and dl.t_dlcontrid = mp.t_dlcontrid(+) and mp.t_marketid(+) = :marketid";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("FromDate", RSDBP_IN, repdate);
   cmd.AddParam("ToDate", RSDBP_IN, repdate);
   cmd.AddParam("pd", RSDBP_IN, repdate);
   cmd.AddParam("marketid", RSDBP_IN, marketid);

   rs = RSDRecordSet(cmd);
   
   if ( Open( datafile, datafilename, "rsansi" ) )      // по BIQ создается в TXTDIR
     while (rs.movenext())
       data_str = "";
       data_str = rs.value("t_number")+";"+rs.value("partycode")+";"+rs.value("ekk")+";"+SQL_ConvTypeStr(rs.value("t_mpcode")); 
       Insert( datafile, data_str );
       CountContr = CountContr+1;
     end;
     Close( datafile );
   else
     msgbox("Ошибка создания файла выгрузки!");
   end;
   rs.close; rs = null;
   cmd.close; cmd = null;
   println("Выгружено записей: "+CountContr);

end;

/* Точка входа */
  if (Gettrue(true, "Выгрузить ЕКК для АБС?"))
     ReportDate = date();
     RunReport(ReportDate);
  end;

end;