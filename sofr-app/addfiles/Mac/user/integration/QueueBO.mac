/*
$Name:        QueueBO.mac
$Module:      Интеграция с внешними системами
$Description: Общий макрос маршрутизации запросов
*/
import "uws_exchng_log.mac";           /* Функционал логирования */
import "secur_prc_msg_transform.mac";  /* Функционал преобразования сообщений */
import "QueueWSClass.mac";             /* Дистрибутивные объекты */
import "AddAccountingEntriesRequestMsg.mac";  /*Обработка входящего платежа*/
import "SendExecStatusReqMsg.mac";           /* Общие классы */
import "global_classes.mac";           /* Общие классы */

/*-----------------------------------------------------------------------*/
/* Поиск и обновление статуса исходного события в utableprocessevent_dbt */
/*-----------------------------------------------------------------------*/
private macro m_find_and_update_src_evnt(p_jmsmessageid, p_description, p_message_id)
   const CV_QNAME_IN = "ESB_TO_SOFR";

   var v_ret = string(0, ": OK");
   var v_sql, v_cmd;

   v_sql =         "DECLARE ";
   v_sql = v_sql + "    PRAGMA autonomous_transaction; "; /* Для записи лога внутри транзакции */

   v_sql = v_sql + "    v_ret              NUMBER(10) := 0; ";
   v_sql = v_sql + "    v_ret_txt          usr_exchng_log_dbt.t_error_msg%TYPE := 'OK'; ";
   v_sql = v_sql + "    v_queue_qname_in   dqueue_log_dbt.t_qname%TYPE := :p_qname_in; ";
   v_sql = v_sql + "    v_messageid        utableprocessevent_dbt.t_messageid%TYPE := :p_message_id; ";
   v_sql = v_sql + "    v_description      utableprocessevent_dbt.t_resulttext%TYPE := :p_description; ";
   v_sql = v_sql + "    v_note             utableprocessevent_dbt.t_note%TYPE := 'Получен Description без Message'; ";
   v_sql = v_sql + "    v_jmsmessageid     dqueue_log_dbt.t_jmsmessageid%TYPE := :p_jmsmessageid; ";
   v_sql = v_sql + "    v_jmsmessageguid   dqueue_log_dbt.t_jmsmessageguid%TYPE; ";
   v_sql = v_sql + "    v_jmscorrelationid dqueue_log_dbt.t_jmscorrelationid%TYPE; ";
   v_sql = v_sql + "    v_status_wait      utableprocessevent_dbt.t_status%TYPE := 2; ";
   v_sql = v_sql + "    v_status_err       utableprocessevent_dbt.t_status%TYPE := 5; ";
   v_sql = v_sql + "    v_log_link_id      dqueue_log_dbt.t_jmscorrelationid%TYPE; ";
   v_sql = v_sql + "    v_recid            utableprocessevent_dbt.t_recid%TYPE; ";
   v_sql = v_sql + "    v_aux_buf          utableprocessevent_dbt.t_recid%TYPE; ";
   v_sql = v_sql + "BEGIN ";
   v_sql = v_sql + "    BEGIN ";
   v_sql = v_sql + "        v_ret := 1; ";
   v_sql = v_sql + "        SELECT t_jmscorrelationid, t_jmsmessageguid ";
   v_sql = v_sql + "          INTO v_jmscorrelationid, v_jmsmessageguid ";
   v_sql = v_sql + "          FROM dqueue_log_dbt ";
   v_sql = v_sql + "         WHERE t_jmsmessageid = v_jmsmessageid ";
   v_sql = v_sql + "           AND t_qname = v_queue_qname_in; ";
/*
   v_sql = v_sql + "        v_ret := 2; ";
   v_sql = v_sql + "        SELECT t_jmscorrelationid INTO v_jmscorrelationid ";
   v_sql = v_sql + "          FROM dqueue_log_dbt ";
   v_sql = v_sql + "         WHERE t_jmsmessageguid = v_jmsmessageguid";
   v_sql = v_sql + "           AND t_jmscorrelationid <> chr(1) ";
   v_sql = v_sql + "           AND t_qname = 'ESB_TO_SOFR'; ";
*/
   v_sql = v_sql + "        v_ret := 3; ";
   v_sql = v_sql + "        SELECT t_jmscorrelationid INTO v_log_link_id ";
   v_sql = v_sql + "          FROM dqueue_log_dbt ";
   v_sql = v_sql + "         WHERE t_jmsmessageid = v_jmscorrelationid; ";

   v_sql = v_sql + "        v_aux_buf := INSTR(v_log_link_id, '-'); ";
   v_sql = v_sql + "        IF v_aux_buf = 0 ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            v_recid := TO_NUMBER(v_aux_buf); ";
   v_sql = v_sql + "        ELSE ";
   v_sql = v_sql + "            v_recid := TO_NUMBER(SUBSTR(v_log_link_id, 1, (v_aux_buf - 1))); ";
   v_sql = v_sql + "        END IF; ";

   v_sql = v_sql + "        v_ret := 4; ";
   v_sql = v_sql + "        SELECT t_recid INTO v_aux_buf ";
   v_sql = v_sql + "          FROM utableprocessevent_dbt ";
   v_sql = v_sql + "         WHERE t_recid = v_recid ";
   v_sql = v_sql + "           AND t_status = v_status_wait; ";

   v_sql = v_sql + "        v_ret := 5; ";
   v_sql = v_sql + "        UPDATE utableprocessevent_dbt ";
   v_sql = v_sql + "           SET t_status = v_status_err, ";
   v_sql = v_sql + "               t_note = v_note, ";
   v_sql = v_sql + "               t_messageid = v_messageid, ";
   v_sql = v_sql + "               t_resulttext = v_description ";
   v_sql = v_sql + "         WHERE t_recid = v_recid; ";
//   v_sql = v_sql + "           AND t_status = v_status_wait; ";
   v_sql = v_sql + "        v_ret := 0; ";

   v_sql = v_sql + "        COMMIT; "; /* Выполняется фиксация данных только для изменений данного анонимного блока */

   v_sql = v_sql + "    EXCEPTION ";
   v_sql = v_sql + "        WHEN NO_DATA_FOUND ";
   v_sql = v_sql + "        THEN ";
//   v_sql = v_sql + "            v_ret := 1; ";
   v_sql = v_sql + "            v_ret := v_ret; ";
   v_sql = v_sql + "            v_ret_txt := 'NO_DATA_FOUND'; ";
   v_sql = v_sql + "        WHEN OTHERS ";
   v_sql = v_sql + "        THEN ";
//   v_sql = v_sql + "            v_ret := 6; ";
   v_sql = v_sql + "            v_ret := SQLCODE; ";
   v_sql = v_sql + "            v_ret_txt := SUBSTR(SQLERRM, 1, 1500); ";
   v_sql = v_sql + "    END; ";

   v_sql = v_sql + "    :p_ret := v_ret; ";
   v_sql = v_sql + "    :p_ret_txt := v_ret_txt; ";
   v_sql = v_sql + "END; ";

   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_qname_in", RSDBP_IN, CV_QNAME_IN);
   v_cmd.addParam("p_message_id", RSDBP_IN, p_message_id);
   v_cmd.addParam("p_description", RSDBP_IN, p_description);
   v_cmd.addParam("p_jmsmessageid", RSDBP_IN, p_jmsmessageid);
   v_cmd.addParam("p_ret", RSDBP_OUT, V_INTEGER);
   v_cmd.addParam("p_ret_txt", RSDBP_OUT, V_STRING, 2000);
   v_cmd.execute();

   v_ret = string(v_cmd.value("p_ret"), ": ", v_cmd.value("p_ret_txt"));

   v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret = string(7); /* Иные проблемы */
   return v_ret;
end; /* macro m_find_and_update_src_evnt() */


/*-------------------------------------------------------------------------*/
/* Процедура, которая будет вызываться при обработке полученного сообщения */
/* Название прописывается в ddatatypews_dbt.t_parmbo для соответствующего  */
/* значения ddatatypews_dbt.t_parmbo                                       */
/*-------------------------------------------------------------------------*/
macro req_distributor_in(in_ReqIDMes,
                         in_SenderID,
                         in_ToBranch,
                         in_ParmBO_ID,
                         in_Direction,
                         in_ParamJMSMessageID,
                         in_XMLMes,
                         in_Status,
                         in_Code,
                         in_Description)
   private const CV_FUNC_INIT_NAME = "INIT";
   private var v_transformator; /* Объект для преобразования транспортных сообщений */
   private var v_out_obj;       /* Объект с результатом выполнения вызываемой функции */
   private var v_result_obj;    /* Возвращаемый объект с результатом выполнения */
   private var v_req_data;      /* Объект с данными запроса */
   private var v_logger_obj;    /* Объект для работы с журналом */
   private var v_log_id;        /* Идентификатор записи в журнале (если требуется что-то добавить) */
   private var v_root_name;     /* Имя корневого тега исходного сообщения */
   private var v_module_name;   /* Название вызываемого макроса */
   private var v_func_name;     /* Название вызываемой функции */
   private var v_aux_buf;
   private var v_aux_txt;
   private var p_WillMakeCIFCode = false;
   private var p_MasterExternalId = NULL;
   private var v_ParamJMSMessageID; /* kva: временно для проверки ситуации с пустым значением in_ParamJMSMessageID */
   private var p_global_error;
   private var p_dlcontrid;

   if(ValType(in_ParamJMSMessageID) == V_UNDEF)
      v_ParamJMSMessageID = "Empty_value_undef";
   else
      if(in_ParamJMSMessageID == strfor(1))
         v_ParamJMSMessageID = "Empty_value_chr(1)";
      elif(in_ParamJMSMessageID == strfor(0))
         v_ParamJMSMessageID = "Empty_value_chr(0)";
      elif(strlen(in_ParamJMSMessageID) == 0)
         v_ParamJMSMessageID = "Empty_value_strlen_0";
      elif(strlen(in_ParamJMSMessageID) == 1)
         v_ParamJMSMessageID = "Empty_value_strlen_1";
      else
         v_ParamJMSMessageID = in_ParamJMSMessageID;
      end;
   end;

   v_transformator = c_secur_msg_converter();
//   v_result_obj = TResGetMessage();
   v_result_obj = add_TResGetMessage(); // gtv: дополненный объект для выгрузки ссылок
   v_result_obj.ReqIDMes = in_ReqIDMes;
   v_result_obj.SenderID = in_SenderID;

   if(v_transformator.m_decode_escaped_char(in_XMLMes))
      v_logger_obj = uws_exchng_logger(null,
                                       null,
                                       XmlRpcRequestId(),
                                       CV_FUNC_INIT_NAME,
                                       modulename(),
                                       v_ParamJMSMessageID, /*in_ParamJMSMessageID,*/
                                       v_transformator.get_pure_msg());
      v_log_id = v_logger_obj.get_log_id();

      v_req_data = v_transformator.m_secur_ip_internal_req(v_transformator.get_pure_msg());
      v_root_name = v_req_data.methodparms[1];

      /* Если не пришел Message */
      if((in_Direction == "ANSWER") and ((in_XMLMes == strfor(0)) or (strupr(in_Status) != "SUCCESS")))
         v_aux_txt = m_find_and_update_src_evnt(in_ParamJMSMessageID, in_Description, v_log_id);
         if(in_XMLMes == strfor(0))
            v_logger_obj.add_response(in_Description,
                                      GC_ADD_DEV_ERR_CODE,
                                      string("Сообщение без тега Message; сохранение Description (", v_aux_txt, ")"));
         else
            v_logger_obj.add_response(in_Description,
                                      GC_ADD_DEV_ERR_CODE,
                                      string("Получена ошибка: ", in_Code, "; сохранение Description (", v_aux_txt, ")"));
         end;
         /* В таком случае ничего не возвращаем */
         v_result_obj.ResponseText = "";

      else
         /*---------------------------------------*/
         /* Создание/изменение Субъекта ЮЛ в СОФР */
         /*---------------------------------------*/
         if(v_root_name == "ClientCreateLERequestMsg")
            v_module_name = "ClientCreateLERequestMsg";
            v_func_name   = "ClientCreateLERequestMsg";

            /* 20190103 - kva - добавлен третий параметр для целей логирования */
            v_out_obj = ExecMacroFile(v_module_name, v_func_name, v_req_data.methodparms[0], v_log_id, in_ParamJMSMessageID, @p_WillMakeCIFCode, @p_MasterExternalId);

            v_result_obj.ResponseText = v_transformator.m_secur_internal_resp(v_out_obj);

            v_logger_obj.add_response(v_result_obj.ResponseText, null, null, null, v_module_name);

            // gtv: формирование ошибки
            if (v_out_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.ResultCode == 1)
               // произошла ошибка
               v_result_obj.ResultCode = -101;

               if ( (v_out_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.ErrorList) and (v_out_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.ErrorList.size > 0) )
                  v_logger_obj.add_error_info(v_out_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.ErrorList.value(0).ErrorCode, 
                                              v_out_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.ErrorList.value(0).ErrorDesc);
               end;
            else
               // gtv: классы для отправки ссылок
               if (p_WillMakeCIFCode)
                  if (ValType(v_out_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.MasterRecordId) != V_UNDEF)
                     v_result_obj.objectid = v_out_obj.ClientCreateLEResponseMsg.ClientCreateLEResponse.MasterRecordId.Objectid;
                     v_result_obj.servicename = "PersonGetXRefLE";
                     v_result_obj.MasterExternalId = p_MasterExternalId;
                  end;
               end;
            end;

         /*---------------------------------------*/
         /* Создание/изменение Субъекта ФЛ в СОФР */
         /*---------------------------------------*/
         elif(v_root_name == "SetPersonRequestMsg")
            v_module_name = "SetPersonRequestMsg";
            v_func_name = "SetPersonRequestMsg";

            /* 20190103 - kva - добавлен третий параметр для целей логирования */
            v_out_obj = ExecMacroFile(v_module_name, v_func_name, v_req_data.methodparms[0], v_log_id, in_ParamJMSMessageID, @p_WillMakeCIFCode, @p_MasterExternalId, @p_global_error, @p_dlcontrid);
            
            v_aux_buf = ValType(v_out_obj);
            if(v_aux_buf == V_GENOBJ)
               v_result_obj.ResponseText = v_transformator.m_secur_internal_resp(v_out_obj);
               v_logger_obj.add_response(v_result_obj.ResponseText, null, null, null, v_module_name);

               // gtv: формирование ошибки
               if (v_out_obj.SetPersonResponseMsg.SetPersonResponse.ResultCode == 1)
                  // произошла ошибка
                  v_result_obj.ResultCode = -101;

                  if ( (v_out_obj.SetPersonResponseMsg.SetPersonResponse.ErrorList) and (v_out_obj.SetPersonResponseMsg.SetPersonResponse.ErrorList.size > 0) )
                     v_logger_obj.add_error_info(v_out_obj.SetPersonResponseMsg.SetPersonResponse.ErrorList.value(0).ErrorCode, 
                                                 v_out_obj.SetPersonResponseMsg.SetPersonResponse.ErrorList.value(0).ErrorDesc);
                  end;
               else
                  if (p_global_error)
                     v_logger_obj.add_error_info(0, p_global_error);
                  end;
                  // gtv: для формирования запроса на биржу
                  v_result_obj.DlContrid = p_dlcontrid;
                  // gtv: классы для отправки ссылок
                  if (p_WillMakeCIFCode)
                     if (ValType(v_out_obj.SetPersonResponseMsg.SetPersonResponse.ClientID) != V_UNDEF)
                        v_result_obj.objectid = v_out_obj.SetPersonResponseMsg.SetPersonResponse.ClientID.Objectid;
                        v_result_obj.servicename = "PersonCreateXref";
                        v_result_obj.MasterExternalId = p_MasterExternalId;
                     end;
                  end;
               end;
            else
               // произошла ошибка, нужен откат
               v_result_obj.ResultCode = -101;
               v_result_obj.ResponseText = "";
               v_logger_obj.add_error_info(-1001, "Ошибка выполнения - пустой исходящий объект");
            end;

         else
            /* Пытаемся вызвать таким образом */
            v_module_name = v_root_name;
            v_func_name = v_root_name;

// 2021-10-25 Cherednichenko DEF-15648 Подтверждение статуса выгрузки/удаления проводки импортится и выполняется отдельно, иначе получаем зомби-класс
            if (v_root_name == "SendExecStatusReqMsg")
              v_out_obj = SendExecStatusReqMsg(v_req_data.methodparms[0], v_log_id);
            else
              /* 20190103 - kva - добавлен третий параметр для целей логирования */
              v_out_obj = ExecMacroFile(v_module_name, v_func_name, v_req_data.methodparms[0], v_log_id, in_ParamJMSMessageID);
            end;

            v_aux_buf = ValType(v_out_obj);
            /* Объект преобразуем в сообщение */
            if(v_aux_buf == V_GENOBJ)
               v_result_obj.ResponseText = v_transformator.m_secur_internal_resp(v_out_obj);
               if ( (v_module_name == "GetBrokerContractInfoReq")  and (v_result_obj.ResponseText == "") )
                  v_out_obj = c_BrokerContractInfo_outObj;

                  v_out_obj.GetBrokerContractInfoResp.GetBrokerContractInfo = NULL;
                  v_out_obj.GetBrokerContractInfoResp.ErrorList = TArray;
                  v_out_obj.GetBrokerContractInfoResp.ErrorList.value(0) = c_Error;
                  v_out_obj.GetBrokerContractInfoResp.ErrorList.value(0).ErrorCode = "003";
                  v_out_obj.GetBrokerContractInfoResp.ErrorList.value(0).ErrorDesc = "Внутренняя ошибка СОФР: невозможно произвести преобразование ответа по схеме XSD";
                  v_result_obj.ResponseText = v_transformator.m_secur_internal_resp(v_out_obj);
               elif (v_module_name == "SendAccreditationStatusReq")
                  v_logger_obj.add_error_info(v_out_obj.SendAccreditationStatusResp.ErrorList.value(0).ErrorCode, 
                                              v_out_obj.SendAccreditationStatusResp.ErrorList.value(0).ErrorDesc);
               end;
            /* Считаем, что для таких случаев ответ отправляться не должен */
            elif(v_aux_buf == V_BOOL)
               /* Что делать в случае ошибки? */
               if(v_out_obj)
                  /* Если ответ в ИП не требуется, то заполняем пустой строкой */
                  v_result_obj.ResponseText = "";

               else
                  /* Пока оставляем как есть - ошибку сохраняем в журнал внутри функционала */
                  v_result_obj.ResponseText = "";
               end;

            /* !!! Пока будем считать, что в таком случае ответ отправлять не будем */
            else
               v_result_obj.ResponseText = "";
            end;


            v_logger_obj.add_response(v_result_obj.ResponseText, null, null, null, v_module_name);
            
         end;
      end;
   else
      /* !!! TO_DO: Добавить заполнение объекта с ошибкой */
      /* v_result_obj =  */
   end;

   return v_result_obj;

onError(err)
   if (v_root_name == "SetPersonRequestMsg")
      // произошла ошибка, нужен откат
      v_result_obj.ResultCode = -101;
      v_result_obj.ResponseText = err.code + " " + err.message;
   end;
   return v_result_obj;

end; /* macro req_distributor_in() */
