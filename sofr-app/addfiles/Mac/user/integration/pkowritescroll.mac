import rsd;
import "diasoft_createPKO_funcobj.mac";
import "lim_utils.mac";

/**
  @parm[1] - запрос
  @parm[2] - колонки (необязательный параметр)
  @parm[3] - количество колонок (необязательный параметр)
  @parm[4] - процедура обработки клавиш (необязательный параметр)
  @parm[5] - заголовок (необязательный параметр)
  @parm[6] - подсказка (необязательный параметр)
*/
macro scrollSomeTable (sql)
  var col,colnums,proc,title,hint;
  getParm(1, col);
  getParm(2, colnums);
  if ((colnums==null) and (col!=null))
    colnums = col.size/6;
  end;
  getParm(3, proc);
  getParm(4, title);
  getParm(5, hint);
  return RunScroll(
    RSDRecordSet(RSDCommand(sql), /*RSDVAL_CLIENT*/ null, RSDVAl_STATIC),
    colnums,
    col,
    "Картотека вызовов",
    proc,
    title,
    hint
  );
end;

PRIVATE MACRO AddCol (ar,ind, fld, head, width, rdonly, ty, dp)
   ar.value (ind * 6)     = fld;
   ar.value (ind * 6 + 1) = head;
   ar.value (ind * 6 + 2) = width;
   ar.value (ind * 6 + 3 ) = rdonly;   // fldType
   ar.value (ind * 6 + 4 ) = dp;//   decPoint
   ar.value (ind * 6 + 5 ) = 0;   // reserv
END;

macro startDiasoftPko(rs)
  if ( (rs.value("dealid")!=null) and (rs.value("dealid")!=0) and (valtype(rs.value("dealid"))!=26) )
    MsgBox("В данной записи заполнен dealid - создание операции невозможно " + string( rs.value("dealid") ) );
    return;
  end;
  if (not getTrue(false,"Создать операцию списания/зачисления ЦБ на основании данной записи?"))
    return;
  end;
  if (not getTrue(false,"Для создания операции будет очищено поле errList. Продолжить?"))
    return;
  else
    var sql = "update pko_writeoff set errlist = '' where id = :1";
    var cmd = rsdcommand(sql);
    cmd.addparam(":1",RSDBP_IN,rs.value("ID"));
    cmd.Execute;
  end; 
  createEnroll(8208, rs.value("ID"), rs.value("GUID") );
end;

macro RecreateWriteOffPKO(rs)
  if (not getTrue(false, "Пересоздать запись в Картотеке на основании имеющейся записи в itt_q_message_log?"))
    return;
  end;
  var sql = 
"  declare                                             "
"    o_MSGCode integer;                                "
"    o_MSGText varchar2(1000);                         "
"  begin                                               "
"    Diasoft_send_info_repeat(p_guid => :1,            "
"                           o_MSGCode => o_MSGCode,    "
"                           o_MSGText => o_MSGText);   "
"    :oText := o_MsgText;                              " 
" end;                                                 "
;
  var cmd = rsdcommand(sql);
  cmd.addparam(":1"    ,RSDBP_IN,rs.value("GUID"));
  cmd.addparam("oText",RSDBP_OUT,v_string);
  cmd.Execute;
  var errText = cmd.value("oText"); 
  if (valtype(errText)!=26)
    MsgBox(errText);
  else
    MsgBox("Запись в Картотеке пересоздана на основании GUID в itt_q_message_log"); 
  end; 
onerror(err)
  MsgBox(GetFullErrorMessage(err));
end;

macro EvProc(rs, cmd, id, key)
   if (cmd == DLG_KEY)
    if (key == 316)  //f2
       RecreateWriteOffPKO(rs);
       UpdateScroll(rs);
       return /*CM_SELECT*/CM_DEFAULT;
    elif (key == 13) //enter
       MsgBox(rs.value("xfd"));
       return CM_DEFAULT;
    elif (key == 351) //ctrl+f2
       startDiasoftPko(rs);
       return CM_SELECT;
    end;   
  end;
end;


macro scroll_pko;
  var col = tArray;
  AddCol (col, 0, "custodyorderid",  "cust_id",   15 ,  0,0);
  AddCol (col, 1, "ID",    "№",   10 ,  0,0);
  AddCol (col, 2, "DEALID",    "DEALID",   10 ,  0,0);
  AddCol (col, 3, "t_FI_CODE",    "ЦБ",   12 ,  0,0);
  AddCol (col, 4, "t_number",    "Субдоговор",   12 ,  0,0);
  AddCol (col, 5, "t_shortname",  "Клиент",   12 ,  0,0);
  AddCol (col, 6, "dealcode",  "Вн.код",   17 ,  0,0);
  AddCol (col, 7, "guid",  "GUID",   12 ,  0,0);
  AddCol (col, 8, "operationid",  "Код операции",   17 ,  0,0);
  AddCol (col, 9, "clientid",  "Код клиента",   17 ,  0,0);
  AddCol (col, 10, "market",  "Рынок",   17 ,  0,0);
  AddCol (col, 11, "securityid",  "Код ЦБ",   17 ,  0,0);
  AddCol (col, 12, "qnty",  "Количество",   17 ,  0,0);
  AddCol (col, 13, "expirationdate",  "Exp.date",   17 ,  0,0);
  AddCol (col, 14, "xfd",  "XML",   17 ,  0,0);
  AddCol (col, 15, "pkostatus",  "PKOstatus",   10 ,  0,0);
  AddCol (col, 16, "idcontract",  "id контракта",   15 ,  0,0);
  AddCol (col, 17, "operationtime",  "operationtime",   20 ,  0,0);
  AddCol (col, 18, "opertype",  "opertype",   15 ,  0,0);
  AddCol (col, 19, "errlist",  "errlist",   15 ,  0,0);
  AddCol (col, 20, "log_id",  "q_logid",   15 ,  0,0);
  var sql = 
" select q.log_id, pko.dealid, fi.t_fi_code, ds.t_number, dp.t_shortname "
" , pko.id, pko.dealcode, q.msgid guid, pko.operationid, pko.clientid    "
" , pko.market, pko.securityid, pko.qnty, pko.expirationdate             "
" , pko.pkostatus, pko.idcontract, pko.operationtime, pko.opertype       "
" , pko.errlist, to_char(q.messbody) xfd, pko.custodyorderid             "
" from itt_q_message_log q                                               "
" left join pko_writeoff pko                                             "
"   on q.msgid = pko.guid                                                "
" left join dparty_dbt dp                                                "
"   on dp.t_partyid = pko.clientid                                       "
" left join dsfcontr_dbt ds                                              "
"   on ds.t_id = pko.idcontract                                          "
" left join dfininstr_dbt fi                                             "
"   on fi.t_fiid = pko.securityid                                        "
" where (pko.dealid is null or pko.dealid = 0)                           "
"   and q.servicename in ('Diasoft.SendPkoInfo')                         "
"   and q.enqdt > to_date('01122023','ddmmyyyy')                         "
"   and q.queuetype in ('IN')                                           "
" order by pko.id desc                                                   "
;
  while (scrollSomeTable (sql,col,null,"EvProc","Картотека","F2-пересоздать запись в Картотеке, Ctrl-F2-создать операцию, Enter-XML"))
  end;
end;

scroll_pko;
exit(1);