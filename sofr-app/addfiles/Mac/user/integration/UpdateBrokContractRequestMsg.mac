/*-----------------------------------------------------*/
/* Создание ссылок по Субъекту ФЛ                        */
/*                                                     */
/* Автор: Гераськина Т.                                */
/*
*/
/*----------------------------------------------*/
import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import "RequestWS.mac";                   /* Функция передачи запроса в Web Sphere */
import "uws_exchng_log.mac";              /* Функционал логирования */
import func_lib;
private var CN;

class c_UpdateContract_Error()
   var ErrorCode: integer = 0;
   var ErrorDesc: string;
   var ContractID:integer;
   var ContractNumber:string;
end;

// BIQ-8786 тарифные планы
// Тариф обслуживания
private class c_BrokTariff
   var IsIndividual  : bool;  // Флаг индивидуального тарифа   Да    true - признак "индивидуальный" установлен
                              //                                     false - признак "индивидуальный" не установлен
   var Comment       : String;// Комментарий к ТП на русском языке  Нет
   var Description   : String;// Справочное описание тарифа         Нет
   var IsHidden      : bool;  // Признак скрытого тарифа (если true - тариф не отображается клиентам и на UI СОФРа) Нет  true - признак "скрытый" установлен
                              //                                                                                         false - признак "скрытый" не установлен
   var BeginDate     : date;  // Дата начала действия тарифаного плана  Нет
   var EndDate       : date;  // Дата окончания действия тарифного плана. Пустая String либо 01.01.0001 - неограничено  Нет
   var BindDate      : date;  // Дата привязки тарифного плана к клиентскому договору. Применимо для сообщений изменяющих ТП.       Нет
   var UnbindDate    : date;  // Дата прекращения действия правил ТП в ЦФТ/СОФРЕ. Техническая отвязка происходит за -3D (рабочих)   Нет
   var TariffId;              // Идентификатор тарифа в системе хранения (СОФР)  Да    Идентификатор
   var TariffKind;            // Вид тарифа                    Да    Справочник  Передаваемые значения в рамках 8786: BROKERAGE; REPO; UNDEFINED
   var TariffPlan;            // Наименование тарифного плана  Да    Справочник
end;

private class c_BrokContract
   var BankRelationship      : string;   //Отношение к банку           Да
   var LastName              : string;   //Фамилия клиента             Да
   var FirstName             : string;   //Имя Отчество клиента        Да
   var BrokContractNumber    : string;   //Номер договора              Да
   var BrokContractOpenDate  : date;     //Дата заключения             Нет
   var BrokContractCloseDate : date;     //Дата закрытия               Нет
   var UserBrokName          : string;   //Сотрудник ФИО               Нет
   var LastModifyDateTime    : datetime; //Дата последней модификации  Нет
   var VSPName               : string;   //Наименование ВСП            Нет
   var BranchId;                         //Код Филиала                 Да
   var LoanBrokContract;                 //Тип Договора                Да
   var BrokStatus;                       //Статус договора             Нет
end;


private class c_BrokCond
   var BrokCondDate        : date;     // Дата введения условия                    Нет
   var BrokEmail           : string;   // Email                                    Нет
   var BrokPhone           : string;   // Телефон                                  Нет
   var UserBrokName        : string;   // Сотрудник (ФИО)                          Нет
   var LastModifyDateTime  : datetime; // Дата последней модификации               Нет
   var IsAvailabilityInfoAnotherMember : bool; // Признак о наличии или отсутствии сведении ИИС у другого проф участника  Нет
   var IsSelectionImperfectTransactions: bool; // Выбор необеспеченных сделок      Нет
   var CurrencyMMVBType    : string;   // Обязателен для заполнения только для контейнера с данными по TradingFloorKind = MMVB 
                                       // Возможные значения: RUR, USD, EUR        Нет
   var BrokPlatform;                   // Намерения в части проведения операций    Нет
   var BrokPRate;                      // Тарифы на оплату услуг в рамках          Нет
   var BrokPRateREPO;                  // Тарифы на оплату сделок РЕПО             Нет
   var BrokOrderMethod;                // Способ подачи поручений                  Нет
   var BrokReceiveMethod;              // Способ получения брокерских отчетов      Нет
   var BrokDocFlow;                    // Способ документооборота с Депозитарием   Нет
   var BrokPayment;                    // Способ оплаты услуг Депозитария          Нет
   var BrokCurrency;                   // Валюта договора БО                       Нет
   // BIQ-8786 тарифные планы
   var TariffList;      //Список тарифов    Да
end;


private class BrokClient
   var BrokCitizenship:string;   // Гражданство (подданство) Символьный код старны (RUS)             Нет
   var BrokRegAddress:string;    // Гражданство (подданство) Символьный код старны (RUS)             Нет
   var BrokActAddress:string;    // Фактический адрес (почтовый)                                     Нет
   var BrokDoc:string;           // ДУЛ                                                              Нет
   var BrokPfa:string;           // Статус ИПДЛ/ПДЛ                                                  Нет
   var BrokPfaRelative:string;   // Степень родства по отношению к ИПДЛ                              Нет
   var BrokINN:string;           // ИНН                                                              Нет
   var BrokSNILS:string;         // СНИЛС                                                            Нет
   var BrokMigrCard:string;      // Миграционная карта                                               Нет
   var BrokResidence:string;     // Документ, подтверждающий право на пребывание (проживание) в РФ   Нет
   var BrokResStatus:string;     // Статус (резидент или нерезидент)                                 Нет
end;

private class c_BrokCB
   var BrokOrderType       : string;   // Тип поручения                                      Нет
   var BrokOrderDate       : date;     // Дата составления                                   Нет
   var BrokCBDescription   : string;   // Описание ценной бумаги                             Нет
   var BrokCBNumber        : string;   // Количество ценной бумаги                           Нет
   var BrokSum             : double;   // Сумма на покупку ценных бумаг                      Нет
   var BrokEmitent         : string;   // Эмитент                                            Нет
   var BrokCBPrice         : double;   // Цена одной ценной бумаги                           Нет
   var BrokOrderTerm       : date;     // Срок действия поручения                            Нет
   var BrokTransFrom       : string;   // Торговая площадка для вывода денежных средств      Нет
   var BrokTransTo         : string;   // Торговая площадка для получения денежных средств   Нет
   var BrokTransSum        : double;   // Сумма перевода                                     Нет
   var UserBrokName        : string;   // Сотрудник (ФИО)                                    Нет
   var LastModifyDateTime  : datetime; // Дата последней модификации                         Нет
   var TypeBrokCB;                     // Вид сделки                                         Нет
   var BrokCBCurrency;                 // Валюта цены                                        Нет
   var BrokPlatformTransCurrency;      // Валюта перевода)                                   Нет
end;

private class c_AccountNumberWithCurrency
   var AccountNumber: string;  // Номер счета    Да
   var CurrencyCode : string;  // Код Валюты     Нет
end;

private class c_LoanAcctBrok
   var AccountDate       : date;       // Дата привязки счета          Нет
   var UserBrokName      : string;     // Сотрудник (ФИО)              Нет
   var LastModifyDateTime: datetime;   // Дата последней модификации   Нет
   var AccountNumber;                  // Номер счета                  Нет
end;


private class adp_UpdateBrokContract(IncomeData, p_closedate, p_typeaction)
   var ClientId;         // Идентификатор клиента в ГО                       Да
   var BrokContract;     // Основные реквизиты договора на БО/ИИС            Да
   var BrokCondList;     // Список условии договора                          Нет
   var BrokClient;       // Реквизиты клиента из условий д-ра                Нет
   var BrokCBList;       // Список поручений по договору                     Нет
   var LoanAcctBrokList; // Список счетов, привязанных к договору на БО/ИИС  Нет
  
   private macro uInitPrime(IncomeData, p_closedate, p_typeaction)
      var err_txt;
      var temp;
      ClientId = c_IntegrationSymbolicIdentifierXType;
      ClientId.ObjectId = GetPartyCode_ByNumber(IncomeData.value("t_partyid"), PARTY_CODEKIND_ABS);
      var pos = Index(ClientId.ObjectId,"#");
      if (pos > 0)
         ClientId.ObjectId = Substr(ClientId.ObjectId,6);
      end;

      ClientId.SystemId = "CFT" /*"BISQUIT"*/;
      ClientId.SystemNodeId = "CFT" /*"BISQUIT"*/;   
    
      BrokContract = c_BrokContract;
      BrokContract.BankRelationship   = "Клиент";   //???
    
      BrokContract.LastName = IncomeData.value("t_name1");
      BrokContract.FirstName = IncomeData.value("t_name2")+" "+IncomeData.value("t_name3");
      BrokContract.FirstName = StrSubst(BrokContract.FirstName,StrFor(1), "");
    
      BrokContract.BrokContractNumber    = IncomeData.value("t_number");
      CN = BrokContract.BrokContractNumber;
      BrokContract.BrokContractOpenDate  = IncomeData.value("t_datebegin");
      BrokContract.BrokContractCloseDate = p_closedate; 

      BrokContract.BranchId = c_IntegrationSymbolicIdentifierXType;   //???
      BrokContract.BranchId.ObjectId = "0000";
      BrokContract.BranchId.SystemId = "CFT" /*"BISQUIT"*/;
      BrokContract.BranchId.SystemNodeId = "CFT" /*"BISQUIT"*/;  
    
      BrokContract.LoanBrokContract = c_IntegrationDictionaryRecordXType;
      if (IncomeData.value("t_iis") == "X" )
         BrokContract.LoanBrokContract.RecordCode = "ИИС";
      else
         BrokContract.LoanBrokContract.RecordCode = "БО";
      end;  

      /* BIQ-7089 Гераськина Т.В. Сервис используется не только для закрытия, но и для обновления договора */
      if (p_typeaction == 3) // закрытие
         BrokContract.BrokStatus = c_IntegrationDictionaryRecordXType;
         BrokContract.BrokStatus.RecordCode = "ЗАКР";
      else 
         BrokContract.BrokStatus = c_IntegrationDictionaryRecordXType;
         BrokContract.BrokStatus.RecordCode = "ДЕЙСТВУЮЩИЙ";
      end;

      if (p_typeaction == 2) // смена ТП у договора
         temp = GetTariffRecord(IncomeData.value("t_sfcontrid"), IncomeData.value("t_dlcontrid"));
         if (temp.size > 0)
            BrokCondList = TArray;
            BrokCondList.value(0) = c_BrokCond;
            BrokCondList.value(0).TariffList = temp;
         end;
         temp = null;
      end;
   end;
  
   uInitPrime(IncomeData, p_closedate, p_typeaction); 
end;


private class c_UpdateBrokContractList
   var UpdateBrokContractList;
end;

private class c_UpdateBrokContractRequest
   var UpdateBrokContractRequest;
end;

/* Основной класс работы с сервисом */
/* BIQ-7089 Гераськина Т.В. Сервис используется не только для закрытия, но и для обновления договора */
/* p_typeaction: 1 - обновление, 3 - закрытие */
private class c_UpdateBrokContractRequestMsg(p_contrid, p_type, p_typeaction)
   var UpdateBrokContractRequestMsg; /* !!! По названию корневого тега запроса !!! */
  
   private macro uInitPrime(p_contrid, p_type, p_typeaction)
      var sql_str, cmd, rs, sql_p, cmd_p, rs_p;
      var err_txt;
      var aux_buff, aux_buff2, ai;  
      var ContractNumber;
      var date_close;

      if(ValType(p_contrid) != V_UNDEF)

        sql_p = " SELECT party.t_legalform                                                   "+
        "   FROM dparty_dbt party                                                            "+
        "  WHERE party.t_partyid = (SELECT sf.t_partyid                                      "+
        "                             FROM dsfcontr_dbt sf                                   "+
        "                            WHERE sf.t_id = (SELECT dd.t_sfcontrid                  "+
        "                                               FROM ddlcontr_dbt dd                 "+
        "                                              WHERE dd.t_dlcontrid = :dlcontrid))   "+
        "        AND EXISTS                                                                  "+
        "               (SELECT *                                                            "+
        "                  FROM dobjcode_dbt obj                                             "+
        "                 WHERE                                                              "+
        "                       ( ( (obj.t_codekind = 16)                                    "+
        "                          AND (LENGTH (obj.t_code) < 10))                           "+
        "                     OR ( ( (obj.t_codekind = 27)                                   "+
        "                           AND (LENGTH (t_code) > 13))                              "+
        "                         OR (obj.t_codekind = 27) AND (LENGTH (t_code) < 1))))      ";
 
         cmd_p = RSDCommand(sql_P);
         cmd_p.AddParam("dlcontrid", RSDBP_IN, p_contrid);
         rs_p = RSDRecordSet(cmd_p);
         if (rs_p.movenext)

             if(rs_p.value("t_legalform") == 2)
           
               sql_str = "SELECT sfcontr.t_number, sfcontr.t_partyid, sfcontr.t_dateclose, sfcontr.t_datebegin, ddlcontr.t_iis, "+
                         "       prsn.t_name1, prsn.t_name2, prsn.t_name3, ddlcontr.t_sfcontrid, ddlcontr.t_dlcontrid "+
                         "  FROM ddlcontr_dbt ddlcontr, "+
                         "       dsfcontr_dbt sfcontr, "+
                         "       dpersn_dbt prsn "+
                         " WHERE sfcontr.t_id = ddlcontr.t_sfcontrid "+
                         "   AND sfcontr.t_partyid = prsn.t_personid "+
                         "   AND ddlcontr.t_dlcontrid = :dlid ";
               cmd = RSDCommand(sql_str);
               cmd.AddParam("dlid", RSDBP_IN, p_contrid);
               rs = RSDRecordSet(cmd);
               if (rs.movenext)
                  ContractNumber = rs.value("t_number");
               end;
              
               if (not ContractNumber)
                  err_txt = "Не найден договор БО ID="+p_contrid;
                  rs.close; cmd.close; cmd = null; rs = null;
                  RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
               end;
               if (p_typeaction == 3) // закрытие
                  if ((p_type != "ONLINE") and (SQL_ConvTypeDate(rs.value("t_dateclose")) == date(0,0,0)))
                     err_txt = "Договор не закрыт ID="+p_contrid;
                     rs.close; cmd.close; cmd = null; rs = null;
                     RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
                  elif (p_type == "ONLINE")
                     date_close = {curdate};
                  else 
                     date_close = SQL_ConvTypeDate(rs.value("t_dateclose"));
                  end;
               else    // обновление
                  date_close = NULL;
               end;
              
               UpdateBrokContractRequestMsg = c_UpdateBrokContractRequest;
               UpdateBrokContractRequestMsg.UpdateBrokContractRequest = c_UpdateBrokContractList;
               UpdateBrokContractRequestMsg.UpdateBrokContractRequest.UpdateBrokContractList = TArray;
               UpdateBrokContractRequestMsg.UpdateBrokContractRequest.UpdateBrokContractList.value(0) = adp_UpdateBrokContract(rs, date_close, p_typeaction);
               rs.close; cmd.close; cmd = null; rs = null;
             else 
               return null;
             end;
         else 
           return null;
         end;

      else
          return null;
      end;
   end;
  
   uInitPrime(p_contrid, p_type, p_typeaction);   
end; 

/*--------------------------------------------------------------------------*/
/* Адаптер для вызова сервиса "Обновление информации по договору на БО/ИИС" */
/* BIQ-7089 Гераськина Т.В. Сервис используется не только для закрытия, но и для обновления договора */
/* _typeaction: 1 - обновление, 3 - закрытие */
/*--------------------------------------------------------------------------*/
macro run_UpdateBrokContractRequestMsg(_contrid, _type, p_error:@object, _typeaction)
   var v_out_obj;
   
   private var RequestText;
   private var v_transformator;
   private var v_answer;
   private var v_answer_data;
   private var v_logger_obj;
   private var v_log_id;
   private var xml_str;
   private var RespData;

   const C_UPDATEBROKCONTRACT = 16;

   var vguid = CreateGUID();
   
   // заполнение класса информацией 
   v_out_obj = c_UpdateBrokContractRequestMsg(_contrid, _type, _typeaction);

   v_transformator = c_secur_msg_converter();
   RequestText = v_transformator.m_secur_internal_resp(v_out_obj);
   
   v_logger_obj = uws_exchng_logger(null, vguid, null, "UpdateBrokContractRequestMsg", modulename(), null, RequestText);
   v_log_id = v_logger_obj.get_log_id();   

   v_answer = SubmitRequestToWS( C_QUEUE_OUT_ID, // ИД очереди.
                                 "",             // ИД сообщения.
                                 "",             // Идентификатор Бэк Офиса. (не обрабатывается).
                                 false,           // Признак синхронной обработки. Если не указан, = False
                                 "SOFR",         // Подразделение системы-назначения.
                                 C_UPDATEBROKCONTRACT,             // ИД типа данных
                                 RequestText,    // Бизнес данные в виде XML.
                                 v_log_id        // ID usr_exchng_log
                                 );
   p_error = c_UpdateContract_Error;
   p_error.ContractID = _contrid;
   p_error.ContractNumber = "";
   p_error.ErrorCode = 0;
   p_error.ErrorDesc = "";

   return true;

onError(err)
   p_error = c_UpdateContract_Error;
   p_error.ContractID = _contrid;
   p_error.ContractNumber = "";
   p_error.ErrorCode = c_err_obj.obtain_err_code(err);
   p_error.ErrorDesc = c_err_obj.obtain_err_msg(err);

   if (ValType(v_logger_obj) == V_UNDEF)
      v_logger_obj = uws_exchng_logger(null, vguid, null, "UpdateBrokContractRequestMsg", modulename(), null, RequestText);
   end;

   v_logger_obj.add_error_info(p_error.ErrorCode, p_error.ErrorDesc);

   EndAction(1);
  
   return false;

end; 


/*-------------------------------------------------*/
/*              Точка входа                        */
/*-------------------------------------------------*/
/* _contrid - ID договора брокерского обслуживания */
/* BIQ-7089 Гераськина Т.В. Сервис используется не только для закрытия, но и для обновления договора */
/* _typeaction: 1 - обновление, 3 - закрытие */
/*-------------------------------------------------*/
macro UpdateBrokContractRequestMsg (_contrid, _type, p_error:@object, _typeaction) 
   BegAction(1, "Выгрузка закрытия ");
   var v_out = run_UpdateBrokContractRequestMsg(_contrid, _type, @p_error, _typeaction);
   EndAction(1);

   return v_out;
end;

