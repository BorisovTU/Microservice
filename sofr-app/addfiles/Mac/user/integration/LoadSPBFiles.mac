/*-----------------------------------------------------------------------------------------
   LoadSPBFiles.mac
   Загрузка файлов по бирже СПБ из ЭП РСХБ
   BIQ-7041
   Гераськина Т.В.
-----------------------------------------------------------------------------------------*/
//import dlcontrmsg_incoming_spb_func;
import dlcontrfunc;
import global_classes;
import "ws_msg_transform_sec.mac"; 
import "usr_table_int.mac";


private const REGPATH_ANSWER_CLIENT_BOOKING = "SECUR\\SPB\\PATH_ANSWER_CLIENT_BOOKING";
private const REGPATH_ANSWER_CLIENT_ONLINE = "SECUR\\SPB\\PATH_ANSWER_CLIENT_ONLINE";
private const REGPATH_ANSWER_CLIENTS = "SECUR\\SPB\\PATH_ANSWER_CLIENTS";
private const REGPATH_ANSWER_FATCA = "SECUR\\SPB\\PATH_ANSWER_FATCA";
private const REGPATH_SPB82 = "SECUR\\SPB\\PATH_SPB82";


private macro GetStrRegVal(regPath:string)
   var stat = 0, path = "";
   var err_txt; 
   
   GetRegistryValue(regPath, V_STRING, path, stat);
   if(stat != 0)
      err_txt ="Ошибка при получении значения настройки: " + regPath;
      RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR)); 
   elif(path == "")
      stat = 1;
      err_txt ="Не задано значение настройки: " + regPath;
      RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
   end;
   return path;
end;


private class (c_usr_tbl_base) c_usr_tbl_uTableProcessIn()
   private class gen_uTableProcessIn()
      var T_RECID :integer,
          T_TIMESTAMP :date,
          T_OBJECTTYPE :integer,
          T_STATUS :integer,
          T_RESULTCODE :string,
          T_RESULTTEXT :string,
          T_FULLFILENAME:string;
   end;

   private macro init_table_obj()
      new_val_obj = gen_uTableProcessIn();
      where_val_obj = gen_uTableProcessIn();
   end;

   Initc_usr_tbl_base("uTableProcessIn_dbt");

   init_table_obj();
end;


macro LoadSPBFiles(_objecttype, _objectid)
   var sql_str, cmd, rs;
   var filePath;
   var fullFulename_src;
   var filetype = 0;
   var DL;
   var out_obj = c_Error, ErrText;
   var filePath_buf = "..\\TxtFile\\";
   var FileName, ExtName;
   var temp_f;
   var ProcessInTbl;


   // найдем исходное событие
/*   sql_str = "select i.t_recid, i.t_status, i.t_objecttype, i.t_fullfilename "+
             "  from utableprocessin_dbt i, "+
             "       dllvalues_dbt ll "+
             " where i.t_recid = :objectid "+
             "   and ll.t_list = 5002 and ll.t_code = to_char(i.t_objecttype) and LL.T_ELEMENT = :objecttype";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("objectid", RSDBP_IN, _objectid);
   cmd.AddParam("objecttype", RSDBP_IN, _objecttype);
   rs = RSDRecordSet(cmd);
   if (rs.movenext)
      fullFulename_src = rs.value("t_fullfilename");  // полный путь к файлу
      filetype = rs.value("t_objecttype");   // тип файла
   end; */

   // сразу все события
   sql_str = "select i.t_recid, i.t_status, i.t_objecttype, i.t_fullfilename "+
             "  from utableprocessin_dbt i "+
             " where t_objecttype in (61,62,63,64,66) and t_status = 3";
   cmd = RSDCommand(sql_str);
   rs = RSDRecordSet(cmd);
   while (rs.movenext)
      fullFulename_src = rs.value("t_fullfilename");  // полный путь к файлу
      filetype = rs.value("t_objecttype");   // тип файла
      // если запись есть, определим папку для обработки
      if (fullFulename_src)
         if (filetype > 0)
            if (filetype == 61)
               filePath = DL_GetFullPath(GetStrRegVal(REGPATH_ANSWER_CLIENT_BOOKING));
            elif (filetype == 62)
               filePath = DL_GetFullPath(GetStrRegVal(REGPATH_ANSWER_CLIENT_ONLINE));
            elif (filetype == 63)
               filePath = DL_GetFullPath(GetStrRegVal(REGPATH_ANSWER_CLIENTS));
            elif (filetype == 66)
               filePath = DL_GetFullPath(GetStrRegVal(REGPATH_SPB82));
            elif (filetype == 64)
               filePath = DL_GetFullPath(GetStrRegVal(REGPATH_ANSWER_FATCA));
            end;
         end;
      end;
      
      DL = TDirList; 
      splitfile(fullFulename_src, FileName, ExtName);
      FileName = FileName + ExtName;

      // если с папкой все получилось
      if (filePath)
         // при совпадающих папках (могут быть по-разному написаны сетевые пути) DL и CopyFile удаляют исходный файл и выпадают в ошибку
         // поэтому копируем через буфер
         DL.Copy(fullFulename_src, "", filePath_buf, false, false, "");   // на всякий случай без удаления
         if (DL.IsCopy(0))
            DL.Copy(filePath_buf+FileName, "", filePath, true, false, "");   // на всякий случай без удаления
         end; 
      end;
      DL = NULL;
   end;
   rs.close; cmd.close; rs = null; cmd = null;
   
/*     
   var MarketID = SPB_ID();
   // запустим обработку файла
   if (filetype == 61)
      ОбработатьВходящие_ClientBooking(MarketID);
   elif (filetype == 62)
      ОбработатьВходящие_ClientOnline(MarketID);
   elif (filetype == 63)
      ОбработатьВходящие_Clients(MarketID);
   elif (filetype == 64)
      ОбработатьВходящие_SPB82(MarketID);
   end;   */

   // обработка сразу всего, до чего дотянется
   if (filetype > 0)
      ExecMacroFile("dlcontrmsg_incoming_spb.mac");
   end;


   // максимально странная простановка кодов
   for (temp_f, vg_outFileArrayForIntegration)
      splitfile(temp_f.FullFileName, FileName, ExtName);
      FileName = FileName + ExtName;

      sql_str = "select t_recid from utableprocessin_dbt d "+
                " where t_objecttype in (61,62,63,64,66) and t_status in (2,3,5) "+
                "   and t_fullfilename like '%"+FileName+"'";
      rs = RSDRecordSet(sql_str);
      if (rs.movenext)
         ProcessInTbl = c_usr_tbl_uTableProcessIn;
         ProcessInTbl.Where_Val_Obj.T_RECID = rs.value(0);  
         ProcessInTbl.New_Val_Obj.T_RESULTCODE = temp_f.ErrorCode;
         ProcessInTbl.New_Val_Obj.T_RESULTTEXT = temp_f.ErrorText;

         if (temp_f.ErrorCode == 0)
            ProcessInTbl.New_Val_Obj.T_STATUS = 4;
         else 
            ProcessInTbl.New_Val_Obj.T_STATUS = 5;
         end;

         ProcessInTbl.Update(); 
         ProcessInTbl = NULL;
      end;
   end;

   out_obj.ErrorCode = 0;
   out_obj.ErrorDesc = "OK";

   return out_obj;

onError(err)
   out_obj = c_Error;
   out_obj.ErrorCode = c_err_obj.obtain_err_code(err);
   out_obj.ErrorDesc = c_err_obj.obtain_err_msg(err);

   return out_obj;
end;

//LoadSPBFiles(5062, 19939);