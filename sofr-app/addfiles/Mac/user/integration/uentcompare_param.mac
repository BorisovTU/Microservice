/**
 @file uentcompare_dlg.mac
 @brief uentcompare_param.mac

 # tag
 - functional_block: Отчетность_внутренняя
 - code_type: Report

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |24.10.2023 |Велигжанин А.В.|DEF-52575                                       |Реализован класс отчета-сверки с локальной работой c Excel
 |           |               |                                                | Класс TParamProcessEntriesVerify перенесен из файла
 |           |               |                                                | uentcompare_dlg.mac
*/

class TAccEntriesVerify( StrParam :string )
var PayerAccount :string = "",
    ReceiverAccount :string = "",
    PersN :string = "";
end;

//класс вызываемых параметров ProcessEvent
class TParamProcessEntriesVerify( StrParam :string )
   var AccEntries :TArray,
       RecId :integer = 0,
       ObjectType :integer = 0,
       ProcessType :integer = 0,
       DateIn :date = date("00.00.00"),
       DateOut :date = date("00.00.00"),
       ProcessStatus :integer = 0,
       QueryType :integer = 0
   ;

   private var StrSeparator :string = ";";


   private macro GetDateStr( StrBuf :string, DateSeparator :string) :string

    StrBuf = Substr( StrBuf, 1, 2 ) + DateSeparator + Substr( StrBuf, 3, 2 ) + DateSeparator + Substr( StrBuf, 5, 4 );
    return StrBuf;

   end;


   private macro InitParam( StrParam :string )
      var StrDigit :string = "0123456789",
          StrParamBuf :string = "",
          i :integer = 0,
          StrPos :integer = 0,
          StrParamAcc :string = "",
          StrAccD :string = "AccD",
          StrAccC :string = "AccC",
          StrPersN :string = "PersN",
          SymbOpen :string = "[",
          SymbClose :string = "]",
          AccEntriesVerify :TAccEntriesVerify;

//1689731;5009;[AccD47421810400002000002;AccC;AccD;AccC47407840399000000002]1;14.05.2019;;2
      if( (Valtype(StrParam) != V_UNDEF) and (Valtype(StrParam) != 26) and (StrParam != "") )

         i = 1;
         StrPos = 0;
         while( i <= StrLen(StrParam) ) //Вырезать часть со спец. разделителями

          if( (StrPos > 0) and ( Substr(StrParam, i, 1) != SymbClose ) )
           StrParamAcc = StrParamAcc + Substr(StrParam, i, 1);
          end;

          if( Substr(StrParam, i, 1) == SymbOpen )
           StrPos = i;
          end;

          if( Substr(StrParam, i, 1) == SymbClose )
           StrPos = -1;
          end;

          if( ((StrPos == 0 ) or (StrPos == -1)) and (Substr(StrParam, i, 1) != SymbOpen) and (Substr(StrParam, i, 1) != SymbClose) )
            StrParamBuf = StrParamBuf + Substr(StrParam, i, 1);
          end;
          i = i + 1;

         end;

         StrParam = StrParamBuf;
         StrParamBuf = "";


         i = 1;
         while( i <= StrLen(StrParam) ) //нормализация оставить, что только цифры или разделитель

            if( Index( (StrDigit + StrSeparator), Substr(StrParam, i, 1) ) >= 1 )
               StrParamBuf = StrParamBuf + Substr(StrParam, i, 1);
            end;
            i = i + 1;

         end;

         i = 1;
         while( (StrLen(StrParamBuf) > 0) and (GenNumProps(this) > i + 1) ) //заполняем свойства со второго
            StrPos = Index( StrParamBuf, StrSeparator );
            if( StrPos > 1 )
               if( ValType(this[i]) == V_DATE )
                this[i] =  date( GetDateStr( Substr(StrParamBuf, 1, StrPos - 1), "." ) );
               else
                this[i] =  int(Substr(StrParamBuf, 1, StrPos - 1));
               end;

               StrParamBuf = Substr(StrParamBuf, StrPos + 1);
            elif( (StrPos == 0) and (StrLen(StrParamBuf) > 0) )
               if( ValType(this[i]) == V_DATE )
                this[i] = date( GetDateStr( StrParamBuf, "." ) );
               else
                this[i] = int(StrParamBuf);
               end;
               StrParamBuf = "";
            end;
            i = i + 1;

         end;


         i = 0;
         AccEntries = Null;
         while( (StrLen(StrParamAcc) > 0) ) //заполняем спец. свойства
            StrPos = Index( StrParamAcc, StrSeparator );
            if( StrPos > 1 )
              StrParamBuf = Substr(StrParamAcc, 1, StrPos - 1);
              if( Index( StrParamBuf, StrPersN ) )
               AccEntriesVerify = Null;
               AccEntriesVerify = TAccEntriesVerify();
               AccEntriesVerify.PersN = StrSubst( StrParamBuf, StrPersN, "" );
              elif( Index( StrParamBuf, StrAccD ) )
               AccEntriesVerify.PayerAccount = StrSubst( StrParamBuf, StrAccD, "" );
              elif( Index( StrParamBuf, StrAccC ) )
               AccEntriesVerify.ReceiverAccount = StrSubst( StrParamBuf, StrAccC, "" );
               if( AccEntries == Null )
                AccEntries = TArray();
               end;
               AccEntries[AccEntries.Size] =  AccEntriesVerify;
              end;

              StrParamAcc = Substr(StrParamAcc, StrPos + 1);
            elif( (StrPos == 0) and (StrLen(StrParamAcc) > 0) )
              StrParamBuf = StrParamAcc;
              if( Index( StrParamBuf, StrPersN ) )
               AccEntriesVerify = Null;
               AccEntriesVerify = TAccEntriesVerify();
               AccEntriesVerify.PersN = StrSubst( StrParamBuf, StrPersN, "" );
              elif( Index( StrParamBuf, StrAccD ) )
               AccEntriesVerify.PayerAccount = StrSubst( StrParamBuf, StrAccD, "" );
              elif( Index( StrParamBuf, StrAccC ) )
               AccEntriesVerify.ReceiverAccount = StrSubst( StrParamBuf, StrAccC, "" );
               if( AccEntries == Null )
                AccEntries = TArray();
               end;
               AccEntries[AccEntries.Size] =  AccEntriesVerify;
              end;
              StrParamBuf = "";
              StrParamAcc = "";
            end;
            i = i + 1;

         end;

      end;

   onError(err)

      RecId = 0;
      ObjectType = 0;
      ProcessStatus = 0;


   end;

   InitParam( StrParam );

end;
