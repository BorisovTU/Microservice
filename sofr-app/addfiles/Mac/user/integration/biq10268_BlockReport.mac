/** 
 @file 		BIQ10268_BlockReport.mac
 @brief 	Начальное решение по BIQ-10268. Отчет о субъектах, блокированных для обмена с CDI
  
 # tag 
 - CDI
   
 # changeLog 
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |27.11.2024 |Велигжанин А.В.|CCBO-10808_CCBO-10846                           |Создание 

*/ 
IMPORT BankInter, RSD, RsbDataSet;


private VAR tableHeader = 
 "┌────────────────────────┬──────────────┬──────────────────────┬──────────────────────────────────────────┐\n"+
 "│ Идентификатор субъекта │ Код субъекта │     ИНН субъекта     │          Наименование субъекта           │\n" +
 "├────────────────────────┼──────────────┼──────────────────────┼──────────────────────────────────────────┤";


/**
  @brief    Формирование заголовка отчета
*/
PRIVATE MACRO PrintHeader()
  println("Отчет о субъектах, блокированных для обмена с CDI...");
  println (tableHeader);
END; // PrintHeader


/**
  @brief    Формирование строки отчета
*/
PRIVATE MACRO PrintRepLine( p_PartyID, p_PartyCode, p_INN, p_PartyName );
 [│ ###################### │ ############ │ #################### │ ######################################## │]  
  (p_PartyID:w, p_PartyCode:w, string(p_INN):w, p_PartyName:w);
END; // PrintRepLine


/**
  @brief    Формирование итога отчета
*/
PRIVATE MACRO PrintEnd()
 [└────────────────────────┴──────────────┴──────────────────────┴──────────────────────────────────────────┘];
END; // PrintEnd


/**
  @brief    Возвращает запрос для получения данных
*/
PRIVATE MACRO getSql : string
  return 
    "SELECT count(*) over() cnt, p.t_partyid AS partyID, c.t_code AS partyCode, NVL(o.t_code, ' ') AS inn, p.t_name AS partyName "
    + " FROM dparty_dbt p "
    + " JOIN dobjatcor_dbt r ON (r.t_objecttype = 3 and r.t_object = LPAD(p.t_partyid, 10, '0') and r.t_groupid = 175 and r.t_attrid = 1) "
    + " LEFT JOIN dpartcode_dbt c ON (c.t_partyid = p.t_partyid and c.t_codekind = 1) "
    + " LEFT JOIN dobjcode_dbt o ON (o.t_codekind = 16 AND o.t_objecttype = 3 AND o.t_objectid = p.t_partyid) "
    + " WHERE p.t_locked != 'X' "
    + " ORDER BY p.t_partyid "
  ;
END; // getSql


/**
  @brief    Процедура запуска процеса
*/
PRIVATE MACRO RepStartReport

  var x_Sql, x_Cmd, x_DS, x_Cnt = 0, i = 0, x_SavePartyID = -1, x_UniqueCnt = 0;

  PrintHeader();

  x_Sql = getSql();
  x_Cmd = RSDCommand(x_Sql);
  x_Cmd.Execute();
  x_DS = TRsbDataSet(x_Cmd);
  while (x_DS.movenext)
    if (x_Cnt == 0)
      x_Cnt = int(x_DS.cnt);
      InitProgress(x_Cnt, "Формирование отчета", "Формирование отчета");
    end;
    if( x_SavePartyID == x_DS.value("partyID") ) 
      PrintRepLine( StrFor(32), StrFor(32), x_DS.value("INN"), StrFor(32) );
    else
      x_SavePartyID = x_DS.value("partyID");
      x_UniqueCnt = x_UniqueCnt + 1;
      PrintRepLine( x_DS.value("partyID"), x_DS.value("partyCode"), x_DS.value("INN"), x_DS.value("partyName") );
    end;
    i = i + 1;
    UseProgress(i);
  end;
  RemProgress();

  PrintEnd();
  println("Общее количество блокированных субъектов: " + x_UniqueCnt);

  return 0;

END; // RepStartReport


/**
  @brief    Запуск отчета
*/
PRIVATE macro StartReport()

  if (MsgBoxEx("Вы желаете сформировать отчет о субъектах, блокированных для обмена с CDI?", MB_YES + MB_NO, IND_YES) == IND_YES)
    SetOutPut(gettxtfilename("biq10268_blockReport_"+StrSubst(string(date()),".","")+StrSubst(string(time()),":","")),false);
    var stat = RepStartReport(); // запуск отчета с формированием протокола
    ViewFile(SetOutPut(null, true));
    SetOutPut(null, true)
  end;
END; // StartReport


/**
  @brief    Точка входа. Вызов отчета.
*/
StartReport();