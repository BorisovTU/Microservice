/**
 @file 		mac\user\integration\uent306compare_pan.mac
 @brief 	Интеграция с внешними системами. Далоговое окно для параметров отчета-сверки зачислений и списаний ДС СОФР_ЦФТ

 # tag
 - functional_block: Отчетность_внутренняя
 - code_type: Report
 - code_type:GUI

 #menu 
  - БО ЦБ\ Отчеты \ РСХБ \ [13408] Сверка зачислений списаний ДС СОФР_ЦФТ

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |05.11.2024 |Велигжанин А.В.|DEF-75364                                       |Load306AccForCompare(), загрузка с учетом формата файла
 |01.11.2024 |Велигжанин А.В.|DEF-75364                                       |Исправлена ошибка обработки x_FileName
 |14.10.2024 |Велигжанин А.В.|BOSS-1266_BOSS-2664                             |Используется u306EntCompare_rep.mac (см. Ent306CompareRep())
 |09.10.2024 |Велигжанин А.В.|BOSS-1266_BOSS-5683                             |Рефакторинг, доработка загрузки файла
 |08.10.2024 |Велигжанин А.В.|BOSS-1266_BOSS-5662                             |Создание 

*/

import RsbFormsInter, globals, Календарь, oralib, likepy, cb_sql;
import "usr_table_int.mac", "u_common_email_utils.mac";
import "uentcompare_param.mac";
import "u306EntCompare_rep.mac", "load_306AccCompare.mac";

private const CV_EMAIL_GRP_ETCOMPARE = 3; //группа сверки проводок загруженных в ЦФТ из СОФР

private const FT_STRING = 7,
              FT_INTEGER = 1,
              FT_DOUBLE = 4,
              FT_DATE = 9,
              FT_MONEY = 25;

var ExitCode :integer,
    DateRep :date = {CurDate},
    minDate: date = date(0,0,0);


private class (c_usr_tbl_base) c_usr_tbl_uTableProcessEvent()
   private class gen_uTableProcessEvent()
      var T_RECID :integer,
          T_TIMESTAMP :date,
          T_OBJECTTYPE :integer,
          T_OBJECTID :integer,
          T_TYPE :integer,
          T_STATUS :integer,
          T_NOTE :string,
          T_MESSAGEID :integer,
          T_RESULTCODE :string,
          T_RESULTTEXT :string;
   end;

   private macro init_table_obj()
      new_val_obj = gen_uTableProcessEvent();
      where_val_obj = gen_uTableProcessEvent();
   end;

   Initc_usr_tbl_base("uTableProcessEvent_dbt");

   init_table_obj();
end; // c_usr_tbl_uTableProcessEvent

private macro AddColumn( ar:@TArray, ind, fld, head, width, fldType, DecPoint )
 ar.value( ind * 6 + 0 ) = fld; //FieldName
 ar.value( ind * 6 + 1 ) = head; //Header
 ar.value( ind * 6 + 2 ) = width; //Null //Width
 ar.value( ind * 6 + 3 ) = fldType; //fldType //2 = FBT
 ar.value( ind * 6 + 4 ) = DecPoint; //Null //DecPoint
 ar.value( ind * 6 + 5 ) = 0; //reserve
end; // AddColumn


private macro InsUserEvent( ObjectType :integer, ObjectId :integer, PocessType :integer, Status :integer, EventParam :string  ) :integer
var Query :string = "",
    cmd,
    ReqId :integer = 0;

 Query = " DECLARE " +
         "  v_ObjectType      utableprocessevent_dbt.T_OBJECTTYPE%TYPE := :p_ObjectType; " +
         "  v_ObjectId        utableprocessevent_dbt.T_OBJECTID%TYPE := :p_ObjectId; " +
         "  v_PocessType      utableprocessevent_dbt.T_TYPE%TYPE := :p_PocessType; " +
         "  v_Status          utableprocessevent_dbt.T_STATUS%TYPE := :p_Status; " +
         "  v_EventParam      utableprocessevent_dbt.T_PARAM%TYPE := :p_EventParam; " +
         "  v_RecId           utableprocessevent_dbt.T_PARAM%TYPE := :p_RecId; " +
         " BEGIN " +
         "  BEGIN " +
         
         "   INSERT INTO utableprocessevent_dbt( T_TIMESTAMP, T_OBJECTTYPE, T_OBJECTID, T_TYPE, T_STATUS, T_PARAM) " +
         "   VALUES( SYSDATE, v_ObjectType, v_ObjectId, v_PocessType, v_Status, v_EventParam) RETURNING T_RECID INTO v_RecId; " +

         "   :p_RecId := v_RecId; " +
           
         "  EXCEPTION " +
         "   WHEN OTHERS THEN " +
         "   BEGIN " +

         "    :p_RecId := -1; " +

         "   END; " +
         "  END; " +

         " END; ";

 cmd = RSDCommand( Query );
 cmd.AddParam( "p_ObjectType", RSDBP_IN, ObjectType );
 cmd.AddParam( "p_ObjectId", RSDBP_IN, ObjectId );
 cmd.AddParam( "p_PocessType", RSDBP_IN, PocessType );
 cmd.AddParam( "p_Status", RSDBP_IN, Status );
 cmd.AddParam( "p_EventParam", RSDBP_IN, EventParam );
 cmd.AddParam( "p_RecId", RSDBP_OUT, V_INTEGER );
 cmd.execute();

 if( ( ValType(cmd.value("p_RecId")) ==  V_UNDEF ) or ( ValType(cmd.value("p_RecId")) ==  26 ) or ( cmd.value("p_RecId") ==  -1 ))
  ReqId = 0;
 else
  ReqId = cmd.value("p_RecId");
 end;
 cmd.Close;


 return ReqId;


onError(err)

 return -1;

end; // InsUserEvent

//Заполняем доп. параметры Request из массива фильтра
private macro GetStrAddParamEntry( EntryList  :TArray ) :string
const ACCD_STR = "AccD",
      ACCC_STR = "AccC",
      PERSN_STR = "PersN",
      BEGIN_STR = "[",
      END_STR = "]",
      SEPARATOR = ";";
//Str_Param = "1689731;5009;[AccD47421810400002000002;AccC;AccD;AccC47407840399000000002]1;14.05.2019;00.00.00;2";
var Str_Param :string = "",
    i :integer = 0,
    fl_first_str :bool = false;

 if( (ValType( EntryList ) == V_UNDEF) or ( EntryList.Size == 0 ) )
  Str_Param = "";
 else

  while( i < EntryList.Size )
   if( ( EntryList[i].PersN != "-1" ) and ( EntryList[i].PayerAccount != "-1" ) and ( EntryList[i].ReceiverAccount != "-1" ) ) //признак неудаленного элемента массива

    if( fl_first_str )
     Str_Param = Str_Param + SEPARATOR;
    else
     fl_first_str = true;
     Str_Param = BEGIN_STR;
    end;

    Str_Param = Str_Param + PERSN_STR + EntryList[i].PersN + SEPARATOR + ACCD_STR + EntryList[i].PayerAccount + SEPARATOR + ACCC_STR + EntryList[i].ReceiverAccount;

   end;
   i = i + 1;
  end;

  if( Str_Param != "" )
   Str_Param = Str_Param + END_STR;
  end;


 end;

 return Str_Param;

end; // GetStrAddParamEntry


//в цикле дергаем загрузку SQL Loader
private macro LoopLoad /*LoadEntryForCompare*/( Mode :integer, CntLoop :integer, File_Path :string, ReqId :string, Fl_SetDialog :integer ) :bool
var Query :string = ""
    , Params :TArray
    , DataSet :RsdRecordset
    , v_email_processor
    , i :integer
    , x_Flag :integer = 1 // DEF-75364 загрузка файла по формату 1 (с доп.полями), если будет неудачно, то загрузка по формату 0
  ;

  if( ( ValType( CntLoop ) == V_UNDEF ) or ( ValType( CntLoop ) == 26 ) )
   CntLoop = 0;
  end;

  for( i, 0, CntLoop, 1)

   if(Load306AccForCompare(File_Path, ReqId, Fl_SetDialog, x_Flag ))
     Query = " SELECT COUNT(1) FROM uload306Accforcompare_dbt WHERE T_REQID = :p_ReqId";

    Params = makeArray( SQLParam("p_ReqId", ReqId) );
    DataSet = execSQLselect( Query, Params );

    if( ( DataSet.MoveNext ) and ( DataSet.value(0, Null, V_INTEGER) > 0 ) )
     return true;
    elif( i == CntLoop)

     if( Fl_SetDialog == 1 )
      MsgBox("Ошибка загрузки файла " + File_Path + " .");
     end;

     //отправляем в почту
     v_email_processor = Null;
     v_email_processor = c_email_proc_env();
     v_email_processor.m_set_msg_head("СОФР - Отчет результат сверки зачислений загруженных из БИСКВИТ в СОФР.");
     v_email_processor.m_add_row_to_msg_text("Ошибка загрузки файла " + File_Path + " ." );
     v_email_processor.m_save_to_submit_grp(CV_EMAIL_GRP_ETCOMPARE, true);
     v_email_processor.m_submit_email_synch();
     v_email_processor = Null;
     //отправляем в почту

     return false;
    end;

   elif(x_Flag == 1)
     // ошибка загрузки по формату 1, пробуем загружать по формату 0
     x_Flag = 0;

   else
     if( Fl_SetDialog == 1 )
      MsgBox("Ошибка загрузки файла " + File_Path + " .");
     end;

     //отправляем в почту
     v_email_processor = Null;
     v_email_processor = c_email_proc_env();
     v_email_processor.m_set_msg_head("СОФР - Отчет результат сверки зачислений загруженных из БИСКВИТ в СОФР.");
     v_email_processor.m_add_row_to_msg_text("Ошибка загрузки файла " + File_Path + " ." );
     v_email_processor.m_save_to_submit_grp(CV_EMAIL_GRP_ETCOMPARE, true);
     v_email_processor.m_submit_email_synch();
     v_email_processor = Null;
     //отправляем в почту

    return false;
   end;


  end; // for

  return true;

end; // LoopLoad


//Скроллинг запросов и сверок
private macro ScrollProcEnt306Compare( EntryList :@object ) :integer
var Query :string = ""
    , DataSet :RsdRecordset
    , cmd
    , Column = TArray()
    , ParamProcess :TParamProcessEntriesVerify
    , ProcessEventTbl :c_usr_tbl_uTableProcessEvent
    , state :integer = 0
    ;

  macro CheckIt ( p_RecID, p_Status )
     if( ((p_Status == 2) or (p_Status == 5)) and (p_RecID > 0) )  
        return 0;
     end;
     return 1;
  end; // CheckIt

  macro DoLoad( p_RecID, p_FileName )
    var x_FileName = p_FileName, x_FilePath;
     GetRegistryValue("РСХБ\\ИНТЕГРАЦИЯ\\ДИРЕКТОРИИ\\IMPORT\\306ACCCOMPAREDIR", V_STRING, x_FilePath);

     if(x_FileName == "") 
       GetString(x_FileName, "Имя файла:"); // DEF-75364 Исправление ошибки
     end;

     if( (x_FileName != "" ) and ( LoopLoad ( 1, 3, x_FilePath + "\\" + x_FileName, string(p_RecID), 1 ) ))
        MsgBox("Загрузка файла " + x_FileName + " завершена." + "Результаты загрузки в лог " + x_FilePath + "\\" + "CTL_" + string(UserNumber) + ".log" + ".");
        return 0;
     end;
     return 1;
  end; // DoLoad

  macro NotifyIt ( res, ParamProcess )
    if( ( res ) or ( ValType(res) == V_UNDEF ) )
     MsgBox( "Сверка зачислений по запросу " + string( ParamProcess.RecId ) + " завершена успешно.");
     //установка статуса в uTableProcessEvent_dbt
     ProcessEventTbl = c_usr_tbl_uTableProcessEvent();
     ProcessEventTbl.New_Val_Obj.T_STATUS = ParamProcess.ProcessStatus;
     ProcessEventTbl.Where_Val_Obj.T_RECID = ParamProcess.RecId;
     ProcessEventTbl.Where_Val_Obj.T_OBJECTTYPE = ParamProcess.ObjectType;
     if( not ProcessEventTbl.Update() ) //инструмент простых действий с таблицами из RSL В.Карпов
      state = 1;
     end;
     ParamProcess = Null;
     ProcessEventTbl = Null;
     //установка статуса в uTableProcessEvent_dbt
    else
     ParamProcess = Null;
     ProcessEventTbl = Null;

     MsgBox( "Сверка зачислений по запросу " + string( ParamProcess.RecId ) + " завершена с ошибкой.");
    end;
  end; // NotifyIt

  macro DoCompare( p_RecID, p_Param )
    var res;
    //Str_Param = "1689731;5009;[AccD47421810400002000002;AccC;AccD;AccC47421810400002000002]1;14.05.2019;00.00.00";
    ParamProcess = Null;
    ParamProcess = TParamProcessEntriesVerify( string( p_RecID )  + ";" +  "5030" + ";" + p_Param + ";4"); //установить статус 4
    res = Ent306CompareRep( p_RecID, ParamProcess, 1 ); // Выполнить сверку в диалоговом режиме
    NotifyIt( res, ParamProcess);
    return 0;
  end; // DoCompare

  macro LoadAndCompare ( rsd )
    var 
      x_State : integer = 0
      , x_RecID : integer = 0
      , x_Status : integer = 0
      , x_Param : string
      , x_FileName : string = ""
    ;

    x_RecID = rsd.value(0);
    x_Status = rsd.value(2);
    x_Param = rsd.value(3);

    // DEF-75364
    if(ValType(rsd.value(5)) == V_STRING)
      x_FileName = rsd.value(5);
    end;

    if(x_State == 0)
      x_State = CheckIt(x_RecID, x_Status);
    end;

    if(x_State == 0)
      x_State = DoLoad(x_RecID, x_FileName);
    end;

    if(x_State == 0)
      x_State = DoCompare(x_RecID, x_Param);
    end;

    return x_State;
  end; // LoadAndCompare

  macro EventHandler( rsd, cmd, id, key)
   if( (cmd == DLG_KEY) and (key == 27 ) ) //Esc
     ExitCode = 27;
     return CM_SELECT;

   elif( (cmd == DLG_KEY) and (key == 18 ) ) //Ctrl+R
     ExitCode = 18;
     return CM_SELECT;

   elif( (cmd == DLG_KEY) and (key == 316 ) ) //F2, Загрузка и сверка
     LoadAndCompare(rsd);
     ExitCode == 316;
     return CM_SELECT;
   end;

  end;

  Query = " SELECT A.T_RECID, A.T_TIMESTAMP, A.T_STATUS, A.T_PARAM, " 
          + "  ( CASE WHEN A.T_STATUS = 1 THEN 'направляется в БИСквит' " 
          + "         WHEN A.T_STATUS = 2 THEN 'обрабатывается в БИСквит' " 
          + "         WHEN A.T_STATUS = 3 THEN 'передача результата обработки ETL' " 
          + "         WHEN A.T_STATUS = 4 THEN 'завершена загрузка в СОФР' " 
          + "    END ) AS T_STATUSNAME, " 
          + "  ( SELECT B.T_RESULTTEXT FROM utableprocessin_dbt B " 
          + "    WHERE TRIM(B.T_RESULTTEXT) = 'GetEntriesForVerify306_' || TO_CHAR( A.T_RECID ) || '.csv' " 
          + "     AND B.T_OBJECTTYPE = 8 " 
          + "     AND B.T_STATUS = 4 ) AS T_FILENAME, " 
          + "  A.T_OPER " 
          + " FROM utableprocessevent_dbt A " 
          + " WHERE A.T_OBJECTTYPE = ? " 
          + " ORDER BY 1 DESC "
  ;


  AddColumn(@Column, 0, "T_RECID", "ИД запроса", 10, V_INTEGER );
  AddColumn(@Column, 1, "T_TIMESTAMP", "Дата запроса", 20, V_DATE );
  AddColumn(@Column, 2, "T_STATUSNAME", "Статус", 26, V_STRING );
  AddColumn(@Column, 3, "T_FILENAME", "Файл", 26, V_STRING );
  AddColumn(@Column, 4, "T_PARAM", "Параметры запроса", 250, V_STRING );
  AddColumn(@Column, 5, "T_OPER", "Опер.", 6, V_INTEGER );


  cmd = RsdCommand(Query);
  cmd.AddParam( "p_ObjectType", RSDBP_IN, 5030 );

  DataSet = RSDRecordSet( cmd, RSDVAL_CLIENT, RSDVAL_STATIC );
  RunScroll( DataSet, 6, Column, "ScrollProcEnt306Compare", @EventHandler, "Запросы в ЦФТ", "~Esc~ Выход    ~F2~ Загрузка и Сверка", true, 2, 8 );
  return ExitCode;
end; // ScrollProcEnt306Compare
//Скроллинг запросов и сверок


//панель параметров сверки
class (TRsbPanel) UserEnt306ComparePanel( DateReport_ :date, p_RepMode: integer )
var DateBegin :date = DateReport_
    , DateEnd :date = DateReport_
    , StrFilterParam :string = ""
    , Fl_Acc :bool = false
    , Fl_Entry :bool = true
    , EntryList :TArray
    , AcctList :TArray
    , FlFilterAcc :string = ""
    , FlFilterEnt :string = ""
    , RepMode: integer = p_RepMode
    ;

  InitTRsbPanel();
  SetCaption("Панель запроса сверки");
  SetStatus("~F2~ Отправить    ~F3~ Выбор    ~F4~ Результат     ~Esc~ Выход");
  SetSize(38,8);
  SetPosition(2,1);

 var DateBeginLabel :TRsbLabel = TRsbLabel(3, 2, "Дата c ");
     addLabel(DateBeginLabel);

 var DateBeginEdFld :TRsbEditField = TRsbEditField(FT_DATE);
     DateBeginEdFld.setPosition(8,2);
     DateBeginEdFld.setSize(9,1);
     DateBeginEdFld.Name = "DateBeginEdFld";
     DateBeginEdFld.value = DateBegin;
     addControl(DateBeginEdFld);

 var DateEndLabel :TRsbLabel = TRsbLabel(17, 2, " по ");
     addLabel(DateEndLabel);

 var DateEndEdFld :TRsbEditField = TRsbEditField(FT_DATE);
     DateEndEdFld.setPosition(20,2);
     DateEndEdFld.setSize(9,1);
     DateEndEdFld.Name = "DateEndEdFld";
     DateEndEdFld.value = DateEnd;
     addControl(DateEndEdFld);

  AddEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "onKeyPressed"));

  macro onKeyPressed(RsbEvent:Object)
  var focusedControl,
      ReqId :integer = 0;

   if( RsbEvent.KeyCode == 317 )  //F3
    focusedControl = this.focusedControl();

    if( focusedControl.Name == "DateBeginEdFld" )
     DateBegin = GetDateByCalendar(DateBegin);
     if( DateBegin != DateBeginEdFld.value  )
      DateBeginEdFld.value = DateBegin;
     end;
    end;

    if( focusedControl.Name == "DateEndEdFld" )
     DateEnd = GetDateByCalendar(DateEnd);
     if( DateEnd != DateEndEdFld.value  )
      DateEndEdFld.value = DateEnd;
     end;
    end;

   elif( RsbEvent.KeyCode == 27 )  //Esc
    this.Close(RsbEvent.KeyCode);

   elif( RsbEvent.KeyCode == 316 )  //F2
    //вызываем функционал Request
    DateBegin = DateBeginEdFld.Value;
    DateEnd = DateEndEdFld.Value;

     //Insert событие Request со статусом 11

     if( ( DateShift(DateBegin, 30) > DateEnd ) or ( MsgBoxEx( "Большой период запроса зачислений. Подтверждаете?", MB_YES+MB_NO, IND_NO, "Предупреждениее","" ) ==  IND_YES ) )
       ReqId = InsUserEvent( 5030, 1, 1, 1, GetStrAddParamEntry( EntryList ) + "1" + ";" + StrSubst( string(DateBegin):10," ","0") + ";"  + StrSubst( string(DateEnd):10," ","0") );
       MsgBox( "Создан запрос в БИСКВИТ, Id события " +  string(ReqId) );
     end;

    //вызываем функционал Request

   elif( RsbEvent.KeyCode == 318 )  //F4
    DateBegin = DateBeginEdFld.Value;
    DateEnd = DateEndEdFld.Value;


     //Скроллинг запросов и сверок
     ExitCode = Null;
     while( ExitCode != 27 )
      ExitCode = ScrollProcEnt306Compare;
     end;
     //Скроллинг запросов и сверок

   end; // if

  end; // onKeyPressed

end; // UserEnt306ComparePanel
