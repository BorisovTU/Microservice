/** 
 @file SendExecStatusReqMsg.mac
 @brief Процедура обновления платежа
  
 # tag 
 - functional_block:Проводки
 - code_type:API

   
 # changeLog 
 |date       |author         |tasks            |note                                                         
 |-----------|---------------|-----------------|--------------------------------
 |14.09.2018 |Киселев А.     |                 | Создание
 |           |               |DEF-51038        | если пришел статус ЛИКВИД, обрабатываем, аналогично АННУЛ
 |18.01.2024 |Гераськина Т.В.|DEF-59041        | Некорректная обработка для проводок, связанных с платежом

*/ 

import PaymInter;
import DVInter, oralib, likepy;
import "global_utils_intgr.mac";
import "global_classes.mac";
import "dlcontrfunc.mac";
import "cblogger2.mac";

private class c_IntegrSymId(p_income_data)
   var ObjectId
      ,SystemId
      ,SystemNodeId;

   private macro m_init_prime(i_income_data)
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         ObjectId = uTryToGetPropS(i_income_data, "ObjectId");
         if(ValType(ObjectId) == V_UNDEF)
            v_service_msg = string(InErrComPart, "ObjectId");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;

         SystemId = uTryToGetPropS(i_income_data, "SystemId");
         SystemNodeId = uTryToGetPropS(i_income_data, "SystemNodeId");
      end;
   end;

   m_init_prime(p_income_data);
end;

/**
   @brief Классы входных данных
*/
///< begin
private class c_ApproveEntry(p_income_data)
   var EntryStatus
      ,SWIFTReply
      ,SWIFTError
      ,SPFSError
      ,UserId
      ,EntryIdBis
      ,EntryIdSofr;

   private macro m_init_prime(i_income_data)
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         EntryStatus = uTryToGetPropS(i_income_data, "EntryStatus");
         if(ValType(EntryStatus) == V_UNDEF)
            v_service_msg = string(InErrComPart, "EntryStatus");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;

         SWIFTReply = uTryToGetPropS(i_income_data, "SWIFTReply");
         SWIFTError = uTryToGetPropS(i_income_data, "SWIFTError");
         SPFSError = uTryToGetPropS(i_income_data, "SPFSError");

         if(ValType(uTryToGetPropS(i_income_data, "UserId")) != V_UNDEF)
            UserId = c_IntegrSymId(i_income_data.UserId);
         else
            v_service_msg = string(InErrComPart, "UserId");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;

         if(ValType(uTryToGetPropS(i_income_data, "EntryIdBis")) != V_UNDEF)
            EntryIdBis = c_IntegrSymId(i_income_data.EntryIdBis);
         else
            v_service_msg = string(InErrComPart, "EntryIdBis");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;

         if(ValType(uTryToGetPropS(i_income_data, "EntryIdSofr")) != V_UNDEF)
            EntryIdSofr = c_IntegrSymId(i_income_data.EntryIdSofr);
         else
            v_service_msg = string(InErrComPart, "EntryIdSofr");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;
      end;
   end;

   m_init_prime(p_income_data);
end;


private class c_SendExecStatusReq(p_income_data)
   var RequestId;
   var ApproveEntry;

   private macro m_init_prime(i_income_data)
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         if(ValType(uTryToGetPropS(i_income_data, "RequestId")) != V_UNDEF)
            RequestId = c_IntegrSymId(i_income_data.RequestId);
         else
            v_service_msg = string(InErrComPart, "RequestId");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;

         if(ValType(uTryToGetPropS(i_income_data, "ApproveEntry")) != V_UNDEF)
            ApproveEntry = c_ApproveEntry(i_income_data.ApproveEntry);
         else
            v_service_msg = string(InErrComPart, "ApproveEntry");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;
      end;
   end;

   m_init_prime(p_income_data);
end;


private class c_SendExecStatusReqMsg_req(p_income_data)
   var SendExecStatusReq;

   private macro m_init_prime(i_income_data)
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         if(ValType(uTryToGetPropS(i_income_data, "SendExecStatusReq")) != V_UNDEF)
            SendExecStatusReq = c_SendExecStatusReq(i_income_data.SendExecStatusReq);
         else
            v_service_msg = string(InErrComPart, "SendExecStatusReq");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;
      end;
   end;

   m_init_prime(p_income_data);
end;
///< end


/**
   @brief Классы исходящих данных
*/
///< begin
private class c_Error_ErrLst
   var ErrorCode
      ,ErrorDesc;
end;

//shev 28.01.2020 isup 504013
private class с_ApproveEntryResultWithoutErrList
   var ResultCode
      ,EntryIdSofr = c_IntegrSymId
      ,EntryIdBis = c_IntegrSymId;
end;

private class с_SendExecStatusWithoutErrListResp
   var RequestId = c_IntegrSymId;
   var ApproveEntryResult = с_ApproveEntryResultWithoutErrList;
end;

private class c_SendExecStatusReqMsgWithoutErrList_resp
   var SendExecStatusResp = с_SendExecStatusWithoutErrListResp;
end;

private class c_out_obj_WithoutErrList
   var SendExecStatusRespMsg = c_SendExecStatusReqMsgWithoutErrList_resp;
end;

//shev 28.01.2020 isup 504013
private class с_ApproveEntryResult
   var ResultCode
      ,EntryIdSofr = c_IntegrSymId
      ,EntryIdBis = c_IntegrSymId
      ,ErrorList = TArray;
end;

private class с_SendExecStatusResp
   var RequestId = c_IntegrSymId;
   var ApproveEntryResult = с_ApproveEntryResult;
end;

private class c_SendExecStatusReqMsg_resp
   var SendExecStatusResp = с_SendExecStatusResp;
end;

private class c_out_obj
   var SendExecStatusRespMsg = c_SendExecStatusReqMsg_resp;
end;
///< end

// BOSS-1585 Отправка стутуса об исполнениии платежа
// BOSS-7010 Отправка статуса только после проведения платежа в ЦФТ
// DEF-82482 Отправка статуса для платежей
private macro send_order_status_done( p_ref_id)

   var v_sql, v_cmd;
   var v_ref_id;

   if(substr(p_ref_id, 1, 4) == "paym") 
      /*---------*/
      /* Платежи */
      /*---------*/
      v_ref_id = Int(substr(p_ref_id, 5));

      v_sql =         "declare ";
      v_sql = v_sql + "  v_nptxop_id dnptxop_dbt.t_id%type; ";
      v_sql = v_sql + "  v_paymentid dpmpaym_dbt.t_paymentid%type := :p_ref_id; ";
      v_sql = v_sql + "begin ";
      v_sql = v_sql + "  select max(n.t_id) into v_nptxop_id ";
      v_sql = v_sql + "    from dpmpaym_dbt t ";
      v_sql = v_sql + "    join dnptxop_dbt n ";
      v_sql = v_sql + "      on n.t_id = t.t_documentid and n.t_dockind = t.t_dockind ";
      v_sql = v_sql + "   where t.t_paymentid = v_paymentid ";
      v_sql = v_sql + "     and n.t_dockind = 4607 and n.t_subkind_operation = 20 ";
      v_sql = v_sql + "     and t.t_payeraccount like '306%' and t.t_receiveraccount not like '603%'; ";
      v_sql = v_sql + "  if v_nptxop_id is not null ";
      v_sql = v_sql + "  then ";
      v_sql = v_sql + "     nontrading_orders_utils.send_order_status(p_operid  => v_nptxop_id,p_status => nontrading_orders_read.buf_status_done ); ";
      v_sql = v_sql + "  end if; ";
      v_sql = v_sql + "exception ";
      v_sql = v_sql + "  when others then ";
      v_sql = v_sql + "    it_log.log_error(p_object => 'nptx_money-> macro send_order_status_done' ";
      v_sql = v_sql + "                    ,p_msg => 'Ошибка при установке статуса Исполнено для платежа ' || v_paymentid); ";
      v_sql = v_sql + "end; ";

      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_ref_id", RSDBP_IN, v_ref_id);
      v_cmd.execute();

      v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   end;
onError(err)
   var v_err_sql =         "begin ";
   v_err_sql = v_err_sql + "  it_log.log_error(p_object => 'nptx_money-> macro send_order_status_done' ";
   v_err_sql = v_err_sql + "                  ,p_msg => 'Ошибка при установке статуса Исполнено для ref_id="+p_ref_id+": "+err.message+"~"+getFullCmdErrorText(v_cmd)+"');";
   v_err_sql = v_err_sql + "end; ";
   var v_err_cmd = RSDCommand(v_err_sql);
   v_err_cmd.execute();
   v_err_cmd.close();
   v_err_cmd = null; v_err_sql = null;
end;

/**
@brief ищем сделку по связке и получаем количество шагов "Готов к выполнению"
@param[in] AccTrnId  ID проводки
@return Количество шагов/0
*/
private macro GetReadyStepCnt( AccTrnId :integer) :integer
var Query :string = "",
    cmd,
    CntStep :integer,
    Params :TArray,
    state :integer = 0;


 Query = " DECLARE " +
         "  v_AccTrnId     dacctrn_dbt.T_ACCTRNID%TYPE := :p_AccTrnId; " +
         "  v_CntStep     INTEGER; " +
         "  p_CntStep     INTEGER; " +
         " BEGIN " +
         "  BEGIN " +

         "   SELECT COUNT(1) INTO v_CntStep FROM doprstep_dbt A " +
         "   WHERE A.T_ID_OPERATION = " +
         
         "    ( SELECT B.T_ID_OPERATION FROM doprdocs_dbt B  " +
         "      WHERE B.T_ACCTRNID = v_AccTrnId " +
         "       AND B.T_DOCKIND = 1 " +
         "       AND B.T_DOCUMENTID = CHR(1) " +
         "       AND ROWNUM = 1 ) " +
         "    AND A.T_ISEXECUTE = 'R'; " +

         "   :p_CntStep := v_CntStep; " +

         "  EXCEPTION " +
         "   WHEN OTHERS THEN " +
         "   BEGIN " +
         "    :p_CntStep := 0; " +
         "   END; " +
         "  END; " +

         " END; ";

 cmd = RSDCommand(Query);
 cmd.AddParam("p_AccTrnId", RSDBP_IN, AccTrnId );
 cmd.AddParam("p_CntStep", RSDBP_OUT, V_INTEGER, 0 );
 cmd.execute();

 if( (ValType( cmd.value("p_CntStep") ) == V_UNDEF) or (ValType( cmd.value("p_CntStep") ) == 26) )
  CntStep = 0;
 else
  CntStep = cmd.value("p_CntStep");
 end;

 return CntStep;

onError(err)

 return 0;

end;


/**
@brief ищем сделку по связке и толкаем операцию
@param[in] AccTrnId  ID проводки
@return 0 - ОК / 1 - ошибка
*/
private macro RunOperationDvnDeal( AccTrnId :integer) :integer
var Query :string = "",
    cmd,
    DvnDealId :integer,
    Params :TArray,
    state :integer = 0;


 Query = " DECLARE " +
         "  v_AccTrnId     dacctrn_dbt.T_ACCTRNID%TYPE := :p_AccTrnId; " +
         "  v_DvnDealId    ddvndeal_dbt.T_ID%TYPE; " +
         "  p_DvnDealId    ddvndeal_dbt.T_ID%TYPE; " +
         " BEGIN " +
         "  BEGIN " +

         "   SELECT C.T_ID INTO v_DvnDealId FROM ddvndeal_dbt C " +
         "   WHERE C.T_ID IN( ( SELECT TO_NUMBER(A.T_DOCUMENTID) FROM doproper_dbt A " +
         "                      WHERE A.T_ID_OPERATION IN( ( SELECT B.T_ID_OPERATION FROM doprdocs_dbt B " +
         "                                                   WHERE B.T_ACCTRNID = v_AccTrnId " +
         "                                                    AND B.T_DOCKIND = 1 " +
         "                                                    AND B.T_DOCUMENTID = CHR(1) ) ) ) ) " +
         "    AND ROWNUM = 1; " +

         "   :p_DvnDealId := v_DvnDealId; " +

         "  EXCEPTION " +
         "   WHEN OTHERS THEN " +
         "   BEGIN " +
         "    :p_DvnDealId := -1; " +
         "   END; " +
         "  END; " +

         " END; ";

 cmd = RSDCommand(Query);
 cmd.AddParam("p_AccTrnId", RSDBP_IN, AccTrnId );
 cmd.AddParam("p_DvnDealId", RSDBP_OUT, V_INTEGER, 0 );
 cmd.execute();

 if( (ValType( cmd.value("p_DvnDealId") ) == V_UNDEF) or (ValType( cmd.value("p_DvnDealId") ) == 26) or (cmd.value("p_DvnDealId") == -1 ))
  DvnDealId = -1;
 else
  DvnDealId = cmd.value("p_DvnDealId");
  state = DV_ExecDeal(DvnDealId, false);
 end;

 if( DvnDealId == -1)
  return 1;
 elif( (state > 0)/* and ( state != 9 )*//*KEA 15.05.20*/  )
  return state;
 end;
 return 0;

onError(err)

 return 1;

end;


/**
@brief проверяем наличие АННУЛ в проводке
@param[in] AccTrnId  ID проводки
@return 1 - нашли / 0 - не нашли

shev 27.06.2019
*/
private macro CheckAccTrnAnnul( AcctrnId :integer ) :integer
var sql :string = "",
    cmd, rs;

  sql = " select * from dacctrn_dbt  where t_acctrnid =  :p_Acctrnid and t_userfield4 like '%АННУЛ%' " ;
 
  cmd = RSDCommand(sql);
  cmd.AddParam("p_Acctrnid", RSDBP_IN, AcctrnId);

  rs = RSDRecordSet(cmd);
  if (rs.movenext)
    return 1;
    rs.close; rs = null; cmd.close; cmd = null;
  end;

  return 0;
  rs.close; rs = null; cmd.close; cmd = null;

end;


/**
@brief проверяем наличие АННУЛ в платеже
@param[in] PaymentId  ID платежа
@return 1 - нашли / 0 - не нашли

shev 27.06.2019
*/
private macro CheckPaymentAnnul( PaymentId:integer ) :integer
var sql :string = "",
    cmd, rs;

  sql = " select * from dpmpaym_dbt  where t_paymentid =  :p_Paymentid and t_userfield4 like '%АННУЛ%' " ;
 
  cmd = RSDCommand(sql);
  cmd.AddParam("p_Paymentid", RSDBP_IN, PaymentId);

  rs = RSDRecordSet(cmd);
  if (rs.movenext)
    return 1;
    rs.close; rs = null; cmd.close; cmd = null;
  end;

  return 0;
  rs.close; rs = null; cmd.close; cmd = null;

end;


/**
@brief приклеиваем в платеж АННУЛ к userfield4
@param[in] PaymentId  ID платежа
@param[in] FldValue  постфикс ("АННУЛ")
@return 0 - ОК / 1 - ошибка

shev 27.06.2019
*/
private macro UpdatePaymentUserfield( PaymentId :integer, FldValue :string ) :integer
var sql :string = "",
    cmd, rs;

  sql = " DECLARE " +
          "  v_PaymentId              dpmpaym_dbt.T_PAYMENTID%TYPE := :p_PaymentId; " +
          "  v_UsrField               dpmpaym_dbt.T_USERFIELD4%TYPE := :p_UsrField; " +
          " BEGIN " +
          
          "  UPDATE dpmpaym_dbt " +
          "  SET T_USERFIELD4 = (select t_userfield4 from dpmpaym_dbt where t_paymentid = v_PaymentId) ||'/'|| v_UsrField  " +
          "  WHERE T_PAYMENTID = v_PaymentId; " +

          " COMMIT;   "+

          " END; ";
 
  cmd = RSDCommand(sql);
  cmd.AddParam("p_PaymentId", RSDBP_IN, PaymentId);
  cmd.AddParam("p_UsrField", RSDBP_IN, FldValue);
  cmd.execute();

  return 0;

onError(err)

 return 1;

end;


/**
@brief приклеиваем в проводку АННУЛ к userfield4
@param[in] AccTrntId  ID проводки
@param[in] FldValue  постфикс ("АННУЛ")
@return 0 - ОК / 1 - ошибка

shev 27.06.2019
*/
private macro UpdateTrnUserfield( AccTrntId :integer, FldValue :string ) :integer
var sql :string = "",
    cmd, rs;

  sql = " DECLARE " +
          "  v_AcctrnId              dacctrn_dbt.T_ACCTRNID%TYPE := :p_AcctrnId; " +
          "  v_UsrField              dacctrn_dbt.T_USERFIELD4%TYPE := :p_UsrField; " +
          " BEGIN " +
          
          "  UPDATE dacctrn_dbt " +
          "  SET T_USERFIELD4 = (Select t_userfield4 from dacctrn_dbt where t_acctrnid = v_acctrnid) ||'/'|| v_UsrField " +
          "  WHERE T_ACCTRNID = v_ACCTRNId; " +
          
          " COMMIT;   "+

          " END; ";

  cmd = RSDCommand(sql);
  cmd.AddParam("p_AcctrnId", RSDBP_IN, AccTrntId);
  cmd.AddParam("p_UsrField", RSDBP_IN, FldValue);
  cmd.execute();

  return 0;

onError(err)

 return 1;

end;


/**
@brief поискать платеж во входящих из АБС
@param[in] Biscod  ID АБС
@return 0 - не нашли / 1 - нашли

shev 27.06.2019
*/
private macro CheckPaymentAccTrn( Biscod:integer ) :integer

   var Params :TArray = TArray();
   var state :integer = 0;

   Params = Null;
   Params = makeArray( SQLParam("p_BiscottoId", Biscod ) ); 
   state = execStoredFunc( "USR_PKG_IMPORT_SOFR.CheckBiscottoId", V_INTEGER, Params );
   Params = Null;

   if(state == 0)
      return 0;
   else
      return 1;
   end;

end;


/**
@brief найти соответствующие дополнительные статусы
@param[in] p_entrystatus  код из XML
@return числовой код статуса

BIQ-8561 gtv
*/
private macro FindNumberEntryStatus(p_entrystatus)
   var sql_str, cmd, rs;
   var res = -1;
   
   sql_str = "select t_flag from dllvalues_dbt where t_list = 5010 and t_code = :entrystatus";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("entrystatus", RSDBP_IN, p_entrystatus);
   rs = RSDRecordSet(cmd);
   if (rs.movenext)
      res = rs.value("t_flag");
   end;
   cmd.close; rs.close; cmd = null; rs = null;

   return res;
end;


/**
@brief обновить статус платежа
@param[in] p_NumberEntryStatus числовой код статуса
@param[in] p_PaymentID_inner ID платежа
@return ОК / текст ошибки

BIQ-8561 gtv
*/
private macro UpdatePaymentStatus(p_NumberEntryStatus, p_PaymentID_inner)
   var sql_str, cmd;
   var res = "Ошибка";  // пессимистично
   sql_str = 
      "declare "+
      "   v_status number := :p_status; "+
      "   v_paymentid number := :p_paymentid; "+
      "   v_oper number := :p_oper; "+
      "   v_paymstatus number; "+
      "   v_out varchar2(200); "+
      "   v_stat number; "+
      "begin "+
      "   v_out := 'OK'; "+
      "   begin "+
      "      select t_paymstatus into v_paymstatus  "+
      "      from dpmpaym_dbt "+
      "      where t_paymentid = v_paymentid; "+
      "      if v_paymstatus in (0,32000) then "+
      "         v_out := 'Платеж '||v_paymentid||' в неподходящем статусе: '||v_paymstatus; "+
      "      else  "+
      "         v_stat := PM_COMMON.ChangePaymStatus(v_paymentid, v_status, 0, 0, v_oper); "+
      "         IF v_stat <> 0 THEN "+
      "             v_out := 'Ошибка при установке статуса платежа'; "+
      "         END IF; "+
      "      end if; "+
      "   exception "+
      "      when no_data_found then "+
      "         v_out := 'Платеж '||v_paymentid||' не найден'; "+
      "   end; "+
      "   :p_out := v_out; "+
      "exception "+
      "   when others then :p_out := sqlerrm; "+
      "end;";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("p_status", RSDBP_IN, p_NumberEntryStatus);
   cmd.AddParam("p_paymentid", RSDBP_IN, p_PaymentID_inner);
   cmd.AddParam("p_oper", RSDBP_IN, {oper});
   cmd.AddParam("p_out", RSDBP_OUT, V_STRING, 200 );
   cmd.execute;
   
   if( (ValType( cmd.value("p_out") ) == V_UNDEF) or (ValType( cmd.value("p_out") ) == 26) )
   else
      res = cmd.value("p_out");
   end;
   cmd.close; cmd = null; 
   return res;
end;

/**
@brief Установить статус операции подкрепления в статус закрыта
*/
macro continue_crediting_of_funds(p_ref_id, p_logID)
  var cmd = RsdCommand("DECLARE " +
                         "v_paymentid integer := :paymentid; " +
                         "v_oper integer := :oper; " +
                         "v_logid integer := :logid; " +
                         "v_refillid integer; " +
                         "v_nptxopid integer; " +
                       "BEGIN " +
                         "BEGIN " +
                           "SELECT t_refillid, t_nptxopid " +
                             "INTO v_refillid, v_nptxopid " +
                             "FROM unptxop_payment_link_dbt " +
                            "WHERE t_paymentid = v_paymentid; " +
                         "EXCEPTION " +
                           "WHEN OTHERS THEN " +
                             "RETURN; " +
                         "END; " +

                         "UPDATE urefill_dbt " +
                            "SET t_status = 2, t_status_name = 'Закрыта' " +
                          "WHERE t_id = v_refillid; " +

                         "INSERT INTO urefill_step_dbt " +
                                     "(t_stepid, t_refillid, t_date_operation, t_systemdate, t_action, t_comment, t_oper) " +
                              "SELECT 0, t_id, t_date_operation, sysdate, 50, null, v_oper " +
                                "FROM urefill_dbt " +
                               "WHERE t_id = v_refillid; " +

                         "UPDATE unptxop_payment_link_dbt " +
                            "SET t_responseid = v_logid " +
                          "WHERE t_paymentid = v_paymentid; " +
  
                         "INSERT INTO unptxop_payment_link_step_dbt " +
                                     "(t_stepid, t_refillid, t_systemdate, t_action, t_comment, t_oper) " +
                              "VALUES (0, v_refillid, sysdate, 50, null, v_oper); " +

                         "INSERT INTO dfuncobj_dbt " +
                                     "(t_funcid, t_objectid, t_param, t_objecttype) " +
                              "VALUES (11503, v_nptxopid, null, 4600); " +
                       "END; ");
  cmd.AddParam("paymentid", RSDBP_IN, Int(StrSubst(p_ref_id, "paym", "")));
  cmd.AddParam("oper", RSDBP_IN, {oper});
  cmd.AddParam("logid", RSDBP_IN, p_logID);
  cmd.Execute();
  
OnError(err)
  NewLogger(c_logger.IT_LOG_TYPE, "continue_crediting_of_funds").Error("p_ref_id = " + String(p_ref_id) + ", p_logID = " + String(p_logID) + ", ошибка: " + GetFullErrMsg(err));
end;

/**
@brief основная процедура
@param[in] RequestObj_ объект входящего XML-запроса
@param[in] logID id лога usr_exchng_log_dbt
@return сформированный объект ответа на XML
*/
macro SendExecStatusReqMsg( RequestObj_ :object, logID ) :object 
const ANNUL = "АННУЛ";   //shev 27.06.2019
const ANNUL1 = "ЛИКВИД";   // DEF-51038, если пришел статус ЛИКВИД, обрабатываем, аналогично АННУЛ
var ResponseObj;
var vc_EntryIdSofr = 0;
var vc_EntryIdBis = 0;
var vc_RequestId = 0;
var Payment :RsbPayment = NULL; //08.10.2020 делаем платеж равным NULL, чтобы не было возможности обратиться к полям объекта без определения объекта
var ReadyStepCnt :integer = 0,
    i :integer = 0,
    Fl_Err :bool = false;
//BIQ-8567
var NumberEntryStatus = 0;
var UpdateStatus_res;
var PaymentID_inner = 0;

var RequestObj = c_SendExecStatusReqMsg_req(RequestObj_);
  
//shev 27.06.2019 приклеиваем к userfield4 АННУЛ, если платеж или проводка аннулированы в бисквит
   vc_EntryIdSofr = RequestObj.SendExecStatusReq.ApproveEntry.EntryIdSofr.ObjectId;
   vc_EntryIdBis = RequestObj.SendExecStatusReq.ApproveEntry.EntryIdBis.ObjectId;
   vc_RequestId = RequestObj.SendExecStatusReq.RequestId.ObjectId;

   if( (RequestObj.SendExecStatusReq.ApproveEntry.EntryStatus == ANNUL) 
     or (RequestObj.SendExecStatusReq.ApproveEntry.EntryStatus == ANNUL1) // DEF-51038, анулируем данные при статусе ЛИКВИД
   )
      Payment = Null;
      Payment = RsbPayment(int(substr(vc_EntryIdSofr,5)));

      if(substr(vc_EntryIdSofr, 1, 4) == "paym")                     //платеж
         if(CheckPaymentAnnul(substr(vc_EntryIdSofr, 5)) == 0)
            UpdatePaymentUserfield(substr(vc_EntryIdSofr,5),ANNUL);
         end;
      elif(substr(vc_EntryIdSofr, 1, 5) == "trnpm")                  //платеж или проводка
         if(CheckPaymentAccTrn(vc_EntryIdBis) == 1)                  // проверяем по коду бисквита платеж это или проводка
            if(CheckPaymentAnnul(substr(vc_EntryIdSofr,6)) == 0)
               UpdatePaymentUserfield(substr(vc_EntryIdSofr,6),ANNUL);
            end;
         else
            if(CheckAccTrnAnnul(substr(vc_EntryIdSofr,6)) == 0)
               UpdateTrnUserfield(substr(vc_EntryIdSofr,6),ANNUL);
            end;
         end;
      else                                                           //проводка
         if(CheckAccTrnAnnul(vc_EntryIdSofr) == 0)
            UpdateTrnUserfield(vc_EntryIdSofr,ANNUL);
         end;
     end;

   elif(strupr(RequestObj.SendExecStatusReq.ApproveEntry.EntryStatus) == "ПРОВЕДКАС")
//shev 28.01.2020 isup 504013
//     ResponseObj = Null;
//     ResponseObj = c_out_obj;

      ReadyStepCnt = GetReadyStepCnt( int(StrSubst(vc_EntryIdSofr,"trnpm","" )) );
      i = 0;
      Fl_Err = false;
      while( i < ReadyStepCnt )
         if( RunOperationDvnDeal( int(StrSubst(vc_EntryIdSofr,"trnpm","" )) ) == 0 ) //!!!!заряжаем функцию толкания операции
            Fl_Err = true;
         else
            Fl_Err = false;
            Break;
         end;
         i = i + 1;
      end;

      if( Fl_Err )
//shev 28.01.2020 isup 504013
         ResponseObj = Null;
         ResponseObj = c_out_obj_WithoutErrList;

       // результат набиваем объект Response
         ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.RequestId = RequestObj.SendExecStatusReq.RequestId;
         ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.ResultCode = 0;
         ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.EntryIdSofr.ObjectId = string(vc_EntryIdSofr);
         ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.EntryIdBis.ObjectId = string(vc_EntryIdBis);

      else
//shev 28.01.2020 isup 504013
         ResponseObj = Null;
         ResponseObj = c_out_obj;
         // результат набиваем объект Response
         ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.ResultCode = UNIVERSAL_ERROR_CODE;
         ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.ErrorList = TArray;
         ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.EntryIdSofr.ObjectId = string(vc_EntryIdSofr);
         ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.EntryIdBis.ObjectId = string(vc_EntryIdBis);

         ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.ErrorList.value(0) = c_Error;
         ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.ErrorList.value(0).ErrorCode = UNIVERSAL_ERROR_CODE;
         ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.ErrorList.value(0).ErrorDesc = "Не удалось выполнить операцию акцепта";

      end;

      RequestObj = Null;
      return ResponseObj;
   end;

   // BIQ-8561 gtv дополнительные статусы   
   if(strupr(RequestObj.SendExecStatusReq.ApproveEntry.EntryStatus) != "ПРОВЕДКАС") 
      if(substr(vc_EntryIdSofr, 1, 4) == "paym")                     //платеж
         NumberEntryStatus = FindNumberEntryStatus(RequestObj.SendExecStatusReq.ApproveEntry.EntryStatus);
         if (NumberEntryStatus > 0)
            PaymentID_inner = int(substr(vc_EntryIdSofr, 5));
            UpdateStatus_res = UpdatePaymentStatus(NumberEntryStatus, PaymentID_inner); // просто меняем статус, безо всяких доп.действий
            if (UpdateStatus_res != "OK")
               RunError(UpdateStatus_res, c_usr_err_obj(UpdateStatus_res));
            end;
         end;
      end;
   end;
   
   // BOSS-7010 Отправка статуса только после проведения платежа в ЦФТ
   if(strupr(RequestObj.SendExecStatusReq.ApproveEntry.EntryStatus) == "ПРОВЕД") 
      send_order_status_done(vc_EntryIdSofr);
      continue_crediting_of_funds(vc_EntryIdSofr, logID);
   end;


   if(strupr(RequestObj.SendExecStatusReq.ApproveEntry.EntryStatus) != "ПРОВЕДКАС")  //ПРОВЕДКАСС А.Киселев 20.01.2020 кто-то должен поставить точку как правильно писаь ПРОВЕДКАСС или ПРОВЕДКАС(шлет БИСКВИТ)
      if (Payment != NULL)//08.10.2020 Условие вставлено чтобы не было обращений к пустому объекту платежа
         Payment.PaymStatus = int(RequestObj.SendExecStatusReq.ApproveEntry.EntryStatus); //меняем статус 
      end;
      ResponseObj = c_out_obj_WithoutErrList;  //shev isup504013

      ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.RequestId = RequestObj.SendExecStatusReq.RequestId;
      ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.ResultCode = 0;
      ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.EntryIdSofr.ObjectId = string(vc_EntryIdSofr); //08.10.2020 раньше было Payment.PaymentId, но в некоторых ситуациях Payment может быть пустой    структурой + смысла к нему обращаться не имеет, т.к. он пока нигде не меняется
      ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.EntryIdBis.ObjectId = string(vc_EntryIdBis);
      /* kva: ErrorList в случае успеха не заполняем */
      RequestObj = Null;
      Payment = Null;
   
      return ResponseObj;
   end;
onError(err)
   ResponseObj = NULL;
   ResponseObj = c_out_obj;
   // DEF-22967 RequestId - обязательный параметр
   if (ValType(RequestObj) == V_GENOBJ)
       ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.RequestId = RequestObj.SendExecStatusReq.RequestId;
   else 
       ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.RequestId.ObjectId = 0;
   end;
   ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.ResultCode = UNIVERSAL_ERROR_CODE;
   ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.EntryIdSofr.ObjectId = string(vc_EntryIdSofr);
   ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.EntryIdBis.ObjectId = string(vc_EntryIdBis);
   ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.ErrorList.value(0) = c_Error_ErrLst;
   ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.ErrorList.value(0).ErrorCode = c_usr_err_obj.obtain_err_code(err);
   ResponseObj.SendExecStatusRespMsg.SendExecStatusResp.ApproveEntryResult.ErrorList.value(0).ErrorDesc = c_usr_err_obj.obtain_err_msg(err);
   return ResponseObj;

end;