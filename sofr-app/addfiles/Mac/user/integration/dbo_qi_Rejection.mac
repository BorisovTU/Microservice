/**
 @file dbo_qi_Rejection.mac
 @brief Функционал отправки уведомления об отказе признания лица квалифицированным инвестором.
 
 Файл содержит UI функционал запроса параметров уведомления и вызова функционала формирования уведомления 
 об отказе признания лица квалифицированным инвестором и его отправки по e-mail.
 
  # tag
 - functional_block:Отчетность_Клиент
 - code_type:GUI
 - Квалификация
 - КИ
 - Квалифицированный инвестор
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |01.11.2023 |Топорков Д.В.|BIQ-16875 BOSS-194 BOSS-1286                              | Создание
 */
 
import rsd, RsbFormsInter, globals;
import "oralib.mac";
import "dbo_qi_RejectionNotification.mac";

/// Константы типа поля
private const FT_CHR    = 12; ///< Символ
private const FT_DATE   =  9; ///< Дата
private const FT_STRING =  7; ///< Строковое

/// Константы клавиш
private const K_ENTER =  13, ///< Клавиша Enter
              K_F2    = 316, ///< Клавиша F2
              K_F3    = 317, ///< Клавиша F3
              K_ESC   =  27; ///< Клавиша Esc

/**
@brief Класс реализация панели корректировки параметров уведомления об отказе
*/
private class (TRsbPanel) CRejectionPanel(rejectionParams: @CQIRejectionParams)
  private var f;
  private var params;
  
  /**
  @brief Процедура создания элемента панели типа TRsbLabel
  @param[in] x Координата x
  @param[in] y Координата y
  @param[in] caption Выводимый текст
  */
  private macro NewLabel(x: Integer, y: Integer, caption: String)
    AddLabel(TRsbLabel(x, y, caption));
  end;

  /**
  @brief Процедура создания элемента панели типа TRsbEditField
  @param[in] x Координата x
  @param[in] y Координата y
  @param[in] w Ширина элемента
  @param[in] h Высота элемента
  @param[in] controlName Имя 
  @param[in] type Тип обрабатываемых данных
  @param[in] length Максимальная длина буфера данных
  @param[in] editable Признак доступности для редактирования
  @param[in] value Значение инициализации
  @return Объект класса TRsbEditField
  */
  private macro NewEditField(x: Integer, y: Integer, w: Integer, h: Integer, controlName: String, type: Integer, length: Integer, editable: Bool, value: Variant)
    var edit: Object;
    
    edit = TRsbEditField(type);
    edit.name = controlName;
    edit.dataType = type;
    edit.SetPosition(x, y);
    edit.SetSize(w, h);
    edit.TextLength = length;
    edit.Value = value;
    edit.Editable = editable;
    edit.Focusable = true;
    edit.Enabled = true;
    AddControl(edit);
    
    return edit;
  end;
  
  /**
  @brief Процедура создания элемента панели типа TRsbComboBox
  @param[in] x Координата x
  @param[in] y Координата y
  @param[in] w Ширина элемента
  @param[in] h Высота элемента
  @param[in] controlName Имя 
  @param[in] type Тип обрабатываемых данных
  @param[in] length Максимальная длина буфера данных
  @param[in] editable Признак доступности для редактирования
  @param[in] value Значение инициализации
  @return Объект класса TRsbComboBox
  */
  private macro NewComboBox(x: Integer, y: Integer, w: Integer, h: Integer, controlName: String, type: Integer, length: Integer, editable: Bool, value: Variant)
    var combo: Object;
    
    combo = TRsbComboBox();
    combo.name = controlName;
    combo.dataType = type;
    combo.SetPosition(x, y);
    combo.SetSize(w, h);
    combo.TextLength = length;
    combo.value = value;
    combo.Editable = true;
    combo.Focusable = true;
    combo.Enabled = true;
    AddControl(combo);
    
    return combo;
  end;
  
  /**
  @brief Обработчик поля "Подписант"
  @param[in] event Событие формы
  */
  macro SignerPressed(event: Object)
    if (event.keyCode == K_F3)
      event.keyCode= -K_F3;
    end;
  end;
  
  /**
  @brief Обработчик формы
  @param[in] event Событие формы
  */
  macro EventProc(event: Object)
    /// Клавиша Esc - Отмена ввода
    if (event.keyCode == K_ESC)
      this.Close( event.keyCode );
    elif (event.keyCode == K_F2)
      /// Клавиша F2 - Выполнить действие формы
      if (params.signer.oper == 0)
        MsgBox("Не выбран подписант!");
      else
        params.notificationDate = GetControl("DATE").value;
        params.reason = GetControl("REASON").value;
        this.Close( event.keyCode );
      end;
    elif (event.keyCode == -K_F3)
      /// Клавиша F3 - Вызвать окно выбора для поля 
      var signer = GetSignerUI();

      if (signer != null)
        params.signer = signer;
        this.focusedControl().value = signer.name;
      end;

    end;
  end;
  
  //--- конструктор ---
  InitTRsbPanel();
  params = rejectionParams;

  params.signer = GetSigner({oper}); // Если текущий подписант в группе подписантов, то установится он
  if (params.signer.oper == 0)       // Иначе попытка прочитать подписанта по умолчанию из реестра
    params.signer = GetQIDefaultSigner();
  end;

  SetCaption("Параметры уведомления об отказе в признании лица КИ");
  SetStatus("~Esc~ Выход   ~F2~ Применить   ~F3~ Подписант");
  SetSize(70, 10);
  SetPosition(45, 10);

  NewLabel(2, 2, "Дата отказа:");            NewEditField(14, 2, 10, 1, "DATE",   FT_DATE,   10,   true,  params.notificationDate);
  NewLabel(2, 4, "Причина отказа:");         NewEditField(14, 4, 54, 3, "REASON", FT_STRING, 1000, true,  params.reason);
  NewLabel(2, 8, "Подписант:");              NewComboBox (14, 8, 25, 1, "SIGNER", FT_STRING, 25,   true,  params.signer.name).AddEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "SignerPressed"));
 
  AddEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "EventProc"));
end;  //CRejectionPanel

/**
@brief Процедура отображения UI и вызова функции формирования Уведомления об отказе в статусе квал.инвестора
@param[in] dlContrID ID Договора брокерского обслуживания (таблица dDlContr_dbt)

В случае получения ошибки формирования Уведомления, выводит эту ошибку на экран.
*/
macro QIRejectionNotificationUI(dlContrID: Integer)
  var rejectionParams = CQIRejectionParams();
  var err: String;
  
  /// Проверка пользователя запускающего процедуру
  if (not CheckQIUserRights())
    MsgBox("Для данной роли функция недоступна.");
    exit(1);
  end;
  
  if (CRejectionPanel(@rejectionParams).Run() != K_F2)
    exit(1);  
  end;
  
  rejectionParams.dlContrID = dlContrID;
  rejectionParams.client = GetClient(dlContrID);
  
  InitProgress(1, "Формирование и отправка уведомления", "Формирование и отправка уведомления");
  err = QIRejectionNotification(rejectionParams);
  RemProgress();
  if (StrLen(err) > 0)
    MsgBox(err);
  end;
end;