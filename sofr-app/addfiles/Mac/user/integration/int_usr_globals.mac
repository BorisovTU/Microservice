/*----------------------------------------------------*/
/* Общие элементы, используемые в интерфейсных формах */
/*                                                    */
/* Автор: Карпов В.                                   */
/*----------------------------------------------------*/

import rsd;
//import CommonInter;
import osfile; /* GetCWD */
import "usr_key_const.mac";


/*----------------------*/
/* Глобальные константы */
/*----------------------*/
const FT_STRING = 7;
const FT_INT = 1;
const FT_DATE = 9;
const FT_TIME = 10;

const CV_CLOSE_FORM_OK = 0;

const CV_MSG_FILE_PREFIX = "msg_";
const CV_MSG_FILE_POSTFIX = ".txt";
const CV_MSG_FILE_PATH = "..\\TxtFile\\";



/*---------------------------------------*/
/* Метод добавления колонки к скроллингу */
/*---------------------------------------*/
macro m_add_column(p_ar, p_ind, p_fld, p_head, p_width, p_edit, p_dec_point)
   p_ar.value(p_ind * 6 + 0) = p_fld;       // fieldName
   p_ar.value(p_ind * 6 + 1) = p_head;      // header
   p_ar.value(p_ind * 6 + 2) = p_width;     // width
   p_ar.value(p_ind * 6 + 3) = p_edit;      // fldType (2 = FBT)
   p_ar.value(p_ind * 6 + 4) = p_dec_point; // decPoint
   p_ar.value(p_ind * 6 + 5) = 0;           // reserv
end; /* macro m_add_column() */


/*----------------------------------*/
/* Сборка имени файла для сообщения */
/*----------------------------------*/
macro m_make_file_name()
   var v_ret;
   var v_date_d;
   var v_date_m;
   var v_date_y;
   var v_time_h;
   var v_time_m;
   var v_time_s;

   DateSplit(date(), v_date_d, v_date_m, v_date_y);
   TimeSplit(time(), v_time_h, v_time_m, v_time_s);

   v_ret = string(CV_MSG_FILE_PREFIX, v_date_y, v_date_m, v_date_d, v_time_h, v_time_m, v_time_s, CV_MSG_FILE_POSTFIX);

   return v_ret;
end; /* macro m_make_file_name() */


/*-----------------------------------------------------------------------------------------*/
/* Получение полного пути к файлу по его относительному пути (попытка заменить FullPath()) */
/*-----------------------------------------------------------------------------------------*/
macro m_rel_path_to_abs(p_rel_path, p_abs_path)
   private const CV_CUR_DIR = ".";
   private const CV_UP_DIR = "..";

   var v_ret = true;
   var v_dirn, v_filen, v_extn, v_slash;
   var v_obj_dir_path;
   var v_abs_path;

   /* Что тут происходит спрашивать в ОПР - функция PathToAbsolute() */
   p_rel_path = trim (p_rel_path);
   v_dirn = SplitFile (p_rel_path, v_filen, v_extn);
   p_rel_path = MergeFile (v_dirn, v_filen, v_extn);

   if (SubStr (v_dirn, 2, 1) == ":")
      return p_rel_path;
   end;

   v_slash = SplitFile ("/");
   if (SubStr (v_dirn, 1, 1) == v_slash)
      return p_rel_path;
   end;
   /**/

   v_obj_dir_path = GetCWD;

   if(index(p_rel_path, CV_UP_DIR) == 1)
      v_abs_path = string(SubStr(v_obj_dir_path, 1, (StrLen(v_obj_dir_path) - 4)), SubStr(p_rel_path, 3));
   elif(index(p_rel_path, CV_CUR_DIR == 1))
      v_abs_path = string(v_obj_dir_path, SubStr(p_rel_path, 2));
   else
      /* Если вдруг оказались здесь, то склеиваем как попало */
      v_abs_path = string(v_obj_dir_path, v_slash, p_rel_path);
   end;

   SetParm(1, v_abs_path);

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end; /* macro m_rel_path_to_abs() */


/*----------------------------*/
/* Выгрузить сообщение в файл */
/*----------------------------*/
macro m_make_file_with_msg(p_msg)
   file v_new_file("") txt write;
   var v_ret = "";
   var v_stat;
   var v_new_file_name;
   var v_rel_path;
   var v_full_path;

   v_new_file_name = m_make_file_name();
   v_rel_path = string(CV_MSG_FILE_PATH, v_new_file_name);

   v_stat = Open(v_new_file, v_rel_path);
   if(v_stat)
      v_new_file.str = p_msg;
      insert(v_new_file);

      close(v_new_file);

//      v_stat = FullPath(v_rel_path, v_full_path);
      v_stat = m_rel_path_to_abs(v_rel_path, v_full_path);

      ViewFile(v_new_file);
/*
      if(v_stat)
         run(GetEnv("COMSPEC"), string("notepad ", v_full_path));
      end;
*/
      if(v_stat)
         v_ret = v_full_path;
      end;
   end;

   return v_ret;

onError(err)
   return v_ret;
end; /* macro m_make_file_with_msg() */


/*-----------------------------------------------------*/
/* Выгрузить сообщение в файл и отобразить уведомление */
/*-----------------------------------------------------*/
macro m_make_file_and_notice(p_msg_str)
   var v_file_path = "";

   v_file_path = m_make_file_with_msg(p_msg_str);
   if(v_file_path != "")
      msgbox(string("Создан файл: ", v_file_path));
   else
      msgbox("Ошибка: файл не создан");
   end;
end; /* m_make_file_and_notice() */


/*-----------------------------------*/
/* Выгрузка сообщения из dxr_log_dbt */
/*-----------------------------------*/
macro m_make_xr_log_msg(p_reqid, p_kind)
   var v_sql, v_cmd;
   var v_rd_stat = true;
   var v_clob_offset = 1; /* Начальное смещение в clob'е - начинаем с первого символа */
   var v_msg_str = "";    /* Сообщение */

   v_sql =         "DECLARE ";
   v_sql = v_sql + "    v_msg_loc    CLOB; ";
   v_sql = v_sql + "    v_msg_id     dxr_log_dbt.t_reqid%TYPE := :p_msg_id; ";
   v_sql = v_sql + "    v_kind       dxr_log_dbt.t_kind%TYPE := :p_kind; ";
   v_sql = v_sql + "    v_msg_offset INTEGER := :p_msg_offset; ";
   v_sql = v_sql + "    v_msg_read   BINARY_INTEGER := 1000; ";
   v_sql = v_sql + "    v_msg_part   VARCHAR2 (2000 Char); ";
   v_sql = v_sql + "    v_ret_length BINARY_INTEGER; ";
   v_sql = v_sql + "BEGIN ";
   v_sql = v_sql + "    BEGIN ";
   v_sql = v_sql + "        SELECT t_message ";
   v_sql = v_sql + "          INTO v_msg_loc ";
   v_sql = v_sql + "          FROM dxr_log_dbt ";
   v_sql = v_sql + "         WHERE t_reqid = v_msg_id ";
   v_sql = v_sql + "           AND t_kind = v_kind ";
   v_sql = v_sql + "           AND ROWNUM = 1 "; /* Для большей универсальности (на текущем проекте не актуально) */
   v_sql = v_sql + "      ORDER BY t_id DESC; ";

   v_sql = v_sql + "        DBMS_LOB.read (v_msg_loc, ";
   v_sql = v_sql + "                       v_msg_read, ";
   v_sql = v_sql + "                       v_msg_offset, ";
   v_sql = v_sql + "                       v_msg_part); ";
   v_sql = v_sql + "        v_ret_length := v_msg_read; ";

   v_sql = v_sql + "    EXCEPTION ";
   v_sql = v_sql + "        WHEN NO_DATA_FOUND ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            v_ret_length := 0; ";
   v_sql = v_sql + "        WHEN OTHERS ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            v_ret_length := 0; ";
   v_sql = v_sql + "    END; ";

   v_sql = v_sql + "    :p_ret_length := v_ret_length; ";
   v_sql = v_sql + "    :p_ret_msg_part := v_msg_part; ";
   v_sql = v_sql + "END; ";

   while(v_rd_stat)
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_msg_id", RSDBP_IN, p_reqid);
      v_cmd.addParam("p_kind", RSDBP_IN, p_kind);
      v_cmd.addParam("p_msg_offset", RSDBP_IN, v_clob_offset);
      v_cmd.addParam("p_ret_length", RSDBP_OUT, V_INTEGER);
      v_cmd.addParam("p_ret_msg_part", RSDBP_OUT, V_STRING, 2000);
      v_cmd.execute();

      if(v_cmd.value("p_ret_length") > 0)
         v_msg_str = string(v_msg_str, v_cmd.value("p_ret_msg_part"));
         v_clob_offset = v_clob_offset + v_cmd.value("p_ret_length");

         v_rd_stat = true;
         v_cmd.close(); v_cmd = null;
      else
         v_rd_stat = false;
         v_cmd.close(); v_cmd = null;
      end;
   end;

   m_make_file_and_notice(v_msg_str);

onError(err)
   msgbox("Ошибка: файл не создан");
end; /* macro m_make_xr_log_msg() */


/*--------------------------------------*/
/* Выгрузка сообщения из dqueue_log_dbt */
/*--------------------------------------*/
macro m_make_queue_log_msg(p_log_id)
   var v_sql, v_cmd;
   var v_rd_stat = true;
   var v_clob_offset = 1; /* Начальное смещение в clob'е - начинаем с первого символа */
   var v_msg_str = "";    /* Сообщение */

   v_sql =         "DECLARE ";
   v_sql = v_sql + "    v_msg_loc    CLOB; ";
   v_sql = v_sql + "    v_msg_id     dqueue_log_dbt.t_id%TYPE := :p_msg_id; ";
   v_sql = v_sql + "    v_msg_offset INTEGER := :p_msg_offset; ";
   v_sql = v_sql + "    v_msg_read   BINARY_INTEGER := 1000; ";
   v_sql = v_sql + "    v_msg_part   VARCHAR2 (2000 Char); ";
   v_sql = v_sql + "    v_ret_length BINARY_INTEGER; ";
   v_sql = v_sql + "BEGIN ";
   v_sql = v_sql + "    BEGIN ";
   v_sql = v_sql + "        SELECT t_message ";
   v_sql = v_sql + "          INTO v_msg_loc ";
   v_sql = v_sql + "          FROM dqueue_log_dbt ";
   v_sql = v_sql + "         WHERE t_id = v_msg_id; ";

   v_sql = v_sql + "        DBMS_LOB.read (v_msg_loc, ";
   v_sql = v_sql + "                       v_msg_read, ";
   v_sql = v_sql + "                       v_msg_offset, ";
   v_sql = v_sql + "                       v_msg_part); ";
   v_sql = v_sql + "        v_ret_length := v_msg_read; ";

   v_sql = v_sql + "    EXCEPTION ";
   v_sql = v_sql + "        WHEN NO_DATA_FOUND ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            v_ret_length := 0; ";
   v_sql = v_sql + "        WHEN OTHERS ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            v_ret_length := 0; ";
   v_sql = v_sql + "    END; ";

   v_sql = v_sql + "    :p_ret_length := v_ret_length; ";
   v_sql = v_sql + "    :p_ret_msg_part := v_msg_part; ";
   v_sql = v_sql + "END; ";

   while(v_rd_stat)
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_msg_id", RSDBP_IN, p_log_id);
      v_cmd.addParam("p_msg_offset", RSDBP_IN, v_clob_offset);
      v_cmd.addParam("p_ret_length", RSDBP_OUT, V_INTEGER);
      v_cmd.addParam("p_ret_msg_part", RSDBP_OUT, V_STRING, 2000);
      v_cmd.execute();

      if(v_cmd.value("p_ret_length") > 0)
         v_msg_str = string(v_msg_str, v_cmd.value("p_ret_msg_part"));
         v_clob_offset = v_clob_offset + v_cmd.value("p_ret_length");

         v_rd_stat = true;
         v_cmd.close(); v_cmd = null;
      else
         v_rd_stat = false;
         v_cmd.close(); v_cmd = null;
      end;
   end;

   m_make_file_and_notice(v_msg_str);

onError(err)
   msgbox("Ошибка: файл не создан");
end; /* macro m_make_queue_log_msg() */


/*------------------------------------------*/
/* Выгрузка сообщения из usr_exchng_log_dbt */
/*------------------------------------------*/
macro m_make_usr_log_msg(p_log_id, p_kind)
   var v_sql, v_cmd;
   var v_rd_stat = true;
   var v_clob_offset = 1; /* Начальное смещение в clob'е - начинаем с первого символа */
   var v_msg_str = "";    /* Сообщение */

   v_sql =         "DECLARE ";
   v_sql = v_sql + "    v_msg_loc    CLOB; ";
   v_sql = v_sql + "    v_msg_id     usr_exchng_log_dbt.t_id%TYPE := :p_msg_id; ";
   v_sql = v_sql + "    v_msg_offset INTEGER := :p_msg_offset; ";
   v_sql = v_sql + "    v_msg_read   BINARY_INTEGER := 1000; ";
   v_sql = v_sql + "    v_msg_part   VARCHAR2 (2000 Char); ";
   v_sql = v_sql + "    v_ret_length BINARY_INTEGER; ";
   v_sql = v_sql + "BEGIN ";
   v_sql = v_sql + "    BEGIN ";
   if(p_kind == 1) /* Запрос */
      v_sql = v_sql + "     SELECT t_req_message ";
   elif(p_kind == 3) /* Ответ */
      v_sql = v_sql + "     SELECT t_resp_message ";
   else
      RunError("Непредусмотренный тип сообщения", -10101);
   end;
   v_sql = v_sql + "          INTO v_msg_loc ";
   v_sql = v_sql + "          FROM usr_exchng_log_dbt ";
   v_sql = v_sql + "         WHERE t_id = v_msg_id; ";

   v_sql = v_sql + "        DBMS_LOB.read (v_msg_loc, ";
   v_sql = v_sql + "                       v_msg_read, ";
   v_sql = v_sql + "                       v_msg_offset, ";
   v_sql = v_sql + "                       v_msg_part); ";
   v_sql = v_sql + "        v_ret_length := v_msg_read; ";

   v_sql = v_sql + "    EXCEPTION ";
   v_sql = v_sql + "        WHEN NO_DATA_FOUND ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            v_ret_length := 0; ";
   v_sql = v_sql + "        WHEN OTHERS ";
   v_sql = v_sql + "        THEN ";
   v_sql = v_sql + "            v_ret_length := 0; ";
   v_sql = v_sql + "    END; ";

   v_sql = v_sql + "    :p_ret_length := v_ret_length; ";
   v_sql = v_sql + "    :p_ret_msg_part := v_msg_part; ";
   v_sql = v_sql + "END; ";

   while(v_rd_stat)
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_msg_id", RSDBP_IN, p_log_id);
      v_cmd.addParam("p_msg_offset", RSDBP_IN, v_clob_offset);
      v_cmd.addParam("p_ret_length", RSDBP_OUT, V_INTEGER);
      v_cmd.addParam("p_ret_msg_part", RSDBP_OUT, V_STRING, 2000);
      v_cmd.execute();

      if(v_cmd.value("p_ret_length") > 0)
         v_msg_str = string(v_msg_str, v_cmd.value("p_ret_msg_part"));
         v_clob_offset = v_clob_offset + v_cmd.value("p_ret_length");

         v_rd_stat = true;
         v_cmd.close(); v_cmd = null;
      else
         v_rd_stat = false;
         v_cmd.close(); v_cmd = null;
      end;
   end;

   m_make_file_and_notice(v_msg_str);

onError(err)
   msgbox("Ошибка: файл не создан");
end; /* macro m_make_usr_log_msg() */


/*---------------------------------------*/
/* Класс для работы с цепочкой сообщений */
/*---------------------------------------*/
class c_log_chain_msg(p_log_id, p_log_module)
   private var gv_service_code = 0; /* Сервисный код */
   private var gv_service_msg = ""; /* Текст сервисного сообщения */

   private const CV_QNAME_IN = "ESB_TO_SOFR";
   private const CV_QNAME_OUT = "SOFR_TO_ESB";

   private var gv_req_mod_sep = TArray;
   gv_req_mod_sep.value(gv_req_mod_sep.size) = "SendRSHBBagRequestMsg";
   private var gv_resp_mod_sep = TArray;
   gv_resp_mod_sep.value(gv_resp_mod_sep.size) = "SendRSHBBagResponseMsg";

   private var gv_log_id_req = 0;
   private var gv_log_id_resp = 0;
   private var gv_log_module_req = "";
   private var gv_log_module_resp = "";
   private var gv_is_separate_msg = false; /* Признак разделенных записей запроса и ответа в журнале */

   private var gv_log_id_req_env = 0;
   private var gv_log_id_resp_env = 0;


   /* Сброс сервисных параметров */
   private macro m_reset_service_info()
      gv_service_code = 0;
      gv_service_msg = "";
   end; /* macro m_reset_service_info() */


   /* Метод получения значения сервисного кода */
   macro m_get_service_code()
      return gv_service_code;
   end; /* macro m_get_service_code() */


   /* Метод получения текста сервисного сообщения */
   macro m_get_service_msg()
      return gv_service_msg;
   end; /* macro get_service_msg() */


   /* Метод получения признака разделенности сообщений запроса и ответа */
   macro m_get_sep_flag()
      return gv_is_separate_msg;
   end; /* macro m_get_sep_flag() */


   /* Получение идентификаторов по id ответа */
   private macro m_get_id_by_resp(p_resp_msg_id, p_depth_lvl)
      var v_sql, v_cmd;

      m_reset_service_info();
      if(ValType(p_depth_lvl) == V_UNDEF)
         p_depth_lvl = 3;
      end;

      v_sql =         "DECLARE ";
      v_sql = v_sql + "    v_log_id           usr_exchng_log_dbt.t_id%TYPE := :p_log_id; ";
      v_sql = v_sql + "    v_qname_in         dqueue_log_dbt.t_qname%TYPE := :p_qname_in; ";

      v_sql = v_sql + "    v_jmsmessageid     dqueue_log_dbt.t_jmsmessageid%TYPE; ";
      v_sql = v_sql + "    v_jmsmessageguid   dqueue_log_dbt.t_jmsmessageguid%TYPE; ";
      v_sql = v_sql + "    v_jmscorrelationid dqueue_log_dbt.t_jmscorrelationid%TYPE; ";
      v_sql = v_sql + "    v_qlog_id_resp     dqueue_log_dbt.t_id%TYPE; ";

      if(p_depth_lvl > 1)
         v_sql = v_sql + " v_qlog_id_req      dqueue_log_dbt.t_id%TYPE; ";
         v_sql = v_sql + " v_log_link_id      dqueue_log_dbt.t_jmscorrelationid%TYPE; ";
      end;

      if(p_depth_lvl > 2)
         v_sql = v_sql + " v_log_id_src       usr_exchng_log_dbt.t_id%TYPE; ";
         v_sql = v_sql + " v_log_module_src   usr_exchng_log_dbt.t_module%TYPE; ";
      end;

      v_sql = v_sql + "    v_ret              NUMBER(10) := 0; ";
      v_sql = v_sql + "    v_ret_txt          usr_exchng_log_dbt.t_error_msg%TYPE := 'OK'; ";
      v_sql = v_sql + "    v_aux_buf          NUMBER(10); ";
      v_sql = v_sql + "    v_log_id_ref       utableprocessevent_dbt.t_recid%TYPE; ";

      v_sql = v_sql + "BEGIN ";
      v_sql = v_sql + "    BEGIN ";
      v_sql = v_sql + "        v_ret := 1; ";
      v_sql = v_sql + "        SELECT t_transparent_id INTO v_jmsmessageid ";
      v_sql = v_sql + "          FROM usr_exchng_log_dbt ";
      v_sql = v_sql + "         WHERE t_id = v_log_id; ";

      v_sql = v_sql + "        v_ret := 2; ";
      v_sql = v_sql + "        SELECT t_id, t_jmscorrelationid, t_jmsmessageguid ";
      v_sql = v_sql + "          INTO v_qlog_id_resp, v_jmscorrelationid, v_jmsmessageguid ";
      v_sql = v_sql + "          FROM dqueue_log_dbt ";
      v_sql = v_sql + "         WHERE t_jmsmessageid = v_jmsmessageid ";
      v_sql = v_sql + "           AND t_qname = v_qname_in; ";

/*
      v_sql = v_sql + "        v_ret := 3; ";
      v_sql = v_sql + "        SELECT t_id, t_jmscorrelationid ";
      v_sql = v_sql + "          INTO v_qlog_id_resp, v_jmscorrelationid ";
      v_sql = v_sql + "          FROM dqueue_log_dbt ";
      v_sql = v_sql + "         WHERE t_jmsmessageguid = v_jmsmessageguid";
      v_sql = v_sql + "           AND t_jmscorrelationid <> chr(1) ";
      v_sql = v_sql + "           AND t_qname = v_qname_in; ";
*/
      if(p_depth_lvl > 1)
         v_sql = v_sql + "     v_ret := 4; ";
         v_sql = v_sql + "     SELECT t_id, t_jmscorrelationid ";
         v_sql = v_sql + "       INTO v_qlog_id_req, v_log_link_id ";
         v_sql = v_sql + "       FROM dqueue_log_dbt ";
         v_sql = v_sql + "      WHERE t_jmsmessageid = v_jmscorrelationid; ";
      end;

      if(p_depth_lvl > 2)
         v_sql = v_sql + "     v_aux_buf := INSTR(v_log_link_id, '-'); ";
         v_sql = v_sql + "     IF v_aux_buf = 0 ";
         v_sql = v_sql + "     THEN ";
         v_sql = v_sql + "         v_log_id_ref := TO_NUMBER(v_log_link_id); ";
         v_sql = v_sql + "     ELSE ";
         v_sql = v_sql + "         v_log_id_ref := TO_NUMBER(SUBSTR(v_log_link_id, (v_aux_buf + 1))); ";
         v_sql = v_sql + "     END IF; ";

         v_sql = v_sql + "     v_ret := 5; ";
         v_sql = v_sql + "     SELECT t_id, t_module ";
         v_sql = v_sql + "       INTO v_log_id_src, v_log_module_src ";
         v_sql = v_sql + "       FROM usr_exchng_log_dbt ";
         v_sql = v_sql + "      WHERE t_id = v_log_id_ref; ";
      end;
      v_sql = v_sql + "        v_ret := 0; ";

      v_sql = v_sql + "    EXCEPTION ";
      v_sql = v_sql + "        WHEN NO_DATA_FOUND ";
      v_sql = v_sql + "        THEN ";
      v_sql = v_sql + "            v_ret := v_ret; ";
      v_sql = v_sql + "            v_ret_txt := 'NO_DATA_FOUND'; ";
      v_sql = v_sql + "        WHEN OTHERS ";
      v_sql = v_sql + "        THEN ";
      v_sql = v_sql + "            v_ret := SQLCODE; ";
      v_sql = v_sql + "            v_ret_txt := SUBSTR(SQLERRM, 1, 1500); ";
      v_sql = v_sql + "    END; ";

      v_sql = v_sql + "    :p_qlog_id_resp := v_qlog_id_resp; ";

      if(p_depth_lvl > 1)
         v_sql = v_sql + " :p_qlog_id_req := v_qlog_id_req; ";
      end;

      if(p_depth_lvl > 2)
         v_sql = v_sql + " :p_log_id_src := v_log_id_src; ";
         v_sql = v_sql + " :p_log_module_src := v_log_module_src; ";
      end;

      v_sql = v_sql + "    :p_ret := v_ret; ";
      v_sql = v_sql + "    :p_ret_txt := v_ret_txt; ";
      v_sql = v_sql + "END; ";

      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_log_id", RSDBP_IN, p_resp_msg_id);
      v_cmd.addParam("p_qname_in", RSDBP_IN, CV_QNAME_IN);
      v_cmd.addParam("p_qlog_id_resp", RSDBP_OUT, V_INTEGER);
      if(p_depth_lvl > 1)
         v_cmd.addParam("p_qlog_id_req", RSDBP_OUT, V_INTEGER);
      end;
      if(p_depth_lvl > 2)
         v_cmd.addParam("p_log_id_src", RSDBP_OUT, V_INTEGER);
         v_cmd.addParam("p_log_module_src", RSDBP_OUT, V_STRING, 256);
      end;
      v_cmd.addParam("p_ret", RSDBP_OUT, V_INTEGER);
      v_cmd.addParam("p_ret_txt", RSDBP_OUT, V_STRING, 2000);
      v_cmd.execute();

      gv_service_code = v_cmd.value("p_ret");
      if(gv_service_code == 0)
         gv_log_id_resp_env = v_cmd.value("p_qlog_id_resp");
         if(p_depth_lvl > 1)
            gv_log_id_req_env = v_cmd.value("p_qlog_id_req");
         end;
         if(p_depth_lvl > 2)
            gv_log_id_req = v_cmd.value("p_log_id_src");
            gv_log_module_req = v_cmd.value("p_log_module_src");
         end;
      else
         gv_service_msg = v_cmd.value("p_ret_txt");
      end;

      v_cmd.close(); v_cmd = null; v_sql = null;

   onError(err)
      gv_service_code = 1;
      gv_service_msg = "Проблема при получении идентификаторов";
   end; /* macro m_get_id_by_resp() */


   /* Получение идентификаторов по id запроса */
   private macro m_get_id_by_req(p_req_msg_id, p_depth_lvl)
      var v_sql, v_cmd;

      m_reset_service_info();
      if(ValType(p_depth_lvl) == V_UNDEF)
         p_depth_lvl = 3;
      end;

      v_sql =         "DECLARE ";
      v_sql = v_sql + "    v_log_id            usr_exchng_log_dbt.t_id%TYPE := :p_log_id; ";
      v_sql = v_sql + "    v_qname_in          dqueue_log_dbt.t_qname%TYPE := :p_qname_in; ";
      v_sql = v_sql + "    v_qname_out         dqueue_log_dbt.t_qname%TYPE := :p_qname_out; ";

      v_sql = v_sql + "    v_qlog_id_req       dqueue_log_dbt.t_id%TYPE; ";
      v_sql = v_sql + "    v_jms_mes_id_req    dqueue_log_dbt.t_jmsmessageid%TYPE; ";

      if(p_depth_lvl > 1)
         v_sql = v_sql + " v_qlog_id_resp      dqueue_log_dbt.t_id%TYPE; ";
         v_sql = v_sql + " v_jms_mes_guid_resp dqueue_log_dbt.t_jmsmessageguid%TYPE; ";
      end;

      if(p_depth_lvl > 2)
         v_sql = v_sql + " v_jms_mes_id_resp   dqueue_log_dbt.t_jmsmessageid%TYPE; ";
         v_sql = v_sql + " v_log_id_resp       usr_exchng_log_dbt.t_id%TYPE; ";
         v_sql = v_sql + " v_log_module_resp   usr_exchng_log_dbt.t_module%TYPE; ";
      end;

      v_sql = v_sql + "    v_ret               NUMBER(10) := 0; ";
      v_sql = v_sql + "    v_ret_txt           usr_exchng_log_dbt.t_error_msg%TYPE := 'OK'; ";

      v_sql = v_sql + "BEGIN ";
      v_sql = v_sql + "    BEGIN ";
      v_sql = v_sql + "        v_ret := 1; ";
      v_sql = v_sql + "        SELECT t_jmsmessageid, t_id ";
      v_sql = v_sql + "          INTO v_jms_mes_id_req, v_qlog_id_req ";
      v_sql = v_sql + "          FROM dqueue_log_dbt ";
      v_sql = v_sql + "         WHERE t_jmscorrelationid = TO_CHAR(v_log_id) ";
      v_sql = v_sql + "           AND t_qname = v_qname_out; ";

      if(p_depth_lvl > 1)
         v_sql = v_sql + "     v_ret := 2; ";
         v_sql = v_sql + "     SELECT t_jmsmessageguid, t_id ";
         v_sql = v_sql + "       INTO v_jms_mes_guid_resp, v_qlog_id_resp ";
         v_sql = v_sql + "       FROM dqueue_log_dbt ";
         v_sql = v_sql + "      WHERE t_jmscorrelationid = v_jms_mes_id_req ";
         v_sql = v_sql + "        AND t_qname = v_qname_in; ";
      end;

      if(p_depth_lvl > 2)
         v_sql = v_sql + "     v_ret := 3; ";
         v_sql = v_sql + "     SELECT t_jmsmessageid ";
         v_sql = v_sql + "       INTO v_jms_mes_id_resp ";
         v_sql = v_sql + "       FROM dqueue_log_dbt ";
         v_sql = v_sql + "      WHERE t_jmsmessageguid = v_jms_mes_guid_resp ";
         v_sql = v_sql + "        AND t_xr_reqid IS NOT NULL; ";

         v_sql = v_sql + "     v_ret := 4; ";
         v_sql = v_sql + "     SELECT t_id, t_module ";
         v_sql = v_sql + "       INTO v_log_id_resp, v_log_module_resp ";
         v_sql = v_sql + "       FROM usr_exchng_log_dbt ";
         v_sql = v_sql + "      WHERE t_transparent_id = v_jms_mes_id_resp; ";
      end;
      v_sql = v_sql + "        v_ret := 0; ";

      v_sql = v_sql + "    EXCEPTION ";
      v_sql = v_sql + "        WHEN NO_DATA_FOUND ";
      v_sql = v_sql + "        THEN ";
      v_sql = v_sql + "            v_ret := v_ret; ";
      v_sql = v_sql + "            v_ret_txt := 'NO_DATA_FOUND'; ";
      v_sql = v_sql + "        WHEN OTHERS ";
      v_sql = v_sql + "        THEN ";
      v_sql = v_sql + "            v_ret := SQLCODE; ";
      v_sql = v_sql + "            v_ret_txt := SUBSTR(SQLERRM, 1, 1500); ";
      v_sql = v_sql + "    END; ";

      v_sql = v_sql + "    :p_qlog_id_req := v_qlog_id_req; ";

      if(p_depth_lvl > 1)
         v_sql = v_sql + " :p_qlog_id_resp := v_qlog_id_resp; ";
      end;

      if(p_depth_lvl > 2)
         v_sql = v_sql + " :p_log_id_resp := v_log_id_resp; ";
         v_sql = v_sql + " :p_log_module_resp := v_log_module_resp; ";
      end;

      v_sql = v_sql + "    :p_ret := v_ret; ";
      v_sql = v_sql + "    :p_ret_txt := v_ret_txt; ";
      v_sql = v_sql + "END; ";

      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_log_id", RSDBP_IN, p_req_msg_id);
      v_cmd.addParam("p_qname_in", RSDBP_IN, CV_QNAME_IN);
      v_cmd.addParam("p_qname_out", RSDBP_IN, CV_QNAME_OUT);
      v_cmd.addParam("p_qlog_id_req", RSDBP_OUT, V_INTEGER);
      if(p_depth_lvl > 1)
         v_cmd.addParam("p_qlog_id_resp", RSDBP_OUT, V_INTEGER);
      end;
      if(p_depth_lvl > 2)
         v_cmd.addParam("p_log_id_resp", RSDBP_OUT, V_INTEGER);
         v_cmd.addParam("p_log_module_resp", RSDBP_OUT, V_STRING, 256);
      end;
      v_cmd.addParam("p_ret", RSDBP_OUT, V_INTEGER);
      v_cmd.addParam("p_ret_txt", RSDBP_OUT, V_STRING, 2000);
      v_cmd.execute();

      gv_service_code = v_cmd.value("p_ret");
      if(gv_service_code == 0)
         gv_log_id_req_env = v_cmd.value("p_qlog_id_req");
         if(p_depth_lvl > 1)
            gv_log_id_resp_env = v_cmd.value("p_qlog_id_resp");
         end;
         if(p_depth_lvl > 2)
            gv_log_id_resp = v_cmd.value("p_log_id_resp");
            gv_log_module_resp = v_cmd.value("p_log_module_resp");
         end;
      else
         gv_service_msg = v_cmd.value("p_ret_txt");
      end;

   onError(err)
      gv_service_code = 1;
      gv_service_msg = "Проблема при получении идентификаторов";
   end; /* macro m_get_id_by_req() */


   /* Получение идентификаторов для "синхронного" обмена */
   private macro m_get_id_by_single(p_msg_id, p_depth_lvl)
      var v_sql, v_cmd;

      m_reset_service_info();
      if(ValType(p_depth_lvl) == V_UNDEF)
         p_depth_lvl = 3;
      end;

      v_sql =         "DECLARE ";
      v_sql = v_sql + "    v_log_id           usr_exchng_log_dbt.t_id%TYPE := :p_log_id; ";
      v_sql = v_sql + "    v_qname_in         dqueue_log_dbt.t_qname%TYPE := :p_qname_in; ";
      v_sql = v_sql + "    v_qname_out        dqueue_log_dbt.t_qname%TYPE := :p_qname_out; ";

      v_sql = v_sql + "    v_jmsmessageid     dqueue_log_dbt.t_jmsmessageid%TYPE; ";
      v_sql = v_sql + "    v_jmsmessageguid   dqueue_log_dbt.t_jmsmessageguid%TYPE; ";
      v_sql = v_sql + "    v_jmscorrelationid dqueue_log_dbt.t_jmscorrelationid%TYPE; ";
      v_sql = v_sql + "    v_qlog_id_req      dqueue_log_dbt.t_id%TYPE; ";

      if(p_depth_lvl > 1)
         v_sql = v_sql + " v_qlog_id_resp     dqueue_log_dbt.t_id%TYPE; ";
         v_sql = v_sql + " v_log_link_id      dqueue_log_dbt.t_jmscorrelationid%TYPE; ";
      end;

      if(p_depth_lvl > 2)
         v_sql = v_sql + " v_log_id_src       usr_exchng_log_dbt.t_id%TYPE; ";
         v_sql = v_sql + " v_log_module_src   usr_exchng_log_dbt.t_module%TYPE; ";
      end;

      v_sql = v_sql + "    v_ret              NUMBER(10) := 0; ";
      v_sql = v_sql + "    v_ret_txt          usr_exchng_log_dbt.t_error_msg%TYPE := 'OK'; ";
      v_sql = v_sql + "    v_aux_buf          NUMBER(10); ";
      v_sql = v_sql + "    v_log_id_ref       utableprocessevent_dbt.t_recid%TYPE; ";

      v_sql = v_sql + "BEGIN ";
      v_sql = v_sql + "    BEGIN ";
      v_sql = v_sql + "        v_ret := 1; ";
      v_sql = v_sql + "        SELECT t_transparent_id INTO v_jmsmessageid ";
      v_sql = v_sql + "          FROM usr_exchng_log_dbt ";
      v_sql = v_sql + "         WHERE t_id = v_log_id; ";

/* Cherednichenko 19-12-2019 - отсекаем дублирующиеся записи
      v_sql = v_sql + "        v_ret := 2; ";
      v_sql = v_sql + "        SELECT t_jmsmessageguid INTO v_jmsmessageguid ";
      v_sql = v_sql + "          FROM dqueue_log_dbt ";
      v_sql = v_sql + "         WHERE t_jmsmessageid = v_jmsmessageid; ";
*/
      v_sql = v_sql + "        v_ret := 2; ";
      v_sql = v_sql + "        SELECT distinct t_jmsmessageguid INTO v_jmsmessageguid ";
      v_sql = v_sql + "          FROM dqueue_log_dbt ";
      v_sql = v_sql + "         WHERE t_jmsmessageid = v_jmsmessageid ";
      v_sql = v_sql + "         AND t_jmsmessageguid IS NOT NULL; ";
/* /Cherednichenko */

      v_sql = v_sql + "        v_ret := 3; ";
      v_sql = v_sql + "        SELECT t_id INTO v_qlog_id_req ";
      v_sql = v_sql + "          FROM dqueue_log_dbt ";
      v_sql = v_sql + "         WHERE t_jmsmessageguid = v_jmsmessageguid";
      v_sql = v_sql + "           AND t_qname = v_qname_in ";
      v_sql = v_sql + "           AND ROWNUM = 1; ";

      if(p_depth_lvl > 1)
         v_sql = v_sql + "     v_ret := 4; ";
         v_sql = v_sql + "     SELECT t_id INTO v_qlog_id_resp ";
         v_sql = v_sql + "       FROM dqueue_log_dbt ";
         v_sql = v_sql + "      WHERE t_jmsmessageguid = v_jmsmessageguid";
         v_sql = v_sql + "        AND t_qname = v_qname_out ";
         v_sql = v_sql + "        AND ROWNUM = 1; ";
      end;

      v_sql = v_sql + "        v_ret := 0; ";

      v_sql = v_sql + "    EXCEPTION ";
      v_sql = v_sql + "        WHEN NO_DATA_FOUND ";
      v_sql = v_sql + "        THEN ";
      v_sql = v_sql + "            v_ret := v_ret; ";
      v_sql = v_sql + "            v_ret_txt := 'NO_DATA_FOUND'; ";
      v_sql = v_sql + "        WHEN OTHERS ";
      v_sql = v_sql + "        THEN ";
      v_sql = v_sql + "            v_ret := SQLCODE; ";
      v_sql = v_sql + "            v_ret_txt := SUBSTR(SQLERRM, 1, 1500); ";
      v_sql = v_sql + "    END; ";

      v_sql = v_sql + "    :p_qlog_id_resp := v_qlog_id_resp; ";

      if(p_depth_lvl > 1)
         v_sql = v_sql + " :p_qlog_id_req := v_qlog_id_req; ";
      end;

      v_sql = v_sql + "    :p_ret := v_ret; ";
      v_sql = v_sql + "    :p_ret_txt := v_ret_txt; ";
      v_sql = v_sql + "END; ";

      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_log_id", RSDBP_IN, p_msg_id);
      v_cmd.addParam("p_qname_in", RSDBP_IN, CV_QNAME_IN);
      v_cmd.addParam("p_qname_out", RSDBP_IN, CV_QNAME_OUT);
      v_cmd.addParam("p_qlog_id_resp", RSDBP_OUT, V_INTEGER);
      if(p_depth_lvl > 1)
         v_cmd.addParam("p_qlog_id_req", RSDBP_OUT, V_INTEGER);
      end;
      v_cmd.addParam("p_ret", RSDBP_OUT, V_INTEGER);
      v_cmd.addParam("p_ret_txt", RSDBP_OUT, V_STRING, 2000);
      v_cmd.execute();

      gv_service_code = v_cmd.value("p_ret");
      if(gv_service_code == 0)
         gv_log_id_resp_env = v_cmd.value("p_qlog_id_resp");
         if(p_depth_lvl > 1)
            gv_log_id_req_env = v_cmd.value("p_qlog_id_req");
         end;
      else
         gv_service_msg = v_cmd.value("p_ret_txt");
      end;

      v_cmd.close(); v_cmd = null; v_sql = null;

   onError(err)
      gv_service_code = 1;
      gv_service_msg = "Проблема при получении идентификаторов";
   end; /* macro m_get_id_by_single() */


   /* Метод получения id запроса в usr_exchng_log_dbt (запрос без обертки) */
   macro m_get_log_id_req()
      m_reset_service_info();

      if(gv_log_id_req == 0)
         if(gv_is_separate_msg)
            if(gv_log_id_resp != 0)
               /* Объект был инициализирован id ответа */
               m_get_id_by_resp(gv_log_id_resp, 3);
            else
               gv_service_code = 1;
               gv_service_msg = "Проблема при получении идентификаторов (входные данные не корректны)";
            end;
         else
            /* Идентификатор уже должен быть - ошибка */
            gv_service_code = 1;
            gv_service_msg = "Проблема при получении идентификаторов (входные данные не корректны)";
         end;
      end;

      return gv_log_id_req;
   end; /* macro m_get_log_id_req() */


   /* Метод получения id запроса в dqueue_log_dbt (запрос с оберткой) */
   macro m_get_log_id_req_env()
      m_reset_service_info();

      if(gv_log_id_req_env == 0)
         if(gv_is_separate_msg)
            if(gv_log_id_req != 0)
               /* Выполняем поиск, начиная с запроса */
               m_get_id_by_req(gv_log_id_req, 1);
            elif(gv_log_id_resp != 0)
               /* Выполняем поиск, начиная с ответа */
               m_get_id_by_resp(gv_log_id_resp, 2);
            else
               gv_service_code = 1;
               gv_service_msg = "Проблема при получении идентификаторов (входные данные не корректны)";
            end;
         else
            /* Всегда выполняем поиск с глубиной 2 */
            if(gv_log_id_req != 0)
               m_get_id_by_single(gv_log_id_req, 2);
            else
               gv_service_code = 1;
               gv_service_msg = "Проблема при получении идентификаторов (входные данные не корректны)";
            end;
         end;
      end;

      return gv_log_id_req_env;
   end; /* macro m_get_log_id_req_env() */


   /* Метод получения id ответа в usr_exchng_log_dbt (ответ без обертки) */
   macro m_get_log_id_resp()
      m_reset_service_info();

      if(gv_log_id_resp == 0)
         if(gv_is_separate_msg)
            if(gv_log_id_req != 0)
               /* Объект был инициализирован id запроса */
               m_get_id_by_req(gv_log_id_req, 3);
            end;
         else
            /* Идентификатор уже должен быть - ошибка */
            gv_service_code = 1;
            gv_service_msg = "Проблема при получении идентификаторов (входные данные не корректны)";
         end;
      end;

      return gv_log_id_resp;
   end; /* macro m_get_log_id_resp() */


   /* Метод получения id ответа в dqueue_log_dbt (ответ с оберткой) */
   macro m_get_log_id_resp_env()
      m_reset_service_info();

      if(gv_log_id_resp_env == 0)
         if(gv_is_separate_msg)
            if(gv_log_id_resp != 0)
               /* Выполняем поиск, начиная с ответа */
               m_get_id_by_resp(gv_log_id_resp, 1);
            elif(gv_log_id_req != 0)
               /* Выполняем поиск, начиная с запроса */
               m_get_id_by_req(gv_log_id_req, 2);
            else
               gv_service_code = 1;
               gv_service_msg = "Проблема при получении идентификаторов (входные данные не корректны)";
            end;
         else
            if(gv_log_id_req != 0)
               /* Всегда выполняем поиск с глубиной 2 */
               m_get_id_by_single(gv_log_id_req, 2);
            else
               gv_service_code = 1;
               gv_service_msg = "Проблема при получении идентификаторов (входные данные не корректны)";
            end;
         end;
      end;

      return gv_log_id_resp_env;
   end; /* macro m_get_log_id_resp_env() */


   /*-------------*/
   /* Конструктор */
   /*-------------*/
   private macro m_init_log_chain_seg(i_log_id, i_log_module)
      var v_aux_buf;
      var v_sql, v_cmd, v_rs;

      m_reset_service_info();

      /* Для инициализации объекта нужен идентификатор записи */
      if(ValType(i_log_id) != V_UNDEF)
         /* Если модуль не передан, то сначала его определяем */
         if(ValType(i_log_module) != V_UNDEF)
            v_aux_buf = i_log_module;
         else
            v_sql = "SELECT t_module FROM usr_exchng_log_dbt WHERE t_id = :p_id ";
            v_cmd = RSDCommand(v_sql);
            v_cmd.addParam("p_id", RSDBP_IN, i_log_id);
            v_rs = RSDRecordSet(v_cmd);
            if(v_rs.movenext())
               v_aux_buf = v_rs.value("t_module");
            else
               gv_service_code = 1;
               gv_service_msg = string("Не найдено сообщение в usr_exchng_log_dbt (id = ", i_log_id, ")");
            end;
            v_rs.close(); v_cmd.close(); v_rs = null; v_cmd = null; v_sql = null;
         end;

         /* Распределяем полученные данные по параметрам */
         if(v_aux_buf == gv_req_mod_sep.value(0))
            gv_is_separate_msg = true;
            gv_log_id_req = i_log_id;
            gv_log_module_req = v_aux_buf;

         elif(v_aux_buf == gv_resp_mod_sep.value(0))
            gv_is_separate_msg = true;
            gv_log_id_resp = i_log_id;
            gv_log_module_resp = v_aux_buf;

         else
            gv_is_separate_msg = false;
            gv_log_id_req = i_log_id;
            gv_log_id_resp = i_log_id;
            gv_log_module_req = v_aux_buf;
            gv_log_module_resp = v_aux_buf;
         end;
      end;

   onError(err)
      gv_service_code = 1;
      gv_service_msg = "Проблема при работе конструктора";
   end; /* macro m_init_log_chain_seg() */


   /*----------------------------------------------------*/
   /* Переинициализация объекта на другую запись журнала */
   /*----------------------------------------------------*/
   macro m_reinit_log_chain_seg(p_log_id, p_log_module)
      gv_service_code = 0;
      gv_service_msg = ""; 

      gv_log_id_req = 0;
      gv_log_id_resp = 0;
      gv_log_module_req = "";
      gv_log_module_resp = "";
      gv_is_separate_msg = false;

      gv_log_id_req_env = 0;
      gv_log_id_resp_env = 0;

      m_init_log_chain_seg(p_log_id, p_log_module);

   onError(err)
      gv_service_code = 1;
      gv_service_msg = "Проблема при переинициализации объекта";
   end; /* macro m_reinit_log_chain_seg() */


   m_init_log_chain_seg(p_log_id, p_log_module);
end; /* class c_log_chain_msg() */