//diasoft_Pko_CancelExpiredOrders.mac
import "spDeal.mac";
import "cblogger.mac";
import "FuncObj.mac";
import "diasoft_createPKO_funcobj.mac"; 

//Точка входа funcobj
macro main(pObjecttype, pObjectid /*pko_writeoff.id*/, pParam)
  var dealid = 0;
  
  //найти в Картотеке запись об созданной операции
  var sql = "select pko.* from pko_writeoff pko where id = :1";
  var cmd = RSDCommand(sql);
  cmd.AddParam(":1",RSDBP_IN,pObjectId);
  cmd.Execute;
  var rs = TrsbDataSet(cmd);
  if (not rs.movenext)
    AddItLog("Операция не найдена в Картотеке pko_writeoff");
    return FuncObjResult(FUNCOBJ_STATE_FATAL_ERROR, "Операция не найдена в Картотеке pko_writeoff", FUNCOBJ_RESULT_ERROR); //если не нашли в Картотеке операцию, то выполнить операцию нельзя
  else
    dealid = rs.dealid;
  end;

  // обеспечить выполнение шага "Ожидание статуса обработки" и пойти по ветке "Отказ" в операции Списания ЦБ
  var sql_proccessed = "update pko_writeoff pko set step1_waitstatus = 1, step2_reject = 1, step3_writeoff = 0  where id = :3";
  var cmd_proccessed = RSDCommand(sql_proccessed);
  cmd_proccessed.AddParam(":3",RSDBP_IN,pObjectId);
  cmd_proccessed.Execute;

  if (SendLimitMessage(dealid, 0))
     AddItLog("Отказано в проведении операции");
  else 
    return FuncObjResult(FUNCOBJ_STATE_FATAL_ERROR, "Ошибка при отправке Diasoft.SendLimitMessage", FUNCOBJ_RESULT_ERROR);
  end;

  //открытие операции, выполнить шаг "Ожидание статуса обработки" при условии, что поле pko_writeoff.step1_waitstatus=1, и пойти по ветке "Отказ" при условии что pko_writeoff.step2_reject=1
  if (SP_ExecDeal(dealid, true /*, dttm(date,time)*/))
    var stat3 = AddCategory(101, string(dealid:34:o), 213, 1); // категорию "Отказано в проведении операции"="да"
    AddItLog("Отказано в проведении операции и шага Отказ" + string(dealid) );
  else
    AddItLog("Ошибка при отказе в операции списания ЦБ");
    return FuncObjResult(FUNCOBJ_STATE_FATAL_ERROR, "Ошибка при отказе в операции списания ЦБ", FUNCOBJ_RESULT_ERROR);
  end; 

  return FuncObjResult(FUNCOBJ_STATE_OK, "", FUNCOBJ_RESULT_OK);   
                      
end;