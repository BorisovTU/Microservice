/*-----------------------------------------------------*/
/* Запрос загрузки лимитов из Кондор+                  */
/*                                                     */
/* Автор: Гераськина Т.                                */
/*
   toDo - непосредственная установка лимита
*/
/*----------------------------------------------*/
import "ws_msg_transform_sec.mac"; /* Инструментарий для преобразования сообщений */

private class c_Result
   var Code : integer;  // Поле Код, обязательное: Да
   var Text : string;   // Поле Текст, обязательное: Нет
end;

// класс Результата по одному Клиенту
private class c_LimitParm     
   var PartyId    : integer;  // Код Клиента
   var LimType    : string;   // Тип лимита  "Э = Лимит на эмитента К = Лимит на контрагента"
   var Result     : c_Result; // Результат обработки запроса
end;

// параметры ответа
private class c_LoadLimitsDealsResponse ()
   var ReqID      : string;   // ИД исходного запроса
   var SenderId   : string;   // Код АС-получателя ответа
   var Result     : c_Result; // Результат обработки запроса
   var LimitList = TArray;    // Результат загрузки лимитов   
end;

// параметры ответа
private class c_outObj()
   var LoadLimitsDeals_resp = c_LoadLimitsDealsResponse; // имя строго по названию тега
end;


// класс входящих параметров 
private class c_LoadLimitsDealsRequest(p_req_data_obj)

   var PartyId       : integer;  // Код Клиента Да
   var LimType       : string;   // Тип лимита Да "Э = Лимит на эмитента К = Лимит на контрагента"
   var LimAmount     : double;   // Значение лимита на дату	Да
   
   /* Кратность параметров в соответствии со спецификацией */
   private macro uInitPrime(p_req_data_obj)
      private var err_txt;

      // проверка производится на этапе проверки XML, оставлено для контроля
      if(ValType(p_req_data_obj) != V_UNDEF)
         PartyId = uTryToGetPropS(p_req_data_obj, "PartyId");
         if (ValType(PartyId) == V_UNDEF)
            err_txt = string(InErrComPart, "PartyId");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_NO_REQUIRED_PRM)); 
         end;
         
         LimType = uTryToGetPropS(p_req_data_obj, "LimType"); 
         if (ValType(LimType) == V_UNDEF)
            err_txt = string(InErrComPart, "LimType");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_NO_REQUIRED_PRM)); 
         end;
         
         LimAmount = uTryToGetPropS(p_req_data_obj, "LimAmount"); 
         if (ValType(LimAmount) == V_UNDEF)
            err_txt = string(InErrComPart, "LimAmount");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_NO_REQUIRED_PRM)); 
         end;         
         
      else
         err_txt = string(InErrComPart, "LimitParm");
         RunError(err_txt, c_err_obj(err_txt,ERR_CODE_NO_REQUIRED_PRM));      
      end;
   end; /* macro uInitPrime() */

   uInitPrime(p_req_data_obj);   
end;


// непосредственная обработка единичной записи
// формирование класса ответа по единичной записи
macro RunLimitsDeals(p_req_data_obj)
   var ResultAction = c_LimitParm;
   
   var req_obj_serv = NULL;
   var resp_obj_serv = NULL;
   
   private var resp_resultCode, resp_resultText;
   
   req_obj_serv = c_LoadLimitsDealsRequest(p_req_data_obj);

   /* Вызываем сервис */
   if (1==1)
      resp_resultCode = 0;
      resp_resultText = "OK";
   end;

   /* Получаем объектный вид ответа */
   ResultAction.PartyId = req_obj_serv.PartyId;
   ResultAction.LimType = req_obj_serv.LimType;
   ResultAction.Result.Code = resp_resultCode;
   ResultAction.Result.Text = resp_resultText;  
   
   return ResultAction;
      
onError(err)
   /* Получаем объектный вид ответа */
   ResultAction.Code = c_err_obj.obtain_err_code(err);
   ResultAction.Text = c_err_obj.obtain_err_msg(err);
   
   return ResultAction;
end;


/*----------------------------------------------*/
/*  Запрос загрузки лимитов из Кондор+          */
/*----------------------------------------------*/
macro adp_LoadLimitsDeals(IncomeData)
   var out_str = "";
  
   var req_msg_obj = NULL;
   var req_data_obj_arr = NULL;   
   var req_data_obj = NULL;
   var req_obj_serv = NULL;
   var resp_data_obj = NULL;
   
   var LimitsDeals_result = NULL;
   var resp_data_obj_arr = TArray();
   var LimitList;   
   
   var v_result, v_xml;
   var err_txt, out_code;
   
   var ReqID            : string;   // ИД запроса Да
   var SenderId         : string;   // Код АС-источника сведений Нет
   var Date_            : date;     // Дата, на которую рассчитаны лимиты Да   
   
   if(ValType(IncomeData) == V_UNDEF)
      RunError(ERR_TEXT_NO_IN_MSG, c_err_obj(ERR_TEXT_NO_IN_MSG,
                                             ERR_CODE_NO_IN_PRM));
   end;
         
   req_msg_obj = psb_msg_converter_sec();
   
   /* Получаем исходный текст сообщения для дальнейшего преобразования */
   if(not req_msg_obj.make_pure_msg_from_ifx(IncomeData))
      err_txt = string("Обработка сообщения: ", req_msg_obj.get_service_msg());
      RunError(err_txt, c_err_obj(err_txt, ERR_CODE_CANT_TRANSFORM));
   end;

   v_xml = req_msg_obj.get_pure_msg();
   v_result = req_msg_obj.sec_validate_xml(v_xml);
   if(ValType(v_result) != V_UNDEF)
      err_txt = "Error!";
      RunError(err_txt, c_err_obj(v_result, ERR_CODE_CANT_TRANSFORM));
   end;
   
   /* Формируем объектный вид запроса */
   req_data_obj_arr = req_msg_obj.sec_internal_req(v_xml);   
   
   // обработка
   ReqID = uTryToGetPropS(req_data_obj_arr, "ReqID");
   if (ValType(ReqID) == V_UNDEF)
      err_txt = string(InErrComPart, "ReqID");
      RunError(err_txt, c_err_obj(err_txt,ERR_CODE_NO_REQUIRED_PRM)); 
   end;
   SenderId = uTryToGetPropS(req_data_obj_arr, "SenderId"); 
   Date_ = uTryToGetPropS(req_data_obj_arr, "Date");
   if (ValType(Date_) == V_UNDEF)
      err_txt = string(InErrComPart, "Date");
      RunError(err_txt, c_err_obj(err_txt,ERR_CODE_NO_REQUIRED_PRM)); 
   end;

   LimitList = uTryToGetPropS(req_data_obj_arr, "LimitList");
   
   resp_data_obj_arr.Size = 0;
   var resp_data_obj_arr_i = 0;

   for (req_data_obj, LimitList)

      // единичная обработка
      LimitsDeals_result = RunLimitsDeals(req_data_obj);      

      // массив результатов обработки
      resp_data_obj_arr_i = resp_data_obj_arr.Size;
      resp_data_obj_arr(resp_data_obj_arr_i) = LimitsDeals_result;
      
      req_data_obj = NULL;
      LimitsDeals_result = NULL;

   end;     
   
   // итоговый класс 
   resp_data_obj = c_outObj;
   resp_data_obj.LoadLimitsDeals_resp.ReqID = ReqID; 
   resp_data_obj.LoadLimitsDeals_resp.SenderId = SenderId;
   resp_data_obj.LoadLimitsDeals_resp.Result.Code = 0;
   resp_data_obj.LoadLimitsDeals_resp.Result.Text = "OK";
   resp_data_obj.LoadLimitsDeals_resp.LimitList = resp_data_obj_arr;
 
   /* Преобразуем объектный вид ответа в строковый */
   out_str = req_msg_obj.sec_internal_resp(resp_data_obj);
      
   req_msg_obj = NULL;

   return out_str;
   
onError(err)
   out_code = c_err_obj.obtain_err_code(err);
   err_txt = c_err_obj.obtain_err_msg(err);

   resp_data_obj = c_outObj;
   resp_data_obj.LoadLimitsDeals_resp.ReqID = XmlRpcRequestId; 
   resp_data_obj.LoadLimitsDeals_resp.SenderId = 0;
   resp_data_obj.LoadLimitsDeals_resp.Result.Code = out_code;
   resp_data_obj.LoadLimitsDeals_resp.Result.Text = err_txt;
   
   out_str = req_msg_obj.sec_internal_resp(resp_data_obj);
   return out_str;
end;


/*---------------------------------------*/
/*              Точка входа              */
/*---------------------------------------*/
macro LoadLimitsDeals (IncomeData)
   var out_str = "";
  
   out_str = adp_LoadLimitsDeals(IncomeData);
   return out_str;
end;

//LoadLimitsDeals (11);



