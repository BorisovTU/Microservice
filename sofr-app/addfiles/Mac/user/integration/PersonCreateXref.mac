/*-----------------------------------------------------*/
/* Создание ссылок по Субъекту ФЛ                        */
/*                                                     */
/* Автор: Гераськина Т.                                */
/*
*/
/*----------------------------------------------*/
import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import "RequestWS.mac";                   /* Функция передачи запроса в Web Sphere */
import "uws_exchng_log.mac";              /* Функционал логирования */
import func_lib;

class c_PersonCreateXrefRequest(Incomedata) 
   var PartyUID  ;               // :c_IntegrationSymbolicIdentifierXType;    //Глобальный идентификатор
   var MasterExternalId ;        // :c_IntegrationSymbolicIdentifierXType;    // Идентификатор записи во внешней cистеме
   var OperationalExternalId ;   // :c_IntegrationSymbolicIdentifierXType;    // Код внешней системы записи

   private macro uInitPrime(IncomeData)
      var aux_buf;
      if(ValType(IncomeData) != V_UNDEF)
         aux_buf = uTryToGetPropS(IncomeData, "PartyUID");
         if (ValType(aux_buf) != V_UNDEF)
            PartyUID = c_IntegrationSymbolicIdentifierXType;
            PartyUID.ObjectId = uTryToGetPropS(aux_buf, "ObjectId");
            PartyUID.SystemId = uTryToGetPropS(aux_buf, "SystemId");
            PartyUID.SystemNodeId = uTryToGetPropS(aux_buf, "SystemNodeId");
         end;
         aux_buf = uTryToGetPropS(IncomeData, "MasterExternalId");
         if (ValType(aux_buf) != V_UNDEF)
            MasterExternalId = c_IntegrationSymbolicIdentifierXType;
            MasterExternalId.ObjectId = uTryToGetPropS(aux_buf, "ObjectId");
            MasterExternalId.SystemId = uTryToGetPropS(aux_buf, "SystemId");
            MasterExternalId.SystemNodeId = uTryToGetPropS(aux_buf, "SystemNodeId");
         end;
         aux_buf = uTryToGetPropS(IncomeData, "OperationalExternalId");
         if (ValType(aux_buf) != V_UNDEF)
            OperationalExternalId = c_IntegrationSymbolicIdentifierXType;
            OperationalExternalId.ObjectId = uTryToGetPropS(aux_buf, "ObjectId");
            OperationalExternalId.SystemId = uTryToGetPropS(aux_buf, "SystemId");
            OperationalExternalId.SystemNodeId = uTryToGetPropS(aux_buf, "SystemNodeId");
         end;
      end;
   end;
   
   uInitPrime(IncomeData); 
      
end;


/* Основной класс работы с сервисом */
private class c_PersonCreateXrefProcessor(IncomeData)
   var PersonCreateXrefRequest; /* !!! По названию корневого тега запроса !!! */
   PersonCreateXrefRequest = c_PersonCreateXrefRequest(IncomeData);
end; 

/*-------------------------------------------------------------------------*/
/* Адаптер для вызова сервиса "Запрос на создание кросс-ссылки клиента ФЛ" */
/*-------------------------------------------------------------------------*/
macro run_PersonCreateXref(ReqData, p_log_id, p_transparent_id)
   var RespData = NULL;
   var err_txt = NULL;
   
   private var v_out_obj;
   private var RequestText;
   private var v_transformator;
   private var v_answer;
   private var v_answer_data;
   private var v_logger_obj;
   private var v_log_id;
   private var xml_str;
   private var v_xml_rpc_reqid = XmlRpcRequestId(); /* 20181229 - kva - чтобы дважды не вызывать функцию */

   if ( (ReqData == NULL) or (ValType(ReqData) == V_UNDEF) )
      return null;
   end;

   v_transformator = c_secur_msg_converter();

   // заполнение класса информацией 
   v_out_obj = c_PersonCreateXrefProcessor(ReqData);
   RequestText = v_transformator.m_secur_internal_resp(v_out_obj);
   
   v_logger_obj = uws_exchng_logger(null, p_transparent_id, v_xml_rpc_reqid, "PersonCreateXref", modulename(), null, RequestText);
   v_log_id = v_logger_obj.get_log_id();

      
   err_txt = v_transformator.m_validate_out_xml(RequestText);
   v_transformator = NULL;
            
   if (err_txt == NULL)
      v_answer = SubmitRequestToWS( 2,              // ИД очереди.
                                    "",             // ИД сообщения.
                                    "",             // Идентификатор Бэк Офиса. (не обрабатывается).
                                    false,          // Признак синхронной обработки. Если не указан, = False
                                    "SOFR",         // Подразделение системы-назначения.
                                    12,             // ИД типа данных
                                    RequestText,    // Бизнес данные в виде XML.
                                    v_log_id,       // ID usr_exchng_log
                                    "CIF"
                                    );

         if (v_answer.ResultCode == 0)
            // ответа не будет
         else 
            v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, v_answer.ResultText);
         end;
   else
      v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, err_txt);
   end;
         
   return RespData;

onError(err)

end; 

/*---------------------------------------*/
/*              Точка входа              */
/*---------------------------------------*/
/* ReqData - данные для отправки         */
/*---------------------------------------*/
macro PersonCreateXref (ReqData, in_log_id, in_transparent_id) /* 20190103 - kva - добавлены параметры логирования */
   return run_PersonCreateXref(ReqData, in_log_id, in_transparent_id); /* 20190103 - kva - добавлены параметры логирования */
end;

