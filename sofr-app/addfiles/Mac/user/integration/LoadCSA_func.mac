/*----------------------------------------------------------*/
/* LoadCSA_func.mac
/*                                                          */
   BIQ-5049. Загрузка CSA - функции                         */
/* Автор: Гераськина Т.                                     */
/*----------------------------------------------------------*/
import rsd;
import global_utils_intgr;
import DVInter;
import func_lib;

const OBJECTTYPE_CSA = 152;

const CATEGORY_MARGINTERMS = 102;
const CATEGORY_MARGINPAYER = 103;

const NOTEKIND_MATURITYCLASS_RP = 105;
const NOTEKIND_MATURITYCLASS_VD = 106;

class c_GenAgrForCSA
   var AgrID : integer;
   var PartyID : integer;
   var Confirmmethod : integer;
   var Trans         : integer; // gtv: для ГС ФИССиКО транспорт имеет свои коды
end;

macro GetGenAgr(code)
   var sql_str, cmd, rs, ret:c_GenAgrForCSA;
   ret.AgrID = -1;      //gtv: если не найдено, то -1, а не 0
   ret.PartyID = 0; 
   ret.Confirmmethod = 2;
   sql_str =   "select t_genagrid, t_partyid, nvl(t_confirmmethod, 0) t_confirmmethod "+
               "  from ddl_genagr_dbt "+
               " where (t_code = :code or t_num_csa_contr = :code) and t_start <= :dt"; 
   cmd = RSDCommand(sql_str);
   cmd.AddParam("code1", RSDBP_IN, code);
   cmd.AddParam("code2", RSDBP_IN, code);
   cmd.AddParam("dt", RSDBP_IN, date);
   rs = RSDRecordSet(cmd);
   if (rs.MoveNext()) 
      ret.AgrId = rs.value(0);
      ret.PartyId = rs.value(1);
      ret.Confirmmethod = rs.value(2);
   end;
   return ret;
end;

macro GetDateFromFile(p_date)
   var yy, mm,dd;
   var pos = 0, t;
   var res = date(0,0,0);
   pos = Index(p_date, "-");
   if (pos > 0)
      yy = Int(substr(p_date,1,pos-1));
      t = substr(p_date, pos+1);
      pos = Index(t,"-");
      if (pos > 0)
         mm = int(substr(t,1,pos-1));
         dd = Int(substr(t,pos+1));
      end;
      if (dd)
         res = date(dd,mm,yy);
      end;
   end;
   return res;
end;

macro GetFiidCodeByName(fiidname)
   var sql_str, cmd, rs;
   var res = 0;
   
   sql_str = "select t.t_fiid from dfininstr_dbt t where t.t_ccy=:cur_code";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("cur_code", RSDBP_IN, fiidname);
   rs = RSDRecordSet(cmd);
   if(rs.moveNext) 
      res = rs.value(0);
   end;
   return res;
end;

macro GetFiidIndex(code)
   var sql_str, cmd, rs;
   var res = -1;

   sql_str = "SELECT t_Fiid fiid FROM dfininstr_dbt WHERE t_definition = :1 AND t_fi_kind = 3 AND t_isclosed = chr(0)";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("1", RSDBP_IN, code);
   rs = RSDRecordSet(cmd);
   if (rs.MoveNext())
      res = rs.value(0);
   end;
   return res;
end;

macro CSABasis(_Basis)
   var res = 3;
   var FBasis = StrUpr(_Basis);
   if  (( FBasis == "360/30" ) or ( FBasis == "30/360" ) or ( FBasis == "C" ))
      res = 1; 
   elif(( FBasis == "360/ACT") or ( FBasis == "ACT/360") or ( FBasis == "M" ))
      res = 1; 
   elif(( FBasis == "365/ACT") or ( FBasis == "ACT/365") or ( FBasis == "5" ))
      res = 2; 
   elif(( FBasis == "ACT/ACT") or ( FBasis == "A" ))
      res = 3; 
   elif( FBasis == "6" )
      res = 3; 
  end;
  return res; 
end;

macro CheckCSA_byPartyid(_partyid, _err, _cpty)
  var res = true;
  var err;
  var sql_str, cmd, rs;
  sql_str = "select t_code from ddvcsa_dbt "+
            " where t_partyid = :p ";
  cmd = RSDCommand(sql_str);
  cmd.AddParam("p", RSDBP_IN, _partyid);
  rs = RSDRecordSet(cmd);
  if (rs.movenext)
      err = "Для контрагента "+_cpty+" уже есть соглашение CSA с кодом "+rs.value(0);
      SetParm(1,err);
      res = false;
  end;
  rs.close; cmd.close;
  return res;
onError(e)
  err = e.message + getFullCmdErrorText(cmd);
  SetParm(1,err);
  return false;
end;

// Проверка ген.соглашения. Так как ген.соглашение ищется по контрагенту, а не по входящим данных,
// то единственный вариант ошибки, что его вообще нет.
macro CheckGenAgr(_GenAgr, err_txt, _cpty)
  var res = true;
  var err;
  if (_GenAgr.AgrID < 0)
      err = "Для контрагента "+_cpty+" не найдено ген.соглашение ";
      SetParm(1,err);
      res = false;
  end;
  return res;
onError(e)
  err = e.message;
  SetParm(1,err);
  return false;
end;

class c_Margin(IncomeData)
   var MarginTerms      :string;
   var MarginPayer      :string;
   var MaturityClass_RP :string;
   var MinThAmount      :string;
   var MinPayAmount     :string;
   var MaturityClass_VD :string;
   
   var true_MarginTerms;
   var true_MarginPayer;

   /* Кратность параметров в соответствии со спецификацией */
   macro uInitPrime(IncomeData)   
      private var err_txt;
      var nametag_up = "AgreementList.Agreement.Margin";
      
      MarginTerms      = getValueFromProperty(IncomeData, "MarginTerms"       , nametag_up);
      MarginPayer      = getValueFromProperty(IncomeData, "MarginPayer"       , nametag_up);
      MaturityClass_RP = getValueFromProperty(IncomeData, "MaturityClass_RP"  , nametag_up);
      MinThAmount      = getValueFromProperty(IncomeData, "MinThAmount"       , nametag_up);
      MinPayAmount     = getValueFromProperty(IncomeData, "MinPayAmount"      , nametag_up);
      MaturityClass_VD = getValueFromProperty(IncomeData, "MaturityClass_VD"  , nametag_up);
   end;

   macro TransformFields

      if (ValType(MinThAmount) == V_STRING)
         MinThAmount = money(MinThAmount);
      end;
      if (ValType(MinPayAmount) == V_STRING)
         MinPayAmount = money(MinPayAmount);
      end;
   
      if ( StrUpr(MarginTerms ) == StrUpr("Одностороннее"))
         true_MarginTerms = 1;
      elif ( StrUpr(MarginTerms ) == StrUpr("Двустороннее"))
         true_MarginTerms = 2;
      else
         true_MarginTerms = 0;
      end;
      if ( StrUpr(MarginPayer ) == StrUpr("Оба"))
         true_MarginPayer = 3;
      elif ( StrUpr(MarginPayer ) == StrUpr("Контрагент"))
         true_MarginPayer = 2;
      elif ( ( StrUpr(MarginPayer ) == StrUpr("Мы")) or (StrUpr(MarginPayer ) == StrUpr("Банк")) )
         true_MarginPayer = 1;
      else
         true_MarginPayer = 0;
      end;
   end;
end;

class c_AgreementRate(IncomeData)
   var Rate          : double;
   var CashRate_Code :string;
   var Basis         :string;
   var StartDate     :date;
   
   /* Кратность параметров в соответствии со спецификацией */
   private macro uInitPrime(IncomeData)   
      private var err_txt;
      var nametag_up = "AgreementList.Agreement.AgreementRate";
      
      Rate           = getValueFromProperty(IncomeData, "Rate"          , nametag_up);
      CashRate_Code  = getValueFromProperty(IncomeData, "CashRate_Code" , nametag_up);
      Basis          = getValueFromProperty(IncomeData, "Basis"         , nametag_up);
      StartDate      = getValueFromProperty(IncomeData, "StartDate"     , nametag_up);
      if (not StartDate)
         StartDate = date();
      end;
   end;
   
   uInitPrime(IncomeData);
end;

class c_CSAFromFile
   var Agreement_Id           : integer;
   var Agreement_Code         : string;
   var Agreement_Name         : string;
   var Agreement_Parent       : string;
   var Agreement_Name_Parent  : string;
   var Cpty                   : string;
   var ValidFrom              : date;
   var ValidTo                : date;
   var Currency               : string;
   var MarginAllowed          : string;
   var Status                 : string;
   var Margin;
   var AgreementRate;
   
   var fAgreement_Id,
       fAgreement_Code,
       fAgreement_Name,
       fAgreement_Parent,
       fAgreement_Name_Parent,
       fCpty,
       fValidFrom,
       fValidTo,
       fCurrency,
       fMarginAllowed,
       fStatus,
       fMarginTerms,
       fMarginPayer,
       fMaturityClass_RP,
       fMinThAmount,
       fMinPayAmount,
       fMaturityClass_VD;
   
   var vPartyid :integer,
       vGenAgr  :c_GenAgrForCSA,
       vCur     :integer,
       vCSAid   :integer,
       vstate   :bool,
       vErrText :string;
       
   macro TransformFields()
      Agreement_Id         = Int(fAgreement_Id);
      Agreement_Code       = fAgreement_Code;
      Agreement_Name       = fAgreement_Name;
      Agreement_Parent     = fAgreement_Parent;
      Agreement_Name_Parent= fAgreement_Name_Parent;
      Cpty                 = fCpty;
      ValidFrom    = GetDateFromFile(fValidFrom);
      if (ValidFrom == date(0,0,0))
         ValidFrom = date();
      end;
      ValidTo        = GetDateFromFile(fValidTo);
      Currency       = fCurrency;
      MarginAllowed  = fMarginAllowed;
      Status         = fStatus;
      Margin         = c_Margin();
      Margin.MarginTerms      = fMarginTerms;
      Margin.MarginPayer      = fMarginPayer;
      Margin.MaturityClass_RP = fMaturityClass_RP;
      Margin.MinThAmount      = fMinThAmount;
      Margin.MinPayAmount     = fMinPayAmount;
      Margin.MaturityClass_VD = fMaturityClass_VD;
      
      Margin.TransformFields();
   end;
   
   macro FindCSA()
      var res = -1;
      var sql_str, cmd, rs;
      vstate = true;
      vErrText = "";
      sql_str = "select t_csaid, t_partyid, t_genagrid from DDVCSA_DBT where upper(t_code) = upper(:code)";
      cmd = RSDCommand(sql_str);
      cmd.AddParam("code", RSDBP_IN, Agreement_Code);
      rs = RSDRecordSet(cmd);
      if (rs.movenext)
         res = rs.value("t_csaid");
         if ( rs.value("t_partyid") != vPartyid )
            vstate = false;
            vErrText = "Передан контрагент id="+vPartyid+" в CSA контрагент id="+rs.value("t_partyid");
/*         elif ( rs.value("t_genagrid") != vGenAgr.AgrId )
            vstate = false;
            vErrText = "Передано ген.соглашение id="+vGenAgr.AgrId+" в CSA ген.соглашение id="+rs.value("t_genagrid"); */
         end;
      end;
      vCSAid = res;
   end;
   
   macro UpdateCSA()
      var sql_str, cmd;
      if (Margin.true_MarginPayer > 0)
         sql_str = "update DDVCSA_DBT "+
                   "   set t_number = :num, t_begdate = :begdate, t_enddate = "+GetSQLDate(ValidTo)+", t_basecurrency = :cur, "+
                   "       t_bankminpaycurr = :cur1, t_partyminpaycurr = :cur2, t_moveminpaycurr = :cur3, "+
                   "       t_bankrecipient = :r1, t_partyrecipient = :r2, t_bankminpaysum = :minpaysum1, t_partyminpaysum = :minpaysum2, "+
                   "       t_bankmarginlimsum = :marginlimsum1, t_partymarginlimsum = :marginlimsum2"+
                   " where t_csaid = :csaid";
      else
         sql_str = "update DDVCSA_DBT "+
                   "   set t_number = :num, t_begdate = :begdate, t_enddate = "+GetSQLDate(ValidTo)+", t_basecurrency = :cur, "+
                   "       t_bankminpaycurr = :cur1, t_partyminpaycurr = :cur2, t_moveminpaycurr = :cur3 "+
                   " where t_csaid = :csaid";
      end;

      cmd = RSDCommand(sql_str);
      cmd.AddParam("num", RSDBP_IN, Agreement_Name);
      cmd.AddParam("begdate", RSDBP_IN, ValidFrom);
      cmd.AddParam("cur", RSDBP_IN, vCur);
      cmd.AddParam("cur1", RSDBP_IN, vCur);
      cmd.AddParam("cur2", RSDBP_IN, vCur);
      cmd.AddParam("cur3", RSDBP_IN, vCur);
      if (Margin.true_MarginPayer == 0)
      elif (Margin.true_MarginPayer == 3)             // плательщик оба
         cmd.AddParam("r1", RSDBP_IN, StrFor(88));
         cmd.AddParam("r2", RSDBP_IN, StrFor(88));
         cmd.AddParam("minpaysum1", RSDBP_IN, Margin.MinPayAmount);
         cmd.AddParam("minpaysum2", RSDBP_IN, Margin.MinPayAmount);
         cmd.AddParam("marginlimsum1", RSDBP_IN, Margin.MinThAmount);
         cmd.AddParam("marginlimsum2", RSDBP_IN, Margin.MinThAmount);
      elif (Margin.true_MarginPayer == 2)             // плательщик контрагент
         cmd.AddParam("r1", RSDBP_IN, StrFor(88));
         cmd.AddParam("r2", RSDBP_IN, StrFor(0));
         cmd.AddParam("minpaysum1", RSDBP_IN, Margin.MinPayAmount);
         cmd.AddParam("minpaysum2", RSDBP_IN, 0);
         cmd.AddParam("marginlimsum1", RSDBP_IN, Margin.MinThAmount);
         cmd.AddParam("marginlimsum2", RSDBP_IN, 0);
      elif (Margin.true_MarginPayer == 1)             // плательщик банк
         cmd.AddParam("r1", RSDBP_IN, StrFor(0));
         cmd.AddParam("r2", RSDBP_IN, StrFor(88));
         cmd.AddParam("minpaysum1", RSDBP_IN, 0);
         cmd.AddParam("minpaysum2", RSDBP_IN, Margin.MinPayAmount);
         cmd.AddParam("marginlimsum1", RSDBP_IN, 0);
         cmd.AddParam("marginlimsum2", RSDBP_IN, Margin.MinThAmount);
      end;
      cmd.AddParam("csaid", RSDBP_IN, vCSAid);

      cmd.Execute;
      vstate = true;
   onError(e)
      vstate = false;
      vErrText = e.message + getFullCmdErrorText(cmd);
   end;
   
   macro AddNotekind(_objecttype, _objectid, _notekind, _notetext)
      var stat = 0;
      if (ValType(_notetext) != V_UNDEF)
         stat = addnoteforobject(_objecttype,_objectid,_notekind,_notetext);
         if (stat)
            vstate = false;
            vErrText = "Ошибка добавления примечания "+_notekind+"="+_notetext;
         end;
      end;
   end;

   macro AddCategory(_objecttype, _objectid, _groupid, _attrid)
      var stat = 0;
      if (_attrid > 0)
         DisconnectObjAttr(_objecttype, _objectid, _groupid, 0);
         ConnectObjAttr(stat, _objecttype, _objectid, _groupid, _attrid);
         if (stat)
            vstate = false;
            vErrText = "Ошибка добавления категории "+_groupid+"="+_attrid;
         end;
      end;
   end;
   
   macro UpdateCSAAdd()
      var objectid = String(vCSAid:34:o);
      AddCategory( OBJECTTYPE_CSA, objectid, CATEGORY_MARGINPAYER, Margin.true_MarginPayer );
      if (vstate)
         AddCategory( OBJECTTYPE_CSA, objectid, CATEGORY_MARGINTERMS, Margin.true_MarginTerms );
      end;
      if (vstate)
         AddNotekind( OBJECTTYPE_CSA, objectid, NOTEKIND_MATURITYCLASS_RP, Margin.MaturityClass_RP);
      end;
      if (vstate)
         AddNotekind( OBJECTTYPE_CSA, objectid, NOTEKIND_MATURITYCLASS_VD, Margin.MaturityClass_VD);
      end;
   end;

   macro CreateCSA()
      var dvcsaindRecArr = TArray();
      var dvcsaindRec = TRecHandler("dvcsaind.dbt");
      var stat;
      
      if (ValType(AgreementRate) == V_GENOBJ)
         dvcsaindRec.rec.fiid = GetFiidIndex(AgreementRate.CashRate_Code); //Индекс. Значение из DFININSTR_DBT -> T_FIID 
         if (dvcsaindRec.rec.fiid == -1)
            vstate = false;
            vErrText = "Не найден индекс "+AgreementRate.CashRate_Code+" в СОФР";
         end;

         dvcsaindRec.rec.spread = AgreementRate.Rate; //Спред
         dvcsaindRec.rec.dyear = CSABasis(AgreementRate.Basis); //Базис. Значение из DNAMEALG_DBT -> T_INUMBERALG c T_ITYPEALG = 8233
         dvcsaindRec.rec.kind = 0;
         dvcsaindRec.rec.days = 0; //Сдвиг
         dvcsaindRec.rec.begdate = AgreementRate.StartDate;
         dvcsaindRecArr[0] = dvcsaindRec;
      end;
      
      //Синтаксис
      //DV_CSAInsert(PartyId:INTEGER, GenAgrId:INTEGER, CSACode:STRING, BeginDate:DATE, BaseFIID:INTEGER, PrcStartDateKind:INTEGER, 
      //   PeriodMonth:INTEGER, PeriodDay:INTEGER, IsRepo:BOOL, TypeSecur:INTEGER, FormSecur:INTEGER, ReportingParty:INTEGER, IndRecArray:TARRAY):INTEGER
      //PartyId - Контрагент по CSA. Значение из DPARTY_DBT -> T_PARTYID
      //GenAgrId - Генеральное соглашение, связанное с контрагентом. Значение из DDL_GENAGR_DBT -> T_GENAGRID
      //CSACode - Номер соглашения.
      //BeginDate - Дата заключения CSA.
      //BaseFIID - Базовая валюта. Значение из DFININSTR_DBT -> T_FIID 
      //PrcStartDateKind - Способ расчёта процентов (на исх. ост., на вх. ост.). Значение из DNAMEALG_DBT -> T_INUMBERALG c T_ITYPEALG = 8235
      //PeriodMonth - Процентный период. Месяцев.
      //PeriodDay - Процентный период. Дней.
      //IsRepo - Признак "Репортинг для репозитария"
      //TypeSecur - Тип обеспечения. Значение из DLLVALUES_DBT -> T_ELEMENT c T_LIST = 4019
      //FormSecur - Форма обеспечения. Значение из DLLVALUES_DBT -> T_ELEMENT c T_LIST = 4020
      //ReportingParty - "Отправитель сообщения об уплате маржевых сумм". Значение из DNAMEALG_DBT -> T_INUMBERALG c T_ITYPEALG = 8236

      //Возвращаемое функцией значение - код ошибки, или 0, если выполнение успешно.

      if (vstate)
         stat = DV_CSAInsert(vPartyid, vGenAgr.AgrID, Agreement_Code, ValidFrom, vCur, 1, 
                              1, 0, true, 1, 1, 1, dvcsaindRecArr);
         if(stat != 0)
            vstate = false;
            vErrText = "Ошибка "+stat+" при вставке соглашения CSA.\n" + GetErrMsg();  
         end;
      end;
   end;

end;
