/**                                                                                                             
 @file ws_dboMessage.mac                                                                                          
 @brief Сервис отправки сообщений. Контроль отправки Уведомлений об открытии договора БО/ИИС.
                                                                                                                                                                                                                                                                                                                            
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.10.09 |Шестаков Д.В.  |BOSS-4499           |Создание макроса для планировщика                                                                                                                                                                                                                                                                                          
*/ 

import "dlQuery.mac","dbo_message_main.mac","func_lib.mac";

private const SS_RESPONSE_END = 3; /* Успешное завершение всего сервиса */
private const SS_RESPONSE_FATAL = 4; /* Ошибка исполнения, обработка всей цепочки прерывается */

class c_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText)
  var ProcessState = p_ProcessState;
  var ErrorNumber = p_ErrorNumber;
  var ErrorText = p_ErrorText;
end;


/**
   @brief Автоматическая отправка уведомления об открытии ДБО\ИИС, каждые три часа
  */ 
MACRO CheckAndSendDBOMessage()
    var sql            :String, 
        cmd            :Object, 
        DataSet        :Object,
        emailProcessor :Object,
        textEmail      :String, 
        subjectEmail   :String,
        dboMess        :Object, 
        statMes        :String,
        err            :Integer, 
        groupId        :Integer,
        new_depo       :Bool;

    sql = 
       " SELECT * FROM  "
     + " (SELECT  "      
     + "        (SELECT to_date(to_char(md.t_respdate,'ddmmyy') || to_char(md.t_resptime,' hh24miss'), 'ddmmyy hh24miss') "
     + "             FROM Ddlcontrmsg_Dbt md "
     + "           WHERE md.t_kind = 1 "
     + "              AND md.t_sendmesstate = 210 "
     + "              AND md.t_respdate <> to_date('01010001','ddmmyyyy') "
     + "              AND md.t_dlcontrid = Dl.t_Dlcontrid) as T_DateMMVBAnswer "  
     + "       ,(SELECT to_date(to_char(td.t_date,'ddmmyy') || to_char(td.t_time,' hh24miss'), 'ddmmyy hh24miss')  "
     + "           FROM dnotetext_dbt td  "
     + "           WHERE td.t_objecttype = 207  "
     + "             AND td.t_notekind = 102 "
     + "             AND td.t_date <> to_date('01010001','ddmmyyyy') "
     + "             AND td.t_documentid = LPAD(t_Dlcontrid, 34, '0')) as T_DateDepoAnswer "
     + "        ,dl.t_dlcontrid "
     + "        ,Sfn.t_Number "
     + "        ,to_char(Sfn.t_Datebegin,'dd.mm.yyyy') as t_Datebegin "
     + "        ,(select p.t_name from dparty_dbt p where p.t_partyid = sfn.t_partyid) as t_partyName  "   
     + "  FROM Ddlcontr_Dbt Dl INNER JOIN Dsfcontr_Dbt Sfn ON (Sfn.t_Id = Dl.t_Sfcontrid) "
     + " WHERE sfn.t_partyid <> 1  "
     + "   AND sfn.t_Dateclose = to_date('01010001','ddmmyyyy')  "
     + "   AND sfn.t_Datebegin > TRUNC(add_months(SYSDATE,-3)) "  
     + "   AND EXISTS (SELECT 1 "
     + "                 FROM Ddlcontrmsg_Dbt m "
     + "                WHERE m.t_kind = 1 "
     + "                  AND m.t_sendmesstate = 210 "
     + "                 AND m.t_respdate <> to_date('01010001','ddmmyyyy') "
     + "                  AND m.t_dlcontrid = Dl.t_Dlcontrid)  "
     + "   AND NOT EXISTS (SELECT 1 "
     + "                     FROM Ddlcontrmsg_Dbt m "
     + "                    WHERE m.t_dlcontrid = Dl.t_Dlcontrid "
     + "                      AND m.t_kind = 500 "
     + "                      AND m.t_senddate <> to_date('01010001','ddmmyyyy')) "
     + "   AND EXISTS (SELECT 1 "
     + "                 FROM dnotetext_dbt t  "
     + "                WHERE t.t_objecttype = 207  "
     + "                  AND t.t_notekind = 102 "
     + "                  AND t.t_documentid = LPAD(Dl.t_Dlcontrid, 34, '0') "
     + "                  AND t.t_date <> to_date('01010001','ddmmyyyy')) "              
     + "      ) t"
     + "   WHERE t.T_DateMMVBAnswer < sysdate - 1/24 "
     + "     and t.T_DateDepoAnswer < sysdate - 1/24 ";

    cmd = DL_RSDCommand();

    DataSet = cmd.Execute(sql);

    while(DataSet.moveNext())
      new_depo = CheckContractForNovelty(DataSet.DlContrID);

      dboMess = dboMessage(DataSet.DlContrID);
      if(dboMess and (dboMess.statsend != -1))  
        //отправляем сообщение клиенту  
        dboMess.CreateMessage(true, true, true, null, null, null, new_depo);
        if(dboMess.statsend == -1)
          err = 1;
          statMes = dboMess.statMes;
        end;
      else
        if(dboMess)
          err = 1;
          statMes = dboMess.statMes;
        else
          err = 1;
          statMes = "Объект уведомления не создан"; 
          dboMess = null;
        end;
      end;
      

      //Если есть ошибка, то отправляем письмо с текстом на Broker@rshb.ru
      if(err == 1)
        //задать для письма тему и текст
        subjectEmail = "Ошибка отправки уведомления клиенту об открытии ДБО\\ИИС. Клиент: " + DataSet.PartyName + ". ДБО: " + DataSet.Number + " от " + DataSet.Datebegin;
        textEmail = "В процессе формирования и отправки уведомления клиенту об открытии ДБО\\ИИС произошла ошибка: \n" + statMes;

        //получим ID группы
        if(not groupId)
           groupId = dboMess.GetNumTaxGroup("DBOMessErr");
        end;

        emailProcessor = c_email_proc_env();
        emailProcessor.m_set_msg_head(subjectEmail);
        emailProcessor.m_add_row_to_msg_text(textEmail);
        emailProcessor.m_save_to_submit_grp(groupId, true, true);
        emailProcessor.m_submit_email_synch();

        if(emailProcessor.m_get_error_status())
         statMes = "Сообщение сформировано, но не отправлено: " + emailProcessor.get_service_msg();
        end;
      end;

      dboMess = null; emailProcessor = null;  err = null; statMes = null;             
    end;

    return c_ssRunResult(SS_RESPONSE_END);

onerror(er)
    statMes =  "onError: " + er.message + " (модуль " + er.module + ", строка " + er.line + ")";
    return c_ssRunResult(SS_RESPONSE_FATAL, 1, statMes);
end;
