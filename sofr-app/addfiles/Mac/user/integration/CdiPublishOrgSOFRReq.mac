/** 
 @file CdiPublishOrgSOFRReq.mac
 @brief Запрос сервиса публикации в СОФР данных по организации по факту изменения данных в CDI 
  
 # tag 
 - functional_block:Клиенты_Регистрация_на_бирже
 - code_type:API
 - CDI

   
 # changeLog 
 |date       |author         |tasks            |note                                                         
 |-----------|---------------|-----------------|-------------------------------------------------------------------------------------------------------------
 |05.09.2023 |Гераськина Т.В.|BIQ-10268        | Создание
 |17.01.2024 |Гераськина Т.В.|DEF-59855        | Создание события для CdiUpdate только в том случае, когда действительно что-то поменялось

*/ 


/*-------------------------------------------------------------------------------*/
/* BIQ-10268                                                                     */
/* Автор: Гераськина Т.                                                          */
/*-------------------------------------------------------------------------------*/
import "func_lib.mac";
import "global_classes.mac";
import "CDIOrg_lib.mac";


// ответ
class c_CdiPublishOrgSOFRResp
   var ErrorList;
end;


// верхний объект ответа
class c_CdiPublishOrgSOFRResp_outobj
   var CdiPublishOrgSOFRResp = c_CdiPublishOrgSOFRResp;
end;

/*--------------------------------------------*/
/* Запрос сервиса публикации в СОФР данных по организации по факту изменения данных в CDI */
/*--------------------------------------------*/
macro adp_CdiPublishOrgSOFRReq(IncomeData, p_log_id, p_transparent_id)
   var resp_data_obj = NULL;
   var err_txt="", out_code = 0;
   var aux_buff;
   var PublishOrg;
   var temp_value;
   var partyid = 0;

   var res_arr = TArray;
   var res = NULL, ai = 0;
   
   if(ValType(IncomeData) == V_UNDEF)
      RunError(ERR_TEXT_NO_IN_MSG, c_err_obj(ERR_TEXT_NO_IN_MSG,
                                             ERR_CODE_NO_IN_PRM));
   end;

   PublishOrg = adp_CdiGetOrg(IncomeData, "CdiPublishOrgSOFRReq");
   
   // во входящем запросе должен быть передан partyid
   if (ValType(PublishOrg.ExtIdList) != V_UNDEF)
      for (temp_value, PublishOrg.ExtIdList)
         if ( Valtype(temp_value.ExtIdSourceSystem) != V_UNDEF )
            if ( (Valtype(temp_value.ExtIdSourceSystem.RecordCode) != V_UNDEF) and (temp_value.ExtIdSourceSystem.RecordCode == "SOFR") )
               if (temp_value.IsActive)
                  partyid = Int(temp_value.ExtIdPartySourceId.ObjectId);
               end;
            end;
         end;
      end;
   else 
      partyid = 0;
   end; 
   
   if (partyid > 0)
      //CCBO-10808 не все субъекты выгружаются
      if (CheckBlockUnloadCDI(partyid)) 
         res_arr.value(0) = c_Error;
         res_arr.value(0).ErrorCode = ERROR_CODE_BLOCK_UNLOAD;
         res_arr.value(0).ErrorDesc = "Субъект в СОФР временно заблокирован для обмена с CDI";
      else 
         res = UpdateParty_ByCDI(PublishOrg, partyid, false); // транзакция инициируется в начале обработки входящего запроса
         res_arr.value(0) = c_Error;
         res_arr.value(0).ErrorCode = res.ErrorCode;
         res_arr.value(0).ErrorDesc = res.ErrorDesc;
         if ( (res.ErrorCode == 0) and (res.NeedUpdate) ) // DEF-59855
            PutInProcess(5055, partyid); // событие вставляется с типом 2, для UpdateOrg будет означать обновление после Publish
         end;
      end;
   else 
      res_arr.value(0) = c_Error;
      res_arr.value(0).ErrorCode = 1;
      res_arr.value(0).ErrorDesc = "Не найден субъект по коду SOFR";
   end;

   // итоговый класс 
   resp_data_obj = c_CdiPublishOrgSOFRResp_outobj();
   resp_data_obj.CdiPublishOrgSOFRResp.ErrorList = TArray;
   if (res_arr.size > 0)
      resp_data_obj.CdiPublishOrgSOFRResp.ErrorList = res_arr;
   else
      resp_data_obj.CdiPublishOrgSOFRResp.ErrorList.value(0) = c_Error;
      resp_data_obj.CdiPublishOrgSOFRResp.ErrorList.value(0).ErrorCode = 0;
      resp_data_obj.CdiPublishOrgSOFRResp.ErrorList.value(0).ErrorDesc = "OK"; 
   end;
   res_arr = NULL;

   return resp_data_obj;
   
onError(err)
   out_code = c_err_obj.obtain_err_code(err);
   err_txt = c_err_obj.obtain_err_msg(err);

   resp_data_obj = c_CdiPublishOrgSOFRResp_outobj;

   resp_data_obj.CdiPublishOrgSOFRResp.ErrorList = TArray;
   resp_data_obj.CdiPublishOrgSOFRResp.ErrorList.value(0) = c_Error;
   resp_data_obj.CdiPublishOrgSOFRResp.ErrorList.value(0).ErrorCode = 1;
   resp_data_obj.CdiPublishOrgSOFRResp.ErrorList.value(0).ErrorDesc = "Ошибка, код "+out_code+", "+err_txt;
   
   return resp_data_obj;
end;

/*---------------------------------------------------------*/
/* Запрос с контейнером записей, подлежащих пересчету с СИ */
/*---------------------------------------------------------*/
macro CdiPublishOrgSOFRReq(IncomeData, in_log_id, in_transparent_id) 
   var out_obj;
   out_obj = adp_CdiPublishOrgSOFRReq(IncomeData, in_log_id, in_transparent_id); 
   return out_obj;
end;
