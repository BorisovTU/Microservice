/*
$Name:        _UpdateXml.mac
$Module:      Securities
$Description: Корректировка xml-файлов клиентов на ММВБ (привязанных к сообщениям по Alt+M)
*/
Import "dlcontrmsg_mmvb.mac";

private const V_SPECVAL     = 26;

private macro ТипСообщения(kind)
  if(kind == OBJTYPE_DLCONTRMSG_MARKETREG)
     return "Регистрация клиента на бирже";
  elif(kind == OBJTYPE_DLCONTRMSG_MPREG)
     return "Регистрация клиента на биржевой площадке";
  elif(kind == OBJTYPE_DLCONTRMSG_MPCLOSE)
     return "Закрытие биржевой площадки";
  elif(kind == OBJTYPE_DLCONTRMSG_MARKETCLOSE)
     return "Закрытие ДБО";
  elif(kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA)
     return "Изменение информации о личных данных клиента";
  else
    return string(kind);
  end;
end;

private macro CloseImage(ImageID, pDate)
  var query = " UPDATE dimgdata_dbt SET "+
                " t_CloseDate = ? "+
              " WHERE t_ImageID = ? ";

  var cmd = RSDCommand(query);
  cmd.AddParam("", RSDBP_IN, pDate);
  cmd.AddParam("", RSDBP_IN, ImageID);

  cmd.Execute();
  return 0;
onError(er)
  return 1;
end;

private macro _GetCountryByCode(partyID:integer) :string
  var countryCode:string = "";
  var client = TRecHandler("party.dbt");

  DL_GetRecHandler(client, "SELECT * FROM DPARTY_DBT WHERE T_PARTYID = "+partyID);
  countryCode = DL_GetCountryByCodeLat3(client.rec.NrCountry).CodeNum3;

  return countryCode;
end;

// обновить прикрепленный файл на сообщении
private macro UpdateXmlFileOnMsg(dlContrMsg:TRecHandler, dlContr:TRecHandler, sfContr:TRecHandler, NeedUpdate:@bool)
  var stat=0, i=0,
      transactionStarted:bool = false;
  NeedUpdate = false;

  RslDefCon.BeginTrans();
  transactionStarted = true;

  var filePathName = "", xmlData = "";
  var queryImg = " SELECT t_imageid, t_filename " +
                   " FROM dimgdata_dbt " +
                  " WHERE t_imagetype = 1 " +
                    " AND t_objecttype = " + OBJTYPE_BSCMSG_DL +
                    " AND t_objectid = LPAD("+dlContrMsg.rec.ID+", 34, '0') " +
                    " AND t_closedate = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
                  " ORDER BY t_IsMain desc ";

  var ds = TRsbDataSet(queryImg);
  if(ds.MoveNext())
     var fd = DlGetTxtFileDir();

     if(WEB_ShowImageByID(Int(ds.ImageID), filePathName))
       var xmlReader = Dl_XmlReader();
       if(xmlReader.Load_file(filePathName))
         var xmlMsg = xmlReader.GetXml();
         var Attr_CountryCode_new, _CountryCode_new, CountryCode;

         var CLIENT_SIMPLES = xmlMsg.getElementsByTagName("CLIENT_SIMPLE");
         if((CLIENT_SIMPLES != null) and (CLIENT_SIMPLES.length > 0))
           while((stat == 0) and (CLIENT_SIMPLES.item(i) != null) and (CLIENT_SIMPLES.item(i) != V_SPECVAL))
             CountryCode = CLIENT_SIMPLES.item(i).getAttribute("CountryCode");

             if((CountryCode != V_SPECVAL) and (CountryCode == "")) // Атрибут 'CountryCode' не заполнен
               NeedUpdate = true;

               _CountryCode_new = _GetCountryByCode(sfContr.rec.PartyID);
               if(_CountryCode_new != "")
                 Attr_CountryCode_new = xmlMsg.CreateAttribute("CountryCode");
                 Attr_CountryCode_new.value = _CountryCode_new;
                 CLIENT_SIMPLES.item(i).SetAttributeNode(Attr_CountryCode_new);
               else
                 stat = 101; // не задана страна
               end;
             end;

             i = i + 1;
           end;
         end;

         i = 0;
         var CLIENT_DU = xmlMsg.getElementsByTagName("CLIENT_DU");
         if((CLIENT_DU != null) and (CLIENT_DU.length > 0))
           while((stat == 0) and (CLIENT_DU.item(i) != null) and (CLIENT_DU.item(i) != V_SPECVAL))
             CountryCode = CLIENT_DU.item(i).getAttribute("CountryCode");

             if((CountryCode != V_SPECVAL) and (CountryCode == "")) // Атрибут 'CountryCode' не заполнен
               NeedUpdate = true;

               _CountryCode_new = _GetCountryByCode(sfContr.rec.PartyID);
               if(_CountryCode_new != "")
                 Attr_CountryCode_new = xmlMsg.CreateAttribute("CountryCode");
                 Attr_CountryCode_new.value = _CountryCode_new;
                 CLIENT_DU.item(i).SetAttributeNode(Attr_CountryCode_new);
               else
                 stat = 101; // не задана страна
               end;
             end;

             i = i + 1;
           end;
         end;

         if((stat == 0) and NeedUpdate)
           var fileName_new, ext;
           SplitFile(filePathName, fileName_new, ext);
           var filePathName_new = fd + "\\" + fileName_new + "_new" + ext;
           xmlMsg.save(filePathName_new);

           if(not LoadImageObj(filePathName_new, OBJTYPE_BSCMSG_DL, UniID(dlContrMsg, OBJTYPE_BSCMSG_DL), 1));
             stat = 1;
             MsgBox("К записи о сообщении не удалось прикрепить файл сообщения");
           end;

           if(stat == 0)
             RemoveFile(filePathName_new);
             // старый прикрепленный файл с сообщением - перенесем в закрытые
             stat = CloseImage(Int(ds.ImageID), {curdate});
           end;
         end;
       end;

       RemoveFile(filePathName);
     end;
  end;

  if(stat == 0)
    RslDefCon.CommitTrans();
  else
    RslDefCon.RollbackTrans();
  end;
  transactionStarted = false;

  return stat;
OnError(er)
  if(transactionStarted)
    RslDefCon.RollbackTrans();
  end;
  if(er.Code != 0)
    stat = er.Code;
  else
    stat = 1;
  end;
  return stat;
end;

private macro UpdateXmlOnMsg()
  var stat = 0, i=0, find=false, NeedUpdate=false;
  var dlContrMsg = TRecHandler("dlcontrmsg.dbt"),
      dlContr = TRecHandler("dlcontr.dbt"), 
      sfContr = TRecHandler("sfcontr.dbt");

  var sql = "SELECT m.* "+
             " FROM DDLCONTRMSG_DBT m, ddlcontr_dbt l, dsfcontr_dbt s "+
            " WHERE m.T_ISONLINESENDMES = 'X' "+
              " AND m.T_SENDMESSTATE = 101 "+
              " AND M.T_DLCONTRID = L.T_DLCONTRID "+
              " AND S.T_ID = L.T_SFCONTRID "+
            " ORDER BY m.t_CreateDate, m.t_CreateTime";

  var cmd = DL_RSDCommand(sql);
  InitProgress(cmd.GetCount(), "Обработка сообщений со статусом 101");

  [
                          Корректировка сообщений со статусом 101 
                                  за ########## г.
   ┌───────────────────┬───────────────────────┬───────────────────────────────┬──────┐
   │      Код ДБО      │ Наименование клиента  │         Тип сообщения         │ stat │
  ]
     ({CurDate});

  var ds = cmd.Execute();
  while(ds.moveNext())
    dlContrMsg.clear();
    ds.GetRecord().CopyTo(dlContrMsg.rec);

    if(DL_GetRecHandler(dlContr, "SELECT * FROM DDLCONTR_DBT WHERE T_DLCONTRID = "+dlContrMsg.rec.DlContrID) and
       DL_GetRecHandler(sfContr, "SELECT * FROM DSFCONTR_DBT WHERE T_ID = "+dlContr.rec.SfContrID)
      )
      stat = UpdateXmlFileOnMsg(dlContrMsg, dlContr, sfContr, @NeedUpdate);

      if(NeedUpdate)
        find = true;
  [├───────────────────┼───────────────────────┼───────────────────────────────┼──────┤
   │###################│#######################│###############################│######│]
           ( sfContr.rec.Number:w,
             DL_GetPartyShortName(sfContr.rec.PartyID):w,
             ТипСообщения(dlContrMsg.rec.Kind):w,
             string(stat):w );
      end;
    end;

    i = i + 1;
    UseProgress(i);
  end;
  [└───────────────────┴───────────────────────┴───────────────────────────────┴──────┘];
  if(not find)
    [Нет сообщений для корректировки.];
  end;
  RemProgress();

end;

UpdateXmlOnMsg();
