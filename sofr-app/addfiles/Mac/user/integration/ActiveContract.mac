/*-----------------------------------------------------*/
/* Запроc активации Договора брокерского обслуживания  */
/*                                                     */
/* Автор: Гераськина Т.                                */
/*
   toDo - непосредственная активация договора
*/
/*----------------------------------------------*/
import "ws_msg_transform_sec.mac"; /* Инструментарий для преобразования сообщений */

private class c_Result
   var Code : integer;  // Поле Код, обязательное: Да
   var Text : string;   // Поле Текст, обязательное: Нет
end;

// параметры ответа
private class c_ActiveContractResponse (IncomeData)
   var ReqID      : string;   // Идентификатор, обязательное: Да
   var SenderId   : string;   // Из запроса, обязательное: Нет
   var Result     : c_Result; // обязательное: Да
end;

// параметры ответа
private class c_outObj()
   var ActiveContract_resp = c_ActiveContractResponse; // имя строго по названию тега
end;

// класс входящих параметров 
private class c_ActiveContractRequest(p_req_data_obj)

   var ReqID            : string;   // ИД запроса Да
   var SenderId         : string;   // Код АС-источника сведений Нет
   var ContractNumber   : string;   // Номер Договора Да
   var ContractDate     : date;     // Дата Договора Да
   var URL              : string;   // URL-ссылка на Эkектронное Досье Нет
   
  
   /* Кратность параметров в соответствии со спецификацией */
   private macro uInitPrime(p_req_data_obj)
      private var err_txt;

      // проверка производится на этапе проверки XML, оставлено для контроля
      if(ValType(p_req_data_obj) != V_UNDEF)
         ReqID = uTryToGetPropS(p_req_data_obj, "ReqID");
         if (ValType(ReqID) == V_UNDEF)
            err_txt = string(InErrComPart, "ReqID");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_NO_REQUIRED_PRM)); 
         end;
         
         SenderId = uTryToGetPropS(p_req_data_obj, "SenderId"); 
         
         ContractNumber = uTryToGetPropS(p_req_data_obj, "ContractNumber"); 
         if (ValType(ContractNumber) == V_UNDEF)
            err_txt = string(InErrComPart, "ContractNumber");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_NO_REQUIRED_PRM)); 
         end;
         
         ContractDate = uTryToGetPropS(p_req_data_obj, "ContractDate"); 
         if (ValType(ContractDate) == V_UNDEF)
            err_txt = string(InErrComPart, "ContractDate");
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_NO_REQUIRED_PRM)); 
         end;         
         
         URL = uTryToGetPropS(p_req_data_obj, "URL"); 
        
      else
         err_txt = string(InErrComPart, "ActiveContract_req");
         RunError(err_txt, c_err_obj(err_txt,ERR_CODE_NO_REQUIRED_PRM));      
      end;
   end; /* macro uInitPrime() */

   uInitPrime(p_req_data_obj);   
end;


// непосредственная обработка единичной записи
// формирование класса ответа по единичной записи
macro RunActiveContract(p_req_data_obj)
   var ResultAction = c_Result;
   
   var req_obj_serv = NULL;
   var resp_obj_serv = NULL;
   
   private var resp_resultCode, resp_resultText;
   
   req_obj_serv = c_ActiveContractRequest(p_req_data_obj);

   /* Вызываем сервис */
   if (1==1)
      resp_resultCode = 0;
      resp_resultText = "OK";
   end;

   /* Получаем объектный вид ответа */
   ResultAction.Code = resp_resultCode;
   ResultAction.Text = resp_resultText;  
   
   return ResultAction;
      
onError(err)
   /* Получаем объектный вид ответа */
   ResultAction.Code = c_err_obj.obtain_err_code(err);
   ResultAction.Text = c_err_obj.obtain_err_msg(err);
   
   return ResultAction;
end;


/*----------------------------------------------*/
/*  активация Договора брокерского обслуживания */
/*----------------------------------------------*/
macro adp_ActiveContract(IncomeData)
   var out_str = "";
  
   var req_msg_obj = NULL;
   var req_data_obj = NULL;
   var req_obj_serv = NULL;
   var resp_data_obj = NULL;
   
   var ActiveContract_result = NULL;
   
   var v_result, v_xml;
   var err_txt, out_code;
   
   if(ValType(IncomeData) == V_UNDEF)
      RunError(ERR_TEXT_NO_IN_MSG, c_err_obj(ERR_TEXT_NO_IN_MSG,
                                             ERR_CODE_NO_IN_PRM));
   end;
         
   req_msg_obj = psb_msg_converter_sec();
   
   /* Получаем исходный текст сообщения для дальнейшего преобразования */
   if(not req_msg_obj.make_pure_msg_from_ifx(IncomeData))
      err_txt = string("Обработка сообщения: ", req_msg_obj.get_service_msg());
      RunError(err_txt, c_err_obj(err_txt, ERR_CODE_CANT_TRANSFORM));
   end;

   v_xml = req_msg_obj.get_pure_msg();
   v_result = req_msg_obj.sec_validate_xml(v_xml);
   if(ValType(v_result) != V_UNDEF)
      err_txt = "Error!";
      RunError(err_txt, c_err_obj(v_result, ERR_CODE_CANT_TRANSFORM));
   end;
   
   /* Формируем объектный вид запроса */
   req_data_obj = req_msg_obj.sec_internal_req(v_xml);   
   
   // обработка
   ActiveContract_result = RunActiveContract(req_data_obj);

   // итоговый класс 
   resp_data_obj = c_outObj;
   resp_data_obj.ActiveContract_resp.ReqID = req_data_obj.ReqID; 
   resp_data_obj.ActiveContract_resp.SenderId = req_data_obj.SenderID;
   resp_data_obj.ActiveContract_resp.Result = ActiveContract_result;

   /* Преобразуем объектный вид ответа в строковый */
   out_str = req_msg_obj.sec_internal_resp(resp_data_obj);
      
   req_msg_obj = NULL;

   return out_str;

onError(err)
   out_code = c_err_obj.obtain_err_code(err);
   err_txt = c_err_obj.obtain_err_msg(err);

   resp_data_obj = c_outObj;
   resp_data_obj.ActiveContract_resp.ReqID = XmlRpcRequestId; 
   resp_data_obj.ActiveContract_resp.SenderId = req_data_obj.SenderID;
   resp_data_obj.ActiveContract_resp.Result.code = out_code;
   resp_data_obj.ActiveContract_resp.Result.text = err_txt;

   out_str = req_msg_obj.sec_internal_resp(resp_data_obj);
   return out_str;
end;


/*---------------------------------------*/
/*              Точка входа              */
/*---------------------------------------*/
macro ActiveContract (IncomeData)
   var out_str = "";
  
   out_str = adp_ActiveContract(IncomeData);
   return out_str;
end;



