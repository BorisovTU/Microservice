/**
 @file dbo_qi_Recognition.mac
 @brief Функционал используемый при признании субъекта квалифицированным инвестором
 
 Файл содержит функционал используемый при признании субъекта квалифицированным инвестором.
 
  # tag
 - functional_block:Отчетность_Клиент
 - code_type:API
 - Квалификация
 - КИ
 - Квалифицированный инвестор
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |01.12.2023 |Топорков Д.В.|BIQ-16875 BOSS-194 BOSS-1421                              | Создание
 */
 
import rsd, RsbFormsInter, globals;
import "oralib.mac";
import RsbDataSet;
import "dbo_qi_RecognitionNotification.mac";

/**
@brief Функция создания и отправки уведомления при присвоении статуса квал.инвестора
@param[in] sfContrID ID Договора обслуживания (таблица dSfContr_dbt)
@param[in] recognitionDate Дата признания КИ
@return Integer Статус выполнения (0 - успешное выполнение)
*/
macro QIRecognitionNotifyBySfContr(sfContrID: Integer, recognitionDate: Date, error: @String): Integer
  var rs = ExecSQLSelect("SELECT t_dlContrID FROM dDlContr_dbt WHERE t_sfContrID = " + sfContrID);
  var params: CQIRecognitionParams = CQIRecognitionParams();

  error = "";
  
  if (not rs.movenext())
    error = "Ошибка формирования уведомления:|Не найден договор брокерского обслуживания для договора обслуживания с ID = " + sfContrID;
    return 1;
  end;
  
  params.dlContrID = rs.value("t_dlContrID");
  params.client = GetClient(params.dlContrID);
  params.signer = GetQIDefaultSigner();
  params.notificationDate = Date();
  params.recognitionDate = recognitionDate;
  params.isSendEmail = true;
  params.isPdfOutput = true;
  params.isDocOutput = false;
  
  error = QIRecognitionNotification(params);
  
  if (StrLen(error) > 0)
    error = "Ошибка формирования уведомления:|" + error;
    return 1;
  end;
  
  return 0;
  
onError(err)
  error = "Ошибка выполнения при формировании уведомления:|" + err.Module + " строка " + err.Line + "|" + err.message;
  return 1;
end;


/**
@brief Функция создания и отправки уведомления при присвоении статуса квал.инвестора
@param[in] PartyID ID Клиента
@param[in] recognitionDate Дата признания КИ
@return Integer Статус выполнения (0 - успешное выполнение)
*/
macro QIRecognitionNotifyByParty(PartyID: Integer, recognitionDate: Date, error: @String): Integer
  var DataSet;
  var sql = " SELECT t_dlContrID  as t_dlContrID "   +
            "   FROM dDlContr_dbt  dl join dsfcontr_dbt sf on dl.t_sfcontrid = sf.t_id and sf.t_servkind = 0 "   +
            "  WHERE sf.t_dateclose = to_date('01.01.0001','dd.mm.yyyy') AND sf.t_partyid = " + PartyID;
  var cmd = DL_RSDCommand();
  var cnt = cmd.GetCount(sql);
  var params: CQIRecognitionParams = CQIRecognitionParams();

  error = "";

  if(cnt > 0)
    DataSet = cmd.Execute(sql);

    while(DataSet.moveNext())
      params.dlContrID = DataSet.value("t_dlContrID");
      params.client = GetClient(params.dlContrID);
      params.signer = GetQIDefaultSigner();
      params.notificationDate = Date();
      params.recognitionDate = recognitionDate;
      params.isSendEmail = true;
      params.isPdfOutput = true;
      params.isDocOutput = false;
      
      error = QIRecognitionNotification(params);
      
      if (StrLen(error) > 0)
        error = "Ошибка формирования уведомления:|" + error;
        return 1;
      end;
    end;
  else 
    error = "Ошибка формирования уведомления:|Не найден договор брокерского обслуживания для клиента с ID = " + PartyID;
    return 1;
  end;
  
  return 0;
  
onError(err)
  error = "Ошибка выполнения при формировании уведомления:|" + err.Module + " строка " + err.Line + "|" + err.message;
  return 1;
end;