//-----------------------------------------------------------------------------------------
// Получение ответа СОФР результата обработки удаления проводки
// В.Горленко
//-----------------------------------------------------------------------------------------
import global_utils_intgr, email_notifyfun, likepy;

Private const EMAIL_SUBJECT = "Ошибка. Загрузка данных по удаленным проводкам";
Private var EMAIL_RECEIVER = "gorlenko@nsk.softlab.ru";
//-----------------------------------------------------------------------------------------

Private macro getProperty ( obj, property )
    if ( genPropId (obj, property) < 0 )
        return NULL;
    end;

    return uTryToGetPropS (obj, property);
onError
    return NULL;
End;
//-----------------------------------------------------------------------------------------

Private macro sendMail ( email, subject, body );
    AddNotifyToDbt(subject, body, email);
    SendNotyfyToEmail ();

    return TRUE;
OnError
    return FALSE;
End;
//-----------------------------------------------------------------------------------------

Macro ApproveAccountingEntries_resp ( response )
    Var trnResult = getProperty (response, "approveEntryResultList"), entryResult, responseCode, responseNote, entryId, mailBody = "";
    if ( (trnResult == NULL) or (valType (trnResult) != V_GENOBJ) )
        return false;
    end;

    if ( ((entryResult = getProperty (trnResult, "approveEntryResult")) == NULL) )
        return false;
    end;

    responseCode = getProperty (entryResult, "responseCode");
    if ( responseCode == NULL )
        return false;
    elif ( int  (responseCode) == 0 )
        return true;
    end;

    entryId = getProperty (entryResult, "entryId");
    responseNote = getProperty (entryResult, "responseNote");

    mailBody = "ApproveAccountingEntries:\n"+"\tentryId: "+entryId+"\n\tКод ошибки: "+responseCode+ifThenElse (responseNote, " ("+responseNote+")", "");

    if ( mailBody )
        if ( not (EMAIL_RECEIVER) )
            return false;
        end;
        return sendMail (EMAIL_RECEIVER, EMAIL_SUBJECT, mailBody);
    end;
    return true;
End;
//-----------------------------------------------------------------------------------------
