/*-----------------------------------------------------*/
/* Создание ссылок по Субъекту ФЛ                        */
/*                                                     */
/* Автор: Гераськина Т.                                */
/*
*/
/*----------------------------------------------*/
import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import "RequestWS.mac";                   /* Функция передачи запроса в Web Sphere */
import "uws_exchng_log.mac";              /* Функционал логирования */
import func_lib;

private class c_LEXref(IncomeData)
   var OperationalDataId : c_IntegrationSymbolicIdentifierXType;
end;
   
class c_PersonCreateXrefLEReq(Incomedata) 
   var MasterDataId : c_IntegrationSymbolicIdentifierXType;    // Универсальный идентификатор клиента ЮЛ (мастер-данные)
   var LEXRef       : c_LEXref;                                // Кросс-ссылка клиента ЮЛ во внешней системе
   
   private macro uInitPrime(IncomeData)
      var aux_buf;
      if(ValType(IncomeData) != V_UNDEF)
         aux_buf = uTryToGetPropS(IncomeData, "MasterDataId");
         if (ValType(aux_buf) != V_UNDEF)
            MasterDataId.ObjectId = uTryToGetPropS(aux_buf, "ObjectId");
            MasterDataId.SystemId = uTryToGetPropS(aux_buf, "SystemId");
            MasterDataId.SystemNodeId = uTryToGetPropS(aux_buf, "SystemNodeId");
         end;
         aux_buf = uTryToGetPropS(IncomeData, "LEXRef");
         if (ValType(aux_buf) != V_UNDEF)
            aux_buf = uTryToGetPropS(IncomeData.LEXRef, "OperationalDataId");
            LEXRef.OperationalDataId.ObjectId = uTryToGetPropS(aux_buf, "ObjectId");
            LEXRef.OperationalDataId.SystemId = uTryToGetPropS(aux_buf, "SystemId");
            LEXRef.OperationalDataId.SystemNodeId = uTryToGetPropS(aux_buf, "SystemNodeId");
         end;
      end;
   end;
   
   uInitPrime(IncomeData); 
      
end;

/* Корневой элемент ответа - AS IS - как пришел */
private class adp_PersonGetXRefLEResp(IncomeData)
   private class c_ResultData(IncomeData)
   	class c_Error
      		var ErrorCode :String;  // Код результата       Нет      Идентификатор
      		var ErrorDesc :String;  // Поясняющий текст     Нет
	end;
	

      var ResultCode : string;
      var ErrorData = c_Error;

      private macro uInitPrime(IncomeData)
         var aux_buff;
         ResultCode = uTryToGetPropS(IncomeData, "ResultCode");
         aux_buff = uTryToGetPropS(IncomeData, "ErrorData");
         if (ValType(aux_buff) != V_UNDEF)
            ErrorData.ErrorCode = uTryToGetPropS(aux_buff, "ErrorCode");
            ErrorData.ErrorDesc = uTryToGetPropS(aux_buff, "ErrorDesc");
         end;
      end;
      
      uInitPrime(IncomeData);
   end;
  
   var ResultData = c_ResultData;      // Контейнер с информацией по результату обработки
   
   /* Кратность параметров в соответствии со схемой/спецификацией */
   private macro uInitPrime(IncomeData)
      var err_txt;
      var aux_buff;

      if(ValType(IncomeData) != V_UNDEF)
         aux_buff = uTryToGetPropS(IncomeData, "ResultData");
         if(ValType(aux_buff) == V_GENOBJ)
            ResultData = c_ResultData(aux_buff);
         end;  
      end;
   end;
   
   uInitPrime(IncomeData);
end;

/* Основной класс работы с сервисом */
private class c_PersonCreateXrefLEProcessor(IncomeData)
   var PersonCreateXrefLEReq; /* !!! По названию корневого тега запроса !!! */
   PersonCreateXrefLEReq = c_PersonCreateXrefLEReq(IncomeData);
end; 

/*-------------------------------------------------------------------------*/
/* Адаптер для вызова сервиса "Запрос на создание кросс-ссылки клиента ЮЛ" */
/*-------------------------------------------------------------------------*/
macro run_PersonCreateXrefLE(ReqData, p_log_id, p_transparent_id)
   var RespData = NULL;
   var err_txt = NULL;
   
   private var v_out_obj;
   private var RequestText;
   private var v_transformator;
   private var v_answer;
   private var v_answer_data;
   private var v_logger_obj;
   private var v_log_id;
   private var xml_str;
   private var v_xml_rpc_reqid = XmlRpcRequestId(); /* 20181229 - kva - чтобы дважды не вызывать функцию */

   if ( (ReqData == NULL) or (ValType(ReqData) == V_UNDEF) )
      return null;
   end;

   v_transformator = c_secur_msg_converter();

   // заполнение класса информацией 
   v_out_obj = c_PersonCreateXrefLEProcessor(ReqData);
   RequestText = v_transformator.m_secur_internal_resp(v_out_obj);
   
   v_logger_obj = uws_exchng_logger(null, p_transparent_id, v_xml_rpc_reqid, "PersonCreateXrefLE", modulename(), null, RequestText);
   v_log_id = v_logger_obj.get_log_id();

      
   err_txt = v_transformator.m_validate_out_xml(RequestText);
   v_transformator = NULL;
            
   if (err_txt == NULL)
      v_answer = SubmitRequestToWS( 2,              // ИД очереди.
                                    "",             // ИД сообщения.
                                    "",             // Идентификатор Бэк Офиса. (не обрабатывается).
                                    true,           // Признак синхронной обработки. Если не указан, = False
                                    "SOFR",         // Подразделение системы-назначения.
                                    13,             // ИД типа данных
                                    RequestText,    // Бизнес данные в виде XML.
                                    v_log_id,       // ID usr_exchng_log
                                    "CIFLE"
                                    );

         if (v_answer.ResultCode == 0)
            xml_str = v_answer.responsetext;
            v_logger_obj.add_response(xml_str); 
            
            v_transformator = NULL;
            
            /* Преобразуем запрос в объектный вид - begin */
            v_transformator = c_secur_msg_converter();
            if(not v_transformator.m_decode_escaped_char(xml_str))
               RunError(v_transformator.get_service_msg(), c_usr_err_obj(v_transformator.get_service_msg()));
            end;
            v_answer_data = v_transformator.m_secur_wol_internal_req(v_transformator.get_pure_msg());
            /* Преобразуем запрос в объектный вид - end */

            if(ValType(v_answer_data) == V_UNDEF)
               err_txt = "Не удалось преобразовать XML в объект";
               RunError(err_txt, c_usr_err_obj(err_txt)); 
            end;
            
            //RespData = adp_PersonGetXRefLEResp(v_answer_data);
            /* Преобразование объекта к виду, ожидаемому Системой */
         else 
            v_logger_obj.add_error_info(v_answer.ResultCode, v_answer.ResultText);
         end;
   else
      v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, err_txt);
   end;
         
   return RespData;

onError(err)
   return null;
end; 

/*---------------------------------------*/
/*              Точка входа              */
/*---------------------------------------*/
/* ReqData - данные для отправки         */
/*---------------------------------------*/
macro PersonCreateXrefLE (ReqData, in_log_id, in_transparent_id) /* 20190103 - kva - добавлены параметры логирования */
   return run_PersonCreateXrefLE(ReqData, in_log_id, in_transparent_id); /* 20190103 - kva - добавлены параметры логирования */
end;

