/*-----------------------------------------------------*/
/* Интерфейс кодовых слов для Брокерского обслуживания */
/*                                                     */
/* Автор: Карпов В.                                    */
/*-----------------------------------------------------*/
/* РСХБ                                                */
/* |- БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ                          */
/*    |- РАБОТНИК ДРРК-БРОКЕР                          */
/*       |- ID РОЛИ                                    */
/*-----------------------------------------------------*/

import rsd;
import "usr_key_const.mac";
import "int_usr_globals.mac";
import "global_utils_intgr.mac"; /* Константы */
import globals; /* Для {oper} */
import "PartyProxyScroll.mac";
import "int_code_word_filter.mac";
import "dlutils.mac";


/*-----------------------------------*/
/* Глобальные переменные и константы */
/*-----------------------------------*/
private const GC_DRRK_ROLE_ID_PATH = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\РАБОТНИК ДРРК-БРОКЕР\\ID РОЛИ";
private const GC_SCROL_RECORDS_LIMIT_PATH = "РСХБ\\ИНТЕГРАЦИЯ\\СКРОЛ.КОД.СЛОВ ЛИМИТ ЗАПИСЕЙ";
private const K_ALTY = 277;
private const K_CTRLALTF4 = 765;

private var gv_drrk_role_id; /* ID роли, наличие которой разрешает работу с интерфейсом */
private var gv_oper_role_list = TArray(); /* Список ролей текущего пользователя */

private var gv_input_prm_val;
private var gv_code_word_type;
private var gv_main_scr_upd_f;
private var gv_show_all_subj;

private var gv_column_list_scr_main = TArray(); /* Список колонок основного скроллинга */
private var gv_num_columns_scr_main = 0;        /* Количество колонок основного скроллинга */
private var gv_scr_focus_main = 0;              /* Номер позиции основного скроллинга */

private var gv_column_list_scr_hist = TArray(); /* Список колонок скроллинга истории Кодовых слов */
private var gv_num_columns_scr_hist = 0;        /* Количество колонок скроллинга истории Кодовых слов */
private var gv_scr_focus_hist = 0;              /* Номер позиции скроллинга истории Кодовых слов */
private var gv_client_id_hist = 0;              /* ID Клиента для построения скроллинга истории Кодовых слов */

private var gv_for_subject_mode = false;        /* Работаем в режиме из списка субъектов - запрешены некоторые действия */
private var gv_filter_params = CCodewordFilterParams(); /* Параметры фильтра скроллинга */
private var gv_disable_default_search = false;  /* Не использовать встроенный в инструмент поиск по F4 */

/*----------------------------------------------------*/
/* Определение идентификаторов пользовательских ролей */
/*----------------------------------------------------*/
private macro m_find_roles_ids(p_oper)
   var v_ret = true;
   var v_stat = 0;
   var v_sql, v_cmd, v_rs;

   gv_drrk_role_id = 0;
   gv_oper_role_list.size = 0;

   /* Определение ID роли, наличие которой разрешает работу с интерфейсом */
   GetRegistryValue(GC_DRRK_ROLE_ID_PATH, V_INTEGER, gv_drrk_role_id, v_stat);
   if(v_stat > 0)
      /* Значение по умолчанию */
      gv_drrk_role_id = 0;
   end;

   /* Определяем список ролей пользователя */
   v_sql =         "SELECT t_roleid, t_oper ";
   v_sql = v_sql + "  FROM dacsoprole_dbt ";
   v_sql = v_sql + " WHERE t_oper = :p_oper ";
   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_oper", RSDBP_IN, p_oper);
   v_rs = RSDRecordSet(v_cmd);

   while(v_rs.movenext())
      gv_oper_role_list.value(gv_oper_role_list.size) = v_rs.value("t_roleid");
   end;

   v_rs.close(); v_cmd.close(); v_rs = NULL; v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end; /* macro m_find_roles_ids() */


/*-------------------------------------------------------------*/
/* Проверка наличия у пользователя роли "Работник ДРРК-Брокер" */
/*-------------------------------------------------------------*/
private macro m_check_drrk_role(p_find_ids, p_oper)
   var v_ret = false;
   var v_find_ids;
   var v_oper;
   var v_aux_cntr;

   if(ValType(p_find_ids) == V_UNDEF)
      v_find_ids = false;
   else
      v_find_ids = p_find_ids;
   end;

   if(ValType(p_oper) == V_UNDEF)
      v_oper = {oper};
   else
      v_oper = p_oper;
   end;

   /* При необходимости, получаем исходные данные */
   if(v_find_ids)
      m_find_roles_ids(v_oper);
   end;

   /* Проверяем наличие роли у пользователя */
   v_aux_cntr = 0;
   while((v_aux_cntr < gv_oper_role_list.size) and (not v_ret))
      if(gv_oper_role_list.value(v_aux_cntr) == gv_drrk_role_id)
         v_ret = true;
      end;

      v_aux_cntr = v_aux_cntr + 1;
   end;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end; /* macro m_check_drrk_role() */


/*-----------------------------*/
/* Заблокировать Кодовое слово */
/*-----------------------------*/
private macro m_cw_block(p_id_to_block)
   var v_ret = 0;
   var v_sql, v_cmd;

   v_sql =         "BEGIN ";
   v_sql = v_sql + "    :p_result := USR_PKG_IMPORT_SOFR.f_block_code_word(:p_cw_id, :p_change_oper); ";
   v_sql = v_sql + "END; ";
   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_result", RSDBP_RETVAL, V_INTEGER);
   v_cmd.addParam("p_cw_client", RSDBP_IN, p_id_to_block);
   v_cmd.addParam("p_change_oper", RSDBP_IN, {oper});
   v_cmd.execute();

   v_ret = v_cmd.value("p_result");

   v_cmd.close(); v_cmd = null; v_sql = null;

   return v_ret;

onError(err)
   v_ret = 5;
   return v_ret;
end; /* macro m_cw_block() */



/*----------------------------------------------*/
/* Получить значение признака Скомпрометировано */
/*----------------------------------------------*/
private macro m_cw_get_compromised(p_id, p_compromised)
   var v_sql, v_cmd, v_rs;
   var v_ret = true;
   v_sql = " SELECT t_cw_compromised " +
             " FROM usr_clnt_cdwrd_hist_dbt " +
             " WHERE t_id = :p_id ";
   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_id", RSDBP_IN, p_id);
   v_rs = RSDRecordSet(v_cmd);
   v_ret = v_rs.movenext();
   if(v_ret)
      p_compromised = v_rs.Value("t_cw_compromised");
      setparm(1, p_compromised);
   end;
   v_rs.close(); v_cmd.close(); v_rs = NULL; v_cmd = NULL;
   return v_ret;
onError(err)
   v_rs = NULL; v_cmd = NULL;
   return false;
end; /* macro m_cw_get_compromised() */



/*----------------------------------------------*/
/* Установить / снять признак Скомпрометировано */
/*----------------------------------------------*/
private macro m_cw_set_compromised(p_id, p_reason)
   const reason_len = 500;
   var v_sql, v_cmd;
   v_sql = " UPDATE usr_clnt_cdwrd_hist_dbt " +
              " SET t_cw_compromised = CHR(88), "+
                  " t_cw_compromised_reason = :p_reason, "+
                  " t_change_oper = :p_oper, "+
                  " t_oper_chng_ts = SYSTIMESTAMP "+
             " WHERE t_id = :p_id ";
   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_reason", RSDBP_IN, substr(p_reason, 1, reason_len));
   v_cmd.addParam("p_oper", RSDBP_IN, {oper});
   v_cmd.addParam("p_id", RSDBP_IN, p_id);
   v_cmd.execute();
   return true;
onError(err)
   return false;
end; /* macro m_cw_set_compromised() */


/*----------------------------------------*/
/* Добавить новое значение Кодового слова */
/*----------------------------------------*/
private macro m_cw_add(p_client_id, p_code_word, p_oper)
   const CV_CW_TYPE = 2;

   var v_ret = true;
   var v_sql, v_cmd;

   v_sql =         "BEGIN ";
   v_sql = v_sql + "    :p_retsult := USR_PKG_IMPORT_SOFR.f_add_code_word(:p_cw_type, ";
   v_sql = v_sql + "                                                      :p_cw_client, ";
   v_sql = v_sql + "                                                      :p_code_word, ";
   v_sql = v_sql + "                                                      :p_man_oper); ";
   v_sql = v_sql + "END; ";
   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_result", RSDBP_RETVAL, V_INTEGER);
   v_cmd.addParam("p_cw_type", RSDBP_IN, CV_CW_TYPE);
   v_cmd.addParam("p_cw_client", RSDBP_IN, p_client_id);
   v_cmd.addParam("p_code_word", RSDBP_IN, p_code_word);
   v_cmd.addParam("p_man_oper", RSDBP_IN, p_oper);
   v_cmd.execute();

   if(v_cmd.value("p_result") != 0)
      v_ret = false;
   end;

   v_cmd.close(); v_cmd = null; v_sql = null;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end; /* macro m_cw_add() */


/*--------------------------------------------*/
/* Метод обработки событий скроллинга адресов */
/*--------------------------------------------*/
macro m_cw_hist_proc_scr(p_rs, p_cmd, p_id, p_key)
   var CM_FLAG = CM_DEFAULT;
   var v_aux_buff;
   var v_code_word;

   if(p_cmd == DLG_INIT)
      while((gv_scr_focus_hist != 0) and p_rs.movenext())
         if(p_rs.value("t_id") == gv_scr_focus_hist)
            gv_scr_focus_hist = 0;
            GoToScroll(p_rs);
         end;
      end;

   elif(p_cmd == DLG_KEY)
      if(p_key == K_ENTER)
         CM_FLAG = CM_IGNORE;

      elif(p_key == K_F5)
         /* Просто обновляем скроллинг */
         gv_scr_focus_hist = p_rs.value("t_id");
         CM_FLAG = CM_SELECT;

      elif(p_key == K_F8)
         if(p_rs.value("t_cw_is_active") == strfor(88))
            if(m_check_drrk_role(true))
               if(gettrue(true, "Заблокировать Кодовое слово?"))
                  v_aux_buff = m_cw_block(p_rs.value("t_id"));
                  if(v_aux_buff == 0)
                     CM_FLAG = CM_SELECT;
                  elif(v_aux_buff == 1)
                     msgbox("Выбранный код не является активным - блокировка невозможна");
                     CM_FLAG = CM_IGNORE;
                  elif(v_aux_buff == 2)
                     msgbox("Выбранная запись не является самой актуальной - блокировка не выполнена");
                     CM_FLAG = CM_IGNORE;
                  elif(v_aux_buff == 3)
                     msgbox("Выбранная запись не найдена - блокировка не выполнена");
                     CM_FLAG = CM_IGNORE;
                  elif((v_aux_buff == 4) or (v_aux_buff == 5))
                     msgbox("При блокировке записи возникла ошибка");
                     CM_FLAG = CM_IGNORE;
                  else
                     msgbox("Неизвестный код ошибки");
                     /* На всякий случай обновляем скроллинг */
                     CM_FLAG = CM_SELECT;
                  end;
               else
                  CM_FLAG = CM_IGNORE;
               end;
            else
               msgbox("У Вашей учетной записи нет прав доступа к операции блокировки Кодовых слов Клиентов.\nВыполнение блокировки невозможно.");
               CM_FLAG = CM_IGNORE;
            end;
         else
            msgbox("Выбранное Кодовое слово не активно.\nВыполнение блокировки невозможно.");
            CM_FLAG = CM_IGNORE;
         end;

      elif(p_key == K_F9)
         if(m_check_drrk_role(true))
            if(gettrue(true, "Вы хотите ввести новое значение Кодового слова?"))
               /* Получаем значение Кодового слова */
               getstring(v_code_word, "Введите значение кодового слова");
               v_code_word = trim(v_code_word);

               if(strlen(v_code_word) != 0)
                  if(not m_cw_add(gv_client_id_hist, v_code_word, {oper}))
                     msgbox("При сохранении значения Кодового слова возникли проблемы.\nНовое значение не сохранено.");
                     CM_FLAG = CM_IGNORE;
                  else
                     gv_main_scr_upd_f = true;
                     CM_FLAG = CM_SELECT;
                  end;
               else
                  msgbox("Значение Кодового слова не получено.\nНовое значение не сохранено.");
                  CM_FLAG = CM_IGNORE;
               end;
            else
               CM_FLAG = CM_IGNORE;
            end;
         else
            msgbox("У Вашей учетной записи нет прав доступа к операции ввода Кодовых слов Клиентов.\nВвод Кодового Слова невозможен.");
            CM_FLAG = CM_IGNORE;
         end;

      elif(p_key == K_ESCAPE)
         if(not gettrue(true, "Выйти из истории Кодовых слов?"))
            CM_FLAG = CM_IGNORE;
         end;
      end;

   end;

   return CM_FLAG;
end; /* macro m_cw_hist_proc_scr() */


/*-----------------------------------------------------------*/
/* Метод запуска скроллинга истории изменения Кодового слова */
/*-----------------------------------------------------------*/
private macro m_run_cw_hist_scroll(p_cw_type, p_client_id)
   private var sql_scr, cmd_scr, rs_scr;
   private var sql_main = "";
   private var sql_filtr = "";
   private var sql_order = "";

   gv_column_list_scr_hist.size = 0;
   gv_num_columns_scr_hist = 0;
   gv_scr_focus_hist = 0;
   gv_client_id_hist = p_client_id;


   sql_main =            "SELECT t_id, t_cw_type, t_cw_proc_type, t_code_word, ";
   sql_main = sql_main + "       t_cw_status, t_cw_accept_try, t_cw_is_active, ";
   sql_main = sql_main + "       t_cw_prev, t_change_oper, t_cw_client, t_manual_oper, ";
   sql_main = sql_main + "       TO_CHAR(t_cw_create_ts, 'DD.MM.YYYY HH24:MI:SS') cw_create_ts, ";
   sql_main = sql_main + "       TO_CHAR(t_cw_accept_ts, 'DD.MM.YYYY HH24:MI:SS') cw_accept_ts, ";
   sql_main = sql_main + "       TO_CHAR(t_oper_chng_ts, 'DD.MM.YYYY HH24:MI:SS') oper_chng_ts ";
   sql_main = sql_main + "  FROM usr_clnt_cdwrd_hist_dbt ";
   sql_main = sql_main + " WHERE t_cw_client = :p_cw_client ";
   sql_main = sql_main + "   AND t_cw_type = :p_cw_type ";
   sql_order = sql_order + "ORDER BY t_cw_create_ts DESC ";

   sql_scr = sql_main + sql_filtr + sql_order;

   cmd_scr = RSDCommand(sql_scr);
   cmd_scr.NullConversion = true;

   cmd_scr.addParam("p_cw_client", RSDBP_IN, p_client_id);
   cmd_scr.addParam("p_cw_type", RSDBP_IN, p_cw_type);

   rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   rs_scr.NullConversion = true;

   m_add_column(gv_column_list_scr_hist, gv_num_columns_scr_hist, "T_CW_IS_ACTIVE",  "Признак активного Кодового слова", 2); gv_num_columns_scr_hist = gv_num_columns_scr_hist + 1;
   m_add_column(gv_column_list_scr_hist, gv_num_columns_scr_hist, "T_ID",            "ID", 10); gv_num_columns_scr_hist = gv_num_columns_scr_hist + 1;
   m_add_column(gv_column_list_scr_hist, gv_num_columns_scr_hist, "T_CODE_WORD",     "Кодовое слово", 10); gv_num_columns_scr_hist = gv_num_columns_scr_hist + 1;
   m_add_column(gv_column_list_scr_hist, gv_num_columns_scr_hist, "CW_CREATE_TS",  "Дата и время установки Кодового слова", 15); gv_num_columns_scr_hist = gv_num_columns_scr_hist + 1;
   m_add_column(gv_column_list_scr_hist, gv_num_columns_scr_hist, "CW_ACCEPT_TS",  "Дата и время подтверждения Кодового слова", 15); gv_num_columns_scr_hist = gv_num_columns_scr_hist + 1;
   m_add_column(gv_column_list_scr_hist, gv_num_columns_scr_hist, "T_CW_ACCEPT_TRY", "Количество попыток подтверждения", 10); gv_num_columns_scr_hist = gv_num_columns_scr_hist + 1;
   m_add_column(gv_column_list_scr_hist, gv_num_columns_scr_hist, "T_CW_PROC_TYPE",  "Тип процесса", 10); gv_num_columns_scr_hist = gv_num_columns_scr_hist + 1;
   m_add_column(gv_column_list_scr_hist, gv_num_columns_scr_hist, "T_CW_STATUS",     "Статус", 10); gv_num_columns_scr_hist = gv_num_columns_scr_hist + 1;
   m_add_column(gv_column_list_scr_hist, gv_num_columns_scr_hist, "T_CW_PREV",       "ID предыдущего Кодового слова", 10); gv_num_columns_scr_hist = gv_num_columns_scr_hist + 1;
   m_add_column(gv_column_list_scr_hist, gv_num_columns_scr_hist, "T_CHANGE_OPER",   "Номер пользователя, изменившего статус", 5); gv_num_columns_scr_hist = gv_num_columns_scr_hist + 1;
   m_add_column(gv_column_list_scr_hist, gv_num_columns_scr_hist, "OPER_CHNG_TS",  "Дата и время изменения статуса", 15); gv_num_columns_scr_hist = gv_num_columns_scr_hist + 1;
   m_add_column(gv_column_list_scr_hist, gv_num_columns_scr_hist, "T_MANUAL_OPER", "Номер операциониста, осуществившего ручной ввод", 5); gv_num_columns_scr_hist = gv_num_columns_scr_hist + 1;

   while(RunScroll(rs_scr, gv_num_columns_scr_hist, gv_column_list_scr_hist, "cw_hist_list", "m_cw_hist_proc_scr", "История изменения кодового слова", "ESC - выход  F5 - обновить  F8 - блокировка  F9 - добавление значения", true, 7, 5, NULL, 40))
      /* Обновляем скроллинг */
      cmd_scr.close(); cmd_scr = NULL;
      rs_scr.close(); rs_scr = NULL;

      sql_filtr = "";

      sql_scr = sql_main + sql_filtr + sql_order;

      cmd_scr = RSDCommand(sql_scr);
      cmd_scr.NullConversion = true;

      cmd_scr.addParam("p_cw_client", RSDBP_IN, p_client_id);
      cmd_scr.addParam("p_cw_type", RSDBP_IN, p_cw_type);

      rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   end;

   rs_scr.close();

end; /* macro m_run_cw_hist_scroll() */


/*----------------------------------------------*/
/* Метод обработки событий основного скроллинга */
/*----------------------------------------------*/
macro m_code_word_proc_scr(p_rs, p_cmd, p_id, p_key)
   var CM_FLAG = CM_DEFAULT;

   if(p_cmd == DLG_INIT)
      gv_main_scr_upd_f = false;
      while((gv_scr_focus_main != 0) and p_rs.movenext())
         if(p_rs.value("t_dlcontrid") == gv_scr_focus_main)
            gv_scr_focus_main = 0;
            GoToScroll(p_rs);
         end;
      end;

   elif(p_cmd == DLG_KEY)
      if((p_key == K_SPACE) AND (NOT gv_for_subject_mode))
         if(m_check_drrk_role(true))
            var v_compromised = "";
            if (m_cw_get_compromised(p_rs.value("t_id"), v_compromised))
               if (v_compromised == strfor(88))
                  msgbox("Признак \"Скомпрометировано\" на данном кодовом слове уже установлен");
                  CM_FLAG = CM_IGNORE;
               else
                  if((gettrue(true, "Вы действительно хотите установить признак \"Скомпрометировано\" на данном кодовом слове?")))
                     /* Получаем значение причины */
                     var v_reason = "";
                     var v_ret = true;
                     while(v_ret AND (strlen(v_reason) == 0))
                        v_ret = getstring(v_reason, "Укажите причину компроментации кодового слова");
                        v_reason = trim(v_reason);
                        if(v_ret AND (strlen(v_reason) == 0))
                           msgbox("Поле обязательно для заполнения");
                        end;
                     end;
                     /* Устанавливаем признак */
                     if(v_ret)
                        if(strlen(v_reason) != 0)
                           if(not m_cw_set_compromised(p_rs.value("t_id"), v_reason))
                              msgbox("При сохранении возникли проблемы.\nПризнак не установлен.");
                              CM_FLAG = CM_IGNORE;
                           else
                              //gv_scr_focus_main = p_rs.value("t_dlcontrid");
                              //CM_FLAG = CM_SELECT;
                              CM_FLAG = CM_IGNORE;
                           end;
                        else
                           //Текст пуст?!
                           msgbox("Значение причины не получено.\nПризнак не установлен.");
                           CM_FLAG = CM_IGNORE;
                        end;
                     else
                        //Текст причины не получен - отказались от ввода причины
                        CM_FLAG = CM_IGNORE;
                     end;
                  else
                     //Отказались от установки признака
                     CM_FLAG = CM_IGNORE;
                  end;
               end;
            else
               msgbox("При определении текущего значения признака возникли проблемы.\nПризнак не установлен.");
               CM_FLAG = CM_IGNORE;
            end;
         else
            msgbox("У Вашей учетной записи нет прав доступа к операции.\nУстановка признака невозможена.");
            CM_FLAG = CM_IGNORE;
         end;
      elif(p_key == K_ENTER)

         /* Отображение истории изменения Кодового слова */
         m_run_cw_hist_scroll(gv_code_word_type, p_rs.value("t_partyid"));
         if(gv_main_scr_upd_f)
            gv_scr_focus_main = p_rs.value("t_dlcontrid");
            CM_FLAG = CM_SELECT;
         else
            CM_FLAG = CM_IGNORE;
         end;

      elif(p_key == K_F2)
         /* Просто обновляем скроллинг */
         gv_scr_focus_main = p_rs.value("t_dlcontrid");
         CM_FLAG = CM_SELECT;

      elif((p_key == K_F5) or
           (gv_disable_default_search and ((p_key == K_F4) or
                                           (p_key == K_CTRLALTF4))))
         /* Форма фильтра */
         if (CCodewordFilterPanel(gv_filter_params).Run() == K_F2)
           CM_FLAG = CM_SELECT;
         else
           CM_FLAG = CM_IGNORE;
         end;
         
      elif(p_key == K_F7)
         /* Переключаем режим отображения субъектов */
         gv_show_all_subj = not gv_show_all_subj;
         CM_FLAG = CM_SELECT;

      elif(p_key == K_CTRL_F9)
         /* Переключение к функционалу миграции данных */
         if(gettrue(true, "Перейти к загруженным из файла данным по Кодовым словам?"))
            gv_scr_focus_main = p_rs.value("t_dlcontrid");

            execmacrofile("code_word_brk_serv_conv", "m_run_migr_data_scroll");

            CM_FLAG = CM_SELECT;
         else
            CM_FLAG = CM_IGNORE;
         end;

      elif(p_key == K_ALTY)
        /* Показать список представителей субъекта */
        CPartyProxyScroll(p_rs.value("t_partyid")).Run();

      elif(p_key == K_ESCAPE)
         if(not gettrue(true, "Выйти из списка?"))
            CM_FLAG = CM_IGNORE;
         end;
      end;

   end;

   return CM_FLAG;
end; /* macro m_code_word_proc_scr() */


/*---------------------------------------*/
/* Метод, запускающий основной скроллинг */
/*---------------------------------------*/
private macro m_run_main_scroll(p_cw_type, p_client_id)
   private const CV_TYPE_BO_CW = 2;

   private var sql_scr, cmd_scr, rs_scr;
   private var sql_main = "";
   private var sql_filtr = "";
   private var sql_order = "";
   private var records_limit = GetRegValueOrDefValue(GC_SCROL_RECORDS_LIMIT_PATH, V_INTEGER, 500);
   private var sql_limit = "";
   private var sql_default_limit = "";
   
   gv_show_all_subj = false;

   gv_for_subject_mode = (ValType(p_client_id) != V_UNDEF);

   if(ValType(p_cw_type) != V_UNDEF)
      gv_code_word_type = p_cw_type;
   else
      gv_code_word_type = CV_TYPE_BO_CW;
   end;
   
   if (records_limit > 0)
     sql_default_limit = " WHERE rownum < " + records_limit;
     gv_disable_default_search = true;
     gv_filter_params.isValidCodesOnly = true;
   end;

   sql_main =            "     WITH cw_time AS (SELECT cd_1.* ";
   sql_main = sql_main + "                        FROM usr_clnt_cdwrd_hist_dbt cd_1 ";
   sql_main = sql_main + "             LEFT OUTER JOIN usr_clnt_cdwrd_hist_dbt cd_2 ON cd_1.t_cw_client = cd_2.t_cw_client ";
   sql_main = sql_main + "                                                         AND cd_1.t_cw_type = cd_2.t_cw_type ";
   sql_main = sql_main + "                                                         AND cd_1.t_cw_create_ts < cd_2.t_cw_create_ts ";
   sql_main = sql_main + "                       WHERE cd_1.t_cw_type = :p_cw_type ";
   /* На случай вызова из карточки Клиента */
   if(ValType(p_client_id) != V_UNDEF)
      sql_main = sql_main + "                      AND cd_1.t_cw_client = :p_client_id_1 ";
   end;
   sql_main = sql_main + "                         AND cd_2.t_cw_client IS NULL) ";
   sql_main = sql_main + "  SELECT subq.* FROM (";
   sql_main = sql_main + "   SELECT dbo_cntr.t_dlcontrid, sf_cntr.t_datebegin, sf_cntr.t_dateclose, ";
   sql_main = sql_main + "          sf_cntr.t_number, mp_cntr.t_mpcode, sf_cntr.t_partyid, ";
   sql_main = sql_main + "          cl_cntr.t_name, dep_cntr.t_name department, cw_time.t_id, cl_usc.t_code, ";
   sql_main = sql_main + "          cw_time.t_cw_type, cw_time.t_code_word, cw_time.t_cw_is_active, ";
   sql_main = sql_main + "          (NVL(cl_cont_3.t_value, '') || ' / ' || NVL(cl_cont_4.t_value, '')) clnt_email, ";
   sql_main = sql_main + "          (NVL(cl_cont_2.t_value, '') || ' / ' || NVL(cl_cont_1.t_value, ''))  mobile_phone, ";
   sql_main = sql_main + "          TO_CHAR(cw_time.t_cw_create_ts, 'DD.MM.YYYY') cw_cr_date, ";
   sql_main = sql_main + "          TO_CHAR(cw_time.t_cw_create_ts, 'HH24:MI:SS') cw_cr_time, ";
   sql_main = sql_main + "          TO_CHAR(cw_time.t_cw_accept_ts, 'DD.MM.YYYY') cw_ac_date, ";
   sql_main = sql_main + "          TO_CHAR(cw_time.t_cw_accept_ts, 'HH24:MI:SS') cw_ac_time, ";
   sql_main = sql_main + "          TO_CHAR(cw_time.t_oper_chng_ts, 'DD.MM.YYYY HH24:MI:SS') cw_chng_dttm, ";
   sql_main = sql_main + "          cw_time.t_change_oper, prev_cw.t_code_word cw_prev, ";
   sql_main = sql_main + "          cl_persn.t_born, cl_persn.t_birsplase, idc_kind.t_name document_name, ";
   sql_main = sql_main + "          (NVL(cl_idc.t_paperseries, '') || ' ' || NVL(cl_idc.t_papernumber, '')) document_number, ";
   sql_main = sql_main + "          cl_idc.t_paperissueddate, cl_idc.t_paperissuer, cl_adr.t_adress, cw_time.t_cw_compromised, cw_time.t_cw_compromised_reason ";
   /* Площадки ДБО */
   sql_main = sql_main + "     FROM ddlcontrmp_dbt mp_cntr ";
   /* Договор обслуживания */
   sql_main = sql_main + "LEFT JOIN dsfcontr_dbt sf_cntr ON sf_cntr.t_id = mp_cntr.t_sfcontrid ";
   /* Договор брокерского обслуживания */
   sql_main = sql_main + "LEFT JOIN ddlcontr_dbt dbo_cntr ON dbo_cntr.t_dlcontrid = mp_cntr.t_dlcontrid ";
   /* Единый краткий код */
   sql_main = sql_main + "LEFT JOIN ddlobjcode_dbt cl_usc ON cl_usc.t_objecttype = 207 ";
   sql_main = sql_main + "                             AND cl_usc.t_codekind = 1 ";
   sql_main = sql_main + "                             AND cl_usc.t_objectid = dbo_cntr.t_dlcontrid ";
   sql_main = sql_main + "                             AND cl_usc.t_BankCloseDate = TO_DATE('01010001', 'DDMMYYYY') ";
   /* Основные данные Клиента */
   sql_main = sql_main + "LEFT JOIN dparty_dbt cl_cntr ON cl_cntr.t_partyid = sf_cntr.t_partyid ";
   sql_main = sql_main + "LEFT JOIN dpersn_dbt cl_persn ON cl_persn.t_personid = sf_cntr.t_partyid ";
   sql_main = sql_main + "LEFT JOIN dpersnidc_dbt cl_idc ON cl_idc.t_personid = sf_cntr.t_partyid ";
   sql_main = sql_main + "                              AND cl_idc.t_ismain = chr(88) ";
   /* Вид документа, удостоверяющего личность */
   sql_main = sql_main + "LEFT JOIN dpaprkind_dbt idc_kind ON idc_kind.t_paperkind = cl_idc.t_paperkind ";
   /* Два адреса, поскольку бывают случаи, когда есть оба */
   /* Номер мобильного телефона Клиента (1 адрес) */
   sql_main = sql_main + "LEFT JOIN dcontact_dbt cl_cont_1 ON cl_cont_1.t_partyid = sf_cntr.t_partyid ";
   sql_main = sql_main + "                                AND cl_cont_1.t_addresstype = 1 ";
   sql_main = sql_main + "                                AND cl_cont_1.t_ismain = chr(88) ";
   sql_main = sql_main + "                                AND cl_cont_1.t_contactkind = 1 ";
   sql_main = sql_main + "                                AND cl_cont_1.t_contacttype = 1 ";
   /* Новый тип Мобильного телефона */
   sql_main = sql_main + "LEFT JOIN dcontact_dbt cl_cont_2 ON cl_cont_2.t_partyid = sf_cntr.t_partyid ";
   sql_main = sql_main + "                                AND cl_cont_2.t_addresstype = 1 ";
   sql_main = sql_main + "                                AND cl_cont_2.t_ismain = chr(88) ";
   sql_main = sql_main + "                                AND cl_cont_2.t_contactkind = " + string(CV_CONTACT_KIND_PHONE);
   sql_main = sql_main + "                                AND cl_cont_2.t_contacttype = " + string(CV_CONTACT_TYPE_MOBILE);
   /* Новый тип e-mail */
   sql_main = sql_main + "LEFT JOIN dcontact_dbt cl_cont_3 ON cl_cont_3.t_partyid = sf_cntr.t_partyid ";
   sql_main = sql_main + "                                AND cl_cont_3.t_addresstype = 1 ";
   sql_main = sql_main + "                                AND cl_cont_3.t_ismain = chr(88) ";
   sql_main = sql_main + "                                AND cl_cont_3.t_contactkind = " + string(CV_CONTACT_KIND_EMAIL);
   sql_main = sql_main + "                                AND cl_cont_3.t_contacttype = " + string(CV_CONTACT_TYPE_REPORT);
   /* Старый тип e-mail */
   sql_main = sql_main + "LEFT JOIN dcontact_dbt cl_cont_4 ON cl_cont_4.t_partyid = sf_cntr.t_partyid ";
   sql_main = sql_main + "                                AND cl_cont_4.t_addresstype = 1 ";
   sql_main = sql_main + "                                AND cl_cont_4.t_ismain = chr(88) ";
   sql_main = sql_main + "                                AND cl_cont_4.t_contactkind = 5 ";
   sql_main = sql_main + "                                AND cl_cont_4.t_contacttype = 8 ";
   /* Адрес Клиента */
   sql_main = sql_main + "LEFT JOIN (SELECT adr.t_partyid, adr.t_adress, adr.t_type, ";
   sql_main = sql_main + "                  ROW_NUMBER() OVER (PARTITION BY adr.t_partyid ORDER BY DECODE(adr.t_type, 5, 0, adr.t_type) ASC) rn ";
   sql_main = sql_main + "             FROM dadress_dbt adr ";
   sql_main = sql_main + "            WHERE adr.t_type IN (5, 1, 2, 3) ";
   sql_main = sql_main + "          ) cl_adr ";
   sql_main = sql_main + "       ON cl_adr.t_partyid = sf_cntr.t_partyid ";
   sql_main = sql_main + "      AND cl_adr.rn = 1 ";
   /* Подразделение определяется по первым цифрам в номере договора */
   sql_main = sql_main + "LEFT JOIN ddp_dep_dbt dp_dep ON dp_dep.t_name = DECODE(INSTR(SUBSTR(sf_cntr.t_number, 1, 5), '/'), ";
   /* Если символ "/" отсутствует в номере договора (среди первых пяти символов), то договор относится к ГО */
   sql_main = sql_main + "                                                       0, '0000', ";
   /* Строку до "/" сначала дополняем нулями до двух символов слева, потом - до 4-х символов нулями справа */
   sql_main = sql_main + "                                                       RPAD(LPAD(SUBSTR(sf_cntr.t_number, 1, (INSTR(sf_cntr.t_number, '/') - 1)), 2, '0'), 4, '0')) ";
   /* Региональный филиал */
   sql_main = sql_main + "LEFT JOIN dparty_dbt dep_cntr ON dep_cntr.t_partyid = dp_dep.t_partyid ";
   /* Самое актуальное кодовое слово (по времени установки) */
   sql_main = sql_main + "LEFT JOIN cw_time ON cw_time.t_cw_client = sf_cntr.t_partyid ";
   /* Предыдущее кодовое слово */
   sql_main = sql_main + "LEFT JOIN usr_clnt_cdwrd_hist_dbt prev_cw ON prev_cw.t_id = cw_time.t_cw_prev ";
   /* На случай вызова из карточки Клиента */
   if(ValType(p_client_id) != V_UNDEF)
      sql_main = sql_main + "    WHERE sf_cntr.t_partyid = :p_client_id_2 ";
   end;
   /* Параметры ручного фильтра */
   sql_filtr = gv_filter_params.GetFilter();
   if (StrLen(sql_filtr) > 0)
     sql_filtr = " AND " + sql_filtr + " ";
   end;
   
   /* Фильтруем юр. лиц */
   if(not gv_show_all_subj)
      if(ValType(p_client_id) != V_UNDEF)
         sql_filtr = " AND cl_cntr.t_legalform <> 1 " + sql_filtr;
      else
         sql_filtr = " WHERE cl_cntr.t_legalform <> 1 " + sql_filtr;
      end;
   end;

   sql_order = sql_order + " ORDER BY cw_time.t_cw_create_ts DESC NULLS LAST ";//" sf_cntr.t_partyid ";

   sql_scr = sql_main + sql_filtr + sql_order + ") subq " + sql_default_limit;

   cmd_scr = RSDCommand(sql_scr);
   cmd_scr.NullConversion = true;

   cmd_scr.addParam("p_cw_type", RSDBP_IN, gv_code_word_type);
   /* На случай вызова из карточки Клиента */
   if(ValType(p_client_id) != V_UNDEF)
      cmd_scr.addParam("p_client_id_1", RSDBP_IN, p_client_id);
      cmd_scr.addParam("p_client_id_2", RSDBP_IN, p_client_id);
   end;

   rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   rs_scr.NullConversion = true;

   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_CODE",         "Уникальный код Клиента", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_NAME",         "ФИО Клиента", 20); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_CODE_WORD",    "Кодовое слово", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_MPCODE",       "Коды Клиента по площадкам", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_NUMBER",       "Номер договора БО", 15); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_DATEBEGIN",    "Дата начала действия договора", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_DATECLOSE",    "Дата окончания действия договора", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "DEPARTMENT",     "Наименование регионального филиала", 20); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "MOBILE_PHONE",   "Номер мобильного телефона Клиента (Договор БО / Контактный телефон)", 20); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "CLNT_EMAIL",     "Адрес эл. почты Клиента (Договор БО / Для отчетов)", 20); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_CW_IS_ACTIVE", "Признак активного кодового слова", 2); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "CW_CR_DATE",     "Дата установки кодового слова", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "CW_CR_TIME",     "Время установки кодового слова", 8); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "CW_PREV",        "Предыдущее кодовое слово", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "CW_AC_DATE",     "Дата валидации Клиентом", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "CW_AC_TIME",     "Время валидации Клиентом", 8); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_CHANGE_OPER",  "Номер пользователя, изменившего статус", 5); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "CW_CHNG_DTTM",   "Дата и время изменения статуса", 15); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_PARTYID",      "ID Клиента", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_BORN",         "Дата рождения", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "DOCUMENT_NAME",  "Документ, удостоверяющий личность", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "DOCUMENT_NUMBER","Номер документа, удостоверяющего личность", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_PAPERISSUER",  "Кем выдан документ, удостоверяющий личность", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_PAPERISSUEDDATE", "Когда выдан документ, удостоверяющий личность", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_BIRSPLASE",    "Место рождения", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_ADRESS",       "Адрес", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_CW_COMPROMISED", "Cкомпрометировано", 2); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_CW_COMPROMISED_REASON", "Причина компрометации", 10); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;

   var stat_line = "[ESC] - выход  [F2] - обновить  [F5] - фильтр  [F7] - показывать всех субъектов (вкл/выкл)  [CTRL + F9] - загрузка кодовых слов из файла  [ENTER] - история изменения кодового слова  [Alt-Y] - Представители";
   if (NOT gv_for_subject_mode)
     stat_line = String(stat_line, " [Space] - Cкомпрометировано");
   end;
   while(RunScroll(rs_scr, gv_num_columns_scr_main, gv_column_list_scr_main, "code_word", "m_code_word_proc_scr",
                   "Брокерское обслуживание. Кодовое слово", stat_line, true, 5, NULL, NULL, 40))
      /* Обновляем скроллинг */
      cmd_scr.close(); cmd_scr = NULL;
      rs_scr.close(); rs_scr = NULL;

      /* Параметры ручного фильтра */
      sql_filtr = gv_filter_params.GetFilter();
      sql_limit = sql_default_limit;
      
      if (StrLen(sql_filtr) > 0)
         sql_filtr = " AND " + sql_filtr + " ";

         if (gv_filter_params.IsHighLimiting())
           sql_limit = "";
         end;
      end;

      /* Фильтруем юр. лиц */
      if(not gv_show_all_subj)
         if(ValType(p_client_id) != V_UNDEF)
            sql_filtr = " AND cl_cntr.t_legalform <> 1 " + sql_filtr;
         else
            sql_filtr = " WHERE cl_cntr.t_legalform <> 1 " + sql_filtr;
         end;
      end;

      sql_scr = sql_main + sql_filtr + sql_order + ") subq " + sql_limit;

      cmd_scr = RSDCommand(sql_scr);
      cmd_scr.NullConversion = true;

      cmd_scr.addParam("p_cw_type", RSDBP_IN, gv_code_word_type);
      /* На случай вызова из карточки Клиента */
      if(ValType(p_client_id) != V_UNDEF)
         cmd_scr.addParam("p_client_id_1", RSDBP_IN, p_client_id);
         cmd_scr.addParam("p_client_id_2", RSDBP_IN, p_client_id);
      end;

      rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   end;

   rs_scr.close();

end; /* macro m_run_main_scroll() */


/*-------------*/
/* Точка входа */
/*-------------*/
m_find_roles_ids({oper});

GetCmdLineParm("p_start", gv_input_prm_val);

if(ValType(gv_input_prm_val) != V_UNDEF)
   if(gv_input_prm_val == "menu")
      if(m_check_drrk_role())
         gv_for_subject_mode = false;
         /* Запуск основного скроллинга */
         m_run_main_scroll();
      else
         msgbox("У Вашей учетной записи нет прав доступа к информации по Кодовым словам Клиентов");
      end;
   end;
end;

Exit(1);