/*---------------------------------*/
/* Сервис Подтверждение от Клиента */
/* Поток "A4"                      */
/*---------------------------------*/
/* РСХБ                            */
/* |- БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ      */
/*    |- ВЗАИМОДЕЙСТВИЕ С ДБО ФЛ   */
/*       |- ОТВЕТЫ                 */
/*          |- ТЕЛЕФОН ДЛЯ СПРАВОК */
/*---------------------------------*/

import "SendRegBrReq_lib.mac";
import "global_classes_adp.mac";
import "SendSMSAccept.mac"; /* Функционал отправки СМС (поток "A5") */
//import "global_utils_intgr.mac"; /* Функции общего назначения и константы */
import BankInter; /* GetRegistryValue() */


private const CV_ERROR_TECH = -10001;
private const CV_ERR_TECH = "Техническая проблема при выполнении запроса";


/**/
private class (c_SendRegBrResp) c_adp_SendRegBrResp(p_MessageId, p_ErrorCode, p_ErrorDesc)
   /*----------------------------------*/
   /* Метод добавления ошибки к списку */
   /*----------------------------------*/
   macro m_add_error_to_list(p_ErrorCode, p_ErrorDesc)
      var v_aux_buf;

      /* Код ошибки считаем первичным параметром */
      if(ValType(p_ErrorCode) != V_UNDEF)
         v_aux_buf = SendCustBrResp.ErrorList.size;
         SendCustBrResp.ErrorList.value(v_aux_buf) = c_Error;

         SendCustBrResp.ErrorList.value(v_aux_buf).ErrorCode = p_ErrorCode;

         if(ValType(p_ErrorDesc) != V_UNDEF)
            SendCustBrResp.ErrorList.value(v_aux_buf).ErrorDesc = p_ErrorDesc;
         else
            SendCustBrResp.ErrorList.value(v_aux_buf).ErrorDesc = "";
         end;
      end;
   end; /* macro m_add_error_to_list() */


   /*--------------------------------*/
   /* Метод добавления списка ошибок */
   /*--------------------------------*/
   macro m_add_error_list_to_list(p_err_list)
      var v_aux_cntr = 0;

      if(ValType(p_err_list) == V_GENOBJ)
         while(v_aux_cntr < p_err_list.size)
            m_add_error_to_list(p_err_list.value(v_aux_cntr).ErrorCode,
                                p_err_list.value(v_aux_cntr).ErrorDesc);

            v_aux_cntr = v_aux_cntr + 1;
         end;
      end;
   end; /* macro m_add_error_list_to_list() */


   /*-----------------------------------*/
   /* Метод "финализации" списка ошибок */
   /*-----------------------------------*/
   macro m_error_list_finalize()
      var v_ret = true;
      var v_aux_cntr = 0;

      if(SendCustBrResp.ErrorList.size == 0)
         m_add_error_to_list(0, "");
      end;

      while((v_ret) and (v_aux_cntr < SendCustBrResp.ErrorList.size))
         if(SendCustBrResp.ErrorList.value(v_aux_cntr).ErrorCode != 0)
            /* Если есть хоть одна ошибка в списке */
            v_ret = false;
         end;

         v_aux_cntr = v_aux_cntr + 1;
      end;

      return v_ret;
   end;


   /*-------------*/
   /* Конструктор */
   /*-------------*/
   private macro m_init_adp_SendRegBrResp(i_MessageId, i_ErrorCode, i_ErrorDesc)
      SendCustBrResp = c_SendCustBrResp();
      SendCustBrResp.ErrorList = TArray;
      SendCustBrResp.MassageId = c_IntegrSymId();

      if(ValType(i_MessageId) != V_UNDEF)
         SendCustBrResp.MassageDate = dttm(date(), time());
         SendCustBrResp.MassageId.ObjectId = i_MessageId;
      end;

      if(ValType(i_ErrorCode) != V_UNDEF)
         m_add_error_to_list(i_ErrorCode, i_ErrorDesc);
      end;
   end; /* macro m_init_adp_SendRegBrResp() */

   Initc_SendRegBrResp();

   m_init_adp_SendRegBrResp(p_MessageId, p_ErrorCode, p_ErrorDesc);
end; /* class c_adp_SendRegBrResp() */


/* Исходящий объект */
private class c_out_obj()
   var SendRegBrResp : object; /* c_adp_SendRegBrResp */
end;


/**/
private class (c_SendRegBrReq) c_adp_SendRegBrReq(p_income_data)
   private const CV_INFO_PHONE_NUM_PATH = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ВЗАИМОДЕЙСТВИЕ С ДБО ФЛ\\ОТВЕТЫ\\ТЕЛЕФОН ДЛЯ СПРАВОК";
   private const CV_SUP_PHONE_NUM = "8 (800) 100-40-40"; /* Значение по умолчанию для замены %SupPhoneNum% */
   private const CV_TEXT_TO_CHNG_PHONE = "%SupPhoneNum%";
   /* Время ожидания ответа на отправку СМС (секунды) */
   private const CV_TIMEOUT_SMS_SUBMIT = 60;
   /* Код ошибки */
   private const CV_ERROR_SIGN = 1; /* Значение для ErrorCode, говорящее о наличии ошибки */
   private const CV_ERROR_CLEAR = 0; /* Значение для ErrorCode, говорящее об отсутствии ошибки */

   /* Описание ошибки */
   private const CV_ERR_CLEAR = ""; /* Текст ошибки по умолчанию */
   private const CV_ERR_DEFAULT = ""; /* Если ничего конкретного нет, то будем возвращать этот текст */

   private var v_sup_phone_num; /* Телефон Банка для справок по Брокерскому обслуживанию (для замены %SupPhoneNum%) */

   private var v_error_code;
   private var v_error_desc;

   private var v_client_phone_for_sms; /* Номер телефона клиента для отправки СМС */

   private var v_sms_send_result;

   macro m_get_error_code()
      return v_error_code;
   end;

   macro m_get_error_desc()
      return v_error_desc;
   end;

   macro m_get_sms_send_result()
      return v_sms_send_result;
   end;


   /*-------------------------------------------------------*/
   /* Получение номера мобильного телефона для отправки СМС */
   /*-------------------------------------------------------*/
   macro m_get_phone_for_sms()
      const CV_ERR_PHONE = "Ошибка: SMS-код не может быть направлен. Для устранения ошибки следует обратиться в Банк по эл. почте (invest@rshb.ru) или телефону %SupPhoneNum%";
      var v_ret = true;
      var v_sql, v_cmd, v_rs;

      /* По регламенту Система должна работать с юр. адресами */
      /* Для единообразия используется следующий запрос, но, возможно, стоило бы его усовершенствовать */
      v_sql =         "SELECT t_value ";
      v_sql = v_sql + "  FROM dcontact_dbt ";
      v_sql = v_sql + " WHERE t_partyid = :p_client_id ";
      v_sql = v_sql + "   AND t_addresstype IN (0,1) "; // юр. адрес или не указан
      v_sql = v_sql + "   AND t_ismain = chr(88) ";     // основной
      v_sql = v_sql + "   AND t_contactkind = :p_contactkind ";
      v_sql = v_sql + "   AND t_contacttype = :p_contacttype ";
      v_sql = v_sql + "ORDER BY t_addresstype DESC ";
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_client_id", RSDBP_IN, ClientLogin);
      v_cmd.addParam("p_contactkind", RSDBP_IN, CV_CONTACT_KIND_PHONE); // телефон
      v_cmd.addParam("p_contacttype", RSDBP_IN, CV_CONTACT_TYPE_MOBILE); // мобильный
      v_rs = RSDRecordSet(v_cmd);

      /* Используем значение из первой строки выборки */
      if(v_rs.movenext())
         /* Считается, что номер телефона хранится в таблице в корректном виде */
         v_client_phone_for_sms = v_rs.value("t_value");
      else
         /* Номер телефона не найден - в ТЗ такой случай не предусмотрен */
         v_error_code = CV_ERROR_SIGN;
         v_error_desc = strsubst(CV_ERR_PHONE, CV_TEXT_TO_CHNG_PHONE, v_sup_phone_num);
         v_ret = false;
      end;

      v_rs.close(); v_cmd.close(); v_rs = NULL; v_cmd = NULL; v_sql = NULL;

      return v_ret;

   onError(err)
      /* Если что-то пошло не так, то описание ошибки не указываем */
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = strsubst(CV_ERR_PHONE, CV_TEXT_TO_CHNG_PHONE, v_sup_phone_num);
      v_ret = false;
      return v_ret;
   end; /* macro m_get_phone_for_sms() */


   /*-------------------------------------------*/
   /* Проверка наличия и статуса кодового слова */
   /*-------------------------------------------*/
   macro m_check_code_word()
      const CV_CW_TYPE = 2;
      var v_ret = true;
      var v_sql, v_cmd, v_rs;

      v_sql =         "SELECT t_code_word, t_cw_status, t_cw_type, t_cw_client ";
      v_sql = v_sql + "  FROM usr_clnt_cdwrd_buf_dbt ";
      v_sql = v_sql + " WHERE t_cw_type = :p_cw_type ";
      v_sql = v_sql + "   AND t_cw_client = :p_cw_client ";
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_cw_type", RSDBP_IN, CV_CW_TYPE);
      v_cmd.addParam("p_cw_client", RSDBP_IN, ClientLogin);
      v_rs = RSDRecordSet(v_cmd);

      /* Считаем, что если нашлась запись не в статусе 3, значит можно подтверждать по СМС */
      /* Статусы в буферной таблице могут быть только 1, 2 и 3 (теоретически) */
      if(v_rs.movenext())
         if(v_rs.value("t_cw_status") == 3)
            /* Удалять запись смысла никакого нет */
            v_error_code = CV_ERROR_SIGN;
            v_ret = false;
         end;
      else
         /* Такое в ТЗ не предусмотрено */
         v_error_code = CV_ERROR_SIGN;
         v_ret = false;
      end;

      v_rs.close(); v_cmd.close(); v_rs = NULL; v_cmd = NULL; v_sql = NULL;

      return v_ret;

   onError(err)
      /* Если что-то пошло не так, то описание ошибки не указываем */
      v_error_code = CV_ERROR_SIGN;
      v_ret = false;
      return v_ret;
   end; /* macro m_check_code_word() */


   /*----------------------*/
   /* Отправка СМС с кодом */
   /* Инициация потока A5  */
   /*----------------------*/
   macro m_submit_sms_code()
      private const CV_ERR_DEF = "Ошибка отправки кода подтверждения";

      var v_ret;
      var v_aux_cntr;

      v_sms_send_result = adp_sms_submit_init(MassageId.ObjectId,
                                              ClientLogin,
                                              v_client_phone_for_sms,
                                              RequestType.RecordCode,
                                              NULL,
                                              CV_TIMEOUT_SMS_SUBMIT); /* 20191029 - передаем значение времени ожидания */

      if(ValType(v_sms_send_result) == V_GENOBJ)
         v_ret = true;
         v_aux_cntr = 0;
         while((v_ret) and (v_aux_cntr < v_sms_send_result.size))
            if(v_sms_send_result.value(v_aux_cntr).ErrorCode != 0)
               /* Если есть хоть одна ошибка в списке */
               v_error_code = CV_ERROR_SIGN;
               v_error_desc = v_sms_send_result.value(v_aux_cntr).ErrorDesc;
               v_ret = false;
            end;

            v_aux_cntr = v_aux_cntr + 1;
         end;
      else
         v_error_code = CV_ERROR_SIGN;
         v_error_desc = CV_ERR_DEF;
         v_ret = false;
      end;

      return v_ret;

   onError(err)
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = CV_ERR_DEF;
      v_ret = false;
      return v_ret;
   end; /* macro m_submit_sms_code() */


   /*-----------------------------------*/
   /* Обновление статуса кодового слова */
   /*-----------------------------------*/
   macro m_update_code_word_stat()
      const CV_CW_TYPE = 2;
      const CV_CW_STAT_WAIT = 2;
      var v_ret = true;
      var v_sql, v_cmd;

      v_sql =         "UPDATE usr_clnt_cdwrd_buf_dbt ";
      v_sql = v_sql + "   SET t_cw_status = :p_cw_status ";
      v_sql = v_sql + " WHERE t_cw_type = :p_cw_type ";
      v_sql = v_sql + "   AND t_cw_client = :p_cw_client ";
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_cw_status", RSDBP_IN, CV_CW_STAT_WAIT);
      v_cmd.addParam("p_cw_type", RSDBP_IN, CV_CW_TYPE);
      v_cmd.addParam("p_cw_client", RSDBP_IN, ClientLogin);
      v_cmd.execute();

      v_cmd.close(); v_cmd = NULL; v_sql = NULL;

      return v_ret;

   onError(err)
      v_error_code = CV_ERROR_SIGN;
      v_ret = false;
      return v_ret;
   end; /* macro m_update_code_word_stat() */


   /*-------------*/
   /* Конструктор */
   /*-------------*/
   private macro m_init_adp_SendRegBrReq(i_income_data)
      var v_aux_buf;
      var v_stat;
      v_error_code = CV_ERROR_CLEAR;
      v_error_desc = CV_ERR_CLEAR;

      GetRegistryValue(CV_INFO_PHONE_NUM_PATH, V_INTEGER, v_sup_phone_num, v_stat);
      if(v_stat > 0)
         /* Значение по умолчанию */
         v_sup_phone_num = CV_SUP_PHONE_NUM;
      end;

      if(ValType(i_income_data) != V_UNDEF)
         MassageDate = uTryToGetPropS(i_income_data, "MassageDate");
         if(ValType(MassageDate) == V_UNDEF)
            RunError(CV_ERR_TECH, CV_ERROR_TECH);
         end;

         ClientLogin = uTryToGetPropS(i_income_data, "ClientLogin");
         if(ValType(ClientLogin) == V_UNDEF)
            RunError(CV_ERR_TECH, CV_ERROR_TECH);
         end;

         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "SMS"));
         if(v_aux_buf == V_GENOBJ)
            SMS = c_adp_IntegrDictRec(i_income_data.SMS);
         else
            RunError(CV_ERR_TECH, CV_ERROR_TECH);
         end;

         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "MassageId"));
         if(v_aux_buf == V_GENOBJ)
            MassageId = c_adp_IntegrSymId(i_income_data.MassageId);
         else
            RunError(CV_ERR_TECH, CV_ERROR_TECH);
         end;

         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "RequestType"));
         if(v_aux_buf == V_GENOBJ)
            RequestType = c_adp_IntegrDictRec(i_income_data.RequestType);
         else
            RunError(CV_ERR_TECH, CV_ERROR_TECH);
         end;

      end;

   onError(err);
      /* Что делать, если параметров не хватает в ТЗ не описано */
      /* В ТЗ есть указание, что состав параметров проверяется ДБО ФЛ */
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = CV_ERR_TECH;
   end;

   Initc_SendRegBrReq();

   m_init_adp_SendRegBrReq(p_income_data);
end; /* class c_adp_SendRegBrReq */


/*-----------------------------------------------*/
/* Точка входа в Сервис Подтверждения от Клиента */
/*-----------------------------------------------*/
macro SendRegBrReq(p_income_data)
   const CV_ERR_DEFAULT = ""; /* Если ничего конкретного нет, то будем возвращать этот текст */

   const CV_TRADE_TERM_ENTRY = 1; /* Тип "запрос на вход в торговые терминалы" */
   const CV_SET_CODE_WORD = 2; /* Тип "запрос на установку/смену Кодового слова" */

   var gv_message_id;
   var v_in_data_proc;
   var v_result_obj = c_out_obj();
   var v_check_stat = true; /* Флаг состояния процесса */

   /* На всякий случай сохраняем для формирования ответа */
   if(ValType(uTryToGetPropS(p_income_data, "MassageId")) == V_GENOBJ)
      gv_message_id = uTryToGetPropS(p_income_data.MassageId, "ObjectId");
   else
      RunError(CV_ERR_TECH, CV_ERROR_TECH);
   end;

   /* Проверка входящего объекта */
   v_in_data_proc = c_adp_SendRegBrReq(p_income_data);
   if(v_in_data_proc.m_get_error_code() != 0)
      v_check_stat = false;
   end;

   /* Поиск номера телефона клиента для отправки СМС */
   if(v_check_stat)
      v_check_stat = v_in_data_proc.m_get_phone_for_sms();
   end;

   if(v_check_stat)
      /* Отправка SMS с кодом для подтверждения генерации временного пароля для входа в торговые терминалы */
      if(p_income_data.RequestType.RecordCode == CV_TRADE_TERM_ENTRY)
         if(not v_in_data_proc.m_submit_sms_code())
            v_check_stat = false;
         end;
      /* Проверка наличия кодового слова и отправка SMS с кодом для его подтверждения */
      elif(p_income_data.RequestType.RecordCode == CV_SET_CODE_WORD)
         if(not v_in_data_proc.m_check_code_word())
            v_check_stat = false;
         else
            if(not v_in_data_proc.m_submit_sms_code())
               v_check_stat = false;
            else
               /* Если ошибок не было - обновляем статус кодового слова */
               v_in_data_proc.m_update_code_word_stat();
            end;
         end;
      else
         RunError(CV_ERR_TECH, CV_ERROR_TECH);
      end;
   end;

   if(v_check_stat)
      v_result_obj.SendRegBrResp = c_adp_SendRegBrResp(gv_message_id);
      v_result_obj.SendRegBrResp.m_add_error_list_to_list(v_in_data_proc.m_get_sms_send_result());
      /* Окончательно формируем список ошибок */
      v_result_obj.SendRegBrResp.m_error_list_finalize()
   else
      v_result_obj.SendRegBrResp = c_adp_SendRegBrResp(gv_message_id,
                                                       v_in_data_proc.m_get_error_code(),
                                                       v_in_data_proc.m_get_error_desc());
   end;

   return v_result_obj;

onError(err)
   v_result_obj.SendRegBrResp = NULL;
//   v_result_obj.SendRegBrResp = c_adp_SendRegBrResp(gv_message_id, 1, c_usr_err_obj.obtain_err_msg(err));
   /* Есть подозрение, что текст будет транслироваться Клиенту как есть */
   v_result_obj.SendRegBrResp = c_adp_SendRegBrResp(gv_message_id, 1, CV_ERR_DEFAULT);

   return v_result_obj;
end; /* macro SendRegBrReq() */