/** 
 @file CdiFindOrg.mac
 @brief Сервис поиска карточек ЮЛ в AC CDI
  
 Сервис поиска карточек ЮЛ в AC CDI. По входному PartyID делает запросы на поиск карточки клиента в CDI.
   
 # tag 
 - functional_block:Клиенты_Регистрация_на_бирже
 - code_type:API
 - CDI

   
 # changeLog 
 |date       |author         |tasks            |note                                                         
 |-----------|---------------|-----------------|--------------------------------
 |2023.09.15 |Фаршатов Г. К. |CCBO-5646        | Реализация сервиса                  
 |2023.12.27 |Гераськина Т.В.|DEF-59172        | Класс ответа для повторного вызова  
 |2024.01.10 |Фаршатов Г. К. |DEF-59434        | Считается, что если в СОФР заполнен ИНН 
 |           |               |                 | и он не совпадает с ИНН в МК, которая возвращается 
 |           |               |                 | в сообщении CdiFindOrg_resp, то такая МК из результатов 
 |           |               |                 | поиска исключается

*/ 
import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import "RequestWS.mac";                   /* Функция передачи запроса в Web Sphere */
import "uws_exchng_log.mac";              /* Функционал логирования */
import func_lib;
import global_classes;
import dlquery;
import "CDIOrg_lib.mac";

/**
 @brief Класс соответствующий типу данных Search в CdiFindOrgReq
 
 Класс необходим для создания объекта xml CdiFindOrgReq
*/
class c_CdiFindOrgReq_Search (FindData)
  var  ShortNameOrg: string;
  var  FullNameOrg: string;
  var  INN: string;
  var  KPP: string;
  var  KIO: string;
  var  OKPO: string;  
  var  OGRN: string; 
  var  PartyId: c_IntegrationSymbolicIdentifierXType;

  INN  = FindData.INN;
  KPP  = FindData.KPP;
  OKPO = FindData.OKPO;
  OGRN = FindData.OGRN;
  //KIO  = FindData.KIO;
  //FullNameOrg = FindData.FullNameOrg;
  PartyId = c_IntegrationSymbolicIdentifierXType();
end;

/**
 @brief Класс соответствующий типу данных Range в CdiFindOrgReq
 
 Класс необходим для создания объекта xml CdiFindOrgReq
*/
class c_CdiFindOrgReq_Range
  var  Count: integer;
end;

/**
 @brief Класс соответствующий типу данных CdiFindOrgReqType
 
 Класс необходим для создания объекта xml CdiFindOrgReq
*/
class c_CdiFindOrgReqType (FindData)
  var  Search: c_CdiFindOrgReq_Search;
  var  Range: c_CdiFindOrgReq_Range;
  Search = c_CdiFindOrgReq_Search (FindData);
  Range = c_CdiFindOrgReq_Range();
end;

/**
 @brief Класс хранения объекта CdiFindOrgReq
 
 Класс необходим для создания объекта xml CdiFindOrgReq
*/
private class c_CdiFindOrgReqProcessor(FindData)
   var CdiFindOrgReq;
   CdiFindOrgReq = c_CdiFindOrgReqType(FindData);
end; 

/**
 @brief Класс с данным для поиска
 
 Класс необходим для хранения всех необходимых данных для поиска в CDI
*/
class c_FindData
  var  FullNameOrg: string;
  var  INN:  string;
  var  KPP:  string;
  var  KIO:  string;
  var  OKPO: string;  
  var  OGRN: string; 
end;

/**
 @brief Класс унаследованный от c_FindData с добавлением текущего шага
 
 Класс необходим для поэтапного заполнения объекта поиска c_FindData
*/
class (c_FindData) c_FindDataS
  var Step;

  step = 1;
  Initc_FindData;
end;

/**
 @brief Класс унаследованный от c_FindData с добавлением ID субъекта в CDI
 
 Класс необходим для сохранения данных от CDI
*/
class (c_FindData) c_FindDataCDI
  var PartyIdCDI;
  
  Initc_FindData;
end;

/**
 @brief Поиск данных в SOFR
 @param[in] PartyId Идентификатор субъекта в SOFR
 @return заполненный  класс c_FindData данными из SOFR

 Процедура осуществляет поиск данных в SOFR 
 и заполняет ими класс c_FindData
*/
private macro FindDataParty (PartyId)
  var Query, cmd, data;
  var FindData = c_FindData();

  Query = " Select t_okpo, t_Name "
          "   from dparty_dbt "
          "  where t_partyId = :p_partyid ";

  cmd = rsdCommand( Query);
  
  cmd.AddParam("p_partyid", RSDBP_IN, PartyId);
    
  data = TRsbDataSet(cmd);
  
  if (data.moveNext())
    //FindData.FullNameOrg = data.Name;
    FindData.OKPO = data.okpo;
  else 
    return NULL;
  end;
  
  SplitFullINN(ПолучитьКодСубъекта(PartyID, PTCK_INN), FindData.INN, FindData.KPP);
  
  FindData.OGRN = ПолучитьКодСубъекта(PartyID, 27, null, 0);

  //FindData.KIO = ПолучитьКодСубъекта(PartyID, 62, null, 0);
  
  return FindData;
end;

/**
 @brief Процедура поэтапного заполнения буфера поиска
 @param[in] FindData класс c_FindData с данными по субъекту из SOFR
 @param[out] FindDataActual класс c_FindDataS заполняемый данными  в соответствии с текущим этапом поиска
 @param[in] FindTypeAdd флаг добавления данных 
                        если true то к предыдущему буферу добавляется значение текущего этапа, 
                        если false удаляет данные предыдущего этапа и заполняет буфер данными только текущего этапа
 @return если буфер заполнен новым значениями true, иначе false

 Заполняет класс поиска данными из SOFR в соответствии  с текущим этапом
*/
private macro getActualFindData (FindData, FindDataActual, FindTypeAdd)

  if(FindDataActual.step == 1)
    if ((ValType(FindData.INN) != V_UNDEF) and (FindData.INN != ""))
      FindDataActual.INN = FindData.INN;
      FindDataActual.KPP = FindData.KPP;
      FindDataActual.step = 2;
      return true;
    else
      FindDataActual.step = 2;
    end;
  end;
  
  if(FindDataActual.step == 2)
    if (FindTypeAdd == false)
      FindDataActual.INN  = NULL;
      FindDataActual.KPP  = NULL;    
    end;
    if ((ValType(FindData.KPP) != V_UNDEF) and (FindData.KPP != ""))
      FindDataActual.INN = FindData.INN;
      FindDataActual.step = 3;
      return true;
    else
      FindDataActual.step = 3;
    end;
  end;
  
  if(FindDataActual.step == 3)
    if (FindTypeAdd == false)
      FindDataActual.INN = NULL;    
    end;
    if ((ValType(FindData.OKPO) != V_UNDEF) and (FindData.OKPO != ""))
      FindDataActual.OKPO = FindData.OKPO;
      FindDataActual.step = 4;
      return true;
    else
      FindDataActual.step = 4;
    end;
  end;
  
  if(FindDataActual.step == 4)
    if (FindTypeAdd == false)
      FindDataActual.OKPO = NULL;    
    end;
    if ((ValType(FindData.OGRN) != V_UNDEF) and (FindData.OGRN != ""))
      FindDataActual.OGRN = FindData.OGRN;
      FindDataActual.step = 5;
      return true;
    else
      FindDataActual.step = 5;
    end;
  end;
  
  if(FindDataActual.step == 5)
    if (FindTypeAdd == false)
      FindDataActual.OGRN = NULL;    
    end;
    /*if ((ValType(FindData.KIO) != V_UNDEF) and (FindData.KIO != ""))
      FindDataActual.KIO = FindData.KIO;
      FindDataActual.step = 6;
      return true;
    else */
      FindDataActual.step = 6;
    //end;
  end;
  
  if(FindDataActual.step == 6)
    if (FindTypeAdd == false)
      FindDataActual.KIO = NULL;    
    end;
    /*if ((ValType(FindData.FullNameOrg) != V_UNDEF) and (FindData.FullNameOrg != ""))
      FindDataActual.FullNameOrg = FindData.FullNameOrg;
      FindDataActual.step = 7;
      return true;
    else*/
      FindDataActual.step = 7;
      return false;
    //end;
  end;
  
  return false;
end;

/**
 @brief Получение данных субъекта из ответа CDI в необходимом виде
 @param[in] RespData Объект данных из CDI
 @param[out] ErrorList Массив ошибок CDI 
 @return Массив объектов c_FindDataCDI с заполненными данными из CDI

 Получение данных субъекта из ответа CDI в виде массива данных поиска c_FindDataCDI
*/
private macro parseRespDate (RespData, INN, ErrorList)
  var PartyList = TArray;
  var err_txt = "";
  
  if(ValType(RespData) != V_UNDEF)
      var CdiFindOrg = uTryToGetPropS(RespData,"CdiFindOrg");
      var ErrorListResp = uTryToGetPropS(RespData,"ErrorList");
      if(ValType(CdiFindOrg) == V_GENOBJ)
        var PartyListResp = uTryToGetPropS(CdiFindOrg,"PartyList");
        var i = 0;
        var FindData;
        var ErrorData;

        if(ValType(PartyListResp) == V_GENOBJ)
          i = 0;
          while(PartyListResp.size > i)
            FindData = c_FindDataCDI();
            //FindData.FullNameOrg = getValueFromProperty(PartyListResp(i),"FullNameOrg");
            FindData.INN         = getValueFromProperty(PartyListResp(i),"INN");
            FindData.KPP         = getValueFromProperty(PartyListResp(i),"KPP");
            //FindData.KIO         = getValueFromProperty(PartyListResp(i),"KIO");
            FindData.OKPO        = getValueFromProperty(PartyListResp(i),"OKPO");
            FindData.OGRN        = getValueFromProperty(PartyListResp(i),"OGRN");    
            FindData.PartyIdCDI  = getValueFromProperty(PartyListResp(i).PartyID,"ObjectId");    
            
            if((FindData.INN == INN) or (INN == NULL) or (INN == ""))
              PartyList(i) = FindData;
            end;
            i = i + 1;
            FindData = NULL;
          end;
        end;
      elif (ValType(CdiFindOrg) == V_UNDEF)   // если субъект не найден, тег CdiFindOrg пустой
      else
        err_txt = string(InErrNotObj,"Ошибка парсинга объекта 'CdiFindOrg'");
        RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
      end;

      if( (ValType(ErrorListResp) == V_GENOBJ) and 
          (ValType(ErrorList) == V_GENOBJ) )
        i = 0;
        while(ErrorListResp.size > i)
          ErrorData = c_ErrorDataCDI();
          ErrorData.ErrorCode = getValueFromProperty(ErrorListResp(i),"ErrorCode");
          ErrorData.ErrorDesc = getValueFromProperty(ErrorListResp(i),"ErrorDesc");
            
          ErrorList(i) = ErrorData;
          i = i + 1;
          ErrorData = NULL;
        end;
      end;
    else
      err_txt = string(InErrNotObj,"Получен некорректный объект");
      RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
  end;
  
  return PartyList;
end;

/**
@brief Класс ответа на запрос CdiFind
*/
class c_FindData_answer
   var ErrorCode;
   var ErrorDesc;
   var RespData; //Массив объектов c_FindDataCDI с заполненными данными из CDI
end;


/**
 @brief Выполнение запроса поиска субъекта в CDI
 @param[in] FindData Объект поиска данных c_FindDataS
 @param[out] ErrorList Массив ошибок CDI 
 @param[in] TransperentID utableprocessevent.t_recid для связки в "Интерфейс таблицы событий"
 @return объект c_FindData_answer

 Выполнение запроса поиска субъекта в CDI по буферу FindData и парсинг ответа
*/
private macro makeCdiFindOrgReq(FindData, ErrorList, TransperentID, INN)
   const QUEUE_OUT = 2;
   const QUEUE_CdiFindOrgReq = GetNumDataType("CdiFindOrg");

   var RespData = c_FindData_answer;
   RespData.ErrorCode = UNIVERSAL_ERROR_CODE;
   RespData.ErrorDesc = "Непредвиденная ошибка";
   RespData.RespData = null;
   var err_txt  = NULL;
   
   private var v_out_obj;
   private var RequestText;
   private var v_transformator;
   private var v_answer;
   private var v_answer_data;
   private var v_logger_obj;
   private var v_log_id;
   private var xml_str;
   private var v_xml_rpc_reqid = XmlRpcRequestId(); 

   v_transformator = c_secur_msg_converter();

   v_out_obj = c_CdiFindOrgReqProcessor(FindData);
   RequestText = v_transformator.m_secur_internal_resp(v_out_obj);
   
   v_logger_obj = uws_exchng_logger(null, null, v_xml_rpc_reqid, "CdiFindOrg_req", modulename(), TransperentID, RequestText);
   v_log_id = v_logger_obj.get_log_id();

   err_txt = v_transformator.m_validate_out_xml(RequestText);
   if (err_txt == NULL)
      v_answer  = SubmitRequestToWS( QUEUE_OUT,   // ИД очереди.
                                     "",          // ИД сообщения.
                                     "",          // Идентификатор Бэк Офиса. (не обрабатывается).
                                     true,        // Признак синхронной обработки. Если не указан, = False
                                     "SOFR",      // Подразделение системы-отпр
                                     QUEUE_CdiFindOrgReq,          // ИД типа данных  
                                     RequestText, // Бизнес данные в виде XML
                                     v_log_id,    // ID usr_exchng_log
                                     "CDI",
                                     30
                                     ); 

      RespData.ErrorCode = v_answer.ResultCode; 
      RespData.ErrorDesc = v_answer.ResultText;
      RespData.RespData = null;

      if (v_answer.ResultCode == 0)
         xml_str = v_answer.responsetext;

         v_logger_obj.add_response(xml_str); 

         v_transformator = NULL;
         if (xml_str)
            /* Преобразуем запрос в объектный вид - begin */
            v_transformator = c_secur_msg_converter();
            if(not v_transformator.m_decode_escaped_char(xml_str))
               RunError(v_transformator.get_service_msg(), c_usr_err_obj(v_transformator.get_service_msg()));
            end;
            v_answer_data = v_transformator.m_secur_wol_internal_req(xml_str);
            /* Преобразуем запрос в объектный вид - end */

            if(ValType(v_answer_data) == V_UNDEF)
               err_txt = "Не удалось преобразовать XML в объект";
               RunError(err_txt, c_usr_err_obj(err_txt)); 
            end;

            RespData.RespData = parseRespDate(v_answer_data, INN, ErrorList); 
         end;  
      else 
         RespData.ErrorCode = 1005; // будем считать, что в любом случае, когда не получен ответ, стоит повторить  
         RespData.ErrorDesc = v_answer.ResultText;
         v_logger_obj.add_error_info(v_answer.ResultCode, v_answer.ResultText);
      end;

   else
      RespData.ErrorCode = UNIVERSAL_ERROR_CODE; 
      RespData.ErrorDesc = err_txt;
      RespData.RespData = null;
      v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, err_txt);
   end;
         
   return RespData;

onError(err)
   RespData.ErrorCode = c_err_obj.obtain_err_code(err); 
   RespData.ErrorDesc = c_err_obj.obtain_err_msg(err);
   RespData.RespData = NULL;
   return RespData;
end;

/**
@brief Класс ответа для run_CdiFindOrgReq 
*/
class c_CdiFindOrgResp
   var ErrorCode;
   var ErrorDesc;
   var PartyIdCDI; // В случае успешного поиска возвращает ID субъекта в CDI, иначе NULL 
end;


/**
 @brief Выполнение обработки сервиса CdiFindOrg
 @param[in] PartyObjId Идентификатор субъекта в SOFR
 @param[in] TransperentID utableprocessevent.t_recid для связки в "Интерфейс таблицы событий"
 @return объект c_CdiFindOrgReq

 Поиск данных в CDI по данным из SOFR
*/
macro run_CdiFindOrgReq(PartyObjId, TransperentID)
   var res = c_CdiFindOrgResp;

   if ( (PartyObjId == NULL) or (ValType(PartyObjId) == V_UNDEF) )
      res.ErrorCode = UNIVERSAL_ERROR_CODE;
      res.ErrorDesc = "Не заданы входящие параметры";
      res.PartyIdCDI = NULL;
      return res;
   end;

   //CCBO-10808 не все субъекты выгружаются
   if (CheckBlockUnloadCDI(PartyObjId)) 
      res.ErrorCode = ERROR_CODE_BLOCK_UNLOAD;
      res.ErrorDesc = ERROR_DESC_BLOCK_UNLOAD;
      res.PartyIdCDI = NULL;
      return res;
   end;
 
   var ErrorListResp = TArray;
   var PartyListResp = TArray;
   var FindDataS = c_FindDataS; //объект субъекта для поиска по этапами
   var FindDataSOFR  = NULL;    //объект субъекта заполненый данными SOFR
   var FindTypeAdd = true;
   var PartyListResp_answer = NULL;

   FindDataSOFR = FindDataParty(PartyObjId); //заполнение данными SOFR

   if(ValType(FindDataSOFR) != V_UNDEF)
       while (getActualFindData(FindDataSOFR,FindDataS,FindTypeAdd))
         PartyListResp_answer = makeCdiFindOrgReq(FindDataS, ErrorListResp, TransperentID, FindDataSOFR.INN);
         PartyListResp = PartyListResp_answer.RespData; //получили массив субъектов по заданному поиску
         FindTypeAdd = true;
         
         if (PartyListResp_answer.ErrorCode != 0) // ошибка при выполнении запроса, сразу выходим
             res.ErrorCode = PartyListResp_answer.ErrorCode;
             res.ErrorDesc = PartyListResp_answer.ErrorDesc;
             res.PartyIdCDI = NULL;
             return res;
         end;

         if ((ValType(PartyListResp) != V_UNDEF) and (PartyListResp.size > 0))
           if(PartyListResp.size == 1)
             res.ErrorCode = 0;
             res.ErrorDesc = "OK";
             res.PartyIdCDI = PartyListResp(0).PartyIdCDI;

             return res;
           end;
         else
           FindTypeAdd = false;
         end;    
       end; 
   else 
      res.ErrorCode = 0;
      res.ErrorDesc = "Не найдены данные для "+PartyObjId;
      res.PartyIdCDI = NULL;
      return res; 
   end;

   res.ErrorCode = 0;
   res.ErrorDesc = "Не найдены данные в CDI";
   res.PartyIdCDI = NULL;
   return res; 

onError(err)
   res.ErrorCode = 0; // ошибка есть, но повторять не будем 
   res.ErrorDesc = c_err_obj.obtain_err_msg(err);
   res.PartyIdCDI = NULL;
   return res;
end;

/**
 @brief Точка входа обработки сервиса CdiFindOrg
 @param[in] PartyObjId Идентификатор субъекта в SOFR
 @param[in] TransperentID utableprocessevent.t_recid для связки в "Интерфейс таблицы событий"
 @return В случаии успешного поиска возвращает ID субъекта в CDI, иначе NULL
*/
macro CdiFindOrgReq (PartyObjId, TransperentID) 
   return run_CdiFindOrgReq(PartyObjId, TransperentID); 
end;
