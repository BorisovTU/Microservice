/*
$Name:        SendAccreditationStatusReq.mac
$Module:      Интеграция с внешними системами
$Description: Запрос по статусу прохождения аккредитации
*/

/*-------------------------------------------------------------------------------*/
/* BIQ-9185                                                                      */
/* Автор: Гераськина Т.                                                          */
/*-------------------------------------------------------------------------------*/
import "func_lib.mac";
import "global_classes.mac";
import "dbo_qi_func.mac";

class c_Note
   var NoteKind : integer;
   var NoteText : integer;
   var NoteDate : date;
end;

// Результат рестирования
class c_Accreditation(IncomeData, nametag_up)
   var Date_acc   : datetime;    // Дата и время прохождения тестирования  Да
   var IsSuccess  : bool;        // Признак успешности прохождения анкеты  Да
   var Code       : string;      // Код пройденной анкеты                  Да
   var groupname  : string;
   var expired    : bool;
   
   private macro uInitPrime(IncomeData, nametag_up)
      Date_acc   = getValueFromProperty(IncomeData, "Date",       nametag_up, true);
      IsSuccess  = getValueFromProperty(IncomeData, "IsSuccess",  nametag_up, true);
      Code       = getValueFromProperty(IncomeData, "Code",       nametag_up, true);
   end; 

   uInitPrime(IncomeData, nametag_up);
end;

// Описание клиента и его шаблона
class c_ClientAccreditation(IncomeData, nametag_up)
   var TemplateName  : string;         // Наименование шаблона                            Да
   var TemplateDate  : dateTime;       // Дата и время заполнения шаблона                 Да
   var ClientId      ;                 // Идентификатор клиента в СОФР     Идентификатор  Да
   var AccreditationList;              // Список результатов тестирования  Контейнер      Нет
   
   private macro uInitPrime(IncomeData, nametag_up)
      var aux_buff, err_txt;
      var temp_a;
      TemplateName   = getValueFromProperty(IncomeData, "TemplateName", nametag_up, true);
      TemplateDate   = getValueFromProperty(IncomeData, "TemplateDate", nametag_up, true);
      ClientId       = getSymbolFromProperty(IncomeData, "ClientId", nametag_up, true);
      
      aux_buff = uTryToGetPropS(IncomeData, "AccreditationList");
      if(ValType(aux_buff) == V_GENOBJ)
         AccreditationList = TArray;
         aux_buff = 0;
         while(IncomeData.AccreditationList.size > aux_buff)
            temp_a = c_Accreditation(IncomeData.AccreditationList.value(aux_buff), nametag_up);
            AccreditationList.value(aux_buff) = temp_a;
            aux_buff = aux_buff + 1;
            temp_a = null;
         end;
      elif(ValType(aux_buff) == V_UNDEF)
      else
         err_txt = string(InErrNotObj, nametag_up+".AccreditationList");
         RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
      end;  
   end; 

   uInitPrime(IncomeData, nametag_up);
   
end;

// ручная установка примечаний
macro SetNotes(p_Objecttype, p_Ojectid, p_Note)
   var note;
   var state = 0;
   note = readNoteForObject( p_Objecttype, p_Ojectid, p_Note.NoteKind);
   if ( (not note) OR // пустое
        ( (note) and (note != p_Note.NoteText) ) )
      state = addNoteForObject( p_Objecttype, p_Ojectid, p_Note.NoteKind, p_Note.NoteText, p_Note.NoteDate);
   end;
   return state;
end;

class c_objgroup
   var groupid, 
       groupname;
end;


macro GetGroupParam(_objecttype, _groupid)
   var sql_str, rs, cmd;
   var res = c_objgroup;
   res.groupid = -1;
   sql_str = "select t_groupid, t_name, t_comment from dobjgroup_dbt where t_objecttype = :objecttype and t_groupid = :groupid";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("objecttype", RSDBP_IN, _objecttype);
   cmd.AddParam("groupid", RSDBP_IN, _groupid);
   rs = RSDRecordSet(cmd);
   if (rs.movenext) // индекс уникальный
      res.groupid = rs.value("t_groupid");
      res.groupname = StrFor(34)+rs.value("t_comment")+StrFor(34);
   end;
   return res;
end;

/**
@brief Проверка, сущесвует ли запись с более поздней датой аккредитации
@param[in] p_dlid ID договора
@param[in] p_temp_Accreditation Экземпляр класса c_Accreditation
@return 0 - не найдено/ >0 - найдено

Запросы по одному клиенту и одной категории могут поступить одновременно и обрабатываться не в том порядке, в каком происходило тестирование
*/
macro CheckAnotherRequest(p_dlid, p_temp_Accreditation)
   var sql_str, cmd, rs;
   var res = 0;
   sql_str = "select count(*) cnt from uqiaccreditation_dbt "+
             " where t_dlcontrid = :pdlid "+
             "   and t_accred_code = :pcode "+
             "   and t_accred_date > :pdate ";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("pdlid", RSDBP_IN, p_dlid);
   cmd.AddParam("pcode", RSDBP_IN, p_temp_Accreditation.Code);
   cmd.AddParam("pdate", RSDBP_IN, p_temp_Accreditation.Date_acc);
   rs = RSDRecordSet(cmd);
   if (rs.movenext())
      res = rs.value("cnt");
   end;

   return res;
end;

// непосредственная обработка единичной записи
// исключения обрабатываются в основном классе 
macro ProcessAccreditation(p_ClientAccreditation, p_transparent_id) 
   var v_partyid;    // заявлено, что в ClientId идентификатор СОФР ???
   var out_obj = c_Error, out_code;
   var err_txt;
   var ObjectType;
   var ObjectId;   
   var note_template = c_Note;
   var v_state = 0;
   var temp_Accreditation = NULL;
   
   var ObjCat;
   var IsAttr;
   var v_Groupid, v_catdate, v_catvalue, v_Group_param;

   var dlid;
   var g_repdate = date();
   var mess;
   var text_good = "", text_bad = "";
   var flag_good = false, flag_bad = false;
   var good_arr = TArray;
   var bad_arr = TArray;
   var ai_good, ai_bad;

   const C_FUNCID = 5072;
   
   //DEF-22517 gtv: передают partyid
/*   v_partyid = GetPartyID_byPartyCode(105, p_ClientAccreditation.ClientId.ObjectId);
   if (v_partyid == -1)
      err_txt = "Не найден субъект с кодом 105 = "+p_ClientAccreditation.ClientId.ObjectId;
      RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
   end;      */
   v_partyid = GetPartyID_byStringPartyID(p_ClientAccreditation.ClientId.ObjectId);
   if (v_partyid == -1)
      err_txt = "Не найден субъект с ID = "+p_ClientAccreditation.ClientId.ObjectId;
      RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
   end;

   ObjectType = OBJTYPE_PARTY;
   ObjectId = string(v_partyid:o:10);   

   note_template.NoteKind = NOTEKIND_TEMPLATE_QI;
   note_template.NoteText = p_ClientAccreditation.TemplateName;
   note_template.NoteDate = p_ClientAccreditation.TemplateDate;
   
   v_state = SetNotes(ObjectType, ObjectId, note_template);
   if (v_state != 0)
      err_txt = "Ошибка установки примечания "+note_template.NoteKind;
      RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
   end;

   ObjCat = RsbObjCategories( ObjectType, ObjectId);
   dlid = FindFirstContract(v_partyid, g_repdate);

   good_arr.size = 0;
   bad_arr.size = 0;
   ai_good = 0; ai_bad = 0;
   for (temp_Accreditation, p_ClientAccreditation.AccreditationList)
      if (ValType(temp_Accreditation.Code) != V_UNDEF)
         v_Groupid = Int(temp_Accreditation.Code);
         if (v_Groupid) // если там число
            v_Groupid = v_Groupid + 200;
            v_Group_param = GetGroupParam(ObjectType, v_Groupid);
            if (v_Group_param.groupid == -1)
               err_txt = "не найдена категория субъекта: "+v_Groupid;
               RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
            end;
         else
            err_txt = "Передано неизвестное значение Code="+temp_Accreditation.Code;
            RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
         end;
         
         v_catdate = date(temp_Accreditation.Date_acc);
         if (temp_Accreditation.IsSuccess)
            v_catvalue = 2; // Да
         else
            v_catvalue = 1; // Нет
         end;
   
         // все категории исключительные
         IsAttr = ObjCat.IsAttrPresense(v_Groupid, v_catvalue, NULL, NULL, true, v_catdate);
         if (IsAttr)
            // такая категория уже установлена в то же значение
         else
            //v_state = ObjCat.DisconnectAttr(v_Groupid, 0);
            v_state = CheckAnotherRequest(dlid, temp_Accreditation);
            if (v_state > 0) //существует запись с более поздним временем, значит не будем ни устанавливать категории, ни делать рассылку
               temp_Accreditation.expired = true;
               v_state = 0;
            else 
               temp_Accreditation.expired = false;
               v_state = ObjCat.ConnectAttr(v_Groupid, v_catvalue, NULL, NULL, v_catdate);
               if (v_state > 0)
                  err_txt = String("Ошибка "+v_state+" установки категории ",v_Groupid,", значение ",v_catvalue);
                  RunError(err_txt, c_err_obj(err_txt, ERR_CODE_TECH_ERROR));
               end;
            end;

            temp_Accreditation.groupname = v_Group_param.groupname;
            if (temp_Accreditation.IsSuccess)
               good_arr.value(ai_good) = temp_Accreditation;
               ai_good = ai_good+1;
            else
               bad_arr.value(ai_bad) = temp_Accreditation;
               ai_bad = ai_bad+1;
            end;

         end;
      end;
      temp_Accreditation = NULL;
   end;
   
   out_obj.ErrorCode = 0;
   out_obj.ErrorDesc = "OK";

   if (v_state == 0)
      mess = dboMessage_qi(dlid, TEST_QI);
      if (mess.statsend == 0) // сформировано без ошибок
         for (temp_Accreditation, good_arr)
            mess.FixUQIAccreditation(temp_Accreditation, p_transparent_id);
            if (mess.statfix != 0)
               v_state = -1;
               out_obj.ErrorCode = -1;
               out_obj.ErrorDesc = mess.fixmes;
            end;
            temp_Accreditation = NULL;
         end;
         for (temp_Accreditation, bad_arr)
            mess.FixUQIAccreditation(temp_Accreditation, p_transparent_id);
            if (mess.statfix != 0)
               v_state = -1;
               out_obj.ErrorCode = -1;
               out_obj.ErrorDesc = mess.fixmes;
            end;
            temp_Accreditation = NULL;
         end;
         if ( ( (bad_arr.size + good_arr.size) > 0 ) and (v_state == 0) )
           //DEF-28122 выносим формирование и отправку уведомления в отдельный процесс
           InsertSequenceJobUsrObjString(dlid, C_FUNCID, C_FUNCID, p_transparent_id, null);
         end;
      else 
         out_obj.ErrorCode = -1;
         out_obj.ErrorDesc = mess.statMes;
      end;
      mess = NULL;
   end;

   // категории сохраняем в последнюю очередь
   if(v_state == 0)
      v_state = ObjCat.Save(ObjectId);
   end;
   ObjCat = NULL;

 
   return out_obj;
   
onError(err)
   out_code = c_err_obj.obtain_err_code(err);
   err_txt = c_err_obj.obtain_err_msg(err);

   out_obj = c_Error;
   out_obj.ErrorCode = out_code;
   out_obj.ErrorDesc = err_txt;
   
   return out_obj;
end;

/*--------------------------------------------*/
/* Запрос по статусу прохождения аккредитации */
/*--------------------------------------------*/
macro adp_SendAccreditationStatusReq(IncomeData, p_log_id, p_transparent_id)
   var resp_data_obj = NULL;
   
   var err_txt="", out_code = 0;
   var aux_buff,temp_c;
   
   var ClientAccreditationList = NULL;
   var temp_ClientAccreditation = NULL;
   var res_arr = TArray;
   var res = NULL, ai = 0;
   
   if(ValType(IncomeData) == V_UNDEF)
      RunError(ERR_TEXT_NO_IN_MSG, c_err_obj(ERR_TEXT_NO_IN_MSG,
                                             ERR_CODE_NO_IN_PRM));
   end;   
         
   //готовый объект, преобразования вовне
   aux_buff = getValueFromProperty(IncomeData, "ClientAccreditationList", "SendAccreditationStatusReq", true);
   if(ValType(aux_buff) == V_GENOBJ)
      ClientAccreditationList = TArray;
      aux_buff = 0;
      while(IncomeData.ClientAccreditationList.size > aux_buff)
         temp_c = c_ClientAccreditation(IncomeData.ClientAccreditationList.value(aux_buff), "SendAccreditationStatusReq.ClientAccreditationList");
         ClientAccreditationList.value(aux_buff) = temp_c;
         aux_buff = aux_buff + 1;
         temp_c = null;
      end;
   else
      err_txt = string(InErrNotObj, "SendAccreditationStatusReq.ClientAccreditationList");
      RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
   end;
   
   ai = 0;
   for (temp_ClientAccreditation, ClientAccreditationList)
      res = ProcessAccreditation(temp_ClientAccreditation, p_transparent_id);
      res_arr.value(ai) = res;
      res = NULL;
      ai = ai+1;
   end;

   // итоговый класс 
   resp_data_obj = c_SendAccreditationStatus_outObj();
   resp_data_obj.SendAccreditationStatusResp.ErrorList = TArray;
   if (res_arr.size > 0)
      resp_data_obj.SendAccreditationStatusResp.ErrorList = res_arr;
   else
      resp_data_obj.SendAccreditationStatusResp.ErrorList.value(0) = c_Error;
      resp_data_obj.SendAccreditationStatusResp.ErrorList.value(0).ErrorCode = "-1";
      resp_data_obj.SendAccreditationStatusResp.ErrorList.value(0).ErrorDesc = "Нет данных";
   end;
   res_arr = NULL;
   
   return resp_data_obj;
   
onError(err)
   out_code = c_err_obj.obtain_err_code(err);
   err_txt = c_err_obj.obtain_err_msg(err);

   resp_data_obj = c_SendAccreditationStatus_outObj;

   resp_data_obj.SendAccreditationStatusResp.ErrorList = TArray;
   resp_data_obj.SendAccreditationStatusResp.ErrorList.value(0) = c_Error;
   resp_data_obj.SendAccreditationStatusResp.ErrorList.value(0).ErrorCode = out_code;
   resp_data_obj.SendAccreditationStatusResp.ErrorList.value(0).ErrorDesc = err_txt;
   
   return resp_data_obj;
end;

/*---------------------------------------*/
/* Информация по всем договорам и счетам */
/*---------------------------------------*/
macro SendAccreditationStatusReq(IncomeData, in_log_id, in_transparent_id) 
   var out_obj;
   out_obj = adp_SendAccreditationStatusReq(IncomeData, in_log_id, in_transparent_id); 
   return out_obj;
end;

