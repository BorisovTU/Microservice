/** 
 @file UpdateBrokContractResponseMsg.mac
 @brief Ответ на выгрузку статуса договора 
  
 Сервис выгрузки статуса договора в АБС 

 # tag 
 - functional_block:Клиенты_Регистрация_на_бирже
 - code_type:API


 # changeLog 
 |date       |author         |tasks            |note                            
 |-----------|---------------|-----------------|--------------------------------
 |2024.03.28 |Гераськина Т.В.|DEF-63987        | Синхронный сервис переделан в асинхронный, для синхронного нет оснований

*/ 

import "func_lib.mac";
import "global_utils_intgr.mac";
import "uws_exchng_log.mac";

private class c_Error(IncomeData)
   var ErrorCode : string;
   var ErrorDesc : string;

   private macro m_init_Error(IncomeData)
      private var nametag_up = "UpdateBrokContractResponse.ErrorList.Error";
      ErrorCode = getValueFromProperty(IncomeData, "ErrorCode", nametag_up);
      ErrorDesc = getValueFromProperty(IncomeData, "ErrorDesc", nametag_up);
   end;

   m_init_Error(IncomeData);
end;


private class c_UpdateBrokContractResponse(IncomeData)
   var ResultCode : String;
   var ErrorList;
   var Response;

   private macro m_init(IncomeData)
      private var err_txt, aux_buff, v_aux_buf, v_aux_cntr;
      private var nametag_up = "UpdateBrokContractResponse";

      if(ValType(IncomeData) != V_UNDEF)
         Response = getValueFromProperty(IncomeData, "UpdateBrokContractResponse" , nametag_up, true);
         if ( ValType(Response) == V_GENOBJ )
            ResultCode  = getValueFromProperty(Response, "ResultCode" , nametag_up, true);

            v_aux_buf = ValType(uTryToGetPropS(Response, "ErrorList"));
            if(v_aux_buf != V_UNDEF)
               if(v_aux_buf == V_GENOBJ)
                  v_aux_cntr = 0;
                  ErrorList = TArray;
                  while(Response.ErrorList.size > v_aux_cntr)
                     ErrorList.value(v_aux_cntr) = c_Error(Response.ErrorList.value(v_aux_cntr));
                     v_aux_cntr = v_aux_cntr + 1;
                  end;
               else
                  err_txt = string(InErrNotObj, "ErrorList");
                  RunError(err_txt, c_err_obj(err_txt,ERR_CODE_TECH_ERROR));
               end;
            end;
         end;
      end;

   end;

   m_init(IncomeData);
end;

private class (c_usr_tbl_base) c_usr_tbl_uTableProcessEvent()
   private class gen_uTableProcessEvent()
      var T_RECID :integer,
          T_TIMESTAMP :date,
          T_OBJECTTYPE :integer,
          T_OBJECTID :integer,
          T_TYPE :integer,
          T_STATUS :integer,
          T_NOTE :string,
          T_MESSAGEID :integer,
          T_RESULTCODE :string,
          T_RESULTTEXT :string,
          T_ATTEMPTS :integer;
   end;

   private macro init_table_obj()
      new_val_obj = gen_uTableProcessEvent();
      where_val_obj = gen_uTableProcessEvent();
   end;

   Initc_usr_tbl_base("uTableProcessEvent_dbt");

   init_table_obj();
end;


/**
 @brief Точка входа 
 @param[p_income_data] Входящий объект XMLRPC
 @param[p_log_id] usr_exchng_log_dbt.t_id
 
*/
macro UpdateBrokContractResponseMsg(p_income_data, p_log_id, p_jmsmessageid)
   var v_logger_obj;
   var v_income_data;
   var v_aux_buf;
   var v_cntr_err;
   
   var Reqid;
   var vresulttext= "", vresultcode; 
   var sql_str, cmd, rs;
   
   /* Получаем проверенный объект со входными данными */
   v_income_data = c_UpdateBrokContractResponse(p_income_data);

   v_logger_obj = uws_exchng_logger();
   v_logger_obj.set_log_id(p_log_id);

   if (StrUpr(v_income_data.ResultCode) == "0")  // успешно
      vresulttext = "OK";
      vresultcode = 0;
   else
      /* Описание ошибок */
      vresulttext = string("Получена ошибка: ",
                            v_income_data.ErrorList.value(0).ErrorCode,
                            ";",
                            v_income_data.ErrorList.value(0).ErrorDesc
                           );
      vresultcode = v_income_data.ErrorList.value(0).ErrorCode;
   end;

   // попытаемся найти исходный запрос
   var parent_eventid = 0, parent_ulog_id = 0, temp_str = "", pos = 0; 
   var NotFound_event = false;

   sql_str = "select l_in.t_jmsmessageid in_jmsmessageid, l_out.t_jmsmessageid out_jmsmessageid, l_out.t_jmscorrelationid user_id"+
             "  from dqueue_log_dbt l_in, "+
             "       dqueue_log_dbt l_out "+
             " where "+
             "     l_in.t_jmsmessageid = :p_jmsmessageid "+
             " and l_in.t_qname = 'ESB_TO_SOFR' "+
             " and l_out.t_jmsmessageid = l_in.t_jmscorrelationid "+
             " and rownum = 1; ";
   cmd = RSDCommand(sql_str);
   cmd.AddParam("p_jmsmessageid", RSDBP_IN, p_jmsmessageid);
   rs = RSDRecordSet(cmd);
   if (rs.movenext)
      temp_str = rs.value("user_id");
   else 
      temp_str = "";
   end;
   pos = Index(temp_str, "-");
   if (pos > 0)
      parent_eventid = Int(substr(temp_str,1,pos-1));
      parent_ulog_id = Int(substr(temp_str, pos+1));
   end;
   rs.close; cmd.close; rs = null; cmd = null;
   
   if (parent_eventid > 0)
      sql_str = "select count(1) cnt from utableprocessevent_dbt "+
                " where t_recid = :p_recid ";
      cmd = RSDCommand(sql_str);
      cmd.Addparam("p_recid", RSDBP_IN, parent_eventid);
      rs = RSDRecordSet(cmd);
      if (rs.movenext)
         if (rs.value("cnt") == 0)
            NotFound_event = true;
         else 
            var ProcessEventTbl = c_usr_tbl_uTableProcessEvent();
            ProcessEventTbl.New_Val_Obj.T_MESSAGEID = p_log_id;
            ProcessEventTbl.Where_Val_Obj.T_RECID = parent_eventid;
            if( not ProcessEventTbl.Update() )
               vresulttext = vresulttext + "\n"+ string("Не удалось обновить статус исходного события (",
                                                    parent_eventid,
                                                    ";",
                                                    parent_ulog_id,
                                                    ")");
            end;
            ProcessEventTbl = Null;

         end;
      end;
   else 
      NotFound_event = true;
   end;

   if (NotFound_event)
      vresulttext = vresulttext + "\n"+ "Невозможно найти исходное событие ";
   end;

   /* Сохраняем в логи */
   v_aux_buf = strlen(vresulttext);
   if(v_aux_buf > 0)
      if(v_aux_buf >= 2000)
         vresulttext = substr(vresulttext, 1, 1999);
      end;
      v_logger_obj.add_error_info(vresultcode, vresulttext);
   end;


   v_logger_obj = NULL;

   return true;

onError(err)
   if (ValType(v_logger_obj) == V_UNDEF)
      v_logger_obj = uws_exchng_logger();
      v_logger_obj.set_log_id(p_log_id);
   end;

   v_logger_obj.add_error_info(c_usr_err_obj.obtain_err_code(err), c_usr_err_obj.obtain_err_msg(err));

   return false;
end;