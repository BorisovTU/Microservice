/**
 @file KondorMonitoringNotify.mac
 @brief Функционал отправки уведомлений в сопровождение о состоянии загрузки и обработки сделок из АСУДР.
 
 Файл содержит функционал отправки уведомлений в сопровождение о состоянии загрузки и обработки сделок из АСУДР
 по e-mail.
 
  # tag
 - functional_block:
 - code_type:
 - АСУДР
 - КОНДОР
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |20.10.2024 |Топорков Д.В.|BIQ-16474 CCBO-10176 CCBO-10178                           | Создание
*/

import "u_common_email_utils.mac";
import bnk_common;
import "CbLogger2.mac";

private var supportEmail = Bnk_GetRegistryValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ИМПОРТ СДЕЛОК ИЗ АСУДР-КОНДОР\\EMAIL ДЛЯ ИНФОРМИРОВАНИЯ", V_STRING, "");

/**
@brief Процедура записи ошибки в itt_log
*/
private macro LogError(errorText: String)
  NewLogger(c_logger.IT_LOG_TYPE, "Kondor.Monitoring").Error(errorText);
end;

/**
@brief Функция подсчета кол-ва сделок пришедших из АСУДР, но не обработанных TOMCAT
@param[out] count Кол-во необработанных сделок
@return 0 В случае нормального завершения работы, -1 в случае ошибки
*/
private macro GetUnprocessedDealsCount(count: @Integer): Integer
  count = 0;
  
  var cmd = RsdCommand("SELECT COUNT(1) AS unprocessedCount " +
                         "FROM Kondor_SOFR_Buffer_dbt b1 " +
                        "WHERE (b1.t_DateComplete IS null " +
                            "OR b1.t_DateComplete = to_date('01.01.0001', 'DD.MM.YYYY')) " +
                          "AND (b1.t_ReqType != 'N' " +
                            "OR NOT EXISTS(SELECT 1 FROM Kondor_SOFR_Buffer_dbt b2 " +
                                           "WHERE b1.t_DealCode = b2.t_DealCode " +
                                             "AND b1.t_ReqType = b2.t_ReqType " +
                                             "AND b1.t_DateCreate < b2.t_DateCreate " +
                                             "AND b2.t_DateComplete > to_date('01.01.0001', 'DD.MM.YYYY'))) ");
  
  var rs = RSDRecordset(cmd);
  
  if (rs.MoveNext())
    count = SQL_ConvTypeInteger(rs.Value("unprocessedCount"));
  end;

  return 0;

OnError(err)
  LogError("Ошибка процедуры GetUnprocessedDealsCount: " + GetFullErrMsg(err));
  return -1;
end;

/**
@brief Функция подсчета среднего и максимального времени обработки сделок через TOMCAT
@param[in] dttmStart Начальная дата периода завершения обработки
@param[in] dttmEnd Конечная дата периода завершения обработки
@param[out] avgExecTime Среднее время обработки сделки за рассматриваемый период (в секундах)
@param[out] maxExecTime Максимальное время обработки сделки за рассматриваемый период (в секундах)
@return 0 В случае нормального завершения работы, -1 в случае ошибки
*/
private macro GetDealExecStat(dttmStart, dttmEnd, avgExecTime: @Integer, maxExecTime: @Integer): Integer
  avgExecTime = maxExecTime = 0;
  
  var cmd = RsdCommand("SELECT AVG((b.t_DateComplete - b.t_DateCreate) * 86400) AS avgTime, MAX((b.t_DateComplete - b.t_DateCreate) * 86400) AS maxTime " +
                         "FROM  Kondor_SOFR_Buffer_dbt b " +
                        "WHERE b.t_DateComplete >= :periodStart " +
                          "AND b.t_DateComplete < :periodEnd ");
  cmd.AddParam("periodStart", RSDBP_IN, dttmStart);
  cmd.AddParam("periodEnd", RSDBP_IN, dttmEnd);
  
  var rs = RSDRecordset(cmd);
  
  if (rs.MoveNext())
    avgExecTime = SQL_ConvTypeInteger(rs.Value("avgTime"));
    maxExecTime = SQL_ConvTypeInteger(rs.Value("maxTime"));
  end;

  return 0;

OnError(err)
  LogError("Ошибка процедуры GetDealExecStat: " + GetFullErrMsg(err));
  return -1;
end;

/**
@brief Функция подсчета количества сделок пришедшем из АСУДР за период
@param[in] dttmStart Начальная дата периода завершения обработки
@param[in] dttmEnd Конечная дата периода завершения обработки
@param[out] createdCount Количество сделок за период
@return 0 В случае нормального завершения работы, -1 в случае ошибки
*/
private macro GetTotalDealsCount(dttmStart, dttmEnd, createdCount: @Integer): Integer
  createdCount = 0;
  
  var cmd = RsdCommand("SELECT COUNT(1) as createdCount " +
                         "FROM Kondor_SOFR_Buffer_dbt b " +
                        "WHERE b.t_DateCreate >= :periodStart " +
                          "AND b.t_DateCreate < :periodEnd ");
  cmd.AddParam("periodStart", RSDBP_IN, dttmStart);
  cmd.AddParam("periodEnd", RSDBP_IN, dttmEnd);
  
  var rs = RSDRecordset(cmd);
  
  if (rs.MoveNext())
    createdCount = SQL_ConvTypeInteger(rs.Value("createdCount"));
  end;

  return 0;

OnError(err)
  LogError("Ошибка процедуры GetTotalDealsCount: " + GetFullErrMsg(err));
  return -1;
end;

/**
@brief Функция информирования сопровождения о загрузке и обработке сделок из АСУДР

Предполагается, что функция вызывается планировщиком с периодом 1 час
*/
macro KondorMonitoringNotify();
  if (StrLen(supportEmail) == 0)
    return;
  end;

  var currentDate: Date = Date();
  var hour: Integer;
  
  TimeSplit(Time(), hour);
  
  var dttmEnd = DtTm(currentDate, Time(hour, 0, 0, 0));
  var dttmStart;
  
  if (hour == 0)
    dttmStart = DtTm(currentDate - 1, Time(23, 0, 0, 0)); // с 23:00 до 00:00
  else
    dttmStart = DtTm(currentDate, Time(hour - 1, 0, 0, 0));
  end;
  
  var unprocessedDealsCount: Integer;
  if (GetUnprocessedDealsCount(@unprocessedDealsCount) == 0)
    SendEmailSynch("Уведомление о кол-ве сделок пришедших из АСУДР, но не обработанных TOMCAT",
                   "На текущий момент необработанно запросов из АСУДР: " + unprocessedDealsCount,
                   supportEmail);
  end;

  var avgExecTime: Integer;
  var maxExecTime: Integer;
  if (GetDealExecStat(dttmStart, dttmEnd, @avgExecTime, @maxExecTime) == 0)
    SendEmailSynch("Уведомление о среднем и максимальном времени обработки сделок через TOMCAT",
                   "Среднее время загрузки сделки из АСУДР - " + avgExecTime + "\n" +
                   "Максимальное время загрузки сделки из АСУДР - " + maxExecTime,
                   supportEmail);
  end;

  var totalDealsCount: Integer;
  if (GetTotalDealsCount(dttmStart, dttmEnd, @totalDealsCount) == 0)
    SendEmailSynch("Уведомление о кол-ве сделок пришедшем из АСУДР за час",
                   "За последний час пришло из АСУДР (кол-во сделок): " + totalDealsCount,
                   supportEmail);
  end;
end;

KondorMonitoringNotify();