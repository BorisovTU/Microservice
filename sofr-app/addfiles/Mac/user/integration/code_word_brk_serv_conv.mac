/*--------------------------------------------*/
/* Миграция данных по кодовым словам Клиентов */
/*--------------------------------------------*/
/* РСХБ                                       */
/* |- ИНТЕГРАЦИЯ                              */
/*    |- ДИРЕКТОРИИ                           */
/*       |- IMPORT                            */
/*          |- CD_WRD_SRV_BRK                 */
/*--------------------------------------------*/
import globals;
import rsd;
import "usr_key_const.mac";
import "int_usr_globals.mac";


private const GV_LOAD_FILE_PATH = "..\\Import\\CD_WRD_SRV_BRK\\CD_WRD_SRV_BRK.csv";

private var gv_col_list_scr_md = TArray(); /* Список колонок скроллинга данных миграции */
private var gv_num_col_scr_md = 0;         /* Количество колонок скроллинга данных миграции */
private var gv_scr_focus_md = 0;           /* Номер позиции скроллинга данных миграции */

private var v_suffics_list = TArray();
v_suffics_list.value(v_suffics_list.size) = "_ф";
v_suffics_list.value(v_suffics_list.size) = "_в";
v_suffics_list.value(v_suffics_list.size) = "_с";


/*-----------------------------------------*/
/* Удаление записей из таблицы конвертации */
/*-----------------------------------------*/
private macro m_delete_prev_conv()
   var v_ret = true;
   var v_sql, v_cmd;

   v_sql = "DELETE FROM usr_clnt_cdwrd_conv_dbt ";
   v_cmd = RSDCommand(v_sql);
   v_cmd.execute();

   v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end; /* macro m_delete_prev_conv() */


/*----------------------------------------------*/
/* Определение имени файла из его ПОЛНОГО имени */
/*----------------------------------------------*/
private macro m_extract_short_name(p_name_oper_file)
   var v_flag = true;

   p_name_oper_file = Trim(p_name_oper_file);
   while (v_flag)
      if(Index(p_name_oper_file,"\\") != 0)
         v_flag = true
      else
         v_flag = false
      end;
      if(v_flag)
         p_name_oper_file = SubStr(p_name_oper_file, Index(p_name_oper_file,"\\")+1)
      end
   end;
   return p_name_oper_file
end; /* macro m_extract_short_name() */


/*--------------------------------*/
/* Загрузка кодовых слов из файла */
/*--------------------------------*/
private macro m_load_code_word_serv_brk( p_file_name : string, p_set_dialog : integer ) :bool
   const CV_CD_WRD_SRV_BRK_PATH = "РСХБ\\ИНТЕГРАЦИЯ\\ДИРЕКТОРИИ\\IMPORT\\CD_WRD_SRV_BRK";

   var v_dsn :string = "";
   var v_user_id :string = "";
   var v_password :string = "";
   var v_connstring :string = GetIniString("CONNSTRING", "rsreq.ini");
   var v_string_sqlldr :string = "";
   var v_state :integer = 0;
   var v_ldr_param :string = "bindsize=100000000 readsize=1000000 columnarrayrows=10000000 rows=100000 errors=100000 ";
   var v_file_name_ctrlfile = ""; /* Имя управляющего файла для SQL*Loader */
   var v_log_file_name = "";
   var v_bad_file_name = "";
   var v_dsc_file_name = "";


   if( ( ValType(p_set_dialog) == V_UNDEF ) or ( ValType(p_set_dialog) == 26 ) )
      p_set_dialog = 0;
   end;

   if( ( ValType(p_file_name) == V_UNDEF ) or  ( ValType(p_file_name) == 26 ) or ( p_file_name == "" ) or ( FindPath( m_extract_short_name( p_file_name ), StrSubst( p_file_name, "\\" + m_extract_short_name(p_file_name), "" ) ) == "" ) )
      return false;
   end;

   /* Удаляем данные предыдущей загрузки */
   if(not m_delete_prev_conv())
      return false;
   end;

   v_dsn = SubStr(v_connstring, 5, index(v_connstring,";") - 5);
   v_connstring = SubStr(v_connstring, index(v_connstring,";") + 1);
   v_user_id = SubStr(v_connstring, index(v_connstring,"user id=") + 8, index(v_connstring,";") - 9);
   v_connstring = SubStr(v_connstring, index(v_connstring,";") + 1);
   v_password = SubStr(v_connstring, index(v_connstring,"password=") + 9);

   v_string_sqlldr = "userid=" + v_user_id + "/" + v_password + "@" + v_dsn;
   GetRegistryValue(CV_CD_WRD_SRV_BRK_PATH, V_STRING, v_file_name_ctrlfile);

   v_log_file_name = v_file_name_ctrlfile + "\\CTL_" + string(UserNumber) + ".log";
   v_bad_file_name = v_file_name_ctrlfile + "\\CTL_" + string(UserNumber) + ".bad";
   v_dsc_file_name = v_file_name_ctrlfile + "\\CTL_" + string(UserNumber) + ".dsc";
   v_file_name_ctrlfile = v_file_name_ctrlfile + "\\CTL_" + string(UserNumber) + ".ctl";

//   T_OPDATE DATE "MM/DD/YYYY HH24:MI:SS" NULLIF (T_OPDATE="NULL"),
//   T_DOCDATE DATE "MM/DD/YYYY HH24:MI:SS" NULLIF (T_DOCDATE="NULL"),

   SetOutput(v_file_name_ctrlfile); /* Создаем CONTROL-file для загрузки файла кодовых слов */
   [LOAD DATA CHARACTERSET CL8MSWIN1251
    INFILE *
    APPEND
    INTO TABLE usr_clnt_cdwrd_conv_dbt
    FIELDS TERMINATED BY ";"
    (
     T_CONTRACT_NUM "NVL(:T_CONTRACT_NUM, chr(1))",
     T_CNTR_OPEN_DATE,
     T_CLIENT_FIO "TRIM(NVL(:T_CLIENT_FIO, chr(1)))",
     T_CLIENT_NUM "TRIM(NVL(:T_CLIENT_NUM, chr(1)))",
     T_CODE_WORD "TRIM(NVL(:T_CODE_WORD, chr(1)))"
    )
    ];
   SetOutput(Null, True); 
   Close(v_file_name_ctrlfile);


   BegAction(Null, "Загружаем файл в RS-Bank...", False);

   if( p_set_dialog == 1 )
      v_state = Run("run_sqlldr.cmd", v_string_sqlldr + " control=" + v_file_name_ctrlfile +
                                                " data="    + p_file_name     +
                                                " log="     + v_log_file_name +
                                                " bad="     + v_bad_file_name +
                                                " discard=" + v_dsc_file_name +
                                                " skip=1"   +
                                                " SKIP_UNUSABLE_INDEXES=true" +
                                                " " + v_ldr_param);
   else
      v_state = StartProg("run_sqlldr.cmd", v_string_sqlldr + " control=" + v_file_name_ctrlfile +
                                                      " data="    + p_file_name     +
                                                      " log="     + v_log_file_name +
                                                      " bad="     + v_bad_file_name +
                                                      " discard=" + v_dsc_file_name +
                                                      " skip=1"   +
                                                      " SKIP_UNUSABLE_INDEXES=true" +
                                                      " " + v_ldr_param, false);
   end;

   EndAction(10);

   if(v_state != -1)
      return true;
   else
      return false;
   end;

end; /* macro m_load_code_word_serv_brk() */


/*---------------------------------------*/
/* Обновление статуса в таблице миграции */
/*---------------------------------------*/
private macro m_update_conv_row(p_id, p_status, p_status_msg, p_client_id)
   var v_ret = true;
   var v_sql, v_cmd;
   var v_aux_buf = ValType(p_client_id);

   v_sql =         "UPDATE usr_clnt_cdwrd_conv_dbt ";
   v_sql = v_sql + "   SET t_status = :p_status, ";
   v_sql = v_sql + "       t_status_msg = :p_status_msg ";
   if(v_aux_buf != V_UNDEF)
      v_sql = v_sql + " ,t_party_id = :p_client_id ";
   end;
   v_sql = v_sql + " WHERE t_id = :p_id ";

   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_status", RSDBP_IN, p_status);
   v_cmd.addParam("p_status_msg", RSDBP_IN, p_status_msg);
   if(v_aux_buf != V_UNDEF)
      v_cmd.addParam("p_client_id", RSDBP_IN, p_client_id);
   end;
   v_cmd.addParam("p_id", RSDBP_IN, p_id);
   v_cmd.execute();

   v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end; /* macro m_update_conv_row() */


/*---------------------------*/
/* Сохранение кодового слова */
/*---------------------------*/
private macro m_save_code_word(p_client_id, p_code_word)
   var v_ret = true;
   var v_sql, v_cmd;

   v_sql =         "INSERT INTO usr_clnt_cdwrd_hist_dbt ";
   v_sql = v_sql + "       (t_id, t_cw_type, t_cw_client, t_cw_proc_type, ";
   v_sql = v_sql + "        t_code_word, t_cw_status, t_cw_create_ts, ";
   v_sql = v_sql + "        t_cw_accept_ts) ";
   v_sql = v_sql + "VALUES (:p_id, :p_cw_type, :p_cw_client, :p_cw_proc_type, ";
   v_sql = v_sql + "        :p_code_word, :p_cw_status, SYSTIMESTAMP, ";
   v_sql = v_sql + "        SYSTIMESTAMP) ";

   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_id", RSDBP_IN, 0);
   v_cmd.addParam("p_cw_type", RSDBP_IN, 2);
   v_cmd.addParam("p_cw_client", RSDBP_IN, p_client_id);
   v_cmd.addParam("p_cw_proc_type", RSDBP_IN, 1);
   v_cmd.addParam("p_code_word", RSDBP_IN, p_code_word);
   v_cmd.addParam("p_cw_status", RSDBP_IN, 4);
   v_cmd.execute();

   v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end; /* macro m_save_code_word() */


/*------------------------------------------*/
/* Проверка значение Биржевого кода Клиента */
/*------------------------------------------*/
private macro m_check_client_code(p_number, p_client_code)
   const CV_TYPE_CNTR = 207;
   const CV_CODE_KIND = 1;

   var v_ret = true;
   var v_sql, v_cmd, v_rs;
   var v_aux_cntr;
   var v_list_size;
   var v_aux_buf;
   var v_suffics_match;

   v_aux_cntr = 0;
   v_list_size = v_suffics_list.size;
   v_suffics_match = false;

   while((not v_suffics_match) and (v_aux_cntr < v_list_size))
      v_aux_buf = 0;
      v_aux_buf = index(p_number, v_suffics_list.value(v_aux_cntr));
      if(v_aux_buf != 0)
         p_number = substr(p_number, 1, (v_aux_buf - 1));
         v_suffics_match = true;
      end;

      v_aux_cntr = v_aux_cntr + 1;
   end;

   v_sql =         "SELECT tbl_code.t_code ";
   v_sql = v_sql + "  FROM ddlobjcode_dbt tbl_code ";
   v_sql = v_sql + " WHERE tbl_code.t_codekind = :p_codekind ";
   v_sql = v_sql + "   AND tbl_code.t_objecttype = :p_objecttype ";
   v_sql = v_sql + "   AND tbl_code.t_objectid = (SELECT tbl_dl.t_dlcontrid ";
   v_sql = v_sql + "                                FROM ddlcontr_dbt tbl_dl ";
   v_sql = v_sql + "                               WHERE tbl_dl.t_sfcontrid = (SELECT tbl_sf.t_id ";
   v_sql = v_sql + "                                                             FROM dsfcontr_dbt tbl_sf ";
   v_sql = v_sql + "                                                            WHERE tbl_sf.t_number = :p_number)) ";
   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_codekind", RSDBP_IN, CV_CODE_KIND);
   v_cmd.addParam("p_objecttype", RSDBP_IN, CV_TYPE_CNTR);
   v_cmd.addParam("p_number", RSDBP_IN, p_number);
   v_rs = RSDRecordSet(v_cmd);

   if(v_rs.movenext())
      if(v_rs.value("t_code") != p_client_code)
         v_ret = false;
      end;
   else
      v_ret = false;
   end;

   v_rs.close(); v_cmd.close(); v_rs = NULL; v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end; /* macro m_check_client_code() */


/*-------------------------------------------------------------------------------------------*/
/* Поиск идентификатора клиента по номеру договора (+ проверка существования кодового слова) */
/*-------------------------------------------------------------------------------------------*/
private macro m_find_client(p_number, p_code_word, p_client_id:@integer)
   const CV_ST_APPROVE = 4;
   const CV_TYPE_CW = 2;

   var v_ret = 0; /* Кодового слова у клиента нет (требуется миграция) */
   var v_sql, v_cmd, v_rs;

   v_sql =         "   SELECT tbl_sf.t_number, tbl_sf.t_partyid, tbl_cw.t_code_word ";
   v_sql = v_sql + "     FROM dsfcontr_dbt tbl_sf ";
   v_sql = v_sql + "LEFT JOIN usr_clnt_cdwrd_hist_dbt tbl_cw ON tbl_cw.t_cw_client = tbl_sf.t_partyid ";
   v_sql = v_sql + "                                        AND tbl_cw.t_cw_type = :p_type_cw ";
   v_sql = v_sql + "                                        AND tbl_cw.t_cw_status = :p_st_approve ";
   v_sql = v_sql + "    WHERE tbl_sf.t_number = :p_number ";
   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_type_cw", RSDBP_IN, CV_TYPE_CW);
   v_cmd.addParam("p_st_approve", RSDBP_IN, CV_ST_APPROVE);
   v_cmd.addParam("p_number", RSDBP_IN, p_number);
   v_rs = RSDRecordSet(v_cmd);

   if(v_rs.movenext()) /* Кодовое слово в статусе 4 может быть только одно */
      if(ValType(v_rs.value("t_code_word")) != 26)
         if(p_code_word == v_rs.value("t_code_word"))
            v_ret = 1; /* Значение кодового слова совпадает (миграция не требуется) */
         else
            v_ret = 2; /* Значение кодового слова не совпадает (требуется ручной разбор) */
         end;
      end;
      p_client_id = v_rs.value("t_partyid");
   else
      v_ret = 3; /* Договор не найден */
   end;

   v_rs.close(); v_cmd.close(); v_rs = NULL; v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret = 5; /* Ошибка общего смысла */
   return v_ret;
end; /* macro m_find_client() */


/*-----------------------------------------------------------*/
/* Выполняем сохранение загруженных данных по кодовым словам */
/*-----------------------------------------------------------*/
private macro m_save_conv_cd_wrd_srv_brk()
   var v_ret = true;
   var v_sql, v_cmd, v_rs;
   var v_aux_cntr;

   var v_stat_bool;
   var v_stat_int;

   var v_stat_num;
   var v_stat_msg;

   var v_client_id;


   v_sql =         "SELECT t_id, t_cntr_open_date, t_client_fio, ";
   v_sql = v_sql + "       DECODE(t_contract_num, CHR(1), NULL, t_contract_num) v_contract_num, ";
   v_sql = v_sql + "       DECODE(t_client_num, CHR(1), NULL, t_client_num) v_client_num, ";
   v_sql = v_sql + "       DECODE(t_code_word, CHR(1), NULL, t_code_word) v_code_word ";
   v_sql = v_sql + "  FROM usr_clnt_cdwrd_conv_dbt ";
   v_sql = v_sql + " WHERE NVL(t_status, -1) <> 0 ";
   v_sql = v_sql + "ORDER BY t_id ";
   v_cmd = RSDCommand(v_sql);
   v_rs = RSDRecordSet(v_cmd);

   v_aux_cntr = 0;

   while(v_rs.movenext())
      v_stat_bool = true;
      v_stat_num = 0;
      v_stat_msg = "OK";
      v_client_id = NULL;
      v_aux_cntr = v_aux_cntr + 1;

      if(ValType(v_rs.value("v_contract_num")) == 26)
         v_stat_msg = "Отсутствует номер договора";
         v_stat_num = 1;
         v_stat_bool = false;
      end;
      if(ValType(v_rs.value("v_code_word")) == 26)
         v_stat_msg = "Отсутствует значение кодового слова";
         v_stat_num = 1;
         v_stat_bool = false;
      end;

      if(v_stat_bool)
         v_stat_int = m_find_client(v_rs.value("v_contract_num"), v_rs.value("v_code_word"), @v_client_id);
         if(v_stat_int != 0)
            if(v_stat_int == 1)
               v_stat_msg = "Значение кодового слова совпадает с существующим";
               v_stat_num = 0;
               v_stat_bool = false;
            elif(v_stat_int == 2)
               v_stat_msg = "Уже есть подтвержденное значение кодового слова (требуется ручной разбор)";
               v_stat_num = 1;
               v_stat_bool = false;
            elif(v_stat_int == 3)
               v_stat_msg = "Не найден Договор по заданному Номеру";
               v_stat_num = 1;
               v_stat_bool = false;
            elif(v_stat_int == 5)
               v_stat_msg = "Произошла ошибка, при поиске Клиента по Номеру Договора";
               v_stat_num = 1;
               v_stat_bool = false;
            else
               v_stat_msg = "Неизвестная ошибка при поиске клиента";
               v_stat_num = 1;
               v_stat_bool = false;
            end;
         end;
      end;

      if(v_stat_bool)
         if(ValType(v_rs.value("v_client_num")) != 26)
            if(not m_check_client_code(v_rs.value("v_contract_num"), v_rs.value("v_client_num")))
               v_stat_msg = "Ошибка при проверке Биржевого кода Клиента";
               v_stat_num = 1;
               v_stat_bool = false;
            end;
         else
            v_stat_msg = "Значение кодового слова сохранено без проверки Биржевого кода (Биржевой код не задан)";
         end;
      end;

      if(v_stat_bool)
         if(not m_save_code_word(v_client_id, v_rs.value("v_code_word")))
            v_stat_msg = "Значение кодового слова не сохранено (произошла ошибка)";
            v_stat_num = 1;
         end;
      end;

      m_update_conv_row(v_rs.value("t_id"), v_stat_num, v_stat_msg, v_client_id);
   end;

   msgbox(string("Обработано записей: ", v_aux_cntr));

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end; /* macro m_save_conv_cd_wrd_srv_brk() */


/*----------------------------------------------------*/
/* Метод обработки событий скроллинга данных миграции */
/*----------------------------------------------------*/
macro m_cw_migr_data_proc_scr(p_rs, p_cmd, p_id, p_key)
   var CM_FLAG = CM_DEFAULT;

   if(p_cmd == DLG_INIT)
      while((gv_scr_focus_md != 0) and p_rs.movenext())
         if(p_rs.value("t_id") == gv_scr_focus_md)
            gv_scr_focus_md = 0;
            GoToScroll(p_rs);
         end;
      end;

   elif(p_cmd == DLG_KEY)
      if(p_key == K_F2)
         /* Переносим данные миграции из таблицы загрузки в функциональную */
         if(gettrue(true, "Вы действительно хотите сохранить данные текущей загрузки?"))
            if(not m_save_conv_cd_wrd_srv_brk())
               msgbox("В процессе сохранения данных возникли проблемы");
            else
               CM_FLAG = CM_SELECT;
            end;
         else
            CM_FLAG = CM_IGNORE;
         end;

         /* Обновляем скроллинг в любом случае */
         CM_FLAG = CM_SELECT;

      elif(p_key == K_F5)
         /* Просто обновляем скроллинг */
         gv_scr_focus_md = p_rs.value("t_id");
         CM_FLAG = CM_SELECT;

      elif(p_key == K_F9)
         /* Выполняем загрузку данных миграции из файла */
         if(gettrue(true, string("Вы действительно хотите выполнить загрузку данных миграции из файла?",
                                 "\nТекущие данные миграции будут утеряны.",
                                 "\nБудет загружен файл ", GV_LOAD_FILE_PATH)))
            if(not m_load_code_word_serv_brk(GV_LOAD_FILE_PATH, 1))
               msgbox("В процессе загрузки данных возникли проблемы");
            else
               CM_FLAG = CM_SELECT;
            end;
         else
            CM_FLAG = CM_IGNORE;
         end;

      elif(p_key == K_ESCAPE)
         if(not gettrue(true, "Выйти из списка?"))
            CM_FLAG = CM_IGNORE;
         end;
      end;

   end;

   return CM_FLAG;
end; /* macro m_cw_migr_data_proc_scr() */


/*----------------------------------------------*/
/* Метод, запускающий скроллинг данных миграции */
/*----------------------------------------------*/
macro m_run_migr_data_scroll()
   private var sql_scr, cmd_scr, rs_scr;
   private var sql_main = "";
   private var sql_filtr = "";
   private var sql_order = "";

   sql_main =            "SELECT t_id, t_contract_num, t_cntr_open_date, ";
   sql_main = sql_main + "       t_client_fio, t_client_num, t_code_word, ";
   sql_main = sql_main + "       NVL(t_status, -1) v_status, t_status_msg, t_party_id ";
   sql_main = sql_main + "  FROM usr_clnt_cdwrd_conv_dbt ";
   sql_order = sql_order + "ORDER BY t_id ";

   sql_scr = sql_main + sql_filtr + sql_order;

   cmd_scr = RSDCommand(sql_scr);
   cmd_scr.NullConversion = true;

   rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   rs_scr.NullConversion = true;

   m_add_column(gv_col_list_scr_md, gv_num_col_scr_md, "T_ID",             "ID записи", 8); gv_num_col_scr_md = gv_num_col_scr_md + 1;
   m_add_column(gv_col_list_scr_md, gv_num_col_scr_md, "t_contract_num",   "Номер ДБО", 10); gv_num_col_scr_md = gv_num_col_scr_md + 1;
   m_add_column(gv_col_list_scr_md, gv_num_col_scr_md, "t_cntr_open_date", "Дата открытия договора", 10); gv_num_col_scr_md = gv_num_col_scr_md + 1;
//   m_add_column(gv_col_list_scr_md, gv_num_col_scr_md, "t_tarif_plan",     "Тарифный план", 10); gv_num_col_scr_md = gv_num_col_scr_md + 1;
   m_add_column(gv_col_list_scr_md, gv_num_col_scr_md, "t_client_fio",     "ФИО Клиента", 20); gv_num_col_scr_md = gv_num_col_scr_md + 1;
   m_add_column(gv_col_list_scr_md, gv_num_col_scr_md, "t_client_num",     "Биржевой код Клиента", 10); gv_num_col_scr_md = gv_num_col_scr_md + 1;
//   m_add_column(gv_col_list_scr_md, gv_num_col_scr_md, "t_filial_name",    "Наименование филиала", 10); gv_num_col_scr_md = gv_num_col_scr_md + 1;
   m_add_column(gv_col_list_scr_md, gv_num_col_scr_md, "t_code_word",      "Кодовое слово", 10); gv_num_col_scr_md = gv_num_col_scr_md + 1;
//   m_add_column(gv_col_list_scr_md, gv_num_col_scr_md, "t_note",           "Примечание", 10); gv_num_col_scr_md = gv_num_col_scr_md + 1;
   m_add_column(gv_col_list_scr_md, gv_num_col_scr_md, "v_status",         "Статус процесса миграции", 3, NULL, 0); gv_num_col_scr_md = gv_num_col_scr_md + 1;
   m_add_column(gv_col_list_scr_md, gv_num_col_scr_md, "t_status_msg",     "Текстовый статус процесса миграции", 10); gv_num_col_scr_md = gv_num_col_scr_md + 1;
   m_add_column(gv_col_list_scr_md, gv_num_col_scr_md, "t_party_id",       "PartyId Клиента", 8); gv_num_col_scr_md = gv_num_col_scr_md + 1;

   while(RunScroll(rs_scr, gv_num_col_scr_md, gv_col_list_scr_md, "cw_migr_data", "m_cw_migr_data_proc_scr",
                   "Кодовое слово. Данные миграции", "[ESC] - выход  [F2] - сохранение данных миграции  [F5] - обновить  [F9] - загрузка данных миграции", true, 5, NULL, NULL, 40))
      /* Обновляем скроллинг */
      cmd_scr.close(); cmd_scr = NULL;
      rs_scr.close(); rs_scr = NULL;

      sql_scr = sql_main + sql_filtr + sql_order;

      cmd_scr = RSDCommand(sql_scr);
      cmd_scr.NullConversion = true;

      rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   end;

   rs_scr.close();

end; /* macro m_run_migr_data_scroll() */


/*-----------------*/
/* Ручная загрузка */
/*-----------------*/
//m_load_code_word_serv_brk("..\\Import\\CD_WRD_SRV_BRK\\CD_WRD_SRV_BRK.csv");