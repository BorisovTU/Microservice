/*
rshb_ratings.mac
Загрузка рейтингов из буф. таблиц рудаты
*/

import oralib, likepy, PTInter, CTInter, globals;

private const GROUPID_RATING = 19;

//Родительские attrid в таблице с категориями
private const RATING_MOODY_PARENT = 210,
              RATING_SP_PARENT    = 110,
              RATING_FITCH_PARENT = 310;

//ИД рейтингов в рудате
private const MOODY_ID   = 130,
              S_AND_P_ID = 9,
              FITCH_ID   = 27;

//возвращает attrid категории по наименованию и parentid
macro GetAttrIDByValue (GroupID, ParentID, value)
  var attrid = 0;
  var sql = "select t_attrid from dobjattr_dbt "
          + "where     t_objecttype  = 3 "
          + "      and t_GroupID     = :groupid "
          + "      and t_parentid    = :parentid "
          + "      and lower(t_name) = :name ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("groupid",  GroupID),
                                     SqlParam("parentid", ParentID),
                                     SqlParam("name",     StrLwr(value))), false);
  if (sql.MoveNext() )
    attrid = sql.value("t_attrid");
  end;

  return attrid;
End;

//устанавливает значение категории субъекту
macro SetCategOnSubject (PartyID, GroupID, AttrID, CategDate)
  var Party = RsbParty(PartyID);
  var PartyCateg = Party.Category();

  var sql = "select 1 from dobjatcor_Dbt atcor "
          + "where     atcor.t_objecttype = 3 "
          + "      and atcor.t_groupid = :groupid "
          + "      and atcor.t_object = lpad(:partyid, 10, 0) "
          + "      and atcor.t_validfromdate = :categdate "
          + "      and atcor.t_attrid in (select t_attrid from dobjattr_dbt attr "
          + "                             where    t_parentid = (select t_parentid from dobjattr_dbt  "
          + "                                                    where t_objecttype = atcor.t_objecttype and "
          + "                                                          t_groupid = atcor.t_groupid and "
          + "                                                          t_attrid = :attrid) "
          + "                                  and t_objecttype = atcor.t_objecttype and  t_groupid = atcor.t_groupid) ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("groupid", GroupID),
                                     SqlParam("partyid", PartyID),
                                     SqlParam("categdate", CategDate),
                                     SqlParam("attrid", AttrID)), false);
  if (sql.MoveNext() )
    return true; //Если уже есть рейтинг этого вида на дату CategDate
  end;

  PartyCateg.ConnectAttr(GroupID, AttrID, null, null, CategDate);
  return Party.Update();
End;

/*****************************************************/
//Устанавливает субъекту значение ретинга Мудиз
macro SetRatingMoody (PartyID, RatingValue, RatingDate)
  var stat = false;
  var AttrID = GetAttrIDByValue(GROUPID_RATING, RATING_MOODY_PARENT, RatingValue);

  if (AttrID != 0) 
    stat = SetCategOnSubject(PartyID, GROUPID_RATING, AttrID, RatingDate);
  end;

  return stat;
End;

//Устанавливает субъекту значение ретинга S&P
macro SetRatingSP (PartyID, RatingValue, RatingDate)
  var stat = false;
  var AttrID = GetAttrIDByValue(GROUPID_RATING, RATING_SP_PARENT, RatingValue);

  if (AttrID != 0) 
    stat = SetCategOnSubject(PartyID, GROUPID_RATING, AttrID, RatingDate);
  end;

  return stat;
End;

//Устанавливает субъекту значение ретинга Fitch
macro SetRatingFitch (PartyID, RatingValue, RatingDate)
  var stat = false;
  var AttrID = GetAttrIDByValue(GROUPID_RATING, RATING_FITCH_PARENT, RatingValue);

  if (AttrID != 0) 
    stat = SetCategOnSubject(PartyID, GROUPID_RATING, AttrID, RatingDate);
  end;

  return stat;
End;

/*****************************************************/

//Ищет значение рейтинга в буфере
macro FindRatingBuf (inn, dt, ratingID, value:@string, RatingDate:@Date)
  var sql, val:String = "";

  sql = "select last, dt from sofr_rating_ratingshistory "
      + "where     rating_object_type = 'Компания' "
      + "      and rating_id = :ratingid "
      + "      and dt       <= :dt "
      + "      and Trim(inn) = :inn "
      //+ "      and last <> 'Снят' " //удалить
      + "order by dt desc";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("ratingid", ratingID),
                                     SqlParam("dt", dt),
                                     SqlParam("inn", Trim(inn))), false);
  if (sql.MoveNext() )
    value = sql.value("last");
    RatingDate = sql.value("dt");
  end;
End;

/*********************************************/
//Загрузка рейтингов заданного вида для одного субъекта
macro FindAndSetMoody (partyid, inn, dt)
  var stat = false;
  var RatingValue = "";
  var RatingDate = Date(0, 0, 0);

  FindRatingBuf(inn, dt, MOODY_ID, @RatingValue, @RatingDate);
  if (RatingValue != "")
    stat = SetRatingMoody(partyid, RatingValue, RatingDate);
  end;
  return stat;
End;

macro FindAndSetSP (partyid, inn, dt)
  var stat = false;
  var RatingValue = "";
  var RatingDate = Date(0, 0, 0);

  FindRatingBuf(inn, dt, S_AND_P_ID, @RatingValue, @RatingDate);
  if (RatingValue != "")
    stat = SetRatingSP(partyid, RatingValue, RatingDate);
  end;
  return stat;
End;

macro FindAndSetFitch (partyid, inn, dt)
  var stat = false;
  var RatingValue = "";
  var RatingDate = Date(0, 0, 0);

  FindRatingBuf(inn, dt, FITCH_ID, @RatingValue, @RatingDate);
  if (RatingValue != "")
    stat = SetRatingFitch(partyid, RatingValue, RatingDate);
  end;
  return stat;
End;

/*********************************************/

macro SetAllRatingsSubject (partyid:integer, inn:string, dt:date)
  if (not FindAndSetMoody(partyid, inn, dt) )
    println("не установлен рейтинг Moody");
  else
    println("Успешно установлен Moody субъекту с ИНН = " + inn);
  end;

  if (not FindAndSetSP(partyid, inn, dt) )
    println("не установлен рейтинг Standard and Poor's");
  else
    println("Успешно установлен Standard and Poor's субъекту с ИНН = " + inn);
  end;

  if (not FindAndSetFitch(partyid, inn, dt) )
    println("не установлен рейтинг Fitch");
  else
    println("Успешно установлен Fitch субъекту с ИНН = " + inn);
  end;

End;

macro SetEmitentsRatings (dt)
  var sql, count = 0;

  sql = "select party.t_partyid partyid, "
      + "       objcode.t_code inn "
      + "from dparty_Dbt party, dpartyown_dbt own, dobjcode_dbt objcode "
      + "where party.t_partyid          = own.t_partyid "
      + "      and own.t_partykind      = 5 /*эмитент*/ "
      + "      and objcode.t_objectid   = party.t_partyid "
      + "      and objcode.t_objecttype = 3 "
      + "      and objcode.t_codekind   = 16 "
      + "      and objcode.t_state      = 0 order by party.t_partyid";
  sql = ExecSqlSelect(sql, null, false);

  InitProgress(-1, "Обновление рейтингов", "Обновление рейтингов");
  while (sql.MoveNext() )
    SetAllRatingsSubject(sql.value("partyid"), Trim(sql.value("inn")), dt);
    UseProgress(count = count + 1);
  end;
  RemProgress();

onerror()
  RemProgress();
End;