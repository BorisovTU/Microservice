/*----------------------------------------------------------------*/
/* Передача из СОФР данных по удаленным проводкам (ответ из СОФР) */
/*----------------------------------------------------------------*/

import "global_utils_intgr.mac";
import "uws_exchng_log.mac";


// 2021-05-04 Cherednichenko Переносим из SendExec
private macro UpdatePaymentUserfieldApprove( PaymentId :integer, FldValue :string ) :integer
var sql :string = "",
    cmd, rs;

  sql = " DECLARE " +
          "  v_PaymentId              dpmpaym_dbt.T_PAYMENTID%TYPE := :p_PaymentId; " +
          "  v_UsrField               dpmpaym_dbt.T_USERFIELD4%TYPE := :p_UsrField; " +
          " BEGIN " +
          
          "  UPDATE dpmpaym_dbt " +
          "  SET T_USERFIELD4 = (select t_userfield4 from dpmpaym_dbt where t_paymentid = v_PaymentId) ||'/'|| v_UsrField  " +
          "  WHERE T_PAYMENTID = v_PaymentId; " +

          " COMMIT;   "+

          " END; ";
 
  cmd = RSDCommand(sql);
  cmd.AddParam("p_PaymentId", RSDBP_IN, PaymentId);
  cmd.AddParam("p_UsrField", RSDBP_IN, FldValue);
  cmd.execute();

  cmd.close(); cmd = NULL; sql = NULL;

  return 0;

onError(err)

 return 1;

end;

private macro UpdateTrnUserfieldApprove( AccTrntId :integer, FldValue :string ) :integer
var sql :string = "",
    cmd, rs;


  sql = " DECLARE " +
          "  v_AcctrnId              dacctrn_dbt.T_ACCTRNID%TYPE := :p_AcctrnId; " +
          "  v_UsrField              dacctrn_dbt.T_USERFIELD4%TYPE := :p_UsrField; " +
          " BEGIN " +
          
          "  UPDATE dacctrn_dbt " +
          "  SET T_USERFIELD4 = (Select t_userfield4 from dacctrn_dbt where t_acctrnid = v_acctrnid) ||'/'|| v_UsrField " +
          "  WHERE T_ACCTRNID = v_ACCTRNId; " +
          
          " COMMIT;   "+

          " END; ";
 

  cmd = RSDCommand(sql);
  cmd.AddParam("p_AcctrnId", RSDBP_IN, AccTrntId);
  cmd.AddParam("p_UsrField", RSDBP_IN, FldValue);
  cmd.execute();

  cmd.close(); cmd = NULL; sql = NULL;

  return 0;

onError(err)

 return 1;

end;
// / Переносим из SendExec

private class c_IntegrSymId(p_income_data)
   var ObjectId     : string
      ,SystemId     : string
      ,SystemNodeId : string;

   private macro m_init_IntegrSymId(i_income_data)
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         ObjectId = uTryToGetPropS(i_income_data, "ObjectId");
         if(ValType(ObjectId) == V_UNDEF)
            v_service_msg = string(InErrComPart, "ObjectId");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;

         SystemId = uTryToGetPropS(i_income_data, "SystemId");
         SystemNodeId = uTryToGetPropS(i_income_data, "SystemNodeId");
      end;
   end;

   m_init_IntegrSymId(p_income_data);
end;

private class c_ApproveEntryResult(p_income_data)
   var ResponseCode : integer
      ,ResponseNote : string
      ,EntryID      : object; /* c_IntegrSymId */

   private macro m_init_ApproveEntryResult(i_income_data)
      private var v_aux_buf;
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         ResponseCode = uTryToGetPropS(i_income_data, "ResponseCode");
         if(ValType(ResponseCode) == V_UNDEF)
            v_service_msg = string(InErrComPart, "ResponseCode");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;

         ResponseNote = uTryToGetPropS(i_income_data, "ResponseNote");

         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "EntryID"));
         if(v_aux_buf != V_UNDEF)
            if(v_aux_buf == V_GENOBJ)
               EntryID = c_IntegrSymId(i_income_data.EntryID);
            else
               v_service_msg = string(InErrNotObj, "EntryID");
               RunError(v_service_msg, c_usr_err_obj(v_service_msg));
            end;
         else
            v_service_msg = string(InErrComPart, "EntryID");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;
      end;
   end;

   m_init_ApproveEntryResult(p_income_data);
end;

private class c_ApproveAccountingEntriesResponse(p_income_data)
   var ApproveEntryResult : object; /* c_ApproveEntryResult */

   private macro m_init_ApproveAccountingEntriesResponse(i_income_data)
      private var v_aux_buf;
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "ApproveEntryResult"));
         if(v_aux_buf != V_UNDEF)
            if(v_aux_buf == V_GENOBJ)
               ApproveEntryResult = c_ApproveEntryResult(i_income_data.ApproveEntryResult);
            else
               v_service_msg = string(InErrNotObj, "ApproveEntryResult");
               RunError(v_service_msg, c_usr_err_obj(v_service_msg));
            end;
         else
            v_service_msg = string(InErrComPart, "ApproveEntryResult");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;
      end;
   end;

   m_init_ApproveAccountingEntriesResponse(p_income_data);
end;

private class c_ApproveAccountingEntriesResponseMsg(p_income_data)
   var ApproveAccountingEntriesResponse : object; /* c_ApproveAccountingEntriesResponse */

   private macro m_init_ApproveAccountingEntriesResponseMsg(i_income_data)
      private var v_aux_buf;
      private var v_service_msg;

      if(ValType(i_income_data) != V_UNDEF)
         v_aux_buf = ValType(uTryToGetPropS(i_income_data, "ApproveAccountingEntriesResponse"));
         if(v_aux_buf != V_UNDEF)
            if(v_aux_buf == V_GENOBJ)
               ApproveAccountingEntriesResponse = c_ApproveAccountingEntriesResponse(i_income_data.ApproveAccountingEntriesResponse);
            else
               v_service_msg = string(InErrNotObj, "ApproveAccountingEntriesResponse");
               RunError(v_service_msg, c_usr_err_obj(v_service_msg));
            end;
         else
            v_service_msg = string(InErrComPart, "ApproveAccountingEntriesResponse");
            RunError(v_service_msg, c_usr_err_obj(v_service_msg));
         end;
      end;
   end;

   m_init_ApproveAccountingEntriesResponseMsg(p_income_data);
end;


private macro m_upd_table_proc_evnt_stat_w_err(p_objectid, p_objecttype, p_status, p_msg_id, p_status_old, p_res_code, p_res_text)

   var v_ret = true;
   var v_obj_tp_type, v_msg_id_type, v_stat_old_type;
   var v_res_code, v_res_text;
   var restest:integer = p_res_code;
   var v_sql, v_cmd;

   v_obj_tp_type = ValType(p_objecttype);
   v_msg_id_type = ValType(p_msg_id);
   v_stat_old_type = ValType(p_status_old);
   v_res_code = ValType(p_res_code);

   v_sql =         "UPDATE utableprocessevent_dbt ";
   v_sql = v_sql + "   SET t_status = :p_status ";
   if(v_msg_id_type != V_UNDEF)
      v_sql = v_sql + "   ,t_messageid = :p_msg_id ";
   end;

   if(v_res_code != V_UNDEF)
      v_sql = v_sql + "   ,t_resultcode = :p_res_code ";
     if(ValType(p_res_text) != V_UNDEF)
        v_sql = v_sql + " , t_resulttext = :p_res_text ";
     end;
   end;

   if(v_obj_tp_type != V_UNDEF)
      v_sql = v_sql + " WHERE t_objecttype = :p_objecttype ";
      v_sql = v_sql + "   AND t_objectid = :p_objectid ";
      v_sql = v_sql + "   AND t_type = 3 ";
   else
      v_sql = v_sql + " WHERE t_recid = :p_recid ";
   end;
   if(v_stat_old_type != V_UNDEF)
      v_sql = v_sql + "AND t_status = :p_status_old ";
   end;

   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_status", RSDBP_IN, p_status);
   if(v_msg_id_type != V_UNDEF)
      v_cmd.addParam("p_msg_id", RSDBP_IN, p_msg_id);
   end;
   if(v_res_code != V_UNDEF)
      v_cmd.addParam("p_res_code", RSDBP_IN, p_res_code);
     if(ValType(p_res_text) != V_UNDEF)
        v_cmd.addParam("p_res_text", RSDBP_IN, substr(p_res_text, 1,999));
      end;
   end;
   if(v_obj_tp_type != V_UNDEF)
      v_cmd.addParam("p_objecttype", RSDBP_IN, p_objecttype);
      v_cmd.addParam("p_objectid", RSDBP_IN, p_objectid);
   else
      v_cmd.addParam("p_recid", RSDBP_IN, p_objectid);
   end;
   if(v_stat_old_type != V_UNDEF)
      v_cmd.addParam("p_status_old", RSDBP_IN, p_status_old);
   end;

   v_cmd.execute();

   v_cmd.close(); v_cmd = NULL; v_sql = NULL;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end;



/*--------------------------------------*/
/* Обновление статуса исходного события */
/*-------------------------------------------------------------------------------------------*/
/* p_obj_id - идентификатор внешней системы, поэтому пытаемся восстановить цепочку сообщений */
/*-------------------------------------------------------------------------------------------*/
private macro m_save_src_event_status(/*p_obj_id,*/p_jms_msg_id, p_result_code, p_log_id,p_result_desc)
   const CV_OBJTYPE_ENTRY = 1;
   const CV_STATUS_OLD = 2;
   var v_ret = true;
   var v_evnt_prm;
   /*var v_sql, v_cmd, v_rs;*/
   var v_status;

   if(p_result_code == 0)
      v_status = 4;
   else
      v_status = 5;
   end;
/*
   v_sql =         "SELECT t_objecttype, t_objectid, t_status ";
   v_sql = v_sql + "  FROM utableprocessevent_dbt ";
   v_sql = v_sql + " WHERE t_objecttype = :p_objecttype ";
   v_sql = v_sql + "   AND t_objectid = :p_objectid ";
   v_sql = v_sql + "   AND t_status = :p_status_old ";
   v_cmd = RSDCommand(v_sql);
   v_cmd.addParam("p_objecttype", RSDBP_IN, CV_OBJTYPE_ENTRY);
   v_cmd.addParam("p_objectid", RSDBP_IN, p_obj_id);
   v_cmd.addParam("p_status_old", RSDBP_IN, CV_STATUS_OLD);
   v_rs = RSDRecordSet(v_cmd);
   if(v_rs.movenext())
      v_ret = m_upd_table_proc_evnt_status(v_rs.value("t_objectid"), v_rs.value("t_objecttype"), v_status, p_log_id, CV_STATUS_OLD);
   else
      v_ret = false;
   end;

   v_rs.close(); v_cmd.close(); v_rs = NULL; v_cmd = NULL; v_sql = NULL;
*/

   v_evnt_prm = m_get_source_event(p_jms_msg_id);

   if(v_evnt_prm.v_func_res == 0)
//      v_ret = m_upd_table_proc_evnt_status(v_evnt_prm.v_recid, NULL, v_status, p_log_id, CV_STATUS_OLD);
//shev 210121 сохраняем ошибки в utableprocessevent
      v_ret = m_upd_table_proc_evnt_stat_w_err(v_evnt_prm.v_objectid, v_evnt_prm.v_objecttype, v_status, p_log_id, CV_STATUS_OLD, 
                                               p_result_code, p_result_desc);

   else
      v_ret = false;
   end;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end;


/*-------------*/
/* ТОЧКА ВХОДА */
/*-------------*/
macro ApproveAccountingEntriesResponseMsg(p_income_data, p_log_id, p_jms_msg_id)
   var v_logger_obj;
   var v_log_err = "";
   var v_income_data;
   var v_aux_buf;

   const ANNUL = "АННУЛ";
   var query, cmd, rs;
   var isPayment, absid, sofrid;

   v_logger_obj = uws_exchng_logger();
   v_logger_obj.set_log_id(p_log_id);

   /* Получаем проверенный объект со входными данными */
   v_income_data = c_ApproveAccountingEntriesResponseMsg(p_income_data);

// 2021-05-04 Cherednichenko После перехода на ЦФТ переносим присваивание АННУЛ к UF4 сюда
   if (v_income_data.ApproveAccountingEntriesResponse.ApproveEntryResult.ResponseCode == 0) //если на стороне ЦФТ нет ошибок

      // определим СОФРид по АБСид из UF4
      isPayment = true;
      sofrid = 0;
      absid = v_income_data.ApproveAccountingEntriesResponse.ApproveEntryResult.EntryID.ObjectId;

      // проверим в платежах
      query =         " select t_paymentid from dpmpaym_dbt where 1=1 ";
      query = query + " and t_userfield4 = :entryID ";
      query = query + " or t_userfield4 = :entryIDp ";
  
      cmd = RSDCommand(query);
      cmd.addParam("entryID", RSDBP_IN, absid);
      cmd.addParam("entryIDp", RSDBP_IN, absid + "#1");
  
      rs = RSDRecordset(cmd);
      if (rs.movenext)
         sofrid =  rs.value("t_paymentid");
      end;
      rs.close(); cmd.close(); rs = NULL; cmd = NULL; query = NULL;
  
      // если не нашли - проверим в проводках
      if (sofrid == 0)
        isPayment = false;
        query =         " select t_acctrnid from dacctrn_dbt where 1=1 ";
        query = query + " and t_userfield4 = :entryID ";
        query = query + " or t_userfield4 = :entryIDp ";
    
        cmd = RSDCommand(query);
        cmd.addParam("entryID", RSDBP_IN, absid);
        cmd.addParam("entryIDp", RSDBP_IN, absid + "#1");
    
        rs = RSDRecordset(cmd);
        if (rs.movenext)
           sofrid =  rs.value("t_acctrnid");
        end;
        rs.close(); cmd.close(); rs = NULL; cmd = NULL; query = NULL;
      end;

      if (sofrid != 0) //если нашли - припишем АННУЛ
        if (isPayment)
          UpdatePaymentUserfieldApprove( sofrid, ANNUL );
        else
          UpdateTrnUserfieldApprove( sofrid, ANNUL );
        end;
      end;

   end;
// /2021-05-04


   if(v_income_data.ApproveAccountingEntriesResponse.ApproveEntryResult.ResponseCode != 0)
      v_log_err = m_add_note_to_str(string("Получена ошибка (",
                                           v_income_data.ApproveAccountingEntriesResponse.ApproveEntryResult.ResponseCode,
                                           ":",
                                           v_income_data.ApproveAccountingEntriesResponse.ApproveEntryResult.ResponseNote,
                                           ")"),
                                    v_log_err);
   end;

   /* Пытаемся восстановить цепочку сообщений */
   if(not m_save_src_event_status(/*v_income_data.ApproveAccountingEntriesResponse.ApproveEntryResult.EntryID.ObjectId,*/
                                  p_jms_msg_id,
                                  v_income_data.ApproveAccountingEntriesResponse.ApproveEntryResult.ResponseCode,
                                  p_log_id, v_log_err))

      v_log_err = m_add_note_to_str(string("Не удалось обновить статус исходного события (",
                                           v_income_data.ApproveAccountingEntriesResponse.ApproveEntryResult.EntryID.ObjectId,
                                           ";",
                                           v_income_data.ApproveAccountingEntriesResponse.ApproveEntryResult.ResponseCode,
                                           ")"),
                                    v_log_err);
   end;

   /* Если были какие-то ошибки - сохраняем их в лог */
   v_aux_buf = strlen(v_log_err);
   if(v_aux_buf > 0)
      if(v_aux_buf >= 2000)
         v_log_err = substr(v_log_err, 1, 1999);
      end;
      v_logger_obj.add_error_info(GC_ADD_DEV_ERR_CODE, v_log_err);
   end;

   v_logger_obj = NULL;

   return true;
end;