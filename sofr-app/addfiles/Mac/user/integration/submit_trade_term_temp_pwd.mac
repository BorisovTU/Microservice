/*----------------------------------------------------*/
/* Обработчик события генерации и отправки временного */
/* пароля для входа в торговые терминалы              */
/*----------------------------------------------------*/
/* TODO                                               */
/*----------------------------------------------------*/

import "global_utils_intgr.mac";
import "SendSMSAccept.mac"; /* Функционал отправки СМС (поток "A5") */
import "u_common_email_utils.mac";
//import BankInter; /* GenPassw() */ /* Должен быть в globals.mac (уже импортирован в global_utils_intgr.mac) */


/*-----------------------------------------------*/
/* Создать задание на отправку временного пароля */
/*-----------------------------------------------*/
macro m_make_submit_trm_pwd(p_client_partyid)
   const CV_SEND_TO_QUIK_YES = 1;
   const CV_SEND_TO_QUIK_NO = 2;
   const CV_ECONOMIC_SUBJ = 3;
   const CV_WAS_SENT_TO_QUIK_CATEG = 171;
   const CV_OBJ_TP_TRM_PWD_TRANSF = 5024;

   var v_ret;
   var v_client_was_sent_to_quik;

   /* Получение значения признака выгрузки данных в QUIK (получаем numinlist) */
   v_client_was_sent_to_quik = m_get_obj_categ_value(CV_ECONOMIC_SUBJ, p_client_partyid, CV_WAS_SENT_TO_QUIK_CATEG);

   if(v_client_was_sent_to_quik == CV_SEND_TO_QUIK_YES)
      v_ret = false;

   else
      /* Формируем задание на отправку СМС с временным паролем */
      v_ret = m_make_event_to_process(CV_OBJ_TP_TRM_PWD_TRANSF, p_client_partyid);
   end;

   return v_ret;

onError(err)
   v_ret = false;
   return v_ret;
end; /* macro m_make_submit_trm_pwd() */


class c_temp_pwd_proc(p_rec_id, p_client_id)
   /* Время ожидания ответа на отправку СМС (секунды) */
   private const CV_TIMEOUT_SMS_SUBMIT = 60;
   /* Код ошибки */
   private const CV_ERROR_SIGN = 1; /* Значение для ErrorCode, говорящее о наличии ошибки */
   private const CV_ERROR_CLEAR = 0; /* Значение для ErrorCode, говорящее об отсутствии ошибки */

   /* Описание ошибки */
   private const CV_ERR_CLEAR = ""; /* Текст ошибки по умолчанию */
   private const CV_ERR_DEFAULT = ""; /* Если ничего конкретного нет, то будем возвращать этот текст */

   private const CV_TYPE_SMS_PROC = 0; /* Тип процесса отправки СМС */
   private const CV_TRY_TO_SUBMIT_QUANT = 3; /* Количество попыток отправки СМС */
   private const CV_TYPE_CODE_TMP_PWD = 1; /* Тип кода для хранения значения в таблице */

   private const CV_EMAIL_GRP_SMS_PWD = 5; /* Группа рассылки по проблемам отправки СМС с временным паролем */

   private var v_error_code;
   private var v_error_desc;

   private var v_evnt_rec_id;
   private var v_evnt_client_id;
   private var v_evnt_state;
   private var v_client_phone_for_sms;
   private var v_tmp_pwd;
   private var v_try_to_submit_cntr;


   macro m_get_error_code()
      return v_error_code;
   end;

   macro m_get_error_desc()
      return v_error_desc;
   end;


   /*--------------------------------------------------------------------*/
   /* Отправка e-mail с указанием на проблему отправки временного пароля */
   /*--------------------------------------------------------------------*/
   macro m_sms_fail_email_notif()
      /* Заменить %client_id% на идентификатор клиента */
      const CV_ERR_SMS_FAIL = "Id Клиента: %client_id%. Не удалось отправить СМС с временным паролем для входа в торговые терминалы";

      var v_ret = true;
      var v_email_processor = NULL;
      var v_email_msg;

      v_email_msg = strsubst(CV_ERR_SMS_FAIL, "%client_id%", string(v_evnt_client_id));

      v_email_processor = c_email_proc_env();
      v_email_processor.m_set_msg_head("СОФР - Отправка временного пароля");
      v_email_processor.m_add_row_to_msg_text(v_email_msg);
      v_email_processor.m_save_to_submit_grp(CV_EMAIL_GRP_SMS_PWD, true);
      v_email_processor.m_submit_email_synch();

      return v_ret;

   onError(err)
      v_ret = false;
      return v_ret;
   end;


   /*-----------------------------------------------*/
   /* Достигнут ли максимум попыток отправки пароля */
   /*-----------------------------------------------*/
   macro m_is_over_try()
      var v_ret = true;

      if(v_try_to_submit_cntr >= CV_TRY_TO_SUBMIT_QUANT)
         v_ret = true;
      else
         v_ret = false;
      end;

      return v_ret;
   end; /* macro m_is_over_try() */


   /*----------------------------------------------*/
   /* Удаление информации о временном пароле из БД */
   /*----------------------------------------------*/
   macro m_delete_pwd()
      var v_ret = true;
      var v_sql, v_cmd;

      v_sql =         "DELETE FROM usr_clnt_cdwrd_buf_dbt ";
      v_sql = v_sql + " WHERE t_cw_type = :p_cw_type ";
      v_sql = v_sql + "   AND t_cw_client = :p_cw_client ";
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_cw_type", RSDBP_IN, CV_TYPE_CODE_TMP_PWD);
      v_cmd.addParam("p_cw_client", RSDBP_IN, v_evnt_client_id);
      v_cmd.execute();

      v_cmd.close(); v_cmd = NULL; v_sql = NULL;

      return v_ret;

   onError(err)
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = "Ошибка при удалении информации о временном пароле";
      v_ret = false;
      return v_ret;
   end; /* macro m_delete_pwd() */


   /*----------------------*/
   /* Отправка СМС с кодом */
   /* Инициация потока A5  */
   /*----------------------*/
   macro m_submit_sms_code()
      var v_ret;
      var v_sms_send_result;
      var v_aux_cntr;

      v_sms_send_result = adp_sms_submit_init(v_evnt_rec_id,
                                              v_evnt_client_id,
                                              v_client_phone_for_sms,
                                              CV_TYPE_SMS_PROC,
                                              v_tmp_pwd,
                                              CV_TIMEOUT_SMS_SUBMIT); /* 20191029 - передаем значение времени ожидания */

      if(ValType(v_sms_send_result) == V_GENOBJ)
         v_ret = true;
         v_aux_cntr = 0;
         while((v_ret) and (v_aux_cntr < v_sms_send_result.size))
            if(v_sms_send_result.value(v_aux_cntr).ErrorCode != 0)
               /* Если есть хоть одна ошибка в списке */
               v_ret = false;
            end;

            v_aux_cntr = v_aux_cntr + 1;
         end;
      else
         v_ret = false;
      end;

      return v_ret;

   onError(err)
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = "Ошибка при отправке СМС";
      v_ret = false;
      return v_ret;
   end; /* macro m_submit_sms_code() */


   /*---------------------------------------*/
   /* Поиск значения временного пароля в БД */
   /*---------------------------------------*/
   private macro m_check_code()
      var v_ret = true;
      var v_sql, v_cmd, v_rs;

      v_sql =         "SELECT t_cw_client, t_cw_type, t_code_word, t_cw_accept_try ";
      v_sql = v_sql + "  FROM usr_clnt_cdwrd_buf_dbt ";
      v_sql = v_sql + " WHERE t_cw_type = :p_cw_type ";
      v_sql = v_sql + "   AND t_cw_client = :p_cw_client ";
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_cw_type", RSDBP_IN, CV_TYPE_CODE_TMP_PWD);
      v_cmd.addParam("p_cw_client", RSDBP_IN, v_evnt_client_id);
      v_rs = RSDRecordSet(v_cmd);
      if(v_rs.movenext())
         v_ret = true;
         v_tmp_pwd = v_rs.value("t_code_word");
         v_try_to_submit_cntr = v_rs.value("t_cw_accept_try");
      else
         v_ret = false;
      end;

      v_rs.close(); v_cmd.close(); v_rs = NULL; v_cmd = NULL; v_sql = NULL;

      return v_ret;

   onError(err)
      v_ret = false;
      return v_ret;
   end; /* macro m_make_sms_message() */


   /*--------------------------------------------*/
   /* Сохранение значения временного пароля в БД */
   /*--------------------------------------------*/
   private macro m_save_tmp_pwd()
      const CV_CODE_STATUS = 2; /* Ожидание отправки */

      var v_ret = true;
      var v_sql, v_cmd;

      v_sql =         "BEGIN ";
      v_sql = v_sql + "    :p_result := USR_PKG_IMPORT_SOFR.f_save_code_word_buf(:p_cw_client, ";
      v_sql = v_sql + "                                                          :p_code_word, ";
      v_sql = v_sql + "                                                          :p_code_type, ";
      v_sql = v_sql + "                                                          :p_code_stat, ";
      v_sql = v_sql + "                                                          :p_ret_txt); ";
      v_sql = v_sql + "END; ";
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_result", RSDBP_RETVAL, V_INTEGER);
      v_cmd.addParam("p_cw_client", RSDBP_IN, v_evnt_client_id);
      v_cmd.addParam("p_code_word", RSDBP_IN, v_tmp_pwd);
      v_cmd.addParam("p_code_type", RSDBP_IN, CV_TYPE_CODE_TMP_PWD);
      v_cmd.addParam("p_code_stat", RSDBP_IN, CV_CODE_STATUS);
      v_cmd.addParam("p_ret_txt", RSDBP_OUT, V_STRING, 2000);
      v_cmd.execute();

      if(v_cmd.value("p_result") != 0)
         v_error_code = CV_ERROR_SIGN;
         v_error_desc = v_cmd.value("p_ret_txt");
         v_ret = false;
      end;

      v_cmd.close(); v_cmd = NULL; v_sql = NULL;

      return v_ret;

   onError(err)
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = "Ошибка сохранения временного пароля для повторной отправки";
      v_ret = false;
      return v_ret;
   end; /* macro m_save_tmp_pwd() */


   /*----------------------------------------------------------*/
   /* Обновление количества попыток отправки временного пароля */
   /*----------------------------------------------------------*/
   private macro m_update_tmp_pwd()
      var v_ret = true;
      var v_sql, v_cmd;

      v_sql =         "UPDATE usr_clnt_cdwrd_buf_dbt ";
      v_sql = v_sql + "   SET t_cw_accept_try = :p_cw_accept_try, ";
      v_sql = v_sql + "       t_cw_accept_ts = SYSTIMESTAMP ";
      v_sql = v_sql + " WHERE t_cw_type = :p_cw_type ";
      v_sql = v_sql + "   AND t_cw_client = :p_cw_client ";
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_cw_accept_try", RSDBP_IN, v_try_to_submit_cntr);
      v_cmd.addParam("p_cw_type", RSDBP_IN, CV_TYPE_CODE_TMP_PWD);
      v_cmd.addParam("p_cw_client", RSDBP_IN, v_evnt_client_id);
      v_cmd.execute();

      v_cmd.close(); v_cmd = NULL; v_sql = NULL;

      return v_ret;

   onError(err)
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = "Ошибка обновления информации о временном пароле";
      v_ret = false;
      return v_ret;
   end; /* macro m_update_tmp_pwd() */


   /*---------------------------------------------------------------*/
   /* Проверить существование временного пароля, либо сгенерировать */
   /*---------------------------------------------------------------*/
   macro m_check_or_make_code()
      const CV_PWD_MIN_LEN = 8; /* Внезапные требования вопреки ТЗ */ /*7;*/
      const CV_PWD_MAX_LEN = 8; /* Внезапные требования вопреки ТЗ */ /*128;*/

      var v_ret = true;
      var v_stat = true;
      var v_sql, c_cmd, v_rs;

      if (v_evnt_state == 1/*Новый запрос */ )
         /*Про старый пароль забываем, даже если он был*/
         m_delete_pwd();
      end;

      v_stat = m_check_code();

      /* Если пароль не нашли, то считаем, что он до этого не генерировался */
      if(not v_stat)
         /* Генерируем пароль */
         /* Дистрибутивный API (BankInter) */
         v_stat = GenPassw(@v_tmp_pwd, CV_PWD_MIN_LEN, CV_PWD_MAX_LEN);

         if(v_stat == 0)
            /* Сразу выставляем число попыток отправки с учетом первой */
            v_try_to_submit_cntr = 1;

            /* Сохраняем значение временного пароля в БД */
            v_stat = m_save_tmp_pwd();
//DEF-59521
            v_stat = m_check_code();
            if(not v_stat)
               RunError();
            end;

         else
            RunError();
         end;
      else
         /* Сразу обновляем число повторных отправлений */
         v_try_to_submit_cntr = v_try_to_submit_cntr + 1;
         v_stat = m_update_tmp_pwd();
      end;

      return v_ret;

   onError(err)
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = "Ошибка генерации временного пароля";
      v_ret = false;
      return v_ret;
   end; /* macro m_check_or_make_code() */


   /*-------------------------------------------------------*/
   /* Получение номера мобильного телефона для отправки СМС */
   /*-------------------------------------------------------*/
   private macro m_get_phone_for_sms()
      var v_sql, v_cmd, v_rs;

      /* По регламенту Система должна работать с юр. адресами */
      /* Для единообразия используется следующий запрос, но, возможно, стоило бы его усовершенствовать */
      v_sql =         "SELECT t_value ";
      v_sql = v_sql + "  FROM dcontact_dbt ";
      v_sql = v_sql + " WHERE t_partyid = :p_client_id ";
      v_sql = v_sql + "   AND t_addresstype IN (0,1) "; // юр. адрес или не указан
      v_sql = v_sql + "   AND t_ismain = chr(88) ";     // основной
      v_sql = v_sql + "   AND t_contactkind = :p_contactkind ";
      v_sql = v_sql + "   AND t_contacttype = :p_contacttype ";
      v_sql = v_sql + "ORDER BY t_addresstype DESC ";
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_client_id", RSDBP_IN, v_evnt_client_id);
      v_cmd.addParam("p_contactkind", RSDBP_IN, CV_CONTACT_KIND_PHONE); // телефон
      v_cmd.addParam("p_contacttype", RSDBP_IN, CV_CONTACT_TYPE_MOBILE); // мобильный

      v_rs = RSDRecordSet(v_cmd);

      /* Используем значение из первой строки выборки */
      if(v_rs.movenext())
         /* Считается, что номер телефона хранится в таблице в корректном виде */
         v_client_phone_for_sms = v_rs.value("t_value");
      else
         /* Номер телефона не найден - в ТЗ такой случай не предусмотрен */
         RunError();
      end;

      v_rs.close(); v_cmd.close(); v_rs = NULL; v_cmd = NULL; v_sql = NULL;

   onError(err)
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = "Ошибка поиска мобильного телефона для отправки СМС";
   end; /* macro m_get_phone_for_sms() */

   private macro m_get_state_event(p_recid)
      var v_sql, v_cmd, v_rs;

      v_sql =         "SELECT t_status ";
      v_sql = v_sql + "  FROM UTABLEPROCESSEVENT_DBT ";
      v_sql = v_sql + " WHERE t_recid = :p_recid ";
      v_cmd = RSDCommand(v_sql);
      v_cmd.addParam("p_recid", RSDBP_IN, p_recid);

      v_rs = RSDRecordSet(v_cmd);

      if(v_rs.movenext())
         v_evnt_state = v_rs.value("t_status");
      else
         /* в теории это невозможно, но мало ли */
         RunError();
      end;

      v_rs.close(); v_cmd.close(); v_rs = NULL; v_cmd = NULL; v_sql = NULL;

   onError(err)
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = CV_ERR_DEFAULT;
   end; /* macro m_get_phone_for_sms() */



   /*-------------*/
   /* Конструктор */
   /*-------------*/
   private macro m_init_temp_pwd_proc(i_rec_id, i_client_id)
      v_error_code = CV_ERROR_CLEAR;
      v_error_desc = CV_ERR_CLEAR;
      v_tmp_pwd = "";
      v_try_to_submit_cntr = 0;

      v_evnt_rec_id = i_rec_id;
      v_evnt_client_id = i_client_id;
      m_get_state_event(v_evnt_rec_id);

      if((ValType(v_evnt_rec_id) == V_UNDEF) or (v_evnt_rec_id == 0))
         v_error_code = CV_ERROR_SIGN;
         v_error_desc = CV_ERR_DEFAULT;

      else
         /* Определяем номер телефона для отправки СМС */
         m_get_phone_for_sms();
      end;

   onError(err)
      v_error_code = CV_ERROR_SIGN;
      v_error_desc = CV_ERR_DEFAULT;
   end; /* macro m_init_temp_pwd_proc() */

   m_init_temp_pwd_proc(p_rec_id, p_client_id);
end; /* class c_temp_pwd_proc() */


/*-------------*/
/* Точка входа */
/*-------------*/
macro submit_trade_term_temp_pwd(p_rec_id, p_client_id)
   const CV_STAT_OK = 0;
   const CV_STAT_REPEAT = 1;
   const CV_STAT_ERROR = 2;

   var v_ret : INTEGER = CV_STAT_OK;
   var v_stat = true;
   var v_in_data;

   v_in_data = c_temp_pwd_proc(p_rec_id, p_client_id);

   if(v_in_data.m_get_error_code() != 0)
      /* Пока считаем, что если попали сюда, то пытаемся запустить процесс повторно */
      v_ret = CV_STAT_REPEAT;
      v_stat = false;
   end;

   if(v_stat)
      v_stat = v_in_data.m_check_or_make_code();
   end;

   if(v_stat)
      v_stat = v_in_data.m_submit_sms_code();
   end;

   if(v_stat)
      /* НЕ Удаляем из БД информацию о временном пароле */
//      v_in_data.m_delete_pwd();
      v_ret = CV_STAT_OK;

   else
      if(v_in_data.m_is_over_try())
         /* Отправка e-mail "Не удалось отправить временный пароль клиенту %client_id%" */
         v_in_data.m_sms_fail_email_notif();

         /* Удаляем из БД информацию о временном пароле */
         v_in_data.m_delete_pwd();

         v_ret = CV_STAT_ERROR;
      else
         v_ret = CV_STAT_REPEAT;
      end;
   end;

   return v_ret;

onError(err)
   /* Пока считаем, что если попали сюда, то пытаемся запустить процесс повторно */
   v_ret = CV_STAT_REPEAT;
   return v_ret;
end;