/*-----------------------------------------------------------------------------------------
   LoadNOBFromDepoToSOFR.mac
   Загрузка файла НОБ из Депозитария и создание по нему НОБ
   BIQ-7335
   Гераськина Т.В.
-----------------------------------------------------------------------------------------*/
import rsexts;
import "global_classes.mac";           /* класс ошибок */
import "ws_msg_transform_sec.mac";     /* для обработки ошибок */
import dlmisc;                         /* SplitString*/
import "loadusernptxobj.mac";          /* оббъекты НРД */
import "func_lib";

class Logger()

    macro RepHeader(pFilename)
       var str;
       str =     "                             ПРОТОКОЛ ЗАГРУЗКИ НОБ (начальное решение) \n";
       str = str+"\n";
       str = str+"Дата загрузки  : "+date()+"\n";
       str = str+"Время загрузки : "+time()+"\n";
       str = str+"Обрабатываемый файл : "+pFilename+"\n";
       str = str+"┌──────┬──────────────────────────────────┬──────────────────────────────┬──────────┬──────────────────┬──────────────────┬───────────────┬────────────┬──────────┬─────────────────────────────────────────────────┐\n";
       str = str+"│      │  ФИО                             │Код Клиента                   │ИИС       │NDRTYPE           │NDRDirection      │NDRID          │Сумма       │Результат │Комментарий                                      │\n";
       str = str+"├──────┼──────────────────────────────────┼──────────────────────────────┼──────────┼──────────────────┼──────────────────┼───────────────┼────────────┼──────────┼─────────────────────────────────────────────────┤";
       //toRep1(str);                                                                                                   
       println(str);                                                                                                   
    end;
             
    macro RepFooter(suc, er, tot)                                                       
       var str = "└──────┴──────────────────────────────────┴──────────────────────────────┴──────────┴──────────────────┴──────────────────┴───────────────┴────────────┴──────────┴─────────────────────────────────────────────────┘\n";
       str = str+"Загружено успешно: "+suc+"\n";               
       str = str+"Ошибки обработки : "+er+"\n";
       str = str+"Всего            : "+tot+"\n";
      println(str);
    end;


    macro RepStr(p0, p1, p2, p3, p4, p5, p6, p7, p8, p9)

        var str  = "│"+string(p0   :6)   +"│"+
                  string(p1    :34:l)+"│"+
                  string(p2    :30:l)+"│"+
                  string(p3    :10:l)+"│"+
                  string(p4    :18:l)+"│"+
                  string(p5    :18:l)+"│"+
                  string(p6    :15:l)+"│"+
                  string(p7    :12:l)+"│"+
                  string(p8    :10:l)+"│"+
                  string(p9    :49:l) +"│"; 
       return println(str);
    end;

end;


macro PutFieldsValueIntoTable(_FullFileName)
   var FS;
   var file_line:string, file_line_fields = TArray;
   var DBONUMBER, DATEOPEN, DATECLOSE, FIO, CLIENTID,
       NDRTYPE, NDRDIRECTION, NDRLEVEL, NDRSUM, SECNUM, 
       IIS, NDRID, NOBID, RESULTCODE, RESULTTEXT, T_TIMESTAMP;
   var sql_str, cmd;
   var ErrText;
   
   /* Пример файла Отчет_НОБ_Депозитарий_20211231_104237.txt
   11-111111;01.01.2021;01.03.2021;Иванов Иван Иванович;2010005007544;ProcSec_TS;доход;2;6,000,000.00;RU000A102Y4;;200005007855*/

   // построчная запись файла в таблицу, в транзакции
   rslDefCon.beginTrans();
   
   // очистим таблицу, delete, чтобы можно было откатить
   sql_str = "delete from SOFR_TAXDEPORESULT_DBT";
   cmd = RSDCommand(sql_str);
   cmd.execute;
   cmd.close;
   
   FS = TStreamDoc(_FullFileName, "R", "rsansi"); // только на чтение
   while (FS.readLine(@file_line))
      file_line_fields = SplitString(file_line, ";");  // получим массив полей
      DBONUMBER      = file_line_fields(0);  // 1 Номер ДБО                   Строка   Да   Номер договора БО
      DATEOPEN       = file_line_fields(1);  // 2 Дата открытия               Дата     Да   Дата открытия договора БО формате ДД.ММ.ГГГГ
      DATECLOSE      = file_line_fields(2);  // 3 Дата закрытия               Дата     Да   Дата закрытия договора БО в формате ДД.ММ.ГГГГ
      FIO            = file_line_fields(3);  // 4 ФИО клиента                 Строка   Да   ФИО клиента полностью
      CLIENTID       = file_line_fields(4);  // 5 ID клиента в системе ЦФТ    Строка   Да   
      NDRTYPE        = file_line_fields(5);  // 6 Вид объекта НДР             Строка   Да   Вид НДР для объекта НДР, формируемого в Депозитарии. 
      NDRDIRECTION   = file_line_fields(6);  // 7 Направление                 Строка   Да   Направление объекта НДР, формируемого в Депозитарии. Возможные значения: доход/расход
      NDRLEVEL       = file_line_fields(7);  // 8 Уровень объекта НДР         Строка   Да   Уровень объекта НДР, формируемого в Депозитарии. 
      NDRSUM         = file_line_fields(8);  // 9 Сумма объекта НДР в руб     Число    Да   Сумма выплаты по объекту НДР, приведенная в рубли. Разделитель разрядов-запятая, разделитель рублей и копеек -точка
      SECNUM         = file_line_fields(9);  // 10 ISIN или Госрег№/инф о ПФИ Строка   Да   ISIN ила гос. регистрационный номер ЦБ в разрезе выпусков (если есть) или информация о ПФИ
      IIS            = file_line_fields(10); // 11 Признак ИИС                Строка   Да   Признак наличия ИИС. Возможные значения: Да или пусто (не ИИС)
      NDRID          = file_line_fields(11); // 12 ID объекта НДР             Число    Да   Целочисленный идентификатор объекта НДР (выплаты). Формируется в Diasoft FA#  
      
      NDRSUM = StrSubst(NDRSUM, ",","");
      NDRSUM = Double(NDRSUM);
      If(not IIS)
        IIS = StrFor(1);
      end;

      sql_str = "insert into SOFR_TAXDEPORESULT_DBT(DBONUMBER, DATEOPEN, DATECLOSE, FIO, CLIENTID, "+
                "                                   NDRTYPE, NDRDIRECTION, NDRLEVEL, NDRSUM, SECNUM, "+
                "                                   IIS, NDRID, T_TIMESTAMP, RESULTCODE) "+
                " values(:DBONUMBER, :DATEOPEN, :DATECLOSE, :FIO, :CLIENTID, "+
                "        :NDRTYPE, :NDRDIRECTION, :NDRLEVEL, :NDRSUM, :SECNUM, "+
                "        :IIS, :NDRID, sysdate, '0' )";
      cmd = RSDCommand(sql_str);
      cmd.AddParam("DBONUMBER", RSDBP_IN, DBONUMBER);
      cmd.AddParam("DATEOPEN", RSDBP_IN, DATEOPEN);
      cmd.AddParam("DATECLOSE", RSDBP_IN, DATECLOSE);
      cmd.AddParam("FIO", RSDBP_IN, FIO);
      cmd.AddParam("CLIENTID", RSDBP_IN, CLIENTID);
      cmd.AddParam("NDRTYPE", RSDBP_IN, NDRTYPE);
      cmd.AddParam("NDRDIRECTION", RSDBP_IN, NDRDIRECTION);
      cmd.AddParam("NDRLEVEL", RSDBP_IN, NDRLEVEL);
      cmd.AddParam("NDRSUM", RSDBP_IN, NDRSUM);
      cmd.AddParam("SECNUM", RSDBP_IN, SECNUM);
      cmd.AddParam("IIS", RSDBP_IN, IIS);
      cmd.AddParam("NDRID", RSDBP_IN, NDRID);
      cmd.execute;
   end;
   
   rslDefCon.commitTrans();
   cmd.close; cmd = null;
      
onError(err)
   if ( rslDefCon.isInTrans() )
      rslDefCon.rollbackTrans();
      ErrText = err.message + getFullCmdErrorText(cmd);
      RunError(ErrText, c_err_obj(ErrText,UNIVERSAL_ERROR_CODE));
   end;
end;


macro FindFIID(_code1)
   var sql_str1, rs1, cmd1;
   var res = -1;
   
   sql_str1 = "select t_fiid from davoiriss_dbt where t_isin = :code1";
   cmd1 = RSDCommand(sql_str1);
   cmd1.AddParam("code1", RSDBP_IN, _code1);
   rs1 = RSDRecordSet(cmd1);
   if (rs1.movenext) // код уникальный
      res = rs1.value(0);
   end;
   
   return res;
onError(err)
   return -1;
end;
 
macro CreateNPTXOBJ(_rs)
   var ErrStr = "", err = 0;
   var res_obj = c_Error;
   var PartyId, ObjKind,fiid;
   var v_IIS = false;
   var v_ndrsum;

   if((valtype(_rs.value("clientid")) == v_specval) and (valtype(_rs.value("ndrid")) == v_specval))
      res_obj.ErrorCode = 5;  // ошибка
      res_obj.ErrorDesc = "No Data Found";
      return res_obj;
   end;

   //Аналитические признаки объекта НДР
   //Для объектов выше 4-го уровня можно не заполнять.
   fiid = FindFIID(_rs.value("SECNUM"));
   if (ObjKind == -1)
      ErrStr = "Финансовый инструмент "+_rs.value("SECNUM")+" не найден";
      RunError(ErrStr, c_err_obj(ErrStr, 5));
   end;
  
   var AnaliticData = TObjAnaliticData(0,-1,0,-1,0,-1,0,-1,0,-1,0,-1);
 // = TObjAnaliticData(0,-1,0,-1,3010,fiid,4010,1,5010,20,0,-1);
   
   PartyId = GetPartyID_byPartyCode(PARTY_CODEKIND_ABS, _rs.value("CLIENTID"));
   if (PartyId == -1)
      ErrStr = "Субъект с кодом "+_rs.value("CLIENTID")+" не найден";
 //     RunError(ErrStr, c_err_obj(ErrStr, 5));

      res_obj.ErrorCode = 5;  // ошибка
      res_obj.ErrorDesc = "Субъект с кодом "+_rs.value("CLIENTID")+" не найден";
      return res_obj;
   end;

   if (StrUpr(_rs.value("IIS")) == "ДА")
      v_IIS = true;
   else
      v_IIS = false;
   end;
   
   
   ObjKind = FindObjectKindNRD(_rs.value("NDRTYPE"));
   if (ObjKind == -1)
      ErrStr = "Вид объекта НДР "+_rs.value("NDRTYPE")+" не найден";
      RunError(ErrStr, c_err_obj(ErrStr, 5));
   end;
 /*  if (_rs.value("NDRTYPE") == "ProcSec_TS")
    AnaliticData  = TObjAnaliticData(0,-1,0,-1,3010,fiid,4010,1,5010,20,0,-1);
   else
     AnaliticData = TObjAnaliticData(0,-1,0,-1,0,-1,0,-1,0,-1,0,-1);
   end;
    */ /*PDV 04072021*/

   v_ndrsum = money(_rs.value("NDRSUM"));

   if (v_ndrsum > 0) // для нулевой суммы вставки не происходит и ошибку не возвращает
      err = InsertOrChangeUserNPTXOBJ(PartyId,                       //Идентификатор клиента
                                      ObjKind,                       //Вид объекта НДР
                                      {curdate},                     //Дата объекта
                                      v_ndrsum,                      //Сумма
                                      NATCUR,                        //Валюта суммы
                                      "DEPO",                        //Код внешней системы
                                      _rs.value("NDRID"),            //Идентификатор во внешней системе
                                      _rs.value("DBONUMBER"),        //Комментарий для объекта
                                      AnaliticData,                  //Информация по аналитикам
                                      v_IIS,                         //Признак ИИС
                                      @ErrStr                        //Возвращаемая строка с текстом ошибки
                                     );
   else
      err = 0;
      ErrStr = "Сумма равна нулю";
   end;

   if(err)
      res_obj.ErrorCode = 5;  // ошибка
      res_obj.ErrorDesc = ErrStr;
   else
      res_obj.ErrorCode = 0;
      res_obj.ErrorDesc = "OK";
   end;
   return res_obj;
onError(err)
   res_obj = c_Error;
   res_obj.ErrorCode = c_err_obj.obtain_err_code(err);
   res_obj.ErrorDesc = c_err_obj.obtain_err_msg(err);

   return res_obj;
end;

macro LoadNOBFromDepoToSOFR()
   const REG_IMPORT_PATH = "РСХБ\\ДИРЕКТОРИИ\\IN\\НОБ_ДЕПО"; //путь для загрузки файла
   const REG_IMPORT_ARCH_PATH = "РСХБ\\ДИРЕКТОРИИ\\IN\\НОБ_ДЕПО_АРХИВ"; 
   var File_mask = "Отчет_НОБ_Депозитарий_*.txt"; // Отчет_НОБ_Депозитарий_ГГГГММДД_ЧЧMMCC.txt, пример: Отчет_НОБ_Депозитарий_20211231_104237.txt
   var Src_Path;
   var Arch_Path;
   var Dest_Path = "..\\TxtFile"; 
   var FileName = "", FullFileName = "", FS;
   var ErrCode, ErrText;
   var DL;
   var out_obj = c_Error;
   var sql_str, rs, sql_upd, cmd_upd;
   var sql_log, cmd_log;
   var create_result;

   //получим значение пути загрузки из реестра
   GetRegistryValue(REG_IMPORT_PATH, V_STRING, Src_Path, ErrCode);
   if( ErrCode > 0 )
      Src_Path = "..\\Import\\In";
   end;
   GetRegistryValue(REG_IMPORT_ARCH_PATH, V_STRING, Arch_Path, ErrCode);
   if( ErrCode > 0 )
      Arch_Path = "..\\Import\\Archive";
   end;
   
   // в директории уже должен находится файл, поэтому считаем, что директория существует
   DL = TDirList; 
   DL.List(Src_Path+"\\"+File_mask, "F");
   DL.Sort(2); // сортировка по дате модификации, хотя файл должен быть один
   if (DL.Count == 0)               // файлов нет
      ErrText = "Файлы "+Src_Path+"\\"+File_mask+" отсутствуют";
      RunError(ErrText, c_err_obj(ErrText,UNIVERSAL_ERROR_CODE));
   else 
      FileName = DL.name(0); // первый по порядку сортировки, если вдруг несколько
      DL.Copy(Src_Path+"\\"+FileName, "", Dest_Path+"\\", false, false, ""); //скопируем к себе без индикатора
      if (DL.IsCopy(0))
         FullFileName = Dest_Path+"\\"+FileName;
      else
         ErrText = "Файл "+Src_Path+"\\"+FileName+" не удалось скопировать";
         RunError(ErrText, c_err_obj(ErrText,UNIVERSAL_ERROR_CODE));
      end;
   end;
   BegAction(null,"Обработка файла");
   PutFieldsValueIntoTable(FullFileName);
   EndAction();
   // если не выпали в ошибку, продолжаем
   sql_str = "select rowid as rn, DBONUMBER, DATEOPEN, DATECLOSE, FIO, CLIENTID, "+
             "       NDRTYPE, NDRDIRECTION, NDRLEVEL, NDRSUM, SECNUM, "+
             "       IIS, to_char(NDRID) NDRID "+
             "  from SOFR_TAXDEPORESULT_DBT ";
   rs = RSDRecordSet(sql_str);
   sql_upd = "update SOFR_TAXDEPORESULT_DBT set RESULTCODE = :code, RESULTTEXT = :text, T_TIMESTAMP = sysdate WHERE rowid = :rn";

   sql_log = String("insert into  SOFR_TAXDEPORESULT_LOG (DBONUMBER, DATEOPEN, DATECLOSE, FIO, CLIENTID, NDRTYPE, NDRDIRECTION,   \n"
                   ,"              NDRLEVEL, NDRSUM, SECNUM, IIS, NDRID, NOBID, RESULTCODE, RESULTTEXT, T_TIMESTAMP, T_FILENAME)   \n"
                   ,"select t.*, ?  as t_filename from SOFR_TAXDEPORESULT_DBT t where rowid = ?  ;   \n");

   var log = Logger();
   var dt = StrSubSt(string(Date()),".","");
   var tm = StrSubSt(string(time()),":","");
   var InfoLogFileName = GetTxtFileName( "!load_"+dt+"_"+StrSubst(tm," ","0") + "_" + FileName);
   var n_load = 0, n_err = 0;
   SetOutput( InfoLogFileName, true ); //Пишем лог в файл. Начало
   log.RepHeader(FileName);
   var  sql_pr = "select count(1) as row_cnt"+
             "  from SOFR_TAXDEPORESULT_DBT ";
   var rs_pr = RSDRecordSet(sql_pr);
   rs_pr.MoveNext();
   InitProgress (int(rs_pr.value("row_cnt")));
   UseProgress (0);

   while (rs.movenext)
      create_result = CreateNPTXOBJ(rs);
      cmd_upd = RSDCommand(sql_upd);    // здесь без транзакции, как обработается, так обработается
      cmd_upd.AddParam("code", RSDBP_IN, create_result.ErrorCode);
      cmd_upd.AddParam("text", RSDBP_IN, create_result.ErrorDesc);
      cmd_upd.AddParam("rn", RSDBP_IN, rs.value("rn"));
      cmd_upd.execute;
      cmd_upd.close;
      if(create_result.ErrorCode == 0)
        n_load =  n_load + 1;
      else
        n_err = n_err + 1;
      end;
//  сохраняем результат
      cmd_log = RSDCommand(sql_log);
      cmd_log.AddParam("filename", RSDBP_IN, FileName);
      cmd_log.AddParam("rn", RSDBP_IN, rs.value("rn"));
      cmd_log.execute;
      cmd_log.close;

      log.RepStr (n_load + n_err ,rs.value("FIO"), rs.value("CLIENTID"),rs.value("IIS"),rs.value("NDRTYPE"),rs.value("NDRDIRECTION"),rs.value("NDRID"),rs.value("NDRSUM"),create_result.ErrorCode,create_result.ErrorDesc);
      UseProgress (n_load + n_err);
   end;
      RemProgress (n_load + n_err);
      log.RepFooter(n_load,n_err,n_load + n_err);
      SetOutput( NULL, true ); //Пишем лог в файл. Конец
      ViewFile( InfoLogFileName );
   // переносим в архив исходный файл
   DL.Copy(Src_Path+"\\"+FileName, "", Arch_Path+"\\", true, false, ""); 
   if (DL.IsCopy(0))
   else
      ErrText = "Файл "+Src_Path+"\\"+FileName+" не удалось перенести в архив";
      RunError(ErrText, c_err_obj(ErrText,UNIVERSAL_ERROR_CODE));
   end;
   // удаляем у себя
   DL.Remove(FullFileName, "F"); 

   out_obj.ErrorCode = 0;
   out_obj.ErrorDesc = "OK";

   return out_obj;

onError(err)
   out_obj = c_Error;
   out_obj.ErrorCode = c_err_obj.obtain_err_code(err);
   out_obj.ErrorDesc = c_err_obj.obtain_err_msg(err);

   return out_obj;
end;

LoadNOBFromDepoToSOFR;