/*-----------------------------------------------------*/
/* Запрос ссылок по Субъекту ФЛ                        */
/*                                                     */
/* Автор: Гераськина Т.                                */
/*
*/
/*----------------------------------------------*/
import "secur_prc_msg_transform.mac";     /* Функционал преобразования сообщений */
import "RequestWS.mac";                   /* Функция передачи запроса в Web Sphere */
import "uws_exchng_log.mac";              /* Функционал логирования */
import func_lib;

class c_PersonGetXrefRequest(Incomedata) 
   var PartyUID  ; //: c_IntegrationSymbolicIdentifierXType;    //Глобальный идентификатор
   var OperationalExternalId :c_IntegrationSymbolicIdentifierXType;

   //PartyUID = c_PartyUID(Incomedata);
   
   private macro uInitPrime(IncomeData)
      var aux_buf;
      if(ValType(IncomeData) != V_UNDEF)
         aux_buf = uTryToGetPropS(IncomeData, "OperationalExternalId");
         if (ValType(aux_buf) != V_UNDEF)
            OperationalExternalId = c_IntegrationSymbolicIdentifierXType;
            OperationalExternalId.SystemId = uTryToGetPropS(aux_buf, "SystemId");
            OperationalExternalId.SystemNodeId = uTryToGetPropS(aux_buf, "SystemNodeId");;
            OperationalExternalId.ObjectId = uTryToGetPropS(aux_buf, "ObjectId");;
         end;
      end;
   end;
   
   uInitPrime(IncomeData); 
      
end;

/* Корневой элемент ответа - AS ID - как пришел */
private class adp_PersonGetXrefResponse(IncomeData)
   private class c_Error
      var ErrorCode :String;  // Код результата       Нет      Идентификатор
      var ErrorDesc :String;  // Поясняющий текст     Нет
   end;

   private class c_NPXRef(IncomeData)
      var OperationalDataId;

      private macro uInitPrime(IncomeData)
         var aux_buff;
         aux_buff = uTryToGetPropS(IncomeData, "OperationalDataId");
         if (ValType(aux_buff) != V_UNDEF)
            OperationalDataId = c_IntegrationSymbolicIdentifierXType;
            OperationalDataId.ObjectId = uTryToGetPropS(aux_buff, "ObjectId");
            OperationalDataId.SystemId = uTryToGetPropS(aux_buff, "SystemId");
            OperationalDataId.SystemNodeId = uTryToGetPropS(aux_buff, "SystemNodeId");
         end;
      end;
      
      uInitPrime(IncomeData);
   end;
   
   private class c_PersonGetXrefResponse
      var NPXRef = TArray; // Кросс-ссылка клиента ФЛ
      var MasterDataId;    // Идентификатор мастер-данных
   end;
   
   var Error = c_Error;
   var PersonGetXrefResponse = c_PersonGetXrefResponse;         // Список: Ссылки
   
   /* Кратность параметров в соответствии со схемой/спецификацией */
   private macro uInitPrime(IncomeData)
      var err_txt;
      var aux_buff, aux_buff2, ai;

      if(ValType(IncomeData) != V_UNDEF)
         aux_buff = uTryToGetPropS(IncomeData, "Error");
         if (ValType(aux_buff) != V_UNDEF)
            Error.ErrorCode = uTryToGetPropS(aux_buff, "ErrorCode");
            Error.ErrorDesc = uTryToGetPropS(aux_buff, "ErrorDesc");
         end;
            
         aux_buff = uTryToGetPropS(IncomeData, "PersonGetXrefResponse");
         if(ValType(aux_buff) == V_GENOBJ)
            aux_buff2 = uTryToGetPropS(aux_buff, "NPXRef");
            if (ValType(aux_buff2) != V_UNDEF)
               aux_buff2 = 0;
               while(aux_buff.NPXRef.size > aux_buff2)
                  ai = PersonGetXrefResponse.NPXRef.size;
                  PersonGetXrefResponse.NPXRef.value(ai) = c_NPXRef(aux_buff.NPXRef.value(aux_buff2));
                  aux_buff2 = aux_buff2 + 1;
               end;
            end;
            aux_buff2 = uTryToGetPropS(aux_buff, "MasterDataId");
            if (ValType(aux_buff2) != V_UNDEF)
               PersonGetXrefResponse.MasterDataId = c_IntegrationSymbolicIdentifierXType;
               PersonGetXrefResponse.MasterDataId.ObjectId = uTryToGetPropS(aux_buff2, "ObjectId");
               PersonGetXrefResponse.MasterDataId.SystemId = uTryToGetPropS(aux_buff2, "SystemId");
               PersonGetXrefResponse.MasterDataId.SystemNodeId = uTryToGetPropS(aux_buff2, "SystemNodeId");
            end;
         end;  
         
      end;
   end;
   
   macro GetSystemXref(_SystemId)
      var res = c_IntegrationSymbolicIdentifierXType;
      res.ObjectId = "-1";
      res.SystemId = "-1";
      res.SystemNodeId = "-1";
      
      var NPXRef;
      
      for (NPXRef, PersonGetXrefResponse.NPXRef)
         if ( Valtype(NPXRef.OperationalDataId) != V_UNDEF )
            if ( (Valtype(NPXRef.OperationalDataId.SystemId) != V_UNDEF) and (NPXRef.OperationalDataId.SystemId == _SystemId) )
               res.ObjectId = NPXRef.OperationalDataId.ObjectId;
               res.SystemId = NPXRef.OperationalDataId.SystemId;
               res.SystemNodeId = NPXRef.OperationalDataId.SystemNodeId;
            end;
         end;
      end;
      return res;
   end;

   uInitPrime(IncomeData);
end; 


/* Основной класс работы с сервисом */
private class c_PersonGetXrefProcessor(IncomeData)
   var PersonGetXrefRequest; /* !!! По названию корневого тега запроса !!! */
   PersonGetXrefRequest = c_PersonGetXrefRequest(IncomeData);
end; 

/*-------------------------------------------------------------------*/
/* Адаптер для вызова сервиса "Запрос ссылок по Субъекту ФЛ"         */
/*-------------------------------------------------------------------*/
macro run_PersonGetXref(ReqData, p_log_id, p_transparent_id)
   var RespData = NULL;
   var err_txt = NULL;
   
   private var v_out_obj;
   private var RequestText;
   private var v_transformator;
   private var v_answer;
   private var v_answer_data;
   private var v_logger_obj;
   private var v_log_id;
   private var xml_str;
   private var v_xml_rpc_reqid = XmlRpcRequestId(); /* 20181229 - kva - чтобы дважды не вызывать функцию */

   if ( (ReqData == NULL) or (ValType(ReqData) == V_UNDEF) )
      return null;
   end;

   v_transformator = c_secur_msg_converter();

   // заполнение класса информацией 
   v_out_obj = c_PersonGetXrefProcessor(ReqData);
   RequestText = v_transformator.m_secur_internal_resp(v_out_obj);
   
   v_logger_obj = uws_exchng_logger(null, p_transparent_id, v_xml_rpc_reqid, "PersonGetXrefRequest", modulename(), null, RequestText);
   v_log_id = v_logger_obj.get_log_id();

      
   err_txt = v_transformator.m_validate_out_xml(RequestText);
   if (err_txt == NULL)
        v_answer = SubmitRequestToWS( 2,            // ИД очереди.
                                       "",          // ИД сообщения.
                                       "",          // Идентификатор Бэк Офиса. (не обрабатывается).
                                       true,        // Признак синхронной обработки. Если не указан, = False
                                       "SOFR",      // Подразделение системы-отпр
                                       10,          // ИД типа данных
                                       RequestText, // Бизнес данные в виде XML
                                       v_log_id,    // ID usr_exchng_log
                                       "CIF"
                                       );
         if (v_answer.ResultCode == 0)
            xml_str = v_answer.responsetext;
            v_logger_obj.add_response(xml_str); 
            
            v_transformator = NULL;
            if (xml_str)
               /* Преобразуем запрос в объектный вид - begin */
               v_transformator = c_secur_msg_converter();
               if(not v_transformator.m_decode_escaped_char(xml_str))
                  RunError(v_transformator.get_service_msg(), c_usr_err_obj(v_transformator.get_service_msg()));
               end;
               v_answer_data = v_transformator.m_secur_wol_internal_req(v_transformator.get_pure_msg());
               /* Преобразуем запрос в объектный вид - end */

               if(ValType(v_answer_data) == V_UNDEF)
                  err_txt = "Не удалось преобразовать XML в объект";
                  RunError(err_txt, c_usr_err_obj(err_txt)); 
               end;
               
               RespData = adp_PersonGetXrefResponse(v_answer_data);
               /* Преобразование объекта к виду, ожидаемому Системой */
            end;
         else 
            v_logger_obj.add_error_info(v_answer.ResultCode, v_answer.ResultText);
         end;

   else
      v_logger_obj.add_error_info(UNIVERSAL_ERROR_CODE, err_txt);
   end;
         
   return RespData;

onError(err)
   return NULL;
end; /* macro ExportStatusDeal(ReqData) */


/*---------------------------------------*/
/*              Точка входа              */
/*---------------------------------------*/
/* ReqData - данные для отправки         */
/*---------------------------------------*/
macro PersonGetXref (ReqData, in_log_id, in_transparent_id) /* 20190103 - kva - добавлены параметры логирования */
   return run_PersonGetXref(ReqData, in_log_id, in_transparent_id); /* 20190103 - kva - добавлены параметры логирования */
end;

