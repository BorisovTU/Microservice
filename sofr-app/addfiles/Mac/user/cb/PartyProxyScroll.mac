/**
 @file PartyProxyScroll.mac
 @brief Скроллинг представителей субъекта (dptproxy_dbt)
 
 Файл содержит скроллинг представителей субъекта (dptproxy_dbt)
 
  # tag
 - functional_block:GUI
 - Представители
 
  # changeLog
 |date       |author         |tasks                                                     |note                                                        
 |-----------|---------------|----------------------------------------------------------|-------------------------------------------------------------
 |29.10.2025 |Топорков Д.В.  |AVT_BOSS-1099                                             | Создание
 */
import RSD, BankInter, RsbFormsInter;
//import "KondorFunc.mac";

private const FIELD_TYPE_READONLY = 2;
private const K_SPACE = 32,
              K_F2    = 316,
              K_F5    = 319,
              K_F8    = 322;
private const READONLY = true;

private macro IIF(condition, valueOnTrue, valueOnFalse)
  if (condition)
    return valueOnTrue;
  end;
  
  return valueOnFalse;
end;

/**
@brief Базовый класс для создания скроллинга
*/
class CRslScroll
  private var columns;

  private macro GetColumnsCount(): Integer
    return columns.size / 6;
  end;

  private macro Add(Value)
    columns.Value(columns.Size) = Value;
  end;

  macro AddColumn (field: String, caption: String, width: Integer, type: Integer, decPoint: Integer)
    Add(field);
    Add(caption);
    Add(width);
    Add(type); 
    Add(decPoint);
    Add(0);  // Резерв
  end;

  macro EventProc (rs, cmd, id, key)
  end;  

  macro Run (rs: Object, scrollUniqueName: String, scrollCaption: String, statusLine: String, isReadOnly: Bool)
    return RunScroll (rs, GetColumnsCount(), columns, scrollUniqueName, R2M(this,"EventProc"), scrollCaption, statusLine, isReadOnly, -1, -1, -1, -1);
  end;

  //----- конструктор -------
  columns = TArray();
end; //CRslScroll

/**
@brief Класс скроллинга для таблицы dPtProxy_dbt
*/
class (CRslScroll) CPartyProxyScroll(pPartyID: Integer)
  private var partyID = pPartyID;
  private var query: String = "";
  private var rs, cmd;

  private macro GetPartyName()
    var cmd = RsdCommand("SELECT t_Name FROM dParty_dbt where t_partyID = :partyid");
    
    cmd.AddParam("partyid", RSDBP_IN, partyID);
    
    var rs = RsdRecordset(cmd);
    
    if (rs.MoveNext())
      return rs.Value("t_Name");
    end;
    
    return "";
  end;

  macro EventProc (rs, cmd, id, key)
    var CM_FLAG = CM_DEFAULT;

    return CM_FLAG;
  end; 
  
  macro Run()
    query = "SELECT party.t_Name, proxy.t_DocNumber, proxy.t_DocDate, proxy.t_ValidityDate, proxy.t_DocDesc, proxy.t_isDefault, proxy.t_isSoleExecutive, " +
                  "(SELECT partcode.t_Code FROM dPartCode_dbt partcode WHERE partcode.t_CodeKind=1 AND partcode.t_PartyID = proxy.t_ProxyID AND rownum = 1) as t_Code, " +
                  "COALESCE(llvalues.t_Name, CHR(1)) as t_postName " +
              "FROM dPtProxy_dbt proxy " +
              "LEFT JOIN dParty_dbt party ON proxy.t_ProxyID = party.t_PartyID " +
              "LEFT JOIN dLLValues_dbt llvalues ON llvalues.t_List=1128 AND llvalues.t_Element=proxy.t_Post ";
    if ((ValType(partyID) == V_INTEGER) and (partyID > 0))
      query = query + "WHERE proxy.t_partyID = :partyid";
    end;
    
    cmd = RsdCommand(query);
    cmd.AddParam("partyid", RSDBP_IN, partyID);
    
    rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
    return this.Run(rs, "PartyProxyScroll", "Представители субъекта " + GetPartyName(), "~Alt+Shift+F11~ Экспорт в Excel     ~Esc~ Выход", READONLY);
  end;
  
  //----- конструктор -------
  InitCRslScroll();
  
  AddColumn("t_Code",            "Код",                  15, FIELD_TYPE_READONLY, 0);
  AddColumn("t_Name",            "Наименование",         30, FIELD_TYPE_READONLY, 0);
  AddColumn("t_isDefault",       "У",                     1, FIELD_TYPE_READONLY, 0);
  AddColumn("t_isSoleExecutive", "Е",                     1, FIELD_TYPE_READONLY, 0);
  AddColumn("t_postName",        "Должность",            15, FIELD_TYPE_READONLY, 0);
  AddColumn("t_docnumber",       "Документ",             15, FIELD_TYPE_READONLY, 0);
  AddColumn("t_docdate",         "Дата",                 15, FIELD_TYPE_READONLY, 0);
  AddColumn("t_validitydate",    "Срок действия",        15, FIELD_TYPE_READONLY, 0);
  AddColumn("t_docdesc",         "Сведения о документе", 80, FIELD_TYPE_READONLY, 0);
end; //CPartyProxyScroll