import payminter;
import rsd, RsbDataSet;
import RsbFormsInter;
import captureoutput;
import globals;
private var fill, dt1, dt2, chk_errors, chk_reject, chk_control, chk_others, chk_secur, chk_dv, chk_v, chk_mbk;

const РолиОткатаПлатежей = "10019, 10020, 1";

/*Выбор клиентов*/
macro payments_controll(indt1, indt2, inchk_errors, inchk_reject, inchk_control, inchk_others, inchk_secur, inchk_dv, inchk_v, inchk_mbk)
  var FileName, Err;
  var nameapp;
  if(valtype(indt1) != V_DATE)
    dt1 = date(0,0,0);
  else
    dt1 = indt1;
  end;
  if(valtype(indt2) != V_DATE)
    dt2 = date(0,0,0);
  else
    dt2 = indt2;
  end;
  if(valtype(inchk_errors) != V_BOOL)
    chk_errors = true;
  else
    chk_errors = inchk_errors;
  end;
  if(valtype(inchk_reject) != V_BOOL)
    chk_reject = true;
  else
    chk_reject = inchk_reject;
  end;
  if(valtype(inchk_control) != V_BOOL)
    chk_control = true;
  else
    chk_control = inchk_control;
  end;
  if(valtype(inchk_others) != V_BOOL)
    chk_others = false;
  else
    chk_others = inchk_others;
  end;
  if(valtype(inchk_secur) != V_BOOL)
    chk_secur = true;
  else
    chk_secur = inchk_secur;
  end;
  if(valtype(inchk_dv) != V_BOOL)
    chk_dv = true;
  else
    chk_dv = inchk_dv;
  end;
  if(valtype(inchk_v) != V_BOOL)
    chk_v = true;
  else
    chk_v = inchk_v;
  end;
  if(valtype(inchk_mbk) != V_BOOL)
    chk_mbk = true;
  else
    chk_mbk = inchk_mbk;
  end;
  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, FileName, Err);
  FileName = FileName + "\\swift." + UserNumber;

  /**/
  macro makeArray():TArray
    var parm:variant, i:integer = 0, result:TArray = TArray( ParmCount(), ParmCount() );
    while( GetParm(i, parm) )
      result.value(i) = parm;
      i = i + 1;
    end;
    return result;
  end;

  /* Доступ - через роль */
  macro IsAdminOper(opernum)
    var cmd, res, rs;
//    cmd = RsdCommand("SELECT * FROM dacsgroupoper_dbt a WHERE A.T_GROUPID = " + ГруппаОткатаПлатежей + " AND A.T_OPER = " + opernum);
    cmd = RsdCommand("SELECT * FROM dacsoprole_dbt a WHERE A.T_ROLEID IN (" + РолиОткатаПлатежей + ") AND A.T_OPER = " + opernum);
    cmd.Execute();
    rs = TRsbDataSet(cmd);
    if(rs.MoveNext())
       return true;
    else
       return false;
    end;

/* ВР.
    if((opernum != 1) and (opernum != 1019))
      return false;
    else
      return true;
    end;
*/
  end;

  /*Информационная панель*/
  class (TRsbPanel) tInfoPanel(paymentid, operationid)
    /*Конструктор*/
    macro InitInfoPanel(paymentid, operationid)
      var control, rows = 0, cmdInfo, rsInfo;
      var nrows = 16;
      var PaymentObj = RsbPayment(paymentid);
      var RejectMes = substr(PaymentObj.Notes.ReadNote(PM_NOTEKIND_DENIALGROUND, {curdate}), 1, 200);
      if(strlen(RejectMes) == 0)
        nrows = nrows - 6;
      end;
      StartCaptureOutput();
      [ 
        select pmpaym.t_dockind t_dockind, 
               nvl(oprkdoc.t_name, chr(1)) t_dockindname, 
               pmpaym.t_documentid t_documentid, 
               nvl(oproper.t_id_operation, 0) t_id_operation, 
               nvl(oproper.t_kind_operation, 0) t_kind_operation, 
               nvl(oprkoper.t_name, chr(1)) t_kind_operation_name
        from dpmpaym_dbt pmpaym
        left join doproper_dbt oproper on oproper.t_dockind = pmpaym.t_dockind and 
                                          oproper.t_documentid = lpad(pmpaym.t_documentid, 34, '0')
        left join doprkoper_dbt oprkoper on oprkoper.t_dockind = oproper.t_dockind and 
                                            oprkoper.t_kind_operation = oproper.t_kind_operation
        left join doprkdoc_dbt oprkdoc on oprkdoc.t_dockind = pmpaym.t_dockind
        where pmpaym.t_paymentid = ?
      ];
      cmdInfo = RsdCommand(EndCaptureOutput());
      cmdInfo.AddParam("paymentid", RSDBP_IN, paymentid);
      rsInfo = RSDRecordset(cmdInfo, RSDVAL_CLIENT, RSDVAL_STATIC);
      if(not rsInfo.movenext)
        rsInfo = null;
        nrows = nrows - 8;
      end;
      /*Параметры панели*/
      SetSize(45, nrows); 
      SetPosition(10, 5);
      SetStatus("~Esc~ Выход"); 
      SetCaption("Информация");
      /*Информация по платежу*/
      addLabel(TRsbLabel(1, rows, "Платеж"));
      rows = rows + 1;
      /*Платеж*/
      addLabel(TRsbLabel(3, rows, "id платежа:"));
      control = TRsbEditField;
      control.name = "paymentid";
      control.dataType = 1/*FT_INT*/;
      control.textLength = 15;
      control.setPosition(15, rows);
      control.setSize(30, 1);
      control.editable = false;
      control.enabled = true;
      control.value = paymentid;
      addControl(control);
      rows = rows + 1;
      /*Операция*/
      addLabel(TRsbLabel(3, rows, "id операции:"));
      control = TRsbEditField;
      control.name = "operationid";
      control.dataType = 1/*FT_INT*/;
      control.textLength = 15;
      control.setPosition(15, rows);
      control.setSize(30, 1);
      control.editable = false;
      control.enabled = true;
      control.value = operationid;
      addControl(control);
      rows = rows + 1;
      /*Данные по первичному документу и операции*/
      if(rsInfo != null)
        /*Информация по первичному документу*/
        addLabel(TRsbLabel(1, rows, "Первичный документ"));
        rows = rows + 1;
        /*Операция*/
        addLabel(TRsbLabel(3, rows, "id документа:"));
        control = TRsbEditField;
        control.name = "t_documentid";
        control.dataType = 1/*FT_INT*/;
        control.textLength = 15;
        control.setPosition(15, rows);
        control.setSize(30, 1);
        control.editable = false;
        control.enabled = true;
        control.value = int(rsInfo.value("t_documentid"));
        addControl(control);
        rows = rows + 1;
        /*Код*/
        addLabel(TRsbLabel(3, rows, "Вид документа:"));
        control = TRsbEditField;
        control.name = "t_dockind";
        control.dataType = 1/*FT_INT*/;
        control.textLength = 15;
        control.setPosition(15, rows);
        control.setSize(30, 1);
        control.editable = false;
        control.enabled = true;
        control.value = int(rsInfo.value("t_dockind"));
        addControl(control);
        rows = rows + 1;
        /*Наименование*/
        addLabel(TRsbLabel(3, rows, "Наименование:"));
        control = TRsbEditField;
        control.name = "t_dockindname";
        control.dataType = 7/*FT_STRING*/;
        control.textLength = 150;
        control.setPosition(15, rows);
        control.setSize(30, 1);
        control.editable = false;
        control.enabled = true;
        control.value = rsInfo.value("t_dockindname");
        addControl(control);
        rows = rows + 1;
        /*Информация по операции первичного документа*/
        addLabel(TRsbLabel(1, rows, "Операция по первичному документу"));
        rows = rows + 1;
        /*Операция*/
        addLabel(TRsbLabel(3, rows, "id операции:"));
        control = TRsbEditField;
        control.name = "t_id_operation";
        control.dataType = 1/*FT_INT*/;
        control.textLength = 15;
        control.setPosition(15, rows);
        control.setSize(30, 1);
        control.editable = false;
        control.enabled = true;
        control.value = int(rsInfo.value("t_id_operation"));
        addControl(control);
        rows = rows + 1;
        /*Код*/
        addLabel(TRsbLabel(3, rows, "Вид операции:"));
        control = TRsbEditField;
        control.name = "t_kind_operation";
        control.dataType = 1/*FT_INT*/;
        control.textLength = 15;
        control.setPosition(15, rows);
        control.setSize(30, 1);
        control.editable = false;
        control.enabled = true;
        control.value = int(rsInfo.value("t_kind_operation"));
        addControl(control);
        rows = rows + 1;
        /*Наименование*/
        addLabel(TRsbLabel(3, rows, "Наименование:"));
        control = TRsbEditField;
        control.name = "t_kind_operation_name";
        control.dataType = 7/*FT_STRING*/;
        control.textLength = 150;
        control.setPosition(15, rows);
        control.setSize(30, 1);
        control.editable = false;
        control.enabled = true;
        control.value = rsInfo.value("t_kind_operation_name");
        addControl(control);
        rows = rows + 1;
      end;
      /*Причина отказа*/
      if(strlen(RejectMes) > 0)
        addLabel(TRsbLabel(1, rows, "Причина отказа"));
        rows = rows + 1;
        control = TRsbEditField;
        control.name = "t_kind_operation_name";
        control.dataType = 7/*FT_STRING*/;
        control.textLength = 200;
        control.setPosition(1, rows);
        control.setSize(44, 5);
        control.editable = false;
        control.enabled = true;
        control.value = RejectMes;
        addControl(control);
      end;
    end;
    /*Обработчик нажатия кнопок*/
    macro eventHandlerKP(ev)
      if(ev.keycode == 27/*Esc*/)
        close(ev.keycode);
        return true;
      end;
    end;
    /*Конструктор*/
    InitTRsbPanel();
    InitInfoPanel(paymentid, operationid);
    /*Обработчики*/
    addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandlerKP"));
  end;
 
  /*Панель фильтра*/
  class (TRsbPanel) tFillPanel()
    /*Конструктор*/
    macro InitFillPanel()
      var control, rows = 0;
      /*Параметры панели*/
      SetSize(29, 11); 
      SetPosition(10, 5);
      SetStatus("~F2~ Применить ~Esc~ Выход"); 
      SetCaption("Фильтр платежей");
      /*Период даты валютирования*/
      addLabel(TRsbLabel(1, rows, "Дата валютирования:"));
      rows = rows + 1;
      addLabel(TRsbLabel(2, rows, "с"));
      control = TRsbEditField;
      control.name = "dt1";
      control.dataType = 9/*FT_DATE*/;
      control.textLength = 10;
      control.setPosition(4, rows);
      control.setSize(10, 1);
      control.editable = true;
      control.enabled = true;
      control.value = dt1;
      addControl(control);
      addLabel(TRsbLabel(15, rows, "по"));
      control = TRsbEditField;
      control.name = "dt2";
      control.dataType = 9/*FT_DATE*/;
      control.textLength = 10;
      control.setPosition(18, rows);
      control.setSize(10, 1);
      control.editable = true;
      control.enabled = true;
      control.value = dt2;
      addControl(control);
      rows = rows + 1;
      /*Состояния*/
      addLabel(TRsbLabel(1, rows, "Состояние:"));
      rows = rows + 1;
      /*Ошибки*/
      control = TRsbCheckBox(1);
      control.name = "chk_errors";
      control.dataType = 12; /*FT_CHR*/
      control.setPosition(3, rows);
      control.checked = chk_errors;
      addControl(control);
      addLabel(TRsbLabel(5, rows, "с ошибкой"));
      rows = rows + 1;
      /*Отвергнутые*/
      control = TRsbCheckBox(1);
      control.name = "chk_reject";
      control.dataType = 12; /*FT_CHR*/
      control.setPosition(3, rows);
      control.checked = chk_reject;
      addControl(control);
      addLabel(TRsbLabel(5, rows, "отвергнутые"));
      rows = rows + 1;
      /*Для контроля*/
      control = TRsbCheckBox(1);
      control.name = "chk_control";
      control.dataType = 12; /*FT_CHR*/
      control.setPosition(3, rows);
      control.checked = chk_control;
      addControl(control);
      addLabel(TRsbLabel(5, rows, "для контроля"));
      rows = rows + 1;
      /*Прочие*/
      control = TRsbCheckBox(1);
      control.name = "chk_others";
      control.dataType = 12; /*FT_CHR*/
      control.setPosition(3, rows);
      control.checked = chk_others;
      addControl(control);
      addLabel(TRsbLabel(5, rows, "прочие"));
      rows = rows + 1;
      /*Бэк-офис*/
      addLabel(TRsbLabel(1, rows, "Бэк-офис:"));
      rows = rows + 1;
      /*БОЦБ*/
      control = TRsbCheckBox(1);
      control.name = "chk_secur";
      control.dataType = 12; /*FT_CHR*/
      control.setPosition(3, rows);
      control.checked = chk_secur;
      addControl(control);
      addLabel(TRsbLabel(5, rows, "БОЦБ"));
      rows = rows + 1;
      /*ФИССиКО*/
      control = TRsbCheckBox(1);
      control.name = "chk_dv";
      control.dataType = 12; /*FT_CHR*/
      control.setPosition(3, rows);
      control.checked = chk_dv;
      addControl(control);
      addLabel(TRsbLabel(5, rows, "ФИССиКО"));
      rows = rows + 1;
      /*Векселя*/
      control = TRsbCheckBox(1);
      control.name = "chk_v";
      control.dataType = 12; /*FT_CHR*/
      control.setPosition(3, rows);
      control.checked = chk_v;
      addControl(control);
      addLabel(TRsbLabel(5, rows, "Векселя"));
      rows = rows + 1;
      /*МБК*/
      control = TRsbCheckBox(1);
      control.name = "chk_mbk";
      control.dataType = 12; /*FT_CHR*/
      control.setPosition(3, rows);
      control.checked = chk_mbk;
      addControl(control);
      addLabel(TRsbLabel(5, rows, "МБК"));
      rows = rows + 1;
    end;
    /*Обработчик смены фокуса*/
    macro eventHandlerRF(ev)
      if((this.focusedControl.name == "dt1") or (this.focusedControl.name == "dt2"))
        if((getControl("dt1").value != date(0,0,0)) and (getControl("dt2").value != date(0,0,0)) and (getControl("dt2").value < getControl("dt1").value))
          msgboxex("Дата окончания периода не может быть меньше даты начала");
          if(this.focusedControl.name == "dt1")
            getControl("dt1").value = getControl("dt2").value;
          else
            getControl("dt2").value = getControl("dt1").value;
          end;
        end;
      end;
      return true;
    end;
    /*Обработчик нажатия кнопок*/
    macro eventHandlerKP(ev)
      if(ev.keycode == 27/*Esc*/)
        fill = false;
        close(ev.keycode);
        return true;
      elif(ev.keycode == 316/*F2*/) 
        eventHandlerRF(ev);
        dt1 = getControl("dt1").value;
        dt2 = getControl("dt2").value;
        chk_errors = getControl("chk_errors").checked;
        chk_reject = getControl("chk_reject").checked;
        chk_control = getControl("chk_control").checked;
        chk_others = getControl("chk_others").checked;
        chk_secur = getControl("chk_secur").checked;
        chk_dv = getControl("chk_dv").checked;
        chk_v = getControl("chk_v").checked;
        chk_mbk = getControl("chk_mbk").checked;
        fill = true;
        close(ev.keycode);
        return true;
      end;
    end;
    /*Конструктор*/
    InitTRsbPanel();
    InitFillPanel();
    /*Обработчики*/
    addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandlerKP"));
    addEventHandler(RSB_EV_REMOVE_FOCUS, r2m(this, "eventHandlerRF"));  
  end;

  /*Контроль платежа*/
  macro ExecuteControl(paymentid, dockind, errmsg:@string) 
    var cmd, res, rs;
    /*Добавляем платеж для испольнения*/
    cmd = RsdCommand("begin "
                   + "  delete from dpmrunopr_tmp; "
                   + "  insert into dpmrunopr_tmp values(?,?,0,chr(1)); "
                   + "end;");
    cmd.AddParam("paymentid", RSDBP_IN, paymentid);
    cmd.AddParam("dockind", RSDBP_IN, dockind);
    cmd.Execute();
    /*Запускаем шаг*/
    res = PM_MassExecuteOperation(true);
    if(res != 0)
      errmsg = "Ошибка при выполеннии операции " + res;
      return false;
    end;
    /*Проверяем результат*/
    cmd = RsdCommand("select t_state, t_errormessage from dpmrunopr_tmp where t_state != 0");
    cmd.Execute();
    rs = TRsbDataSet(cmd);
    if(rs.MoveNext())
      errmsg = "Ошибка при выполнении контроля: " + rs.value("t_errormessage") + " (" + rs.value("t_state") + ")";
      return false;
    end;
    return true;
  end;

  /*Откат всех шагов по операции платежа*/
  macro RollBackOperation(paymentid, id_operation, errmsg:@string) 
    var cmd, rs, res;

    StartCaptureOutput();
    [ 
      select oprstep.t_id_step t_id_step
      from doproper_dbt oproper
      join dpmpaym_dbt pmpaym on pmpaym.t_paymentid = oproper.t_documentid and
                                                      pmpaym.t_paymentid = ?
      join doprstep_dbt oprstep on oprstep.t_id_operation = oproper.t_id_operation and 
                                   oprstep.t_isexecute = chr(88)
      where oproper.t_id_operation = ?
      order by oprstep.t_id_step desc
    ];
    cmd = RsdCommand(EndCaptureOutput());
    cmd.AddParam("paymentid", RSDBP_IN, paymentid);
    cmd.AddParam("id_operation", RSDBP_IN, id_operation);
    rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
    while(rs.movenext)
      res = PM_RollbackStep(paymentid, id_operation, rs.value("t_id_step"), "Н");
      if(res != 0)
        errmsg = "Ошибка при откате шага " + rs.value("t_id_step") + " " + res;
        return false;
      end;
    end;
    return true;
  end;

  /*Получение актуальных данных по платежу*/
  macro GetActualRow(paymentid, id_operation)
    var cmd;

    StartCaptureOutput();
    [ 
      select t_status, 
             case when t_status = 'Отвергнут' then 0 
                  when t_status = 'Для контроля' then 1 
                  when t_status = 'Для выгрузки' then 2 
                  when t_status = 'Для подтверждения' then 3 
                  when t_status = 'Подтвержден' then 4 
                  else -1 end t_prior 
      from(select case when nvl(oprstep_for_execute.t_symbol, chr(1)) = 'К' then 
                         case when oprstep_last_executed.t_symbol is null then 'Для контроля' else 'Ошибка состояния для контроля' end
                       when nvl(oprstep_for_execute.t_symbol, chr(1)) = 'В' then 
                         case when nvl(oprstep_last_executed.t_symbol,chr(0)) = 'К' then 'Для выгрузки' else 'Ошибка состояния для выгрузки' end
                       when nvl(oprstep_for_execute.t_symbol, chr(1)) = 'П' then 
                         case when nvl(oprstep_last_executed.t_symbol,chr(0)) = 'В' then 'Для подтверждения' else 'Ошибка состояния для подтверждения' end
                       when oprstep_for_execute.t_symbol is null then 
                         case when nvl(oprstep_last_executed.t_symbol,chr(0)) = 'П' then case when p.t_paymstatus = 32000 then 'Подтвержден' when p.t_paymstatus = 100 then 'Отвергнут' else 'Ошибка состояния подтвержден/отвергнут' end else 'Ошибка состояния подтвержден' end else '???' end t_status
           from (select oproper.t_id_operation,
                        pmpaym.t_paymstatus t_paymstatus,
                        (select nvl(min(t_id_step),0) from doprstep_dbt oprstep where oprstep.t_id_operation = oproper.t_id_operation and oprstep.t_isexecute = 'R') t_id_step_for_execute,
                        (select nvl(max(t_id_step),0) from doprstep_dbt oprstep where oprstep.t_id_operation = oproper.t_id_operation and oprstep.t_isexecute = chr(88)) t_id_last_executed_step
                 from doproper_dbt oproper 
                 join dpmpaym_dbt pmpaym on pmpaym.t_paymentid = oproper.t_documentid and
                                            pmpaym.t_paymentid = ?
                 where oproper.t_id_operation = ?) p
           left join doprstep_dbt oprstep_for_execute on oprstep_for_execute.t_id_operation = p.t_id_operation and 
                                                         oprstep_for_execute.t_id_step = p.t_id_step_for_execute
           left join doprstep_dbt oprstep_last_executed on oprstep_last_executed.t_id_operation = p.t_id_operation and 
                                                           oprstep_last_executed.t_id_step =  p.t_id_last_executed_step)
    ];
    cmd = RsdCommand(EndCaptureOutput());
    cmd.AddParam("paymentid", RSDBP_IN, paymentid);
    cmd.AddParam("id_operation", RSDBP_IN, id_operation);
    return RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  end;

  /*Внешний валютный платеж*/
  macro PaymentIsOutCur(paymentid)
    var cmd, rs;
    StartCaptureOutput();
    [ 
      select count(1) t_cnt
      from dpmpaym_dbt pmpaym 
      where pmpaym.t_paymentid = ? and
            (pmpaym.t_fiid_futurepayacc != 0 or pmpaym.t_fiid_futurerecacc != 0) and
            (exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = pmpaym.t_payerbankid) or
             exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = pmpaym.t_payer)) and
            not exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = pmpaym.t_receiverbankid)
    ];
    cmd = RsdCommand(EndCaptureOutput());
    cmd.AddParam("paymentid", RSDBP_IN, paymentid);
    rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
    rs.movenext();
    if(rs.value("t_cnt") > 0)
      return true;
    else
      return false;
    end;
  end;

  /*Подготовка данных*/
  macro PrepareData()
    var cmd, f_dt1 = "", f_dt2 = "", f_chk_status = "", f_chk_bo = "";
    nameapp = "";
    if(dt1 != date(0,0,0))
      nameapp = nameapp + " c " + dt1;
      f_dt1 = " pmpaym.t_valuedate >= ? and ";
    end;
    if(dt2 != date(0,0,0))
      nameapp = nameapp + " по " + dt2;
      f_dt2 = " pmpaym.t_valuedate <= ? and ";
    end;
    if((chk_errors) and (chk_reject) and (chk_control) and (chk_others))
      f_chk_status = " 1 = 1 and ";
    elif(not chk_errors)
      if(chk_reject)
        f_chk_status = " (t_status = 'Отвергнут' ";
      end;
      if(chk_control)
        if(f_chk_status == "")
          f_chk_status = " (t_status = 'Для контроля' ";
        else
          f_chk_status = f_chk_status + " or t_status = 'Для контроля' ";
        end;
      end;
      if(chk_others)
        if(f_chk_status == "")
          f_chk_status = " (t_status in ('Для выгрузки', 'Для подтверждения', 'Подтвержден') ";
        else
          f_chk_status = f_chk_status + " or t_status in ('Для выгрузки', 'Для подтверждения', 'Подтвержден') ";
        end;
      end;
      if(f_chk_status != "")
        f_chk_status = f_chk_status + ") and "
      else
        f_chk_status = " 1 = 0 and "
      end
    else
      if(not chk_reject)
        f_chk_status = " t_status != 'Отвергнут' and ";
      end;
      if(not chk_control)
        f_chk_status = f_chk_status + " t_status != 'Для контроля' and ";
      end;
      if(not chk_others)
        f_chk_status = f_chk_status + " t_status not in ('Для выгрузки', 'Для подтверждения', 'Подтвержден') and ";
      end;
    end;
    if((chk_secur) and (chk_dv) and (chk_v) and (chk_mbk))
      f_chk_bo = " 1 = 1 and "
    else
      if(chk_secur)
        f_chk_bo = " t_paymdockind in (101, 107, 4607, 176";
      end;
      if(chk_dv)
        if(f_chk_bo == "")
          f_chk_bo = " t_paymdockind in (194, 199, 4813, 140, 4811, 4815, 4627";
        else
          f_chk_bo = f_chk_bo + ", 194, 199, 4813, 140, 4811, 4815, 4627";
        end;
      end;
      if(chk_v)
        if(f_chk_bo == "")
          f_chk_bo = " t_paymdockind in (141, 110";
        else
          f_chk_bo = f_chk_bo + ", 141, 110";
        end;
      end;
      if(chk_mbk)
        if(f_chk_bo == "")
          f_chk_bo = " t_paymdockind in (102";
        else
          f_chk_bo = f_chk_bo + ", 102";
        end;
      end;
      if(f_chk_bo == "")
        f_chk_bo = " 1 = 0 and "
      else
        f_chk_bo = f_chk_bo + ") and ";
      end;
    end;

    StartCaptureOutput();
    [ 
      begin
        delete from dset_payment_u_tmp;
        insert into dset_payment_u_tmp
        select t_paymentid, t_dockind, t_valuedate, t_amount, t_note, t_id_operation, t_symbol_for_execute, t_last_executed_symbol, t_status, t_payername, t_receivername, t_isswift, 
               case when t_status = 'Отвергнут' then 0 
                    when t_status = 'Для контроля' then 1 
                    when t_status = 'Для выгрузки' then 2 
                    when t_status = 'Для подтверждения' then 3 
                    when t_status = 'Подтвержден' then 4 
                    else -1 end t_prior,
               t_ccy
        from(select p.t_paymentid, 
                    p.t_dockind, 
                    p.t_valuedate, 
                    nvl(paymfiid.t_ccy,chr(1)) t_ccy,
                    p.t_amount, 
                    t_note,
                    p.t_id_operation, 
                    oprstep_for_execute.t_symbol t_symbol_for_execute, 
                    oprstep_last_executed.t_symbol t_last_executed_symbol, 
                    case when nvl(oprstep_for_execute.t_symbol, chr(1)) = 'К' then 
                           case when oprstep_last_executed.t_symbol is null then 'Для контроля' else 'Ошибка состояния для контроля' end
                         when nvl(oprstep_for_execute.t_symbol, chr(1)) = 'В' then 
                           case when nvl(oprstep_last_executed.t_symbol,chr(0)) = 'К' then 'Для выгрузки' else 'Ошибка состояния для выгрузки' end
                         when nvl(oprstep_for_execute.t_symbol, chr(1)) = 'П' then 
                           case when nvl(oprstep_last_executed.t_symbol,chr(0)) = 'В' then 'Для подтверждения' else 'Ошибка состояния для подтверждения' end
                         when oprstep_for_execute.t_symbol is null then 
                           case when nvl(oprstep_last_executed.t_symbol,chr(0)) = 'П' then
                                  case when p.t_paymstatus = 32000 then 'Подтвержден' when p.t_paymstatus = 100 then 'Отвергнут' else 'Ошибка состояния подтвержден/отвергнут' end
                           else 'Ошибка состояния подтвержден' end
                         else '???' end t_status,
                    nvl(pmrmprop.t_payername, chr(1)) t_payername,
                    nvl(pmrmprop.t_receivername, chr(1)) t_receivername,
                    case when wlpm_u.t_paymentid is not null then 'X' else chr(0) end t_isswift,
                    p.t_paymdockind
             from (select pmpaym.t_paymentid t_paymentid, 
                          oproper.t_dockind t_dockind, 
                          pmpaym.t_valuedate t_valuedate, 
                          pmpaym.t_fiid t_paymfiid,
                          pmpaym.t_amount t_amount, 
                          nvl(oprkdoc.t_name, chr(1)) t_note, 
                          oproper.t_id_operation t_id_operation, 
                          pmpaym.t_paymstatus t_paymstatus,
                          pmpaym.t_dockind t_paymdockind, 
                          (select nvl(min(t_id_step),0) from doprstep_dbt oprstep where oprstep.t_id_operation = oproper.t_id_operation and oprstep.t_isexecute = 'R') t_id_step_for_execute,
                          (select nvl(max(t_id_step),0) from doprstep_dbt oprstep where oprstep.t_id_operation = oproper.t_id_operation and oprstep.t_isexecute = chr(88)) t_id_last_executed_step
                   from doproper_dbt oproper 
                   join dpmpaym_dbt pmpaym on pmpaym.t_paymentid = oproper.t_documentid and
                                              ##############################
                                              ##############################
                                              (exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = pmpaym.t_payerbankid) or
                                               exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = pmpaym.t_payer) and
                                              not exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = pmpaym.t_receiverbankid)) and
                                              pmpaym.t_chapter = 1 and
                                              pmpaym.t_netting = chr(0)
                   left join doprkdoc_dbt oprkdoc on oprkdoc.t_dockind = pmpaym.t_dockind 
                   where (oproper.t_kind_operation = ########## and 
                          oproper.t_dockind = #####) or
                         (pmpaym.t_dockind = ##### and 

                          pmpaym.t_paymstatus = 3000)) p
             left join dpmrmprop_dbt pmrmprop on pmrmprop.t_paymentid = p.t_paymentid
             left join dwlpm_u_dbt wlpm_u on wlpm_u.t_paymentid = p.t_paymentid 
             left join doprstep_dbt oprstep_for_execute on oprstep_for_execute.t_id_operation = p.t_id_operation and 
                                                           oprstep_for_execute.t_id_step = p.t_id_step_for_execute
             left join doprstep_dbt oprstep_last_executed on oprstep_last_executed.t_id_operation = p.t_id_operation and 
                                                             oprstep_last_executed.t_id_step = p.t_id_last_executed_step
             left join dfininstr_dbt paymfiid on paymfiid.t_fiid = p.t_paymfiid)
        where ########################################################################################################################################################################################################################################################
              ########################################################################################################################################################################################################################################################
              1 = 1;
      end;
    ](f_dt1, f_dt2, 14001, 450, 4627, f_chk_status, f_chk_bo);
    cmd = RsdCommand(EndCaptureOutput());
    if(f_dt1 != "")
      cmd.AddParam("dt1", RSDBP_IN, dt1);
    end;
    if(f_dt2 != "")
      cmd.AddParam("dt2", RSDBP_IN, dt2);
    end;
    cmd.Execute();
  end;

  /*Обработчик скроллинга*/
  macro Payments_Scroll(rs, cmd, id, key)
    var errmsg, ars;
    /*Обновить строку*/
    macro RefreshRow()
      ars = GetActualRow(rs.value("t_paymentid"), rs.value("t_id_operation"));
      if(not ars.movenext())
        msgboxex("Данные по текущему платежу не найдены");
      end;
      var upd;
      if((chk_errors) and (chk_reject) and (chk_control) and (chk_others))
        upd = true;
      elif(ars.value("t_status") == "Отвергнут")
        upd = chk_reject;
      elif(ars.value("t_status") == "Для контроля")
        upd = chk_control;
      elif((ars.value("t_status") == "Для выгрузки") or (ars.value("t_status") == "Для подтверждения") or (ars.value("t_status") == "Подтвержден"))
        upd = chk_others;
      else
        upd = chk_errors;
      end;
      if(upd)
        rs.edit();
        rs.value("t_status") = ars.value("t_status");
        rs.value("t_prior") = ars.value("t_prior");
        rs.update();
      else
        rs.delete();
      end;
      updatescroll(rs, 5);
    end;
    /*Обработчик*/
    if(cmd == DLG_KEY)
      /*Enter*/
      if(key == 13)
        if(rs.RecCount > 0)
          var PaymentObj = RsbPayment(rs.value("t_paymentid"));
          PM_ProcessPanel(PaymentObj, 0);
        end;
        return CM_IGNORE;
      /*Esc*/
      elif(key == 27)
        return CM_CANCEL;
      /*F2*/
      elif(key == 316)
        if(rs.RecCount > 0)
          if(rs.value("t_status") != "Для контроля")
            msgboxex("Проконтролированным может быть только платеж в состоянии 'Для контроля'");
            return CM_IGNORE;
          end;
          /*Внешний валютный платеж должен содержать SWIFT-сообщение*/
          if((rs.value("t_isswift") != "X") and (PaymentIsOutCur(rs.value("t_paymentid"))))
            msgboxex("Внешний валютный платеж должен содержать SWIFT-сообщение!");
            return CM_IGNORE;
          end;
          ars = GetActualRow(rs.value("t_paymentid"), rs.value("t_id_operation"));
          if(not ars.movenext())
            msgboxex("Данные по текущему платежу не найдены");
            return CM_IGNORE;
          end;
          if((rs.value("t_status") != ars.value("t_status")) or (rs.value("t_prior") != ars.value("t_prior")))
            msgboxex("Состояние платежа изменилось|Обновите список");
            return CM_IGNORE;
          end;
          if(ExecuteControl(rs.value("t_paymentid"), rs.value("t_dockind"), @errmsg))
            RefreshRow();
            return CM_IGNORE;
          else
            msgboxex("Ошибка при контроле " + errmsg);
            return CM_IGNORE;
          end;
        end;
      /*F5*/
      elif(key == 319)
        var pf = tFillPanel();
        pf.run();
        if(fill)
          return CM_SELECT;
        end;
      /*F8*/
      elif(key == 322)
        if(rs.RecCount > 0)
          if(msgboxex("Вы действительно хотите отложить платеж?", MB_YES + MB_NO, IND_NO) == IND_YES)
            ars = GetActualRow(rs.value("t_paymentid"), rs.value("t_id_operation"));
            if(not ars.movenext())
              msgboxex("Данные по текущему платежу не найдены");
              return CM_IGNORE;
            end;
            if((rs.value("t_status") != ars.value("t_status")) or (rs.value("t_prior") != ars.value("t_prior")))
              msgboxex("Состояние платежа изменилось|Обновите список");
              return CM_IGNORE;
            end;
            if((not IsAdminOper({oper})) and (ars.value("t_status") != "Отвергнут"))
/* ВР.
              msgboxex("Отложить можно только отвергнутый платеж");
*/
              msgboxex("У вас нет прав на откат платежа. Обратитесь к администратору");
              return CM_IGNORE;
            end;
            if(RollBackOperation(rs.value("t_paymentid"), rs.value("t_id_operation"), @errmsg))
              RefreshRow();
              return CM_IGNORE;
            else
              msgboxex("Ошибка при откладывании платежа " + errmsg);
              return CM_IGNORE;
            end;
          end;
        end;
        return CM_IGNORE;
      /*Alt+R*/
      elif(key == 275)
        return CM_SELECT;
      /*Alt+S*/
      elif(key == 287)
        if(rs.RecCount > 0)
          if(rs.value("t_isswift") != "X")
            msgboxex("По платежу SWIFT-сообщение не формировалось");
          else
            StartCaptureOutput();
            [ 
              select ':'||wltpfld.t_name || ':'||wlmesval.t_value t_mes 
              from dwlpm_u_dbt wlpm_u 
              join dwlpm_dbt wlpm on wlpm.t_paymentid = wlpm_u.t_paymentid 
              join dwlmeslnk_dbt wlmeslnk on wlmeslnk.t_objid = wlpm.t_wlpmid and wlmeslnk.t_objkind = 501/*Платеж*/ 
              join dwlmesval_dbt wlmesval on wlmesval.t_mesid = wlmeslnk.t_mesid                                     
              join dwltpfld_dbt wltpfld on wltpfld.t_tpfieldid = wlmesval.t_tpfieldid
              where wlpm_u.t_paymentid = ?                                
              order by wlpm_u.t_paymentid, wlmesval.t_index        
            ];
            var cmd_mes, rs_mes;
            cmd_mes = RsdCommand(EndCaptureOutput());
            cmd_mes.AddParam("paymentid", RSDBP_IN, rs.value("t_paymentid"));
            rs_mes = RSDRecordset(cmd_mes, RSDVAL_CLIENT, RSDVAL_STATIC);
            SetOutput(FileName);
            while(rs_mes.movenext)
              println(rs_mes.value("t_mes"));
            end;
            SetOutput();
            ViewFile(FileName);
          end;
        end;
        return CM_IGNORE;
      /*Alt+I*/
      elif(key == 279)
        if(rs.RecCount > 0)
          var pi = tInfoPanel(rs.value("t_paymentid"), rs.value("t_id_operation"));
          pi.run();
        end;
      end;
    end;
  end;

  /*Точка входа*/
  var que, rs, cmd;
  PrepareData();
  que = "select * from dset_payment_u_tmp order by t_prior, t_paymentdate, t_paymentsum";
  cmd = RsdCommand(que);
  rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  while(RunScroll(rs, 8, MakeArray("t_status", "Состояние", 15, 2, -1, 0,
                                   "t_paymentdate", "Дата", 9, 2, -1, 0,  
                                   "t_ccy", "Валюта", 3, 2, -1, 0,
                                   "t_paymentsum", "Сумма", 15, 2, 2, 0, 
                                   "t_payername", "Плательщик", 40, 2, -1, 0, 
                                   "t_receivername", "Получатель", 40, 2, -1, 0, 
                                   "t_note", "Первичный документ", 20, 2, -1, 0,
                                   "t_isswift", "S", 2, 2, -1, 0), 
                  null, "Payments_Scroll", "Исходящие платежи бэк-офисов" + nameapp, "~F5~ Фильтр ~F2~ Контроль ~F8~ Отложить ~Enter~ Платеж ~Alt+S~ SWIFT-сообщение ~Alt+R~ Обновить список ~Alt+I~ Информация ~Esc~ Выход", true))
    PrepareData();
    cmd = RsdCommand(que);
    rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  end;
end;
