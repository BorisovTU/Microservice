/*
$Name:             impruonia.mac
$Module:           Главная книга 
$Description:      Макрос загрузки котировок RUONIA
*/

import  XmlRpcInter, ServiceAction, FIInter, BankInter, globals, lib_str;
import RSD;

private var Index_FI_Code = "";
private var Index_FIID = -10;
private const psfx_priv = " п";
private const psfx_razm = " р";

private class TIndexRate( rateVal, tp ) // tp: 1-привлечение, 2-размещение
  //properties
  var m_Vname:string;
  var m_Vnom:integer;
  var m_Vcurs;
  var m_Vcode:string;
  var m_VchCode:string;
  
  var m_FI_Code:string;
  var m_Capture:string;
  var m_bRsRateFound:bool;

  file isocur( "isocur.dbt" ) key 1;

  //constructor
  m_Vnom = 1;
  if(rateVal)
    m_Vcurs = rateVal;
  else
    m_Vcurs = "";
  end;
  m_Vcode = "";
  m_VchCode = "";
  if (tp==1)
    m_VchCode = m_FI_Code = Index_FI_Code + psfx_priv;
  else
    m_VchCode = m_FI_Code = Index_FI_Code + psfx_razm;
  end;
  m_Capture = "";

  m_bRsRateFound = false;

  var cmd = RsdCommand("select t_iso_number, t_name from dfininstr_dbt t where t.t_fi_code = :fi_code");
  cmd.addParam( "fi_code", RSDBP_IN, m_FI_Code );
  cmd.execute;
  var RS = RsdRecordset(cmd);
  if ( RS.moveNext )
    m_Vcode = rs.value("t_iso_number");
    m_Vname = rs.value("t_name");
  end;

  //methods
  macro isEqual( ratedef )
    if( (m_Vcurs == ratedef.Rate) and (m_Vnom == ratedef.Scale) and (NATCUR == ratedef.FIID) )
      return true;
    else
      return false;
    end;
  end;

  macro getPoint()
    var Point = 0;
    var i = Index( string(m_Vcurs), "." );
    if( i > 0 )
      Point = strlen( string(m_Vcurs) ) - i;
    end;
    return Point;
  end;

  macro printLogLine()
[├──────┼──────────┼───────────────┼───────────┼─────────┼────────────────────────────────────────────┤
 │ ###  │ ######## │###############│ ######### │ ####### │############################################│]
 (m_Vcode, m_VchCode, m_Vname:w, m_Vcurs, m_Vnom, m_Capture:w);
  end;

end;

private macro AreRatedefsEquals( rd1, rd2 )

  if( (rd1.IsInverse == rd2.IsInverse) or
      (rd1.Rate == rd2.Rate) or 
      (rd1.Scale == rd2.Scale) or
      (rd1.Point == rd2.Point)
    )
    return true;
  else
    return false;
  end;
end;

private macro decideIfNewRateNeeded( impOnDate, ratedef, indexRate:TIndexRate )
  
  var bNewRateNeeded = false;
  record curratedef( "ratedef.dbt" );
  copy( curratedef, ratedef );
  var onSysDate = date();  
  
  if( ПолучитьЗначениеКурса(ratedef, impOnDate) != 0 )
    indexRate.m_Capture = "Импортировано новое значение курса в историю.";
    bNewRateNeeded = true;
  else
    if( indexRate.isEqual(ratedef) )
      indexRate.m_Capture = "Значение курса равно импортируемому, в импорте нет необходимости.";
    else
      if( impOnDate > onSysDate )
        indexRate.m_Capture = "Импортировано новое будущее значение курса.";
        bNewRateNeeded = true;
      elif( ПолучитьЗначениеКурса(curratedef, OnSysDate) == 0 )
        var err = 0;
        indexRate.m_Capture = "Импортировано новое действующее значение курса взамен ранее имевшегося.";
        bNewRateNeeded = true;
        /*
        if( AreRatedefsEquals(ratedef,curratedef) )
          GetRegistryValue( "CB\\FINTOOLS\\ИМПОРТ_КУРСОВ_С_САЙТА_ЦБ\\ЗАМЕЩАТЬ_ДЕЙСТВУЮЩИЙ_КУРС", V_BOOL, bNewRateNeeded, err );
          if( bNewRateNeeded == false )
            indexRate.m_Capture = "Действующее значение курса не равно импортируемому, ";
            indexRate.m_Capture = indexRate.m_Capture + "однако импорт действующего значения запрещен настройкой реестра.";
          else
            indexRate.m_Capture = "Импортировано новое действующее значение курса взамен ранее имевшегося.";
          end;
        else
          GetRegistryValue( "CB\\FINTOOLS\\ИМПОРТ_КУРСОВ_С_САЙТА_ЦБ\\ЗАМЕЩАТЬ_ИСТОРИЧЕСКИЙ_КУРС", V_BOOL, bNewRateNeeded, err );
          if( bNewRateNeeded == false )
            indexRate.m_Capture = "Значение курса в истории не равно импортируемому, ";
            indexRate.m_Capture = indexRate.m_Capture + "однако импорт исторического значения запрещен настройкой реестра.";
          else
            indexRate.m_Capture = "Импортировано новое значение курса в историю взамен ранее имевшегося.";
          end;
        end;
        */
        if( err != 0 )
          bNewRateNeeded = false;
        end;
      end;
    end;
  end;
  return bNewRateNeeded;
end;

private macro getErrorDesc( errCode )
  var errDesc = "";
  if( errCode != 0 )
    errDesc = "Ошибка установки котировки:";

    if( errCode == 1 )
      errDesc = errDesc + " Не найден котируемый финансовый инструмент.";
    elif( errCode == 2 )
      errDesc = errDesc + " Не найден базовый финансовый инструмент.";
    elif( errCode == 3 )
      errDesc = errDesc + " Не найден вид курса для текущего финансового инструмента.";
    elif( errCode == 98 )
      errDesc = errDesc + " Котировка на данную или более позднюю дату уже введена";
    elif( errCode == 99 )
      errDesc = errDesc + " Курс за данную дату уже установлен.";
    elif( errCode > 100 )
      errDesc = errDesc + " Ошибка Btrieve № " + errCode - 100;
    else
      errDesc = errDesc + " Описание ошибки отсутствует";
    end;
  end;

  return errDesc;
end;

private macro getFIID(fi_code)
  var cmd = RsdCommand("select t_fiid from dfininstr_dbt t where t.t_fi_code = :fi_code");
  cmd.addParam( "fi_code", RSDBP_IN, fi_code );
  cmd.execute;
  var RS = RsdRecordset(cmd);
  if ( RS.moveNext )
    return rs.value("t_fiid");
  end;
  return -1;
end;

private macro УстановитьКотировку( rp );
  var OtherFI, rateid;

  OtherFI = getFIID(rp.OtherFI);
  if ( OtherFI == -1 )
    return 1;
  end;

  // Ищем запись в dratedef_dbt
  var cmd = RsdCommand("select t_rateid, t_sincedate from dratedef_dbt t where t.t_fiid = :fiid and t.t_otherfi = :otherfi");
  cmd.addParam( "fiid", RSDBP_IN, rp.FI_Code );
  cmd.addParam( "otherfi", RSDBP_IN, OtherFI );
  cmd.execute;
  var RS = RsdRecordset(cmd);

  if ( not RS.moveNext )
    // Если записи нет, то создаём её
    // Не реализовано
    return 99;
  elif ( rs.value("t_sincedate") >= rp.SinceDate )
    // пишем только свежие курсы
    return 98;
  else
    rateid = rs.value("t_rateid");
  end;  

  // вставляем курс

  // И переносим в историю dratehist_dbt
  cmd = RsdCommand("insert into dratehist_dbt select t_rateid, t_isinverse, t_rate, t_scale, t_point, t_inputdate, t_inputtime, t_oper, t_sincedate, t_ismanualinput from dratedef_dbt t where t.t_rateid=:rateid");
  cmd.addParam( "rateid", RSDBP_IN, rateid );
  cmd.execute;

  // Новый курс сохраняем в dratedef_dbt 
  cmd = RsdCommand("update dratedef_dbt " +
                   "   set t_rate = :rate," +
                   "       t_sincedate = :sincedate," +
                   "       t_ismanualinput = chr(0)," +
                   "       t_inputdate = trunc(sysdate)," +
                   "       t_inputtime = to_date('01.01.0001 '||to_char(sysdate,'HH24:MI:SS'),'DD.MM.YYYY HH24:MI:SS')" +
                   " where t_rateid = :rateid");
  cmd.addParam( "Rate", RSDBP_IN, rp.Rate );
  cmd.addParam( "SinceDate", RSDBP_IN, rp.SinceDate );
  cmd.addParam( "rateid", RSDBP_IN, rateid );
  cmd.execute;

  return 0;
end;

private macro insertNewRate( indexRate, ratedef, realOnDate, ВидКурса )

  record rp( rateparm );  //RATEPARM

  if( indexRate.m_bRsRateFound == true )
    if( ratedef.FIID == Index_FIID )
      rp.FI_Code   = Index_FIID;
      rp.OtherFI   = indexRate.m_FI_Code;
      rp.IsInverse = "";
    else
      rp.FI_Code   = indexRate.m_FI_Code;
      rp.OtherFI   = Index_FIID;
      rp.IsInverse = "X";
    end;
    rp.Type      = ratedef.Type;
  else
    rp.FI_Code   = Index_FIID;
    rp.OtherFI   = indexRate.m_FI_Code;
    rp.Type      = ВидКурса;
    rp.IsInverse = "";

    indexRate.m_Capture = indexRate.m_Capture + " Курс для этого индекса отсутствовал, создан.";
  end;

  rp.SinceDate = realOnDate;
  rp.Point     = indexRate.getPoint();
  rp.Rate      = double(indexRate.m_Vcurs) * pow( 10, rp.Point );
  rp.Scale     = indexRate.m_Vnom;
  rp.IsDominant = "";

  var errCode = УстановитьКотировку( rp );

  if( errCode != 0 )
    indexRate.m_Capture = getErrorDesc( errCode );
  else
    if( indexRate.m_Capture == "" )
      indexRate.m_Capture = indexRate.m_Capture + " Курс введен.";
    end;
  end;

  indexRate.printLogLine();
end;

// загрузка полученных значений курса с сайта ЦБ в RS-Bank
private macro transferOneRateToBase( impOnDate, realOnDate, ВидКурса, indexRate:TIndexRate )

  var bNewRateNeeded = false;

  record rd( "ratedef.dbt" );

  if( ПолучитьКурс( rd, Index_FIID, getFIID(indexRate.m_FI_Code), ВидКурса ) != 0 )
    bNewRateNeeded = true;
    indexRate.m_bRsRateFound = false;
  else //	если котировка найдена, то решаем переписывать ее или нет
    bNewRateNeeded = decideIfNewRateNeeded( impOnDate, rd, indexRate );
    indexRate.m_bRsRateFound = true;
  end;

  if( bNewRateNeeded )      
    insertNewRate( indexRate, rd, realOnDate, ВидКурса );
  else
    indexRate.printLogLine();
  end;

end;

private macro dateFromStr( strDate )
  return date( int(substr(strDate, 7, 8)), int(substr(strDate, 5, 2)), int(substr(strDate, 1, 4)) );
end;

private macro dateToStr( onDate )
  var S = "", dd, mm, yy;
  DateSplit(onDate, dd, mm, yy);

  // год
  S = S + yy + "-";

  // месяц
  if(mm < 10)
    S = S + "0" + mm + "-";
  else
    S = S + mm + "-";
  end;

  // день
  if(dd < 10)
    S = S + "0" + dd;
  else
    S = S + dd;
  end;
  return S;
end;

private macro printReportHeader( impOnDate, realOnDate )

[ Протокол импорта котировок ];
[ 
  Время запуска процедуры: ########## ########               ](date(), time() );
[ Пользователь: ##### ###################################### ]({oper}, {Name_Oper});

end;

private macro printTableHeader()
[┌──────┬──────────┬───────────────┬───────────┬─────────┬────────────────────────────────────────────┐
 │ Цифр │ Буквенн  │  Название     │   Курс    │ Номинал │       Причина включения в протокол         │];
end;

private macro printTableFooter()
[└──────┴──────────┴───────────────┴───────────┴─────────┴────────────────────────────────────────────┘];
end;

private macro setIndex_FI_Code(_Index_FI_Code)
//  record fi( "fininstr.dbt" );
//  if( ПолучитьФинИн( "643", fi, NULL, NULL, NULL, FICK_ISONUMBER ) == 0 )
//    Index_FI_Code = fi.FI_Code;
//  end;
  Index_FI_Code = _Index_FI_Code;
end;

private macro Trunc(FullPath, Path: @string)
   Path = FullPath;
   while (Index(FullPath, "\\") != 0)
       FullPath = Substr(FullPath, Index(FullPath, "\\") + 1);
   end;
   Path = substr(Path,1,strlen(Path)-strlen(FullPath));
   SetParm ( 1, Path );
   return FullPath;
end;

private const ImportDir = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\IMPORTDIR";
private var ПутьК_ФАЙЛУ = "", FName;

IMPORT PTInter, activex;

private macro getIndexFromFile( ВидКурса )
  /*
  if( trim( ПутьК_ФАЙЛУ ) == "" )
     GetRegistryValue( ImportDir,    V_STRING,  ПутьК_ФАЙЛУ,    "" );
     FName = ПутьК_ФАЙЛУ + "RUONIA\\"
  end;
  */

//  ПутьК_ФАЙЛУ = "D:\\RSHB_SOFR_UPGRADE\\Import\\Index";
  ПутьК_ФАЙЛУ = "$D:\\";
  printReportHeader();

  if(SelectFile(FName, "C:\\*.xls*", "Выберите файл для загрузки", 0, true))

    FName = Trunc(FName, ПутьК_ФАЙЛУ);
    println("\n Загрузка файла котировок " + FName + "\n");

/*
    if(not IsStandAlone) /*Трехзвенка*/
      if (not CopyFile(ПутьК_ФАЙЛУ + FName, "$C:\\tmp\\" + FName))
        msgbox ("Ошибка при передаче файла на терминал");
        return false;
      end;
    end;
*/

    if ( not OpenExcelFile (FName, false, true, ПутьК_ФАЙЛУ) )
      println("\n Загрузка не произведена");
      ExcelApplication.ActiveWorkbook.Close;  /* Показывать сам файл не будем, сразу закрываем его */
      exit(1);
    end;

    printTableHeader();

    var i = 1;
    var indexRate;

    while ((ValType(ActiveSheet.Range("A"+i).Value)!=0)and(ActiveSheet.Range("A"+i).Value!=""))
      setIndex_FI_Code(ActiveSheet.Range("A"+i).Value);
      indexRate = TIndexRate( ActiveSheet.Range("C"+i).Value, 1 );
      transferOneRateToBase( {curdate}, ActiveSheet.Range("B"+i).Value, ВидКурса, indexRate );
      indexRate = TIndexRate( ActiveSheet.Range("C"+i).Value, 2 );
      transferOneRateToBase( {curdate}, ActiveSheet.Range("B"+i).Value, ВидКурса, indexRate );
      i = i + 1;
    end;

    printTableFooter();

    ExcelApplication.ActiveWorkbook.Close;  /* Показывать сам файл не будем, сразу закрываем его */
  else
    println("\n Загрузка отменена");
  end;
end;

macro IndexImport
  
  var ВидКурса = 0;

  var err = 0;
  //GetRegistryValue( "CB\\FINTOOLS\\CBCURRATEKIND", V_INTEGER, ВидКурса, err );
  ВидКурса = 1;
  if( (err != 0) or (ValType(ВидКурса) != V_INTEGER) or (ВидКурса == 0) )
    msgBox("Не задано значение настройки CB\\FINTOOLS\\CBCURRATEKIND.");
    exit(1);
  else
    err = getIndexFromFile( ВидКурса );
  end;

  return err;
end;

IndexImport();