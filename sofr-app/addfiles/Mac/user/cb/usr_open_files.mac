/* Simanov. Пробный макрос
 * Макрос, объединающий в себя функции по открытию различных файлов
 */
 import rsexts;

private macro ByDefault (parm, default_value)
   if (valtype(parm) == V_UNDEF)
      SetParm(0, default_value);
   end;
End;


//Копирует файл на сервер приложений. Возвращает полный путь на СП
macro CopyOnSP (FullNameFile)
   var path = "";
   var FileName, ExtName, DirName;
   var TermPath = GetCurDir(false) + "\\..\\TxtFile";// + "\\txtfile\\";

   //Отделяем путь файла от имени
   splitfile(FullNameFile, FileName, ExtName);
   FileName = FileName + ExtName;
   DirName = substr(FullNameFile, 1, index(FullNameFile, FileName) - 1);

   if (CopyFile("$" + DirName + FileName, TermPath +"\\"+ FileName) )
      path = TermPath +"\\"+ FileName;
   end;

   return path;
End;

class c_txt_file ()
   var localFileName = "";

   /* Открывает текстовые (txt, csv).
    * mask: маска для выбора файла. По умолчанию "*.txt"
    * term: true - выбор файла на терминале. false - на сервере
    * auto: true - уже задан полный путь для автоматического открытия файла (FullNameFile)
    * FullNameFile: путь для автоматического открытия файла
    * encode: кодировка
    *
    * Вовращает: null - пользователь отказался от выбора файла (нажал ESC)
    *            false - ошибка при открытии файла
    *            true - файл успешно выбран
    */
   macro openFile (doc, mask, term, auto, FullNameFile, encode, error_message:@string)
      ByDefault(mask, "*.txt");
      ByDefault(term, true);
      ByDefault(auto, false);
      ByDefault(FullNameFile, "");

      if (not auto) 
         /*Выбираем файл для обработки*/
         if(not SelectFile(FullNameFile, mask, "Выберите файл для загрузки", 0, term))
            return null;
         end;
      end;

      //В условиях РСХБ файл можно открыть только на СП. Если файл не на СП, то необходимо его туда копировать
      if (term)
         FullNameFile = localFileName = CopyOnSP(FullNameFile);
      end;

      if (not Open(doc, FullNameFile, encode)) 
        error_message = "Ошибка открытия файла " + FullNameFile;
        return false;
      end;

      return true;
   End;

   macro closeFile (doc)
      close(Doc);
      if (localFileName != "")
         RemoveFile (localFileName);
      end;
   End;
    
END;