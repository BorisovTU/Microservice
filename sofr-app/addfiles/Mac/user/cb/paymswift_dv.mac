/*
$Name:        paymswift_dv.mac
$Description: Работа с платежами
$Programmer:  Кротов А.
*/
import dlquery;
import captureoutput;
import payminter;
import bankInter;
//import dv_categ;

private const GLOB_PARAM_CSAPayment_PMID= "dvcsa020_PMID";
       
/*Вставка/удаление SWIFT сообщения по платежу на постобработке выполнения/отката шага*/
macro PostStepSWIFT(CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                    errTrn,           /* Статус выполнения шага-!0-произошла ошибка */
                    FirstDoc,         /* Указатель на первичный документ */
                    ID_Operation,     /* Номер экземпляра операции */
                    Num_Step,         /* Номер шага операции(из настроек) */
                    Kind_Operation,   /* Вид операции */
                    KindDoc,          /* Вид первичного документа */
                    KindStep,         /* Вид шага операции */
                    ID_Step)
  record deal(dvndeal);
  record nett(dl_nett);//teg 12/02/19
  record dvcsa(dvcsa);//KS 01/04/19
  //var dealfd;
  var cmdPM, rsPM = null, contrid = null, GenagrID = -1, GenagrObjtype = 126;
  var dId,dContractor;

  /*При ошибке ничего не делаем*/
  if(errTrn)
    return;
  end;


  /*Ищем платежи*/
  if(KindDoc == 4813/*Конверсионная сделка ФИССиКО*/)
    SetBuff(deal, FirstDoc);
    //dealfd = DVFirstDocFXDeal(DL_DVFXDEAL, deal);
    /*Выполнение шага*/
    if(CommitOrRollback == 1)
      StartCaptureOutput();
      /*В документах по шагу нет свзяки с платежом*/
      [ 
        select pmpaym.t_paymentid t_paymentid
        from dpmpaym_dbt pmpaym
        where pmpaym.t_dockind = ########## and
              pmpaym.t_documentid = ? and
              pmpaym.t_chapter = 1 and
              pmpaym.t_netting = chr(0) and
              pmpaym.t_paymstatus = ########## and
              (pmpaym.t_fiid_futurepayacc != 0 or pmpaym.t_fiid_futurerecacc != 0) and
              (exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = pmpaym.t_payerbankid) or
               exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = pmpaym.t_payer)) and
              not exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = pmpaym.t_receiverbankid)
      ](KindDoc, PM_READY_TO_SEND);
      cmdPM = RSDCommand(EndCaptureOutput());
      cmdPM.AddParam("docid", RSDBP_IN, deal.id);
      cmdPM.Execute();
      rsPM = TRsbDataSet(cmdPM);
      contrid = deal.contractor;
      GenagrID = deal.GenagrID;
    end;
  elif((KindDoc == 199/*Внебиржевая сделка с ПИ*/) or (KindDoc == 194/*Расчеты по ПИ*/) or (KindDoc == 140/*Документ неттинга*/))
    if (KindDoc == 140)//TEG 12.02.19
        SetBuff(nett, FirstDoc); 
        dId=nett.nettingid;
        dContractor=nett.contractor;
        GenagrID = nett.GenagrID;
    else   
        SetBuff(deal, FirstDoc);
        dId=deal.id;
        dContractor=deal.contractor;
        GenagrID = deal.GenagrID;
    end;
    //dealfd = DVFirstDocFXDeal(DL_DVFXDEAL, deal);
    /*Выполнение шага*/
    if(CommitOrRollback == 1)
      StartCaptureOutput();
      [ 
        select pmpaym.t_paymentid t_paymentid
        from doprdocs_dbt oprdocs
        join dpmpaym_dbt pmpaym on pmpaym.t_paymentid = substr(oprdocs.t_documentid,13) and
                                   pmpaym.t_dockind = ########## and
                                   pmpaym.t_documentid = ? and
                                   pmpaym.t_chapter = 1 and
                                   pmpaym.t_netting = chr(0) and
                                   pmpaym.t_paymstatus in ########## and
                                   (pmpaym.t_fiid_futurepayacc != 0 or pmpaym.t_fiid_futurerecacc != 0) and
                                   (exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = pmpaym.t_payerbankid) or
                                    exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = pmpaym.t_payer)) and
                                   not exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = t_receiverbankid)
        where oprdocs.t_id_operation = ? and
              oprdocs.t_id_step = ? and
              oprdocs.t_dockind = 451 /*Связка с изменением статуса платежа*/
      ](KindDoc, PM_READY_TO_SEND);
      cmdPM = RSDCommand(EndCaptureOutput());
      cmdPM.AddParam("docid", RSDBP_IN, dId/*deal.id*/);
      cmdPM.AddParam("id_operation", RSDBP_IN, ID_Operation);
      cmdPM.AddParam("id_step", RSDBP_IN, ID_Step);
      cmdPM.Execute();
      rsPM = TRsbDataSet(cmdPM);
      contrid = dContractor/*deal.contractor*/;
    end;
  elif(KindDoc == 4626/*Платеж по CSA*/)
    var CSAPayment_PMID = getGlobalParameter(GLOB_PARAM_CSAPayment_PMID, false);
    SetBuff(dvcsa, FirstDoc);
    dContractor=dvcsa.partyid;
    GenagrID = dvcsa.csaID;
    GenagrObjtype = 152;
    if (CSAPayment_PMID != null)
      var query = "select t_paymentid " +
                  "  from dpmpaym_dbt t " +
                  " where t.t_dockind = ? " +
                  "   and t.t_documentid = ? " + 
                  "   and (t.t_fiid_futurepayacc != 0 or t.t_fiid_futurerecacc != 0)";
      cmdPM = RSDCommand(query);
      cmdPM.AddParam("", RSDBP_IN, 4627);
      cmdPM.AddParam("", RSDBP_IN, CSAPayment_PMID);
      cmdPM.Execute();
      rsPM = TRsbDataSet(cmdPM);
      contrid = dContractor/*deal.contractor*/;
    end;
  else
    msgboxex("Платеж для SWIFT-сообщения не найден|"+"KindDoc="+KindDoc+"|Kind_Operation="+Kind_Operation+"|ID_Operation="+ID_Operation+"|Num_Step="+Num_Step+"|ID_Step="+ID_Step);
    return 1;
  end;

  /*Проходим по платежам платеж*/
  if((rsPM != null)/*Выполнение шага*/ or (CommitOrRollback == 2)/*Откат шага*/)
    ExecMacroFile("paymswift", "ModifySWIFT", rsPM, CommitOrRollback, ID_Operation, ID_Step, contrid);
  end;


  //Rogl
  //Расчет вида ED для УФЕБС сообщения по рублевым платежам на постобработке выполнения
  //Ищем рублевые платежи
  rsPM = null;

  /*При откате шага: ничего не делаем*/
  if(CommitOrRollback == 2)
     return;
  end;

  /*Ищем рублевые исходящие платежи*/
  if(KindDoc == 4813/*Конверсионная сделка ФИССиКО*/)
    StartCaptureOutput();
    /*В документах по шагу нет свзяки с платежом*/
    [ 
      select pmpaym.t_paymentid t_paymentid
      from dpmpaym_dbt pmpaym
      inner join dpmprop_dbt pmp on pmp.t_paymentid = pmpaym.t_paymentid and pmp.t_debetcredit = 1 and pmp.t_corschem = 0
      where pmpaym.t_dockind = ########## and
            pmpaym.t_documentid = ? and
            pmpaym.t_chapter = 1 and
            pmpaym.t_netting = chr(0) and
            pmpaym.t_paymstatus = ########## and
            pmpaym.t_payfiid = 0
    ](KindDoc, PM_READY_TO_SEND);
    cmdPM = RSDCommand(EndCaptureOutput());
    cmdPM.AddParam("docid", RSDBP_IN, deal.id);
    cmdPM.Execute();
    rsPM = TRsbDataSet(cmdPM);
  elif((KindDoc == 199/*Внебиржевая сделка с ПИ*/) or (KindDoc == 194/*Расчеты по ПИ*/) or (KindDoc == 140/*Документ неттинга*/))
    StartCaptureOutput();
    [ 
      select pmpaym.t_paymentid t_paymentid
      from doprdocs_dbt oprdocs
      join dpmpaym_dbt pmpaym on pmpaym.t_paymentid = substr(oprdocs.t_documentid,13) and
                                 pmpaym.t_dockind = ########## and
                                 pmpaym.t_documentid = ? and
                                 pmpaym.t_chapter = 1 and
                                 pmpaym.t_netting = chr(0) and
                                 pmpaym.t_paymstatus in ########## and
                                 pmpaym.t_payfiid = 0 
      inner join dpmprop_dbt pmp on pmp.t_paymentid = pmpaym.t_paymentid and pmp.t_debetcredit = 1 and pmp.t_corschem = 0
      where oprdocs.t_id_operation = ? and
            oprdocs.t_id_step = ? and
            oprdocs.t_dockind = 451 /*Связка с изменением статуса платежа*/
    ](KindDoc, PM_READY_TO_SEND);
    cmdPM = RSDCommand(EndCaptureOutput());
    cmdPM.AddParam("docid", RSDBP_IN, dId/*deal.id*/);
    cmdPM.AddParam("id_operation", RSDBP_IN, ID_Operation);
    cmdPM.AddParam("id_step", RSDBP_IN, ID_Step);
    cmdPM.Execute();
    rsPM = TRsbDataSet(cmdPM);
  elif(KindDoc == 4626/*Платеж по CSA*/)
    if (CSAPayment_PMID != null)
      cmdPM = RSDCommand("select t_paymentid " +    
                         "  from dpmpaym_dbt t " +  
                         " where t.t_dockind = ? " +
                         "   and t.t_payfiid = 0 " +
                         "   and t.t_documentid = ?");
      cmdPM.AddParam("", RSDBP_IN, 4627);
      cmdPM.AddParam("", RSDBP_IN, CSAPayment_PMID);
      cmdPM.Execute();
      rsPM = TRsbDataSet(cmdPM);
    end;
  else
    msgboxex("Неизвестный вид документа |"+"KindDoc="+KindDoc+"|Kind_Operation="+Kind_Operation+"|ID_Operation="+ID_Operation+"|Num_Step="+Num_Step+"|ID_Step="+ID_Step);
    return 1;
  end;

  if(rsPM == null)
    return 1;
  end;

  /*Проходим по рублевым платежам*/
  while(rsPM.movenext())
    ExecMacroFile("paymswift", "SetTypeED", rsPM.value("t_paymentid"), contrid, GenagrID, GenagrObjtype);
  end;

  /*Возвращаем результат*/
  return 1;
end;

