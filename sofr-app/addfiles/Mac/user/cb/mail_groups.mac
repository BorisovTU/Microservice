/*
mail_groups.mac
*/
import oralib, likepy, RsbFormsInter, BankInter;

private const ESC      = 27,
              F2       = 316,
              F3       = 317,
              F6       = 320,
              F8       = 322,
              F9       = 323,
              kbInsert = 338,
              kbDelete = 339,
              ctrl_R   = 18,
              Enter    = 13,
              ctrl_g   = 7;

private macro _AddCol(ar, ind, fld, head, width, fldType, decPoint)
  ar.value( ind * 6 + 0 ) = fld;
  ar.value( ind * 6 + 1 ) = head;
  ar.value( ind * 6 + 2 ) = width;
  ar.value( ind * 6 + 3 ) = fldType;
  ar.value( ind * 6 + 4 ) = decPoint;
  ar.value( ind * 6 + 5 ) = 0;   // reserv
end;

private macro IIF(a,b,c)
  if(a) return b;
  else return c;
  end;
end;

//Наименование филиала
private macro GetDepName (DepCode):string
  var sql = "select t_name from dparty_dbt where t_partyid = (select t_partyid from ddp_dep_dbt where t_code = :dep)";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("dep", DepCode)), false);
  if (sql.MoveNext() )
    return sql.value(0);
  end;
  return "";
End;

class (TRsbPanel) Group_panel (_name:string, _comment:string)       
  var FT_INT     = 0,
      FT_DOUBLE  = 4,
      FT_STRING  = 7,
      FT_DATE    = 9,
      FT_CHR     = 12,
      FT_NUMERIC = 25;

  var name:string = _name,
      comment:string = _comment;

  var FlagOK = false;

  macro Init ()
    var control;
    var rn = 0;

    if (valtype(name) == V_UNDEF)
      name = "";
    end;

    if (valtype(comment) == V_UNDEF)
      comment = "";
    end;

    SetSize    (35, 7); //Ширина, высота
    SetPosition(20,  10);

    setCaption("Группа пользователей");

    addLabel(TRsbLabel(4, rn, "Наименование"));

    rn = rn + 1;
    control = TRsbEditField(FT_STRING);
    control.setPosition(2, rn);
    control.setSize (30, 1);

    control.name       = "name";
    control.textLength = 30;
    control.Editable   = true;
    control.value      = name;

    addControl(control);
    /*****************************************************/
    rn = rn + 1;
    addLabel(TRsbLabel(4, rn, "Описание"));

    rn = rn + 1;
    control = TRsbEditField(FT_STRING);
    control.setPosition(2, rn);
    control.setSize (30, 4);

    control.name       = "comment";
    control.textLength = 1000;
    control.Editable   = true;
    control.value      = comment;

    addControl(control);

    addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
  End;

  macro eventHandler (EV)
    if (ev.KeyCode == F9)

      if (GetControl("name").value == "")
        MsgBox("Необходимо ввести наименование группы");
        return;
      end;

      name    = GetControl("name").value;
      comment = GetControl("comment").value;
      FlagOK = true;
      close(0);
      return true;
    end;

    return true;
  End;

  InitTRsbPanel();
  Init();
END;


class UserFromGroup (_operid, _name, _depcode)
  var OperID:integer = _operid;
  var OperName:string = _name;
  var Department:string = GetDepName(_depcode);
END;


class Group ()       
  var id:integer = 0;
  var name:string = "";
  var comment:string = "";

  private macro GetGroup ()
    var sql = "select id, groupname, commentgroup from mail_groups_dbt where id = :id ";
    sql = ExecSqlSelect(sql, MakeArray(SqlParam("id", id)), false);
    if (sql.MoveNext() )
      name = sql.value("groupname");
      comment = sql.value("commentgroup");
      return true;
    end;
    return false;
  onerror()
    return false;
  End;

  //Найти группу по имени
  private macro GetGroupByName (_name)
    var sql = "select id, groupname, commentgroup from mail_groups_dbt where groupname = :name ";
    sql = ExecSqlSelect(sql, MakeArray(SqlParam("name", _name)), false);
    if (sql.MoveNext() )
      id = sql.value("id");
      name = sql.value("groupname");
      comment = sql.value("commentgroup");
      return true;
    end;
    return false;
  onerror()
    return false;
  End;

  //Следующее значение сиквенса mail_groups_dbt_seq
  private macro GetNextVal ():integer
    var sql = ExecSqlSelect("select mail_groups_dbt_seq.nextval from dual", null, false);
    if (sql.MoveNext() )
      return sql.value(0);
    end;
    return 0;
  End;

  //Проверяет наличие записи со значенимем id
  private macro ChecID (id)
    var sql = "select 1 from mail_groups_dbt where id = :id ";
    if (ExecSqlSelect(sql, MakeArray(SqlParam("id", id)), false).MoveNext() )
      return true;
    end;
    return false;
  End;

  //Удалить всех пользователей группы
  private macro DeleteAllUsers ()
    if (not GetGroup() )
      return false;
    end;
    var sql = ExecSql("delete mail_grouplink_dbt where groupid = :groupid ", MakeArray(SqlParam("groupid", id)), false);
    return IIF(sql == null, false, true);
  onerror()
    return false;
  End;

  //Удаляет пользователя из группы
  macro DeleteUser (OperID, Department)
    var sql = "delete mail_grouplink_dbt where OperID = :oper and GroupID = :groupid and department = :dep";
    sql = ExecSql(sql, MakeArray(SqlParam("oper", OperID),
                                 SqlParam("groupid", id),
                                 SqlParam("dep", Department)), false);
    return IIF(sql == null, false, true);
  onerror()
    return false;
  End;

  //Вставить группу в БД
  macro Insert ()
    var sql = "insert into mail_groups_dbt values(:id, :name, :com)";
    if (name == "")
      return false;
    end;
    if (id == 0)
      id = GetNextVal();
      if (id == 0)
        return false; //не удалось получить занчение сиквенса
      end;
    elif (ChecID(id))
      return false; //Запись с ид=id уже существует
    end;

    sql = ExecSql(sql, MakeArray(SqlParam("id", id), SqlParam("name", name), SqlParam("com", comment)), false);
    return IIF(sql == null, false, true);
  onerror()
    return false;
  End;

  macro Update ()
    var sql = "update mail_groups_dbt set groupname = :name, commentgroup = :com where id = :id";
    sql = ExecSql(sql, MakeArray(SqlParam("name", name),
                                 SqlParam("com", comment),
                                 SqlParam("id", id)), false);
    return IIF(sql == null, false, true);
  onerror()
    return false;
  End;

  //Удаляет запись из таблицы. Далее сделать проверку на наличие юзеров и запрос на их удаление
  macro Delete ()
    if (id == 0) 
      return false;
    elif (not DeleteAllUsers() )
      return false;
    elif (ExecSql("delete from mail_groups_dbt where id = :id", MakeArray(SqlParam("id", id)), false) == null)
      return false;
    end;

    return true;
  onerror()
    return false;
  End;

  //Вставить запись в таблицу связки. Возвращает номер добавленного пользователя, или 0
  private macro InsertUser (OperID, Dep)
    var sql;
    var params = MakeArray(SqlParam("oper", OperID), SqlParam("groupid", id), SqlParam("dep", Dep));

    sql = "select 1 from mail_grouplink_dbt where operid = :oper and groupid = :groupid and department = :dep";
    if (ExecSqlSelect(sql, params, false).MoveNext() )
      return 0; //Пользователь уже прикреплён к группе
    end;

    sql = "insert into mail_grouplink_dbt values (:oper, :groupid, :dep)";
    if (ExecSql(sql, params, false) == null)
      return 0;
    end;
    return OperID;
  onerror()
    return 0;
  End;

  //Открыть панель для добавления новой группы пользователей
  macro CreateNewGroup_pn ()
    var pn = Group_panel;
    pn.Run();

    if (not pn.FlagOK)
      return false;
    end;

    name = pn.name;
    comment = pn.comment;

    return Insert();
  End;

  //Открыть панель для редактирования группы пользователей
  macro UpdateGroup_pn ()
    var pn;

    if (not GetGroup() )
      return false;
    end;
    pn = Group_panel(name, comment);
    pn.Run();

    if (not pn.FlagOK)
      return false;
    end;
    name = pn.name;
    comment = pn.comment;

    return update();
  onerror()
    return false;
  End;

  //Добавляет нового пользователя к группе. Возвращает класс с данными пользователя или null
  macro AddNewUser ()
    record person ("person");
    if (not ListOper(person) )
      return null;
    end;

    if (InsertUser(person.Oper, person.CodeDepart) )
      return UserFromGroup(person.Oper, person.Name, person.CodeDepart);
    end;
    return null;
  End;

  //Сгенерировать список адресов
  macro GetContact (grpName, dep, FlagTxt)
    var sql;
    var ContactArr = TArray();
    var str = "";
    var arr;

    if (not GetGroupByName(grpName) )
      return ContactArr;
    end;

    sql = "select trim(contact.t_value) mail "
        + "from dperson_dbt person, dcontact_dbt contact, mail_grouplink_dbt lnk "
        + "where     person.t_oper = lnk.operid "
        + "      and person.t_partyid = contact.t_partyid "
        + "      and contact.t_contactkind = 5 /*вид связи - e-mail*/ "
        + "      and contact.t_contacttype = 1001 /*тип связи - рабочий*/ "
        + "      and lnk.groupid = :grpid ";

    if (valtype(dep) != V_UNDEF) 
      sql = sql + " and lnk.department = :dep ";
      arr = MakeArray(SqlParam("grpid", id), SqlParam("dep", dep));
    else
      arr = MakeArray(SqlParam("grpid", id));
    end;

    sql = sql + "order by lnk.operid ";
    sql = ExecSqlSelect(sql, arr, false);

    while (sql.MoveNext() )
      if ((valtype(FlagTxt) != V_UNDEF) and FlagTxt)
        str = str + sql.value("mail") + "; ";
      else
        ContactArr(ContactArr.Size) = sql.value("mail");
      end;
    end;

    return IIF(FlagTxt, str, ContactArr);
  onerror()
    return null;
  End;

  //Список пользователей группы
  macro ListUsers ()
    var col = TArray();
    var sql, params = null;

    if (not GetGroup() )
      return false;
    end;

    macro HandlerScrollUsers (sql, cmd, id, key)
      var Oper;
      if (DLG_INIT == cmd)
        message("~ESC~ выход  ~Delete~ Удалить  ~Insert~ Добавить");
      elif(cmd == DLG_KEY)
        if (key == kbInsert)

          //Добавить нового
          Oper = AddNewUser();
          if (Oper)
            sql.addNew ();
            sql.value ("operid") = Money(Oper.OperID);
            sql.value ("opername") = OPer.OperName;
            sql.value ("depname") = Oper.Department;
            sql.update ();

            goToScroll (sql);
            updateScroll (sql, 5);
          end;
          return CM_IGNORE;

        elif (key == kbDelete)
          //Удалить
          if (DeleteUser(sql.value("operid"), sql.value("department")) )
            sql.delete();
            updateScroll (sql, 5);
          end;
        end;
      end;
    End;

    sql = "select operid, "
        + "       (select t_name from dperson_dbt where t_oper = operid) opername, "
        + "       (select (select t_name from dparty_dbt where t_partyid = dep.t_partyid) name "
        + "        from ddp_dep_dbt dep where dep.t_code = department) depname, "
        + "       Department "
        + "from mail_grouplink_dbt where groupid = :groupid ";
    sql = ExecSqlSelect(sql, MakeArray(SqlParam("groupid", id)), false, RSDVAL_CLIENT, RSDVAL_STATIC);

    sql.UpdateCommand = RsdCommand("select 1 from dual");
    sql.DeleteCommand = RsdCommand("select 1 from dual");
    sql.insertCommand = RsdCommand("select 1 from dual");

    _AddCol ( col, 0, "operid", "Код пользователя", 7, null, 0 );
    _AddCol ( col, 1, "opername", "ФИО", 30 );
    _AddCol ( col, 2, "depname", "Филиал", 20 );

    RunScroll (sql, col.size/6, col, "ScrollGroups", "HandlerScrollUsers", "Пользователи группы", "ESC Выход  F4 Поиск", true, 15, 0, 75, 100);
  End;
END;


//Скролинг груп пользователей для отправки писем
macro ListGroups ()
  var sql, query;
  var col = TArray();

  macro ScrollProcGroups (sql, cmd, id, key)
    var grp = Group;

    if (DLG_INIT == cmd)
      message("~ESC~ выход  ~F8~ Удалить  ~F9~ Ввод  ~F6~ Список пользователей  ctrl+g Сгенерировать список адресов");
    elif(cmd == DLG_KEY)

      if (key == ctrl_R)
        return CM_SELECT;
      elif (key == F8)
        //удалить
        if (MsgBoxEx("Вы действительно хотите удалить запись?", MB_YES+MB_NO, IND_NO) == IND_YES)
          grp.id = sql.value("id");
          if (grp.Delete(sql.value("id")))
            sql.delete();
            updateScroll (sql, 5);
          end;
        end;
        return CM_IGNORE;

      elif (key == F9)
        //создать
        if (grp.CreateNewGroup_pn() )
          sql.addNew ();
          sql.value ("id") = Money(grp.id);
          sql.value ("groupname") = grp.name;
          sql.value ("commentgroup") = grp.comment;
          sql.update ();

          goToScroll (sql);
          updateScroll (sql, 5);
        end;
        return CM_IGNORE;

      elif (key == Enter)
        //изменить
        grp.id = sql.value("id");

        if (grp.UpdateGroup_pn() )
          sql.Edit();
          sql.value ("groupname") = grp.name;
          sql.value ("commentgroup") = grp.comment;
          sql.Update();
          updateScroll (sql, 5);
        end;
        return CM_IGNORE;

      elif (key == F6)
        //список пользователей
        grp.id = sql.value("id");
        grp.ListUsers();
      elif (key == ctrl_g)
        //сгенерировать список адресов
        MsgBox(grp.GetContact(sql.value("groupname"), null, true));
      end;

    end;
  End;

  query = "select id, groupname, commentgroup from mail_groups_dbt order by id";
  sql = ExecSqlSelect(query, null, false, RSDVAL_CLIENT, RSDVAL_STATIC);

  sql.UpdateCommand = RsdCommand("select 1 from dual");
  sql.DeleteCommand = RsdCommand("select 1 from dual");
  sql.insertCommand = RsdCommand("select 1 from dual");

  _AddCol ( col, 0, "id", "ИД", 7, null, 0 );
  _AddCol ( col, 1, "groupname", "Назввание", 20 );
  _AddCol ( col, 2, "commentgroup", "Описание", 30 );

  RunScroll (sql, col.size/6, col, "ScrollGroups", "ScrollProcGroups", "Список групп", "ESC Выход  F4 Поиск", false, 0, 0, 750, 300);
End;