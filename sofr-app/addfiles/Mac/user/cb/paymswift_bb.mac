/*
$Name:        paymswift_bb.mac
$Description: Работа с платежами
$Programmer:  Котов С.
*/
import dlquery;
import captureoutput;
import OprInter;

/*Вставка/удаление SWIFT сообщения по платежу на постобработке выполнения/отката шага*/
macro PostStepSWIFT(CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                    errTrn,           /* Статус выполнения шага-!0-произошла ошибка */
                    FirstDoc,         /* Указатель на первичный документ */
                    ID_Operation,     /* Номер экземпляра операции */
                    Num_Step,         /* Номер шага операции(из настроек) */
                    Kind_Operation,   /* Вид операции */
                    KindDoc,          /* Вид первичного документа */
                    KindStep,         /* Вид шага операции */
                    ID_Step,
                    PaymentID         /* Для совместимости*/)
  record bbdoc(bbcpord);
  var cmdPM, rsPM = null, cmdContrId, rsContrId = null, contrid = null, sqlstr;

  /*При ошибке ничего не делаем*/
  if(errTrn)
    return;
  end;

  /*Ищем валютные платежи*/
  if(KindDoc == BBANK_CPORDER/*27 - Валютный платёж в ББ*/)
    //SetBuff(bbdoc, FirstDoc);
    /*Выполнение шага*/
    if(CommitOrRollback == 1)
      if((valtype(PaymentID) == V_INTEGER) and (PaymentID > 0))
        StartCaptureOutput();
        [ 
          select pmpaym.t_paymentid 
          from dpmpaym_dbt pmpaym 
          where pmpaym.t_paymentid = ? and pmpaym.t_payfiid > 0
        ];
        sqlstr = EndCaptureOutput();
        cmdPM = RSDCommand(sqlstr);
        cmdPM.AddParam("PaymentID", RSDBP_IN, PaymentID);
        cmdContrId = RSDCommand(strsubst(sqlstr, "select pmpaym.t_paymentid", "select pmpaym.t_payer"));
        cmdContrId.AddParam("PaymentID", RSDBP_IN, PaymentID);
      else
        StartCaptureOutput();
        [ 
          select pmpaym.t_paymentid
          from doproper_dbt oproper
          join dpmpaym_dbt pmpaym on lpad(pmpaym.t_paymentid,34,'0') = oproper.t_documentid and
                                     (pmpaym.t_fiid_futurepayacc != 0 or pmpaym.t_fiid_futurerecacc != 0) and
                                     pmpaym.t_chapter = 1 and
                                     pmpaym.t_netting = chr(0) and
                                     (exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = pmpaym.t_payerbankid) or
                                      exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = pmpaym.t_payer)) and
                                     not exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = pmpaym.t_receiverbankid)
          where oproper.t_id_operation = ? and
                oproper.t_dockind = 27 /*?*/
        ];
        sqlstr = EndCaptureOutput();
        cmdPM = RSDCommand(sqlstr);
        cmdPM.AddParam("ID_Operation", RSDBP_IN, ID_Operation);
        cmdContrId = RSDCommand(strsubst(sqlstr, "select pmpaym.t_paymentid", "select pmpaym.t_payer"));
        cmdContrId.AddParam("ID_Operation", RSDBP_IN, ID_Operation);
      end;

      cmdPM.Execute();
      rsPM = TRsbDataSet(cmdPM);
      cmdContrId.Execute();
      rsContrId = TRsbDataSet(cmdContrId);
      if(rsContrId.movenext())
        contrid = rsContrId.value("t_payer");
      end;
    end;
  elif(KindDoc != DLDOC_BANKPAYMENT/*16 - Платёж в ББ*/)
    msgboxex("Платеж для SWIFT-сообщения не найден|"+"KindDoc="+KindDoc+"|Kind_Operation="+Kind_Operation+"|ID_Operation="+ID_Operation+"|Num_Step="+Num_Step+"|ID_Step="+ID_Step);
    return 1;
  end;

  /*Проходим по платежам платеж*/
  if((rsPM != null)/*Выполнение шага*/ or (CommitOrRollback == 2)/*Откат шага*/)
    ExecMacroFile("paymswift", "ModifySWIFT", rsPM, CommitOrRollback, ID_Operation, ID_Step, contrid);
  end;

  //Rogl
  //Расчет вида ED для УФЕБС сообщения по рублевым платежам на постобработке выполнения
  //Ищем рублевые платежи
  rsPM = null;

  /*При откате шага: ничего не делаем*/
  if(CommitOrRollback == 2)
     return;
  end;

  /*Ищем рублевые исходящие платежи*/
  if((valtype(PaymentID) == V_INTEGER) and (PaymentID > 0))
    StartCaptureOutput();
    [ 
      select pmpaym.t_paymentid 
      from dpmpaym_dbt pmpaym 
      inner join dpmprop_dbt pmp on pmp.t_paymentid = pmpaym.t_paymentid and pmp.t_debetcredit = 1 and pmp.t_corschem = 0
      where pmpaym.t_paymentid = ? and pmpaym.t_payfiid = 0
    ];
    sqlstr = EndCaptureOutput();
    cmdPM = RSDCommand(sqlstr);
    cmdPM.AddParam("PaymentID", RSDBP_IN, PaymentID);
    cmdContrId = RSDCommand(strsubst(sqlstr, "select pmpaym.t_paymentid", "select pmpaym.t_payer"));
    cmdContrId.AddParam("PaymentID", RSDBP_IN, PaymentID);
  else
    StartCaptureOutput();
    [ 
          select pmpaym.t_paymentid
          from doproper_dbt oproper
          join dpmpaym_dbt pmpaym on lpad(pmpaym.t_paymentid,34,'0') = oproper.t_documentid and
                                     pmpaym.t_payfiid = 0 and
                                     pmpaym.t_chapter = 1 and
                                     pmpaym.t_netting = chr(0)
          inner join dpmprop_dbt pmp on pmp.t_paymentid = pmpaym.t_paymentid and pmp.t_debetcredit = 1 and pmp.t_corschem = 0
          where oproper.t_id_operation = ? and
                oproper.t_dockind = 16 /*?*/
    ];
    sqlstr = EndCaptureOutput();
    cmdPM = RSDCommand(sqlstr);
    cmdPM.AddParam("ID_Operation", RSDBP_IN, ID_Operation);
    cmdContrId = RSDCommand(strsubst(sqlstr, "select pmpaym.t_paymentid", "select pmpaym.t_payer"));
    cmdContrId.AddParam("ID_Operation", RSDBP_IN, ID_Operation);
  end;
  cmdPM.Execute();
  rsPM = TRsbDataSet(cmdPM);
  cmdContrId.Execute();
  rsContrId = TRsbDataSet(cmdContrId);
  if(rsContrId.movenext())
    contrid = rsContrId.value("t_payer");
  end;

  if(rsPM == null)
    return 1;
  end;

  /*Проходим по рублевым платежам*/
  while(rsPM.movenext())
    ExecMacroFile("paymswift", "SetTypeED", rsPM.value("t_paymentid"), contrid, 0/*tick.GenagrID*/, 124);
  end;

  /*Возвращаем результат*/
  return 1;
end;
