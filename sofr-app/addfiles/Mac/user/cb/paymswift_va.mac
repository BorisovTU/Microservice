/*
$Name:        paymswift_va.mac
$Description: Работа с платежами
$Programmer:  Кротов А.
*/
import dlquery;//, DealsInter, OprInter;
import captureoutput, likepy;
//import sp_class, settl, dlntfd;
//import vatickfd;

private macro GetPartyID (CodeKind, Code)
  var cmdPM, rsPM = null, partyid=0;

  StartCaptureOutput();
  [select t_objectid from dobjcode_dbt where t_objecttype=3 and t_codekind=? and t_code=? and t_state = 0];
  cmdPM = RSDCommand(EndCaptureOutput());
  cmdPM.AddParam("codekind", RSDBP_IN, CodeKind);
  cmdPM.AddParam("code", RSDBP_IN, Code);
  cmdPM.Execute();
  rsPM = TRsbDataSet(cmdPM);

  if(rsPM == null)
    return 1;
  end;

  /*Проходим по рублевым платежам*/
  if(rsPM.movenext())
    partyid = rsPM.value("t_objectid");
  end;

  return partyid;
End;

/*Вставка/удаление SWIFT сообщения по платежу на постобработке выполнения/отката шага*/
macro PostStepSWIFT(CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                    errTrn,           /* Статус выполнения шага-!0-произошла ошибка */
                    FirstDoc,         /* Указатель на первичный документ */
                    ID_Operation,     /* Номер экземпляра операции */
                    Num_Step,         /* Номер шага операции(из настроек) */
                    Kind_Operation,   /* Вид операции */
                    KindDoc,          /* Вид первичного документа */
                    KindStep,         /* Вид шага операции */
                    ID_Step)
  record tick(dl_tick);
  record order(dl_order);
  //var tickfd;
  var cmdPM, rsPM = null, contrid = null;
  var regValue, MakeSWIFT = true;

  /*При ошибке ничего не делаем*/
  if(errTrn)
    return;
  end;

  /*Simanov. По указаниям от Медведевой Л.В. не нужно формировать SWIFT при погашении собственных векселей,
    когда у клиента рассчетный счет в НРД.
    partyid НРД лежит в настройке ВЕКСЕЛЯ БАНКА\\РСХБ\\PARTYNOSWIFT.
  */
  if (KindDoc == 110)
    SetBuff(order, FirstDoc);
       
    if (not GetRegistryValue( "ВЕКСЕЛЯ БАНКА\\РСХБ\\PARTYNOSWIFT", 0, regValue) )
      regValue = null;
    end;

    regValue = StrSubst(regValue, " ", "");
    regValue = split(regValue, ","); //Сделать из строки массив

    if (find(regValue, string(GetPartyID(order.ContractorBankCodeKind, order.ContractorBankCode))) != -1)
      MakeSWIFT = false;   
    end;
  end;

  /*Ищем платежи*/
  if((KindDoc == DL_VEKSELACCOUNTED/*141 - Сделка с учтенными векселями*/) or (KindDoc == 110 /*Заявление на погашение*/))
    SetBuff(tick, FirstDoc); //Simanov. МБ тут стоит использовать dl_order для СВ???
    //tickfd = VATickFD(tick);
    /*Выполнение шага*/
    if(CommitOrRollback == 1 and MakeSWIFT)
      StartCaptureOutput();
      [ 
        select pmpaym.t_paymentid t_paymentid
        from doprdocs_dbt oprdocs
        join dpmpaym_dbt pmpaym on pmpaym.t_paymentid = substr(oprdocs.t_documentid,13) and
                                   pmpaym.t_dockind = ########## and
                                   pmpaym.t_documentid = ? and
                                   pmpaym.t_chapter = 1 and
                                   pmpaym.t_netting = chr(0) and
                                   pmpaym.t_paymstatus = ########## and
                                   (pmpaym.t_fiid_futurepayacc != 0 or pmpaym.t_fiid_futurerecacc != 0) and
                                   (exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = pmpaym.t_payerbankid) or
                                    exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = pmpaym.t_payer)) and
                                   not exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = pmpaym.t_receiverbankid)
        where oprdocs.t_id_operation = ? and
              oprdocs.t_id_step = ? and
              oprdocs.t_dockind = 451 /*Связка с изменением статуса платежа*/
      ](KindDoc, PM_READY_TO_SEND);
      cmdPM = RSDCommand(EndCaptureOutput());
      cmdPM.AddParam("docid", RSDBP_IN, tick.dealid);
      cmdPM.AddParam("id_operation", RSDBP_IN, ID_Operation);
      cmdPM.AddParam("id_step", RSDBP_IN, ID_Step);
      cmdPM.Execute();
      rsPM = TRsbDataSet(cmdPM);
      contrid = tick.partyid;
    end;
  else
    msgboxex("Платеж для SWIFT-сообщения не найден|"+"KindDoc="+KindDoc+"|Kind_Operation="+Kind_Operation+"|ID_Operation="+ID_Operation+"|Num_Step="+Num_Step+"|ID_Step="+ID_Step);
    return 1;
  end;

  /*Проходим по платежам*/
  if((rsPM != null)/*Выполнение шага*/ or (CommitOrRollback == 2)/*Откат шага*/)
    ExecMacroFile("paymswift", "ModifySWIFT", rsPM, CommitOrRollback, ID_Operation, ID_Step, contrid);
  end;


  //Rogl
  //Расчет вида ED для УФЕБС сообщения по рублевым платежам на постобработке выполнения
  //Ищем рублевые платежи
  rsPM = null;

  /*При откате шага: ничего не делаем*/
  if(CommitOrRollback == 2)
     return;
  end;

  /*Ищем рублевые исходящие платежи*/
  if((KindDoc == DL_VEKSELACCOUNTED/*141 - Сделка с учтенными векселями*/) or (KindDoc == 110 /*Заявление на погашение*/))
    if(CommitOrRollback == 1)
      StartCaptureOutput();
      [ 
        select pmpaym.t_paymentid t_paymentid
        from doprdocs_dbt oprdocs
        join dpmpaym_dbt pmpaym on pmpaym.t_paymentid = substr(oprdocs.t_documentid,13) and
                                   pmpaym.t_dockind = ########## and
                                   pmpaym.t_documentid = ? and
                                   pmpaym.t_chapter = 1 and
                                   pmpaym.t_netting = chr(0) and
                                   pmpaym.t_paymstatus = ########## and
                                   pmpaym.t_payfiid = 0 
        inner join dpmprop_dbt pmp on pmp.t_paymentid = pmpaym.t_paymentid and pmp.t_debetcredit = 1 and pmp.t_corschem = 0
        where oprdocs.t_id_operation = ? and
              oprdocs.t_id_step = ? and
              oprdocs.t_dockind = 451 /*Связка с изменением статуса платежа*/
      ](KindDoc, PM_READY_TO_SEND);
      cmdPM = RSDCommand(EndCaptureOutput());
      cmdPM.AddParam("docid", RSDBP_IN, tick.dealid);
      cmdPM.AddParam("id_operation", RSDBP_IN, ID_Operation);
      cmdPM.AddParam("id_step", RSDBP_IN, ID_Step);
      cmdPM.Execute();
      rsPM = TRsbDataSet(cmdPM);
    end;
  else
    msgboxex("Неизвестный вид документа |"+"KindDoc="+KindDoc+"|Kind_Operation="+Kind_Operation+"|ID_Operation="+ID_Operation+"|Num_Step="+Num_Step+"|ID_Step="+ID_Step);
    return 1;
  end;

  if(rsPM == null)
    return 1;
  end;

  /*Проходим по рублевым платежам*/
  while(rsPM.movenext())
    ExecMacroFile("paymswift", "SetTypeED", rsPM.value("t_paymentid"), contrid, -1); //В векселях генсоглашения не используются
  end;


  /*Возвращаем результат*/
  return 1;
end;

