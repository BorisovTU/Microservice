/*
CloseAccounts.mac
Процедура закрытия счетов за период. Отбираются счета, у которых значение примечания 113 "Дата закрытия счета" попадает в заданный период
*/
import oralib, likepy, RsbFormsInter, globals, Календарь, BankInter;

private var FT_DATE = 9;

private var F2 = 316,
            F3 = 317;

private class (TRsbPanel) GetPeriodPanel() 
  InitTRsbPanel();
  Caption = "Укажите период";         
  Status = "F2 - Продолжить  F3 - Календарь  ESC - Выход";
  setSize(29,6); 
  setPosition(15,8); 
  var BDate:Date = {curdate}; 
  var EDate:Date = {curdate};
  var m_EditField1:TRsbEditField = TRsbEditField(FT_DATE); 
  var m_EditField2:TRsbEditField = TRsbEditField(FT_DATE);
  var m_LabelBeginDate:TRsbLabel = TRsbLabel(3, 2, "Начало периода:"); 
  var m_LabelEndDate:TRsbLabel = TRsbLabel(3, 4,  " Конец периода:");
  addLabel(m_LabelBeginDate);
  addLabel(m_LabelEndDate);
  m_EditField1.bindValue(this, "BDate", 10);
  m_EditField1.Name = "BeginDate";
  m_EditField1.setPosition(17,2); 
  m_EditField1.setSize(10,1); 
  addControl(m_EditField1);
  m_EditField2.bindValue(this, "EDate", 10);
  m_EditField2.Name = "EndDate"; 
  m_EditField2.setPosition(17,4); 
  m_EditField2.setSize(10,1); 
  addControl(m_EditField2);
  addEventHandler(RSB_EV_KEY_PRESSED,R2M(this,"KeyPressed"));   
  macro KeyPressed(RsbEvent:Object)
    var focusedControl;      
    if (rsbEvent.KeyCode == F3)                
      focusedControl = this.focusedControl();                    
      if ((focusedControl.Name == "BeginDate") or (focusedControl.Name == "EndDate"))
        focusedControl.Value = GetDateByCalendar(focusedControl.Value);                
      end;
    elif (rsbEvent.KeyCode == F2)
      if ((m_EditField1.Value == date(0,0,0)) or
          (m_EditField1.Value == date(0,0,0)) or
          (m_EditField1.Value > m_EditField2.Value))
        Msgbox("Укажите правильный период!");
        return false;
      end;
      this.Close(1);
    end; 
    return true;
  end;
end;

macro StartClose ()
  var sql;
  var panel = GetPeriodPanel();
  var err = "";
  var count = 0, counterr = 0, countall = 0;

  SetOutput("ClosedAccount.txt");
  if (not panel.run) 
    return 1;
  end;

  rslDefCon.beginTrans();
  sql = " select acc.t_chapter chapter, acc.t_code_currency cur, acc.t_account acc, RSB_Struct.GetDate(note.t_text) dt \n"
      + "from daccount_dbt acc, dnotetext_dbt note \n"
      + "where     acc.t_open_close = chr(0) \n"
      + "      and note.t_documentid = lpad(acc.t_chapter, 2, 0) || lpad(acc.t_code_currency, 7, 0) || acc.t_account \n"
      + "      and note.t_objecttype = 4 \n"
      + "      and note.t_notekind = 113 \n"
      + "      and RSB_struct.GetDate(note.t_text) between :datebegin and :dateend \n";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("datebegin", panel.BDate), SqlParam("dateend", panel.EDate)), false);

  InitProgress(-1, "Процедура закрытия счетов");
  while (sql.MoveNext() )
    err = "";
    CB_CloseAccount (sql.value("chapter"), sql.value("cur"), sql.value("acc"), NULL, err);
    if (err == "")
      println("Счет " + sql.value("acc") + " успешно закрыт");
      count = count + 1;
    else
      println("Ошибка при закрытии счета " + sql.value("acc") + ". " + err);
      counterr = counterr + 1;
    end;
    UseProgress(countall = countall + 1);
  end;
  RemProgress();

  println("успешно закрытых счетов: " + count);
  println("Ошибок: " + counterr);

  rslDefCon.commitTrans();

  viewFile (setoutput (null, true));

onerror()
  RemProgress();
  if ( rslDefCon.isInTrans() )
    rslDefCon.rollbackTrans();
  end;
  MsgBox("Ошибка при выполнении процедуры");
End;

StartClose();
exit(1);