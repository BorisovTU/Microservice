/*──────────────────────────────────────────────────────────────────────────┐

  File Name   : check_pmautoac.mac
  Created     : 19.07.2019
  Programmer  : Котов С.С.
  Description : Отчет о незавершённых платежах по КУ (субъекту)

└───────────────────────────────────────────────────────────────────────────*/

import cb_sql;

/* Вспомогательная процедура подготовки информации с атрибутами полей*/
private macro AddCol( ar,ind, fld, head, width, rdonly )
    ar.value( ind * 6 + 0 ) = fld;
    ar.value( ind * 6 + 1 ) = head;
    ar.value( ind * 6 + 2 ) = width;
    ar.value( ind * 6 + 3 ) = 2;   // fldType
    ar.value( ind * 6 + 4 ) = -1;  // decPoint
    ar.value( ind * 6 + 5 ) = 0;   // reserv
end;

macro checkPmStatus(partyid, servicekind, settaccid)
  var cmd = RsdCommand("select t.t_valuedate, t.t_amount, f.t_ccy, rm.t_payername, rm.t_payerbankname, t.t_payeraccount, rm.t_receivername, rm.t_receiverbankname, t.t_receiveraccount, st.t_name " +
                       "  from dpmpaym_dbt t, dpmrmprop_dbt rm, dpmstatus_dbt st, dfininstr_dbt f " +
                       " where case " +
                       "         when t_PrimDocKind = 102 then 2 " +
                       "         when t_PrimDocKind in (109) then 8 " +
                       "         when t_PrimDocKind in (141, 142, 143) then 14 " +
                       "         when t_PrimDocKind in (320, 450) then -1 " +
                       "         else 15 " +
                       "       end " + ternary(servicekind==-1,"in (select pc.t_servicekind from DPMAUTOAC_DBT pc where pc.t_settaccid = ?)","= ?") +
                       "   and ? in (t.t_receiver, t.t_payer) " +
                       "   and rm.t_paymentid = t.t_paymentid " +
                       "   and t.t_paymstatus not in (32000, 150) " +
                       "   and t.t_fiid = f.t_fiid(+) " +
                       "   and t.t_paymstatus = st.t_paymstatus(+) " +
                       "   and st.t_type(+) = 0 " +
                       " order by t.t_valuedate, t.t_paymentid");

    cmd.addParam("", RSDBP_IN, ternary(servicekind==-1, settaccid, servicekind));
    cmd.addParam("", RSDBP_IN, partyid);
    cmd.execute();
    
    var Col:TArray = TArray;
    AddCol (col, 0, "t_name",            "Статус платежа",        25,   true);
    AddCol (col, 1, "t_valuedate",       "Дата валютирования",        12,   true);
    AddCol (col, 2, "t_amount",          "Сумма",                     18,   true);
    AddCol (col, 3, "t_ccy",             "Вал",                       3,   true);
    AddCol (col, 4, "t_payername",       "Плательщик",                30,   true);
    AddCol (col, 5, "t_payerbankname",   "Банк плательщика",          30,   true);
    AddCol (col, 6, "t_payeraccount",    "Счет плательщика",          25,   true);
    AddCol (col, 7, "t_receivername",    "Получатель",                30,   true);
    AddCol (col, 8, "t_receiverbankname","Банк получателя",           30,   true);
    AddCol (col, 9, "t_receiveraccount", "Счет получателя",           25,   true);

    var  rs = RsdRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC );
    if(rs.MoveNext())
      msgbox("По данному контрагенту найдены незавершенные платежи!");
      RunScroll(rs, 10, col, null, null, "Отчет о незавершённых платежах", "~Esc~ Отмена ~F4~  Поиск ", true );
      return true;
    end;
    return false;
end;
