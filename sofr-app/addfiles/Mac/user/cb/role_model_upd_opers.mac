/*
role_model_upd_opers.mac
Обновление операциониста у созданных и невыгруженных объектов
*/
import RsbFormsInter, globals, Календарь, BankInter;
import oralib, likepy;
import or_rep_h;

private var FT_INT     = 0,
            FT_DOUBLE  = 4,
            FT_STRING  = 7,
            FT_DATE    = 9,
            FT_CHR     = 12,
            FT_NUMERIC = 25;

private var KB_F2 = 316,
            KB_F3 = 317;

private var gv_evnt_sel_prm;
private var gv_result_rep;

/*---------------------------*/
/* Параметры отбора объектов */
/*---------------------------*/
private class c_evnt_selection_prm()
   var v_begin_date;
   var v_end_date;

   var v_old_oper;
   var v_old_oper_name;
   var v_new_oper;
   var v_new_oper_name;

   /* Значения по умолчанию */
   private macro m_init_evnt_selection_prm()
      v_begin_date = {curdate};
      v_end_date = {curdate};

      v_old_oper = 0;
      v_old_oper_name = "";
      v_new_oper = 0;
      v_new_oper_name = "";
   end;

   m_init_evnt_selection_prm();
end; /* class c_evnt_selection_prm() */


//Класс для вывода отчёта
private class c_result_report(_name, columns)
  var acctrn_query  = "select 1 from dual";
  var account_query = "select 1 from dual";
  var paym_query    = "select 1 from dual";

  var acctrn_sql, account_sql, paym_sql;

  var Rep, sheet = 1;
  var format_head = "";
  var format_line = "";
  var report_name = _name;

  /*апдейт изменяет сразу все записи, и отловить изменения тяжело. Поэтому приходится сначала делать селект, чтобы запомнить данные, которые будут изменятсья, для вывода в отчет.
    Отдельно делать на каждую запись один апдейт будет очень долго
  */
  macro m_init_result_report (_acctrn_query, _account_query, _paym_query)
    var params = MakeArray(SqlParam("begindate", gv_evnt_sel_prm.v_begin_date ), //Для всех селектов параметры будут одинаковые
                           SqlParam("enddate",   gv_evnt_sel_prm.v_end_date   ),
                           SqlParam("oldoper",   gv_evnt_sel_prm.v_old_oper   ));
    acctrn_query  = _acctrn_query;
    account_query = _account_query;
    paym_query    = _paym_query;

    acctrn_sql  = ExecSqlSelect( acctrn_query,  params );
    account_sql = ExecSqlSelect( account_query, params );
    paym_sql    = ExecSqlSelect( paym_query,    params );
  End;

  macro m_add_new_sheet (name, columns)
    if (Rep == null)
      Rep = CMakeReport(columns);
    else
      Rep.AddNewSheetBreak(name, columns);
    end;

    Rep.SetDefaultFontName("Times New Roman");
    Rep.SetDefaultFontSize(10);
    Rep.SetFlagShowGrid(true);

  End;

  macro m_makerep_acctrn ()
    var rn = 1;
    Rep.AddPrintCell("ИД",           0, 0, format_head);
    Rep.AddPrintCell("Номер",        0, 0, format_head);
    Rep.AddPrintCell("Дата",         0, 0, format_head);
    Rep.AddPrintCell("Счет дебет",   0, 0, format_head);
    Rep.AddPrintCell("Сумма дебет",  0, 0, format_head);
    Rep.AddPrintCell("Счет кредит",  0, 0, format_head);
    Rep.AddPrintCell("Сумма кредит", 0, 0, format_head);
    Rep.AddPrintCell("Пачка",        0, 0, format_head);
    Rep.AddPrintCell("Основание",    0, 0, format_head);
    Rep.AddStr();

    while (acctrn_sql.MoveNext() )
      rn = rn + 1;
      Rep.AddPrintCell(acctrn_sql.value("t_acctrnid"),         0, 0, format_line);
      Rep.AddPrintCell(acctrn_sql.value("t_numb_document"),    0, 0, format_line);
      Rep.AddPrintCell(Date(acctrn_sql.value("t_date_carry")), 0, 0, format_line);
      Rep.AddPrintCell(acctrn_sql.value("t_account_payer"),    0, 0, format_line);
      Rep.AddPrintCell(acctrn_sql.value("t_sum_payer"),        0, 0, format_line);
      Rep.AddPrintCell(acctrn_sql.value("t_account_receiver"), 0, 0, format_line);
      Rep.AddPrintCell(acctrn_sql.value("t_sum_receiver"),     0, 0, format_line);
      Rep.AddPrintCell(acctrn_sql.value("t_number_pack"),      0, 0, format_line);
      Rep.AddPrintCell(acctrn_sql.value("t_ground"),           0, 0, format_line);
      Rep.AddStr();
    end;
  End;

  macro m_makerep_account ()
    var rn = 1;
    Rep.AddPrintCell("ИД",            0, 0, format_head);
    Rep.AddPrintCell("Дата открытия", 0, 0, format_head);
    Rep.AddPrintCell("Глава",         0, 0, format_head);
    Rep.AddPrintCell("Счет",          0, 0, format_head);
    Rep.AddPrintCell("Наименование",  0, 0, format_head);
    Rep.AddStr();

    while (account_sql.MoveNext() )
      rn = rn + 1;
      Rep.AddPrintCell(account_sql.value("t_accountid"),       0, 0, format_line);
      Rep.AddPrintCell(Date(account_sql.value("t_open_date")), 0, 0, format_line);
      Rep.AddPrintCell(account_sql.value("t_chapter"),         0, 0, format_line);
      Rep.AddPrintCell(account_sql.value("t_account"),         0, 0, format_line);
      Rep.AddPrintCell(account_sql.value("t_nameaccount"),     0, 0, format_line);
      Rep.AddStr();
    end;
  End;

  macro m_makerep_paym ()
    var rn = 1;
    Rep.AddPrintCell("ИД",                 0, 0, format_head);
    Rep.AddPrintCell("Номер",              0, 0, format_head);
    Rep.AddPrintCell("Дата валютирования", 0, 0, format_head);
    Rep.AddPrintCell("Дата создания",      0, 0, format_head);
    Rep.AddPrintCell("Счет плательщика",   0, 0, format_head);
    Rep.AddPrintCell("Счет получателя",    0, 0, format_head);
    Rep.AddPrintCell("Сумма",              0, 0, format_head);
    Rep.AddPrintCell("Пачка",              0, 0, format_head);
    Rep.AddPrintCell("Основание",          0, 0, format_head);
    Rep.AddStr();

    while (paym_sql.MoveNext() )
      rn = rn + 1;
      Rep.AddPrintCell(paym_sql.value("t_paymentid"),       0, 0, format_line);
      Rep.AddPrintCell(paym_sql.value("t_number"),          0, 0, format_line);
      Rep.AddPrintCell(paym_sql.value("t_valuedate"),       0, 0, format_line);
      Rep.AddPrintCell(paym_sql.value("t_creationdate"),    0, 0, format_line);
      Rep.AddPrintCell(paym_sql.value("t_payeraccount"),    0, 0, format_line);
      Rep.AddPrintCell(paym_sql.value("t_receiveraccount"), 0, 0, format_line);
      Rep.AddPrintCell(paym_sql.value("t_amount"),          0, 0, format_line);
      Rep.AddPrintCell(paym_sql.value("t_numberpack"),      0, 0, format_line);
      Rep.AddPrintCell(paym_sql.value("t_ground"),          0, 0, format_line);
      Rep.AddStr();
    end;
  End;

  macro Print ()
    Rep.PrintWinRep(report_name);
    Rep.SetZoomType(ZOOM_TYPE_A4L);
    Rep.SetWinRepOutput();
    Rep.ShowWinRep();
  End;

  Rep = CMakeReport(columns);
  Rep.SetDefaultFontName("Times New Roman");
  Rep.SetDefaultFontSize(10);
  Rep.SetFlagShowGrid(true);

end; /* c_result_report() */


//Основной класс замены операционистов.
private class c_oper_update_main()
  var acctrn_select_query;
  var acctrn_update_query;
  var account_select_query;
  var account_update_query;
  var paym_select_query;
  var paym_update_query;

  private macro m_init_oper_update_main ()
    var tmp_query:string = "";

    tmp_query = " where    t_state <> 4 "
              + "      and t_date_carry between :begindate and :enddate "
              + "      and t_oper = :oldoper "
    //          + "      and t_userfield4 <> chr(1) "
              + "      and not t_userfield4 like '%АННУЛ%' "
              + "      and exists (select 1 from utableprocessevent_dbt where t_objecttype = 1 and t_objectid = acctrn.t_acctrnid and t_status <> 4) "
              + "      and not exists (select 1 from utableprocessevent_dbt where t_objecttype = 1 and t_objectid = acctrn.t_acctrnid and t_status = 4) ";
    acctrn_update_query = "update dacctrn_dbt acctrn set t_oper = :newoper " + tmp_query;
    acctrn_select_query = "select t_acctrnid, t_numb_document, t_date_carry, t_account_payer, t_sum_payer, t_account_receiver, t_sum_receiver, t_number_pack, t_ground from dacctrn_dbt acctrn " + tmp_query;


    tmp_query = " where     t_open_date between :begindate and :enddate "
              + "       and t_oper = :oldoper "
    //          + "       and t_userfield4 <> chr(1) "
              + "       and exists (select 1 from utableprocessevent_dbt where t_objecttype = 4 and t_objectid = acc.t_accountid and t_status <> 4) "
              + "       and not exists (select 1 from utableprocessevent_dbt where t_objecttype = 4 and t_objectid = acc.t_accountid and t_status = 4) ";
    account_update_query = "update daccount_dbt acc set t_oper = :newoper " + tmp_query;
    account_select_query = "select t_accountid, t_account, t_chapter, t_open_date, t_nameaccount from daccount_dbt acc " + tmp_query;

    tmp_query = " where     t_creationdate between :begindate and :enddate "
              + "       and t_oper = :oldoper "
    //          + "       and t_userfield4 <> chr(1) "
              + "       and not t_userfield4 like '%АННУЛ%' "
              + "       and exists (select 1 from utableprocessevent_dbt where t_objecttype = 501 and t_objectid = pmpaym.t_paymentid and t_status <> 4) "
              + "       and not exists (select 1 from utableprocessevent_dbt where t_objecttype = 501 and t_objectid = pmpaym.t_paymentid and t_status = 4) ";
    paym_update_query = "update dpmpaym_dbt pmpaym set t_oper = :newoper " + tmp_query;
    paym_select_query = "select t_paymentid, (select t_number from dpmrmprop_dbt where t_paymentid = pmpaym.t_paymentid) t_number, t_valuedate, t_creationdate, t_payeraccount, t_receiveraccount, "
                      + "t_amount, t_numberpack, (select t_ground from dpmrmprop_dbt where t_paymentid = pmpaym.t_paymentid) t_ground from dpmpaym_dbt pmpaym " + tmp_query;
  End;

  macro m_exec_upd (query)
    var params = MakeArray(SqlParam("newoper",   gv_evnt_sel_prm.v_new_oper   ), /*Для всех апдейтов параметры будут одинаковые*/
                           SqlParam("begindate", gv_evnt_sel_prm.v_begin_date ),
                           SqlParam("enddate",   gv_evnt_sel_prm.v_end_date   ),
                           SqlParam("oldoper",   gv_evnt_sel_prm.v_old_oper   ));
    var stat = ExecSql(query, params);
    return IfThenElse( not stat, false, true);

  onerror()
    return false;
  End;

  macro m_start_update ()
    
    gv_result_rep.m_init_result_report(acctrn_select_query, account_select_query, paym_select_query);

    if (m_exec_upd(acctrn_update_query) )
      gv_result_rep.m_makerep_acctrn();
    end;

    gv_result_rep.m_add_new_sheet("Счета", "┌─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┐");

    if (m_exec_upd(account_update_query) )
      gv_result_rep.m_makerep_account();
    end;

    gv_result_rep.m_add_new_sheet("Платежи", "┌─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┐");

    if (m_exec_upd(paym_update_query) )
      gv_result_rep.m_makerep_paym();
    end;

  End;

  m_init_oper_update_main();
end; /* c_oper_update_main() */


/*----------------------------------------*/
/* Класс основной формы                   */
/*----------------------------------------*/
class (TRsbPanel) c_oper_update_form ()
  const CV_POS_X = 3;

  var v_begin_date_fld;
  var v_end_date_fld;
  var v_old_oper_fld;
  var v_old_oper_name_fld;
  var v_new_oper_fld;
  var v_new_oper_name_fld;

  var v_replace_btn;
  var v_result_btn;

  var rownum:integer;
  var v_oper_update_main = c_oper_update_main();

  macro EnableDisableFields(_sign)
    v_begin_date_fld.enabled = _sign;
    v_end_date_fld.enabled = _sign;
    v_old_oper_fld.enabled = _sign;
    v_new_oper_fld.enabled = _sign;
    
    v_replace_btn.enabled = _sign;
    v_result_btn.enabled = (not _sign);

    redraw();
  end;

  macro m_chec_oper_exists (_oper)
    if (ExecSqlSelect("select 1 from dperson_dbt where t_oper = :oper and t_userclosed <> chr(88)", MakeArray(SqlParam("oper", _oper))).MoveNext() )
      return true;
    end;
    return false;
  End;

  macro m_get_oper_name(_oper)
    var name = "";
    var sql = ExecSqlSelect("select t_name from dperson_dbt where t_oper = :oper", MakeArray(SqlParam("oper", _oper)));
    if (sql.MoveNext() ) 
      name = sql.value(0);
    end;
    return name;
  End;

  macro m_init_opers_name ()
    v_old_oper_name_fld.value = m_get_oper_name(v_old_oper_fld.value);
    v_new_oper_name_fld.value = m_get_oper_name(v_new_oper_fld.value);
    redraw();
  End;

  macro m_check_params ()
    if (gv_evnt_sel_prm.v_begin_date > gv_evnt_sel_prm.v_end_date)
      MsgBox("Дата начала периода не должна быть больше даты окончания периода");
      return false;
    end;

    if (gv_evnt_sel_prm.v_old_oper < 1)
      MsgBox("Введите заменяемого операциониста");
      return false;
    end;

    if (gv_evnt_sel_prm.v_new_oper < 1)
      MsgBox("Введите заменяющего операциониста");
      return false;
    end;
    
    if (gv_evnt_sel_prm.v_new_oper == gv_evnt_sel_prm.v_old_oper)
      MsgBox("Выберите разных операционистов");
      return false;
    end;

    if (not m_chec_oper_exists(gv_evnt_sel_prm.v_new_oper) )
      MsgBox("Не найден операционист с номером " + gv_evnt_sel_prm.v_new_oper);
      return false;
    end;

    if (not m_chec_oper_exists(gv_evnt_sel_prm.v_old_oper) )
      MsgBox("Не найден операционист с номером " + gv_evnt_sel_prm.v_old_oper);
      return false;
    end;

    return true;
  End;

  macro m_evnt_replace_btn (p_rsb_evnt)
    if(p_rsb_evnt.id == RSB_EV_BUTTON_CLICKED)
      if (m_check_params() )
        m_init_opers_name();
         
        if (msgboxex("Вы уверены, что хотите выполнить замену? ", MB_YES+MB_NO, IND_NO) == IND_YES)
          v_oper_update_main.m_start_update();
          EnableDisableFields(false);
        end;

      end;
    end;
    return true;
  end;

  macro m_evnt_getresult_btn(p_rsb_evnt)
    if(p_rsb_evnt.id == RSB_EV_BUTTON_CLICKED)
      begAction (100, "Пострение отчета", false);
      gv_result_rep.Print();
      EndAction();
    end;
    return true;
  end;

  macro m_evnt_on_key_pressed(p_rsb_evnt)
    record buffoper ( person );

    if(p_rsb_evnt.KeyCode == KB_F2)
      close(0);
    elif (p_rsb_evnt.KeyCode == KB_F3)

      if (p_rsb_evnt.Source.DataType == FT_DATE)
        p_rsb_evnt.Source.value = GetDateByCalendar(p_rsb_evnt.Source.value);
        return true;
      end;

      if (p_rsb_evnt.Source.Name == v_old_oper_fld.name)

        if (listOper(buffoper, 0) )
          v_old_oper_fld.value = buffoper.oper;
          v_old_oper_name_fld.value = buffoper.name;
        end;

      elif (p_rsb_evnt.Source.Name == v_new_oper_fld.name)

        if (listOper(buffoper, 0) )
          v_new_oper_fld.value = buffoper.oper;
          v_new_oper_name_fld.value = buffoper.name;
        end;

      end;
    end;

    return true;
  end;

  private macro m_init_oper_update_form (this_obj)
    Caption = "Смена операциониста";
    Status = "[Esc] Выход";
    rownum = 2;


    AddLabel(TRsbLabel(CV_POS_X, rownum, "Период выборки"));

    rownum = rownum + 1;
    v_begin_date_fld = TRsbEditField(FT_DATE);
    v_begin_date_fld.bindValue(gv_evnt_sel_prm, "v_begin_date", 10);
    v_begin_date_fld.setPosition(CV_POS_X, rownum);
    v_begin_date_fld.setSize(10, 1);
    AddControl(v_begin_date_fld);

    AddLabel(TRsbLabel(13, rownum, "-"));

    v_end_date_fld = TRsbEditField(FT_DATE);
    v_end_date_fld.bindValue(gv_evnt_sel_prm, "v_end_date", 10);
    v_end_date_fld.setPosition(CV_POS_X + 11, rownum);
    v_end_date_fld.setSize(10, 1);
    AddControl(v_end_date_fld);


    rownum = rownum + 2;
    AddLabel(TRsbLabel(CV_POS_X, rownum, "Заменяемый операционист"));

    rownum = rownum + 1;
    v_old_oper_fld = TRsbEditField(FT_INT);
    v_old_oper_fld.bindValue(gv_evnt_sel_prm, "v_old_oper", 6);
    v_old_oper_fld.name = "1";
    v_old_oper_fld.setPosition(CV_POS_X, rownum);
    v_old_oper_fld.setSize(7, 1);
    AddControl(v_old_oper_fld);

    v_old_oper_name_fld = TRsbEditField(FT_STRING);
    v_old_oper_name_fld.bindValue(gv_evnt_sel_prm, "v_old_oper_name", 50);
    v_old_oper_name_fld.setPosition(CV_POS_X + 8, rownum);
    v_old_oper_name_fld.setSize(30, 1);
    v_old_oper_name_fld.editable = false;
    AddControl(v_old_oper_name_fld);


    rownum = rownum + 2;
    AddLabel(TRsbLabel(CV_POS_X, rownum, "Заменяющий операционист"));

    rownum = rownum + 1;
    v_new_oper_fld = TRsbEditField(FT_INT);
    v_new_oper_fld.bindValue(gv_evnt_sel_prm, "v_new_oper", 6);
    v_new_oper_fld.name = "2";
    v_new_oper_fld.setPosition(CV_POS_X, rownum);
    v_new_oper_fld.setSize(7, 1);
    AddControl(v_new_oper_fld);

    v_new_oper_name_fld = TRsbEditField(FT_STRING);
    v_new_oper_name_fld.bindValue(gv_evnt_sel_prm, "v_new_oper_name", 50);
    v_new_oper_name_fld.setPosition(CV_POS_X + 8, rownum);
    v_new_oper_name_fld.setSize(30, 1);
    v_new_oper_name_fld.editable = false;
    AddControl(v_new_oper_name_fld);


    rownum = rownum + 2;
    /* Кнопка выполнения */
    v_replace_btn = TRsbPushButton("Выполнить замену");
    v_replace_btn.setPosition(CV_POS_X, rownum);
    v_replace_btn.setSize(12, 1);
    v_replace_btn.onClicked(R2M(this_obj, "m_evnt_replace_btn"));
    AddControl(v_replace_btn);

    v_result_btn = TRsbPushButton("Посмотреть результат");
    v_result_btn.setPosition((CV_POS_X + 22), rownum);
    v_result_btn.setSize(15, 1);
    v_result_btn.onClicked(R2M(this_obj, "m_evnt_getresult_btn"));
    v_result_btn.enabled = false;
    AddControl(v_result_btn);

    setSize(45, 14);
    setPosition(20, 5);

    addEventHandler(RSB_EV_KEY_PRESSED, r2m(this_obj, "m_evnt_on_key_pressed"));
  End;

  InitTRsbPanel();

  m_init_oper_update_form(this);

END;

gv_evnt_sel_prm = c_evnt_selection_prm();
gv_result_rep = c_result_report("Проводки", "┌─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┐");

var v_update_form = c_oper_update_form();
v_update_form.run;

exit(1);