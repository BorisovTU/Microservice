/*
usr_connect_attr.mac
*/
import CTInter, oralib, likepy, globals;

const ATTR_GROUP_ED_FORMAT = 101;    // группа категории "Формат ED для УФЭБС"
const ATTR_GROUP_DOCUMENT_BIS = 102; // группа категории "Код документа БИС"

/*
По основным параметрам находит t_attrid категории
*/
macro Get_attrid_by_name (objecttype, groupid, name)
  var sql = "select t_attrid from dobjattr_dbt where t_objecttype = :1 and t_groupid = :2 and t_name = :3";
  var attrid = -1;

  sql = ExecSqlselect(sql, MakeArray(SqlParam("1", objecttype), SqlParam("2", groupid), SqlParam("3", name)));
  if (sql.MoveNext() )
    attrid = sql.value(0);
  end;
  return attrid;
onerror()
  return attrid;
End;

/*
Берёт значение категории pm_groupid с платежа (только одно - основное) и устанавливает на проводку значение категории trn_groupid с таким же именем
*/
macro Connect_attr_from_paym_to_trn (paymentid, acctrnid, pm_groupid, trn_groupid, connect_date)
  var PM_Categ  = RsbObjCategories(OBJTYPE_PAYMENT, string(paymentid:o:10));
  var TRN_categ = RsbObjCategories(OBJTYPE_DOCUMENT, string(acctrnid:o:34));
  var attrid    = -1;
  var Valid_from_date = ifThenElse(valtype(connect_date) != V_UNDEF, connect_date, {curdate});
  record pm_attr("objattr.dbt");

  if (PM_Categ.GetMainAttr(pm_groupid, {curdate}, pm_attr) != 0 )
    return; //Не найдено значение категории на платеже
  end;

  attrid = Get_attrid_by_name(OBJTYPE_DOCUMENT, trn_groupid, pm_attr.Name);

  if (attrid > -1)
    TRN_categ.ConnectAttr(trn_groupid, attrid, null, null, Valid_from_date);
    TRN_Categ.Save();
  end;
End;

//Присвоение категории на проводку. attrid находит по значению
macro Connect_attr_to_trn (acctrnid, groupid, name, connect_date)
  var TRN_categ = RsbObjCategories(OBJTYPE_DOCUMENT, string(acctrnid:o:34));
  var Valid_from_date = ifThenElse(valtype(connect_date) != V_UNDEF, connect_date, {curdate});
  var attrid = Get_attrid_by_name(OBJTYPE_DOCUMENT, groupid, name);

  if (attrid > -1)
    TRN_categ.ConnectAttr(groupid, attrid, null, null, Valid_from_date);
    TRN_Categ.Save();
  end;
End;

//Присвоение категории на платеж. attrid находит по значению
macro Connect_attr_to_paym (paymid, groupid, name, connect_date)
  var PM_Categ  = RsbObjCategories(OBJTYPE_PAYMENT, string(paymid:o:10));
  var Valid_from_date = ifThenElse(valtype(connect_date) != V_UNDEF, connect_date, {curdate});
  var attrid = Get_attrid_by_name(OBJTYPE_PAYMENT, groupid, name);

  if (attrid > -1)
    PM_Categ.ConnectAttr(groupid, attrid, null, null, Valid_from_date);
    PM_Categ.Save();
  end;
End;