//-----------------------------------------------------------------------------------------
// Библиотека функций для настройки дополнительных параметров для выбора схемы расчетов
// 02.03.2019 В.Горленко 1я версия 
// 04.03.2019 А.Рогалев  переработан (Rogl)
//-----------------------------------------------------------------------------------------

import oratools, globals, ptInter, fiInter, likepy;

//BankInter, RsbDataSet, CTInter, OprInter,
//create table user_corsparm (t_number number (5), t_fiid number (10), t_gsid number (10), t_oper number (5), t_sysdate date);
//create unique index user_corsparm_idx0 on user_corsparm (t_number, t_fiid, t_gsid);

   
Private const kbEnter = 13, kbESC = 27, kbSpace = 32, kbF3 = 317, kbF8 = 322, kbF9 = 323;
Private var ptname;
Private var can_secur, can_currency, can_metall, can_banknote, can_mbk;

Private macro IIF(condition, value_true, value_false)
    If(condition)
       return value_true;
    else
       return value_false;
    end;
end;

Private class (TRecHandler) CParmPanel ( _rs, num, fi)
    Var rs = _rs, number = num, fiid = fi, 
        pValue, dealkind = -1, dealkindList = makeArray (), dealkindNum  = makeArray (), sql, sBuff, isnew;

    initTRecHandler ("parm", "corsparm.lbr", true);
    sBuff = TRecHandler ("parm", "corsparm.lbr", true);

    dealkindNum [dealkindNum.size] = 0; dealKindList [dealKindList.size] = "Все виды сделок";
    dealkindNum [dealkindNum.size] = 1; dealKindList [dealKindList.size] = "Сделки с ценными бумагами";
    dealkindNum [dealkindNum.size] = 2; dealKindList [dealKindList.size] = "Сделки с валютой";
    dealkindNum [dealkindNum.size] = 3; dealKindList [dealKindList.size] = "Сделки с драгметаллами";
    dealkindNum [dealkindNum.size] = 4; dealKindList [dealKindList.size] = "Банкнотные сделки";
    dealkindNum [dealkindNum.size] = 5; dealKindList [dealKindList.size] = "Сделки МБК";

    Macro change
        for (Var x, 0, fldNumber ()-1, 1)
            if ( this.(x) != sBuff.(x) )
                return true;
            end;
        end;
        return false;
    End;
    
    Macro check
        Var sql; 
        if ( int (ПолучитьКодСубъекта (this.("code"), 1)) == 0 )
            msgBox ("Некорректно указан контрагент");
            setFocus (this, fldIndex ("code"));
        elif ( isNew ) 
            sql = execSqlSelect ("select count(1) from user_corsparm where t_number=:1 and t_fiid=:2 and t_partyid=:3  and t_opkind=:4",
                                 makeArray (sqlParam ("1", number), 
                                            sqlParam ("2", fiid), 
                                            sqlParam ("3", int (ПолучитьКодСубъекта (this.("code"), 1))), 
                                            sqlParam ("4", ifThenElse (dealkind < 0, 0, dealkindNum [dealkind])) 
                                           )
                                );
            sql.movenext;
            if (int(sql.value(0)) > 0)
               msgbox ("Указанный параметр уже имеется в настройках данной корсхемы");
               setFocus (this, fldIndex ("code"));
            else
               return true;
            end;
        else
            return true;
        end;
        return false;
    End;

    Macro save
        if ( isNew )
            if ( execSql ("insert into user_corsparm values (:1, :2, :3, :4, :5, sysdate)",
                          makeArray (sqlParam ("1", number),
                                     sqlParam ("2", fiid),
                                     sqlParam ("3", int (ПолучитьКодСубъекта (this.("code"), 1))),
                                     sqlParam ("4", ifThenElse (dealkind < 0, 0, dealkindNum [dealkind])),
                                     sqlParam ("5", {oper})), FALSE) == NULL )
                msgBox ("Ошибка сохранения записи");
                return false;
            end;
            rs.addNew ();
            rs.value ("t_number") = number;
            rs.value ("t_fiid") = fiid;
            rs.value ("t_partyid") = int (ПолучитьКодСубъекта (this.("code"), 1));
            rs.value ("t_ptname") = this.("name");
            rs.value ("t_opkind") = ifThenElse (dealkind < 0, 0, dealkindNum [dealkind]);
            rs.value ("t_currency") = ПолучитьКодФинИн (fiid);
            rs.value ("t_opname") = this.("dealkind");
            rs.update ();
        else
            if ( execSql ("update user_corsparm set t_opkind = :1 where t_number = :2 and t_fiid = :3",
                          makeArray (sqlParam ("1", ifThenElse (dealkind < 0, 0, dealkindNum [dealkind])),
                                     sqlParam ("2", number),
                                     sqlParam ("3", fiid)), FALSE) == NULL )
                msgBox ("Ошибка сохранения записи");
                return false;
            end;
            rs.edit ();
            rs.value ("t_opkind") = ifThenElse (dealkind < 0, 0, dealkindNum [dealkind]);
            rs.value ("t_opname") = this.("dealkind");
            rs.update ();
        end;
        return true;
    End;
    
    Macro handler ( dlg, cmd, id, key )
        if ( cmd == DLG_SETFOCUS )
            pValue = dlg.(id);
        elif ( cmd == DLG_INIT )
            copy (sBuff, this);
            if ( not isNew )
                disableFields (dlg, fldIndex ("code"));
                updateFields (dlg);
            end;
        elif ( (cmd == DLG_KEY) and (key == kbF3) and (id == fldIndex ("code")) and listPt (key = TRecHandler ("party"), 1, dlg.("code"), PTLIST_ALLCLIENT, int (ПолучитьКодСубъекта (dlg.("code"), 1))) )
            dlg.("name") = key.("shortName");
            pValue = dlg.(id);
            updateFields (dlg);
        elif ( (cmd == DLG_REMFOCUS) and (pValue != dlg.(id)) and (id == fldIndex ("code")) )
            if ( key = ПолучитьКодСубъекта (dlg.("code"), 1) )
                key = execSqlSelect ("select t_shortname from dparty_dbt where t_partyid=:1", makeArray (sqlParam ("1", key)));
                if ( key.moveNext ()  )
                    dlg.("name") = key.value (0);
                end;
            else
                dlg.("name") = "";
            end;
            updateFields (dlg);
        elif ( (cmd == DLG_KEY) and (key == kbF3) and (id == fldIndex ("dealkind")) and ((key = menu (dealkindList, NULL, NULL, 19, 10, dealkind)) >= 0) )
            dlg.("dealkind") = dealkindList [dealkind = key];
        elif ( (cmd == DLG_KEY) and (key == kbSpace) and (id == fldIndex ("dealkind")) )
            dealkind = 0;
            dlg.("dealkind") = dealKindList [0];
            updateFields (dlg);
        elif ( (cmd == DLG_KEY) and (key == kbESC) and change () )
            if ( (key = msgBoxEx ("Запись изменена. Сохранить?", MB_Yes+MB_No+MB_Cancel)) == IND_CANCEL )
                return CM_IGNORE;
            elif ( key == IND_YES )
                return CM_SAVE;
            end;
        elif ( (cmd == DLG_SAVE) and (not (check () and save ())) )
            return CM_CANCEL;
        end;
    End;
    
    Macro new
        isnew = true;
        return runDialog (this, r2m (this, "handler"));
    End;

    Macro edit
        isnew = false;
        this.("code") = ПолучитьКодСубъекта (int (rs.value ("t_partyid")), 1);
        Var sql = execSqlSelect ("select t_shortname from dparty_dbt where t_partyid=:1", makeArray (sqlParam ("1", int (rs.value ("t_partyid")))));
        if ( sql.movenext () )
            this.("name") = sql.value (0);
        end;
        dealkind = find (dealkindNum, int (rs.value ("t_opkind")));
        this.("dealkind") = rs.value ("t_opname");
        return runDialog (this, r2m (this, "handler"));
    End;
End;
//-----------------------------------------------------------------------------------------

class CCoshemParm
    Var rs;
    Macro listParm ( num, fiid, curr )
        while ( true )
            Var rs = execSqlSelect (  " select t.*, "
                                    + "        (select t_fi_code from dfininstr_dbt where t_fiid = t.t_fiid) t_currency, "
                                    + "        (select t_shortname from dparty_dbt where t_partyid = t.t_partyid) t_ptname, "
                                    + "        decode (t_opkind, 0, 'Все виды сделок', 1, 'Сделки с ценными бумагами', 2, 'Сделки с валютой', 3, 'Сделки с драгметаллами' , 4, 'Банкнотные сделки' , 5, 'Сделки МБК') t_opname "
                                    + " from user_corsparm t "
                                    + " where t_number = :1 and t_fiid = :2 "
                                    + " order by t_partyid, t_opkind ",
                                    makeArray (sqlParam ("1", num), sqlParam ("2", fiid)), NULL, RSDVAL_CLIENT, RSDVAL_STATIC);
                
            rs.updateCommand = rsdCommand ("select 1 from dual");
            rs.insertCommand = rsdCommand ("select 1 from dual");
            rs.deleteCommand = rsdCommand ("select 1 from dual");
            if ( not runScroll (rs, 4, makeArray ("t_number", "Номер сх.", 5, 0, 0, 0, "t_currency", "Валюта", 3, 0, 3, 0, "t_ptname", "Контрагент по сделке", 50, 0, 50, 0, "t_opname", "Вид сделок", 25, 0, 25, 0),
                                NULL, r2m (this, "Handler1"), "Настройки для схмы расчетов "+num+" валюта "+curr, "Esc Выход  F8 Удалить  F9 Добавить", TRUE, 0, 0) )
                break;
            end;
        end;
    End;

    Macro handler ( rs, cmd, id, key )
        if ( (cmd == DLG_KEY) and (key == kbEnter) and (rs.recCount) )
            listParm (int (rs.value ("t_number")), int (rs.value ("t_fiid")), rs.value ("t_currency"));
            return CM_IGNORE;
        end;
    End;

    Macro Handler1 ( rs, cmd, id, key )
        if ( (cmd == DLG_KEY) and (key == kbF9) and CParmPanel (rs, int (this.rs.value ("t_number")), int (this.rs.value ("t_fiid"))).new () )
            goToScroll (rs);
            updateScroll (rs);
        elif ( (cmd == DLG_KEY) and (key == kbEnter) and (rs.recCount) )
            if ( CParmPanel (rs, int (this.rs.value ("t_number")), int (this.rs.value ("t_fiid"))).edit () )
                updateScroll (rs);
            end;
            return CM_IGNORE;
        elif ( (cmd == DLG_KEY) and (key == kbF8) and (rs.reccount) and getTrue (true, "Удалить запись?") )
            if ( execSql ("delete user_corsparm where t_number=:1 and t_fiid=:2 and t_partyid=:3 and t_opkind=:4", 
                          makeArray (sqlParam ("1", int (rs.value ("t_number"))), sqlParam ("2", int (rs.value ("t_fiid"))), sqlParam ("3", int (rs.value ("t_partyid"))), sqlParam ("4", int (rs.value ("t_opkind")))), 
                          FALSE
                         ) == NULL 
               )
                msgbox ("Ошибка удаления записи");
            else
                rs.delete ();
                if ( rs.reccount )
                    updateScroll (rs, 5);
                else
                    return CM_SELECT;
                end;
            end;
        end;
    End;

    Macro run
        runScroll (rs = execSqlSelect ("select t_number, t_name, t_fiid, (select t_fi_code from dfininstr_dbt where t_fiid=t.t_fiid) t_currency from dcorschem_dbt t order by t_fiid, t_number",
                                       NULL, NULL, RSDVAL_CLIENT, RSDVAL_STATIC),
                        3, makeArray ("t_number", "Номер", 5, 0, 0, 0, "t_currency", "Валюта", 3, 0, 3, 0, "t_name", "Наименование", 40, 0, 40, 0),
                        "CORSPARM", r2m (this, "handler"), "Параметры схем расчетов", "Esc Выход  Enter Параметры", TRUE, 0, 0);
    End;
End;
//-----------------------------------------------------------------------------------------

class CCoshemParm_by_gs 
    Var rs, gsid, gscode;

    Macro handler ( rs, cmd, id, key )
        if ( (cmd == DLG_KEY) and (key == kbEnter) and (rs.recCount) )
            return CM_IGNORE;

        elif ( (cmd == DLG_KEY) and (key == kbF9) 
               and not listCorschem (key = TRecHandler ("corschem"), "Корсхемы",  iif (GetTrue(true, "Смотрим ностро-схемы?"), "X", ""))   )
        
            if (not execSql ("insert into user_corsparm values (:1, :2, :3, :4, sysdate)", 
                              makeArray (sqlParam ("1", key.("Number")), sqlParam ("2", key.("fiid")), sqlParam ("3", gsid), sqlParam ("4", {oper})), 
                              FALSE)  );
                msgbox ("Ошибка вставки записи");
                return CM_IGNORE;
            end;

            rs.addNew ();
            rs.value ("t_number") = key.("Number");
            rs.value ("t_fiid") = key.("fiid");
            rs.value ("t_currency") = ПолучитьКодФинИн (key.("fiid"));
            rs.value ("t_name") = key.("name");
            rs.update ();
            goToScroll (rs);
            updateScroll (rs, 5);
        elif ( (cmd == DLG_KEY) and (key == kbF8) and (rs.reccount) and getTrue (true, "Удалить запись?") )
            if ( execSql ("delete user_corsparm where t_number=:1 and t_fiid=:2 and t_gsid=:3", 
                          makeArray (sqlParam ("1", int (rs.value ("t_number"))), sqlParam ("2", int (rs.value ("t_fiid"))), sqlParam ("3", gsid)), FALSE
                         ) == NULL 
               )
                msgbox ("Ошибка удаления записи");
            else
                rs.delete ();
                if ( rs.reccount )
                    updateScroll (rs, 5);
                else
                    return CM_SELECT;
                end;
            end;
        end;
    End;

    Macro run (type_entity, entity_rec)
        if (type_entity == 1) //генсоглашения ФИССиКО
        elif (type_entity == 2) //генсоглашения МБК
        end;

        gsid = entity_rec.GenagrID;
        gscode = entity_rec.Code;
        rs = execSqlSelect (  "select t.t_number t_number, t.t_name t_name, t.t_fiid t_fiid, (select t_fi_code from dfininstr_dbt where t_fiid=t.t_fiid) t_currency "
                            + "from dcorschem_dbt t, (select :1 gsID from dual) gs, user_corsparm u "
                            + "where u.t_number = t.t_number and u.t_fiid = t.t_fiid and u.t_gsID = gs.gsID "
                            + "order by t_fiid, t_number",
                            makeArray (sqlParam ("1", gsid)), NULL, RSDVAL_CLIENT, RSDVAL_STATIC);
        rs.updateCommand = rsdCommand ("select 1 from dual");
        rs.insertCommand = rsdCommand ("select 1 from dual");
        rs.deleteCommand = rsdCommand ("select 1 from dual");
        runScroll (rs, 3, makeArray ("t_number", "Номер", 5, 0, 0, 0, "t_currency", "Валюта", 3, 0, 3, 0, "t_name", "Наименование", 40, 0, 40, 0),
                   "CORSPARM", r2m (this, "handler"), string("Схемы расчетов для генсоглашения ", gscode), "Esc Выход F8 Удалить F9 Добавить", TRUE, 0, 0);
    End;
End;
//-----------------------------------------------------------------------------------------

