/*
$Name:        paymswift.mac
$Description: Модификация SWIFT-сообщения по платежу
$Programmer:  Кротов А.
*/
import dlquery, OprInter, PaymInter, ctinter;
//import settl;
import InsCarryDoc, BankInter;
import captureoutput;


/*Генерация SWIFT-сообщения по платежу*/
macro CreateMessageSWIFT(paymentid:integer, ID_Operation, ID_Step, IsStepOp:bool, mess:@string, errmsg:@string, ShowMessage:bool)
  var MesID;
  var cmdMes, DataSetMes;
  var op = -1;
  var res, resmes;
  mess = "";
  /*На шаге операции*/
  if((valtype(IsStepOp) == V_BOOL) and (IsStepOp))
    if(InsertPaymentMessage(paymentid, OPR_ID_TMPFILE/*OPR_ID_MEMORY*//*OPR_ID_TMPFILE*//*OPR_ID_REAL*/, @MesID))
      errmsg = "Ошибка при генерации сообщения по платежу (" + paymentid + ")";
      return false;
    end;
    if((valtype(ShowMessage) == V_BOOL) and (ShowMessage))
      cmdMes = DL_RSDCommand("select ':'||tf.t_Name || ':'||mv.t_Value t_mes "
                           + "from dwlmesval_tmp mv, dwltpfld_dbt tf "
                           + "where mv.t_MesID = ? "
                           + "      and tf.t_TpFieldID = mv.t_TpFieldID "
                           + "order by mv.t_Index");
      cmdMes.AddParam(MesID);
      DataSetMes = cmdMes.Execute();
      while(DataSetMes.moveNext())
        mess = mess + DataSetMes.value("t_mes")
      end;
    end;
  /*Вне шага операции*/
  else
    op = SetDialogFlag(0);
    res = PM_GenerateMessage(paymentid);
    SetDialogFlag(op);
    if(res)
      resmes = "";
      /*Определяем текст сообщения об ошибке*/
      if((valtype(res) == V_INTEGER) and (res > 0))
        cmdMes = DL_RSDCommand("select bank.t_contents t_mes "
                             + "from dbank_msg bank "
                             + "where t_number = ?");
        cmdMes.AddParam(res);
        DataSetMes = cmdMes.Execute();
        if(DataSetMes.moveNext())
          resmes = resmes + " \"" + DataSetMes.value("t_mes") + "\"";
        end;
      end;
      errmsg = "Ошибка" + resmes + " при генерации сообщения по платежу (" + paymentid + ")";
      return false;
    end;
    cmdMes = RSDCommand("insert into dwlpm_u_dbt(t_paymentid, t_id_operation, t_id_step) values(?,?,?)");
    cmdMes.AddParam("paymentid", RSDBP_IN, paymentid);
    cmdMes.AddParam("ID_Operation", RSDBP_IN, ID_Operation);
    cmdMes.AddParam("ID_Step", RSDBP_IN, ID_Step);
    cmdMes.Execute();
    if((valtype(ShowMessage) == V_BOOL) and (ShowMessage))
      cmdMes = DL_RSDCommand("select ':'||wltpfld.t_name || ':'||wlmesval.t_value t_mes "
                           + "from dwlpm_dbt wlpm "
                           + "join dwlmeslnk_dbt wlmeslnk on wlmeslnk.t_objid = wlpm.t_wlpmid and wlmeslnk.t_objkind = 501/*Платеж*/ "
                           + "join dwlmesval_dbt wlmesval on wlmesval.t_mesid = wlmeslnk.t_mesid "
                           + "join dwltpfld_dbt wltpfld on wltpfld.t_tpfieldid = wlmesval.t_tpfieldid "
                           + "where wlpm.t_paymentid = ? "
                           + "order by wlmesval.t_index");
      cmdMes.AddParam(paymentid);
      DataSetMes = cmdMes.Execute();
      while(DataSetMes.moveNext())
        mess = mess + DataSetMes.value("t_mes")
      end;
    end;
  end;
  /*Показываем сообщение*/
  if((valtype(ShowMessage) == V_BOOL) and (ShowMessage))
    msgboxex("Сообщение:|"+mess);
  end;
  /*Возвращаем результат*/
  return true;
onError(er)
  errmsg = "Исключение: " + er.module + " " + er.line + " " + er.message;
  if(op >= 0)
    SetDialogFlag(op);
  end;
  return false;
end;


/*Удаление SWIFT-сообщения по платежу*/
macro DeleteMessageSWIFT(paymentid:integer, errmsg:@string)
  var cmdMes;
  StartCaptureOutput();
  [ 
    declare 
      errmes varchar2(4000);
      res integer := 1;
      paymentid number := ?;--111;
      wlpmid dwlpm_dbt.t_wlpmid%type;
      mesid dwlmeslnk_dbt.t_mesid%type;
    begin
      begin
        -- Ищем данные о выгрузке платежа
        begin
          select t_wlpmid
          into wlpmid
          from dwlpm_dbt wlpm
          where wlpm.t_paymentid = paymentid;
        exception when no_data_found then
          -- По БОЦБ сообщение удаляется при удалении платежа
          wlpmid := 0;
          -- Удаляем пользовательскую связку
          delete from dwlpm_u_dbt wlpm_u
          where wlpm_u.t_paymentid = paymentid;
        end;
        -- Ищем сообщение по платежу
        if((res != 0) and (wlpmid > 0))then
          begin
            select wlmeslnk.t_mesid
            into mesid
            from dwlmeslnk_dbt wlmeslnk
            where wlmeslnk.t_objid = wlpmid and
                  wlmeslnk.t_objkind = 501/*Платеж*/;
          exception when no_data_found then
            errmes := 'Сообщение по платежу не найдено';
            res := 0;
          end;
        end if;
        -- Удаляем сообщение
        if((res != 0) and (wlpmid > 0))then
          -- dwlhistor_dbt
          delete from dwlhistor_dbt wlhistor
          where wlhistor.t_objid = mesid and 
                wlhistor.t_objkind = 502/*Сообщение МБР*/;
          -- dwlmesval_dbt
          delete from dwlmesval_dbt wlmesval
          where wlmesval.t_mesid = mesid;
          -- dwlmes_dbt
          delete from dwlmes_dbt wlmes
          where wlmes.t_mesid = mesid;
          -- dwlmeslnk_dbt
          delete from  dwlmeslnk_dbt wlmeslnk
          where wlmeslnk.t_objid = wlpmid and
                wlmeslnk.t_objkind = 501/*Платеж*/ and
                wlmeslnk.t_mesid = mesid;
          -- dwlpm_dbt
          delete from dwlpm_dbt wlpm
          where wlpm.t_paymentid = paymentid;
          -- Удаляем пользовательскую связку
          delete from dwlpm_u_dbt wlpm_u
          where wlpm_u.t_paymentid = paymentid;
        end if;
      exception when others then
        errmes := 'Исключение: '||sqlerrm;
        res := 0;
        rollback;
      end;
      -- Возвращаем результат
      ? := res;
      ? := errmes;
    end;
  ];
  cmdMes = RSDCommand(EndCaptureOutput());
  cmdMes.AddParam("paymentid", RSDBP_IN, paymentid);
  cmdMes.AddParam("res", RSDBP_OUT, V_INTEGER);
  cmdMes.AddParam("errmes", RSDBP_IN_OUT, "                                                                                                                        ");
  cmdMes.Execute();
  /*Возвращаем результат*/
  if(valtype(cmdMes.Value("res")) != V_INTEGER)
    errmsg = "Неизвестная ошибка при удалении сообщения по платежу";
    return false;
  elif(cmdMes.Value("res") != 1)
    if((valtype(cmdMes.Value("errmes")) != V_STRING) or (strlen(cmdMes.Value("errmes")) <= 0))
      errmsg = "Неизвестная ошибка при удалении сообщения по платежу";
      return false;
    else
      errmsg = cmdMes.Value("errmes");
      return false;
    end;
  else
    return true;
  end;
onError(er)
  errmsg = "Исключение: " + er.module + " " + er.line + " " + er.message;
  return false;
end;


/*Вставка/удаление SWIFT сообщения по платежу на постобработке выполнения/отката шага*/
macro ModifySWIFT(rsPM, CommitOrRollback, ID_Operation, ID_Step, ContrID)
  var cmdMes, rsMes;
  var pmObj, Transport;
  var errmsg;
  /*Откат шага*/
  if(CommitOrRollback == 2)
    cmdMes = DL_RSDCommand("select t_paymentid "
                         + "from dwlpm_u_dbt "
                         + "where t_id_operation = ? and "
                         + "      t_id_step = ? ");
    cmdMes.AddParam(ID_Operation);
    cmdMes.AddParam(ID_Step);
    rsMes = cmdMes.Execute();
    while(rsMes.movenext())
      if(not DeleteMessageSWIFT(rsMes.value("t_paymentid"), @errmsg))
        msgboxex("Ошибка удаления SWIFT-сообщения по платежу:|"+errmsg);
      end;
    end;
  /*Выполнение шага*/
  else
    /*Проходим по платежам*/
    while(rsPM.movenext())
      /*Ищем сообщение по платежу*/
      cmdMes = DL_RSDCommand("select count(1) cnt "
                           + "from dwlpm_dbt wlpm "
                           + "where wlpm.t_paymentid = ?");
      cmdMes.AddParam(rsPM.value("t_paymentid"));
      rsMes = cmdMes.Execute();
      rsMes.moveNext();
      if(rsMes.value("cnt") > 0)
        msgboxex("Ошибка формирования SWIFT-сообщения по платежу:|Сообщение уже сформировано");
      else
        /*Определение транспорта*/
        pmObj = RsbPayment(rsPM.value("t_paymentid"));
        /*Сохраняем текущий транспорт*/
        Transport = pmObj.OutTransport;
        /*Меняем транспорт на SWIFT*/
        pmObj.OutTransport = 2/*SWIFT*/;
        pmObj.OutTpSchem = 0;
        pmObj.OutRlsForm = 0;
        var TpID = 0, TpSchemID = 0, RlsFormID = 0, FormID = 0, SubKindMes = 0;
        /*Меняем позиционирование на пользовательское*/
        cmdMes = RSDCommand("update dpmprop_dbt "
                          + "set t_corrpostype = 1 "
                          + "where t_paymentid = ? and "
                          + "      t_debetcredit = 1 and "
                          + "      t_corrpostype != 1");
        cmdMes.AddParam("paymentid", RSDBP_IN, pmObj.PaymentID);
        cmdMes.Execute();
        /*Определяем подтип формы/релиза*/
        if((valtype(ContrID) == V_INTEGER) and (ContrID > 0))
          cmdMes = DL_RSDCommand("select count(1) cnt "
                               + "from dpartyown_dbt partyown "
                               + "where partyown.t_partyid = ? and "
                               + "      partyown.t_partykind = 2/*Банком*/");
          cmdMes.AddParam(ContrID);
          rsMes = cmdMes.Execute();
          rsMes.moveNext();
          if(rsMes.value("cnt") == 0)
            pmObj.OutSubKindMessage = 1; //Клиентский перевод (поручение)
            //SVE Сделан костыль чтобы явно определять формы релизов между МТ103 и МТ202
            pmObj.OutRlsForm = 139;
            pmObj.OutTpSchem = 51;
          else
            pmObj.OutSubKindMessage = 3; //Общий банковский перевод
            
            pmObj.OutRlsForm = 141;
            pmObj.OutTpSchem = 51;
          end;
          SubKindMes = pmObj.OutSubKindMessage;
        end;                             //-SVE
/*	                                 Закоментил чтобы не сбивало работу костыля
        if(DefineRlsForm(pmObj, TpID, TpSchemID, FormID, RlsFormID, SubKindMes))
        msgboxex(pmObj.OutSubKindMessage+"|"+SubKindMes);
          /*Проконтролируем подтип формы/релиза*/
          if((valtype(ContrID) == V_INTEGER) and (ContrID > 0) and (pmObj.OutSubKindMessage != SubKindMes))
            msgboxex("Ошибка формирования SWIFT-сообщения по платежу:|Не удалось установить подтип формы/релиза|Откатите шаг операции и выполните его снова");
            pmObj.OutTransport = Transport;
          else
            pmObj.OutTransport = TpID;
            pmObj.OutTpSchem = TpSchemID;
            pmObj.OutRlsForm = RlsFormID;
            pmObj.OutSubKindMessage = SubKindMes;
            Transport = pmObj.OutTransport;
          end;
        else 
          msgboxex("Ошибка формирования SWIFT-сообщения по платежу:|Не удалось подобрать релиз для вида платежа");
          pmObj.OutTransport = Transport;
        end;
*/
        /*Формируем сообщение*/
        if((pmObj.OutTransport == 2/*SWIFT*/) and (not CreateMessageSWIFT(rsPM.value("t_paymentid"), ID_Operation, ID_Step, false/*IsStepOp*/, null, @errmsg, false/*ShowMessage*/)))
          msgboxex("Ошибка формирования SWIFT-сообщения по платежу:|"+errmsg)
        end;
      end;
    end;
  end;

  /*Возвращаем результат*/
  return 1;
end;


//Rogl
//Установка предписывающей категории на рублевом платеже, какой тип ED нужно формировать
macro SetTypeED (PaymentID, ContrID, GenagrID, GenagrObjtype)
  var ObjCat;
  var ObjID;
  var cmdsql, rssql, IsAttr, stat, err_txt, typeED, isBank, isNOTUSE107;

  if((valtype(contrid) == V_INTEGER) and (contrid > 0))
    cmdsql = DL_RSDCommand("select count(1) cnt "
                         + "from dpartyown_dbt partyown "
                         + "where partyown.t_partyid = ? and "
                         + "      partyown.t_partykind = 2/*Банком*/");
    cmdsql.AddParam(ContrID);
    rssql = cmdsql.Execute();
    rssql.moveNext();
    if(rssql.value("cnt") == 0)
      isBank = false;
    else
      isBank = true; 
    end;
  end;

  //Если в сделке МБК не задано генсоглашение, поищем его сами  
  if ( (valtype(contrid) == V_INTEGER) and (GenagrID<=0) and (GenagrObjtype ==124)) 
    cmdsql = DL_RSDCommand("select t_GenagrID gID "
                         + "from ddl_genagr_dbt "
                         + "where t_dockind = 4611 and t_partyID = ? and rownum = 1 ");
    cmdsql.AddParam(contrid);
    rssql = cmdsql.Execute();
    if (rssql.moveNext())
      GenagrID = rssql.value("gID");
    end;
  end;

  if (GenagrID>0)
     ObjID = String(GenagrID:o:34);
     ObjCat = RsbObjCategories( GenagrObjtype, ObjID);
     isNOTUSE107 = ObjCat.IsAttrPresense(101, 1, null, null, true, {curdate});
  else
     isNOTUSE107 = true;
  end;

  if (isBank and not isNOTUSE107)
    typeED = 7;
  else
    typeED = 1;
  end;

  //Msgbox(string ("isBank = ", isBank, ", GenagrID=", GenagrID, ", typeED=", typeED));

  ObjID = String(PaymentID:o:10);
  ObjCat = RsbObjCategories( 501, ObjID);
  ObjCat.DisconnectAttr(101, 0);

  //IsAttr = ObjCat.IsAttrPresense(101, typeED, null, null, true, {curdate});
  //if (IsAttr)
  //   Msbbox(string("Категория <Формат ED для УФЭБС> уже установлена на платеж с ID = ",PaymentID,"!"));
  //else
  //end;

  stat = ObjCat.ConnectAttr(101, typeED, null, null, {curdate} );
  if (stat > 0)
     err_txt = String("Ошибка "+stat+" установки категории <Формат ED для УФЭБС>, значение Id=",typeED);
     Msgbox(err_txt);
     return 1;
  else
     stat = ObjCat.Save(ObjID);
  end;

  return stat;
end;

