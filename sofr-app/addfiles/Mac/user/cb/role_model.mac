/*
role_model.mac
*/
import oralib, likepy, RsbFormsInter, globals, lib_lang;
import CbForms_h;

private const ESC      = 27,
              F2       = 316,
              F3       = 317,
              F6       = 320,
              F8       = 322,
              F9       = 323,
              kbInsert = 338,
              kbDelete = 339,
              ctrl_R   = 18,
              Enter    = 13,
              ctrl_g   = 7;

private var errtext:string = "";

//Накапливает строку с ошибками
private macro AddErr (err)
  if (errtext == "")
    errtext = err;
  else
    errtext = errtext + "\n" + err;
  end;
End;

//Выводит строку с ошибками, или переданную ошибку.
//Возвращает true при наличии ошибок
private macro ShowErr (err:string)
  if (valtype(err) != V_UNDEF) 
    MsgBox(err);
    return true;
  end;

  if (errtext == "")
    return false;
  end;
  MsgBox(errtext);
  errtext = "";
  return true;
End;

macro GetMainOperInGroup (dockind:integer, kind_oper:integer, Department:integer):integer
  DefaultParm(kind_oper, 0);
  DefaultParm(Department, {OperDprt});
  var oper:integer = -1; 

// def-47615
  var query_t = " select * from dobjects_dbt obj, dllvalues_dbt dll where obj.t_objecttype = dll.t_list and obj.t_code = 'Teh_Oper' and dll.t_code = to_char(:oper)";
  var sql_t = ExecSqlSelect(query_t,MakeArray(SqlParam("oper", {oper})));
  if (sql_t.MoveNext() ) // входит в список технических, продолжаем обработку
     oper = -1;
  else
     return {oper} ;
  end;

  if (oper == -1)
     var query = "select (select t_comment from dacsgroup_dbt where t_groupid = t.t_groupid) opernum from RSHB_FD_GROUPS t where t_dockind = :dockind and t_kind_operation = :kind_oper and t_department = :dep";
     var sql = ExecSqlSelect(query, MakeArray(SqlParam("dockind", DocKInd), SqlParam("kind_oper", kind_oper), SqlParam("dep", Department)));

     if (sql.MoveNext() )
       oper = sql.value(0);
     end;

     if ((oper == -1) and (kind_oper > 0))
       kind_oper = 0;
       sql = ExecSqlSelect(query, MakeArray(SqlParam("dockind", DocKInd), SqlParam("kind_oper", kind_oper), SqlParam("dep", Department)));
       if (sql.MoveNext() )
         oper = sql.value(0);
       end;
     end;
  end;

  if(oper < 0 )
    return 0;
  else
    return oper;
  end;

onerror()
  return 0;
End;

//Панель для редактирования/добавления записи
class (TRsbPanel) FD_Groups_panel (_DocKInd,
                                   _DocKInd_Name,
                                   _Operation,
                                   _Operation_Name,
                                   _Group,
                                   _Group_Name,
                                   _Department,
                                   _Department_Name,
                                   _Boffice,
                                   _Boffice_Name,
                                   _IsEdit
                                   )
  DefaultParm(_DocKInd, 0);
  DefaultParm(_DocKInd_Name, "");
  DefaultParm(_Operation, 0);
  DefaultParm(_Operation_Name, "");
  DefaultParm(_Group, 0);
  DefaultParm(_Group_Name, "");
  DefaultParm(_Department, {OperDprt});
  DefaultParm(_Department_Name, "");
  DefaultParm(_Boffice, "");
  DefaultParm(_Boffice_Name, "");
  DefaultParm(_IsEdit, false);

  var FT_INT     = 0,
      FT_DOUBLE  = 4,
      FT_STRING  = 7,
      FT_DATE    = 9,
      FT_CHR     = 12,
      FT_NUMERIC = 25;

  var
      fld_DocKind         = null, // поле "Первичный документ"
      fld_DocKind_name    = null, // поле "Наименование первичного документа"
      fld_operation       = null, // поля "Вид операции"
      fld_operation_name  = null, // поле "Наименование операции"
      fld_group           = null, // поле "Группа"
      fld_group_name      = null, // поле "Наименование группы"
      fld_department      = null, // поле "Филиал"
      fld_department_name = null, // поле "Наименование филиала"
      fld_Boffice         = null, // поле "Бэк-офис"
      fld_Boffice_name    = null; // поле "Наименование Бэк-офиса"


  var DocKInd:integer        = _DocKInd;
  var DocKInd_Name:string    = _DocKInd_Name;
  var Operation:integer      = _Operation;
  var Operation_Name:string  = _Operation_Name;
  var Group:integer          = _Group;
  var Group_Name:string      = _Group_Name;
  var Department:integer     = _Department;
  var Department_Name:string = _Department_Name;
  var Boffice:string         = _Boffice;
  var Boffice_Name:string    = _Boffice_Name;
  var IsEdit:bool            = _IsEdit; //Изменение уже введённой записи
  var IsBreak                = true; //Признак выхода по Escape

  macro init ()
    var rn = 0;

    SetSize    (50, 6); //Ширина, высота
    SetPosition(20,  10);

    setCaption("Добавление/редактирование записи");

    rn = rn + 1;
    addLabel(TRsbLabel(2, rn, "ПД"));

    fld_DocKind = TRsbEditField(FT_INT);
    fld_DocKind.setPosition(9, rn);
    fld_DocKind.setSize (5, 1);
    fld_DocKind.name       = "DocKind";
    fld_DocKind.textLength = 4;
    fld_DocKind.Editable   = IfThenElse(IsEdit, false, true);
    fld_DocKind.value      = DocKInd;
    fld_DocKind.onKeyPressed( R2M(this, "OnPressed") );
    addControl(fld_DocKind);

    fld_DocKind_name = TRsbEditField(FT_STRING);
    fld_DocKind_name.setPosition(15, rn);
    fld_DocKind_name.setSize (25, 1);
    fld_DocKind_name.name       = "DocKind_name";
    fld_DocKind_name.textLength = 100;
    fld_DocKind_name.Editable   = false;
    fld_DocKind_name.value      = DocKInd_Name;
    addControl(fld_DocKind_name);
    
    /*****************************************************/
    rn = rn + 1;
    addLabel(TRsbLabel(2, rn, "Операция"));

    fld_operation = TRsbEditField(FT_INT);
    fld_operation.setPosition(9, rn);
    fld_operation.setSize (5, 1);
    fld_operation.name       = "Operation";
    fld_operation.textLength = 5;
    fld_operation.Editable   = IfThenElse(IsEdit, false, true);
    fld_operation.value      = Operation;
    fld_operation.onKeyPressed( R2M(this, "OnPressed") );
    addControl(fld_operation);

    fld_operation_name = TRsbEditField(FT_STRING);
    fld_operation_name.setPosition(15, rn);
    fld_operation_name.setSize (25, 1);
    fld_operation_name.name       = "Operation_name";
    fld_operation_name.textLength = 100;
    fld_operation_name.Editable   = false;
    fld_operation_name.value      = Operation_name;
    addControl(fld_operation_name);
    
    /*****************************************************/
    rn = rn + 1;
    addLabel(TRsbLabel(2, rn, "Группа"));

    fld_group = TRsbEditField(FT_INT);
    fld_group.setPosition(9, rn);
    fld_group.setSize (5, 1);
    fld_group.name       = "Group";
    fld_group.textLength = 6;
    fld_group.Editable   = true;
    fld_group.value      = Group;
    fld_group.onKeyPressed( R2M(this, "OnPressed") );
    addControl(fld_group);

    fld_group_name = TRsbEditField(FT_STRING);
    fld_group_name.setPosition(15, rn);
    fld_group_name.setSize (25, 1);
    fld_group_name.name       = "Group_name";
    fld_group_name.textLength = 100;
    fld_group_name.Editable   = false;
    fld_group_name.value      = Group_name;
    addControl(fld_group_name);

    /*****************************************************/
    rn = rn + 1;
    addLabel(TRsbLabel(2, rn, "Филиал"));

    fld_department = TRsbEditField(FT_INT);
    fld_department.setPosition(9, rn);
    fld_department.setSize (5, 1);
    fld_department.name       = "Department";
    fld_department.textLength = 4;
    fld_department.Editable   = IfThenElse(IsEdit, false, true);
    fld_department.value      = Department;
    fld_department.onKeyPressed( R2M(this, "OnPressed") );
    addControl(fld_department);

    fld_department_name = TRsbEditField(FT_STRING);
    fld_department_name.setPosition(15, rn);
    fld_department_name.setSize (25, 1);
    fld_department_name.name       = "Department_name";
    fld_department_name.textLength = 100;
    fld_department_name.Editable   = false;
    fld_department_name.value      = Department_name;
    addControl(fld_department_name);

    /*****************************************************/
    rn = rn + 1;
    addLabel(TRsbLabel(2, rn, "Бэк-офис"));

    fld_boffice = TRsbEditField(FT_STRING);
    fld_boffice.setPosition(9, rn);
    fld_boffice.setSize (5, 1);
    fld_boffice.name       = "Boffice";
    fld_boffice.textLength = 1;
    fld_boffice.Editable   = IfThenElse((Operation!=0), false, true);
    fld_boffice.value      = Boffice;
    fld_boffice.onKeyPressed( R2M(this, "OnPressed") );
    addControl(fld_boffice);

    fld_boffice_name = TRsbEditField(FT_STRING);
    fld_boffice_name.setPosition(15, rn);
    fld_boffice_name.setSize (25, 1);
    fld_boffice_name.name       = "boffice_name";
    fld_boffice_name.textLength = 100;
    fld_boffice_name.Editable   = false;
    fld_boffice_name.value      = boffice_name;
    addControl(fld_boffice_name);

    addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
  End;

  macro OnPressed( RsbEvent )
    var Choise, query;
    if( StrUpr(GenClassName(rsbEvent)) == "TRSBKEYPRESSEDEVENT" )
      if( (RsbEvent.Id == RSB_EV_KEY_PRESSED ) and (RsbEvent.KeyCode == F3) )
        //Выбор первички
        if (StrUpr(RsbEvent.source.name) == "DOCKIND")
          Choise = Dl_Scroll("select t_dockind, t_name from doprkdoc_dbt where t_primary = 'X' order by t_dockind",
                             "Виды первичных документов", 10, 10,
                             "t_dockind", "Номер", "t_name", "Первичный документ");
          if (Choise != null)
            fld_DocKind.value = Choise.value("t_dockind");
            fld_DocKind_name.value = Choise.value("t_name");
            fld_operation.value = 0;
            fld_operation_name.value = "";
          else
            fld_DocKind.value = 0;
            fld_DocKind_name.value = "";
            fld_operation.value = 0;
            fld_operation_name.value = "";
            fld_boffice.Editable = true;
            fld_boffice.ReDraw();
          end;
        end;

        //Выбор операции
        if (StrUpr(RsbEvent.source.name) == "OPERATION")
          query = "select t_kind_operation, t_dockind, t_name, (select t_name from doprkdoc_dbt where t_dockind = t.t_dockind) dockindname from doprkoper_dbt t " +
                  "where t_kind_operation > 0 and t_notinuse = chr(0) " + IfThenElse(fld_DocKind.value > 0, " and t_dockind="+fld_DocKind.value, "");
          Choise = Dl_Scroll(query, "Виды операций", 10, 5,
                             "t_kind_operation", "Номер операции",
                             "t_name", "Операция",
                             "t_dockind", "Номер ПД",
                             "dockindname", "Первичный документ");
          if (Choise != null)
            fld_DocKind.value = Choise.value("t_dockind");
            fld_DocKind_name.value = Choise.value("dockindname");
            fld_operation.value = Choise.value("t_kind_operation");
            fld_operation_name.value = Choise.value("t_name");
            fld_boffice.Editable = false;
            fld_boffice.ReDraw();
          else
            fld_operation.value = 0;
            fld_operation_name.value = "";
            fld_boffice.Editable = true;
            fld_boffice.ReDraw();
          end;
        end;

        //Выбор группы
        if (StrUpr(RsbEvent.source.name) == "GROUP")
          Choise = Dl_Scroll("select t_groupid, t_name from dacsgroup_dbt", "Группы", 10, 5,
                             "t_groupid", "Номер",
                             "t_name", "Группа");
          if (Choise != null)
            fld_group.value = Choise.value("t_groupid");
            fld_group_name.value = Choise.value("t_name");
          else
            fld_group.value = 0;
            fld_group_name.value = "";
          end;
        end;

        //Выбор филиала
        if (StrUpr(RsbEvent.source.name) == "DEPARTMENT")
          Choise = Dl_Scroll("select t_code, (select t_name from dparty_dbt where t_partyid = t.t_partyid) depname from ddp_dep_dbt t", "Филиалы", 10, 5,
                             "t_code", "Номер",
                             "depname", "Филиал");
          if (Choise != null)
            fld_department.value = Choise.value("t_code");
            fld_department_name.value = Choise.value("depname");
          end;
        end;

        //Выбор бэк-офиса
        if ( (StrUpr(RsbEvent.source.name) == "BOFFICE") and fld_boffice.Editable)
          //MsgBox(RsbEvent.Id);
          Choise = Dl_Scroll("select t_type_account t_code, t_name_type from dtypeac_dbt where t_inumtype = 22 and instr((select to_char(t_fmtblobdata_xxxx) from dregval_dbt where t_keyid = 2122), t_type_account) > 0",
                             "Список", 10, 5,
                             "t_code", "Код",
                             "t_name_type", "Бэк-офис");
          if (Choise != null)
            fld_boffice.value = Choise.value("t_code");
            fld_boffice_name.value = Choise.value("t_name_type");
          else
            fld_boffice.value = "";
            fld_boffice_name.value = "";
          end;
        end;

      end; //if( (RsbEvent.Id == RSB_EV_KEY_PRESSED ) and (RsbEvent.KeyCode == F3) )
    end;
    return true;
  end;

  macro eventHandler (EV)
    if (ev.KeyCode == F9) 
      if (fld_DocKind.value <= 0)
        AddErr("Не введён первичный документ");
      end;

      if (fld_group.value <= 0)
        AddErr("Не введена группа");
      end;

      if ( (fld_operation.value == 0) and (fld_boffice.value == "") )
        AddErr("Не введен бэк-офис");
      end;

      if (ShowErr())
        return;
      end;

      DocKInd    = fld_DocKind.value;
      Operation  = fld_operation.value;
      Group      = fld_group.value;
      Department = fld_department.value;
      Boffice    = IfThenElse((Operation!=0), "", fld_boffice.value);
      IsBreak    = false;
      close(0);
      return true;
    end;
  End;

  InitTRsbPanel();
  Init();
END;


//-------------------------------------------------------------------------------------------------------------------------

//Класс для добавления/удаления записей в RSHB_FD_GROUPS
class  FD_Groups ()
  var ID:integer         = 0;
  var FDID:integer       = 0;
  var Operation:integer  = 0;
  var Group:integer      = 0;
  var Department:integer = 0;
  var Boffice:string     = "";

  private macro GetNextVal ():integer
    var sql = ExecSqlSelect("select RSHB_FD_GROUPS_SEQ.nextval from dual");
    if (sql.MoveNext )
      return sql.value(0);
    end;
    return 0;
  onerror()
    return 0;
  End;

  //Найти запись по ИД
  private macro GetRecord (_id:integer)
    var sql = ExecSqlSelect("select t_dockind, t_kind_operation, t_groupid, t_department, t_boffice from RSHB_FD_GROUPS where t_id = :1", MakeArray(SqlParam("1", _id)));

    if (sql.MoveNext )
      ID = _id;
      FDID       = sql.value("t_dockind");
      Operation  = sql.value("t_kind_operation");
      Group      = sql.value("t_groupid");
      Department = sql.value("t_department");
      Boffice    = sql.value("t_boffice");
      return true;
    end;

    return false;
  onerror()
    ShowErr("Не найдена запись с ИД " + _id);
    return false;
  End;

  macro GetDocKindName ():string
    var name:string = "";
    var sql = ExecSqlSelect("select t_name from doprkdoc_dbt where t_dockind = :dockind", MakeArray(SqlParam("dockind", FDID)));
    if (sql.MoveNext )
      name = sql.value("t_name");
    end;
    return name;
  End;

  macro GetOperationName ():string
    var name:string = "";
    var sql = ExecSqlSelect("select t_name from doprkoper_dbt where t_kind_operation = :kind_oper", MakeArray(SqlParam("kind_oper", Operation)));
    if (sql.MoveNext )
      name = sql.value("t_name");
    end;
    return name;
  End;

  macro GetDepName ():string
    var name:string = "";
    var sql = ExecSqlSelect("select (select t_name from dparty_dbt where t_partyid = t.t_partyid) name from ddp_dep_dbt t where t_code = :dep", MakeArray(SqlParam("code", Department)));
    if (sql.MoveNext )
      name = sql.value("name");
    end;
    return name;
  End;

  macro GetGroupName ():string
    var name:string = "";
    var sql = ExecSqlSelect("select t_name from dacsgroup_dbt where t_groupid = :grp", MakeArray(SqlParam("grp", Group)));
    if (sql.MoveNext )
      name = sql.value("t_name");
    end;
    return name;
  End;

  macro GetBofficeName ():string
    var name:string = "";
    var sql = ExecSqlSelect("select t_name_type from dtypeac_dbt where t_inumtype = 22 and t_type_account = :bof", MakeArray(SqlParam("bof", Boffice)));
    if (sql.MoveNext )
      name = sql.value("t_name_type");
    end;
    return name;
  End;

  //Проверка данных в классе перед вставкой или изменением
  private macro CheckData (edit)
    DefaultParm(edit, false);
    var sql;

    if (not edit) 
      sql = "select 1 from RSHB_FD_GROUPS where t_dockind = :dockind and t_kind_operation = :operation and t_department = :dep";
      sql = ExecSqlSelect(sql, MakeArray(SqlParam("dockind", FDID), SqlParam("operation", Operation), SqlParam("dep", Department)));
      if (sql.MoveNext() )
        AddErr("Уже существует запись с такими параметрами|Первичный документ: "+GetDocKindName()+"|Операция: "+GetOperationName()+"|Филиал: "+GetDepName());
      end;

      sql = "select 1 from RSHB_FD_GROUPS where t_dockind = :dockind and t_kind_operation = :operation and t_groupid = :grp";
      sql = ExecSqlSelect(sql, MakeArray(SqlParam("dockind", FDID), SqlParam("operation", Operation), SqlParam("grp", Group)));
      if (sql.MoveNext() )
        AddErr("Уже существует запись с такими параметрами|Первичный документ: "+GetDocKindName()+"|Операция: "+GetOperationName()+"|Группа: "+GetGroupName());
      end;

      if (not ExecSqlSelect("select 1 from doprkdoc_dbt where t_dockind = :dockind", MakeArray(SqlParam("dockind", FDID))).MoveNext )
        AddErr("В справочнике не найден первичный документ с номером " + FDID);
      end;

      if (Operation != 0) 
        if (not ExecSqlSelect("select 1 from doprkoper_dbt where t_kind_operation = :1 and t_dockind = :2", MakeArray(SqlParam("1", Operation), SqlParam("2", FDID))).MoveNext )
          AddErr("В справочнике не найдена операция с номером " + Operation + " и первичным документом № " + FDID);
        end;
      end;
    end;

    if (not ExecSqlSelect("select 1 from dacsgroup_dbt where t_groupid = :1", MakeArray(SqlParam("1", Group))).MoveNext )
      AddErr("В справочнике не найдена группа с номером " + Group);
    end;

    if ( (Operation==0) and (not ExecSqlSelect("select 1 from dregval_dbt where t_keyid = 2122 and instr(to_char(t_fmtblobdata_xxxx), :1) > 0", MakeArray(SqlParam("1", Boffice))).MoveNext ) )
      AddErr("В справочнике не найден Бэк-офис с кодом \"" + Boffice + "\"");
    end;

    return not ShowErr();
  End;

  //Добавление новой записи. с проверками
  private macro InsertNewRecord ()
    var sql;

    if (not CheckData())
      return false;
    end;

    ID = GetNextVal();
    sql = ExecSql("insert into RSHB_FD_GROUPS values (:recid, :dockind, :kind_oper, :grp, :dep, :bof)",
                  MakeArray(SqlParam("recid", ID), SqlParam("dockind", FDID), SqlParam("kind_oper", Operation), SqlParam("grp", Group), SqlParam("dep", Department), SqlParam("bof", Boffice)));

    if (sql == null) 
      ShowErr("Ошибка при добавлении записи");
      return false;
    end;

    return true;
  onerror()
    ShowErr("Ошибка при добавлении записи");
    return false;
  End;

  //Обновить группу
  private macro UpdateNewGroup ()
    var sql;
    if (not CheckData(true))
      return false;
    end;

    sql = ExecSql("update RSHB_FD_GROUPS set t_groupid = :grpid, t_boffice = :bof where t_id = :recid", MakeArray(SqlParam("grpid", Group), SqlParam("bof", Boffice), SqlParam("recid", ID)));
    if (sql == null) 
      ShowErr("Ошибка при редактировании записи");
      return false;
    end;

    return true;
  onerror()
    ShowErr("Ошибка при редактировании записи");
    return false;
  End;

  macro DeleteRecord (id_record)
    execSql("delete RSHB_FD_GROUPS where t_id = :recid", MakeArray(SqlParam("recid", id_record)));
    return true;
  onerror()
    ShowErr("Ошибка при удалении записи");
    return false;
  End;

  //Панель для добавления новой записи
  macro CreateNewRecord_pn ()
    var pn = FD_Groups_panel();
    pn.Run();

    if (pn.IsBreak)
      return false;
    end;

    FDID       = pn.DocKInd;
    Operation  = pn.Operation;
    Group      = pn.Group;
    Department = pn.Department;
    Boffice    = pn.Boffice;

    return InsertNewRecord();
  End;

  //Панель для редактирования
  macro EditRecord_pn (recid)
    if (not GetRecord(recid) )
      return false;
    end;

    var pn = FD_Groups_panel(FDID,
                             GetDocKindName(),
                             Operation,
                             GetOperationName(),
                             Group,
                             GetGroupName(),
                             Department,
                             GetDepName(),
                             Boffice,
                             GetBofficeName(),
                             true
                             );
    pn.Run();

    if (pn.IsBreak)
      return false;
    end;

    if ( (Group == pn.Group) and (Boffice == pn.Boffice))
      return true;
    end;

    Group   = pn.Group;
    Boffice = pn.Boffice;

    return UpdateNewGroup();
  End;

  macro WievOper (recid:integer)
    if (not GetRecord(recid) )
      return -1;
    end;

    return GetMainOperInGroup(FDID, Operation, Department);
  End;
END;


//-------------------------------------------------------------------------------------------------------------------------
macro StartScrollRole()

  private macro _AddCol(ar, ind, fld, head, width, fldType, decPoint)
    ar.value( ind * 6 + 0 ) = fld;
    ar.value( ind * 6 + 1 ) = head;
    ar.value( ind * 6 + 2 ) = width;
    ar.value( ind * 6 + 3 ) = fldType;
    ar.value( ind * 6 + 4 ) = decPoint;
    ar.value( ind * 6 + 5 ) = 0;   // reserv
  end;

  macro ScrollProcFD (sql, cmd, id, key)
    var grp = FD_Groups;
    if (DLG_INIT == cmd)
      message("~ESC~ выход  ~F8~ Удалить  ~F9~ Ввод  ~Enter~ Редактировать  ~ctrl+g~ Показать номер операциониста");
    elif(cmd == DLG_KEY)
      if (key == F9) //Добавление новой записи
        if (grp.CreateNewRecord_pn() )
          sql.AddNew();
          sql.value("t_id")      = money(grp.id);
          sql.value("dockind")   = grp.GetDocKindName();
          sql.value("operation") = grp.GetOperationName();
          sql.value("groupname") = grp.GetGroupName();
          sql.value("depname")   = grp.GetDepName();
          sql.value("boffice")   = grp.GetBofficeName();
          sql.update();

          GoToScroll(sql);
          updateScroll (sql, 5);
        end;
      elif (key == F8) //Удаление записи
        if (MsgBoxEx("Вы действительно хотите удалить запись?", MB_YES+MB_NO, IND_NO) == IND_YES)
          if (grp.DeleteRecord(sql.value("t_id")))
            sql.delete();
            updateScroll (sql, 5);
          end;
        end;
      elif (key == Enter)
        if (grp.EditRecord_pn(sql.value("t_id")) )
          sql.Edit();
          sql.value("groupname") = grp.GetGroupName();
          sql.value("boffice")   = grp.GetBofficeName();
          sql.Update();
          updateScroll (sql, 5);
        end;
        return CM_IGNORE;
      elif (key == ctrl_g)
        MsgBox(grp.WievOper(sql.value("t_id")));
      end; // if (key == ..)
    end;
  End;

  var col = TArray();
  var query = "select t_id, "
            + "       nvl((select t_name from doprkdoc_dbt where t_dockind = gr.t_dockind), '') dockind, "
            + "       nvl((select t_name from doprkoper_dbt where t_kind_operation = gr.t_kind_operation), '') operation, "
            + "       nvl((select t_name from dacsgroup_dbt where t_groupid = gr.t_groupid), '') groupname, "
            + "       nvl((select (select t_name from dparty_dbt where t_partyid = t.t_partyid) from ddp_dep_dbt t where t_code = gr.t_department), '') depname, "
            + "       (select t_name_type from dtypeac_dbt where t_inumtype = 22 and t_type_account = t_boffice) boffice "
            + "from RSHB_FD_GROUPS gr";
  var sql = ExecSqlSelect(query, null, false, RSDVAL_CLIENT, RSDVAL_STATIC);

  sql.UpdateCommand = RsdCommand("select 1 from dual");
  sql.DeleteCommand = RsdCommand("select 1 from dual");
  sql.insertCommand = RsdCommand("select 1 from dual");

  _AddCol (col, 0, "dockind",   "Первичный документ", 30, null, 0 );
  _AddCol (col, 1, "operation", "Вид операции",       30, null, 0 );
  _AddCol (col, 2, "groupname", "Группа (СУД)",       50, null, 0 );
  _AddCol (col, 3, "depname",   "Филиал",             40, null, 0 );
  _AddCol (col, 4, "boffice",   "Бэк-офис",           10, null, 0 );

  RunScroll (sql, col.size/6, col, "ScrollGroups", "ScrollProcFD", "Список групп", "ESC Выход  F4 Поиск", true, 0, 0, 750, 300);

end;