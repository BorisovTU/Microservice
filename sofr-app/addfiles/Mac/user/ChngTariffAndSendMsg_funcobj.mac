/**                                                                                                             
 @file  ChngTariffAndSendMsg_funcobj.mac                                                                                           
 @brief Обработчик задания funcobj 7778, реализующий смену тарифного плана по ДБО и субдоговорам, обновление значений связанных примечаний и отправку клиенту письма о смене ТП 

 #tag                                                                                                          
 - functional_block:СО                                                                             
 - code_type:Event_handler 
 - code_type:API                                                                                                                                                                                               
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.02.27 |Жиц М.В.       |DEF-62334           |Использовани в примечании 145 номера ТП вместо ID 
 |2024.02.22 |Жиц М.В.       |BOSS-2335           |Доработки макроса смены тарифного плана с Трейдера на Инвестор                                                                                                                                                 
 |2024.02.19 |Жиц М.В.       |DEF-61897           |Вывод информации об успешной смене ТП при ошибках в рассылке, замена текста уведомления
 |2024.02.16 |Жиц М.В.       |DEF-61897,DEF-61876 |Вывод номера договора в начале сообщения об ошибке
 |2024.02.15 |Жиц М.В.       |DEF-61768,DEF-61771,|Изменен текст письма, новое сообщение добавляется в список сообщений по ДБО системной датой,
 |           |               |DEF-61772           |убрана провека на действие тарифа трейдер в предыдущий день до смены тарифа                                                                                                                       
 |2024.02.14 |Жиц М.В.       |BOSS-2110           |Создание макроса обработки задания funcobj                                                                                                                                                 
*/ 

import sp_car, "FuncObj.mac", "SendNotifITP.mac", "SetPersonRequestMsg.mac";

PRIVATE CONST DLCONTR_MESSAGE_CHNG_TARIFF = 600; //Вид сообщения "Уведомление о смене Тарифного плана клиенту банком"

/**
  @brief Класс параметров задания funcobj 7778
  @param[in] StrParam строка параметров задания funcobj
*/                    
PRIVATE CLASS TParamProcessSlowEvent(StrParam:string)
  var NewTarifID:integer = 0,  //Идентификатор нового тарифного плана
      OldTarifID:integer = 0,  //Идентификатор старого тарифного плана
      BegDate:date = ZeroDate; //Дата начала действия (смены) тарифного плана

  private var StrSeparator:string = ";"; //Символ, разделяющий параметры в строке

  /**
    @brief Инициализация параметров путем парсинга строки
    @param[in] StrParam строка параметров задания funcobj
  */                    
  private macro InitParam(StrParam:string)
    var StrDigit:string = "0123456789",
        StrParamBuf:string = "",
        i:integer = 0,
        StrPos:integer = 0;

    if((Valtype(StrParam) != V_UNDEF) and (Valtype(StrParam) != 26) and (StrParam != ""))
      
      i = 1;
      while(i <= StrLen(StrParam)) //нормализация оставить, что только цифры или разделитель
        if(Index((StrDigit + StrSeparator + "."), Substr(StrParam, i, 1)) >= 1)
          StrParamBuf = StrParamBuf + Substr(StrParam, i, 1);
        end;
        i = i + 1;
      end;
       
      i = 0;
      while((StrLen(StrParamBuf) > 0) and (GenNumProps(this) > i)) //заполняем свойства
        StrPos = Index(StrParamBuf, StrSeparator);
        if(StrPos > 1)
          if(i == 2)
            this[i] = date(Substr(StrParamBuf, 1, StrPos - 1));
          else
            this[i] = int(Substr(StrParamBuf, 1, StrPos - 1));
          end;
          StrParamBuf = Substr(StrParamBuf, StrPos + 1);
        elif((StrPos == 0) and (StrLen(StrParamBuf) > 0))
          this[i] = int(StrParamBuf);
          StrParamBuf = "";
        end;
        i = i + 1;
      end;
    end;

  OnError(erru)
    NewTarifID = 0;
    OldTarifID = 0;
    BegDate = ZeroDate;
  end;

  InitParam(StrParam);
end;

/**
  @brief Смена тарифного плана на ДБО и субдоговорах 
  @param[in]     DlContrID     идентификатор ДБО
  @param[in]     SfContrIDRoot идентификатор родительской записи SfContr
  @param[in]     ContrNumber   номер договора
  @param[in]     NewTarifID    идентификатор нового ТП
  @param[in]     BegDate       дата начала действия нового ТП
  @param[in/out] ErrMsg        сообщение об ошибке
  @return статус обработки: 0 - обработано без ошибок, 1 - обработано с ошибками 
*/
PRIVATE MACRO ChngTariff(DlContrID:integer, SfContrIDRoot:integer, ContrNumber:string, NewTarifID:integer, BegDate:date, ErrMsg:@string):integer
  var cmd, ds;
  var stat:integer = 0;

  stat = SfContrChangeSfPlan(SfContrIDRoot, NewTarifID, BegDate, false);
  if(stat != 0)
    ErrMsg = "Ошибка установки нового ТП для ДБО " + ContrNumber;
    return 1;
  else
    cmd = DL_RSDCommand("SELECT sf.t_ID, sf.t_Number " + 
                        "  FROM ddlcontrmp_dbt mp, dsfcontr_dbt sf " +
                        " WHERE sf.t_ID = mp.t_SfContrID " +
                        "   AND sf.t_DateClose = TO_DATE('01.01.0001', 'dd.mm.yyyy') " +      
                        "   AND mp.t_DlContrID = ? " 
                       );
    cmd.addParam(DlContrID);
    ds = cmd.Execute();

    while(ds.MoveNext())
      stat = SfContrChangeSfPlan(ds.ID, NewTarifID, BegDate, false);
      if(stat != 0)
        ErrMsg = "Ошибка установки нового ТП для субдоговора " + ds.Number;
        return 1;
      end;
    end;
  end;
  
  return 0;

OnError(erru)  
  ErrMsg = "onError: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
  return 1;
END;

/**
  @brief Получение наименования тарифного плана по идентификатору 
  @param[in] SfPlanID идентификатор ТП
  @return наименование ТП 
*/
PRIVATE MACRO GetSfPlanNameByID(SfPlanID:integer):string
  var cmd, ds;

  cmd = DL_RSDCommand("SELECT t_Name " +
                      "  FROM dsfplan_dbt " + 
                      " WHERE t_SfPlanID = ? "
                     ); 
  cmd.addParam(SfPlanID);
  ds = cmd.Execute();

  if(ds.MoveNext())
    return ds.Name;
  end;

  return "";
END;

/**
  @brief Получение номера тарифного плана по идентификатору 
  @param[in] SfPlanID идентификатор ТП
  @return номер ТП 
*/
PRIVATE MACRO GetSfPlanNumByID(SfPlanID:integer):integer
  var cmd, ds;

  cmd = DL_RSDCommand("SELECT t_Num " +
                      "  FROM dsfplan_dbt " + 
                      " WHERE t_SfPlanID = ? "
                     ); 
  cmd.addParam(SfPlanID);
  ds = cmd.Execute();

  if(ds.MoveNext())
    return ds.Num;
  end;

  return 0;
END;

/**
  @brief Установка новых значенй для примечаний на ДБО, связанных со значением тарифного плана 
  @param[in]     ObjectType    вид объекта ДБО
  @param[in]     DlContrID     идетификатор ДБО
  @param[in]     NewTarifID    идентификатор нового ТП
  @param[in]     BegDate       дата начала действия нового ТП
  @param[in/out] ErrMsg        сообщение об ошибке
  @return статус обработки: 0 - обработано без ошибок, 1 - обработано с ошибками 
*/
PRIVATE MACRO AddNotes(ObjectType:integer, DlContrID:integer, NewTarifID:integer, BegDate:date, ErrMsg:@string):integer
  
  //Для изменения значений примечаний используем специальную ф-цию из SetPerson, a не addNoteForObject, т.к. она не умеет устанавливать примечания в будущую дату
  CreateTarifNote(ObjectType, 
                  L_Z(DlContrID, 34),
                  CONTRACT_NOTEKIND_TARIFFID_REPO, 
                  string(1000 + GetSfPlanNumByID(NewTarifID)), 
                  BegDate); 

  CreateTarifNote(ObjectType, 
                  L_Z(DlContrID, 34),
                  CONTRACT_NOTEKIND_TARIFFPLAN_REPO, 
                  GetSfPlanNameByID(NewTarifID), 
                  BegDate); 

  CreateTarifNote(ObjectType, 
                  L_Z(DlContrID, 34),
                  CONTRACT_NOTEKIND_BINDDATE_REPO, 
                  BegDate, 
                  BegDate); 

  return 0;

OnError(erru)  
  ErrMsg = c_err_obj.obtain_err_msg(erru);
  return 1;
END;

/**
  @brief Отправка клиенту письма о смене тарифного плана 
  @param[in]     DlContrID     идетификатор ДБО
  @param[in]     NewTarifID    идентификатор нового ТП
  @param[in]     OldTarifID    идентификатор старого ТП
  @param[in]     BegDate       дата начала действия нового ТП
  @param[in/out] ErrMsg        сообщение об ошибке
  @return статус обработки: 0 - обработано без ошибок, -1 - обработано с ошибками отправки 
*/
PRIVATE MACRO SendMsg(DlContrID:integer, NewTarifID:integer, OldTarifID:integer, BeginDate:date, ErrMsg:@string):integer
  var cmd, ds, query, cmd1, ds1;
  var ClntEmail:string = ""; //Почта клиента
  var Subject:string = ""; //Заголовок письма
  var EmailText:string = ""; //Текст письма
  var v_email_processor = c_email_proc_env(); //Обработчик рассылки
  var ErrBase:string = "Тариф успешно сменен, однако возникли ошибки при отправке письма клиенту: ";

  //Получим данные по клиенту
  cmd = DL_RSDCommand("SELECT sf.t_PartyID, sf.t_Number, pt.t_Name as t_FIO, persn.t_IsMale " + 
                      "  FROM dsfcontr_dbt sf, ddlcontr_dbt dl, dparty_dbt pt, dpersn_dbt persn " +
                      " WHERE sf.t_ID = dl.t_SfContrID " +      
                      "   AND dl.t_DlContrID = ? " +
                      "   AND pt.t_PartyID = sf.t_PartyID " +
                      "   AND persn.t_PersonID = sf.t_PartyID "
                     );
  cmd.addParam(DlContrID);
  ds = cmd.Execute();

  if(ds.MoveNext())
    //Проверим, что еще не создавали сообщение на договоре в эту дату, чтобы не плодить их в случае повторного запуска задания 
    cmd1 = DL_RSDCommand("SELECT 1 " +
                         "  FROM ddlcontrmsg_dbt " +
                         " WHERE t_DlContrID = ? " +
                         "   AND t_Kind = ? " +
                         "   AND t_CreateDate = ? "
                        );
    cmd1.AddParam(DlContrID);
    cmd1.AddParam(DLCONTR_MESSAGE_CHNG_TARIFF);
    cmd1.AddParam(Date());
    ds1 = cmd1.Execute();

    if(not ds1.MoveNext())
      //Создадим сообщение на договоре
      query = RSDCommand("INSERT INTO ddlcontrmsg_dbt (t_DlContrID, t_Kind, t_CreateDate, t_CreateTime, t_MarketID) " +
                         "                    VALUES (?, ?, ?, ?, ?) "
                        );
      query.AddParam("", RSDBP_IN, DlContrID);
      query.AddParam("", RSDBP_IN, DLCONTR_MESSAGE_CHNG_TARIFF);
      query.AddParam("", RSDBP_IN, Date());
      query.AddParam("", RSDBP_IN, Time());
      query.AddParam("", RSDBP_IN, -1);
      query.Execute();
    end;

    ClntEmail = USER_GetClientRepEmail(DlContrID, ds.PartyID); //Получение почты клиента: сначала пытаемся найти ее на ДБО, а если там нет, то на клиенте
    if(ClntEmail == "")
      ErrMsg = ErrBase + "Не удалось опеределить Email клиента " + ds.FIO;
      return -1;
    else
      Subject = "Уведомление о смене тарифа! " + ds.Number + "_" + ds.FIO;

      EmailText = IIF(ds.IsMale == SET_CHAR, "Уважаемый", "Уважаемая") + " " + ds.FIO + "!\n"
                + "\n"
                + "В соответствии с п.5.4 Регламента оказания брокерских услуг и услуг по инвестиционному консультированию АО \x22Россельхозбанк\x22 15-Р уведомляем, " 
                + "что в связи с наличием у Вас на брокерском счете (Соглашение " + ds.Number + ") просроченной задолженности сроком свыше 60 календарных дней, "  
                + "Ваш тарифный план \x22" + GetSfPlanNameByID(OldTarifID) + "\x22 был изменен на тарифный план \x22" + GetSfPlanNameByID(NewTarifID) + "\x22 c " + string(BeginDate:f) + ".\n"
                + "\n"
                + "С уважением, АО \x22Россельхозбанк\x22";
        
      v_email_processor.m_set_msg_head(Subject);
      v_email_processor.m_add_email_to_list(ClntEmail);
      v_email_processor.m_add_row_to_msg_text(EmailText);
      v_email_processor.m_save_to_submit_synch();
      v_email_processor.m_submit_email_synch();

      if(v_email_processor.m_get_error_status())
        ErrMsg = "Ошибка при отправке письма клиенту: " + v_email_processor.get_service_msg();
        return 1;
      else
        //Установим время отправки в сообщении на договоре, если успешно отправили
        query = RSDCommand("UPDATE ddlcontrmsg_dbt " +
                           "   SET t_SendDate = ?, " +
                           "       t_SendTime = ? " +
                           " WHERE t_DlContrID = ? " +
                           "   AND t_Kind = ? " +
                           "   AND t_CreateDate = ? "
                          );
        query.AddParam("", RSDBP_IN, Date());
        query.AddParam("", RSDBP_IN, Time());
        query.AddParam("", RSDBP_IN, DlContrID);
        query.AddParam("", RSDBP_IN, DLCONTR_MESSAGE_CHNG_TARIFF);
        query.AddParam("", RSDBP_IN, Date());
        query.Execute();
      end;
    end;
  end; 

  return 0; 

OnError(erru) 
  if(IsEqClass("TRsComErr", erru.err))
    ErrMsg = ErrBase + erru.err.Message(1);
  elif(erru.AxCode)
    ErrMsg = ErrBase + erru.AxMes;
  end;
  
  if(ErrMsg == "") 
    ErrMsg = ErrBase + "onError: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
  end;
  return -1;                                                            
END;

/**
  @brief Обработчик задания funcobj 7778, реализующий смену тарифного плана по ДБО и субдоговорам, обновление значений связанных примечаний и отправку клиенту письма о смене ТП 
  @param[in]     ObjectType  вид объекта (ДБО)
  @param[in]     ObjectID    идетификатор объекта (ДБО)
  @param[in]     ParamStr    строка параметров задания funcobj
  @return объект задания funcobj 
*/
MACRO ChngTariffAndSendMsg(ObjectType, ObjectID, ParamStr):FuncObjResult
  var cmd, ds;
  var err:integer = 0; //Идентификатор ошибки
  var errMsg:string = ""; //Описание ошибки
  var ReturnObj = FuncObjResult(FUNCOBJ_RESULT_OK, "", 0); //Объект задания funcobj
  var ParamProcess:object /*TParamProcessEntriesVerify*/; //Объект параметров задания funcobj
  
  if(ParamStr == "")
    ReturnObj.state = FUNCOBJ_RESULT_ERROR;
    ReturnObj.errorText = "Переданы неверные параметры";
    ReturnObj.errorCode = 0;
  else
    ParamProcess = TParamProcessSlowEvent(ParamStr + ";"); //Заполнение параметров в результате парсинга строки с ними
    
    //Проверим, что ТП на ДБО соответствует тому, с которого меняем: такая проверка осуществляется и перед вставкой задания, 
    //но при наличии ошибок при отправке письма (некритично и не включено в транзакцию) задание перезупустится, а ТП уже будет изменен, поэтому повторно этот блок выполнять не нужно
    cmd = DL_RSDCommand("SELECT sf.t_ID, sf.t_Number " + 
                        "  FROM dsfcontr_dbt sf, ddlcontr_dbt dl " +
                        " WHERE sf.t_ID = dl.t_SfContrID " +      
                        "   AND dl.t_DlContrID = ? " +
                        "   AND EXISTS (SELECT 1 " +
                        "                 FROM dsfcontrplan_dbt sfplan " +
                        "                WHERE sfplan.t_sfplanid = ? " +
                        "                  AND sfplan.t_sfcontrid = sf.t_id " +                                         
                        "                  AND sfplan.t_begin <= ? " +                                                 
                        "                  AND sfplan.t_end = TO_DATE('01010001', 'ddmmyyyy') " +
                        "              ) "  
                       );
    cmd.addParam(ObjectID);
    cmd.addParam(ParamProcess.OldTarifID);
    cmd.addParam(ParamProcess.BegDate);
    ds = cmd.Execute();

    if(ds.MoveNext())
      rslDefCon.BeginTrans(); //Открытие транзакции для смены ТП и обновления примечаний

      err = ChngTariff(ObjectID, ds.ID, ds.Number, ParamProcess.NewTarifID, ParamProcess.BegDate, @errMsg);
      if(err == 0)
        err = AddNotes(ObjectType, ObjectID, ParamProcess.NewTarifID, ParamProcess.BegDate, @errMsg);
      end;   

      if(err != 0)
        rslDefCon.RollbackTrans();
      else
        RslDefCon.CommitTrans();
      end;
    end;

    if(err == 0)
      err = SendMsg(ObjectID, ParamProcess.NewTarifID, ParamProcess.OldTarifID, ParamProcess.BegDate, @errMsg);
    end;
  
    if(err != 0)
      ReturnObj.state = FUNCOBJ_RESULT_ERROR;
      ReturnObj.errorText = errMsg;
      ReturnObj.errorCode = err;
    end;
  end;
  
  return ReturnObj;
END;

//ChngTariffAndSendMsg(207, 8790, "18;17;13.02.2024"); //Тестирование работы задания без funcobj
