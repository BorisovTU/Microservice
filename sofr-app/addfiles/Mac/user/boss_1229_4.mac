/**
 @file      boss_1229_4.mac
 @brief     "BOSS-2706. BOSS-2341 BOSS-1229.4 Неравенство между количеством исходных и целевых лотов

  В скрипте участвуют ЦБ (точность при конвертации на 22.08.2024)
    2010015090094  8
    US04537Y2081   6
    1-02-06556-А   6
    US83422E2046   6
    US88688T1007   6
    US21077C3051   6
    US54405Q2093   6
    US45773H4092   9
    US10802T2042   6
    US37960A6698   6
    US04634X2027   6
    US9344231041   9
    1-02-06556-А   на момент разработки не нашел в справочнике ЦБ, хотя в ТЗ указан 

  Перед запуском макроса переименовать EXCEL-файл "Расхождения по КД между ВУ и БУ_18.07.24.xlsx" в "diffBOSS1229_18_07_24.xlsx" и поместить в папку Import на сервере приложения
  Перед выполеннием второго пункта меню программы перезайти в СОФР
  Запускать через ctrl-F5 из отладчика

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |29.05.2025 |Велигжанин А.В.|DEF-82174                                       |Исправление, была ошибка компиляции в boss1229readExcelRun()
 |28.05.2025 |Велигжанин А.В.|DEF-82174                                       |Доработка getPKOparams(), поиск бумаги не только по коду, но и по LSIN
 |26.05.2025 |Велигжанин А.В.|DEF-82174                                       |Доработка для Excel, загрузка файла с терминала, 
 |           |               |                                                |настройка 'РСХБ\\ДИРЕКТОРИИ\\ИМПОРТ_1229'
 |25.04.2025 |Велигжанин А.В.|DEF-82174                                       |Доработка логгирования ProcessWrtOperation()
 |25.04.2025 |Велигжанин А.В.|DEF-82174                                       |Доработка для Excel, загрузка файла с сервера приложения
 |18.04.2025 |Велигжанин А.В.|DEF-82174                                       |Доработка для Excel
 |12.03.2025 |Велигжанин А.В.|DEF-82174                                       |Использование CExcelPoi вместо ActiveX

*/
import globals, rsd, activex, "cb_sql.mac", oralib, commonutil,secinter,cblogger2;
import "xls_parser.mac", "FileManager.mac"; // DEF-82174 CExcelPoi

/**
 @brief Получение кода ЕКК по договору
 @param[in] dogid идентификатор субдоговора
 @return код ЕКК
*/
private macro GetEKKDBO(dogid:integer):string 
  var sql, cmd, rs;
  sql = "select code.* from ddlcontrmp_dbt mp, ddlobjcode_dbt code where mp.t_sfcontrid = ? and code.t_objecttype = 207 and code.t_objectid = mp.t_dlcontrid and code.t_codekind = 1";
  cmd = RSDCommand(sql);
  cmd.AddParam("dogid", RSDBP_IN, dogid);
  rs = RSDRecordSet(cmd);
  if (rs.MoveNext())
     return rs.value("t_code");
  else
     return "";
  end;
end;

/**
  @brief getWriteOffNumkey - возвращает значение последовательности
  @param[out] - строка - номер последовательности
*/
private macro getWriteOffNumkey()
  CaptureOutput();
  [
  declare
    result varchar2(5);
    function getWriteOffNumkey(dealDate in date) return varchar2 is
      yyyy   number(4) := 0;
      result number(10) := 0;
      numpad integer := 5;
    begin
      select extract(year from dealDate) into yyyy from dual;
      execute immediate 'select writeoff_' || yyyy || '_seq.nextval n from dual'
        into result;
      return lpad(to_char(result), numpad, '0');
    exception
      when others then
        select extract(year from dealDate) into yyyy from dual;
        execute immediate 'CREATE SEQUENCE writeoff_' || to_char(yyyy) || '_seq START WITH 1 ' ||
                          ' MAXVALUE 9999999999999999999999999999  NOCYCLE NOCACHE';
        execute immediate 'select writeoff_' || yyyy || '_seq.nextval n from dual'
          into result;
        return lpad(to_char(result), numpad, '0');
    end;
  begin
    result := getWriteOffNumkey(:1);
    :2 := result; 
  end;
  ];

  var params = MakeArray(SQLParam(":1",{curdate}), SQLParam(":2",V_STRING,RSDBP_OUT));
  ExecSQL(StopCaptureOutput(),params);
  return Params.Value(1).value;
end;

// остаток на счете ВУ
private macro getVUamount(acc)
  CaptureOutput();
  [
  SELECT dr.t_rest FROM DRESTDATE_DBT dr 
  WHERE dr.T_ACCOUNTID = (SELECT da.T_ACCOUNTID FROM DACCOUNT_DBT da WHERE T_ACCOUNT = :1)
  ORDER by dr.t_restdate DESC
  FETCH FIRST 1 ROWS ONLY
  ];
  var rs = execSQLselectPrmDyn(StopCaptureOutput(),acc);
  if (rs.movenext)
    return rs.value("t_rest");
  end;
  return 0;
end;

// остаток БУ 
private macro getBUamount(contract,ficode)
  CaptureOutput();
  [
  SELECT DP.T_AMOUNT FROM DPMWRTCL_DBT dp
  WHERE dp.T_CONTRACT = 
    (SELECT dsc.T_ID FROM DSFCONTR_DBT dsc WHERE dsc.T_NUMBER = :1 FETCH FIRST 1 ROWS ONLY)
  AND dp.T_FIID = (SELECT df.T_FIID FROM DFININSTR_DBT df WHERE df.T_FI_CODE = :2) 
  AND dp.t_enddate = to_date('31129999','ddmmyyyy')
  ];
  var rs = execSQLselectPrmDyn(StopCaptureOutput(),contract,ficode);
  if (rs.movenext)
    return rs.value("t_amount");
  end;
  return 0;
end;

//предотвратить появление в конце кода ".0000" для случая, если это обычное число
private macro cutZeros(cell_value):string
  var code: string = cell_value;
  if (index(code,".")>0)
    code = substr(code,1,index(code,".")-1);
  else
    code = cell_value;  
  end;
  return code; 
end;

/*Создание и запуск СОБУ*/
private macro WRT_AccountingExecOp_Schedul(commdate, opernum, num, clientid, contractID, fiid, flag1, flag4, flag6, documentid:@integer)
  var cmdD, rsD;
  var comm = TRecHandler("dl_comm.dbt"), stat = 0, nextnumb = 0;
  documentid = 0;
  GenerateReference(230, nextnumb);
  comm.rec.dockind = DL_SCACCOUNTING/*4615*/;
  comm.rec.oper = opernum;
  comm.rec.division = 1;
  comm.rec.commstatus = 0;
  comm.rec.PartyId = -1;
  comm.rec.issuerID = -1;
  comm.rec.FIID = fiid;//Выпуск ц/б
  comm.rec.commcode = nextnumb + num;
  comm.rec.commdate = commdate;//Дата операции
  comm.rec.contractID = contractID;
  comm.rec.clientid = clientid;
  comm.rec.avoirkind = 0;//Вид ц/б
  comm.rec.currency = -1;// Валюта номинала
  if(flag1)
    comm.rec.flag1 = "X";//искл.
  else
    comm.rec.flag1 = " ";//искл.
  end;
  comm.rec.flag2 = " ";//Неттинг
  comm.rec.flag3 = " ";//Клиентские комиссии за обороты
  if(flag4)
    comm.rec.flag4 = "X";//Рассчеты на бирже
  else
    comm.rec.flag4 = " ";//Рассчеты на бирже
  end;

  if(flag6)
    comm.rec.flag6 = "X";
  else
    comm.rec.flag6 = " ";
  end;
  comm.rec.createreport = "X";
  comm.rec.FlagSecur = "X";
  stat = SC_CreateDlCommOperation(comm);
  if(stat == 0)
     documentid = Int(comm.rec.DocumentID);
     stat = SC_ServOperExecuteSilent(DL_SCACCOUNTING, Int(comm.rec.DocumentID));
  end;
  if(stat == 0)
    CaptureOutput();
    [ 
      select dl_comm.t_commstatus
      from ddl_comm_dbt dl_comm
      where dl_comm.t_documentid = ? and
            dl_comm.t_commstatus = 2
    ];
    cmdD = RSDCommand(StopCaptureOutput());
    cmdD.AddParam("documentid", RSDBP_IN, Int(comm.rec.DocumentID));
    cmdD.Execute();
    rsD = TRsbDataSet(cmdD);
    if(not rsD.movenext)
      return 1;
    end;
  end;
  return stat;
end;


private class boss1229readExcelClass(p_fullNameFile)

  var InputDir, FullExcelFile = getCurDir(true) + p_fullNameFile, FileName, ExtName, DirName;
  var FullNameFile="$" + FullExcelFile;
  var errmsg = "";
  var n_success = 0; // количество успешных закрытых операций 
  var n_failed = 0;  // количество не закрытых операций 
  var n_not_created = 0;  // количество не созданных операций 
  var lobj = NewLogger(c_logger.FILE_TYPE, "", c_logger.DateTimeName+"boss1229readExcel"); 
  lobj.info("Логер создан"); 

  // объект записи из Экселя
  private class recordOfExcel(objExcel, row, fcol)
    var contract_number = SQL_ConvTypeStr(objExcel.CellValueByRowAndCol(row, 3));  
    var fi_code = SQL_ConvTypeStr(cutZeros(objExcel.CellValueByRowAndCol(row, 8))); 
    var VUamount:money = strsubst(string(objExcel.CellValueByRowAndCol(row, 9)),",","."); //в экселе запятая вместо точки
    var BUamount:money = strsubst(string(objExcel.CellValueByRowAndCol(row, 11)),",","."); 
    var VUamountFromDB = getVUamount(cutZeros(objExcel.CellValueByRowAndCol(row, 1))); 
    var BUamountFromDB = getBUamount(contract_number,fi_code); 
    var idcontract,clientid,securityid,dealcode,dealid,
        stat/*статус операции списания/зачисления*/,
        id/*id лога*/;
  end;

  // выбрать интерактивно файл
  macro selectExcel()
    /*Выбираем файл для обработки*/
    if(not SelectFile(FullExcelFile, FullExcelFile, "Выберите файл для загрузки", 0, true))
      errmsg = "Отказ от загрузки";
      return false;
    end;
    FullNameFile="$" + FullExcelFile;
    return true;
  end;

  // записать первый раз в лог 
  macro writeRecordInTable(contract,sec,dealid,stat)
    var query = "insert into LOSECALIGNMENT_TMP_LOG (t_contract, t_sec, t_dealid, t_stat) values (?,?,?,?) returning t_id into ? ";     
    var sql = RSDCommand(query);		                                                                                          
          sql.addParam("", RSDBP_IN, contract);  // 3 столбец в Excel                                                                                            
          sql.addParam("", RSDBP_IN, sec);       // 8 столбец в Excel                                                                                  
          sql.addParam("", RSDBP_IN, dealid);    //                                                                                       
          sql.addParam("", RSDBP_IN, stat);      //                                                                                     
          sql.addParam("t_id", RSDBP_OUT, V_INTEGER);
    sql.execute;
    return sql.value("t_id");
  end;

  // напечатать предупреждения
  macro PrintWarning(rec)
      if (abs(rec.VUamount)!=abs(rec.VUamountFromDB))
        lobj.info("Предупреждение: остатки ВУ в файле Excel и в СОФР отличаются. Контракт "+rec.contract_number+", ЦБ "+rec.fi_code);
        lobj.info("Excel - "+abs(rec.VUamount));
        lobj.info("СОФР - "+abs(rec.VUamountFromDB));
      end;
      if (abs(rec.BUamount)!=abs(rec.BUamountFromDB))
        lobj.info("Предупреждение: остатки БУ в файле Excel и в СОФР отличаются. Контракт "+rec.contract_number+", ЦБ "+rec.fi_code);
        lobj.info("Excel - "+rec.BUamount);
        lobj.info("СОФР - "+abs(rec.BUamountFromDB));
      end;
  end;

  /**
    @brief getPKOparams - дополняет структуру типа recordOfExcel данными для создания операции
    @param[in] rec - структура recordOfExcel, заполняется по ссылке, поэтому нет выходного параметра
  */
  macro getPKOparams(rec)
    var stat = 0;
    CaptureOutput();
    [
    select 
      sf.t_id idcontract, sf.t_partyid clientid
      , coalesce (f.t_fiid, a.t_fiid, a1.t_fiid) AS securityid
    from (select :1 AS fi_code, :2 AS contract_number from dual) p
    inner join dsfcontr_dbt sf on (sf.t_number = p.contract_number)
    left join dfininstr_dbt f on (f.t_fi_code = p.fi_code)
    left join davoiriss_dbt a on (a.t_lsin = p.fi_code)
    left join davoiriss_dbt a1 on (a1.t_isin = p.fi_code)
    fetch first 1 rows only
    ];
    var rs = execSQLselectPrmDyn(StopCaptureOutput(),rec.fi_code,rec.contract_number);
    if (rs.movenext)
      rec.idcontract = rs.value("idcontract");
      rec.clientid = rs.value("clientid");
      rec.securityid = rs.value("securityid");
      rec.dealcode = GetEKKDBO(rec.idcontract)+"/"+getWriteOffNumkey()+"/ДЕПО";
    else
      stat = 1;
    end;
    return stat; 
  end; 

  macro kill_VU_Schedule(dealcode)
    CaptureOutput();
    [
    UPDATE ddlgracc_dbt
       SET t_state = 2
    WHERE t_accnum = 3 AND t_state = 1
       AND t_grdealid =
              (SELECT t_id
                 FROM ddlgrdeal_dbt
               WHERE t_dockind = 127 AND t_templnum = 17
                  AND t_docid =
                       (SELECT t_dealid
                          FROM ddl_tick_dbt
                          WHERE t_dealcode = :dealcode
                            AND t_bofficekind = 127));
    ];
    execSql(StopCaptureOutput(),makearray(sqlparam(":dealcode",dealcode)));
  end;

  /**
    @brief UpdateLog - запись в лог результата работы программы
  */
  macro UpdateLog(rec)
    if (rec.dealid==null)
      lobj.info(string("Операция не создалась: договор ",rec.contract_number,", ЦБ ",rec.fi_code));
      n_not_created = n_not_created + 1;
      return;
    end;
    var sql = "select d.t_dealstatus from ddl_tick_dbt d where d.t_dealid = :1";
    var rs = execSQLselectPrmDyn(sql,rec.dealid);
    if (rs.movenext)
      if (rs.value("t_dealstatus")==20)
        rec.stat = 2;
        n_success = n_success + 1;  
      else
        rec.stat = 1;
        n_failed = n_failed + 1;  
      end;
    end;
    sql = "update LOSECALIGNMENT_TMP_LOG set t_stat = :1, t_dealid = :2 where t_id = :3";
    rs = ExecSQL(sql,makeArray(sqlparam(":1",rec.stat),sqlparam(":2",rec.dealid),sqlparam(":3",rec.id)));
    RemoveNoteForObject(101,string(rec.dealid:34:o),404);    
  end;

  
  /** 
     @brief  ProcessWrtOperation
     создание операции, 
     установка примечания 404, 
     продвижение операции до статуса "Открытая", 
     закрытие графика ВУ  
     @param rec - объект приватного класса recordOfExcel
  */
  macro ProcessWrtOperation(rec)
    var BoardID = 0, DealIdSOBU, errMsg = "", OperDate = {curdate}; 
    var stat, stat1, stat2=-1, stat3=-1, stat4=-1;
    //если разницы в БУ и ВУ нет, то выходим
    if (abs(rec.VUamountFromDB)==abs(rec.BUamountFromDB))
      lobj.error(string("Остатки равны, операцию не будем создавать: договор ",rec.contract_number,", ЦБ ",rec.fi_code));
      return;
    else 
      lobj.info(string("Начало процедуры создания операции: договор ",rec.contract_number,", ЦБ ",rec.fi_code));
    end;
    stat = getPKOparams(rec); //дополнить структуру параметрами создания операции
    if (stat != 0)
      lobj.error(string("Ошибка при нахождении параметров создания операции, операцию не будем создавать: договор ",rec.contract_number,", ЦБ ",rec.fi_code));
      return; 
    end;
    record BoardPrm("BoardPrm.rec");
    BoardPrm.Point = 13;
    BoardPrm.Contragent = -1; 
    BoardPrm.Department = 1; 
    BoardPrm.Client = SQL_ConvTypeInteger(rec.clientid); // id клиента
    BoardPrm.ClientContr = SQL_ConvTypeInteger(rec.idcontract); // id договора 
    BoardPrm.DeliveringFIID = BoardPrm.FIID = SQL_ConvTypeInteger(rec.securityID); //id ЦБ 
    BoardPrm.Oper = {oper}; 
    BoardPrm.DealCode = SQL_ConvTypeStr(rec.dealcode);
    BoardPrm.Custody = {ourbank};
    BoardPrm.DepoClientCustody = {ourbank};
    BoardPrm.UseInTax = "X";
    if (abs(rec.VUamountFromDB)>abs(rec.BUamountFromDB))
      BoardPrm.DealType = 2011; //зачисление 
      BoardPrm.Amount = SQL_ConvTypeSum(abs(rec.VUamountFromDB)-abs(rec.BUamountFromDB)); //количество ЦБ
    else
      BoardPrm.DealType = 2010; //списание
      BoardPrm.Amount = SQL_ConvTypeSum(abs(rec.BUamountFromDB)-abs(rec.VUamountFromDB)); //количество ЦБ
    end;
    BoardPrm.DealTime = Time;
    BoardPrm.DealDate = OperDate;
    //создание операции
    stat1 = SP_InsertBoardLotNoIFace( BoardPrm, BoardID, errMsg); //возвращает 0/1
    //установка примечания обеспечивает, чтобы СОБУ и СОВУ не создались на последнем шаге. В операции закроются все шаги, но статус операции "Открытая", т.к. графики созданы
    if (stat1==0)
      rec.dealId = BoardId;
      lobj.info(string("Создана операция с id=",rec.dealId,": договор ",rec.contract_number,", ЦБ ",rec.fi_code));
      stat2 = AddNoteForObject(101, string(Boardid:34:o), 404, "Операция выравнивания" );  
    else
      lobj.error(string("Ошибка при создании операции: договор ",rec.contract_number,", ЦБ ",rec.fi_code, ", err: ", errMsg));
      return;
    end;
    if (stat2==0)
      lobj.info(string("Создано примечание 404 для операции с id=",rec.dealId,": договор ",rec.contract_number,", ЦБ ",rec.fi_code));
      stat3 = SP_ExecDeal(Boardid, false, OperDate);  // может можно передавать будущую дату сюда??? В ТЗ говорится, что дата операции равна ОперДень+1
    else
      lobj.error(string("Ошибка при создании примечания 404 для операции: договор ",rec.contract_number,", ЦБ ",rec.fi_code));
      return;
    end;
    if (stat3==0)
      lobj.info(string("Успешно запущена операция с id=",rec.dealId,": договор ",rec.contract_number,", ЦБ ",rec.fi_code));
      // принудительное закрытие графика ВУ операции
      kill_VU_Schedule(rec.dealcode);       
      lobj.info(string("Закрыт график ВУ для операции с id=",rec.dealId,": договор ",rec.contract_number,", ЦБ ",rec.fi_code));
      // создание и запуск СОБУ
      stat4 = WRT_AccountingExecOp_Schedul(OperDate, {oper}, "", rec.clientid, rec.idcontract, rec.securityID, false, false, true, @DealIdSOBU);
    else
      lobj.error(string("Ошибка при исполнении операции: договор ",rec.contract_number,", ЦБ ",rec.fi_code));
      return;
    end;
    if (stat4==0)
      lobj.info(string("Успешно исполнено СОБУ для операции с id=",rec.dealId,": договор ",rec.contract_number,", ЦБ ",rec.fi_code));
    else
      lobj.error(string("Ошибка при исполнении СОБУ (", DealIdSOBU, ") по операции с id=", rec.dealId,": договор ", rec.contract_number,", ЦБ ",rec.fi_code));
      return;
    end; 
  end;

  macro truncateTemproraryTable()
    RSDCommand("truncate table LOSECALIGNMENT_TMP_LOG").execute;
  end;

  macro CopyToAppTxtFile(p_WorkFileName)
    var FileName, ExtName;
    var appServPath;

    //Отделяем путь файла от имени
    SplitFile(p_WorkFileName, FileName, ExtName);
    appServPath = MergeFile (getCurDir(false) + "\\..\\txtfile\\", FileName, ExtName);
    if(ExistFile(appServPath))
      RemoveFile(appServPath);
    end;
    if (not CopyFile(p_WorkFileName, appServPath))
      return "";
    end;
    return appServPath;
  end;

  macro writeExcelInTemproraryTable()

    var objExcel:CExcelPoi = CExcelPoi(); // DEF-82174
    var x_workFileName = CopyToAppTxtFile(FullNameFile);

    if(x_workFileName == "")
      lobj.info("Ошибка копирования файла на СП: " + FullExcelFile);
      return false;
    end; 

    // открываем файл Excel
    if(not objExcel.Open(x_workFileName))
      lobj.info("Ошибка открытия файла \"" + x_workFileName + "\"");
      return false;
    end;

    var OldPrec = setDefMoneyPrec(13); //установить точность для типа money

    /*Читаем строки*/
    var row = 8; //первая строка со значениями
    var rec; //объект записи, заполняемый из строки файла Excel 
    if (RslDefCon.IsInTrans()) //на всякий случай
        RslDefCon.RollbackTrans();
    end;
    var iLastRow = objExcel.GetLastRowNum()+1;
    while(row <= iLastRow)
      var x_Val = objExcel.CellValueByRowAndCol(row, 1);
      var x_ValStr = trim(string(x_Val));
      if((valtype(x_Val) == V_UNDEF) or (x_ValStr == "") or (x_ValStr == NULL) or (x_ValStr == "Undefined"))
        break;
      end;
      rec = recordOfExcel(objExcel, row);  //дополнительно структура заполняется из СОФР
      rec.id = writeRecordInTable(rec.contract_number, 
                                        rec.fi_code, 
                                        0,0);
      lobj.info(" ");
      PrintWarning(rec); // напечатать предупреждения
      ProcessWrtOperation(rec); // создать операцию и запустить ее
      UpdateLog(rec);  // записать в лог статус и id операции, убрать примечание у операции
      row = row+1;
    end;

    objExcel.Close();

    setDefMoneyPrec(OldPrec);
    var message_itog = 
      "Обработка завершена. Количество успешно закрытых операций:" + n_success 
      + ".  Количество не закрытых операций: " + n_failed 
      + ". Количество не созданных операций: " + n_not_created; 
    lobj.Info(message_itog);
    MsgBox(message_itog); 
  end;

  macro viewLogFile()
    lObj.viewLogFile();
  end;

end;

private macro boss1229readExcelRun()
  var stat = 0, filePath = "";
  GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\ИМПОРТ_1229", V_STRING, filePath, stat);
  var readExcelObject = boss1229readExcelClass(filePath+"diffBOSS1229_18_07_24.xlsx");
  if (readExcelObject.selectExcel==false) MsgBox("Отказ от выгрузки"); return;  end;
  readExcelObject.truncateTemproraryTable;
  readExcelObject.writeExcelInTemproraryTable;
  readExcelObject.viewLogFile();
onError(er)
  MsgBox("Ошибка при загрузке данных " + er.Message);
  RunError("Ошибка при загрузке данных " + er.Message);
end;

private macro boss1229SavePrecision;
  CaptureOutput();
  [
    declare
      n      number;
      t_name varchar2(100) := 'boss_1229_save_precision';
      v_sql varchar2(2000);
    begin
      select count(*) into n from user_tables t where t.table_name = upper(t_name);
      if n = 0 then
        v_sql := 'CREATE TABLE ' || t_name || ' as (SELECT T_FI_CODE, T_NAME, T_SUMPRECISION 
        FROM DFININSTR_DBT WHERE T_FI_CODE IN (
        ''2010015090094'',
        ''US04537Y2081'',
        ''1-02-06556-А'',
        ''US83422E2046'',
        ''US88688T1007'',
        ''US21077C3051'',
        ''US54405Q2093'',
        ''US45773H4092'',
        ''US10802T2042'',
        ''US37960A6698'',
        ''US04634X2027'',
        ''US9344231041''
        ) )';
        execute immediate v_sql;

        v_sql := 'MERGE INTO DFININSTR_DBT t1
          USING (SELECT * FROM boss_1229_save_precision) t2
          ON (t1.t_fi_code = t2.t_fi_code)
          WHEN MATCHED THEN
          UPDATE SET t1.T_SUMPRECISION = 13';
        execute immediate v_sql;
        COMMIT; 
      end if;
    end;
  ];
  ExecSQL(StopCaptureOutput());
end;

// просмотр какого-либо запроса
private macro scrollSomeTable(sql)
  RunScroll(RSDRecordSet(RSDCommand(sql), RSDVAL_CLIENT, RSDVAl_STATIC));
end;

// просмотр точностей
private macro scrollFininstr()
  CaptureOutput();
  [
    select f.t_fi_code, f.t_sumprecision
    FROM DFININSTR_DBT f WHERE f.T_FI_CODE IN (
    '2010015090094',
    'US04537Y2081',
    '1-02-06556-А',
    'US83422E2046',
    'US88688T1007',
    'US21077C3051',
    'US54405Q2093',
    'US45773H4092',
    'US10802T2042',
    'US37960A6698',
    'US04634X2027',
    'US9344231041'
    )
  ];
  scrollSomeTable(StopCaptureOutput())
end;

private macro boss1229RestorePrecision();
  CaptureOutput();
  [
    begin
  	MERGE INTO DFININSTR_DBT t1
  	USING
  	(
  	SELECT * FROM boss_1229_save_precision
  	) t2
  	ON (t1.t_fi_code = t2.t_fi_code and t2.t_sumprecision <> 13 )
  	WHEN MATCHED THEN UPDATE SET
    	t1.T_SUMPRECISION = t2.t_sumprecision;
       COMMIT;
    end;
  ];
  ExecSQL(StopCaptureOutput());
end;

private macro SelectMenu ()
  array Pmenu;
  Pmenu(0) = " Отказ ";
  Pmenu(1) = " 1. Сохранить точности ЦБ в таблицу boss_1229_save_precision и заменить точности у ЦБ из дефекта. Однократно";
  Pmenu(2) = " Просмотр таблицы boss_1229_save_precision";
  Pmenu(3) = " Просмотр точностей в таблице dfininstr_dbt для ЦБ из дефекта";
  Pmenu(4) = " 2. Запустить процедуру создания операций списании/зачислений ЦБ. Предварительно выйти из СОФР. Через Ctrl-F5. Однократно";
  Pmenu(5) = " 3. Восстановить точности для ЦБ из дефекта. Однократно";
  Pmenu(6) = " Просмотр лога созданных операций";
  var Prompt   = " Запустите процедуру или откажитесь... Процедуры пронумерованы в логическом порядке"; 
  return Menu (Pmenu,Prompt,"Выберите режим работы");
end;

var NumSel = 0; // Выбрано в меню
NumSel = SelectMenu ();
if (NumSel != 0)
  if (NumSel == 1)
    boss1229SavePrecision;
  elif (NumSel == 2)
    scrollSomeTable("select * from boss_1229_save_precision");
  elif (NumSel == 3)
    scrollFininstr();
  elif (NumSel == 4)
    boss1229readExcelRun;
  elif (NumSel == 5)
    boss1229RestorePrecision();
  elif (NumSel == 6)
    scrollSomeTable("select l.*, t.t_dealcode from LOSECALIGNMENT_TMP_LOG l left join ddl_tick_dbt t on l.t_dealid = t.t_dealid order by t_id");
  end;
else
    println ("Вы отказались от выполнения процедуры.");
end;


