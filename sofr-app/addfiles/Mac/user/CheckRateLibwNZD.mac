import cb_sql;
import календарь;

Macro CheckRate()

  var rs = RSDRecordset("Select RSBSessionData.curdate as curdate from dual");/*Аналогия {curdate} из globals (globals в startbnk.mac при запуске в безынтерфейсном режиме ведёт себя некорректно)*/
  var Curdate = Date();
  getparm(0,Curdate);
  if (ValType(Curdate)!=v_date)
    Curdate = Date();
    if (rs.movenext)
      Curdate = rs.value(0, null, V_DATE);
    end;
  end;

  var cmd, loop = true, mes = "", daymes = "";
  while ( loop )
    daymes = "";
    cmd = RsdCommand("select f.t_name " + // Ищем валюту
                     "  From dfininstr_dbt f " +
                     " where f.t_fi_kind = 1 " +
                     "   and f.t_isclosed = chr(0) " +
		     "       AND f.t_fiid !=  26434 " + // NZD                
                     "   and f.t_fiid not in " + // для которой нет курса за дату
                     "       (select f.t_fiid " +
                     "          From dratedef_dbt t, dfininstr_dbt f, dratehist_dbt h " +
                     "         where t.t_otherfi = f.t_fiid  " +
                     "           and f.t_fi_kind = 1 " +
                     "           and f.t_isclosed = chr(0) " +
                     "           and t.t_isdominant = chr(88) " +
                     "           and t.t_fiid = 0 " +
                     "           and t.t_rateid = h.t_rateid " +
                     "           and ? in (h.t_sincedate, t.t_sincedate)) " +
                     "   and f.t_fiid in " + // хотя ещё недавно курс вёлся
                     "       (select f.t_fiid " +
                     "          From dratedef_dbt t, dfininstr_dbt f " +
                     "         where t.t_otherfi = f.t_fiid  " +
                     "           and f.t_fi_kind = 1 " +
                     "           and f.t_isclosed = chr(0) " +
                     "           and t.t_isdominant = chr(88) " +
                     "           and t.t_fiid = 0 " +
                     "           and t.t_inputdate >= ? - 7)");

    cmd.addParam("", RSDBP_IN, Curdate);
    cmd.addParam("", RSDBP_IN, Curdate);
    cmd.execute();
    
    rs = RsdRecordset(cmd);

    while(rs.MoveNext())
      daymes = daymes + "\"" + rs.value(0) + "\", ";
    end;

    if(strlen(daymes)>0)
      mes = mes + "\nНе заданы курсы на дату " + Curdate + " для следующих валют: " + substr(daymes,1, strlen(daymes)-2);
    end;

    Curdate = Curdate - 1;
    if (not isHoliday(Curdate))
      loop = false;
    end;
  end;

  if(strlen(mes)>0)
    return("Внимание!!!"+mes);
  end;
  return "";

end;
