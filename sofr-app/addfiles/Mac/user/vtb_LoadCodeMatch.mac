/*----------------------------------------------------------*/
/* vtb_LoadCodeMatch.mac
   Соответствие кодов ВТБ и ЦФТ - загрузка из *.csv         
   Формат данных:
   VTB|CFT                                                  */
/*                                                          */
/* Автор: Гераськина Т.                                     */
/*----------------------------------------------------------*/
import rsd, rsexts;
import "usr_open_files.mac";
import BankInter;

private class Logger()

    macro RepHeader(dt, tm)
       var str;
       str =     "                             ОТЧЕТ о загрузке кодов \n";
       str = str+"\n";
       str = str+"Дата загрузки  : "+dt+"\n";
       str = str+"Время загрузки : "+tm+"\n";
       str = str+"┌─┬──────────────────┬──────────────────────────────┬───────────────┬─────────────────────────────────────────────────────────────────────────────────────────┐\n";
       str = str+"│ │Код АБС           │Код ВТБ                       │Статус загрузки│Комментарий                                                                              │\n";
       str = str+"├─┼──────────────────┼──────────────────────────────┼───────────────┼─────────────────────────────────────────────────────────────────────────────────────────┤";
       println(str);
    end;

    macro RepFooter(suc, er, tot)
       var str = "└─┴──────────────────┴──────────────────────────────┴───────────────┴─────────────────────────────────────────────────────────────────────────────────────────┘\n";
       str = str+"Загружено успешно: "+suc+"\n";
       str = str+"Не загружено     : "+er+"\n";
       str = str+"Всего            : "+tot+"\n";
      println(str);
    end;

    macro RepStr(_err,_code, _ca, _stat,_comm)
        var status;
        if (_stat == false)
            status = "Загружено";
        else
            status = "Не загружено";
        end;
        var str = "│"+string(_err  :1)   +"│"+
                  string(_code     :18:l)+"│"+
                  string(_ca       :30:l)+"│"+
                  string(status    :15:l)+"│"+
                  string(_comm          );
       return println(str);
    end;
end;

// получение sql-ошибки. 
// использовать в onError(e) так:  RunError(e.message + getFullCmdErrorText(cmd));
private macro getFullCmdErrorText(_cmd:RSDCommand)
   var cmd = _cmd;
   var err_str = "", i = 0;

   while( i < cmd.connection.environment.ErrorCount)
      err_str = err_str + cmd.connection.environment.Error(i).Descr + " ";
      i = i+1;
   end;

   return trim(err_str);
end;

private macro InsRec(_code_abs, _code_cft)
   var sql_str, cmd, rs;
   var res = "OK";
   
   sql_str = "declare "+
             "    v_Err varchar2(2000);"+
             " begin "+
             "    begin "+
             "       insert into usr_code_match (t_code_abs, t_code_vtb, t_partyid) "+
             "       values (:code_abs, :code_vtb, 0);"+
             "       v_Err := 'OK'; "+
             "    exception "+
             "       when DUP_VAL_ON_INDEX then v_Err := 'Дублирование кода АБС'; "+
             "       when OTHERS then v_Err := 'Ошибка вставки: '|| SQLERRM; "+
             "    end; "+
             "    :p_Err := v_Err; "+
             "end; ";
             
   cmd = RSDCommand(sql_str);
   cmd.AddParam("code_abs", RSDBP_IN, _code_abs);
   cmd.AddParam("code_vtb", RSDBP_IN, _code_cft);
   cmd.AddParam("p_Err", RSDBP_OUT, v_string, 2000);
   cmd.execute;
   if ((cmd.Value("p_Err") != null) and (cmd.Value("p_Err") != nullval))
      res = cmd.Value("p_Err");
   end;

   return res;
onError(e)
   return e.message + getFullCmdErrorText(cmd);
end;

macro runLoadCodeMatch(errp:@string, result_protocol:@variant)
   var ErrStr = "",ErrStat = false;
   var row = 0;
   var suc = 0, err = 0;
   var log = Logger();
   var dt = StrSubSt(string(Date()),".","")+StrSubSt(string(Time()),":","");
   var InfoLogFileName = GetTxtFileName( "load_codes_"+dt);

   var cur_file_mask = "*.csv";
   file datafile() txt;
   SetDelim("|");
      
   var c_file = c_txt_file();
   var FullFileName;
   var stat;
              
   var sql_str, cmd, rs;

   stat = c_file.openFile(datafile, cur_file_mask, true, false, FullFileName, "rsansi");

   if ( (valtype(stat) == V_UNDEF) or (stat == false) )
      errp = "Ошибка открытия файла";
      return false;
   end;

   /*Открываем файл*/
   BegAction(1, "Обработка файла \"" + c_file.localFileName + "\". Ждите...");

   /* формат файла, первая строка - всегда заголовок, данные со второй строки
   CFT;VTB
1	Идентификатор CFT (код 101)
2	Идентификатор ВТБ (код 109)
     */

   next(datafile);

/*   if (  (StrUpr(datafile(0)) == StrUpr("CFT")) and
         (StrUpr(datafile(1)) == StrUpr("VTB"))  )
   else 
      EndAction(1);
      errp = "Файл имеет некорректный заголовок, корректный: CFT;VTB";
      return false;
   end;     */
   SetOutput( InfoLogFileName, true ); //Пишем лог в файл. Начало
   log.RepHeader(date(),time());
   
   // очищать таблицу не будем, так как возможны частичные загрузки
   /*sql_str = "truncate table usr_code_match";
   cmd = RSDCommand(sql_str);
   cmd.execute;
   cmd.close;
   cmd = null;*/

   row = 2;
   //Бежим по файлу
   InitProgress(-1);
   while (next(datafile))
      if (StrLen(trim(datafile.str)) == 0)
         continue;
      end;
      
      if (StrLen(datafile(1)) == 0)
         ErrStat = true;
         ErrStr = "Не задан код ЦФТ, строка "+row;
         log.RepStr("X","","", ErrStat, ErrStr);
         row = row + 1;
         UseProgress(row);
         err = err + 1;
         continue;
      end;
      
      if (StrLen(datafile(0)) == 0)
         ErrStat = true;
         ErrStr = "Не задан код ВТБ, строка "+row;
         log.RepStr("X",datafile(0),"", ErrStat, ErrStr);
         row = row + 1;
         UseProgress(row);
         err = err + 1;
         continue;
      end;
      
      stat = InsRec(datafile(1), datafile(0));

      if (stat == "OK")
         ErrStat = false;
         ErrStr = "";
         log.RepStr("",datafile(1), datafile(0), ErrStat, ErrStr);
         suc = suc + 1;
      else 
         ErrStat = true;
         ErrStr = stat;
         log.RepStr("X",datafile(1), datafile(0), ErrStat, ErrStr);
         row = row + 1;
         UseProgress(row);
         err = err + 1;
         continue;
      end;

      row = row+1;
      UseProgress(row);
      ErrStr = "";
      ErrStat = false;
   end;
   RemProgress();

   log.RepFooter(suc,err,suc+err);
   SetOutput( NULL, true ); //Пишем лог в файл. Конец

   c_file.closeFile(datafile);
   ViewFile( InfoLogFileName );

   errp = "";
   result_protocol.addString("Обработано " + row + " строк " );
   return true;


end;

//runLoadCodeMatch();