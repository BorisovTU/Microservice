//Исправление резерва на лотах по счетам
import CTInter, sp_car, spserv, secinter, dlquery;

private const TestMode = 1; //режим исполнения. 0 - отражаем в БД, иначе только протокол
private const CalcDate = {curdate};

private var protocolArr = TArray();

//Занести разницу на лоты
private macro CorrectLots(FIID, Portfolio, AddSumReserv, FieldName, TotalAmount)
  var query, cmd, DataSet;
  var AddLotSum = 0;
  var RestAmount = TotalAmount, RestAddSum = AddSumReserv;

  if(TotalAmount == 0) 
    return 0;
  end;

  query =   " SELECT lot.t_SumID, lot.t_Amount "
          + "   FROM dpmwrtsum_dbt lot "
          + "  WHERE lot.t_Party = -1 "
          + "    AND lot.t_FIID = ? "
          + "    AND lot.t_Portfolio = ? "
          + "    AND lot.t_Amount > 0 "
          + " ORDER BY lot.t_Amount DESC";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(FIID);
  cmd.AddParam(Portfolio);

  DataSet = cmd.Execute();

  while(DataSet.moveNext())

    if(RestAmount == DataSet.Amount)
      AddLotSum = round(RestAddSum, 2);
    else
      AddLotSum = round(DataSet.Amount * AddSumReserv / TotalAmount, 2);
    end;

    SQL_Execute(  " UPDATE dpmwrtsum_dbt lot"
                + "    SET lot."+FieldName+" = lot."+FieldName+" + "+AddLotSum
                + "  WHERE lot.t_SumID = " + DataSet.SumID
                , 
                null, false );


    RestAmount = RestAmount - DataSet.Amount;
    RestAddSum = RestAddSum - AddLotSum;
  end;

  return 0;

OnError(er)
  return 1;
end;

private class TProtocolData(_Account, _RestAc, _OldRestOnLots, _NewRestOnLots, _Message)
  var Account       = _Account;      
  var RestAc        = _RestAc;       
  var OldRestOnLots = _OldRestOnLots;
  var NewRestOnLots = _NewRestOnLots;
  var Message       = _Message;       
end;

private macro GetResSum(FIID, Portfolio, FieldName)
  var query, cmd, DataSet;
  var SumRes = 0;

  query =   " SELECT NVL(SUM(lot."+FieldName+"), 0) as SumRes "
          + "   FROM dpmwrtsum_dbt lot "
          + "  WHERE lot.t_Party = -1 "
          + "    AND lot.t_FIID = ? "
          + "    AND lot.t_Portfolio = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(FIID);
  cmd.AddParam(Portfolio);

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    SumRes = money(DataSet.SumRes);
  end;

  return SumRes;
end;

private macro ExecCorrectByAccount(Account:string)
  var query, cmd, DataSet;
  var err = 0;

  var AddSumReserv = $0;
  var FieldName = "";
  var RestOnLots = 0;

  query =   " WITH q AS (SELECT md.t_FIID, cat.t_Code, " 
          + "                   rsb_account.restac(md.t_Account, md.t_Currency, "+GetSQLDate(CalcDate)+", md.t_Chapter, NULL) AS RestAcc, "
          + "                   (CASE WHEN cat.t_Class1 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value1 "
          + "                         WHEN cat.t_Class2 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value2 "
          + "                         WHEN cat.t_Class3 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value3 "
          + "                         WHEN cat.t_Class4 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value4 "
          + "                         WHEN cat.t_Class5 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value5 "
          + "                         WHEN cat.t_Class6 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value6 "
          + "                         WHEN cat.t_Class7 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value7 "
          + "                         WHEN cat.t_Class8 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value8 "
          + "                         ELSE -1 "
          + "                    END) as Portfolio, "
          + "                   (CASE WHEN cat.t_Class1 = "+530/*LLCLASS_KINDRESERV_KIND*/+" THEN tpl.t_Value1 "
          + "                         WHEN cat.t_Class2 = "+530/*LLCLASS_KINDRESERV_KIND*/+" THEN tpl.t_Value2 "
          + "                         WHEN cat.t_Class3 = "+530/*LLCLASS_KINDRESERV_KIND*/+" THEN tpl.t_Value3 "
          + "                         WHEN cat.t_Class4 = "+530/*LLCLASS_KINDRESERV_KIND*/+" THEN tpl.t_Value4 "
          + "                         WHEN cat.t_Class5 = "+530/*LLCLASS_KINDRESERV_KIND*/+" THEN tpl.t_Value5 "
          + "                         WHEN cat.t_Class6 = "+530/*LLCLASS_KINDRESERV_KIND*/+" THEN tpl.t_Value6 "
          + "                         WHEN cat.t_Class7 = "+530/*LLCLASS_KINDRESERV_KIND*/+" THEN tpl.t_Value7 "
          + "                         WHEN cat.t_Class8 = "+530/*LLCLASS_KINDRESERV_KIND*/+" THEN tpl.t_Value8 "
          + "                         ELSE -1 "
          + "                    END) as KindReserv "
          + "              FROM dmcaccdoc_dbt md, dmccateg_dbt cat, dmctempl_dbt tpl "
          + "             WHERE md.t_Account = ? "
          + "               AND md.t_Chapter = 1 "
          + "               AND md.t_IsCommon = 'X' "
          + "               AND md.t_DisablingDate = " + GetSQLDate(date(0,0,0))
          + "               AND cat.t_ID = md.t_CatID"
          + "               AND tpl.t_CatID = md.t_CatID "
          + "               AND tpl.t_Number = md.t_TemplNum "
          + "               AND ROWNUM = 1 "
          + "           ) "
          + " SELECT q.t_Code as CatCode, q.KindReserv, q.Portfolio, q.RestAcc, q.t_FIID, "
          + "        NVL(SUM(lot.t_Amount), 0)         as SumAmount, "
          + "        NVL(SUM(lot.t_ReservAmount), 0)   as SumReservAmount, "
          + "        NVL(SUM(lot.t_IncomeReserv), 0)   as SumIncomeReserv, "
          + "        NVL(SUM(lot.t_EstReserve), 0)     as SumEstReserve, "
          + "        NVL(SUM(lot.t_CorrEstReserve), 0) as SumCorrEstReserve "
          + "   FROM dpmwrtsum_dbt lot, q "
          + "  WHERE lot.t_Party = -1 "
          + "    AND lot.t_FIID = q.t_FIID "
          + "    AND lot.t_Portfolio = q.Portfolio "
          + "  GROUP BY q.t_Code, q.KindReserv, q.Portfolio, q.RestAcc, q.t_FIID ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(Account);

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    AddSumReserv = $0;
    FieldName = "";

    if(DataSet.CatCode == "Резерв ц/б")
      if(DataSet.KindReserv == 4) //РВЦБ Резерв по вложениям в ц/б
        RestOnLots   = DataSet.SumReservAmount;
        if(DataSet.SumReservAmount != DataSet.RestAcc)
          FieldName    = "t_ReservAmount";
        end;
      elif(DataSet.KindReserv == 13) //РВПЦБ Резерв по начисленному ПДД по ц/б
        RestOnLots   = DataSet.SumIncomeReserv;
        if(DataSet.SumIncomeReserv != DataSet.RestAcc)
          FieldName    = "t_IncomeReserv";
        end;
      end;
    elif((DataSet.CatCode == "-Кор_Резерв, ЦБ") or (DataSet.CatCode == "+Кор_Резерв, ЦБ"))
      if(DataSet.Portfolio == KINDPORT_SSSD)
        RestOnLots   = DataSet.SumEstReserve;
        if(DataSet.SumEstReserve != DataSet.RestAcc)
          FieldName    = "t_EstReserve";
        end;
      else
        RestOnLots   = DataSet.SumCorrEstReserve;
        if(DataSet.SumCorrEstReserve != DataSet.RestAcc)
          FieldName    = "t_CorrEstReserve";
        end;
      end;
    end;

    if(FieldName != "")
      AddSumReserv = DataSet.RestAcc - RestOnLots;
    end;

    if(AddSumReserv != 0)
      if(0 != CorrectLots(DataSet.FIID, DataSet.Portfolio, AddSumReserv, FieldName, DataSet.SumAmount))
        protocolArr[protocolArr.size] = TProtocolData(Account, 
                                                      ЧислоCЗаданнойТочностью(DataSet.RestAcc,2), 
                                                      ЧислоCЗаданнойТочностью(RestOnLots,2), 
                                                      ЧислоCЗаданнойТочностью(GetResSum(DataSet.FIID, DataSet.Portfolio, FieldName), 2), 
                                                      "Ошибка при выполнении корректировки: "+GetErrMsg()
                                                     );
      else
        protocolArr[protocolArr.size] = TProtocolData(Account, 
                                                      ЧислоCЗаданнойТочностью(DataSet.RestAcc,2), 
                                                      ЧислоCЗаданнойТочностью(RestOnLots,2), 
                                                      ЧислоCЗаданнойТочностью(GetResSum(DataSet.FIID, DataSet.Portfolio, FieldName), 2), 
                                                      "Корректировка резерва на лотах на сумму "+ЧислоCЗаданнойТочностью(AddSumReserv, 2)+" выполнена успешно."
                                                     );
      end;
    else
      protocolArr[protocolArr.size] = TProtocolData(Account, 
                                                  ЧислоCЗаданнойТочностью(DataSet.RestAcc,2), 
                                                  ЧислоCЗаданнойТочностью(RestOnLots,2), 
                                                  ЧислоCЗаданнойТочностью(RestOnLots,2), 
                                                  "Корректировка не требуется."
                                                 );
    end;
  end;
end;


private macro ExecCorrect()

  protocolArr.size = 0;

  RslDefCon.BeginTrans();

  InitProgress(-1, "Корректировка лотов", "Корректировка лотов");

    ExecCorrectByAccount("10630810099005001981");
    ExecCorrectByAccount("10630810999005101981");
    ExecCorrectByAccount("10635810099000001981");
    ExecCorrectByAccount("10630810799005001993");
    ExecCorrectByAccount("10635810799000001993");

  RemProgress();

  if(TestMode)
    RslDefCon.RollbackTrans();

    println("ВНИМАНИЕ!!! Включен тестовый режим. Фиксация изменения в БД не выполнена!");
    println();

  else 
    RslDefCon.CommitTrans();
  end;

  if(protocolArr.size > 0)
    println("                                               Корректировка резерва на лотах по остатку на счете");
    println("");
    println("┌─────┬──────────────────────┬────────────────┬────────────────┬────────────────┬───────────────────────────────────────────────────────┐");
    println("│     │                      │    Остаток на  │    Значение    │    Значение    │                     Сообщение                         │");
    println("│     │     Лицевой счет     │      счете     │    на лотах    │ на лотах после │                                                       │");
    println("│     │                      │                │до корректировки│  корректировки │                                                       │");
    println("│     │                      │                │                │                │                                                       │");
    println("├─────┼──────────────────────┼────────────────┼────────────────┼────────────────┼───────────────────────────────────────────────────────┤");
    println("│  1  │           2          │        3       │        4       │        5       │                            6                          │");
    println("├─────┼──────────────────────┼────────────────┼────────────────┼────────────────┼───────────────────────────────────────────────────────┤");

    var i = 0;
    while(i < protocolArr.size)
                                                                                                                              
            [│#####│######################│################│################│################│#######################################################│]
            (string(i+1):c,
             string(protocolArr[i].Account):c,        
             string(protocolArr[i].RestAc):r,         
             string(protocolArr[i].OldRestOnLots):r, 
             string(protocolArr[i].NewRestOnLots):r,
             string(protocolArr[i].Message):l:w        
            );
    
      i = i + 1;
    end;
                                                                                                                                          
    println("└─────┼──────────────────────┼────────────────┼────────────────┼────────────────┼───────────────────────────────────────────────────────┘");

  end;

OnError(er)
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;
  RemProgress();
  msgbox("Ошибка при выполнении");
end;


ExecCorrect();

