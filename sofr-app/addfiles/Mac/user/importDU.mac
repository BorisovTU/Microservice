/** 
 @file importDU.mac
 @brief Загрузить операции вывода из ДУ
  
 Загрузить операции вывода из ДУ

 # tag 
 - functional_block:СО
 - code_type:API
 - BIQ-15249


 # changeLog 
 |date       |author         |tasks            |note                            
 |-----------|---------------|-----------------|--------------------------------
 |2023.12.12 |Фаршатов Г. К. |CCBO-7947        | Реализация операции             
 |           |               |                 |                   

*/ 
import global_classes;
import dlquery;
import func_lib;
import xls_parser, osfile, ws_validation, ws_txreportform;
import ws_file;

private const CONST_ROWNUM = 12;
private const V_SPECVAL = 26;
private const DUDATE_CATEGORY = 117;

/**
 @brief Выполнить SQL запрос
 @param[in] sqltext Текст SQL запроса
 @param[in] params Параметры запроса
 @return объект RsdCommand
*/
private macro myExecSQL ( sqltext, params )
    var rsdErrText = "";
    if ( sqltext == NULL ) sqltext = rsdErrText; end;
    var cmd:RsdCommand = RsdCommand (sqltext), i = 0;
    cmd.NullConversion = false;

    while ( params and (i < params.size) )
        cmd.addParam (params [i].name, params [i].type, params [i].value);
        i = i + 1;
    end;

    cmd.execute ();
    return cmd;
End;

/**
 @brief Класс с данными об загружаемых сделках
*/
class c_DUdata ()
  var Name         = "", //Наименование ЦБ
      ISIN         = "", //ISIN
      DealDate         , //Дата заключения сделки на приобретение ЦБ
      TransferDate     , //Дата перехода права собственности при приобретении ЦБ
      ISO          = "", //Код валюты номинала
      TransferNom  = 0,  //Номинал на дату перехода права собственности при приобретении ЦБ
      ExportDUNom  = 0,  //Номинал на дату вывода ЦБ из ДУ (ДД.ММ.ГГГГ)
      AmountDU     = 0,  //Количество ЦБ, выведенных из ДУ, шт.
      PriceNom     = 0,  //Цена приобретения, % от номинала
      PricePortfolioTransfer = 0, //Стоимость портфеля ЦБ по налоговому учету - на дату перехода права соственности при приобретении ЦБ, в валюте номинала
      Sum          = 0,  //Сумма расходов, учтенная при амортизации долга на дату вывода ЦБ из ДУ, в валюте номинала
      PricePortfolioExportDU = 0, //Стоимость портфеля ЦБ по налоговому учету - на дату вывода ЦБ из ДУ, в валюте номинала
      SumOther     = 0,  //Сумма прочих расходов, уплаченных при приобретении ЦБ, не учтенная на дату вывода ЦБ из ДУ, руб.
      NKD          = 0,  //НКД на дату вывода ЦБ из ДУ, в валюте номинала (ДД.ММ.ГГГГ)
      PFI          = 0,  
      FIID         = 0,  //Идентификатор ЦБ
      DEALCODE     = "", //Код сделки
      ImportID     = ""; //Идентификатор сделки при загрузке
end;

/**
 @brief Получить AttrID по значению категории 
 @param[in] BoardID Идентификатор сделки
 @param[in] dateDU Дата вывода из ДУ
 @return AttrID
 
 Получить AttrID по значению категории. В случае отсутствия значения добавляет его
*/
private macro GetAttrIDBoardID(BoardID, dateDU)
  var query = "SELECT t_attrid " +
              "  FROM dobjattr_dbt " +
              " WHERE t_objecttype = 101 AND t_groupid = :1 AND t_numinlist = :2 ";

  var cmd = myExecSql(query,makeArray (sqlParam ("1", DUDATE_CATEGORY),sqlParam ("2", dateDU)));  
  var DataSet = TRsbDataSet(cmd);
    
  if( DataSet.MoveNext() )
    return DataSet.attrid;
  else 
     var query1 = 
      "DECLARE " +
      "   val      VARCHAR2 (10) := ?; " +
      "   retval   NUMBER (10) := -1; " +
      "BEGIN " +
      "   INSERT INTO DOBJATTR_DBT (T_OBJECTTYPE, " +
      "                             T_GROUPID, " +
      "                             T_ATTRID, " +
      "                             T_PARENTID, " +
      "                             T_CODELIST, " +
      "                             T_NUMINLIST, " +
      "                             T_NAMEOBJECT, " +
      "                             T_CHATTR, " +
      "                             T_LONGATTR, " +
      "                             T_INTATTR, " +
      "                             T_NAME, " +
      "                             T_FULLNAME, " +
      "                             T_OPENDATE, " +
      "                             T_CLOSEDATE, " +
      "                             T_CLASSIFICATOR, " +
      "                             T_CORRACTYPE, " +
      "                             T_BALANCE, " +
      "                             T_ISOBJECT) " +
      "        VALUES (101, " +
      "                117, " +
      "                (SELECT NVL(MAX (t_attrid), 0)  + 1 " +
      "                   FROM dobjattr_dbt " +
      "                  WHERE t_objecttype = 101 AND t_groupid = 117), " +
      "                0, " +
      "                chr(1), " +
      "                val, " +
      "                val, " +
      "                '', " +
      "                0, " +
      "                0, " +
      "                val, " +
      "                val, " +
      "                TO_DATE ('01/01/0001 00:00:00', 'MM/DD/YYYY HH24:MI:SS'), " +
      "                TO_DATE ('01/01/0001 00:00:00', 'MM/DD/YYYY HH24:MI:SS'), " +
      "                0, " +
      "                chr(1), " +
      "                chr(1), " +
      "                '') " +
      "     RETURNING t_attrid " +
      "          INTO ?; " +
      "END; " ;
    var cmd1 = RSDCommand(query1);   
    cmd1.addParam("val", RSDBP_IN, dateDU);
    cmd1.addParam("retval", RSDBP_OUT, V_INTEGER);
    cmd1.execute();
    return cmd1.Value("retval");
  end;
end;

/**
 @brief Создать сделку по параметрам
 @param[in] DealCode Код сделки
 @param[in] DealCodeEts Код сделки
 @param[in] DealType Тип сделки
 @param[in] ClientID Идентификатор клиента
 @param[in] ContractID Идентификатор договора
 @param[in] Amount Количество
 @param[in] Price Цена
 @param[in] Cost Стоимость
 @param[in] Nkd НКД
 @param[in] Cms Комиссия
 @param[in] DealDate Дата сделки
 @param[in] DealTime Время сделки
 @param[in] DealDateBegin Дата начала сделки
 @param[in] DealTimeBegin Время начала сделки
 @param[in] FIID Идентификатор ЦБ
 @param[in] DeliveringFIID Идентификатор ЦБ
 @param[in] PriceCur 
 @param[in] Custody 
 @param[in] Department 
 @param[in] Oper Операционист 
 @param[in] Point Число знаков после запятой
 @param[in] Portfolio Портфель
 @param[in] ContragentID Идентификатор контрагента
 @param[in] dateDU Дата вывода из ДУ
 @param[out] msg Сообщение
 @param[out] RetID Идентификатор созданной сделки
 @return 0 - успешно
 @return 1 - ошибка
*/
private macro _CreateBoard (
                    DealCode,
                    DealCodeEts,
                    DealType, 
                    ClientID,
                    ContractID,
                    Amount,
                    Price,
                    Cost,
                    Nkd,
                    Cms,
                    DealDate,
                    DealTime,
                    DealDateBegin,
                    DealTimeBegin,
                    FIID,
                    DeliveringFIID,
                    PriceCur,
                    Custody,
                    Department,
                    Oper,
                    Point,
                    Portfolio,
                    ContragentID,
                    dateDU,
                    msg:@string,
                    RetID:@integer,
                    importID,
                    SumOther
                    )
    var BoardID = 0, stat = 0;
    record BoardPrm ("BoardPrm.rec");

    private macro InTrn()
       var statB = SP_InsertBoardLot (BoardPrm, BoardId, true);
       if ( not statB )
          runError ("-");
       end;

       if ( (dealcodeEts) and (myExecSql ("update ddl_tick_dbt set t_dealcodets = :1, t_TaxOwnBegDate = :2 where t_dealid = :3", makeArray (sqlParam ("1", dealcodeEts), sqlParam ("2", DealDateBegin), sqlParam ("3", BoardId))) == NULL) )
           runError ("-");
       end;

       if ( (Price) and (myExecSql ("UPDATE ddlsum_dbt SET t_date = :1, t_sum = :2,  t_currency = :3 where t_dockind = 127 and  t_docid = :4 and t_kind = :5",
                              makeArray (sqlParam ("1", DealDateBegin), sqlParam ("2", Price), sqlParam ("3", PriceCur), sqlParam ("4", BoardId), sqlParam ("5", 1220) )) == NULL) )
           runError ("-");
       end;

       if ( (Cost) and (myExecSql ("UPDATE  ddlsum_dbt SET t_date = :1, t_sum = :2,  t_currency = :3 where t_dockind = 127 and  t_docid = :4 and t_kind = :5",
                              makeArray (sqlParam ("1", DealDateBegin), sqlParam ("2", Cost), sqlParam ("3", PriceCur), sqlParam ("4", BoardId), sqlParam ("5", 1230) )) == NULL) )
           runError ("-");
       end;

       if ( (nkd) and (myExecSql ("UPDATE  ddlsum_dbt SET t_date = :1, t_sum = :2,  t_currency = :3 where t_dockind = 127 and  t_docid = :4 and t_kind = :5",
                              makeArray (sqlParam ("1", DealDateBegin), sqlParam ("2", nkd), sqlParam ("3", PriceCur), sqlParam ("4", BoardId), sqlParam ("5", 1240) )) == NULL) )
           runError ("-");
       end;

       if ( (Cms) and (myExecSql ("UPDATE  ddlsum_dbt SET t_date = :1, t_sum = :2,  t_currency = :3 where t_dockind = 127 and  t_docid = :4 and t_kind = :5",
                              makeArray (sqlParam ("1", DealDateBegin), sqlParam ("2", Cms), sqlParam ("3", PriceCur), sqlParam ("4", BoardId), sqlParam ("5", 1250) )) == NULL) )
           runError ("-");
       end;

       if ( (PriceCur) and (myExecSql ("UPDATE  ddlsum_dbt SET t_currency = :1 where t_dockind = 127 and  t_docid = :2 and t_kind = :3",
                              makeArray ( sqlParam ("1", PriceCur), sqlParam ("2", BoardId), sqlParam ("3", 1100) )) == NULL) )
           runError ("-");
       end;

        if (  (myExecSql (
                          "Insert into DDLCOMIS_DBT \n" +
                          "   (T_ID, T_DOCKIND, T_DOCID, T_CONTRACT, T_FEETYPE, T_COMNUMBER, T_SUM, T_NDS, T_DATE, T_PLANPAYDATE,  " 
                          " T_FACTPAYDATE, T_INPUTMEDIUM, T_SERVOPERID, T_ISBACK, T_VERSION, T_RECEIVERID, T_PARENT, T_GROUND, T_RUNCARRY, T_MONTHAMORTSUM, T_COMTYPE, T_STARTAMORTDATE, T_SETTLECODE) \n" +
                          " Values \n" +
                          "   (0, 127, :1, :2, 1,  \n" +
                          "    60, :3, 0, :4, :5,  \n" +
                          "    :6, 'П', 0, chr(0),  \n" +
                          "    0, 0, 0, CHR(1), CHR(0),  \n" +
                          "    0, 0, TO_DATE('01/01/0001 00:00:00', 'MM/DD/YYYY HH24:MI:SS'), CHR(1)); \n" ,
              makeArray (sqlParam ("1", BoardID), sqlParam ("2", 0), sqlParam ("3", Cms), sqlParam ("4", DealDateBegin), sqlParam ("5", DealDateBegin), sqlParam ("6", DealDateBegin))) == NULL) )
            runError ("-");
        end;


        if (  (myExecSql ("update ddl_tick_dbt set t_dealstatus = 20 where t_dealid = :1", makeArray (sqlParam ("1", BoardId))) == NULL) )
            runError ("-");
        end;

        if (  (myExecSql ("update ddl_tick_dbt set t_ofbu = chr(88) where t_dealid = :1", makeArray (sqlParam ("1", BoardId))) == NULL) )
            runError ("-");
        end;

        if (  (myExecSql ("update ddl_tick_dbt set t_flag3 = chr(88) where t_dealid = :1", makeArray (sqlParam ("1", BoardId))) == NULL) )
            runError ("-");
        end;

        if (  (myExecSql ("update ddlrq_dbt set t_state = 2, t_plandate = :1, t_factdate = :2 where t_docid = :3 and t_dockind = 127", makeArray (sqlParam ("1", DealDateBegin),sqlParam ("2", DealDateBegin),sqlParam ("3", BoardId))) == NULL) )
            runError ("-");
        end;

        if (  (myExecSql ("update ddl_leg_dbt set t_start = ? where t_dealid = ?", makeArray (sqlParam ("1", DealDateBegin),sqlParam ("2", BoardId))) == NULL) )
            runError ("-");
        end; 

        if (  (myExecSql ("update ddl_leg_dbt set t_RelativePrice = ? where t_dealid = ?", makeArray (sqlParam ("1", "X"),sqlParam ("2", BoardId))) == NULL) )
            runError ("-");
        end;

        if (  (myExecSql ("insert into ddlsum_dbt (t_Dockind, t_Docid , t_kind, t_date, t_sum, t_nds, t_currency, t_fiid) values (127, ?, ?, ?, ?, 0, ?, -1)", 
               makeArray (sqlParam ("1", BoardId),sqlParam ("2", 1100),sqlParam ("3", DealDateBegin),sqlParam ("4", SumOther),sqlParam ("5", PriceCur))) == NULL) )
            runError ("-");
        end;

       ConnectObjAttr(stat, OBJTYPE_SECDEAL, string(BoardID:o:34), DUDATE_CATEGORY, GetAttrIDBoardID(BoardID, dateDU), null, null, {curdate});

       AddNoteForObject(OBJTYPE_SECDEAL, string(BoardID:o:34), 39, importID);

       msg = string("ID операции: "+BoardID);
       RetID = BoardID;

    OnError ( err )
       AbortTrn();
    end;

    BoardPrm.DealType           = DealType; 
    BoardPrm.DealCode           = DealCode;
    BoardPrm.Client             = IIF (ClientID > 0, ClientID, {OurBank});
    BoardPrm.ClientContr        = IIF (ContractId > 0, ContractId, 0);
    BoardPrm.Amount             = Amount;
    BoardPrm.Price              = price;
    BoardPrm.Cost               = Cost;
    BoardPrm.Nkd                = nkd;
    BoardPrm.DealDate           = DealDate;

    if ( ClientID <= 0 )
        BoardPrm.Begbonusdate   = DealDate;
    end;
    if ( valType (dealTime) == V_TIME )
        BoardPrm.DealTime       = dealTime;
    end;
    BoardPrm.PreOutlay          = 0;
    BoardPrm.Fiid               = FIID;
    BoardPrm.DeliveringFIID     = DeliveringFIID;
    BoardPrm.priceFi            = priceCur;
    BoardPrm.PreOutlayFiid      = 0;
    BoardPrm.Custody            = Custody;
    BoardPrm.DepoClientCustody  = {ourBank};
    BoardPrm.DepoContrCustody   = Custody;
    BoardPrm.Department         = 1;
    BoardPrm.Oper               = Oper;
    BoardPrm.DealState          = DL_PREPARING; 
    BoardPrm.Outlay             = Cms;
    BoardPrm.LastOverDate       = DealDate;
    BoardPrm.BegDate            = DealDate;
    BoardPrm.Point              = point;
    BoardPrm.Portfolio          = portfolio;
    BoardPrm.Contragent         = ContragentID;

   if (ProcessTrn(0, "InTrn"))
      return 1;
   else
      return 0;
   end;
End;

/**
 @brief Получить строку
 @param[in] val Значение для получения строки
 @return Строка со значением val
*/
private macro GetStr(val)
    var Type = ValType( val );

    if( Type == V_STRING )
       return Trim(val);
    elif( ( Type == V_DOUBLE ) or ( Type == V_MONEY ) )
       return string(int(val));
    else
       return "";
    end;
end;

/**
 @brief Преобразуем строку в вещественное число
 @param[in] strSum Значение для получения числа
 @return Число со значением strSum
*/
private macro GetDoubleFromString(strSum: String)
    var res = 0.0;
    strSum = Trim(strSum);
    if( strSum != "" )
      var i = 1; 
      var resultStr = "";
      var str;
      var position = strBrk(strSum, ",");
      var sign = IIF(subStr(strSum, 1, 1)=="-", -1, 1);
      if( position < 1 ) // число без запятой (целое)
        position = strlen(strSum) + 1;
      end;
      while( i < position )
        str = subStr(strSum, i, 1);
        if( StrIsNumber(str) )
          resultStr = resultStr + str;
        end;
        i = i + 1;
      end;
      if( position > 0 ) // число c запятой
        resultStr = resultStr + ".";
        resultStr = resultStr + subStr(strSum, position + 1);
      end;
      res = Double(resultStr)*sign;
    end;      
    return res;
end;

/**
 @brief Преобразуем вариативную переменную в вещественное число
 @param[in] val2 Значение для получения числа
 @return Число со значением val2
*/
private macro GetDoubleFromValue2(val2)
    var t = ValType( val2 );

    /* Передан SQL'ый NULL*/
    if ((t == V_SPECVAL) or (t == V_UNDEF))
      return 0.0;
    elif( ( t == V_DOUBLE ) or ( t == V_DOUBLEL ) )
        return val2;
    elif( ( t == V_MONEY ) or ( t == V_MONEYL ) or ( t == V_INTEGER ) )
        return Double( val2 );
    elif( t == V_STRING )
        return GetDoubleFromString( val2 );
    else
        return 0.0;
    end;
end;

/**
 @brief Получить дату по числу из Excel
 @param[in] Num Номер для
 @return Дата соответствующая числу
*/
private macro getDateFromExcelNum(Num)
 var dateOne =  Date ("01.01.1900");

 if( valType (Num) == V_DTTM )
   DtTmSplit ( Num, dateOne, NULL );
   return dateOne;
 elif( valType (Num) == V_DATE )
   return Num;
 elif( valType (Num) == V_INTEGER )
   return dateOne + int(Num - 2);
 elif( valType (Num) == V_DOUBLE )
   return dateOne + int(Num - 2);
 end;

 return Date ("01.01.0001");
end;

/**
 @brief Сохранить запись вывода в таблицу
 @param[in] DUdata Класс c_DUdata с данными для вставки
*/
private macro SaveDataDUtoTMP(DUdata)
  var cmd;

  cmd = RSDCommand( "    INSERT INTO DIMPOPRDU_TMP ( " +
                    "      T_AMOUNTDU, T_DEALDATE, T_EXPORTDUNOM, " +
                    "      T_ISIN, T_NAMEAVR, T_NKD, " +
                    "      T_ISO, T_PRICENOM, T_PRICEPORTFOLIOEXPORTDU, " +
                    "      T_PRICEPORTFOLIOTRANSFER, T_SUM, T_SUMOTHER, " +
                    "      T_TRANSFERDATE, T_TRANSFERNOM, T_IMPORTID) " +
                    "   VALUES ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?) ");
        
  cmd.addParam( "", RSDBP_IN, DUdata.AmountDU );/* T_AMOUNTDU */
  cmd.addParam( "", RSDBP_IN, DUdata.DealDate );/* T_DEALDATE */
  cmd.addParam( "", RSDBP_IN, DUdata.ExportDUNom );/* T_EXPORTDUNOM */
  cmd.addParam( "", RSDBP_IN, DUdata.ISIN );/* T_ISIN */
  cmd.addParam( "", RSDBP_IN, DUdata.Name );/* T_NAMEAVR */
  cmd.addParam( "", RSDBP_IN, DUdata.NKD );/* T_NKD */
  cmd.addParam( "", RSDBP_IN, DUdata.ISO ); //CFI!!   /* T_PFI */
  cmd.addParam( "", RSDBP_IN, DUdata.PriceNom );/* T_PRICENOM */
  cmd.addParam( "", RSDBP_IN, DUdata.PricePortfolioExportDU );/* T_PRICEPORTFOLIOEXPORTDU */
  cmd.addParam( "", RSDBP_IN, DUdata.PricePortfolioTransfer );/* T_PRICEPORTFOLIOTRANSFER */
  cmd.addParam( "", RSDBP_IN, DUdata.Sum );/* T_SUM */
  cmd.addParam( "", RSDBP_IN, DUdata.SumOther );/* T_SUMOTHER */
  cmd.addParam( "", RSDBP_IN, DUdata.TransferDate );/* T_TRANSFERDATE */
  cmd.addParam( "", RSDBP_IN, DUdata.TransferNom );/* T_TRANSFERNOM */
  cmd.addParam( "", RSDBP_IN, DUdata.ImportID );/* T_IMPORTID */
  cmd.execute();
end;

/**
 @brief Очистить таблицу для импорта
*/
private macro DeleteDataDUfromTMP()
  var cmd;

  cmd = RSDCommand( " TRUNCATE TABLE DIMPOPRDU_TMP ");
  cmd.execute();
end;

/**
 @brief Получить путь к файлам вывода из ДУ
 @return Путь к файлам вывода из ДУ
*/
private macro GetDUDir():STRING
  var TotalTaxRegDir = "", err;
  TotalTaxRegDir = "..\\Import\\DU\\"; 
  return TotalTaxRegDir;
end;

/**
 @brief Преобразование относительного пути в абсолютный
 @param[in] p_path Путь для преобразования
 @return Абсолютный путь
*/
private macro processPath( p_path )
   var l_str, l_p, l_path = "";

   p_path = trim( p_path );

   if( substr( p_path, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
      p_path = substr( p_path, 3 );

      l_str  = getCWD(false);    
      l_p    = strbrk( l_str, "\\" );
      while( l_p != 0 )
         l_path = l_path + substr( l_str, 1, l_p - 1 ) + "\\";
         l_str  = substr( l_str, l_p + 1 );
         l_p    = strbrk( l_str, "\\" );
      end;
      return ( substr( l_path, 1, strlen( l_path ) - 1 ) + p_path );

   elif( substr( p_path, 1, 1 ) == "." ) /* ссылка на текущий каталог */
      return ( getCWD + substr( p_path, 2 ) );
   end;

  return p_path;
end;

/**
 @brief Получить полный путь к файлу
 @param[in] relativePath путь к файлу
 @param[in] isRemote Флаг терминала
 @return Полный путь к файлу
*/
macro GetFullPath(relativePath:string, isRemote:bool)
   var Path = "";

   if(isRemote)
      if( substr( relativePath, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
         relativePath = substr(relativePath, 3 );
      end;

      if( substr( relativePath, 1, 1 ) == "\\" ) /* ссылка на вышестоящий каталог */
         relativePath = substr(relativePath, 2 );
      end;

      Path = GetCurDir(true) + "\\" + relativePath + "\\"; 
   else
      Path = processPath(relativePath) + "\\";
 
      if( substr( relativePath, 1, 1 ) == "\\" )
         Path = GetCurDir(true) + Path;
      end;
   end;

   return Path;
END;

/**
 @brief Загрузить данные из файла с наименованием dateDU 
 @param[in] dateDU Дата импорта в формате ddmmyyyy
 @param[in] isWeb Флаг веб
*/     
private macro loadData(dateDU, isWeb)
  var objExcel:CExcelPoi = CExcelPoi(), ErrMes = "";
  var ProgressIndicator = CreateProgressIndicator(isWeb);
  
  var PathFiles = GetFullPath(GetDUDir());

  var MaskFile :string = dateDU + "*.xls*";
  var ListFiles:TDirList = TDirList;
  ListFiles.List( PathFiles + MaskFile, "F"); // Перебираем все файлы в каталоге по маске

  DeleteDataDUfromTMP();
  
  if( ListFiles.Count > 0 )
    if( objExcel.Open(PathFiles + ListFiles.Name(0)) )
      if( objExcel.SheetChange("Сделки приобретения") )
        var i = CONST_ROWNUM,
            iLastRow = objExcel.GetLastRowNum() - 1;
        var DUdata = c_DUdata();

        ProgressIndicator.Start(iLastRow - i, "... Обработка листа в файле " + objExcel.GetWorkbookName(), "Обработка листа в файле " + objExcel.GetWorkbookName());
        while( i <= iLastRow )
          IF (GetStr(objExcel.CellValue("B"+string(i))) == "х")
            BREAK;
          END;
          DUdata.Name = GetStr(objExcel.CellValue("C"+string(i)));
          DUdata.ImportID = GetStr(objExcel.CellValue("B"+string(i)));
          DUdata.ISIN = GetStr(objExcel.CellValue("D"+string(i)));
          DUdata.DealDate = getDateFromExcelNum(objExcel.CellValue("E"+string(i)));
          DUdata.TransferDate = getDateFromExcelNum(objExcel.CellValue("F"+string(i)));
          DUdata.ISO = GetStr(objExcel.CellValue("G"+string(i)));
          DUdata.TransferNom = GetDoubleFromValue2(objExcel.CellValue("H"+string(i)));
          DUdata.ExportDUNom = GetDoubleFromValue2(objExcel.CellValue("I"+string(i)));
          DUdata.AmountDU = GetDoubleFromValue2(objExcel.CellValue("J"+string(i)));
          DUdata.PriceNom = GetDoubleFromValue2(objExcel.CellValue("K"+string(i)));
          DUdata.PricePortfolioTransfer = GetDoubleFromValue2(objExcel.CellValue("L"+string(i)));
          DUdata.Sum = GetDoubleFromValue2(objExcel.CellValue("M"+string(i)));
          DUdata.PricePortfolioExportDU = GetDoubleFromValue2(objExcel.CellValue("N"+string(i)));
          DUdata.SumOther = GetDoubleFromValue2(objExcel.CellValue("O"+string(i)));
          DUdata.NKD = GetDoubleFromValue2(objExcel.CellValue("P"+string(i)));

          SaveDataDUtoTMP(DUdata);

          i = i + 1;
          ProgressIndicator.Update(i);
        end;
        ProgressIndicator.Stop();
      end;
      objExcel.Close();
    else
       msgbox("Файлы по маске <",MaskFile,"> отсутствуют");
    end;
  else
    msgbox("Файлы " + MaskFile + " по пути " + PathFiles + " отсутствуют." );
  end;
end;

/**
 @brief Установить идентификатор созданной сделки в таблице импорта
 @param[in] DUID Идентификатор записи в таблице импорта
 @param[in] DealID Идентификатор созданной сделки
*/
private macro setDealID (DUID, DealID)
  myExecSql ("update DIMPOPRDU_TMP set T_DealID = :1 where T_ID = :2 ", makeArray (sqlParam ("1", DealID), sqlParam ("2", DUID)));
end;

private macro createDealsDU (dateDU)
  var msg = "";
  var DealID = -1;
  var OK = false;
  
  var cmd = rsdCommand ("select * from DIMPOPRDU_TMP WHERE T_DealID = 0 ");
  var dataDU = TRsbDataSet(cmd);
  
  while(dataDU.moveNext())
    SP_BeginInsertBoardLot ();
    OK = _CreateBoard (  dataDU.DealCode, 
                         dataDU.DealCode,  
                         2011, 
                         0, 
                         0, 
                         dataDU.AmountDU,  
                         dataDU.PriceNom, 
                         dataDU.PriceNom * dataDU.TransferNom * dataDU.AmountDU / 100,
                         dataDU.NKD,
                         $0.0, 
                         dataDU.DealDate, 
                         time(0,0,0), 
                         dataDU.TransferDate, 
                         time(0,0,0), 
                         dataDU.FIID, 
                         dataDU.FIID, 
                         dataDU.PFI, 
                         4, 
                         1,
                         1,
                         6,
                         2,
                         -1, 
                         dateDU,
                         @msg,
                         @DealID,
                         dataDU.ImportID,
                         dataDU.SumOther);
                         
    SP_EndInsertBoardLot ();
    
    if (OK)
      setDealID(dataDU.ID, DealID);
    end;
  end;
end;

/**
 @brief Получить количество записей в таблице импорта
 @return Количество записей в таблице импорта
*/
private macro getGetCntTempDU 
  var cnt = 0;
  var cmd = rsdCommand ("select count(1) cnt from DIMPOPRDU_TMP ");
  var data = TRsbDataSet(cmd);

  if (data.moveNext())
    cnt = data.cnt;
  end;

  return cnt;
end;

/**
 @brief Подгрузить данные из СОФР
 @param[in] dateDU Дата импорта в формате ddmmyyyy
*/
private macro pumpDataSOFR (dateDU)
  execSQL("BEGIN RSB_IMPORT_DU.pumpDataSOFR (:1); END;", makeArray (sqlParam ("1", dateDU)))
end;

/**
 @brief Обновить данные в СОФР
*/
private macro updateSOFRdeals ()
  SQL_Execute("BEGIN RSB_IMPORT_DU.updateSOFRdeals; END;")
end;

/**
 @brief Установить флаг начала работы процедуры расчета связей
 @return true флаг установлен
 @return false Уже запущена процедура расчета связей
*/
private macro Begin_RunSCTX( )
   VAR rsd = TRsbDataSet( "select t_Message ProcName from DSCTXMES_DBT where T_ID = -2" );

   if( rsd.MoveNext() )
      VAR Mes = "Выполнение запрещено т.к. уже запущена процедура \"" + rsd.ProcName +"\".\nВ случае, если предыдущий запуск процедуры был завершен некорректно, перед следующим запуском необходимо удалить записи в таблице DSCTXMES_DBT.";
      MsgBoxEX( Mes, 0, 0, "Ошибка запуска процедуры", "Ошибка запуска процедуры"  );
      println( Mes );
      return false;
   end;

   return true;
end;

/**
 @brief Сформировать отчет по процедуре загрузки операций
 @param[out] ReportFiles массив файлов для отображения
*/
private macro showReportImportDU (ReportFiles)
  var reportNameImport = GetFullPath("..\\TxtFile\\") + "ImportDU_" + UserNumber + ".txt";
  FILE outFile() txt write;
  
  if( Open( outFile, reportNameImport ))
    SetOutPut(reportNameImport); 

    Var sql = execSqlSelect ("select DIMPOPRDU_TMP.*, count (1) over () t_cnt, (T_PriceNom * T_TransferNom * T_AmountDU / 100) as t_cost, rownum as t_num from DIMPOPRDU_TMP"),
        DS = sql.moveNext ();

    if ( not DS ) 
        [Загруженные записи отсутствуют];
        return; 
    end;

    println ("Загружены / обновлены записи (", int (sql.value ("t_cnt")), " шт.):");
    [┌───────┬──────────┬──────────────────────┬────────────────────────────────────┬────────────┬───────────────┬─────────────┬───────────┐
     │ №     │  DealID  │      Код сделки      │            Название ЦБ             │ Количество │     Сумма     │     НКД     │    Дата   │
     ├───────┼──────────┼──────────────────────┼────────────────────────────────────┼────────────┼───────────────┼─────────────┼───────────┤];
     
    while ( DS )
      [│#######│##########│######################│####################################│############│###############│#############│###########│]
     (Int(sql.value("t_num")):r,sql.value("T_DealID"):r,sql.value("T_DEALCODE"):r,sql.value("T_NAMEAVR"):r,sql.value("T_AmountDU"):r,sql.value("T_cost"):r,sql.value("T_NKD"):r,date(sql.value("t_DealDate")):r);
      DS = sql.moveNext ();
    end;
    [└───────┴──────────┴──────────────────────┴────────────────────────────────────┴────────────┴───────────────┴─────────────┴───────────┘
                                                                                                                              ];
    SetOutput (NULL,TRUE);                                                                                                                           
    Close( reportNameImport);    
  end;

  ReportFiles[0] = CreateLargeFileContainer(ConvertTxt2Html(reportNameImport), true);
end;

/**
 @brief Сформировать отчет по процедуре расчета связей
 @param[out] ReportFiles массив файлов для отображения
*/
private macro showReportTX (ReportFiles)
  var reportNameLinksCalcDU = GetFullPath("..\\TxtFile\\") + "LinksCalcDU_" + UserNumber + ".txt";
  FILE outFile() txt write;
  if( Open( outFile, reportNameLinksCalcDU ))
    SetOutPut(reportNameLinksCalcDU);
    var sql = " SELECT * FROM dsctxmes_dbt WHERE t_ID > 0 order by t_id ";
    var rsd = TRsbDataSet(sql);
    println("Протокол работы расчета связей: ");
    while(rsd.MoveNext())
      println(rsd.t_Message);
    end;
    SetOutput (NULL,TRUE);                                                                                                                           
    Close( reportNameLinksCalcDU);
  end;
  ReportFiles[0] = CreateLargeFileContainer(ConvertTxt2Html(reportNameLinksCalcDU), true);
end;   

/**
 @brief Запустить процедуру расчета связей
 @param[out] ReportFiles массив файлов для отображения
*/
macro CalcTX ()
   var ReportFiles = TArray();
   var ProgressIndicator = CreateProgressIndicator(true);
   if (Begin_RunSCTX())
     ProgressIndicator.Start(1, "Пересчет связей по обработанным данным");
     SQL_Execute("BEGIN RSB_IMPORT_DU.TXCreateLotsForDUImport; END;");
     ProgressIndicator.Update(1);
     ProgressIndicator.Stop();
     showReportTX(ReportFiles);
     SQL_Execute("TRUNCATE TABLE DSCTXMES_DBT");
   end;
   return ReportFiles;
end;

/**
 @brief Запуск загрузки операции вывода из ДУ  
 @param[in] dateDU dateDU Дата импорта в формате ddmmyyyy
 @param[in] isWeb Флаг веб
*/
macro importDU (dateDU, isWeb)
  var ReportFiles = TArray();
  
  if(isWeb)
    ReplaceMacro("MsgBox", "RunError");
  end;
  
  loadData(dateDU, isWeb);
 
  if (getGetCntTempDU() == 0)
    MsgBox("Отсутствуют данные для импорта");
    return 2;
  end;
 
  pumpDataSOFR(dateDU);
  
  updateSOFRdeals();
  
  createDealsDU(dateDU);

  showReportImportDU(ReportFiles);

  return ReportFiles;
end;