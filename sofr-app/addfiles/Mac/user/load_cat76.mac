import bankinter, rsd;
private var nf;

private Macro AddRec2Cat()
  var sql, cmd;

  sql = "declare\n" +
        " cnt pls_integer;\n" + 
        "begin\n" + 
        "  select count(1)\n" + 
        "    into cnt\n" + 
        "   from dobjattr_dbt t\n" + 
        "  where t.t_objecttype = 3\n" + 
        "    and t.t_groupid = 76\n" + 
        "    and t.t_nameobject = '510';\n" + 
        "  if (cnt = 0) then\n" + 
        "     insert into dobjattr_dbt values (3, 76, 11, 0, '510', '510', '510', chr(0), 0, 0, 'небанковские финансовые организации', 'небанковские финансовые организации', to_date('01.01.0001','dd.mm.yyyy'), to_date('01.01.0001','dd.mm.yyyy'), 0, chr(1), chr(1), chr(0));\n" + 
        "  end if;\n" + 
        "end;";
   cmd = RSDCommand(sql);
   cmd.Execute();
end;

private Macro GetPtid(codekind, code)
  var sql, cmd, rs;
  sql = "select t_partyid from dpartcode_dbt where t_codekind = ? and t_code= ?";
  cmd = RSDCommand(sql);
  cmd.AddParam("codekind", RSDBP_IN, codekind);
  cmd.AddParam("code", RSDBP_IN, code);
  rs = RSDRecordSet(cmd);
  if (rs.MoveNext())
    return Int(rs.Value(0));
  end;
  return -1;
end;

private Macro SetCateg(diascode, biscode, inn, categ)
 var sql, cmd, partyid = null;
 sql = "";


 partyId = GetPtid(102, diascode);
 if (partyId == -1)
   partyId = GetPtid(101, biscode);
 end;
 if (partyid == -1)
   println( "Не удалось определить субъекта " + diascode + " " + biscode );
   return false;
 end;

sql = "declare\n" +
      "  ptid dparty_dbt.t_partyid%type := :ptid;\n" + 
      "  cat  varchar2(3) := :cat;\n" + 
      "  obj pls_integer := 3;\n" + 
      "  grp pls_integer := 76;\n" + 
      "begin\n" + 
      "  delete from dobjatcor_dbt where t_objecttype = obj and t_groupid = grp  and t_object = lpad (ptid, 10, '0');\n" + 
      "  insert into dobjatcor_dbt values (obj,\n" + 
      "                                    grp,\n" + 
      "                                    (select t_attrid from dobjattr_dbt where t_objecttype = obj and t_groupid = grp and t_nameobject= cat),\n" + 
      "                                    lpad (ptid, 10, '0'),\n" + 
      "                                    'X',\n" + 
      "                                    to_date ('01010001','ddmmyyyy'),\n" + 
      "                                    1,\n" + 
      "                                    to_date ('31129999', 'ddmmyyyy'),\n" + 
      "                                    to_date ('01010001','ddmmyyyy'),\n" + 
      "                                    to_date ('00:00:00','hh24:mi:ss'),\n" + 
      "                                    chr(0),\n" + 
      "                                    0);\n" + 
      "end;";

 cmd = RSDCommand(sql);
 cmd.AddParam("ptid", RSDBP_IN, partyid);
 cmd.AddParam("cat",   RSDBP_IN, categ);
 cmd.Execute;
 return true;
end;


private Macro ScanSrc(namefile)
  file src() txt;
  var i = 0, cntset = 0;
  SetDelim(";");
  if (not Open(src, namefile))
    Msgbox("Не удается открыть файл " + namefile);
  end;
  rewind(src);
  Initprogress(766, null,  "Установка категории по субъектам");
  next(src);
  while (next(src))
    i = i + 1;
    if ((StrLen(src(3)) > 0 ) and 
       (SetCateg(src(0), src(1), src(2), Trim(src(3)))))
      cntset = cntset + 1;
    end;
    UseProgress(i);
  end;
  Close(src);
  RemProgress();
  Msgbox("Категория эмитента установлена для " + String(cntset) + " субъектов");
  Println("Категория эмитента установлена для " + String(cntset) + " субъектов");
 OnError(err)
  Msgbox(err.Message);
  Close(src);
end;

if (selectFile (nf, mergeFile ("..\\Import\\", "*.csv"))) 
  AddRec2Cat();
  ScanSrc(nf);
  exit(0);
end;

exit(1);