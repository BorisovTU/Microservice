/*BIQ-1291 Загрузка выводов ДС                               */
/*Дорохов А.Н.                                               */
/*Процедура перенумерации операций и поручений на списание ДС*/


import global;
import rsbformsinter;
import "dlquery.mac";
import dlcontrfunc;

//import spcomcalc;
import autoprocess;

private const FT_DATE = 9;
private const FT_INT32 = 1;
private const FT_STR = 7;

macro SendNotification(dt)
  var Eml, EmailText, Subject, v_email_processor;
  
  var err_reg;
  var email_group;
  GetRegistryValue("РСХБ\\ИНТЕГРАЦИЯ\\SEND_NOTIFY_BO", V_INTEGER, email_group, err_reg);
  if (err_reg)
     // MsgBox("Ошибка получение значений настроек \"РСХБ\\ИНТЕГРАЦИЯ\\SEND_NOTIFY_BO\"");
    return 0; 
  end;

      
  Subject = "Уведомление о результатах выполнения процедуры перенумерации выводов ДС за " + dt;
  EmailText = string("Добрый день!",
                     "\nПри выполнении процедуры перенумерации выводов ДС за " + dt + " зафиксированы ошибки. Необходимо ознакомиться с протоколом выполнения процедуры.",
                     "\nДанное письмо было отправлено автоматически.");

  v_email_processor = c_email_proc_env();
  v_email_processor.m_get_email_group(email_group);
  

  v_email_processor.m_set_msg_head(Subject);
  v_email_processor.m_add_row_to_msg_text(EmailText);

  v_email_processor.m_save_to_submit_grp(email_group, true);
  //v_email_processor.m_save_to_submit();

end;

class Logger()

    macro RepMainHeader()
       var str;
       str =     "                             ПРОТОКОЛ ВЫПОЛНЕНИЯ ПЕРЕНУМЕРАЦИИ ОПЕРАЦИЙ ВЫВОДА ДС  \n";
       println(str);                                                                                               
    end;

    macro RepHeader(pDate, pCount)
       var str = "";
       str = str+"\n";
       str = str+"Дата выполнения  : "+date()+"\n";
       str = str+"Время выполнения : "+time()+"\n";
       str = str+"Параметры выполнения: \n";
       str = str+"Перенумеровывать с даты : "+pDate+"         " + "начальное значение счетчика : " + pCount + "\n";
       str = str+"┌─┬──────────────────────────────────┬────────────────────────┬────────────────────────────────┬─────────────────────────────────────────────────────────────────────┬\n";
       str = str+"│ │ Старый номер операции(поручения) │ дата поручения         │ новый номер операции(поручения)│  Результат выполнения                                               │\n";
       str = str+"├─┼──────────────────────────────────┼────────────────────────┼────────────────────────────────┼─────────────────────────────────────────────────────────────────────┤";
       //toRep1(str);                                                                                               
       println(str);                                                                                               
    end;                   

    macro RepFooter(suc, er)                                                       
       var RollBack_str = "";
       var suc_cnt = suc;
       if(er > 0)
         suc_cnt = 0;
         RollBack_str = RollBack_str + "\n";
         RollBack_str = RollBack_str + "Откат обработки\n";
       end;
       var str = "└─┴──────────────────────────────────┴────────────────────────┴────────────────────────────────┴─────────────────────────────────────────────────────────────────────┘\n";
       str = str+"Обработано успешно: " + suc_cnt + "\n";               
       str = str+"Ошибки обработки : " + er + "\n";
       str = str+"Всего            : " + (suc + er) +"\n";
       str = str + RollBack_str;
      return println(str);
    end;

    macro RepStr(p0,p1,p2,p3,p4)
        var str  = "│"+string(p0    :1)   +"│"+
                       string(p1    :34:l)+"│"+
                       string(p2    :24:l)+"│"+
                       string(p3    :32:l)+"│"+
                       string(p4    :69:l)+"│"; 
       return println(str);
    end;
end;

class Shed_Logger()

    macro RepMainHeader()
       var str;
       str =     "                             ПРОТОКОЛ ВЫПОЛНЕНИЯ ПЕРЕНУМЕРАЦИИ ОПЕРАЦИЙ ВЫВОДА ДС  \n";
       return str;                                                                                               
    end;

    macro RepHeader(pDate, pCount)
       var str = "";
       str = str+"\n";
       str = str+"Дата выполнения  : "+date()+"\n";
       str = str+"Время выполнения : "+time()+"\n";
       str = str+"Параметры выполнения: \n";
       str = str+"Перенумеровывать с даты : "+pDate+"         " + "начальное значение счетчика : " + pCount + "\n";
       str = str+"┌─┬──────────────────────────────────┬────────────────────────┬────────────────────────────────┬─────────────────────────────────────────────────────────────────────┬\n";
       str = str+"│ │ Старый номер операции(поручения) │ дата поручения         │ новый номер операции(поручения)│  Результат выполнения                                               │\n";
       str = str+"├─┼──────────────────────────────────┼────────────────────────┼────────────────────────────────┼─────────────────────────────────────────────────────────────────────┤";

       return str;                                                                                               
    end;                   

    macro RepFooter(suc, er)                                                       
       var RollBack_str = "";
       var suc_cnt = suc;
       if(er > 0)
         suc_cnt = 0;
         RollBack_str = RollBack_str + "\n";
         RollBack_str = RollBack_str + "Откат обработки\n";
       end;
       var str = "└─┴──────────────────────────────────┴────────────────────────┴────────────────────────────────┴─────────────────────────────────────────────────────────────────────┘\n";
       str = str+"Обработано успешно: " + suc_cnt + "\n";               
       str = str+"Ошибки обработки : " + er + "\n";
       str = str+"Всего            : " + (suc + er) +"\n";
       str = str + RollBack_str;
      return str;
    end;

    macro RepStr(p0,p1,p2,p3,p4)
        var str  = "│"+string(p0    :1)   +"│"+
                       string(p1    :34:l)+"│"+
                       string(p2    :24:l)+"│"+
                       string(p3    :32:l)+"│"+
                       string(p4    :69:l)+"│"; 
       return str;
    end;
end;

private macro GetLastOrderCount(pDate)
  var sql, cmd, ds;
  sql = String( "select user_NPTXFunc.GetLastClientOrderCount(:pDate) as LastCount from dual");
  cmd = Dl_RSDCommand();
  cmd.AddParam(pDate);
  ds = cmd.Execute(sql);
  if(ds.MoveNext)
    return ds.LastCount;
  end;
end;

private macro GetLastOrderNUM(pDate)
  var sql, cmd, ds;
  sql = String( "select user_NPTXFunc.getLastClientOrderNum(:pDate) as LastNum from dual");
  cmd = Dl_RSDCommand();
  cmd.AddParam(pDate);
  ds = cmd.Execute(sql);
  if(ds.MoveNext)
    return ds.LastNum;
  end;
end;

class (TRsbPanel) NPTXRenumPanel
    var n = 0;
    
    private macro execRenum(pDate, pCount)
      var sql, cmd, ds;
      var suc = 0, er = 0;
      if (pCount <= 1)
        if  (not(GetTRUE (TRUE,"Вы действительно хотите начать нумерацию с номера 1?")))
          return 0;
        end;
      end;
       sql = "declare     \n" +
             "  pDate Date    := :p_Date;   \n" +
             "  pCount Number := :p_Count; \n" +
             "  i Number;                   \n" +
             "begin   \n" + 
             " i := user_NPTXFunc.UpdateOrderXLD(pDate,pCount); \n" +
             "end;    \n";
       cmd = RSDCommand(sql);
       cmd.AddParam("p_Date", rsdbp_in, pDate);
       cmd.AddParam("p_Count", rsdbp_in, pCount);
       cmd.Execute;

       var flog = Logger();
       var dt = StrSubSt(string(Date()),".","");
       var tm = StrSubSt(string(time()),":","");

       var InfoLogShortFileName = "Renum_NPTX_"+dt+"_"+StrSubst(tm," ","0");
       var InfoLogFileName = GetTxtFileName(InfoLogShortFileName);

       SetOutput( InfoLogFileName, true ); //Пишем лог в файл. Начало
       flog.RepMainHeader();
       flog.RepHeader(pDate, pCount);
       sql = "select * from uNPTXRename_tmp  order by T_REGISTRDATETIME asc";
       cmd = DL_RSDCommand;
       ds = cmd.Execute(sql);
       while(ds.movenext())
         flog.RepStr("",ds.CODE,string(date(ds.REGISTRDATETIME), " ", time(ds.REGISTRDATETIME)),ds.new_xld,ds.RENUM_ERR);
         if(ds.RENUM_ERR == "OK")
           suc = suc + 1;
         else
           er = er + 1;
         end;
       end;
       flog.RepFooter(suc,er);

       SetOutput( NULL, true ); //Пишем лог в файл. Конец
       ViewFile( InfoLogFileName );

      return 1;
    end;


    InitTRsbPanel();
    setSize(60,8);
    setPosition(35,15);

    Status("F3 - выбор даты F2,F9 - выполнить");
    Caption("Операция перенумерации операций/поручений списания ДС");
    
    n = 2;
    addLabel(TRsbLabel(2, n, "Процедура перенумерации операций/поручений списания ДС"));

    n = n + 1;
    addLabel(TRsbLabel(2, n, "Параметры операции:"));

    n = n + 1;
    addLabel(TRsbLabel(6, n, "Дата начала:"));
    var p1_DateBeg = TRSBEditField (FT_Date);
    p1_DateBeg.setPosition(26,n);
    p1_DateBeg.setSize(8,1);
    p1_DateBeg.name = "p1_DateBeg";
    p1_DateBeg.value = {CurDate} ;
    p1_DateBeg.enabled = True;
      addControl(p1_DateBeg);

    n = n + 1;
    addLabel(TRsbLabel(6, n, "Номер последнего поручения"));
    var p2_Count:TRsbEditField = TRsbEditField(FT_INT32);
    p2_Count.setPosition(26,n);
    p2_Count.setSize(8,1);
    p2_Count.value = int(GetLastOrderCount(p1_DateBeg.value));
    p2_Count.enabled = true;
    p2_Count.Name = "p2_Count";
      addControl(p2_Count);
    n = n + 1;
    addLabel(TRsbLabel(6, n, "Найденное поручение"));
    var p3_Num:TRsbEditField = TRsbEditField(FT_STR);
    p3_Num.setPosition(26,n);
    p3_Num.setSize(30,1);
    p3_Num.textLength = 30;
    p3_Num.value = GetLastOrderNUM(p1_DateBeg.value);
    p3_Num.enabled = false;
    p3_Num.Name = "p3_Num";
      addControl(p3_Num);
                                             

    addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "Panel_ButtonEvent"));
    addEventHandler(RSB_EV_REMOVE_FOCUS, R2M(this, "Panel_FocusEvent"));

    macro Panel_FocusEvent(RsbEvent)
      if(this.focusedControl.name == "p1_DateBeg")
        this.p2_Count.value = int(GetLastOrderCount(this.focusedControl.value));
        this.p3_Num.value = GetLastOrderNUM(this.focusedControl.value);
      end;
      this.redraw();
    end;
 
    macro Panel_ButtonEvent(RsbEvent)
      if((RsbEvent.keyCode == 316) or (RsbEvent.keyCode == 323))/*F2 F9*/
          if(not(execRenum(p1_DateBeg.value, dl_iif(p2_Count.value < 0, 0, p2_Count.value) + 1 )))
            msgbox("Отказ от выполнения.")
          end;
          this.close(1);
      elif(RsbEvent.keyCode == 317) /**/
        if(this.focusedControl.name == "p1_DateBeg")
          this.focusedControl.value = GetDateByCalendar(this.focusedControl.value);
          this.p2_Count.value = int(GetLastOrderCount(this.focusedControl.value));
          this.p3_Num.value = GetLastOrderNUM(this.focusedControl.value);
        end;
        this.redraw();
      end;
    end;
end;


macro execRenum_shed(dt, result_protocol:@variant/*TStrCollector*/, recs:@integer, errrecs:@integer, errmsg:@string, wrnrecs:@integer)
    recs = 0;
    errrecs = 0;
    wrnrecs = 0; // Пока возвращаем 0
    errmsg = "";
    var suc = 0, er = 0;
    result_protocol.ClearArray;

    var Last_Count;
    var sql, cmd, ds;

    var ShLog = Shed_Logger();

    BegAction(1000, "Выполнение процедуры", false); 

      if((Last_Count = int(GetLastOrderCount(dt))) and  (Last_Count == 0))
        errrecs = 1;
        errmsg = "Ошибка получения номера последнего поручения за предыдущий день";
        return false;
      end;

      sql = "declare     \n" +
            "  pDate Date    := :p_Date;   \n" +
            "  pCount Number := :p_Count; \n" +
            "  i Number;                   \n" +
            "begin   \n" + 
            " i := user_NPTXFunc.UpdateOrderXLD(pDate,pCount); \n" +
            "end;    \n";

      cmd = RSDCommand(sql);
      cmd.AddParam("p_Date", rsdbp_in, dt);
      cmd.AddParam("p_Count", rsdbp_in, Last_Count + 1);
      cmd.Execute;

      sql = "select * from uNPTXRename_tmp  order by T_REGISTRDATETIME asc";
      cmd = DL_RSDCommand;
      ds = cmd.Execute(sql);

      result_protocol.AddString(Shlog.RepMainHeader());
      result_protocol.AddString(Shlog.RepHeader(dt, Last_Count+1));

      while(ds.movenext())
        result_protocol.AddString(Shlog.RepStr("",ds.CODE,string(date(ds.REGISTRDATETIME), " ", time(ds.REGISTRDATETIME)),ds.new_xld,ds.RENUM_ERR));
        if(ds.RENUM_ERR == "OK")
          suc = suc + 1;
        else
          er = er + 1;
        end;
      end;

     result_protocol.AddString(Shlog.RepFooter(suc,er));
     errrecs = er;
     if(errrecs == 0)
       recs = suc;
     else
       SendNotification(dt);
     end;
     EndAction;
     return true;
end;


  var panel = NPTXRenumPanel;
  panel.run;
  exit(1);


