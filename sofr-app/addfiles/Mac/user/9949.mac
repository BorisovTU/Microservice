
import RSD;
import dlmisc; 
import dlquery;

private const OPEN_DIR = "..\\Import\\";
private const  OPEN_DIALOG_HEADER = "Выберите файл для загрузки "; 
private const  FILE_MASK = "cft_???_???_*.csv";

private const old_CorSchem = 602;
private const new_CorSchem = 642;
var Mlog;
var file_pos     = 0;
var err_count    = 0;
var m_rsd_count  = 0;
var pm_rsd_count = 0;
var rq_rsd_count = 0;

class Logger()

//1.
//выводить в протокол сообщение: "Не найден старый филиал с БИК = " и поле файла "БИК старый";
//                               "Не найден новый филиал с БИК = " и поле файла "БИК новый";
    var M_Item_ar         = TArray;
    private class cl_M_Item (_fpos, _err_msg)
    	var fpos    = _fpos;
    	var err_msg = _err_msg;
    end;
    macro Add_M_Item (_fpos, _err_msg)
    	M_Item_ar(M_Item_ar.size) = cl_M_Item (_fpos, _err_msg);
    end;


//2.
//выводить в протокол код клиента платёжной инструкции, номер платёжной ин-струкции, старый номер счёта, новый номер счёта и текст: "Платёжная инструкция успешно обновлена";
//выводить в протокол значения полей загрузочного файла "Идентификатор клиента в ЦФТ",  "ФИО Клиента" и текст: "Ошибка. Не найдены платёжные инструкции старого филиала для клиента";
    var SetAcc_UpdItem_ar = TArray;
	private class cl_SetAcc_UpdItem (_fpos, _cft_cod, _fio, _settacc_num, _old_acc, _new_acc, _err_msg)
		var fpos = _fpos;
		var cft_cod = _cft_cod;
		var fio = _fio;
		var settacc_num = _settacc_num;
		var old_acc = _old_acc;
		var new_acc = _new_acc;
		var err_msg = _err_msg;
	end;
	macro Add_SetAcc_UpdItem (_fpos, _cft_cod, _fio, _settacc_num, _old_acc, _new_acc, _err_msg)
    	SetAcc_UpdItem_ar(SetAcc_UpdItem_ar.size) = cl_SetAcc_UpdItem (_fpos, _cft_cod, _fio, _settacc_num, _old_acc, _new_acc, _err_msg);
    end;

//3.
//выводить в протокол код клиента-получателя (t_Reveiver), идентификатор платежа, старый номер счёта, новый номер счёта и текст: "Платёж успешно обновлён";
    var PM_UpdItem_ar     = TArray;
	private class cl_PM_UpdItem(_fpos, _Receiver, _PAymID, _old_acc, _new_acc, _err_msg)
		var fpos = _fpos;
		var Receiver = _Receiver;
		var PAymID   = _PAymID;
		var old_acc  = _old_acc;
		var new_acc  = _new_acc;
		var err_msg  = _err_msg;
	end;
	macro Add_PM_UpdItem (_fpos, _Receiver, _PAymID, _old_acc, _new_acc, _err_msg)
    	PM_UpdItem_ar(PM_UpdItem_ar.size) = cl_PM_UpdItem (_fpos, _Receiver, _PAymID, _old_acc, _new_acc, _err_msg);
    end;
    

//4.
//выводить в протокол поля "Идентификатор клиента в ЦФТ",  "ФИО Клиента", код операции списания денежных средств, старый номер счёта, новый номер счёта и текст: "Требование-обязательство успешно обновлено"
    var RQ_UpdItem_ar     = TArray;
	private class cl_RQ_UpdItem (_fpos,_cftid,_fio,_opnumber,_old_acc,_new_acc,_err_msg)
		var fpos = _fpos;
		var cftid = _cftid;
		var fio = _fio;
		var opnumber = _opnumber;
		var old_acc = _old_acc;
		var new_acc = _new_acc;
		var err_msg = _err_msg;
	end;
	macro Add_RQ_UpdItem (_fpos,_cftid,_fio,_opnumber,_old_acc,_new_acc,_err_msg)
    	RQ_UpdItem_ar(RQ_UpdItem_ar.size) = cl_RQ_UpdItem (_fpos,_cftid,_fio,_opnumber,_old_acc,_new_acc,_err_msg);
    end;


    var InfoLogFileName;


    macro RepHeader()
       var str;
       str =     "                   ПРОТОКОЛ ПРОЦЕДУРЫ ЗАМЕНЫ РЕКВИЗИТОВ ФИЛИАЛА В ПЛАТЕЖНЫХ ИНСТРУКЦИЯХ  \n";
       str = str+"\n";
       str = str+"Дата выполнения  : "+date()+"\n";
       str = str+"Время выполнения : "+time()+"\n";

       println(str);                                                                                                   
    end;
            

//1. Проверка филиалов    
    private macro print_m_block_header()
       var str;	
       str =     " 1.Проверка филиалов:  \n";
       str = str+"┌──────┬──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┐\n";
       str = str+"│ стр. │   Ошибки проверки                                                                                                                                                                                    │\n";
       str = str+"├──────┼──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤";
       return println(str);
    end;

    private macro print_m_block_footer()
       var str = "└──────┴──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┘\n";
       return println(str);
    end;

    private macro print_m_block_RepStr(p0, p1)
        var str = "";
        str  = str + "│" + string(p0   :6)     +"│"+
                           string(p1   :198:l) +"│";
        return println(str);
    end;

    private macro print_m_block()
        var item;
    	if(M_Item_ar.size > 0)
    		print_m_block_header();
    		for (item, M_Item_ar)
				print_m_block_RepStr(item.fpos, item.err_msg);
			end;
    		print_m_block_footer();
    	end;

    end;


//2. Обновление платежных инструкций
    private macro print_SetAcc_Upd_header()
       var str;	
       str =     " 2.Обновление платежных инструкций:  \n";
       str = str+"┌──────┬──────────────┬───────────────────────────────┬──────────┬──────────────────────────┬─────────────────────┬───────────────────────────────────────────────────────────────────────────────────────────┐\n";
       str = str+"│ стр. │ Код ЦФТ      │ ФИО                           │ номер ПИ │    Старый счет           │ Новый счет          │ Результат                                                                                 │\n";
       str = str+"├──────┼──────────────┼───────────────────────────────┼──────────┼──────────────────────────┼─────────────────────┼───────────────────────────────────────────────────────────────────────────────────────────┤";
       return println(str);
    end;
    
    private macro print_SetAcc_Upd_footer()
       var str = "└──────┴──────────────┴───────────────────────────────┴──────────┴──────────────────────────┴─────────────────────┴───────────────────────────────────────────────────────────────────────────────────────────┘\n";
       return println(str);
    end;

    private macro print_SetAcc_Upd_RepStr(p0, p1, p2, p3, p4, p5, p6)
        var str = "";
        str  = str + "│"+ string(p0   :6)     +"│"+
        				  string(p1   :14)     +"│"+
        				  string(p2   :31)     +"│"+
        				  string(p3   :10)     +"│"+
        				  string(p4   :26)     +"│"+
        				  string(p5   :21)     +"│"+
                          string(p6   :91:l) +"│";
        return println(str);
    end;

    private macro print_SetAcc_Upd_block()
        var item;
    	if(SetAcc_UpdItem_ar.size > 0)
    		print_SetAcc_Upd_header();
    		for (item, SetAcc_UpdItem_ar)
				print_SetAcc_Upd_RepStr(item.fpos, item.cft_cod, item.fio, item.settacc_num, item.old_acc, item.new_acc, item.err_msg);
			end;
    		print_SetAcc_Upd_footer();
    	end;

    end;


//3. Обновление необработанных платежей
//выводить в протокол код клиента-получателя (t_Reveiver), идентификатор платежа, старый номер счёта, новый номер счёта и текст: "Платёж успешно обновлён";
    private macro print_PM_Upd_header()
       var str;	
       str =     " 3. Обновление необработанных платежей:  \n";
       str = str+"┌──────┬──────────────────┬────────────────┬─────────────────────────┬─────────────────────────┬──────────────────────────────────────────────────────────────────────────────────────────────────────────────┐\n";
       str = str+"│ стр. │ Код получателя   │  ID платежа    │   Старый номер счета    │    Новый номер счета    │    Результат                                                                                                 │\n";
       str = str+"├──────┼──────────────────┼────────────────┼─────────────────────────┼─────────────────────────┼──────────────────────────────────────────────────────────────────────────────────────────────────────────────┤";
       return println(str);
    end;
    
    private macro print_PM_Upd_footer()
       var str = "└──────┴──────────────────┴────────────────┴─────────────────────────┴─────────────────────────┴──────────────────────────────────────────────────────────────────────────────────────────────────────────────┘\n";
       return println(str);
    end;

    private macro print_PM_Upd_RepStr(p0, p1, p2, p3, p4, p5)
        var str = "";
        str  = str + "│"+ string(p0   :6)     +"│"+
        				  string(p1   :18)     +"│"+
        				  string(p2   :16)     +"│"+
        				  string(p3   :25)     +"│"+
        				  string(p4   :25)     +"│"+
                          string(p5   :110:l) +"│";
        return println(str);
    end;

    private macro print_PM_Upd_block()
        var item;
    	if(PM_UpdItem_ar.size > 0)
    		print_PM_Upd_header();
    		for (item, PM_UpdItem_ar)
				print_PM_Upd_RepStr(item.fpos,item.Receiver,item.PAymID,item.old_acc,item.new_acc,item.err_msg);
			end;
    		print_PM_Upd_footer();
    	end;

    end;


//4. Обновление платежных реквизитов
//выводить в протокол поля "Идентификатор клиента в ЦФТ",  "ФИО Клиента", код операции списания денежных средств, старый номер счёта, новый номер счёта и текст: "Требование-обязательство успешно обновлено"
    private macro print_RQ_Upd_header()
       var str;	
       str =     " 4. Обновление платежных реквизитов ТО:  \n";
       str = str+"┌──────┬───────────────┬─────────────────────────┬────────────┬─────────────────────────┬─────────────────────────┬───────────────────────────────────────────────────────────────────────────────────────────┐\n";
       str = str+"│ стр. │ Код ЦФТ       │  ФИО                    │ Код операц.│   Старый номер счета    │    Новый номер счета    │    Результат                                                                              │\n";
       str = str+"├──────┼───────────────┼─────────────────────────┼────────────┼─────────────────────────┼─────────────────────────┼───────────────────────────────────────────────────────────────────────────────────────────┤";
       return println(str);
    end;
    
    private macro print_RQ_Upd_footer()
       var str = "└──────┴───────────────┴─────────────────────────┴────────────┴─────────────────────────┴─────────────────────────┴───────────────────────────────────────────────────────────────────────────────────────────┘\n";
       return println(str);
    end;

    private macro print_RQ_Upd_RepStr(p0, p1, p2, p3, p4, p5, p6)
        var str = "";
        str  = str + "│"+ string(p0   :6)     +"│"+
        				  string(p1   :15)     +"│"+
        				  string(p2   :25)     +"│"+
        				  string(p3   :12)     +"│"+
        				  string(p4   :25)     +"│"+
        				  string(p5   :25)     +"│"+
                          string(p6   :91:l) +"│";
        return println(str);
    end;

    private macro print_RQ_Upd_block()
        var item;
    	if(RQ_UpdItem_ar.size > 0)
    		print_RQ_Upd_header();
    		for (item, RQ_UpdItem_ar)
				print_RQ_Upd_RepStr( item.fpos, item.cftid, item.fio, item.opnumber, item.old_acc, item.new_acc, item.err_msg);
			end;
    		print_RQ_Upd_footer();
    	end;

    end;

    macro RepFooter()                                                       
       var str = "";
       
       str = str+"Обновлено платежных инструкций: " + m_rsd_count + "\n";               
       str = str+"Обновлено платежей: " + pm_rsd_count + "\n";               
       str = str+"Обновлено требований-обязательств : " + rq_rsd_count + "\n";
       str = str+"Количество ошибок : " + err_count + "\n";
       str = str+"Всего обработано строк: " + file_pos + "\n";               
       
      println(str);
    end;

    macro RepStr(p0, p1, p2)
        if(p2 == "")
           p2 = "OK"
        end;
       var str = "", a;
       var ar = StrSplit2(p2, 160, true);
       var i = 0;
       while (i < ar.size)
         if (i >=1)
           str = str + "\n";
           p0 = "";
           p1 = "";
         end;
         str  = str + "│"+string(p0   :6)   +"│"+
                          string(p1    :34:l)+"│"+
                          string(ar(i)    :163:l)+"│";
         i = i + 1;
      end;
       return println(str);
    end;

    macro init()
    	var dt = StrSubSt(string(Date()),".","");
    	var tm = StrSubSt(string(time()),":","");
    	InfoLogFileName = GetTxtFileName( "!LOG_BRANCH_MIGRATION"+dt+"_"+StrSubst(tm," ","0") );
    	SetOutput( InfoLogFileName, true ); //Пишем лог в файл. Начало
    end;

    macro print()
    	RepHeader();
    	print_m_block();
    	print_SetAcc_Upd_block();
    	print_PM_Upd_block();
    	print_RQ_Upd_block();
        RepFooter();

        SetOutput( NULL, true ); //Пишем лог в файл. Конец
        ViewFile( InfoLogFileName );
    end;

end;


macro loadAccessFile(errmsg:@string)

    private macro Check_table()
		var sql = String( "DECLARE\n"
		                 ,"   retval    NUMBER := 0;\n" 
		                 ,"   sql_crt   VARCHAR2 (1000);\n" 
		                 ,"   tbl_name  VARCHAR2(20) := 'u_biq_9949_tmp';\n" 
		                 ,"BEGIN\n" 
		                 ,"   sql_crt := 'CREATE GLOBAL TEMPORARY TABLE '|| tbl_name || chr(10) ||\n" 
		                 ,"           '   (t_id number(7),T_CODE  VARCHAR2(20),' || chr(10) ||\n" 
		                 ,"           '    T_PARTYNAME VARCHAR2(100),' || chr(10) ||\n" 
		                 ,"           '    T_NUMBER VARCHAR2(50),' || chr(10) ||\n" 
		                 ,"           '    T_DATEBEGIN DATE,' || chr(10) ||\n" 
		                 ,"           '    T_ACCOUNT_OLD VARCHAR2(20),' || chr(10) ||\n" 
		                 ,"           '    OLD_BIC VARCHAR2(13),' || chr(10) ||\n" 
		                 ,"           '\t  T_ACCOUNT_NEW VARCHAR2(20),' || chr(10) ||\n" 
		                 ,"           '\t  NEW_BIC VARCHAR2(13),' || chr(10) ||\n" 
		                 ,"           '\t  ERRMSG VARCHAR2(200))' || chr(10) ||\n" 
		                 ,"           'ON COMMIT PRESERVE ROWS';\n" 
		                 ,"   SELECT COUNT (1)\n" 
		                 ,"     INTO retval\n" 
		                 ,"     FROM user_tables\n" 
		                 ,"    WHERE UPPER (table_name) = upper(tbl_name);\n" 
		                 ,"   IF retval = 0\n" 
		                 ,"   THEN\n" 
		                 ,"      EXECUTE IMMEDIATE (sql_crt);\n" 
		                 ,"   ELSE \n"
		                 ,"      EXECUTE IMMEDIATE ('DELETE FROM ' || tbl_name); \n"
		                 ,"   END IF;\n" 
		                 ,"END;");
		var cmd = RSDCommand(sql);
   			cmd.execute;
    end;

    private macro Fill_Table(_FullFileName)
 	    FILE inFile() txt;
    	var file_line:string, file_line_fields = TArray;
    	var CFT_CODE, PARTYNAME, SFNUMBER, DATEBEGIN, ACCOUNT_OLD, OLD_BIC, ACCOUNT_NEW, NEW_BIC, ERRMSG;
    	var sql_str, cmd;
    	var ErrText;

    	if(Open(inFile, _FullFileName, "rsansi"))
    	    var n = 0;
    		SetDelim (";", true);
			while(next(inFile))

			    n = n + 1;
			    CFT_CODE      = inFile(0);  // 1 
			    PARTYNAME     = inFile(1);  // 2 
			    SFNUMBER      = inFile(2);  // 3 
			    DATEBEGIN     = inFile(3);  // 4 
			    ACCOUNT_OLD   = inFile(4);  // 5 
			    OLD_BIC       = inFile(5);  // 6 
			    ACCOUNT_NEW   = inFile(6);  // 7 
			    NEW_BIC       = inFile(7);  // 8 
			    ERRMSG        = inFile(8);  // 9 

				sql_str = String(
				                 "declare\n" 
					                 ,"DATEBEGIN DATE;\n"
				                 "begin\n" 
					                 "begin\n" 
						                 ,"DATEBEGIN:=to_date(:pDATEBEGIN,'DD.MM.YYYY');\n"
					                 ,"exception\n"
						                 ,"when others then DATEBEGIN:=trunc(sysdate);\n"
					                 ,"end;\n"
					                 ,"insert into u_biq_9949_tmp\n"
				    	             ,"  (t_id, T_CODE,\n" 
					                 ,"   T_PARTYNAME,\n" 
					                 ,"   T_NUMBER,\n" 
					                 ,"   T_DATEBEGIN,\n" 
					                 ,"   T_ACCOUNT_OLD,\n" 
					                 ,"   OLD_BIC,\n" 
					                 ,"   T_ACCOUNT_NEW,\n" 
					                 ,"   NEW_BIC,\n" 
					                 ,"   ERRMSG)\n" 
					                 ,"values\n" 
					                 ,"  (:id, :pCFTCODE,\n" 
					                 ,"   :pPARTYNAME,\n" 
					                 ,"   :pSFNUMBER,\n" 
					                 ,"   DATEBEGIN,\n" 
					                 ,"   :pACCOUNT_OLD,\n" 
					                 ,"   :pOLD_BIC,\n" 
					                 ,"   :pACCOUNT_NEW,\n" 
					                 ,"   :pNEW_BIC,\n" 
					                 ,"   :pERRMSG);\n"
				                 ,"end;"
				                 );
			    cmd = RSDCommand(sql_str);
			    cmd.AddParam("pDATEBEGIN", 		RSDBP_IN, DATEBEGIN);
			    cmd.AddParam("ID", 		        RSDBP_IN, n);
			    cmd.AddParam("pCFTCODE", 		RSDBP_IN, CFT_CODE);
			    cmd.AddParam("pPARTYNAME", 		RSDBP_IN, PARTYNAME);
			    cmd.AddParam("pSFNUMBER", 		RSDBP_IN, SFNUMBER);
			    cmd.AddParam("pACCOUNT_OLD", 	RSDBP_IN, ACCOUNT_OLD);
			    cmd.AddParam("pOLD_BIC", 		RSDBP_IN, OLD_BIC);
			    cmd.AddParam("pACCOUNT_NEW", 	RSDBP_IN, ACCOUNT_NEW);
			    cmd.AddParam("pNEW_BIC", 		RSDBP_IN, NEW_BIC);
			    cmd.AddParam("pERRMSG", 		RSDBP_IN, ERRMSG);
			    cmd.execute;
			end;
		//runscroll(RSDRecordset("Select * from u_biq_9949_tmp order by t_id asc", rsdval_client, rsdval_static))	;
		end;    

    end;

    private macro Process_Data(_ds)

		private macro GetPartyidByBic(pBIC)
		  var partyid = 0; 
		  var sql = String( "select objcode.t_objectid\n" 
		                   ,"  from dobjcode_dbt objcode\n"  
		                   ," where objcode.t_objecttype = 3\n"  
		                   ,"   and objcode.t_codekind = 3\n"  
		                   ,"   and objcode.t_code = ?");
		  var cmd = Dl_RsdCommand(sql);
		      cmd.AddParam(pBIC);
		  var rs = cmd.execute;
		  if(rs.movenext)
		    partyid = rs.objectid;
		  end;
		  return partyid;
		end;

		private macro getPartyNameByCFTCode(_CFTCode)
			var SName = "";
			var sql = String( "select party.t_shortname\n" 
			                 ,"  from dobjcode_dbt objcode, dparty_dbt party\n"  
			                 ," where objcode.t_objecttype = 3\n"  
			                 ,"   and objcode.t_codekind = 101\n"  
			                 ,"   and party.t_partyid = objcode.t_objectid\n"  
			                 ,"   and objcode.t_code = ?");
			var cmd = DL_RSDCommand(sql);
			    cmd.AddParam(_CFTCode);
			var rs = cmd.Execute();
			if(rs.MoveNext())
			  SName = rs.ShortName;
			end;
			return SName;
		end;

		private macro getNptxOpNumByRQID(_rqid)
			var OPCode = "";
			var sql = String( "select nptxop.t_code\n" 
			                 ,"  from dnptxop_dbt nptxop, ddlrqacc_dbt rq\n"  
			                 ," where rq.t_dockind = nptxop.t_dockind\n"  
			                 ,"   and rq.t_docid = nptxop.t_id\n"  
			                 ,"   and nptxop.t_dockind = 4607\n"  
			                 ,"   and nptxop.t_kind_operation = 2037\n"  
			                 ,"   and nptxop.t_subkind_operation = 20\n"  
			                 ,"   and rq.t_id = ?");
			var cmd = DL_RSDCommand(sql);
			    cmd.AddParam(_rqid);
			var rs = cmd.Execute();
			if(rs.MoveNext())
			  OPCode = rs.Code;
			end;
			return OPCode;
		end;

		private macro SetRQPlace(p_new_place, p_old_place, p_dockind, p_docid)
		  var cmd = "update  dnptxop_dbt set t_place = ?    \n"+
		            "  WHERE    t_place = ? and  t_dockind = ? and t_id = ? \n"+
		            "        AND t_kind_operation = 2037    \n"+
		            "        AND t_subkind_operation = 20  \n";
		      cmd = RSDCommand(cmd);
		      cmd.AddParam("new_place",   rsdbp_in, p_new_place);
		      cmd.AddParam("old_place",   rsdbp_in, p_old_place);
		      cmd.AddParam("dockind", rsdbp_in, p_dockind);
		      cmd.AddParam("id",      rsdbp_in, p_docid);
		      cmd.execute;
		      cmd.cmdText = "update ddlrq_dbt set t_placeid = ? where t_placeid = ? and t_dockind = ? and t_docid = ? ";
		      cmd.execute;
		end;	

		var ID_OldBranch, ID_NewBranch, ID_CorrBranch;
		var OldBranch, NewBranch, NewCorrBranch;
		var m_sql, m_cmd, m_rsd;
		var pm_sql, pm_cmd, pm_rsd;
		var prop_sql, prop_cmd, prop_rsd;
		var rmprop_sql, rmprop_cmd, rmprop_rsd;
		var crprop_sql, crprop_cmd, crprop_rsd;
		var rq_sql, rq_cmd, rq_rsd;

		class cl_BranchData(_BranchID)
			var Branch_ID = _BranchID;
			var Branch_Name = "";
			var Branch_CRSA_Acc = "";
			var Branch_CRSA_BIC = "";
			private var cmd, ds;

			cmd = DL_RSDCommand	("select t_name from dparty_dbt where t_partyid = ?");
			cmd.AddParam(_BranchID);
			ds = cmd.Execute();
			if(ds.MoveNext())
				Branch_Name = ds.Name;
			end;

			cmd = Dl_RsdCommand( "select accnt.t_accnt, accnt.t_accntcbrbic \n " +
                                 "  from dbankdprtaccnt_dbt accnt \n " + 
                                 " where accnt.t_partyid = ? \n " + 
                                 "   and accnt.t_regaccnttype = 'CRSA' \n " + 
                                 "   and accnt.t_accntstatus = 'ACAC'");
			cmd.AddParam(_BranchID);
			ds = cmd.Execute();
			if(ds.MoveNext)
				Branch_CRSA_Acc = ds.accnt;
				Branch_CRSA_BIC = ds.accntcbrbic;
			end;
		end;

		m_sql = "Select * from dsettacc_dbt where t_bankid = ? and t_account = ?";

    	pm_sql = String( "select pmp.* \n" 
			            ,"  from dpmpaym_dbt pmp \n"  
			            ," where pmp.t_paymStatus not in (150, 3010, 32000) \n"  
			            ,"   and pmp.t_receiverbankid = ? \n"  
			            ,"   and pmp.t_receiveraccount = ? ");

		prop_sql = "select prop.* from dpmprop_dbt prop where prop.t_paymentid = ?";

    	rmprop_sql = "select prop.* from dpmrmprop_dbt prop where prop.t_paymentid = ?";

    	crprop_sql = "select prop.* from dpmprop_dbt prop where prop.t_paymentid = ? and prop.t_debetcredit = 1";

    	rq_sql = String( "select rqacc.* \n" 
		                ,"  from ddlrqacc_dbt rqacc \n"  
        		        ," where rqacc.t_id in (select rq.t_id \n"  
                		,"                        from dnptxop_dbt nptxop, ddlrqacc_dbt rq, ddlrq_dbt dlrq \n"  
                 		,"                       where rq.t_dockind = nptxop.t_dockind \n"  
                 		,"                         and rq.t_docid = nptxop.t_id \n"  
                 		,"                         and nptxop.t_dockind = 4607 \n"  
                 		,"                         and nptxop.t_kind_operation = 2037 \n"  
                 		,"                         and nptxop.t_subkind_operation = 20 \n"  
                 		//,"                         and nptxop.t_status = 1 \n"  
                 		,"                         and dlrq.t_docid = rq.t_docid \n"  
                 		,"                         and dlrq.t_state != 2 \n"  
                 		,"                         and nptxop.t_dockind = dlrq.t_dockind \n"  
                 		,"                         and rq.t_bankid = ? \n"  
                 		,"                         and rq.t_account = ? )");

    	while (_ds.MoveNext())
 			file_pos = file_pos + 1;
    		ID_OldBranch = GetPartyidByBic(_ds.OLD_BIC);
    		if(ID_OldBranch == 0)
    		  // прерываем обработку строки выводим в протокол ошибку "Не найден старый филиал с БИК = " и поле файла "БИК старый";
    		  MLog.Add_M_Item(file_pos, "Не найден старый филиал с БИК = " + _ds.OLD_BIC);
    		  err_count = err_count + 1;
    		  CONTINUE; //переходим дальше по циклу
    		end;
    		OldBranch = cl_BranchData(ID_OldBranch);

    		ID_NewBranch = GetPartyidByBic(_ds.NEW_BIC);
    		if(ID_NewBranch == 0)
    		  // прерываем обработку строки выводим в протокол ошибку "Не найден новый филиал с БИК = " и поле файла "БИК новый";
    		  MLog.Add_M_Item(file_pos, "Не найден новый филиал с БИК = " + _ds.NEW_BIC);
    		  err_count = err_count + 1;
    		  CONTINUE; //переходим дальше по циклу
    		end;
    		NewBranch = cl_BranchData(ID_NewBranch);

    		ID_CorrBranch = GetPartyidByBic(NewBranch.Branch_CRSA_BIC);
    		NewCorrBranch = cl_BranchData(ID_CorrBranch);

// 2. Обработка платежный инстукции
    		m_cmd = RSDCommand(m_sql);
    		m_cmd.AddParam("bankid",  RSDBP_IN, OldBranch.Branch_ID);
    		m_cmd.AddParam("account", RSDBP_IN, _ds.ACCOUNT_OLD);
    		m_rsd = RSDRecordset(m_cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
    		var m_rsd_count_tmp = 0;
    		while(m_rsd.MoveNext())
		    	if ((ValType(_ds.ACCOUNT_NEW) != 0) and (_ds.ACCOUNT_NEW != " "))
	    			m_rsd_count = m_rsd_count + 1;
	    			m_rsd_count_tmp = m_rsd_count_tmp + 1;
	    			m_rsd.Edit();
		    		 m_rsd.value("t_bankid")  = NewBranch.Branch_ID;
		    		 m_rsd.value("t_account") = _ds.ACCOUNT_NEW;
		    		 m_rsd.value("t_BankCode") = _ds.NEW_BIC;
		    		 if(m_rsd.value("t_CorrAcc") != "")
		    			m_rsd.value("t_CorrAcc") = NewBranch.Branch_CRSA_Acc;
		    			m_rsd.value("t_BankCorrCode") = NewBranch.Branch_CRSA_BIC;
		    			m_rsd.value("t_bankcorrid") = ID_CorrBranch;
		    			m_rsd.value("t_BankCorrName") = NewCorrBranch.Branch_Name;
		    		 end;
		    		 m_rsd.value("t_BankName") = NewBranch.Branch_Name;
	    			m_rsd.Update();
	    			// выводим в протокол протокол код клиента платёжной инструкции, номер платёжной инструкции, ста-рый номер счёта, новый номер счёта и текст: "Платёжная инструкция успешно об-новлена";
	    			MLog.Add_SetAcc_UpdItem(file_pos, _ds.Code, _ds.PARTYNAME, m_rsd.value("T_SETTACCID"), _ds.ACCOUNT_OLD, _ds.ACCOUNT_NEW, "Платёжная инструкция успешно обновлена");
		    	else
		    		// если новый счёт не заполнен, то такую запись файла не обрабатывать, а в протокол выдавать сообщение "Не задан новый счёт".
	    			MLog.Add_SetAcc_UpdItem(file_pos, _ds.Code, _ds.PARTYNAME, m_rsd.value("T_SETTACCID"), _ds.ACCOUNT_OLD, "", "Не задан новый счёт");
    				err_count = err_count + 1;
                                m_rsd_count_tmp = -1;
		    	end;

    		end;
    		if(m_rsd_count_tmp == 0)
    			//выводить в протокол значения полей загрузочного файла "Идентификатор клиента в ЦФТ",  "ФИО Клиента" и текст: "Ошибка. Не найдены платёжные инструкции старого филиала для клиента";
    			MLog.Add_SetAcc_UpdItem(file_pos, _ds.Code, _ds.PARTYNAME, "", "", "", "Ошибка. Не найдены платёжные инструкции старого филиала для клиента");
    			err_count = err_count + 1;
    		end;

// 3. Обработка платежей
    		pm_cmd = RSDCommand(pm_sql);
    		pm_cmd.AddParam("receiverbankid",  RSDBP_IN, OldBranch.Branch_ID);
    		pm_cmd.AddParam("receiveraccount", RSDBP_IN, _ds.ACCOUNT_OLD);
    		pm_rsd = RSDRecordset(pm_cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
    		
    		while(pm_rsd.MoveNext())
		    	if ((ValType(_ds.ACCOUNT_NEW) != 0) and (_ds.ACCOUNT_NEW != " "))
	    			pm_rsd_count = pm_rsd_count + 1;
	    			pm_rsd.Edit();
		    		 pm_rsd.value("t_ReceiverBankID")  = NewBranch.Branch_ID;
		    		 pm_rsd.value("t_ReceiverAccount") = _ds.ACCOUNT_NEW;
		    		pm_rsd.Update();

	   				prop_cmd = RSDCommand(prop_sql);
	    			prop_cmd.AddParam("paymentid",  RSDBP_IN, pm_rsd.value("T_PAYMENTID"));
		    		prop_rsd = RSDRecordset(prop_cmd, RSDVAL_CLIENT, RSDVAL_STATIC); 
		    		while(prop_rsd.MoveNext())
		    			prop_rsd.Edit();
		    			 if(prop_rsd.value("t_DebetCredit") == 1)
		    			 	prop_rsd.value("t_BankCode") = _ds.NEW_BIC;
		    			 end;            
		    			prop_rsd.Update();
		    		end;

   				rmprop_cmd = RSDCommand(rmprop_sql);
	    			rmprop_cmd.AddParam("paymentid",  RSDBP_IN, pm_rsd.value("T_PAYMENTID"));
		    		rmprop_rsd = RSDRecordset(rmprop_cmd, RSDVAL_CLIENT, RSDVAL_STATIC); 
		    		while(rmprop_rsd.MoveNext())
		    			 rmprop_rsd.value("t_ReceiverCorrAccNostro") = NewBranch.Branch_CRSA_Acc;
		    			 rmprop_rsd.value("t_ReceiverBankName") = NewBranch.Branch_Name;
		    			 rmprop_rsd.value("t_ReceiverCorrBankName") = NewCorrBranch.Branch_Name;
		    			 rmprop_rsd.Update();
		    		end;

   				crprop_cmd = RSDCommand(crprop_sql);
	    			crprop_cmd.AddParam("paymentid",  RSDBP_IN, pm_rsd.value("T_PAYMENTID"));
		    		crprop_rsd = RSDRecordset(crprop_cmd, RSDVAL_CLIENT, RSDVAL_STATIC); 
		    		while(crprop_rsd.MoveNext())
		    			 crprop_rsd.value("t_CorrAcc") = NewBranch.Branch_CRSA_Acc;
		    			 crprop_rsd.value("t_CorrCodeKind") = 3;
		    			 crprop_rsd.value("t_CorrCodeName") = "БИК";
		    			 crprop_rsd.value("t_CorrCode") = NewBranch.Branch_CRSA_BIC;
                                         crprop_rsd.value("t_corschem") = new_CorSchem;
		    			 crprop_rsd.Update();
		    		end;


		    		//выводим в протокол код клиента-получателя (t_Reveiver), идентификатор платежа, старый номер счёта, новый номер счёта и текст: "Платёж успешно обновлён";
		    		MLog.Add_PM_UpdItem(file_pos, pm_rsd.value("t_Receiver") , pm_rsd.value("T_PAYMENTID"), _ds.ACCOUNT_OLD, _ds.ACCOUNT_NEW, "Платёж успешно обновлён");
		    	else
		    		// если новый счёт не заполнен, то такую запись файла не обрабатывать, а в протокол выдавать сообщение "Не задан новый счёт".
		    		MLog.Add_PM_UpdItem(file_pos, pm_rsd.value("t_Receiver") , pm_rsd.value("T_PAYMENTID"), _ds.ACCOUNT_OLD, "", "Не задан новый счёт");
    				err_count = err_count + 1;
		    	end;
    		end;

// 4. Обработка платежных реквизитов
    		rq_cmd = RSDCommand(rq_sql);
    		rq_cmd.AddParam("bankid",  RSDBP_IN, OldBranch.Branch_ID);
    		rq_cmd.AddParam("account", RSDBP_IN, _ds.ACCOUNT_OLD);
    		rq_rsd = RSDRecordset(rq_cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
	    	while(rq_rsd.MoveNext())
		    	if ((ValType(_ds.ACCOUNT_NEW) != 0) and (_ds.ACCOUNT_NEW != " "))
		    		rq_rsd_count = rq_rsd_count + 1;
		    		rq_rsd.Edit();
		    		 rq_rsd.value("t_BankID")    = NewBranch.Branch_ID;
		    		 rq_rsd.value("t_account")   = _ds.ACCOUNT_NEW;
		    		 rq_rsd.value("t_BankCode")  = _ds.NEW_BIC;
		    		 rq_rsd.value("t_CorrAcc")   = NewBranch.Branch_CRSA_Acc;
		    		 rq_rsd.value("t_BankName")  = NewBranch.Branch_Name;
		    		 rq_rsd.value("t_BankCorrCode") = NewBranch.Branch_CRSA_BIC;
		    		 rq_rsd.value("t_bankcorrid") = ID_CorrBranch;
		    		 rq_rsd.value("t_BankCorrName") = NewCorrBranch.Branch_Name;
		    		rq_rsd.Update();
                                SetRQPlace(newBranch.Branch_ID, oldBranch.Branch_ID, rq_rsd.value("T_DOCKIND"), rq_rsd.value("T_DOCID"));
		    		// выводим в протокол поля "Идентификатор клиента в ЦФТ",  "ФИО Клиента", код операции списания денежных средств, старый номер счёта, новый номер счёта и текст: "Требование-обязательство успешно обновлено".
		    		MLog.Add_RQ_UpdItem(file_pos, _ds.Code,_ds.PARTYNAME,getNptxOpNumByRQID(rq_rsd.value("t_ID")),_ds.ACCOUNT_OLD, _ds.ACCOUNT_NEW, "Требование-обязательство успешно обновлено");
		    	else
		    		// если новый счёт не заполнен, то такую запись файла не обрабатывать, а в протокол выдавать сообщение "Не задан новый счёт".
		    		MLog.Add_RQ_UpdItem(file_pos, _ds.Code,_ds.PARTYNAME,getNptxOpNumByRQID(rq_rsd.value("t_ID")),_ds.ACCOUNT_OLD, "", "Не задан новый счёт");
    				err_count = err_count + 1;
		    	end;
	    	end;

    	end;
    end;

   var FullNameFile;
   FILE inFile() txt;
   var cmd, ds;
   
   if(not SelectFile(FullNameFile, /*"$" +*/ OPEN_DIR + FILE_MASK, OPEN_DIALOG_HEADER, 0, false))
     MsgBox("Отказ от загрузки");
     return 0;
   end;

   Check_table();

   Fill_Table(FullNameFile);

    cmd = DL_RSDCommand("Select * from u_biq_9949_tmp order by t_id asc");
    if(cmd.GetCount()  > 0 )
    	ds = cmd.Execute();
    	Process_Data(ds);
    end;

end;

var errmsg;

If(not GetTrue(false, "Вы желаете запустить процесс замены реквизитов филиала в платёжных инструкциях?"))
	msgboxex("Отказ от обработки");
    return;    
end;

Mlog = logger;
Mlog.init();

loadAccessFile(errmsg);

Mlog.print();
exit(1);