import bankinter, rsd, "or_exl_hm.mac";
private var nf;

private Macro SaveRow(clname, bracc, outrest, cur, bdate)
 var sql, cmd;
 sql = "Insert into dias_bracc values(:1, :2, :3, :4, :5)";
 cmd = RSDCommand(sql);
 cmd.AddParam("p01", RSDBP_IN, clname);
 cmd.AddParam("p02", RSDBP_IN, bracc);
 cmd.AddParam("p03", RSDBP_IN, outrest);
 cmd.AddParam("p04", RSDBP_IN, cur);
 cmd.AddParam("p16", RSDBP_IN, bdate);
 cmd.Execute;
 return true;
end;

private macro Getdate(fname)
  var vtxt = fname;
  While (index(vtxt, "\\") > 0)
    vtxt = Substr(vtxt, index(vtxt, "\\") + 1);
  end;
//  return date(int(substr(vtxt, 12, 2)), int(substr(vtxt, 15, 2)), int(substr(vtxt, 18, 2)) + 2000);
  return date(int(substr(vtxt, 1, 2)), int(substr(vtxt, 3, 2)), int(substr(vtxt, 5, 2)) + 2000);
end;

Private macro GetSavedRows(bdate)
  var sql, cmd, rs;
  sql = "select count(1) from dias_bracc where trunc(rdate) = to_date(:p1,'dd.mm.yyyy')";
  cmd = RSDCommand(sql);
  cmd.AddParam("p1", RSDBP_IN, strsubst(string(bdate), " ", "0"));
  rs = RSDRecordSet(cmd);
  if (rs.MoveNext()) 
    return Int(rs.Value(0));
  end;
  return 0;
End;

private Macro DeleteBDate(bdate)
  var sql, cmd;
  sql = "delete from dias_bracc b where trunc(b.rdate) = to_date(:p1,'dd.mm.yyyy')";
  cmd = RSDCommand(sql);
  cmd.AddParam("p1", RSDBP_IN, strsubst(string(bdate), " ", "0"));
  cmd.Execute;
end;


private Macro LoadBracc(namefile)
  var objExcel = CDAOMSExcel();
  var i = 1, cntempty = 0, cntsave = 0;
  var bdate;

  bdate = Getdate(namefile);
  if( GetSavedRows(bdate) > 0) 
   if(not GetTrue(false, "За " + bdate + " данные уже загружались, желаете обновить?"))
     return;
   end;
   DeleteBDate(bdate);
  end;
  if(objExcel.CreateApplication())
    if(objExcel.open(namefile))
      objExcel.Book.Sheets(1).Activate();
      BegAction(0, "Ждите, выполняется загрузка данных ...", false);
      While (cntempty < 20)
        if (StrIsNumber(substr(objExcel.Book.ActiveSheet.Cells(i, 2).text, 1, 20)))
          if (SaveRow(objExcel.Book.ActiveSheet.Cells(i, 1).text,
                      objExcel.Book.ActiveSheet.Cells(i, 2).text,
                      objExcel.Book.ActiveSheet.Cells(i, 3).text,
                      objExcel.Book.ActiveSheet.Cells(i, 4).text,
                      bdate))
            cntsave = cntsave + 1;
          end;
          cntempty = 0;
        else
          cntempty = cntempty + 1;
        end;
        i = i + 1;
      end;
      EndAction;
      Msgbox("За " + bdate +  " загружено "+ trim(string(cntsave)) + " счетов.");

    end;
    objExcel.Book.Close();
  end;
  objExcel = null;
OnError(err)
  Msgbox(err.Message);
  objExcel.Book.Close();
  objExcel = null;
end;

While (selectFile (nf, mergeFile ("..\\Import\\BRACC", "*.xls"))) 
  LoadBracc(nf);
end;

exit(1);