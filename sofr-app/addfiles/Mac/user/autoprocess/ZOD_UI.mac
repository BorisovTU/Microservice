/*
$Name:           ZOD_UI.mac
$Module:         Главная книга
$Description:    Интерфейс настройки заданий ЗОД
*/


import rsd;
import RsbFormsInter;
import "usr_key_const.mac";
import "int_usr_globals.mac";


private macro get_process_list_sql() 
  var sql = String( "select t_market_kind, t_type, t_subtype, t_num, t_name  from(\n"
                   ,"    select 'Фондовый рынок ММВБ' as t_market_kind, t_type, t_subtype, t_num, t_name from (\n"
                   ,"    select 1 t_type, 101 t_subtype,  1 t_num, chr(0) t_isfinish, 'Загрузка в шлюз заявок на сделки' t_name from dual union all\n"
                   ,"    select 1 t_type, 102 t_subtype,  2 t_num, chr(0) t_isfinish, 'Загрузка в шлюз заключенных сделок' t_name from dual union all\n"
                   ,"    select 1 t_type, 103 t_subtype,  3 t_num, chr(0) t_isfinish, 'Загрузка в шлюз сделок, принятых на клиринг' t_name from dual union all\n"
                   ,"    select 1 t_type, 105 t_subtype,  4 t_num, chr(0) t_isfinish, 'Загрузка в шлюз движений КСУ' t_name from dual union all\n"
                   ,"    select 1 t_type, 106 t_subtype,  5 t_num, chr(0) t_isfinish, 'Загрузка в шлюз Итоговых нетто-ТО по ФИ' t_name from dual union all\n"
                   ,"    select 1 t_type, 107 t_subtype,  6 t_num, chr(0) t_isfinish, 'Загрузка в шлюз комиссионных вознаграждений по валютному рынку' t_name from dual union all\n"
                   ,"    select 1 t_type, 201 t_subtype,  7 t_num, chr(0) t_isfinish, 'Загрузка в БО заявок на клиентские сделки' t_name from dual union all\n"
                   ,"    select 1 t_type, 202 t_subtype,  8 t_num, chr(0) t_isfinish, 'Загрузка в БО отчета биржи' t_name from dual union all\n"
                   ,"    select 1 t_type, 203 t_subtype,  9 t_num, chr(0) t_isfinish, 'Загрузка в БО клиентских сделок с ценными бумагами' t_name from dual union all\n"
                   ,"    select 1 t_type, 204 t_subtype, 10 t_num, chr(0) t_isfinish, 'Загрузка в БО клиентских сделок, принятых на клиринг' t_name from dual union all\n"
                   ,"    select 1 t_type, 401 t_subtype, 11 t_num,    'S' t_isfinish, 'Расчет комиссий' t_name from dual union all\n"
                   ,"    select 1 t_type, 701 t_subtype, 12 t_num,    'S' t_isfinish, 'Расчет Лимитов Фондовый рынок' t_name from dual union all\n"
                   ,"    select 1 t_type, 702 t_subtype, 13 t_num,    'S' t_isfinish, 'Расчет Лимитов Фондовый рынок и ЕДП' t_name from dual union all\n"
                   ,"    select 1 t_type, 703 t_subtype, 14 t_num,    'S' t_isfinish, 'Сверка и выгрузка общего файла лимитов' t_name from dual union all\n"
                   ,"    select 1 t_type, 301 t_subtype, 15 t_num,    'S' t_isfinish, 'Старт биржевых клиентских сделок БО' t_name from dual union all\n"
                   ,"    select 1 t_type, 506 t_subtype, 16 t_num,    'S' t_isfinish, 'СОБУ по клиентским операциям' t_name from dual union all\n"
                   ,"    select 1 t_type, 508 t_subtype, 17 t_num,    'S' t_isfinish, 'СОВУ по клиентским операциям' t_name from dual union all\n"
                   ,"    select 1 t_type, 104 t_subtype, 18 t_num, chr(0) t_isfinish, 'Загрузка в шлюз движений ДС' t_name from dual union all\n"
                   ,"    select 1 t_type, 505 t_subtype, 19 t_num, chr(0) t_isfinish, 'Создание операций движения ДС' t_name from dual union all\n"
                   ,"    select 1 t_type, 205 t_subtype, 20 t_num, chr(0) t_isfinish, 'Загрузка в БО движений ДС' t_name from dual union all\n"
                   ,"    select 1 t_type, 206 t_subtype, 21 t_num, chr(0) t_isfinish, 'Загрузка в БО движений КСУ' t_name from dual union all\n"
                   ,"    select 1 t_type, 207 t_subtype, 22 t_num, chr(0) t_isfinish, 'Загрузка в БО Итоговых нетто-ТО по ФИ' t_name from dual union all\n"
                   ,"    select 1 t_type, 208 t_subtype, 23 t_num, chr(0) t_isfinish, 'Загрузка в БО комиссионных вознаграждений по валютному рынку' t_name from dual union all\n"
                   ,"    select 1 t_type, 209 t_subtype, 24 t_num, chr(0) t_isfinish, 'Загрузка в БО заявок на собственные сделки' t_name from dual union all\n"
                   ,"    select 1 t_type, 210 t_subtype, 25 t_num, chr(0) t_isfinish, 'Загрузка в БО собственных сделок с ценными бумагами' t_name from dual union all\n"
                   ,"    select 1 t_type, 230 t_subtype, 26 t_num, chr(0) t_isfinish, 'Загрузка в БО собственных сделок, принятых на клиринг' t_name from dual union all\n"
                   ,"    select 1 t_type, 302 t_subtype, 27 t_num, chr(0) t_isfinish, 'Загрузка котировок с ММВБ' t_name from dual union all\n"
                   ,"    select 1 t_type, 303 t_subtype, 28 t_num, chr(0) t_isfinish, 'Старт биржевых собственных сделок БО' t_name from dual union all\n"
                   ,"    select 1 t_type, 503 t_subtype, 29 t_num, chr(0) t_isfinish, 'Утреннее начисление дохода/расхода' t_name from dual union all\n"
                   ,"    select 1 t_type, 501 t_subtype, 30 t_num,    'S' t_isfinish, 'Расчет текущей справедливой стоимости' t_name from dual union all\n"
                   ,"    select 1 t_type, 502 t_subtype, 31 t_num,    'S' t_isfinish, 'Формирование сервисных операций' t_name from dual union all\n"
                   ,"    select 1 t_type, 504 t_subtype, 32 t_num,    'S' t_isfinish, 'Переоценка' t_name from dual union all\n"
                   ,"    select 1 t_type, 601 t_subtype, 33 t_num,    'S' t_isfinish, 'Формирование отчета брокера' t_name from dual union all\n"
                   ,"    select 1 t_type, 507 t_subtype, 34 t_num,    'S' t_isfinish, 'Запуск процедуры перенумерации операций списания ДС' t_name from dual) union\n"
                   ,"select 'Срочный рынок' as t_market_kind,t_type, t_subtype, t_num, t_name from (\n"
                   ,"    select 4 t_type, 111 t_subtype,  1 t_num, chr(0) t_isfinish, 'Загрузка в шлюз биржевых данных' t_name from dual union all\n"
                   ,"    select 4 t_type, 211 t_subtype,  2 t_num, chr(0) t_isfinish, 'Загрузка в БО производных инструментов' t_name from dual union all\n"
                   ,"    select 4 t_type, 212 t_subtype,  3 t_num, chr(0) t_isfinish, 'Загрузка в БО курсов финансовых инструментов' t_name from dual union all\n"
                   ,"    select 4 t_type, 213 t_subtype,  4 t_num, chr(0) t_isfinish, 'Загрузка в БО биржевых операций с ПИ' t_name from dual union all\n"
                   ,"    select 4 t_type, 214 t_subtype,  5 t_num, chr(0) t_isfinish, 'Загрузка в БО итоги дня по позиции ПИ' t_name from dual union all\n"
                   ,"    select 4 t_type, 215 t_subtype,  6 t_num, chr(0) t_isfinish, 'Загрузка в БО разовой комиссии' t_name from dual union all\n"
                   ,"    select 4 t_type, 216 t_subtype,  7 t_num, chr(0) t_isfinish, 'Загрузка в БО отчета биржи' t_name from dual union all\n"
                   ,"    select 4 t_type, 217 t_subtype,  8 t_num, chr(0) t_isfinish, 'Загрузка в БО обработка отчета по операции' t_name from dual union all\n"
                   ,"    select 4 t_type, 218 t_subtype,  9 t_num, chr(0) t_isfinish, 'Загрузка в БО информации о гарантийном обеспечении' t_name from dual union all\n"
                   ,"    select 4 t_type, 412 t_subtype, 10 t_num, chr(0) t_isfinish, 'Инициализация расчетов с биржей по клиентам' t_name from dual union all\n"
                   ,"    select 4 t_type, 411 t_subtype, 11 t_num, chr(0) t_isfinish, 'Обработка клиентских сделок' t_name from dual union all\n"
                   ,"    select 4 t_type, 413 t_subtype, 12 t_num, chr(0) t_isfinish, 'Обработка расчетов с биржей по клиентам' t_name from dual union all\n"
                   ,"    select 4 t_type, 414 t_subtype, 13 t_num, chr(0) t_isfinish, 'Закрытие расчетов с биржей по клиентам' t_name from dual union all\n"
                   ,"    select 4 t_type, 711 t_subtype, 14 t_num, chr(0) t_isfinish, 'Расчет лимитов Срочный рынок' t_name from dual union all\n"
                   ,"    select 4 t_type, 112 t_subtype, 15 t_num,    'S' t_isfinish, 'Загрузка в шлюз заявок-поручений на сделки' t_name from dual union all\n"
                   ,"    select 4 t_type, 219 t_subtype, 16 t_num,    'S' t_isfinish, 'Загрузка в БО заявок-поручений на сделки' t_name from dual  union all\n"
                   ,"    select 4 t_type, 415 t_subtype, 17 t_num, chr(0) t_isfinish, 'Инициализация расчетов с биржей по собственным сделкам' t_name from dual union all\n"
                   ,"    select 4 t_type, 416 t_subtype, 18 t_num, chr(0) t_isfinish, 'Обработка собственных сделок ' t_name from dual union all\n"
                   ,"    select 4 t_type, 417 t_subtype, 19 t_num, chr(0) t_isfinish, 'Обработка расчетов с биржей по собственным сделкам' t_name from dual union all\n"
                   ,"    select 4 t_type, 418 t_subtype, 20 t_num, chr(0) t_isfinish, 'Закрытие расчетов с биржей по собственным сделкам' t_name from dual) union\n"
                   ,"select 'Валютный рынок' as t_market_kind,t_type, t_subtype, t_num, t_name from (\n"
                   ,"    select 3 t_type, 121 t_subtype,  1  t_num, chr(0) t_isfinish, 'Загрузка в Шлюз переводов и комиссий' t_name from dual union all\n"
                   ,"    select 3 t_type, 221 t_subtype,  2  t_num, chr(0) t_isfinish, 'Загрузка в БО переводов' t_name from dual union all\n"
                   ,"    select 3 t_type, 122 t_subtype,  3  t_num,    'S' t_isfinish, 'Загрузка в Шлюз итогов валютных торгов' t_name from dual union all\n"
                   ,"    select 3 t_type, 222 t_subtype,  4  t_num,    'S' t_isfinish, 'Загрузка в БО итогов валютных торгов' t_name from dual union all\n"
                   ,"    select 3 t_type, 226 t_subtype,  5  t_num, chr(0) t_isfinish, 'Загрузка в БО комиссий' t_name from dual union all\n"
                   ,"    select 3 t_type, 123 t_subtype,  6  t_num, chr(0) t_isfinish, 'Загрузка в Шлюз сделок' t_name from dual union all\n"
                   ,"    select 3 t_type, 223 t_subtype,  7  t_num, chr(0) t_isfinish, 'Загрузка в БО сделок ' t_name from dual union all\n"
                   ,"    select 3 t_type, 224 t_subtype,  8  t_num, chr(0) t_isfinish, 'Загрузка в БО Отчета биржи' t_name from dual union all\n"
                   ,"    select 3 t_type, 421 t_subtype,  9  t_num, chr(0) t_isfinish, 'Расчёт комиссий' t_name from dual union all\n"
                   ,"    select 3 t_type, 721 t_subtype, 10  t_num, chr(0) t_isfinish, 'Расчет лимитов Валютный рынок' t_name from dual union all\n"
                   ,"    select 3 t_type, 422 t_subtype, 11  t_num, chr(0) t_isfinish, 'Старт сделок БО' t_name from dual union all\n"
                   ,"    select 3 t_type, 423 t_subtype, 12  t_num,    'S' t_isfinish, 'Обработка итогов' t_name from dual union all\n"
                   ,"    select 3 t_type, 124 t_subtype, 13  t_num,    'S' t_isfinish, 'Загрузка в Шлюз заявок' t_name from dual union all\n"
                   ,"    select 3 t_type, 225 t_subtype, 14  t_num,    'S' t_isfinish, 'Загрузка в БО заявок на сделки' t_name from dual) union\n"
                   ,"select 'Фондовый рынок СПБ' as t_market_kind,t_type, t_subtype, t_num, t_name from (\n"
                   ,"    select 2 t_type, 4010 t_subtype,  1  t_num, chr(0) t_isfinish, 'Загрузка в шлюз заявок на сделки на бирже СПБ' t_name from dual union all\n"
                   ,"    select 2 t_type, 4020 t_subtype,  2  t_num, chr(0) t_isfinish, 'Загрузка в шлюз заключенных сделок на бирже СПБ' t_name from dual union all\n"
                   ,"    select 2 t_type, 4030 t_subtype,  3  t_num, chr(0) t_isfinish, 'Загрузка в шлюз сделок, принятых на клиринг на бирже СПБ' t_name from dual union all\n"
                   ,"    select 2 t_type, 4040 t_subtype,  4  t_num, chr(0) t_isfinish, 'Загрузка в шлюз Итоговых нетто-ТО по ФИ на бирже СПБ' t_name from dual union all\n"
                   ,"    select 2 t_type, 4050 t_subtype,  5  t_num, chr(0) t_isfinish, 'Загрузка в БО заявок на клиентские сделки на бирже СПБ' t_name from dual union all\n"
                   ,"    select 2 t_type, 4060 t_subtype,  6  t_num, chr(0) t_isfinish, 'Загрузка в БО отчета биржи на бирже СПБ' t_name from dual union all\n"
                   ,"    select 2 t_type, 4070 t_subtype,  7  t_num, chr(0) t_isfinish, 'Загрузка в БО клиентских сделок с ценными бумагами на бирже СПБ' t_name from dual union all\n"
                   ,"    select 2 t_type, 4080 t_subtype,  8  t_num, chr(0) t_isfinish, 'Загрузка в БО клиентских сделок, принятых на клиринг на бирже СПБ' t_name from dual union all\n"
                   ,"    select 2 t_type, 4090 t_subtype,  9  t_num, chr(0) t_isfinish, 'Создание операций движения ДС на бирже СПБ' t_name from dual union all\n"
                   ,"    select 2 t_type, 4100 t_subtype, 10  t_num, chr(0) t_isfinish, 'Загрузка в БО Итоговых нетто-ТО по ФИ на бирже СПБ' t_name from dual union all\n"
                   ,"    select 2 t_type, 4110 t_subtype, 11  t_num, chr(0) t_isfinish, 'Загрузка в шлюз движений ДС на бирже СПБ' t_name from dual union all\n"
                   ,"    select 2 t_type, 4120 t_subtype, 12  t_num, chr(0) t_isfinish, 'Загрузка в БО движений ДС на бирже СПБ' t_name from dual union all\n"
                   ,"    select 2 t_type, 4130 t_subtype, 13  t_num, chr(0) t_isfinish, 'Загрузка котировок с СПБ' t_name from dual))\n"
                   ,"order by t_market_kind, t_num");
  return sql;
end;


private macro IIF(val, retval, retval2)
   if (val)
      return retval;
   else
      return retval2;
   end;
end;


private MACRO AD_Scroll( Select:STRING, Header:STRING, X:INTEGER, Y:INTEGER /*...*/):RsdRecordset

  var rs     = RsdRecordset( Select, null, RSDVAL_STATIC ),
      Column = TArray(),
      i, ColumnValue, ColumnName, ColumnWidth, NumColumns = 0;

  macro EvProc( rs, cmd, id, key )
    if( (cmd == DLG_KEY) and (key == /*KEY_ENTER*/ 13) )
       return CM_SELECT;
    end;
    return CM_DEFAULT;
  end;
  
  /* атрибуты полей */
  macro AddColumn( ar,ind, fld, head, Width )
     ar.value( ind * 6 + 0 ) = fld;  // fieldName
     ar.value( ind * 6 + 1 ) = head; // header 
     ar.value( ind * 6 + 2 ) = Width; // width
     ar.value( ind * 6 + 3 ) = 2;    // fldType (2 = FBT)
     ar.value( ind * 6 + 4 ) = /*null*/ 0; // decPoint
     ar.value( ind * 6 + 5 ) = 0;    // reserv
     NumColumns = NumColumns + 1;
  end;
 
  i = 4;
  while( GetParm(i, ColumnValue ) AND GetParm(i+1, ColumnName) AND GetParm(i+2, ColumnWidth) )              
     AddColumn( Column, NumColumns, ColumnValue, ColumnName, ColumnWidth );
     i = i + 3;
  end;   

  if( rs.MoveNext() )
    if( RunScroll( rs, NumColumns, Column, null, @EvProc, Header, "Esc Выход F4 Поиск Enter Выбор", true, X, Y, null, 25 ) )
       return rs;
    end;
  end;

  return null;
END;


private macro GetStatusVariant(p_control_proc_prm:@Object)
 
  var sqls = String( "select 0 as t_status, 'Не обработан' as t_status_name from dual\n"
                    ,"union\n"
                    ,"select 1 as t_status, 'Обрабатывается' as t_status_name from dual\n"
                    ,"union\n"
                    ,"select 2 as t_status, 'Ошибка обработки' as t_status_name from dual\n"
                    ,"union\n"
                    ,"select 3 as t_status, 'Обработан с ошибками' as t_status_name from dual\n"
                    ,"union\n"
                    ,"select 10 as t_status, 'Обработан успешно' as t_status_name from dual");

  var Choise = AD_Scroll(sqls, "Выбор статуса", 10, 5,"t_status", "Номер", 20,"t_status_name", "Название", 20);
  if (Choise != null)
    p_control_proc_prm.v_status = Int(Choise.value("t_status"));
    p_control_proc_prm.v_status_name = Choise.value("t_status_name");
  end;
end;


macro GetMainProc(p_main_proc_prm:@object)

    var sqls;

    sqls = get_process_list_sql();

    var Choise = AD_Scroll(sqls, "Список процедур обработки дня", 10, 5,"t_market_kind", "Рынок", 15,"t_subtype", "ID процедуры", 10,"t_num", "№ процедуры", 10,"t_name", "Процедура", 50);
    if (Choise != null)
      p_main_proc_prm.v_market = Choise.value("t_market_kind");
      p_main_proc_prm.v_type = int(Choise.value("t_type"));
      p_main_proc_prm.v_subtype = int(Choise.value("t_subtype"));
      p_main_proc_prm.v_num = int(Choise.value("t_num"));
      p_main_proc_prm.v_name = Choise.value("t_name");
    end;
     
 end;


/*-------------------------------*/
/* Параметры зависимого процесса */
/*-------------------------------*/
private class c_control_proc_prm(p_id,p_procid,p_type,p_subtype,p_num,p_name,p_comment,p_status)
   
  var v_id;
  var v_procid;
  var v_type;
  var v_subtype;
  var v_num;
  var v_name;
  var v_comment;
  var v_status; 
  var v_status_name;
  var v_market;

  private macro m_sv0_decode(p_value, p_type)
    private var v_ret;
    if(ValType(p_value) == 26)
      if(p_type == V_STRING)
        v_ret = "";
      elif(p_type == V_INTEGER)
        v_ret = 0;
      /* Предполагается, что все возможные варианты должны быть явно учтены */
      end;
    else
      v_ret = p_value;
    end;

    return v_ret;
  end;


  private macro m_init_control_proc_prm(p_id,p_procid,p_type,p_subtype,p_num,p_name,p_comment,p_status)
    private var v_aux_buf = ValType(p_id);

    v_procid = p_procid;
   
    if((v_aux_buf != V_UNDEF) and (v_aux_buf != 26)) 
      v_id         = p_id;
      v_type       = p_type;
      v_subtype    = p_subtype;
      v_num        = p_num;
      v_name       = p_name;
      v_comment    = p_comment;
      v_status     = p_status;

      if  (p_type == 1)
        v_market = "Фондовый рынок ММВБ";
      elif(p_type == 2)
        v_market = "Срочный рынок";
      elif(p_type == 3)
        v_market = "Валютный рынок";
      elif(p_type == 4)
        v_market = "Фондовый рынок СПБ";
      else v_market = ""; 
      end;

      if  (p_status == 0)
        v_status_name = "Не обработан";
      elif(p_status == 1)
        v_status_name = "Обрабатывается";
      elif(p_status == 2)
        v_status_name = "Ошибка обработки";
      elif(p_status == 3)
        v_status_name = "Обработан с ошибками";
      elif(p_status == 10)
        v_status_name = "Обработан успешно";
      else v_status_name = ""; 
      end;

    else 
      v_id         = 0;
      v_type       = 0;
      v_subtype    = 0;
      v_num        = 0;
      v_name       = "";
      v_comment    = "";
      v_market     = "";
      v_status     = 0; v_status_name = "";
    end; 

  end;   
   
  m_init_control_proc_prm(p_id,p_procid,p_type,p_subtype,p_num,p_name,p_comment,p_status);
end; /* class c_control_proc_prm() */


/*-------------------------------*/
/* Параметры  процесса ЗОД       */
/*-------------------------------*/
private class c_main_proc_prm(p_id, p_type, p_subtype, p_num, p_name, p_skip_type, p_comment, p_macro, p_func)
   
  var v_proc_id     :integer;
     
  var v_id;
  var v_type; var v_market;
  var v_subtype;
  var v_num;
  var v_name;
  var v_skip_type;
  var v_comment;
  var v_macro;
  var v_func;


  private macro m_sv0_decode(p_value, p_type)
    private var v_ret;
    if(ValType(p_value) == 26)
      if(p_type == V_STRING)
        v_ret = "";
      elif(p_type == V_INTEGER)
        v_ret = 0;
      /* Предполагается, что все возможные варианты должны быть явно учтены */
      end;
    else
      v_ret = p_value;
    end;

    return v_ret;
  end;


  private macro m_init_main_proc_prm(p_id, p_type, p_subtype, p_num, p_name, p_skip_type, p_comment, p_macro, p_func)
    private var v_aux_buf = ValType(p_id);

    v_proc_id = 0;
   
    if((v_aux_buf != V_UNDEF) and (v_aux_buf != 26)) 
      v_id         = p_id;
      v_type       = p_type;
      v_subtype    = p_subtype;
      v_num        = p_num;
      v_name       = p_name;
      v_skip_type  = p_skip_type;
      v_comment    = p_comment;
      v_macro      = m_sv0_decode(p_macro,V_STRING);
      v_func       = m_sv0_decode(p_func, V_STRING);
       
      if  (p_type == 1)
        v_market = "Фондовый рынок ММВБ";
      elif(p_type == 2)
        v_market = "Срочный рынок";
      elif(p_type == 3)
        v_market = "Валютный рынок";
      elif(p_type == 4)
        v_market = "Фондовый рынок СПБ";
      else v_market = ""; 
      end;
    else 
      v_id         = 0;
      v_type       = 0;
      v_subtype    = 0;
      v_num        = 0;
      v_name       = "";
      v_skip_type  = "";
      v_comment    = "";
      v_market     = "";
      v_macro      = "";
      v_func       = ""; 
    end; 
  end;

  m_init_main_proc_prm(p_id, p_type, p_subtype, p_num, p_name, p_skip_type, p_comment, p_macro, p_func);
end; /* class c_main_proc_prm() */


/*------------------------------------------------*/
/* Сохранение параметров контролируемой процедуры */
/*------------------------------------------------*/
private macro m_save_control_proc_prm(p_control_proc_prm : @object)
  private var v_ret = true;
  private var v_sql, v_cmd;
   
  if((ValType(p_control_proc_prm.v_subtype) == V_INTEGER) and (p_control_proc_prm.v_subtype > 0))

    v_sql = String( "MERGE INTO usr_control_proczod_dbt tbl_main\n"
                   ,"     USING (SELECT :p_id p_id,\n"
                   ,"                   :p_procid p_procid,\n"
                   ,"                   :p_type p_type,\n"
                   ,"                   :p_subtype p_subtype,\n"
                   ,"                   :p_num p_num,\n"
                   ,"                   :p_name p_name,\n"
                   ,"                   :p_status p_status,\n"
                   ,"                   :p_comment p_comment\n"
                   ,"              FROM DUAL) src_data\n"
                   ,"        ON (tbl_main.t_subtype = src_data.p_subtype and tbl_main.t_procid = src_data.p_procid)\n"
                   ,"WHEN MATCHED\n"
                   ,"THEN\n"
                   ,"   UPDATE SET tbl_main.t_status = src_data.p_status\n"
                   ,"WHEN NOT MATCHED\n"
                   ,"THEN\n"
                   ,"   INSERT     (tbl_main.t_id,\n"
                   ,"               tbl_main.t_procid,\n"
                   ,"               tbl_main.t_type,\n"
                   ,"               tbl_main.t_subtype,\n"
                   ,"               tbl_main.t_num,\n"
                   ,"               tbl_main.t_name,\n"
                   ,"               tbl_main.t_status,\n"
                   ,"               tbl_main.t_comment)\n"
                   ,"       VALUES (0,\n"
                   ,"               src_data.p_procid,\n"
                   ,"               src_data.p_type,\n"
                   ,"               src_data.p_subtype,\n"
                   ,"               src_data.p_num,\n"
                   ,"               src_data.p_name,\n"
                   ,"               src_data.p_status,\n"
                   ,"               src_data.p_comment)");


    v_cmd = RSDCommand(v_sql);
    v_cmd.addParam("p_id",    RSDBP_IN, p_control_proc_prm.v_id);
    v_cmd.addParam("p_procidid",    RSDBP_IN, p_control_proc_prm.v_procid);
    v_cmd.addParam("p_type",    RSDBP_IN, p_control_proc_prm.v_type);
    v_cmd.addParam("p_subtype",   RSDBP_IN, p_control_proc_prm.v_subtype);
    v_cmd.addParam("p_num",     RSDBP_IN, p_control_proc_prm.v_num);
    v_cmd.addParam("p_name",    RSDBP_IN, p_control_proc_prm.v_name);
    v_cmd.addParam("p_status", RSDBP_IN, p_control_proc_prm.v_status);
    v_cmd.addParam("p_comment",   RSDBP_IN, p_control_proc_prm.v_comment);
    v_cmd.execute();

    v_cmd.close(); v_cmd = NULL; v_sql = NULL;
  else
    v_ret = false;
  end;

  return v_ret;

  onError(err)
    v_ret = false;
    return v_ret;
end; /* macro m_save_control_proc_prm() */


/*------------------------------------------------*/
/* Сохранение параметров процедуры ЗОД            */
/*------------------------------------------------*/
private macro m_save_main_proc_prm(p_main_proc_prm : @object)
  private var v_ret = true;
  private var v_sql, v_cmd;
   
  if((ValType(p_main_proc_prm.v_subtype) == V_INTEGER) and (p_main_proc_prm.v_subtype > 0))

  v_sql = String( "MERGE INTO usr_main_proczod_dbt tbl_main\n"
                 ,"     USING (SELECT :p_id p_id,\n"
                 ,"                   :p_type p_type,\n"
                 ,"                   :p_subtype p_subtype,\n"
                 ,"                   :p_num p_num,\n"
                 ,"                   :p_name p_name,\n"
                 ,"                   :p_skip_type p_skip_type,\n"
                 ,"                   :p_comment p_comment,\n"
                 ,"                   :p_macro p_macro,\n"
                 ,"                   :p_func p_func\n"
                 ,"              FROM DUAL) src_data\n"
                 ,"        ON (tbl_main.t_subtype = src_data.p_subtype)\n"
                 ,"WHEN MATCHED\n"
                 ,"THEN\n"
                 ,"   UPDATE SET tbl_main.t_skip_type = src_data.p_skip_type, tbl_main.t_macro = src_data.p_macro, tbl_main.t_func = src_data.p_func\n"
                 ,"WHEN NOT MATCHED\n"
                 ,"THEN\n"
                 ,"   INSERT     (tbl_main.t_id,\n"
                 ,"               tbl_main.t_type,\n"
                 ,"               tbl_main.t_subtype,\n"
                 ,"               tbl_main.t_num,\n"
                 ,"               tbl_main.t_name,\n"
                 ,"               tbl_main.t_skip_type,\n"
                 ,"               tbl_main.t_comment,\n"
                 ,"               tbl_main.t_macro,\n"
                 ,"               tbl_main.t_func)\n"
                 ,"       VALUES (0,\n"
                 ,"               src_data.p_type,\n"
                 ,"               src_data.p_subtype,\n"
                 ,"               src_data.p_num,\n"
                 ,"               src_data.p_name,\n"
                 ,"               src_data.p_skip_type,\n"
                 ,"               src_data.p_comment,\n"
                 ,"               src_data.p_macro,\n"
                 ,"               src_data.p_func)");

  v_cmd = RSDCommand(v_sql);
  v_cmd.addParam("p_id",    RSDBP_IN, p_main_proc_prm.v_id);
  v_cmd.addParam("p_type",    RSDBP_IN, p_main_proc_prm.v_type);
  v_cmd.addParam("p_subtype",   RSDBP_IN, p_main_proc_prm.v_subtype);
  v_cmd.addParam("p_num",     RSDBP_IN, p_main_proc_prm.v_num);
  v_cmd.addParam("p_name",    RSDBP_IN, p_main_proc_prm.v_name);
  v_cmd.addParam("p_skip_type", RSDBP_IN, p_main_proc_prm.v_skip_type);
  v_cmd.addParam("p_comment",   RSDBP_IN, p_main_proc_prm.v_comment);
  v_cmd.addParam("p_macro",   RSDBP_IN, p_main_proc_prm.v_macro);
  v_cmd.addParam("p_func",   RSDBP_IN, p_main_proc_prm.v_func);
  v_cmd.execute();

  v_cmd.close(); v_cmd = NULL; v_sql = NULL;
  else
    v_ret = false;
  end;

  return v_ret;

  onError(err)
    v_ret = false;
    return v_ret;
end; /* macro m_save_main_proc_prm() */


/*-------------------------------------------------*/
/* Класс формы параметров контролируемой процедуры */
/*-------------------------------------------------*/
class (TRsbPanel) c_control_proc_prm_form(p_id,p_procid,p_type,p_subtype,p_num,p_name,p_comment,p_status)
  const CV_POS_X = 3;
  const CV_POS_Y = 1;

  var v_control_proc_prm;
   
  var v_proc_id_fld;
  var v_market_kind_fld;
  var v_proc_name_fld;
  var v_proc_status_fld;

  /* Обработчик событий нажатия кнопок */
  macro m_control_proc_on_key_pressed(p_rsb_evnt)
      
    if(p_rsb_evnt.KeyCode == K_F2)
      v_control_proc_prm.v_subtype     = v_proc_id_fld.value;
      v_control_proc_prm.v_market = v_market_kind_fld.value;
      if(v_market_kind_fld.value == "Фондовый рынок ММВБ")
        v_control_proc_prm.v_type == 1;
      elif(v_market_kind_fld.value == "Срочный рынок")
        v_control_proc_prm.v_type == 2;
      elif(v_market_kind_fld.value == "Валютный рынок")
        v_control_proc_prm.v_type == 3;
      elif(v_market_kind_fld.value == "Фондовый рынок СПБ")
        v_control_proc_prm.v_type == 4;
      else 
        v_control_proc_prm.v_type == 0;
      end;

      v_control_proc_prm.v_name      = v_proc_name_fld.value;

      if(not m_save_control_proc_prm(@v_control_proc_prm))
        msgbox("Не удалось сохранить параметры контролируемой процедуры!");
      else
        msgbox("Параметры процедуры сохранены");
      end;
    elif(p_rsb_evnt.KeyCode == K_F3)

      if(p_rsb_evnt.source.focusedControl.name == "status_fld")
        GetStatusVariant(@v_control_proc_prm);
        v_proc_status_fld.value     = v_control_proc_prm.v_status_name;           
      else
        GetMainProc(@v_control_proc_prm);
           
        v_proc_id_fld.value     = v_control_proc_prm.v_subtype;
        v_market_kind_fld.value = v_control_proc_prm.v_market;
        v_proc_name_fld.value   = v_control_proc_prm.v_name;
      end;

      this.redraw;

    end;

    return true;
  end; /* m_control_proc_on_key_pressed() */


  private macro m_init_control_proc_prm_form(i_this_obj, i_id, i_procid, i_type, i_subtype, i_num, i_name, i_comment, i_status)

    v_control_proc_prm = c_control_proc_prm(i_id, i_procid, i_type, i_subtype, i_num, i_name, i_comment, i_status);

    Caption = "Параметры контролируемой процедуры " + iif(v_control_proc_prm.v_subtype != 0, v_control_proc_prm.v_subtype, "(Новая)" );;
    Status = "[Esc] Выход  [F2] Сохранение  [F3] Список";

    /*ID Операции ЗОД*/
    AddLabel(TRsbLabel((CV_POS_X + 0), CV_POS_Y, "ID Операции ЗОД"));
    v_proc_id_fld = TRsbEditField(FT_INT);
    v_proc_id_fld.bindValue(v_control_proc_prm, "v_subtype", 25);
    v_proc_id_fld.setPosition((CV_POS_X + 0), (CV_POS_Y + 1));
    v_proc_id_fld.setSize(11, 1);
    v_proc_id_fld.focusable = true;
    v_proc_id_fld.enabled = true;
    v_proc_id_fld.editable = false;
    AddControl(v_proc_id_fld);

    /*Рынок*/
    AddLabel(TRsbLabel(CV_POS_X + 15, (CV_POS_Y ), "Рынок"));
    v_market_kind_fld = TRsbEditField(FT_STRING);
    v_market_kind_fld.bindValue(v_control_proc_prm, "v_market", 2000);
    v_market_kind_fld.setPosition(CV_POS_X + 15, (CV_POS_Y + 1));
    v_market_kind_fld.setSize(25, 1);
    v_market_kind_fld.focusable = true;
    v_market_kind_fld.enabled = true;
    v_market_kind_fld.editable = false;
    AddControl(v_market_kind_fld);

    /*Название процедуры*/
    AddLabel(TRsbLabel(CV_POS_X + 0, (CV_POS_Y + 3), "Название процедуры"));
    v_proc_name_fld = TRsbEditField(FT_STRING);
    v_proc_name_fld.bindValue(v_control_proc_prm, "v_name", 2000);
    v_proc_name_fld.setPosition(CV_POS_X + 0, (CV_POS_Y + 4));
    v_proc_name_fld.setSize(40, 1);
    v_proc_name_fld.focusable = true;
    v_proc_name_fld.enabled = true;
    v_proc_name_fld.editable = false;
    AddControl(v_proc_name_fld);

    /* Статус процедуры */
    AddLabel(TRsbLabel((CV_POS_X + 0), CV_POS_Y + 5, "Статус процедуры"));
    v_proc_status_fld = TRsbEditField(FT_STRING);
    v_proc_status_fld.name = "status_fld";
    v_proc_status_fld.bindValue(v_control_proc_prm, "v_status_name", 25);
    v_proc_status_fld.setPosition((CV_POS_X + 0), (CV_POS_Y + 6));
    v_proc_status_fld.setSize(25, 1);
    v_proc_status_fld.focusable = true;
    v_proc_status_fld.enabled = true;
    v_proc_status_fld.editable = false;
    AddControl(v_proc_status_fld);

    AddEventHandler(RSB_EV_KEY_PRESSED, R2M(i_this_obj, "m_control_proc_on_key_pressed"));

    setSize(47, 12);
    setPosition(20, 6);
  end; /* macro m_init_control_proc_prm_form() */

  InitTRsbPanel();

  m_init_control_proc_prm_form(this, p_id,p_procid,p_type,p_subtype,p_num,p_name,p_comment,p_status);
end; /* class (TRsbPanel) c_control_proc_prm_form() */


/*-------------------------------------------------*/
/* Класс формы параметров процедуры ЗОД            */
/*-------------------------------------------------*/
class (TRsbPanel) c_main_proc_prm_form(p_id,   p_type,   p_subtype,   p_num,   p_name,   p_skip_type,   p_comment, p_macro, p_func)
  const CV_POS_X = 3;
  const CV_POS_Y = 1;

  var v_proc_id_fld;
  var v_market_kind_fld;
  var v_proc_name_fld;
  var v_skip_run_fld;
  var v_macro_name_fld;
  var v_func_name_fld;

  var v_main_proc_prm;
  
  /* Обработчик событий нажатия кнопок */
  macro m_main_proc_on_key_pressed(p_rsb_evnt)

    if(p_rsb_evnt.KeyCode == K_F2)
         
      v_main_proc_prm.v_subtype     = v_proc_id_fld.value;
      v_main_proc_prm.v_market = v_market_kind_fld.value;
      if(v_market_kind_fld.value == "Фондовый рынок ММВБ")
        v_main_proc_prm.v_type == 1;
      elif(v_market_kind_fld.value == "Срочный рынок")
        v_main_proc_prm.v_type == 2;
      elif(v_market_kind_fld.value == "Валютный рынок")
        v_main_proc_prm.v_type == 3;
      elif(v_market_kind_fld.value == "Фондовый рынок СПБ")
        v_main_proc_prm.v_type == 4;
      else 
        v_main_proc_prm.v_type == 0;
      end;

      v_main_proc_prm.v_name      = v_proc_name_fld.value;
      v_main_proc_prm.v_skip_type = iif(v_skip_run_fld.checked,"X","");
      v_main_proc_prm.v_macro     = v_macro_name_fld.value;
      v_main_proc_prm.v_func      = v_func_name_fld.value; 

      if(not m_save_main_proc_prm(@v_main_proc_prm))
        msgbox("Не удалось сохранить параметры процедуры ЗОД!");
      else
        msgbox("Параметры процедуры сохранены");
      end;

    elif(p_rsb_evnt.KeyCode == K_F3)
      GetMainProc(@v_main_proc_prm);
         
      v_proc_id_fld.value     = v_main_proc_prm.v_subtype;
      v_market_kind_fld.value = v_main_proc_prm.v_market;
      v_proc_name_fld.value   = v_main_proc_prm.v_name;

      this.redraw;
    end;

    return true;
  end; /* m_control_main_proc_on_key_pressed() */


  private macro m_init_main_proc_prm_form(i_this_obj, i_id,   i_type,   i_subtype,   i_num,   i_name,   i_skip_type,   i_comment, i_macro, i_func)

    v_main_proc_prm = c_main_proc_prm(i_id,   i_type,   i_subtype,   i_num,   i_name,   i_skip_type,   i_comment, i_macro, i_func);      

    Caption = "Параметры  процедуры ЗОД " + iif(v_main_proc_prm.v_subtype != 0, v_main_proc_prm.v_subtype, "(Новая)" );
    Status = "[Esc] Выход  [F2] Сохранение  [F3] Список";

	  /*ID Операции ЗОД*/
    AddLabel(TRsbLabel((CV_POS_X + 0), CV_POS_Y, "ID Операции ЗОД"));
    v_proc_id_fld = TRsbEditField(FT_INT);
    v_proc_id_fld.bindValue(v_main_proc_prm, "v_subtype", 25);
    v_proc_id_fld.setPosition((CV_POS_X + 0), (CV_POS_Y + 1));
    v_proc_id_fld.setSize(11, 1);
    v_proc_id_fld.focusable = true;
    v_proc_id_fld.enabled = true;
    v_proc_id_fld.editable = false;
    AddControl(v_proc_id_fld);

    /*Рынок*/
    AddLabel(TRsbLabel(CV_POS_X + 15, (CV_POS_Y ), "Рынок"));
    v_market_kind_fld = TRsbEditField(FT_STRING);
    v_market_kind_fld.bindValue(v_main_proc_prm, "v_market", 2000);
    v_market_kind_fld.setPosition(CV_POS_X + 15, (CV_POS_Y + 1));
    v_market_kind_fld.setSize(25, 1);
    v_market_kind_fld.focusable = true;
    v_market_kind_fld.enabled = true;
    v_market_kind_fld.editable = false;
    AddControl(v_market_kind_fld);

    /*Название процедуры*/
    AddLabel(TRsbLabel(CV_POS_X + 0, (CV_POS_Y + 3), "Название процедуры"));
    v_proc_name_fld = TRsbEditField(FT_STRING);
    v_proc_name_fld.bindValue(v_main_proc_prm, "v_name", 2000);
    v_proc_name_fld.setPosition(CV_POS_X + 0, (CV_POS_Y + 4));
    v_proc_name_fld.setSize(40, 1);
    v_proc_name_fld.focusable = true;
    v_proc_name_fld.enabled = true;
    v_proc_name_fld.editable = false;
    AddControl(v_proc_name_fld);

    /*Запуск с признаком пропуска*/
    AddLabel(TRsbLabel((CV_POS_X + 0), (CV_POS_Y + 6), "Запуск с признаком пропуска"));
    v_skip_run_fld = TRsbCheckBox(1);
    v_skip_run_fld.name = "chk_skip_run";
    v_skip_run_fld.dataType = 12; /*FT_CHR*/
	  v_skip_run_fld.enabled = false;
    v_skip_run_fld.setPosition((CV_POS_X + 1), (CV_POS_Y + 7));
    v_skip_run_fld.checked = /*""*/v_main_proc_prm.v_skip_type;
    AddControl(v_skip_run_fld);

    /*Название макроса*/
    AddLabel(TRsbLabel(CV_POS_X + 0, (CV_POS_Y + 8), "Название макроса"));
    v_macro_name_fld = TRsbEditField(FT_STRING);
    v_macro_name_fld.bindValue(v_main_proc_prm, "v_macro", 100);
    v_macro_name_fld.setPosition(CV_POS_X + 0, (CV_POS_Y + 9));
    v_macro_name_fld.setSize(40, 1);
    v_macro_name_fld.focusable = true;
    v_macro_name_fld.enabled = true;
    v_macro_name_fld.editable = true;
    AddControl(v_macro_name_fld);

    /*Название функции*/
    AddLabel(TRsbLabel(CV_POS_X + 0, (CV_POS_Y + 11), "Название функции"));
    v_func_name_fld = TRsbEditField(FT_STRING);
    v_func_name_fld.bindValue(v_main_proc_prm, "v_func", 100);
    v_func_name_fld.setPosition(CV_POS_X + 0, (CV_POS_Y + 12));
    v_func_name_fld.setSize(40, 1);
    v_func_name_fld.focusable = true;
    v_func_name_fld.enabled = true;
    v_func_name_fld.editable = true;
    AddControl(v_func_name_fld);

    AddEventHandler(RSB_EV_KEY_PRESSED, R2M(i_this_obj, "m_main_proc_on_key_pressed"));

    setSize(47, 15);
    setPosition(20, 6);
  end; /* macro m_init_main_proc_prm_form() */

  InitTRsbPanel();

  m_init_main_proc_prm_form(this, p_id,   p_type,   p_subtype,   p_num,   p_name,   p_skip_type,   p_comment, p_macro, p_func);
end; /* class (TRsbPanel) c_main_proc_prm_form() */


/*-------------------------------------------------------------*/
/* Удаление заданной позиции из списка контролируемых процедур */
/*-------------------------------------------------------------*/
private macro m_delete_control_proc(p_control_proc_id)
  private var v_ret = true;
  private var v_sql, v_cmd;

  v_sql =         "DELETE FROM usr_control_proczod_dbt ";
  v_sql = v_sql + " WHERE t_id = :p_control_proc_id ";

  v_cmd = RSDCommand(v_sql);
  v_cmd.addParam("p_control_proc_id", RSDBP_IN, p_control_proc_id);
  v_cmd.execute();

  v_cmd.close(); v_cmd = NULL; v_sql = NULL;

  return v_ret;

  onError(err)
    v_ret = false;
    return v_ret;
end; /* macro m_delete_control_proc() */


/*------------------------------------------------------------*/
/* Метод обработки событий скроллинга контролируемых процедур */
/*------------------------------------------------------------*/
macro m_control_proc_scr(p_rs, p_cmd, p_id, p_key)
  var CM_FLAG = CM_DEFAULT;
  var gv_control_proc_prm_form;

  if(p_cmd == DLG_INIT)

  elif(p_cmd == DLG_KEY)
    if(p_key == K_ENTER)
      gv_control_proc_prm_form = c_control_proc_prm_form( p_rs.value("t_id"),
                                                          p_rs.value("t_procid"),
                                                          p_rs.value("t_type"), 
                                                          p_rs.value("t_subtype"), 
                                                          p_rs.value("t_num"), 
                                                          p_rs.value("t_name"), 
                                                          p_rs.value("t_comment"), 
                                                          p_rs.value("t_status"));
      gv_control_proc_prm_form.run;
      CM_FLAG = CM_SELECT;

    elif(p_key == K_F8)

      if(gettrue(true, "Удалить запись?"))
        /* Удаление заданной позиции из списка процедур */
        if(not m_delete_control_proc(p_rs.value("t_id")))
          msgbox(string("Не удалось удалить процедуру ",
                         p_rs.value("t_name"),
                         " (",
                         p_rs.value("t_subtype"),
                         ") ",
                         "из списка процедур"));
        else
          CM_FLAG = CM_SELECT;
        end;
        end;

      elif(p_key == K_F9)
        gv_control_proc_prm_form = c_control_proc_prm_form();
        gv_control_proc_prm_form.run;

        CM_FLAG = CM_SELECT;

      elif(p_key == K_ESCAPE)
        if(not gettrue(true, "Выйти из списка контролируемых процедур?"))
          CM_FLAG = CM_IGNORE;
        end;
      end;

   end;

   return CM_FLAG;
end; /* macro m_control_proc_proc_scr() */


/*-------------------------------------------------------------*/
/* Метод, запускающий скроллинг списка контролируемых процедур */
/*-------------------------------------------------------------*/
private macro m_run_control_proc_scroll(p_main_proc_id)

  private var gv_column_list_scr = TArray(); /* Список колонок скроллинга списка зависимых процессов */
  private var gv_num_columns_scr = 0;        /* Количество колонок скроллинга списка зависимых процессов */
  
  private var sql_scr, cmd_scr, rs_scr;
  private var sql_main = "";
  private var sql_filtr = "";
  private var sql_order = "";

  gv_column_list_scr.size = 0;
  gv_num_columns_scr = 0;
     
  sql_main = String( "SELECT tbl.t_id,\n"
                    ,"       tbl.t_procid,\n"
                    ,"       tbl.t_type,\n"
                    ,"       decode(tbl.t_type,1,'Фондовый рынок ММВБ',2,'Срочный рынок',3,'Валютный рынок','Фондовый рынок СПБ') as t_marketkind,\n"
                    ,"       tbl.t_subtype,\n"
                    ,"       tbl.t_num,\n"
                    ,"       tbl.t_name,\n"
                    ,"       tbl.t_comment,\n"
                    ,"       tbl.t_status\n"
                    ,"       , decode (tbl.t_status,0,'Не обработан',1,'Обрабатывается',2,'Ошибка обработки',3,'Обработан с ошибками', 10,'Обработан успешно','') as t_status_name\n"
                    ,"  FROM usr_control_proczod_dbt tbl where tbl.t_procid = :p_main_proc_id ");
  sql_order = sql_order + "ORDER BY tbl.t_type, tbl.t_num ASC ";

  sql_scr = sql_main + sql_filtr + sql_order;

  cmd_scr = RSDCommand(sql_scr);
  cmd_scr.NullConversion = true;
  cmd_scr.addParam("p_main_proc_id", RSDBP_IN, p_main_proc_id);

  rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
  rs_scr.NullConversion = true;

  m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_marketkind",   "Рынок", 20); gv_num_columns_scr = gv_num_columns_scr + 1;
  m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_subtype",      "ID процедуры ЗОД", 20); gv_num_columns_scr = gv_num_columns_scr + 1;
  m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_num",          "Номер процедуры ЗОД", 20); gv_num_columns_scr = gv_num_columns_scr + 1;
  m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_name",         "Наименование", 45); gv_num_columns_scr = gv_num_columns_scr + 1;
  m_add_column(gv_column_list_scr, gv_num_columns_scr, "t_status_name",       "Статус обработки", 25); gv_num_columns_scr = gv_num_columns_scr + 1;

  while(RunScroll(rs_scr, gv_num_columns_scr, gv_column_list_scr, "control_proc_list", "m_control_proc_scr", "Список контролируемых процедур", "ESC - выход  ENTER - редактирование  F8 - удалить  F9 - добавить", true, 7, 5, NULL, 40))
    /* Обновляем скроллинг */
    cmd_scr.close(); cmd_scr = NULL;
    rs_scr.close(); rs_scr = NULL;

    sql_filtr = "";

    sql_scr = sql_main + sql_filtr + sql_order;

    cmd_scr = RSDCommand(sql_scr);
    cmd_scr.NullConversion = true;
    cmd_scr.addParam("p_main_proc_id", RSDBP_IN, p_main_proc_id);

    rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
  end;

  rs_scr.close();

end; /* macro m_run_control_proc_scroll() */


/*----------------------------------------------*/
/* Удаление заданной позиции из списка процедур */
/*----------------------------------------------*/
private macro m_delete_main_proc(p_main_proc_id)
  private var v_ret = true;
  private var v_sql, v_cmd;

  v_sql =         "DELETE FROM usr_main_proczod_dbt ";
  v_sql = v_sql + " WHERE t_id = :p_main_proc_id ";

  v_cmd = RSDCommand(v_sql);
  v_cmd.addParam("p_main_proc_id", RSDBP_IN, p_main_proc_id);
  v_cmd.execute();

  v_cmd.close(); v_cmd = NULL; v_sql = NULL;

  return v_ret;

  onError(err)
  v_ret = false;
  return v_ret;
end; /* macro m_delete_main_proc() */


/*----------------------------------------------*/
/* Метод обработки событий основного скроллинга */
/*----------------------------------------------*/
macro m_main_proc_scr(p_rs, p_cmd, p_id, p_key)
  var CM_FLAG = CM_DEFAULT;
  var gv_main_proc_prm_form;

  if(p_cmd == DLG_INIT)
  elif(p_cmd == DLG_KEY)
    if(p_key == K_ENTER)
        
      gv_main_proc_prm_form = c_main_proc_prm_form( p_rs.value("t_id"),
                                                    p_rs.value("t_type"), 
                                                    p_rs.value("t_subtype"), 
                                                    p_rs.value("t_num"), 
                                                    p_rs.value("t_name"), 
                                                    p_rs.value("t_skip_type"), 
                                                    p_rs.value("t_comment"),
                                                    p_rs.value("t_macro"),
                                                    p_rs.value("t_func"));
      gv_main_proc_prm_form.run;
      CM_FLAG = CM_SELECT;

    elif(p_key == K_F8)
      if(gettrue(true, "Удалить запись?"))
      /* Удаление заданной позиции из списка процедур */
        if(not m_delete_main_proc(p_rs.value("t_id")))
          msgbox(string("Не удалось удалить процедуру ",p_rs.value("t_name")," (",p_rs.value("t_subtype"),") ","из списка процедур"));
        else
          CM_FLAG = CM_SELECT;
        end;
      end;

    elif(p_key == K_F9)

      gv_main_proc_prm_form = c_main_proc_prm_form();
      gv_main_proc_prm_form.run;

      CM_FLAG = CM_SELECT;

    elif(p_key == K_F6)
         
      /* Отображение списка контролируемых процедур */
      m_run_control_proc_scroll(p_rs.value("t_id"));

      CM_FLAG = CM_IGNORE;

    elif(p_key == K_ESCAPE)
      if(not gettrue(true, "Выйти из списка процедур ЗОД?"))
        CM_FLAG = CM_IGNORE;
      end;
    end;

  end;

    return CM_FLAG;
end; /* macro m_main_proc_scr() */


/*---------------------------------------*/
/* Метод, запускающий основной скроллинг */
/*---------------------------------------*/
private macro m_run_main_scroll()
  private var gv_column_list_scr_main = TArray(); /* Список колонок основного скроллинга */
  private var gv_num_columns_scr_main = 0;        /* Количество колонок основного скроллинга */
  
  private var sql_scr, cmd_scr, rs_scr;
  private var sql_main = "";
  private var sql_filtr = "";
  private var sql_order = "";


  sql_main =            "SELECT tbl.t_id, ";
  sql_main = sql_main + " tbl.t_type, decode(tbl.t_type,1,'Фондовый рынок ММВБ',2,'Срочный рынок',3,'Валютный рынок', 'Фондовый рынок СПБ') as t_marketkind, ";
  sql_main = sql_main + " tbl.t_subtype, ";
  sql_main = sql_main + " tbl.t_num, ";
  sql_main = sql_main + " tbl.t_name, ";
  sql_main = sql_main + " tbl.t_skip_type, ";
  sql_main = sql_main + " tbl.t_comment, tbl.t_macro, tbl.t_func ";
  sql_main = sql_main + "FROM usr_main_proczod_dbt tbl ";

  sql_order = sql_order + "ORDER BY tbl.t_type ASC ";   

  sql_scr = sql_main + sql_filtr + sql_order;

  cmd_scr = RSDCommand(sql_scr);
  cmd_scr.NullConversion = true;

  rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
  rs_scr.NullConversion = true;

  m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "t_marketkind",   "Рынок", 20); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
  m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "t_subtype",  "ID процедуры ЗОД", 20); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
  m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "t_num",          "Номер процедуры ЗОД", 20); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
  m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "t_name",         "Наименование", 45); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
  m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "t_skip_type",    "Обработка в режиме пропуска", 25); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;

  while(RunScroll(rs_scr, gv_num_columns_scr_main, gv_column_list_scr_main, "main_proc_list", "m_main_proc_scr", "Список процедур ЗОД", "ESC - выход  F6 - список связанных процедур.  ENTER - параметры.  F8- удалить.  F9 -добавить. ", true, 5, NULL, NULL, 40))
    /* Обновляем скроллинг */
    cmd_scr.close(); cmd_scr = NULL;
    rs_scr.close(); rs_scr = NULL;
    sql_filtr = "";
    sql_scr = sql_main + sql_filtr + sql_order;
    cmd_scr = RSDCommand(sql_scr);
    cmd_scr.NullConversion = true;
    rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
  end;

  rs_scr.close();

end; /* macro m_run_main_scroll() */


/* Запуск основного скроллинга */
m_run_main_scroll();
Exit(1);





