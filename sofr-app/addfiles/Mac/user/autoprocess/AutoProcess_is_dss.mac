/*
$Name:        AutoProcess_is_dss.mac
$Module:      Ценные бумаги
$Description: 
*/

import AutoProcess;
import "SimpleService.mac";
import "err_handl.mac";
/*макрос сервиса */
var FIX_DATE = date(0,0,0);


private class (TArray) TArrCollector
   /*Очистить массив.*/
  macro ClearArray
    this.Size = 0;
  end;
  /*Добавить элемент в массив.*/
  macro AddItem( Item )
    this.(this.Size) = Item;
  end;
end;

private class clConSet(pConType, pConStatus)
   var ConType   : integer;
   var ConStatus : integer;

   macro init(pConType, pConStatus)
      ConType   = pConType;
      ConStatus = pConStatus;
   end;
   init(pConType, pConStatus);
end;

/*Набор проверяемых условий необходимых для выполнения пункта обработчика*/

/*ММВБ      
1  101           Загрузка в шлюз заявок на сделки
2  108           Загрузка в шлюз заявок на внебиржевые сделки
3  102           Загрузка в шлюз заключенных сделок
4  109           Загрузка в шлюз заключенных внебиржевых сделок
5  103           Загрузка в шлюз сделок, принятых на клиринг               
6  105           Загрузка в шлюз движений КСУ
7  106           Загрузка в шлюз Итоговых нетто-ТО по ФИ
8  107           Загрузка в шлюз комиссионных вознаграждений по валютному рынку
9  201           Загрузка в БО заявок на клиентские сделки
10 202           Загрузка в БО отчета биржи
11 203           Загрузка в БО клиентских сделок с ценными бумагами
12 204           Загрузка в БО клиентских сделок, принятых на клиринг
13 401      'S'  Расчет комиссий
14 701      'S'  Расчет Лимитов Фондовый рынок
15 702      'S'  Расчет Лимитов Фондовый рынок и ЕДП  
16 703      'S'  Сверка и выгрузка общего файла лимитов  
*/

/*СПБ
1  4010   chr(0)  Загрузка в шлюз заявок на сделки на бирже СПБ
2  4020   chr(0)  Загрузка в шлюз заключенных сделок на бирже СПБ
3  4030   chr(0)  Загрузка в шлюз сделок, принятых на клиринг на бирже СПБ
4  4040   chr(0)  Загрузка в шлюз Итоговых нетто-ТО по ФИ на бирже СПБ
5  4050   chr(0)  Загрузка в БО заявок на клиентские сделки на бирже СПБ
6  4060   chr(0)  Загрузка в БО отчета биржи на бирже СПБ
7  4070   chr(0)  Загрузка в БО клиентских сделок с ценными бумагами на бирже СПБ
8  4080   chr(0)  Загрузка в БО клиентских сделок, принятых на клиринг на бирже СПБ
9  4090   chr(0)  Создание операций движения ДС на бирже СПБ
10 4100   chr(0)  Загрузка в БО Итоговых нетто-ТО по ФИ на бирже СПБ
11 4110   chr(0)  Загрузка в шлюз движений ДС на бирже СПБ
12 4120   chr(0)  Загрузка в БО движений ДС на бирже СПБ
13 4130   chr(0)  Загрузка котировок с СПБ
*/

/*Срочный рынок
1  111   chr(0)   Загрузка в шлюз биржевых данных
2  211   chr(0)   Загрузка в БО производных инструментов
3  212   chr(0)   Загрузка в БО курсов финансовых инструментов
4  213   chr(0)   Загрузка в БО биржевых операций с ПИ
5  214   chr(0)   Загрузка в БО итоги дня по позиции ПИ
6  215   chr(0)   Загрузка в БО разовой комиссии
7  216   chr(0)   Загрузка в БО отчета биржи
8  217   chr(0)   Загрузка в БО обработка отчета по операции
9  218   chr(0)   Загрузка в БО информации о гарантийном обеспечении
10 412   chr(0)   Инициализация расчетов с биржей
11 411   chr(0)   Обработка сделок
12 413   chr(0)   Обработка расчетов с биржей
13 414   chr(0)   Закрытие расчетов с биржей
14 711   chr(0)   Расчет лимитов Срочный рынок 
15 112   chr(0)   Загрузка в шлюз заявок-поручений на сделки
16 219   chr(0)   Загрузка в БО заявок-поручений на сделки

*/

/*Валютный рынок
1  121 chr(0) Загрузка в Шлюз переводов и комиссий
2  221 chr(0) Загрузка в БО переводов
3  122    'S' Загрузка в Шлюз итогов валютных торгов
4  222    'S' Загрузка в БО итогов валютных торгов
5  226 chr(0) Загрузка в БО комиссий
6  123 chr(0) Загрузка в Шлюз сделок
7  223 chr(0) Загрузка в БО сделок 
8  224 chr(0) Загрузка в БО Отчета биржи
9  421 chr(0) Расчёт комиссий
10 721 chr(0) Расчет лимитов Валютный рынок
*/

var Comiss_ConSetArr = TArrCollector();
    Comiss_ConSetArr.AddItem(clConSet(101,ProcSuccess_Status)); //Загрузка в шлюз заявок на сделки
    Comiss_ConSetArr.AddItem(clConSet(108,ProcSuccess_Status)); //Загрузка в шлюз заявок на внебиржевые сделки
    Comiss_ConSetArr.AddItem(clConSet(102,ProcSuccess_Status)); //Загрузка в шлюз заключенных сделок
    Comiss_ConSetArr.AddItem(clConSet(109,ProcSuccess_Status)); //Загрузка в шлюз заключенных внебиржевых сделок
    Comiss_ConSetArr.AddItem(clConSet(103,ProcSuccess_Status)); //Загрузка в шлюз сделок, принятых на клиринг
    Comiss_ConSetArr.AddItem(clConSet(105,ProcSuccess_Status)); //Загрузка в шлюз движений КСУ
    Comiss_ConSetArr.AddItem(clConSet(106,ProcSuccess_Status)); //Загрузка в шлюз Итоговых нетто-ТО по ФИ
    Comiss_ConSetArr.AddItem(clConSet(107,ProcSuccess_Status)); //Загрузка в шлюз комиссионных вознаграждений  по валютному рынку
    Comiss_ConSetArr.AddItem(clConSet(201,ProcSuccess_Status)); //Загрузка в БО заявок на клиентские сделки
    Comiss_ConSetArr.AddItem(clConSet(202,ProcSuccess_Status)); //Загрузка в БО отчета биржи
    Comiss_ConSetArr.AddItem(clConSet(203,ProcSuccess_Status)); //Загрузка в БО клиентских сделок с ценными бумагами
    Comiss_ConSetArr.AddItem(clConSet(204,ProcSuccess_Status)); //Загрузка в БО клиентских сделок принятых на клиринг

    Comiss_ConSetArr.AddItem(clConSet(4010,ProcSuccess_Status)); //Загрузка в шлюз заявок на сделки на бирже СПБ
    Comiss_ConSetArr.AddItem(clConSet(4020,ProcSuccess_Status)); //Загрузка в шлюз заключенных сделок на бирже СПБ
    Comiss_ConSetArr.AddItem(clConSet(4030,ProcSuccess_Status)); //Загрузка в шлюз сделок, принятых на клиринг на бирже СПБ
    Comiss_ConSetArr.AddItem(clConSet(4040,ProcSuccess_Status)); //Загрузка в шлюз Итоговых нетто-ТО по ФИ на бирже СПБ
    Comiss_ConSetArr.AddItem(clConSet(4050,ProcSuccess_Status)); //Загрузка в БО заявок на клиентские сделки на бирже СПБ
    Comiss_ConSetArr.AddItem(clConSet(4060,ProcSuccess_Status)); //Загрузка в БО отчета биржи на бирже СПБ
    Comiss_ConSetArr.AddItem(clConSet(4070,ProcSuccess_Status)); //Загрузка в БО клиентских сделок с ценными бумагами на бирже СПБ
    Comiss_ConSetArr.AddItem(clConSet(4080,ProcSuccess_Status)); //Загрузка в БО клиентских сделок, принятых на клиринг на бирже СПБ

    Comiss_ConSetArr.AddItem(clConSet(711,ProcSuccess_Status));  //Расчет лимитов Срочный рынок 

    Comiss_ConSetArr.AddItem(clConSet(721,ProcSuccess_Status));  //Расчет лимитов Валютный рынок 

var StockLimit_ConSetArr = TArrCollector();
StockLimit_ConSetArr.AddItem(clConSet(CALC_STOCK_COMISS_SUBTYPE, ProcSuccess_Status));

var EDPLimit_ConSetArr = TArrCollector();
//EDPLimit_ConSetArr.AddItem(clConSet(CALC_STOCK_LIMIT_SUBTYPE, ProcSuccess_Status));
//DEF-31513 
EDPLimit_ConSetArr.AddItem(clConSet(CALC_STOCK_COMISS_SUBTYPE, ProcSuccess_Status));

var UnloadLimit_ConSetArr = TArrCollector();
UnloadLimit_ConSetArr.AddItem(clConSet(CALC_EDP_LIMIT_SUBTYPE,ProcSuccess_Status));

var VMBegin_ConSetArr = TArrCollector();
VMBegin_ConSetArr.AddItem(clConSet(CALC_EDP_LIMIT_SUBTYPE,ProcSuccess_Status));

var HoldLucre_ConSetArr = TArrCollector();
HoldLucre_ConSetArr.AddItem(clConSet(702,ProcSuccess_Status));

private macro GetArrCondition(SubType)
  var ArrCond;

  if(SubType == CALC_STOCK_COMISS_SUBTYPE)
    ArrCond = Comiss_ConSetArr;
  end;

  if(SubType == CALC_STOCK_LIMIT_SUBTYPE)
    ArrCond = StockLimit_ConSetArr;
  end;

  if(SubType == CALC_EDP_LIMIT_SUBTYPE)
    ArrCond = EDPLimit_ConSetArr;
  end;

  if(SubType == UNLOAD_LIMIT_SUBTYPE)
    ArrCond = UnloadLimit_ConSetArr; 
  end;

  if (SubType == VM_DEAL_START_SUBTYPE)
    ArrCond = VMBegin_ConSetArr; 
  end;

  if (SubType == 611) /*Удержание НДФЛ по мат. выгоде*/
    ArrCond = HoldLucre_ConSetArr; 
  end;
  return ArrCond;
end;


private macro IIF(cond, ret1, ret2)
   if (cond)
     return ret1;
   else
     return ret2;
   end;
end;

/*проверка необходимости выполнения пункта*/
/*пункт должен быть в статусе "Не обработан"*/
private macro ItemNeedProcessed(ProcSUBTYPE, procDate)

  var ProcStatus = GetProcessStatus(ProcSUBTYPE, procDate);
  if ((ProcStatus == NotProccesed_Status) or (ProcStatus == SkipProc_Status))
    return True;
  end;

  return False;
end;

/*проверка выполнения необходимых условий для пункта*/
/*Для разных пунктов - разные критерии*/
private macro CheckConditionForExec(ProcSUBTYPE, procDate)
  var ArrCondition = GetArrCondition(ProcSUBTYPE);
  if (valtype(ArrCondition) == v_undef)
    return False;
  end;

  var ob = ArrCondition.createEnum;
  var pStatus;
  while (ob.next)
    pStatus = GetProcessStatus(ob.item.ConType, procDate);
    if (pStatus != ob.item.ConStatus)

      if(pStatus == -1)
       if ((ob.item.ConType == CALC_STOCK_COMISS_SUBTYPE) or (ob.item.ConType == CALC_STOCK_LIMIT_SUBTYPE) or (ob.item.ConType == CALC_EDP_LIMIT_SUBTYPE))
         return false;
       else
         continue;
       end;
      end;

      return false;
    end;
  end;
  return true;
end;

private macro RunProcItem(pItemSubtype, dt, proctype)
   AutoProcess("-exec:101 -date:"+string(dt:f)+" -opernum:1 -dialogflag:0 -processtype:"+string(proctype)+" "+IIF(FIX_DATE != date(0,0,0), "-fixdate:1 ","")+"-subtypes:" + pItemSubtype);
end;                                                                                   
                                                                                       
                                                                                       
macro RunProcess(ProcParam, dt, proctype)                                                                     

   var CheckProcSUBTYPE = ProcParam.Value(0);

   var ProcParamString = "";
   var ob = ProcParam.createEnum;
   ProcParamString = CheckProcSUBTYPE;
   ob.next;
   while (ob.next)
       ProcParamString = ProcParamString + ",";
       ProcParamString = ProcParamString + ob.item;
   end;

   if((ItemNeedProcessed(CheckProcSUBTYPE, dt)) and (CheckConditionForExec(CheckProcSUBTYPE, dt) )  )
     RunProcItem(ProcParamString, dt, proctype);
   end;

end;

macro RunProcess_dss(p1, p2, p3)

  private macro GetProcParamsArr(pSTR, pArr)
   macro emp_str(p_par)
      if ((p_par == null) or (p_par == nullval) or (p_par == strfor(1)) or (p_par == strfor(0)) or (p_par == "01.01.0001"))
         return "";
      end;         
      return p_Par;
   end;

   pSTR = emp_str(pSTR);
   pSTR = strsubst(pSTR, strfor(0), "");
   pSTR = strsubst(pSTR, strfor(1), "");
   pSTR = strsubst(pSTR, strfor(7), "");
   pSTR = strsubst(pSTR, strfor(9), "");
   pSTR = strsubst(pSTR, strfor(10), "");
   pSTR = strsubst(pSTR, strfor(13), "");
   pSTR = strsubst(pSTR, " ", "");
   pSTR = trim(pSTR);  

   var rArr = TArrCollector(); 

   var nParam = "";
   var vArr = StrSplit2(pSTR,1); 
   var ob = vArr.createEnum;

   while (ob.next)
    if (ob.item != ";")
      nParam = nParam + ob.item;
    else
      if(StrIsNumber(nParam))
        rArr.AddItem(nParam);
      end;
      nParam = "";
    end;
   end;
   if(StrIsNumber(nParam))
     rArr.AddItem(nParam);
   end;
    return rArr;
  end;
  var ProcParam;
  GetParm(2, ProcParam);
  var i;
  var fixdate;
  i = index(strlwr(ProcParam), "-fixdate:{");
  if(i > 0)
    i = i + 10;
    fixdate= "";
    while((codefor(substr(ProcParam,i,1)) >= codefor("0")) and (codefor(substr(ProcParam,i,1)) <= codefor("9")))
      fixdate = fixdate + substr(ProcParam,i,1);
      i = i + 1;
    end;
    if(strlen(fixdate) > 0)
      fixdate = int(fixdate)
    else
      fixdate = 0;
    end;
  else
    fixdate = 0;
  end;
  var processtype;
  /*-processtype:*/
  i = index(strlwr(ProcParam), "-processtype:");
  if(i > 0)
    i = i + 13;
    processtype = "";
    while((codefor(substr(ProcParam,i,1)) >= codefor("0")) and (codefor(substr(ProcParam,i,1)) <= codefor("9")))
      processtype = processtype + substr(ProcParam,i,1);
      i = i + 1;
    end;
    if(strlen(processtype) > 0)
      processtype = int(processtype)
    else
      processtype = 1;
    end;
  else
    processtype = 1;
  end;
  var subtypes;
  /*-processtype:*/
  i = index(strlwr(ProcParam), "-subtypes:{");
  if(i > 0)
    i = i + 11;
    subtypes = "";
    while(codefor(substr(ProcParam,i,1)) != codefor("}")) 
      subtypes = subtypes + substr(ProcParam,i,1);
      i = i + 1;
    end;
  end;
  /*-date:*/
  var dt;
  i = index(strlwr(ProcParam), "-date:{");
  if(i > 0)
    if(StrLwr(substr(ProcParam,i+7,7)) == "curdate")
      dt = {curdate};
    else
      i = i + 7;
      dt = "";
      while(((codefor(substr(ProcParam,i,1)) >= codefor("0")) and (codefor(substr(ProcParam,i,1)) <= codefor("9"))) or (substr(ProcParam,i,1) == "."))
        dt = dt + substr(ProcParam,i,1);
        i = i + 1;
      end;
      dt = date(dt)
    end;
  else
    dt = {curdate};
  end;
/*  Переопределение dt */
  if( (not fixdate))
     if ( GetDateAfterWorkDays (dt, 0) != date () ) //выходной
         exit (1);
     else 
         dt = GetDateAfterWorkDays (date ()/*dt*/, -1);
     end;
  else
     dt = GetDateAfterWorkDays (date ()/*dt*/, -1);
     FIX_DATE = dt;
  end;
  ProcParam = GetProcParamsArr(subtypes);
  if(ProcParam.size > 0)
    RunProcess(ProcParam, dt, processtype);
  end;

  return SsExecResult(SS_RESPONSE_END);
onError(err)
  return SsExecResult(SS_RESPONSE_FATAL, GetFullErrorMessage(err));
end;


