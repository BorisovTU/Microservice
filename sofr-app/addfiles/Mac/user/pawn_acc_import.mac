/**
 @file pawn_acc_import.mac
 @brief Первоначальная загрузка счетов в примечание векселей, находящихся в залоге на момент вывода задачи в релиз

  # tag
 - functional_block:Векселя
 - code_type:One_time
 - Векселя банка
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |14.03.2025 |Топорков Д.В.|BOSS-7047 BOSS-7103                                       | Создание
 */
import "StorageInfo.mac", "logger.mac", "xls_parser.mac";
import rsd;
import vsbnrfd;

private const LOGTAG = "LOG";

private const COLUMN_SECURITY_TYPE = 4;
private const COLUMN_SECURITY_SERIE = 5;
private const COLUMN_SECURITY_NUMBER = 6;
private const COLUMN_SECURITY_DATE = 7;
private const COLUMN_ACCOUNT_NUMBER = 20;

private const BOSS7047_FEATURE_SWITCH_REGVAL_NAME = "РСХБ\\ИНТЕГРАЦИЯ\\ОБРАБОТКА ЗАПР ОТ ЦФТ BOSS-7047";

// Получить настройки для временных файлов
private macro GetStorageInfo(): CStorageInfo
  var storageInfo = CStorageInfo("PawnAcc", "xls");
  var temporaryFile: CFile = storageInfo.GetFile(TXTTAG, TXTTAG); // временный xls файл

  temporaryFile.isTemporary = true;
  storageInfo.AddFile(TXTTAG, LOGTAG, temporaryFile.name, "txt", true); // временный лог файл
  
  return storageInfo;
end;

// Преобразование смещения в днях (excel) в дату
private macro GetDateFromIntervalDay ( interval ) : date
  var startDate = date("01.01.1900");
  return DateShift(startDate, int(interval) - 2);
  
OnError(err)
  return null;
end;

// Найти id векселя по его параметрам
private macro GetBnrID(serie, number, issueDate)
  var result = 0;
  var cmd = RsdCommand("SELECT t_BCID " +
                         "FROM dVSBanner_dbt " +
                        "WHERE t_BCSeries = :BCSeries " +
                          "AND t_BCNumber = LPAD(:BCNumber, 20, ' ') " +
                          "AND t_issueDate = :issueDate");
    
  cmd.AddParam("BCSeries", RSDBP_IN, serie);
  cmd.AddParam("BCNumber", RSDBP_IN, number);
  cmd.AddParam("issueDate", RSDBP_IN, issueDate);
  var rs = RSDRecordset(cmd);
  
  if (rs.MoveNext())
    result = Int(rs.Value("t_BCID"));
  end;
  
  return result;
  
OnError(err)
  return 0;
end;

// Процедура обработки файла
private macro ProcessFile(filename: String, log: CTxtLogger)
  var poi: CExcelPoi = CExcelPoi();
  
  if (not poi.Open(filename))
    RunError("POI ошибка открытия файла " + filename);
  end;
  
  if ((Index(String(poi.CellValueByRowAndCol(1,1)), "Данные о собственных векселях Банка") == 0) or
      (Index(String(poi.CellValueByRowAndCol(2,1)), "Эмитент") == 0))
    RunError("Заголовок таблицы отличается от ожидаемого");
  end;
  
  var lastRowIndex = poi.GetLastRowNum();
  var currentRowIndex = 3; // После заголовка
  var errorCount = 0;
  var totalCount = 0;
  var updatedCount = 0;
  
  InitProgress(-1, "Выполнение процедуры импорта данных", "Просмотр данных...");
    
  while (currentRowIndex <= lastRowIndex)
    var isValid = true;
    var bcID = 0;
    var pawnAccount;
  
    if (isValid and (String(poi.CellValueByRowAndCol(currentRowIndex, COLUMN_SECURITY_TYPE)) != "Вексель"))
      log.Error("    Ячейка D" + currentRowIndex + ": некорректный тип ЦБ. Ожидается 'Вексель'");
      isValid = false;
    end;
    
    if (isValid and ((pawnAccount = Trim(String(poi.CellValueByRowAndCol(currentRowIndex, COLUMN_ACCOUNT_NUMBER)))) == ""))
      log.Error("    Ячейка T" + currentRowIndex + ": не указано значение внебалансового лицевого счета");
      isValid = false;
    end;
    
    if (isValid)
      var serie = String(poi.CellValueByRowAndCol(currentRowIndex, COLUMN_SECURITY_SERIE));
      var number = String(poi.CellValueByRowAndCol(currentRowIndex, COLUMN_SECURITY_NUMBER));
      var issueDate = poi.CellValueByRowAndCol(currentRowIndex, COLUMN_SECURITY_DATE);
      
      if (ValType(issueDate) != V_DATE)
        log.Error("    Ячейка G" + currentRowIndex + ": значение не является датой");
        isValid = false;
      end;
    end;
    
    if (isValid)
      bcID = GetBnrID(serie, number, issueDate);
      
      if (bcID <= 0)
        log.Error("    Строка " + currentRowIndex + ": не найден вексель " + serie + " " + number + " " + issueDate);
        isValid = false;
      end;
    end;
    
    if (isValid)
      var objID = String(bcID);
      objID = SubStr("0000000000", StrLen(objID) + 1, 10) + objID;
      var currentAccount = Trim(readNoteForObject(OBJTYPE_FICERT, ObjID, 102, date(31, 12, 9999)));
      
      if (currentAccount != pawnAccount)
        if (addNoteForObject(OBJTYPE_FICERT, objID, 102, pawnAccount, Date()))
          log.Error("    Строка " + currentRowIndex + ": ошибка установки примечания 102 для веселя");
          errorCount = errorCount + 1;
        else
          updatedCount = updatedCount + 1;
        end;
      end;
    else
      errorCount = errorCount + 1;
    end;
    
    totalCount = totalCount + 1;
    currentRowIndex = currentRowIndex + 1;
    UseProgress(totalCount);
  end;
  
  RemProgress();
  
  log.Info("=================================================================");
  log.Info("Всего найдено записей: " + totalCount);
  log.Info("      получено ошибок: " + errorCount);
  log.Info(" обновлено примечаний: " + updatedCount);
  
  poi.Close();
  
OnError(err)
  log.Error("Ошибка (" + String(err.Code) + ") " + err.message);
  
  if (poi)
    poi.Close();
  end;
end;

// Процедура импорта
macro ImportAccounts()
  var isOnTerminal: Bool = not IsStandAlone();
  var storage = GetStorageInfo();
  var selectedFilename: String;
  var processingFilename: String = storage.FilePath(TXTTAG, TXTTAG);
  var logFilename: String = storage.FilePath(TXTTAG, LOGTAG);
  var log = CTxtLogger(logFilename);
  
  log.UseTimestamp(false);
  log.Info("ПРОТОКОЛ ИМПОРТА ЛИЦЕВЫХ СЧЕТОВ ДЛЯ ВЕКСЕЛЕЙ НАХОДЯЩИХСЯ В ЗАЛОГЕ");
  log.Info("=================================================================");
  
  if (SelectFile(selectedFilename, "*.xls", "Выберите файл для импорта", 0, isOnTerminal))
    log.Info("Выбран файл для импорта: " + selectedFilename);
    
    if (isOnTerminal)
      selectedFilename = "$" + selectedFilename;
    end;
    
    if (not CopyFile(selectedFilename, processingFilename))
      log.Error("Ошибка копирования файла\n      " + selectedFilename  + "\n  во временный файл\n      " + processingFilename);
    else
      log.Info("Создан временный файл " + processingFilename);
      ProcessFile(processingFilename, log);
    end;
  else
    log.Info("Файл не выбран, процедура импорта отменена.");
  end;
  
  log.Close();
  ViewFile(logFilename);
end;

private macro IsFeatureEnable()
   var isEnable = false;
   var error: integer;
   
   GetRegistryValue(BOSS7047_FEATURE_SWITCH_REGVAL_NAME, V_BOOL, isEnable, error);
   
   if (error > 0)
      isEnable = false;
   end;

   return isEnable;
end;

// ======== Точка входа ========
if (not IsFeatureEnable())
  msgbox("Не включена настройка " + BOSS7047_FEATURE_SWITCH_REGVAL_NAME);
  exit(1);
end;

ImportAccounts();
Exit(1);