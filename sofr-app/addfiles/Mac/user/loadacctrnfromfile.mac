import likepy;
import cb_sql;
import oralib;
import secinter, "dl_car.mac", bankinter;
import "dlutils.mac";
import "xls_parser.mac";


private macro GetChapter(Account)

  var sql, cmd, rs;
  sql = "SELECT t.t_chapter FROM daccount_dbt t WHERE  t.t_account = :p_Account";
  cmd = RSDCommand(sql);
  cmd.AddParam("p_Account",RSDBP_IN, Account);
  rs = RSDRecordSet(cmd);
  if (rs.MoveNext()) 
    return rs.Value(0);
  end;

end;

private macro GetCurrencyFIID(FiCode:string)
  if (FiCode == "")
    return -1;
  end;
  var cmd = DL_RSDCommand(
     " SELECT t_fiid "
    +"   FROM DFININSTR_DBT "
    +"  WHERE T_Fi_CODE = ? AND ROWNUM = 1 "
    );
  cmd.AddParam(FiCode);

  var ds = cmd.Execute();
  if (ds.MoveNext())
    return SQL_ConvTypeInteger(ds.fiid);
  end;

  return -1;
end;

PRIVATE class AccTrnParam ()
  var Carry_date = date(0,0,0),
      Number = "",
      DebCur = "",
      DebAcc = "",
      DebSum = $0,
      CrCur = "",
      CrAcc = "",
      CrSum = $0,
      Ground = "",
      Number_Pack = "",
      Oper = 1;
end;

PRIVATE macro ExecuteParse()
  var i = 2;
  var file_xlsx;
  var AccTrn = TArray();
  var j = NULL;
  var acctrnCount = 0, acctrnCountErr = 0;
  var size = 0;
  record PayerAccount("account");
  record ReceiverAccount("account");
  var FilePath = "";
  var isTerm = false;
  if (not isStandalone())
    var choise = DL_Menu(makeArray("Терминал","Сервер"),"Где ищем файл?");
    if (choise == -1)
      return;
    end;
    if (choise == 0)
      isTerm = true;
    end;
  end;
  if (SelectFile ( FilePath, "*.xlsx,*.xls", "Выберите файл к обработке", 0, isTerm ))
    FilePath = IIF(isTerm, "$", "") + FilePath;
    file_xlsx = CExcelPoi();
    var fileMov = TempFileToServerMover();
    fileMov.CreateTempFromAbsolutPath(FilePath);
    if (not file_xlsx.open(fileMov.GetTempFilePath()))
      RunError("Не удалось открыть excel файл " + FilePath);
    end;
    var lastRowNumb = file_xlsx.GetLastRowNum()+1;
    var curRow = 0;
    while ((curRow=curRow+1) <= lastRowNumb)
      if (ValType(file_xlsx.CellValue("A"+curRow)) == V_UNDEF)
        continue;
      end;
      size = AccTrn.size;
      AccTrn[size] = AccTrnParam();
      AccTrn[size].Carry_date = date(file_xlsx.CellValueByRowAndCol(curRow, 1));
      AccTrn[size].Number = string(IIF(valtype(file_xlsx.CellValueByRowAndCol(curRow, 2)) == V_DOUBLE, int(file_xlsx.CellValueByRowAndCol(curRow, 2)), file_xlsx.CellValueByRowAndCol(curRow, 2)));
      AccTrn[size].DebCur = string(IIF(valtype(file_xlsx.CellValueByRowAndCol(curRow, 3)) == V_DOUBLE, int(file_xlsx.CellValueByRowAndCol(curRow, 3)), file_xlsx.CellValueByRowAndCol(curRow, 3)));
      AccTrn[size].DebAcc = string(file_xlsx.CellValueByRowAndCol(curRow, 4));
      AccTrn[size].DebSum = money(file_xlsx.CellValueByRowAndCol(curRow, 5));
      AccTrn[size].CrCur = string(IIF(valtype(file_xlsx.CellValueByRowAndCol(curRow, 6)) == V_DOUBLE, int(file_xlsx.CellValueByRowAndCol(curRow, 6)), file_xlsx.CellValueByRowAndCol(curRow, 6)));
      AccTrn[size].CrAcc = string(file_xlsx.CellValueByRowAndCol(curRow, 7));
      AccTrn[size].CrSum = money(file_xlsx.CellValueByRowAndCol(curRow, 8));
      AccTrn[size].Ground = string(file_xlsx.CellValueByRowAndCol(curRow, 9));
      AccTrn[size].Number_Pack = string(IIF(valtype(file_xlsx.CellValueByRowAndCol(curRow, 10)) == V_DOUBLE, int(file_xlsx.CellValueByRowAndCol(curRow, 10)), file_xlsx.CellValueByRowAndCol(curRow, 10)));
      AccTrn[size].Oper = int(file_xlsx.CellValueByRowAndCol(curRow, 11));
    end;
    
    for(j, AccTrn)
      ClearRecord(PayerAccount);
      ClearRecord(ReceiverAccount);  

      var tr = RsbAccTransaction();
      tr.Chapter         = GetChapter(j.DebAcc);
      tr.Date_Carry      = j.Carry_date;
      tr.Number_Pack     = j.Number_Pack;
      tr.Numb_Document   = j.Number;
      tr.Ground          = j.Ground;
      tr.Department      = {OperDprt};
      tr.FIIDPayer       = GetCurrencyFIID(j.DebCur);
      tr.FIIDReceiver    = GetCurrencyFIID(j.CrCur);
      tr.AccountPayer    = j.DebAcc;
      tr.AccountReceiver = j.CrAcc;

      if(DL_GetAccount(tr.Chapter, tr.FIIDPayer, tr.AccountPayer, PayerAccount)
        and DL_GetAccount(tr.Chapter, tr.FIIDReceiver, tr.AccountReceiver, ReceiverAccount))
        tr.Shifr_Oper = DL_GetShifrOper(1, PayerAccount, ReceiverAccount);
      end;

      Tr.Oper = j.Oper;
      tr.SumPayer    = j.DebSum;
      tr.SumReceiver = j.CrSum;
      print("Выполняем проводку в дату " + tr.Date_Carry + " со счета " + tr.AccountPayer + " на счет " + tr.AccountReceiver);
      if( not tr.Carry() )
        println(" : Ошибка");
        acctrnCountErr = acctrnCountErr + 1;
      else
        println(" : Удачно");
        acctrnCount = acctrnCount + 1;
      end;
    end;
  end;

  println("Выполнено проводок удачно: " + acctrnCount + ", с ошибкой: " + acctrnCountErr);

end;

ExecuteParse();