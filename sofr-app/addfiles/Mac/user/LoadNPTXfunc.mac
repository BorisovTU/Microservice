/*
$Name:        LoadNPTXfunc.mac
$Module:      Ценные бумаги
$Description: Основная реализация процедуры загрузки выводов ДС
*/

/*----------------------------------------------------------*/
/*BIQ-1291 Загрузка выводов ДС                              */
/*Дорохов А.Н.                                              */
/*Основная реализация процедуры загрузки выводов ДС         */
/*----------------------------------------------------------*/

import rsd, rsexts, dlcontrfunc;
import "usr_open_files.mac";
import BankInter;
import "u_common_email_utils.mac";

/*Паттерны для проверки корректности переданных данных*/
private const PTRN_FN_DTTM = "\\d{4}-\\d{1,2}-\\d{1,2}\\s\\d{1,2}:\\d{1,2}:\\d{1,2}\\Z"; //паттерн даты


//private CONST ENCODE = "utf8"; //"rsansi" "rsoem";
private CONST ENCODE = "utf8";

private const NPTXOP_IMPORT_path = "РСХБ\\ДИРЕКТОРИИ\\NPTXOP_IMPORT";

private const FN_IDOP         = "Идентификатор_в_ДБО";
private const FN_IDCLIENT     = "id_клиента_в_АБС";
private const FN_EKK          = "Единый_код_клиента";
private const FN_CONTRACT     = "Номер_договора_БО_ИИС";
private const FN_MPLACE       = "Торговая_площадка";
private const FN_TYPEOP       = "TYPEOP";
private const FN_CUR          = "Валюта";
private const FN_AMOUNT       = "Сумма";
private const FN_DTTM         = "Дата_поручения";
private const FN_ACCOUNT      = "Счет_зачисления";
private const FN_FILIAL       = "Номер_филиала_зачисления";
private const FN_ALLBALANCE   = "Весь_остаток";

private const pfx = "*";
private const ext = ".csv";
private const OUTDIR = "..\\Export"; 

private var _MMVB_ID = MMVB_ID();
private var _SPB_ID = SPB_ID();
var flog;

var rec_count = 0;
var rec_count_success = 0;
var rec_count_err;

class Logger()

    macro RepMainHeader()
       var str;
       str =     "                             ПРОТОКОЛ ЗАГРУЗКИ ОПЕРАЦИЙ ВЫВОДА ДС  \n";
       str = str+"\n";
       println(str);                                                                                               
    end;

    macro RepHeader(pFilename)
       [ ];
       [Дата загрузки  : #](date());
       [Время загрузки : ########](time());
       [Обрабатываемый файл : #](pFilename);
       [┌─┬────────────────┬──────────────────────┬───────────────────┬────────────────────┬───────┬───────────┬─────────────────────┬─────────────────────────┬──────────────────────────────────────────────────────────────────────────────────────────────────────────────┐
        │ │id_клиента_в_АБС│ Номер_договора_БО_ИИС│ Единый_код_клиента│  Торговая_площадка │ Валюта│ Сумма     │Счет_зачисления      │  Дата_поручения         │                                                                                                              │
        ├─┼────────────────┼──────────────────────┼───────────────────┼────────────────────┼───────┼───────────┼─────────────────────┼─────────────────────────┼──────────────────────────────────────────────────────────────────────────────────────────────────────────────┤];
    end;                   

    macro RepFooter(suc, er, tot)                                                       
       [└─┴────────────────┴──────────────────────┴───────────────────┴────────────────────┴───────┴───────────┴─────────────────────┴─────────────────────────┴──────────────────────────────────────────────────────────────────────────────────────────────────────────────┘];
       [Загружено успешно: #](rec_count_success);
       [Ошибки обработки : #](rec_count - rec_count_success);
       [Всего            : #](rec_count);
       [Время            : ########](time());
    end;

    macro RepStr(p0,p1,p2,p3,p4,p5,p6,p7,p8,p9)
        [│#│################│######################│###################│####################│#######│###########│#####################│#########################│##############################################################################################################│]
        (p0:l,p1:l,p2:l,p3:l,p4:l,p5:l,p6:l,p7:l,p8:l,p9:l:w);
    end;
end;

private class FileFields()
   private var fields = TArray();

   private class FileField(_name, _id)
      var name, id;
      name = _name;
      id   = _id;
   end;

   macro FieldsCount()
      return fields.size;
   end;

   macro addField(_name, _id)
      fields(fields.size) = FileField(_name, _id);
   end;

   macro GetId(_name)
      var i=0;
      for (i, fields) 
         if (StrUpr(i.name) == StrUpr(_name))
            return i.id;
         end;
      end;
      return -1;
   end;

end;

private macro GenerateOPCodeBySYQ(pEKK, pYear, pDBO)
   var OPCode;
   var sql = "begin \n ? := user_NPTXFUNC.GenerateOpCodebySyq(?, ?, ?); \n end;";
   var cmd = RSDCommand(sql);
       cmd.AddParam("p_Code", RSDBP_OUT, V_STRING);
       cmd.AddParam("pEKK", rsdbp_in, pEKK);
       cmd.AddParam("pYear", rsdbp_in, pYear);
       cmd.AddParam("pDBO", rsdbp_in, pDBO);
       cmd.Execute();

   OPCode = SQL_ConvType(cmd.value("p_Code"));
   return OPCode;
end;


// отправка уведомления
macro SendNotification(AttachementShortName, AttachementFullName)
  var Eml, EmailText, Subject, v_email_processor;
  
  var err_reg;
  var email_group;
  GetRegistryValue("РСХБ\\ИНТЕГРАЦИЯ\\SEND_NOTIFY_BO", V_INTEGER, email_group, err_reg);
  if (err_reg)
     // MsgBox("Ошибка получение значений настроек \"РСХБ\\ИНТЕГРАЦИЯ\\SEND_NOTIFY_BO\"");
    return 0; 
  end;

      
  Subject = "Отчет о загрузке выводов ДС";
  EmailText = string("Добрый день!",
                     "\nПолучен результат работы процедуры загрузки выводов денежных средств по заданию планировщика",
                     "\nДанное письмо было отправлено автоматически.");

  v_email_processor = c_email_proc_env();
  v_email_processor.m_get_email_group(email_group);
  

  v_email_processor.m_set_msg_head(Subject);
  if (strlen(trim(AttachementShortName)) != 0)
    v_email_processor.m_add_attach_to_list(AttachementFullName);
  end;
  v_email_processor.m_add_row_to_msg_text(EmailText);

  v_email_processor.m_save_to_submit_grp(email_group, true);
  //v_email_processor.m_save_to_submit();

end;


private macro CheckStringByPattern(instr, pattern)
  var res = 0;
  var sql = String( "with parm as\n"
                   ," (select :instr as str,\n" 
                   ,"         :pattern as ptrn\n" 
                   ,"    from dual)\n" 
                   ,"select count(1) as match\n" 
                   ,"  from parm\n" 
                   ," where regexp_like(parm.str, parm.ptrn)");
  var cmd = dl_RSDCommand();
      cmd.AddParam(instr);     //'2021-10-25 12:45:23'
      cmd.AddParam(pattern);   "\\d{4}-\\d{1,2}-\\d{1,2}\\s\\d{1,2}:\\d{1,2}:\\d{1,2}\\Z"; //паттерн даты
  var ds = cmd.execute(sql);
  if (ds.movenext)    
    res = ds.match;
  end;
  return res;
end;

private macro GetStrRegVal(regPath:string)
  var stat = 0, path = "";
  GetRegistryValue(regPath, V_STRING, path, stat);
  if(stat != 0)
    msgbox("Ошибка при получении значения настройки: " + regPath);
  elif(path == "")
    stat = 1;
    msgbox("Не задано значение настройки: " + regPath);
  end;
  return path;
end;

private macro getListFiles( pfx:string, ext:string, filePath:string, List:@object )
  var ErrStr = "";
  if(filePath == "")
    return 1;
  end;

  var filemask = pfx + ext;
  List = TDirList(filePath + filemask, "f");
  List.Sort(0);

  if(List.Count == 0)
    return 1;
  else
    return 0;
  end;
end;

private macro ProcessFileFunc()
  return "ОбработатьФайл";
end;

private macro ProcessedDate(date_val)
  var day, month, year;
  DateSplit(date_val,day,month,year);
  return L_Z(year,4) + "-" + L_Z(month,2) + "-" + L_Z(day,2);
end;

private macro ResultCopyFile(done:bool, filePath:string, fileName:string)
  var stat = 0;
  var filePath_new, fullPathName_new;

  if(done)
    filePath_new = filePath + "Done\\";
  else
    filePath_new = filePath + "Err\\";
  end;

  MakeDir(filePath_new);
  filePath_new = filePath_new + string(ProcessedDate({curdate})) + "\\";
  MakeDir(filePath_new);

  fullPathName_new = filePath_new + fileName;

  if(not CopyFile(filePath + fileName, fullPathName_new) )
    stat = 1;
    flog.RepStr("X","","","","","","","","","Ошибка. Невозможно сохранить файл "+fileName+" в "+fullPathName_new+".");
  elif(not RemoveFile(filePath + fileName))
    stat = 1;
    flog.RepStr("X","","","","","","","","","Ошибка. Невозможно удалить файл "+fileName+" в "+filePath+".");
  end;

  return stat;
end;

private macro ResultCopyLOG(filePath:string, InfoLogFileFullName:string, InfoLogFileShortName:string)
  var stat = 0;
  var filePath_new, fullPathName_new;

  filePath_new = filePath + "LOG\\";

  MakeDir(filePath_new);
  filePath_new = filePath_new + string(ProcessedDate({curdate})) + "\\";
  MakeDir(filePath_new);

  fullPathName_new = filePath_new + InfoLogFileShortName;

  if(not CopyFile(InfoLogFileFullName, fullPathName_new) )
    stat = 1;
//  elif(not RemoveFile(InfoLogFileFullName))
//    stat = 1;
  end;

  /** копирование на терминал */
  if(not isStandAlone())
    if(not CopyFile(InfoLogFileFullName, "$"+GetCurDir(true)+"\\TxtFile\\"+InfoLogFileShortName))
      stat = 1;
    end;
  end;

  return stat;
end;

private macro ОбработатьФайл(FileName)
  var c_file = c_txt_file();
  file datafile() txt;
  file outfile() txt WRITE;
  SetDelim(";");

  var dt = StrSubSt(string(Date()),".","");
  var tm = StrSubSt(string(time()),":","");

  var p_IDOP        : string = "";
  var p_IDCLIENT    : string = "";
  var p_EKK         : string = "";
  var p_CONTRACT    : string = "";
  var p_MPLACE      : string = "";
  var p_TYPEOP      : string = "20";
  var p_CUR         : string = "";
  var p_AMOUNT      : string = "";
  var p_DTTM        : string = "";
  var p_ACCOUNT     : string = "";
  var p_FILIAL      : string = "";
  var p_ALLBALANCE  : string = "";

  var OpNum = 0;

  var errstr = "";
  var PartyID;

  var dlcontr   = TRecHandler("dlcontr.dbt");
  var sfcontr   = TRecHandler("sfcontr.dbt");
  var dlcontrmp = TRecHandler("dlcontrmp.dbt");

  var DlcontrID = 0;
  var SfcontrID = 0;

  var stat;

  private macro uTrim(pstr:String)
   var a = "\"";
   pStr = Trim(pStr);
   if (SubStr(pstr,1,1) == a)
     pstr = SubStr(pstr,2);
   end;
   if((SubStr(pstr,StrLen(pstr)) == a)) 
     pstr = SubStr(pstr, 1,StrLen(pstr) - 1);
   end;

   return Trim(pStr);
  end;

  private macro PrintLogStr(vErr,vStr)
    flog.RepStr(vErr,p_IDCLIENT,p_CONTRACT,p_EKK,p_MPLACE,p_CUR,dl_IIF(StrLwr(p_ALLBALANCE) == "true","Остаток",p_AMOUNT),p_ACCOUNT,p_DTTM,vStr);
  end;

  private macro ConvertSpecialDate(_DTTM:String)
    var sql, cmd, ds;
    sql = "with parm as (select :dt_spec_string as time from dual) \n" +
          " select to_date(parm.time, 'yyyy-mm-dd HH24:MI:SS') as DTTM from parm";
    cmd = dl_RSDCommand();
    cmd.AddParam(StrSubst ( _DTTM, ".", "-"));
    ds = cmd.Execute(sql);
    if(ds.movenext)
      return ds.DTTM;
    end;
    return DTTM(date(),time());
  end;

  private macro ПолучитьСубъектаПоКоду(CodeKind, Code)
    var query, cmd, DataSet;
    PartyID = 0;
    query =   " select objcode.t_ObjectID "
            + "   from dobjcode_dbt objcode "
            + "  where objcode.t_ObjectType = " + OBJTYPE_PARTY
            + "    and objcode.t_CodeKind = ? " 
            + "    and objcode.t_State = 0 "
            + "    and objcode.t_Code = ? ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(CodeKind);
    cmd.AddParam(Code);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      PartyID = DataSet.ObjectID;
    end;

    return PartyID;
  end;


  private macro ПроверитьКлиента()
    return ПолучитьСубъектаПоКоду(101, p_IDCLIENT);
  end;

  private macro ПроверитьДоговор()
    var cmd = DL_RSDCommand();
    var sql = String("Select dlc.t_dlcontrid, sfc.* from ddlcontr_dbt dlc, dsfcontr_dbt sfc\n",
                      "where 1 = 1\n",
                      "and dlc.t_sfcontrid = sfc.t_id\n",
                      "and sfc.t_servkind = 0\n",
                      "and sfc.t_number = :ContrNum\n",
                      "and sfc.t_partyid = :PartyID\n");
    cmd.AddParam(p_CONTRACT);
    cmd.AddParam(PartyID);
    var ds = cmd.Execute(sql);
    var sql_s, cmd_s, ds_s;
    var servkindsub;
    var _SPB_ID = SPB_ID();

    if(ds.MoveNext)
      DlContrID = ds.DlContrID;
      DL_GetDlContrRH(dlContr, DlContrID);
      if(p_MPLACE == "Биржевой рынок")
        servkindsub = 8;
      elif(p_MPLACE == "Внебиржевой рынок")
        servkindsub = 9;
      else //непонятно что может быть еще, поэтому пусть будет так
        servkindsub = 8;
      end;
      sql_s = String( "select sfc.t_id\n" 
                     ,"  from ddlcontrmp_dbt mp, dsfcontr_dbt sfc\n"  
                     ," where mp.t_dlcontrid = :dlcontrid\n"  
                     ,"   and sfc.t_id = mp.t_sfcontrid\n"  
                     ,"   and t_servkindsub = :servkindsub\n"  
                     ,"   and t_servkind = 1\n"  
                     ,"   and t_marketid != :spb_id;");
      cmd_s = DL_RSDCommand();
      cmd_s.AddParam(DlContrID);
      cmd_s.AddParam(servkindsub);
      cmd_s.AddParam(_SPB_ID);
      ds_s = cmd_s.Execute(sql_s);
      if(ds_s.MoveNext)
        SfcontrID = ds_s.ID;
        DL_GetSfContrRH(sfContr, SfContrID);
        DL_GetRecHandler(dlcontrmp, "SELECT * FROM DDLCONTRMP_DBT WHERE T_DLCONTRID = " + DlContrID + " AND T_MArketID = " + _MMVB_ID);
      end;
      return 1;
    end;
    return 0;
  end;

  private macro ОпределитьEKK()
    p_EKK = dlcontrmp.rec.MPCode;
  end;

  private macro ПроверитьДубльОперации(find_OP, clientID)
    var sql = String( "select rsb_struct.getstring(nt.t_text),\n" 
                     ,"       to_number(nt.t_documentid),\n"  
                     ,"       op.t_code\n"  
                     ,"  from dnotetext_dbt nt, dnptxop_dbt op\n"  
                     ," where nt.t_objecttype = 131\n"  
                     ,"   and nt.t_notekind = 102\n"  
                     ,"   and op.t_client = :ClID\n"
                     ,"   and op.t_dockind = :DocKind\n" 
                     ,"   and op.t_kind_operation = 2037 "
                     ,"   and op.t_subkind_operation = :SubDoc\n "
                     ,"   and op.t_id = to_number(nt.t_documentid) and replace(rsb_struct.getstring(nt.t_text),chr(0)) = :idOP");

    var cmd = DL_RSDCommand();
        cmd.AddParam(clientID);
        cmd.AddParam(DL_WRTMONEY);
        cmd.AddParam(DL_NPTXOP_WRTKIND_WRTOFF);
        cmd.AddParam(p_IDOP);
    var ds = cmd.Execute(sql);
    if(ds.MoveNext)
       setParm(0, ds.Code);
       return 0;
    end;
    return 1;
  end;
  
  macro СформироватьОперацию()
    var rs, cmd, sql;
    var err = 0;
    var _fiid = 0, _acc = 0, _taxacc = 0;
    var BankID,BankCode,BankCodeKind;
    var ObjCat;

    macro ПолучитьВалюту()
      var cmd, ds;
      cmd = dl_RSDCommand();
      cmd.AddParam(DL_IIF(StrUpr(p_CUR)== "RUR","RUB",StrUpr(p_CUR)));
      var sql = String( "select t_fiid\n" 
                       ,"  from dfininstr_dbt\n"  
                       ," where t_ccy = :cur\n"  
                       ,"   and t_fi_kind = 1");
       ds = cmd.Execute(sql);
       if(ds.MoveNext)
         return ds.fiid;
       end;
       return 0;
    end;

    macro ПолучитьСчет(_cur)
      var sql = String( "select mc.t_account, ac.t_accountid\n" 
                       ,"  from dmcaccdoc_dbt mc, daccount_dbt ac\n"  
                       ," where rownum = 1\n"  
                       ,"   and mc.t_catnum = 201\n"  
                       ,"   and mc.T_CURRENCY = :cur\n"  
                       ,"   and mc.t_dockind = 3001\n"  
                       ,"   and mc.t_docid = :sfcontrid\n"  
                       ,"   and ac.t_account = mc.t_account;");
       var cmd = dl_RSDCommand;
           cmd.AddParam(_cur);
           cmd.AddParam(SfcontrID);
       var ds = cmd.Execute(sql);
       if(ds.MoveNext)
         return ds.account/*id*/;
       end;
       
       err = 1;

       return "";
    end;

    macro ПолучитьСчетВывода()
       return ПолучитьСчет(_fiid);
    end;

    macro ПолучитьВалютуНалога()
      return 0; 
    end;

    macro ПолучитьСчетНалога()
      return ПолучитьСчет(ПолучитьВалютуНалога());
    end;

    private macro ПолучитьРеквизитыБанка(_BankID,_BankCode,_BankCodeKind)

      var v_BankID = 0, v_BankCode = "", v_BankCodeKind = 3;

      var sql = String( " select dp.t_partyid from dDP_dep_dbt dp\n" 
                       ,"where dp.t_name = substr(:code, 1, 2) || '00';");
      var cmd = dl_RSDCommand();
          cmd.AddParam(p_FILIAL);
      var ds = cmd.Execute(sql);
      if (ds.movenext)
         v_BankID = ds.partyid;
         v_BankCode = ПолучитьКодСубъекта (v_BankID,v_BankCodeKind);
      end;
      setParm(0,v_BankID);
      setParm(1,v_BankCode);
      setParm(2,v_BankCodeKind);
    end;

//    GenerateReference(1000016, OpNum);
//    OpNum = String(/*p_IDCLIENT*/p_EKK, "/", OpNum);

    var pYear;
    DateSplit(date, null, null, pYear); 
    OpNum = GenerateOPCodeBySYQ(p_EKK, String(pYear), "ДБО" );

    _fiid = ПолучитьВалюту();
    _acc  = ПолучитьСчетВывода();

    if(_acc == "")
       PrintLogStr("X", "Ошибка. Счет вывода не найден.");
       return false;
    end;

    ПолучитьРеквизитыБанка(BankID,BankCode,BankCodeKind);

    sql = "DECLARE                        "+
          "  v_ret_id NUMBER(10);         "+
          "BEGIN                          "+
          "  user_NPTXFUNC.AddTransfer(:p_KindOP,       "+
          "              :p_OperCode,     "+
          "              :p_OperDate,     "+
          "              :p_Time,         "+
          "              :p_PartyID,      "+
          "              :p_ContrID,      "+
          "              :p_Amount,       "+
          "              :p_Account,      "+
          "              :p_FIID,         "+
          "              :p_Filial,       "+
          
          "              :p_CalcNOB,      "+
          "              :p_TransIIS,     "+
          "              :p_IISAmount,    "+
          "              :p_CalcNDFL,     "+
          "              :p_Partial,      "+
          "              :p_CloseContr,   "+
          
          "              :p_TaxAccount,   "+
          "              :p_TaxFIID,      "+
          
          "              :p_SPIAccount,   "+
          "              :p_BankID,       "+
          "              :p_BankCode,     "+
          "              :p_BankCodeKind, "+
          
          "              :p_CorrAccount,  "+
          "              :p_CorrBankID,   "+
          "              :p_CorrBankCode,  "+
          "              :p_CorrBankCodeKind,  "+

          "              :p_SPALTXLD,  "+
          "              :p_SPREGISTRDATE,  "+
          "              :p_SPREGISTRTIME,  "+  

          "              :p_RetOperID,    "+
          "              :p_err);         "+
          "END;                           ";
        
    cmd = RsdCommand( sql );
    cmd.addParam("p_KindOP",          RSDBP_IN, 20);
    cmd.addParam("p_OperCode",        RSDBP_IN, OpNum);
    cmd.addParam("p_OperDate",        RSDBP_IN, Date());  /*date(ConvertSpecialDate(p_DTTM)))*/
    cmd.addParam("p_Time",            RSDBP_IN, Time());  /*time(ConvertSpecialDate(p_DTTM)))*/
    cmd.addParam("p_PartyID",         RSDBP_IN, PartyID);
    cmd.addParam("p_ContrID",         RSDBP_IN, SfcontrID);
    cmd.addParam("p_Amount",          RSDBP_IN, DL_IIF(StrLwr(p_ALLBALANCE) == "true", 0, money(p_AMOUNT)));
    cmd.addParam("p_Account",         RSDBP_IN, _acc);
    cmd.addParam("p_FIID",            RSDBP_IN, _fiid);
    cmd.addParam("p_Filial",          RSDBP_IN, 1);
    cmd.addParam("p_CalcNOB",         RSDBP_IN, "X");
    cmd.addParam("p_TransIIS",        RSDBP_IN, DL_IIF(dlContr.rec.IIS=="X","X",""));
    cmd.addParam("p_IISAmount",       RSDBP_IN, 0);
    cmd.addParam("p_CalcNDFL",        RSDBP_IN, "X");
    cmd.addParam("p_Partial",         RSDBP_IN, "X");
    cmd.addParam("p_CloseContr",      RSDBP_IN, DL_IIF(dlContr.rec.IIS=="X","X",""));
    cmd.addParam("p_TaxAccount",      RSDBP_IN, ПолучитьСчетНалога());
    cmd.addParam("p_TaxFIID",         RSDBP_IN, ПолучитьВалютуНалога());
    /*параметры счета для СПИ*/
    cmd.addParam("p_SPIAccount",      RSDBP_IN, p_ACCOUNT);
    cmd.addParam("p_BankID",          RSDBP_IN, BankID);
    cmd.addParam("p_BankCode",        RSDBP_IN, BankCode);
    cmd.addParam("p_BankCodeKind",    RSDBP_IN, BankCodeKind);
    cmd.addParam("p_CorrAccount",     RSDBP_IN, /*BankAccount*/"");
    cmd.addParam("p_CorrBankID",      RSDBP_IN, /*CorrBankID*/0);
    cmd.addParam("p_CorrBankCode",    RSDBP_IN, /*CorrBankCode*/"");
    cmd.addParam("p_CorrBankCodeKind",RSDBP_IN, /*CorrBankKind*/0);
    /*параметры для документа основания*/
    cmd.addParam("p_SPALTXLD",        RSDBP_IN, /*OpNum*/"б/н");
    cmd.addParam("p_SPREGISTRDATE",   RSDBP_IN, date(ConvertSpecialDate(p_DTTM))); 
    cmd.addParam("p_SPREGISTRTIME",   RSDBP_IN, time(ConvertSpecialDate(p_DTTM))); 
    /*выходные параметры*/
    cmd.addParam("p_RetOperID",    RSDBP_OUT, V_INTEGER);
    cmd.addParam("p_err",          RSDBP_OUT, V_STRING, 1000);

    if(not err)
      cmd.Execute();
    else
      return 1;
    end;
    
    if (cmd and (cmd.value("p_err") != null) and (cmd.value("p_err") != nullval))
      SetParm(1, cmd.value("p_err"));
      PrintLogStr("X","Ошибка!"  + cmd.value("p_err"));
      return false;
    end;
    
    SetParm(0, cmd.value("p_RetOperID"));

    //Записываем уникальный идентифкатор в примечание на созданную операцию
    addnoteforobject(131,string(cmd.value("p_RetOperID"):34:o),102,p_IDOP);
    //устанавливаем категорию для операции
    ObjCat = RsbObjCategories( 131, cmd.value("p_RetOperID"));
    ObjCat.ConnectAttr(121, 3, NULL, NULL,  {curdate} ); // Устанавливаем значение категории Источник операции = ДБО

    PrintLogStr("","Создана операция: " + OpNum);
    rec_count_success = rec_count_success + 1;

    return 0;
  onerror(x)
    SetParm(1, x.message + " Module: " + x.Module + " Line:" + x.Line);
    return 1;
  end;

  stat = Open(datafile, FileName, ENCODE);
  if ( (valtype(stat) == V_UNDEF) or (stat == false) )
    errstr = "Ошибка открытия файла " + FileName;
    flog.RepStr("X","","","","","","","","",errstr);
    return 1;
  end;
  
  /*НАЧАЛО ПРОЦЕССА*/
  /*Открываем файл*/
  /*Структура файла ( в формате CSV)
    1./*IDOP*/    ID поручения в ДБО ФЛ
    2./*IDCLIENT*/ID CFT клиента  
    3 /*EKK*/     ЕКК клиента
    4./*CONTRACT*/Номер Договора БО/ИИС
    5./*MPLACE*/  Торговая площадка (Биржевой рынок или Внебиржевой рынок) 
    6./*TYPEOP*/  Тип поручения ( 1 - в сумме остатка; 2 - вывод конкретной суммы) Будет поле "Весть остаток" с типом булево.
    7./*CUR*/     Валюта вывода
    8./*AMOUNT*/  Сумма вывода 
    9./*DTTM*/    Дата поручения и время подачи поручения в формате   (Формат: ДД.ММ.ГГГГ, ЧЧ:ММ:СС)
    10./*ACCOUNT*/Номер счета для зачисления денежных средств (лицевой счет 408) 
    11./*FILIAL*/ Номер РФ, где открыт счет 408 (например 1200) */

  next(datafile);
  rec_count = 0;
  rec_count_success = 0;

  var Fields = FileFields();
  var c = 0;
  while(datafile(c) != "")
    Fields.AddField(uTrim(datafile(c)),c);
    c = c + 1;
  end;

  //Бежим по файлу
  InitProgress(-1, "Обработка файла \"" + FileName + "\". Ждите...", "Обработка файла. Ждите...");
  while(next(datafile ))

    stat = 0;

    if (StrLen(uTrim(datafile.str)) == 0)
      continue;
    end;
    
    rec_count = rec_count + 1;
    useProgress(rec_count);

    p_IDOP       = uTrim(datafile(Fields.GetID(FN_IDOP)));
    p_IDCLIENT   = uTrim(datafile(Fields.GetID(FN_IDCLIENT)));
    p_EKK        = uTrim(datafile(Fields.GetID(FN_EKK)));
    p_CONTRACT   = uTrim(datafile(Fields.GetID(FN_CONTRACT)));
    p_MPLACE     = uTrim(datafile(Fields.GetID(FN_MPLACE)));
    p_CUR        = uTrim(datafile(Fields.GetID(FN_CUR)));
    p_AMOUNT     = uTrim(datafile(Fields.GetID(FN_AMOUNT)));
    p_DTTM       = uTrim(datafile(Fields.GetID(FN_DTTM)));
    p_ACCOUNT    = uTrim(datafile(Fields.GetID(FN_ACCOUNT)));
    p_FILIAL     = uTrim(datafile(Fields.GetID(FN_FILIAL)));
    p_ALLBALANCE = uTrim(datafile(Fields.GetID(FN_ALLBALANCE)));

    if (StrLen(p_IDCLIENT) == 0)
      stat = 1;
      errstr = "Ошибка. Не указан p_IDCLIENT";
      PrintLogStr("X",errstr);
      continue;
    end;

    if (StrLen(p_CONTRACT) == 0)
      stat = 1;
      errstr = "Ошибка. Не указан p_CONTRACT";
      PrintLogStr("X",errstr);
      continue;
    end;

    PartyID = ПроверитьКлиента();
    if (PartyID == 0)
      stat = 1;
      errstr = "Ошибка. Не найлен клиент";
      PrintLogStr("X",errstr);
      continue;
    end;

    if (not ПроверитьДоговор())
      stat = 1;
      errstr = "Ошибка. Не найлен договор по переданным параметрам";
      PrintLogStr("X",errstr);
      continue;
    end;

    if (StrLen(p_EKK) == 0)
      ОпределитьEKK();
      if (StrLen(p_EKK) == 0)
      errstr = "Ошибка. Не определен ЕКК";
      PrintLogStr("X",errstr);
      continue;
      end;
    end;

    var existOP = "";
    if (not ПроверитьДубльОперации(existOP, PartyID))
      stat = 1;
      errstr = "Ошибка. Существует операция " + existOP + " с таким же идентификатором " + p_IDOP;
      PrintLogStr("X",errstr);
      continue;
    end;

    if (not CheckStringByPattern(p_DTTM, PTRN_FN_DTTM))
      errstr = "Ошибка. Переданное значение " + FN_DTTM + ": " + p_DTTM + " не соотв. шаблону YYYY-MM-DD HH24:mi:ss ";
      PrintLogStr("X",errstr);
      continue;
    end;

    if (not stat)
      LoopInTrn(true);
      if (not ProcessTrn(0, "СформироватьОперацию"))
        stat = 1;
        errstr = "Ошибка. Ошибка формирования операции";
        PrintLogStr("X",errstr);
      end;
    end;

  end;
  RemProgress();

  Close(datafile);
  RETURN 0;
  onerror(x)
    if(x.message == "Error: неверный индекс ")
       PrintLogStr("X","Ошибка разбора файла  \"Неверный индекс\". Проверьте(1251) кодировку и наличие строки с заголовком.");
       Println("* Загружаемый файл должен быть сохранен в кодировке WIN-1251, иметь строку с заголовком.\n    Файл не должен содержать лишних знаков табуляции и пробелов.\n    Разделителем в файле является символ ;" );
    else
       PrintLogStr("X","Ошибка. " + x.message );
    end;
    Close(datafile);
    return 1;
end;

macro runLoadNPTX(SilentMode)
  
  if(valType(SilentMode) != v_Bool) //Если вместо True/False не передано ничего или передано что то иное 
    SilentMode = False;
  end;

  var filePath = DL_GetFullPath(GetStrRegVal(NPTXOP_IMPORT_path));
  var list, i = 0;

  var dt = StrSubSt(string(Date()),".","");
  var tm = StrSubSt(string(time()),":","");
  var InfoLogShortFileName = "load_NPTX_"+dt+"_"+StrSubst(tm," ","0");
  var InfoLogFileName = GetTxtFileName(InfoLogShortFileName);

  if((SilentMode) and (GetDialogFlag))
    SetDialogFlag(0);
  else 
    SetDialogFlag(1);
  end;

  if(getListFiles(pfx, ext, filePath, @list) == 0)
     if(list.count > 0)
       flog = Logger();
       SetOutput( InfoLogFileName, true ); //Пишем лог в файл. Начало
       flog.RepMainHeader();
     end;

    while(i < list.Count)
     flog = Logger();
     SetOutput( InfoLogFileName, true ); //Пишем лог в файл. Начало

      flog.RepHeader(List.name(i));
      
      if(ExecMacro2(ProcessFileFunc, filePath+List.name(i)) == 0)
        ResultCopyFile(true, filePath, List.name(i));
      else
        ResultCopyFile(false, filePath, List.name(i));
      end;
      flog.RepFooter(0,0,0);
      i = i + 1;
    end;

     if(list.count > 0)
       SetOutput( NULL, true ); //Пишем лог в файл. Конец
       ResultCopyLOG(filePath, InfoLogFileName, InfoLogShortFileName:string);
       if(not SilentMode)
         ViewFile( InfoLogFileName );
       else
         SendNotification(InfoLogShortFileName,InfoLogFileName);
       end;
     end;
  else
   msgboxex("Нет файлов для загрузки.", MB_YES, IND_YES);
  end;

  SetDialogFlag(1);

end;

  exit(1);

