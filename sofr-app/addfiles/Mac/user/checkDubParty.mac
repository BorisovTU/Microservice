/** 
 @file checkDubParty.mac
 @brief Проверка дублирования субъекта
  
 Проверка дублирования субъекта для BIQ-10268
   
 # tag 
 - functional_block:
 - code_type:API
 - CDI
 - BIQ-10268

   
 # changeLog 
 |date       |author         |tasks            |note                                                         
 |-----------|---------------|-----------------|--------------------------------
 |2023.10.12 |Фаршатов Г. К. |CCBO-5646        | Реализация проверки дублирования       
 |2024.01.16 |Фаршатов Г. К. |DEF-56573        | Изменение работы определения типа субъекта    
 |           |               |                 |                   

*/ 

import global_classes;
import dlquery;
import func_lib;

/**
 @brief Класс с параметрами проверки дублирования
 
 Класс необходим для определения полей по которым осуществлять проверку дублирования
*/
class c_DupParam (PartyData)
  var FullName     = false,
      IdentityCard = false,
      INN          = false,
      LastName     = false,
      Name         = false,
      OGRN         = false,
      Registration = false,
      Resident     = false,
      ShortName    = false,
      Patronymic   = false,
      PensionCard  = false,
      Address      = false,
      BirthDate    = false,
      KIO          = false,
      OKOPF        = false,
      LEntity      = false,
      IsIP         = false,
      IsFLCHP      = false,
      OKPO         = false;
end;

/**
 @brief Краткая форма условия if
 @param[in] condition Условие
 @param[out] value_true Результат если да
 @param[out] value_false Результат если нет
 @return значение параметра value_true или value_false

*/
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else
      return value_false;
   end;
end;

/**
 @brief Получить параметры дублирования
 @param[out] parm Объект параметров c_DupParam
 @param[in] ParamSetID ID записи в таблице DPTDUPPRM_DBT
 @return true если удалось заполнить параметры
 @return false если не удалось заполнить параметры

 Заполняет parm в соответствии с наполнением таблицы DPTDUPPRM_DBT по заданному  ParamSetID
*/
private macro getParmDupByID (parm, ParamSetID)
  var cmd, DS;

  cmd = RSDCommand( "select * from DPTDUPPRM_DBT where T_PARAMSETID = ? and T_ISACTIVE = chr(88)");
        
  cmd.addParam( "", RSDBP_IN, ParamSetID );
  cmd.execute();

  DS = TRsbDataSet(cmd);

  if( DS.MoveNext() )
    parm.FullName     = IIF(DS.FullName == "X", true, false); 
    parm.IdentityCard = IIF(DS.IdentityCard == "X", true, false); 
    parm.INN          = IIF(DS.INN == "X", true, false); 
    parm.LastName     = IIF(DS.LastName == "X", true, false); 
    parm.Name         = IIF(DS.Name == "X", true, false); 
    parm.OGRN         = IIF(DS.OGRN == "X", true, false); 
    parm.Registration = IIF(DS.Registration == "X", true, false); 
    parm.Resident     = IIF(DS.Resident == "X", true, false);
    parm.ShortName    = IIF(DS.ShortName == "X", true, false);
    parm.Patronymic   = IIF(DS.Patronymic == "X", true, false);
    parm.PensionCard  = IIF(DS.PensionCard == "X", true, false);
    parm.Address      = IIF(DS.Address == "X", true, false);
    parm.BirthDate    = IIF(DS.BirthDate == "X", true, false);
    parm.KIO          = IIF(DS.KIO == "X", true, false);
    parm.OKOPF        = IIF(DS.OKOPF == "X", true, false);
    parm.OKPO         = IIF(DS.OKPO == "X", true, false);
    parm.LEntity      = IIF(((DS.PartyType == 5) or (DS.PartyType == 4)), true, false);
    if (parm.LEntity  == true)
      parm.Resident   = IIF(DS.PartyType == 4, true, false);
    end;
    parm.IsIP         = IIF(DS.PartyType == 6, true, false);
    parm.IsFLCHP      = IIF(DS.PartyType == 7, true, false);
    return true;
  end;
  
  return false;
end;

/**
 @brief Проверить наличие дубликата субъекта
 @param[in] Subject Объект RsbParty по которому осуществляется поиск дубликата
 @param[in] ParamSetID ID записи в таблице DPTDUPPRM_DBT
 @param[out] PartyDubID ID субъекта дубликата, если найден
 @param[int] OKOPF AttrID категории ОКОПФ для проверки дублирования
 @return true если дубликат субъекта не найден
 @return false если дубликат субъекта найден
 @note ОКОПФ необходим не для всех наборов параметров и может быть не задан 
 @note Проверка по параметрам PensionCard, IdentityCard и Registration не реализованная

 Проверить наличие дубликата субъекта по набору параметров ParamSetID
*/
macro checkDubPartyOnCDI (Subject, ParamSetID, PartyDubID : @integer, pOKOPF)
  var EnableCDI = false;
  var OKOPF = 0;
  var error  = 0;

  GetRegistryValue( "РСХБ\\ИНТЕГРАЦИЯ\\ВЗАИМОДЕЙСТВИЕ С AC CDI\\АКТИВНО", V_BOOL, EnableCDI, error );

  if (ValType(pOKOPF) !=  V_INTEGER)
    var party = TRecHandler ("party");
    party.rec.PartyID = Subject.PartyID;

    GetMainObjAttr(Error, OBJTYPE_PARTY, party, 53, OKOPF);
    if(Error or ValType(OKOPF) !=  V_INTEGER)
      OKOPF = 0;
    end;

    Error = 0;
  else
    OKOPF = pOKOPF;
  end;

  if (error !=0 ) EnableCDI = false; end;

  if (EnableCDI)
    var parm = c_DupParam();
 
    if(getParmDupByID (parm, ParamSetID));
      var mainSQL =  "select partyAll.t_ID from (select  TO_CHAR(party.t_PartyID) t_ID, " +
                     "                 NVL(objcodeCODE.t_Code, chr(1)) Code, " +
                     "                 NVL(person.t_Name1, chr(1)) LastName, " +
                     "                 NVL(person.t_Name2, chr(1)) Name, " +
                     "                 NVL(person.t_Name3, chr(1)) Patronymic, " +
                     "                 NVL2(person.t_Born, TO_CHAR(person.t_Born,'MM.DD.YYYY'), TO_CHAR(TO_DATE('01.01.0001','MM.DD.YYYY'),'MM.DD.YYYY')) BirthDate, " +
                     "                 NVL(address.t_Adress, chr(1)) Address, " +
                     "                 DECODE( party.t_NotResident, 'X', DECODE(party.t_NRCountry, chr(0), 'Б', 'Н'), 'Р') Resident, " +
                     "                 NVL2 (persnidc.t_personid, TRIM(DECODE(persnidc.t_paperseries, CHR(1), CHR(0), persnidc.t_paperseries) || ' ' || DECODE(persnidc.t_papernumber, CHR(1), CHR(0), persnidc.t_papernumber) || ' ' || DECODE(persnidc.t_paperissueddate, '01-JAN-0001', CHR(0), TO_CHAR(persnidc.t_paperissueddate,'MM.DD.YYYY'))), CHR (1) ) identitycard, " +
                     "                 NVL2(objrgdocPENS.t_ObjectID, objrgdocPENS.t_Series ||' '|| objrgdocPENS.t_Number ||' '|| TO_CHAR(objrgdocPENS.t_DocDate,'MM.DD.YYYY'), chr(1)) PensionCard, " +
                     "                 NVL(objcodeINN.t_Code, chr(1)) INN, " +
                     "                 NVL(SUBSTR(objcodeINN.t_Code, 1, INSTR(objcodeINN.t_Code, '/') - 1 ),  NVL(objcodeINN.t_Code, chr(1))) INNnoKPP, " +
                     "                 NVL(objcodeOGRN.t_Code, chr(1)) OGRN, " +
                     "                 NVL(objcodeKIO.t_Code, chr(1)) KIO, " +
                     "                 NVL2(objrgdocREG.t_ObjectID, objrgdocREG.t_Series ||' '|| objrgdocREG.t_Number ||' '|| TO_CHAR(objrgdocREG.t_DocDate,'MM.DD.YYYY'), chr(1)) Registration, " +
                     "                 party.t_Name FullName, " +
                     "                 party.t_ShortName ShortName, " +
                     "                 party.T_OKPO OKPO, " +
                     "                 NVL(person.t_IsEmployer, chr(0)) IsEmployer, " +
                     "                 NVL(persnidc.T_PAPERKIND, -1) PaperKind, " +
                     "                 NVL(objrgdocREG.T_REGDOCKIND, -1) RegDocKind, " +
                     "                 NVL(objrgdocREG.T_REGPARTYKIND, -1) RegPartyKind, " +
                     "                 NVL(objatcorOKOPF.T_ATTRID, 0) OKOPFAttr, " +
                     "                 party.t_LegalForm LegalForm, " +
                     "                 NVL(objatcorCDI.T_ATTRID, 0) CDIAttr " +
                     "          from dparty_dbt party, dpartcode_dbt objcodeCODE, dpersn_dbt person, dadress_dbt address, dpersnidc_dbt persnidc, " +
                     "               dobjrgdoc_dbt objrgdocPENS, dpartcode_dbt objcodeINN, dpartcode_dbt objcodeOGRN, dobjrgdoc_dbt objrgdocREG,  " +
                     "               dpartcode_dbt objcodeKIO, DOBJATCOR_DBT objatcorOKOPF, DOBJATCOR_DBT objatcorCDI " +
                     "          where party.t_PartyID                = objcodeCODE.t_PartyID(+) " +
                     "            and objcodeCODE.T_CODEKIND(+)      = 1 " +
                     "            and party.t_PartyID                = person.T_PERSONID(+) " +
                     "            and party.t_PartyID                = address.t_PartyID(+) " +
                     "            and party.t_PartyID                = persnidc.T_PERSONID(+) " +
                     "            and address.t_type(+)              = 1 " +
                     "            and persnidc.T_ISMAIN(+)           = 'X' " +
                     "            and party.t_PartyID                = objrgdocPENS.T_OBJECTID(+) " +
                     "            and objrgdocPENS.T_OBJECTTYPE(+)   = 3 " +
                     "            and objrgdocPENS.T_REGDOCKIND(+)   = 29 " +
                     "            and objrgdocPENS.T_REGPARTYKIND(+) = 11 " +
                     "            and party.t_PartyID                = objcodeINN.t_PartyID(+) " +
                     "            and objcodeINN.T_CODEKIND(+)       = 16 " +
                     "            and party.t_PartyID                = objcodeOGRN.t_PartyID(+) " +
                     "            and objcodeOGRN.T_CODEKIND(+)      = 27 " +
                     "            and party.t_PartyID                = objcodeKIO.t_PartyID(+) " +
                     "            and objcodeKIO.T_CODEKIND(+)       = 62 " +
                     "            and party.t_PartyID                = objrgdocREG.T_OBJECTID(+) " +
                     "            and objrgdocREG.T_OBJECTTYPE(+)    = 3 " +
                     "            and objrgdocREG.T_ISMAIN(+)        = 'X' " +
                     "            and objrgdocreg.t_isclosed(+)     <> 'X' " +
                     "            and lpad(party.t_PartyID, 10, '0') = objatcorOKOPF.T_OBJECT(+) " +
                     "            and objatcorOKOPF.T_OBJECTTYPE(+)  = 3  " +
                     "            and objatcorOKOPF.T_GROUPID(+)     = 53  " +
                     "            and objatcorOKOPF.t_validfromdate(+) <= to_date('" + {Curdate} + "', 'DD.MM.YYYY') " +
                     "            and objatcorOKOPF.t_validtodate(+) > to_date('" + {Curdate} + "', 'DD.MM.YYYY') " +
                     "            and lpad(party.t_PartyID, 10, '0') = objatcorCDI.T_OBJECT(+) " +
                     "            and objatcorCDI.T_OBJECTTYPE(+)  = 3  " +
                     "            and objatcorCDI.T_GROUPID(+)     = 150 " +
                     "            and objatcorCDI.t_validfromdate(+) <= to_date('" + {Curdate} + "', 'DD.MM.YYYY') " +
                     "            and objatcorCDI.t_validtodate(+) > to_date('" + {Curdate} + "', 'DD.MM.YYYY') ) partyAll ";

      var whereSQL = "where partyAll.t_ID <> ? ";
      
      if (parm.FullName)
        whereSQL = whereSQL + "and partyAll.FullName = '" + Subject.FullName + "' ";
      end;
      if (parm.LastName)
        whereSQL = whereSQL + "and partyAll.LastName = '" + Subject.GetLastName + "' ";
      end;
      if (parm.OGRN)
        whereSQL = whereSQL + "and partyAll.OGRN = '" + Subject.code.GetCode(PTCK_OGRN) + "' ";
      end;
      if (parm.INN) 
        whereSQL = whereSQL + "and partyAll.INN = '" + Subject.code.GetCode(PTCK_INN) + "' ";
      end;
      if (parm.Resident) 
        whereSQL = whereSQL + "and partyAll.Resident = DECODE( '"+ IIF(Subject.IsNotResident(),"X"," ") +"', 'X', DECODE( '"+ Subject.NRCountry +"' , chr(0), 'Б', 'Н'), 'Р') ";
      end;
      if (parm.ShortName)
        whereSQL = whereSQL + "and partyAll.ShortName = '" + Subject.GetShortName() + "' ";
      end;
      if (parm.Patronymic)
        whereSQL = whereSQL + "and partyAll.Patronymic = '" + Subject.GetPatronymic() + "' ";
      end;
      if (parm.Address)
        whereSQL = whereSQL + "and partyAll.Address = '" + Subject.Address(1).Address + "' ";
      end;
      if (parm.BirthDate)
        whereSQL = whereSQL + "and partyAll.BirthDate = TO_CHAR(TO_DATE('" + Subject.GetBirthDate() + "','MM.DD.YYYY') ";
      end;
      if (parm.KIO)
        whereSQL = whereSQL + "and partyAll.KIO = '" + Subject.code.GetCode(62/*КИО*/) + "' ";
      end;
      if (parm.OKPO)
        whereSQL = whereSQL + "and partyAll.OKPO = '" + Subject.OKPO() + "' ";
      end;
      if (parm.OKOPF)
        whereSQL = whereSQL + "and partyAll.OKOPFAttr = '" + OKOPF + "' ";
      end;
      if (parm.LEntity)
        whereSQL = whereSQL + "and (partyAll.CDIAttr = 2 or ( partyAll.LegalForm = 1 and LENGTH(partyAll.INNnoKPP) <= 10 and LENGTH(partyAll.OGRN) <= 13 ) )";
      end;
      if (parm.IsIP)
        whereSQL = whereSQL + "and (partyAll.CDIAttr = 3 or ( partyAll.LegalForm = 1 and LENGTH(partyAll.INNnoKPP) > 10 or LENGTH(partyAll.OGRN) > 13 ) or (partyAll.LegalForm = 2 " +
                              "and partyAll.OKOPFAttr in (SELECT T_ATTRID from DOBJATTR_DBT a WHERE a.t_objecttype = 3 AND a.t_groupid = 53 AND a.t_nameobject LIKE '5 01%') ) or ( partyAll.LegalForm = 2 and partyAll.IsEmployer = chr(88) ))";
      end;
      if (parm.IsFLCHP)
        whereSQL = whereSQL + "and (partyAll.CDIAttr = 4 or ( partyAll.LegalForm = 1 and LENGTH(partyAll.INNnoKPP) > 10 and LENGTH(partyAll.OGRN) < 2 ) or (partyAll.LegalForm = 2 " +
                              "and partyAll.OKOPFAttr in (SELECT T_ATTRID from DOBJATTR_DBT a WHERE a.t_objecttype = 3 AND a.t_groupid = 53 AND a.t_nameobject LIKE '5 02%') ))";
      end;

      var cmd = RSDCommand( mainSQL + whereSQL );
      
      cmd.addParam( "", RSDBP_IN, Subject.PartyID );
      cmd.execute();

      var DS = TRsbDataSet(cmd);

      if( DS.MoveNext() )
        PartyDubID = DS.ID;
        return false;
      end;
    end;
    
  end;
 
  return true; 
end;
