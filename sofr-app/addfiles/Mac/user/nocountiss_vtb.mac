/*
$Name:        nptxskipcat_form.mac
$Module:      Ценные бумаги
$Description: 
*/
import "DlRepForm_h.mac", "dlutils.mac", "nocountiss_vtb_form.mac", "poisimplerepo.mac", "xls_parser.mac", "partysql.mac", "nptxfun.mac";

macro NoCountIISOperation(LoadFile, IsEnab)
  var simpleRepo = PoiSimpleRepo("Протокол выполнения операции",
                               makeArray(
                                  DL_KeyPair("Внутренний код операции зачисления", 20),
                                  DL_KeyPair("Id клиента", 20),
                                  DL_KeyPair("Код ЦФТ клиента", 20),
                                  DL_KeyPair("Старое значение T_NOTCOUNTEDONIIS на операции", 20),
                                  DL_KeyPair("Новое значение T_NOTCOUNTEDONIIS на операции", 20),
                                  DL_KeyPair("Id налогового лота приобретения", 20),
                                  DL_KeyPair("Старое значение T_NOTCOUNTEDONIIS на лоте", 20),
                                  DL_KeyPair("Новое значение T_NOTCOUNTEDONIIS на лоте", 20),
                                  DL_KeyPair("Id лота реализации с датой >= 01.01.2025 из связи", 20),
                                  DL_KeyPair("Дата реализации", 20),
                                  DL_KeyPair("Требуется пересчет", 20),
                                  DL_KeyPair("Комментарий", 20)
                               ));

  var setValueStr = IIF(IsEnab, "Да", "Нет");
  var objExcel = CExcelPoi();
  var fileMov = TempFileToServerMover();
  fileMov.CreateTempFromAbsolutPath(LoadFile);
  if (not objExcel.open(fileMov.GetTempFilePath()))
    RunError("Не удалось открыть excel файл " + LoadFile);
  end;
  var lastRowNumb = objExcel.GetLastRowNum()+1;
  var curRow = 0;
  InitProgress(lastRowNumb, "Чтение файла со списком операций", "Чтение файла со списком операций");
  while ((curRow=curRow+1) <= lastRowNumb)
    var cellValue = objExcel.CellValue("A"+curRow);
    if (ValType(cellValue) == V_UNDEF)
      continue;
    end;
    var curRowData = Trim(string(cellValue));
    var dotIdx = Index(curRowData, ".");
    if (dotIdx > 0)
      curRowData = SubStr(curRowData, 1, dotIdx-1);
    end;

    var query = 
      " SELECT tk.T_DEALID, "
     +"        tk.T_BOFFICEKIND, "
     +"        tk.t_dealdate, "
     +"        tk.T_CLIENTID, "
     +"        tk.T_CLIENTCONTRID, "
     +"        dl_leg.t_CFI, "
     +"        dlsum.T_DLSUMID, "
     +"        CASE WHEN dlsum.T_NOTCOUNTEDONIIS = CHR(88) THEN 'Да' else 'Нет' END AS T_NOTCOUNTEDONIISDLSUM, "
     +"        LOT.T_ID as t_buylot, "
     +"        CASE WHEN dlsum.T_NOTCOUNTEDONIIS = CHR(88) THEN 'Да' else 'Нет' END AS T_NOTCOUNTEDONIISLOT, "
     +"        lnk.t_saleid as t_salelot, "
     +"        LNK.T_DATE as t_saledate"
     +"   FROM DDL_TICK_DBT tk "
     +"       INNER JOIN ddl_leg_dbt dl_leg "
     +"     ON     dl_leg.t_DealID = tk.t_DealID "
     +"        AND dl_leg.t_LegKind = 0 "
     +"        AND dl_leg.t_LegID = 0 "
     +"        LEFT JOIN ddlsum_dbt dlsum "
     +"           ON     dlsum.t_dockind = tk.T_BOFFICEKIND "
     +"              AND dlsum.t_docid = tk.t_dealid "
     +"              AND dlsum.t_kind = 1230 "
     +"        LEFT JOIN DNPTXLOT_DBT lot "
     +"           ON LOT.T_DOCKIND = tk.T_BOFFICEKIND AND LOT.T_DOCID = tk.T_DEALID "
     +"        LEFT JOIN dnptxlnk_dbt lnk "
     +"           ON LNK.T_BUYID = LOT.T_ID "
     +"              AND LNK.T_DATE >= TO_DATE ('01.01.2025', 'DD.MM.YYYY') "
     +"  WHERE tk.T_DEALCODE = ? ";
    var cmd = DL_RSDCommand(query);
    cmd.addParam( curRowData );
    var ds = cmd.Execute();

    var flg = ds.MoveNext();

    if (flg)
      if (ds.BOFFICEKIND != DL_AVRWRT)
        simpleRepo.PrintLine(curRowData,
                             "",
                             "",
                             "",
                             "",
                             "",
                             "",
                             "",
                             "",
                             "",
                             "",
                             "Операция не является зачислением ц/б");
        flg = false;
      end;
      if (DL_CheckContrIIS(SQL_ConvTypeInteger(ds.ClientContrID)))
        simpleRepo.PrintLine(curRowData,
                             "",
                             "",
                             "",
                             "",
                             "",
                             "",
                             "",
                             "",
                             "",
                             "",
                             "Операция сформирована по договору ИИС");
        flg = false;
      end;
      while (flg)
        var clientId = "";
        var clientCFTCode = "";
        var oldOperationNotCountedOnIIS = "";
        var newOperationNotCountedOnIIS = "";
        var taxPurchaseLotId = "";
        var oldLotNotCountedOnIIS = "";
        var newLotNotCountedOnIIS = "";
        var realizationLotIdFrom2025 = "";
        var realizationDate = "";
        var recalculationRequired = "Нет";
        if (not (ds.dlsumid > 0))
          execSQL("insert into ddlsum_dbt (t_Dockind, t_Docid , t_kind, t_date, t_sum, t_nds, t_currency, t_fiid) values (127, ?, ?, ?, 0, 0, ?, -1)",
                  makeArray(SQLParam("docid", ds.dealid),
                            SQLParam("kind", 1230),
                            SQLParam("date", ds.dealdate),
                            SQLParam("currency", ds.cfi)
                           )
                 );
        end;
        clientId = string(ds.ClientID);
        clientCFTCode = string(GetCftIDFromSofrID(ds.ClientID));
        oldOperationNotCountedOnIIS = string(ds.NotCountedOnIISDlSum);
        newOperationNotCountedOnIIS = setValueStr;
        if ((ds.buylot != null ) and (ds.buylot > 0))
          taxPurchaseLotId = string(ds.buylot);
          oldLotNotCountedOnIIS = string(ds.NotCountedOnIISLot);
          newLotNotCountedOnIIS = setValueStr;
        end;
        if ((ds.salelot != null) and (ds.salelot > 0))
          realizationLotIdFrom2025 = string(ds.salelot);
          realizationDate = string(date(ds.saledate));
          if ((ds.NotCountedOnIISDlSum != setValueStr) or (ds.NotCountedOnIISLot != setValueStr))
            recalculationRequired = "Да";
          end;
        end;
        execSQL("update ddlsum_dbt set T_NOTCOUNTEDONIIS = "+IIF(IsEnab, "CHR(88)", "CHR(0)")+" where t_dockind = 127 and t_docid = ? and t_kind = ? ",
                  makeArray(SQLParam("docid", ds.dealid),
                            SQLParam("kind", 1230)
                           )
              );
        execSQL("update DNPTXLOT_DBT set T_NOTCOUNTEDONIIS = "+IIF(IsEnab, "CHR(88)", "CHR(0)")+" where t_dockind = 127 and t_docid = ?",
                  makeArray(SQLParam("docid", ds.dealid)
                           )
              );
        simpleRepo.PrintLine(curRowData,
                             clientId,
                             clientCFTCode,
                             oldOperationNotCountedOnIIS,
                             newOperationNotCountedOnIIS,
                             taxPurchaseLotId,
                             oldLotNotCountedOnIIS,
                             newLotNotCountedOnIIS,
                             realizationLotIdFrom2025,
                             realizationDate,
                             recalculationRequired,
                             "");
        flg = ds.MoveNext();
      end;
    else
      simpleRepo.PrintLine(curRowData,
                           "",
                           "",
                           "",
                           "",
                           "",
                           "",
                           "",
                           "",
                           "",
                           "",
                           "Операция зачисления не найдена в БД");
    end;

    UseProgress(curRow);
  end;
  RemProgress();

  if (not simpleRepo.IsHasData())
    simpleRepo.PrintLine("",
                         "",
                         "",
                         "",
                         "",
                         "",
                         "",
                         "",
                         "",
                         "",
                         "",
                         "Файл пуст");
  end;

  simpleRepo.View();
end;

MACRO NoCountIISOperationMenuRun(errmsg:@string)
  errmsg = "";
  var Form = NOCOUNTISSVTB_Panel();
  if (Form.Run())
    NoCountIISOperation(Form.m_LoadFile, Form.m_ChangeToEnab)
  end;
  return true;
OnError(er)
  errmsg = GetFullErrorMessage(er);
  return false;
END;