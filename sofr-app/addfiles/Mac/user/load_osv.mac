import bankinter, rsd, "or_exl_hm.mac";
private var nf;

private Macro SaveRow(acc, cur, aname, in_cur_rest, inc_dc, in_rub_rest, inr_dc, deb_cur, deb_rub, crd_cur, crd_rub, out_cur_rest, outc_dc, out_rub_rest, outr_dc, bdate)
 var sql, cmd;
 sql = "Insert into bis_balance values(:1, :2, :3, :4, :5, :6, :7, :8, :9, :10, :11, :12, :13, :14, :15, :16)";
 cmd = RSDCommand(sql);
 cmd.AddParam("p01", RSDBP_IN, acc);
 cmd.AddParam("p02", RSDBP_IN, cur);
 cmd.AddParam("p03", RSDBP_IN, aname);
 cmd.AddParam("p04", RSDBP_IN, in_cur_rest);
 cmd.AddParam("p05", RSDBP_IN, inc_dc);
 cmd.AddParam("p06", RSDBP_IN, in_rub_rest);
 cmd.AddParam("p07", RSDBP_IN, inr_dc);
 cmd.AddParam("p08", RSDBP_IN, deb_cur);
 cmd.AddParam("p09", RSDBP_IN, deb_rub);
 cmd.AddParam("p10", RSDBP_IN, crd_cur);
 cmd.AddParam("p11", RSDBP_IN, crd_rub);
 cmd.AddParam("p12", RSDBP_IN, out_cur_rest);
 cmd.AddParam("p13", RSDBP_IN, outc_dc);
 cmd.AddParam("p14", RSDBP_IN, out_rub_rest);
 cmd.AddParam("p15", RSDBP_IN, outr_dc);
 cmd.AddParam("p16", RSDBP_IN, bdate);
 cmd.Execute;
 return true;
end;

private macro Getdate(fname)
  var vtxt = fname;
  While (index(vtxt, "\\") > 0)
    vtxt = Substr(vtxt, index(vtxt, "\\") + 1);
  end;
  return date(int(substr(vtxt, 1, 2)), int(substr(vtxt, 4, 2)), int(substr(vtxt, 7, 2)) + 2000);
end;

Private macro GetSavedRows(bdate)
  var sql, cmd, rs;
  sql = "select count(1) from bis_balance b where trunc(b.bdate) = to_date(:p1,'dd.mm.yyyy')";
  cmd = RSDCommand(sql);
  cmd.AddParam("p1", RSDBP_IN, strsubst(string(bdate), " ", "0"));
  rs = RSDRecordSet(cmd);
  if (rs.MoveNext()) 
    return Int(rs.Value(0));
  end;
  return 0;
End;

private Macro DeleteBDate(bdate)
  var sql, cmd;
  sql = "delete from bis_balance b where trunc(b.bdate) = to_date(:p1,'dd.mm.yyyy')";
  cmd = RSDCommand(sql);
  cmd.AddParam("p1", RSDBP_IN, strsubst(string(bdate), " ", "0"));
  cmd.Execute;
end;


private Macro LoadOsv(namefile)
  var objExcel = CDAOMSExcel();
  var i = 1, cntempty = 0, cntsave = 0;
  var bdate;

  bdate = Getdate(namefile);
  if( GetSavedRows(bdate) > 0) 
   if(not GetTrue(false, "За " + bdate + " данные уже загружались, желаете обновить?"))
     return;
   end;
   DeleteBDate(bdate);
  end;

  if(objExcel.CreateApplication())
    if(objExcel.open(namefile))
      objExcel.Book.Sheets(1).Activate();
      BegAction(0, "Ждите, выполняется загрузка данных ...", false);
      While (cntempty < 20)
        if (StrIsNumber(substr(objExcel.Book.ActiveSheet.Cells(i, 1).text, 1, 5)))
          if (SaveRow(objExcel.Book.ActiveSheet.Cells(i, 1).text,
                      objExcel.Book.ActiveSheet.Cells(i, 2).text,
                      objExcel.Book.ActiveSheet.Cells(i, 3).text,
                      objExcel.Book.ActiveSheet.Cells(i, 4).text,
                      objExcel.Book.ActiveSheet.Cells(i, 5).text,
                      objExcel.Book.ActiveSheet.Cells(i, 6).text,
                      objExcel.Book.ActiveSheet.Cells(i, 7).text,
                      objExcel.Book.ActiveSheet.Cells(i, 8).text,
                      objExcel.Book.ActiveSheet.Cells(i, 9).text,
                      objExcel.Book.ActiveSheet.Cells(i,10).text,
                      objExcel.Book.ActiveSheet.Cells(i,11).text,
                      objExcel.Book.ActiveSheet.Cells(i,12).text,
                      objExcel.Book.ActiveSheet.Cells(i,13).text,
                      objExcel.Book.ActiveSheet.Cells(i,14).text,
                      objExcel.Book.ActiveSheet.Cells(i,15).text,
                      bdate))
            cntsave = cntsave + 1;
          end;
          cntempty = 0;
        else
          cntempty = cntempty + 1;
        end;
        i = i + 1;
      end;
      EndAction;
      Msgbox("За " + bdate +  " загружено "+ trim(string(cntsave)) + " счетов.");

    end;
    objExcel.Book.Close();
  end;
  objExcel = null;
OnError(err)
  Msgbox(err.Message);
  objExcel.Book.Close();
  objExcel = null;
end;

While (selectFile (nf, mergeFile ("..\\Import\\OSV", "*.xls"))) 
  LoadOsv(nf);
end;

exit(1);