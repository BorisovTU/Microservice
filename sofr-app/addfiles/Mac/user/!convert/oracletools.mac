///-----------------------------------------------------------------------------------------
// Библиотека для работы с SQL
// Горленко
//-----------------------------------------------------------------------------------------

import oralib, likepy, bankinter;

Private var _sqlString;
//-----------------------------------------------------------------------------------------

Macro ForDate ( dt )
    if ( dt == Date (0,0,0) ) dt = "to_date('01010001','ddmmyyyy')"; end;
    return String ("to_date ('", dt, "', 'dd.mm.yyyy')");
end;
//-----------------------------------------------------------------------------------------

Macro ForString ( str )
    if ((str==NULL) or (str==""))
       return "CHR(1)";
    end;
    return String ("'", StrSubst (str, "'", "''"), "'");
end;

Macro ForChar ( str )
    if ((str==NULL) or (str==""))
       return "CHR(0)";
    end;
    return String ("'", StrSubst (str, "'", "''"), "'");
end;
//-----------------------------------------------------------------------------------------

Macro NotNull ( val )
    if ( val == NULL ) return false; end;
    if ( ValType (val) == 26 ) return false; end;
    return true;
End;
//-----------------------------------------------------------------------------------------

Macro Sql_String ( str )
    if ( NotNull (str) and (str != StrFor (0)) and (str != StrFor (1)) )
        return str;
    end;
    return "";
End;
//-----------------------------------------------------------------------------------------

Macro Sql_Date ( dt )
    if ( NotNull (dt) and (dt != Date (2,1,1)-1) )
        return Date (dt);
    end;
    return Date (0,0,0);

End;
//-----------------------------------------------------------------------------------------

Macro _SqlTime ( tm )
    if ( NotNull (tm) )
        return Time (tm);
    end;
    return Time (0,0,0);

End;
//-----------------------------------------------------------------------------------------

Macro Sql_Money ( sum )
    if ( NotNull (sum) )
        return Money (sum);
    end;
    return $0;
End;
//-----------------------------------------------------------------------------------------

Macro Sql_Integer ( num )
    if ( NotNull (num) )
        return Int (num);
    end;
    return 0;
End;
//-----------------------------------------------------------------------------------------

Macro Capture ( str )
    _sqlString = String (_sqlString, " ", str);
End;
//-----------------------------------------------------------------------------------------

Macro StartCapture
    _sqlString = "";
    SetOutHandler ("Capture");
End;
//-----------------------------------------------------------------------------------------

Macro EndCapture
    SetOutHandler ();
End;
//-----------------------------------------------------------------------------------------

Macro ExecuteSql ( Parm )
    return ExecSql (_sqlString, Parm, false);
End;
//-----------------------------------------------------------------------------------------

Macro ExecuteSqlSelect ( Parm )
    return ExecSqlSelect (_sqlString, Parm, false);
End;
//-----------------------------------------------------------------------------------------

Macro CloneExt ( x, y )
    Var fName, fExt;
    if ( Clone (x, y) )
        SplitFile (y, fName, fExt);
        ExecSql ("alter table d"+fName+"_"+StrSubst (fExt, ".", "")+"  nologging");
        return true;
    end;
    return false;
End;
//-----------------------------------------------------------------------------------------

//-----------------------------------------------------------------------------------------

Macro CopySql2Rsl ( sql, rsl )
    Var i = 0;
    ClearRecord (rsl);
    if ( (ValType (rsl) == V_FILE) or (ValType (rsl) == V_STRUC) )
        while ( i < FldNumber (rsl) )
            if ( ValType (sql.value ("t_"+FldName (rsl, i))) == 26 )
            elif (  ValType (rsl(i)) == V_STRING )
                rsl(i) = Sql_String (sql.value ("t_"+FldName (rsl, i)));
            elif (  ValType (rsl(i)) == V_DATE )
                rsl(i) = Sql_Date (sql.value ("t_"+FldName (rsl, i)));
            else
                rsl(i) = sql.value ("t_"+FldName (rsl, i));
            end;
            i = i + 1;
        end;
    else
        while ( i < rsl.FldNumber )
            if (  ValType (rsl.(i)) == V_STRING )
                rsl.(i) = Sql_String (sql.value ("t_"+rsl.FldName(i)));
            elif ( NotNull (sql.value ("t_"+rsl.FldName(i))) )
                rsl.(i) = sql.value ("t_"+rsl.FldName(i));
            end;
            i = i + 1;
        end;
    end;
End;
//-----------------------------------------------------------------------------------------

Macro execSQLext ( sqltext:string, params:TArray, throw:bool ):RsdCommand
    var cmd:RsdCommand = RsdCommand (sqltext);

    var i:integer = 0;
    var param:SQLParam = NULL;
    while ( params and (i < params.size) )
        param = params.value (i);
        if   (valtype(param.len)!=v_undef)
             cmd.addParam( param.name, param.type, param.value, param.len);
        else
             cmd.addParam( param.name, param.type, param.value );
        end;
        i = i + 1;
    end;

    cmd.execute ();
    return cmd;
  OnError ( x )
    if ( (throw == NULL) or throw )
        SetOutput ("..\\txtfile\\oralib.txt",True);
        println (sqltext);
        println (getRSDErrorsString (cmd));
        SetOutput (Null, True);
        RunError (getRSDErrorsString (cmd));
    else
        return NULL;
    end;
End;
//-----------------------------------------------------------------------------------------

Macro GetsSqlString
    return _sqlString;
End;
//-----------------------------------------------------------------------------------------


