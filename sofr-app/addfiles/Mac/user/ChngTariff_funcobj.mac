import sfinter, RsbDataSet, rsd, sp_car, "FuncObj.mac";

private class TParamProcessSlowEvent( StrParam :string )
   var T_ID :integer = 0,
       t_NewTarifId :integer = 0,
       t_BegDate :date = ZeroDate;

   private var StrSeparator :string = ";";

   private macro InitParam( StrParam :string )
      var StrDigit :string = "0123456789",
          StrParamBuf :string = "",
          i :integer = 0,
          StrPos :integer = 0;

      if( (Valtype(StrParam) != V_UNDEF) and (Valtype(StrParam) != 26) and (StrParam != "") )
        
         i = 1;
         while( i <= StrLen(StrParam) ) //нормализация оставить, что только цифры или разделитель
            if( Index( (StrDigit + StrSeparator+"."), Substr(StrParam, i, 1) ) >= 1 )
               StrParamBuf = StrParamBuf + Substr(StrParam, i, 1);
            end;
            i = i + 1;
         end;
          
         i = 0;
         while( (StrLen(StrParamBuf) > 0) and (GenNumProps(this) > i) ) //заполняем свойства
            StrPos = Index( StrParamBuf, StrSeparator );
            if( StrPos > 1 )
               if(i==2)
                 this[i] =  date(Substr(StrParamBuf, 1, StrPos - 1));
               else
                 this[i] =  int(Substr(StrParamBuf, 1, StrPos - 1));
               end;
               StrParamBuf = Substr(StrParamBuf, StrPos + 1);
            elif( (StrPos == 0) and (StrLen(StrParamBuf) > 0) )
               this[i] = int(StrParamBuf);
               StrParamBuf = "";
            end;
            i = i + 1;
         end;
      end;

   onError(err)
     T_ID = 0;
     t_NewTarifId = 0;
     t_BegDate= ZeroDate;
   end;

   InitParam( StrParam );

end;

macro ChngTariff(param0, param1, param2):FuncObjResult
  var err:integer = 0;
  var ReturnObj = FuncObjResult(FUNCOBJ_RESULT_OK, "", 0);
  var ParamProcess :object /*TParamProcessEntriesVerify*/;
  if(param2 == "")
      ReturnObj.state = FUNCOBJ_RESULT_ERROR;
      ReturnObj.errorText = "Переданы неверные параметры";
      ReturnObj.errorCode = 0;
  else
    ParamProcess = TParamProcessSlowEvent( param2 + ";" ); 
  
    err = SfContrChangeSfPlan(ParamProcess.t_id, ParamProcess.t_NewTarifId, ParamProcess.t_BegDate, false);
  
    if((err != 0) )
      ReturnObj.state = FUNCOBJ_RESULT_ERROR;
      ReturnObj.errorText = GetErrMsg();
      ReturnObj.errorCode = err;
    end;
  end;  
  return ReturnObj;
end;
