import rsd,globals,"Календарь";
import rsbformsinter;

CONST KEY_ENTER = 13, KEY_F2 = 316, KEY_F3 = 317, KEY_ALTF3 = 362, KEY_CTRLF3 = 352, KEY_SPACE = 32, EXITFIELD = 530; /*много полезных клавищ*/
var debugmode = index(strupr(GetIniString("CONNSTRING", "rsreq.ini")),"RSHB_SOFR_DEBUG");
debugmode = 0;

//разбиение строки по запятым
macro split(strParam)
  var a = Tarray;
  var StrDigit :string = "0123456789",
    StrParamBuf :string = "",
    i :integer = 0,
    StrSeparator: string = ",",
    StrPos :integer = 0;
  if( (Valtype(StrParam) != V_UNDEF) and (Valtype(StrParam) != 26) and (StrParam != "") )
   i = 1;
   while( i <= StrLen(StrParam) ) 
      if( Index( (StrDigit + StrSeparator), Substr(StrParam, i, 1) ) >= 1 )
         StrParamBuf = StrParamBuf + Substr(StrParam, i, 1);
      end;
      i = i + 1;
   end;
   i = 0;
   while( (StrLen(StrParamBuf) > 0) ) 
      StrPos = Index( StrParamBuf, StrSeparator );
      if( StrPos > 1 )
         a[i] =  int(Substr(StrParamBuf, 1, StrPos - 1));
         StrParamBuf = Substr(StrParamBuf, StrPos + 1);
      elif( (StrPos == 0) and (StrLen(StrParamBuf) > 0) )
         a[i] = int(StrParamBuf);
         StrParamBuf = "";
      end;
      i = i + 1;
   end;
  end;
  return a;
end;

class (TRsbPanel) SvodPanel

    var stat = 0;
    var menu_selected = false;
    var selected_menu_label = "";
    var opers = "";
    InitTRsbPanel();
    setSize(40,16);
    setPosition(5,5);
    Status("F9 - выполнить");
    Caption("Создание пункта меню для пользователя или роли");

    addLabel(TRsbLabel(3, 1, "F9 - выполнить"));
    addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "Panel_ButtonEvent"));

    var l_sysmodule:TRsbLabel = TRsbLabel( 3, 2, "Модуль системный:" );
    addLabel( l_sysmodule );
    var m_sysmoduleFld:string = "не выбран";
    var m_sysmodule:TRsbEditField = TRsbEditField(7);
    m_sysmodule.bindValue(this,"m_sysmoduleFld",20);
    m_sysmodule.setPosition(20,2);                
    m_sysmodule.setSize(16,1);
    m_sysmodule.name = "m_sysmodule";
    addControl(m_sysmodule);
    m_sysmodule.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "SYSMODULE_KEY_PRESSED"));

    var rs_subsystem;
    var rs_sysmodule;
    var rs_module;
    var m_moduleFld:string = "не выбран";
    var m_module:TRsbEditField = TRsbEditField(7);
    macro getsysmodule()
      macro EvProc( rs, cmd, id, key )
        if( (cmd == DLG_KEY) and (key == KEY_ENTER) )
          rs_sysmodule = rs;  
          rs_module = null; 
          return CM_SELECT;
        end;
        return CM_DEFAULT;
      end;
      if ((rs_subsystem==null))
        MsgBox("Подсистема не выбрана"); 
        return; 
      end; 
      var sql = "SELECT * FROM ditemsyst_dbt t WHERE  t.t_cidentprogram = '" + rs_subsystem.value("T_TYPE_ACCOUNT") + "' ORDER BY t.t_cidentprogram, t.t_icaseitem";
      var cmd = rsdcommand(sql);
      var rs = rsdrecordset(cmd, rsdval_client, RSDVAL_STATIC);
      if (RunScroll(rs, null, null, null, @EvProc, "Выбор системного модуля", "Esc Выход Enter Выбор", true, -1, -1, null, 25)==false)
        return; 
      end;
      if (rs!=null)
        getControl("m_sysmodule").value = rs.value("t_sznameitem");
        m_sysmoduleFld = rs.value("t_sznameitem");  
        getControl("m_module").value = "не выбрано";
        m_moduleFld = "не выбрано";  
      end;
    end;
    macro SYSMODULE_KEY_PRESSED(RsbEvent)
       if(RsbEvent.keyCode == 317 /*F3*/)
         getSysModule();
       end;
    end;

    var l_module:TRsbLabel = TRsbLabel( 3, 4, "Модуль пользоват.:" );
    addLabel( l_module );
    m_module.bindValue(this,"m_moduleFld",20);
    m_module.setPosition(20,4);                
    m_module.setSize(16,1);
    m_module.name = "m_module";
    addControl(m_module);
    m_module.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "MODULE_KEY_PRESSED"));

    macro getmodule()
      macro EvProc( rs, cmd, id, key )
        if( (cmd == DLG_KEY) and (key == KEY_ENTER) )
          rs_module = rs;  
          rs_sysmodule = null;  
          return CM_SELECT;
        end;
        return CM_DEFAULT;
      end;
      if ((rs_subsystem==null))
        MsgBox("Подсистема не выбрана"); 
        return; 
      end; 
      var sql = "SELECT * FROM ditemuser_dbt t WHERE  t.t_cidentprogram = '" + rs_subsystem.value("T_TYPE_ACCOUNT") + "' ORDER BY t.t_cidentprogram, t.t_icaseitem";
      var cmd = rsdcommand(sql);
      var rs = rsdrecordset(cmd, rsdval_client, RSDVAL_STATIC);
      if (RunScroll(rs, null, null, null, @EvProc, "Выбор модуля", "Esc Выход Enter Выбор", true, -1, -1, null, 25)==false)
        return; 
      end;
      if (rs!=null)
        getControl("m_module").value = rs.value("t_sznameitem");
        m_moduleFld = rs.value("t_sznameitem");  
        getControl("m_sysmodule").value = "не выбрано";
        m_sysmoduleFld = "не выбрано";  
      end;
    end;
    macro MODULE_KEY_PRESSED(RsbEvent)
       if(RsbEvent.keyCode == 317 /*F3*/)
         getModule();
       end;
    end;

    var l_role:TRsbLabel = TRsbLabel( 3, 6, "Роль:" );
    addLabel( l_role );
    var m_roleFld:string = "не выбрана";
    var m_role:TRsbEditField = TRsbEditField(7);
    m_role.bindValue(this,"m_roleFld",20);
    m_role.setPosition(20,6);                
    m_role.setSize(16,1);
    m_role.name = "m_role";
    addControl(m_role);
    m_role.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "ROLE_KEY_PRESSED"));

    var rs_role;
    var rs_user;
    var m_userFld:string = "не выбран";
    var m_user:TRsbEditField = TRsbEditField(7);
    macro getRole()
      macro EvProc( rs, cmd, id, key )
        if( (cmd == DLG_KEY) and (key == KEY_ENTER) )
          rs_role = rs;  
          rs_user = null;  
          return CM_SELECT;
        end;
        return CM_DEFAULT;
      end;
      var sql = "SELECT * FROM DACSROLETREE_DBT tt where tt.t_roleid not in (-1,11) order by tt.t_roleid";
      var cmd = rsdcommand(sql);
      var rs = rsdrecordset(cmd, rsdval_client, RSDVAL_STATIC);
      if (RunScroll(rs, null, null, null, @EvProc, "Выбор роли", "Esc Выход Enter Выбор", true, -1, -1, null, 25)==false)
        return; 
      end;
      if (rs!=null)
        getControl("m_role").value = rs.value("t_name");
        m_roleFld = rs.value("t_name");  
        getControl("m_user").value = "не выбрана";
        m_userFld = "не выбрана";  
      end;
    end;
    macro ROLE_KEY_PRESSED(RsbEvent)
       if(RsbEvent.keyCode == 317 /*F3*/)
         getRole();
       end;
    end;

    var l_user:TRsbLabel = TRsbLabel( 3, 8, "Пользователь:" );
    addLabel( l_user );
    m_user.bindValue(this,"m_userFld",20);
    m_user.setPosition(20,8);                
    m_user.setSize(16,1);
    m_user.name = "m_user";
    addControl(m_user);
    m_user.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "USER_KEY_PRESSED"));

    macro getUser()
      macro EvProc( rs, cmd, id, key )
        if( (cmd == DLG_KEY) and (key == KEY_ENTER) )
          rs_user = rs;  
          rs_role = null;  
          return CM_SELECT;
        end;
        return CM_DEFAULT;
      end;
      var sql = "select * from dperson_dbt";
      var cmd = rsdcommand(sql);
      var rs = rsdrecordset(cmd, rsdval_client, RSDVAL_STATIC);
      if (RunScroll(rs, null, null, null, @EvProc, "Выбор пользователя", "Esc Выход Enter Выбор", true, -1, -1, null, 25)==false)
        return; 
      end;
      if (rs!=null)
        getControl("m_user").value = rs.value("t_name");
        m_userFld = rs.value("t_name");  
        getControl("m_role").value = "не выбрана";
        m_roleFld = "не выбрана";  
      end;
    end;

    macro USER_KEY_PRESSED(RsbEvent)
       if(RsbEvent.keyCode == 317 /*F3*/)
         getUser();
       end;
    end;

    var l_subsystem:TRsbLabel = TRsbLabel( 3, 10, "Подсистема:" );
    addLabel( l_subsystem );
    var m_subsystemFld:string = "не выбрана";
    var m_subsystem:TRsbEditField = TRsbEditField(7);
    m_subsystem.bindValue(this,"m_subsystemFld",20);
    m_subsystem.setPosition(20,10);                
    m_subsystem.setSize(16,1);
    m_subsystem.name = "m_subsystem";
    addControl(m_subsystem);
    m_subsystem.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "SUBSYSTEM_KEY_PRESSED"));

    macro getSubsystem()
      macro EvProc( rs, cmd, id, key )
        if( (cmd == DLG_KEY) and (key == KEY_ENTER) )
          rs_subsystem = rs;  
          return CM_SELECT;
        end;
        return CM_DEFAULT;
      end;
      var sql = "select T_TYPE_ACCOUNT, T_NAME_TYPE from dtypeac_dbt where t_inumtype = 22";
      var cmd = rsdcommand(sql);
      var rs = rsdrecordset(cmd, rsdval_client, RSDVAL_STATIC);
      if (RunScroll(rs, null, null, null, @EvProc, "Выбор подсистемы", "Esc Выход Enter Выбор", true, -1, -1, null, 25)==false)
        return; 
      end;
      if (rs!=null)
        getControl("m_subsystem").value = rs.value("t_name_type");
        m_subsystemFld = rs.value("t_type_account");  
      end;
    end;
    macro SUBSYSTEM_KEY_PRESSED(RsbEvent)
       if(RsbEvent.keyCode == 317 /*F3*/)
         getSubsystem();
       end;
    end;

    var l_mainmenu:TRsbLabel = TRsbLabel( 3, 12, "Позиция в меню:" );
    addLabel( l_mainmenu );
    var m_mainmenuFld:string = "не выбрано";
    var m_mainmenu:TRsbEditField = TRsbEditField(7);
    m_mainmenu.bindValue(this,"m_mainmenuFld",20);
    m_mainmenu.setPosition(20,12);                
    m_mainmenu.setSize(16,1);
    m_mainmenu.name = "m_mainmenu";
    addControl(m_mainmenu);
    m_mainmenu.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "MAINMENU_KEY_PRESSED"));

    macro hasChilds(rs,opers,subsystem)  // пункт меню имеет подпункты. Предполагается, что прочие поля выбраны
      var sql = " SELECT * FROM dmenuitem_dbt t WHERE  t.t_istemplate = chr(0) AND t.t_objectid in (" + opers + ") " +
                "  AND t.t_iidentprogram = " + subsystem + 
                " AND t.t_inumberfather = " + rs.value("t_inumberpoint") ;
      var cmd = rsdcommand(sql);
      var l_rs = rsdrecordset(cmd, rsdval_client, RSDVAL_STATIC);
      return l_rs.movenext(); 
    end;

    macro getOpersByRole(roleid)
      var sql = " SELECT t2.t_oper                                   "+
                "  FROM dacsroletree_dbt t1, dacsoprole_dbt t2       "+ 
                "  WHERE t1.t_roleid = t2.t_roleid and t2.t_roleid = "+roleid; 
      var cmd = rsdcommand(sql);
      var rs = rsdrecordset(cmd,rsdval_client, RSDVAL_STATIC);
      var opers = "";
      while (rs.movenext)
         if (opers!="")
           opers = opers + "," + rs.value("t_oper");
         else
           opers = string(rs.value("t_oper"));
         end; 
      end;  
      return opers;
    end;    
    var rs_mainmenu;
    macro getMainmenu(id_farther)
      var subsystem;
      macro EvProc( rs, cmd, id, key )
        if( (cmd == DLG_KEY) and (key == KEY_ENTER) )
          if (rs.value("t_inumberline")!=null) 
            getMainmenu(rs.value("t_inumberpoint"));
          end;
          if (menu_selected==false)
            return CM_DEFAULT;
          else
            return CM_SELECT;
          end;  
        elif( (cmd == DLG_KEY) and (key == 316)/*F2*/ )
          if (not hasChilds(rs,opers,subsystem)) 
            MsgBox("Необходимо выбрать меню, но не пункт меню");
            return;
          end;   
          rs_mainmenu = rs;  
          menu_selected = true;
          getControl("m_mainmenu").value = rs.value("t_sznameitem");
          return CM_SELECT;
        end;
        return CM_DEFAULT;
      end;
      if (not debugmode)
        if ((rs_user==null) and (rs_role==null))
          MsgBox("Пользователь или роль не выбрана"); 
          return; 
        end; 
        if (rs_subsystem==null)
          MsgBox("Подсистема не выбрана"); 
          return; 
        end; 
        if (rs_user!=null)  
          opers = string(rs_user.value("t_oper")); 
        else
          opers = getOpersByRole(rs_role.value("t_roleid"));   
        end;   
        subsystem = codefor(rs_subsystem.value("T_TYPE_ACCOUNT"));
      else // debug
        if ((rs_user==null) and (rs_role==null))
          opers = 1; 
        else
          if (rs_user!=null)  
            opers = rs_user.value("t_oper"); 
          else
            opers = getOpersByRole(rs_role.value("t_roleid"));   
          end;   
        end;    
        if (rs_subsystem==null)
          subsystem = codefor("S");
        else
          subsystem = codefor(rs_subsystem.value("T_TYPE_ACCOUNT"));
        end;   
      end;
      var sql = " SELECT distinct T_IIDENTPROGRAM,T_INUMBERPOINT,T_INUMBERFATHER,T_INUMBERLINE,T_ICASEITEM,T_CSYSTEMITEM,T_SZNAMEITEM,T_SZNAMEPROMPT "+
                "  FROM dmenuitem_dbt t WHERE  t.t_istemplate = chr(0) AND t.t_objectid in (" + opers + ") " +
                "  AND t.t_iidentprogram = " + subsystem + " AND t.t_inumberfather = " + id_farther +
                "  ORDER BY t.t_inumberline ";
      var cmd = rsdcommand(sql);
      var rs = rsdrecordset(cmd, rsdval_client, RSDVAL_STATIC);
      if (RunScroll(rs, null, null, null, @EvProc, "Выбор позиции в меню", "Esc-Выход Enter-Провалиться F2-выбрать", true, -1, -1, null, 25)==false)
        return; 
      end;
      if (rs!=null) //нажата F2
        if (selected_menu_label=="")
          selected_menu_label =  rs.value("t_sznameitem");
        else 
          selected_menu_label =  selected_menu_label + " в " + rs.value("t_sznameitem");
        end;
        m_mainmenuFld = rs.value("t_sznameitem");  
      end;
    end;
    macro MAINMENU_KEY_PRESSED(RsbEvent)
       var save_selected_menu_label;
       if(RsbEvent.keyCode == 317 /*F3*/)
         menu_selected = false;
         save_selected_menu_label = selected_menu_label;  
         selected_menu_label = ""; 
         getMainmenu(0);
         if (menu_selected)
           MsgBox("Выбрано меню: "+selected_menu_label);
         else
           selected_menu_label = save_selected_menu_label;    
         end;    
         this.redraw();   
       end;
    end;

    var l_title:TRsbLabel = TRsbLabel( 3, 14, "Название пункта меню:" );
    addLabel( l_title );
    var m_titleFld:string = "";
    var m_title:TRsbEditField = TRsbEditField(7);
    m_title.bindValue(this,"m_titleFld",20);
    m_title.setPosition(20,14);                
    m_title.setSize(16,1);
    m_title.name = "m_titlemenu";
    addControl(m_title);
   
    macro getMaxINUMBERPOINT(objectid,identprogram)
      var sql = " SELECT max(t_inumberpoint) FROM dmenuitem_dbt t WHERE  t.t_istemplate = chr(0) AND t.t_objectid = " + objectid + 
                "   AND t.t_iidentprogram = " + identprogram ;
      var cmd = rsdcommand(sql); 
      var rs = rsdrecordset(cmd, rsdval_client, RSDVAL_STATIC);
      if (rs.movenext) 
        return rs.value(0);
      end;
    end;

    macro getMaxNumberLine(objectid,identprogram)
      var sql = " SELECT max(t_inumberline) FROM dmenuitem_dbt t WHERE  t.t_istemplate = chr(0) AND t.t_objectid = " + objectid + 
                "   AND t.t_iidentprogram = " + identprogram ;
      var cmd = rsdcommand(sql); 
      var rs = rsdrecordset(cmd, rsdval_client, RSDVAL_STATIC);
      if (rs.movenext) 
        return rs.value(0);
      end;
    end;
  
    macro insertMenuItems()
      var moduleId;   
      var chrSysModule = 0;
      var progitem; 
      var INUMBERPOINT;

      macro insertMenuItem(l_oper)
        var newINUMBERLINE = getMaxNumberLine(l_oper,progitem);
        if (ValType(newINUMBERLINE)==3)
          newINUMBERLINE = newINUMBERLINE + 1; 
        else
          MsgBox("У пользователя t_oper="+l_oper+" данное меню не задано");
          println("У пользователя t_oper="+l_oper+" данное меню не задано");  
          return;
        end;  
        var newINUMBERPOINT = getMaxINUMBERPOINT(l_oper,progitem);
        if (ValType(newINUMBERPOINT)==3)
          newINUMBERPOINT = newINUMBERPOINT + 1; 
        else
          MsgBox("У пользователя t_oper="+l_oper+" данное меню не задано");
          println("У пользователя t_oper="+l_oper+" данное меню не задано");
          return;
        end;  
        var sql = " INSERT INTO dmenuitem_dbt (T_OBJECTID,T_ISTEMPLATE,T_IIDENTPROGRAM,T_INUMBERPOINT,T_INUMBERFATHER,T_INUMBERLINE, " + 
                  "   T_ICASEITEM,T_CSYSTEMITEM,T_SZNAMEITEM,T_SZNAMEPROMPT,T_IHELP,T_IPROGITEM) " +
                  " VALUES (?,chr(0),?,?,?,?,?,chr(?),?,?,?,?) ";
  /* отладка
                        " VALUES ("+l_oper+",chr(0),"+progitem+","+newINUMBERPOINT+","+
                  INUMBERPOINT+","+newINUMBERLINE+
                  ","+moduleid+",chr("+chrSysModule+"),"+"'test2'"+","+"'test2'"+",0,"+progitem+") ";
  */
        var cmd = RSDCommand(sql);
        cmd.addParam("", RSDBP_IN, l_oper );  // T_OBJECTID
        cmd.addParam("", RSDBP_IN, progitem );  // T_IIDENTPROGRAM
        cmd.addParam("", RSDBP_IN, newINUMBERPOINT); // T_INUMBERPOINT
        cmd.addParam("", RSDBP_IN, INUMBERPOINT); // T_INUMBERFATHER
        cmd.addParam("", RSDBP_IN, newINUMBERLINE ); // T_INUMBERLINE
        cmd.addParam("", RSDBP_IN, moduleid); // T_ICASEITEM
        cmd.addParam("", RSDBP_IN, chrSysModule); // T_CSYSTEMITEM
        cmd.addParam("", RSDBP_IN, m_title.value); // T_SZNAMEITEM
        cmd.addParam("", RSDBP_IN, m_title.value); // T_SZNAMEPROMPT
        cmd.addParam("", RSDBP_IN, 0); // T_IHELP
        cmd.addParam("", RSDBP_IN, progitem); // T_IPROGITEM
        cmd.execute();
        var str_line =  "Пункт меню с названием \""+m_title.value+"\" добавлен для пользователя id="+
                         l_oper+" . Модуль id="+moduleid+". Подсистема "+strfor(progitem)+". Родительское меню \""+selected_menu_label+"\"";
        MsgBox(str_line);
        println(str_line);
      end; 

      if (not debugmode)
        if ((rs_module==null) and (rs_sysmodule==null))
          MsgBox("Не выбран пользовательский или системный модуль");
          return;
        end;
        if (strlen(m_title.value)==0)
          MsgBox("Не введено название пункта меню");
          return;
        end; 
      else
        m_title.value = "test100";
      end;

      if (not debugmode)
        if (rs_sysmodule!=null)
          chrSysModule = 88; 
          moduleId = rs_sysmodule.value("t_icaseitem"); 
        else
          moduleId = rs_module.value("t_icaseitem"); 
        end;
      else //debug
          moduleid = 924; 
      end;

      if (not debugmode)
        if (rs_subsystem==null)
          MsgBox("Не выбрана подсистема"); 
          return;
        else
          progitem = codefor(rs_subsystem.value("t_type_account")); 
        end;
      else //debug
        if (rs_subsystem==null)
          progitem = 83; 
        else
          progitem = codefor(rs_subsystem.value("t_type_account")); 
        end;   
      end;

      if (not debugmode)
        if (rs_mainmenu==null)
          MsgBox("Не выбрана позиция в меню"); 
          return;
        else
          INUMBERPOINT = rs_mainmenu.value("T_INUMBERPOINT");  
        end; 
      else //debug
        if (rs_mainmenu==null)
         INUMBERPOINT = 4; 
        else
          INUMBERPOINT = rs_mainmenu.value("T_INUMBERPOINT");  
        end; 
      end;

      var opers_array = tarray;
      if (strlen(opers)==0)  
        MsgBox("Не заданы пользователи или роль, а после необходимо задать позицию в меню");  
      end;
      if (debugmode)
        opers_array(0)=1; 
      else
        opers_array = split(opers);
      end;
      for (var oper,opers_array)
        insertMenuItem(oper); 
      end;
    end;

    macro Panel_ButtonEvent(RsbEvent)
       if(RsbEvent.keyCode == 323)/*F2 F9*/
           stat = 0;
           insertMenuItems(); 
//           this.close(1);
//           this.redraw(); 
       elif(RsbEvent.keyCode == 27) 
           stat = 1;
       end;
    end;

end;

var panel = SvodPanel;
panel.run;
//exit(1);