/*
$Name:         vtb_load.mac
$Module:       Ценные бумаги
$Description:  Начальное решение ВТБ. Макрос запуска
*/

import dealsinter, cb_sql;
import captureoutput;
import "dlquery.mac";
import vtb_loadlot;
import vtb_LoadCodeMatch;
import vtb_LoadContract;
import vtb_MarkClientsAndContract;
import vtb_loadlot_scroll;
import vtb_LoadLot2;
import vtb_LoadLotNDFL;
import vtb_nptxoprecalc;
import vtb_Upd_Price;
import "vtb_SendNotification.mac";
import vtb_stat;
import "nocountiss_vtb.mac";

private macro ProcessRecords()

end;

class (TArray) VtbTStrCollector
   /*Очистить массив.*/
  macro ClearArray
    this.Size = 0;
  end;
  /*Добавить строку в массив.*/
  macro AddString( Str )
    this.(this.Size) = Str;
  end;
end;


private macro DeleteProcessItem(process:@rsdrecordset, errmsg:@string)
  var exec;
  StartCaptureOutput();
  [
    delete from UPROCVTB_PR_DBT
    where t_subtype = ?
  ];
  exec = RsdCommand(EndCaptureOutput());
  exec.AddParam("processsubtype", RSDBP_IN, process.value("t_subtype"));
  exec.Execute();
  process.edit();
  process.value("t_isfinish") = "";
  process.value("t_status") = 0;
  process.value("t_statusname") = "Не обработан";
  process.value("t_statusmsg") = "";
  process.update();
  return true;
onError(erru)  
  errmsg = "onError: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
  SetDialogFlag(1);
  return false;
end;


private macro RunProcessItem(process:@rsdrecordset, errmsg:@string)

  var result_comm = tarray();
  var seanceid = 0;
  var resultproc;
  var exec;
  var result_protocol = VtbTStrCollector;

  errmsg = "";

  /*Сохраняем результат*/
  macro SaveResult()

    var i = 0;

    /*Протокол работы*/
    InitProgress(result_protocol.size, "Сохранение протокола");
    while(i < result_protocol.size)
      StartCaptureOutput();
      [
        declare
          processsubtype number := ?;
          str varchar2(4000) := ?;
          cl clob := null;
        begin
          -- Читаем текущее значение
          begin
            select t_protocol
            into cl
            from UPROCVTB_PR_DBT 
            where t_subtype = processsubtype 
            for update nowait;
          exception when no_data_found then
            insert into UPROCVTB_PR_DBT values(processsubtype, null);
          end;
          -- Создаем CLOB при необходимости
          if(cl is null)then
            dbms_lob.createtemporary(cl, true);
          end if;
          -- Дописываем строку в CLOB
          dbms_lob.writeappend(cl, length(str), str);
          -- Сохраняем результат
          update UPROCVTB_PR_DBT
          set t_protocol = cl
            where t_subtype = processsubtype ;
        end;
      ];
      exec = RsdCommand(EndCaptureOutput());
      exec.AddParam("processsubtype", RSDBP_IN, process.value("t_subtype"));
      exec.AddParam("str", RSDBP_IN, result_protocol.(i) + "\n");
      exec.Execute();
      UseProgress(i = i + 1);
    end;
    RemProgress;
  end;



  /*Создаем экземпляр процесса*/
  StartCaptureOutput();
  [
    declare
      processsubtype integer := ?;
      process_u uProcVTB_dbt%rowtype;
      res integer := 0;
    begin
      begin
        -- Захватываем запись
        select *
        into process_u
        from uProcVTB_dbt
        where t_subtype = processsubtype and
              (t_isfinish = chr(0) or t_isfinish = 'S')
        for update nowait;
        -- Результат
        res := process_u.t_count + 1;
      exception when others then
        null;
      end;
      ? := res;
    end;
  ];
  exec = RsdCommand(EndCaptureOutput());
  exec.AddParam("processsubtype", RSDBP_IN, process.value("t_subtype"));
  exec.AddParam("res", RSDBP_OUT, V_INTEGER);
  exec.Execute();
  if((valtype(exec.value("res")) != V_INTEGER) or (exec.value("res") <= 0))
    errmsg = "Процесс уже запущен/выполнен";
    return false;
  end;


  /*Запуск обработки процессов по типам*/
  errmsg = "";
  if(process.value("t_subtype") == 100)
     resultproc = VTB_LoadLots(@errmsg, @result_protocol);
  elif(process.value("t_subtype") == 150)
     resultproc = RunLoadLotScroll();
  elif(process.value("t_subtype") == 200)
     resultproc = runLoadCodeMatch(@errmsg, @result_protocol);
     if (errmsg) msgbox(errmsg) end;
  elif(process.value("t_subtype") == 250)
     resultproc = runLoadContract(@errmsg, @result_protocol);
     if (errmsg) msgbox(errmsg) end;
  elif(process.value("t_subtype") == 300)
     resultproc = runMarkClientsAndContract(@errmsg, @result_protocol);
     if (errmsg) msgbox(errmsg) end;
  elif(process.value("t_subtype") == 350)
     resultproc = runLoadLot2(false, @errmsg, @result_protocol);
     if (errmsg) msgbox(errmsg) end;
  elif(process.value("t_subtype") == 400)
     resultproc = runLoadLot2(true, @errmsg, @result_protocol);
     if (errmsg) msgbox(errmsg) end;
  elif(process.value("t_subtype") == 450)
     resultproc = runProcessLot2(@errmsg, @result_protocol);
     if (errmsg) msgbox(errmsg) end;
  elif(process.value("t_subtype") == 500)
     resultproc = runLoadLot3NDFL(@errmsg, @result_protocol);
     if (errmsg) msgbox(errmsg) end;
  elif(process.value("t_subtype") == 550)
     resultproc = runProcessLot2NDFL(@errmsg, @result_protocol);
     if (errmsg) msgbox(errmsg) end;
  elif(process.value("t_subtype") == 600)
     resultproc = CreateNptxopRecalvVTB(Date("21.04.2022"), {curdate},"VTB_r", @errmsg, @result_protocol);
     if (errmsg) msgbox(errmsg) end;
  elif(process.value("t_subtype") == 650)
     resultproc = UpdatePrice(@errmsg, @result_protocol);
     if (errmsg) msgbox(errmsg) end;
  elif(process.value("t_subtype") == 700)
     resultproc = ProcSendTaxOverPayNotify(@errmsg, @result_protocol);
     if (errmsg) msgbox(errmsg) end;
  elif(process.value("t_subtype") == 750)
     resultproc = RunStatReport(@errmsg, @result_protocol);
     if (errmsg) msgbox(errmsg) end;
  elif(process.value("t_subtype") == 800)
     resultproc = RunUvtbDataScroll();
  elif(process.value("t_subtype") == 900)
     resultproc = NoCountIISOperationMenuRun(@errmsg);
     if (errmsg) msgbox(errmsg) end;
  else
    errmsg = "Неизвестный процесс " + process.value("t_subtype");
    resultproc = false;
  end; 

  if (process.value("t_statusname") != "Скроллинг")
     if(not resultproc)
        if (errmsg == "")
           errmsg = "Неизвестная ошибка при обработке процесса";
        end;
        SaveResult();
        process.edit();
        process.value("t_statusname") = "Ошибка обработки";
        process.value("t_isfinish") = "E";
        process.value("t_status") = 2;
        process.value("t_statusmsg") = substr(errmsg,1,200);
        process.update();
     else
        SaveResult();
        process.edit();
        process.value("t_isfinish") = "X";
        process.value("t_status") = 10;
        if (errmsg == "" )
           process.value("t_statusname") = "Обработан";
        else 
           process.value("t_statusname") = "Обработан с предупреждениями";
        end;
        process.value("t_statusmsg") = substr(errmsg,1,200);
        process.update();
     end;
  end;
  MsgBox("Обработка завершена");
  return true;
end;

macro eInfoDialog(rs, cmd, id, key)

   file ReportOutFile() txt write;
   var ReportFileName;


   if(cmd == DLG_KEY)
      /*Esc*/
      if(key == 27)
         return CM_CANCEL;
      elif(key == 13)
         if(rs.RecCount > 0)
            var cmdLog, rsLog, strLog;
            var cmdLogs, rsLogs;
            var DLComm;
            var ind = 1, deltaind = 1024, idec;

            ReportFileName = GetTxtFileName("protocol");
            if(not Open(ReportOutFile, ReportFileName))
               msgboxex("Ошибка при открытии файла отчета|"+ReportFileName);
            end;
            strLog = "";
            while(ind > 0)
               StartCaptureOutput();
               [
               select nvl(dbms_lob.substr(process_oper_u.t_protocol, ?, ?),' ') t_log
               from UPROCVTB_PR_DBT process_oper_u
               where process_oper_u.t_subtype = ?
               ];
               cmdLog = DL_RsdCommand(EndCaptureOutput());
               cmdLog.AddParam(deltaind);
               cmdLog.AddParam(1 + deltaind * (ind - 1));
               cmdLog.AddParam(rs.value("t_subtype"));
               rsLog = cmdLog.Execute();
               if((rsLog.moveNext()) and (rsLog.value("t_log") != " "))
                  strLog = strLog + rsLog.value("t_log");
                  while(index(strLog, "\n") > 0)
                     idec = index(strLog, "\n");
                     insert(ReportOutFile, substr(strLog, 1, idec-1));
                     strLog = substr(strLog, idec+1);
                  end;
                  ind = ind + 1;
               else
                  if(strlen(strLog) > 0)
                     insert(ReportOutFile, strLog);
                  end;
                  ind = 0;
               end;
            end;
            close(ReportOutFile);
            viewfile(ReportFileName);
         end;
      end;
   end;
end;


    
private macro RunProcess()

  var errmsg;
  var inprocessprogress = 0;

  macro makeArray():TArray
    var parm:variant, i:integer = 0, result:TArray = TArray( ParmCount(), ParmCount() );
    while( GetParm(i, parm) )
      result.value(i) = parm;
      i = i + 1;
    end;
    return result;
  end;

  var SaveNum = 0;
  macro eRunProcessDialog(rs, cmd, id, key)

    if(cmd == DLG_INIT)
      /*Позиционируемся*/
      if(SaveNum > 0)
        while(rs.movenext)
          if(SaveNum == rs.value("t_num"))
            GoToScroll(rs);
            break;
          end;
        end;
      end;
    elif(cmd == DLG_KEY)
      /*Esc*/
      if(key == 27)
        return CM_CANCEL;
      /*F2*/
      elif(key == 316)
        if((rs.value("t_isfinish") != "") and (rs.value("t_isfinish") != "S"))
          msgboxex("Процесс '" + rs.value("t_name") + "' обработан");
        elif(msgboxex("Выполнить обработку процесса '" + rs.value("t_name") + "'?", MB_YES + MB_NO, IND_YES) == IND_YES)
          BegAction(1, rs.value("t_name") + ". Ждите...");
          if(not RunProcessItem( @rs, @errmsg))
            updatescroll(rs, 5);
            EndAction(1);
            msgboxex("Ошибка при выполнении процесса '" + rs.value("t_name") + "'|" + errmsg);
          else
            EndAction(1);
            updatescroll(rs, 5);
          end;
        end;
        return CM_IGNORE;
      /*F8*/
      elif(key == 322)
         if (rs.value("t_statusname") != "Скроллинг")
            if(rs.value("t_isfinish") == "")
              msgboxex("Процесс '" + rs.value("t_name") + "' не обработан");
            elif(msgboxex("Удалить обработку процесса '" + rs.value("t_name") + "'?", MB_YES + MB_NO, IND_YES) == IND_YES)
              if(not DeleteProcessItem(@rs, @errmsg))
                updatescroll(rs, 5);
                msgboxex("Ошибка при удалении обработки процесса '" + rs.value("t_name") + "'|" + errmsg);
              else
                updatescroll(rs, 5);
              end;

            end;
        end;
        return CM_IGNORE;
//----------------------------
      /*Enter*/
      elif(key == 13)
        var cmdinfo, rsinfo;
        if(rs.value("t_isfinish") != "")

            StartCaptureOutput();
            [
              select chr(1) t_oprcode, 
                     'Протокол работы процедуры' t_oprname, 
                     chr(1) t_oprstatus,
                     process_oper_u.t_subtype
              from UPROCVTB_PR_DBT process_oper_u
              where process_oper_u.t_subtype = ? 
            ];
            cmdInfo = RsdCommand(EndCaptureOutput());

          cmdInfo.AddParam("processsubtype", RSDBP_IN, rs.value("t_subtype"));
          rsInfo = RSDRecordset(cmdInfo, RSDVAL_CLIENT, RSDVAL_STATIC);
          RunScroll(rsInfo, 3, MakeArray("t_oprcode", "Код", 25, 2, -1, 0,
                                         "t_oprname", "Наименование", 30, 2, -1, 0,  
                                         "t_oprstatus", "Состояние", 20, 2, -1, 0), 
                    "_eInfoDialog", "eInfoDialog", rs.value("t_name"), "~Enter~ Информация ~Esc~ Выход", true);
        end;
        return CM_IGNORE;


//----------------------------
      end;
    end;
  end;


  var sql, cmd, rs;
  var exec;
  sql = "declare " +
        " retval number := 0; " +
        " sql_crt varchar2(1000); " +
        "begin " +
        "sql_crt := 'CREATE TABLE uProcVTB_dbt '|| " +
        "           '( T_SUBTYPE       NUMBER(5), T_NUM  NUMBER(5),'|| " +
        "           'T_NAME          VARCHAR2(100 BYTE), '|| " +
        "           'T_STATUS        NUMBER, '|| " +
        "           'T_STATUSNAME    VARCHAR2(30 BYTE), T_ISFINISH  CHAR(1 BYTE), T_COUNT  NUMBER(10),'|| " +
        "           'T_STATUSMSG     VARCHAR2(200 BYTE)) '; " +
        " " +
        "select count(1) into retval from user_tables where upper (table_name) = 'UPROCVTB_DBT'; " +
        "if retval = 0 then " +
        "  execute immediate (sql_crt); " +
        "end if;  " +
        "end; " ;
   cmd = RSDCommand(sql);
   cmd.execute;

  sql = "declare " +
        " retval number := 0; " +
        " sql_crt varchar2(1000); " +
        "begin " +
        "sql_crt := ' CREATE TABLE UPROCVTB_PR_DBT '|| " +
        "           ' ( T_SUBTYPE       NUMBER(5), '|| " +
        "           '  T_PROTOCOL  CLOB ) '; " +
        " " +
        "select count(1) into retval from user_tables where upper (table_name) = 'UPROCVTB_PR_DBT'; " +
        "if retval = 0 then " +
        "  execute immediate (sql_crt); " +
        "end if;  " +
        "end; " ;
   cmd = RSDCommand(sql);
   cmd.execute;


  StartCaptureOutput();
  [
    declare
    begin
        merge into uProcVTB_dbt p
        using
        (select t_subtype, t_num, t_status, t_isfinish, t_name
         from (select 100 t_subtype,   1 t_num, '------------' t_status, chr(0) t_isfinish, 'Загрузка файла с остатками ЦБ' t_name from dual union all
               select 150 t_subtype,   2 t_num, 'Скроллинг' t_status, chr(0) t_isfinish, 'Остатки ЦБ' t_name from dual union all
               select 200 t_subtype,   3 t_num, '------------' t_status, chr(0) t_isfinish, 'Загрузка файла соответствия кодов клиентов' t_name from dual union all 
               select 250 t_subtype,   4 t_num, '------------' t_status, chr(0) t_isfinish, 'Загрузка файла договоров' t_name from dual union all
               select 300 t_subtype,   5 t_num, '------------' t_status, chr(0) t_isfinish, 'Разметка клиентов и договоров признаком принадлежности к ВТБ ' t_name from dual union all
               select 350 t_subtype,   6 t_num, '------------' t_status, chr(0) t_isfinish, 'Загрузка депозитарных зачислений (краткий формат без цены) ' t_name from dual union all
               select 400 t_subtype,   7 t_num, '------------' t_status, chr(0) t_isfinish, 'Загрузка депозитарных зачислений с обновлением остатка (краткий формат без цены) ' t_name from dual union all
               select 450 t_subtype,   8 t_num, '------------' t_status, chr(0) t_isfinish, 'Выполнение операций зачисления депозитарных зачислений ' t_name from dual union all 
               select 500 t_subtype,   9 t_num, '------------' t_status, chr(0) t_isfinish, 'Загрузка зачислений для целей НДФЛ' t_name from dual union all 
               select 550 t_subtype,  10 t_num, '------------' t_status, chr(0) t_isfinish, 'Исполнение зачислений для целей НДФЛ' t_name from dual union all  
               select 600 t_subtype,  11 t_num, '------------' t_status, chr(0) t_isfinish, 'Создание операций пересчета НОБ по НДФЛ' t_name from dual union all  
               select 650 t_subtype,  12 t_num, '------------' t_status, chr(0) t_isfinish, 'Сверка кол-ва НДФЛ и ДЕПО зачислений. Расчет средней цены для ДЕПО зачислений' t_name from dual union all 
               select 700 t_subtype,  13 t_num, '------------' t_status, chr(0) t_isfinish, 'Рассылка уведомлений клиентам о перерасчете НДФЛ' t_name from dual  union all
               select 750 t_subtype,  14 t_num, '------------' t_status, chr(0) t_isfinish, 'Отчет по статистике загрузки' t_name from dual union all
               select 800 t_subtype,  15 t_num, 'Скроллинг' t_status, chr(0) t_isfinish, 'Исходные данные по ценам от ВТБ' t_name from dual union all
               select 900 t_subtype,  16 t_num, '------------' t_status, chr(0) t_isfinish, 'Проставление признака "Ц/б не учитывались на ИИС"' t_name from dual
              )) p_new
        on ( p.t_subtype = p_new.t_subtype)
        when not matched then
          insert values(p_new.t_subtype, p_new.t_num, p_new.t_name, 0, p_new.t_status, p_new.t_isfinish, 0,chr(1));
    end;
  ];
  exec = RsdCommand(EndCaptureOutput());
  exec.Execute();

  var cmdQuery, cmdScroll, rsScroll;

  StartCaptureOutput();
  [
    select *
    from UPROCVTB_DBT
    order by t_num, t_name
  ];
  cmdQuery = EndCaptureOutput();
  cmdScroll = RsdCommand(cmdQuery);
  rsScroll = RSDRecordset(cmdScroll, RSDVAL_CLIENT, RSDVAL_STATIC);
  while(RunScroll(rsScroll, 4, MakeArray("t_num", "№", 2, 2, -1, 0,
                                         "t_name", "Процесс", 60, 2, 2, 0, 
                                         "t_statusname", "Состояние", 17, 2, -1, 0, 
                                         "t_statusmsg", "", 50, 2, -1, 0), 
                        "eRunProcessDialog", "eRunProcessDialog", "ВТБ. Начальное решение ", "~F2~ Обработать ~F8~ Сбросить ошибку", true))

    cmdScroll = RsdCommand(cmdQuery);
    rsScroll = RSDRecordset(cmdScroll, RSDVAL_CLIENT, RSDVAL_STATIC);
  end;
end;
runprocess();
exit(1);
      