import RsbFormsInter, rsd;

const RSB_EV_CHANGE_VALUE = RSB_EV_MIN_USER + 77;
const RSB_EV_LIST_GETDSET = RSB_EV_MIN_USER + 75;
 
// данные сессии
class CommonSessUtil
   macro get_dttms
      var sqls, rsdc;
      sqls = String( 
                     "BEGIN   \n"
                    ,"   :p_Ret := To_Char(Sysdate, 'DD.MM.YYYY HH24:MI:SSSSS');   \n"
                    ,"END;   \n"
                   );
      rsdc = rsdcommand(sqls);
      rsdc.AddParam("p_Ret", RSDBP_OUT, v_string);
      rsdc.execute;
      sqls = rsdc.Value("p_Ret");
      rsdc = null;
      return sqls;
   end;
   // текущий филиал
   macro get_oper_fil
      var sqls, rsdc;
      sqls = String( 
                     "BEGIN   \n"
                    ,"   :p_Ret := Rsbsessiondata.OperDprt;   \n"
                    ,"END;   \n"
                   );
      rsdc = rsdcommand(sqls);
      rsdc.AddParam("p_Ret", RSDBP_OUT, v_integer);
      rsdc.execute;                              
      sqls = int(rsdc.Value("p_Ret"));
      rsdc = null;
      return sqls;
   end;
   // текущий код операциониста
   macro get_oper_cod      
      var sqls, rsdc;
      sqls = String( 
                     "BEGIN   \n"
                    ,"   :p_Ret := Rsbsessiondata.Oper;   \n"
                    ,"END;   \n"
                   );
      rsdc = rsdcommand(sqls);
      rsdc.AddParam("p_Ret", RSDBP_OUT, v_integer);
      rsdc.execute;                              
      sqls = int(rsdc.Value("p_Ret"));
      rsdc = null;
      return sqls;
   end;
   // текущий доп.офиса
   macro get_oper_dpt
      var sqls, rsdc;
      sqls = String( 
                     "BEGIN   \n"
                    ,"   :p_Ret := Rsbsessiondata.OperDprtNode;   \n"
                    ,"END;   \n"
                   );
      rsdc = rsdcommand(sqls);
      rsdc.AddParam("p_Ret", RSDBP_OUT, v_integer);
      rsdc.execute;                              
      sqls = int(rsdc.Value("p_Ret"));
      rsdc = null;
      return sqls;
   end;
   // текущая дата
   macro get_oper_dat
      var sqls, rsdc;
      sqls = String( 
                     "BEGIN   \n"
                    ,"   :p_Ret := Rsbsessiondata.Curdate;   \n"
                    ,"END;   \n"
                   );
      rsdc = rsdcommand(sqls);
      rsdc.AddParam("p_Ret", RSDBP_OUT, v_Dttm);
      rsdc.execute;
      dttmsplit(rsdc.Value("p_Ret"), sqls);                              
      rsdc = null;
      return sqls;
   end;
   // текущая дата
   macro get_oper_ses
      var sqls, rsdc;
      sqls = String( 
                     "BEGIN   \n"
                    ,"   :p_Ret := Sys_Context('Userenv', 'SESSIONID');   \n"
                    ,"END;   \n"
                   );

      rsdc = rsdcommand(sqls);
      rsdc.AddParam("p_Ret", RSDBP_OUT, v_String);
      rsdc.execute;
      dttmsplit(rsdc.Value("p_Ret"), sqls);                              
      rsdc = null;
      return sqls;
   end;
   // параметр null или  nullval
   macro is_null_prm(fld)
      if ((fld != null)
          and
          (fld != nullval)
         )
         return false;
      end;
      return true;
   end;     
   // показ содержимого таблицы фильтра
   macro show_tbl(sql)
      if (CommonSessUtil.is_null_prm(sql) or (strlen(trim(sql)) == 0))
         sql = "select * from dual";
         if (not getstring(sql, "Запрос ?", 150))
            return;
         end; 
         if (strlen(trim(sql)) == 0)
            return;
         end;
      end;
      runscroll(RsdRecordset(sql, RSDVAL_CLIENT, RSDVAL_STATIC),null, null, null);
   onerror(err);
      msgbox(err.Message);
   end;
   // метка в трассе
   macro mark_ora_trace(p)
      var sqls, rsdc;
      sqls = "declare v_mrk varchar2(100); begin select :p into v_mrk from dual; end;";
      rsdc = rsdcommand(sqls);
      if ((p == null)
          or
          (p == nullval)
         )
         p = 0;
      end;
      if (valtype(p) != v_String)
         p = string(p);
      end;
      rsdc.AddParam("p",RSDBP_IN, p);
      rsdc.execute;
      rsdc = null;
   end;
   // обработчик исключения
   macro except_msg(err, rsdc)
      var i = 0 ,str = "", err_str = string(err.message, " ", err.Module, " строка ", err.Line), count = 0;
      if (err.Err)
         if (IsEqClass ("TRsComErr", err.err))
            count = err.err.Count;
            i = 0;
            while (i < count)
               str = string(str, err.err.Message (i), " (Code = ",err.err.Code (i), ", Level = ",err.err.Level (i), ")", strfor(10));
               i = i + 1
            end;
         elif (valtype(err.err) == v_string)
            str = err.err;
         else
            str = err_str;
         end;    
      elif (err.Code == 42)   
         if (rsdc and rsdc.Connection and rsdc.Connection.environment)
            i = 0;
            str = "";
            while (i < rsdc.Connection.environment.ErrorCount)
                  str = string(str,rsdc.Connection.environment.Error(i).Descr, strfor(10));
                  i = i + 1;
            end;
            if (strlen(trim(str)) == 0)
               str = err_str;
            end;      
            rsdc.Connection.environment.ClearErrors;
         else
            str = err_str;
         end;
      else
         str = err_str;
      end;
      return str;
   onerror(er)
      return(err_str);
   end;
end;
// класс скроллинга
class T_RslScroll ()
   var scr_name  = "";    
   var scr_ronly = true;
   var colInfo;

   var title   = "";
   var st_line = "";
   //
   macro numCol
      if (colInfo)
         return colInfo.size / 6
      end
   end;
   //                            
   macro RmvCol
      colInfo = null;
   end;
   //
   macro is_pos(sql)
      gotoscroll(sql);
      if ((sql.Value(0) != null) and (sql.Value(0) != nullval))
         return true;
      end;
      return false;
   end;
   //
   macro AddCol(fld, head, width, fldtype, decpoint) 
      var ind = 0;    
      if (not colInfo)
         colInfo = TArray;
      end;       
      ind = colInfo.Size/6;
      colInfo.value (ind * 6)      = fld;
      colInfo.value (ind * 6 + 1)  = head;
      colInfo.value (ind * 6 + 2)  = width;
      colInfo.value (ind * 6 + 3 ) = fldType;
      colInfo.value (ind * 6 + 4 ) = decPoint;
      colInfo.value (ind * 6 + 5 ) = 0;
   end;
   // Обработчики клавиш
   macro on_scr_key_13(sql, field, key_kod)
   end;
   macro on_scr_key_32(sql, field, key_kod)
   end;
   macro on_scr_key_PLUS(sql, field, key_kod)
   end;
   macro on_scr_key_MINUS(sql, field, key_kod)
   end;
   macro on_scr_key_F2(sql, field, key_kod)
   end;
   macro on_scr_key_F3(sql, field, key_kod)
   end;
   macro on_scr_key_F5(sql, field, key_kod)
   end;
   macro on_scr_key_F6(sql, field, key_kod)
   end;
   macro on_scr_key_AltF6(sql, field, key_kod)
   end;
   macro on_scr_key_F7(sql, field, key_kod)
   end;
   macro on_scr_key_F8(sql, field, key_kod)
   end;
   macro on_scr_key_AltF8(sql, field, key_kod)
   end;
   macro on_scr_key_AltF7(sql, field, key_kod)
   end;
   macro on_scr_key_CtrlF8(sql, field, key_kod)
   end;
   macro on_scr_key_F9(sql, field, key_kod)
   end;
   macro on_scr_key_AltF9(sql, field, key_kod)
   end;
   macro on_scr_key_Esc(sql, field, key_kod)
   end;
   macro on_scr_key_empty(sql, field, key_kod)
   end;
   macro on_scr_key_CTRL_UP(sql, field, key_kod)
   end;
   macro on_scr_key_CTRL_DN(sql, field, key_kod)
   end;
   macro on_scr_init(sql, field, key_kod)
   end;
   macro on_scr_mlt_beg(sql, field, key_kod)
   end;
   macro on_scr_mlt_end(sql, field, key_kod)
   end;
   macro on_scr_mlt_sel(sql, field, key_kod)
   end;
   macro on_scr_preinit(sql, field, key_kod)
   end;
   macro on_scr_mouse_left(sql, field, key_kod)
   end;
   macro on_scr_mouse_right(sql, field, key_kod)
   end;
   /************************************/
   macro EvProc (sql, event, field, key_kod)
      //execmacrofile("all_event_dlg.mac", "decode_event", sql, event, field, key_kod, null);
      if (event == DLG_INIT)
         on_scr_init(sql, event, field, key_kod);
      end;
      if (event == DLG_PREINIT)
         on_scr_preinit(sql, event, field, key_kod);
      end;
      if (event == DLG_MSELSTART)
         return on_scr_mlt_beg(sql, field, key_kod);
      end;
      if (event == DLG_MSEL)
         return on_scr_mlt_sel(sql, field, key_kod);
      end;
      if (event == DLG_MSELEND)
         return on_scr_mlt_end(sql, field, key_kod);
      end;
      if (event == DLG_MOUSE)
         if   (key_kod == 0)  
              return on_scr_mouse_left(sql, field, key_kod);
         elif (key_kod == 4)
              return on_scr_mouse_right(sql, field, key_kod);
         end;
      end;
      if (event == DLG_KEY)
         if (key_kod==316)
            return on_scr_key_f2(sql, field, key_kod);
         elif (key_kod==317)
            return on_scr_key_f3(sql, field, key_kod);
         elif (key_kod==319)
            return on_scr_key_f5(sql, field, key_kod);
         elif (key_kod==320)
            return on_scr_key_f6(sql, field, key_kod);
         elif  (key_kod==321)
            return on_scr_key_f7(sql, field, key_kod);
         elif  (key_kod==322)
            return on_scr_key_f8(sql, field, key_kod);
         elif  (key_kod==323)
            return on_scr_key_f9(sql, field, key_kod);
         elif  (key_kod==365)
            return on_scr_key_altf6(sql, field, key_kod);
         elif  (key_kod==366)
            return on_scr_key_altf7(sql, field, key_kod);
         elif  (key_kod==367)
            return on_scr_key_altf8(sql, field, key_kod);
         elif  (key_kod==368)
            return on_scr_key_altf9(sql, field, key_kod);
         elif  (key_kod==357)
            return on_scr_key_ctrlf8(sql, field, key_kod);
         elif  (key_kod==32)
            return on_scr_key_32(sql, field, key_kod);
         elif  (key_kod==13)
            return on_scr_key_13(sql, field, key_kod);
         elif  (key_kod==27)
            return on_scr_key_Esc(sql, field, key_kod);
         elif  (key_kod == 43)  //PLUS серый
            return on_scr_key_PLUS(sql, field, key_kod);
         elif  (key_kod == 45)  //MINUS серый
            return on_scr_key_MINUS(sql, field, key_kod);
         elif  (key_kod == 397)
            return on_scr_key_CTRL_UP(sql, field, key_kod);
         elif  (key_kod == 401)
            return on_scr_key_CTRL_DN(sql, field, key_kod);
         elif  (key_kod == 0)
            return on_scr_key_empty(sql, field, key_kod);
         end;
      end; 
   
//   msgbox(event
//          ,"|DLG_KEY - ",dlg_Key
//          ,"|DLG_MOUSE - ",dlg_Mouse
//          ,"|DLG_DESTROY - ",dlg_destroy
//          ,"|DLG_save - ",dlg_save
//          ,"|DLG_outloop - ",dlg_outloop
//          ,"|DLG_inloop - ",dlg_inloop
//          ,"|DLG_preinit - ",dlg_preinit
//          ,"|DLG_init - ",dlg_init
//          ,"|DLG_setfocus - ",dlg_setfocus
//          ,"|DLG_remfocus - ",dlg_remfocus
//          ,"|DLG_inrec - ",dlg_inrec
//          ,"|DLG_outrec - ",dlg_outrec
//          ,"|Key_Kod - ",key_kod
//         );   
         
   end;

   macro Run (rs, X, Y, CX, CY)
      return RunScroll (rs, numCol, colInfo, scr_name, R2M(this,"EvProc"), title, st_line, scr_ronly, X, Y, CX, CY);
   end;
onerror(err);
   msgbox(CommonSessUtil.except_msg(err,null));
   return false;
end;
// кнопка
class (TRSBPushButton) TPanel_PushButton(panel, b_name, b_title, ev_hnd, x_l, y_h, length, high)
   var panel_ = panel, v_x_pos = x_l, v_y_pos = y_h, v_x_siz = length, v_y_siz = high;

   InitTRSBPushButton(b_title);
   panel.AddControl(this);
   SetPosition(x_l, y_h);
   if ((length == null) or (length == nullval) or (length == 0))
      length = strlen(b_title);
   end;
   if ((high == null) or (high == nullval) or (high == 0))
      high = 2;
   end;
   SetSize(length, high);
   Name = b_name;
   onClicked(R2M(panel, ev_hnd));

   macro get_y_bot
      return v_y_pos + v_y_siz;
   end;

   macro get_x_rgt
      return v_x_pos + v_x_siz;
   end;

end;
// линия кнопок
class  TPanel_PushButton_L(panel, btn_cnt, length, high)
   private var fl_w = true, ind = 0, prnt = panel, x_l = 0, y_h = 0, btn_stp = 0, btn_sid = 0, btn_len = length, btn_hgh = high;
   private var btns, evhs;
   btns = TArray;
   evhs = TArray;
   if ((btn_len == null) or (btn_len == nullval) or (btn_len == 0))
      btn_len = 16;
   end;
   prnt.getSize(x_l, y_h);
   // увеличить ширину панели, если кнопки не помещаются
   if (1 + btn_cnt * btn_len + btn_cnt > x_l)
      x_l = 3 + btn_cnt * btn_len + btn_cnt;
      prnt.SetSize(x_l, y_h);
   end;
   // расстояние между кнопками
   btn_stp = (x_l - (btn_cnt * btn_len))/double(btn_cnt + 1);
   if (btn_stp > int(btn_stp))
      btn_sid = int(btn_stp) + 1;
      btn_stp = int(btn_stp);
   else
      btn_sid = int(btn_stp);
      btn_stp = int(btn_stp);
   end;
   while (fl_w)
      if (x_l - btn_sid * 2 - btn_stp * (btn_cnt - 1) - btn_len * btn_cnt < 2)
         fl_w = false;
      elif (x_l - (btn_sid + 1) * 2 - (btn_stp + 1) * (btn_cnt - 1) - btn_len * btn_cnt >= 0)
         btn_stp = btn_stp + 1;
         btn_sid = btn_sid + 1;
      elif (x_l - btn_sid * 2 - (btn_stp + 1)  * (btn_cnt - 1) - btn_len * btn_cnt >= 0)
         btn_stp = btn_stp + 1;
      elif (x_l - (btn_sid + 1) * 2 - btn_stp  * (btn_cnt - 1) - btn_len * btn_cnt >= 0)
         btn_sid = btn_sid + 1;
      end;
   end;

   macro Add_Button(title, ev_hnd)
      var i = 0;
      i = btns.Size;
      btns(i) = TPanel_PushButton(prnt, string("BTN_", i + 1), title, ev_hnd, btn_sid + (btn_len + btn_stp) * i, y_h - 2, btn_len, btn_hgh);
   end;
end;
// radioButton
class (TRsbRadioButton) TPanel_RadioButton(panel, name, title, group, ev_hnd, x_l, y_t)
   private var pnl = panel, x_lft = x_l, y_top = y_t, grp = group;
   InitTRsbRadioButton(grp);   
   pnl.AddControl(this);
   SetPosition(x_l, y_t);
   if ((not CommonSessUtil.is_null_Prm(title)) and (strlen(trim(title)) > 0))
      panel.addLabel(TRsbLabel(x_l + 3, y_t, title));
   end;
   this.Name = Name;
   onChecked(R2M(pnl, ev_hnd));

   macro get_y_bot
   return y_top + 1;
   end;

end;
// RadioButtonGroup
class TPanel_RadioButtonGroup(panel, ev_hnd, x_l, y_t, group)
   private var pnl = panel, x_lft = x_l, y_top = y_t, y_bot = y_t, ev_hnd_ = ev_hnd, grp = group;
   var btns = TArray, ;
   if (CommonSessUtil.is_null_prm(grp))
      grp = 0;
   end;

   macro set(p_nam)
      var i = 0;
      while (i < btns.Size)
         if ((not CommonSessUtil.is_null_prm(p_nam)) and (p_nam == btns[i].name))
            btns[i].Checked = true;
         else   
            btns[i].Checked = false;
         end; 
         i = i + 1;      
      end;
   end;

   macro add(name, title, checked)
      btns[btns.Size] = TPanel_RadioButton(pnl, name, title, grp, ev_hnd_, x_lft, y_bot);
      if (checked)
         set(name);
         btns[btns.Size - 1].checked = true;
      end;           
      y_bot = y_bot + 1;
   end;

   macro get_y_bot
      return y_bot;
   end;  
end;
// CheckBox
class(TRsbCheckBox) TPanel_CheckBox(panel, name, ev_hnd, x_l, y_t, title, chkd, align)
   private var pnl = panel, x_lft = x_l, y_top = y_t, algn = align;
   InitTRsbCheckBox();
   pnl.AddControl(this);
   if ((not CommonSessUtil.is_null_Prm(title)) and (strlen(trim(title)) > 0))
      if (strupr(substr(algn, 1, 1)) == "R")
         SetPosition(x_lft + strlen(title) + 3, y_top);
         pnl.addLabel(TRsbLabel(x_lft, y_t, title));
      else
         SetPosition(x_lft, y_t);
         pnl.addLabel(TRsbLabel(x_lft + 3, y_top, title));
      end;  
   else
      SetPosition(x_l, y_t);
   end;
   this.Name = Name;
   onChecked(R2M(pnl, ev_hnd));
   this.Checked = false;
   if (chkd)
      this.Checked = true;
   end;

   macro get_y_bot
      return y_top + 1;
   end;
end;
// поле для редактирования
class (TRSBEditField) TPnl_Edit_Box(panel, p_dat_tp, p_name, p_x_pos, p_y_pos, p_x_siz, p_y_siz, title)
   var panel_ = panel, v_x_pos = p_x_pos, v_y_pos = p_y_pos  , v_x_siz = p_x_siz, v_y_siz = p_y_siz, v_dat_tp = p_dat_tp;
   var title_ = title;
   var lb_tit, old_val;
   
   //
   macro norm_str_val(val)
      if ((val == null) or (val == nullval))
         return ZeroValue(v_Dat_tp);
      end;         
      if (v_dat_tp == v_money)
         return round(double(trim(val)), 2);
      elif (v_dat_tp == v_double)
         return double(trim(val));
      elif (v_dat_tp == v_date)
         if (trim(val) == "01.01.0001")    
            return date(0,0,0);
         end;
         return date(trim(val));
      end; 
      return trim(val);
   end; 
   //
   macro set_value(val)
      var fl = false;
      Value = val;  
      if (old_val != this.Value)
         fl = true;  
      end;   
      old_val = Value;
      if (fl)
         this.PostEvent(TRsbEvent(RSB_EV_CHANGE_VALUE, this));
      end;   
   end;                    

   macro on_RemFocus(event)
      var src = Event.Source;
      if (old_val != this.Value)     
         this.PostEvent(TRsbEvent(RSB_EV_CHANGE_VALUE, this));
      end;
      old_val = this.Value;
   end;
   
   // получить тип данных для ВФорм
   private macro get_data_typ(fl_val)
      if (fl_val == v_string)
         return 7;
      elif (fl_val == v_integer)
         return 1;
      elif (fl_val == v_double)
         return 4;
      elif (fl_val == v_money)
         return 4;
      elif (fl_val == v_date)
         return 9;
      elif (fl_val == v_time)
         return 10;
      else
         return 7;
      end;
   end;
   // сделать нередактируемым
   macro set_edit(flg)
      Enabled  = true;
      editable = flg;
      focusable= flg;
   end;
   // получить координату высоты объекта
   macro get_y_top
      return v_y_pos;
   end;

   macro get_y_bot
      return v_y_pos + v_y_siz;
   end;

   macro get_x_lft
      return v_x_pos;
   end;

   macro get_x_rgt
      if (strlen(title_) > 0)
         return v_x_pos + v_x_siz + strlen(title_);
      else
         return v_x_pos + v_x_siz;
      end;
   end;
   
   InitTRSBEditField(get_data_typ(v_dat_tp));
   panel_.AddControl(this);
   Name  = p_name;
   set_edit(true);
   if (v_dat_tp == v_string)
      TextLength = 1000;
   end;
   if (v_dat_tp == v_money)
      ValuePrecision = 2;
   end;
   if (v_dat_tp == v_double)
      ValuePrecision = 4;
   end;
   if (strlen(title_) > 0)
      lb_tit = TRSBLabel();
      panel_.AddLabel(lb_tit);
      lb_tit.Text = title_;
      lb_tit.SetPosition(v_x_pos,  v_y_pos);
      SetPosition( v_x_pos + strlen(title_), v_y_pos);
   else
      SetPosition( v_x_pos , v_y_pos);
   end;          
   SetSize(v_x_siz, v_y_siz);     
   AddEventHandler(RSB_EV_REMOVE_FOCUS, R2M(this, "on_RemFocus"));   
end;
// поле со списком  в датасете 1-е осн.величина 2-е - ID величины, запрос в последнем параметре
class (TPnl_Edit_Box) TPnl_List_Box (panel, p_name, p_x_pos, p_y_pos, p_x_siz, p_y_siz, title, p_sql_list)
   var cbx_rsdc, cbx_rsds, cbx_ival, sval = TArray;
   var p_sql_list_ = p_sql_list, id_val = 0;
   // добавление параметров
   macro param_add(p_val)
      sval[sval.Size] = p_val;
   end;
   // очистка параметров
   macro param_clr(p_val)
      sval = TArray;
   end;
   //получить датасет
   private macro get_dataset()
      var i = 0;
      if (CommonSessUtil.is_null_prm(p_sql_list_) or (strlen(trim(p_sql_list_)) == 0))
         return false;
      end;                      
      this.PostEvent(TRsbEvent(RSB_EV_LIST_GETDSET, this));
      cbx_rsdc = rsdcommand(p_sql_list_);
      while (i < sval.Size)
         cbx_rsdc.AddParam("", RSDBP_IN, sval[i]);
         i = i + 1;
      end;
      cbx_rsds = rsdrecordset(cbx_rsdc, RSDVAL_CLIENT, RSDVAL_STATIC);
      cbx_rsds.Open;
      cbx_rsds.MoveNext();              
      return true;
   onerror(err);
      msgbox(CommonSessUtil.except_msg(err, cbx_rsdc));
      cbx_rsds = null;
      cbx_rsdc = null;
      return false;
   end;
   // установка значений поля
   macro set_value(val, id)
      id_val = id;    
      set_value(val);       
   end;

   macro set_sel_value(rset)
      set_value(rset.Value(0), rset.Value(1));
   end;
   // установка начального
   macro set_init
      if (get_dataset())                                 
         if ((cbx_ival == null) or (cbx_ival == nullval))
            cbx_rsds.MoveFirst();
            set_sel_value(cbx_rsds);
         else
            cbx_rsds.MoveFirst();
            while (cbx_rsds.Value(1) != cbx_ival)
                  if (not cbx_rsds.MoveNext())
                     cbx_rsds.MoveFirst();
                     break;
                  end;
            end;    
            set_sel_value(cbx_rsds);
         end;
      else
         value = "";
         id_val = -1;
      end;  
   end;
   //обработчик сроллинга
   macro ScrlEvProc (sql, event, field, key_kod)
      var ind = 0, payment;
      if (event == DLG_INIT)
         if (not ((cbx_ival == null) or (cbx_ival == nullval)))
            sql.Movefirst();
            while (sql.Value(1) != cbx_ival)
                  if (not sql.MoveNext())
                     sql.MoveFirst();
                     break;
                  end;
            end;
            gotoscroll(sql);
         end;
      end;
      if (event == DLG_KEY)
         if (key_kod==13)
            gotoscroll(sql);
            if ((sql.value(0) != null) and (sql.value(0) != nullval))
               set_sel_value(sql);
               cbx_ival = sql.Value(1);
               return cm_select;
            end;
            return cm_ignore;
         end;
      end;
   end;
   //формирование списка
   private macro ScrlRun(p_sql)
      var col_i:Tarray, x, y, x_t, y_t, ind = 0, ia = 0, name_scr = "";
      panel_.getPosition(x, y);
      this.getposition(x_t, y_t);
      if (not get_dataset())
         msgbox("Ошибка реализации источника данных");
         return false;
      end;
      cbx_rsds.Open();
      col_i = Tarray();
      while (ind < cbx_rsds.FldCount)
         if (index(cbx_rsds.Fld(ind).Name, "V") > 0)
            col_i[0 + ia * 6] = cbx_rsds.Fld(ind).Name;
            col_i[1 + ia * 6] = substr(utf8tooem(cbx_rsds.Fld(ind).Name, true), 5);
            col_i[2 + ia * 6] = 50;
            col_i[3 + ia * 6] = true;
            col_i[4 + ia * 6] = 0;
            col_i[5 + ia * 6] = 0;
            ia = ia + 1;
         end;
         ind = ind + 1;
      end;
      name_scr = name + "_SCROLL";
      return RunScroll (cbx_rsds, col_i.Size/6, col_i, name_scr, R2M(this, "ScrlEvProc"), title_, "Enter-Выбор Esc-Выход", true, x + x_t, y + y_t + 1, null, 15);
   onerror(err);
      msgbox(CommonSessUtil.except_msg(err, cbx_rsdc));
      cbx_rsds = null;
      cbx_rsdc = null;
   end;
   // вызов списка
   macro on_key_pressed(event)
      var lst;
      if (event.keycode == 317)
         ScrlRun(p_sql_list_);
      end;
   end;  
   // введенное руками: пустое - обнуляет поле, непустое - меняется на прежнее
   macro on_RemFocus(event)
      var src = Event.Source;
      if (old_val != this.Value)   
         if (strlen(trim(this.Value)) == 0)
            set_value("", 0);
         else
            this.Value = old_val;
         end;
      end;
   end;
   // инициализация объекта
   InitTPnl_Edit_Box(panel, v_string, p_name, p_x_pos, p_y_pos, p_x_siz, p_y_siz, title, p_name);
   clear();
   onKeyPressed(R2M(this, "on_key_pressed"));
end;
// поле со списком для мн.выбора - в датасете 1-е поле маркер, 2-е осн.величина  запрос должне позволять Update
class (TPnl_List_Box) TPnl_Listm_Box (panel, p_name, p_x_pos, p_y_pos, p_x_siz, p_y_siz, title, p_sql_list)
   var v_sel = "X", v_all = 0, v_mrk = 0, old_vls;
   //отображение полей
   macro set_sel_value(rset)
      var fl = true, v_str = "";                 
      rset.MoveFirst();
      while (fl)
         v_All = v_All + 1;          
         if (rset.Value(0) == v_Sel)
            v_mrk = v_mrk + 1;
            v_str = v_str + rset.Value(1);
         end;
         fl = rset.MoveNext();
      end;
      fl = false;       
      if (v_Str != old_val)
         fl = true; 
         old_val = v_str;
      end;
      if ((v_all == v_mrk) or (v_mrk == 0))
         Value = "Все";
      elif (v_mrk == 1)  
         Value = v_str;   
      else 
         if (strlen(v_str) > v_x_siz)
            v_str = "Ряд";
         end;       
         Value = v_str;   
      end;        
      old_vls = v_str;
      if (fl)
         this.PostEvent(TRsbEvent(RSB_EV_CHANGE_VALUE, this));
      end;
   onerror(err);
      msgbox(CommonSessUtil.except_msg(err, cbx_rsdc));
   end;
   //отображение стартового значения
   macro set_init()      
      set_sel_value(cbx_rsds);
   end;    
   //очистить поле
   macro clr_fld() 
      var fl = true;                 
      cbx_rsds.MoveFirst();
      while (fl)             
         if (cbx_rsds.Value(0) == v_Sel)
            cbx_rsds.Edit;
            cbx_rsds.Value(0) = " ";
            cbx_rsds.Update();
         end;
         fl = cbx_rsds.MoveNext();
      end;
      set_sel_value(cbx_rsds);   
   end;  
   // вызов списка
   macro on_key_pressed(event)
      var lst;
      if (event.keycode == 317)
         ScrlRun;
      end;
      if (event.keycode == 322)
         clr_fld;
      end;
   end;
      //обработчик сроллинга
   macro ScrlEvProc (sql, event, field, key_kod)
      var ind = 0, payment;   
      if (((event == DLG_KEY) and ((key_kod == 13) or (key_kod==32)))
          or                                                            
          ((event == DLG_MOUSE) and (key_kod == 4)))
         gotoscroll(sql);
         if ((sql.value(1) != null) and (sql.value(1) != nullval))
            sql.Edit;
            if (sql.Value(0) == v_Sel)
               sql.Value(0) = " ";
            else
               sql.Value(0) = v_Sel;
            end;                
            sql.Update();
            updatescroll(sql, 5);
            set_sel_value(sql);
         end;
         return cm_ignore;
      end;
   end;            
   //формирование списка
   private macro ScrlRun
      var col_i:Tarray, x, y, x_t, y_t, ind = 0, ia = 0;
      panel_.getPosition(x, y);
      this.getposition(x_t, y_t);
      if (not get_dataset())
         msgbox("Нет реализации источника данных");
         return false;
      end;
      cbx_rsds.Open();
      col_i = Tarray();
      while (ia < 2)
            col_i[0 + ia * 6] = cbx_rsds.Fld(ia).Name;
            if (ia == 0)
               col_i[1 + ia * 6] = "X";
               col_i[2 + ia * 6] = 1;
            else
               col_i[1 + ia * 6] = "Значение";
               col_i[2 + ia * 6] = 100;
            end;   
            col_i[3 + ia * 6] = 5;
            col_i[4 + ia * 6] = null;
            col_i[5 + ia * 6] = null;
            ia = ia + 1;
      end;
      while (RunScroll (cbx_rsds, col_i.Size/6, col_i, "CBX_SCROLL", R2M(this, "ScrlEvProc"), title_, "Enter-Выбор Esc-Выход", true, x + x_t, y + y_t + 1, null, null))
      end;
   onerror(err);
      msgbox(CommonSessUtil.except_msg(err, cbx_rsdc));
      cbx_rsds = null;
      cbx_rsdc = null;
   end;
   //
   macro on_RemFocus(event)
      var src = Event.Source;
      if (old_val != this.Value)
         if (strlen(trim(this.Value)) == 0)
            clr_fld;
         else
            this.Value = old_vls;
         end;  
      end;
   end;
   // инициализация объекта
   InitTPnl_List_Box (panel, p_name, p_x_pos, p_y_pos, p_x_siz, p_y_siz, title, p_sql_list);
   param_add(p_Name);
   clear();   
end;
// примитив панели
/*
class (TRsbPanel) TPnl_Window();
   var x_lft = 35, y_hgh = 10, x_siz = 32, y_siz = 10, frm, y_beg = 1;
   var btn_l;
   private var lbl;

   // закрытие окна
   macro on_panel_close(event)
      var ind = 0;
      if (event.StatusCode == 1)
      end;                
   end;   
   // открытие окна
   macro on_panel_open(event)
   end;
   // Реакция на клавиши
   macro on_key_pressed(event)
      var e_s, s_n, k_k, ind = 0;
      k_k = event.keycode;
      e_s = event.Source;             
      if (IsEqClass ("TRsbPanel", e_s))
         return;
      else   
         s_n = e_s.Name; 
      end; 
      if (k_k == 316)  
         close(0);
      end;
      if (k_k == 323) 
         close(0);
      end;
   end;
   // Реакция на кнопки
   macro on_click_btn(event)
      close(0);
   end;
   macro on_remfocus(event)
   var src = event.Source;
   end;
   macro on_change_value(event)  
   var src = event.Source;
   end; 
   // построение панели
   macro build_panel()  
      setPosition(x_lft, y_hgh);
      setSize(x_siz, y_siz);
      setStatus("F3-Список  F2-Выполнить F9-Сохранить и Выполнить Alt-F3-Готовые фильтры  F8-Очистить выделение  Esc-Выход");
      setCaption("Параметры отчета");         
      btn_l = TPanel_PushButton_L(this, 2);
      btn_l.Add_Button("F2-Выполнить", "on_click_btn");
      btn_l.Add_Button("Esc-Выход"   , "on_click_btn");
      addEventHandler(RSB_EV_KEY_PRESSED,    R2M(this, "on_key_pressed"));
      addEventHandler(RSB_EV_WINDOW_CLOSING, R2M(this, "on_panel_close"));
      addEventHandler(RSB_EV_ENTER_WINDOW,   R2M(this, "on_panel_open"));
      addEventHandler(RSB_EV_REMOVE_FOCUS,   R2M(this, "on_remfocus"));
      addEventHandler(RSB_EV_CHANGE_VALUE,   R2M(this, "on_change_value"));
      addEventHandler(RSB_EV_LIST_GETDSET,   R2M(this, "on_list_getdset"));
   end;
                                             
   macro show()  
      return run;
   onerror(err)
      if (err.Code != 0)
         msgbox(CommonSessUtil.except_msg(err, null));
      end;
      return 0;     
   end;
   
InitTRsbPanel();
build_panel();
end;
*/
