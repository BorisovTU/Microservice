import "cdrecordsrun.mac";

private var DealList = TArray();
private var DealOKList = TArray();
private var DealErrList = TArray();
private var ErrorList = TArray();

private macro importID (LoadFile)
  var CsvFile = TStreamDoc(LoadFile, "r");
  var ID = 0;
  DealList.size = 0;
  CsvFile.readLine (@ID);
    
  while(CsvFile.readLine (@ID))

    if((ID == "") or (ID == NULL) or (ID == "Undefined"))
      break;
    end;

    ID = int(StrSubSt(ID," ",""));

    DealList[DealList.size] = ID;
  end;
END;

private macro insertIDInTmp(arr)
  var queryInsert = "INSERT INTO DTOCALCREDM_TMP ( " +
                      "                                t_ID " +
                      "                             ) " +
                      "                      VALUES ( " +
                      "                                ? " +
                      "                             ) ";

  var i = 0;
  while( i < arr.size)
    var cmdInsert = DL_RSDCommand(queryInsert);
    cmdInsert.AddParam(arr[i]);
    cmdInsert.ExecuteCMD();
    i = i + 1;
  end;
END;

private macro updateState()
  var queryUpdate = "UPDATE dcdrecords_dbt SET t_Status = " + CDRECORDS_STATUS_NEW +
                    " WHERE dcdrecords_dbt.t_id in (select t_id from dtocalcredm_tmp)";

  var cmdUpdate = DL_RSDCommand(queryUpdate);
      cmdUpdate.ExecuteCMD();

END;

private macro runCalc()
  var file_name = "";
  var query, cmd, DataSet;
  DealOKList.size = 0;
  DealErrList.size = 0;
  ErrorList.size = 0;
  SQL_Execute("DELETE FROM dtocalcredm_tmp");
  
  if(SelectFile(file_name, "*.csv*")) 
    importID(file_name);
  else
    ErrorList[ErrorList.size] = "Ошибка выбора файла";
  end;
  
  if(DealList.size == 0)
    ErrorList[ErrorList.size] = "Отсутствуют идентификаторы для обработки";
	return;
  end;

  insertIDInTmp (DealList);
  updateState();
  
  query =   " select TO_CHAR(t.t_ID) as ID_str  "
          + "   from dcdrecords_dbt t, dtocalcredm_tmp tmp "
          + "  where t.t_id = tmp.t_id "
          + "  order by t.t_RequestDate DESC, t.t_RequestTime DESC, t.t_ID DESC ";

  cmd = DL_RSDCommand(query);

  InitProgress(cmd.GetCount(), "Ждите, выполняется обработка ..", "Выполняется обработка");
  
  DataSet = cmd.Execute();
  var curr_row = 1;
  while(DataSet.moveNext())
   var idNum = bigint(DataSet.ID_str);
    if(ExecCDRecords(idNum, false))
      DealErrList[DealErrList.size] = idNum;
    else
      DealOKList[DealOKList.size] = idNum;
    end;
      UseProgress(curr_row = curr_row + 1);
  end;
  RemProgress();
END;

private macro makeProtocol()
  var query, cmd, DataSet, i = 0;

  println ("Всего записей в файле - " + DealList.size +" ");
  println ("Всего обработано записей - " + DealList.size +" ");
  println ("из них:");
  println ("статус Обработано - " + DealOKList.size +" ");
  println ("статус Ошибка обработки - " + DealErrList.size +" ");

  query =   " select CAST(t.t_id as varchar2(20) ) as t_id, " 
          + "        CAST(t.t_RECORDPAYMENTID as varchar2(20) ) as t_RECORDPAYMENTID, t.t_error "
          + "   from dcdrecords_dbt t, dtocalcredm_tmp tmp "
          + "  where t.t_id = tmp.t_id and "
          + "  t.t_status = " + CDRECORDS_STATUS_PROCESSING_ERROR 
          + "  order by t.t_RequestDate DESC, t.t_RequestTime DESC, t.t_ID DESC ";

  cmd = DL_RSDCommand(query);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    println ("Ошибка обработки:");
    println (DataSet.ID + ";" + DataSet.RECORDPAYMENTID + ";" + DataSet.Error);
  end;
  
  while(i < ErrorList.size)
    println (ErrorList[i]);
    i = i + 1;
  end;
END;

runCalc();
makeProtocol();