//Скроллинг сообщений СПБ

import globals, oralib, likepy, rsexts;

import RsbFormsInter;
import "usr_key_const.mac";
import "int_usr_globals.mac";
IMPORT "scsrvrepfun.mac";

import "dlcontrmsg.mac";
import "usr_msg_lib.mac";
import "ProcMsgSPBByList.mac";

CONST SPB_CODE = "ПАО СПБ";    /* код СПБ */

private var gv_column_list_scr_main = TArray(); /* Список колонок основного скроллинга */
private var gv_num_columns_scr_main = 0;        /* Количество колонок основного скроллинга */
private var gv_scr_focus_main = 0;
private var gv_sec_group_id = 0;
private var gv_num_columns_scr_sec = 0;
private var gv_scr_focus_sec = 0;

private var gv_column_list_scr_sec = TArray(); 
var inprocess, inprocessprogress = 0;


/*----------------------------------------------*/
/* Вспомогательные функции                      */
/*----------------------------------------------*/

PRIVATE MACRO processPath( p_path )
   var l_str, l_p, l_path = "";
   p_path = trim( p_path );

   if( substr( p_path, 1, 2 ) == ".." ) /*Ссылка на вышестоящий каталог */
      p_path = substr( p_path, 3 );
      l_str  = getCWD(false);    
      l_p    = strbrk( l_str, "\\" );
      while( l_p != 0 )
         l_path = l_path + substr( l_str, 1, l_p - 1 ) + "\\";
         l_str  = substr( l_str, l_p + 1 );
         l_p    = strbrk( l_str, "\\" );
      end;

      return ( substr( l_path, 1, strlen( l_path ) - 1 ) + p_path );

   elif( substr( p_path, 1, 1 ) == "." ) /* Ссылка на текущий каталог */
      return ( getCWD + substr( p_path, 2 ) );
   end;

   return p_path;
END;

PRIVATE MACRO GetTxtPath()
   var RegistryPath = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR";
   var Path = "";
   var stat = 0;
   
   GetRegistryValue(RegistryPath, V_STRING, Path, stat);
   if(stat) 
      msgbox("Не задана настрйка в реестре банка:|", RegistryPath);      
   end;

   return Path;
END;

MACRO GetRepFullPath(isRemote:bool)
   var Path = "";

   if(isRemote)
      var txtPath = GetTxtPath();

      if( substr( txtPath, 1, 2 ) == ".." ) /*ссылка на вышестоящий каталог */
         txtPath = substr(txtPath, 3 );
      end;

      if( substr( txtPath, 1, 1 ) == "\\" ) /*ссылка на вышестоящий каталог*/
         txtPath = substr(txtPath, 2 );
      end;

      Path = GetCurDir(true) + "\\" + txtPath + "\\"; 
   else
      Path = processPath(GetTxtPath()) + "\\";
   end;

   // получить полный путь
   return Path;
END;

macro GetFatcaMsg(msgid: integer, pfilename:@string, pmsg_text:@string)
   var sqls;
       sqls = String( "SELECT   \n"
                       ,"imdata.t_filename,to_char(im.t_fmtblobdata_xxxx) as t_msg   \n"
                       ," FROM dimgdata_dbt imdata,dimage_dbt im   \n"
                       ,"WHERE t_ObjectID = lpad(:pmsgid, 34, '0')   \n"
                       ,"AND t_ObjectType = 208   \n"
                       ,"AND t_ImageType = 1   \n"
                       ,"and imdata.t_imageid = im.t_imageid   \n"
                          ,"ORDER BY t_IsMain DESC;   \n");
      var cmd = RSDCommand(sqls);
          cmd.addparam("pmsgid", rsdbp_in, msgid);
      var rs = rsdrecordset(cmd, rsdval_static, rsdval_client);
      if (rs.movenext)
        pfilename = rs.value("t_filename");
        pmsg_text = rs.value("t_msg");
        return 1;
      end;
      return 0;
end;

private macro ShowOutgoingMSG(pmsgid, pmsgkind)
   var CreateOnTerm = (not isStandAlone());
   var FullPath = GetRepFullPath(CreateOnTerm);
   var xmlNameFile, ShowFilePath, strm;
   var msgfileName, msg_text;
   if(pmsgkind ==10)
      if(GetFatcaMsg(pmsgid,@msgfileName, @msg_text))
         xmlNameFile = FullPath + msgfileName;
         ShowFilePath = IIF(CreateOnTerm, "$", "") + FullPath;
         strm = TStream(xmlNameFile, "C");
         strm.write(OemToUtf8(MSG_text,true));
         strm.Flush();
         strm = null;
         SC_ShowFile(ShowFilePath, msgfileName);
      else
         msgbox ("Прикрепленный объект не найден");   
      end;      
   end;
end;

macro usr_DlPrintSendMesResp(dlContrMsgID)
   var dlContrMsg = TRecHandler("dlcontrmsg.dbt");
   
   var queryF = " SELECT DLCONTRMSG.* FROM DDLCONTRMSG_DBT DLCONTRMSG WHERE DLCONTRMSG.T_ID = " + DlContrMsgID;
   var cmdF = DL_RSDCommand(queryF);
   var dsF = cmdF.Execute();
   while(dsF.moveNext())
      dlContrMsg.Clear();
      dsF.GetRecord().CopyTo( dlcontrMsg.rec);
   end;
   var respData = DL_GetClobData("t_RespData", " from DDLCONTRMSG_DBT where t_ID = "+Int(dlContrMsg.rec.ID));
   PrintSendMesResp(dlContrMsg.rec.Id, respData);
end;


/*----------------------------------------------*/
/* Метод обработки событий скроллинга сообщений */
/*----------------------------------------------*/
macro m_sec_list_scr(p_rs, p_cmd, p_id, p_key)
   var CM_FLAG = CM_DEFAULT;
   //

   if(p_cmd == DLG_INIT)
      while((gv_scr_focus_sec != 0) and p_rs.movenext())
         if(p_rs.value("t_dlcontrid") == gv_scr_focus_sec)
            gv_scr_focus_sec = 0;
            GoToScroll(p_rs);
         end;
      end;
      AddMultiAction(p_rs, K_F2);
   elif(p_cmd == DLG_MSELSTART)
      
      if(p_key == K_F2)
             if(msgboxex("Выполнить повторную отправку для выбранных сообщений? ", MB_YES + MB_NO, IND_YES) == IND_YES)
               inprocess = true;
               inprocessprogress = 0;
               InitProgress(int(GetMultiCount()), "Ждите...", "Обработка сообщений");
             end;
      end;

   elif(p_cmd == DLG_MSELEND)
      
      if(p_key == K_F2)
         if(inprocess)
               inprocess = false;
               RemProgress();
               msgboxex("Обновите список по Ctrl+R");

               CM_FLAG = CM_IGNORE;
         end;
      end;
   elif(p_cmd == DLG_MSEL)
      
      if(p_key == K_F2)  
            //msgbox(p_rs.value("t_dlcontrid"));
         if (not CheckExistMSG(p_rs.value("t_dlcontrid"), 10,310, 151337))
            usr_ReCreateMSG(p_rs.value("t_dlcontrid"), 151337, p_rs.value("t_kind")); 
         end;

            usr_ReCreateMSG(p_rs.value("t_dlcontrid"), 151337, p_rs.value("t_kind"));
            UseProgress(inprocessprogress = inprocessprogress + 1);
            updatescroll(p_rs, 5);
            return CM_MSEL_CONT_CLEAR;
      end;


   elif(p_cmd == DLG_KEY)
      if(p_key == K_ENTER)
         ShowOutgoingMSG(p_rs.value("msg_id"), 10);
         CM_FLAG = CM_IGNORE;
      elif(p_key == 422 )
         usr_DlPrintSendMesResp(p_rs.value("msg_id"));
         CM_FLAG = CM_IGNORE;

      elif(p_key == K_F2)
         gv_scr_focus_sec = p_rs.value("t_dlcontrid");
         //msgbox(p_rs.value("t_dlcontrid"));
         if (CheckExistMSG(p_rs.value("t_dlcontrid"), 10,310, 151337))
            CM_FLAG = CM_IGNORE;
            if (gettrue(false, "Сообщение подтверждено биржей.\nВы уверены, что его необходимо переформировать?"))
               usr_ReCreateMSG(p_rs.value("t_dlcontrid"), 151337, p_rs.value("t_kind")); 
               CM_FLAG = CM_SELECT;
            end;

         else
             usr_ReCreateMSG(p_rs.value("t_dlcontrid"), 151337, p_rs.value("t_kind")); 
             CM_FLAG = CM_SELECT;
         end;

                   

      //elif(p_key == K_F2)   

      elif(p_key == 18/*Ctrl+R*/)
         return CM_SELECT;
   

      elif(p_key == K_ESCAPE)
         if(not gettrue(true, "Выйти из списка сообщений?"))
            CM_FLAG = CM_IGNORE;
         end;
      elif(p_key == 361) /*alt + F2*/
         //msgbox(p_key);
         if(ProcMsgSPBByList())
           msgboxex("Обработка завершена. Обновите список по Ctrl+R");
         end;  
         CM_FLAG = CM_IGNORE;
      end;

   end;

   return CM_FLAG;
end; /* macro m_sec_list_scr() */



/*-------------------------------------------------------------*/
/* Метод, запускающий скроллинг списка сообщений               */
/*-------------------------------------------------------------*/
private macro m_run_sec_scroll(p_msgkind)
   private var sql_scr, cmd_scr, rs_scr;
   private var sql_main = "";
   private var sql_filtr = "";
   private var sql_order = "";

   gv_sec_group_id = p_msgkind;

   gv_column_list_scr_sec.size = 0;
   gv_num_columns_scr_sec = 0;
   gv_scr_focus_sec = 0;

   sql_main = String( "select dlc.t_dlcontrid, sfc.t_id, sfc.t_number,   \n"
                 ,"sfc.t_datebegin,   \n"
                 ,"msg.t_kind, msg.t_id as msg_id, msg.t_createdate, msg.t_senddate, msg.t_sendmesstate, msg.t_respdate   \n"
                 ,",(select na.T_SZNAMEALG from dnamealg_dbt na where na.t_itypealg = 7535 and na.t_inumberalg = msg.t_sendmesstate) as t_state_name"
                 ,"  from ddlcontrmsg_dbt msg, ddlcontr_dbt dlc, dsfcontr_dbt sfc   \n"
                 ," where dlc.t_dlcontrid = msg.t_dlcontrid   \n"
                 ,"   and sfc.t_id = dlc.t_sfcontrid   \n"
                 ,"   and msg.t_kind = :pmsgkind   \n");

   sql_order = sql_order + "ORDER BY dlc.t_dlcontrid ASC ";

   sql_scr = sql_main + sql_filtr + sql_order;

   cmd_scr = RSDCommand(sql_scr);
   cmd_scr.AddParam("pmsgkind", rsdbp_in, p_msgkind);
   cmd_scr.NullConversion = true;
   
   rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   rs_scr.NullConversion = true;

   m_add_column(gv_column_list_scr_sec, gv_num_columns_scr_sec, "T_NUMBER",         "Договор", 30); gv_num_columns_scr_sec = gv_num_columns_scr_sec + 1;
   m_add_column(gv_column_list_scr_sec, gv_num_columns_scr_sec, "T_DATEBEGIN",      "Дата договора", 20); gv_num_columns_scr_sec = gv_num_columns_scr_sec + 1;
   m_add_column(gv_column_list_scr_sec, gv_num_columns_scr_sec, "T_KIND",           "Тип сообщения", 20); gv_num_columns_scr_sec = gv_num_columns_scr_sec + 1;
   m_add_column(gv_column_list_scr_sec, gv_num_columns_scr_sec, "t_createdate",     "Дата создания", 20); gv_num_columns_scr_sec = gv_num_columns_scr_sec + 1;
   m_add_column(gv_column_list_scr_sec, gv_num_columns_scr_sec, "t_senddate",       "Дата отправки", 20); gv_num_columns_scr_sec = gv_num_columns_scr_sec + 1;
   m_add_column(gv_column_list_scr_sec, gv_num_columns_scr_sec, "t_sendmesstate",   "Код", 6); gv_num_columns_scr_sec = gv_num_columns_scr_sec + 1;
   m_add_column(gv_column_list_scr_sec, gv_num_columns_scr_sec, "t_state_name",     "Статус", 30); gv_num_columns_scr_sec = gv_num_columns_scr_sec + 1;
   m_add_column(gv_column_list_scr_sec, gv_num_columns_scr_sec, "t_respdate",       "Дата ответа", 20); gv_num_columns_scr_sec = gv_num_columns_scr_sec + 1;

   while(RunScroll(rs_scr, gv_num_columns_scr_sec, gv_column_list_scr_sec, "sec_list", "m_sec_list_scr", "Список сообщений", "ESC - выход  F2 - отправить повторно ENTER - просмотр исх.  ALT+Enter - просмотр ответа", true, 7, 5, NULL, 40))
      /* Обновляем скроллинг */
      cmd_scr.close(); cmd_scr = NULL;
      rs_scr.close(); rs_scr = NULL;

      sql_filtr = "";

      sql_scr = sql_main + sql_filtr + sql_order;

      cmd_scr = RSDCommand(sql_scr);
      cmd_scr.AddParam("pmsgkind", rsdbp_in, p_msgkind);
      cmd_scr.NullConversion = true;

      rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   end;

   rs_scr.close();

end; /* macro m_run_sec_scroll() */



/*----------------------------------------------*/
/* Метод обработки событий основного скроллинга */
/*----------------------------------------------*/
macro m_pr_list_scr(p_rs, p_cmd, p_id, p_key)
   var CM_FLAG = CM_DEFAULT;

   if(p_cmd == DLG_INIT)
      while((gv_scr_focus_main != 0) and p_rs.movenext())
         if(p_rs.value("t_element") == gv_scr_focus_main)
            gv_scr_focus_main = 0;
            GoToScroll(p_rs);
         end;
      end;

   elif(p_cmd == DLG_KEY)
      if(p_key == K_ENTER)

         /* Отображение списка адресов заданной группы рассылки */
         m_run_sec_scroll(p_rs.value("t_kind"));

         CM_FLAG = CM_IGNORE;

      elif(p_key == K_ESCAPE)
         if(not gettrue(true, "Завершить работу?"))
            CM_FLAG = CM_IGNORE;
         end;
      end;

   end;

   return CM_FLAG;
end; /* macro m_pr_list_scr() */



/*---------------------------------------*/
/* Метод, запускающий основной скроллинг */
/*---------------------------------------*/
private macro m_run_main_scroll()
   private var sql_scr, cmd_scr, rs_scr;
   private var sql_main = "";
   private var sql_filtr = "";
   private var sql_order = "";

   sql_main = String(   "select *   \n"
                       ,"  from (select 3 as t_num,'Cообщения FATCA(10)' as t_name, 10 as t_kind from dual   \n"
                       ,"           \n"
                       ,"        ) t   \n");
   sql_order = sql_order + " order by t_num asc   \n";


   sql_scr = sql_main + sql_filtr + sql_order;

   cmd_scr = RSDCommand(sql_scr);
   cmd_scr.NullConversion = true;
   
   rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   rs_scr.NullConversion = true;

   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_NUM",   "Номер пп", 10, null, 0); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_NAME",      "Название выбора",40); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;
   m_add_column(gv_column_list_scr_main, gv_num_columns_scr_main, "T_KIND",      "Тип",5, null, 0); gv_num_columns_scr_main = gv_num_columns_scr_main + 1;

   while(RunScroll(rs_scr, gv_num_columns_scr_main, gv_column_list_scr_main, "pr_list", "m_pr_list_scr", "Сообщения СПБ", "ESC - выход  ENTER - список сообщений", true, 5, NULL, NULL, 60))
      /* Обновляем скроллинг */
      cmd_scr.close(); cmd_scr = NULL;
      rs_scr.close(); rs_scr = NULL;

      sql_filtr = "";

      sql_scr = sql_main + sql_filtr + sql_order;

      cmd_scr = RSDCommand(sql_scr);
      cmd_scr.NullConversion = true;

      rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
   end;

   rs_scr.close();

end; /* macro m_run_main_scroll() */






m_run_main_scroll();
exit(1);
