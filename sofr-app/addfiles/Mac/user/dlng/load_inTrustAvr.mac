import bankinter, rsd, "or_exl_h.mac", "likepy.mac", "dlQuery.mac", "scsrvrepfun.mac", "dlutils.mac";

var name_file = "";

class ReportTrustLoader()
   private var _activeSheet, book;
   private var _rep_date = date(0,0,0);
   private var _contr_num = "";
   private var decimal_sep = GetLocaleInfo(0,LOCALE_SDECIMAL,(NOT IsStandAlone()));

   /* протокол */
   macro print_header
      [ Протокол загрузки данных отчета ДУ по договору #
        на дату # 
       ](_contr_num, _rep_date);
      [┌──────────────────────────────────────┬────────────────┬───────────────────┬─────────────────┐
       │                Бумага                │     Кол-во     │     Стоимость     │       НКД       │
       ├──────────────────────────────────────┼────────────────┼───────────────────┼─────────────────┤];
   end;

   macro print_line(_avr, _amount, _cost, _nkd)
      [│######################################│################│###################│#################│
       │                                      │                │                   │                 │](_avr:w, _amount:a, _cost:a, _nkd:a);
   end;

   macro print_bottom
      [└──────────────────────────────────────┴────────────────┴───────────────────┴─────────────────┘];
   end;
   /************/

   private macro FindOnSheet(sheet, str)
      var row, cell, irow = sheet.rowIterator(), icell, data;
      while (irow.hasNext())
         row = irow.next();
         icell = row.cellIterator();
         while (icell.hasNext())
            cell = icell.{next@()Lorg/apache/poi/ss/usermodel/Cell;}();
            if (cell.getCellType().code == 1)  //STRING
               data = cell.getStringCellValue();
               if (data == str)
                  return cell;
               end;
            end;
         end;
      end;

      return null;
   end;
   
   macro findDataTable()
      private var excel_row = 1;
      private var found = false;

      var objRange = _activeSheet.UsedRange;
      var objTarget = objRange.Find("Состав и стоимость Активов, находящихся в доверительном управлении на конец периода");
      if(objTarget)
         setparm(1, objTarget.row);
         return true;
      else
         return false;
      end;
/*
      while(excel_row < _activeSheet.rows.count) 
         if(_activeSheet.range("B"+int(excel_row)).text == "Состав и стоимость Активов, находящихся в доверительном управлении на конец периода")
            found = true;
            setParm(1, excel_row);
            break;
         end;
         excel_row = excel_row + 1; 
      end;

      return found;
      */
   end;

   macro findDataTablePOI()
      var objTarget = FindOnSheet( _activeSheet, "Состав и стоимость Активов, находящихся в доверительном управлении на конец периода");
      if(objTarget)
         setparm(1, objTarget.getRowIndex());
         return true;
      else
         return false;
      end;
   end;

   macro getFiidByLSIN(lsin)
      var query, cmd, DataSet;
      query = "select * from davoiriss_dbt where upper(t_lsin) = upper(:lsin) ";

      cmd = DL_RSDCommand(query);         
      cmd.AddParam(lsin);

      DataSet = cmd.Execute();

      if(DataSet.moveNext())
         setparm(2, DataSet.fiid);
         return true;
      end;

      return false 
   end;

   macro getFiidByISIN(isin)
      var query, cmd, DataSet;
      query = "select * from davoiriss_dbt where upper(t_isin) = upper(:isin) ";

      cmd = DL_RSDCommand(query);         
      cmd.AddParam(isin);

      DataSet = cmd.Execute();

      if(DataSet.moveNext())
         setparm(2, DataSet.fiid);
         return true;
      end;

      return false 
   end;

   macro findAvoiriss(_findText)
      var textArray = tarray();
      var avr_fiid = -1;
      var item;
      textArray = split(_findText,",");

      for(item, textArray)
         if(getFiidByLSIN(trim(item), avr_fiid))
            setparm(2, avr_fiid);
            return true;

         elif(getFiidByISIN(trim(item), avr_fiid))
            setparm(2, avr_fiid);
            return true;

         end;   
      end;

      return false;
   end;            

   macro loadAvoirissInTable(_fiid, _amount, _cost, _nkd)
      private var cmd_query;
      private var cmd;
      
      cmd_query = " DELETE DAVR_IN_TRUST_DBT "
                + "  WHERE t_repdate = :repdate AND T_CONTRNUM = :contrnum AND t_fiid = :fiid ";

      cmd = rsdCommand(cmd_query);
      cmd.addParam("repdate", RSDBP_IN, _rep_date-1);
      cmd.addParam("contrnum",RSDBP_IN, _contr_num);
      cmd.addParam("fiid",    RSDBP_IN, _fiid);
      cmd.execute();

      cmd_query = "INSERT INTO DAVR_IN_TRUST_DBT VALUES(0, :repdate, :contrnum, :fiid, :amount, :cost, :nkd)";
      cmd = rsdCommand(cmd_query);
      cmd.addParam("repdate", RSDBP_IN, _rep_date-1);
      cmd.addParam("contrnum",RSDBP_IN, _contr_num);
      cmd.addParam("fiid",    RSDBP_IN, _fiid);
      cmd.addParam("amount",  RSDBP_IN, _amount);
      cmd.addParam("cost",    RSDBP_IN, _cost);
      cmd.addParam("nkd",     RSDBP_IN, _nkd);

      cmd.execute();
   end;

   macro getMoney(_valueString)
      return money(_valueString);
   onerror
      return $0.0;
   end;

   macro loadAvoiriss(_data_row)
      private var excel_row = _data_row + 1;
      private var currentText = "";
      private var avr_fiid = -1;
      private var avr_count = 0;
      private var avr_amount = 0;
      private var avr_cost = 0; 
      private var avr_nkdamount = 0; 

      print_header;

      initProgress(0);

      while(excel_row < _activeSheet.rows.count)
         currentText = _activeSheet.range("A"+int(excel_row)).text;

         if(currentText == "Иные виды активов")
            break;
         end;

         if(findAvoiriss(currentText, avr_fiid))
             avr_count = avr_count + 1;

             avr_amount    = int(_activeSheet.range("D"+int(excel_row)).value);
             avr_cost      = getMoney(_activeSheet.range("E"+int(excel_row)).value);
             avr_nkdamount = getMoney(_activeSheet.range("F"+int(excel_row)).value);

             loadAvoirissInTable(avr_fiid, avr_amount, avr_cost, avr_nkdamount);
             
             print_line(currentText, avr_amount, avr_cost, avr_nkdamount);
         else
             print_line("Бумага "+currentText+" не найдена в справочнике.", 0, 0, 0);
         end;

         excel_row = excel_row + 1;
         UseProgress(avr_count);
      end;
 
      RemProgress;

      print_bottom;

      [Обработано строк:     #](avr_count);
   end;

   macro loadAvoirissPOI(_data_row)
      private var row = _data_row + 1;
      private var currentText = "";
      private var avr_fiid = -1;
      private var avr_count = 0;
      private var avr_amount = 0;
      private var avr_cost = 0; 
      private var avr_nkdamount = 0; 

      print_header;

      initProgress(0);

      while(row < book.getRowCount(_activeSheet))
         currentText = string(book.getCellValue("A"+(row+1)));

         if(currentText == "Иные виды активов")
            break;
         end;

         if(findAvoiriss(currentText, avr_fiid))
             avr_count = avr_count + 1;

             avr_amount    = int(book.getCellValue("D"+(row+1)));
             avr_cost      = getMoney(book.getCellValue("E"+(row+1)));
             avr_nkdamount = getMoney(book.getCellValue("F"+(row+1)));

             loadAvoirissInTable(avr_fiid, avr_amount, avr_cost, avr_nkdamount);
             
             print_line(currentText, avr_amount, avr_cost, avr_nkdamount);
         else
             print_line("Бумага "+currentText+" не найдена в справочнике.", 0, 0, 0);
         end;

         row = row + 1;
         UseProgress(avr_count);
      end;
 
      RemProgress;

      print_bottom;

      [Обработано строк:     #](avr_count);
   end;


   macro loadTable(_data_row)
      private var excel_row = _data_row;
      private var currentText = "";
      while(excel_row < _activeSheet.rows.count)
         currentText = _activeSheet.range("A"+int(excel_row)).text;
         if(currentText == "Ценные бумаги, в т.ч.")         
            loadAvoiriss(excel_row);
            break;
         end;  
         excel_row = excel_row + 1;
      end;
   end;

   macro loadTablePOI(_data_row)
      private var row = _data_row;
      private var currentText = "";
      while(row < book.getRowCount(_activeSheet))
         currentText = string(book.getCellValue("A"+(row+1)));
         if(currentText == "Ценные бумаги, в т.ч.")         
            loadAvoirissPOI(row);
            break;
         end;  
         row = row + 1;
      end;
   end;

   macro findReportDate()
      private var excel_row = 1;
      private var currentText = "";
      private var datatext = "";
      private var date_sep = GetLocaleInfo(0,LOCALE_SDATE,(NOT IsStandAlone()));
      var objRange = _activeSheet.UsedRange;
      var objTarget = objRange.Find("Дата:");
      if(objTarget)
         datatext = _activeSheet.range("D"+int(objTarget.row)).text;
         _rep_date = date(int(split(datatext,date_sep)[0]),
                        int(split(datatext,date_sep)[1]),
                        int(split(datatext,date_sep)[2]));
      end;

      if(_rep_date != date(0,0,0))
         return true;
      end;

      return false;

   onerror   
      return false;
   end;

   macro findReportDatePOI()
      private var row = 0;
      private var currentText = "";
      private var datatext = "";
      private var date_sep = GetLocaleInfo(0,LOCALE_SDATE,(NOT IsStandAlone()));

      var objTarget = FindOnSheet( _activeSheet, "Дата:");
      if(objTarget)
         row = objTarget.getRowIndex();
         datatext = string(book.getCellValue("D"+(row+1)));
         _rep_date = date(int(split(datatext,date_sep)[0]),
                        int(split(datatext,date_sep)[1]),
                        int(split(datatext,date_sep)[2]));
      end;

      if(_rep_date != date(0,0,0))
         return true;
      end;

      return false;

   onerror   
      return false;
   end;

   macro findContrNum()
      private var excel_row = 1;
      private var currentText = "";

      while(excel_row < _activeSheet.rows.count)
         currentText = _activeSheet.range("A"+int(excel_row)).text;
         if(currentText == "Договор доверительного управления, № дата")         
             _contr_num = _activeSheet.range("D"+int(excel_row)).text;
             _contr_num = trim(_contr_num);
             break;
         end;  
         excel_row = excel_row + 1;
      end;

      if(_contr_num == "")
         return false;
      end;

      return true;

   onerror 
      return false;
   end;

   macro findContrNumPOI()
      private var row = 0;
      private var currentText = "";

      while(row < book.getRowCount(_activeSheet))
         currentText = string(book.getCellValue("A"+(row+1)));
         if(currentText == "Договор доверительного управления, № дата")         
             _contr_num = string(book.getCellValue("D"+(row+1)));
             _contr_num = trim(_contr_num);
             break;
         end;  
         row = row + 1;
      end;

      if(_contr_num == "")
         return false;
      end;

      return true;

   onerror 
      return false;
   end;

   macro load(_name_file)
      var data_row = -1;   
      var jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
      var rc = jvm.callStaticMethod("ru.softlab.rsbank.poireports.ReportCreator", "getInstance");
      var fileMov = TempFileToServerMover();
      fileMov.CreateTempFromAbsolutTermOrServPath(_name_file);
      book = rc.openWorkbook(fileMov.GetTempFilePath());
      if (book)
         _activeSheet = book.getSheet(0);

         if(not findReportDatePOI())
            return false; 
         end;

         if(not findContrNumPOI())
            return false;
         end;

         if(findDataTablePOI(data_row))
            loadTablePOI(data_row);
         end;

         book.close();
         book = null;
         rc = null;
      end;
   end;
end;

if(selectFile (name_file, mergeFile ("..\\", "*.xls"), "Выберите файл отчета ДУ" , 0, not isStandAlone))
   ReportTrustLoader().load(name_file);
end;


exit(0);
