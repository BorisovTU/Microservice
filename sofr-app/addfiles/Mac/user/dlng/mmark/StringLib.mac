/*-------------------------------------------------------------------------

	Имя файла	: StringLib.mac 

	Описание	: Библиотека по работе со строками

	Программист	: Селезнёв Роман

	Создан		: 29.02.2008


-------------------------------------------------------------------------*/


//
// Разбиение длинной строки, заданной первым параметром на несколько сегментов (строк).
// str -- исходная строка; delim -- символ(строка)-разделитель, отделяющий сегменты
// Возвращается объект TArray
//
macro StrSplitByDelim(str:String, delim:String):TArray
   var res = TArray();
   var curr = Trim(str);	// Уменьшающаяся после поиска каждого разделителя строка 
   var delimLen = StrLen(delim);// Длина строки-разделителя

   var pos = Index(curr, delim);

   while(0 != pos)		// Пока есть разделители в строке
      if(1 != pos)   		// Первым символом является разделитель или разделитель в середине строки
         res(res.Size) = Trim(SubStr(curr, 1, pos-1));
      end;
      curr = Trim(SubStr(curr, pos+delimLen));
      pos  = Index(curr, delim);  
   end;
    
   if("" != curr) 		// Последним символом в строке был разделитель
      res(res.Size) = curr;
   end;

   return res;
end;


//
// Получение большой строки из массива строк (сегментов) с отделением каждого сегмента (подстроки) разделителем
// arr -- исходный массив строк; delim -- символ(строка)-разделитель, отделяющий сегменты
// Возвращает полученную строку
//
macro StrJoinByDelimA(arr:TArray, delim:String):String
   var res:String = "";
   var i = 0;

   while(i < arr.Size) 
      if(0 != i)
         res = res + delim;
      end;

      res = res + arr(i);
      i = i+1;
   end;
 
   return res;
end;


//
// Получение большой строки из массива строк (сегментов) с отделением каждого сегмента (подстроки) разделителем
// delim -- символ(строка)-разделитель, отделяющий сегменты (нулевой параметр). Сегменты задаются, начиная с 1го параметра в произвольном кол-ве
// Возвращает полученную строку
//
macro StrJoinByDelimS(delim:String):String
   var i=1;	// Номер параметра
   var str="";	// Строка (сегмент) для склеивания
   var arr = TArray();

   while(getParm(i, str))
      arr(arr.Size) = str;
      i = i+1;
   end;

   return StrJoinByDelimA(arr, delim);
end;


//
// Получение строки, инициализированной символами(строками)-заполнителями symb в кол-ве len штук
//
macro StrInit(len:Integer, symb:String):String
   var i = 0;
   var res = "";

   while (i<len)   
      res = res + symb;
      i = i+1;
   end;

   return res;
end;


//
// Получение строки, полученной дополнением слева исходной строки символами-заполнителями до требуемой длины результирующей строки
// source -- исходная строка, дополняемая слева; num -- размер результирующей строки; symb -- символ-заполнитель
// Возвращает полученную путём дополнения слева символами symb строку source, чтобы её длина стала равна num
//
macro StrLeftPad(source:String, num:Integer, symb:String):String
   var res = source;
   
   while (StrLen(res) < num)
      res = symb + res;
   end;

   return res;
end;


//
// Получение строки, полученной дополнением справа исходной строки символами-заполнителями до требуемой длины результирующей строки
// source -- исходная строка, дополняемая справа; num -- размер результирующей строки; symb -- символ-заполнитель
// Возвращает полученную путём дополнения справа символами symb строку source, чтобы её длина стала равна num
//
macro StrRightPad(source:String, num:Integer, symb:String):String
   var res = source;
   
   while (StrLen(res) < num)
      res =  res + symb;
   end;

   return res;
end;

//
// Получение строки, полученной путём выделения справа из исходной строки (str) count символов
// str -- исходная строка, count -- кол-во символов, которые надо вырезать из строки справа
// Возвращает полученную путём вырезания справа заданного кол-ва символов строку
//
macro StrRight(str:String, count:Integer):String
   var len = strlen(str);

   if(len <= count)
      return str;
   end;

   return substr(str, len-count+1, count);
end;

//
// Получение строки, полученной путём выделения слевава из исходной строки (str) count символов
// str -- исходная строка, count -- кол-во символов, которые надо вырезать из строки слева
// Возвращает полученную путём вырезания слевава заданного кол-ва символов строку
//
macro StrLeft(str:String, count:Integer):String
   var len = strlen(str);

   if(len <= count)
      return str;
   end;

   return substr(str, 1, count);
end;





