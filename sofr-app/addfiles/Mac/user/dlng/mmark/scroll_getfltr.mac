/*******************************************************************************
 DESCRIPTION  :   Скроллинг с обработкой массового выбора.
 PROGRAMMED BY:   BPV
 CREATION DATE:   10.03.10
*******************************************************************************/
/*******************************************************************************************
  макрофункция для возвращения списка значений первого поля выбранных строк через запятую.
  Опытным путем выявлено, что больше 1000 элементов лучше не пробовать.
  Например, для использования в SQL-условии IN  
  USR_GetList(query, arnames)
  query   - sql-запрос для формирования скроллинга для выбора. Количество колонок любое.
  arnames - необязательный параметр. Массив наименований колонок запроса. 
            Должен строго соответствовать количеству отбираемых полей в sql-запросе. 
  Пример:
  
  import rsbdataset, "scroll_getfltr.mac";
  var arr = TArray();
  arr(0) = "Номер";
  arr(1) = "Наименование";
  var cmd = RsdCommand (" select t_number,t_name from dsfcontr_dbt where t_id in (" 
                          + USR_GetList(" select t_partyid, t_shortname from dparty_dbt where t_partyid in (select t_partyid from dsfcontr_dbt)", arr) 
                          + ")");
  var rs = RSDRecordset(cmd,RSDVAL_CLIENT, RSDVAL_STATIC );
  RunScroll(rs);
***********************************************************************************************/

import "globals.mac", "dlrepform_h.mac";

// Вспомогательная процедура подготовки информации с атрибутами полей
PRIVATE MACRO AddCol (ar,ind, fld, head, width, rdonly, ty, dp)
   ar.value (ind * 6)     = fld;
   ar.value (ind * 6 + 1) = head;
   ar.value (ind * 6 + 2) = width;
   ar.value (ind * 6 + 3 ) = ty;   // fldType
   ar.value (ind * 6 + 4 ) = dp;//   decPoint
   ar.value (ind * 6 + 5 ) = 0;   // reserv
END;

MACRO USR_GetList(query, arnames)
    var cmd, rs, filter = "", a, n, total = 0, count = 0, i = 0;

    /*Обработчик скроллинга*/
    macro EvProc (rs, cmd, id, key )
      if (cmd == DLG_INIT)
         /*Инициализация клавиш для запуска массовой обработки*/
         AddMultiAction(rs, 13/* Enter*/); 
      elif (cmd == DLG_MSELSTART)/*Запуск массовой обработки*/
        /* if( GetMultiCount(rs) > 2000)
            msgbox("Выделено слишком много записей (более 1001)");
            return CM_MSEL_STOP_CLEARALL;
         end; */
         filter = "";
         //InitProgress(total, "", "Обработка ");
      elif (cmd == DLG_MSELEND) /*Завершение массовой обработки*/
         //Remprogress;
         filter = SubStr(filter,3);
         //msgbox(filter);
         return CM_MSEL_STOP_CLEARALL;
      elif (cmd == DLG_MSEL)/*Обработка одной записи из выделения*/
         //Useprogress(count = count + 1);
         filter = filter + ", " + rs.value(0);
         return CM_MSEL_CONT_CLEAR;
      elif (cmd == DLG_KEY)         
         if (key == 13 /* Enter*/)           
            filter = rs.value(0);
         end;
         if (filter != "")//что бы закрыть скроллинг после выбора записей
            return CM_CANCEL;
         end;
      end;	
      return CM_DEFAULT;
   end;
                                 

/******************************************/
/*    точка входа                         */
/******************************************/

 
   cmd = RsdCommand (query);
   rs = RSDRecordset(cmd,RSDVAL_CLIENT, RSDVAL_STATIC );

   a = TArray;
   if (ValType(arnames) != V_UNDEF)
      n = -1;
      rs.movefirst();//чтобы стал доступен fldCount
      for (i, 0, rs.fldCount - 1)
         AddCol (a, n = n+1, rs.fld(i).name, arnames(i) , 15, true, 2,0);
      end;
   end;

   if (RunScroll (rs, a.size/6 , a, "", "EvProc", " Список ", " Enter - Выбор", false))
   end;
   
   return filter;
OnError(er)
   MsgBox (er.Message,"| Модуль:",er.Module,"| строка:",er.Line);
   return "";
END;

//var arname = TArray();
//arname(0) = "Идентификатор";
//arname(1) = "Наименование";
//println(USR_GetList(" SELECT t_partyid, t_shortname from dparty_dbt where rownum < 10 "/*, arname*/));

             
