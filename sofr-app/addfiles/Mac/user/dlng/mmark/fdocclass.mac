
import BankInter, PaymInter, OprInter, PTInter, Globals, RSD,fx_globals;

const КассовыйПриход        = CASH_BOF_INCORDER,    // 430
      КассовыйРасход        = CASH_BOF_OUTORDER,    // 440
      /* КассовыйПодкрепление = 400, */
      Мемордер              = DLDOC_MEMORIALORDER,  //  70
      Мультивалютный        = CB_MULTYDOC,          //  15
      ПлатежББ              = DLDOC_BANKPAYMENT,    //  16
      ПлатежББВАЛ           = BBANK_CPORDER,        //  27
      ТребованиеББ          = DLDOC_BANKCLAIM,      //  17
      ПлатежРКО             = PS_PAYORDER,          // 201
      ПлатежРКОВАЛ          = PS_CPORDER;           // 202

/*────────────────────────────────────────────────────────────────────────────────────────────────────────────────*/
class TBBPrimDoc () 

/*╒═════════════════════════════════════════════════════════════════════╗
  │      private macro getChapterByAccount (t_account)                  ║
  │      private macro getIDByAccount      (t_account)                  ║
  │      private macro getCorrAccountByID  (id)                         ║
  │      private macro getIDByBankCode     (t_code)                     ║
  │      private macro getSWIFTCodeById    (id)                         ║
  │      private macro isDigitNum          (what)                       ║
  │      private macro getFIIDByAccount    (t_account)                  ║
  │      private macro getNameByPartyID    (id)                         ║
  ├─────────────────────────────────────────────────────────────────────╢
  │      private macro InsertCashDoc     ()                             ║
  │      private macro InsertBBPayment   ()                             ║
  │      private macro InsertPSPayment   ()                             ║
  │      private macro InsertMemOrderDoc ()                             ║
  │      private macro InsertMultyDoc    ()                             ║
  ├─────────────────────────────────────────────────────────────────────╢
  │      macro insert ()                                                ║
  └─────────────────────────────────────────────────────────────────────╜*/


    var m_docKind,                                /* ВидДокумента                                       */
        m_Kind_Operation   = 0,
        m_LaunchOper       = false,
        m_rate             = 0,
        m_docNumber,                              /* НомерДокумента                                     */
        m_docDate          = {curdate},           /* ДатаДокумента                                      */
        m_valueDate        = {curdate},           /* ДатаЗначения                                       */
        m_ClientDate       = {curdate},           /* Дата принятия документа от клиента                 */
        m_BankPayerCode    = {MFO_BANK},          /* SWIFT или БИК банка плательщика                    */
        m_PayerINNKPP      = "",                  /* ИНН/КППплательщика                                 */
        m_PayerAccount,                           /* СчетПлательщика                                    */
        m_PayerCurrency    = 0,                   /* Валюта плательщика - для загрузки SWIFT-документов */
        m_Payername        = "",                  /* НаименованиеПлательщика                            */
        m_BankReceiverCode = {MFO_BANK},          /* SWIFT или БИК банка получателя                     */
        m_ReceiverINNKPP,                         /* ИНН/КППполучателя                                  */
        m_ReceiverAccount,                        /* СчетПолучателя                                     */
        m_ReceiverCurrency = 0,                   /* Валюта получателя - для загрузки SWIFT-документов  */
        m_ReceiverName     = "",                  /* НаименованиеПолучателя                             */
        m_ReceiverBankName = "",                  /* Наименование банка получателя                      */
        m_AmountDt         = $0.00,               /* Сумма по дебету                                    */
        m_AmountKt         = $0.00,               /* Сумма по кредиту                                   */
        m_PaymKind         = "Э",                 /* ВидПлатежа                                         */
        m_ShifrOper        = "09",                /* ШифрОперации                                       */
        m_NumberPack       = {Oper},              /* НомерПачки                                         */
        m_Oper             = {Oper},              /* Операционист                                       */
        m_Department       = {OperDPRTNode},      /* Филиал                                             */
        m_Ground,                                 /* Основание                                          */
        m_Priority         = 6,                   /* Очередность                                        */
        m_Origin           = 0,                   /* Происхождение                                      */
        m_OriginStatus     = CodeFor (1),         /* СтатусСоставителя                                  */
        /*----------------------------------------------------------------------------------------------*/
        m_KbK              = "0",                 /* КБК                                                */
        m_OcatoCode        = "0",                 /* ОКАТО                                              */
        m_TaxGround        = "0",                 /* ОснованиеНП                                        */
        m_TaxPeriod        = 0,                   /* НалоговыйПериод                                    */
        m_TaxDocNumber     = 0,                   /* НомерНД                                            */
        m_TaxDocDate       = "0",                 /* ДатаНД                                             */
        m_TaxDocType       = "0",                 /* ТипНД                                              */
        /*----------------------------------------------------------------------------------------------*/
        m_CashSymbol       = CodeFor (1),         /* КассовыйСимвол                                     */
        m_FIOClient        = CodeFor (1),         /* ФИО клиента                                        */
        m_PaperKind        = 0,                   /* Тип документа, удостоверяющего личность            */
        m_PaperSeries      = "",                  /* Серия                                              */
        m_PaperNumber      = "",                  /* Номер                                              */
        m_PaperIssuedDate  = date ("01.01.0001"), /* Дата выдачи                                        */
        m_PaperIssuer      = "",                  /* Кем выдан                                          */
        /*----------------------------------------------------------------------------------------------*/
        m_KNP:string       = "",                  // КНП - код назначения платежа
        m_KOD:string       = "",                  // КОД - код отправителя денег
        m_KBE:string       = "",                  // КБе - код бенефициара
        m_ReceiverCountry:string = "",            //     - страна бенефициара
        /*----------------------------------------------------------------------------------------------*/
        m_TypeDocument     = strfor(1),
        m_UserTypeDocument = strfor(0),
        m_Kind_Oper        = strfor(0);

/*======================================================================*/
    private macro fillKZPaymentProp( RsbPaymentBuff )

        RsbPaymentBuff.KZ_GroundCode        =  m_KNP;
        RsbPaymentBuff.KZ_PayerCode         =  m_KOD;
        RsbPaymentBuff.KZ_ReceiverCode      =  m_KBE;
        RsbPaymentBuff.KZ_ReceiverCountry   =  m_ReceiverCountry;
    end;
/*────────────────────────────────────────────────────────────────────────────────────────────────────────────────*/
    /* заполнение налоговых реквизитов платежа */
    private macro fillTaxPaymentStuff (RsbPaymentBuff)
        //RsbPaymentBuff.TaxAuthorState = m_OriginStatus;  /* СтатусСоставителя */
        RsbPaymentBuff.BttTICode      = m_KbK;           /* КБК               */
        //RsbPaymentBuff.OKATOCode      = m_OcatoCode;     /* ОКАТО             */          
        //RsbPaymentBuff.TaxPmGround    = m_TaxGround;     /* ОснованиеНП       */
        //RsbPaymentBuff.TaxPmPeriod    = m_TaxPeriod;     /* НалоговыйПериод   */
        //RsbPaymentBuff.TaxPmNumber    = m_TaxDocNumber;  /* НомерНД           */
        //RsbPaymentBuff.TaxPmDate      = m_TaxDocDate;    /* ДатаНД            */
        //RsbPaymentBuff.TaxPmType      = m_TaxDocType;    /* ТипНД             */
    end;
/*────────────────────────────────────────────────────────────────────────────────────────────────────────────────*/
    private macro getChapterByAccount (t_account)
        var cmd = RSDCommand ("SELECT bal.t_chapter"
                              "  FROM dbalance_dbt bal"
                              " WHERE bal.t_balance = SUBSTR (?, 0, 3)"
                              "   AND bal.t_inumplan = 0" );
        cmd.NullConversion = true;
        cmd.AddParam ("acc", RSDBP_IN, t_account);
        
        var rs = RSDRecordSet ( cmd );
        if (rs.moveNext)
            return rs.value ("t_chapter");
        end;

        return 1;
    end;
/*────────────────────────────────────────────────────────────────────────────────────────────────────────────────*/
    private macro getFilialByAccount (t_account)
        var cmd = RSDCommand ("select  t_partyid from ddp_dep_dbt where t_code= (select a.t_department from daccount_dbt a where a.t_account= ? ) " ) ;
        cmd.NullConversion = true;
        cmd.AddParam ("acc", RSDBP_IN, t_account);
        
        var rs = RSDRecordSet ( cmd );
        if (rs.moveNext)
            return rs.value ("t_partyid");
        end;

        return 1;
    end;


    
/*────────────────────────────────────────────────────────────────────────────────────────────────────────────────*/
    private macro getIDByAccount (t_account)
        var cmd = RSDCommand ("SELECT t_client FROM daccount_dbt WHERE t_account = ?");       
        cmd.NullConversion = true;
        cmd.AddParam ("acc", RSDBP_IN, t_account);
        
        var rs = RSDRecordSet ( cmd );
        
        if (rs.moveNext)
            return rs.value ("t_client");
        end;
        return 0;
    end;
    
/*────────────────────────────────────────────────────────────────────────────────────────────────────────────────*/
    private macro GetCorrAccountByID ( id )
        var cmd = RSDCommand ("select t_coracc from dbankdprt_dbt where t_partyid = ?");
        cmd.NullConversion = true;
        cmd.AddParam ("pid", RSDBP_IN, id);
        
        var rs = RSDRecordSet (cmd);
        
        if( rs.MoveNext )
            return rs.value(0);
        end;
        
        return "";
    end;

/*────────────────────────────────────────────────────────────────────────────────────────────────────────────────*/
    private macro getIDByBankCode (t_code)
        var cmd = RSDCommand ("SELECT t_objectid FROM dobjcode_dbt WHERE t_codekind = 1 AND t_code = ?");
        cmd.NullConversion = true;
        cmd.AddParam ("code", RSDBP_IN, t_code);
        
        var rs = RSDRecordSet ( cmd );

        if (rs.moveNext)
            return rs.value ("t_objectid");
        end;
        
        return 0;
        
    end;

/*────────────────────────────────────────────────────────────────────────────────────────────────────────────────*/
    private macro getSWIFTCodeById (id)
        var cmd = RSDCommand ("SELECT t_code FROM dobjcode_dbt WHERE t_codekind = 6 AND t_objectid = ?");
        cmd.NullConversion = true;
        cmd.AddParam ("obj_id", RSDBP_IN, id);
        
        var rs = RSDRecordSet ( cmd );
        
        if (rs.moveNext)
            return rs.value ("t_code");
        end;
        
        return "";
    end;

/*────────────────────────────────────────────────────────────────────────────────────────────────────────────────*/
    private macro isDigitNum (what:string)
        var stat = false, 
            i = 1, 
            ch, 
            DigitString = "0123456789";

        while ((not stat) and (i <= strlen(what)))
            ch = substr (what, i, 1 );
        
            if( not index( DigitString, ch ))
                stat = 1;
            end;
            
            i = i + 1;
        end;
    
        return not (stat);
    end;
    
/*────────────────────────────────────────────────────────────────────────────────────────────────────────────────*/
    private macro getFIIDByAccount (t_account)
        var cmd = RSDCommand ("SELECT t_code_currency FROM daccount_dbt WHERE t_account = ?");
        cmd.NullConversion = true;
        cmd.AddParam ("acc", RSDBP_IN, t_account);
        
        var rs = RSDRecordSet ( cmd );
        
        if (rs.moveNext)
            return rs.value ("t_code_currency");
        end;
        
        return 0;
    end;

/*────────────────────────────────────────────────────────────────────────────────────────────────────────────────*/
    private macro getNameByPartyID (id)
        var Party = RSBParty (id);
        return Party.FullName;
    end;

/*────────────────────────────────────────────────────────────────────────────────────────────────────────────────*/
    private macro InsertCashDoc () /**** Кассовый документ ****/
        var stat,fil;
        var Order:object;
        
        if (m_docKind == КассовыйПриход)
            Order               = RsbBBIncCashOrder (0);
            Order.ClientAccount = m_ReceiverAccount; 
        
        elif (m_docKind == КассовыйРасход )
            Order               = RsbBBOutCashOrder (0);
            Order.ClientAccount = m_PayerAccount;
        end;
        
        Order.Kind_Operation    = m_Kind_Operation;
        Order.Status            = 1;
        Order.LaunchOper        = m_LaunchOper;
        Order.Oper              = m_Oper;
        Order.Origin            = m_Origin;
        
        if (m_FIOClient != CodeFor(1)) // если есть сведения о документе
            Order.FIOClient       = m_FIOClient;
            Order.PaperIssuedDate = m_PaperIssuedDate;
            Order.PaperIssuer     = string(m_PaperIssuer);
            Order.PaperKind       = int(m_PaperKind);
            Order.PaperNumber     = string(m_PaperNumber);
            Order.PaperSeries     = string(m_PaperSeries);
        end;
        
        var Payment :RsbPayment = Order.Payment;
        
        Payment.DocKind         = 
        Payment.PrimDocKind     = m_docKind;
        Payment.CryptoAction    (string ("Автоматическое_формирование_платежей"));
        Payment.Purpose         = 14;
        Payment.Oper            = m_Oper;

        Payment.Number          = string (m_docNumber);
        Payment.NumberPack      = m_NumberPack;
        Payment.ShifrOper       = m_ShifrOper; 
        Payment.Date            = m_docDate;
        Payment.ClientDate      = m_ClientDate;
        Payment.PayDate         = m_valueDate;
        Payment.ValueDate       = m_valueDate;
        Payment.PayerAmount     = m_AmountDt;
        Payment.ReceiverAmount  = m_AmountDt;
        Payment.StartDepartment =
        Payment.Department      = m_Department;
        if ( m_Department == 7 )
           fil=112839;
        else
           fil=_SelfID;
        end;

        /*
        Payment.FuturePayerAmount    = m_AmountDt;
        Payment.FutureReceiverAmount = m_AmountDt;
        */
        Payment.Ground          = string (m_Ground);
        Payment.PayerFIID       = m_PayerCurrency;
        Payment.ReceiverFIID    = m_ReceiverCurrency;
        Payment.BaseFIID        = Payment.PayerFIID;
        Payment.SetPayerPI 
                            (
                                PAYMENTS_GROUP_UNDEF, 
                                fil,
                                3, 
                                {MFO_BANK}, 
                                {NAME_BANK}, 
                                {CORAC_BANK}, 
                                m_PayerCurrency, 
                                1, 
                                m_PayerAccount/*, 
                                _SelfID, 
                                {NAME_BANK}, 
                                {INN_BANK} */
                            ); 
 
        Payment.SetReceiverPI 
                            ( 
                                PAYMENTS_GROUP_UNDEF, 
                                fil, 
                                3, 
                                {MFO_BANK}, 
                                {NAME_BANK}, 
                                {CORAC_BANK}, 
                                m_ReceiverCurrency, 
                                1, 
                                m_ReceiverAccount/*, 
                                _SelfID, 
                                {NAME_BANK}, 
                                {INN_BANK} */
                            );

        if (m_CashSymbol != CodeFor(1)) // если задан кассовый символ
            var symb = TRecHandler("symbcash.dbt");

            var symbdockind = 1;
            if (Payment.PayerFIID != 0)
                symbdockind = 7;
            end;

            var RS = RSDRecordSet (" SELECT t_type_symbol "+
                                   "   FROM dlistsymb_dbt ls "+
                                   "  WHERE ls.t_dockind = "+string(symbdockind)+
                                   "    AND ls.t_symb_cash = " + string (m_cashSymbol));
            
            if (RS.moveNext)
                symb.rec.dockind = symbdockind;
                symb.rec.kind    = RS.value("t_type_symbol");
                symb.rec.symbol  = string (m_cashSymbol);
                symb.rec.sum     = m_amountDt;
            end;

            if (Payment.CashSymbols.Insert(symb))
               [##############  -err: Ошибка при вставке кассового символа] (Payment.DocumentID);
               //msgbox ( "-err: Ошибка при вставке кассового символа" );
            end;
        end;
        if (Order.Update() == 0)
          return Payment.DocumentID;
        end;
        return 0;
    end; /* private macro InsertCashDoc () */

/*────────────────────────────────────────────────────────────────────────────────────────────────────────────────*/
    private macro InsertBBPayment () /*** Банковский платеж ***/
        var CodeKind;
        var BankPaym :object;

        if   ( m_docKind == ПлатежББ)
            BankPaym            = GenObject ( "RsbBankPayment", 0 );
            BankPaym.Status     = 1; 
        
        elif ( m_docKind == ПлатежББВал)
            BankPaym            = GenObject ( "RsbBbCpOrder", 0 )
        
        elif ( m_docKind == ТребованиеББ)
            BankPaym            = GenObject ( "RsbBankClaim", 0 );
            BankPaym.Status     = 1; 
        end;

        BankPaym.Kind_Operation = m_Kind_Operation;
        
        BankPaym.Origin     = m_Origin;
        BankPaym.Oper       = m_Oper; 
        BankPaym.LaunchOper = m_LaunchOper;
        
        var Payment = BankPaym.Payment;
        

        if (m_KBK     != CodeFor (1)) 
            fillTaxPaymentStuff (Payment);
        end;
        // ----------
        
        if (m_docKind == ТребованиеББ)
            Payment.DemandAcceptTerm = 1; /*PM_DEMAND_TERM_WITHOUTACCEPT*/
            Payment.DemandAccept     = 0; /*PM_DEMAND_ACCEPT_NONE*/
        end;
        
        Payment.DocKind            = m_docKind;
        Payment.Ground             = m_Ground;
        Payment.ShifrOper          = m_ShifrOper;
        Payment.Number             = string (m_docNumber);
        Payment.Oper               = m_Oper;
        Payment.FuturePayerFIID    =
        Payment.FutureReceiverFIID =
        Payment.PayerFIID          =
        Payment.ReceiverFIID       =
        Payment.BaseFIID           = getFIIDByAccount (m_PayerAccount);
        Payment.PaymentKind        = m_PaymKind;
        Payment.Priority           = m_Priority;
        Payment.NumberPack         = m_NumberPack;
        Payment.Purpose            = 15; /*PM_PURP_BANKPAYMENT*/
        Payment.IsPlanPaym         = CodeFor (88);
        Payment.ClientDate         = m_ClientDate;
        Payment.StartDepartment    =
        Payment.Department         = m_Department;

        
        Payment.Date           = 
        Payment.ValueDate      = m_valueDate;
        Payment.ReceiverAmount = 
        Payment.PayerAmount    = 
        Payment.BaseAmount     = m_AmountDt;
        
        Payment.PayerBankEnterDate = m_docDate;
        
        if (isDigitNum(m_BankReceiverCode))  
            CodeKind = 3;
        else
            CodeKind = 6;
        end;

        if (m_ReceiverBankName = "")
            m_ReceiverBankName = getNameByPartyID (getIDByBankCode(m_BankReceiverCode));
        end;
        
        if (m_docKind == ТребованиеББ) /* Платежные инструкции для требования банка */
            Payment.SetReceiverPI 
                                ( 
                                    PAYMENTS_GROUP_UNDEF, 
                                    {OurBank}, 
                                    3, 
                                    {MFO_BANK}, 
                                    {NAME_BANK},
                                    {CORAC_BANK},
                                    0, 
                                    1, 
                                    m_ReceiverAccount, 
                                    {OurBank}, 
                                    m_ReceiverName, 
                                    m_ReceiverINNKPP
                                );

            Payment.SetPayerPI
                                (
                                    PAYMENTS_GROUP_UNDEF, 
                                    getIDByBankCode (m_BankReceiverCode), 
                                    CodeKind, 
                                    m_BankPayerCode, 
                                    getNameByPartyID   (getIDByBankCode(m_BankPayerCode)),
                                    getCorrAccountByID (getIDByBankCode(m_BankPayerCode)),
                                    0, 
                                    1, 
                                    m_PayerAccount, 
                                    -1, 
                                    m_PayerName, 
                                    m_PayerINNKPP
                                );
        
        elif (m_docKind == ПлатежББ) /* Платежные инструкции для рублевого платежа банка */
            CodeKind = 3;
            Payment.SetPayerPI 
                                ( 
                                    PAYMENTS_GROUP_INTERNAL, 
                                    {OurBank}, 
                                    3, 
                                    {MFO_BANK}, 
                                    {NAME_BANK},
                                    {CORAC_BANK},
                                    0, 
                                    1, 
                                    m_PayerAccount, 
                                    {OurBank}, 
                                    m_Payername, 
                                    m_PayerINNKPP
                                );

            if ((m_BankReceiverCode == {MFO_BANK}) and (CodeKind == 3)) // внутренный платеж
                Payment.SetReceiverPI
                                    ( 
                                        PAYMENTS_GROUP_INTERNAL,                 /* Group        : INTEGER */
                                        _SelfID,                             /* BankID       : INTEGER */
                                        3,                                    /* BankCodeKind : INTEGER */
                                        {MFO_BANK},                           /* BankCode     : STRING  */
                                        {NAME_BANK},                          /* BankName     : STRING  */
                                        {CORAC_BANK},                         /* CorrAcc      : STRING  */
                                        0,                                    /* FIID         : INTEGER */
                                        1,                                    /* Chapter      : INTEGER */
                                        m_ReceiverAccount,                    /* Account      : STRING  */
                                        0,                                    /* Client       : INTEGER */
                                        m_ReceiverName,                       /* ClientName   : STRING  */
                                        m_ReceiverINNKPP                      /* ClientINN    : STRING  */
                                    ); 
            else
                Payment.SetReceiverPI
                                    ( 
                                        PAYMENTS_GROUP_UNDEF,              /* Group        : INTEGER */
                                        getIDByBankCode (m_BankReceiverCode), /* BankID       : INTEGER */
                                        CodeKind,                             /* BankCodeKind : INTEGER */
                                        m_BankReceiverCode,                   /* BankCode     : STRING  */
                                        m_ReceiverBankName,                   /* BankName     : STRING  */
                                        getCorrAccountByID (getIDByBankCode(m_BankReceiverCode)), /* CorrAcc      : STRING  */
                                        0,                                    /* FIID         : INTEGER */
                                        1,                                    /* Chapter      : INTEGER */
                                        m_ReceiverAccount,                    /* Account      : STRING  */
                                        0,                                   /* Client       : INTEGER */
                                        m_ReceiverName,                       /* ClientName   : STRING  */
                                        m_ReceiverINNKPP                      /* ClientINN    : STRING  */
                                    );
            end;

        elif (m_docKind == ПлатежББВал) /* Платежные инструкции для валютного платежа банка */
            CodeKind = 6;
            Payment.SetPayerPI 
                                ( 
                                    PAYMENTS_GROUP_UNDEF, 
                                    {OurBank}, 
                                    6, 
                                    getSWIFTCodeById ({OurBank}), 
                                    {NAME_BANK},
                                    {CORAC_BANK},
                                    getFIIDByAccount (m_PayerAccount), 
                                    1, 
                                    m_PayerAccount, 
                                    {OurBank}, 
                                    m_Payername, 
                                    m_PayerINNKPP
                                );

            Payment.SetReceiverPI
                                ( 
                                    PAYMENTS_GROUP_UNDEF, 
                                    getIDByBankCode (m_BankReceiverCode), 
                                    CodeKind, 
                                    m_BankReceiverCode, 
                                    m_ReceiverBankName,
                                    getCorrAccountByID (getIDByBankCode(m_BankReceiverCode)),
                                    getFIIDByAccount (m_ReceiverAccount), 
                                    1,
                                    m_ReceiverAccount, 
                                    -1, 
                                    m_ReceiverName, 
                                    m_ReceiverINNKPP
                                );
        end;
    

    fillKZPaymentProp( Payment );

        if (BankPaym.Update () == 0)
          return Payment.DocumentID;
        end;
        return 0;
    end; /* private macro InsertBBPayment () */


/*────────────────────────────────────────────────────────────────────────────────────────────────────────────────*/
    private macro InsertPSPayment () /**** Клиентский платеж ****/
        var PSPayOrd : object;
        var Payment  : object;
        var CodeKind;
        
        if (m_docKind == ПлатежРКО)
            PSPayOrd = GenObject("RsbPSPayOrder", 0);

            if   (m_ShifrOper == "01")
                PSPayOrd.DocKind    = PSPOKIND_ORDER;
            
            elif (m_ShifrOper == "02")
                PSPayOrd.DocKind    = PSPOKIND_DEMAND;
                PSPayOrd.AcceptTerm = PSPAYDEM_TERM_WITHOUTACCEPT;
                PSPayOrd.Accept     = PSPAYDEM_ST_ACCEPT;
                
                /* если кому захочется привинтить акцепт - вот ориентир... */
                // PSPayord.AcceptTerm == PSPAYDEM_TERM_ACCEPT;
                // PSPayOrd.AcceptPeriod = m_AcceptPeriod;
                // PSPayord.AcceptDate   = GetDateAfterWorkDays( {curdate}, m_AcceptPeriod );        
            
            elif (m_ShifrOper == "06")
                PSPayOrd.DocKind    = PSPOKIND_REQUEST;
                PSPayOrd.AcceptTerm = PSPAYDEM_TERM_WITHOUTACCEPT;
                PSPayOrd.Accept     = PSPAYDEM_ST_ACCEPT;
                PSPayOrd.ReqSum     = m_AmountDt;
            end; 
            
        elif (m_docKind == ПлатежРКОВал)
            PSPayOrd = GenObject("RsbPsCpOrder", 0);
        end;
        
        PSPayOrd.Kind_Operation = m_Kind_Operation;

        PSPayOrd.Origin     = m_Origin; 
        PSPayOrd.Oper       = m_Oper; 
        PSPayOrd.LaunchOper = m_LaunchOper;
        
        Payment = PSPayOrd.Payment;
        
        if (m_KBK     != CodeFor (1)) 
            fillTaxPaymentStuff (Payment);
        end;
        // ----------
        
        Payment.DocKind        = m_docKind;
        Payment.Purpose        = 7; /*PM_PURP_POPRIMARY*/
        Payment.PaymStatus     = 1;
        Payment.StartDepartment=
        Payment.Department     = m_Department;
        Payment.NumberPack     = m_NumberPack;
        Payment.Number         = m_docNumber;  
        Payment.Oper           = m_Oper;
        
        Payment.BaseAmount     =
        Payment.PayerAmount    =
        Payment.ReceiverAmount = m_AmountDt;
        
        Payment.ClientDate     = m_ClientDate;
        Payment.Date           = m_docDate;
        Payment.ValueDate      = m_valueDate;
        Payment.PayDate        = m_valueDate;
        Payment.Ground         = m_Ground;
        Payment.Priority       = m_Priority;
        
        if (m_docKind == ПлатежРКО)
            Payment.ShifrOper      = m_ShifrOper;
        end;
        
        Payment.SetPayerPI
                            ( 
                                PAYMENTS_GROUP_UNDEF,        
                                _SelfID,                    
                                0,                           
                                "",                          
                                "",                          
                                "",                          
                                m_PayerCurrency,
                                1/*CHAPT1*/,                 
                                m_PayerAccount,    
                                0,                           
                                "",                          
                                "" 
                            );

        if (isDigitNum (m_BankReceiverCode))
            CodeKind = 3;
        else
            CodeKind = 6;
        end;        
        
        if (m_ReceiverBankName = "")
            m_ReceiverBankName = getNameByPartyID (getIDByBankCode(m_BankReceiverCode));
        end;
        
        Payment.SetReceiverPI
                            ( 
                                PAYMENTS_GROUP_UNDEF,
                                0,
                                CodeKind,
                                m_BankReceiverCode,
                                m_ReceiverBankName,
                                getCorrAccountByID (getIDByBankCode(m_BankReceiverCode)),
                                m_ReceiverCurrency,
                                1/*CHAPT1*/,                   
                                m_ReceiverAccount,
                                0, // -1 ???
                                m_ReceiverName,
                                m_ReceiverINNKPP
                            );
        
        /*** указание FIID вверх не передвигать, РСБанку от этого почему-то мощно сносит крышу  <zip_z.> ***/
        Payment.BaseFIID       =
        Payment.PayerFIID      = getFIIDByAccount (m_PayerAccount);
        Payment.ReceiverFIID   = getFIIDByAccount (m_ReceiverAccount);


    fillKZPaymentProp( Payment );
        
        return PSPayord.Update ();
    end;  /* private macro InsertPSPayment () */


/*────────────────────────────────────────────────────────────────────────────────────────────────────────────────*/
    private macro InsertMemOrderDoc ()     /**** Мемордер ****/
        var Memorial                = GenObject( "RsbMemorialOrder", 0 );
        var Payment                 = Memorial.Payment();
        var fil;

        Memorial.Kind_Operation = m_Kind_Operation;

        Memorial.State            = 0;
        Memorial.Oper             = m_Oper;
        Memorial.Chapter          = getChapterByAccount (m_PayerAccount);
        Memorial.Code_Currency    = getFIIDByAccount (m_PayerAccount);
        Memorial.Kind_Oper        = m_Kind_Oper;
        Memorial.Origin           = m_Origin;

        Memorial.LaunchOper       = m_LaunchOper;
        Memorial.TypeDocument     = m_TypeDocument;
        Memorial.UserTypeDocument = m_UserTypeDocument;
        Payment.StartDepartment   =
        Payment.Department        = m_Department;
        Payment.DocKind         = m_docKind;
        Payment.Purpose         = PM_PURP_MEMORDER;
        Payment.ShifrOper       = m_ShifrOper;

        Payment.BaseFIID            = Memorial.Code_Currency;
        Payment.Number              = m_docNumber;
        Payment.Oper                = m_Oper;
        Payment.Ground              = m_Ground;
        Payment.PayerAmount         = 
        Payment.ReceiverAmount      = 
        Payment.BaseAmount          = m_AmountDt;
        Payment.ValueDate           = 
        Payment.Date                = 
        Payment.PayerBankEnterDate  = 
        Payment.ClientDate          = m_docDate;
        Payment.IsPlanPaym          = "X";
        Payment.PaymentKind         = m_PaymKind;
        Payment.Priority            = m_Priority;
        Payment.NumberPack          = m_NumberPack;


//  var RDepartmentID         = Получить_ID_Филиала( getAccountDepartment(chapter, accReceiver, currency) );


        Payment.SetPayerPI
                        ( 
                            PAYMENTS_GROUP_INTERNAL,                 /* Group        : INTEGER */
                            getFilialByAccount(m_PayerAccount),      /* BankID       : INTEGER */
                            3,                                       /* BankCodeKind : INTEGER */
                            {MFO_BANK},                              /* BankCode     : STRING  */
                            {NAME_BANK},                             /* BankName     : STRING  */
                            {CORAC_BANK},                            /* CorrAcc      : STRING  */
                            getFIIDByAccount (m_PayerAccount),       /* FIID         : INTEGER */
                            getChapterByAccount (m_PayerAccount),    /* Chapter      : INTEGER */
                            m_PayerAccount,                          /* Account      : STRING  */
                            getIDByAccount(m_PayerAccount), 
                            m_PayerName,
//                            getNameByPartyID(getIDByAccount(m_PayerAccount)), // m_PayerName

                            m_PayerINNKPP
                        );

        Payment.SetReceiverPI
                        ( 
                           PAYMENTS_GROUP_INTERNAL,                 /* Group        : INTEGER */
                           getFilialByAccount(m_ReceiverAccount),   /* BankID       : INTEGER */
                           3,                                       /* BankCodeKind : INTEGER */
                           {MFO_BANK},                              /* BankCode     : STRING  */
                           {NAME_BANK},                             /* BankName     : STRING  */
                           {CORAC_BANK},                            /* CorrAcc      : STRING  */
                           getFIIDByAccount (m_ReceiverAccount),    /* FIID         : INTEGER */
                           getChapterByAccount (m_ReceiverAccount), /* Chapter      : INTEGER */
                           m_ReceiverAccount,                       /* Account      : STRING  */
                           getIDByAccount (m_ReceiverAccount), 
                           m_ReceiverName,
//                         getNameByPartyID (getIDByAccount (m_ReceiverAccount)), 
                           m_ReceiverINNKPP
                        );
                               
        if (m_CashSymbol != CodeFor(1)) // если задан кассовый символ
            Payment.SymbNotBalDebet = string (" ", m_CashSymbol);
        end;

    fillKZPaymentProp( Payment );

        if (Memorial.Update() == 0)
          return Payment.DocumentID;
        end;
        return 0;

    end; /* private macro InsertMemOrderDoc () */

    
/*────────────────────────────────────────────────────────────────────────────────────────────────────────────────*/
    private macro InsertMultyDoc () /* Мультивалютный документ */
        var Multy    = GenObject( "RsbMultyDoc", 0 );
        var Payment  = Multy.Payment();

        Multy.Kind_Operation = m_Kind_Operation;

        Multy.Oper                 = m_oper;
        Multy.Chapter              = 1;
        Multy.Origin               = m_origin;
        Multy.Status               = 1;
        Multy.LaunchOper           = m_Kind_Operation;

        
        Payment.DocKind            = m_docKind;
        Payment.Purpose            = 49;
        Payment.PaymStatus         = 0;
        Payment.StartDepartment    =
        Payment.Department         = m_Department;
        Payment.NumberPack         = m_NumberPack;
        Payment.Number             = m_docNumber;
        Payment.Oper               = m_Oper;
        
        Payment.ClientDate         = m_ClientDate;
        Payment.Date               = m_docDate;
        Payment.ValueDate          = m_valueDate;
        Payment.PayDate            = m_valueDate;
        
        Payment.Priority           = m_Priority;
        Payment.ShifrOper          = m_ShifrOper;
        
        Payment.PayerFIID          = getFIIDByAccount (m_PayerAccount);
        Payment.ReceiverFIID       = getFIIDByAccount (m_ReceiverAccount);
        Payment.BaseFIID           = Payment.PayerFIID;

        Payment.FuturePayerFIID    = Payment.PayerFIID;
        Payment.FutureReceiverFIID = Payment.ReceiverFIID;
        
        Payment.PayerAmount        = m_AmountDt;
        Payment.ReceiverAmount     = m_AmountKt;
        Payment.Ground             = m_Ground;


        Payment.SetPayerPI
                        ( 
                            PAYMENTS_GROUP_UNDEF,
                            getIDByBankCode (m_BankPayerCode),
                            6,
                            getSWIFTCodeById (getIDByBankCode (m_BankPayerCode)),
                            getNameByPartyID (getIDByBankCode (m_BankPayerCode)),
                            getCorrAccountByID(getIDByBankCode (m_BankPayerCode)),
                            getFIIDByAccount (m_PayerAccount),
                            getChapterByAccount (m_PayerAccount),
                            m_PayerAccount,
                            getIDByAccount(m_PayerAccount),
                            m_PayerName 
                        );


        Payment.SetReceiverPI 
                        ( 
                            PAYMENTS_GROUP_UNDEF,
                            getIDByBankCode (m_BankReceiverCode),
                            6,
                            getSWIFTCodeById (getIDByBankCode(m_BankReceiverCode)),
                            getNameByPartyID (getIDByBankCode (m_BankReceiverCode)),
                            getCorrAccountByID (getIDByBankCode(m_BankReceiverCode)),
                            getFIIDByAccount (m_ReceiverAccount),
                            getChapterByAccount (m_ReceiverAccount),
                            m_ReceiverAccount,
                            getIDByAccount (m_ReceiverAccount),
                            m_ReceiverName
                        );


        fillKZPaymentProp( Payment ); 

        if (Multy.Update () == 0)
          return Payment.DocumentID;
        end;
        return 0;
    end; /* private macro InsertMultyDoc () */

/*────────────────────────────────────────────────────────────────────────────────────────────────────────────────*/
    macro insert () /* Вставка документа */
        var retDocId; /* возвращаемый ID документа */

        if ((m_docKind == КассовыйПриход ) 
         or (m_docKind == КассовыйРасход ))
            return InsertCashDoc();
        elif (( m_docKind == ПлатежББ ) 
           or ( m_docKind == ТребованиеББ) 
           or ( m_docKind == ПлатежББВал ))
            return InsertBBPayment (); 
        
        elif ((m_docKind == ПлатежРКО) 
           or (m_docKind == ПлатежРКОВАЛ))
            return InsertPSPayment ();
        
        elif ( m_docKind == Мемордер )
            return InsertMemOrderDoc ();
        
        elif ( m_docKind == Мультивалютный )
            return InsertMultyDoc ();
        end;
    end; /* macro insert () */
end;