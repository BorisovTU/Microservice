/*
$Name:        mmpostd.mac
$Module:      МБ Кредиты
$Description: Распоряжения в бухгалтерию
*/
import RsbDataSet, globals, CTInter, OprInter, BankInter, FIInter, mmlibr, mmscan, mmlib, rsexts, or_tpl_h,fx_globals;
/*ak*/private/*~ak*/
const SET_CHAR = "X",
      ALG_PC_BASIS = 2317,
      ALG_MM_NETTING = 2116; 

VAR WordApp, WordDoc;

RECORD tick(dl_tick);
PRIVATE VAR leg = TBfile("dl_leg");
PRIVATE VAR nalog = TBfile("mmnalog.dbt");


macro par_ground(pa1)
 private var que,srez,rs51;
   srez=" ";
   que="select t_ground from dpmrmprop_dbt   where t_paymentid="+pa1;
   rs51 = LnGetRecordSet(que);
   if(rs51 != null)
     if (rs51.moveNext) 
        srez=rs51.value(0);
     end;
   end;
 return srez;
end;



PRIVATE CLASS grntRec(_FIID, _Sum)
  var FIID;
  var Sum;
  FIID = _FIID;
  Sum = _Sum;
END;

PRIVATE CLASS spgRec(_ContractNumb, _ContractDate, _ContractTime)
  var ContractNumb;
  var ContractDate;
  var ContractTime;
  ContractNumb = _ContractNumb;
  ContractDate = _ContractDate;
  ContractTime = _ContractTime;
END;  

MACRO NameAlg(type, num)
  var alg = TBfile("namealg");
  alg.KeyNum = 0;
  alg.rec.iTypeAlg = type;
  alg.rec.iNumberAlg = num;
  if( not GetEq(alg) )
    return "";
  else
    return alg.rec.szNameAlg;
  end;
END;

PRIVATE MACRO GetBossBO
  var officer = TBfile("officer"),
      ptoffice = TBfile("ptoffice"),
      persn = TBfile("persn"),
      OfficeCode = "",
      stat = 0;

  GetRegistryValue("MMARK\\ОБЩИЕ\\ПОДРАЗД_МБК", V_STRING, OfficeCode, stat);
  if (stat == 0)
    ptoffice.KeyNum = 1;
    ptoffice.rec.PartyID = {OurBank};
    ptoffice.rec.OfficeCode = OfficeCode;
    if (GetEq(ptoffice))
      officer.KeyNum = 5;
      officer.rec.PartyID = {OurBank};
      officer.rec.OfficeID = ptoffice.rec.OfficeID;
      officer.rec.IsFirstOfficePerson = SET_CHAR;
      if (GetEq(officer))
        persn.KeyNum = 0;
        persn.rec.PersonID = officer.rec.PersonID;
        if (GetEq(persn))
          return persn.rec.Name1 + " " + persn.rec.Name2 + " " + persn.rec.Name3;
        end;
      end;
    end;
  end;
  return "______________________________";
END;

PRIVATE MACRO GetBoss
  if ({FIO_Boss} == "")
    return "______________________________";
  else
    return {FIO_Boss};
  end;
END;

PRIVATE MACRO OperFIO
  var person = TBfile("person");
  person.KeyNum = 0;
  person.rec.Oper = {oper};
  if( not GetEq(person) )
    return "______________________________";
  else
    return person.rec.Name;
  end;
END;

PRIVATE MACRO PartyName(ID)
  if (ID == 0)
    return {Name_Bank};
  end;
  var party = TBfile("party");
  party.KeyNum = 0;
  party.rec.PartyID = ID;
  if( not GetEq(party) )
    return ID;
  else
    return party.rec.Name;
  end;
END;

PRIVATE MACRO Direction()
  if (isSale(GetOperationGroup(tick.DealType)))
    return "Размещаем";
  else
    return "Привлекаем";
  end;
END;

PRIVATE MACRO Product()
  if (Index(tick.TypeDoc, "L") != 0) /* Кредит */
    return "Кредит";
  else
    return "Депозит";      
  end;
END;

PRIVATE MACRO Currency(PFI)
  var fininstr = TBfile("fininstr");
  fininstr.KeyNum = 0;
  fininstr.rec.FIID = PFI;
  if( not GetEq(fininstr) )
    return "___";
  else
    return fininstr.rec.Ccy;
  end;
END;

PRIVATE MACRO GetDuration
  if (Index(tick.TypeDoc, "C") != 0) /* До востребования*/
    return "___";
  else
    return "срок "+Int(leg.rec.Duration);
  end;
END;

PRIVATE MACRO Rate
  return leg.rec.Price/(pow(10, leg.rec.Point));
END;

PRIVATE MACRO Basis
  return NameAlg(ALG_PC_BASIS, leg.rec.Basis);
END;

PRIVATE MACRO Duration
  if (Index(tick.TypeDoc, "C") != 0) /* До востребования*/
    return "до востребования";
  else
    return "";
  end;
END;

PRIVATE MACRO PrintHeader
  ARRAY TmpName;
/*0         1         2         3         4         5         6         7         8*/
 [                                                                              ];
 [                                                                              ];
 [                                                                              ];
   strsplit({Name_Bank}, TmpName, 50);
 [              В БУХГАЛТЕРИЮ ##################################################] (TmpName(0));
   if (valType(TmpName(1)) == V_STRING)   
 [                            ##################################################] (TmpName(1));
   end;
 [              РАСПОРЯЖЕНИЕ № ________________             от  ########## #####] (date, time:5:t);

END;

PRIVATE MACRO PrintOriginalDeal(Code:@variant, Date:@variant)
  var ordeal = TBfile("dl_tick"),
      oprdocs = TBfile("oprdocs"),
      oproper = TBfile("oproper");

  macro MakeDocumentID(ID)
/*    var template = "00000000000000000000000000000000000";
    return String(SubStr(template, StrLen(ID)), ID);*/
    return String(ID:34:o);//Было 35:o, при этом когда присваивали в oprdocs.rec.DocumentID  обрезался последний символ и вместо например 0000...0955 получалось 0000..095 
  end;
  macro RestoreDocumentID(DocID)
    return Int(DocID);
  end;

  oprdocs.KeyNum = 0;
  oprdocs.rec.DocKind = tick.BofficeKind;
  oprdocs.rec.DocumentID = MakeDocumentID(tick.DealID);
  oprdocs.rec.Origin = 0;
  if ((not GetGE(oprdocs)) OR
      (oprdocs.rec.DocKind != tick.BofficeKind) OR
      (oprdocs.rec.DocumentID != MakeDocumentID(tick.DealID)))
    Code = "";
    Date = "";  
    return;
  end;
  oproper.KeyNum = 0;
  oproper.rec.ID_Operation = oprdocs.rec.ID_Operation;
  if (not GetEQ(oproper))
    Code = "";
    Date = "";
    return;
  end;
  ordeal.KeyNum = 0;
  ordeal.rec.DealID = RestoreDocumentID(oproper.rec.DocumentID);
  if (not GetEQ(ordeal))
    Code = "";
    Date = "";
    return;
  end;
  
  Code = ordeal.rec.DealCode;
  Date = ordeal.rec.DealDate; 
 /*68914*/ 
/* [Пролонгируем сделку № ################ от ##########         Неттинг #########] (ordeal.rec.DealCode, ordeal.rec.DealDate, NameAlg(ALG_MM_NETTING, tick.Netting));*/
END;

PRIVATE MACRO GetSecurCur(secur)
  var fin = TBFile("fininstr.dbt");
  var leg = TBFile("dl_leg.dbt");
  fin.rec.FIID = secur.rec.FIID;
  if (GetEQ(fin))
    if (fin.rec.AvoirKind == AVOIRISSKIND_BILL)
      leg.rec.LegKind = LEG_KIND_VSBANNER;
      leg.rec.DealID = secur.rec.SecurityProperties;
      leg.rec.LegID = 0;
      if (GetEQ(leg))
        return leg.rec.PFI;
      end;
    else
      return fin.rec.FaceValueFI;
    end;
  end;
  return -1;
END;

PRIVATE MACRO GetGrnt(grntmoney,grnt)
  var fgrnt = TBFile("dl_grnt.dbt");
  var secur = TBFile("dl_secur.dbt");
  var ord = TBFile("dl_order.dbt");
  var fin = TBFile("fininstr.dbt");
  var spg;
  var stat, flag;
  var i = 0;
  var CurFIID, CurSum;
  var ins;
  fgrnt.rec.DealID = tick.DealID;
  stat = GetGE(fgrnt) AND (fgrnt.rec.DealID == tick.DealID);
  while (stat)
    ins = false;
    CurFIID = -1;
    CurSum = 0;
    i = 0;
    ord.rec.ContractID = fgrnt.rec.ContractID;
    if (GetEQ(ord))
      spg = TRsbDataSet("select t_Xld, t_RegistrDate, t_RegistrTime " +
                         "from dspground_dbt " +
                         "where t_SourceDocKind = " + ord.rec.DocKind + " and t_SourceDocID = " + ord.rec.ContractID);              
      if (spg.MoveNext())           
        grnt(grnt.Size) = spgRec(spg.Xld, Date(spg.RegistrDate), Time(spg.RegistrTime));
      end;
      
      if (ord.rec.Flag1)
      //сумму берем из grnt, валюту из первой бумаги по контакту
        ClearRecord(secur);
        secur.rec.ContractKind = DL_MMPAWN;
        secur.rec.ContractID = ord.rec.ContractID;
        if (GetGE(secur) AND (secur.rec.ContractKind == DL_MMPAWN) AND (secur.rec.ContractID == ord.rec.ContractID))
          CurSum = fgrnt.rec.Amount;
          CurFIID = GetSecurCur(secur);
        end;
      else
      //сумму собираем с каждой бумаги по сделке
        ClearRecord(secur);
        secur.rec.ContractKind = DL_IBCDOC;
        secur.rec.ContractID = tick.DealID;
        flag = GetGE(secur) AND (secur.rec.ContractKind == DL_IBCDOC) AND (secur.rec.ContractID == tick.DealID);
        while(flag)
          if (secur.rec.GeneralContract == fgrnt.rec.ContractID)
            CurSum = CurSum + secur.rec.Cost;
            CurFIID = GetSecurCur(secur);
          end;
          flag = next(secur) AND (secur.rec.ContractKind == DL_IBCDOC) AND (secur.rec.ContractID == tick.DealID);
        end;
      end;
    end;
    //если что-то насчитали, то надо поместить в массив
    if (CurSum > 0)
      while (i < grntmoney.Size)
        if (grntmoney(i).FIID == CurFIID)
          grntmoney(i).Sum = grntmoney(i).Sum + CurSum;
          ins = true;
        end;
        i = i + 1;
      end;
      if (not ins)
        grntmoney(grntmoney.Size) = grntRec(CurFIID, CurSum);
      end;
    end;
    stat = next(fgrnt) AND (fgrnt.rec.DealID == tick.DealID);
  end;
END;

PRIVATE MACRO PrintBase
  var i = 0;
  var Code = "";
  var Date = "";
  var grntmoney = TArray();
  var grnt = TArray();
  ARRAY TmpComment, TmpName;
/*0         1         2         3         4         5         6         7         8*/
 [                                                                              ];
 [---------------------------------Основание------------------------------------];
 strsplit(PartyName(tick.PartyID), TmpName, 25, 14);
 [Сделка № ################ заключена ########## ##### Контрагент ##############] (tick.DealCode, tick.DealDate, tick.DealTime:5:t, TmpName(i));
 i = i + 1;
 while (valType(TmpName(i)) == V_STRING)
 [                                                     #########################] (TmpName(i));
   i = i + 1;
 end;
 i = 0; 
 GetGrnt(grntmoney, grnt);
 if (grnt.Size)
 [договор № ###################### от ########## #####                          ] (grnt(i).ContractNumb, grnt(i).ContractDate , grnt(i).ContractTime:5:t);  
 i = 1;
 while (i<grnt.Size)
 [          ###################### от ########## #####                          ] (grnt(i).ContractNumb, grnt(i).ContractDate , grnt(i).ContractTime:5:t);     
 i = i + 1;
 end;
 i = 0;
 else 
 [договор № ______________________ от __________ _____                          ];
 end;
 [Мы ########## ####### в ##################### ### на ######## дн. с ##########] (Direction(), Product(), leg.rec.Principal:21:2, Currency(leg.rec.PFI), GetDuration():t:r, leg.rec.Start);
 [                                                                 по ##########] (leg.rec.Maturity);
 [под ########## %/год (базис ##########)                    ################   ] (Rate(), Basis(), Duration());
 /*GetGrnt(grntmoney);*/
 if (grntmoney.Size)
 [Сумма обеспечения: ##################### ###                                  ] (grntmoney(i).Sum, Currency(grntmoney(i).FIID));
 end;
 i = 1;
 while (i < grntmoney.Size)
 [                   ##################### ###                                  ] (grntmoney(i).Sum, Currency(grntmoney(i).FIID));
 i = i + 1;
 end;
 i = 0;
 [                                                                              ];
 if (tick.Flag1 == SET_CHAR) 
   PrintOriginalDeal(@Code, @Date);
 else
 Code = "";
 Date = "";
 end;     
 [Пролонгируем сделку № ################ от ##########         Неттинг #########] (Code, Date, NameAlg(ALG_MM_NETTING, tick.Netting));  
 strsplit(tick.Comment, TmpComment, 78, 62, 3);
 [Особые условия: ##############################################################] (TmpComment(i));
 i = i + 1;
 while (valType(TmpComment(i)) == V_STRING)
 [##############################################################################](TmpComment(i));
   i = i + 1;
 end;
 [------------------------------------------------------------------------------];

END;

// Категория качества сделки (ККС)
PRIVATE MACRO PrintQualityCategory(CarryDate)
var ККС = "-", iQC = ПолучитьКатегориюКачества( tick.DealID, CarryDate );
 if(iQC > 0) ККС = string(iQC); end;
 [Категория качества ##                                                               ] (ККС);
 [------------------------------------------------------------------------------];
END;

PRIVATE MACRO PrintCarries(opID, stID, CarryDate)
 RECORD Carry("acctrn");
 var stat = 0;
 ClearRecord(Carry);
 stat = GetDocsByOperStep(Carry, opID, stID);
 if (stat)
 [Проведите датой ##########                                                    ](CarryDate);
 [+----------------------------------------------------------------------------+];
 [|   Сумма проводки    |       Дебет счета         |      Кредит счета        |];
 [+----------------------------------------------------------------------------+];
 end;
 while (stat)
 [|#####################|###########################|##########################|](Carry.Sum_Payer:21:2:c, Carry.Account_Payer:c, Carry.Account_Receiver:c);
 stat = GetDocsByOperStep(Carry, opID, stID);
 end;
END;

PRIVATE MACRO PrintFooter
/*0         1         2         3         4         5         6         7         8*/
 [                                                                              ];
 [Руководитель _____________ ##############################                     ] (GetBoss():30:t);
 [               (Подпись)   Ф.И.О.                                             ];
 [                                                                              ];
 [Начальник БО _____________ ##############################                     ] (GetBossBO());
 [               (Подпись)   Ф.И.О.                                             ];
 [                                                                              ];
 [Исполнитель  _____________ ##############################                     ] (OperFIO());
 [               (Подпись)   Ф.И.О.                                             ];
 [                                                                              ];
 [                                                                              ];
 [ОТМЕТКА БУХГАЛТЕРИИ                                                           ];
 [                                                                              ];
 [Исполнитель  _____________ ______________________________ ____________________];
 [               (Подпись)   Ф.И.О.                         дата и время приема ];
 [                                                                              ];
END;

MACRO CreateFileName
    var hour,min,sec, Path,
        txtRegistryPath = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR";

    if(MM_SetRegistryPath(txtRegistryPath, Path))
       return "";
    end;

    // получить полный путь
    Path = MMCMT_processPath(Path) + "\\";
    TimeSplit (Time,hour,min,sec);
    return String(Path,{oper},hour,min,sec,".txt");
END;

MACRO PrintStepDoc (
                    ID_Operation,  /* Номер экземпляра операции */                      
                    Kind_Operation,/* Вид операции */               
                    KindStep,      /* Вид шага операции */
                    ID_Step)
                    
   var SaveFileName = "",
        CarryDate = {curdate},
      stat = 0;

   RECORD Carry("acctrn");
  file out() txt;                 

  /*проверим, а есть ли проводки и получим дату*/ 
  ClearRecord(Carry);
  stat = GetDocsByOperStep(Carry, ID_Operation, ID_Step);
  if (stat)
       CarryDate = Carry.Date_Carry;
  else
    return;
  end;  

  leg.KeyNum = 0;
  leg.rec.LegKind = LEG_KIND_DL_TICK;
  leg.rec.DealID = tick.DealID;
  leg.rec.LegID = 1;
  if( not GetEq(leg) )
    mm_error( "Не найден транш сделки ", tick.DealCode );
    return 1;
  end;

  if (PrintOrderOutFileName == "")
    PrintOrderOutFileName = GetTxtFileName("mmpostd");
    SaveFileName = SetOutput(PrintOrderOutFileName, FALSE);
  else
    SaveFileName = SetOutput(PrintOrderOutFileName, TRUE);
  end;

  PrintHeader();
  PrintBase();
    PrintQualityCategory(CarryDate);
    PrintCarries(ID_Operation, ID_Step, CarryDate);
  PrintFooter();
  SetOutput(CreateFileName(), FALSE);
  if (not IsGroupAction)    
    open(out, PrintOrderOutFileName);
    ViewFile(out);
  end;
  SetOutput(SaveFileName);

END;  

PRIVATE MACRO OpenNalogRasp
    const RegKey   :string = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEMPLSDIR";
    const TempName :string = "MbkNalogRasp.dot";
    var DirName_Template   = "";
    var TempStr            = "";
    var TermFullDotFileName = "";
    var FullDotFileName    = "";

    GetRegistryValue( RegKey, V_STRING, DirName_Template, "" );
    if( NOT DirName_Template )
       MsgBox( "Ошибка!|Не задан путь до папки с шаблонами." );
       return false;
    end;
    TempStr = FindPath(toANSI(TempName), DirName_Template);
    if( NOT TempStr )
       MsgBox( "В путях <",DirName_Template, ">|не найден шаблон|<"+TempName+">!|",
          "Поместите шаблон в указанный путь и повторите операцию." );
       return false;
    end;
    
    if (isStandAlone())
        FullDotFileName = MergeFile( GetCurDir(), TempStr );
    else
        MakeDir( "$"+NameTemplateDirTerm );
        TermFullDotFileName = NameTemplateDirTerm+"\\"+TempName;
        if( NOT CheckFilesEqual( TempStr, "$"+ TermFullDotFileName ) )
            CopyFile(TempStr, "$"+ TermFullDotFileName);
        end;
        FullDotFileName = GetCurDir(true)+"\\"+ TermFullDotFileName;
    end;
    
    var startAX;  
    if (isStandAlone())
        WordApp = ActiveX("Word.Application", null, true);
    else
        if (startAX == null)
            startAX = CreateObject("rsax", "TRsAxServer", "MmAxServer", isStandalone());
        end;
        WordApp = startAX.CreateComObject("Word.Application");
    end;
    
    if ( WordApp )
        WordDoc = WordApp.Documents.Add( FullDotFileName );
    else 
        MsgBox("Ошибка при открытии MS Word!");
        return false;
    end;
    return true;
END;

PRIVATE MACRO PrintNalogHeader
    WordDoc.Bookmarks("A01").Range.Text = {Name_Bank};
    WordDoc.Bookmarks("A02").Range.Text = string(nalog.rec.datestep);
END;

PRIVATE MACRO PrintNalogBody(ID_Operation, ID_Step)
    VAR cmd = "SELECT deals.t_dealcode, deals.t_partyname, deals.t_genagr, " + 
              " NVL(to_char(genagr.t_start, 'dd.mm.yyyy'), ' ') as genagrstart, " +
              " fininstr.t_name, fininstr.t_ccy, " +
              " fininstr.t_iso_number, deals.t_principal, deals.t_start as dealstart, " + 
              " nalog.t_datestep, nalog.t_sum, nalog.t_rate_nalog, nalog.t_sum_nalog " + 
              " FROM dmmnalog_dbt nalog, dmm_deals_dbt deals, dfininstr_dbt fininstr, " +
              " ddl_leg_dbt leg, ddl_tick_dbt tick LEFT JOIN ddl_genagr_dbt genagr " +
              " ON genagr.t_genagrid = tick.t_genagrid " +
              " WHERE deals.t_dealid = leg.t_dealid AND " + 
              " deals.t_dealid = tick.t_dealid AND " +
              " deals.t_dealid = nalog.t_dealid AND " +
              " fininstr.t_fiid = leg.t_pfi AND " +
              " nalog.t_id_operation = ? AND " + 
              " nalog.t_id_step = ? ";
    VAR RSC = RSDCommand(cmd);
    RSC.addparam("", RSDBP_IN, ID_Operation);
    RSC.addparam("", RSDBP_IN, ID_Step);
    VAR RS = TRSBDataSet(RSC);
    if (not RS.movenext) return 1; end;
    VAR genagr = RS.t_genagr;
    if (not (RS.genagrstart==" ")) genagr = "№"+genagr; end;
    
    VAR ratecmd = "SELECT rateval.t_ratedate, rateval.t_rate " +
		    " from dprcrateval_dbt rateval,  dprcrate_dbt rate, " +
  		    " dprccontract_dbt contract, dmm_deals_dbt deals, " +
  		    " dmmnalog_dbt nalog " +
		    " where contract.t_contracttype = 7  " +
  		    " AND contract.t_objecttype = 103 " + 
 		    " AND contract.t_objectid = deals.t_dealcode " + 
  		    " AND rate.t_rateid = contract.t_rateid " +
		    " AND rateval.t_rateid = contract.t_rateid " +
		    " AND deals.t_dealid = nalog.t_dealid " +
		    " AND nalog.t_id_operation = ? " +
		    " AND nalog.t_id_step = ? " +
		    " AND rateval.t_ratedate <= ? " + 
		    " order by t_ratedate desc";
    VAR RateCom = RSDCOMMAND(ratecmd);
    RateCom.addparam("", RSDBP_IN, ID_Operation);
    RateCom.addparam("", RSDBP_IN, ID_Step);
    RateCom.addparam("", RSDBP_IN, {curdate});
    var RateSet = TRSBDataSet(RateCom);
    if (not RateSet.movenext) return 1; end;
    
    WordDoc.Bookmarks("A03").Range.Text = RS.t_dealcode;
    WordDoc.Bookmarks("A04").Range.Text = RS.t_partyname;
    WordDoc.Bookmarks("A05").Range.Text = genagr+" "+RS.genagrstart;
    WordDoc.Bookmarks("A06").Range.Text = "МБК на срок, размещение";
    WordDoc.Bookmarks("A071").Range.Text = RS.t_name;
    WordDoc.Bookmarks("A072").Range.Text = RS.t_ccy;
    WordDoc.Bookmarks("A073").Range.Text = RS.t_iso_number;
    WordDoc.Bookmarks("A08").Range.Text = string(RS.t_principal:0:2:a);
    WordDoc.Bookmarks("A09").Range.Text = RateSet.rate;
    WordDoc.Bookmarks("A10").Range.Text = RS.dealstart;
    WordDoc.Bookmarks("A11").Range.Text = RS.t_datestep;
    WordDoc.Bookmarks("A12").Range.Text = string(RS.t_sum:0:2:a);
    WordDoc.Bookmarks("A13").Range.Text = RS.t_rate_nalog;
    WordDoc.Bookmarks("A14").Range.Text = string(RS.t_sum_nalog:0:2:a);
    WordDoc.Bookmarks("A15").Range.Text = string(RS.t_sum-RS.t_sum_nalog:0:2:a);
    return 0;
END;

PRIVATE MACRO PrintNalogFooter
    WordDoc.Bookmarks("A16").Range.Text = OperFIO();
END;

MACRO PrintNalogDoc(ID_Operation, ID_Step)
    nalog.rec.id_operation = id_operation;
    nalog.rec.id_step = id_step;
    if (nalog.geteq())
        if ( openNalogRasp )
            PrintNalogHeader;
            VAR err = PrintNalogBody(ID_Operation, ID_Step);
            PrintNalogFooter;
            
            if (not (err==0))
                mm_error("Ошибка при формировании распоряжения на удержание налога на доходы");
                return 1;
            end;
            
            if (not WordDoc.Application.Visible) 
                WordDoc.Application.Visible = true;
            end;
            var WordName = WordDoc.Application.Name;
            if (WordDoc.Application.Tasks.Exists(WordName))
                WordDoc.Application.Tasks(WordName).Activate(false);
            else
                WordDoc.Application.Activate;
            end;

            return 0; 
        else 
            mm_error("Ошибка при формировании распоряжения на удержание налога на доходы");
            return 1;
        end;
    end;
    return 1;
END;

/* Макрос печати 
*/
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */
               
    var oproper = TBfile("oproper.dbt"),
         dl_tick = TBfile("dl_tick.dbt") ;
    
    oproper.rec.ID_Operation = ID_Operation;
    
    if (not oproper.GetEq )
      mm_error( "Не найдена сделка ", ID_Operation );
      return 1;
    end;  

    ClearRecord( tick );
    
    if( (not RestoreFromUniID(oproper.rec.DocumentID, dl_tick, OBJTYPE_MMARKDEAL,oproper.rec.DocKind)) OR       
          (not GetEQ(dl_tick)) )    
      mm_error( "Не найдена сделка ");
      return 1;
    end;    

    Copy (tick, dl_tick);

//    PrintStepDoc (ID_Operation, Kind_Operation, KindStep, ID_Step);
 //   PrintNalogDoc (ID_Operation, ID_Step);

    exit(1);

END;
              
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */  



  private var que,rs1,rs9,rs,idsd,acctrnid,kod,paymentid=0,kod2,ii,pur=11,dockind,kod322,acc10=" ",ground2=" ";
  SetBuff(tick, FirstDoc );

      que="select b.t_paymentid, c.t_purpose,c.t_dockind,c.t_userfield4, c.t_receiveraccount from doprdocs_dbt a, dpmdocs_dbt b, dpmpaym_dbt c  where c.t_paymentid=b.t_paymentid and  a.t_id_operation="+ID_Operation+" and a.t_id_step="+ID_Step+" and a.t_acctrnid > 0  and b.t_acctrnid=a.t_acctrnid ";
      rs9 = LnGetRecordSet(que);
      if(rs9 != null)
        if (rs9.moveNext) 
           paymentid=rs9.value(0);
           pur=rs9.value(1);
           dockind=rs9.value(2);
           kod322=rs9.value(3);
           acc10=substr(rs9.value(4),1,5);
           ground2=par_ground(paymentid);
        end;
      end;



     if (( CommitOrRollback == 1 ) and  (tick.partyid != _BANK_RF) )
        if ({oper} > 1 )
          que="update dpmpaym_dbt set t_userfield3='1' where t_paymentid="+paymentid;
           // rs = LnGetRecordset(que);
        end;

      que="update dpmpaym_dbt set t_numberpack =60 ,t_userfield4=chr(1) where t_dockind !=322 and t_paymentid="+paymentid;
      rs = LnGetRecordset(que);
      que="select a.t_acctrnid from doprdocs_dbt a, dacctrn_dbt b  where a.t_id_operation="+ID_Operation+" and a.t_id_step="+ID_Step+" and a.t_acctrnid > 0 ";
      que=que+" and b.t_acctrnid=a.t_acctrnid  and b.t_ground like '%Налог%' ";
      rs1 = LnGetRecordSet(que);
      if(rs1 != null)
         while(rs1.moveNext())
               acctrnid=rs1.value(0);
               que="update dpmdocs_dbt  set t_acctrnid=0 where t_acctrnid="+rs1.value(0)+" and t_paymentid="+paymentid;
               rs = LnGetRecordSet(que);
         end;
       end;


     end;

   if (( CommitOrRollback == 1 ) and  (tick.dealtype == 12310) )
      que="update dpmpaym_dbt set t_numberpack = 63  where t_paymentid="+paymentid;
      rs = LnGetRecordset(que);
   end;
   if (( CommitOrRollback == 1 ) and  (tick.dealtype == 12335) )
      que="update dpmpaym_dbt set t_numberpack = 63  where t_paymentid="+paymentid;
      rs = LnGetRecordset(que);
   end;


 
  return 0;

END;

