/*-------------------------------------------------------------------------

        Имя файла       : PartyInfo.mac 

        Описание        : Информация о субъектах экономики

        Программист     : Селезнёв Роман

        Создан          : 19.03.2008


-------------------------------------------------------------------------*/

import oralib, likepy, dlgCommon, PTInter, CTInter, CommonInfo, Globals, dlgCommon;


//
// Получить имя операциониста по его Id
// Данные берутся из dPerson_dbt
//
macro piGetOperName(operId)
   var sql = " select t_name from dPerson_dbt where t_oper=:oper ";
   var params = MakeArray(SQLParam("oper", operId)); 

   var rs = ExecSQLSelect(sql, params, false);

   if(rs.MoveNext()) 
      return rs.value(0);
   end; 

   return "";
end;


//
// Поиск субъекта по Id
//
private macro GetPartyName(partyId)
   var code = "", name = "";

   var sql = "select t_shortName, t_name from dParty_dbt where t_partyId=:party";
   var params = MakeArray(SQLParam("party", partyId)); 

   var rs = ExecSQLSelect(sql, params, false);
 
   if(rs.MoveNext())
      return rs.value(1);
   end;

   return "";
end;


/******************************** Коды у субъектов экономики ******************************/

//
// Получить внутренний идентификатор PartyId по внешнему коду субъекта
// Аналогична ПолучитьКодСубъекта из PTInter в части 1го варианта вызова, однако исключает неоднозначное толкование типа параметров
// Плюс снимаются проблемы вызова из контекста функции пользователя ГКБО (SCR #120183, SCR #120289)  
// code -- внешний код субъекта, codeKind -- вид кода субъекта. В случае неудачной отработки возвращает -1.
// 
macro GetPartyIdByCode(code:String, codeKind:Integer):Integer
   var sql = " select t_objectId from dObjCode_dbt obj "
           + " where obj.t_code = :code and obj.t_codeKind = :codeKind and obj.t_state = 0 " ;

   var params = MakeArray(SQLParam("code", code), SQLParam("codeKind", codeKind)); 

   var rs = ExecSQLSelect(sql, params, false);

   if(rs.MoveNext()) 
      return rs.value(0);
   end; 

   return -1;
end;

//
// Получить внешний код субъекта по внутреннему идентификатору PartyId  
// Аналогична ПолучитьКодСубъекта из PTInter в части 2го варианта вызова, однако исключает неоднозначное толкование типа параметров
// Плюс снимаются проблемы вызова из контекста функции пользователя ГКБО (SCR #120183, SCR #120289)  
// party -- внутренний идентификатор субъекта, codeKind -- вид кода субъекта. В случае неудачной отработки возвращает пустую строку.
// 
macro GetCodeByPartyId(party:Integer, codeKind:Integer):String
   var sql = " select obj.t_code from dObjCode_dbt obj "
           + " where t_objectId = :pt and obj.t_codeKind = :codeKind " ;
      
   var params = MakeArray(SQLParam("pt", party), SQLParam("codeKind", codeKind)); 
   
   var rs = ExecSQLSelect(sql, params, false);

   if(rs.MoveNext())   
      return rs.value(0);
   end; 

   return "";
end;


//
// Получить название кода объекта по типу объекта и идентификатору вида кода объекта
// objType -- тип объекта (константы OBJTYPE_*), codeKind -- вид кодов субъектов (константы PTCK_*)
// name -- наименование вида кода (OUT), shortName -- краткое наименование (OUT), def -- описание вида кода (OUT)
//
macro GetObjCodeName(objType:Integer, codeKind:Integer, name:String, shortName:String, def:String)
   var sql = " select t_name, t_shortName, t_definition from dObjKCode_dbt where t_objectType=:otype and t_codeKind=:ckind ";

   var params = MakeArray(SQLParam("otype", objType), SQLParam("ckind", codeKind)); 
   
   var rs = ExecSQLSelect(sql, params, false);

   if(rs.MoveNext())   
      SetParm(2, rs.value(0));
      SetParm(3, rs.value(1));
      SetParm(4, rs.value(2));
      return true;
   end; 

   return false;
end; 
 

/******************************** Список субъектов экономики ******************************/


private var g_selectedParty = 0; // Id искомого субъекта в скроллинге (или 0, если искать не надо)

//
// Процедура обработки событий скроллинга субъектов экономики
//
private macro ListPartyScrollEvProc(rs, cmd, id, key)
   if (cmd == DLG_INIT) 
      rs.MoveFirst();          

      // Прокрутка до нужной записи
      if (g_selectedParty > 0)  // Надо найти запись
         while ( (Int(rs.value ("ptId")) != g_selectedParty) And (Not rs.EOF) )  
            rs.Move(1, RELATIVE);
         end;
      end;
      
      GoToScroll(rs, true);
   elif (DLG_KEY == cmd) 
      if(DLG_KEY_ENTER == Key) 
         return CM_SELECT;
      end;
   end;

   return CM_DEFAULT;
end;
                     


//
// Построение списка субъектов экономики с возможностью выбора субъекта
// Аналогична ListPT из PTInter, однако снимаются проблемы вызова из контекста функции пользователя ГКБО (SCR #120289)  
// плюс имеется возможность выбирать субъектов по заданной категории (группе) и значению (см. справку по GetMainObjAttr)
// partyId -- id выбранного субъекта (OUT), code -- выбранный код (OUT), ptList -- вид списка субъектов (константы PTLIST_*),
// codeKind -- вид кодов субъектов (константы PTCK_*), selectedPartyId -- id выбранного ранее субъекта;
// [ GroupId -- группа, к которой принадлежит объект (IN); AttrId -- идентификатор признака (IN) ]
// Возвращает true в случае успешного выбора клавишей Enter субъекта из списка или false -- при выходе по Escape
//
macro PartyList(partyId:Integer, code:String, ptList:Integer, codeKind:Integer, selectedPartyId:Integer, GroupId:Integer, AttrId:Integer)   


   if(V_UNDEF == ValType(selectedPartyId))
      g_selectedParty = selectedPartyId = 0;    // Позиционирование на 1й записи
   else
      g_selectedParty = selectedPartyId;
   end;
   
   var add = "";
   if((V_UNDEF != ValType(GroupId)) AND (V_UNDEF != ValType(AttrId)))   // Категории субъекта заданы явно и полностью
      add = " inner join dObjAtCor_dbt oc "
          + " on oc.t_objectType=3 and oc.t_groupId=:groupId and oc.t_attrId=:attrId and oc.t_general='X' and oc.t_validFromDate<=:curDate and oc.t_validToDate>:curDate "
          + " and oc.t_object = lpad(p.t_partyId, 10, '0') ";

   else
      GroupId = AttrId = -1;    
   end;
 
   add = add + "order by t_shortName";


   var kindSql = " select DISTINCT c.t_code code, p.t_name part, p.t_partyId ptId, p.t_shortName sh from dParty_dbt p "
          +"     inner join dPartyOwn_dbt o " 
          +"     on p.t_partyId = o.t_partyId and o.t_partyKind=:ptlist and p.t_locked = chr(0)" 
          +"         inner join dPtKind_dbt k " 
          +"         on o.t_partyKind = k.t_partyKind " 
          +"             left outer join dObjCode_dbt c " 
          +"             on c.t_objectId = p.t_partyId and c.t_objectType=3 and c.t_codeKind=:codeKind " 
          + add ;

           
   var sql, params;
   
   // Параметр ptList не задействован полностью
   if(PTLIST_ALLBANK == ptList)   
     // Можно было бы использовать запрос kindSql при ptList == 2, но этот вариант экономичней
//     sql = " select c.t_code code, p.t_name part, p.t_partyId ptId, p.t_shortName sh from dParty_dbt p "
//          +"     left outer join dObjCode_dbt c " 
//          +"     on c.t_objectId = p.t_partyId and c.t_objectType=3 and c.t_codeKind=:codeKind "
//          + add ;    
     sql = kindSql;
     params = MakeArray(SQLParam("ptlist", 2), SQLParam("codeKind", codeKind)); 
   elif(PTLIST_ALLCLIENT == ptList)
     sql = kindSql;
     params = MakeArray(SQLParam("ptlist", 1), SQLParam("codeKind", codeKind)); 

   elif(PTLIST_ALLCORRESP == ptList)
     sql = kindSql;
     params = MakeArray(SQLParam("ptlist", 11), SQLParam("codeKind", codeKind)); 

   elif(PTLIST_ALLSWIFT == ptList)
     sql = kindSql;
     params = MakeArray(SQLParam("ptlist", 10), SQLParam("codeKind", codeKind)); 

   elif(PTLIST_CONTR == ptList)
     sql = kindSql;
     params = MakeArray(SQLParam("ptlist", 7), SQLParam("codeKind", codeKind)); 

   elif(PTLIST_ISSUER == ptList)
     sql = kindSql;
     params = MakeArray(SQLParam("ptlist", 5), SQLParam("codeKind", codeKind)); 

   elif(PTLIST_PENSFOND == ptList)
     sql = kindSql;
     params = MakeArray(SQLParam("ptlist", 8), SQLParam("codeKind", codeKind)); 

   elif(PTLIST_REGHOLDER == ptList)
     sql = kindSql;
     params = MakeArray(SQLParam("ptlist", 12), SQLParam("codeKind", codeKind)); 

   elif(PTLIST_DEPOSITORY == ptList)
     sql = kindSql;
     params = MakeArray(SQLParam("ptlist", 4), SQLParam("codeKind", codeKind)); 

   elif(PTLIST_MEDICAL == ptList)
     sql = kindSql;
     params = MakeArray(SQLParam("ptlist", 9), SQLParam("codeKind", codeKind)); 

   elif(PTLIST_TAXINSTITUTE  == ptList)
     sql = kindSql;
     params = MakeArray(SQLParam("ptlist", 6), SQLParam("codeKind", codeKind)); 

   elif(PTLIST_MARKETPLASE  == ptList)
     sql = kindSql;
     params = MakeArray(SQLParam("ptlist", 3), SQLParam("codeKind", codeKind)); 

   else
      MsgBox("Ошибочный параметр ptList");
      return false;
   end;
 

 
   // Вывод скроллинга на экран
   
   if("" != add)        // Требуется фильтрация данных по категориям субъектов
      params(params.Size) = SQLParam("groupId", GroupId);
      params(params.Size) = SQLParam("attrId", AttrId);
      params(params.Size) = SQLParam("curDate", {CurDate});
   end;

   var rs = ExecSQLSelect(sql, params, false, RSDVAL_CLIENT, RSDVAL_STATIC); 

   var col = TArray();
 
   var codeName;
 
   if(not GetObjCodeName(OBJTYPE_PARTY, codeKind, codeName))
      MsgBox("Ошибка получения наименования вида кода субъекта");
      return false;
   end;
 

   AddCol (col, 0, "code",  codeName, 12);
   AddCol (col, 1, "part",  "Наименование", 55); 
   AddCol (col, 2, "sh",    "Краткое наименование", 15);
 
   if(RunScroll (rs, 3, col, "ListPartyScroll", @ListPartyScrollEvProc, "Выбор субъекта", 
        "~ESC~ выход, ~Enter~ выбор", true, 0, 1, 100) )
                         
      SetParm(0, rs.value("ptId"));
      SetParm(1, rs.value("code"));
      return true;
   end;     

   return false;
end;




/********************************** Виды субъекта экономики *******************************/

//
// Определение, является ли субъект экономики с внутренним идентификатором partyId субъектом вида partyKind
// partyId -- Id субъекта, partykind -- вид субъекта (константы PTK_*)
// Возвращает true в случае положительного ответа или false -- в случае отрицательного
//
macro IsSubjectKind(partyId:Integer, partyKind:Integer):Bool
   return ВидСубъекта(partyId, partyKind);
end;


//
// Задание субъекту экономики с внутренним идентификатором partyId вида субъекта partyKind
// partyId -- Id субъекта, partykind -- вид субъекта (константы PTK_*)
// Возвращает true в случае удачного выполнения или false -- в случае ошибки
//
macro SetSubjectKind(partyId:Integer, partyKind:Integer):Bool
   var sql = " insert into dPartyOwn_dbt (t_partyId, t_partyKind, t_superior, t_subKind, t_branch, t_numSession) " 
           + " values (:partyId, :partyKind, 0, 0, 0, 0) ";

   var params = MakeArray(SQLParam("partyId", partyId), SQLParam("partyKind", partyKind));
   var rs = ExecSQL(sql, params, false);
   if(null == rs)  
      MsgBox("Ошибка при задании вида субъекта");
      return false;
   end; 

   return true;
end;

//
// Удаление у субъекта экономики с внутренним идентификатором partyId вида субъекта partyKind
// partyId -- Id субъекта, partykind -- вид субъекта (константы PTK_*)
// Возвращает true в случае удачного выполнения или false -- в случае ошибки
//
macro ResetSubjectKind(partyId:Integer, partyKind:Integer):Bool
   var sql = " delete from dPartyOwn_dbt where t_partyId=:partyId and t_partyKind=:partyKind ";

   var params = MakeArray(SQLParam("partyId", partyId), SQLParam("partyKind", partyKind));
   var rs = ExecSQL(sql, params, false);
   if(null == rs) 
      MsgBox("Ошибка при удалении вида субъекта");
      return false;
   end; 

   return true;
end;


/************************************ Категории контрагента *******************************/

//
// Получить название категории контрагента (его типа) по его Id
// contragentCatId -- Id категории контрагента. Возвращает название категории контрагента или пустую строку, если портфель не найден 
//
macro GetContragentCatName(contragentCatId:Integer):String
   var sql = "select t_Name from dObjAttr_dbt where t_objectType=3 and t_groupId=9 and t_attrId=:attrId "; 
   var params = MakeArray(SQLParam("attrId", contragentCatId)); 
   var rs = ExecSQLSelect(sql, params, false, RSDVAL_CLIENT, RSDVAL_STATIC); 

   if(rs.MoveNext())                
      return rs.value(0);
   end;

   return "";
end;
             
//
// Получить полное название категории контрагента (его типа) по его Id
// contragentCatId -- Id категории контрагента. Возвращает название категории контрагента или пустую строку, если портфель не найден 
//
macro GetContragentCatFullName(contragentCatId:Integer):String
   var sql = "select t_fullName from dObjAttr_dbt where t_objectType=3 and t_groupId=9 and t_attrId=:attrId "; 
   var params = MakeArray(SQLParam("attrId", contragentCatId)); 
   var rs = ExecSQLSelect(sql, params, false, RSDVAL_CLIENT, RSDVAL_STATIC); 

   if(rs.MoveNext())                
      return rs.value(0);
   end;

   return "";
end;


//
// Получить категорию контрагента
// partyId -- Id субъекта
// Возвращает номер категории портфеля или 0, если категория не была задана
//
macro GetContragentCat(partyId:Integer):Integer
   var CatErr:Integer = 0;
   
   var uId:Object; 
   file party ( party );                                   
   SetBuff(party, uId); 
   party.partyId = partyId; 
   
   if(not getEQ(party) )
      return "";
   end;
 
   var AttrId = 0, Code = 0, Num = 0;

/*
   GetMainObjAttr( OBJTYPE_PARTY,
                   UniID(party, OBJTYPE_PARTY),
                   9,
                   AttrId, Code, Num
                 );

 
   if( CatErr != 0 )      
      MsgBox("Ошибка получения категории контрагента");
      return 0;
   end;
*/
   AttrId = GetObjAttr( OBJTYPE_PARTY,
                        UniID(party, OBJTYPE_PARTY),
                        9 
                      );

   return AttrId;
end;

//
// Проверка на задание категории у контрагента
// partyId -- Id субъекта
// Возвращает true, если у контрагента задана категория или false -- если категория не была задана
//
macro IsContragentCatExist(partyId:Integer):Bool
   var sql = "select * from dObjAtCor_dbt where t_objectType=3 and t_groupId=9 and t_general='X' and t_object = LPAD(:partyId, 10, '0') "; 
   var params = MakeArray(SQLParam("partyId", partyId)); 
   var rs = ExecSQLSelect(sql, params, false, RSDVAL_CLIENT, RSDVAL_STATIC); 

   if(rs.MoveNext())                
      return true;
   end;

   return false;
end;


//
// Удалить категорию контрагента
// partyId -- Id субъекта (контрагента)
//
macro ResetContragentCat(partyId:Integer):Bool
   var CatErr:Integer = 0;  

   var uId:Object; 
   file party ( party );                                   
   SetBuff(party, uId); 
   party.partyId = partyId; 

   if(not getEQ(party) )
      return "";
   end;

/*   if( not DisconnectObjAttr( OBJTYPE_PARTY, 
                              UniID(party, OBJTYPE_PARTY),
                              9 )
     )

 
      MsgBox("Ошибка удаления категории контрагента");
      return false;
   end;        
*/
   if( not ResetObjAttr( OBJTYPE_PARTY, 
                         UniID(party, OBJTYPE_PARTY),
                         9 )
      )

      MsgBox("Ошибка удаления категории контрагента");
      return false;
   end;

   return true;
end;

//
// Задать категорию контрагента
// partyId -- Id субъекта (контрагента), catId -- Id категории (dObjAttr_dbt.t_attrId)
// Возвращает true в случае успешного выполнения и false -- в случае ошибки
// 
macro SetContragentCat(partyId:Integer, catId:Integer):Bool
   var CatErr:Integer = 0;  

   var uId:Object; 
   file party ( party );                                   
   SetBuff(party, uId); 
   party.partyId = partyId; 

   if(not getEQ(party) )
      return "";
   end;

/*
   if(not ResetContragentCat(partyId)) // Удаление имеющейся категории (категория м.б. только одна)
      return false;
   end;

   if( (not ConnectObjAttr( CatErr, OBJTYPE_PARTY,
                            UniID(party, OBJTYPE_PARTY),
                            9,
                            catId
                          )    
       ) OR 
       ( CatErr != 0 )
     )
      MsgBox("Ошибка задания категории контрагента");
      return false;
   end;
*/

   if(not SetObjAttr( OBJTYPE_PARTY,
                      UniID(party, OBJTYPE_PARTY),
                      9,
                      catId
                    )
     )
      MsgBox("Ошибка задания категории контрагента");
      return false;
   end;

   return true;
end;







