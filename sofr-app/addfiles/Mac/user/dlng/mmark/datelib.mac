/*-------------------------------------------------------------------------

  Имя файла : DateLib.mac 

  Описание  : Библиотека по работе с датами

  Программист : Селезнёв Роман

  Создан    : 16.11.2007


-------------------------------------------------------------------------*/
import Календарь, Globals, StringLib;

private var aFullDayName = TArray (7, 0),   // Полное название дня недели
            aShortDayName= TArray (7, 0);   // Краткое название дня недели

aFullDayName [0] = "Понедельник";        aShortDayName [0] = "Пн";
aFullDayName [1] = "Вторник";            aShortDayName [1] = "Вт";
aFullDayName [2] = "Среда";              aShortDayName [2] = "Ср";
aFullDayName [3] = "Четверг";            aShortDayName [3] = "Чт";
aFullDayName [4] = "Пятница";            aShortDayName [4] = "Пт";
aFullDayName [5] = "Суббота";            aShortDayName [5] = "Сб";
aFullDayName [6] = "Воскресенье";        aShortDayName [6] = "Вс";


private var aShortMonName = TArray (12, 0);  // Краткое наименование месяца
aShortMonName [0] = "Янв";   aShortMonName [6]  = "Июл";
aShortMonName [1] = "Фев";   aShortMonName [7]  = "Авг";
aShortMonName [2] = "Мар";   aShortMonName [8]  = "Сен";
aShortMonName [3] = "Апр";   aShortMonName [9]  = "Окт";
aShortMonName [4] = "Май";   aShortMonName [10] = "Ноя";
aShortMonName [5] = "Июн";   aShortMonName [11] = "Дек";

private var aDaysInMonth = TArray (12, 0); // Кол-во дней в месяце (для февраля задано 28)
aDaysInMonth [0] = 31; /* Янв */   aDaysInMonth [6]  = 31; /* Июл */
aDaysInMonth [1] = 28; /* Фев */   aDaysInMonth [7]  = 31; /* Авг */
aDaysInMonth [2] = 31; /* Мар */   aDaysInMonth [8]  = 30; /* Сен */
aDaysInMonth [3] = 30; /* Апр */   aDaysInMonth [9]  = 31; /* Окт */
aDaysInMonth [4] = 31; /* Май */   aDaysInMonth [10] = 30; /* Ноя */
aDaysInMonth [5] = 30; /* Июн */   aDaysInMonth [11] = 31; /* Дек */


// Получить год из даты
Macro ДатаГод ( dt:Date ):Integer
    var yyyy;
    DateSplit (dt, NULL, NULL, yyyy);   
    return yyyy;
End;
//-----------------------------------------------------------------------------------------

// Получить месяц из даты
Macro ДатаМесяц ( dt:Date ):Integer
    var mm;
    DateSplit (dt, NULL, mm);   
    return mm;
End;
//-----------------------------------------------------------------------------------------

// Получить число из даты
Macro ДатаЧисло ( dt:Date ):Integer
    var dd;
    DateSplit (dt, dd);   
    return dd;
End;
//-----------------------------------------------------------------------------------------

// Получить номер дня недели  по дате (1 - понедельник, 2 - вторник,..., 7 - воскресенье)
// (актуально для любой даты григорианского календаря позднее 1583 года)
Macro ПолучитьНомерДняНедели ( dt:Date ):Integer
    var dd, mm, yyyy;   
    DateSplit (dt, dd, mm, yyyy);
    var a = int ((14 - mm) / 12);
    var y = yyyy - a;
    var m = mm + 12*a - 2;

    var result = mod (( 7000 + (dd + y + Int (y/4) - int (y/100) + int (y/400) + int ((31*m)/12))), 7);

    if ( result == 0 ) // Воскресенье становится нулём
        return 7;
    end;
    return result;
End;
//-----------------------------------------------------------------------------------------

// Получить номер дня в году, заданного датой
Macro ПолучитьНомерДняГода ( dt:Date ):Integer
    var dd, yyyy;   
    DateSplit (dt, dd, NULL, yyyy);
    return (dt - date (1, 1,yyyy) + 1 );
End;
//-----------------------------------------------------------------------------------------

// Получить дату по номеру дня в году. Если 2й параметр не задан, считается текущий год
Macro ПолучитьДатуПоНомеруДняГода ( num:Integer, yyyy:Integer ):Date
    var year;

    if ( yyyy == NULL )
        year = DateSplit (date (), null, null, year);
    else
        year = yyyy;
    end;

    return date (1, 1, year) + num - 1;
End;
//-----------------------------------------------------------------------------------------

// Получить номер квартала по дате, относящейся к искомому кварталу
Macro ПолучитьНомерКвартала ( dt:Date ):Integer
    var mm;
    DateSplit (dt, NULL, mm);

    if ( mm <= 3 ) 
        return 1;
    elif ( mm <= 6 )
        return 2;
    elif ( mm <= 9 )
        return 3;
    else
        return 4;
    end;
End;
//-----------------------------------------------------------------------------------------

// Получить название дня недели по дате (Понедельник, Вторник и т.д.)
Macro ПолучитьНазваниеДняНедели ( dt:Date ):String
    return aFullDayName [ПолучитьНомерДняНедели (dt)-1];
End;
//-----------------------------------------------------------------------------------------

// Получить краткое название (обозначение) дня недели по дате (Пн, Вт и т.д.)
Macro ПолучитьКраткоеНазваниеДняНедели ( dt:Date ):String
    return aShortDayName [ПолучитьНомерДняНедели (dt)-1];
End;
//-----------------------------------------------------------------------------------------

// Получить название месяца по дате (Январь, Февраль и т.д.)
Macro ПолучитьНазваниеМесяца ( dt:Date ):String
    var mm;
    DateSplit(dt, NULL, mm);
    return MonName (mm);
End;
//-----------------------------------------------------------------------------------------

// Получить краткое название (обозначение) месяца по дате (Янв, Фев и т.д.)
Macro ПолучитьКраткоеНазваниеМесяца ( dt:Date ):String
    var mm;
    DateSplit (dt, NULL, mm);
    return aShortMonName [mm-1];
End;
//----------------------------------------------------------------------------------------- 

// Проверка года, представленного датой, на високосность
Macro ВисокосныйГод ( dt:Date ):Bool
    var yyyy;
    DateSplit (dt, NULL, NULL, yyyy);
    return (int (yyyy/4)*4 == yyyy) and ((int (yyyy/100)*100 != yyyy) or (int (yyyy/400)*400 == yyyy));
End;
//-----------------------------------------------------------------------------------------

// Получить количество дней в месяце, заданном датой
Macro ПолучитьКолВоДнейВМесяце ( dt:Date ):Integer
    var mm;
    DateSplit (dt, NULL, mm);

    if ( mm != 2 )          
        return aDaysInMonth [mm-1];
    elif ( ВисокосныйГод (dt) )
        return 29;
    else
        return 28;
    end;
End;
//-----------------------------------------------------------------------------------------

// Получить количество дней в году, заданном датой
Macro ПолучитьКолВоДнейВГоду ( dt:Date ):Integer
    if ( ВисокосныйГод (dt) )
        return 366;
    else
        return 365;
    end;
End;
//-----------------------------------------------------------------------------------------

// Получить начальную дату в году, к которому относится переданная дата (1 января)
Macro ПолучитьНачалоГода ( dt:Date ):Date
    var yyyy;
    DateSplit(dt, NULL, NULL, yyyy);
    return Date(1, 1, yyyy);
End;
//-----------------------------------------------------------------------------------------

// Получить конечную дату в году, к которому относится переданная дата (31 декабря)
Macro ПолучитьКонецГода ( dt:Date ):Date
    var yyyy;
    DateSplit (dt, NULL, NULL, yyyy);
    return Date (31, 12, yyyy);
End;
//-----------------------------------------------------------------------------------------

// Получить начальную дату в месяце, к которому относится переданная дата
Macro ПолучитьНачалоМесяца ( dt:Date ):Date
    var mm, yyyy;
    DateSplit (dt, NULL, mm, yyyy);
    return Date (1, mm, yyyy);
End;
//-----------------------------------------------------------------------------------------

// Получить конечную дату в месяце, к которому относится переданная дата
Macro ПолучитьКонецМесяца ( dt:Date ):Date
    var mm, yyyy;
    DateSplit (dt, NULL, mm, yyyy);
    return Date (ПолучитьКолВоДнейВМесяце (dt), mm, yyyy);
End;
//-----------------------------------------------------------------------------------------

// Получить начальную дату в квартале, к которому относится переданная дата
Macro ПолучитьНачалоКвартала ( dt:Date ):Date
    var mm, yyyy;
    DateSplit (dt, NULL, mm, yyyy);

    if ( mm <= 3 ) 
        return date (1, 1, yyyy);
    elif ( mm <= 6 )
        return date (1, 4, yyyy);
    elif ( mm <= 9 )
        return date (1, 7, yyyy);
    else
        return date (1, 10, yyyy);
    end;
End;
//-----------------------------------------------------------------------------------------

// Получить конечную дату в квартале, к которому относится переданная дата
Macro ПолучитьКонецКвартала ( dt:Date ):Date
    return ПолучитьКонецМесяца (ПолучитьНачалоКвартала (dt));
End;
//-----------------------------------------------------------------------------------------

// Получить начальную дату в неделе, к которой относится переданная дата
Macro ПолучитьНачалоНедели ( dt:Date ):Date
    return Date (dt-ПолучитьНомерДняНедели (dt)+1);
End;
//-----------------------------------------------------------------------------------------

// Получить конечную дату в неделе, к которой относится переданная дата
Macro ПолучитьКонецНедели ( dt:Date ):Date
    return Date(dt+7-ПолучитьНомерДняНедели (dt));
End;
//-----------------------------------------------------------------------------------------

// Служебная процедура по инициализации смещения в датах 
// (если параметр не определён, установит его в 1)
Private macro InitOffset ( offset:Integer ):Bool
    if( offset == NULL )
        SetParm (0, 1);
        return true; // Была инициализация
    end;

    return false;
End;
//-----------------------------------------------------------------------------------------

// Добавить необходимое кол-во дней к дате (кол-во может быть отрицательным)
// Если 2й параметр не задан, считается 1 день
Macro ДобавитьДень ( dt:Date, offset:Integer ):Date
    InitOffset (offset);
    return Date (dt + offset);
End;
//-----------------------------------------------------------------------------------------

// Добавить необходимое кол-во месяцев к дате (кол-во может быть отрицательным)
// Если 2й параметр не задан, считается 1 месяц
//
Macro ДобавитьМесяц ( dt:Date, offset:Integer ):Date
    InitOffset (offset);
    return DateAfterCalenMonths (dt, offset);
End;
//-----------------------------------------------------------------------------------------

// Добавить необходимое кол-во лет к дате (кол-во может быть отрицательным)
// Если 2й параметр не задан, считается 1 год
Macro ДобавитьГод ( dt:Date, offset:Integer ):Date
    InitOffset (offset);
    return DateAfterCalenMonths (dt, offset*12);   
End;
//-----------------------------------------------------------------------------------------

// Получить из даты строку формата ДД.ММ.ГГГГ
Macro ПолучитьИзДатыСтроку ( dt:date ):String
    if ( (dt == null) or (string (dt) == "") )
        return "00.00.0000";
    end;

    return StrLeftPad (Trim (String (dt)), 10, "0");
End;
//-----------------------------------------------------------------------------------------

// Получить дату с месяцем просью
Macro ПолучитьДатуСМесяцемПрописью ( dat:Date ):String
    return String (dat:f:m);   
End;
//-----------------------------------------------------------------------------------------

// Служебная процедура обработки сообщений диалогового окна
Private macro DtIntrvalEvProc ( dlg, cmd, id, key ) 
    if ( cmd == DLG_INIT )
        Message("~ESC~ выход, ~F2~ ввод, ~F3~ выбор с помощью календаря");  
    elif ( (cmd == DLG_KEY) and (key == 13) and (id == FldNumber (dlg)-1) )
         return CM_IGNORE
    elif ( (cmd == DLG_KEY) and (key == 316) )
         return CM_SAVE;
    elif ( (cmd == DLG_KEY) and (key == 317) )
        dlg (id) = GetDateByCalendar (dlg (id));
    end;
End;
//-----------------------------------------------------------------------------------------

// Вывод диалогового окна ввода диапазона дат
Macro ВвестиДиапазонДат ( BeginDate:Date, EndDate:Date ):Bool
    record inDate (inperiod, "DateLib.lbr") dialog; // Панель ввода диапазона дат  (..\\user\\report\\DateLib.lbr)

    if( BeginDate == NULL )
        BeginDate = {curdate};
    end;

    if (EndDate == NULL )
        EndDate   = {curdate};
    end;

    inDate.BDate = BeginDate;
    inDate.EDate = EndDate;

    if ( RunDialog (inDate, @DtIntrvalEvProc) )
        SetParm (0, inDate.BDate);
        SetParm (1, inDate.EDate);

        return true;  // Успешное завершение работы
    end;

    return false;  // Работа была прервана
End;
//-----------------------------------------------------------------------------------------