 
 ///////////////////////////////////////////////////////////////////////////////////
//*******************************************************************************//
//  задание на загрузку индекса из экселя(эмуляция WS)                           //
//*******************************************************************************//
///////////////////////////////////////////////////////////////////////////////////
IMPORT DealsInter, Проценты, PTInter, PaymInter, "gtconst.inc","rgtgtrec.mac", "gtdlfun.mac", "dlref.mac", OprInter, SfInter, FIInter,Календарь, funk,dl_plib;

VAR ER,basi,isUpdate;


private macro InsNewPrice(dealid, price, stav)
   private var rs, que,da,{curdate};

   que ="insert into dstd_pls_dbt ("+
         "t_id,   "+
         "t_dizm, "+ 
         "t_stav,  "+
         "t_kor_pro) values ("+
         string(dealid) + ",'"+
         "to_date('"+{curdate}+"','dd.mm.yyyy'), "+
         string(price*Pow(10,6))+","+
         string(stav)+")";
   msgbox(que);
   rs  = LnGetRecordset(que);
end;

private macro AddField (ar, name, tp, sz, dec)
  ar [ar.size] = name;
  ar [ar.size] = tp;
  ar [ar.size] = sz;
  ar [ar.size] = dec;
  ar [ar.size] = 0;
end;


macro MakeArRates()
  var arRates = TArray;

  AddField (arRates, "IDRATE",       V_INTEGER,  7,  0);
  AddField (arRates, "IDFI",         V_INTEGER,  7,  0);// идентификатор финансового инструмента
  AddField (arRates, "DATERATE",     V_STRING,  11,  0);
  AddField (arRates, "PREVDATERATE", V_STRING,  11,  0);//Дата предыдущей ставки
  AddField (arRates, "CODEFI",       V_STRING,  20,  0);
  AddField (arRates, "CODE",         V_STRING,   4,  0);
  AddField (arRates, "SECTION",      V_STRING,  10,  0);
  AddField (arRates, "RATE",         V_STRING,  10,  0);
  AddField (arRates, "PREVRATE",     V_STRING,  10,  0);//Предыдущая величина индекса
  AddField (arRates, "SCALE",        V_INTEGER,  4,  0);
  AddField (arRates, "POINT",        V_INTEGER,  4,  0);
  AddField (arRates, "ISDOMINANT",   V_STRING,   2,  0);
  AddField (arRates, "NEEDACTION",   V_INTEGER,  2,  0);//Признак необходимости действий
  AddField (arRates, "RESCOMMENT",   V_STRING, 500,  0);//Коментарий по результату
  return arRates;
end;


var locRates = TRecHandler ("rates", MakeArRates());

private class Ob_RatesFileImport( obRate )
 // private var
 //     prm_SeanceID,
 //     prm_RecordID;
  private var
      prm_Comment,
      prm_ratehist;

  PRIVATE MACRO ДАТ0(s);
      private var ss,d1;
      ss=trim(substr(string(s),1,10));
      if (substr(ss,3,1) != "." )
       ss="0"+ss;
      end; 
      d1=date(ss);
      return(d1);
     END;

//  Записыввает в историю изменения ставки
  private macro WriteHist(rs)
    
      prm_ratehist.rec.rateid=rs.value("t_rateid");
      prm_ratehist.rec.isinverse=rs.value("t_isinverse");
      prm_ratehist.rec.rate=rs.value("t_rate");
      prm_ratehist.rec.scale=rs.value("t_scale");
      prm_ratehist.rec.point=rs.value("t_point");
      prm_ratehist.rec.inputdate=rs.value("t_inputdate");
      prm_ratehist.rec.inputtime=rs.value("t_inputtime");
      prm_ratehist.rec.oper=rs.value("t_oper");
      prm_ratehist.rec.sincedate=rs.value("t_sincedate");
      prm_ratehist.rec.ismanualinput=rs.value("t_ismanualinput");
      if (not prm_ratehist.insert)
        RunError( "Ошибка при сохранении истории индекса." );
        prm_Comment="Ошибка при сохранении истории индекса." ;
      end;    
  end; 
//основные операции по загрузке индекса
  private macro MakeRates()
     var sql,rs;
     var loadtime = time; 
     var loaddate = date;
   //оперделяем старые значения.
     if (locRates.rec.IDRATE==0) //в текущей реализации такое не должно быть
       prm_Comment="Не определено первоначальное значение индекса "+LocRates.rec.CODEFI+".";
       RunError( "Не определено первоначальное значение индекса." );
     end;
     sql="SELECT r.t_rateid, r.t_isinverse, r.t_rate, r.t_scale, r.t_point, r.t_inputdate, r.t_inputtime, r.t_oper," 
          +" r.t_sincedate, r.t_ismanualinput FROM dratedef_dbt r  WHERE r.t_rateid="+LocRates.rec.IDRATE;
     rs=LnGetRecordset(sql);
     if(rs != null)
      if (rs.moveNext) 
         locRates.rec.PREVDATERATE=string(rs.value("t_sincedate"));
         LocRates.rec.PREVRATE=string(rs.value("t_rate")/Pow(10,rs.value("t_point")));
         //записываем в исторю
         WriteHist(rs)
      end; 
     else
      prm_Comment="Не удалось определить данные по ставке с идентификатором IDRATE = "+LocRates.rec.IDRATE; 
      RunError("Не удалось определить данные по ставке с идентификатором IDRATE = "+LocRates.rec.IDRATE);
     end;    
     //заносим новые значения
     sql="Update dratedef_dbt set  t_rate= "+money(LocRates.rec.rate)*Pow(10,int(LocRates.rec.POINT))
                                  +", t_scale=  "+locRates.rec.SCALE
                                  +", t_point=  "+locRates.rec.POINT
                                  +", t_inputdate= to_date('"+string(loaddate)+"','dd.mm.yyyy') "  
                                  +", t_inputtime= to_date('01.01.0001 "+string(loadtime)+"','dd.mm.yyyy HH24:MI:SS') "
                                  +", t_oper= "+{oper}
                                  +", t_sincedate= to_date('"+string(locRates.rec.DATERATE)+"','dd.mm.yyyy') "
          +"  WHERE t_rateid="+LocRates.rec.IDRATE;
     // msgbox(sql);    
     rs=LnGetRecordset(sql);
     if (rs!=null)
       if (locRates.rec.rate!=locRates.rec.PREVRATE)
           sql= " SELECT  a.t_dealid dealid , b.t_correct correct FROM  "
                  +" ddl_tick_dbt a, ddl_leg_dbt b  WHERE b.t_dealid=a.t_dealid  AND  "
                   +"  (a.t_dealstatus <=10 OR   (a.t_dealstatus=20 AND  to_date('"+locRates.rec.DATERATE+"','dd.mm.yyyy') BETWEEN a.t_dealdate AND a.t_closedate)) "
                   +"   AND a.t_bofficekind=102   AND b.t_caption = "+LocRates.rec.IDFI;
            rs=LnGetRecordset(sql);
            if (rs!=null)//ЕСЛИ ЕСТЬ ПОДХОДЯЩИЕ СДЕЛКИ С ДАННЫМ фИ то заполним 
                while (rs.moveNext) 
                    InsNewPrice(rs.value("dealid"), double(LocRates.rec.rate)+rs.value("correct") ,double(LocRates.rec.rate));  
                end;
            else    
              prm_Comment="Ошибка добавления данных в таблицу dstd_pls_dbt. "; 
              RunError( "Ошибка добавления данных в таблицу dstd_pls_dbt." )
            end;        
      end;    
     else
       prm_Comment="Не удалось обновить данные по ставке с идентификатором IDRATE = "+LocRates.rec.IDRATE; 
       RunError( "Ошибка при обновлении индекса." )
     end; 
   return true;
  end;
  
  private macro LoadRatesTrn()
    var
        retVal = true;
        retVal = MakeRates();
        prm_Comment="Индекс с идентификатором IDRATE = "+LocRates.rec.IDRATE+" за "+LocRates.rec.DATERATE+" успешно закачан.";
        return retVal;

    OnError( RslErrObj )
      prm_Comment = RslErrObj.Message;

      AbortTrn();
      return false;
  end;

  macro LoadRates()
       macro Local_LoadRatesTrn()
         return LoadRatesTrn();
       end;
      if (locRates.rec.NEEDACTION==0)
          prm_Comment="Никаких действий не требуется.";
      else
         if (not ProcessTrn(0, "Local_LoadRatesTrn"))
          AbortTrn();//Если все же не хорошо что то то ролбэкнем информацию
          return 1;
         end;  
      end;  
      locRates.rec.NEEDACTION=0; 
      return 0;
  end; 

  macro Get_Comment()
      return prm_Comment;
  end;

  private macro ClassConstructor( obRate)
      //prm_SeanceID = 0; // _SeanceID;
      //prm_RecordID = 0;// _RecordID;
      //prm_ObjID   = -1;
      prm_Comment = "";
      //msgbox(obdeal.rec.dealid);
      copy(locRates, obRate);
      //msgbox(locdeal.rec.dealid);

      prm_ratehist    = Tbfile("ratehist.dbt", "W");
  end;

  ClassConstructor( obRate);
end;

///////////////////////////////////////////////////////////////////////////////////
//вызываем транзакцию импорта индекса, возвращаем нужные параметры	
macro _LoadRates_CreateNew( obRate ):INTEGER
  var 
      Obj = Ob_RatesFileImport(obRate),
      retVal = 0;
      retVal = Obj.LoadRates(obRate);
      obRate.rec.RESCOMMENT=Obj.Get_Comment(); 
      obRate.rec.NEEDACTION=locRates.rec.NEEDACTION;

    return retVal;
end;


