/*           
 Filename    : imp_cb.mac
 Description : Загрузка в справочник ценных бумаг из буферных таблиц (Ru Data)
 Programmer  : 
 21.12.2017  : Создан
*/
import globals, likepy, oralib, "oratools.mac", FIInter, CTInter, "fimp_rudata.mac",funk;
// import "createdepo.mac";
var ConvAv,fi1,fi2,kod,ii,que,rs;
array mas,mas1;

mas1(0)="AFLT"; 
mas1(1)="ALRS"; 
mas1(2)="CHMF"; 
mas1(3)="FEES"; 
mas1(4)="GAZR"; 
mas1(5)="GMKR"; 
mas1(6)="HYDR"; 
mas1(7)="LKOH"; 
mas1(8)="MGNT"; 
mas1(9)="MOEX"; 
mas1(10)="MTSI";
mas1(11)="NLMK";
mas1(12)="NOTK";
mas1(13)="ROSN";
mas1(14)="RTKM";
mas1(15)="SBPR";
mas1(16)="SBRF";
mas1(17)="SNGP";
mas1(18)="SNGR";
mas1(19)="TATN";
mas1(20)="TRNF";
mas1(21)="VTBR";
mas1(22)="GBMW";
mas1(23)="GDAI";
mas1(24)="GDBK";
mas1(25)="GSIE";
mas1(26)="GVW3";

mas(0)="RU0009062285";
mas(1)="RU0007252813";
mas(2)="RU0009046510";
mas(3)="RU000A0JPNN9";
mas(4)="RU0007661625";
mas(5)="RU0007288411";
mas(6)="RU000A0JPKH7";
mas(7)="RU0009024277";
mas(8)="RU000A0JKQU8";
mas(9)="RU000A0JR4A1";
mas(10)="RU0007775219";
mas(11)="RU0009046452";
mas(12)="RU000A0DKVS5";
mas(13)="RU000A0J2Q06";
mas(14)="RU0008943394";
mas(15)="RU0009029557";
mas(16)="RU0009029540";
mas(17)="RU0009029524";
mas(18)="RU0008926258";
mas(19)="RU0009033591";
mas(20)="RU0009091573";
mas(21)="RU000A0JP5V6";
mas(22)="DE0005190003";
mas(23)="DE0007100000";
mas(24)="DE0005140008";
mas(25)="DE0007236101";
mas(26)="DE0007664039";



CONST TECH_EMITENT = 7; //технический эмитент

CONST OBJECT_AVOIRISS = 9,
      CODE_MMVB       = 11,
      CODE_STATE_OPEN = 0;

macro RepHeader(dt, tm, oper)
   var str;
   str =     "                             ОТЧЕТ О ЗАГРУЗКЕ ЦБ В RS-BANK V6.0\n";
   str = str+"\n";
   str = str+"Дата загрузки  : "+dt+"\n";
   str = str+"Время загрузки : "+tm+"\n";
   str = str+"Операционист   : "+oper+"\n";
   str = str+"┌──────┬───────────────────────┬──────────────┬──────────────────────────────────────────────────────────────────────────────────────────────────────────────┐\n";
   str = str+"│id    │       isin            │shortname     │ comment                                                                                                      │\n";
   str = str+"├──────┼───────────────────────┼──────────────┼──────────────────────────────────────────────────────────────────────────────────────────────────────────────┤";
   println(str);
end;
             
macro RepFooter(_id:integer, _isin, _shortname, errcount, errtext)                                                       
  var str, count = 0;
  if (errcount == 0) 
    errtext(count) = "Успешно!";
  end;
  str = "│"+string(_id        :6)   +"│"+
            string(_isin      :23)  +"│"+
            string(_shortname :14)  +"│"+
            string(errtext(count) :110) +"│\n";

  while (count < errcount - 1)
    count = count + 1;
    str = str + "│"+string("" :6) +"│"+
                    string("" :23)+"│"+
                    string("" :14)+"│"+
                    string(errtext(count) :110) +"│\n";
  end;
//  str = str+"└──────┴───────────────────────┴──────────────┴──────────────────────────────────────────────────────────────────────────────────────────────────────────────┘\n";
  return println(str);
end;

private macro _AddCol(ar, ind, fld, head, width, fldType, decPoint)
  ar.value( ind * 6 + 0 ) = fld;
  ar.value( ind * 6 + 1 ) = head;
  ar.value( ind * 6 + 2 ) = width;
  ar.value( ind * 6 + 3 ) = fldType;
  ar.value( ind * 6 + 4 ) = decPoint;
  ar.value( ind * 6 + 5 ) = 0;   // reserv
end;

private macro LPAD( str, num )
  var tmp = string(str);
  if( num < 1 )
    return str;
  end;
  if( strlen(str) >= num )
    return str;
  end;
  while( strlen(tmp) < num )
    tmp = string("0") + tmp;
  end;
  return tmp;
end;

private macro SetCateg (fiid, groupid, attrid)
  var Categ = RsbObjCategories(12, LPAD(fiid, 10));
  Categ.ConnectAttr(groupid, attrid, null, null, {curdate});
  return Categ.Save();
End;

class Convert_Avoiriss ()
  var ProcDate = {curdate};
  var ProcTime = Time();
  var err  = 0, errtext = TArray();
  var bond = false, depo = false;
  var DRSHB_CB_A_DBT  ,
      DRSHB_CB_AM_DBT ,
      DRSHB_CB_CUP_DBT,
      DRSHB_CB_DBT    ,
      DRSHB_CB_R_DBT  ,
      DRSHB_EMI_DBT   ,
      DRSHB_EMI_R_DBT ;

  var Iss      = TRecHandler ("avoiriss.dbt");
  var FI       = TRecHandler ("fininstr.dbt");
  var Fiwarnts = TRecHandler ("Fiwarnts.dbt");
  var Invst    = TRecHandler ("avrinvst.dbt");

  //выбрать данные из буфферных таблиц
  private macro SelectAll (isin, regcode)
    var rez = true, sql;

    if ((valtype(isin) == V_UNDEF) or (isin == null) or (isin == ""))
     isin = "noisin";
    end;
    if ((valtype(regcode) == V_UNDEF) or (regcode == null) or (regcode == ""))
       regcode = "noregcode";
    end;
    //анкета
    sql = "select * from DRSHB_CB_A_DBT where t_isin = :isin or t_regcode_nrd = :regcode";
    DRSHB_CB_A_DBT = ExecSqlSelect(sql, MakeArray(SqlParam("isin"   , isin    ),
                                                  SqlParam("regcode", regcode)), false);
    if (DRSHB_CB_A_DBT.MoveNext())
      bond = true;
    end;

    //ЦБ эмитентов
    sql = "select * from DRSHB_CB_DBT where t_isin = :isin or t_reg_code = :regcode";
    DRSHB_CB_DBT = ExecSqlSelect(sql, MakeArray(SqlParam("isin"   , isin    ),
                                                SqlParam("regcode", regcode)), false);
    if (not DRSHB_CB_DBT.MoveNext())
      rez = false;
    end;
    
    //купоны
    sql = "select * from DRSHB_CB_CUP_DBT where t_id_fintool = :id";
    DRSHB_CB_CUP_DBT = ExecSqlSelect(sql, MakeArray(SqlParam("id", DRSHB_CB_DBT.value("t_fintoolid"))), false);

    //частичные погашения
    sql = "select * from DRSHB_CB_AM_DBT where t_id_fintool = :id";
    DRSHB_CB_AM_DBT = ExecSqlSelect(sql, MakeArray(SqlParam("id", DRSHB_CB_DBT.value("t_fintoolid"))), false);

    return rez;
  End;

  //найти страну эмитента
  private macro GetCountry (fininstid)
    var sql = ExecSqlSelect("select t_country from DRSHB_EMI_DBT where t_fininstid = :id ", MakeArray(SqlParam("id", fininstid)), false);
    if (sql.MoveNext()) 
      return sql.value("t_country");
    end;
    return "";
  End;

  /*Обращаемость*/
  private macro SetIsMktPrice (fiid, ismktprice)
    if (ismktprice == 1) 
      return SetCateg(fiid, 17, 1);
    elif ((ismktprice == 2) or (ismktprice == 3))
      return SetCateg(fiid, 17, 2);
    end;
    return 0;
  end;

  /*Котируемость*/
  private macro SetListing (fiid, listinglevel)
    if ((listinglevel == 1) or (listinglevel == 2))
      return SetCateg(fiid, 18, 1);
    elif (listinglevel == 3)
      return SetCateg(fiid, 18, 2);
    end;
    return 0;
  End;

  private macro УстановитьКодЦБ (fiid, code:string, codekind)
    var Objcode = TBFile("objcode.dbt", "W");
    if (ПолучитьФинИн(fiid) != 0)
      return 1; //не найден фин. инструмент
    end;
    if ((code == "") or (code == null)) 
      return 1;
    end;
    Objcode.rec.objecttype = OBJECT_AVOIRISS;
    Objcode.rec.codekind   = codekind;
    Objcode.rec.objectid   = fiid;
    Objcode.rec.code       = code;
    Objcode.rec.state      = CODE_STATE_OPEN;
    Objcode.rec.bankdate   = {curdate};
    Objcode.rec.sysdate    = date();
    Objcode.rec.userid     = {oper};
    if (Objcode.insert()) 
      return 0;
    end;
    return 1;
onerror()
  exit(1);
  End;

  //проверяет и устанавливает принадлежность к эмитентам(5)
  private macro SetOwnEmi (pid)
    var sql;
    var Own = Tbfile ("partyown", "W");

    sql = "select 1 from dpartyown_dbt where t_partyid = :pid and t_partykind = 5";
    if (ExecSqlSelect(sql, MakeArray(SqlParam("pid", pid)), false).MoveNext() )
      return true;
    end;

    Own.Clear();
    Own.("partyid") = pid;
    Own.("partykind") = 5;
    if (not Own.insert())
        errtext(err) = "Ошибка при установке принадлежности субъекта id="+pid+" к эмитентам";
        err = err + 1;
      return false;
    end;
    return true;
  End;

  //найти субъект по имени
  private macro GetPartyByName (name)
    var sql = ExecSqlSelect(" select t_partyid from dparty_dbt where t_name = :name ", MakeArray(SqlParam("name", name)), false);
    if (sql.MoveNext())
      SetOwnEmi(sql.value("t_partyid"));
      return sql.value("t_partyid");
    end;    
    return -1;
  End;

  //найти субъект по ИНН
  private macro GetPartyByINN (_fininstid, _id_emitent)
    var sql = " select oc.t_objectid partyid "
            + " from DRSHB_EMI_DBT emi, dobjcode_dbt oc "
            + " where     oc.t_codekind = 16 "
            + "       and oc.t_objecttype = 3 "
            + "       and emi.t_inn = substr(oc.t_code, 1, 10) ";
    if (_fininstid == 0) 
      sql = sql + " and emi.t_id_emitent = :1 ";
      _fininstid = _id_emitent;
    else
      sql = sql + " and emi.t_fininstid = :1 ";
    end;

    sql = ExecSqlSelect(sql, MakeArray(SqlParam("1", _fininstid)), false);
    if (sql.MoveNext())
      SetOwnEmi(sql.value("partyid"));
      return sql.value("partyid");
    end;
    return -1;
  End;

  //ид эмитента
  private macro GetPartyID (_fininstid, _id_emitent, name)
    var id = 0;
    id = GetPartyByName(name); //проверка по имени
    if (id > 0)
      return id;
    end;
    id = GetPartyByINN(_fininstid, _id_emitent); //проверка по ИНН
    if (id > 0)
      return id;
    end;
    errtext(err) = "Не найден субъект в справочнике, в качестве эмитента установлен технический эмитент";
    err = err + 1;
    return TECH_EMITENT;
  End;

  //определить базис 
  private macro GetBasis ()
    if (DRSHB_CB_A_DBT.value("t_Basis_NRD") == "act/365")
      return 0;
    elif (DRSHB_CB_A_DBT.value("t_Basis_NRD") == "30E/360")
      return 1;
    end;
    return 1;
  End;

  private macro Getvaluta (ccy)
    var sql = "select t_fiid from dfininstr_dbt where t_ccy = :ccy";
    sql = ExecSqlSelect(sql, MakeArray(SqlParam("ccy", ccy)), false);
    if (sql.MoveNext())
      return sql.value(0);
    end;
    return -1;
  End;

  /*Вид облигации*/
  private macro SetKIndBond (kind, country)
    country = StrLwr(country);
    if (country != "россия")
      return 50;
    end;
    if (kind == "Гос")
      if (country == "россия")
        Iss.rec.state = "X";
        return 21;
      else
        return 50;
      end;
    elif (kind == "ЕвроГос")
      if (country == "россия")
        Iss.rec.state = "X";
        return 28;
      else
        return 50;
      end;
    elif (kind == "Корп")
      return 42;
    elif (kind == "Муни")
      return 38;
    elif (kind == "ЕвроМуни")
      return 39;
    elif (kind == "ЕвроКорп")
      return 43;
    end;
    return 0;
  End;

  /*Вид акции*/
  private macro SetKindSecurity (kind)
    if (kind == "Обыкн-ая")
      return 1;
    elif (kind == "Привилег-ая")
      return 2;
    end;
    return 0;
  End;

  /*Вид депо-расписки*/
  private macro SetKindDepo (kind)
    depo = true;
    if (kind == "GDR") //глобальная
      return 46;
    elif (kind == "ADR") //американская
      return 45;
    elif (kind == "RDR") //российская
      return 47;
    elif (kind == "SDR") //европейская
      return 49;
    end;
    return 10;
  End;

  //определить тип цб
  private macro SetAvoirKind (type, kind, country)
    var rez = 0;
    if (type == "Облигация")
      rez = SetKIndBond(kind, country);
    elif (type == "Акция")
      rez = SetKindSecurity(kind);
    elif (type == "Депозитарная расписка")
      rez = SetKindDepo(kind);
    elif (type == "Фонд")
      rez = 16;
    elif (type == "Ипотечный сертификат")
      rez = 35;
    end;
    return rez;
  End;

  /*Выбрать вид ценной бумаги*/
  private macro ВыборВидаЦеннойБумаги ()
    var avr = TRechandler("avrkinds.dbt");
    if(ListAvrKindsTree(2, avr) == true)
      return avr.rec.avoirkind;
    end;
    return 0;
  End;

  /*вид дохода: 1 - процентный, 2 - дисконтный*/
  private macro GetIncomeType (name)
    if (index(StrLwr(name), "дисконт") != 0)
      return 2;
    end;
    return 1;
  End;

  /*форма выпуска цб (1-документарная, 0-бездокументарная)*/
  private macro GetSettlement (name)
    if (index(StrLwr(name), " документ") != 0)
      return 1;
    end;
    return 0;
  End;

  private macro SetInvstType ()
    if (DRSHB_CB_DBT.value("t_securitytype") == "ETF")
      Invst.rec.type = 3;
    elif (DRSHB_CB_DBT.value("t_securitytype") == "Закрытый")
      Invst.rec.type = 2;
    elif (DRSHB_CB_DBT.value("t_securitytype") == "Открытый")
      Invst.rec.type = 1;
    elif (DRSHB_CB_DBT.value("t_securitytype") == "Интервальный")
      Invst.rec.type = 0;
    end;
  End;

  private macro InitInvst ()
    if (FI.rec.avoirkind != 16) //инвестиционный пай
      Invst = null;
      return;
    end;
    SetInvstType();
    Invst.rec.minvaluefiid = FI.rec.facevaluefi;
    Invst.rec.formvaluefiid = FI.rec.facevaluefi;
    Invst.rec.name = DRSHB_CB_DBT.value("t_shortname_rus");
  End;

  private macro GetFiid (isin)
    if ((isin == "") or (isin == null))
      errtext(err) = "Не возможно определить базовый ФИ по депозитарной расписке.";
      err = err + 1;
      return 0;
    end;

    var newfi, sql = ExecSqlSelect("select t_fiid from davoiriss_dbt where t_isin = :1 or t_lsin = :2", MakeArray(SqlParam("1", isin), SqlParam("2", isin)), false);
    if (sql.MoveNext()) 
      return sql.value("t_fiid");
    end;
    errtext(err) = "Не найден базовый ФИ по депозитарной расписке. Попытка добавить новый ФИ в справочник";
    err = err + 1;
    newfi = Convert_Avoiriss();
    return newfi.Main(isin);
  End;

  private macro GetIsin (fiid)
    var sql = " select case when t_isin = chr(1) then t_lsin   "
            + "             else t_isin   "
            + "        end isin "
            + " from davoiriss_dbt "
            + " where t_fiid = :1";
    sql = ExecSqlSelect(sql, MakeArray(SqlParam("1", fiid)), false);
    if (sql.MoveNext()) 
      return sql.value("isin");
    end;
    return "";
  End;

  private macro SetPoint (rate:string)
    var ind = index(rate, ".");
    if (ind > 0)
      return (strlen(rate)-ind);
    end;
    return 1;
  End;

  private macro InsertRates (pai)
    var sql;
    var isin = OraString (GetChildNodeByName10 (pai, "ISIN"));
    var dt = TransformDateForBD (GetChildNodeByName10 (pai, "TIME"));
    sql = "select 1 from DRSHB_RATES_DBT where t_isin = :1 and t_date = :2";
    sql = ExecSqlSelect(sql, MakeArray(SqlParam("1", isin),
                                       SqlParam("2", dt)), false);
    if (sql.MoveNext()) 
      sql = "delete from DRSHB_RATES_DBT where t_isin = :1 and t_date = :2";
    sql = ExecSql(sql, MakeArray(SqlParam("1", isin),
                                 SqlParam("2", dt)), false);
    end;

    sql = " insert into DRSHB_RATES_DBT (t_low, "
        + "                              t_high, "
        + "                              t_isin, "
        + "                              t_mprice, "
        + "                              t_date) "
        + " values (:low, "
        + "         :high, "
        + "         :isin, "
        + "         :mprice, "
        + "         :dt) ";
    sql = ExecSql(sql, MakeArray(SqlParam("low",    OraMoney  (GetChildNodeByName10 (pai, "LOW"))),
                                 SqlParam("high",   OraMoney  (GetChildNodeByName10 (pai, "HIGH"))),
                                 SqlParam("isin",   isin),
                                 SqlParam("mprice", OraMoney  (GetChildNodeByName10 (pai, "MPRICE"))),
                                 SqlParam("dt",     dt)), false);
    if (sql == null) 
      return 1;
    end;
    return 0;
  onerror()
    return 1;
  End;



  macro InsertAvoir (isin, regcode)
    var str, country, mmvb_code, updflag = false;
    var stat;

    if (not SelectAll(isin, regcode))
      errtext(err) = "Данные по ценной бумаге не найдены в буферной таблице";
      err = err + 1;
      return 1;
    end;

    FI.rec.facevaluefi = Getvaluta(DRSHB_CB_DBT.value("t_facevalue_currency"));
    country            = GetCountry(DRSHB_CB_DBT.value("t_fininstid"));
    FI.rec.issuer      = GetPartyID(DRSHB_CB_DBT.value("t_fininstid"), DRSHB_CB_DBT.value("t_id_emitent"), DRSHB_CB_DBT.value("T_EMITENT_SHORTNAME_RUS"));
    FI.rec.avoirkind   = SetAvoirKind(trim(DRSHB_CB_DBT.value("t_fintooltype")), trim(DRSHB_CB_DBT.value("t_securitytype")), trim(country));

    if (FI.rec.issuer == -1)
      return 1;
    end;

    if (FI.rec.facevaluefi == -1) 
      errtext(err) = "Не найдена валюта для данной бумаги";
      err = err + 1;
      return 1;
    end;

    if (FI.rec.avoirkind == 0)
      str = "Не удалось установить вид финансового инструмента:\n"+DRSHB_CB_DBT.value("t_fullname_rus")+"\nУстановить вручную?";
      str = MsgBoxEx(str, MB_YES+MB_NO, IND_YES, "Внимание!");
      if (str == 2)
        return 1;
      end;
      FI.rec.avoirkind = ВыборВидаЦеннойБумаги();
      if (FI.rec.avoirkind == 0)
        errtext(err) = "Не удалось установить вид финансового инструмента";
        err = err + 1;
        return 1;
      end;
      /*
        FI.rec.avoirkind = 29;
        errtext(err) = "Возможно, не верно установлен подвид ценной бумаги. Необходимо перепроверить вручную";
        err = err + 1;
      */
    end;

    //данные для dfininstr_dbt
    FI.rec.fi_code         = OraString(DRSHB_CB_DBT.value("t_fintoolid"));
    FI.rec.fi_kind         = FIKIND_AVOIRISS;
    FI.rec.name            = OraString(DRSHB_CB_DBT.value("t_shortname_rus"));
    FI.rec.definition      = OraString(DRSHB_CB_DBT.value("t_fullname_rus"));
    FI.rec.settlement_code = GetSettlement(DRSHB_CB_DBT.value("t_fullname_rus"));
    FI.rec.facevalue       = OraInteger(DRSHB_CB_DBT.value("t_facevalue"));
    if (depo) 
      FI.rec.parentfi      = GetFiid(DRSHB_CB_DBT.value("t_isincodebase_nrd"));
      if (FI.rec.parentfi == 0) 
        errtext(err) = "Не указан базовый ФИ депозитарной расписки";
        err = err + 1;
      end;
    end;
//    if (OraDate(DRSHB_CB_DBT.value("T_ENDMTY_DATE")) != date("01.01.2001")) 
      FI.rec.drawingdate = OraDate(DRSHB_CB_DBT.value("T_ENDMTY_DATE"));  //дата погашения
//    end;

    //данные для davoiriss_dbt
    Iss.rec.isin           = DRSHB_CB_DBT.value("t_isin");
    Iss.rec.lsin           = DRSHB_CB_DBT.value("t_reg_code");
    Iss.rec.qty            = DRSHB_CB_DBT.value("t_issue_vol");
    Iss.rec.nkdround_kind  = 1;                 //вид округления при расчёте нкд
    Iss.rec.incometype     = GetIncomeType(DRSHB_CB_DBT.value("t_fullname_rus"));
    if (bond == true) 
      Iss.rec.nkdbase_kind = GetBasis();
      Iss.rec.incirculationdate = DRSHB_CB_A_DBT.value("t_begdistdate_nrd");  //дата ввода в обращение
      FI.rec.expirydate         = OraDate(DRSHB_CB_A_DBT.value("T_enddistdate_nrd"));  //дата закрытия
    end;

    InitInvst();
    if (FI_InsertAvoiriss(FI, Iss, Invst, null, true) != 0)
      errtext(err) = "Ошибка при вставке финансового инструмента";
      err = err + 1;
      return 1;
    end;

    //вставка инф-ии о купонах
    while (DRSHB_CB_CUP_DBT.MoveNext())
      Fiwarnts.clear();
      Fiwarnts.rec.fiid         = FI.rec.fiid;
      Fiwarnts.rec.number       = DRSHB_CB_CUP_DBT.value("t_id_coupon");
      Fiwarnts.rec.drawingdate  = OraDate(DRSHB_CB_CUP_DBT.value("t_end_period"));
      Fiwarnts.rec.incomerate   = OraMoney(DRSHB_CB_CUP_DBT.value("t_coupon_rate"));
      Fiwarnts.rec.incomevolume = OraMoney(DRSHB_CB_CUP_DBT.value("t_pay_per_bond"));
      Fiwarnts.rec.firstdate    = date(DRSHB_CB_CUP_DBT.value("t_begin_period"))+1;
      Fiwarnts.rec.factdate     = OraDate(DRSHB_CB_CUP_DBT.value("t_pay_date"));
      Fiwarnts.rec.incomescale  = 1;

      if (Fiwarnts.rec.drawingdate < ProcDate)
        Fiwarnts.rec.isclosed   = "X";
        Fiwarnts.rec.spisclosed = "X";
        Fiwarnts.rec.tsisclosed = "X";        
      end;

      if (FI_InsertWarnt(Fiwarnts) != 0)
        errtext(err) = "Не удалось добавить купон номер "+DRSHB_CB_CUP_DBT.value("t_id_coupon");
        err = err + 1;
      end;  
    end;

    //вставка инф-ии о частичном погашении
    while (DRSHB_CB_AM_DBT.MoveNext())
      Fiwarnts.clear();
      Fiwarnts.rec.fiid         = FI.rec.fiid;
      Fiwarnts.rec.number       = DRSHB_CB_AM_DBT.value("t_id_tranche");
      Fiwarnts.rec.drawingdate  = OraDate(DRSHB_CB_AM_DBT.value("t_mty_date"));
      Fiwarnts.rec.incomerate   = OraMoney(DRSHB_CB_AM_DBT.value("t_mty_part"));
      Fiwarnts.rec.incomevolume = OraMoney(DRSHB_CB_AM_DBT.value("t_pay_per_bond"));
      Fiwarnts.rec.listdate     = OraDate(DRSHB_CB_AM_DBT.value("t_fix_date"));
      Fiwarnts.rec.factdate     = OraDate(DRSHB_CB_AM_DBT.value("t_pay_date"));
      Fiwarnts.rec.ispartial    = "X";
      Fiwarnts.rec.incomescale  = 1;

      if (Fiwarnts.rec.drawingdate < ProcDate)
        Fiwarnts.rec.isclosed   = "X";
        Fiwarnts.rec.spisclosed = "X";
        Fiwarnts.rec.tsisclosed = "X";
        updflag = true;
      end;  

      if (FI_InsertWarnt(Fiwarnts) != 0)
        errtext(err) = "Не удалось добавить купон частичного погашения номер"+DRSHB_CB_AM_DBT.value("t_id_tranche");
        err = err + 1;
      end;
    end;
    
    if (SetIsMktPrice(Fi.rec.fiid, int(DRSHB_CB_DBT.value("t_ismktprice"))) != 0)
      errtext(err) = "Ошибка при установки обращаемости";
      err = err + 1;
    end;

    if (SetListing(Fi.rec.fiid, int(DRSHB_CB_DBT.value("t_listinglevel"))) != 0)
      errtext(err) = "Ошибка при установки котируемости";
      err = err + 1;
    end;

      if (УстановитьКодЦБ(FI.rec.fiid, kod, 17))
        errtext(err) = "Ошибка при установки кода типа 17 ";
        err = err + 1;
      end;

    return 0;
  end;

  macro Main()
    var rez, oldfile, FileName, error = "",isin;

    FileName = "new.txt";
    oldfile = SetOutPut(FileName);
    RepHeader (ProcDate, Proctime, {oper});

   ii=0;
   while ( ii < 27 )
      fi1=mas(ii);
      isin=fi1;
      fi2="";
      kod=mas1(ii);
      que="select count(*) from dfininstr_dbt a , davoiriss_dbt b where a.t_fiid=b.t_fiid and b.t_isin='"+fi1+"'";
      rs = LnGetRecordSet(que);
      if(rs != null)
        if (rs.moveNext) 
          if (rs.value(0) == 0)
            rez = InsertAvoir(fi1, fi2);
            if (rez != 0) 
               RepFooter(0, isin, DRSHB_CB_DBT.value("t_shortname_rus"), err, errtext);
            end;
	    if ( error != "") 
	      errtext(err) = error;
	      err = err + 1;
	    end;

	    RepFooter(FI.rec.fiid, isin, DRSHB_CB_DBT.value("t_shortname_rus"), err, errtext);
	    if (rez == 0)
	     // println("Ценная бумага успешно добавлена в справочник");
	    else
	     // println("Ценная бумага не добавлена в справочник");
	    end;

          end;
        end;
      end;
      ii=ii+1;
   end;


/*
    
    if (rez != 0) 
      RepFooter(0, isin, DRSHB_CB_DBT.value("t_shortname_rus"), err, errtext);
      SetOutPut(oldfile, true);
      Close(FileName);
      Viewfile(FileName);
      return 0;
    end;

    // ЗагрузитьКотировки(FI.rec.fiid);
    // ПоставитьНаОбслуживаниеВДепо(FI.rec.fiid, @error);




    if ( error != "") 
      errtext(err) = error;
      err = err + 1;
    end;

    RepFooter(FI.rec.fiid, isin, DRSHB_CB_DBT.value("t_shortname_rus"), err, errtext);
    if (rez == 0)
      println("Ценная бумага успешно добавлена в справочник");
    else
      println("Ценная бумага не добавлена в справочник");
    end;
*/

    SetOutPut(oldfile, true);
    Close   (FileName);
    Viewfile(FileName);
    return FI.rec.fiid;
  end;
END;


     ConvAv = Convert_Avoiriss();
     ConvAv.Main();



onerror()
MsgBoxEx("Ошибка функции");
exit(1);


