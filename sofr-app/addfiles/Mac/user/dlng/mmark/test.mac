/*
$Name:       rep0011.mac
$Module:     БОЦБ
$Код формы:  L11 
$Description:Список клиентов, у которых не соответствует налоговый статус и гражданство
$Changed:    18.09.2017
*/
import rsd, RsbDataSet, RsbFormsInter;
import or_rep_h, SecLib;

private const FT_DATE = 9;

const FMT_ALLBORDERS = ":ex_B(blrt)";
const TabName      = "Нал.статус";
const RepShortName = "Налоговый статус";
const RepFullName  = "Список клиентов, у которых не соответствует налоговый статус и гражданство";

//***************************************************************
CLASS (TRSBPanel) T_Param_Panel

 var EFRepDate = TArray;

  macro Init()
    SetSize(32, 6);
    SetPosition(8, 4);
    Caption = RepShortName;
    Status  = " Esc Выход  F3 Справочник  F2(F9) Выполнить";

    AddEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "OnKeyPressed"));

    for(var i, 0, 1)
      addLabel(TRsbLabel(2, 2+i, "Дата "+ iif(i==0, "начала", "окончания")));
      EFRepDate[i] = TRsbEditField;
      EFRepDate[i].name       = string("RepDate", i+1);
      EFRepDate[i].dataType   = FT_DATE;
      EFRepDate[i].textLength = 10;
      EFRepDate[i].setPosition(16, 2+i);
      EFRepDate[i].setSize    (10, 1);
      EFRepDate[i].Editable   = true;
      EFRepDate[i].Value      = {curdate};
      addControl(EFRepDate[i]);
    end;
  end;

  MACRO Check_Data()
    if(EFRepDate[0].Value > EFRepDate[1].Value)
      MsgBox("Дата начала периода превышает дату окочнания");
      return false;
    end;
    Return TRUE;
  END;

  macro PrintReport()
    var sqlStr = "", rs, cmd, rep, LimitDiff,
        header = CHeader(),
        PrevTabsExist = false,
        CellFormat;

    BegAction(0, "Формрование отчета");

    header.AddChild("Номер п/п"                             ,  5);
    header.AddChild("ФИО клиента"                           , 35);
    header.AddChild("Основной документ клиента"             , 35);
    header.AddChild("Брокерский договор"                    , 15);
    header.AddChild("Депозитарный договор"                  , 15);
    header.AddChild("Гражданство (резидени/нерезидент)"     , 15);
    header.AddChild("Налоговый статус (из карточки клиента)", 20);
    header.AddChild("Дата выдачи"                           , 10);
    header.AddChild("Дата истечения срока действия"         , 10);
    header.AddChild("Комментарий"                           , 20);
                    
    rep = CMakeReport(header.CreateHeader());
    StartCapture();

//******************************************
[
with p as (select :Date1 date1, :date2 date2, :OurBank OurBank from dual),
    s1 as (
select pt.t_partyid,
       ps.t_Name1 || ' ' || ps.t_Name2  || ' ' || ps.t_Name3 ClientName,
       case when idc.t_paperseries || idc.t_papernumber is null then chr(1)
       else pk.t_name || ' ' || idc.t_paperseries ||  '-' || idc.t_papernumber || ' выдан ' || idc.t_paperissuer
       end ClientIDC,
       nvl(idc.t_paperissueddate, to_date('01.01.0001')) StartDate, 
       nvl(idc.t_validtodate, to_date('01.01.0001')) EndDate,
       decode(pt.t_notresident, 'X', 'не', null) || 'резидент' ResType,
       oac.t_sysdate TaxStateDate,
       max(oac.t_sysdate) over (partition by oac.t_objecttype, oac.t_groupid, oac.t_attrid, oac.t_object) MaxTaxStateDate,
       oa.t_name TaxStateName,
       nvl(scse.t_number, chr(1)) ContractNumberSE,
       nvl(scdepo.t_number , chr(1)) ContractNumberDepo
      
  from dparty_dbt pt
inner join p on 1=1
inner join dpersn_dbt ps on ps.t_personid = pt.t_partyid
inner join dpartyown_dbt pown on pown.t_partyid = pt.t_partyid AND pown.t_partykind = 1
left join dpersnidc_dbt idc on idc.t_personid = pt.t_partyid AND idc.t_ismain = 'X'
left join dpaprkind_dbt pk on pk.t_paperkind = idc.t_paperkind
left join dobjatcor_dbt oac on oac.t_objecttype = 3
      AND oac.t_groupid =  42
      AND oac.t_object = lpad(to_char(pt.t_partyid), 10, '0')
      AND p.date2 between oac.t_validfromdate AND oac.t_validtodate
left join dobjattr_dbt oa on oa.t_groupid = oac.t_groupid AND oa.t_objecttype = 3 AND oa.t_attrid = oac.t_attrid
left join dsfcontr_dbt scse
 on scse.t_partyid = pt.t_partyid
   AND scse.t_contractorid = p.ourBank
   AND scse.t_servkind in (1, 15)
left join dsfcontr_dbt scdepo
 on scdepo.t_partyid = pt.t_partyid
   AND scdepo.t_contractorid = p.ourBank
   AND scdepo.t_servkind = 7
  where pt.t_legalform = 2
    AND exists 
(select 1
   from dclient_dbt cl
  where cl.t_partyid = pt.t_partyid
    AND cl.t_servicekind in (1, 7, 15))
  AND exists
(select 1 
   from dobjatcor_dbt oac
  where oac.t_objecttype = 3
    AND oac.t_groupid =  42
    AND oac.t_object = lpad(to_char(pt.t_partyid), 10, '0')
   AND NOT (oac.t_validtodate < p.date1 OR p.date2 < oac.t_validfromdate)
    AND ((oac.t_attrid = 1 and pt.t_notresident = 'X') OR
         (oac.t_attrid between 2 AND 6 and pt.t_notresident = chr(0)))))
         
select *
  from s1
 where s1.TaxStateDate = MaxTaxStateDate
];
    SqlStr = EndCapture();
    cmd = rs = null;
    cmd = RsdCommand(SqlStr);
    cmd.addParam("date1", RSDBP_IN, EFRepDate[0].Value);
    cmd.addParam("date2", RSDBP_IN, EFRepDate[1].Value);
    cmd.addParam("OurBank", RSDBP_IN, {OurBank});
    rs = RsdRecordSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);

    rep.AutoScan("[\n#\nза период с ############ по ############\n\n]",
                 RepFullName, "c",
                 EFRepDate[0].Value, "",
                 EFRepDate[1].Value, "");
    for(var i, 1, 10)
      Rep.AddPrintCell(i, 0, 0, FMT_ALLBORDERS+":c");
    end;
    rep.AddStr();

    while (rs.MoveNext)
      Rep.AddPrintCell(KEY_AUTOINC,   0, 0, FMT_ALLBORDERS);
      Rep.AddPrintCell(rs.Value("ClientName"),  0, 0); 
      Rep.AddPrintCell(rs.Value("ClientIDC"),  0, 0);  
      Rep.AddPrintCell(rs.Value("ContractNumberSE"),  0, 0);
      Rep.AddPrintCell(rs.Value("ContractNumberDEpo"),  0, 0);
      Rep.AddPrintCell(rs.Value("ResType"),  0, 0);
      Rep.AddPrintCell(rs.Value("TaxStateName"),  0, 0);
      Rep.AddPrintCell(SQL_ConvTypeDate(rs.Value("StartDate")),  0, 0); 
      Rep.AddPrintCell(SQL_ConvTypeDate(rs.Value("EndDate")),  0, 0);
      Rep.AddPrintCell("",  0, 0);
      rep.AddStr();
    end;

/*
    rep.AutoScan("[\n\nИсполнитель                                         /______________________/   "
                 "\n\nДата создания отчета: ###########\n]",
      EFRepDate.Value, "");
*/

    Rep.PrintWinRep(TabName);
    Rep.ShowWinRep();

    EndAction();
    MsgBox("Отчет сформирован");

  end;

  MACRO OnKeyPressed(RsbEvent:Object)
    if ((RsbEvent.KeyCode == 316) OR  // F2
        (RsbEvent.KeyCode == 323))    // F9
      if (Check_Data())
        PrintReport();
      end;
    elif(RsbEvent.KeyCode == 317)
      if(index(RsbEvent.source.Name, "Date"))
        RsbEvent.source.Value = GetDateByCalendar(RsbEvent.source.Value);
      end;
    end;
  END;

  InitTRsbPanel();
  Init();
END;  // T_Param_Panel

/*****************************

******************************/
T_Param_Panel.Run();
exit(1);
