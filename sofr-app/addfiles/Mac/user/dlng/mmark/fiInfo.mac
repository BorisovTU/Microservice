/*-------------------------------------------------------------------------

	Имя файла	: fiInfo.mac 

	Описание	: Информация о финансовых инструментах

	Программист	: Селезнёв Роман

	Создан		: 11.10.2007


-------------------------------------------------------------------------*/


import oralib, likepy, globals, FIInter;

// Финансовые инструменты (справочник dfininstr_dbt)            
const фиНациональнаяВалюта   = 0;   // Национальная валюта (Российский рубль)


//
// Получить финансовый инструмент
//
macro GetFinInstrParams( FIID, Ccy, ISO_Number, FI_Code, Name )
   var t_Ccy = "", t_ISO_Number = "", t_FI_Code = 0, t_Name="", ok = false;
   var sql = "select t_Ccy, t_ISO_Number, t_FI_Code, t_Name from dfininstr_dbt where t_FIID = :fi";

   var params:TArray;
   params = MakeArray(SQLParam("fi", FIID));

   var res = ExecSQLSelect(sql, params, false); 

   if(res.MoveNext())
      t_Ccy = res.value(0);
      t_ISO_Number = res.value(1);
      t_FI_Code = res.value(2);
      t_Name = res.value(3);
      ok = true;
   end;

   SetParm( 1, t_Ccy );
   SetParm( 2, t_ISO_Number );
   SetParm( 3, t_FI_Code );
   SetParm( 4, t_Name );       

   return ok;
end;


// 
// Получить буквенный код ISO валюты (например, RUR)
//
macro CurrencyISOLet(PFI:Integer):String
  var Ccy = "";

  if(GetFinInstrParams( PFI, Ccy ))
     return Ccy;
  end;

  return "___";
end;


//
// Получить цифровой код ISO валюты
//
macro CurrencyISONum(PFI:Integer):Integer
  var Ccy = "", nISO = 0;

  if(GetFinInstrParams( PFI, Ccy, nISO ))
     return nISO;
  end;

  return "000";
end;


//
// Получить код финансового инструмента
//
macro CurrencyFICode(PFI:Integer):String
  var Ccy="", nIso=0, Code=0;

  if(GetFinInstrParams( PFI, Ccy, nIso, Code)) 
     return Code;
  end;

  return "000";

end;



//
// Получить название валюты, например "Российский рубль"
//     
macro CurrencyName(PFI:Integer):String
  var Name = "", ccy="", nIso=0, Code=0;

  if(GetFinInstrParams( PFI, Ccy, nIso, Code, Name )) 
     return Name;
  end;

  return "";

end;


//
// Получить fiid по имени финансового инструмента
//
macro GetFiidByName(name:String):Integer
   var sql = "select t_fiid from dfininstr_dbt where t_name = :name";

   var params:TArray;
   params = MakeArray(SQLParam("name", name));

   var res = ExecSQLSelect(sql, params, false); 

   if(res.MoveNext())
      return res.value(0);
   end;

   return -1;
end;


//
// Получить курс ЦБ РФ на дату
//
macro ПолучитьКурсЦБРФ (Rate, SourceFI, DateR, Type)
    var stat;
    record RATEDEF ( ratedef ); 
                
    if(фиНациональнаяВалюта == SourceFI)
       SetParm (0, $1);
       return 0;
    end;     

    // По умолчанию дата сегодняшняя
    if ( ValType (DateR) == V_UNDEF )
       DateR  = {curdate};
    end;   

    // Получение параметров курса
    if ( ValType (Type) == V_UNDEF )
        stat = ПолучитьКурс (RATEDEF, фиНациональнаяВалюта, SourceFI);
    else
        stat = ПолучитьКурс (RATEDEF, фиНациональнаяВалюта, SourceFI, Type);
    end;

    if ( stat != 0 ) 
	return 1; 
    end;

    // Получение текущего значения курса   
    stat = ПолучитьЗначениеКурса (RATEDEF, DateR);
    
    if ( stat != 0 ) 
       return 1; 
    end;

    SetParm (0, RATEDEF.RATE/Pow(10, RATEDEF.POINT));
    return 0;
end;     


//
// Конвертировать финансовый инструмент в другой финансовый инструмент
//
macro КонвертФИ (изФИ, вФИ, Сколько, Результат, Дата)
    var stat;
 
    if(изФИ == вФИ)
       SetParm(3, Сколько);
       return 0;
    end;
    
    // По умолчанию дата сегодняшняя
    if ( ValType (Дата) == V_UNDEF )
       Дата  = {curdate};
    end;   
                                        
    stat = ConvSum(Результат, Сколько, Дата, изФИ, вФИ, 0);
    if (stat == 0)
        SetParm(3, Результат);
    else
        if (Дата != Date (0,0,0))
           MsgBox("Не найден курс ",ПолучитьКодФинИн(изФИ), " относительно ",ПолучитьКодФинИн(вФИ), " за ", Дата);
        else
           MsgBox("Не найден курс ",ПолучитьКодФинИн(изФИ), " относительно ",ПолучитьКодФинИн(вФИ), " за 00.00.0000");
        end;
    end;

    return stat;
end;

