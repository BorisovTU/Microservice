/*******************************************************************************
 DESCRIPTION  :   Создание заявок/поручений по журналу срочного рынка
 PROGRAMMED BY:   Levchenko
 CREATION DATE:   10.10.18
*******************************************************************************/

import oralib, likepy, ptinter, globals, fiinter, rcw, oprinter;

const KIND          = 350, //Тип документа в связке заявки-поручения
      ПоручКлНаОпер = 251,
      FORTS         = 192, //Срочный рынок
      DOCLOG_SECUR  = 513,
      DOCLOG_FORTS  = 250;



file infile () txt;

var PathIn   ="..\\_gate\\"     ,
    PathArh  ="..\\_gate\\Arh\\";
    
Private Macro NotNull ( val )
    if ( val == NULL ) return false; end;
    if ( ValType (val) == 26 ) return false; end;
    return true;
End;

Private macro OpenExcel
    return CreateObject ("rsax", "TRsAxServer", "RsBankAxServer", isStandAlone ()).CreateComObject ("Excel.Application");
OnError
    return NULL;
End;

Private macro OpenExcelFile ( fName )
    Var ex = OpenExcel ();
    if ( ex == NULL )
        MsgBox ("Ошибка открытия MS-Excel");
        return NULL;
    end;
    ex.Workbooks.Open (fName);
    return ex;
OnError
    ex = NULL;
    MsgBox ("Ошибка открытия файла ", fName);
    return NULL;
End;

Private macro CnvInt (text)
    var ret = string (text), ind = index (ret,".");
    if (ind == 0)
       return ret;
    else
       return substr (ret, 1, ind-1);
    end;
End;


macro IdentifyDirection (OperType)
   var type = 0;

   if (OperType == "B") 
      type = 1;
   elif (OperType == "S")
      type = 2
   end;

   return type;
End;


macro ПроверитьПоручениеНаОперацию(DealID:integer, MarketType)
   var spgroundid = 0;
   var sql = "select spground.t_spgroundid id  "
           + "from dspgrdoc_dbt grdoc, dspground_dbt spground "
           + "where grdoc.t_sourcedockind = :dockind "
           + "      and grdoc.t_sourcedocid = :dealid "
           + "      and spground.t_kind = :ordertype "
           + "      and spground.t_spgroundid = grdoc.t_spgroundid ";
   sql = ExecSqlSelect(sql, MakeArray(SqlParam("dockind",   MarketType    ),
                                      SqlParam("dealid",    DealID        ),
                                      SqlParam("ordertype", ПоручКлНаОпер )), false);
   if (sql.MoveNext() )
      spgroundid = sql.value("id");
   end;
   return spgroundid;
End;


macro СоздатьПоручениеНаОперацию (DealID,
                                  Code,
                                  OrderDate,
                                  OrderTime,
                                  ContrAgent,
                                  codets,
                                  doclog,
                                  MarketType,
                                  xld
                                 )
   var Spground = Tbfile ("spground", "W");
   var Spgrdoc  = Tbfile ("spgrdoc", "W");
   var partyname = GetPartyNames (ContrAgent, PTKN_FULLNAME, false);
   var spgroundid = 0;

   partyname = partyname(0);
   spgroundid = ПроверитьПоручениеНаОперацию(DealID, MarketType);

   if (spgroundid > 0) //Поручение для данной сделки уже существует!
      return spgroundid;
   end;

   Spground.rec.doclog       = doclog;     //Не знаю что это, и почему такое значение
   Spground.rec.kind         = ПоручКлНаОпер;
   Spground.rec.direction    = 1;          //0-взодящий, 1-исходящий, 2-исходящий и отправлен
   Spground.rec.xld          = xld;       //Номер в банке
   Spground.rec.registrdate  = OrderDate;  //Дата регистрации
   Spground.rec.registrtime  = Time;  //время регистрации
   Spground.rec.party        = ContrAgent; //Контрагент по поручению.
   Spground.rec.altxld       = xld;     //Номер у составителя/получателя (внешний)
   Spground.rec.signeddate   = OrderDate;  //Дата с которой действует поручение
   Spground.rec.registrtime  = time;  //время. Скорей всего не обязательно
   Spground.rec.receptionist = {oper};
   Spground.rec.partyname    = partyname;  //Наименование контрагента
   Spground.rec.partycode    = ПолучитьКодСубъекта(ContrAgent, PTCK_CONTR); //Код контрагента
   Spground.rec.department   = 1;          //филиал. пока всегда 1
   Spground.rec.branch       = 1;
   
   if (not Spground.insert()) 
      return 0;
   end;

   spgroundid = Spground.rec.spgroundid;

   if (DealID == 0) 
      return spgroundid;
   end;
   Spgrdoc.rec.sourcedockind = MarketType;
   Spgrdoc.rec.sourcedocid   = DealID;
   Spgrdoc.rec.spgroundid    = spgroundid;
   if (not Spgrdoc.insert()) 
      spgroundid = 0;
   end;

   return spgroundid;
End;

macro СоздатьНовоеПоручение (code,             //код
                             codets,           //код внешний
                             OrderDate,        //Дата
                             OrderTime:string, //Время заявки
                             ContrAgent,       //Контрагент
                             Client,           //Клиент
                             fiid,             //Ценная бумага
                             Amount,           //Количество ц/б в штуках
                             Price,            //цена за 1 ц/б
                             pricetype,        //Тип цены. Для облигаций в %, для срочных не указан, для остальных обычный
                             PriceFiID,        //Валюта (цены за 1 ц/б)
                             DealID,           //ИД сделки
                             Currency,         //Валюта рассчётов
                             Direction,        //Направление. Значения описаны в начале макроса
                             ClientContr,      //Ид договора клиента
                             RepoRate,         //Ставка РЕПО
                             IsExchange,       //Признак биржи
                             pfikind,          //Вид производного финансового инструмента
                             doclog,
                             MarketType,
                             xld,
                             MarketKind,       //Вид рынка
                             MarketID          //Идентификатор биржи
                            )
   var reqid = 0;
   var req     = TBFile("dl_req", "W");
   var Spgrdoc = Tbfile ("spgrdoc", "W");
   var spgroundid;
   var sql;

   sql = ExecSqlSelect("select t_id id from ddl_req_dbt where t_kind = :kind and t_code = :code and t_marketkind = :marketkind and t_marketid = :marketid", 
                       MakeArray(SqlParam("kind", KIND), SqlParam("code", code), SqlParam("marketkind", MarketKind), SqlParam("marketid", MarketID)), 
                       false
                      );

   if(sql.MoveNext()) 
      return int(sql.value("id"));
   end;

   rslDefCon.beginTrans();

   spgroundid = СоздатьПоручениеНаОперацию(DealID,
                                           Code,
                                           OrderDate,
                                           OrderTime,
                                           ContrAgent,
                                           codets,
                                           doclog,
                                           MarketType,
                                           xld
                                           );
   if (spgroundid == 0)
      if ( rslDefCon.isInTrans () )
         rslDefCon.rollbackTrans ();
      end;
      return 0;
   end;

   req.rec.kind        = KIND;
   req.rec.code        = code;
   req.rec.codets      = codets;
   req.rec.date        = OrderDate;
   req.rec.time        = Time;
   req.rec.client      = Client;
   req.rec.party       = ContrAgent;
   req.rec.fiid        = fiid;
   req.rec.Amount      = Amount;
   req.rec.Price       = Price;
   req.rec.PriceFiID   = PriceFiID;
   req.rec.sourcekind  = MarketType;
   req.rec.SourceID    = DealID;
   req.rec.CurrencyID  = Currency;
   req.rec.IsExchange  = IsExchange;
   req.rec.Direction   = Direction;
   req.rec.ClientContr = ClientContr;
   req.rec.RepoRate    = RepoRate;
   req.rec.pfikind     = pfikind;
   req.rec.methodapplic = 1; //???
   req.rec.MarketKind  = MarketKind; 
   req.rec.MarketID    = MarketID; 

   if (DealID > 0) 
      req.rec.status = "M"; //(М = заключена сделка)
   end;

   if (not req.Insert() )
      reqid = 0;
   else
      reqid = req.rec.id;
   end;

   Spgrdoc.clear();
   Spgrdoc.rec.sourcedockind = KIND; 
   Spgrdoc.rec.sourcedocid   = reqid;//ид из dl_req
   Spgrdoc.rec.spgroundid    = spgroundid; //ид документа в dspground_dbt

   if (not Spgrdoc.insert())
      reqid = 0;
   end;

   if (reqid > 0)
      rslDefCon.commitTrans ();
   else
      rslDefCon.rollbackTrans ();
   end;

   return reqid;

OnError ( err )
   if ( rslDefCon.isInTrans () )
      rslDefCon.rollbackTrans ();
   end;
   return 0;
End;

macro getFi (id)

   var ret ="";
   var req = rsdcommand ("select t_avoirkind from dfininstr_dbt f where f.t_fiid = :id");
   req.AddParam (":id", RSDBP_IN, id);
   var y = rsdrecordset (req);
   if (y.movenext)
      ret = y.value (0);
   end;
   return ret;

End;

macro Check_req (id)
   var sql = rsdcommand ("select * from ddl_req_dbt ddl_req where ddl_req.t_code = :id");
   sql.AddParam (":id", RSDBP_IN, id);

   var z = rsdrecordset (sql);

   if (z.MoveNext())
      println ("Сделка с кодом "+id+" уже заведена");
      return FALSE;
   end;
   return TRUE;
End;


macro Поручения (id, numb)
   var stat;
   var fininstr = TRecHandler ("fininstr");

   var Code;
   var Codets = "tst";
   var OrderDt;
   var OrderTime;
   var Client;
   var ContrAgent = 2; //ММВБ
   var fiid;
   var Amount;
   var Price;
   var DealID;
   var PriceFiID = 0;
   var Currency = 0;
   var Direction;
   var ClientContr = 2;
   var price2 = 0;
   var pfikind = -1;
   var OperType;
   var RepoRate;
   var doclog;
   var MarketType;
   var pricetype;
   var IsExchange = "X";
   var count = 0;
   var MarketKind = DV_MARKETKIND_DERIV;
   var MarketID = ContrAgent;

   var sql = rsdcommand ("select * from ddvdeal_dbt ddvdeal where ddvdeal.t_code = :id");
   sql.AddParam (":id", RSDBP_IN, id);

   var x = rsdrecordset (sql);

   if (x.MoveNext() )
      fininstr.clear();

      DealID     = x.value("t_id");
      Code       = x.value("t_code");
      Codets     = x.value("t_extcode");
      Price      = x.value("t_price");
      Amount     = x.value("t_amount");
      OrderTime  = time;
      OrderDt    = x.value("t_date");
      Client     = x.value("t_client");
      Fiid       = x.value("t_fiid");
      ClientContr= x.value("t_clientcontr");

      doclog     = DOCLOG_FORTS;
      MarketType = FORTS;
      pfikind    = getFi (fiid);
      PriceFiID  = -1;
      Currency   = -1;
      Pricetype  = -1;
      RepoRate   = 0;

      Direction = IdentifyDirection(x.value("t_type"));

      stat = СоздатьНовоеПоручение(Code,
                                   Codets,
                                   OrderDt,
                                   Time,
                                   ContrAgent,
                                   Client,
                                   fiid,
                                   Amount,
                                   Price,
                                   pricetype,
                                   PriceFiID,
                                   DealID,
                                   Currency,
                                   Direction,
                                   ClientContr,
                                   RepoRate,
                                   IsExchange,
                                   pfikind,
                                   doclog,
                                   MarketType,
                                   numb,
                                   MarketKind,
                                   MarketID
                                  );

      println(stat);
   end;
End;




Private macro LoadFMarket  (ex, EndLine)

    Var str = "", error = FALSE, sql, cnt = 0;
    Var Line = 2, Col = 1, EndCol = 0;
        
    Var N1 = ""; //Номер поручения                      
    Var N2 = ""; //Дата получения поручения             
    Var N3 = ""; //Время получения поручения (чч.мм.сс) 
    Var N4 = ""; //Наименование (ФИО)/Код клиента       
    Var N5 = ""; //ФИО работника,                       
    Var N6 = ""; //Способ подачи поручения              
    Var N7 = ""; //Наименование электронной системы     
    Var N8 = ""; //номер сделки                         
    Var N9 = ""; //контракт                             
    Var N10= ""; //цена                                 
    Var N11= ""; //количество                           
    Var N12= ""; //направление сделки (buy/sell)        

    EndLine = EndLine + Line;
    InitProgress (EndLine - Line, NULL, "Чтение файла");
    while ( (Line=Line+1) < EndLine )
        UseProgress (cnt = cnt+1);
        if ( NotNull (ex.Cells (Line, Col+1).Value) )
            N1 = CnvInt (ex.Cells (Line, Col).Value);          
            N2 = string (date(ex.Cells (Line, Col+1 ).Value)); 
            N3 = ex.Cells (Line, Col+2 ).Value;                
            N4 = trim (string(ex.Cells (Line, Col+3 ).Value)); 
            N5 = {oper};
            N6 = trim (string(ex.Cells (Line, Col+5 ).Value)); 
            N7 = trim (string(ex.Cells (Line, Col+6 ).Value)); 
            N8 = trim (string(ex.Cells (Line, Col+7 ).Value)); 
            N9 = trim (string(ex.Cells (Line, Col+8 ).Value)); 
            N10= trim (string(ex.Cells (Line, Col+9 ).Value)); 
            N11= trim (string(ex.Cells (Line, Col+10).Value)); 
            N12= trim (string(ex.Cells (Line, Col+11).Value)); 
            if (Check_req (N8) )
                Поручения (N8, N1);
            end;
        end;
    end;
    RemProgress ();

    ex.Quit ();
    ex = NULL;
End;


Private macro LoadData

    Var fName, ex, cnt = 0;

    if ( not SelectFile (fName, (PathIn+"*.*"), NULL, NULL, TRUE) )
        return FALSE;
    elif ( (ex = OpenExcelFile (fName)) == NULL )
        return FALSE;
    end;

    Var Line = ex.ActiveSheet.UsedRange.Row,
        EndLine = ex.ActiveSheet.UsedRange.Rows.Count,
        Col = ex.ActiveSheet.UsedRange.Column,
        EndCol = 0;

    while ( NotNull (ex.Cells (Line, Col+EndCol).Value) )
       Var Dprt = String (ex.Cells (Line, Col+EndCol).Value);
       EndCol = EndCol + 1;
    end;

    Var FMarket = 8;  //Срочный рынок - колонок в файле
    Var NMarket = 7;  //Внебиржевой рынок - колонок в файле

    if   ( EndCol == FMarket ) 
       if (msgboxex ("Загрузка заявок по журналу Срочного рынка ?", MB_YES+MB_NO, IND_YES)==IND_YES)
           LoadFMarket ( ex, EndCol );
       else
           msgboxex ("Отказ от загрузки заявок по журналу Срочного рынка");
           ex.Quit ();
           ex = NULL;
           return FALSE;
       end;
    else
       MsgBox ("Нарушена структура файла.|Заголовок таблицы не содержит необходимое количество колонок "+EndCol);
       ex.Quit ();
       ex = NULL;
       return FALSE;
    end

End;

LoadData;