/************************************************************************************************************
  docsign.mac
  Макрос -обработчик списка подписантов со стороны банка

  Автор: 	RSSL, Горленко В.
  Изменения:
************************************************************************************************************/

import "oratools.mac", "globals.mac";

Private const kbF2 = 316, kbF3 = 317, kbEnter = 13, kbESC = 27;
//-----------------------------------------------------------------------------------------

Class (TRecHandler) CDocumentSign ( _docName )
    InitTRecHandler ("sign", "sign.lbr", TRUE);
    Var Done = FALSE, docName = StrUpr (_docName);

    Macro scrHandler ( rs, cmd, id, key )
        if ( (cmd == DLG_KEY) and (key == kbEnter) and (rs.RecCount) )
            return CM_SELECT;
        end;
    End;

    Macro ListOfiicer (show_all)
//        Var sql = "select (select t_shortname from dparty_dbt where t_partyid = t_personid) t_name, t_post, ' ' as t_br from dofficer_dbt where t_post != chr (1) and t_partyid = "+{OurBank}+" order by (select t_shortname from dparty_dbt where t_partyid = t_personid)";

// подписаны с учетом ВСП, убраны сотрудники, кто не имеет права подписи (= категория "Право подписи" на субъекте, связанном с сотрудником)
        Var sql = "select t_shortname as t_name, t_post, p.t_codedepart as t_br \n"+
		"from dofficer_dbt o \n"+
		"inner join dparty_dbt dp on dp.t_partyid = o.t_personid  \n"+
		"left join dperson_dbt p on p.t_partyid = o.t_personid and p.t_userblocked !='X'  \n"+
		"left join ddp_dep_dbt d on d.t_code = p.t_codedepart  \n"+
		"where o.t_partyid = "+{OurBank}+" and o.t_post != chr (1)   \n"+
		"and o.t_dateto = to_date ('01.01.0001','dd.mm.yyyy')  \n"+
		"and o.t_post not in ('Администратор', 'Ревизор', 'Старший смены','Адм. безопасности')  \n"+
		"and not exists (select 1 from dobjatcor_dbt at where 1=1 and at.t_objecttype =3 and at.t_groupid=507 and at.t_attrid =2 and lpad(dp.t_partyid,10,'0') = at.t_object)   \n"+
		"and p.t_codedepart = "+ {OperDprtNode}+"  \n"+
		"order by t_name \n"; 

        sql = ExecSqlSelect (sql, null, FALSE, RSDVAL_CLIENT, RSDVAL_STATIC);

        if ( RunScroll (sql, 3, MakeArray ("t_name", "ФИО", 20, 0, 20, 0, "t_post", "Должность", 60, 0, 40, 0, "t_br", "ВСП", 3, 0, 0, 0),
                        "BANKOFFICER_1", R2M (this, "scrHandler"), "Список сотрудников "," "," ", string({OperDprtNode}), "Enter Выбор  ESC Отмена", TRUE, 6, 7) )
            SetParm (2, OraString (sql.value ("t_name")));
            SetParm (3, OraString (sql.value ("t_post")));
            return TRUE;
        end;
        return FALSE;
    End;

    
    
    Macro Handler ( dlg, cmd, id, key )

        if ( (cmd == DLG_KEY) and (key == kbF3) and InList (id, FldIndex ("Name1"), FldIndex ("Name2")) and ListOfiicer (true, dlg.(id), dlg.(id+1)) )    
            UpdateFields (dlg);

        elif ((cmd == DLG_KEY) and (key == kbESC))
                 msgbox("Подписанты не определены!");
            return CM_CANCEL;

        elif ( (cmd == DLG_SAVE) and (not Done) )
            return CM_CANCEL;

        elif ( (cmd == DLG_KEY) and (key == kbF2) )
            if ( (strlen(this.("Name1")) == 0) or strlen(this.("Name2") == 0) )
                 msgbox("Не все подписанты определены!");
                 return CM_IGNORE;
            end;

            UpdateFields (dlg);
            Done = TRUE;
            return CM_SAVE;
        end;
    End;


    Macro Init
//
    End;

    
    Macro Run
        Init ();
        return RunDialog (this, R2M (this, "Handler"));
    End;
End;
//-----------------------------------------------------------------------------------------
