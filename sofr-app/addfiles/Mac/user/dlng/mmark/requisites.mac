/*-------------------------------------------------------------------------

	Имя файла	: requisites.mac 

	Описание	: Работа с банковскими реквизитами 

	Программист	: Селезнёв Роман

	Создан		: 29.02.2008


-------------------------------------------------------------------------*/


import PTInter, oralib, likepy, dealInfo, dlgCommon;


//
// Типы банковских систем (расчёта)
//
const bstCB 	= 0,   
      bstSWIFT	= 1;


//
// Типы сторон в сделке (дебет и кредит)
//
const dcDebet   = 0,
      dcCredit	= 1;



//
// Класс по работе с кодом SWIFT
//
class КодSwift (swiftCode)
   private var m_swiftCode    = swiftCode;
   private var m_finOrgCode   = SubStr(m_swiftCode, 1, 4);	// 4 символа
   private var m_countryCode  = SubStr(m_swiftCode, 5, 2);	// 2 символа
   private var m_placeCode    = SubStr(m_swiftCode, 7, 2);	// 2 символа
   private var m_addCode      = "";                             // 3 символа

   if(StrLen(swiftCode) > 8)
      m_addCode = SubStr(m_swiftCode, 9);
   end;
  
  
   // Получить Всемирный четырёхбуквенный код финансовой организации
   macro ПолучитьВсемирныйКодФинансовойОрганизации()
      return m_finOrgCode;  
   end;

   // Получить двухбуквенный код страны в соответствии со стандартами ISO 
   // (например: RU - Российская Федерация, US - Соединенные Штаты Америки, GB - Великобритания)
   macro ПолучитьКодСтраны()
      return m_countryCode;
   end;

   // Получить двухбуквенный код местоположения финансовой организации 
   // (например: MM - Москва, 2Р - Санкт-Петербург, 2L, 21 - Лондон)
   macro ПолучитьКодМестоположения()
      return m_placeCode;
   end;

   // Получить трёхбуквенный вспомогательный код 
   // (для финансовой организации, не являющейся пользователем SWIFT, проставляется буквенный код BIC; 
   // для пользователя SWIFT трёхбуквенный код может быть использован для идентификации его конкретного местоположения 
   // в стране или для филиала/отделения организации)
   macro ПолучитьВспомогательныйКод()
      return m_addCode; 
   end;
   
   // Проверка на наличие вспомогательного кода
   macro ЕстьВспомогательныйКод()
      if("" == m_addCode)
         return false;
      else
         return true;
      end;
   end;

   // Форматирование кода SWIFT путём отделения пробелами составляющих кодов   
   macro Формат()
      var res = m_finOrgCode + " " + m_countryCode + " " + m_placeCode;

      if (this.ЕстьВспомогательныйКод()) // Если есть вспомогательный код
         res = res + " " + m_addCode; 
      end;

      return res;
   end;

   // Получить полный код SWIFT
   macro ПолучитьКодSwift()
      return m_swiftCode;
   end;
end;
   


//
// Отформатировать код SWIFT
//
macro ФорматКодSwift(swiftCode)
   var sw = КодSwift(swiftCode);

   return sw.Формат();
end;


//
// Получить название РКЦ по его БИК РКЦ 
//
macro ПолучитьИмяРКЦпоБИК(rcc)
   var res; 
   var sql = " select part.t_name ""РКЦ"" "
          +" from dObjkCode_dbt kind " 
          +"     inner join dObjCode_dbt code " 
          +"     on kind.t_codeKind = code.t_codeKind and kind.t_codeKind=3 and code.t_code = :rcc " 
          +"        inner join dParty_dbt part " 
          +"        on part.t_partyId=code.t_objectId " ;

   var params:TArray;
   params = MakeArray(SQLParam("rcc", rcc));
   res = ExecSQLSelect(sql, params, false);

   if(res.MoveNext())
      return res.value(0);
   end;
 
   return "";
end;

//
// Получить корреспондентский счёт по id участника (стороны) сделки (к/с ищется через дистрибутивный механизм)
//
macro ПолучитьКоррСчёт(corAcc, bicRcc, rccName, partyId)
   var res; 

   var sql = " select bank.t_corAcc, bank.t_bic_rcc  "
          +" from dBankDprt_dbt bank " 
          +" where bank.t_partyId=:pty " ;

    var params:TArray;
   params = MakeArray(SQLParam("pty", partyId));
   res = ExecSQLSelect(sql, params, false);

   if((res.MoveNext()) and (null != res.value(0)))
      Setparm(0, res.value(0)); // Счёт
      SetParm(1, res.value(1));	// БИК РКЦ        
        if(null != res.value(1))
           SetParm(2, ПолучитьИмяРКЦпоБИК(res.value(1)));
        end; 
      return true;
   end;

   return false;
end;


//
// Получить свойства платежа  
//
private macro GetPMProp(pmntId, debCred)
   var sql = " select * from dPmProp_dbt where t_paymentId=:pmnt and t_debetCredit=:debcred ";
 
   var params = MakeArray(SQLParam("pmnt", pmntId), SQLParam("debcred", debCred)); 
   var rsPmProp = ExecSQLSelect(sql, params, false);
   if( rsPmProp.MoveNext() )   
      return rsPmProp;
   end;

   return null;
end;


//
// Получение корсчёта по Id участника из таблицы платёжных схем
//
macro GetCorrAccByParty(partyId, codeKind, fiid)
   var sql = " select t_corrAcc from dstd_req_dbt " 
           + " where t_code = :code and t_codeKind = :codeKind and t_fiid = :fiid  ";
    
   var params = MakeArray(SQLParam("code", ПолучитьКодСубъекта(Int(partyId), codeKind)), SQLParam("codeKind", codeKind), SQLParam("fiid", fiid)); 
   var res = ExecSQLSelect(sql, params, false);
 
   if(res.MoveNext()) 
      return res.value(0);
   end; 

end;


//
// Получить реквизиты в системе ЦБ РФ
//
macro ПолучитьРеквизитыСистемыЦБРФ(debCred, dealId, purp)
   var party, acc, bank;	// Плательщик/получатель, к/с и банк
   var midBank, midAcc;		// Банк-посредник и к/с в банке-посреднике
   var add;			// Дополнительные посредники в примечании

   var codeKind;		// Тип кодов (BIC или SWIFT)
   var req = "";                // Строка с реквизитами
   var prop = null;		// Рекордсет со свойствами платежа dPmProp_dbt


   var sql = "select * from dPmPaym_dbt pmnt where pmnt.t_documentId=:doc and pmnt.t_docKind in (100, 102, 140) and pmnt.t_purpose=:purp ";

   var params:TArray;
   params = MakeArray(SQLParam("doc", dealID), SQLParam("purp", purp));

   var rs = ExecSQLSelect(sql, params, false);

   if(rs.MoveNext())      
      if(dcDebet == debCred)
         party = rs.value("t_payer");
	 acc   = GetCorrAccByParty(party, PTCK_BIC, rs.value("t_fiid")); //rs.value("t_payerAccount");
         bank  = rs.value("t_payerBankId");
      else
         party = rs.value("t_receiver");
	 acc   = GetCorrAccByParty(party, PTCK_BIC, rs.value("t_fiid"));//rs.value("t_receiverAccount");
         bank  = rs.value("t_receiverBankId");
      end;


      req = "к / с "+acc+" в\n"
          + diGetPartyNameById(bank) + "\n";

      prop = GetPMProp(rs.value("t_paymentId"), debCred);
      if(null != prop)                         		// Чтение без ошибок
         if("" != SpecialValue0ToEmpty(prop.value("t_CorrCode")))	// Указан посредник 

            codeKind = prop.value("t_corrCodeKind");  	// Должен быть равен PTCK_BIC
            midAcc   = prop.value("t_corrAcc");
            midBank  = ПолучитьКодСубъекта( String(prop.value("t_CorrCode")), codeKind );
            add      = Trim(String(prop.value("t_reserve")));
 
            req = req
                + "к / с "+ midAcc +" в\n"
                + diGetPartyNameById(midBank) + "\n"
                + "БИК " + ПолучитьКодСубъекта (Int(bank), PTCK_BIC) + "\n"
                + "ИНН " + ПолучитьКодСубъекта (Int(bank), PTCK_INN);

            if("" != add)
               req = req + "\n" + add;
            end;

         else						// Посредников нет	
            req = req 
                + "БИК " + ПолучитьКодСубъекта (Int(party), PTCK_BIC) + "\n"
                + "ИНН " + ПолучитьКодСубъекта (Int(party), PTCK_INN);
         end;      
      else
         MsgBox("Ошибка при получении свойств платежа");
      end;
   else
      MsgBox("Не удалось получить реквизиты ЦБ РФ.");        
   end;    
 
   return req;
end;


//
// Получить реквизиты в системе Swift
//
macro ПолучитьРеквизитыСистемыSwift(debCred, dealId, purp)
   var party, acc, bank;	// Плательщик/получатель, к/с и банк
   var midBank, midAcc;		// Банк-посредник и к/с в банке-посреднике
   var add;			// Дополнительные посредники в примечании

   var codeKind;		// Тип кодов (BIC или SWIFT)
   var req = "";                // Строка с реквизитами
   var prop = null;		// Рекордсет со свойствами платежа dPmProp_dbt


   var sql = "select * from dPmPaym_dbt pmnt where pmnt.t_documentId=:doc and pmnt.t_docKind in (100, 102, 140) and pmnt.t_purpose=:purp ";

   var params:TArray;
   params = MakeArray(SQLParam("doc", dealID), SQLParam("purp", purp));

   var rs = ExecSQLSelect(sql, params, false);

   if(rs.MoveNext())      
      if(dcDebet == debCred)
         party = rs.value("t_payer");
	 acc   = GetCorrAccByParty(party, PTCK_SWIFT, rs.value("t_fiid")); //rs.value("t_payerAccount");
         bank  = rs.value("t_payerBankId");
      else
         party = rs.value("t_receiver");
	 acc   = GetCorrAccByParty(party, PTCK_SWIFT, rs.value("t_fiid")); //rs.value("t_receiverAccount");
         bank  = rs.value("t_receiverBankId");
      end;
 
      req = "SWIFT:" + ПолучитьКодСубъекта (Int(party), PTCK_SWIFT) + "\n"
          + "acc. " +acc+" with\n"
          + diGetPartyNameById(bank) + "\n"
          + "S.W.I.F.T.: " + ФорматКодSwift( ПолучитьКодСубъекта (Int(bank), PTCK_SWIFT) );

      prop = GetPMProp(rs.value("t_paymentId"), debCred);
      if(null != prop)                         		// Чтение без ошибок
         if("" != SpecialValue0ToEmpty(prop.value("t_CorrCode")))	// Указан посредник 

            codeKind = prop.value("t_corrCodeKind");  	// Должен быть равен PTCK_SWIFT
            midAcc   = prop.value("t_corrAcc");
            midBank  = ПолучитьКодСубъекта( String(prop.value("t_CorrCode")), codeKind );
            add      = Trim(String(prop.value("t_reserve")));
 
            req = req + "\n"
                + "acc. "+ midAcc +" with\n"
                + diGetPartyNameById(midBank) + "\n"
                + "S.W.I.F.T.: " + ФорматКодSwift( ПолучитьКодСубъекта (Int(midBank), PTCK_SWIFT) );

            if("" != add)
               req = req + "\n" + add;
            end;

         else						// Посредников нет	
             
         end;      
      else
         MsgBox("Ошибка при получении свойств платежа");
      end;
   else
      MsgBox("Не удалось получить реквизиты SWIFT.");        
   end;    
 
   return req;
end;


//
// Получить реквизиты в системе ЦБ РФ или в системе SWIFT
//
macro ПолучитьРеквизиты(payerReceiver, bstSystem, dealId, purp)
 
   if (bstSWIFT == bstSystem)    	// Счета в системе SWIFT
      return ПолучитьРеквизитыСистемыSwift(payerReceiver, dealId, purp);

   elif(bstCB == bstSystem)		// Счета в системе ЦБ РФ
      return ПолучитьРеквизитыСистемыЦБРФ(payerReceiver, dealId, purp);

   else
      MsgBox("Банковская система "+bstSystem+" не существует.");
      return "";
   end;
end;


                               