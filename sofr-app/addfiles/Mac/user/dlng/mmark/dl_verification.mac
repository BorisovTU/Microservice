/* ╔══════════════════════════════════════════════════════════════════╗ */
/*         Автоматизированная банковская система RS-Bank v5.0           */
/*                 Copyright (c) R-Style Software Lab 1998              */
/*                                                                      */
/*  Имя файла        : pspoinit.mac                                     */
/*                                                                      */
/*  Описание         : Макрос инициализации операции для клиентского    */
/*                     документа                                        */
/*                                                                      */
/*  Программист      : Дудин В.А.                                       */
/*                                                                      */
/*  Создан           : 14.05.99                                         */
/*                                                                      */
/* ╚══════════════════════════════════════════════════════════════════╝ */
//import  BankInter, PSInter;
import rsd, globals, lib_lang;
import "dl_note_lib.mac";
                              
record deal (dl_tick);

record panel    (reentry, "mbk_op.lbr" ) dialog;
record errpanel (err    , "mbk_op.lbr" ) dialog;

var rs, sql;
var code = 0, DealId=0;;
var pSumma = 0,  pPercent = 0, pDate1 = date(0,0,0), pDate2= date(0,0,0);
var rSumma = 0,  rPercent = 0, rDate1 = date(0,0,0), rDate2= date(0,0,0);
var rIdRate = 0, rNameRate = "";
var pIdRate = 0, pNameRate = "";
var idFIID = 0,  NameFIID  = "";
var pidFIID = 0, pNameFIID = "";
var ridFIID = 0, rNameFIID = "";
var pWay = "", rWay = "";
var ways = "Не выбрано";
var unc  = "", xunc = "";
var vproc ="", xvproc = "";
var pid = "";
var extnum = "", pextnum = "";

Macro GetConversFromDeal (id_deal) //Вывод примечания 300 на панель верификации сделки
	var sql_conv,  rs_conv, conv;
	
	  sql_conv = "select nvl(rsb_struct.getstring (t_text),' ') text from  dnotetext_dbt "+
		     "where t_DOCUMENTID = LPAD (?, 34, 0) and t_notekind=300";
	  sql_conv = rsdcommand (sql_conv);
          sql_conv.addParam(":1", RSDBP_IN, id_deal);
          rs_conv  = rsdrecordset (sql_conv);
            if (rs_conv.movenext () )
                 conv = rs_conv.value("text");
            end;
	Return nvl(conv,"");
End;

Macro PanelErr (dlg, cmd, id, key ) 
   if (cmd == DLG_INIT)

      dlg.Summa   =pSumma  ;   
      dlg.Percent =pPercent; 
      dlg.Date1   =pDate1  ;   
      dlg.Date2   =pDate2  ;   
      dlg.pRate   =pNameRate;
      dlg.way     =pWay;
      dlg.cur     =pIdFIID;
      dlg.unc     =xunc;
      dlg.pccur   =xvproc;
      dlg.extn    =pextnum;
   
      dlg.xSumma   =rSumma  ;   
      dlg.xPercent =rPercent; 
      dlg.xDate1   =rDate1  ;   
      dlg.xDate2   =rDate2  ;   
      dlg.xRate    =rNameRate;
      dlg.xway     =rWay;
      dlg.xcur     =rIdFIID;
      dlg.xunc     =unc;
      dlg.xpccur   =vproc;
      dlg.xextn    =extnum;

      updateFields (dlg);
   
   end;
   if ( cmd == DLG_KEY )
      if ( key == 27 ) 
         return CM_CANCEL;
      else
         return CM_IGNORE;
      end;
   end;
End;

var IdRate=0, NameRate="";

Macro AddCol2 (ar,ind, fld, head, width, fldType,decPoint)
   ar.value (ind * 6)      = fld;
   ar.value (ind * 6 + 1)  = head;
   ar.value (ind * 6 + 2)  = width;
   ar.value (ind * 6 + 3 ) = fldType; 
   ar.value (ind * 6 + 4 ) = decPoint;
   ar.value (ind * 6 + 5 ) = 0; 
End;

Macro ScrollRate ()
   macro EvList (x, cmd, id, key)
      if(cmd == DLG_KEY)
         if(key == 13) 
            idRate   = x.value ("t_fiid");
            NameRate = x.value ("t_fi_code");
            return CM_CANCEL;
         elif (key == 27) 
            idRate  = 0;
            NameRate = "";
            return false;
         elif ((key == 322) or (key == 323))
            return CM_IGNORE;
         end;
      end;
   end;
   var sel = ("select f.t_fiid, f.t_fi_code, f.t_name from dfininstr_dbt f where f.t_fi_kind = 3 and f.t_avoirkind = 1");
   sel = RSDCommand (sel);
   private var x = RSDRecordset(sel, RSDVAL_CLIENT, RSDVAL_STATIC );
   var col = TArray;
   AddCol2 (col , 0, "t_fi_code", "Код"  , 8, 2, null);
   AddCol2 (col , 1, "t_name"   , "Наименование" , 10, 2, null);
   RunScroll (x,2,col,null,@EvList,"Плавающие ставки","ENTER-Выбор ESC-Отмена",false,27,9,30,10);
End;

Macro ScrollFIID (head, xo, yo, zo)

   macro EvList0 (xsel, cmd, id, key)
      if(cmd == DLG_KEY)
         if(key == 13) 
            idFIID   = xsel.value ("t_fiid");
            NameFIID = xsel.value ("t_codeinaccount");
            return CM_CANCEL;
         elif (key == 27) 
            idFIID  = 0;
            NameFIID = "";
            return false;
         elif ((key == 322) or (key == 323))
            return CM_IGNORE;
         end;
      end;
   end;
   var sel = ("select t.t_fiid, t.t_codeinaccount, t.t_name from dfininstr_dbt t where t.t_isclosed = chr(0) and t.t_fi_kind = 1");
   sel = RSDCommand (sel);
   private var xsel = RSDRecordset(sel, RSDVAL_CLIENT, RSDVAL_STATIC );
   var col = TArray;
   AddCol2 (col , 0, "t_codeinaccount",     "Код"  , 8, 2, null);
   AddCol2 (col , 1, "t_name"   , "Наименование" , 10, 2, null);
   if (RunScroll (xsel,2,col,null,@EvList0,head,"ENTER-Выбор ESC-Отмена",false,xo,yo,30,zo))
      return true;
   end;
   return false;
End;


Macro ScrollWAY ()

   var way = Tarray ();
   way [0] = "привлечение";   
   way [1] = "размещение";    
//   way [2] = "приобретение";
   var tway = Menu(way, "Выберите направление сделки", "Выберите направление сделки", 27, 11);
   if (tway >= 0)
      ways = way[tway];
   else
      ways = "Нет";
   end;
   return ways;
End;

Macro IsProlong (id)
   var sel = rsdcommand("select tick.t_flag1 from ddl_tick_dbt tick where tick.t_dealid = :id");
   sel.addParam(":id", RSDBP_IN, id);
   var get = rsdrecordset (sel);
   if (get.movenext)
      sel =  rsdcommand ("select tick.t_dealcode, tick.t_dealcodets from doprdocs_dbt doc, ddl_tick_dbt tick, doproper_dbt oper "+                      
                         " where doc.t_id_operation=oper.t_id_operation and doc.t_docKind=102 and oper.t_docKind=102 "+
                         "   and doc.t_DocumentID=LPad("+id+",34,'0') and tick.t_dealid = To_Number(substr(oper.t_DocumentID, 25, 10))");                                         
    
      var gt = rsdrecordset (sel);
      if (gt.movenext)
         extnum = gt.value(1);
         return true;
      end;
   else
      return False;
   end;
   return False;
End;

Macro GetNote (id, type)
   var getnote; 
   if (type == 301)
      getnote = "select RSB_Struct.GetString (txt.t_text) from dnotetext_dbt txt where txt.t_objecttype = 103 and txt.t_notekind = 301 and txt.t_documentid = lpad(:id,34,'0') AND txt.t_validtodate=to_date('31129999','DDMMYYYY')";
   else
      getnote = "select RSB_Struct.GetDouble (txt.t_text) from dnotetext_dbt txt where txt.t_objecttype = 103 and txt.t_notekind = 300 and txt.t_documentid = lpad(:id,34,'0') AND txt.t_validtodate=to_date('31129999','DDMMYYYY')";
   end;
   getnote = rsdcommand (getnote);
   getnote.addParam(":id", RSDBP_IN, id);
   var data = rsdrecordset (getnote);
   if (data.movenext)
      return data.value(0);
   end;
   if (type == 300)
       return 0.00;
   else
       return "";
   end;
End;

Macro mainPanel (dlg, cmd, id, key ) 
   if (cmd == DLG_INIT)
      dlg.pRate = "";
      disablefields (dlg, FldIndex(dlg, "pRate"));
      unc = 0;//Simanov. Ошибка формата данных unc    = getNote(DealId, 300); // 300│Сумма в унциях
      vproc  = getNote(DealId, 301); // 301│Валюта процентов
      
      dlg.convers = GetConversFromDeal (DealId);//примечание 300
      updateFields(dlg); 

      if (not IsProlong (DealId))
          disablefields (dlg, fldindex (dlg,"extnum"));
      end;


      if (strlen (vproc) == 0 )
          disablefields (dlg, fldindex (dlg,"perc_cur"));
      end;
      
      if (unc == 0 )
          disablefields (dlg, fldindex (dlg,"summ_unc"));
      end;
      updateFields (dlg); 
   end;
   if (cmd == DLG_KEY)
      if (key == 323) 
         sql = ("select leg.t_start, leg.t_maturity, leg.t_tablepercent, leg.t_typepercent, leg.t_caption, leg.t_principal, tick.t_flag1, "+
                "       (select t.t_codeinaccount from dfininstr_dbt t where t.t_isclosed = chr(0) and t.t_fi_kind = 1 and t.t_fiid = leg.t_pfi) t_pfi,"
                "       case when leg.t_tablepercent = chr(88) then 0 else leg.t_price / 100 end t_cost,"+
                "       case when leg.t_tablepercent = chr(88) then (select nvl(f.t_fi_code, chr(0)) from dfininstr_dbt f where f.t_fi_kind = 3 and f.t_avoirkind = 1 and f.t_fiid = leg.t_caption) else '' end nRate, "+ 
                "       case "+
                "         when tick.t_dealtype in (2300, 12300, 22300, 12310, 32300, 12301) then 'привлечение' "+
                "         when tick.t_dealtype in (2305, 12305, 22305, 12315, 12335, 32305, 12306) then 'размещение' "+
                "         when tick.t_dealtype = 12306 then 'размещение'"+
                "         else '' end way "+
                "  from ddl_leg_dbt leg, ddl_tick_dbt tick where leg.t_dealid = :id and tick.t_dealid = leg.t_dealid ");
 
         sql = rsdcommand (sql);


         sql.addParam(":id", RSDBP_IN, DealId);
         rs  = rsdrecordset (sql);
         if (rs.movenext)

            if (unc != dlg.summ_unc)
               if (MsgBoxEx("Не совпадает сумма в унциях |в сделке: "+unc+"|в панели подтверждения: "+dlg.summ_unc+"|Продолжить?", MB_YES+MB_NO, IND_YES) == IND_YES)
                  code = 0;
               else
                  code = 1;
               end;
            end;
            if (vproc != dlg.perc_cur)
              /*
               if (MsgBoxEx("Не совпадает валюта по процентам |в сделке: "+vproc+"|в панели подтверждения: "+dlg.perc_cur+"|Продолжить?", MB_YES+MB_NO, IND_YES) == IND_YES)
                  code = 0;
               else
                  code = 1;
               end;
              */
                  code = 1;

            end;


            xunc = dlg.summ_unc;
            xvproc = dlg.perc_cur;
            if ( ( (rs.value("t_cost") != dlg.Percent) or
                   (rs.value("t_principal") != dlg.Summa  ) or
                   (rs.value("t_start") != dlg.Date1) or
                   (rs.value("t_maturity") != dlg.Date2) ) or
                   ( (rs.value("t_tablepercent") == "X") and (rs.value("nRate") != dlg.pRate ) ) or
                   ( (rs.value("way") != dlg.way ) and (valtype(rs.value("way")) != 26 ) ) or    /*SVE 490107 открыл строку и добавил проверку на случай если поле не нужно заполнять */
                   (rs.value("t_pfi") != dlg.cur ) or
                   (extnum != dlg.extnum)
               ) 

         
               msgbox ("Верификация не выполнена !");
               pSumma    = dlg.Summa; 
               pPercent  = dlg.Percent; 
               pDate1    = dlg.Date1;
               pDate2    = dlg.Date2;
               pNameRate = dlg.pRate;
               pWay      = dlg.way; 
               pIdFIID   = dlg.cur; 
               pextnum   = dlg.extnum;
         
               rSumma    = rs.value("t_principal");
               rPercent  = rs.value("t_cost");
               rDate1    = rs.value("t_start");
               rDate2    = rs.value("t_maturity"); 
               rNameRate = rs.value("nRate"); 
               

               if ( (rs.value("nRate") == NULL) or (ValType(rs.value("nRate")) == 26))
                  rNameRate = "";
               end;
               if ( (rs.value("way") == NULL) or (ValType(rs.value("way")) == 26))
                  rWay = "";
               else
                 rWay    = rs.value("way"); 
               end;

               //rWay    = rs.value("way"); 

               rIdFIID = rs.value("t_pfi"); 
               code = 1;
               rundialog ( errpanel, "PanelErr" );
            else
               msgbox ("Верификация выполнена !");
               code = 0;
            end;
         end;
         return CM_CANCEL;

      elif (key == 27) 
         msgbox ("Верификация не выполнена !");
         code = 1;
         return CM_CANCEL;
      elif ((key == 32) and (id == FldIndex(dlg, "k0")))
         if (dlg.k0 == "X")
            dlg.k0 = "";
            enablefields (dlg, FldIndex(dlg, "Percent"));
            disablefields  (dlg, FldIndex(dlg, "pRate"));
            dlg.pRate = "";
         else
            dlg.k0 = "X";
            disablefields (dlg, FldIndex(dlg, "Percent"));
            enablefields  (dlg, FldIndex(dlg, "pRate"));
            msgbox ("Для выбора плавающей ставки нажмите F3");
            ScrollRate;
            dlg.pRate = NameRate;
            updateFields (dlg);
            setfocus (dlg, FldIndex(dlg, "pRate"));
         end;
         updatefields (dlg);
      elif ((key == 13) and (id == FldIndex(dlg, "extnum")));
         setfocus (dlg, 0);
         return CM_IGNORE;
      elif ((key != 317) and (id == FldIndex(dlg, "pRate")));
         if (Substr (dlg.pRate ,1, 2) == "Не")
            dlg.k0 = "";
            enablefields (dlg, FldIndex(dlg, "Percent"));
            disablefields  (dlg, FldIndex(dlg, "pRate"));
            dlg.pRate = "";
            updatefields (dlg);
         end;


      elif ((key == 317) and (id == FldIndex(dlg, "pRate")));
         ScrollRate;
         dlg.pRate = NameRate;
         updateFields (dlg);
      elif ((key == 317) and (id == FldIndex(dlg, "way")));
         dlg.way = ScrollWAY;
         updateFields (dlg);
      elif ((key == 13) and (id == FldIndex(dlg, "way")) and ((dlg.way != "привлечение") and (dlg.way != "размещение") and (dlg.way != "приобретение")))        
	 msgbox ("Для выбора направления нажмите F3");
         dlg.way = ScrollWAY;
         updateFields (dlg);
      elif ((key == 13) and (id == FldIndex(dlg, "cur")) and (dlg.cur == "") and (not ScrollFIID ("Валюта сделки",27,12,15)))
         msgbox ("Для выбора валюты сделки нажмите F3");
         ScrollFIID ("Валюта сделки",27,12,15);
         dlg.cur = NameFiid;
         updateFields (dlg);
      elif ( (key == 317) and (id == FldIndex(dlg, "cur")));
         ScrollFIID ("Валюта сделки",27,12,15);
         dlg.cur = NameFiid;
         updateFields (dlg);
      elif ((key == 13) and (id == FldIndex(dlg, "Perc_Cur")) and (dlg.perc_cur == ""));
         msgbox ("Для выбора валюты по процентам нажмите F3");
         ScrollFIID ("Валюта по процентам",27,14,15);
         dlg.Perc_Cur = NameFiid;
         updateFields (dlg);
      elif ((key == 317) and (id == FldIndex(dlg, "Perc_Cur")));
         ScrollFIID ("Валюта по процентам",27,14,15);
         dlg.Perc_Cur = NameFiid;
         updateFields (dlg);
      end;

   end;
End;

MACRO InitOperation( KindOp, KindDoc, dl_tick )
 
   code = 0;
   SetBuff( deal, dl_tick );                           
   if ({oper} == 1 );
       return 0;
   end;
  
  if (deal.dealdate > {curdate} )
    MsgBox (" Дата сделки больше текущей даты ! Преждевременное выполнение шага запрещено ! ");
    return 1;
  end;

  DealId = deal.dealid;
  if ({oper} == deal.oper)
     msgbox ("Пользователь № "+ deal.oper+", вводивший сделку, не может ее верифицировать");
     code = 1;
     //return code;
  end; 
  rundialog ( panel, "mainPanel" );
  return code;
 
END;
