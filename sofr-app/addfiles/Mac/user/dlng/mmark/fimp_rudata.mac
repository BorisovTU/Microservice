/*           
 Filename    : fimp_rudata.mac
 Description : вспомогательны функции для закрузки данных из Rudata в буферные таблицы
 Programmer  : Симанов А.
 21.12.2017  : Создан
*/
import rcw, likepy, oralib, "oratools.mac";

const serverurl = "https://test-datahub.efir-net.ru:443/hub.axd";

/*Если stat == 0, то это POST запрос*/
macro ReQuest (AuthService, stat, query)
  private var ax     = CreateObject ("rsax","TRsAxServer","RsAxServer", false);
  var docXML         = ax.CreateCOMObject("MsXml2.DOMDocument.6.0");
  var xmlHttpRequest = ax.CreateCOMObject("MSXML2.ServerXMLHTTP.6.0");
  var test           = "";

  if (query != null) 
    test = query;
  end;

  if (stat == 0)
    xmlHttpRequest.Open("POST", serverurl+AuthService, false);
  else
    xmlHttpRequest.Open("GET", serverurl+AuthService, false);
  end;
  
  xmlHttpRequest.setRequestHeader( "Content-Type", "application/json; charset=utf-8");
  xmlHttpRequest.setRequestHeader( "Accept", "application/xml");
  BegAction(2000,"Ожидание ответа от сервера. Запрос ...");
  xmlHttpRequest.send(test);
  EndAction();

  if (xmlHttpRequest.Status == 200)
      docXML.async =false;
      docXML.load(xmlHttpRequest.responsestream);
      docXML.text;
  else
      //MsgBoxEx("Ошибка при обращении к сервису Rudata");
      //MsgBox("Ошибка статус-"+xmlHttpRequest.Status+" Время: "+time());
      docXML.async = false;
      docXML.load(xmlHttpRequest.responsestream);
      exit();
  end;

  if ((stat != 0) or (query != null))
    return docXML.xml;
  else
    return docXML.text;
  end;

onerror(err)
  if(err.code != 0)
    if(err.code == 40)
      //MsgBoxEx("Сервис RuData не доступен");
    else
      //MsgBoxEx("Ошибка при обращении к сервису Rudata");
    end;
  end;
  exit(1);
End;

/*Получить токен*/
macro GetToken ()
  var AuthService = "/Account/Login";
  return ReQuest (AuthService, 0);
End;

//перевести дату в формат для Рудаты для GET запроса
macro TransformDateForRuData (dt : string)
  var rez;
  rez = substr(dt, 7);
  rez = rez + "-";
  rez = rez + substr(dt, 4, 2);
  rez = rez + "-";
  if (substr(dt, 1, 1) == " ")
    rez = rez + "0" + substr(dt, 2, 1);
  else
    rez = rez + substr(dt, 1, 2);
  end;
  rez = rez + "T00%3A00%3A00.0000000Z";
  return rez;
End;

//перевести дату в формат для БД
macro TransformDateForBD (dt)
  var year, month, day;
  if ((dt == null) or (dt == "") or (dt == -1))
    return date(0,0,0);
  end;
  year  = substr(dt, 1, 4);
  month = substr(dt, 6, 2);
  day   = substr(dt, 9, 2);
  return date(day+"."+month+"."+year);
End;

/*наличие бумаги по fintoolid в буф. таблице*/
macro CBInBase (fintoolid)
  if (ExecSqlSelect("select 1 from DRSHB_CB_DBT where t_fintoolid = :1", MakeArray(SqlParam("1", OraInteger(fintoolid))), false).MoveNext() )
    return 1;
  end;
  return 0;
End;

/*Наличие эмитента в буф. таблице*/
macro EmitentInBase (fininstid)
  if (ExecSqlSelect("select 1 from DRSHB_EMI_DBT where t_FININSTID = :finid", MakeArray(SqlParam("finid", fininstid)), false).MoveNext() )
    return 1;
  end;
  return 0;
End;

//см. ниже + запоминает местоположение найденого слова в строке
macro GetChildNodeByName0( xml, nodeName, pend:@integer)
  Var x1,x2,x3,rez;
  x1 = index(xml,"<"+nodeName+">");
  x2 = index(xml,"</"+nodeName+">");
  x3 = index(xml,"<"+nodeName+"/>");
  if ((x1==0) and (x2==0) and (x3==0))
    pend = strlen(xml);
    return -1;
  end;
  if ((x3 > 0) and (x1 == 0) and (x2 == 0)) 
    pend = x3 + strlen(nodeName) + 3;
    return "";
  end;
  if ((x3 < x1) and (x3 > 0))
    pend = x3 + strlen(nodeName) + 3;
    return "";
  end;

  pend=x2+strlen(nodeName)+3;

  if ( x2 > 0 )
     x2=x2-x1-strlen(nodeName)-2;
  end;
  rez=substr(xml,x1+strlen(nodeName)+2,x2);
    return rez;
End;

//Взять имя. пример: <nodeName>Имя_которое_нужно_взять</nodename>
//так же работает с <nodeName/>, возвращает пустую строку
//возвращает 0, когда во всей строке не был обнаружен nodeName  
macro GetChildNodeByName10( xml, nodeName )
  Var x1,x2,x3,rez;
  x1 = index(xml,"<"+nodeName+">");
  x2 = index(xml,"</"+nodeName+">");
  x3 = index(xml,"<"+nodeName+"/>");
  //нет в строке xml слова nodename
  if ((x1==0) and (x2==0) and (x3==0))
    return -1;
  end;
  //в строке xml не встерчается <nadename></nodename>, но есть <nodename/>
  if ((x3 > 0) and (x1 == 0) and (x2 == 0)) 
    return "";
  end;
  //в строке xml <nodename/> встречается раньше, чем <nodename></nodename>
  if ((x3 < x1) and (x3 != 0))
    return "";
  end;
  if ( x2 > 0 )
     x2=x2-x1-strlen(nodeName)-2;
  end;
  rez=substr(xml,x1+strlen(nodeName)+2,x2);
  return rez;
End;

//кол-во данных в XML документе
macro GetCount (pai, nodeName)
  var pend = 0, ii = 1, count = 0;  
  while (ii > 0)
    ii = index(pai, nodeName);
    if(ii == 0)
      continue;
    end;  
    if(GetChildNodeByName0(pai, nodeName, @pend) == -1)
      return count;
    end;    
    pai = substr(pai, pend);
    count = count + 1;
 end;
  return count;
End;


macro ClearAllBuf ()
  if (ExecSql("delete from DRSHB_CB_A_DBT", null, false) == null)
    println("Ошибка при удалении данных из DRSHB_CB_A_DBT");
  end;
  if (ExecSql("delete from DRSHB_CB_DBT", null, false) == null)
    println("Ошибка при удалении данных из DRSHB_CB_DBT");
  end;
  if (ExecSql("delete from DRSHB_CB_AM_DBT", null, false) == null)
    println("Ошибка при удалении данных из DRSHB_CB_AM_DBT");
  end;

  if (ExecSql("delete from DRSHB_CB_CUP_DBT", null, false) == null)
    println("Ошибка при удалении данных из DRSHB_CB_CUP_DBT");
  end;

  if (ExecSql("delete from DRSHB_CB_R_DBT", null, false) == null)
    println("Ошибка при удалении данных из DRSHB_CB_R_DBT");
  end;
  if (ExecSql("delete from DRSHB_EMI_DBT", null, false) == null)
    println("Ошибка при удалении данных из DRSHB_EMI_DBT");
  end;
  if (ExecSql("delete from DRSHB_EMI_R_DBT", null, false) == null)
    println("Ошибка при удалении данных из DRSHB_EMI_R_DBT");
  end;
  if (ExecSql("delete from DRSHB_DIV_DBT", null, false) == null)
    println("Ошибка при удалении данных из DRSHB_DIV_DBT");
  end;
  if (ExecSql("delete from DRSHB_RATES_DBT", null, false) == null)
    println("Ошибка при удалении данных из DRSHB_RATES_DBT");
  end;
End;


/*
удаляет символы " из строки.
у некоторых бумаг в регистрационном коде есть кавычки, с кавычками некоторые запросы выдают ошибку
*/
macro deletehooks (str)
  var len = strlen(str);
  var count = 1;
  var tmp;
  if (index(str, "\"") == 0) 
    return str;
  end;
  while (count < len)
    tmp = substr(str, count, 1);
    if (tmp == "\"")
      str = substr(str, 1, count-1) + substr(str, count+1);
    end;
    count = count + 1;
  end;
  return str;
End;