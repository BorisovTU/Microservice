
/*-------------------------------------------------------------------------

        Имя файла       : fxDealInfo.mac 

        Описание        : Информация о сделках КО

        Программист     : Селезнёв Роман

        Создан          : 19.12.2007


-------------------------------------------------------------------------*/

import oralib, likepy, Календарь, PTInter;

/*

Типы сделок:

- Сделки типа "today" есть Сделки купли-продажи иностранной валюты за рубли или другую иностранную валюту 
        с датой валютирования в день заключения Сделки;
- Сделки типа "tomorrow" есть Сделки купли-продажи иностранной валюты за рубли или другую иностранную валюту 
        с датой валютирования на следующий за днем заключения Сделки рабочий день;
- Сделки типа "spot" есть Сделки купли-продажи иностранной валюты за рубли или другую иностранную валюту 
        с датой валютирования на второй рабочий день от даты заключения Сделки, не считая её; 
- Сделки типа "forward" есть Сделки купли-продажи иностранной валюты за рубли или другую иностранную валюту 
        с датой валютирования на третий или более поздний рабочий день от даты заключения Сделки, не считая её;
- Сделки типа "swap" есть Сделки купли-продажи иностранной валюты за рубли или другую иностранную валюту 
        на условиях "today", "tomorrow" или "spot" с одновременной обратной контрсделкой 
        на условиях "tomorrow", "spot" или "forward" по курсам, согласованным в момент заключения Сделки;
- Сделки типа "split" (или сделки предоплаты) есть Сделки с условием расчётов, 
        при котором поставка разных валют по заключённой сделке происходит в разные даты расчётов. 
        При этом валютные обязательства клиента перед банком должны исполняться в дату расчётов, 
        предшествующую дате расчётов по валютным обязательствам банка перед клиентом. 
        Используется в сделках по поставке валюты.
*/

const DEAL_TYPE_TODAY   = 0,
      DEAL_TYPE_TOMORROW= 1,
      DEAL_TYPE_SPOT    = 2,
      DEAL_TYPE_FORWARD = 3,
      DEAL_TYPE_SPLIT   = 100,
      DEAL_TYPE_SWAP    = 200;

private var aFullDealName = TArray(4, 0),       // Полное название типов сделок
            aShortDealName = TArray(4, 0);      // Краткое название типов сделок
aFullDealName(0) = "Today";
aFullDealName(1) = "Tomorrow";
aFullDealName(2) = "Spot";
aFullDealName(3) = "Forward";

aShortDealName(0) = "Tod";
aShortDealName(1) = "Tom";
aShortDealName(2) = "Spot";
aShortDealName(3) = "Forw";


//
//  Получить тип конверсионной сделки по дате сделки и дате валютирования
//
macro diGetDealTypeByDates(dealDate:Date, maturityDate:Date)
   if( DateAfterWorkDays(dealDate, 0) == maturityDate ) 
      return DEAL_TYPE_TODAY;
   elif( DateAfterWorkDays(dealDate, 1) == maturityDate ) 
      return DEAL_TYPE_TOMORROW;
   elif( DateAfterWorkDays(dealDate, 2) == maturityDate ) 
      return DEAL_TYPE_SPOT;
   elif( DateAfterWorkDays(dealDate, 3) <= maturityDate ) 
      return DEAL_TYPE_FORWARD;
   end;

   return -1;
end;


//
//  Получить тип конверсионной сделки по id сделки и транша
//
macro diGetDealType(dealId, legId)
   var sql = " select * from ddl_tick_dbt tick "
          +"     inner join ddl_leg_dbt leg " 
          +"     on tick.t_dealId = leg.t_dealId and tick.t_bOfficeKind=100 and tick.t_dealId= :dealId " 
          +"     and leg.t_legKind=0 and leg.t_legId=:legId " ;
 
   var params = MakeArray(SQLParam("dealId", dealId), SQLParam("legId", legId)); 
   var rsDeal = ExecSQLSelect(sql, params, false);
  
   var dealDate, maturityDate;
   
   if(rsDeal.MoveNext())          
      dealDate     = date(rsDeal.value("t_dealDate")); 
      maturityDate = Min( date(rsDeal.value("t_maturity")), date(rsDeal.value("t_expiry")));
   else
      return -1;        // Ошибка
   end;
    
   return diGetDealTypeByDates(dealDate, maturityDate);          
end;


//
// Получить полное название типа сделки по Id типа
//
macro diGetDealTypeName(dealType)
   return aFullDealName(dealType);
end;


//
// Получить краткое название типа сделки по Id типа
//
macro diGetShortDealTypeName(dealType)
   return aShortDealName(dealType);
end;


//
// Проверить, является ли сделка сделкой SWAP
// 
macro diIsSWAP(dealId)
   var sql = " select t_legId from ddl_leg_dbt leg "
           + " where t_dealId = :dealId ";

   var params = MakeArray(SQLParam("dealId", dealId)); 
   var rsDeal = ExecSQLSelect(sql, params, false);
   if( not rsDeal.MoveNext() )
      return false;
   end;
 
   var count = 1;
       
   while(rsDeal.MoveNext())          
      count = count+1;    
   end;

   if(2 == count)       // SWAP есть 2 спаренные сделки
      return true;
   else
      return false;
   end;
end;


//
// Проверка, является ли текущая конверсионная сделка банкнотной БН (true) или сделкой FOREX (false)
//
macro diIsCashDeal(dealId)      
   var sql = "select t_deliveryKind from dPmPaym_dbt where t_purpose = 1 and t_dockind=100 and t_documentId=:deal ";

   var params = MakeArray(SQLParam("deal",  dealId)); 

   var rs = ExecSQLSelect(sql, params, false);
 
   if(rs.MoveNext())
      if (rs.value(0) > 0)
         return true;
      end;
   end; 

   return false;
end;


//
// Определение того, является ли субъект экономики биржей
//
macro diIsExchange(partyId)
   return ВидСубъекта(int(partyId), PTK_MARKETPLASE);
end;


//
// Определение того, является ли субъект клиринговым центром
//
private macro diIsClearingCenter(partyId)
   return ВидСубъекта(int(partyId), 500);
end;


//
// Проверка, является ли текущая конверсионная сделка биржевой, т.е. заключённой с биржей или её клиринговым центром (true), или обычной сделкой FOREX (false)
//
macro diIsExchangeDeal(dealId)
   var sql = "select t_partyId from ddl_tick_dbt where t_bOfficeKind=100 and t_dealId=:deal";
                                                        
   var params = MakeArray(SQLParam("deal", dealId)); 
   var rs = ExecSQLSelect(sql, params, false);

   if(rs.MoveNext()) 
      if( (diIsExchange(rs.value(0))) /*or (diIsClearingCenter(rs.value(0)))*/ )
         return true;  
      end;
   end; 

   return false;
end;



//
// Проверка, является ли сделка срочной
// Возвращает true -- срочная сдекла, false -- кассовая
//
macro diIsTimeBargain(dealId, legId) 
   // Срочной считаем только форвардные сделки
   return (DEAL_TYPE_FORWARD == diGetDealType(dealId, legId));
end;

 
// 
// Задать дату цепочки
//
macro diSetChainDate(dt, operationId)
   var sql = " update doprdates_dbt set t_Date = :dt where t_dateKindId = 9999999 and t_id_operation = :operationId ";
   var params, res; 
   
   params = MakeArray( SQLParam("dt", dt), SQLParam("oper", operationId) );
   res = ExecSQL(sql, params, false);
   if(null == res)
      MsgBox("Ошибка при изменении даты цепочки");
      return false;
   end; 

   return true;
end;

//
// Получить id операции
//
macro diGetOperationId (dealId, operKind)
   var sql = " select t_id_operation from dOprOper_dbt where t_kind_operation = :operKind and t_docKind = 100 and lpad(:dealId, 34, '0') = t_documentId ";
   var params = MakeArray(SQLParam("operKind", operKind), SQLParam("dealId", dealId)); 
   var rs = ExecSQLSelect(sql, params, false);
 
   if(not rs.MoveNext())
      MsgBox("Не найден id операции");
      return -1;
   end;

   return rs.value(0);
end;

//
// Получить сумму удержанных единовременных комиссий по сделке
//
macro diGetUnitComiss(dealId, operKind)
   var oper = diGetOperationId (dealId, operKind);
   var sql = " select nvl(sum(t_sum),0) + nvl(sum(t_sumNds),0) comiss from doprsfcom_dbt where t_id_operation = :oper and 'X' = t_isDoc and 3 = t_feeType ";

   var params = MakeArray(SQLParam("oper", oper)); 
   var rs = ExecSQLSelect(sql, params, false);
 
   if(not rs.MoveNext())
      MsgBox("Ошибка поиска комиссий");
      return 0;
   end;

   return rs.value(0);
end;








