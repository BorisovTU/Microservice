// =============================================================================
//            Copyright (c) 1993 - 2009 R-Style Softlab 
//                      All Rights Reserved. 
// 
//  System      : Rs-Core
//  File name   : oratools.mac
//  Date        : 26.01.2015
//  Description : Библиотека для работы с SQL 
//  Programmer  : Горленко В.В.
//  Remarks     : 
// 
// =============================================================================

import bankinter, "oralib.mac", "likepy.mac";

Private var _OraString;
//-----------------------------------------------------------------------------------------

Macro NotNull ( val )
    if ( val == NULL ) return false; end;
    if ( ValType (val) == 26 ) return false; end;
    return true;
End;
//-----------------------------------------------------------------------------------------

Macro OraString ( str )
    if ( NotNull (str) and (str != StrFor (0)) and (str != StrFor (1)) )
        return str;
    end;
    return "";
End;
//-----------------------------------------------------------------------------------------

Macro OraDate ( dt )
    if ( NotNull (dt) and (dt != Date (2,1,1)-1) )
        return Date (dt);
    end;
    return Date (0,0,0);

End;
//-----------------------------------------------------------------------------------------

Macro OraMoney ( sum )
    if ( NotNull (sum) )
        return Money (sum);
    end;
    return $0;
End;
//-----------------------------------------------------------------------------------------

Macro OraInteger ( num )
    if ( NotNull (num) )
        return Int (num);
    end;
    return 0;
End;
//-----------------------------------------------------------------------------------------

Macro Capture ( str )
    _OraString = String (_OraString, " ", str);
End;
//-----------------------------------------------------------------------------------------

Macro StartCapture
    _OraString = "";
    SetOutHandler ("Capture");
End;
//-----------------------------------------------------------------------------------------

Macro EndCapture
    SetOutHandler ();
End;
//-----------------------------------------------------------------------------------------

Macro TestSql
    println(_OraString);
End;
//-----------------------------------------------------------------------------------------
Macro ExecuteSql ( Parm )
    return ExecSql (_OraString, Parm, false);
End;
//-----------------------------------------------------------------------------------------

Macro ExecuteSqlSelect ( Parm, crsLocation, crsType )
    return ExecSqlSelect (_OraString, Parm, false, crsLocation, crsType);
End;
//-----------------------------------------------------------------------------------------

Macro CloneExt ( x, y )
    Var fName, fExt;
    if ( Clone (x, y) )
        SplitFile (y, fName, fExt);
        ExecSql ("alter table d"+fName+"_"+StrSubst (fExt, ".", "")+"  nologging");
        return true;
    end;
    return false;
End;
//-----------------------------------------------------------------------------------------

Macro CopySql2Rsl ( sql, rsl, NotCopyField )
    if ( NotCopyField == NULL ) NotCopyField = ""; end;
    Var i = 0;
    ClearRecord (rsl);
    if ( (ValType (rsl) == V_FILE) or (ValType (rsl) == V_STRUC) )
        while ( i < FldNumber (rsl) )
            if ( Index (StrUpr (NotCopyField), "\\"+StrUpr (FldName (rsl, i))+"\\") )
                ;
            elif ( ValType (sql.value ("t_"+FldName (rsl, i))) == 26 )
            elif (  ValType (rsl(i)) == V_STRING )
                rsl(i) = OraString (sql.value ("t_"+FldName (rsl, i)));
            elif (  ValType (rsl(i)) == V_DATE )
                rsl(i) = OraDate (sql.value ("t_"+FldName (rsl, i)));
            else
                rsl(i) = sql.value ("t_"+FldName (rsl, i));
            end;
            i = i + 1;
        end;
    else
        while ( i < rsl.FldNumber )
            if ( Index (StrUpr (NotCopyField), "\\"+StrUpr (rsl.FldName (i))+"\\") )
                ;
            elif ( ValType (sql.value ("t_"+rsl.FldName(i))) == 26 )
            elif (  ValType (rsl.(i)) == V_STRING )
                rsl.(i) = OraString (sql.value ("t_"+rsl.FldName(i)));
            else
                rsl.(i) = sql.value ("t_"+rsl.FldName(i));
            end;
            i = i + 1;
        end;
    end;
End;
//-----------------------------------------------------------------------------------------

Macro ExtractOraString
    return _OraString;
End;
//-----------------------------------------------------------------------------------------
