/*LKL
Панель - "Параметры приобретения требований"
*/

IMPORT  funk, RSD, RSBDataSet, dlgCommon, dl_lib;
import "gtconst.inc",calculate,funk,"cb_sql.mac",dl_plib, CurrInter;

Private const kbF2 = 316, kbF3 = 317, kbF7 = 522, kbF9 = 323, kbEnter = 13, kbSpace = 32;

//Курс по FIID на дату
macro GetRateMain(FIID, D: date)
  var ft;
  record rate(ratedef);
  if (FIID == 0)
    return 1;
  end;
  ft = ПолучитьКурс(rate, NATCUR, FIID);
  if (ft == 0)
    ft = ПолучитьЗначениеКурса(rate, D);
  end;
  return ConvertSum($1, rate.rate, rate.scale, rate.point, rate.isinverse);
end;


Class (TRecHandler) CPanelCdemands ( _DealId, _DealDate, _IsEnable )
    private var  IsEnable, DealId, DealDate, CurrentField, ValCode, AmountCom, Comment, IsContrComPay;

    InitTRecHandler ("cdemands", "mbkadd.lbr", TRUE);
    DealId    = _DealId;
    IsEnable  = _IsEnable;
    DealDate  = _DealDate;

//обработчик для списка валют
    Macro scrHandler ( rs, cmd, id, key )
        if ( (cmd == DLG_KEY) and (key == kbEnter) and (rs.RecCount) )
            return CM_SELECT;
        end;
    End;

//проверка на наличие параметров для сделки
    private macro IsExistParamUstup()
        private  var que, rs;

        que = "select count(*) from dl_ustup_dbt where t_dealid = "+ String(DealId);
        rs = LnGetRecordSet(que);
        if(rs != null)
           if (rs.moveNext) 
               if ( rs.value(0) > 0 ); 
                  return True;
               else
                  return False;
               end;
           end;	
        end;
    End;

//получить FIID сделки
    private macro GetDealFiid()
        private  var que, rs;

        que = "select t_pfi from ddl_leg_dbt where t_dealid = "+ String(DealId);
        rs = LnGetRecordSet(que);
        if(rs != null)
           if (rs.moveNext) 
                return rs.value(0);
           end;	
        end;
    End;

//получить ID контрагента сделки
    private macro GetDealParty()
        private  var que, rs;

        que = "select t_partyid from ddl_tick_dbt where t_dealid = "+ String(DealId);
        rs = LnGetRecordSet(que);
        if(rs != null)
           if (rs.moveNext) 
                return rs.value(0);
           end;	
        end;
    End;


//получение параметров по сделке
    private macro GetParamUstup()
        private  var que, rs;

        que = "select t_amount, t_flag1, t_flag2, t_flag3, t_price, (select f.t_ccy from dfininstr_dbt f where  t.t_fiid =f. t_fiid and f.t_fi_kind =1 and f.t_fi_code<>1) as t_ccy, t_period, t_fiid from dl_ustup_dbt t where t.t_dealid = "+ String(DealId);

        rs = LnGetRecordSet(que);
        if(rs != null)
           if (rs.moveNext) 
            SetParm (1, rs.value ("t_amount"));
            SetParm (2, rs.value ("t_flag1"));
            SetParm (3, rs.value ("t_flag2"));
            SetParm (4, rs.value ("t_flag3"));
            SetParm (5, rs.value ("t_price"));
            SetParm (6, rs.value ("t_ccy"));
            SetParm (7, rs.value ("t_period"));
            ValCode = rs.value ("t_fiid"); 
           end;	
        end;

    End;

//установка параметров по сделке
    private macro SetParamUstup()
        private  var que, cmd;
        if (IsExistParamUstup)
          que = "update dl_ustup_dbt set t_amount="+String(this.("SUMMAP"))+
                ", t_flag1=decode(ascii(?),88,chr(88),chr(0)), t_flag2=decode(ascii(?),88,chr(88),chr(0)),t_flag3=decode(ascii(?),88,chr(88),chr(0)), t_price=?, t_fiid=?, t_period=? where t_dealid="+String(DealId);
        else
          que = "insert into dl_ustup_dbt (t_dealid, t_dealdate, t_amount, t_flag1, t_flag2, t_flag3, t_price, t_fiid, t_period, t_reserve) "+
                 "values (?,?,"+String(this.("SUMMAP"))+",decode(ascii(?),88,chr(88),chr(0)), decode(ascii(?),88,chr(88),chr(0)),decode(ascii(?),88,chr(88),chr(0)),?,?,?" + ",'')"; 
        end;

        cmd = RSDCommand(que);
        if (not IsExistParamUstup) 
  	    cmd.addParam("", RSDBP_IN, DealId);
            cmd.addParam("", RSDBP_IN, DealDate);
        end;

        cmd.addParam("", RSDBP_IN, this.("ATTRPROC"));
        cmd.addParam("", RSDBP_IN, this.("ATTRCOM"));
        cmd.addParam("", RSDBP_IN, this.("ATTRPAY"));
        cmd.addParam("", RSDBP_IN, this.("PROCCOM"));
        cmd.addParam("", RSDBP_IN, ValCode);
        cmd.addParam("", RSDBP_IN, this.("PERIOD"));
        cmd.execute();
    End;


//удаление параметров для сделки
    private macro DeleteParamUstup()
        private  var que, rs;
        que = "delete from dl_ustup_dbt where t_dealid = "+ String(DealId);
        rs = LnGetRecordSet(que);
    End;


//получить список валют
    private macro ListVal()
        private var sql, cmd; 
//        sql = "select t_ccy, t_name, t_fiid from dfininstr_dbt  where t_fi_kind=1 and t_fi_code<>1";
        sql = "select f.t_ccy, f.t_name, f.t_fiid from ddl_leg_dbt t, dfininstr_dbt f  where t.t_dealid="+String(DealId)+" and (t.t_pfi=f.t_fiid or f.t_fiid = 0) and f.t_fi_kind=1 and f.t_fi_code<>1";
        cmd = RSDRecordset(sql, RSDVAL_CLIENT, RSDVAL_STATIC );
        if ( RunScroll (cmd, 3, MakeArray ("t_ccy", "Код", 3, 2, 3, 0, "t_name", "Наименование", 20, 2, 20, 0, "t_fiid", "Id", 10, 2, 0, 0),
                        "ListVal", R2M (this, "scrHandler"), "Список валют", "Enter Выбор  ESC Отмена", TRUE, 0, 0) )
            SetParm (1, cmd.value ("t_ccy"));
            SetParm (2, cmd.value ("t_fiid"));
            return TRUE;
        end;
        return FALSE;
    End;




// Макрос записи нового платежа МБК (Комиссия)
   macro _Create_op

   private   var paym      = Tbfile( "pmpaym.dbt", "W" );
   private   var paymprop  = Tbfile( "pmprop.dbt", "W" );
   private   var paymprop2 = Tbfile( "pmprop.dbt", "W" );
   private   var pmrmprop  = Tbfile( "pmrmprop.dbt", "W" );

   var type:integer;
   var yearlen:integer;
   var stat=0,kontr,su,prpr,pss,ku,toch,dil,kurs,alg,d3,d1,v1,v2,kden, pu, subpu, npach, d2, d22, sup, cparty;
   pu    = 9;
   subpu = 2;
   npach = 1;
   d2  = DealDate;
   d22 = DealDate;
   sup = AmountCom;                  
//  платеж
   cparty = GetDealParty();
//кто платит комиссию

    if (IsContrComPay)

//контрагент
       paym.rec.PAYER = cparty;
       paym.rec.RECEIVER = 1;

       paym.rec.PAYERBANKID = 10680;
       paym.rec.RECEIVERBANKID = cparty;

       paym.rec.PAYERACCOUNT = "12345678901234567890"; 
       paym.rec.RECEIVERACCOUNT = "12345678901234567890";
    else
//банк
       paym.rec.PAYER = 1;
       paym.rec.RECEIVER = cparty;

       paym.rec.PAYERBANKID =  10680;
       paym.rec.RECEIVERBANKID = cparty;

       paym.rec.PAYERACCOUNT = "12345678901234567890"; 
       paym.rec.RECEIVERACCOUNT = "12345678901234567890"; 

    end;
    paym.rec.PAYMENTID = 0;
    paym.rec.DOCKIND = 102;
    paym.rec.DOCUMENTID = DealId;
    paym.rec.PURPOSE = pu;
    paym.rec.SUBPURPOSE = subpu;

    paym.rec.VALUEDATE=d2;

    paym.rec.AMOUNT = sup;
    paym.rec.BASEAMOUNT = sup;
    paym.rec.PAYAMOUNT = sup;
    paym.rec.FUTUREPAYERAMOUNT = sup;
    paym.rec.FUTURERECEIVERAMOUNT = sup;
    paym.rec.NUMBERPACK=npach;
    if ( d22 != d2 )
      paym.rec.USERFIELD3=СДАТ(d22);
    end;
    paym.rec.PAYMSTATUS = 0;

        stat = paym.insert;
        if (not stat)
                RunError( "Ошибка при создании новой сделки 10" );
        end;

//debet

                paymprop.rec.PAYMENTID = paym.rec.PAYMENTID;
                paymprop.rec.DEBETCREDIT = 0;
                paymprop.rec.TRANSFERDATE = paym.rec.VALUEDATE;
                stat = paymprop.insert;
                if (not stat)
                        RunError( "Ошибка при создании новой сделки 11." );
                end;
//credit
                paymprop2.rec.PAYMENTID = paym.rec.PAYMENTID ;
                paymprop2.rec.DEBETCREDIT = 1;
                paymprop2.rec.TRANSFERDATE = paym.rec.VALUEDATE;
                stat = paymprop2.insert;
                if (not stat)
                        RunError( "Ошибка при создании новой сделки 12." );
            end;
//Реквизиты платежа         

            pmrmprop.rec.PAYMENTID = paym.rec.PAYMENTID ;
            pmrmprop.rec.DATE = d2;
            pmrmprop.rec.PAYDATE = d2;
            pmrmprop.rec.CLIENTDATE = d2;
                stat = pmrmprop.insert;
                if (not stat)
                        RunError( "Ошибка при создании новой сделки 13." );
                end;

// -----------------
  OnError( RslErrObj ) //А если ошибка - заполняем коммент
        Comment = RslErrObj.Message;
    AbortTrn();

end;

// ------------------------------------------------------





//обработчик панели
    Macro Handler ( dlg, cmd, id, key )

      if (not IsEnable)
	disableFields( this);
      end;	

      if( (cmd == DLG_KEY) and (key == kbF9)  )

           if( this.("ATTRDEMAND") == "X" ) 
              if ( (this.("PERIOD") < 0) or (this.("SUMMAP")<=0) )
                 msgbox("Не все условия определены корректно!");
                 return CM_IGNORE;
              end;

              if ( (this.("ATTRCOM") =="X") and ( (strlen(this.("VALCOM")) <3) or (this.("PROCCOM") <=0)) )
                 msgbox("Не все условия по комиссии определены корректно!");
                 return CM_IGNORE;
              end;

              if ( this.("ATTRCOM") != "X")
		this.("VALCOM")  = "";
                this.("PROCCOM") = 0;
              end;
              UpdateFields (dlg);
              SetParamUstup();
              PaymDemandsUpd(DealId, this.("SUMMAP"));

              if ( this.("ATTRCOM") == "X" )

                 if (this.("ATTRPAY") == "X")
			 IsContrComPay = True;
		 else
			 IsContrComPay = False;
		 end;

                 AmountCom = this.("SUMMAP")*this.("PROCCOM")/100.0;
//msgbox(GetRateMain(ValCode, DealDate))
                 if ( ValCode > GetDealFiid() ) ;
		     AmountCom = Money(round(AmountCom / GetRateMain(ValCode, DealDate),2));
                 elif (ValCode < GetDealFiid())
		     AmountCom = Money(round(AmountCom * GetRateMain(GetDealFiid(), DealDate),2));
		 else
                     AmountCom = Money(round(AmountCom,2));
                 end;
/*
msgbox("Сумма комиссии " + String(AmountCom));
 
                 if (ProcessTrn(0, R2M(this, "_Create_op")))
                 else
                             msgbox("Платеж не сформирован !");
                             MSGBOX(Comment);
                             return false;
                 end;
*/

              end;

            else  //ОТКАТ
                 if ( IsExistParamUstup() )
                     if (MsgBoxEx("Удалить параметры приобретения требований?", MB_YES+MB_NO, IND_NO) == IND_YES)
//                       msgbox(GetSummPaymByPurpose(DealId, 10, 0) );
                       PaymDemandsUpd(DealId, GetSummPaymByPurpose(DealId, 10, 0) );
                       DeleteParamUstup();
                     end
                 end;


	    end;
           return CM_SAVE;

      elif (cmd == 5) //DLG_IN???
           CurrentField = id;

      elif( (cmd == DLG_KEY) and (key == kbSpace) and ( (CurrentField == 0 ) 
                                                    or  (CurrentField == 3 ) 
                                                    or  (CurrentField == 4 )
                                                    or  (CurrentField == 5 ) ))
         if (dlg.(id) == "X")
            dlg.(id) = "";
         else
            dlg.(id) = "X";
         end;
      elif( (cmd == DLG_KEY) and (key == kbF3) and ( CurrentField == 7 )  )
           ListVal(dlg.(id), ValCode);
           UpdateFields (dlg);

      elif( (cmd == DLG_KEY) and (key == kbEnter) )

           return CM_IGNORE;
      end;

    End;

//инициализация панели
    Macro Init
      this.("DDATE")   = DealDate;
      if ( IsExistParamUstup() )
        this.("ATTRDEMAND") = "X";
        GetParamUstup(this.("SUMMAP"), this.("ATTRPROC"), this.("ATTRCOM"), this.("ATTRPAY"), this.("PROCCOM"), this.("VALCOM") , this.("PERIOD"));
        
      end;
    End;

//запуск панели    
    Macro Run
        Init ();
        return RunDialog (this, R2M (this, "Handler"));

    End;

end;