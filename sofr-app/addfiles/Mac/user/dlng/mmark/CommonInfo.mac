/*-------------------------------------------------------------------------

	Имя файла	: CommonInfo.mac

	Описание	: Информация об объектах и общесистемные функции

	Программист	: Селезнёв Роман

	Создан		: 21.03.2008


-------------------------------------------------------------------------*/

import oralib, likepy, globals,fx_globals, CTInter;



//
// Поменять 2 переменные (аргументы процедуры) местами
// В случае успешного выполнения возвращает true, а в случае ошибки -- false
//
macro SwapValues(v1, v2):Bool
   var tmp;
   tmp = v1;
   v1 = v2;
   v2 = tmp;

   var ok = true;

   ok = ok and SetParm(0, v1);
   ok = ok and SetParm(1, v2);
   return ok;
end;


//
// Проверка, установлен ли признак хранения истории (старых значений) для типа объекта ObjType для группы GroupId
// ObjType -- вид объекта, к которому привязывается признак (константы OBJTYPE_*);  
// GroupId -- группа, к которой принадлежит объект
// Возвращает true в случае положительного ответа или false -- в отрицательного
//
macro IsKeepOldValues(ObjType:Integer, GroupId:Integer):Bool
   var sql = "select t_keepOldValues from dObjGroup_dbt where t_objectType=:objType and t_groupId=:groupId ";

   var params = MakeArray(SQLParam("objType", ObjType), SQLParam("groupId", GroupId)); 
   var rs = ExecSQLSelect(sql, params, false);

   if(rs.MoveNext()) 
      return IfThenElse("X" == rs.value(0), true, false);
   end; 

   MsgBox("Ошибка чтения Категории признака");
   return false;
end;


//
// Служебная процедура задания даты окончания действия признака у объекта (если хранится история, то обновляется последний признак)
// ObjType -- вид объекта, к которому привязывается признак (константы OBJTYPE_*); ObjId -- идентификатор объекта; 
// GroupId -- группа, к которой принадлежит объект 
// Возвращает true в случае успешного завершения или false -- в случае возникновения ошибки
//
private macro SetEndDateObjAttr(ObjType:Integer, ObjId:String, GroupId:Integer):Bool
   var sql = " update dObjAtCor_dbt set t_validToDate=:dateTo where t_objectType=:objType and t_groupId=:groupId and t_object=:object "
           + " and t_validToDate = "
           + " (select max(t_validToDate) from dObjAtCor_dbt where t_objectType=:objType and t_groupId=:groupId and t_object=:object) ";

   var params = MakeArray(SQLParam("dateTo", {CurDate}), SQLParam("objType", ObjType), SQLParam("groupId", GroupId), SQLParam("object", ObjId));
   var rs = ExecSQL(sql, params, false);
   if(null == rs) 
      MsgBox("Ошибка при задании даты окончания действия признака");
      return false;
   end; 

   return true;
end;

//
// Служебная процедура получения даты начала действия признака у объекта (если хранится история, то последнего признака в истории)
// ObjType -- вид объекта, к которому привязывается признак (константы OBJTYPE_*); ObjId -- идентификатор объекта; 
// GroupId -- группа, к которой принадлежит объект 
// Возвращает дату окончания действия или null -- в случае отсутствия требуемой записи
//
private macro GetStartDateObjAttr(ObjType:Integer, ObjId:String, GroupId:Integer) 
   var sql = " select t_validFromDate from dObjAtCor_dbt where t_objectType=:objType and t_groupId=:groupId and t_object=:object "
           + " order by t_validToDate desc, t_validFromDate desc ";

   var params = MakeArray(SQLParam("objType", ObjType), SQLParam("groupId", GroupId), SQLParam("object", ObjId));
   var rs = ExecSQLSelect(sql, params, false);

   if(rs.MoveNext()) 
      return date(rs.value(0));
   end; 

   //MsgBox("Ошибка чтения связи объект-признак");
   return null;
end;

//
// Служебная процедура полного удаления признака у объекта (если хранится история, то удаляется последний признак)
// ObjType -- вид объекта, к которому привязывается признак (константы OBJTYPE_*); ObjId -- идентификатор объекта; 
// GroupId -- группа, к которой принадлежит объект 
// Возвращает true в случае успешного завершения или false -- в случае возникновения ошибки
//
private macro DelObjAttr(ObjType:Integer, ObjId:String, GroupId:Integer):Bool
   var sql = " delete from dObjAtCor_dbt where t_objectType=:objType and t_groupId=:groupId and t_object=:object "
           + " and t_validToDate = "
           + " (select max(t_validToDate) from dObjAtCor_dbt where t_objectType=:objType and t_groupId=:groupId and t_object=:object) ";

   var params = MakeArray(SQLParam("objType", ObjType), SQLParam("groupId", GroupId), SQLParam("object", ObjId));
   var rs = ExecSQL(sql, params, false);
   if(null == rs) 
      MsgBox("Ошибка при удалении признака объекта");
      return false;
   end; 

   return true;
end;


//
// Процедура предназначена для поиска основной категории объекта с идентификатором ObjID и видом ObjType в группе GroupID.
// ObjType -- вид объекта, к которому привязывается признак (константы OBJTYPE_*); ObjId -- идентификатор объекта; 
// GroupId -- группа, к которой принадлежит объект
// В случае успешно поиска возвращается номер категории AttrID, в противном случае возвращается 0 
//
macro GetObjAttr(ObjType:Integer, ObjId:String, GroupId:Integer):Integer
   var CatErr:Integer = 0;   
   var AttrId = 0, Code = 0, Num = 0;

   GetMainObjAttr( CatErr, ObjType,
                   ObjId,
                   GroupId,
                   AttrId, Code, Num
                 );

 
   if( CatErr != 0 )      
      //MsgBox("Ошибка получения категории контрагента");
      return 0;
   end;

   return AttrId;
end;


//
// Процедура предназначена для снятия признака, заданного параметром AttrId (или номером уровня Code и номером признака в уровне Num) 
// с объекта с идентификатором ObjId и видом ObjType для группы GroupId. 
// Аналогична DisconnectObjAttr из CTInter, однако снимаются проблемы вызова из контекста функции пользователя ГКБО (SCR #122712)
// ObjType -- вид объекта, к которому привязывается признак (константы OBJTYPE_*); ObjId -- идентификатор объекта; 
// GroupId -- группа, к которой принадлежит объект 
// Возвращает true в случае успешного завершения или false -- в случае возникновения ошибки
//
macro ResetObjAttr(ObjType:Integer, ObjId:String, GroupId:Integer):Bool
   // В справке F1 отсутствуют сведения о DisconnectObjAttr, поэтому кол-во параметров неполное!!! 
   var start = null;

   if(IsKeepOldValues(ObjType, GroupId))				// Требуется хранить историю
      start = GetStartDateObjAttr(ObjType, ObjId, GroupId);
      if(({CurDate} != start) and (null != start))		// Дата начала действия предыдущего признака есть и отлична от текущей даты
         return SetEndDateObjAttr(ObjType, ObjId, GroupId);    // "Закрыть" срок действия предыдущего признака
      end;
   end;

   return DelObjAttr(ObjType, ObjId, GroupId);        		// Удалить признак
end;




//
// Процедура предназначена для установки признака, заданного параметром AttrId (или номером уровня Code и номером признака в уровне Num) 
// к объекту с идентификатором ObjId и видом ObjType для группы GroupId.
// Аналогична ConnectObjAttr из CTInter, однако снимаются проблемы вызова из контекста функции пользователя ГКБО (SCR #122712)
// ObjType -- вид объекта, к которому привязывается признак (константы OBJTYPE_*); ObjId -- идентификатор объекта; 
// GroupId -- группа, к которой принадлежит объект; AttrId -- идентификатор признака; Code -- номер уровня признака; Num -- номер признака в уровне;
// DateRSL -- дата начала действия признака (см. справку по ConnectObjAttr).  
// Возвращает true в случае успешного завершения или false -- в случае возникновения ошибки
// ЗАМЕЧАНИЕ: Параметры Code, Num и DateRSL в настоящее время не задействованы!!!
//
macro SetObjAttr(ObjType:Integer, ObjId:String, GroupId:Integer, AttrId:Integer, Code:String, Num:String, DateRSL:Date):Bool
   var prevAttr = GetObjAttr(ObjType, ObjId, GroupId);

   if(0 == AttrId) 	  	// Ошибка получения	
      return false;
   elif(prevAttr == AttrId) // Предыдущий признак равен тому, который задаётся сейчас
      return true;
   end;
    
   // Удаление ранее заданного признака (или помещение его в историю, если по признаку ведётся история)
   if(not ResetObjAttr(ObjType, ObjId, GroupId))
      return false;
   end;

   // Установка признака
   var sql = " insert into dObjAtCor_dbt (t_objectType, t_groupId, t_attrId, t_object, t_general, t_validFromDate, t_oper, t_validToDate, t_sysDate, t_sysTime, t_isAuto) " 
           + " values (:objType, :groupId, :attrId, :object, :general, :dateFrom, :oper, :dateTo, to_date(to_char(sysdate, 'DD.MM.YYYY'), 'DD.MM.YYYY'), "
           + " TO_DATE('01.01.0001 ' || TO_CHAR(sysdate, 'HH24.MI.SS'), 'DD.MM.YYYY HH24.MI.SS'), chr(0)) "; 

   var params = MakeArray(SQLParam("objType", ObjType), SQLParam("groupId", GroupId), SQLParam("attrId", AttrId), SQLParam("object", ObjId)
		, SQLParam("general", "X"), SQLParam("dateFrom", {CurDate}),  SQLParam("oper", {Oper}), SQLParam("dateTo", date("31.12.9999"))); 
 
   var rs = ExecSQL(sql, params, false);
   if(null == rs) 
      MsgBox("Ошибка при задании признака объекта");
      return false;
   end; 

   return true; 
end;


//
// Получить название признака AttrId объекта типа ObjType из группы GroupId
// contragentCatId -- Id категории контрагента. Возвращает название категории контрагента или пустую строку, если портфель не найден 
// ObjType -- вид объекта, к которому привязывается признак (константы OBJTYPE_*);  
// GroupId -- группа, к которой принадлежит объект; AttrId -- идентификатор признака
// Возвращает название признака в случае успеха или "" в случае ошибки
//
macro GetObjAttrName(ObjType:Integer, GroupId:Integer, AttrId:Integer)
   var sql = "select t_Name from dObjAttr_dbt where t_objectType=:objType and t_groupId=:groupId and t_attrId=:attrId "; 

   var params = MakeArray(SQLParam("objType", ObjType), SQLParam("groupId", GroupId), SQLParam("attrId", AttrId)); 
   var rs = ExecSQLSelect(sql, params, false, RSDVAL_CLIENT, RSDVAL_STATIC); 

   if(rs.MoveNext())                
      return rs.value(0);
   end;

   return "";
end;

//
// Получить полное название признака AttrId объекта типа ObjType из группы GroupId
// contragentCatId -- Id категории контрагента. Возвращает название категории контрагента или пустую строку, если портфель не найден 
// ObjType -- вид объекта, к которому привязывается признак (константы OBJTYPE_*);  
// GroupId -- группа, к которой принадлежит объект; AttrId -- идентификатор признака
// Возвращает полное название признака в случае успеха или "" в случае ошибки
//
macro GetObjAttrFullName(ObjType:Integer, GroupId:Integer, AttrId:Integer)
   var sql = "select t_fullName from dObjAttr_dbt where t_objectType=:objType and t_groupId=:groupId and t_attrId=:attrId "; 

   var params = MakeArray(SQLParam("objType", ObjType), SQLParam("groupId", GroupId), SQLParam("attrId", AttrId)); 
   var rs = ExecSQLSelect(sql, params, false, RSDVAL_CLIENT, RSDVAL_STATIC); 

   if(rs.MoveNext())                
      return rs.value(0);
   end;

   return "";
end;


 



