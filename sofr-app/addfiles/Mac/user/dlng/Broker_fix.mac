/*
$Name:        Broker_fix.mac
$Module:      ПЗО
$Description: Макрос расчета фиксированной комиссии
*/

import FIInter, "dlcnst.inc", "dps_com.inc", dlquery;

// Идентификатор биржи ММВБ
private macro u_MMVB_ID()
  var stat = 0;
  var MMVB_Code = "", _MMVB_ID = -1;

  GetRegistryValue("SECUR\\MICEX_CODE", V_STRING, MMVB_Code, stat);
  if(stat != 0)
    msgbox("Ошибка при получении значения настройки SECUR\\MICEX_CODE");
  elif(MMVB_Code == "")
    stat = 1;
    msgbox("Не задано значение настройки SECUR\\MICEX_CODE");
  else
    _MMVB_ID = GetCodeParty(MMVB_Code,PTCK_CONTR);
  end;

  return _MMVB_ID;
end;

// Идентификатор биржи СПБ
private macro u_SPB_ID()
  var stat = 0;
  var SPB_Code = "", _SPB_ID = -1;

  GetRegistryValue("SECUR\\SPBEX_CODE", V_STRING, SPB_Code, stat);
  if(stat != 0)
    msgbox("Ошибка при получении значения настройки SECUR\\SPBEX_CODE");
  elif(SPB_Code == "")
    stat = 1;
    msgbox("Не задано значение настройки SECUR\\SPBEX_CODE");
  else
    _SPB_ID = GetCodeParty(SPB_Code,PTCK_CONTR);
  end;

  return _SPB_ID;
end;

private macro CheckDBO(rContr)
  var sql = "Select * from ddlcontrmp_dbt where t_sfcontrid = ?";
  var cmd = DL_rsdCommand; 
  var ds;

  If((rcontr.rec.servkind == 1) and (rcontr.rec.servkindsub == 8))
      cmd.AddParam(rContr.rec.ID);
      ds = cmd.Execute(sql);
      if(ds.moveNext)
        if(ds.MarketID == u_MMVB_ID())
          return true;
        end;
      end;
  end;
  return false;
end;

Private Macro GetMaxMaxSumByConCom(concomid)
  var CurMaxSum = 0, rs659, rs57, rscom;

  var rs = ExecSQLSelect(" select * from dsfconcom_dbt con where con.t_id = ? ", MakeArray(SQLParam("id", concomid)), false);

  if (rs.MoveNext())
    rs659 = ExecSQLSelect(
      "select NVL(max(sfcal.t_summax), 0) as summax659 from dsfconcom_dbt con, dsfcalcal_dbt sfcal where con.t_id = ? and sfcal.t_concomid = con.t_id",
      MakeArray(SQLParam("id", concomid)),
      false
    );
    While(rs659.movenext())
      CurMaxSum = Max(SQL_ConvTypeSum(rs659.value("summax659")), CurMaxSum);
    end;

    If(CurMaxSum == 0)
      rs57 = ExecSQLSelect(
        " select NVL(max(cl.t_summax), 0) summax57 from  dsfconcom_dbt con , dsfcalcal_dbt cl  where  con.t_commnumber = ? and con.t_objecttype = 57 and con.t_objectid = ? and cl.t_concomid = con.t_id and con.t_feetype = 1",
        MakeArray(SQLParam("commnumber", SQL_ConvTypeInteger(rs.value("t_commnumber"))),SQLParam("sfplanid", SQL_ConvTypeInteger(rs.value("t_sfplanid")))),
        false
      );
      While(rs57.movenext())
        CurMaxSum = Max(SQL_ConvTypeSum(rs57.value("summax57")), CurMaxSum);
      end;
      If(CurMaxSum == 0)
        rscom = ExecSQLSelect(
          " select t_summax as summaxcom from dsfcalcal_dbt where t_commnumber = ? and t_feetype = 1 and t_concomid = 0",
          MakeArray(SQLParam("commnumber", SQL_ConvTypeInteger(rs.value("t_commnumber")))),
          false
        );
        If(rscom.movenext)
          CurMaxSum = Max(SQL_ConvTypeSum(rscom.value("summaxcom")),CurMaxSum);
        end;
      end;
    end;
  end;

  return max(CurMaxSum, 0);
end;

Private Macro GetMaxMaxSum(sfcontrid, Begdate, Enddate)
  var sql, cmd, rs, CurMaxSum = 0, month, year;

  DateSplit(EndDate, null, month, year);
  BegDate = Date(1,month,year);
  EndDate = Date(GetDaysInMonth(EndDate),month,year);
  sql  = String( "select distinct con.* from dsfcontrplan_dbt sfplan, dsfconcom_dbt con, dsfcomiss_dbt comiss  where sfplan.t_sfcontrid = ? and sfplan.t_begin <= ?\n" 
                ,"  and ((sfplan.t_end >= ?) OR ( sfplan.t_end = to_date('01.01.0001','dd.mm.yyyy')) )\n"  
                ," and con.t_objecttype = 659 and con.t_objectid = sfplan.t_sfcontrid and con.t_commnumber = comiss.t_number\n"  
                ,"   and comiss.t_feetype = 1 and UPPER(comiss.t_code) = 'БРОКЕРФИКС'\n");
  cmd = DL_RsdCommand(sql);
  cmd.AddParam(sfcontrid);
  cmd.AddParam(Enddate);
  cmd.AddParam(Begdate);
  rs = cmd.execute;
  While(rs.movenext)
    CurMaxSum = GetMaxMaxSumByConCom(SQL_ConvTypeInteger(rs.id));
  end; // eow1

  return Max(CurMaxSum,0);
end;

// получение сумм брокерских комиссий за период по фондовому рынку
private macro Get_STOCK_BrokComiss_Sum(sfcontr,   // Договор обслуживания
                                       BeginDate, // Начало периода
                                       EndDate)   // Конец периода
   var BaseSum = $0;
   var sql = "with sfcontrlist as (select mp2.t_sfcontrid from ddlcontrmp_dbt mp1 , ddlcontrmp_dbt mp2, dsfcontr_dbt sfc\n" +
             "                                 where  mp1.t_sfcontrid = ?\n" + 
             "                                    and mp2.t_dlcontrid = mp1.t_dlcontrid\n" + 
             "                                    and sfc.t_id = mp2.t_sfcontrid\n" + 
             "                                    and sfc.t_servkind = 1\n" + 
             "                                    and sfc.t_servkindsub = 8)\n" + 
             "select nvl(sum(RSI_RSB_FIInstr.ConvSum (t_sum,sfcom.t_fiid_comm,0,deal.t_dealdate)), 0) as t_sum\n" + 
             "  from ddl_tick_dbt deal, ddlcomis_dbt dlcom, dsfcomiss_dbt sfcom, sfcontrlist\n" + 
             " where deal.t_clientcontrid = sfcontrlist.t_sfcontrid\n" + 
             "   and decode(deal.t_commdate, to_date('01.01.0001','DD.MM.YYYY'), deal.t_dealdate, deal.t_commdate) between ? and ? \n" + 
             "   and deal.t_bofficekind = dlcom.t_dockind\n" + 
             "   and deal.t_dealid = dlcom.t_docid\n" + 
             "   and  dlcom.t_comnumber = sfcom.t_number and sfcom.t_iscompensationcom != chr(88)\n" + 
             "   and sfcom.T_RECEIVERID = 1 and  sfcom.t_feetype = 1;";
   var cmd = RSDCommand(sql);
       cmd.AddParam("", rsdbp_in, sfcontr);
       cmd.AddParam("", rsdbp_in, BeginDate);
       cmd.AddParam("", rsdbp_in, EndDate);
   var rs = RSDRecordset(cmd, rsdval_client, rsdval_static);
   if(rs.MoveNext)
     BaseSum = rs.value("t_sum");
   end;
  return BaseSum;
onerror(err)
  return BaseSum; 
end;
// получение сумм брокерских комиссий за период по валютному рынку
private macro Get_CUR_BrokComiss_Sum(sfcontr,   // Договор обслуживания
                                       BeginDate, // Начало периода
                                       EndDate)   // Конец периода
   var BaseSum = $0;
   var sql = "with sfcontrlist as (select mp2.t_sfcontrid, sfc.t_partyid from ddlcontrmp_dbt mp1 , ddlcontrmp_dbt mp2, dsfcontr_dbt sfc\n" +
             "                                 where  mp1.t_sfcontrid = ?\n" + 
             "                                    and mp2.t_dlcontrid = mp1.t_dlcontrid\n" + 
             "                                    and sfc.t_id = mp2.t_sfcontrid\n" + 
             "                                    and sfc.t_servkind = 21\n" + 
             "                                    and sfc.t_servkindsub = 8)\n" + 
             "select nvl(sum(RSI_RSB_FIInstr.ConvSum (t_sum,t_fiid_comm,0,deal.t_date)), 0) as t_sum\n" + 
             "  from ddvndeal_dbt deal, ddlcomis_dbt dlcom, dsfcomiss_dbt sfcom, sfcontrlist\n" + 
             " where deal.t_clientcontr = sfcontrlist.t_sfcontrid\n" + 
             "   and deal.t_dockind = dlcom.t_dockind\n" + 
             "   and deal.t_client = sfcontrlist.t_partyid\n" + 
             "   and deal.t_date between ? and  ?\n" + 
             "   and deal.t_id = dlcom.t_docid\n" + 
             "   and dlcom.t_comnumber = sfcom.t_number\n" + 
             "   and sfcom.t_iscompensationcom != chr(88)\n" + 
             "   and sfcom.T_RECEIVERID = 1  and  sfcom.t_feetype = 1\n" + 
             "   and sfcom.t_iscompensationcom != chr(88) ;";
   var cmd = RSDCommand(sql);
       cmd.AddParam("", rsdbp_in, sfcontr);
       cmd.AddParam("", rsdbp_in, BeginDate);
       cmd.AddParam("", rsdbp_in, EndDate);
   var rs = RSDRecordset(cmd, rsdval_client, rsdval_static);
   if(rs.MoveNext)
     BaseSum = rs.value("t_sum");
   end;
  return BaseSum;
onerror(err)
  return BaseSum; 
end;
// получение сумм брокерских комиссий за период по срочному рынку
private macro Get_DV_BrokComiss_Sum(sfcontr,   // Договор обслуживания
                                    BeginDate, // Начало периода
                                    EndDate)   // Конец периода
   var BaseSum = $0;
   var sql = "with sfcontrlist as (select mp2.t_sfcontrid, sfc.t_partyid from ddlcontrmp_dbt mp1 , ddlcontrmp_dbt mp2, dsfcontr_dbt sfc\n" +
             "                                 where  mp1.t_sfcontrid = ?\n" + 
             "                                    and mp2.t_dlcontrid = mp1.t_dlcontrid\n" + 
             "                                    and sfc.t_id = mp2.t_sfcontrid\n" + 
             "                                    and sfc.t_servkind = 15\n" + 
             "                                    and sfc.t_servkindsub = 8)\n" + 
             "select  nvl(sum(RSI_RSB_FIInstr.ConvSum (t_sum,t_fiid_comm,0,t_date)), 0) as t_sum\n" + 
             "  from (select dvdeal.t_id, sfcom.t_code, dlcom.t_sum, dvdeal.t_date, sfcom.t_fiid_comm, dvdeal.t_code as deal_code, sfcom.t_code as comcode\n" + 
             "          from ddvdeal_dbt dvdeal,\n" + 
             "               ddvdlcom_dbt dlcom,\n" + 
             "               dsfcomiss_dbt sfcom,\n" + 
             "               sfcontrlist\n" + 
             "         where dvdeal.t_id = dlcom.t_dealid\n" + 
             "           and dlcom.t_comissid = sfcom.t_comissid  and sfcom.t_iscompensationcom != chr(88)\n" + 
             "           and sfcom.T_RECEIVERID = 1 and  sfcom.t_feetype = 1\n" + 
             "           and sfcom.t_code not in ('БрокерКомпенсацС')\n" + 
             "           and dvdeal.t_clientcontr = sfcontrlist.t_sfcontrid\n" + 
             "           and dvdeal.t_client = sfcontrlist.t_partyid\n" +
             "           and dvdeal.t_date between ? and ?) t;";
   var cmd = RSDCommand(sql);
       cmd.AddParam("", rsdbp_in, sfcontr);
       cmd.AddParam("", rsdbp_in, BeginDate);
       cmd.AddParam("", rsdbp_in, EndDate);
   var rs = RSDRecordset(cmd, rsdval_client, rsdval_static);
   if(rs.MoveNext)
     BaseSum = rs.value("t_sum");
   end;
  return BaseSum;
onerror(err)
  return BaseSum; 
end;

private macro Get_BrokComiss_Sum(sfcontr,   // Договор обслуживания
                                 BeginDate, // Начало периода
                                 EndDate)   // Конец периода
  var BaseSum = $0;
      BaseSum = BaseSum + Get_STOCK_BrokComiss_Sum(sfcontr, BeginDate,EndDate);
      BaseSum = BaseSum + Get_CUR_BrokComiss_Sum(sfcontr, BeginDate,EndDate);
      BaseSum = BaseSum + Get_DV_BrokComiss_Sum(sfcontr, BeginDate,EndDate);
  return BaseSum;
end;

/*фильтр*/

/* добавление суммы ПЗО */
Private macro AddBasObj( DealSum, FIID )
    record  rBaseSum( "sfbassum.str" );

    rBaseSum.baseType    = SF_BASETYPE_SUM;
    rBaseSum.baseType2   = SF_BASETYPE_SUM;
    rBaseSum.baseSum     = DealSum;
    rBaseSum.baseSum2    = DealSum;

    rBaseSum.BaseObjectType = OBJTYPE_AVOIRISS;
    rBaseSum.BaseObjectID   = FIID;

    if( InsertSumList(rBaseSum) )
       MsgBox("Ошибка при вставке базовой суммы");
       return false;
    end;

    return true;
end;

/* Получить базовую сумму для расчета периодической комиссии  
  sfcontr_addr - Договор обслуживания
  BeginDate    - Начало периода
  EndDate      - Конец периода*/
macro CalcServiceSum( sfcontr_addr, BeginDate, EndDate )
  var rContr = TRecHandler("sfcontr.dbt");

  var     stat=0, AinPEnabled; 
  var     BaseSum = 0;

  // Переопределение начала периода
  var day, month, year;
  DateSplit(EndDate, null, month, year);
  BeginDate = Date(1,month,year);
  EndDate = Date(GetDaysInMonth(EndDate),month,year);

  SetBuff( rContr, sfcontr_addr );

  if(CheckDBO(rContr))
     BaseSum = BaseSum + Get_BrokComiss_Sum(rContr.rec.ID, BeginDate, EndDate);     // получение сумм брокерских комиссий за период по фондовому и валютному рынкам
     AddBasObj(BaseSum, 0);
  end;
  return;
end;

class MaxSumCacheObj()
  private var cache = TArray ();

  macro GetMaxSum(concomid)
    var idx = DL_FindKeyPairInArr(cache,concomid);
    if (idx >= 0)
      return cache[idx].value;
    end;
    var maxSum = SQL_ConvTypeSum(GetMaxMaxSumByConCom(concomid));
    cache[cache.size] = DL_KeyPair(concomid,maxSum);
    return maxSum;
  end;
end;

macro CalcServiceSumBatch( calcalID )
  var stockSql = " select nvl(sum(RSI_RSB_FIInstr.ConvSum (t_sum,sfcom.t_fiid_comm,0,deal.t_dealdate)), 0) as t_sum\n" + 
                 "   from ddl_tick_dbt deal, ddlcomis_dbt dlcom, dsfcomiss_dbt sfcom, \n" +
                 " (select mp2.t_sfcontrid from ddlcontrmp_dbt mp1 , ddlcontrmp_dbt mp2, dsfcontr_dbt sfc\n" +
                 "                                  where  mp1.t_sfcontrid = parms.t_contrid\n" + 
                 "                                     and mp2.t_dlcontrid = mp1.t_dlcontrid\n" + 
                 "                                     and sfc.t_id = mp2.t_sfcontrid\n" + 
                 "                                     and sfc.t_servkind = 1\n" + 
                 "                                     and sfc.t_servkindsub = 8) sfcontrlist \n" + 
                 "  where deal.t_clientcontrid = sfcontrlist.t_sfcontrid\n" + 
                 "    and decode(deal.t_commdate, to_date('01.01.0001','DD.MM.YYYY'), deal.t_dealdate, deal.t_commdate) between TRUNC(parms.t_periodbegindate, 'MM') and TRUNC(ADD_MONTHS(parms.t_periodenddate,1),'MM')-1 \n" + 
                 "    and deal.t_bofficekind = dlcom.t_dockind\n" + 
                 "    and deal.t_dealid = dlcom.t_docid\n" + 
                 "    and  dlcom.t_comnumber = sfcom.t_number and sfcom.t_iscompensationcom != chr(88)\n" + 
                 "    and sfcom.T_RECEIVERID = 1 and  sfcom.t_feetype = 1";
  var curSql = " select nvl(sum(RSI_RSB_FIInstr.ConvSum (t_sum,t_fiid_comm,0,deal.t_date)), 0) as t_sum\n" + 
               "   from ddvndeal_dbt deal, ddlcomis_dbt dlcom, dsfcomiss_dbt sfcom, \n" + 
               " (select mp2.t_sfcontrid, sfc.t_partyid from ddlcontrmp_dbt mp1 , ddlcontrmp_dbt mp2, dsfcontr_dbt sfc\n" +
               "                                  where  mp1.t_sfcontrid = parms.t_contrid\n" + 
               "                                     and mp2.t_dlcontrid = mp1.t_dlcontrid\n" + 
               "                                     and sfc.t_id = mp2.t_sfcontrid\n" + 
               "                                     and sfc.t_servkind = 21\n" + 
               "                                     and sfc.t_servkindsub = 8) sfcontrlist\n" + 
               "  where deal.t_clientcontr = sfcontrlist.t_sfcontrid\n" + 
               "    and deal.t_dockind = dlcom.t_dockind\n" + 
               "    and deal.t_client = sfcontrlist.t_partyid\n" + 
               "    and deal.t_date between TRUNC(parms.t_periodbegindate, 'MM') and TRUNC(ADD_MONTHS(parms.t_periodenddate,1),'MM')-1 \n" + 
               "    and deal.t_id = dlcom.t_docid\n" + 
               "    and dlcom.t_comnumber = sfcom.t_number\n" + 
               "    and sfcom.t_iscompensationcom != chr(88)\n" + 
               "    and sfcom.T_RECEIVERID = 1  and  sfcom.t_feetype = 1\n" + 
               "    and sfcom.t_iscompensationcom != chr(88)";
  var dvSql = " select  nvl(sum(RSI_RSB_FIInstr.ConvSum (t_sum,t_fiid_comm,0,t_date)), 0) as t_sum\n" + 
              "   from (select dvdeal.t_id, sfcom.t_code, dlcom.t_sum, dvdeal.t_date, sfcom.t_fiid_comm, dvdeal.t_code as deal_code, sfcom.t_code as comcode\n" + 
              "           from ddvdeal_dbt dvdeal,\n" + 
              "                ddvdlcom_dbt dlcom,\n" + 
              "                dsfcomiss_dbt sfcom,\n" + 
              " (select mp2.t_sfcontrid, sfc.t_partyid from ddlcontrmp_dbt mp1 , ddlcontrmp_dbt mp2, dsfcontr_dbt sfc\n" +
              "                                  where  mp1.t_sfcontrid = parms.t_contrid\n" + 
              "                                     and mp2.t_dlcontrid = mp1.t_dlcontrid\n" + 
              "                                     and sfc.t_id = mp2.t_sfcontrid\n" + 
              "                                     and sfc.t_servkind = 15\n" + 
              "                                     and sfc.t_servkindsub = 8) sfcontrlist \n" + 
              "          where dvdeal.t_id = dlcom.t_dealid\n" + 
              "            and dlcom.t_comissid = sfcom.t_comissid  and sfcom.t_iscompensationcom != chr(88)\n" + 
              "            and sfcom.T_RECEIVERID = 1 and  sfcom.t_feetype = 1\n" + 
              "            and sfcom.t_code not in ('БрокерКомпенсацС')\n" + 
              "            and dvdeal.t_clientcontr = sfcontrlist.t_sfcontrid\n" + 
              "            and dvdeal.t_client = sfcontrlist.t_partyid\n" +
              "            and dvdeal.t_date between TRUNC(parms.t_periodbegindate, 'MM') and TRUNC(ADD_MONTHS(parms.t_periodenddate,1),'MM')-1)";
  var OldDialogFlag = SetDialogFlag(1);
  var maxSumCache = MaxSumCacheObj();
  var sfbasesumCache = RsbSQLInsert("sfbasesum.tmp");
  var sfBaseRec = TRecHandler("sfbasesum.tmp");
  var counter = 0;
  var cmd = DL_RSDCommand("select parms.*, "
                         /*проверка что договор ММВБ. Аналогично функции CheckDBO */
                         /*общая суть в том что если проверка не пройдена то t_totalBaseSum = 0, иначе получаем из сложных запросов */
                         +"CASE WHEN (NVL((Select t_MarketID from ddlcontrmp_dbt mp where mp.t_sfcontrid = parms.t_contrid and rownum = 1),-1) = ?) "
                         +"      AND sfcontr.t_servkind = 1 AND sfcontr.t_servkindsub = 8 "
                         +"THEN (" + stockSql + ") + (" + curSql + ") + (" + dvSql + ") ELSE 0 END as t_totalBaseSum "
                         +" from dsfbasesumparms_tmp parms left join dsfcontr_dbt sfcontr on sfcontr.t_id = parms.t_contrid"
                         +" where parms.t_calcalid = " + calcalID);
  cmd.AddParam(u_MMVB_ID());
  InitProgress(cmd.GetCount(), "Расчёт данных для пакетной обработки","Расчёт данных для пакетной обработки");
  var ds = cmd.Execute();
  while(ds.moveNext())
    var maxSum = maxSumCache.GetMaxSum(SQL_ConvTypeInteger(ds.concomid));
    var comSum = Max((maxSum - SQL_ConvTypeSum(ds.totalBaseSum)),$0);
    sfBaseRec.clear();
    sfBaseRec.rec.feetype        = SF_FEE_TYPE_PERIOD;
    sfBaseRec.rec.sfdefid        = SQL_ConvTypeInteger(ds.sfdefid);
    sfBaseRec.rec.calcalid       = SQL_ConvTypeInteger(ds.calcalid);
    sfBaseRec.rec.concomid       = SQL_ConvTypeInteger(ds.concomid);
    sfBaseRec.rec.basetype       = SF_BASETYPE_SUM;
    sfBaseRec.rec.basequont      = 0;
    sfBaseRec.rec.baseSum        = comSum;
    sfBaseRec.rec.basetype2      = SF_BASETYPE_SUM;
    sfBaseRec.rec.basequont2     = 0;
    sfBaseRec.rec.baseSum2       = comSum;
    sfBaseRec.rec.baseObjectType = OBJTYPE_AVOIRISS;
    sfBaseRec.rec.BaseObjectID   = NATCUR;
    sfBaseRec.rec.commSum        = comSum;
    sfbasesumCache.AddRecord(sfBaseRec);
    UseProgress((counter = counter + 1));
  end;
  RemProgress();
  SetDialogFlag(OldDialogFlag);
  return sfbasesumCache.insert();
end;/*CalcServiceSumBatch*/