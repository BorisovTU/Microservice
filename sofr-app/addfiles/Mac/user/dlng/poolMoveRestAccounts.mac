import dv_categ;

macro printProtocolHeader
[┌─────────────────────┬─────────────────────┬──────────────────┬───────────────────────────────────────────────┐
 │     Счет дебета     │     Счет кредита    │       Сумма      │                   Сообщение                   │
 ├─────────────────────┼─────────────────────┼──────────────────┼───────────────────────────────────────────────┤];

end;

macro printProtocolLine(debet,credit,amount,message)
[│#####################│#####################│##################│###############################################│
 │                     │                     │                  │                                               │](debet,credit,amount:w,message:w);
end;

macro printProtocolfooter

[└─────────────────────┴─────────────────────┴──────────────────┴───────────────────────────────────────────────┘];

end;

macro makeCarry(debet, credit, amount, ground)
  var transaction = RsbAccTransaction();
  var errorText;

  transaction.oper             = {oper};
  transaction.Chapter          = 1;
  transaction.FIIDPayer        = debet.rec.code_currency;
  transaction.FIIDReceiver     = credit.rec.code_currency;
  transaction.ResultCarry      = 1;
  transaction.number_pack      = 10;
  transaction.AccountPayer     = debet.rec.account;
  transaction.AccountReceiver  = credit.rec.account;
  transaction.SumReceiver      = MoneyL(amount);
  transaction.Date_Carry       = {curdate};
  transaction.Date_Carry       = {curdate};
  transaction.Ground           = ground;

  if(not transaction.Carry(ACCTRN_STATUS_DATE_CARRY,errorText))
     println("Ошибка выполнения проводки "+errorText);
     return false;
  end;

  return true;
end;

CONST USDCUR = 7, EURCUR = 8;

var AccountNumber, Result, ErrorDescription;

var СчетОбеспечения = TRecHandler("account");
var КлиринговыйСчет = TRecHandler("account");
var ФинансовыйИнструмент = TRecHandler("fininstr");

var Остаток;
var TemplNum;
record attrs(mcaccdoc);

printProtocolHeader;

var query = DL_RSDCommand("   SELECT categ.t_code,                                     " +
                          "          accdoc.t_currency,                                " +
                          "          accdoc.t_marketplaceid,                           " +
                          "          accdoc.t_marketplaceofficeid                      " +
                          "     FROM dmcaccdoc_dbt accdoc, dmccateg_dbt categ          " +
                          "    WHERE accdoc.t_catid = categ.t_id                       " +
                          "          AND categ.t_code IN                               " +
                          "                 ('Клиринговый счет',                       " +
                          "                  'Клиринговый счет1')                      " +
                          "          AND accdoc.t_dockind = 0                          " +
                          "          AND accdoc.t_templnum = 3                         " +
                          "          AND accdoc.t_marketplaceofficeid IN (7, 8, 9, 14) " +
                          " GROUP BY categ.t_code,                                     " +
                          "          accdoc.t_currency,                                " +
                          "          accdoc.t_marketplaceid,                           " +
                          "          accdoc.t_marketplaceofficeid                      " +
                          " ORDER BY accdoc.t_currency, accdoc.t_marketplaceofficeid   ");

var count = query.GetCount();
var i = 0;

var data = query.Execute();

initProgress(count, "Перенос остатков клиринговых счетов на счета обеспечения","Перенос остатков клиринговых счетов на счета обеспечения");

while(data.MoveNext())

    useProgress(i = i + 1);
/*
    if((data.marketPlaceOfficeId == 7) and (data.currency == NATCUR))
       continue;
    end;
*/
    if(ПолучитьФинИн(data.currency, ФинансовыйИнструмент) != 0) 
       //println("Не найден финансовый инструмент с ID = "+string(data.currency));
       printProtocolLine("","","","Не найден финансовый инструмент с ID = "+string(data.currency));
       continue;
    end;

    if(data.currency == NATCUR)
        TemplNum = 3;
    elif(ФинансовыйИнструмент.rec.FI_Kind == FIKIND_METAL)
        TemplNum = 2;
    else
        TemplNum = 1;
    end;
/*
    if(data.currency == NATCUR)
        attrs.marketplaceid = data.marketPlaceId;
        attrs.marketplaceofficeid = 7; // Рынок Т+
        AccountNumber = MC_FindAndOpenCommonAcc("Клиринговый счет", {curdate}, MC_OPENACC_CREATE, TemplNum, attrs, null, data.currency, null, null, СчетОбеспечения, Result);
    else
*/    
        ClearRecord(СчетОбеспечения);
        attrs.marketplaceid = data.marketPlaceId;
        attrs.marketplaceofficeid = 15; // Единый пул
        AccountNumber = MC_FindAndOpenCommonAcc("+Обеспечение", {curdate}, MC_OPENACC_CREATE, TemplNum, attrs, null, data.currency, null, null, СчетОбеспечения, Result, null, MC_PAIRACCMODE_AUTO);
//    end;

    if((result == 0) or (result == 2))
        ClearRecord(КлиринговыйСчет);
        attrs.marketplaceid = data.marketPlaceId;
        attrs.marketplaceofficeid = data.marketPlaceOfficeId;
        AccountNumber = MC_FindAndOpenCommonAcc(data.code, {curdate}, MC_OPENACC_CHECKEXIST, 3/*Собственный*/, attrs, null, data.currency, null, null, КлиринговыйСчет, Result);
        if((result == 0) and (КлиринговыйСчет.rec.account != ""))
            message("Перенос остатка с клирингового счета "+КлиринговыйСчет.rec.account+" <fiid = "+data.currency+"> на счет обеспечения "+СчетОбеспечения.rec.account);    
            Остаток = GetRestAccount(КлиринговыйСчет.rec,{curdate});
            
            if(КлиринговыйСчет.rec.kind_account == "А")
                Остаток = -Остаток;
            end;

            if(Остаток > 0.0)
                if(makeCarry(СчетОбеспечения, КлиринговыйСчет, abs(Остаток), "Перенос остатка с клирингового счета на счет обеспечения"))
                    printProtocolLine(СчетОбеспечения.rec.account,КлиринговыйСчет.rec.account,abs(Остаток),"Выполнен перенос остатка с клирингового счета "+КлиринговыйСчет.rec.account+" на счет обеспечения "+СчетОбеспечения.rec.account+"");
                end;
            elif(abs(Остаток) > 0)
                printProtocolLine(СчетОбеспечения.rec.account,КлиринговыйСчет.rec.account,Остаток,"На счете "+КлиринговыйСчет.rec.account+" красное сальдо. Остаток не перенесён");
            end;

            Остаток = GetRestAccount(КлиринговыйСчет.rec,{curdate});
            if((Остаток == 0.0) AND NOT (data.code == "Клиринговый счет1"))
                
                if(КлиринговыйСчет.rec.code_currency != NATCUR) // Golovkin 25.06.2020
                    OverValueAccount(КлиринговыйСчет.rec.account, КлиринговыйСчет.rec.code_currency, КлиринговыйСчет.rec.chapter, {curdate});
                end;

                ErrorDescription = "";
                if(CB_CloseAccount (КлиринговыйСчет.rec.chapter, КлиринговыйСчет.rec.code_currency, КлиринговыйСчет.rec.account, {curdate}, ErrorDescription) == 0 )
                    printProtocolLine("","","","Закрыт счет "+КлиринговыйСчет.rec.account);
                else
                    printProtocolLine("","","","Не удалось закрыть счет "+КлиринговыйСчет.rec.account+": "+ErrorDescription);
                end;    
            end;
        else
            printProtocolLine("",КлиринговыйСчет.rec.account,"","Не открыт клиринговый счет <fiid = "+data.currency+"> <marketPlaceOfficeId = "+data.marketPlaceOfficeId+">. Остаток не перенесён");
        end;      
    else
        printProtocolLine("","","","Не открыт счет обеспечения в валюте <fiid = "+data.currency+">");
    end;


end;

remProgress;


query = DL_RSDCommand(" SELECT *                                                      " +
                      "   FROM daccount_dbt                                           " +
                      "  WHERE (t_account, t_code_currency, t_chapter) IN             " +
                      "            (  SELECT t_account, t_currency, t_chapter         " +
                      "                 FROM dmcaccdoc_dbt                            " +
                      "                WHERE t_catid =                                " +
                      "                      (SELECT t_id                             " +
                      "                         FROM dmccateg_dbt                     " +
                      "                        WHERE     t_code = 'Клиринговый счетР' " +
                      "                              AND t_leveltype = 1)             " +
                      "             GROUP BY t_account, t_currency, t_chapter)        " +
                      " UNION ALL                                                     " +
                      " SELECT *                                                      " +
                      "   FROM daccount_dbt                                           " +
                      "  WHERE t_account IN ('30424978100000000001')                  ");
data = query.Execute();

while(data.MoveNext())
    data.GetRecord().copyTo(КлиринговыйСчет.rec);

    if(data.code_currency == NATCUR)
        TemplNum = 3;
    else
        TemplNum = 1;
    end;
/*
    if(data.code_currency == NATCUR)
        attrs.marketplaceid = 4; // НРД
        attrs.marketplaceofficeid = 7; // Рынок Т+
        AccountNumber = MC_FindAndOpenCommonAcc("Клиринговый счет", {curdate}, MC_OPENACC_CREATE, TemplNum, attrs, null, data.code_currency, null, null, СчетОбеспечения, Result);
    else
*/    
        ClearRecord(СчетОбеспечения);
        attrs.marketplaceid = 4; // НРД
        attrs.marketplaceofficeid = 15; // Единый пул
        AccountNumber = MC_FindAndOpenCommonAcc("+Обеспечение", {curdate}, MC_OPENACC_CREATE, TemplNum, attrs, null, data.code_currency, null, null, СчетОбеспечения, Result, null, MC_PAIRACCMODE_AUTO);
//    end;
    if((result == 0) or (result == 2))
        message("Перенос остатка с клирингового счета "+КлиринговыйСчет.rec.account+" <fiid = "+data.code_currency+"> на счет обеспечения "+СчетОбеспечения.rec.account);
        Остаток = GetRestAccount(КлиринговыйСчет.rec,{curdate});
        
        if(КлиринговыйСчет.rec.kind_account == "А")
            Остаток = -Остаток;
        end;

        if(Остаток > 0.0)
            if(makeCarry(СчетОбеспечения, КлиринговыйСчет, abs(Остаток), "Перенос остатка с клирингового счета на счет обеспечения"))
                printProtocolLine(СчетОбеспечения.rec.account,КлиринговыйСчет.rec.account,abs(Остаток),"Выполнен перенос остатка с клирингового счета "+КлиринговыйСчет.rec.account+" на счет обеспечения "+СчетОбеспечения.rec.account+"");
            end;
        elif(abs(Остаток) > 0)
            printProtocolLine(СчетОбеспечения.rec.account,КлиринговыйСчет.rec.account,Остаток,"На счете "+КлиринговыйСчет.rec.account+" красное сальдо. Остаток не перенесён");
        end;

        Остаток = GetRestAccount(КлиринговыйСчет.rec,{curdate});
        if(Остаток == 0.0)

            if(КлиринговыйСчет.rec.code_currency != NATCUR) // Golovkin 25.06.2020
                OverValueAccount(КлиринговыйСчет.rec.account, КлиринговыйСчет.rec.code_currency, КлиринговыйСчет.rec.chapter, {curdate});
            end;

            ErrorDescription = "";
            if(CB_CloseAccount (КлиринговыйСчет.rec.chapter, КлиринговыйСчет.rec.code_currency, КлиринговыйСчет.rec.account, {curdate}, ErrorDescription) == 0 )
                printProtocolLine("","","","Закрыт счет "+КлиринговыйСчет.rec.account);
            else
                printProtocolLine("","","","Не удалось закрыть счет "+КлиринговыйСчет.rec.account+": "+ErrorDescription);
            end;    
        end;
    else
        printProtocolLine("","","","Не открыт счет обеспечения в валюте <fiid = "+data.code_currency+">");
    end;

end;

printProtocolfooter;
