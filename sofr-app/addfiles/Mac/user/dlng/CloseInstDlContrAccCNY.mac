/*
$Name:        CloseInstDlContrAccCNY.mac
$Module:      Ядро Securities
$Description: Макрос закрытия излишне открытых счетов в юанях для фондовых площадок ЮЛ
*/

import "secinter.mac", "dlquery.mac";
import DealsInter, CTInter;


private macro CloseDlContrAcc(DlContrID:integer, FIID:integer, AccountID:integer, MPID:integer, ErrStr:@string)
  var err = 0;

  ErrStr = "";

  err = DL_CloseAccInDLCONTR(DlContrID, FIID, AccountID, {curdate}, ErrStr, MPID);

  return err;

OnError(er)
  ErrStr = er.message;

  return 1;
end;

private macro GetAccCloseDate(AccountID:integer):date
  var cmd, query, DataSet;
  var dt = date(0,0,0);

  query = "select t_Close_Date from daccount_dbt where t_AccountID = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(AccountID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    dt = date(DataSet.Close_Date);
  end;

  return dt;
end;

private macro CloseAllInstAcc()
  var query, cmd, DataSet;
  var cnt = 0, i = 0;
  var err = 0, MessStr = "";

  println("Закрытие излишне открытых счетов CNY по фондовым площадкам ЮЛ");
  println("");

  query =   " SELECT sf.t_Number, acc.t_Account, dlacc.t_AccountID, dlacc.t_MPID, dlacc.t_FIID, dlacc.t_DlContrID "
          + "   FROM ddlcontr_dbt dlc, "
          + "        dsfcontr_dbt sf, "
          + "        dparty_dbt pt,"
          + "        ddlcontrmp_dbt mp, "
          + "        dsfcontr_dbt sfmp, "
          + "        ddlcontracc_dbt dlacc, "
          + "        daccount_dbt acc "
          + "  WHERE     sf.t_ID = dlc.t_SfContrID "
          + "        AND mp.t_DlContrID = dlc.t_DlContrID "
          + "        AND sfmp.t_ID = mp.t_SfContrID "
          + "        AND sfmp.t_ServKind = " + PTSK_STOCKDL
          + "        AND sfmp.t_ServKindSub = 8 " //Биржевой рынок
          + "        AND pt.t_PartyID = sfmp.t_PartyID "
          + "        AND (pt.t_LegalForm = 1 " //ЮЛ
          + "             OR (    EXISTS(SELECT 1 FROM dpersn_dbt p WHERE p.t_PersonId = pt.t_PartyID AND p.t_IsEmployer = 'X') " //или ИП и не ЕДП
          + "                 AND NOT EXISTS(SELECT 1 "
          + "                                  FROM dobjatcor_dbt a "
          + "                                 WHERE a.t_objecttype = " + OBJTYPE_SFCONTR
          + "                                   AND a.t_groupid = 102 "
          + "                                   AND a.t_attrid = 1 "
          + "                                   AND a.t_object = LPAD(mp.t_SfContrID, 10, '0') "
          + "                               )"
          + "                )"
          + "            ) "
          + "        AND dlacc.t_DlContrID = mp.t_DLContrID "
          + "        AND dlacc.t_FIID = (SELECT fin.t_FIID FROM dfininstr_dbt fin WHERE fin.t_FI_Kind = "+FIKIND_CURRENCY+" AND fin.t_CCY = 'CNY')"
          + "        AND dlacc.t_MPID = mp.t_ID "
          + "        AND acc.t_AccountID = dlacc.t_AccountID "
          + "        AND acc.t_Close_Date = " + GetSQLDate(date(0,0,0));

  cmd = DL_RSDCommand(query);

  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, "Идет закрытие счетов", "Закрытие счетов");

    println("┌──────────────────────┬──────────────────────┬──────────────────────────────────────────────────────────┐");
    println("│                      │                      │                                                          │");
    println("│       Договор        │         Счет         │                          Сообщение                       │");
    println("│                      │                      │                                                          │");
    println("│                      │                      │                                                          │");
    println("├──────────────────────┼──────────────────────┼──────────────────────────────────────────────────────────┤");



    DataSet = cmd.Execute();
    while(DataSet.moveNext())

      err = CloseDlContrAcc(DataSet.DlContrID, DataSet.FIID, DataSet.AccountID, DataSet.MPID, @MessStr);

      if(err == 0)
        var dt = GetAccCloseDate(DataSet.AccountID);
        if(dt > date(0,0,0))
          MessStr = "Счет закрыт.";
        else
          MessStr = "Счет не закрыт.";
        end;
      end;


      [│######################│######################│##########################################################│]
      (string(DataSet.Number):c,                                                                                 
       string(DataSet.Account):c,      
       string(MessStr):l:w        
      );

      UseProgress(i = i + 1);

      if(i != cnt)
        println("├──────────────────────┼──────────────────────┼──────────────────────────────────────────────────────────┤");
      end;
    end;

    println("└──────────────────────┴──────────────────────┴──────────────────────────────────────────────────────────┘");

    RemProgress();
  else
    println("Нет подходящих счетов для закрытия");
  end;

end;


CloseAllInstAcc();