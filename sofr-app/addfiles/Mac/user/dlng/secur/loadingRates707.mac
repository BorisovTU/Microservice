/*Загрузка котировок Bloomberg*/
import activex, or_rep_h, globals, "spserv.mac";
import fiinter, ctinter;


/*Загрузка*/
macro LoadingRates(errmsg:@string)
  record fininstr (fininstr);
  var InputDir, FullNameFile, FileName, ExtName, DirName;
  var Sheet, SheetNum, i, fcol = 1, frow = 1, row;
  var result = tarray(), iresult;
  var action = 0;
  var protocol, fi;
  var AttrID, error;
  var errcnt = 0;
  var rparm = TRecHandler("rateparm");

  /**/
  class tRate
    var isin;
    var bbcode;
    var dt1;
    var dt2;
    var bid;
    var mes;
    var fiid;
  end;

  /**/
  macro getdir()
    var stateA, errCodeA, sRegValA;
    var statA = GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\RATES", V_STRING, sRegValA, errCodeA);
    if((statA == V_STRING) and (errCodeA == 0) and (strlen(sRegValA) > 0))
      return sRegValA + "\\";
    else
      msgboxex("Не задана директория для Excel-файла с курсами");
      return "";
    end;
  end;

  /**/
  macro InsertProtocol(r);
    protocol.AddPrintCell(r.isin);
    protocol.AddPrintCell(r.bbcode);
    protocol.AddPrintCell(r.dt1);
    protocol.AddPrintCell(r.dt2);
    protocol.AddPrintCell(r.bid, null, 4);
    protocol.AddPrintCell(r.fiid);
    protocol.AddPrintCell(r.mes);
    protocol.AddStr();
  end;

  InputDir = getdir();

  /*Выбираем файл для обработки*/
  if(not SelectFile(FullNameFile, InputDir + "*.xls*", "Выберите файл для загрузки", 0, true))
    errmsg = "Отказ от загрузки";
    return false;
  end;
  splitfile(FullNameFile, FileName, ExtName);
  FileName = FileName + ExtName;
  DirName = substr(FullNameFile, 1, index(FullNameFile, FileName) - 1);

  /*Открываем файл*/
  BegAction(1, "Обработка файла \"" + FullNameFile + "\". Ждите...");
  action = 1;
  if(not OpenExcelFile(FileName, false, true, DirName))
    errmsg = "Ошибка открытия файла \"" + FullNameFile + "\"";
    EndAction(1);
    return false;
  end;

  /*Ищем лист BACKOFFICE/ДЛЯ БЭК-ОФИСА*/
  i = 1;
  while(i <= ExcelApplication.Sheets.Count)
    if((strupr(ExcelApplication.Sheets(i).Name) == "BACKOFFICE") or (strupr(ExcelApplication.Sheets(i).Name) == "ДЛЯ БЭК-ОФИСА"))
      break;
    end;
    i = i + 1;
  end;
  if(i > ExcelApplication.Sheets.Count)
    errmsg = "В файле \"" + FullNameFile + "\" не найден лист \"BACKOFFICE\"";
    EndAction(1);
    return false;
  end;
  Sheet = ExcelApplication.Sheets(i);

  /*Ищем первую колонку*/
  while((valtype(Sheet.Cells(1, fcol).value) == V_UNDEF) and (fcol <= 10))
    fcol = fcol + 1;
  end;
  if(valtype(Sheet.Cells(1, fcol).value) == V_UNDEF)
    errmsg = "Не найдено значимое поле в первой строке файла \"" + FullNameFile + "\"";
    EndAction(1);
    return false;
  end;

  /*Ищем первую строку*/
  while(frow <= 10)
    if((valtype(Sheet.Cells(frow, fcol + 6).value) != V_UNDEF) and 
       ((strupr(Sheet.Cells(frow, fcol + 6).value) == "BLOOMBERG") or
        (strupr(Sheet.Cells(frow, fcol + 6).value) == "KAZN")))
      break;
    end;
    frow = frow + 1;
  end;
  if((valtype(Sheet.Cells(frow, fcol + 6).value) == V_UNDEF) or 
     ((strupr(Sheet.Cells(frow, fcol + 6).value) != "BLOOMBERG") and
      (strupr(Sheet.Cells(frow, fcol + 6).value) != "KAZN")))
    errmsg = "Не найдены строки с результатами в файла \"" + FullNameFile + "\"";
    EndAction(1);
    return false;
  end;

  /*Читаем строки до тех пор, пока есть источник*/
  row = frow;
  while(true)
    result[result.size] = tRate;
    if(index(strlwr(Sheet.Cells(row, fcol + 1).value), "equity") > 0)
      result[result.size-1].bbcode = "equity";
    else
      result[result.size-1].bbcode = "";
    end;
    result[result.size-1].isin = trim(Sheet.Cells(row, fcol + 2).value);//уберем непотребство, если isin откуда-то копировали
    result[result.size-1].dt1 = date(Sheet.Cells(row, fcol + 3).value);
    result[result.size-1].dt2 = date(Sheet.Cells(row, fcol + 4).value);
    if(Valtype(Sheet.Cells(row, fcol + 5).value) != V_DOUBLE)
      result[result.size-1].bid = double(StrSubst(Sheet.Cells(row, fcol + 5).value, ",", "."));
    else
      result[result.size-1].bid = double(Sheet.Cells(row, fcol + 5).value);
    end;
    result[result.size-1].fiid = string(Sheet.Cells(row, fcol + 7).Text);//валюта котировки
    row = row + 1;
    if((valtype(Sheet.Cells(row, fcol + 6).value) == V_UNDEF) or 
       ((strupr(Sheet.Cells(row, fcol + 6).value) != "BLOOMBERG") and
        (strupr(Sheet.Cells(row, fcol + 6).value) != "KAZN")))
      break;
    end;
  end;

  /*Закрываем файл*/
  ExcelApplication.DisplayALerts = false;
  ExcelApplication.ActiveWorkbook.Close;
  EndAction(1);
  action = 0;

  /*Создаем файл для протокола*/
  protocol = CMakeReport("┌──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┐\n"+
                         "│Протокол загрузки курсов                                                                                                                              │\n"+
                         "├──────────────────┬────────┬───────────────┬───────────────┬───────────────┬─────────────┬────────────────────────────────────────────────────────────┤\n"+
                         "│ISIN              │Equity  │Date of Update │Required date  │Bid            │Вал.котировки│Результат                                                   │\n"+
                         "├──────────────────┼────────┼───────────────┼───────────────┼───────────────┼─────────────┼────────────────────────────────────────────────────────────┤");

  /*Проходим по полученным записям*/
  InitProgress(int(result.size), "Ждите...", "Обработка результатов загрузки курсов");
  action = 2;
  iresult = 0;
  var old_rate;
  while(iresult < result.size)
    /*ISIN*/
    fi = ПолучитьФинИн(result[iresult].isin, fininstr, null, null, null, 5);

    /*Проверяем, что нашли ЦБ*/
    if(fi != 0)
      errcnt = errcnt + 1;
      result[iresult].mes = "ЦБ не найдена " + fi;
      InsertProtocol(result[iresult]);
      iresult = iresult + 1;
      UseProgress(iresult);
      continue;
    end;
    
    //Chesnokov D.S. 511429- состояние бумаги не проверяем.
    /*Проверяем, что ЦБ открыта*/
    //if(fininstr.isclosed == "X")
    //  errcnt = errcnt + 1;
    //  result[iresult].mes = "ЦБ закрыта ";
    //  InsertProtocol(result[iresult]);
    //  iresult = iresult + 1;
    //  UseProgress(iresult);
    //  continue;
    //end;

    /*Устанавливаем курс*/
    rparm.Clear();
    rparm.rec.OtherFI = fininstr.fi_code;
    if((result[iresult].bbcode == "equity") or (not FI_IsBond(fininstr.fiid)))
      rparm.rec.IsRelative = "";
    else
      rparm.rec.IsRelative = "X";
    end;
    rparm.rec.Type = 1002;//Chesnokov D.S. Теперь курс грузим так
    rparm.rec.Market_Place = 0;
    rparm.rec.Section = 0;
    rparm.rec.Name = "Цена закрытия \"Bloomberg\"";
    //rparm.rec.FI_Code = ПолучитьКодФинИн(fininstr.facevaluefi);
    rparm.rec.FI_Code = iif(result[iresult].fiid != "", result[iresult].fiid, ПолучитьКодФинИн(fininstr.facevaluefi));
    rparm.rec.Scale = 1;
    rparm.rec.Point = 4;
    rparm.rec.Rate = result[iresult].bid  * pow(10,rparm.rec.Point);
    rparm.rec.SinceDate = result[iresult].dt2;
/* 
    old_rate = GetRateOnDateCrossDbl(result[iresult].dt2, fininstr.fiid, fininstr.facevaluefi, false, rparm.rec.Type);
    if ((old_rate != NULL) and (old_rate != 0.0))
      errcnt = errcnt + 1;
      result[iresult].mes = "ЦБ " + rparm.rec.OtherFI + " имеет курс за эту дату";
      InsertProtocol(result[iresult]);
      iresult = iresult + 1;
      UseProgress(iresult);
      continue;
    end;
*/
    if(УстановитьКурс(rparm) != 0 )
      errcnt = errcnt + 1;
      result[iresult].mes = "ЦБ " + rparm.rec.OtherFI + " ошибка при установке курса Цена закрытия \"Bloomberg\" " + Geterrmsg();
      InsertProtocol(result[iresult]);
      iresult = iresult + 1;
      UseProgress(iresult);
      continue;
    end;

    /*Результат успешной загрузки*/
    result[iresult].mes = "Курс загружен";
    /*Для протокола покажем в какой валюте загрузили котировку*/
    if (result[iresult].fiid == "")
      result[iresult].fiid = ПолучитьКодФинИн(fininstr.facevaluefi);
    end;
    InsertProtocol(result[iresult]);

    iresult = iresult + 1;
    UseProgress(iresult);
  end;
  RemProgress();
  action = 0;

  /*Итоги*/
  protocol.AddPrintCell("Обработано строк: " + iresult + ", из них с ошибками: " + errcnt, protocol.GetColWidthTable(0) + protocol.GetColWidthTable(1) + protocol.GetColWidthTable(2) + protocol.GetColWidthTable(3) + protocol.GetColWidthTable(4), null, "l:ex_FS(b)", REP_ELEM_STR);
  protocol.AddStr();

  /*Показываем протокол*/
  protocol.PrintWinRep();
  protocol.ShowWinRep();

  /*Возвращаем результат*/
  if(errcnt == 0)
    return true;
  else
    return false;
  end;
OnError(er)                                                                       
  errmsg = "Исключение: " + er.module + " " + er.line + " " + er.message;
  if(action == 1)
    EndAction(1);
  elif(action == 2)
    RemProgress();
  end;
  return false;
end;


/*Точка входа*/
var errmsg = "";
if(not LoadingRates(@errmsg))
  if(strlen(errmsg) > 0)
    msgboxex(errmsg);
  else
    msgboxex("Курсы загружены с ошибками");
  end;
else
  msgboxex("Курсы успешно загружены");
end;
exit(1);
