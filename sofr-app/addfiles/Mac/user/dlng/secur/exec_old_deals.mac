import rsd, RsbDataSet, spInter, globals;
import or_rep_h;

var oper_date = {curdate};

var format_line = "c:ex_B(tblr):ex_FS()";
var format_head = "c:ex_FS(b):ex_B(tblr)";

var Rep = CMakeReport();

macro protocolInit()
   Rep.SetDefaultFontName("Times New Roman");
   Rep.SetDefaultFontSize(10);
   Rep.SetFlagShowGrid(true);
end;

macro protocolHeader()
   Rep.SetExecute("iBook.Sheets(1).Rows(1).WrapText = false;");
   Rep.AddPrintCell("Протокол выполнения отложенных операций " + oper_date, 10, 0, "c:ex_FS(b):ex_B()");
   Rep.SetExecute("iBook.Sheets(1).range(\"A1:M1\").Merge;");
   Rep.AddStr();

   Rep.AddPrintCell(" ", 10, 0, "c:ex_FS(b):ex_B()");
   Rep.AddStr();
   Rep.AddPrintCell(" ", 10, 0, "c:ex_FS(b):ex_B()");
   Rep.AddStr();

   Rep.AddPrintCell("ID операции",            15, null, format_head);
   Rep.AddPrintCell("Номер операции",         15, null, format_head);
   Rep.AddPrintCell("Инициирование операции", 10, null, format_head);
   Rep.AddPrintCell("Закрытие графиков",      10, null, format_head);
   Rep.AddPrintCell("ОК",                     10, null, format_head);
   Rep.AddStr();
end;

macro protocolLine(_dealid, _dealcode, _resultExec, _resultSched, _resultAll)
   Rep.AddPrintCell(_dealid,      15, null, format_line);
   Rep.AddPrintCell(_dealcode,    15, null, format_line);
   Rep.AddPrintCell(_resultExec,  10, null, format_line);
   Rep.AddPrintCell(_resultSched, 10, null, format_line);
   Rep.AddPrintCell(_resultAll,   10, null, format_line);
   Rep.AddStr();
end;

macro protocolEnd()
   var CreateDate = date();
   var CreateTime = time();
   var day, mon, yearF;
   var hour, min, sec;

   DateSplit(CreateDate, day, mon, yearF);
   TimeSplit(CreateTime, hour, min, sec);

   var m_CurrentRepName = "exec_old_"+"_"+string(yearF)+string(mon:f)+string(day:f)
                                     +"_"+string(hour:f)+string(min:f)+string(sec:f);


   Rep.SetFileName(m_CurrentRepName);
   Rep.PrintWinRep("exec_old");
   Rep.SetZoomType(ZOOM_TYPE_A4L);
   Rep.SetWinRepOutput();
   Rep.ShowWinRep();
end;

macro checkDealState(_dealid, _state)
   var sql = " SELECT t_dealstatus, t_dealcode " +
             "   FROM ddl_tick_dbt             " +
             "  WHERE t_dealid = "+_dealid;
   var rs = TrsbDataSet(sql);
   if(rs.movenext)
      setParm(2, rs.dealcode);

      return rs.dealstatus == _state;
   end;

   return false;
end;

macro updateSched(_dealid)
   var cmd = rsdCommand(" UPDATE ddlgracc_dbt gracc                                               " +
                        "    SET gracc.t_state = 2,                                               " +
                        "        gracc.t_factdate =                                               " +
                        "            (SELECT grdeal.T_PLANDATE                                    " +
                        "               FROM ddlgrdeal_dbt grdeal                                 " +
                        "              WHERE grdeal.t_id = gracc.t_grdealid)                      " +
                        "  WHERE     gracc.t_accnum IN (2, 3, 4)                                  " +
                        "        AND gracc.t_state = 1                                            " +
                        "        AND gracc.t_grdealid IN                                          " +
                        "                (SELECT grdeal.t_id                                      " +
                        "                   FROM ddl_tick_dbt  tick                               " +
                        "                        INNER JOIN ddlgrdeal_dbt grdeal                  " +
                        "                            ON     grdeal.t_docid = tick.t_dealid        " +
                        "                               AND grdeal.t_dockind = tick.T_BOFFICEKIND " +
                        "                        INNER JOIN DDLGRTEMPL_DBT grtempl                " +
                        "                            ON grtempl.T_NUM = grdeal.T_TEMPLNUM         " +
                        "                  WHERE tick.t_dealid = :p_dealid)                       ");
   cmd.AddParam("", RSDBP_IN, _dealid);

   cmd.execute();

   return true;
onerror
   return false;   
end;

macro runExecution()
   var deals_pack = TArray();
   var pack_size = 100;
   
   protocolInit();
   protocolHeader();

   var query = "   SELECT tick.T_DEALSTATUS, tick.*                                              " +
               "     FROM ddl_tick_dbt tick                                                      " +
               "    WHERE     ((tick.t_dealcode LIKE 'ARNU/%') OR (tick.t_dealcode LIKE 'CL/%')) " +
               "          AND tick.T_DEALSTATUS != 20                                            " +
               "          AND tick.t_dealdate < TO_DATE ('01.01.2019', 'dd.mm.yyyy')             " +
               "          AND tick.t_bofficekind = 127                                           " +
               "          AND tick.t_dealtype = 2011                                             " +
               //" and rownum <= 1000 " +
               " ORDER BY tick.T_DEALSTATUS DESC                                                 ";



   var dealsCount = 0;
   var rsDealsCount = TrsbDataSet("select count(*) cnt from ( "+query+")");
   if(rsDealsCount.movenext)
      dealsCount = int(rsDealsCount.cnt);
   end;

   var i = 0;
   initProgress(dealsCount, "Выполнение шагов отложенных операций зачисления", "Выполнение отложенных операций зачисления");

   var rsDeals = TrsbDataSet(query);
   while(rsDeals.movenext)
      deals_pack[deals_pack.size] = rsDeals.dealid;
      if(dealsCount<100)
         pack_size = dealsCount;
      end;

      if(deals_pack.size == pack_size)
         if(SP_ExecDeal(deals_pack, false, oper_date) != 0)
            println("Ошибка при проведении сделок зачисления");
         end;

         initProgress(deals_pack.size, "Обработка графиков отложенных операций зачисления", "Выполнение отложенных операций зачисления");
         var j = 0;
         for(var deal, deals_pack)
            var dealCode = "";
            var resultExec = "", resultSched = "", resultAll = "";

            if(checkDealState(deal, 10, dealCode))
               resultExec = "OK";
                
               if(updateSched(deal))
                  resultSched = "OK"; 
               end;
            end;
            
            if(checkDealState(deal, 20, dealCode))
               resultAll = "OK";
            end;

            protocolLine(deal, dealCode, resultExec, resultSched, resultAll);
            useProgress(j = j + 1);
         end;

         remProgress();

         deals_pack.size = 0;
         dealsCount = dealsCount - 100;
      end;

      useProgress(i = i + 1);
   end;

   remProgress();

   protocolEnd();
end;

runExecution();