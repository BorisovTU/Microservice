//Макрос корректировки счетов -/+ПО ДК 2014
import DealsInter, spserv, sp_class, sp_car, spreserv;

PRIVATE VAR TestMode = 1; //0 - с сохранение в БД, иначе не сохраняем

PRIVATE MACRO InsertDL_VALUE(FIID:integer, Kind:integer, Dat:date, Sum:money, SumFI:integer)
  var query, cmd;

  query =   " DECLARE "
          + "   v_ID NUMBER := 0; "
          + "   v_FIID NUMBER; "
          + "   v_Kind NUMBER; "
          + "   v_Dat DATE; "
          + "   v_Sum NUMBER; "
          + "   v_SumFI NUMBER; "
          + "   v_ID_Operation NUMBER; "
          + "   v_ID_Step NUMBER;"
          + " BEGIN "

          + "   v_FIID         := ?; "
          + "   v_Kind         := ?; "
          + "   v_Dat          := ?; "
          + "   v_Sum          := ?; "
          + "   v_SumFI        := ?; "
          + "   v_ID_Operation := ?; "
          + "   v_ID_Step      := ?; "

          + "   BEGIN "
          + "     SELECT t_ID INTO v_ID "
          + "       FROM ddl_value_dbt "
          + "      WHERE t_DocKind = " + DLDOC_ISSUE
          + "        AND t_DocID = v_FIID "
          + "        AND t_Kind = v_Kind "
          + "        AND t_Date = v_Dat "
          + "        AND t_SumFIID = v_SumFI "
          + "        AND t_ID_Operation = v_ID_Operation "
          + "        AND t_ID_Step = v_ID_Step; "
          
          + "     EXCEPTION "
          + "         WHEN OTHERS THEN v_ID := 0; "
          + "   END; "

          + "   IF v_ID > 0 THEN "
          + "     UPDATE ddl_value_dbt "
          + "        SET t_Sum = v_Sum "
          + "      WHERE t_ID = v_ID;"
          + "   ELSE"
          + "     RSI_DLGR.RSI_InsertDL_VALUE("+DLDOC_ISSUE+", " 
          + "                                 v_FIID, "
          + "                                 v_Kind, "
          + "                                 v_Dat, "
          + "                                 v_Sum, "
          + "                                 v_SumFI, "
          + "                                 v_ID_Operation, "
          + "                                 v_ID_Step, "
          + "                                 0 "
          + "                                ); "
          + "   END IF;"
          + " END;";

  cmd = RSDCommand(query);

  cmd.addParam("", RSDBP_IN, FIID);
  cmd.addParam("", RSDBP_IN, Kind);
  cmd.addParam("", RSDBP_IN, Dat);
  cmd.addParam("", RSDBP_IN, Sum);
  cmd.addParam("", RSDBP_IN, SumFI);
  cmd.addParam("", RSDBP_IN, 9995);
  cmd.addParam("", RSDBP_IN, -1);

  cmd.Execute();

  return 0;
END;

private macro CreateCommonAcc(FD, CatNum, CatCode, FIID, CheckAccount)
  var q2, cmd2, ds2;
  var CommonAccDoc = TRecHandler("mcaccdoc.dbt");
  var AccDocIns = RsbSQLInsert("mcaccdoc.dbt");
  var err = 0;

  q2 =   " select * from dmcaccdoc_dbt "
       + "  where t_CatNum = ? "
       + "    and t_IsUsable = 'X' "
       + "    and t_DocKind = " + DLDOC_ISSUE
       + "    and t_DocID = ? "
       + "    and t_FIID = t_DocID "
       + "    and t_IsCommon = CHR(0) "
       + "  order by t_ActivateDate DESC";

  cmd2 = DL_RSDCommand(q2);
  cmd2.AddParam(CatNum);
  cmd2.AddParam(FIID);

  ds2 = cmd2.Execute();
  if(ds2.moveNext())

    ds2.GetRecord().CopyTo(CommonAccDoc.rec);
    
    CommonAccDoc.rec.ID = 0;
    CommonAccDoc.rec.IsCommon = SET_CHAR;
    CommonAccDoc.rec.DocKind = 0;
    CommonAccDoc.rec.DocID = 0;
    CommonAccDoc.rec.FiRole = -1;

    AccDocIns.AddRecord(CommonAccDoc);

    if(not AccDocIns.Insert())
      err = 1;
      println("   Ошибка при создании нового общесистемного счета: "+GetErrMsg());
    else
      if(CheckAccount == "")
        println("   Актуализированный общесистемный счет = " + CommonAccDoc.rec.Account);
      end;
    end;

    if((not err) and (CheckAccount != ""))
      var Account = MC_FindAndOpenCommonAccByFD(CatCode, FD, {curdate}, 0, MC_OPENACC_CHECKEXIST, NULL,CommonAccDoc.rec.Currency, 
                                                NULL, MC_PAIRACCMODE_MANUAL, CommonAccDoc.rec.Account, NULL, NULL, NULL, NULL, NULL, NULL, false, false);

      if(Account != CheckAccount)
        err = 1;
        println("   Ошибка при открытии нового общесистемного счета: "+GetErrMsg());
      else
        println("   Актуализированный общесистемный счет = " + Account);
      end;
    end; 
  else
    println("   Ошибка при актуализации общесистемного счета");
  end;

  return err;
end;

private macro ExecuteCorrect(CatNum:integer)
  var query, cmd, DataSet;
  var q, cmd1, ds;
  var currFIID;
  var FD = NULL;
  var BadAcc = TRecHandler("account.dbt");
  var RightAcc = TRecHandler("account.dbt");
  var RealAcc = TRecHandler("account.dbt");
  var CatCode = "";
  var sum = $0, cnt = 0, i = 0;
  var err = 0;

  if(CatNum == 1251)
    CatCode = "+ПО ДК 2014";
  elif(CatNum == 1252)
    CatCode = "-ПО ДК 2014";
  else
    return;
  end;

  println("Обрабатываем категорию с кодом " + CatCode);

  //Обработать +ПО ДК 2014
  query =   " select t_FIID, count(1) as cnt "
          + "  from (select distinct t_Account, t_FIID "
          + "          from dmcaccdoc_dbt "
          + "         where t_CatNum = ? and t_IsUsable = 'X') "
          + " group by t_FIID "
          + " having count(1) > 1 ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(CatNum);

  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, "Обработка категории " + CatCode, "Обработка категории " + CatCode);


    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      
      if(DataSet.cnt != 2)
        msgbox("Ошибка. Непредвиденная ситуация.");
        continue;
      end;

      currFIID = DataSet.FIID;

      //ищем общесистемный счет
      q =   " select acc.* "
          + "   from dmcaccdoc_dbt accdoc, daccount_dbt acc "
          + "  where accdoc.t_CatNum = ? "
          + "    and accdoc.t_IsCommon = 'X' "
          + "    and accdoc.t_FIID = ? "
          + "    and acc.t_Account = accdoc.t_Account "
          + "    and acc.t_Code_Currency = accdoc.t_Currency "
          + "    and acc.t_Chapter = accdoc.t_Chapter";

      cmd1 = DL_RSDCommand(q);
      cmd1.AddParam(CatNum);
      cmd1.AddParam(currFIID);

      ds = cmd1.Execute();
      if(ds.moveNext())

        ds.GetRecord().CopyTo(BadAcc.rec);

        FD = SPFirstDocFI(DLDOC_ISSUE, currFIID, currFIID, KINDPORT_SALE, {OurBank}, null, null, null);

        println("Обрабатываем бумагу " + FD.fininstr.rec.FI_Code);

        if(FD.IsExistAccount(CatCode, null, true, GetFIRoleByPortfolio(KINDPORT_SALE), RealAcc, MC_PAIRACCMODE_MANUAL, {curdate}, BadAcc.rec.Code_Currency))

          if(ds.Account != RealAcc.rec.Account)
            //общесистемный счет не совпадает со счетом по бумаге - исправляем

            RslDefCon.BeginTrans();

            //счет ищем с учётом парности, чтобы сразу на нужном счете в результате проводки изменился остаток
            if(not FD.IsExistAccount(CatCode, null, true, GetFIRoleByPortfolio(KINDPORT_SALE), RightAcc, NULL, {curdate}, BadAcc.rec.Code_Currency))
              err = 1;
              println("   Не найден счет");
            end;

            //1. Перенести остаток с неправильного общесистемного счета на правильный счет по ц/б
            if(not err)
              sum = abs(GetRestAccount(BadAcc.rec, {curdate}));
              if(sum > 0)
                var DebetAcc = TRecHandler("account.dbt");
                var CreditAcc = TRecHandler("account.dbt");


                if(CatCode == "+ПО ДК 2014")
                  DebetAcc  = BadAcc;
                  CreditAcc = RightAcc;
                else
                  DebetAcc  = RightAcc;
                  CreditAcc = BadAcc;
                end;


                if( not ПроводкаПоКатегориямУчета( FD,
                                                   DebetAcc, CreditAcc,          /* КатегДеб, КатегКредит*/
                                                   {curdate},                    /* Дата */
                                                   DebetAcc.rec.Chapter,         /* Глава */
                                                   DebetAcc.rec.Code_Currency,        /* Валюта */
                                                   sum,                          /* Сумма */
                                                   NULL,                         /* Документ */
                                                   INPCARRY,                     /* Result_Carry */
                                                   " 1",                         /* Kind_Oper */
                                                   "",                           /* Numb_Document */
                                                   "Перенос остатка на верный счет по бумаге " + FD.fininstr.rec.definition,
                                                   null
                                                 )
                  )
                   err = 1;
                   println("   Ошибка при выполнении проводки");
                else
                   println("   Выполнена проводка "+DebetAcc.rec.Account+" <-> " +CreditAcc.rec.Account+" на сумму " + sum);
                end;
              end;
            end;

            //2.Закрыть неправильный счет
            if(not err)
              if( CB_CloseAccount(BadAcc.rec.Chapter, BadAcc.rec.Code_Currency, BadAcc.rec.Account, {curdate}, null) != 0 )
                 var msg = GetErrMsg();
                 println("   Ошибка закрытия счета: "+msg+". Счет "+BadAcc.rec.Account+" не закрыт.");
                 err = 1;
              else
                 println("   Счет "+BadAcc.rec.Account+" успешно закрыт!");
              end;
            end;

            //3. Удалить неправильный счет из mcaccdoc
            if(not err)
              SQL_Execute("DELETE FROM dmcaccdoc_dbt WHERE t_Account = '"+BadAcc.rec.Account+"' and t_Currency = "+BadAcc.rec.Code_Currency+" and t_Chapter = "+BadAcc.rec.Chapter); 
              println("   Привязки счета "+BadAcc.rec.Account+" в dmcaccdoc_dbt успешно удалены!");
            end;

            //4. Обновить регистр переоценки
            if((not err) and (FD.ActualAccount("+ПО ДК 2014", null, true, GetFIRoleByPortfolio(KINDPORT_SALE), RightAcc, {curdate}, null, null, MC_PAIRACCMODE_MANUAL) != 0))
              err = InsertDL_VALUE(currFIID, DL_VALUE_KIND_PLUSDK, {curdate}, GetRestAccount(RightAcc.rec, {curdate}), RightAcc.rec.Code_Currency);
            end;

            if((not err) and (FD.ActualAccount("-ПО ДК 2014", null, true, GetFIRoleByPortfolio(KINDPORT_SALE), RightAcc, {curdate}, null, null, MC_PAIRACCMODE_MANUAL) != 0))
              err = InsertDL_VALUE(currFIID, DL_VALUE_KIND_PLUSDK, {curdate}, GetRestAccount(RightAcc.rec, {curdate}), RightAcc.rec.Code_Currency);
            end;

            //5. Сформировать новый общесистемный счет на основе счета по бумаге
            if(not err)
              err = CreateCommonAcc(FD, CatNum, CatCode, currFIID, RealAcc.rec.Account);
            end;

            if(err)
              RslDefCon.RollbackTrans();
              println("   При выполнении возникли ошибки!");
            else
              if(TestMode)
                RslDefCon.RollbackTrans();
              else 
                RslDefCon.CommitTrans();
              end;
            end;

            err = 0;

          end;
        end;

      end;

      println("");
      UseProgress(i = i + 1);
    end;

    RemProgress();
  end;

  println("Создание общесистемных счетов");

  query =   " SELECT DISTINCT accdoc.t_FIID "
          + "   FROM dmcaccdoc_dbt accdoc "
          + "  WHERE     accdoc.t_CatNum = ? "
          + "        AND accdoc.t_IsCommon = CHR (0) "
          + "        AND accdoc.t_DocKind = " + DLDOC_ISSUE
          + "        AND accdoc.t_DocID = accdoc.t_FIID "
          + "        AND accdoc.t_IsUsable = 'X' "


          + "        AND NOT EXISTS "
          + "                   (SELECT 1 "
          + "                      FROM dmcaccdoc_dbt accdoc1 "
          + "                     WHERE     accdoc1.t_CatNum = accdoc.t_CatNum "
          + "                           AND accdoc1.t_IsCommon = 'X' "
          + "                           AND accdoc1.t_DocKind = 0 "
          + "                           AND accdoc1.t_DocID = 0 "
          + "                           AND accdoc1.t_FIID = accdoc.t_FIID) ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(CatNum);

  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, "Формирование общесистемных счетов", "Формирование общесистемных счетов");

    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      currFIID = DataSet.FIID;

      FD = SPFirstDocFI(DLDOC_ISSUE, currFIID, currFIID, KINDPORT_SALE, {OurBank}, null, null, null);
      
      println("Обрабатываем бумагу " + FD.fininstr.rec.FI_Code);

      RslDefCon.BeginTrans();

      err = CreateCommonAcc(FD, CatNum, CatCode, currFIID, "");
    
      if(err)
        RslDefCon.RollbackTrans();
        println("   При выполнении возникли ошибки!");
      else
        if(TestMode)
          RslDefCon.RollbackTrans();
        else 
          RslDefCon.CommitTrans();
        end;
      end;

      err = 0;

      UseProgress(i = i + 1);
    end;

    RemProgress();
  end;

  println("");

OnError( err );
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  println("Критическая ошибка при выполнении.");
end;

ExecuteCorrect(1251);
ExecuteCorrect(1252);
