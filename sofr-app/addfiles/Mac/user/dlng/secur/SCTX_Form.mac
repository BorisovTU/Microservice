
import "DlRepForm_h.mac", "globals.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_CLOSE_TAX_DATE            = 0,
      PNFLD_LAST_CALC_DATE            = 1,
      PNFLD_IS_CALC_TO_DATE           = 2,
      PNFLD_CALC_TO_DATE              = 3,
      PNFLD_IS_CALC_FROM_DATE         = 4,
      PNFLD_CALC_FROM_DATE            = 5,
      PNFLD_IS_CALC_FROM_DATE_TO_DATE = 6,
      PNFLD_CALC_FROM_DATE_TO_DATE    = 7,
      PNFLD_AVRGROUP                  = 8,
      PNFLD_AVRCODE                   = 9,
      PNFLD_IS_CLOSE_TAX              = 10,
      PNFLD_CLOSE_TAX                 = 11,
      PNFLD_PARALLEL                  = 12;

CONST DL_PNFLD_CLOSE_TAX_DATE            = 100,
      DL_PNFLD_LAST_CALC_DATE            = 101,
      DL_PNFLD_IS_CALC_TO_DATE           = 102,
      DL_PNFLD_CALC_TO_DATE              = 103,
      DL_PNFLD_IS_CALC_FROM_DATE         = 104,
      DL_PNFLD_CALC_FROM_DATE            = 105,
      DL_PNFLD_IS_CALC_FROM_DATE_TO_DATE = 106,
      DL_PNFLD_CALC_FROM_DATE_TO_DATE    = 107,
      DL_PNFLD_AVR_GROUP                 = 108,
      DL_PNFLD_AVR_CODE                  = 109,
      DL_PNFLD_IS_CLOSE_TAX              = 110,
      DL_PNFLD_CLOSE_TAX                 = 111,
      DL_PNFLD_PARALLEL                  = 112;

PRIVATE MACRO GetLastCalcDate()
  var query = " SELECT TO_DATE(rsb_struct.getString(t_fmtblobdata_xxxx),'DD.MM.YYYY') as t_date FROM dregval_dbt " +
              "  WHERE t_KeyID = rsb_tools.find_regkey('SECUR\\DATE_BUILD_TAXREG') ";
  var cmd = DL_RSDCommand(query);

  var DataSet = cmd.Execute();
  if(DataSet.MoveNext())
    return DataSet.date;
  end;
  return ZeroDate;
END;

PRIVATE MACRO GetCloseDate()
  var query = " SELECT TO_DATE(CASE WHEN LENGTH(rsb_struct.getString(t_fmtblobdata_xxxx)) < 8 THEN '01.01.0001' ELSE rsb_struct.getString(t_fmtblobdata_xxxx) END,'DD.MM.YYYY') as t_date FROM dregval_dbt " +
              "  WHERE t_KeyID = rsb_tools.find_regkey('SECUR\\DATE_CLOSE_TAXREG') ";
  var cmd = DL_RSDCommand(query);

  var DataSet = cmd.Execute();
    if(DataSet.MoveNext())
      return DataSet.date;
    end;
  return ZeroDate;
END;

/*Группа налогового учета*/
PRIVATE MACRO FldProc_AvrGroupFltr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  record llvalues("llvalues.dbt");
  VAR Choice, Select;
  VAR Avoiriss, AvrID;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetLLVALname (2903, FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( pThis.ExistField(DL_PNFLD_AVR_CODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVR_CODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVR_CODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Avoiriss = TRecHandler( "avoiriss.dbt" );
              if( ПолучитьФинИн( AvrID, null, Avoiriss) OR
                  (GetLLVALname (2903, Avoiriss.rec.TaxGroup) != FldRealValue)
                )
                 pThis.SetLinkedValue( DL_PNFLD_AVR_CODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     //группы 12, 13, 22, 35, 36, 37, 919, 943  выбрать нельзя

     Select =   " select t_Element, t_Code, t_Name from dllvalues_dbt where t_List = 2903 "
              + " order by t_Element asc";

     Choice = DL_Scroll(Select,
                        "Группы налогового учета", pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_Element","Номер","t_Code","Код","t_Name","Название"
                       );

     if(Choice != NULL)
        FldShowValue = Choice.Value("t_Name");    
        FldRealValue = Choice.Value("t_Element");
     end;
  end;

  return CM_DEFAULT;
END;

/*Ценная бумага*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR AddTable = "", WhereCond = "1=1", AvrAttrID;
  VAR RetVal = CM_DEFAULT;
  VAR Fininstr = TRecHandler( "fininstr.dbt" );
  VAR Avoiriss = TRecHandler( "avoiriss.dbt" );

  if( Cmd == DLG_KEY ) 
     if( (Key == KEY_F3) OR (Key == KEY_ALTF3) OR (Key == KEY_CTRLF3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

        AddTable  = AddTable  + "davoiriss_dbt AV";
        WhereCond = WhereCond + " AND AV.t_FIID = t.t_FIID ";

        AvrAttrID = pThis.GetLinkedValue(DL_PNFLD_AVR_GROUP);
        if( (AvrAttrID != NULL) AND (AvrAttrID > 0) )
           WhereCond = WhereCond + " AND AV.t_TaxGroup = " + AvrAttrID;
        end;

        /* только неиндивидуальные (FI_IsSecurIndividualNoFind) */    
        if( AddTable != "" )
           AddTable  = AddTable + ",";
        end;
        AddTable  = AddTable  + " davrkinds_dbt AKind";
        WhereCond = WhereCond + " AND (AKind.t_FI_Kind = t.t_FI_Kind AND AKind.t_AvoirKind = t.t_AvoirKind)";


        if( Key == KEY_F3 )
           /*эмиссионные (обобщенные) или (фактические, для которых не указана ссылка на обобщенный) или неэмиссионные любые*/
           WhereCond = WhereCond + " AND ( AKind.t_IsEmissive != 'X' OR ( t.t_IsGeneralized = 'X' OR t.t_GeneralizedFIID < 0 ) )";
        elif( Key == KEY_ALTF3 )
           /* фактические выпуски, у которых указана ссылка на обобщенный*/
           WhereCond = WhereCond + " AND ( t.t_IsGeneralized != 'X' AND t.t_GeneralizedFIID > 0 )";
        elif( Key == KEY_CTRLF3 )
           /*все неиндивидуальные и необобщенные (фактические) ц/б*/
           WhereCond = WhereCond + " AND t.t_IsGeneralized != 'X' ";
        end;

        if( ListAvoiriss( 0, Fininstr, Avoiriss, null, WhereCond, AddTable ) )
           FldRealValue = Fininstr.rec.FIID;
           FldShowValue = Fininstr.rec.FI_Code;
           
           pThis.SetLinkedValue( DL_PNFLD_AVR_GROUP, Avoiriss.rec.TaxGroup );
        end;
     end;

     if(Key == KEY_SPACE)
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
     end;
  else
     RetVal = DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;

  return RetVal;
END;

private macro Default(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER,
                      FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
end;

PRIVATE MACRO DL_FldProc_Radio_Calc_To( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = UNSET_CHAR;
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      if(FldRealValue == UNSET_CHAR)
        FldShowValue = FldRealValue = SET_CHAR;
        pThis.SetLinkedValue(DL_PNFLD_IS_CALC_FROM_DATE, UNSET_CHAR);
        pThis.SetLinkedValue(DL_PNFLD_IS_CALC_FROM_DATE_TO_DATE, UNSET_CHAR);
        pThis.SetLinkedValue(DL_PNFLD_IS_CLOSE_TAX, UNSET_CHAR);
      else
        FldShowValue = FldRealValue = UNSET_CHAR;
      end;
    end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_Radio_Calc_from( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = UNSET_CHAR;
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      if(FldRealValue == UNSET_CHAR)
        FldShowValue = FldRealValue = SET_CHAR;
        pThis.SetLinkedValue(DL_PNFLD_IS_CALC_TO_DATE, UNSET_CHAR);
        pThis.SetLinkedValue(DL_PNFLD_IS_CLOSE_TAX, UNSET_CHAR);
      else
        FldShowValue = FldRealValue = UNSET_CHAR;
      end;
    end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_Radio_Calc_Close( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = UNSET_CHAR;
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      if(FldRealValue == UNSET_CHAR)
        FldShowValue = FldRealValue = SET_CHAR;
        pThis.SetLinkedValue(DL_PNFLD_IS_CALC_FROM_DATE, UNSET_CHAR);
        pThis.SetLinkedValue(DL_PNFLD_IS_CALC_FROM_DATE_TO_DATE, UNSET_CHAR);
        pThis.SetLinkedValue(DL_PNFLD_IS_CALC_TO_DATE, UNSET_CHAR);
      else
        FldShowValue = FldRealValue = UNSET_CHAR;
      end;
    end;
  end;

  return CM_DEFAULT;
END;

private macro DL_FLDProc_Parallel(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER,
                                  FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) 
      FldRealValue = 0;
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    if(FldShowValue > 8)
      msgbox("Устанавливать значение выше 8 не рекомендуется");
    end;
    FldRealValue = FldShowValue;
  end;

  return CM_DEFAULT;
end;

CLASS (DL_CPanel) ARNU_SCTX_Form()

  PRIVATE MACRO Init()
     var LastCaclDate = GetLastCalcDate();
     var CloseDate = GetCloseDate();

     DisableFields( This.Data(), PNFLD_CLOSE_TAX_DATE );
     DisableFields( This.Data(), PNFLD_LAST_CALC_DATE );

     AddFieldProc( 0, PNFLD_CLOSE_TAX_DATE,      DL_PNFLD_CLOSE_TAX_DATE,       @DL_FLDProc_BeginDate, CloseDate ); 
     AddFieldProc( 0, PNFLD_LAST_CALC_DATE,      DL_PNFLD_LAST_CALC_DATE,       @DL_FLDProc_BeginDate, LastCaclDate);
     AddFieldProc( 0, PNFLD_IS_CALC_TO_DATE,     DL_PNFLD_IS_CALC_TO_DATE,      @DL_FldProc_Radio_Calc_To);
     AddFieldProc( 0, PNFLD_CALC_TO_DATE,        DL_PNFLD_CALC_TO_DATE,         @DL_FLDProc_BeginDate);
     AddFieldProc( 0, PNFLD_IS_CALC_FROM_DATE,   DL_PNFLD_IS_CALC_FROM_DATE,    @DL_FldProc_Radio_Calc_from);
     AddFieldProc( 0, PNFLD_CALC_FROM_DATE,      DL_PNFLD_CALC_FROM_DATE,       @DL_FLDProc_BeginDate, LastCaclDate);
     AddFieldProc( 0, PNFLD_IS_CALC_FROM_DATE_TO_DATE, DL_PNFLD_IS_CALC_FROM_DATE_TO_DATE, @DL_FldProc_Radio_Calc_from);
     AddFieldProc( 0, PNFLD_CALC_FROM_DATE_TO_DATE, DL_PNFLD_CALC_FROM_DATE_TO_DATE,       @DL_FLDProc_BeginDate);
     AddFieldProc( 0, PNFLD_AVRGROUP,            DL_PNFLD_AVR_GROUP,             @FldProc_AvrGroupFltr, null, 10, 8 );
     AddFieldProc( 0, PNFLD_AVRCODE,             DL_PNFLD_AVR_CODE,              @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_IS_CLOSE_TAX,        DL_PNFLD_IS_CLOSE_TAX,          @DL_FldProc_Radio_Calc_Close );
     AddFieldProc( 0, PNFLD_CLOSE_TAX,           DL_PNFLD_CLOSE_TAX,             @DL_FLDProc_BeginDate, ZeroDate );
     AddFieldProc( 0, PNFLD_PARALLEL,            DL_PNFLD_PARALLEL,              @DL_FLDProc_Parallel, 0 );
  END;

  PRIVATE MACRO Check():BOOL
     if(GetFieldValue(PNFLD_IS_CALC_TO_DATE) == SET_CHAR)
       if(GetFieldValue(PNFLD_CALC_TO_DATE) > {curdate})
         msgbox("Дата должна быть меньше текущей");
         return false;
       end;
     elif(GetFieldValue(PNFLD_IS_CALC_FROM_DATE) == SET_CHAR)
       if(GetFieldValue(PNFLD_CALC_FROM_DATE) > GetLastCalcDate())
         msgbox("Дата должна быть меньше даты последнего расчета");
         return false;
       end;
       if(GetFieldValue(PNFLD_CALC_FROM_DATE_TO_DATE) > {curdate})
         msgbox("Дата должна быть меньше текущей");
         return false;
       end;
       if(GetFieldValue(PNFLD_CALC_FROM_DATE) > GetFieldValue(PNFLD_CALC_FROM_DATE_TO_DATE))
         msgbox("Дата должна быть не меньше даты начала расчета");
         return false;
       end;
     elif(GetFieldValue(PNFLD_IS_CLOSE_TAX) == SET_CHAR)
       if(GetFieldValue(PNFLD_CLOSE_TAX) > GetLastCalcDate())
         msgbox("Дата должна быть меньше даты последнего расчета");
         return false;
       end;
     elif( (GetFieldValue(PNFLD_IS_CLOSE_TAX) == UNSET_CHAR) and 
           (GetFieldValue(PNFLD_IS_CALC_TO_DATE) == UNSET_CHAR) and 
           (GetFieldValue(PNFLD_IS_CALC_FROM_DATE) == UNSET_CHAR) )
         msgbox("Тип расчета должен быть заполнен");
         return false;
     end;
     return true;
  END;

  initDL_CPanel("ARNU.lbr", "SCTXRUN");
END;
