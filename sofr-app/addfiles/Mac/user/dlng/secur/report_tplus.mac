/*----------------------------------------------------------------------------*/
/*               Отчет по переоценке Т+                                      */
/*----------------------------------------------------------------------------*/
import globals      ,
       sp_class     ,
       Календарь    ,
       RsbFormsInter,
       captureoutput;

Import "or_tpl_h.mac",
       "or_const.mac",
       "scservop.mac";

var ExitCode:integer, FProcPanel:TRsbPanel;

var ObjTempl :CTemplateXLS = CTemplateSXLSX(NULL); // нужны аргументы, а лучше брать новую версию инструмента, там аргументы не требуются

private const FT_INT32  = 1;
private const FT_STRING = 7;
private const FT_DATE   = 9;

Private var csvTable;

var csvFile;
//    ObjTempl; 
var FName    :string;

var i        :integer      = 0;
var Size     :integer      = 2;

var npp = 0;
/*----------------------------------------------------------------------------*/
/*                   Если используем POI зададим формат                       */
/*----------------------------------------------------------------------------*/
Private MACRO CREATE_XLS()
  var printEngine = CTemplateXLS(NULL, NULL, NULL, NULL, NULL, NULL, true, false);

  // Если используем POI зададим формат
  if( printEngine.UsePoi() )
    printEngine.SetXLSXFormat();
  end;

  // Итоговая книга для результата
  printEngine.CreateTotalBook(); 

  return printEngine;

  OnError

  return NULL;
END;
/*----------------------------------------------------------------------------*/
/*----------------------------------------------------------------------------*/
macro GetData(BegDate, EndDate)
  
  var rs, cmd, query;
  StartCaptureOutput();
  [ 
       SELECT f.t_fiid,
              f.t_definition,
              a.t_isin,
              (SELECT t_shortname FROM dparty_dbt WHERE t_partyid = f.t_issuer) issuer,
              f.t_facevalue,
              t.t_kind,
              t.t_sum,
              tick.t_portfolioid,
              tick.t_dealcode,
              tick.t_dealdate,
              (SELECT MAX ( DECODE (t_factdate, TO_DATE ('01010001', 'ddmmyyyy'), t_plandate, t_factdate)) expdate
                 FROM ddlrq_dbt WHERE t_dockind = tick.t_bofficekind AND t_docid = tick.t_dealid) expdate,
              leg.t_principal,
              (SELECT t_account FROM dmcaccdoc_dbt WHERE t_catnum = 318 AND t_dockind = 101 AND t_docid = tick.t_dealid) gacc,
              (SELECT ABS (rsb_account.restall (t_account, t_chapter, t_currency, :p_calcdate)) 
                 FROM dmcaccdoc_dbt WHERE t_catnum = 318 AND t_dockind = 101 AND t_docid = tick.t_dealid) restgacc,
              (SELECT t_account
                 FROM (  SELECT *
                           FROM (SELECT a.t_account,
                                        ABS (rsb_account.restall (a.t_account, a.t_chapter, a.t_currency, :p_calcdate)) rest
                                   FROM    dmcaccdoc_dbt a
                                        LEFT JOIN
                                           dmctempl_dbt mctempl
                                        ON     mctempl.t_catid = a.t_catid
                                           AND mctempl.t_number = a.t_templnum
                                           AND mctempl.t_isclose = CHR (0)
                                  WHERE t_catnum IN (1247, 1248, 1249, 1250, 152, 162)
                                        AND mctempl.t_value1 = tick.t_portfolioid
                                        AND t_fiid = tick.t_pfi
                                        /*AND t_iscommon = CHR (88)*/) ORDER BY rest DESC) WHERE ROWNUM = 1) ovracc, /*KEA IS 522716*/
              tick.t_dealid dealid,
              tick.t_bofficekind dockind,
              leg.t_totalcost totalcost,
              case when regexp_like(
                                    (select oprkoper.t_systypes 
                                       from doprkoper_dbt oprkoper 
                                      where oprkoper.t_kind_operation = tick.t_dealtype 
                                        and regexp_like(oprkoper.t_systypes, '[B|S]'/*Покупка/Продажа*/)), 'B') then 'B' else 'S' end dealtype
         FROM ddl_value_dbt t,
              dfininstr_dbt f,
              davoiriss_dbt a,
              ddl_tick_dbt tick,
              ddl_leg_dbt leg
        WHERE     t.t_dockind = tick.t_bofficekind
              AND t.t_docid = tick.t_dealid
              AND t.t_sum <> 0
              AND tick.t_pfi = a.t_fiid
              AND a.t_fiid = f.t_fiid
              AND tick.t_dealid = leg.t_dealid
              AND leg.t_legkind = 0
              AND t.t_date =
                     (SELECT MAX (t_date)
                        FROM ddl_value_dbt
                       WHERE     t_dockind = t.t_dockind
                             AND t_docid = t.t_docid
                             AND t_date <= :p_calcdate
                             AND t_date >=
                                    rsi_rsbcalendar.
                                     GetDateAfterWorkDay (:p_calcdate, -1))
     ORDER BY t_fiid, t_dealdate, t_dealcode
  ];
  query = EndCaptureOutput();
  cmd = RSDCommand(query);
  cmd.AddParam("", RSDBP_IN, BegDate);
  cmd.AddParam("", RSDBP_IN, BegDate);
  cmd.AddParam("", RSDBP_IN, BegDate);
  cmd.AddParam("", RSDBP_IN, BegDate);
  rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  npp = 0;

  BegAction();
  while(rs and rs.MoveNext())
    Message(npp);
    var restgacc = rs.value("restgacc");
    if ((rs.value("dockind") == 101) and (rs.value("gacc") != NULL))
      restgacc = rs.value("totalcost") + GetSumTrnOverFromDocs(rs.value("dealid"), 101, BegDate, IIF(rs.value("dealtype") == "B", false, true)) + rs.value("t_sum");
    end;
    csvFile.AddCell( rs.value("ovracc") );
    csvFile.AddCell( rs.value("t_isin") );
    csvFile.AddCell( rs.value("issuer") );
    csvFile.AddCell( rs.value("t_dealcode") );
    csvFile.AddCell( sql_convtypedate(rs.value("t_dealdate")) );
    csvFile.AddCell( sql_convtypedate(rs.value("expdate")) );
    csvFile.AddCell( rs.value("t_principal") );
    csvFile.AddCell( rs.value("t_facevalue") );
    csvFile.AddCell( rs.value("gacc") );
    csvFile.AddCell( restgacc );
    csvFile.AddCell( rs.value("t_sum") );
    csvFile.AddRow();
    npp = npp + 1;
  end;
  EndAction();
end;
/*----------------------------------------------------------------------------*/
/*----------------------------------------------------------------------------*/
class(TRsbPanel) DatePanel()

  var LBegDate:TRsbLabel,
      LEndDate:TRsbLabel,
      FieldBegDate:TRsbEditField,
      FieldEndDate:TRsbEditField,
      BegDate:date = date(0,0,0),
      EndDate:date = date(0,0,0);

  InitTRsbPanel();
  SetCaption("Выберите дату отчета");
  SetStatus("~Esc~ Выход ~F3~ Календарь ~F2~ Выполнить ~F9~ Выполнить");
  SetSize(28, 4);
  SetPosition(45,10);
  
  LBegDate = TRsbLabel(3, 2, "Дата отчета:");
  addLabel( LBegDate );

  FieldBegDate = TRsbEditField(FT_DATE);
  FieldBegDate.setPosition(15,2);
  FieldBegDate.setSize(10,1);
  FieldBegDate.Name = "BegDate";
  FieldBegDate.bindValue( this, "BegDate" );
  FieldBegDate.value = {Curdate};
  addControl( FieldBegDate );

  addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "KeyEvent"));//KeyEvent
  
  
  macro KeyEvent(RsbEvent:Object)
    var focusedControl;
    
    if( rsbEvent.KeyCode == 27 ) //ESC
      this.Close( rsbEvent.KeyCode );
    elif( ( rsbEvent.KeyCode == 316 ) or // F2
          ( rsbEvent.KeyCode == 323 ) )  // F9
      BegDate = FieldBegDate.value;
      this.Close(rsbEvent.KeyCode);
    elif( rsbEvent.KeyCode == 317 ) //F3
      focusedControl = this.focusedControl();
      BegDate = GetDateByCalendar(BegDate);
      FieldBegDate.Value = BegDate;
    end;
  end;
  
end;
/*----------------------------------------------------------------------------*/

if( ( ObjTempl = CREATE_XLS() ) == NULL )
  msgbox ("Невозможно запустить табличный редактор");
  return;
end;

FProcPanel = DatePanel();
ExitCode = FProcPanel.Run();

if(ExitCode)

  // Заполним шаблон данными из csv-файла
  if( ObjTempl.OpenTemplate ("user_tplus.xlsx", true) )

      message( "Формирование отчета по переоценке Т+" );
  	
      csvTable = ObjTempl.RegisterTable("templateTable", "templateTableHeader", false);
      csvFile = C_CSVFile();

      // Лимит для печати больших отчетов
      if (ObjTempl.UsePoi())
        csvFile.SetLimitRow(FLUSH_COUNT_XLSX);
      end;

      // Заполним поименованные ячейки
      ObjTempl.SetValue_NameCell("TemplateTitle", "Данные по переоценке будущих бумаг в портфеле за " + FProcPanel.BegDate );

      // Скопируем заголовок
      ObjTempl.CopyAllSheetInTotalBook (null, false, "templateHeader", 0,"Протокол");

      // Зададим имя для csv-файла
      fName = csvFile.CreateFullFileName("user_tplus_csv.txt");

      // Заполним файл данных прикладными данными
      if( csvFile.FileOpen( FName, true)  )
        GetData(FProcPanel.BegDate, FProcPanel.EndDate);
      else
          msgbox("Ошибка открытия csv-файла");
      end;

      // Закроем файл и сохраним его
      csvFile.FileClose();

      // После скопируем таблицу в шаблон и закроем ее
      csvTable.FillTabelFromCSV(csvFile.GetlsFileName());
      csvTable.EndTable();

      // Скопируем таблицу в итоговую книгу
      ObjTempl.CopyAllSheetInTotalBook( null,                   //Имя закладки,по умолчанию имя закладки из шаблона
                                        false,                  // true - на новый лист, false - на существующий(если имя листа отличается, всегда на новый лист)
                                        csvTable.GetTableRange, //Что копировать м.б. диапозон вида "A1:T60" или имя именованного диапозона
                                        0,
                                        "Протокол" );

      // Закроем шаблон
      ObjTempl.Close();

      // Сохраним итоги. Можно как задать имя, так и не задавать. Лучше не задавать, WinRep сам определит имя и сохранит, а после выведет результат
      ObjTempl.SaveTotalBook("tplus_user");

  else
    msgbox("Ошибка открытия шаблона");
  end;
end;
Exit( 1 );
