/**
 @file SendMessQI.mac
 @brief Функционал создания уведомлений о включении в реестр квалифицированных инвесторов
 
 Файл содержит функционал для формирования уведомления о включении субъекта в реестр
 квалифицированных инвесторов.
 
  # tag
 - functional_block:Отчетность_Клиент
 - code_type:API
 - code_type:Report
 - Квалификация
 - КИ
 - Квалифицированный инвестор
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |05.12.2023 |Топорков Д.В.|BIQ-16875 BOSS-194 BOSS-1282 BOSS-1421                    | Модификация формы уведомления, согласно BOSS-194 история 2
 |28.09.2018 |Филиппов Б.В.|                                                          | Создание
 */

Import rslscr, or_rep_h;
Import rsd, RsbDataSet;
import email_library;
import RsbFormsInter, globals, Календарь, CTInter;
import "dlquery.mac";
import "dbo_qi_RecognitionNotification.mac";

/* протокол */
macro PrintHeader(ReportFileName: String)
   SetOutput(ReportFileName, false);
   [ Протокол формирования уведомлений о признании лица квалифицированным инвестором];
   [┌──────────────────────────────────────┬──────────────────────────────────────────────┬─────────────────────────────────────────┐
    │           Клиент                     │                   Файл                       │                                         │
    ├──────────────────────────────────────┼──────────────────────────────────────────────┼─────────────────────────────────────────┤];
   SetOutput (NULL, true);
end;

macro PrintLine(_client_name, _file_name, _result)
   [│######################################│##############################################│#########################################│](_client_name:w, _file_name:w, _result:w);
end;

macro PrintInfo(clientName: String, fileName: String, result: String, ReportFileName: String)
  var part1: String;
  var part2: String;
  var part3: String;
  var clientNameLines: TArray = StrSplit2(clientName, 38, false);
  var fileNameLines: TArray = StrSplit2(fileName, 46, false);
  var resultLines: TArray = StrSplit2(result, 41, false);
  var maxLines: Integer = resultLines.size;

  SetOutput(ReportFileName, true);
  
  if (maxLines < fileNameLines.size)
    maxLines = fileNameLines.size;
  elif (maxLines < clientNameLines.size)
    maxLines = clientNameLines.size;
  end;
  
  var i: Integer = 0;
  while (i < maxLines)
    part1 = part2 = part3 = "";
  
    if (i < clientNameLines.size)
      part1 = clientNameLines(i);
    end;
    
    if (i < fileNameLines.size)
      part2 = fileNameLines(i);
    end;
    
    if (i < resultLines.size)
      part3 = resultLines(i);
    end;
    
    PrintLine(part1, part2, part3);
    
    i = i + 1;
  end;

  SetOutput (NULL, true);
end;

macro PrintBottom(ReportFileName: String)
   SetOutput(ReportFileName, true);
   [└──────────────────────────────────────┴──────────────────────────────────────────────┴─────────────────────────────────────────┘];
end;

private macro DDMMYYYY( dt ):string
  var day, mon, year, str;
  DateSplit( date(dt), day, mon, year );
  str = string(day:2, mon:2, string(year));
  str = StrSubst ( str, " ", "0" );
  return str;
end;

Macro CreateRecognitionNotifications(_DateBegin, _DateEnd)
    var error = "";
    var select, rs;
           
    select = " SELECT party.t_partyid, party.t_shortname, party.t_name,                                  " +
             "        scqinvhist.t_begdate, dlcontr.t_dlContrID                                          " +
             "   FROM dv_scqinvhist scqinvhist                                                           " +
             "        JOIN dsfcontr_dbt sfcontr ON scqinvhist.t_partyid = sfcontr.t_partyid              " +
             "        JOIN ddlcontr_dbt dlcontr ON dlcontr.t_sfcontrid = sfcontr.t_id                    " +
             "        JOIN dparty_dbt party     ON sfcontr.t_partyid = party.t_partyid /*AND ROWNUM = 1*/" +
             "  WHERE scqinvhist.t_state = 1                                                             " +
             "        AND scqinvhist.t_begdate BETWEEN :beg_date AND :end_date                           " +
             "        AND (scqinvhist.t_EndDate = TO_DATE ('01.01.0001', 'DD.MM.YYYY')                   " +
             "             OR scqinvhist.t_EndDate > :end_date2)                                         " +
// Проверка на наличие сообщения на договоре убрана по согласованию с аналитиком
//             "        AND NOT EXISTS                                                                     " + 
//             "                   (SELECT 1                                                               " +
//             "                      FROM ddlcontrmsg_dbt msg                                             " +
//             "                     WHERE msg.t_DlContrID = dlcontr.t_dlcontrid                           " +
//             "                           AND msg.t_kind = 501)                                           " +
             "          group by party.t_partyid, party.t_shortname, party. t_name, scqinvhist.t_begdate," +
             "                   dlcontr.t_dlContrID                                                     ";

    var cmd_main = DL_RSDCommand(select);
        cmd_main.addParam(_DateBegin); 
        cmd_main.addParam(_DateEnd); 
        cmd_main.addParam(_DateEnd);

    var cnt = cmd_main.GetCount();
    var i = 0;
    var params = CQIRecognitionParams();
    
    params.signer = GetQIDefaultSigner();
    params.notificationDate = Date();
    params.isDocOutput = true;
    params.isPdfOutput = true;
    params.isSendEmail = false;

    rs = cmd_main.execute();

    FILE ReportOutFile() txt write;
    var ReportFileName = GetTxtFileName("SendMessQI_" + DDMMYYYY({curdate}));
    ReportFileName = SubStr(ReportFileName, 1, index(ReportFileName, ".", 3)-1) + ".out";

    PrintHeader(ReportFileName);
    InitProgress(cnt, "Формирование уведомлений", "Формирование уведомлений");

    while(rs.MoveNext())
        params.dlContrID = rs.dlContrID;
        params.client = GetClient(rs.dlContrID);
        params.recognitionDate = rs.begDate;
        
        error = QIRecognitionNotification(params);
        PrintInfo(params.client.shortName, params.resultFileName, error, ReportFileName);

        i = i + 1;
        UseProgress(i);
     end;

     RemProgress;
     PrintBottom(ReportFileName);
end;

private const FT_STRING = 7;
private const FT_DATE = 9;
class (TRsbPanel) DatePeriodPanel(_dateBegin,_dateEnd)
    InitTRsbPanel();
    setSize(35,6);
    setPosition(35,15);

    var ResultDateBegin = _dateBegin;
    var ResultDateEnd = _dateEnd;

    class (TRsbEditField) EditField(typeFld: integer, x: integer, y: integer, width: integer, height: integer, bindVal, active: bool, fieldName: string, panel)
        var bindString = string(bindVal);
        
        initTRsbEditField(typeFld);
        setPosition(x, y);
        setSize(width, height);
        
        if(active == false) 
            editable = focusable = false; 
        end;

        if(typeFld == FT_STRING)
            bindValue( this, "bindString", 100 );
        elif(bindVal != null)
            value = bindVal; 
        end;

        if(panel != null)
            panel.addControl(this); 
        end;

        if(fieldName != null) 
            name = fieldName; 
        end;
    end;

    addLabel(TRsbLabel(3, 2, "Начало периода:"));
    var m_DateBegin = EditField( FT_DATE, 20, 2, 10, 1, _dateBegin, true, "Date", this );
    
    addLabel(TRsbLabel(3, 4, "Конец  периода:"));
    var m_DateEnd   = EditField( FT_DATE, 20, 4, 10, 1, _dateEnd, true, "Date", this );

    m_DateBegin.addEventHandler(RSB_EV_KEY_PRESSED,  R2M(this, "DATEBEGIN_KEY_PRESSED"));
    m_DateEnd.addEventHandler(RSB_EV_KEY_PRESSED,  R2M(this, "DATEEND_KEY_PRESSED"));
    addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "Panel_ButtonEvent"));

    macro DATEBEGIN_KEY_PRESSED(RsbEvent)
       if(RsbEvent.keyCode == 317)
           m_DateBegin.value = GetDateByCalendar(m_DateBegin.value);
       elif((RsbEvent.keyCode == 408) OR (RsbEvent.keyCode == 411))/*KEY_ALT_UP KEY_ALT_LEFT*/
           m_DateBegin.value = m_DateBegin.value - 1;
       elif((RsbEvent.keyCode == 416) OR (RsbEvent.keyCode == 413))/*KEY_ALT_DOWN KEY_ALT_RIGHT*/
           m_DateBegin.value = m_DateBegin.value + 1;
       end;
    end;

    macro DATEEND_KEY_PRESSED(RsbEvent)
       if(RsbEvent.keyCode == 317)
           m_DateEnd.value = GetDateByCalendar(m_DateEnd.value);
       elif((RsbEvent.keyCode == 408) OR (RsbEvent.keyCode == 411))/*KEY_ALT_UP KEY_ALT_LEFT*/
           m_DateEnd.value = m_DateEnd.value - 1;
       elif((RsbEvent.keyCode == 416) OR (RsbEvent.keyCode == 413))/*KEY_ALT_DOWN KEY_ALT_RIGHT*/
           m_DateEnd.value = m_DateEnd.value + 1;
       end;
    end; 

    macro Panel_ButtonEvent(RsbEvent)
       if((RsbEvent.keyCode == 316) or (RsbEvent.keyCode == 13))
          ResultDateBegin = m_DateBegin.value;
          ResultDateEnd = m_DateEnd.value;
          close(1);       
       elif(RsbEvent.keyCode == 27)
          close(0);
       end;
    end;
end;

macro getPeriod(_dateBegin,_dateEnd,_caption)
    if(not _dateBegin)
        _dateBegin = {curdate};
    end;
    if(not _dateEnd)
        _dateEnd = {curdate};
    end;

    var panel = DatePeriodPanel(_dateBegin,_dateEnd);
        panel.setCaption(_caption);

    if(panel.run == 1)
        setparm(0,panel.ResultDateBegin);
        setparm(1,panel.ResultDateEnd);
        return true;
    end;
    return false;
end;

/// Проверка пользователя запускающего процедуру
if (not CheckQIUserRights())
  MsgBox("Для данной роли функция недоступна.");
  exit(1);
end;

var DateBegin, DateEnd;
if (GetPeriod(DateBegin, DateEnd, "Выберите период"))
    CreateRecognitionNotifications(DateBegin, DateEnd);
end; 
     
exit(0);

  







  
