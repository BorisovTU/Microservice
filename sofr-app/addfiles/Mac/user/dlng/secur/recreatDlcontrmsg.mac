/*  
$Name:         recreatDlcontrmsg.mac
$Module:       Ядро Securities
$Description:  Создание/пересоздание сообщений ДБО
*/

import dlcontrmsg, rsbDataSet;

macro recreateIISOpenMessage(DlContr)
    private var SfContr = TRecHandler( "sfcontr" );
    private var dlcontrmsg = TRecHandler("dlcontrmsg");

    var rsb = TRsbDataSet(" SELECT * "
                        + "   FROM dsfcontr_dbt sfcontr "
                        + "  WHERE SFCONTR.T_ID = " + int(DlContr.rec.sfcontrid));
    
    if(not rsb.movenext)
        return;
    end;

    rsb.GetRecord().CopyTo(SfContr.rec);

    rsb = TRsbDataSet(" SELECT * "
                    + "   FROM ddlcontrmsg_dbt "
                    + "  WHERE     t_dlcontrid = " + int(DlContr.rec.dlcontrid)
                    + "        AND t_kind = " + OBJTYPE_DLCONTRMSG_IISOPEN);

    if(not rsb.movenext)
        return;
    end;

    rsb.GetRecord().CopyTo(dlcontrmsg.rec);

    var mFNS = NPTX_MFNS_Report_XML(GetFNSData(OBJTYPE_DLCONTRMSG_IISOPEN, DlContr, SfContr));
    var XMLFileName = mFNS.Run();

    RslDefCon.BeginTrans();

    rsb = TRsbDataSet(" SELECT msg.t_id, "
                    + "        msg.t_dlcontrid, "
                    + "        msg.t_kind, "
                    + "        dim.t_imageid, "
                    + "        dim.t_objectid, "
                    + "        dim.t_objecttype, "
                    + "        t_filename, "
                    + "        DIG.T_FMTBLOBDATA_XXXX, "
                    + "        DBMS_LOB.GETLENGTH (DIG.T_FMTBLOBDATA_XXXX) AS t_size "
                    + "   FROM ddlcontrmsg_dbt msg, dimgdata_dbt dim, dimage_dbt dig "
                    + "  WHERE     LPAD (msg.t_id, 34, '0') = dim.t_objectid "
                    + "        AND msg.t_kind = " + OBJTYPE_DLCONTRMSG_IISOPEN
                    + "        AND dim.t_objecttype = " + OBJTYPE_BSCMSG_DL
                    + "        AND dim.t_imageid = dig.t_imageid "
                    + "        AND msg.t_dlcontrid = " + int(DlContr.rec.dlcontrid));

    if(rsb.movenext)
        rsdCommand("DELETE dimage_dbt WHERE t_imageid = " + rsb.imageid).execute();
        rsdCommand("DELETE dimgdata_dbt WHERE t_imageid = " + rsb.imageid).execute();
    end;

    if(not LoadImageObj(XMLFileName, OBJTYPE_BSCMSG_DL, UniID(dlcontrmsg, OBJTYPE_BSCMSG_DL), 1))
        RslDefCon.RollbackTrans();
        msgbox("К записи о сообщении не удалось прикрепить файл сообщения");
    end;

 
    if(RslDefCon.IsInTrans())
       RslDefCon.CommitTrans();
    end;        

onError(e)
    if(RslDefCon.IsInTrans())
       RslDefCon.RollbackTrans();
    end;
end;

macro recreatDlcontrMessage_market(DlContr, Kind, MpId, MarketId)
    private var SfContr = TRecHandler( "sfcontr" );
    private var dlcontrmsg = TRecHandler("dlcontrmsg");
    var rsb = TRsbDataSet(" SELECT * "
                        + "   FROM dsfcontr_dbt sfcontr "
                        + "  WHERE SFCONTR.T_ID = " + int(DlContr.rec.sfcontrid));
    
    if(not rsb.movenext)
        return 1;
    end;

    rsb.GetRecord().CopyTo(SfContr.rec);

    if (Kind != OBJTYPE_DLCONTRMSG_MPREG)
       rsb = TRsbDataSet(" SELECT * "
                       + "   FROM ddlcontrmsg_dbt "
                       + "  WHERE t_dlcontrid = " + int(DlContr.rec.dlcontrid)
                       + "    AND t_kind = " + Kind);
       if(rsb.movenext)
          if ( (Kind == OBJTYPE_DLCONTRMSG_MARKETREG) and (rsb.isonlineSendmes == strFor(88)) )
             msgbox("Запрос на онлайн-регистрацию договора на МБ уже существует");
             return 1;
          elif (Kind == OBJTYPE_DLCONTRMSG_BIDCODE)
             msgbox("Запрос на бронирование кода по договору уже существует");
             return 1;
          elif (Kind == OBJTYPE_DLCONTRMSG_IISOPEN)
             return 1;
          end;

          RslDefCon.BeginTrans();

          rsb = TRsbDataSet(" SELECT msg.t_id, "
                          + "        msg.t_dlcontrid, "
                          + "        msg.t_kind, "
                          + "        dim.t_imageid, "
                          + "        dim.t_objectid, "
                          + "        dim.t_objecttype, "
                          + "        t_filename, "
                          + "        DIG.T_FMTBLOBDATA_XXXX, "
                          + "        DBMS_LOB.GETLENGTH (DIG.T_FMTBLOBDATA_XXXX) AS t_size "
                          + "   FROM ddlcontrmsg_dbt msg, dimgdata_dbt dim, dimage_dbt dig "
                          + "  WHERE     LPAD (msg.t_id, 34, '0') = dim.t_objectid "
                          + "        AND msg.t_kind = " + Kind
                          + "        AND dim.t_objecttype = " + OBJTYPE_BSCMSG_DL
                          + "        AND dim.t_imageid = dig.t_imageid "
                          + "        AND msg.t_dlcontrid = " + int(DlContr.rec.dlcontrid));

          while(rsb.movenext)
              rsdCommand("DELETE dimage_dbt WHERE t_imageid = " + rsb.imageid).execute();
              rsdCommand("DELETE dimgdata_dbt WHERE t_imageid = " + rsb.imageid).execute();
              rsdCommand("DELETE ddlcontrmsg_dbt WHERE t_id = " + rsb.id).execute();
          end;
       end;
    else
       RslDefCon.BeginTrans();
    end;
 
    var stat = СоздатьСообщениеДБО(Kind, MarketID, DlContr, SfContr, MpID);
    
    if(stat == 1)
        RslDefCon.RollbackTrans();
    end;

    if(RslDefCon.IsInTrans())
       RslDefCon.CommitTrans();
    end;  

    return stat;  

onError(e)
    if(RslDefCon.IsInTrans())
       RslDefCon.RollbackTrans();
    end;
    return 1;
end;

//DEF-32182 Пересоздание сообщений регистрации
macro recreatDlcontrMessage(DlContr, Kind, MarketId)
   var stat = 0;
   var rsb = TRsbDataSet(" SELECT mp.t_id, mp.t_marketid "
                       + "   FROM ddlcontrmp_dbt mp, dsfcontr_dbt sf  "
                       + "  WHERE mp.t_sfcontrid = sf.t_id "
                       + "    /*AND sf.t_servkind = 1*/ AND sf.t_servkindsub = 8 "
                       + "    AND mp.t_dlcontrid = " + int(DlContr.rec.dlcontrid)
                       + "    AND mp.t_marketid = " + int(MarketId));
   while (rsb.movenext)
//DEF-36640 для ММВБ запись тоже необходима
//      if (Kind == OBJTYPE_DLCONTRMSG_BIDCODE) // для СПб необходима запись в таблице
         var cmd = RSDCommand("  insert into DDLCONTRGENMSG_TMP(t_id, t_kind, t_mpid, t_marketid) "
                              +" values(0,:kind,:mpid,:marketid) ");
         cmd.addparam("kind", RSDBP_IN, Kind);
         cmd.addparam("mpid", RSDBP_IN, rsb.id);
         cmd.addparam("marketid", RSDBP_IN, MarketId);
         cmd.execute;
//      else 
//      end;  
     stat = stat+1;              
   end;

   if (stat>0)
      stat = recreatDlcontrMessage_market(DlContr, Kind, rsb.id, MarketId); // sfcontrid нужен только для Спб

      if (DlContr.rec.iis == "X")
         stat = recreatDlcontrMessage_market(DlContr, OBJTYPE_DLCONTRMSG_IISOPEN, rsb.id, MarketId);
      end;

      if (     ( (Kind == OBJTYPE_DLCONTRMSG_MARKETREG) or (Kind == OBJTYPE_DLCONTRMSG_MPREG) ) 
           and (stat == 0) )
         DL_AddAllMsgToMQ();
      end;

      cmd = RSDCommand("DELETE from DDLCONTRGENMSG_TMP");
      cmd.execute;
   end;


end;
