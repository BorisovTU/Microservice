/************************************************************************/
/*  Имя файла      : SendMessQI.mac                                     */
/*                                                                      */
/*  Описание       : Отправка сообщений Квалифицированным инвесторам    */
/*                 : включенных в реестр.                               */
/*                                                                      */
/*  Программист    : Филиппов Б.В.                                      */
/*                                                                      */
/*  Создан         : 28.09.2018                                         */
/************************************************************************/


Import rslscr, or_rep_h;
Import rsd, RsbDataSet;
import email_library;
import RsbFormsInter, globals, Календарь, CTInter;
import "dlquery.mac";
import "padej.mac";
//import LoansGUI;

var rs, rs_cb, rs_send, select, select_cb, select_send;
var Rep;
var FlNm : string;
var TM   : string;
var DT   : string;
var Eml;
var cmd;
var FileDir;
var Shubl = 1;
var PATH_OUT;
var FileDirForm;

var SMTP_SERVER;    
var SMTP_SERVER_PORT = 25;
var SMTP_USERNAME;
var SMTP_PASSWORD;
var SMTP_USESSL = true;


/* протокол */
macro print_header
   [ Протокол формирования уведомлений о признании лица квалифицированным инвестором];
   [┌──────────────────────────────────────┬──────────────────────────────────────────────┬─────────────────────────────────────────┐
    │           Клиент                     │                   Файл                       │                                         │
    ├──────────────────────────────────────┼──────────────────────────────────────────────┼─────────────────────────────────────────┤];
end;

macro print_line(_client_name, _file_name, _result)
   [│######################################│##############################################│#########################################│](_client_name:w, _file_name:w, _result:w);
end;

macro print_bottom
   [└──────────────────────────────────────┴──────────────────────────────────────────────┴─────────────────────────────────────────┘];
end;
/************/

GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\QINV", V_STRING, PATH_OUT);
if(substr(PATH_OUT, strlen(PATH_OUT)) != "\\" )
    PATH_OUT = PATH_OUT + "\\";
end;

Var Path_Out_Str = SubStr (StrSubst (PATH_OUT, "\\\\", "\\"),3);
                           
FileDir =(SubStr (GetCurDir, 1, StrLen(GetCurDir())- 3) + Path_Out_Str);


//     FileDir =(SubStr (GetCurDir, 1, StrLen(GetCurDir())- 3) + "\WorkFile\\");
macro _isStandalone()
    return true;
end;


/***********************************/  
PRIVATE MACRO GetTxtPath()
    var RegistryPath = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR";
    var Path = "";
    var stat = 0;

    GetRegistryValue(RegistryPath, V_STRING, Path, stat);
    if(stat) 
        msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
    end;

    return Path;
END;

PRIVATE MACRO processPath( p_path )
    var l_str, l_p, l_path = "";

    p_path = trim( p_path );

    if( substr( p_path, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
        p_path = substr( p_path, 3 );

        l_str  = getCWD(false);    
        l_p    = strbrk( l_str, "\\" );
        while( l_p != 0 )
             l_path = l_path + substr( l_str, 1, l_p - 1 ) + "\\";
             l_str  = substr( l_str, l_p + 1 );
             l_p    = strbrk( l_str, "\\" );
        end;
        return ( substr( l_path, 1, strlen( l_path ) - 1 ) + p_path );

    elif( substr( p_path, 1, 1 ) == "." ) /* ссылка на текущий каталог */
        return ( getCWD + substr( p_path, 2 ) );
    end;

    return p_path;
END;

MACRO GetRepFullPath(isRemote:bool)
    var Path = "";

    if(isRemote)
        var txtPath = GetTxtPath();

        if( substr( txtPath, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
            txtPath = substr(txtPath, 3 );
        end;

        if( substr( txtPath, 1, 1 ) == "\\" ) /* ссылка на вышестоящий каталог */
            txtPath = substr(txtPath, 2 );
        end;

        Path = GetCurDir(true) + "\\" + txtPath + "\\"; 
    else
        Path = processPath(GetTxtPath()) + "\\";
    end;

    // получить полный путь
    return Path;
END;
/***********************************/  

//Путь к txtfile
FileDirForm = GetRepFullPath(false/*not isStandAlone()*/);

macro CreateQiMessage(dlcontrid, qiRegDate)
    var DT = string(Date:f);
    var TM = string(Time:f);
    var FlNm;
    var clientAdress = "";
    var errmsg = "";
    var err = false;
    var Rep = CMakeReport;

    var select = " SELECT party.t_partyid, party.t_shortname, party.t_name, t_dlcontrid,   " +
                 "        lpad(t_dlcontrid, 34, '0') t_dicontr                             " +
                 "   FROM ddlcontr_dbt dlcontr                                             " +
                 "        JOIN dsfcontr_dbt sfcontr ON dlcontr.t_sfcontrid = sfcontr.t_id  " +
                 "        JOIN dparty_dbt party     ON sfcontr.t_partyid = party.t_partyid " +
                 "  WHERE dlcontr.t_dlcontrid = :dlcontrid                                 ";
    var cmd_main = DL_RSDCommand(select);
        cmd_main.addParam(dlcontrid); 
    var rs = cmd_main.execute();
    if(not rs.movenext)
        return;
    end;

    clientAdress = "";
    var cmd_adress = DL_RSDCommand("select t_postindex||', '||t_adress as t_adress from dadress_dbt where t_partyid = :partyid order by t_type"); 
        cmd_adress.addParam(rs.partyid); 
    var rs_adress = cmd_adress.execute();
    if(rs_adress.moveNext)
        clientAdress = rs_adress.adress;
    end;

    Shubl = Shubl + 1;

    var ClNameForFile = rs.shortname;
    ClNameForFile = StrSubst(ClNameForFile,".","");
    ClNameForFile = StrSubst(ClNameForFile," ","");
    FlNm = ClNameForFile + "_" + StrSubst(DT, ".", "") + "_" + StrSubst(TM, ":", "");

    Rep.RegisterForm
    ("letter" + Shubl, "ФОД 1_5.doc", string(FlNm, ".doc"), "Клиент", "Адрес_клиента", "ПКлиент", "ПКлиентПредлож", "Дата");
  
    Rep.AddDoc("letter" + Shubl,"Клиент", "Адрес_клиента", "ПКлиент",  "ПКлиентПредлож", "Дата");
    Rep.AddDocFields("Клиент"        , rs.shortname);
    Rep.AddDocFields("Адрес_клиента" , clientAdress);
    Rep.AddDocFields("ПКлиент"       , Падеж(rs.name,2));
    Rep.AddDocFields("ПКлиентПредлож", Падеж(rs.name,6));

    Rep.AddDocFields("Дата"       , StrSubst(string(qiRegDate:f), ".", "/"));

    Rep.SetFlagShowReport(false);
    Rep.CreateTemplateData();
    Rep.ShowTemplateRep();
    Rep.ClearRegisterForm("letter" + Shubl);

    MakeDir(PATH_OUT);
//    if(not CopyFile(string("$"+FileDirForm,FlNm,".doc"), string(PATH_OUT,FlNm,".doc")))
    if(not CopyFile(string(FileDirForm,FlNm,".doc"), string(PATH_OUT,FlNm,".doc")))
        err = true;
        errmsg = "Не удалось скопировать файл в " + PATH_OUT;
        errmsg = errmsg + "\n" + "$"+string(FileDirForm,FlNm,".doc") +"\n" + string(PATH_OUT,FlNm,".doc");
    end;

    var dlcontrmsg = Tbfile("dlcontrmsg", "W", 0);

    // Создать запись о сообщении ДБО
    dlcontrmsg.clear();
    dlcontrmsg.rec.DlContrID = rs.dlcontrid;
    dlcontrmsg.rec.Kind = 501; // Уведомление о признании лица квалифицированным инвестором
    dlcontrmsg.rec.CreateDate = qiRegDate;//date();
    dlcontrmsg.rec.CreateTime = Time();
    
    if(not err)
        if(not dlcontrmsg.insert())
            err = true;
            errmsg = "Ошибка добавления сообщения к договору";
        end;
    end;

    if(not err)
//        if(not LoadImageObj("$"+FileDirForm + FlNm +".doc", OBJTYPE_BSCMSG_DL, UniID(dlcontrmsg, OBJTYPE_BSCMSG_DL), 1))
        if(not LoadImageObj(FileDirForm + FlNm +".doc", OBJTYPE_BSCMSG_DL, UniID(dlcontrmsg, OBJTYPE_BSCMSG_DL), 1))
            err = true;
            errmsg = "К записи о сообщении не удалось прикрепить файл сообщения";
        end;
    end;

    if(not err)
        setparm(2,FlNm +".doc");
    end;
    
    setparm(3,errmsg);

end;

Macro Head(_DateBegin, _DateEnd)
    var clientAdress = "";
    var errmsg = "";
    var err = false;
           
    select = " SELECT party.t_partyid, party.t_shortname, party.t_name,                                  " +
             "        scqinvhist.t_begdate, t_email, t_dlcontrid,                                        " +
             "        lpad(t_dlcontrid, 34, '0') t_dicontr                                               " +
             "   FROM dv_scqinvhist scqinvhist                                                           " +
             "        JOIN dsfcontr_dbt sfcontr ON scqinvhist.t_partyid = sfcontr.t_partyid              " +
             "        JOIN ddlcontr_dbt dlcontr ON dlcontr.t_sfcontrid = sfcontr.t_id                    " +
             "        JOIN dparty_dbt party     ON sfcontr.t_partyid = party.t_partyid /*AND ROWNUM = 1*/" +
             "  WHERE scqinvhist.t_state = 1                                                             " +
             "        AND scqinvhist.t_begdate BETWEEN :beg_date AND :end_date                           " +
             "        AND (scqinvhist.t_EndDate = TO_DATE ('01.01.0001', 'DD.MM.YYYY')                   " +
             "             OR scqinvhist.t_EndDate > :end_date2)                                         " +
             "        AND NOT EXISTS                                                                     " + 
             "                   (SELECT 1                                                               " +
             "                      FROM ddlcontrmsg_dbt msg                                             " +
             "                     WHERE msg.t_DlContrID = dlcontr.t_dlcontrid                           " +
             "                           AND msg.t_kind = 501)                                           ";
    var cmd_main = DL_RSDCommand(select);
        cmd_main.addParam(_DateBegin); 
        cmd_main.addParam(_DateEnd); 
        cmd_main.addParam(_DateEnd);

    var cnt = cmd_main.GetCount();

    rs = cmd_main.execute();

    print_header;

    var i=0;
    InitProgress(cnt, "Формирование уведомлений", "Формирование уведомлений");

    ReplaceMacro ( "isStandalone", "_isStandalone" );

    while(rs.moveNext())
  /*
    select_cb = "select davrkinds_dbt.t_definition "          
                "from dscqinvfi_dbt "       
                "join dparty_dbt on dscqinvfi_dbt.t_partyid = dparty_dbt.t_partyid "
                "join davrkinds_dbt on davrkinds_dbt.t_avoirkind = dscqinvfi_dbt.t_avoirkind "
                "where dparty_dbt.t_partyid = '" + rs.partyid + "' and davrkinds_dbt.t_fi_kind = 2";

    rs_cb = RsdRecordSet(RsdCommand(select_cb));

    var cb = "";
    var cb1 = "";
    var cb2 = "";
    var i = 0;

      while (rs_cb.moveNext())
          if (i==0)
             cb =  rs_cb.value("t_definition");
          elif (i==1)
             cb1 = ", " + rs_cb.value("t_definition");
          elif (i==2)
             cb2 = ", " + rs_cb.value("t_definition");
          end;
          i = i + 1
      end;
*/
/*
        select_send = "select t_comment "          
                      "from dimgdata_dbt "       
                      "where t_objectid = LPAD('" + rs.dlcontrid + "', 34, '0') "
                      "and t_objecttype = 207";

        rs_send = RsdRecordSet(RsdCommand(select_send));
        rs_send.moveNext();
*/    

    /* Email Плучателя*/
        //Eml =  rs.email;
  
   /* Email отправителя из реестра банка */
/* 
   GetRegistryValue("COMMON\\EMAIL\\MAIL_SMTPSERVER", V_STRING, SMTP_SERVER);
   GetRegistryValue("COMMON\\EMAIL\\MAIL_USERNAME", V_STRING, SMTP_USERNAME);
   GetRegistryValue("COMMON\\EMAIL\\MAIL_PASSWORD", V_STRING, SMTP_PASSWORD);

  var file_path, config = SmtpSettings();

  config.SMTP_SERVER      = SMTP_SERVER;
  config.SMTP_SERVER_PORT = SMTP_SERVER_PORT;
  config.SMTP_USERNAME    = SMTP_USERNAME;
  config.SMTP_PASSWORD    = SMTP_PASSWORD;
  config.SMTP_USESSL      = SMTP_USESSL;

  var mail = MailSender(config);

  if ((SMTP_SERVER != "") or 
//      (SMTP_USERNAME != "") or 
//      (SMTP_PASSWORD != "") or 
      (Eml != StrFor(1)))
       mail.send("АО Россельхозбанк", Eml, "Присвоен статус квалифицированного инвестора", "Присвоен статус квалифицированного инвестора", FileDir +  FlNm + ".doc");
  end;
*/
        var outFileName = "";
        CreateQiMessage(rs.dlcontrid, date(rs.begdate), outFileName, errmsg);
        
        print_line(rs.shortname, outFileName, errmsg);

        i=i+1;
        UseProgress(i);
     end;

     RemProgress;

     print_bottom;

     ReplaceMacro ( "isStandalone", null );
End;

private const FT_STRING = 7;
private const FT_DATE = 9;
class (TRsbPanel) DatePeriodPanel(_dateBegin,_dateEnd)
    InitTRsbPanel();
    setSize(35,6);
    setPosition(35,15);

    var ResultDateBegin = _dateBegin;
    var ResultDateEnd = _dateEnd;

    class (TRsbEditField) EditField(typeFld: integer, x: integer, y: integer, width: integer, height: integer, bindVal, active: bool, fieldName: string, panel)
        var bindString = string(bindVal);
        
        initTRsbEditField(typeFld);
        setPosition(x, y);
        setSize(width, height);
        
        if(active == false) 
            editable = focusable = false; 
        end;

        if(typeFld == FT_STRING)
            bindValue( this, "bindString", 100 );
        elif(bindVal != null)
            value = bindVal; 
        end;

        if(panel != null)
            panel.addControl(this); 
        end;

        if(fieldName != null) 
            name = fieldName; 
        end;
    end;

    addLabel(TRsbLabel(3, 2, "Начало периода:"));
    var m_DateBegin = EditField( FT_DATE, 20, 2, 10, 1, _dateBegin, true, "Date", this );
    
    addLabel(TRsbLabel(3, 4, "Конец  периода:"));
    var m_DateEnd   = EditField( FT_DATE, 20, 4, 10, 1, _dateEnd, true, "Date", this );

    m_DateBegin.addEventHandler(RSB_EV_KEY_PRESSED,  R2M(this, "DATEBEGIN_KEY_PRESSED"));
    m_DateEnd.addEventHandler(RSB_EV_KEY_PRESSED,  R2M(this, "DATEEND_KEY_PRESSED"));
    addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "Panel_ButtonEvent"));

    macro DATEBEGIN_KEY_PRESSED(RsbEvent)
       if(RsbEvent.keyCode == 317)
           m_DateBegin.value = GetDateByCalendar(m_DateBegin.value);
       elif((RsbEvent.keyCode == 408) OR (RsbEvent.keyCode == 411))/*KEY_ALT_UP KEY_ALT_LEFT*/
           m_DateBegin.value = m_DateBegin.value - 1;
       elif((RsbEvent.keyCode == 416) OR (RsbEvent.keyCode == 413))/*KEY_ALT_DOWN KEY_ALT_RIGHT*/
           m_DateBegin.value = m_DateBegin.value + 1;
       end;
    end;

    macro DATEEND_KEY_PRESSED(RsbEvent)
       if(RsbEvent.keyCode == 317)
           m_DateEnd.value = GetDateByCalendar(m_DateEnd.value);
       elif((RsbEvent.keyCode == 408) OR (RsbEvent.keyCode == 411))/*KEY_ALT_UP KEY_ALT_LEFT*/
           m_DateEnd.value = m_DateEnd.value - 1;
       elif((RsbEvent.keyCode == 416) OR (RsbEvent.keyCode == 413))/*KEY_ALT_DOWN KEY_ALT_RIGHT*/
           m_DateEnd.value = m_DateEnd.value + 1;
       end;
    end; 

    macro Panel_ButtonEvent(RsbEvent)
       if((RsbEvent.keyCode == 316) or (RsbEvent.keyCode == 13))
          ResultDateBegin = m_DateBegin.value;
          ResultDateEnd = m_DateEnd.value;
          close(1);       
       elif(RsbEvent.keyCode == 27)
          close(0);
       end;
    end;
end;

macro getPeriod(_dateBegin,_dateEnd,_caption)
    if(not _dateBegin)
        _dateBegin = {curdate};
    end;
    if(not _dateEnd)
        _dateEnd = {curdate};
    end;

    var panel = DatePeriodPanel(_dateBegin,_dateEnd);
        panel.setCaption(_caption);

    if(panel.run == 1)
        setparm(0,panel.ResultDateBegin);
        setparm(1,panel.ResultDateEnd);
        return true;
    end;
    return false;
end;

var DateBegin, DateEnd;
if(getPeriod(DateBegin, DateEnd,"Выберите период"))
//if(ВвестиПериодДат(DateBegin, DateEnd,"Выберите период")) //период дат курильщика
    Head(DateBegin, DateEnd);
end; 
     
exit(0);

  







  
