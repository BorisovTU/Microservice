/*
$Name:        RepPersonalIncomeTax.mac
$Module:      Ценные бумаги
$Description: Общие классы и функции 2-НДФЛ
*/

import RepPersonalIncomeTax;
import captureoutput;

CLASS (NPTXRepCalcCommon) NPTXRepCalcCommonU( _RepDate, _Investor, _Sfcontr, _Signer, _MessKind, _MessNum, _ErrLog )

  m_repdate = _RepDate;
  private var m_ClientID = _Investor;
  private var m_sfcontrid = _Sfcontr;
  private var m_SignerID = _Signer;
  private var m_MessKind = _MessKind;
  private var m_MessNum = _MessNum;
  private var ErrLog = _ErrLog;
  private var m_GenDate = null;
  private var m_BankID = null;
  private var m_FNSCodeA = null;
  private var m_FNSCodeK = null;
  private var m_BankINN = null;
  private var m_BankKPP = null;
  private var m_SignN = null;
  private var m_SignLastName = null;
  private var m_SignFirstName = null;
  private var m_SignMiddleName = null;
  private var m_SignNumber = null;
  private var m_SignDate = null;

  File adress (adress);
  ClearRecord (adress);

  /*GenDate*/
  macro GenDate()
    if(m_GenDate == null)
      var cmd, rs;
      StartCaptureOutput();
      [ 
        select to_char(trunc(sysdate),'yyyymmdd') t_gendate from dual
      ];
      cmd = RsdCommand(EndCaptureOutput());
      rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
      rs.movenext();
      m_GenDate = rs.value("t_gendate");
    end;
    return m_GenDate;
  end;

  /*Код клиента*/
  macro ClientID()
    return m_ClientID;
  end;

  /*Код договора*/
  macro SfContrID()
    return m_SfContrID;
  end;

  /*Номер сообщения*/
  macro MessNum()
    return m_MessNum;
  end;

  /*Код банка*/
  macro BankID()
    if(m_BankID == null)
      var cmd, rs;
      StartCaptureOutput();
      [ 
        select dp_dep.t_partyid
        from dsfcontr_dbt sfcontr
        left join ddp_dep_dbt dp_dep on dp_dep.t_name = rpad(lpad(regexp_substr(sfcontr.t_number, '^\d{1,2}'),2,'0'),4,'0')
        where sfcontr.t_id = ? and
        regexp_like(sfcontr.t_number,'^\d{1,2}[/]')
      ];
      cmd = RsdCommand(EndCaptureOutput());
      cmd.AddParam("sfcontrid", RSDBP_IN, m_sfcontrid);
      rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
      if(rs.movenext)
        m_BankID = int(rs.value("t_partyid"));
      else
        m_BankID = -1;
      end;
    end;
    return m_BankID;
  end;

  /*Код налогового органа, которому направляется файл обмена*/
  macro FNSCodeA()
    if(m_FNSCodeA == null)
      m_FNSCodeA = "9979";
    end;
    return m_FNSCodeA;
  end;

  /*Код налогового органа, для которого предназначен файл*/
  macro FNSCodeK()
    if(m_FNSCodeK == null)
      m_FNSCodeK = "7704";
    end;
    return m_FNSCodeK;
  end;
  
  /*ИНН банка*/
  macro OurINN()
    if(m_BankINN == null)
      var cmd, rs;
      StartCaptureOutput();
      [ 
        select objcode.t_code
        from dobjcode_dbt objcode 
        where objcode.t_ObjectType = ########## and
              objcode.t_CodeKind = ########## and
              objcode.t_ObjectID = ? and
              objcode.t_State = 0;
      ](OBJTYPE_PARTY, PTCK_INN);
      cmd = RsdCommand(EndCaptureOutput());
      cmd.AddParam("partyid", RSDBP_IN, {OurBank});
      rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
      m_BankINN = "";
      m_BankKPP = "";
      if(rs.movenext)
        SplitFullINN(rs.value("t_code"), m_BankINN, m_BankKPP);
      end;
    end;
    return m_BankINN;
  end;

  /*КПП банка*/
  macro OurKPP()
    if(m_BankKPP == null)
      OurINN();
    end;
    return m_BankKPP;
  end;

  /*Наименование банка*/
  macro OurNameU()
    if({OurBank} == 1)
      return "Акционерное общество \"Российский Сельскохозяйственный банк\"";
    else
      return OurName();
    end;
  end;

  /*Телефон банка*/
  macro OurPhone()
    return "(495)363-02-94";
  end;

  /*Данные по подписанту*/
  macro SignN()
    if(m_SignN == null)
      var cmd, rs;
      StartCaptureOutput();
      [ 
        select t_name1,
               t_name2,
               t_name3,
               case when nvl(officer.t_isfirstperson,chr(0)) = chr(88) then 1 else 2 end t_n,
               nvl(legalproxy.t_number,chr(1)) t_number,
               nvl(legalproxy.t_begindate,to_date('01.01.0001','dd.mm.yyyy')) t_date
        from dpersn_dbt persn
        left join dofficer_dbt officer on officer.t_personid = persn.t_personid
        left join dlegalproxy_dbt legalproxy on legalproxy.t_partyid = ? and
        legalproxy.t_proxyid = persn.t_personid and 
                               legalproxy.t_begindate <= ? and 
                               legalproxy.t_enddate >= ?
        where persn.t_personid = ? and
              rownum = 1
      ];
      cmd = RsdCommand(EndCaptureOutput());
      cmd.AddParam("partyid", RSDBP_IN, {OurBank});
      //cmd.AddParam("dt1", RSDBP_IN, m_repdate);
      //cmd.AddParam("dt2", RSDBP_IN, m_repdate);
      cmd.AddParam("dt1", RSDBP_IN, date());//Chesnokov D.S. I-Support 503927, доверенность ищем на календарную дату.
      cmd.AddParam("dt2", RSDBP_IN, date());
      cmd.AddParam("signerid", RSDBP_IN, m_SignerID);
      rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
      if(rs.movenext)
        m_SignN = rs.value("t_n");
        m_SignLastName = rs.value("t_name1");
        m_SignFirstName = rs.value("t_name2");
        m_SignMiddleName = rs.value("t_name3");
        m_SignNumber = rs.value("t_number");
        m_SignDate = date(rs.value("t_date"));
      end;
    end;
    return m_SignN;
  end;
  macro SignLastName()
    if(m_SignLastName == null)
      SignN();
    end;
    return m_SignLastName;
  end;
  macro SignFirstName()
    if(m_SignFirstName == null)
      SignN();
    end;
    return m_SignFirstName;
  end;
  macro SignMiddleName()
    if(m_SignMiddleName == null)
      SignN();
    end;
    return m_SignMiddleName;
  end;
  macro SignNumber()
    if(m_SignNumber == null)
      SignN();
    end;
    return m_SignNumber;
  end;
  macro SignDate()
    if(m_SignDate == null)
      SignN();
    end;
    return m_SignDate;
  end;

  /*Адрес*/
  MACRO MailIndex():STRING
    if(adress.PostIndex == "")
      return "";
    else
      return adress.PostIndex;
    end;
  END;

  MACRO RegionCode()
    if(adress.RegionNum == "")
      return "-";
    else
      return adress.RegionNum;
    end;
  END;

  /*Район*/
  MACRO District():STRING
    if(adress.Province == "")
      return "-";
    else
      return adress.Province;
    end;
  END;

  /*Город*/
  MACRO City():STRING
    if(adress.District == "")
      return "-";
    else
      return adress.District;
    end;
  END;

  /*Населенный пункт*/
  MACRO Town():STRING
    if(adress.Place == "")
      return "-";
    else
      return adress.Place;
    end;
  END;

  MACRO Street():STRING
    if(adress.Street == "")
      return "-";
    else
      return adress.Street;
    end;
  END;

  MACRO House():STRING
    if(adress.House == "")
      return "-";
    else
      return adress.House;
    end;
  END;
            
  MACRO Building():STRING
    if(adress.NumCorps == "")
      return "-";
    else
      return adress.NumCorps;
    end;
  END;
         
  MACRO Appartment():STRING
    if(adress.Flat == "")
      return "-";
    else
      return adress.Flat;
    end;
  END;
       

  InitNPTXRepCalcCommon(_RepDate, _Investor, null);

END;
