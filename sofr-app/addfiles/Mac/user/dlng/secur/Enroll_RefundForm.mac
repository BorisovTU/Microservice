/** 
 @file 		mac\user\dlng\secur\Enroll_RefundForm.mac
 @brief 	Форма параметров возвратного платежа по зачислению
  
 # tag 
 - functional_block:Клиенты_Зачисление
 - code_type:API
 - Зачисление
 - Возврат зачисления
   
 # changeLog 
 |date       |author         |tasks            |note                            
 |-----------|---------------|-----------------|--------------------------------
 |24.11.2025 |Велигжанин А.В.|DEF-108968       | Добавление поля БИК
 |25.07.2025 |Топорков Д.В.  |BOSS-8439        | Создание
*/ 

import "DlRepForm_h.mac";

// Номера полей в форме отчета
private const PNFLD_REFUND_DEBIT       = 0, // Счет по дебету
              PNFLD_REFUND_CREDIT      = 1, // Счет по кредиту
              PNFLD_REFUND_REASON      = 2, // Причина возврата
              PNFLD_REFUND_REASONDOC   = 3, // Информация о входящем плат поручении
              PNFLD_REFUND_RECEIVER    = 4, // Получатель
              PNFLD_REFUND_BIC         = 5, // БИК (DEF-108968)
              PNFLD_REFUND_RECEIVERACC = 6; // Счет получателя

private const H_PNFLD_ACCOUNT  = 100,
              H_PNFLD_TEXT     = 101,
              H_PNFLD_BIC      = 102
;

private const K_ENTER = 13,
              K_F2    = 316,
              K_F9    = 323;

/**
@brief Класс параметров возврата
*/
class CEnrollRefundParams()
  var debitAccount = "";
  var creditAccount = "";
  
  var currency = -1;
  var reason = "ОТСУТСТВУЕТ СОГЛАШЕНИЕ";
  var reasonDoc = "ПО ПЛАТ.ПОР. ОТ";
  var receiverName = "";
  var BIC = "";					// DEF-108968
  var receiverAccount = "";
  
  macro GetGround()
    return "ВОЗВРАТ СРЕДСТВ ПО ПРИЧИНЕ-" + StrUpr(reason) + "." + reasonDoc + 
           "(ПОЛУЧАТЕЛЬ " + StrUpr(receiverName) + ", " + receiverAccount + ")";
  end;
end;

/**
@brief Обработчик счета
*/
private macro FldProc_Account( pThis:DL_CPanel, Cmd: Integer, Key: Integer, FldShowValue: @Variant, FldRealValue: @Variant): Integer
  if (Cmd == DLG_INIT)
    if( FldRealValue == null ) // инициализация по умолчанию
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif (Cmd == DLG_CHANGEVALUE) // значение поля было изменено
    FldRealValue = FldShowValue;
  elif ((Cmd == DLG_KEY) AND (Key == KEY_F3)) // Вызов листалки и заполнение FldShowValue и FldRealValue
    var account = TRecHandler("account");
    if (ListAccount(account, 1, pThis.refundParams.currency, FldRealValue))
      FldRealValue = account.rec.account;
      FldShowValue = FldRealValue;
    end;
  end;
  return CM_DEFAULT;
end;


/**
@brief DEF-108968 Получение БИК
*/
private macro GetPartCode( p_PartyID: Integer, p_CodeKind: Integer): string
  var partcode = TBFile( "partcode.dbt",  "R", 0 ), x_PartCode : string = "";
  partcode.Clear();
  partcode.rec.PartyID  = p_PartyID;
  partcode.rec.CodeKind = p_CodeKind;
  if (partcode.GetEQ())
    x_PartCode = partcode.rec.Code;
  end;
  return x_PartCode;
end; // GetPartCode()


/**
@brief DEF-108968 Обработчик БИК
*/
private macro FldProc_Bic( pThis:DL_CPanel, Cmd: Integer, Key: Integer, FldShowValue: @Variant, FldRealValue: @Variant): Integer
  if (Cmd == DLG_INIT)
    if( FldRealValue == null ) // инициализация по умолчанию
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif (Cmd == DLG_CHANGEVALUE) // значение поля было изменено
    FldRealValue = FldShowValue;
  elif ((Cmd == DLG_KEY) AND (Key == KEY_F3)) // Вызов листалки и заполнение FldShowValue и FldRealValue
    var party = TRecHandler("party.dbt");
    if( ListPT( party, PTCK_BIC, FldShowValue, PTLIST_ALLBANK, PTCK_BIC) == true ) 
      FldRealValue = GetPartCode(party.rec.PartyID, PTCK_BIC);
      FldShowValue = FldRealValue;
    end;
  end;
  return CM_DEFAULT;
end;

private macro FldProc_Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) // инициализация по умолчанию
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) // значение поля было изменено
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
end;

/**
@brief Класс работы с панелью параметров выпуска отчета
*/
class (DL_CPanel) CEnroll_RefundForm()
  var refundParams = CEnrollRefundParams();

  private macro Init()
    AddFieldProc(0, PNFLD_REFUND_DEBIT, H_PNFLD_ACCOUNT, @FldProc_Account, refundParams.debitAccount);
    AddFieldProc(0, PNFLD_REFUND_CREDIT, H_PNFLD_ACCOUNT, @FldProc_Account, refundParams.creditAccount);
    AddFieldProc(0, PNFLD_REFUND_REASON, H_PNFLD_TEXT, @FldProc_Default, refundParams.reason);
    AddFieldProc(0, PNFLD_REFUND_REASONDOC, H_PNFLD_TEXT, @FldProc_Default, refundParams.reasonDoc);
    AddFieldProc(0, PNFLD_REFUND_RECEIVER, H_PNFLD_TEXT, @FldProc_Default, refundParams.receiverName);
    AddFieldProc(0, PNFLD_REFUND_BIC, H_PNFLD_BIC, @FldProc_Bic, refundParams.BIC); // DEF-108968
    AddFieldProc(0, PNFLD_REFUND_RECEIVERACC, H_PNFLD_TEXT, @FldProc_Default, refundParams.receiverAccount);
  end;

  private macro IsAccountAvailable(value)
    var cmd = RsdCommand("SELECT 1 FROM dAccount_dbt WHERE t_close_date = to_date('01.01.0001', 'DD.MM.YYYY') and t_account = :acc ");
    cmd.AddParam("acc", RSDBP_IN, value);
    return RsdRecordSet(cmd).MoveNext();
  end;
  
  private macro CheckInput(error: @String)
    error = "";

    if (not IsAccountAvailable(GetFieldValue(PNFLD_REFUND_DEBIT)))
      error = "Счет Дебет '" + GetFieldValue(PNFLD_REFUND_DEBIT) + "' закрыт или не существует";
    elif (not IsAccountAvailable(GetFieldValue(PNFLD_REFUND_CREDIT)))
      error = "Счет Кредит '" + GetFieldValue(PNFLD_REFUND_CREDIT) + "' закрыт или не существует";
    elif (StrLen(Trim(GetFieldValue(PNFLD_REFUND_REASON))) == 0)
      error = "Не указана причина возврата";
    elif (StrLen(Trim(GetFieldValue(PNFLD_REFUND_REASONDOC))) == 0)
      error = "Не указано входящее платежное поручение возврата";
    elif (StrLen(Trim(GetFieldValue(PNFLD_REFUND_RECEIVER))) == 0)
      error = "Не указан получатель";
    else
      var receiverAccount = Trim(GetFieldValue(PNFLD_REFUND_RECEIVERACC));
      if ((StrLen(receiverAccount) != 20) or (not StrIsNumber(receiverAccount)))
        error = "Некорректный счет получателя";
      end;
    end;

    if (StrLen(error) > 0)
      return false;
    end;
    
    return true;
  end;

  macro KeyProc(Pan:Variant, Cmd:Integer, FldId:Integer, Key:Integer)
    var error = "";
    var result = this.KeyProc(Pan, Cmd, FldId, Key);
 
    if (Cmd == DLG_KEY)
      if ((Key == K_ENTER) and (FldId == PNFLD_REFUND_RECEIVERACC))
        result = CM_IGNORE;
      elif (Key == K_F9)
        result = CM_IGNORE;
      elif ((Key == K_F2) and (not CheckInput(@error)))
        MsgBox(error);
        result = CM_IGNORE;
      end;
    end;
    
    return result;
  end;

  macro Run()
    var result = Run();

    if (result)
      refundParams.debitAccount    = GetFieldValue(PNFLD_REFUND_DEBIT);
      refundParams.creditAccount   = GetFieldValue(PNFLD_REFUND_CREDIT);
      refundParams.reason          = Trim(GetFieldValue(PNFLD_REFUND_REASON));
      refundParams.reasonDoc       = Trim(GetFieldValue(PNFLD_REFUND_REASONDOC));
      refundParams.receiverName    = Trim(GetFieldValue(PNFLD_REFUND_RECEIVER));
      refundParams.BIC             = Trim(GetFieldValue(PNFLD_REFUND_BIC));   		// DEF-108968
      refundParams.receiverAccount = Trim(GetFieldValue(PNFLD_REFUND_RECEIVERACC));
    end;
    
    return result;
  end;

  initDL_CPanel( "sp_res.lbr", "ENRREFND" );
end;
