/** 
 @file loadCodeNKC.mac
 @brief Первичная настройка РК на ДБО всех клиентов
  
 # tag 
 - functional_block:Клиенты_Зачисление
 - code_type:API

   
 # changeLog 
 |date       |author         |tasks            |note                            
 |-----------|---------------|-----------------|--------------------------------
 |03.05.2024 |Гераськина Т.В.|BOSS-1702        | Создание

*/ 

import rsd, "activex.mac";
import DealsInter;
import "dlcontrfunc.mac";
import Refill_Class;
import "StorageInfo.mac", "xls_parser.mac";
import "dlutils.mac";

private const COL_NAME     = 1,
              COL_ACCOUNT  = 2,
              COL_EKK      = 3,
              COL_TKS      = 4,
              COL_CODE     = 5,
              COL_CUR      = 6;
              
private const C_CATEGORY_GRANT_RIGHT = 6,
              C_CATEGORY_GRANT_RIGHT_YES = 1,
              C_CATEGORY_GRANT_RIGHT_NO = 2;

private var marketid_moex = ПолучитьБиржуПоВидуКодаДБО(DLCCK_USC);


/**
@brief Получить значение настройки для временного файла
*/
private macro GetStorageInfo(): CStorageInfo
  var storageInfo = CStorageInfo("CodeNKC", "xls");
  
  storageInfo.GetFile(TXTTAG, TXTTAG).isTemporary = true; // временный xls файл

  return storageInfo;
end;

/**
@brief Получение значения из ячейки Excel
@param[in] str Содержимое ячейки

@return Значение ячейки или обрезанный текст
*/
private Macro TrimStr(str)
   If (valtype(str) != V_STRING)
      return String(str);
   else
      return trim(str);
   end;
End;


/**
@brief Класс ошибки
*/
private class c_Error()
   var errcode:integer = 0;
   var errtext:string = "OK";
end;


/**
@brief Класс строки Excel
*/
private class ExcelStr()
   var str_Name;
   var str_Account;
   var str_EKK;
   var str_TKS;
   var str_CODE;
   var str_cur;
end;


/**
@brief Установить код для договора значением из ячейки Excel
@param[in] pcode Код РЦ
@param[in] pekk Код ЕКК

@return Экземпляр класса ошибки
*/
private macro SetCodeForMP(pcode, pekk)
   var sql, cmd, rs;
   var res = c_Error();
   private const SERV_SUBKIND_STOCK = 8;
   var vid = -1;

   sql = String(  "SELECT mp.t_id from ddlcontrmp_dbt mp, dsfcontr_dbt sf                 \n",
                  " WHERE mp.t_sfcontrid = sf.t_id                                        \n",
                  "   AND sf.t_servkind = :pservkind AND sf.t_servkindsub = :pservkindsub \n",
                  "   AND mp.t_marketid = :pmarketid AND mp.t_mpcode = :pekk                ");
   cmd = RSDCommand(sql);
   cmd.AddParam("pservkind", RSDBP_IN, PTSK_STOCKDL);
   cmd.AddParam("pservkindsub", RSDBP_IN, SERV_SUBKIND_STOCK);
   cmd.AddParam("pmarketid", RSDBP_IN, marketid_moex);
   cmd.AddParam("pekk", RSDBP_IN, pekk);
   rs = RSDRecordSet(cmd);
   if (rs.movenext())
      vid = rs.value("t_id");
   end;

   if (vid > 0)
      sql = "UPDATE ddlcontrmp_dbt SET t_rcodetks = :pcode WHERE t_id = :pid";
      cmd = RSDCommand(sql);
      cmd.AddParam("pcode", RSDBP_IN, pcode);
      cmd.AddParam("pid", RSDBP_IN, vid);
      cmd.Execute();
      cmd.close();
   else 
      res.errcode = -1;
      res.errtext = "Не найдена площадка с кодом "+pekk;
   end;

   return res;
onError(err)
   res.errcode = err.code;
   res.errtext = err.message + refill_getFullCmdErrorText(cmd);
end;


/**
@brief Загрузка данных из Excel версия для astra
@param[in] FileName Имя файла
*/
macro OpLoadFileWine(FileName)
   var poi: CExcelPoi = CExcelPoi();
   var row = 1;
   var StrFromExcel, sheet, sheetIndex;
   var Error;

   /*Открываем файл*/
   BegAction(1, "Обработка файла. Ждите...");
   if (not poi.Open(FileName))
      MsgBox("POI ошибка открытия файла " + FileName);
      EndAction(1);
      return 0;
   end;

   PrintLn("Загрузка индивидуальных расчетных кодов клиентов");

   sheetIndex = 0;
   while (poi.SheetChange(sheetIndex))
      var lastRowIndex = poi.GetLastRowNum();
      row = 1;

      //Бежим по файлу
      InitProgress(-1);
      while (row <= lastRowIndex) 
         Error = null;
         if ((ValType(poi.CellValueByRowAndCol(row, COL_NAME)) == V_UNDEF) or (StrUpr(String(poi.CellValueByRowAndCol(row, COL_NAME))) == "КЛИЕНТ"))
            row = row + 1;
            continue; 
         end;

         StrFromExcel = ExcelStr(); 
         StrFromExcel.str_Name      = Trim(String(poi.CellValueByRowAndCol(row, COL_NAME)));
         StrFromExcel.str_Account   = Trim(String(poi.CellValueByRowAndCol(row, COL_ACCOUNT)));
         StrFromExcel.str_EKK       = String(Int(poi.CellValueByRowAndCol(row, COL_EKK)));
         StrFromExcel.str_TKS       = String(poi.CellValueByRowAndCol(row, COL_TKS));
         StrFromExcel.str_CODE      = String(Int(poi.CellValueByRowAndCol(row, COL_CODE)));
         StrFromExcel.str_cur       = String(poi.CellValueByRowAndCol(row, COL_CUR));

         Error = SetCodeForMP(StrFromExcel.str_CODE, StrFromExcel.str_EKK);
         PrintLn(StrFromExcel.str_EKK + "  " + Error.errtext);

         row = row + 1;

      end; //while
      
      RemProgress();
      sheetIndex = sheetIndex + 1;
   end;//eow sheet
   
   poi.Close();

onerror(er)
   if (valtype(poi) != V_UNDEF)
      poi.Close();
   end;
   EndAction(1);
   PrintLn("Внимание! В процессе загрузки файла " + FileName + " произошли следующие ошибки :" + er.message);
end;

/**
@brief Загрузка данных из Excel версия для win
@param[in] FileName Имя файла
@param[in] TermPath Папка файла
*/
macro OpLoadFileWin(FileName, TermPath)
   var row = 1;
   var StrFromExcel, sheet, sheetcount;
   var Error;

   /*Открываем файл*/
   BegAction(1, "Обработка файла \"" + TermPath + "\\" + FileName + "\". Ждите...");
   if(not OpenExcelFile(FileName, false, true, TermPath))
      MsgBox("Ошибка открытия файла \"" + TermPath + "\\" + FileName + "\"");
      EndAction(1);
      return 0;
   end;

   PrintLn("Загрузка индивидуальных расчетных кодов клиентов");

   sheetcount = 1;
   while(ExcelApplication.Sheets.count >= sheetcount)
      sheet = ExcelApplication.Sheets(sheetcount);
        
      row = 1;

      //Бежим по файлу
      InitProgress(-1);
      while (valtype(sheet.cells(row, COL_NAME).value) != V_UNDEF) 
         Error = null;
         if (StrUpr(sheet.cells(row, COL_NAME).value) == "КЛИЕНТ") 
            row = row + 1;
            continue; 
         end;

         StrFromExcel = ExcelStr(); 
         StrFromExcel.str_Name      = TrimStr(sheet.cells(row, COL_NAME).value);
         StrFromExcel.str_Account   = TrimStr(sheet.cells(row, COL_ACCOUNT).value);
         StrFromExcel.str_EKK       = String(Int(sheet.cells(row, COL_EKK).value));
         StrFromExcel.str_TKS       = sheet.cells(row, COL_TKS).value;
         StrFromExcel.str_CODE      = String(Int(sheet.cells(row, COL_CODE).value));
         StrFromExcel.str_cur       = sheet.cells(row, COL_CUR).value;

         Error = SetCodeForMP(StrFromExcel.str_CODE, StrFromExcel.str_EKK);
         PrintLn(StrFromExcel.str_EKK + "  " + Error.errtext);

         row = row + 1;

      end; //while
      RemProgress();
      sheetcount = sheetcount+1;
   end;//eow sheet
   ExcelApplication.ActiveWorkbook.Close;

onerror(er)
   if (valtype(ExcelApplication) != V_UNDEF)
      ExcelApplication.ActiveWorkbook.Close;
   end;
   EndAction(1);
   PrintLn("Внимание! В процессе загрузки файла " + FileName + " произошли следующие ошибки :" + er.message);
end;

/**
@brief Установка кодов клиентам единого пула
*/
macro SetSinglePool()
   var sql, cmd, rs;
   var sql_upd, cmd_upd;
   var rcode = "";
   const OBJTYPE_SUBCONTR = 659;

   BegAction(1, "Обработка кодов единого пула. Ждите...");
   sql = String("SELECT mp.t_id, sf.t_number, att.t_attrid, mp.t_mpcode                          \n",
                "  FROM ddlcontrmp_dbt mp, dsfcontr_dbt sf,                                      \n",
                "       dobjatcor_dbt att                                                        \n",
                " WHERE mp.t_sfcontrid = sf.t_id                                                 \n",
                "   AND sf.t_servkind = :pservkind and sf.t_servkindsub = :pservkindsub          \n",
                "   and mp.t_marketid = :pmarketid and nvl(mp.t_rcodetks,chr(1)) = chr(1)        \n",
                "   and att.t_objecttype = "+OBJTYPE_SUBCONTR+"                                  \n",
                "   and att.t_object = lpad(sf.t_id, 10,'0')                                     \n",
                "   and att.t_groupid = "+C_CATEGORY_GRANT_RIGHT+"                               \n",
                "   and att.t_validtodate = to_date('31129999','ddmmyyyy')  ");
   cmd = RSDCommand(sql);
   cmd.AddParam("pservkind", RSDBP_IN, PTSK_STOCKDL);
   cmd.AddParam("pservkindsub", RSDBP_IN, CC_SERV_SUBKIND_STOCK);
   cmd.AddParam("pmarketid", RSDBP_IN, marketid_moex);

   rs = RSDRecordSet(cmd);
   
   sql_upd = "UPDATE ddlcontrmp_dbt SET t_rcodetks = :pcode WHERE t_id = :pid";
   
   while (rs.movenext())
      cmd_upd = RSDCommand(sql_upd);
      if (rs.value("t_attrid") == C_CATEGORY_GRANT_RIGHT_YES) 
         rcode = C_RCODE_NONSEPARATE;
      elif (rs.value("t_attrid") == C_CATEGORY_GRANT_RIGHT_NO) 
         rcode = C_RCODE_SEPARATE;
      end;
      cmd_upd.AddParam("pcode", RSDBP_IN, rcode);
      cmd_upd.AddParam("pid", RSDBP_IN, rs.value("t_id"));
      cmd_upd.Execute();
      cmd_upd.close();
      PrintLn(rs.value("t_mpcode") + "  " + rcode + " OK");
      rcode = "";
   end;
   EndAction(1);
onError(err)
   PrintLn (err.message + refill_getFullCmdErrorText(cmd) + refill_getFullCmdErrorText(cmd_upd));
   EndAction(1);
end;


/**
@brief Основная процедура
*/
macro RunLoadCode()
   var isOnTerminal: Bool = not IsStandAlone();
   var storage = GetStorageInfo();
   var selectedFilename: String;
   var processingFilename: String = storage.FilePath(TXTTAG, TXTTAG);
   var reg_path = "", regname = "";

   var stat = IsRegAutoRefillOn(regname);
   if (not stat)
      msgbox("Не включена настройка "+regname);
      exit(0);
   end;

   /*Выбираем файл для обработки*/
   var List = TDirList(reg_path + "\*.xlsx", "F");
   if(not SelectFile(selectedFilename, reg_path + "\*.xls*", "Выберите файл для загрузки", 0, true))
     MsgBox("Отказ от загрузки");
     return null;
   end;

   if (IsRunFromWine(true))
     if (isOnTerminal)
       selectedFilename = "$" + selectedFilename;
     end;
     
     if (not CopyFile(selectedFilename, processingFilename))
       MsgBox("Ошибка копирования файла\n      " + selectedFilename  + "\n  во временный файл\n      " + processingFilename);
       return;
     end;
   
     OpLoadFileWine(processingFilename);
   else
     var FileName, ExtName;
     var DirName = SplitFile(selectedFilename, FileName, ExtName);
     FileName = FileName + ExtName;
  
     OpLoadFileWin(FileName, DirName);
   end;

   /*Установка кодов для обособленных/необособленных ДС*/
   // SetSinglePool(); Отключено, согласно изменений ТЗ
end;

if (IsRunFromWine(true))
  MsgBox("Рекомендуемый режим запуска - без отладки (Ctrl + F5)");
end;

RunLoadCode();
exit(0);
