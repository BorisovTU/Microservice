/**
  @file: 		mac\user\dlng\secur\Enroll_Sched.mac
  @brief: 		Обработчики для планировщика "Операции списания и зачисления денежных средств"

  # changelog
  |date       |author         |tasks                                       |note                                                        
  |-----------|---------------|--------------------------------------------|-------------------------------------------------------------
  |2025.07.24 |Велигжанин А.В.|BOSS-9380_BOSS-10126                        | IsWorkDay( ..., AUTO_CALENDAR_ID )

*/
import enroll_class, "cblogger2.mac", "RegvalReader.mac";

private const SS_RESPONSE_END = 3; /* Успешное завершение всего сервиса */
PRIVATE CONST AUTO_CALENDAR_ID = 41;// id календаря автопополнения брокерского счета.

private class c_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText)
   var ProcessState;
   var ErrorNumber;
   var ErrorText;

   private macro init_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText)
      if(ValType(p_ProcessState) != V_UNDEF)
         ProcessState = p_ProcessState;
      end;
      if(ValType(p_ErrorNumber) != V_UNDEF)
         ErrorNumber = p_ErrorNumber;
      end;
      if(ValType(p_ErrorText) != V_UNDEF)
         ErrorText = p_ErrorText;
      end;
   end;

   init_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText);
end;

private class ResultCounter
   var all:integer = 0;
   var success:integer = 0;

   macro Increment()
      all = all + 1;
   end;

   macro IncrementSuccess()
      success = success + 1;
   end;

   macro Reset()
      all = 0;
      success = 0;
   end;
end;

macro CreateEnrolls()
   var Dt, sql;
   var counter = ResultCounter();
   var logger:c_logger = LoggerFactory()
      .NewItLog("enroll_sched.CreateEnrolls")
      .WithElapsedTime()
      .GetLogger();

   // запускаемся только в рабочий день по календарю 41 
   if ( IsWorkDay(date(), AUTO_CALENDAR_ID) )
      sql = " SELECT t_EnrollID from usr_acc306enroll_dbt "
          + " where t_state in ("+C_ENROLL_ACTION_TRANSFERRED+" ,"+C_ENROLL_ACTION_CREATED+")";
      Dt = TRsbDataSet(sql);

      var starter = EnrollStarter();
      while (Dt.movenext())
         counter.Increment();
         if (starter.RunEnrollById(Dt.EnrollID))
            counter.IncrementSuccess();
         end;
      end;
   end;

   logger.Info("всего: " + counter.all + ". успешно: " + counter.success);

   return c_ssRunResult(SS_RESPONSE_END);
end;

macro PushEnrolls()
   var Dt,sql;
   var counter = ResultCounter();
   var logger:c_logger = LoggerFactory()
      .NewItLog("enroll_sched.PushEnrolls")
      .WithElapsedTime()
      .GetLogger();

   // запускаемся только в рабочий день по календарю 41 
   if ( IsWorkDay(date(), AUTO_CALENDAR_ID) )
      var starter = EnrollStarter();

      // BIQ-15498 автоматическое выполнение операций зачисления, где "Разрешено зачисление" = да
      if (not RegValReader.GetBool("РСХБ\\ИНТЕГРАЦИЯ\\НЕТОРГОВЫЕ ПОРУЧЕНИЯ\\ИСПОЛНЕНИЕ ОПЕРАЦИЙ\\ЗАЧИСЛЕНИЯ НА FUNCOBJ", false))
         if ( not rslDefCon.isInTrans() )
            rslDefCon.beginTrans();
         end;

         sql = "select u.t_enrollid "+
               "  from USR_ACC306ENROLL_DBT u, "+
               "       dnptxop_dbt op         "+
               " where u.t_state = "+C_ENROLL_ACTION_CREATEDNPTXOP+
               "   and u.t_nptxopid = op.t_id           "+
               "   and op.t_status in (0,1)             "+
               "   and op.t_dockind = 4607              "+
               "   and exists (select 1 from dobjatcor_dbt attr "+
                             "  where attr.t_objecttype = 131  "+
                             "    and attr.t_groupid = "+C_CATEGORY_ALLOWED_INCOME+          
                             "    and attr.t_object = lpad(op.t_id,34,'0') and t_attrid = 1) "+
               "  and not exists (select 1 from  doprstep_dbt s "+
                                 "  join doproper_dbt o "+
                                 "    on s.t_id_operation = o.t_id_operation "+
                                 "  where o.t_dockind = 4607 "+
                                 "    and o.t_documentid = lpad (op.t_id, 34, '0')"+
                                 "    and s.t_symbol = 'А' and s.t_isexecute = 'R')" ;
         Dt = TRsbDataSet(sql);

         while (Dt.movenext())
            counter.Increment();
            if (starter.ExecuteOperationByEnrollId(Dt.EnrollID))
               counter.IncrementSuccess();
            end;
         end;

         logger.Info("Обработаны зачисления. Всего: " + counter.all + ". успешно: " + counter.success);

         if ( rslDefCon.isInTrans() )
            rslDefCon.commitTrans();   // на всякий случай
         end;
      end;

      counter.Reset();
      // отдельно запустим ЮЛ, так как старт операции при создании не делаем
      sql = "select op.t_id "
            "  from dnptxop_dbt op, dparty_dbt p    "+
            " where op.t_client = p.t_partyid        "+
            "   and p.t_legalform = 1                "+
            "   and op.t_status in (0,1)             "+
            "   and op.t_dockind = 4607              "+
            "   and exists (select 1 from dobjatcor_dbt attr "+
                            " where attr.t_objecttype = 131  "+
                            "   and attr.t_groupid = "+C_CATEGORY_ALLOWED_INCOME+          
                            "   and attr.t_object = lpad(op.t_id,34,'0') and t_attrid = 1) "+
            "  and not exists (select 1 from  doprstep_dbt s "+
                              "  join doproper_dbt o "+
                              "    on s.t_id_operation = o.t_id_operation "+
                              "  where o.t_dockind = 4607 "+
                              "    and o.t_documentid = lpad (op.t_id, 34, '0')"+
                              "    and s.t_symbol = 'А' and s.t_isexecute = 'R')" ;
      Dt = TRsbDataSet(sql);
      while (Dt.movenext())
         counter.Increment();
         if (starter.ExecuteOperationByNptxopId(Dt.ID))
            counter.IncrementSuccess();
         end;
      end;

      logger.Info("Обработаны зачисления ЮЛ. Всего: " + counter.all + ". успешно: " + counter.success);
   end;

   if ( rslDefCon.isInTrans() )
      rslDefCon.commitTrans();   // на всякий случай
   end;

   return c_ssRunResult(SS_RESPONSE_END);
end;

macro CreateRefunds()
   var Dt,sql;
   var starter = EnrollStarter();
   var counter = ResultCounter();
   var logger:c_logger = LoggerFactory()
      .NewItLog("enroll_sched.CreateRefunds")
      .WithElapsedTime()
      .GetLogger();

   // запускаемся только в рабочий день по календарю 41 
   if ( IsWorkDay(date(), AUTO_CALENDAR_ID) )
      sql = " SELECT * from usr_acc306enroll_dbt where t_state in ("+C_ENROLL_ACTION_TRANSFERRED+") ";
      Dt = TRsbDataSet(sql);
      while (Dt.movenext())
         counter.Increment();
         if (starter.ExecuteRefund(Dt.EnrollID))
            counter.IncrementSuccess();
         end;
      end;
   end;

   logger.Info("всего: " + counter.all + ". успешно: " + counter.success);
   return c_ssRunResult(SS_RESPONSE_END);
end;