import RsbFormsInter;

// строка кнопок
class (TRSBPushButton) TPanel_PushButton_L(title, panel, nmb_btn, ev_hnd)
var x_l = 0, y_h = 0,length = 12, btn_stp = 0, btn_sid = 0;
private var fl_w = true, ind = 0;
InitTRSBPushButton(title);
panel.AddControl(this);
panel.getSize(x_l, y_h);
if (1 + panel.btn_cnt * length + panel.btn_cnt > x_l)
   runerror("Не могу разместить кнопки на форме");
end;
btn_stp = (x_l - (panel.btn_cnt * length))/double(panel.btn_cnt + 1);
if (btn_stp > int(btn_stp))
   btn_sid = int(btn_stp) + 1;
   btn_stp = int(btn_stp);
else
   btn_sid = int(btn_stp);
   btn_stp = int(btn_stp);
end;
while (fl_w)
      if    (x_l - btn_sid * 2 - btn_stp * (panel.btn_cnt - 1) - length * panel.btn_cnt < 2)
            fl_w = false;
      elif  (x_l - (btn_sid + 1) * 2 - (btn_stp + 1) * (panel.btn_cnt - 1) - length * panel.btn_cnt >= 0)
            btn_stp = btn_stp + 1;
            btn_sid = btn_sid + 1;
      elif  (x_l - btn_sid * 2 - (btn_stp + 1)  * (panel.btn_cnt - 1) - length * panel.btn_cnt >= 0)
            btn_stp = btn_stp + 1;
      elif  (x_l - (btn_sid + 1) * 2 - btn_stp  * (panel.btn_cnt - 1) - length * panel.btn_cnt >= 0)
            btn_sid = btn_sid + 1;
      end;
end;
SetPosition(btn_sid + (length + btn_stp) * (nmb_btn - 1) , y_h - 2);
SetSize( length, 2);
Name = string("BTN_", nmb_btn);;
onClicked(R2M(panel, ev_hnd));
end;
// поле для редактирования
class (TRSBEditField) TPnl_Edit_Box(panel, p_dat_tp, p_name, p_x_pos, p_y_pos, p_x_siz, p_y_siz, title)
var panel_ = panel, v_x_pos = p_x_pos, v_y_pos = p_y_pos  , v_x_siz = p_x_siz, v_y_siz = p_y_siz, v_dat_tp = p_dat_tp;
var title_ = title;
var lb_tit;
   //
   macro set_value(val)
   Value = val;
   end;
   // получить тип данных для ВФорм
   private macro get_data_typ(fl_val)
   if   (fl_val == v_string)
        return 7;
   elif (fl_val == v_integer)
        return 1;
   elif (fl_val == v_double)
        return 4;
   elif (fl_val == v_money)
        return 4;
   elif (fl_val == v_date)
        return 9;
   elif (fl_val == v_time)
        return 10;
   else
        return 7;
   end;
   end;
   // сделать нередактируемым
   macro set_edit(flg)
   Enabled  = true;
   editable = flg;
   focusable= flg;
   end;
   // получить координату высоты объекта
   macro get_y_top
   return v_y_pos;
   end;

   macro get_y_bot
   return v_y_pos + v_y_siz;
   end;

   macro get_x_lft
   return v_x_pos;
   end;

   macro get_x_rgt
   if (strlen(title_) > 0)
      return v_x_pos + v_x_siz + strlen(title_);
   else
      return v_x_pos + v_x_siz;
   end;
   end;

   InitTRSBEditField(get_data_typ(v_dat_tp));
   panel_.AddControl(this);
   Name  = p_name;
   set_edit(true);
   if (v_dat_tp == v_string)
      TextLength = 1000;
   end;
   if (v_dat_tp == v_money)
      ValuePrecision = 2;
   end;
   if (v_dat_tp == v_double)
      ValuePrecision = 4;
   end;
   if (strlen(title_) > 0)
      lb_tit = TRSBLabel();
      panel_.AddLabel(lb_tit);
      lb_tit.Text = title_;
      lb_tit.SetPosition(v_x_pos,  v_y_pos);
      SetPosition( v_x_pos + strlen(title_), v_y_pos);
   else
      SetPosition( v_x_pos , v_y_pos);
   end;          
   SetSize(v_x_siz, v_y_siz);
end;
//панель фильтра
class (TRsbPanel) Panel2Date();
var x_lft = 35, y_hgh = 10, x_siz = 32, y_siz = 20, frm, btn_cnt = 2, y_beg = 1;
var btn_1, btn_2;
var c_Dat1, c_Dat2;
var dat1, dat2;
private var lbl;

   // Реакция на клавиши
   macro on_key_pressed(event)
   var e_s, s_n, k_k, ind = 0;
   k_k = event.keycode;
   e_s = event.Source;             
   if (IsEqClass ("TRsbPanel", e_s))
      return;
   else   
      s_n = e_s.Name; 
   end; 
   if (k_k == 316)  
      close(1);
   end;
   if (k_k == 323) 
      close(1);
   end;
   end;
   // закрытие окна
   macro on_panel_close(event)
   var ind = 0;
   if (event.StatusCode == 1)
      if (C_dat2.Value < C_dat1.Value)
         msgbox("Не верно задан период");
         return false;
      end;
      dat1 = C_dat1.Value;
      if (C_dat1.Value == date(0,0,0))
         dat1 = "";
      end;      
      dat2 = C_dat2.Value;
      if (C_dat2.Value == date(0,0,0))
         dat2 = "";
      end;      
   end;                
   end;   
   // открытие окна
   macro on_panel_open(event)
   end;
   // Реакция на кнопки
   macro on_click_btn_1(event)
   close(1);
   end;
   macro on_click_btn_2(event)
   close(0);
   end;
   // инициализация панели
   macro build_panel()  
   setPosition(x_lft, y_hgh);
   setSize(x_siz, y_siz);
   setStatus("F3-Список  F2-Выполнить F9-Сохранить и Выполнить Alt-F3-Готовые фильтры  F8-Очистить выделение  Esc-Выход");
   setCaption("Параметры отчета");
   C_dat1 = TPnl_Edit_Box(this, v_date, "DAT1", 2, 2, 10, 1, "Дата С         ");
   C_dat1.set_edit(true);  
   C_dat2 = TPnl_Edit_Box(this, v_date, "DAT2", 2, C_dat1.get_y_bot, 10, 1, "Дата По        ");
   C_dat2.set_edit(true);
   SetSize(x_Siz, C_dat2.get_y_bot + 3);

   btn_1 = TPanel_PushButton_L("F2-Выполнить", this, 1, "on_click_btn_1");
   btn_2 = TPanel_PushButton_L("Esc-Выход"   , this, 2, "on_click_btn_2");

   addEventHandler(RSB_EV_KEY_PRESSED,    R2M(this, "on_key_pressed"));
   addEventHandler(RSB_EV_WINDOW_CLOSING, R2M(this, "on_panel_close"));
   addEventHandler(RSB_EV_ENTER_WINDOW,   R2M(this, "on_panel_open"));
   end;
                                             
   macro show()
   if (Run > 0) 
      return true;
   end; 
   onerror(err)
      if (err.Code != 0)
         msgbox(err.message);
      end;
      return false;     
   end;
   
InitTRsbPanel();
build_panel();
C_dat2.set_value(date);
C_dat1.set_value(dateshift(C_dat2.Value, null, -12));
end;
