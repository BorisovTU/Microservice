/*
$Name:        LoadForbiddenCodes.mac
$Module:      Интеграция с внешними системами
$Description: Первоначальное заполнение справочника "Запрещенные коды срочного рынка"
*/
// DEF-44155 Коды срочного рынка, недоступные для регистрации
// Гераськина Т.В. 01/06/2023
import globals, rsd;
import rsexts;
import "usr_open_files.mac";

class Logger()

   macro RepHeader(dt, tm)
      var str;
      str =     "     Загрузка запрещённых кодов клиентов срочного рынка \n";
      str = str+"\n";
      str = str+"Дата загрузки  : "+dt+"\n";
      str = str+"Время загрузки : "+tm+"\n";
      println(str);
   end;

   macro RepFooter(cnt, tot)
      var str = "\n";
      str = str+"Загружено: "+cnt+"\n";
      str = str+"Всего    : "+tot+"\n";
      str = str+"\n";
      str = str+"Время  : "+time()+"\n";
      println(str);
   end;

   macro RepStr(_code)
      var str = "В список запрещённых кодов добавлен код: "+_code;
      println(str);
   end;

end;



macro RunLoadCodes(RepDate)
   CONST C_FORBIDDEN_CODES = 5055;

   var FullNameFile = "";
   var stat;
   var c_file = c_txt_file();
   var cur_file_mask = "clientsF500*.csv";
   var reg_path = "..\\Import\\";
   file datafile() txt;
   SetDelim(";");
   
   var log = Logger();
   var cur_dt = StrSubSt(string(Date()),".","")+"_"+StrSubSt(string(Time()),":","");
   private var InfoLogFileName = "..\\TxtFile\\ForbiddenCodes_"+ cur_dt + ".txt" ;
   
   var cnt = 0;
   var all = 0;
   var v_code = "-";
   var isfound = false;
   var max_num;
   
   var sql_str, sql_str2, sql_ins, cmd, rs;
   
   SetOutput( InfoLogFileName, true ); //Пишем лог в файл. Начало
   log.RepHeader(date(),time());
   
   /*Выбираем файл для обработки*/
   stat = c_file.openFile(datafile, reg_path+cur_file_mask, true, false, FullNameFile, "rsansi");

   if ( (valtype(stat) == V_UNDEF) or (stat == false) )
      msgbox("Ошибка открытия файла");
      return 0;
   end;

   next(datafile);

   if (  (StrUpr(datafile(0)) == StrUpr("DATE")) and
         (StrUpr(datafile(1)) == StrUpr("KOD")) and
         (StrUpr(datafile(2)) == StrUpr("ACCOUNT")) )
   else 
      MsgBox("Файл имеет некорректный заголовок");
      return 0;
   end;
   
   sql_str = "select nvl(max(t_element),0)+1 from  dllvalues_dbt where t_list = :list";
   cmd = RSDCommand(sql_str);
   cmd.addParam( "list", RSDBP_IN, C_FORBIDDEN_CODES);
   rs = RSDRecordSet(cmd);
   if (rs.movenext)
      max_num = rs.value(0);
   else 
      max_num = 1;
   end;
   rs.close; cmd.close;
         
   sql_str = "select count(1) countrec "+
             "  from ddlcontrmp_dbt mp, dsfcontr_dbt sf "+
             " where mp.t_sfcontrid = sf.t_id "+
             "   and sf.t_servkind = 15 "+
             "   and mp.t_mpcode = :mpcode"; 
   
   sql_str2 = "select count(1) countrec "+
              "  from dllvalues_dbt "+
              " where t_list = :list "+
              "   and t_code = :code ";
              
              
   sql_ins = " Insert into DLLVALUES_DBT (T_LIST, T_ELEMENT, T_CODE, T_NAME, T_FLAG, T_NOTE, T_RESERVE) "+
             " Values (:list, :num, :code, :code, 0, chr(1), chr(1))";
   
   
   
   //Бежим по файлу
   InitProgress(-1);
   while (next(datafile))
      if (StrLen(trim(datafile.str)) == 0)
         continue;
      end;
      isfound = false;
      
      v_code = datafile(1);
      cmd = RSDCommand(sql_str);
      cmd.addParam( "mpcode", RSDBP_IN, v_code);
      rs = RSDRecordSet(cmd);
      if (rs.movenext)
         if (rs.value(0) == 0)
            isfound = false;
         else 
            isfound = true;
         end;
      end;
      rs.close; cmd.close;
      
      if (not isfound)
         cmd = RSDCommand(sql_str2);
         cmd.addParam( "list", RSDBP_IN, C_FORBIDDEN_CODES);
         cmd.addParam( "code", RSDBP_IN, v_code);
         rs = RSDRecordSet(cmd);
         if (rs.movenext)
            if (rs.value(0) == 0)
               isfound = false;
            else 
               isfound = true;
            end;
         end;
         rs.close; cmd.close;
      end;

      if (not isfound)
         cmd = RSDCommand(sql_ins);
         cmd.addParam( "list", RSDBP_IN, C_FORBIDDEN_CODES);
         cmd.addParam( "num", RSDBP_IN, max_num);
         cmd.addParam( "code1", RSDBP_IN, v_code);
         cmd.addParam( "code2", RSDBP_IN, v_code);
         cmd.execute;
         cmd.close;
         
         max_num = max_num + 1;
         cnt = cnt + 1;
         log.RepStr(v_code);
      end;
      
      UseProgress(all);
      all = all + 1;
   end;
   RemProgress();


   log.RepFooter(cnt,all);
   SetOutput( NULL, true ); //Пишем лог в файл. Конец

   c_file.closeFile(datafile);
   ViewFile( InfoLogFileName );   

End;

/* Точка входа */
RunLoadCodes();


