import globals, rsd, activex;

class def34128readExcelClass

	var InputDir, FullNameFile, FileName, ExtName, DirName;
	var errmsg = "";
	macro getdir()
	  return  "..\\Import\\";
	end;

	macro selectExcel()
	  InputDir = getdir();
	  /*Выбираем файл для обработки*/
	  if(not SelectFile(FullNameFile, InputDir + "*.xls*", "Выберите файл для загрузки", 0, true))
	    errmsg = "Отказ от загрузки";
	    return false;
	  end;
	  return true;
	end;
	
	macro writeRecordInTable(id,contract,contract_date)
		var v_contract_date: Date = contract_date;
		var query = "insert into d_tmp_compens_def34128_dbt (t_id, t_contract, t_contract_date, t_createtime) values (?,?,?,systimestamp)";
		var sql = RSDCommand(query);		
	        sql.addParam("", RSDBP_IN, id);
	        sql.addParam("", RSDBP_IN, contract);
	        sql.addParam("", RSDBP_IN, v_contract_date);
		sql.execute;
	end;

	macro writeExcelInTemproraryTable()
		splitfile(FullNameFile, FileName, ExtName);
		FileName = FileName + ExtName;
		DirName = substr(FullNameFile, 1, index(FullNameFile, FileName) - 1);
		if(not OpenExcelFile(FileName, false, true, DirName))
			errmsg = "Ошибка открытия файла \"" + FullNameFile + "\"";
			return false;
		end;
		var Sheet = ExcelApplication.Sheets(1);
		/*Читаем строки*/
		var row = 2; //первая строка со значениями
		var fcol = 1;  //первый столбец
		while(true)
			if(valtype(Sheet.Cells(row, fcol).value) == V_UNDEF)
				break;
			end;
			var contract_number: string = Sheet.Cells(row, fcol+1).value;
			//предотвратить появление в конце номера договора ".0000" для случая, если номер договора обычное число
			if (index(contract_number,".")>0)
				contract_number = substr(contract_number,1,index(contract_number,".")-1);
			else
				contract_number = Sheet.Cells(row, fcol+1).value;  
			end;
			writeRecordInTable(Sheet.Cells(row, fcol).value,
                                           contract_number,
                                           Sheet.Cells(row, fcol+2).value);
			row = row+1;
		end;
	end;

	macro truncateTemproraryTable()
		var query = "truncate table d_tmp_compens_def34128_dbt";
	       var cmd = RSDCommand(query);
		cmd.execute;
	end;
end;

macro def34128readExcelRun()
	var readExcelObject = def34128readExcelClass;
// отладка
//readExcelObject.FullNameFile = "D:\\RSHB_SOFR_DEBUG\\Import\\def34128.xlsx";  
	if (readExcelObject.selectExcel==false)
		MsgBox("Отказ от выгрузки");
		return;
	end; 
	readExcelObject.truncateTemproraryTable;
	readExcelObject.writeExcelInTemproraryTable;
onError(er)
	MsgBox("Ошибка при загрузке данных " + er.Message);
	RunError("Ошибка при загрузке данных " + er.Message);
end;

