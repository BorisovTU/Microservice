/**
 @file 		mac\user\dlng\secur\BrokerAccStatement.mac
 @brief 	Выписка по брокерскому счету

 # menu
 - ЦБ \ Отчеты \ РСХБ \ Выписка по брокерскому счету

 # tag
 - functional_block: Отчетность_внутренняя
 - code_type: Report

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |10.06.2025 |Велигжанин.А.В.|DEF-92676                                       |SC_ConvertToFormat(..., CreateOnTerm) 
*/
import rsd;
import "DLReport_h.mac", "dlreport.mac", "dlQuery.mac", "scsrvrepfun.mac", "sc_inacc.mac";
import "dpstdrep.mac", "dl_util_xml_obj.mac", "dl_util_poi_obj.mac";
import RsbFormsInter;


private class ReportParams()
    var dateBegin:date,
        dateEnd:date,
        clientid:integer,
        sfcontrid:integer, 
        dlcontrid:integer,
        account:string;
end;

private class CBrokerAccStatement_Report(_params)
    PRIVATE VAR m_rep = T_Poi_Rep();
    PRIVATE VAR m_params = _params;
    private const V_SQLUNDEF = 26;/*GAA: 494483*/

    var CreateOnTerm = (not isStandAlone());

    macro getClientFIO()
        return "";
    end;

    PRIVATE MACRO GetClientRestAC(Account:string, OnDate:date):MONEY
        var query, cmd, DataSet;
        var Rest = $0;
    
        if(Account)
            query = " select rsb_account.restac(t_Account, t_Code_Currency, ?, t_Chapter, null) as t_rest"
                  + "   from daccount_dbt "
                  + "  where t_Account = :account_num ";

            cmd = DL_RSDCommand(query);         

            cmd.AddParam(OnDate);
            cmd.AddParam(Account);

            DataSet = cmd.Execute();
            if(DataSet.moveNext())
                Rest = DataSet.Rest;
            end; 
        end;

        return Rest;
    END;

    

    macro getAccDebet()
        var query = " SELECT SUM (t_debet) debet, SUM (t_credit) credit "
                  + "   FROM drestdate_dbt rest, daccount_dbt account "
                  + "  WHERE     account.t_account = '"+m_params.account+"' "
                  + "        AND account.t_accountid = rest.t_accountid "
                  + "        AND account.t_code_currency = rest.t_restcurrency "
                  + "        AND t_restdate BETWEEN :dateBegin AND :dateEnd ";

        var cmd = DL_RSDCommand(query);
            cmd.AddParam(m_params.dateBegin);
            cmd.AddParam(m_params.dateEnd);
        var DataSet = cmd.Execute();

        if(DataSet.moveNext())
            return iif(valtype(DataSet.debet)==v_undef,"",DataSet.debet);
        end;
    end;

    macro getAccСredit()
        var query = " SELECT SUM (t_debet) debet, SUM (t_credit) credit "
                  + "   FROM drestdate_dbt rest, daccount_dbt account "
                  + "  WHERE     account.t_account = '"+m_params.account+"' "
                  + "        AND account.t_accountid = rest.t_accountid "
                  + "        AND account.t_code_currency = rest.t_restcurrency "
                  + "        AND t_restdate BETWEEN :dateBegin AND :dateEnd ";

        var cmd = DL_RSDCommand(query);
            cmd.AddParam(m_params.dateBegin);
            cmd.AddParam(m_params.dateEnd);
        var DataSet = cmd.Execute();

        if(DataSet.moveNext())
            return iif(valtype(DataSet.credit)==v_undef,"",DataSet.credit);   /*GAA:494483*/
        end;
    end;

/*GAA: 494483*/
    macro getNumDoc(trnid:integer)
        var query=" SELECT nptxop.t_code num"
                 +"  FROM doprdocs_dbt oprdocs, doproper_dbt oproper, dnptxop_dbt nptxop"
                 +" WHERE     oprdocs.t_acctrnid = :trnid"
                 +"       AND OPRDOCS.T_ID_OPERATION = OPROPER.T_ID_OPERATION"
                 +"       AND OPROPER.T_DOCUMENTID = LPAD (nptxop.t_id, 34, 0)"
                 +"       AND OPROPER.T_DOCKIND = 4607";

        var cmd = DL_RSDCommand(query);
            cmd.AddParam(trnid);
        var DataSet = cmd.Execute();

        if(DataSet.moveNext())
            return  DataSet.num;
        else
            return "";
        end;

    end;

    macro PrintClientInfo()
        var query = " SELECT party.t_name partyname, sfc.t_number AS base_num, sf.*, dlcontr.* "
                  + "   FROM dsfcontr_dbt sf, ddlcontrmp_dbt dlcon, ddlcontr_dbt dlcontr, dparty_dbt party, dsfcontr_dbt sfc "
                  + "  WHERE     dlcon.t_dlcontrid = "+m_params.dlcontrid+" "
                  + "        AND sf.t_id = dlcon.t_sfcontrid "
                  + "        AND dlcon.t_dlcontrid = dlcontr.t_dlcontrid "
                  + "        AND party.t_partyid = sf.t_partyid "
                  + "        AND sfc.t_id = dlcontr.t_sfcontrid ";

        var cmd = DL_RSDCommand(query);
        var DataSet = cmd.Execute();

        var text = "";

        if(DataSet.moveNext())
            if(DataSet.iis == "X") 
                text = "Индивидуальный инвестиционный счет ";
            else
                text = "Брокерский счет "
            end;

            text = text + m_params.account + " " + DataSet.partyname + " №" + DataSet.base_num + "  " + date(DataSet.dateBegin);
            /*CHVA 521419*/
        var query2 = "SELECT sf.t_servkind "
                  +" FROM dsettacc_dbt s, dsfssi_dbt ss, dsfcontr_dbt sf,  daccount_dbt a,  ddlcontrmp_dbt dlcon,  dparty_dbt party "
                  +" WHERE     s.t_settaccid = ss.t_setaccid "
                  +" AND ss.t_objectid = sf.t_id "
                  +" AND a.t_account = s.t_account "
                  +" AND a.t_chapter = s.t_chapter "
                  +" AND a.t_account ='"+ m_params.account + "'"
                  +" AND sf.t_id = dlcon.t_sfcontrid "
                  +" AND party.t_partyid = sf.t_partyid ";
         var cmd2 = DL_RSDCommand(query2);
         var DataSet2 = cmd2.Execute();
          /*CHVA 521419*/
         If(DataSet2.moveNext())
            if(DataSet2.servkind == PTSK_STOCKDL ) /*CHVA 521419*/
                text = text + "(фондовый рынок ПАО Московская биржа)";
            elif(DataSet2.servkind == PTSK_DV) /*CHVA 521419*/
                text = text + "(срочный рынок ПАО Московская биржа)";
            end;

            m_rep.Put_Str(text, "START;R=9;HA=L;END;");
        end;
        end;

    end;

    macro PrintAccData()
        var res = "";/*CHVA 521349*/
        var sql;
        var query = " SELECT * "
                  + "   FROM dacctrn_dbt "
                  + "  WHERE     (t_account_payer = '"+m_params.account+"' OR t_account_receiver = '"+m_params.account+"') "
                  + "        AND t_date_carry BETWEEN :dateBegin AND :dateEnd "
                  + "        AND t_result_carry != 18 "
                  + "        AND t_state = 1 order by t_date_carry asc ";

        var cmd = DL_RSDCommand(query);
            cmd.AddParam(m_params.dateBegin);
            cmd.AddParam(m_params.dateEnd);

        var cnt = cmd.GetCount();

        if(cnt)
            var AccData = cmd.Execute();

            while(AccData.movenext)
                m_rep.Put_Val(AccData.date_Carry, "START;BRL;BRR;WT;HA=C;VA=C;T=D;");
                sql = ExecSqlSelect("select t_dealcodets from ddl_tick_dbt where t_dealcode = :tmp", MakeArray(SqlParam("tmp", AccData.numb_document)));
                sql.movenext();
                /*GAA: 494483*/
                if (valtype (sql.value(0))==V_SQLUNDEF)
                  res  = getNumDoc(AccData.acctrnid);/*CHVA 521349 проводки формируются также в СОБУ getNumDoc не учитывает это*/
                  m_rep.Put_Val(IIF(res == "", AccData.numb_document, res)/*getNumDoc(AccData.acctrnid)*/, "BRL;BRR;WT;HA=C;VA=C;");   /*CHVA 521349*/              
                else
                  m_rep.Put_Val(sql.value(0), "BRL;BRR;WT;HA=C;VA=C;");
                end;
                m_rep.Put_Val(AccData.shifr_oper, "BRL;BRR;WT;HA=C;VA=C;");
                m_rep.Put_Val("", "BRL;BRR;WT;HA=C;VA=C;");

                if(AccData.account_payer == m_params.account)
                    m_rep.Put_Val(AccData.account_receiver, "BRL;BRR;WT;HA=C;VA=C;");
                else
                    m_rep.Put_Val(AccData.account_payer, "BRL;BRR;WT;HA=C;VA=C;");
                end;

                if(AccData.account_payer == m_params.account)
                    m_rep.Put_Val(AccData.sum_payer, "BRL;BRR;;WT;HA=C;VA=C;T=M2");
                else
                    m_rep.Put_Val("", "BRL;BRR;WT;HA=C;VA=C;");
                end;

                if(AccData.account_receiver == m_params.account)
                    m_rep.Put_Val(AccData.sum_receiver, "BRL;BRR;WT;HA=C;VA=C;T=M2");
                else
                    m_rep.Put_Val("", "BRL;BRR;WT;HA=C;VA=C;");
                end;

                m_rep.Put_Val("", "BRL;BRR;WT;HA=C;VA=C;");

                m_rep.Put_Val(AccData.ground,"BRL;BRR;WT;HA=L;VA=C;END;");
            end;
        end;
    end;

/***********************************/  
    PRIVATE MACRO GetTxtPath()
        var RegistryPath = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR";
        var Path = "";
        var stat = 0;
 
        GetRegistryValue(RegistryPath, V_STRING, Path, stat);
        if(stat) 
            msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
        end;

        return Path;
    END;

    PRIVATE MACRO processPath( p_path )
        var l_str, l_p, l_path = "";

        p_path = trim( p_path );

        if( substr( p_path, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
            p_path = substr( p_path, 3 );

            l_str  = getCWD(false);    
            l_p    = strbrk( l_str, "\\" );
            while( l_p != 0 )
                 l_path = l_path + substr( l_str, 1, l_p - 1 ) + "\\";
                 l_str  = substr( l_str, l_p + 1 );
                 l_p    = strbrk( l_str, "\\" );
            end;
            return ( substr( l_path, 1, strlen( l_path ) - 1 ) + p_path );

        elif( substr( p_path, 1, 1 ) == "." ) /* ссылка на текущий каталог */
            return ( getCWD + substr( p_path, 2 ) );
        end;

        return p_path;
    END;
    MACRO GetRepFullPath(isRemote:bool)
        var Path = "";

        if(isRemote)
            var txtPath = GetTxtPath();

            if( substr( txtPath, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
                txtPath = substr(txtPath, 3 );
            end;

            if( substr( txtPath, 1, 1 ) == "\\" ) /* ссылка на вышестоящий каталог */
                txtPath = substr(txtPath, 2 );
            end;

            Path = GetCurDir(true) + "\\" + txtPath + "\\"; 
        else
            Path = processPath(GetTxtPath()) + "\\";
        end;

        // получить полный путь
        return Path;
    END;
/***********************************/  
  
    macro run()
        m_rep.Init("utf8", true);

        m_rep.Set_Column_Width(/* 1*/  65,
                               /* 2*/  80,
                               /* 3*/  59,
                               /* 4*/  50,
                               /* 5*/  150,
                               /* 6*/  100,
                               /* 7*/  100,
                               /* 8*/  40,
                               /* 9*/  400,
                               /*10*/  40,
                               /*11*/  51,
                               /*12*/  51,
                               /*13*/  58,
                               /*14*/  66,
                               /*15*/  66,
                               /*16*/  93,
                               /*17*/  65,
                               /*18*/  56,
                               /*19*/  50,
                               /*20*/  163,
                               /*21*/  59,
                               /*22*/  52,
                               /*23*/  60,
                               /*24*/  77,
                               /*25*/  77);


        m_rep.Set_Landscape();
        m_rep.Set_Indent(0);

        m_rep.Print_Header("Выписка по лицевому счету "+m_params.account, "Calibri", 9);

        m_Rep.Set_WrapText(0);
        m_rep.Put_Str("ВЫПИСКА ПО ЛИЦЕВОМУ СЧЕТУ ЗА ПЕРИОД с "+DL_DateToStr(m_params.dateBegin)+" по "+DL_DateToStr(m_params.dateEND), "START;R=5;HA=L;END;");
        m_rep.Blank_Line();

        PrintClientInfo();

        m_rep.Blank_Line();
        m_rep.Blank_Line();
        m_rep.Blank_Line();
        m_rep.Blank_Line();
        m_rep.Blank_Line();

        var rest_in = GetClientRestAC(m_params.account, m_params.dateBegin-1);
        var rest_out = GetClientRestAC(m_params.account, m_params.dateEND);

        var d_k_in = iif(rest_in>0,"(К)",iif(rest_in==0,"()","(Д)"));
        var d_k_out = iif(rest_out>0,"(К)",iif(rest_out==0,"()","(Д)"));

        //m_rep.Put_Str("Входящее сальдо     "+rest_in+d_k_in,"START;R=4;HA=C;BOR;END;");
        m_rep.Put_Str("Входящее сальдо     ","START;R=2;HA=C;BRL;BRT;");
        m_rep.Put_Str(rest_in,               "HA=R;MX;T=M2;BRT;");
        m_rep.Put_Str(d_k_in,                "HA=L;BRR;BRT;END;");
        m_rep.Put_Val("Дата проводки",       "START;BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("Номер докум.",        "BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("В\\О",                "BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("БИК",                 "BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("Счет",                "BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("Дебет",               "BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("Кредит",              "BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("С/К",                 "BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("Содержание документа","BOR;WT;HA=C;VA=C;END;");

        PrintAccData();

        m_rep.Put_Val("ИТОГО по оборотам: ",    "START;R=4;HA=L;BOR;");
        m_rep.Put_Val(getAccDebet(), "BOR;WT;HA=C;VA=C;T=M2");
        m_rep.Put_Val(getAccСredit(), "BOR;WT;HA=C;VA=C;T=M2");
        m_rep.Put_Val("", "BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("", "BOR;WT;HA=C;VA=C;END;");
        //m_rep.Put_Str("Исходящее сальдо     "+rest_out+d_k_out,    "START;R=4;HA=C;BOR;END;");
        m_rep.Put_Str("Исходящее сальдо     ","START;R=2;HA=C;BRL;BRB;");
        m_rep.Put_Str(rest_out,               "HA=R;MX;T=M2;BRB;");
        m_rep.Put_Str(d_k_out,                "HA=L;BRR;BRB;END;");
        m_Rep.Set_WrapText(1);
        m_rep.Blank_Line();


        m_rep.Put_Str("АО \"РОССЕЛЬХОЗБАНК\"",    "START;R=3;HA=L;END;");

        m_rep.Print_Bottom();

        var m_CurrentRepName = "BROKERREP_REPORT";
        var brokFileManager = BrokerRepFileStatusManager(m_CurrentRepName+".xlsx", CreateOnTerm);
        m_rep.Save(brokFileManager.Get_GENERATED_REPORTS_path());
        brokFileManager.ShowFile();

    end;
end;

private const FT_STRING = 7;
private const FT_DATE = 9;

macro accounts_event(rs, cmd, id, key)
    if(cmd == DLG_KEY)
        if(key == 13)
            return CM_SELECT;
        end;
    end;
end;

class (TRsbPanel) MyRsbPanel1
    InitTRsbPanel();
    setSize(40,6);
    setPosition(35,15);


    var currentRS;

    addLabel(TRsbLabel(3, 2, "Счет:"));
    var m_sEditFld:String = "";
    var m_EditField:TRsbEditField = TRsbEditField(FT_STRING);
    m_EditField.bindValue(this, "m_sEditFld", 20);
    m_EditField.setPosition(10,2);
    m_EditField.setSize(20,1);
    addControl(m_EditField);

    addLabel(TRsbLabel(3, 3, "Клиент:"));
    var m_sEditFld2:String = "";
    var m_EditField2:TRsbEditField = TRsbEditField(FT_STRING);
    m_EditField2.bindValue(this, "m_sEditFld2", 20);
    m_EditField2.setPosition(10,3);
    m_EditField2.setSize(20,1);
    m_EditField2.enabled = false;
    addControl(m_EditField2);

    addLabel(TRsbLabel(3, 4, "Договор:"));
    var m_sEditFld3:String = "";
    var m_EditField3:TRsbEditField = TRsbEditField(FT_STRING);
    m_EditField3.bindValue(this, "m_sEditFld2", 20);
    m_EditField3.setPosition(10,4);
    m_EditField3.setSize(20,1);
    m_EditField3.enabled = false;
    addControl(m_EditField3);


    addLabel(TRsbLabel(3, 1, "Период:"));
    var m_DateBegin:TRsbEditField = TRsbEditField(FT_DATE);
    m_DateBegin.setPosition(10,1);
    m_DateBegin.setSize(10,1);
    m_DateBegin.value = {curdate};
    m_DateBegin.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "DATEBEGIN_KEY_PRESSED"));
    addControl(m_DateBegin);

    var m_DateEnd:TRsbEditField = TRsbEditField(FT_DATE);
    m_DateEnd.setPosition(24,1);
    m_DateEnd.setSize(10,1);
    m_DateEnd.value = {curdate};
    m_DateEnd.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "DATEEND_KEY_PRESSED"));
    addControl(m_DateEnd);

    m_EditField.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "EditField_KEY_PRESSED"));
    m_EditField.addEventHandler(RSB_EV_REMOVE_FOCUS , R2M(this, "EditField_EXIT"));
    addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "Panel_ButtonEvent"));

    PRIVATE MACRO AddCol (ar,ind, fld, head, width, rdonly, ty, dp)
        ar.value (ind * 6)     = fld;
        ar.value (ind * 6 + 1) = head;
        ar.value (ind * 6 + 2) = width;
        ar.value (ind * 6 + 3 ) = rdonly;   // fldType
        ar.value (ind * 6 + 4 ) = dp;//   decPoint
        ar.value (ind * 6 + 5 ) = 0;   // reserv
    END;

    macro getClientsList(default)
        var query = " SELECT party.t_name, sf.t_number, a.t_account, dlcon.t_dlcontrid "
                  + "   FROM dsettacc_dbt s, "
                  + "        dsfssi_dbt ss, "
                  + "        dsfcontr_dbt sf, "
                  + "        daccount_dbt a, "
                  + "        ddlcontrmp_dbt dlcon, "
                  + "        dparty_dbt party "
                  + "  WHERE     s.t_settaccid = ss.t_setaccid "
                  + "        AND ss.t_objecttype = 659 "
                  + "        AND ss.t_objectid = sf.t_id "
                  + "        AND a.t_account = s.t_account "
                  + "        AND a.t_chapter = s.t_chapter "
                  + "        AND a.t_chapter = 1 "
                  + "        AND sf.t_id = dlcon.t_sfcontrid "
                  + "        AND party.t_partyid = sf.t_partyid ";

        var col1 = TArray;   
        var arnum = -1;
        AddCol (col1, arnum=arnum+1, "t_name",   "Клиент",        30,  0,0);
        AddCol (col1, arnum=arnum+1, "t_number", "Номер договора",20,  0,0);
        AddCol (col1, arnum=arnum+1, "t_account","Счет",          25,  0);

        var cmd1 = RsdCommand(query);
        var rs1 = RsdRecordSet(cmd1, rsdval_client, rsdval_static);
        if(RunScroll(rs1, arnum+1, col1, null ,"accounts_event","Брокерские счета "))
            currentRS = rs1;
            return rs1.value("t_account");
        end;

        return default;
    end;

    macro findContr()
        var query = " SELECT party.t_name, sf.t_number, a.t_account, dlcon.t_dlcontrid "
                  + "   FROM dsettacc_dbt s, "
                  + "        dsfssi_dbt ss, "
                  + "        dsfcontr_dbt sf, "
                  + "        daccount_dbt a, "
                  + "        ddlcontrmp_dbt dlcon, "
                  + "        dparty_dbt party "
                  + "  WHERE     s.t_settaccid = ss.t_setaccid "
                  + "        AND ss.t_objecttype = 659 "
                  + "        AND ss.t_objectid = sf.t_id "
                  + "        AND a.t_account = s.t_account "
                  + "        AND a.t_chapter = s.t_chapter "
                  + "        AND a.t_chapter = 1 "
                  + "        AND a.t_account = '" + m_EditField.value + "'"
                  + "        AND sf.t_id = dlcon.t_sfcontrid "
                  + "        AND party.t_partyid = sf.t_partyid ";

        var cmd1 = RsdCommand(query);
        var rs1 = RsdRecordSet(cmd1, rsdval_client, rsdval_static);
        if(rs1.movenext)
            currentRS = rs1;
        end;
    end;

    macro EditField_KEY_PRESSED(RsbEvent)
       if(RsbEvent.keyCode == 317)

          m_sEditFld = getClientsList(m_sEditFld);
          m_EditField.value = currentRS.value("t_account");
          m_EditField2.value = currentRS.value("t_name");
          m_EditField3.value = currentRS.value("t_number");
       end;

       if(RsbEvent.keyCode == 27)
          close(0);
       end

    end;

    macro EditField_EXIT(RsbEvent)
        findContr();
        if(currentRS)
          if(currentRS.value("t_account") != null)
            m_EditField.value = currentRS.value("t_account");
            m_EditField2.value = currentRS.value("t_name");
            m_EditField3.value = currentRS.value("t_number");
          else
            m_EditField2.value = "";
            m_EditField3.value = "";
          end;
        else
          m_EditField2.value = "";
          m_EditField3.value = "";
        end;
    end;

    macro Panel_ButtonEvent(RsbEvent)
       if(RsbEvent.keyCode == 316)
           var RepParams = ReportParams();

           RepParams.dateBegin = m_DateBegin.value;
           RepParams.dateEND = m_DateEnd.value;
           RepParams.dlcontrid = currentRS.value("t_dlcontrid");
           RepParams.account = m_EditField.value;

           CBrokerAccStatement_Report(RepParams).run();
       end;
    end;

    macro DATEBEGIN_KEY_PRESSED(RsbEvent)
       if(RsbEvent.keyCode == 317)
           m_DateBegin.value = GetDateByCalendar(m_DateBegin.value);
       end;
    end;

    macro DATEEND_KEY_PRESSED(RsbEvent)
       if(RsbEvent.keyCode == 317)
           m_DateEnd.value = GetDateByCalendar(m_DateEnd.value);
       end;
    end;
end;

var panel = MyRsbPanel1;
panel.run;
