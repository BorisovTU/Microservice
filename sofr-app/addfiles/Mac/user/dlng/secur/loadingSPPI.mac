/*Загрузка результата SPPI-теста*/
import activex, or_rep_h, globals;
import fiinter, ctinter, dlmisc, "dlutils.mac","xls_parser.mac", "scsrvrepfun.mac";

private var errors_str = "";
private macro ResultCopyFile(done:bool, data_filepath:string, data_filename:string, TermPath:string)

   var data_file_new_path, data_file_new_file;

   if(done)
      data_file_new_path = data_filepath + "Arh\\";
   else
      data_file_new_path = data_filepath + "Err\\";
   end;

   MakeDir(data_file_new_path);
   data_file_new_path = data_file_new_path + "\\";
   MakeDir(data_file_new_path);

   data_file_new_file = data_file_new_path + data_filename;

   if(not CopyFile(data_filepath + data_filename, data_file_new_file ) )
      Println("Ошибка копирования файла " + data_filename + " в " + data_file_new_file);
   else
      Println("Файл " + data_filename + " скопирован в " + data_file_new_file);
      RemoveFile(data_filepath + data_filename);
   end;

   if((ValType(TermPath) != V_UNDEF) and (TermPath != ""))
    RemoveFile (TermPath + "\\" + data_filename);
   end;
end;

private macro WriteLog(StrErr)
  if(IsShedulerRunning())
    errors_str = errors_str + "\n" + StrErr;
  end;
end;


/*Загрузка*/
macro LoadingSPPI(errmsg:@string)
  record fininstr (fininstr);
  var InputDir, FullNameFile, FileName, ExtName, DirName;
  var Sheet, fcol = 1, frow = 1, row;
  var result = tarray(), iresult;
  var action = 0;
  var protocol, fi, tableObj;
  var dt = {curdate};
  var AttrID, error, appmes;
  var errcnt = 0;
  var TermPath = GetCurDir(true) + "\\TxtFile";
  var DirWithFile = "";

  /**/
  class tSPPI
    var isin;
    var rnum;
    var rSPPI;
  end;

  /**/
  macro getdir()
    var stateA, errCodeA, sRegValA;
    var statA = GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\SPPI", V_STRING, sRegValA, errCodeA);
    if((statA == V_STRING) and (errCodeA == 0) and (strlen(sRegValA) > 0))
      return sRegValA + "\\";
    else
      msgboxex("Не задана директория для Excel-файла результатами SPPI-тестов");
      return "";
    end;
  end;

  /**/
  macro resultSPPI(r)
    if(r)
      return "Тест пройден";
    else
      return "Тест не пройден";
    end;
  end;

  /**/
  macro nameSP(sp)
    if(valtype(sp) == V_UNDEF)
      return "";
    else
      return sp;
    end;
  end;

  /**/
  macro InsertProtocol(isin, rnum, rSPPI, fiid, mes);
    tableObj.SetValueCell("bodyline_F01", isin);
    tableObj.SetValueCell("bodyline_F02", rnum);
    tableObj.SetValueCell("bodyline_F03", rSPPI);
    tableObj.SetValueCell("bodyline_F04", fiid);
    tableObj.SetValueCell("bodyline_F05", mes);
    tableObj.AddStr();
  end;


  InputDir = getdir();
  
   var datafilemask;
   var List;

    datafilemask = "*.xlsx";
    List = TDirList ( InputDir + datafilemask, "f" );

   List.Sort(0);

   if (List.Count == 0)
      errmsg ="Ошибка открытия файла данных. Не найден файл по маске " + InputDir + datafilemask;
      EndAction(1);
      return false;
   else
        FileName = List.name(0);
   end;

/* Опрделим дату */

    var dd, mm, yyyy, tempstr1 = "";
    tempstr1 = substr(FileName, strlen(FileName) - 15, 11 );

    yyyy = substr(tempstr1, 1, 5);
    mm = substr(tempstr1, 7, 2);
    dd = substr(tempstr1, 10, 2);
    dt =  date(int(dd), int(mm), int(yyyy));


/**/
  /*Открываем файл*/
  BegAction(1, "Обработка файла \"" + FileName + "\".|Ждите...");
  action = 1;
  
  var poiReader = CExcelPoi();

  if (not poiReader.Open(PathCombine(InputDir,FileName)))
    RunError("Не удалось открыть файл \""+FileName+"\" при помощи POI");
  end;
  
  /*Ищем первую колонку*/
  while((valtype(poiReader.CellValueByRowAndCol(1, fcol)) == V_UNDEF) and (fcol <= 10))
    fcol = fcol + 1;
  end;
  if(valtype(poiReader.CellValueByRowAndCol(1, fcol)) == V_UNDEF)
    errmsg = "Не найдено значимое поле в первой строке файла \"" + FullNameFile + "\"";
    EndAction(1);
    return false;
  end;

  /*Ищем первую строку*/
  while(frow <= 10)
    if((valtype(poiReader.CellValueByRowAndCol(frow, fcol + 2)) != V_UNDEF) and 
       ((strupr(poiReader.CellValueByRowAndCol(frow, fcol + 2)) == "ТЕСТ ПРОЙДЕН") or 
        (strupr(poiReader.CellValueByRowAndCol(frow, fcol + 2)) == "ТЕСТ НЕ ПРОЙДЕН")))
      break;
    end;
    frow = frow + 1;
  end;
  if((valtype(poiReader.CellValueByRowAndCol(frow, fcol + 2)) == V_UNDEF) or 
     ((strupr(poiReader.CellValueByRowAndCol(frow, fcol + 2)) != "ТЕСТ ПРОЙДЕН") and 
      (strupr(poiReader.CellValueByRowAndCol(frow, fcol + 2)) != "ТЕСТ НЕ ПРОЙДЕН")))
    errmsg = "Не найдены строки с результатами в файла \"" + FullNameFile + "\"";
    EndAction(1);
    return false;
  end;

  /*Читаем строки до тех пор, пока есть результат теста*/
  row = frow;
  while(true)
    result[result.size] = tSPPI;
    result[result.size-1].isin = trim(poiReader.CellValueByRowAndCol(row, fcol));
    result[result.size-1].rnum = trim(poiReader.CellValueByRowAndCol(row, fcol + 1));
    if(strupr(poiReader.CellValueByRowAndCol(row, fcol + 2)) == "ТЕСТ ПРОЙДЕН")
      result[result.size-1].rSPPI = true;
    else
      result[result.size-1].rSPPI = false;
    end;
    row = row + 1;
    if((valtype(poiReader.CellValueByRowAndCol(row, fcol + 2)) == V_UNDEF) or 
       ((strupr(poiReader.CellValueByRowAndCol(row, fcol + 2)) != "ТЕСТ ПРОЙДЕН") and 
        (strupr(poiReader.CellValueByRowAndCol(row, fcol + 2)) != "ТЕСТ НЕ ПРОЙДЕН")))
      break;
    end;
  end;

  poiReader = null;
  EndAction(1);
  action = 0;

  /*Создаем файл для протокола*/
  protocol = CTemplateSXLSX("loadingSPPI.xlsx");
  protocol.SetValue_NameCell("header_text", string("Протокол загрузки результатов SPPI-теста за " + dt));

  tableObj = protocol.RegisterTable("bodyline");

  /*Проходим по полученным записям*/
  InitProgress(int(result.size), "Ждите...", "Обработка результатов SPPI-тестов");
  action = 2;
  iresult = 0;
  while(iresult < result.size)
    /*ISIN*/
    if(valtype(result[iresult].isin) != V_UNDEF)
      fi = ПолучитьФинИн(result[iresult].isin, fininstr, null, null, null, 5);
    /*№ госрегистрации*/
    elif(valtype(result[iresult].rnum) != V_UNDEF)
      fi = ПолучитьФинИн(result[iresult].rnum, fininstr, null, null, null, 4);
    else
      errcnt = errcnt + 1;
      WriteLog("Загрузка SPPI-теста " + date() + " " + time() + ". В строке " + string(frow + iresult) + " для ценной бумаги не указан ISIN/Рег.номер. Подробнее см. протокол загрузки в каталоге :\\Term\\TxtFile");
      InsertProtocol(nameSP(result[iresult].isin), nameSP(result[iresult].rnum), resultSPPI(result[iresult].rSPPI), "", "В строке " + string(frow + iresult) + " для ценной бумаги не указан ISIN/Рег.номер");
      iresult = iresult + 1;
      UseProgress(iresult);
      continue;
    end;
    /*Проверяем, что нашли ЦБ*/
    appmes = "";
    if(fi != 0)
      appmes = "Бумага не найдена. Попытка поиска в РУДата. ";
      /*Загружаем бумагу*/
/* - пока не грузим бумагу из рудаты*/
      error = execmacrofile("loadRUData", "SOFR_LoadRuDataByID", result[iresult].isin, 1);
      if(not error)
        errcnt = errcnt + 1;
        WriteLog("Загрузка SPPI-теста " + date() + " " + time() + ". " + appmes + "Бумагу не удалось загрузить из РУДата. Введите бумагу вручную. Подробнее см. протокол загрузки в каталоге :\\Term\\TxtFile");
        InsertProtocol(nameSP(result[iresult].isin), nameSP(result[iresult].rnum), resultSPPI(result[iresult].rSPPI), "", appmes + "Бумагу не удалось загрузить из РУДата. Введите бумагу вручную");
        iresult = iresult + 1;
        UseProgress(iresult);
        continue;
      else
        appmes = appmes + "Бумага загружена из РУДата. ";
        /*Опять ищем*/
        /*ISIN*/
        if(valtype(result[iresult].isin) != V_UNDEF)
          fi = ПолучитьФинИн(result[iresult].isin, fininstr, null, null, null, 5);
        /*№ госрегистрации*/
        elif(valtype(result[iresult].rnum) != V_UNDEF)
          fi = ПолучитьФинИн(result[iresult].rnum, fininstr, null, null, null, 4);
        end;
        /*Проверяем, что все создали*/
        if(fi != 0)
          errcnt = errcnt + 1;
          WriteLog("Загрузка SPPI-теста " + date() + " " + time() + ". " + appmes + "Бумага не найдена. Подробнее см. протокол загрузки в каталоге :\\Term\\TxtFile");
          InsertProtocol(nameSP(result[iresult].isin), nameSP(result[iresult].rnum), resultSPPI(result[iresult].rSPPI), "", appmes + "Бумага не найдена");
          iresult = iresult + 1;
          UseProgress(iresult);
          continue;
        end;
      end;
/**/
    end;
    /*Проверим текущее значение результата прохождения SPPI теста*/
/* - не сравниваем установленное значение с устанавливаемым
    error = 0;
    AttrID = null;
    GetMainObjAttr(error, OBJTYPE_AVOIRISS, UniID(fininstr, OBJTYPE_AVOIRISS), 62/*Результат прохождения SPPI теста*/, AttrID, null, null, dt);
    if(error != 0)
      errcnt = errcnt + 1;
      WriteLog("Загрузка SPPI-теста " + date() + " " + time() + ". " + appmes + "Ошибка определения значения категории \"Результат прохождения SPPI теста\" " + error + ". Подробнее см. протокол загрузки в каталоге :\\Term\\TxtFile");
      InsertProtocol(nameSP(result[iresult].isin), nameSP(result[iresult].rnum), resultSPPI(result[iresult].rSPPI), fininstr.fi_code, appmes + "Ошибка определения значения категории \"Результат прохождения SPPI теста\" " + error);
      iresult = iresult + 1;
      UseProgress(iresult);
      continue;
    end;
    /*Есть текущее значение и оно не изменилось*/
    if((valtype(AttrID) != V_UNDEF) and
       (((AttrID == 1/*Да*/) and (result[iresult].rSPPI)) or ((AttrID == 2/*Нет*/) and (not result[iresult].rSPPI))))
      InsertProtocol(nameSP(result[iresult].isin), nameSP(result[iresult].rnum), resultSPPI(result[iresult].rSPPI), fininstr.fi_code, appmes + "Результат SPPI-теста не изменился");
      iresult = iresult + 1;
      UseProgress(iresult);
      continue;
    end;
*/
    /*Результат прохождения SPPI теста*/
    if(result[iresult].rSPPI)
      AttrID = 1/*Да*/;
    else
      AttrID = 2/*Нет*/;
    end;
    error = 0;
    ConnectObjAttr(error, OBJTYPE_AVOIRISS, UniID(fininstr, OBJTYPE_AVOIRISS), 62/*Результат прохождения SPPI теста*/, AttrID, null, null, dt);
    if(error != 0)
      errcnt = errcnt + 1;
      WriteLog("Загрузка SPPI-теста " + date() + " " + time() + ". " + appmes + "Ошибка установки значения категории \"Результат прохождения SPPI теста\" " + error + ". Подробнее см. протокол загрузки в каталоге :\\Term\\TxtFile");
      InsertProtocol(nameSP(result[iresult].isin), nameSP(result[iresult].rnum), resultSPPI(result[iresult].rSPPI), fininstr.fi_code, appmes + "Ошибка установки значения категории \"Результат прохождения SPPI теста\" " + error);
      iresult = iresult + 1;
      UseProgress(iresult);
      continue;
    end;
    /*Приоритетный портфель*/
    if(not result[iresult].rSPPI)
      AttrID = 2/*ССПУ_ЦБ*/;
    else
      /*Определяем текущее значение портфеля*/
      error = 0;
      AttrID = null;
      GetMainObjAttr(error, OBJTYPE_AVOIRISS, UniID(fininstr, OBJTYPE_AVOIRISS), 61/*Приоритетный портфель*/, AttrID, null, null, dt);
      if(error != 0)
        errcnt = errcnt + 1;
        WriteLog("Загрузка SPPI-теста " + date() + " " + time() + ". " + appmes + "Ошибка определения значения категории \"Приоритетный портфель\" " + error + ". Подробнее см. протокол загрузки в каталоге :\\Term\\TxtFile");
        InsertProtocol(nameSP(result[iresult].isin), nameSP(result[iresult].rnum), resultSPPI(result[iresult].rSPPI), fininstr.fi_code, appmes + "Ошибка определения значения категории \"Приоритетный портфель\" " + error);
        iresult = iresult + 1;
        UseProgress(iresult);
        continue;
      end;
      /*Есть текущее значение и оно равно ССПУ_ЦБ*/
      if((valtype(AttrID) != V_UNDEF) and (AttrID == 2/*ССПУ_ЦБ*/))
        /*Меняем на СССД_ЦБ*/
        AttrID = 3/*СССД_ЦБ*/;
      else
        AttrID = 0;
      end;
    end;
    if(AttrID > 0)
      error = 0;
      ConnectObjAttr(error, OBJTYPE_AVOIRISS, UniID(fininstr, OBJTYPE_AVOIRISS), 61/*Приоритетный портфель*/, AttrID, null, null, dt);
      if(error != 0)
        errcnt = errcnt + 1;
        WriteLog("Загрузка SPPI-теста " + date() + " " + time() + ". " + appmes + "Ошибка установки значения категории \"Приоритетный портфель\" " + error + ". Подробнее см. протокол загрузки в каталоге :\\Term\\TxtFile");
        InsertProtocol(nameSP(result[iresult].isin), nameSP(result[iresult].rnum), resultSPPI(result[iresult].rSPPI), fininstr.fi_code, appmes + "Ошибка установки значения категории \"Приоритетный портфель\" " + error);
        iresult = iresult + 1;
        UseProgress(iresult);
        continue;
      end;
    end;
    /*Результат успешной загрузки*/
    if(result[iresult].rSPPI)
      InsertProtocol(nameSP(result[iresult].isin), nameSP(result[iresult].rnum), resultSPPI(result[iresult].rSPPI), fininstr.fi_code, appmes + "Установлен результат прохождения SPPI теста: Да");
    else
      InsertProtocol(nameSP(result[iresult].isin), nameSP(result[iresult].rnum), resultSPPI(result[iresult].rSPPI), fininstr.fi_code, appmes + "Установлен результат прохождения SPPI теста: Нет");
    end;
    iresult = iresult + 1;
    UseProgress(iresult);
  end;
  tableObj.EndTable();
  RemProgress();
  action = 0;

  /*Итоги*/
  protocol.SetValue_NameCell("count_text", string("Обработано строк: " + iresult + ", из них с ошибками: " + errcnt));
  /*Показываем протокол*/
  
  var expFileName = GetExlusiveFileName("xlsx");
  IF (not protocol.GetWorkbook().saveAs(PathCombine(GetAbsoluteTxtPath(),expFileName)))
    RunError("Не удалось создать файл " + expFileName);
  END;
  if(not IsShedulerRunning())
    SC_ShowFile(GetAbsoluteTxtPath(), expFileName);
  END;
  /*Возвращаем результат*/
  if(errcnt == 0)
    ResultCopyFile(true, InputDir, FileName, IIF(not IsStandAlone(), TermPath, NULL));
    return true;
  else
    ResultCopyFile(false, InputDir, FileName, IIF(not IsStandAlone(), TermPath, NULL));

    if (IsShedulerRunning() AND (errors_str != ""))
      RunError("errcnt != 0");
    end;
    return false;
  end;
OnError(er)                                                                       
  if(index(er.message, "errcnt != 0") == 0 )
    errmsg = "Исключение: " + er.module + " " + er.line + " " + er.message;
    WriteLog(errmsg);
  end;
  if(action == 1)
    EndAction(1);
  elif(action == 2)
    RemProgress();
  end;
  if ((index(er.message, "errcnt != 0") > 0 ) AND IsShedulerRunning())
    RunError(errors_str);
  end;
  return false;
end;

/*Точка входа*/
var errmsg = "";
if(not LoadingSPPI(@errmsg))
  if(strlen(errmsg) > 0)
    if(not IsShedulerRunning())
       msgboxex(errmsg);
    end;
  else
    if(not IsShedulerRunning())
       msgboxex("SPPI-тесты загружены с ошибками");
    end;
  end;
else

if(not IsShedulerRunning())
  msgboxex("Результат SPPI-теста успешно загружен");
end;

end;
exit(1);
