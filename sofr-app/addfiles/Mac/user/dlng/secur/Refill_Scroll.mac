/** 
 @file Refill_Scroll.mac
 @brief Операция ручного подкрепления счетов клиентов в НКЦ 
  
 # tag 
 - functional_block:Клиенты_Зачисление
 - code_type:API

   
 # changeLog 
 |date       |author         |tasks            |note                            
 |-----------|---------------|-----------------|--------------------------------
 |13.05.2024 |Гераськина Т.В.|BOSS-1702        | Создание

*/ 

import refill_class;
import "sud_utils.mac";


private const K_ENTER   = 13,
              K_ESC     = 27,
              K_F2      = 316,
              K_F3      = 317,
              K_F4      = 318,
              K_F5      = 319,
              K_F6      = 320,
              K_F7      = 321,
              K_F8      = 322,
              K_F9      = 323,
              K_CTRL_R  = 18,
              K_ALT_P   = 281,
              K_ALT_F2  = 361,
              K_ALT_F6  = 365,
              K_ALT_F8  = 367;


/**
@brief Класс Условия фильтрации
*/
private class TFiltrCond()
   var BegDate          : date;
   var EndDate          : date;
   var St_Created       : bool;
   var St_Opened        : bool;
   var St_Closed        : bool;
   var St_Deleted       : bool;
   
   macro Init()
      BegDate           = GetDateAfterWorkDays(date(),-100);
      EndDate           = date();
      St_Created        = true;
      St_Opened         = true;
      St_Closed         = false;
      St_Deleted        = false;
   end;
end;

private var FiltrCond = TFiltrCond();
FiltrCond.Init();


/**
@brief Класс Изменяемые параметры операции
*/
private class TOperationParam()
   var Type       : integer;
   var Type_Name  : string;
   var Sum        : money;
   var RCode      : string;
   var Account    : string;
   var Contract   : integer;
   var Contract_number : string;
   
   macro Init()
      Type           = -1;
      Type_Name      = "";
      Sum            = $0.0;
      RCode          = "";
      Account        = "";
      Contract       = -1;
      Contract_number= "";
   end;
end;

private var OperationParam = TOperationParam();
OperationParam.Init();


macro m_add_column(p_ar, p_ind, p_fld, p_head, p_width, p_edit, p_dec_point)
   p_ar.value(p_ind * 6 + 0) = p_fld;       // fieldName
   p_ar.value(p_ind * 6 + 1) = p_head;      // header
   p_ar.value(p_ind * 6 + 2) = p_width;     // width
   p_ar.value(p_ind * 6 + 3) = p_edit;      // fldType (2 = FBT)
   p_ar.value(p_ind * 6 + 4) = p_dec_point; // decPoint
   p_ar.value(p_ind * 6 + 5) = 0;           // reserv
end; /* macro m_add_column() */


macro EvProc_Contr(rs,cmd,id,key)
   var CM_DEFAULT = null;
   if ((cmd == DLG_KEY)and(key == K_ENTER)) 
      return CM_SELECT;
   elif ((cmd == DLG_KEY)and(key == K_F4))
      return CM_DEFAULT;
   elif ((cmd == DLG_KEY)and(key == K_ESC))
      return CM_CANCEL;
   end;

   return CM_DEFAULT;
end;


class TRefillScroll()
   
   macro TryBlockRecord(pid)
      var cmd, rs;
      cmd = " select 1 from UREFILL_DBT where t_id = :1 for update nowait";
      cmd = RSDCommand(cmd);
      cmd.addParam("",RSDBP_IN, pid);
      rs = RsdRecordSet(cmd);
      rs.movenext();
      return true;
   onError(er)
      return false;
   end;
   
   
   private class(TRsbPanel) TFiltrPanel()
      const FT_DATE =  9;
      const FT_CHR  = 12;

      var LBegDate:TRsbLabel, ,
          FieldData:TRsbEditField;

/*
          1         2         3
 123456789012345678901234567890123
╔═══════════Фильтр════════════════╗
║ За период с[00.00.0000]         ║1 
║          по[00.00.0000]         ║2 
║ Статусы:                        ║3 
║ [X] Отложенная                  ║4 
║ [X] Открытая                    ║5 
║ [X] Закрытая                    ║6 
║                                 ║7 
╚═════════════════════════════════╝ 
*/                                  
     var rows=1, control;
     InitTRsbPanel();

     SetCaption("Фильтр");
     SetStatus("~Esc~ Выход ~F2~ Применить");
     SetSize(28, 10);
     SetPosition(45,10);

     rows = rows + 1;                               //║ За период с[00.00.0000]         ║1 
     LBegDate = TRsbLabel(2, rows, "За период с");
     addLabel(LBegDate);                 	
    
     FieldData = TRsbEditField(FT_DATE);
     FieldData.setPosition(14,rows);
     FieldData.setSize(10,1);
     FieldData.Name = "BegDate";
     FieldData.value = FiltrCond.BegDate;
     addControl(FieldData);

     rows = rows + 1;                               //║          по[00.00.0000]         ║2 
     FieldData = TRsbEditField(FT_DATE);
     FieldData.setPosition(14,rows);
     FieldData.setSize(10,1);
     FieldData.Name = "EndDate";
     FieldData.value = FiltrCond.EndDate;
     addControl(FieldData);

     rows = rows + 1;                               //║ Статусы:                        ║3 
     addLabel(TRsbLabel(2, rows, "Статусы:"));

     rows = rows + 1;                               //║ [X] Отложенная                  ║4 
     control = TRsbCheckBox(1);                    
     control.name = "St_Created";
     control.dataType = FT_CHR;
     control.setPosition(3, rows);
     control.checked = FiltrCond.St_Created;
     addControl(control);
     addLabel(TRsbLabel(5, rows, "Отложенная"));

     rows = rows + 1;                               //║ [X] Открытая                    ║5 
     control = TRsbCheckBox(1);
     control.name = "St_Opened";
     control.dataType = FT_CHR; 
     control.setPosition(3, rows);
     control.checked = FiltrCond.St_Opened;
     addControl(control);
     addLabel(TRsbLabel(5, rows, "Открытая"));

     rows = rows + 1;                               //║ [X] Закрытая                    ║6 
     control = TRsbCheckBox(1);
     control.name = "St_Closed";
     control.dataType = FT_CHR; 
     control.setPosition(3, rows);
     control.checked = FiltrCond.St_Closed;
     addControl(control);
     addLabel(TRsbLabel(5, rows, "Закрытая"));

     rows = rows + 1;                               //║ [X] Удаленная                   ║7 
     control = TRsbCheckBox(1);
     control.name = "St_Deleted";
     control.dataType = FT_CHR; 
     control.setPosition(3, rows);
     control.checked = FiltrCond.St_Deleted;
     addControl(control);
     addLabel(TRsbLabel(5, rows, "Удаленная"));

     rows = rows + 1;
     
     addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "KeyEvent"));//KeyEvent
     
     macro KeyEvent(RsbEvent:Object)
        var focusedControl;
       
        if (rsbEvent.KeyCode == K_ESC)
           this.Close(rsbEvent.KeyCode);
        elif ((rsbEvent.KeyCode == K_F2) or (rsbEvent.KeyCode == K_F9))
           FiltrCond.BegDate          = getControl("BegDate"   ).value;
           FiltrCond.EndDate          = getControl("EndDate"   ).value;
           FiltrCond.St_Created       = getControl("St_Created").checked;
           FiltrCond.St_Opened        = getControl("St_Opened" ).checked;
           FiltrCond.St_Closed        = getControl("St_Closed" ).checked;
           FiltrCond.St_Deleted       = getControl("St_Deleted" ).checked;
           Close(rsbEvent.KeyCode);
        elif (rsbEvent.KeyCode == K_F3)
           focusedControl = this.focusedControl();
           if(focusedControl.Name == "BegDate")
              FiltrCond.BegDate = GetDateByCalendar(FiltrCond.BegDate);
              getControl("BegDate").Value = FiltrCond.BegDate;
           elif(focusedControl.Name == "EndDate")
              FiltrCond.EndDate = GetDateByCalendar(FiltrCond.EndDate);
              getControl("EndDate").Value = FiltrCond.EndDate;
           end;
         end;
      end;
   end;


   private class(TRsbPanel) TNewOperationPanel()
      const FT_STRING   = 7;
      const FT_DATE     = 9;
      const FT_CHR      = 12;
      const FT_MONEY    = 25;

      //const C_OWN          = 1, C_OWN_NAME         = "собственные счета РСХБ в НКЦ";
      //const C_SINGLE_POOL  = 2, C_SINGLE_POOL_NAME = "единый пул клиентов";
      //const C_INDIVIDUAL   = 4, C_INDIVIDUAL_NAME  = "индивидуальный РК(ТКС)";

      var ComboData:TRsbComboBox;
      var marketid_moex = refill_ПолучитьБиржуПоВидуКодаДБО(DLCCK_USC); // ММВБ
      
/*
          1         2         3         4         5         6
 1234567890123456789012345678901234567890123456789012345678901
╔═══════════Создание операции════════════════════════════════╗
║ Тип подкрепления   [единый пул клиентов - не обособленные] ║1
║ Сумма подкрепления [000000000000000.00]                    ║2
║ Расчетный код      [xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx] ║3
║ Субдоговор         [xxxxxxxxxxxxxxxxxx_ф]                  ║4
║                                                            ║5
╚════════════════════════════════════════════════════════════╝
*/
      var rows=1, control;
      InitTRsbPanel();
      OperationParam.Init();

      SetCaption("Создание операции");
      SetStatus("~Esc~ Выход ~F2/F9~ Создать ~F3~ Выбор");
      SetSize(64,7);
      SetPosition(45,8);

      rows = rows + 1;                               //║ Тип подкрепления   [единый пул клиентов - не обособленные] ║1
      addLabel(TRsbLabel(2, rows, "Тип подкрепления"));

      ComboData = TRsbComboBox();
      ComboData.setPosition(21,rows);
      ComboData.setSize(40,1);
      ComboData.enabled = true;
      ComboData.Name = "Type";
      ComboData.addChoice(C_REFILL_TYPE_OWN        , C_REFILL_TYPE_OWN_NAME);
      ComboData.addChoice(C_REFILL_TYPE_SINGLE_POOL, C_REFILL_TYPE_SINGLE_POOL_NAME);
      ComboData.addChoice(C_REFILL_TYPE_INDIVIDUAL , C_REFILL_TYPE_INDIVIDUAL_NAME);
      ComboData.currentChoice = C_REFILL_TYPE_INDIVIDUAL;
      OperationParam.Type = ComboData.currentChoice;
      OperationParam.Type_Name = ComboData.value;
      ComboData.OnItemSelected(R2M(this, "COMBOBOX_SELECT"));
      addControl(ComboData);

      rows = rows + 1;                               //║ Сумма подкрепления [000000000000000.00]                    ║2
      addLabel(TRsbLabel(2, rows, "Сумма подкрепления"));

      var FieldData_sum = TRsbEditField(FT_MONEY);
      FieldData_sum.setPosition(21,rows);
      FieldData_sum.setSize(18,1);
      FieldData_sum.Name = "Sum";
      FieldData_sum.bindValue(OperationParam, "Sum");
      FieldData_sum.valuePrecision = 2;
      FieldData_sum.focusable = true;
      FieldData_sum.enabled = true;
      FieldData_sum.editable = true;
      addControl(FieldData_sum); 

      rows = rows + 1;                               //║ Расчетный код      [xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx] ║3
      addLabel(TRsbLabel(2, rows, "Расчетный код"));

      var ComboData_rcode = TRsbComboBox();
      ComboData_rcode.setPosition(21,rows);
      ComboData_rcode.setSize(37,1);
      ComboData_rcode.enabled = true;
      ComboData_rcode.focusable = true;
      ComboData_rcode.Name = "RCode";
      ComboData_rcode.TextLength = 25;
      ComboData_rcode.value = "";
      ComboData_rcode.AddEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "NoAction"));
      addControl(ComboData_rcode);

      rows = rows + 1;                               //║ Субдоговор         [xxxxxxxxxxxxxxxxxx_ф]                  ║4
      addLabel(TRsbLabel(2, rows, "Субдоговор"));

      var FieldData_contr = TRsbEditField(FT_STRING);
      FieldData_contr.setPosition(21,rows);
      FieldData_contr.setSize(20,1);
      FieldData_contr.Name = "Contract_number";
      FieldData_contr.bindValue(OperationParam, "Contract_number", 64);
      FieldData_contr.focusable = true;
      FieldData_contr.enabled = true;
      FieldData_contr.editable = false;
      addControl(FieldData_contr);
      
      this.EnableDisableFields(ComboData.currentChoice);

      rows = rows + 1;

      macro NoAction(event)
        if (event.keyCode == K_F3)
          event.keyCode= -K_F3;
        end;
      end;

      macro FillOperation(NewOp, OperationParam)
         NewOp.Rec.m_ID                = 0;
         NewOp.Rec.m_DATE_OPERATION    = {curdate};
         NewOp.Rec.m_TIME_OPERATION    = time();
         NewOp.Rec.m_TIME_ACCEPT       = time(0,0,0);
         NewOp.Rec.m_TYPE              = C_REFILL_TYPE_MANUAL;
         NewOp.Rec.m_TYPE_REFILL       = OperationParam.Type;
         NewOp.Rec.m_TYPE_REFILL_NAME  = OperationParam.Type_Name;
         NewOp.Rec.m_SUM_OPERATION     = OperationParam.Sum;
         NewOp.Rec.m_RCODE             = OperationParam.RCode;
         NewOp.Rec.m_Account           = OperationParam.Account;
         NewOp.Rec.m_CONTRACT          = OperationParam.Contract;
         NewOp.Rec.m_CONTRACT_NUMBER   = OperationParam.Contract_number;
         NewOp.Rec.m_STATUS            = C_REFILL_ACTION_CREATED;
         NewOp.Rec.m_STATUS_NAME       = refill_GetStatusName(C_REFILL_ACTION_CREATED);
         NewOp.Rec.m_OPER              = {Oper};
         NewOp.Rec.m_OPER_NAME         = refill_OperName(NewOp.Rec.m_OPER);
      end;

      macro CheckParams()
         var cmd_contr, rs_contr;
         OperationParam.Sum =  FieldData_sum.value;
         OperationParam.RCode = ComboData_rcode.value;
         if (FieldData_contr.Value != OperationParam.Contract_number)
            OperationParam.Contract_number = "";
            OperationParam.Contract = -1;
            OperationParam.RCode = "";
            ComboData_rcode.value = "";
            redraw;
         end;
      
         if ( (ComboData.currentChoice < C_REFILL_TYPE_OWN) and (ComboData.currentChoice > C_REFILL_TYPE_INDIVIDUAL) )
            MsgBox("Не задан тип подкрепления");
            return false;
         end;
         
         if (ComboData.currentChoice == C_REFILL_TYPE_OWN)
            OperationParam.Account = C_ACCOUNT_PAYER_OWN;
         elif (ComboData.currentChoice == C_REFILL_TYPE_INDIVIDUAL)
            OperationParam.Account = C_ACCOUNT_PAYER_INDIVIDUAL;
         elif (ComboData.currentChoice == C_REFILL_TYPE_SINGLE_POOL)
            if (OperationParam.RCode == C_RCODE_SEPARATE)
               OperationParam.Account = C_ACCOUNT_PAYER_SEPARATE;
            else 
               OperationParam.Account = C_ACCOUNT_PAYER_NONSEPARATE;
            end;
         end;
         
         if (OperationParam.Sum <= 0)
            msgbox("Не задана сумма операции");
            return false;
         end;
         
         if (StrLen(OperationParam.RCode) <= 1)
            if (OperationParam.Type == C_REFILL_TYPE_INDIVIDUAL)
               if ( (OperationParam.Contract == -1) and (StrLen(FieldData_contr.Value) > 1 ) ) // попробуем найти договор, если его не выбрали, а указали вручную
                  cmd_contr = String(" SELECT sf.t_id, sf.t_number, mp.t_rcodetks         \n",
                                     "  FROM dsfcontr_dbt sf, ddlcontrmp_dbt mp, dparty_dbt p \n",
                                     " WHERE mp.t_sfcontrid = sf.t_id AND mp.t_marketid = "+marketid_moex+"     \n",
                                     "   AND sf.t_partyid = p.t_partyid AND p.t_legalform = "+PTLEGF_INST+"     \n",
                                     "   AND sf.t_dateclose = to_date('01010001', 'ddmmyyyy')                   \n",
                                     "   AND sf.t_servkind = "+PTSK_STOCKDL+"                                   \n",
                                     "   AND sf.t_servkindsub = "+CC_SERV_SUBKIND_STOCK+"                       \n",
                                     "   AND sf.t_number = :pnum                                                 ");
                  cmd_contr = RSDCommand(cmd_contr);
                  cmd_contr.AddParam("pnum", RSDBP_IN, FieldData_contr.Value);
                  rs_contr = RSDRecordset(cmd_contr, null, RSDVAL_STATIC );
                  if (rs_contr.movenext())
                     OperationParam.RCode = rs_contr.value("t_rcodetks");
                     OperationParam.Contract = rs_contr.value("t_id");
                     OperationParam.Contract_number = rs_contr.value("t_number");

                     ComboData_rcode.value = OperationParam.RCode;
                     FieldData_contr.value = OperationParam.Contract_number;
                     
                     redraw();
                     if (StrLen(OperationParam.RCode) <= 1)
                        msgbox("У договора не указан расчетный код");
                        return false;
                     end;
                  else 
                     msgbox("Договор с номером "+ FieldData_contr.Value +" не найден среди открытых субдоговоров по бирже для юр.лиц");
                     return false;
                  end;
               else 
                  msgbox("У договора не указан расчетный код");
                  return false;
               end;

            else
               msgbox("Не задан расчетный код");
               return false;
            end;
         end;

         return true;
      end;

      AddEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "KeyEvent"));
      AddEventHandler(RSB_EV_REMOVE_FOCUS, R2M(this, "RemoveFocus"));
      
      macro EnableDisableFields(_currentChoice)
         OperationParam.RCode = "";
         OperationParam.Contract = -1;
         OperationParam.Contract_number = "";
         ComboData_rcode.value = "";
         FieldData_contr.value = "";

         if (_currentChoice == C_REFILL_TYPE_OWN)
            ComboData_rcode.enabled = false;
            FieldData_contr.editable = false;
            FieldData_contr.enabled = false;
            
            OperationParam.RCode = C_RCODE_OWN;
            ComboData_rcode.value = C_RCODE_OWN;
            
         elif (_currentChoice == C_REFILL_TYPE_SINGLE_POOL)
            ComboData_rcode.enabled = true;
            FieldData_contr.editable = false;
            FieldData_contr.enabled = false;
            
         elif (_currentChoice == C_REFILL_TYPE_INDIVIDUAL)
            ComboData_rcode.enabled = false;
            FieldData_contr.editable = true;
            FieldData_contr.enabled = true;
         else 
            FieldData_contr.editable = true;
            FieldData_contr.enabled = true;
         end;

      end;

      /*Обработчик выбора*/
      macro COMBOBOX_SELECT(RsbEvent:Object)
         OperationParam.Type = RsbEvent.source.currentChoice;
         OperationParam.Type_Name = RsbEvent.source.value;
         
         EnableDisableFields(RsbEvent.source.currentChoice);

         redraw();
      end; 

     
       /*Обработчик смены фокуса*/
       macro RemoveFocus(RsbEvent:Object)
         if(this.focusedControl.Name == "RCode")
            OperationParam.RCode = this.focusedControl.value;
            redraw();
         elif(this.focusedControl.Name == "Sum")
            OperationParam.Sum = this.focusedControl.value;
            redraw();
         elif(this.focusedControl.Name == "Type")
         end;
       end;

      /*Обработчик клавиш*/
      macro KeyEvent(RsbEvent:Object)
         var focusedControl;
         var NewOp;
         var cmd_contr, rs_contr;
         var cmd_rcode, rs_rcode;
       
         if (rsbEvent.KeyCode == K_ESC)
            this.Close(rsbEvent.KeyCode);
         elif ((rsbEvent.KeyCode == K_F2) or (rsbEvent.KeyCode == K_F9))
            if (CheckParams())
               NewOp = TRefill();
               FillOperation(NewOp, OperationParam);
               NewOp.SaveRefillOperationTrn();

               Close(rsbEvent.KeyCode);
            end;
         elif ((rsbEvent.KeyCode == K_F3) or (rsbEvent.KeyCode == -K_F3))
            focusedControl = this.focusedControl();
            if(focusedControl.Name == "Type")
            elif(focusedControl.Name == "Contract_number")
               if (ComboData.currentChoice == C_REFILL_TYPE_INDIVIDUAL)
                  var col_ar_contr = TArray(), col_num_contr = -1;
                  cmd_contr = String(" SELECT sf.t_id, sf.t_number, obj.t_code, p.t_name, mp.t_rcodetks         \n",
                                     "  FROM dsfcontr_dbt sf, ddlcontrmp_dbt mp, dparty_dbt p, dobjcode_dbt obj \n",
                                     " WHERE mp.t_sfcontrid = sf.t_id AND mp.t_marketid = "+marketid_moex+"     \n",
                                     "   AND sf.t_partyid = p.t_partyid AND p.t_legalform = "+PTLEGF_INST+"     \n",
                                     "   AND obj.t_objecttype = "+OBJTYPE_PARTY+"                               \n",
                                     "   AND obj.t_codekind = "+C_PARTY_CODEKIND_ABS+"                          \n",
                                     "   AND obj.t_objectid = p.t_partyid AND obj.t_state = 0                   \n",
                                     "   AND sf.t_dateclose = to_date('01010001', 'ddmmyyyy')                   \n",
                                     "   AND sf.t_servkind = "+PTSK_STOCKDL+"                                   \n",
                                     "   AND sf.t_servkindsub = "+CC_SERV_SUBKIND_STOCK+"                        ");
                  cmd_contr = RSDCommand(cmd_contr);
                  rs_contr = RSDRecordset(cmd_contr, null, RSDVAL_STATIC );

                  m_add_column(col_ar_contr, col_num_contr=col_num_contr+1, "t_id"        , "ID"                  , 10,0,0);
                  m_add_column(col_ar_contr, col_num_contr=col_num_contr+1, "t_number"    , "Номер договора"      , 22);
                  m_add_column(col_ar_contr, col_num_contr=col_num_contr+1, "t_code"      , "Код клиента"         , 15);
                  m_add_column(col_ar_contr, col_num_contr=col_num_contr+1, "t_name"      , "Наименование клиента", 25);
                  m_add_column(col_ar_contr, col_num_contr=col_num_contr+1, "t_rcodetks"  , "Счет"                , 25);

                  if (RunScroll (rs_contr,col_num_contr+1,col_ar_contr,"list_rcodetks","EvProc_Contr", "", "~Enter~ Выбор ~ESC~ Выход"))
                     OperationParam.RCode = rs_contr.value("t_rcodetks");
                     OperationParam.Contract = rs_contr.value("t_id");
                     OperationParam.Contract_number = rs_contr.value("t_number");

                     ComboData_rcode.value = OperationParam.RCode;
                     FieldData_contr.value = OperationParam.Contract_number;
                     
                     redraw();
                  end;
               end;

            elif(focusedControl.Name == "RCode")
               if (ComboData.currentChoice == C_REFILL_TYPE_SINGLE_POOL)
                  var col_ar_rcode = TArray(), col_num_rcode = -1;
                  cmd_rcode = String(" SELECT "+GetSQLString(C_RCODE_NONSEPARATE)+" rcode, "+GetSQLString(C_RCODE_NONSEPARATE_NAME)+" rcode_name FROM dual         \n",
                                     "  UNION \n",
                                     " SELECT "+GetSQLString(C_RCODE_SEPARATE)+" rcode, "+GetSQLString(C_RCODE_SEPARATE_NAME)+" rcode_name FROM dual     ");
                  cmd_rcode = RSDCommand(cmd_rcode);
                  rs_rcode = RSDRecordset(cmd_rcode, null, RSDVAL_STATIC );

                  m_add_column(col_ar_rcode, col_num_rcode=col_num_rcode+1, "rcode"       , "Код"               , 10,0,0);
                  m_add_column(col_ar_rcode, col_num_rcode=col_num_rcode+1, "rcode_name"  , "Наименование"      , 22);

                  if (RunScroll (rs_rcode,col_num_rcode+1,col_ar_rcode,"list_rcodetks_name","EvProc_Contr", "", "~Enter~ Выбор ~ESC~ Выход"))
                     OperationParam.RCode = rs_rcode.value("rcode");

                     ComboData_rcode.value = OperationParam.RCode;
                     
                     redraw();
                  end;
               end;
            
            end;
         end;
      end;
   end;


   macro Run()
      private var SaveNum=0;
      private var sql_scr, cmd_scr, rs_scr, nrecs=0,i=0;
      private var sql_main  = "";
      private var sql_filtr = "";
      private var sql_order = "";

      private macro getSQlFiltr()
         return   " and T_DATE_OPERATION between "+GetSQLDate(FiltrCond.BegDate)+ " and " + GetSQLDate(FiltrCond.EndDate) 
                + " and T_STATUS in (" +
                                    refill_iif (FiltrCond.St_Created , string(C_REFILL_ACTION_CREATED) ,"-1") + ", " +
                                    refill_iif (FiltrCond.St_Opened  , string(C_REFILL_ACTION_OPENED)  ,"-1") + ", " +
                                    refill_iif (FiltrCond.St_Closed  , string(C_REFILL_ACTION_CLOSED)  ,"-1") + ", " + 
                                    refill_iif (FiltrCond.St_Deleted , string(C_REFILL_ACTION_DELETED) ,"-1") + 
                                   ") " ;
      end;

      macro EvProcMain(rs,cmd,id,key)
         var CM_DEFAULT = null;
         var cmd1, rs1, col_ar = TArray(), col_num = -1,Refill;
         var stat, err_msg;
         if (cmd == DLG_INIT)
            /*Позиционируемся*/
            if(SaveNum > 0)
               while(rs.movenext)
                  if(SaveNum == rs.value("t_id"))
                     GoToScroll(rs);
                     break;
                  end;
               end;
            end;
         elif ((cmd == DLG_KEY)and(key == K_ENTER))/*показать текст комментария/ошибки*/
            //msgbox(rs.value("t_comment"));
         elif ((cmd == DLG_KEY) and (key == K_F2))
            if ( not TryBlockRecord(rs.Value("t_id")) )
               msgboxex("Запись уже обрабатывается", MB_OK);
               if ( rslDefCon.isInTrans() )
                  rslDefCon.rollbackTrans();
               end;
               return CM_SELECT;
            end;

            Refill = TRefill(rs.Value("t_id"));

            if ( not rslDefCon.isInTrans() )
               rslDefCon.beginTrans();
            end;

            stat = Refill.CreatePaymentForOperation();
            if (not stat)
               msgboxex("Возникла ошибка "+Refill.rec.m_Comment, MB_OK);
            elif (StrLen(Refill.rec.m_Comment) > 1)
               msgboxex(Refill.rec.m_Comment, MB_OK);
               var link = TLink(Refill.Rec.m_ID);
               if ((link.errorcode != 0) and (link.eventid == 0))
                 link.errorcode = 0;
                 link.errortext = "";
                 
                 var rsdcmd = RSDCommand("UPDATE UNPTXOP_PAYMENT_LINK_DBT SET t_errorcode = :errorcode, t_errortext = :errortext WHERE T_REFILLID = :refillid");
                 rsdcmd.AddParam("errorcode", RSDBP_IN, link.errorcode);
                 rsdcmd.AddParam("errortext", RSDBP_IN, link.errortext);
                 rsdcmd.AddParam("refillid", RSDBP_IN, link.refillid);
                 rsdcmd.Execute();
               end;
            end;

            err_msg = "";
            stat = RunRefillPayment(@err_msg, Refill.rec.m_Paymentid);
            if (not stat)
               msgboxex("Возникла ошибка "+err_msg, MB_OK);
            end;

            SaveNum = rs.value("t_id");

            if ( rslDefCon.isInTrans() )
               rslDefCon.commitTrans();   // на всякий случай
            end;

            return CM_SELECT;
         elif ((cmd == DLG_KEY)and(key == K_F3))

         elif ((cmd == DLG_KEY)and(key == K_F5))/*фильтр*/
            var FiltrPanel = TFiltrPanel();
            FiltrPanel.Run();
            sql_filtr =  getSQLFiltr();
            return CM_SELECT;
         elif ((cmd == DLG_KEY)and(key == K_F9)) /* новая операция */
            var NewOperationPanel = TNewOperationPanel();
            NewOperationPanel.Run;
            return CM_SELECT;
         elif ((cmd == DLG_KEY)and(key == K_CTRL_R))/*обновить*/
            SaveNum = rs.value("t_id");
            if ((ValType(SaveNum) == V_UNDEF) or (ValType(SaveNum) == 26))
               SaveNum = 0;
            end;
            return CM_SELECT;
         elif ((cmd == DLG_KEY)and(key == K_ALT_P))
            cmd1 = String(" SELECT u.t_paymentid FROM unptxop_payment_link_dbt u \n",
                          "  WHERE u.t_refillid = :pid AND u.t_paymentid <> 0   ");
            cmd1 = RSDCommand(cmd1);
            cmd1.addParam("pid",RSDBP_IN, rs.Value("t_id"));
            rs1 = RsdRecordSet(cmd1);
            if (rs1.movenext())
               var PaymentObj = RsbPayment(rs1.value("t_paymentid"));
               PM_ProcessPanel(PaymentObj, 0, null, null, null, null, null, null, null, null, null, null, null, 4);
            end;
         elif ((cmd == DLG_KEY)and(key == K_ALT_F2))

            if ( not rslDefCon.isInTrans() )
               rslDefCon.beginTrans();
            end;

            cmd1 = String(" SELECT t_id FROM urefill_dbt \n",
                          "  WHERE t_status = ", C_REFILL_ACTION_CREATED);
            nrecs = SQL_GetNRecs(cmd1);
            if (nrecs == 0)
               msgboxex("Нет подходящих записей",MB_OK);
            else
               InitProgress(nrecs, "Создание платежей");
               cmd1 = RSDCommand(cmd1+" for update skip locked ");
               rs1 = RsdRecordSet(cmd1);
               i = 0;
               while (rs1.movenext())
                  Refill = null;
                  Refill = TRefill(rs1.Value("t_id"));
                  Refill.CreatePaymentForOperation();  // ошибки будут в комментариях 
                  UseProgress(i=i+1);
               end;
               RemProgress();
               SaveNum = rs.value("t_id");

               err_msg = "";
               stat = RunRefillPayment(@err_msg);
               if (not stat)
                  msgboxex("Возникла ошибка "+err_msg, MB_OK);
               end;

               return CM_SELECT;
            end;

            if ( rslDefCon.isInTrans() )
               rslDefCon.commitTrans();   // на всякий случай
            end;

         elif ((cmd == DLG_KEY)and(key == K_ALT_F8))
            Refill = TRefill(rs.Value("t_id"));
            if (Refill.rec.m_STATUS == C_REFILL_ACTION_CREATED)
               Refill.rec.m_STATUS = C_REFILL_ACTION_DELETED;
               Refill.SaveRefillOperation();
               return CM_SELECT;
            else 
               msgbox("Неподходящий статус операции. |Для удаления операция должна быть в статусе \"Отложена\"");
            end;

         elif ((cmd == DLG_KEY)and(key == K_ALT_F6))
            cmd1 = String("SELECT trn.t_date_carry, trn.t_account_payer, trn.t_account_receiver, trn.t_sum_payer, trn.t_ground,  \n",
                          "       u.t_paymentid                                                                                  \n",
                          "  FROM unptxop_payment_link_dbt u, dacctrn_dbt trn                                                    \n",
                          " WHERE u.t_refillid = :pid and trn.t_acctrnid = u.t_acctrnid                                       ");
            cmd1 = RSDCommand(cmd1);
            cmd1.addParam("pid",RSDBP_IN, rs.Value("t_id"));
            rs1 = RSDRecordset(cmd1, null, RSDVAL_STATIC );

            m_add_column(col_ar, col_num=col_num+1, "t_date_carry"      , "Дата"       , 10);
            m_add_column(col_ar, col_num=col_num+1, "t_account_payer"   , "Дебет"      , 22);
            m_add_column(col_ar, col_num=col_num+1, "t_account_receiver", "Кредит"     , 22);
            m_add_column(col_ar, col_num=col_num+1, "t_sum_payer"       , "Сумма"      , 12);
            m_add_column(col_ar, col_num=col_num+1, "t_ground"          , "Назначение" , 25);
            m_add_column(col_ar, col_num=col_num+1, "t_paymentid"       , "ID платежа" , 12,0,0);

            if (RunScroll (rs1,col_num+1,col_ar,"refill_trans","EvProc2", "", "ESC - выход"))
            end;

         elif ((cmd == DLG_KEY)and(key == K_F6))
            cmd1 = String( "SELECT row_number() OVER (ORDER BY t_stepid) num, t_date_operation,                \n",
                           "       to_char(t_systemdate,'dd.mm.yyyy hh24:mi:ss') t_systemdate,                 \n",
                           "       decode(t_action, "+C_STEP_ACTION_INIT+", 'Инициализация',                   \n",
                           "                        "+C_STEP_ACTION_UNLOAD+", 'Выгрузка платежного поручения', \n",
                           "                        "+C_STEP_ACTION_PROCESS_STATUS+", 'Обработка статуса',     \n",
                           "                        "+C_STEP_ACTION_FORM_ENTRY+", 'Формирование проводок',     \n",
                           "                        "+C_STEP_ACTION_CLOSE+", 'Закрытие операции',              \n",
                           "                        'Не определен' ) t_action,                                 \n",
                           "       t_comment, t_oper, t_stepid                                                 \n",
                           "  FROM urefill_step_dbt WHERE t_refillid = :pid ORDER BY t_stepid           ");
            cmd1 = RSDCommand(cmd1);
            cmd1.addParam("pid",RSDBP_IN, rs.Value("t_id"));
            rs1 = RSDRecordset(cmd1, null, RSDVAL_STATIC);

            m_add_column(col_ar, col_num=col_num+1, "num",              "№"             , 3 ,false,0);
            m_add_column(col_ar, col_num=col_num+1, "t_date_operation", "Дата операции" , 10);
            m_add_column(col_ar, col_num=col_num+1, "t_systemdate",     "Системная дата", 18);
            m_add_column(col_ar, col_num=col_num+1, "t_action",         "Действие"      , 20);
            m_add_column(col_ar, col_num=col_num+1, "t_comment",        "Комментарий"   , 15);
            m_add_column(col_ar, col_num=col_num+1, "t_oper",           "Операционист"  , 5);
            m_add_column(col_ar, col_num=col_num+1, "t_stepid",         "ID"            , 5);

            if (RunScroll (rs1,col_num+1,col_ar,"refill_step","EvProc2", "", "ESC - выход", true, 7, 5, 95, 30))
            end;

         elif ((cmd == DLG_KEY)and(key == K_ESC))
            return 2;
         end;

         return CM_DEFAULT;

      end;

      /* точка входа скроллинг */

      var col_ar = TArray();
      col_ar.size = 0;
      var col_num = -1;

      sql_main = String(" SELECT t_id, t_date_operation, to_char(t_time_operation, 'HH24:MI:SS') t_time_operation, to_char(t_time_accept, 'HH24:MI:SS') t_time_accept, t_type, \n",
                        "        t_type_refill, t_type_refill_name, t_sum_operation, t_rcode, \n",
                        "        t_contract, t_contract_number, t_status, t_status_name, t_oper, t_oper_name \n",
                        "   FROM urefill_dbt \n",
                        "  WHERE 1=1 ");
      sql_order = " order by t_id desc";
      sql_filtr = getSQlFiltr();
      sql_scr = sql_main + sql_filtr + sql_order;

      cmd_scr = RSDCommand(sql_scr);
      cmd_scr.NullConversion = true;

      rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
      rs_scr.NullConversion = true;

      m_add_column(col_ar, col_num=col_num+1, "t_id",                "ID",                   4);
      m_add_column(col_ar, col_num=col_num+1, "t_date_operation",    "Дата подкрепления",    10);
      m_add_column(col_ar, col_num=col_num+1, "t_time_operation",    "Время создания записи",8);
      m_add_column(col_ar, col_num=col_num+1, "t_time_accept",       "Время подтверждения",  8);
      m_add_column(col_ar, col_num=col_num+1, "t_type_refill_name",  "Тип подкрепления",     40);
      m_add_column(col_ar, col_num=col_num+1, "t_sum_operation",     "Сумма подкрепления",   10);
      m_add_column(col_ar, col_num=col_num+1, "t_rcode",             "Расчетный код в НКЦ",  20);
      m_add_column(col_ar, col_num=col_num+1, "t_contract_number",   "Номер субдоговора",    20);
      m_add_column(col_ar, col_num=col_num+1, "t_status_name",       "Статус операции",      15);
      m_add_column(col_ar, col_num=col_num+1, "t_oper_name",         "Операционист",         25);
      
      while(RunScroll(rs_scr, col_num+1, col_ar, "refill_list", "EvProcMain", "", "~ESC~ Выход ~F2~ Создание платежа ~Alt+F2~ Массовое создание ~F5~ Фильтр ~F6~ История действий ~F9~ Создание операции ~Alt+F6~ Проводки  ~Alt+P~ Платежи")) //, true/*, 7, 5, NULL, 40*/))
      /* Обновляем скроллинг */
         cmd_scr.close(); cmd_scr = NULL;
         rs_scr.close(); rs_scr = NULL;

         sql_scr = sql_main + sql_filtr + sql_order;

         cmd_scr = RSDCommand(sql_scr);
         cmd_scr.NullConversion = true;

         rs_scr = RSDRecordSet(cmd_scr, RSDVAL_CLIENT, RSDVAl_STATIC);
      end;

      rs_scr.close();
   end;
end;



var regname;
var stat = IsRegAutoRefillOn(regname);
if (not stat)
   msgbox("Не включена настройка "+regname);
   exit(0);
end;

var s = TRefillScroll();
s.Run();

exit(1);