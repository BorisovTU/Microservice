/*
$Name:        paymlink_sp.mac
$Description: Работа с платежами
$Programmer:  Кротов А.
*/
import dlquery;
import captureoutput;


/*Вставка/удаление связки с ТО по платежу на постобработке выполнения/отката шага*/
macro PostStepLink(CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                   errTrn,           /* Статус выполнения шага-!0-произошла ошибка */
                   FirstDoc,         /* Указатель на первичный документ */
                   ID_Operation,     /* Номер экземпляра операции */
                   Num_Step,         /* Номер шага операции(из настроек) */
                   Kind_Operation,   /* Вид операции */
                   KindDoc,          /* Вид первичного документа */
                   KindStep,         /* Вид шага операции */
                   ID_Step)
  record tick(dl_tick);
  var cmdPM, rsPM = null;
  var DocID = 0;

  /*При ошибке ничего не делаем*/
  if(errTrn)
    return;
  end;

  /*Ищем платежи*/
  if(KindDoc == 107 /*Документ свертки*/)
    SetBuff(tick, FirstDoc);
    DocID = tick.dealid;
    /*Выполнение шага*/
    if(CommitOrRollback == 1)
      StartCaptureOutput();
      [ 
        select pmpaym.t_paymentid t_paymentid
        from doprdocs_dbt oprdocs
        join dpmpaym_dbt pmpaym on pmpaym.t_paymentid = substr(oprdocs.t_documentid,13) and
                                   pmpaym.t_dockind = ########## and
                                   pmpaym.t_documentid = ?
        where oprdocs.t_id_operation = ? and
              oprdocs.t_id_step = ? and
              oprdocs.t_dockind = 451 /*Связка с изменением статуса платежа*/
      ](KindDoc);
      cmdPM = RSDCommand(EndCaptureOutput());
      cmdPM.AddParam("docid", RSDBP_IN, tick.dealid);
      cmdPM.AddParam("id_operation", RSDBP_IN, ID_Operation);
      cmdPM.AddParam("id_step", RSDBP_IN, ID_Step);
      cmdPM.Execute();
      rsPM = TRsbDataSet(cmdPM);
    end;
  else
//    msgboxex("Платеж для связки не найден|"+"KindDoc="+KindDoc+"|Kind_Operation="+Kind_Operation+"|ID_Operation="+ID_Operation+"|Num_Step="+Num_Step+"|ID_Step="+ID_Step);
    return 1;
  end;

  /*Проходим по платежам платеж*/
  if((rsPM != null)/*Выполнение шага*/ or (CommitOrRollback == 2)/*Откат шага*/)
    ExecMacroFile("paymlink", "ModifyLink", rsPM, CommitOrRollback, ID_Operation, ID_Step, KindDoc, DocID);
  end;

  /*Возвращаем результат*/
  return 1;
end;
