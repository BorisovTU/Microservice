/*
$Name:        paymutil.mac
$Module:      Ценные бумаги
$Description: Работа с платежами
$Programmer:  Кротов А.
*/
import dlquery, OprInter, DealsInter;
import sp_class, settl, dlntfd;
import captureoutput;


/*Созданеие платежа по ТО
  >=0 - число созданных платежей
  <0 - ошибка (текст ошибки в errmsg)
var errmsg;
msgbox(CreatingPaym(1535, @errmsg)+"|"+errmsg);
*/
macro CreatingPaym(dlrqid:integer, dlrq:trechandler, errmsg:@string, InStepOp:bool, IsSWIFT:bool)
  var err = 0, cnt = 0, i = 0;
  var query, cmd, DataSet;
  var CurRQ = NULL, CurFD = NULL, DealCode;
  if(valtype(InStepOp) != V_BOOL)
    InStepOp = false;
  end;

  if(valtype(IsSWIFT) != V_BOOL)
    IsSWIFT = false;
  end;

  MACRO GetDealCode(DocKind, DocID)
    var cmd, DataSet;
    var DealCode = "";

    cmd = DL_RSDCommand(  " select dt.t_DealCode "
                        + "   from dv_sctotaldeals dt "
                        + "  where dt.t_DocKind = ? "
                        + "    and dt.t_DocID = ?"
                       );
    cmd.AddParam(DocKind);
    cmd.AddParam(DocID);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      DealCode = DataSet.DealCode;
    end;

    return DealCode;
  END;

  /*Получили идентификатор ТО*/
  if((valtype(dlrqid) == V_INTEGER) and (dlrqid > 0))
    cmd = DL_RSDCommand();
    query =   "select rq.* "
            + "from ddlrq_dbt rq "
            + "join ddlrqacc_dbt rqacc on rqacc.t_ID = rq.t_RqAccID and "
//Только внешние платежи
            + "                  rqacc.t_bankid > 0 and "
            + "                  not exists(select 1 from ddp_dep_dbt dp_dep where dp_dep.t_partyid = rqacc.t_bankid) "
            + "where rq.t_id = ? and"
//Только обязательства
            + "      rq.t_Kind = " + DLRQ_KIND_COMMIT + " and "
//Только денежные
            + "      rq.t_SubKind = " + DLRQ_SUBKIND_CURRENCY + " and "
//не только выполненные          + "    rq.t_State = " + DLRQ_STATE_EXEC + " and "
            + "      rq.t_Netting = CHR(0) and "
//пустые ссылки не учитываем
            + "      not Exists(select 1 from drqpmlink_dbt rl where rl.t_RQID = rq.t_ID and nvl(rl.t_paymentid, 0) != 0) and "
            + "      not Exists(select 1 from ddlrq_upgr_dbt rqup where rqup.t_RQID = rq.t_ID)";
    cmd.AddParam(dlrqid);
    DataSet = cmd.Execute(query);
    if(DataSet.moveNext())
      CurRQ = TRecHandler("dlrq.dbt");
      DataSet.GetRecord().CopyTo(CurRQ.rec);
    end;
    /*Удалим пустую ссылку на платеж*/
    cmd = RSDCommand("delete from drqpmlink_dbt where t_rqid = ? and nvl(t_paymentid, 0) = 0");
    cmd.AddParam("rqid", RSDBP_IN, dlrqid);
    cmd.Execute();
  /*Получили рекорд*/
  elif((dlrq != null) and
       (dlrq.rec.SubKind == DLRQ_SUBKIND_CURRENCY) and
       (dlrq.rec.Netting != SET_CHAR))
    CurRQ = dlrq;
  /*ТО не найдено*/
  else
    CurRQ = null;
  end;

  if(CurRQ != null)

    DealCode = GetDealCode(CurRQ.rec.DocKind, CurRQ.rec.DocID);
    if(DealCode == "")
      errmsg = "Не определен номер сделки";
      //return -1;
    end;

    if((CurRQ.rec.DocKind == DL_SECURITYDOC) or 
       (CurRQ.rec.DocKind == DL_RETIREMENT) or
//       (CurRQ.rec.DocKind == DL_INVESTSHARE) or 
       (CurRQ.rec.DocKind == DL_SECUROWN) or
       (CurRQ.rec.DocKind == DL_RETIREMENT_OWN)) //Сделки и погашения
      CurFD = SPFirstDoc( IIF(CurRQ.rec.DealPart == 2, LEG_KIND_DL_TICK_BACK, LEG_KIND_DL_TICK), CurRQ.rec.DocID );
    elif(CurRQ.rec.DocKind == DL_SETTLEMENT) //Расчеты на бирже
      CurFD = SPFirstDocDLCOMM( CurRQ.rec.DocKind, CurRQ.rec.DocID );
    elif(CurRQ.rec.DocKind == DL_WRTMONEY) //Зачисление/списание ДС
      CurFD = DLFirstDocNPTXOP(CurRQ.rec.DocKind, CurRQ.rec.DocID);
    elif(CurRQ.rec.DocKind == DL_NTGSEC) //Неттинг
      CurFD = DL_NettingDoc(CurRQ.rec.DocKind, CurRQ.rec.DocID);
    else
      errmsg = "Документ вида "+CurRQ.rec.DocKind+" не обрабатывается";
      return -1;
    end;

    var pmrq  = DLRQ_PAYM(CurRQ, CurFD, true);
if (ValType(pmrq.PayerBankID) == v_Undef)
   return 0;
end;
    var pmObj = RsbPayment(0);

    pmObj.DocKind    = CurRQ.rec.DocKind;
    pmObj.DocumentID = CurRQ.rec.DocID;

    pmObj.Purpose    = pmrq.Purpose;
    pmObj.SubPurpose = pmrq.SubPurpose;

    pmObj.ValueDate  = 
    pmObj.Date       = pmrq.ValueDate;

    pmObj.OrderFIID   = pmrq.OrderFIID;
    pmObj.OrderAmount = pmrq.OrderAmount;

    pmObj.BaseFIID   = pmrq.BaseFIID;
    pmObj.BaseAmount = pmrq.BaseAmount;

    pmObj.PayerAmount = pmrq.PayAmount;
    pmObj.ReceiverAmount = pmrq.PayAmount;

    pmObj.Number     = DealCode;

/*
    if(IsSWIFT)
      var error;
      if(pmrq.PayerBankCodeKind != 6/*BIC ISO (SWIFT)*/)
        pmrq.m_rqacc_payer.rec.BankCode = ПолучитьКодСубъекта(pmrq.PayerBankID, 6, error, 0);
        if(error != 0)
          errmsg = "Не найден SWIFT-код банка плательщика";
          return -1;
        end;
        pmrq.m_rqacc_payer.rec.BankCodeKind = 6/*BIC ISO (SWIFT)*/;
      end;
      if(pmrq.ReceiverBankCodeKind != 6/*BIC ISO (SWIFT)*/)
        pmrq.m_rqacc_receiver.rec.BankCode = ПолучитьКодСубъекта(pmrq.ReceiverBankID, 6, error, 0);
        if(error != 0)
          errmsg = "Не найден SWIFT-код банка получателя";
          return -1;
        end;
        pmrq.m_rqacc_receiver.rec.BankCodeKind = 6/*BIC ISO (SWIFT)*/;
      end;
    end;
*/
    pmObj.SetPayerPI (PAYMENTS_GROUP_UNDEF,                      
                      pmrq.PayerBankID,                          
                      pmrq.PayerBankCodeKind,                    
                      pmrq.PayerBankCode,                        
                      pmrq.PayerBankName,                        
                      pmrq.PayerCorrAcc,                         
                      pmrq.PayerAccountRec.rec.Code_Currency,    
                      1,                                         
                      pmrq.PayerAccountRec().rec.Account,                         
                      pmrq.Payer,                                
                      pmrq.PayerName,                            
                      pmrq.PayerINN,                             
                      pmrq.PayerCodeKind,                        
                      pmrq.PayerCode,                            
                      pmrq.PayerCorSchem,                        
                      0);                                       

    pmObj.SetReceiverPI (PAYMENTS_GROUP_UNDEF, 
                         pmrq.ReceiverBankID,
                         pmrq.ReceiverBankCodeKind,
                         pmrq.ReceiverBankCode,
                         pmrq.ReceiverBankName,
                         pmrq.ReceiverCorrAcc,
                         pmrq.ReceiverAccountRec.rec.Code_Currency,
                         1,
                         pmrq.ReceiverAccountRec().rec.Account,
                         pmrq.Receiver,
                         pmrq.ReceiverName,
                         pmrq.ReceiverINN,
                         pmrq.ReceiverCodeKind,
                         pmrq.ReceiverCode,
                         pmrq.ReceiverCorSchem,
                         0);

    pmObj.Department  = pmrq.Department;
    pmObj.PaymStatus  = PM_READY_TO_SEND;//PM_READIED;//pmrq.PaymStatus; всегда "Готов к обработке"
    pmObj.Oper        = {oper};
    pmObj.Origin      = PAYMENT_OR_AUTO;
    pmObj.NumberPack  = 0;
    pmObj.Netting     = UNSET_CHAR;
    pmObj.Priority    = 5;

    pmObj.ShifrOper  = pmrq.ShifrOper; 

    pmObj.Ground = pmrq.Ground;
    if(pmrq.IsTick())
      SP_SetPaymentGround(pmObj.GetPM_PAYM(), -1, CurFD);
    end;

    pmObj.IsFactPaym  = "X";
    pmObj.PrimDocKind = DOC_BO_PAYMENT;

    /*Формирование форматов СВИФТ МТ103/202*/
    if((IsSWIFT) and ((pmrq.PayerAccountRec.rec.Code_Currency != 0) or (pmrq.ReceiverAccountRec.rec.Code_Currency != 0)))
      pmObj.OutTransport = 2/*SWIFT*/;
      pmObj.OutTpSchem = 0;
      pmObj.OutRlsForm = 0;
      var TpID = 0, TpSchemID = 0, RlsFormID = 0, FormID = 0, SubKindMes = 0;
      if(DefineRlsForm(pmObj, TpID, TpSchemID, FormID, RlsFormID, SubKindMes))
        pmObj.OutTransport = TpID;
        pmObj.OutTpSchem = TpSchemID;
        pmObj.OutRlsForm = RlsFormID;
      else 
//        errmsg = "Не удалось подобрать релиз для вида платежа";
//        return -1;
      end;
    end;

    if(not InStepOp)
      RslDefCon.BeginTrans();

      if(pmObj.Update())
        RslDefCon.RollbackTrans();
        errmsg = "Ошибка вставки платежа";
        return -1;
      end;

      if(PM_BOOperation(pmObj) == false)
        RslDefCon.RollbackTrans();
        errmsg = "Ошибка PM_BOOperation";
        return -1;
      end;
    end;

    if(CurRQ.rec.ID > 0)
      var ins_rqpmlink = RsbSQLInsert("rqpmlink.dbt");
      var RqPmLink = TRecHandler("rqpmlink.dbt");

      RqPmLink.Clear();

      RqPmLink.rec.RQID        = CurRQ.rec.ID;
      RqPmLink.rec.ServDocKind = CurRQ.rec.DocKind;
      RqPmLink.rec.ServDocID   = CurRQ.rec.DocID;
      if(not InStepOp)
        RqPmLink.rec.PaymentID = pmObj.PaymentID;
      end;
      
      ins_rqpmlink.AddRecord(RqPmLink);
      if(not ins_rqpmlink.Insert())
        RslDefCon.RollbackTrans();
        errmsg = "Ошибка при создании записей в drqpmlink_dbt";
        return -1;
      end;
    end;

    if(not InStepOp)
      RslDefCon.CommitTrans();
    end;

    return 1;
  else
    errmsg = "ТО для создания платежа не найдено (" + dlrqid + ")";
    return 0;
  end;

OnError(er)
   errmsg = "Исключение: " + er.module + " " + er.line + " " + er.message;
   if(not InStepOp)
     if(RslDefCon.IsInTrans)
       RslDefCon.RollbackTrans();
     end;
   end;
   return -99;
end;

/*
macro CreatingPaymTMP(CurRQ:trechandler, errmsg:@string)


  msgboxex(CurRQ.rec.DocKind+"|"+CurRQ.rec.DocID);

  var CurFD = SPFirstDocDLCOMM( CurRQ.rec.DocKind, CurRQ.rec.DocID );

  var pmrq  = DLRQ_PAYM(CurRQ, CurFD, true);

  msgboxex("="+pmrq.Purpose+"|="+pmrq.SubPurpose+"|="+pmrq.ValueDate+"|="+pmrq.OrderFIID+"|="+pmrq.OrderAmount+"|="+pmrq.BaseFIID+"|="+pmrq.BaseAmount+"|="+pmrq.PayAmount);
  msgboxex(pmrq.PayerBankID+"|"+pmrq.PayerBankCodeKind+"|"+pmrq.PayerBankCode+"|"+pmrq.PayerBankName+"|"+pmrq.PayerCorrAcc+"|"+pmrq.PayerAccountRec.rec.Code_Currency);
  msgboxex(pmrq.PayerAccountRec().rec.Account+"|"+pmrq.Payer+"|"+pmrq.PayerName+"|"+pmrq.PayerINN+"|"+pmrq.PayerCodeKind+"|"+pmrq.PayerCode+"|"+pmrq.PayerCorSchem);
  msgboxex(pmrq.ReceiverBankID+"|"+pmrq.ReceiverBankCodeKind+"|"+pmrq.ReceiverBankCode+"|"+pmrq.ReceiverBankName+"|"+pmrq.ReceiverCorrAcc+"|"+pmrq.ReceiverAccountRec.rec.Code_Currency);
  msgboxex(pmrq.ReceiverAccountRec().rec.Account+"|"+pmrq.Receiver+"|"+pmrq.ReceiverName+"|"+pmrq.ReceiverINN+"|"+pmrq.ReceiverCodeKind+"|"+pmrq.ReceiverCode+"|"+pmrq.ReceiverCorSchem);

  return 1;
OnError(er)
   errmsg = "Исключение: " + er.module + " " + er.line + " " + er.message;
   return -99;
end;
*/