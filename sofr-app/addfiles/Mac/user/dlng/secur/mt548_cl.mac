/*
Скроллинг MT548 для внебиржевых сделок
Дорохов А.Н.
*/
import RSD;
import captureoutput;

private macro Fill_MT548_TMP ()
    var sql, cmd, rs;
    cmd = RSDCommand;

    //Очистка вр таблицы
    cmd.CmdText = "Truncate table ddeal_confirm_tmp";
    cmd.execute;

    //заполнение данными
    StartCaptureOutput();
    [
    insert into ddeal_confirm_tmp (t_dealid, 
                                         t_dealcode, 
                                         t_dealcodets, 
                                         t_date) 
       select tick.t_dealid, 
              tick.t_dealcode,  
              tick.t_dealcodets, 
              tick.t_dealdate 
         from ddl_tick_dbt tick  
        where     tick.t_bofficekind = 101 
              and tick.t_marketofficeid = 0
              and tick.t_dealstatus != 20  
    ];
    cmd.CmdText = EndCaptureOutput();
    cmd.execute;

    StartCaptureOutput();
    [
        DECLARE
           retval        NUMBER;
           P_DRAFTKIND   NUMBER;
           P_DRAFTID     NUMBER;
           P_NUM         VARCHAR2 (1000);
           P_DATE        VARCHAR2 (1000);
           P_TYPEIN      VARCHAR2 (1000);
           P_STATUS      VARCHAR2 (1000);
           P_REASON      VARCHAR2 (1000);
           P_COMMENT     VARCHAR2 (1000);
        BEGIN
           FOR i IN (SELECT * FROM DDEAL_CONFIRM_TMP)
           LOOP
                retval :=
                     RSP_Security.GETCONFIRMPARAMS (8,
                                                      i.t_dealid,
                                                      P_NUM,
                                                      P_DATE,
                                                      P_TYPEIN,
                                                      P_STATUS,
                                                      P_REASON,
                                                      P_COMMENT);
                update DDEAL_CONFIRM_TMP tmp set  tmp.t_confirm_num      = P_NUM,
                                                  tmp.t_confirm_date     = case to_date (P_DATE , 'dd.mm.yyyy hh24:mi:ss' ) when to_date('01.01.0001','dd.mm.yyyy') then null else to_date (P_DATE , 'dd.mm.yyyy hh24:mi:ss' ) end,
                                                  tmp.t_confirm_typein   = P_TYPEIN,
                                                  tmp.t_confirm_status   = P_STATUS,
                                                  tmp.t_confirm_reason   = P_REASON,
                                                  tmp.t_confirm_comment  = P_COMMENT
                                 where t_dealid = i.t_dealid;                                                                           
                                                      
           END LOOP;
        END;
    ];
    cmd.CmdText = EndCaptureOutput();
    cmd.execute;

end;


class RslScroll

  const K_Enter = 13;
  const K_F2    = 316;
  const K_F3    = 317;
  const K_F8    = 322;
  const K_F9    = 323;
  const K_Alt_F3= 362;

  private var colInfo;

  private macro numCol
     if (colInfo)
        return colInfo.size / 6
     end
  end;    

  macro AddCol (ind, fld, head, width, fldType,decPoint)

     if (not colInfo)
        colInfo = TArray;
     end;

     colInfo.value (ind * 6)     = fld;
     colInfo.value (ind * 6 + 1) = head;
     colInfo.value (ind * 6 + 2) = width;
     colInfo.value (ind * 6 + 3 ) = fldType; 
     colInfo.value (ind * 6 + 4 ) = decPoint;
     colInfo.value (ind * 6 + 5 ) = 0;   // reserv
  end;

  macro EvProc (rs, cmd, id, key)

  end;


  macro Run (rs,name,Head, StLn, rdOnly)
     return RunScroll (rs, numCol, colInfo,name, R2M(this,"EvProc"), Head, StLn, rdOnly);
  end;

end;

//Вывод сообщения
class (RslScroll) Scroll_548

  InitRslScroll;  
  macro EvProc (rs, cmd, id, key)
  end;

  AddCol ( 0, "STR_548", "Текст сообщения", null, true);
  
end;

//Скроллинг всех подтверждений
class (RslScroll) All_548_Scroll

  InitRslScroll;  
  macro EvProc (rs, cmd, id, key)
       var cmd548,rs548;  
 
    if (cmd == DLG_KEY)
        if  (key == K_ENTER) 
        StartCaptureOutput;
           [    select to_char(ltrim(substr(txt,
                                                    instr(txt, chr(10), 1, level) + 1,
                                                    instr(txt, chr(10), 1, level + 1) - instr(txt, chr(10), 1, level) - 1))) str_548
                          from (select chr(10)||str||chr(10) txt, typ
                                  from (select str, type typ
                                          from mtrec
                                          where id_paym = :IDPaym))
                        connect by level <= length(txt) - length(replace(txt, chr(10))) - 1];
                        cmd548 = RsdCommand(EndCaptureOutput());
              cmd548.AddParam("IDPaym", RSDBP_IN, rs.value("PAYM_SEC"));
              rs548 = RSDRecordset(cmd548, rsdval_client, rsdval_static); 
              Scroll_548.run(rs548, null, "Содержимое сообщения", "ESC - Выход"); 
              return CM_IGNORE;         

        end;
      return CM_DEFAULT;
    end;    


  end;

  AddCol ( 0, "NumReferdoc", "Номер сообщения", null, true);
  AddCol ( 1, "Confirm_date", "Дата/время сообщения",    null, true);
  AddCol ( 2, "Confirm_state", "Статус ",    null, true);
  AddCol ( 3, "Confirm_reason", "Расшифровка статуса",    null, true);

end;


// Производный класс задаёт колонки и переопределяет процедуру обработки сообщений

class (RslScroll) MainScroll

  InitRslScroll;  

  macro EvProc (rs, cmd, id, key)
    var cmd_548_all,rs_548_all;  
    var cmd_548,rs_548;  

    if (cmd == DLG_KEY)
        if  (key == K_ENTER) 
           if (rs.value("t_confirm_num")!="")
              StartCaptureOutput;
              [    SELECT TO_CHAR (LTRIM (SUBSTR (txt, INSTR (txt, CHR (10), 1, LEVEL) + 1,   INSTR (txt, CHR (10), 1, LEVEL + 1) - INSTR (txt, CHR (10), 1, LEVEL) - 1))) str_548
                      FROM (SELECT CHR (10) || str || CHR (10) txt, typ
                              FROM (SELECT str, TYPE typ
                                      FROM mtrec
                                     WHERE id_paym =
                                              (  SELECT NVL (MAX (pp.id_paym), MAX (a.id_paym))
                                                   FROM pckpay pp,
                                                        ipspck mp,
                                                        payass a,
                                                        mtrec m,
                                                        payass a1,
                                                        mtn9x mt
                                                  WHERE     pp.id_ipspck(+) = mp.id_ipspck
                                                        AND mp.id_paym(+) = a.id_paym
                                                        AND a1.id_paym = m.id_paym(+)
                                                        AND a1.id_paym = mt.id_paym(+)
                                                        AND a.associate = TO_CHAR (:dealid)
                                                        AND a.bankid =
                                                               TO_CHAR ( NVL ( 8, DECODE (m.TYPE, 518, 3, 599, 4, 8)))
                                                        AND a1.id_paym = pp.id_paym
                                                        AND a1.id_sass =RSP_COMMON.GET_ID_BY_CODE_DICT ( P_TABLE   => 'SASS', P_CODE    => 1) AND m.TYPE || mt.TYPE = '548' 
                                               GROUP BY m.TYPE || mt.TYPE)))
                CONNECT BY LEVEL <= LENGTH (txt) - LENGTH (REPLACE (txt, CHR (10))) - 1];
              cmd_548 = RsdCommand(EndCaptureOutput());
              cmd_548.AddParam("DealID", RSDBP_IN, rs.value("t_dealid"));
              rs_548 = RSDRecordset(cmd_548, rsdval_client, rsdval_static);
              Scroll_548.run(rs_548, null, "Содержимое сообщения", "ESC - Выход");
              return CM_IGNORE;
           else
            msgbox("Нет сквитованных МТ548");
            return CM_IGNORE;
           end;
           return CM_IGNORE;

        elif (key == 18)//  CTRL+R
             return cm_select;
                
        elif (key == 291)//  ALT+H
           if (rs.value("t_confirm_num")!="")
            StartCaptureOutput;
            [
              SELECT ass.*,
                     PCK_MAIN.ID_IPSPCK,
                     Pay_MAIN.ID_PAYM paym_sec,
                     REC.TYPE, p.numreferdoc,
                     (SELECT TO_CHAR (
                                TO_DATE (SUBSTR (str, 13, 14), 'yyyy.mm.dd hh24:mi:ss'),
                                'dd.mm.yyyy hh24:mi:ss')
                        FROM (    SELECT TO_CHAR (LTRIM (SUBSTR (txt, INSTR (txt,CHR (10),1,LEVEL)+ 1,   INSTR (txt,CHR (10),1,LEVEL + 1)- INSTR (txt,CHR (10),1,LEVEL)- 1)))str,
                                         TO_CHAR (REPLACE (SUBSTR (SUBSTR (txt,INSTR (txt, ':70D:'),INSTR (SUBSTR (txt, INSTR (txt, ':70D:')),CHR (10) || ':')),13),CHR (10)))f70D,
                                         typ
                                    FROM (SELECT CHR (10) || str || CHR (10) txt, typ
                                            FROM (SELECT str, TYPE typ
                                                    FROM mtrec
                                                   WHERE id_paym = Pay_MAIN.ID_PAYM))
                              CONNECT BY LEVEL <= LENGTH (txt) - LENGTH (REPLACE (txt, CHR (10)))- 1)
                       WHERE str LIKE (':98_::PREP//%'))
                        confirm_date,
                     (SELECT SUBSTR (str, 7, 10)
                        FROM (    SELECT TO_CHAR (LTRIM (SUBSTR (txt, INSTR (txt,CHR (10),1,LEVEL)+ 1,   INSTR (txt,CHR (10),1,LEVEL + 1)- INSTR (txt,CHR (10),1,LEVEL)- 1)))str,
                                         TO_CHAR (REPLACE (SUBSTR (SUBSTR (txt,INSTR (txt, ':70D:'),INSTR (SUBSTR (txt, INSTR (txt, ':70D:')),CHR (10) || ':')),13),CHR (10)))f70D,
                                         typ
                                    FROM (SELECT CHR (10) || str || CHR (10) txt, typ FROM (SELECT str, TYPE typ
                                                    FROM mtrec
                                                   WHERE id_paym = Pay_MAIN.ID_PAYM))
                              CONNECT BY LEVEL <= LENGTH (txt) - LENGTH (REPLACE (txt, CHR (10)))- 1)
                       WHERE str LIKE (':25D::%') AND ROWNUM = 1)
                        confirm_state,
                     (SELECT SUBSTR (str, 7, 10)
                        FROM (    SELECT TO_CHAR (LTRIM (SUBSTR (txt, INSTR (txt,CHR (10),1,LEVEL)+ 1,   INSTR (txt,CHR (10),1,LEVEL + 1)- INSTR (txt,CHR (10),1,LEVEL)- 1)))str,
                                         TO_CHAR (REPLACE (SUBSTR (SUBSTR (txt,INSTR (txt, ':70D:'),INSTR (SUBSTR (txt, INSTR (txt, ':70D:')),CHR (10) || ':')),13),CHR (10)))f70D,
                                         typ
                                    FROM (SELECT CHR (10) || str || CHR (10) txt, typ
                                            FROM (SELECT str, TYPE typ
                                                    FROM mtrec
                                                   WHERE id_paym = Pay_MAIN.ID_PAYM))
                              CONNECT BY LEVEL <= LENGTH (txt) - LENGTH (REPLACE (txt, CHR (10))) - 1)
                       WHERE str LIKE (':24B::%') AND ROWNUM = 1)
                        confirm_reason
                FROM payass ass,
                     ipspck pck_main,
                     pckpay pay_main,
                     mtrec rec,  paym p
               WHERE associate = TO_CHAR (:Dealid) AND ass.bankid = 8
                     AND ass.id_sass =RSP_COMMON.GET_ID_BY_CODE_DICT (P_TABLE => 'SASS', P_CODE => 3)
                     AND PCK_MAIN.ID_PAYM = ASS.ID_PAYM
                     AND PAY_MAIN.ID_ipspck = PCK_MAIN.ID_IPSPCK
                     AND REC.ID_PAYM = pay_main.id_paym
                     AND REC.TYPE = '548'   and P.ID_PAYM =  Pay_MAIN.ID_PAYM            
            ]; 

            cmd_548_all = RsdCommand(EndCaptureOutput());
            cmd_548_all.AddParam("dealid", RSDBP_IN, rs.value("t_dealid"));
            rs_548_all = RSDRecordset(cmd_548_all,rsdval_client, rsdval_static);

            All_548_Scroll.run(rs_548_ALL, null, "История статусов","Enter - Просмотр, ESC - Выход");
            return CM_IGNORE;
           
           else
            msgbox("Нет сквитованных МТ548");
            return CM_IGNORE;
           end; 
        end;
    end;
    return CM_DEFAULT;

  end;

  AddCol ( 0, "t_dealid",         "ID сделки",               8, true);
  AddCol ( 1, "t_dealcode",       "Код сделки (внутренний)", 11, true);
  AddCol ( 2, "t_dealcodeTS",     "Код сделки (внешний)",    11, true);
  AddCol ( 3, "t_date",           "Дата  сделки",            10, true);
  AddCol ( 4, "t_confirm_num",    "Номер сообщения",     17, true);
  AddCol ( 5, "confirm_date",     "Дата Время" ,             16, true);
  AddCol ( 6, "t_confirm_status", "Статус",                  10, true);
  AddCol ( 7, "t_confirm_reason", "Причина",                 10, true);
  AddCol ( 8, "t_confirm_comment","Комментарий",             50, true);

end;

    var rsd;

    Fill_MT548_TMP;

    StartCaptureOutput;
     [
        select  T_AUTOINC,    
                T_DEALID , 
                T_DEALCODE, 
                T_DEALCODETS, 
                T_DATE , 
                T_CONFIRM_NUM , 
                to_char(T_CONFIRM_DATE,  'yyyy.mm.dd hh24:mi:ss' ) confirm_date, 
                T_CONFIRM_TYPEIN, 
                T_CONFIRM_STATUS , 
                T_CONFIRM_REASON , 
                T_CONFIRM_COMMENT 
        from ddeal_confirm_tmp
     ];

var m_sql = EndCaptureOutput();
rsd = RSDRecordset(m_sql, rsdval_client, rsdval_static);   
while(MainScroll.Run (rsd, null,"Статусы внебиржевых сделок (по МТ548)","Enter Просмотр  ALT+H История  CTRL+R Обновить  ESC Выход"))
  Fill_MT548_TMP ();
  rsd = RSDRecordset(m_sql, rsdval_client, rsdval_static);   
end;
exit(1);

