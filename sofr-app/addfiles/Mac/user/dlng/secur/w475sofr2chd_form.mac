/**                                                                                                             
 @file w475sofr2chd_form.mac                                                                                          
 @brief Обработка формы Буферная таблица "Отчет о результатах БО"   
 #menu 
  - БО ЦБ\Сервисные операции\Буферная таблица "Отчет о результатах БО"                                                                                                                                                                                                                        
                                                                                                                                                                                                             
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.04.04 |Шестаков Д.В.  |BIQ-15436           |Создание макроса для обработки формы                                                                                                                                                                                                                                                                                           
*/ 

import "DlRepForm_h.mac", "globals.mac", rsd, dlquery;
import DatesUtility;


PRIVATE CONST PNFLD_W475SOFR_DTBEGIN = 0;
PRIVATE CONST PNFLD_W475SOFR_DTEND = 1;
VAR m_datesUtility = TDatesUtility;

/**
   @brief Получить последний день месяца
   @param[in] CurDate  Дата 
   @return Последний день месяца
  */ 
 macro GetLastDayInMonth( CurDate:Date )
    var day, mon, year;

    DateSplit(CurDate,day,mon,year);
    if( mon == 12 )
      return Date(1,1,year+1) - 1;
    else
      return Date(1,mon+1,year) - 1;
    end;
end;

/**
 @brief Начальная дата выпуска отчета
*/ 
private MACRO DL_FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
      FldRealValue = FldShowValue;
      pThis.SetLinkedValue( DL_PNFLD_ENDDATE, getLastDayInMonth(date(FldRealValue)));
     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
        if( pThis.GetLinkedValue(DL_PNFLD_ENDDATE) < FldRealValue )
           pThis.SetLinkedValue( DL_PNFLD_ENDDATE, FldRealValue );
        end;
     end;
     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
       if( FldRealValue > pThis.GetLinkedValue(DL_PNFLD_ENDDATE) )
         MsgBox( "Дата окончания не может быть меньше даты начала периода.");
         return CM_IGNORE;
       end;
     end; 
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;



/**
 @brief Конечная дата выпуска отчета
*/ 
private MACRO DL_FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;
     pThis.SetLinkedValue( DL_PNFLD_BEGINDATE, m_datesUtility.getFirstDayInMonth(date(FldRealValue)));
     if( pThis.ExistField(DL_PNFLD_BEGINDATE) )
        if( FldRealValue < pThis.GetLinkedValue(DL_PNFLD_BEGINDATE) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;


/**
 @brief Класс формы 
*/                    
CLASS (DL_CPanel) BufferTable_DBO_Panel()

 // var m_datesUtility = TDatesUtility;
  var m_dtBegin:Date = m_datesUtility.getFirstDayInMonth(Date()); //начальная дата
  var m_dtEnd:Date   = getLastDayInMonth(Date()); //конечная дата 
 
  /**
   @brief Инициализация параметров формы, назначения обработчиков полей
  */                    
  PRIVATE MACRO Init()
    AddFieldProc(0, PNFLD_W475SOFR_DTBEGIN, DL_PNFLD_BEGINDATE, @DL_FldProc_BeginDate, m_dtBegin);
    AddFieldProc(0, PNFLD_W475SOFR_DTEND,   DL_PNFLD_ENDDATE,   @DL_FldProc_EndDate, m_dtEnd);
  END;

  /**
   @brief Проверка параметров формы
   @return true, если проверка пройдена успешно
   @return false, если в процессе проверки были обнаружены ошибки
  */                    
  PRIVATE MACRO Check():BOOL
    return true;
  END;

  /**
   @brief Запуск формы с последующей записью полученных параметров
   @return true, если форма отработала успешно и была подтверждена по F2
   @return false, если в процессе работы формы возникли ошибки или из нее вышли по Esc
  */                    
  MACRO Run()
    var stat = Run();

    m_dtBegin = GetFieldValue(PNFLD_W475SOFR_DTBEGIN);
    m_dtEnd   = GetFieldValue(PNFLD_W475SOFR_DTEND);

    return stat;
  END;

  initDL_CPanel("sp_rep.lbr", "p_w475f");
END;