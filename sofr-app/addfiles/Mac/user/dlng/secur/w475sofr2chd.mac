/**                                                                                                             
 @file w475sofr2chd.mac                                                                                          
 @brief Заполнение буферной таблицы dkl11sofr2dwh_dbt результатами отчета по "Результаты брокерского обслуживания для ДРРК"
 #menu 
  - БО ЦБ\Сервисные операции\Буферная таблица "Отчет о результатах БО"                                                                                                                                                                                                                        
                                                                                                                                                                                                             
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.04.04 |Шестаков Д.В.  |BIQ-15436           |Создание макроса для отчета                                                                                                                                                                                                                                                                                             
*/ 
 
import SFInter , globals, BankInter, RSD, ReportUser;
import w475sofr2chd_form,"dlmisc.mac",DatesUtility; 


/**
   @brief Создание файла логов 
   @param[in] message_  Текст ошибки
  */   
 macro CreateLogFile(message_:string) 
   
   var m_dtString =StrSubst(StrSubst(string(Date():f)+ "_" + Time(),".", ""),":", "");
   var m_FileName = string("..\\TxtFile\\DWH\\w475sofr2chd_",m_dtString, ".txt");

   if(ExistDir("..\\TxtFile\\DWH\\") == 2)
      MakeDir("..\\TxtFile\\DWH\\");
   end;     

   File protocol_file() txt WRITE;
   var FC;

   if( Open(protocol_file, m_FileName) )
      SetOutPut(m_FileName);
      println(message_);
      SetOutput (NULL,TRUE);
      Close( m_FileName );
   end; 
 end;


/**
   @brief Отправка уведомлений в телеграм и на почту СОФР мониторинг 
   @param[in] message_  Текст ошибки
  */ 
 macro CreateLogEvent(message_:string) 
   
   var sql:String, 
       cmd;

   sql =  "begin  " +  
          "    RSB_W475SOFR2CHD.log_register_event(p_status => 1 "           +  
          "                                       ,p_message => :err_msg); " +                       
          "end;"; 

   cmd = RsdCommand(sql);
   cmd.addParam("err_msg" , RSDBP_IN, message_ );
   cmd.execute();

 end;


/**
  @brief Рассчет значений для заполнения буферной таблицы dkl11sofr2dwh_dbt
  @param[in] dtBegin  Дата начала 
  @param[in] dtEnd Дата окончания
  @param[out] errmsg  Текст ошибки
  @return 0 - успешно, 1 - с ошибками 
*/
 MACRO MainCalc_Run(dtBegin:date,dtEnd:date, errmsg:@string) 
  
  PRIVATE CONST BROKERREP_DL_SETTLOPER = 2030; //Операция РОВУ
           
   var sql:string,
       cmd,
       result:Integer, 
       err, 
       msgSucces:string,
       auto_mode:bool;

   var curTask:Integer  = 0; //Текущая выполняемая задача
   var tasks:Integer = 23; //Всего задач процедур

   result  = 0;


   if((ValType(dtBegin) == V_UNDEF) or (ValType(dtEnd) == V_UNDEF))
      dtBegin = m_datesUtility.getFirstDayInMonth(DL_AddMonths(Date(), -1));
      dtEnd =  getLastDayInMonth(DL_AddMonths(Date(), -1)); 
   end;


  //инициалиация переменных
   sql = " declare "  +
        "  	begin   "     +
        "      RSB_W475SOFR2CHD.initial_param(p_dt_begin                =>  :date_begin"  +
        "                                    ,p_dt_end                  =>  :date_end"    +
        "                       	            ,p_BROKERREP_DL_SETTLOPER  =>  :broker"      +
        "                       	            ,p_FIKIND_CURRENCY         =>  :fikind"      +
        "                       	            ,p_DL_CALCOPER             =>  :calcoper"    +
        "                             	      ,p_ourbank                 =>  :ourbank); "  +
	     "    end;";

      cmd = RsdCommand(sql);
      cmd.addParam("date_begin" , RSDBP_IN, dtBegin);
      cmd.addParam("date_end"   , RSDBP_IN, dtEnd);
      cmd.addParam("broker"     , RSDBP_IN, BROKERREP_DL_SETTLOPER);
      cmd.addParam("fikind"     , RSDBP_IN, FIKIND_CURRENCY );
      cmd.addParam("calcoper"   , RSDBP_IN, DL_CALCOPER);
      cmd.addParam("ourbank"    , RSDBP_IN, {ourbank} );
      cmd.execute();

   if(not IsShedulerRunning())
      InitProgress(tasks, "Буферная таблица", "Заполнение буферной таблицы...");
   end;

   while (curTask < tasks)
      curTask = curTask + 1;

   if(not IsShedulerRunning())
         UseProgress(curTask);
      end;   
  
      sql = " declare "         +
        "  l_err_msg varchar2(32000);"   +
        "  l_result  number;"            + 
        "  	begin   "                  + 
        "  	    RSB_W475SOFR2CHD.load_bufferTable_" + curTask  + "(p_out_err_msg =>  l_err_msg , "   +
        "                             		                    p_out_result  =>  l_result); "         +
        "   ?:= substr(l_err_msg,0,73); "  +
        "   ?:= l_result; "   +
	      "    end;";

    	cmd = RsdCommand(sql);
    	cmd.addParam("l_err_msg", RSDBP_OUT, V_STRING);
    	cmd.addParam("l_result",  RSDBP_OUT, V_INTEGER);
	 
    	cmd.execute();
    	err    = cmd.value("l_err_msg");
    	result = cmd.value("l_result"); 
        //в случае ошибки
       if(result == 1)
         if(not IsShedulerRunning())
            RemProgress();
         end;

         errmsg = "Процедура w475sofr2chd.mac: ошибка заполнения буферной таблицы dkl11sofr2dwh_dbt результатами БО для ДРРК. \n" + err;
         CreateLogFile(errmsg);
         break;
       end; 

       //в случае прохождения всех храниых процедур
       if((curTask == tasks) and (result == 0))
          msgSucces = "Успешное заполнение буферной таблицы dkl11sofr2dwh_dbt результатами БО для ДРРК";
          if(not IsShedulerRunning())
            RemProgress();
          end; 
          msgbox(msgSucces);
          CreateLogFile(msgSucces);
      end;
   end;

  onError(erru)
    RemProgress();
    errmsg = "onError: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
    CreateLogFile(errmsg);
    //CreateLogEvent(errmsg);
    msgbox(errmsg);
    return 1;
 end;   
