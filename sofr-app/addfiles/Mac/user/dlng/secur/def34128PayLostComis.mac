import globals, InsCarryDoc;
import rsd;
import DealsInter, spserv, sp_class, sp_car, spreserv;
import "def34128common.mac"; 

//начисление и оплата комиссий, ранее рассчитанных и сохраненных в таблице DDLCOMIS_LOST_34128_DBT
//также проводится документ внутреннего учета
class def34128PayLostComisClass()

  var debug = false; //признак отладки

  var trn_temp;  //может понадобиться для отката
  var InitDocName = "Документ основание для выполнения компенсации"; //документ - основание проводок

  var acc47423str = ""; var acc70601str = ""; 
  var prevPartyId = 0;
  var prevDay = date(1,1,1970);  //не менять - внизу проверка на (1,1,1970)
  var prevContrid = 0;
  var paySumByDay = 0;
  var prevGuid = "";
  var FD;
  var prevIIS;
  var prevDealId;
  var trn1, trn2, trn3;
  trn1 = trn2 = trn3 = 0;
  var str_str = "";
  var dt:date;
  var contract_number;

  // простая проводка 
  macro trn_predefined_accounts_natcur(sum, acc_pay, acc_rec, grnd, chapter)
		var resultCarry;
		var tr = RsbAccTransaction();
		tr.Chapter         	= chapter;
		tr.Date_Carry		= {curdate};
		tr.Oper		= {oper};
		tr.Numb_Document   	= "";
		tr.ResultCarry     = 1;
		//tr.Kind_Oper       = "";
		tr.Ground          = grnd;
		tr.Department      = {OperDprt};
		tr.FIIDPayer       = 0;
		tr.FIIDReceiver    = 0;
		tr.SumPayer        = sum;
		tr.SumReceiver     = sum;
		tr.AccountPayer    = acc_pay;
		tr.AccountReceiver = acc_rec;
		//tr.Number_Pack     = "55";
		//tr.UserField3      = "1";
		resultCarry = tr.Carry();
		trn_temp = getLastTrnId(acc_pay, acc_rec);  //запомнить для сохранения в лог
		return resultCarry;
  end;

  // оплата комиссии за день - обе проводки в национальной валюте
  macro trnByClientByDay()
    record acc47423_( account ); ClearRecord( acc47423_); 
    record acc30601( account );  ClearRecord( acc30601 ); 
    record acc27810( account );  ClearRecord( acc27810 ); 
    record acc17810( account );  ClearRecord( acc17810 ); 
    // FD определяется в trnByDeal, который всегда перед trnByClientByDay
    FD.OpenAccount( "+РасчетыКомисс1", NULL, NULL, Acc47423_, {curdate}, 0 );
    FD.OpenAccount( "ДС клиента, ц/б", NULL, NULL, acc30601, {curdate}, 0 ); 
    var rest30601 = GetAbsAccRest(acc30601.account, {curdate}, 1, 0);
    if (rest30601==0)
      o("По счету "+acc30601.account+" остаток равен 0, проводок по оплате не будет" );
      return;
    end;
    if (rest30601<PaySumByDay)  //не превышаем 30601
      o("По счету "+acc30601.account+" сумма проводки равна остатку на 30601 "+ rest30601 +" вместо большего размера комиссии за день "+PaySumByDay);
      PaySumByDay =  rest30601;
    end;
    ПроводкаПоКатегориямУчета( FD,   
                                       Acc30601, 
                                       Acc47423_,
                                       {curdate},
                                       1,
                                       0,
                                       paySumByDay,  
                                       null,
                                       INPCARRY,
                                       "",
                                       "",
                                       "Оплата комиссии в рамках Соглашения об оказании брокерских услуг № "+contract_number+" от "+dt,
                                       0,
                                       null, 
                                       null,
                                       0, 0,
                                       null, null, NULL, null,
                                       true,
                                       null
                                     );
    trn_temp = getLastTrnId(Acc30601.account, Acc47423_.account);  //запомнить для сохранения
    if (str_str!="") 
      str_str = str_str + "," + string(trn_temp); 
    else 
      str_str = string(trn_temp);
    end;
    o("Проводка "+Acc30601.account+" "+Acc47423_.account+" "+PaySumByDay + " по сделке " + prevDealId); 
    if (prevIIS=="X")  //по договорам ИИС проводок внутреннего учета не должно быть
      o("Счет ИИС - проводок по ВУ не будет");
      return;
    end;
    FD.OpenAccount( "ДС, Расч. с клиентом, ВУ", NULL, NULL, acc27810, {curdate}, 0 );  
    FD.OpenAccount( "ДС Клиента, ВУ", NULL, NULL, acc17810, {curdate}, 0 ); 
    userПроводкаПоКатегориямУчета( FD,   
                                       Acc27810,
                                       Acc17810, 
                                       {curdate},
                                       21,
                                       0,
                                       paySumByDay,  
                                       null,
                                       INPCARRY,
                                       "",
                                       "",
                                       "Оплата комиссии в рамках Соглашения об оказании брокерских услуг № "+contract_number+" от "+dt,
                                       0,
                                       null, 
                                       null,
                                       0, 0,
                                       null, null, NULL, null,
                                       true,
                                       null
                                     );
    trn_temp = getLastTrnId(Acc27810.account,Acc17810.account);  //запомнить для сохранения
    if (str_str!="") 
      str_str = str_str + "," + string(trn_temp); 
    else 
      str_str = string(trn_temp);
    end;
    o("Проводка "+acc27810.account+" "+acc17810.account+" "+PaySumByDay + " по сделке " + prevDealId); 
  end;

  macro trnByDeal(rs)
    record Acc47423( account ); 
    ClearRecord( Acc47423 );
    record Acc70601( account ); 
    ClearRecord( Acc70601 );
    var PaySum = rs.value("t_summa"); 
    FD = DL_SubContrFD(rs.value("T_CONTRID"));  // не удалось использовать dv_car по примеру dvfx016.mac и dvfx060.mac. Видимо, потому что сканируем субдоговора
    if (acc47423str == "")
      FD.OpenAccount( "+РасчетыКомисс1", NULL, NULL, Acc47423, {curdate}, 0 );
      acc47423str =  Acc47423.account;
    else
      Acc47423.account = acc47423str;
    end;
    if (acc70601str == "")
      FD.OpenAccount( "+КВ, ц/б", NULL, NULL, Acc70601, {curdate}, 0 );
      acc70601str = Acc70601.account;
    else
      Acc70601.account = acc70601str;
    end;
    var ConvSum2Param = makeArray(SQLParam("SumB", PaySum),
                                    SQLParam("pFromFI", rs.value("t_pricefiid")),
                                    SQLParam("pToFI", NATCUR),
                                    SQLParam("pbdate", {curdate}),
                                    SQLParam("pRound", 0),
                                    SQLParam("pRateType", 0, RSDBP_IN_OUT),
                                    SQLParam("pRate", V_DOUBLE, RSDBP_OUT),
                                    SQLParam("pScale", V_DOUBLE, RSDBP_OUT),
                                    SQLParam("pPoint", V_DOUBLE, RSDBP_OUT),
                                    SQLParam("pIsInverse", V_STRING, RSDBP_OUT));

    paySumByDay = paySumByDay +  execStoredFunc("RSI_RSB_FIInstr.ConvSum2", V_DOUBLE, ConvSum2Param);
    // начисление комиссии  // todo проводка в валюте должна быть
    ПроводкаПоКатегориямУчета( FD, 
                                       Acc47423, 
                                       Acc70601,
                                       {curdate},
                                       1,
                                       0,
                                       PaySum,  
                                       null,
                                       INPCARRY,
                                       "",
                                       "",
                                       "Начисление комиссии по сделке № "+rs.value("t_extcode")+" в рамках Соглашения об оказании брокерских услуг № "+strsubst(rs.value("t_number"),"_v","")+" от "+dt,
                                       0,
                                       null, 
                                       null,
                                       0, 0,
                                       null, null, NULL, null,
                                       true,
                                       null
                                     );
    trn_temp = getLastTrnId(Acc47423.account, Acc70601.account);  //запомнить для сохранения
    if (str_str!="") 
      str_str = str_str + "," + string(trn_temp); 
    else 
      str_str = string(trn_temp);
    end;
    o("Проводка "+Acc47423.account+" "+Acc70601.account+" "+PaySum + " по сделке " + rs.value("t_dealid")); 
  end;

  macro updateStr()
    var query = "update DDLCOMIS_LOST_34128_DBT lo set t_str = ? where t_guid = ? ";
    var cmd = RSDCommand(query);
    cmd.AddParam("", RSDBP_IN, str_str);
    cmd.AddParam("", RSDBP_IN, prevGuid);
    cmd.execute;
  end;

  macro pay_Comis()
    var query =
      "SELECT lo.*, dp.t_notresident, sfc_s.t_number, sfc_s.t_datebegin, dlc.t_iis, deal.t_date, dli.t_pricefiid, deal.t_extcode "+
      "  from DDLCOMIS_LOST_34128_DBT lo,                       "+
      "       dparty_dbt              dp,                       "+
      "       dsfcontr_dbt            sfc_s,                    "+
      "       ddlcontr_dbt            dlc,                      "+
      "       ddlcontrmp_dbt          mp,                       "+
      "       ddvndeal_dbt            deal,                     "+
      "       ddvnfi_Dbt              dli                       "+
      " where lo.t_dealid <> 0                                  "+
      "   and dp.t_partyid = lo.t_payerid                       "+
      "   and sfc_s.t_id = lo.t_contrid                         "+
      "   and mp.t_dlcontrid = dlc.t_dlcontrid                  "+
      "   and sfc_s.t_id = mp.t_sfcontrid                       "+
      "   and deal.t_id = lo.t_dealid                           "+
      "   and dli.t_Dealid = deal.t_Id                          "+  
      "   and nvl(length(trim(lo.t_str)),1) = 1                 "+ 
//"   and dp.t_partyid in (161653,160525) " + //отладка
      " order by dp.t_partyid, deal.t_date                      ";
    if (debug == true)
      query = query + "  fetch first 10 rows only ";  //отладка                    
    end;
    var cmd = RSDCommand(query);
    var rs = RsdRecordSet(cmd);
    start_o; //начало лога
    while (rs.movenext)
      dt = rs.value("t_datebegin");
      contract_number = strsubst(rs.value("t_number"),"_v","");
      if ( (prevDay != date(1,1,1970)) and  // не первый цикл
           ( ( (rs.value("t_date")!=prevDay) and (rs.value("t_payerid")==prevPartyId) ) or  // клиент тот же, но дата сменилась (страховка на случай если клиент поменялся, а дата сделки у другого клиента та же)
             ( rs.value("t_payerid")!=prevPartyId ) // клиент сменился
           ) 
         )
        trnByClientByDay(); //оплата комиссий за день
        updateStr();
        paySumByDay = 0;  
        acc47423str = acc70601str = "";
      end;
      str_str = "";  
      trnByDeal(rs);
      prevDay = rs.value("t_date");
      prevPartyId = rs.value("t_payerid");
      prevContrId = rs.value("t_contrid");
      prevIis = rs.value("t_iis");
      prevDealId = rs.value("t_dealid");
      prevGuid = rs.value("t_guid");
      updateStr();
    end;
    if (prevContrId>0)
      trnByClientByDay();  //после последнего цикла 
      updateStr();
    end; 
  end;

end;

macro def34128PayLostComisRun()
  var trnObject = def34128PayLostComisClass;
  trnObject.pay_comis;
onError(er)
  MsgBox("Ошибка при Оплате комиссий " + er.Message);
end;
