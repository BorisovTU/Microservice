/** 
 @file Refill_Class.mac
 @brief Классы для операций подкрепления счетов клиентов в НКЦ 
  
 # tag 
 - functional_block:Клиенты_Зачисление
 - code_type:API

   
 # changeLog 
 |date       |author         |tasks            |note                            
 |-----------|---------------|-----------------|--------------------------------
 |13.05.2024 |Гераськина Т.В.|BOSS-1702        | Создание

*/ 
import rsbformsinter, DealsInter, cb_sql;
import Refill_CreatePayment;
import ws_msg_transform_sec;

CONST REG_AUTOREFILL_ACTIVE = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\АВТОПОДКРЕПЛЕНИЕ РК В НКЦ";

CONST
   C_REFILL_TYPE_MANUAL = 0, // Создана вручную из скроллинга
   C_REFILL_TYPE_AUTO   = 1; // Создана автоматически по операции зачисления в плановой процедуре

CONST
   C_REFILL_ACTION_CREATED  = 0, /*просто создана*/
   C_REFILL_ACTION_OPENED   = 1, /*открыта*/
   C_REFILL_ACTION_CLOSED   = 2, /*закрыта*/
   C_REFILL_ACTION_DELETED  = 8; /*удалена*/

CONST 
   C_PARTY_CODEKIND_ABS   = 101;

CONST
   C_STEP_ACTION_INIT            = 10, // Инициализация
   C_STEP_ACTION_UNLOAD          = 20, // Выгрузка платежного поручения
   C_STEP_ACTION_PROCESS_STATUS  = 30, // Обработка статуса
   C_STEP_ACTION_FORM_ENTRY      = 40, // Формирование проводок
   C_STEP_ACTION_CLOSE           = 50; // Закрытие операции

CONST CC_SERV_SUBKIND_STOCK = 8;

CONST C_RCODE_OWN = "01356",
      C_RCODE_SEPARATE = "25834", C_RCODE_SEPARATE_NAME = "обособленные ДС",
      C_RCODE_NONSEPARATE = "06263", C_RCODE_NONSEPARATE_NAME = "необособленные ДС";
      
      
const C_ACCOUNT_PAYER_INDIVIDUAL  = "30424810000000000104",
      C_ACCOUNT_PAYER_NONSEPARATE = "30424810199002202617",
      C_ACCOUNT_PAYER_SEPARATE    = "30424810499003202617",
      C_ACCOUNT_PAYER_OWN         = "30424810099000001347";
      
const C_REFILL_TYPE_OWN          = 1, C_REFILL_TYPE_OWN_NAME         = "собственные счета РСХБ в НКЦ",
      C_REFILL_TYPE_SINGLE_POOL  = 2, C_REFILL_TYPE_SINGLE_POOL_NAME = "единый пул клиентов",
      C_REFILL_TYPE_INDIVIDUAL   = 4, C_REFILL_TYPE_INDIVIDUAL_NAME  = "индивидуальный РК(ТКС)";
      

private const K_ENTER   = 13,
              K_ESC     = 27,
              K_F2      = 316,
              K_F3      = 317,
              K_F4      = 318,
              K_F5      = 319,
              K_F6      = 320,
              K_F7      = 321,
              K_F8      = 322,
              K_F9      = 323,
              K_CTRL_R  = 18,
              K_ALT_P   = 281,
              K_ALT_F2  = 361,
              K_ALT_F6  = 365,
              K_ALT_F8  = 367;


/**
@brief Класс связки операции, платежа и проводки

*/
class TLink(p_refillID)
   var Refillid   : integer;
   var Nptxopid   : integer;
   var PaymentId  : integer;
   var AccTrnId   : integer;
   var EventId    : integer;
   var ResponseId : integer;
   var ErrorCode  : integer;
   var ErrorText  : string;
   
   private macro Construct(id)
     if (ValType(id) != V_INTEGER)
       return;
     end;
     
     var cmd = RSDCommand("SELECT * FROM UNPTXOP_PAYMENT_LINK_DBT WHERE T_REFILLID = :refillid");
     cmd.AddParam("refillid", RSDBP_IN, id);
     var rs = RsdRecordSet(cmd);
     
     if (rs.Movenext())
       Refillid = rs.Value("t_refillid");
       Nptxopid = rs.Value("t_nptxopid");
       PaymentId = rs.Value("t_paymentid");
       AccTrnId = rs.Value("t_acctrnid");
       EventId = rs.Value("t_eventid");
       ResponseId = rs.Value("t_responseid");
       ErrorCode = rs.Value("t_errorcode");
       ErrorText = String(rs.Value("t_errortext"));
     end;
   end;
   // === конструктор ===
   Construct(p_refillID);
end;


/**
@brief Получение текстового наименования статуса операции
@param[in] pstatus Числовой код статуса

@return Наименование статуса
*/
macro refill_GetStatusName(pstatus)
   var res = "Не определен";
   if (pstatus == C_REFILL_ACTION_CREATED)
      res = "Создана";
   elif (pstatus == C_REFILL_ACTION_OPENED)
      res = "Открыта";
   elif (pstatus == C_REFILL_ACTION_CLOSED)
      res = "Закрыта";
   elif (pstatus == C_REFILL_ACTION_DELETED)
      res = "Удалена";
   end;
   return res;
end;

/**
@brief Получение текста sql-ошибки
@param[in] _cmd RSDCommand

@return Полный текст ошибки СУБД

Использовать в onError(e) так:  RunError(e.message + getFullCmdErrorText(cmd));
*/
macro refill_getFullCmdErrorText(_cmd:RSDCommand)
   var cmd = _cmd;
   var err_str = "", i = 0;

   while( i < cmd.connection.environment.ErrorCount)
      err_str = err_str + cmd.connection.environment.Error(i).Descr + " ";
      i = i+1;
   end;

   return trim(err_str);
end;

/**
@brief Создание связки id операции зачисления - id платежа подкрепления
@param[in] p_Link Экземпляр класса связки

При возникновении ошибки будет вызвано стандартное RunError
*/
macro FixLink(v_Link)
   var sql_str, cmd;

   sql_str = String("declare \n"+
                    "   v_refillid  UNPTXOP_PAYMENT_LINK_DBT.t_refillid%type := :p_refillid; \n"+
                    "   v_nptxopid  UNPTXOP_PAYMENT_LINK_DBT.t_nptxopid%type := :p_nptxopid; \n"+
                    "   v_errortext UNPTXOP_PAYMENT_LINK_DBT.t_errortext%type := :p_errortext; \n"+
                    "begin  \n"+
                    "   insert into UNPTXOP_PAYMENT_LINK_DBT \n"+
                    "      (t_refillid, t_nptxopid, t_paymentid, t_acctrnid, t_eventid, t_responseid, t_errorcode, t_errortext) \n"+
                    "   values (v_refillid, v_nptxopid, :p_paymentid, :p_acctrnid, 0, 0, :p_errorcode, v_errortext); \n"+
                    "   INSERT INTO UNPTXOP_PAYMENT_LINK_STEP_DBT                                        \n"+
                    "      (T_STEPID, T_REFILLID, T_SYSTEMDATE, T_ACTION, T_COMMENT, T_OPER)             \n"+
                    "   VALUES (0, v_refillid, sysdate, 10, v_errortext, :poper);                        \n"+
                    "end; ");
   cmd = RSDCommand(sql_str);
   cmd.AddParam("p_refillid", RSDBP_IN, v_Link.Refillid);
   cmd.AddParam("p_nptxopid", RSDBP_IN, v_Link.Nptxopid);
   cmd.AddParam("p_errortext", RSDBP_IN, v_Link.ErrorText);
   cmd.AddParam("p_paymentid", RSDBP_IN, v_Link.PaymentId);
   cmd.AddParam("p_acctrnid", RSDBP_IN, v_Link.AccTrnId);
   cmd.AddParam("p_errorcode", RSDBP_IN, v_Link.ErrorCode);
   cmd.AddParam("poper", RSDBP_IN, {oper});

   cmd.execute();
   cmd.close();
onError(e)
   var err_txt = e.message + refill_getFullCmdErrorText(cmd);
   RunError(err_txt, c_usr_err_obj(err_txt));
end;

/**
@brief Класс Единичная запись операции подкрепления счета 
*/
private class TRefill_rec()
   /*Свойства соответствующие полям таблицы*/
   var m_ID                : integer; 
   var m_DATE_OPERATION    : date; 
   var m_TIME_OPERATION    : time;
   var m_TIME_ACCEPT       : time;
   var m_TYPE              : integer;
   var m_TYPE_REFILL       : integer;
   var m_TYPE_REFILL_NAME  : string;
   var m_SUM_OPERATION     : money;
   var m_RCODE             : string;
   var m_Account           : string;
   var m_CONTRACT          : integer;
   var m_CONTRACT_NUMBER   : string; 
   var m_STATUS            : integer;
   var m_STATUS_NAME       : string;
   var m_OPER              : integer;
   var m_OPER_NAME         : string;

   /*Свойства Получаемые*/
   var m_PartyId        : integer;
   var m_PartyCode      : integer;

   /*Свойства Вспомогательные*/
   var m_CurStepID      : integer;
   var m_Comment        : string;
   var m_Paymentid      : integer;

   m_PartyId=m_PartyCode=m_CurStepID=null;
   m_Comment = "";
   m_Paymentid = 0;

   /**
   @brief Получить ID клиента и его код из договора
   */
   macro GetParty()
      var Params : TArray;
      var state = 0, DataSet, Query;
      var errcode;

      Query = " select t_partyid from dsfcontr_dbt where t_id = :pcontrid ";
      Params = makeArray( SQLParam("pcontrid", m_CONTRACT) );
      DataSet = execSQLselect( Query, Params );

      if( DataSet.MoveNext )
         m_PartyId = DataSet.value(0, Null, V_INTEGER);
      else
         m_PartyId = -1;
      end;

      m_PartyCode = ПолучитьКодСубъекта(m_PartyId, C_PARTY_CODEKIND_ABS, errcode);
      if (errcode != 0)
         m_PartyCode = null;
      end;

      Params = Null;
   end;


   macro State()
      return m_STATUS;
   end;

   macro PartyID()
      if(m_partyID == null)
         GetParty();
      end;
      return m_PartyID;
   end;

end;

/**
@brief Класс Операция подкрепления счета 
*/
class TRefill( ID )
   var Rec : TRefill_rec;

   private macro DeleteRefillOperaton()
   end;

   private macro InsertStepRefillOperaton(paction)
      var sql = "";
      
      if (ValType(paction) == V_UNDEF)
         paction = C_STEP_ACTION_INIT;
      end;
      
      sql = String(  " INSERT INTO UREFILL_STEP_DBT  \n",
                     "   (T_STEPID, T_REFILLID, T_DATE_OPERATION, T_SYSTEMDATE, T_ACTION, T_COMMENT, T_OPER)   \n",
                     " VALUES (0, :prefillid, :pdate_operation, sysdate, :paction, :pcomment, :poper)         \n",
                     " RETURNING t_stepid INTO :v_ret" );
      var cmd = RsdCommand(sql);
      cmd.addParam("prefillid"      , RSDBP_IN, Rec.m_ID);
      cmd.addParam("pdate_operation", RSDBP_IN, Rec.m_DATE_OPERATION);
      cmd.addParam("paction"        , RSDBP_IN, paction);
      cmd.addParam("pcomment"       , RSDBP_IN, Rec.m_COMMENT);
      cmd.addParam("poper"          , RSDBP_IN, {oper});
      cmd.addParam("v_ret"          , RSDBP_OUT, V_INTEGER);
      cmd.execute();

      rec.m_CurStepID = cmd.Value("v_ret");

      return 0;
   OnError(e);
      return 1;
   end;
 
   macro SaveRefillOperation()
      var cmd, sql = "";

      if (rec.m_ID == 0)
         sql = String ( " INSERT INTO urefill_dbt                                                    \n",
                        "   (  t_id, t_date_operation, t_time_operation, t_time_accept, t_type,             \n",
                        "      t_type_refill, t_type_refill_name, t_sum_operation, t_rcode, t_account,      \n",
                        "      t_contract, t_contract_number, t_status, t_status_name, t_oper, t_oper_name )\n",
                        " VALUES                                                                            \n",
                        "   (0, :1, :2, :3, :4, :5, :6, :7, :8, :9, :10, :11, :12, :13, :14, :15)           \n",
                        " RETURNING t_id INTO :v_refillid;" );
         cmd = RsdCommand(sql);
         cmd.addParam("", RSDBP_IN, Rec.m_DATE_OPERATION);  //1
         cmd.addParam("", RSDBP_IN, Rec.m_TIME_OPERATION);  //2
         cmd.addParam("", RSDBP_IN, Rec.m_TIME_ACCEPT);     //3
         cmd.addParam("", RSDBP_IN, Rec.m_TYPE);            //4
         cmd.addParam("", RSDBP_IN, Rec.m_TYPE_REFILL);     //5
         cmd.addParam("", RSDBP_IN, Rec.m_TYPE_REFILL_NAME);//6
         cmd.addParam("", RSDBP_IN, Rec.m_SUM_OPERATION);   //7
         cmd.addParam("", RSDBP_IN, Rec.m_RCODE);           //8
         cmd.addParam("", RSDBP_IN, Rec.m_Account);         //9
         cmd.addParam("", RSDBP_IN, Rec.m_CONTRACT);        //10
         cmd.addParam("", RSDBP_IN, Rec.m_CONTRACT_NUMBER); //11
         cmd.addParam("", RSDBP_IN, Rec.m_STATUS);          //12
         cmd.addParam("", RSDBP_IN, Rec.m_STATUS_NAME);     //13
         cmd.addParam("", RSDBP_IN, Rec.m_OPER);            //14
         cmd.addParam("", RSDBP_IN, Rec.m_OPER_NAME);       //15
         cmd.addParam("v_refillid", RSDBP_OUT, V_INTEGER);
         cmd.execute();

         rec.m_ID =  cmd.Value("v_refillid");
         
         if (InsertStepRefillOperaton(C_STEP_ACTION_INIT) != 0)
            return false;
         end;
      else
         sql = String(  " UPDATE urefill_dbt                  \n",
                        "    SET t_status = :1, t_status_name = :2   \n",
                        "  WHERE t_id = :v_refillid " );
         cmd = RsdCommand(sql);
         cmd.addParam("", RSDBP_IN, Rec.m_STATUS);
         cmd.addParam("", RSDBP_IN, refill_GetStatusName(Rec.m_STATUS));
         cmd.addParam("v_refillid", RSDBP_IN, Rec.m_ID);
         cmd.execute();
         
         if (InsertStepRefillOperaton(rec.m_CurStepID) != 0)
            return false;
         end;
         
      end;

      return true;

   OnError(e);
      return false;
   end;

   macro SaveRefillOperationTrn()
      var cmd, sql = "";
      var stat = false;

      if ( not rslDefCon.isInTrans() )
         rslDefCon.beginTrans();
      end;

      stat = SaveRefillOperation();

      if ( rslDefCon.isInTrans() )
        rslDefCon.commitTrans();
      end;
      return stat;

   OnError(e);
      if ( rslDefCon.isInTrans() )
        rslDefCon.rollbackTrans();
      end;
      return false;
   end;

   private macro ClearError();
      rec.m_Comment = StrFor(1);
   end;

   private macro SetError(Err);
      rec.m_Comment = Err;
      SaveRefillOperation();
   end;

   private macro SetWarning(Err);
      rec.m_Comment = Err;
      SaveRefillOperation();
   end;

   private macro Construct( ID )
      var cmd, dt, sql = "";

      if (ValType(ID) == V_UNDEF) 
         ID = 0;
      end;

      if (ID > 0)
         sql = String(  " SELECT t_id, t_date_operation, t_time_operation, t_time_accept, t_type,                   \n",
                        "        t_type_refill, t_type_refill_name, t_sum_operation, nvl(t_rcode, chr(1)) t_rcode,  \n",
                        "        nvl(t_account, chr(1)) t_account,                                                  \n",
                        "        nvl(t_contract, -1) t_contract, nvl(t_contract_number, chr(1)) t_contract_number,  \n",
                        "        t_status, nvl(t_status_name, chr(1)) t_status_name, nvl(t_oper, 0) t_oper,         \n",
                        "        nvl(t_oper_name, chr(1)) t_oper_name                                               \n",
                        "   FROM urefill_dbt                                                                        \n",
                        "  WHERE t_id = :1 ");
         cmd = RsdCommand(sql);
         cmd.NullConversion = true;
         cmd.addParam("",RSDBP_IN, ID);
         Dt = RsdRecordSet(cmd);
         if (Dt.movenext())
            Rec.m_ID                = Dt.Value("t_id");
            Rec.m_DATE_OPERATION    = Dt.Value("t_date_operation");
            Rec.m_TIME_OPERATION    = Dt.Value("t_time_operation");
            Rec.m_TIME_ACCEPT       = Dt.Value("t_time_accept");
            Rec.m_TYPE              = Dt.Value("t_type");
            Rec.m_TYPE_REFILL       = Dt.Value("t_type_refill");
            Rec.m_TYPE_REFILL_NAME  = Dt.Value("t_type_refill_name");
            Rec.m_SUM_OPERATION     = Dt.Value("t_sum_operation");
            Rec.m_RCODE             = Dt.Value("t_rcode");
            Rec.m_Account           = Dt.Value("t_account");
            Rec.m_CONTRACT          = Dt.Value("t_contract");
            Rec.m_CONTRACT_NUMBER   = Dt.Value("t_contract_number");
            Rec.m_STATUS            = Dt.Value("t_status");
            Rec.m_STATUS_NAME       = Dt.Value("t_status_name");
            Rec.m_OPER              = Dt.Value("t_oper");
            Rec.m_OPER_NAME         = Dt.Value("t_oper_name");
         else
            Rec.m_ID = 0; 
         end;
      else
         Rec.m_ID = 0; 
      end;

      if ( Rec.m_ID == 0 )
         Rec.m_ID                = 0;
         Rec.m_DATE_OPERATION    = date(0,0,0);
         Rec.m_TIME_OPERATION    = time(0,0,0);
         Rec.m_TIME_ACCEPT       = time(0,0,0);
         Rec.m_TYPE              = 0;
         Rec.m_TYPE_REFILL       = 0;
         Rec.m_TYPE_REFILL_NAME  = "";
         Rec.m_SUM_OPERATION     = 0;
         Rec.m_RCODE             = "";
         Rec.m_Account           = "";
         Rec.m_CONTRACT          = -1;
         Rec.m_CONTRACT_NUMBER   = "";
         Rec.m_STATUS            = -1;
         Rec.m_STATUS_NAME       = "";
         Rec.m_OPER              = 0;
         Rec.m_OPER_NAME         = "";
      end;
   end;


   macro CreatePaymentForOperation()
      var stat = false, err="";
      var v_Link = TLink();
      v_Link.Refillid = Rec.m_ID;
      v_Link.Nptxopid = 0;
      
      var cmd1 = String(" SELECT u.t_paymentid, u.t_acctrnid FROM unptxop_payment_link_dbt u \n",
                        "  WHERE u.t_refillid = :pid AND u.t_paymentid <> 0   ");
      cmd1 = RSDCommand(cmd1);
      cmd1.addParam("pid",RSDBP_IN, v_Link.RefillID);
      var rs1 = RsdRecordSet(cmd1);
      if (rs1.movenext())  // есть созданный платеж
         rec.m_STATUS = C_REFILL_ACTION_OPENED;
         rec.m_CurStepID = C_STEP_ACTION_UNLOAD;
         rec.m_Paymentid = rs1.value("t_paymentid");
         rec.m_Comment = "Платеж № " + rec.m_Paymentid;
         stat = true;
      else 
         if ( not rslDefCon.isInTrans() )
            rslDefCon.beginTrans();
         end;

         v_Link.Nptxopid = 0;
         v_Link.ErrorCode = CreateRefillPayment(Rec.m_DATE_OPERATION, Rec.m_SUM_OPERATION, Rec.m_RCODE, v_Link.Nptxopid, v_Link.Refillid, Rec.m_Account,
                                                @v_Link.AccTrnId, @v_Link.PaymentId, @v_Link.ErrorText);

         if ( rslDefCon.isInTrans() )
            if (v_Link.ErrorCode == 1) // произошла ошибка
               rslDefCon.rollbackTrans();
               SetError("Не удалось создать платеж. "+v_Link.ErrorText);
            else 
               rslDefCon.commitTrans();
            end;
         end;

         FixLink(v_Link);  // фиксирование результата вне транзакции
         
         if (v_Link.ErrorCode == 0)    // успешное создание 
            rec.m_STATUS = C_REFILL_ACTION_OPENED;
            rec.m_CurStepID = C_STEP_ACTION_UNLOAD;
            rec.m_Paymentid = v_Link.PaymentId;
            stat = true;
         else
            rec.m_STATUS = C_REFILL_ACTION_CREATED;
            rec.m_CurStepID = C_STEP_ACTION_INIT;
            err = v_Link.ErrorText;
            stat = false;
         end;
            
      end;

      if ((err != rec.m_Comment) or (rec.m_STATUS == C_REFILL_ACTION_OPENED))
         rec.m_Comment = err;
         if (rec.m_STATUS == C_REFILL_ACTION_OPENED)
            rec.m_Comment = "Платеж № " + rec.m_Paymentid;
            if (StrLen(err) > 1)
               rec.m_Comment = rec.m_Comment + " " + err;
            end;
         end;
         stat = SaveRefillOperation();
      end;

      return stat;

   OnError(e)
      if ( rslDefCon.isInTrans() )
        rslDefCon.rollbackTrans();
      end;
      rec.m_STATUS = C_REFILL_ACTION_CREATED;
      rec.m_CurStepID = C_STEP_ACTION_INIT;
      SetError(e.Message +"\n"+ e.Module +"\nLine:"+ e.Line);
      SaveRefillOperation();
      return false;
   end;


   Construct( ID );
end;

/**
@brief Проверка настройки 
@param[out] имя настройки 
@return true - включено/существует, false - выключено/не существует
*/
macro IsRegAutoRefillOn()
   var isactive = false;
   var error:integer;
   GetRegistryValue(REG_AUTOREFILL_ACTIVE, V_BOOL, isactive, error);
   if( error > 0 )
      isactive = false;
   end;
   SetParm(0, REG_AUTOREFILL_ACTIVE);

   return isactive;
end;


MACRO refill_IIF(condition,value_true,value_false)
    if (condition)
        return value_true;
    else
        return value_false;
    end;
END;

/**
@brief Определить идентификатор биржи по виду кода ДБО
@param[in] CodeKind вид кода биржи (ММВБ/СПБ)
@return код биржи

Функция описана в dlcontrfunc, однако при импорте начинаются пересечения переменных при создании платежа.
Поэтому вынесена локально
*/
macro refill_ПолучитьБиржуПоВидуКодаДБО(CodeKind)
  var MarketCode = "";
  var err = 0;

  var MarketID = -1;

  if(CodeKind == DLCCK_USC)
    GetRegistryValue("SECUR\\MICEX_CODE", V_STRING, MarketCode, err);
    if(err != 0)
      msgbox("Ошибка при получении значения настройки SECUR\\MICEX_CODE");
    elif(MarketCode == "")
      msgbox("Не задано значение настройки SECUR\\MICEX_CODE");
    else
      MarketID = GetCodeParty(MarketCode,PTCK_CONTR);
    end;

  elif(CodeKind == DLCCK_SPB_BIDCODE)
    GetRegistryValue("SECUR\\SPBEX_CODE", V_STRING, MarketCode, err);
    if(err != 0)
      msgbox("Ошибка при получении значения настройки SECUR\\SPBEX_CODE");
    elif(MarketCode == "")
      msgbox("Не задано значение настройки SECUR\\SPBEX_CODE");
    else
      MarketID = GetCodeParty(MarketCode,PTCK_CONTR);
    end;
  end;

  return MarketID;
end;

/**
@brief Получение имени операциониста
@param[in] poper Номер операциониста

@return Наименование операциониста
*/
macro refill_OperName(poper)
   var RS = RSDRecordSet("SELECT person.t_Name " +
                         "  FROM dperson_dbt person " +
                         " WHERE person.t_oper = " + string(poper) );
   if( RS.MoveNext() )
      return RS.value("t_Name");
   end;
   return "";
end;

