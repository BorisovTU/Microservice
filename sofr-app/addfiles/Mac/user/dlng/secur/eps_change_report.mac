import or_rep_h, globals, oralib, likepy;
import fiinter;
import "sp_categ.mac";
import RsbFormsInter;


private const FT_STRING = 7;
private const FT_DATE = 9;

private const FORMAT_REPORT_DAY = 1;
private const FORMAT_REPORT_MONTH = 2;
private const FORMAT_REPORT_PERIOD = 3;

var format_line = "c:ex_B(tblr):ex_FS()";
var format_head = "c:ex_FS(b):ex_B(tblr)";

var Rep;

macro print_eps_transactions(ID_OPERATION, ID_STEP)
    var query =
    " SELECT ACCTRN.T_DATE_CARRY,                                                   " +
    "        ACCTRN.T_ACCOUNT_PAYER,                                                " +
    "        ACCTRN.T_ACCOUNT_RECEIVER,                                             " +
    "        ACCTRN.T_SUM_PAYER,                                                    " +
    "        ACCTRN.T_GROUND                                                        " +
    "   FROM doprstep_dbt oprstep_calc                                              " +
    "        INNER JOIN doprdocs_dbt oprdocs                                        " +
    "           ON     OPRSTEP_CALC.T_SERVDOCID = oprdocs.T_SERVDOCID               " +
    "              AND OPRSTEP_CALC.T_SERVDOCKIND = oprdocs.T_SERVDOCKIND           " +
    "              AND OPRSTEP_CALC.T_ID_OPERATION = OPRDOCS.T_ID_OPERATION         " +
    "        INNER JOIN dacctrn_dbt acctrn                                          " +
    "           ON OPRDOCS.T_ACCTRNID = ACCTRN.T_ACCTRNID                           " +
    "  WHERE     OPRSTEP_CALC.T_ID_OPERATION = :m_id_operation                      " +
    "        AND OPRSTEP_CALC.T_ID_STEP = :m_id_step                                " +
    "        AND OPRDOCS.T_DOCKIND = 1                                              " +
    "        AND EXISTS                                                             " +
    "               (SELECT 1                                                       " +
    "                  FROM dmcaccdoc_dbt mc                                        " +
    "                 WHERE (   mc.t_account = ACCTRN.T_ACCOUNT_PAYER               " +
    "                        OR mc.t_account = ACCTRN.T_ACCOUNT_RECEIVER)           " +
    "                       AND t_catid IN                                          " +
    "                              (SELECT t_id                                     " +
    "                                 FROM dmccateg_dbt                             " +
    "                                WHERE t_code IN                                " +
    "                                         ('+КорЭПС_%, ц/б','-КорЭПС_%, ц/б'))) ";
    var cmd = DL_RSDCommand(query);
        cmd.AddParam(ID_OPERATION);
        cmd.AddParam(ID_STEP);
    var sql = cmd.Execute();

    Rep.AddPrintCell("Дата проводки", 10,  null, format_head);
    Rep.AddPrintCell("Дебет",         21,  null, format_head);
    Rep.AddPrintCell("Кредит",        21,  null, format_head);    
    Rep.AddPrintCell("Сумма",         15,  null, format_head);
    Rep.AddPrintCell("Основание",     100, null, format_head);
    Rep.AddStr();

    while(sql.movenext)
        Rep.AddPrintCell(date(sql.date_carry), 10,  null, format_line);
        Rep.AddPrintCell(sql.account_payer,    21,  null, format_line);
        Rep.AddPrintCell(sql.account_receiver, 21,  null, format_line);
        Rep.AddPrintCell(sql.sum_payer,        15,  2,    format_line);
        Rep.AddPrintCell(sql.ground,           100, null, format_line);
        Rep.AddStr();
    end;
end;

macro print_avr_header()
    //Rep.AddPrintCell("Наименование бумаги",   35, null, format_head);
    //Rep.AddPrintCell("ISIN",                  15, null, format_head);
    Rep.AddPrintCell("Количество",            10, null, format_head);    
    Rep.AddPrintCell("ЭПС до",                10, null, format_head);
    Rep.AddPrintCell("ЭПС после",             10, null, format_head);
    //Rep.AddPrintCell("Дата изменения ставки", 20, null, format_head);
    Rep.AddStr();
end;

macro createReport(DATE_BEGIN, DATE_END, FORMAT_REPORT)
    var prev_id_operarion = -1;    
    var prev_id_step = -1;
    var prev_fiid = -1;
    var prev_changedate = date(0,0,0);
    var is_first_loop = true;

    var query =
    "   SELECT wrt_before.T_EFFECTINTERESTRATE eps_before,                             " +
    "          NVL (bc_after.T_EFFECTINTERESTRATE, wrtsum.T_EFFECTINTERESTRATE)        " +
    "             eps_after,                                                           " +
    "          wrt_after.t_changedate,                                                 " +
    "          wrt_after.t_fiid,                                                       " +
    "          avr.t_isin,                                                             " +
    "          fin.t_name,                                                             " +
    "          sum(wrt_after.t_amount) amount_after,                                   " +
    "          WRT_AFTER.T_ID_OPERATION,                                               " +
    "          WRT_AFTER.T_ID_STEP                                                     " +
    "     FROM V_SCWRTHISTEX wrt_after                                                 " +
    "          LEFT JOIN dpmwrtbc_dbt bc_after                                         " +
    "             ON wrt_after.t_bcid = bc_after.t_bcid                                " +
    "          INNER JOIN dpmwrtsum_dbt wrtsum                                         " +
    "             ON wrt_after.t_sumid = wrtsum.t_sumid                                " +
    "          INNER JOIN dpmwrtbc_dbt wrt_before                                      " +
    "             ON wrt_after.t_sumid = wrt_before.t_sumid                            " +
    "                AND wrt_before.t_instance = wrt_after.t_instance - 1              " +
    "          INNER JOIN davoiriss_dbt avr                                            " +
    "             ON wrt_after.t_fiid = avr.t_fiid                                     " +
    "          INNER JOIN dfininstr_dbt fin                                            " +
    "             ON wrt_after.t_fiid = fin.t_fiid                                     " +
    "    WHERE WRT_AFTER.T_CHANGEDATE BETWEEN :m_date_begin AND :m_date_end            " +
    "          AND WRT_AFTER.t_state IN (1, 3)                                         " +
    "          AND WRT_AFTER.t_amount > 0                                              " +
    "          AND WRT_AFTER.T_PORTFOLIO != 0                                          " +
    "          AND wrtsum.T_AMORTCALCKIND = 2                                          " +
    "          AND NVL (bc_after.T_EFFECTINTERESTRATE, wrtsum.T_EFFECTINTERESTRATE) != " +
    "                 wrt_before.T_EFFECTINTERESTRATE                                  " +
    " GROUP BY wrt_before.T_EFFECTINTERESTRATE,                                        " +
    "          NVL (bc_after.T_EFFECTINTERESTRATE, wrtsum.T_EFFECTINTERESTRATE),       " +
    "          wrt_after.t_changedate,                                                 " +
    "          wrt_after.t_fiid,                                                       " +
    "          avr.t_isin,                                                             " +
    "          fin.t_name,                                                             " +
    "          WRT_AFTER.T_ID_OPERATION,                                               " +
    "          WRT_AFTER.T_ID_STEP                                                     " +
    " ORDER BY wrt_after.t_changedate, wrt_after.t_fiid                                ";

    var cmd = DL_RSDCommand(query);
        cmd.AddParam(DATE_BEGIN);
        cmd.AddParam(DATE_END);

    Rep = CMakeReport();

    Rep.SetDefaultFontName("Times New Roman");
    Rep.SetDefaultFontSize(10);
    Rep.SetFlagShowGrid(true);

    Rep.SetExecute("iBook.Sheets(1).Rows(1).WrapText = false;");
    Rep.AddPrintCell("Отчет по проверке изменения значения ЭПС c "+string(DATE_BEGIN)+" по "+string(DATE_END), 171, 0, "c:ex_FS(b):ex_B()");
    Rep.AddStr();

    Rep.AddPrintCell(" ", 10, 0, "c:ex_FS(b):ex_B()");
    Rep.AddStr();
    Rep.AddPrintCell(" ", 10, 0, "c:ex_FS(b):ex_B()");
    Rep.AddStr();

    BegAction(0, "Сбор данных");
    var cnt = cmd.GetCount();
    EndAction();

    var i = 0;    
    InitProgress(cnt, "Сбор данных", "Сбор данных");

    var sql = cmd.Execute();

    while(sql.movenext)
        if( (   (prev_id_operarion != sql.id_operation) 
             OR (prev_id_step != sql.id_step) 
             OR (prev_fiid != sql.fiid) 
             OR (prev_changedate != date(sql.changedate))))

            if(not is_first_loop)
                print_eps_transactions(prev_id_operarion, prev_id_step);
                Rep.AddEmptyStr();
            end;

            Rep.AddPrintCell("Бумага "+sql.name+" " + sql.isin + " Дата изменения:  " + string(date(sql.changedate)), 100, null, "l:ex_FS(b):ex_B()");
            Rep.AddStr();
            print_avr_header();
        end;

        //Rep.AddPrintCell(sql.name,             35, null, format_line);
        //Rep.AddPrintCell(sql.isin,             15, null, format_line);
        Rep.AddPrintCell(sql.amount_after,     10, 0,    format_line);
        Rep.AddPrintCell(sql.eps_before,       10, 5,    format_line);
        Rep.AddPrintCell(sql.eps_after,        10, 5,    format_line);
        //Rep.AddPrintCell(date(sql.changedate), 20, null, format_line);
        Rep.AddStr();

        prev_id_operarion = sql.id_operation;
        prev_id_step = sql.id_step;
        prev_fiid = sql.fiid;
        prev_changedate = date(sql.changedate);

        is_first_loop = false;

        UseProgress(i = i + 1);
    end;

    print_eps_transactions(prev_id_operarion, prev_id_step);
    Rep.AddEmptyStr();

    RemProgress();

    Rep.SetFileName("eps_change_report_"+DATE_BEGIN);
    Rep.PrintWinRep("Проверка ЭПС");
    Rep.SetZoomType(ZOOM_TYPE_A4L);
    Rep.SetWinRepOutput();
    Rep.ShowWinRep();
end;

class (TRsbPanel) DateReportPanel()
    InitTRsbPanel();
    setSize(50,5);
    setPosition(35,15);

    class (TRsbEditField) EditField(typeFld: integer, x: integer, y: integer, width: integer, height: integer, bindVal, active: bool, fieldName: string, panel)
        var bindString = string(bindVal);
        
        initTRsbEditField(typeFld);
        setPosition(x, y);
        setSize(width, height);
        
        if(active == false) 
            editable = focusable = false; 
        end;

        if(typeFld == FT_STRING)
            bindValue( this, "bindString", 100 );
        elif(bindVal != null)
            value = bindVal; 
        end;

        if(panel != null)
            panel.addControl(this); 
        end;

        if(fieldName != null) 
            name = fieldName; 
        end;
    end;

    addLabel(TRsbLabel(3, 2, "Укажите дату:"));
    var m_DateEnd = EditField( FT_DATE, 18, 2, 10, 1, {curdate}, true, "DateEnd", this );
    
    addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "Panel_ButtonEvent"));

    addLabel(TRsbLabel(3, 3, "Проверить изменение:"));
    var fld_Format = TRsbComboBox();
    fld_Format.name = "fld_Format";
    fld_Format.dataType = FT_STRING;
    fld_Format.textLength = 22;
    fld_Format.setPosition(18, 3);
    fld_Format.setSize (10, 1);
    fld_Format.addChoice(FORMAT_REPORT_DAY, "за день");
    fld_Format.addChoice(FORMAT_REPORT_MONTH, "за месяц");
    fld_Format.addChoice(FORMAT_REPORT_PERIOD, "с даты");
    fld_Format.currentChoice = FORMAT_REPORT_DAY;
    fld_Format.onItemSelected(r2m(this, "onFormatSelected"));
    addControl(fld_Format);

    var m_DateBegin = EditField( FT_DATE, 31, 3, 10, 1, DateAfterCalenDays(m_DateEnd.value, -1), false, "DateBegin", this );

    m_DateBegin.addEventHandler(RSB_EV_KEY_PRESSED,  R2M(this, "DATE_KEY_PRESSED"));
    m_DateEnd.addEventHandler(RSB_EV_KEY_PRESSED,  R2M(this, "DATE_KEY_PRESSED"));

    macro switch_datebegin()
        if(fld_Format.currentChoice == FORMAT_REPORT_DAY)
            m_DateBegin.editable = m_DateBegin.focusable = false;
            m_DateBegin.value = DateAfterCalenDays(m_DateEnd.value, -1);
        elif(fld_Format.currentChoice == FORMAT_REPORT_MONTH)
            m_DateBegin.editable = m_DateBegin.focusable = false;
            m_DateBegin.value = DateAfterCalenMonths(m_DateEnd.value, -1);
        else
            m_DateBegin.editable = m_DateBegin.focusable = true;
            m_DateBegin.value = m_DateEnd.value;
        end;    
    end;

    macro DATE_KEY_PRESSED(RsbEvent)
       if(RsbEvent.keyCode == 317)
           RsbEvent.source.value = GetDateByCalendar(RsbEvent.source.value);
           
           if(RsbEvent.source.name == "DateEnd")
               switch_datebegin();
           end;
       elif((RsbEvent.keyCode == 408) OR (RsbEvent.keyCode == 411))/*KEY_ALT_UP KEY_ALT_LEFT*/
           RsbEvent.source.value = RsbEvent.source.value - 1;

           if(RsbEvent.source.name == "DateEnd")
               switch_datebegin();
           end;
       elif((RsbEvent.keyCode == 416) OR (RsbEvent.keyCode == 413))/*KEY_ALT_DOWN KEY_ALT_RIGHT*/
           RsbEvent.source.value = RsbEvent.source.value + 1;

           if(RsbEvent.source.name == "DateEnd")
               switch_datebegin();
           end;
       end;


    end;

    MACRO onFormatSelected(RsbEvent)
       switch_datebegin();
    END;

    macro Panel_ButtonEvent(RsbEvent)
       if((RsbEvent.keyCode == 316) or (RsbEvent.keyCode == 13))
          createReport(m_DateBegin.value, m_DateEnd.value, fld_Format.currentChoice);
          close(1);       
       elif(RsbEvent.keyCode == 27)
          close(0);
       end;
    end;
end;

var panel = DateReportPanel();
    panel.setCaption("Отчет по проверке изменения значения ЭПС");
    panel.run();

exit(1);