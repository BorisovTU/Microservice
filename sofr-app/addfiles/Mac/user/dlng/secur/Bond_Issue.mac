/*ЭМИССИЯ*/
/*Выполнение проводок по зачислению на раздел в размещении и перевода на торговый раздел эмиссионного счета*/
/*Дорохов А.Н.*/

IMPORT Global, RsbFormsInter, "cb_sql.mac", PTInter, "dlquery.mac", rsd;
IMPORT "qracctools.mac";
IMPORT "role_model.mac";//ролевая модель

CLASS (tRsbPanel) pnl_BondIssue (FIID)
  private const FT_STRING = 7;

  const K_Enter = 13;
  const K_F3    = 317;
  const K_F2    = 316;
  const K_Alt_F3= 362;

  const PlacementDate_ID = 5;
  const TransferDate_ID  = 7;

  const IssueDepoAcc  = "MZ0412010040";
  const IssueDepoPart = "19000000000000000";
  const TradeDepoPart = "31MC0134700000P00";

  record Субъект(party);
  ПолучитьСубъекта({OurBank},Субъект);

  record AVR (avoiriss);
  record ФинИн(fininstr);
  ПолучитьФинИн(FIID,ФинИН,AVR);

  InitTRSBPanel();
  Caption = "Операции с эмиссионным счетом.";
  Status  = "~ESC~Выход  ~F2~Выполнить  ~F3~Выбор  ~ALT+F3~Проводки";
  setSize(60,15);
  SetPosition(30,10);
  
  var AVRID = FIID;  
 
  var curstr = 0;
  var PlacementDate, lbl_PlacementDate; 
  var Filial_Name, Filial_Code, lbl_Filial;
  var TransferDate, lbl_TransferDate;
  var lbl_Bond, Bond_ISIN, Bond_Def;
  var lbl_Amount, Bond_Amount;
  var lbl_IssueAcc, IssueAcc;
  var lbl_IssuePart, IssuePart;
  var lbl_TradePart, TradePart;
  var isIssue_fld;
  var BondName = ФинИн.Definition;
  var isIssue = True;

  private macro ShowAccTrn
   var acc_cmd, acc_rs;
   var col;
   var arnum = -1;
   
   private macro AddCol (ar,ind, fld, head, width, rdonly, ty, dp)
       ar.value (ind * 6)     = fld;
       ar.value (ind * 6 + 1) = head;
       ar.value (ind * 6 + 2) = width;
       ar.value (ind * 6 + 3 ) = rdonly;   // fldType
       ar.value (ind * 6 + 4 ) = dp;//   decPoint
       ar.value (ind * 6 + 5 ) = 0;   // reserv
   end;

   col= TArray;
   AddCol (col, arnum=arnum+1, "T_DATE_CARRY",       "Дата Проводки", 12 ,  0,0);
   AddCol (col, arnum=arnum+1, "T_ACCOUNT_PAYER",    "Счет Дебет",    20 ,  0,0);
   AddCol (col, arnum=arnum+1, "T_ACCOUNT_RECEIVER", "Счет Кредит",   20 ,  0,0);
   AddCol (col, arnum=arnum+1, "T_ISIN",             "Выпуск",        15 ,  0,0);
   AddCol (col, arnum=arnum+1, "T_SUM_PAYER",        "Количество",    15 ,  0,0);
   AddCol (col, arnum=arnum+1, "T_GROUND",           "Основание",     100 ,  0,0);

     acc_cmd ="SELECT " +
                    "acctrn.T_DATE_CARRY , " +
                    "acctrn.T_ACCOUNT_PAYER , " +
                    "acctrn.T_ACCOUNT_RECEIVER , " +
                    "av.t_isin , " +
                    "acctrn.T_SUM_PAYER , " +
                    "ACCTRN.T_GROUND  " +
                    "  FROM dacctrn_dbt acctrn, davoiriss_dbt av " +
                    " WHERE " +
                    " ACCTRN.T_FIID_PAYER = av.t_fiid " +
                    " and  " +
                    " acctrn.T_ACCOUNT_Payer IN " +
                    "          (SELECT mc.t_account " +
                    "             FROM dmcaccdoc_dbt mc " +
                    "            WHERE mc.t_catnum = 1369 " +
                    "                  AND EXISTS " +
                    "                         (SELECT t_qrid " +
                    "                            FROM dscqracc_dbt qr " +
                    "                           WHERE qr.t_fiid = ? AND QR.T_DEPOACCCODE = ?" +
                    "                                 AND (QR.T_DEPOPARTCODE = ? " +
                    "                                      OR QR.T_DEPOPARTCODE = ?) " +
                    "                                 AND qr.t_qrid = mc.t_docid)) " ;

                   acc_cmd = RSDCommand(acc_cmd);
                   acc_cmd.addParam("",rsdbp_in,AVRID);
                   acc_cmd.addParam("",rsdbp_in,IssueDepoAcc);
                   acc_cmd.addParam("",rsdbp_in,IssueDepoPart);
                   acc_cmd.addParam("",rsdbp_in,TradeDepoPart);
                   acc_rs = RSDRecordset(acc_cmd, rsdval_client, rsdval_static);
                   RunScroll(acc_rs, arnum+1, col, null ,"scroll_event","Проводки операции")
  end;

  private macro FoundCreateQrRest(ScQrAcc:TRecHandler, Rest:money, RestDate:date, ErrStr:@string)
    var err=0, bufStr="";
    var ScQrRestTB = TBFile("scqrrest.dbt","w",1);
    ScQrRestTB.rec.QrId = ScQrAcc.rec.QrId;
    ScQrRestTB.rec.Date = RestDate;
    if (ScQrRestTB.GetEQ())
      ScQrRestTB.rec.Rest = Rest;
      if (not ScQrRestTB.Update())
        RunError("Ошибка обновления записи таблицы scqrrest.dbt:|"+status(bufStr)+"-"+bufStr);
      end;
    else
      ScQrRestTB.rec.QrId = ScQrAcc.rec.QrId;
      ScQrRestTB.rec.Date = RestDate;
      ScQrRestTB.rec.Rest = Rest;
      if (not ScQrRestTB.Insert())
        RunError("Ошибка вставки записи в таблицу scqrrest.dbt:|"+status(bufStr)+"-"+bufStr);
      end;
    end;
  OnError(ErrObj)
    if (IsEqClass ("TrslError", ErrObj))
      ErrStr=ErrObj.Message;
      if(IsEqClass ("TDbError", ErrObj.err))
        ErrStr=ErrStr+":|"+ErrObj.err.Stat+"-"+ErrObj.err.Oper+"-"+ErrObj.err.Table+"-"+ErrObj.err.Message;
      end;
    elif (not ErrStr)
      ErrStr="Ошибка выполнения FoundOpenScQr";
    end;
    return 1;
  end;

  private macro FoundOpenScQr(EurPartyId:integer, DepoAcc:string, DepoPart:string, 
    Fiid:integer, ScQrAcc:TRecHandler, AvoirRec:TRecHandler, FininstRec:TRecHandler, Rest:money, RestDate:date, ErrStr:@string)
    var err=0, bufStr="";
    var cmd, DataSet;
    var ScQrAccTB = TBFile("scqracc.dbt","w");
    ScQrAcc.Clear();
    FininstRec.Clear();
    AvoirRec.Clear();
    cmd = DL_RSDCommand(  "select * from dscqracc_dbt qracc"
                               + " where qracc.t_StorageId  = ? "
                               + "   and qracc.t_Fiid  = ? "
                               + "   and qracc.t_DepoAccCode = ? "
                               + "   and qracc.t_DepoPartCode = ? "
                               + "   and rownum = 1"
                               ); 
    cmd.AddParam(EurPartyId);
    cmd.AddParam(Fiid);
    cmd.AddParam(DepoAcc);
    cmd.AddParam(DepoPart);
    DataSet = cmd.Execute();
    if (DataSet.moveNext())
      DataSet.GetRecord().CopyTo(ScQrAcc.rec);
      err=FoundCreateQrRest(ScQrAcc, Rest, RestDate, @ErrStr);
    else
      ScQrAcc.rec.StorageId = EurPartyId;
      ScQrAcc.rec.Fiid = Fiid;
      ScQrAcc.rec.DepoAccCode = DepoAcc;
      ScQrAcc.rec.DepoPartCode = DepoPart;
      err = СоздатьЗаписьВРегистреКДУ(ScQrAcc, RestDate, Rest, @ErrStr);
    end;
    if (not err)
      ПолучитьФинИн(Int(Fiid),FininstRec,AvoirRec);
    end;
    return err;
  OnError(ErrObj)
    if (IsEqClass ("TrslError", ErrObj))
      ErrStr=ErrObj.Message;
      if(IsEqClass ("TDbError", ErrObj.err))
        ErrStr=ErrStr+":|"+ErrObj.err.Stat+"-"+ErrObj.err.Oper+"-"+ErrObj.err.Table+"-"+ErrObj.err.Message;
      end;
    elif (not ErrStr)
      ErrStr="Ошибка выполнения FoundOpenScQr";
    end;
    return 1;
  end;

  private macro isAlreadyIssue()
    //здесь проверяем существование проводок
    var rs, cmd,sql;
            SQL ="SELECT acctrn.* " +
        "  FROM dacctrn_dbt acctrn " +
        " WHERE acctrn.T_ACCOUNT_Payer IN " +
        "          (SELECT mc.t_account " +
        "             FROM dmcaccdoc_dbt mc " +
        "            WHERE mc.t_catnum = 1369 " +
        "                  AND EXISTS " +
        "                         (SELECT t_qrid " +
        "                            FROM dscqracc_dbt qr " +
        "                           WHERE qr.t_fiid = :p1 AND QR.T_DEPOACCCODE = :p2 " +
        "                                 AND (QR.T_DEPOPARTCODE = :p3 " +
        "                                      OR QR.T_DEPOPARTCODE = :p4) " +
        "                                 AND qr.t_qrid = mc.t_docid)) "; 
        cmd = DL_RSDCommand(SQL);
        cmd.AddParam(AVRID); 
        cmd.AddParam(IssueDepoAcc);
        cmd.AddParam(IssueDepoPart);
        cmd.AddParam(TradeDepoPart);
        rs = cmd.execute();
        if(rs.movenext())
          return 1;
        end;
    return 0;
  end;

  private macro ОткрытьСчетаРазделы()
    var err, errstr;
    var NrdPartyID;
    var SQL, RS, cmd;
    var tr;

    var ScQrAcc    = TRecHandler("scqracc.dbt");
    var AvoirRec   = TRecHandler("avoiriss.dbt");
    var FininstRec = TRecHandler("fininstr.dbt"); 

    var corBuf = TRecHandler("account.dbt");
    var accBuf1 = TRecHandler("account.dbt");
    var accBuf2 = TRecHandler("account.dbt");

    var CodeStr = "",_Rest,_ExDate;
    _Rest   = $0;
    _ExDate = THIS.PlacementDate.value;
    
      GetRegistryValue( "SECUR\\NRD_CODE", V_STRING, CodeStr, err );
          if (not err)
            NRDPartyID = ПолучитьКодСубъекта(String(CodeStr), PTCK_CLIENT);
            NRDPartyID = IIF(NRDPartyID,Int(NRDPartyID),0);
          end;

      err = FoundOpenScQr(NRDPartyId, IssueDepoAcc, IssueDepoPart, 
                AVRID, ScQrAcc, AvoirRec, FininstRec, _Rest, _ExDate, @ErrStr);
     
      if (not err)
        err = FoundOpenScQr(NRDPartyId, IssueDepoAcc, TradeDepoPart, 
                AVRID, ScQrAcc, AvoirRec, FininstRec, _Rest, _ExDate, @ErrStr);
      end;

     if (not err)  /******Выполняем проводки********/
 //Первоначальное зачисление
            SQL = "SELECT sc.t_qrid,  " +
        "                 sc.t_fiid,  " +
        "                 SC.T_DEPOPARTCODE, " +
        "                 ac.t_account " +
        "            FROM dscqracc_dbt sc, daccount_dbt ac " +
        "           WHERE SC.T_ACCOUNTID = AC.T_ACCOUNTID  " +
        "           and sc.T_DEPOPARTCODE = ?  " +
        "                  AND sc.t_fiid = ?  " +
        "                  AND rownum =1      " ;

        CMD = DL_RSDCommand(SQL);
        CMD.AddParam(IssueDepoPart);
        CMD.AddParam(AVRID);
        RS = CMD.Execute();
        if(RS.movenext())
          err = НайтиОткрытьСчетаКДУ(RS.QRID,_ExDate,accBuf1,corBuf);
          if(not err)
            tr = RSbAccTransaction();
            if(tr == null)
              RunError("Ошибка при формировании проводки");
            end;
        /*DAN.Ролевая модель. Определяем конечного операциониста для проводки */
              var retvalGetMO = GetMainOperInGroup(5);
              if (retvalGetMO > 0)
                Tr.Oper = retvalGetMO;
              else
                 Tr.Oper = {Oper};
              end;
        /*DAN*/
              tr.Date_Carry = {CurDate};
              tr.Chapter         = 5;
              tr.Department      = {OperDprt};
              tr.Number_Pack     = 10;
              tr.Numb_Document   = "";
              tr.Ground          = "Зачисление цб на эмиссионный счет при первичном размещении. Выпуск " + BondName ;
              tr.FIIDPayer       = AVRID;
              tr.FIIDReceiver    = AVRID;
              tr.SumPayer        = THIS.Bond_Amount.value;
              tr.SumReceiver     = THIS.Bond_Amount.value;
              tr.AccountPayer    = accBuf1.rec.Account;
              tr.AccountReceiver = corBuf.rec.Account;
              tr.Shifr_Oper = DL_GetShifrOper(5, accBuf1.rec, corBuf.rec);
              if( not tr.Carry() )
                  RunError("Ошибка при выполнении проводки");
              end;    
          end;
        end;

        tr = null;
 //Перевод с раздела в размещении на торговый раздел
           _ExDate = THIS.TransferDate.value; 
           cmd.m_params.value(0) = TradeDepoPart;
           RS = CMD.Execute();
        if(RS.movenext())
          err = НайтиОткрытьСчетаКДУ(RS.QRID,_ExDate,accBuf2,corBuf);
          if(not err)
            tr = RSbAccTransaction();
            if(tr == null)
              RunError("Ошибка при формировании проводки");
            end;
              Tr.Oper = retvalGetMO;
              tr.Date_Carry = {CurDate};
              tr.Chapter         = 5;
              tr.Department      = {OperDprt};
              tr.Number_Pack     = 10;
              tr.Numb_Document   = "";
              tr.Ground          = "Перевод цб с раздела \"В размещении\" на торговый раздел эмиссионного счета. Выпуск " + BondName ;
              tr.FIIDPayer       = AVRID;
              tr.FIIDReceiver    = AVRID;
              tr.SumPayer        = THIS.Bond_Amount.value;
              tr.SumReceiver     = THIS.Bond_Amount.value;
              tr.AccountPayer    = accBuf2.rec.Account;
              tr.AccountReceiver = accbuf1.rec.Account;
              tr.Shifr_Oper = DL_GetShifrOper(5, accBuf2.rec, accBuf1.rec);
              if( not tr.Carry() )
                  RunError("Ошибка при выполнении проводки");
              end;    
          end;
        end;
           
     end;  /********Заканчиваем с проводками***********/


     if (not err)
      return 1;
     end; 
      return 0;
  end;

  private macro ВыполнитьПроводки()
    return 1;
  end;
  
  if (isAlreadyIssue())
    isIssue = false;
  end;
 
  curstr = curstr + 1;  
     lbl_Filial = TRSBLabel(2, curstr,"Филиал:");
      addlabel (lbl_Filial);
  
    Filial_Code = TRSBEditField (FT_STRING);
    Filial_Code.setPosition(8,curstr);
    Filial_Code.setSize(4,1);
    Filial_Code.name = "Filial_Code";
    Filial_Code.textLength = 4;
    Filial_Code.value = "0000";
    Filial_Code.editable = false;
      addControl(Filial_Code);
    Filial_Name = TRSBEditField (FT_STRING);
    Filial_Name.setPosition(15,curstr);
    Filial_Name.setSize(38,1);
    Filial_Name.name = "Filial_Name";
    Filial_Name.textLength = 38;
    Filial_Name.value = Субъект.Name;
    Filial_Name.editable = false;
      addControl(Filial_Name);

  curstr = curstr + 1;
    lbl_PlacementDate = TRSBLabel(2, curstr,"Дата зачисления на раздел \"в размещении\" эмиссионного счета:");
      addlabel (lbl_PlacementDate);
  
    PlacementDate = TRSBEditField (V_Date);
    PlacementDate.setPosition(45,curstr);
    PlacementDate.setSize(8,1);
    PlacementDate.name = "PlacementDate";
    PlacementDate.value = {CurDate};
    PlacementDate.enabled = IsIssue;
      addControl(PlacementDate);
 //  addEventHandler (RSB_EV_Key_Pressed, R2M(this,"on_KeyPressed"));

   curstr = curstr + 1;
    lbl_TransferDate = TRSBLabel(2, curstr,"Дата перевода на торговый раздел эмиссионного счета:");
      addlabel (lbl_TransferDate);
  
    TransferDate = TRSBEditField (V_Date);
    TransferDate.setPosition(45,curstr);
    TransferDate.setSize(8,1);
    TransferDate.name = "TransferDate";
    TransferDate.value = {CurDate};
    TransferDate.enabled = isIssue;
      addControl(TransferDate);
   addEventHandler (RSB_EV_Key_Pressed, R2M(this,"on_KeyPressed"));

   curstr = curstr + 2;
    lbl_Bond = TRSBLabel(2, curstr,"Ценная бумага:");
      addlabel (lbl_Bond);

    Bond_ISIN = TRSBEditField (FT_STRING);
    Bond_ISIN.setPosition(12,curstr);
    Bond_ISIN.setSize(12,1);
    Bond_ISIN.focusable = false;
    Bond_ISIN.name = "Bond_ISIN";
    Bond_ISIN.textLength = 12;
    Bond_ISIN.value = AVR.ISIN;
    Bond_ISIN.Editable = isIssue;
    Bond_ISIN.enabled = isIssue;
      addControl(Bond_ISIN);
    Bond_Def = TRSBEditField (FT_STRING);
    Bond_Def.setPosition(25,curstr);
    Bond_Def.setSize(28,1);
    Bond_Def.name = "Bond_Def";
    Bond_Def.textLength = 25;
    Bond_Def.focusable = false;
    Bond_Def.value = ФинИН.Definition;
    Bond_Def.editable = false;
    Bond_Def.enabled = isIssue;
      addControl(Bond_Def);
   
   curstr = curstr + 1;
    lbl_Amount = TRSBLabel(2, curstr,"Количество:");
      addlabel (lbl_Amount);
  
    Bond_Amount = TRSBEditField (V_INTEGER);
    Bond_Amount.setPosition(12,curstr);
    Bond_Amount.setSize(12,1);
    Bond_Amount.name = "Bond_Amount";
    Bond_Amount.value = 0;
    Bond_Amount.Editable = isIssue;
    Bond_Amount.enabled = isIssue;
      addControl(Bond_Amount);
   
   curstr = curstr + 2;
    lbl_IssueAcc = TRSBLabel(2, curstr," Эмиссионный счет:          Раздел \"В размещении\"         Торговый раздел");
      addlabel (lbl_IssueAcc);
   
   curstr = curstr + 1;
    IssueAcc = TRSBEditField (FT_STRING);
    IssueAcc.setPosition(2,curstr);
    IssueAcc.setSize(15,1);
    IssueAcc.name = "IssueAcc";
    IssueAcc.textLength = 15;
    IssueAcc.value = IssueDepoAcc;
    IssueAcc.Editable = False;
    IssueAcc.enabled = isIssue;
      addControl(IssueAcc);
  
    IssuePart = TRSBEditField (FT_STRING);
    IssuePart.setPosition(20,curstr);
    IssuePart.setSize(18,1);
    IssuePart.name = "IssuePart";
    IssuePart.textLength = 18;
    IssuePart.value = IssueDepoPart;
    IssuePart.Editable = False;
    IssuePart.enabled = isIssue;
      addControl(IssuePart); 

    TradePart = TRSBEditField (FT_STRING);
    TradePart.setPosition(40,curstr);
    TradePart.setSize(18,1);
    TradePart.name = "TradePart";
    TradePart.textLength  = 18;
    TradePart.value = TradeDepoPart;
    TradePart.Editable = False;
    TradePart.enabled = isIssue;
      addControl(TradePart);  
     
    if(not isIssue)
      curstr = curstr + 2;
    isIssue_fld = TRSBEditField (FT_STRING);
    isIssue_fld.setPosition(2,curstr);
    isIssue_fld.setSize(55,2);
    isIssue_fld.name = "isIssue_fld";
    isIssue_fld.textLength  = 150;
    isIssue_fld.value = "В системе найдены проводки по Эмиссионному счету для данного выпуска. Повторное выполнение невозможно. ";
    isIssue_fld.Editable = False;
    isIssue_fld.enabled = false;
      addControl(isIssue_fld);  
   
    end;  
  

    macro on_KeyPressed(ev)
        var errflag = 0;
        if (ev.KeyCode == K_F3)
     
        if(valtype(ev.source.parent) != V_UNDEF)
           if (ev.source.name == "PlacementDate")
            PlacementDate.value = GetDateByCalendar({CurDate});
            this.redraw; 
           elif (ev.source.name == "TransferDate")
            TransferDate.value = GetDateByCalendar({CurDate});
            this.redraw;  
           elif (ev.source.name == "Bond_ISIN")
            msgbox("выбираем бумагу");
            this.redraw;   
           end;
        end;
        elif (ev.KeyCode == K_Enter)
            this.redraw; 
        elif (ev.KeyCode == K_Alt_F3)
          ShowAccTrn;                 
        elif((ev.keyCode == K_F2) and (not isAlreadyIssue()))
          if (THIS.Bond_Amount.value == 0)
            errflag = 1;
            THIS.prompt("Введите объем выпуска.", THIS.Bond_Amount);
          end;      
          if(THIS.PlacementDate.value > THIS.TransferDate.value )
            errflag = 1;
            THIS.prompt("Дата первоначального зачисления не может быть больше даты перевода на торговый раздел", THIS.PlacementDate);
          end;        

          if(THIS.TransferDate.value > {CurDate} )
            errflag = 1;
            THIS.prompt("Дата перевода не должна быть больше текущей даты", THIS.TransferDate);
          end;        
      

         if(not errflag)
           if(ОткрытьСчетаРазделы)
             if(ВыполнитьПроводки())
               ShowAccTrn;
               this.close(1);
             end;
           end;
         end;
        end;
    end; 
  
  
END; //CLASS pnl_BondIssuer

macro exec_bond_issue
  var fiid;
  getParm (0,fiid);
  var pnlIssue:TRSBpanel = pnl_bondIssue(fiid);
  pnlIssue.run();
  
end;


//exec_bond_issue;