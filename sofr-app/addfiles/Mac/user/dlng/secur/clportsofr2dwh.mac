/**                                                                                                             
 @file clportsofr2dwh.mac                                                                                          
 @brief Заполнение буферной таблицы dclportsofr2dwh_dbt данными по остаткам ДС и ЦБ клиентов ФЛ и данными, необходимыми для расчета стоимости портфеля этих клиентов ФЛ в мобильном приложении. 
 #menu 
  - БО ЦБ\Сервисные операции\Буферная таблица "Мобильное приложение"                                                                                                                                                                                                                    
                                                                                                                                                                                                             
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.21.04 |Шестаков Д.В.  |BIQ-15004           |Создание макроса для отчета                                                                                                                                                                                                                                                                                             
*/ 
 
import SFInter , globals, BankInter, RSD, ReportUser, Календарь;
import "dlmisc.mac"; 


/**
   @brief Создание файла логов 
   @param[in] message_  Текст ошибки
  */   
 macro CreateLogFile(message_:string) 
   
   var m_dtString =StrSubst(StrSubst(string(Date():f)+ "_" + Time(),".", ""),":", "");
   var m_FileName = string("..\\TxtFile\\DWH\\clportsofr2dwh_",m_dtString, ".txt");

   if(ExistDir("..\\TxtFile\\DWH\\") == 2)
      MakeDir("..\\TxtFile\\DWH\\");
   end;     

   File protocol_file() txt WRITE;
   var FC;

   if( Open(protocol_file, m_FileName) )
      SetOutPut(m_FileName);
      println(message_);
      SetOutput (NULL,TRUE);
      Close( m_FileName );
   end; 
 end;


/**
   @brief Отправка уведомлений в телеграм и на почту СОФР мониторинг 
   @param[in] message_  Текст ошибки
  */ 
 macro CreateLogEvent(message_:string) 
   
   var sql:String, 
       cmd;

   sql =  "begin  " +  
          "    rsb_dclportsofr2dwh.log_register_event(p_status => 1 "           +  
          "                                          ,p_message => :err_msg); " +                       
          "end;"; 

   cmd = RsdCommand(sql);
   cmd.addParam("err_msg" , RSDBP_IN, message_ );
   cmd.execute();

 end;


/**
  @brief Рассчет значений для заполнения буферной таблицы dclportsofr2dwh_dbt
  @param[in] dtBegin  Дата начала 
  @param[out] errmsg  Текст ошибки
  @return 0 - успешно, 1 - с ошибками 
*/
 MACRO MainCalc_Run_Pan(dtBegin:date, errmsg:@string) 

   CONST ACCOUNTING_CALENDAR = 10003;
           
   var sql:string,
       cmd,
       result:Integer, 
       err, 
       msgSucces:string,
       cntLimit:Integer,
       dtIn:Date;


   var curTask:Integer  = 0; //Текущая выполняемая задача
   var tasks:Integer = 8; //Всего задач процедур
   var IsShedulerRunning:Bool = false;

   result  = 0;

   if(ValType(dtBegin) == V_UNDEF)
      dtBegin =  GetDateAfterWorkDays(Date(), -1, ACCOUNTING_CALENDAR);
      IsShedulerRunning = true;
   end;


   sql = 
   "declare                                                              "  +
   "l_cnt_row number;                                                    "  + 
   "begin                                                                "  + 
   "   select  MIN(q.cnt_row)                                            "  + 
   "   into l_cnt_row                                                    "  +
   "   from (                                                            "  +
   "   select count(1) cnt_row                                           "  +
   "      from DDL_LIMITSECURITES_DBT limsec                             "  +
   "      where limsec.t_date = :dt_sec                                  "  +
   "      and limsec.t_limit_kind = 365                                  "  +
   "   union all                                                         "  +
   "   select count(1) cnt_row                                           "  +
   "   from DDL_LIMITCASHSTOCK_DBT limcash                               "  +
   "   where limcash.t_date = :dt_cash                                   "  +
   "      and limcash.t_limit_kind = 365) Q;                             "  +
   "  ?:= l_cnt_row;                                                     "  +  
   "  end;                                                               " ;                        
  
   dtIn = iif(IsShedulerRunning,Date(),dtBegin);
   cmd = RsdCommand(sql);
   //если запуск планировщика, то проверяем лимиты на сегодня
   cmd.addParam("dt_sec" ,   RSDBP_IN, dtIn);
   cmd.addParam("dt_cash" ,  RSDBP_IN, dtIn);
   cmd.addParam("l_cnt_row", RSDBP_OUT, V_INTEGER);
   cmd.execute();
   cntLimit = cmd.value("l_cnt_row");

  //проверка на наличие лимитов
   if(cntLimit == 0) 
      errmsg = "Дата в таблице лимитов не соответствует дате отчета";
      CreateLogFile(errmsg);
      CreateLogEvent(errmsg);
      return 1;
   end;

  //инициалиация переменных
   sql = " declare "  +
        "  	begin   "     +
        "      rsb_dclportsofr2dwh.initial_param(p_dt_begin =>  :date_begin); "  +
	     "    end;";

      cmd = RsdCommand(sql);
      cmd.addParam("date_begin" , RSDBP_IN, dtBegin);
      cmd.execute();

   if(not IsShedulerRunning)
      InitProgress(tasks, "Буферная таблица", "Заполнение буферной таблицы...");
   end;

   while (curTask < tasks)
      curTask = curTask + 1;

      if(not IsShedulerRunning)
         UseProgress(curTask);
      end;   
  
      sql = " declare "         +
        "  l_err_msg varchar2(32000);"   +
        "  l_result  number;"            + 
        "  	begin   "                  + 
        "  	    rsb_dclportsofr2dwh.load_bufferTable_" + curTask  + "(p_out_err_msg =>  l_err_msg , "   +
        "                             		                            p_out_result  =>  l_result); "     +
        "   ?:= substr(l_err_msg,0,73); "  +
        "   ?:= l_result; "   +
	      "    end;";

    	cmd = RsdCommand(sql);
    	cmd.addParam("l_err_msg", RSDBP_OUT, V_STRING);
    	cmd.addParam("l_result",  RSDBP_OUT, V_INTEGER);
	 
    	cmd.execute();
    	err    = cmd.value("l_err_msg");
    	result = cmd.value("l_result"); 
        //в случае ошибки
       if(result == 1)
         if(not IsShedulerRunning)
            RemProgress();
         end;

         errmsg = "Процедура clportsofr2dwh.mac:  ошибка заполнения буферной таблицы dclportsofr2dwh_dbt аналитикой по портфелю клиента. \n" + err;
         CreateLogFile(errmsg);
         break;
       end; 

       //в случае прохождения всех храниых процедур
       if((curTask == tasks) and (result == 0))
          msgSucces = "Успешное заполнение буферной таблицы dclportsofr2dwh_dbt аналитикой по портфелю клиента";
          if(not IsShedulerRunning)
            RemProgress();
          end; 
          msgbox(msgSucces);
          CreateLogFile(msgSucces);
      end;
   end;

  onError(erru)
    RemProgress();
    errmsg = "onError: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
    CreateLogFile(errmsg);
    //CreateLogEvent(errmsg);
    msgbox(errmsg);
    return 1;
 end;  



 /**
  @brief Процедура для запуска планировщика
*/
 MACRO MainCalc_Run()

  MainCalc_Run_Pan();

 end; 
