/*
$Name:        spcomcalc.mac
$Module:      Ценные бумаги
$Description: Операция расчета комиссий по сделкам
*/
import InsCarryDoc, "sp_car2.mac", "SCPeriodComm.mac", "dlquery.mac", "dv_categ.mac";
import AutoProcess; /*ak*/

const METHOD_CALC   = 1,/*расчет*/
      METHOD_RECALC = 2,/*пересчет*/
      METHOD_DEL    = 3;/*удаление*/

private var ИспользоватьРазбиениеПоСделкам = true; //Использовать разбиение групп обработки в конвейере по сделкам
private var КоличествоСделокВГруппе = 500; //Количество сделок в группе, если используется разбиение по сделкам

private var bo_act_init = "";
private var not_print_calced_comiss = "";//biq-11080
      
private macro GetMethodKind(METHOD:integer)
   if(METHOD == METHOD_CALC)
      return "расчет";
   elif(METHOD == METHOD_RECALC)
      return "пересчет";
   elif(METHOD == METHOD_DEL)
      return "удаление";
   end;

   return "";
end;

private class SourceData(OperDate_:Date,DealID_:INTEGER,PartyID_:INTEGER,ContractID_:INTEGER,ConCom_:INTEGER,Method_:INTEGER,GUID_:string,PrintRep_:bool,IsWeb_:bool,IsExec_:bool)
   var OperDate    = OperDate_,
       DealID      = DealID_,
       PartyID     = PartyID_,
       ContractID  = ContractID_,
       ConCom      = ConCom_,
       Method      = Method_,
       GUID        = GUID_,
       PrintRep    = PrintRep_,
       IsWeb       = IsWeb_,
       IsExec      = IsExec_;
       
end;

private var RsltStr = TStrCollector;

private file LogFile( "SCCOMMCALC.DBT" ) write;

private macro InsertIntoProtocol(GUID:string, DEALID:integer, CONTRID:integer, PAYERID:integer, RECEIVERID:integer, SUMMA:money, NDS:money, FIID:integer, COMMNUMBER:integer, ISBACK:bool )

   ClearRecord(LogFile);
   
   LogFile.GUID       = GUID;
   LogFile.DEALID     = DEALID;
   LogFile.CONTRID    = CONTRID;
   LogFile.PAYERID    = PAYERID;
   LogFile.RECEIVERID = RECEIVERID;
   LogFile.SUMMA      = SUMMA;
   LogFile.NDS        = NDS;
   LogFile.FIID       = FIID;
   LogFile.COMMNUMBER = COMMNUMBER;
   LogFile.ISBACK     = IIF(IsBack,SET_CHAR,UNSET_CHAR);
   LogFile.Type       = 1;
   LogFile.Str        = "";
  
   InsertRecTempFile( LogFile );

end;

private macro InsertError(GUID:string, ErrStr:string)
   ClearRecord(LogFile);
   
   if(ErrStr != "")

     LogFile.GUID       = GUID;
     LogFile.Type       = 2;
     LogFile.Str        = ErrStr;
    
     InsertRecTempFile( LogFile );
   end;
end;

private macro InsertWebRep(GUID:string, Str:string)
   ClearRecord(LogFile);
   
   if(Str != "")

     LogFile.GUID       = GUID;
     LogFile.Type       = 3;
     LogFile.Str        = Str;
    
     InsertRecTempFile( LogFile );
   end;
end;

private macro InsertRecs(GUID:string)
   ClearRecord(LogFile);
   
   LogFile.GUID       = GUID;
   LogFile.Type       = 4;
   LogFile.Str        = "";
    
   InsertRecTempFile( LogFile );
end;

private macro InsertWrnRecs(GUID:string)
   ClearRecord(LogFile);
   
   LogFile.GUID       = GUID;
   LogFile.Type       = 5;
   LogFile.Str        = "";
    
   InsertRecTempFile( LogFile );
end;

private const ITOG    = 1,/*была напечатана итоговая строка*/
              COMMISS = 2,/*была напечатана строка с комиссией*/
              CONTR   = 3,/*был напечатан договор*/
              SUBHEAD = 4;/*был напечатан подзаголовок*/

private macro PrintHeader(OperDate:Date,Method:string)
[
             ПРОТОКОЛ ОПЕРАЦИИ РАСЧЕТ КОМИССИЙ ПО СДЕЛКАМ

Дата расчета      #
Режим работы:     #

┌────────────────────┬────────────────────┬────────────────────┬───┬────────────────────┬────────────────────┐
│      Комиссия      │ Получатель комиссии│       Сделка       │2ч.│        Сумма       │      В т.ч. НДС    │
├────────────────────┴────────────────────┴────────────────────┴───┴────────────────────┴────────────────────┤]
(OperDate:f, GetMethodKind(Method));
end;

private macro PrintSubHeader(IsClient)
   if(IsClient)
[│ Клиентские сделки                                                                                          │];
   else                                                                                     
[│ Собственные сделки                                                                                         │];
   end;                                                                                     
end;

private macro PrintSubHeaderDelim_1()
[├──────────────────────────────────────────────────────────────┴───┴────────────────────┴────────────────────┤];
end;

private macro PrintSubHeaderDelim_2()
[├────────────────────────────────────────────────────────────────────────────────────────────────────────────┤];
end;

private macro PrintSubHeaderDelim(LastLinePrint:integer)
   if( LastLinePrint == ITOG )
      PrintSubHeaderDelim_1();
   elif( LastLinePrint == SUBHEAD )
      PrintSubHeaderDelim_2();
   end;
end;

private macro PrintItog(ComCode:string,Sum:money, NDS:money, FIIDCcy:string)
[├────────────────────┴────────────────────┴────────────────────┴───┼────────────────────┼────────────────────┤
│           Итого по  ####################                         │################ ###│################ ###│]
(DL_StrCut(ComCode,20," ",true),
  Sum:r,
  FIIDCcy,
  NDS:r,
  FIIDCcy
 );
end;


private macro PrintItogMinimal(Sum:money, NDS:money, FIIDCcy:string, NotPrintItogStr:bool)
   if(not NotPrintItogStr)
      [ Итого рассчитано                 Сумма: ################ ###   НДС: ################ ### ]
      ( Sum:r,
        FIIDCcy,
        NDS:r,
        FIIDCcy
      );
   else
      [                                  Сумма: ################ ###   НДС: ################ ### ]
      ( Sum:r,
        FIIDCcy,
        NDS:r,
        FIIDCcy
      );
   end;
end;


private macro PrintFooter()
[└──────────────────────────────────────────────────────────────────┴────────────────────┴────────────────────┘];
end;

private macro PrintDelim_1()
[├────────────────────┬────────────────────┬────────────────────┬───┬────────────────────┬────────────────────┤];
end;

private macro PrintDelim_2()
[├────────────────────┬────────────────────┬────────────────────┬───┼────────────────────┼────────────────────┤];
end;

private macro PrintDelim_3()
[├────────────────────┼────────────────────┼────────────────────┼───┼────────────────────┼────────────────────┤];
end;

private macro PrintDelim(LastLinePrint:integer)
   if( LastLinePrint == ITOG )
      PrintDelim_2();
   elif( LastLinePrint == COMMISS )
      PrintDelim_3();
   elif( LastLinePrint == CONTR )
      PrintDelim_1();
   end;
end;

private macro PrintContrNumber(PayerName:string,ContrNumber:string)
[│ Плательщик ################################### по договору № ###############                               │]
(DL_StrCut(PayerName,45," ",true), DL_StrCut(ContrNumber,15," ",true));
end;

private macro PrintLine(ComCode:string,ReceiverName:string,DealCode:string, IsBack:string, Sum:money, NDS:money, FIIDCcy:string )
[│####################│####################│####################│###│################ ###│################ ###│]
(DL_StrCut(ComCode,20," ",true):c, 
  DL_StrCut(ReceiverName,20," ",true):c,
  DL_StrCut(DealCode,20," ",true):c,
  DL_StrCut(IsBack,3," ",true):c,
  Sum:r,
  FIIDCcy,
  NDS:r,
  FIIDCcy
 );
end;

private macro log2file
   var i = 0;
   i = 0;
   while (i < RsltStr.Size)
      println(RsltStr(i));
      i = i + 1;
   end;        
end;

private macro ResultCaptureStart
   RsltStr.ClearArray; 
   SetOutHandler("ResultCaptureLine");
end;

macro ResultCaptureLine(str)
   RsltStr.AddString(str);
end;

private macro ResultCaptureStop
   SetOutHandler();
end;

private macro ПечататьПротоколПусто(Data, str);
  ResultCaptureStart();
[
 #
](str:l);
  ResultCaptureStop();

  if (Data.PrintRep)
    log2file();
  end;
end;

private macro SaveComis(Data, maxcalcid)
	var query = "insert into DDLCOMIS_LOST_34128_DBT (t_guid, t_dealid, t_contrid, t_payerid, t_receiverid, t_summa, t_nds, t_fiid, t_commnumber, t_isback, t_type, t_str, t_calcid, t_calcdate) "+
          " select t_guid, t_dealid, t_contrid, t_payerid, t_receiverid, t_summa, t_nds, t_fiid, t_commnumber, t_isback, t_type, t_str, ?, ? from Dsccommcalc_dbt where T_GUID = ? "; 
	var cmd = RSDCommand(query);
	cmd.AddParam("", RSDBP_IN, maxcalcid+1);
	cmd.AddParam("", RSDBP_IN, {curdate});
	cmd.AddParam("", RSDBP_IN, Data.GUID);
	cmd.execute;
end;

private macro ПечататьПротокол(Data, PrintCalcdComiss/*dan*/)
  var cmd, DataSet, stat = 0, i = 0;
  var PrevComNumber = -1, PrevComCode = "", PrevContrID = -1, IsHeaderPrint = false, PrevClientID = -1;
  var SumItog = $0, NDSItog =  $0, FIIDCcy = "";
  var LastLinePrint = -1;/*что за строка была напечатана последней*/

  if(PrintCalcdComiss) // dan biq-11080
     if(bo_act_init == "ФИССиКО")
        cmd = DL_RSDCommand(
               string( "SELECT s.*   \n"
                      ,"      ,Dl.t_Code t_Dealcode   \n"
                      ,"      ,Contr.t_Number Contrnumber   \n"
                      ,"      ,Dl.t_Clientcontr t_Clientid   \n"
                      ,"      ,m.t_Code Comcode   \n"
                      ,"      ,(SELECT p.t_Shortname FROM Dparty_Dbt p WHERE p.t_Partyid = s.t_Payerid) Payername   \n"
                      ,"      ,(SELECT p.t_Shortname FROM Dparty_Dbt p WHERE p.t_Partyid = s.t_Receiverid) Receivername   \n"
                      ,"      ,(SELECT p.t_Ccy FROM Dfininstr_Dbt p WHERE p.t_Fiid = s.t_Fiid) Fiidccy   \n"
                      ,"  FROM Dsccommcalc_dbt s   \n"
                      ," INNER JOIN Dsfcontr_Dbt Contr ON (Contr.t_Id = s.t_Contrid)   \n"
                      ," INNER JOIN Ddvndeal_Dbt Dl ON (Dl.t_Id = s.t_Dealid)   \n"
                      ," INNER JOIN Dsfcomiss_Dbt m ON (m.t_Feetype = 1 AND m.t_Number = s.t_Commnumber)   \n"
                      ," WHERE s.t_GUID = ? AND s.t_Type = 1 \n"
                      ," ORDER BY CASE   \n"
                      ,"             WHEN Dl.t_Clientcontr > 0 THEN   \n"
                      ,"              1   \n"
                      ,"             ELSE   \n"
                      ,"              0   \n"
                      ,"          END   \n"
                      ,"         ,Contr.t_Id   \n"
                      ,"         ,s.t_Commnumber   \n"
                      ,"         ,s.t_Receiverid   \n"
                      ,"         ,Dl.t_Code   \n"
                     ));
     else
        cmd = DL_RSDCommand(" select S.*, tick.t_DealCode, Contr.t_Number ContrNumber, tick.t_ClientID, M.t_Code ComCode, "+
                            "        (select p.t_ShortName from dparty_dbt p where p.t_PartyID = S.t_PAYERID) PAYERName, "+
                            "        (select p.t_ShortName from dparty_dbt p where p.t_PartyID = S.t_RECEIVERID) RECEIVERName, "+
                            "        (select p.t_Ccy from dfininstr_dbt p where p.t_FIID = S.t_FIID) FIIDCcy "+
                            "   from DSCCOMMCALC_DBT S, DSFCONTR_DBT Contr, ddl_tick_dbt tick, DSFCOMISS_DBT M "+
                            "  where S.t_GUID = ? " +
                            "    and S.t_Type = 1 " +
                            "    and S.t_CONTRID = Contr.t_ID "+
                            "    and tick.t_DealID = S.t_DEALID "+
                            "    and M.T_FEETYPE   = "+SF_FEE_TYPE_PERIOD+  
                            "    and M.T_NUMBER    = S.T_COMMNUMBER " +
                            " order by case when tick.t_ClientID > 0 then 1 else 0 end, Contr.t_ID, S.t_COMMNUMBER, S.t_RECEIVERID, tick.t_DealCode "
                           );
     end;
     cmd.addParam(Data.GUID);
     DataSet = cmd.execute();

     ResultCaptureStart();
     while( DataSet.MoveNext() )

       if( not IsHeaderPrint )
          PrintHeader(Data.OperDate,Data.Method);
          PrintSubHeader(IIF(DataSet.ClientID>0,true,false));
          LastLinePrint = SUBHEAD;
       else
          if( ((PrevContrID > 0) and (PrevContrID != DataSet.ContrID)) OR
              ((PrevComNumber > 0) and (PrevComNumber != DataSet.COMMNUMBER)) OR
              ((PrevClientID <= 0 ) and (DataSet.ClientID > 0))
            )
             PrintItog(PrevComCode,SumItog,NDSItog,FIIDCcy);
             LastLinePrint = ITOG;
             SumItog  =  NDSItog = $0;
             PrevComNumber = -1;
          end;

          if( (PrevClientID <= 0 ) and (DataSet.ClientID > 0) )
             PrintSubHeaderDelim_1();
             PrintSubHeader(true);
             LastLinePrint = SUBHEAD;
          end;
       end;

       if( PrevContrID != DataSet.ContrID )
          PrintSubHeaderDelim(LastLinePrint);
          PrintContrNumber(DataSet.PAYERName,DataSet.ContrNumber);
          LastLinePrint = CONTR;
       end;

       PrintDelim(LastLinePrint);
       PrintLine(IIF(PrevComNumber==DataSet.COMMNUMBER,"",DataSet.ComCode),IIF(PrevComNumber==DataSet.COMMNUMBER,"",DataSet.ReceiverName),DataSet.DealCode, DataSet.IsBack, DataSet.SUMMA, DataSet.NDS, DataSet.FIIDCcy );
       LastLinePrint = COMMISS;

       PrevComNumber = DataSet.COMMNUMBER;
       PrevComCode   = DataSet.ComCode;
       PrevContrID   = DataSet.ContrID;
       IsHeaderPrint = true;
       PrevClientID  = DataSet.ClientID;

       SumItog  = SumItog + DataSet.SUMMA; 
       NDSItog  = NDSItog + DataSet.NDS; 
       FIIDCcy  = DataSet.FIIDCcy; 
     end;
  else
     if(bo_act_init == "ФИССиКО")
        cmd = DL_RSDCommand("SELECT p.t_Ccy, SUM(s.t_Summa) as SumItog, SUM(s.t_NDS) as NDSItog " +
                            "  FROM Dsccommcalc_dbt s, Dsfcontr_Dbt Contr, Ddvndeal_Dbt Dl, Dsfcomiss_Dbt m, Dfininstr_Dbt p " +
                            " WHERE s.t_GUID = ? AND s.t_Type = 1 " +
                            "   AND Contr.t_Id = s.t_Contrid " +
                            "   AND Dl.t_Id = s.t_Dealid " +
                            "   AND m.t_Feetype = 1 " +
                            "   AND m.t_Number = s.t_Commnumber " +
                            "   AND p.t_Fiid = s.t_Fiid " +
                            " GROUP BY p.t_Ccy "
                           );
     else
        cmd = DL_RSDCommand(" select p.t_Ccy, SUM(S.t_Summa) as SumItog, SUM(S.t_NDS) as NDSItog "+
                            "   from DSCCOMMCALC_DBT S, DSFCONTR_DBT Contr, ddl_tick_dbt tick, DSFCOMISS_DBT M, dfininstr_dbt p "+
                            "  where S.t_GUID = ? " +
                            "    and S.t_Type = 1 " +
                            "    and S.t_CONTRID = Contr.t_ID " +
                            "    and tick.t_DealID = S.t_DEALID " +
                            "    and M.T_FEETYPE = "+SF_FEE_TYPE_PERIOD+  
                            "    and M.T_NUMBER = S.T_COMMNUMBER " +
                            "    and p.t_FIID = S.t_FIID " +
                            " GROUP BY p.t_Ccy "
                           );
     end;
     cmd.addParam(Data.GUID);
     DataSet = cmd.execute();

     ResultCaptureStart();
     while( DataSet.MoveNext() )
        PrintItogMinimal(DataSet.SumItog, DataSet.NDSItog, DataSet.Ccy, IsHeaderPrint);
        IsHeaderPrint = true;  
     end;
  end;

  if( IsHeaderPrint )
     if(PrintCalcdComiss)
        PrintItog(PrevComCode,SumItog,NDSItog,FIIDCcy);
        PrintFooter();
        after_44_footer();
     end;
  else
      [
       #
      ]("Подходящих для расчета/удаления комиссий не найдено.":l);
  end;

  cmd = DL_RSDCommand("select t_Str from dsccommcalc_dbt where t_GUID = ? and t_Type = 2");
  cmd.AddParam(Data.GUID);

  if(cmd.GetCount() > 0)
    [

                   Ошибки расчета/удаления комиссий по сделкам:
    ]; 

    DataSet = cmd.Execute();

    while(DataSet.moveNext())
      [
       #
      ](DataSet.Str:l);
      i = i + 1;
    end;
  end;
  ResultCaptureStop();

  if(Data.PrintRep)
     log2file();
  end;
end;

private macro УдалитьКомиссию(ID:integer,ErrMsg:@string)
   var stat = DL_DeleteComisByID(ID);
   ErrMsg = GetErrMsg();
   return stat;
end;

/*единый кусок для проверки, что
 лот по 1ч имеет статус "Не поставлен" или не существует ИЛИ
 лот по 2ч имеет статус "Не поставлен" или не существует для случая, когда выставлен регсбор по 2 ч, задан регистратор и договор с ним
используется на этапе расчета и в сишнике при отборе сделок в список для выбора из панели расчетов комиссий по сделкам
*/
private macro GetLotsByDealNotDelivery
   return " ( t.t_ClientID > 0 " +
          "   or NOT EXISTS(SELECT 1 " +
          "                   FROM dpmwrtsum_dbt s, ddlrq_dbt rq "+
          "                  WHERE s.t_DocKind   = "+DLDOC_PAYMENT+
          "                    AND s.t_DocID     = rq.t_ID "+
          "                    AND rq.t_DOCKIND  = t.t_BofficeKind "+
          "                    AND s.t_DealID    = rq.t_DOCID "+
          "                    AND s.t_DealID    = t.t_DealID "+
          "                    AND rq.T_TYPE     =  "+DLRQ_TYPE_DELIVERY+
          "                    AND rq.T_DEALPART =  1 "+
          "                    AND rq.t_SUBKIND  = " +DLRQ_SUBKIND_AVOIRISS+
          "                    AND s.t_State != "+PM_WRTSUM_NOTFORM+") "+
          "   or (rsb_secur.IsRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(t.t_DealType,t.t_BofficeKind)))=1 and "+
          "       NOT EXISTS(SELECT 1 " +
          "                    FROM dpmwrtsum_dbt s, ddlrq_dbt rq, ddl_leg_dbt leg2 "+
          "                   WHERE s.t_DocKind      = "+DLDOC_PAYMENT+
          "                     AND s.t_DocID        = rq.t_ID "+
          "                     AND rq.t_DOCKIND     = t.t_BofficeKind "+
          "                     AND s.t_DealID       = rq.t_DOCID "+
          "                     AND s.t_DealID       = t.t_DealID "+
          "                     AND rq.T_TYPE        = "+DLRQ_TYPE_DELIVERY+
          "                     AND rq.T_DEALPART    = 2 "+
          "                     AND rq.t_SUBKIND     = " +DLRQ_SUBKIND_AVOIRISS+
          "                     AND leg2.t_DealID    = t.t_DealID "+
          "                     AND leg2.t_LegID     = 0 "+
          "                     AND leg2.t_LegKind   = "+LEG_KIND_DL_TICK_BACK+
          "                     AND leg2.t_PayRegTax = 'X' "+
          "                     AND leg2.t_RegistrarContrID > 0 "+
          "                     AND s.t_State != "+PM_WRTSUM_NOTFORM+") "+
          "     ) "+                    
          " ) ";
end;

/*расчет\пересчет*/
private macro RunCalculation(Data, NumPack:integer)
  var rComiss  = TRecHandler( "sfcomiss.dbt" );
  var query, cmd, DataSet, stat = 0, Num  = 0, errMes = "", ErrMsg = "";
  VAR ArrComSums = TArray(), i = 0, PLANPAYDATE = Date(0,0,0);
  var Progress = TProgressBar, CheckExist = false;                                                                                                    
  var ByDeals = false;

  if(ValType(NumPack) == V_UNDEF)
    NumPack = 0;
  end;

  if(NumPack > 0)
    cmd = DL_RSDCommand("select count(1) as cnt from dspcalccommdeal_dbt where t_GUID = ? and t_NumPack = ? and t_DealID > 0 and ROWNUM = 1");
    cmd.AddParam(Data.GUID);
    cmd.AddParam(NumPack);

    DataSet = cmd.Execute();
    if((DataSet.moveNext()) and (DataSet.cnt > 0))
      ByDeals = true;
    end;
  end;


  cmd = DL_RSDCommand();

  query =   " with scc as (select distinct t_ContrID, t_ConComID "
          + "                from dspcalccommdeal_dbt "
          + "               where t_GUID = ? ";

  cmd.AddParam(Data.GUID);

  if(NumPack > 0)
    query = query + " and t_NumPack = ? ";

    cmd.AddParam(NumPack);
  end;

  query = query + ") ";

  query =  query
          + " select scc.t_ContrID, scc.t_ConComID, concom.t_CommNumber, COMISS.t_ReceiverID, COMISS.T_FIID_COMM, Contr.t_PartyID, "
          + "        COMISS.t_Code AS CODECOMISS, Contr.t_Number AS NUMBERCONTR, "
          + "        concom.t_IsBankExpenses, COMISS.t_ServiceKind, "
          + "        CASE WHEN Contr.T_PARTYID > 0 THEN (SELECT OC.T_CODE FROM DOBJCODE_DBT OC WHERE OC.T_OBJECTTYPE = 3 AND OC.T_CODEKIND = 1 AND OC.T_STATE = 0 AND OC.T_OBJECTID = Contr.T_PARTYID) ELSE CHR(0) END AS CODEPAYER, " 
          + "        CASE WHEN Contr.T_PARTYID > 0 THEN (SELECT PR.T_SHORTNAME FROM DPARTY_DBT PR WHERE Contr.T_PARTYID = PR.T_PARTYID ) ELSE CHR(0) END AS PAYER " 
          + "   from scc, dsfcontr_dbt contr, dsfconcom_dbt concom, dsfcomiss_dbt comiss "
          + "  where concom.t_ID = scc.t_ConComID "
          + "    and comiss.t_FeeType = concom.t_FeeType " 
          + "    and comiss.t_Number = concom.t_CommNumber "
          + "    and contr.t_ID = scc.t_ContrID ";

  //добавляем подсчет записей для отоображения индикатора процесса выполнения
  query = "select tb.*, count (1) over () Cnt from (" + query + ") tb order by tb.t_ContrID, tb.t_commnumber";

  DataSet = cmd.execute(query);

  while( DataSet.MoveNext() )
     if (num == 0)
        num = Int(DataSet.Cnt);
        Progress.Init( Num, "", "Просмотр и расчет периодических комиссий" );
     end;

     rComiss.Clear();
     if( SfGetComiss( SF_FEE_TYPE_PERIOD, DataSet.CommNumber, rComiss ) == false ) 
        InsertError(Data.GUID, "Не найдена периодическая комиссия с номером <"+ DataSet.CommNumber +"> " );
        Progress.Use;                                                                                                                    
        InsertRecs(Data.GUID);
        continue;
     end;   

     if( ПроверитьКатегориюКомиссии( rComiss, "DC", OBJGROUP_SFCOM_CALCBYBO ) == false )
        Progress.Use;                                                                                                                    
        continue;
     end;

        var ContrID = DataSet.ContrID;
        var BankContrID = 0;

     SetGlobalDealIDForCalc(Data.DealID);

     ArrComSums.Size = 0;
     if( CalcPeriodCommByContr_TMP( DataSet.ContrID, DataSet.ConComID, Data.OperDate, errMes, ArrComSums ) != 0 )
        InsertError(Data.GUID, "Ошибка расчета сумм периодической комиссии с номером "+DataSet.CommNumber+".\n" + errMes) ;
        Progress.Use;                                                                                                                    
        InsertRecs(Data.GUID);
        continue;
     end; 

     SetGlobalDealIDForCalc(0);
     i = 0;
     while( i < ArrComSums.Size() )
        InsertRecs(Data.GUID);
        var rDLComis = TRecHandler( "dlcomis.dbt" );
        var rRqAcc = TRecHandler( "dlrqacc.dbt" );
        var rSA = TRecHandler( "settacc.dbt" );
        var IsBack = false;

        var DataTick, query_tick, cmd_tick = DL_RSDCommand();

        query_tick = "select tick.*, leg.*, "+
                        "       (CASE WHEN '"+DataSet.IsBankExpenses+"' = 'X' THEN 'X' ";

           if(DataSet.ReceiverID != {OurBank})
             query_tick = query_tick + " WHEN RSB_SECUR.GetMainObjAttr("+OBJTYPE_SECDEAL+", LPAD(tick.t_DealID, 34, '0'), 53, tick.t_DealDate) = 2 /*Первичное размещение = Да*/ THEN 'X' ";
           end;

           query_tick = query_tick +
                        "             ELSE CHR(0) END) as IsBankExpenses " +
                     "  from ddl_tick_dbt tick, ddl_leg_dbt leg "+
                     "  where tick.t_DealID = leg.t_DealID "+
                     "    and leg.t_ID      = ? ";

        cmd_tick.addParam(int(ArrComSums[i].rec.baseobjectid) );

        if((NumPack > 0) and (ByDeals == true))
          query_tick = query_tick + " and Exists(select 1 "
                                  + "              from dspcalccommdeal_dbt scc "
                                  + "             where scc.t_GUID = ? "
                                  + "               and scc.t_NumPack = ? "
                                  + "               and scc.t_ContrID = ? " 
                                  + "               and scc.t_ConComID = ? "
                                  + "               and scc.t_DealID = tick.t_DealID"
                                  + "           ) ";

          cmd_tick.addParam(Data.GUID);
          cmd_tick.addParam(NumPack);
          cmd_tick.addParam(int(DataSet.ContrID));
          cmd_tick.addParam(int(DataSet.ConComID));
        end;

        DataTick = cmd_tick.execute(query_tick);

        PLANPAYDATE = Date(0,0,0);

        if( DataTick.MoveNext() )

           if( (Data.DealID > 0) and (DataTick.DealID!=Data.DealID) )
              i = i + 1;
              continue;
           end;

              if(DataTick.IsBankExpenses == SET_CHAR)
                if(BankContrID > 0)
                  ContrID = BankContrID;
                else
                  var query_sf, ds_sf, cmd_sf;

                  query_sf =   " select sf.t_ID "
                             + "   from dsfcontr_dbt sf "
                             + "  where sf.t_PartyID = " + {OurBank}
                             + "    and sf.t_ContractorID = ? "
                             + "    and sf.t_ServKind = ? "
                             + "    and sf.t_DateBegin <= ? "
                             + "    and (sf.t_DateClose = "+GetSQLDate(date(0,0,0))+" or sf.t_DateClose >= ?)";

                  cmd_sf = DL_RSDCommand(query_sf);

                  cmd_sf.AddParam(DataSet.ReceiverID);
                  cmd_sf.AddParam(DataSet.ServiceKind);
                  cmd_sf.AddParam(Data.OperDate);
                  cmd_sf.AddParam(Data.OperDate);

                  ds_sf = cmd_sf.Execute();
                  if(ds_sf.moveNext())
                    ContrID = ds_sf.ID;
                    BankContrID = ContrID;
                  else
                    InsertError(Data.GUID, "Не найден договор обслуживания банка с получателем комиссии <"+ DataSet.CommNumber +"> " );
                    Progress.Use;
                    i = i + 1;                                                                                                                    
                    continue;
                  end;
                end;
              end;

           if( int(ArrComSums[i].rec.BaseObjectType) == LEG_KIND_DL_TICK_BACK )
              IsBack = true;
           else
              IsBack = false;
           end;

           if( Data.Method == METHOD_CALC )
              if( DataSet.ReceiverID == {OurBank} ) 
/*  ААИ                                     
                 PLANPAYDATE = Data.OperDate;
*/
                 PLANPAYDATE = GetDateAfterWorkDays(SQL_ConvTypeDate(DataTick.expiry), 0);
              elif( (DataTick.CommDate != Date(0,0,0)) and  (Data.OperDate > DataTick.CommDate) )
                 PLANPAYDATE = DataTick.CommDate;
              else
                 PLANPAYDATE = Data.OperDate;
              end;

              if( DL_FindDLCOMIS(DataTick.BofficeKind, DataTick.DealID, ContrID, SF_FEE_TYPE_PERIOD, DataSet.CommNumber, PLANPAYDATE, IsBack, 0, "") == 0 )/*нашли комиссию*/
                 i = i + 1;
                 continue;
              end;
// ААИ
              if(rcomiss.rec.fiid_comm != ArrComSums[i].rec.DocFiid)
                 i = i + 1;
                 continue;
              end;

              if(DataSet.ContrID != DataTick.ClientContrId )
                 i = i + 1;
                 continue;
              end;
           else/*пересчет*/
              stat = 0;
              if( (DL_FindDLCOMIS(DataTick.BofficeKind, DataTick.DealID, ContrID, SF_FEE_TYPE_PERIOD, DataSet.CommNumber, Data.OperDate, IsBack, 0, "", rDLComis) == 0) and 
                  ((stat = SP_CheckExistGrDealByCom(rDLComis,DLGRACC_STATE_FACTEXEC,CheckExist)) == 0) and (CheckExist == false) and 
                  (rDLComis.rec.INPUTMEDIUM == GetInputMedium(INPUTMEDIUM_CALC)) )/*не нашли комиссию*/
                 PLANPAYDATE = rDLComis.rec.PLANPAYDATE;
                 if( УдалитьКомиссию(rDLComis.rec.ID,@ErrMsg) != 0 )
                    InsertError(Data.GUID, "Нельзя удалять комиссию с номером \""+DataSet.COMMNUMBER+"\" по сделке с ID = "+string(DataTick.DealID)+": "+ErrMsg) ;
                    i = i + 1;
                    continue;
                 end;
              else
                 if( stat != 0 )
                    ErrMsg = GetErrMsg();
                    InsertError(Data.GUID, "Ошибка определения оплаченности комиссии с номером \""+DataSet.COMMNUMBER+"\" по сделке с ID = "+string(DataTick.DealID)+": "+ErrMsg) ;
                 end;
                 i = i + 1;
                 continue;
              end;
           end;
  
           rRqAcc.Clear();
           if(ПолучитьПлатежныеРеквизитыСубъектаПоСделке(int(DataTick.BofficeKind), DataTick.DealID, DataSet.ReceiverID, DLRQ_SUBKIND_CURRENCY, DataSet.FIID_COMM, DLRQ_TYPE_COMISS, rRqAcc) != 0) //нет подходящих платежных реквизитов
             rSA.Clear();
             if(SP_GetPartySETTACC(DataSet.ReceiverID, 0, DataSet.FIID_COMM, FIKIND_CURRENCY, PTSK_STOCKDL, rSA, DataTick.DealType) == 0)
               //создать новые платежные реквизиты
               rRqAcc.rec.ID               = 0;     
               rRqAcc.rec.DocKind          = int(DataTick.BofficeKind);     
               rRqAcc.rec.DocID            = DataTick.DealID;     
               rRqAcc.rec.SubKind          = DLRQ_SUBKIND_CURRENCY;     
               rRqAcc.rec.Type             = DLRQ_TYPE_COMISS;     
               rRqAcc.rec.Party            = DataSet.ReceiverID;     
               rRqAcc.rec.FIID             = rSA.rec.FIID;                 
               rRqAcc.rec.BankID           = rSA.rec.BankID;               
               rRqAcc.rec.Chapter          = rSA.rec.Chapter;              
               rRqAcc.rec.Account          = rSA.rec.Account;         
               rRqAcc.rec.BankCodeKind     = rSA.rec.BankCodeKind;         
               rRqAcc.rec.BankCode         = rSA.rec.BankCode;        
               rRqAcc.rec.BankName         = rSA.rec.BankName;        
               rRqAcc.rec.BankCorrID       = rSA.rec.BankCorrID;           
               rRqAcc.rec.BankCorrCodeKind = rSA.rec.BankCorrCodeKind;     
               rRqAcc.rec.BankCorrCode     = rSA.rec.BankCorrCode;    
               rRqAcc.rec.BankCorrName     = rSA.rec.BankCorrName;    
               rRqAcc.rec.CorrAcc          = rSA.rec.CorrAcc;         

               if( DL_InsertDLRQACC(rRqAcc) )
                 ErrMsg = GetErrMsg();
                 InsertError(Data.GUID, "Ошибка вставки платежных реквизитов для комиссии с номером \""+DataSet.COMMNUMBER+"\" по сделке с ID = "+string(DataTick.DealID)+": "+ErrMsg) ;
                 i = i + 1;
                 continue;
               end;
             else
               InsertError(Data.GUID, "Не найдена СПИ по деньгам для субъекта с ID = "+DataSet.ReceiverID);
               i = i + 1;
               continue;
             end;
           end;

           rDLComis.Clear();
           var FD = SPFirstDoc(LEG_KIND_DL_TICK, DataTick.DealID);
           var CountRepoDays = 1;
           if(( IsRepo(FD.Group)) and ((index(rComiss.rec.Code, "БрокерКомпенсац") == 0)))   
              var FD2 = SPFirstDoc(LEG_KIND_DL_TICK_BACK, DataTick.DealID);
                  CountRepoDays = FD2.dl_leg.rec.expiry - FD.dl_leg.rec.expiry;
                  If(CountRepoDays == 0)
                     CountRepoDays = 1;
                  end;
           else
              CountRepoDays = 1;
           end;
  

           rDLComis.rec.ID          = 0;
           rDLComis.rec.DOCKIND     = int(DataTick.BofficeKind);
           rDLComis.rec.DOCID       = DataTick.DealID;
           rDLComis.rec.CONTRACT    = ContrID;
           rDLComis.rec.FEETYPE     = SF_FEE_TYPE_PERIOD;
           rDLComis.rec.COMNUMBER   = DataSet.CommNumber;
           rDLComis.rec.SUM         = (ArrComSums[i].rec.CommSum+ArrComSums[i].rec.NDSSum)*CountRepoDays;
           rDLComis.rec.NDS         = ArrComSums[i].rec.NDSSum;
           rDLComis.rec.DATE        = Data.OperDate;
           rDLComis.rec.PLANPAYDATE = PLANPAYDATE;

           rDLComis.rec.INPUTMEDIUM = GetInputMedium(INPUTMEDIUM_CALC);
           rDLComis.rec.ServOperID  = 0;
           rDLComis.rec.IsBack      = IIF(IsBack,SET_CHAR,UNSET_CHAR);

              rDLComis.rec.ClientContrID = DataSet.ContrID;
              rDLComis.rec.IsBankExpenses= DataTick.IsBankExpenses;

           rDLComis.rec.FACTPAYDATE = rDLComis.rec.PLANPAYDATE;

           if((Data.IsWeb == true) and (Data.IsExec == false)) // Запущено из Web без выполнения
              InsertWebRep(Data.GUID, String("  ",DataSet.CODECOMISS:10," | ",DataSet.NUMBERCONTR:22, " | ", DataSet.CODEPAYER:15, " | ", DataSet.PAYER:24, " |"));
           end;

           if((Data.IsWeb == false) or (Data.IsExec == true)) // Запущено из EasyWin или из Web на выполнение
             if( DL_InsertComis(rDLComis) )
                ErrMsg = GetErrMsg();
                InsertError(Data.GUID, "Ошибка вставки комиссии с номером \""+DataSet.COMMNUMBER+"\" по сделке с ID = "+string(DataTick.DealID)+": "+ErrMsg) ;
                i = i + 1;
                continue;
             end;

             InsertIntoProtocol(Data.GUID, DataTick.DealID, DataSet.ContrID, DataSet.PartyID, DataSet.ReceiverID, ArrComSums[i].rec.CommSum*CountRepoDays, ArrComSums[i].rec.NDSSum, DataSet.FIID_COMM, DataSet.COMMNUMBER, IsBack );
           end;  

        end;

        i = i + 1;
     end;
  
     Progress.Use;                                                                                                                    
  
  end;
  Progress.Remove;

  return 0;
end;

private macro RunDelete(Data)
  var rComiss  = TRecHandler( "sfcomiss.dbt" );
  var query, cmd, DataSet, stat = 0, Num  = 0, ErrMsg = "";
  var Progress = TProgressBar;                                                                                                    
  
  cmd = DL_RSDCommand();

  query = " select com.*, Contr.T_PartyID, M.T_ReceiverID receiverid1, Contr.t_ID ContrID, M.T_FIID_COMM "+
          "   from DDLCOMIS_DBT com, DSFCOMISS_DBT M, DSFCONCOM_DBT concom, DSFCONTR_DBT Contr, DDL_TICK_DBT tick "+
          "  where rsb_secur.CheckExistGrDealByCom(Com.T_DocKind, Com.T_DocID, Com.T_Contract, Com.T_PLANPAYDATE, "+DLGRACC_STATE_FACTEXEC+") = 0 "+
          "    and com.T_DATE              = ? "+
          "    and com.T_INPUTMEDIUM       = '"+GetInputMedium(INPUTMEDIUM_CALC)+"'"+
          "    and M.T_FEETYPE             = Com.T_FEETYPE "+  
          "    and M.T_NUMBER              = Com.T_COMNUMBER " +
          "    and Contr.t_ID              = Com.T_ClientContrID " +
          "    and Com.T_DOCKIND           = tick.t_BofficeKind "+
          "    and Com.T_DOCID             = tick.t_DealID "+
          "    and concom.t_ObjectId       = Contr.t_ID "+
          "    and concom.t_ObjectType     = ? "+
          "    and concom.t_FeeType        = Com.T_FEETYPE "+
          "    and concom.t_calcPeriodType = ? "+ 
          "    and M.t_ServiceKind         = ? "+ 
          "    and M.t_FeeType             = concom.t_FeeType "+ 
          "    and M.t_Number              = concom.t_CommNumber ";

  cmd.addParam(Data.OperDate );        
  cmd.addParam(OBJTYPE_SFCONTR );        
  cmd.addParam(SF_FEE_TYPE_PERIOD );        
  cmd.addParam(SF_TYPE_PERIOD_DAY );        

  if( Data.DealID > 0 )
     query = query +
          "    and Com.T_DOCID       = ? ";

     cmd.addParam(Data.DealID );
  end;

  if( Data.ContractID > 0 )/*отбираем комиссии по указанному договору*/
     query = query +
          "    and Com.T_ClientContrID    = ? ";

     cmd.addParam(Data.ContractID );
  elif( Data.PartyID > 0 )/*только комиссии, где плательщик - заданный субъект*/
     query = query +
          "    and Contr.T_PartyID   = ? ";                       

     cmd.addParam(Data.PartyID );               
  end;

  if( Data.ConCom > 0 )/*только комиссии этого вида*/
     query = query +
          "    and M.T_NUMBER  = (select CM.t_CommNumber from DSFCONCOM_DBT CM where CM.t_ID = ? ) ";

     cmd.addParam(Data.ConCom );
  end;

  Num = int(cmd.GetCount(query));

  if( Num > 0 )
     DataSet = cmd.execute(query);
 
     Progress.Init( Num, "", "Удаление рассчитанных периодических комиссий" );                                                                                 
   
     while( DataSet.MoveNext() )
    
        rComiss.Clear();
        if( SfGetComiss( SF_FEE_TYPE_PERIOD, DataSet.ComNumber, rComiss ) == false ) 
           InsertError(Data.GUID, "Не найдена периодическая комиссия с номером <"+ DataSet.ComNumber +"> " );
           Progress.Use;                                                                                                                    
           continue;
        end;   
    
        if( ПроверитьКатегориюКомиссии( rComiss, "DC", OBJGROUP_SFCOM_CALCBYBO ) == false )
           Progress.Use;                                                                                                                    
           continue;
        end;

        if( УдалитьКомиссию(DataSet.ID,@ErrMsg) != 0 )
           InsertError(Data.GUID, "Нельзя удалять комиссию с номером \""+DataSet.COMNUMBER+"\" по сделке с ID = "+string(DataSet.DOCID)+": "+ErrMsg) ;
           Progress.Use;                                                                                                                    
           continue;
        else
           InsertRecs(Data.GUID);
        end;
        Progress.Use;                                                                                                                    
     end;

     Progress.Remove;                                                                                                                                                                                                                                                               
  end;

  return 0;
                                                                   
end;

private macro RunCalculation_BrkCur(Data)
  var rComiss  = TRecHandler( "sfcomiss.dbt" );
  var rDLComis = TRecHandler( "dlcomis.dbt" );
  var sqls, rsdc, rsds;
  var Num  = -1, errMes = "", IsDataExists = false, ErrMsg = "", stat = 0;
  VAR ArrComSums = TArray(), i = 0, r_com, IsBack = false, PLANPAYDATE;
  var Progress = TProgressBar, CheckExist = false;                                                                                                    
  var dtset = null;/*CHVA*/
  var _kind=0;/*CHVA*/
  var CountSwapDays = 1;

  // сохранить результат
  macro save_res(mes, all, err, wrn/*dan*/)
     if (all == 1)
        InsertRecs(Data.GUID);
     end;
     if (err == 1)
        InsertError(Data.GUID, mes) ;
     end;
     if (wrn == 1)
        InsertWrnRecs(Data.GUID) ;
     end;
  end;

  sqls = String( 
                 "SELECT Cms.t_Feetype Cms_Tp   \n"
                ,"      ,Cms.t_Number Cms_Nm   \n"
                ,"      ,Cms.t_Code Cms_Cd   \n"
                ,"      ,Cms.t_Fiid_Comm Cms_Fi   \n"
                ,"      ,Cms.t_ReceiverID   \n"
                ,"      ,Sfcc.t_Id Cms_Cntr_Id   \n"
                ,"      ,Sfc.t_Id Cntr_Id   \n"
                ,"      ,Sfc.t_Number Cntr_Nm   \n"
                ,"      ,Sfc.t_Partyid Cntr_Pt   \n"
                ,"      ,COUNT(1) Deal_Cnt   \n"  
                ,"      ,COUNT(1) OVER() Cnt_All   \n"
                ,"  FROM Ddvndeal_Dbt Dl   \n"
                ," INNER JOIN Ddvnfi_Dbt Dli   \n"
                ,"    ON (Dli.t_Dealid = Dl.t_Id)   \n"
                ," INNER JOIN Dsfcontr_Dbt Sfc   \n"
                ,"    ON (Sfc.t_Id = Dl.t_Clientcontr AND Sfc.t_Servkind = :p_srv_kd AND Sfc.t_Servkindsub = :p_srv_sb)   \n"
                ," INNER JOIN Dsfconcom_Dbt Sfcc   \n"
                ,"    ON (Sfcc.t_Objecttype = :p_obj_sf AND Sfcc.t_Objectid = Dl.t_Clientcontr AND Sfcc.t_Feetype = :p_fee_tp AND Sfcc.t_Calcperiodtype = :p_prd_tp)   \n"
                ," INNER JOIN Dsfcomiss_Dbt Cms   \n"
                ,"    ON (Cms.t_Feetype = Sfcc.t_Feetype AND Cms.t_Number = Sfcc.t_Commnumber AND Cms.t_Fiid_Comm = Dli.t_Pricefiid)   \n"
                ," WHERE Dl.t_Date = :p_Date   \n"
//                ,"   AND Dl.t_State != :p_dl_stt   \n"
                ,"   AND Dli.t_Type = :p_dl_prt   \n"
                ,"   AND Sfc.t_Dateconc <= Dl.t_Date   \n"
                ,"   AND Sfcc.t_Status = 0   \n"
//                ,"   AND Sfcc.t_Datebegin <= Dl.t_Date   \n"
                ,"   AND Cms.t_Formalg != :p_alg_fm   \n"
//                ,"   AND Sfcc.t_Commnumber in (1028, 1027, 1029, 1025, 1026) \n"
                ,"   AND Sfcc.t_Commnumber >1000 \n"
                ,"   AND CASE   \n"
                ,"          WHEN Sfcc.t_Dateend = To_Date('01010001', 'DDMMYYYY') THEN   \n"
                ,"           To_Date('31129999', 'DDMMYYYY')   \n"
                ,"          WHEN Sfcc.t_Dateend IS NULL THEN   \n"
                ,"           To_Date('31129999', 'DDMMYYYY')   \n"
                ,"          ELSE   \n"
                ,"           Sfcc.t_Dateend   \n"
                ,"       END >= Dl.t_Date   \n"
               );
  rsdc = rsdcommand(sqls);
  rsdc.AddParam("p_srv_kd", RSDBP_IN, 21);
  rsdc.AddParam("p_srv_sb", RSDBP_IN, 8);
  rsdc.AddParam("p_obj_sf", RSDBP_IN, OBJTYPE_SFCONTR);      
  rsdc.AddParam("p_fee_tp", RSDBP_IN, SF_FEE_TYPE_PERIOD);
  rsdc.AddParam("p_prd_tp", RSDBP_IN, SF_TYPE_PERIOD_DAY);
  rsdc.AddParam("p_date",   RSDBP_IN, Data.OperDate);
//  rsdc.AddParam("p_dl_stt", RSDBP_IN, 2);
  rsdc.AddParam("p_dl_prt", RSDBP_IN, 0);
  rsdc.AddParam("p_alg_fm", RSDBP_IN, SFCOMISS_FORMALG_MANUAL);

  if (Data.ConCom > 0)
     sqls = string(sqls, "   AND Sfcc.t_Id = :p_Cms_Cntr_Id   \n");
     rsdc.CmdText = sqls;
     rsdc.AddParam("p_Cms_Cntr_Id", RSDBP_IN, Data.ConCom);
  end;

  if (Data.PartyID > 0) //только комиссии, где плательщик - заданный субъект
     sqls = string(sqls, "   AND Dl.t_Client = :p_Clnt   \n");
     rsdc.CmdText = sqls;
     rsdc.AddParam("p_Clnt", RSDBP_IN, Data.PartyID);
  end;

  if (Data.ContractID > 0) //отбираем комиссии по указанному договору
     sqls = string(sqls, "   AND Dl.t_Clientcontr = :p_Cntr   \n");
     rsdc.CmdText = sqls;
     rsdc.AddParam("p_Cntr", RSDBP_IN, Data.ContractID);
  end;

  if (Data.DealID > 0)
     sqls = string(sqls, "   AND Dl.t_Id = :p_Deal   \n");
     rsdc.CmdText = sqls;
     rsdc.AddParam("p_Deal", RSDBP_IN, Data.DealID);
  end;
  sqls = string(sqls," GROUP BY Sfc.t_Id, Cms.t_Feetype, Cms.t_Number, Cms.t_Code, Cms.t_Fiid_Comm, Cms.t_ReceiverID, Sfcc.t_Id, Sfc.t_Partyid, Sfc.t_Number   \n" );
  rsdc.CmdText = sqls;
  rsds = rsdrecordset(rsdc, RSDVAL_CLIENT_IF_NEEDED, RSDVAL_FORWARD_ONLY);
  rsds.Open();
  while (rsds.MoveNext)
     if (Num < 0)
        Num = int(rsds.Value("Cnt_All"));
        Progress.Init( Num, "", "Просмотр и расчет периодических комиссий" );                                                                                 
     end;
     Progress.Use;                                                                                                                    
     rComiss.Clear();
     if (SfGetComiss(int(rsds.Value("Cms_Tp")), int(rsds.Value("Cms_Nm")), rComiss ) == false)
        save_res("Не найдена периодическая комиссия с номером <" + int(rsds.Value("Cms_Nm")) + "> ", 1, 1, 0/*dan*/);  
        continue;
     end;   
     if (ПроверитьКатегориюКомиссии( rComiss, "DC", OBJGROUP_SFCOM_CALCBYBO ) == false)
        continue;
     end;                                
     // Рассчитать комиссию
     if (Data.DealID > 0)
        SetGlobalDealIDForCalc(Data.DealID);
     else
        SetGlobalDealIDForCalc(0);
     end;   
     ArrComSums.Size = 0;
     if (CalcPeriodCommByContr_TMP( int(rsds.Value("Cntr_Id")), int(rsds.Value("Cms_Cntr_Id")), Data.OperDate, errMes, ArrComSums ) != 0)
        save_res("Ошибка расчета сумм периодической комиссии с номером " + int(rsds.Value("Cms_Nm")) + ".\n" + errMes, 1, 1, 0/*dan*/);
        continue;
     end; 
     SetGlobalDealIDForCalc(0);
     i = 0;                 
     // из полученного расчета выбрать нужное
     while (i < ArrComSums.Size)
        r_com = ArrComSums[i];
        i = i + 1;
        // если указана сделка, то из массива обработать строку для сделки                          
        if ((Data.DealID > 0) and (r_com.rec.baseobjectid != Data.DealID))
           continue;
        end;                                           

        /*CHVA 515379*/
         dtset = TRsbDataSet("select t_kind from ddvndeal_dbt where t_id =" + r_com.rec.BaseObjectId);
        if (dtset.movenext())
          _kind = dtset.kind;
        end;    
        /*CHVA 515379*/   
         if(_kind == 32715)
        // в массиве комиссий выбрать только совпадающие с комиссией валюты
           if (r_com.rec.baseamountfiid != int(rsds.Value("Cms_Fi")))
             continue;
           end;
        else
        if (r_com.rec.docfiid != int(rsds.Value("Cms_Fi")))
           continue;
        end;
        end;
        /*CHVA 515379*/
        InsertRecs(Data.GUID);

        // найти запись о комиссии в сделке
        PLANPAYDATE = Data.OperDate;
        rDLComis.Clear;   
        if (DL_FindDLCOMIS(r_com.rec.BaseObjectType, r_com.rec.BaseObjectId, int(rsds.Value("Cntr_Id")), int(rsds.Value("Cms_Tp")), int(rsds.Value("Cms_Nm")), PLANPAYDATE, IsBack, 0,"", rDLComis) != 0)
           rDLComis.Clear;
        end;
        // расчет
        if (Data.Method == METHOD_CALC)
           // комиссия уже есть
           if (rDLComis.rec.ID > 0)
              continue;
           end;
        else   //пересчет
           stat = 0;
           if ((rDLComis.rec.FactPayDate != null) and (rDLComis.rec.FactPayDate != nullval) and (rDLComis.rec.DATE != date(0,0,0)) and (rDLComis.rec.FactPayDate >= rDLComis.rec.DATE) )
              save_res("Нельзя удалять комиссию с номером \"" + int(rsds.Value("Cms_Nm")) + "\" по сделке с ID = " + r_com.rec.BaseObjectId + ": " + ErrMsg, 0, 1, 0/*dan*/);
              continue;
           end;
           if ((rDLComis.rec.ID > 0) and (УдалитьКомиссию(rDLComis.rec.ID, @ErrMsg) != 0))
              save_res("Ошибка удаления комиссии \"" + int(rsds.Value("Cms_Nm")) + "\" по сделке с ID = " + r_com.rec.BaseObjectId + ": " + ErrMsg, 0, 1, 0/*dan*/);
              continue;
           end;
        end;
        
        var FD = DVFirstDocFXDeal(r_com.rec.BaseObjectType/*DL_DVFXDEAL*/, int(r_com.rec.BaseObjectId));
        var FD2;

        if( ((FD.deal.rec.dvkind == 3) or (FD.deal.rec.dvkind == 6)) and (index(rComiss.rec.Code, "БрокерКомпенсацВ") == 0) )
           FD2 = DVFirstDocFXDeal(r_com.rec.BaseObjectType/*DL_DVFXDEAL*/, int(r_com.rec.BaseObjectId), 2);
           CountSwapDays = FD2.nfi.rec.execdate - FD.nfi.rec.execdate;
           If(CountSwapDays == 0)
              CountSwapDays = 1;
           end;
        else
           CountSwapDays = 1;
        end;

        rDLComis.Clear();
        rDLComis.rec.ID          = 0;
        rDLComis.rec.DOCKIND     = r_com.rec.BaseObjectType;
        rDLComis.rec.DOCID       = r_com.rec.BaseObjectId;
        rDLComis.rec.CONTRACT    = int(rsds.Value("Cntr_Id"));
        rDLComis.rec.FEETYPE     = int(rsds.Value("Cms_Tp"));
        rDLComis.rec.COMNUMBER   = int(rsds.Value("Cms_Nm"));
        rDLComis.rec.SUM         = (r_com.rec.CommSum + r_com.rec.NDSSum)*CountSwapDays;
        rDLComis.rec.NDS         = r_com.rec.NDSSum;
        rDLComis.rec.DATE        = Data.OperDate;
        rDLComis.rec.PLANPAYDATE = PLANPAYDATE;
        rDLComis.rec.INPUTMEDIUM = GetInputMedium(INPUTMEDIUM_CALC);
        rDLComis.rec.ServOperID  = 0;
        rDLComis.rec.IsBack      = IIF(IsBack, SET_CHAR, UNSET_CHAR);
        rDLComis.rec.Comtype     = 0;
        
        //rDLComis.rec.FACTPAYDATE = rDLComis.rec.PLANPAYDATE;
        if ((Data.IsWeb == true) and (Data.IsExec == false)) // Запущено из Web без выполнения
           InsertWebRep(Data.GUID, String("  ",rsds.Value("Cms_Cd"):10," | ",rsds.Value("Cntr_Nm"):22, " | ", mkstr(" ", 15), " | ", mkstr(" ", 24), " |\n"));
        end;
        if ((Data.IsWeb == false) or (Data.IsExec == true)) // Запущено из EasyWin или из Web на выполнение
           if (DL_InsertComis(rDLComis))
              ErrMsg = GetErrMsg();
              save_res("Ошибка вставки комиссии с номером \""+  int(rsds.Value("Cms_Nm")) + "\" по сделке с ID = " + r_com.rec.BaseObjectId + ": " + ErrMsg, 0, 1, 0/*dan*/);
              continue;
           end;
           InsertIntoProtocol(Data.GUID, r_com.rec.BaseObjectId, int(rsds.Value("Cntr_Id")), int(rsds.Value("Cntr_Pt")), int(rsds.Value("t_ReceiverID")), r_com.rec.CommSum*CountSwapDays, r_com.rec.NDSSum, int(rsds.Value("Cms_Fi")), int(rsds.Value("Cms_Nm")), IsBack);
           IsDataExists = true;
        end;  
     end;
  end;
  if (Num > 0)
     Progress.Remove;                                                                                                                                                                                                                                                               
  end;

  return 0;
end;

private macro RunDelete_BrkCur(Data)
  var rComiss  = TRecHandler( "sfcomiss.dbt" );
  var sqls, rsdc, rsds, Num = -1, IsDataExists = false, ErrMsg = "";
  var Progress = TProgressBar;                                                                                                    

  sqls = String( 
                 "SELECT Cmf.t_Feetype    Cms_Tp   \n"
                ,"      ,Cmf.t_Comnumber  Cms_Nm   \n"
                ,"      ,Cms.t_Fiid_Comm  Cms_Fi   \n"
                ,"      ,Cms.t_Code       Cms_Cd   \n"
                ,"      ,Cmf.t_Id         Cmf_Id   \n"
                ,"      ,Sfc.t_Id         Cntr_Id   \n"
                ,"      ,Sfc.t_Number     Cntr_Nm   \n"
                ,"      ,Sfc.t_Partyid    Cntr_Pt   \n"
                ,"      ,Cms.t_Receiverid Rcvr_Id   \n"
                ,"      ,Cmf.t_Dockind    Deal_Tp   \n"
                ,"      ,Cmf.t_Docid      Deal_Id   \n"
                ,"      ,Cmf.t_Sum        Cmf_Sum   \n"
                ,"      ,Cmf.t_Nds        Cmf_Nds   \n"
                ,"      ,Cmf.t_Isback     Cmf_Bck   \n"
                ,"      ,COUNT(1) Over()  Cnt_All   \n"
                ,"  FROM Ddlcomis_Dbt Cmf   \n"
                ," INNER JOIN Dsfcomiss_Dbt Cms   \n"
                ,"    ON (Cms.t_Feetype = Cmf.t_Feetype AND Cms.t_Number = Cmf.t_Comnumber)   \n"
                ," INNER JOIN Dsfcontr_Dbt Sfc   \n"
                ,"    ON (Sfc.t_Id = Cmf.t_Contract)   \n"
                ," INNER JOIN Dsfconcom_Dbt Sfcc   \n"
                ,"    ON (Sfcc.t_Objecttype = :p_obj_sf AND Sfcc.t_Objectid = Cmf.t_Contract AND Sfcc.t_Feetype = Cmf.t_Feetype AND   \n"
                ,"       Sfcc.t_Commnumber = Cmf.t_Comnumber and (SFCC.T_DATEEND = To_Date('01010001', 'DDMMYYYY') or  SFCC.T_DATEEND >= Cmf.t_Date) )   \n"
                ," INNER JOIN Ddvndeal_Dbt Dl   \n"
                ,"    ON (Dl.t_Dockind = Cmf.t_Dockind AND Dl.t_Id = Cmf.t_Docid)   \n"
                ," WHERE Cms.t_Servicekind = :p_Srv_kd   \n"
                ,"   AND Cms.t_Servicesubkind = :p_Srv_sb   \n"
                ,"   AND Cmf.t_Inputmedium = :p_Inp_md    \n"
                ,"   AND Cmf.t_Feetype = :p_Cms_Tp   \n"
                ,"   AND Sfcc.t_Calcperiodtype = :p_Prd_tp   \n"
                ,"   AND Cmf.t_Date = :p_Date   \n"
                ,"   AND Nvl(Cmf.t_Factpaydate, To_Date('01010001', 'DDMMYYYY')) = To_Date('01010001', 'DDMMYYYY')   \n"
               );

  rsdc = rsdcommand(sqls);
  rsdc.AddParam("p_obj_sf", RSDBP_IN, OBJTYPE_SFCONTR);
  rsdc.AddParam("p_Srv_kd", RSDBP_IN, 21);
  rsdc.AddParam("p_Srv_sb", RSDBP_IN, 8);
  rsdc.AddParam("p_Inp_md", RSDBP_IN, GetInputMedium(INPUTMEDIUM_CALC));
  rsdc.AddParam("p_Cms_Tp", RSDBP_IN, SF_FEE_TYPE_PERIOD);
  rsdc.AddParam("p_Prd_tp", RSDBP_IN, SF_TYPE_PERIOD_DAY);
  rsdc.AddParam("p_Date",   RSDBP_IN, Data.OperDate);
  if (Data.DealID > 0)
     sqls = string(sqls, "   AND Cmf.t_Docid = :p_Deal_Id   \n");
     rsdc.CmdText = sqls;
     rsdc.AddParam("p_Deal_Id", RSDBP_IN, Data.DealID );
  end;
  if (Data.ContractID > 0)
     sqls = string(sqls, "   AND Cmf.t_Contract = :p_Cntr_Id   \n");
     rsdc.CmdText = sqls;
     rsdc.AddParam("p_Cntr_Id", RSDBP_IN, Data.ContractID);
  end;   
  if (Data.PartyID > 0)
     sqls = string(sqls, "   AND Sfc.t_Partyid = :p_Clnt   \n");
     rsdc.CmdText = sqls;
     rsdc.AddParam("p_Clnt", RSDBP_IN, Data.PartyID);
  end;
  if( Data.ConCom > 0 )
     sqls = string(sqls, "   AND Sfcc.t_Id = :p_Cms_Cntr_Id   \n");
     rsdc.CmdText = sqls;
     rsdc.AddParam("p_Cms_Cntr_Id", RSDBP_IN, Data.ConCom);
  end;
  rsds = rsdrecordset(rsdc, RSDVAL_CLIENT_IF_NEEDED, RSDVAL_FORWARD_ONLY);
  rsds.Open();
  while (rsds.MoveNext())
     if (Num < 0) 
        Num = int(rsds.Value("Cnt_All"));
        Progress.Init(Num, "", "Удаление рассчитанных периодических комиссий"); 
     end;
     Progress.Use;                                                                                                                    
     rComiss.Clear();
     if ( SfGetComiss(int(rsds.Value("Cms_Tp")), int(rsds.Value("Cms_Nm")), rComiss ) == false) 
        InsertError(Data.GUID, "Не найдена периодическая комиссия с номером <" + int(rsds.Value("Cms_Nm")) + "> " );
        continue;
     end;   
     if (ПроверитьКатегориюКомиссии( rComiss, "DC", OBJGROUP_SFCOM_CALCBYBO ) == false)
        continue;
     end;
     if (УдалитьКомиссию(int(rsds.Value("Cmf_Id")),@ErrMsg) != 0)
        InsertError(Data.GUID, "Ошибка удаления комиссии с номером \"" + int(rsds.Value("Cms_Nm")) + "\" по сделке с ID = " + int(rsds.Value("Deal_Id")) + ": " + ErrMsg) ;
        continue;
     else
        InsertRecs(Data.GUID);   
     end;
  end;                               
  if (Num > 0)
     Progress.Remove;                                                                                                                                                                                                                                                               
  end;

  return 0;
end;

macro СформироватьГруппыРасчета(Data, РазбиватьПоСделкам:bool)
  var query, cmd;

  RslDefCon.BeginTrans();

  cmd = DL_RSDCommand();
  
  //Сначала сформируем группы по договору и комиссии                                                                                           
  
//Chuikov BIQ-7247 доработка отбора данных для оптимизации
  query = " INSERT INTO DSPCALCCOMMDEAL_DBT (T_GUID, T_NUMPACK, T_CONTRID, T_CONCOMID, T_DEALID, T_NUM, T_RECEIVERID) "
          " select distinct ?, 0, Contr.t_ID, concom.t_ID, 0, 0, comiss.t_ReceiverID " +
//предварительно отберем сделки и данные для проверки релевантности комиссий
          " from (select decode(Tick.t_clientid, -1, 0, 1) IsClientDeal, " +
          "              rsb_secur.IsExchange(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(Tick.t_DealType, Tick.t_BofficeKind)), 1) IsExchange, " +
          "              rsb_secur.IsOutExchange(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(Tick.t_DealType, Tick.t_BofficeKind)), 1) IsOutExchange, " +
          "              rsb_secur.isRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(Tick.t_DealType, Tick.t_BofficeKind))) isRepo, " +
          "              case when rsb_secur.GetMainObjAttr("+string(OBJTYPE_SECDEAL)+", LPAD(Tick.t_DealID, 34, '0'), 103, Tick.t_DealDate) = 1 then " +
          "                 case when nvl((SELECT t_dlcontrid FROM ddlcontrmp_dbt WHERE t_sfcontrid = Tick.t_Clientcontrid), 0) != 0 then " +
          "                    decode(rsb_secur.GetMainObjAttr(207, LPAD((SELECT t_dlcontrid FROM ddlcontrmp_dbt WHERE t_sfcontrid = Tick.t_Clientcontrid), 34, '0'), 103, Tick.t_DealDate), 0, 0, 1) " +
          "                   else 0 end " +
          "                else 0 " +
          "              end IsSpecRepo, " +
          "              case when rsb_secur.GetMainObjAttr("+string(OBJTYPE_SECDEAL)+", LPAD(Tick.t_DealID, 34, '0'), 106, Tick.t_DealDate) != 0 then" +
          "                 rsi_rsb_mask.CompareStringWithMask((select rsb_common.GetRegStrValue('РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ПЕРВИЧ_РАЗМЕЩ_РЕЖИМЫ_ТОРГОВ') from dual), " +
          "                                                    rsb_secur.GetObjAttrNumber("+string(OBJTYPE_SECDEAL)+", 106, " +
          "                                                                               rsb_secur.GetMainObjAttr("+string(OBJTYPE_SECDEAL)+", LPAD(Tick.t_DealID, 34, '0'), 106, Tick.t_DealDate))) " +
          "                else 0 " +
          "              end IsPervRazm, " +
          "              rsb_secur.IsBUY(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(Tick.t_DealType, Tick.t_BofficeKind))) IsDealBuy, " +
          "              Tick.t_clientcontrid, Tick.t_DealID," + //Chuikov JIRA DEF-19808 исправление ошибки.
          "              (select t_cfi from ddl_leg_dbt l where l.t_dealid = Tick.t_Dealid and l.t_Legkind = 0 and l.t_Legid = 0) DealFIID, " +
          "              case when (select t_issuer from dfininstr_dbt where t_fiid = tick.t_pfi) = 1 then 1 else 0 end isOwnIssuer " +
          "       from ddl_tick_dbt Tick " +
          "       where Tick.t_Clientcontrid != 0 " +
          "          and Tick.t_dealdate = ? " +
          "          and Tick.t_DealStatus != ? " +
          "      ) t, DSFCONTR_DBT contr, DSFCONCOM_DBT concom, DSFCOMISS_DBT comiss " +
          " where t.t_clientcontrid = contr.t_id " +
          "    and contr.t_ServKind = ? " +
          "    and contr.t_DateConc <= ? " +
          "    and concom.t_ObjectId = contr.t_ID " +
          "    and concom.t_ObjectType = ? " +
          "    and concom.t_FeeType = ? " +
          "    and concom.t_calcPeriodType = ? " +
          "    and comiss.t_ServiceKind = ? " +
          "    and comiss.t_FeeType = concom.t_FeeType " +
          "    and comiss.t_Number = concom.t_CommNumber " +
          "    and comiss.t_FormAlg != ? " +
          "    and concom.t_status = 0 " +
          "    and (concom.t_dateEnd = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
          "         or concom.t_dateEnd >= ?) " +
             "    and  concom.t_datebegin <= ? " +  
          "    and comiss.t_Number >=1000 " +
//дальше идут дополнительные проверки релевантности по комиссиям
          "    and (  (comiss.t_code in ('ПАОМскБирж_RUR','ПАОМскБирж_USD','ПАОМскБирж_EUR') and t.isclientdeal = 1 and t.isexchange = 1 and t.isrepo = 0) " +
          "        or (comiss.t_code in ('ПАОМскБиржПервРСХБ_RUR','ПАОМскБиржПервРСХБ_USD','ПАОМскБиржПервРСХБ_EUR') and t.isclientdeal = 1 and t.isexchange = 1 and t.isrepo = 0 and t.ispervrazm = 1 and isOwnIssuer = 1) " +
          "        or (comiss.t_code in ('ПАОМскБиржПервСТОР_RUR','ПАОМскБиржПервСТОР_USD','ПАОМскБиржПервСТОР_EUR') and t.isclientdeal = 1 and t.isexchange = 1 and t.isrepo = 0 and t.ispervrazm = 1 and isOwnIssuer = 0) " +
          "        or (comiss.t_code in ('ПАОМскБиржБаз_RUR','ПАОМскБиржБаз_USD','ПАОМскБиржБаз_EUR') and t.isclientdeal = 1 and t.isexchange = 1 and t.isrepo = 0 and t.ispervrazm = 0) " +
          "        or (comiss.t_code in ('БрокерКомпенсац_RUR','БрокерКомпенсац_USD','БрокерКомпенсац_EUR', 'БрокерКомпенсац_CHF') and t.isclientdeal = 1 and t.isexchange = 1 and t.ispervrazm = 0 and t.isrepo = 0) " +
          "        or (comiss.t_code in ('НеБирж_RUR','НеБирж_USD','НеБирж_GBP','НеБирж_EUR') and t.isclientdeal = 1 and t.isoutexchange = 1 and t.isrepo = 0) " +
          "        or (comiss.t_code in ('НеБиржБезНот_RUR','НеБиржБезНот_USD','НеБиржБезНот_GBP','НеБиржБезНот_EUR') and t.isclientdeal = 1 and t.isoutexchange = 1 and t.isrepo = 0) " +
          "        or (comiss.t_code in ('НеБиржБезНотНов_RUR','НеБиржБезНотНов_USD','НеБиржБезНотНов_GBP','НеБиржБезНотНов_EUR') and t.isclientdeal = 1 and t.isoutexchange = 1 and t.isrepo = 0) " +
          "        or (comiss.t_code in ('НеБиржНоты_RUR','НеБиржНоты_USD','НеБиржНоты_GBP','НеБиржНоты_EUR') and t.isclientdeal = 1 and t.isoutexchange = 1 and t.isrepo = 0 and t.isdealbuy = 1) " +
          "        or (comiss.t_code in ('РЕПО_RUR','РЕПО_USD','РЕПО_EUR') and t.isclientdeal = 1 and t.isrepo = 1 and t.isspecrepo = 0) " +
          "        or (comiss.t_code in ('СПЕЦРЕПО_RUR','СПЕЦРЕПО_USD','СПЕЦРЕПО_EUR') and t.isclientdeal = 1 and t.isrepo = 1 and t.isspecrepo = 1)) " +
//проверка по валюте сделки и комиссии
          "    and comiss.t_fiid_comm = t.dealfiid ";

  cmd.addParam(Data.GUID);
  cmd.addParam(Data.OperDate);
  cmd.addParam(DL_CLOSED);
  cmd.addParam(PTSK_STOCKDL);
  cmd.addParam(Data.OperDate);
  cmd.addParam(OBJTYPE_SFCONTR);
  cmd.addParam(SF_FEE_TYPE_PERIOD);
  cmd.addParam(SF_TYPE_PERIOD_DAY);
  cmd.addParam(PTSK_STOCKDL);
  cmd.addParam(SFCOMISS_FORMALG_MANUAL);
  cmd.addParam(Data.OperDate);
  cmd.addParam(Data.OperDate);

  if( Data.ConCom > 0 )/*только комиссии этого вида(комиссия привязанная к договору)*/
     query = query +
          "    and comiss.T_NUMBER = (select CM.t_CommNumber from DSFCONCOM_DBT CM where CM.t_ID = ?) ";
     cmd.addParam(Data.ConCom );
  end;

  if( Data.PartyID > 0 )/*только комиссии, где плательщик - заданный субъект*/
     query = query +
          "    and contr.T_PartyID = ? ";                       
     cmd.addParam(Data.PartyID );                           
  end;

  if( Data.ContractID > 0 )/*отбираем комиссии по указанному договору*/
     query = query +
          "    and Contr.T_ID    = ? ";
     cmd.addParam(Data.ContractID );
  end;

  if( Data.DealID > 0 )
     query = query +
          "    and t.t_DealID = ? ";
     cmd.addParam(Data.DealID);
  end;

  cmd.ExecuteCMD(query);

  //На основе сформированных групп формируем группы с учетом количества сделок
  if(РазбиватьПоСделкам)
    query =   "DECLARE "
            + "  TYPE calccomm_t IS TABLE OF DSPCALCCOMMDEAL_DBT%ROWTYPE INDEX BY BINARY_INTEGER; "
            + "  v_calccomm calccomm_t; "
            + "  v_Num NUMBER; "
            + "BEGIN "

            + "  FOR one_rec IN (SELECT * FROM DSPCALCCOMMDEAL_DBT WHERE T_GUID = ? /*1*/ AND T_DEALID = 0)"
            + "  LOOP "

            + "    INSERT INTO DSPCALCCOMMDEAL_DBT (T_GUID, T_NUMPACK, T_CONTRID, T_CONCOMID, T_DEALID, T_NUM, T_RECEIVERID) "
            + "    SELECT one_rec.t_GUID, 0, one_rec.t_ContrID, one_rec.t_ConComID, tick.t_DealID, -1, one_rec.t_ReceiverID "
            + "      FROM ddl_tick_dbt tick "
            + "     WHERE tick.t_BOfficeKind = " + DL_SECURITYDOC
            + "       AND tick.t_DealStatus != " + DL_CLOSED
            + "       AND tick.t_DealDate = ? /*2*/ "
            + "       AND tick.t_ClientContrID = one_rec.t_ContrID; "

            + "  END LOOP; "

            + "  DELETE FROM DSPCALCCOMMDEAL_DBT WHERE T_GUID = ? /*3*/ AND T_DEALID = 0; "


            + "  FOR one_rec IN (SELECT DISTINCT T_GUID, T_CONTRID, T_RECEIVERID FROM DSPCALCCOMMDEAL_DBT WHERE T_GUID = ? /*4*/ AND T_NUM = -1 )"
            + "  LOOP "

            + "   UPDATE DSPCALCCOMMDEAL_DBT SC"
            + "      SET SC.T_NUM = (SELECT ROUND((q.N-1)/"+КоличествоСделокВГруппе+") "
            + "                        FROM (SELECT DISTINCT T_DEALID, DENSE_RANK() OVER(ORDER BY SC1.T_GUID, SC1.T_CONTRID, SC1.T_DEALID, SC1.T_RECEIVERID) as N "
            + "                                FROM DSPCALCCOMMDEAL_DBT SC1 "
            + "                               WHERE SC1.T_GUID = one_rec.t_GUID "
            + "                                 AND SC1.T_CONTRID = one_rec.t_ContrID"
            + "                                 AND SC1.T_RECEIVERID = one_rec.t_ReceiverID "
            + "                             ) q "
            + "                       WHERE q.T_DEALID = SC.T_DEALID "
            + "                     ) "
            + "    WHERE SC.T_GUID = one_rec.t_GUID "
            + "      AND SC.T_CONTRID = one_rec.t_ContrID"
            + "      AND SC.T_RECEIVERID = one_rec.t_ReceiverID "
            + "      AND SC.T_NUM = -1; "

            + "  END LOOP; "










         /*   + "  FOR one_rec IN (SELECT COUNT(1) AS Rest T_GUID, T_CONTRID, T_RECEIVERID FROM DSPCALCCOMMDEAL_DBT WHERE T_GUID = ? /*4*/ AND T_NUM = -1 GROUP BY T_GUID, T_CONTRID, T_RECEIVERID)"
            + "  LOOP "

            + "    v_Rest := one_rec.Rest; "
            + "    v_Num  := 0; "

            + "    WHILE v_Rest > 0 LOOP "
            + "      UPDATE DSPCALCCOMMDEAL_DBT "
            + "         SET T_NUM = v_Num "
            + "       WHERE T_GUID = one_rec.t_GUID "
            + "         AND T_CONTRID = one_rec.t_ContrID"
            + "         AND T_RECEIVERID = one_rec.t_ReceiverID "
            + "         AND T_NUM = -1 "
            + "         AND ROWNUM <= "+КоличествоСделокВГруппе+"; "

            + "      UPDATE DSPCALCCOMMDEAL_DBT SC "
            + "         SET SC.T_NUM = NVL((SELECT SC1.T_NUM FROM DSPCALCCOMMDEAL_DBT SC1 WHERE SC.T_GUID = one_rec.t_GUID AND SC.T_CONTRID = one_rec.t_ContrID AND SC.T_RECEIVERID = one_rec.t_ReceiverID AND SC.T_NUM > -1 AND ROWNUM = 1), -1) "
            + "       WHERE SC.T_GUID = one_rec.t_GUID "
            + "         AND SC.T_CONTRID = one_rec.t_ContrID"
            + "         AND SC.T_RECEIVERID = one_rec.t_ReceiverID "
            + "         AND SC.T_NUM = -1; "

            + "      SELECT COUNT(1) INTO v_Rest FROM FROM DSPCALCCOMMDEAL_DBT WHERE T_GUID = one_rec.t_GUID AND T_CONTRID = one_rec.t_COntrID AND T_RECEIVERID = one_rec.t_ReceiverID AND T_NUM = -1 AND ROWNUM = 1; "

            + "      v_Num := v_Num + 1;"

            + "    AND LOOP;"

            + "  END LOOP; "  */
                  

            + "END;"; 

    cmd = DL_RSDCommand(query);

    cmd.addParam(Data.GUID);
    cmd.addParam(Data.OperDate);
    cmd.addParam(Data.GUID);
    cmd.addParam(Data.GUID);

    cmd.ExecuteCMD();
  end;

  //Назначить номер пачки группам
  query =   "DECLARE "
          + "  TYPE calccomm_t IS TABLE OF DSPCALCCOMMDEAL_DBT%ROWTYPE INDEX BY BINARY_INTEGER; "
          + "  v_calccomm calccomm_t; "
          + "BEGIN "

          + "  SELECT T_GUID, DENSE_RANK() OVER(ORDER BY T_GUID, T_CONTRID, T_RECEIVERID, T_NUM), T_CONTRID, T_CONCOMID, T_DEALID, T_NUM, T_RECEIVERID "
          + "    BULK COLLECT INTO v_calccomm "
          + "    FROM DSPCALCCOMMDEAL_DBT "
          + "   WHERE T_GUID = ? /*1*/ ;"

          + "  DELETE FROM DSPCALCCOMMDEAL_DBT WHERE T_GUID = ? /*2*/; "

          + "  IF v_calccomm.COUNT > 0 THEN "
          + "    FORALL indx IN v_calccomm.FIRST .. v_calccomm.LAST "
          + "     INSERT INTO DSPCALCCOMMDEAL_DBT "
          + "     VALUES v_calccomm (indx); "
          + "  END IF; "

          + "END;"; 

  cmd = DL_RSDCommand(query);

  cmd.addParam(Data.GUID);
  cmd.addParam(Data.GUID);

  cmd.ExecuteCMD();

  RslDefCon.CommitTrans();

OnError(er)
  var err_str = "", delim = "";
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();                    
  end;
  
  if(er and er.err and er.err.environment)
      var j = 0;
      while(j < er.err.environment.ErrorCount)
          err_str = err_str + delim + er.err.environment.error(j).descr;
          delim = "\n";
          j = j + 1;
      end;

      InsertError(Data.GUID, err_str);
      RunError(err_str);
  else
      if(RslDefCon.IsInTrans)
        RslDefCon.RollbackTrans();
      end;
      InsertError(Data.GUID, er.message);
      return 1;
  end;
end;

macro ОтобратьОбъектыДляРасчетаКом(_taskID, _execID, _conveyerID, _packetSize, _Params)
  var query, cmd, DataSet;
  var cnt = 0;

  /* формируем объекты для конвейера, если работаем с его использованием */
  query =   " INSERT INTO DCNVDOC_DBT(T_TASKID,T_EXECID,T_CONVTYPEID,T_PACKID,T_OBJECTTYPE,T_OBJECTID) "
          + " SELECT ?,?,?,decode("+_packetSize+", 0, 1, (TRUNC((RowNum + "+_packetSize+" - 1)/"+_packetSize+"))), 0, q.t_NumPack "
          + "   FROM (SELECT scc.t_NumPack, COUNT(1) as cnt "
          + "           FROM dspcalccommdeal_dbt scc "
          + "          WHERE scc.t_GUID = ? "
          + "         GROUP BY scc.t_NumPack " 
          + "        ) q ORDER BY q.cnt DESC";

  cmd = RSDCommand(query);

  cmd.addParam( "", RSDBP_IN, _taskID);
  cmd.addParam( "", RSDBP_IN, _execID);
  cmd.addParam( "", RSDBP_IN, _conveyerID);
  cmd.addParam( "", RSDBP_IN, _Params.GUID );
  cmd.execute();
  
  if(_packetSize == 0)
    cnt = 1;
  else
    query =   " select count(1) as cnt "
            + "   from dcnvdoc_dbt "
            + "  where t_ExecID = ? "
            + "    and t_ConvTypeID = ?";
    cmd = DL_RSDCommand(query);
    cmd.AddParam(_execID);
    cmd.AddParam(_conveyerID);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      cnt = int(round((int(DataSet.cnt) + _packetSize - 1) / _packetSize, 0));
    end;
  end;

  return cnt;
end;

macro ExecuteCommCalc(_taskID, _execID, _conveyerID, _operationID, _packetID, _Params)
  var query, cmd, DataSet;
  
  query =   " select cnvdoc.t_ObjectID "
          + "   from dcnvdoc_dbt cnvdoc "
          + "  where cnvdoc.t_ExecID = ? "
          + "    and cnvdoc.t_ConvTypeID = ? "
          + "    and cnvdoc.t_PackID = ? ";
  cmd = DL_RSDCommand(query);
  
  cmd.AddParam(_execID);
  cmd.AddParam(_conveyerID);
  cmd.AddParam(_packetID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    RunCalculation(_Params, int(DataSet.ObjectID));
  end;

  /* не зависимо от того, делали что-то или нет - ставим исходящий статус обрабатываемой пачке */
  var exec = RSDCommand(  " UPDATE dcnvpack_dbt "
                        + " SET t_State = t_State + 1, t_Error = ? "
                        + " WHERE t_ExecID = ? "
                        + "   AND t_ConvTypeID = ? "
                        + "   AND t_PackID = ? "
                       );

  exec.addParam( "", RSDBP_IN, /*IIF(err != 0, 1, 0)*/ 0 );
  exec.addParam( "", RSDBP_IN, _execID );
  exec.addParam( "", RSDBP_IN, _conveyerID );
  exec.addParam( "", RSDBP_IN, _packetID );
  exec.execute();

  return 0;

OnError(er)
    var j;
    var err_text = "", delim = "";

    if(RslDefCon.IsInTrans)
      RslDefCon.RollbackTrans();                    
    end;

    if(er and er.err and er.err.environment)
        j = 0;
        while(j < er.err.environment.ErrorCount)
            err_text = err_text + delim + er.err.environment.error(j).descr;
            delim = "\n";
            j = j + 1;
        end;

        InsertError(_Params.GUID,err_text);
        RunError(err_text);
    else
        InsertError(_Params.GUID,er.Message);
        RunError(er.Message);
    end;
    return 1;
end;

macro RunComCalc(maxcalcid,OperDate:Date,DealID:INTEGER,PartyID:INTEGER,ContractID:INTEGER,ConCom:INTEGER,Method:INTEGER,PrintRep:bool,webreport:@string/*ak*/, result_protocol:@TStrCollector, recs:@integer, errrecs:@integer/*~ak*/, wrnrecs:@integer/*dan*/, errmsg:@string)
  var OperID = 23; //Операция для конвейра - Пакетная процедура расчета периодических комиссий БО ЦБ
  var ConvID = 28; //Конвейер - Расчет комиссий БО ЦБ
  var UseConveyer = false; //SecurUseConveyer(ConvID, OperID);
  var IsWeb = false, IsExec = false;
  var cmd, DataSet;
  var PrintCalcdComiss = True; // dan печатать расчитанные комиссии biq-11080
  var stat = 0;

  recs = 0;
  errrecs = 0;
  wrnrecs = 0;

  bo_act_init = GetGlobalParameter("BO_ACT_INITIATOR", true);
  if ((bo_act_init == null) or (bo_act_init == nullval))
    bo_act_init = "";
  end;

  not_print_calced_comiss = GetGlobalParameter("NOT_PRINT_CALCED_COMISS", true);
  if (not_print_calced_comiss == set_char)
    PrintCalcdComiss = False;
  end;


  if(ValType(webreport) == V_STRING)
    IsWeb = true; IsExec = true;
    if(webreport != "" )
      IsExec = true;
      PrintRep = true;
    end;
  end;

  var Data = SourceData(OperDate,DealID,PartyID,ContractID,ConCom,Method,SP_GetGUID(),PrintRep,IsWeb,IsExec);

  RsltStr.ClearArray;

  if( (Method == METHOD_CALC) or (Method == METHOD_RECALC) )
      RunCalculation_BrkCur(Data);
  end;

  SaveComis(Data, maxcalcid);

  return;
  
  if(stat != 0)
    CreateErrMsg(stat);
    InsertError(Data.GUID, GetErrMsg());
  end;

  //подсчет количества записей
  cmd = DL_RSDCommand("select NVL(count(1),0) as recs from dsccommcalc_dbt where t_GUID = ? and t_Type = 4");
  cmd.AddParam(Data.GUID);
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    recs = int(DataSet.recs);
  end;

  //подсчет количества ошибок
  cmd = DL_RSDCommand("select NVL(count(1),0) as errrecs from dsccommcalc_dbt where t_GUID = ? and t_Type = 2");
  cmd.AddParam(Data.GUID);
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    errrecs = int(DataSet.errrecs);
  end;

  //подсчет предупреждений
  cmd = DL_RSDCommand("select NVL(count(1),0) as wrnrecs from dsccommcalc_dbt where t_GUID = ? and t_Type = 5");
  cmd.AddParam(Data.GUID);
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    wrnrecs = int(DataSet.wrnrecs);
  end;

  if((ValType(errmsg) == V_STRING) and (errmsg == "") and (recs == 0) and (wrnrecs == 0) and (errrecs == 0) and (Method == METHOD_CALC))
      cmd = DL_RSDCommand(" SELECT LISTAGG(t_dealcode, ', ') WITHIN GROUP (ORDER BY t_dealcode) deals " 
                         +"   FROM (SELECT ndeal.t_code t_dealcode" 
                         +"           FROM ddvndeal_dbt ndeal "
                         +"          WHERE ndeal.t_client > 0 "
                         +"            AND ndeal.t_date = ? "
                         +"            AND NOT EXISTS (SELECT 1 "
                         +"                              FROM ddlcomis_dbt dlc, dsfcomiss_dbt sfc "
                         +"                             WHERE dlc.t_dockind    = ndeal.t_dockind "
                         +"                               AND dlc.t_docid      = ndeal.t_id "
                         +"                               and dlc.t_feetype    = sfc.t_feetype " 
                         +"                               and dlc.t_comnumber  = sfc.t_number "  
                         +"                               and sfc.t_ReceiverID = ? " 
                         +"                           ) "
                         +"       GROUP BY ndeal.t_code) "
                         );
      cmd.AddParam(OperDate);
      cmd.AddParam({ourbank});

    DataSet = cmd.Execute();     
    while(DataSet.moveNext() and (SQL_ConvTypeStr(DataSet.deals) != ""))
      stat = 1;
      errmsg = "Найдены сделки, по которым не взята комиссия брокера: " + SQL_ConvTypeStr(DataSet.deals);
      InsertError(Data.GUID, errmsg);
    end;
  end;

  if( (Method == METHOD_CALC) or (Method == METHOD_RECALC) )
    ПечататьПротокол(Data, PrintCalcdComiss/*dan*/);
      
    result_protocol = RsltStr;
  else
    if (errrecs > 0) 
      ПечататьПротокол(Data);
    elif (recs > 0)
      ПечататьПротоколПусто(Data,"Удалено комиссий: " + recs);
    else
      ПечататьПротоколПусто(Data,"Подходящих для удаления комиссий не найдено.");
    end;   
  end;

  webreport = "";
  cmd = DL_RSDCommand("select t_Str from dsccommcalc_dbt where t_GUID = ? and t_Type = 3");
  cmd.AddParam(Data.GUID);
  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    webreport = webreport + DataSet.Str+"\n";
  end;

  cmd = DL_RSDCommand("delete from dspcalccommdeal_dbt where t_GUID = ?");
  cmd.AddParam(Data.GUID);

  cmd.ExecuteCMD();

  cmd = DL_RSDCommand("delete from dsccommcalc_dbt where t_GUID = ?");
  cmd.AddParam(Data.GUID);

  cmd.ExecuteCMD();

  return stat;
end;