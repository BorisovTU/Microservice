/*
reserve_perc_load.mac
Загрузка оценочныйх резервов из файла excel
Для объекта REPO пока ищет только по номеру счета
*/
import oralib, likepy, globals, BankInter, FIInter, or_rep_h, Календарь;
import "u_common_email_utils.mac", "file_archive.mac", "or_exl_h.mac";

private var log;
private var work_folder = "";

private macro error_message (err_text)
  log.addError(err_text);
  return 1;
End;

private macro info_message (info_text)
  log.addInfo(info_text);
End;

private macro ByDefault (parm, default_value)
   if (valtype(parm) == V_UNDEF)
      SetParm(0, default_value);
   end;
End;

//Перемещает файл
private macro copyFromTo (from, to)
  return TDirList().copy(from, null, to, true, false);
End;

private class c_protocol ()
  private var Rep = CMakeReport();
  private var count = 0;

  macro Header ()
    Rep.AddPrintCell("Протокол загрузки ставок оценочных резервов", 100, 0, "c:ex_FS(b)", REP_ELEM_STR ); Rep.AddStr();
    Rep.AddEmptyStr();
    Rep.AddPrintCell("Дата запуска: "+string({curdate}:f)+"   Время запуска " + Time(), 100, 0, "l:ex_FS(b)", REP_ELEM_STR ); Rep.AddStr();
    Rep.AddPrintCell("Опрационист: "+{Name_Oper}, 100, 0, "l:ex_FS(b)", REP_ELEM_STR ); Rep.AddStr();
    Rep.AddEmptyStr();
  End;

  private macro newLine (text:string, status:string)
    ByDefault(status, "");

    count = count + 1;
    Rep.AddPrintCell(count, 3, 0, "l:ex_FS(b)", REP_ELEM_TABL);
    Rep.AddPrintCell(status, 10, 0, "l:ex_FS(b)", REP_ELEM_TABL);
    Rep.AddPrintCell(text, 100, 0, "l:ex_FS(b)", REP_ELEM_TABL);
    Rep.AddStr();
  End;

  macro addError (text:string)
    newLine(text, "Ошибка!");
  End;

  macro addInfo (text:string)
    newLine(text);
  End;

  macro Print (IsHandler)
    var usr_filename = string(date():f)+"_load_or_rate";
    usr_filename = strsubst(usr_filename, ".", "");

    var log_file = GetTxtFileName(usr_filename+".txt");

    Rep.SetFileNameTxt(usr_filename);
    Rep.PrintRep();

    if (not IsHandler) 
      copyFromTo(log_file, "$"+work_folder+"\\..\\logs\\");
    else
      ViewFile( log_file );
    end;
  End;

END;

class Reserve_load ()
  private var FILES_COUNT = 4; //количество файлов, которое ожидается для загрузки

  //Типы объектов
  var TYPE_VEKSEL = "BANKWECHSEL";
  var TYPE_MBK    = "INTERBANK";
  var TYPE_REPO   = "REPO";
  var TYPE_SECUR  = "SECURITIES";

  //Имя файла excel, соответствующее номеру метода. 20200313 - теперь эти значения есть в справочнике 5026. Можно взять оттуда
  var Method_Type1 = "DEALS";  // расчетный метод
  var Method_Type2 = "ID";     // по контрагенту/эмитенту
  var Method_Type3 = "RATING"; // по рейтингу контрагента/эмитента
  var Method_Type4 = "TTM";    // по сроку инструмента

  //ИД методов поиска ставки
  var METHOD_ID_CALC   = 1;
  var METHOD_ID_ISSUER = 2;
  var METHOD_ID_RATING = 3;
  var METHOD_ID_TTM    = 4;

  var LoadFiles   = false;
  var StartUpdate = false;
  var StartReport = false;
  var SendReport  = false;

  var ValueDate = Date(0, 0, 0);
  var Handler = true; //Признак ручного запуска

  var FilesArrTerm = TArray(); //Массив с именами файлов. используется для удаления файлов с терминала
  var FilesArrAppServ = TArray(); //Массив с именами файлов. используется для удаления файлов с сервера приложений
  var rc, ActiveBook;
  var TmpFilePath = "";

  macro SelectFileMask (mask)
    var FullNameFile = "";

    /*Выбираем файл для обработки*/
    if(not SelectFile(FullNameFile, "$C:\\*"+mask+".xls*", "Выберите файл для загрузки *"+mask, 0, true))
      MsgBox("Отказ от загрузки");
      return "";
    end;
    return FullNameFile;
  onerror()
    return "";
  End;

  //последний рабочий день предыдущего месяца
  private macro GetWorkDay (_dt)
    var dt = _dt - 1; //Файл приходит с датой 01.ХХ.ХХХХ. Нужен последний рабочий день предыдущего месяца

    while (IsHoliday(dt) )
      dt = dt - 1;
    end;
    return dt;
  End;

  private macro GetDateFromIntervalDay ( interval ) : date
    var startDate = date("01.01.1900");
    return DateShift(startDate, int(interval) - 2);
  end;

  macro SaveFileToAppServ(FullName:string) 
    var FileName = "";
    var DirName = "";
    var ExtName = "";
    var Path_AppServ = "";
      
    if(not isStandAlone()) // когда мы на терминале, копируем файл на СП     
      splitfile(FullName, FileName, ExtName);
      FileName = FileName + ExtName;
      DirName = substr(FullName, 1, index(FullName, FileName) - 1);
      Path_AppServ = "..\\workfile\\" + FileName;

      if (not copyfile("$" + DirName + FileName, Path_AppServ) )
        msgbox("Ошибка при передаче файла на сервер приложений");
        return false;
      end;
         
      FilesArrAppServ(FilesArrAppServ.Size) = Path_AppServ;
      TmpFilePath = Path_AppServ;
      return true;
    else
      TmpFilePath = FullName;
      return true;
    end;
  end;

  //Перед открытием копирует файл на терминал пользователя
  macro SaveFileToTerm (FullName:string)
    var FileName = "";
    var DirName = "";
    var ExtName = "";
    var TermPath = GetCurDir(true) + "\\TxtFile";

    //FullName = StrSubSt(FullName, "$", ""); //убрать лишние $. На всякий случай
    //Отделяем путь файла от имени
    splitfile(FullName, FileName, ExtName);
    FileName = FileName + ExtName;
    DirName = substr(FullName, 1, index(FullName, FileName) - 1);

    if (not copyfile("$" + DirName + FileName, "$" + TermPath + "\\" + FileName) )
      MsgBox("Ошибка при передаче файла на терминал");
      //msgbox(string("$" + DirName + FileName) +"|"+string(TermPath + "\\" + FileName));
      return false;
    end;

    FilesArrTerm(FilesArrTerm.Size) = "$" + TermPath + "\\" + FileName;

    return true;
  onerror()
    return false;
  End;

  //Удаление файлов с терминала пользователя
  macro RemoveFiles ()
    var i = 0;
    while (i < FilesArrTerm.Size)
      if (not RemoveFile (FilesArrTerm(i)))
        error_message("Ошибка удаления с терминала файла " + FilesArrTerm(i));
      end;
      
      i = i + 1;
    end;

    i = 0;
    while (i < FilesArrAppServ.Size)
      if (not RemoveFile (FilesArrAppServ(i)))
        error_message("Ошибка удаления с терминала файла " + FilesArrAppServ(i));
      end;
      
      i = i + 1;
    end;
  End;

  private macro stop_read_file (text)
    if (valtype(ActiveBook) != V_UNDEF)
      ActiveBook.close();
      ActiveBook = null;
      rc = null;
    end;
    if ( rslDefCon.isInTrans() )
      rslDefCon.rollbackTrans();
    end;
    RemProgress();
    return error_message(text);
  End;

  private macro m_read_file (FileName, method_id)
    var COL_RATE       = 0;
    /*all*/
    var COL_DATE       = "A";
    var COL_TYPE       = "B";
    var COL_OBJECT     = "C";
    //Для метода 1
    var COL_START_DATE = "D";
    var COL_RECOVERY   = "E"; //Признак восстановления
    var COL_STAGE_FACT = "F";
    var COL_STAGE      = "G";
    //Для метода 4
    var COL_TERM       = "D";

    var row = 2; //Данные начинаются со второй строки
    var sql, query_insert;
    var stagefact:integer, stage:integer;
    var object_id:string;
    var dt:date;
    var rate:money = $0;
    var type:string = "";
    var CheckExistsData = false;
    var recovery; //Признак восстановления
    var start_dt:Date;
    var nullDate = Date(0, 0, 0);

    var jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
    var rc = jvm.callStaticMethod("ru.softlab.rsbank.poireports.ReportCreator", "getInstance");
    
    if   (method_id == METHOD_ID_CALC)   COL_RATE = "I";
    elif (method_id == METHOD_ID_ISSUER) COL_RATE = "E";
    elif (method_id == METHOD_ID_RATING) COL_RATE = "E";
    elif (method_id == METHOD_ID_TTM)    COL_RATE = "F";
    end;

    SaveFileToTerm(FileName);
    if (not SaveFileToAppServ(FileName) )
      return stop_read_file("Ошибка при открытии файла " + filename);
    end;

    ActiveBook = rc.openWorkbook(TmpFilePath);
    if (not ActiveBook)
      return stop_read_file("Ошибка при открытии файла " + filename);
    end;

    ActiveBook.setActiveSheet(0);

    query_insert = "insert into USER_MSFORESERV (method_id, valuedate, objecttype, object, stagefact, stage, rate, start_dt, recovery_flg) "
                 + "Values (:method_id, :valuedate, :objtype, :object, :stagefact, :stage, :rate, :start_dt, :recovery) ";

    InitProgress(-1, "Чтение файла " + TmpFilePath, "Чтение файла..");
    rslDefCon.beginTrans();
    while (valtype(ActiveBook.getCellValue(COL_DATE+row)) != V_UNDEF)
    
      rate      = Money(ActiveBook.getCellValue(COL_RATE+row)) * 100;
      dt        = GetWorkDay(ActiveBook.getCellValue(COL_DATE+row));
      object_id = ActiveBook.getCellValue(COL_OBJECT+row:string);
      type      = ActiveBook.getCellValue(COL_TYPE+row);
      stage     = 0;
      start_dt  = nullDate;
      recovery  = -1;

      if (method_id == METHOD_ID_CALC)
        object_id = StrSubSt(String(object_id), "'", "");
        stagefact = ActiveBook.getCellValue(COL_STAGE_FACT+row);
        stage     = ActiveBook.getCellValue(COL_STAGE+row);
        recovery = ifThenElse(valtype(ActiveBook.getCellValue(COL_RECOVERY+row)) != V_UNDEF, ActiveBook.getCellValue(COL_RECOVERY+row), -1);
	
        start_dt  = ActiveBook.getCellValue(COL_START_DATE+row);

        start_dt = ifThenElse(valtype(start_dt) == V_UNDEF, nullDate, start_dt);

        //костыль. ЦКРР выгружает счета по векселям с fi_type == "INTERBANK"
        type = ifThenElse( ((type == TYPE_MBK) and (index(object_id, "5") == 1)), TYPE_VEKSEL, type);

      elif (method_id == METHOD_ID_ISSUER)
        object_id = StrSubSt(String(object_id), "'", "");
        stagefact = 0;

        //костыль. ЦКРР выгружает счета по векселям с fi_type == "INTERBANK"
        type = ifThenElse( ((type == TYPE_MBK) and (index(object_id, "5") == 1)), TYPE_VEKSEL, type);

      elif (method_id == METHOD_ID_RATING)
        type      = "";
        object_id = ActiveBook.getCellValue(COL_TYPE+row);
        stagefact = ActiveBook.getCellValue(COL_OBJECT+row);

      elif (method_id == METHOD_ID_TTM)
        stagefact = SubStr(String(ActiveBook.getCellValue(COL_TERM+row)), 1, 1);
      end;

      if (not CheckExistsData)
        if (ExecSqlSelect("select 1 from USER_MSFORESERV where valuedate = :dt and method_id = :id", MakeArray(SqlParam("dt", dt), SqlParam("id", method_id))).MoveNext() )
          stop_read_file("Данные за дату " + dt + " методом " + method_id + " уже есть в системе");
          return 0;
        end;
        CheckExistsData = true;
      end;

      sql = ExecSql(query_insert, MakeArray(SqlParam("id",        method_id ),
                                            SqlParam("dt",        dt        ),
                                            SqlParam("objtype",   type      ),
                                            SqlParam("object",    object_id ),
                                            SqlParam("stagefact", stagefact ),
                                            SqlParam("stage",     stage     ),
                                            SqlParam("rate",      rate      ),
                                            SqlParam("start_dt",  start_dt  ),
                                            SqlParam("recovery",  recovery  )));
      if (sql == null) 
        return stop_read_file("Ошибка при вставке данных из строки " + row + " файла " + filename);
      end;

      UseProgress(row = row + 1);
    end;

    if (valtype(ActiveBook) != V_UNDEF)
      ActiveBook.close();
      ActiveBook = null;
      rc = null;
    end;
    rslDefCon.commitTrans();
    RemProgress();
    
    return 0;

  onerror()
    return stop_read_file("Неизвестная ошибка при обработке файла " + filename + ". Строка " + row);
  End;

  //взять дату из названия. прямо тестом. 20191001_МБК_ЦБ_РЕПО_Векселя_Ставки резерва_DEALS.xlsx
  private macro GetFileDate (FileName):string
    return SubStr(FileName, 1, index(FileName, "_") - 1);
  End;

  //ИД метода по имени файла
  private macro GetMethodIDByFileName (name):integer
    var method_id = 0;
    if   (index(name, Method_Type1) > 0) method_id = METHOD_ID_CALC;
    elif (index(name, Method_Type2) > 0) method_id = METHOD_ID_ISSUER;
    elif (index(name, Method_Type3) > 0) method_id = METHOD_ID_RATING;
    elif (index(name, Method_Type4) > 0) method_id = METHOD_ID_TTM;
    end;
    return method_id;
  End;

  private macro auto_select_folder ():string
    var path = "";
    var mm, yy;
    if (not GetRegistryValue( "РСХБ\\ДИРЕКТОРИИ\\RESERVE_RATE", V_STRING, path) )
      path = "";
    end;

    DateSplit({curdate}, null, mm, yy);
    if (mm == 12) 
      mm = 1;
      yy = yy + 1;
    else
      mm = mm + 1;
    end;

    path = path + string(yy) + string(mm:o:2) + "01" + "\\";

    return path;
  End;

  //Выбор дирректонрии и начало загрузки файлов
  macro m_start_load_files ()
    var i = 0;
    var filedate = "no_date";
    var files;
    var method_id;
    var stat = 0;

    //Выбор дирректории с файлами
    if (Handler) 
      if (not SelectFolder(work_folder, "*", null, true) )
        work_folder = "";
      end;
    else
      work_folder = auto_select_folder();
    end;

    if (work_folder == "") 
      return error_message("Не определена директория с файлами");
    end;

    info_message("Выбран каталог: " + work_folder);

    files = TDirList("$" + work_folder+"\\*.xls*", "F");
    files.Sort(0); //сортировка по имени

    if (files.count != FILES_COUNT)
      return error_message("В указанном каталоге должно быть " + FILES_COUNT + " файла с маской *.xls*. У Вас: " + files.count + "\nОбработка прекращена!");
    end;

    WriteFiscLog( OLstrproc, "Загрузка файлов с оценочными резервами");

    while ( (i < FILES_COUNT) and (stat == 0) )
      method_id = GetMethodIDByFileName(files.Name(i));

      info_message("Обрабатывается файл: " + files.Name(i));

      if (method_id == 0)
        return error_message("Не удалось определить вид файла " + files.Name(i) + ". Обработка прекращается");
      end;

      if (filedate == "no_date")
        filedate = GetFileDate(files.Name(i));
      elif(filedate != GetFileDate(files.Name(i)) )
        return error_message("Внимание! Обнаружены файлы за разные даты! Обработка прекращается");
      end;

      stat = m_read_file(work_folder + "\\" + files.Name(i), method_id);
      i = i + 1;
    end;

    RemoveFiles();

    return stat;

  onerror(text)
    return;
  End;

//------------------------------------------------------------------------------------------------------------------------------------------------------

  //Запуск пакетной процедуры обновления ставок ОР
  private macro UpdateRates (dt:Date)
    info_message("Запуск обновления ставок оценочного резерва");
    WriteFiscLog( OLstrproc, "Запуск обновления ставок оценочного резерва");
    ExecSql("declare begin rshb_reserve_or.Load_rates_or(:dt); end;", MakeArray(SqlParam("dt", dt)), false);
  End;

//------------------------------------------------------------------------------------------------------------------------------------------------------

  /* Отправить письмо с вложением xls
   * attach_path - путь к прикрепляемому файлу
   * attach_name - имя прикрепляемого файла
   * вложение архивируется
   */
  macro SendMessage (tittle:string, message:string, receiverGroupID:integer, attach_path:string, attach_name:string)
    var email = c_email_proc_env();
    var arh_name = StrSubst(attach_path,"$","")+StrSubst(attach_name, ".xls", ".7z");

    email.m_set_msg_head(tittle);
    email.m_add_row_to_msg_text(message);
    email.m_save_to_submit_grp(receiverGroupID);

    var arh = c_inarch_kit( arh_name, StrSubst(attach_path,"$","") );
    arh.add_file_name(attach_name);


    if (not (arh.Add_to_Archive("zip7")))
      msgbox("Ошибка архивирования файла " + attach_name + " " + arh.get_state_message() + " \n" +
                     "Путь 7zip на терминале: " + arh.get_zip7_util_path_term() + "\n" +
                     "Путь 7zip на СП: " + arh.get_zip7_util_path_sp() + "\n" +
                     "Путь архивации: " + attach_path + "\n" +
                     "Файл архива: " + arh_name);
      return;
    end;

    //arh_name = "$" + arh_name;

    email.m_add_attach_to_list(arh_name);
    email.m_save_to_submit_synch();
    email.m_submit_email_synch();
  End;

  macro Run ()
    var stat = 0;
    log = c_protocol();

    log.Header;

    if (LoadFiles)
      stat = m_start_load_files();
    end;

    if (StartUpdate and (stat == 0) )
      UpdateRates({curdate});
    end;

    if (StartReport and (stat == 0) )
      execmacrofile("reserve_perc_report.mac", "print_or_rep", valuedate, SendReport);
    end;

    if (not SendReport) 
      log.Print(Handler);
    end;

  onerror()
    if (valtype(ActiveBook) != V_UNDEF)
      ActiveBook.close();
      ActiveBook = null;
      rc = null;
    end;
  End;

END;