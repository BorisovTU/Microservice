import enroll_class, "dlutils.mac";
import oralib, "u_common_email_utils.mac";

private const SS_RESPONSE_END = 3; /* Успешное завершение всего сервиса */

private class c_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText)
   var ProcessState;
   var ErrorNumber;
   var ErrorText;

   private macro init_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText)
      if(ValType(p_ProcessState) != V_UNDEF)
         ProcessState = p_ProcessState;
      end;
      if(ValType(p_ErrorNumber) != V_UNDEF)
         ErrorNumber = p_ErrorNumber;
      end;
      if(ValType(p_ErrorText) != V_UNDEF)
         ErrorText = p_ErrorText;
      end;
   end;

   init_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText);
end;

private macro contractInfo(dealID:integer)
  var sql = ""+
    " select sfc_s.t_servkindsub, t.*,  "
    "        f.t_fi_code,               "
    "        a.t_isin,                  "
    "        l.t_principal,             "
    "        d.t_shortname,             "
    "        o.t_code                   "
    "   from ddl_tick_dbt t,            "
    "        dsfcontr_dbt sfc_s,        "
    "        dparty_dbt d,              "
    "        dfininstr_dbt f,           "
    "        davoiriss_dbt a,           "
    "        ddl_leg_dbt l,             "
    "        dobjcode_dbt  o            "
    "  WHERE t.t_BofficeKind = 127      "
    "    AND t.t_dealid  in (:1)   "  
    "    AND t.t_clientcontrid = sfc_s.t_id  "
    "    and d.t_partyid = t.t_clientid      "
    "    and f.t_fiid = t.t_pfi              "
    "    and f.t_fiid = a.t_fiid             "
    "    and o.t_objectid = d.t_partyid      "
    "    and o.t_codekind = 101              "
    "    and o.t_objecttype = 3              "
    "    and o.t_state = 0                   "
    "    and l.t_dealid = t.t_dealid         ";
  var cmd = rsdCommand(sql);
  cmd.addParam(":1", RSDBP_IN, dealID);
  cmd.Execute;
  var rs = RSDRecordSet(cmd);
  var  Dt, Mn, Year; 
  var text="", codeets, RefValue; //текстовые переменные для письма уведомления
  if (rs.movenext)
    DateSplit ( date(rs.value("t_dealdate")), Dt, Mn, Year );
    RefValue = StrSubst( string(Dt:2), " ", "0" ) + "." + 
                       StrSubst( string(Mn:2), " ", "0" ) + "." + 
                       SubStr(StrSubst( string(Year), " ", "0" ), 3) ;
    //заранее подготовим текст для предполагаемой ошибки СОБУ или СОВУ
    if ((rs.value("t_dealcodets")==null) or (rs.value("t_dealcodets")==strfor(1)))
      codeets =""; 
    else
      codeets=" ("+rs.value("t_dealcodets")+")";
    end;
    text = string("N",
                  rs.value("t_dealcode"),codeets," от ",RefValue, 
                  " по клиенту ",rs.value("t_shortname")," ("+rs.value("t_code"),") ",
                  " по ц/б ",rs.value("t_fi_code")," (",rs.value("t_isin"),") в количестве ",
                  rs.value("t_principal")," штук");    
  end;
  return text;
end;

private macro JoinArr(arr1, arr2)
  for (var curVal, arr2)
    arr1[arr1.size] = curVal;
  end;
end;

macro PushEnrollsRunDeal(dealid, dt)
  var ErrCount = SP_ExecDeal(dealid, false, dt);
  var errArr = TArray();
  if(ErrCount > 0)
    var errDs = TRsbDataSet("select LTRIM(t_documentid,'0') as documentid, t_errormessage from doprdistaff_tmp where t_errorstatus not in (0)");
    While(errDs.movenext())
      errArr[errArr.size] = "Сделка с ID = " + errDs.documentid + " " + contractInfo(dealid) + ". Ошибка: " + errDs.errormessage;
    end;
  end;
  return errArr;
OnError(err)
  errArr[errArr.size] = GetFullErrorMessage(err);
  return errArr;
end;

// получить ключ рубильника BOSS-1350  -  DEF-65583
PRIVATE MACRO  GetRelay ( p_RelayName: STRING )
  var ErrCode, FlagValue:bool;

  GetRegistryValue( p_RelayName, V_BOOL, FlagValue, ErrCode );
  if ( ErrCode != 0 )
     FlagValue = false;
  end;

  return FlagValue;
onError(erru)  
  return false;
END;

private macro GetStepExecuteStateIdByBatch(operId, symb)
    var ds =
      ExecSQLSelect("select T_ISEXECUTE from doprstep_dbt where t_id_operation = ? and T_SYMBOL = ?",
                     MakeArray(SQLParam("operId", operId), SQLParam("symb", symb)), false);
    if (ds.MoveNext())
      return SQL_ConvTypeStr(ds.value("T_ISEXECUTE"));
    end;
    return NULL;
end;

private macro GetOperationIdByBatch(batch_id)
    var ds =
      ExecSQLSelect("select t_id_operation from doproper_dbt where t_dockind = 127 and t_documentid = lpad(:docid, 34, 0)",
                     MakeArray(SQLParam("batch_id", batch_id)), false);
    if (ds.MoveNext())
      return SQL_ConvTypeInteger(ds.value("t_id_operation"));
    end;
    return 0;
end;

private var MAIL_GRP_LOC = 76; ///< BOSS-1350
private macro sendmailgrp(head, text, grp)
  var emailText = text;
  var v_email = c_email_proc_env();
  v_email.m_set_msg_head(head);
  v_email.m_add_row_to_msg_text(text);
  v_email.m_save_to_submit_grp(grp, true);
  ///v_email.m_submit_email_synch();  отключим отправку, просто ставим в очередь
end;

macro PushEnrolls()
  var Dt,sql, enroll,ResultSheduler :object;
  var errArr = TArray();

  //проверка рубильника BOSS-1350
  var xUseSwitchBOSS1350:BOOL = GetRelay( "РСХБ\\ИНТЕГРАЦИЯ\\РУБИЛЬНИК-BOSS-1350" );
  if (not xUseSwitchBOSS1350)
    return c_ssRunResult(SS_RESPONSE_END,100,"не включен рубильник");
  end;

  sql = "select op.* from ddl_tick_dbt op            "+
        " where op.t_dealstatus in (0,10)            "+
        "   and op.t_dealtype = 2011                 "+
        "   and op.t_bofficekind = 127               "+
        "   and op.t_dealid > 10597062               "+    
   "   order by op.t_dealdate                        ";

  Dt = TRsbDataSet(sql);

  var stat = 0;

  while (Dt.movenext())
    //запускаем через exec, чтобы выделить исполнение в отдельный экземпляр RSL, чтобы друг другу никто не мешал (такое может быть)
    //и цепляем полученные на выходе ошибки к общему массиву ошибок
    var enrollSchedLocker = UniConcurrentLocker("Enroll_Sched_Securities_Lock", 300);
    if (not enrollSchedLocker.Lock())
      RunError("Ошибка создания блокировки для подзадачи планировщик");
    end;

    JoinArr(errArr,ExecMacroFile( "Enroll_Sched_Securities.mac", "PushEnrollsRunDeal", SQL_ConvTypeInteger(DT.dealid), SQL_ConvTypeDate(DT.DealDate)));

    enrollSchedLocker = null; 

  end;

  if (errArr.size > 0)
    AddDBLogDebug("PushEnrollsOperRunErr",join(errArr, ";"));
    sendmailgrp("Ошибка при выполнении операции зачисления Ц/Б", string("Ошибка при выполнении операции зачисления Ц/Б: ", join(errArr, ";")), MAIL_GRP_LOC);        
  end;

  return ResultSheduler = c_ssRunResult(SS_RESPONSE_END,1,"");
OnError(err)
  AddDBLogWithErrorObj("PushEnrolls",err);
  sendmailgrp("Ошибка при выполнении операции зачисления Ц/Б", string("Ошибка при выполнении операции зачисления Ц/Б: ",err.message), MAIL_GRP_LOC);        
  return ResultSheduler = c_ssRunResult(SS_RESPONSE_END);
end;
