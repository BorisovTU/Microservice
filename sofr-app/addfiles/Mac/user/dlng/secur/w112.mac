/*
$Name:        w112.mac
$Module:      Ценные бумаги
$Description: Фин.мониторинг для БОФР - отчет, содержащий список операций, каждую из которых сотрудник должен проверить и при необходимости сформировать сообщение в формате Word вне системы
$Programmer:  Кротов А.
*/
import BankInter, w112_panel, ReportUser;

PRIVATE CLASS (TX_ReportUser) W112_Report
  var m_BeginDate/*, m_EndDate*/;
  var m_ClientType, m_ContragentType, m_PaperType, m_InMarketList, m_Market, m_Route, m_DealSum, m_AvrKind, m_AvrCode, m_PaperNum; 

  /*Создание отчета*/
  PRIVATE MACRO Create()

/*Точка входа*/
    var query;

/*Отключаем все индикаторы*/
    if(m_UseTemplate)
      m_ObjTemplateXLS.SetFlagShowIndicator(false);
    else
      __BUG_Global_m_ObjTemplateXLS.SetFlagShowIndicator(false);
    end;
    m_ProgressIndicator = CreateProgressIndicator(true);

    /*Даты начала и окончания периода отчета*/
    m_BeginDate = FormData.BeginDate;
    m_EndDate   = FormData.EndDate;

/*Лист "Фин.мониторинг"*/
    SetActiveSheet("Фин.мониторинг");
    PrintFormatString( "", "ReportName", "Фин.мониторинг для БОФР за период с " + m_BeginDate + " по " + m_EndDate);
    RegisterTable("ReportNameRow", "", 
           /* 1*/ "ReportName1", 
           /* 2*/ "",
           /* 3*/ "", 
           /* 4*/ "ReportName2",
           /* 5*/ "");
    m_ClientType = m_Form.GetFieldValue(P_W112_CLIENTTYPE);
    if(m_ClientType >= 0)
      PrintTableLine(
              /* 1*/ "ReportName1", "Тип клиента:", null,
              /* 2*/ "", "", null,
              /* 3*/ "", "", null,
              /* 4*/ "ReportName2", FormData.ClientType, null,
              /* 5*/ "", "", null);
    end;
    m_ContragentType = m_Form.GetFieldValue(P_W112_CONTRAGENTTYPE);
    if(m_ContragentType >= 0)
      PrintTableLine(
              /* 1*/ "ReportName1", "Тип контрагента:", null,
              /* 2*/ "", "", null,
              /* 3*/ "", "", null,
              /* 4*/ "ReportName2", FormData.ContragentType, null,
              /* 5*/ "", "", null);
    end;
    m_PaperType = m_Form.GetFieldValue(P_W112_PAPERTYPE);
    if(m_PaperType >= 0)
      PrintTableLine(
              /* 1*/ "ReportName1", "Тип ценнтой бумаги:", null,
              /* 2*/ "", "", null,
              /* 3*/ "", "", null,
              /* 4*/ "ReportName2", FormData.PaperType, null,
              /* 5*/ "", "", null);
    end;
    m_InMarketList = m_Form.GetFieldValue(P_W112_INMARKETLIST);
    if(m_InMarketList >= 0)
      PrintTableLine(
              /* 1*/ "ReportName1", "Включение в листинг биржи:", null,
              /* 2*/ "", "", null,
              /* 3*/ "", "", null,
              /* 4*/ "ReportName2", FormData.InMarketList, null,
              /* 5*/ "", "", null);
    end;
    m_Market = m_Form.GetFieldValue(P_W112_MARKET);
    if(m_Market >= 0)
      PrintTableLine(
              /* 1*/ "ReportName1", "Рынок заключения сделки:", null,
              /* 2*/ "", "", null,
              /* 3*/ "", "", null,
              /* 4*/ "ReportName2", FormData.Market, null,
              /* 5*/ "", "", null);
    end;
    m_Route = m_Form.GetFieldValue(P_W112_ROUTE);
    if(m_Route >= 0)
      PrintTableLine(
              /* 1*/ "ReportName1", "Направление сделки:", null,
              /* 2*/ "", "", null,
              /* 3*/ "", "", null,
              /* 4*/ "ReportName2", FormData.Route, null,
              /* 5*/ "", "", null);
    end;
    this.SetCellAlign("ReportName2", align_right);
    this.SetCellNumberFormat("ReportName2", "# ##0,00;-# ##0,00");
    m_DealSum = m_Form.GetFieldValue(P_W112_DEALSUM);
    PrintTableLine(
            /* 1*/ "ReportName1", "Сумма сделки в RUB от:", null,
            /* 2*/ "", "", null,
            /* 3*/ "", "", null,
            /* 4*/ "ReportName2", m_DealSum, null,
            /* 5*/ "", "", null);
    m_AvrKind = m_Form.GetFieldValue(P_W112_AVRKIND);
    if(m_AvrKind >= 0)
      PrintTableLine(
              /* 1*/ "ReportName1", "Вид ц/б:", null,
              /* 2*/ "", "", null,
              /* 3*/ "", "", null,
              /* 4*/ "ReportName2", FormData.AVRKIND, null,
              /* 5*/ "", "", null);
    end;
    m_AvrCode = m_Form.GetFieldValue(P_W112_AVRCODE);
    if(m_AvrCode >= 0)
      PrintTableLine(
              /* 1*/ "ReportName1", "Код ц/б:", null,
              /* 2*/ "", "", null,
              /* 3*/ "", "", null,
              /* 4*/ "ReportName2", FormData.AVRCODE, null,
              /* 5*/ "", "", null);
    end;
    this.SetCellAlign("ReportName2", align_right);
    this.SetCellNumberFormat("ReportName2", "# ##0,000000000000;-# ##0,000000000000");
    m_PaperNum = m_Form.GetFieldValue(P_W112_PAPERNUM);
    PrintTableLine(
            /* 1*/ "ReportName1", "Количество бумаг от:", null,
            /* 2*/ "", "", null,
            /* 3*/ "", "", null,
            /* 4*/ "ReportName2", m_PaperNum, null,
            /* 5*/ "", "", null);
    EndTable();

    RegisterTable( "ReportRow", "",
            /* 1*/ "DealCode", 
            /* 2*/ "DealExtCode", 
            /* 3*/ "DealDate", 
            /* 4*/ "DealDelivDate", 
            /* 5*/ "DealPayDate", 
            /* 6*/ "DealClient", 
            /* 7*/ "DealClientType", 
            /* 8*/ "DealClientDogNum", 
            /* 9*/ "DealContr", 
            /*10*/ "DealContrType", 
            /*11*/ "DealCBCode", 
            /*12*/ "DealCBName", 
            /*13*/ "DealCBType", 
            /*14*/ "DealCBMarketList", 
            /*15*/ "DealCBMarket", 
            /*16*/ "DealRoute", 
            /*17*/ "DealSum", 
            /*18*/ "DealCBCount");
    query = "select t_dealcode, t_dealcodets, t_dealdate, t_delfactdate, t_payfactdate, t_clientname, t_clientresident, t_contrnumber, t_contragentname, "
          + "       t_contragentresident, t_papercode, t_papername, t_papertype, t_marketlist, t_market, t_route, t_dealsum, t_papercount, t_sumprecision "
          + "from (select dl_tick.t_dealcode t_dealcode, "
          + "             dl_tick.t_dealcodets t_dealcodets, "
          + "             dl_tick.t_dealdate t_dealdate, "
          + "             dlrqdel.t_factdate t_delfactdate, "
          + "             dlrqpay.t_factdate t_payfactdate, "
          + "             particlient.t_name t_clientname, "
          + "             decode(particlient.t_notresident, chr(88), 'Нерезидент', 'Резидент') t_clientresident, "
          + "             nvl(sfcontr.t_number, chr(1)) t_contrnumber, "
          + "             nvl(particontragent.t_name, chr(1)) t_contragentname, "
          + "             case when particontragent.t_notresident is null then chr(1) "
          + "                  when particontragent.t_notresident = chr(88) then 'Нерезидент' "
          + "                  else 'Резидент' end t_contragentresident, "
          + "             avoiriss.t_lsin t_papercode, "
          + "             fininstr.t_name t_papername, "
          + "             case when nvl(issuerparty.t_notresident, chr(0)) = chr(88) then 'Иностранная ЦБ' "
          + "                  else 'Российская ЦБ' end t_papertype, "
          + "             case when (select trim(lower(nvl(max(t_name),chr(1)))) "
          + "                        from dobjattr_dbt objattr, dobjatcor_dbt objatcor "
          + "                        where objattr.t_objecttype = " + OBJTYPE_AVOIRISS/*12*/ + " and "
          + "                              objattr.t_groupid = 15 and /*Биржевая классификация*/ "
          + "                              objatcor.t_objecttype = objattr.t_objecttype and "
          + "                              objatcor.t_groupid = objattr.t_groupid and "
          + "                              objatcor.t_attrid = objattr.t_attrid and "
          + "                              objatcor.t_object = lpad(dl_tick.t_pfi, 10, '0') and "
          + "                              objatcor.t_validfromdate <= dl_tick.t_dealdate and "
          + "                              objatcor.t_validtodate > dl_tick.t_dealdate) = 'не допущен к торгам' then 'Не включена' "
          + "                  else 'Включена' end t_marketlist, "
          + "             case when (dl_tick.t_marketid > 0 and rsb_secur.IsOTC(rsb_secur.get_operationgroup(rsb_secur.get_opersystypes(dl_tick.t_dealtype, dl_tick.t_bofficekind))) = 0) then 'Биржевой' "
          + "                  else 'Внебиржевой' end t_market, "
          + "             case when rsb_secur.IsBuy(rsb_secur.get_operationgroup(rsb_secur.get_opersystypes(dl_tick.t_dealtype, dl_tick.t_bofficekind))) = 1 then 'Покупка' "
          + "                  else 'Продажа' end t_route, "
          + "             case when dl_leg.t_cfi = -1 then dl_leg.t_totalcost "
          + "                  else RSI_RSB_FIInstr.ConvSum(dl_leg.t_totalcost, dl_leg.t_cfi, 0, dl_tick.t_dealdate, 1) end t_dealsum, "
          + "             dl_leg.t_principal t_papercount, "
          + "             fininstr.t_sumprecision t_sumprecision "
          + "      from ddl_tick_dbt dl_tick "
          + "      join ddlrq_dbt dlrqdel on dlrqdel.t_docid = dl_tick.t_dealid and "
          + "                                dlrqdel.t_dealpart = 1 and "
          + "                                dlrqdel.t_subkind = " + DLRQ_SUBKIND_AVOIRISS/*1*/ + " and "
          + "                                dlrqdel.t_type = " + DLRQ_TYPE_DELIVERY/*8*/ + " "
          + "      join ddlrq_dbt dlrqpay on dlrqpay.t_docid = dl_tick.t_dealid and "
          + "                                dlrqpay.t_dealpart = 1 and "
          + "                                dlrqpay.t_subkind = " + DLRQ_SUBKIND_CURRENCY/*0*/ + " and "
          + "                                dlrqpay.t_type = " + DLRQ_TYPE_PAYMENT/*2*/ + " "
          + "      join dparty_dbt particlient on particlient.t_partyid = dl_tick.t_clientid "
          + "      left join dsfcontr_dbt sfcontr on sfcontr.t_id = dl_tick.t_clientcontrid "
          + "      left join dparty_dbt particontragent on particontragent.t_partyid = dl_tick.t_partyid "
          + "      join davoiriss_dbt avoiriss on avoiriss.t_fiid = dl_tick.t_pfi "
          + "      join dfininstr_dbt fininstr on fininstr.t_fiid = dl_tick.t_pfi "
          + "      left join dparty_dbt issuerparty on issuerparty.t_partyid = fininstr.t_issuer "
          + "      join ddl_leg_dbt dl_leg on dl_leg.t_dealid = dl_tick.t_dealid and "
          + "                                 dl_leg.t_legkind = 0 and "
          + "                                 dl_leg.t_legid = 0 and "
          + "                                 dl_leg.t_principal >= ? "
          + "      where dl_tick.t_bofficekind = " + DL_SECURITYDOC/*101*/ + " and "  /*только БОЦБ*/
          + "            dl_tick.t_clientid > 0 and  " /*только клиентские*/
          + "            rsb_secur.IsRepo(rsb_secur.get_operationgroup(rsb_secur.get_opersystypes(dl_tick.t_dealtype, dl_tick.t_bofficekind))) = 0 and " /*только одночастные (не репо)*/
          + "            dl_tick.t_dealdate between ? and ? ";
      if(m_ClientType >= 0)
        if(m_ClientType == 0)
          query = query
          + "            and particlient.t_notresident != chr(88) ";
        else
          query = query
          + "            and particlient.t_notresident = chr(88) ";
        end;
      end;
      if(m_ContragentType >= 0)
        if(m_ContragentType == 0)
          query = query
          + "            and nvl(particontragent.t_notresident, chr(88)) != chr(88) ";
        else
          query = query
          + "            and nvl(particontragent.t_notresident, chr(0)) = chr(88) ";
        end;
      end;
      if(m_PaperType >= 0)
        if(m_PaperType == 0)
          query = query
          + "            and nvl(issuerparty.t_notresident, chr(0)) = chr(88) ";
        else
          query = query
          + "            and nvl(issuerparty.t_notresident, chr(88)) != chr(88) ";
        end;
      end;
      if(m_Market >= 0)
        if(m_Market == 0)
          query = query
          + "            and (dl_tick.t_marketid > 0 and rsb_secur.IsOTC(rsb_secur.get_operationgroup(rsb_secur.get_opersystypes(dl_tick.t_dealtype, dl_tick.t_bofficekind))) = 0) ";
        else
          query = query
          + "            and (dl_tick.t_marketid <= 0 or rsb_secur.IsOTC(rsb_secur.get_operationgroup(rsb_secur.get_opersystypes(dl_tick.t_dealtype, dl_tick.t_bofficekind))) = 1) ";
        end;
      end;
      if(m_AvrCode >= 0)
        query = query
          + "            and dl_tick.t_pfi = ? ";
      elif(m_AvrKind >= 0)
        query = query
          + "            and RSB_FIInstr.FI_AvrKindsEQ(2, ?, fininstr.t_avoirkind) = 1 ";
      end;
      query = query
          + ")"
          + "where t_dealsum >= ? ";
      if(m_InMarketList >= 0)
        if(m_InMarketList == 0)
          query = query
          + "            and t_marketlist = 'Включена' ";
        else
          query = query
          + "            and t_marketlist = 'Не включена' ";
        end;
      end;
      if(m_Route >= 0)
        if(m_Route == 0)
          query = query
          + "            and t_route = 'Покупка' ";
        else
          query = query
          + "            and t_route = 'Продажа' ";
        end;
      end;
      query = query
          + "order by t_dealdate, t_clientname, t_contragentname, t_papername";
    query = RSDCommand(query);
    query.AddParam("m_PaperNum", RSDBP_IN, m_PaperNum);
    query.AddParam("dt1", RSDBP_IN, m_BeginDate);
    query.AddParam("dt2", RSDBP_IN, m_EndDate);
    if(m_AvrCode >= 0)
      query.AddParam("m_AvrCode", RSDBP_IN, m_AvrCode);
    elif(m_AvrKind >= 0)
      query.AddParam("m_AvrKind", RSDBP_IN, m_AvrKind);
    end;
    query.AddParam("m_DealSum", RSDBP_IN, m_DealSum);
    query.Execute();
    m_RS = TRsbDataSet(query);
    while(m_RS.MoveNext())
      PrintTableLine(
          /* 1*/ "DealCode", m_RS.value("t_dealcode"), null,
          /* 2*/ "DealExtCode", m_RS.value("t_dealcodets"), null,
          /* 3*/ "DealDate", date(m_RS.value("t_dealdate")), null,
          /* 4*/ "DealDelivDate", date(m_RS.value("t_delfactdate")), null,
          /* 5*/ "DealPayDate", date(m_RS.value("t_payfactdate")), null,
          /* 6*/ "DealClient", m_RS.value("t_clientname"), null,
          /* 7*/ "DealClientType", m_RS.value("t_clientresident"), null,
          /* 8*/ "DealClientDogNum", m_RS.value("t_contrnumber"), null,
          /* 9*/ "DealContr", m_RS.value("t_contragentname"), null,
          /*10*/ "DealContrType", m_RS.value("t_contragentresident"), null,
          /*11*/ "DealCBCode", m_RS.value("t_papercode"), null,
          /*12*/ "DealCBName", m_RS.value("t_papername"), null,
          /*13*/ "DealCBType", m_RS.value("t_papertype"), null,
          /*14*/ "DealCBMarketList", m_RS.value("t_marketlist"), null,
          /*15*/ "DealCBMarket", m_RS.value("t_market"), null,
          /*16*/ "DealRoute", m_RS.value("t_route"), null,
          /*17*/ "DealSum", m_RS.value("t_dealsum"), 2,
          /*18*/ "DealCBCount", m_RS.value("t_papercount"), m_RS.value("t_sumprecision"));
    end;

    EndTable();
    
  END;
  
  initTX_RegistrReport(W112_Panel(), "Report_W112.xls");

END;

/*Точка входа*/
var defprec = SetDefPrec(12);
var r = W112_Report();
r.InitReport("Фин.мониторинг для БОФР", null);
r.Run();
SetDefPrec(defprec);

exit(1);