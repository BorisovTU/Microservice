/*****************************************************************************/
/*                                                           R-Style Softlab */
/*                                                                           */
/*               Отчет по собственным сделкам в разрезе заявок               */
/*                                                                           */
/*  Create : Chesnokov D.S.                                                  */
/*  Date   : 27.03.2019                                                      */
/*****************************************************************************/
import RsbFormsInter, globals, Календарь, sp_class, captureoutput;
import or_tpl_h;
import or_const;

private const FT_INT32  = 1;
private const FT_STRING = 7;
private const FT_DATE   = 9;

var ExitCode:integer, FProcPanel:TRsbPanel, Rep, rn = 1, sheet = 1;
var csvFile, fName, csvTable, printEngine;

private var _sqlString:string;

    macro initRep()
		   printEngine = CTemplateSXLSX(NULL);

         if(printEngine.UsePoi())
            printEngine.SetXLSXFormat();
         end;

         printEngine.CreateTotalBook();
         InitProgress(-1, "", ""); 
         printEngine.OpenTemplate("Report_dealreq.xlsx", true);
         printEngine.SheetChange(1);

         csvTable = printEngine.RegisterTable("templateTable", "templateTableHeader", true);

         csvFile = C_CSVFile();

         if (printEngine.UsePoi())
             csvFile.SetLimitRow(FLUSH_COUNT_XLSX);
         end;
         fName = csvFile.CreateFullFileName("Report_dealreq_csv.txt");
         csvFile.FileOpen(fName, true);
    end;

macro print_header(Type, Header_str)
       printEngine.SetValue_NameCell("templateTitle", "Отчет по собственным сделкам в разрезе заявок за период с " + FProcPanel.BegDate + " по " + FProcPanel.EndDate);
       printEngine.CopyAllSheetInTotalBook(null, false, "templateHeader", 0, "Протокол");
end;

macro PrintStr(DealCode, DealDate, KindDeal, ISIN, SumObligation, SumLiability, DealCur, KindRequest)

      csvFile.AddCell(DealCode); 
      csvFile.AddCell(DealDate); 
      csvFile.AddCell(KindDeal); 
      csvFile.AddCell(ISIN);
      csvFile.AddCell(SumObligation); 
      csvFile.AddCell(SumLiability); 
      csvFile.AddCell(DealCur); 
      csvFile.AddCell(KindRequest);
      csvFile.AddCell(" "); 
      csvFile.AddRow();      
end;

    macro showRep()
  		csvFile.FileClose();
      csvTable.FillTabelFromCSV(csvFile.GetlsFileName());
      csvTable.EndTable();

      printEngine.CopyAllSheetInTotalBook(null, false, csvTable.GetTableRange(), 0, "Протокол");
      printEngine.Close();
      printEngine.SaveTotalBook();
      remProgress();
    end;

macro GetData(BegDate, EndDate)
  
  var rs, cmd, AllObligation = $0, AllLiability = $0;
  StartCaptureOutput();
  [ SELECT t_dealcode, t_dealid, t_dealdate,
           t_dealtype, NVL(t_kinddeal, ' ') as t_kinddeal, t_partyid,
           t_isin, NVL(t_kind, ' ' ) as t_request, t_cur,
           SUM (obligation) as t_obligation,
           SUM (liability) as t_liability
      FROM (SELECT t_dealcode, t_dealid, t_dealdate,
                   t_dealtype, t_partyid,
                   CASE
                      WHEN t_dealtype IN (2127, 12127, 12128, 12129, 12137, 12138, 12139)
                      THEN 'Прямое РЕПО'
                      WHEN t_dealtype IN (2122, 2124, 12122, 12123, 12132, 12133, 12134)
                      THEN 'Обратное РЕПО'
                      WHEN t_dealtype IN (2162, 12143, 12144, 12183)
                      THEN 'Покупка'
                      WHEN t_dealtype IN (2172, 12153, 12154, 12193)
                      THEN 'Продажа'
                   END as t_kinddeal,
                   (SELECT t_name
                      FROM dobjattr_dbt o
                     WHERE     t_objecttype = 101
                           AND t_groupid = 105
                           AND t_attrid = RSB_SECUR.GetMainObjAttr (101, LPAD (t.T_DEALID, 34, '0'), 105, SYSDATE)) as t_kind,
                   CASE WHEN rq.t_kind = 0 THEN rq.t_amount ELSE 0 END liability,
                   CASE WHEN rq.t_kind = 1 THEN rq.t_amount ELSE 0 END obligation,
                   (SELECT t_isin FROM davoiriss_dbt WHERE t_fiid = t.t_pfi) as t_isin,
                   (SELECT t_ccy FROM dfininstr_dbt WHERE t_fiid = rq.t_fiid) as t_cur
              FROM ddl_tick_dbt t, ddlrq_dbt rq
             WHERE t_clientid = -1
               AND t_dealdate BETWEEN ? AND ?
               AND t_dealtype NOT IN (3143, 3153, 12183, 12193, 12132, 12137)
               AND t.t_dealid = rq.t_docid
               AND rq.t_dockind = 101
               AND rq.t_subkind = 0
               AND rq.t_type <> 6) q
    GROUP BY t_dealcode,
             t_dealid,
             t_dealdate,
             t_dealtype,
             t_kinddeal,
             t_isin,
             t_partyid,
             t_kind,
             t_cur
    ORDER BY t_dealdate, t_dealid 
  ];
  
  cmd = RSDCommand(EndCaptureOutput());
  cmd.AddParam("1", RSDBP_IN, BegDate);
  cmd.AddParam("2", RSDBP_IN, EndDate);
  rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  while(rs and rs.MoveNext())
    PrintStr(rs.value("t_dealcode"), Date(rs.value("t_dealdate")), rs.value("t_kinddeal"), rs.value("t_isin"), 
             rs.value("t_obligation"), rs.value("t_liability"), rs.value("t_cur"), rs.value("t_request"));
    AllObligation = AllObligation + rs.value("t_obligation");
    AllLiability = AllLiability + rs.value("t_liability");
  end;
   PrintStr("", "", "", "ИТОГО:", AllObligation, AllLiability, "", "");
end;

class(TRsbPanel) DatePanel()

  var LBegDate:TRsbLabel, LEndDate:TRsbLabel,
      FieldBegDate:TRsbEditField, FieldEndDate:TRsbEditField,
      BegDate:date = date(0,0,0), EndDate:date = date(0,0,0);

  InitTRsbPanel();
  SetCaption("Выберите период отчета");
  SetStatus("~Esc~ Выход ~F3~ Календарь ~F2~ Выполнить ~F9~ Выполнить");
  SetSize(28, 5);
  SetPosition(45,10);
  
  LBegDate = TRsbLabel(3, 2, "Начало периода:");
  addLabel(LBegDate);
  LEndDate = TRsbLabel(3, 3, "Конец  периода:");
  addLabel(LEndDate);
  
  FieldBegDate = TRsbEditField(FT_DATE);
  FieldBegDate.setPosition(15,2);
  FieldBegDate.setSize(10,1);
  FieldBegDate.Name = "BegDate";
  FieldBegDate.bindValue( this, "BegDate" );
  FieldBegDate.value = {Curdate};
  addControl(FieldBegDate);
  
  FieldEndDate = TRsbEditField(FT_DATE);
  FieldEndDate.setPosition(15,3);
  FieldEndDate.setSize(10,1);
  FieldEndDate.Name = "EndDate";
  FieldEndDate.bindValue( this, "EndDate" );
  FieldEndDate.value = {Curdate};
  addControl(FieldEndDate);
  
  addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "KeyEvent"));//KeyEvent
  
  
  macro KeyEvent(RsbEvent:Object)
    var focusedControl;
    
    if (rsbEvent.KeyCode == 27)//ESC
      this.Close(rsbEvent.KeyCode);
    elif ((rsbEvent.KeyCode == 316) or (rsbEvent.KeyCode == 323))//F2 or F9
      BegDate = FieldBegDate.value;
      EndDate = FieldEndDate.value;
      if (BegDate > EndDate)
        prompt("Укажите правильную дату","BegDate");
        this.setFocus("BegDate");
      else
        this.Close(rsbEvent.KeyCode);
      end;
    elif (rsbEvent.KeyCode == 317)//F3
      focusedControl = this.focusedControl();
      if(focusedControl.Name == "BegDate")
        BegDate = GetDateByCalendar(BegDate);
        FieldBegDate.Value = BegDate;
      elif(focusedControl.Name == "EndDate")
        EndDate = GetDateByCalendar(EndDate);
        FieldEndDate.Value = EndDate;
      end;
    end;
  end;
  
end;


FProcPanel = DatePanel();
ExitCode = FProcPanel.Run();

 if(ExitCode)

   initRep();
   print_header();
   GetData(FProcPanel.BegDate, FProcPanel.EndDate);
   showRep();

 end;
exit(1);