import RegistrReport, Проценты;

CLASS (TX_RegistrReport) TX_ReportUser
  var m_partyid, m_clientname;
  var m_reportname;
  var m_mode;

  /*Инициализация*/
  macro InitReport(reportname, mode)
    m_reportname = reportname;
    m_mode = mode;
  end;

  /*Выравнивание в ячейке*/
  CLASS (CFormat) CFontAlign( CurrentCSV:STRING, Row:INTEGER, CellID:INTEGER, HorAlign:integer, VerAlign:integer )
     VAR m_HorAlign = HorAlign;
     VAR m_VerAlign = VerAlign;

     MACRO SetFormat( AddressDiapazon:OBJECT )
        VAR Address;
        if( m_CellID == NULL ) /*Для всей строки*/
           Address = AddressDiapazon.GetRow( m_Row );
        else
           Address = AddressDiapazon.GetCell( m_Row, m_CellID );
        end;
        __BUG_Global_m_ObjTemplateXLS.SetDiapazon( Address );
        if(valtype(m_HorAlign) == V_INTEGER)
          __BUG_Global_m_ObjTemplateXLS.Range(Address).HorizontalAlignment = m_HorAlign;
        end;
        if(valtype(m_VerAlign) == V_INTEGER)
          __BUG_Global_m_ObjTemplateXLS.Range(Address).VerticalAlignment = m_VerAlign;
        end;
     ONERROR
       return;
     END;
     initCFormat( CurrentCSV, Row, CellID );
  END;

  MACRO SetCellAlign( CellName:STRING, HorAlign:integer, VerAlign:integer )
     if( m_UseTemplate AND ( m_OutMode != DL_OUTREPORT_EXCEL_NO_FORMAT ) )
        if( m_UseCSV )
           m_CellsFormat[m_CellsFormat.Size] = CFontAlign( m_CurrentCSV, m_CurrentRow, NC(CellName), HorAlign, VerAlign );
        else
           m_ObjTemplateXLS.SetDiapazon( m_ReportTable.GetCurAddress(CellName) );
           if(valtype(HorAlign) == V_INTEGER)
             m_ObjTemplateXLS.Range(m_ReportTable.GetCurAddress(CellName)).HorizontalAlignment = HorAlign;
           end;
           if(valtype(VerAlign) == V_INTEGER)
             m_ObjTemplateXLS.Range(m_ReportTable.GetCurAddress(CellName)).VerticalAlignment = VerAlign;
           end;
        end;
     end;
  END;

  /*Формат числа*/
  CLASS (CFormat) CFontNumberFormat( CurrentCSV:STRING, Row:INTEGER, CellID:INTEGER, NumberFormat:string )
     VAR m_NumberFormat = NumberFormat;

     MACRO SetFormat( AddressDiapazon:OBJECT )
        VAR Address;
        if( m_CellID == NULL ) /*Для всей строки*/
           Address = AddressDiapazon.GetRow( m_Row );
        else
           Address = AddressDiapazon.GetCell( m_Row, m_CellID );
        end;
        __BUG_Global_m_ObjTemplateXLS.SetDiapazon( Address );
        __BUG_Global_m_ObjTemplateXLS.sheet.Range( Address ).NumberFormat = m_NumberFormat;
     ONERROR
       return;
     END;
     initCFormat( CurrentCSV, Row, CellID );
  END;

  MACRO SetCellNumberFormat( CellName:STRING, NumberFormat:string )
     if( m_UseTemplate AND ( m_OutMode != DL_OUTREPORT_EXCEL_NO_FORMAT ) )
        if( m_UseCSV )
           m_CellsFormat[m_CellsFormat.Size] = CFontNumberFormat( m_CurrentCSV, m_CurrentRow, NC(CellName), NumberFormat );
        else
           m_ObjTemplateXLS.SetDiapazon( m_ReportTable.GetCurAddress(CellName) );
           m_ObjTemplateXLS.Range(m_ReportTable.GetCurAddress(CellName)).NumberFormat = NumberFormat;
        end;
     end;
  END;

  /*Удаление строки*/
  CLASS (CFormat) CDelRow( CurrentCSV:STRING, Row:integer, dRow:integer, bCell:string, eCell:string)
     var m_dRow = dRow;
     var m_bCell = bCell;
     var m_eCell = eCell;

     MACRO SetFormat( AddressDiapazon:OBJECT )
        var Address;
        var eRow = AddressDiapazon.Get_bRow() + 1;
        var bRow = eRow - m_dRow + 1;
        if(bRow == eRow)
          Address = m_bCell+bRow+":"+m_eCell+bRow;
        else
          Address = m_bCell+bRow+":"+m_eCell+bRow+":"+m_bCell+eRow+":"+m_eCell+eRow;
        end;
        __BUG_Global_m_ObjTemplateXLS.SetDiapazon( Address );
        __BUG_Global_m_ObjTemplateXLS.sheet.Range( Address ).Delete();
     ONERROR
       return;
     END;
     initCFormat( CurrentCSV, Row );
  END;

  MACRO DelRow( dRow, bCell, eCell )
     if( m_UseTemplate AND ( m_OutMode != DL_OUTREPORT_EXCEL_NO_FORMAT ) )
        if( m_UseCSV )
           m_CellsFormat[m_CellsFormat.Size] = CDelRow( m_CurrentCSV, m_CurrentRow, dRow, bCell, eCell );
        else
           m_ObjTemplateXLS.SetDiapazon( m_ReportTable.GetCurAddress() );
           m_ObjTemplateXLS.Range(m_ReportTable.GetCurAddress()).DeleteRow();
        end;
     end;
  END;

  /*Группировка*/
  CLASS (CFormat) CGroupRows( CurrentCSV:STRING, bRow:integer, eRow:integer)
     var m_bRow = bRow;
     var m_eRow = eRow;
     MACRO SetFormat( AddressDiapazon:OBJECT )
        var Address;
        if(m_bRow == m_eRow)
          Address = m_bRow+":"+m_bRow;
        else
          Address = m_bRow+":"+m_bRow+":"+m_eRow+":"+m_eRow;
        end;
        __BUG_Global_m_ObjTemplateXLS.sheet.Range(Address).EntireRow.Group();
     ONERROR
       return;
     END;
     initCFormat( CurrentCSV );
  END;

  MACRO GroupRows( bRow, eRow )
     if( m_UseTemplate AND ( m_OutMode != DL_OUTREPORT_EXCEL_NO_FORMAT ) )
        if( m_UseCSV )
           m_CellsFormat[m_CellsFormat.Size] = CGroupRows( m_CurrentCSV, bRow, eRow );
        else
           m_ObjTemplateXLS.SetDiapazon( m_ReportTable.GetCurAddress() );
           m_ObjTemplateXLS.Range(m_ReportTable.GetCurAddress()).SetGroupRows();
        end;
     end;
  END;

  /*Автофильтр*/
  CLASS (CFormat) CAutoFilter( CurrentCSV:STRING, Row:integer, bCell:string, eCell:string)
     var m_bCell = bCell;
     var m_eCell = eCell;
     MACRO SetFormat( AddressDiapazon:OBJECT )
      __BUG_Global_m_ObjTemplateXLS.SetAutoFilter( m_bCell+m_Row+":"+m_eCell+m_Row );
     ONERROR
       return;
     END;
     initCFormat( CurrentCSV, Row );
  END;

  MACRO AutoFilter( Row, bCell, eCell )
     if( m_UseTemplate AND ( m_OutMode != DL_OUTREPORT_EXCEL_NO_FORMAT ) )
        if( m_UseCSV )
           m_CellsFormat[m_CellsFormat.Size] = CAutoFilter( m_CurrentCSV, Row, bCell, eCell );
        else
           m_ObjTemplateXLS.SetDiapazon( m_ReportTable.GetCurAddress() );
           m_ObjTemplateXLS.Range(m_ReportTable.GetCurAddress()).AutoFilter();
        end;
     end;
  END;

  /*Нарисовать рамку*/
  CLASS (CFormat) CCreateBorder(CurrentCSV:STRING, Row:integer, bRow:integer, eRow:integer, bCell:string, eCell:string, Border:integer, Style:integer, Weight:integer)
     var m_bRow = bRow;
     var m_eRow = eRow;
     var m_bCell = bCell;
     var m_eCell = eCell;
     var m_Border = Border;
     var m_Style = Style;
     var m_Weight = Weight;

     MACRO SetFormat( AddressDiapazon:OBJECT )
        var Address;
        if(m_bRow == m_eRow)
          Address = m_bCell+m_bRow+":"+m_eCell+m_bRow;
        else
          Address = m_bCell+m_bRow+":"+m_eCell+m_bRow+":"+m_bCell+m_eRow+":"+m_eCell+m_eRow;
        end;
        __BUG_Global_m_ObjTemplateXLS.sheet.Range( Address ).Borders(m_Border).LineStyle = m_Style;
        __BUG_Global_m_ObjTemplateXLS.sheet.Range( Address ).Borders(m_Border).Weight = m_Weight;
     ONERROR
       return;
     END;
     initCFormat( CurrentCSV, Row );
  END;

  MACRO CreateBorder( bRow, eRow, bCell, eCell, Border, Style, Weight )
     if( m_UseTemplate AND ( m_OutMode != DL_OUTREPORT_EXCEL_NO_FORMAT ) )
        if( m_UseCSV )
           m_CellsFormat[m_CellsFormat.Size] = CCreateBorder( m_CurrentCSV, m_CurrentRow, bRow, eRow, bCell, eCell, Border, Style, Weight );
        end;
     end;
  END;

  /*Показать отчет на экран*/
  /*Явно передадим ShowAppl, чтобы не отображать отчет на экране для массового формирования*/
  MACRO CReport_Show( NumCopy:INTEGER, ShowAppl)
     VAR FileName : STRING = "";
     if(valtype(ShowAppl) != V_BOOL)
       ShowAppl = true;
     end;

    if(not CReport_DataExist()) // если нет данных, не надо ничего показывать,
      AuditLog(REPORT_NO_DATA, @AuditMsg);
      CReport_NoDataMsg();      // кроме, может быть, сообщения пользователю.
      return;
    end;

     if( __CalculateTimeExecute__ )
        BeginMarkTime();
     end;

     if( m_UseTemplate )
        if( m_UseTotalBook == false )
           DeleteNotUsedSheet();
        end;

        if( m_IsWebService )
          ShowAppl = false;
        end;

        m_ObjTemplateXLS.SheetChange( 1 );
        SplitFile( GetUniqAdd( m_TemplateName ), FileName );
        m_ObjTemplateXLS.SaveAsTemplate( FileName, ShowAppl );
        AuditLog(REPORT_FINISH, @AuditMsg, m_ObjTemplateXLS.ReportFileName_xls);

        /*Явное закрытие, если не надо показывать*/
        if(not ShowAppl)
          m_ObjTemplateXLS.Close();
        end;

        m_FullReportFileNames[m_FullReportFileNames.Size] = m_ObjTemplateXLS.ReportPath + "\\" + m_ObjTemplateXLS.ReportFileName_xls;

        /*вызвать деструкторы что бы отконнетится. Актуально только если отвалились при выполнении по exception*/
        if( WR_GetLoadNewApplicationMode() or (NumCopy == 1)/*последняя*/ )
           m_ObjTemplateXLS = __BUG_Global_m_ObjTemplateXLS = NULL;
        end;
     else
        m_ObjWinRep.CReport_Show(NumCopy, AuditMsg);
        /*все должно быть в одном тектовом файле, поэтому только один объект на все копии*/
        if( NumCopy == 1 )
           m_FullReportFileNames = m_ObjWinRep.GetFullReportFileNames();
           m_ObjWinRep = NULL;
        end;
     end;

     if( __CalculateTimeExecute__ )
        EndMarkTime( "Время показа в Excel (CReport_Show):" );
     end;
  END;


  /*Только Excel*/
  PRIVATE MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;
  
  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
    Dl_CallInsertStat(PrintMode());
  END;

  /*Вывод на экран строки*/
  macro f_MsgBox(str)
    var j = 0;
    while(j < strlen(str))      
      msgboxex(substr(str, j+1, 1000));
      j = j + 1000;      
    end;
  end;

END;

