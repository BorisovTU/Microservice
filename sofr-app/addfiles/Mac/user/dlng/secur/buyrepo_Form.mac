/*
$Name:             BUYREPO_Form.mac
$Module:           Ценные бумаги
$Description:      Реестр предстоящих выплат по облгациям
*/
import "DlRepForm_h.mac", "globals.mac", "dlquery.mac", "spserv.mac";

CONST PNFLD_BUYREPO_BEGDATE      = 0,
      PNFLD_BUYREPO_ENDDATE      = 1,
      PNFLD_BUYREPO_AVRCODE      = 2,
      PNFLD_BUYREPO_AVRNAME      = 3;

PRIVATE MACRO DL_ListAvoiriss( WhereCondition:STRING, AdditionalFromTable:STRING, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):BOOL
  VAR Fininstr = TRecHandler( "fininstr.dbt" );

  if( ListAvoiriss( 0, Fininstr, null, null, WhereCondition, AdditionalFromTable ) )
     FldRealValue = Fininstr.rec.FIID;
     FldShowValue = Fininstr.rec.FI_Code;
     return true;
  end;
  return false;
END;

/*Код Ц/Б*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr,  WhereCond;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = ПолучитьКодФинИн(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     Fininstr = TRecHandler( "fininstr.dbt" );

     if( (FldShowValue == STR_ALLVALUE) or (StrLen(Trim(FldShowValue)) == 0) )
        FldShowValue = STR_ALLVALUE;
        FldRealValue = -1;
        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, "" );
     else
        if( ПолучитьФинИн(FldShowValue, Fininstr) 
        )
           MsgBox( "Неверное значение поля" );
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;

        FldRealValue = Fininstr.rec.FIID;
        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
/*     WhereCond = " AV.t_FIID = t.t_FIID " ;
     WhereCond = " AV.t_FIID = t.t_FIID " +
                 " AND " + IIF(CheckTaxDepositoryMode() == false, " AV.t_SPIsClosed <> CHR(88) ", " t.t_IsClosed <> CHR(88) ") +
                 " AND t.t_Issuer = " + {OurBank};  */
     DL_ListAvoiriss( "", "", @FldShowValue, @FldRealValue );
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) buyrepo_Form_Panel()
  var BegDate:date    = {curdate},
      EndDate:date    = {curdate},
      FIID:integer = 0 ;
      
  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_BUYREPO_BEGDATE,      DL_PNFLD_BEGINDATE,    @DL_FldProc_BeginDate,   BegDate);
     AddFieldProc( 0, PNFLD_BUYREPO_ENDDATE,      DL_PNFLD_ENDDATE,      @DL_FldProc_EndDate,     EndDate);
     AddFieldProc( 0, PNFLD_BUYREPO_AVRCODE,      DL_PNFLD_AVRCODE,      @FldProc_AvrCode,        FIID );
     AddFieldProc( 0, PNFLD_BUYREPO_AVRNAME,      DL_PNFLD_AVRNAME,      @DL_FldProc_AvrName,     "" );

  END;


  private macro GetFieldValue(fldNum:integer):variant
    var showValue:variant, fldValue:variant;

    GetFieldValue(fldNum, @showValue, @fldValue);
    return fldValue;
  end;
  macro Run()
    var stat = Run();
    BegDate = GetFieldValue(PNFLD_BUYREPO_BEGDATE);
    EndDate = GetFieldValue(PNFLD_BUYREPO_ENDDATE);
    FIID   = GetFieldValue(PNFLD_BUYREPO_AVRCODE);

    return stat;
  end;

  PRIVATE MACRO Check():BOOL
    if(GetFieldValue(PNFLD_BUYREPO_BEGDATE) > GetFieldValue(PNFLD_BUYREPO_ENDDATE))
      MsgBox("Дата начала отчетного периода не должна быть больше даты окончания");
      return false;
    end;

    return true;
  END;

  initDL_CPanel( "sp_repu.lbr", "PBUYREPO" );

END;
