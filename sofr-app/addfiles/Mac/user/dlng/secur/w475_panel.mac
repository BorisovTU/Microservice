/*
$Name:        w475.mac
$Module:      Ценные бумаги
$Description: Отчет о результатах брокерского обслуживания для ДРРК
$Programmer:  Кротов А.
*/
import "DlRepForm_h.mac", "globals.mac", rsd, dlquery;

/*Номера полей в форме отчета*/
CONST P_W475_BEGINDATE            = 0,
      P_W475_ENDDATE              = 1,
      P_W475_INVESTORCODE         = 2,
      P_W475_SERVCONTR            = 3,
      P_W475_StockMarket          = 4,
      P_W475_FuturesMarket        = 5,
      P_W475_OverTheCounterMarket = 6,
      P_W475_CurrencyMarket       = 7,
      P_W475_ShowReport           = 8;

/* Обработчики для нестандарных контролов */
private CONST H_StockMarket          = 101,
              H_FuturesMarket        = 102,
              H_OverTheCounterMarket = 103,
              H_CurrencyMarket       = 104,
              H_ShowReport           = 105;

/**/
private MACRO DL_FldProc_CheckBox( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "X";
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      if(FldRealValue == "X")
        FldShowValue = FldRealValue = " ";
      else
        FldShowValue = FldRealValue = "X";
      end;
    end;
  end;

  return CM_DEFAULT;
END;

/*Установка значений в панели*/
private macro SetName(pThis:DL_CPanel)
  var rscmd, rscnt;
    rscmd = null;
    rscmd = DL_RSDCommand("select count(1) cnt, "
                         + "      nvl(sum(case when t_setflag = chr(88) then 1 else 0 end),0) cntx, "
                         + "      max(case when t_setflag = chr(88) then t_clientname else ' ' end) t_clientname "
                         + "from dset_cln_u_tmp_");
    rscnt = rscmd.Execute();
    rscnt.movenext();
    if(rscnt.value("cnt") == rscnt.value("cntx"))
      if(rscnt.value("cnt") == 0)
        pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, "Не выбран");
      else
        pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, STR_ALLVALUE);
      end;
    elif(rscnt.value("cntx") > 1)
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, "Несколько");
    elif(rscnt.value("cntx") == 1)
      pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE, rscnt.value("t_clientname"));
    elif(rscnt.value("cntx") == 0)
      pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE, "Не выбран");
    end;
    rscmd = null;
    rscmd = DL_RSDCommand("select count(1) cnt, "
                         + "      nvl(sum(case when t_setflag = chr(88) then 1 else 0 end), 0) cntx, "
                         + "      max(case when t_setflag = chr(88) then t_contrnumber else ' ' end) t_contrnumber "
                         + "from dset_sfc_u_tmp_");
    rscnt = rscmd.Execute();
    rscnt.movenext();
    if(rscnt.value("cnt") == rscnt.value("cntx"))
      if(rscnt.value("cnt") == 0)
        pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, "Не выбран");
      else
        pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, STR_ALLVALUE);
      end;
    elif(rscnt.value("cntx") > 1)
      pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, "Несколько");
    elif(rscnt.value("cntx") == 1)
      pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, rscnt.value("t_contrnumber"));
    elif(rscnt.value("cntx") == 0)
      pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, "Не выбран");
    end;
end;

/*Выбор клиентов*/
macro SelectClient()
  /*Обработчик скроллинга*/
  macro Client_Scroll(rs, cmd, id, key)
    /*Обработчик*/
    if(cmd == DLG_KEY)
      /*Enter*/
      if(key == 13)
        return CM_IGNORE;
      /*Esc*/
      elif(key == 27)
        return CM_CANCEL;
      /*Space*/
      elif(key == 32)
        if(rs.RecCount > 0)
          rs.edit();
          if(rs.value("t_setflag") == "X")
            rs.value("t_setflag") = " ";
            SQL_Execute("update dset_sfc_u_tmp_ set t_setflag = chr(0) where t_clientid = " + rs.value("t_clientid"));
          else                                                                                         
            rs.value("t_setflag") = "X";
            SQL_Execute("update dset_sfc_u_tmp_ set t_setflag = chr(88) where t_clientid = " + rs.value("t_clientid"));
          end;
          rs.update();
          updatescroll(rs, 5);
          return CM_IGNORE;
        end;
      end;
    end;
  end;
  var rs, cmd;
  var que = "select t_setflag, t_clientid, t_clientname from dset_cln_u_tmp_ order by t_clientname";
  cmd = RsdCommand(que);
  rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  while(RunScroll(rs, 3, MakeArray("t_setflag", "В", 1, 2, -1, 0,  
                                   "t_clientname", "Наименование клиента", 100, 2, -1, 0,
                                   "t_clientid", "Наименование клиента", 8, 2, -1, 0), 
                  null, "Client_Scroll", "Выбор клиентов", "~Esc~ Выход ~Пробел~ Изменить", false))
    cmd = RsdCommand(que);
    rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  end;
end;

/*Обработчик поля клиент*/
macro Refresh_Clients(BeginDate, EndDate)
  var rscmd, rscnt;
    SQL_Execute("delete from dset_cln_u_tmp_");
    SQL_Execute("insert into dset_cln_u_tmp_(t_setflag, t_clientid, t_clientname) "
               +"select chr(88), party.t_partyid, party.t_name "
               +"from ddlcontr_dbt dlcontr "
               +"join dsfcontr_dbt sfcontr on sfcontr.t_id = dlcontr.t_sfcontrid and "
               + "                            sfcontr.t_datebegin <= " + GetSQLDate(EndDate) + " and "
               + "                            (sfcontr.t_dateclose = to_date('01.01.0001','dd.mm.yyyy') or "
               + "                             sfcontr.t_dateclose >= " + GetSQLDate(BeginDate) + ") "
               +"join dparty_dbt party on party.t_partyid = sfcontr.t_partyid "
               +"group by party.t_partyid, party.t_name");
    rscmd = DL_RSDCommand("select count(1) cnt from dset_cln_u_tmp_");
    rscnt = rscmd.Execute();
    rscnt.movenext();
    return rscnt.value("cnt");
end;
private MACRO DL_FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if(Cmd == DLG_INIT)
    var rscmd, rscnt;
    rscmd = DL_RSDCommand("select count(1) cnt from dset_cln_u_tmp_");
    rscnt = rscmd.Execute();
    rscnt.movenext();
    if(rscnt.value("cnt") == 0)
      if(Refresh_Clients(pThis.GetLinkedValue(DL_PNFLD_BEGINDATE), pThis.GetLinkedValue(DL_PNFLD_ENDDATE)) == 0)
        FldShowValue = "Не выбран";
      else
        FldShowValue = STR_ALLVALUE;
      end;
    else
      SetName(pThis);
      FldShowValue = pThis.GetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE);
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
    if(pThis.GetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE) != STR_ALLVALUE)
      SQL_Execute("update dset_cln_u_tmp_ set t_setflag = chr(88)");
      SQL_Execute("update dset_sfc_u_tmp_ set t_setflag = chr(88)");
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, STR_ALLVALUE);
      pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, STR_ALLVALUE);
      FldShowValue = STR_ALLVALUE;
    else
      SQL_Execute("update dset_cln_u_tmp_ set t_setflag = chr(0)");
      SQL_Execute("update dset_sfc_u_tmp_ set t_setflag = chr(0)");
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, "Не выбран");
      pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, "Не выбран");
      FldShowValue = "Не выбран";
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
    SelectClient();
    SetName(pThis);
    FldShowValue = pThis.GetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE);
  end;
  FldRealValue = FldShowValue;

  return CM_DEFAULT;
END;

/*Выбор договоров обслуживания*/
macro SelectSfcontr()
  /*Обработчик скроллинга*/
  macro Sfcontr_Scroll(rs, cmd, id, key)
    /*Обработчик*/
    if(cmd == DLG_KEY)
      /*Enter*/
      if(key == 13)
        return CM_IGNORE;
      /*Esc*/
      elif(key == 27)
        return CM_CANCEL;
      /*Space*/
      elif(key == 32)
        if(rs.RecCount > 0)
          rs.edit();
          if(rs.value("t_setflag") == "X")
            rs.value("t_setflag") = " ";
          else                                                                                         
            rs.value("t_setflag") = "X";
          end;
          rs.update();
          updatescroll(rs, 5);
          return CM_IGNORE;
        end;
      end;
    end;
  end;
  var rs, cmd;
  var que = "select t_setflag, t_clientid, t_clientname, t_contrid, t_contrnumber, t_contrname, t_contrdate "
          + "from dset_sfc_u_tmp_ sfc "
          + "where exists(select 1 from dset_cln_u_tmp_ cln where cln.t_clientid = sfc.t_clientid and cln.t_setflag = chr(88)) "
          + "order by t_clientname";
  cmd = RsdCommand(que);
  rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  while(RunScroll(rs, 5, MakeArray("t_setflag", "В", 1, 2, -1, 0,  
                                   "t_clientname", "Наименование клиента", 50, 2, -1, 0, 
                                   "t_contrnumber", "Номер договора", 20, 2, -1, 0,
                                   "t_contrname", "Наименование договора", 50, 2, -1, 0,
                                   "t_contrdate", "Дата договора", 10, 2, -1, 0), 
                  null, "Sfcontr_Scroll", "Выбор договоров обслуживания", "~Esc~ Выход ~Пробел~ Изменить", false/*, posx, posy*/))
    cmd = RsdCommand(que);
    rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  end;
end;

/*Обработчик поля договор обслуживания*/
macro Refresh_Contracts(BeginDate, EndDate)
  var rscmd, rscnt;
    SQL_Execute("delete from dset_sfc_u_tmp_");
    SQL_Execute("insert into dset_sfc_u_tmp_(t_setflag, t_clientid, t_clientname, t_contrid, t_contrnumber, t_contrname, t_contrdate) "
               +"select chr(88), party.t_partyid, party.t_name, sfcontr.t_id, sfcontr.t_number, sfcontr.t_name, sfcontr.t_datebegin "
               +"from ddlcontr_dbt dlcontr "
               +"join dsfcontr_dbt sfcontr on sfcontr.t_id = dlcontr.t_sfcontrid and "
               + "                            sfcontr.t_datebegin <= " + GetSQLDate(EndDate) + " and "
               + "                            (sfcontr.t_dateclose = to_date('01.01.0001','dd.mm.yyyy') or "
               + "                             sfcontr.t_dateclose >= " + GetSQLDate(BeginDate) + ") "
               +"join dparty_dbt party on party.t_partyid = sfcontr.t_partyid");
    rscmd = DL_RSDCommand("select count(1) cnt from dset_sfc_u_tmp_");
    rscnt = rscmd.Execute();
    rscnt.movenext();
    return rscnt.value("cnt");
end;
private MACRO DL_FldProc_Contract( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if(Cmd == DLG_INIT)
    var rscmd, rscnt;
    rscmd = DL_RSDCommand("select count(1) cnt from dset_sfc_u_tmp_");
    rscnt = rscmd.Execute();
    rscnt.movenext();
    if(rscnt.value("cnt") == 0)
      if(Refresh_Contracts(pThis.GetLinkedValue(DL_PNFLD_BEGINDATE), pThis.GetLinkedValue(DL_PNFLD_ENDDATE)) == 0)
        FldShowValue = "Не выбран";
      else
        FldShowValue = STR_ALLVALUE;
      end;
    else
      SetName(pThis);
      FldShowValue = pThis.GetLinkedValue(DL_PNFLD_CONTRACT_OFBU);
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
    if(pThis.GetLinkedValue(DL_PNFLD_CONTRACT_OFBU) != STR_ALLVALUE)
      SQL_Execute("update dset_sfc_u_tmp_ set t_setflag = chr(88)");
      SQL_Execute("update dset_cln_u_tmp_ set t_setflag = chr(88)");
      pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, STR_ALLVALUE);
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, STR_ALLVALUE);
      FldShowValue = STR_ALLVALUE;
    else
      SQL_Execute("update dset_sfc_u_tmp_ set t_setflag = chr(0)");
      SQL_Execute("update dset_cln_u_tmp_ set t_setflag = chr(0)");
      pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, "Не выбран");
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, "Не выбран");
      FldShowValue = "Не выбран";
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
    SelectSfcontr();
    SetName(pThis);
    FldShowValue = pThis.GetLinkedValue(DL_PNFLD_CONTRACT_OFBU);
  end;
  FldRealValue = FldShowValue;

  return CM_DEFAULT;
END;

/*Начальная дата выпуска отчета*/
private MACRO DL_FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;
     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
        if( pThis.GetLinkedValue(DL_PNFLD_ENDDATE) < FldRealValue )
           pThis.SetLinkedValue( DL_PNFLD_ENDDATE, FldRealValue );
        end;
     end;
     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
       if( FldRealValue > pThis.GetLinkedValue(DL_PNFLD_ENDDATE) )
         MsgBox( "Дата окончания не может быть меньше даты начала периода.");
         return CM_IGNORE;
       end;
     end; 
     if(Refresh_Clients(FldRealValue, pThis.GetLinkedValue(DL_PNFLD_ENDDATE)) == 0)
       pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, "Не выбран");
     else
       pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, STR_ALLVALUE);
     end;
     if(Refresh_Contracts(FldRealValue, pThis.GetLinkedValue(DL_PNFLD_ENDDATE)) == 0)
       pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, "Не выбран");
     else
       pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, STR_ALLVALUE);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

/*Конечная дата выпуска отчета*/
private MACRO DL_FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;
     if( pThis.ExistField(DL_PNFLD_BEGINDATE) )
        if( FldRealValue < pThis.GetLinkedValue(DL_PNFLD_BEGINDATE) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
     if(Refresh_Clients(pThis.GetLinkedValue(DL_PNFLD_BEGINDATE), FldRealValue) == 0)
       pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, "Не выбран");
     else
       pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, STR_ALLVALUE);
     end;
     if(Refresh_Contracts(pThis.GetLinkedValue(DL_PNFLD_BEGINDATE), FldRealValue) == 0)
       pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, "Не выбран");
     else
       pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, STR_ALLVALUE);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) W475_Panel()

  PRIVATE MACRO Init()

     AddFieldProc(0, P_W475_BEGINDATE,            DL_PNFLD_BEGINDATE,           @DL_FldProc_BeginDate, {curdate});
     AddFieldProc(0, P_W475_ENDDATE,              DL_PNFLD_ENDDATE,             @DL_FldProc_EndDate, {curdate});
     AddFieldProc(0, P_W475_INVESTORCODE,         DL_PNFLD_CLIENT_STOCKDL_CODE, @DL_FldProc_Client);
     AddFieldProc(0, P_W475_SERVCONTR,            DL_PNFLD_CONTRACT_OFBU,       @DL_FldProc_Contract);
     AddFieldProc(0, P_W475_StockMarket,          H_StockMarket,                @DL_FldProc_CheckBox, "X" );
     AddFieldProc(0, P_W475_FuturesMarket,        H_FuturesMarket,              @DL_FldProc_CheckBox, "" );
     AddFieldProc(0, P_W475_OverTheCounterMarket, H_OverTheCounterMarket,       @DL_FldProc_CheckBox, "X" );
     AddFieldProc(0, P_W475_CurrencyMarket,       H_CurrencyMarket,             @DL_FldProc_CheckBox, "" );
     AddFieldProc(0, P_W475_ShowReport,           H_ShowReport,                 @DL_FldProc_CheckBox, "" );

  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel("ReportUserPanels.lbr", "W475"); 

END;

