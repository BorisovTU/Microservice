/**
 * Контроль остатков денежных средств на счетах ППС/ПРС за дату
 */

import rsd;
import or_rep_h, globals, oralib, likepy;
import fiinter;
import "sp_categ.mac";
import RsbFormsInter;
var firstRow = 1;

macro getAccountLastTransactionDate(InAcc)
    var query =
    "   SELECT *                                                             " +
    "     FROM dacctrn_dbt trn                                               " +
    "    WHERE (TRN.T_ACCOUNTID_PAYER = :1 OR TRN.T_ACCOUNTID_RECEIVER = :2) " +
    "          AND t_state = 1                                               " +
    " ORDER BY t_date_carry DESC                                             ";
    var cmd = DL_RSDCommand(query);
        cmd.AddParam(InAcc.rec.accountid);
        cmd.AddParam(InAcc.rec.accountid);

    var sql = cmd.Execute();

    if(sql.movenext)
        if(ABS(GetRestAccount(InAcc.rec, date(sql.date_carry))) == 0)
            return date(sql.date_carry);
        end;
    end;

    return "";
end;

macro printAccount(Table, dealdata, InAcc)

    if (firstRow == 0)
        Table.AddStr();
    end;

    Table.SetValueCell("num"      ,  dealdata.dealcode);
    Table.SetValueCell("account"  ,  InAcc.rec.account);
    Table.SetValueCell("acc_name" ,  InAcc.rec.nameaccount);
    Table.SetValueCell("date1"    ,  date(dealdata.date1));
    Table.SetValueCell("date2"    ,  date(dealdata.date2));

    if(date(dealdata.factdate) == date(0,0,0))
        Table.SetValueCell("date_fact",  "");
    else
        Table.SetValueCell("date_fact", date(dealdata.factdate));
    end;

    Table.SetValueCell("date_w", getAccountLastTransactionDate(InAcc));

    var rest;

    if(date(dealdata.factdate) == date(0,0,0)) // если сделка не исполнена, то получаем остаток на дату планируемой поставки
        rest = ABS(GetRestAccount(InAcc.rec, date(dealdata.date2)));
        if (rest != 0.0)
            Table.SetValueCell("rest", rest );
        else
            Table.SetValueCell("rest", 0.00 );
        end;
    else
        debugbreak;
        rest = ABS(GetRestAccount(InAcc.rec, date(dealdata.factdate)));
        if (rest != 0.0)
            Table.SetValueCell("rest", rest );
        else
            Table.SetValueCell("rest", 0.00 );
        end;
    end;
    firstRow = 0;
end;

macro getDealAccount(dealdata, catcode)
    PRIVATE VAR InAcc = TRecHandler("account");

    var query =
    " SELECT acnt.*                                                     " +
    "   FROM dmcaccdoc_dbt mcaccdoc, daccount_dbt acnt                  " +
    "  WHERE     t_dockind = 176                                        " +
    "        AND t_docid = :1                                           " +
    "        AND t_catnum = (SELECT t_number                            " +
    "                          FROM dmccateg_dbt                        " +
    "                         WHERE T_CODE = :2 AND T_LEVELTYPE = 1)    " +
    "        AND acnt.t_account = mcaccdoc.t_account                    " +
    "        AND acnt.t_code_currency = mcaccdoc.t_currency             " +
    "        AND acnt.t_chapter = mcaccdoc.t_chapter                    ";

    var cmd = DL_RSDCommand(query);
        cmd.AddParam(dealdata.LegID1);
        cmd.AddParam(catcode);

    var sql = cmd.Execute();

    if(sql.movenext)
        sql.getRecord.CopyTo(InAcc.rec);
        setParm(2,InAcc);
        return true;
    end;

    cmd = DL_RSDCommand(query);
    cmd.AddParam(dealdata.LegID2);
    cmd.AddParam(catcode);

    sql = cmd.Execute();

    if(sql.movenext)
        sql.getRecord.CopyTo(InAcc.rec);
        setParm(2,InAcc);
        return true;
    end;

    return false;
end;

/**
 * "-ОД","+ОД","-% к погашению","+% к погашению","Ц/б, ПВО","Ц/б, ПВО_БПП"
 */
macro printDealAccounts(Table, dealdata)
    PRIVATE VAR InAcc = TRecHandler("account");
    var FD = SPFirstDoc( LEG_KIND_DL_TICK_BACK, dealdata.DealID );

    //if( FD.IsExistAccount("-ОД", null, true, FIROLE_BA, InAcc, null, date(dealdata.date2)) )
    if(getDealAccount(dealdata, "-ОД", InAcc))
        printAccount(Table, dealdata, InAcc)
    end;

    //if( FD.IsExistAccount("+ОД", null, true, FIROLE_BA, InAcc, null, date(dealdata.date2)) )
    if(getDealAccount(dealdata, "+ОД", InAcc))
        printAccount(Table, dealdata, InAcc)
    end;

    //if( FD.IsExistAccount("-% к погашению", null, true, FIROLE_BA, InAcc, null, date(dealdata.date2)) )
    if(getDealAccount(dealdata, "-% к погашению", InAcc))
        printAccount(Table, dealdata, InAcc)
    end;

    //if( FD.IsExistAccount("+% к погашению", null, true, FIROLE_BA, InAcc, null, date(dealdata.date2)) )
    if(getDealAccount(dealdata, "+% к погашению", InAcc))
        printAccount(Table, dealdata, InAcc)
    end;    

    //if( FD.IsExistAccount("Ц/б, ПВО", null, true, FIROLE_BA, InAcc, null, date(dealdata.date2)) )
    if(getDealAccount(dealdata, "Ц/б, ПВО", InAcc))
        printAccount(Table, dealdata, InAcc)
    end; 

    //if( FD.IsExistAccount("Ц/б, ПВО_БПП", null, true, FIROLE_BA, InAcc, null, date(dealdata.date2)) )
    if(getDealAccount(dealdata, "Ц/б, ПВО_БПП", InAcc))
        printAccount(Table, dealdata, InAcc)
    end;
end;

macro createReport(date_begin, date_end)
    var query =
    " SELECT dl_tick.t_dealid,                                                                          " +
    "        dl_tick.t_dealcode,                                                                        " +    
    "        leg1.t_id LegID1,                                                                          " +
    "        leg2.t_id LegID2,                                                                          " +
    "        leg1.t_expiry date1,                                                                       " +
    "        leg2.t_expiry date2,                                                                       " +
    "        (SELECT MAX (gracc.t_factdate)                                                             " +
    "           FROM ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc                                           " +
    "          WHERE grdeal.t_templnum IN                                                               " +
    "                   (SELECT t_num                                                                   " +
    "                      FROM DDLGRTEMPL_DBT                                                          " +
    "                     WHERE t_name IN ('Оплата 2-й части РЕПО','Поставка по 2-й части РЕПО'))       " +
    "                AND grdeal.t_dockind = 101                                                         " +
    "                AND grdeal.t_docid = dl_tick.t_dealid                                              " +
    "                AND gracc.t_accnum = 2                                                             " +
    "                AND gracc.t_grdealid = grdeal.t_id) t_factdate                                     " +
    "   FROM ddl_tick_dbt dl_tick, ddl_leg_dbt leg1, ddl_leg_dbt leg2                                   " +
    "  WHERE     dl_tick.t_bofficekind = 101                                                            " +
    "        AND DL_TICK.T_CLIENTID = -1                                                                " +
    "        AND rsb_secur.IsRepo(                                                                      " +
    "                rsb_secur.get_operationgroup(                                                      " +
    "                    rsb_secur.get_opersystypes(dl_tick.t_dealtype,dl_tick.t_bofficekind))) != 0    " +
    "        AND leg1.t_dealid = dl_tick.t_dealid                                                       " +
    "        AND leg1.t_legkind = 0                                                                     " +
    "        AND leg2.t_dealid = dl_tick.t_dealid                                                       " +
    "        AND leg2.t_legkind = 2                                                                     " +
    "        AND leg2.t_expiry between :date_begin and :date_end ";
    var cmd = DL_RSDCommand(query);
        cmd.AddParam(date_begin);
        cmd.AddParam(date_end);


    var ObjPrint:CTemplateXLS = CTemplateXLS(NULL, NULL, NULL, NULL, NULL, NULL, true); // основной объект отчёта
    
    ObjPrint.CreateTotalBook();

    var stat =  ObjPrint.OpenTemplate("od23.xls");

    if( stat )
      ObjPrint.SetValue_NameCell("title", "Контроль остатков денежных средств на счетах ППС/ПРС за " + date_end);           
      Var Table = ObjPrint.RegisterTable("N1_MainTable");

      var cnt = cmd.GetCount();

      if(not cnt)
         Table.SetValueCell("num"      ,  "");
         Table.SetValueCell("account"  ,  "");
         Table.SetValueCell("acc_name" ,  "Нет подходящих сделок"); 
         Table.SetValueCell("date1"    ,  "");
         Table.SetValueCell("date2"    ,  "");
         Table.SetValueCell("date_fact",  ""); 
         Table.SetValueCell("date_w"   ,  "");
         Table.SetValueCell("rest"     ,  ""); 
      else
          var i = 0;    
          InitProgress(cnt, "Сбор данных", "Сбор данных");

          var sql = cmd.Execute();

          while(sql.movenext)
              printDealAccounts(Table, sql);
              UseProgress(i = i + 1);
          end;
          RemProgress();
      end;
   
    ObjPrint.CopyAllSheetInTotalBook(); // кидаем закладку в книгу
    ObjPrint.Close(FALSE);

    ObjPrint.SaveTotalBook("ОД_23_"+date_end);
  //  ObjPrint.SaveTotalBook("OD_23_"+date_end+".xls");
  end;
end;

private const FT_STRING = 7;
private const FT_DATE = 9;
class (TRsbPanel) DateReportPanel()
    InitTRsbPanel();
    setSize(35,4);
    setPosition(35,15);

    class (TRsbEditField) EditField(typeFld: integer, x: integer, y: integer, width: integer, height: integer, bindVal, active: bool, fieldName: string, panel)
        var bindString = string(bindVal);
        
        initTRsbEditField(typeFld);
        setPosition(x, y);
        setSize(width, height);
        
        if(active == false) 
            editable = focusable = false; 
        end;

        if(typeFld == FT_STRING)
            bindValue( this, "bindString", 100 );
        elif(bindVal != null)
            value = bindVal; 
        end;

        if(panel != null)
            panel.addControl(this); 
        end;

        if(fieldName != null) 
            name = fieldName; 
        end;
    end;

    addLabel(TRsbLabel(3, 2, "Укажите дату:"));
    var m_DateBegin = EditField( FT_DATE, 20, 2, 10, 1, {curdate}, true, "Date", this );

    m_DateBegin.addEventHandler(RSB_EV_KEY_PRESSED,  R2M(this, "DATEBEGIN_KEY_PRESSED"));
    addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "Panel_ButtonEvent"));

    macro DATEBEGIN_KEY_PRESSED(RsbEvent)
       if(RsbEvent.keyCode == 317)
           m_DateBegin.value = GetDateByCalendar(m_DateBegin.value);
       elif((RsbEvent.keyCode == 408) OR (RsbEvent.keyCode == 411))/*KEY_ALT_UP KEY_ALT_LEFT*/
           m_DateBegin.value = m_DateBegin.value - 1;
       elif((RsbEvent.keyCode == 416) OR (RsbEvent.keyCode == 413))/*KEY_ALT_DOWN KEY_ALT_RIGHT*/
           m_DateBegin.value = m_DateBegin.value + 1;
       end;
    end;

    macro Panel_ButtonEvent(RsbEvent)
       if((RsbEvent.keyCode == 316) or (RsbEvent.keyCode == 13))
          createReport(m_DateBegin.value, m_DateBegin.value);
          close(1);       
       elif(RsbEvent.keyCode == 27)
          close(0);
       end;
    end;
end;  

var panel = DateReportPanel();
    panel.setCaption("Контроль остатков на счетах ППС/ПРС");
    panel.run();

exit(1);
