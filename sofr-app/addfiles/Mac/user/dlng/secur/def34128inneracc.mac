import globals,rsd;
import sp_car; 

var debug = false;  //режим отладки

class innerAccountingTrn()

  var trn_temp = 0;
  var logname = "def34128";
  var curdt = {curdate};


  /*  Копия процедуры из sp_car - для замены пачки на 0. DCV 07.03.2023
   Проводка по категориям учета (в том числе и мультивалютная)
   СчетДебет,СчетКредит - если тип V_STRING то категории, иначе record ACCOUNT*/
  MACRO UserПроводкаПоКатегориямУчета( FD, СчетДебет, СчетКредит, ДатаВалютирования,
                                 Chapter ,FIID, СуммаОперации, docum,
                                 Result_Carry, Kind_Oper,Numb_Document,Ground,
                                 PaymentID, FIRolePayer, FIRoleReceiver,
                                 СчетДебетFIID, СчетКредитFIID, FIID2, СуммаОперации2,
                                 PaymID_type, 
                                 IsSummaryCarry, /*Признак сводной проводки*/
                                 IsInnerCarry,   /*Признак провордки по ВУ*/
                                 ВидРО, PmObj, DebAccNumber:@string, CredAccNumber:@string, NatSum:@money,
                                 DebPairAcc, CredPairAcc, 
                                 SumEquivalentCarry )
   var stat:bool = true, CуммаДебет = $0, СуммаКредит  = $0, 
      СчетОВПДебет = "", СчетОВПКредит = "", СчетДоходов = "", СчетРасходов = "";

   var tr, NoCarry:bool = false;
   var PaymentObj = null;

   record AccountPayer(account);
   record AccountReceiver(account);

   macro SFContr_Is_Serv(p_cntr, p_srv_kd, p_srv_sb)
      var sqls, rsdc;
      sqls = 
      "DECLARE\n" +
      "   v_Cntr   Dsfcontr_Dbt.t_Id%TYPE := :p_Cntr;\n" + 
      "   v_Srv_Kd Dsfcontr_Dbt.t_Servkind%TYPE := :p_Srv_Kd;\n" + 
      "   v_Srv_Sb Dsfcontr_Dbt.t_Servkindsub%TYPE := :p_Srv_Sb;\n" + 
      "BEGIN\n" + 
      "   SELECT COUNT(1)\n" + 
      "     INTO :p_Res\n" + 
      "     FROM Dsfcontr_Dbt s\n" + 
      "    WHERE s.t_Id = v_Cntr\n" + 
      "      AND s.t_Servkind = v_Srv_Kd\n" + 
      "      AND (Nvl(v_Srv_Sb, 0) = 0 OR s.t_Servkindsub = v_Srv_Sb);\n" + 
      "END;";
      rsdc = rsdcommand(sqls);
      if ((p_cntr == null) or (p_cntr == nullval))
         p_cntr = "";
      end;
      if ((p_srv_kd == null) or (p_srv_kd == nullval))
         p_srv_kd = "";
      end;
      if ((p_srv_sb == null) or (p_srv_sb == nullval))
         p_srv_sb = "";
      end;
      rsdc.AddParam("p_Cntr", RSDBP_IN, p_cntr);
      rsdc.AddParam("p_Srv_Kd", RSDBP_IN, p_Srv_Kd);
      rsdc.AddParam("p_Srv_Sb", RSDBP_IN, p_Srv_Sb);
      rsdc.AddParam("p_Res", RSDBP_OUT, v_integer);
      rsdc.Execute;
      sqls = int(rsdc.Value("p_res"));
      rsdc = null;
      return sqls > 0;
   end;
   
   if ( FD != NULL )
    if(FD.kind !=159)    /*CHVA*/
      if ((Numb_Document == "") AND (GenPropID(FD,"tick") != -1) AND (FD.tick != NULL))
        Numb_Document = FD.tick.rec.DealCode;
      end;
    end;
   end;

   var CarryError = "";
   macro SayCarryError( error )
     if( isOprMultiExec() == false )
        msgbox( error );
     else
        CarryError = error;
     end;
     return false;
   end;

   if( IsSummaryCarry == null )
     IsSummaryCarry = false;
   end;

   if( (СуммаОперации == null) AND (СуммаОперации2 == null) )  
     return SayCarryError( "В проводке не задана ни одна из сумм." );
   end;
     
   /*оставить только копейки для первой суммы, если она задана*/
   if( СуммаОперации != null )
     if( Chapter != 5 ) /*количество ц/б может быть дробным*/
        СуммаОперации = money( round( СуммаОперации, 2 ) );    
     end;
     if( СуммаОперации < $0 )  
        return SayCarryError( "Сумма проводки < 0." );
     end;
   else  
    СуммаОперации = $0;
   end;
   /*для второй суммы, если она задана*/
   if( СуммаОперации2 != null )
     if( Chapter != 5 ) /*количество ц/б может быть дробным*/
        СуммаОперации2 = money( round( СуммаОперации2, 2 ) );
     end;
     if( СуммаОперации2 < $0 )  
        return SayCarryError( "Вторая сумма проводки < 0." );
     end;
   else
    СуммаОперации2 = $0;
   end;

   if( (СуммаОперации == $0) AND (СуммаОперации2 == $0) )  
     return true;
   end;

   if( ValType(СчетДебет) == V_STRING ) /*задан через категорию*/
     if( /*(not FD.IsExistAccount( СчетДебет, null, true, FIRolePayer, AccountPayer, null, ДатаВалютирования, СчетДебетFIID)) and*/
         (not FD.OpenAccount( СчетДебет, null, true, FIRolePayer, AccountPayer, ДатаВалютирования, СчетДебетFIID, null, null, DebPairAcc)) )
         if( not FD.ReOpenAccount( СчетДебет, null, false, FIRolePayer, AccountPayer, ДатаВалютирования, СчетДебетFIID, null, DebPairAcc) )
             return SayCarryError( "Ошибка при определении счета по категории "+СчетДебет+" в проводке.");
         end;
     end;
   else
     copy( AccountPayer, СчетДебет );
   end;

   if( ValType(СчетКредит) == V_STRING ) /*задан через категорию*/
     if( /*(not FD.IsExistAccount( СчетКредит, null, true, FIRoleReceiver, AccountReceiver, null, ДатаВалютирования, СчетКредитFIID)) and*/
         (not FD.OpenAccount( СчетКредит, null, true, FIRoleReceiver, AccountReceiver, ДатаВалютирования, СчетКредитFIID, null, null, CredPairAcc)) )
         if( not FD.ReOpenAccount(СчетКредит, null, false, FIRoleReceiver, AccountReceiver, ДатаВалютирования, СчетКредитFIID, null, CredPairAcc) )
            return SayCarryError( "Ошибка при определении счета по категории "+СчетКредит+" в проводке." );
         end;
     end;
   else
     copy( AccountReceiver, СчетКредит );
   end;

   if( PaymID_type == null )
     PaymID_type = PMMET_ID;
   end;

   if( AccountPayer.Code_Currency == AccountReceiver.Code_Currency)

     /*задана только вторая сумма в валюте одного из счетов*/
     if( (FIID == null) AND (FIID2 != null) )
        FIID = FIID2;
     elif( (FIID == null) AND (FIID2 == null) )
        return SayCarryError("Не задана ни одна из валют проводки.");
     end;

     if( (FIID != AccountPayer.Code_Currency) AND
         (FIID != AccountReceiver.Code_Currency) )
        return SayCarryError( "В проводке не совпадают валюты счетов: " 
                               + string(AccountPayer.Code_Currency) + " " + string( AccountReceiver.Code_Currency ) +
                              "| и валюта документа: " + string(FIID) );
     end;

     if( СуммаОперации != $0 )
        CуммаДебет = СуммаКредит = СуммаОперации;
     elif( СуммаОперации2 != $0 )
        CуммаДебет = СуммаКредит = СуммаОперации2;
     end;
     
     if( (IsSummaryCarry == false) AND 
         НужнаСводнаяПроводка( FD, СчетДебет, СчетКредит, Chapter )
       )
        stat = ДобавитьСводнуюПроводку( FD, AccountPayer, AccountReceiver, ДатаВалютирования, CуммаДебет, $0, Result_Carry, Ground );
        if( not stat ) 
           return SayCarryError("Ошибка при вставке документа сводной проводки.");
        else
               NoCarry = true; /*сформировали сводную проводку - ниже выполнять проводку не надо*/
           end;
     end;

   else /* валюты разные - мультивалютная проводка */
      
     СуммаКредит = $0;
     CуммаДебет  = $0;
     
     /*задана одна сумма в валюте одного из счетов*/
     if( (FIID != null) AND (FIID2 == null) )
        if( FIID == AccountPayer.Code_Currency )
           CуммаДебет  = СуммаОперации;
        else
           СуммаКредит = СуммаОперации;
        end;
     /*задана только вторая сумма в валюте одного из счетов*/
     elif( (FIID2 != null) AND (FIID == null) )
        if( FIID2 == AccountPayer.Code_Currency )
           CуммаДебет  = СуммаОперации2;
        else
           СуммаКредит = СуммаОперации2;
        end;
     /*заданы обе суммы в валютах счетов по которым выполняем проводку*/
     elif( (FIID2 != null) AND (FIID != null) )                         
       if( FIID == AccountPayer.Code_Currency ) 
          CуммаДебет  = СуммаОперации;
       elif( FIID2 == AccountPayer.Code_Currency ) 
          CуммаДебет  = СуммаОперации2;
       end;

       if( FIID == AccountReceiver.Code_Currency ) 
          СуммаКредит = СуммаОперации;
       elif( FIID2 == AccountReceiver.Code_Currency ) 
          СуммаКредит = СуммаОперации2;
       end;
     else
        return SayCarryError("Не задана ни одна из валют проводки.");
     end;

     if( not CheckMultyCarrySum( AccountPayer.Code_Currency, AccountReceiver.Code_Currency,
                                 CуммаДебет, СуммаКредит, ДатаВалютирования ) )
        return SayCarryError("Ошибка при определении сумм мультивалютной проводки.");
     end;

     if( (IsSummaryCarry == false) AND 
         НужнаСводнаяПроводка( FD, СчетДебет, СчетКредит, Chapter )
       )
        stat = ДобавитьСводнуюПроводку( FD, AccountPayer, AccountReceiver, ДатаВалютирования, CуммаДебет, СуммаКредит, Result_Carry, Ground );
        if( not stat ) 
           return SayCarryError("Ошибка при вставке документа сводной проводки.");
        else
              NoCarry = true; /*сформировали сводную проводку - ниже выполнять проводку не надо*/
           end;
        end;
     end;

   //NoCarry = true;
   if( NoCarry == false )
      /* ------ Выполнить проводку ----------------------------------------------*/
      if( PmObj OR ((PaymID_type != PMMET_ID_TMP_OP) AND ((PaymentID != NULL) AND (PaymentID > 0))) )
           /*проводку формируем по платежу*/
         if (PmObj)
            PaymentObj = PmObj;
         else
            PaymentObj = RSBPayment( PaymentID );
         end;

         tr = PaymentObj.MakeTransaction();
      else
         /*проводку формируем без платежа*/
         tr = RsbAccTransaction();
      end;
      if( tr == NULL )
         return SayCarryError("Ошибка при создании проводки");
      end;

      tr.Chapter         = Chapter;
      tr.Date_Carry      = ДатаВалютирования;
      if( PaymentObj != null )
         tr.Number_Pack  = PaymentObj.NumberPack;
      end;
/*
  if((FD.Kind == DL_SECUROWN) or (FD.Kind == DL_AVRWRTOWN)  or  (FD.Kind == DL_RETIREMENT_OWN))
    Tr.Number_Pack  = 20/*СЭБ*/;
  else
    Tr.Number_Pack  = 10/*БОЦБ*/;
  end;
*/
      //Tr.Number_Pack  = 10/*БОЦБ*/;
      Tr.Number_Pack  = 0/*ВУ*/; 

//IMPORT "role_model.mac";//ролевая модель
/*DAN.Ролевая модель Определяем конечного операциониста для проводки */
      var retvalGetMO = GetMainOperInGroup(FD.Kind);
      if (retvalGetMO > 0)
	      Tr.Oper = retvalGetMO;
      else
         Tr.Oper = {Oper};
      end;
/*DAN*/

                        
      if (index(Ground,"писание оценочного резерва") > 0)
          Tr.Number_Pack  = 125/*Восстановление резервов*/;
      end;
      /*
      if ((FD.Kind != NULL) AND (FD.KindReserv != NULL) AND (FD.Kind == 5))
         Tr.Number_Pack  = 120/* ВР. Резерв */;
      end;*/
      
      /*BPV проводки по списаниям/зачислениям дс*/
      if (genclassname(FD) == "DLFIRSTDOCNPTXOP")
        //Chesnokov D.S. 514737 - отключил USERFIELD2 = 1 для выгрузки в Бисквит
        //if (FD.nptxop.rec.SubKind_Operation == DL_NPTXOP_WRTKIND_ENROL) //Simanov. U2 = 1 только для операций зачисления
        //  tr.UserField2 = "1";
        //end;
         Tr.Number_Pack  = 11;
         if (SFContr_Is_Serv(Fd.Nptxop.rec.Contract, 21, 8))
            Tr.Number_Pack  = 90;
         end;

         if(index(Ground,"Возврат излишне удержанной суммы налога на доходы физического лица по операциям с ЦБ и производными инструментами") > 0)
           tr.UserField3 = "1";
         end;
      end;

      tr.Numb_Document   = Numb_Document;
      tr.ResultCarry     = 1;
      tr.Kind_Oper       = Kind_Oper;
      tr.Ground          = Ground;
      tr.Department      = {OperDprt};
     // tr.Oper            = {oper};
      tr.FIIDPayer       = AccountPayer.Code_Currency;
      tr.FIIDReceiver    = AccountReceiver.Code_Currency;
      tr.SumPayer        = CуммаДебет;
      tr.SumReceiver     = СуммаКредит;
      tr.AccountPayer    = AccountPayer.Account;
      tr.AccountReceiver = AccountReceiver.Account;
      tr.Shifr_Oper      = DL_GetShifrOper(Chapter, AccountPayer, AccountReceiver);
      tr.AddOFRSymbol(SC_GetOFRRecID(FD, AccountPayer, AccountReceiver, FIRolePayer, FIRoleReceiver, ДатаВалютирования));

      if(SumEquivalentCarry != null)
        tr.SumEquivalentCarry = SumEquivalentCarry;
      end;

      if( not tr.Carry() )
         return SayCarryError("Ошибка при выполнении проводки");
      end;

      /*-------------------------------------------------------------------------*/  
      if( DebAccNumber != null )
         DebAccNumber = tr.AccountPayer;
      end;

      if( CredAccNumber != null )
         CredAccNumber = tr.AccountReceiver;
      end;

      if( NatSum != null )
         if( tr.FIIDPayer == NATCUR )
            NatSum = tr.SumPayer;
         elif( tr.FIIDReceiver == NATCUR )
            NatSum = tr.SumReceiver;
         else
            NatSum = $0.0;
         end;
      end;

   end;

   if( not stat ) 
      return SayCarryError("Ошибка при вставке документа проводки.");
   end;

   return stat;
  END;


  macro GetAbsAccRest( Account : string, DateRest : date, Chapter : integer, Code_Currency : integer ) : money
    var RestAcc = $0;
    RestAcc = RestA( Account, DateRest, null, Chapter, Code_Currency, NATCUR );
    if( RestAcc < $0 )
      RestAcc = -RestAcc;
    end;
    return RestAcc;
  end;


  macro start_o(name)
      setoutput(GetTxtFileName(name),false);
      println("Начало работы в " + date + " " + time);
      setoutput(null, true);
      logname = name;
  end;

  macro o(text)
      setoutput(GetTxtFileName(logname),true);
      println(text);
      setoutput(null, true);
  end;

  macro getLastTrnId(acc_debet, acc_credit)
  var query = "select * from dacctrn_dbt dtr where dtr.t_account_payer = ? and dtr.t_account_receiver = ? and dtr.t_date_carry = ? order by t_acctrnid desc ";
  var cmd = RSDCommand(query); 
    cmd.addParam("", RSDBP_IN, acc_debet);
    cmd.addParam("", RSDBP_IN, acc_credit);
    cmd.addParam("", RSDBP_IN, {curdate});
    var rs = RsdRecordSet(cmd);
    if (rs.movenext) 
      return rs.value("t_acctrnid");
    end; 
    return "";
  end;


  //выплата компенсации по служебной записке
  macro trnCompens(rs)
    record acc17810( account );  ClearRecord( acc17810 ); 
    record acc27810( account );  ClearRecord( acc27810 ); 
    var FD = DL_SubContrFD(rs.value("t_id")); 
    FD.OpenAccount( "ДС Клиента, ВУ",           NULL, NULL, acc17810, {curdate}, 0 ); 
    FD.OpenAccount( "ДС, Расч. с клиентом, ВУ", NULL, NULL, acc27810, {curdate}, 0 );  
    var PaySum = rs.value("t_sum_natcur");
    UserПроводкаПоКатегориямУчета( FD,   
                                       Acc17810, 
                                       Acc27810,
                                       curdt,
                                       21,
                                       0,
                                       PaySum,  
                                       null,
                                       INPCARRY,
                                       "",
                                       "",
                                       rs.value("t_ground"),
                                       0,
                                       null, 
                                       null,
                                       0, 0,
                                       null, null, NULL, null,
                                       true,
                                       null
                                     );
    trn_temp = 0;
    trn_temp = getLastTrnId(Acc17810.account, Acc27810.account);  //запомнить для сохранения
    o("Добавлена проводка № "+ trn_temp +" Дт "+acc17810.account+" Кт "+acc27810.account+" "+ rs.value("t_sum_natcur") + " по контракту id=" + rs.value("t_number")); 
  end;

  macro trnComiss(rs)
    record acc27810( account );  ClearRecord( acc27810 ); 
    record acc17810( account );  ClearRecord( acc17810 ); 
    var FD = DL_SubContrFD(rs.value("t_id")); 
    FD.OpenAccount( "ДС, Расч. с клиентом, ВУ", NULL, NULL, acc27810, {curdate}, 0 );  
    FD.OpenAccount( "ДС Клиента, ВУ",           NULL, NULL, acc17810, {curdate}, 0 ); 
    var dt1:date = rs.value("t_datebegin");
    var PaySum = rs.value("t_sum_natcur");
    UserПроводкаПоКатегориямУчета( FD,   
                                       Acc27810, 
                                       Acc17810,
                                       curdt,
                                       21,
                                       0,
                                       PaySum,  
                                       null,
                                       INPCARRY,
                                       "",
                                       "",
                                       "Оплата комиссии в рамках Соглашения об оказании брокерских услуг № "+strsubst(rs.value("t_number"),"_v","")+" от "+dt1,
                                       0,
                                       null, 
                                       null,
                                       0, 0,
                                       null, null, NULL, null,
                                       true,
                                       null
                                     );
    trn_temp = 0;
    trn_temp = getLastTrnId(Acc27810.account, Acc17810.account);  //запомнить для сохранения
    o("Добавлена проводка № "+ trn_temp +" Дт "+acc27810.account+" Кт "+acc17810.account+" "+ rs.value("t_sum_natcur") + " по контракту " + rs.value("t_number")); 
  end;

  //проводки внутреннего учета выплаты компенсации
  macro innerAccountingCompensationMethod(dt)
    curdt = dt;
    var query = 
    "select t.*, da.t_client, dsf.* from (                                                                                                             "+
    "select t_account_payer, t_account_receiver, t_sum_natcur, t_ground from (                                                                         "+
    "select * from dacctrn_dbt trn where 1=1                                                                                                           "+
    "and trn.t_date_carry = ?                                                                                                                          "+
    "and trn.t_fiid_payer = 0                                                                                                                          "+
    "and trn.t_account_payer = '47422810500000056122'                                                                                                  "+
    "and trn.t_account_receiver like '30601%'                                                                                                          "+
    "and trn.t_state = 1                                                                                                                               "+
    "and trn.t_result_carry = 1                                                                                                                        "+
    "and trn.t_ground like 'Компенсация по служебной записке ДРРК №34-0-11/264 от 02.03.2023 в рамках Соглашения об оказании брокерских услуг%'        "+
    ") group by t_account_receiver, t_account_payer, t_sum_natcur, t_ground                                                                            "+
    ") t, daccount_dbt da, dsfcontr_dbt dsf                                                                                                            "+
    "where da.t_account = t.t_account_receiver and dsf.t_partyid = da.t_client                                                                         "+
    "and instr(dsf.t_number, '_v')>0                                                                                                                   "+
    "and instr(da.t_account,dsf.t_acccode)>1                                                                                                           ";
    if (debug)
      query = query + " fetch first 1 rows only ";  //отладка
    end;
    var cmd = RSDCommand(query);
    cmd.AddParam("", RSDBP_IN, dt);
    var rs = RsdRecordSet(cmd);
    while (rs.movenext)
      trnCompens(rs);
    end;
  end;

  //проводки внутреннего учета выплаты комиссии
  //делаются на основании ранее проведенных проводок Дт 30601 Кт 474
  macro innerAccountingComissionMethod(dt)
    curdt = dt;
    var query = 
"select da.t_account, dsf.t_acccode, t.*, da.t_client, dsf.*                        "+
"  from (select t_account_receiver, t_account_payer, t_sum_natcur, t_ground         "+
"          from (select *                                                           "+
"                  from dacctrn_dbt trn                                             "+
"                 where 1 = 1                                                       "+
//"                   and trn.t_systemdate = to_date('04032023', 'ddmmyyyy')          "+
"                   and trn.t_date_carry = ?                                        "+
"                   and trn.t_fiid_payer = 0                                        "+
"                   and trn.t_account_payer like '30601%'                           "+
"                   and trn.t_account_receiver like '47423%'                        "+
"                   and trn.t_state = 1                                             "+
"                   and trn.t_result_carry = 1                                      "+
"                   and (trn.t_ground like 'Оплата комиссии по сделке%' or          "+
"                       trn.t_ground like 'Оплата комиссии по сделкам%'))           "+
"         group by t_account_receiver,                                              "+
"                  t_account_payer,                                                 "+
"                  t_sum_natcur,                                                    "+
"                  t_ground) t,                                                     "+
"       daccount_dbt da,                                                            "+
"       dsfcontr_dbt dsf                                                            "+
" where da.t_account = t.t_account_payer                                            "+
"   and dsf.t_partyid = da.t_client                                                 "+
"   and instr(dsf.t_number, '_v') > 0                                               "+
"   and instr(da.t_account, dsf.t_acccode) > 1                                      ";
    if (debug)
      query = query + " fetch first 1 rows only ";  //отладка
    end;
    var cmd = RSDCommand(query);
    cmd.AddParam("", RSDBP_IN, dt);
    var rs = RsdRecordSet(cmd);
    while (rs.movenext)
      trnComiss(rs);
    end;
  end;
  
  macro trnDelete(rs)
	var d = RsbAccTransaction();
        
        if (d.Find(rs.value("t_acctrnid")) !=0 )
          o("Не найдена проводка №"+rs.value("t_acctrnid"));
          return;
        end;
        if (d.Delete() == true ) 
          o("Удалена проводка №"+rs.value("t_acctrnid"));
        else
          o("Ошибка при удалении проводки №"+rs.value("t_acctrnid"));
        end;
  end;

  //удаление неверных проводок внутреннего учета выплаты комиссий
  macro innerAccountingDeleteTrnMethod(dt)
    curdt = dt;
    var query = 
      "select t_account_receiver, t_account_payer, t_sum_natcur, t_ground, t.t_acctrnid from (    "+
      "select * from dacctrn_dbt trn where 1=1                                                    "+
      "and trn.t_date_carry = ?                                                                   "+
      "and trn.t_fiid_payer = 0                                                                   "+
      "and trn.t_account_payer like '17810%'                                                      "+
      "and trn.t_account_receiver like '27810%'                                                   "+
      "and trn.t_state = 1                                                                        "+
      "and trn.t_result_carry = 1                                                                 "+
      "and trn.t_ground like 'Оплата комиссии по сделке%'                                         "+
      ") t group by t_account_receiver, t_account_payer, t_sum_natcur, t_ground, t.t_acctrnid     ";
    if (debug)
      query = query + " fetch first 1 rows only ";  //отладка
    end;
    var cmd = RSDCommand(query);
    cmd.AddParam("", RSDBP_IN, dt);
    var rs = RsdRecordSet(cmd);
    while (rs.movenext)
      trnDelete(rs);
    end;
  end;

  macro viewLog(name)
      file view () txt;
          open(view,GetTxtFileName(name));
          viewfile(view);
          close(view);
  end;

end;

macro innerAccountingCompensation(dt)
  var cl = innerAccountingTrn();
  cl.start_o("def34128_1"); //начало лога
  cl.innerAccountingCompensationMethod(dt);
  cl.viewLog("def34128_1")
end;

macro innerAccountingComission(dt)
  var cl = innerAccountingTrn();
  cl.start_o("def34128_2"); //начало лога
  cl.innerAccountingComissionMethod(dt);
  cl.viewLog("def34128_2");
end;

macro innerAccountingDelete(dt)
  var cl = innerAccountingTrn();
  cl.start_o("def34128_3"); //начало лога
  cl.innerAccountingDeleteTrnMethod(dt);
  cl.viewLog("def34128_3");
end;

macro viewLog1(name)
  var cl = innerAccountingTrn();
  cl.viewLog(name)
end;


macro SelectMenu1 ()
  array Pmenu;
  Pmenu(0) = " Отказ ";
  Pmenu(1) = " Удаление неверных проводок Дт17-Кт27 ";
  Pmenu(2) = " Просмотр лога удаления";
  Pmenu(3) = " Проводки ВУ компенсации Дт17-Кт27 ";
  Pmenu(4) = " Просмотр лога ВУ компенсации ";
  Pmenu(5) = " Проводки ВУ оплаты комиссий Дт27-Кт17";
  Pmenu(6) = " Просмотр лога ВУ оплаты комиссий ";
  var Prompt   = " Запустите процедуру или откажитесь... Процедуры пронумерованы в логическом порядке"; 
  return Menu (Pmenu,Prompt,"Выберите режим работы");
end;

macro mainmenu1(dt)
  var   NumSel = 0; // Выбрано в меню:
  NumSel = SelectMenu1 ();
  if (NumSel != 0)
	if (NumSel == 1)
                if (getTrue(false, "Запустить удаление неверных проводок ВУ Дт17-Кт27"))
		    //innerAccountingDelete(dt); 
                  MsgBox("Пункт меню отключен");
                end;
	elif (NumSel == 2)
          viewLog1("def34128_3"); 
	elif (NumSel == 3)
                if (getTrue(false, "Запустить проведение проводок ВУ компенсации Дт17-Кт27"))
		  innerAccountingCompensation(dt)
                end;
	elif (NumSel == 4)
          viewLog1("def34128_1"); 
	elif (NumSel == 5)
                if (getTrue(false, "Запустить проведение проводок ВУ комиссий Дт27-Кт17"))
		  innerAccountingComission(dt)
                end;
	elif (NumSel == 6)
          viewLog1("def34128_2"); 
        end;
  else
    println ("Вы отказались от выполнения процедуры.");
  end;
onError(er)
  MsgBox("Ошибка: " + er.Message);
end;
  
var dt =  date(3,3,2023); //актуально для ПРОДа
if (debug)
  dt =  date(9,1,2023);  //отладка
end;

if (not GetDate(dt, "Введите дату операционного дня"))
    dt = {Curdate};
end;
mainmenu1(dt);
