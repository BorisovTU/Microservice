/**
  @file: 		mac\user\dlng\secur\Enroll_Sched_IRK.mac
  @brief: 		Обработчики для планировщика "Запуск операций зачисления д/с ЮЛ по ИРК"

  # changelog
  |date       |author         |tasks                                       |note                                                        
  |-----------|---------------|--------------------------------------------|-------------------------------------------------------------
  |2025.09.22 |Васильев Н.А.  |BOSS-9135                      | 

*/
import enroll_class, "cblogger2.mac", "RegvalReader.mac";


private const SS_RESPONSE_END = 3; /* Успешное завершение всего сервиса */
PRIVATE CONST AUTO_CALENDAR_ID = 41;// id календаря автопополнения брокерского счета.

private class c_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText)
   var ProcessState;
   var ErrorNumber;
   var ErrorText;

   private macro init_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText)
      if(ValType(p_ProcessState) != V_UNDEF)
         ProcessState = p_ProcessState;
      end;
      if(ValType(p_ErrorNumber) != V_UNDEF)
         ErrorNumber = p_ErrorNumber;
      end;
      if(ValType(p_ErrorText) != V_UNDEF)
         ErrorText = p_ErrorText;
      end;
   end;

   init_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText);
end;

private class ResultCounter
   var all:integer = 0;
   var success:integer = 0;

   macro Increment()
      all = all + 1;
   end;

   macro IncrementSuccess()
      success = success + 1;
   end;

   macro Reset()
      all = 0;
      success = 0;
   end;
end;


macro PushEnrollsIRK()
   var Dt,sql, enroll,ResultSheduler :object;
   var stat, ErrStr;
   var logger = LoggerFactory().NewItLog("EnrollSchedIRK.MAC").WithElapsedTime().GetLogger();

   // запускаемся только в рабочий день по календарю 41 
   if ( IsWorkDay(date(), AUTO_CALENDAR_ID) )
      // BIQ-15498 автоматическое выполнение операций зачисления, где "Разрешено зачисление" = да
      if (not RegValReader.GetBool("РСХБ\\ИНТЕГРАЦИЯ\\НЕТОРГОВЫЕ ПОРУЧЕНИЯ\\ИСПОЛНЕНИЕ ОПЕРАЦИЙ\\ЗАЧИСЛЕНИЯ НА FUNCOBJ", false))
         if ( not rslDefCon.isInTrans() )
            rslDefCon.beginTrans();
         end;

         sql = "select u.*, op.t_code, op.t_id "+
               "  from USR_ACC306ENROLL_DBT u, "+
               "       dnptxop_dbt op         "+
               " where u.t_state = "+C_ENROLL_ACTION_CREATEDNPTXOP+
               "   and u.t_nptxopid = op.t_id           "+
               "   and op.t_status in (0,1)             "+
               "   and op.t_dockind = 4607              "+
               "   and exists (select 1 from dobjatcor_dbt attr "+
                             "  where attr.t_objecttype = 131  "+
                             "    and attr.t_groupid = "+C_CATEGORY_ALLOWED_INCOME+          
                             "    and attr.t_object = lpad(op.t_id,34,'0') and t_attrid = 1) "+
               "  and not exists (select 1 from  doprstep_dbt s "+
                                 "  join doproper_dbt o "+
                                 "    on s.t_id_operation = o.t_id_operation "+
                                 "  where o.t_dockind = 4607 "+
                                 "    and o.t_documentid = lpad (op.t_id, 34, '0')"+
                                 "    and s.t_symbol = 'А' and s.t_isexecute = 'R')" ;
         Dt = TRsbDataSet(sql);
         while (Dt.movenext())
            enroll = GetClientEnroll(Dt.EnrollID);
            enroll.ExecuteOp(Dt.Code);
            logger.Debug("Выполнен запуск буферного зачисления EnrollID=" + string(Dt.EnrollID) + "; nptxopid = " + string(Dt.ID));
         end; 

         if ( rslDefCon.isInTrans() )
            rslDefCon.commitTrans();   // на всякий случай
         end;
      end;

      // отдельно запустим ЮЛ, так как старт операции при создании не делаем
      sql = "select op.* from dnptxop_dbt op, dparty_dbt p    "+
            " where op.t_client = p.t_partyid        "+
            "   and p.t_legalform = 1                "+
            "   and op.t_status in (0,1)             "+
            "   and op.t_dockind = 4607              "+
            "   and exists (select 1 from dobjatcor_dbt attr "+
                            " where attr.t_objecttype = 131  "+
                            "   and attr.t_groupid = "+C_CATEGORY_ALLOWED_INCOME+          
                            "   and attr.t_object = lpad(op.t_id,34,'0') and t_attrid = 1) "+
            "  and not exists (select 1 from  doprstep_dbt s "+
                              "  join doproper_dbt o "+
                              "    on s.t_id_operation = o.t_id_operation "+
                              "  where o.t_dockind = 4607 "+
                              "    and o.t_documentid = lpad (op.t_id, 34, '0')"+
                              "    and s.t_symbol = 'А' and s.t_isexecute = 'R')" ;
      Dt = TRsbDataSet(sql);
      while (Dt.movenext())
         stat = DL_ExecuteNptxOp(Dt.id, false, ErrStr);  // не будем оборачивать в транзакцию
         logger.Debug("Выполнен запуск буферного зачисления nptxopid = " + string(Dt.ID) + "; stat=" + string(stat));
     end; 

   end;

   if ( rslDefCon.isInTrans() )
      rslDefCon.commitTrans();   // на всякий случай
   end;

   return ResultSheduler = c_ssRunResult(SS_RESPONSE_END);
end;