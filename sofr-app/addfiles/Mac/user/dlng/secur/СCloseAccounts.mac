/* Закрытие счетов 306 47423 и ВУ по закрытым договоарм*/
import RSD, globals;
import BankInter, dlquery, cb_sql, oralib, likepy;

private class CLog;
   private macro PrepareLog()
      if ( execSqlSelect ("select 1 from user_tables where upper(table_name) = upper('RSHB_CloseAccLog')").moveNext () )
        /*  if ( not execSql ("truncate table RSHB_CloseAccLog") )
              return false;
          end;*/
      else
          if ( not execSql ("create table RSHB_CloseAccLog (t_kind number not null, t_account varchar2(25),  t_text varchar2(4000), t_timestamp timestamp, t_oper number, t_operdate date) ") )
              return false;
          end;
      end;
      return true;
   end;

   Macro add ( kind, account, text )
       execSql ("insert into RSHB_CloseAccLog values (:1, :2, :3, systimestamp, :4, :5)", makeArray (sqlParam ("1", kind), sqlParam ("2", account), sqlParam ("3", text),sqlParam ("4", {oper}),sqlParam ("5", {curdate})));
   End;

   if (not PrepareLog() );
      msgbox("Ошибка инициализации логирования");
      exit(1);
   end;
end;


class CCloseContrAccounts(_dlcontrid)

   private var dlcontrid;

   private class ErrRec(_acc,_rest, _err)
      var err,rest,acc;
         acc  = _acc;
         err  = _err;
         rest = _rest;
   end;


   private  macro _doCloseAcc( acc, chapter, currency, CloseDate, ErrMsg)
      var stat, mes;
      var IsError = false;

      if( (stat=CB_CloseAccount(chapter, currency, acc, CloseDate , mes)) != 0 )
         if (strlen(mes) == 0)
            InitError();
            MemoryError(stat);
            mes = GetErrMsg();
         end;
         IsError = true;
      end;
      SetParm(4, mes);
      return not IsError;

      onError(er)
        SetParm(4, er.message);
        return not IsError;
   end;



   macro CloseAcc(ArErr:@TArray)
      var sql, cmd, rs, err;
      var Log = CLog();
      ArErr = TArray();

         sql = 
      "select rownum,t.*, RSB_ACCOUNT.RESTALL(t.t_account, t.t_chapter, t.t_code_currency, "+GetSQLDate({curdate})+") rest  from ( " +
      "SELECT   " +
      "       a.t_open_close, " +
      "       a.t_close_date, " +
      "       (select t_ccy from dfininstr_dbt where t_fiid = a.t_code_currency) t_ccy, " +
      "       a.t_code_currency, " +
      "       sf.t_number, " +
      "       sf.t_id, " +
      "       sf.t_dateclose, " +
      "       sf.t_name, " +
      "       s.t_account, " +
      "       s.t_chapter, " +
      "       decode(s.t_chapter,1,'БУ',21,'ВУ ДС',22,'ВУ ЦБ') kind " +
      "  FROM dsettacc_dbt s, " +
      "       dsfssi_dbt ss, " +
      "       dsfcontr_dbt sf, " +
      "       daccount_dbt a " +
      " WHERE     s.t_settaccid = ss.t_setaccid " +
      "       AND ss.t_objecttype = 659 " +
      "       AND ss.t_objectid = sf.t_id " +
      "       AND a.t_account = s.t_account " +
      "       AND a.t_chapter = s.t_chapter " +
      "union all " +
      "SELECT a.t_open_close,        " +
      "       a.t_close_date, " +
      "       (select decode(t_ccy,chr(1),t_definition, t_ccy) t_ccy from dfininstr_dbt where t_fiid = a.t_code_currency) t_ccy, " +
      "       a.t_code_currency, " +
      "       sf.t_number, " +
      "       sf.t_id, " +
      "       sf.t_dateclose, " +
      "       sf.t_name, " +
      "       mc.t_account, " +
      "       mc.t_chapter, " +
      "       decode(a.t_chapter,1,'БУ',21,'ВУ ДС',22,'ВУ ЦБ') kind " +
      "  FROM dmcaccdoc_dbt mc, dsfcontr_dbt sf, daccount_dbt a " +
      " WHERE    " 
      "           mc.t_chapter in (21,22) " +
      "       AND mc.t_iscommon = CHR (88) " +
      "       AND sf.t_id = mc.t_clientcontrid " +
      "       AND a.t_account = mc.t_account " +
      "       AND a.t_chapter = mc.t_chapter " +
      "union all " +
      "SELECT a.t_open_close,        " +
      "       a.t_close_date, " +
      "       (select t_ccy from dfininstr_dbt where t_fiid = a.t_code_currency) t_ccy, " +
      "       a.t_code_currency, " +
      "       sf.t_number, " +
      "       sf.t_id, " +
      "       sf.t_dateclose, " +
      "       sf.t_name, " +
      "       mc.t_account, " +
      "       mc.t_chapter, " +
      "       decode(a.t_chapter,1,'БУ',21,'ВУ ДС',22,'ВУ ЦБ') kind " +
      "  FROM dmcaccdoc_dbt mc, dsfcontr_dbt sf, daccount_dbt a " +
      " WHERE     " 
      " t_catnum = 5087 " +
      "       AND mc.t_iscommon = CHR (88) " +
      "       AND sf.t_id = mc.t_clientcontrid " +
      "       AND a.t_account = mc.t_account " +
      "       AND a.t_chapter = mc.t_chapter "  +
      "           ) t "  +
      "    where  t.t_id in ( select t_sfcontrid from ddlcontrmp_dbt where t_dlcontrid =  " + dlcontrid + ")";
      sql = sql + "        order by t.t_dateclose" ;

    
         cmd = RSDCommand(sql);
         rs = RSDRecordset(cmd);
         while ( rs.movenext() )
            if (rs.Value("rest") != 0)
               ArErr(ArErr.size) = ErrRec(rs.Value("t_account"), rs.Value("rest"), "Ненулевой остаток");
            end;
         end;

         rs = null;
         rs = RSDRecordset(cmd);
         if ( ArErr.size == 0 )
            while ( rs.movenext() )
               if ( not _doCloseAcc (rs.Value("t_account"), rs.Value("t_chapter"), rs.Value("t_code_currency"), SQL_ConvTypeDate(rs.Value("t_dateclose")),err) )
                  Log.add(1, rs.Value("t_account"), err);
                  ArErr(ArErr.size) = ErrRec(rs.Value("t_account"), rs.Value("rest"), err);
               else
                  Log.add(0, rs.Value("t_account"), "Закрыт");
               end;
            end;
         end;

      rs = null;

      return ArErr.size;

   end;

  /*init*/

  dlcontrid = _dlcontrid;

end;
