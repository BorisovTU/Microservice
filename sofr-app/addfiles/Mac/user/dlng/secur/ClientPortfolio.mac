import rsd;
import "DLReport_h.mac", "dlreport.mac", "dlQuery.mac", "scsrvrepfun.mac", "sc_inacc.mac";
import "dpstdrep.mac", "dl_util_xml_obj.mac";
import RsbFormsInter, spinter;


private class ReportParams()
    var dateBegin:date,
        dateEnd:date,
        clientid:integer,
        sfcontrid:integer, 
        dlcontrid:integer,
        account:string;
end;

private class CClientPortfolio_Report(_params)
    PRIVATE VAR m_rep:T_XML_Rep;
    PRIVATE VAR m_params = _params;

    var CreateOnTerm = (not isStandAlone());

    macro PrintClientInfo()
        var query = " SELECT party.t_name partyname "
                  + "   FROM dparty_dbt party "
                  + "  WHERE party.t_partyid = "+int(m_params.clientid);

        var cmd = DL_RSDCommand(query);
        var DataSet = cmd.Execute();

        var text = "";

        if(DataSet.moveNext())
            text = "Портфель клиента " + DataSet.partyname + " за " + date(m_params.dateBegin);

            //m_rep.Put_Str(text, "R=8;START;HA=L;END;");
            m_rep.Put_Str("Портфель клиента ", "START;HA=L;");
            m_rep.Put_Str(DataSet.partyname, "R=7;HA=L;FB;END;");
            m_rep.Put_Str(" за " + date(m_params.dateBegin), "START;HA=L;END;");
            m_rep.Blank_Line();
        end;

    end;

    /*   macro GetContrNum()
        var query = " SELECT party.t_name partyname, sf.*, dlcontr.* "
                  + "   FROM dsfcontr_dbt sf, ddlcontrmp_dbt dlcon, ddlcontr_dbt dlcontr, dparty_dbt party "
                  + "  WHERE     sf.t_id = dlcon.t_sfcontrid "
                  + "        AND dlcon.t_dlcontrid = dlcontr.t_dlcontrid "
                  + "        AND party.t_partyid = sf.t_partyid AND party.t_partyid = "+int(m_params.clientid);

        var cmd = DL_RSDCommand(query);
        var DataSet = cmd.Execute();

        var text = "";

        if(DataSet.moveNext())
            return DataSet.Number;
        end;
    end;
    */

    macro GetRate(fromFi, toFi, type, Dat, isRelative:@integer)
        record RateRec(ratedef);                                                                                                     
                ClearRecord( RateRec );
                var ErrorFlag = ПолучитьКурс( RateRec, toFi, fromFi,  type );

                var Scale = 0, Point = 0, Rate = 0;

                if( not ErrorFlag )                                                                                                      
                    if( ПолучитьЗначениеКурса( RateRec, dat) == 0 )                                                      
                        Scale = RateRec.Scale;                                                                                          
                        Point = RateRec.Point;                                                                                          
                        Rate = RateRec.Rate*RateRec.Scale/Pow(10,RateRec.Point);
                    else
                        Rate = 0;
                    end; 
                    if (RateRec.IsRelative == StrFor(88))
                       isRelative = 1;
                    else              
                       isRelative = 0;                                    
                    end;
                end;
        return Rate;
    end;

    macro PrintContrData(m_contrid,m_contrnum,m_servkindsub)
        record RateRecTSS(ratedef);                                                                                              
        record RateRecAvg(ratedef);                                                                                              
        record RateRecBloom(ratedef);                                                                                              
        var Scale = 0, Point = 0, Rate = 0.0, TSS = 0.0, AvgPrice = 0.0, cbRate = 0.0;
        var AllSumRUB = $0.0;
        var Dt, ratefi, isRelative, nominal;

        var query = " SELECT avr.t_lsin, "
                  + "        fin.t_name, "
                  + "        SUM(wrthist.t_amount) t_amount, "
                  + "        (SELECT finface.t_ccy "
                  + "           FROM dfininstr_dbt finface "
                  + "          WHERE finface.t_fiid = fin.t_facevaluefi) face_fi, "
                  + "          fin.t_facevaluefi, "
                  + "          fin.t_fiid "
                  + "   FROM V_SCWRTHISTEX wrthist, DAVOIRISS_DBT avr, dfininstr_dbt fin "
                  + "  WHERE     wrthist.t_amount > 0 ";
                  if (ValType(m_contrid) == V_UNDEF)
                     query = query + "        AND wrthist.t_party = :partyid ";
                  else
                     query = query + "        AND wrthist.t_contract = :contrid ";
                  end;
                  query = query
                  + "        AND wrthist.t_fiid = avr.t_fiid "
                  + "        AND wrthist.t_fiid = fin.t_fiid "
                  + "        AND wrthist.t_state = 1 "
                  + "        AND wrthist.t_instance = "
                  + "               (SELECT MAX (t_instance) "
                  + "                  FROM V_SCWRTHISTEX wrthist2 "
                  + "                 WHERE wrthist2.t_sumid = wrthist.t_sumid "
                  + "                       AND t_changedate <= :beg_date) "
                  + " GROUP BY avr.t_lsin, fin.t_name, fin.t_facevaluefi, fin.t_fiid ";

        var cmd = DL_RSDCommand(query);
            if (ValType(m_contrid) == V_UNDEF)
               cmd.AddParam(m_params.clientid);
            else
               cmd.AddParam(m_contrid);
            end;
            cmd.AddParam(m_params.dateBegin);

        var cnt = cmd.GetCount();

        BegAction(1000,"Идет отбор данных");

        if(cnt)
            m_rep.Put_Str("Номер договора обслуживания: "+m_contrnum+" / "+iif(m_servkindsub == 8,"Биржа","Внебиржа"), "R=8;START;HA=L;END;");
            m_rep.Blank_Line();

            m_rep.Put_Val("Номер гос.регистрации/Выпуск ЦБ", "START;BOR;WT;HA=C;VA=C;", 20);
            m_rep.Put_Val("Остаток исходящий (шт)",          "BOR;WT;HA=C;VA=C;");
            m_rep.Put_Val("Котировка (ТСС)",                  "BOR;WT;HA=C;VA=C;");
            m_rep.Put_Val("Валюта котировки",                 "BOR;WT;HA=C;VA=C;");
            m_rep.Put_Val("Валюта номинала",                 "BOR;WT;HA=C;VA=C;");
            m_rep.Put_Val("Курс ЦБ на дату отчета",          "BOR;WT;HA=C;VA=C;");
            m_rep.Put_Val("Стоимость",                       "BOR;WT;HA=C;VA=C;");
            m_rep.Put_Val("НКД в валюте бумаги",             "BOR;WT;HA=C;VA=C;");
            m_rep.Put_Val("Стоимость с НКД",                 "BOR;WT;HA=C;VA=C;");
            m_rep.Put_Val("Стоимость по курсу ЦБ на дату отчета(RUB)", "BOR;WT;HA=R;VA=C;END;");

            var ClientData = cmd.Execute();
            //SetDialogFlag(0);
            while(ClientData.movenext)
                m_rep.Put_Val(ClientData.name, "START;BRL;BRR;WT;HA=L;VA=C;");
                m_rep.Put_Val(ClientData.amount, "BRL;BRR;WT;HA=R;VA=C;T=M0");

                Dt = TRsbDataSet( " select t_fiid, (select t_ccy from dfininstr_dbt where t_fiid = t.t_fiid) ccy from dratedef_dbt t where t_type in (12,4,23) and t_otherfi = " + clientdata.fiid + " and rownum = 1 order by t_fiid desc" );
                if (Dt.movenext())
                   ratefi = Dt.fiid;
                else
                   ratefi = ClientData.facevaluefi;
                end;
                TSS = GetRate(ClientData.fiid, ratefi, 12 /*rightcost*/, m_params.dateBegin);
                AvgPrice = GetRate(ClientData.fiid, ratefi, 4 /*avgprice*/, m_params.dateBegin, isRelative);
                if (AvgPrice == 0)
                   Rate = GetRate(ClientData.fiid, ratefi, 23 /*Bloomberg*/, m_params.dateBegin,isRelative);
                else
                   Rate = AvgPrice;
                end;

                /*GAA: 496721 */
    //                if (isRelative)
                if ( not SP_GetNominal(ClientData.Fiid, m_params.dateBegin, nominal))
                      nominal = 0; //не удалось получить
                end;
    //                else 
    //                   nominal = 100;//
    //                end;


                m_rep.Put_Val(Rate, "BRL;BRR;WT;HA=R;VA=C;T=M4"/*+RateRecTSS.Point*/);
                m_rep.Put_Val(Dt.ccy, "BRL;BRR;WT;HA=C;VA=C;");
                m_rep.Put_Val(ClientData.face_fi, "BRL;BRR;WT;HA=C;VA=C;");

                cbRate = GetRate(ratefi, NATCUR, 7 /*cbrate*/, m_params.dateBegin);
    //                SmartConvertSum(cbRate,money(cbRate),m_params.dateBegin, ClientData.facevaluefi, NATCUR, false);
                m_rep.Put_Val(cbRate, "BRL;BRR;WT;HA=R;VA=C;T=M4");

                if (TSS == 0)
                   m_rep.Put_Val( 0, "BRL;BRR;WT;HA=R;VA=C;T=M4");
                else
    //                   m_rep.Put_Val(TSS*ClientData.amount - НКД(ClientData.fiid,ClientData.amount,m_params.dateBegin), "BRL;BRR;WT;HA=C;VA=C;T=M4");
    //                   m_rep.Put_Val(Rate*ClientData.amount * nominal/100, "BRL;BRR;WT;HA=C;VA=C;T=M4");
                   m_rep.Put_Val(ClientData.amount * nominal, "BRL;BRR;WT;HA=R;VA=C;T=M4");  //GAA: 496721

                end;
                m_rep.Put_Val(НКД(ClientData.fiid,ClientData.amount,m_params.dateBegin), "BRL;BRR;WT;HA=R;VA=C;T=M4");


                var Sum = (Rate*ClientData.amount + НКД(ClientData.fiid,ClientData.amount,m_params.dateBegin)) * nominal/100;
                m_rep.Put_Val(Sum, "BRL;BRR;WT;HA=R;VA=C;T=M4");
                                                                                                               
                var SumRUB = 0;
                SmartConvertSum(SumRUB,money(Sum),m_params.dateBegin, ratefi, NATCUR, false);
                m_rep.Put_Val(SumRUB, "BRL;BRR;WT;HA=R;VA=C;T=M4;END;");

                AllSumRUB = AllSumRUB + SumRUB;
            end;
            //SetDialogFlag(1);
            m_rep.Put_Val("ИТОГО: ",    "START;R=8;HA=L;BOR;FB;");
            m_rep.Put_Val(AllSumRUB,    "HA=R;BOR;T=M4;FB;END;");
            m_rep.Blank_Line();
        end;

        EndAction();
    end;


    macro printData()
        var query = " SELECT party.t_partyid,                                                      " +
                    "        code.t_code,                                                          " +
                    "        party.t_name,                                                         " +
                    "        sf.t_number,                                                          " +
                    "        dlcon.t_dlcontrid,                                                    " +
                    "        sf.t_id,                                                              " +
                    "        sf.t_servkindsub,                                                     " +
                    "        (SELECT sfcontr.t_number                                              " +
                    "           FROM dsfcontr_dbt sfcontr                                          " +
                    "          WHERE sfcontr.t_id =                                                " +
                    "                   (SELECT dlcontr.t_sfcontrid                                " +
                    "                      FROM ddlcontr_dbt dlcontr                               " +
                    "                     WHERE dlcontr.t_dlcontrid = dlcon.t_dlcontrid)) contrnum " +
                    "   FROM dsfcontr_dbt sf,                                                      " +
                    "        ddlcontrmp_dbt dlcon,                                                 " +
                    "        dparty_dbt party,                                                     " +
                    "        dpartcode_dbt code                                                    " +
                    "  WHERE     sf.t_id = dlcon.t_sfcontrid                                       " +
                    "        AND party.t_partyid = sf.t_partyid                                    " +
                    "        AND party.t_partyid = code.t_partyid                                  " +
                    "        AND SF.T_SERVKIND = 1                                                 " +
                    "        AND code.t_codekind = 1                                               " +
                    "        AND code.t_state = 0                                                  ";
                    if (ValType(m_params.sfcontrid) == V_UNDEF)
                       query = query + " AND party.t_partyid = :partyid ";
                    else
                       query = query + " AND SF.t_id = :contrid ";
                    end;
        var cmd = DL_RSDCommand(query);
        if (ValType(m_params.sfcontrid) == V_UNDEF)
           cmd.AddParam(m_params.clientid);
        else
           cmd.AddParam(m_params.sfcontrid);
        end;

        var Contracts = cmd.Execute();

        while(Contracts.movenext)
            PrintContrData(Contracts.id, Contracts.contrnum, Contracts.servkindsub);
        end;

    end;

    /***********************************/  
    PRIVATE MACRO GetTxtPath()
        var RegistryPath = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR";
        var Path = "";
        var stat = 0;
 
        GetRegistryValue(RegistryPath, V_STRING, Path, stat);
        if(stat) 
            msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
        end;

        return Path;
    END;

    PRIVATE MACRO processPath( p_path )
        var l_str, l_p, l_path = "";

        p_path = trim( p_path );

        if( substr( p_path, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
            p_path = substr( p_path, 3 );

            l_str  = getCWD(false);    
            l_p    = strbrk( l_str, "\\" );
            while( l_p != 0 )
                 l_path = l_path + substr( l_str, 1, l_p - 1 ) + "\\";
                 l_str  = substr( l_str, l_p + 1 );
                 l_p    = strbrk( l_str, "\\" );
            end;
            return ( substr( l_path, 1, strlen( l_path ) - 1 ) + p_path );

        elif( substr( p_path, 1, 1 ) == "." ) /* ссылка на текущий каталог */
            return ( getCWD + substr( p_path, 2 ) );
        end;

        return p_path;
    END;
    MACRO GetRepFullPath(isRemote:bool)
        var Path = "";

        if(isRemote)
            var txtPath = GetTxtPath();

            if( substr( txtPath, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
                txtPath = substr(txtPath, 3 );
            end;

            if( substr( txtPath, 1, 1 ) == "\\" ) /* ссылка на вышестоящий каталог */
                txtPath = substr(txtPath, 2 );
            end;

            Path = GetCurDir(true) + "\\" + txtPath + "\\"; 
        else
            Path = processPath(GetTxtPath()) + "\\";
        end;

        // получить полный путь
        return Path;
    END;
    /***********************************/  
    macro run()
        m_rep.Init("utf8", true);

    /*
        m_rep.Put_Val("Номер гос.регистрации/Выпуск ЦБ", "START;BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("Остаток исходящий (шт)",          "BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("Котировка (ТСС)",                  "BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("Валюта котировки",                 "BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("Валюта номинала",                 "BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("Курс ЦБ на дату отчета",          "BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("Стоимость",                       "BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("НКД в валюте бумаги",             "BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("Стоимость с НКД",                 "BOR;WT;HA=C;VA=C;");
        m_rep.Put_Val("Стоимость по курсу ЦБ на дату отчета(RUB)", "BOR;WT;HA=C;VA=C;END;");      */
        m_rep.Set_Column_Width(/* 1*/  172,   //strlen("Номер гос.регистрации/Выпуск ЦБ")*5.55 = 31 * 5.55 = 172
                               /* 2*/  122,   //22 * 5.55 = 122
                               /* 3*/  84,    //15 * 5.55 = 83
                               /* 4*/  90,    //16
                               /* 5*/  90,    //15 
                               /* 6*/  122,   //22  Курс ЦБ на дату отчета
                               /* 7*/  92,    //тут длинное число
                               /* 8*/  106,   //19
                               /* 9*/  92,    //15
                               /*10*/  228,   //41  можно поставить 122 при высоте 33, когда будет работать перенос по словам
                               /*11*/  40,
                               /*12*/  51,
                               /*13*/  58,
                               /*14*/  66,
                               /*15*/  66,
                               /*16*/  93,
                               /*17*/  65,
                               /*18*/  56,
                               /*19*/  50,
                               /*20*/  163,
                               /*21*/  59,
                               /*22*/  52,
                               /*23*/  60,
                               /*24*/  77,
                               /*25*/  77);


        m_rep.Set_Landscape();
        m_rep.Set_Indent(0);

        m_rep.Print_Header("Портфель клиента", "Calibri", 9);

        m_Rep.Set_WrapText(0);

        PrintClientInfo();
    
        //PrintClientData();
        printData();
        m_Rep.Set_WrapText(1);
        m_rep.Blank_Line();


        m_rep.Put_Str("АО \"РОССЕЛЬХОЗБАНК\"",    "START;R=3;HA=L;END;");

        m_rep.Print_Bottom();
     
    /*        var contrnum = GetContrNum();
        contrnum = strSubst(contrnum,"\"","");
        contrnum = strSubst(contrnum,"\\","-");
        contrnum = strSubst(contrnum,"/","-");

        var m_CurrentRepName = "PORTFOLIO_"+contrnum;
            */
       var m_CurrentRepName = "PORTFOLIO_"+strsubst(strsubst(string(date():f) + string(time():f), ":",""),".","");

        var FullPath = GetRepFullPath(CreateOnTerm);
        var xmlNameFile = FullPath + string(m_CurrentRepName+".xml");
        var ShowFilePath = IIF(CreateOnTerm, "$", "") + FullPath;

        m_rep.Move(IIF(CreateOnTerm, "$", "") + xmlNameFile);


        var  resultFileName = m_CurrentRepName + ".xls";

        var  err = SC_ConvertToFormat(xmlNameFile, FullPath+resultFileName, SC_SRVREPFORMAT_EXCEL_XLSX, false, CreateOnTerm);

        if(not err)
            SC_ShowFile(ShowFilePath, resultFileName);
        end;

        m_rep.Delete();

    end;
end;

private const FT_STRING = 7;
private const FT_DATE = 9;

macro scroll_event(rs, cmd, id, key)
    if(cmd == DLG_KEY)
        if(key == 13)
            return CM_SELECT;
        end;
    end;
end;

class (TRsbPanel) MyRsbPanel1
    InitTRsbPanel();
    setSize(40,6);
    setPosition(35,15);

    Caption = "Отчет \"Портфель клиента\"";
    Status = "~Esc~ Выход  ~F2~ Выпуск отчета  ~F3~ Список";

    var currentContr,currentClient;

    addLabel(TRsbLabel(3, 3, "Клиент:"));
    var m_sEditFld2:String = "";
    var m_EditField2:TRsbEditField = TRsbEditField(FT_STRING);
    m_EditField2.bindValue(this, "m_sEditFld2", 20);
    m_EditField2.setPosition(10,3);
    m_EditField2.setSize(20,1);
    m_EditField2.Name = "Client";
    m_EditField2.rawType = FBT;
    addControl(m_EditField2);

    addLabel(TRsbLabel(3, 4, "Договор:"));
    var m_sEditFld3:String = "";
    var m_EditField3:TRsbEditField = TRsbEditField(FT_STRING);
    m_EditField3.bindValue(this, "m_sEditFld2", 20);
    m_EditField3.setPosition(10,4);
    m_EditField3.setSize(20,1);
    m_EditField3.Name = "Contr";
    m_EditField3.rawType = FBT;
    addControl(m_EditField3);

    addLabel(TRsbLabel(3, 1, "Дата:"));
    var m_DateBegin:TRsbEditField = TRsbEditField(FT_DATE);
    m_DateBegin.setPosition(10,1);
    m_DateBegin.setSize(10,1);
    m_DateBegin.value = {curdate};
    m_DateBegin.Name = "Date";
    m_DateBegin.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "DATEBEGIN_KEY_PRESSED"));
    addControl(m_DateBegin);

    m_EditField2.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "EditField_KEY_PRESSED"));
    m_EditField3.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "EditField_KEY_PRESSED"));
    m_EditField3.addEventHandler(RSB_EV_REMOVE_FOCUS , R2M(this, "EditField_EXIT"));
    m_EditField2.addEventHandler(RSB_EV_REMOVE_FOCUS , R2M(this, "EditField_EXIT"));
    addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "Panel_ButtonEvent"));

    PRIVATE MACRO AddCol (ar,ind, fld, head, width, rdonly, ty, dp)
        ar.value (ind * 6)     = fld;
        ar.value (ind * 6 + 1) = head;
        ar.value (ind * 6 + 2) = width;
        ar.value (ind * 6 + 3 ) = rdonly;   // fldType
        ar.value (ind * 6 + 4 ) = dp;//   decPoint
        ar.value (ind * 6 + 5 ) = 0;   // reserv
    END;

    macro getContrList()
        var query = " SELECT party.t_partyid, code.t_code, party.t_name, sf.t_number, dlcon.t_dlcontrid, sf.t_id "
                  + "   FROM dsfcontr_dbt sf, "
                  + "        ddlcontrmp_dbt dlcon, "
                  + "        dparty_dbt party, "
                  + "        dpartcode_dbt code "
                  + "  WHERE     sf.t_id = dlcon.t_sfcontrid "
                  + "        AND party.t_partyid = sf.t_partyid "
                  + "        AND party.t_partyid =code.t_partyid "
                  + "        AND code.t_codekind = 1 and code.t_state = 0 ";
        if (ValType(CurrentClient) != V_UNDEF)
           query = query + "        AND party.t_partyid = " + currentClient.value("t_partyid");
        end;

        var col = TArray;   
        var arnum = -1;
        AddCol (col, arnum=arnum+1, "t_code",    "Код",        10,  0,0);
        AddCol (col, arnum=arnum+1, "t_number",  "Номер",        50,  0,0);
        AddCol (col, arnum=arnum+1, "t_name",    "Клиент",        30,  0,0);
        AddCol (col, arnum=arnum+1, "t_partyid", "Идентификатор",20,  0,0);

        var cmd = RsdCommand(query);
        var rs = RsdRecordSet(cmd, rsdval_client, rsdval_static);
        if(RunScroll(rs, arnum+1, col, null ,"scroll_event","Договоры"))
            currentContr = rs;
            return true;
        end;

        return false;
    end;

    macro getClientsList()
        var query = " SELECT party.t_partyid, code.t_code, party.t_name "
                  + "   FROM dparty_dbt party, "
                  + "        dpartcode_dbt code, "
                  + "        dsfcontr_dbt sf "
                  + "  WHERE party.t_partyid =code.t_partyid "
                  + "        AND code.t_codekind = 1 and code.t_state = 0 "
                  + "        AND sf.t_partyid = party.t_partyid and sf.t_servkind = 1  "
                  + "        GROUP BY party.t_partyid, code.t_code, party.t_name ";
        var col = TArray;   
        var arnum = -1;
        AddCol (col, arnum=arnum+1, "t_code",    "Код",        10,  0,0);
        AddCol (col, arnum=arnum+1, "t_name",    "Клиент",        30,  0,0);
        AddCol (col, arnum=arnum+1, "t_partyid", "Идентификатор",20,  0,0);

        var cmd = RsdCommand(query);
        var rs = RsdRecordSet(cmd, rsdval_client, rsdval_static);
        if(RunScroll(rs, arnum+1, col, null ,"scroll_event","Клиенты"))
            currentClient = rs;
            return true;
        end;

        return false;
    end;


    macro findContr()
        var query = " SELECT party.t_partyid, code.t_code, party.t_name, sf.t_number, dlcon.t_dlcontrid, sf.t_id "
                  + "   FROM dsfcontr_dbt sf, "
                  + "        ddlcontrmp_dbt dlcon, "
                  + "        dparty_dbt party, "
                  + "        dpartcode_dbt code "
                  + "  WHERE     sf.t_id = dlcon.t_sfcontrid "
                  + "        AND sf.t_number = '" + m_EditField3.value + "'"
                  + "        AND party.t_partyid = sf.t_partyid "
                  + "        AND party.t_partyid =code.t_partyid "
                  + "        AND code.t_codekind = 1 and code.t_state = 0 ";

        var cmd = RsdCommand(query);
        var rs = RsdRecordSet(cmd, rsdval_client, rsdval_static);
        if(rs.movenext)
            currentClient = rs;
            currentContr = rs;
            return true;
        end;
        return false;
    end;

    macro EditField_KEY_PRESSED(RsbEvent)
       if(RsbEvent.keyCode == 317)
          if (RsbEvent.Source.Name == "Contr")
             if (getContrList())
                m_EditField2.value = currentContr.value("t_name");
                m_EditField3.value = currentContr.value("t_number");
             end;
          elif (RsbEvent.Source.Name == "Client")
             if (getClientsList())
                m_EditField2.value = currentClient.value("t_name");
                m_EditField3.value = "";
             end;
          end;
       elif(RsbEvent.keyCode == 27)
          close(0);
       end

    end;

    macro EditField_EXIT(RsbEvent)
        if (RsbEvent.Source.Name == "Contr")
           if (trim(m_EditField3.value) == "")
              currentContr = null;
           else
              if (findContr())
                 m_EditField2.value = currentContr.value("t_name");
                 m_EditField3.value = currentContr.value("t_number");
              else
                 m_EditField3.value = "";
                 currentContr = null;
              end;
           end;
        elif (RsbEvent.Source.Name == "Client")
           if (trim(m_EditField2.value) == "")
              currentClient = null;
           end;
        end;
    end;

    macro Panel_ButtonEvent(RsbEvent)
       if(RsbEvent.keyCode == 316)
          var RepParams = ReportParams();

          RepParams.dateBegin = m_DateBegin.value;
          if (currentClient)
             if (CurrentContr)
                RepParams.dlcontrid = currentContr.value("t_dlcontrid");
                RepParams.sfcontrid = currentContr.value("t_id");  
                RepParams.clientid  = currentContr.value("t_partyid");
             else
                RepParams.clientid  = currentClient.value("t_partyid");
             end;
             CClientPortfolio_Report(RepParams).run();
          else
             msgbox("Необходимо указать клиента");
          end;
       end;
    end;

    macro DATEBEGIN_KEY_PRESSED(RsbEvent)
       if(RsbEvent.keyCode == 317)
           m_DateBegin.value = GetDateByCalendar(m_DateBegin.value);
       end;
    end;
end;

var panel = MyRsbPanel1;
panel.run;
exit(1);


