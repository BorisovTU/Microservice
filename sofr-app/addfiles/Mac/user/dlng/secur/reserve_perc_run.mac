//Запуск загрузки ставок оценочных резервов
//reserve_perc_run.mac

import reserve_perc_load, RsbFormsInter;

class (TRsbPanel) Reserve_or_rate_panel (_reserve)
  var Reserve = _reserve;

var FT_INT     = 0,
    FT_DOUBLE  = 4,
    FT_STRING  = 7,
    FT_DATE    = 9,
    FT_CHR     = 12,
    FT_NUMERIC = 25;

var ESC  = 27,
    F2   = 316,
    F3   = 317,
    F9   = 323,
    SHF6 = 345;

var ISChecked = false;

  macro Init ()
    var control;
    var rn = 0;

    SetSize    (25, 7); //Ширина, высота
    SetPosition(20,  10);

    setCaption("Загрузка ставок ОР");

/*****************************************************/
    rn = rn + 1;
    addLabel(TRsbLabel(4, rn, "Загрузка файлов"));

    control = TRsbCheckBox;
    control.setPosition(2, rn);
    control.setSize (1, 1);

    control.name       = "load_files";
    control.dataType   = FT_CHR;
    control.textLength = 1;
    control.Editable   = true;
    control.Checked    = "X";

    addControl(control);

/*****************************************************/
    rn = rn + 1;
    addLabel(TRsbLabel(0, rn, "---------------------------------------"));

/*****************************************************/
    rn = rn + 1;
    addLabel(TRsbLabel(4, rn, "Заполнить ставки ОР"));

    control = TRsbCheckBox;
    control.setPosition(2, rn);
    control.setSize(1, 1);

    control.name       = "update_rates";
    control.dataType   = FT_CHR;
    control.textLength = 1;
    control.Editable   = true;
    control.Checked    = "X";
    control.onChecked(R2M(this, "onChecked_update_rates"));
    
    addControl(control);
/*****************************************************/
    rn = rn + 1;
    addLabel(TRsbLabel(0, rn, "---------------------------------------"));
/*****************************************************/
    rn = rn + 1;
    addLabel(TRsbLabel(4, rn, "Выпустить отчет"));

    control = TRsbCheckBox;
    control.setPosition(2, rn);
    control.setSize(1, 1);

    control.name       = "make_rep";
    control.dataType   = FT_CHR;
    control.textLength = 1;
    control.Editable   = true;
    control.Checked    = "X";
    control.onChecked(R2M(this, "onChecked_make_rep"));
    
    addControl(control);

/*****************************************************/
    rn = rn + 1;
    addLabel(TRsbLabel(6, rn, "Отправить в ЦКРР"));

    control = TRsbCheckBox;
    control.setPosition(4, rn);
    control.setSize(1, 1);

    control.name       = "send_rep";
    control.dataType   = FT_CHR;
    control.textLength = 1;
    control.Editable   = true;
    control.enabled    = false;
    control.Checked    = "X";
    
    addControl(control);

/*****************************************************/
    rn = rn + 1;
    addLabel(TRsbLabel(2, rn, "Дата отчета:"));

    control = TRsbEditField;
    control.setPosition(13, rn);
    control.setSize    (10, 1);

    control.name       = "dt";
    control.dataType   = FT_DATE;
    control.textLength = 10;
    control.Editable   = true;
    control.value      = {curdate};

    addControl(control);

    addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
  End;

  macro onChecked_update_rates (RsbEvent:Object):variant
    var cb_make_rep = GetControl("make_rep");
    var cb_send_rep = GetControl("send_rep");

    if (RsbEvent.Id == RSB_EV_CONTROL_CHECKED)

      if (GetControl("update_rates").Checked)
        cb_make_rep.Checked = "X";
        cb_make_rep.enabled = false;
        cb_make_rep.ReDraw();

        cb_send_rep.enabled = false;
        cb_send_rep.Checked = "X";
        cb_send_rep.ReDraw();
      else
        cb_make_rep.enabled = true;
        if (cb_make_rep.Checked)
          cb_send_rep.enabled = true;
          cb_send_rep.ReDraw();
        end;
        cb_make_rep.ReDraw();
      end;

    end;

    return true;

  End;

  macro onChecked_make_rep (RsbEvent:Object):variant
    var cb_send_rep = GetControl("send_rep");

    if (RsbEvent.Id == RSB_EV_CONTROL_CHECKED)

      if (GetControl("make_rep").Checked)
        cb_send_rep.enabled = true;
        cb_send_rep.Checked = "X";
      else
        cb_send_rep.Checked = "";
        cb_send_rep.enabled = false;
      end;
      cb_send_rep.ReDraw();
    end;

    return true;
  End;

  macro eventHandler(EV)
    if (EV.KeyCode == F2)

      if (GetControl("load_files").Checked)
        Reserve.LoadFiles = true;
        ISChecked = true;
      end;

      if (GetControl("update_rates").Checked)
        Reserve.StartUpdate = true;
        ISChecked = true;
      end;

      if (GetControl("make_rep").Checked)
        Reserve.StartReport = true;
        ISChecked = true;
      end;

      if (GetControl("send_rep").Checked)
        Reserve.SendReport = true;
        ISChecked = true;
      end;

      Reserve.ValueDate = GetControl("dt").value;

      close(0);

      return true;
    end;

    if (EV.KeyCode == ESC)
      return true;
    end;

    return true;
  end;

  InitTRsbPanel();
  Init();
END;

macro Reserve_or_rate ()
  var Reserve = Reserve_load();
  var Pn = Reserve_or_rate_panel(Reserve);
  Pn.Run();
  
  if (not Pn.ISChecked)
    return 1;
  end;

  Reserve.Run();
End;

Reserve_or_rate();
msgbox("Выполнение завершено");
exit(1)
