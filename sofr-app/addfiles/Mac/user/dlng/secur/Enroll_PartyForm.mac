/** 
 @file Enroll_PartyForm.mac
 @brief Форма параметров создаваемого контрагента
  
 # tag 
 - functional_block:Клиенты_Зачисление
 - code_type:API
 - Зачисление
 - Создать клиента
   
 # changeLog 
 |date       |author         |tasks            |note                            
 |-----------|---------------|-----------------|--------------------------------
 |25.07.2025 |Топорков Д.В.  |BOSS-8439        | Создание
*/ 

import "DlRepForm_h.mac";

// Номера полей в форме отчета
private const PNFLD_PARTY_NAME     = 0, // Наименование субъекта
              PNFLD_PARTY_INN      = 1, // ИНН
              PNFLD_PARTY_KPP      = 2; // КПП

private const H_PNFLD_TEXT  = 100;
private const TOO_MUCH_RECORDS = -2;

private const K_ENTER = 13,
              K_F2    = 316,
              K_F9    = 323;

/**
@brief Класс параметров возврата
*/
class CEnrollPartyParams()
  var partyID = null;
  
  var partyName = "";
  var partyINN = "";
  var partyKPP = "";
  
  macro GetINN()
    var result = partyINN;
    
    if (StrLen(partyKPP) > 0)
      result = result + "/" + partyKPP;
    end;
    
    return result;
  end;
end;

private macro FldProc_Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) // инициализация по умолчанию
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) // значение поля было изменено
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
end;

/**
@brief Найти id контрагента по ИНН
*/
private macro FindPartyByINN(INN): Integer
  if ((ValType(INN) == V_STRING) and (StrLen(INN) > 0))
    var cmd = RsdCommand("SELECT DISTINCT(t_objectID) " + 
                           "FROM dObjCode_dbt " +
                          "WHERE t_objectType = " + OBJTYPE_PARTY +
                           " AND t_codeKind = " + PTCK_INN + 
                           " AND t_BankCloseDate = TO_DATE('01010001', 'DDMMYYYY') " +
                           " AND (t_code LIKE '" + INN + "/%' OR t_code = '" + INN +"') " +
                          "ORDER BY t_objectID");
    var rs = RsdRecordSet(cmd);
    if (rs.MoveNext())
      var partyID = rs.Value("t_objectID");
      if ((partyID != 1) and rs.MoveNext())
        return TOO_MUCH_RECORDS;
      end;

      return partyID;
    end;
  end;

  return -1;
end;

/**
@brief Класс работы с панелью параметров выпуска отчета
*/
class (DL_CPanel) CEnroll_NewPartyForm()
  var params = CEnrollPartyParams();

  private macro Init()
    AddFieldProc(0, PNFLD_PARTY_NAME, H_PNFLD_TEXT, @FldProc_Default, params.partyName);
    AddFieldProc(0, PNFLD_PARTY_INN,  H_PNFLD_TEXT, @FldProc_Default, params.partyINN);
    AddFieldProc(0, PNFLD_PARTY_KPP,  H_PNFLD_TEXT, @FldProc_Default, params.partyKPP);
  end;

  private macro CheckInput(error: @String)
    error = "";
    var INN = Trim(GetFieldValue(PNFLD_PARTY_INN));
    var KPP = Trim(GetFieldValue(PNFLD_PARTY_KPP));
    
    if (StrLen(Trim(GetFieldValue(PNFLD_PARTY_NAME))) == 0)
      error = "Не указано наименование субъекта";
    elif (((StrLen(INN) != 10) and (StrLen(INN) != 12)) or (not StrIsNumber(INN)))
      error = "Неверный ИНН";
    elif ((StrLen(KPP) != 0) and ((StrLen(KPP) != 9) or (not StrIsNumber(KPP))))
      error = "Неверный КПП";
    elif (FindPartyByINN(INN) != -1)
      error = "Найден субъект с указанным ИНН";
    end;
    
    if (StrLen(error) > 0)
      return false;
    end;
    
    return true;
  end;

  macro KeyProc(Pan:Variant, Cmd:Integer, FldId:Integer, Key:Integer)
    var error = "";
    var result = this.KeyProc(Pan, Cmd, FldId, Key);
 
    if (Cmd == DLG_KEY)
      if ((Key == K_ENTER) and (FldId == PNFLD_PARTY_KPP))
        result = CM_IGNORE;
      elif (Key == K_F9)
        result = CM_IGNORE;
      elif ((Key == K_F2) and (not CheckInput(@error)))
        MsgBox(error);
        result = CM_IGNORE;
      end;
    end;
    
    return result;
  end;

  macro Run()
    var result = Run();

    if (result)
      params.partyName = Trim(GetFieldValue(PNFLD_PARTY_NAME));
      params.partyINN = Trim(GetFieldValue(PNFLD_PARTY_INN));
      params.partyKPP = Trim(GetFieldValue(PNFLD_PARTY_KPP));
      
      var party = RsbParty();

      party.legalForm   = PTLEGF_INST; // 2
      party.shortName = params.partyName;
      party.fullName = params.partyName;
      party.Code.SetCode(PTCK_INN, params.GetINN());
      party.Update();
      
      if (party.partyID > 0)
        params.partyID = party.partyID;
      else
        MsgBox("Ошибка создания субъекта " + params.partyName);
      end;
    end;
    
    return result;
  end;

  initDL_CPanel( "sp_res.lbr", "ENRNWPRT" );
end;
