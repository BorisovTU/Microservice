/** 
 @file Refill_CreatePayment.mac
 @brief Создание платежа подкрепления
  
 # tag 
 - functional_block:Клиенты_Зачисление
 - code_type:API

   
 # changeLog 
 |date       |author         |tasks            |note                            
 |-----------|---------------|-----------------|--------------------------------
 |01.04.2024 |Гераськина Т.В.|BOSS-1702        | Создание

*/ 
import rsd, BankInter, PaymInter, PTInter, OprInter;
import globals;
import usr_connect_attr;

const C_PAYMENT_STATUS = 3000;  // на отправке



/**
@brief Контроль платежа (запуск на выполнение)
@param[in] p_paymentid ID платежа
@return true / false 
*/
macro RunRefillPayment(errmsg:@string, pid)
   var sql_str, cmd, res, rs;
   errmsg = "";

   /* Служебная таблица для массового выполнения */
   sql_str = String(" BEGIN                                                  \n",
                    "    DELETE FROM dpmrunopr_tmp;                          \n",
                    "    INSERT INTO dpmrunopr_tmp                           \n",
                    "      (t_dockind, t_paymentid, t_state, t_errormessage) \n",
                    "    SELECT p.t_primdockind, p.t_paymentid, 0, chr(1)    \n",
                    "     FROM unptxop_payment_link_dbt l, dpmpaym_dbt p     \n",
                    "    WHERE l.t_paymentid = p.t_paymentid                 \n",
                    "      AND l.t_errorcode = 0                             \n");
   if (Valtype(pid) == V_INTEGER)
      sql_str = String(sql_str, " AND p.t_paymentid = "+pid+"                \n");
   end;
   sql_str = String(sql_str, " AND p.t_paymstatus = " + C_PAYMENT_STATUS + ";\n",
                    " END;                                                    " );
   cmd = RSDCommand(sql_str);
   cmd.execute();
   cmd.close();

   /* Запускаем шаг */
   res = PM_MassExecuteOperation(true); //silent true/false
   if(res != 0)
      errmsg = "Ошибка при выполнении операции " + res;
      return false;
   end;
   
   /* Проверяем результат */
   sql_str = String(" DECLARE                                                   \n",
                    "    v_refillid  UNPTXOP_PAYMENT_LINK_DBT.t_refillid%type;  \n",
                    " BEGIN                                                     \n",
                    "   FOR x in (SELECT t_paymentid, t_state, t_errormessage   \n",
                    "               FROM dpmrunopr_tmp) LOOP                    \n",
                    "      UPDATE unptxop_payment_link_dbt l                    \n",
                    "         SET t_errorcode = x.t_state,                      \n",
                    "             t_errortext = x.t_errormessage                \n",
                    "       WHERE l.t_paymentid = x.t_paymentid                 \n",
                    "      RETURNING t_refillid INTO v_refillid;                \n", 
                    "      INSERT INTO UNPTXOP_PAYMENT_LINK_STEP_DBT                                        \n"+
                    "         (T_STEPID, T_REFILLID, T_SYSTEMDATE, T_ACTION, T_COMMENT, T_OPER)             \n"+
                    "      VALUES (0, v_refillid, sysdate, 30, 'Исполнение платежа '||x.t_errormessage, :poper); \n"+
                    "   END LOOP;                                               \n",
                    " END;                                                       " );
   cmd = RsdCommand(sql_str);
   cmd.Addparam("poper", RSDBP_IN, {oper});
   cmd.execute();
   cmd.close();

   return true;
end;


/**
@brief Создание платежа подкрепления
@param[in] p_dateop дата операции
@param[in] p_sumop сумма операции
@param[in] p_clientaccount расчетный код клиента
@param[in] p_nptxopid id операции зачисления
@param[out] acctrnid id проводки
@param[out] paymentid id платежа
@param[out] error текст ошибки
@return 0 - успешно, 1 - ошибка

Получатель платежа НКО НКЦ (АО), банка получателя НКО АО НРД.
Считаем, что это константы и что в справочнике субъектов корректные данные
*/
macro CreateRefillPayment(p_dateop:date, p_sumop:money, p_clientaccount:string, p_nptxopid:integer, p_refillid:integer, p_account:string, 
                          acctrnid:@integer, paymentid:@integer, error:@string)
   var pmobj = RSBPayment();
   var stat = 0;

   const C_NKO_NKC_ID = 1181;
   const C_NKO_NRD_ID = 4;
   const C_ACCOUNT_RECEIVER = "30414810000000000911";
   const C_GLOBAL_REFILL_DOCKIND = 460; //Распоряжение на оплату
   const C_SINGLE_REFILL_DOCKIND = 4607; //Списание зачисление денежных средств
   const C_PURPOSE = 58; //Перевод средств
   const NATCUR = 0;
   
   acctrnid = 0;
   paymentid = 0;
   error = "";

   if (p_nptxopid > 0) // индивидуальное подкрепление под конкретную операцию
      pmobj.DocKind   = C_SINGLE_REFILL_DOCKIND;
      pmobj.DocumentID= string(p_nptxopid:o:34);
      pmobj.Number    = p_nptxopid;
   else 
      pmobj.DocKind   = C_GLOBAL_REFILL_DOCKIND;
      pmobj.Number    = p_refillid;
   end;
   pmobj.Purpose      = C_PURPOSE;  
   pmobj.BaseAmount   = p_sumop;
   pmobj.PayerAmount  = p_sumop;
   pmobj.BaseFIID     = NATCUR;
   pmobj.ReceiverFIID = NATCUR;
   pmobj.PayerFIID    = NATCUR;
   pmobj.ValueDate    = p_dateop;  
   pmobj.PaymStatus   = C_PAYMENT_STATUS; //на отправку 
   pmobj.Ground       = "Перечисление средств обеспечения ФРОРК"+p_clientaccount+". НДС не облагается";
   pmobj.PrimDocKind  = DOC_BO_PAYMENT;
   pmobj.NumberPack   = 11;
   pmobj.Priority     = 5;
   pmobj.ShifrOper    = "09";
   pmobj.Oper         = {oper};
   pmobj.paymentkind  = "С";

   Pmobj.SetPayerPI(
               PAYMENTS_GROUP_UNDEF,    // Group
               {OurBank},               // BankID  // Банк плательщика
               PTCK_BIC,                // BankCodeKind
               {MFO_Bank},              // BankCode
               {Name_Bank},             // BankName
               {CORAC_Bank},            // CorrAcc
               pmobj.BaseFIID,          // AccountFI
               1,                       // AccountChapter
               p_account,               // Account   // Счет плательщика
               {OurBank},               // ClientID  // Плательщик
               {Name_Bank},             // ClientName,
               {INN_Bank},              // ClientINN,
               PTCK_BIC,                // ClientCodeKind,
               {MFO_Bank}               // ClientCode
              );

   var ReceiverID = C_NKO_NKC_ID; // НКО НКЦ (АО)
   var PartyReceiver = RSBParty(ReceiverID);
   var ReceiverBankID = C_NKO_NRD_ID;  // НКО АО НРД
   var PartyReceiverBank = RSBParty(ReceiverBankID);

   Pmobj.SetReceiverPI(
               PAYMENTS_GROUP_UNDEF,            // Group
               ReceiverBankID,                  // BankID
               PTCK_BIC,                        // BankCodeKind
               PartyReceiverBank.Code(PTCK_BIC),// BankCode
               PartyReceiverBank.FullName,      // BankName
               "30105810345250000505",          // CorrAcc
               pmobj.BaseFIID,                  // AccountFI
               1,                               // AccountChapter
               C_ACCOUNT_RECEIVER,              // Account
               ReceiverID,                      // ClientID
               PartyReceiver.FullName,          // ClientName,
               PartyReceiver.Code(PTCK_INN),    // ClientINN,
               PTCK_CONTR,                      // ClientCodeKind,
               PartyReceiver.Code(PTCK_CONTR),  // ClientCode
               0,                              // Corschem
               PM_CORRPOS_TYPE_USER             // CorPosType
            );

   Pmobj.IsFactPaym   = "X"; 

   pmobj.BaseRate.Actuate(pmobj.ValueDate, false);
   pmobj.FactRate.Actuate(pmobj.ValueDate, false);
   pmobj.Actuate();
   pmobj.Update();

  //поставить платежу признак выгрузки
   ExecSql("update dpmpaym_dbt set t_userfield2=chr(1), t_userfield3='1' where t_paymentid = :paymid", MakeArray(SqlParam("paymid", pmobj.paymentid)));

   Connect_attr_to_paym (pmobj.paymentid, ATTR_GROUP_ED_FORMAT, "ЕД107");

   if(substr(pmobj.futurereceiveraccount,1,5) =="30102")
      Connect_attr_to_paym (pmobj.paymentid, ATTR_GROUP_DOCUMENT_BIS, "01");
   else
      Connect_attr_to_paym (pmobj.paymentid, ATTR_GROUP_DOCUMENT_BIS, "06o");
   end;

   if(PM_BOOperation(pmObj) == false)
     stat = 1;
   end;
   if ( not stat )
      var tr = pmobj.MakeTransaction();
      if( tr == NULL )
         error = "Ошибка при создании проводки по платежу";
         stat = 1;
      else
         tr.Number_Pack = pmobj.NumberPack;
         if( not tr.Carry() )
            error = "Ошибка при выполнении проводки";
            stat = 1;
         else 
            paymentid = pmobj.PaymentID;
            acctrnid = tr.acctrnid;
         end;
      end;
   end;

   return stat;
OnError(e);
   error = RsbGetErrorEx(e);
   return 1;
end;