import BankInter, ReportUser;

PRIVATE CLASS (TX_ReportUser) W525_Report
  var m_Date;
  var m_DataExists = false,
      m_exitFlag   = false;

  /*Создание отчета*/
  PRIVATE MACRO Create()

    /*Точка входа*/
    var query = "";
    var m_DS;
    var errmsg;

    /*Отключаем все индикаторы*/
    if(m_UseTemplate)
      m_ObjTemplateXLS.SetFlagShowIndicator(false);
    else
      __BUG_Global_m_ObjTemplateXLS.SetFlagShowIndicator(false);
    end;
    m_ProgressIndicator = CreateProgressIndicator(true);

    m_Date = {curdate};
    if (not getDate(m_Date,"Дата формирования отчёта"));
      m_exitFlag = true;
      return false;
    end;

    /*Лист "Остатки по 306"*/
    SetActiveSheet("Остатки по 306");

    PrintFormatString( "", "ReportName_1", "Остатки по 306* за дату: " + date(m_Date));

    query = " select settacc.t_partyid,                                               "+ 
            "        settacc.t_recname t_client,                                      "+ 
            "        sfcontr.t_number,                                                "+ 
            "        settacc.t_fiid,                                                  "+ 
            "        fininstr.t_ccy t_currency,                                       "+ 
            "        settacc.t_account,                                               "+ 
            "        acc.t_nameaccount,                                               "+ 
            "        nvl(abs(rsi_rsb_account.restall(settacc.t_account,               "+ 
            "                                        settacc.t_chapter,               "+ 
            "                                        settacc.t_fiid,                  "+ 
            "                                        :pDate1)),                       "+ 
            "            0) t_rest                                                    "+
            "   from dsettacc_dbt settacc                                             "+ 
            "   join dsfssi_dbt sfssi                                                 "+ 
            "     on sfssi.t_setaccid = settacc.t_settaccid                           "+ 
            "   join dsfcontr_dbt sfcontr                                             "+ 
            "     on sfcontr.t_id = TO_NUMBER(sfssi.t_objectid)                       "+ 
            "    and sfssi.t_objecttype = 659                                         "+ 
            "   join dfininstr_dbt fininstr                                           "+ 
            "     on fininstr.t_fiid = settacc.t_fiid                                 "+ 
            "   join daccount_dbt acc                                                 "+ 
            "     on acc.t_chapter = settacc.t_chapter                                "+ 
            "    and acc.t_account = settacc.t_account                                "+ 
            "    and acc.t_code_currency = settacc.t_fiid                             "+ 
            "  where sfcontr.t_datebegin <= :pDate2                                   "+ 
            "    and (sfcontr.t_dateclose = to_date('01.01.0001', 'dd.mm.yyyy') or    "+ 
            "        sfcontr.t_dateclose >= :pDate3 - 1)                              "+
            "    and acc.t_balance like '306%'                                        "+
			"    and sfcontr.t_servkind <> 0                                          "+
            "  order by settacc.t_partyid, settacc.t_fiid                             ";
    m_DS = RSDCommand(query);
    m_DS.AddParam(":pDate1", RSDBP_IN, m_Date);
    m_DS.AddParam(":pDate2", RSDBP_IN, m_Date);
    m_DS.AddParam(":pDate3", RSDBP_IN, m_Date);
    m_DS.Execute();
    m_RS = TRsbDataSet(m_DS);    

    RegisterTable( "ReportRow_1" , "",
                   "Field_1", 
                   "Field_2", 
                   "Field_3", 
                   "Field_4", 
                   "Field_5", 
                   "Field_6");
        
    var i = 1;
    while (m_RS.MoveNext())
      PrintTableLine(
        "Field_1", i, null,
        "Field_2", m_Rs.value("t_client"), null,
        "Field_3", m_Rs.value("t_number"), null,
        "Field_4", m_Rs.value("t_currency"), null,
        "Field_5", string(m_Rs.value("t_account")), null,
        "Field_6", m_Rs.value("t_nameaccount"), null,
        "Field_7", m_Rs.value("t_rest"), null);
      i = i + 1;
    end;
    EndTable();

    if (i > 1)            
      m_DataExists = true;
    end;    
  END;

  MACRO CReport_DataExist() 
    if (m_DataExists)
      return true; 
    else
      return false;
    end;
  END;

  MACRO CReport_NoDataMsg() 
    if ((not m_DataExists) and (not m_exitFlag))
      msgbox("Информация за указанную дату отсутствует.");
    end;
  END; 

  initTX_RegistrReport(NULL, "Report_W525.xlsx");/*GAA:503577, old: "Report_W525.xls"*/
END;


/*Точка входа*/
var defprec = SetDefPrec(12);
var r = W525_Report();
r.InitReport("Остатки по 306", null);
r.Run();
SetDefPrec(defprec);

SetExitFlag(1);
