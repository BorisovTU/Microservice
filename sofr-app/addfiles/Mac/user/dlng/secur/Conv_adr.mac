/*
$Name:        conv_adr.mac
$Module:      Ценные бумаги
$Description: Конвертация АДР в акции по НДФЛ-зачислениям
*/

// ver 14/10/2022 Добавлена возможность делать сплит
// ver 20/12/2022 Добавлена возможность просматривать историю расчетов и запускать генерацию операций Пересчета НОБ по клиентам с переконвертированными НДФЛ-зачислниями

import RsbFormsInter, cb_sql, globals, FiInter, PTInter, vtb_createenroll;

private const OBJGROUP_VTBNDFL = 210; // Категория Зачисления ВТБ для НДФЛ 



private var glErr = 0;
private var gSQL = "";

private macro SqlHandler(str)
  gSQL = gSQL + str;
end;


macro CreateNptxopRecalc(ConvID,
                            ClientID:integer,
                            UniCodeStr:string, //Часть кода для операций
			    errp:@string,
			    result_protocol:@string
                           )
  var query, cmd;

  gSQL = "";

  SetOutHandler(@SqlHandler);

[ DECLARE
    v_nptxop                    DNPTXOP_DBT%ROWTYPE;
    TYPE ListNPTXOP_t IS TABLE OF DNPTXOP_DBT%ROWTYPE;
    v_ListNPTXOP                ListNPTXOP_t := ListNPTXOP_t ();

    TYPE nptxobj_t IS TABLE OF DNPTXOBJ_DBT%ROWTYPE INDEX BY BINARY_INTEGER;
    v_nptxobj nptxobj_t;

    v_BegDate DATE;
    v_ClientID NUMBER;
    v_SubKind NUMBER;
    v_ConvID NUMBER;
    v_i       NUMBER := 1;
    v_f       NUMBER := 1;
    v_UniCodeStr VARCHAR2(15);

    PROCEDURE insertOP(p_begdate IN DATE, p_operdate IN DATE, p_prevdate IN DATE, pSubKind IN NUMBER, p_PartyID IN NUMBER, p_FIID IN NUMBER, p_UniCodeStr IN VARCHAR2)
    IS
      v_OperDprt                  NUMBER (10) := 1;
      v_oper                      NUMBER (10) := 1;
      v_Kind_Operation            NUMBER (10) := 2035;
      OBJTYPE_NPTXCALC   CONSTANT NUMBER (5) := 132;
      REFOBJ_NPTXCALC    CONSTANT NUMBER (5) := 1;

      function Get_Count_OP_Name(OP_name in varchar2) return number is
       i number := 0;
      begin 
       select count(1) into i from dnptxop_dbt where t_code like OP_Name and t_dockind = 4605;
        return i;
      end;

     function Get_OP_Name(OP_name in varchar2) return varchar2 is
       i number := 0;
       flag_exit number := 0;
       New_OP_Name varchar2(100);
     begin

      if Get_Count_OP_Name(OP_name) > 0 then
       i := i+1;
       loop
         EXIT WHEN flag_exit = 1;
           if Get_Count_OP_Name(OP_name||'_'||i) = 0 then 
           New_OP_Name := OP_name||'_'||i;
           flag_exit := 1;
         else
           i := i+1;   
         end if;
       end loop;
      else 
        New_OP_Name := OP_name;
      end if;
      return New_OP_Name;
     end;

    BEGIN
        v_nptxop.t_Code := Get_OP_Name(p_UniCodeStr||'_' ||v_i||'_'||p_PartyID);
        v_nptxop.t_ID := dnptxop_dbt_seq.NEXTVAL;
        v_nptxop.t_DocKind := RSI_NPTXC.DL_CALCNDFL;
        v_nptxop.t_OperDate := p_operdate;
        v_nptxop.t_Kind_Operation := v_Kind_Operation;
        v_nptxop.t_Client := p_PartyID;
        v_nptxop.t_Department := v_OperDprt;
        v_nptxop.t_Oper := v_oper;
        v_nptxop.t_Status := RSI_NPTXC.DL_TXOP_Prep;
        v_nptxop.t_SubKind_Operation := pSubKind;
        v_nptxop.t_IIS := CNST.UNSET_CHAR;
        --ЗПУ
        v_nptxop.t_Account := RSI_RsbOperation.ZERO_STR;
        v_nptxop.t_AccountTax := RSI_RsbOperation.ZERO_STR;
        v_nptxop.t_BegRecalcDate := p_begdate;
        v_nptxop.t_CalcNDFL := CNST.SET_CHAR;
        v_nptxop.t_Contract := 0;
        v_nptxop.t_Currency := 0;
        v_nptxop.t_CurrencySum := 0;
        v_nptxop.t_CurrentYear_Sum := 0;
        v_nptxop.t_EndRecalcDate := p_prevdate;
        v_nptxop.t_FIID := p_FIID;
        v_nptxop.t_FlagTax := CNST.UNSET_CHAR;
        v_nptxop.t_LimitStatus := 0;
        v_nptxop.t_MarketPlace := 0;
        v_nptxop.t_MarketPlace2 := 0;
        v_nptxop.t_MarketSector := 0;
        v_nptxop.t_MarketSector2 := 0;
        v_nptxop.t_Method := 0;
        v_nptxop.t_OutCost := 0;
        v_nptxop.t_OutSum := 0;
        v_nptxop.t_Partial := CNST.UNSET_CHAR;
        v_nptxop.t_Place := 0;
        v_nptxop.t_Place2 := 0;
        v_nptxop.t_PlaceKind := 0;
        v_nptxop.t_PlaceKind2 := 0;
        v_nptxop.t_PrevDate := p_prevdate;
        v_nptxop.t_PrevTaxSum := 0;
        v_nptxop.t_Recalc := CNST.SET_CHAR;
        v_nptxop.t_Tax := 0;
        v_nptxop.t_TaxBase := 0;
        v_nptxop.t_TaxSum := 0;
        v_nptxop.t_TaxSum2 := 0;
        v_nptxop.t_TaxToPay := 0;
        v_nptxop.t_Time := NPTAX.UnknownTime;
        v_nptxop.t_TotalTaxSum := 0;
        v_nptxop.t_TOUT := 0;
        v_nptxop.t_TaxDp := 0;
        v_ListNPTXOP.EXTEND ();
        v_ListNPTXOP (v_ListNPTXOP.LAST) := v_nptxop;

        v_i := v_i + 1;
    END;

  BEGIN
    v_i := 1;

    v_ConvID := ?/*1*/;
    v_UniCodeStr := ? /*3*/;
    v_ClientID := ? /*4*/;


   FOR cCl IN ( select t_ClientID, t_Fiid, t_Fiid_new from U_CONV_ADR_DATA_DBT where (( t_Clientid = v_ClientID) or (v_ClientID = -1)) and t_convid = v_ConvID and t_flag1 = 'X' and t_flag2 <> 'X'
               group by t_ClientID, t_Fiid, t_Fiid_new)
   LOOP

      SELECT MIN (rq.t_factdate) INTO  v_BegDate
        FROM U_CONV_ADR_DATA_DBT t, ddl_tick_dbt tk, ddlrq_dbt rq
       WHERE     t.t_clientid = cCl.t_Clientid
             AND t.t_fiid_new = cCL.t_Fiid_new
             AND tk.t_dealid = t.t_dealid
             AND t.t_dealid = rq.t_docid
             AND tk.t_bofficekind = rq.t_dockind
             AND rq.t_type = 8
             AND t.t_convid = v_ConvID;


       FOR cData IN ( 
                         select oc.t_Client,
                             (select count(1)
                                from dnptxop_dbt op
                               where op.t_DocKind = 4605
                                 and op.t_OperDate >= v_BegDate
                                 and op.t_SubKind_Operation = 10 --Окончание года
                                 and op.t_IIS != 'X'
                                 and op.t_Client = oc.t_Client
                             ) as CntEndYear,
                             (SELECT MAX(op.t_OperDate)
                                FROM DNPTXOP_DBT op
                               WHERE op.t_DocKind = 4605
                                 AND op.t_Client = oc.t_Client
                                 AND op.t_IIS != 'X'
                                 AND t_Recalc = chr(0) /*DEF-31726 Неверный расчет финансового результата и суммы возврата НДФЛ*/
                                 AND op.t_OperDate >= v_BegDate
                             ) as PrevOpDate
                        from (select DISTINCT op.t_Client
                                from dnptxop_dbt op
                               where op.t_DocKind = 4605
                                 and op.t_OperDate >= v_BegDate
                                 and op.t_IIS != 'X' and op.t_Client = cCl.t_ClientID
                             ) oc
                    )
       LOOP

         v_SubKind := 20; --Обычный расчет
         
         IF cData.CntEndYear > 0 THEN
           v_SubKind := 10; --Окончание года
         END IF;

         insertOP(v_BegDate, cData.PrevOpDate, cData.PrevOpDate, v_SubKind, cData.t_Client, cCL.t_Fiid, v_UniCodeStr);
         insertOP(v_BegDate, cData.PrevOpDate, cData.PrevOpDate, v_SubKind, cData.t_Client, cCl.t_Fiid_new, v_UniCodeStr);
         UPDATE u_conv_adr_data_dbt set t_flag2 = chr(88), t_Comment = t_comment||'+Пересчёт НОБ' where t_clientid = cData.t_Client and t_convid = v_ConvID;

       END LOOP;
    END LOOP;
    --Вставки
    ? := v_ListNPTXOP.COUNT;
    IF v_ListNPTXOP.COUNT > 0
    THEN
      FORALL i IN v_ListNPTXOP.FIRST .. v_ListNPTXOP.LAST
        INSERT INTO DNPTXOP_DBT
             VALUES v_ListNPTXOP (i);

      v_ListNPTXOP.DELETE;
    END IF;

  END;
];

  SetOutHandler(NULL);

  cmd = RSDCommand(gSQL);
  
  cmd.addParam("", RSDBP_IN, ConvID);         /*1*/
  cmd.addParam("", RSDBP_IN, UniCodeStr); /*2*/
  cmd.addParam("", RSDBP_IN, ClientID); /*3*/
  cmd.addParam("cnt", RSDBP_OUT, V_INTEGER); /*4*/
 
//  result_protocol.AddString("Начало: "+time());
  InitProgress(-1, "Идёт генерация операций расчета НОБ", "Формирование операций расчета НОБ");

  cmd.execute();

  RemProgress();
 
  if (cmd.Value("cnt") == 0)
     result_protocol = "Нет записей для обработки";
  else
     result_protocol = "Создано "+cmd.Value("cnt")+" операций пересчета НОБ";
  end;
//  result_protocol.AddString("Конец: "+time());

  return true;

OnError(er)
  var err_text = "", delim = "";
  var j = 0;

  RemProgress();

  if(er and (er.err != NULL) and er.err.environment)
    while(j < er.err.environment.ErrorCount)
      err_text = err_text + delim + er.err.environment.error(j).descr;
      delim = "\n";
      j = j + 1;
    end;
    MsgBox(err_text);
    result_protocol = "Ошибка при создании операций пересчета ' " + err_text;

  else
    result_protocol = "Ошибка при создании операций пересчета ' " + er.Message;
    MsgBox(er.Message);
  end;

  return 0;
end;


private class TConvParm ()
  // private var fin  = TRecHandler( "fininstr.dbt" );
   private var pt   = TRecHandler( "party.dbt" );
   private class fi_item (_fin, _coef)
      var fin  = TRecHandler( "fininstr.dbt" );
      var coef = 1.0;
      fin = _fin;
      coef = _coef;
   end;
   var ConvDate = {curdate};

   //ClearRecord(fin);
   ClearRecord(pt);
   var fi_old = TRecHandler( "fininstr.dbt" );
   var fi_new = TArray(fi_item(TRecHandler( "fininstr.dbt" ), 1.0));
   var Party =  pt;
   var fi_new_count = 1;
   var SeanceID = 0;

   macro IncCountFi
     fi_new_count = fi_new_count + 1;
     fi_new[fi_new.size] = fi_item(TRecHandler( "fininstr.dbt" ), 1.0);
   end;

   macro DecCountFi
     fi_new_count = fi_new_count - 1;
   end;

   fi_old = TRecHandler( "fininstr.dbt" );
   private var i=0;
   for (i,0,fi_new_count-1)
      fi_new[fi_new.size] = fi_item(TRecHandler( "fininstr.dbt" ), 1.0);
   end;

end;

class (TRsbPanel) TPan (_Parm)
   private const FT_INT    = 0;
   private const FT_DOUBLE = 4;
   private const FT_STRING = 7;
   private const FT_DATE   = 9;

   var Parm = TConvParm();
   Parm = _Parm;

   private var m_EditField = TRsbEditField;
   private var m_Label = TRsbLabel;

   private macro IIF(a,b,c)
      if (a) return b; 
      else return c; 
      end;
   end;

   private macro makeArray():TArray
     var parm:variant, i:integer = 0, result:TArray = TArray( ParmCount(), ParmCount() );
     while( GetParm(i+1, parm) ) /* +1 нужен для работы внутри classa. В первом параметре экземпляр класса*/
       result.value(i) = parm;
       i = i + 1;
     end;
     return result;
   end;

   private macro _addCheckBox(str, col, name, def, label)
      var checkBox = TRsbCheckbox;
      checkBox.setPosition(col,str);
      checkBox.name = name;
      checkBox.checked = def;
      addControl(checkBox);
      var m_Label:TrsbLabel = TRsbLabel(col+8,str,label);
      addLabel (m_Label);
      return checkBox;
   end;

   private macro _addEditField(str, col, name, def, editable, type, w, h)
      var Dt = TRsbEditField(type);
      Dt.setPosition(col,str);
      Dt.setSize(w,h);
      Dt.name = name;
      Dt.value = def;
      Dt.editable = editable;
      return Dt;
   end;

   initTRsbPanel();
   var curstr = 0;
   Caption = "Конвертация";
   Status = "~ESC~ Выход ~Space~ Сбросить значение поля ~F3~ Выбор ~F2~ Выполнить ~F6~ История";

   var Tab1 = 2;
   var Tab2 = 15;

   setSize(58,8+Parm.fi_new_count*3);
   setPosition(8,1);

   /*           1         2         3         4         5          */
   /*  12345678901234567890123456789012345678901234567890123456789
      ╔═══════════════ Конвертация АДР ════════════════════════╗
    1 ║ Дата конвертации 15.02.2021                            ║
    2 ║                                                        ║
    3 ║ Старый выпуск [               ][                     ] ║
    4 ║ Новый выпуск  [               ][                     ] ║
    5 ║ Клиент        [               ][                     ] ║
    6 ║ Коэффициент   [               ]                        ║
    7 ║                                                        ║
    8 ║ Новый выпуск 2[               ][                     ] ║
    9 ║ Коэффициент 2 [               ]                        ║
      ╚════════════════════════════════════════════════════════╝
   */

   var eConvDate, eFi_old, eFi_name_old, eFi_new, eFi_name_new, eFi_new2 , eFi_name_new2, eClient, eClient_name, eCoef, eCoef2;

   /*1*/curstr = curstr+1;
   m_Label = null;
   m_Label = TRsbLabel(Tab1,curstr,"Дата конвертации");
   addLabel (m_Label);
   eConvDate = _addEditField(curstr, Tab2, "ConvDate", Parm.ConvDate, true, FT_DATE, 8, 1);
   addControl (eConvDate);
  
   /*2*/curstr = curstr+1;
   /*3*/curstr = curstr+1;
   m_Label = null;
   m_Label = TRsbLabel(Tab1,curstr,"Клиент");
   addLabel (m_Label);
   eClient = null;
   eClient= TRsbEditField(FT_STRING);
   eClient.setPosition(Tab2,curstr);
   eClient.setSize(15,1);
   eClient.enabled = true;
   eClient.name = "client";
   eClient.focusable = true;
   eClient.editable = true;
   eClient.TextLength = 50;
   if (Parm.Party.rec.PartyId > 0)
      eClient.value = ПолучитьКодСубъекта(Parm.Party.rec.partyid,1);
   else
      eClient.value = "Все";
   end;
   addControl(eClient);

   eClient_name = null;
   eClient_name= TRsbEditField(FT_STRING);
   eClient_name.setPosition(Tab2+17,curstr);
   eClient_name.setSize(22,1);
   eClient_name.enabled = false;
   eClient_name.name = "client_name";
   eClient_name.focusable = true;
   eClient_name.editable = false;
   eClient_name.TextLength = 50;
   if (Parm.Party.rec.PartyId > 0)
      eClient_name.value = SubStr(Parm.Party.rec.shortname,1,30);
   else
      eClient_name.value = "Все";
   end;
   addControl(eClient_name);


   /*4*/curstr = curstr+1;
   m_Label = null;
   m_Label = TRsbLabel(Tab1,curstr,"Старый выпуск");
   addLabel (m_Label);
   eFi_old = null;
   eFi_old= TRsbEditField(FT_STRING);
   eFi_old.setPosition(Tab2,curstr);
   eFi_old.setSize(15,1);
   eFi_old.enabled = true;
   eFi_old.name = "fi_old";
   eFi_old.focusable = true;
   eFi_old.editable = true;
   eFi_old.TextLength = 50;
   if (Parm.Fi_old.rec.FiId > 0)
      eFi_old.value = Parm.Fi_old.rec.Fi_code;
   else
      eFi_old.value = "";
   end;
   addControl(eFi_old);

   eFi_name_old = null;
   eFi_name_old= TRsbEditField(FT_STRING);
   eFi_name_old.setPosition(Tab2+17,curstr);
   eFi_name_old.setSize(22,1);
   eFi_name_old.enabled = false;
   eFi_name_old.name = "fi_name_old";
   eFi_name_old.focusable = true;
   eFi_name_old.editable = false;
   eFi_name_old.TextLength = 50;
   if (Parm.Fi_old.rec.FiId > 0)
      eFi_name_old.value = Parm.Fi_old.rec.Name;
   else
      eFi_name_old.value = "";
   end;
   addControl(eFi_name_old);

   var i = 0;

   for (i,0,Parm.fi_new_count-1)
      /*5*/curstr = curstr+1;
      /*6*/curstr = curstr+1;

      m_Label = null;
      m_Label = TRsbLabel(Tab1,curstr,"Новый выпуск "+string(i+1));
      addLabel (m_Label);
      eFi_new = null;
      eFi_new= TRsbEditField(FT_STRING);
      eFi_new.setPosition(Tab2,curstr);
      eFi_new.setSize(15,1);
      eFi_new.enabled = true;
      eFi_new.name = "fi_new"+string(i+1);
      eFi_new.focusable = true;
      eFi_new.editable = true;
      eFi_new.TextLength = 50;
      if (Parm.fi_new[i].fin.rec.fiid > 0)
         eFi_new.value = Parm.fi_new[i].fin.rec.fi_code;
      else
         eFi_new.value = "";
      end;
      addControl(eFi_new);

      eFi_name_new = null;
      eFi_name_new= TRsbEditField(FT_STRING);
      eFi_name_new.setPosition(Tab2+17,curstr);
      eFi_name_new.setSize(22,1);
      eFi_name_new.enabled = false;
      eFi_name_new.name = "fi_name_new"+string(i+1);
      eFi_name_new.focusable = true;
      eFi_name_new.editable = false;
      eFi_name_new.TextLength = 50;
      if (Parm.fi_new[i].fin.rec.fiid > 0)
         eFi_name_new.value = Parm.fi_new[i].fin.rec.name;
      else
         eFi_name_new.value = "";
      end;
      addControl(eFi_name_new);

      /*7*/curstr = curstr+1;
      m_Label = null;
      m_Label = TRsbLabel(Tab1,curstr,"Коэффициент "+string(i+1));
      addLabel (m_Label);
      eCoef = null;
      eCoef= TRsbEditField(FT_DOUBLE);
      eCoef.setPosition(Tab2,curstr);
      eCoef.setSize(15,1);
      eCoef.enabled = true;
      eCoef.name = "coef"+string(i+1);
      eCoef.focusable = true;
      eCoef.valuePrecision = 10;
      eCoef.value = Parm.fi_new[i].Coef;
      addControl(eCoef);
   end;

     /*8*/curstr = curstr+1;
     /*9*/curstr = curstr+1;
   m_Label = TRsbLabel(Tab1,curstr,"Добавить/удалить выпуск");
   addLabel (m_Label);

   var m_PushButtonAdd = TRsbPushButton(" + ");
   m_PushButtonAdd.setPosition(Tab2+7,curstr);
   m_PushButtonAdd.setSize(3,1);
   m_PushButtonAdd.name = "BtAdd";
   addControl(m_PushButtonAdd);
   var m_PushButtonRemove = TRsbPushButton(" - ");
   m_PushButtonRemove.setPosition(Tab2+7+4,curstr);
   m_PushButtonRemove.setSize(3,1);
   m_PushButtonRemove.name = "BtRemove";
   addControl(m_PushButtonRemove);

 
   addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "onKeyPressed"));
   addEventHandler(RSB_EV_REMOVE_FOCUS ,R2M(this, "onRemoveFocus"));
   //addEventHandler(RSB_EV_CONTROL_CHECKED,R2M(this,"OnChecked"));
   addEventHandler(RSB_EV_BUTTON_CLICKED,R2M(this,"OnClicked"));

   // SetFocus("ConvDate");
   // updateNavigationRoute();
   ReDraw();
 
   /***Конец рисования панели***/


   private macro InitOper(RetVal:@integer)
      RslDefCon.BeginTrans();
      var cmd = RsdCommand(" INSERT INTO u_conv_adr_dbt " +
                           "  VALUES (0,:0,:1,:2,NULL,:3,SYSDATE) returning t_id into :4; ");
      cmd.addParam("", RSDBP_IN, eConvDate.value);
      cmd.addParam("", RSDBP_IN, Parm.fi_old.rec.fiid);
      cmd.addParam("", RSDBP_IN, Parm.Party.rec.partyid);
      cmd.addParam("", RSDBP_IN, {oper});
      cmd.addParam("", RSDBP_OUT, V_INTEGER);
      cmd.Execute();
      RetVal = cmd.Value(4);

      var i=0;
      for (i,0,Parm.fi_new_count-1)
         cmd = RsdCommand(" INSERT INTO u_conv_adr_fi_dbt VALUES (?,?,?);");
         cmd.addParam("", RSDBP_IN, RetVal);
         cmd.addParam("", RSDBP_IN, Parm.fi_new[i].fin.rec.fiid);
         cmd.addParam("", RSDBP_IN, Parm.fi_new[i].coef);
         cmd.Execute();
      end;
      RslDefCon.CommitTrans();
      return 0;
   OnError(e)
      RslDefCon.RollbackTrans();
      return 1;
   end;

   private macro PrepareData(SeanceID)
      var cmd, sql = 
                          " INSERT INTO U_CONV_ADR_DATA_DBT SELECT t.t_dealid, " +
                          "       t.t_dealcode, " +
                          "       t.T_TAXOWNBEGDATE, "
                          "       t.t_clientid, " +
                          "       code101.t_code, " +
                          "       code109.t_code, " +
                          "       p.t_shortname, " +
                          "       ?, " +
                          "       t.t_pfi, " +
                          "       f.t_fi_code, " +
                          "       f.t_name, " +
                          "       l.t_principal, " +
                          "       L.T_COST, " +
                          "       l.t_price, " +
                          "       NVL(s.t_sum, 0), " +
                          "       l.t_cfi, " +
                          "       "+GetSQLDate(date(0,0,0))+", " + //T_TAXOWNBEGDATE_new
                          "       ?, " + //fiid
                          "       ?, " + //ficode
                          "       ?, " + //finame
                          "       0, " + //amount
                          "       0, " + //cost
                          "       0, " + //price
                          "       0, " + //outlay_rub
                          "       ?, " + //cfi  
                          "       SYSDATE, " +
                          "       ?, " +
                          "       CHR (0), " +
                          "       CHR (0), " +
                          "       CHR (1), ? , 0, ?" +
                          "  FROM dfininstr_dbt f, " +
                          "       dparty_dbt p, " +
                          "       ddlrq_dbt rq, " +
                          "       ddl_leg_dbt l, " +
                          "                ddl_tick_dbt t " +
                          "             LEFT JOIN " +
                          "                dobjcode_dbt code101 " +
                          "             ON     code101.t_objectid = t.t_clientid " +
                          "                AND code101.t_codekind = 101 " +
                          "                AND code101.t_objecttype = 3 " +
                          "                AND code101.t_state = 0 " +
                          "          LEFT JOIN " +
                          "             dobjcode_dbt code109 " +
                          "          ON     code109.t_objectid = t.t_clientid " +
                          "             AND code109.t_codekind = 109 " +
                          "             AND code109.t_objecttype = 3 " +
                          "             AND code109.t_state = 0 " +
                          "       LEFT JOIN " +
                          "          ddlsum_dbt s " +
                          "       ON     s.t_dockind = t.t_bofficekind " +
                          "          AND s.t_docid = t.t_dealid " +
                          "          AND s.t_kind = 1100 " +
                          " WHERE     t.t_bofficekind = rq.t_dockind " +
                          "       AND t.t_dealid = rq.t_docid " +
                          "       AND p.t_partyid = t.t_clientid " +
                          "       AND f.t_fiid = t.t_pfi " +
                          "       AND t.t_dealid = l.t_dealid " +
                          "       AND l.t_legkind = 0 " +
                          "       AND l.t_legid = 0 " +
                          "       AND t_bofficekind = 127 " +
                          "       AND ((? = 0) or (t.t_clientid = ?)) " +
                          "       AND t.t_pfi = ? " +
                          "       AND t.t_flag3 = CHR (88) " +
                          "       AND t.t_taxownbegdate <= ? " ;
      var i=0;
      for(i,0,Parm.fi_new_count-1)
         cmd = RsdCommand(sql);
         cmd.addParam("",RSDBP_IN, Parm.fi_new[i].Coef);
         cmd.addParam("",RSDBP_IN, Parm.fi_new[i].fin.rec.fiid);
         cmd.addParam("",RSDBP_IN, Parm.fi_new[i].fin.rec.fi_code);
         cmd.addParam("",RSDBP_IN, Parm.fi_new[i].fin.rec.name);
         cmd.addParam("",RSDBP_IN, Parm.fi_new[i].fin.rec.facevaluefi);
         cmd.addParam("",RSDBP_IN, {oper});
         cmd.addParam("",RSDBP_IN, SeanceID);
         cmd.addParam("",RSDBP_IN, Parm.fi_new[i].fin.rec.sumprecision);
         cmd.addParam("",RSDBP_IN, Parm.Party.rec.partyid);
         cmd.addParam("",RSDBP_IN, Parm.Party.rec.partyid);
         cmd.addParam("",RSDBP_IN, Parm.fi_old.rec.fiid);
         cmd.addParam("",RSDBP_IN, Parm.ConvDate);
         cmd.Execute();
      end;

      /*Наличие курсов на дату первоначального приобретения*/
      cmd = RsdCommand(
                      "UPDATE U_CONV_ADR_DATA_DBT  " +
                      "   SET t_comment = 'Нет курса на дату '|| to_char(t_taxownbegdate,'dd.mm.yyyy'), " +
                      "       t_flag1 = 'E' " +
                      " WHERE RSI_RSB_FIInstr.ConvSum (1,t_curprice,t_curprice_new,t_taxownbegdate) IS NULL " +
                      "       AND t_convid = ? " );
      cmd.addParam("",RSDBP_IN, SeanceID);
      cmd.Execute();

      /*Наличие лота по операции НДФЛ. Менять выпуск можно только по операции без лота*/
      cmd = RsdCommand(
                      "UPDATE U_CONV_ADR_DATA_DBT d " +
                      "   SET t_comment = 'По операции '||t_dealcode||' есть лот', " +
                      "       t_flag1 = 'E' " +
                      " WHERE t_flag1 != 'E' and exists (select 1 from dpmwrtsum_dbt l where l.t_dockind = 29 and l.t_docid = (select r.t_id from ddlrq_dbt r where r.t_dockind = 127 and r.t_docid = d.t_dealid)) " +
                      "       AND t_convid = ? " );
      cmd.addParam("",RSDBP_IN, SeanceID);
      cmd.Execute();

      /*Наличие выбытий по клиенту. Если были продажи или списания, то ндфл при такой конвертации не рассчитывается и конвертировать нельзя*/
      cmd = RsdCommand(
                      "UPDATE U_CONV_ADR_DATA_DBT d \n" +
                      "   SET t_comment = 'По выпуску  ' || t_fi_name || ' были выбытия', t_flag1 = 'E' \n" +
                      " WHERE t_flag1 != 'E' and EXISTS \n" +
                      "              (SELECT 1 \n" +
                      "                 FROM ddlrq_Dbt r, ddl_tick_dbt t \n" +
                      "                WHERE     r.t_party = d.t_clientid \n" +
                      "                      AND r.t_fiid = d.t_fiid \n" +
                      "                      AND t_kind = 1 \n" +
                      "                      AND t_state = 2 \n" +
                      "                      AND t_factdate <> TO_DATE ('01-01-0001', 'DD-MM-YYYY') \n" +
                      "                      AND t.t_dealid = R.T_DOCID and r.t_dockind = t.t_bofficekind \n" +
                      "                      and t.t_pfi = r.t_fiid \n" +
                      "                      and t.t_clientid = r.t_party) \n" +
                      "       AND t_convid = ? \n" );
      cmd.addParam("",RSDBP_IN, SeanceID);
      cmd.Execute();

      /*Дополнительный контроль зафиксированных на предыдущем шаге выбытий по клиенту. Если это списание, то при наличии в этой же дате зачисления на то же кол-во снимаем признак ошибки*/
      cmd = RsdCommand(String( "UPDATE U_CONV_ADR_DATA_DBT d   \n"
                              ,"   SET d.t_comment = CHR(1), d.t_flag1 = CHR(0)   \n"
                              ," where d.t_flag1 = 'E'   \n"
                              ,"   and instr(d.t_comment, 'выбытия') > 0   \n"
                              ,"   and not exists   \n"
                              ," (SELECT 1   \n"
                              ,"          FROM ddlrq_Dbt r   \n"
                              ,"         WHERE r.t_party = d.t_clientid   \n"
                              ,"           AND r.t_fiid = d.t_fiid   \n"
                              ,"           AND r.t_kind = 1   \n"
                              ,"           AND r.t_state = 2   \n"
                              ,"           AND r.t_factdate <> TO_DATE('01-01-0001', 'DD-MM-YYYY')   \n"
                              ,"           and 0 = case when r.t_dockind = 127 then (select count(1)   \n"
                              ,"                                                       from ddlrq_Dbt r1   \n"
                              ,"                                                      where r1.t_dockind = r.t_dockind   \n"
                              ,"                                                        and r1.t_kind = 0   \n"
                              ,"                                                        and r1.t_state = 2   \n"
                              ,"                                                        and r1.t_plandate = r.t_plandate   \n"
                              ,"                                                        and r1.t_fiid = r.t_fiid   \n"
                              ,"                                                        and r1.t_amount = r.t_amount)   \n"
                              ,"                 else 0 end)   \n"
                              ,"   and d.t_convid = ?   \n"));

      cmd.addParam("",RSDBP_IN, SeanceID);
      cmd.Execute();


      /*Расчет новых значений. Этими значениями затем обновим операции после подтверждения пользователя*/

      cmd = RsdCommand(
                      "UPDATE U_CONV_ADR_DATA_DBT " +
                      "   SET t_amount_new = round(t_amount * t_coef,t_sumprecision), " +
                      "       t_cost_new = round(RSI_RSB_FIInstr.ConvSum (t_cost,t_curprice,t_curprice_new,t_taxownbegdate),2), " +
                      "       t_price_new =RSI_RSB_FIInstr.ConvSum (t_price,t_curprice,t_curprice_new,t_taxownbegdate) / t_coef, " +
                      "       t_outlay_new = t_outlay, " +
                      "       t_taxownbegdate_new = ? " +
                      " WHERE RSI_RSB_FIInstr.ConvSum (1,t_curprice,t_curprice_new,t_taxownbegdate) IS NOT NULL " +
                      "       AND t_convid = ? " );
      cmd.addParam("",RSDBP_IN, eConvDate.value);
      cmd.addParam("",RSDBP_IN, SeanceID);
      cmd.Execute();


      /*дальнейшие действия только для Сплита*/
      if (Parm.fi_new_count > 1)
      /*Если выпусков больше чем 1, то идем от максимального коэфф, затраты разносим по коэфф  на тот выпуск, в котором коэфф больше. Но только если коэфф не больше 1*/
         var sql_avr = " select t_fiid_new, t_coef from u_conv_adr_fi_Dbt t where t_convid = ? and t_coef = (select max(t_coef) from u_conv_adr_fi_Dbt where t_convid = t.t_convid )";
         cmd = RsdCommand(sql_avr);
         cmd.addParam("",RSDBP_IN,SeanceID);
         var rs = RsdRecordSet(cmd);
         var fiid_max_coef = 0;
         /*обнулим все затраты кроме одного выпуска с макс коэф*/
         if (rs.movenext())
            fiid_max_coef = rs.Value("t_fiid_new");
            cmd = RsdCommand("update U_CONV_ADR_DATA_DBT set t_outlay_new = 0 where t_convid = ? and t_fiid_new <> ?");
            cmd.addParam("",RSDBP_IN, SeanceID);
            cmd.addParam("",RSDBP_IN, fiid_max_coef);
            cmd.Execute();
         end;
         /*если коэффициент >=1 , то на этом заканчиваем, т.к. все затраты уже распределены*/
         if (rs.Value("t_coef") < 1)
            /*а если нет, то сконвертируем затраты по коэфф для максимального коэфф*/
            cmd = RsdCommand("update U_CONV_ADR_DATA_DBT set " + 
                             " t_outlay_new = round(t_outlay * t_coef,2) " + 
                             " where t_convid = ? and t_fiid_new = ?");
            cmd.addParam("",RSDBP_IN, SeanceID);                        
            cmd.addParam("",RSDBP_IN, fiid_max_coef);
            cmd.Execute();
            /*остаток сбрасываем на следующий коэф. он точно будет меньше первого. Остальные (если выпусков больше двух) останутся с нулями*/
            sql_avr = " select t_fiid_new, t_coef from u_conv_adr_fi_Dbt t where t_convid = ? and " + 
                      " t_coef = (select max(t_coef) from u_conv_adr_fi_Dbt where t_convid = t.t_convid  and t_fiid_new <> ? )";
            cmd = RsdCommand(sql_avr);
            cmd.addParam("",RSDBP_IN, SeanceID);
            cmd.addParam("",RSDBP_IN, fiid_max_coef);
            rs = RsdRecordSet(cmd);
            if (rs.movenext())
               cmd = RsdCommand("update U_CONV_ADR_DATA_DBT t set t_outlay_new = NVL((select round(t_outlay - t_outlay_new,2) from U_CONV_ADR_DATA_DBT  " + 
                                "                                                      where t_convid = t.t_convid and t_fiid_new = ? and t_dealid = t.t_dealid), 0) " +
                                " where t_convid = ? and t_fiid_new = ?");
               cmd.addParam("",RSDBP_IN, fiid_max_coef);
               cmd.addParam("",RSDBP_IN,SeanceID);
               cmd.addParam("",RSDBP_IN,rs.Value("t_fiid_new"));
               cmd.Execute();
            end;
         end;


         /*стоимость и цену сначала рассчитаем исходя из общего коэффицента и поделим пропорционально количеству бумаг*/
         /* при суммарном коэф = 1 делать ничего не надо. А если суммарный != 1, то нужно на всякий случай скорректировать стоиость и цены */
         sql_avr = " select sum(t_coef) sc from U_CONV_ADR_FI_DBT t where t_convid = ?  ";
         cmd = RsdCommand(sql_avr);
         cmd.addParam("",RSDBP_IN,SeanceID);
         rs = RsdRecordSet(cmd);
         var sum_coef = 0;
         if (rs.movenext())
            sum_coef = rs.Value("sc");
         end;

         cmd = RsdCommand(
                         "UPDATE U_CONV_ADR_DATA_DBT " +
                         "   SET  " +
                         "       t_cost_new = round(RSI_RSB_FIInstr.ConvSum (t_cost * t_coef,t_curprice,t_curprice_new,t_taxownbegdate) /  ? ,2) , " +
                         "       t_price_new =RSI_RSB_FIInstr.ConvSum (t_price,t_curprice,t_curprice_new,t_taxownbegdate) / ?  " +
                         " WHERE RSI_RSB_FIInstr.ConvSum (1,t_curprice,t_curprice_new,t_taxownbegdate) IS NOT NULL " +
                         "       AND t_convid = ? " );
         cmd.addParam("",RSDBP_IN, sum_coef);
         cmd.addParam("",RSDBP_IN, sum_coef);
         cmd.addParam("",RSDBP_IN, SeanceID);
         cmd.Execute();

      end;
   end;


   /*Собственно сама конвертация*/
   private macro UpdateDeals(rs, msg:@string);
      var sql = "", n=0,c=0,e=0, s=0, cmd;
      var Dt, BoardID;
      private macro UpdateDeal()
         var cmd;
         /*DDL_TICK_DBT - замены выпуска*/
         cmd = RsdCommand("update ddl_tick_dbt set t_pfi = ? where  t_dealid = ? " );
         cmd.addParam ("", RSDBP_IN, Dt.fiid_new);
         cmd.addParam ("", RSDBP_IN, Dt.dealid);
         cmd.Execute();

         /*DDL_LEG_DBT - замена выпуска, обновление количества, стоимости, цены, валюты*/
         cmd = RsdCommand("update ddl_leg_dbt set t_pfi = ?, t_deliveringfiid = ?, t_principal = ?, t_price = ?, t_cost = ?, t_totalcost = ?, t_cfi = ?  where  t_dealid = ? and t_legkind = 0 and t_legid = 0" );
         cmd.addParam ("", RSDBP_IN, Dt.fiid_new);
         cmd.addParam ("", RSDBP_IN, Dt.fiid_new);
         cmd.addParam ("", RSDBP_IN, Dt.amount_new);
         cmd.addParam ("", RSDBP_IN, Dt.price_new);
         cmd.addParam ("", RSDBP_IN, Dt.cost_new);
         cmd.addParam ("", RSDBP_IN, Dt.cost_new);
         cmd.addParam ("", RSDBP_IN, Dt.curprice_new);
         cmd.addParam ("", RSDBP_IN, Dt.dealid);
         cmd.Execute();

         /*DDLRQ_DBT - замена выпуска и обновление количества в т/о*/
         cmd = RsdCommand("update ddlrq_dbt set t_fiid = ?, t_amount = ? where  t_dockind = 127 and t_docid = ? and t_dealpart = 1 and t_type = 8 " );
         cmd.addParam ("", RSDBP_IN, Dt.fiid_new);
         cmd.addParam ("", RSDBP_IN, Dt.amount_new);
         cmd.addParam ("", RSDBP_IN, Dt.dealid);
         cmd.Execute();

         /* DDLGRDEAL_DBT - замены выпуска в графике сделки */
         cmd = RsdCommand("update DDLGRDEAL_DBT set t_fiid = ? where  t_dockind = 127 and t_docid = ? and t_fiid = ? " );
         cmd.addParam ("", RSDBP_IN, Dt.fiid_new);
         cmd.addParam ("", RSDBP_IN, Dt.dealid);
         cmd.addParam ("", RSDBP_IN, Dt.fiid);
         cmd.Execute();

         /*DDLSUM_DBT - затраты и данные НУ*/
         /*1100 - затраты(панель сделки) - Значение может меняться только при сплите */
         /*1220 - цена                     */
         /*1230 - стоимость                */
         /*1240 - нкд                    - конвертируем АДР в акции. НКД нет и обновлять его не требуется  */
         /*1250 - затраты (панель НУ)    - Значение может меняться только при сплите   */
         cmd = RsdCommand("update DDLSUM_DBT set t_sum = ?, t_currency = ? where  t_dockind = 127 and t_docid = ? and t_kind = ? " );
         cmd.addParam ("", RSDBP_IN, Dt.price_new);
         cmd.addParam ("", RSDBP_IN, Dt.curprice_new);
         cmd.addParam ("", RSDBP_IN, Dt.dealid);
         cmd.addParam ("", RSDBP_IN, 1220 /*PRICE*/);
         cmd.Execute();

         cmd = RsdCommand("update DDLSUM_DBT set t_sum = ?, t_currency = ? where  t_dockind = 127 and t_docid = ? and t_kind = ? " );
         cmd.addParam ("", RSDBP_IN, Dt.cost_new);
         cmd.addParam ("", RSDBP_IN, Dt.curprice_new);
         cmd.addParam ("", RSDBP_IN, Dt.dealid);
         cmd.addParam ("", RSDBP_IN, 1230 /*COST*/);
         cmd.Execute();

         cmd = RsdCommand("update DDLSUM_DBT set t_sum = ?, t_currency = ? where  t_dockind = 127 and t_docid = ? and t_kind in (?,?) " );
         cmd.addParam ("", RSDBP_IN, Dt.outlay_new);
         cmd.addParam ("", RSDBP_IN, 0);
         cmd.addParam ("", RSDBP_IN, Dt.dealid);
         cmd.addParam ("", RSDBP_IN, 1100 /*OUTLAY*/);
         cmd.addParam ("", RSDBP_IN, 1250 /*OUTLAY*/);
         cmd.Execute();

         cmd = RsdCommand("update ddl_tick_dbt set t_taxownbegdate = ? where  t_dealid = ? " );
         cmd.addParam ("", RSDBP_IN, eConvDate.value);
         cmd.addParam ("", RSDBP_IN, Dt.dealid);
         cmd.Execute();

      OnError(e)
         msg = e.Message + " " + e.Module + " " + e.Line;
         AbortTrn;
      end;

      private macro runDeals(SeanceID)
         Var cmd, DataSet, er, cmd1;
         var ArrDeal = tarray(),recs = 0, errrecs = 0, RunDate = {curdate}, documentid, i = 0;

         EndAction(0);

         var nCount :integer = 0;
         var nSql = "select t_dealid_new from U_CONV_ADR_DATA_DBT where t_convid = " + string(SeanceID);
         var nCmd = Dl_RsDCommand();

         var nRecs = nCMD.GetCount(nSql);
         nCount = nRecs;

         cmd1 = RSDCommand(  " update doprostep_dbt set t_notinuse = 'X' where T_BLOCKID in(201000, 201100) and t_number_step = 40 " );
         cmd1.execute();

         cmd = RsdCommand (nSql);
         DataSet = RsdRecordSet(cmd);
         
         while (DataSet.movenext())
            ArrDeal[recs] = DataSet.value("t_dealid_new");
            recs = recs + 1;
         end;
         endaction(0);

         if(SP_ExecDeal(ArrDeal, false, RunDate) != 0)
            errrecs = errrecs + 1;
         end;
         endaction(0);
         cmd1 = RSDCommand(  " update doprostep_dbt set t_notinuse = chr(0) where T_BLOCKID in(201000, 201100) and t_number_step = 40 " );
         cmd1.execute();


         /*Закрытие Графиков*/
         if(errrecs == 0)

            var sql_100 = String( "DECLARE\n" 
                                 ,"    n     NUMBER(10)  := 0;\n"  
                                 ,"BEGIN\n"  
                                 ,"   FOR i\n"  
                                 ,"      IN (SELECT ac.t_id\n"  
                                 ,"            FROM ddl_tick_dbt tick,\n"  
                                 ,"                 U_CONV_ADR_DATA_DBT op,\n"  
                                 ,"                 ddlgrdeal_dbt deal,\n"  
                                 ,"                 ddlgracc_dbt ac\n"  
                                 ,"           WHERE     tick.t_bofficekind = 127\n"  
                                 ,"                 AND tick.t_dealstatus = 10                           /*!=20*/\n"  
                                 ,"                 AND tick.t_dealdate <= TRUNC (SYSDATE)\n"  
                                 ,"                 AND tick.t_dealid = op.t_dealid_new\n"  
                                 ,"                 AND deal.t_dockind = tick.t_bofficekind\n"  
                                 ,"                 AND deal.t_docid = tick.t_dealid\n"  
                                 ,"                 AND ac.t_grdealid = deal.t_id\n"  
                                 ,"                 AND ac.t_state = 1\n"  
                                 ,"                 AND ac.t_accnum > 1)\n"  
                                 ,"   LOOP\n"  
                                 ,"      UPDATE ddlgracc_dbt\n"  
                                 ,"         SET t_state = 2\n"  
                                 ,"       WHERE t_id = i.t_id;\n"  
                                 ,"      n := n + 1;\n"  
                                 ,"      IF (MOD (n, 100) = 0)\n"  
                                 ,"      THEN\n"  
                                 ,"         COMMIT;\n"  
                                 ,"      END IF;\n"  
                                 ,"   END LOOP;\n"  
                                 ,"   commit;\n"  
                                 ,"END;");
            cmd1 = RSDCommand(sql_100);
            cmd1.execute();
         end;

         Remprogress();
         return true;

      OnError(er)
         return false;
      End;


      private macro CreateNdflDeal()

         Private Macro CheckOperNo(OperNo)
            var cnt = 1;
            var sql = " select 1 from ddl_tick_dbt tick where tick.t_bofficekind = 127 and RSB_SECUR.IsAvrWrtIn(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) > 0 and tick.t_DealCode = ? ";
            var cmd = RsdCommand(sql);
            cmd.AddParam("", RSDBP_IN, OperNo);
            var rs = RsdRecordSet(cmd);
            if (rs.movenext())
               cmd = RsdCommand(sql);
               cmd.AddParam("", RSDBP_IN, OperNo+"("+string(cnt)+")");
               rs = RsdRecordSet(cmd);
               while (rs.movenext())
                  cnt = cnt+1;
                  cmd = RsdCommand(sql);
                  cmd.AddParam("", RSDBP_IN, OperNo+"("+string(cnt)+")");
                  rs = RsdRecordSet(cmd);
               end;
               return OperNo+"("+string(cnt)+")";
            else
               return OperNo;
            end;
         End;


         var  stat = 0;

         var cmd, tick_old;
         var DealCode;

         cmd = RsdCommand(" select t.t_DealCode, t.t_dealdate, t.t_DealTime, t.t_ClientID, t.t_ClientContrID, l.t_Point, l.t_DepositID, t.t_TaxOwnBegDate, " +
                          "        RSI_NPTO.GetDateFromAvrWrtIn(t.t_DealID, t.t_DealDate, t.t_DealDate) as DateBegin"
                          "   from ddl_tick_dbt t, ddl_leg_dbt l " + 
                          "  where t.t_dealid = ? and t.t_dealid = l.t_dealid and l.t_legid = 0 and l.t_legkind = 0 ");
         cmd.addParam("" ,RSDBP_IN, Dt.DealID);
         tick_old = RsdRecordSet(cmd);
         if (tick_old.movenext())
            DealCode = CheckOperNo(tick_old.value("t_DealCode"));
            stat = CreateDepoBoardWithPrice (
                                   DealCode, 
                                   SQL_ConvTypeDate(tick_old.value("t_DealDate")), 
                                   tick_old.value("t_Dealtime"),
                                   tick_old.value("t_ClientID"),
                                   tick_old.value("t_ClientContrID"),
                                   Dt.Value("t_fiid_new"),
                                   Dt.Value("t_amount_new"), 
                                   tick_old.Value("t_point"),
                                   tick_old.Value("T_DepositID"),
                                   false /*IsSingle*/, 1, 
                                   Dt.Value("t_CurPrice_new"),  
                                   Dt.Value("t_Price_new"), 
                                   Dt.Value("t_cost_new"), 
                                   0/*NKD !!!*/, 
                                   Dt.Value("t_OutLay_new"), 
                                   SQL_ConvTypeDate(tick_old.Value("DateBegin")),
                                   @msg, @BoardID
                                    );

            if (stat == 0)
               if( ( not DisconnectObjAttr(OBJTYPE_SECDEAL,  string(BoardID:o:34), OBJGROUP_VTBNDFL) ) OR
                     ( not ConnectObjAttr(stat, OBJTYPE_SECDEAL, string(BoardID:o:34), OBJGROUP_VTBNDFL, 1, null, null, {curdate}) )  OR
                 ( stat != 0 ))
                  //msg = "Не удалось установить категорию 'Зачисления ВТБ для НДФЛ'";
                  //todo добавление категории нужно внести внутрь транзакции
               end;
            end;

            cmd = RsdCommand("update ddl_tick_dbt set t_taxownbegdate = ? where  t_dealid = ? " );
            cmd.addParam ("", RSDBP_IN, SQL_ConvTypeDate(Dt.Value("t_TaxOwnBegDate_new")));
            cmd.addParam ("", RSDBP_IN, BoardID);
            cmd.Execute();

            cmd = RsdCommand("update DDLSUM_DBT set t_date = ? where t_dockind = 127 and t_docid = ? and t_kind in (1220, 1230, 1240, 1250) " );
            cmd.addParam ("", RSDBP_IN, SQL_ConvTypeDate(tick_old.Value("DateBegin")));
            cmd.addParam ("", RSDBP_IN, BoardID);
            cmd.Execute();

         end;
         return stat;
      end;

      /*Если выпусков больше чем 1, то идем от максимального коэфф*/
      var sql_avr = " select t_fiid_new, t_coef from u_conv_adr_fi_Dbt t where t_convid = ? and t_coef = (select max(t_coef) from u_conv_adr_fi_Dbt where t_convid = t.t_convid )";
      cmd = RsdCommand(sql_avr);
      cmd.addParam("",RSDBP_IN, rs.value("t_convid"));
      var rs1 = RsdRecordSet(cmd);
      var fiid_max_coef = 0;
      if (rs1.movenext())
         fiid_max_coef = rs1.Value("t_fiid_new");
      end;
      if (Parm.fi_new_count > 1)
         SP_BeginInsertBoardLot ();
         /*Если выпусков больше одного, то по всем выпускам, кроме выпуска с максимальным коэфф. */
         sql = " select * from U_CONV_ADR_DATA_DBT where t_flag1 <> 'E' and t_flag1 <> chr(88) and t_fiid_new <> "+ string(fiid_max_coef) +" and t_convid = " + rs.value("t_convid");
         n = SQL_GetNRecs(sql);
         InitProgress(n, "Создание новых НДФЛ-зачислений");
         Dt = TRsbDataSet(sql);
         while (Dt.movenext())
            msg = "";
            if (CreateNdflDeal() == 0)
              cmd = RsdCommand("update U_CONV_ADR_DATA_DBT set t_flag1 = chr(88), t_comment = 'Выполнено', t_dealid_new = ? where t_convid = ? and t_dealid = ? and t_fiid_new = ?" );
              cmd.addParam ("", RSDBP_IN, BoardID);
              cmd.addParam ("", RSDBP_IN, Dt.convid);
              cmd.addParam ("", RSDBP_IN, Dt.dealid);
              cmd.addParam ("", RSDBP_IN, Dt.fiid_new);
              cmd.Execute();
              s = s+1;
            else
              cmd = RsdCommand("update U_CONV_ADR_DATA_DBT set t_flag1 = 'E', t_comment = ? where t_convid = ? and t_dealid = ?  and t_fiid_new = ?" );
              cmd.addParam ("", RSDBP_IN, SubStr(msg,1,200));
              cmd.addParam ("", RSDBP_IN, Dt.convid);
              cmd.addParam ("", RSDBP_IN, Dt.dealid);
              cmd.addParam ("", RSDBP_IN, Dt.fiid_new);
              cmd.Execute();
              e=e+1;
            end;
            UseProgress(c=c+1);
         end;
         RemProgress;
         SP_EndInsertBoardLot ();

         if ((Parm.fi_new_count > 1) and (c > 0) )
            if (not runDeals (rs.value("t_convid")) )
               msg = msg + "| Error! Не удалось закрыть НДФЛ зачисления, необходимо провести закрытие вручную, либо откатить ";
            end;
         end;

      end;
      sql = " select * from U_CONV_ADR_DATA_DBT where t_flag1 <> 'E' and t_flag1 <> chr(88) and t_fiid_new = "+fiid_max_coef+" and t_convid = " + rs.value("t_convid");
      n = SQL_GetNRecs(sql);
      if (n > 0)
         c=s=e=0;
         InitProgress(n, "Выполнение конвертации");
         Dt = TRsbDataSet(sql);
         while (Dt.movenext())
            msg = "";

            if (ProcessTrn(0, "UpdateDeal"))
              cmd = RsdCommand("update U_CONV_ADR_DATA_DBT set t_flag1 = chr(88), t_comment = 'Выполнено' where t_convid = ? and t_dealid = ?  and t_fiid_new = ?" );
              cmd.addParam ("", RSDBP_IN, Dt.convid);
              cmd.addParam ("", RSDBP_IN, Dt.dealid);
              cmd.addParam ("", RSDBP_IN, Dt.fiid_new);
              cmd.Execute();
              s = s+1;
            else
              cmd = RsdCommand("update U_CONV_ADR_DATA_DBT set t_flag1 = 'E', t_comment = ? where t_convid = ? and t_dealid = ?  and t_fiid_new = ?" );
              cmd.addParam ("", RSDBP_IN, SubStr(msg,1,200));
              cmd.addParam ("", RSDBP_IN, Dt.convid);
              cmd.addParam ("", RSDBP_IN, Dt.dealid);
              cmd.addParam ("", RSDBP_IN, Dt.fiid_new);
              cmd.Execute();
              e=e+1;
            end;
            UseProgress(c=c+1);
         end;
         RemProgress;
         msg = "Обработано всего: "+string(c) + "|Успешно:" +string(s) + "|Ошибок:" + string(e);
      else
         msg = "Нет записей для обработки";
      end;

   end;

   private macro SetState(rs, Symb)
      var cmd = RsdCommand(" update U_CONV_ADR_DATA_DBT set t_flag1 = ? where t_convid = ? and t_dealid = ? and t_fiid_new = ? ");
      cmd.addParam("", RSDBP_IN, Symb);
      cmd.addParam("", RSDBP_IN, rs.value("t_convid"));
      cmd.addParam("", RSDBP_IN, rs.value("t_dealid"));
      cmd.addParam("", RSDBP_IN, rs.value("t_fiid_new"));
      cmd.Execute();
      return true;
   OnError(e)
      return false;
   end;


   private macro ClearState(rs)
      var cmd = RsdCommand(" update U_CONV_ADR_DATA_DBT set t_flag1 = chr(0) where t_convid = ? and t_dealid = ? and t_fiid_new = ? ");
      cmd.addParam("", RSDBP_IN, rs.value("t_convid"));
      cmd.addParam("", RSDBP_IN, rs.value("t_dealid"));
      cmd.addParam("", RSDBP_IN, rs.value("t_fiid_new"));
      cmd.Execute();
      return true;
   OnError(e)
      return false;
   end;


   private macro ShowList(SeanceID)
      var inprocess, inprocessprogress;
      macro eRunConvProcessDialog(rs, cmd, id, key)
        var msg = "", err="";
        if (cmd == DLG_INIT)
           AddMultiAction(rs, 259/*Ctrl+2*/);
           AddMultiAction(rs, 260/*Ctrl+3*/);
        elif(cmd == DLG_KEY)
 message(key);
           if(key == 27)       /*Esc*/
              return CM_CANCEL;
           elif(key == 316)      /*F2*/
              UpdateDeals(rs, @msg);
              msgbox(msg);
              return CM_SELECT;
           elif(key == 361)      /*Alt+F2*/
              CreateNptxopRecalc(rs.value("t_convid"),
                            -1,
                            "CONV", //Часть кода для операций
			    @err, @msg
                           );

              msgbox(err +"|"+msg);
              return CM_SELECT;
           elif(key == 18/*Ctrl+R*/)
              return CM_SELECT;
           elif(key == 259)  /*Ctrl+2*/
             if(msgboxex("Установить статус записей в 'E' для пропуска обработки? ", MB_YES + MB_NO, IND_YES) == IND_YES)
                SetState(rs, "E");
                return CM_SELECT;
             end;
           elif(key == 260)  /*Ctrl+3*/
             if(msgboxex("Сбросить статус для  обработки записей?", MB_YES + MB_NO, IND_YES) == IND_YES)
                ClearState(rs);
                return CM_SELECT;
             end;
           end;
        elif(cmd == DLG_MSELSTART)
          if(key == 259)  /*Ctrl+2*/
             if(msgboxex("Установить статус записей в 'E' для пропуска обработки? ", MB_YES + MB_NO, IND_YES) == IND_YES)
               inprocess = true;
               inprocessprogress = 0;
               InitProgress(int(GetMultiCount()), "Ждите...", "Смена статуса");
             end;
          elif(key == 260)  /*Ctrl+3*/
             if(msgboxex("Сбросить статус для  обработки записей?", MB_YES + MB_NO, IND_YES) == IND_YES)
               inprocess = true;
               inprocessprogress = 0;
               InitProgress(int(GetMultiCount()), "Ждите...", "Смена статуса");
             end;
           end;
        elif(cmd == DLG_MSELEND)
         if(key == 259)  /*Ctrl+2*/
            if(inprocess)
               inprocess = false;
               RemProgress();
               msgboxex("Статус изменен. Обновите список по Ctrl+R");
               updatescroll(rs, 5);
               return CM_SELECT;
            end;
          elif(key == 260)  /*Ctrl+3*/ 
            if(inprocess)
               inprocess = false;
               RemProgress();
               msgboxex("Статус изменен. Обновите список по Ctrl+R");
               updatescroll(rs, 5);
               return CM_SELECT;
            end;
          end;
        elif(cmd == DLG_MSEL)
          if(key == 259)  /*Ctrl+2*/
             SetState(rs, "E");
             UseProgress(inprocessprogress = inprocessprogress + 1);
             updatescroll(rs, 5);
             return CM_MSEL_CONT_CLEAR;
          elif(key == 260)  /*Ctrl+3*/
              ClearState(rs);
             UseProgress(inprocessprogress = inprocessprogress + 1);
             updatescroll(rs, 5);
             return CM_MSEL_CONT_CLEAR;
          end;
        end;
      end;

      var cmdQuery = " select  " + 
                          "t_dealid," +             
                          "t_dealcode," +     
                          "t_taxownbegdate," +     
                          "t_clientcode_abs," +
                          "t_clientcode_vtb,"           
                          "t_clientname," +              
                          "t_fiid," +                    
                          "t_fi_code," +                 
                          "t_fi_name," +                 
                          "t_amount," +                  
                          "t_cost," +                    
                          "t_price," +                   
                          "t_outlay," +                  
                          "(select t_ccy from dfininstr_dbt where t_fiid = t_curprice) t_curprice," +                
                          "t_coef,"
                          "t_taxownbegdate_new," + 
                          "t_fiid_new," +                
                          "t_fi_code_new," +             
                          "t_fi_name_new," +             
                          "t_amount_new," +              
                          "t_cost_new," +                
                          "t_price_new," +               
                          "t_outlay_new," +              
                          "(select t_ccy from dfininstr_dbt where t_fiid = t_curprice_new) t_curprice_new," +  
                          " RSI_RSB_FIInstr.ConvSum (1,t_curprice,t_curprice_new,t_taxownbegdate) rate, " + 
                          "t_comment, t_flag1, t_flag2, '|' delim, t_convid, t_dealid_new from U_CONV_ADR_DATA_DBT where t_convid = ? order by decode(t_flag1,'E',1,chr(0), 2, 'X', 99), t_clientname, t_dealid, t_fiid_new ";                 
      var cmdScroll = RsdCommand(cmdQuery);
      cmdScroll.AddParam("", RSDBP_IN, SeanceID);
      var rsScroll = RSDRecordset(cmdScroll, RSDVAL_CLIENT, RSDVAL_STATIC);
      var Ar = MakeArray("t_dealid",         "ID",              7,  2, -1, 0,
                         "t_dealcode",       "Код операции",    10, 2, -1, 0,  
                         "t_taxownbegdate",  "Дата приоб.",     10, 2, -1, 0,
                         "t_comment",        "Комментарий",     20, 2, -1, 0,
                         "t_flag1",          "Статус",          1 , 2, -1, 0,
                         "t_flag2",          "Пересчет НОБ",          1 , 2, -1, 0,
                         "t_clientcode_abs", "Код клиента АБС", 12, 2, -1, 0, 
                         "t_clientcode_vtb", "Код клиента ВТБ", 8,  2, -1, 0, 
                         "t_clientname",     "Наименование",    20, 2, -1, 0, 
                         "t_fiid",           "ФИ ID",           5,  2, -1, 0,
                         "t_fi_code",        "Код ФИ",          10, 2, -1, 0,
                         "t_fi_name",        "Наименование ФИ", 15, 2, -1, 0,
                         "t_amount",         "Количество",      10, 2,  8, 0,
                         "t_cost",           "Стоимость",       10, 2, -1, 0,
                         "t_price",          "Цена",            10, 2,  8, 0,
                         "t_outlay",         "Затраты, руб",    10, 2, -1, 0,
                         "t_curprice",       "Валюта цены",     3,  2, -1, 0,
                         "delim",            "",                1,  2, -1, 0,
                         "t_coef",           "Коэффициент",     8,  2,  8, 0,
                         "t_taxownbegdate_new","nДата приоб.",  10, 2, -1, 0,
                         "rate",             "Курс ",           8,  2,  6, 0,
                         "t_fiid_new",       "nФИ ID  ",        5,  2, -1, 0,
                         "t_fi_code_new",    "nКод ФИ",         10, 2, -1, 0,
                         "t_fi_name_new",    "nНаименование ФИ",15, 2, -1, 0,
                         "t_amount_new",     "nКоличество",     10, 2,  8, 0,
                         "t_cost_new",       "nСтоимость",      10, 2, -1, 0,
                         "t_price_new",      "nЦена",           10, 2,  8, 0,
                         "t_outlay_new",     "nЗатраты, руб",   10, 2, -1, 0,
                         "t_curprice_new",   "nВалюта цены",    3,  2, -1, 0,
                         "t_dealid_new",   "nID сплит",    5,  2, -1, 0
                          );
      while(RunScroll(rsScroll, ar.size/6, Ar, "eRunConvProcessDialog", "eRunConvProcessDialog", "Конвертация", "~F2~ Обработать все записи ~Ctrl+2~ Установить статус 'E' ~Ctrl+3~ Сбросить статус 'E' ~Esc~ Выход ~Ctrl+Shift+F11~ Выгрузить в Excel", true))

        cmdScroll = RsdCommand(cmdQuery);
        cmdScroll.AddParam("", RSDBP_IN, SeanceID);
        rsScroll = RSDRecordset(cmdScroll, RSDVAL_CLIENT, RSDVAL_STATIC);
      end;

   end;

   private macro ShowHist()

      var inprocess, inprocessprogress;
      macro eRunConvHistProcessDialog(rs, cmd, id, key)
        var msg = "";
        if (cmd == DLG_INIT)
        elif(cmd == DLG_KEY)
           if(key == 27)       /*Esc*/
              return CM_CANCEL;
           elif(key == 18/*Ctrl+R*/)
              return CM_SELECT;
           elif(key == 13)       /*Enter*/
              ShowList(rs.value("t_id"));
           end;
        end;
      end;

      var cmdQuery = 
                     "SELECT t.t_id, \n" +
                     "         t_convdate, \n" +
                     "         CASE \n" +
                     "            WHEN EXISTS \n" +
                     "                    (SELECT 1 \n" +
                     "                       FROM U_CONV_ADR_DATA_DBT \n" +
                     "                      WHERE t_convid = t.t_id AND t_flag1 = CHR (88)) THEN 'X' \n" +
                     "            ELSE CHR (0) \n" +
                     "         END \n" +
                     "            executed, \n" +
                     "         CASE \n" +
                     "            WHEN EXISTS \n" +
                     "                    (SELECT 1 \n" +
                     "                       FROM U_CONV_ADR_DATA_DBT \n" +
                     "                      WHERE t_convid = t.t_id AND t_flag2 <> CHR (0)) THEN 'X' \n" +
                     "            ELSE CHR (0) \n" +
                     "         END \n" +
                     "            created_nob, \n" +
                     "         t.t_fiid, \n" +
                     "         f.t_name, \n" +
                     "         a.t_isin, \n" +
                     "         CASE \n" +
                     "            WHEN fi.cnt = 1 THEN (SELECT TO_CHAR (t_coef) \n" +
                     "                                    FROM u_conv_adr_fi_dbt \n" +
                     "                                   WHERE t_convid = t.t_id) \n" +
                     "            ELSE 'Несколько' \n" +
                     "         END \n" +
                     "            t_coef, \n" +
                     "         CASE \n" +
                     "            WHEN fi.cnt = 1 THEN (SELECT TO_CHAR (t_fiid_new) \n" +
                     "                                    FROM u_conv_adr_fi_dbt \n" +
                     "                                   WHERE t_convid = t.t_id) \n" +
                     "            ELSE 'Несколько' \n" +
                     "         END \n" +
                     "            t_fiid_new, \n" +
                     "         CASE \n" +
                     "            WHEN fi.cnt = 1 \n" +
                     "            THEN \n" +
                     "               (SELECT t_name \n" +
                     "                  FROM u_conv_adr_fi_dbt, dfininstr_dbt \n" +
                     "                 WHERE t_convid = t.t_id AND t_fiid = t_fiid_new) \n" +
                     "            ELSE \n" +
                     "               'Несколько' \n" +
                     "         END \n" +
                     "            t_name_new, \n" +
                     "         CASE \n" +
                     "            WHEN fi.cnt = 1 \n" +
                     "            THEN \n" +
                     "               (SELECT t_isin \n" +
                     "                  FROM u_conv_adr_fi_dbt, davoiriss_dbt \n" +
                     "                 WHERE t_convid = t.t_id AND t_fiid = t_fiid_new) \n" +
                     "            ELSE \n" +
                     "               'Несколько' \n" +
                     "         END \n" +
                     "            t_isin_new, \n" +
                     "         t.t_oper, \n" +
                     "         (SELECT t_name \n" +
                     "            FROM dperson_dbt \n" +
                     "           WHERE t_oper = t.t_oper) \n" +
                     "            t_opername, \n" +
                     "         to_char(t_sysdate,'dd.mm.yyyy hh24:mi:ss') t_sysdate \n" +
                     "    FROM U_CONV_ADR_DBT t \n" +
                     "         JOIN dfininstr_dbt f \n" +
                     "            ON f.t_fiid = t.t_fiid \n" +
                     "         JOIN davoiriss_dbt a \n" +
                     "            ON f.t_fiid = a.t_fiid \n" +
                     "         JOIN (  SELECT t_convid, COUNT (1) cnt \n" +
                     "                   FROM U_CONV_ADR_FI_DBT \n" +
                     "               GROUP BY t_convid) fi \n" +
                     "            ON fi.t_convid = t.t_id \n" +
                     "ORDER BY t_id DESC \n" ;

                 
      var cmdScroll = RsdCommand(cmdQuery);
      //cmdScroll.AddParam("", RSDBP_IN, SeanceID);
      var rsScroll = RSDRecordset(cmdScroll, RSDVAL_CLIENT, RSDVAL_STATIC);
      var Ar = MakeArray("t_id",        "ID",        5,  2, -1, 0,
                         "t_convdate",  "Дата",      10, 2, -1, 0,  
                         "executed",    "Исполнение",2, 2, -1, 0,
                         "created_nob",    "Пересчет НОБ",2, 2, -1, 0,
                         "t_fiid",      "ФИ ID",     5, 2, -1, 0,
                         "t_name",      "Выпуск",    15 , 2, -1, 0,
                         "t_isin",      "ISIN",      10, 2, -1, 0, 
                         "t_coef",      "Коэффициент", 8,  2, -1, 0, 
                         "t_fiid_new",  "nФИ ID",    5,  2, -1, 0,
                         "t_name_new",  "nВыпуск",   15,  2, -1, 0,
                         "t_isin_new",  "nISIN",     10,  2, -1, 0,
                         "t_oper",      "Опер",      4,  2, -1, 0,
                         "t_opername",  "ФИО Операциониста",       20,  2, -1, 0,
                         "t_sysdate",   "Системная дата", 18,  2, -1, 0
                          );
      while(RunScroll(rsScroll, ar.size/6, Ar, "eRunConvHistProcessDialog", "eRunConvHistProcessDialog", "История запусков", "~Enter~ Просмотр ~Esc~ Выход ~Ctrl+Shift+F11~ Выгрузить в Excel", true))

        cmdScroll = RsdCommand(cmdQuery);
        rsScroll = RSDRecordset(cmdScroll, RSDVAL_CLIENT, RSDVAL_STATIC);
      end;


   end;

   macro OnChecked(RsbEvent:Object)
    /* if (RsbEvent.id == RSB_EV_CONTROL_CHECKED)
     end;   */
     return true;
   end;

   macro OnClicked(RsbEvent:Object)
     var i=0;
     if (RsbEvent.source.name == "BtAdd")
        if(Parm.fi_new_count == 5)
          // msgbox("Достигнут максимум");
        else
           this.getControl("BtRemove").Enabled = true;
           for (i,0,Parm.fi_new_count-1)
              Parm.fi_new[i].coef = this.getControl("coef"+string(i+1)).Value;
           end;
           Parm.IncCountFi;
           if(Parm.fi_new_count == 5)
              this.getControl("BtAdd").Enabled = false;
           end;
  
           this.Close(9);
        end;
     elif (RsbEvent.Source.name == "BtRemove")
        if(Parm.fi_new_count > 1)
           for (i,0,Parm.fi_new_count-1)
              Parm.fi_new[i].coef = this.getControl("coef"+string(i+1)).Value;
           end;
           Parm.DecCountFi;
           if(Parm.fi_new_count == 1)
              RsbEvent.Source.Enabled = false;
           end;
           this.getControl("BtAdd").Enabled = true;
           this.Close(9);
        end;
     end;   
     return true;
   end;

   macro onRemoveFocus(ev)   
      var Fin = TRecHandler( "fininstr.dbt" );
      var Prt = TRecHandler( "party.dbt" );

      if (ev.Source.Name == "fi_old") 
         if (eFi_old.value == "")
            eFi_old.value = "";
            eFi_name_old.value = "";
            ClearRecord(Parm.fi_old);
         else
            if (ПолучитьФинИн(ev.Source.value, fin) == 0)
               Copy(Parm.fi_old, Fin);
               ev.Source.value = SubStr(Parm.fi_old.rec.FI_Code,1,15);
               eFi_name_old.value = SubStr(Parm.fi_old.rec.Name,1,30);
            else
               msgbox("Не найден ФинИнструмент с кодом " + ev.Source.value);
               ev.Source.value = SubStr(Parm.fi_old.rec.FI_Code,1,15);
               eFi_name_old.value = SubStr(Parm.fi_old.rec.Name,1,30);
               SetFocus("fi_old");
            end;
         end;
      end;
      if (index(ev.Source.Name, "fi_new") > 0)
         var i = int(Substr(ev.Source.Name,StrLen(ev.Source.Name)))-1;
         if (this.getControl(ev.Source.Name).value == "")
            this.getControl(ev.Source.Name).value = "";
            this.getControl("fi_name_new"+string(i+1)).value = "";
            ClearRecord(Parm.fi_new[i].fin);
         else
            if (ПолучитьФинИн(this.getControl(ev.Source.Name).value, fin) == 0)
               Copy(Parm.fi_new[i].fin, Fin);
               this.getControl(ev.Source.Name).value = SubStr(fin.rec.FI_Code,1,15);
               this.getControl("fi_name_new"+string(i+1)).value = SubStr(fin.rec.Name,1,30);
            else
               msgbox("Не найден ФинИнструмент с кодом " + this.getControl(ev.Source.Name).value);
               this.getControl(ev.Source.Name).value = SubStr(Fin.rec.FI_Code,1,15);
               this.getControl("fi_name_new"+string(i+1)).value = SubStr(Fin.rec.Name,1,30);
               SetFocus("fi_new"+string(i+1));
            end;
         end;
      end;

      if (ev.Source.Name == "client")
         if (eClient.value != "Все")
            if (eClient.value == "")
               eClient.value = "Все";
               eClient_name.value = "Все";
               ClearRecord(Parm.Party);
            else
               if(ПолучитьСубъекта(ПолучитьКодСубъекта(eClient.value,1), Prt) == 0)
                  Copy(Parm.Party, Prt);
                  eClient.value = ПолучитьКодСубъекта(Parm.Party.rec.partyid,1);
                  eClient_name.value = SubStr(Parm.Party.rec.shortname,1,30);
               else
                  msgbox("Не найден Клиент с кодом " + eClient.value);
                  eClient.value = ПолучитьКодСубъекта(Parm.Party.rec.partyid,1);
                  eClient_name.value = SubStr(Parm.Party.rec.shortname,1,30);
                  SetFocus("client");
               end;
            end;
         end;
      end;
   end;

   macro onKeyPressed(ev)   
      var stat = 0, msg = "", i=0;

      if (ev.keycode == 32 /*Space*/) 
         if  (this.focusedControl().Name == "client")
            eClient.value = "Все";
            eClient_name.value = "Все";
            ClearRecord(Parm.Party);
         end;
         if (this.focusedControl().Name == "fi_old") 
            eFi_old.value = "";
            eFi_name_old.value = "";
            ClearRecord(Parm.fi_old);
         end;
         if  (index(this.focusedControl().Name, "fi_new") > 0)
            i = int(Substr(this.focusedControl().Name,StrLen(this.focusedControl().Name)-1))-1;
            this.focusedControl().value = "";
            this.getControl("fi_name_new"+string(i+1)).value = "";
            ClearRecord(Parm.fi_new[i].fi);
         end;
      elif (ev.keycode == 317/*F3*/)
         if (this.focusedControl().Name == "fi_old") 
            if( ListAvoiriss( 0, Parm.fi_old, null, null ) )
               eFi_old.value = SubStr(Parm.fi_old.rec.FI_Code,1,15);
               eFi_name_old.value = SubStr(Parm.fi_old.rec.Name,1,30);
            end;
         end;
         if  (index(this.focusedControl().Name, "fi_new") > 0)
            i = int(Substr(this.focusedControl().Name,StrLen(this.focusedControl().Name)))-1;
            if( ListAvoiriss( 0, Parm.fi_new[i].fin, null, null ) )
               this.focusedControl().value = SubStr(Parm.fi_new[i].fin.rec.FI_Code,1,15);
               this.getControl("fi_name_new"+string(i+1)).value = SubStr(Parm.fi_new[i].fin.rec.Name,1,30);
            end;
         end;
         if  (this.focusedControl().Name == "client")
            if( ListPT( Parm.Party, 1, "", PTLIST_STOCKDLCLIENT, PTCK_ALL ))
               eClient.value = ПолучитьКодСубъекта(Parm.Party.rec.partyid,1);
               eClient_name.value = SubStr(Parm.Party.rec.shortname,1,30);
            end;
         end;

       //  this.Redraw();
      elif (ev.keycode == 316) /*F2*/

         for (i,0,Parm.fi_new_count-1)
            Parm.fi_new[i].coef = this.getControl("coef"+string(i+1)).Value;
         end;
         /*проверить корректность заполнения*/
         if (Parm.fi_old.rec.fiid <= 0)
            msg = "Не задан конвертируемый фининструмент";
            stat = 1;
         end;
         for (i,0,Parm.fi_new_count-1)
            if (Parm.fi_new[i].fin.rec.fiid <= 0)
               msg = IIF (StrLen(msg)==0,"",msg+"|") + "Не задан новый фининструмент №"+string(i+1);
               stat = 1;
            end;
         end;
         if (ecoef.value == 0.0)
            msg = IIF (StrLen(msg)==0,"",msg+"|") + "Не задан коэффициент";
            stat = 1;
         end;
         if (ecoef.value < 0.0)
            msg = IIF (StrLen(msg)==0,"",msg+"|") + "Коэффициент не может быть меньше нуля";
            stat = 1;
         end;

         if (stat)
            MsgBox(msg);
         else
            /*создать сеанс запуска*/
            if (InitOper(@Parm.SeanceID) == 0)
               /*Отбор операций для конвертации*/
               BegAction(null, "Отбор операций для конвертации");
               PrepareData(Parm.SeanceID);
               EndAction();
               /*Показать список. */
               ShowList(Parm.SeanceID);
            else
               MsgBox("Ошибка инициализации операции");
            end;
         end;
      elif (ev.keycode == 320) /*F6*/
         /*История запусков*/
         ShowHist();
      elif (ev.keycode == 27) /*Esc*/
         exit(2);
      end;/*end F2*/

 /*  OnError(e)
      EndAction;
      Msgbox("Ошибка выполнения:|" + e.message + "|Module:" + e.Module+ "|line:"+e.line);   */
   end; /*onKeyPressed*/
end; /*class Pan*/

var ConvParm = TConvParm();
/*test default  
ПолучитьФинИн("US71922G2093", ConvParm.fi_old);
ПолучитьФинИн("US71922G3083", ConvParm.fi_new[0].fin);
ConvParm.fi_new[0].coef = 0.9936;
ConvParm.IncCountFi();
ПолучитьФинИн("US71922G4073", ConvParm.fi_new[1].fin);
ConvParm.fi_new[1].coef = 0.0064;
*******/

var myPanel:TRsbPanel = TPan(ConvParm);
var prev_fi_count = 0;
while (myPanel.Parm.fi_new_count != prev_fi_count )
   prev_fi_count = myPanel.Parm.fi_new_count;
   myPanel.run;
   ConvParm = myPanel.Parm;
   myPanel = null;
   myPanel = TPan(ConvParm);
end;
exit(1);