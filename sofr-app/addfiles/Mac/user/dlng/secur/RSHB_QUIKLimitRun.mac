/*
$Name:         RSHB_QUIKLimitRun.mac
$Module:       
$Description:  
*/
import BankInter;


import "RSHB_Quik2.mac", "limout.mac", "spcomcalc.mac", "lim_utils.mac"; //"splibu.mac" ;

PRIVATE CONST PAN_FORALL_CHECKED    = true;
PRIVATE CONST PAN_CALCCOMM_CHECKED    = false;
PRIVATE CONST PAN_CALCCOMM_STOCK_CHECKED  = false;
PRIVATE CONST PAN_CALCCOMM_CUR_CHECKED    = false;
PRIVATE CONST PAN_CALC_CHECKED     = true;
PRIVATE CONST PAN_EDP_CHECKED      = true;
PRIVATE CONST PAN_SECUR_CHECKED    = true;
PRIVATE CONST PAN_FUTUR_CHECKED    = true;
PRIVATE CONST PAN_MMCUR_CHECKED    = true;
PRIVATE CONST PAN_SECUR_SPB_CHECKED    = true;


PRIVATE CONST PAN_CALCDATE         = {curdate};
PRIVATE CONST PAN_COMPARE_CHECKED  = true;
PRIVATE CONST PAN_RELOAD_CHECKED   = false;
PRIVATE CONST PAN_UNLOAD_CHECKED   = true;
PRIVATE CONST PAN_UNLOAD_SELECTED_CHECKED = false;
PRIVATE CONST PAN_DELETE_SELECTED_CHECKED = false;

 
PRIVATE CONST MARKET_KIND_STOCK    = 1; //фондовый
PRIVATE CONST MARKET_KIND_DERIV    = 2; //срочный
PRIVATE CONST MARKET_KIND_CURR     = 3; //валютный
PRIVATE CONST MARKET_KIND_EDP      = 4; //ЕДП


class (TRsbPanel) Pan
   initTRsbPanel();

   var CalcDate, calc, compare, reload, unload, forAll,calcComm, securComm, curComm, byEDP, byStockMB, byFortsMB, byCurMB, byStockSPB;
   var dtEDP, dtStockMB, dtFortsMB, dtCurMB, dtStockSPB, unload_sel, delete_sel;
   var m_Label:TrsbLabel;

 /*  class (TArray) TStrCollector
      /*Очистить массив.*/
     macro ClearArray
       this.Size = 0;
     end;
     /*Добавить строку в массив.*/
     macro AddString( Str )
       this.(this.Size) = Str;
     end;
   end;      */

   macro _addCheckBox(str, col, name, def, label)
      var checkBox = TRsbCheckbox;
      checkBox.setPosition(col,str);
      checkBox.name = name;
      checkBox.checked = def;
      addControl(checkBox);
      var m_Label:TrsbLabel = TRsbLabel(col+8,str,label);
      addLabel (m_Label);
      return checkBox;
   end;

   macro _addEditFieldDate(str, col, name, def, editable)
      var Dt = TRsbEditField(V_DATE);
      Dt.setPosition(col,str);
      Dt.setSize(8,1);
      Dt.name = name;
      Dt.value = def;
      Dt.editable = editable;
      addControl(Dt);
      return Dt;
   end;

   macro CheckCalcExists(MarketKind, MarketID, CheckDate)
      var dt = TRsbDataSet(" select RSHB_RSI_SCLIMIT.RSI_CheckCalcExists("+MarketKind+", "+MarketID+", " + GetSqlDate(CheckDate)+") CalcExists from dual ");
      if (dt.movenext())
         return dt.CalcExists;
      end;
      return 0;
   end;

   macro UpdateDateFields()
      var ld = TRsbDataSet(" select RSHB_RSI_SCLIMIT.RSI_GetLastDateCalc(1, "+MMVB_ID()+") t_date from dual ");
      if (ld.movenext())
         dtStockMB.value = SQL_COnvTypeDate(ld.date);
      end;
      ld = TRsbDataSet(" select RSHB_RSI_SCLIMIT.RSI_GetLastDateCalc(3, "+MMVB_ID()+") t_date from dual ");
      if (ld.movenext())
         dtCurMB.value = SQL_COnvTypeDate(ld.date);
      end;
      ld = TRsbDataSet(" select RSHB_RSI_SCLIMIT.RSI_GetLastDateCalc(2, "+MMVB_ID()+") t_date from dual ");
      if (ld.movenext())
         dtFortsMB.value = SQL_COnvTypeDate(ld.date);
      end;
      ld = TRsbDataSet(" select RSHB_RSI_SCLIMIT.RSI_GetLastDateCalc(1, "+SPB_ID()+") t_date from dual ");
      if (ld.movenext())
         dtStockSPB.value = SQL_COnvTypeDate(ld.date);
      end;
      ld = TRsbDataSet(" select RSHB_RSI_SCLIMIT.RSI_GetLastDateCalc(4, "+MMVB_ID()+") t_date from dual ");
      if (ld.movenext())
         dtEDP.value = SQL_COnvTypeDate(ld.date);
   end;
   end;


   macro ReFillContracts()
      //var cmd = RsdCommand("truncate table DDL_PANELCONTR_DBT");
      var cmd = RsdCommand("ALTER TABLE DDL_PANELCONTR_DBT TRUNCATE PARTITION P99999999X");
      cmd.execute();
      var sql = 
      " insert into DDL_PANELCONTR_DBT (T_SETFLAG,T_CLIENTID,T_CLIENTCODE,T_CLIENTNAME,T_DLCONTRID,T_CONTRNUMBER,T_CONTRNAME,T_CONTRDATE,T_EKK,T_ISEDP) " + 
      " select chr(0), party.t_partyid, objcode.t_Code, party.t_name , dlcontr.t_dlcontrid, sfcontr.t_number, SFCONTR.T_NAME, SFCONTR.T_DATEBEGIN , dlobjcode.t_code, chr(0) " +
      " from ddlcontr_dbt dlcontr  " +
      " join dsfcontr_dbt sfcontr on sfcontr.t_id = dlcontr.t_sfcontrid and  " +
      "                            sfcontr.t_datebegin <= "+GetSQlDate(calcdate.value)+" and  " +
      "                            (sfcontr.t_dateclose = to_date('01.01.0001','dd.mm.yyyy') or  " +
      "                             sfcontr.t_dateclose >= "+GetSQlDate(calcdate.value)+")  " +
      " join dparty_dbt party on party.t_partyid = sfcontr.t_partyid  " +
      " join DOBJCODE_DBT objcode on party.t_partyid= OBJCODE.T_OBJECTID and objcode.t_objecttype = 3 and objcode.t_codekind = 1 and OBJCODE.T_STATE = 0 " +
      " join ddlobjcode_dbt dlobjcode on dlcontr.t_dlcontrid = DLOBJCODE.T_OBJECTID and dlobjcode.t_objecttype = 207 and dlobjcode.t_codekind = 1  and dlobjcode.t_bankclosedate = to_date('01010001','ddmmyyyy');" ;
      cmd = null;
      cmd = RsdCommand(sql);
      cmd.execute();
     /* 
      sql = 
      " MERGE INTO DDL_PANELCONTR_DBT c " +
      "     USING (SELECT CHR (88) AS IsEDP, d.t_dlcontrid " +
      "              FROM ddlcontr_dbt d, dsfcontr_dbt sm " +
      "             WHERE d.t_sfcontrid = sm.t_id " +
      "                   AND (sm.T_DATEclose = TO_DATE ('01010001', 'ddmmyyyy') " +
      "                        OR sm.T_DATEclose > "+GetSQlDate(calcdate.value)+") " +
      "                   AND EXISTS " +
      "                          (SELECT 1 " +
      "                             FROM dsfcontr_dbt s, ddlcontrmp_dbt mp " +
      "                            WHERE t_servkindsub = 8 " +
      "                                  AND (s.T_DATEclose = " +
      "                                          TO_DATE ('01010001', 'ddmmyyyy') " +
      "                                       OR s.T_DATEclose > "+GetSQlDate(calcdate.value)+") " +
      "                                  AND RSB_SECUR. " +
      "                                       GetGeneralMainObjAttr ( " +
      "                                         659, " +
      "                                         LPAD (s.t_ID, 10, '0'), " +
      "                                         102, " +
      "                                         "+GetSQlDate(calcdate.value)+") = 1 " +
      "                                  AND mp.t_sfcontrid = s.t_id " +
      "                                  AND mp.t_dlcontrid = d.t_dlcontrid)) d " +
      "        ON ( c.t_calc_sid='X' and c.t_dlcontrid = d.t_dlcontrid) " +
      " WHEN MATCHED " +
      " THEN " +
      "   UPDATE SET c.t_IsEDP = d.IsEDP; ";
      cmd = null;
      cmd = RsdCommand(sql);
      cmd.execute();  */

   end;



   /*Расчет комиссий*/
   macro CalcComiss(dt, ContractID, result_protocol:@TStrCollector, recs:@integer, errrecs:@integer, errmsg:@string, wrnrecs:@integer)
     recs = 0;
     errrecs = 0;
     wrnrecs = 0; // Пока возвращаем 0
     errmsg = "";
     result_protocol.ClearArray;


     RunComCalc(dt, -1/*DealID*/, -1/*PartyID*/, ContractID, -1/*ConCom*/, METHOD_CALC, false/*PrintRep*/, null/*webreport*/, @result_protocol, @recs, @errrecs, @wrnrecs/*dan*/);

     return true;
   onError(erru)  
     errmsg = "onError: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
     return false;
   end;



   /***Начало рисования панели***/

   var curstr = 0;
   Caption = "Расчет лимитов для QUIK";
   Status = "~ESC~ Выход ~Space~ Переключить ~F3~ Выбор ~F2~ Выполнить ";

   var Tab1 = 3;
   var Tab2 = 7;
   var Tab3 = 44;

   setSize(54,32);
   setPosition(8,1);

   /*
      ╔═══════ Расчет лимитов для QUIK ════════════════════════╗
    1 ║ Дата расчета 15.02.2021                                ║
    2 ║                                                        ║
    3 ║ Отбор по клиентам:                                     ║
    4 ║        [ ] Все клиенты                                 ║
    5 ║        выбрано клиентов: 4                             ║
    6 ║                                                        ║
    7 ║ [х] Расчет комиссий по выбранным клиентам              ║
    8 ║        [х] фондовый рынок                              ║
    9 ║        [х] валютный рынок                              ║
   10 ║                                                        ║
   11 ║ [х] Расчет лимитов по выбранным клиентам               ║
   12 ║ Позиции:                   Последний расчет в базе на: ║
   13 ║  [х] единая денежная позиция (ЕДП)         14.02.2021  ║
   14 ║  Без использования ЕДП:                                ║
   15 ║        [х] фондовый ММВБ                   14.02.2021  ║
   16 ║        [х] срочный ММВБ                    14.02.2021  ║
   17 ║        [х] валютный ММВБ                   14.02.2021  ║
   18 ║        [х] фондовый СПБ                    14.02.2021  ║
   19 ║                                                        ║
   20 ║ [х] Сверка лимитов                                     ║
   21 ║     [х] Перегрузить файл для сверки из директории:     ║
   22 ║          \QUIK\In\*.lim                                ║
   23 ║                                                        ║
   24 ║ [х] Выгрузить файл с лимитами в директорию:            ║
   25 ║        [ ] только по выбранным клиентам                ║
   26 ║                                                        ║
   27 ║                                                        ║
   28 ║                                                        ║
   29 ║                                                        ║
   30 ║                                                        ║
   31 ║ [ ] Удалить лимиты выбранных клиентов из файлов лимитов║
   32 ║                                                        ║
      ╚════════════════════════════════════════════════════════╝
   */

   /*1*/curstr = curstr+1;
   m_Label = TRsbLabel(Tab1,curstr,"Дата расчета");
   addLabel (m_Label);
   CalcDate = _addEditFieldDate(curstr, 14, "CalcDate", PAN_CALCDATE, true);

   /*2*/curstr = curstr+1;
   /*3*/curstr = curstr+1;
   m_Label = TRsbLabel(Tab1,curstr,"Отбор по клиентам:");
   addLabel (m_Label);

   /*4*/curstr = curstr+1;
   forAll = _addCheckBox(curstr, Tab2, "forAll", PAN_FORALL_CHECKED, "Все клиенты");

   /*5*/curstr = curstr+1;
   m_Label = TRsbLabel(Tab2,curstr,"выбрано клиентов: ");
   addLabel (m_Label);
   var m_SelectedClients:TRsbEditField = TRsbEditField(7/*V_STRING*/);
   m_SelectedClients.SetPosition(34,curstr);
   m_SelectedClients.SetSize(10,1);
   m_SelectedClients.Name = "SelectedClients";
   m_SelectedClients.editable = false;
   m_SelectedClients.focusable = true;
   m_SelectedClients.enabled = true;
   m_SelectedClients.textLength = 30;
   m_SelectedClients.value = "Все";
   m_SelectedClients.OnKeyPressed(R2M(this, "onKeyPressed"));
   addControl (m_SelectedClients);

   /*6*/curstr = curstr+1;
   /*7*/curstr = curstr+1;
   calcComm = _addCheckBox(curstr, Tab1, "calcComm", PAN_CALCCOMM_CHECKED, "Расчет комиссий по выбранным клиентам");

   /*8*/curstr = curstr+1;
   securComm = _addCheckBox(curstr, Tab2, "securComm", PAN_CALCCOMM_STOCK_CHECKED, "Фондовый рынок");

   /*9*/curstr = curstr+1;
   curComm = _addCheckBox(curstr, Tab2, "curComm", PAN_CALCCOMM_CUR_CHECKED, "Валютный рынок");

   /*10*/curstr = curstr+1;
   /*11*/curstr = curstr+1;
   calc = _addCheckBox(curstr, Tab1, "calc", PAN_CALC_CHECKED, "Расчет лимитов по выбранным клиентам");

   /*12*/curstr = curstr+1;
   m_Label = TRsbLabel(Tab1,curstr,"Позиции:");
   addLabel (m_Label);
   m_Label = TRsbLabel(32,curstr,"Последний расчет в базе на:");
   addLabel (m_Label);

   /*13*/curstr = curstr+1;
   dtEDP = _addEditFieldDate(curstr, Tab3, "dtEDP", date(0,0,0), false);
   byEDP = _addCheckBox(curstr, Tab2, "byEDP", PAN_EDP_CHECKED, "единая денежная позиция (ЕДП) ");

   /*14*/curstr = curstr+1;
   m_Label = TRsbLabel(Tab1,curstr,"Без использования ЕДП:");
   addLabel (m_Label);

   /*15*/curstr = curstr+1;
   byStockMB = _addCheckBox(curstr, Tab2, "byStockMB", PAN_SECUR_CHECKED, "фондовый ММВБ");
   dtStockMB = _addEditFieldDate(curstr, Tab3, "dtStockMB", date(0,0,0), false);
   /*16*/curstr = curstr+1;
   byFortsMB = _addCheckBox(curstr, Tab2, "byFortsMB", PAN_FUTUR_CHECKED, "срочный ММВБ");
   dtFortsMB = _addEditFieldDate(curstr, Tab3, "dtFortsMB", date(0,0,0), false);
   /*17*/curstr = curstr+1;
   byCurMB = _addCheckBox(curstr, Tab2, "byCurMB", PAN_MMCUR_CHECKED, "валютный ММВБ");
   dtCurMB = _addEditFieldDate(curstr, Tab3, "dtCurMB", date(0,0,0), false);
   /*18*/curstr = curstr+1;
   byStockSPB = _addCheckBox(curstr, Tab2, "byStockSPB", PAN_SECUR_SPB_CHECKED, "фондовый СПБ");
   dtStockSPB = _addEditFieldDate(curstr, Tab3, "dtStockSPB", date(0,0,0), false);

   /*19*/curstr = curstr+1;
   /*20*/curstr = curstr+1;
   compare = _addCheckBox(curstr, Tab1, "compare", PAN_COMPARE_CHECKED, "Сверка лимитов");

   /*21*/curstr = curstr+1;
   reload = _addCheckBox(curstr, Tab2, "reload", PAN_RELOAD_CHECKED, "Перегрузить файл для сверки из директории:");

   /*22*/curstr = curstr+1;
   var m_PathReload:TRsbEditField = TRsbEditField(7/*V_STRING*/);
   m_PathReload.SetPosition(11,curstr);
   m_PathReload.SetSize(35,1);
   m_PathReload.Name = "PathReload";
   m_PathReload.editable = false;
   m_PathReload.textLength = 70;
   var PathIn = "";
   getregistryvalue("РСХБ\\ДИРЕКТОРИИ\\QUIK_IN", V_STRING, PathIn );
   m_PathReload.value = PathIn /*SubStr(PathIn, index(PathIn,"inbox")+5)*/ + "\\*.lim";
   addControl (m_PathReload);


   /*23*/curstr = curstr+1;
   /*24*/curstr = curstr+1;
   unload = _addCheckBox(curstr, Tab1, "unload", PAN_UNLOAD_CHECKED, "Выгрузить файл с лимитами в директорию:");

   /*25*/curstr = curstr+1;
   unload_sel = _addCheckBox(curstr, Tab2, "unload_sel", PAN_UNLOAD_SELECTED_CHECKED, "только по выбранным клиентам");

   /*26*/curstr = curstr+1;
   var m_PathUnloadSMB:TRsbEditField = TRsbEditField(7/*V_STRING*/);
   m_PathUnloadSMB.SetPosition(Tab2+1,curstr);
   m_PathUnloadSMB.SetSize(35,1);
   m_PathUnloadSMB.Name = "PathUnloadSMB";
   m_PathUnloadSMB.editable = false;
   m_PathUnloadSMB.textLength = 200;
   m_PathUnloadSMB.value = "";
   addControl (m_PathUnloadSMB);

   /*27*/curstr = curstr+1;
   var m_PathUnloadCMB:TRsbEditField = TRsbEditField(7/*V_STRING*/);
   m_PathUnloadCMB.SetPosition(Tab2+1,curstr);
   m_PathUnloadCMB.SetSize(35,1);
   m_PathUnloadCMB.Name = "PathUnloadCMB";
   m_PathUnloadCMB.editable = false;
   m_PathUnloadCMB.textLength = 200;
   m_PathUnloadCMB.value = "";
   addControl (m_PathUnloadCMB);

   /*28*/curstr = curstr+1;
   var m_PathUnloadFMB:TRsbEditField = TRsbEditField(7/*V_STRING*/);
   m_PathUnloadFMB.SetPosition(Tab2+1,curstr);
   m_PathUnloadFMB.SetSize(35,1);
   m_PathUnloadFMB.editable = false;
   m_PathUnloadFMB.Name = "PathUnloadFMB";
   m_PathUnloadFMB.textLength = 200;
   m_PathUnloadFMB.value = "";
   addControl (m_PathUnloadFMB);

   /*29*/curstr = curstr+1;
   var m_PathUnloadSSPB:TRsbEditField = TRsbEditField(7/*V_STRING*/);
   m_PathUnloadSSPB.SetPosition(Tab2+1,curstr);
   m_PathUnloadSSPB.SetSize(35,1);
   m_PathUnloadSSPB.Name = "PathUnloadSSPB";
   m_PathUnloadSSPB.editable = false;
   m_PathUnloadSSPB.textLength = 200;
   m_PathUnloadSSPB.value = "";
   addControl (m_PathUnloadSSPB);

   /*30*/curstr = curstr+1;
   var m_PathUnloadEDP:TRsbEditField = TRsbEditField(7/*V_STRING*/);
   m_PathUnloadEDP.SetPosition(Tab2+1,curstr);
   m_PathUnloadEDP.SetSize(35,1);
   m_PathUnloadEDP.Name = "PathUnloadEDP";
   m_PathUnloadEDP.editable = false;
   m_PathUnloadEDP.textLength = 200;
   m_PathUnloadEDP.value = "";
   addControl (m_PathUnloadEDP);

   /*31*/curstr = curstr+1;
   delete_sel = _addCheckBox(curstr, Tab2, "delete_sel", PAN_DELETE_SELECTED_CHECKED, "Удалить лимиты выбранных клиентов из файла лимитов");


   curstr = curstr+2;
   var m_Log:TRsbEditField = TRsbEditField(7/*V_STRING*/);
   m_Log.SetPosition(3,curstr);
   m_Log.SetSize(50,20);
   m_Log.editable = false;
   m_Log.textLength = 3000;
   //addControl (m_Log);
   m_Log.value = "...";
   macro AddStrLog(str)
      m_Log.value = m_Log.value + "\n" + str;
     // this.ReDraw;
   end;

   macro ClearLog()
      m_Log.value = "";
   end;

   addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "onKeyPressed"));
   addEventHandler(RSB_EV_CONTROL_CHECKED,R2M(this,"OnChecked"));

   //sql_execute("truncate table DDL_PANELCONTR_DBT");
   sql_execute("ALTER TABLE DDL_PANELCONTR_DBT TRUNCATE PARTITION P99999999X");

   this.getControl("securComm").enabled = iif(this.getControl("calcComm").checked, true, false);
   this.getControl("curComm").enabled = iif(this.getControl("calcComm").checked, true, false);
   this.getControl("unload_sel").enabled = iif(this.getControl("forAll").checked, false, true) and iif(this.getControl("unload").checked, true, false);
   this.getControl("delete_sel").enabled = iif(this.getControl("forAll").checked, false, true) and iif(this.getControl("unload_sel").checked, false, true)  and iif(this.getControl("unload").checked, true, false);

   UpdateDateFields();

   SetFocus("calc");
   updateNavigationRoute();
   ReDraw();

   /***Конец рисования панели***/



   /***Обработчик***/

   macro OnChecked(RsbEvent:Object)
     if (RsbEvent.id == RSB_EV_CONTROL_CHECKED)
        if ((RsbEvent.source.name == "calc") or  (RsbEvent.source.name == "calcComm") or (RsbEvent.source.name == "unload"))
           calcdate.editable = this.getControl("calc").checked or this.getControl("calcComm").checked or this.getControl("unload").checked;
           this.redraw();
        elif (RsbEvent.source.name == "compare")
           reload.editable = not reload.editable;
           this.redraw();
        end;
     end;
     return true;
   end;

   macro onKeyPressed(ev)   
      /*Выбор клиентов*/
      macro SelectClient()

         /*Обработчик скроллинга*/
         macro Client_Scroll(rs, cmd, id, key)
            /*Обработчик*/
            if(cmd == DLG_KEY)
               if(key == 13) /*Enter*/
                  return CM_IGNORE;
               elif(key == 27) /*Esc*/
                  return CM_CANCEL;
               elif(key == 32) /*Space*/
                  if(rs.RecCount > 0)
                     rs.edit();
                     if(rs.value("t_setflag") == "X")
                        rs.value("t_setflag") = " ";
                     else                                                                                         
                        rs.value("t_setflag") = "X";
                     end;
                     rs.update();
                     updatescroll(rs, 5);
                     return CM_IGNORE;
                  end;
               end;
            end;
         end;

         var rs, cmd;
         var que = "select t_setflag, t_clientid, t_clientname, t_dlcontrid, t_contrnumber, t_ekk /*, t_isedp*/ from DDL_PANELCONTR_DBT where t_calc_sid ='X' order by t_setflag desc, t_clientname";
         cmd = RsdCommand(que);
         rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
         while(RunScroll(rs, 5, MakeArray("t_setflag", "В", 1, 2, -1, 0,  
                                         "t_clientname", "Наименование клиента", 30, 2, -1, 0,
                                         "t_contrnumber", "Номер", 8, 2, -1, 0, 
                                         "t_ekk", "ЕКК", 8, 2, -1, 0,
                                         "t_clientid", "ID", 8, 2, -1, 0 
                                         //"t_isedp", "ЕДП", 8, 2, -1, 0
                                         ), 
                        null, "Client_Scroll", "Выбор клиентов", "~Esc~ Выход ~Пробел~ Изменить", false))
            cmd = RsdCommand(que);
            rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
         end;
      end;


      var Dt, flag = true, str, LogText = "", sql = "", cmd, msg = "";

      if (ev.keycode == 32 /*Space*/) 
         if (this.focusedControl().Name == "forAll")
            if (this.focusedControl().checked)
               this.getControl("SelectedClients").value = "Все";
            else
               this.getControl("SelectedClients").value = "0";
               Dt = TRsbDataSet("select 1 from DDL_PANELCONTR_DBT where t_calc_sid ='X'");
               if (not Dt.movenext())
                  BegAction();
                  ReFillContracts();
                  EndAction();
               end;
               SelectClient();
               Dt = TRsbDataSet("select count(1) cnt from DDL_PANELCONTR_DBT where t_calc_sid ='X' and t_setflag = chr(88)");
               if (Dt.movenext())
                  this.getControl("SelectedClients").value = strsubst(string(dt.cnt), ".0000","")
               end;
            end;
            this.getControl("delete_sel").enabled = iif(this.getControl("forAll").checked, false, true);
            this.getControl("unload_sel").enabled = iif(this.getControl("forAll").checked, false, true) and iif(this.getControl("unload").checked, true, false);
         elif (this.focusedControl().Name == "calcComm")
            this.getControl("securComm").enabled = iif(this.focusedControl().checked, true, false);
            this.getControl("curComm").enabled = iif(this.focusedControl().checked, true, false);
            this.getControl("securComm").focusable = iif(this.getControl("calcComm").checked, true, false);
            this.getControl("curComm").focusable = iif(this.getControl("calcComm").checked, true, false);
         elif (this.focusedControl().Name == "unload")
            this.getControl("unload_sel").enabled = iif(this.getControl("forAll").checked, false, true) and iif(this.getControl("unload").checked, true, false);
            this.getControl("delete_sel").enabled = iif(this.getControl("forAll").checked, false, true) and iif(this.getControl("unload_sel").checked, false, true) and iif(this.getControl("unload").checked, true, false);
         elif (this.focusedControl().Name == "unload_sel")
            this.getControl("delete_sel").enabled = iif(this.getControl("forAll").checked, false, true) and iif(this.getControl("unload_sel").checked, false, true) and iif(this.getControl("unload").checked, true, false);
         elif (this.focusedControl().Name == "delete_sel")
            this.getControl("unload_sel").enabled = iif(this.getControl("forAll").checked, false, true) and iif(this.getControl("delete_sel").checked, false, true) and iif(this.getControl("unload").checked, true, false);
         end;
         this.updateNavigationRoute();
         this.ReDraw();
      elif (ev.keycode == 317/*F3*/)
         if(this.focusedControl().Name == "CalcDate")
           this.focusedControl().Value = GetDateByCalendar(PAN_CALCDATE);
         end;

         if ((this.focusedControl().name == "forAll") or (this.focusedControl().name == "SelectedClients"))
            Dt = TRsbDataSet("select 1 from DDL_PANELCONTR_DBT where t_calc_sid ='X' ");
            if (not Dt.movenext())
               BegAction();
               ReFillContracts();
               EndAction();
            end;
            SelectClient();
            Dt = TRsbDataSet("select count(1) cnt from DDL_PANELCONTR_DBT where t_calc_sid ='X' and t_setflag = chr(88)");
            if (Dt.movenext())
               this.getControl("SelectedClients").value = strsubst(string(dt.cnt), ".0000","")
            end;
            this.getControl("forAll").checked = false;
            this.getControl("delete_sel").enabled = iif(this.getControl("forAll").checked, false, true);
            this.getControl("unload_sel").enabled = iif(this.getControl("forAll").checked, false, true) and iif(this.getControl("unload").checked, true, false);
            this.updateNavigationRoute();
            this.ReDraw();
         end;
      elif (ev.keycode == 316) /*F2*/

         SetOutPut(gettxtfilename("QuikLimitRun_log_"+StrSubst(string(date()),".","")+StrSubst(string(time()),":","")),false);


         /*** расчет комиссий ***/
         if (this.getControl("calcComm").checked)
            var i=0, j=0, resultproc, recs=0, errrecs=0,errmsg="",wrnrecs=0, result_protocol = TStrCollector;
            if (not (this.getControl("securComm").checked or this.getControl("curComm").checked))
               MsgBox("Не выбран рынок");
            elif (this.getControl("forAll").checked)
            if (this.getControl("securComm").checked)
                  println("Расчет комиссий по фондовому рынку за " + GetDateAfterWorkDays(this.getControl("CalcDate").value,-1));
                  resultproc = CalcComiss ( GetDateAfterWorkDays(this.getControl("CalcDate").value,-1), -1, @result_protocol, @recs, @errrecs, @errmsg, @wrnrecs);
                  if (not resultproc)
                     if(strlen(errmsg) <= 0)
                       errmsg = "Неизвестная ошибка при обработке процесса расчета комиссий по фондовому рынку";
                     end;
                     println(errmsg);
                  else
                  end;
                  //println("Рассчитано: " + recs);
                  //println("Ошибок: " + errrecs);
                  //println("Предупреждений: " + wrnrecs);

                  while(i < result_protocol.size)
                     print (result_protocol.(i));
                     i = i + 1;
                  end;
                  println;
            end;
            if (this.getControl("curComm").checked)
                  i=0; recs=0;  errrecs=0; errmsg=""; wrnrecs=0; result_protocol = TStrCollector;
                  println("Расчет комиссий по валютному рынку за " + GetDateAfterWorkDays(this.getControl("CalcDate").value,-1));
               SetGlobalParameter("BO_ACT_INITIATOR", "ФИССиКО");
                  resultproc =  CalcComiss ( GetDateAfterWorkDays(this.getControl("CalcDate").value,-1), -1, @result_protocol, @recs, @errrecs, @errmsg, @wrnrecs);
                  if (not resultproc)
                     if(strlen(errmsg) <= 0)
                       errmsg = "Неизвестная ошибка при обработке процесса расчета комиссий по валютному рынку";
                     end;
                     println(errmsg);
                  else
                  end;
                  //println("Рассчитано: " + recs);
                  //println("Ошибок: " + errrecs);
                  //println("Предупреждений: " + wrnrecs);

                  while(i < result_protocol.size)
                     print (result_protocol.(i));
                     i = i + 1;
                  end;
               end;
            else 
               var sql_comm = "SELECT p.t_dlcontrid,sf.t_id,sf.t_servkind,mp.t_marketid, sf.t_number " +
                              "  FROM DDL_PANELCONTR_DBT p, ddlcontrmp_dbt mp, dsfcontr_dbt sf " +
                              " WHERE   p.t_calc_sid ='X' and p.t_dlcontrid = mp.t_dlcontrid " +
                              "       AND mp.t_sfcontrid = sf.t_id " +
                              "       AND sf.t_servkind <> 15 " +
                              "       AND sf.t_servkindsub = 8 " +
                              "       AND p.t_setflag = CHR (88) order by sf.t_servkind, p.t_dlcontrid";
               Dt = TRsbDataSet(sql_comm);
               InitProgress(SQL_GetNRecs(sql_comm), "Расчет комиссий по выбранным клиентам" );
               var _recs=0,_errrecs=0,_wrnrecs=0;
               while (Dt.movenext())
                  resultproc = false; i=0;
                  if ((this.getControl("securComm").checked) and (Dt.servkind == 1))
                     resultproc = CalcComiss ( GetDateAfterWorkDays(this.getControl("CalcDate").value,-1), dt.id, @result_protocol, @recs, @errrecs, @errmsg, @wrnrecs);
                     if (not resultproc)
                        if(strlen(errmsg) <= 0)
                          errmsg = "Неизвестная ошибка при обработке процесса расчета комиссий по фондовому рынку по договору " + dt.t_number;
                        end;
                        println(dt.number + ":  " + errmsg);
                     else
                     end;
                     println(dt.number);
                     while(i < result_protocol.size)
                        if ( not (  (index(result_protocol.(i),"Директор") > 0) 
                             or (index(result_protocol.(i),"бухгалтер") > 0) 
                             or (index(result_protocol.(i),"Исполнитель") > 0) 
                             or (index(result_protocol.(i),"подпись") > 0) 
                             or (index(result_protocol.(i),"Телефон") > 0) 
                             or (strlen(result_protocol.(i)) == 0) 
                            ) )
                           print (result_protocol.(i));
                        end;
                        i = i + 1;
                     end;
                     println;

                  elif((this.getControl("curComm").checked) and (Dt.servkind == 21))
                     SetGlobalParameter("BO_ACT_INITIATOR", "ФИССиКО");
                     resultproc =  CalcComiss ( GetDateAfterWorkDays(this.getControl("CalcDate").value,-1), dt.id, @result_protocol, @recs, @errrecs, @errmsg, @wrnrecs);
                     if (not resultproc)
                        if(strlen(errmsg) <= 0)
                          errmsg = "Неизвестная ошибка при обработке процесса расчета комиссий по валютному рынку по договору " + dt.t_number;
                        end;
                        println(dt.number + ":  " + errmsg);
                     else
                     end;
                     println(dt.number);
                     while(i < result_protocol.size)
                        if ( not (  (index(result_protocol.(i),"Директор") > 0) 
                             or (index(result_protocol.(i),"бухгалтер") > 0) 
                             or (index(result_protocol.(i),"Исполнитель") > 0) 
                             or (index(result_protocol.(i),"подпись") > 0) 
                             or (index(result_protocol.(i),"Телефон") > 0) 
                             or (strlen(result_protocol.(i)) == 0) 
                            ) )
                           print (result_protocol.(i));
                        end;
                        i = i + 1;
                     end;
                  println;

               end;
                  _recs = _recs + recs;
                  _errrecs = _errrecs + errrecs;
                  _wrnrecs = _wrnrecs + wrnrecs;
                  UseProgress(j=j+1);
               end;
               RemProgress();
               //println("Рассчитано: " + recs);
               //println("Ошибок: " + errrecs);
               //println("Предупреждений: " + wrnrecs);

            end;
         end;


         /*** расчет лимитов ***/
         if (this.getControl("calc").checked)
            RemProgress ;
            InitProgress(-1,"Проверка данных", "Проверка данных");

            var  NeedCalc = true;
            msg = "";
            var quikFilesExists = false;
            var zeroPriceCount = 0, notZeroPriceCount = 0, totalCount = 0;
            var calcPart = 0;

            if ((this.getControl("byStockMB").checked or this.getControl("byStockSPB").checked) and
                (QuikFilesAutoExists())
               )
               if (not IsCalcWaPositionPrice())
                  msg = "В директории \""+ GetQuikInAutoFolder()+"\" обнаружен необработанный вечерний файл QUIK! "
                       +"Влияние на корректность цен приобретения и сверку лимитов. Продолжить расчет?";
                  if (MsgBoxEx(msg, MB_YES + MB_NO, IND_NO) == IND_NO)
                     NeedCalc = false;
                  end;
               end;
               quikFilesExists = true;
            end;


            //var Sessions = TArray();
            msg = "";
            var CalcExistsStockMB = CheckCalcExists(MARKET_KIND_STOCK, MMVB_ID(), this.getControl("CalcDate").value);
            var CalcExistsStockSPB = CheckCalcExists(MARKET_KIND_STOCK, SPB_ID() , this.getControl("CalcDate").value);
            var CalcExistsCurrMB = CheckCalcExists(MARKET_KIND_CURR , MMVB_ID(), this.getControl("CalcDate").value);
            var CalcExistsDerivMB = CheckCalcExists(MARKET_KIND_DERIV, MMVB_ID() , this.getControl("CalcDate").value);
            var CalcExistsEDP = CheckCalcExists(MARKET_KIND_EDP  , MMVB_ID() , this.getControl("CalcDate").value);
            if (   (CalcExistsStockMB  and this.getControl("byStockMB") .checked )
                or (CalcExistsStockSPB  and this.getControl("byStockSPB").checked )
                or (CalcExistsCurrMB  and this.getControl("byCurMB")   .checked )
                or (CalcExistsDerivMB  and this.getControl("byFortsMB") .checked )
                or (CalcExistsEDP  and this.getControl("byEDP")     .checked )
               )
               msg = "В системе уже есть рассчитанные на " +this.getControl("CalcDate").value+ " лимиты по следующим рынкам:";
               if ( CalcExistsStockMB and this.getControl("byStockMB") .checked )
                  msg = msg + "|фондовый МБ";
               end;
               if ( CalcExistsStockSPB and this.getControl("byStockSPB").checked )
                  msg = msg + iif(StrLen(msg) != index(msg,":"),"/", "|") + "фондовый СПБ";
               end;
               if ( CalcExistsCurrMB and this.getControl("byCurMB")   .checked )
                  msg = msg + iif(StrLen(msg) != index(msg,":"),"/", "|") + "валютный МБ";
               end;
               if ( CalcExistsDerivMB and this.getControl("byFortsMB") .checked )
                  msg = msg + iif(StrLen(msg) != index(msg,":"),"/", "|") + "срочный МБ";
               end;
               if ( CalcExistsEDP and this.getControl("byEDP")     .checked )
                  msg = msg + iif(StrLen(msg) != index(msg,":"),"/", "|") + "ЕДП";
               end;
               msg = msg + "| Новый расчет приведет к их удалению" + iif(this.getControl("forAll").checked,""," по выбранным клиентам") + "! Продолжить расчет?";
            end;

            if ((msg != "") and (NeedCalc))
               if (MsgBoxEx(msg, MB_YES + MB_NO, IND_YES) == IND_NO)
                  NeedCalc = false;
               end;
            end;
            var calc_direct,ErrorCode = 0, ErrorDesc = "", calc_wrnrecs = 0, calc_recs = 0 ;
            var calc_protocol = TStrCollector;

            if (NeedCalc)
               ClearLog();
               calc_direct = CreateLimits_calc_direct(this.getControl("CalcDate").value, -1 ,
                         this.getControl("byStockMB").checked,
                         this.getControl("byStockSPB").checked,
                         this.getControl("byCurMB").checked,
                         this.getControl("byFortsMB").checked,
                         this.getControl("byEDP").checked,
                         (not this.getControl("forAll").checked),
                         @ErrorCode, @ErrorDesc) ;

               calc_protocol = GetProtocol_calc_direct(calc_direct,-1 , 
                         this.getControl("byStockMB").checked,
                         this.getControl("byStockSPB").checked,
                         this.getControl("byCurMB").checked,
                         this.getControl("byFortsMB").checked,
                         this.getControl("byEDP").checked,
                         (not this.getControl("forAll").checked),
                          @calc_recs, @calc_wrnrecs, @ErrorCode, @ErrorDesc) ;

               if (ErrorCode != 0)
                  NeedCalc = false;
                  MsgBox(ErrorDesc);
               end;
            end;
            if (calc_protocol.size > 1)
              for ( i, 0, calc_protocol.size  - 1)
                println (calc_protocol(i));
              end;
            end;
            SetProtocol_calc_direct(calc_direct,ErrorCode,ErrorDesc,calc_protocol);
            /*обновить даты расчетов на панели*/
            UpdateDateFields();
            this.ReDraw;

         end;
         RemProgress ;

         /*выгрузка в файл*/
         if (this.getControl("unload").checked)
            var path_f = "", path_s = "";
            msg = "";
            ws_lmt_to_buf(null, 
                          this.getControl("CalcDate").value,
                          @path_f, 
                          @path_s, 
                          @msg, 
                          (this.getControl("byStockMB").checked or this.getControl("byStockSPB").checked), 
                          this.getControl("byFortsMB").checked, 
                          this.getControl("byCurMB").checked, 
                          this.getControl("byEDP").checked, 
                          null, 
                          ifthenelse(((this.getControl("byStockMB").checked or this.getControl("byCurMB").checked or this.getControl("byFortsMB").checked) and (this.getControl("byStockSPB").checked)),-1, ifthenelse(this.getControl("byStockSPB").checked, SPB_ID(), MMVB_ID() )), 
                          iif(this.getControl("forAll").checked, false, this.getControl("unload_sel").checked), 
                          iif(this.getControl("forAll").checked, false, this.getControl("delete_sel").checked) );
            if (this.getControl("byCurMB").checked)
               this.getControl("PathUnloadCMB").value = path_f;
            end;
            if (this.getControl("byStockMB").checked)
               this.getControl("PathUnloadSMB").value = path_f;
            end;
            if (this.getControl("byStockSPB").checked)
               this.getControl("PathUnloadSSPB").value = path_f;
            end;
            if (this.getControl("byEDP").checked)
               this.getControl("PathUnloadEDP").value = path_f;
            end;
            if (this.getControl("byFortsMB").checked)
               this.getControl("PathUnloadFMB").value = path_s;
            end;
            println(string(msg:50) + string(time()));
         end;

         /* сверка */
         var stat = 0;
         if (this.getControl("compare").checked)
            if (   this.getControl("byStockMB").checked 
                or this.getControl("byStockSPB").checked 
                or this.getControl("byCurMB").checked
                or this.getControl("byEDP").checked )

               if (this.getControl("reload").checked)
                  stat = LoadDataSecur(this.getControl("CalcDate").value);
               end;
               if (not stat)
                  var isAdd = IsReviseAdditional();
                  var quikLoadKind = null;
                  if (isAdd)
                     var quikLoader = QuikDealsLoader();
                     quikLoadKind = quikLoader.PanelStart(
                        GetPrevTradeDateFromLim(this.getControl("byStockMB").checked or 
                                                this.getControl("byStockSPB").checked or
                                                this.getControl("byEDP").checked, this.getControl("byCurMB").checked));
                     if (quikLoadKind == REVISE_KIND_NORMAL)
                       isAdd = false;
                     end;
                     quikLoader = null;
                  end;
                  if ((quikLoadKind == null) or (quikLoadKind != REVISE_KIND_ABORT))
                     CheckDataSecur(this.getControl("byStockMB").checked or this.getControl("byStockSPB").checked or this.getControl("byEDP").checked, this.getControl("byCurMB").checked,
                                    null, null, null, isAdd);
                     var market_str = "";
                     if   ( (this.getControl("byStockMB").checked or this.getControl("byStockSPB").checked or this.getControl("byEDP").checked) 
                        and this.getControl("byCurMB")  .checked) market_str = "фондовому и валютному рынкам";
                     elif (this.getControl("byStockMB").checked or this.getControl("byStockSPB").checked or this.getControl("byEDP").checked) market_str = "фондовому рынку";
                     elif (this.getControl("byCurMB")  .checked) market_str = "валютному рынку";
                     end;
                     Println(string("Сверка по "+market_str+" завершена":50) + string(time()));
                  end;
               end;
            end;
            if (this.getControl("byFortsMB").checked)
               if (this.getControl("reload").checked)
                  stat = LoadDataFutur(this.getControl("CalcDate").value);
               end;
               if (not stat)
                  CheckDataFutur(this.getControl("CalcDate").value);
                  println(string("Сверка по срочному рынку завершена":50) + string(time()));
               end;
            end;
         end;


         if (not (this.getControl("calc").checked 
               or this.getControl("calcComm").checked 
               or this.getControl("compare").checked 
               or this.getControl("unload").checked )
            )
            MSgBox("Выберите действие");
         end;

         ViewFile(SetOutPut(null, true));
         SetOutPut(null, true)

      end;/*end F2*/

   OnError(e)
      RemProgress ;
      EndAction;
      Msgbox("Ошибка выполнения:|" + e.message + "|Module:" + e.Module+ "|line:"+e.line);
   end; /*onKeyPressed*/
end; /*class Pan*/

var _locker = ExclusiveLimCalcLocker();
if (_locker.Lock())
   var myPanel:TRsbPanel = Pan;
   myPanel.run;
else
   MsgBox("Запущен другой экземпляр расчета лимитов. Повторный запуск невозможен.|(если какой-либо из расчетов завершен аварийно, рекоммендуется перезапустить окна RS-Bank)");
end;


//SetORATrace(false);
//SetRStrace(false);


exit(1);
//ChekDataSecur;                  
