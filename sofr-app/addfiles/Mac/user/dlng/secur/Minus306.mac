/*****************************************************************************/
/*                                                           R-Style Softlab */
/*                                                                           */
/*               Отчет по отрицательным остаткам 306-х счетов                */
/*                                                                           */
/*  Create : Kadin D.V.                                                      */
/*  Date   : 21.05.2019                                                      */
/*****************************************************************************/
import RsbFormsInter, globals, Календарь, sp_class, captureoutput;

private const FT_INT32  = 1;
private const FT_STRING = 7;
private const FT_DATE   = 9;

//Переменные для варианта WindowsReports, поддерживающего POIReports
private var
  csvFile, fName, csvTable, printEngine;
  const TEMPL_NAME  = "Report_m306.xlsx";
  const FILE_REPORT = "Report_m306";
  const CSV_NAME    = "Report_m306_csv.txt";
  var SHEET_NAME = "Отчет";

var ExitCode:integer, FProcPanel:TRsbPanel, Rep, rn = 1, sheet = 1;

private var _sqlString:string;

macro print_header(Type, Header_str)
 
// var EXCEL_MONEY = "\"# ##0,0000\"";
  var EXCEL_MONEY = "\"##0,0000;-##0,0000;##0,0000\"";
  Rep.SetExecute("iBook.Sheets(1).Columns(1).NumberFormat = "+EXCEL_MONEY+";");
 
  Rep.AddEmptyStr();
  Rep.AddEmptyStr();

  Rep.AddPrintCell("Остаток",            20, null, "c:ex_FS(b):ex_B(tblr)");
  Rep.AddPrintCell("Номер счета",                    20, null, "c:ex_FS(b):ex_B(tblr)");
  Rep.AddPrintCell("Краткое наименование клиента",              60, null, "c:ex_FS(b):ex_B(tblr)");
  Rep.AddPrintCell("Номер ДБО",              20, null, "c:ex_FS(b):ex_B(tblr)");
  Rep.AddPrintCell("Дата",              10, null, "c:ex_FS(b):ex_B(tblr)");
  
  Rep.AddStr();
end;

macro PrintStr(RestAcc, AccNumber, ShortName, Number, RepDate)    
  Rep.AddPrintCell(RestAcc,      20, null, "r:ex_FS():ex_B(tblr)");
  Rep.AddPrintCell(AccNumber,      20, null, "c:ex_FS():ex_B(tblr)");
  Rep.AddPrintCell(ShortName,      60, null, "c:ex_FS():ex_B(tblr)");
  Rep.AddPrintCell(Number,          20, null, "c:ex_FS():ex_B(tblr)");
  Rep.AddPrintCell(RepDate, 10, null, "r:ex_FS():ex_B(tblr)");
  Rep.AddStr();
end;

macro PrintStrXLS(RestAcc, AccNumber, ShortName, Number, RepDate)   
  if (RestAcc < 0) 
    csvFile.AddCell( RestAcc);
    csvFile.AddCell( AccNumber); 
    csvFile.AddCell( ShortName); 
    csvFile.AddCell( Number); 
    csvFile.AddCell( RepDate);  
    csvFile.AddRow();  
  end;
end;

macro GetData(BegDate)
  csvFile.FileOpen(CSV_NAME, true);
  var rs, cmd;
  var sql = 

" select RSI_RSB_ACCOUNT.RESTALL( ac.t_account, "+
"                   ac.t_chapter, "+
"                  ac.t_code_currency, "+
"                   "+GetSqlDate(BegDate)+
"                 ) as Rest, cl.t_shortname, sf.t_number, ac.* from daccount_dbt ac, dparty_dbt cl, dmcaccdoc_dbt doc, dsfcontr_dbt sf, ddlcontr_dbt dl, ddlcontrmp_dbt con   where ac.t_balance in ('30601','30606') " +
"                 and RSI_RSB_ACCOUNT.RESTALL( ac.t_account, "+
"                   ac.t_chapter, "+
"                   ac.t_code_currency, "+
"                   "+GetSqlDate(BegDate) +
"                 ) <0 " +
"                 and cl.t_partyid = ac.t_client and doc.t_account = ac.t_account and doc.t_iscommon = 'X' and doc.t_catnum = 201 "+
"                 and con.t_sfcontrid =doc.t_clientcontrid and dl.t_dlcontrid = con.t_dlcontrid and SF.T_ID = dl.t_sfcontrid  ";

  cmd = RSDCommand(sql);
  rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);

  var cnt = 0;
  InitProgress(SQL_GetNRecs(sql),"Счета с отрицательным остатком", "Счета с отрицательным остатком");
  printEngine.SetValue_NameCell("repheader", "Счета с отрицательным остатком на дату " +  BegDate);
  while(rs and rs.MoveNext())
    PrintStrXLS(rs.value("rest"), rs.value("t_account"), rs.value("t_shortname"), rs.value("t_number"), BegDate);
    UseProgress(cnt=cnt+1);
  end;
  csvFile.FileClose();

  RemProgress;
end;

class(TRsbPanel) DatePanel()

  var LBegDate:TRsbLabel, ,
      FieldBegDate:TRsbEditField, ,
      BegDate:date = date(0,0,0);

  InitTRsbPanel();
  SetCaption("Выберите дату отчета");
  SetStatus("~Esc~ Выход ~F3~ Календарь ~F2~ Выполнить ~F9~ Выполнить");
  SetSize(28, 4);
  SetPosition(45,10);
  
  LBegDate = TRsbLabel(3, 2, "За дату:");
  addLabel(LBegDate);
  
  FieldBegDate = TRsbEditField(FT_DATE);
  FieldBegDate.setPosition(15,2);
  FieldBegDate.setSize(10,1);
  FieldBegDate.Name = "BegDate";
  FieldBegDate.bindValue( this, "BegDate" );
  FieldBegDate.value = {Curdate};
  addControl(FieldBegDate);
  
  
  addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "KeyEvent"));//KeyEvent
  
  
  macro KeyEvent(RsbEvent:Object)
    var focusedControl;
    
    if (rsbEvent.KeyCode == 27)//ESC
      this.Close(rsbEvent.KeyCode);
    elif ((rsbEvent.KeyCode == 316) or (rsbEvent.KeyCode == 323))//F2 or F9
      BegDate = FieldBegDate.value;
      this.Close(rsbEvent.KeyCode);
    elif (rsbEvent.KeyCode == 317)//F3
      focusedControl = this.focusedControl();
      if(focusedControl.Name == "BegDate")
        BegDate = GetDateByCalendar(BegDate);
        FieldBegDate.Value = BegDate;
      end;
    end;
  end;
  
end;


FProcPanel = DatePanel();
ExitCode = FProcPanel.Run();

if(ExitCode)
  printEngine = CTemplateSXLSX( NULL );
  if(printEngine.UsePoi())
      printEngine.SetXLSXFormat();
  end;

  printEngine.CreateTotalBook();
  printEngine.OpenTemplate(TEMPL_NAME, true);
  printEngine.SheetChange(0);
  csvTable = printEngine.RegisterTable("tableRow");
  csvFile = C_CSVFile();
  GetData(FProcPanel.BegDate);
  csvTable.FillTabelFromCSV(csvFile.GetlsFileName());
  printEngine.CopyAllSheetInTotalBook (null, false, csvTable.GetTableRange(), 0, SHEET_NAME);
  csvTable.EndTable();
  printEngine.Close();
  printEngine.SaveTotalBook(GetUniqAdd(FILE_REPORT));

end;
exit(1);

