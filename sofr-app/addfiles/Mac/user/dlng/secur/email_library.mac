/*
  $Name:        email_library.mac
  $Description: Библиотека для отправки уведомлений по электронной почте.
  $Author:      Попов А.В.
  убрать комменты в строке 90!
*/
import BankInter, rcw;

/*
  Класс для получения настроек SMTP сервера из реестра банка
    Технически для рассылок из депозитария может использоваться адрес depo_bot@bank.ru, а для рассылки из ценных бумаг secur_bot@bank.ru,
    таким образом им будет соотвествовать одинаковые настройки: SMTP_SERVER_PATH и SMTP_SERVER_PORT_PATH, но разные SMTP_USERNAME_PATH и SMTP_PASSWORD_PATH.
    Тогда мы можем просто отнаследоваться от класса SmtpSettings и переопределить SMTP_USERNAME_PATH и SMTP_PASSWORD_PATH в производном классе.
*/
class SmtpSettings()
  //Пути в реестре банка по умолчанию
  private var SMTP_SERVER_PATH      = "COMMON\\EMAIL\\MAIL_SMTPSERVER";
  private var SMTP_SERVER_PORT_PATH = "COMMON\\EMAIL\\MAIL_SERVERPORT";
  private var SMTP_USERNAME_PATH    = "COMMON\\EMAIL\\MAIL_USERNAME";
  private var SMTP_PASSWORD_PATH    = "COMMON\\EMAIL\\MAIL_PASSWORD";
  private var SMTP_USESSL_PATH      = "COMMON\\EMAIL\\MAIL_USESSL";
  private var SMTP_USESMTPAUTH_PATH = "COMMON\\EMAIL\\MAIL_USESMTPAUTH";

  //Значения настроек по умолчанию
  var SMTP_SERVER         = "";
  var SMTP_SERVER_PORT    = 25;
  var SMTP_USERNAME       = "";
  var SMTP_PASSWORD       = "";
  var SMTP_USESSL         = false;
  var SMTP_USESMTPAUTH    = false;
  private var error_array = TArray();

  //Функция вернет значение настройки реестра
  //  RegistryPath:string - путь в реестре для получения значения настройки
  //  RegType:integer     - тип значения настройки (V_STRING, V_BOOL, V_INTEGER)
  private macro GetRegValue(RegistryPath:string, RegType:integer)
    var val  = "";
    var stat = 0;
    var RegistryPathNotFound    = string("Не найдена настройка в реестре банка:", RegistryPath);
    var RegistryValueNotDefined = string("Не задано значение настройки:", RegistryPath);

    GetRegistryValue(RegistryPath, RegType, val, stat);
    if(stat)
      error_array(error_array.size) = RegistryPathNotFound;
    end;
    if((RegType == V_STRING) and (val == ""))
      error_array(error_array.size) = RegistryValueNotDefined;
    elif((RegType == V_INTEGER) and (val <= 0))
      error_array(error_array.size) = RegistryValueNotDefined;
    elif(val == "")
      error_array(error_array.size) = RegistryValueNotDefined;
    end;
    return val;
  end;

  //Конструктор класса
  macro construct()
    SMTP_SERVER      = GetRegValue(SMTP_SERVER_PATH,      V_STRING);
    SMTP_SERVER_PORT = GetRegValue(SMTP_SERVER_PORT_PATH, V_INTEGER);
    SMTP_USERNAME    = GetRegValue(SMTP_USERNAME_PATH,    V_STRING);
    SMTP_PASSWORD    = GetRegValue(SMTP_PASSWORD_PATH,    V_STRING);
    SMTP_USESSL      = GetRegValue(SMTP_USESSL_PATH,      V_BOOL);
    SMTP_USESMTPAUTH = GetRegValue(SMTP_USESMTPAUTH_PATH, V_BOOL);
  end;

  //Вывод настроек на печать
  macro prnConfig()
    println("SMTP_SERVER      = " + SMTP_SERVER);
    println("SMTP_SERVER_PORT = " + SMTP_SERVER_PORT);
    println("SMTP_USERNAME    = " + SMTP_USERNAME);
    println("SMTP_PASSWORD    = " + SMTP_PASSWORD);
    print  ("SMTP_USESSL      = ");
    println(SMTP_USESSL);
    print  ("SMTP_USESMTPAUTH = ");
    println(SMTP_USESMTPAUTH);
  end;

  //Функция вернет 0 если при инициализации класса не было ошибок:
  // - пути настроек в реестре существуют и для них определены значения
  macro errorCount()
    return error_array.size;
  end;

  //Фнукция печати ошибок в настройке реестра
  macro prnErrors()
    var error;
    if(errorCount() > 0)
      println(" При чтении настроек для отправки уведомлений по электронной почте, обнаружены следующие ошибки:");
      for(error, error_array)
        println("  - " + error + ";");
      end;
    end;
  end;

  //construct();
end;

//Класс для отправки сообщений посредством электронной почты
class MailSender(config: SmtpSettings)
  //Настройки SMTP сервера
  private var configuration: SmtpSettings = null;
  private var ErrorMessage = "";

  //Конструктор класса
  macro construct(config)
    if(config != null)
      configuration = config;
    else
      configuration = SmtpSettings();
    end;
  end;

  macro errorCount()
    return configuration.errorCount();
  end;

  macro prnErrors()
    configuration.prnErrors();
  end;

  macro prnConfig()
    configuration.prnConfig();
  end;

  macro getSendErrorMessage()
    return ErrorMessage;
  end;

  //Функция для отправки одинаковых сообщений по электронной почте списку получателей
  macro send(Sender, Recipients, Subject, Body, Attachment)
    var attach, Recipient;
    var result      = 0;
    var jvm = createObject( "rsjvm", "TJavaHost" );
    var RsMail = jvm.CreateJavaObject( "ru.softlab.core.mail.RsMail", "<init>" );
    var attachments = TArray();

    if((not IsEqClass("TArray", Recipients)) and (VALTYPE(Recipients) != V_STRING))
      ErrorMessage = "Список получателей должен быть задан, в виде строки или массива TArray";
      return 1;
    end;

    if(RsMail != null)
       var StrUseSSL = "";
       if(configuration.SMTP_USESSL)
         StrUseSSL = "X";
       end;

       var StrUseSmtpAuth = "";
       if(configuration.SMTP_USESMTPAUTH)
         StrUseSmtpAuth = "X";
       end;

       RsMail.Sender(configuration.SMTP_USERNAME, configuration.SMTP_PASSWORD, configuration.SMTP_SERVER, String(configuration.SMTP_SERVER_PORT), StrUseSSL, StrUseSmtpAuth);
       RsMail.ClearListAttach();

       if(Attachment != null)
        //Сформировать массив файлов для отправки как вложений в эл. сообщении.
        if(VALTYPE(Attachment) == V_STRING)
          attachments(0) = Attachment;
        elif(IsEqClass("TArray", Attachment))
          attachments = TArray(Attachment);
        end;

        //Добавляем вложения в сообщение
        for(attach, attachments)
          if(ExistFile(attach))
            RsMail.AddAttachment(attach);
          else
            ErrorMessage = "Файл: " + Attachment + " не найден. Невозможно прикрепить файл к сообщению.";
            return 1;
          end;          
        end;
      end;

      var email_to = "";
      if(IsEqClass("TArray", Recipients))
        for(Recipient, Recipients)
          email_to = email_to + Recipient + ";";
        end;
      else
        email_to = Recipients;
      end;
      
      var HtmlBody = "<!doctype html> <html> <body>" + Body + "</html> </body>";      
      result = RsMail.SendHtml(subject, HtmlBody, configuration.SMTP_USERNAME, Sender, email_to, ""/*email_bcc*/, ""/*email_cc*/);
    else
      ErrorMessage = "Ошибка создания RSCOM объекта для отправки сообщения.";
      return 1;
    end;
    return result;

    onError(er)
      if(IsEqClass("TRsComErr", er.err))
        ErrorMessage = er.err.Message(1);
      elif(er.AxCode)
        ErrorMessage = er.AxMes;
      end;

      if(ErrorMessage == "")
        ErrorMessage = er.message;
      end;

      return 1;
  end;

  construct(config);
end;

