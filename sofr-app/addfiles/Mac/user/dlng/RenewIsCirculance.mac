/** 
 @file RenewIsCirculance.mac
 @brief Обновление признака обращаемости для ранее созданных объектов
  
 # tag 
 - functional_block:Налоги
 - code_type:API

   
 # changeLog 
 |date       |author         |tasks            |note                            
 |-----------|---------------|-----------------|--------------------------------
 |24.06.2024 |Гераськина Т.В.| CCBO-3366       | Создание
*/ 

import rsexts, rsd;
import globals, cb_sql;
import "usr_open_files.mac";

var g_count_diff = 0;
var g_count_newval_obj = 0;

/**
  @brief Класс логирования
*/
class Logger()

   /**
      @brief Печать заголовока лога
      @param[in] dt Дата формирования
      @param[in] tm Время формирования
   */
   macro RepHeader(dt, tm)
      var str;
      str =     "                             ОТЧЕТ  \n";
      str = str+"   обновление информации по ранее загруженным объектам BOSS-3366 \n";
      str = str+"\n";
      str = str+"Дата загрузки  : "+dt+"\n";
      str = str+"Время загрузки : "+tm+"\n";
      str = str+"┌─┬─────────────────────────┬──────────┬───────────────┬─────────────────────────────────────────────────────────────────────────────────────────┐\n";
      str = str+"│ │Код                      │Дата      │Статус загрузки│Комментарий                                                                              │\n";
      str = str+"├─┼─────────────────────────┼──────────┼───────────────┼─────────────────────────────────────────────────────────────────────────────────────────┤";
      println(str);
   end;

   /**
      @brief Формирование дополнительной строки комментария
      @param[in] _text Дополнительный текст
      @return сформированная отформатированная строка
      
      Первые 4 поля намеренно ничем не заполняются, так как это вывод дополнительной информации, данные по коду, дате и статусу выведены строками выше
      Строка не печается, а только формируется, так как доп.иформация многостроковая
   */
   macro MakeCommString(_text)
      var str = "│"+ string(""         :1)   +"│"+
                     string(""         :25:l)+"│"+
                     string(""         :10:l)+"│"+
                     string(""         :15:l)+"│"+
                     string(_text      :89:0)+"│";
      return str;
   end;
   
   
   /**
      @brief Печать подвала лога
      @param[in] success Количество успешно обработанных записей
      @param[in] error Количество записей с ошибкой
      @param[in] total Общее количество записей
   */
   macro RepFooter(success, error, total)
      var rs;
      var cdrec_count = 0;
   
      var str = "└─┴─────────────────────────┴──────────┴───────────────┴─────────────────────────────────────────────────────────────────────────────────────────┘\n";
      str = str+"Загружено успешно из файла: "+success+"\n";
      str = str+"Не загружено из файла     : "+error+"\n";
      str = str+"Всего записей в файле     : "+total+"\n\n";
      
      rs = RSDRecordset("select count(*) as cnt from dcdrecords_dbt ");
      if (rs.movenext())
         cdrec_count = SQL_ConvTypeInteger(rs.value("cnt"));
      end;
      
      str = str+"Всего записей по КД в таблице dcdrecords_dbt на дату запуска:                                                      "+cdrec_count+"\n";
      str = str+"Количество обновленных записей в таблице dcdrecords_dbt:                                                           "+success+"\n";
      str = str+"Количество ЦБ из КД вида INTR, по которым отличается признак обращаемости на T_PAYMENTDATE и на T_PAYRECEIVEDDATE: "+g_count_diff+"\n";
      str = str+"Количество НДР, созданных по этим КД в которых необходимо обновить признак обращаемости:                           "+g_count_newval_obj;

      println(str);
   end;

   /**
      @brief Печать строки лога
      @param[in] _err Признак ошибки: пусто - успех, "X" - ошибка
      @param[in] _code ISIN ценной бумаги
      @param[in] _paymentdate Дата платежа
      @param[in] _stat Признак ошибки: false - успех, true - ошибка
      @param[in] _paymentdate Дата платежа
   */
   macro RepStr(_err,_code, _paymentdate, _stat,_comm)
      var status;
      if (ValType(_stat) ==  V_BOOL)
         if (not _stat)
            status = "Загружено";
         else
            status = "Не загружено";
         end;
      else 
         status = "";
      end;
      var str = "│"+ string(_err          :1)   +"│"+
                     string(_code         :25:l)+"│"+
                     string(_paymentdate  :10:l)+"│"+
                     string(status        :15:l)+"│"+
                     string(_comm         :89:0)+"│";
      println(str);
   end;

end;

/**
  @brief Класс обработки данных из файла
*/
class c_DataFromFile()
   var ISIN;
   var PaymentId;
   var PaymentDate;
   var PayReceivedDate;
   var FixingDate;
   var PaymentSecurityQuantity;
   
   var id_arr = TArray();
   var v_id = -1;
   var v_text_compare;
   var v_flagchange;
   
   /**
      @brief Преобразование данных из строки в ожидаемые типы данных
   */
   macro TransformFields()
      if (ValType(PaymentId) == V_STRING)
         PaymentId = double(PaymentId);  // длина идентификатора больше 10 цифр, хотя это целое число
      end;
      if (ValType(PaymentDate) == V_STRING)
         PaymentDate = date(PaymentDate);
      end;
      if (ValType(PayReceivedDate) == V_STRING)
         PayReceivedDate = date(PayReceivedDate);
      end;      
      if (ValType(FixingDate) == V_STRING)
         FixingDate = date(FixingDate);
      end;
      if (ValType(PaymentSecurityQuantity) == V_STRING)
         PaymentSecurityQuantity = double(PaymentSecurityQuantity);
      end;
   end;
   
   /**
      @brief Найти запись по ISIN, ID и дате платежа
   */
   macro FindRecord()
      var sql_str, cmd, rs;
      var ai = id_arr.size();
      sql_str = String("select t_id from dcdrecords_dbt                             \n"+
                       " where t_isinregistrationnumber = :p_isinregistrationnumber \n"+
                       "   and t_recordpaymentid = :p_recordpaymentid               \n"+
                       "   and t_paymentdate = :p_paymentdate                        ");
      cmd = RSDCommand(sql_str);
      cmd.AddParam("p_isinregistrationnumber", RSDBP_IN, ISIN);
      cmd.AddParam("p_recordpaymentid"       , RSDBP_IN, PaymentId);
      cmd.AddParam("p_paymentdate"           , RSDBP_IN, PaymentDate);
      rs = RSDRecordSet(cmd);
      while (rs.movenext())
         id_arr.value(ai) = Int(rs.value("t_id"));
         ai = ai+1;
      end;
   end;
   
   /**
      @brief Обновить запись данными из файла
   */
   macro UpdateRecord()
      var sql_str, cmd, rs;
      var ai = 0; 
      var sql_filter = "("+id_arr.value(ai); // массив точно содержит хотя бы 1 элемент
      
      ai = ai+1;
      while (ai < id_arr.size())
         sql_filter = sql_filter + "," + id_arr.value(ai);
         ai = ai+1;
      end;
      sql_filter = sql_filter+")";
      
      sql_str = String("update dcdrecords_dbt                           \n"+
                       "   set t_payreceiveddate = :p_payreceiveddate,  \n"+
                       "       t_fixingdate = :p_fixingdate,            \n"+
                       "       t_quantity = :p_quantity                 \n"+
                       " where t_id in ", sql_filter                     );
      
      cmd = RSDCommand(sql_str);
      cmd.AddParam("p_payreceiveddate" , RSDBP_IN, PayReceivedDate);
      cmd.AddParam("p_fixingdate"      , RSDBP_IN, FixingDate);
      cmd.AddParam("p_quantity"        , RSDBP_IN, PaymentSecurityQuantity);
      cmd.AddParam("p_id"              , RSDBP_IN, v_id);
      cmd.execute();
      
   end;
   
   /**
      @brief Изменить признак обращаемости, если подходящие данные будут найдены
      @param[in] logg Лог
      
      После обработки сразу печатается строка лога с доп.комментарием
   */
   macro FindCirculate(logg:Logger, p_id)
      var sql_str, cmd, rs; 
      v_text_compare = " Изменений обращаемости нет";
      var add_text = "";
      var sql_str_upd, cmd_upd;
      var sql_obj, cmd_obj, rs_obj;
      var cnt = 0;                     
      sql_str = String("select d.t_isinregistrationnumber, av.t_fiid, d.t_recordpaymentid,    \n"+
                       "       d.t_paymentdate, d.t_payreceiveddate,                          \n"+
                       "       RSI_NPTO.Market1date(av.t_fiid, d.t_paymentdate) by_date1,     \n"+
                       "       RSI_NPTO.Market1date(av.t_fiid, d.t_payreceiveddate) by_date2  \n"+   
                       "  from dcdrecords_dbt d,                                              \n"+
                       "       davoiriss_dbt av                                               \n"+
                       " where av.t_isin = d.t_isinregistrationnumber                         \n"+
                       "   and d.t_corporateactiontype = 'INTR'                               \n"+
                       "   and d.t_id = :p_id                                                 ");

      sql_obj = String("select t_objid, t_date, t_client, t_analitic4\n"+
                       "  from dnptxobj_dbt                          \n"+
                       " where t_objid in (                          \n"+
                       "        select t_objid                       \n"+
                       "          from dcdnptxobdc_dbt               \n"+
                       "         where t_recid = :pcdid              \n"+
                       "         )                                    ");

      sql_str_upd = String("update dnptxobj_dbt             \n"+
                           "   set t_analitic4 = :pnew_val  \n"+
                           " where t_objid = :pobjid         ");

      cmd = RSDCommand(sql_str);
      cmd.AddParam("p_id", RSDBP_IN, p_id);
      rs = RSDRecordSet(cmd);
      if (rs.movenext())
         if (rs.value("by_date1") != rs.value("by_date2"))
            v_text_compare = "Признак обращаемости = "+SQL_ConvTypeInteger(rs.value("by_date1"))+" на "+SQL_ConvTypeDate(rs.value("t_paymentdate"))
                            +", признак обращаемости = "+SQL_ConvTypeInteger(rs.value("by_date2"))+" на "+SQL_ConvTypeDate(rs.value("t_payreceiveddate"));
                            
            g_count_diff = g_count_diff+1;
            
            // для вывода списка обновленных записей нужен цикл
            cmd_obj = RSDCommand(sql_obj);
            cmd_obj.AddParam("pcdid", RSDBP_IN, p_id);
            rs_obj = RSDRecordSet(cmd_obj);
            cnt = 0;
            add_text = logg.MakeCommString("   Cписок обновленных НДР для dcdrecords_dbt.t_id="+p_id+":");
            
            while (rs_obj.movenext())
               cmd_upd = RSDCommand(sql_str_upd);
               cmd_upd.AddParam("pnew_val", RSDBP_IN, rs.value("by_date2"));
               cmd_upd.AddParam("pobjid"  , RSDBP_IN, rs_obj.value("t_objid"));
               cmd_upd.execute();
               cmd_upd.close();
               
               add_text = add_text +"\n"+ logg.MakeCommString("      T_OBJID = "+rs_obj.value("t_objid")+", T_DATE = "+SQL_ConvTypeDate(rs_obj.value("t_date"))+", T_CLIENT="+rs_obj.value("t_client"));
               add_text = add_text +"\n"+ logg.MakeCommString("         обращаемость ДО обновления = "+SQL_ConvTypeInteger(rs_obj.value("t_analitic4"))
                                                            + " обращаемость ПОСЛЕ обновления = "+SQL_ConvTypeInteger(rs.value("by_date2")) );
               cnt = cnt + 1;
               g_count_newval_obj = g_count_newval_obj + 1;
            end;
            
            if (cnt == 0)
               add_text = add_text +"\n"+ logg.MakeCommString("      отсутствует");
            end;
         end;
      end;
      logg.RepStr("", ISIN, PaymentDate, true, v_text_compare);
      if (StrLen(add_text) > 1)
         println(add_text);
      end;
   end;

end;

/**
  @brief Запуск обработки
*/
macro runLoadData()
   var ErrStr = "",ErrStat = false;
   var row = 0;
   var all = 0,suc = 0, err = 0;
   var log = Logger();
   var dt = StrSubSt(string(Date()),".","")+StrSubSt(string(Time()),":","");
   var InfoLogFileName = GetTxtFileName( "load_data_"+dt);
   var ai = 0;

   var cur_file_mask = "*.csv";
   file datafile() txt;
   SetDelim(";");
   
   var RecFromFile;
   
   var state;

   var c_file = c_txt_file();
   var FullFileName;
   var stat;

   stat = c_file.openFile(datafile, cur_file_mask, true, false, FullFileName, "rsansi");

   if ( (valtype(stat) == V_UNDEF) or (stat == false) )
      msgbox("Ошибка открытия файла");
      return 0;
   end;

   /*Открываем файл*/
   BegAction(1, "Обработка файла \"" + c_file.localFileName + "\". Ждите...");

   /* формат файла, первая строка - всегда заголовок, данные со второй строки
   ISIN;PaymentId;PaymentDate;PayReceivedDate;FixingDate;PaymentSecurityQuantity
   
1	(ISIN)                     ISIN ц/б                                                 строка
2	(PaymentId)                ID платежа                                               число целое
3	(PaymentDate)              Дата платежа                                             дата
4	(PayReceivedDate)          Дата поступления на корр.счет Банка платежа от эмитента  дата
5	(FixingDate)               Дата фиксации списка владельцев                          дата
6	(PaymentSecurityQuantity)  Количество ц/б                                           число(32,12)
    */

   next(datafile);

   if (  (StrUpr(datafile(0)) == StrUpr("ISIN")) and
         (StrUpr(datafile(1)) == StrUpr("PaymentId")) )
   else 
      MsgBox("Файл имеет некорректный заголовок");
      EndAction(1);
      return 0;
   end;
   SetOutput( InfoLogFileName, true ); //Пишем лог в файл. Начало
   log.RepHeader(date(),time());

   //Бежим по файлу
   InitProgress(-1);
   while (next(datafile))
      if (StrLen(trim(datafile.str)) == 0)
         continue;
      end;
      
      RecFromFile = c_DataFromFile;
      RecFromFile.ISIN = datafile(0);
      RecFromFile.PaymentId = datafile(1);
      RecFromFile.PaymentDate = datafile(2);
      RecFromFile.PayReceivedDate = datafile(3);
      RecFromFile.FixingDate = datafile(4);
      RecFromFile.PaymentSecurityQuantity = datafile(5);
      RecFromFile.id_arr.size() = 0;

      RecFromFile.TransformFields();

      RecFromFile.FindRecord();
      if (RecFromFile.id_arr.size() == 0)
         ErrStat = true;
         ErrStr = "Не найдена запись с параметрами: "+RecFromFile.ISIN+" "+String(RecFromFile.PaymentId:20:0)+" "+RecFromFile.PaymentDate;
         log.RepStr("X", RecFromFile.ISIN, RecFromFile.PaymentDate, ErrStat, ErrStr);
         row = row + 1;
         err = err + 1;
         all = all + 1;
         continue;
      end;
      
      RecFromFile.UpdateRecord();
      ai = 0;
      while (ai < RecFromFile.id_arr.size())
         RecFromFile.FindCirculate(log, RecFromFile.id_arr.value(ai));
         ai = ai+1;
         suc = suc + 1;
      end;

      RecFromFile = NULL;

      row = row+1;
      UseProgress(row);
      all = all + 1;
      ErrStr = "";
      ErrStat = false;
   end;
   RemProgress();

   log.RepFooter(suc,err,all);
   
   SetOutput( NULL, true ); //Пишем лог в файл. Конец

   c_file.closeFile(datafile);
   ViewFile( InfoLogFileName );
end;

runLoadData();
exit(1); 

