import bankinter, rsd, "or_exl_h.mac", "likepy.mac", "dlQuery.mac";
var name_file = "";
class ReportTrustLoader()
   private var _activeSheet;
   var objExcel = CDAOMSExcel();

   macro get_bill_repaymentdate_sql(billnum)
       return " (select t_repaymentdate from dvsbanner_dbt where to_number(trim(t_bcnumber)) = "+billnum+" and rownum = 1) ";
   end;

   macro get_account_to_upload_sql(account)
      return " BEGIN " +
                " INSERT INTO usr_close_acc_dbt (T_ACCOUNTID, T_ACCOUNT) " +
                  " SELECT T_ACCOUNTID, T_ACCOUNT " +
                    " FROM daccount_dbt " +
                   " WHERE t_account = '"+account+"'; " +
             " EXCEPTION " +
                " WHEN DUP_VAL_ON_INDEX THEN NULL; " +
             " END; ";
   end;

   macro print_in_sql_file(sql_text)
      var close_acc_file = GetTXTFileName("close_accounts");
      close_acc_file = strsubst(close_acc_file,"."+UserNumber(),".sql");

      SetOutput(close_acc_file,true);
      println(sql_text);
      SetOutput(null,false);
   end;

   macro process_bill_table(column, row)
      private var excel_row = row+1;
      private var currentText = "";
      private var bill_number = "";
      private var current_bill_number = "";

      while(excel_row < _activeSheet.rows.count)
         currentText = _activeSheet.columns(column).rows(excel_row).text;
         bill_number = _activeSheet.columns(column+1).rows(excel_row).text;
         if(currentText == "")
            break;
         end;

         currentText = strsubst(SubStr(currentText,1,24),"-","");
         if(bill_number != "") 
            current_bill_number = bill_number;
         end;

         /*println*/
         print_in_sql_file(" UPDATE daccount_dbt SET t_open_close = chr("+int(codeFor("З"))+"), t_close_date = "+get_bill_repaymentdate_sql(current_bill_number)+
                           " where t_account = '"+currentText+"'"+
                           " and ( t_close_date < "+get_bill_repaymentdate_sql(current_bill_number)+" or "+
                           "       t_close_date = to_date('01.01.0001','dd.mm.yyyy') ); " );
         
         /*println*/
         print_in_sql_file(get_account_to_upload_sql(currentText));

         excel_row = excel_row + 1;
      end;

   end;

   macro close_bill_accs()
      var isFirst = true;
      var firstCol = -1;

      objExcel.Book.Sheets(1).Activate();
      _activeSheet = objExcel.Book.ActiveSheet;

      var objRange = _activeSheet.UsedRange;
      var objTarget = objRange.Find("№ лицевого счета");
      while(objTarget)

         if(objTarget.column == firstCol)
            break;
         end;

         if(isFirst)
             isFirst = false;
             firstCol = objTarget.column;
         end;

         process_bill_table(objTarget.column, objTarget.row);
         objTarget = objRange.FindNext(objTarget);
      end;

   end;

   macro process_bond_table(column, row)
      private var excel_row = row+1;
      private var currentText = "";
      private var close_date = "";
      private var current_close_date = "";

      while(excel_row < _activeSheet.rows.count)
         currentText = _activeSheet.columns(column).rows(excel_row).text;
         close_date = _activeSheet.columns(column+2).rows(excel_row).text;
         if(currentText == "")
            break;
         end;

         currentText = strsubst(SubStr(currentText,1,24),"-","");
         if(close_date != "") 
            current_close_date = close_date;
         end;

         /*println*/
         print_in_sql_file(" UPDATE daccount_dbt SET t_open_close = chr("+int(codeFor("З"))+"), t_close_date = to_date('"+current_close_date+"','dd.mm.yyyy') " +
                           " where t_account = '"+currentText+"'"+
                           " and ( t_close_date < to_date('"+current_close_date+"','dd.mm.yyyy') or "+
                           "       t_close_date = to_date('01.01.0001','dd.mm.yyyy') ); " );
         /*println*/
         print_in_sql_file(get_account_to_upload_sql(currentText));

         excel_row = excel_row + 1;
      end;
   end;

   macro close_bond_accs()
      var isFirst = true;
      var firstCol = -1;

      objExcel.Book.Sheets(2).Activate();
      _activeSheet = objExcel.Book.ActiveSheet;

      var objRange = _activeSheet.UsedRange;
      var objTarget = objRange.Find("№ лицевого счета");
      while(objTarget)

         if(objTarget.column == firstCol)
            break;
         end;

         if(isFirst)
             isFirst = false;
             firstCol = objTarget.column;
         end;

         process_bond_table(objTarget.column, objTarget.row);
         objTarget = objRange.FindNext(objTarget);
      end;
   end;

   macro load(_name_file)

      var data_row = -1;   

      if(objExcel.CreateApplication())
         if(objExcel.open(_name_file))  
            close_bill_accs();
            close_bond_accs();
         end;
      end;

      if(objExcel.Book)
          objExcel.Book.Close();
      end;

      objExcel = null;
   end;
end;

println ("show begin!");

if(selectFile (name_file, mergeFile("..\\import\\", "*.xls"), "Выберите файл" , 0, not isStandAlone))
   ReportTrustLoader().load(name_file);
end;


println ("show end ...");