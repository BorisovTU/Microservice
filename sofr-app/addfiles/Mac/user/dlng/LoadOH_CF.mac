/*
$Name:        LoadOH_CF.mac
$Module:      Интеграция с внешними системами
$Description: Загрузка денежных потоков ОХ
*/
// BIQ-10394
import globals, rsd;
import "u_common_email_utils.mac";
import "usr_open_files.mac";

Var Repdate;

//Расположение загружаемого файла
var  reg_path, errcode;
var  arch_path;
var  err_path;

var c_file = c_txt_file();
file datafile() txt;
SetDelim(",");

private const CV_EMAIL_GRP_HEDG = 31; /* Группа рассылки по проблемам загрузки файла с данными по Инструментам Хеджирования */

private const COL1               = 0,
              COL2               = 1,
              COL3               = 2,
              COL4               = 3,
              COL5               = 4,
              COL6               = 5,
              COL7               = 6,
              COL8               = 7; 

class StatusRecIH()
   var OperDate         :date,      // Дата операционного дня, в котором создан файл
       IdOHAsudr        :string,    // ID ОХ в учетной системе
       IDDealOHAsudr    :string,    // ID сделки ОХ в АСУДР
       IDHedgRel        :string,    // ID отношений хеджирования
       EndDateOHAsudr   :date,      // Дата окончания сделки ОХ в АСУДР (дата окончания отношений хеджирования),
       PayDate          :date,      // Дата платежа
       PaySum           :double,    // Сумма платежа
       PayCur           :string;    // Валюта платежа
end;

private macro PutSlash(_text)
   var res = _text;
   var pos = Strlen(_text), t = "";
   if (pos > 0)
      t = Substr(_text, pos, 1);
      if (t == "\\")
      else 
         _text = _text + "\\";
      end;
   end;
   return _text;
end;


Private macro SendErrMsg(error:string, LoadFileName:string, receiverGroupID:integer)
   var email = c_email_proc_env();
   var FileName, ExtName, DirName;
   VAR SpPath = "", SpFileName = "", tittle = "", message = "";

   //Отделяем путь файла от имени
   tittle = "Внимание! Хеджирование. Загрузка файла.  " + LoadFileName;
   message = "Сообщение отправлено автоматически. В процессе загрузки файла по денежным потокам " + LoadFileName + " возникли следующие ошибки: \n " + error;
   email.m_set_msg_head(tittle);
   email.m_add_row_to_msg_text(message);
   email.m_save_to_submit_grp(receiverGroupID);
   email.m_submit_email_synch();
End;

Private macro SendGoodMsg(error:string, LoadFileName:string, receiverGroupID:integer)
   var email = c_email_proc_env();
   var FileName, ExtName, DirName;
   VAR SpPath = "", SpFileName = "", tittle = "", message = "";

   //Отделяем путь файла от имени
   tittle = "Внимание! Хеджирование. Загрузка файла.  " + LoadFileName;
   message = "Сообщение отправлено автоматически. Файл по денежным потокам " + LoadFileName + " успешно загружен ";
   email.m_set_msg_head(tittle);
   email.m_add_row_to_msg_text(message);
   email.m_save_to_submit_grp(receiverGroupID);
   email.m_submit_email_synch();
End;

// временные таблицы для загрузки 
private macro CheckCreateTable()
   var sql = "declare " +
        " retval number := 0; " +
        " sql_cur varchar2(4000); " +
        "begin " +
        " sql_cur := 'CREATE GLOBAL TEMPORARY TABLE USR_LOADOH_CF ( "+
        "   OperDate       date,               "+
        "   IdOHAsudr      VARCHAR2(255 CHAR), "+
        "   IDDealOHAsudr  VARCHAR2(255 CHAR), "+
        "   IDHedgRel      VARCHAR2(255 CHAR), "+
        "   EndDateOHAsudr date,               "+
        "   PayDate        date,               "+
        "   PaySum         VARCHAR2(255 CHAR), "+
        "   PayCur         VARCHAR2(255 CHAR)  "+
        "  ) "+
        "  ON COMMIT PRESERVE ROWS'; "+
        " select count(1) into retval from user_tables where upper (table_name) = upper('USR_LOADOH_CF'); " +
        " if retval = 0 then " +
        "  execute immediate (sql_cur); " +
        "  execute immediate 'create index USR_LOADOH_CF_idx1 on USR_LOADOH_CF (IDHedgRel)'; "+
        " end if;  " +
        "end; " ;
   var cmd = RSDCommand(sql);
   cmd.execute;
   cmd.close;
onError(err)
   var ErrText = err.message + getFullCmdErrorText(cmd);
   RunError(ErrText);
end;

private macro ClearTable()
   var sql = " begin " +
             "    delete from USR_LOADOH_CF; "+
             " end; " ;
   var cmd = RSDCommand(sql);
   cmd.execute;
   cmd.close;
onError(err)
   var ErrText = err.message + getFullCmdErrorText(cmd);
   RunError(ErrText);
end;

private macro InsertTable(StRecCF_OH)
   var sql = " begin " +
             "    insert into USR_LOADOH_CF (OperDate, IdOHAsudr, IDDealOHAsudr, IDHedgRel, EndDateOHAsudr, PayDate, PaySum, PayCur) "+
             "    values (:1,:2,:3,:4,:5,:6,:7,:8); "+
             " end; " ;
   var cmd = RSDCommand(sql);
   cmd.AddParam("", RSDBP_IN, StRecCF_OH.OperDate);
   cmd.AddParam("", RSDBP_IN, StRecCF_OH.IdOHAsudr);
   cmd.AddParam("", RSDBP_IN, StRecCF_OH.IDDealOHAsudr);
   cmd.AddParam("", RSDBP_IN, StRecCF_OH.IDHedgRel);
   cmd.AddParam("", RSDBP_IN, StRecCF_OH.EndDateOHAsudr);
   cmd.AddParam("", RSDBP_IN, StRecCF_OH.PayDate);
   cmd.AddParam("", RSDBP_IN, StRecCF_OH.PaySum);
   cmd.AddParam("", RSDBP_IN, StRecCF_OH.PayCur);
   cmd.execute;
   cmd.close;
onError(err)
   var ErrText = err.message + getFullCmdErrorText(cmd);
   RunError(ErrText);
end;

//Перемещает файл
private macro copyFromTo (from, to)
  return TDirList().copy(from, null, to, true, false);
End;

Private macro GetRepDate(RepDate)
   var day,mon,year;
   DateSplit(RepDate, day,mon,year);
   if (day < 10)
      day = string(0,day)
   end;
   If(mon <10)
      mon = string(0,mon);
   end;
   return string (day,mon,year);
End;

Private Macro GetValFiid(ficode, err:@string)
   var cmd, rs ;
   err= "";
  cmd = RsdCommand(" select * from dfininstr_dbt where t_ccy = :code"); 

  cmd.addParam("code", RSDBP_IN, ficode);

  cmd.execute();
  
  rs = RsdRecordset(cmd);

  if(rs.MoveNext())
        return rs.value("t_fiid");
  else
     Err = "Не определена валюта в файле по объектам Хеджирования по коду валюты " + ficode;
     return -1;
  end;

end;


Private macro InsertTableCF(_rs, ObjectID, ObjKind, objtype, fiid, IHOHID)
   var sql, cmd;

   sql = " Insert into DDV_AMORT_RHDP_DBT "+
         "    (T_OBJID, T_OBJDOCKIND, T_OBJTYPE, T_DATE, T_SUM, T_CURR, T_RELATIONID) "+
         " values(:1, :2, :3, :4, :5, :6, :7)";
   cmd = RSDCommand(sql);
   cmd.AddParam("p01", RSDBP_IN, ObjectID);
   cmd.AddParam("p02", RSDBP_IN, ObjKind);
   cmd.AddParam("p03", RSDBP_IN, objtype);
   cmd.AddParam("p04", RSDBP_IN, _rs.value("PayDate"));
   cmd.AddParam("p05", RSDBP_IN, _rs.value("PaySum"));
   cmd.AddParam("p06", RSDBP_IN, fiid);
   cmd.AddParam("p07", RSDBP_IN, IHOHID);
   cmd.Execute;
   return true;
end;


private macro CheckExistingCF(IDHedgRel, id2, IdOHAsudr, ObjectId, ObjKind, objtype, err:@string)
   var sql, cmd, rs;
   var res = true;
   
   sql = "select * "+
         "  from ddv_amort_rhdp_dbt "+
         " where t_relationid = :rel "+
         "   and t_objid = :ID "+
         "   and t_ObjdocKind = :kind "+
         "   and t_objtype = :type";
   cmd = RSDCommand(sql);
   cmd.AddParam("rel", RSDBP_IN, IDHedgRel);
   cmd.AddParam("ID", RSDBP_IN, ObjectId);
   cmd.AddParam("kind", RSDBP_IN, ObjKind);
   cmd.AddParam("type", RSDBP_IN, objtype);
   rs = RSDRecordSet(cmd);
   if (rs.movenext)
      err = "Ошибка в файле загрузки! Для инструмента хеджирования "+id2+ " уже существуют денежные потоки объекта хеджирования "+IdOHAsudr;
      res = false;
   end;
   
   return res;
end;


Private Macro GetIHOHIDParm (IDHedgRel, IdOHAsudr, IHOHID:@integer, Objectid:@integer, objdockind:@integer, objtype:@integer, err:@string)
   var cmd, rs ;
   err= "";
   cmd = RsdCommand(" select * from ddlhdgrelation_dbt where t_ext_hedgeid = :Rel"); 

   cmd.addParam("Rel", RSDBP_IN, IDHedgRel);
   rs = RsdRecordset(cmd);

   if(rs.MoveNext())
      if (IdOHAsudr != rs.value("t_ext_objid"))
         Err = "Ошибка в файле загрузки! Код объекта хеджирования не совпадает с кодом объекта хеджирования, связанного с инструментом хеджирования " + IDHedgRel +
               " передано "+IdOHAsudr+", существующий "+rs.value("t_ext_objid");
         return -1;
      end;
      IHOHID = rs.value("t_id");
      Objectid = rs.value("t_objid");
      objdockind = rs.value("t_objdockind");
      objtype = rs.value("t_objtype");
   else
      Err = "Не определены отношения Хеджирования по ID отношений Хеджирования " + IDHedgRel;
      return -1;
   end;
End;

macro GetDateFromFile(p_date)
   var yy, mm,dd;
   var pos = 0, t;
   var res = date(0,0,0);
   pos = Index(p_date, "-");
   if (pos > 0) // формат даты yyyy-mm-dd
      yy = Int(substr(p_date,1,pos-1));
      t = substr(p_date, pos+1);
      pos = Index(t,"-");
      if (pos > 0)
         mm = int(substr(t,1,pos-1));
         dd = Int(substr(t,pos+1));
      end;
      if (dd)
         res = date(dd,mm,yy);
      end;
   else // формат даты dd.mm.yyyy
      pos = Index(p_date, ".");
      if (pos > 0)
         res = date(p_date);
      end;
   end;
   
   return res;
end;


/* Непосредственная обработка единичного файла */
macro OpLoadFileCF_OH(FileName)
   var row = 1;
   var StRecCF_OH;
   var ErrStr = "",ErrStat = false, AllErrStr = "", curerr = "";
   Var ObjectID = -1, ObjKind = -1, objtype = -1;
   var check_stat = true;
   var IHOHID = -1;
   var ValFiid = -1;
   
   var sql_str, cmd, rs;
   var sql_str_rel, cmd_rel, rs_rel;

   /* формат файла, первая строка - всегда заголовок, данные со второй строки
	Дата операционного дня, в котором создан файл,
	ID ОХ в учетной системе,
	ID сделки ОХ в АСУДР,
	ID отношений хеджирования,
	Дата окончания сделки ОХ в АСУДР (дата окончания отношений хеджирования),
	Дата платежа,
	Сумма платежа,
	Валюта платежа.
     */

   ClearTable;

   next(datafile); // строка заголовка

   //Бежим по файлу
   InitProgress(-1);
   while (next(datafile))
      if (StrLen(trim(datafile.str)) == 0)
         continue;
      end;

      StRecCF_OH = StatusRecIH(); 
      StRecCF_OH.OperDate        = GetDateFromFile(datafile(COL1));
      StRecCF_OH.IdOHAsudr       = datafile(COL2);
      StRecCF_OH.IDDealOHAsudr   = datafile(COL3);
      StRecCF_OH.IDHedgRel       = datafile(COL4);
      StRecCF_OH.EndDateOHAsudr  = GetDateFromFile(datafile(COL5));
      StRecCF_OH.PayDate         = GetDateFromFile(datafile(COL6));
      StRecCF_OH.PaySum          = datafile(COL7);
      StRecCF_OH.PayCur          = datafile(COL8);
   
      InsertTable(StRecCF_OH);
      StRecCF_OH = null;
   end;

   sql_str = "select distinct IDHedgRel, IdOHAsudr from USR_LOADOH_CF ";
   sql_str_rel = "select * from USR_LOADOH_CF where IDHedgRel = :hedgerel and IdOHAsudr = :idoh";
   rs = RSDRecordSet(sql_str);
   while (rs.movenext)
      IHOHID = -1;

      GetIHOHIDParm (rs.value("IDHedgRel"), rs.value("IdOHAsudr"), @IHOHID, @Objectid, @ObjKind, @objtype, @curerr);
      If(IHOHID == -1)
         ErrStat = true;
         ErrStr = curerr;
         AllErrStr = AllErrStr + ErrStr + "\n";
      end;
      
      if (not ErrStat)
         check_stat = CheckExistingCF(IHOHID, rs.value("IDHedgRel"), rs.value("IdOHAsudr"), ObjectID, ObjKind, objtype, @curerr);
         if (not check_stat)
            ErrStat = true;
            ErrStr = curerr;
            AllErrStr = AllErrStr + ErrStr + "\n";
         end;
      end;
      
      // успешно нашли ИХ/ОХ, можно грузить потоки
      if (not ErrStat)
         cmd_rel = RSDCommand(sql_str_rel);
         cmd_rel.AddParam("hedgerel1", RSDBP_IN, rs.value("IDHedgRel"));
         cmd_rel.AddParam("idoh", RSDBP_IN, rs.value("IdOHAsudr"));
         
         rs_rel = RSDRecordSet(cmd_rel);
         while (rs_rel.movenext)
            ValFiid = GetValFiid(rs_rel.value("PayCur"), @curerr);
            If(ValFiid == -1)
               ErrStat = true;
               ErrStr = curerr;
               AllErrStr = AllErrStr + ErrStr + "\n";
            end;
            
            If(not errstat)
               rslDefCon.beginTrans ();
               InsertTableCF(rs_rel, ObjectID, ObjKind, objtype, ValFiid, IHOHID);
               rslDefCon.commitTrans ();
            end;
            UseProgress(row);
         end;
      end;

      ErrStr = "";
      ErrStat = false;
      ObjectID = 0;
      ObjKind = 0;

      row = row + 1;
   end; //while

   RemProgress();
   If(AllErrStr != "")
      PrintLn("Внимание! В процессе загрузки файла " + FileName + " произошли следующие ошибки :");
      Println();
      PrintLn(AllErrStr);
      Println();
      Println("Файл " + FileName + " будет перемещен по пути : " + err_path);
   end;

   If(AllErrStr != "")
      SendErrMsg(AllErrStr, FileName, CV_EMAIL_GRP_HEDG);
      return 1;
   else
      Println("Файл " + FileName + " обработан успешно");
      SendGoodMsg("", FileName, CV_EMAIL_GRP_HEDG);
      return 0;
   end;

onerror(er)
   if ( rslDefCon.isInTrans () )
      rslDefCon.rollbackTrans ();
   end;

   RemProgress();
   EndAction(1);
   PrintLn("Внимание! В процессе загрузки файла " + FileName + " произошли следующие ошибки :" + er.message);

   SendErrMsg(er.message, FileName, CV_EMAIL_GRP_HEDG);

   return 0;
end;


/* Загружаем все файлы по маске из директории в настройках */
macro OpLoadCF_OH(RepDate)

   var FullNameFile = "";
   var FileName = "";
   var TermPath = GetCurDir(true) + "\\TxtFile";
   var i = 0, WIthErr = 0;
   var stat;
   var cur_file_mask = "*.csv";
   
   CheckCreateTable;
   
   /*Выбираем файл для обработки*/
   var List = TDirList(reg_path + "CF_OH_" + GetRepDate(RepDate) + cur_file_mask, "F");
   
   while( i < List.Count )
      FullNameFile = reg_path+List.Name(i);
      stat = c_file.openFile(datafile, cur_file_mask, true, true, FullNameFile, "rsansi");

      if ( (valtype(stat) == V_UNDEF) or (stat == false) )
         msgbox("Ошибка открытия файла");
         return 0;
      end;

      WIthErr = OpLoadFileCF_OH(FullNameFile);
      c_file.closeFile(datafile);

      If(WIthErr)
         copyFromTo(FullNameFile, "$" + err_path);
      else
         copyFromTo(FullNameFile, "$" + arch_path);
      end;
      i = i + 1;
   end;

End;

/* Точка входа */
GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\ИМПОРТ_ХЕДЖИРОВАНИЕ", V_STRING, reg_path, errcode );
if ( errcode != 0 )
   reg_path = "";
else 
   reg_path = PutSlash(reg_path);
end; 
GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\ИМПОРТ_ХЕДЖИРОВАНИЕ_АРХИВ", V_STRING, arch_path, errcode );
if ( errcode != 0 )
   arch_path = "";
else 
   arch_path = PutSlash(arch_path);
end; 

GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\ИМПОРТ_ХЕДЖИРОВАНИЕ_ОШИБКИ", V_STRING, err_path, errcode );
if ( errcode != 0 )
   err_path = "";
else 
   err_path = PutSlash(err_path);
end; 


if( not IsShedulerRunning())
   If( not GetDate(Repdate, "Введите дату загрузки"))
      exit(1);
   end;
   OpLoadCF_OH(Repdate);
end;
