/*
$Name:        dlcontrmsg_incoming_spb_func.mac
$Module:      Ядро Securities
$Description: Обработка входящих сообщений СПБ
*/
import "dlcontrmsg_spb.mac";

const REGPATH_ANSWER_CLIENT_BOOKING = "SECUR\\SPB\\PATH_ANSWER_CLIENT_BOOKING";
const REGPATH_ANSWER_CLIENT_ONLINE = "SECUR\\SPB\\PATH_ANSWER_CLIENT_ONLINE";
const REGPATH_ANSWER_CLIENTS = "SECUR\\SPB\\PATH_ANSWER_CLIENTS";
const REGPATH_SPB82 = "SECUR\\SPB\\PATH_SPB82";

private const CHILD_NODE = 1;

// Протокол обработки сообщений
private var ProcessIncomingProtocol = TDlMsgProtocol();
// Протокол обработки сообщений с ошибками
private var ErProcessIncomingProtocol = TDlMsgProtocol();

private var ErrMes = TArray();

macro incoming_spb_msgbox(msg)
  ErrMes[ErrMes.Size] = msg;
  MsgBox(msg);
end;

private macro SetupMsgHandler()
  ErrMes = TArray();
  ReplaceMacro( "MsgBox", "incoming_spb_msgbox");
end;

private macro RestoreMsgHandler()
  ReplaceMacro( "MsgBox");
end;

private macro GetErrMes():String
  var i = 0;
  var str = "";
  while(i < ErrMes.Size )
    str = str + "\n" + ErrMes[i];
    i = i + 1;
  end;
  return str;
end;

private macro GetFirstErrMes():String
  var str = "";
  if(ErrMes.Size > 0)
    str = "\n" + ErrMes[0];
  end;
  return str;
end;


private macro GetStrRegVal(regPath:string)
  var stat = 0, path = "";
  GetRegistryValue(regPath, V_STRING, path, stat);
  if(stat != 0)
    msgbox("Ошибка при получении значения настройки: " + regPath);
  elif(path == "")
    stat = 1;
    msgbox("Не задано значение настройки: " + regPath);
  end;
  return path;
end;

private macro GetDlContr( rh:TRecHandler, dlContrID ) :BOOL
  rh.Clear();
  if(DL_GetRecHandler(rh, "SELECT DLCONTR.* FROM DDLCONTR_DBT DLCONTR WHERE DLCONTR.T_DLCONTRID = "+dlContrID))
     return true;
  end;
  return false;
end;

private macro GetSfContr( rh:TRecHandler, sfContrID ) :BOOL
  rh.Clear();
  if(DL_GetRecHandler(rh, "SELECT SFCONTR.* FROM DSFCONTR_DBT SFCONTR WHERE SFCONTR.T_ID = "+sfContrID))
     return true;
  end;
  return false;
end;

private macro GetDlContrMP( rh:TRecHandler, dlContrID, marketID, mpCode ) :BOOL
  rh.Clear();
  if(DL_GetRecHandler(rh, "SELECT * FROM DDLCONTRMP_DBT WHERE T_DLCONTRID = "+dlContrID+" AND T_MARKETID = "+marketID+" AND T_MPCODE = '"+mpCode+"'"))
     return true;
  end;
  return false;
end;

private macro GetDlContrMsg(dlContrMsg:@variant, MarketID, MsgKind, MpCode)
  var stat = 0;
  dlContrMsg.clear();
  //dlContrMsg.KeyNum = 2;
  dlContrMsg.rec.MarketID = MarketID;
  dlContrMsg.rec.Kind = MsgKind;
  dlContrMsg.rec.MpCode = MpCode;
  if(not GetEQ(dlContrMsg))
    stat = 1;
    MsgBox("Не удалось найти сообщение о бронировании кода " + dlContrMsg.rec.MpCode);
  end;
  return stat;
end;

private macro getListFiles( pfx:string, ext:string, filePath:string, List:@object )
  var ErrStr = "";
  if(filePath == "")
    return 1;
  end;

  var filemask = pfx + ext;
  List = TDirList(filePath + filemask, "f");
  List.Sort(0);

  if(List.Count == 0)
    return 1;
  else
    return 0;
  end;
end;

private macro ProcessedDate(date_val)
  var day, month, year;
  DateSplit(date_val,day,month,year);
  return L_Z(year,4) + "-" + L_Z(month,2) + "-" + L_Z(day,2);
end;

private macro ResultCopyFile(done:bool, filePath:string, fileName:string)
  var stat = 0;
  var filePath_new, fullPathName_new;

  if(done)
    filePath_new = filePath + "Done\\";
  else
    filePath_new = filePath + "Err\\";
  end;

  MakeDir(filePath_new);
  filePath_new = filePath_new + string(ProcessedDate({curdate})) + "\\";
  MakeDir(filePath_new);

  fullPathName_new = filePath_new + fileName;

  if(not CopyFile(filePath + fileName, fullPathName_new) )
    stat = 1;
  elif(not RemoveFile(filePath + fileName))
    stat = 1;
  end;
  return stat;
end;

private macro getMsgType(msgType:string)
  if(msgType == SPBMSGTYPE_K)
    return OBJTYPE_DLCONTRMSG_MPREG;
  elif(msgType == SPBMSGTYPE_D)
    return OBJTYPE_DLCONTRMSG_MPCLOSE;
  elif(msgType == SPBMSGTYPE_U)
    return OBJTYPE_DLCONTRMSG_CHNGPERSDATA;
  else
    return "";
  end;
end;

private macro ОбновитьСообщениеДБО(dlContrMsg, respData)
  var query = " UPDATE DDLCONTRMSG_DBT SET "+
                " T_SENDMESSTATE = ?, "+
                " T_RESPDATA = ?, "+
                " T_RESPDATE = ?, "+
                " T_RESPTIME = ? "+
              " WHERE T_ID = ? ";

  var cmd = RSDCommand(query);
  cmd.AddParam("", RSDBP_IN, dlContrMsg.rec.SendMesState);
  cmd.AddParam("", RSDBP_IN, respData);
  cmd.AddParam("", RSDBP_IN, dlContrMsg.rec.RespDate);
  cmd.AddParam("", RSDBP_IN, dlContrMsg.rec.RespTime);
  cmd.AddParam("", RSDBP_IN, dlContrMsg.rec.ID);

  cmd.Execute();
  return true;
onError(er)
  return false;
end;

private macro СоздатьНовоеСообщ(newMsgKind, DlContr, SfContr, dlContrMp)
  var stat = DL_AddDlContrGenMes(newMsgKind, dlContrMp.rec.ID, dlContrMp.rec.MarketID);
  if(stat == 0)
    stat = СоздатьСообщениеДБО_СПБ(newMsgKind, dlContrMp.rec.MarketID, DlContr, SfContr);
  end;
  SQL_Execute("DELETE FROM DDLCONTRGENMSG_TMP");
  return stat;
end;

private macro GetDlContrStruct(dlContrMsg, dlContr, sfContr, dlContrMp)
  if((GetDlContr(dlContr, dlContrMsg.rec.DlContrID) == false) or
     (GetSfContr(sfContr, dlContr.rec.SfContrID) == false) or
     (GetDlContrMp(dlContrMp, dlContrMsg.rec.DlContrID, dlContrMsg.rec.MarketID, dlContrMsg.rec.MpCode) == false))
    return 1;
  end;
  return 0;
end;

private macro ОбработатьФайл_ClientBooking(MarketID, fullPathName:string)
  FILE inFile() txt;
  var dlContrMsg = Tbfile("dlcontrmsg.dbt", "W", 2);
  var dlContr = TRecHandler("dlcontr.dbt"), sfContr = TRecHandler("sfcontr.dbt"), dlContrMp = TRecHandler("dlcontrmp.dbt");
  var stat = 0, transactionStarted:bool = false, i = 0, line_Cnt;

  RslDefCon.BeginTrans();
  transactionStarted = true;

  if(Open(inFile, fullPathName, "rsansi"))
    setdelim(SYM_SPLIT);
    next(inFile); // первую строку заголовка пропускаем

    if(next(inFile) and (Int(inFile(6)) == 0)) // по второй строке заголовка узнаем о статусе ответного сводного сообщения биржи в целом (Ok)
      line_Cnt = Int(inFile(5)); // количество записей сообщения

      while(next(inFile) and (i < line_Cnt) and (stat == 0))
        stat = GetDlContrMsg(@dlContrMsg, MarketID, OBJTYPE_DLCONTRMSG_BIDCODE, inFile(0));

        if((stat == 0) and ((dlContrMsg.rec.SendMesState == DLSENDMESSTATE_AWAITCONFIRM) or (dlContrMsg.rec.SendMesState == DLSENDMESSTATE_AWAITGENNOTIFY)))
          if(inFile(5/*???*/) == 0) // Ok
            dlContrMsg.rec.SendMesState = DLSENDMESSTATE_CONFIRM;
          else
            dlContrMsg.rec.SendMesState = DLSENDMESSTATE_NOCONFIRM;
          end;
          dlContrMsg.rec.RespDate = {curdate};
          dlContrMsg.rec.RespTime = time();

          if(not ОбновитьСообщениеДБО(dlContrMsg, inFile(6/*???*/)))
            stat = 1;
            MsgBox("Не удалось обновить сообщение с кодом = " + dlContrMsg.rec.MpCode);
          end;

          if(stat == 0)
            if((stat = GetDlContrStruct(dlContrMsg, dlContr, sfContr, dlContrMp)) != 0)
              MsgBox("Не удалось найти запись ДБО для DlContrID = " + dlContrMsg.rec.DlContrID);
            end;
          end;

          //если сообщение CLIENT_BOOKING подтверждено, генерация на ДБО сообщения CLIENT_ONLINE
          if((stat == 0) and (dlContrMsg.rec.SendMesState == DLSENDMESSTATE_CONFIRM))
            stat = СоздатьНовоеСообщ(OBJTYPE_DLCONTRMSG_MPREG, dlContr, sfContr, dlContrMp);
          end;

          if(stat == 0)
            // добавляем информацию в протокол
            if(dlContrMsg.rec.SendMesState = DLSENDMESSTATE_CONFIRM)
              ProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode,
                                          ПолучитьКороткоеИмяСубъекта(sfContr.rec.PartyID),
                                          "\"SB\" - Бронирование ККК на СПБ");
            else
              ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode,
                                            ПолучитьКороткоеИмяСубъекта(sfContr.rec.PartyID),
                                            "\"EB\" - Ошибка бронирования ККК на СПБ",
                                            inFile(6/*???*/));
            end;
          end;
        end;

        i = i + 1;
      end;
    else
      stat = 1;
    end;

    Close(inFile);
  else
    stat = 1;
    msgbox("Не удалось открыть файл сообщения \"" + fullPathName + "\"");
  end;

  if(stat == 0)
    RslDefCon.CommitTrans();
  else
    RslDefCon.RollbackTrans();
    // добавляем информацию в протокол
    ErProcessIncomingProtocol.Add("",
                                  "",
                                  "Обработка файлов ClientBooking",
                                  "Произошла ошибка при обработке: "+ fullPathName);
  end;
  transactionStarted = false;

  return stat;
OnError(er)
  if( transactionStarted )
    RslDefCon.RollbackTrans();
  end;

  if(er.Code != 0)
    stat = er.Code;
  else
    stat = 1;
  end;
  return stat;
end;

// обработка файлов с подтверждением CLIENT_BOOKING (бронирование кода)
macro ОбработатьВходящие_ClientBooking(MarketID)
  var list, i = 0;
  var filePath = DL_GetFullPath(GetStrRegVal(REGPATH_ANSWER_CLIENT_BOOKING));

  if(getListFiles("*", ".txt", filePath, @list) == 0)
    while(i < list.Count)
      if(ОбработатьФайл_ClientBooking(MarketID, filePath + List.name(i)) == 0)
        ResultCopyFile(true, filePath, List.name(i));
      else
        ResultCopyFile(false, filePath, List.name(i));
      end;
      i = i + 1;
    end;
  end;
end;

private macro ОбработатьФайл_ClientOnline(MarketID, fullPathName:string)
  FILE inFile() txt;
  var dlContrMsg = Tbfile("dlcontrmsg.dbt", "W", 2);
  var dlContr = TRecHandler("dlcontr.dbt"), sfContr = TRecHandler("sfcontr.dbt"), dlContrMp = TRecHandler("dlcontrmp.dbt");
  var stat = 0, transactionStarted:bool = false, i = 0, line_Cnt;

  SetupMsgHandler();
  RslDefCon.BeginTrans();
  transactionStarted = true;

  if(Open(inFile, fullPathName, "rsansi"))
    setdelim(SYM_SPLIT);
    next(inFile); // первую строку заголовка пропускаем

    if(next(inFile) and (Int(inFile(6)) == 0)) // по второй строке заголовка узнаем о статусе ответного сводного сообщения биржи в целом (Ok)
      line_Cnt = Int(inFile(5)); // количество записей сообщения
      var sender = inFile(2);

      while(next(inFile) and (i < line_Cnt) and (stat == 0))
        stat = GetDlContrMsg(@dlContrMsg, MarketID, OBJTYPE_DLCONTRMSG_MPREG, inFile(0));

        if((stat == 0) and ((dlContrMsg.rec.SendMesState == DLSENDMESSTATE_AWAITCONFIRM) or (dlContrMsg.rec.SendMesState == DLSENDMESSTATE_AWAITGENNOTIFY)))
          if(inFile(12/*???*/) == 0) // Ok
            dlContrMsg.rec.SendMesState = DLSENDMESSTATE_CONFIRM;
          else
            dlContrMsg.rec.SendMesState = DLSENDMESSTATE_NOCONFIRM;
          end;
          dlContrMsg.rec.RespDate = {curdate};
          dlContrMsg.rec.RespTime = time();
/*убрать после закрытия запроса 295107*/  
          var cnt = 13;
          for
            if(index(inFile(cnt), sender))
              break;
            end;
            cnt = cnt + 1;
          end; 

/*295107*/
          if(not ОбновитьСообщениеДБО(dlContrMsg, inFile(cnt - 1/*13*//*???*/)))
            stat = 1;
            MsgBox("Не удалось обновить сообщение с кодом = " + dlContrMsg.rec.MpCode);
          end;

          if(stat == 0)
            if((stat = GetDlContrStruct(dlContrMsg, dlContr, sfContr, dlContrMp)) != 0)
              MsgBox("Не удалось найти запись ДБО для DlContrID = " + dlContrMsg.rec.DlContrID);
            end;
          end;

          //если сообщение CLIENT_ONLINE подтверждено, генерация на ДБО сообщения FATCA (add)
          if((stat == 0) and (dlContrMsg.rec.SendMesState == DLSENDMESSTATE_CONFIRM))
            //	Значение Поля 14 = добавляем в вид кода 2 (КУТ) на ДБО
            stat = DL_AddCodeToDLCONTR(dlContrMsg.rec.DlContrID, DLCCK_SPB_BIDCODE,
                                       inFile(cnt/*14*//*???*/), //Регистрационный код, присвоенный Клиенту
                                       {curdate}          //Дата начала действия
                                      );
            // FATCA, о первичной упрощенной идентификации клиента
            if(stat == 0)
              stat = СоздатьНовоеСообщ(OBJTYPE_DLCONTRMSG_FATCAA, dlContr, sfContr, dlContrMp);
            end;

            if(stat == 0)
              // добавляем информацию в протокол
              if(dlContrMsg.rec.SendMesState = DLSENDMESSTATE_CONFIRM)
                ProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode,
                                            ПолучитьКороткоеИмяСубъекта(sfContr.rec.PartyID),
                                            "\"SO\" - Регистрация ККК на СПБ");
              else
                ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode,
                                              ПолучитьКороткоеИмяСубъекта(sfContr.rec.PartyID),
                                              "\"EO\" - Ошибка регистрации ККК на СПБ",
                                              inFile(13/*???*/));
              end;
            end;
          end;
        end;

        i = i + 1;
      end;
    else
      stat = 1;
    end;

    Close(inFile);
  else
    stat = 1;
    msgbox("Не удалось открыть файл сообщения \"" + fullPathName + "\"");
  end;

  RestoreMsgHandler();

  if(stat == 0)
    RslDefCon.CommitTrans();
  else
    RslDefCon.RollbackTrans();
    // добавляем информацию в протокол
    ErProcessIncomingProtocol.Add("",
                                  "",
                                  "Обработка файлов ClientOnline",
                                  "Произошла ошибка при обработке: "+ fullPathName + GetFirstErrMes());
  end;
  transactionStarted = false;

  return stat;
OnError(er)
  if( transactionStarted )
    RslDefCon.RollbackTrans();
  end;

  RestoreMsgHandler();

  if(er.Code != 0)
    stat = er.Code;
  else
    stat = 1;
  end;
  return stat;
end;

// обработка файлов с подтверждением CLIENT_ONLINE
macro ОбработатьВходящие_ClientOnline(MarketID)
  var list, i = 0;
  var filePath = DL_GetFullPath(GetStrRegVal(REGPATH_ANSWER_CLIENT_ONLINE));

  if(getListFiles("*", ".txt", filePath, @list) == 0)
    while(i < list.Count)
      if(ОбработатьФайл_ClientOnline(MarketID, filePath + List.name(i)) == 0)
        ResultCopyFile(true, filePath, List.name(i));
      else
        ResultCopyFile(false, filePath, List.name(i));
      end;
      i = i + 1;
    end;
  end;
end;

private macro GetClientsProtType(kind)
  if(kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA)
    return "\"SU\" - Обновление анкетных данных на СПБ";
  elif(kind == OBJTYPE_DLCONTRMSG_MPCLOSE)
    return "\"SD\" - Удаление торговой площадки СПБ";
  else
    return "";
  end;
end;

private macro GetClientsErProtType(kind)
  if(kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA)
    return "\"EU\" - Ошибка обновления анкеты на СПБ";
  elif(kind == OBJTYPE_DLCONTRMSG_MPCLOSE)
    return "\"ED\" - Ошибка удаления торговой площадки СПБ";
  else
    return "";
  end;
end;

private macro ОбработатьФайл_Clients(MarketID, fullPathName:string)
  FILE inFile() txt;
  var dlContrMsg = Tbfile("dlcontrmsg.dbt", "W", 2);
  var dlContr = TRecHandler("dlcontr.dbt"), sfContr = TRecHandler("sfcontr.dbt"), dlContrMp = TRecHandler("dlcontrmp.dbt");
  var stat = 0, transactionStarted:bool = false, i = 0, line_Cnt;

  RslDefCon.BeginTrans();
  transactionStarted = true;

  if(Open(inFile, fullPathName, "rsansi"))
    setdelim(SYM_SPLIT);
    next(inFile); // первую строку заголовка пропускаем

    if(next(inFile) and (Int(inFile(6)) == 0)) // по второй строке заголовка узнаем о статусе ответного сводного сообщения биржи в целом (Ok)
      line_Cnt = Int(inFile(5)); // количество записей сообщения

      while(next(inFile) and (i < line_Cnt) and (stat == 0))
        stat = GetDlContrMsg(@dlContrMsg, MarketID, getMsgType(inFile(1)), inFile(0));

        if((stat == 0) and ((dlContrMsg.rec.SendMesState == DLSENDMESSTATE_AWAITCONFIRM) or (dlContrMsg.rec.SendMesState == DLSENDMESSTATE_AWAITGENNOTIFY)))
          if(inFile(12/*???*/) == 0) // Ok
            dlContrMsg.rec.SendMesState = DLSENDMESSTATE_CONFIRM;
          else
            dlContrMsg.rec.SendMesState = DLSENDMESSTATE_NOCONFIRM;
          end;
          dlContrMsg.rec.RespDate = {curdate};
          dlContrMsg.rec.RespTime = time();

          if(not ОбновитьСообщениеДБО(dlContrMsg, inFile(13/*???*/)))
            stat = 1;
            MsgBox("Не удалось обновить сообщение ДБО с кодом = " + dlContrMsg.rec.MpCode + " и типом " + inFile(1));
          end;

          if(stat == 0)
            if((stat = GetDlContrStruct(dlContrMsg, dlContr, sfContr, dlContrMp)) != 0)
              MsgBox("Не удалось найти запись ДБО для DlContrID = " + dlContrMsg.rec.DlContrID);
            end;
          end;

          if(stat == 0)
            // добавляем информацию в протокол
            if(dlContrMsg.rec.SendMesState = DLSENDMESSTATE_CONFIRM)
              ProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode,
                                          ПолучитьКороткоеИмяСубъекта(sfContr.rec.PartyID),
                                          GetClientsProtType(dlContrMsg.rec.Kind));
            else
              ErProcessIncomingProtocol.Add(dlContrMsg.rec.MpCode,
                                            ПолучитьКороткоеИмяСубъекта(sfContr.rec.PartyID),
                                            GetClientsErProtType(dlContrMsg.rec.Kind),
                                            inFile(13/*???*/));
            end;
          end;
        end;

        i = i + 1;
      end;
    else
      stat = 1;
    end;

    Close(inFile);
  else
    stat = 1;
    msgbox("Не удалось открыть файл сообщения \"" + fullPathName + "\"");
  end;

  if(stat == 0)
    RslDefCon.CommitTrans();
  else
    RslDefCon.RollbackTrans();
    // добавляем информацию в протокол
    ErProcessIncomingProtocol.Add("",
                                  "",
                                  "Обработка файлов Clients",
                                  "Произошла ошибка при обработке: "+ fullPathName);
  end;
  transactionStarted = false;

  return stat;
OnError(er)
  if( transactionStarted )
    RslDefCon.RollbackTrans();
  end;

  if(er.Code != 0)
    stat = er.Code;
  else
    stat = 1;
  end;
  return stat;
end;

// обработка файлов с подтверждением CLIENTS
macro ОбработатьВходящие_Clients(MarketID)
  var list, i = 0;
  var filePath = DL_GetFullPath(GetStrRegVal(REGPATH_ANSWER_CLIENTS));

  if(getListFiles("*", ".txt", filePath, @list) == 0)
    while(i < list.Count)
      if(ОбработатьФайл_Clients(MarketID, filePath + List.name(i)) == 0)
        ResultCopyFile(true, filePath, List.name(i));
      else
        ResultCopyFile(false, filePath, List.name(i));
      end;
      i = i + 1;
    end;
  end;
end;

private class TDataSPB82
  var ReportDate:Date;      // Дата отчета
  var ClientCode:string;    // Краткий код Клиента
  var RegulatorCode:string; // Регистрационный код Клиента / Участника торгов
  var Status:string;        // Статус Клиента

  macro InitRecord()
    ClientCode = RegulatorCode = Status = "";
  end;
end;

/* в xml файле дата приходит в формате "YYYY-MM-DD"*/
private macro XML_ПолучитьДату( str:string ):date
  var YYYY:integer, MM:integer, DD:integer;

  YYYY = int(substr(str, 1, 4));
  MM   = int(substr(str, 6, 2));
  DD   = int(substr(str, 9, 2));

  return date(DD, MM, YYYY);
end;

// добавить сообщение о получении отчета SPB82
private macro ДобавитьСообщОтчет(marketID, dlContrID, dataSPB82:TDataSPB82)
  var dlcontrmsg = Tbfile("dlcontrmsg.dbt", "W", 0);
  var kind = -1;
  if(dataSPB82.Status == "A")
    kind = OBJTYPE_DLCONTRMSG_SPB82A;
  elif(dataSPB82.Status == "U")
    kind = OBJTYPE_DLCONTRMSG_SPB82U;
  elif(dataSPB82.Status == "D")
    kind = OBJTYPE_DLCONTRMSG_SPB82D;
  end;

  dlcontrmsg.rec.DlContrID = dlContrID;
  dlcontrmsg.rec.Kind = kind;
  dlcontrmsg.rec.CreateDate = {curdate};
  dlcontrmsg.rec.CreateTime = Time();
  dlcontrmsg.rec.IsOnlineSendMes = UNSET_CHAR;
  dlcontrmsg.rec.MarketID = marketID;
  dlcontrmsg.rec.MpCode = dataSPB82.ClientCode;
  if(not dlcontrmsg.insert())
    return 1;
  end;
  return 0;
end;

private macro GetSPB82ProtType(status)
  if(status == "A")
    return "S82A - получен отчет SPB82, регистрация";
  elif(status == "U")
    return "S82U - получен отчет SPB82, обновление";
  elif(status == "D")
    return "S82D - получен отчет SPB82, прекращение рег";
  else
    return "";
  end;
end;

private macro ОбработатьЗаписьSPB82(marketID, dataSPB82:TDataSPB82)
  var stat = 0;
  // найти ДБО по КУТ
  var query = " SELECT dlcontr.t_DlContrID, pt.t_ShortName " +
                " FROM dparty_dbt pt, ddlcontr_dbt dlcontr, dsfcontr_dbt sfcontr " +
               " WHERE pt.t_PartyID = sfcontr.t_PartyID " +
                 " AND sfcontr.t_ID = dlcontr.t_SfContrID " +
                 " AND dlcontr.t_DlContrID IN ( SELECT t_ObjectID " +
                                                " FROM dobjcode_dbt " +
                                               " WHERE t_ObjectType = ? " +
                                                 " AND t_CodeKind = ? " +
                                                 " AND t_Code = ? " +
                                                 " AND rownum < 2 ) ";
  var cmd = DL_RSDCommand(query);
  cmd.addParam(OBJTYPE_BROKERCONTR_DL);
  cmd.addParam(DLCCK_SPB_BIDCODE);
  cmd.addParam(dataSPB82.RegulatorCode);

  var ds = cmd.execute();
  if(ds.MoveNext())
    // найти площадку
    if((dataSPB82.Status == "A") or (dataSPB82.Status == "D"))
      var cmdMp = DL_RSDCommand("select t_ID from ddlcontrmp_dbt where t_DlContrID = ? and t_MarketID = ? and t_MPCode = ?");
      cmdMp.addParam(Int(ds.DlContrID));
      cmdMp.addParam(marketID);
      cmdMp.addParam(dataSPB82.ClientCode);
      // обновить на площадке поле "Зарегистрирован"/ "Снят с обслуживания"
      var dsMp = cmdMp.execute();
      if(dsMp.MoveNext())
        var fldUpd = IIF(dataSPB82.Status == "A", "t_MpRegDate", "t_MpCloseDate");
        var cmdUpd = RsdCommand("update ddlcontrmp_dbt set "+fldUpd+" = ? where t_ID = ?");
        cmdUpd.addParam("", RSDBP_IN, dataSPB82.ReportDate);
        cmdUpd.addParam("", RSDBP_IN, dsMp.ID);
        cmdUpd.Execute();
      else
        stat = 1;
      end;
    end;

    // добавить сообщение об отчете
    if(stat == 0)
      stat = ДобавитьСообщОтчет(marketID, Int(ds.DlContrID), dataSPB82);

      if(stat == 0)
        // добавляем информацию в протокол
        ProcessIncomingProtocol.Add(dataSPB82.ClientCode,
                                    ds.ShortName,
                                    GetSPB82ProtType(dataSPB82.Status));
      end;
    end;
  else
    stat = 1;
  end;

  return stat;
end;

private macro ОбработатьФайл_SPB82(marketID, fullPathName:string)
  var stat = 0, transactionStarted:bool = false;

  RslDefCon.BeginTrans();
  transactionStarted = true;

  // загрузить xml файл
  var xmlSPB82 = Dl_XmlReader();
  if(not xmlSPB82.Load_file(fullPathName))
    stat = 1;
    msgbox("Не удалось открыть файл сообщения \"" + fullPathName + "\"");
  end;

  if(stat == 0)
    var node = xmlSPB82.GetXml().DocumentElement;
    var i=0, j, k;
    var dataSPB82 = TDataSPB82();
    var child_RtsDoc, child_SPB82, child_Firm;

    while(i < node.childNodes.length) /* бегаем по RTS_DOC */
      child_RtsDoc = node.childNodes.item(i);
      if(child_RtsDoc and (child_RtsDoc.nodeType==CHILD_NODE))
        if(child_RtsDoc.tagname == "SPB82")
          dataSPB82.ReportDate = XML_ПолучитьДату(child_RtsDoc.getAttribute("ReportDate"));
          j=0;
          while(j<child_RtsDoc.childNodes.length) /* бегаем по SPB82 */
            child_SPB82 = child_RtsDoc.childNodes.item(j);
            if(child_SPB82 and (child_SPB82.nodeType==CHILD_NODE))
              if(child_SPB82.tagname == "FIRM")
                k=0;
                while((k<child_SPB82.childNodes.length) and (stat == 0)) /* бегаем по FIRM */
                  child_Firm = child_SPB82.childNodes.item(k);
                  if(child_Firm and (child_Firm.nodeType==CHILD_NODE))
                    if(child_Firm.tagname == "RECORDS")
                      dataSPB82.InitRecord();
                      dataSPB82.ClientCode = child_Firm.getAttribute("ClientCode");
                      dataSPB82.RegulatorCode = child_Firm.getAttribute("RegulatorCode");
                      dataSPB82.Status = child_Firm.getAttribute("Status");

                      stat = ОбработатьЗаписьSPB82(marketID, dataSPB82);
                    end;
                  end;

                  k = k + 1;
                end;
              end;
            end;

            j = j + 1;
          end;
        end;
      end;

      i = i + 1;
    end;
  end;

  if(stat == 0)
    RslDefCon.CommitTrans();
  else
    RslDefCon.RollbackTrans();
    // добавляем информацию в протокол
    ErProcessIncomingProtocol.Add("",
                                  "",
                                  "Обработка файлов Fatca",
                                  "Произошла ошибка при обработке: "+ fullPathName);
  end;
  transactionStarted = false;

  return stat;
OnError(er)
  if( transactionStarted )
    RslDefCon.RollbackTrans();
  end;

  if(er.Code != 0)
    stat = er.Code;
  else
    stat = 1;
  end;
  return stat;
end;

// Уведомление о зарегистрированных Кодах/Кратких кодах Клиентов и (или) Участника торгов
macro ОбработатьВходящие_SPB82(MarketID)
  var list, i = 0;
  var filePath = DL_GetFullPath(GetStrRegVal(REGPATH_SPB82));

  if(getListFiles("*", ".xml", filePath, @list) == 0)
    while(i < list.Count)
      if(ОбработатьФайл_SPB82(MarketID, filePath + List.name(i)) == 0)
        ResultCopyFile(true, filePath, List.name(i));
      else
        ResultCopyFile(false, filePath, List.name(i));
      end;
      i = i + 1;
    end;
  end;
end;


macro PrintProcessIncomingProtocol()
  var i = 0, j = 0, exists = false;
[
                                               Протокол обработки сообщений биржи СПБ
                                                     за ########## г. ########
  Сообщения, подтвержденные биржей
 ┌─────────────────────────────┬─────────────────────────────────────────────────────┬───────────────────────────────┐
 │           Код ККК           │                Наименование субъекта                │         Тип сообщения         │
]
  ({CurDate}, Time());

  while( i < ProcessIncomingProtocol.size )
    j = 0;
    while( j < ProcessIncomingProtocol(i).arrMessType.size )
[├─────────────────────────────┼─────────────────────────────────────────────────────┼───────────────────────────────┤
 │#############################│#####################################################│###############################│]
      ( ProcessIncomingProtocol(i).Code:w, ProcessIncomingProtocol(i).Name:w, ProcessIncomingProtocol(i).arrMessType[j].MessType:w );

      exists = true;
      j = j + 1;
    end;
    i = i + 1;
  end;

[└─────────────────────────────┴─────────────────────────────────────────────────────┴───────────────────────────────┘];

  if((not exists) and (ErProcessIncomingProtocol.size == 0))
[Нет сообщений биржи СПБ.];
  end;
end;

macro PrintErProcessIncomingProtocol()
  var i = 0, j = 0;

  if(ErProcessIncomingProtocol.size > 0)
[
                                               Протокол обработки сообщений биржи СПБ
                                                     за ########## г. ########
  Сообщения с ошибками
 ┌─────────────────────────────┬─────────────────────────────────────────────────────┬───────────────────────────────┬─────────────────────────────────────────┐
 │           Код ККК           │                Наименование субъекта                │         Тип сообщения         │             Описание ошибки             │
]
  ({CurDate}, Time());

  while( i < ErProcessIncomingProtocol.size )
    j = 0;
    while( j < ErProcessIncomingProtocol(i).arrMessType.size )
[├─────────────────────────────┼─────────────────────────────────────────────────────┼───────────────────────────────┼─────────────────────────────────────────┤
 │#############################│#####################################################│###############################│#########################################│]
      ( ErProcessIncomingProtocol(i).Code:w, ErProcessIncomingProtocol(i).Name:w, ErProcessIncomingProtocol(i).arrMessType[j].MessType:w, ErProcessIncomingProtocol(i).arrMessType[j].Text:w );

      j = j + 1;
    end;
    i = i + 1;
  end;

[└─────────────────────────────┴─────────────────────────────────────────────────────┴───────────────────────────────┴─────────────────────────────────────────┘];
  end;
end;


macro ОбработатьВходящиеСообщенияСПБ()
  InitError();
  var MarketID = SPB_ID();
  if(MarketID > 0)
    InitProgress(4, "Обработка входящих сообщений биржи", "Обработка входящих сообщений биржи");

    ОбработатьВходящие_ClientBooking(MarketID);
    UseProgress(1);

    ОбработатьВходящие_ClientOnline(MarketID);
    UseProgress(2);

    ОбработатьВходящие_Clients(MarketID);
    UseProgress(3);

    //ОбработатьВходящие_Fatca(MarketID); // отнесено на 2 очередь

    ОбработатьВходящие_SPB82(MarketID);
    UseProgress(4);

    RemProgress();
  end;

  // Показать протокол
  PrintProcessIncomingProtocol();
  PrintErProcessIncomingProtocol();
end;
