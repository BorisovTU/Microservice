/*Simanov - процедуры для проверки квитовки подтверждений MT300 в фиссико и MT518 в БОЦБ.
  На 13.01.2020 реализовано для внебиржевых сделок фиссико со способом заключения "по телефону"
  dl_check_mt300.mac
*/
import oralib, likepy, CTinter, globals, OprInter;

private const OBJTYPE_DV_DEAL = 148; //Вид объекта - сделка фиссико

private macro m_get_attrid (objecttype, groupid, objectid):integer
   var obj_categ = RsbObjCategories(objecttype, string(objectid:o:34));
   var attrid = -1;
   record attr_buf("objattr.dbt");

   if ( obj_categ.GetMainAttr(groupid, {curdate}, attr_buf) == 0 )
      attrid = attr_buf.attrid;
   end;

   return attrid;
onerror()
   return -1;
End;

//ФИССиКО "Способ заключения" == "Телефон" - true
private macro dv_phone_deal (dealid):bool
   return (m_get_attrid(OBJTYPE_DV_DEAL, 5, dealid) == 4);
End;

//БОЦБ "Способ заключения" == "Телефон" - true
private macro sec_phone_deal (dealid):bool
   return (m_get_attrid(DL_SECURITYDOC, 50, dealid) == 4);
End;

//поиск входящего подтверждения по сделке
private macro dv_find_kvit_mt (dealid)
   return ExecSqlSelect("select 1 from drblnkmes_dbt where t_objkind = "+OBJTYPE_DV_DEAL+" and t_objid = "+dealid+" and t_direct = chr(88) and t_frmname = 300").MoveNext();
onerror()
   return false;
End;

private macro sec_find_kvit_mt (dealid):bool
   var sql = execStoredFunc("RSP_SECURITY.GETCONFIRMPARAMSSEC", V_INTEGER, makeArray( sqlParam("1", dealid,    RSDBP_IN ),
                                                                                      sqlParam("2", V_STRING,  RSDBP_OUT),
                                                                                      sqlParam("3", V_STRING,  RSDBP_OUT),
                                                                                      sqlParam("4", V_STRING,  RSDBP_OUT),
                                                                                      sqlParam("5", V_INTEGER, RSDBP_OUT)));
   return (sql == 0);
End;

private macro m_quetion_continue (dealcode)
   return getTrue(true, "Не найдено сквитованное входящее подтверждение для сделки " + dealcode + ".|Продолжить обработку?");
End;

//Возвращает наличие шага в операции с определённым символом
private macro m_find_step_by_symbol (dockind, docid, symbol):bool
   var sql = "select 1 from doprstep_dbt step "
           + "where t_id_operation = (select t_id_operation from doproper_dbt where t_dockind = :dockind and t_documentid = lpad(:docid, 34, 0)) "
           + "and t_symbol = :symb and t_isexecute = 'X' ";
   sql = ExecSqlSelect(sql, MakeArray(SqlParam("dockind", dockind),
                                      SqlParam("docid",   docid   ),
                                      SqlParam("symb",    symbol)));
   return sql.MoveNext();

onerror()
   return false;
End;

/*
  Проверка, сквитовано ли входящее подтверждение по сделке фиссико.
  Возвращает false если:
    1. требуется проверка на наличие входящего подтверждения
    2. не найдено входящее подтверждение
    3. в операции нет ниодного выполненного шага с квитовкой
    4. на вопрос о продолжении выполнении шага получен ответ "нет"
  Иначе true
*/
macro DV_check_kvit_mt (dockind, dealid, dealcode)
   if (     dv_phone_deal(dealid) and
        not dv_find_kvit_mt(dealid) and
        not m_find_step_by_symbol (dockind, dealid, "Ш") and
        not m_quetion_continue(dealcode)
      )
      return false;
   end;
   return true;
End;

/*
  Проверяет на наличие квитовки входящих подтверждений по всем сделкам фиссико, которые участвуют в операции неттинга
*/
macro DL_nett_check_kvit_mt (nett)
   var result = true;
   var sql = "select distinct t_documentid t_id, (select t_code from ddvndeal_dbt where t_id = t_documentid) t_dealcode, t_dockind "
           + "from dpmpaym_dbt paym "
           + "where t_paymentid in (select t_initialpayment from dpmlink_dbt pmlink where pmlink.t_Dockind = "+nett.DocKind+" and pmlink.t_documentid = "+nett.NettingID+") ";

   //Уже выполнен хотя бы один шаг с квитовкой платежа, уведомление не нужно
   if ( m_find_step_by_symbol (nett.DocKind, nett.NettingID, "Ш"))
      return true;
   end;

   sql = ExecSqlSelect(sql);
   while (sql.MoveNext() and result)
      result = DV_check_kvit_mt(sql.value("t_dockind"), sql.value("t_id"), sql.value("t_dealcode"));
   end;

   return result;
End;

/*
  Проверка, сквитовано ли входящее подтверждение по сделке БОЦБ.
  Возвращает false если:
    1. требуется проверка на наличие входящего подтверждения
    2. не найдено входящее подтверждение
    4. на вопрос о продолжении выполнении шага получен ответ "нет"
  Иначе true
*/
macro Sec_check_kvit_mt (dealid, dealcode)
   if (     sec_phone_deal(dealid) and
        not sec_find_kvit_mt(dealid) and
        not m_quetion_continue(dealcode)
      )
      return false;
   end;
   return true;
End;

