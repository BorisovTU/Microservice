/*
$Name:        _UpdateXml_Try_and_Duplicate.mac
$Module:      Securities
$Description: Корректировка xml-файлов клиентов на ММВБ (привязанных к сообщениям по Alt+M)
              со статусом 103 (ResCode="3") и 102 (Duplicate entry)
*/
Import "dlcontrmsg_mmvb.mac", "Likepy.mac";

private const V_SPECVAL     = 26;
private const BEGIN_DATE    = Date(14, 06, 2022); //обновить сообщения, начиная с указанной даты

private macro ТипСообщения(kind)
  if(kind == OBJTYPE_DLCONTRMSG_MARKETREG)
     return "Регистрация клиента на бирже";
  elif(kind == OBJTYPE_DLCONTRMSG_MPREG)
     return "Регистрация клиента на биржевой площадке";
  elif(kind == OBJTYPE_DLCONTRMSG_MPCLOSE)
     return "Закрытие биржевой площадки";
  elif(kind == OBJTYPE_DLCONTRMSG_MARKETCLOSE)
     return "Закрытие ДБО";
  elif(kind == OBJTYPE_DLCONTRMSG_CHNGPERSDATA)
     return "Изменение информации о личных данных клиента";
  else
    return string(kind);
  end;
end;

private macro CloseImage(ImageID, pDate)
  var query = " UPDATE dimgdata_dbt SET "+
                " t_IsMain = CHR(0), "+
                " t_CloseDate = ?, "+
                " t_CloseTime = ? "+
              " WHERE t_ImageID = ? ";

  var cmd = RSDCommand(query);
  cmd.AddParam("", RSDBP_IN, pDate);
  cmd.AddParam("", RSDBP_IN, Time());
  cmd.AddParam("", RSDBP_IN, ImageID);

  cmd.Execute();
  return 0;
onError(er)
  return 1;
end;

private macro SetIsMainImage(pMsgID:integer, pFileName:string) :integer
  var query = " UPDATE dimgdata_dbt " +
                 " SET t_IsMain = CHR(88) " +
               " WHERE t_objectid = LPAD(?, 34, '0') " +
                 " AND t_FileName = ? " +
                 " AND t_imagetype = 1 " +
                 " AND t_objecttype = " + OBJTYPE_BSCMSG_DL +
                 " AND t_closedate = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
                 " AND t_IsMain = CHR(0) " +
                 " AND ROWNUM < 2 ";

  var cmd = RSDCommand(query);
  cmd.AddParam("", RSDBP_IN, pMsgID);
  cmd.AddParam("", RSDBP_IN, pFileName);

  cmd.Execute();
  return 0;
onError(er)
  return 1;
end;

private macro GetFormatDate(_date)
  var y, mon, d;
  DateSplit (_date,d,mon,y);
  return string(L_Z(y, 4)) + "-" + string(L_Z(mon,2)) + "-" + string(L_Z(d,2));
end;

private macro GetFormatTime(_time)
  var h, m, s;
  TimeSplit(Time(),h,m,s);
  return string(L_Z(h,2)) + ":" + string(L_Z(m,2)) + ":" + string(L_Z(s, 2));
end;

private macro GetNewFileName(pFileName:string)
  var fileName_new:string = "";

  var idx:integer = 0, tmp:integer = 0;
  while((tmp = Index(pFileName, "_new", idx + 1)) > 0)
    idx = tmp;
  end;
  
  if(idx > 0)
   var num_correct:integer = 1;

   var str_num:string = substr(pFileName, idx + 4);
   if((str_num != "") and (IsDigitalNumber(str_num) == 0))
     num_correct = Int(str_num) + 1;
   end;

   fileName_new = SubStr(pFileName, 1, idx + 3) + string(num_correct);
  else
    fileName_new = pFileName + "_new";
  end;

  return fileName_new;
onError(er)
  return fileName_new + "_new";
end;

// обновить прикрепленный файл на сообщении
private macro UpdateXmlFileOnMsg(dlContrMsg:TRecHandler, dlContr:TRecHandler, sfContr:TRecHandler, NeedUpdate:@bool)
  var stat=0, i=0,
      transactionStarted:bool = false;
  NeedUpdate = false;

  RslDefCon.BeginTrans();
  transactionStarted = true;

  var filePathName = "", xmlData = "";
  var queryImg = " SELECT t_imageid, t_filename " +
                   " FROM dimgdata_dbt " +
                  " WHERE t_imagetype = 1 " +
                    " AND t_objecttype = " + OBJTYPE_BSCMSG_DL +
                    " AND t_objectid = LPAD("+dlContrMsg.rec.ID+", 34, '0') " +
                    " AND t_closedate = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
                  " ORDER BY t_IsMain desc ";

  var ds = TRsbDataSet(queryImg);
  if(ds.MoveNext())
     var fd = DlGetTxtFileDir();

     if(WEB_ShowImageByID(Int(ds.ImageID), filePathName))
       var xmlReader = Dl_XmlReader();
       if(xmlReader.Load_file(filePathName))
         var xmlMsg = xmlReader.GetXml();

         var List_DOC_REQUISITES = xmlMsg.getElementsByTagName("DOC_REQUISITES");
         if((stat == 0) and (List_DOC_REQUISITES.item(0) != null) and (List_DOC_REQUISITES.item(0) != V_SPECVAL))
           var DOC_REQUISITES = List_DOC_REQUISITES.item(0);
           NeedUpdate = true;

           var RefID:integer, RefNum:string = "";
           if(not DL_GenerateNumberByReference(OBJTYPE_BROKERCONTR_DL, 5, @RefID, @RefNum))
              RefNum = "";
           end;

           var cmd = RsdCommand("select substr(sys_guid(), 9, 12-length(?)) || ? t_guid from dual");
           cmd.AddParam("n1", RSDBP_IN, string(RefNum));
           cmd.AddParam("n2", RSDBP_IN, string(RefNum));

           var ds_guid = TRsbDataSet(cmd);
           if(ds_guid.movenext())
             DOC_REQUISITES.setAttribute("DOC_NO", ds_guid.guid);
           else
             stat = 1;
           end;

           DOC_REQUISITES.setAttribute("DOC_TIME", GetFormatTime(Time()));
           DOC_REQUISITES.setAttribute("DOC_DATE", GetFormatDate(Date()));

           var ListClientNode = xmlMsg.getElementsByTagName("CLIENT");
           if((ListClientNode.item(0) != null) and (ListClientNode.item(0) != V_SPECVAL))
              var ClientNode = ListClientNode.item(0);
              ClientNode.setAttribute("ClientOperation", "L");
           end;

           var ListCLIENT_INFONode = xmlMsg.getElementsByTagName("CLIENT_INFO");
           for(var CLIENT_INFO, ListCLIENT_INFONode)
              CLIENT_INFO.parentNode.removeChild(CLIENT_INFO);
           end;

           if((stat == 0) and NeedUpdate)
             var fileName_new, ext;
             SplitFile(filePathName, fileName_new, ext);

             fileName_new = GetNewFileName(fileName_new);
             var filePathName_new = fd + "\\" + fileName_new + ext;

             xmlMsg.save(filePathName_new);

             if(not LoadImageObj(filePathName_new, OBJTYPE_BSCMSG_DL, UniID(dlContrMsg, OBJTYPE_BSCMSG_DL), 1));
               stat = 1;
               MsgBox("К записи о сообщении не удалось прикрепить файл сообщения");
             end;

             if((stat == 0) and (stat = SetIsMainImage(dlContrMsg.rec.ID, fileName_new + ext)) != 0)
               MsgBox("Не удалось сделать обновленный файл основным");
             end;

             if(stat == 0)
               RemoveFile(filePathName_new);
               // старый прикрепленный файл с сообщением - перенесем в закрытые
               stat = CloseImage(Int(ds.ImageID), {curdate});
             end;

           end;

         end;
       end;
       RemoveFile(filePathName);
     end;
  end;

  if(stat == 0)
    RslDefCon.CommitTrans();
  else
    RslDefCon.RollbackTrans();
  end;
  transactionStarted = false;

  return stat;
OnError(er)
  if(transactionStarted)
    RslDefCon.RollbackTrans();
  end;
  if(er.Code != 0)
    stat = er.Code;
  else
    stat = 1;
  end;
  return stat;
end;

private macro UpdateXmlOnMsg()
  var stat = 0, i=0, find=false, NeedUpdate=false;
  var dlContrMsg = TRecHandler("dlcontrmsg.dbt"),
      dlContr = TRecHandler("dlcontr.dbt"), 
      sfContr = TRecHandler("sfcontr.dbt");

  var sql = "SELECT m.* "+
             " FROM DDLCONTRMSG_DBT m, dsfcontr_dbt s, ddlcontr_dbt l "+
            " WHERE m.T_ISONLINESENDMES = 'X' "+
              " AND m.t_CreateDate >= ? "+
              " AND m.T_SENDMESSTATE IN (?, ?) "+
              " AND (INSTR(m.t_RespData, 'Повторите попытку') > 0 or "+
              "      INSTR(m.t_RespData, 'Duplicate entry') > 0 or "+
              "      INSTR (m.t_RespData, 'уже присвоен другому Клиенту Участника') > 0) "+
              " AND M.T_DLCONTRID = L.T_DLCONTRID "+
              " AND S.T_ID = L.T_SFCONTRID "+
              " AND S.t_DateClose = TO_DATE('01.01.0001', 'DD.MM.YYYY') "+
              " AND NOT EXISTS ( "+
                              " select 1 "+
                                " from ddlcontrmp_dbt mp, dsfcontr_dbt sfmp, USR_FOCODES_DBT FO "+
                               " where mp.t_DlContrID = l.t_DlContrID "+
                                 " and sfmp.t_ID = MP.T_SFCONTRID "+
                                 " and sfmp.t_ServKindSub = 8 "+
                                 " and sfmp.t_ServKind = 15 "+ //PTSK_DV
                                 " and FO.Code = mp.t_MpCode "+
                             " ) "+
            " ORDER BY m.t_CreateDate, m.t_CreateTime, m.t_ID";

  var cmd = DL_RSDCommand(sql);
  cmd.addParam(BEGIN_DATE);
  cmd.addParam(DLSENDMESSTATE_ER_CALLSTATSERV); //103
  cmd.addParam(DLSENDMESSTATE_ER_CALLREGSERV); //102

  InitProgress(cmd.GetCount(), "Обработка сообщений со статусом 102, 103");

  [
      Корректировка сообщений со статусом 103 (Повторите попытку) и 102 (Duplicate entry)
                                  за ########## г.
   ┌──────────────────────────────┬───────────────────────┬───────────────────────────────┬──────┐
   │           Код ДБО            │ Наименование клиента  │         Тип сообщения         │ stat │
  ]
     ({CurDate});

  var ds = cmd.Execute();
  while(ds.moveNext())
    dlContrMsg.clear();
    ds.GetRecord().CopyTo(dlContrMsg.rec);

    if(DL_GetRecHandler(dlContr, "SELECT * FROM DDLCONTR_DBT WHERE T_DLCONTRID = "+dlContrMsg.rec.DlContrID) and
       DL_GetRecHandler(sfContr, "SELECT * FROM DSFCONTR_DBT WHERE T_ID = "+dlContr.rec.SfContrID)
      )
      stat = UpdateXmlFileOnMsg(dlContrMsg, dlContr, sfContr, @NeedUpdate);

      if(NeedUpdate)
        find = true;
  [├──────────────────────────────┼───────────────────────┼───────────────────────────────┼──────┤
   │##############################│#######################│###############################│######│]
           ( sfContr.rec.Number:w,
             DL_GetPartyShortName(sfContr.rec.PartyID):w,
             ТипСообщения(dlContrMsg.rec.Kind):w,
             string(stat):w );
      end;
    end;

    i = i + 1;
    UseProgress(i);
  end;
  [└──────────────────────────────┴───────────────────────┴───────────────────────────────┴──────┘];
  if(not find)
    [Нет сообщений для корректировки.];
  end;
  RemProgress();

end;

UpdateXmlOnMsg();
