/*
Макрос: va_printdocs.mac
Симанов А.В.
Печать документов по сторонним векселям
*/
import dlwreps, likepy, oralib, globals, FIInter, PTInter, PaymInter;

private MACRO FormatDate(OldDate)
VAR str;

     str = string((date(OldDate)):m);

     return SubStr(str, 3);

END;

//наименование филиала
private macro GetDprtName (NUmDprt)
  var name = "";
  var sql = " select party.t_shortname name "
          + "from ddp_dep_dbt dep, dparty_dbt party "
          + "where dep.t_partyid = party.t_partyid and dep.t_code = :NumDprt ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("NumDprt", NumDprt)), false);
  if (sql.MoveNext() ) 
    name = sql.value("name");
  end;
  return name;
End;

private macro CreateFileName ()
    return GetTxtFileName ("tmpout");
END;

//Возвращает название города по номеру филиала
private macro GetDprtCity (NumDprt)
   var partyid = 0;
   var Party, PartyAddress;
   var sql = ExecSqlSelect("select t_partyid from ddp_dep_dbt where t_code = :num", MakeArray(SqlParam("num", NumDprt)), false);
   if (sql.MoveNext() )
      partyid = int(sql.value("t_partyid"));
   end;
   if (partyid == 0)
      return "";
   end;

   Party = RsbParty(partyid);
   PartyAddress = Party.Address(PTADDR_LEGAL);
   return PartyAddress.District;
End;

private macro GetOPerTypeName (dockind, operkind)
  var opername = "";
  var sql = "select t_name opername from doprkoper_dbt where t_dockind = :dockind and t_kind_operation = :operkind";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("dockind", dockind), SqlParam("operkind", operkind)), false);
  if (sql.MoveNext() )
    opername = sql.value("opername");
  end;
  return opername;
End;

private macro Get_NumToStr_2(num:money, fiid:integer)
  var rub :string = "",
      kop :string = "",
      s1  :string;

  var cmd :object = NULL,
  rs  :object = NULL;

  cmd = RsdCommand("select * from dfininstr_dbt where t_fiid = ?");
  cmd.addParam("fiid", RSDBP_IN, fiid);
  cmd.execute;
  rs = RsdRecordset(cmd);

  if (not rs.movenext())
    return "";
  end;

  s1 = CurToStrAlt(num, rub, kop, int(rs.value("t_iso_number")));
  s1 = trim(StrSubSt(s1, rub, ""));
  s1 = trim(StrSubSt(s1, SubStr(s1, index(s1, kop)), ""));

  return rub+" и "+kop+"/100 "+s1;
end;


//Распоряжение на оплату. Настроено только на случай, когда наш банк кому-то платит
macro Applic_5 (dl_tick)
  var D;
  var sum = 0;
  var curr = -1;
  var sql;
  var paydate = date(0, 0, 0);
  var Party = RsbParty(dl_tick.PartyID);
  var PartyAddress = Party.Address(PTADDR_LEGAL);
  var nostroacc = "";
  var Paym = RsbPayment (dl_tick.BOfficeKind, dl_tick.DealID, 2, 1);
  var PartyINN = party.Code(PTCK_INN);

  if (Paym.ReceiverBankID == {Ourbank}) 
    nostroacc = Paym.ReceiverAccount;
  else
    nostroacc = Paym.ReceiverCorrAccNostro;
  end;

  sql = "select t_cost cost, "
      + "       t_cfi cur, "
      + "       t_expiry paydate "
      + "from ddl_leg_dbt where t_dealid = :dealid and t_legid = 0 and t_legkind <> 1 ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("dealid", dl_tick.DealID)), false);
  if (sql.MoveNext() )
    sum = money(sql.value("cost"));
    curr = int(sql.value("cur"));
    paydate = date(sql.value("paydate"));
  end;

  println("DB_5.dotx");

  [~depname];
  println(GetDprtName({OperDprtNode}));

  [~dt];
  DateSplit({curdate}, D);

  println(string(D:o:2:t));

  [~date];
  println(FormatDate({curdate}));

  [~city];
  println(GetDprtCity({OperDprtNode}));

  [~department];
  println(GetDprtName(dl_tick.Department));

  [~DealType];
  println(GetOPerTypeName(dl_tick.BOfficeKind, dl_tick.DealType));

  [~sum];
  println(sum);

  [~sumwr];
  println(Get_NumToStr_2(sum, curr));

  [~currency];
  println(ПолучитьИмяФинИн(curr));

  [~docnum];
  println(dl_tick.DealCode);

  [~docdt];
  DateSplit(date(dl_tick.DealDate), D);

  println(string(D:o:2:t));

  [~docdate];
  println(FormatDate(dl_tick.DealDate));

  [~paydate];
  println(paydate);

  [~nostro];
  println(nostroacc);



  [~CounterpartyCaption];
  println(Party.FullName);

  [~CounterpartyAddress];
  println(PartyAddress.Address);

  [~TaxId_IdPurpose];
  println(PartyINN);

  [~CounterpartyAccount];
  println(Paym.ReceiverAccount);

  [~CounterpartyBankCaption];
  println(Paym.ReceiverBankName);

  [~CounterpartyBankAccount];
  println(Paym.ReceiverBankCorrAcc);
  
  [~CounterpartyBankAlias];
  if (Paym.ReceiverBankCodeKind == 3)
    println(Paym.ReceiverBankCode);
  end;


  DLWREPS_PrintReportFromTagFile (SetOutput (CreateFileName));
  SetOutput(NULL, false);
End;

//Распоряжение о постановку на ностро-позицию. настроено только для операции продажи векселя
macro Applic_6 (dl_tick)
  var Paym = RsbPayment (dl_tick.BOfficeKind, dl_tick.DealID, 2, 1);
  var Party = RsbParty(dl_tick.PartyID);
  var D;

  println("DB_6.dotx");

  [~depname];
  println(GetDprtName({OperDprtNode}));

  [~dt];
  DateSplit({curdate}, D);

  println(string(D:o:2:t));

  [~date];
  println(FormatDate({curdate}));

  [~city];
  println(GetDprtCity({OperDprtNode}));

  [~paydate];
  println(Paym.ValueDate);

  [~num];
  println(1);

  [~sum];
  println(Paym.BaseAmount);

  [~sumwr];
  println(Get_NumToStr_2(Paym.BaseAmount, Paym.BaseFIID));

  [~currency];
  println(ПолучитьИмяФинИн(Paym.BaseFIID));

  [~nostro];
  println("???");

  [~docnumber];
  println(dl_tick.DealCode);

  [~docdt];
  DateSplit(date(dl_tick.DealDate), D);

  println(string(D:o:2:t));

  [~docdate];
  println(FormatDate(dl_tick.DealDate));

  [~client];
  println(Party.FullName);

  DLWREPS_PrintReportFromTagFile (SetOutput (CreateFileName));
  SetOutput(NULL, false);
End;