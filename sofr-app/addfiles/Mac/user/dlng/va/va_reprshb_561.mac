
/*
$Name:         va_reprshb_561.mac
$Module:       Учтенные векселя
$Description:  Отчет по собственным векселям
*/
import or_rep_h, oralib, likepy, globals, Календарь, vslib, VSInter, vscateg, vaconv;

private const LIGHT_GRAY = 12632256;

//плановая дата погашения векселя
private macro BnrPlanRepayDate (termformula, start:date, maturity:date, expiry:date, Basis)
  var repaydt = Date(0, 0, 0);
  if (termformula == VS_TERMF_FIXEDDAY) //на определенный день
    repaydt = maturity;
  elif (termformula == VS_TERMF_ATSIGHT) //по предъявлении
    if ( (valtype(expiry) != V_UNDEF) and (expiry != Date(0, 0, 0)) ) //есть дата не позднее
      repaydt = expiry;
    elif ( (valtype(maturity) != V_UNDEF) and (maturity != Date(0, 0, 0)) ) //есть дата не ранее
      repaydt = maturity + DaysInYearByBasis(Basis, Maturity, true);
    else
      repaydt = start + DaysInYearByBasis(Basis, start, true);
    end;
  end;
  return repaydt;
End;

macro va_report (repdate)
  var decsep = GetLocaleInfo (0, LOCALE_SDECIMAL, IsStandAlone() );
  var Rep;
  var fName;
  var csvFile, csvTable;
  var sql;
  var IncomeForm = "";
  var acc = "";
  var bln = "";
  var RestAcc = 0;
  var RestAccNATCUR = 0;
  var SumDiscont = 0;
  var DaysToRepay = 0;

  if ( (valtype(repdate) == V_UNDEF) or (repdate == Date(0, 0, 0)) )
    repdate = {curdate};
    if (not GetDate(repdate))
      return 1;
    end;
  end;

  Rep = CTemplateXLS(NULL, NULL, NULL, NULL, NULL, NULL, true, false);
  if(Rep.UsePoi())
    Rep.SetXLSXFormat();
  end;

  Rep.CreateTotalBook();

  Rep.openTemplate("f_657_6_template.xlsx",true);
  Rep.SheetChange(1);
  
  csvTable = Rep.RegisterTable ("templateTable", "templateHeader", true);
  csvFile = C_CSVFile ();
  
  if (Rep.UseBigReport ())
      csvFile.SetLimitRow (FLUSH_COUNT_XLSX);
  end;
  
  Rep.SetValue_NameCell ("Date", "Портфель учтенных векселей по состоянию за "+string(repdate:f), "");

  Rep.CopyAllSheetInTotalBook (null, false, "templateHeader", 0, "Отчет");

  fName = csvFile.CreateFullFileName ("f_657_6_csv.txt");
  csvFile.FileOpen (fName, true);

  sql = "select trim(t.t_bcseries) || ' ' || trim(t.t_bcnumber) sernum, "
      + "       trim(t.t_issuername) issuername, "
      + "       t.facevaluefi, "
      + "       t.t_holdername, "
      + "       t.fiid, "
      + "       t.t_issuedate, "
      + "       t.t_principal, "
      + "       case when ((t.t_formula = 0) and (t.discount_all <> 0)) then 'Д' else 'П' end as formula, "
      + "       t.discount_all, "
      + "       t.percent, "
      + "       t.termformula, "
      + "       t.maturity, "
      + "       t.expiry, "
      + "       t.basis, "
      + "       t.startdate, "
      + "       t.body_acc, "
      + "       abs(rsi_rsb_account.RestAll(t.body_acc, 1, t.fiid, :dt, t.fiid)) body_rest, "
      + "       abs(rsi_rsb_account.RestAll(t.body_acc, 1, t.fiid, :dt, 0)) body_rest_natcur, "
      + "       t.pdd_acc, "
      + "       abs(rsi_rsb_account.RestAll(t.pdd_acc, 1, t.fiid, :dt, t.fiid)) pdd_rest, "
      + "       abs(rsi_rsb_account.RestAll(t.pdd_acc, 1, t.fiid, :dt, 0)) pdd_rest_natcur, "
      + "       t.kor_up_acc, "
      + "       abs(rsi_rsb_account.RestAll(t.kor_up_acc, 1, t.fiid, :dt, 0)) kor_up_rest_natcur, "
      + "       t.kor_down_acc, "
      + "       abs(rsi_rsb_account.RestAll(t.kor_down_acc, 1, t.fiid, :dt, 0)) kor_down_rest_natcur, "
      + "       t.res_acc, "
      + "       abs(rsi_rsb_account.RestAll(t.res_acc, 1, t.fiid, :dt, t.fiid)) res_rest, "
      + "       abs(rsi_rsb_account.RestAll(t.res_acc, 1, t.fiid, :dt, 0)) res_rest_natcur, "
      + "       t.res_pdd_acc, "
      + "       abs(rsi_rsb_account.RestAll(t.res_pdd_acc, 1, t.fiid, :dt, t.fiid)) res_pdd_rest, "
      + "       abs(rsi_rsb_account.RestAll(t.res_pdd_acc, 1, t.fiid, :dt, 0)) res_pdd_rest_natcur, "
      + "       t.res_kor_up_acc, "
      + "       abs(rsi_rsb_account.RestAll(t.res_kor_up_acc, 1, t.fiid, :dt, 0)) res_kor_up_rest, "
      + "       t.res_kor_down_acc, "
      + "       abs(rsi_rsb_account.RestAll(t.res_kor_down_acc, 1, t.fiid, :dt, 0)) res_kor_down_rest, "
      + "       t.bccost, "
      + "       t.dealdate, "
      + "       t.rescateg, "
      + "       t.resperc, "
      + "       t.t_dealcode "
      + "from (select bnr.t_bcseries, "
      + "             bnr.t_bcnumber, "
      + "             bnr.t_issuername, "
      + "             bnr.t_holdername, "
      + "             (select t_ccy from dfininstr_dbt where t_fi_kind = 1 and t_fiid = leg.t_pfi) facevaluefi, "
      + "             leg.t_pfi fiid, "
      + "             bnr.t_issuedate, "
      + "             leg.t_principal, "
      + "             leg.t_formula, "
      + "             leg.t_principal-lnk.t_bccost discount_all, "
      + "             leg.t_price * power(10, -leg.t_point)/100 percent, "
      + "             bnr.t_bctermformula termformula, "
      + "             leg.t_maturity maturity, "
      + "             leg.t_expiry expiry, "
      + "             leg.t_basis basis, "
      + "             leg.t_start startdate, "
      + "             /*Счет учета номинала*/ "
      + "             (select t_account from dmcaccdoc_dbt "
      + "              where     t_dockind = 164 /*Первичный документ - Вексель*/ "
      + "                    and t_docid = bnr.t_bcid "
      + "                    and t_catnum = case when instr(bnr.t_bcstate, 'Ч') > 0 then 1492 /*КУ - Просроч_вексель*/ else 462 /*КУ - Учтенные векселя*/ end "
      + "                    and t_firole = 5 /*Вексель*/ "
      + "                    and t_isusable = chr(88)) body_acc, "
      + "             /*Счет учета процентов и дисконта*/ "
      + "             nvl((select t_account from dmcaccdoc_dbt "
      + "                  where     t_dockind = 164 "
      + "                        and t_docid = bnr.t_bcid "
      + "                        and t_catnum = case when instr(bnr.t_bcstate, 'Ч') > 0 then 1492 /*КУ - Просроч_вексель*/ else 464 /*КУ - Начисл.ПДД, УВ*/ end "
      + "                        and t_firole = case when leg.t_formula = 1 then 42 /*процент*/ else 43 /*дисконт*/ end "
      + "                        and t_iscommon = chr(0) "
      + "                        and t_isusable = chr(88)), ' ') pdd_acc, "
      + "             /*Корректировка, увеличивающая стоимость векселя*/ "
      + "             nvl((select t_account from dmcaccdoc_dbt "
      + "                  where     t_dockind = 164 "
      + "                        and t_docid = bnr.t_bcid "
      + "                        and t_catnum = 1391 /*КУ - КорСт_Увеличение, вексель*/ "
      + "                        and t_firole = 5 "
      + "                        and t_iscommon = chr(0) "
      + "                        and t_isusable = chr(88)), ' ') kor_up_acc, "
      + "             /*Корректировка, уменьшающая стоимость векселя*/ "
      + "             nvl((select t_account from dmcaccdoc_dbt "
      + "                  where     t_dockind = 164 "
      + "                        and t_docid = bnr.t_bcid "
      + "                        and t_catnum = 1392 /*КУ - КорСт_Уменьшение, вексель*/ "
      + "                        and t_firole = 5 "
      + "                        and t_iscommon = chr(0) "
      + "                        and t_isusable = chr(88)), ' ') kor_down_acc, "
      + "              /*счет учета резерва (тело)*/ "
      + "             nvl((select t_account from dmcaccdoc_dbt "
      + "                  where     t_dockind = 164 "
      + "                        and t_docid = bnr.t_bcid "
      + "                        and t_catnum = case when instr(bnr.t_bcstate, 'Ч') > 0 then 1460 /*КУ - РезервПросроч, вексель*/ else 463 /*КУ - Резерв, вексель*/ end "
      + "                        and t_isusable = chr(88) "
      + "                        and t_firole = 5 /*Роль ФИ - Вексель*/), ' ') res_acc, "
      + "             /*Счет учета резерва (пдд)*/ "
      + "             nvl((select t_account from dmcaccdoc_dbt "
      + "                  where     t_dockind = 164 "
      + "                        and t_docid = bnr.t_bcid "
      + "                        and t_catnum = case when instr(bnr.t_bcstate, 'Ч') > 0 then 1460 /*КУ - РезервПросроч, вексель*/ else 463 /*КУ - Резерв, вексель*/ end "
      + "                        and t_firole = case when leg.t_formula = 1 then 42 /*процент*/ else 43 /*дисконт*/ end "
      + "                        and t_iscommon = chr(0) "
      + "                        and t_isusable = chr(88)), ' ') res_pdd_acc, "
      + "             /*Корректировка, увеличивающая резерв*/ "
      + "             nvl((select t_account from dmcaccdoc_dbt "
      + "                  where     t_dockind = 164 "
      + "                        and t_docid = bnr.t_bcid "
      + "                        and t_catnum = case when instr(bnr.t_bcstate, 'Ч') > 0 then 1462 /*КУ - -Кор_РезервПроср, вексель*/ else 1395 /*КУ - -Кор_Резерв, вексель*/ end "
      + "                        and t_firole = 5 "
      + "                        and t_iscommon = chr(0) "
      + "                        and t_isusable = chr(88)), ' ') res_kor_up_acc, "
      + "             /*Корректировка, уменьшающая резерв*/ "
      + "             nvl((select t_account from dmcaccdoc_dbt "
      + "                  where     t_dockind = 164 "
      + "                        and t_docid = bnr.t_bcid "
      + "                        and t_catnum = case when instr(bnr.t_bcstate, 'Ч') > 0 then 1461 /*КУ - +Кор_РезервПроср, вексель*/ else 1394 /*КУ - +Кор_Резерв, вексель*/ end "
      + "                        and t_firole = 5 "
      + "                        and t_iscommon = chr(0) "
      + "                        and t_isusable = chr(88)), ' ') res_kor_down_acc, "
      + "             lnk.t_bccost bccost, "
      + "             tick.t_dealdate dealdate, "
      + "             tick.t_dealcode, "
      + "             nvl((select t_qualitycategory from ddlreslnk_dbt where t_parentid = bnr.t_bcid and rownum = 1), 0) rescateg, "
      + "             nvl((select t_reservepercent from ddlreslnk_dbt where t_parentid = bnr.t_bcid and rownum = 1), 0) resperc "
      + "      from ddl_leg_dbt leg, dvsbanner_dbt bnr "
      + "      left join dvsordlnk_dbt lnk on  lnk.t_bcid = bnr.t_bcid  "
      + "                              and lnk.t_linkkind = 1 /*Принимаемые*/  "
      + "                              and t_dockind = 141 /*Покупка векселя*/, "
      + "           ddl_tick_dbt tick "
      + "      where     /*bnr.t_abcstatus = 100 "
      + "            and*/ bnr.t_bcformkind = 1 "
      + "            and not exists (select 1 from ddp_dep_dbt where t_partyid = bnr.t_issuer) "
      + "            and leg.t_dealid = bnr.t_bcid "
      + "            and leg.t_legkind = 1 "
      + "            and leg.t_legid = 0 "
      + "            and tick.t_dealid = lnk.t_contractid "
      + "            and ( (:dt between bnr.t_issuedate and (bnr.t_repaymentdate - 1)) "
      + "                   or (bnr.t_repaymentdate = to_date('01.01.0001', 'dd.mm.yyyy') and bnr.t_issuedate <= :dt ) "
      + "                ) "
      + "      order by body_acc, bnr.t_bcid "
      + "      ) t ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("dt", repdate)), false);
  
  while (sql.MoveNext() )
    DaysToRepay = BnrPlanRepayDate(sql.value("termformula"),
                                   sql.value("startdate"),
                                   sql.value("maturity"),
                                   sql.value("expiry"),
                                   sql.value("basis")) - repdate;

    csvFile.AddCell("ГО");                                   // 1. ГО/РФ
    csvFile.AddCell(sql.value("issuername"));                // 2. Векселедатель
    csvFile.AddCell(sql.value("t_holdername"));              // 3. Первый векселедержатель
    csvFile.AddCell(sql.value("facevaluefi"));               // 4. Валюта номинала
    csvFile.AddCell(sql.value("t_issuedate", null, V_DATE)); // 5. Дата составления
    csvFile.AddCell(sql.value("sernum"));                    // 6. Серия Номер
    csvFile.AddCell(sql.value("t_principal"));               // 7. Номинал
    csvFile.AddCell(sql.value("percent"));                   // 8. Процентная ставка
    csvFile.AddCell(Date(sql.value("dealdate")));            // 9. Дата приобретения
    csvFile.AddCell(VS_TermFormula(sql.value("termformula"),
                                    sql.value("maturity"),
                                    sql.value("expiry")));   // 10. Плановая дата погашения
    csvFile.AddCell("");                                     // 11. Срок платежа по векселю
    csvFile.AddCell(DaysToRepay);                            // 12. Срок до погашения векселя
    csvFile.AddCell(sql.value("bccost"));                    // 13. Цена приобретения
    csvFile.AddCell(sql.value("rescateg"));                  // 14. Категория качества
    csvFile.AddCell(sql.value("resperc"));                   // 15. Размер расчетного резерва, %
    csvFile.AddCell(sql.value("body_acc"));                  // 16. Лицевой счет
    csvFile.AddCell(sql.value("body_rest"));                 // 17. Остаток в валюте
    csvFile.AddCell(sql.value("body_rest_natcur"));          // 18. Остаток в рублях
    csvFile.AddCell(sql.value("res_acc"));                   // 19. Балансовый счет резерва
    csvFile.AddCell(sql.value("res_rest"));                  // 20. Остаток в валюте
    csvFile.AddCell(sql.value("res_rest_natcur"));           // 21. Остаток в рублях
    csvFile.AddCell(sql.value("pdd_acc"));                   // 22. Балансовый счет дисконта/процента
    csvFile.AddCell(sql.value("pdd_rest"));                  // 23. Остаток в валюте
    csvFile.AddCell(sql.value("pdd_rest_natcur"));           // 24. Остаток в рублях
    csvFile.AddCell(sql.value("res_pdd_acc"));               // 25. Балансовый счет резерва по дисконту/проценту
    csvFile.AddCell(sql.value("res_pdd_rest"));              // 26. Остаток в валюте
    csvFile.AddCell(sql.value("res_pdd_rest_natcur"));       // 27. Остаток в рублях
    csvFile.AddCell(sql.value("kor_down_acc"));              // 28. Балансовый счет уменьшающей корректировки
    csvFile.AddCell(sql.value("kor_down_rest_natcur"));      // 29. Остаток в рублях
    csvFile.AddCell(sql.value("kor_up_acc"));                // 30. Балансовый счет увеличивающей корректировки
    csvFile.AddCell(sql.value("kor_up_rest_natcur"));        // 31. Остаток в рублях
    csvFile.AddCell(sql.value("res_kor_down_acc"));          // 32. Балансовый счет корректировки, уменьшающей резерв
    csvFile.AddCell(sql.value("res_kor_down_rest"));         // 33. Остаток в рублях
    csvFile.AddCell(sql.value("res_kor_up_acc"));            // 34. Балансовый счет корректировки, увеличивающей резерв
    csvFile.AddCell(sql.value("res_kor_up_rest"));           // 35. Остаток в рублях
    csvFile.AddCell("");                                     // 36. Агентство
    csvFile.AddCell("");                                     // 37. Рейтинг
    csvFile.AddCell(sql.value("t_dealcode"));                // 38. Договор
    csvFile.AddCell("");                                     // 39. Место хранения
    csvFile.AddCell("");                                     // 40. Зона ответственности
    csvFile.AddRow();
  end;
  csvFile.FileClose();
  csvTable.FillTabelFromCSV(csvFile.GetlsFileName());
  csvTable.EndTable();
  Rep.CopyAllSheetInTotalBook(null, false, csvTable.GetTableRange(), 0, "Отчет");
  Rep.Close();
  Rep.SaveTotalBook();

End;

va_report();

exit(1);