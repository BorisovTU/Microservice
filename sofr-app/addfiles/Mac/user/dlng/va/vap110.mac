/*
$Name:         vap110.mac
$Module:       Учтенные векселя
$Description:  Залог векселей. Закрытие залога МБК

            va-вексель
             p-операция залога векселей (pawn)
           110-номер (соответствующий шагу)
*/

import vapawnbk, vamisc, vapawn, oralib, likepy, "mail.mac", BankInter;

private var StepDate = {curdate}; /* Дата выполнения шага */

/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_tick)
  var stat = 0, sql;
  var sum:money = $0;
  record tick( dl_tick );

  SetBuff( tick, dl_tick );

  sql = "select nvl(sum((select RSB_STRUCT.GETMONEY(t_text) from dnotetext_dbt where t_objecttype=24 and t_notekind=101 and to_number(t_documentid)=b.t_bcid)), 0) "
      + "from dvsbanner_dbt b "
      + "where t_issuer not in (select t_partyid from ddp_dep_dbt) and instr(t_bcstate, 'Н')>0 and upper(t_description) = 'Z' ";
  sql = ExecSqlSelect(sql);
  if (sql.MoveNExt() )
    sum = sql.value(0);
  end;
  if(not ОтрОбесп(tick, StepDate, sum, true)) // проводка "ОтрОбесп"
    stat = 1;
  end;

  sql = "update dvsbanner_dbt b set t_description = 'O' "
      + "where t_issuer not in (select t_partyid from ddp_dep_dbt) and instr(t_bcstate, 'Н')>0 and upper(t_description)='Z' ";
  if (ExecSql(sql) == null)
    stat = 1;
  end;

  return stat;
END;


/* Макрос постобработки
*/
MACRO PostStep (
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */
  var query_rollback = "update dvsbanner_dbt b set t_description = 'Z' "
                     + "where t_issuer not in (select t_partyid from ddp_dep_dbt) and instr(t_bcstate, 'Н')>0 and upper(t_description)='O' ";
  var regval = "";
  record tick( dl_tick );
  SetBuff( tick, FirstDoc );

  if (errTrn OR (CommitOrRollback==2))
    /* Произошла ошибка или происходит откат */
    
    ExecSql(query_rollback);
    return;
  end;

  var v_email_processor = email_proc_env();
  var mail_arr = TArray();
  var i = 0;

  GetRegistryValue("ВЕКСЕЛЯ БАНКА\\РСХБ\\MAIL_BO_VEKS", V_STRING, regval);
  mail_arr(mail_arr.size) = regval;

  while (i < mail_arr.Size)
   v_email_processor.m_add_email_to_list(mail_arr(i));
   i = i + 1;
  end;

  v_email_processor.m_set_msg_head("Уведомление об оформлении залогов векселей в МБК");
  v_email_processor.m_add_row_to_msg_text("Вывод из залога. " + VA_GetVekselList(ID_Operation) + ". Договор " + tick.DealCode);

  /* Сохраняем текст письма для отправки плановой процедурой */
  v_email_processor.m_save_to_submit();
  /* Отправляем все не отправленные письма */
  v_email_processor.m_submit_email();

  return 1;
END;