import rsd;
record deal     (dvndeal);
record panel    (reentry2, "mbk_oper.lbr" ) dialog;
record errpanel (err2    , "mbk_oper.lbr" ) dialog;
record panel2    (reentry3, "mbk_oper.lbr" ) dialog;
record errpanel2 (err3    , "mbk_oper.lbr" ) dialog;

var _code = 0, _dealId = 0; 
var Deal_main, Deal_dlg;
var Deal_buy, Deal_sale, Deal_buy_dlg, Deal_sale_dlg;

class cDeal (id, type)
      var dealId, dealtype, dealDate, dealWay;
      var summ_BA, cur_BA, date_BA;
      var summ_K,  cur_K,  date_K;

      macro InitFields ()
            this.dealId   = 0;
            this.dealtype = -1;
            this.dealDate = date(0,0,0);
            this.dealWay  = "";
            this.summ_BA  = 0;
            this.cur_BA   = "";
            this.date_BA  = date(0,0,0);
            this.summ_K   = 0;
            this.cur_K    = "";
            this.date_K   = date(0,0,0);
      end;

      macro getDealInfo (id, type)
            var sql, rs;
            sql = ( " select dv.t_date,                                                                     "+
                    "        decode (dv.t_type, 1, 'Покупка', 2, 'Продажа', 5, 'СВОП', chr(0)) as deal_way, "+
                    "        vn.t_amount as summ_BA,                                                        "+
                    "        (select fi.t_iso_number                                                        "+
                    "           from dfininstr_dbt fi                                                       "+
                    "          where fi.t_fiid = vn.t_fiid                                                  "+
                    "            and fi.t_fi_kind = 1                                                       "+
                    "            and fi.t_isclosed = chr(0)) as cur_BA,                                     "+
                    "        vn.t_supldate as date_BA,                                                      "+
                    "        vn.t_cost as summ_K,                                                           "+
                    "        (select fi.t_iso_number                                                        "+
                    "           from dfininstr_dbt fi                                                       "+
                    "          where fi.t_fiid = vn.t_pricefiid                                             "+
                    "            and fi.t_fi_kind = 1                                                       "+
                    "            and fi.t_isclosed = chr(0)) as cur_K,                                      "+
                    "        vn.t_paydate as date_K                                                         "+
                    "  from DDVNDEAL_DBT dv                                                                 "+
                    "  join ddvnfi_dbt vn                                                                   "+
                    "     on vn.t_dealid = dv.t_id                                                          "+
                    "     and vn.t_type = :1                                                                 "+
                    "  where dv.t_id = :2 ");
      
            sql = rsdcommand (sql);
            sql.addParam(":1", RSDBP_IN, type);
            sql.addParam(":2", RSDBP_IN, id);
            rs  = rsdrecordset (sql);

      return rs;
        OnError(er)
           MsgBox (er.Message,"| Модуль: ",er.Module,"| строка: ",er.Line );
           return false;
      end;

      macro loadDealInfo (id, type)
            private var m_rs = getDealInfo (id, type);  
              
            if (m_rs.movenext () )
                 this.dealId   = id;
                 this.dealtype = type;
                 this.dealDate = m_rs.value("t_date");
                 this.dealWay  = m_rs.value("deal_way");
                 this.summ_BA  = m_rs.value("summ_BA");
                 this.cur_BA   = m_rs.value("cur_BA");
                 this.date_BA  = m_rs.value("date_BA");
                 this.summ_K   = m_rs.value("summ_K");
                 this.cur_K    = m_rs.value("cur_K");
                 this.date_K   = m_rs.value("date_K");
            else
                 InitFields();   
            end;
      end;

      if (id != null )
         loadDealInfo (id, type);
      else
         InitFields();    
      end;   
end;

Macro PanelErr (dlg, cmd, id, key ) 
   if (cmd == DLG_INIT)

      dlg.date1   = Deal_dlg.dealDate;  
      dlg.way     = Deal_dlg.dealWay;    
      dlg.summ_BA = Deal_dlg.summ_BA;
      dlg.cur_BA  = Deal_dlg.cur_BA; 
      dlg.date_BA = Deal_dlg.date_BA;
      dlg.summ_K  = Deal_dlg.summ_K; 
      dlg.cur_K   = Deal_dlg.cur_K;  
      dlg.date_K  = Deal_dlg.date_K; 
   
      dlg.xDate1   = Deal_main.dealDate;   
      dlg.xWay     = Deal_main.dealWay;
      dlg.xSumm_BA = Deal_main.summ_BA; 
      dlg.xCur_BA  = Deal_main.cur_BA;   
      dlg.xDate_BA = Deal_main.date_BA;   
      dlg.xSumm_K  = Deal_main.summ_K;
      dlg.xCur_K   = Deal_main.cur_K;
      dlg.xDate_K  = Deal_main.date_K;
      
      updateFields (dlg);
   
   end;
   if ( cmd == DLG_KEY )
      if ( key == 27 ) 
         return CM_CANCEL;
      else
         return CM_IGNORE;
      end;
   end;
End;

Macro PanelErr2 (dlg, cmd, id, key ) 
   if (cmd == DLG_INIT)
      //покупка
      //--введено
      dlg.date1   = Deal_buy_dlg.dealDate;  
      dlg.way     = Deal_buy_dlg.dealWay;    
      dlg.summ_BA = Deal_buy_dlg.summ_BA;
      dlg.cur_BA  = Deal_buy_dlg.cur_BA; 
      dlg.date_BA = Deal_buy_dlg.date_BA;
      dlg.summ_K  = Deal_buy_dlg.summ_K; 
      dlg.cur_K   = Deal_buy_dlg.cur_K;  
      dlg.date_K  = Deal_buy_dlg.date_K; 
      //--в сделке
      dlg.xDate1   = Deal_buy.dealDate;   
      dlg.xWay     = Deal_buy.dealWay;
      dlg.xSumm_BA = Deal_buy.summ_BA; 
      dlg.xCur_BA  = Deal_buy.cur_BA;   
      dlg.xDate_BA = Deal_buy.date_BA;   
      dlg.xSumm_K  = Deal_buy.summ_K;
      dlg.xCur_K   = Deal_buy.cur_K;
      dlg.xDate_K  = Deal_buy.date_K;
      //продажа
      //--введено
      dlg.sell_summ_BA = Deal_sale_dlg.summ_BA;
      dlg.sell_cur_BA  = Deal_sale_dlg.cur_BA; 
      dlg.sell_date_BA = Deal_sale_dlg.date_BA;
      dlg.sell_summ_K  = Deal_sale_dlg.summ_K; 
      dlg.sell_cur_K   = Deal_sale_dlg.cur_K;  
      dlg.sell_date_K  = Deal_sale_dlg.date_K; 
      //--в сделке
      dlg.xSell_Summ_BA = Deal_sale.summ_BA; 
      dlg.xSell_Cur_BA  = Deal_sale.cur_BA;   
      dlg.xSell_Date_BA = Deal_sale.date_BA;   
      dlg.xSell_Summ_K  = Deal_sale.summ_K;
      dlg.xSell_Cur_K   = Deal_sale.cur_K;
      dlg.xSell_Date_K  = Deal_sale.date_K;
      updateFields (dlg);
   
   end;
   if ( cmd == DLG_KEY )
      if ( key == 27 ) 
         return CM_CANCEL;
      else
         return CM_IGNORE;
      end;
   end;
End;
Macro ScrollWAY ()
   var res;
   var way = Tarray ();
   way [0] = "Покупка";   
   way [1] = "Продажа";    
   way [2] = "СВОП";
   
   var tway = Menu(way, "Выберите направление сделки", "Выберите направление сделки", 27, 11);
   if (tway >= 0)
      res = way[tway];
   else
      res = "Нет";
   end;
   return res;
End;

macro compareDeals (deal1, deal2)
       if ( 
            (deal1.dealDate != deal2.dealDate) or
            (deal1.dealWay  != deal2.dealWay) or
            (deal1.summ_BA  != deal2.summ_BA  ) or
            (deal1.cur_BA   != deal2.cur_BA) or
            (deal1.date_BA  != deal2.date_BA)  or
            (deal1.summ_K   != deal2.summ_K )or
            (deal1.cur_K    != deal2.cur_K ) or
            (deal1.date_K   != deal2.date_K ) 
            ) 
          return false;
     else return true;
     end;
end;

Macro mainPanel2 (dlg, cmd, id, key ) 
      
      if (cmd == DLG_INIT)
          Deal_buy = cDeal (_dealId, 0); //0-своп-покупка
          Deal_sale = cDeal (_dealId, 2); //2-своп-продажа
          Deal_buy_dlg  = cDeal ();
          Deal_sale_dlg  = cDeal ();

      elif (cmd == DLG_KEY)
            if (key == 323)  //F9
               Deal_buy_dlg.dealDate = dlg.date1;
               Deal_buy_dlg.dealWay  = dlg.way;  
               Deal_buy_dlg.summ_BA  = dlg.summ_BA;
               Deal_buy_dlg.cur_BA   = dlg.cur_BA;
               Deal_buy_dlg.date_BA  = dlg.date_BA;
               Deal_buy_dlg.summ_K   = dlg.summ_K;
               Deal_buy_dlg.cur_K    = dlg.cur_K;
               Deal_buy_dlg.date_K   = dlg.date_K;

               Deal_sale_dlg.dealDate = dlg.date1;
               Deal_sale_dlg.dealWay  = dlg.way;  
               Deal_sale_dlg.summ_BA  = dlg.sell_summ_BA;
               Deal_sale_dlg.cur_BA   = dlg.sell_cur_BA;
               Deal_sale_dlg.date_BA  = dlg.sell_date_BA;
               Deal_sale_dlg.summ_K   = dlg.sell_summ_K;
               Deal_sale_dlg.cur_K    = dlg.sell_cur_K;
               Deal_sale_dlg.date_K   = dlg.sell_date_K;

               if ( (not compareDeals (Deal_buy, Deal_buy_dlg )) or
                    (not compareDeals (Deal_sale, Deal_sale_dlg )) 
                   )
                  msgbox ("Верификация не выполнена !");
                  _code = 1;
                  rundialog ( errpanel2, "PanelErr2" );
               else 
                  msgbox ("Верификация выполнена !");
                  _code = 0;
               end; 
               return CM_CANCEL;
            elif ((key == 317) and (id == FldIndex(dlg, "way")));
                dlg.way = ScrollWAY();
                updateFields (dlg);   
            elif (key == 27) //Esc
               msgbox ("Верификация не выполнена !");
               _code = 1;
               return CM_CANCEL;
            elif (key == 13);
               return CM_IGNORE;   
            end;
      end;

End;
Macro mainPanel (dlg, cmd, id, key ) 
      
      if (cmd == DLG_INIT)
          Deal_main = cDeal (_dealId, 0); //0-покупка/продажа
          Deal_dlg  = cDeal ();
      elif (cmd == DLG_KEY)
            if (key == 323)  //F9
               Deal_dlg.dealDate = dlg.date1;
               Deal_dlg.dealWay  = dlg.way;  
               Deal_dlg.summ_BA  = dlg.summ_BA;
               Deal_dlg.cur_BA   = dlg.cur_BA;
               Deal_dlg.date_BA  = dlg.date_BA;
               Deal_dlg.summ_K   = dlg.summ_K;
               Deal_dlg.cur_K    = dlg.cur_K;
               Deal_dlg.date_K   = dlg.date_K;

               if (not compareDeals (Deal_main, Deal_dlg ))
                  msgbox ("Верификация не выполнена !");
                  _code = 1;
                  rundialog ( errpanel, "PanelErr" );
               else 
                  msgbox ("Верификация выполнена !");
                  _code = 0;
               end; 
               return CM_CANCEL;
            elif ((key == 317) and (id == FldIndex(dlg, "way")));
                dlg.way = ScrollWAY();
                updateFields (dlg);   
            elif (key == 27) //Esc
               msgbox ("Верификация не выполнена !");
               _code = 1;
               return CM_CANCEL;
            elif (key == 13);
               return CM_IGNORE;   
            end;
      end;

End;
macro ExecuteStep( doc, FDoc )
   
   SetBuff(deal, FDoc);
   
   _dealId = deal.id;
	//временно mda
   if ({oper} == deal.oper)
      msgbox ("Пользователь № "+ deal.oper+", вводивший сделку, не может ее верифицировать");
      _code = 1;
      // return _code;
   end;
   if (deal.kind == 12730) //покупка-продажа
      rundialog ( panel, "mainPanel" );
   elif ((deal.kind == 12740) or (deal.kind == 12710))     //своп
      rundialog ( panel2, "mainPanel2" );
   end;   

   return _code;

  return 0;
end;
