/*
  Загрузка фиксинга из файла xml
  Симанов А.В.
  
Update:
  Перевод из текстового протокола в Excel. Сохранение протокола.
  Пичугин А.В. BIQ-9318. 12.10.2021 

*/
import dvsw250, oralib, likepy, globals, "scsrvrepfun.mac", "xls_parser.mac", "jvmutils.mac";

private var errors = TArray();
private var Data;

PRIVATE CONST NullCell = "Undefined";

private macro AddErr (err:string)
  errors[errors.size] = err;
  return false;
End;

private macro ByDefault (parm, default_value)
  if (valtype(parm) == V_UNDEF)
    SetParm(0, default_value);
  end;
End;

//Копирует файл на сервер приложений. Возвращает полный путь на СП
private macro CopyOnSP (FullNameFile)
  var path = "";
  var FileName, ExtName, DirName;
  var TermPath = GetCurDir(true) + "\\txtfile\\";

  //Отделяем путь файла от имени
  splitfile(FullNameFile, FileName, ExtName);
  FileName = FileName + ExtName;
  DirName = substr(FullNameFile, 1, index(FullNameFile, FileName) - 1);

  if (CopyFile(DirName + FileName, TermPath + FileName))
    path = TermPath + FileName;
  elif (CopyFile("$" + DirName + FileName, "$" + TermPath + FileName))
    path = TermPath + FileName;
  else 
    msgbox("Не удалось скопировать загружаемый файл на сервер приложений!");
  end;
  
  return path;
End;

//Simanov. Слижком уж часто приходится делать процедурки с открытием того или иного файла. На перспективу это можно будет вынести в отдельный макрос
/* Открывает xml и возвращает его как объект MSXML.DOMDocument в параметр _xml.
 * mask: маска для выбора файла. По умолчанию "*.xml"
 * term: true - выбор файла на терминале. false - на сервере
 *
 * Вовращает: null - пользователь отказался от выбора файла (нажал ESC)
 *            false - ошибка при открытии файла
 *            true - файл успешно выбран
 */
macro OpenXMLFile (_xml, mask, term, error_message:@string)
  ByDefault(mask, "*.xml");
  ByDefault(term, true);

  var FullNameFile;
  var xml:object = ActiveX("MSXML.DOMDocument");

  /*Выбираем файл для обработки*/
  if(not SelectFile(FullNameFile, mask, "Выберите файл для загрузки", 0, term))
    return null;
  end;

  //В условиях РСХБ файл можно открыть только на СП. Если файл не на СП, то необходимо его туда копировать
/* AEA на копирование у пользователя не хватает прав
  if (term)
    FullNameFile = CopyOnSP(FullNameFile);
  end;
*/
  /* откроем файл импорта */
  if( not xml.load(FullNameFile) )
    error_message = "Неверный формат файла импорта\n" + FullNameFile + "|Невозможно загрузить xml-документ";
    return false;
  end;

  if (not RemoveFile (FullNameFile))
    error_message = "Ошибка удаления файла " + FullNameFile;
  end;

  SetParm(0, xml);
  return true;
End;

private macro OpenFileXls( xls, DataFileName ):bool
  if( (not xls) OR (not xls.Workbooks) OR (not xls.Workbooks.Open(DataFileName)) )
    return false;
  end;
  return true;
  
  OnError( er )            
    return false;
end;

macro OpenXLSBFile (_xlsb, mask, term, error_message:@string)
  ByDefault(mask, "*.xlsb");
  ByDefault(term, true);

  var FullNameFile;
  var xlsb:object;
  var startAX:object;
  /*Выбираем файл для обработки*/
  if(not SelectFile(FullNameFile, mask, "Выберите файл для загрузки", 0, term))
    return null;
  end;

  if(isStandAlone())
    xlsb = ActiveX("Excel.Application", null, false);
    if( not xlsb )
      return error_message = "Невозможно создать объект Excel.Application";
    end;
  else
    startAX = CreateObject("rsax", "TRsAxServer", "RTSRateImport", isStandalone());
    if( startAX )
      xlsb = startAX.CreateComObject("Excel.Application");
    end;
    if( (not startAX) OR (not xlsb) )
      return error_message = "Невозможно создать объект Excel.Application";
    end;
  end;

  //В условиях РСХБ файл можно открыть только на СП. Если файл не на СП, то необходимо его туда копировать
/* AEA на копирование у пользователя не хватает прав
  if (term)
    FullNameFile = CopyOnSP(FullNameFile);
  end;
*/               
  /* откроем файл импорта */
  if( not OpenFileXls( xlsb, FullNameFile ) )
    return error_message = "Невозможно загрузить xls-документ";
  end;
        
  /*
  if (not RemoveFile (FullNameFile))
    error_message = "Ошибка удаления файла " + FullNameFile;
  end;
  */
  SetParm(0, xlsb);
  return true;
End;

macro OpenXLSBFilePOIReader (_poiReader, mask, term, error_message:@string)
  ByDefault(mask, "*.xlsb");
  ByDefault(term, true);

  var FullNameFile;

  /*Выбираем файл для обработки*/
  if(not SelectFile(FullNameFile, mask, "Выберите файл для загрузки", 0, term))
    return null;
  end;

  var poiReader = CExcelPoi();

  var tempFilePath = PathCombine(WR_GetRepFullPath(term),GetExlusiveFileName("xlsb"));
  var tempOutFilePath = PathCombine(WR_GetRepFullPath(term),GetExlusiveFileName("xlsx"));
  if (not CopyFile(iif(term,"$","") + FullNameFile, iif(term,"$","") + tempFilePath))
    RunError("Не удалось скопировать выбранный файл по временному пути");
  end;
  if (not WR_ConvertToFormatLibreOffice(iif(term,"$","")+tempFilePath, iif(term,"$","") + tempOutFilePath, WINREP_FORMAT_XLSX, term, false ))
    RunError("Не удалось преобразовать файл в xlsx формат");
  end;
  RemoveFile(iif(term,"$","") + tempFilePath);

  if (term)
    var newPath = PathCombine2(WR_GetRepFullPath(false),SubStr(CreateGUID(), 2, 36),".xlsx");
    if (not CopyFile("$" + tempOutFilePath, newPath))
      RunError("Не удалось скопировать выбранный файл по временному пути");
    end;
    RemoveFile("$" + tempOutFilePath);
    tempOutFilePath = newPath;
  end;

  if (not poiReader.Open(tempOutFilePath))
    RunError("Не удалось открыть файл при помощи POI");
  end;

  SetParm(0, poiReader);
  return true;
End;

//-------------------------------------------------------------------
//Класс для формирования протокола выполнения
class c_fixing_Rep () 

  // Формируем отчет в Excel через ActiveX
  var ob = CreateObject ("rsax","TRsAxServer","RsBankAxServer",false);
  var excelBook = ob.CreateComObject ("Excel.Application",false);
  excelBook.Workbooks.Add;
  	
  var columnNum = 1;
  var rowNum = 8;

  macro PrintHead ()
    excelBook.Columns("A:A").ColumnWidth = 10;
    excelBook.Columns("B:B").ColumnWidth = 100;
    excelBook.Range("A1:E1").Merge();
    excelBook.Cells(1, 1).HorizontalAlignment = -4108;
    excelBook.Cells(1, 1).Value = "Протокол выполнения операций фиксинга";
    excelBook.Cells(1, 1).Font.Size = 14;
    excelBook.Cells(1, 1).Font.Bold = TRUE;
    excelBook.Range ("A2:E2").Merge();
    excelBook.Range ("A3:E3").Merge();
    excelBook.Cells(3,1).Value = "Дата запуска: " + {curdate};
    excelBook.Cells(3,1).Font.Size = 12;
    excelBook.Range("A4:E4").Merge();
    excelBook.Cells(4,1) = "Операционист: "+{Name_Oper};
    excelBook.Cells(4,1).Font.Size = 12;
    
    if (Data.DataArr.Size > 0)
      excelBook.Columns("A:A").ColumnWidth = 15;
      excelBook.Columns("B:B").ColumnWidth = 110;
      excelBook.Columns("C:C").ColumnWidth = 30;
      excelBook.Range("A7:C7").HorizontalAlignment = -4108;
      excelBook.Range("A7:C7").Borders.LineStyle = True; 
      excelBook.Range("A7:C7").WrapText = True;
      excelBook.Cells(7,1).Value() = "Номер сделки";
      excelBook.Cells(7,2).Value() = "Данные по промежуточным платежам";
      excelBook.Cells(7,3).Value() = "Результат";
    end;
  End;

  macro PrintLine (DealCode, definition, result)
    excelBook.Cells(rowNum, columnNum).Value() = DealCode;
    excelBook.Cells(rowNum, columnNum + 1).Value() = definition;
    excelBook.Cells(rowNum, columnNum + 2).Value() = result;
    excelBook.Range("A" + rowNum + ":C" + rowNum).Borders.LineStyle = True;
    excelBook.Range("A" + rowNum + ":C" + rowNum).WrapText = True;
    rowNum = rowNum + 1;
  End;

  macro ShowErrors ()
    var i = 0;
        
    if (errors.size > 0) 
      excelBook.Cells(rowNum, columnNum).Value() = "Ошибки при выполнении";
      rowNum = rowNum + 1;
    end;
		
    while (i < errors.size)
      excelBook.Cells(rowNum, columnNum).Value() = i+1;
      excelBook.Cells(rowNum, columnNum + 1).Value() = errors[i];
      excelBook.Range("A" + rowNum + ":B" + rowNum).Borders.LineStyle = True;
      excelBook.Range("A" + rowNum + ":B" + rowNum).WrapText = True;
      rowNum = rowNum + 1;
      i = i + 1;
    end;
  End;

  macro ShowRep (count)
    if (Data.DataArr.size > 0)
      rowNum = rowNum + 1;
      excelBook.Cells(rowNum, 1).Value() = "Успешно обработано: " + count;
      rowNum = rowNum + 2;			
    end;

    ShowErrors();
		
    excelBook.Visible = true;

    var pathFile : string = "";
    GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\IMPORTFIXINGPROT", V_STRING, pathFile);
    pathFile = Trim(pathFile + "\\");

    var protocolDate = StrSubst (StrSubst (string ({curdate}), " ", "0"), ".", "");
    var obTDirList = TDirList("$" + pathFile +  "protocol_" + protocolDate + "_*.xlsx", "F");
    var protocolName : string = "protocol_" + protocolDate + "_" + (obTDirList.Count + 1) + ".xlsx";
  
    if (pathFile == "\\") // Пустой путь проверка
      msgbox("Не задана настройка реестра: РСХБ\\ДИРЕКТОРИИ\\IMPORTFIXINGPROT\n Автоматическое сохранение протокола невозможно!\n Сохраните протокол в ручную!");
    elif ((ExistDir("$" + pathFile) == 1) or (ExistDir("$" + pathFile) == 2)) 
      if (not MakeDir("$" + pathFile)) 
        msgbox("Заданный путь: " + pathFile + " не доступен!\n Автоматическое сохранение протокола невозможно!\n Сохраните протокол в ручную!");
      else
        excelBook.ActiveWorkbook.SaveAs(pathFile + protocolName);
      end;
    else
      excelBook.ActiveWorkbook.SaveAs(pathFile + protocolName);
    end;
  End;
END;

class c_fixing_Rep_Poi () 

  // Формируем отчет в Excel через Poi
  var jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
  var poiRepCreator = jvm.callStaticMethod("ru.softlab.rsbank.poireports.ReportCreator", "getInstance");
  var poiBook = poiRepCreator.addWorkbookXLSX();
  var poiWorkBook = poiBook.getCurrentWorkbook();
  var poiCurrentSheet = poiWorkBook.{getSheetAt@(I)Lorg/apache/poi/xssf/usermodel/XSSFSheet;}(0); 
  var poiCurrentRow = poiCurrentSheet.{createRow@(I)Lorg/apache/poi/xssf/usermodel/XSSFRow;}(0);
  var poiCurrentCell = null;

  private macro GetAllBorderStyle()
    var cellStyle = poiWorkBook.{createCellStyle@()Lorg/apache/poi/ss/usermodel/CellStyle;}();
    var borderStyle = GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.BorderStyle", "THIN");
    var borderColor = GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.IndexedColors", "BLACK");

    cellStyle.setBorderRight(borderStyle);
    cellStyle.setRightBorderColor(borderColor.getIndex());
    cellStyle.setBorderLeft(borderStyle);
    cellStyle.setLeftBorderColor(borderColor.getIndex());
    cellStyle.setBorderBottom(borderStyle);
    cellStyle.setBottomBorderColor(borderColor.getIndex());
    cellStyle.setBorderTop(borderStyle);
    cellStyle.setTopBorderColor(borderColor.getIndex());
    return cellStyle;
  end;
  
  var columnNum = 0;
  var rowNum = 7;

  macro PrintHead ()
    var headerStyle = poiWorkBook.{createCellStyle@()Lorg/apache/poi/ss/usermodel/CellStyle;}();
    var headerFont = poiWorkBook.{createFont@()Lorg/apache/poi/xssf/usermodel/XSSFFont;}();
    headerFont.setFontHeightInPoints(14);
    headerFont.setBold(true);
    headerStyle.setFont(headerFont);
    headerStyle.setAlignment(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.HorizontalAlignment", "CENTER"));

    poiCurrentCell = poiCurrentRow.{createCell@(I)Lorg/apache/poi/xssf/usermodel/XSSFCell;}(0);
    poiCurrentCell.{setCellValue@(Ljava/lang/String;)V}("Протокол выполнения операций фиксинга");
    poiCurrentCell.setCellStyle(headerStyle);
    poiCurrentSheet.setColumnWidth(0, 10 * 256);
    poiCurrentSheet.setColumnWidth(1, 100 * 256);
    var mergeAddress = jvm.createJavaObject("org.apache.poi.ss.util.CellRangeAddress",
                                            "<init>@(IIII)V", 0, 0, 0, 4);
    poiCurrentSheet.addMergedRegion(mergeAddress);

    poiCurrentRow = poiCurrentSheet.{createRow@(I)Lorg/apache/poi/xssf/usermodel/XSSFRow;}(1);

    mergeAddress = jvm.createJavaObject("org.apache.poi.ss.util.CellRangeAddress",
                                            "<init>@(IIII)V", 1, 1, 0, 4);
    poiCurrentSheet.addMergedRegion(mergeAddress);

    poiCurrentRow = poiCurrentSheet.{createRow@(I)Lorg/apache/poi/xssf/usermodel/XSSFRow;}(2);
    poiCurrentCell = poiCurrentRow.{createCell@(I)Lorg/apache/poi/xssf/usermodel/XSSFCell;}(0);

    poiCurrentCell.{setCellValue@(Ljava/lang/String;)V}("Дата запуска: " + {curdate});
    headerFont = poiWorkBook.{createFont@()Lorg/apache/poi/xssf/usermodel/XSSFFont;}();
    headerStyle = poiWorkBook.{createCellStyle@()Lorg/apache/poi/ss/usermodel/CellStyle;}();
    headerFont.setFontHeightInPoints(12);
    headerStyle.setFont(headerFont);
    poiCurrentCell.setCellStyle(headerStyle);
    mergeAddress = jvm.createJavaObject("org.apache.poi.ss.util.CellRangeAddress",
                                            "<init>@(IIII)V", 2, 2, 0, 4);
    poiCurrentSheet.addMergedRegion(mergeAddress);

    poiCurrentRow = poiCurrentSheet.{createRow@(I)Lorg/apache/poi/xssf/usermodel/XSSFRow;}(3);
    poiCurrentCell = poiCurrentRow.{createCell@(I)Lorg/apache/poi/xssf/usermodel/XSSFCell;}(0);
    poiCurrentCell.{setCellValue@(Ljava/lang/String;)V}("Операционист: "+{Name_Oper});
    headerFont = poiWorkBook.{createFont@()Lorg/apache/poi/xssf/usermodel/XSSFFont;}();
    headerStyle = poiWorkBook.{createCellStyle@()Lorg/apache/poi/ss/usermodel/CellStyle;}();
    headerFont.setFontHeightInPoints(12);
    headerStyle.setFont(headerFont);
    poiCurrentCell.setCellStyle(headerStyle);
    mergeAddress = jvm.createJavaObject("org.apache.poi.ss.util.CellRangeAddress",
                                            "<init>@(IIII)V", 3, 3, 0, 4);
    poiCurrentSheet.addMergedRegion(mergeAddress);
    
    if (Data.DataArr.Size > 0)
      poiCurrentSheet.setColumnWidth(0, 15 * 256);
      poiCurrentSheet.setColumnWidth(1, 110 * 256);
      poiCurrentSheet.setColumnWidth(2, 30 * 256);

      poiCurrentRow = poiCurrentSheet.{createRow@(I)Lorg/apache/poi/xssf/usermodel/XSSFRow;}(6);
      poiCurrentCell = poiCurrentRow.{createCell@(I)Lorg/apache/poi/xssf/usermodel/XSSFCell;}(0);
      poiCurrentCell.{setCellValue@(Ljava/lang/String;)V}("Номер сделки");
      var cellStl = GetAllBorderStyle();
      cellStl.setWrapText(true);
      cellStl.setAlignment(GetJavaEnumConstByName(jvm, "org.apache.poi.ss.usermodel.HorizontalAlignment", "CENTER"));
      poiCurrentCell.setCellStyle(cellStl);

      poiCurrentCell = poiCurrentRow.{createCell@(I)Lorg/apache/poi/xssf/usermodel/XSSFCell;}(1);
      poiCurrentCell.{setCellValue@(Ljava/lang/String;)V}("Данные по промежуточным платежам");
      poiCurrentCell.setCellStyle(cellStl);

      poiCurrentCell = poiCurrentRow.{createCell@(I)Lorg/apache/poi/xssf/usermodel/XSSFCell;}(2);
      poiCurrentCell.{setCellValue@(Ljava/lang/String;)V}("Результат");
      poiCurrentCell.setCellStyle(cellStl);
    end;
  End;

  macro PrintLine (DealCode, definition, result)
    poiCurrentRow = poiCurrentSheet.{createRow@(I)Lorg/apache/poi/xssf/usermodel/XSSFRow;}(rowNum);
    poiCurrentCell = poiCurrentRow.{createCell@(I)Lorg/apache/poi/xssf/usermodel/XSSFCell;}(columnNum);
    poiCurrentCell.{setCellValue@(Ljava/lang/String;)V}(String(DealCode));
    var cellSt = GetAllBorderStyle();
    cellSt.setWrapText(true);
    poiCurrentCell.setCellStyle(cellSt);

    poiCurrentCell = poiCurrentRow.{createCell@(I)Lorg/apache/poi/xssf/usermodel/XSSFCell;}(columnNum+1);
    poiCurrentCell.{setCellValue@(Ljava/lang/String;)V}(String(definition));
    poiCurrentCell.setCellStyle(cellSt);

    poiCurrentCell = poiCurrentRow.{createCell@(I)Lorg/apache/poi/xssf/usermodel/XSSFCell;}(columnNum+2);
    poiCurrentCell.{setCellValue@(Ljava/lang/String;)V}(String(result));
    poiCurrentCell.setCellStyle(cellSt);
    rowNum = rowNum + 1;
  End;

  macro ShowErrors ()
    var i = 0;
        
    if (errors.size > 0)
      poiCurrentRow = poiCurrentSheet.{createRow@(I)Lorg/apache/poi/xssf/usermodel/XSSFRow;}(rowNum);
      poiCurrentCell = poiCurrentRow.{createCell@(I)Lorg/apache/poi/xssf/usermodel/XSSFCell;}(columnNum);
      poiCurrentCell.{setCellValue@(Ljava/lang/String;)V}("Ошибки при выполнении");
      rowNum = rowNum + 1;
    end;
		
    while (i < errors.size)
      poiCurrentRow = poiCurrentSheet.{createRow@(I)Lorg/apache/poi/xssf/usermodel/XSSFRow;}(rowNum);
      poiCurrentCell = poiCurrentRow.{createCell@(I)Lorg/apache/poi/xssf/usermodel/XSSFCell;}(columnNum);
      poiCurrentCell.{setCellValue@(D)V}(i+1);
      var cellS = GetAllBorderStyle();
      cellS.setWrapText(true);
      poiCurrentCell.setCellStyle(cellS);

      poiCurrentCell = poiCurrentRow.{createCell@(I)Lorg/apache/poi/xssf/usermodel/XSSFCell;}(columnNum+1);
      poiCurrentCell.{setCellValue@(Ljava/lang/String;)V}(errors[i]);
      poiCurrentCell.setCellStyle(cellS);
      rowNum = rowNum + 1;
      i = i + 1;
    end;
  End;

  macro ShowRep (count)
    if (Data.DataArr.size > 0)
      rowNum = rowNum + 1;
      poiCurrentRow = poiCurrentSheet.{createRow@(I)Lorg/apache/poi/xssf/usermodel/XSSFRow;}(rowNum);
      poiCurrentCell = poiCurrentRow.{createCell@(I)Lorg/apache/poi/xssf/usermodel/XSSFCell;}(0);
      poiCurrentCell.{setCellValue@(Ljava/lang/String;)V}("Успешно обработано: " + count);
      rowNum = rowNum + 2;			
    end;

    ShowErrors();
    debugbreak;

    var servFileName =  GetExlusiveFileName("xlsx");

    // poiBook.saveAs(PathCombine(GetAbsoluteTxtPath(),servFileName));
    var pathFile : string = "";
    GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\IMPORTFIXINGPROT", V_STRING, pathFile);
    pathFile = Trim(pathFile + "\\");
    var protocolDate = StrSubst (StrSubst (string ({curdate}), " ", "0"), ".", "");
    var obTDirList = TDirList("$" + pathFile +  "protocol_" + protocolDate + "_*.xlsx", "F");
    var protocolName : string = "protocol_" + protocolDate + "_" + (obTDirList.Count + 1) + ".xlsx";
    var termFilePath = "$"+ GetCurDir(true) + StrSubst(pathFile, "..", "") ;


    if (pathFile == "\\") // Пустой путь проверка
      msgbox("Не задана настройка реестра: РСХБ\\ДИРЕКТОРИИ\\IMPORTFIXINGPROT\n Автоматическое сохранение протокола невозможно!\n Сохраните протокол в ручную!");
    elif ((ExistDir(pathFile) == 1) or (ExistDir(pathFile) == 2)) 
      if (not MakeDir(pathFile)) 
        msgbox("Заданный путь: " + pathFile + " не доступен!\n Автоматическое сохранение протокола невозможно!\n Сохраните протокол в ручную!");
      else
        poiBook.saveAs(pathFile + servFileName);
        // CopyFile(PathCombine(GetAbsoluteTxtPath(),servFileName),"$" + pathFile + protocolName);
        CopyFile(pathFile + servFileName, termFilePath + protocolName );
      end;
    else
      poiBook.saveAs(pathFile + servFileName);
      // CopyFile(PathCombine(GetAbsoluteTxtPath(),servFileName),"$" + pathFile + protocolName);
      CopyFile(pathFile + servFileName, termFilePath + protocolName );
    end;

    // SC_ShowFile(GetAbsoluteTxtPath(), servFileName);
      SC_ShowFile(termFilePath, protocolName);
  End;
END;

/************************************************************************************/
//Класс для работы со структурой xml для фиксинга
private class c_xml_fixing ()
  //Номера колонок в исходном файле
  private const COL_INFO       = 0,
                COL_FIX_DATE   = 1,
                COL_END_DATE   = 5, /*PNV 515513*/
                COL_PAY_DATE   = 6, /*PNV 515513*/
                COL_AMOUNT     = 9,
                COL_RATE       = 10;

  private const TAGNAME_ROW  = "Row",
                TAGNAME_CELL = "Cell",
                TAGNAME_DATA = "Data",
                ATTRNAME_TYPE = "ss:Type",
                ATTRNAME_ROWCOUNT = "ss:ExpandedRowCount";

  private var xml:object; //Объект XMLDOM
  private var table_node;
  private var rows_node; //Узел со строками
  private var row_node; //Узел с одной строкой
  private var cell_node; //Узел с ячейками

  macro Init (_xml)
    xml = _xml;
    table_node = xml.SelectSingleNode("/Workbook/Worksheet/Table");
    rows_node   = table_node.SelectNodes(TAGNAME_ROW);
  End;

  //Актуализирует данные для строки row
  macro InitNodes (row)
    row_node = rows_node.item(row);
    cell_node = null;

    if (valtype(row_node) != V_UNDEF ) 
      cell_node = row_node.SelectNodes(TAGNAME_CELL);
    end;
  End;

  //Количество значимых строк
  macro RowsCount ():integer
    return table_node.attributes.getNamedItem(ATTRNAME_ROWCOUNT).value;
  End;

  //Конвретирует дату из формата 2019-04-30T00:00:00.000
  private macro ConvertDate (xmldate:string):Date
    var year:integer, mm:integer, dd:integer;
    year = SubStr(xmldate, 1, 4);
    mm = SubStr(xmldate, 6, 2);
    dd = SubStr(xmldate, 9, 2);

    return Date(dd, mm, year);
  End;

  //Проверка, что ячейка существует и не пустая
  private macro CheckValidCellNode (itemID)
    return ((valtype(cell_node) != V_UNDEF) and (valtype(cell_node.item(itemID)) != V_UNDEF));
  End;

  //Значение из колонки с номером itemID
  private macro GetValue (itemID)
    if ( CheckValidCellNode(itemID) )
      return cell_node.item(itemID).text;
    end;
    return "";
  End;

  //Тип значения в колонке "Principal Amount"
  private macro CellAmount_TypeName ()
    var data_amount_node = null;
    if ( CheckValidCellNode(COL_AMOUNT) )
      data_amount_node = cell_node.item(COL_AMOUNT).SelectSingleNode(TAGNAME_DATA);
    end;

    if ( valtype(data_amount_node) != V_UNDEF )
      return data_amount_node.attributes.getNamedItem(ATTRNAME_TYPE).value;
    end;
    return "";
  End;

  //Значение из первой колонки
  macro Info ()
    return GetValue(COL_INFO);
  End;

  //Значение из колонки с суммой
  macro Amount ():money
    if (CellAmount_TypeName() == "Number") 
      return GetValue(COL_AMOUNT);
    end;
    return $0;
  End;

  //Дата фиксинга из файла
  macro Date ():Date
    return ConvertDate(GetValue(COL_FIX_DATE));
  End;
/*PNV 515513*/
  //Дата окончания из файла
  macro EndDate ():Date
    return ConvertDate(GetValue(COL_End_DATE));
  End;

  //Дата платежа из файла
  macro PAYDate ():Date
    return ConvertDate(GetValue(COL_PAY_DATE));
  End;
/*PNV 515513*/

  macro Rate ()
    return GetValue(COL_RATE);
  End;

  //Направление
  macro Side (default_side):variant
    var str = Info;
    if (index(str, "Cash flow") > 0)
      if (index(str, "receive") > 0) 
        return ALG_DV_PMGR_SIDE_DEMAND; //Требование
      elif (index(str, "pay") > 0)
        return ALG_DV_PMGR_SIDE_LIABILITY; //Обязательство
      end;
    end;
    return default_side;
  End;

  macro Destructor ()
    row_node = cell_node = rows_node = table_node = xml = null;
  End;
END;
/************************************************************************************/


/****************************************/
private class c_xlsb_fixing ()
  //Номера колонок в исходном файле
  private const COL_INFO       = "B",
                COL_FIX_DATE   = "D",
                COL_BEGIN_DATE = "E",
                COL_END_DATE   = "F",
                COL_PAY_DATE   = "G",
                COL_AMOUNT     = "J",
                COL_RATE       = "K",
                COL_RECEIVING  = "N",
                COL_PAYING     = "O";


  private var xlsb:object; 
  private var Book;
  private var Sheet;

  private var poiMode = false;

  private var poiReader;


  macro Init (_xlsb)
    xlsb = _xlsb;
    Book  = xlsb.Application.Workbooks.Item(1);
    Sheet = Book.Sheets("Sheet-1");
  End;

  macro InitPoi(_poiReader)
    poiMode = true;
    poiReader = _poiReader;
    poiReader.SheetChange("Sheet-1");
  end;

  //Конвретирует дату из формата 2019-04-30T00:00:00.000
  private macro ConvertDate (xmldate:string):Date
    var year:integer, mm:integer, dd:integer;
    year = SubStr(xmldate, 1, 4);
    mm = SubStr(xmldate, 6, 2);
    dd = SubStr(xmldate, 9, 2);

    return Date(dd, mm, year);
  End;

  macro GetFromRange(addr:string)
    if (poiMode)
      return poiReader.CellValue(addr);
    else 
      return Sheet.Range(addr).value;
    end;
  end;

  macro GetCol_Info()             /*Получить букву столбца с номером сделки*/
    return COL_INFO;
  End;                            
  macro GetCol_FixDate()          /*Получить букву столбца с датой фиксинга*/
    return COL_FIX_DATE;
  End;
  macro GetCol_BeginDate()          /*Получить букву столбца с датой начала периода*/
    return COL_Begin_DATE;
  End;
  macro GetCol_EndDate()          /*Получить букву столбца с датой окончания периода*/
    return COL_END_DATE;
  End;
  macro GetCol_PayDate()          /*Получить букву столбца с датой платежа*/
    return COL_PAY_DATE;
  End;
  macro GetCol_Amount()           /*Получить букву столбца с суммой платежа в нац. валюте*/
    return COL_AMOUNT;
  End;
  macro GetCol_Rate()             /*Получить букву столбца с значением ставки*/
    return COL_RATE;
  End;
  macro GetCol_Receiving()        /*Получить букву столбца с Требованием*/
    return COL_RECEIVING;
  End;
  macro GetCol_Paying()           /*Получить букву столбца с Обязательством*/
    return COL_PAYING;
  End;

  macro Destructor ()
    if (not poiMode)
      xlsb.Quit ();
      Book = Sheet = xlsb = null;
    end;
  End;
END;
/***********************************/

private class c_fixing_parms ()
  var dealID:integer;
  var Department:integer;
  var parms = TArray();
END;

//Класс с данными по фиксингу из файла
private class c_collectData
  var DataArr = TArray();

  //В массиве parms всегда будет только один элемент класса c_fixing_parms
  private macro appendNewRecord (Department, DealID, parms)
    var arrsize = DataArr.Size;
    DataArr[arrsize] = c_fixing_parms;
    DataArr[arrsize].Department = Department;
    DataArr[arrsize].dealId     = DealID;
    DataArr[arrsize].parms[0]   = parms;
  End;

  macro AddRec (Department, DealID, PaymID, Sum:money, Rate, dt, side, FIID)
    var index;
    var parm = FixingParm();
    parm.id       = PaymID;
    parm.sum      = Abs(Sum);
    parm.rate     = Rate;
    parm.dvkind   = Side;
    parm.operDate = dt;
    parm.fiid     = FIID;

    appendNewRecord(Department, DealID, parm);
  End;
END;

/************************************************************************************/

private macro GetDealID (dealCode, NettingPMGR:@bool):integer
  var DealID = 0;
  var sql = ExecSqlSelect("select t_id, t_netting_pmgr from ddvndeal_dbt where t_dvkind = 4 and t_code = :code", MakeArray(SqlParam("code", DealCode)));
  if (sql.MoveNext() )
    DealID = sql.value(0);
    NettingPMGR = ifThenElse(sql.value(1)=="X", true, false);
  end;
  return DealID;
End;

private macro GetDealCodeByID (dealid)
  var dealcoe = "";
  var sql = ExecSqlSelect("select t_code from ddvndeal_dbt where t_id = :dealid", MakeArray(SqlParam("dealid", dealid)));
  if (sql.MoveNext() )
    dealcoe = sql.value(0);
  end;
  return dealcoe;
End;

//Возвращает ИД промежуточного платежа
private macro GetPaymID (DealID, side, dt, Enddate, PayDate):integer /*PNV 515513*/
  var PaymID:integer = 0;
  var sql = ExecSqlSelect("select t_id from ddvnpmgr_dbt where t_dealid = :dealid and t_side = :side and t_paydate = :PayDate/* and t_fixdate <= :dt*/", /*PNV 515513*/
                          MakeArray(SqlParam("dealid", DealID), SqlParam("side", side), SqlParam("PayDate", PayDate)/*, SqlParam("dt", dt)*/)); /*PNV 515513*/
  if (sql.MoveNext() )
    PaymID = sql.value(0);
  end;
  return PaymID;
End;

private macro GetFIID (dealid, side)
  var fiid = -1;
  var query = "select t_fiid from ddvnfi_dbt where t_dealid = " + dealid + "and t_type = " + ifThenElse(side==ALG_DV_PMGR_SIDE_LIABILITY, 0, 2);
  var sql = ExecSqlSelect(query);
  if (sql.MoveNext() )
    fiid = sql.value(0);
  end;
  return fiid;
onerror()
  return -1;
End;

private macro GetPaym(PaymID)
  var paym;
  var query = "select * from ddvnpmgr_dbt where t_id = " + PaymID;
  var sql = ExecSqlSelect(query);
  if (sql.MoveNext() )
     paym = sql;
  end;
  return paym;
onerror()
  return -1;
End;

private macro IsFloatRate(dealid, dealtype)   /*проверяем по требованиям/обязательсвтам плавающая ставка или нет*/
   var query = "select t_rate from ddvnfi_dbt where t_dealid = " + dealid + " and t_type = " + dealtype;
   var sql = ExecSqlSelect(query);
   if (sql.MoveNext() )
      if (sql.value("t_rate") == 0)
         return true;
      end;
   end;
   return false;
End;

private macro GetDealRate(dealid, dealtype)     /*получаем фиксированную ставку*/
   var query = "select t_rate from ddvnfi_dbt where t_dealid = " + dealid + " and t_type = " + dealtype;
   var sql = ExecSqlSelect(query);
   if (sql.MoveNext() )
      return sql.value("t_rate")
   end;
   return 0;
End;

private macro GetFirstPaym(dealid)  /*находим первый платеж с плавающей ставкой*/
  var query = "select t_id from ddvnpmgr_dbt where t_dealid = " + dealID + " and t_floatratevalue > 0 ORDER BY t_id";
  var sql = ExecSqlSelect(query);
  if (sql.MoveNext() )
     return sql.value(0);
  end;
  return -1;
End;

private macro GetNameCell(Type:string, i:integer)
   return Type + string(i);
end;

/************************************************************************************/

private macro ReadFile ()  
  var xlsb:object;
  //var fixingDate:Date = Date(10, 10, 2019); //test
  var fixingDate:Date = {curdate}; 
  var dealCode = "", dealID = 0;
  var stat, error_message = "", err = 0;
  var NettingPMGR:bool = false;
  var rows_count:integer = 0;
  var side:integer = -1;
  var BeginDate = fixingDate;
  var EndDate = fixingDate;  /*PNV 515513*/
  var PayDate = fixingDate;  /*PNV 515513*/
  var amount:double;
  var row = 0;
  var PaymID, FIID;
  var Paym, Rate;
  var Sheet;
  var FixDate = fixingDate;
  var xlsb_fixing = c_xlsb_fixing;
  var DealRate;
  var poiReader;

  var runFromWine = IsRunFromWine();

  if (runFromWine)
    stat = OpenXLSBFilePOIReader(poiReader,"$\\..\\..\\..\\*.xlsb",true,error_message);
  else
    stat = OpenXLSBFile(xlsb, "$\\..\\..\\..\\*.xlsb", true, error_message);  
  end;
   
  if (valtype(stat) == V_UNDEF)
    return null; //отказ от выбора файла
  elif (not stat)
    return AddErr(error_message); //Ошибка при открытии файла
  end;

  if (runFromWine)
    xlsb_fixing.InitPoi(poiReader);
  else
    xlsb_fixing.Init(xlsb);
  end;

  data = c_collectData();
  var i:integer = 6;  
  /*считаем количество сделок в документе*/
  while (1)
    if(string(xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_Info() + string(i) )) != NullCell)
      i = i + 1;
      rows_count = rows_count + 1;
    else
      i = 6;
      BREAK;
    end;
  End;   

  initProgress(rows_count, "Чтение файла", "Чтение файла");

  /*Бежим по файлу*/
  while(1)
    dealcode = amount = EndDate = PayDate = Rate = FixDate = Side = null;
    /*Получаем код сделки*/
    if(string(xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_Info() + string(i) )) != NullCell)
      dealCode = xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_Info() + string(i) );
    end;
    /*Получаем сумму платежа*/
    if(string(xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_Amount() + string(i) )) != NullCell)
      amount = xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_Amount() + string(i) );
    end;
    /*Получаем дату начала периода*/
    if(string(xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_BeginDate() + string(i) )) != NullCell)
      // BeginDate = Date(1, 1, 1900) + Int(xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_BeginDate() + string(i) )) - 2;
      BeginDate = xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_BeginDate() + string(i) );
    end;
    /*Получаем дату окончания периода*/
    if(string(xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_EndDate() + string(i) )) != NullCell)
      // EndDate = Date(1, 1, 1900) + Int(xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_EndDate() + string(i) )) - 2;
      EndDate = xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_EndDate() + string(i) );
    end;
    /*Получаем дату исполнения платежа*/
    if(string(xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_PayDate() + string(i) )) != NullCell)
      // PayDate = Date(1, 1, 1900) + Int(xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_PayDate() + string(i) )) - 2;
      PayDate = xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_PayDate() + string(i) );
    end;
    /*Получаем дату фиксинга*/
    if(string(xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_FixDate() + string(i) )) != NullCell)
      // FixDate = Date(1, 1, 1900) + Int(xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_FixDate() + string(i) )) - 2;
      FixDate = xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_FixDate() + string(i) );
    end;
    /*Получаем значение ставки*/
    if(string(xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_Rate() + string(i) )) != NullCell)
      Rate = xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_Rate() + string(i) );
    end;
    /*Полчучаем направление платежа*/
    if((string(xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_Receiving() + string(i) )) != NullCell) and (string(xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_Receiving() + string(i) )) != "")) /*PNV 537372*/
      Side = ALG_DV_PMGR_SIDE_DEMAND;
    elif (string(xlsb_fixing.GetFromRange(xlsb_fixing.GetCol_Paying() + string(i) )) != NullCell)
      Side = ALG_DV_PMGR_SIDE_LIABILITY;
    else
      Side = -1;
    end;

    if ( (Dealcode == Null) and
         (amount   == Null) and
         (EndDate  == Null) and
         (PayDate  == Null) and
         (Rate     == Null)
       )
       BREAK;
    end;

    if (Dealcode != "")
       DealID = GetDealID(dealCode, @NettingPMGR);
       if (DealID == 0)
          AddErr("Не найдена сделка с номером " + dealCode);
       end;
    end;
    //Строка с непустой суммой фиксинга.
    if ( (dealID > 0) and (amount != 0) )     
      PaymID   = GetPaymID(Dealid, ifThenElse(NettingPMGR, 0, side), fixingDate, EndDate, Paydate); /*PNV 515513*/
      FIID     = GetFIID(dealid, side);
      Paym     = GetPaym(PaymID);
      DealRate = GetDealRate(dealID, ifThenElse( (Side == 1), 0, 2) );
      /*Дополнительные проверки нужны только если платеж с фиксированной ставкой или если это платеж в первом периоде*/
      if (PaymID == 0)
            AddErr("По сделке " + dealCode + " не найден платеж по " + ifThenElse( (Side == 1), "обязательству", "требованию") + " за период с " + BeginDate + " по " + EndDate + " с датой платежа " + PayDate);
            err = err + 1;         
      elif ( (not IsFloatRate(dealID, ifThenElse( (Side == 1), 0, 2)) ) /*or (GetFirstPaym(DealID) == PaymID)*/ )   /*если платеж - обязательство, то смотрим ставку первой части сделки, иначе вторую*/               
         if (Paym.value("t_paymentsum") != abs(amount))
            AddErr("По сделке " + dealCode + " в платеже за период с " + BeginDate + " по " + EndDate + " сумма платежа отличается от суммы платежа в файле фиксинга. Сумма в системе: " + Paym.value("t_paymentsum") + " в файле: " + abs(amount));
            err = err + 1;
         end;
         if (Paym.value("t_floatratevalue") != 0)    /*если есть значение плавающей ставки, то сравниваем ставку из файла фиксинга с данным значением*/
            Dealrate = Paym.value("t_floatratevalue");            
         end;
         if (DealRate != Rate)
            AddErr("По сделке " + dealCode + " в платеже за период с " + BeginDate + " по " + EndDate + " ставка платежа " + DealRate + " отличается от значения ставки в файле фиксинга " + Rate);
            err = err + 1;
         end;  
      end;
      if (fixingDate != Date(FixDate))
        AddErr("По сделке "+dealCode+" дата фиксинга " + FixDate + " отличается от текущего опер. дня");
        err = err + 1;
      end;
      if (err == 0)
        data.AddRec({OperDPRT}, dealID, PaymID, amount, Rate, FixDate, side, FIID);
      end;
      dealID = amount = PaymID = err = 0;
      dealCode = "";
      FIID = -1;
    end;

    i = i + 1;
    UseProgress(row = row + 1);
  end;

  xlsb_fixing.Destructor();

  RemProgress();
  return true;
onerror()
  RemProgress();
  return false;
End;

macro StartFixing ()
  var i = 0;
  var stat;
  var Rep;
  if (IsRunFromWine())
    Rep = c_fixing_Rep_Poi();
  else
    Rep = c_fixing_Rep();
  end;
  var PaymInfo = "";
  var count_successfull = 0;
  var dealCode;
  var parm;
 
  stat = ReadFile();
  if (valtype(stat) == V_UNDEF) //Если ничего не возвращает - не был выбран файл - прекращаем обработку
    return false;
  elif (not stat)
    AddErr("Ошибка при обработке файла");
  end;

  Rep.PrintHead;
  initProgress(Data.DataArr.Size, "Выполнение фиксинга", "Выполнение фиксинга");
  while (i < Data.DataArr.Size)
    dealCode = GetDealCodeByID(Data.DataArr[i].DealID);
    parm     = Data.DataArr[i].parms[0];
    PaymInfo = ifThenElse(parm.dvkind==ALG_DV_PMGR_SIDE_DEMAND, "Требование", "Обязательство")+"; "+parm.operDate+"; Сумма="+parm.sum+"; Ставка="+parm.rate;
  
    stat = RunFixing(Data.DataArr[i].Department, Data.DataArr[i].DealID, Data.DataArr[i].parms);
    if (stat != 0) 
      AddErr("Ошибка при выполнении фиксинга по сделке " + dealCode);
    else
      count_successfull = count_successfull + 1;
    end;

    Rep.PrintLine(dealCode, PaymInfo, ifThenElse(stat==0, "Успешно", "Ошибка"));
    UseProgress(i = i + 1);
  end;
  RemProgress();

  Rep.ShowRep(count_successfull);
  return true;

onerror()
  RemProgress();
  return true; //true - чтобы вывести на экран ошибки
End;

StartFixing();
Exit(1);