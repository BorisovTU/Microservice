/*
$Name:        RevisUrgTrans.mac (на основе дистрибутивного revision.mac для БОЦБ)
$Module:      ФИСС и КО
$Description: Процедура проведения ревизии при завершении дня
$Programmer:  Кротов А.
*/
import "spRepFun.mac", RevisUrgTransPanel, dv_period;

private var IsReportRun = false,
            AllErrorCount  = 0,
            AllObjectCount = 0;

private var allPortf = TArray();

private macro PrintHead( RevDate:Date, SubTitle:String )
   if( IsReportRun == false )
      IsReportRun = true;
      println( "\n                       Протокол процедуры ревизии счетов" );
      println( " Дата ревизии: ", RevDate );
   end;

   Message( SubTitle + " ..." );
   println( "\n ", SubTitle, "\n" );
end;

private macro PrintFooter()
   if( IsReportRun == false )
      MsgBoxEX( "В параметрах ревизии не задано ни одного вида проверок.", 0, 0, "Запуск процедуры ревизии", "Запуск процедуры ревизии" );
      exit(1);
   end;
   println();
   FooterExecuterSign({oper});
end;

private macro PrintError( ErrorNum:@Integer )
   var i, curval, StrError = "";

   i = 1;
   while( GetParm(i, curval) )
      StrError = StrError + string(curval);
      i = i + 1;
   end;

[   #########################################################################################

]
(StrError:w);

   ErrorNum = ErrorNum + 1;
   InitError();
end;

private macro ПроверитьОбработкуПлатежей( RevDate:Date )

   var ErrorNum = 0;

   macro GetDocCode(dbfile, docid)
     var SelectDoc, DataSetDoc, QueryDoc;
     if(dbfile == "dl_tick.dbt")
       QueryDoc = "select t_dealcode t_code "
                + "from ddl_tick_dbt "
                + "where t_dealid = ?";
     elif(dbfile == "dvdeal.dbt")
       QueryDoc = "select t_code "
                + "from ddvdeal_dbt "
                + "where t_id = ?";
     elif(dbfile == "dvfipos.dbt")
       QueryDoc = "select fininstr.t_fi_code t_code "
                + "from ddvfipos_dbt dvfipos "
                + "join dfininstr_dbt fininstr on fininstr.t_fiid = dvfipos.t_fiid "
                + "where dvfipos.t_id = ?";
     elif(dbfile == "dvoper.dbt")
       QueryDoc = "select t_code "
                + "from ddvoper_dbt "
                + "where t_id = ?";
     elif(dbfile == "dvndeal.dbt")
       QueryDoc = "select t_code "
                + "from ddvndeal_dbt "
                + "where t_id = ?";
     elif(dbfile == "ps_bcord.mac")
       QueryDoc = "select t_number t_code "
                + "from dpmrmprop_dbt "
                + "where t_paymentid = ?";
     else
       return "???";
     end;
     SelectDoc = DL_RSDCommand(QueryDoc);
     SelectDoc.addParam(docid);
     DataSetDoc = SelectDoc.execute(QueryDoc);
     if(DataSetDoc.MoveNext())
       return DataSetDoc.value("t_code");
     else
       return "?";
     end;
   end;

   macro CheckPaymByDocKind( DocKind:Integer, CheckKindTxt )
      var SelectCurPaym, DataSet, QueryCurPaym;
      var PaymNum, ObjectNum = 0;

      QueryCurPaym = "select 'Платеж \"'||nvl(pmpurp.t_shortname,'?')||'\"'|| "
                   + "       ' на сумму '||translate(ltrim(to_char(t_baseamount,'999G999G999G999G999G999G990D99',  'NLS_NUMERIC_CHARACTERS = .\"')),'\"','''')||' '||case when fininstra.t_fi_kind is null then '?' when fininstra.t_fi_kind = 1 then fininstra.t_ccy else fininstrd.t_fi_code end|| "
                   + "       ', сумма дебета '||translate(ltrim(to_char(t_futurepayeramount,'999G999G999G999G999G999G990D99',  'NLS_NUMERIC_CHARACTERS = .\"')),'\"','''')||' '||case when fininstrd.t_fi_kind is null then '?' when fininstrd.t_fi_kind = 1 then fininstrd.t_ccy else fininstrd.t_fi_code end|| "
                   + "       ', сумма кредита '||translate(ltrim(to_char(t_futurereceiveramount,'999G999G999G999G999G999G990D99',  'NLS_NUMERIC_CHARACTERS = .\"')),'\"','''')||' '||case when fininstrc.t_fi_kind is null then '?' when fininstrc.t_fi_kind = 1 then fininstrc.t_ccy else fininstrc.t_fi_code end t_mes, "
                   + "       nvl(lower(oprkdoc.t_dbfile),chr(1)) t_dbfile, "
                   + "       pmpaym.t_documentid t_documentid "
                   + "from (select t_paymentid, nvl(max(t_statusid),-1) t_statusid "
                   + "      from (select pmpaym.t_paymentid t_paymentid, "
                   + "                   first_value(pmhist.t_statusidto) over(partition by pmpaym.t_paymentid order by pmhist.t_date desc, pmhist.t_autokey desc) t_statusid "
                   + "            from dpmpaym_dbt pmpaym "
                   + "      left join dpmhist_dbt pmhist on pmhist.t_paymentid = pmpaym.t_paymentid and "
                   + "                                      pmhist.t_date <= ? "
                   + "      where pmpaym.t_dockind = ? and "
                   + "            pmpaym.t_valuedate <= ?) "
                   + "      group by t_paymentid) t "
                   + "join dpmpaym_dbt pmpaym on pmpaym.t_paymentid = t.t_paymentid "
                   + "left join dpmpurp_dbt pmpurp on pmpurp.t_purpose = pmpaym.t_purpose "
                   + "left join dfininstr_dbt fininstra on fininstra.t_fiid = pmpaym.t_basefiid "
                   + "left join dfininstr_dbt fininstrd on fininstrd.t_fiid = pmpaym.t_fiid_futurepayacc "
                   + "left join dfininstr_dbt fininstrc on fininstrc.t_fiid = pmpaym.t_fiid_futurerecacc "
                   + "left join doprkdoc_dbt oprkdoc on oprkdoc.t_dockind = pmpaym.t_dockind "
                   + "where t.t_statusid not in(" + PM_OVERDUE + ", " + PM_FINISHED + ", " + PM_CLOSED_W_M_MOVEMENT + ")";

      SelectCurPaym = DL_RSDCommand(QueryCurPaym);
      SelectCurPaym.addParam(RevDate);
      SelectCurPaym.addParam(DocKind);
      SelectCurPaym.addParam(RevDate);

      PaymNum = SelectCurPaym.GetCount();

      QueryCurPaym = QueryCurPaym + " order by pmpaym.t_paymentid";

      DataSet = SelectCurPaym.execute(QueryCurPaym);

      InitProgress(int(PaymNum), "Проверка обработки платежей", CheckKindTxt);
      InitError(); 

      while(DataSet.MoveNext())
         PrintError(@ErrorNum, CheckKindTxt + ", код \"" + GetDocCode(DataSet.value("t_dbfile"), DataSet.value("t_documentid")) + "\". " + DataSet.value("t_mes") + " - не вынесен на просрочку/не закрыт в срок.");
         UseProgress(ObjectNum = ObjectNum + 1);
      end;

      RemProgress();
   end;

   PrintHead(RevDate, "Проверка обработки платежей");

   CheckPaymByDocKind( 192, "Операция с ПИ");
   CheckPaymByDocKind( 193, "Позиция по ПИ");
   CheckPaymByDocKind( 194, "Расчеты по ПИ");
   CheckPaymByDocKind( 199, "Внебиржевая сделка с ПИ");
   CheckPaymByDocKind(4815, "Сделка Т+3");

   CheckPaymByDocKind( 100, "Конверсионная сделка");
   CheckPaymByDocKind( 200, "Поруч. на конвертацию валюты");
   CheckPaymByDocKind(4813, "Конверсионная сделка СВОП");

   if(ErrorNum > 0)
     println( " Найдено ошибок: ", ErrorNum, "\n" );
   else
     println( " Не вынесеных на просрочку/не завершенных платежей не найдено\n" );
   end;
end;


private macro ПроверитьCчетаСНулевымОстатком( RevDate:Date )
   var mcaccdoc, Sql, SqlQuery, SqlNRec, SqlNRecData;
   var ErrorNum = 0, ObjectNum = 0, NumAccDoc = 0;

   SqlQuery  = "select account, code_currency, ltrim(to_char(rest,'99999999999999999999.99')) rest "
             + "from (select account.t_account account, "
             + "             account.t_code_currency code_currency, "
             + "             rsi_rsb_account.restall(account.t_account, account.t_chapter, account.t_code_currency, ?) rest "
             + "      from daccount_dbt account "
             + "      where account.t_chapter = 1 and "
             + "            account.t_account like '61601%' and "
             + "            account.t_open_date <= ? and "
             + "            (account.t_open_close = chr(0) or account.t_close_date > ?)) "
             + "where rest != 0";

   PrintHead( RevDate, "Проверка оcтатков счетов, которые должны быть нулевыми на конец дня" );

   Sql = RSDCommand(SqlQuery);
   SqlNRec = RSDCommand("select count(1) NRec from ("+SqlQuery+")");

   Sql.addParam("", RSDBP_IN, RevDate);
   Sql.addParam("", RSDBP_IN, RevDate);
   Sql.addParam("", RSDBP_IN, RevDate);

   SqlNRec.addParam("", RSDBP_IN, RevDate);
   SqlNRec.addParam("", RSDBP_IN, RevDate);
   SqlNRec.addParam("", RSDBP_IN, RevDate);

   Sql.execute();
   SqlNRec.execute();

   SqlNRecData = TRsbDataSet(SqlNRec);
   mcaccdoc = TRsbDataSet(Sql);

   if( SqlNRecData.MoveNext() )
      NumAccDoc = int(SqlNRecData.Nrec);
   end;

   InitError(); 
   InitProgress( NumAccDoc, "Проверка оcтатков счетов, которые должны быть нулевыми на конец дня", "Просмотр счетов" );
   while( mcaccdoc.MoveNext() )
      PrintError( @ErrorNum, "Остаток счета \"", mcaccdoc.Account,
                  "\" не равен нулю ", "(", mcaccdoc.Rest, " ", 
                  ПолучитьКодФинИн( mcaccdoc.Code_Currency, FICK_ISOSTRING), ")"
                );
      UseProgress(ObjectNum = ObjectNum + 1);
   end;
   RemProgress();

   if(ErrorNum > 0)
     println( " Найдено ошибок: ", ErrorNum, "\n" );
   else
     println( " Счетов с нулевыми остатками на конец дня не найдено\n" );
   end;
end;


private macro ПроверитьСрочность( RevDate:Date )
   record СчетДебета(account);
   record СчетКредита(account);
   var FD, ErrorNum = 0, ObjectNum = 0;
   var queryTxt, querySelect, cmd, rsdeals, cnt = 0;   

   macro CheckAccount(КатУчета, AccFIID, FIRoleFI, ObjName)
      var stat, TransfDate, BegDate, FinDate;

      BegDate = FD.GetParametr(MC_TYPE_PARAMETR_BEGDATE, RevDate, КатУчета, FD.GetBasisFiRole(FIRoleFI));
      FinDate = FD.GetParametr(MC_TYPE_PARAMETR_FINDATE, RevDate, КатУчета, FD.GetBasisFiRole(FIRoleFI));
      stat = ПИ_НужноПереноситьПоСрокам(FD, КатУчета, BegDate, FinDate, FD.ID, AccFIID, FIRoleFI, TransfDate, FD.Department(), null, RevDate);

      if((stat == true) AND (TransfDate <= RevDate))
         PrintError(@ErrorNum, "Для", ObjName, "сделки \"", FD.DealCode, "\" не выполнен перенос на счет, соответствующий сроку до даты исполнения ", FinDate, ".");
      else
         return true;
      end;

      InitError();
   end;

   queryTxt = "select t_id "
            + "from ddvndeal_dbt dvndeal "
            + "join doproper_dbt oproper on oproper.t_dockind in (199, 4815) and "
            + "                             oproper.t_documentid = lpad(dvndeal.t_id, 34, '0') "
            + "where dvndeal.t_state = 1 and "
            + "      dvndeal.t_date <= ?";

   cmd = DL_RSDCommand(queryTxt);
   
   cmd.addParam(RevDate);

   cnt = cmd.GetCount();

   PrintHead(RevDate, "Проверка соответствия внебалансовых счетов срокам исполнения сделок");
   InitProgress(int(cnt), "Проверка соответствия внебалансовых счетов срокам исполнения сделок", "Просмотр сделок");

   rsdeals = cmd.execute();

   while(rsdeals.MoveNext())    
      InitError();

      FD = DVFirstDocNDeal(DL_DVNDEAL, rsdeals.ID);

      var CredAcc:string = ""; var CredFIID:integer = ALLFININSTR; var CredFiRole = FIROLE_UNDEF; var CredAccNum:string = "";
      var DebAcc:string = ""; var DebFIID:integer = ALLFININSTR; var DebFiRole = FIROLE_UNDEF; var DebAccNum:string = "";

      ПолучитьСрочныеСчетаВнб(FD, @CredAcc, @CredFIID, @CredFiRole, @DebAcc, @DebFIID, @DebFiRole);
      if(FD.IsExistAccount(DebAcc, DebAccNum, true, DebFiRole, СчетДебета, null, RevDate, DebFIID))
        CheckAccount(DebAcc, DebFIID, DebFiRole, " счета \"" + DebAccNum + "\" по категории \"" + DebAcc + "\" ");
      end;
      if(FD.IsExistAccount(CredAcc, CredAccNum, true, CredFiRole, СчетКредита, null, RevDate, CredFIID))
        CheckAccount(CredAcc, CredFIID, CredFiRole, " счета \"" + CredAccNum + "\" по категории \"" + CredAcc + "\" ");
      end;

      UseProgress(ObjectNum = ObjectNum + 1);
   end;

   RemProgress();

   println( " Проверено сделок: ", ObjectNum );
   println( " Найдено ошибок: ", ErrorNum, "\n" );
end;

macro ReportRun( RevDate:date, Flag_1: bool, Flag_2: bool, Flag_3: bool)

   /*Проверить обработку платежей*/
   if( Flag_1 == true ) 
      ПроверитьОбработкуПлатежей( RevDate );
   end;

   /*Проверить оcтатки на счетах на которых должен быть нулевой остаток на конец дня*/
   if( Flag_2 == true ) 
      ПроверитьCчетаСНулевымОстатком( RevDate );
   end;

   /*Проверить срочность внебалансовых счетов*/
   if( Flag_3 == true ) 
      ПроверитьСрочность( RevDate );
   end;

   PrintFooter();
end;

/*Точка входа*/
var m_Form = RevisUrgTransPanel();
if(not m_Form.Run())
  msgboxex("Отказ от выполнения процедуры ревизии при завершении дня");
  exit(1);
end;
var f1, f2, f3;
if(m_Form.GetFieldValue(P_CheckPayments) == "X")
  f1 = true;
else
  f1 = false;
end;
if(m_Form.GetFieldValue(P_ChechRestAcc) == "X")
  f2 = true;
else
  f2 = false;
end;
if(m_Form.GetFieldValue(P_CheckDurAcc) == "X")
  f3 = true;
else
  f3 = false;
end;
ReportRun(m_Form.GetFieldValue(P_BEGINDATE), f1, f2, f3);

