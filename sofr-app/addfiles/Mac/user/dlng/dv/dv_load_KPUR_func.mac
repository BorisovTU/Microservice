/*
*/
import activex, oralib, likepy, BankInter, DVInter, globals, rsd,RsbDataSet, PTInter,CTInter, "cb_sql.mac", secinter;
import "u_common_email_utils.mac", "dlquery.mac";
import rsexts;
import "xls_parser";

private const COL_FIO               = 1,
              COL_SFNum             = 2,
              COL_DateSFBegin       = 3,
              COL_ClientCodeF       = 4,
              COL_Status            = 5,
              COL_DateBeginStatus   = 6,
              COL_DateReclassStatus = 7,
              COL_Notes             = 8;

private const Print_WellDown = true;              
private const set_char = "X";

private const OPEN_DIR = "C:\\*.xls*",
              OPEN_DIALOG_HEADER = "Выберите файл для загрузки категорий риска";

private const PARTY_ATTR_GROUP_RISKCATEG = 95,
              
              COL_VALUEDATE = 3,
              COL_CPTY      = 6,
              COL_SS        = 16;

private const ATTR_KSUR = "2",
              ATTR_KPUR = "1",
              ATTR_KOUR = "3",
              ATTR_KNUR = "4";              

private const ERR_SET_STATUS   = "Ошибка установки статуса;",
              ERR_SEND_MSG     = "Ошибка отпрвки уведомления;",              
              ERR_FIND_PT      = "Ошибка определения клиента;",
              ERR_WELL_DOWN    = "Успешная обработка.";


//Расположение загружаемого файла
var  reg_path, errcode;
 GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\КПУР", V_STRING, reg_path, errcode );
  if ( errcode != 0 )
    reg_path = "";
  end; 

if (substr(reg_path,strlen(reg_path)) != "\\")
  reg_path = string(reg_path,"\\");
end;

var  arch_path;
 GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\arcdir", V_STRING, arch_path, errcode );
  if ( errcode != 0 )
    reg_path = "";
  end; 
if (substr(arch_path,strlen(arch_path)) != "\\")
  arch_path = string(arch_path,"\\");
end;

private macro GetClientEmailByContactType(ClientID:integer, ContactType:integer)
  var Cmd, RS;
  var Email = "";

  Cmd = RSDCommand(    " SELECT c.t_Value as email"
                        + "   FROM dcontact_dbt c "
                        + "  WHERE c.t_PartyID = ? "
                        + "    AND c.t_ContactKind = " + CNTK_EMAIL
                        + "    AND c.t_ContactType = ? " 
                        + "  order by c.t_IsMain desc " );
  Cmd.AddParam(ClientID);
  Cmd.AddParam(ContactType);

  RS = RSDRecordset(Cmd, rsdval_client, rsdval_static);
  if(RS.MoveNext())
    Email = RS.value("t_Email");
  end;

  return Email;
END;

private macro SC_GetClientRepEmail(ClientID:integer, ContractID)
  var Cmd, RS;
  var email = "", contrnumber;
  if (ValType(ContractID) != V_UNDEF)

      
     Cmd = RSDCommand(String("select dlcontr.t_email ",
                      "  from ddlcontr_dbt dlcontr ",
                      " where dlcontr.t_sfcontrid = ", ContractID));
     RS = RSDRecordset(Cmd, rsdval_client, rsdval_static);
     if (RS.movenext())
        email = RS.value("t_Email");
     end;
  end;

  if(email == "")
     email = GetClientEmailByContactType(ClientID, CNTT_TOSENDREPORTS);
     if(email == "")
        email = GetClientEmailByContactType(ClientID, 1005);
        if(email == "")
           email = GetClientEmailByContactType(ClientID, 0);
        end;
     end;
  end;


  return email;
END;


private macro iif(a,b,c)
    if(a)
        return b;
    else
        return c;
    end;
end;

class ProcErr(pNumRec, pPartyName, pContractCode, pErrMsg)
  var numrec:integer      = pNumRec;
  var PartyName:string    = pPartyName;
  var ContractCode:string = pContractCode;
  var ErrMsg:string       = pErrMsg;
end;

class StatusRec()
  var FIO              :string,
      SFNum            :string,
      DateSFBegin      :date,
      ClientCodeF      :string,
      Status           :string,
      DateBeginStatus  :date,
      DateReclassStatus:date,
      Notes            :string,
      PartyID          :integer,
      SFContrID        :integer,
      DlContrID        :integer,
      EmailAddr        :string = ""; 

  /**
    @brief проверка договора на существование
    @return false - Договор не найден, true - Договор  найден
  */  
  macro existSfContrByNumber()
      var sfcontr = TBfile("sfcontr.dbt", "R", 8);
      sfcontr.rec.number = SFNum;
      if (sfcontr.GetEQ())
        return true;
      end;
      return false;
  end;
  
  macro FindClient()
  
   var sql = String( "select t1.*,   \n"
                                   ,"(SELECT  count(1)   \n"
                                   ,"        FROM dobjatcor_dbt AtCor   \n"
                                   ,"       WHERE AtCor.t_ObjectType  = 3   \n"
                                   ,"         AND AtCor.t_GroupID     = 95   \n"
                                   ,"         AND AtCor.t_Object      = LPAD(t1.t_partyid, 10, '0')   \n"
                                   ,"         and AtCor.t_Validtodate != to_date('31 12 9999','dd mm yyyy'))histrec   \n"
                                   ," from   \n"
                                   ,"(select dlc.t_dlcontrid, dlc.t_sfcontrid, sf.t_partyid, sf.t_number, sf.t_datebegin   \n"
                                   ,"  from ddlcontr_dbt dlc, dsfcontr_dbt sf, (select to_date('13 10 2020','dd mm yyyy')as r_date from dual )  params   \n"
                                   ,"where dlc.t_sfcontrid = sf.t_id) t1   \n"
                                   ,"where t1.t_number = ?   \n");
                                   
   if (ClientCodeF != "")
//     sql = string(sql, "and t_mpcode_f = ?   \n")
   end;

   var Cmd = RSDCommand( sql);
    
        Cmd.AddParam("SFNumber", rsdbp_in, SFNum); 
        if(ClientCodeF != "")
  //        Cmd.AddParam("mpCode", rsdbp_in, ClientCodeF);
        end; 
   var RS = RSDRecordset(Cmd, rsdval_client, rsdval_static);
    if (RS.MoveNext())
      PartyID   = RS.value("t_PartyID"); 
      SFContrID = RS.value("t_SFContrID");
      DlContrID = RS.value("t_DlContrID");
      return 1;
    end;    
    return 0;   
  end;

  macro SetStatus(pVal:String,ErrStr:@String)
    Status = 0; 
    if (StrUpr(pVal) == "КСУР")
       status = ATTR_KSUR;
    elif (StrUpr(pVal) == "КПУР")
       status = ATTR_KPUR;
    elif (StrUpr(pVal)  == "КОУР")
       status = ATTR_KOUR;
    elif (StrUpr(pVal)  == "КНУР")
       status = ATTR_KNUR;
    end;
    if (not int(Status))
      ErrStr = "Ошибка определения категории риска.";
      return 1;
    end;
    return 0;
  end;

  macro SetCateg()
    var err;
    if   ( (status == 0) or (not ConnectObjAttr( Err, OBJTYPE_PARTY,string(PartyID:o:10), PARTY_ATTR_GROUP_RISKCATEG, null,"", Status, {curdate} ))  OR  ( Err != 0 ))
      return 0;
    end;
    return 1;
  end;


  macro setDlCateg103 (DlContrID:integer, pAttrID:integer, pDate:Date, msg:@string, pProc:@bool)

    const Categ_Dl_Risk = 103; 
    const c_objtype_dlcontr = 207;
    private file attr("objattr.dbt") key 0;
    var DlRec  = null, Objcat = null;
    var oldCategValue = null, newCategValue = null;
    var err = 0, stat = null;
    var numInList = null;

    if(pAttrID == 1)
      numInList = 2;
    elif(pAttrID == 2)
      numInList = 1;
    else
      numInList = pAttrID;
    end;

    msg = null; pProc = False;

    newCategValue = GetNameCategValue( c_objtype_dlcontr, Categ_Dl_Risk, pAttrID, true );
    DlRec = TRecHandler( "dlcontr.dbt" );

    DL_GetRecHandler(DlRec, "SELECT DLCONTR.* FROM DDLCONTR_DBT DLCONTR WHERE DLCONTR.T_DLCONTRID = ?", DlContrID);
   
    objcat = RsbObjCategories( c_objtype_dlcontr,  UniID(DlRec, c_objtype_dlcontr) );
    if ((ObjCat.GetMainAttr( Categ_Dl_Risk, {curdate}, attr ) == 0) and (pAttrID != 0))
      if(attr.AttrID != pAttrID) 
        oldCategValue = GetNameCategValue( c_objtype_dlcontr, Categ_Dl_Risk, attr.AttrID, true );
        stat = ObjCat.ConnectAttr(Categ_Dl_Risk, NULL, "", numInList, pDate );
        if (stat > 0)
           msg = String("Ошибка "+stat+" изменения категории.");
           err = 1;
        else
           msg = String("Значение категории риска изменено c "+ oldCategValue + " на " + newCategValue + ".");
           ObjCat.Save;  
           err = 0; 
           pProc = true;           
        end;
      else
        msg = "Изменение категории не требуется.";
        err =0;
        pProc = False;
      end;
    elif(pAttrID != 0)
        stat = ObjCat.ConnectAttr(Categ_Dl_Risk, NULL, "", numInList, pDate );
        if (stat > 0)
           msg = String("Ошибка "+stat+" установки категории.");
           err = 1; 
        else
           msg = String("Значение категории " + newCategValue + " риска установлено."); 
           ObjCat.Save;   
           err = 0;
           pProc = true;
        end;
    elif((pAttrID == 0))
      msg = String("Ошибка определения категории риска."); 
      err = 1;
    end;
    return err;
  end;

  macro SendMsg()
    var err;
    var typeCateg:String;
    EmailAddr = SC_GetClientRepEmail(PartyID, SFContrID);
    if (EmailAddr == "")
      println ("Для клиента не задан емайл.");
      err = 1;
    end; 
    if(not err) 
      if(Status == ATTR_KSUR)
        typeCateg= "со стандартным"
      elif(Status == ATTR_KPUR)
        typeCateg= "с повышенным"
      elif(Status == ATTR_KNUR)
         typeCateg= "с начальным"
      end;
    
      var EmailMSG = c_email_proc_env();
      if(EmailMSG != NULL)
        EmailMSG.m_set_msg_head("Уведомление об отнесении к категории Клиентов " + typeCateg + " уровнем риска.");
        var EmailText = "\n\nДобрый день!"
                      + "\n\n"   
                      + "\n В соответствии регламентом оказания брокерских услуг АО 'Россельхозбанк' направляем Вам уведомление об отнесении к категории Клиентов " + typeCateg + " уровнем риска."
                      + "\n Данное письмо было отправлено автоматически."
                      + "\n ";
        EmailMSG.m_add_row_to_msg_text(EmailText);

        EmailMSG.m_add_email_to_list(EmailAddr);
 

        if(not err)
          EmailMSG.m_save_to_submit_synch();
          EmailMSG.m_submit_email_synch();
          return 1;
        end;
       
      end;
      return 0;
    end;

  end;

end;

class Logger()

    macro RepHeader(pFilename)
       var str;
       str =     "                             ПРОТОКОЛ ЗАГРУЗКИ УРОВНЕЙ РИСКА ПО КЛИЕНТАМ \n";
       str = str+"\n";
       str = str+"Дата загрузки  : "+date()+"\n";
       str = str+"Время загрузки : "+time()+"\n";
       str = str+"Обрабатываемый файл : "+pFilename+"\n";
       str = str+"┌─┬──────────────────┬──────────────────────────────┬──────────────────┬──────────────────┬───────────────┬─────────────────────────────────────────────────────────────────────────────────────────────────────┐\n";
       str = str+"│ │  ФИО             │Номер соглашения              │Код Клиента       │Статус КПУР       │Дата установки │Комментарий                                                                                          │\n";
       str = str+"├─┼──────────────────┼──────────────────────────────┼──────────────────┼──────────────────┼───────────────┼─────────────────────────────────────────────────────────────────────────────────────────────────────┤";
       //toRep1(str);                                                                                                   
       println(str);                                                                                                   
    end;
                                                                                                                                                                     
    macro RepFooter(suc, er, tot)                                                       
       var str = "└─┴──────────────────┴──────────────────────────────┴──────────────────┴──────────────────┴───────────────┴─────────────────────────────────────────────────────────────────────────────────────────────────────┘\n";
       str = str+"Загружено успешно: "+suc+"\n";               
       str = str+"Ошибки обработки : "+er+"\n";
       str = str+"Всего            : "+tot+"\n";
      println(str);
    end;

  macro RepProcHeader()
       var str = "\n";;
       str = str + "                             СПИСОК ОБНОВЛЕННЫХ КЛИЕНТОВ  \n";
//     str = str+"\n";
       str = str+"┌────────┬──────────────────┬──────────────────────────────┬─────────────────────────────────────────────────────────────────────────────────────────────────────┐\n";
       str = str+"│№ строки│  ФИО             │Номер соглашения              │Комментарий                                                                                          │\n";
       str = str+"├────────┼──────────────────┼──────────────────────────────┼─────────────────────────────────────────────────────────────────────────────────────────────────────┤";
       println(str);                                                                                                   
    end;

   macro RepProcFooter()                                                       
       var str = "└────────┴──────────────────┴──────────────────────────────┴─────────────────────────────────────────────────────────────────────────────────────────────────────┘\n";
      println(str);
    end;
    macro RepErrHeader()
       var str = "\n";;
       str = str + "                             СПИСОК ЗАФИКСИРОВАННЫХ ОШИБОК  \n";
//     str = str+"\n";
       str = str+"┌────────┬──────────────────┬──────────────────────────────┬─────────────────────────────────────────────────────────────────────────────────────────────────────┐\n";
       str = str+"│№ строки│  ФИО             │Номер соглашения              │Комментарий                                                                                          │\n";
       str = str+"├────────┼──────────────────┼──────────────────────────────┼─────────────────────────────────────────────────────────────────────────────────────────────────────┤";
       println(str);                                                                                                   
    end;

   macro RepErrFooter()                                                       
       var str = "└────────┴──────────────────┴──────────────────────────────┴─────────────────────────────────────────────────────────────────────────────────────────────────────┘\n";
      println(str);
    end;



    macro RepStr(_err,_code, _ca, _ss,_status,_date, _comm)
        var status;

        if  (_status==ATTR_KSUR)
          _status="КСУР";
        elif(_status==ATTR_KPUR)
          _status="КПУР";
        elif(_status==ATTR_KOUR)
          _status="КОУР";
        elif(_status==ATTR_KNUR)
          _status="КНУР";
        end;    

        if(_err == set_char)
          if(_status == 0)
            _status="Ошибка значения";   
          else
            _status = "";
          end;    
//          _date   = ""; 
        end;

        var str  = "│"+string(_err   :1)   +"│"+
                  string(_code    :18:l)+"│"+
                  string(_ca    :30:l)+"│"+
                  string(_ss      :18:l)+"│"+
                  string(_status  :18:l)+"│"+
                  string(_date    :15:l)+"│"+
                  string(_comm   :101:l) +"│"; 
       return println(str);
    end;

    macro RepErrStr(pNumRec, pPartyName, pContractCode, pErrMsg)
      var str:string ;

        str = "│"+string(pnumrec       :8)   +"│"+
                  string(pPartyName    :18:l)+"│"+
                  string(pContractCode :30:l)+"│"+
                  string(pErrMsg       :101:l)+"│";                  
       return println(str);

    end;    

   macro RepProcStr(pNumRec, pPartyName, pContractCode, pErrMsg)
      var str:string ;

        str = "│"+string(pnumrec       :8)   +"│"+
                  string(pPartyName    :18:l)+"│"+
                  string(pContractCode :30:l)+"│"+
                  string(pErrMsg       :101:l)+"│";                  
       return println(str);

    end;    


end;

private macro GetDateFromIntervalDay ( interval ) : date
    var startDate = date("01.01.1900");
    return DateShift(startDate, int(interval) - 2);
end;

macro SaveFileToAppServ(FullName:string)
    var FileName = "";
    var DirName = "";
    var ExtName = "";
    var Path_AppServ = "";

    if(not isStandAlone()) // когда мы на терминале, копируем файл на СП
      splitfile(FullName, FileName, ExtName);
      FileName = FileName + ExtName;
      DirName = substr(FullName, 1, index(FullName, FileName) - 1);
      Path_AppServ = "..\\txtfile\\" + FileName;

      if (not copyfile("$" + DirName + FileName, Path_AppServ) )
        msgbox("Ошибка при передаче файла на сервер приложений");
        return false;
      end;

      return Path_AppServ;
    else
      return FullName;
    end;
end;

//Загрузка данных из Excel
macro OpLoadRisk(autostart : bool)

  var DealID_Excel:string;
  var DealCode:string = "";
  var FullNameFile = "";
  var FileName = "";
  var TermPath = GetCurDir(true) + "\\txtfile";
  var Userpath = "";
  var DirName = "";
  var ExtName = "";
  var frow = 1;
  var row = 1;
  var excel, sql, query_insert, query_check, query_update;
  var valuedate:date =  Date(0, 0, 0);
  var ss = $0;
  var str;
  var DealID,FrVal = 0;
  var day = date();
  var ErrStr = "",ErrStat = false,IsOption = false;
  var all = 0,suc = 0, err = 0, numProc = 0;
  var Proc = False;
  var log = Logger();
  var StRec;
  var dt = StrSubSt(string(Date()),".","");
  var tm = StrSubSt(string(time()),":","");
  var InfoLogFileName = GetTxtFileName( "load_kpur_"+dt+"_"+StrSubst(tm," ","0"));
  var tdir;
  var flMask = "*.xls*";
  var ErrArray = TArray;
  var ProcArray = TArray;
  var DateBegin: Date;

  if(autostart)
    InfoLogFileName = string(InfoLogFileName,".auto");
  end;

  /*Выбираем файл для обработки*/
  if(not autostart)
    if(not SelectFile(FullNameFile, /*OPEN_DIR*/reg_path, OPEN_DIALOG_HEADER, 0, true))
      MsgBox("Отказ от загрузки");
      return 0;
    end;
    //Отделяем путь файла от имени
    DirName = splitfile(FullNameFile, FileName, ExtName);
    FileName = FileName + ExtName;

  else
    tdir = TDirList (reg_path + flMask,"f");
    tdir.Sort (0);
    DirName = reg_path;
    FileName = tdir.name(0);
  end;

  //копируем файл на СП, так как POI работает с файлом там
  FullNameFile = SaveFileToAppServ(FullNameFile);

  /*
  if (not copyfile(DirName + FileName, "$" + TermPath + "\\" + FileName) )
    MsgBox("Ошибка при передаче файла на терминал");
  end;

  BegAction(1, "Обработка файла \"" + TermPath + "\\" + FileName + "\". Ждите...");
  */

  InitProgress(-1);

  excel = CExcelPoi();
  if (not excel.Open(FullNameFile) )
    MsgBox("Ошибка открытия файла \"" + FullNameFile + "\"");
    EndAction(1);
    return 0;
  end;

  excel.SheetChange(0);

  SetOutput( InfoLogFileName, true ); //Пишем лог в файл. Начало
  log.RepHeader(FileName);
  /*Ищем первую строку*/
  while(frow <= 10)
    if((valtype(excel.CellValueByRowAndCol(frow, COL_FIO)) != V_UNDEF) and  ((strupr(excel.CellValueByRowAndCol(frow, COL_FIO)) == "ФИО")))
      break;
    end;
    frow = frow + 1;
  end;

  row = frow;
  //Бежим по файлу
  while (valtype(excel.CellValueByRowAndCol(row, COL_FIO)) != V_UNDEF)

       if (StrUpr(excel.CellValueByRowAndCol(row, COL_FIO)) == "ФИО")
         row = row + 1;
         continue; 
       end;

       all = all + 1;

       StRec = StatusRec();
       StRec.FIO               = excel.CellValueByRowAndCol(row, COL_FIO);
       StRec.SFNum             = excel.CellValueByRowAndCol(row, COL_SFNum);
       if (valtype(excel.CellValueByRowAndCol(row, COL_DateSFBegin))==v_double)
          StRec.DateSFBegin      = GetDateFromIntervalDay(excel.CellValueByRowAndCol(row, COL_DateSFBegin));
       elif (valtype(excel.CellValueByRowAndCol(row, COL_DateSFBegin))==v_date)
          StRec.DateSFBegin       = excel.CellValueByRowAndCol(row, COL_DateSFBegin);
       end;
       StRec.ClientCodeF       = /*int*/(excel.CellValueByRowAndCol(row, COL_ClientCodeF));
       errStat = StRec.SetStatus(excel.CellValueByRowAndCol(row, COL_Status), @ErrStr);
       if (valtype(excel.CellValueByRowAndCol(row, COL_DateBeginStatus))==v_double)
         StRec.DateBeginStatus = GetDateFromIntervalDay(excel.CellValueByRowAndCol(row, COL_DateBeginStatus));
       elif (valtype(excel.CellValueByRowAndCol(row, COL_DateBeginStatus))==v_date)
         StRec.DateBeginStatus = excel.CellValueByRowAndCol(row, COL_DateBeginStatus);
       else
         StRec.DateBeginStatus = null;
       end;
       if (valtype(excel.CellValueByRowAndCol(row, COL_DateReclassStatus))==v_double)
         StRec.DateReclassStatus = GetDateFromIntervalDay(excel.CellValueByRowAndCol(row, COL_DateReclassStatus));
       elif (valtype(excel.CellValueByRowAndCol(row, COL_DateReclassStatus))==v_date)
         StRec.DateReclassStatus = excel.CellValueByRowAndCol(row, COL_DateReclassStatus);
       end;
      StRec.Notes             = excel.CellValueByRowAndCol(row, COL_Notes);

      DateBegin = iif(StRec.Status == ATTR_KNUR, StRec.DateReclassStatus,StRec.DateBeginStatus);

      if( (DateBegin == null) or (valtype(DateBegin) == v_undef) )
         ErrStat = 1;
         ErrStr = string(ErrStr,"Ошибка определения даты.");
      end;
      
      if(not StRec.existSfContrByNumber() and not ErrStat)
         ErrStat = 1;
         ErrStr = string(ErrStr,"В СОФР не найден договор с номером " + StRec.SFNum);
      end;

      if(not ErrStat)
       if(StRec.FindClient())
          ErrStat = StRec.setDlCateg103(StRec.DlContrID, StRec.Status, DateBegin, @ErrStr, @Proc);
          if(not ErrStat)/*SetCateg()*/
            if(StRec.status!="0")
              If(StRec.SendMsg())
              else
                ErrStr = string(ErrStr,ERR_SEND_MSG); 
                ErrStat = true;
              end;
            end;
          else
          ErrStr = string(ErrStr,ERR_SET_STATUS);
          ErrStat = True;
          end;
       else   
         ErrStr = string(ErrStr,ERR_FIND_PT);
         ErrStat = True;
       end;
      end;

      if(not ErrStat)
        if(Print_WellDown)
          log.RepStr("",StRec.FIO, StRec.SFNum, StRec.ClientCodeF, StRec.Status, string(DateBegin:f), ErrStr); 
          if(Proc)
            ProcArray(ProcArray.Size) = ProcErr(row, StRec.FIO, StRec.SFNum, ErrStr);
            numProc = numProc + 1;
          end;  
        end;
        suc = suc + 1;

      else
        log.RepStr("X",StRec.FIO, StRec.SFNum, StRec.ClientCodeF, StRec.Status, iif(valtype(DateBegin) == v_undef,"Ошибка значения",string(DateBegin:f)), ErrStr);
        err = err + 1;
        ErrArray(ErrArray.Size) = ProcErr(row, StRec.FIO, StRec.SFNum, ErrStr);
      end;

       UseProgress(row);

       ErrStr = "";
       ErrStat = false;
       STrec = null;
       row = row + 1;
  
  
  end; //while

  log.RepFooter(/*suc*/numProc,err,all);

  if(numProc)
    log.RepProcHeader();
    var i:integer = 0;
    while(i < ProcArray.Size)
      log.RepErrStr(ProcArray(i).numrec, ProcArray(i).PartyName, ProcArray(i).ContractCode, ProcArray(i).ErrMsg);
      i = i +1;
    end;
    log.RepProcFooter();
  end;

  
  if(err)
    log.RepErrHeader();
    i = 0;
    while(i < ErrArray.Size)
      log.RepErrStr(ErrArray(i).numrec, ErrArray(i).PartyName, ErrArray(i).ContractCode, ErrArray(i).ErrMsg);
      i = i +1;
    end;
    log.RepErrFooter();
  end;

  SetOutput( NULL, true ); //Пишем лог в файл. Конец
  excel.Close();

  RemProgress();

  if (not copyfile(DirName + FileName, "$" + arch_path + "\\" + FileName) )
    log.repstr("Ошибка при копировании файла в архив");
  else 
    log.repstr("Исходный файл скопирован в архив: " + arch_path + "\\" + FileName);
  end;

  if (not RemoveFile (DirName + "\\" + FileName))
    log.repstr("Ошибка при удалении исходного файла");
  end;


  /*
  if (not RemoveFile ("$" + TermPath + "\\" + FileName))
    MsgBox("Ошибка удаления файла " + TermPath + "\\" + FileName);
  end;
  */

  if (not autostart)
    ViewFile( InfoLogFileName );
  end;

onerror()
  if (valtype(ExcelApplication) != V_UNDEF)
    ExcelApplication.ActiveWorkbook.Close;
  end;
  EndAction(1);
End;

//OpLoadRisk();