import "dv_compay_utils.mac";
import "dv_compay_payreq.mac";
import "dv_compay_trnpay.mac";

class (DVComPayScrollBase) DVComPayScroll()
  InitDVComPayScrollBase();
  var calcIdAndDate;

  macro ReDrawCurLine()
    var ds = execSQLselect(BuildSQL(SQL_ConvTypeInteger(rs.value("t_sfcontrid"))), TArray(), true);
    if (ds.MoveNext())
      rs.value("T_SUM") = ds.value("T_SUM");
      rs.value("T_REQSUM") = ds.value("T_REQSUM");
      rs.value("T_PAYED_PM") = ds.value("T_PAYED_PM");
      rs.value("T_PAYED_TR") = ds.value("T_PAYED_TR");
      rs.value("T_REST306") = ds.value("T_REST306");
      rs.value("T_REST47423") = ds.value("T_REST47423");
      rs.value("T_REST458") = ds.value("T_REST458");
      rs.value("T_REST306_OUTMARKET") = ds.value("T_REST306_OUTMARKET");
      rs.update();
      updatescroll(rs, 5);
    end;
  end;

  macro RecalcCurLine()
    var tmpCalcIdAndDate = DVComPayFindAndInsert(null, SQL_ConvTypeInteger(rs.value("t_clientid")), SQL_ConvTypeInteger(rs.value("t_calcId")));
    DVComPayUpdateCalcIdByNewCalc(
      tmpCalcIdAndDate.Key,
      SQL_ConvTypeInteger(rs.value("t_calcId")),
      SQL_ConvTypeInteger(rs.value("t_clientid"))
    );
    DVComPayDeleteCalcId(tmpCalcIdAndDate.Key);
    ReDrawCurLine();
  end;

  macro PayMenu()
    var arMenu = makeArray(
       "Сформировать и оплатить требование",
       "Проводки без платежа по ДБО",
       "Проводки оплаты без платежа по всем ДБО"
    );
    var choice = menu(arMenu,"Выберите действие","Выберите действие");
    var sfContrId = IIF(choice == 2, null, SQL_ConvTypeInteger(rs.value("t_sfcontrid")));
    if (choice == 0)
      if (SQL_ConvTypeSum(rs.value("t_payed_tr")) != $0)
        MsgBox("Для выполнения оплаты необходимо обработать по клиенту предыдущие оплаты не имеющие платежи");
      else
        DVComPayScrollPayList(calcIdAndDate.Key,sfContrId,SQL_ConvTypeSum(rs.value("t_rest306"))).ConstructAndShowScroll();
        RecalcCurLine();
      end;
    elif ((choice == 1) or (choice == 2))
      DVComPayScrollTrnList(calcIdAndDate.Key, sfContrId);
      RecalcCurLine();
    end;
  end;

  macro EvProc(rs, cmd, id, key)
    /*Обработчик*/
    if(cmd == DLG_KEY)
      /*Esc*/
      if (key == 27)
        return CM_CANCEL;
      end;
      /*ничего не делаем, если в текущей строке нет данных*/
      if (SQL_ConvTypeInteger(rs.value("t_clientid")) <= 0)
        return CM_IGNORE;
      end;
      /*Enter*/
      if(key == 13)
        PayMenu();
        return CM_IGNORE;
      end;
    end;
  end;

  macro BuildSQL(sfContrId)
    if (ValType(sfContrId)!=V_INTEGER)
      sfContrId = null;
    end;
    return
      "   SELECT t_ekk, "
     +"          t_number, "
     +"          t_calcId, "
     +"          t_clientid, "
     +"          t_clientname, "
     +"          t_sfcontrid, "
     +"          t_acc306, "
     +"          t_rest306, "
     +"          t_acc47423, "
     +"          t_rest47423, "
     +"          t_acc458, "
     +"          t_rest458, "
     +"          t_acc306_outmarket, "
     +"          t_rest306_outmarket, "
     +"          t_begdate, "
     +"          CAST (SUM (t_sum) AS NUMBER(32,12)) as t_sum, "
     //т.к. таблица U_COMPAY_DBT теперь содержит данные в разрезе сделок
     //то вычитаем сумму проводок без платежа мы уже после группировки
     +"          CAST ((SUM (t_reqSum) - t_payed_tr) AS NUMBER(32,12)) as t_reqSum, "
     +"          CAST (SUM (t_payed_pm) AS NUMBER(32,12))   as t_payed_pm, "
     +"          t_payed_tr "
     +"     FROM U_COMPAY_DBT "
     +"    WHERE T_CALCID = (SELECT MAX (t_calcid) FROM U_COMPAY_DBT) AND t_sysdate is not null"
     +IIF(sfContrId == null, "", " AND t_sfcontrid = " + string(sfContrId))
     +" GROUP BY t_ekk, "
     +"          t_number, "
     +"          t_calcId, "
     +"          t_clientid, "
     +"          t_clientname, "
     +"          t_sfcontrid, "
     +"          t_acc306, "
     +"          t_rest306, "
     +"          t_acc47423, "
     +"          t_rest47423, "
     +"          t_acc458, "
     +"          t_rest458, "
     +"          t_acc306_outmarket, "
     +"          t_rest306_outmarket, "
     +"          t_begdate, "
     +"          t_payed_tr "
     +" ORDER BY t_clientid ";
  end;

  macro BuildRS()
    rs = execSQLselect(BuildSQL(), TArray(), true, RSDVAL_CLIENT_IF_NEEDED, RSDVAL_STATIC);
    rs.UpdateCommand = RsdCommand("select 1 from dual");
    return rs;
  end;

  macro GetCFTWorkDate():date
    var cmd, DataSet;
    cmd = DL_RSDCommand("SELECT TRUNC(t_sysdate) WorkDate FROM duserdebttocft_dbt WHERE ROWNUM = 1 ");
    DataSet = cmd.Execute(); 

    if(DataSet.moveNext())
      return DataSet.WorkDate;
    end;
    return date(0,0,0);
  end;

  macro ConstructAndShowScroll()
    calcIdAndDate = DVComPayGetMaxCalcIdAndDate();
    var needRecalc = calcIdAndDate == null;
    if ((not needRecalc) and (calcIdAndDate.Value < date()))
      needRecalc = getTrue(true,"Данные на "+calcIdAndDate.Value +". Выполнить пересчет?");
    end;
    if (needRecalc)
      if(date() > GetCFTWorkDate())
        var begDate = DVCOMPAY_BEGDATE_FIND;
        if (getDate(begDate,"Введите начало периода поиска неоплаченных требований:"))
          calcIdAndDate = DVComPayFindAndInsert(begDate);
        end;
      else
        MsgBox("В текущем дне пересчет невозможен, так как уже запущен процесс списания задолженностей со счетов в ЦФТ");
      end;
    end;

    AddColumn(0, "t_ekk", "Код ЕКК", 7, 10);
    AddColumn(1, "t_number", "Номер договора", 13, 10);
    AddColumn(2, "t_clientid", "IDкл.", 5, 10);
    AddColumn(3, "t_clientname", "Клиент", 10, 30);
    AddColumn(4, "t_sum", "Начислено", 10, 10);
    AddColumn(5, "t_payed_pm", "Оплачено", 10, 10);
    AddColumn(6, "t_payed_tr", "Оплачено без платежа", 17, 10);
    AddColumn(7, "t_reqSum", "Требование", 10, 10);
    AddColumn(8, "t_acc306", "Счет 306", 16, 25);
    AddColumn(9, "t_rest306", "Ост. 306", 10, 10);
    AddColumn(10, "t_acc306_outmarket", "Счет 306 внеб.", 16, 25);
    AddColumn(11, "t_rest306_outmarket", "Ост. 306 внеб.", 12, 10);
    AddColumn(12, "t_acc458", "Счет 458", 16, 25);
    AddColumn(13, "t_rest458", "Ост. 458", 10, 10);
    AddColumn(14, "t_acc47423", "Счет 47423", 16, 25);
    AddColumn(15, "t_rest47423", "Ост. 47423", 10, 10);

    var res = true;
    while(res)
      res = RunScroll (BuildRS(), NumColumns, Columns, "DVCPSCRL", R2M(this,"EvProc"), string("Текущие требования по клиентам ",calcIdAndDate.Value), 
         "Enter - выбор действия, Esc - выход", true, -1, -1);
    end;
  end;

end;

DVComPayScroll().ConstructAndShowScroll();
exit(1);
