import spcomcalc, UtlDlgCls;
private var sqls_deal = String( 
   "SELECT Dl.t_Code || ' (' || Opk.t_Name || ') ' || Dl.t_Extcode  deal_vis \n"
  ,"      ,Dl.t_Id  deal_id \n"
  ,"      ,Dl.t_Code  deal_cd  \n"
  ,"      ,Opk.t_Name deal_tp   \n"
  ,"      ,Dl.t_Date deal_dt   \n"
  ,"      ,Sfc.t_Number cntr_nm   \n"
  ,"      ,Dli.t_Cost deal_sm   \n"
  ,"      ,Fi.t_Ccy deal_fi   \n"
  ,"      ,Oc.t_Code || ' - ' || Prt.t_Name clnt_nm   \n"
  ,"      ,Dl.t_Client Clnt_Id   \n"
  ,"      ,Dl.t_Clientcontr Cntr_Id   \n"
  ,"  FROM Ddvndeal_Dbt Dl   \n"
  ," INNER JOIN Doprkoper_Dbt Opk   \n"
  ,"    ON (Opk.t_Kind_Operation = Dl.t_Kind)   \n"
  ," INNER JOIN Ddvnfi_Dbt Dli   \n"
  ,"    ON (Dli.t_Dealid = Dl.t_Id AND Dli.t_Type = 0)   \n"
  ," INNER JOIN Dfininstr_Dbt Fi   \n"
  ,"    ON (Fi.t_Fiid = Dli.t_Pricefiid)   \n"
  ," INNER JOIN Dsfcontr_Dbt Sfc   \n"
  ,"    ON (Sfc.t_Id = Dl.t_Clientcontr)   \n"
  ," INNER JOIN Dparty_Dbt Prt   \n"
  ,"    ON (Prt.t_Partyid = Sfc.t_Partyid)   \n"
  ," INNER JOIN Dobjcode_Dbt Oc   \n"
  ,"    ON (Oc.t_Objecttype = 3 AND Oc.t_Codekind = 1 AND Oc.t_Objectid = Sfc.t_Partyid AND Oc.t_State = 0)   \n"
  ," WHERE Sfc.t_Servkind = ?   \n"
  ,"   AND Dl.t_Date = ?   \n"
  ," ORDER BY Dl.t_Id   \n"
 );  
private var sqls_clnt = String( 
   "WITH Prm AS   \n"
  ," (SELECT ? Srv, ? Dat, ? Prt FROM Dual)   \n"
  ,"SELECT Oc.t_Code || ' - ' || Prt.t_Name, Prt.t_Partyid, Oc.t_Code V01_Код, Prt.t_Name V02_Наименование   \n"
  ,"  FROM Prm   \n"
  ," INNER JOIN Dclient_Dbt Cln   \n"
  ,"    ON (Cln.t_Servicekind = Prm.Srv)   \n"
  ," INNER JOIN Dparty_Dbt Prt   \n"
  ,"    ON (Prt.t_Partyid = Cln.t_Partyid)   \n"
  ," INNER JOIN Dobjcode_Dbt Oc   \n"
  ,"    ON (Oc.t_Objecttype = 3 AND Oc.t_Codekind = 1 AND Oc.t_Objectid = Prt.t_Partyid AND Oc.t_State = 0)   \n"
  ," WHERE (Prm.Prt = 0 OR Cln.t_Partyid = Prm.Prt)   \n"
  ,"   AND EXISTS (SELECT 1   \n"
  ,"          FROM Dsfcontr_Dbt Sfc   \n"
  ,"         WHERE Sfc.t_Partyid = Cln.t_Partyid   \n"
  ,"           AND Sfc.t_Servkind = Cln.t_Servicekind   \n"
  ,"           AND Sfc.t_Datebegin <= Prm.Dat   \n"
  ,"           AND (Sfc.t_Dateclose = To_Date('01010001', 'DDMMYYYY') OR Sfc.t_Dateclose > Prm.Dat))   \n"
  ," ORDER BY Oc.t_Code   \n"
 );        
private var sqls_cntr = String( 
   "WITH Prm AS   \n"
  ," (SELECT ? Srv, ? Dat, ? Prt, ? Cnt FROM Dual)   \n"
  ,"SELECT Sfc.t_Number Cntr_Nm   \n"
  ,"      ,Sfc.t_Id Cntr_Id   \n"
  ,"      ,Sfc.t_Dateconc Cntr_Dt   \n"
  ,"      ,(SELECT Sk.t_Name FROM Dservkind_Dbt Sk WHERE Sk.t_Servisekind = Prm.Srv) Serv_Nm   \n"
  ,"      ,Oc.t_Code || ' - ' || Prt.t_Name Clnt_Nm   \n"
  ,"      ,Prt.t_Partyid Clnt_Id   \n"
  ,"  FROM Prm   \n"
  ," INNER JOIN Dsfcontr_Dbt Sfc   \n"
  ,"    ON (Sfc.t_Servkind = Prm.Srv)   \n"
  ," INNER JOIN Dparty_Dbt Prt   \n"
  ,"    ON (Prt.t_Partyid = Sfc.t_Partyid)   \n"
  ," INNER JOIN Dobjcode_Dbt Oc   \n"
  ,"    ON (Oc.t_Objecttype = 3 AND Oc.t_Codekind = 1 AND Oc.t_Objectid = Sfc.t_Partyid AND Oc.t_State = 0)   \n"
  ," WHERE (Prm.Cnt = 0 OR Sfc.t_Id = Prm.Cnt)   \n"
  ,"   AND (Prm.Prt = 0 OR Sfc.t_Partyid = Prm.Prt)   \n"
  ,"   AND Sfc.t_Datebegin <= Prm.Dat   \n"
  ,"   AND (Sfc.t_Dateclose = To_Date('01010001', 'DDMMYYYY') OR Sfc.t_Dateclose > Prm.Dat)   \n"
  ," ORDER BY Oc.t_Code   \n"
 );     
private var sqls_coms = String( 
   "SELECT Sfc.t_Code  \n"
  ,"      ,Sfcc.t_Id   \n"
  ,"      ,Sfc.t_Code V01_Комиссия   \n"
  ,"      ,Sfc.t_Name  V02_Наименование   \n"
  ,"      ,Sfcc.t_Objectid Cntr_Id   \n"
  ,"  FROM Dsfconcom_Dbt Sfcc   \n"
  ," INNER JOIN Dsfcomiss_Dbt Sfc   \n"
  ,"    ON (Sfc.t_Feetype = Sfcc.t_Feetype AND Sfc.t_Number = Sfcc.t_Commnumber)   \n"
  ," WHERE Sfcc.t_Objecttype = 659   \n"
  ,"   AND Sfcc.t_Feetype = 1   \n"
  ,"   AND Sfcc.t_Status = 0   \n"
  ,"   AND Sfc.t_Servicekind = ?   \n"
  ,"   AND Sfcc.t_Datebegin <= ?   \n"
  ,"   AND Sfcc.t_Objectid = ?   \n"
  ,"   AND ( (Sfcc.t_Dateend > ?)   \n"
  ,"       OR (Sfcc.t_Dateend = to_date('01010001','ddmmyyyy')))   \n"
 ); 

class (TPnl_List_Box) List_Deal(panel, p_name, p_x_pos, p_y_pos, p_x_siz, p_y_siz, title)
   var cntr_id = 0, clnt_id = 0, clnt_nm = "", cntr_nm = "";

   macro set_sel_value(rset)
   this.cntr_id = int(rset.Value("Cntr_Id"));
   this.cntr_nm = rset.Value("Cntr_Nm");
   this.clnt_id = int(rset.Value("Clnt_Id"));
   this.clnt_nm = rset.Value("Clnt_Nm");
   set_sel_value(rset);
   end;
   
   macro on_remfocus(event)
   if (strlen(trim(this.Value)) == 0)
      this.cntr_id = 0;
      this.cntr_nm = "";
      this.clnt_id = 0;
      this.clnt_nm = "";
   end;
   on_remfocus(event);
   end;
   
   macro ScrlRun(p_sql)
   var col_i:Tarray, x, y, x_t, y_t, ind = 0, ia = 0, name_scr = "";
   
      macro add_col(f_name, f_titl, f_size)  
      var ia = col_i.Size/6;
      col_i[0 + ia * 6] = f_Name;
      col_i[1 + ia * 6] = f_titl;
      col_i[2 + ia * 6] = f_size;
      col_i[3 + ia * 6] = true;
      col_i[4 + ia * 6] = 0;
      col_i[5 + ia * 6] = 0;
      end;
      
   panel_.getPosition(x, y);
   this.getposition(x_t, y_t);
   if (not get_dataset())
      msgbox("Ошибка реализации источника данных");
      return false;
   end;
   cbx_rsds.Open();
   col_i = Tarray();
   add_col("deal_cd", "Код", 20);
   add_col("deal_tp", "Тип", 20);
   add_col("deal_dt", "Дата", 10);
   add_col("deal_sm", "Сумма", 12);
   add_col("deal_fi", "Вал", 3);
   add_col("cntr_nm", "Договор", 20);
   add_col("clnt_nm", "Клиент", 50);
   name_scr = name + "_SCROLL";
   return RunScroll (cbx_rsds, col_i.Size/6, col_i, name_scr, R2M(this, "ScrlEvProc"), title_, "Enter-Выбор Esc-Выход", true, x + x_t, y + y_t + 1, null, 15);
   onerror(err);
      msgbox(CommonSessUtil.except_msg(err, cbx_rsdc));
      cbx_rsds = null;
      cbx_rsdc = null;
   end;

   InitTPnl_List_Box(panel, p_name, p_x_pos, p_y_pos, p_x_siz, p_y_siz, title, sqls_deal);

end;

class (TPnl_List_Box) List_Cntr(panel, p_name, p_x_pos, p_y_pos, p_x_siz, p_y_siz, title)
   var clnt_id = 0, clnt_nm = "";
   
   macro set_sel_value(rset)
   this.clnt_id = int(rset.Value("Clnt_Id"));
   this.clnt_nm = rset.Value("Clnt_Nm");
   set_sel_value(rset)
   end;

   macro on_remfocus(event)
   if (strlen(trim(this.Value)) == 0)
      this.clnt_id = 0;
      this.clnt_nm = "";
   end;
   on_remfocus(event);
   end;
   
   macro ScrlRun(p_sql)
   var col_i:Tarray, x, y, x_t, y_t, ind = 0, ia = 0, name_scr = "";
   
      macro add_col(f_name, f_titl, f_size)  
      var ia = col_i.Size/6;
      col_i[0 + ia * 6] = f_Name;
      col_i[1 + ia * 6] = f_titl;
      col_i[2 + ia * 6] = f_size;
      col_i[3 + ia * 6] = true;
      col_i[4 + ia * 6] = 0;
      col_i[5 + ia * 6] = 0;
      end;
      
   panel_.getPosition(x, y);
   this.getposition(x_t, y_t);
   if (not get_dataset())
      msgbox("Ошибка реализации источника данных");
      return false;
   end;
   cbx_rsds.Open();
   col_i = Tarray();
   add_col("cntr_nm", "Договор", 20);
   add_col("cntr_dt", "Дата закл.", 10);
   add_col("clnt_nm", "Клиент", 50);
   add_col("serv_nm", "Вид обслуживания", 55);
   name_scr = name + "_SCROLL";
   return RunScroll (cbx_rsds, col_i.Size/6, col_i, name_scr, R2M(this, "ScrlEvProc"), title_, "Enter-Выбор Esc-Выход", true, x + x_t, y + y_t + 1, null, 15);
   onerror(err);
      msgbox(CommonSessUtil.except_msg(err, cbx_rsdc));
      cbx_rsds = null;
      cbx_rsdc = null;
   end;
   
   InitTPnl_List_Box(panel, p_name, p_x_pos, p_y_pos, p_x_siz, p_y_siz, title, sqls_cntr);
  
end;

class (TPnl_List_Box) List_Coms(panel, p_name, p_x_pos, p_y_pos, p_x_siz, p_y_siz, title)
   var cntr_id = 0;	
   
   macro set_sel_value(rset)
   this.cntr_id = int(rset.Value("Cntr_Id"));
   set_sel_value(rset)
   end;

   macro on_remfocus(event)
   if (strlen(trim(this.Value)) == 0)
      this.cntr_id = 0;
   end;                    
   on_remfocus(event);
   end;
  
   InitTPnl_List_Box(panel, p_name, p_x_pos, p_y_pos, p_x_siz, p_y_siz, title, sqls_coms);
  
end;

class (TRsbPanel) TPnl_FltComClc();
   var x_lft = 10, y_hgh = 10, x_siz = 76, y_siz = 20, frm, y_beg = 1;
   var F_Date, F_Deal, F_Clnt, F_Cntr, F_Coms, F_Logf; 
   //var deal_id = 0, cntr_id = 0, clnt_id = 0, coms_id = 0;
   var method = METHOD_CALC, log_work = true, ServKind = 21;
   private var lbl;
   var btn_l;

   // закрытие окна
   macro on_panel_close(event)
      var ind = 0;
      var web_rep = null;
      var all_str = 0;
      var all_err = 0;
      var all_mes = TStrCollector;

      if (event.StatusCode == 1)   
         SetGlobalParameter("BO_ACT_INITIATOR", "ФИССиКО");
         RunComCalc(F_Date.Value, F_Deal.id_Val, F_Clnt.id_Val, F_Cntr.id_Val, F_Coms.id_Val, this.method, this.log_work, @web_rep, @all_mes, @all_str, @all_err);
      end;                
   end;   
   // открытие окна
   macro on_panel_open(event)
   end;
   // Реакция на клавиши
   macro on_key_pressed(event)
   var e_s, s_n, k_k, ind = 0;
   k_k = event.keycode;
   e_s = event.Source;             
   if (IsEqClass ("TRsbPanel", e_s))
      return;
   else   
      s_n = e_s.Name; 
   end; 
   if (k_k == 316)  
      close(1);
   end;
   if (k_k == 323) 
      close(1);
   end;
   end;
   // Реакция на кнопки формы
   macro on_click_btn1(event)
   close(1);
   end;
   macro on_click_btn2(event)
   close(0);
   end;   
   // выбор метода            
   macro onRbgChecked(event)
   var src = event.Source;  
   if ((event.Id == Rsb_Ev_Control_Checked) and (event.Source.Checked))
      method = int(event.Source.Name);
   end;
   end;
   // отметка протоколирования
   macro onLogChecked(event)
   var src = event.Source;  
   if (event.Id == Rsb_Ev_Control_Checked)
      log_work = event.Source.Checked;
   end;
   end;
   // подготовка сроллингов
   macro on_list_getdset(event)
   var p_ctrl = event.Source;      
   if (p_ctrl.Name == "LIST_DEAL")
      p_ctrl = F_Deal;
      p_ctrl.param_clr;  
      p_ctrl.param_add(ServKind);
      p_ctrl.param_add(F_Date.Value);
   elif (p_ctrl.Name == "LIST_CLNT")
      p_ctrl = F_Clnt;
      p_ctrl.param_clr;  
      p_ctrl.param_add(ServKind);
      p_ctrl.param_add(F_Date.Value);
      p_ctrl.param_add(F_Deal.clnt_id);
   elif (p_ctrl.Name == "LIST_CNTR")
      p_ctrl = F_Cntr;
      p_ctrl.param_clr;  
      p_ctrl.param_add(ServKind);
      p_ctrl.param_add(F_Date.Value);
      if (F_Deal.id_val > 0)
         p_ctrl.param_add(F_Deal.clnt_id);
      elif (F_Clnt.id_val > 0)
         p_ctrl.param_add(F_Clnt.id_val);
      else   
         p_ctrl.param_add(0);
      end;      
      if (F_Deal.id_val > 0)
         p_ctrl.param_add(F_Deal.cntr_id);
      else
         p_ctrl.param_add(0);
      end;  
   elif (p_ctrl.Name == "LIST_COMS")
      p_ctrl = F_Coms;
      p_ctrl.param_clr;  
      p_ctrl.param_add(ServKind);
      p_ctrl.param_add(F_Date.Value);
      p_ctrl.param_add(F_Cntr.id_val);
      p_ctrl.param_add(F_Date.Value);
   end;
   end;             
   // послед вызов после выбора в списках
   macro on_change_value(event)
   var p_ctrl = event.Source.Name;      
   if (p_ctrl == "LIST_DEAL")
      p_ctrl = F_Deal;
      if ((F_Clnt.id_val != p_ctrl.clnt_id) and (p_ctrl.clnt_id != 0))
         F_Clnt.set_value(p_ctrl.clnt_nm, p_ctrl.clnt_id);
      end;   
      if ((F_Cntr.id_val != p_ctrl.cntr_id) and (p_ctrl.cntr_id != 0))
         F_Cntr.clnt_id = p_ctrl.clnt_id;
         F_Cntr.set_value(p_ctrl.cntr_nm, p_ctrl.cntr_id);
         F_Coms.cntr_id = 0;
         F_Coms.set_value("", 0);
      end;
   elif (p_ctrl == "LIST_CLNT")
      p_ctrl = F_Clnt;
      if (F_Cntr.clnt_id != p_ctrl.id_val)
         F_Cntr.clnt_id = 0;
         F_Cntr.clnt_nm = "";
         F_Cntr.set_value("", 0);
         F_Coms.cntr_id = 0;
         F_Coms.set_value("", 0);
      end;
   elif (p_ctrl == "LIST_CNTR")
      p_ctrl = F_Cntr;
      if ((F_Clnt.id_val != p_ctrl.clnt_id) and (p_ctrl.clnt_id != 0))
         F_Clnt.set_value(p_ctrl.clnt_nm, p_ctrl.clnt_id);
      end;   
      if (F_Coms.cntr_id != p_ctrl.id_val)
         F_Coms.cntr_id = 0;
         F_Coms.set_value("", 0);
      end;
   end;
   end;
   // выход из поля
   macro on_remfocus(event)
   var src = event.Source;  
   end;
   // построение панели
   macro build_panel()        
      var rbg;
      setPosition(x_lft, y_hgh);
      setStatus("F3-Список  F2/F9-Выполнить Esc-Выход");
      setCaption("Параметры расчета комиссии");         
      F_Date = TPnl_Edit_Box(this, v_date, "DAT1", 2, 2, 10, 1, "Дата        ");
      F_Date.set_edit(true);
      F_Date.set_value({curdate});                                                  
      F_Deal = List_Deal(this, "LIST_DEAL", 2, F_Date.get_y_bot,  60, 1, "Сделка      ");
      F_Deal.set_value("", 0);
      F_Clnt = TPnl_List_Box(this, "LIST_CLNT", 2, F_Deal.get_y_bot,  60, 1, "Плательщик  ", sqls_clnt);
      F_Clnt.set_value("", 0);
      F_Cntr = List_Cntr(this, "LIST_CNTR", 2, F_Clnt.get_y_bot,  60, 1, "Договор     ");
      F_Cntr.set_value("", 0);
      F_Coms = List_Coms(this, "LIST_COMS", 2, F_Cntr.get_y_bot,  60, 1, "Комиссия    ");
      F_Coms.set_value("", 0);

      addLabel(TRsbLabel(2, F_Coms.get_y_bot + 1, "Режим работы"));
      rbg = TPanel_RadioButtonGroup(this, "OnRbgChecked", 25, F_Coms.get_y_bot + 1);
      rbg.add(METHOD_CALC, "расчет", true);
      rbg.add(METHOD_RECALC, "пересчет");
      rbg.add(METHOD_DEL, "удаление");
      
      F_Logf = TPanel_CheckBox(this, "LOG_WORK", "OnLogChecked", 2, rbg.get_y_bot, "Формировать протокол", true, "R");

      addLabel(TRsbLabel(2, F_Logf.get_y_bot + 1, "Операционист"));
      addLabel(TRsbLabel(14, F_Logf.get_y_bot + 1, {oper}));
      addLabel(TRsbLabel(20, F_Logf.get_y_bot + 1, GetOperName({oper})));
      setSize(x_siz, F_Logf.get_y_bot + 5);
      
      btn_l = TPanel_PushButton_L(this, 2);
      btn_l.Add_Button("F2-Выполнить", "on_click_btn1");
      btn_l.Add_Button("Esc-Выход"   , "on_click_btn2");
      addEventHandler(RSB_EV_KEY_PRESSED,    R2M(this, "on_key_pressed"));
      addEventHandler(RSB_EV_WINDOW_CLOSING, R2M(this, "on_panel_close"));
      addEventHandler(RSB_EV_ENTER_WINDOW,   R2M(this, "on_panel_open"));
      addEventHandler(RSB_EV_REMOVE_FOCUS,   R2M(this, "on_remfocus"));
      addEventHandler(RSB_EV_CHANGE_VALUE,   R2M(this, "on_change_value"));
      addEventHandler(RSB_EV_CHANGE_VALUE,   R2M(this, "on_change_value"));
      addEventHandler(RSB_EV_LIST_GETDSET,   R2M(this, "on_list_getdset"));
   end;
                                             
   macro show()  
   return run;
   onerror(err)
      if (err.Code != 0)
         msgbox(CommonSessUtil.except_msg(err, null));
      end;
      return 0;     
   end;
   
InitTRsbPanel();
build_panel();
end;

var pnl; 
pnl = TPnl_FltComClc;
if (pnl.Show() == 0)
   exit(1);
end;
exit(0);
end;