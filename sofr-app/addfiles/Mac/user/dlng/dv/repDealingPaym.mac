/*отчет по сделкам РСХб Дилинг 2*/
IMPORT Global, RsbFormsInter, "cb_sql.mac", PTInter, "dlquery.mac", rsd;
IMPORT DlReport_h;

//Вид финансового инструмента
  MACRO FIKind_Name( FiKindID:INTEGER )
     FiKindID = int(FiKindID) ;
     if( FiKindID == 1) 
         return "Валюта";
      elif(FIKindID == 2)
        return  "Ценная бумага";
      elif(FIKindID == 3)
        return  "Индекс";
      elif(FIKindID == 4)
        return  "Производный инструмент";
      elif(FIKindID == 6)
        return  "Драгоценный металл";
      elif(FIKindID == 7)
        return  "Артикул";
      elif(FIKindID == 8)
        return  "Спец.фин.инструмент";
      end;
      return "";
  END;


CLASS (tRsbPanel) pnl_DealingPaym ()
  private const FT_STRING = 7;
  const K_Enter = 13;
  const K_F3    = 317;
  const K_F2    = 316;
  
  InitTRSBPanel();
  Caption = "Отчет по сделкам, заключенным через РСХБ-Дилинг";
  Status  = "~ESC~Выход  ~F2~Выполнить  ~F3~Выбор";
  setSize(40,6);
  SetPosition(30,10);

  var delta = 5; 
  var curstr = 0,
      lbl_beg_Date,
      fld_beg_Date; 

  curstr = curstr + 2;  
    lbl_beg_Date = TRSBLabel(delta + 2, curstr,"Начало периода:");
      addlabel (lbl_beg_Date);

    fld_beg_Date = TRSBEditField (V_Date);
    fld_beg_Date.setPosition(delta + 16,curstr);
    fld_beg_Date.setSize(8,1);
    fld_beg_Date.name = "fld_beg_Date";
    fld_beg_Date.value = {CurDate};
      addControl(fld_beg_Date);

  var lbl_end_Date,
      fld_end_Date; 

  curstr = curstr + 1;  
    lbl_end_Date = TRSBLabel(delta + 2, curstr,"Окончание периода:");
      addlabel (lbl_end_Date);

    fld_end_Date = TRSBEditField (V_Date);
    fld_end_Date.setPosition(delta + 16,curstr);
    fld_end_Date.setSize(8,1);
    fld_end_Date.name = "fld_end_Date";
    fld_end_Date.value = {CurDate};
      addControl(fld_end_Date);

    addEventHandler (RSB_EV_Key_Pressed, R2M(this,"on_KeyPressed"));

    macro PrintDealingPaym(beg_date, end_date)
      var rn    = 1;
      var rep, sheet = 1;
      var sqls, cmd, rs; 

//Ширина ячеек
      var col1_len  = 15;
      var col2_len  = 15;
      var col3_len  = 15;
      var col4_len  = 15;
      var col5_len  = 15;
      var col6_len  = 15;
      var col7_len  = 15;
      var col8_len  = 15;
      var col9_len  = 15;
      var col10_len = 15;
      var col11_len = 15;


      macro print_header()
        var ColorD = 14069116;
        var head_format = "ex_FS(b):ex_B(tblr):ex_BC(" + colorD + ")";
        Rep.AddPrintCell("Код",              col1_len,  null, "c:" + head_format);
        Rep.AddPrintCell("Дата",             col2_len,  null, "c:" + head_format);
        Rep.AddPrintCell("Направление",      col3_len,  null, "c:" + head_format);
        Rep.AddPrintCell("Контрагент",       col4_len,  null, "c:" + head_format);
        Rep.AddPrintCell("Вид сделки",       col5_len,  null, "c:" + head_format); 
        Rep.AddPrintCell("Вид баз. актива",  col6_len,  null, "c:" + head_format);
        Rep.AddPrintCell("Кол-во",           col7_len,  null, "c:" + head_format);
        Rep.AddPrintCell("Цена",             col8_len,  null, "c:" + head_format);
        Rep.AddPrintCell("Валюта",           col9_len,  null, "c:" + head_format);
        Rep.AddPrintCell("Дата исполн.",     col10_len, null, "c:" + head_format);
        Rep.AddPrintCell("Статус обработки", col11_len, null, "c:" + head_format);
        Rep.AddStr();rn = rn + 1;
      end;

      Rep = CMakeReport("┌┬┬┬┬┬┬┬┬┬┬┬┐");
      Rep.SetDefaultFontName("Times New Roman");
      Rep.SetDefaultFontSize(10);
      Rep.SetFlagShowGrid(true);

      Rep.SetExecute("iBook.Sheets("+sheet+").Rows("+rn+").WrapText = false;");
      Rep.AddPrintCell(string("РСХБ-Дилинг. Отчет по сделкам за период c ",beg_date," по ",end_date), col1_len, null, "c:ex_FS(b):ex_B(tblr)");
      Rep.SetExecute("iBook.Sheets("+sheet+").Range(\"A"+(rn)+":K"+(rn)+"\").merge;");
      Rep.AddStr();rn = rn + 1;

      print_header();
/*********************************************************/
      sqls = String("SELECT t.*,                                                                              \n"
                    ,"       objatrcor.t_object,                                                              \n"
                    ,"       objatrcor.t_objecttype,                                                          \n"
                    ,"       objatrcor.t_attrid                                                               \n"
                    ,"  FROM (SELECT dvn.t_code,                                                              \n"
                    ,"               TO_CHAR (dvn.t_date, 'dd.mm.yyyy') AS t_dealdate,                        \n"
                    ,"               CASE dvn.t_type                                                          \n"
                    ,"                  WHEN 1 THEN 'Покупка'                                                 \n"
                    ,"                  WHEN 2 THEN 'Продажа'                                                 \n"
                    ,"                  WHEN 5 THEN 'Покупка-Продажа'                                         \n"  
                    ,"                  WHEN 6 THEN 'Продажа-Покупка'                                         \n"
                    ,"                  ELSE CHR (1)                                                          \n"
                    ,"               END                                                                      \n"
                    ,"                  AS str_type,                                                          \n"
                    ,"               party.t_shortname,                                                       \n"
                    ,"               oprkoper.t_Name T_NAME_operation,                                        \n"
                    ,"               --  basefi.t_fi_kind,                                                    \n"
                    ,"               pmpaym.t_baseamount,                                                     \n"
                    ,"               dvnfi.t_price,                                                           \n"
                    ,"               --  pricefi.t_ccy,                                                       \n"
                    ,"               TO_CHAR (pmpaym.t_valuedate, 'dd.mm.yyyy') AS t_paymDate,                \n"
                    ,"               pmstatus.t_shortname t_paymstatus,                                       \n"
                    ,"               CASE WHEN DVN.T_DOCKIND = 199 THEN 145 ELSE 148 END                      \n"
                    ,"                  AS t_objtype,  dvn.t_id , /*pricefi*/basefi.t_ccy, basefi.t_fi_kind   \n"
                    ,"          FROM ddvndeal_dbt dvn,                                                        \n"
                    ,"               dpmpaym_dbt pmpaym,                                                      \n"
                    ,"               dparty_dbt party,                                                        \n"
                    ,"               doprkoper_dbt oprkoper,                                                  \n"
                    ,"               ddvnfi_dbt dvnfi,                                                        \n"
                    ,"               dpmstatus_dbt pmstatus,                                                  \n"
                    ,"               dfininstr_dbt pricefi,                                                   \n"
                    ,"               dfininstr_dbt basefi                                                     \n"
                    ,"         WHERE     1 = 1                                                                \n"
                    ,"               AND pmpaym.t_dockind = dvn.t_dockind                                     \n"
                    ,"               AND pmpaym.t_documentid = dvn.t_id                                       \n"
                    ,"               AND party.t_partyid = dvn.t_contractor                                   \n"
                    ,"               AND oprkoper.t_kind_operation = dvn.t_kind                               \n"
                    ,"               AND oprkoper.t_dockind = dvn.t_dockind                                   \n"
                    ,"               AND pmpaym.t_receiverbankid = 1                                          \n"
                    ,"               AND dvnfi.t_dealid = dvn.t_id                                            \n"
                    ,"               AND dvnfi.t_type = 0                                                     \n"
                    ,"               AND pmstatus.t_paymstatus = pmpaym.t_paymstatus                          \n"
                    ,"               AND pmstatus.t_type = 0                                                  \n"
                    ,"               AND pricefi.t_fiid = dvnfi.t_pricefiid                                   \n"
                    ,"               AND basefi.t_fiid = pmpaym.t_basefiid                                    \n"
                    ,"               AND dvn.t_date BETWEEN :begdate AND :enddate                             \n"
                    ,"               /*AND dvn.t_state != 0*/                                                 \n"
                    ,"                                        ) t,                                            \n"
                    ,"       dobjatcor_dbt objatrcor                                                          \n"
                    ," WHERE     1 = 1                                                                        \n"
                    ,"       AND objatrcor.t_object = LPAD (t.t_id, 34, '0')                                  \n"
                    ,"       AND objatrcor.t_objecttype = t.t_objtype                                         \n"
                    ,"       AND objatrcor.t_groupid =                                                        \n"
                    ,"              CASE WHEN t.t_objtype = 145 THEN 3 ELSE 5 END                             \n"
                    ,"       AND objatrcor.t_attrid = 5");                                                    

      cmd = RSDCommand(sqls);
      cmd.AddParam("begdate", rsdbp_in, beg_date); 
      cmd.AddParam("enddate", rsdbp_in, end_date);
      rs = RSDRecordset(cmd);

      BegAction(1, "Подготовка реестра. Ждите...");
      while(rs.MoveNext())
         Rep.AddPrintCell(rs.value("T_CODE"),                 col1_len,  null, "l:ex_FS():ex_B(tblr)");  
         Rep.AddPrintCell(rs.value("T_DEALDATE"),             col2_len,  null, "l:ex_FS():ex_B(tblr)");  
         Rep.AddPrintCell(rs.value("STR_TYPE"),               col3_len,  null, "l:ex_FS():ex_B(tblr)");  
         Rep.AddPrintCell(rs.value("T_SHORTNAME"),            col4_len,  null, "l:ex_FS():ex_B(tblr)");  
         Rep.AddPrintCell(rs.value("T_NAME_OPERATION"),       col5_len,  null, "l:ex_FS():ex_B(tblr)");  
         Rep.AddPrintCell(FIKind_Name(rs.value("t_fi_kind")), col6_len,  null, "l:ex_FS():ex_B(tblr)");  
         Rep.AddPrintCell(rs.value("T_BASEAMOUNT"),           col7_len,  null, "l:ex_FS():ex_B(tblr)");  
         Rep.AddPrintCell(rs.value("T_PRICE"),                col8_len,  null, "l:ex_FS():ex_B(tblr)");  
         Rep.AddPrintCell(rs.value("t_ccy"),                  col8_len,  null, "l:ex_FS():ex_B(tblr)");  
         Rep.AddPrintCell(rs.value("T_PAYMDATE"),             col10_len, null, "l:ex_FS():ex_B(tblr)");  
         Rep.AddPrintCell(rs.value("T_PAYMSTATUS"),           col11_len, null, "l:ex_FS():ex_B(tblr)");  
         Rep.AddStr();rn = rn + 1;   

      end;
      EndAction(1);
/*********************************************************/

      Rep.PrintWinRep("Отчет");
      Rep.SetZoomType(ZOOM_TYPE_A4L);
      Rep.SetWinRepOutput();
      Rep.SetFileNameWin("РСХБ-Дилинг. Отчет по сделкам за период");
      Rep.ShowWinRep(); 

    end;

    macro on_KeyPressed(ev)
      var errflag = 0;
      if (ev.KeyCode == K_F3)
     
        if(valtype(ev.source.parent) != V_UNDEF)
          if(ev.source.name == "fld_beg_Date")
            fld_beg_Date.value = GetDateByCalendar({CurDate});
            this.redraw; 
          elif(ev.source.name == "fld_end_Date") 
            fld_end_Date.value = GetDateByCalendar({CurDate});
            this.redraw; 
          end;
        end;
      elif (ev.KeyCode == K_Enter)
        this.redraw; 
      elif(ev.keyCode == K_F2) 
        PrintDealingPaym(THIS.fld_beg_Date.value, THIS.fld_end_Date.value);
        this.close(1);
      end;
    end; 
END; 


macro exec_report
  var pnlReport:TRSBpanel = pnl_DealingPaym();
  pnlReport.run();
end;

exec_report();
exit(1);

