/**                                                                                                             
 @file  dlUpdateDvCompay.mac                                                                                           
 @brief Разовая процедура переноса остатков по задолженности по требованиям по сделкам валютного рынка от 03.03.2023 на счета пророченной задолженности 458*,
        а также включение в представление "Просмотр и оплата требований по клиентам" не оплаченных требований по сделкам валютного рынка от 11.02.2022 
        с последующим автоматическим пересчетом данных этого представления                                                                          
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2025.04.15 |Жиц М.В.       |BOSS-2211           |Создание макроса процедуры                                                                                                                                                 
*/ 
import "dv_compay_utils.mac";

private var CarryDate:date = {curdate};
private var ErrMes:string = "";
private var NDebt:integer = 0; 

/**
 @brief Транзакция переноса всей задолженности от 03.03.2023 на счета просроченной задолженности 458*
*/	
PRIVATE MACRO TrnTransferDebtTo458()
  var cmd, DataSet;
  var FD = NULL;
  record DebtAcc(account);
  record ComAcc(account);

  cmd = DL_RSDCommand("SELECT mp.t_SfContrID, com.t_ClientID, com.t_DlContrID, com.t_RSum  " +
                      "  FROM (SELECT u.t_ClientID, u.t_DlContrID, SUM(u.t_ReqSum - u.t_Payed_Tr) AS t_RSum " +                        
                      "          FROM u_compay_dbt u " +                                       
                      "         WHERE u.t_CalcID = (SELECT MAX(t_CalcID) FROM u_compay_dbt) " +
                      "           AND u.t_SysDate IS NOT NULL " +                              
                      "           AND (u.t_ReqSum - u.t_Payed_Tr) <> 0 " +
                      "         GROUP BY u.t_ClientID, u.t_DlContrID) com, " +
                      "       ddlcontrmp_dbt mp, dsfcontr_dbt sf " +
                      " WHERE mp.t_DlContrID = com.t_DlContrID " +
                      "   AND sf.t_ID = mp.t_SfContrID " + 
                      "   AND sf.t_ServKind = 21 " +
                      "   AND sf.t_DateClose = to_date('01.01.0001','dd.mm.yyyy') "
                     );
  DataSet = cmd.Execute();

  while(DataSet.moveNext())
    NDebt = NDebt + 1;
    FD = DL_SubContrFD(DataSet.SfContrID);

    if(not OpenDebtAccount(FD, CarryDate, NATCUR, DebtAcc))
      RunError("Ошибка при открытии счета по категории \"Треб. с н.с. брок\" в рамках договора брокерского обслуживания № "+GetDLContrNumber(DataSet.DlContrID));
    end;
  
    if(not FD.OpenAccount("+РасчетыКомисс1", NULL, true, ComAcc, CarryDate, NATCUR))
      RunError("Ошибка: не открыт счет категории \"+РасчетыКомисс1\" в рамках договора брокерского обслуживания № "+GetDLContrNumber(DataSet.DlContrID));
    end;

    CreateCarryByExpired(FD, DebtAcc, ComAcc,
                         CarryDate, 1, NATCUR, DataSet.RSum,
                         "Перенос на счет просроченной задолженности в рамках договора брокерского обслуживания № "+GetDLContrNumber(DataSet.DlContrID)+". Клиент - "+DL_getPartyShortName(DataSet.ClientID),
                         {oper}, "15029"+string(NDebt), false, null);
  end;

onError(erru)  
  if(IsEqClass("TrslError", erru))
    ErrMes = erru.Message;
    if(IsEqClass("TDbError", erru.err))
      ErrMes = ErrMes+":|"+erru.err.Stat+"-"+erru.err.Oper+"-"+erru.err.Table+"-"+erru.err.Message;
    end;
  elif(not ErrMes)
    ErrMes = "Неизвестная ошибка выполнения транзакции по переносу задолженности от 03.03.2023 на счета 458*";
  end;
  AbortTrn();
END;

/**
 @brief Транзакция включения задолженности от 11.02.2022 в общий перечень путем удаления/корректировки платежей
*/	
PRIVATE MACRO TrnDeletePayments110222()
  var Query = RSDCommand("DECLARE " +
                         "BEGIN " +
                         "  FOR cur IN (SELECT pm.t_paymentid " +
                         "                FROM ddvndeal_dbt ndeal " +
                         "               INNER JOIN ddlcomis_dbt comm " +
                         "                  ON comm.t_docid = ndeal.t_id " +
                         "                 AND comm.t_dockind = ndeal.t_dockind " +
                         "                 AND comm.t_isbankexpenses <> CHR(88) " +
                         "                 AND comm.t_clientcontrid > 0 " +
                         "                 AND comm.t_factpaydate = TO_DATE('11.02.2022', 'dd.mm.yyyy') " +
                         "                 AND comm.t_factpaydate > ndeal.t_date " +
                         "               INNER JOIN dsfcomiss_dbt sfc " +
                         "                  ON sfc.t_number = comm.t_comnumber " +
                         "                 AND sfc.T_FEETYPE = comm.T_FEETYPE " +
                         "                 AND sfc.t_receiverid IN (SELECT d.t_PartyID FROM ddp_dep_dbt d) " +
                         "               INNER JOIN dpmpaym_dbt pm " +
                         "                  ON pm.t_dockind = ndeal.t_dockind " +
                         "                 AND pm.t_documentid = ndeal.t_id " +
                         "                 AND pm.t_purpose = 72 " +
                         "                 AND pm.t_payer = ndeal.t_client " +
                         "                 AND pm.t_fiid = sfc.T_FIID_COMM " +
                         "               WHERE ndeal.t_dockind IN (4813, 199) " +
                         "                 AND ndeal.t_state = 2 " +
                         "                 AND ndeal.t_client IN (150153, 138001, 140344, 151125, 151912, 152027, 138176, 144589) " +
                         "                 AND ndeal.t_id NOT IN (606292, 606293, 606294, 606297, 606302, 606303, 607507, 607508, 607509, 607510, 607511, 607512) " +
                         "             ) " +
                         "  LOOP " +
                         "    DELETE FROM dpmpaym_dbt WHERE t_paymentid = cur.t_paymentid; " + 
                         "    DELETE FROM dpmprop_dbt WHERE t_paymentid = cur.t_paymentid; " + 
                         "    DELETE FROM dpmrmprop_dbt WHERE t_paymentid = cur.t_paymentid; " +                                                                           
                         "  END LOOP; " +
                         "  UPDATE dpmpaym_dbt " +
                         "     SET t_amount = 240.93, " +
                         "         t_payamount = 240.93, " +
                         "         t_baseamount = 240.93, " +
                         "         t_futurepayeramount = 240.93, " +
                         "         t_futurereceiveramount = 240.93, " +
                         "         t_futurebaseamount = 240.93, " +
                         "         t_orderamount = 240.93 " +
                         "   WHERE t_dockind = 4813 " +
                         "     AND t_documentid = 607512 " +
                         "     AND t_purpose = 72; " +
                         "END; ");
  Query.execute();

onError(erru)  
  if(IsEqClass("TrslError", erru))
    ErrMes = erru.Message;
    if(IsEqClass("TDbError", erru.err))
      ErrMes = ErrMes+":|"+erru.err.Stat+"-"+erru.err.Oper+"-"+erru.err.Table+"-"+erru.err.Message;
    end;
  elif(not ErrMes)
    ErrMes = "Неизвестная ошибка выполнения транзакции по включению задолженности от 11.02.2022 в общий перечень";
  end;
  AbortTrn();
end; 

/**
 @brief Основаная ф-ция процедуры
*/	
MACRO UpdateDvCompay()  
  var Progress = TProgressBar;

  if(GetDate(CarryDate, "Дата формирования проводок по переносу остатков:"))
    Progress.Init(-1, "Перенос задолженности от 03.03.2023 на счета 458*", "Перенос задолженности на 458*"); 
    if(not ProcessTrn(0, "TrnTransferDebtTo458"))
      println("При переносе задолженности от 03.03.2023 на счета 458* возникли ошибки:\n" + ErrMes);
      return;
    else
      println("Успешно выполнен перенос задолженности от 03.03.2023 на счета 458* для " + NDebt + " субдоговоров.");
    end;
    Progress.Remove();

    Progress.Init(-1, "Включение задолженности от 11.02.2022 в общий перечень", "Включение задолженности от 2022 года"); 
    if(not ProcessTrn(0, "TrnDeletePayments110222"))
      println("При включении задолженности от 11.02.2022 в общий перечень возникли ошибки:\n" + ErrMes);
    else
      println("Успешно выполнено включение задолженности от 11.02.2022 в общий перечень.");
    end;
    Progress.Remove();

    Progress.Init(-1, "Пересчет данных представления \"Просмотр и оплата требований по клиентам\"", "Пересчет данных"); 
    DVComPayFindAndInsert(DVCOMPAY_BEGDATE_FIND);
    Progress.Remove();
  end;
END; 

UpdateDvCompay();
