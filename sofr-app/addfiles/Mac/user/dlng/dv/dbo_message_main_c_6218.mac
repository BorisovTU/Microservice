// таблица содержит только заголовок, после создания файла он открывается снова и заполняются счета
import oralib, likepy, cb_sql, globals;
import rsexts;
Import rsd, RsbDataSet;
Import rslscr;
import email_library;
import "u_common_email_utils.mac";

class dboMessage(_dlcontrid)
   var ddl_id = _dlcontrid, isLast = true; 
   var PartyId = 0, Prt_Cod1 ="", Prt_Cod8 ="", Prt_Empl = 0, Prt_Lfrm = 0, Prt_FNam = "", Prt_Nnam = ""; 
   var Cnt_Eml = "", Cnt_Iis = "", Cnt_Dbeg, Cnt_Stat = 0, Cnt_Emsg = 0;
   var n101 = "", n102 = "", n103 = "", n104 = "";   
   var ar_accs = TArray();   

   const wdFormatPDF = 17;

   private var oWrd, oDoc, IsNtr = isStandalone();

   private var PATH_OUT;
   private var FileDirForm;
   var Mess = "";

   var TemplName_UL = "";
   var TemplName_FL = "";
   var TemplName_IIS = "";
   var Templ_Dir = "";

   var place, doname, donnam, doeml;

   var statMes = "";
   var statsend = 0;
                      
   var GODBO = "";
   var partyForm = "";
   var iisNumber = "";
   var NameDP   = "";
   var NumberDBO = "";
   var NumberBO = "";
   var NameBO   = "";      
   var FolderName = "";
   var FormFilename = "";
   var TempFileName = "";
   var w_ext = ".docx";

   var NumberSend;

   var FormFilenamePDF = "";
   var MessPDF = "";
   var pdfSaveStat = false;
/*CHVA*/
  private macro IIF(val, retval, retval2)
    if (Val)
       return retval;
    else
       return retval2;
    end;
  end;

   statMes = "Сообщение уже было сформировано";
   // наименование секции обслуживания
   private macro GetNameSectionGetNameSection(kind, kindsub, isocur)
      var retval = "";

      if (kind == 15)
         retval = "Срочный рынок";
      elif(kind == 1)
         if (KindSub == 8)
             retval = "Фондовый рынок";
         elif (KindSub == 9)
             retval = "Внебиржевой рынок";
         end;
      elif(kind == 21)
         if (KindSub == 8)
            retval = "Валютный рынок";
         end;
      end;

      return RetVal + " (" + isocur + ")";
   end;
   // пустая SQL строка
   macro emp_str(p_par)
      if ((p_par == null) or (p_par == nullval) or (p_par == strfor(1)) or (p_par == strfor(0)) or (p_par == "01.01.0001"))
         return "";
      end;         
      return p_Par;
   end;
   // клиент - ГО
   macro isGOClient(p_par)
      return ((p_Par == "") or (p_par == "00"));
   end;
   // статус договора - обработка завершена
   macro processingCompleted()      
      return Cnt_Stat > 0;  
      // реализация с обращением к базе
      var sqls, rsdc;
      sqls = String( 
                  "DECLARE   \n"
                 ,"   v_Ret NUMBER(5);   \n"
                 ,"BEGIN   \n"
                 ,"   SELECT COUNT(1)   \n"
                 ,"     INTO v_Ret   \n"
                 ,"     FROM Dobjatcor_Dbt Dob   \n"
                 ,"    WHERE Dob.t_Object = Lpad(:p_Cnt_Id, 34, '0')   \n"
                 ,"      AND Dob.t_Attrid = 3   \n"
                 ,"      AND Dob.t_Objecttype = 207   \n"
                 ,"      AND Dob.t_Groupid = 101;   \n"
                 ,"   :p_Ret := v_Ret;   \n"
                 ,"END;   \n"
                );
      rsdc = rsdcommand(sqls);
      rsdc.AddParam("p_Cnt_Id", RSDBP_IN, ddl_id);
      rsdc.AddParam("p_Ret", RSDBP_OUT, v_integer  );
      rsdc.execute;
      sqls = int(rsdc.Value("p_Ret"));
      rsdc = close; 
      return sqls > 0;
      onError
         return false;
   end;
   // наличие сообщения по договору
   macro isExistsMessage() 
      return Cnt_Emsg > 0;
      // реализация с обращением к базе
      var sqls, rsdc;
      sqls = String( 
                     "DECLARE   \n"
                    ,"   v_Ret INTEGER;   \n"
                    ,"BEGIN   \n"
                    ,"   SELECT COUNT(1)   \n"
                    ,"     INTO v_Ret   \n"
                    ,"     FROM Ddlcontrmsg_Dbt Msg, Dimgdata_Dbt Dim, Dimage_Dbt Dig   \n"
                    ,"    WHERE Dim.t_Objectid = Lpad(Msg.t_Id, 34, '0')   \n"
                    ,"      AND Dig.t_Imageid = Dim.t_Imageid   \n"
                    ,"      AND Msg.t_Kind = 500   \n"
                    ,"      AND Msg.t_Dlcontrid = :p_Cnt_Id;   \n"
                    ,"   :p_Ret := v_Ret;   \n"
                    ,"END;   \n"
                   );
      rsdc = rsdcommand(sqls);
      rsdc.AddParam("p_Cnt_Id", RSDBP_IN, ddl_id);
      rsdc.AddParam("p_Ret", RSDBP_OUT, v_integer);
      rsdc.execute;
      sqls = int(rsdc.Value("p_Ret"));
      rsdc = null;
      return sqls > 0;  
   onerror(e);
      return false;   
   end;
   // структура хранения счетов
   private class CAccs(_acc, _mpcode, _mpregdate,_servkind, _servkindsub, _isocur, _sect_name)
      var acc, mpcode, servkind, servkindsub, mpregdate, isocur, sect_name;
      acc         = _acc;         
      mpcode      = _mpcode;      
      mpregdate   = _mpregdate;
      servkind    = _servkind;    
      servkindsub = _servkindsub;
      isocur      = _isocur;
      sect_name   = _sect_name;
   end;
   // получить список счетов по договору
   macro get_acc_set
      var sqls, rsdc, rsds;
      sqls = String( 
                     "SELECT Decode(p.t_Legalform   \n"
                    ,"             ,1   \n"
                    ,"             ,St.t_Account   \n"
                    ,"             ,DECODE ("+Prt_Empl+", 1 , St.t_Account, (SELECT Ba.t_Account   \n"/*CHVA 524787*/
                    ,"                FROM Dbrokacc_Dbt Ba   \n"
                    ,"            WHERE Ba.t_Servkind = case when RSB_SECUR.GetGeneralMainObjAttr (659,LPAD (sf.t_ID, 10, '0'),102,:p_CalcDate) = 1 /*edp*/ THEN 0 ELSE Sf.t_Servkind END    \n"
                     "                 AND Ba.t_Servkindsub = case when RSB_SECUR.GetGeneralMainObjAttr (659,LPAD (sf.t_ID, 10, '0'),102,:p_CalcDate1) = 1 /*edp*/ THEN 0 ELSE Sf.t_Servkindsub END    \n"
                    ,"                 AND Ba.t_Currency = St.t_Fiid   \n"
                    ,"                 AND Substr(Ba.t_Account, 1, 5) = Substr(St.t_Account, 1, 5)))) b_Account   \n"
                    ,"      ,Mp.t_Mpcode   \n"
                    ,"      ,NVL(TO_CHAR(Mp.t_Mpregdate, 'DD.MM.YYYY'), '01.01.0001') t_Mpregdate  \n"
                    ,"      ,Sf.t_Servkind   \n"
                    ,"      ,Sf.t_Servkindsub   \n"
                    ,"      ,(SELECT t_Ccy FROM Dfininstr_Dbt WHERE t_Fiid = St.t_Fiid) Cur   \n"
                    ,"      ,CASE   \n"
                    ,"          WHEN Sf.t_Servkind = 15 THEN   \n"
                    ,"           'Срочный рынок'   \n"
                    ,"          WHEN Sf.t_Servkind = 1 AND Sf.t_Servkindsub = 8 THEN   \n"
                    ,"           'Фондовый рынок'   \n"
                    ,"          WHEN Sf.t_Servkind = 1 AND Sf.t_Servkindsub = 9 THEN   \n"
                    ,"           'Внебиржевой рынок'   \n"
                    ,"          WHEN Sf.t_Servkind = 21 AND Sf.t_Servkindsub = 8 THEN   \n"
                    ,"           'Валютный рынок'   \n"
                    ,"          ELSE   \n"
                    ,"           ''   \n"
                    ,"       END || '( ' || (SELECT t_Ccy FROM Dfininstr_Dbt WHERE t_Fiid = St.t_Fiid) || ' )' Sect_Name   \n"
                    ,"  FROM Ddlcontrmp_Dbt Mp   \n"
                    ," INNER JOIN Dsfcontr_Dbt Sf   \n"
                    ,"    ON (Sf.t_Id = Mp.t_Sfcontrid and sf.t_dateclose = to_date('01010001','ddmmyyyy'))   \n"
                    ," INNER JOIN Dparty_Dbt p   \n"
                    ,"    ON (p.t_Partyid = Sf.t_Partyid)   \n"
                    ," INNER JOIN Dsfssi_Dbt Ssi   \n"
                    ,"    ON (Ssi.t_Objecttype = 659 AND Ssi.t_Objectid = Lpad(Sf.t_Id, 10, '0'))   \n"
                    ," INNER JOIN Dsettacc_Dbt St   \n"
                    ,"    ON (St.t_Settaccid = Ssi.t_Setaccid)   \n"
                    ," WHERE Mp.t_Dlcontrid = :Ddl_Id   \n"
                    ," ORDER BY Sf.t_Servkind, Sf.t_Servkindsub   \n"
                   );
      rsdc = rsdcommand(sqls);
      rsdc.AddParam("p_CalcDate", RSDBP_IN,  {curdate});
      rsdc.AddParam("p_CalcDate1", RSDBP_IN, {curdate});
      rsdc.AddParam("ddl_id", RSDBP_IN, ddl_id);
      rsds = rsdrecordset(rsdc, RSDVAL_CLIENT_IF_NEEDED, RSDVAL_FORWARD_ONLY);
      ar_accs = TArray;
      while (rsds.moveNext())
         if ((rsds.value("b_Account") != null) and (rsds.value("b_Account") != nullval)) 
            ar_accs(ar_accs.size) = CAccs(rsds.value("b_Account")
                                         ,emp_str(rsds.value("t_Mpcode"))
                                         ,emp_str(rsds.value("t_Mpregdate"))
                                         ,rsds.value("t_Servkind")
                                         ,rsds.value("t_Servkindsub")
                                         ,rsds.value("Cur")
                                         ,emp_str(rsds.value("Sect_Name")));
         end;
      end;
   end;
   // собрать данные по договору
   macro constructor()           
      var sqls, rsdc, rsds, StrErr;
      statMes = "";
      statsend = 0;
      isLast = true;
      sqls = String( 
                     "DECLARE   \n"
                    ,"   v_Cnt_Id   Ddlcontr_Dbt.t_Dlcontrid%TYPE := :p_Cnt_Id;   \n"
                    ,"   v_Partyid  Dparty_Dbt.t_Partyid%TYPE;   \n"
                    ,"   v_Prt_Cod1 Dobjcode_Dbt.t_Code%TYPE;   \n"
                    ,"   v_Prt_Cod8 Dobjcode_Dbt.t_Code%TYPE;   \n"
                    ,"   v_Prt_Lfrm Dparty_Dbt.t_Legalform%TYPE;   \n"
                    ,"   v_Prt_Fnam Dparty_Dbt.t_Name%TYPE;   \n"
                    ,"   v_Prt_Nnam Dparty_Dbt.t_Name%TYPE;   \n"
                    ,"   v_Prt_Empl NUMBER(5);   \n"
                    ,"   v_Cnt_Numb Dsfcontr_Dbt.t_Number%TYPE;   \n"
                    ,"   v_Cnt_Name Dsfcontr_Dbt.t_Name%TYPE;   \n"
                    ,"   v_Cnt_Dbeg DATE;   \n"
                    ,"   v_Cnt_Stat NUMBER(5);   \n"
                    ,"   v_Cnt_Emsg NUMBER(5);   \n"
                    ,"   v_Cnt_N101 VARCHAR2(500);   \n"
                    ,"   v_Cnt_N102 VARCHAR2(500);   \n"
                    ,"   v_Cnt_N103 DATE;   \n"
                    ,"   v_Cnt_N104 VARCHAR2(500);   \n"
                    ,"   v_Cnt_Iis  Ddlcontr_Dbt.t_Iis%TYPE;   \n"
                    ,"   v_Cnt_I1n  Ddlcontr_Dbt.t_Iisfirstnumber%TYPE;   \n"
                    ,"   v_Godbo    VARCHAR2(50);   \n"
                    ,"   v_Cnt_Eml  Ddlcontr_Dbt.t_Email%TYPE;   \n"
                    ,"   v_Do_Name  Dparty_Dbt.t_Name%TYPE;   \n"
                    ,"   v_Do_Plac  Dadress_Dbt.t_Place%TYPE;   \n"
                    ,"   v_Do_Nnam  Dparty_Dbt.t_Normalizedname%TYPE;   \n"
                    ,"   v_Do_Eml   Dcontact_Dbt.t_Value%TYPE;   \n"
                    ,"   v_Err      VARCHAR2(2000);   \n"
                    ,"BEGIN   \n"
                    ,"   BEGIN   \n"
                    ,"      SELECT Dp.t_Partyid   \n"
                    ,"            ,Dp.t_Name Prt_Name   \n"
                    ,"            ,Dp.t_Normalizedname Nrm_Name   \n"
                    ,"            ,Dp.t_Legalform Prt_Lf   \n"
                    ,"            ,(SELECT t_Code   \n"
                    ,"                FROM Dobjcode_Dbt   \n"
                    ,"               WHERE t_Objectid = Dsf.t_Partyid   \n"
                    ,"                 AND t_Objecttype = 3   \n"
                    ,"                 AND t_State = 0   \n"
                    ,"                 AND t_Codekind = 1) t_Code1   \n"
                    ,"            ,(SELECT t_Code   \n"
                    ,"                FROM Ddlobjcode_Dbt   \n"
                    ,"               WHERE t_Objectid = Dl.t_Sfcontrid   \n"
                    ,"                 AND t_Objecttype = 207   \n"
                    //,"                 AND t_State = 0   \n"
                    ,"                 AND t_Codekind = 1) t_Code8   \n"
                    ,"            ,CASE   \n"
                    ,"                WHEN Dp.t_Legalform = 2 THEN   \n"
                    ,"                 Nvl((SELECT Decode(p.t_Isemployer, Chr(88), 1, 0) FROM Dpersn_Dbt p WHERE p.t_Personid = Dp.t_Partyid)   \n"
                    ,"                    ,0)   \n"
                    ,"                ELSE   \n"
                    ,"                 0   \n"
                    ,"             END   \n"
                    ,"            ,Dsf.t_Name Dsf_Name   \n"
                    ,"            ,Dsf.t_Number Dsf_Numb   \n"
                    ,"            ,Dsf.t_Datebegin Dsf_Datb   \n"
                    ,"            ,Dl.t_Iis   \n"
                    ,"            ,Decode(Dl.t_Iis, 'X', Dl.t_Iisfirstnumber, 'Нет ИИС')   \n"
                    ,"            ,Dl.t_Email   \n"
                    ,"        INTO v_Partyid   \n"
                    ,"            ,v_Prt_Fnam   \n"
                    ,"            ,v_Prt_Nnam   \n"
                    ,"            ,v_Prt_Lfrm   \n"
                    ,"            ,v_Prt_Cod1   \n"
                    ,"            ,v_Prt_Cod8   \n"
                    ,"            ,v_Prt_Empl   \n"
                    ,"            ,v_Cnt_Name   \n"
                    ,"            ,v_Cnt_Numb   \n"
                    ,"            ,v_Cnt_Dbeg   \n"
                    ,"            ,v_Cnt_Iis   \n"
                    ,"            ,v_Cnt_I1n   \n"
                    ,"            ,v_Cnt_Eml   \n"
                    ,"        FROM Ddlcontr_Dbt Dl   \n"
                    ,"       INNER JOIN Dsfcontr_Dbt Dsf   \n"
                    ,"          ON (Dsf.t_Id = Dl.t_Sfcontrid)   \n"
                    ,"       INNER JOIN Dparty_Dbt Dp   \n"
                    ,"          ON (Dp.t_Partyid = Dsf.t_Partyid)   \n"
                    ,"       WHERE Dl.t_Dlcontrid = v_Cnt_Id;   \n"
                    ,"      -- примечания по договору   \n"
                    ,"      FOR Nt IN (SELECT t.t_Notekind, t.t_Text   \n"
                    ,"                   FROM Dnotetext_Dbt t   \n"
                    ,"                  WHERE t.t_Objecttype = 207   \n"
                    ,"                    AND t.t_Documentid = Lpad(v_Cnt_Id, 34, '0')   \n"
                    ,"                  ORDER BY t.t_Notekind, t.t_Validtodate) LOOP   \n"
                    ,"         BEGIN   \n"
                    ,"            CASE   \n"
                    ,"               WHEN Nt.t_Notekind = 101 THEN   \n"
                    ,"                  v_Cnt_N101 := TRIM(Chr(0) FROM Rsb_Struct.Getstring(Nt.t_Text));   \n"
                    ,"               WHEN Nt.t_Notekind = 102 THEN   \n"
                    ,"                  v_Cnt_N102 := TRIM(Chr(0) FROM Rsb_Struct.Getstring(Nt.t_Text));   \n"
                    ,"               WHEN Nt.t_Notekind = 103 THEN   \n"
                    ,"                  v_Cnt_N103 := Rsb_Struct.Getdate(Nt.t_Text);   \n"
                    ,"                  IF v_Cnt_N103 = To_Date('01010001', 'DDMMYYYY') THEN   \n"
                    ,"                     v_Cnt_N103 := NULL;   \n"
                    ,"                  END IF;   \n"
                    ,"               WHEN Nt.t_Notekind = 104 THEN   \n"
                    ,"                  v_Cnt_N104 := TRIM(Chr(0) FROM Rsb_Struct.Getstring(Nt.t_Text));   \n"
                    ,"               ELSE   \n"
                    ,"                  NULL;   \n"
                    ,"            END CASE;   \n"
                    ,"         EXCEPTION   \n"
                    ,"            WHEN OTHERS THEN   \n"
                    ,"               NULL;   \n"
                    ,"         END;   \n"
                    ,"      END LOOP;   \n"
                    ,"      -- состояние договора  по категории 101 ДБО   \n"
                    ,"      SELECT Decode(COUNT(1), 0, 0, 1)   \n"
                    ,"        INTO v_Cnt_Stat   \n"
                    ,"        FROM Dobjatcor_Dbt t   \n"
                    ,"       WHERE t.t_Objecttype = 207   \n"
                    ,"         AND t.t_Groupid = 101   \n"
                    ,"         AND t.t_Object = Lpad(v_Cnt_Id, 34, '0')   \n"
                    ,"         AND t.t_Validtodate = To_Date('31129999', 'DDMMYYYY')   \n"
                    ,"         AND t.t_Attrid = 3;   \n"
                    ,"      -- наличие сообщений по договору   \n"
                    ,"      SELECT Decode(COUNT(1), 0, 0, 1)   \n"
                    ,"        INTO v_Cnt_Emsg   \n"
                    ,"        FROM Ddlcontrmsg_Dbt Msg, Dimgdata_Dbt Dim, Dimage_Dbt Dig   \n"
                    ,"       WHERE Dim.t_Objectid = Lpad(Msg.t_Id, 34, '0')   \n"
                    ,"         AND Dig.t_Imageid = Dim.t_Imageid   \n"
                    ,"         AND Msg.t_Kind = 500   \n"
                    ,"         AND Msg.t_Dlcontrid = v_Cnt_Id;   \n"
                    ,"      -- номер филиала   \n"
                    ,"      IF Instr(v_Cnt_Numb, '-') > 0 AND Instr(v_Cnt_Numb, '-') < 8  THEN   \n"
                    ,"         IF Instr(v_Cnt_Numb, '/') > 0 THEN   \n"
                    ,"            v_Godbo := Substr(v_Cnt_Numb, 1, Instr(v_Cnt_Numb, '/') - 1);   \n"
                    ,"         ELSE   \n"
                    ,"            v_Godbo := Substr(v_Cnt_Numb, 1, Instr(v_Cnt_Numb, '-') - 1);   \n"
                    ,"         END IF;   \n"
                    ,"         IF Length(v_Godbo) > 2 THEN  \n"
                    ,"            v_Godbo := '';   \n"
                    ,"         END IF;   \n"
                    ,"      END IF;   \n"
                    ,"      -- наименование филиала, очень похоже на рег.номер КО   \n"
                    ,"      IF TRIM(v_Godbo) IS NOT NULL THEN   \n"
                    ,"         v_Do_Name := '3349/' || v_Godbo;   \n"
                    ,"         BEGIN   \n"
                    ,"            SELECT Decode(Adr.t_Place, Chr(1), Adr.t_District, Adr.t_Place), Prt.t_Normalizedname   \n"
                    ,"              INTO v_Do_Plac, v_Do_Nnam   \n"
                    ,"              FROM Ddp_Dep_Dbt Dpt   \n"
                    ,"             INNER JOIN Dparty_Dbt Prt   \n"
                    ,"                ON (Prt.t_Partyid = Dpt.t_Partyid)   \n"
                    ,"             INNER JOIN Dadress_Dbt Adr   \n"
                    ,"                ON (Adr.t_Partyid = Dpt.t_Partyid AND Adr.t_Type = 1)   \n"
                    ,"             WHERE Dpt.t_Name = v_Godbo || '00';   \n"
                    ,"         EXCEPTION   \n"
                    ,"            WHEN OTHERS THEN   \n"
                    ,"               v_Do_Name := 'АО РОССЕЛЬХОЗБАНК';   \n"
                    ,"               v_Do_Plac := 'Москва';   \n"
                    ,"         END;   \n"
                    ,"         BEGIN   \n"
                    ,"            SELECT '<' || Cnt.t_Value || '>'   \n"
                    ,"              INTO v_Do_Eml   \n"
                    ,"              FROM Ddp_Dep_Dbt Dpt   \n"
                    ,"             INNER JOIN Dcontact_Dbt Cnt   \n"
                    ,"                ON (Cnt.t_Partyid = Dpt.t_Partyid AND Cnt.t_Contactkind = 5 AND Cnt.t_Contacttype = 8)   \n"
                    ,"             WHERE Dpt.t_Name = v_Godbo || '00';   \n"
                    ,"         EXCEPTION   \n"
                    ,"            WHEN OTHERS THEN   \n"
                    ,"               v_Do_Eml := '';   \n"
                    ,"         END;   \n"
                    ,"      ELSE   \n"
                    ,"         v_Do_Name := 'АО Россельхозбанк';   \n"
                    ,"         v_Do_Plac := 'Москва';   \n"
                    ,"      END IF;   \n"
                    ,"      v_Do_Plac := Nvl(TRIM(TRIM(Chr(1) FROM v_Do_Plac)), 'Москва');   \n"
                    ,"   EXCEPTION   \n"
                    ,"      WHEN No_Data_Found THEN   \n"
                    ,"         v_Err := 'Сообщение не сформировано и не отправлено';   \n"
                    ,"      WHEN OTHERS THEN   \n"
                    ,"         v_Err := 'Сообщение не сформировано и не отправлено' || SQLERRM;   \n"
                    ,"   END;   \n"
                    ,"   :p_Partyid  := v_Partyid;   \n"
                    ,"   :p_Prt_Cod1 := v_Prt_Cod1;   \n"
                    ,"   :p_Prt_Cod8 := v_Prt_Cod8;   \n"
                    ,"   :p_Prt_Lfrm := v_Prt_Lfrm;   \n"
                    ,"   :p_Prt_Fnam := v_Prt_Fnam;   \n"
                    ,"   :p_Prt_Nnam := v_Prt_Nnam;   \n"
                    ,"   :p_Prt_Empl := v_Prt_Empl;   \n"
                    ,"   :p_Cnt_Numb := v_Cnt_Numb;   \n"
                    ,"   :p_Cnt_Name := v_Cnt_Name;   \n"
                    ,"   :p_Cnt_Dbeg := v_Cnt_Dbeg;   \n"
                    ,"   :p_Cnt_Stat := v_Cnt_Stat;   \n"
                    ,"   :p_Cnt_Emsg := v_Cnt_Emsg;   \n"
                    ,"   :p_Cnt_Iis  := v_Cnt_Iis;   \n"
                    ,"   :p_Cnt_I1n  := v_Cnt_I1n;   \n"
                    ,"   :p_Cnt_Eml  := v_Cnt_Eml;   \n"
                    ,"   :p_Cnt_N101 := v_Cnt_N101;   \n"
                    ,"   :p_Cnt_N102 := v_Cnt_N102;   \n"
                    ,"   :p_Cnt_N103 := To_Char(v_Cnt_N103, 'DD.MM.YYYY');   \n"
                    ,"   :p_Cnt_N104 := v_Cnt_N104;   \n"
                    ,"   :p_Godbo    := v_Godbo;   \n"
                    ,"   :p_Do_Name  := v_Do_Name;   \n"
                    ,"   :p_Do_Plac  := v_Do_Plac;   \n"
                    ,"   :p_Do_Nnam  := v_Do_Nnam;   \n"
                    ,"   :p_Do_Eml   := v_Do_Eml;   \n"
                    ,"   :p_Err      := v_Err;   \n"
                    ,"END;   \n"
                   );
      rsdc = rsdcommand(sqls);
      rsdc.AddParam("p_Cnt_Id", RSDBP_IN, ddl_id);
      rsdc.AddParam("p_Partyid", RSDBP_OUT, v_integer);
      rsdc.AddParam("p_Prt_Cod1", RSDBP_OUT, v_string, 50);
      rsdc.AddParam("p_Prt_Cod8", RSDBP_OUT, v_string, 50);
      rsdc.AddParam("p_Prt_Lfrm", RSDBP_OUT, v_integer);
      rsdc.AddParam("p_Prt_Fnam", RSDBP_OUT, v_string, 500);
      rsdc.AddParam("p_Prt_Nnam", RSDBP_OUT, v_string, 500);
      rsdc.AddParam("p_Prt_Empl", RSDBP_OUT, v_integer);
      rsdc.AddParam("p_Cnt_Numb", RSDBP_OUT, v_string, 500);
      rsdc.AddParam("p_Cnt_Name", RSDBP_OUT, v_string, 500);
      rsdc.AddParam("p_Cnt_Dbeg", RSDBP_OUT, v_date);
      rsdc.AddParam("p_Cnt_Stat", RSDBP_OUT, v_integer);
      rsdc.AddParam("p_Cnt_Emsg", RSDBP_OUT, v_integer);
      rsdc.AddParam("p_Cnt_Iis", RSDBP_OUT, v_string);
      rsdc.AddParam("p_Cnt_I1n", RSDBP_OUT, v_string, 50);
      rsdc.AddParam("p_Cnt_Eml", RSDBP_OUT, v_string, 500);
      rsdc.AddParam("p_Cnt_N101", RSDBP_OUT, v_string, 100);
      rsdc.AddParam("p_Cnt_N102", RSDBP_OUT, v_string, 100);
      rsdc.AddParam("p_Cnt_N103", RSDBP_OUT, v_string);
      rsdc.AddParam("p_Cnt_N104", RSDBP_OUT, v_string, 100);
      rsdc.AddParam("p_Godbo", RSDBP_OUT, v_string);
      rsdc.AddParam("p_Do_Name", RSDBP_OUT, v_string, 500);
      rsdc.AddParam("p_Do_Plac", RSDBP_OUT, v_string, 500);
      rsdc.AddParam("p_Do_Nnam", RSDBP_OUT, v_string, 500);
      rsdc.AddParam("p_Do_Eml", RSDBP_OUT, v_string, 500);
      rsdc.AddParam("p_Err", RSDBP_OUT, v_string, 2000);
      rsdc.execute;   
      if ((rsdc.Value("p_Err") != null) and (rsdc.Value("p_Err") != nullval))
         runerror("Ошиба получения данных по договору", rsdc.Value("p_Err"));
      end;        
      PartyId   = int(rsdc.value("p_Partyid"));
      Prt_Cod1  = emp_str(rsdc.value("p_Prt_Cod1"));
      Prt_Cod8  = emp_str(rsdc.value("p_Prt_Cod8"));
      Prt_Lfrm  = int(rsdc.value("p_Prt_Lfrm"));
      if (Prt_Lfrm == 1)
         PartyForm = "Юридическое лицо";
      elif (Prt_Lfrm == 2)
         PartyForm = "Физическое лицо";
      end;
      Prt_Fnam  = emp_str(rsdc.value("p_Prt_Fnam"));
      Prt_Nnam  = emp_str(rsdc.value("p_Prt_Nnam"));
      Prt_Empl  = int(rsdc.value("p_Prt_Empl"));
      if ((Prt_Nnam == null) or (Prt_Nnam == nullval))
         Prt_Nnam = strsubst(strupr(Prt_Fnam), "-", " ");
         Prt_Nnam = strsubst(strupr(Prt_Nnam), "Ё", "Е ");
         Prt_Nnam = strsubst(strupr(Prt_Nnam), "ё", "е");
         while (index(Prt_Nnam, "  ") > 0)
            Prt_Nnam = strsubst(Prt_Nnam, "  ", " ");
         end;
      end;
      NumberDBO = emp_str(rsdc.value("p_Cnt_Numb"));
      NumberDBO = emp_str(NumberDBO);
      NameBO    = rsdc.value("p_Cnt_Name");
      if (strlen(trim(NameBO)) == 0)
         NameBO = Prt_Fnam;
      end;
      Cnt_Dbeg = rsdc.value("p_Cnt_Dbeg"); 
      Cnt_Stat = int(rsdc.value("p_Cnt_Stat")); 
      Cnt_Emsg = int(rsdc.value("p_Cnt_Emsg")); 
      NumberBO  = NumberDBO; 
      Cnt_Iis   = emp_str(rsdc.Value("p_Cnt_Iis"));
      iisNumber   = rsdc.Value("p_Cnt_I1n");
      if ((iisNumber == null) or (iisNumber == nullval))
         iisNumber = "Нет ИИС";
      end;
      Cnt_Eml = emp_str(rsdc.Value("p_Cnt_Eml"));
      n101 = rsdc.Value("p_Cnt_N101");  
      n101 = emp_str(n101);
      n102 = rsdc.Value("p_Cnt_N102");
      n102 = emp_str(n102);
      n103 = rsdc.Value("p_Cnt_N103");
      n103 = emp_str(n103);
      n104 = rsdc.Value("p_Cnt_N104");
      n104 = emp_str(n104);

      GODBO  = rsdc.value("p_Godbo");
      GODBO  = emp_str(GODBO); ;
      place  = rsdc.value("p_Do_Plac");
      place  = emp_str(place);
      doname = rsdc.value("p_Do_Name");
      doname = emp_str(doname); 
      donnam = rsdc.value("p_Do_Nnam");
      donnam = emp_str(donnam); 
      doeml  = rsdc.value("p_Do_Eml");
      doeml  = emp_str(doeml); 

      if (PartyId == 114800) 
         NameDP = NameBO;
      else   
         NameDP = Prt_Fnam;
      end;
      get_acc_set();
      // 8-й код клиента - 1-й код ДБО
      if ((ar_accs.size > 0) and (ar_accs(0).mpcode != ""))
         Prt_Cod8 = ar_accs(0).mpcode; 
      end;
      PATH_OUT = "";
      if (isGOClient(GODBO))
         GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\OUT\\ГО", V_STRING, PATH_OUT, StrErr);
         FolderName = "";
      else 
         GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\OUT\\ФИЛИАЛ", V_STRING, PATH_OUT, StrErr);
         FolderName = GODBO + "00 " + strSubst(donnam,"\"","") + "\\";
      end;
      GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEMPLSDIR", V_STRING, Templ_Dir, StrErr);
      FileDirForm = "..\\txtFile\\";

      FormFilename = NumberDBO + "_" + NameDP;// + w_ext;
      FormFilename = strSubst(FormFilename,"\"","");
      FormFilename = strSubst(FormFilename,"\\","-");
      FormFilename = strSubst(FormFilename,"/","-");

      FormFilenamePDF = FormFilename + ".pdf";
      FormFilename    = FormFilename + w_ext;

      rsdc = null;
   onerror(e);
      rsdc = null;       
      if ((e.Err != null) or (e.Err != nullval))
         statMes = e.Err;
      else 
         statMes = e.Message;
      end;
      statsend = -1;
   end;
   // полное имя файла из относительного
   macro abs_path(p)
      var i = 0, l = 0, pth = "";
      if (substr(p, 1, 1) != ".")
         return p;
      end;        
      if (IsNtr)
         pth = GetCurDir(false);
         while (true)
            i = index(pth, "\\", l + 1);
            if (i == 0)
               break;
            else
               l = i;
            end;
         end;       
         if (l > 0)          
            pth = substr(pth, 1, l - 1);
         end;              
      else
         pth = GetCurDir(true);
      end;
      p = strsubst(p, "..", "");
      return string(pth, p); 
   end;
   // закрыть Word
   macro cls_word()
      if (oDoc)
         // закрыть без сохранения, так как сохранение уже могло не выполниться
         // после этого Word в скрытом режиме держит обращение к пользователю
         oDoc.Close(false);
      end;   
      oDoc = null;
      if (isLast)
         if (oWrd)
            oWrd.Quit;
         end;   
         oWrd = null;
      end;  
   onerror(e)
      if (oWrd)
         oWrd.Quit;
      end;   
      oDoc = oWrd = null;
   end;  
   // коррекция терминального имени для копирования
   macro fnam4copy(p)
   if (not isNtr)
      if (substr(p, 1, 1) == "$")
         return p;
      end;
      return string("$", p);
   end;
   return p;
   end;
   // открыть Word
   macro opn_word(fnam)
      if (oWrd == null)     
         if (IsNtr)
            oWrd = ActiveX("Word.Application", NULL, false);
         else
            oWrd = CreateObject("rsax", "TRsAxServer", "RsAxServer", false).CreateComObject("Word.Application", false);
         end;
      end;   
      oWrd.DisplayAlerts = 0;
      oWrd.Visible = false;
// shev 10.12.2019 иначе при работе макроса через sheduler word зависает и съедает всю память    
//    oDoc = oWrd.Documents.Open(fnam, false, false, false);
      oDoc = oWrd.Documents.Open(fnam);
      return true;
   onerror (e);                                              
      return false;
   end;
   // значение из Word - пустое
   macro nrm_wstr(p_val)
   p_val = emp_str(p_val);
   p_val = strsubst(p_val, strfor(0), "");
   p_val = strsubst(p_val, strfor(1), "");
   p_val = strsubst(p_val, strfor(7), "");
   p_val = strsubst(p_val, strfor(9), "");
   p_val = strsubst(p_val, strfor(10), "");
   p_val = strsubst(p_val, strfor(13), "");
   p_val = strsubst(p_val, " ", "");
   p_val = trim(p_val);            
   return p_val;
   end;
   // закладки 
   macro fill_bmk
      var bmks, nbmk = 1, bmkc, nam_b = "", val_b = "";     
      bmks = oDoc.Bookmarks;
      bmkc = bmks.Count;
      while (bmkc > 0)                

         nam_b = Strupr(bmks.item(1).Name); 
         if (index(nam_b, "КЛИЕНТ_НАИМЕНОВАНИЕ") > 0)  
            val_b = NameDp;                             
         elif (index(nam_b, "НОМЕР_УВЕДОМЛЕНИЯ") > 0)
            if (valtype(NumberSend)==0)
               val_b = "1";
            else
               val_b = NumberSend;
            end;
         elif (index(nam_b, "ДАТА_УВЕДОМЛЕНИЯ") > 0)
            val_b = string(date():f);
         elif (index(nam_b, "ДАТА_СОГЛ") > 0)
            val_b = string(Cnt_Dbeg:f);
         elif (index(nam_b, "НОМЕР_СОГЛ") > 0)
            val_b = NumberDBO;
         elif (index(nam_b, "КОДКЛИЕНТА") > 0)
            val_b = Prt_Cod8;
         elif (index(nam_b, "ДОГОВОРДЕПО") > 0)
            val_b = n102;
   /*      elif (index(nam_b, "СЧЕТДЕПО") > 0)
            val_b = n101;*/
         /*CHVA 500663*/
         elif (substr(nam_b,1,8) ==  "СЧЕТДЕПО")
            val_b = n101;
           /*****************/ 
         elif (index(nam_b, "ТОРГСЧЕТДЕПО") > 0)
            val_b = n104;
         elif (index(nam_b, "ДАТАДЕПО") > 0)
            val_b = n103;
         elif (index(nam_b, "ЛОГИН") > 0)
            val_b = trim(string(PartyID));
         elif (index(nam_b, "ГОРОД") > 0)
            val_b = place;
         elif (index(nam_b, "ДОВСП") > 0)
            val_b = doname;
         else             
            bmks.item(1).delete;
         end;           
         bmks.item(1).Range.Text = val_b;
         if (bmkc == bmks.Count)
            bmks.item(1).delete;
         else   
         end;
         bmkc = bmks.Count;
      end;
      end;
   // таблицы 
   macro fill_tbl
      var n_Tbl = 1, n_Lin = 1, n_Col = 1, l_All = 0 , t_All = 0, c_All = 0, w_Lin, w_Tbl, w_Rws;
      // определить таблицу и строку, откуда начинаются счета
      t_All = oDoc.Tables.Count;
      while (n_Tbl <= t_All)         
         l_All = oDoc.Tables(n_Tbl).Rows.Count;
         n_Lin = 1;
         while (n_Lin <= l_All)
            c_All = oDoc.Tables(n_Tbl).Rows(n_Lin).Cells.Count;
            n_Col = 1;
            while (n_Col <= c_All)
               if (index(strupr(nrm_wstr(oDoc.Tables(n_Tbl).Cell(n_Lin, n_Col).Range.Text)), "КОДВТОРГОВОЙ(БИРЖЕВОЙ)СЕКЦИИ") > 0)
                  w_Tbl = oDoc.Tables(n_Tbl);           
                  w_Lin = n_Lin + 1;
                  n_Col = c_All;
                  n_Lin = l_All;
                  n_Tbl = t_All;
               end;
               n_Col = n_Col + 1;
            end;
            n_Lin = n_Lin + 1; 
         end;
         n_Tbl = n_Tbl + 1;   
      end;
      if (not w_Tbl)
         cls_word;
         return;
      end;
      // если счет один - заполнить строку таблицы из шаблона
      if (ar_accs.size == 1)
         n_Lin = 0;
         w_Tbl.Cell(w_Lin, 1).Range.Text = ar_accs(n_Lin).acc;                                    
         w_Tbl.Cell(w_Lin, 2).Range.Text = ar_accs(n_Lin).sect_name;                              
         w_Tbl.Cell(w_Lin, 3).Range.Text = ar_accs(n_Lin).mpcode;                                 
         w_Tbl.Cell(w_Lin, 4).Range.Text = ar_accs(n_Lin).mpregdate;
      end;
      // если счетов больше одного - добавить недостающие строки в таблицу 
      if (ar_accs.size > 1)
         n_Lin = 0;
         while (n_Lin < ar_accs.size - 1)
            w_Rws = w_Tbl.Rows.Add(w_Tbl.Rows(w_Lin));
            n_Lin = n_Lin + 1;
         end;
         n_Lin = 0;
         while (n_Lin < ar_accs.size)
            w_Tbl.Cell(n_Lin + w_Lin, 1).Range.Text = ar_accs(n_Lin).acc;                                    
            w_Tbl.Cell(n_Lin + w_Lin, 2).Range.Text = ar_accs(n_Lin).sect_name;                              
            w_Tbl.Cell(n_Lin + w_Lin, 3).Range.Text = ar_accs(n_Lin).mpcode;                                 
            w_Tbl.Cell(n_Lin + w_Lin, 4).Range.Text = ar_accs(n_Lin).mpregdate;
            n_Lin = n_Lin + 1;
         end;
      end;
   end;

   // Golovkin 13.11.2019 ID : 499257 сохраняем в pdf
   macro save_pdf()
       MessPDF = abs_path(toAnsi(MessPDF));
       oDoc.SaveAs(MessPDF, wdFormatPDF);
       pdfSaveStat = true;
   onerror(e)
       pdfSaveStat = false; 
   end;

   // формирование текста уведомления
   macro Head_Form(p_Templ)
   var tpl_dir, strErr, fil_nam = "", count = 0, str = "", i = 0;
   var tpl_nam = "";
   //доставить шаблон в место формирования
   tpl_nam = FindPath(toANSI(p_Templ), Templ_Dir);
   if (strlen(trim(tpl_nam)) == 0)
      runerror("Ошибка поиска шаблона", "Ошибка поиска шаблона " + p_Templ);
   end;  
   fil_nam = abs_path(toAnsi(MESS));                        
   MESS = fil_nam;
   if (NOT CopyFile(tpl_nam, fnam4copy(fil_nam), false, "Копирование шаблона"))
      runerror("Ошибка получения шаблона", "Ошибка получения шаблона " + toOem(tpl_nam));
   end;                                   
   if (not opn_word(fil_nam))
      cls_word();            
      runerror("Ошибка открытия файла", "Ошибка открытия файла " + toOem(fil_nam));
   end;
   fill_bmk();
   fill_tbl();
   oDoc.Save;
//   save_pdf();
   cls_word();     
   return "";       
   onerror(e)
      if (e.Err)
         if (IsEqClass ("TRsComErr", e.err))
            count = e.err.Count;
            i = 0;
            while (i < count)
               str = string(str, e.err.Message(i), " (Code = ",e.err.Code(i), ", Level = ",e.err.Level(i), ")", strfor(10));
               i = i + 1
            end;
            str = str + " " + e.Module + " " + e.Line + " " + e.Message;
         elif (valtype(e.err) == v_string)
            str = e.err;
         else
            str = e.Module + " " + e.Line + " " + e.Message;
         end;  
      else
         str = e.Module + " " + e.Line + " " + e.Message;
      end;    
      //если ошибки работы с Word то надо полностью закрывать Word для этой сессии
      //иначе дальнешее поведение непредсказуемо
      isLast = true;
      cls_word();
      return str;
   end;
   // создать уведомление
   macro CreateNotification()
      Mess = string(FileDirForm, FormFilename);
      MessPDF = string(FileDirForm, FormFilenamePDF);

      if (Prt_Lfrm == 1)
         if (Cnt_Iis == "X")
            return Head_Form(TemplName_IIS);
         else
            return Head_Form(TemplName_UL);
         end;
      elif (Prt_Lfrm == 2)
         if (Prt_Empl == 1)
            if (Cnt_Iis == "X")
               return Head_Form(TemplName_IIS);
            else
               //return Head_Form(TemplName_FL);
               //Chesmokov D.S. 514595 для ИП шаблон юрлица
               return Head_Form(TemplName_UL);
            end;
         else
            if (Cnt_Iis == "X")
               return Head_Form(TemplName_IIS);
            else
               return Head_Form(TemplName_FL);
            end;
         end;
      end;                   
      return "";       
   end;
   // выгрузка сообщения в файл
   macro downLoadMessage()
      var sqls, rsdc, rsds;
      sqls = String( 
                     "SELECT Dbms_Lob.Getlength(Dig.t_Fmtblobdata_Xxxx) Siz Dig.t_Fmtblobdata_Xxxx Val   \n"
                    ,"  FROM Ddlcontrmsg_Dbt Msg, Dimgdata_Dbt Dim, Dimage_Dbt Dig   \n"
                    ," WHERE Lpad(Msg.t_Id, 34, '0') = Dim.t_Objectid   \n"
                    ,"   AND Msg.t_Kind = 500   \n"
                    ,"   AND Dim.t_Imageid = Dig.t_Imageid   \n"
                    ,"   AND Msg.t_Dlcontrid = :p_Cnt_Id   \n"
                   );
      rsdc = RsdCommand(sqls);
      rsdc.addParam("p_cnt_id" ,RSDBP_IN, ddl_id);
      rsds = rsdrecordset(rsdc,RSDVAL_CLIENT_IF_NEEDED,RSDVAL_FORWARD_ONLY);
      if (rsds.MoveNext)
         rsds.Fld("val").Read(sqls, Int(rsds.Value("siz")));
      end;
      rsds = null;
      rsdc = TStream(FileDirForm + FormFilename, "WA");
      rsdc.write2(sqls);
      rsdc = null;
      onerror(err);
   end;
   // имя файла - сообщения
   macro getMessageNameInDB()            
      var sqls, rsdc;
      sqls = String( 
                     "DECLARE   \n"
                    ,"   v_Ret Dimgdata_Dbt.t_Filename%TYPE;   \n"
                    ,"BEGIN   \n"
                    ,"   BEGIN   \n"
                    ,"      SELECT t_Filename   \n"
                    ,"        INTO v_Ret   \n"
                    ,"        FROM (SELECT Dim.t_Filename   \n"
                    ,"                FROM Ddlcontrmsg_Dbt Msg, Dimgdata_Dbt Dim, Dimage_Dbt Dig   \n"
                    ,"               WHERE Dim.t_Objectid = Lpad(Msg.t_Id, 34, '0')   \n"
                    ,"                 AND Dig.t_Imageid = Dim.t_Imageid   \n"
                    ,"                 AND Msg.t_Kind = 500   \n"
                    ,"                 AND Msg.t_Dlcontrid = :p_Cnt_Id   \n"
                    ,"               ORDER BY Msg.t_Createdate DESC, Msg.t_Createtime DESC)   \n"
                    ,"       WHERE Rownum = 1;   \n"
                    ,"   EXCEPTION   \n"
                    ,"      WHEN OTHERS THEN   \n"
                    ,"         v_Ret := NULL;   \n"
                    ,"   END;   \n"
                    ,"   :p_Ret := v_Ret;   \n"
                    ,"END;   \n"
                   );
      rsdc = rsdcommand(sqls);
      rsdc.AddParam("p_Cnt_Id", RSDBP_IN, ddl_id);
      rsdc.AddParam("p_Ret", RSDBP_OUT, v_string, 200);
      rsdc.execute;
      sqls = rsdc.Value("p_Ret");
      rsdc = null;                                   
      if ((sqls != null) and (sqls != nullval))
         FormFilename = sqls;
      end;
   end;
   // отправка уведомления
   macro SendNotification(sendClient, sendTest, testEmail)
      private macro GetFullPath( Path )
          var fullpath;
          if( substr( Path, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
            Path = substr(Path, 3 );
          end;
          if( substr( Path, 1, 1 ) == "\\" ) /* ссылка на вышестоящий каталог */
            Path = substr(Path, 2 );
          end;
          fullpath =   strsubst( GetCurDir(false) , "\\Obj","")  + "\\" + Path ; 
        // получить полный путь
        return fullpath;
      end;;
      var Eml, emailText, Subject, v_email_processor;
      var p_Attacment = "Памятка клиента.pdf";
      var templ_dir;
      var    Attachement_name;
      var strerr;
      GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEMPLSDIR", V_STRING, Templ_Dir, StrErr);
      Attachement_name = FindPath(toANSI(p_Attacment), Templ_Dir);
      if (SendClient)
         Eml = Cnt_Eml;
         Subject = "Уведомление!!! " + NumberDBO + "_" + NameDP;
//       emailText = "Добрый день!\n" +
//                   "В соответствии с условиями Регламента оказания брокерских услуг АО Россельхозбанк\n" +
//                   "направляем Вам Уведомление о приеме на брокерское и депозитарное обслуживание. \n" +
//                   "Данное письмо было отправлено автоматически.";
         emailText = string("Добрый день!",
                            "\n",
                            "\nВ соответствии с условиями Регламента оказания брокерских услуг АО Россельхозбанк направляем Вам Уведомление о приеме на брокерское и депозитарное обслуживание.",
                            "\n",
                            "Получение временного пароля для входа в торговые терминалы QUIK и РСХБ-БРОКЕР, а также установка кодового слова производится на сайте Банка https://www.rshb.ru/ в разделе <Частным лицам> - <Брокерское обслуживание>.",
                            "\n",
                            "\nДистрибутив QUIK для ПК находится на сайте Банка в разделе <Частным лицам> - <Брокерское обслуживание> - <Тарифы и документы> - <Торговые терминалы>.",
                            "\nУстановка программного обеспечения для мобильных устройств iQUIK X, QUIK Android X и РСХБ-БРОКЕР производится из App Store или Google Play.",
                            "\n",
                            "\nВ приложенном файле <Памятка клиента> представлена краткая информация по брокерскому обслуживанию.",
                            "\n",
                            "\nТелефоны поддержки клиентов 8 (800) 100-40-40; +7 (495) 651-60-91",
                            "\n",
                            "\nДанное письмо было отправлено автоматически.");
      else
         Eml = doeml;
         Subject = "Уведомление!!! " + NumberDBO + "_" + NameDP;
         emailText = "Добрый день!\n" +
                     "В соответствии с условиями Регламента оказания брокерских услуг АО Россельхозбанк\n" +
                     "направляем Вам Уведомление о приеме на брокерское и депозитарное обслуживание Клиента вашего РФ\n" +
                     "Необходимо распечатать Уведомление, заполнить Место заключения Соглашения, регистрационный номер РФ, \n" +
                     "уполномоченного работника РФ и дату. Затем следует передать Уведомление Клиенту.\n" + 
                     "Копию уведомления с отметкой Клиента о получении направить на адрес BROKER/RSHB.\n" +
                     "Данное письмо было отправлено автоматически.";
      end;
      if ((ValType(Eml) == V_UNDEF) or (ValType(Eml) == 26) or (trim(eml) == StrFor(1)))
         statMes = "Сообщение сформировано, но не отправлено. Не задан email";
         msgbox(statmes);
         statsend = 2;
         return;
      end;
      if (isGOClient(GODBO) and (not sendClient)) /*если отправка напрямую клиенту, то для ГО тоже отправляем*/
         statMes = "Сообщение сформировано, но не отправлено. Клиент обслуживается в ГО";
         statsend = 2;
         return;
      end;
      v_email_processor = c_email_proc_env();
      if (SendTest)
         emailText =
         emailText  + "\n\nЭто сообщение отправлено в целях тестирования.\nПожалуйста, не отвечайте на него."
                    + "\n\n" + Eml 
                    + "\n invest@rshb.ru"
                    + "\n broker@rshb.ru";
         v_email_processor.m_add_email_to_list(testEmail);
      else
         v_email_processor.m_add_email_to_list(Eml);
        // v_email_processor.m_add_email_to_bcc_list("custody@rshb.ru");
         v_email_processor.m_add_email_to_bcc_list("invest@rshb.ru");
         v_email_processor.m_add_broker_email_to_bcc_list();
      end;
      v_email_processor.m_set_msg_head(Subject);
      v_email_processor.m_add_attach_to_list(mess);
      if (strlen(trim(Attachement_name)) != 0)
         v_email_processor.m_add_attach_to_list(GetFullPath(Attachement_name));
      end;
      v_email_processor.m_add_row_to_msg_text(emailText);
      // Сохраняем текст письма для отправки плановой процедурой 
//      v_email_processor.m_save_to_submit_synch();
      v_email_processor.m_save_to_submit();
      // Отправляем все не отправленные письма
//    v_email_processor.m_submit_email_synch();
//v_email_processor.m_submit_email_asynch();

      if(v_email_processor.m_get_error_status())
         statMes = "Сообщение сформировано, но не отправлено: " + v_email_processor.get_service_msg();
         statsend = 2;
      else
         statMes = "Сообщение отправлено";
         statsend = 1;
      end;
   end;
   // сохранить сообщение в образах
   macro ReForm()
      var v_msg = "", sqls, rsdc;
      RslDefCon.BeginTrans();
      sqls = String( 
                     "DECLARE   \n"
                    ,"   v_Cnt_Id Ddlcontr_Dbt.t_Dlcontrid%TYPE := :p_Cnt_Id;   \n"
                    ,"   v_Snd_St NUMBER(5) := :p_Snd_St;   \n"
                    ,"   v_Err    VARCHAR2(2000);   \n"
                    ,"   v_Msg    Ddlcontrmsg_Dbt.t_Id%TYPE;   \n"
                    ,"   Ex_Er EXCEPTION;   \n"
                    ,"BEGIN   \n"
                    ,"   BEGIN   \n"
/*
                    ,"      FOR Rec IN (SELECT Imd.t_Imageid, Msg.t_Id   \n"
                    ,"                    FROM Ddlcontrmsg_Dbt Msg   \n"
                    ,"                   INNER JOIN Dimgdata_Dbt Imd   \n"
                    ,"                      ON (Imd.t_Objecttype = 208 AND Imd.t_Objectid = Lpad(Msg.t_Id, 34, '0'))   \n"
                    ,"                   WHERE Msg.t_Dlcontrid = v_Cnt_Id   \n"
                    ,"                     AND Msg.t_Kind = 500) LOOP   \n"
                    ,"         BEGIN   \n"
                    ,"            DELETE FROM Dimage_Dbt Img WHERE Img.t_Imageid = Rec.t_Imageid;   \n"
                    ,"            DELETE FROM Dimgdata_Dbt Imd WHERE Imd.t_Imageid = Rec.t_Imageid;   \n"
                    ,"            DELETE FROM Ddlcontrmsg_Dbt Msg WHERE Msg.t_Id = Rec.t_Id;   \n"
                    ,"         EXCEPTION   \n"
                    ,"            WHEN OTHERS THEN   \n"
                    ,"               v_Err := 'Ошибка замены сообщения (удаление старого)';   \n"
                    ,"               RAISE Ex_Er;   \n"
                    ,"         END;   \n"
                    ,"      END LOOP;   \n"
*/
                    ,"      IF v_Cnt_Id IS NOT NULL THEN   \n"
                    ,"         INSERT INTO Ddlcontrmsg_Dbt   \n"
                    ,"            (t_Dlcontrid, t_Kind, t_Createdate, t_Createtime, t_Senddate, t_Sendtime)   \n"
                    ,"         VALUES   \n"
                    ,"            (v_Cnt_Id   \n"
                    ,"            ,500   \n"
                    ,"            ,Trunc(SYSDATE)   \n"
                    ,"            ,To_Date('01010001 ' || To_Char(SYSDATE, 'HH24MISS'), 'DDMMYYYY HH24MISS')   \n"
                    ,"            ,CASE v_Snd_St   \n"
                    ,"                WHEN 1 THEN   \n"
                    ,"                 Trunc(SYSDATE)   \n"
                    ,"                ELSE   \n"
                    ,"                 NULL   \n"
                    ,"             END   \n"
                    ,"            ,CASE v_Snd_St   \n"
                    ,"                WHEN 1 THEN   \n"
                    ,"                 To_Date('01010001 ' || To_Char(SYSDATE, 'HH24MISS'), 'DDMMYYYY HH24MISS')   \n"
                    ,"                ELSE   \n"
                    ,"                 NULL   \n"
                    ,"             END)   \n"
                    ,"         RETURNING t_Id INTO v_Msg;   \n"
                    ,"      END IF;   \n"
                    ,"   EXCEPTION   \n"
                    ,"      WHEN OTHERS THEN   \n"
                    ,"         v_Err := Nvl(v_Err, Dbms_Utility.Format_Error_Stack || ' ' || Dbms_Utility.Format_Error_Backtrace);   \n"
                    ,"         v_Msg := NULL;   \n"
                    ,"   END;   \n"
                    ,"   :p_Msg := Lpad(v_Msg, 34, '0');   \n"
                    ,"   :p_Err := v_Err;   \n"
                    ,"END;   \n"
                   );
      rsdc = rsdcommand(sqls);
      rsdc.AddParam("p_cnt_id", RSDBP_IN, ddl_id);
      rsdc.AddParam("p_snd_st", RSDBP_IN, statsend);
      rsdc.AddParam("p_Msg", RSDBP_OUT, v_string, 50);
      rsdc.AddParam("p_Err", RSDBP_OUT, v_string, 2000);
      rsdc.execute;
      v_msg = rsdc.Value("p_err");   
      if ((v_Msg != null) and (v_msg != nullval))
         runerror(v_msg, v_Msg);
      end;                    
      v_msg = rsdc.Value("p_Msg");   
      rsdc = null;
      if (not LoadImageObj(Mess, 208, v_Msg, 1))
         v_Msg = "К записи о сообщении не удалось прикрепить файл сообщения";
         runerror(v_msg, v_Msg);
      end;
      if (RslDefCon.IsInTrans())
         RslDefCon.CommitTrans();
      end;        
   onError(e)
      rsdc = null;
      if (RslDefCon.IsInTrans())
         RslDefCon.RollbackTrans();
      end;                  
      if ((e.Err != null) and (e.Err != nullval))
         statMes = e.Err;
      else        
         statMes = e.Message;
      end; 
   end;
   // создать сообщение
   macro CreateMessage(reformMessage, sendEmail, sendClient, sendTest, testEmail, SendNumber)
      var FileFrom, FileFromPDF, FileFromDOC; 
      macro remove_tmp()
         RemoveFile(FileFromDOC);
         if (pdfSaveStat) // удаление pdf
            RemoveFile(FileFromPDF);
         end;
      end;
      
      if (not processingCompleted()) 
         statsend = -1;
         statMes = "Сообщение не сформировано: Статус договора: Обработка незавершена";
         return this;
      end;
      if (not reformMessage) 
         reformMessage = false;
      end;
      if (not sendEmail) 
         sendEmail = false;
      end;


      // bpv определим наименование шаблона. Параметр sendClient означает прямую отправку клиенту  
      // и вместе с этим нововведением меняются и шаблоны 
      // bpv upd 15 11 2019 оставил только нвоые шаблоны. При отпарвке в филиад - отправим новый шаблон.
//      if (sendClient)
         TemplName_UL = "ОД-1-4.1_" + w_ext;
         TemplName_FL = "ОД-1-4.2_" + w_ext;
         TemplName_IIS = "ОД-1-4.3_" + w_ext;
//      else
//         TemplName_UL = "Ф340_1_4_1" + w_ext;
//         TemplName_FL = "Ф340_1_4_2" + w_ext;
//        TemplName_IIS = "Ф340_1_4_3" + w_ext;
//      end;

      if (not isExistsMessage or reformMessage)
         NumberSend = SendNumber;
         statMes = CreateNotification();

         // локальный путь для трехзвенки
         FileFromDOC = fnam4copy(Mess);
         FileFromPDF = fnam4copy(MessPDF); // Golovkin pdf файл
         
         if (strlen(trim(statMes)) > 0)
            runerror("Ошибка формирования уведомления", "Ошибка формирования сообщения " + FormFilename + " " + statMes);
         end;
         MakeDir(string(PATH_OUT, FolderName));
         // Golovkin пытаемся скопировать Уведомление в сетевую папку 
         if( false /*pdfSaveStat */ ) /*pdf работает, но отключен до тех пор, пока не научимся хорошо нумеровать Уведомления*/
            FileFrom = FileFromPDF;
            Mess = string(PATH_OUT, FolderName, FormFilenamePDF);
         else
            FileFrom = FileFromDOC;
            Mess = string(PATH_OUT, FolderName, FormFilename);
         end;
         
         // перенос файла
         if (not CopyFile(FileFrom, Mess))
            runerror("Ошибка формирования уведомления", "Не удалось скопировать файл " + FormFilename + " в директорию " + string(PATH_OUT, FolderName));
         end;            

         // удаление временных
         remove_tmp();

         // отправка уведомления
         if (sendEmail)
            SendNotification(sendClient, sendTest, testEmail);
         else
            statsend = 2;
         end;
         ReForm();
      else
         if (sendEmail)
             //downLoadMessage();
            getMessageNameInDB();            
            Mess = string(PATH_OUT, FolderName, FormFilename);
            SendNotification(sendClient, sendTest, testEmail);
         end;           
      end;
      return this;
   onerror(e)   
      if ((e.Err != null) and (e.Err != nullval))
         statMes = e.Err;
      else   
         statMes = e.Message;
      end;   
      statsend = -1;
      // удаление временных
      remove_tmp();
      return this;
   end;

   constructor();
end