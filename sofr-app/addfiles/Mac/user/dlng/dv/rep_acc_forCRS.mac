/***********************************************************
07.11.2019 Выгрузка данных по брокерским счетам в целях CRS. BIQ-3529
************************************************************/
/*Добавлены требования по 2 этапу. Выгрузка через планировщик за неограниченный период. Упразднена панель для ввода периода*/
/*С 01.01.2021 заменятся коды клиента в поле 101 с БИСквит на ЦФТ. Соответственно код из поля 101 будем выводить в колонку ЦФТ */
/* 09.11.2020 согласовано с банком: если в коде есть символ # (решетка), то считаем это кодом Бисквит, иначе в ЦФТ.*/
/* 09.11.2020  исключения - t_partyid in (318,134263,134411), их действующие коды 2843249, CC0001001862, CC0001001887 */
/* 12.11.2020 пожелание банка выводить суммы в виде 115,046,200.00*/
/* 08.12.2020 вывод файла в директорию из настроек*/
/*24.12.2021 i-support 535292 переведен на шаблон т.к. winreport не может напечатать 360тыс.записей*/

IMPORT "likepy.mac", oralib, RsbDataSet, RsbFormsInter, "spserv.mac";
IMPORT RSD, or_rep_h, rsexts, "it_utils.mac";

var glb_startDate = date();
var day, mon, year, dat_rec, glb_time_rec, sql_num, num, i;

DateSplit(glb_startDate, day,mon,year);
if (day < 10)
  day = string(0,day)
end;
dat_rec =string (day,".",mon,".",year);

glb_time_rec = dat_rec+" "+string(time) ;

var File_Name = "CRS_SOFR_"+dat_rec+".csv"; 
var File_Name_xls = "CRS_SOFR_"+dat_rec; 
var Path_Out = "..\\TxtFile\\";//+File_Name; 
var Path_Out_xls = GetCurDir(true) + "\\TxtFile\\";
var _Path = "C:\\CRS_UpLoad\\"; //2 этап, вариант локального каталога
GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\CRS_UPLOAD", V_STRING, _Path, null); 

macro OffStandAloneLoc()
  return true;
end;

private macro GetSqlInsertStatement(DateBeg, DateEnd, RecDate, RecTimeStr, LoadTo)
  var sql_ins = 
   "insert into  USR_CLNT_ACC_CRS_DBT "
  +" (T_ID_CLNT_SOFR, T_ACCOUNT, T_CHAPTER, T_OPEN_DATE, T_CLOSE_DATE, T_REST, T_CURRENCY "
  +" ,T_ID_CLNT_BISQ, T_SALE, T_FIL_CONTRACT, T_ID_CLNT_CFT, T_CREATE_DT, T_CREATE_DTTM, T_LOAD_TO ) "

  +" WITH acc_filtered AS ( "
  +"     SELECT * "
  +"     FROM daccount_dbt "
  +"     WHERE T_ACCOUNT LIKE '306%' "
  +"       AND ( "
  +"             (T_OPEN_DATE <= "+GetSQLDate(DateBeg)+" AND  "
  +"              (T_CLOSE_DATE > "+GetSQLDate(DateBeg)+" OR T_CLOSE_DATE = TO_DATE('01.01.0001', 'DD.MM.YYYY'))) "
  +"             OR  "
  +"             (T_OPEN_DATE > "+GetSQLDate(DateBeg)+" AND T_OPEN_DATE < "+GetSQLDate(DateEnd)+") "
  +"           ) "
  +" ) "
  +"  "
  +" SELECT DISTINCT "
  +"     acc.T_CLIENT, "
  +"     acc.T_ACCOUNT, "
  +"     chapt.t_symbol, "
  +"     acc.T_OPEN_DATE, "
  +"     acc.T_CLOSE_DATE, "
  +"    rsi_rsb_account.restall( "
  +"         acc.t_account, "
  +"         acc.t_chapter, "
  +"         acc.t_code_currency, "
  +"         "+GetSQLDate(DateEnd)+", "
  +"         acc.t_code_currency "
  +"     ) AS rest, "
  +"     fininst.t_ccy, "
  +"     CASE  "
  +"         WHEN (objcode_101.T_CODE IN ('2843249','CC0001001862','CC0001001887')  "
  +"               OR (SUBSTR(objcode_101.T_CODE, 3, 1) = '#' AND SUBSTR(objcode_101.T_CODE, 5, 1) = '#')) "
  +"         THEN objcode_101.T_CODE  "
  +"         ELSE ' '  "
  +"     END AS ID_BISQ, "
  +"     NVL(( "
  +"         SELECT SUM(t_credit) "
  +"         FROM drestdate_dbt "
  +"         WHERE t_accountid = acc.T_ACCOUNTID "
  +"           AND t_restdate BETWEEN "+GetSQLDate(DateBeg)+" AND "+GetSQLDate(DateEnd)+" "
  +"     ), 0) AS SALE, "
  +"     DECODE(SF.T_NUMBER, NULL, CHR(0), NVL(SUBSTR(SF.T_NUMBER, 1, INSTR(SF.T_NUMBER, '-', 1) - 1), '00')) AS F_CNTR, "
  +"     CASE  "
  +"         WHEN (objcode_101.T_CODE IN ('2843249','CC0001001862','CC0001001887')  "
  +"               OR (SUBSTR(objcode_101.T_CODE, 3, 1) = '#' AND SUBSTR(objcode_101.T_CODE, 5, 1) = '#')) "
  +"         THEN ' ' "
  +"         ELSE objcode_101.T_CODE  "
  +"     END AS ID_CFT, "
  +"     "+GetSQLDate(RecDate)+" AS DAT, "
  +"     "+GetSQLString(RecTimeStr)+" AS TS, "
  +"     "+GetSQLString(LoadTo)+" AS LOAD_TO "
  +"  "
  +" FROM acc_filtered acc "
  +" JOIN dobchaptr_dbt chapt ON chapt.T_CHAPTER = acc.T_CHAPTER "
  +" JOIN dfininstr_dbt fininst ON fininst.t_fiid = acc.t_code_currency "
  +" LEFT JOIN dobjcode_dbt objcode_101 ON objcode_101.t_objectid = acc.T_CLIENT  "
  +"                                    AND objcode_101.t_objecttype = 3  "
  +"                                    AND objcode_101.t_codekind = 101  "
  +"                                    AND objcode_101.T_BANKCLOSEDATE = TO_DATE('01.01.0001', 'DD.MM.YYYY') "
  +" LEFT JOIN DSETTACC_DBT SA ON acc.T_ACCOUNT = SA.T_ACCOUNT   "
  +"                           AND acc.T_CHAPTER = SA.T_CHAPTER   "
  +"                           AND acc.T_CODE_CURRENCY = SA.T_FIID "
  +" LEFT JOIN DSFSSI_DBT SI ON SI.T_OBJECTTYPE = 659  "
  +"                         AND SA.T_SETTACCID = SI.T_SETACCID "
  +" LEFT JOIN DSFCONTR_DBT SF ON SI.T_OBJECTID = LPAD(SF.T_ID, 10, '0') ";

  return sql_ins;
end;


private macro GetSqlSelectStatement(RecDate, RecTimeStr, LoadTo)
  var sql_sel =
    "     SELECT CRS_DBT.T_ACCOUNT, "
   +"            CRS_DBT.T_CHAPTER, "
   +"            TO_CHAR (CRS_DBT.T_OPEN_DATE, 'DD.MM.YYYY') OPEN_DAT, "
   +"            DECODE (CRS_DBT.T_CLOSE_DATE, "
   +"                    TO_DATE ('01.01.0001', 'DD.MM.YYYY'), CHR (0), "
   +"                    TO_CHAR (CRS_DBT.T_CLOSE_DATE, 'DD.MM.YYYY')) "
   +"               CLOSE_DAT, "
   +"            CRS_DBT.T_REST, "
   +"            CRS_DBT.T_CURRENCY, "
   +"            NVL ( "
   +"               (SELECT T_CODE "
   +"                  FROM dobjcode_dbt "
   +"                 WHERE     t_objectid = CRS_DBT.T_ID_CLNT_SOFR "
   +"                       AND t_objecttype = 3 "
   +"                       AND t_codekind = 1 "
   +"                       AND T_BANKCLOSEDATE = TO_DATE ('01.01.0001', 'DD.MM.YYYY')), "
   +"               0) "
   +"               CODE_SOFR, "
   +"            CRS_DBT.T_ID_CLNT_BISQ, "
   +"            CRS_DBT.T_SALE, "
   +"            RPAD (REPLACE (CRS_DBT.T_FIL_CONTRACT, '/', ''), 4, '0') FIL_CONTR, "
   +"            ' ' AS T_BISQ_RF, "
   +"            NVL (CRS_DBT.T_ID_CLNT_CFT, 0) ID_CLNT_CFT "
   +"       FROM USR_CLNT_ACC_CRS_DBT CRS_DBT "
   +"      WHERE CRS_DBT.T_CREATE_DT = "+GetSQLDate(RecDate)+" and CRS_DBT.T_CREATE_DTTM = " + GetSQLString(RecTimeStr)
   +"            AND CRS_DBT.T_LOAD_TO = "+GetSQLString(LoadTo)+" "
   +"   ORDER BY CRS_DBT.T_ACCOUNT, FIL_CONTR DESC";
  return sql_sel;
end;

private macro GetFuncStatement(RecDate, RecTimeStr, LoadTo)
    var sql_func =   
    " declare "
   +"    "
   +"   cursor cur_data is "
   +"     "+GetSqlSelectStatement(RecDate, RecTimeStr, LoadTo)+"; "
   +"  "
   +"   v_row cur_data%rowtype; "
   +" begin "
   +"   it_rsl_string.clear; "
   +"  "
   +"   for v_row in cur_data loop "
   +"     it_rsl_string.AddCell(v_row.T_ACCOUNT); "
   +"     it_rsl_string.AddCell(v_row.T_CHAPTER); "
   +"     it_rsl_string.AddCell(v_row.OPEN_DAT); "
   +"     it_rsl_string.AddCell(v_row.CLOSE_DAT); "
   +"  "
   +"     it_rsl_string.AddCell(to_char(round(v_row.T_REST, 2), 'FM999999999990.00')); "
   +"  "
   +"     it_rsl_string.AddCell(v_row.T_CURRENCY); "
   +"     it_rsl_string.AddCell(to_char(v_row.CODE_SOFR)); "
   +"     it_rsl_string.AddCell(to_char(v_row.T_ID_CLNT_BISQ)); "
   +"  "
   +"     it_rsl_string.AddCell(to_char(round(v_row.T_SALE, 2), 'FM999999999990.00')); "
   +"  "
   +"     it_rsl_string.AddCell(v_row.FIL_CONTR); "
   +"     it_rsl_string.AddCell(v_row.T_BISQ_RF); "
   +"     it_rsl_string.AddCell(to_char(v_row.ID_CLNT_CFT), p_last => true); "
   +"   end loop; "
   +"  "
   +" exception "
   +"   when others then "
   +"     it_error.put_error_in_stack; "
   +"     it_log.log(p_msg_type => it_log.C_MSG_TYPE__ERROR); "
   +"     raise; "
   +" end; ";
  return sql_func;
end;

private macro GetAccInfoToCsv (filePath, dt_beg, dt_end, dat_rec, time_rec) 
  //Заголовки колонок CSV
  println (); //пожелание  банка оставить в начале пустую строку 09,11,20
  println ("Номер счёта;Область учёта;Дата открытия;Дата закрытия;Остаток;Валюта;ID клиента СОФР;ID клиента в ГО БИСквит;Продажа;Филиал договора;ID клиента в БИСквит РФ;ID клиента в ЦФТ"); 
  i = 0;
  SetOutput ( NULL, TRUE);
  IT_StoreClobBufer(false, filePath, "CSV", null, null, true, true);
  SetOutput ( filePath, TRUE);
  RemProgress();
  return 0;
end; 

private macro GetAccInfoToXls (dt_beg, dt_end, dat_rec, time_rec) 
  //Заголовки Excel
  var Rep = CTemplateSXLSX();
  Rep.OpenTemplate("Report_306crs.xlsx", false);//имя шаблона
  Rep.SetXLSXFormat();
  var table  = Rep.RegisterTable("bodyline");
  BegAction(2000, "Формирование csv файлов", false);
  var fileList = IT_StoreClobBufer(true, PathCombine(GetAbsoluteTxtPath(),string(UserNumber)+"_306crs.txt"), "CSV", true, 100, null, true);
  EndAction();

  Rep.SetValue_NameCell("bodyline");
  table.FillTabelFromCSV(fileList);
  ReplaceMacro ( "IsStandalone", "OffStandAloneLoc");
  Rep.SaveAsTemplate(File_Name_xls, false);
  ReplaceMacro ( "IsStandalone", NULL);

  Path_Out_xls =  DLM_PathCanonicalize(Rep.ReportPath);
  File_Name_xls = Rep.ReportFileName_xls;

  return 0;
end; 

macro RunnCsv(dt_beg, dt_end, dat_rec, time_rec)
  var path = DLM_PathCanonicalize(PrcPath(PathCombine(Path_Out,File_Name)));
  SetOutput(path, false);
  // Для запуска из планировщика используем весь диапазон дат
  GetAccInfoToCsv (path, dt_beg, dt_end, dat_rec, time_rec );
  SetOutput ( NULL, TRUE);
  var path2 = DLM_PathCanonicalize(PrcPath(PathCombine(_Path,File_Name)));
  if ((NOT IsStandalone()) AND (Substr(path2, 1, 1) != "$"))
    path2 = "$" + path2;
  end;
  // копируем файл, 2 этап
  if ((path != path2) and (not CopyFile (path, path2)))
    msgbox ("Ошибка копирования CSV файла "+File_Name);
  end;
  return true;
end;

macro RunnXls(dt_beg, dt_end, dat_rec, time_rec)
  // Для запуска из планировщика используем весь диапазон дат
  GetAccInfoToXls (dt_beg, dt_end, dat_rec, time_rec );
  var path = DLM_PathCanonicalize(PrcPath(PathCombine(Path_Out_xls,File_Name_xls)));
  var path2 = DLM_PathCanonicalize(PrcPath(PathCombine(_Path,File_Name_xls)));
  if ((NOT IsStandalone()) AND (Substr(path, 1, 1) != "$"))
    path2 = "$" + path2;
  end;
  if ((path != path2) and (not CopyFile(path, path2)))
    msgbox ("Ошибка копирования XLS файла "+ File_Name_xls);
  end;
  return true;
end;

macro PrepFillData(dt_beg, dt_end, dat_rec, time_rec, load_to)
  
  if (not IsNoInterfaceRun())
    var bgSql = BackgroundSQLExecuter(GetSqlInsertStatement(dt_beg, dt_end, dat_rec, time_rec, load_to));
    bgSql.Run("Сбор данных");
    bgSql = null;
  else
    if (not ExecSql(GetSqlInsertStatement(dt_beg, dt_end, dat_rec, time_rec, load_to)))
      msgbox ("Ошибка выгрузки даннных в протокол.");
    end;
  end;
  BegAction(2000, "Формирование буфера", false);
  ExecSql(GetFuncStatement(dat_rec, time_rec, load_to));
  EndAction();
end;

macro ClearData ()
  ExecSql("TRUNCATE TABLE USR_CLNT_ACC_CRS_DBT;");
end;

// Для запуска из планировщика используем весь диапазон дат
var prm_dt_beg = date("01.01.1900");
var prm_dt_end = date("01.01.9999");
var prm_daterec = glb_startDate;
var prm_time_rec = glb_time_rec;
var prm_load_to = "None";

ClearData();
PrepFillData(prm_dt_beg, prm_dt_end, prm_daterec, prm_time_rec, prm_load_to);
RunnXls(prm_dt_beg, prm_dt_end, prm_daterec, prm_time_rec); 
RunnCsv(prm_dt_beg, prm_dt_end, prm_daterec, prm_time_rec);

IT_ClearBufer();