//-----------------------------------------------------------------------------------------
// Просмотр протоколов загрузки фиксинга из файлов xml
// Д.Чуйков
//-----------------------------------------------------------------------------------------
import globals, rsexts, oratools, rcw;

Private class CProtokolView
    var regPath = "РСХБ\\ДИРЕКТОРИИ\\IMPORTFIXINGPROT";
    var protDir = "";

    Macro check
        Var err, regVal;

        //проверяем заполнение настройки реестра
        if ( (getRegistryValue (regPath, V_STRING, regVal, err) != V_STRING) or (err) or (regVal == "") )
            msgbox("Не задана настройка реестра "+regPath+"!");
            return false;
        else
            //проверяем корректность пути из настройки
            if (ExistDir(trim(regVal)) == 2) 
                if (ExistDir("$"+trim(regVal)) == 2)
                    msgbox("Каталог "+regVal+" не найден!\n Проверьте корректность настройки "+regPath);
                    return false;
                else
                    protDir = "$"+trim(regVal);
                end;
            else
                protDir = trim(regVal);
            end;		
        end;

        //проверяем наличие файлов в директории
        if ( TDirList (trim(protDir)+"\\protocol_*.xlsx", "F").Count == 0 )
            msgbox("В каталоге "+protDir+"\n не найдены файлы протоколов по фиксингу!");
            return false;
        end;

        return true;
    End;

    Macro readPath
        var Files, cntFile = 0, sql;

        if (ExecSql("delete from usr_fixingpview_tmp", null, false) == null)
            return false;
        end;

        //прочитаем список файлов в каталоге
        Files = TDirList(protDir + "\\protocol_*.xlsx", "F");
        Files.List(); 

        while (cntFile < Files.Count)
            var nameDt = substr(Files.Name(cntFile), index(Files.Name(cntFile), "protocol_")+9, 8); 
            var fName = substr(Files.Name(cntFile), index(Files.Name(cntFile), "protocol_"));

            if ( execSql ("insert into usr_fixingpview_tmp values (:1, :2)",
                          makeArray (sqlParam ("1", date(int(substr(nameDt,1,2)),int(substr(nameDt,3,2)),int(substr(nameDt,5,4)))),
                                     sqlParam ("2", fName)), FALSE) == NULL )
                return false;
            end;

            cntFile = cntFile + 1;
        end;

        return true;
    End;

    //Копирует файл на сервер приложений. Возвращает полный путь на СП
    private macro CopyOnSP (FullNameFile)
        var path = "";
        var FileName, ExtName, DirName;
        var TermPath = GetCurDir(false)+"\\txtfile\\";

        splitfile(FullNameFile, FileName, ExtName);
        FileName = FileName + ExtName;
        DirName = substr(FullNameFile, 1, index(FullNameFile, FileName) - 1);
    
        if ( CopyFile(DirName + FileName, TermPath + FileName) or 
             CopyFile(DirName + FileName, "$"+TermPath + FileName) )
            path = TermPath + FileName;
        else //не скопировалось, пробуем открыть исходный файл
            path = DirName + FileName;
        end;
    
        return path;
    End;

    Macro openFile( fName )
        var startAX:object;
        var xls:object;

        //В условиях РСХБ файл можно открыть только на СП. Если файл не на СП, то необходимо его туда копировать
        //UPD: оказалось что копировать не нужно, но на всякий случай код оставим.
        //var FullNameFile = CopyOnSP(fName);
        var FullNameFile = fName;

        if (isStandAlone())
            xls = ActiveX("Excel.Application", null, false);
            if (not xls)
                msgbox("Невозможно создать объект Excel.Application");
                return false;
            end;
        else
            startAX = CreateObject("rsax", "TRsAxServer", "RTSRateImport", isStandalone());
            if (startAX)
                xls = startAX.CreateComObject("Excel.Application");
            end;
            if ( (not startAX) OR (not xls) )
                msgbox("Невозможно создать объект Excel.Application");
                return false;
            end;
        end;
               
        if ( (not xls.Workbooks) OR (not xls.Workbooks.Open(FullNameFile)) )
            msgbox("Ошибка открытия файла протокола");
            xls.Quit ();
            xls = NULL;
            return false;
        else
            xls.Visible = TRUE;
        end;
      
        return true;
  
      OnError( er )            
        xls.Quit ();
        xls = NULL;
        return false;
    End;

    Macro handlerF ( rs, cmd, id, key )
        if ( (cmd == DLG_KEY) and (key == 13) and (rs.recCount) )
            openFile ( protDir+"\\"+OraString (rs.value ("protname")) );
        end;
    End;

    Macro showDetail ( pdate )
        Var rs = execSqlSelect ("select t.protname from usr_fixingpview_tmp t where t.protdate = :pdt order by t.protname",
                                 makeArray (sqlParam ("pdt", pdate)), NULL, RSDVAL_CLIENT, RSDVAL_STATIC);
        while ( true )
            if (not runScroll (rs, 1, makeArray ("protname", "Имя протокола", 30, 0, 0, 0), 
                   NULL, r2m (this, "HandlerF"), "Список протоколов за дату"+pdate, "Esc-Выход  Enter-Просмотр", TRUE, 5, 5, 30, 25) )
                break;
            end;
        end;
    End;

    Macro handler ( rs, cmd, id, key )
        if ( (cmd == DLG_KEY) and (key == 13) and (rs.recCount) )
            showDetail ( oraDate (rs.value ("protdate")) );
        end;
    End;

    Macro run
        if (not readPath())
           msgBox ("Ошибка анализа файлов в каталоге");
           return;
        end;

        Var rs = execSqlSelect ("select t.protdate,count(t.protname) pcount from usr_fixingpview_tmp t group by t.protdate order by t.protdate", NULL, NULL, RSDVAL_CLIENT, RSDVAL_STATIC);
        runScroll (rs, 2, makeArray ("protdate", "Дата протокола", 13, 2, 0, 0,
                                     "pcount"  , "Количество протоколов", 20, 2, 0, 0),
                   "Данные по протоколам загрузки", r2m (this, "handler"), "Данные по протоколам загрузки", "Esc-Выход  Enter-Список протоколов", TRUE, 0, 1, 40, 25);
    End;

End;
//-----------------------------------------------------------------------------------------

    
//начало макроса
var proc = CProtokolView();
if (proc.check())
   proc.run();
end;

Exit (1); 

