/* Процедура автоматической сверки входящих подтверждений МТ300 с исходящими. 
   Квитовка входящих подтверждений со сделкой.
   Бухарова Е.А. 13.02.2014*/

/* 
  ВР. 29.09.2016. Заплаты из-за несоответствия видов объектов подтверждений 
                  в МБР (OBJTYPE_CONVDEAL, 102) и ФИССИиКО (OBJTYPE_FXOPER_DV, 148).
*/

IMPORT RSBDataSet, "cb_sql.mac", globals;
IMPORT CTInter; /* ВР. */
import RsbFormsInter, Global, or_rep_h,"cb_sql.mac";

const v_specval = 26;

CLASS OfferRep(pDate)

  var  rs, report;                                
  var ReportEX;

  ReportEX = CMakeReport("┌┬┬┬┬┬┬┬┬┬┬┬┐");          
  ReportEX.SetDefaultFontName("Times New Roman");   
  ReportEX.SetDefaultFontSize(10);                  
  ReportEX.SetFlagShowGrid(true);                   


  var vDAte   = pDAte;
  var rn = 1;
  var EXCEL_MONEY = "\"# ##0,00\"";
  var sheet = 1,n_sheet = 2;
  var count = 0;

  var col1_len = 20,  col2_len = 10, col3_len = 30;
  //var col4_len = 15,  col5_len = 12,  col6_len = 16;
  //var col7_len = 9,   col8_len = 7, col9_len = 7;
  var color  = 12632256;  
  var ColorD = 16316664;
  var ColorL = 15395562;

  var head_format = "ex_FS(b):ex_B(tblr):ex_BC(" + color + ")";
  var format      = "ex_FS():ex_B(tblr):ex_BC(" + color + ")";

  private macro ДобавитьСтроку()
    ReportEx.AddStr();
    rn = rn + 1;
  end;

  private macro ДобавитьПустуюСтроку()
    ReportEx.AddEmptyStr();
    rn = rn + 1;
  end; 

  private macro ПечатьШапки ()        

    ДобавитьПустуюСтроку();

  /*1*/   ReportEX.AddPrintCell ("Референс Входящего сообщения",         col1_len,  null, "c:" + head_format);
  /*2*/   ReportEX.AddPrintCell ("Признак успешной квитовки",  col2_len,  null,"c:" + head_format);
  /*2*/   ReportEX.AddPrintCell ("Результат",   col3_len,  null,"c:" + head_format);


    //ReportEX.SetExecute("iBook.Sheets("+sheet+").Rows("+(rn)+").AutoFilter;");
    ДобавитьСтроку;

  end;
                                         
  PRIVATE MACRO IIF( condition, value_true, value_false )
    if( condition )
      return value_true;
    else 
      return value_false;   end;
  END;

  MACRO PrintHead()
   [ ];
   [ ];
   [              ПРОТОКОЛ РАБОТЫ ПРОЦЕДУРЫ                                                         ];
   [ ];                               
   [┌──────────────────┬────────┬──────────────────────────────────────────────────────────────────┐];
   [│ Референс         │Признак │                                                                  │];
   [│ входящего        │успешной│                      Результат                                   │];
   [│ сообщения        │квитовки│                                                                  │];
   [├──────────────────┼────────┼──────────────────────────────────────────────────────────────────┤];

  END;                                 

  Private MACRO PrintLineRev(inref, err, res, fend) 

   [│##################│        │##################################################################│](inref, res);

   if(err == 1)
     [│                  │        │Сообщение отправлено на ручную квитовку                           │];
     if(fend)
       [└──────────────────┴────────┴──────────────────────────────────────────────────────────────────┘];
     else
       [├──────────────────┼────────┼──────────────────────────────────────────────────────────────────┤];
     end;
   end; 

  END;


  Private MACRO PrintLineRevEX(inref, err , res,fend)
    format = "ex_FS():ex_B(tblr)";                                                                             
    var test;
    if(err == 1)                                                                                             
       ReportEX.AddPrintCell(inref, col1_len, null, "c:"+format); // 1.
       ReportEX.AddPrintCell((" "), col2_len, null, "c:"+format); // 2.  
       ReportEX.AddPrintCell(res+"                                        "+"Сообщение отправлено на ручную квитовку", col3_len, null, "l:"+format); // 3.

       ДобавитьСтроку;                                                                                                  
    end;                                                                                                                                                                                                            
  END;                                                                                                                  
  
  Private MACRO PrintLineMat(dealcode, err, fend) 

    if(err == 1)                                                                                      
      [│                  │        │Ошибка: Сообщение не сквитовано                                   │];
    else
      [│                  │   X    │Сообщение сквитовано со сделкой №###################              │](dealcode);
    end;

    if(fend)
      [└──────────────────┴────────┴──────────────────────────────────────────────────────────────────┘];
    else
      [├──────────────────┼────────┼──────────────────────────────────────────────────────────────────┤];
    end;
  END;

  Private MACRO PrintLineMatEX(dealcode, err, fend,inref )                                                                              
   // format = "ex_FS():ex_B(tblr)";
    var test;                                                                                                                 
    if(err == 1)                                                                                                        
                                                                                                                
        ReportEX.AddPrintCell(inref, col1_len, null, "c:"+format); // 1.   
        ReportEX.AddPrintCell((" "), col2_len, null, "c:"+format); // 2.       
        ReportEX.AddPrintCell((err + "    "+ "Сообщение не сквитовано"), col3_len, null, "l:"+format); // 3.     
        ДобавитьСтроку;                                
    else                                                                                                                
                                                                                                           
        ReportEX.AddPrintCell(inref, col1_len, null, "c:"+format); // 1.                            
        ReportEX.AddPrintCell(("X"), col2_len, null, "c:"+format); // 2.                                   
        ReportEX.AddPrintCell(("Найдено исходящее сообщение." + "     " + "Сообщение сквитовано со сделкой"+ " "  +dealcode), col3_len, null, "l:"+format); // 3.             
                                                                                                      
        ДобавитьСтроку; 
                                                                            
    end;                                                                                                                                                                                                                               
  END;                                                                                                                 



  /*
  Установить признак квитовки
  type = 0 ручная квитовка
  type = 1 автоматическая
  */
  MACRO SetSignMes(mesid, type)
    var sql, cmd;
    sql = " UPDATE dwlmes_dbt SET T_RESPPROCESSINGDESCR = '" + IIF(type == 0, "Квитовка вручную", "Автоматическая квитовка") + "' WHERE t_mesid = " + mesid;    
    cmd = RsdCommand(sql);
    cmd.execute;
  END;

  /*Найти значение поля*/
  MACRO TakeField(mesid, namefield) 
    var sql = " select trim(mvl.t_value) value from DWLTPFLD_DBT wlf, DWLMESVAL_DBT mvl "
          + " where wlf.t_name = '" + namefield + "' and wlf.t_tpid = 2 /*SWIFT*/ and mvl.t_tpfieldid = wlf.t_tpfieldid and mvl.t_mesid = " + mesid;
    var cmd = TRSBDataSet(sql);
    if(cmd.MoveNext())    
      return cmd.rec.value;
    end;
    return ""; 
  END;

  /*Найти исходящее сообщение с таким же референсом
  err = 0 найдено сообщение
  err = 1 не найдено сообщение
  err = 2 найдено несколько */
  MACRO FindOutRef(inmesid, inref, objkind, err)
    var sql, cmd;
    objkind = OBJTYPE_FXOPER_DV; /* 148 */ /* ВР. */

    sql = " SELECT mvl.t_mesid FROM DWLTPFLD_DBT wlf, DWLMESVAL_DBT mvl, dwlmeslnk_dbt lnk, DWLMESVAL_DBT mvl30, DWLTPFLD_DBT wlf30 "
      + " WHERE wlf.t_tpid = 2 AND mvl.t_tpfieldid = wlf.t_tpfieldid AND TRIM (mvl.t_value) = '" + inref + "' AND lnk.t_mesid = mvl.t_mesid "
      + " AND wlf.t_name = '22C' AND lnk.t_direct = CHR (0) AND lnk.t_objkind = " + objkind + " AND lnk.t_objid <> 0 AND TRIM (mvl30.t_value) = '" + TakeField(inmesid, "30T") + "' "
      + " AND lnk.t_mesid = mvl30.t_mesid AND wlf30.t_tpid = 2 AND mvl30.t_tpfieldid = wlf30.t_tpfieldid AND wlf30.t_name = '30T'";

    if(SQL_GetNRecs(sql) == 0)
      SetParm(3, 1);
    elif(SQL_GetNRecs(sql) > 1)
      SetParm(3, 2);
    else
      SetParm(3, 0);
      cmd = TRSBDataSet(sql);
      if(cmd.MoveNext())    
        return cmd.rec.mesid;
      end;
    end; 
    return 0;

  END;

   /*Извлекаем валюту из числа, заданного строкой*/
   MACRO FindCurrency(namefield, mesid)
     var sql, cmd;
     sql = " select substr(trim(mvl.t_value), 1, 3) value from DWLTPFLD_DBT wlf, DWLMESVAL_DBT mvl "
         + "  where wlf.t_name = '" + namefield + "' and wlf.t_tpid = 2  and mvl.t_tpfieldid = wlf.t_tpfieldid and mvl.t_mesid = " + mesid;

     cmd = TRSBDataSet(sql);
     if(cmd.MoveNext())    
       return cmd.rec.value;
     end; 

     return "";

   END;

   /*Извлекаем число из строки*/
   MACRO FindSummMoney(namefield, mesid)
     var sql, cmd;
     sql = " select to_number(replace(substr(trim(mvl.t_value), 4), ',', '.'))  value from DWLTPFLD_DBT wlf, DWLMESVAL_DBT mvl "
         + "  where wlf.t_name = '" + namefield + "' and wlf.t_tpid = 2  and mvl.t_tpfieldid = wlf.t_tpfieldid and mvl.t_mesid = " + mesid;

     cmd = TRSBDataSet(sql);
     if(cmd.MoveNext())    
       return cmd.rec.value;
     end; 

     return 0;

   END;

   /*Извлекаем число из строки*/
   MACRO FindSummInt(namefield, mesid)
     var sql, cmd;
     sql = " select to_number(replace(trim(mvl.t_value), ',', '.'))  value from DWLTPFLD_DBT wlf, DWLMESVAL_DBT mvl "
         + "  where wlf.t_name = '" + namefield + "' and wlf.t_tpid = 2  and mvl.t_tpfieldid = wlf.t_tpfieldid and mvl.t_mesid = " + mesid;

     cmd = TRSBDataSet(sql);
     if(cmd.MoveNext())    
       return cmd.rec.value;
     end; 

     return 0;

   END;

   /*Сравниваем деньги(формат ВАЛЧИСЛО) в строках*/
   MACRO CompareMoneyStrings(mesid1, mesid2, namefield1, namefield2)
     if( (StrLen(TakeField(mesid1, namefield1)) > 0) and
         (StrLen(TakeField(mesid2, namefield2)) > 0) and 
         (StrLen(FindCurrency(namefield1, mesid1)) > 0) and 
         (StrLen(FindCurrency(namefield2, mesid2)) > 0) and 
         (FindSummMoney(namefield1, mesid1) > 0) and
         (FindSummMoney(namefield2, mesid2) > 0) and
         (FindCurrency(namefield1, mesid1) == FindCurrency(namefield2, mesid2)) and
         (FindSummMoney(namefield1, mesid1) == FindSummMoney(namefield2, mesid2))
        )
         return true;
     end;

     return false;
   END;

   /*Сравниваем числа(формат ЧИСЛО) в строках*/
   MACRO CompareInt(mesid1, mesid2, namefield)
     if( (StrLen(TakeField(mesid1, namefield)) > 0) and
         (FindSummInt(namefield, mesid1) > 0) and
         (FindSummInt(namefield, mesid1) == FindSummInt(namefield, mesid2))
        )
         return true;
     end;

     return false;
   END;

   /*Сравниваем строки*/
   MACRO CompareStr(mesid1, mesid2, namefield)
     if( (StrLen(TakeField(mesid1, namefield)) > 0) and
         (TakeField(mesid1, namefield) == TakeField(mesid2, namefield))
        )
         return true;
     end;

     return false;
   END;

   /*Сравниваем разные строки*/
   MACRO CompareStrings(mesid1, mesid2, namefield1, namefield2)
     if( (StrLen(TakeField(mesid1, namefield1)) > 0) and
         (StrLen(TakeField(mesid2, namefield2)) > 0) and
         (TakeField(mesid1, namefield1) == TakeField(mesid2, namefield2))
        )
         return true;

     elif( (StrLen(TakeField(mesid1, namefield1)) > 0) and
           (StrLen(TakeField(mesid2, namefield2)) > 0) and
           (SubStr(TakeField(mesid1, namefield1), 1, 8) == SubStr(TakeField(mesid2, namefield2), 1, 8))
        )
         return true;

     end;

     return false;
   END;

   /*Квитовка*/
   MACRO TakeMatching(outmesid, inmesid)
     var sql, cmd;
     var dealid = 0, dealstatus, dealcode = "", objkind = 0;

   /*
     sql = " SELECT tick.T_DEALID, tick.T_DEALSTATUS, tick.t_dealcode, lnk.t_objkind FROM dwlmeslnk_dbt lnk, ddl_tick_dbt tick "
         + " WHERE lnk.t_mesid = " + outmesid + " and tick.t_dealid = lnk.t_objid ";
     ВР.
   */
     sql = " SELECT tick.T_ID, tick.T_STATE, tick.t_code, lnk.t_objkind FROM dwlmeslnk_dbt lnk, ddvndeal_dbt tick "
         + " WHERE lnk.t_mesid = " + outmesid + " and tick.t_id = lnk.t_objid ";
                                                                                      
     cmd = TRSBDataSet(sql);
     if(cmd.movenext())
       dealid = cmd.rec.id;
       dealstatus = cmd.rec.state;
       dealcode = cmd.rec.code;
       objkind = cmd.rec.objkind;
     end;

   /*
     sql = " UPDATE dwlmeslnk_dbt SET T_OBJID = " + dealid + " WHERE T_MESID = " + inmesid + " AND T_OBJID = 0  AND T_OBJKIND = " + objkind;
     ВР.
   */
     sql = " UPDATE dwlmeslnk_dbt SET T_OBJID = " + dealid + ", T_OBJKIND = " + objkind + " WHERE T_MESID = " + inmesid + " AND T_OBJID = 0  AND T_OBJKIND = " + OBJTYPE_CONVDEAL; /* 102 */

     cmd = RsdCommand(sql);
     cmd.execute;

     return dealcode;

   END;
          
   MACRO TakeReviseMatching()
      var sql, cmd, i = 1;
      var err = 0, errtext = "";
      var  report;                                 
      var inref = "", outmesid = 0, inmesid = 0;         
      var dealcode = "";
      var sql_count:integer = 0;

      ПечатьШапки();                                  
     
      if(GetTrue(true, "Произвести сверку и квитовку входящих сообщений МТ300?"))

         sql = "SELECT mes.t_mesid, lnk.t_objkind "
             + "FROM dwldlfx_dbt fx, dwlmeslnk_dbt lnk, dwlmes_dbt mes, dwlmesfrm_dbt frm, dwlmesrls_dbt rls "
             + "WHERE lnk.t_mesid = fx.t_mesid AND "
             + "      mes.t_mesid = lnk.t_mesid AND "
             + "      mes.t_respprocessingdescr = CHR (1) AND "
             + "      rls.t_rlsformid = mes.t_rlsformid AND "
             + "      frm.t_formid = rls.t_formid AND frm.t_name = '300' ";
         cmd = TRSBDataSet(sql);

         if(SQL_GetNRecs(sql) == 0)
            println("Нет входящих сообщений для квитовки");
         else
            PrintHead();
            InitProgress( SQL_GetNRecs(sql), "Сверка и квитовка подтверждений МТ300", "Выполнение..." );
         end;

         while(cmd.movenext())
            inmesid   = cmd.rec.mesid;
            inref     = TakeField(inmesid, "22C");     /*Общий референс*/
            outmesid  = FindOutRef(inmesid, inref, cmd.rec.objkind, err);
            sql_count = SQL_GetNRecs(sql); //Можно ли вынести за пределы цикла?


            if( err == 0)
               if (CompareStr(inmesid, outmesid, "30T"))   /*Дата сделки*/
                  if (CompareStr(inmesid, outmesid, "30V"))    /*Дата валютирования*/ 
                     if (CompareInt(inmesid, outmesid, "36"))    /*Курс конвертации*/
                        if ( (CompareStrings(inmesid, outmesid, "82A", "87A") and CompareStrings(inmesid, outmesid, "87A", "82A")) or      /*Стороны А и B*/
                             (CompareStrings(inmesid, outmesid, "82A", "82A") and CompareStrings(inmesid, outmesid, "87A", "87A"))
                           )
                           if ((CompareStrings(inmesid, outmesid, "82A", "87A") and CompareStrings(inmesid, outmesid, "87A", "82A") and      /*Валюта, сумма*/
                                CompareMoneyStrings(inmesid, outmesid, "32B", "33B") and CompareMoneyStrings(inmesid, outmesid, "33B", "32B")
                               ) OR
                               (CompareStrings(inmesid, outmesid, "82A", "82A") and CompareStrings(inmesid, outmesid, "87A", "87A") and      
                                CompareMoneyStrings(inmesid, outmesid, "32B", "32B") and CompareMoneyStrings(inmesid, outmesid, "33B", "33B")
                               )
                              )
                              PrintLineRev(inref, 0, "Найдено исходящее сообщение.");
                              PrintLineRevEX(inref, 0, "Найдено исходящее сообщение.");
                              SetSignMes(inmesid, 1);
                              
                              /*************Квитовка********************/
                              dealcode = TakeMatching(outmesid, inmesid);
                              if(StrLen(dealcode) > 0)
                                 PrintLineMat(dealcode, 0, IIF(i-1 == sql_count, true, false));                                                                                                                                              
                                 PrintLineMatEx(dealcode, 0, IIF(i-1 == sql_count, true, false),inref);                                                                                      
                              else
                                 PrintLineMat(dealcode, 1, IIF(i-1 == sql_count, true, false));
                                 PrintLineMatEx(dealcode, 1, IIF(i-1 == sql_count, true, false),inref);                                         
                              end; 
                              /****************************************/
                           else
                              errtext = "Ошибка: 32B, 33B Валюта, сумма.";
                           end; 
                        else
                           errtext = "Ошибка: 82а, 87а Сторона А, Сторона В";
                        end; /*82A 87A*/
                     else
                        errtext = "Ошибка: 36 Курс конвертации.";
                     end; /*36*/
                  else
                     errtext = "Ошибка: 30V Дата валютирования.";
                  end; /*30V*/  
               else
                  errtext = "Ошибка: 30T Дата сделки.";
               end; /*30T*/  

            else
               if(err == 1)
                  errtext = "Ошибка: 22С Общий референс. Не найдено исходящее сообщение.";
               elif(err == 2)
                  errtext = "Ошибка: 22С Общий референс. Найдено несколько исходящих сообщений.";
               end;
            end; /*22C*/

            if(StrLen(errtext) > 0)
               PrintLineRev(inref, 1, errtext, IIF(i-1 == sql_count, true, false));
   	         PrintLineRevEX(inref, 1, errtext, IIF(i-1 == sql_count, true, false));                 
               SetSignMes(inmesid, 0);
            end;

            UseProgress(i);
            err = 0;
            errtext = "";
            i = i + 1;                                     
         end;
         ReportEX.PrintRep();
         //ReportEX.PrintWinRep("Квитовка МТ300");        
         ReportEX.SetWinRepOutput();                                    
         //ReportEX.ShowWinRep();                                         

         RemProgress;

      end;
   end;
END;
var start = OfferRep({curdate});
start.TakeReviseMatching();  /*Сверка и квитовка подтверждений*/