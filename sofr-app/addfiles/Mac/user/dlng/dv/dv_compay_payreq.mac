import "dv_compay_utils.mac";

class (DVComPayScrollBase) DVComPayScrollPayList(_calcId:integer, _sfContrId, _paySum:Money, _AccountCFT, _CarryDate)
  InitDVComPayScrollBase();
  var paySum = _paySum;
  var calcId = _calcId;
  var sfContrId = IIF(ValType(_sfContrId) != V_INTEGER, null, _sfContrId);

  var AccountCFT = IIF(ValType(_AccountCFT) != V_STRING, null, _AccountCFT);
  var CarryDate = IIF(ValType(_CarryDate) != V_DATE, null, _CarryDate);
  var IsPayFromCFT:bool = (AccountCFT != null); //Признак списания комиссии с текущего счета от ЦФТ

  macro MakePayment()
    var count = 0;
    if(RslDefCon.IsInTrans)
      RslDefCon.RollbackTrans();
    end;
    RslDefCon.BeginTrans();
    var cmd = DL_RSDCommand("select com.*, NDEAL.t_DocKind from U_COMPAY_TR_TMP com, ddvndeal_dbt ndeal where NDEAL.T_ID = com.T_DEALID");
    var DataSet = cmd.Execute();
    while (DataSet.MoveNext())
      DVComPayCreatePaymentAndTrnAndGetFD(
        SQL_ConvTypeDate(DataSet.DEALDATE),
        SQL_ConvTypeStr(DataSet.DEALCODE),
        SQL_ConvTypeInteger(DataSet.CLIENTID),
        SQL_ConvTypeInteger(DataSet.DocKind),
        SQL_ConvTypeInteger(DataSet.DealID),
        SQL_ConvTypeSum(DataSet.Amount),
        SQL_ConvTypeInteger(DataSet.Fiid),
        null,
        AccountCFT,
        CarryDate
      );
      count = count + 1;
    end;
    RslDefCon.CommitTrans();

    if(not IsPayFromCFT)
      MsgBox("Выполнена оплата комиссий по " + count + " сделке/ам");
    end;
    return true;
  OnError( err )
    if(RslDefCon.IsInTrans)
      RslDefCon.RollbackTrans();
    end;
    DVComPayShowError(err);
    return false;
  end;

  macro EvProc(rs, cmd, id, key)
    /*Обработчик*/
    if(cmd == DLG_KEY)
      /*Esc*/
      if(key == 27)
        return CM_CANCEL;
      /*F2*/
      elif(key == 316)
        return IIF(MakePayment(),CM_SELECT,CM_CANCEL);
      end;
    end;
  end;

  macro BuildSQL()
    return 
      "SELECT com.*, "
     +"       (SELECT fin.T_CCY "
     +"          FROM DFININSTR_DBT fin "
     +"         WHERE fin.t_fiid = com.t_fiid) "
     +"          AS t_fiidStr "
     +"  FROM U_COMPAY_TR_TMP com ";
  end;

  macro BuildRS()
    rs = execSQLselect(BuildSQL(), TArray(), true, RSDVAL_CLIENT, RSDVAL_STATIC);
    return rs;
  end;

  macro ConstructAndShowScroll()
    execSql ("delete from U_COMPAY_TR_TMP");
    if(not IsPayFromCFT)
      if (not getMoney(paySum, "Введите сумму оплаты"))
        return;
      end;
    end;
    var query =
      "INSERT INTO U_COMPAY_TR_TMP (T_DEALID, "
     +"                             T_DEALDATE, "
     +"                             T_DEALCODE, "
     +"                             T_CLIENTID, "
     +"                             T_ACCOUNT_PAYER, "
     +"                             T_ACCOUNT_RECEIVER, "
     +"                             T_FIID, "
     +"                             T_AMOUNT, "
     +"                             T_GROUND) "
     +"   SELECT q4.T_ID, "
     +"          q4.T_DATE, "
     +"          q4.T_CODE, "
     +"          q4.T_CLIENTID, "
     +"          q4.T_ACC306, "
     +"          q4.T_ACC458, "
     +"          q4.T_FIID, "
     +"          q4.t_topaysum, "
     +"             'Оплата комиссии по сделке № ' "
     +"          || T_CODE "
     +"          || ' от ' "
     +"          || TO_CHAR (T_DATE, 'DD.MM.YYYY') "
     +"          || ' в рамках Соглашения об оказании брокерских услуг № ' "
     +"          || T_NUMBER "
     +"          || ' от ' "
     +"          || TO_CHAR (t_contrDate, 'DD.MM.YYYY') "
     +"          || '. Клиент - ' "
     +"          || T_CLIENTNAME "
     +"             AS t_ground "
     +"     FROM (SELECT q3.*, "
     +"                  CASE "
     +"                     WHEN q3.t_RestSum >= 0 THEN t_reqSum "
     +"                     ELSE q3.t_PrevRest "
     +"                  END "
     +"                     t_toPaySum "
     +"             FROM (SELECT q2.*, "
     +"                          NVL (LAG (t_RestSum) OVER (ORDER BY T_DATE, T_ID), "
     +"                               t_InpPaySum) "
     +"                             t_PrevRest "
     +"                     FROM (SELECT q1.*, "
     +"                                  q1.t_InpPaySum - q1.t_IncReqSum "
     +"                                     AS t_RestSum "
     +"                             FROM (  SELECT NDEAL.T_ID, "
     +"                                            NDEAL.T_DATE, "
     +"                                            NDEAL.T_CODE, "
     +"                                            COMPAY.T_CLIENTID, "
     +"                                            COMPAY.T_ACC306, "
     +"                                            COMPAY.T_ACC458, "
     +"                                            COMPAY.T_NUMBER, "
     +"                                            compay.t_contrDate, "
     +"                                            COMPAY.T_CLIENTNAME, "
     +"                                            COMPAY.T_FIID, "
     +"                                            COMPAY.T_REQSUM, "
     +"                                            SUM ( "
     +"                                               COMPAY.T_REQSUM) "
     +"                                            OVER ( "
     +"                                               ORDER BY "
     +"                                                  NDEAL.T_DATE, NDEAL.T_ID "
     +"                                               ROWS BETWEEN UNBOUNDED PRECEDING "
     +"                                               AND     CURRENT ROW) "
     +"                                               AS t_IncReqSum, "
     +"                                            ? AS t_InpPaySum "
     +"                                       FROM U_COMPAY_DBT compay, "
     +"                                            ddvndeal_dbt ndeal "
     +"                                      WHERE t_calcid = ? and t_sfcontrid = ? "
     +"                                        AND NDEAL.T_ID = COMPAY.T_DEALID "
     +"                                   ORDER BY NDEAL.T_DATE, NDEAL.T_ID) q1) q2) q3) q4 "
     +"    WHERE q4.t_topaysum > 0 ";
    BegAction(2000, "Построение оплат", false);
    execSql(query, makeArray(SQLParam("inPay", paySum), SQLParam("calcId", calcId), SQLParam("sfContrId", sfContrId)), true);
    EndAction();

    AddColumn(0, "t_dealid", "ID сделки", 7, 10);
    AddColumn(1, "t_dealcode", "Код сделки", 15, 15);
    AddColumn(2, "t_account_payer", "Дебет", 16, 20);
    AddColumn(3, "t_account_receiver", "Кредит", 16, 20);
    AddColumn(4, "t_amount", "Сумма", 10, 10);
    AddColumn(5, "t_fiidStr", "Валюта", 5, 5);
    AddColumn(6, "t_ground", "Основание", 100, 100);

    if(IsPayFromCFT) //При списании со счетов в ЦФТ скроллинг показывать не нужно, сразу переходим к оплате
      return MakePayment();
    else
      return RunScroll (BuildRS(), NumColumns, Columns, "DVCPPSCRL", R2M(this,"EvProc"), "Проводки по оплате", 
                   "F2 - выполнить проводки, Esc - выход", true, -1, -1);
    end;
  end;
end;



