/*
Добавление в буфер данных по СС ПФИ из excel
dv_loadpfibuf.mac
*/
import dl_activex, oralib, likepy, BankInter;

private const COL_DEALID    = 1,
              COL_VALUEDATE = 3,
              COL_CPTY      = 6,
              COL_SS        = 16;

//Заполняется буферная таблица данными из Excel
macro WriteBufSSPFI ()
  var DealID_Excel:string;
  var DealCode:string = "";
  var FullNameFile = "";
  var FileName = "";
  var TermPath = GetCurDir(true) + "\\TxtFile";
  var Userpath = "";
  var DirName = "";
  var ExtName = "";
  var frow = 1;
  var row = 1;
  var sheet, sql, query_insert, query_check, query_update;
  var valuedate:date =  Date(0, 0, 0);
  var ss = $0;
  var str;

  /*Выбираем файл для обработки*/
  if(not SelectFile(FullNameFile, "$C:\\*.xls*", "Выберите файл для загрузки", 0, true))
    MsgBox("Отказ от загрузки");
    return 0;
  end;

  //Отделяем путь файла от имени
  splitfile(FullNameFile, FileName, ExtName);
  FileName = FileName + ExtName;
  DirName = substr(FullNameFile, 1, index(FullNameFile, FileName) - 1);

  if (not copyfile("$" + DirName + FileName, "$" + TermPath + "\\" + FileName) )
    MsgBox("Ошибка при передаче файла на терминал");
    //msgbox(string("$" + DirName + FileName) +"|"+string(TermPath + "\\" + FileName));
    //return 0;
  end;

  /*Открываем файл*/
  BegAction(1, "Обработка файла \"" + TermPath + "\\" + FileName + "\". Ждите...");

  if(not OpenExcelFile(FileName, false, true, TermPath))
    MsgBox("Ошибка открытия файла \"" + TermPath + "\\" + FileName + "\"");
    EndAction(1);
    return 0;
  end;

  sheet = ExcelApplication.Sheets(1);

  /*Ищем первую строку*/
  while(frow <= 10)
    if((valtype(sheet.cells(frow, COL_DEALID).value) != V_UNDEF) and  ((strupr(sheet.cells(frow, COL_DEALID).value) == "DEAL ID")))
      break;
    end;
    frow = frow + 1;
  end;

  query_insert = "insert into user_sspfi_tmp (dealcode, valuedate, ss) "
               + "values (:dealcode, :dt, :ss)";

  query_check = "select ss from user_sspfi_tmp "
              + "where dealcode = :dealcode and valuedate = :dt";

  query_update = "update user_sspfi_tmp set SS = :ss "
               + "where dealcode = :dealcode and valuedate = :dt";

  str = sheet.cells(4, COL_VALUEDATE).value;
  //str = StrSubSt(str, "/", ".");
  valuedate = str;//всегда должна быть в этой колонке

  row = frow;
  //Бежим по файлу
  while (valtype(sheet.cells(row, COL_DEALID).value) != V_UNDEF)
    DealID_Excel = sheet.cells(row, COL_DEALID).value;
    ss = sheet.cells(row, COL_SS).value;
    if (    /*(StrUpr(sheet.cells(row, COL_CPTY).value) != "MICR")
         or */((Index(StrUpr(DealID_Excel), "VAL_IMP_FXS") == 0) and (Index(StrUpr(DealID_Excel), "VAL_IMP_FXO") == 0)) )
      row = row + 1;
      continue;       
    end;

    if ((Index(StrUpr(DealID_Excel), "VAL_IMP_FXS") != 0))
      DealCode = "SWP" + SubStr(DealID_Excel, index(DealID_Excel, ":"));
    elif ((Index(StrUpr(DealID_Excel), "VAL_IMP_FXO") != 0))
      DealCode = "FXO" + SubStr(DealID_Excel, index(DealID_Excel, ":"));
    else
      Dealcode = "";
    end;

    sql = ExecSqlSelect(query_check, MakeArray(SqlParam("dealcode", Dealcode),
                                               SqlParam("dt", valuedate)), false);

    if (sql.MoveNext())
      if (sql.value(0) != ss) 
        sql = ExecSql(query_update, MakeArray(SqlParam("ss",       sql.value(0)),
                                              SqlParam("dealcode", DealCode),
                                              SqlParam("dt",       valuedate)), false);
      end;
    else
      sql = ExecSql(query_insert, MakeArray(SqlParam("dealcode", DealCode),
                                            SqlParam("dt",       valuedate),
                                            SqlParam("ss",       ss)), false);
    end;

    if (sql == null)
      MsgBox("Ошибка при обработке файла " + FileName + " в строке " + row);
    end;

    row = row + 1;
  end;

  ExcelApplication.ActiveWorkbook.Close;

  if (not RemoveFile ("$" + TermPath + "\\" + FileName))
    MsgBox("Ошибка удаления файла " + TermPath + "\\" + FileName);
  end;

onerror()
  if (valtype(ExcelApplication) != V_UNDEF)
    ExcelApplication.ActiveWorkbook.Close;
  end;
  EndAction(1);
End;