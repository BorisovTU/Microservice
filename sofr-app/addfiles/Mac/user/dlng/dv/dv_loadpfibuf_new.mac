/*
$Name:        dv_loadpfibuf_new.mac
$Module:      Производные инструменты
$Description: Добавление в буфер данных по СС ПФИ из excel
*/
/*
Добавление в буфер данных по СС ПФИ из excel
dv_loadpfibuf.mac
*/
import dl_activex, oralib, likepy, BankInter, DVInter, globals, rsd,RsbDataSet, PTInter;
import dlmisc;                         /* SplitString*/
import "usr_open_files.mac";


private const COL_DEALID    = "A",
              COL_VALUEDATE = "C",
              COL_CPTY      = "F",
              COL_SS        = "T";

private macro iif(a,b,c)
    if(a)
        return b;
    else
        return c;
    end;
end;


class Logger()


    macro RepHeader(dt, tm)
       var str;
       str =     "                             ОТЧЕТ о выгрузке СС для сделок ПФИ \n";
       str = str+"\n";
       str = str+"Дата загрузки  : "+dt+"\n";
       str = str+"Время загрузки : "+tm+"\n";
       str = str+"┌─┬──────────────────┬──────────────────────────────┬──────────────────┬──────────────────┬───────────────┬─────────────────────────────────────────────────────────────────────────────────────────┐\n";
       str = str+"│ │Сделка            │Контрагент                    │Справ. стоимость  │Дата СС           │Статус загрузки│Комментарий                                                                              │\n";
       str = str+"├─┼──────────────────┼──────────────────────────────┼──────────────────┼──────────────────┼───────────────┼─────────────────────────────────────────────────────────────────────────────────────────┤";
       //toRep1(str);                                                                                                   
       println(str);                                                                                                   
    end;
                                                                                                                                                                     
    macro RepFooter(suc, er, tot)                                                       
       var str = "└─┴──────────────────┴──────────────────────────────┴──────────────────┴──────────────────┴───────────────┴─────────────────────────────────────────────────────────────────────────────────────────┘\n";
       str = str+"Загружено успешно: "+suc+"\n";               
       str = str+"Не загружено     : "+er+"\n";
       str = str+"Всего            : "+tot+"\n";
      println(str);
    end;

    macro RepStr(_err,_code, _ca, _ss,_datess,_stat,_comm)
        var status;
        if (_stat == false)
            status = "Загружена";
        else
            status = "Не загружена";
        end;
        var str = "│"+string(_err   :1)   +"│"+
                  string(_code    :18:l)+"│"+
                  string(_ca    :30:l)+"│"+
                  string(_ss      :18:l)+"│"+
                  string(_datess  :18:l)+"│"+
                  string(status    :15:l)+"│"+
                  string(_comm   ) ;
       return println(str);
    end;

end;


private macro GetDealID(dealcode, dockind):integer
  var sql,rs;
  var ret = -1;
  sql = "SELECT d.t_id as did, d.t_dockind FROM ddvndeal_dbt d WHERE d.t_code = '"+dealcode+"' AND d.t_state != 2";
  var cmd = RSDCommand(sql);
  cmd.execute();

  rs = TRsbDataSet(cmd);

  if (rs.MoveNext())
    ret = rs.did;
    dockind = rs.dockind;
  end;
  setparm(1, dockind);
  return ret;
end;

/*PNV*/
/*проверяем на бирж.сделку Т+3 - для нее СС не загружается, для нее вар.маржа из бирж.файла*/
private macro GetDealType(dealcode)
  var sql,rs;
  sql = "SELECT d.t_id as did FROM ddvndeal_dbt d WHERE d.t_code = '"+dealcode+"' AND d.t_state != 2 and T_PERIODCLS = 3 and t_sector = 'X' ";
  var cmd = RSDCommand(sql);
  cmd.execute();

  rs = TRsbDataSet(cmd);

  if (rs.MoveNext())
    return true;
  end;
  return false;
end;
/*PNV*/

private macro ContrAgName(did)
    var sql,rs;
    var ret = "";
    sql = "SELECT d.t_contractor as clnt FROM ddvndeal_dbt d WHERE d.t_id = "+did;
    var cmd = RSDCommand(sql);
    cmd.execute();
    rs = TRsbDataSet(cmd);
    if (rs.MoveNext())
        ret = GetPartyNames(rs.clnt, 2)[0];
  end;

  return ret;
end;

macro SaveFileToAppServ(FullName:string)
    var FileName = "";
    var DirName = "";
    var ExtName = "";
    var Path_AppServ = "";

    if(not isStandAlone()) 
      splitfile(FullName, FileName, ExtName);
      FileName = FileName + ExtName;
      DirName = substr(FullName, 1, index(FullName, FileName) - 1);
      Path_AppServ = "..\\txtfile\\" + FileName;

      if (not copyfile("$" + DirName + FileName, Path_AppServ) )
        msgbox("Ошибка при передаче файла на сервер приложений");
        return false;
      end;

      return Path_AppServ;
    else
      return FullName;
    end;
end;



private macro CheckFrValOnDate(did, frvdate, dockind):money
  var sql,rs;
  //var ret:money = 0; //нулевая СС тоже может быть
  var ret:money;

  sql = "SELECT f.t_fairvalue AS frval FROM ddvnfrval_dbt f WHERE f.t_dealid = "+did+"  AND f.t_date = to_date('"+frvdate+"','dd.mm.yyyy') AND f.t_dockind = " + dockind;
  var cmd = RSDCommand(sql);
  cmd.execute();
  rs = TRsbDataSet(cmd);


  if (rs.MoveNext())
    ret = money(rs.frval);
  end;

  return ret;
end;

/*PNV 491912*/
private macro CheckLastFrValOnDate(did, frvdate, dockind):money
  var sql,rs;
  var ret:money = 0;

  sql = "SELECT sum(f.t_fairvalue) AS frval FROM ddvnfrval_dbt f WHERE f.t_dealid = "+did+"  AND f.t_date < to_date('"+frvdate+"','dd.mm.yyyy') AND f.t_dockind = " + dockind;
  var cmd = RSDCommand(sql);
  cmd.execute();
  rs = TRsbDataSet(cmd);


  if (rs.MoveNext())
    ret = money(rs.frval);
  end;

  return ret;
end;
/*PNV 491912*/

private macro InsertSS(did,frval,valdate,fname,ldate,uid,status,comment,row):bool
  var sql,st;
  
  if (status)
    st = 0;
  else
    st = 1;
  end;

  var query_insert = "insert into ddvnloadfrval_dbt (t_id, T_DEALID, T_FRVAL, T_DATEFRVAL, T_FILENAME, T_FILEDATE, T_LOADUSERID, T_STATUS, T_COMMENT) " +
                     "values (0, "+did+", "+frval+", to_date('"+valdate+"','dd.mm.yyyy'), '"+fname+"', to_date('"+ldate+"','dd.mm.yyyy'), "+uid+", "+st+", '"+comment+"') ";
  var cmd = RSDCommand(query_insert);
  cmd.execute();

  return true;
end;


 macro OptionCorrectSumSS(id, ss, dockind):money
    var sql,rs,cnt;
    var type = 0,bns = $0;
    var ret = $0;
    var cmd;

    sql = "SELECT count(f.t_id) as cnt FROM ddvnfrval_dbt f WHERE f.t_dockind = " + dockind + " AND f.t_dealid = "+id;
    cmd = RSDCommand(sql);
    cmd.execute();
    rs = TRsbDataSet(cmd);    
    if (rs.MoveNext())
        cnt = rs.cnt;
    end;

    if (cnt == 0) //Первоначальный ввод СС по опциону
        sql = "SELECT d.t_type as type,d.t_bonus as bonus FROM ddvndeal_dbt d WHERE d.t_id ="+id;
        cmd = RSDCommand(sql);
        cmd.execute();
        rs = TRsbDataSet(cmd);

        if (rs.MoveNext())
            type = rs.type;
            bns = rs.bonus;
        end;

        if (type == 1) //Покупка
            if (ss > bns)
                ret = ss - bns;
            elif (ss < bns)
                ret = bns - ss;
            end;
        elif (type == 2) //Продажа
            if ( abs(ss) > bns )
                ret = (abs(ss) - bns);
            elif ( abs(ss) < bns )
                ret = (bns - abs(ss));
            end;
        end;
    else
        ret = ss;
    end;

    return ret;
end;

private macro UpdOptionFrVal(dealid, fdate, ss, dockind)
    var query = "UPDATE ddvnfrval_dbt d SET d.t_fairvalue = "+ss+" WHERE d.t_dealid ="+dealid+" AND d.t_date = to_date('"+fdate+"','dd.mm.yyyy') AND d.t_dockind = " + dockind;
    var cmd = RSDCommand(query);
    cmd.execute();
end;

private macro GetValueDateFromFile(p_date)
  var yy, mm,dd;
  var pos = 0, t;
  var res = null;
  pos = Index(p_date, "-");
  if (pos > 0)
    yy = Int(substr(p_date,1,pos-1));
    t = substr(p_date, pos+1);
    pos = Index(t,"-");
    if (pos > 0)
      mm = Int(substr(t,1,pos-1));
      dd = Int(substr(t,pos+1));
    end;
    res = date(dd,mm,yy);
  end;
  return res;
end;


//Заполняется буферная таблица данными из Excel
macro ServOpSSPFI()
  var DealID_Excel:string;
  var DealCode:string = "";
  var FullNameFile = "";
  var FileName = "";
  var TermPath = GetCurDir(true) + "\\TxtFile";
  var Userpath = "";
  var DirName = "";
  var ExtName = "";
  var frow = 1;
  var row = 1;
  var sheet, sql, query_insert, query_check, query_update;
  var valuedate:date =  Date(0, 0, 0);
  var ss = $0;
  var str;
  var DealID, FrVal, dockind = 0;
  var day = date();
  var ErrStr = "",ErrStat = false,IsOption = false;
  var all = 0,suc = 0, err = 0;
  var log = Logger();
  var dt = StrSubSt(string(Date()),".","");
  var InfoLogFileName = GetTxtFileName( "load_ss_"+dt);

  file datafile() txt;
  SetDelim(";");

/*  /*Выбираем файл для обработки*/
  if(not SelectFile(FullNameFile, "$C:\\*.xls*", "Выберите файл для загрузки", 0, true))
    MsgBox("Отказ от загрузки");
    return 0;
  end;
*/
   // BIQ-7791 Гераськина Т.В. загрузка СС из *.csv
   private var v_choice;
   private var v_acc_trn_menu = TArray;
   private var cur_file_mask = "";
   private var v_state = false;
   private var FS, file_line, file_line_fields;
   private var valuedate_str;
   private var FullNameFile_copy;

   v_acc_trn_menu.value(0) = "Загрузка СС из файла формата *.csv";
   v_acc_trn_menu.value(1) = "Загрузка СС из файла формата *.xls*";
 
   v_choice = menu(v_acc_trn_menu, "Выберите вариант загрузки", "Выберите вариант загрузки", null, null, 0);
   if(v_choice == 0)
     cur_file_mask = "*.csv";
   elif (v_choice == 1 )
     cur_file_mask = "*.xls*";
   else
      msgbox("Выбор не подтвержден");
      return 0;
   end;
   //~ BIQ-7791

   var PATH_LOAD;
   var REG_PATH_LOAD = "РСХБ\\ДИРЕКТОРИИ\\ЗАГРУЗКА_СПРАВЕДЛИВОЙ_СТОИМОСТИ";
   var ErrCode;
   GetRegistryValue(REG_PATH_LOAD, V_STRING, PATH_LOAD, ErrCode);
   if( ( ErrCode > 0 ) or (not PATH_LOAD) )
      PATH_LOAD = "..\\TxtFile\\";
   end;

  if (v_choice == 1)
    /*PNV i-support 488322 IM2845619*/
    /*Выбираем файл для обработки*/
    if(not SelectFile(FullNameFile, PATH_LOAD+cur_file_mask, "Выберите файл для загрузки", 0, true))
      MsgBox("Отказ от загрузки");
      return 0;
    end;
    /*PNV*/

    //Отделяем путь файла от имени
    splitfile(FullNameFile, FileName, ExtName);
    FileName = FileName + ExtName;

    DirName = substr(FullNameFile, 1, index(FullNameFile, FileName) - 1);

    if (not copyfile("$" + DirName + FileName, "$" + TermPath + "\\" + FileName) )
      MsgBox("Ошибка при передаче файла на терминал");
    end;

    if (not copyfile("$" + DirName + FileName, "$" + TermPath + "\\" + FileName) )
      MsgBox("Ошибка при передаче файла на СП");
    end;

    /*Открываем файл*/
    BegAction(1, "Обработка файла \"" + TermPath + "\\" + FileName + "\". Ждите...");
  elif (v_choice == 0)
    var c_file = c_txt_file();
    var stat;

    stat = c_file.openFile(datafile, PATH_LOAD+cur_file_mask, true, false, FullNameFile, "rsansi");

    if ( (valtype(stat) == V_UNDEF) or (stat == false) )
      msgbox("Ошибка открытия файла");
      return 0;
    end;

    /*Открываем файл*/
    BegAction(1, "Обработка файла \"" + c_file.localFileName + "\". Ждите...");
  end;

  if (v_choice == 1)  // BIQ-7791 Excel сохранен 
    var jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
    var rc = jvm.callStaticMethod("ru.softlab.rsbank.poireports.ReportCreator", "getInstance");
    FullNameFile = SaveFileToAppServ(FullNameFile);
    var ActiveBook = rc.openWorkbook(FullNameFile);
    if(not ActiveBook)
      ActiveBook.close();
      ActiveBook = null;
      rc = null;
      MsgBox("Ошибка открытия файла \"" + TermPath + "\\" + FileName + "\"");
      EndAction(1);
      return 0;
    end;

    ActiveBook.setActiveSheet(0);

    SetOutput( InfoLogFileName, true ); //Пишем лог в файл. Начало
    log.RepHeader(date(),time());
    /*Ищем первую строку*/
    while(frow <= 10)
      if((valtype(ActiveBook.getCellValue(COL_DEALID + frow)) != V_UNDEF) and  ((strupr(ActiveBook.getCellValue(COL_DEALID + frow)) == "DEAL ID")))
        break;
      end;
      frow = frow + 1;
    end;

    str = ActiveBook.getCellValue(COL_VALUEDATE + 4);
    //TEG 04.03.19
    if (ValType(str) == V_UNDEF)
      MsgBoxEx("В файле не указана дата установки СС. Продолжение операции невозможно.");
      if (valtype(ActiveBook) != V_UNDEF)
        ActiveBook.close();
        ActiveBook = null;
      end;
      EndAction(1);
      return 0;
    end;
    valuedate = Date(str);//всегда должна быть в этой колонке
    v_state = true;
  elif (v_choice == 0) // BIQ-7791 *.csv 
    /* формат файла, первая строка - всегда заголовок, данные со второй строки
    DealId;Cpty;Currency;TradeDate;ValueDate;MarketValue;
    "C_and_F:80A";"UBMS";"RUB";"2021-08-26";"2021-08-26";158.25;*/

    next(datafile);
    if ( (StrUpr(datafile(0)) == StrUpr("DealId")) and
         (StrUpr(datafile(4)) == StrUpr("FairValueDate")) and
         (StrUpr(datafile(5)) == StrUpr("MarketValue")) )
    else 
      MsgBox("Файл имеет некорректный заголовок");
      EndAction(1);
      return 0;
    end;
    SetOutput( InfoLogFileName, true ); //Пишем лог в файл. Начало
    log.RepHeader(date(),time());

    file_line = "";
    file_line_fields = null;
    v_state = true;
  end;

  row = frow;
  //Бежим по файлу
  InitProgress(-1);
  while (v_state) 
    if (v_choice == 1)
      if (valtype(ActiveBook.getCellValue(COL_DEALID+row)) == V_UNDEF)
         v_state = false;
         continue;
      else
         DealID_Excel = ActiveBook.getCellValue(COL_DEALID+row);
         ss = ActiveBook.getCellValue(COL_SS+row);
         IsOption = false;
         UseProgress(row);

         /*Simanov. формат первой колонки нужной нам строки имеет вид VAL_XXX_TTT:DDDDD.
         где ХХХ - непостоянные значения и не понятно, от чего они зависят;
         T - тип сделки;
         D - номер сделки.
         Если строка сразу не вписывается в данный формат, то идём к следующей.
         Если же строку нужно обрабатывать, то преобразуем её из VAL_XXX_TTT:DDDDD в TTT:DDDDD. Результат - код сделки в СОФР.
         */

         if (StrUpr(SubStr(DealID_Excel, 1, 3)) != "VAL") 
           Dealcode = "";
           row = row + 1;
           continue; 
         end;

         DealID_Excel = SubStr(DealID_Excel, index(DealID_Excel, "_")+1 );
         dealcode = SubStr(DealID_Excel, index(DealID_Excel, "_")+1 );
         
         if ((Index(StrUpr(dealcode), "FXS") != 0))
           Dealcode = StrSubSt(Dealcode, "FXS", "SWP" );
         end;

         all = all + 1;
         ErrStr = "";
         ErrStat = false;
      end;
    elif (v_choice == 0)
      v_state = next(datafile);
      if (v_state)
         if (StrLen(trim(datafile.str)) == 0)
            continue;
         end;
         Dealcode  = datafile(0); // заявлено, что будете передаваться номер сделки без необходимости преобразований
         Dealcode = StrSubst(Dealcode,"\"","");
         valuedate_str = datafile(4);
         valuedate_str = StrSubst(valuedate_str,"\"","");
         ss = datafile(5);
         ss = StrSubst(ss,"\"","");
         valuedate_str = GetValueDateFromFile(valuedate_str);

         IsOption = false;
         UseProgress(row);
         all = all + 1;
         ErrStr = "";
         ErrStat = false;
      else
         continue;
      end;
    end;

    if (v_choice == 0) 
       if (ValType(valuedate_str) == V_UNDEF)
          ErrStat = true;
          ErrStr = Dealcode+". Некорректная дата в 5 позиции";
          log.RepStr("X",dealcode,"NONE", ss, valuedate, ErrStat, ErrStr);
          row = row + 1;
          err = err + 1;
          continue;
       else 
          valuedate = valuedate_str;
       end;
    end;

    DealID = GetDealID(Dealcode, @dockind);

    /*PNV*/
    /*проверяем на бирж.сделку Т+3 - для нее СС не загружается, для нее вар.маржа из бирж.файла*/
    if (DealID != -1) 
      if (GetDealType(Dealcode))/*биржевая Т+3*/
        DealID = -1; /*не находим*/
      end;
    end;
    /*PNV*/

    if (DealID == -1)
      ErrStat = true;
      ErrStr = "Сделка с кодом "+Dealcode+" не найдена в СОФР или имеет статус закрыта!";
      log.RepStr("X",dealcode,"NONE", ss, valuedate, ErrStat, ErrStr);
      row = row + 1;
      err = err + 1;
      continue;
    end;

    FrVal = NULL;
    FrVal = CheckFrValOnDate(DealID, valuedate, dockind);

    //if (FrVal != 0) // нулевая СС тоже может быть
    if (ValType(FrVal) != V_UNDEF) // если СС найдена
      ErrStat = true;
      ErrStr = "У cделки "+Dealcode+" на "+valuedate+" СС = "+FrVal+" . СС из файла = "+Round(ss,2);
      InsertSS(DealID,ss,valuedate,FileName,day,{oper},ErrStat,ErrStr,row);
      log.RepStr("X",dealcode, ContrAgName(DealID), ss, valuedate, ErrStat, ErrStr);
      row = row + 1;
      err = err + 1;
      continue;
    end;

    /*PNV 491912*/
/*    if (Round(ss,2) == 0)
        FrVal = CheckLastFrValOnDate(DealID, valuedate, dockind);
        if (FrVal != 0)
           ss = -1*FrVal;
        end;  
    end;*/
    /*PNV 491912*/

    if (DV_InsertFairValue(DealID, dockind, valuedate, Money(Round(ss, 2)) ,0 ,true, ErrStr) > 0) /*PNV округляем до 2-х знаков в файле больше*/
      ErrStat = true;
      ErrStr = "Ошибка вставки СС "+ErrStr+" по сделке "+Dealcode+" в строке "+row;
      InsertSS(DealID,ss,valuedate,FileName,day,{oper},ErrStat,ErrStr,row);
      log.RepStr("X",dealcode, ContrAgName(DealID), ss, valuedate, ErrStat, ErrStr);
      row = row + 1;
      err = err + 1;
      continue;
    else
      InsertSS(DealID,ss,valuedate,FileName,day,{oper},ErrStat,ErrStr,row);
      log.RepStr("",dealcode, ContrAgName(DealID), ss, valuedate, ErrStat, ErrStr);
      suc = suc + 1;
    end;

    //Постобработка для опциона, для правильной суммы СС
    if (IsOption)
      UpdOptionFrVal(DealID, valuedate, ss, dockind);
    end;
    row = row + 1;
  end;
  RemProgress();

  log.RepFooter(suc,err,all);
  SetOutput( NULL, true ); //Пишем лог в файл. Конец
  if (v_choice == 1)
    ActiveBook.close();
    ActiveBook = null;
  end;
  
  FS = NULL;
  if (v_choice == 1)
    if (not RemoveFile ("$" + TermPath + "\\" + FileName))
      MsgBox("Ошибка удаления файла " + TermPath + "\\" + FileName);
    end;
  elif (v_choice == 0)
    c_file.closeFile(datafile);
  end;
  
  ViewFile( InfoLogFileName );

onerror()
  if (valtype(ActiveBook) != V_UNDEF)
    ActiveBook.close();
    ActiveBook = null;
  end;
  FS = NULL;
  EndAction(1);
End;

/*
macro RunInsFrVal()
    var rs;
    var query = "SELECT l.t_dealid as id,l.t_correctss as val,l.t_datefrval as dt FROM DDVNLOADFRVAL_DBT l WHERE l.t_status = 0";
    var cmd = RSDCommand(query);
    cmd.execute();

    rs = TRsbDataSet(cmd);
    while( rs.MoveNext() )
        if ( DV_InsertFairValue(rs.id,date(rs.dt),money(rs.val),0) == false )
            MsgBox("Not");
        else
            MsgBox("Ok");
        end;
    end;


end;*/
