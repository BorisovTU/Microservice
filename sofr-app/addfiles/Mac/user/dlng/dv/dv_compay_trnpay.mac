import "dv_compay_utils.mac";

class (DVComPayScrollBase) DVComPayScrollTrnList(_calcId:integer, _sfContrId:integer)
  InitDVComPayScrollBase();
  var calcId = _calcId;
  var sfContrId = IIF(ValType(_sfContrId) != V_INTEGER, null, _sfContrId);

  var mSelectArr = TArray();

  var dealsCount = 0;

  private macro CreateInner(FD, Amount, Fiid, boTrnId)
    var createInner = false;
    var DebAccBY = TRecHandler("account");
    var KredAccBY = TRecHandler("account");
    var GroundBY;
    var oprDate = DVComPayGetTrnDate(boTrnId);
    var ClientName = "";
    var trnId = 0;
    if( ПИ_ВестиВнутреннийУчет() )
      //GroundBY = FD.ВУ_ОснованиеПроводки(8, false);
      var Client = TBfile("party.dbt");
      if(ПолучитьСубъекта(FD.Deal.rec.Client, Client) == 0)
        ClientName = client.rec.ShortName;
      end;

      GroundBY = "Оплата комиссии по сделке "  + IIF(FD.Kind==DL_DVFXDEAL,"","СВОП ") + "N " + FD.DealCode() + 
      " в рамках Соглашения об оказании брокерских услуг N " + FD.ClientDBOContrName() + " от " + FD.ClientDBOContrDate() + ". Клиент - " + ClientName;

      if( not FD.OpenAccount(FD.ВУ_ПолучитьКатегориюДебет(8, false), null, null, FD.ВУ_ПолучитьРольФИ(8, true, false), DebAccBY, oprDate, Fiid) )
         RunError("Ошибка получения счета ВУ");
      end;
      if( not FD.OpenAccount(FD.ВУ_ПолучитьКатегориюКредит(8, false), null, null, FD.ВУ_ПолучитьРольФИ(8, true, false), KredAccBY, oprDate, Fiid) ) 
         RunError("Ошибка получения счета ВУ");
      end;

      if (not DVComPayCheckCarryExists (DebAccBY.rec.account, KredAccBY.rec.account, Amount, Fiid))
        createInner = getTrue(true,"По клиенту с договором "+FD.ClientDBOContrName()+" не найдена соответствующая проводка ВУ. Создавать проводки ВУ по оплате комиссии?");
      end;

      if (createInner)
        if( (not DVComPayCarry( FD, 
                                 DebAccBY, KredAccBY,
                                 oprDate,
                                 FD.ВУ_ОпределениеГлавы(Fiid),
                                 Fiid,
                                 Amount,
                                 null, 
                                 INPCARRY,
                                 "",
                                 "",
                                 GroundBY,
                                 0, null, null, @trnId,
                                 Fiid, Fiid,
                                 null, null, null, null,
                                 true,
                                 8
                               ))
          )
          RunError("Ошибка выполнения проводки ВУ");
        end;
      end;
    end;
  end;

  macro MakePayments(AccTrnId)
    if (AccTrnId <= 0)
      return;
    end;
    var count = 0;
    if(RslDefCon.IsInTrans)
      RslDefCon.RollbackTrans();
    end;
    RslDefCon.BeginTrans();
    var FD;
    var totalAmount = $0;
    var cmd = DL_RSDCommand(
      "SELECT q4.t_date AS t_dealdate, "
     +"       q4.t_code AS t_dealcode, "
     +"       q4.T_CLIENTID, "
     +"       q4.T_DOCKIND, "
     +"       q4.t_dealid, "
     +"       q4.t_toPaySum, "
     +"       q4.T_FIID "
     +"  FROM (SELECT q3.*, "
     +"               CASE "
     +"                  WHEN q3.t_RestSum >= 0 THEN t_reqSum "
     +"                  ELSE q3.t_PrevRest "
     +"               END "
     +"                  t_toPaySum "
     +"          FROM (SELECT q2.*, "
     +"                       NVL (LAG (t_RestSum) OVER (ORDER BY T_DATE, T_DEALID), "
     +"                            t_InpPaySum) "
     +"                          t_PrevRest "
     +"                  FROM (SELECT q1.*, "
     +"                               q1.t_InpPaySum - q1.t_IncReqSum AS t_RestSum "
     +"                          FROM (  SELECT NDEAL.T_CODE, "
     +"                                         COMPAY.T_REQSUM, "
     +"                                         NDEAL.T_DATE, "
     +"                                         COMPAY.T_CLIENTID, "
     +"                                         ndeal.T_ID AS t_dealid, "
     +"                                         NDEAL.T_DOCKIND, "
     +"                                         COMPAY.T_FIID, "
     +"                                         tmp.T_AMOUNT AS t_InpPaySum, "
     +"                                         SUM ( "
     +"                                            COMPAY.T_REQSUM) "
     +"                                         OVER ( "
     +"                                            ORDER BY NDEAL.T_DATE, NDEAL.T_ID "
     +"                                            ROWS BETWEEN UNBOUNDED PRECEDING "
     +"                                            AND     CURRENT ROW) "
     +"                                            AS t_IncReqSum "
     +"                                    FROM U_COMPAY_TR_TMP tmp, "
     +"                                         U_COMPAY_DBT compay, "
     +"                                         ddvndeal_dbt ndeal "
     +"                                   WHERE tmp.T_ACCTRNID = ? "
     +"                                     AND COMPAY.T_DEALID = TMP.T_DEALID "
     +"                                     AND compay.t_calcid = ? "
     +"                                     AND NDEAL.T_ID = COMPAY.T_DEALID "
     +"                                ORDER BY NDEAL.T_DATE, NDEAL.T_ID) q1) q2) q3) q4 "
     +" WHERE q4.t_topaysum > 0 "
    );
    cmd.AddParam(AccTrnId);
    cmd.AddParam(calcId);
    var DataSet = cmd.Execute();
    SetDialogFlag(0);
    while (DataSet.MoveNext())
      FD = DVComPayCreatePaymentAndTrnAndGetFD(
        SQL_ConvTypeDate(DataSet.DEALDATE),
        SQL_ConvTypeStr(DataSet.DEALCODE),
        SQL_ConvTypeInteger(DataSet.CLIENTID),
        SQL_ConvTypeInteger(DataSet.DocKind),
        SQL_ConvTypeInteger(DataSet.DealID),
        SQL_ConvTypeSum(DataSet.ToPaySum),
        SQL_ConvTypeInteger(DataSet.Fiid),
        SQL_ConvTypeInteger(AccTrnId)
      );
      totalAmount = totalAmount + SQL_ConvTypeSum(DataSet.ToPaySum);
      count = count + 1;
    end;
    //выключаем диалоговый режим перед созданием внутренней проводки только не в массовом режиме
    if (mSelectArr.size == 0)
      SetDialogFlag(1);
    end;
    CreateInner(FD, totalAmount, 0, AccTrnId);
    if (mSelectArr.size > 0) //значит мы в массом режиме
      dealsCount = dealsCount + count;
    else
      MsgBox("Выполнена оплата комиссий по " + count + " сделке/ам");
    end;
    RslDefCon.CommitTrans();
    SetDialogFlag(1);
    return true;
  OnError( err )
    SetDialogFlag(1);
    if(RslDefCon.IsInTrans)
      RslDefCon.RollbackTrans();
    end;
    DVComPayShowError(err);
    return false;
  end;

  macro EvProc(rs, cmd, id, key)
    /*Обработчик*/
    if (cmd == DLG_INIT)
      AddMultiAction(rs, 316);
    elif((cmd == DLG_MSELSTART) and (key == 316))
      mSelectArr = TArray();
      dealsCount = 0;
      BegAction(2000, "Создание платежей по проводкам", false);
    elif((cmd == DLG_MSELEND) and (key == 316))
      for (var elem, mSelectArr)
        MakePayments(elem);
      end;
      EndAction();
      MsgBox("Выполнена оплата комиссий по " + dealsCount + " сделке/ам");
    elif((cmd == DLG_MSEL) and (key == 316))
      mSelectArr[mSelectArr.size] = SQL_ConvTypeInteger(rs.value("T_ACCTRNID"));
      return CM_MSEL_CONT_CLEAR;
    elif(cmd == DLG_KEY)
      /*Esc*/
      if(key == 27)
        return CM_CANCEL;
      elif (key == 0) //в конце mselect выбрасывается такое сообщение, поэтому перестроим скроллинг
        return CM_SELECT;
      /*F2*/
      elif(key == 316)
        var accTrnId = SQL_ConvTypeInteger(rs.value("T_ACCTRNID"));
        if (accTrnId > 0)
          MakePayments(accTrnId);
          updatescroll(rs, 5);
          return CM_SELECT;
        end;
      end;
    end;
  end;

  macro BuildSQL()
    return 
      "  SELECT T_DEALDATE, "
     +"         T_DEALCODE, "
     +"         T_ACCTRNID, "
     +"         T_CLIENTID, "
     +"         T_ACCOUNT_PAYER, "
     +"         T_ACCOUNT_RECEIVER, "
     +"         T_FIID, "
     +"         T_AMOUNT, "
     +"         T_GROUND, "
     +"         T_FIIDSTR "
     +"    FROM (SELECT com.*, "
     +"                 (SELECT fin.T_CCY "
     +"                    FROM DFININSTR_DBT fin "
     +"                   WHERE fin.t_fiid = com.t_fiid) "
     +"                    AS t_fiidStr "
     +"            FROM U_COMPAY_TR_TMP com) q1 "
     +"GROUP BY T_DEALDATE, "
     +"         T_DEALCODE, "
     +"         T_ACCTRNID, "
     +"         T_CLIENTID, "
     +"         T_ACCOUNT_PAYER, "
     +"         T_ACCOUNT_RECEIVER, "
     +"         T_FIID, "
     +"         T_AMOUNT, "
     +"         T_GROUND, "
     +"         T_FIIDSTR ";
  end;

  macro BuildRS()
    execSql ("delete from U_COMPAY_TR_TMP");
    var query =
      "INSERT INTO U_COMPAY_TR_TMP (T_ACCTRNID, "
     +"                             T_DEALID, "
     +"                             T_DEALDATE, "
     +"                             T_DEALCODE, "
     +"                             T_CLIENTID, "
     +"                             T_ACCOUNT_PAYER, "
     +"                             T_ACCOUNT_RECEIVER, "
     +"                             T_AMOUNT, "
     +"                             T_FIID, "
     +"                             T_GROUND) "
     +"   SELECT DISTINCT TR.T_ACCTRNID, "
     +"                   COMPAY.T_DEALID, "
     +"                   TR.T_DATE_CARRY, "
     +"                   tr.T_NUMB_DOCUMENT, "
     +"                   COMPAY.T_CLIENTID, "
     +"                   tr.t_account_payer, "
     +"                   TR.T_ACCOUNT_RECEIVER, "
     +"                   TR.T_SUM_PAYER, "
     +"                   TR.T_FIID_PAYER, "
     +"                   TR.T_GROUND "
     +"     FROM dacctrn_dbt tr, U_COMPAY_DBT compay "
     +"    WHERE     (tr.t_account_payer = COMPAY.T_ACC306 "
     +"               OR tr.t_account_payer = COMPAY.T_ACC306_OUTMARKET) "
     +"          AND tr.t_account_receiver like '458%' "
     +"          AND COMPAY.T_CALCID = ? "
     +"          AND COMPAY.T_SFCONTRID = " + IIF(sfContrId != null, String(sfContrId), "COMPAY.T_SFCONTRID")
     +"          AND t_date_carry >= compay.T_BEGDATE "
     +"          AND (t_ground LIKE 'Оплата комиссии по сделк%' "
     +"               OR t_ground LIKE "
     +"                     'Оплаты комиссии по сделк%') "
     +"          AND NOT EXISTS "
     +"                 (SELECT 1 "
     +"                    FROM doprdocs_dbt oprdocs "
     +"                   WHERE oprdocs.t_acctrnid = tr.t_acctrnid) "
     +"          AND NOT EXISTS "
     +"                 (SELECT 1 "
     +"                    FROM U_COMPAY_TR_DBT comTrn "
     +"                   WHERE comTrn.t_acctrnid = tr.t_acctrnid) ";
    BegAction(2000, "Построение проводок без платежей", false);
    execSql(query, makeArray(SQLParam("calcId", calcId)), true);
    EndAction();

    rs = execSQLselect(BuildSQL(), TArray(), true, RSDVAL_CLIENT, RSDVAL_STATIC);
    return rs;
  end;

  macro ConstructAndShowScroll()
    AddColumn(0, "t_dealcode", "Номер проводки", 15, 15);
    AddColumn(1, "t_dealdate", "Дата проводки", 15, 10);
    AddColumn(2, "t_account_payer", "Дебет", 16, 20);
    AddColumn(3, "t_account_receiver", "Кредит", 16, 20);
    AddColumn(4, "t_amount", "Сумма", 10, 10);
    AddColumn(5, "t_fiidStr", "Валюта", 5, 5);
    AddColumn(6, "t_ground", "Основание", 100, 100);

    var res = true;
    while(res)
      res = RunScroll (BuildRS(), NumColumns, Columns, "DVCPTRSCRL", R2M(this,"EvProc"), "Проводки по оплате комиссий без платежей", 
         "F2 - создать платежи, Esc - выход", true, -1, -1);
    end;
  end;

  ConstructAndShowScroll();
end;



