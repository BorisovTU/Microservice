import InsCarryDoc, "dv_categ.mac", "dv_car.mac";

PRIVATE MACRO IsDealReady( FD )
   var RetVal = false;
   var Query, Data, Sql;

   Sql = " SELECT count(1) as CountStep " +
         "   FROM doprstep_dbt step, doproper_dbt oper " +
         "  WHERE oper.t_DocKind      = ? AND " +
         "        oper.t_DocumentID   = ? AND " +
         "        step.t_ID_Operation = oper.t_ID_Operation AND " +
         "        step.t_IsExecute   != chr(88) ";

   Query = RSDCommand( Sql );
   Query.addParam("", RSDBP_IN, FD.Kind );
   Query.addParam("", RSDBP_IN, UniID(FD.Deal, OBJTYPE_OUTOPER_DV) );
   Query.execute();

   Data = TRsbDataSet(Query);
   if( Data.MoveNext() and (SQL_ConvTypeInteger(Data.CountStep) == 1) )
      RetVal = true;
   end;

   return RetVal;
END;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )
  
  record ndeal(dvndeal);
  var FD;

  SetBuff( ndeal, FDoc );
  FD = DVFirstDocNDeal(DocKind, ndeal);

  var ДатаИсполненияШага:date;
  ДатаИсполненияШага = FD.ДатаИсполнения();


  if( IsDealReady(FD) == false )
    MsgBox( "Шаг не может быть выполнен. Остальные шаги операции должны иметь статуc \"Выполнен\"." );
    return 1;
  end;

  if (ФинРезПФИ( FD, Doc, ДатаИсполненияШага ) != 0)
    return 1;
  end;

  if( not InsertOprStatus(FD.DV_OPERSTATUS_DOC(), DV_OPERSTATUS_DOC_CLOSE) )
    MsgBox("Ошибка при установке статуса <Документооборот> по операции.");
    return 1;
  end;

  return 0;
end;
