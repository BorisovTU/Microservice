/*
Макрос корректировки промежуточных платежей процентных свопов
*/
import dvsw250, globals, oralib, likepy, or_rep_h;

//Номера колонок в исходном файле
const IRS         = 0,
      FLOWTYPE    = 1,
      PAYMENTDATE = 7,
      CASHFLOW    = 10,
      RATE        = 11,
      NUM         = 9,/*PNV 536327*/
      PeriodEndDate = 6;/*PNV 536327*/

private var errors = TArray();
private var Data;

private macro AddErr (err:string)
  errors[errors.size] = err;
  return false;
End;

//-------------------------------------------------------------------
//Класс для формирования протокола выполнения
class CorrectPP_Rep ()
  private var Rep = CMakeReport();

  macro PrintHead ()
    Rep.AddPrintCell("Протокол выполнения операции корректировки промежуточных платежей", 100, 0, "c:ex_FS(b)", REP_ELEM_STR ); Rep.AddStr();
    Rep.AddEmptyStr();
    Rep.AddPrintCell("Дата запуска: "+{curdate}, 100, 0, "l:ex_FS(b)", REP_ELEM_STR ); Rep.AddStr();
    Rep.AddPrintCell("Опрационист: "+{Name_Oper}, 100, 0, "l:ex_FS(b)", REP_ELEM_STR ); Rep.AddStr();
    Rep.AddEmptyStr();

    if (Data.DataArr.Size > 0)
      Rep.AddPrintCell( "Номер сделки", 10, 0, "c:", REP_ELEM_TABL );
      Rep.AddPrintCell( "Данные по промежуточным платежам", 110, 0, "c:", REP_ELEM_TABL );
      Rep.AddPrintCell( "Результат", 20, 0, "c:", REP_ELEM_TABL );
      Rep.AddStr();
    end;
  End;

  macro PrintLine (DealCode, definition, result)
    Rep.AddPrintCell(DealCode,    10, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell(definition,  110, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell(result,      20, 0, "c:", REP_ELEM_TABL );
    Rep.AddStr();
  End;

  macro ShowErrors ()
    var i = 0;

    if (errors.size > 0) 
      Rep.AddEmptyStr();
      Rep.AddPrintCell("Ошибки при выполнении", 100, 0, "l:ex_FS(b)", REP_ELEM_STR );
      Rep.AddStr();
    end;

    while (i < errors.size)
      Rep.AddPrintCell(i+1, 3, 0, "l:ex_FS(b)", REP_ELEM_TABL);
      Rep.AddPrintCell(errors[i], 100, 0, "l:ex_FS(b)", REP_ELEM_TABL);
      Rep.AddStr();
      i = i + 1;
    end;
  End;

  macro ShowRep (count)
    if (Data.DataArr.size > 0)
      Rep.AddEmptyStr();
      Rep.AddPrintCell("Успешно обработано: "+count, 100, 0, "l:ex_FS(b)", REP_ELEM_STR );
      Rep.AddStr();
    end;

    ShowErrors();
    Rep.PrintRep;
  End;
END;

//-------------------------------------------------------------------
//Класс для хранения данных по одной сделке
private class IRS_DATA ()
  var DealID:integer;
  var DealCode:string;
  var Department:integer;
  var OperDate:Date;
  var Parms = TArray();
END;

//Класс с данными по промежуточным платежам из файла
private class CollectData ()
  var DataArr = TArray();

  private macro AddNew (OperDate, Department, DealCode, DealID, Parm)
    var arrsize = DataArr.Size;
    DataArr[arrsize] = IRS_DATA();
    DataArr[arrsize].DealID = DealID;
    DataArr[arrsize].DealCode = DealCode;
    DataArr[arrsize].OperDate = OperDate;
    DataArr[arrsize].Department = Department;
    DataArr[arrsize].Parms[DataArr[arrsize].Parms.size] = Parm;
  End;

  private macro FindDeal (DealID, index:@integer)
    var i = 0;
    while (i < DataArr.Size)
      if (DealID == DataArr[i].DealID)
        index = i;
        return true;
      end;
      i = i + 1;
    end;
    return false;
  End;

  private macro AddParm (index, parm)
    DataArr[index].Parms[DataArr[index].Parms.size] = Parm;
  End;

  macro AddRec (OperDate, Department, DealCode, DealID, PaymID, Sum:money, Rate, Side)
    var index;
    var parm = OverValParm();
    parm.id = PaymID;
    parm.sum = Abs(Sum);
    parm.rate = Rate;
    parm.side = Side;

    if (FindDeal(DealID, @index))
      AddParm(index, parm);
    else
      AddNew(OperDate, Department, DealCode, DealID, parm);
    end;
  End;
END;

//-------------------------------------------------------------------
//Конвретирует дату из формата 2019-04-30T00:00:00.000
private macro ConvertDate (xmldate:string):Date
  var year:integer, mm:integer, dd:integer;
  year = SubStr(xmldate, 1, 4);
  mm = SubStr(xmldate, 6, 2);
  dd = SubStr(xmldate, 9, 2);

  return Date(dd, mm, year);
End;

//Возвращает ИД дочернего элемента по наименованию
private macro GetChildNoteID (xml, ChildNoteName):integer
  var i = 0;
  ChildNoteName = StrLwr(ChildNoteName);

  while (i < xml.childNodes.Length)
    if (StrLwr(xml.childNodes(i).nodename) == ChildNoteName);
      return i;
    end;
    i = i + 1;
  end;
  return -1;
End;

//Возвращает ИД сделки. в NettingPMGR записывает признак неттинга промежуточных платежей
private macro GetDealID (DealCode, NettingPMGR:@bool):integer
  var DealID = 0;
  var sql = ExecSqlSelect("select t_id, t_netting_pmgr from ddvndeal_dbt where t_dvkind in (3,4) and t_code = :code", MakeArray(SqlParam("code", DealCode)));
  if (sql.MoveNext() )
    DealID = sql.value(0);
    NettingPMGR = ifThenElse(sql.value(1)=="X", true, false);
  end;
  return DealID;
End;

//Возвращает ИД промежуточного платежа
private macro GetPaymID (DealID, side, dt, num_, dt_end):integer/*PNV 536327*/
  var PaymID:integer = 0;
  var sql = ExecSqlSelect("select t_id from ddvnpmgr_dbt where t_dealid = :dealid and t_side = :side and t_paydate = :dt and (t_enddate - t_begdate) = :num and t_enddate = :dt_end",/*PNV 536327*/
                          MakeArray(SqlParam("dealid", DealID), SqlParam("side", side), SqlParam("dt", dt), SqlParam("num", num_), SqlParam("dt_end", dt_end)));/*PNV 536327*/
  if (sql.MoveNext() )
    PaymID = sql.value(0);
  end;
  return PaymID;
End;

//Параметры платежа: Сумма, Дата
private macro GetPaymParm (pmid, side, sum:@money, paydate:@date)
  var sql, pmgr_side;

  if (side == ALG_DV_PMGR_SIDE_LIABILITY)
    pmgr_side = "t_liabilitysum";
  elif (side == ALG_DV_PMGR_SIDE_DEMAND)
    pmgr_side = "t_demandsum";
  end;

  sql = ExecSqlSelect("select t_paydate, "+pmgr_side+" as t_paymentsum from ddvnpmgr_dbt where t_id = :pid", MakeArray(SqlParam("pid", pmid)));
  if (sql.MoveNext() )
    sum = sql.value("t_paymentsum");
    paydate = sql.value("t_paydate");
    return true;
  end;
  return false;
End;

//-------------------------------------------------------------------

private macro ReadFile ()
  var xml:object = ActiveX("MSXML.DOMDocument");
  var FullNameFile,FileName, ExtName, DirName, node;
  var DealNumber:string;
  var PaymDate:Date;
  var side:integer; // 2-требования, 1-обязательства
  var DealID:integer;
  var PaymID:integer;
  var NettingPMGR:bool = false; //Признак неттинга промежуточных платежей
  var OperDate:Date = {curdate};
  var Department:integer = {OperDprt};
  var rownum:integer = 0;
  var TermPath = GetCurDir(false);// + "\\txtfile\\";
  var sum:money = $0;
  var Number = 0;/*PNV 536327*/
  var EndDate :Date;/*PNV 536327*/

  Data = CollectData();

  private macro GetValue (ChildID:integer)
    return node.childNodes(rownum).childNodes(ChildID).text;
  End;

  /*Выбираем файл для обработки*/
  if(not SelectFile(FullNameFile, "$\\..\\..\\..\\*.xml", "Выберите файл для загрузки", 0, true))
    return;
  end;

  //Отделяем путь файла от имени
  splitfile(FullNameFile, FileName, ExtName);
  FileName = FileName + ExtName;
  DirName = substr(FullNameFile, 1, index(FullNameFile, FileName) - 1);

  if (not copyfile("$" + DirName + FileName, TermPath + FileName) )
    return AddErr("Ошибка при передаче файла на терминал");
  end;

  FullNameFile = TermPath + FileName;

  /* откроем файл импорта */
  if( not xml.load(FullNameFile) )
    return AddErr("Неверный формат файла импорта\n", FullNameFile, "|Невозможно загрузить xml-документ");
  end;

  if (not RemoveFile (FullNameFile))
    AddErr("Ошибка удаления файла " + FullNameFile);
  end;

  node = xml.childNodes( GetChildNoteID(xml, "Workbook"));
  node = node.childNodes( GetChildNoteID(node, "Worksheet"));
  node = node.childNodes( GetChildNoteID(node, "Table"));
  rownum = GetChildNoteID(node, "Row") + 6; //Находим 1-ю строку. Прибавляем ещё 6, т.к. информация начинается с 7-й строки

  initProgress(node.childNodes.Length, "Чтение файла", "Чтение файла");

  //Чтение xml файла
  while (rownum < node.childNodes.Length)
    if (StrUpr(GetValue(FLOWTYPE)) != "INTEREST")
      UseProgress(rownum = rownum + 1);
      continue;
    end;

    DealNumber = "IRS:"+GetValue(IRS);
    Number     = GetValue(NUM); /*PNV 536327*/
    EndDate    = ConvertDate(GetValue(PeriodEndDate)); /*PNV 536327*/
    PaymDate   = ConvertDate(GetValue(PAYMENTDATE));
    side       = ifThenElse(GetValue(CASHFLOW)>0, ALG_DV_PMGR_SIDE_DEMAND, ALG_DV_PMGR_SIDE_LIABILITY);
    DealID     = GetDealID(DealNumber, @NettingPMGR);
    PaymID     = GetPaymID(Dealid, ifThenElse(NettingPMGR, 0, side), PaymDate, Number, EndDate);/*PNV 536327*/

    GetPaymParm (PaymID, side, @sum);

    if (sum == Abs(Money(GetValue(CASHFLOW)))) //костыль. суммы равны - нечего изменять
      UseProgress(rownum = rownum + 1);
      continue;
    end;

    if (DealID == 0)
      AddErr("Не найдена сделка с кодом "+DealNumber);
      UseProgress(rownum = rownum + 1);
      continue;
    end;

    if (Abs(Money(GetValue(CASHFLOW))) > 0)/*PNV 536327*/
       if (PaymID == 0)
           AddErr("Не найден платеж. Номер сделки="+DealNumber+", сторона="+ifThenElse(side==ALG_DV_PMGR_SIDE_DEMAND, "Требование", "Обязательство")+", дата платежа="+PaymDate);
           UseProgress(rownum = rownum + 1);
           continue;
       end;
    Data.AddRec(OperDate, Department, DealNumber, DealID, PaymID, GetValue(CASHFLOW), GetValue(RATE), side);
    end;/*PNV 536327*/
    
    UseProgress(rownum = rownum + 1);
  end;
  RemProgress();
  return true;

onerror()
  RemProgress();
  return false;
End;

macro StartCorrectPP ()
  var Rep = CorrectPP_Rep();
  var i = 0, j = 0, DataArr, paym;
  var sum:money, paydate, paymstr, result;
  var count = 0;
  var stat;

  stat = ReadFile();
  if (valtype(stat) == V_UNDEF) //Если ничего не возвращает - не был выбран файл - прекращаем обработку
    return false;
  elif (not stat)
    AddErr("Ошибка при обработке файла");
  end;

  Rep.PrintHead;
  DataArr = Data.DataArr;

  while (i < DataArr.Size)
    j = 0;
    paymstr = "";
    result = "";

    while (j < DataArr[i].parms.size)
      sum = $0;
      paydate = Date(0, 0, 0);
      paym = DataArr[i].parms[j];

      GetPaymParm (paym.id, paym.side, @sum, @paydate);
      paymstr = ifThenElse(paym.side==ALG_DV_PMGR_SIDE_DEMAND, "Требование", "Обязательство")+"; "+paydate+"; старая сумма="+string(sum:*:2)+"; новая сумма="+string(paym.sum:*:2)+"; ставка="+paym.rate+"\n";
      Rep.PrintLine(DataArr[i].DealCode, paymstr, result);
      j = j + 1;
    end;

    stat = RunOverval(DataArr[i].OperDate, DataArr[i].Department, DataArr[i].DealID, DataArr[i].parms);
    if (stat == 0)
      result = "Успешно";
      count = count + 1;
    elif (stat == 1) result = "Ошибка при вставке шага в операцию";
    elif (stat == 2) result = "Ошибка при выполнении шага операции";
    else result = "Ошибка при обработке записи";
    end;
    Rep.PrintLine(DataArr[i].DealCode, "", result);

    i = i + 1;
  end;
  Rep.ShowRep(count);
  return true;

onerror()
  return true; //true - чтобы вывести на экран ошибки
End;

if (not StartCorrectPP())
  exit(1);
else
  exit(0);
end;