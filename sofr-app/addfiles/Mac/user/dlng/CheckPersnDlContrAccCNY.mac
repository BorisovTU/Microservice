/*
$Name:        CheckPersnDlContrAccCNY.mac
$Module:      Ядро Securities
$Description: Макрос поиска счетов ФЛ в юанях, у которых счет на фондовой площадке не равен счету на валютной
*/

import "secinter.mac", "dlquery.mac";
import DealsInter, CTInter;


private macro CheckPersnAcc()
  var query, cmd, DataSet;
  var cnt = 0, i = 0;
  var err = 0, MessStr = "";

  println("Список ДБО физлиц, у которых счета в CNY по фондовому и валютному рынку не совпадают");
  println("");

  query =   " SELECT sf.t_Number, acc21.t_Account as Account21, acc1.t_Account as Account1, pt.t_PartyID "
          + "   FROM ddlcontr_dbt dlc, "
          + "        dsfcontr_dbt sf, "
          + "        dparty_dbt pt, "
          + "        ddlcontrmp_dbt mp21, "
          + "        dsfcontr_dbt sfmp21, "
          + "        ddlcontracc_dbt dlacc21, "
          + "        daccount_dbt acc21, "
          + "        ddlcontrmp_dbt mp1, "
          + "        dsfcontr_dbt sfmp1, "
          + "        ddlcontracc_dbt dlacc1, "
          + "        daccount_dbt acc1, "
          + "        dpersn_dbt ps "
          + "  WHERE sf.t_ID = dlc.t_SfContrID "
          + "    AND pt.t_PartyID = sf.t_PartyID "
          + "    AND pt.t_LegalForm = 2 "
          + "    AND ps.t_PersonID = pt.t_PartyID "
          + "    AND ps.t_IsEmployer <> 'X' "  //Кроме ИП
          + "    AND mp21.t_DlContrID = dlc.t_DlContrID "
          + "    AND sfmp21.t_ID = mp21.t_SfContrID "
          + "    AND sfmp21.t_ServKind = " + PTSK_CM
          + "    AND sfmp21.t_ServKindSub = 8 "
          + "    AND dlacc21.t_DlContrID = mp21.t_DLContrID "
          + "    AND dlacc21.t_FIID = (SELECT fin.t_FIID FROM dfininstr_dbt fin WHERE fin.t_FI_Kind = "+FIKIND_CURRENCY+" AND fin.t_CCY = 'CNY') "
          + "    AND dlacc21.t_MPID = mp21.t_ID "
          + "    AND acc21.t_AccountID = dlacc21.t_AccountID "
          + "    AND mp1.t_DlContrID = dlc.t_DlContrID "
          + "    AND sfmp1.t_ID = mp1.t_SfContrID "
          + "    AND sfmp1.t_ServKind = " + PTSK_STOCKDL
          + "    AND sfmp1.t_ServKindSub = 8 "
          + "    AND dlacc1.t_DlContrID = mp21.t_DLContrID "
          + "    AND dlacc1.t_FIID = dlacc21.t_FIID "
          + "    AND dlacc1.t_MPID = mp1.t_ID "
          + "    AND acc1.t_AccountID = dlacc1.t_AccountID "
          + "    AND acc1.t_AccountID != acc21.t_AccountID ";

  cmd = DL_RSDCommand(query);

  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, "Идет печать", "Печать протокола");

    println("┌──────────────────────┬──────────────────────┬──────────────────────┬──────────────────────┐");
    println("│                      │                      │                      │                      │");
    println("│     Код клиента      │       Договор        │         Счет         │         Счет         │");
    println("│                      │                      │   (валютный рынок)   │   (фондовый рынок)   │");
    println("│                      │                      │                      │                      │");
    println("├──────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┤");
    
                                                                                                                                             
                                                                                                                                             
    DataSet = cmd.Execute();
    while(DataSet.moveNext())



      [│######################│######################│######################│######################│]
      (ПолучитьКодСубъекта(DataSet.rec.PartyID, PTCK_CONTR):c,
       string(DataSet.Number):c,                                                                                 
       string(DataSet.Account21):c,      
       string(DataSet.Account1):c        
      );

      UseProgress(i = i + 1);

      if(i != cnt)
        println("├──────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┤");
      end;
    end;

    println("└──────────────────────┴──────────────────────┴──────────────────────┴──────────────────────┘");

    RemProgress();
  else
    println("Нет подходящих ДБО");
  end;

end;


CheckPersnAcc();