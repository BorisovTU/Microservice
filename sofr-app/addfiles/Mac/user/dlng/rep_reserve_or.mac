/*
Отчёт для проверки резервов под ОКУ
Симанов А.В.
*/

import or_tpl_h, oralib, likepy, or_rep_h, globals, RsbFormsInter, Календарь, CbForms_h;

var csvFile, fName, csvTable, printEngine;

private var sqlString;
private const ALG_PERIOD_ID = 2263;

private macro ByDefault (parm, default_value)
  if (valtype(parm) == V_UNDEF)
    SetParm(0, default_value);
  end;
End;

//-----------------------------------------------------------------------------------------
private macro collectOutProc ( str )
  sqlString = sqlString + "\n" + trim (str);
End;

private macro CaptureOutput
  sqlString = "";
  SetOutHandler (@collectOutProc);
End;

private macro StopCaptureOutput
  SetOutHandler ();
  return sqlString;
End;

//-----------------------------------------------------------------------------------------

macro query_bonds ():string
  CaptureOutput();
[with parm as (select :1 t_maindate, :2 t_prevdate from dual)
  select fin.t_name t_finame,
         avr.t_isin t_id,
         RSI_RSB_FIInstr.ConvSum(lot.t_balancecost, lot.t_currency, 0, (select t_maindate from parm)-1, 1) t_balancecost,
         nvl((select DISTINCT t_account from dmcaccdoc_dbt mcacc, dmccateg_dbt categ, dmctempl_dbt templ
          where mcacc.t_fiid = lot.t_fiid and
                mcacc.t_iscommon = chr(0) and
                categ.t_id = mcacc.t_catid and
                categ.t_code in ('-Кор_Резерв, ЦБ', '+Кор_Резерв, ЦБ') and
                templ.t_catid = categ.t_id and
                templ.t_number = mcacc.t_templnum and
                templ.t_value1 = lot.t_portfolio and

                rsb_account.restall (mcacc.t_account, mcacc.t_chapter, mcacc.t_currency, (select t_maindate from parm)-1, 0) <> 0), chr(1)) t_account_main,
         lot.t_correstreserve t_restreserve,
         (select DISTINCT t_account from dmcaccdoc_dbt mcacc, dmccateg_dbt categ, dmctempl_dbt templ
              where mcacc.t_fiid = lot_prev.t_fiid and
                    mcacc.t_iscommon = chr(0) and
                    categ.t_id = mcacc.t_catid and
                    categ.t_code in ('-Кор_Резерв, ЦБ', '+Кор_Резерв, ЦБ') and
                    templ.t_catid = categ.t_id and
                    templ.t_number = mcacc.t_templnum and
                    templ.t_value1 = lot_prev.t_portfolio and
                    rsb_account.restall (mcacc.t_account, mcacc.t_chapter, mcacc.t_currency, (select t_prevdate from parm)-1, 0) <> 0) t_account_prev,
         nvl(lot_prev.t_correstreserve, 0) t_restreserve_prev,
         lot.t_correstreserve - nvl(lot_prev.t_correstreserve, 0) t_difference
  from v_scwrthistex lot
  join dfininstr_dbt fin ON fin.t_fiid = lot.t_fiid and
                            fin.t_fi_kind = 2
  join davoiriss_dbt avr ON avr.t_fiid = lot.t_fiid
  left join (select t_sumid, t_fiid, t_portfolio, t_groupid, t_state, t_party, t_correstreserve, t_instance, t_changedate from v_scwrthistex) lot_prev
             ON lot_prev.t_sumid = lot.t_sumid and
                lot_prev.t_fiid = lot.t_fiid and
                lot_prev.t_groupid = lot.t_groupid and
                lot_prev.t_portfolio = lot.t_portfolio and
                lot_prev.t_state = lot.t_state and
                lot_prev.t_party = lot.t_party
  where lot.t_state = 1 and
        lot.t_groupid = 5 and
        lot.t_party = -1 AND
        lot.t_correstreserve <> 0 and
        lot.t_instance = (SELECT MAX (t_instance)
                          FROM v_scwrthistex
                          WHERE lot.t_sumid = t_sumid AND
                                lot.t_changedate = t_changedate) AND
        lot.t_changedate = (SELECT MAX (t.t_changedate)
                            FROM v_scwrthistex t
                            WHERE lot.t_sumid = t_sumid AND
                                  t.t_changedate <= (select t_maindate from parm)) and
        lot_prev.t_instance = (SELECT MAX (t_instance)
                              FROM v_scwrthistex
                              WHERE lot_prev.t_sumid = t_sumid AND
                                    lot_prev.t_changedate = t_changedate) AND
        lot_prev.t_changedate = (SELECT MAX (t.t_changedate)
                                 FROM v_scwrthistex t
                                 WHERE lot_prev.t_sumid = t_sumid AND

                                       t.t_changedate <= (select t_prevdate from parm))
  order by t_id
      ];
  return StopCaptureOutput();
End;

macro query_mbk ():string
  CaptureOutput();
[with parm as (select :1 t_maindate, :2 t_prevdate from dual)
  select tick.*, tick.t_restreserve - tick.t_restreserve_prev as t_difference
  from (select 'МБК' t_finame,
               tick.t_dealcode t_id,
               abs(rsb_account.restall(acc_od.t_account, acc_od.t_chapter, acc_od.t_currency, (select t_maindate from parm))) t_balancecost,
               acc_res.t_account t_account_main,
               rsb_account.restall (acc_res.t_account, acc_res.t_chapter, acc_res.t_currency, (select t_maindate from parm), 0) t_restreserve,
               nvl(acc_res_prev.t_account, chr(1)) t_account_prev,
               rsb_account.restall (acc_res_prev.t_account, acc_res_prev.t_chapter, acc_res_prev.t_currency, (select t_prevdate from parm), 0) t_restreserve_prev 
        from ddl_tick_dbt tick
        join dmcaccdoc_dbt acc_res on acc_res.t_dockind = tick.t_bofficekind and
                                      acc_res.t_docid = tick.t_dealid and
                                      acc_res.t_catnum in (1439, 1440, 1445, 1446, 5116, 5122, 5123, 5124, 5167, 5168 ) and
                                      rsb_account.restall (acc_res.t_account, acc_res.t_chapter, acc_res.t_currency, (select t_maindate from parm), 0) <> 0  
        left join dmcaccdoc_dbt acc_od on acc_od.t_dockind = tick.t_bofficekind and
                                          acc_od.t_docid = tick.t_dealid and
                                          acc_od.t_activatedate < (select t_maindate from parm) and
                                          acc_od.t_catnum = case when acc_res.t_catnum in (1445, 1446, 5123, 5124) then 621
                                                                 when tick.t_dealtype = 12335 then 5084 /*ОДТФ*/
                                                                 when tick.t_dealtype = 12306 then 5000 /*СЧЕТ_91418*/
                                                                 else 601/*ОД*/
                                                            end
        left join dmcaccdoc_dbt acc_res_prev on acc_res_prev.t_dockind = tick.t_bofficekind and
                                                acc_res_prev.t_docid = tick.t_dealid and
                                                acc_res_prev.t_catnum = acc_res.t_catnum and
                                                rsb_account.restall (acc_res_prev.t_account, acc_res_prev.t_chapter, acc_res_prev.t_currency, (select t_prevdate from parm), 0) <> 0
        where tick.t_bofficekind = 102) tick
  order by tick.t_id
];
  return StopCaptureOutput();
End;

macro query_bills ():string
  CaptureOutput();
[with parm as (select :1 t_maindate, :2 t_prevdate from dual)
  select bnr.*, bnr.t_restreserve - bnr.t_restreserve_prev as t_difference
  from (select 'Вексель' t_finame,
                trim(bnr.t_bcnumber) t_id,
                abs(rsb_account.restall(balance_acc.t_account, balance_acc.t_chapter, balance_acc.t_currency, (select t_maindate from parm))) t_balancecost,
                acc_res.t_account t_account_main,
                rsb_account.restall (acc_res.t_account, acc_res.t_chapter, acc_res.t_currency, (select t_maindate from parm), 0) t_restreserve,
                nvl(acc_res_prev.t_account, chr(1)) t_account_prev,
                rsb_account.restall (acc_res_prev.t_account, acc_res_prev.t_chapter, acc_res_prev.t_currency, (select t_prevdate from parm), 0) t_restreserve_prev
        from dvsbanner_dbt bnr
        join dmcaccdoc_dbt acc_res on acc_res.t_dockind = 164 and
                                      acc_res.t_docid = bnr.t_bcid and
                                      acc_res.t_catnum in (1394, 1395, 1461, 1462) and
                                      rsb_account.restall (acc_res.t_account, acc_res.t_chapter, acc_res.t_currency, (select t_maindate from parm), 0) <> 0
        left join dmcaccdoc_dbt acc_res_prev on acc_res_prev.t_dockind = 164 and
                                                acc_res_prev.t_docid = bnr.t_bcid and
                                                acc_res_prev.t_catnum = acc_res.t_catnum and
                                                rsb_account.restall (acc_res_prev.t_account, acc_res_prev.t_chapter, acc_res_prev.t_currency, (select t_prevdate from parm), 0) <> 0
        left join dmcaccdoc_dbt balance_acc on balance_acc.t_dockind = 164 and
                                               balance_acc.t_docid = bnr.t_bcid and
                                               balance_acc.t_catnum in (462, 1492) and
                                               balance_acc.t_activatedate < (select t_maindate from parm) and
                                               -1 = case when balance_acc.t_catnum = 1492
                                                         then (select t_value1 from dmctempl_dbt where t_catid = balance_acc.t_catid and t_number = balance_acc.t_templnum)
                                                         else -1
                                                     end and
                                               abs(rsb_account.restall(balance_acc.t_account, balance_acc.t_chapter, balance_acc.t_currency, (select t_maindate from parm))) <> 0
        where bnr.t_issuer not in (select t_partyid from ddp_dep_dbt) and
              bnr.t_bcformkind = 1) bnr
  order by t_id
];
  return StopCaptureOutput();
End;

//-----------------------------------------------------------------------------------------

private class (TRsbPanel) Pn_report_or (_dt:Date)
  var ed_report_date:TRsbEditField,
      ed_report_date_prev:TRsbEditField;

  var cb_bond:TRsbCheckBox,
      cb_veksel:TRsbCheckBox,
      cb_mbk_deal:TRsbCheckBox,
      cb_repo_deal:TRsbCheckBox;

  var FT_INT     = 0,
      FT_DOUBLE  = 4,
      FT_STRING  = 7,
      FT_DATE    = 9,
      FT_CHR     = 12,
      FT_NUMERIC = 25;

  var ESC   = 27,
      F2    = 316,
      F3    = 317,
      F9    = 323,
      SHF6  = 345,
      Enter = 13;

  private var row:integer;
  private var start_dt:Date = _dt; //Дата выпуска отчёта.

  private macro getPrevDate(dt)
    var day, month, year;
    DateSplit(dt, day, month, year);

    if (month == 1)
      month = 12;
      year = year - 1;
    else
      month = month - 1;
    end;

    while (day > GetDaysInMonth(Date(1, month, year)))
      day = day - 1;
    end;

    return Date(day, month, year);
  END;

  private macro m_init_report_panel ()
    setSize(40,9);
    setPosition(40,5);
    setCaption("Введите параметры отчета");
    setStatus("~F2~ Выполнить  ~F3~ Список  ~ESC~ Отмена");

    row = 2;
    addLabel(TRsbLabel(2,  row, "Текущая отчётная дата"));
    addLabel(TRsbLabel(20, row, "Предыдущая отчётная дата"));

    //отчётная дата
    row = row + 1;
    ed_report_date = TRsbEditField(FT_DATE);
    ed_report_date.setPosition(2, row);
    ed_report_date.setSize(8,1);
    ed_report_date.textLength = 15;
    ed_report_date.name = "ed_report_date";
    ed_report_date.Value = start_dt;
    ed_report_date.onKeyPressed( R2M(this, "onKeyPressed_report_date") );
    addControl(ed_report_date);

    //предыдущая отчетная дата
    ed_report_date_prev = TRsbEditField(FT_DATE);
    ed_report_date_prev.setPosition(20, row);
    ed_report_date_prev.setSize(8,1);
    ed_report_date_prev.textLength = 15;
    ed_report_date_prev.name = "ed_report_date_prev";
    ed_report_date_prev.Value = getPrevDate(start_dt);
    ed_report_date_prev.onKeyPressed( R2M(this, "onKeyPressed_report_date_prev") );
    addControl(ed_report_date_prev);

    row = row + 1;
    addLabel(TRsbLabel(2,  row, "Данные в отчёте:"));

    //Облигации
    row = row + 1;
    cb_bond = TRsbCheckBox(FT_CHR);
    cb_bond.setPosition(2, row);
    cb_bond.setSize(1, 1);
    cb_bond.name       = "cb_bond";
    cb_bond.textLength = 1;
    cb_bond.Editable   = true;
    cb_bond.Checked    = "X";
    addControl(cb_bond);
    addLabel(TRsbLabel(4,  row, "Облигации"));

    //Векселя
    row = row + 1;
    cb_veksel = TRsbCheckBox(FT_CHR);
    cb_veksel.setPosition(2, row);
    cb_veksel.setSize(1, 1);
    cb_veksel.name       = "cb_veksel";
    cb_veksel.textLength = 1;
    cb_veksel.Editable   = true;
    cb_veksel.Checked    = "X";
    addControl(cb_veksel);
    addLabel(TRsbLabel(4,  row, "Векселя"));

    //Сделки МБК
    row = row + 1;
    cb_mbk_deal = TRsbCheckBox(FT_CHR);
    cb_mbk_deal.setPosition(2, row);
    cb_mbk_deal.setSize(1, 1);
    cb_mbk_deal.name       = "cb_mbk_deal";
    cb_mbk_deal.textLength = 1;
    cb_mbk_deal.Editable   = true;
    cb_mbk_deal.Checked    = "X";
    addControl(cb_mbk_deal);
    addLabel(TRsbLabel(4,  row, "Сделки МБК"));

    //Сделки РЕПО
    row = row + 1;
    cb_repo_deal = TRsbCheckBox(FT_CHR);
    cb_repo_deal.setPosition(2, row);
    cb_repo_deal.setSize(1, 1);
    cb_repo_deal.name       = "cb_repo_deal";
    cb_repo_deal.textLength = 1;
    cb_repo_deal.Editable   = true;
    cb_repo_deal.Checked    = "X";
    addControl(cb_repo_deal);
    addLabel(TRsbLabel(4,  row, "Сделки РЕПО"));

    addEventHandler(RSB_EV_KEY_PRESSED,R2M(this,"onKeyPressed_main"));
  End;

  //Обработчик нажатий для поля Отчетная дата
  macro onKeyPressed_report_date(RsbEvent:Object)
    if (RsbEvent.keyCode == F3)
      ed_report_date.value = GetDateByCalendar(ed_report_date.value);
      ed_report_date_prev.value = getPrevDate(ed_report_date.value);
    end;
  end;

  //Обработчик нажатий для поля Предыдущая отчётная дата
  macro onKeyPressed_report_date_prev(RsbEvent:Object)
    if (RsbEvent.keyCode == F3)
      ed_report_date_prev.value = GetDateByCalendar(ed_report_date_prev.value);
    end;
  end;

  //Основной обработчик панели
  macro onKeyPressed_main(RsbEvent:Object)
    if(RsbEvent.keyCode == ESC)
       RsbEvent.keyCode = F9;
       this.close(ESC);
    elif ( (RsbEvent.keyCode == Enter) or (RsbEvent.keyCode == F2) )
      this.close(0);
    end;
  end;

  macro prevDate ():Date
    return ed_report_date_prev.value;
  End;

  macro mainDate ():Date
    return ed_report_date.value;
  End;

  macro Bonds ():bool
    return (cb_bond.Checked == "X");
  End;

  macro MBK ():bool
    return (cb_mbk_deal.Checked == "X");
  End;

  macro Bills ():bool
    return (cb_veksel.Checked == "X");
  End;

  macro REPO ():bool
    return (cb_repo_deal.Checked == "X");
  End;

  macro getCount ():integer
    var count:integer = 0;
    if (Bonds) count = count + 1; end;
    if (MBK)   count = count + 1; end;
    if (Bills) count = count + 1; end;
    if (REPO)  count = count + 1; end;
    return count;
  End;

  initTRsbPanel();
  m_init_report_panel();
END;

//-----------------------------------------------------------------------------------------
//класс для вывода отчёта в excel
class c_report_reserve ()
  private var sheet;
  private var main_sheet_name = "";

  var isCreated:bool = false;
  var maindate:Date;
  var prevdate:Date;

  macro print_Row (/*...*/)
    var i = 1;
    var Value:variant = null;
    
    while( GetParm(i, Value) )
      csvFile.AddCell(Value);
      i = i + 1;
    end;
    csvFile.AddRow();
  End;

  macro PrintNewSheet(sheetName:string, query:string, params:TArray)
    var sql;
    
    csvFile = C_CSVFile();
    if (printEngine.UsePoi())
      csvFile.SetLimitRow(FLUSH_COUNT_XLSX);
    end;
    fName = csvFile.CreateFullFileName("report_781_csv_"+sheetName+".txt");
    csvFile.FileOpen(fName, true);
    
    if (sheetName == "Ценные бумаги")
      printEngine.SetValue_NameCell("head1", "Наименование ценной бумаги");
      printEngine.SetValue_NameCell("head2", "ISIN");
    elif (sheetName == "Сделки МБК")
      printEngine.SetValue_NameCell("head1", "Тип ФИ");
      printEngine.SetValue_NameCell("head2", "ID сделки");
    elif (sheetName == "Учтенные векселя")
      printEngine.SetValue_NameCell("head1", "Тип ФИ");
      printEngine.SetValue_NameCell("head2", "ID ФИ");
    elif (sheetName == "Сделки РЕПО")
      printEngine.SetValue_NameCell("head1", "Тип ФИ");
      printEngine.SetValue_NameCell("head2", "ID сделки");
    end;
    
    csvTable = printEngine.RegisterTable("TemplateTable", "TemplateTableHeader", true);
    printEngine.CopyAllSheetInTotalBook(null, false, "TemplateTableHeader", 0, sheetName);
    
    if ((StrUpr(query) != "UNDEFINED") and (query != ""))
      sql = execSqlSelect(query, params);
      while(sql.moveNext)
        print_Row(sql.value("t_finame")
                 ,sql.value("t_id")
                 ,sql.value("t_balancecost")
                 ,prevdate
                 ,sql.value("t_account_prev")
                 ,sql.value("t_restreserve_prev")
                 ,maindate
                 ,sql.value("t_account_main")
                 ,sql.value("t_restreserve")
                 ,sql.value("t_difference")
                 );
      end;
    end;
    
    csvFile.FileClose();
    csvTable.FillTabelFromCSV(csvFile.GetlsFileName());
    csvTable.EndTable();
    printEngine.CopyAllSheetInTotalBook(null, false, csvTable.GetTableRange(), 0, sheetName);

  End;

  macro printReport ()
    printEngine.Close();
    printEngine.SaveTotalBook("report_781");

  End;
      
END;



//-----------------------------------------------------------------------------------------
macro m_report_start()
  var panel = Pn_report_or({curdate});
  var stat = panel.Run();
  var Rep:c_report_reserve = c_report_reserve();
  var params;
  var count:integer = 0;
  
  if (stat != 0) 
    return;
  end;
  
  printEngine=CTemplateXLS(NULL, NULL, NULL, NULL, NULL, NULL, true, false);
  if(printEngine.UsePoi())
    printEngine.SetXLSXFormat();
  end;
  printEngine.CreateTotalBook();
  printEngine.OpenTemplate("report_781.xlsx", true);
  
  Rep.maindate = panel.mainDate;
  Rep.prevdate = panel.prevDate;
  params = MakeArray(sqlParam("1", Rep.maindate), sqlParam("2", Rep.prevdate));

  initProgress(panel.getCount, "Ожидайте, идёт построение отчета", "Ожидайте, идёт построение отчета");

  if (panel.Bonds)
    Rep.PrintNewSheet("Ценные бумаги", query_bonds(), params);
    useProgress(count = count + 1);
  end;

  if (panel.MBK)
    Rep.PrintNewSheet("Сделки МБК", query_mbk(), params);
    useProgress(count = count + 1);
  end;

  if (panel.Bills)
    Rep.PrintNewSheet("Учтенные векселя", query_bills(), params);
    useProgress(count = count + 1);
  end;

  if (panel.REPO)
    Rep.PrintNewSheet("Сделки РЕПО", "");
    useProgress(count = count + 1);
  end;
  remProgress();
  Rep.PrintReport();

onerror()
  remProgress();
End;

m_report_start();
exit(1);