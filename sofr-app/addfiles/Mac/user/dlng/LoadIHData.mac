import activex, oralib, likepy, BankInter, DVInter, globals, rsd,RsbDataSet, PTInter,CTInter, "cb_sql.mac";
import "u_common_email_utils.mac", "dlquery.mac";
import OprInter;

                                                                                                      
private file attr("objattr.dbt") key 0;
private file attrcor("objatcor.dbt") key 0;

Var Repdate;

private const COL1               = 1,
              COL2               = 2,
              COL3               = 3,
              COL4               = 4,
              COL5               = 5,
              COL6               = 6,
              COL7               = 7,
              COL8               = 8, 
              COL9               = 9, 
              COL10              = 10, 
              COL11              = 11, 
              COL12              = 12; 


class StatusRecIH()
  var OperDate         :date,
      IdIhAsudr        :string,
      IdIhSOFR         :string,
      Portf            :string,
      SubPortf         :string,
      Val              :string,
      IDOHSOFR         :string,
      IDDealOHAsudr    :string,
      KindIH           :string,
      KindDealOH       :string,
      IDHedgRel        :string,
      EndDateOHAsudr   :date,
//BIQ-10394 Тип отношений хеджирования
      Relationtype     :integer,
      Portfolio        :string;
 end;

    private const CV_EMAIL_GRP_HEDG = 31; /* Группа рассылки по проблемам загрузки файла с данными по Инструментам Хеджирования */


Private Macro TrimStr(str)
   If (valtype(str) != V_STRING)
      return str;
   else
      return trim(str);
   end;
End;

Private macro SendErrMsg(error:string, LoadFileName:string, receiverGroupID:integer)
   var email = c_email_proc_env();
   var FileName, ExtName, DirName;
   VAR SpPath = "", SpFileName = "", tittle = "", message = "";
   //Отделяем путь файла от имени
   tittle = "Внимание! Хеджирование. Загрузка файла.  " + LoadFileName;
   message = "Сообщение отправлено автоматически. В процессе загрузки файла по инструментам хеджирования " + LoadFileName + " воpникли следующие ошибки: \n " + error;
   email.m_set_msg_head(tittle);
   email.m_add_row_to_msg_text(message);
   email.m_save_to_submit_grp(receiverGroupID);
   email.m_submit_email_synch();
End;

Private macro SendGoodMsg(error:string, LoadFileName:string, receiverGroupID:integer)
   var email = c_email_proc_env();
   var FileName, ExtName, DirName;
   VAR SpPath = "", SpFileName = "", tittle = "", message = "";
   //Отделяем путь файла от имени
   tittle = "Внимание! Хеджирование. Загрузка файла.  " + LoadFileName;
   message = "Сообщение отправлено автоматически. Файл по инструментам хеджирования " + LoadFileName + " успешно загружен ";
   email.m_set_msg_head(tittle);
   email.m_add_row_to_msg_text(message);
   email.m_save_to_submit_grp(receiverGroupID);
   email.m_submit_email_synch();
End;



//Перемещает файл
private macro copyFromTo (from, to)
  return TDirList().copy(from, null, to, true, false);
End;

Private macro GetRepDate(RepDate)
   var day,mon,year;
   DateSplit(RepDate, day,mon,year);
   if (day < 10)
     day = string(0,day)
   end;
   If(mon <10)
      mon = string(0,mon);
   end;
     return string (day,mon,year);

End;

// DEF-41212: BIQ-10394. Не заполняется СС по денежным потокам если несколько отношений хеджирования
private macro CheckExistingIDHedgRel(IDHedgRel, ObjectId, err:@string)
   var sql, cmd, rs;
   var res = true;
   
   sql = "select t_ext_hedgeid, t_ext_subportf, t_ext_instrid "+
         "  from ddlhdgrelation_dbt "+
         " where t_ext_hedgeid = :ObjectId "+
         "   and t_objid = :ID";
   cmd = RSDCommand(sql);
   cmd.AddParam("hedgeid", RSDBP_IN, IDHedgRel);
   cmd.AddParam("ID", RSDBP_IN, ObjectId);
   rs = RSDRecordSet(cmd);
   if (rs.movenext)
      err = "Уже существует запись с "+IDHedgRel+ " и субпортфелем " + rs.value("t_ext_subportf") +" для "+rs.value("t_ext_instrid");
      res = false;
   end;
   
   return res;
end;

Private macro InsertTablehedgeinstr (strhi, IrsId, ID, kind)
   var sql, cmd, err, objkind, KindCateg;

   If((kind == 1) OR (kind == 2))
      objkind = OBJTYPE_AVOIRISS;  // 12
   elIf((kind == 3) OR (kind == 4))
      objkind = OBJTYPE_FICERT;    // 24
   elIf((kind == 5) OR (kind == 6))
      objkind = OBJTYPE_CONVDEAL;  // 102, не 103 DEF-40616
   else
      objkind = 0;
   end;

   sql = "Insert into ddlhdgrelation_dbt ( "+
         "  T_ID, T_OBJID, T_OBJDOCKIND, T_OBJTYPE, T_INSTRID, "+
         "  T_INSTRDOCKIND, T_INSTRTYPE, T_CHANGEDATE, T_BEGINDATE, T_ENDDATE, "+ 
         "  T_HEDGETYPE, T_EXT_HEDGEID, T_EXT_PORTF, T_EXT_SUBPORTF, T_EXT_OBJID, "+ 
         "  T_EXT_INSTRID, T_EXT_DEALID, T_ACCPORTF ) "+
         " values(:1, :2, :3, :4, :5, :6, :7, to_date('01010001','ddmmyyyy'), :8, :9, :10, :11, :12, :13, :14, :15, :16, :17)";
   cmd = RSDCommand(sql);
   cmd.AddParam("p01", RSDBP_IN, 0);
   cmd.AddParam("p02", RSDBP_IN, ID);
   cmd.AddParam("p03", RSDBP_IN, objkind);
   cmd.AddParam("p04", RSDBP_IN, kind);
   cmd.AddParam("p05", RSDBP_IN, IrsId);
   cmd.AddParam("p06", RSDBP_IN, 199);
   cmd.AddParam("p07", RSDBP_IN, 1);
   cmd.AddParam("p08", RSDBP_IN, strhi.OperDate);
   cmd.AddParam("p09", RSDBP_IN, strhi.EndDateOHAsudr);
   cmd.AddParam("p10", RSDBP_IN, strhi.Relationtype);
   cmd.AddParam("p11", RSDBP_IN, strhi.IDHedgRel);
   cmd.AddParam("p12", RSDBP_IN, strhi.Portf);
   cmd.AddParam("p13", RSDBP_IN, strhi.SubPortf);
   //If(ID == 0)
   //DEF-40680
       cmd.AddParam("p14", RSDBP_IN, string(strhi.IDOHSOFR));
    /*else
       cmd.AddParam("p14", RSDBP_IN, strfor(1));
    end; */
    cmd.AddParam("p15", RSDBP_IN, strhi.IdIhAsudr);
    cmd.AddParam("p16", RSDBP_IN, strhi.IdIhSOFR);
    cmd.AddParam("p17", RSDBP_IN, "Оцениваемые по "+strhi.Portfolio);

    cmd.Execute;

   //connectobjattr(err, 145, string (IrsId:o:34), 4, 1, null, null, strhi.OperDate);
   If(valtype(strhi.EndDateOHAsudr) != 0)
   //   connectobjattr(err, 145, string (IrsId:o:34), 4, 2, null, null, date(strhi.EndDateOHAsudr)+1);
   end;

   If(objkind == 103)
   //	connectobjattr(err, objkind, string (ID:o:34), 11, 1, null, null, strhi.OperDate);
      If(valtype(strhi.EndDateOHAsudr) != 0)
   //   connectobjattr(err, objkind, string (ID:o:34), 11, 2, null, null, date(strhi.EndDateOHAsudr)+1);
      end;
   elif((objkind == 12) OR (objkind == 24))
      If(objkind == 12)
         KindCateg = 68;
      elif(objkind == 24)
      	 KindCateg = 6;
      end;
   //  connectobjattr(err, objkind, string (ID:o:10), KindCateg, 1, null, null, strhi.OperDate);
      If(valtype(strhi.EndDateOHAsudr) != 0)
   //   connectobjattr(err, objkind, string (ID:o:10), KindCateg, 2, null, null, date(strhi.EndDateOHAsudr) + 1);
      end; 
   end;
   return true;
end;


Private Macro GetIrsDealID (code, err:@string)
   var cmd, rs ;
   err= "";
   cmd = RsdCommand(" select deal.*, case when exists (select 1 from ddlhedgeinstr_dbt where t_dealid = deal.t_id ) then 1 else 0 end as hedg from ddvndeal_dbt deal where deal.t_code = :code "); 

   cmd.addParam("code", RSDBP_IN, code);
   cmd.execute();
  
   rs = RsdRecordset(cmd);

   if(rs.MoveNext())
      If(rs.value("t_state") == 2)
         Err = "Cделка СВОП c кодом " + code + " закрыта. Невозможно установить отношения хеджирования";
         return -1;
      elif (rs.value("hedg") == 1)
         Err = "По сделке СВОП c кодом " + code + " уже установлены отношения Хеджирования. Повторная загрузка невозможна.";
         return -1;
      else
         return rs.value("t_id");
      end;
   else
      Err = "Не определена сделка СВОП по коду " + code;
      return -1;
   end;
end;

Private Macro GetVekselDate(SerNumVeks, ObjectID:@integer, ObjKind:@integer, err:@string, portfolio:@string)
   var cmd, rs ;
   Var Ser, Num;
   err = "";

   num = substr(SerNumVeks, 1, strlen(SerNumVeks) -(strlen(SerNumVeks) - index(SerNumVeks, "-")) - 1);
   ser = substr(SerNumVeks, index(SerNumVeks, "-") + 1);
   ObjectID = -1;
   ObjKind = -1;

   cmd = RsdCommand(" select " + 
                    " (case when exists( select 1 from ddp_dep_dbt where t_partyid = bnr.t_issuer) Then 1 else 0 end) as IsSobstv," +
                    " bnr.* from dvsbanner_dbt bnr where bnr.t_bcseries = :seria and Trim(bnr.t_bcnumber) = :num"); 

   cmd.addParam("seria", RSDBP_IN, Ser);
   cmd.addParam("num", RSDBP_IN, num);

   cmd.execute();
  
   rs = RsdRecordset(cmd);

   if(rs.MoveNext())
      ObjectID = rs.value("t_bcid");
      If(rs.value("IsSobstv") == 1)
         ObjKind = 4;
      else
         ObjKind = 3;
      end;
      If(rs.value("t_BcStatus") == 30)
         err= "Ошибка загрузки данных по ОХ. Вексель, определенный по комбинации номер-серия " + SerNumVeks + " погашен";
         ObjectID = -1;
         ObjKind = -1;
      end;
      if (ObjectID > 0)
         if (rs.value("t_portfolioid") > 0)   // см. справочник Портфели (dllvalue_dbt t_list = 1100)
            if (rs.value("t_portfolioid") == KINDPORT_DB_AMRTCOST) // Учтенные векселя, оцениваемые по амортизированной стоимости
               portfolio = "АС";
            elif (rs.value("t_portfolioid") == KINDPORT_OWNB_OTHER_CMPR_INCOME)  // Собственные векселя, оцениваемые по амортизированной стоимости
               portfolio = "АС";
            elif (rs.value("t_portfolioid") == KINDPORT_DB_OTHER_CMPR_INCOME) // Учтенные векселя, оцениваемые по СС через прочий совокупный доход
               portfolio = "СССД";
            elif (rs.value("t_portfolioid") == KINDPORT_DB_PROFIT_LOSS) // Учтенные векселя, оцениваемые по СС через прибыль или убыток
               portfolio = "ССПУ";
            elif (rs.value("t_portfolioid") == KINDPORT_OWNB_PROFIT_LOSS) // Собственные векселя, оцениваемые по СС через прибыль или убыток 
               portfolio = "ССПУ";
            else 
               portfolio = rs.value("t_portfolioid");
            end;
         else 
            portfolio = "АС";
         end;
      end;
   else
      err= "Ошибка загрузки данных по ОХ. Не удалось определить вексель по комбинации номер-серия " + SerNumVeks;
      ObjectID = -1;
      ObjKind = -1;
   end;
onerror(RslErrObj)

End;


Private Macro GetAvoirDate(ISINCode, ObjectID:@integer, ObjKind:@integer, err:@string)
   var cmd, rs ;
   err = "";
   cmd = RsdCommand(" select avr.*, fin.* from davoiriss_dbt avr, dfininstr_dbt fin where avr.t_isin = :isin and fin.t_fiid = avr.t_fiid"); 

   cmd.addParam("isin", RSDBP_IN, ISINCode);
   cmd.execute();
  
   rs = RsdRecordset(cmd);

   if(rs.MoveNext())
      ObjectID = rs.value("t_fiid");
      If(rs.value("t_issuer") == {ourbank})
         ObjKind = 2;
      else
         ObjKind = 1;
      end;

      If(rs.value("t_isclosed") == "X")
         err= "Ошибка загрузки данных по ОХ. Выпуск ценных бумаг с кодом " + ISINCode + " закрыт";
         ObjectID = -1;
         ObjKind = -1;
      end;
   else
      err= "Ошибка загрузки данных по ОХ. Не удалось определить выпуск ценных бумаг по коду " + ISINCode;
      ObjectID = -1;
      ObjKind = -1;
   end;
End;

Private Macro GetMBKDealDate(MBKDealCode, ObjectID:@integer, ObjKind:@integer, err:@string, portfolio:@string)
  var cmd, rs ;
  err = "";
  ObjKind = -1;
  cmd = RsdCommand(" select * from ddl_tick_dbt where t_bofficekind = 102 and t_dealcode = :dealcode    "); 

  cmd.addParam("dealcode", RSDBP_IN, MBKDealCode);
  cmd.execute();
  
  rs = RsdRecordset(cmd);

  if(rs.MoveNext())
     ObjectID = rs.value("t_dealid");
     If((rs.value("t_dealtype") == 2300) OR (rs.value("t_dealtype") == 12300) OR (rs.value("t_dealtype") == 12301) OR (rs.value("t_dealtype") == 12310) )
        ObjKind = 6;
     elif((rs.value("t_dealtype") == 12305) OR (rs.value("t_dealtype") == 12306) OR (rs.value("t_dealtype") == 12308) OR (rs.value("t_dealtype") == 12318) OR (rs.value("t_dealtype") == 12335))
        ObjKind = 5;
     else 
        // DEF-47627 нельзя оставлять без типа, создается как будто пустая строка, которая не пустая
        if (isSale(GetOperationGroup(rs.value("t_dealtype"))))  //размещение
          ObjKind = 5;
        else    //привлечение
          ObjKind = 6;
        end;
     end;
     If(rs.value("t_dealstatus") == 20)
         ObjKind = -1;
         ObjectID = -1;
         err = "Ошибка загрузки данных по ОХ. Сделка МБК с кодом " + MBKDealCode + " закрыта";
     end;
     if (ObjectID > 0)
        var ObjCat = RsbObjCategories(103, string(ObjectID:34:o));
        if (ObjCat.GetMainAttr( 5, date(), attr, attrcor ) == 0)   // категория "Категория оценки"
           if (attrcor.attrid == 1)    
              portfolio = "АС";
           elif (attrcor.attrid == 2)    
              portfolio = "АС";
           elif (attrcor.attrid == 3)    
              portfolio = "ССПУ";
           elif (attrcor.attrid == 4)    
              portfolio = "СССД";
           else
              portfolio = "";
           end;
        end;
        ObjCat = NULL;
     end;
  else
     err = "Ошибка загрузки данных по ОХ. Не удалось определить сделку МБК по коду " + MBKDealCode;
     ObjectID = -1;
     ObjKind = -1;
  end;

End;


Private Macro GetOXParm(Kind, ID, ObjectID:@integer, ObjKind:@integer, err:@string, portfolio:@string);
   err = "";
   If(strupr(Kind) == "МБК")
      GetMBKDealDate(ID, @ObjectID, @ObjKind, @err, @portfolio);
   elIf(strupr(Kind) == "ОБЛИГАЦИЯ")
      GetAvoirDate(ID, @ObjectID, @ObjKind, @err);
   elIf(strupr(Kind) == "ВЕКСЕЛЬ")
      GetVekselDate(ID, @ObjectID, @ObjKind, @err, @portfolio);
   elIf((strupr(Kind) == "ДЕПОЗИТ") OR (strupr(Kind) == "КРЕДИТ") OR (strupr(Kind) == "LOAN") OR (strupr(Kind) == "DEPOSIT"))
      ObjectID = 0;
      If((strupr(Kind) == "КРЕДИТ") OR (strupr(Kind) == "LOAN"))
         ObjKind = 7;
      else
         ObjKind = 8;
      end;
   else
      ObjectID = -1;
      ObjKind = -1;
   end;
End;


//Расположение загружаемого файла
 var  reg_path, errcode;
 GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\ИМПОРТ_ХЕДЖИРОВАНИЕ", V_STRING, reg_path, errcode );
 if ( errcode != 0 )
    reg_path = "";
 end; 
 var  arch_path;                                               
 GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\ИМПОРТ_ХЕДЖИРОВАНИЕ_АРХИВ", V_STRING, arch_path, errcode );
 if ( errcode != 0 )
    arch_path = "";
 end; 

 var  err_path;                                               
 GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\ИМПОРТ_ХЕДЖИРОВАНИЕ_ОШИБКИ", V_STRING, err_path, errcode );
 if ( errcode != 0 )
    err_path = "";
 end; 


 //Загрузка данных из Excel

macro OpLoadFile(FileName, TermPath, RepDate)
   var frow = 1;
   var row = 1;
   var StRecIH, sheet, sheetcount;
   var all = 0,suc = 0, err = 0;
   var ErrStr = "",ErrStat = false, AllErrStr = "", curerr = "";
   Var IrsDealCode = "";
   Var ObjectID = -1, ObjKind = -1, IrsDealId = -1;
   var check_stat = true;

//BIQ-10394
   var portfolio = "";
   
  /*Открываем файл*/
   BegAction(1, "Обработка файла \"" + TermPath + "\\" + FileName + "\". Ждите...");
   if(not OpenExcelFile(FileName, false, true, TermPath))
      MsgBox("Ошибка открытия файла \"" + TermPath + "\\" + FileName + "\"");
      EndAction(1);
      return 0;
   end;

   sheetcount = 1;
   WHILE(ExcelApplication.Sheets.count >= sheetcount)
      sheet = ExcelApplication.Sheets(sheetcount);
        
      frow = 1;
      row = 1;

      row = frow;
      //Бежим по файлу
      InitProgress(-1);
      while (valtype(sheet.cells(row, COL1).value) != V_UNDEF) 
         ErrStat = false;
         if (strupr(sheet.cells(row, COL1).value) == "ДАТА ОПЕРАЦИОННОГО ДНЯ") 
            row = row + 1;
            continue; 
         end;

         all = all + 1;

         StRecIH = StatusRecIH(); 
         StRecIH.OperDate       = TrimStr(sheet.cells(row, COL1).value);
         StRecIH.IdIhSOFR       = TrimStr(sheet.cells(row, COL2).value);
         StRecIH.IdIhAsudr       = TrimStr(sheet.cells(row, COL3).value);

         StRecIH.Portf       = TrimStr(sheet.cells(row, COL4).value);
         StRecIH.SubPortf       = TrimStr(sheet.cells(row, COL5).value);

         // BIQ-10394
         if (StRecIH.SubPortf == "FV_HEDGE" )
            StRecIH.Relationtype = 1;           // Тип отношений хеджирования = Хеджирование СС
         elif (StRecIH.SubPortf == "CF_HEDGE" )
            StRecIH.Relationtype = 2;           // Тип отношений хеджирования = Хеджирование ДП
         else 
            StRecIH.Relationtype = 0;           // других типов не предусмотрено
            ErrStat = true;
            ErrStr = "Неизвестный субпортфель " + StRecIH.SubPortf;
            AllErrStr = AllErrStr + ErrStr + "\n";
         end;

         StRecIH.Val       = TrimStr(sheet.cells(row, COL6).value);
         If(valtype(sheet.cells(row, COL7).value) != V_STRING)
            StRecIH.IDOHSOFR       = string(sheet.cells(row, COL7).value:0:0);
         else
            StRecIH.IDOHSOFR       = sheet.cells(row, COL7).value;
         end;
         //StRecIH.IDDealOHAsudr     = TrimStr(sheet.cells(row, COL8).value);
         StRecIH.KindIH            = TrimStr(sheet.cells(row, COL9).value);
         StRecIH.KindDealOH        = TrimStr(sheet.cells(row, COL10).value);
         StRecIH.IDHedgRel         = TrimStr(sheet.cells(row, COL11).value);
         StRecIH.EndDateOHAsudr    = TrimStr(sheet.cells(row, COL12).value);
         StRecIH.Portfolio         = TrimStr(sheet.cells(row, COL8).value);
        
         // BIQ-10394
         if (StRecIH.Portfolio == "АС" )
         elif (StRecIH.Portfolio == "СССД" )
         elif (StRecIH.Portfolio == "ССПУ" )
         else 
            // других типов не предусмотрено
            ErrStat = true;
            ErrStr = "Неизвестный учетный портфель " + StRecIH.Portfolio;
            AllErrStr = AllErrStr + ErrStr + "\n";
         end;

         IrsDealCode = StRecIH.IdIhSOFR; 
         If(IrsDealCode != "")
            IrsDealId = GetIrsDealID(IrsDealCode, @curerr);
            If(IrsDealid == -1)
               ErrStat = true;
               ErrStr = curerr;
               AllErrStr = AllErrStr + ErrStr + "\n";
            end;
         end;
         
         portfolio = "";
         GetOXParm(StRecIH.KindDealOH, StRecIH.IDOHSOFR, @ObjectID, @ObjKind, @curerr, @portfolio);
         If(ObjectID == -1)
            ErrStat = true;
            ErrStr = curerr;
            AllErrStr = AllErrStr + ErrStr + "\n";
         end;

         if (portfolio)  // портфель задан не у всех объектов
            if (portfolio != StRecIH.Portfolio)
               ErrStat = true;
               ErrStr = "Учетный портфель для ОХ в файле "+StRecIH.Portfolio+" не соответствует учетному портфелю объекта "+portfolio+" в СОФР!";
               AllErrStr = AllErrStr + ErrStr + "\n";
            end;
        end;

         // DEF-41212: BIQ-10394. Не заполняется СС по денежным потокам если несколько отношений хеджирования
         check_stat = CheckExistingIDHedgRel(StRecIH.IDHedgRel, ObjectID, @curerr);
         if (not check_stat)
            ErrStat = true;
            ErrStr = curerr;
            AllErrStr = AllErrStr + ErrStr + "\n";
         end;

         If(not errstat)
            rslDefCon.beginTrans ();
            InsertTablehedgeinstr(StRecIH, IrsDealId, ObjectID, ObjKind);
            rslDefCon.commitTrans ();
         end;
         UseProgress(row);

         ErrStr = "";
         ErrStat = false;
         ObjectID = 0;
         ObjKind = 0;
         row = row + 1;

      end; //while
      RemProgress();
      If(AllErrStr != "")
         PrintLn("Внимание! В процессе загрузки файла " + FileName + " произошли следующие ошибки :");
         Println();
         PrintLn(AllErrStr);
         Println();
         Println("Файл " + FileName + " будет перемещен по пути : " + err_path);
      end;
     sheetcount = sheetcount+1;
   end;//eow sheet
   ExcelApplication.ActiveWorkbook.Close;

   If(AllErrStr != "")
      SendErrMsg(AllErrStr, FileName, CV_EMAIL_GRP_HEDG);
      return 1;
   else
      Println("Файл " + FileName + " обработан успешно");
      SendGoodMsg("", FileName, CV_EMAIL_GRP_HEDG);
      return 0;
   end;

onerror(er)
   if ( rslDefCon.isInTrans () )
      rslDefCon.rollbackTrans ();
   end;
   if (valtype(ExcelApplication) != V_UNDEF)
      ExcelApplication.ActiveWorkbook.Close;
   end;
   EndAction(1);
   PrintLn("Внимание! В процессе загрузки файла " + FileName + " произошли следующие ошибки :" + er.message);

   SendErrMsg(er.message, FileName, CV_EMAIL_GRP_HEDG);

   return 0;
end;


macro OpLoadIH(RepDate)

  var DealID_Excel:string;
  var DealCode:string = "";
  var FullNameFile = "";
  var FileName = "";
  var TermPath = GetCurDir(true) + "\\TxtFile";
  var Userpath = "";
  var DirName = "";
  var ExtName = "";
  var valuedate:date =  Date(0, 0, 0);
  var ss = $0;
  var str;
  var DealID,FrVal = 0;
  var day = date();
  var i = 0, WIthErr = 0;
  var dt = StrSubSt(string(Date()),".","");
  /*Выбираем файл для обработки*/
  
  var List = TDirList(reg_path + "\FI_IH_" + GetRepDate(RepDate) + "*.xlsx", "F");
  
  while( i < List.Count )
     FullNameFile = List.Name(i);
     DirName = splitfile(FullNameFile, FileName, ExtName);
     FileName = FileName + ExtName;

     if (not copyfile("$" + reg_path + FileName, "$" + TermPath + "\\" + FileName) )
       MsgBox("Ошибка при передаче файла на терминал");
     end;
     WIthErr = OpLoadFile(FileName, TermPath, RepDate);
     If(WIthErr)
        copyFromTo(reg_path + FullNameFile, "$" + err_path+"\\");
     else
        copyFromTo(reg_path + FullNameFile, "$" + arch_path+"\\");
     end;

     i = i + 1;
  end;

End;


 if( not IsShedulerRunning())
   If( not GetDate(Repdate, "Введите дату загрузки"))
      exit(1);
   end;
   OpLoadIH(Repdate);
 end;
