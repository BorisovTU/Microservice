/*
$Name:         vssetnumbers.mac
$Module:       Векселя банка
$Description:  Присвоение номеров векселей. Общая часть, выполняемая в операциях эмиссии и мены
*/

import oralib, likepy, VSInter;
private const VS_DOCKIND_EMIS = 109;

private macro _AddCol(ar, ind, fld, head, width, fldType, decPoint)
  ar.value( ind * 6 + 0 ) = fld;
  ar.value( ind * 6 + 1 ) = head;
  ar.value( ind * 6 + 2 ) = width;
  ar.value( ind * 6 + 3 ) = fldType;
  ar.value( ind * 6 + 4 ) = decPoint;
  ar.value( ind * 6 + 5 ) = 0;   // reserv
end;

macro ИзменитьНомераВекселей (order)
  var col = TArray();
  var sql;

  sql = "select t_bcseries, "
      + "       trim(t_bcnumber), "
      + "       t_issuername, "
      + "       t_bcid "
      + "From dvsbanner_dbt "
      + "where t_bcid in (select t_bcid from dvsordlnk_dbt where t_contractid = :contractid and t_dockind = :dockind and t_linkkind = 0) ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("contractid", order.ContractID), SqlParam("dockind", order.DocKind)), false, RSDVAL_CLIENT, RSDVAL_STATIC);

  macro ScrollProc(sql, cmd, id, key)
    if(cmd == DLG_KEY)
      if(key == 13) //enter
        if (id != 1)
          return CM_IGNORE;
        end;
      elif (key == 322) //F8
        return CM_IGNORE;
      elif (key == 323) //F9
        return CM_CANCEL;
      end;
    end;
  end;

  _AddCol ( col, 0, "t_bcseries", "Серия", 15 );
  _AddCol ( col, 1, "t_bcnumber", "Номер", 15 );
  _AddCol ( col, 2, "t_issuername", "Векселедатель", 25 );

  if (RunScroll (sql, col.size/6, col, "users_scroll", "ScrollProc", "Список типов бланка", "ESC Выход  F4 Поиск", false, 50, 0, 85, 40))
    ;
  end;

  return true;
End;

macro ПроверитьУникальностьВекселей (order)
  var stat = true;
  var sql, query;

  sql = "select t_bcseries, t_bcnumber, t_issuer, t_bcid from dvsbanner_dbt "
      + "where t_bcid in (select t_bcid from dvsordlnk_dbt where t_contractid = :contractid and t_dockind = :dockind and t_linkkind = 0) ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("contractid", order.ContractID), SqlParam("dockind", order.DocKind)), false);

  while ((sql.MoveNext()) and stat )
    query = "select 1 from dvsbanner_dbt where trim(t_bcseries) = :bcseries and trim(t_bcnumber) = :bcnumber and t_bcid <> :bcid ";
    query = ExecSqlSelect(query, MakeArray(SqlParam("bcseries", Trim(sql.value("t_bcseries"))),
                                           SqlParam("bcnumber", Trim(sql.value("t_bcnumber"))),
                                           SqlParam("bcid", sql.value("t_bcid"))), false);
    if (query.MoveNext() )
      stat = false;
      MsgBox("Вексель серия " + sql.value("t_bcseries") + " номер " + sql.value("t_bcnumber") + " не уникален в системе");
    end;
  end;

  return stat;
End;

macro ОбнулитьНомераВекселей (order)
  var sql;

  sql = "update dvsbanner_dbt "
      + "set t_bcnumber = chr(1) "
      + "where t_bcid in (select t_bcid from dvsordlnk_dbt where t_contractid = :contractid and t_dockind = :dockind and t_linkkind = 0) ";
  sql = ExecSql(sql, MakeArray(SqlParam("contractid", order.ContractID), SqlParam("dockind", order.DocKind)), false);
End;

/************************************************************************************************************/
//Новая версия

//открывает скроллинг векселей, привязанных к договору
macro ВыбратьНомерИзСписка (order)
  var sql, number = "";
  var col = TArray();

  sql = "select vsbanner.t_bcseries bcseries, "
      + "       Trim(vsbanner.t_bcnumber) bcnumber "
      + "from dvsordlnk_dbt lnk, dvsbanner_dbt vsbanner "
      + "where t_contractid = :contractid "
      + "      and t_linkkind = 0 "
      + "      and t_dockind = :dockind "
      + "      and lnk.t_bcid = vsbanner.t_bcid ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("contractid", order.ContractID), SqlParam("DocKind", order.DocKind)), false, RSDVAL_CLIENT, RSDVAL_STATIC);

  macro ScrollProc(sql, cmd, id, key)
    if(cmd == DLG_KEY)
      if(key == 13) //enter
          return CM_SELECT;
      end;
    end;
  end;

  _AddCol ( col, 0, "bcseries", "Серия", 15 );
  _AddCol ( col, 1, "bcnumber", "Номер", 15 );

  if (RunScroll (sql, col.size/6, col, "users_scroll", "ScrollProc", "Список векселей", "ESC Выход  F4 Поиск", true, 50, 0, 85, 40))
    number = Trim(sql.value("bcnumber", null, V_STRING));
  end;
  return number;
End;

//Проверка, привязан ли к договорку order вексель с номером bcnum
macro ПроверитьНомерНаНаличиеВДоговоре (bcnum, order)
  var sql;
  sql = "select 1 "
      + "from dvsbanner_dbt vsbanner, dvsordlnk_dbt lnk "
      + "where     Trim(vsbanner.t_bcnumber) = :bcnumber "
      + "      and vsbanner.t_bcid = lnk.t_bcid "
      + "      and lnk.t_linkkind = 0 "
      + "      and lnk.t_dockind = :dockind "
      + "      and lnk.t_contractid = :contractid ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("bcnumber", bcnum),
                                     SqlParam("dockind", order.DocKind),
                                     SqlParam("contractid", order.ContractID)), false);
  if (sql.MoveNext() )
    return true;
  end;
  return false;
End;

//Проверяет наличие бланка с номером blanknum в филиале и его состояние
macro НаличиеБланка (blanknum)
  var sql, stat = true;
  sql = "select t_status "
      + "from dvsform_dbt "
      + "where     Trim(t_number) = :blanknum "
      + "      and t_department = :dep ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("blanknum", blanknum),
                                     SqlParam("dep", {NumDprt})), false);
  if (not sql.MoveNext() )
    stat = false;
  elif (sql.value("t_status", null, V_INTEGER) != VSFORM_STATUS_ADVANCE)
    stat = false;
  end;
  return stat;
End;

//проверяет, существует ли вексель с нумером vsnum
macro IsExistVs (vsnum)
  var sql;
  sql = "select 1 from dvsbanner_dbt where Trim(t_bcnumber) = :bcnum";
  if (ExecSqlSelect(sql, MakeArray(SqlParam("bcnum", vsnum)), false).MoveNext() ) 
    return true;
  end;
  return false;
End;

//Изменяет номер векселя OldNum на Newnum
macro ИзменитьНомер (OldNum, NewNum)
  var sql, stat = true;
  sql = "update dvsbanner_dbt "
      + "set t_bcnumber = lpad(:bcnewnum, 20, ' ') "
      + "where Trim(t_bcnumber) = :bcoldnum ";
  sql = ExecSql(sql, MakeArray(SqlParam("bcnewnum", NewNum),
                               SqlParam("bcoldnum", OldNum)), false);
  if (sql == null)
    stat = false;
  end;
  return stat;
End;

  /********************************************************/
  /*                                                      */
  /*   Старый номер                  Новый номер          */
  /*   _____________                ____________          */
  /*   |___________|                |___________|         */
  /*                                                      */
  /*                                                      */
  /********************************************************/
macro ИзменитьНомерВекселя (order)
  var OldNum, NewNum;
  var stat = 0;

  record dlg (VSUPDNUM, "rshb_docs.lbr") dialog;

  macro EvProc90(dlg, cmd, id, key )
    var party;
    if ( DLG_INIT == cmd )
      message("~ESC~ выход  F3 открыть список F9 запуск");
    elif (DLG_KEY == cmd)
      //f3
      if (key == 317)
        if (FldIndex(dlg, "OldNum") == id)
          dlg.OldNum = ВыбратьНомерИзСписка(order);
        end;
        updatefields(dlg);
      end;
    end;
  END;
  
  dlg.OldNum = "";
  dlg.NewNum = "";

  if (RunDialog (dlg, @EvProc90) == false)
    return 1;
  end;

  OldNum = Trim(dlg.OldNum);
  NewNum = Trim(dlg.NewNum);

  if (not ПроверитьНомерНаНаличиеВДоговоре(OldNum, order) )
    MsgBox("Вексель с номером " + OldNum + " не найден в договоре");
    stat = 1;
  elif (IsExistVs(NewNum))
    MsgBox("Вексель с номером " + NewNum + " уже существует");
    stat = 1;
  elif (not НаличиеБланка(NewNum))
    MsgBox("Бланк с номером " + NewNum + " и в статусе \"Под отчетом\" не найден в справочнике");
    stat = 1;
  elif (not ИзменитьНомер(OldNum, NewNum))
    MsgBox("Ошибка при изменении номера векселя");
    stat = 1;
  end;

  return stat;
End;