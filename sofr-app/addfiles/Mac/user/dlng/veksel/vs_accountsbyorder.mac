/*
vs_accountsbyorder.mac
*/
import or_rep_h, oralib, likepy, vsprrvp, CbForms_h;

private var sqlString = "";

//-----------------------------------------------------------------------------------------
private macro collectOutProc ( str )
  sqlString = sqlString + "\n" + trim (str);
End;

private macro CaptureOutput
  sqlString = "";
  SetOutHandler (@collectOutProc);
End;
                     
private macro StopCaptureOutput
  SetOutHandler ();
  return sqlString;
End;
//-----------------------------------------------------------------------------------------

//Формирует Excel со всеми счетами, привязанными к векселям договора
macro PrintAccounts (ContractID, DocKind, dt)
  var Rep, sql;
  var printEngine, csvTable, csvFile, fName;
  var rn = 0;

  printEngine = CTemplateSXLSX(NULL);
  
  if(printEngine.UsePoi())
    printEngine.SetXLSXFormat();
  end;

  printEngine.CreateTotalBook();

  printEngine.OpenTemplate("vsscv.xlsx", true);
  printEngine.SheetChange(1);

  csvTable = printEngine.RegisterTable("templateTable", "templateTableHeader", true);

  csvFile  = C_CSVFile();

  if (printEngine.UsePoi())
      csvFile.SetLimitRow(FLUSH_COUNT_XLSX);
  end;

  fName = csvFile.CreateFullFileName("vsscv_csv.txt");
  csvFile.FileOpen(fName, true);

  rn = rn + 1;

  CaptureOutput();
  [with parm as (select :dt t_reportdate from dual) 
   select trim(bnr.t_bcseries) || ' ' || trim(bnr.t_bcnumber) sernum, 
         /*Счет учета вложений в вексель*/ 
         nvl((select t_account from dmcaccdoc_dbt mcacc
          where     t_dockind = 164 /*Первичный документ - Вексель*/ 
                and t_docid = bnr.t_bcid 
                and t_catnum = case when (select nvl(min(t_changedate), to_date('31.12.9999', 'dd.mm.yyyy')) 
                                          from DVSBNRBCK_DBT 
                                          where instr(t_newbcstate, 'И') > 0 and t_bcid = t_docid) <= (select t_reportdate from parm) 
                                    then 451 /*КУ - СВексель к исполнению*/ 
                                    else 450 /*КУ - Наш вексель*/ 
                               end
                and t_periodid = (select min(t_periodid)
                                  from dmcaccdoc_dbt m
                                  where     m.t_dockind = mcacc.t_dockind
                                        and m.t_docid = mcacc.t_docid
                                        and m.t_catnum = mcacc.t_catnum
                                        and m.t_iscommon = mcacc.t_iscommon)
                /* Чтобы показывать уже закрытый счет 52406 из операции погашения
                and ((select t_reportdate from parm) between t_activatedate and t_disablingdate-1 or  
                    (t_disablingdate = to_date('01.01.0001', 'dd.mm.yyyy') and t_activatedate <= (select t_reportdate from parm) and t_isusable = chr(88)))*/
                and t_iscommon = chr(0)), chr(1)) body_acc, 
         /*Счет учета начисленных процентов/дисконта*/ 
         nvl((select t_account from dmcaccdoc_dbt 
              where     t_dockind = 164 
                    and t_docid = bnr.t_bcid 
                    and t_catnum = case when leg.t_formula = 1 then 452 /*КУ - Обяз%,СВексель*/ else 1384 /*КУ - Дисконт, Свексель*/ end
                    and ((select t_reportdate from parm) between t_activatedate and t_disablingdate-1 or  
                        (t_disablingdate = to_date('01.01.0001', 'dd.mm.yyyy') and t_activatedate <= (select t_reportdate from parm) and t_isusable = chr(88)))
                    and t_iscommon = chr(0)), chr(1)) pdd_acc, 
         /*Корректировки, уменьшающие стоимость*/ 
         nvl((select t_account from dmcaccdoc_dbt 
              where     t_dockind = 164 
                    and t_docid = bnr.t_bcid 
                    and t_catnum = 1381 /*КУ - +Корр, Свексель*/  
                    and ((select t_reportdate from parm) between t_activatedate and t_disablingdate-1 or  
                        (t_disablingdate = to_date('01.01.0001', 'dd.mm.yyyy') and t_activatedate <= (select t_reportdate from parm) and t_isusable = chr(88))) 
                    and t_iscommon = chr(0)), chr(1)) kor_down_acc, 
         /*Корректировки, увеличивающие стоимость*/ 
         nvl((select t_account from dmcaccdoc_dbt 
              where     t_dockind = 164 
                    and t_docid = bnr.t_bcid 
                    and t_catnum = 1380 /*КУ - -Корр, Свексель*/ 
                    and ((select t_reportdate from parm) between t_activatedate and t_disablingdate-1 or  
                        (t_disablingdate = to_date('01.01.0001', 'dd.mm.yyyy') and t_activatedate <= (select t_reportdate from parm) and t_isusable = chr(88)))  
                    and t_iscommon = chr(0)), chr(1)) kor_up_acc 
  from dvsordlnk_dbt lnk, dvsbanner_dbt bnr, ddl_leg_dbt leg 
  where     lnk.t_contractid = :contractid 
        and lnk.t_bcid = bnr.t_bcid 
        and lnk.t_dockind = :dockind 
        and leg.t_dealid = bnr.t_bcid 
        and leg.t_legkind = 1 
        and leg.t_legid = 0];

  sql = ExecSqlSelect(StopCaptureOutput(), MakeArray(SqlParam("", dt), SqlParam("contractid", ContractID), SqlParam("dockind", DocKind)));

  while (sql.MoveNext() )
    rn = rn + 1;
    CSVFile.AddCell(sql.value("sernum")      );
    CSVFile.AddCell(sql.value("body_acc")    );
    CSVFile.AddCell(sql.value("pdd_acc")     );
    CSVFile.AddCell(sql.value("kor_down_acc"));
    CSVFile.AddCell(sql.value("kor_up_acc")  );
    CSVFIle.AddRow();
  end;

  csvFile.FileClose();
  csvTable.FillTabelFromCSV(csvFile.GetlsFileName());
  csvTable.EndTable();
  
  printEngine.CopyAllSheetInTotalBook(null,         //Имя закладки,по умолчанию имя закладки из шаблона
                                      false, // true - на новый лист, false - на существующий(если имя листа отличается, всегда на новый лист)
                                      csvTable.GetTableRange(),     //Что копировать м.б. диапозон вида "A1:T60" или имя именованного диапозона
                                      0) ;

  printEngine.Close();
  printEngine.SaveTotalBook();
End;

private macro GetAccounts (operarray)
  var sql, i = 0, AccountsArr = TArray();
  var query = "select to_number(substr(t_documentid, 0, 2)) t_chapter, "
            + "       to_number(substr(t_documentid, 3, 7)) t_fiid, "
            + "       substr(t_documentid, 10) t_account "
            + "from doprdocs_dbt "
            + "where t_id_operation = :id_oper "
            + "and t_id_step = :id_step "
            + "and t_dockind = :dockind ";

  while (i < operarray.size)
    sql = ExecSqlSelect(query, MakeArray(SqlParam("id_oper", operarray(i).id_oper), SqlParam("id_step", operarray(i).id_step), SqlParam("dockind", DLDOC_ACCOUNT)));
    while ( sql.MoveNext() )
      AccountsArr(AccountsArr.size) = "'"+string(sql.value("t_account")) + "'";
    end;
    i = i + 1;
  end;
  return AccountsArr;
End;

//Отчет выводит список счетов и векселей по операции переноса по срокам
macro vs_accounts_tr ()
  var AccountsArr = GetAccounts(RvpHelperObj.operarray);
  var sql;

  if (AccountsArr.Size == 0)
    return;
  end;

  sql = " select (select trim(t_name) from dparty_dbt where t_partyid = b.t_issuer) t_issuer, "
      + "       trim(b.t_bcseries) t_bcseries, "
      + "       trim(b.t_bcnumber) t_bcnumber, "
      + "       m.t_account "
      + "from dmcaccdoc_dbt m, dvsbanner_dbt b "
      + "where m.t_dockind = 164 and m.t_docid = b.t_bcid "
      + "and t_account in ("+join(AccountsArr, ",")+") "
      + "order by t_bcid  ";

  Dl_Scroll(sql, "Счета, открытые по операции", 10, 5,
           "t_account", "Счет",
           "t_bcseries", "Серия",
           "t_bcnumber", "Номер",
           "t_issuer", "Филиал");
onerror();
  MsgBox("Ошибка при поиске счетов");
  return;
End;