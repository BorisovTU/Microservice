/*
vsnewbanner.mac
Макрос по созданию новых, одинаковых векселей в договоре выдачи.
За шаблон берется вексель, уже прикрепленный к договору
*/
import oralib, likepy;

private var Bnr      = TBFile ( "vsbanner", "W");
private var Leg      = TBFile ( "dl_leg", "W" );
private var VsOrdlnk = TBFile ( "vsordlnk", "W" );
private var Prc      = TBFile ( "prccontract", "W" );
private var amount   = 0;
private var BCID     = 0;
private var order;

//Проверка
macro Check ()
  var stat = true;
  var sql;

  //Проверка договора
  if (order.ContractID == -1)
    MsgBox("Сначала сохраните договор!");
    stat = false;
  elif (order.ContractStatus != 0)
    MsgBox("Договор должен быть в статусе \"Отложен\"!");
    stat = false;
  end;

  //Проверка прикрепленных векселей
  if (stat)
    sql = ExecSqlSelect("select count(*) over() cnt, t_bcid from dvsordlnk_dbt where t_contractid = :cid", MakeArray(SqlParam("cid", order.ContractID)), false);
    if (sql.MoveNext())
      BCID = sql.value("t_bcid", null, V_INTEGER);
      sql = sql.value("cnt", null, V_INTEGER);

      if (sql > 1)
        MsgBox("Прикрепленных к договору векселей больше одного! ||Продолжение не возможно!");
        stat = false;
      end;
    end; //if (sql.MoveNext())
  end; //if (stat == 0)

  //Если векселея не нашли
  if (BCID == 0)
    MsgBox("Не найден вексель, прикрепленный к договору");
    stat = false;
  end;

  return stat;
onerror()
  return false;
End;

//
macro ЗапуститьДиалог ():integer
  record dlg (VSEMIS, "rshb_docs.lbr") dialog;

  macro DlgProc(dlg, cmd, id, key )
    var party;
    if ( DLG_INIT == cmd )
      message("~ESC~ выход");
    elif (DLG_KEY == cmd)
      updatefields(dlg);
    end;
  END;
  
  dlg.count = 0;

  if (RunDialog (dlg, @DlgProc) == false)
    return 0;
  end;

  return dlg.count;
End;

//Инициализировать основную информацию по векселю
macro InitMainData ()
  var sql, stat = true;

  Bnr.rec.BCID = BCID;

  Leg.rec.DealID  = BCID;
  Leg.rec.LegID   = 0;
  Leg.rec.LegKind = 1;

  VsOrdlnk.rec.BCID       = BCID;
  VsOrdlnk.rec.ContractID = order.ContractID;
  VsOrdlnk.rec.DocKInd    = order.DocKind;
  VsOrdlnk.rec.LinkKind   = 0;

  if ((not Bnr.GetEQ()) or (not Leg.GetEQ()) or (not VsOrdlnk.GetEQ()) )
    stat = false;
  end;

  //if (Leg.rec.Formula == 1) //Процентный

  //Нет никакой уверенности, что это правильный запрос
  sql = "select t_contractid from dprccontract_dbt "
      + "where     t_contracttype = 1013 " /*?!*/
      + "      and t_objecttype = 651 "
      + "      and t_objectid = :bcid ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("bcid", BCID)), false);
  if (sql.MoveNext() )
    Prc.rec.ContractID = sql.value("t_contractid", null, V_INTEGER);
    Prc.GetEQ();
  end;
  //end;

  return stat;
onerror()
  return false;
End;

//Создать вексель
macro CreateVS ()
  var NewBnr, NewLeg, NewVsOrdLnk, NewPrc, sql;
  var stat = true;

  //vsbanner
  NewBnr = Bnr;
  NewBnr.rec.BCID = 0;

  if (not NewBnr.Insert()) 
    stat = false;
  end;

  //dl_leg
  NewLeg = Leg;
  NewLeg.rec.ID = 0;
  NewLeg.rec.DealID = NewBnr.rec.BCID;

  //Vsordlnk - Свзяка векселя и договора
  NewVsOrdLnk = VsOrdlnk;
  NewVsOrdLnk.rec.BCID = NewBnr.rec.BCID;

  if ((not NewLeg.Insert()) or (not NewVsOrdLnk.Insert()) )
    stat = false;
  end;

  sql = "insert into dficert_dbt (t_ficertid, t_avoirkind, t_certid) values ( "
      + " 0, "       // 1 t_ficertid
      + " 5, "       // 2 t_avoirkind
      + " :bcid ) "; // 3 t_certid
  sql = ExecSql(sql, MakeArray(SqlParam("bcid", NewBnr.rec.BCID)), false);
  if (sql == null)
    stat = false;
  end;

  //prccontract - "Процентный договор" нужен для процентных векселей
  if (Prc.rec.ContractID > 0)
    NewPrc = Prc;
    NewPrc.rec.ContractID = 0;
    NewPrc.rec.Number = NewPrc.rec.Number + 1;
    NewPrc.rec.ObjectID = string(NewBnr.rec.BCID);
    if (not NewPrc.Insert() )
      stat = false;
    end;
  end;

  return stat;
onerror()
  return false;
End;

//Создание векселей
macro StartCreate ()
  var count = 0;
  var stat = true;

  if (not InitMainData())
    stat = false;
  end;

  while ((count < amount) and stat)
    if (not CreateVS())
      RunError("Ошибка при добавлении векселя");
      stat = false;
    end;
    count = count + 1;
  end;

  return stat;

End;

macro CreateNewBanners (_order)
  order = _order;

  if (not Check(order))
    return 1;
  end;

  amount = ЗапуститьДиалог();

  if (not ProcessTrn(0, "StartCreate"))
    AbortTrn();
    return 1;
  end;

  MsgBox("Векселя добавлены в договор");
  return 0;
End;