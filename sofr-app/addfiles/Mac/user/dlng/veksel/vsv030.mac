/*
$Name:         vsv030.mac
$Module:       Векселя банка
$Description:  Выкуп собственных векселей банка. Списание
*/

//import globals;
import "vscateg.mac", "vsrep.mac", "vsrepay2.mac";

// Осуществляет перевод текущего векселя в состяние - погашенный
//
private macro СписаниеВекселя(bnr, leg, emi, order, numOrd, lnk, ЗакрывашкаДЭ:@variant, emiLnk)
  var stat = 0, ДатаСписания = ЗакрывашкаДЭ.ДатаДействия;

  if ( (stat = 0) and  (Index( bnr.rec.BCState, "Ч" ) !=0) )
    if((stat == 0) and not ИзменитьВексель(bnr.rec.BCID, ДатаСписания,
                                           "rmbcstate", "Ч"))
      stat = Ошибка("Ошибка при снятии признака",
                    "|состояния векселя",
                    "|", GetTypeAc(170, "Ч"), ".");
    end;
  end;

  if((stat == 0) and not ИзменитьВексель(bnr.rec.BCID, ДатаСписания,
                                         "repaymentdate", ДатаСписания,
                                         "bcstatus", VSBANNER_STATUS_ENDED))
      stat = Ошибка("Ошибка при изменении векселя");
  end;

  if((stat == 0) and not VS_AutoRunStep(VS_BANNER, bnr.rec.BCID, "З"))
      stat = Ошибка("Ошибка при закрытии операции сопровождения векселя");
  end;

  if((      stat == 0)
       and (bnr.rec.Department == {OperDprt}) // вексель нашего филиала
       and (Valtype(emi) != V_UNDEF) // есть договор эмиссии
       and ЗакрывашкаДЭ.ВексельПоследний(emi) // больше нет непогашенных векселей по договору эмисии
       and (emiLnk.rec.DocKind != DL_VEKSELACCOUNTED)) // вексель эмитирован не в сделке мены в УВ

      ЗакрывашкаДЭ.ЗапомнитьДЭ(emi); // установить признак закрытия договора эмиссии
  end;

  return stat;
end;

private macro СписатьВекселя(order, ЗакрывашкаДЭ)
    var stat = 0;

    stat = ДляКаждогоВекселя(order, @СписаниеВекселя, null, @ЗакрывашкаДЭ);

    return (stat == 0);
end;

private macro СписаниеВекселей(order, ДатаСписания)
    var stat = 0, ЗакрывашкаДЭ = VSRepayHelper(order, ДатаСписания);

    if(ЗакрывашкаДЭ == null)
        stat = 1;
    end;

    if((stat == 0) and not СписатьВекселя(order, ЗакрывашкаДЭ))
        stat = 1;
    end;

    if((stat == 0) and not ЗакрывашкаДЭ.ЗакрытьДЭ())
        stat = 1;
    end;

    return stat;
end;

/* Запуск шага
*/
MACRO ExecuteStep(Buffer, dl_order)
  var stat = 0, paym;
  record order( dl_order );
  SetBuff( order, dl_order );

  if(not НайтиПлатежПоДоговору(@paym, order, PM_PURP_VEKSELDRAW))
    msgBox( "Не найден платеж по договору" );
    return 1;
  end;

  stat = СписаниеВекселей(order, paym.ValueDate);

  return stat;
END;

/* Макрос постобработки
*/
MACRO PostStep(
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

   if(errTrn or (CommitOrRollback == 2))
      /* Произошла ошибка или происходит откат */
      return;
   end;

   return 1;
END;