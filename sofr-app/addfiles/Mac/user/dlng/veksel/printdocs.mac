import dlwreps, oralib, likepy, VSInter, PTInter, globals, FIInter, vslib, BankInter, vsrep, dlprxytg, padej;

private const DPRT_NAME_IN_HO = "Департамент по работе на рынках капитала"; //Наименование департамента в головном офисе

private var months = MakeArray(null,"января","февраля","марта","апреля","мая","июня","июля","августа","сентября","октября","ноября","декабря");

//наименование филиала
private macro GetDprtName (NUmDprt)
  var name = "";
  var sql = " select party.t_shortname name "
          + "from ddp_dep_dbt dep, dparty_dbt party "
          + "where dep.t_partyid = party.t_partyid and dep.t_code = :NumDprt ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("NumDprt", NumDprt)), false);
  if (sql.MoveNext() ) 
    name = sql.value("name");
  end;
  return name;
End;

//Возвращает название города по номеру филиала
macro GetDprtCity (NumDprt)
   var partyid = 0;
   var Party, PartyAddress;
   var sql = ExecSqlSelect("select t_partyid from ddp_dep_dbt where t_code = :num", MakeArray(SqlParam("num", NumDprt)), false);
   if (sql.MoveNext() )
      partyid = int(sql.value("t_partyid"));
   end;
   if (partyid == 0)
      return "";
   end;

   Party = RsbParty(partyid);
   PartyAddress = Party.Address(PTADDR_LEGAL);
   return PartyAddress.District;
End;

private macro GetThing (count)
  var rez = "";
  var s = 0, d = 0, e = 0;
  var s_str = "", d_str = "", e_str = "";
  var tail;

  if (count == 0)
   count = 1;
  end;

  s = int(count/100);
  d = mod(int(count/10), 10);
  e = mod(count, 10);

  if ((e == 1) and (d != 1))
    tail = "штуки";
  else
    tail = "штук";
  end;

  if (s == 0)
    s_str = "";
  elif (s == 1)
    s_str = "ста ";
  elif (s == 2)
    s_str = "двухсот ";
  elif (s == 3)
    s_str = "трёхсот ";
  elif (s == 4)
    s_str = "четырёхсот ";
  elif (s == 5)
    s_str = "пятисот ";
  elif (s == 6)
    s_str = "шестисот ";
  elif (s == 7)
    s_str = "семисот ";
  elif (s == 8)
    s_str = "восьмисот ";
  elif (s == 9)
    s_str = "девятисот ";
  end;

  if (d == 0)
    d_str = "";
  elif (d == 1)
    d_str = ""
  elif (d == 2)
    d_str = "двадцати ";
  elif (d == 3)
    d_str = "тридцати ";
  elif (d == 4)
    d_str = "сорока ";
  elif (d == 5)
    d_str = "пятидесяти ";
  elif (d == 6)
    d_str = "шестидесяти ";
  elif (d == 7)
    d_str = "семидесяти ";
  elif (d == 8)
    d_str = "восьмидесяти ";
  elif (d == 9)
    if (e == 0) 
      d_str = "девяноста ";   
    else
      d_str = "девяносто ";
    end;
  end;

  if (d != 1) 
    if (e == 0)
      e_str = "";
    elif (e == 1)
      e_str = "одной";
    elif (e == 2)
      e_str = "двух";
    elif (e == 3)
      e_str = "трёх";
    elif (e == 4)
      e_str = "четырёх";
    elif (e == 5)
      e_str = "пяти";
    elif (e == 6)
      e_str = "шести";
    elif (e == 7)
      e_str = "семи";
    elif (e == 8)
      e_str = "восьми";
    elif (e == 9)
      e_str = "девяти";
    end;
  end;

  if (d == 1) 
    if (e == 0)
      e_str = "десяти";
    elif (e == 1)
      e_str = "одиннадцати";
    elif (e == 2)
      e_str = "двенадцати";
    elif (e == 3)
      e_str = "тринадцати";
    elif (e == 4)
      e_str = "четырнадцати";
    elif (e == 5)
      e_str = "пятнадцати";
    elif (e == 6)
      e_str = "шестнадцати";
    elif (e == 7)
      e_str = "семнадцати";
    elif (e == 8)
      e_str = "восемнадцати";
    elif (e == 9)
      e_str = "девятнадцати";
    end;
  end;

  rez = string(count) + " (" + s_str + d_str + e_str + ") " + tail;

  return rez;
End;

//принимает сумму и валюту
//возвращает: 200 900.14 (Двести тысяч девятьсот и 14/100) рублей
private macro Get_NumToStr(num:money, fiid:integer)
  var rub :string = "",
      kop :string = "",
      s1  :string;

  var cmd :object = NULL,
  rs  :object = NULL;

  cmd = RsdCommand("select * from dfininstr_dbt where t_fiid = ?");
  cmd.addParam("fiid", RSDBP_IN, fiid);
  cmd.execute;
  rs = RsdRecordset(cmd);

  if (not rs.movenext())
    return "";
  end;

  s1 = CurToStrAlt(num, rub, kop, int(rs.value("t_iso_number")));
  s1 = trim(StrSubSt(s1, rub, ""));
  s1 = trim(StrSubSt(s1, SubStr(s1, index(s1, kop)), ""));

  return StrSubSt(string(num:a),"'"," ")+" ("+rub+" и "+kop+"/100) "+s1;
end;

//в отличие от первой возвращает только текст без скобок.
private macro Get_NumToStr_2(num:money, fiid:integer)
  var rub :string = "",
      kop :string = "",
      s1  :string;

  var cmd :object = NULL,
  rs  :object = NULL;

  cmd = RsdCommand("select * from dfininstr_dbt where t_fiid = ?");
  cmd.addParam("fiid", RSDBP_IN, fiid);
  cmd.execute;
  rs = RsdRecordset(cmd);

  if (not rs.movenext())
    return "";
  end;

  s1 = CurToStrAlt(num, rub, kop, int(rs.value("t_iso_number")));
  s1 = trim(StrSubSt(s1, rub, ""));
  s1 = trim(StrSubSt(s1, SubStr(s1, index(s1, kop)), ""));

  return rub+" и "+kop+"/100 "+s1;
end;

//количество векселей в вексельном договоре
macro GetAmountVeksInOrder (contractid, dockind)
  var amount = 0;
  var sql = ExecSqlSelect("select count (1) cnt from dvsordlnk_dbt where t_contractid = :id and t_dockind = :2", MakeArray(SqlParam("id", contractid), SqlParam("2", dockind)));
  if (sql.MoveNext() )
    amount = sql.value("cnt");
  end;
  return int(amount);
End;

/*Возвращает ИД подотчетного лица бланка по ИД договора с векселем*/
macro GetResponsPerson (DocID, DocKind):integer
  var person:integer = 0;
  var sql = "select t_responsperson from dvsfrmord_dbt "
          + "where t_frmordid in "
          + "   (select to_number(t_objectid) "
          + "    from dobjlink_dbt "
          + "    where t_attrid = (select trim(t_bcnumber) from dvsbanner_Dbt "
          + "                      where t_bcid in (select t_bcid from dvsordlnk_dbt where t_contractid = :1 and t_dockind = :2 and rownum = 1/*Берем первый попавшийся вексель*/))) "
          + "and t_kind_operation = 4911 "
          + "order by t_signed desc ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("1", DocID), SqlParam("2", DocKind)));
  if (sql.MoveNext() )
    person = sql.value(0);
  end;
  return person;
End;

macro printnumber (num)
  println(StrSubSt(string(num:a),"'"," "));
End;

//печатает основную информацию по векселям договора. buyer - покупатель/приобретатель
macro PrintVeksInfo (contractid, dockind, buyer)
  var count = 0;
  var TotalFaceValue = 0;
  var sql = " select bnr.t_bcseries series,  "
          + "       bnr.t_bcnumber bcnumber,  "
          + "       leg.t_principal facevalue,  "
          + "       leg.t_pfi currency,  "
          + "       leg.t_start bcstart, "
          + "       bnr.t_bctermformula formula, "
          + "       leg.t_maturity maturity, "
          + "       leg.t_expiry expiry, "
          + "       leg.t_diff diff, "
          + "       leg.t_price/power(10, leg.t_point) rate, "
          + "       bnr.t_holdername holdername, "
          + "       bnr.t_issuedate issuedate "
          + "from dvsordlnk_dbt lnk, dvsbanner_dbt bnr, ddl_leg_dbt leg "
          + "where lnk.t_contractid = :contractid "
          + "      and lnk.t_dockind = :dockind "
          + "      and lnk.t_bcid = bnr.t_bcid "
          + "      and leg.t_dealid = bnr.t_bcid "
          + "      and leg.t_legid = 0 "
          + "      and leg.t_legkind = 1 "
          + "order by bnr.t_bcseries ";

  sql = ExecSqlSelect(sql, MakeArray(SqlParam("contractid", contractid), SqlParam("dockind", dockind)), false);
  while (sql.MoveNext() )
    count = count + 1;

    println("~series" + count);
    println(trim(sql.value("series")));

    println("~bcnumber" + count);
    println(trim(sql.value("bcnumber")));

    println("~facevalue" + count);
    printnumber(sql.value("facevalue"));

    println("~bcdate" + count);
    println(date(sql.value("bcstart")));

    println("~buyer" + count);
    println(buyer);

    println("~bcrate" + count);
    println(ifThenElse(sql.value("rate")==0, "-", sql.value("rate")));

    println("~bcterm" + count);
    println(VS_TermFormula(sql.value("formula"), sql.value("maturity"), sql.value("expiry")));

    println("~bcholder" + count);
    println(sql.value("holdername"));

    println("~issuedate" + count);
    println( string(Date(sql.value("issuedate")):m:f) );

    TotalFaceValue = TotalFaceValue + double(sql.value("facevalue"));
  end;
  if (count > 0) 
    [~currency];
    println(ПолучитьИмяФинИн(sql.value("currency")));

    [~TotalFaceValue];
    println(Get_NumToStr(TotalFaceValue, sql.value("currency")));
  end;
End;

//печать даты документа
macro PrintDate (_date)
  var dt:string, mm, year;

  DateSplit(_date, dt, mm, year);

  [~dt];
  println(ifThenElse(strlen(dt)==1, "0"+dt, dt));

  [~month];
  println(months(mm));

  [~year];
  println(substr(string(year:4),3));
End;

//------------------------------------------------------------------------------------------
class TResponsedBlanks ()       
  var first:string;
  var last:string;
  var count:integer = 0;
END;

//Возвращает массив с имеющимися бланками у субъекта
macro ResponsedBlanks (partyid)
  var BlanksArr = TArray();
  var CurrentElement = 0;
  var sql = ExecSqlSelect("select trim(t_number) cur, lag(trim(t_number), 1, 0) over (order by t_number) prev from dvsform_Dbt where t_responsperson = :1 order by to_number(t_number)",
                          MakeArray(SqlParam("1", partyid)));

  private macro AddNew (val:string)
    BlanksArr(CurrentElement) = TResponsedBlanks();
    BlanksArr(CurrentElement).first = val;
    BlanksArr(CurrentElement).last  = val;
    BlanksArr(CurrentElement).count = 1;
  End;

  while (sql.MoveNext() )
    if (int(sql.value("prev")) == 0) //Первая строка
      AddNew(sql.value("cur"));
    elif ( (int(sql.value("cur"))-int(sql.value("prev"))) == 1 )
      BlanksArr(CurrentElement).last = sql.value("cur");
      BlanksArr(CurrentElement).count = BlanksArr(CurrentElement).count + 1;
    else
      CurrentElement = CurrentElement+1;
      AddNew(sql.value("cur"));
    end;
  end;

  return BlanksArr;
onerror()
  return TArray();
end;

//------------------------------------------------------------------------------------------

private macro PrintDoc (name)
  DLWREPS_PrintReportFromTagFile (SetOutput (CreateFileName(name)));
  SetOutput(NULL, false);
End;

//Служебная записка на выдачу под отчёт бланков векселей
macro VD_8 (fmo)
  var count = 0;

  VS_GetBlanksEnumStr(fmo.frmordid, count);

  println("VD_8.dotx");

  PrintDate (fmo.signed);

  [~depname];
  println(GetDprtName(fmo.SendToDepartment));

  [~amount];
  println(GetThing(count));

  [~executor];
  println({Name_Oper});

  PrintDoc;
  return 0;
onerror()
  return 1;
End;

//Служебная записка на выдачу под отчёт бланков векселей
macro VD_9 (fmo)
  var count;

  VS_GetBlanksEnumStr(fmo.frmordid, count);

  println("VD_9.dotx");

  PrintDate (fmo.signed);

  [~responsperson];
  println(Падеж(VS_GetPartyShortName(fmo.responsperson), 2/*родительный_падеж*/, 2/*женский пол*/));

  [~amount];
  println(GetThing(count));

  [~executor];
  println({Name_Oper});

  PrintDoc;
  return 0;
onerror()
  return 1;
End;

//справка о сверке бланков
macro VD_10 ()
  record buffoper ("person.dbt");
  var BlanksArr, count = 0, i = 0;

  if (not ListOper(buffoper, 0, {oper}) )
    return;
  end;

  BlanksArr = ResponsedBlanks(buffoper.PartyID);

  println("VD_10.dotx");

  PrintDate ({curdate});

  [~city];
  println(GetDprtCity({OperDprtNode}));

  [~ResponseFIO];
  println(VS_GetPartyShortName(buffoper.PartyID));

  [~MultipleRow];
  println(1);                     /* идентификатор таблицы */
  println(BlanksArr.size - 1);    /* кол-во недостающих строк */
  println();                      /* Закладка, по которой определять таблицу для добавления строк */
  println(3);                     /* номер строки, которую надо размножить */

  while (i < BlanksArr.Size)

    println("~NumFrom" + (i+1));
    println(BlanksArr(i).first);

    println("~NumTo" + (i+1));
    println(BlanksArr(i).last);

    println("~NumCount" + (i+1));
    println(BlanksArr(i).count);

    count = count + BlanksArr(i).count;
    i = i + 1;
  end;

  [~AllCount];
  println(count);

  PrintDoc;
  return 0;
onerror()
  return 1;
End;

//распоряжение на списание бланков
macro VD_12 (doc, blanknum, count)
  var responspersonid;
  var reptype = 1; //тип распоряжения. 1 - вызывается из операций с бланками векселей, 2 - вызывается из договоров эмиссии векселей
  var responsperon;

  if ((valtype(blanknum) != V_UNDEF) or (valtype(count) != V_UNDEF))
    responsperon = GetResponsPerson(doc.ContractID, doc.DocKind);
  else
    blanknum = VS_GetBlanksEnumStr(doc.frmordid, count);
  end;

  println("VD_12.dotx");

  PrintDate ({curdate});

  [~city];
  println(GetDprtCity(doc.branch));

  [~depname];
  if (doc.branch == 1)
    println(DPRT_NAME_IN_HO);
  else
    println(GetDprtName(doc.branch));
  end;

  [~responsperson];
  println(VS_GetPartyShortName(responsperon));

  [~blanknumber];
  println(blanknum);

  [~count];
  println(count);

  [~executor];
  println({Name_Oper});

  PrintDoc;
  return 0;
onerror()
  return 1;
End;

//Распоряжение о зачислении векселя в хранилище
macro VD_24 (doc)

  println("VD_24.dotx");

  PrintDate({curdate});

  [~depname];
  if (doc.Department == 1)
    println(DPRT_NAME_IN_HO);
  else
    println(GetDprtName({OperDprtNode}));
  end;

  [~city];
  println(GetDprtCity({OperDprtNode}));

  [~holder];
  println(doc.ContractorName);

  [~docnumber];
  println(doc.ordernumber);

  [~docdate];
  println(doc.SignDate);

  [~MultipleRow];
  println(1);                     /* идентификатор таблицы */
  println(GetAmountVeksInOrder(doc.contractid, doc.DocKind) - 1);      /* кол-во недостающих строк */

  PrintVeksInfo(doc.contractid, doc.DocKind, doc.ContractorName);

  [~executor];
  println({Name_Oper});

  PrintDoc;
  return 0;
onerror()
  return 1;
End;

//Распоряжение о выдаче векселя из хранилища
macro VD_25 (doc)
  var proxyid = 0;
  record buffoper ("person.dbt");

  if (not ListOper(buffoper, 0, {oper}) )
    return;
  end;

  println("VD_25.dotx");

  PrintDate({curdate});

  if (doc.Department == 1)
    [~depname];
    println(DPRT_NAME_IN_HO);

    [~deptype];
    println("ВЦ");
  else
    [~depname];
    println(GetDprtName({OperDprtNode}));

    [~deptype];
    println(GetDprtName({OperDprtNode}));
  end;

  [~city];
  println("г. " + GetDprtCity({OperDprtNode}));

  [~responsperson];
  println(Падеж(VS_GetPartyShortName(buffoper.PartyID), 3/*дательный падеж*/, 2/*женский пол*/));

  [~buyername];
  println(doc.ContractorName);

  [~MultipleRow];
  println(1);                     /* идентификатор таблицы */
  println(GetAmountVeksInOrder(doc.contractid, doc.DocKind) - 1);      /* кол-во недостающих строк */

  PrintVeksInfo(doc.contractid, doc.DocKind, doc.contractorname);

  [~executor];
  println({Name_Oper});

  PrintDoc;
  return 0;
onerror()
  return 1;
End;

//акт приёма-передачи векселей. РСХБ принимает
macro VD_27 (doc)
  var count;

  count = GetAmountVeksInOrder(doc.ContractID, doc.DocKind);

  println("VD_27.dotx");

  PrintDate({curdate});

  [~city];
  println(GetDprtCity({OperDprtNode}));

  [~contractor];
  println(doc.contractorname);

  [~ShortContractorName];
  println(VS_GetPartyShortName(doc.Contractor));

  ProxyTag(doc.DocKind, doc.ContractID, doc.Contractor);

  [~MultipleRow];
  println(1);           /* идентификатор таблицы */
  println(count - 1);   /* кол-во недостающих строк */

  PrintVeksInfo(doc.contractid, doc.DocKind, doc.contractorname);

  [~amount];
  println(count);

  [~amountwr];
  println(NumToStr(count));

  PrintDoc;
  return 0;
End;

//Распоряжение о списании с баланса векселя
macro VD_38 (doc)
  var CountVeks = GetAmountVeksInOrder (doc.FrmOrdID, 156);
  var count = 0;

  /*Уже должен быть перенесён ""К исполнению""*/
  var sql = "select nvl((select t_account from dmcaccdoc_dbt where t_catnum = 451 and t_docid = bnr.t_bcid), chr(1)) balance_acc, "
          + "       bnr.t_bcseries, "
          + "       trim(bnr.t_bcnumber) t_bcnumber, "
          + "       leg.t_principal, "
          + "       leg.t_receiptamount, "
          + "       nvl ((select sum(t_sumcalc) from dprccntsched_dbt where t_contractid =  "
          + "             (select t_contractid from dprccontract_dbt where t_contracttype = 1013 and t_objecttype = 651 and t_objectid = bnr.t_bcid)), 0) perc, "
          + "       bnr.t_issuedate, "
          + "       bnr.t_holdername, "
          + "       leg.t_pfi, "
          + "       bnr.t_bctermformula, "
          + "       leg.t_maturity, "
          + "       leg.t_expiry "
          + "from dvsbanner_dbt bnr, ddl_leg_dbt leg "
          + "where t_bcid in (select t_bcid from dvsordlnk_dbt where t_contractid = :1 and t_dockind = 156) "
          + "and bnr.t_bcid = leg.t_dealid "
          + "and leg.t_legid = 0 "
          + "and leg.t_legkind = 1 ";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("1", doc.FrmOrdID)));

  while (sql.MoveNext() )
    count = count + 1;
     
    println("VD_38.dotx");

    PrintDate({curdate});

    [~city];
    println(GetDprtCity({OperDprtNode}));

    [~balanceacc];
    println(sql.value("balance_acc"));

    [~series];
    println(sql.value("t_bcseries"));

    [~bcnumber];
    println(sql.value("t_bcnumber"));

    [~facevaluewr];
    println(Get_NumToStr(sql.value("t_principal"), sql.value("t_pfi")));

    [~bccost];
    println(Get_NumToStr(sql.value("t_receiptamount"), sql.value("t_pfi")));

    [~bcpercent];
    println(Get_NumToStr(sql.value("perc"), sql.value("t_pfi")));

    [~Issuedate];
    println( string(Date(sql.value("t_issuedate")):f:m) );

    [~TermForm];
    println(VS_TermFormula(sql.value("t_bctermformula"), sql.value("t_maturity"), sql.value("t_expiry")));

    [~bcholder];
    println(sql.value("t_holdername"));

    //[~TermLastDay];
    
    PrintDoc("pril_38_" + count);
  end;
End;