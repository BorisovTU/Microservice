/*
$Name: SendNotifITP.mac
$Module: Ядро ГКБО          
$Description: 
*/

import "u_common_email_utils.mac";

  Var TemplName = "Смена_ИТП.docx", oDoc, oWrd, IsNtr = isStandalone(), isLast = true, Mess = "", FormFilename, FileDirForm;

    PRIVATE MACRO GetTxtPath()
        var RegistryPath = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR";
        var Path = "";
        var stat = 0;
 
        GetRegistryValue(RegistryPath, V_STRING, Path, stat);
        if(stat) 
            msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
        end;

        return Path;
    END;

    private macro GetFileName(nam)

       var res = SubStr(nam,index(nam,"\\")+1);
       while (index(res,"\\")>0)
          res = SubStr(res,index(res,"\\")+1);
       end;
       return res;
    end;

   // коррекция терминального имени для копирования
   Private macro fnam4copy(p)
   if (not isNtr)
      if (substr(p, 1, 1) == "$")
         return p;
      end;
      return string("$", p);
   end;
   return p;
   end;

   // полное имя файла из относительного
   private macro abs_path(p)
      var i = 0, l = 0, pth = "";
      if (substr(p, 1, 1) != ".")
         return p;
      end;        
      if (IsNtr)
         pth = GetCurDir(false);
         while (true)
            i = index(pth, "\\", l + 1);
            if (i == 0)
               break;
            else
               l = i;
            end;
         end;       
         if (l > 0)          
            pth = substr(pth, 1, l - 1);
         end;              
      else
         pth = GetCurDir(true);
      end;
      p = strsubst(p, "..", "");
      return string(pth, p); 
   end;

   // открыть Word
   Private macro opn_word(fnam)
      if (oWrd == null)     
         if (IsNtr)
            oWrd = ActiveX("Word.Application", NULL, false);
         else
            oWrd = CreateObject("rsax", "TRsAxServer", "RsAxServer", false).CreateComObject("Word.Application", false);
         end;
      end;   
      oWrd.DisplayAlerts = 0;
      oWrd.Visible = false;
      oDoc = oWrd.Documents.Open(fnam);
      return true;
   onerror (e);                                              
      return false;
   end;

   // закрыть Word
   Private macro cls_word()
      if (oDoc)
         // закрыть без сохранения, так как сохранение уже могло не выполниться
         // после этого Word в скрытом режиме держит обращение к пользователю
         oDoc.Close(false);
      end;   
      oDoc = null;
      if (isLast)
         if (oWrd)
            oWrd.Quit;
         end;   
         oWrd = null;
      end;  
   onerror(e)
      if (oWrd)
         oWrd.Quit;
      end;   
      oDoc = oWrd = null;
   end;  


   Private macro fill_bmk(ClientName, DopDogNum, DopDogDate, DogNum, DogDate)
      var bmks, nbmk = 1, bmkc, nam_b = "", val_b = "";     
         bmks = oDoc.Bookmarks;
         bmkc = bmks.Count;
         while (bmkc > 0)                

            nam_b = Strupr(bmks.item(1).Name); 
            if (index(nam_b, "ФИО") > 0)  
               val_b = ClientName;                             
            elif (index(nam_b, "ДАТА_СОГЛАШЕНИЯ") > 0)
               val_b = string(DopDogDate:f);
            elif (index(nam_b, "НОМЕР_СОГЛАШЕНИЯ") > 0)
               val_b = string(DopDogNum:f);
            elif (index(nam_b, "ДАТА_БРОКЕРСКОГО_ДОГОВОРА") > 0)
               val_b = string(DogDate:f);
            elif (index(nam_b, "НОМЕР_БРОКЕРСКОГО_ДОГОВОРА") > 0)
               val_b = string(DogNum:f);
            else             
               bmks.item(1).delete;
            end;           
            bmks.item(1).Range.Text = val_b;
            if (bmkc == bmks.Count)
               bmks.item(1).delete;
            else   
            end;
            bmkc = bmks.Count;
         end;
      end;

   Private macro Head_Form(p_Templ, Templ_Dir, ClientName, DopDogNum, DopDogDate, DogNum, DogDate)
      var tpl_dir, strErr, fil_nam = "", count = 0, str = "", i = 0;
      var tpl_nam = "";
      //доставить шаблон в место формирования
      tpl_nam = FindPath(toANSI(p_Templ), Templ_Dir);
      if (strlen(trim(tpl_nam)) == 0)
         runerror("Ошибка поиска шаблона", "Ошибка поиска шаблона " + p_Templ);
      end;  
      fil_nam = abs_path(toAnsi(MESS));                        
      MESS = fil_nam;
      if (NOT CopyFile(tpl_nam, fnam4copy(fil_nam), false, "Копирование шаблона"))
         runerror("Ошибка получения шаблона", "Ошибка получения шаблона " + toOem(tpl_nam));
      end;                                   
      if (not opn_word(fil_nam))
         cls_word();            
         runerror("Ошибка открытия файла", "Ошибка открытия файла " + toOem(fil_nam));
      end;
      fill_bmk(ClientName, DopDogNum, DopDogDate, DogNum, DogDate);
      oDoc.Save;
      cls_word();     
      return "";       
      onerror(e)
         if (e.Err)
            if (IsEqClass ("TRsComErr", e.err))
               count = e.err.Count;
               i = 0;
               while (i < count)
                  str = string(str, e.err.Message(i), " (Code = ",e.err.Code(i), ", Level = ",e.err.Level(i), ")", strfor(10));
                  i = i + 1
               end;
               str = str + " " + e.Module + " " + e.Line + " " + e.Message;
            elif (valtype(e.err) == v_string)
               str = e.err;
            else
               str = e.Module + " " + e.Line + " " + e.Message;
            end;  
         else
            str = e.Module + " " + e.Line + " " + e.Message;
         end;    
         //если ошибки работы с Word то надо полностью закрывать Word для этой сессии
         //иначе дальнешее поведение непредсказуемо
         isLast = true;
         cls_word();
         return str;
   end;


  private macro GetClientEmailByContactType(ClientID:integer, ContactType:integer)
    var cmd, DataSet;
    var email = "";
    cmd = RSDCommand(    " SELECT c.t_Value as email"
                          + "   FROM dcontact_dbt c "
                          + "  WHERE c.t_PartyID = ? "
                          + "    AND c.t_ContactKind = " + CNTK_EMAIL
                          + "    AND c.t_ContactType = ? " 
                          //+ "    AND c.t_IsMain = 'X' "
                          //+ "    AND ROWNUM = 1 "
                          + "  order by c.t_IsMain desc " // Golovkin 
                         );
    cmd.addParam("", RSDBP_IN, ClientID);
    cmd.addParam("", RSDBP_IN, ContactType);

    DataSet = RSDRecordSet(cmd);
    if(DataSet.moveNext())
      email = DataSet.value("email");
    end;

    return email;
  end;

  
  macro USER_GetClientRepEmail(DBOid:integer, ClientID)
    var cmd, DataSet;
    var email = "", contrnumber;

    DataSet = TRsbDataSet(String( "SELECT dlc.t_email\n" 
                                 ,"  FROM dsfcontr_dbt sfc, ddlcontr_dbt dlc\n" 
                                 ," WHERE     dlc.t_sfcontrid = sfc.t_id\n"  
                                 ,"       AND dlc.t_dlcontrid = ", DBOid));

    if (DataSet.movenext())
      email = DataSet.Email;
    end;

    if(email == "")
       email = GetClientEmailByContactType(ClientID, CNTT_TOSENDREPORTS);
       if(email == "")
          email = GetClientEmailByContactType(ClientID, 1005);
          if(email == "")
             email = GetClientEmailByContactType(ClientID, 0);
          end;
       end;
    end;
    return email;
  END;

  macro SendNotification_ITP(DBOID, ClientID, ClientName, DopDogNum, DopDogDate, DogNum, DogDate  )
     var err = "", Templ_Dir, StrErr;
     GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEMPLSDIR", V_STRING, Templ_Dir, StrErr);
      FileDirForm = "..\\txtFile\\";

      FormFilename = DogNum + "_ITP";// + w_ext;
      FormFilename = strSubst(FormFilename,"\"","");
      FormFilename = strSubst(FormFilename,"\\","-");
      FormFilename = strSubst(FormFilename,"/","-");
      FormFilename = FormFilename + ".docx";
      Mess = string(FileDirForm, FormFilename);

     Head_Form(TemplName, Templ_Dir, ClientName, DopDogNum, DopDogDate, DogNum, DogDate);

     var email_adr = USER_GetClientRepEmail(DBOID, ClientID);
     if(email_adr == "")
       err = "Ошибка получения адреса электронной почты для клиента";
       return err;
     end;


     var email = c_email_proc_env();
     if (email != NULL)
       email.m_set_msg_head("Уведомление об установлении индивидуального тарифного плана");

     var emailText;
     if  (mess != "") 
        if (not CopyFile("$"+mess, GetTxtPath()+"\\"+GetFileName(mess)))
          MsgBox("Ошибка копирования файла " + mess + " на сервер приложения для отправки.");
        end;
     end;

	  emailText = string("\n","    Клиент   ", ClientName
     ,"\n"
     ,"\n"
     ,"                            				               Уведомление                                                  \n" 
     ,"                            об установлении индивидуального тарифного плана                                \n"
     ,"\n"
     ,"\n" 
     ,"Настоящим АО \"Россельхозбанк\" извещает о том, что в соответствии с Дополнительным соглашением от\n"  
     ,DopDogDate,"  N ", DopDogNum, " по Вашему Соглашению от ", DogDate, " N ",DogNum,"\n"  
     ," установлен Индивидуальный тарифный план.\n"  
     ,"\n"  
     ,"\n"
     ,"                            				            Контактная информация                                                  \n" 
     ,"\n"  
     ,"\n"
     ,"Торговые операции: тел. 8 (495) 663-65-12, 8 (800) 100-40-60\n"  
     ,"Клиентская поддержка: 8 (800) 100-0-100, 8 (800) 100-40-40, эл. почта invest@rshb.ru.\n"  
     ,"Неторговые операции: эл. почта broker@rshb.ru\n"  
     ,"Техническая поддержка ИТС Quik: эл. почта quik@rshb.ru\n"  
     ,"Компрометация ключа ЭП, сертификат ключа проверки ЭП для которого выдан УЦ РСХБ: тел. 8-800-200-02-90\n"  
     ,"\n"  
     ,"\n"
     ,"_________________________________________________________________________________________________________________________________________________________________________________________________\n"
     ,"                   Место обслуживания: Гагаринский переулок, дом 3, Москва, Российская Федерация, 119034                                                  \n" 
     );

       email.m_add_row_to_msg_text(emailText);
       email.m_add_attach_to_list(GetTxtPath()+"\\"+GetFileName(mess));
       email.m_add_email_to_list(email_adr);
   //        email.m_add_attach_to_list(FullPathRep + srvobj.rec.FileName);  
         email.m_save_to_submit();
     else
       err = "Ошибка инициализации объекта сообщения электронной почты";
     end;
     return err;
  end;




