/**                                                                                                             
 @file vtb_upd_price.mac                                                                                           
 @brief Расчет средней цены приобретения 

 #menu 
  - БО ЦБ\Договоры\Договоры брокерского обслуживания\ВТБ. Начальное решение\12. Сверка кол-ва НДФЛ и ДЕПО зачислений. Расчет средней цены для ДЕПО зачислений                                                                         
                                                                                                                
 #tag                                                                                                          
 - functional_block:СО 
 - functional_block:НДФЛ   
 - functional_block:Лимиты                                                                                                                                                                                                                                                                                                               
 - code_type:GUI 
 - code_type:API                                                                                                                                                                                               
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.01.19 |Жиц М.В.       |DEF-56911           |Реализация обновления стоимости и данных в панели НУ для обновляемой операции, добавление комментариев                                                                                                                                                 
 |2023.12.08 |Жиц М.В.       |DEF-56911           |Исправлена обработка признака "По нулевым ценам"                                                                                                                                                 
 |2023.08.09 |Велигжанин А.В.|DEF-48887           |Запрет изменения поля "Многопоточный режим"                                                                                                                                                 |2023.12.27 |Жиц М.В.       |DEF-59221           |BIQ-14887. При удалении комиссии по всем клиентам и договорам обрабатывается только один                                                                                                                                                 
 |2023.01.27 |Белохонов П.В. |DEF-35404,DEF-36768 |beautifying по замечаниям CodeReview                                                                                                                                                 
 |2023.01.23 |Белохонов П.В. |DEF-35404,DEF-36768 |Исправлено определение цен по облигациям, если цена в сделке не в %
 |           |               |                    |Исправлено определение цен, если покупки совершены в разных валютах
 |           |               |                    |Исправлено ошибочное попадание в расчет зачислений миграции от 31/12/2018
 |           |               |                    |Исправлен расчет цен по позициям, по которым были корпоративные действия                                                                                                                                                 
 |2023.01.09 |Белохонов П.В. |DEF-33274           |Выкидывает из формы ввода при неверном селекте в ВТБ-нач.решение п12.2                                                                                                                                                 
 |2023.11.18 |Белохонов П.В. |DEF-34149           |Исправление цен по разблокированным ц/б                                                                                                                                                 
 |?          |?              |?                   |Создание макроса операции                                                                                                                                                 
*/ 

import rsd, BankInter, dlcontrfunc;

private const NOTEKIND_GUID = 404; //Примечание "GUID депозитарного зачисления"
private const OBJGROUP_VTBNDFL = 210; // Категория Зачисления ВТБ для НДФЛ 

/**
 @brief Аналог оператора ?: в си
 @param[in] cond условие 
 @param[in] val1 значение, если условие верно
 @param[in] val1 значение, если условие неверно
 @return соответствующее значение
*/
private macro IIF(cond, val1, val2)
   if (cond) 
      return val1; 
   else 
      return val2; 
   end;
end;

/**
 @brief Получение значения кода вида "Код ЦФТ" по клиенту
 @param[in] ID идентификатор клиента 
 @return значения кода вида "Код ЦФТ" по клиенту, в случае его отсутствия - -1
*/
Private Macro GetPartyCode(ID)
   var cmd, rs;
   cmd = RsdCommand(" select * from dobjcode_dbt  where t_objecttype = 3 and t_objectid = :ID and t_codekind = 101 and t_state = 0"); 

   cmd.addParam("ID", RSDBP_IN, ID);

   cmd.execute();
   
   rs = RsdRecordset(cmd);

   if(rs.MoveNext())
      return rs.value("t_code");
   else
      Println("Не определен код клиента по ID =  " + ID);
      return -1;
   end;
end;

/**
 @brief Получение номера ДБО клиента по идентификатору
 @param[in] ID идентификатор ДБО 
 @return номер ДБО, в случае его отсутствия - -1
*/
Private Macro GetDBOCode(ID)
   var cmd, rs;
   cmd = RsdCommand(" select sf.* from dsfcontr_dbt sf, ddlcontr_dbt dl where sf.t_id = dl.t_sfcontrid and dl.t_dlcontrid =  :ID"); 
   cmd.addParam("ID", RSDBP_IN, ID);
   cmd.execute();
  
   rs = RsdRecordset(cmd);

   if(rs.MoveNext())
      return rs.value("t_number");
   else
      Println("Не определен ДБО клиента по ID =  " + ID);
      return -1;
   end;
end;

/**
 @brief Получение ISIN ц/б по идентификатору
 @param[in] ID идентификатор финансового инструмента 
 @return ISIN ц/б, в случае его отсутствия - -1
*/
Private Macro GetISINCode(ID)
   var cmd, rs;
   cmd = RsdCommand(" select * from davoiriss_dbt where t_fiid = :ID"); 
   cmd.addParam("ID", RSDBP_IN, ID);
   cmd.execute();
  
   rs = RsdRecordset(cmd);

   if(rs.MoveNext())
      return rs.value("t_isin");
   else
      Println("Не определен ISIN выпуска по ID =  " + ID);
      return -1;
   end;
end;

/**
 @brief Получение буквенного кода валюты по идентификатору
 @param[in] ID идентификатор финансового инструмента 
 @return iso код валюты, в случае его отсутствия - -1
*/
Private Macro GetCcyFiid(fiid)
   var cmd, rs;
   cmd = RsdCommand(" select * from dfininstr_dbt where t_fiid = :fiid"); 
   cmd.addParam("fiid", RSDBP_IN, fiid);
   cmd.execute();
   
   rs = RsdRecordset(cmd);

   if(rs.MoveNext())
      return rs.value("t_ccy");
   else
      Println("Не определен iso код валюты по fiid =  " + fiid);
      return -1;
   end;
end;

/**
 @brief Создание вспомогательных таблиц и индексов для расчета цен по ДЕПО-зачислениям
*/
private macro CreateTable()
   var cmd;
        
   cmd = RsdCommand(
        "BEGIN \n" +
        "   execute immediate 'CREATE TABLE UDEPOOP_UpdPrice '|| \n" +
        "'(T_DEALID     NUMBER(10) NOT NULL,T_DEALDATE DATE, T_CLIENTID   NUMBER(10),T_SFCONTRID  NUMBER(10),'|| \n" +
        " ' T_DLCONTRID  NUMBER(10),T_PFI        NUMBER,T_PRINCIPAL  NUMBER,'|| \n" +
        "  ' T_PRICE      FLOAT(53),T_CFI        NUMBER(10) )'; \n" +
        "  EXCEPTION when others then NULL; \n" +
        "END; ");
   cmd.Execute();

   cmd = RsdCommand(
        "BEGIN \n" +
        "  execute immediate ' CREATE INDEX UDEPOOP_UpdPrice_IDX0 ON UDEPOOP_UpdPrice (T_DEALID)'; \n" +
        "EXCEPTION when others then NULL; \n" +
        "END; " );
   cmd.Execute();

   cmd = RsdCommand(
        "BEGIN \n" +
        "       execute immediate 'CREATE INDEX UDEPOOP_UpdPrice_IDX1 ON UDEPOOP_UpdPrice (T_CLIENTID)'; \n" +
        "    EXCEPTION when others then NULL; \n" +
        "    END; " );
   cmd.Execute();

   cmd = RsdCommand(
        "BEGIN \n" +
        "  execute immediate ' CREATE INDEX UDEPOOP_UpdPrice_IDX2 ON UDEPOOP_UpdPrice (T_DLCONTRID, T_PFI)'; \n" +
        "EXCEPTION when others then NULL; \n" +
        "END; \n") ;
   cmd.Execute();

   cmd = RsdCommand(
        "BEGIN \n" +
        "  execute immediate ' CREATE INDEX UDEPOOP_UpdPrice_IDX3 ON UDEPOOP_UpdPrice (T_DEALDATE)'; \n" +
        "EXCEPTION when others then NULL; \n" +
        "END; \n") ;
   cmd.Execute();


   cmd = RsdCommand(
        "    BEGIN \n" +
        "       EXECUTE IMMEDIATE 'CREATE TABLE UVTB_FORPRICE_DBT ( T_DEALCODE   VARCHAR2(30 BYTE), T_DEALID     NUMBER(10)                       NOT NULL, ' || \n" +
        "  ' T_PRICE      FLOAT(53), T_PRINCIPAL  NUMBER(32,12), T_CLIENTID   NUMBER(10), T_PFI        NUMBER(10), T_CFI        NUMBER(10),' || \n" +
        "  ' T_FIID       NUMBER(10), T_DLCONTRID  NUMBER(10), T_TAXOWNBEGDATE  DATE, T_DEALID_DEPO NUMBER(10) )'; \n" +
        "    EXCEPTION \n" +                                   
        "       WHEN OTHERS \n" +
        "       THEN \n" +
        "          NULL; \n" +
        "    END; \n" );
   cmd.Execute();

   cmd = RsdCommand(
        "    BEGIN \n" +
        "       EXECUTE IMMEDIATE 'CREATE INDEX UVTB_FORPRICE_DBT_IDX1 ON UVTB_FORPRICE_DBT (T_PFI)'; \n" +
        "    EXCEPTION \n" +                                   
        "       WHEN OTHERS \n" +
        "       THEN \n" +
        "          NULL; \n" +
        "    END; \n" );
   cmd.Execute();

   cmd = RsdCommand(
        "    BEGIN \n" +
        "       EXECUTE IMMEDIATE 'CREATE INDEX UVTB_FORPRICE_DBT_IDX2 ON UVTB_FORPRICE_DBT (T_DEALID_DEPO)'; \n" +
        "    EXCEPTION \n" +                                   
        "       WHEN OTHERS \n" +
        "       THEN \n" +
        "          NULL; \n" +
        "    END; \n" );
   cmd.Execute();

end;

/**
 @brief Создание вспомогательной таблицы и индекса для расчета цен в лимитах
*/
private macro CreateTableLimit()
   var cmd;

   cmd = RsdCommand(
        "BEGIN \n" +
        "   EXECUTE IMMEDIATE 'CREATE TABLE UVTB_LIMITS_DBT'|| \n" +
        "'( '|| \n" +
        " ' T_ID                 NUMBER(10), '|| \n" +
        " ' T_CLIENT_CODE        VARCHAR2(35 BYTE), '|| \n" +
        " ' T_CLIENT             NUMBER(10), '|| \n" +
        " ' T_SECCODE            VARCHAR2(35 BYTE), '|| \n" +
        " ' T_SECURITY           NUMBER(10), '|| \n" +
        " ' T_LIMIT_KIND         NUMBER(5), '|| \n" +
        " ' T_WA_POSITION_PRICE  NUMBER(32,12), '|| \n" +
        " ' T_MARKET             NUMBER, '|| \n" +
        " ' T_SFID               NUMBER '|| \n" +
        " ') '; \n"  +
        "  EXECUTE IMMEDIATE 'CREATE UNIQUE INDEX UVTB_LIMITS_DBT_IDX0 ON UVTB_LIMITS_DBT (T_ID)'; \n" +
        "EXCEPTION \n" +
        "   WHEN OTHERS \n" +
        "   THEN \n" +
        "      NULL; \n" +
        "END; \n" );
   cmd.Execute();
end;

/**
 @brief Отбор ДЕПО-зачислений с их последующей вставкой в uDEPOOP_UpdPrice
 @param[in] sqlWhere дополнительные условия отбора 
*/
private macro ReFillTableDepo(sqlWhere)
   var cmd, sql = "",rsdErrText = "", rsdErrorCount = 0;
   BegAction(0, "Отбор ДЕПО-зачислений");
   cmd = RsdCommand(
        "BEGIN \n" +
        "   EXECUTE IMMEDIATE ' truncate table UDEPOOP_UpdPrice '; \n" +
        "END; \n" );
   cmd.Execute();
   sql = "INSERT INTO uDEPOOP_UpdPrice \n" +
        "SELECT tick.t_dealid, tick.t_dealdate, tick.t_clientid, tick.t_clientcontrid as t_sfcontrid, mp.t_dlcontrid, decode(leg.T_PFI,2544,60775,leg.T_PFI) t_pfi, leg.T_PRINCIPAL*decode(tick.t_dealtype, 2011,1,2010,-1) t_principal, leg.t_price, leg.t_cfi  \n" +
        "  FROM ddl_tick_dbt tick, dnotetext_dbt text, dobjcode_dbt code, ddlcontrmp_dbt mp, ddl_leg_dbt leg \n" +
        "WHERE     tick.t_bofficekind = 127  \n" +
        "       AND text.t_objecttype = 101  \n" +
        "       AND text.t_documentid = LPAD (tick.t_dealid, 34, '0')  \n" +
        "       AND text.t_notekind = 404  \n" +
        "       AND code.T_OBJECTTYPE = 3  \n" +
        "       AND code.T_CODEKIND = 101  \n" +
        "       AND code.T_OBJECTID = tick.T_CLIENTID  \n" +
        "       AND code.t_state = 0  \n" +
        "       and mp.T_SFCONTRID = tick.t_clientcontrid  \n" +
        "       and leg.t_legkind = 0 and leg.t_legid = 0 and leg.t_dealid = tick.t_dealid \n" +
        "       and tick.t_flag3 = chr(0) \n" +
        "       and tick.t_dealtype = 2011 and exists ( select 1 from   UVTB_CHECKOP_DBT c, ddl_tick_dbt n  where   n.t_dealid = C.T_DEALID_NDFL and  \n" +
        "       tick.t_clientid = C.T_clientid and tick.t_pfi = c.t_pfi and ( (C.T_DEPO_SUM = C.T_NDFL_SUM) or (n.t_dealstatus = 20))  ) "  + sqlWhere;
   cmd = RsdCommand(sql);
   cmd.Execute();

   cmd = RsdCommand(
        "BEGIN \n" +
        "        dbms_stats.gather_table_stats(ownname => SYS_CONTEXT('USERENV','CURRENT_SCHEMA'), \n" +
        "        tabname => 'UDEPOOP_UPDPRICE', \n" +
        "        estimate_percent => DBMS_STATS.AUTO_SAMPLE_SIZE, \n" +
        "        cascade=>TRUE, method_opt=>'FOR ALL COLUMNS SIZE AUTO'); \n" +
        "  END; \n" );
   cmd.Execute();
   EndAction();
   return rsdErrText;
OnError(e)
    EndAction();
    while ( rsdErrorCount < cmd.connection.environment.ErrorCount )
       rsdErrText = rsdErrText + IIF (rsdErrText, "\n", "") + cmd.connection.environment.error (rsdErrorCount).descr;
       rsdErrorCount = rsdErrorCount + 1;
    end;
    return rsdErrText;
end;

/**
 @brief Подготовка данных с их последующей вставкой в созданные таблицы
 @param[in] sqlWhere дополнительные условия отбора 
*/
private macro ReFillTable(sqlWhere)
   var cmd, sql = "";
   cmd = RsdCommand(
        "BEGIN \n" +
        "   EXECUTE IMMEDIATE ' truncate table UVTB_FORPRICE_DBT '; \n" +
        "END; \n" );
   cmd.Execute();
   EndAction(0);
   BegAction(0, " Подготовка данных. Шаг 1/3 Отбор зачислений");


   cmd = RsdCommand(
        "INSERT INTO uvtb_forprice_dbt \n" +
        "   SELECT t.t_dealcode, \n" +
        "          t.t_dealid, \n" +
        "          l.t_price, \n" +
        "          l.t_principal, \n" +
        "          t.t_clientid, \n" +
        "          t.t_pfi, \n" +
        "          l.t_cfi, \n" +
        "          -1 t_fiid, \n" +
        "          mp.t_dlcontrid, \n" +
        "          T.T_TAXOWNBEGDATE, \n" +
        "          op_depo.t_dealid \n" +
        "     FROM    uDEPOOP_UpdPrice op_depo \n" +
        "           JOIN \n" +
        "             uNDFLOP_ReadyExec op_ndfl \n" +
        "          ON op_depo.T_DLCONTRID = op_ndfl.T_DLCONTRID \n" +
        "             AND op_depo.T_pfi = op_ndfl.T_pfi , \n" +
        "          ddl_tick_dbt t, \n" +
        "          ddl_leg_dbt l, \n" +
        "          ddlcontrmp_dbt mp \n" +
        "    WHERE     op_ndfl.t_dealid = t.t_dealid \n" +
        "          AND l.t_dealid = t.t_dealid \n" +
        "          AND l.t_legkind = 0 \n" +
        "          AND l.t_legid = 0 \n" +
        "          AND T.T_CLIENTCONTRID = mp.t_sfcontrid; "
          );
   cmd.Execute();
   cmd = RsdCommand(
        "BEGIN \n" +
        "        dbms_stats.gather_table_stats(ownname => SYS_CONTEXT('USERENV','CURRENT_SCHEMA'), \n" +
        "        tabname => 'UVTB_FORPRICE_DBT', \n" +
        "        estimate_percent => DBMS_STATS.AUTO_SAMPLE_SIZE, \n" +
        "        cascade=>TRUE, method_opt=>'FOR ALL COLUMNS SIZE AUTO'); \n" +
        "  END; \n" );
   cmd.Execute();

   EndAction(0);

   BegAction(0, " Подготовка данных. Шаг 2/3 Определение валюты цены по котировкам");
   cmd = RsdCommand(
        "DECLARE \n" +
        "   v_fiid   NUMBER (10); \n" +
        "BEGIN \n" +
        "   FOR i IN (                              \n" +
        "              SELECT p.t_pfi, m.t_marketid \n" +
        "                FROM uvtb_forprice_dbt p, ddl_tick_dbt t, ddlcontrmp_dbt m \n" +
        "               WHERE p.t_dealid_depo = t.t_dealid AND t.t_clientcontrid = m.t_sfcontrid \n" +
        "            GROUP BY p.t_pfi, m.t_marketid ) \n" +
        "   LOOP \n" +
        "      v_fiid := -1; \n" +
        "      BEGIN \n" +
        "         SELECT t_fiid \n" +
        "           INTO v_fiid \n" +
        "           FROM (  SELECT t_fiid \n" +
        "                     FROM dratedef_dbt t \n" +
        "                    WHERE t_otherfi = i.t_pfi AND t_type = 1 and t_market_place = i.t_marketid \n" +
        "                 ORDER BY t_market_place, t_type) \n" +
        "          WHERE ROWNUM = 1; \n" +
        "      EXCEPTION \n" +
        "         WHEN NO_DATA_FOUND \n" +
        "         THEN \n" +
        "            v_fiid := -1; \n" +
        "      END; \n" +
        "      IF v_fiid = -1 \n" +
        "      THEN \n" +
        "         BEGIN \n" +
        "            SELECT t_fiid \n" +
        "              INTO v_fiid \n" +
        "              FROM (  SELECT t_fiid \n" +
        "                        FROM dratedef_dbt t \n" +
        "                       WHERE t_otherfi = i.t_pfi AND t_type = 23 \n" +
        "                    ORDER BY t_market_place, t_type) \n" +
        "             WHERE ROWNUM = 1; \n" +
        "         EXCEPTION \n" +
        "            WHEN NO_DATA_FOUND \n" +
        "            THEN \n" +
        "               v_fiid := -1; \n" +
        "         END; \n" +
        "      END IF; \n" +
        "      IF v_fiid > -1 \n" +
        "      THEN \n" +
        "         UPDATE uvtb_forprice_dbt \n" +
        "            SET t_fiid = v_fiid \n" +
        "          WHERE t_pfi = i.t_pfi; \n" +
        "          commit; \n" +
        "      END IF; \n" +
        "   END LOOP; \n" +
        "END; \n" );
   cmd.Execute();
   EndAction(0);
   BegAction(0, " Подготовка данных. Шаг 3/3 Определение валюты цены по исходной покупке");
   cmd = RsdCommand(
        "UPDATE uvtb_forprice_dbt \n" +
        "   SET t_fiid = t_cfi \n" +
        " WHERE t_fiid = -1 \n" +
        "       AND t_clientid NOT IN (SELECT t_clientid \n" +
        "                                FROM (  SELECT t_pfi, t_cfi, t_clientid \n" +
        "                                          FROM (  SELECT t_clientid, t_pfi, t_cfi \n" +
        "                                                    FROM uvtb_forprice_dbt \n" +
        "                                                   WHERE t_fiid = -1 \n" +
        "                                                GROUP BY t_clientid, t_pfi, t_cfi) \n" +
        "                                      GROUP BY t_pfi, t_cfi, t_clientid \n" +
        "                                        HAVING COUNT (1) > 1)) \n") ;
   cmd.Execute();
   EndAction(0);
end;

/**
 @brief Отбор данных по лимитам с их последующей вставкой в uvtb_LIMITS_DBT
 @param[in] sqlWhere дополнительные условия отбора 
*/
private macro ReFillTableLimit(sqlWhere)
   var cmd, sql, rsdErrText = "", rsdErrorCount = 0;

   cmd = RsdCommand(
        " truncate table uvtb_LIMITS_DBT " );
   cmd.Execute();

   sql = " insert into  uvtb_LIMITS_DBT  SELECT t_id, t_client_code, \n" +
         "                       t_client,t_seccode,t_security,t_limit_kind,t_wa_position_price,t_market, 0 t_sfid \n" +
         "                  FROM DDL_LIMITSECURITES_DBT t \n" +
         "                 WHERE     t.t_limit_kind = 2  \n" +
         "                       AND t.t_market_kind = 'фондовый'and t_open_balance <> 0  \n" ;
   sql = sql + sqlWhere;

   cmd = RsdCommand(sql);
   cmd.Execute();
   return rsdErrText;
OnError(e)
    while ( rsdErrorCount < cmd.connection.environment.ErrorCount )
       rsdErrText = rsdErrText + IIF (rsdErrText, "\n", "") + cmd.connection.environment.error (rsdErrorCount).descr;
       rsdErrorCount = rsdErrorCount + 1;
    end;
    return rsdErrText;
end;

/**
 @brief Класс панели расчета цен для лимитов QUIK
*/
private class (TRsbPanel) PanLim
   initTRsbPanel();

   /**
    @brief Создание признаков панели
    @param[in] str   номер строки
    @param[in] col   смещение поля 
    @param[in] name  наименование в системе 
    @param[in] def   значение по умолчанию 
    @param[in] label отображаемое наименование
    @return созданный объект признака  
   */
   macro _addCheckBox(str, col, name, def, label)
      var checkBox = TRsbCheckbox;
      checkBox.setPosition(col,str);
      checkBox.name = name;
      checkBox.checked = def;
      addControl(checkBox);
      var m_Label:TrsbLabel = TRsbLabel(col+8,str,label);
      addLabel (m_Label);
      return checkBox;
   end;

   var curstr = 0;
   Caption = "Расчет цен для лимитов QUIK";
   Status = "~ESC~ Выход ~Space~ Переключить ~F2~ Выполнить ~F6~ Проверить выборку";

   var Tab1 = 3;

   setSize(53,25);
   setPosition(8,1);

   /*
      ╔═══════ Расчет цен ════════════════════════╗
    6 ║                                           ║
    7 ║ [х] Многопоточный режим                   ║
    8 ║                                           ║
    8 ║ Пересчитать                               ║
    8 ║ [х] только нулевые цены                   ║
    8 ║ [х] только по клиентам ВТБ                ║
    8 ║ [х] только позиции сформированные КД      ║
    8 ║                                           ║
    8 ║ [ and t_client_code = '12345'            ]║
    8 ║ [                                        ]║
    8 ║ [                                        ]║
    8 ║ [                                        ]║
    8 ║ [                                        ]║
    8 ║ [                                        ]║
    8 ║ [                                        ]║
    8 ║                                           ║
    8 ║ Отобрано 160000                           ║
      ╚═══════════════════════════════════════════╝
    */

   curstr = curstr + 1;
   var cb = _addCheckBox(curstr = curstr + 1, Tab1, "cbMultyThread", "X", "Многопоточный режим");
   this.GetControl("cbMultyThread").enabled = false;
   curstr = curstr + 1;
   curstr = curstr + 1;
   var m_Label = TRsbLabel(Tab1,curstr,"Пересчитать");
   addLabel (m_Label);
   cb = _addCheckBox(curstr = curstr + 1, Tab1, "cbZeroPrice", "X", "по позициям с нулевой ценой");
   cb = _addCheckBox(curstr = curstr + 1, Tab1, "cbVtbClients", "X", "по клиентам ВТБ");
   cb = _addCheckBox(curstr = curstr + 1, Tab1, "cbKDPos", "", "по бумагам и клиентам, которые участвовали в КД");
   curstr = curstr + 1;
   cb = _addCheckBox(curstr = curstr + 1, Tab1, "cbManuslSQL", "", "редактирование условия");

   var m_sqlWhere:TRsbEditField = TRsbEditField(7/*V_STRING*/);
   m_sqlWhere.SetPosition(2,curstr = curstr + 1);
   m_sqlWhere.SetSize(50,15);
   m_sqlWhere.editable = false;
   m_sqlWhere.textLength = 3000;
   m_sqlWhere.name = "eSqlWhere";
   addControl (m_sqlWhere);
   m_sqlWhere.value = "1=1 ";

   addEventHandler(RSB_EV_CONTROL_CHECKED,R2M(this,"OnChecked"));
   addEventHandler(RSB_EV_KEY_PRESSED,R2M(this,"OnKeyPressed"));

   this.InitSqlWhere();

   /**
    @brief Формирование дополнительных проверок в зависимости от установленных признаков
   */
   macro InitSqlWhere()
      m_sqlWhere.value = "";

      if (this.GetControl("cbZeroPrice").checked == "X")
         m_sqlWhere.value = m_sqlWhere.value + " and t.t_wa_position_price = 0 \n";
      end;

      if (this.GetControl("cbVtbClients").checked == "X")
         m_sqlWhere.value = m_sqlWhere.value + " and t.t_client in  ( select t_objectid from dobjcode_dbt where t_codekind = 109 and t_objecttype = 3 and t_state = 0) \n";
      end;

      if (this.GetControl("cbKDPos").checked == "X")
         m_sqlWhere.value = m_sqlWhere.value + " and exists (select 1 from dscdlpmwr_dbt p where p.t_dealkind = 135 and p.t_newfiid = t.t_security and p.t_party = t.t_client ) \n";
      end;

   end;

   /**
    @brief Обработка полей панели
    @param[in] RsbEvent объект параметров панели
   */
   macro OnChecked(RsbEvent:Object)
     if (RsbEvent.id == RSB_EV_CONTROL_CHECKED)
        if ( ( RsbEvent.Source.Name == "cbZeroPrice") or
             ( RsbEvent.Source.Name == "cbVtbClients") or
             ( RsbEvent.Source.Name == "cbKDPos") )
           InitSqlWhere();
        end;

        if (RsbEvent.Source.Name == "cbManuslSQL")
           if (this.GetControl("cbManuslSQL").checked == "X")
              m_sqlWhere.editable = true;
              this.GetControl("cbZeroPrice").enabled = this.GetControl("cbVtbClients").enabled =  this.GetControl("cbKDPos").enabled = false;
           else
              m_sqlWhere.editable = false;
              this.GetControl("cbZeroPrice").enabled = this.GetControl("cbVtbClients").enabled =  this.GetControl("cbKDPos").enabled = true;
           end;
        end;

        this.Redraw();
        
     end;   
     return true;
   end;

   /**
    @brief Обработка нажатия клавиш
    @param[in] ev объект параметров панели
   */
   macro onKeyPressed(ev)   
      var err = "";
      if (ev.keycode == 316) /*F2*/
         err = ReFillTableLimit(m_sqlWhere.value);
         if (err == "")
            this.Close(1);
         else
            MsgBox(err);
         end;
      end;
      if (ev.keycode == 320) /*F6*/
         err = ReFillTableLimit(m_sqlWhere.value);
         if (err == "")
            MsgBox("Успешно. Отобрано " + SQl_GetNRecs(" select 1 from uvtb_limits_dbt ") + " записей" );
         else
            MsgBox(err);
         end;
      end;
   end;

end;

/**
 @brief Класс панели расчета цен по ДЕПО-зачислениям
*/
private class (TRsbPanel) PanUpdPrice
   initTRsbPanel();

   /**
    @brief Создание признаков панели
    @param[in] str   номер строки
    @param[in] col   смещение поля 
    @param[in] name  наименование в системе 
    @param[in] def   значение по умолчанию 
    @param[in] label отображаемое наименование
    @return созданный объект признака  
   */
   macro _addCheckBox(str, col, name, def, label)
      var checkBox = TRsbCheckbox;
      checkBox.setPosition(col,str);
      checkBox.name = name;
      checkBox.checked = def;
      addControl(checkBox);
      var m_Label:TrsbLabel = TRsbLabel(col+8,str,label);
      addLabel (m_Label);
      return checkBox;
   end;

   /**
    @brief Создание поля ввода даты
    @param[in] str   номер строки
    @param[in] col   смещение поля 
    @param[in] name  наименование в системе 
    @param[in] def   значение по умолчанию 
    @param[in] editable признак редактируемого поля
    @return созданный объект признака  
   */
   macro _addEditFieldDate(str, col, name, def, editable)
      var Dt = TRsbEditField(V_DATE);
      Dt.setPosition(col,str);
      Dt.setSize(8,1);
      Dt.name = name;
      Dt.value = def;
      Dt.editable = editable;
      addControl(Dt);
      return Dt;
   end;

   var curstr = 0;
   Caption = "Расчет цен по ДЕПО-зачислениям";
   Status = "~ESC~ Выход ~Space~ Переключить ~F2~ Выполнить ~F6~ Проверить выборку";

   var Tab1 = 3;

   setSize(53,25);
   setPosition(8,1);

   /*
      ╔═══════ Расчет цен ════════════════════════╗
    6 ║                                           ║
    8 ║ Рассчитать цену по операциям              ║
    8 ║ [х] с нулевыми ценами              ║
    8 ║ [х] по клиентам ВТБ                ║
    8 ║ [х] по операциям за период:        ║
    8 ║     от 01.01.0001 по 01.01.0001           ║
    8 ║                                           ║
    8 ║                                           ║
    8 ║ [ and t_client_code = '12345'            ]║
    8 ║ [                                        ]║
    8 ║ [                                        ]║
    8 ║ [                                        ]║
    8 ║ [                                        ]║
    8 ║ [                                        ]║
    8 ║ [                                        ]║
    8 ║                                           ║
    8 ║ Отобрано 160000                           ║
      ╚═══════════════════════════════════════════╝
    */

   curstr = curstr + 1;
   curstr = curstr + 1;
   var m_Label = TRsbLabel(Tab1,curstr,"Рассчитать");
   addLabel (m_Label);
   var cb = _addCheckBox(curstr = curstr + 1, Tab1, "cbZeroPrice", "X", " с нулевыми ценами");
   cb = _addCheckBox(curstr = curstr + 1, Tab1, "cbVtbClients", "X", "по клиентам ВТБ");
   cb = _addCheckBox(curstr = curstr + 1, Tab1, "cbPeriod", "", "по операциям за период");
   curstr = curstr + 1;
   m_Label = TRsbLabel(Tab1,curstr,"от");
   addLabel (m_Label);   
   var d = _addEditFieldDate(curstr, Tab1+3, "dtFrom", {curdate}, true);
   m_Label = TRsbLabel(Tab1+14,curstr,"по");
   addLabel (m_Label);   
   d = _addEditFieldDate(curstr, Tab1+17, "dtTo", {curdate}, true);

   curstr = curstr + 1;
   cb = _addCheckBox(curstr = curstr + 1, Tab1, "cbManuslSQL", "", "редактирование условия");

   var m_sqlWhere:TRsbEditField = TRsbEditField(7/*V_STRING*/);
   m_sqlWhere.SetPosition(2,curstr = curstr + 1);
   m_sqlWhere.SetSize(50,15);
   m_sqlWhere.editable = false;
   m_sqlWhere.textLength = 3000;
   m_sqlWhere.name = "eSqlWhere";
   addControl (m_sqlWhere);
   m_sqlWhere.value = "1=1 ";

   addEventHandler(RSB_EV_CONTROL_CHECKED,R2M(this,"OnChecked"));
   addEventHandler(RSB_EV_KEY_PRESSED,R2M(this,"OnKeyPressed"));
   addEventHandler(RSB_EV_REMOVE_FOCUS ,R2M(this, "onRemoveFocus"));

   this.InitSqlWhere();

   /**
    @brief Формирование дополнительных проверок в зависимости от установленных признаков
   */
   macro InitSqlWhere()
      m_sqlWhere.value = "";

      if (this.GetControl("cbZeroPrice").checked == "X")
         m_sqlWhere.value = m_sqlWhere.value + " and leg.t_price = 0 \n";
      end;

      if (this.GetControl("cbVtbClients").checked == "X")
         m_sqlWhere.value = m_sqlWhere.value + " and tick.t_clientid in  ( select t_objectid from dobjcode_dbt where t_codekind = 109 and t_objecttype = 3 and t_state = 0) \n";
      end;

      if (this.GetControl("cbPeriod").checked == "X")
         m_sqlWhere.value = m_sqlWhere.value + " and tick.t_dealdate between "+GetSQLDate(this.GetControl("dtFrom").value)+" and "+GetSQLDate(this.GetControl("dtTo").value)+" \n";
      end;
     
   end;

   /**
    @brief Обработка смещения фокуса с поля
    @param[in] RsbEvent объект параметров панели
   */
   macro onRemoveFocus(RsbEvent:Object)   
      if ((RsbEvent.Source.Name == "dtFrom") or (RsbEvent.Source.Name == "dtTo"))
         InitSqlWhere();
      end;
   end;

   /**
    @brief Обработка полей панели
    @param[in] RsbEvent объект параметров панели
   */
   macro OnChecked(RsbEvent:Object)
      if (RsbEvent.id == RSB_EV_CONTROL_CHECKED)
         if ( ( RsbEvent.Source.Name == "cbZeroPrice") or
             ( RsbEvent.Source.Name == "cbVtbClients") or
             ( RsbEvent.Source.Name == "cbPeriod") )
            InitSqlWhere();
         end;

         if (RsbEvent.Source.Name == "cbManuslSQL")
            if (this.GetControl("cbManuslSQL").checked == "X")
               m_sqlWhere.editable = true;
               this.GetControl("cbZeroPrice").enabled = this.GetControl("cbVtbClients").enabled =  this.GetControl("cbPeriod").enabled = 
               this.GetControl("dtFrom").enabled = this.GetControl("dtTo").enabled = false;
            else
               m_sqlWhere.editable = false;
               this.GetControl("cbZeroPrice").enabled = this.GetControl("cbVtbClients").enabled =  this.GetControl("cbPeriod").enabled = 
               this.GetControl("dtFrom").enabled = this.GetControl("dtTo").enabled = true;
            end;
         end;

         this.Redraw();
         
      end;   
      return true;
   end;

   /**
    @brief Обработка нажатия клавиш
    @param[in] ev объект параметров панели
   */
   macro onKeyPressed(ev)   
      var err = "";
      if (ev.keycode == 316) /*F2*/
         err = ReFillTableDepo(m_sqlWhere.value);
         if (err == "")
            this.Close(1);
         else
            MsgBox(err);
         end;
      end;
      if (ev.keycode == 320) /*F6*/
         err = ReFillTableDepo(m_sqlWhere.value);
         if (err == "")
            MsgBox("Успешно. Отобрано " + SQl_GetNRecs(" select 1 from UDEPOOP_UpdPrice ") + " записей" );
         else
            MsgBox(err);
         end;
      end;
   end;

end;

/**
 @brief Подготовка скрипта для расчете цены в лимитах в многопоточном режиме
        В нем смотрится какие сейчас есть сформированные джобы, формируется новое имя джоба, чтобы не пересечься с уже созданными, и создается джоб, который выполняет скрипт v_action
 @param[in] str_id список идентификаторов лимитов
 @return скрипт, который должен быть выполнен
*/
private macro GetSqlAction(str_id)
   return
        "DECLARE \n" +
        "   cnt        NUMBER (10) := 0; \n" +
        "   flag       BOOLEAN := FALSE; \n" +
        "   njob       NUMBER (10) := 1; \n" +
        "   v_action   VARCHAR2 (2000); \n" +
        "   check_job number(5):=0; \n" +
        "BEGIN \n" +
        "    BEGIN \n"
        "    SELECT nvl(MAX (TO_NUMBER (REPLACE (job_name, 'CALCPRICE')))+1,1) INTO njob \n" +
        "      FROM USER_SCHEDULER_JOBS                              \n" +
        "     WHERE job_name LIKE 'CALCPRICE%';                       \n" +
        "    EXCEPTION \n"
        "    when no_data_found THEN njob := 1; \n"
        "    END; \n"
        " \n" +
        "   v_action := \n" +
        " ' BEGIN '|| \n" +
        " '   UPDATE DDL_LIMITSECURITES_DBT s '|| \n" +
        " '      SET t_wa_position_price = (select '||  \n" +
        " '             RSHB_RSI_SCLIMIT.GETWAPOSITIONPRICE (s.t_date,T.T_CLIENT,T.T_SFID,T.T_SECURITY,T.T_CLIENT_CODE,T.T_SECCODE,s.t_firm_id, 2,s.t_trdaccid,1) '|| \n" +
        " '                from UVTB_LIMITS_DBT t where T.T_ID = s.t_id) '||  \n" +
        " '    WHERE s.t_id in ("+str_id+") ;COMMIT; END; '; \n" +
        "   DBMS_SCHEDULER.create_job (job_name => 'calcprice' || TO_CHAR (njob), \n" +
        "                              job_type => 'PLSQL_BLOCK', \n" +
        "                              job_action => v_action, \n" +
        "                              auto_drop => TRUE \n" +
        "                             ); \n" +
        "   DBMS_SCHEDULER.run_job ('calcprice' || TO_CHAR (njob), FALSE); \n" +
        "END; " ;
end;

/**
 @brief Главная функция расчета цен, вызываемая из пункта меню, содержащая выбор варианта действий, вызов формирования панелей и саму обработку обоих пунктов
        При расчете цены в лимитах в многопоточном режиме формируется массив оракловых заданий, которые затем параллельно исоплняются ораклом 
 @param[out] errp            сообщения об ошибке
 @param[out] result_protocol протокол работы процедуры
*/
Macro  UpdatePrice(errp:@string, result_protocol:@variant)

   var cmd, rs, cmd1, rs1, rs2, cmd2, cnt = 0, cntndfl = 0 , updCmd, i=0, f=false, suc = 0, cnt_zero = 0, sql = "";
   var newPrice = 0, newCurrency = -1; //Новые цена и валюта цены ДЕПО-зачисления
   var Panel;
   EndAction(0);

   var ar_menu = TArray();
   ar_menu(0) = "Рассчитать цены по ДЕПО зачислениям";
   ar_menu(1) = "Рассчитать цены в лимитах";
   var choice = menu(ar_menu);  

   if (choice == 0) //"Рассчитать цены по ДЕПО зачислениям";
      CreateTable();

      Panel = PanUpdPrice();
      if ( not Panel.Run() ) 
         exit(1);
      end;

      ReFillTable();

      sql = "SELECT t_clientid, t_dlcontrid,  \n" +
            "         SUM (RSI_RSB_FIInstr.ConvSum (t_price,t_cfi,t_fiid,t_taxownbegdate) * t_principal) SumPrice,  \n" +
            "         SUM (t_principal) SumAmount,  \n" +
            "         t_pfi, COUNT (*) sumcount, t_fiid  \n" +
            "    FROM uvtb_forprice_dbt n \n " +
            " GROUP BY t_clientid, t_dlcontrid, t_pfi, t_fiid order by t_clientid, t_dlcontrid \n";
      cmd = DL_RSDCommand(sql );

      sql = "SELECT t_clientid, t_dlcontrid, t_pfi  \n" +
            "    FROM uvtb_forprice_dbt n \n" +
            "GROUP BY t_clientid, t_dlcontrid, t_pfi  \n";
      cmd1 = DL_RSDCommand(sql );

      BegAction(0,"Расчет средних цен по НДФЛ зачислениям ");
      Message("Расчет средних цен по НДФЛ зачислениям ");
      cntndfl = cmd1.GetCount();
      rs = cmd.Execute;
      EndAction(0);

      InitProgress(cntndfl, "Расчет средних цен по НДФЛ зачислениям ", "Расчет средних цен по НДФЛ зачислениям ");
      While (rs.movenext())
         sql = 
              "SELECT tick.*, leg.t_price, leg.t_principal \n" +
              "  FROM ddl_tick_dbt tick, dnotetext_dbt text, ddl_leg_dbt leg \n" +
              " WHERE     tick.t_bofficekind = 127 \n" +
              "       and tick.t_flag3 = chr(0) \n" +
              "       AND tick.t_dealtype = 2011 \n" +
              "       AND text.t_objecttype = 101 \n" +
              "       AND text.t_documentid = LPAD (tick.t_dealid, 34, '0') \n" +
              "       AND leg.t_legkind = 0 \n" +
              "       AND leg.t_dealid = tick.t_dealid \n" +
              "       AND text.t_notekind = 404 \n" +
              "       AND tick.t_clientid = ? \n" +
              "       AND tick.t_clientcontrid IN (SELECT t_sfcontrid \n" +
              "                                      FROM ddlcontrmp_dbt \n" +
              "                                     WHERE t_dlcontrid = ?) \n";
         if ((rs.value("t_pfi") == 60775) or (rs.value("t_pfi") == 2544))
            sql = sql + "       AND tick.t_pfi in ( 60775, 2544)  ";
         else
            sql = sql + "       AND tick.t_pfi = ?  ";
         end;
         cmd1 = DL_RSDCommand( sql );
         cmd1.addParam(rs.value("t_clientid"));
         cmd1.addParam(rs.value("t_dlcontrid"));
         if ((rs.value("t_pfi") == 60775) or (rs.value("t_pfi") == 2544))
         else
            cmd1.addParam(rs.value("t_pfi"));
         end;
         rs1 = cmd1.Execute();
         f = false;
         While (rs1.movenext())
            f = true;
            if (rs.value("t_fiid") == -1)
               result_protocol.AddString("Ошибка: По клиенту с кодом = "+ GetPartyCode(rs.value("t_clientid"))+ ", ДБО " + GetDBOCode(rs.value("t_dlcontrid")) + ", и выпуску " + GetISINCode(rs.value("t_pfi")) + 
                                         " не удалось определить валюту ");
               continue;                                                                                    
            end;
            if (ValType(rs.value("SumPrice")) == 0)
               result_protocol.AddString("Ошибка: По клиенту с кодом = "+ GetPartyCode(rs.value("t_clientid"))+ ", ДБО " + GetDBOCode(rs.value("t_dlcontrid")) + ", и выпуску " + GetISINCode(rs.value("t_pfi")) + 
                                         " не удалось рассчитать цену ");
               continue;                                                                                    
            end;
            If (rs.value("SumPrice") == 0)
              // result_protocol.AddString("Ошибка: По клиенту с кодом = "+ GetPartyCode(rs.value("t_clientid"))+ ", ДБО " + GetDBOCode(rs.value("t_dlcontrid")) + ", и выпуску " + GetISINCode(rs.value("t_pfi")) + 
              //                          " нулевая цена ");
               cnt_zero = 0;
               continue;                                                                                    
            end;

            newPrice = rs.value("SumPrice")/rs.value("SumAmount");
            newCurrency = rs.value("t_fiid");

            updCmd = RSDCommand("update ddl_leg_dbt " 
                               +"   set t_price = ?, "
                               +"       t_cfi = ?, "
                               +"       t_cost = ?, "
                               +"       t_totalcost = (? + RSI_RSB_FIInstr.ConvSum(t_nkd, t_nkdfiid, ?, ?)), "
                               +"       t_point = 6 "
                               +" where t_legkind = 0 "
                               +"   and t_dealid = ? "
                               );
            updCmd.addParam("",RSDBP_IN, newPrice);
            updCmd.addParam("",RSDBP_IN, newCurrency);
            updCmd.addParam("",RSDBP_IN, rs1.t_principal * newPrice);
            updCmd.addParam("",RSDBP_IN, rs1.t_principal * newPrice);
            updCmd.addParam("",RSDBP_IN, newCurrency);
            updCmd.addParam("",RSDBP_IN, date(rs1.t_dealdate));
            updCmd.addParam("",RSDBP_IN, rs1.t_dealid);
            updCmd.execute();

            //Обновим данные в панели НУ для обновляемой операции
            updCmd = RSDCommand("update ddlsum_dbt " 
                               +"   set t_sum = case when t_kind = 1220 then ? else ? end, "
                               +"       t_currency = ? "
                               +" where t_dockind = ? "
                               +"   and t_docid = ? "
                               +"   and t_kind in (1220/*цена*/, 1230/*стоимость*/) "
                               );
            updCmd.addParam("",RSDBP_IN, newPrice);
            updCmd.addParam("",RSDBP_IN, rs1.t_principal * newPrice);
            updCmd.addParam("",RSDBP_IN, newCurrency);
            updCmd.addParam("",RSDBP_IN, rs1.t_bofficekind);
            updCmd.addParam("",RSDBP_IN, rs1.t_dealid);
            updCmd.execute();

            suc = suc + 1;
         end;
         if (not f)
            Println("Ошибка: По клиенту с кодом = "+ rs.value("t_clientid")+ ", ДБО " + rs.value("t_dlcontrid") + ", и выпуску " + rs.value("t_pfi") + " не найдено ни одного Депо зачисления");
            result_protocol.AddString("Ошибка: По клиенту с кодом = "+ GetPartyCode(rs.value("t_clientid"))+ ", ДБО " + GetDBOCode(rs.value("t_dlcontrid")) + ", и выпуску " + GetISINCode(rs.value("t_pfi")) + " не найдено ни одного Депо зачисления");
         end;
         i=i+1;
         Useprogress(i);
      end;//eow
      result_protocol.AddString("Рассчитано цен: " + suc);
      result_protocol.AddString("Цен равных нулю: " + cnt_zero);
      RemProgress;
      if(cntndfl == 0)
         result_protocol.AddString("Не найдено ни одного НДФЛ зачисления");
      end;
      return 1;

   elif (choice == 1) //"Рассчитать цены в лимитах";

      var flag = true, msg = "", sqlWhere = " 1=1 ";
      var action = "", str_id = "";

      CreateTableLimit();

      Panel = PanLim();
      if ( not Panel.Run() ) 
         exit(1);
      end;

      BegAction(0,"Подготовка данных");
    
      cmd1 = RsdCommand(" UPDATE uvtb_limits_dbt t \n" +
                        "        SET t_sfid = ( SELECT t_sfcontrid \n" +
                        "   FROM ddlcontrmp_dbt mp, dsfcontr_dbt s \n" +
                        "  WHERE     t_mpcode = t.t_client_code \n" +
                        "        AND mp.t_sfcontrid = s.t_id \n" +
                        "        AND s.t_servkind = 1 \n" +
                        "        AND s.t_servkindsub = 8 \n" +
                        "        AND mp.t_marketid = t.t_market \n" +
                        "        AND S.T_DATECLOSE = TO_DATE ('01010001', 'ddmmyyyy')) " );
      cmd1.execute();

      cmd = DL_RSDCommand(" select * from uvtb_limits_dbt l ");
      cnt = cmd.GetCount();
      rs = cmd.Execute();
      EndAction(0);
      InitProgress(cnt, "Расчет цен по лимитам ", "Расчет цен по лимитам");

      while (rs.movenext())
         if (Panel.GetControl("cbMultyThread").checked == "")
            sql = 
                 " UPDATE DDL_LIMITSECURITES_DBT s \n" +
                 "   SET t_wa_position_price = \n" +
                 "          RSHB_RSI_SCLIMIT.GETWAPOSITIONPRICE (s.t_date, \n" +
                 "                                                 ?, \n" +
                 "                                                 ?, \n" +
                 "                                                 ?, \n" +
                 "                                                 ?, \n" +
                 "                                                 ?, \n" +
                 "                                                 ?, \n" +
                 "                                                 s.t_trdaccid, \n" +
                 "                                                 1 \n" +
                 "                                                ) \n" +
                  " WHERE s.t_id = ? ";
            cmd1 = RsdCommand(sql) ;
            cmd1.addParam("", RSDBP_IN, rs.value("t_client"));
            cmd1.addParam("", RSDBP_IN, rs.value("t_sfd"));
            cmd1.addParam("", RSDBP_IN, rs.value("t_security"));
            cmd1.addParam("", RSDBP_IN, rs.value("t_client_code"));
            cmd1.addParam("", RSDBP_IN, rs.value("t_seccode"));
            cmd1.addParam("", RSDBP_IN, rs.value("t_limit_kind"));
            cmd1.addParam("", RSDBP_IN, rs.value("t_id"));
            cmd1.Execute();
         elif (Panel.GetControl("cbMultyThread").checked == "X")
            if ((mod(i,10) != 0) or (i == 0 ))
               str_id = str_id + rs.value("t_id") + ",";
            else 
               str_id = str_id + rs.value("t_id");
               action = GetSqlAction(str_id);
               cmd1 = RsdCommand(action);
               cmd1.Execute();
               str_id = "";
            end;
            if (mod(i,1000) == 0) /*периодически чистим исполненные джобы*/
               cmd1 = RsdCommand("BEGIN \n" +
                                 "   FOR i \n" +
                                 "      IN (SELECT * \n" +
                                 "            FROM USER_SCHEDULER_JOBS \n" +
                                 "           WHERE     job_name LIKE 'CALCPRICE%' \n" +
                                 "                 AND state = 'DISABLED' \n" +
                                 "                 AND run_count = 1 \n" +
                                 "                 AND failure_count = 0) \n" +
                                 "   LOOP \n" +
                                 "      DBMS_SCHEDULER.drop_job (i.job_name); \n" +
                                 "   END LOOP; \n" +
                                 "END; \n") ;
               cmd1.execute();

               cmd1 = RsdCommand (" select state, count(1) cnt from USER_SCHEDULER_JOBS where job_name like 'CALCPRICE%' group by state order by state");
               rs1 = RsdRecordSet(cmd1);
               msg = "";
               while (rs1.movenext())
                  msg = msg + rs1.value(0) +": " + rs1.value(1)+" ";
               end;
               Message(msg);
            end;
            if (mod(i,4000) == 0)  /*периодически запускаем нестартанувшие джобы*/
               cmd1 = RsdCommand ("BEGIN \n" +
                                  "   FOR i IN (SELECT * FROM USER_SCHEDULER_JOBS \n" +
                                  "           WHERE     job_name LIKE 'CALCPRICE%' \n" +
                                  "                 AND state IN ('DISABLED', 'SCHEDULED') \n" +
                                  "                 AND failure_count = 0 AND run_count = 0) \n" +
                                  "   LOOP \n" +
                                  "      BEGIN \n" +
                                  "         DBMS_SCHEDULER.enable (i.job_name); \n" +
                                  "         DBMS_SCHEDULER.run_job (i.job_name, FALSE); \n" +
                                  "      EXCEPTION \n" +
                                  "         WHEN OTHERS \n" +
                                  "         THEN NULL; \n" +
                                  "      END; \n" +
                                  "   END LOOP; \n" +
                                  "END; \n" );
               cmd1.execute();
            end;
         end;
         UseProgress(i = i + 1);
      end;
      RemProgress;

      if (Panel.GetControl("cbMultyThread").checked == "X")
         if (str_id != "")
            str_id = SubStr(str_id,1,StrLen(str_id)-1);
            action = GetSqlAction(str_id);
            cmd1 = RsdCommand(action);
            cmd1.Execute();
            str_id = "";
         end;

         BegAction(500,"Завершение");
         RslWait(5000);
         cmd1 = RsdCommand ("BEGIN \n" +
                            "   FOR i \n" +
                            "      IN (SELECT * FROM USER_SCHEDULER_JOBS \n" +
                            "           WHERE     job_name LIKE 'CALCPRICE%' \n" +
                            "                 AND state IN ('DISABLED', 'SCHEDULED') \n" +
                            "                 AND failure_count = 0 AND run_count = 0) \n" +
                            "   LOOP \n" +
                            "      BEGIN \n" +
                            "         DBMS_SCHEDULER.enable (i.job_name); \n" +
                            "         DBMS_SCHEDULER.run_job (i.job_name, FALSE); \n" +
                            "      EXCEPTION \n" +
                            "         WHEN OTHERS \n" +
                            "         THEN NULL; \n" +
                            "      END; \n" +
                            "   END LOOP; \n" +
                            "END; \n" );
         cmd1.execute();
         RslWait(5000);

         while (flag)
            cmd1 = RsdCommand (" select state, count(1) cnt from USER_SCHEDULER_JOBS where job_name like 'CALCPRICE%' group by state order by state");
            rs1 = RsdRecordSet(cmd1);
            msg = "";
            while (rs1.movenext())
               msg = msg + rs1.value(0) +": " + rs1.value(1)+" ";
            end;
            Message(msg);
            cmd1 = RsdCommand (" select 1 from USER_SCHEDULER_JOBS where job_name like 'CALCPRICE%' and state = 'RUNNING'");
            rs1 = RsdRecordSet(cmd1);
            if (rs1.movenext())
               RslWait(500);
               continue;
            end;
            cmd1 = RsdCommand("BEGIN \n" +
                              "   FOR i IN (SELECT * FROM USER_SCHEDULER_JOBS \n" +
                              "           WHERE     job_name LIKE 'CALCPRICE%' AND state = 'DISABLED' \n" +
                              "                 AND run_count = 1 AND failure_count = 0) \n" +
                              "   LOOP \n" +
                              "      DBMS_SCHEDULER.drop_job (i.job_name); \n" +
                              "   END LOOP; \n" +
                              "END; \n" );
            cmd1.execute();

            cmd1 = RsdCommand(" select 1 from USER_SCHEDULER_JOBS where job_name like 'CALCPRICE%' and failure_count = 0");
            rs1 = RsdRecordSet(cmd1);
            if (rs1.movenext())
               cmd1 = RsdCommand("BEGIN \n" +
                                 "   FOR i IN (SELECT * FROM USER_SCHEDULER_JOBS \n" +
                                 "           WHERE     job_name LIKE 'CALCPRICE%' AND state IN ('DISABLED', 'SCHEDULED') \n" +
                                 "                 AND failure_count = 0 AND run_count = 0) \n" +
                                 "   LOOP \n" +
                                 "      BEGIN \n" +
                                 "         DBMS_SCHEDULER.enable (i.job_name); \n" +
                                 "         DBMS_SCHEDULER.run_job (i.job_name, FALSE); \n" +
                                 "      EXCEPTION \n" +
                                 "         WHEN OTHERS \n" +
                                 "         THEN NULL; \n" +
                                 "      END; \n" +
                                 "   END LOOP; \n" +
                                 "END; \n" );
               cmd1.execute();
            else
               flag = false;
            end;
            RslWait(5000);
         end;
      end;

      cmd1 = RsdCommand(" MERGE INTO DDL_LIMITSECURITES_DBT i " +
                        "      USING (SELECT * " +
                        "               FROM DDL_LIMITSECURITES_DBT t " +
                        "              WHERE     t.t_market_kind = 'фондовый' " +
                        "                    AND t.t_limit_kind = 2 " +
                        "                    AND t.t_open_balance <> 0 " +
                        "                    AND t_wa_position_price <> 0) u " +
                        "         ON (    i.t_client_code = u.t_client_code " +
                        "             AND i.t_security = u.t_security " +
                        "             AND i.t_trdaccid = u.t_trdaccid " +
                        "             AND i.t_market_kind = 'фондовый' " +
                        "             AND i.t_limit_kind = 365 " +
                        "             AND i.t_date = u.t_date " +
                        "             AND i.t_open_balance <> 0) " +
                        " WHEN MATCHED " +
                        " THEN " +
                        "    UPDATE SET i.t_wa_position_price = u.t_wa_position_price; ");
      cmd1.Execute();

      cmd1 = RsdCommand("BEGIN \n" +
                        "   FOR i IN (SELECT * FROM USER_SCHEDULER_JOBS \n" +
                        "           WHERE     job_name LIKE 'CALCPRICE%' AND state = 'DISABLED' \n" +
                        "                 AND run_count = 1 AND failure_count = 0) \n" +
                        "   LOOP \n" +
                        "      DBMS_SCHEDULER.drop_job (i.job_name); \n" +
                        "   END LOOP; \n" +
                        "END; \n" );
      cmd1.execute();

      Message(" ");
      cmd1 = RsdCommand ("select count(1) cnt  from DDL_LIMITSECURITES_DBT where t_id in ( select t_id from uvtb_LIMITS_DBT ) and  t_wa_position_price <> 0");
      rs1 = RsdRecordSet(cmd1);
      if (rs1.movenext())
         result_protocol.AddString("Рассчитано цен: " + rs1.value("cnt"));
      end;

      EndAction(0);
      return 1;
   end;
End;

//UpdatePrice();