/****************************************************\
 *            Cherednichenko 10.12.2019              *
 * Выгрузка данных из журналов событий в эксель файл *
\****************************************************/

import ActiveX, rsd, RsbFormsInter, Global;
import "or_tpl_h.mac";
import "TFE_lite.mac";

private class c_tables
  var tableName;       // имя таблицы
  var tableDateColumn; // столбец с датой
  var tableTimeColumn; // столбец с временем
end;

private class c_datatype
  var columnName;       // имя таблицы
  var columnType;      // столбец с типом данных
end;

  var table_names: TArray = TArray(); // tablenames and date filedname array
  table_names(0) = c_tables();
  table_names(0).tableName = "dxr_log_dbt";
  table_names(0).tableDateColumn = "t_sysdate";
  table_names(0).tableTimeColumn = "t_systime";

  table_names(1) = c_tables();
  table_names(1).tableName = "dfuncobj_dbt";
  table_names(1).tableDateColumn = "t_createdate";
  table_names(1).tableTimeColumn = null;

  table_names(2) = c_tables();
  table_names(2).tableName = "dss_history_dbt";
  table_names(2).tableDateColumn = "t_beginstamp";
  table_names(2).tableTimeColumn = null;

  table_names(3) = c_tables();
  table_names(3).tableName = "dfuncobj_hist_dbt";
  table_names(3).tableDateColumn = "t_createdate";
  table_names(3).tableTimeColumn = null;

  table_names(4) = c_tables();
  table_names(4).tableName = "dssh_action_dbt";
  table_names(4).tableDateColumn = "t_addstamp";
  table_names(4).tableTimeColumn = null;

  table_names(5) = c_tables();
  table_names(5).tableName = "usr_exchng_log_dbt";
  table_names(5).tableDateColumn = "t_req_dttm";
  table_names(5).tableTimeColumn = null;

  table_names(6) = c_tables();
  table_names(6).tableName = "dqueue_log_dbt";
  table_names(6).tableDateColumn = "t_sysdate";
  table_names(6).tableTimeColumn = "t_systime";

// Преобразование типа clob в текст. ID записи в таблице, длина CLOB (32000), имя таблицы, название поля с CLOBданными
macro GetClobData(ID:integer, LengthClob:integer, tablename:string, datafield:string)
   var dataSetLength;
   var Amount = 32000;
   var Offset = 0;
   var Data = "";

   if(LengthClob > 0)
      while((LengthClob - Offset * Amount) > 0)

         var cmd = RSDCommand( " DECLARE  "
                              +"  v_clobData CLOB; "
                              +"  v_amount INTEGER := " + String(Amount) + "; "
                              +"  v_buffer VARCHAR2(" +String(Amount) + "); "
                              +" BEGIN "
//                              +"   select replace(replace(" + datafield + ", CHR(10), ''), '" + "\"" + "', '" + "\"\"" + "') into v_clobData from " + tablename + " d where t_id = ?; "
                              +"   select replace(replace(replace(" + datafield + ", CHR(10), ''), CHR(13), ''),'^', '') into v_clobData from " + tablename + " d where t_id = ?; "
                              +"   if ( length(v_clobData) > 0 ) then "
                              +"     DBMS_LOB.READ(v_clobData, v_amount, " + String(Offset * Amount + 1) + ", v_buffer); "
                              +"   end if;"
                              +"   ? := v_buffer; "
                              +" END;");

         cmd.addParam("", RSDBP_IN, ID );
         cmd.addParam("", RSDBP_OUT, V_STRING, Amount + 1);
	         cmd.execute();

         if (ValType(cmd.value(1)) != 26 ) // 26 = тип данных SPECVAL
           Data = Data + cmd.value(1);
           Offset = Offset + 1;
         else
           Data = "";
           Offset = Offset + 1;
         end;

      end;
   end;
   return Data;
end;


// Новый макрос, заполняющий Excel файл
macro MakeReport(bdate, edate, utime, btime, etime)
  var i, j;
  var sql, rs, rs2;
  var strBuf:string;
  var begindate;                                                // Begin date
  var enddate;                                                  // End date
  var begintime;                                                // Begin time
  var endtime = etime;                                          // End time
  var begindatetime;                                            // Begin time
  var enddatetime;                                              // End time

  //var table_names = c_tablenames();
  var LogBook=TFE( table_names(0).tableName );

  InitProgress(table_names.size, "Обработка...", "Создание отчетов" );
  i = 0;
  while (i < table_names.size)
  
    if ( i > 0 )
      LogBook.AddSheet(table_names(i).tableName);
      LogBook.ActiveSheet(0);
    end;

    var datatypes: TArray = TArray(); // tablenames and date filedname array
    sql = "select column_name, data_type from user_tab_columns where table_name = upper('" + table_names(i).tableName + "')";
    rs = RsdRecordSet(sql);
    j = 0;
    while (rs.movenext())
      datatypes(j) = c_datatype();
      datatypes(j).columnName = rs.value(0);      // имя столбца
      datatypes(j).columnType = rs.value(1);      // тип данных столбца
      j = j + 1;
    end;
  
    // приводим входящие значения даты и времени к sql виду
    begindate = "to_date('" + bdate + "', 'dd.mm.yyyy')";
    enddate   = "to_date('" + edate + "', 'dd.mm.yyyy')";
    if (utime)
      begintime = "to_date('01.01.01 " + btime + "', 'dd.mm.yyyy hh24:mi:ss')";
      endtime   = "to_date('01.01.01 " + etime + "', 'dd.mm.yyyy hh24:mi:ss')";
      begindatetime = "to_date('" + bdate + " " + btime + "', 'dd.mm.yyyy hh24:mi:ss')";
      enddatetime   = "to_date('" + edate + " " + etime + "', 'dd.mm.yyyy hh24:mi:ss')";
    end;

    // основной запрос к таблице из массива, где учтены даты и время, выбранные в панели
    sql = "select * from " + table_names(i).tablename + " where 1=1 ";
    if (utime)
      if ( table_names(i).timecolumn != "" )
        sql = sql + " and ("
                  + "   ( ("
                  + "     " + table_names(i).tabledatecolumn + " = " + begindate + " "
                  + "     and " + table_names(i).tabletimecolumn + " >= " + begintime + " "
                  + "   ) "
                  + "   or ( "
                  + "     " + table_names(i).tabledatecolumn + " = " + enddate + " "
                  + "     and " + table_names(i).tabletimecolumn + " <= " + endtime + " "
                  + "   ) )"
                  + "   or " + table_names(i).tabledatecolumn + " between " + begindate + "+1 and " + enddate + "-1 "
                  + " ) "
      else
        sql = sql + " and " + table_names(i).tabledatecolumn + " "
                  + " between " + begindatetime + " and " + enddatetime + " "
                  + " order by 1 desc "
      end;
    else
      sql = sql + " and trunc(" + table_names(i).tabledatecolumn + ") ";
      sql = sql + " between trunc(" + begindate + ") and trunc(" + enddate + ") order by 1 desc ";
    end;
//    sql = sql   + " fetch next 10 rows only ";
  
    rs2 = RsdRecordSet(sql);
  
    // выставим ширину столбцов
    j = 0;
    strBuf = "";
    while (j < datatypes.size)
      if (j > 0)
        strBuf = strBuf + ",";
      end;
      strBuf = strBuf + "50";
      j = j + 1;
    end;
    LogBook.ColSizes(strBuf);
  
    var ColumnsRow: TArray = TArray();				 // Одна строка данных    
    // Добавим строку со столбцами
    j = 0;
    strBuf = "";
    while (j < datatypes.size)
      strBuf = datatypes(j).columnName;
      ColumnsRow(ColumnsRow.size) = "B^" + strBuf;
      j = j + 1;
    end;
    LogBook.AddRow(ColumnsRow);
  
    // Заполним лист
    while (rs2.movenext())
      j = 0;
      ColumnsRow = TArray();
      while (j < rs2.fldCount)
        if (datatypes(j).columnType == "CLOB") // Переводим клобы в текстовый вид
          strBuf = GetClobData(rs2.value(0), 32000, table_names(i).tablename, datatypes(j).columnName);
        else
          if ( Valtype(rs2.value(j)) == 26 )
            //strBuf = "SPECVALUE 0";
            strBuf = "NULL";
          else
            strBuf = rs2.value(j);
            strBuf = StrSubst(strBuf, "\n", "");
            strBuf = StrSubst(strBuf, "\r", "");
          end;
        end;
        ColumnsRow(ColumnsRow.size) = strBuf;
        j = j + 1;
      end;
      LogBook.AddRow(ColumnsRow);
    end;
  
    i = i + 1;
    UseProgress(i);

  end;

  RemProgress(table_names.size);
  sql = "select to_char(sysdate, 'ddmmyyyyhh24miss') from dual";
  rs = RsdRecordSet(sql);
  if(rs.movenext())
    LogBook.Go("D:\\RSHB_SOFR\\TxtFile\\intergt_diagnostic_" + rs.value(0));
  else
    msgbox("Не удалось найти текущую дату sysdate, отчет сохранен как intergt_diagnostic_0");
    LogBook.Go("D:\\RSHB_SOFR\\TxtFile\\intergt_diagnostic_0");
  end;
end;

// Формирование панели
class (TRsbPanel) c_GetDataPanel
  // Загрузим текущую sysdate
  var sql = "select to_char(sysdate, 'dd.mm.yyyy hh24:mi:ss') from dual";
  var rs = RsdRecordSet(sql);
  rs.movenext();
  var curdate:Date = substr(rs.value(0), 1, 10);
  //var curtime:Time = substr(rs.value(0), 12, 8);

  private const USER_PUSH_BUTTON_EVENT = 5;

  InitTRsbPanel();
  setSize(33, 13);
  setPosition (40, 11);

  // чекбокс флага использования времени
  var m_useTimeLabel:TRsbLabel = TRsbLabel (9, 1, "Использовать определенное время");
  addLabel(m_useTimeLabel);
  var m_useTime:TRsbCheckBox = TRsbCheckBox;
  m_useTime.setPosition(6, 1);
  m_useTime.checked = false;
  m_useTime.onChecked(R2M(this, "enableTime"));
  addControl(m_useTime);

  // тектсовое поле начальной даты
  var m_beginDateLabel:TRsbLabel = TRsbLabel (2, 3, "Начальная дата:");
  addLabel(m_beginDateLabel);
  var m_beginDateField:TRsbEditField = TRsbEditField(9);
  m_beginDateField.setPosition (2, 4);
  m_beginDateField.setSize (10, 1);
  m_beginDateField.name = "bDate";
  m_beginDateField.value = curdate;
  addControl(m_beginDateField);

  // тектсовое поле конечной даты
  var m_endDateLabel:TRsbLabel = TRsbLabel (20, 3, "Конечная дата:");
  addLabel(m_endDateLabel);
  var m_endDateField:TRsbEditField = TRsbEditField(9);
  m_endDateField.setPosition (20, 4);
  m_endDateField.setSize (10, 1);
  m_endDateField.name = "eDate";
  m_endDateField.value = curdate;
  addControl(m_endDateField);

  // тектсовое поле начального времени
  var m_beginTimeLabel:TRsbLabel = TRsbLabel (2, 5, "Начальное время:");
  addLabel(m_beginTimeLabel);
  var m_beginTimeField:TRsbEditField = TRsbEditField(10);
  m_beginTimeField.setPosition (2, 6);
  m_beginTimeField.setSize (10, 1);
  m_beginTimeField.editable = false;
  var curtime:Time = "00:00:00";
  m_beginTimeField.value = curtime;
  addControl(m_beginTimeField);

  // тектсовое поле конечного времени
  var m_endTimeLabel:TRsbLabel = TRsbLabel (20, 5, "Конечное время:");
  addLabel(m_endTimeLabel);
  var m_endTimeField:TRsbEditField = TRsbEditField(10);
  m_endTimeField.setPosition (20, 6);
  m_endTimeField.setSize (10, 1);
  m_endTimeField.editable = false;
  curtime = time("23:59:59");
  m_endTimeField.value = curtime;
  addControl(m_endTimeField);

  // тектсовое поле информации о периоде выборки для отчетов
  var m_InfoFieldLabel:TRsbLabel = TRsbLabel (2, 8, "Отчеты буду созданы за (включительно):");
  addLabel(m_InfoFieldLabel);
  var m_infoField:TRsbEditField = TRsbEditField(7);
  var m_infoField_v:string = "";
  m_infoField.bindValue(this, "m_infoField_v", 80);
  m_infoField.setPosition (2, 9);
  m_infoField.setSize (30, 2);
  m_infoField.enabled = false;
  addControl(m_infoField);

  var m_goButton: TRsbPushButton = TRsbPushButton ("Ок");
  m_goButton.setPosition(14, 12);
  m_goButton.setSize(6, 1);
  m_goButton.onClicked(R2M(this, "MakeReportOnButton"));
  addControl(m_goButton);


  addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "onKeyPressed"));
  addEventHandler(RSB_EV_REMOVE_FOCUS, R2M(this, "changeInfoText"));

  // меняем информационную панель, где отображается, за какой период будет происходить выборка
  macro changeInfoText()
    //var infoText = "Выборка за: \n";
    var infoText = "";
    if (m_beginDateField.value == m_endDateField.value)
      infoText = infoText + m_beginDateField.value;
      if (m_useTime.checked)
        infoText = infoText + ", " + m_beginTimeField.value + " - " + m_endTimeField.value;
      end;
    else
      infoText = infoText + m_beginDateField.value;
      if (m_useTime.checked)
        infoText = infoText + " " + m_beginTimeField.value;
      end;
      infoText = infoText + " -\n" + m_endDateField.value;
      if (m_useTime.checked)
        infoText = infoText + " " + m_endTimeField.value;
      end;
    end;
    m_infoField.value = infoText;
  end;

  // включить/выключить поля времени
  macro enableTime()
    if (m_useTime.checked() )
      m_beginTimeField.editable = true;
      m_endTimeField.editable = true;
    else
      m_beginTimeField.editable = false;
      m_endTimeField.editable = false;
    end;
    changeInfoText();
    this.redraw();
  end;

  // если дата указана, запустить создание отчетов
  macro MakeReportOnButton()
    if (m_beginDateField.value == "")
      msgbox("Некорректная начальная дата!");
      return false;
      
    elif (m_endDateField.value == "")
      msgbox("Некорректная конечная дата!");
      return false;
    end;

    //makereports(m_beginDateField.value, m_endDateField.Value, m_useTime.checked, m_beginTimeField.value, m_endTimeField.value);
    MakeReport(m_beginDateField.value, m_endDateField.Value, m_useTime.checked, m_beginTimeField.value, m_endTimeField.value);
    this.close(0);
    return true;
  end;

  macro onKeyPressed(RE)
    var newDate;
    if (RE.KeyCode == 317) // 317 = клавиша F3, в полях даты позволяем выбор по календарю
      if (this.focusedControl.name == "bDate")
        newDate = GetDateByCalendar(m_beginDateField.value);
       if (newDate <= m_endDateField.value)
         m_beginDateField.value = newDate;
       end;
      elif (this.focusedControl.name == "eDate")
        newDate = GetDateByCalendar(m_endDateField.value);
       if (newDate >= m_beginDateField.value)
         m_endDateField.value = newDate;
       end;
      end;
      changeInfoText();
      this.redraw();
    elif (RE.KeyCode == 316) // 316 0 F2
      MakeReportOnButton();
    end;
  end;

  changeInfoText();

end;

var GetDataPanel = c_GetDataPanel;
GetDataPanel.run;
