/*
$Name:        nptxskipcat_form.mac
$Module:      Ценные бумаги
$Description: 
*/
import "DlRepForm_h.mac", "dlutils.mac";

CONST PNFLD_NOCOUNTISSVTB_LOADFILE           = 0,
      PNFLD_NOCOUNTISSVTB_CHANGETOENAB       = 1,
      PNFLD_NOCOUNTISSVTB_CHANGETODISAB      = 2;

PRIVATE CONST
  H_LOADFILE         = 101,
  H_CHANGETOENAB     = 102,
  H_CHANGETODISAB    = 103;

/*Переключатель*/
private MACRO DL_FldProc_CheckBox_NoCountIIS( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        var changeFldNumber = IIF((pThis.m_CurrentNumberInPanel == PNFLD_NOCOUNTISSVTB_CHANGETOENAB),H_CHANGETODISAB,H_CHANGETOENAB);
        if( FldRealValue != SET_CHAR )
           FldShowValue = FldRealValue = SET_CHAR;
           pThis.SetLinkedValue(changeFldNumber, UNSET_CHAR);
        else
           FldShowValue = FldRealValue = UNSET_CHAR;
           pThis.SetLinkedValue(changeFldNumber, SET_CHAR);
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_File( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    var FilePath = "";
    var isTerm = false;
    if (not isStandalone())
      var choise = DL_Menu(makeArray("Терминал","Сервер"),"Где ищем файл?");
      if (choise == -1)
        return CM_DEFAULT;
      end;
      if (choise == 0)
        isTerm = true;
      end;
    end;
    if (not SelectFile ( FilePath, "*.xlsx,*.xls", "Выберите файл к обработке", 0, isTerm ))
      return CM_DEFAULT;
    end;
    FldRealValue = IIF(isTerm, "$", "") + FilePath;
    FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;


CLASS (DL_CPanel) NOCOUNTISSVTB_Panel()
  var m_LoadFile;
  var m_ChangeToEnab;

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_NOCOUNTISSVTB_LOADFILE,      H_LOADFILE,         @FldProc_File,                   "");
     AddFieldProc( 0, PNFLD_NOCOUNTISSVTB_CHANGETOENAB,  H_CHANGETOENAB,     @DL_FldProc_CheckBox_NoCountIIS, SET_CHAR);
     AddFieldProc( 0, PNFLD_NOCOUNTISSVTB_CHANGETODISAB, H_CHANGETODISAB,    @DL_FldProc_CheckBox_NoCountIIS, UNSET_CHAR );
  END;

  PRIVATE MACRO Check():BOOL
    if( this.GetFieldValue( PNFLD_NOCOUNTISSVTB_LOADFILE ) == "" )
      msgbox("Дальнейшее выполнение операции невозможно, необходимо задать путь к файлу");
      return false;
    end;
    return true;
  END;

  MACRO Run()
    var stat = Run();

    m_LoadFile         = GetFieldValue(PNFLD_NOCOUNTISSVTB_LOADFILE);
    m_ChangeToEnab     = IIF(GetFieldValue(PNFLD_NOCOUNTISSVTB_CHANGETOENAB) == SET_CHAR, true, false);

    return stat;
  END;

  initDL_CPanel( "VTB.lbr", "NOCNTIIS" );
END;