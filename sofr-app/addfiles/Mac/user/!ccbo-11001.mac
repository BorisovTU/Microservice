IMPORT RSD;

/**
@brief 		Функция возвращает строку даты-времени
*/
private macro DateTimeStr
   var dd, mm, yyyy;
   var hh, min, sec;
   var dt: date;
   var tm: time;

   dt = date;
   tm = time;
   DateSplit(dt, dd, mm, yyyy);
   TimeSplit(tm, hh, min, sec);

   return string(yyyy:o:4)+"/"+string(mm:o:2)+"/"+string(dd:o:2)+" "+string(hh:o:2)+":"+string(min:o:2)+":"+string(sec:o:2);
end;

/**
@brief 		Функция возвращает строку для статуса результата
@param[in] 	p_Ret 		код результата
*/
private macro GetResultString(p_Ret: integer):  string
  var x_ResStr: string;
  if( p_Ret == 0 )
    x_ResStr = "(Ошибок нет)";
  elif( p_Ret == 1 ) 
    x_ResStr = "(ошибка при выполнении процедуры)";
  elif( p_Ret == 2 ) 
    x_ResStr = "(ошибка определения размера)";
  elif( p_Ret == 3 ) 
    x_ResStr = "(размер не сходится)";
  elif( p_Ret == 4 ) 
    x_ResStr = "(слишком маленький размер)";
  else
    x_ResStr = "(неизвестная ошибка)";
  end;
  return x_ResStr;
end;


/**
@brief 		Функция возвращает строку для полноты копирования
@param[in] 	p_Limit 	количество заданий для обработки, если 0, то все
*/
private macro GetLimitString(p_Limit: integer):  string
  var x_LimitStr: string;
  if( p_Limit == 0 )
    x_LimitStr = "(полное копирование)";
  else
    x_LimitStr = "(частичное копирование)";
  end;
  return x_LimitStr;
end;


/**
@brief 		Функция возвращает строку для параметра фиксации результата
@param[in] 	p_FixFlag 	флаг фиксации, 1 -- фиксация производится, иначе -- не производится
@param[in] 	p_SrcTable 	таблица-источник (непартиционированная, например, "DOPRDOCS_DBT")
*/
private macro GetFixFlagString(p_FixFlag: integer, p_SrcTable: string):  string
  var x_FixFlagStr: string;
  if( p_FixFlag == 0 )
    x_FixFlagStr = "(результат не фиксируется)";
  else
    x_FixFlagStr = "(при успехе результат будет зафиксирован, таблица "+string(p_SrcTable)+" будет удалена)";
  end;
  return x_FixFlagStr;
end;


/**
@brief 		Запуск процесса копирования данных таблицы OPRDOCS_DBT
                (непартиционированной в партиционированную)
@param[in] 	p_SrcTable 	таблица-источник (непартиционированная, например, "DOPRDOCS_DBT")
@param[in] 	p_DstTable 	таблица-приемник (партиционированная, например, "DOPRDOCS1_DBT")
@param[in] 	p_Limit 	количество заданий для обработки, если 0, то все
@param[in] 	p_SwitchFlag 	флаг переключения контекста, 1 -- переключение до копирования, 2 -- после
@param[in] 	p_FixFlag 	флаг фиксации, 1 -- фиксация производится, иначе -- не производится
*/
macro CopyOPRDOCS(p_SrcTable: string, p_DstTable: string, p_Limit: integer, p_SwitchFlag: integer, p_FixFlag: integer)
  var 
     sql, cmd
     , x_Ret : INTEGER
     , x_CalcID : STRING
     , x_Expected : INTEGER
     , x_Processed : INTEGER
     , x_SrcTable : STRING = p_SrcTable
     ;
 
  sql =  "DECLARE "
       + "BEGIN "
       + "  ? := OPRDOCS_utl.InitProcess(); "
       + "END; "
  ;
  cmd = RSDCommand(sql);
  cmd.addParam("x_CalcID", RSDBP_OUT, V_STRING, 100);
  cmd.execute();
  x_CalcID = cmd.value("x_CalcID");
    
  println(DateTimeStr() + " Запуск процедуры копирования данных из "+string(p_SrcTable)+" в "+string(p_DstTable)+"(x_CalcID: "+string(x_CalcID)+")");
  println();

  if( p_SwitchFlag == 1 )
    println("Переключение контекста выполняется до копирования");
  elif( p_SwitchFlag == 2 ) 
    println("Переключение контекста выполняется после копирования");
  else 
    println("Переключение контекста не выполняется");
  end;

  if( p_SwitchFlag > 0 )
    x_SrcTable = p_SrcTable+"_OLD";
    println(string(p_SrcTable)+" будет переименовано в "+string(x_SrcTable));
    println(string(p_DstTable)+" будет переименовано в "+string(p_SrcTable));
  end;

  println("Количество заданий для обработки: "+string(p_Limit)+" "+GetLimitString(p_Limit));
  println("Флаг фиксации результата: "+string(p_FixFlag)+" "+GetFixFlagString(p_FixFlag, x_SrcTable));
  println("");

  sql =  "DECLARE "
       + "BEGIN "
       + "  ? := OPRDOCS_utl.ExecProcess(?, ?, ?, ?, ?, ?, ?, ?); "
       + "END; "
  ;

  cmd = RSDCommand(sql);
  cmd.addParam("x_Ret", RSDBP_OUT, V_INTEGER);
  cmd.addParam("", RSDBP_IN, x_CalcID);
  cmd.addParam("", RSDBP_IN, p_SrcTable);
  cmd.addParam("", RSDBP_IN, p_DstTable);
  cmd.addParam("", RSDBP_IN, p_Limit);
  cmd.addParam("", RSDBP_IN, p_SwitchFlag);
  cmd.addParam("", RSDBP_IN, p_FixFlag);
  cmd.addParam("x_Expected", RSDBP_OUT, V_INTEGER);
  cmd.addParam("x_Processed", RSDBP_OUT, V_INTEGER);
  cmd.execute();
  x_Ret = cmd.value("x_Ret");
  x_Expected = cmd.value("x_Expected");
  x_Processed = cmd.value("x_Processed");

  println(DateTimeStr() + " Процедура копирования данных завершена");
  println("");
  println("Ожидалось записей: "+string(x_Expected));
  println("Скопировано: "+string(x_Processed));
  println("");
  println("Статус результата: "+string(x_Ret)+" "+GetResultString(x_Ret));

end;

/**
@brief 		Запуск процесса копирования данных таблицы OPRDOCS_DBT
                (непартиционированной в партиционированную)
@param[in] 	p_Mode 		режим переноса: 0 -- для прода, 1 -- для тестирования, 2 -- для разработки
*/
macro ExecProcess( p_Mode: integer ) 

  println("CCBO-11001 start");

  if( p_Mode == 0 )
    // Режим для прода
    CopyOPRDOCS("DOPRDOCS_DBT", "DOPRDOCS1_DBT"
      , 0     	// копируется все
      , 1	// контекст переключается до копирования
      , 1	// фиксация производится
    );
  elif( p_Mode == 1 ) 
    // Режим для тестирования
    CopyOPRDOCS("DOPRDOCS_DBT", "DOPRDOCS1_DBT"
      , 0     	// копируется все
      , 2	// контекст переключается после копирования
      , 0	// фиксация не производится
    );
  else
    // Режим для разработки
    CopyOPRDOCS("DOPRDOCS_DBT", "DOPRDOCS1_DBT"
      , 0     	// копируется все
      , 0	// контекст не переключается
      , 0	// фиксация не производится
    );
  end;

  println("CCBO-11001 end");

end;

ExecProcess( 0 );
