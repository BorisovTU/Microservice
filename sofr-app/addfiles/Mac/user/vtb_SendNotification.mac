/*
$Name:         vtb_SendNotification.mac
$Module:       Ценные бумаги
$Description:  Рассылка уведомлений о переплате НДФЛ
*/

import rsd, BankInter, dlcontrfunc;
import "spRepFun.mac";
import "u_common_email_utils.mac";

class Logger()

    macro RepMainHeader()
       var str;
       str =     "                             ПРОТОКОЛ ВЫПОЛНЕНИЯ ПРОЦЕДУРЫ РАССЫЛКИ УВЕДОМЛЕНИЙ  \n";
       return str;                                                                                               
    end;

    macro RepHeader()
       var str = "";
       str = str+"\n";
       str = str+"Дата выполнения  : "+date()+"\n";
       str = str+"Время выполнения : "+time()+"\n";
       str = str+"┌─┬─────────────────────┬─────────────────────────────────────┬────────────────────────────────┬─────────────────────────────────────────────────────────────────────┬\n";
       str = str+"│ │ Код клиента         │ ФИО Клиента                         │ Сумма переплаты(НДФЛ СОФР)     │  Результат выполнения                                               │\n";
       str = str+"├─┼─────────────────────┼─────────────────────────────────────┼────────────────────────────────┼─────────────────────────────────────────────────────────────────────┤";
       return str;                                                                                               
    end;                   

    macro RepFooter(suc, er)                                                       
       var suc_cnt = suc;
       var str = "└─┴─────────────────────┴─────────────────────────────────────┴────────────────────────────────┴─────────────────────────────────────────────────────────────────────┘\n";
       str = str+"Обработано успешно: " + suc_cnt + "\n";               
       str = str+"Ошибки обработки : " + er + "\n";
       str = str+"Всего            : " + (suc + er) +"\n";
      return str;
    end;

    macro RepStr(p0,p1,p2,p3,p4)
        var str  = "│"+string(p0    :1)   +"│"+
                       string(p1    :21:l)+"│"+
                       string(p2    :37:l)+"│"+
                       string(p3    :32:l)+"│"+
                       string(p4    :69:l)+"│"; 
       return str;
    end;
end;

MACRO uGetTAXCalculatedDue(pClient, ResidenceStatus, pBeg, pEnd, Year, pIIS, pRg, pRs, pRp, Bs, Dg:@MONEY, Ds:@MONEY, Dm:@MONEY, Dp:@MONEY, Pg:@MONEY,  Pm:@MONEY, Pp:@MONEY, Ps:@MONEY, FromOutSyst:BOOL)
   var S, S15, Bg=$0, Bm=$0,  Bp=$0, SN=$0,/*, Pg=$0, Pm=$0, Pp=$0,*/ ToPay, ToReturn, PayNDR, ReturnNDR, TaxPriority;
   var Bg1 = $0, Bg2_2 = $0, Bg2_3 = $0, Bg9 = $0, Bg5 = $0, Pg1 = $0, Pg2 = $0, Pg9 = $0, Pg5 = $0, Pp1 = $0, Pp2 = $0, Pp9 = $0, Pp5 = $0;
   var Dg1 = $0, Dg2 = $0, Dg9 = $0, Dg5 = $0, Dp1 = $0, Dp2 = $0, Dp9 = $0, Dp5 = $0;
   var SPg15 = $0;
   var ToutNat = $0;
   var query, cmd, DataSet;
   var AddWhereNotOut = " obj.t_FromOutSyst <> 'X'";
   var AddWhereFromOut = " obj.t_FromOutSyst = 'X'";   

   var Taxe   = $0; //Уплаченный налог по базовой ставке по сформированным в этой ф-ции НДР
   var Taxe15 = $0; //Уплаченный налог по повышенной ставке по сформированным в этой ф-ции НДР

   var PtIsRes = DL_GetPartyResident(pClient);
   Dg = $0;
   Dm =  $0;       
   Ds =  $0;
   Dp =  $0;
   Pg = $0;
   Pm =  $0;       
   Ps =  $0;
   Pp =  $0;

   PRIVATE MACRO IsPartyResident()
      if (PtIsRes == NPTXPARTY_RESIDENT)
          return true;
      else
          return false;
      end;
   END;

   PRIVATE MACRO Rg()
      var retval;
      if (IsPartyResident())
          retval = 13.0;
      else
          //Если вдруг на клиенте физлице нерезиденте задано примечание "Налоговая ставка для физического лица - нерезидента по общим доходам"
          var _stat = ПолучитьЗначениеПримечанияНаДату(pClient, PARTY_NOTE_KIND_NPTX_RATE_NOTRES /*налоговая ставка физикаНерезидента*/, pEnd, @retval);
          if (_stat != 0)
              retval = 30.0;
          end;
      end;
      return retval;
   END;
 
   PRIVATE MACRO Rp()
      var retval;
      if (IsPartyResident())
          retval = 15.0;
      else
          retval = 0.0;
      end;
      return retval;
   END;

   PRIVATE MACRO Rs()
      if (IsPartyResident())
          return 9.0;
      else
          return 15.0;
      end;
   END;

   private macro GetPg(NpTxKindsStr:string, AddWhereCond)
     var _Pg = $0;

     _Pg = GetRubSumByClientOperDocKind(pClient, string(NpTxKindsStr), pBeg, pEnd, DL_CALCNDFL, null, pIIS, AddWhereCond) + //созданные в операциях расчета НОБ
           GetRubSumByClientOperDocKind(pClient, string(NpTxKindsStr), pBeg, pEnd, 0 /*только пользовательские*/,null, pIIS, AddWhereCond) +
           GetRubSumByClientOperDocKind(pClient, string(NpTxKindsStr), NULL, NULL, DL_HOLDNDFL, pBeg, pIIS, AddWhereCond) + //созданные в операциях удержания НДФЛ с T_PREVDATE такой же, как и текушей операции
           GetRubSumByClientOperDocKind(pClient, string(NpTxKindsStr), pBeg, pEnd, DL_WRTMONEY, null, pIIS, AddWhereCond) + //созданные в операциях списания/зачисления денежных средств
           GetRubSumByClientOperDocKind(pClient, string(NpTxKindsStr), pBeg, pEnd, DL_RETIREMENT_OWN, null, pIIS, AddWhereCond) + //созданные в операциях погашения ОЭБ
           /*плюс операции в ПС <Векселя банка>*/
           GetRubSumByClientFromVeksel(pClient, string(NpTxKindsStr), pBeg, pEnd, string(DL_VEKSELDRAWORDER)+", "+ //погашение сюда же входит выкуп
                                                                                  string(DL_VSBARTERORDER)  +", "+ //мена
                                                                                  string(DL_VSINTERCHANGE) );      //зачет взаимных требований

     if(pIIS == 0)
       _Pg = _Pg + GetRubSumByClientFromDepo(pClient, string(NpTxKindsStr), pBeg, pEnd); //созданные в операции "Расчет и начисление дохода" в депозитарии
     end;

     return _Pg;
   end;

   private macro GetPp(NpTxKindsStr:string, AddWhereCond)
     var _Pp = $0;

     _Pp = GetRubSumByClientOperDocKind(pClient, string(NpTxKindsStr), pBeg, pEnd, DL_CALCNDFL, null, pIIS, AddWhereCond) + //созданные в операциях расчета НОБ
           GetRubSumByClientOperDocKind(pClient, string(NpTxKindsStr), pBeg, pEnd, 0 /*только пользовательские*/,null, pIIS, AddWhereCond) +
           GetRubSumByClientOperDocKind(pClient, string(NpTxKindsStr), NULL, NULL, DL_HOLDNDFL, pBeg, pIIS, AddWhereCond) + //созданные в операциях удержания НДФЛ с T_PREVDATE такой же, как и текушей операции
           GetRubSumByClientOperDocKind(pClient, string(NpTxKindsStr), pBeg, pEnd, DL_WRTMONEY, null, pIIS, AddWhereCond); //созданные в операциях списания/зачисления денежных средств

     if(pIIS == 0)
       _Pp = _Pp + GetRubSumByClientFromDepo(pClient, string(NpTxKindsStr), pBeg, pEnd); //созданные в операции "Расчет и начисление дохода" в депозитарии
     end;

     return _Pp;
   end;

   private macro GetPs(NpTxKindsStr:string, AddWhereCond)
     var _Ps = $0;

     _Ps = GetRubSumByClientOperDocKind(pClient, string(NpTxKindsStr), pBeg, pEnd, DL_CALCNDFL, null, pIIS, AddWhereCond) + //созданные в операциях расчета НОБ
           GetRubSumByClientOperDocKind(pClient, string(NpTxKindsStr), pBeg, pEnd, 0 /*только пользовательские*/, null, pIIS, AddWhereCond) +
           GetRubSumByClientOperDocKind(pClient, string(NpTxKindsStr), NULL, NULL, DL_HOLDNDFL, pBeg, pIIS, AddWhereCond); //созданные в операциях удержания НДФЛ с T_PREVDATE такой же, как и текушей операции

     if(pIIS == 0)
       _Ps = _Ps + GetRubSumByClientFromDepo(pClient, string(NpTxKindsStr), pBeg, pEnd); //созданные в операции "Расчет и начисление дохода" в депозитарии
     end;

     return _Ps;
   end;

   private macro GetPm(NpTxKindsStr:string, AddWhereCond)
     var _Pm = $0;

     _Pm = GetRubSumByClientOperDocKind(pClient, string(NpTxKindsStr), pBeg, pEnd, DL_CALCNDFL, null, pIIS, AddWhereCond) + //созданные в операциях расчета НОБ
           GetRubSumByClientOperDocKind(pClient, string(NpTxKindsStr), pBeg, pEnd, 0 /*только пользовательские*/,null, pIIS, AddWhereCond) +
           GetRubSumByClientOperDocKind(pClient, string(NpTxKindsStr), NULL, NULL, DL_HOLDNDFL, pBeg, pIIS, AddWhereCond); //созданные в операциях удержания НДФЛ с T_PREVDATE такой же, как и текушей операции

     if(pIIS == 0)
       _Pm = _Pm + GetRubSumByClientFromDepo(pClient, string(NpTxKindsStr), pBeg, pEnd); //созданные в операции "Расчет и начисление дохода" в депозитарии
     end;

     return _Pm;
   end;

   private macro RedistrD(_Dg:@money, _Dp:@money, TxKind, Comment, TxKind_15, Comment_15)
      var Sg = $0, Sp = $0;
 
      if((_Dg != 0) or (_Dp != 0))

       if( ((abs(_Dg) != _Dg) and (abs(_Dp) == _Dp)) or
           ((abs(_Dg) == _Dg) and (abs(_Dp) != _Dp))
         ) //Значения _Dg И _Dp с разными знаками

            if(abs(_Dg) >= abs(_Dp))
               Sg = _Dp;
               Sp = -Sg;
         elif(abs(_Dg) < abs(_Dp))
               Sg = _Dg;
               Sp = -Sg;
            end;
 
            if(Sg != 0)
               _Dg = _Dg - Sg;
            end;
 
            if(Sp != 0)
               _Dp = _Dp - Sp;
           SPg15 = SPg15 + Sp;

            end;

         end;
      end;
   end;
    
   if((Year >= 2021) and (IsPartyResident()) and ((pIIS == NULL) or (pIIS == 0)))
     Bg1 = GetRubSumByClient(pClient, string(TXOBJ_BASEG1), pBeg, pEnd, null, pIIS, null, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
     Pg1 = GetPg(TXOBJ_DIVPAY_SEC+","+TXOBJ_DIVPAY_SEC_0, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
     Pp1 = GetPp(TXOBJ_PAIDGENERAL_15_1+","+TXOBJ_PAIDGENERAL_15_1_0, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut)); 
     Dg1 = round(Rg() * Min(Bg1, NPTX_LIMINCOME_MAX15) / 100.0 - Pg1, 0);
     Dp1 = round(Rp() * Max(Bg1-NPTX_LIMINCOME_MAX15, $0) / 100.0 - Pp1, 0);

     Bg2_2 = GetRubSumByClient(pClient, TXOBJ_BASEG2, pBeg, pEnd, null, pIIS);
     Bg2_3 = GetRubSumByClient(pClient, TXOBJ_BASEG3, pBeg, pEnd, null, pIIS);
     Pg2 = GetPg(TXOBJ_PAIDGENERAL+","+TXOBJ_PAIDGENERAL_0+","+TXOBJ_PAIDMATERIAL, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
     Pp2 = GetPp(TXOBJ_PAIDGENERAL_15_2, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut))/*+GetPp(TXOBJ_PAIDGENERAL_15,AddWhereFromOut)*/;
     Dg2 = round(Rg() * Min(Bg2_2, NPTX_LIMINCOME_MAX15) / 100.0 + Rg() * Min(Bg2_3, NPTX_LIMINCOME_MAX15) / 100.0 - Pg2, 0);
     Dp2 = round(Rp() * Max(Bg2_2-NPTX_LIMINCOME_MAX15, $0) / 100.0 + Rp() * Max(Bg2_3-NPTX_LIMINCOME_MAX15, $0) / 100.0 - Pp2, 0);

     Bg9 = GetRubSumByClient(pClient, string(TXOBJ_BASEG9), pBeg, pEnd, null, pIIS);
     Pg9 = GetPg(TXOBJ_PAIDBILL);
     Pp9 = GetPp(TXOBJ_PAIDGENERAL_15_9);
     Dg9 = round(Rg() * Min(Bg9, NPTX_LIMINCOME_MAX15) / 100.0 - Pg9, 0);
     Dp9 = round(Rp() * Max(Bg9-NPTX_LIMINCOME_MAX15, $0) / 100.0 - Pp9, 0);
   end;

   if((Year >= 2021) and (IsPartyResident()) )
        if( (pIIS == NULL) or (pIIS == 0) )
          Bs = GetRubSumByClient(pClient, string(TXOBJ_BASESPECIAL), pBeg, pEnd, null, pIIS);
          Ps = GetPs(TXOBJ_PAIDSPECIAL+ ","+TXOBJ_PAIDSPECIAL_0);

          if(SN != 0)
             RedistrD(@Dg1, @Dp1, TXOBJ_DIVPAY_SEC,  "Уплаченный налог на дивиденды", TXOBJ_PAIDGENERAL_15_1, "Уплаченный налог по повышенной ставке 15% по НОБ вида 1");
             RedistrD(@Dg2, @Dp2, TXOBJ_PAIDGENERAL, "Уплаченный налог",              TXOBJ_PAIDGENERAL_15_2, "Уплаченный налог по повышенной ставке 15% по НОБ вида 2");
             RedistrD(@Dg9, @Dp9, TXOBJ_PAIDBILL,    "Уплаченный налог по векселям",  TXOBJ_PAIDGENERAL_15_9, "Уплаченный налог по повышенной ставке 15% по НОБ вида 9");
          end;

          //Pg = GetPg(TXOBJ_PAIDGENERAL+","+TXOBJ_PAIDGENERAL_0 + "," + TXOBJ_PAIDMATERIAL + "," + TXOBJ_PAIDBILL + "," + TXOBJ_DIVPAY_SEC + "," + TXOBJ_DIVPAY_SEC_0, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
          Pm = 0;
        end;
        if((pIIS == NULL) or (pIIS == 1))
          Bg5 = GetRubSumByClient(pClient, string(TXOBJ_BASEG5), pBeg, pEnd, null, null, null, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
          Pg5 = GetPg(TXOBJ_PAIDGENERAL_IIS, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
          Pp5 = GetPp(TXOBJ_PAIDGENERAL_15_IIS, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
          Dg5 = Rg() * Min(Bg5, NPTX_LIMINCOME_MAX15) / 100.0 - Pg5;
          Dp5 = Rp() * Max(Bg5-NPTX_LIMINCOME_MAX15, $0) / 100.0 - Pp5;

          Bs = Bs + GetRubSumByClient(pClient, string(TXOBJ_BASESPECIAL_IIS ), pBeg, pEnd, null, pIIS, null, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
          Ps = Ps + GetPs(TXOBJ_PAIDSPECIAL_IIS, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));

        end;
		
        Pg = Pg1 + Pg2 + Pg9 + Pg5;
        Pp = Pp1 + Pp2 + Pp9 + Pp5;
        Dg = Dg1 + Dg2 + Dg9 + Dg5;
        Dp = Dp1 + Dp2 + Dp9 + Dp5;

        Ds = Rs() * Bs / 100.0 - Ps;
   else

        if( (pIIS == NULL) or (pIIS == 0) )
            Bg  =   GetRubSumByClient(pClient, string(TXOBJ_BASEGENERAL), pBeg, pEnd, null, pIIS, null, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut))
                  + GetRubSumByClient(pClient, string(TXOBJ_BASEG2), pBeg, pEnd, null, pIIS, null, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));

            Bm = GetRubSumByClient(pClient, string(TXOBJ_BASEMATERIAL), pBeg, pEnd, null, pIIS, null, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
            Bs = GetRubSumByClient(pClient, string(TXOBJ_BASESPECIAL_DIV), pBeg, pEnd, null, pIIS, null, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));

            Pg = GetPg(TXOBJ_PAIDGENERAL+","+TXOBJ_PAIDGENERAL_0, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
            Pm = GetPm(TXOBJ_PAIDMATERIAL, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
            Ps = GetPs(TXOBJ_DIVPAY_SEC+","+TXOBJ_DIVPAY_SEC_0, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
        end;
        if((pIIS == NULL) or (pIIS == 1))
            Bg = Bg + GetRubSumByClient(pClient, string(TXOBJ_BASEGENERAL_IIS ), pBeg, pEnd, null, pIIS, null, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
            Bm = Bm + GetRubSumByClient(pClient, string(TXOBJ_BASEMATERIAL_IIS), pBeg, pEnd, null, pIIS, null, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
            Bs = Bs + GetRubSumByClient(pClient, string(TXOBJ_BASESPECIAL_IIS ), pBeg, pEnd, null, pIIS, null, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
       
            Pg = Pg + GetPg(TXOBJ_PAIDGENERAL_IIS, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
            Pm = Pm + GetPm(TXOBJ_PAIDMATERIAL_IIS, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
            Ps = Ps + GetPs(TXOBJ_PAIDSPECIAL_IIS, IIF((FromOutSyst == true) or (FromOutSyst == NULL), NULL, AddWhereNotOut));
        end;
      
        Dg = Rg() * Bg / 100.0 - Pg;
        Dm = Rg() * Bm / 100.0 - Pm;
        Ds = Rs() * Bs / 100.0 - Ps;
   end;

   Dg = round(Dg,0);
   Dm = round(Dm,0);         
   Ds = round(Ds,0);
   Dp = round(Dp,0);
   Pg = round(Pg,0);
   Ps = round(Ps,0);
   Pm = round(Pm,0);
   Pp = round(Pp,0);
END;



private macro preptable()
  var cmd = RSDCommand("create table vtb_overpay_notify (t_operdate date, t_clientid integer, t_overpaysum number, t_issend char(1) default chr(0) );");
      cmd.execute;
 onError(er)
   return null;
end;

private macro insert_overpay_send_log(pClientID, pOverPaySum)
 var cmd = RSDCommand("Insert into vtb_overpay_notify values(sysdate, ?,?, chr(88))");
     cmd.AddParam("", rsdbp_in, pClientID);
     cmd.AddParam("", rsdbp_in, pOverPaySum);
     cmd.Execute;
 onError(er)
   return null;
end;

private macro IsNotifySendByClient(pClientID)
  var sql = "Select count(1) as count_send from vtb_overpay_notify where t_clientid = ? and t_issend = chr(88)";
  var cmd = DL_RSDCommand();
     cmd.AddParam(pClientID);
  var rs = cmd.execute(sql);
  if(rs.movenext)
    if(rs.count_send > 0)
      return true;
    else
      return false;
    end;
  else
    return false;
  end;
end;

macro uGetOverPayAmount(pClient, pBegDate, pEndDate, pYear, pIIS, pOperID, pFromOutSyst)
  //IN
  //var pClient = 6209;
  //var pBegDate = date(1, 1, 2021);
  //var pEndDate = date(31, 12, 2021);
  //var Year = 2021;
    var Year = pYear;
  //var IIS = NULL;//0, 1, NULL
    var IIS = pIIS;
  //var OperID = 507;
    var OperID = pOperID;
  //var FromOutSyst = true;
    var FromOutSyst = pFromOutSyst;

  //OUT
  var Bg = $0, Bm = $0, Bs = $0, Bp = $0, Bd = $0;
  var Dm = $0, Ds = $0, Dg = $0, Dp = $0;
  var Pg = $0, Pm = $0, Ps = $0, Pp = $0, Pd = $0;

  var Kind = "";
  if((IIS == NULL) or (IIS == 0))
     Kind = string(TXOBJ_PAIDSPECIAL);
  end;
  if((IIS == NULL) or (IIS == 1))
     if(IIS == NULL)
        Kind = Kind + ", ";
     end;

     Kind = Kind + string(TXOBJ_PAIDSPECIAL_IIS);
  end;


  DL_GetNPTXOBJSumByClientAndOper(pClient,
                                  Kind,
                                  pBegDate,
                                  pEndDate,
                                  null,
                                  OperID,
                                  null,
                                  @Bs
                                 );
  
  GetTAXCalculatedDue(pClient, NULL, pBegDate, pEndDate, Year, IIS, NULL, NULL, NULL, Bs, @Dg, @Ds, @Dm, @Dp, @Pg, @Pm, @Pp, @Ps, FromOutSyst);
//  if((Dg + Ds + Dm + Dp) < 0)
//    println("Dg" + Dg + "; " + "Ds" + Ds + "; "+"Dm" + Dm + "; "+"Dp" + Dp + "; ");
//  end;
  return Dg + Ds + Dm + Dp;
end;

macro uGetSOFRPGPP(pClient, pBegDate, pEndDate, pYear, pIIS, pOperID, pFromOutSyst)
  //IN
  //var pClient = 6209;
  //var pBegDate = date(1, 1, 2021);
  //var pEndDate = date(31, 12, 2021);
  //var Year = 2021;
    var Year = pYear;
  //var IIS = NULL;//0, 1, NULL
    var IIS = pIIS;
  //var OperID = 507;
    var OperID = pOperID;
  //var FromOutSyst = true;
    var FromOutSyst = pFromOutSyst;

  //OUT
  var Bg = $0, Bm = $0, Bs = $0, Bp = $0, Bd = $0;
  var Dm = $0, Ds = $0, Dg = $0, Dp = $0;
  var Pg = $0, Pm = $0, Ps = $0, Pp = $0, Pd = $0;

  var Kind = "";
  if((IIS == NULL) or (IIS == 0))
     Kind = string(TXOBJ_PAIDSPECIAL);
  end;
  if((IIS == NULL) or (IIS == 1))
     if(IIS == NULL)
        Kind = Kind + ", ";
     end;

     Kind = Kind + string(TXOBJ_PAIDSPECIAL_IIS);
  end;


  DL_GetNPTXOBJSumByClientAndOper(pClient,
                                  Kind,
                                  pBegDate,
                                  pEndDate,
                                  null,
                                  OperID,
                                  null,
                                  @Bs
                                 );
  
  uGetTAXCalculatedDue(pClient, NULL, pBegDate, pEndDate, Year, IIS, NULL, NULL, NULL, Bs, @Dg, @Ds, @Dm, @Dp, @Pg, @Pm, @Pp, @Ps, FromOutSyst);
//  if((Dg + Ds + Dm + Dp) < 0)
//    println("Dg" + Dg + "; " + "Ds" + Ds + "; "+"Dm" + Dm + "; "+"Dp" + Dp + "; ");
//  end;
//  return PG;
  return PG+PP+Pm+Ps;
end;




// отправка уведомления
macro SendNotification_TaxOverPay(ClientID, pClientName, TaxOverPay)
  var err = "";

  var ClientName;
  if((valtype(pClientName) == v_undef) or (pClientName == ""))
    ClientName = "клиент";
  else
    ClientName = pClientName;
  end;

  var p_Attacment = "Заявление на возврат переплаты.docx";
  var    Attachement_name;
  var strerr, Templ_Dir;
  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEMPLSDIR", V_STRING, Templ_Dir, StrErr);
  Attachement_name = FindPath(toANSI(p_Attacment), Templ_Dir);

  private macro GetFullPath( Path )
      var fullpath;
      if( substr( Path, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
        Path = substr(Path, 3 );
      end;
      if( substr( Path, 1, 1 ) == "\\" ) /* ссылка на вышестоящий каталог */
        Path = substr(Path, 2 );
      end;
      fullpath =   strsubst( GetCurDir(false) , "\\Obj","")  + "\\" + Path ; 
    // получить полный путь
    return fullpath;
  end;

  private macro GetClientEmailByContactType(ClientID:integer, ContactType:integer)
    var cmd, DataSet;
    var email = "";
    cmd = DL_RSDCommand(    " SELECT c.t_Value as email"
                          + "   FROM dcontact_dbt c "
                          + "  WHERE c.t_PartyID = ? "
                          + "    AND c.t_ContactKind = " + CNTK_EMAIL
                          + "    AND c.t_ContactType = ? " 
                          //+ "    AND c.t_IsMain = 'X' "
                          //+ "    AND ROWNUM = 1 "
                          + "  order by c.t_IsMain desc " // Golovkin 
                         );
    cmd.addParam(ClientID);
    cmd.addParam(ContactType);

    DataSet = cmd.execute();
    if(DataSet.moveNext())
      email = DataSet.email;
    end;

    return email;
  end;

  macro SC_GetClientRepEmail(ClientID:integer)
    var cmd, DataSet;
    var email = "", contrnumber;

    DataSet = TRsbDataSet(String( "SELECT dlc.t_email\n" 
                                 ,"  FROM dsfcontr_dbt sfc, ddlcontr_dbt dlc\n" 
                                 ," WHERE     dlc.t_sfcontrid = sfc.t_id\n"  
                                 ,"       AND dlc.t_iis != CHR (88)\n"  
                                 ,"       AND sfc.t_partyid = ", ClientID));

    if (DataSet.movenext())
      email = DataSet.Email;
    end;

    if(email == "")
       email = GetClientEmailByContactType(ClientID, CNTT_TOSENDREPORTS);
       if(email == "")
          email = GetClientEmailByContactType(ClientID, 1005);
          if(email == "")
             email = GetClientEmailByContactType(ClientID, 0);
          end;
       end;
    end;
    return email;
  END;

      var email_adr = SC_GetClientRepEmail(ClientID);
      if(email_adr == "")
        err = "Ошибка получения адреса электронной почты для клиента";
        return err;
      end;

      var email = c_email_proc_env();
      if (email != NULL)
        email.m_set_msg_head("Уведомление о перерасчете НДФЛ");
        var emailText = String( "Уважаемый(ая) " + ClientName + ",\n"
                               ,"Банк получил данные по Вашим затратам на покупку ценных бумаг и пересчитал размер НДФЛ, в результате выявлена переплата в размере "+abs(TaxOverPay)+" р.\n" 
                               ,"Вы можете написать заявление на возмещение излишне уплаченного налога и направить его по почте на адрес broker@rshb.ru с указанием темы \"Возмещение налога\".\n" 
                               ,"Либо Вы можете ничего не делать, в таком случае при последующих выводах налог не будет удерживаться до полного использования переплаты в течении текущего налогового периода (2022 год).\n"  
                               ,"Приносим свои извинения за доставленные неудобства.\n"  
                               ,"\n"  
                               ,"                                                                                                                                                                     АО \"Россельхозбанк\"");

        email.m_add_row_to_msg_text(emailText);

        if (strlen(trim(Attachement_name)) != 0)
           email.m_add_attach_to_list(GetFullPath(Attachement_name));
        end;

        email.m_add_email_to_list(email_adr);
//        email.m_add_attach_to_list(FullPathRep + srvobj.rec.FileName);  
          email.m_save_to_submit();
      else
        err = "Ошибка инициализации объекта сообщения электронной почты";
      end;
    return err;
end;

macro ProcSendTaxOverPayNotify(errp:@string , result_protocol:@variant)

  private macro CheckClient(ClientID)
   var sql = String(" select op.t_clientid from uVTB_checkOp_dbt op, ddl_tick_dbt tick \n"
                   ,"   where op.t_dealid_ndfl = tick.t_dealid \n"
                   ,"     and op.t_depo_sum = op.t_ndfl_sum \n"
                   ,"     and tick.t_dealstatus = 20 and op.t_clientid = ?\n"
                   ,"     and not exists (select 1 from ddl_tick_dbt t,  uVTB_checkOp_dbt o where t.t_dealid = o.t_dealid_ndfl and  t.t_dealstatus <> 20 and O.T_CLIENTID = op.t_clientid)\n"
                   ,"     and not exists (select 1 from UVTB_CHECKOP_DBT o where o.t_dealid_ndfl is null and o.t_clientid = op.t_clientid)\n"
                   ,"     /*group by op.t_clientid*/  \n" );
   var cmd = DL_RSDCommand();
       cmd.AddParam(ClientID);
   var ds = cmd.Execute(sql);
   if(ds.movenext)
      return true;
   end;
   return false;
  end;
  var sql_with = String( "WITH clnt AS\n" 
                   ," (SELECT oc.t_Client\n" 
                   ,"    FROM (SELECT DISTINCT op.t_Client\n" 
                   ,"            FROM dnptxop_dbt op\n" 
                   ,"           WHERE op.t_DocKind = 4605\n" 
                   ,"             AND op.t_OperDate > :v_begdate\n" 
                   ,"             AND op.t_IIS != 'X') oc\n" 
                   ,"   WHERE /*oc.t_client = 211701 and */ EXISTS (SELECT 1\n" 
                   ,"            FROM dobjcode_dbt\n" 
                   ,"           WHERE t_objectid = oc.t_Client\n" 
                   ,"             AND t_objecttype = 3\n" 
                   ,"             AND t_codekind = 109\n" 
                   ,"             AND ROWNUM = 1))\n" 
                   ,"\n"); 
  var sql_cnt =  String("select count(1) as rec_cnt\n");
  var sql_rec =  String("select *\n");  
  var sql_body = String(" from (SELECT t.*, p.T_SHORTNAME, (select t_code from dobjcode_dbt where t_objecttype = 3 and t_state = 0 and t_objectid = t.t_client and t_codekind = 101) as cft_code,\n" 
                   ,"               nop.t_id,\n" 
                   ,"               nop.t_operdate,\n" 
                   ,"               nop.t_code,\n" 
                   ,"               MAX(t_id) OVER(PARTITION BY t.t_client, t_operdate) max_op_id\n" 
                   ,"          FROM (SELECT clnt.t_client\n" 
                   ,"                  FROM clnt\n" 
                   ," where 1=1 \n"
//                   ,"                 WHERE clnt.t_client in (select op.t_clientid from uVTB_checkOp_dbt op, ddl_tick_dbt tick \n"
//                   ,"                                where op.t_dealid_ndfl = tick.t_dealid \n"
//                   ,"                                  and op.t_depo_sum = op.t_ndfl_sum \n"
//                   ,"                                  and tick.t_dealstatus = 20\n"
//                   ,"                                  and not exists (select 1 from ddl_tick_dbt t,  uVTB_checkOp_dbt o where t.t_dealid = o.t_dealid_ndfl and  t.t_dealstatus <> 20 and O.T_CLIENTID = op.t_clientid)\n"
//                   ,"                                  and not exists (select 1 from UVTB_CHECKOP_DBT o where o.t_dealid_ndfl is null and o.t_clientid = op.t_clientid)\n"
//                   ,"                                /*group by op.t_clientid*/ ) \n" 
                   ,"                 GROUP BY clnt.t_client) t,\n" 
                   ,"               (SELECT t_id, t_client, t_operdate, t_code\n" 
                   ,"                  FROM (SELECT t_id,\n" 
                   ,"                               t_client,\n" 
                   ,"                               t_operdate,\n" 
                   ,"                               t_code,\n" 
                   ,"                               MAX(t_operdate) OVER(PARTITION BY t_client) max_op_date\n" 
                   ,"                          FROM dnptxop_dbt)\n" 
                   ,"                 WHERE t_operdate = max_op_date) nop, dparty_dbt p\n" 
                   ,"         WHERE t.t_client = nop.t_client and p.t_partyid = t.t_client)\n" 
                   ," where t_id = max_op_id ");

  var cmd = DL_RSDCommand;
      cmd.AddParam(date(21,4,2022));

  EndAction;
  Message(" Подсчет количества. Шаг 1/3");
  BegAction(0, " Подсчет количества. Шаг 1/3");
  var cnt = cmd.Execute(string(sql_with,sql_cnt,sql_body));
  EndAction;

  var PayAmount; var SOFR_PG, DEPO_PG;
  if ((cnt.movenext) and (int(cnt.rec_cnt) > 0))
    preptable();

    EndAction;
    Message(" Отбор записей. Шаг 2/3");
    BegAction(0, " Отбор записей. Шаг 2/3");
    var ds = cmd.execute(string(sql_with,sql_rec,sql_body));
    EndAction;

    var IIS = /*NULL*/ 0; 
    var flog;
    flog = Logger();
    result_protocol.AddString(flog.RepHeader());
    var vErr = "", err_msg = "", err_cnt = 0, ncount = 0; 
    Initprogress(int(cnt.rec_cnt), "Поиск клиентов с переплатой НДФЛ и формирование уведомлений", "Формирование рассылки о переплате НДФЛ");
    var i = 0;

    EndAction;
    Message(" Поиск переплат и формирование уведомлений. Шаг 3/3");
    BegAction(0, " Поиск переплат и формирование уведомлений. Шаг 3/3");
    while(ds.MoveNext)
      if(CheckClient(ds.Client))
        vErr = ""; err_msg = "";
        PayAmount = uGetOverPayAmount(ds.Client, date(1,1,2022), ds.Operdate, 2022, IIS, ds.ID, True);
        if (PayAmount < 0) 
         // SOFR_PG = UGetSofrPG(ds.Client, date(1,1,2022), ds.Operdate, 2022, IIS, ds.ID, False);
          SOFR_PG = UGetSofrPGPP(ds.Client, date(1,1,2022), ds.Operdate, 2022, IIS, ds.ID, False);

          if(SOFR_PG == 0)
            vErr = "X";
            err_cnt = err_cnt + 1;
            err_msg = "У клиента нет удержаний НДФЛ в СОФР";
          end;
          if(vErr == "")
            if(not IsNotifySendByClient(ds.Client))  
              err_msg = SendNotification_TaxOverPay(ds.Client, ds.shortname, PayAmount);
              if (err_msg != "")
               vErr = "X";
               err_cnt = err_cnt + 1;
              else
               insert_overpay_send_log(ds.Client, PayAmount);
               err_msg = "Уведомление создано";
               ncount = ncount + 1;
              end;
            else
              err_msg = "Данному клиенту сообщение уже отправлялось ранее";
              vErr = "X";
              err_cnt = err_cnt + 1;
            end;
          end;
          result_protocol.AddString(flog.RepStr(vErr,ds.cft_code,ds.shortname,string(abs(PayAmount),"(",SOFR_PG,")" ),err_msg));
        end;
        i = i + 1;
        UseProgress(i);
      end;
    end;
    EndAction;
    result_protocol.AddString(flog.RepFooter(ncount,err_cnt));
    RemProgress (cnt.rec_cnt);
  end;

  return true;

OnError(er)
      result_protocol.AddString("Ошибка при формировании рассылки ' " + er.Message);
      return false;
end;

//ProcSendTaxOverPayNotify();