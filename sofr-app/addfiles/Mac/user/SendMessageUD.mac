// отправка сообщений для клиентов  
import dbo_message_UD;
import "usr_open_files.mac";

println(time());

   var ErrStr = "",ErrStat = false;
   var row = 0;
   var suc = 0, err = 0;

   var cur_file_mask = "*.csv";
   file datafile() txt;
   SetDelim(";");
      
   var c_file = c_txt_file();
   var FullFileName;
   var stat;
              
   var sql_str, cmd, rs;
   var pf, v_error;

   stat = c_file.openFile(datafile, cur_file_mask, true, false, FullFileName, "rsansi");

   if ( (valtype(stat) == V_UNDEF) or (stat == false) )
      msgbox( "Ошибка открытия файла");
      return false;
   end;

   /*Открываем файл*/
   BegAction(1, "Обработка файла \"" + c_file.localFileName + "\". Ждите...");

   /* формат файла, первая строка - всегда заголовок, данные со второй строки
   CFT;FIO
1	Идентификатор CFT (код 101)
2	ФИО
     */

   next(datafile);

   row = 2;
   //Бежим по файлу
   InitProgress(-1);
   while (next(datafile))
      if (StrLen(trim(datafile.str)) == 0)
         continue;
      end;
      
      if (StrLen(datafile(0)) == 0)
         ErrStr = "Не задан код ЦФТ, строка "+row;
         println(ErrStr);
         row = row + 1;
         UseProgress(row);
         err = err + 1;
         continue;
      end;
      
      sql_str = 
      "select dl.t_dlcontrid "+
      "   from dobjcode_dbt obj, "+
      "         dsfcontr_dbt sf, "+
      "         ddlcontr_dbt dl "+
      " where obj.t_objecttype = 3 and obj.t_codekind = 101 and obj.t_state =0 and obj.t_code = :pcode "+
      "   and sf.t_partyid = obj.t_objectid and sf.t_dateclose = to_date('01010001','ddmmyyyy') "+
      "   and sf.t_id = dl.t_sfcontrid ";  
      cmd = RSDCommand(sql_str);
      cmd.AddParam("pcode", RSDBP_IN, datafile(0));
      rs = RSDRecordSet(cmd);
      
      if (rs.movenext)
         SetDialogFlag(0);
         pf = dboMessage(rs.value("t_dlcontrid")).CreateMessage(true,true,true);
         println(pf.NumberDBO+" "+pf.Prt_Fnam +" "+ pf.statMes);
         SetDialogFlag(1);
         suc = suc + 1;
      else 
         println("Не найден клиент "+datafile(0)+", строка "+row);
         row = row + 1;
         UseProgress(row);
         err = err + 1;
         continue;
      end;

      row = row+1;
      UseProgress(row);
      ErrStr = "";
      ErrStat = false;
   end;
   RemProgress();

   c_file.closeFile(datafile);
   println("Успешно: "+suc);
   println("С ошибкой: "+err);

println(time());

exit(0);


