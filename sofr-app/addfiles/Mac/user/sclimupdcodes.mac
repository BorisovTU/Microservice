/*
$Name:         sclimupdcodes.mac
$Module:       Ценные бумаги
$Description:  Обновление кодов ценных бумаг по лимитам
*/

import "lim_utils.mac";

private macro GetEmptyLimSecCodesDataset()
  var query =
    "   SELECT av.t_isin, "
   +"          fin.t_name, "
   +"          (SELECT NVL (t_Code, '') "
   +"             FROM dobjcode_dbt "
   +"            WHERE     t_ObjectType = " + OBJTYPE_PARTY
   +"                  AND t_CodeKind = " + PTCK_CONTR
   +"                  AND t_State = 0 "
   +"                  AND t_ObjectID = sec.t_market "
   +"                  AND ROWNUM = 1) "
   +"             AS t_MP, "
   +"          COUNT (*) AS t_poscount "
   +"     FROM DDL_LIMITSECURITES_DBT sec, dfininstr_dbt fin, davoiriss_dbt av "
   +"    WHERE     (t_seccode = CHR (0) OR t_seccode = CHR (1) OR t_seccode IS NULL) "
   +"          AND fin.t_fiid = sec.T_SECURITY "
   +"          AND av.t_fiid = fin.t_fiid "
   +"          AND fin.T_FI_KIND = " + FIKIND_AVOIRISS
   +" GROUP BY T_ISIN, t_name, t_market ";
  return ExecSQLSelect(query);
end;

private macro GetUpdatedLimSecCodesDataset(AddFilter)
  var query = 
    "  SELECT q1.t_isin, "
   +"         q1.t_name, "
   +"         q1.t_MP, "
   +"         q1.T_SECCODE AS t_OldCode, "
   +"         q1.t_RealCode AS t_NewCode, "
   +"         COUNT (*) AS t_PosCount "
   +"    FROM (SELECT av.t_isin, "
   +"                 fin.t_name, "
   +"                 sec.t_market, "
   +"                 sec.T_SECCODE, "
   +"                 (SELECT NVL (t_Code, '') "
   +"                    FROM dobjcode_dbt "
   +"                   WHERE     t_ObjectType = " + OBJTYPE_PARTY
   +"                         AND t_CodeKind = " + PTCK_CONTR
   +"                         AND t_State = 0 "
   +"                         AND t_ObjectID = sec.t_market "
   +"                         AND ROWNUM = 1) "
   +"                    AS t_MP, "
   +"                 (  SELECT t_Code "
   +"                      FROM DOBJCODE_DBT "
   +"                     WHERE t_ObjectType = " + OBJTYPE_FININSTR
   +"                           AND t_CodeKind = "
   +"                                  RSHB_RSI_SCLIMIT. "
   +"                                   GetKindMarketCodeOrNote (sec.T_MARKET, 1, 0) "
   +"                           AND t_ObjectID = sec.T_SECURITY "
   +"                           AND t_BankDate <= sec.T_DATE "
   +"                           AND t_BankCloseDate = "
   +"                                  TO_DATE ('01.01.0001', 'dd.mm.yyyy') "
   +"                  ORDER BY t_BankDate DESC "
   +"                  FETCH NEXT 1 ROWS ONLY) "
   +"                    AS t_RealCode "
   +"            FROM DDL_LIMITSECURITES_DBT sec, "
   +"                 davoiriss_dbt av, "
   +"                 dfininstr_dbt fin "
   +"           WHERE     av.T_FIID = sec.T_SECURITY "
   +"                 AND fin.t_fiid = sec.T_SECURITY "
   +"                 AND fin.T_FI_KIND = "+FIKIND_AVOIRISS+" "+AddFilter+") q1 "
   +"   WHERE q1.t_RealCode IS NOT NULL AND q1.t_RealCode <> q1.T_SECCODE "
   +"GROUP BY t_isin, "
   +"         t_name, "
   +"         t_MP, "
   +"         T_SECCODE, "
   +"         t_RealCode "
   +"ORDER BY t_MP, T_SECCODE ";
  return ExecSQLSelect(query);
end;

private macro UpdateLimitSecCodes(AddFilter)
  var query = 
    " MERGE INTO DDL_LIMITSECURITES_DBT limsec "
   +"      USING (SELECT * "
   +"               FROM (SELECT sec.t_id, "
   +"                            sec.T_SECCODE, "
   +"                            (  SELECT t_Code "
   +"                                 FROM DOBJCODE_DBT "
   +"                                WHERE t_ObjectType = " + OBJTYPE_FININSTR
   +"                                      AND t_CodeKind = "
   +"                                             RSHB_RSI_SCLIMIT. "
   +"                                              GetKindMarketCodeOrNote ( "
   +"                                                sec.T_MARKET, "
   +"                                                1, "
   +"                                                0) "
   +"                                      AND t_ObjectID = sec.T_SECURITY "
   +"                                      AND t_BankDate <= sec.T_DATE "
   +"                                      AND t_BankCloseDate = "
   +"                                             TO_DATE ('01.01.0001', "
   +"                                                      'dd.mm.yyyy') "
   +"                             ORDER BY t_BankDate DESC "
   +"                             FETCH NEXT 1 ROWS ONLY) "
   +"                               AS t_RealCode "
   +"                       FROM DDL_LIMITSECURITES_DBT sec "
   +"                      WHERE 1=1 "+AddFilter+") q1 "
   +"              WHERE q1.t_RealCode IS NOT NULL "
   +"                    AND q1.t_RealCode <> q1.T_SECCODE) q2 "
   +"         ON (limsec.t_id = q2.t_id) "
   +" WHEN MATCHED "
   +" THEN "
   +"    UPDATE SET LIMSEC.T_SECCODE = q2.t_RealCode ";
  execSQL(query);
end;

macro ExecLimSecCodeUpdate()
  var menuArr = makeArray("Обновить коды по всем позициям", "Обновить отсутствующие коды", "Обновить по выпуску");

  var addFilter = "";

  var choice = Menu(menuArr, "Выбор действия", "Выбор действия");

  var isDataExistsUpdate; //DEF-63983
  
  if (choice == 0)
    addFilter = " AND sec.T_MARKET_KIND = 'фондовый' ";
  elif (choice == 1)
    addFilter = " AND sec.T_MARKET_KIND = 'фондовый' and ((sec.t_seccode = CHR (0) OR sec.t_seccode = CHR (1) OR sec.t_seccode IS NULL)) ";
  elif (choice == 2)
    var fi = TRecHandler("fininstr.dbt");
    if (ListAvoiriss(0,fi))
      addFilter = "AND sec.t_security = " + fi.rec.fiid;
    else
      return;
    end;
  else
    return;
  end;


  const tableHeaderUpdated = 
"┌────────────────────────┬────────────────────────┬─────────────┬────────────┬───────────┬──────────────────────┐\n"+
"│         ISIN           │      Наименованиеe     │     ТП      │ Старый код │ Новый код │  Количество позиций  │\n"+
"├────────────────────────┼────────────────────────┼─────────────┼────────────┼───────────┼──────────────────────┤"; 

  const tableHeaderEmpty = 
"┌────────────────────────┬────────────────────────┬─────────────┬──────────────────────┐\n"+
"│         ISIN           │      Наименование      │     ТП      │  Количество позиций  │\n"+
"├────────────────────────┼────────────────────────┼─────────────┼──────────────────────┤"; 

  var Rep = CMakeReport();

  BegAction(2000, "Построение отчета обновления кодов ценных бумаг для лимитов", false);
  var updatedCodesDataset = GetUpdatedLimSecCodesDataset(addFilter);
  EndAction();

  Rep.AddPrintCell("Обновлены коды: ", null, 0, "l", REP_ELEM_STR);
  Rep.AddStr();
  var hasData = false;

  Rep.SetHeaderStr(tableHeaderUpdated);
  while (updatedCodesDataset.moveNext())
    Rep.AddPrintCell(SQL_ConvTypeStr(updatedCodesDataset.value("t_isin")));
    Rep.AddPrintCell(SQL_ConvTypeStr(updatedCodesDataset.value("t_name")));
    Rep.AddPrintCell(SQL_ConvTypeStr(updatedCodesDataset.value("t_MP")));
    Rep.AddPrintCell(SQL_ConvTypeStr(updatedCodesDataset.value("t_OldCode")));
    Rep.AddPrintCell(SQL_ConvTypeStr(updatedCodesDataset.value("t_NewCode")));
    Rep.AddPrintCell(SQL_ConvTypeInteger(updatedCodesDataset.value("t_poscount")));
    Rep.AddStr();
    hasData = true;
  end;
  if (not hasData)
    Rep.AddPrintCell("Нет данных ", null, 0, "l", REP_ELEM_STR);
    Rep.AddStr();
  end;
  
  isDataExistsUpdate = Rep.IsDataExist(True);

  BegAction(2000, "Обновление кодов ценных бумаг для лимитов", false);
  UpdateLimitSecCodes(AddFilter);
  EndAction();

  Rep.AddNewSheetBreak("Позиции с пустым кодом");

  Rep.AddPrintCell("Позиции с пустым кодом: ", null, 0, "l", REP_ELEM_STR);
  Rep.AddStr();

  hasData = false;

  Rep.SetHeaderStr(tableHeaderEmpty);
  var empyCodesDs = GetEmptyLimSecCodesDataset();
  while (empyCodesDs.moveNext())
    Rep.AddPrintCell(SQL_ConvTypeStr(empyCodesDs.value("t_isin")));
    Rep.AddPrintCell(SQL_ConvTypeStr(empyCodesDs.value("t_name")));
    Rep.AddPrintCell(SQL_ConvTypeStr(empyCodesDs.value("t_MP")));
    Rep.AddPrintCell(SQL_ConvTypeInteger(empyCodesDs.value("t_poscount")));
    Rep.AddStr();
    hasData = true;
  end;
  if (not hasData)
    Rep.AddPrintCell("Нет данных ", null, 0, "l", REP_ELEM_STR);
    Rep.AddStr();
  end;

  if(Rep.IsDataExist(isDataExistsUpdate) or isDataExistsUpdate)
    Rep.PrintRep();
  end;

end;

ExecLimSecCodeUpdate();