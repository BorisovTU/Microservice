/*
$Name:        rccrep.mac
$Module:      БО ЦБ
$Description: Отчет "Реестр договоров клиентов для РЕПО с ЦК"
*/

IMPORT FIInter,"DLReport_h.mac", "dlmisc.mac";
IMPORT "rccrep_form.mac";
import RsbDataSet;


private var ErrorString = "###############################################################################################################################";

private var Header = "      Реестр договоров клиентов для РЕПО с ЦК за ##########";

var Footer = "Составлено: ##########\n\n";

private var TableHeader = "┌──────────────────────┬────────────────────────┬──────────────┬──────────────┐\n"+
                          "│                      │                        │     Дата     │     Дата     │\n"+
                          "│ Идентификатор на ФР  │       № договора       │    начала    │  окончания   │\n"+
                          "│                      │                        │              │              │\n"+
                          "├──────────────────────┼────────────────────────┼──────────────┼──────────────┤"; 

PRIVATE VAR  m_Form = DL_RCCREP_Form();

class ReportData
  var Code;      /*Единый краткий код клиента */
  var Number;    /*Номер ДБО*/
  var DateBegin; /*Дата начала*/
  var DateClose;   /*Дата окончания*/

end;
  
class (DL_CReportTemplate) Dl_RCCREP_Report(_DateRep:date /*Отчетная дата*/, _ToExcel)
  var DateRep = _DateRep;
  var ToExcel = _ToExcel;

  macro PrintFooter(RepDate:date)
    PrintFormatString(NULL, "N1_F1", RepDate); 
    AddStandardFooter("N1_", true, false);
  end;
  
  macro PrintHead()
    SetActiveSheet( "Отчет" );
    PrintFormatString( Header,
                      "N1_A0", String(DateRep));
  end;
  
  macro PrintError( error:string )
    SetActiveSheet( "Отчет" );
    PrintFormatString( ErrorString,
                      "N1_E1", error);
  end;
  
  private macro PrintMode():INTEGER
     if(ToExcel)
        return DL_OUTREPORT_EXCEL;
     else
        return DL_OUTREPORT_STD;
     end;
  end;
  
  macro PrintErrorMessage(msg:string)
    MsgBox( msg );
  end;
  
  private macro PrintLine(DataSet)
    PrintTableLine( "N1_A1",  String(DataSet.Code),   null,
                    "N1_A2",  String(DataSet.Number),  null,
                    "N1_A3",  date(DataSet.DateBegin),  null, 
                    "N1_A4",  date(DataSet.DateClose),    null);
  end;
  
  private macro GetQuery():string
    var query = "SELECT Obj.t_code, DboContr.t_number, rsb_struct.getDate(NOTETEXT3.T_TEXT) AS t_dateBegin, rsb_struct.getDate(NOTETEXT4.T_TEXT) AS t_dateClose " +
                "FROM ddlcontr_dbt Contr, ddlobjcode_dbt Obj, dsfcontr_dbt DboContr, dnotetext_dbt Notetext3, dnotetext_dbt Notetext4 "+
                "WHERE Contr.T_SFCONTRID = DboContr.T_ID "+
                     "AND DboContr.t_dateBegin <= ? "+
                     "AND (DboContr.t_dateclose = to_date('01.01.0001', 'dd.mm.yyyy') OR DboContr.t_dateclose >= ?) "+
                     "AND EXISTS ( SELECT 1 "+
                                  "FROM dsfcontr_dbt SubContr, ddlcontrmp_dbt Contrmp "+
                                  "WHERE SubContr.T_ID = Contrmp.T_SFCONTRID "+
                                        "AND SubContr.T_SERVKIND = 1 "+
                                        " AND Obj.t_codekind = 1 "+
                                        "AND Contrmp.T_DLCONTRID = Contr.T_DLCONTRID "+
                                 ") "+
                      "AND NOTETEXT3.t_ObjectType = 207 AND NOTETEXT3.t_NoteKind = 160 AND  NOTETEXT3.t_DocumentID = lpad(Contr.T_DLCONTRID,34,'0') and NOTETEXT3.t_validtodate >= ? and NOTETEXT3.t_date <= ? "+
                          "AND rsb_struct.getDate(NOTETEXT3.T_TEXT) <= ? "+
                      "AND NOTETEXT4.t_ObjectType = 207 AND NOTETEXT4.t_NoteKind = 161 AND  NOTETEXT4.t_DocumentID = lpad(Contr.T_DLCONTRID,34,'0') and NOTETEXT4.t_validtodate >= ? and NOTETEXT4.t_date <= ? "+
                          "AND(rsb_struct.getDate(NOTETEXT4.T_TEXT) >= ? OR  rsb_struct.getDate(NOTETEXT4.T_TEXT) = to_date('01.01.0001', 'dd.mm.yyyy')) "+
                      "AND OBJ.T_OBJECTTYPE = 207 "+
                      "AND Obj.t_objectid = CONTR.T_DLCONTRID";
    return query;
  end;

  macro PrintProtocol()
    PrintHead();
    
    var Rdata = ReportData();

    RegisterTable( "N1_MainTable", TableHeader,
                   "N1_A1",
                   "N1_A2",
                   "N1_A3",
                   "N1_A4" );
                   
    var Count = 0;
    var Num = 0;
    var cmd = DL_RSDCommand(GetQuery());
    cmd.AddParam(DateRep);
    cmd.AddParam(DateRep);
    cmd.AddParam(DateRep);
    cmd.AddParam(DateRep);
    cmd.AddParam(DateRep);
    cmd.AddParam(DateRep);
    cmd.AddParam(DateRep);
    cmd.AddParam(DateRep);
    Count = cmd.GetCount();
    var DataSet = cmd.Execute();
    if(Count > 0)
      Num = 0;
      InitProgress(Count, "Печать отчета", "Печать отчета");
    end;
    
    while( DataSet.moveNext() )
      PrintLine(DataSet);
      UseProgress(Num = Num + 1); 
    end;
    RemProgress(); 
    
    if(ToExcel) 
       EndTable();
    end;
    if( not Count )
      PrintError("Для заданной даты договора не найдены");
    end;
    PrintFooter({curdate});
  end;

  macro Create()
    SetActiveSheet("Отчет");
    PrintProtocol(); 
  end;
  
  initDL_CReportTemplate( null, "Report_RCC.xlsx", true );
END;
  
macro MakeReports()
  var stat = 0;

  stat = m_Form.Run();
  // Проверяем можем ли мы делать отчет по предоставленным данным
  if (not stat)
   return 1;
  end;
  
  var Report = Dl_RCCREP_Report(m_Form.GetFieldValue(PNFLD_RCCREP_DATE), true);
  Report.Run();

  return 0;
end;

while( not MakeReports() ) 
end;
exit(1);
