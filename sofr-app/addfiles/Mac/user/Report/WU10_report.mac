/**
 @file 		mac\user\Report\WU10_report.mac
 @brief 	Журнал учета поручений по внебиржевым сделкам

 # menu
 - ЦБ \ Отчеты \ РСХБ \ [ВУ10 ЖурПорВС] Журнал учета поручений по внебиржевым сделкам

 # tag
 - functional_block: Отчетность_внутренняя
 - code_type: Report

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |05.06.2025 |Велигжанин.А.В.|DEF-91583                                       |Рефакторинг BrokerClients_Report
*/
import RsbFormsInter, Global;
import or_rep_h;
import or_tpl_h;
import or_const;

var printEngine, csvFile, csvTable, fName;

CLASS OfferRep(Begdate, Enddate)

var _BegDate = begdate;
var _EndDate = enddate;

private var v_specval = 26;

private macro initRep()
  printEngine = CTemplateSXLSX(NULL);
  if(printEngine.UsePoi())
     printEngine.SetXLSXFormat();
  end;
  printEngine.CreateTotalBook();
  InitProgress(-1, "Esc отмена", "Обработка"); 
  printEngine.OpenTemplate("Report_WU10.xlsx", true);
  printEngine.SheetChange(1);
  csvTable = printEngine.RegisterTable("templateTable", "templateTableHeader", true);
  csvFile = C_CSVFile();
  if (printEngine.UsePoi())
      csvFile.SetLimitRow(FLUSH_COUNT_XLSX);
  end;
  fName = csvFile.CreateFullFileName("Report_WU10_csv.txt");
  csvFile.FileOpen(fName, true);
end;

private macro addHeader()
  printEngine.SetValue_NameCell("templateTitle1", "Журнал регистрации поручений (требований) клиентов №2");
  printEngine.SetValue_NameCell("templateTitle2", "За период: " + _Begdate + "-" + _EndDate);
  printEngine.CopyAllSheetInTotalBook(null, false, "templateHeader", 0, "Отчет.");
end;

private macro showRep()
	csvFile.FileClose();
  csvTable.FillTabelFromCSV(csvFile.GetlsFileName());
  csvTable.EndTable();
  printEngine.SetAutoFilterTotalbook("A4:H4", "Отчет.");
  printEngine.CopyAllSheetInTotalBook(null, false, csvTable.GetTableRange(), 0, "Отчет.");
  printEngine.Close();
  printEngine.SaveTotalBook();
  remProgress ();
end;

private macro ПечатьБлокаДанных(_rs);
    if(valtype(_rs) != v_undef)
      while(_rs.movenext)
        csvFile.AddCell(_rs.value(0)); // 1. 
        csvFile.AddCell(string(_rs.value(1))); // 1. 
        csvFile.AddCell(time(_rs.value(2))); // 2.  
        csvFile.AddCell(_rs.value(3)); // 3. 
        if (valtype(_rs.value(4)) == v_specval)
          csvFile.AddCell(""); // 4. 
        else
          csvFile.AddCell(_rs.value(4)); // 4.
        end;
        csvFile.AddCell(_rs.value(5)); // 5. 
        csvFile.AddCell(_rs.value(6)); // 6. 
        csvFile.AddCell(_rs.value(7)); // 7. 
        csvFile.AddRow();       
      end;
    end;
end;

macro run()
var SQL, cmd, rs;
  initRep();
  addHeader();

  SQL =" select req.t_code, "
     + "        to_char(req.t_date, 'dd.mm.yyyy'), "
     + "        REQ.T_TIME, "
     + "        sf.t_name || '/' || "
     + "               (select t_code "
     + "                  from ddlobjcode_dbt "
     + "                 where t_objecttype = 207 "
     + "                   and t_codekind = 1 "
     + "                   and t_objectid = mp.t_dlcontrid "
     + "                   and t_bankclosedate = TO_DATE ('01010001', 'ddmmyyyy') "
     + "                   and rownum = 1), "
     + "        utl_raw.cast_to_varchar2(nt.t_text), DECODE(alg.t_sznamealg, 'Голосовое сообщение', 'По телефону', alg.t_sznamealg), "
     + "        note_read.get_note(p_object_type => dlreq_utils.dlreq_objecttype, "
     + "                           p_note_kind   => 104, "
     + "                           p_document_id => lpad(req.t_id, 34, '0')), "
     + "        fi.t_isin || '; ' || isr.t_name || '; price ' || req.t_price || '; vol. ' || req.t_amount || ';' || DECODE(req.t_direction, 1, 'BUY', 'SELL') "
     + " from ddl_req_dbt req "
     + " join dparty_dbt party on req.t_client = party.t_partyid "
     + " join davoiriss_dbt fi on req.t_fiid = fi.t_fiid "
     + " left join dnotetext_dbt nt on nt.t_objecttype = 149 and nt.t_notekind = 101 and nt.t_documentid = LPAD(req.t_id, 34, '0') "
     + " left join dnamealg_dbt alg on alg.t_itypealg = 3172 and alg.t_inumberalg = req.t_methodapplic "
     + " join ddl_tick_dbt tick on tick.t_bofficekind = req.t_sourcekind and tick.t_dealid = req.t_sourceid "
     + " join dsfcontr_dbt sf on sf.t_id =  req.t_clientcontr and sf.t_partyid = req.t_client "
     + " join ddlcontrmp_dbt mp on mp.t_sfcontrid = req.t_clientcontr  "
     + " LEFT JOIN dfininstr_dbt f ON (f.t_fiid = req.t_fiid) "
     + " LEFT JOIN dparty_dbt isr ON (isr.t_partyid = f.t_issuer) "
     + "  where req.t_kind = 350  "
     + "  and tick.t_portfolioid = 0 "
     + "  and TICK.T_MARKETID = -1 "
     + "  and req.t_code != CHR(0) "
     + "  and req.t_date between :datebeg and :dateend "
     + " order by req.t_date, req.t_time, TO_NUMBER(REGEXP_SUBSTR(req.t_code, '[[:digit:]]{2,4}', 3)) "
     ;
   //31.07.2020 RAS 513868
   //" order by TO_NUMBER(REGEXP_SUBSTR(req.t_code, '[[:digit:]]{2,4}', 3)) ";//~RAS
   
    cmd = RSDCommand(SQL);
    cmd.AddParam("datebeg", RSDBP_IN, _BegDate);
    cmd.AddParam("dateend", RSDBP_IN, _EndDate);
    begaction(100,"Сбор данных...");

    rs = RSDRecordset(cmd);
    endaction;
    ПечатьБлокаДанных(rs);
    showRep();
end; 

END;

CLASS (TRsbPanel) Pan
    initTRsbPanel();


    const K_Enter = 13;
    const K_F3    = 317;
    const K_F2    = 316;

    const BeginCalcDate_ID = 1;
    const EndCalcDate_ID = 2;
    var curstr = 1;
    Caption = "Поручения клиентов";
    Status = "~ESC~Выход ~F2~Выполнить";
    setSize(37,5);
    setPosition(30,10);
    var BeginCalcDate, EndCalcDate, dx;
    var RuDataFlag = 0;
    var InPortfolioFlag = 1;

    curstr = curstr+1;
    BeginCalcDate = TRsbEditField(V_DATE);
    BeginCalcDate.setPosition(16,curstr);
    BeginCalcDate.setSize(8,1);
    BeginCalcDate.name = "begincalcdate";
    BeginCalcDate.value = {curdate};
    addControl(BeginCalcDate);

    EndCalcDate = TRsbEditField(V_DATE);
    EndCalcDate.setPosition(27,curstr);
    EndCalcDate.setSize(8,1);
    EndCalcDate.name = "endincalcdate";
    EndCalcDate.value = {curdate};
    addControl(EndCalcDate);

    var m_LabelCD:TrsbLabel = TRsbLabel(2,curstr,"Поручения за период:");
    addLabel (m_LabelCD);

    var m_LabelT:TrsbLabel = TRsbLabel(25,curstr,"~");
    addLabel (m_LabelT);
 
    addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "onKeyPressed"));


    macro onKeyPressed(ev)
        if (ev.KeyCode == K_F3)
           if ((ev.id == BeginCalcDate_ID) or (ev.id == EndCalcDate_ID ))
            BeginCalcDate.value = GetDateByCalendar({CurDate});
//            EndCalcDate.value   = BeginCalcDate.value + Delta;
            this.redraw; 
           end;
        elif (ev.KeyCode == K_Enter)
            //this.update;
//            EndCalcDate.value = BeginCalcDate.value + Delta;
            this.redraw; 
        elif(ev.keyCode == K_F2)
  //       EndCalcDate.value   = BeginCalcDate.value + Delta;
        
         var myRep = OfferRep(BeginCalcDate.value, EndCalcDate.value);
         myRep.run;
        end;
    end; 
END;

var myPanel:TRsbPanel = Pan;
myPanel.run;
exit(1);