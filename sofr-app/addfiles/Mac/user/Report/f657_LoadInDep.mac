import VBAconst, oratools, rcw, cb_sql;

//-----------------------------------------------------------------------------     
private macro InsertToTable(sheet,row,repDate)
  private var sql = "insert into f657_ind_dep(t_date_load,t_isin,t_name_security,t_type_security,t_count_security,t_ownership,t_indicator) "+
                    "values(:t_date_load,:t_isin,:t_name_security,:t_type_security,:t_count_security,:t_ownership,:t_indicator)";

  private var param = makeArray(SQLParam(":t_date_load", repDate),
                                SQLParam(":t_isin", sheet.cells(row,1).Value),
                                SQLParam(":t_name_security", sheet.cells(row,2).Value),
                                SQLParam(":t_type_security", sheet.cells(row,3).Value),
                                SQLParam(":t_count_security", sheet.cells(row,4).Value),
                                SQLParam(":t_ownership", sheet.cells(row,5).Value),
                                SQLParam(":t_indicator", sheet.cells(row,6).Value));
  ExecSql(sql,param,false);
end;

//-----------------------------------------------------------------------------
private macro LoadFromSheet(sheet,repDate)
  InitProgress(sheet.UsedRange.Rows.Count,"Загрузка показателей обесценения.","Загрузка показателей обесценения.");
  if (not RslDefCon.IsInTrans()) RslDefCon.BeginTrans(); end;
  for (var i,2,sheet.UsedRange.Rows.Count,1)
    if ((sheet.cells(i,1).Value != "") and (ValType(sheet.cells(i,1).Value) != V_UNDEF))
      InsertToTable(sheet,i,repDate);
    end;  
    UseProgress(i);
  end;
  RslDefCon.CommitTrans();
  RemProgress();
end;

   





//-----------------------------------------------------------------------------
private macro LoadIndDep(repDate)
  var startAX          : Object = null,
      excelApp         : Object = null,
      SelectedFile;

  if(NOT SelectFile(SelectedFile, "*.xls, *.xlsx", "Выберите файл с показателями обесценения для обработки",0,true))
    MsgBox("Файл не выбран!");
    Exit(0);
  end;
    
  if (isStandAlone())
    excelApp = ActiveX("Excel.Application", null, true);
  else
    if (startAX == null)
      startAX = CreateObject("rsax", "TRsAxServer", null, isStandalone());
    end;
    excelApp = startAX.CreateComObject("Excel.Application");
  end;
  
  var prevDisplayAlerts = ExcelApp.DisplayAlerts;
  ExcelApp.DisplayAlerts = false;
  
  var workbooks = excelApp.Workbooks.Open(SelectedFile);
  var sheet = excelApp.Sheets(1);
  sheet.Select;
  LoadFromSheet(sheet,repDate);
  
  ExcelApp.DisplayAlerts = prevDisplayAlerts;  
  excelApp.Quit();
end;

//проверяем есть ли данные за текущую отчетную дату
private macro checkDateIndDep(repDate)
  var sql="select distinct 1 from f657_ind_dep i where i.t_date_load = :repDate";

  var cmd = rsdCommand(sql);
  cmd.addParam(":repDate", RSDBP_IN, repDate);
  cmd.Execute();
  var getData = TRsbDataSet(cmd);
  var rs = rsdRecordSet(cmd);
  if (rs.MoveNext)
     return 1;
  else
     return 0;
  end;
end;

//процедура удаления данных за дату
private macro delDateIndDep(repDate)
  var sql= "begin "+
           "delete from f657_ind_dep where t_date_load = :repDate; "+
           "commit; "+
           "end; ";
  
  var cmd = rsdCommand(sql);
  cmd.addParam(":repDate", RSDBP_IN, repDate);
  cmd.Execute();
end;

//определяем является ли дата концом месяца
private macro isEndMonth(repDate)
  var day, mon, year; 
  DateSplit (repDate, day, mon, year);
  if (day == GetDaysInMonth(repDate))
     return 1;
  else
     return 0;
  end;
end;
//main-------------------------------------------------------------------------   
macro runIndDep(repDate)
   //если дата отчета - конец месяца то загружаем данные по обесценению
   
  if (isEndMonth(repDate))
     if (checkDateIndDep(repDate))
        //будем удалять ранее загруженные данные да/нет
        if (GetTRUE (FALSE,"Данные с показателями обесценения за отчетную дату "+(repDate)+" уже загружены. Перезагрузить их?"))
           delDateIndDep(repDate);
           LoadIndDep(repDate);
        end;
     else
        //загружаем
        if (GetTRUE (TRUE,"Данные с показателями обесценения за отчетную дату "+(repDate)+" отсутствуют. Загружаем данные ?"))
           LoadIndDep(repDate);
        end;
     end;
  end;
end;
//----------------------------------------------------------------------------   
End; 