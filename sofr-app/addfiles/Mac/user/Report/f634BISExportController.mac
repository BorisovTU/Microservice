/*
Эспорт расчетных данных формы 634 в файл формата БИСквит
*/
import Globals, RcbCoreInter, ReptCBInter, BankInter, rsexts;
import print_f634pcb;

const 
    ТочВалОкруг  = 4,
    ТочДрагОкруг = 4;

//  Фильтр счетов по вхождению в расчет показателя формы
//-----------------------------------------------------------------------------
private class TPositionCodeFilter()

    private const ADDING_TYPE_INCLUDE = 1;
    private const ADDING_TYPE_EXCLUDE = 2;

    private var m_includedPositionCodeList : Object;

    private var m_excludedPositionCodeList : Object;

    private macro add(positionCode, addingType)
        if (addingType == ADDING_TYPE_INCLUDE)
            m_includedPositionCodeList[m_includedPositionCodeList.size] = positionCode;
        else
            m_excludedPositionCodeList[m_excludedPositionCodeList.size] = positionCode;
        end;
    end;

    macro addIncluded(positionCode)
        add(positionCode, ADDING_TYPE_INCLUDE);
    end;

    macro addExcluded(positionCode)
        add(positionCode, ADDING_TYPE_EXCLUDE);
    end;

    macro reset()
        m_includedPositionCodeList.size = 0;
        m_excludedPositionCodeList.size = 0;
    end;

    macro getAsSqlString(accountTableAlias)

        defaultParm(accountTableAlias, "");

        var sqlClauseIncluded;

        var sqlClauseExcluded;

        var sqlClause;

        var fullFieldName = ternary((accountTableAlias != ""), accountTableAlias + ".t_usedByPosition", "t_usedByPosition");

        var positionCode;

        var i;

        i = 0;
        sqlClauseIncluded = "";

        while (i < m_includedPositionCodeList.size)
            if (i > 0)
                sqlClauseIncluded = sqlClauseIncluded + " OR ";
            end;

            positionCode = getSqlChar(m_includedPositionCodeList[i]);
            sqlClauseIncluded = sqlClauseIncluded + "(INSTR(" + fullFieldName + ", " + positionCode + ") > 0)";
            i = i + 1;
        end;

        if (sqlClauseIncluded != "")
            sqlClauseIncluded = "(" + sqlClauseIncluded +")";
        end;

        i = 0;
        sqlClauseExcluded = "";
        while (i < m_excludedPositionCodeList.size)
            if (i > 0)
                sqlClauseExcluded = sqlClauseExcluded + " AND ";
            end;

            positionCode = getSqlChar(m_excludedPositionCodeList[i]);

            sqlClauseExcluded = sqlClauseExcluded + "(INSTR(" + fullFieldName + ", " + positionCode + ") = 0)";

            i = i + 1;
        end;

        if (sqlClauseExcluded != "")
            sqlClauseExcluded = "(" + sqlClauseExcluded +")";
        end;

        if ((sqlClauseIncluded != "") and (sqlClauseExcluded != ""))
            sqlClause = "(" + sqlClauseIncluded + " AND " + sqlClauseExcluded + ")";
        else
            sqlClause = sqlClauseIncluded + sqlClauseExcluded;
        end;

        if (sqlClause == "")
            sqlClause = "(1 = 1)";
        end;

        return sqlClause;
    end;

    private macro constructorTPositionCodeFilter()
        m_includedPositionCodeList = TArray(5);
        m_excludedPositionCodeList = TArray(5);
    end;

    constructorTPositionCodeFilter();
end;
//Источник данных по резервам
//-----------------------------------------------------------------------------
private class (TPrinterDataSource) TDataSourceCbReserves(parameters : TParameters, positionCodeFilter : TPositionCodeFilter)

    private var m_positionCodeFilter : TPositionCodeFilter;

    private macro makeDataQuery()

        var query;

        var accountReserveClause = "SUM(account.t_reserveAmount)";

        var accountEquivalentReserveClause = "SUM(rsb_fiinstr.convSumType(account.t_reserveAmount, 0, fi.t_id, 7, account.t_reserveDate))";

        var usedByPositionFilterClause = m_positionCodeFilter.getAsSqlString("account");

        query =          "WITH protocolData AS"
                + "\n" + "("
                + "\n" + " SELECT DECODE(GROUPING(account.t_accountNumber), 1, 'Итого по валюте:', fi.t_code) t_fiCode,"
                + "\n" + "        NVL(account.t_accountNumber, " + getSqlString("") + ")                      t_accountNumber,"
                + "\n" + "    " + accountReserveClause + "                                                    t_reserve,"
                + "\n" + "    " + accountEquivalentReserveClause + "                                          t_equivalentReserve,"
                + "\n" + "        account.t_chapterNumber                                                     t_chapterNumber,"
                + "\n" + "        fi.t_id                                                                     t_fiId,"
                + "\n" + "        fi.t_code                                                                   t_sortingFiCode,"
                + "\n" + "        NVL(account.t_accountNumber, LPAD('9', 30, '9'))                            t_sortingAccountNumber,"
                + "\n" + "        GROUPING_ID("
                + "\n" + "                    fi.t_code,"
                + "\n" + "                    fi.t_id,"
                + "\n" + "                    account.t_accountNumber,"
                + "\n" + "                    account.t_chapterNumber"
                + "\n" + "                   )                                                                t_grouping,"
                + "\n" + "        account.t_fiIdMain                                                          t_accountFiId,"
                + "\n" + "        account.t_reserveDate                                                       t_reserveDate"
                + "\n" + "   FROM df634cb_tmp account,"
                + "\n" + "        v_rcb_f634_fininstr fi"
                + "\n" + "  WHERE (account.t_fiId = fi.t_id OR account.t_fiIdLinked = fi.t_id)"
                + "\n" + "    AND (" + usedByPositionFilterClause + ")"
                + "\n" + " GROUP BY ROLLUP("
                + "\n" + "                 fi.t_code,"
                + "\n" + "                 fi.t_id,"
                + "\n" + "                 (account.t_accountNumber, account.t_reserveDate),"
                + "\n" + "                 (account.t_chapterNumber, account.t_fiIdMain)"
                + "\n" + "                )"
                + "\n" + ")"
                + "\n" + "SELECT protocolData.*,"
                + "\n" + "       CASE WHEN t_reserveDate IS NULL"
                + "\n" + "                THEN 0"
                + "\n" + "            ELSE rsb_fiinstr.convSumType(1, t_fiId, 0, 7, t_reserveDate)"
                + "\n" + "       END                                                                          t_rate"
                + "\n" + "  FROM protocolData"
                + "\n" + " WHERE t_grouping IN (0, 3)"
                + "\n" + "ORDER BY t_sortingFiCode, t_sortingAccountNumber";

        return query;

    end;

    macro getDataSource()
        return m_container;
    end;

    private macro constructorTDataSourceCbReserves(parameters : TParameters, positionCodeFilter : TPositionCodeFilter)
        initTPrinterDataSource(parameters);

        m_positionCodeFilter = positionCodeFilter;

        m_container = TRsbDataset(makeDataQuery());

        m_container.setFieldType("t_reserve",           V_MONEY);
        m_container.setFieldType("t_equivalentReserve", V_MONEY);
    end;

    constructorTDataSourceCbReserves(parameters, positionCodeFilter);
end;
//-----------------------------------------------------------------------------
//Резервы на возможные потери (Val6)
private class (TProtocol) TGetReserve(parameters : TParameters, dateReserve : Date)

  private var m_isEmpty;

//-----------------------------------------------------------------------------
  private macro isEmpty()
    return m_isEmpty;
  end;
//-----------------------------------------------------------------------------
  private macro GetDataSourceReserve(positionCodeFilter)

    var dataSource = TDataSourceCbReserves(getParameters(), positionCodeFilter).getDataSource();
    var SumReserve = 0;

      while (dataSource.next())
        m_isEmpty = false;
        SumReserve = SumReserve + dataSource.reserve;
//            dataSource.reserve, -dataSource.equivalentReserve
      end;
    return 0;  
  end;
//-----------------------------------------------------------------------------
  macro GetSumReserve()

    var parameters = getParameters();
    var positionCodeFilter = TPositionCodeFilter();

    positionCodeFilter.reset();
    positionCodeFilter.addIncluded(parameters.getCode_vvv_rez_bal_gk());
    positionCodeFilter.addIncluded(parameters.getCode_vvv_rez_bal_gk_810());
    return GetDataSourceReserve(positionCodeFilter);

    onError(error)
      exceptionMessage(error);
      exit(1);
  end;
//-----------------------------------------------------------------------------
  private macro constructorTGetReserves(parameters : TParameters, dateReserve : Date)
    initTProtocol(parameters);
    m_isEmpty = true;
  end;
//-----------------------------------------------------------------------------
  constructorTGetReserves(parameters);
end;
//Расчет резервов на возможные потери (Val6)
//-----------------------------------------------------------------------------
private macro CalcRreserve(reportAll, reserveDate : Date)
  var context;
  var period;
  var report;

  BegAction(1,"Расчет полей для резервов на возможные потери.",false);
  context = reportAll.context;
  period  = RcbPeriod(reserveDate, reserveDate);
  context = RcbReportContext(period, context.departmentCode, context.issueMode, context.organizationStructure, context.isSummaryMode);
  report  = RcbApplication().objectFactory().createReport("Форма 634", context);

  execMacroFile("calc_f634pre.mac", "main", report);      // Предварительные действия

  execMacroFile("calc_f634cb.mac", "main", report);       // Расчет по данным ГКБО

  execMacroFile("calc_f634post.mac", "main", report);     // Завершающие действия

  EndAction();
end;
//-----------------------------------------------------------------------------
//Выборка резервов на возможные потери (Val6)
//-----------------------------------------------------------------------------
private macro GetRreserve(report,reserveDate : Date)
  //Переделать на выбрку по валюте или счетам!!!!!!!!
  var protRes = TGetReserve(TParameters(report),reserveDate);
  return(protRes.GetSumReserve());
end;
//-----------------------------------------------------------------------------
//Считывание поля из структуры(valuaiterator) Scaled;
//-----------------------------------------------------------------------------
private macro GetAttrFromReport634Scaled(valuaiterator,fieldName)

  var value = valuaiterator.currentitem.fieldValue(fieldName).scaled;
  if (((ValType(value) == V_MONEY) or (ValType(value) == V_NUMERIC)) and (value == 0))
    return 0;
  end;
  return valuaiterator.currentitem.fieldValue(fieldName).scaledAsString;
end;
////Считывание поля из структуры(valuaiterator) Scaled;
//-----------------------------------------------------------------------------
private macro GetAttrFromReport634Exact(valuaiterator,fieldName)

  var value = valuaiterator.currentitem.fieldValue(fieldName).exact;
  if (((ValType(value) == V_MONEY) or (ValType(value) == V_NUMERIC)) and (value == 0))
    return 0;
  end;
  return valuaiterator.currentitem.fieldValue(fieldName).exact;
end;
//Считывание поля и поля fieldName из структуры structName за дату curReportDate
//-----------------------------------------------------------------------------
private macro GetOtherAttrFromReport634(curReportDate,structName,fieldName)

  var report = RcbApplication().currentReport;
  var attributeValueIterator = report.attributeValue(structName);
  var valuaiterator = attributeValueIterator.createValueIterator();
  
  valuaiterator.movefirst; 
  while (not valuaiterator.IsDone())    
    if (valuaiterator.currentitem.fieldValue("Отчетная дата").exact == curReportDate)
     return GetAttrFromReport634Scaled(valuaiterator,fieldName);
    end;
  end;
  return 0;
end;  
//Подготовка строки Валюта
//-----------------------------------------------------------------------------
private macro GetDataСurFromReport634(valuaiterator)
 
  var sum1 = "\""+GetAttrFromReport634Scaled(valuaiterator,"Код валюты")+"\" ";
  var sum2 = "\"\" ";
  var sum3 = "\"\" ";
  var sum4 = "\"\" ";

  var val1 = GetAttrFromReport634Scaled(valuaiterator,"3")+" ";
  var val2 = GetAttrFromReport634Scaled(valuaiterator,"4")+" ";
  var val3 = GetAttrFromReport634Scaled(valuaiterator,"5")+" ";
  var val4 = GetAttrFromReport634Scaled(valuaiterator,"6")+" ";
  var val5 = GetAttrFromReport634Scaled(valuaiterator,"7")+" ";

  var val6 = "0 ";//Не знаю откуда брать, возможно здесь 0 (значение в строку "руб")!!!

  var val7  = GetAttrFromReport634Scaled(valuaiterator,"11")+" ";
  var val8,val9,val10,val11;
  if (GetAttrFromReport634Scaled(valuaiterator,"Валюта/металл") == 1)    
    
    val8  = String(GetAttrFromReport634Exact(valuaiterator,"13") : 0 : *, ТочВалОкруг)+" ";
    val9  = String(GetAttrFromReport634Exact(valuaiterator,"14") : 0 : *, ТочВалОкруг)+" ";
    val10 = String(GetAttrFromReport634Exact(valuaiterator,"15") : 0 : *, ТочВалОкруг)+" ";
    val11 = String(GetAttrFromReport634Exact(valuaiterator,"16") : 0 : *, ТочВалОкруг)+" ";
  else
    val8  = String(GetAttrFromReport634Exact(valuaiterator,"13") : 0 : *, ТочДрагОкруг)+" ";
    val8  = String(GetAttrFromReport634Exact(valuaiterator,"14") : 0 : *, ТочДрагОкруг)+" ";
    val10 = String(GetAttrFromReport634Exact(valuaiterator,"15") : 0 : *, ТочДрагОкруг)+" ";
    val11 = String(GetAttrFromReport634Exact(valuaiterator,"16") : 0 : *, ТочДрагОкруг)+" ";
  end;
  var val12 = GetAttrFromReport634Scaled(valuaiterator,"9")+" ";

  var txt = "\""+GetAttrFromReport634Scaled(valuaiterator,"12")+"~0~"+GetOtherAttrFromReport634(GetAttrFromReport634Scaled(valuaiterator,"Отчетная дата"),"Итоги","КОВПСум_18")+"~0.0000~\"";

  return sum1+sum2+sum3+sum4+val1+val2+val3+val4+val5+val7+val8+val9+val10+val11+val12+txt;
end;
//Подготовка строки руб
//-----------------------------------------------------------------------------
private macro GetDataRurFromReport634(valuaiterator)
    
  var sum1 = "\""+GetAttrFromReport634Scaled(valuaiterator,"Код валюты")+"\" ";
  var sum2 = "\"руб\" ";
  var sum3 = "\"\" ";
  var sum4 = "\"\" ";

  var val1 = "0 ";
  var val2 = "0 ";
  var val3 = "0 ";
  var val4 = "0 ";
  var val5 = "0 ";

  var val6 = GetRreserve(RcbApplication().currentReport,valuaiterator.currentitem.fieldValue("Отчетная дата").exact);//"0 "; 

  var val7  = "0 ";
  var val8  = "0 ";
  var val9  = "0 ";
  var val10 = "0 ";
  var val11 = "0 ";
  var val12 = "0 ";

  var txt = "\"\"";

  return sum1+sum2+sum3+sum4+val1+val2+val3+val4+val5+val7+val8+val9+val10+val11+val12+txt;
end;
//Подготовка строки Руб
//-----------------------------------------------------------------------------
private macro GetDataRurSum1FromReport634(valuaiterator)
  var sum1 = "\"Руб\" ";
  var sum2 = "\"\" ";
  var sum3 = "\"\" ";
  var sum4 = "\"\" ";

  var val1  = "0 ";
  var val2  = "0 ";
  var val3  = "0 ";
  var val4  = "0 ";
  var val5  = "0 ";
  var val6  = "0 ";
  var val7  = "0 ";
  var val8  = "0 ";
  var val9  = "0 ";
  var val10 = "0 ";
  var val11 = GetOtherAttrFromReport634(GetAttrFromReport634Scaled(valuaiterator,"Отчетная дата"),"Итоги","КОВПСум_18");
  var val12 = "0 ";

  var txt   = "\"~~           0.0000~\"";

  return sum1+sum2+sum3+sum4+val1+val2+val3+val4+val5+val6+val7+val8+val9+val10+val11+val12+txt;
end;
//Подготовка строки Сум
//-----------------------------------------------------------------------------
private macro GetDataRurSum2FromReport634(valuaiterator)
  var sum1 = "\"Сум\" ";
  var sum2 = "\"\" ";
  var sum3 = "\"\" ";
  var sum4 = "\"\" ";

  var val1  = "0 ";
  var val2  = "0 ";
  var val3  = "0 ";
  var val4  = "0 ";
  var val5  = "0 ";
  var val6  = "0 ";  
  var val7  = "0 ";
  var val8  = "0 ";
  var val9  = "0 ";
  var val10 = "0 ";
  var val11 = GetOtherAttrFromReport634(GetAttrFromReport634Scaled(valuaiterator,"Отчетная дата"),"Итоги","ЛимСОВП_16");
  var val12 = "0 ";

  var txt   = "\"~~           0.0000~\"";

  return sum1+sum2+sum3+sum4+val1+val2+val3+val4+val5+val6+val7+val8+val9+val10+val11+val12+txt;
end;
//Вставка подготовленных строк в файл
//-----------------------------------------------------------------------------
private macro SaveDataFromReport634(fileId,departmnrtId)
 
  var report = RcbApplication().currentReport;
  var attributeValueIterator = report.attributeValue("ОВП");
  var valuaiterator = attributeValueIterator.createValueIterator(); 
  var i = 0;
  var curDate, oldDate = "",tmpDate;
  var flagRurSum = false;

  InitProgress (valuaiterator.count, NULL, "Сбор данных из расчета 634 формы");
  valuaiterator.movefirst;
  while (not valuaiterator.IsDone())    
    curDate = valuaiterator.currentitem.fieldValue("Отчетная дата").exact;
    if (curDate != oldDate)
      if (oldDate != "")
          if (not flagRurSum)
            insert(fileID,GetDataRurSum1FromReport634(valuaiterator));
            insert(fileID,GetDataRurSum2FromReport634(valuaiterator));
          end;
          insert(fileId,".");
      end;    
      oldDate = curDate;
      if (StrLen(Trim(curDate))==9)
        tmpDate="0"+Trim(curDate);
      else  
        tmpDate=curDate;
      end;
      CalcRreserve(report,curDate);    
      insert(fileId,"\"OLAPDataBlock\" \"4.1D\" \"f634alln\" \""+departmnrtId+"\" \""+StrSubst(tmpDate,".","/")+"\" \""+StrSubst(tmpDate,".","/")+"\" yes");
      flagRurSum = false;
    end;     

    if ((substr(valuaiterator.currentitem.fieldValue("Код валюты").exact,1,1) == "A") and (not flagRurSum))
      flagRurSum = true;
      insert(fileID,GetDataRurSum1FromReport634(valuaiterator));
      insert(fileID,GetDataRurSum2FromReport634(valuaiterator));
    end;

    insert(fileID,GetDataСurFromReport634(valuaiterator));
    insert(fileID,GetDataRurFromReport634(valuaiterator));
    valuaiterator.MoveNext(); 
    UseProgress (i+1);
    i = i + 1;
  end;
  if (not flagRurSum)
    insert(fileID,GetDataRurSum1FromReport634(valuaiterator));
    insert(fileID,GetDataRurSum2FromReport634(valuaiterator));
  end;
  RemProgress (i+1);  
  println ("Количество выгруженных строк: "+(i+3));
end;
//проверяем наличие файла экспорта
//-----------------------------------------------------------------------------
private macro CheckFileExists(fullName)
  var UserChoice, DirList:TDirList=TDirList(fullName, "f");
  array itemChoice;
  array headMsg;
  itemChoice(0)="Перезаписать"; itemChoice(1)="Прервать";
  headMsg(0)="Файл " + fullName + " уже существует";

  if(DirList.Count>0)
    UserChoice=ConfWin(headMsg, itemChoice, 0);
    if(UserChoice == 1)
      return 1;
    end;
  end;
end;    
//-----------------------------------------------------------------------------
//экспорт в файл
private macro save634DataToFile(departmnrtId)
  var fPath, fName, fullName,
      day, mon, year;
          
  if (ПолучитьКаталогЭкспорта!="")
    fPath=StrSubst(ПолучитьКаталогЭкспорта,"\\","\\\\")+"\\\\";
  else
    msgbox("Не задан каталог экспорта файлов!!! || Меню Настройки->Параметры экспорта/импорта");
    println ("Не задан каталог экспорта файлов!!!");
    println ("Задайте его в меню подсистемы РеглОтч Настройки->Параметры экспорта/импорта");
    return 1;
  end;

  File fileID01 () txt write;
          
  DateSplit (date,day,mon,year);           
  fName="f634n_"+string(year)+string(mon)+string(day)+"."+departmnrtId;
  fullName=fPath+fName;
         
  if (CheckFileExists(fullName))
    println ("Файл экспорта уже существует: "+fullName);
    println ("Экспорт прерван пользователем!!!");
    return 1;
  end;
 
  open(fileID01,fullName,"rsoem",false,1);

  SaveDataFromReport634(fileID01,departmnrtId);

  insert(fileID01,".");
  close(fileID01);
  println ("Файл экспорта: "+fullName);

  var filenameT, extT, dirT = "$c:\\sofr\\txtfile";
  if(ExistDir(dirT) == 2)
    if(not makedir(dirT))
      println("Ошибка создания директории " + dirT);
    end;
  end;
  SplitFile(fullName, filenameT, extT);
  if(not copyfile(fullName, dirT + "\\" + filenameT + extT))
    println("Ошибка копирования файла " + fullName + " на терминал (" + dirT + "\\" + filenameT + extT + ")");
  end;

end;
//Вывод заголовка протокола
//-----------------------------------------------------------------------------
private macro exportProtocolHeader(beginDate, endDate)
  println("Форма 634");
  println("");
  println("ПРОТОКОЛ ЭКСПОРТА В БИСквит");
  println("");
  println("Период отчета с "+beginDate+" по "+endDate);
  println("Дата и время выпуска отчета "+date+" "+time);
  println("Исполнитель "+{oper});
  println("");
end;
//-----------------------------------------------------------------------------
//проверить наличие расчета в текущей 634 форме
private macro checkCalcForm634()
  var report    = RcbApplication().currentReport; 
  var beginDate = RcbApplication().currentReport.context.period.beginDate;
  var endDate   = RcbApplication().currentReport.context.period.endDate;

  if (report.isCalculated==false)
    msgbox("Для запуска экспорта необходима расчитанная форма 634 || Выполните расчет формы 634 за "+beginDate+" - "+endDate+" период");
    exportProtocolHeader();
    println("Для запуска экспорта необходима расчитанная форма 634");
    println("Выполните расчет формы 634 за "+beginDate+" - "+endDate+" период");
    return 1;
  end;
  exportProtocolHeader(beginDate,endDate);

  return 0;
end;
//-----------------------------------------------------------------------------
macro Export634ToFile(departmnrtId)
  if (checkCalcForm634()==0)
    save634DataToFile(departmnrtId);
  end;  
end;
//-----------------------------------------------------------------------------
  private var dep={OperDprtNode};

  Export634ToFile(dep);
//-----------------------------------------------------------------------------
