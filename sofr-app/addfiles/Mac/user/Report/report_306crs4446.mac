/*04.2020 BPV Выгрузка счетов 306 для нужд CRS. BIQ-4446*/
/*GAA: 541934. Доработка отчета для возможности сохранения csv-файла в произвольное место на машине пользователя, а не только на терминале или СП */
import RsbFormsInter, cbreport_h, cb_sql, "dl_util_xml_obj.mac";
import "DLReport_h.mac", "dlreport.mac", scsrvrepfun;

PRIVATE CONST  C_FILENAME = "sofrbroker_" + strsubst(strsubst(string(date()), " ", "0"), ".", "")  + ".csv";
//PRIVATE CONST MAXROWS_L: Integer = 500000;
PRIVATE CONST MAXROWS_L: Integer = 10000;

private var _cond = "";//" where t_gross_proceeds <> 0 or t_other_total <> 0 ";
private var isshow = false;
private var isCSV = false;
private var fileFullName = "";

private macro CollectData(_Year, _TypeAccVarMar)
   var year = string(_Year);
   sql_execute(" truncate table report_crs_4446_tmp ");

   InitProgress(4);

   var sql = 
      "insert into report_crs_4446_tmp SELECT distinct "+year+" t_year, a.t_account, a.t_accountid, a.t_open_date, a.t_close_date,  " + //AVN: 542875
      "RSB_ACCOUNT.RESTALL(a.t_account, a.t_chapter, a.t_code_currency, TO_DATE ('3112"+year+"', 'ddmmyyyy')) t_rest, " +
      " (select t_ccy from dfininstr_dbt f where t_fiid = a.t_code_currency), " +
      "0,0,0,0," + 
      " nvl((select t_code from dobjcode_dbt where t_state = 0 and t_objecttype = 3 and t_codekind = 102 and t_objectid = a.t_client),chr(0)), " + //код Диасфот
      "  nvl(nvl((select t_code " +
      "                from dobjcode_dbt " +
      "              where t_state = 0 and t_objecttype = 3 and t_codekind = 108 and t_objectid = a.t_client)," +
      "             (select t_code " +
      "                from dobjcode_dbt " +
      "              where t_state = 0 and t_objecttype = 3 and t_codekind = 101 and t_objectid = a.t_client and regexp_like(t_code, '^\\d{2}#.#'))) , chr(0))," + //код БИСквит
      " nvl((select t_code from dobjcode_dbt where t_state = 0 and t_objecttype = 3 and t_codekind = 101 and t_objectid = a.t_client and regexp_like(t_code, '^\\d*$')),chr(0)), " + //Код ЦФТ
      " case when a.t_open_date <= to_date('19.07.2018','dd.mm.yyyy') then '1' else '2' end, " + //14.	<Проверка PreEx> если данный счет 306* был открыт до 19.07.2018 (включительно) проставлять 1,  иначе проставлять 2.
	  " a.t_client"
      "  FROM daccount_dbt a " +
      "       INNER JOIN dmcaccdoc_dbt ma " +
      "          ON     a.t_account = ma.t_account " +
      "             AND a.t_chapter = ma.t_chapter " +
      "             AND A.T_CODE_CURRENCY = ma.t_currency and ma.t_iscommon = chr(88) " +
      "       LEFT JOIN dsfcontr_dbt sf " +
      "          ON (ma.t_clientcontrid = sf.t_id) " +
      " WHERE MA.T_CATNUM = 201 AND sf.t_id IS NOT NULL " +
      "       AND a.t_open_date <= TO_DATE ('3112"+year+"', 'ddmmyyyy') " + 
//      "       AND  ma.t_account = '30601810399000116111' " +     //debug
      "       AND ( (a.t_close_date = TO_DATE ('01010001', 'ddmmyyyy')) " +
      "            OR (a.t_close_date BETWEEN TO_DATE ('0101"+year+"', 'ddmmyyyy') " +
      "                                   AND TO_DATE ('3112"+year+"', 'ddmmyyyy'))) " ;
   UseProgress(0);
   message("Отбор счетов");
   sql_execute(sql);

   sql = 
      "MERGE INTO REPORT_CRS_4446_TMP tt " +
      "     USING (  SELECT tmp.t_account, SUM (TRN.T_SUM_RECEIVER) smr " +
      "                FROM ddlgrdoc_dbt grdoc, " +
      "                     ddlgrdeal_dbt grdeal, " +
      "                     dacctrn_dbt trn, " +
      "                     REPORT_CRS_4446_TMP tmp " +
      "               WHERE     GRDEAL.T_ID = GRDOC.T_GRDEALID " +
      "                     AND grdoc.t_docid = trn.t_acctrnid " +
      "                     AND TRN.T_ACCOUNT_receiver = TMP.T_ACCOUNT " +
      "                     and exists (select 1 from drestdate_dbt where t_accountid = tmp.t_accountid)/*boost*/ " +
      "                     AND GRDEAL.T_TEMPLNUM <> 9 " +//комиссия
      "                     and trn.t_date_carry BETWEEN TO_DATE ('0101"+year+"', 'ddmmyyyy') " +
      "                                   AND TO_DATE ('3112"+year+"', 'ddmmyyyy') " 
      "            GROUP BY tmp.t_account) t " +
      "        ON (tt.t_account = t.t_account) " +
      "WHEN MATCHED " +
      "THEN " +
      "   UPDATE SET TT.T_GROSS_PROCEEDS = t.smr " ;
   UseProgress(1);
   message("Gross");
   sql_execute(sql);

   sql =  
      "MERGE INTO REPORT_CRS_4446_TMP tt " +
      "     USING (  SELECT tmp.t_account, SUM (TRN.T_SUM_RECEIVER) smr " +
      "                FROM doproper_dbt oper, " +
      "                     doprdocs_dbt docs, " +
      "                     dacctrn_dbt trn, " +
      "                     REPORT_CRS_4446_TMP tmp " +
      "               WHERE     DOCS.T_ACCTRNID = trn.t_acctrnid " +
      "                     AND TRN.T_ACCOUNT_receiver = TMP.T_ACCOUNT " +
      "                     AND oper.t_id_operation = docs.t_id_operation " +
      "                     AND EXISTS " +
      "                            (SELECT 1 " +
      "                               FROM drestdate_dbt " +
      "                              WHERE t_accountid = tmp.t_accountid)   /*boost*/ " +
      "                     AND SUBSTR (trn.t_ground, 1, 3) <> 'Вар' " +
      "                     AND oper.t_dockind NOT IN " +
      "                            (4607 /*списания зачисления*/, " +
      "                             159/*РОВУ*/, " +
      "                             4813/*валютка*/) /*194 расчеты по пи*/ " +
      "                     and trn.t_date_carry BETWEEN TO_DATE ('0101"+year+"', 'ddmmyyyy') " +
      "                                   AND TO_DATE ('3112"+year+"', 'ddmmyyyy') " 
      "            GROUP BY tmp.t_account) t " +
      "        ON (tt.t_account = t.t_account) " +
      "WHEN MATCHED " +
      "THEN UPDATE SET TT.T_OTHER_TOTAL= t.smr " ;
   UseProgress(2);
   message("Other_total");
   sql_execute(sql);

   sql =  
      "MERGE INTO REPORT_CRS_4446_TMP tt " +
      "     USING (  SELECT tmp.t_account, SUM (TRN.T_SUM_RECEIVER) smr " +
      "                FROM doproper_dbt oper, " +
      "                     doprdocs_dbt docs, " +
      "                     dacctrn_dbt trn, " +
      "                     REPORT_CRS_4446_TMP tmp " +
      "               WHERE     DOCS.T_ACCTRNID = trn.t_acctrnid " +
      "                     AND TRN.T_ACCOUNT_receiver = TMP.T_ACCOUNT " +
      "                     AND oper.t_id_operation = docs.t_id_operation " +
      "                     AND EXISTS " +
      "                            (SELECT 1 " +
      "                               FROM drestdate_dbt " +
      "                              WHERE t_accountid = tmp.t_accountid)   /*boost*/ " +
      "                     AND SUBSTR (trn.t_ground, 1, 3) = 'Вар' " +
      "                     AND oper.t_dockind NOT IN " +
      "                            (4607 /*списания зачисления*/, " +
      "                             159/*РОВУ*/, " +
      "                             4813/*валютка*/) /*194 расчеты по пи*/ " +
      "                     and trn.t_date_carry BETWEEN TO_DATE ('0101"+year+"', 'ddmmyyyy') " +
      "                                   AND TO_DATE ('3112"+year+"', 'ddmmyyyy') " 
      "            GROUP BY tmp.t_account) t " +
      "        ON (tt.t_account = t.t_account) " +
      "WHEN MATCHED " +
      "THEN UPDATE SET TT.T_OTHER_VM_INC = t.smr, TT.T_OTHER_VM_PROF = t.smr " ; /*T_OTHER_VM_PROF тут только положительную записываем, а следующим апдейтом отнимем отрицательную*/
   UseProgress(3);
   message("Other_vm_inc");
   sql_execute(sql);

   sql =  
      "MERGE INTO REPORT_CRS_4446_TMP tt " +
      "     USING (  SELECT tmp.t_account, SUM (TRN.T_SUM_RECEIVER) smr " +
      "                FROM doproper_dbt oper, " +
      "                     doprdocs_dbt docs, " +
      "                     dacctrn_dbt trn, " +
      "                     REPORT_CRS_4446_TMP tmp " +
      "               WHERE     DOCS.T_ACCTRNID = trn.t_acctrnid " +
      "                     AND TRN.T_ACCOUNT_PAYER = TMP.T_ACCOUNT " +
      "                     AND oper.t_id_operation = docs.t_id_operation " +
      "                     AND EXISTS " +
      "                            (SELECT 1 " +
      "                               FROM drestdate_dbt " +
      "                              WHERE t_accountid = tmp.t_accountid)   /*boost*/ " +
      "                     AND SUBSTR (trn.t_ground, 1, 3) = 'Вар' " +
      "                     AND oper.t_dockind NOT IN " +
      "                            (4607 /*списания зачисления*/, " +
      "                             159/*РОВУ*/, " +
      "                             4813/*валютка*/) /*194 расчеты по пи*/ " +
      "            GROUP BY tmp.t_account) t " +
      "        ON (tt.t_account = t.t_account) " +
      "WHEN MATCHED " +
      "THEN UPDATE SET TT.T_OTHER_VM_PROF = case when (TT.T_OTHER_VM_PROF - t.smr) < 0 then 0 else TT.T_OTHER_VM_PROF - t.smr end " ; /*T_OTHER_VM_PROF положительную посчитали в предыдущем апдейте*/
   UseProgress(3);
   message("Other_vm_prof");
   sql_execute(sql);

   RemProgress;
end;

private macro GetSavePath(): String
  var folder = "";

  if (not SelectFolder(folder, null, "Выберите папку для выгрузки", true))
     folder = "..\\TxtFile";
  end;
  
  return folder;
end;

private macro SaveReport(_Year, _TypeAccVarMar)
/***********************/
   //var Obtemp = CTemplateXLS();
   var Obtemp = CTemplateSXLSX();
   var obtable;
   var csvFileTmp  = C_CSVFile(";");
   FILE csvFileResult () txt write; // csv таблица, которая будет передана пользователю
   var csvFileResultName: String = String(UserNumber) + "_4446.csv";
   var cnt_sql = "select * from report_crs_4446_tmp " + _cond + " order by t_close_date, t_open_date, t_account ";
   var nrecs0 = SQL_GetNRecs(cnt_sql);

   private macro AddRow(value)
     if (isCSV)
       Insert(csvFileResult, csvFileTmp.GetCurRow());
     end;
     csvFileTmp.AddRow(value);
   end;

   private macro SheetReport(n)
     var sql = "select * from (select tmp.*, row_number() over(order by t_year, t_account, t_accountid, t_open_date, t_close_date, " + 
            " t_rest, t_code_currency, t_gross_proceeds, t_other_total, t_other_vm_inc, t_other_vm_prof, t_diasid, " + 
            " t_bisid, t_cftid, t_preex, t_sofrid) rn from report_crs_4446_tmp tmp " + _cond + 
            " ) tmp1 where tmp1.rn <=  " + MAXROWS_L * n + " and tmp1.rn > " + MAXROWS_L * (n-1);
     var nrecs = SQL_GetNRecs(sql);

     sql = TRsbDataSet(sql);
     csvFileTmp.FileOpen(string(UserNumber)+"_4446_"+n+".csv",true); // csv таблица для формирования xls файла
     InitProgress(nrecs, "Сохранение данных");
     nrecs = 0;
     while (sql.MoveNext())
       // def-40139 в or_tpl_h.mac в addcell добавлена возможность отключить кавычки   
       csvFileTmp.AddCell(String(sql.value("t_year")),null,true);
       csvFileTmp.AddCell(sql.value("t_account"),null,true);
       csvFileTmp.AddCell(String(sql_convtypedate(sql.value("t_open_date")):f),null,true);
       csvFileTmp.AddCell(String(sql_convtypedate(sql.value("t_close_date")):f),null,true);
       csvFileTmp.AddCell(sql.value("t_rest"));
       csvFileTmp.AddCell(sql.value("t_code_currency"),null,true);
       csvFileTmp.AddCell(sql.value("t_gross_proceeds"));
       csvFileTmp.AddCell(sql.value("t_other_total") + iif(_TypeAccVarMar, sql.value("t_other_vm_inc"), sql.value("t_other_vm_prof")));
       csvFileTmp.AddCell(sql.value("t_other_vm_inc"));
       csvFileTmp.AddCell(sql.value("t_other_vm_prof"));
       csvFileTmp.AddCell(sql.value("t_sofrid"));
       csvFileTmp.AddCell(sql.value("t_diasid"),null,true);
       csvFileTmp.AddCell(sql.value("t_bisid"),null,true);
       csvFileTmp.AddCell(sql.value("t_cftid"),null,true);
       csvFileTmp.AddCell(sql.value("t_preex"),null,true);
       AddRow(true);
       UseProgress(nrecs=nrecs+1);
     end;
     RemProgress();  
     csvFileTmp.FileClose();	 
   end;

   Obtemp.OpenTemplate("Report_306crs4446.xlsx", false); //имя шаблона
   Obtemp.SetXLSXFormat(); 
   obtable  = Obtemp.RegisterTable("bodyline");

   if (isCSV AND NOT WR_Open(csvFileResult, csvFileResultName))
     MsgBox("Ошибка открытия файла <" + csvFileResultName + ">!");
     isCSV = false;
   end;

   for (var i:integer, 1, nrecs0/MAXROWS_L + 1);
      SheetReport(i);
   end; 

   if (isCSV)
     Close(csvFileResult);
   end;

   Obtemp.SetValue_NameCell("bodyline");
   obtable.FillTabelFromCSV(csvFileTmp.GetlsFileName);
   Obtemp.SaveAsTemplate("report_"+string(UserNumber)+"_4446", isShow);

   for (var i1:integer, 0, nrecs0/MAXROWS_L)
     RemoveFile(csvFileTmp.getlsfilename[i1]);
   end;

   if (isCSV)
     if( not CopyFile("..\\Txtfile\\" + csvFileResultName, "$" + fileFullName, true) )
       MsgBox("Ошибка копирования файла " + csvFileResultName + " на терминал пользователя");
     else
       RemoveFile("..\\Txtfile\\" + csvFileResultName);
       MsgBox("Выгружен " + fileFullName);
     end; 
   end;
/***************************/
end;


class (TRsbPanel) Pan
   initTRsbPanel();
   var curstr = 2;
   Caption = "Отчет по счетам 306* для целей CRS";
   Status = "~ESC~Выход ~F2~Выполнить";
   setSize(35,12);
   setPosition(8,1);
   var _Year, _TypeAccVarMar;

   var yy;
   DateSplit({curdate},null,null,yy);
   var m_eYear:TRsbEditField = TRsbEditField(1/*FT_INT32*/);
   m_eYear.SetPosition(7,curstr);
   m_eYear.SetSize(5,1);
   m_eYear.editable = true;
   m_eYear.textLength = 5;
   m_eYear.name = "_Year";
   m_eYear.Value = yy;
   addControl (m_eYear);

   var m_Label1:TrsbLabel = TRsbLabel(2,curstr,"Год: ");
   addLabel (m_Label1);


   curstr = curstr + 2;
   var m_eTypeAccVarMar1:TRsbRadioButton = TRsbRadioButton(1/*FT_INT32*/);
   m_eTypeAccVarMar1.SetPosition(4,curstr);
   m_eTypeAccVarMar1.OnChecked(R2M(this, "OnChecked"));
   m_eTypeAccVarMar1.name = "m_eTypeAccVarMar1";
   m_eTypeAccVarMar1.checked = true;
   addControl (m_eTypeAccVarMar1);
   var m_Label2:TrsbLabel = TRsbLabel(6,curstr,"VarMar Inc");
   addLabel (m_Label2);

   curstr = curstr + 1;
   var m_eTypeAccVarMar2:TRsbRadioButton = TRsbRadioButton(1/*FT_INT32*/);
   m_eTypeAccVarMar2.SetPosition(4,curstr);
   m_eTypeAccVarMar2.OnChecked(R2M(this, "OnChecked"));
   m_eTypeAccVarMar2.name = "m_eTypeAccVarMar2";
   addControl (m_eTypeAccVarMar2);
   var m_Label3:TrsbLabel = TRsbLabel(6,curstr,"VarMar Prof");
   addLabel (m_Label3);

   curstr = curstr + 2;
   var m_cCSV:TRsbCheckBox = TRsbCheckBox;
   m_cCSV.setPosition(4,curstr);
   m_cCSV.name = "m_cCSV";
   m_cCSV.checked = true;
   m_cCSV.onChecked(R2M(this, "OnChecked"));
   addControl (m_cCSV);
   var m_Label4:TrsbLabel = TRsbLabel(6,curstr,"Выгрузка в CSV");
   addLabel (m_Label4);

   curstr = curstr + 1;
   var m_cExcel:TRsbCheckBox = TRsbCheckBox;
   m_cExcel.setPosition(4,curstr);
   m_cExcel.name = "m_cExcel";
   m_cExcel.checked = true;
   m_cExcel.onChecked(R2M(this, "OnChecked"));
   addControl (m_cExcel);
   var m_Label5:TrsbLabel = TRsbLabel(6,curstr,"Выгрузка в Excel");
   addLabel (m_Label5);


   curstr = curstr + 2;
   var m_PushB_Run:TRsbPushButton = TRsbPushButton("Старт");
   m_PushB_Run.setPosition(6,curstr);
   m_PushB_Run.setSize(7,1);
   m_PushB_Run.onClicked(R2M(this, "_Run"));
   addControl(m_PushB_Run);

   var m_PushB_Canc:TRsbPushButton = TRsbPushButton("Отмена");
   m_PushB_Canc.setPosition(17,curstr);
   m_PushB_Canc.setSize(7,1);
   m_PushB_Canc.onClicked(R2M(this, "_Cancel"));
   addControl(m_PushB_Canc);


   addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "onKeyPressed"));
   addEventHandler(RSB_EV_CONTROL_CHECKED,R2M(this,"OnChecked"));

   macro OnChecked(RsbEvent:Object)
      if (RsbEvent.id == RSB_EV_CONTROL_CHECKED)
         if (RsbEvent.source.name == "m_cExcel")
            RsbEvent.source.checked = not RsbEvent.source.checked;
            this.redraw();
         elif (RsbEvent.source.name == "m_cCSV")
            RsbEvent.source.checked = not RsbEvent.source.checked;
            this.redraw();
         end;
      end;
      return true;
   end;

   macro onKeyPressed(RsbEvent)   
      if (RsbEvent.id = RSB_EV_KEY_PRESSED)
         if (RsbEvent.keycode == 316)
            this._Run(RsbEvent)
         end;   
      end;
   end;

   macro _Run(RsbEvent)
      CollectData(m_eYear.Value, m_eTypeAccVarMar1.Checked);
      if (m_cExcel.checked)
         isshow = true;/*GAA: 541934 */
      end;
      if (m_cCsv.checked)
         fileFullName = GetSavePath() + "\\" + C_FILENAME;
         isCSV = true; /*GAA: 541934 */
      end;
      /*GAA: 541934 */
      SaveReport(m_eYear.Value, m_eTypeAccVarMar1.Checked);
      isCSV = false; 
      isshow = false;
      /*GAA: 541934 */
   end;


   macro _Cancel(RsbEvent)
      if (RsbEvent.id == RSB_EV_BUTTON_CLICKED)
        close(0);
      end;

   end;
end;

private macro CanExec()
  var sql;
  sql = "select count(1) cnt from dacsgroupoper_dbt lg where (lg.t_groupid = 10027 or lg.t_groupid = 10010) and lg.t_oper = " + string({oper});
  sql = TRsbDataSet(sql);
  if (sql.MoveNext() )
     return int(sql.value("cnt")) > 0;
  end; 
  return false;
end;

if (not CanExec())
    MsgBox("У Вас нет прав на выпуск данного отчета!");
	exit(1);
end;

var Panel = Pan();
Pan.Run;
exit(1);
