/*
ВУ13 - Элементы расчетной базы резервирования по операциям РЕПО за 31.07.2018
*/
import oralib, likepy, or_rep_h, globals;

private var sqlString:string;

//-----------------------------------------------------------------------------------------
private macro collectOutProc ( str )
    sqlString = sqlString + "\n" + trim (str);
End;

private macro CaptureOutput
   sqlString = "";
   SetOutHandler (@collectOutProc);
End;
                     
private macro StopCaptureOutput
    SetOutHandler ();
    return sqlString;
End;
//-----------------------------------------------------------------------------------------

macro Start_vu13 ()
  var Rep, sql, repdate = {curdate};
  var rn:integer = 0, sheet:integer = 1;
  var EXCEL_MONEY = "\"# ##0"+GetLocaleInfo (0, LOCALE_SDECIMAL, IsStandAlone ())+"00\"";
  var MakeResult:bool;
  var format_line = "c:ex_B(tblr):ex_FS()";
  var format_head = "c:ex_B(tblr):ex_FS(b)";

  macro ПустаяСтрока (height:double)
    rn = rn + 1;
    if (valtype(height) != V_UNDEF)
      Rep.SetExecute("iBook.Sheets("+sheet+").Rows("+rn+").RowHeight = " + height + ";");
    end;
    Rep.AddPrintCell("", 13, null, "ex_B()");
    Rep.AddStr();
  End;

  if (not GetDate(repdate))
    return;
  end;

  Rep = CMakeReport();

  Rep.SetDefaultFontName("Times New Roman");
  Rep.SetDefaultFontSize(11);
  Rep.SetFlagShowGrid(true);

  Rep.SetExecute("iBook.Sheets("+sheet+").Columns(7).NumberFormat = "+EXCEL_MONEY+";");
  Rep.SetExecute("iBook.Sheets("+sheet+").Columns(9).NumberFormat = "+EXCEL_MONEY+";");
  Rep.SetExecute("iBook.Sheets("+sheet+").Columns(10).NumberFormat = "+EXCEL_MONEY+";");
  Rep.SetExecute("iBook.Sheets("+sheet+").Columns(12).NumberFormat = "+EXCEL_MONEY+";");
  Rep.SetExecute("iBook.Sheets("+sheet+").Columns(13).NumberFormat = "+EXCEL_MONEY+";");
  Rep.SetExecute("iBook.Sheets("+sheet+").Columns(16).NumberFormat = "+EXCEL_MONEY+";");

  ПустаяСтрока();

  rn = rn + 1;
  //Rep.SetExecute("iBook.Sheets("+sheet+").Range(\"A"+rn+":J"+rn+"\").merge;");
  Rep.SetExecute("iBook.Sheets("+sheet+").Rows("+rn+").RowHeight = " + 16 + ";");
  Rep.SetExecute("iBook.Sheets("+sheet+").Rows("+rn+").WrapText = false;");

  Rep.AddPrintCell("", 13, 0, "r:ex_B()");
  Rep.AddPrintCell("", 13, 0, "r:ex_B()");
  Rep.AddPrintCell("", 13, 0, "r:ex_B()");
  Rep.AddPrintCell("Элементы расчетной базы резервирования по операциям РЕПО за "+repdate, 13, 0, "l:ex_B():ex_FS(b):ex_FZ(12)");
  Rep.AddStr();

  ПустаяСтрока();

  rn = rn + 1;
  Rep.SetExecute("iBook.Sheets("+sheet+").Rows("+rn+").VerticalAlignment = "+ALIGN_CENTER+";");
  Rep.SetExecute("iBook.Sheets("+sheet+").Rows("+rn+").WrapText = true;");
  Rep.SetExecute("iBook.Sheets("+sheet+").Rows("+rn+").RowHeight = 152;");

  Rep.AddPrintCell("Номер сделки", 13, null, format_head);
  Rep.AddPrintCell("Наименование контрагента по сделке", 13, null, format_head);
  Rep.AddPrintCell("Наименование эмитента ценной бумаги", 13, null, format_head);
  Rep.AddPrintCell("ISIN/гос.номер бумаги", 13, null, format_head);
  Rep.AddPrintCell("Кол-во ценных бумаг по сделке", 13, null, format_head);
  Rep.AddPrintCell("Номер лицевого счета по учету ЭРБ резерв-я по п.2.8.1 Положения 283-П", 13, null, format_head);
  Rep.AddPrintCell("Сумма ЭРБ резерв-я по п.2.8.1 Положения 283-П", 13, null, format_head);
  Rep.AddPrintCell("Категория качества по ЭРБ", 13, null, format_head);
  Rep.AddPrintCell("Процент резервирования", 13, null, format_head);
  Rep.AddPrintCell("Расчетный резерв", 13, null, format_head);
  Rep.AddPrintCell("Счет по учету резерва по ЭРБ*", 13, null, format_head);
  Rep.AddPrintCell("Сумма резерва по ЭРБ**", 13, null, format_head);
  Rep.AddPrintCell("Сумма ЭРБ необеспеченной части сделки", 13, null, format_head);
  Rep.AddPrintCell("Лицо, на которое оценивается риск по необеспеченная часть сделки (заполняется значениями контрагент / эмитент)", 13, null, format_head);
  Rep.AddPrintCell("Номер счета по учету обеспеченной части сделки (счет по учету обязательств по возврату ден.ср-в)", 13, null, format_head);
  Rep.AddPrintCell("Сумма ЭРБ обеспеченной части сделки", 13, null, format_head);
  Rep.AddStr();


  CaptureOutput();
  [
   with parm as (select :1 t_dt from dual)
   select  t_dealcode
          ,t_contractorname
          ,t_issuername
          ,t_isin
          ,t_principal
          ,t_account
          ,t_sum1
          ,t_categ
          ,t_percent
          ,t_sum1*t_percent/100 t_calcreserv
          ,t_account2
          ,t_sum2
          ,t_partykind
          ,t_account3
          ,t_sum3
          ,abs(t_sum1-t_sum3) t_sum4
          ,count(1) over (partition by t_dealcode) t_dealscount
          ,sum(t_sum1) over (partition by t_dealcode) t_itog_sum1
          ,sum(t_sum1*t_percent/100) over (partition by t_dealcode) t_itog_calcreserv
          ,count(1) over (partition by t_dealcode order by t_dealcode rows between unbounded preceding and current row) t_dealscount_num
   from (select tick.t_dealcode
                ,nvl((select t_shortname from dparty_dbt where t_partyid = tick.t_partyid), chr(1)) t_contractorname
                ,(select t_shortname from dparty_dbt where t_partyid = fin.t_issuer) t_issuername
                ,(select decode(t_isin, chr(1), t_lsin, t_isin) from davoiriss_dbt where t_fiid = fin.t_fiid) t_isin
                ,case when rsb_secur.IsBasket(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) = 1
                      then nvl((select nvl(sum(t_principal), 0) from DDL_TICK_ENS_DBT where t_dealid = tick.t_dealid and t_kind = 0 and t_fiid = fin.t_fiid ), 0) - nvl((select sum(t_principal) from DDL_TICK_ENS_DBT where t_dealid = tick.t_dealid and t_kind = 1 and t_fiid = fin.t_fiid ), 0)
                      else (select t_principal from ddl_leg_dbt where t_dealid = tick.t_dealid and t_legid = 0 and t_legkind = 0)
                end t_principal
                ,mcacc.t_account
                ,abs(rsb_account.restall (mcacc.t_account, mcacc.t_chapter, mcacc.t_currency, (select t_dt from parm), 0)) t_sum1
                ,nvl ((select t_attrid from dobjatcor_dbt where t_objecttype=12 and t_groupid=13 and t_object=lpad (fin.t_fiid, 10, '0') and (select t_dt from parm) between t_validfromdate and t_validtodate), 1) t_categ
                ,nvl ((select TO_NUMBER(regexp_replace(rsb_struct.getString(t_text), '[^0-9.]+', '')) from dnotetext_dbt where t_objecttype=12 and t_notekind=3 and t_documentid=lpad (fin.t_fiid, 10, '0') and (select t_dt from parm) between t_date and t_validtodate), 0) t_percent
                ,nvl((select t_account from dmcaccdoc_dbt m where t_catnum = 242 and exists (select 1 from dmctempl_dbt where t_catid = m.t_catid and t_number = m.t_templnum and t_value1 = 19) and t_docid = tick.t_dealid /*todo: Тут связь вообще сомнительная. Надо проверить и сделать, как надо*/ ), chr(1)) t_account2
                ,nvl((select rsb_account.restall (t_account, t_chapter, t_currency, (select t_dt from parm), 0) from dmcaccdoc_dbt m where t_catnum = 242 and exists (select 1 from dmctempl_dbt where t_catid = m.t_catid and t_number = m.t_templnum and t_value1 = 19) and t_docid = tick.t_dealid /*todo: Тут связь вообще сомнительная. Надо проверить и сделать, как надо*/ ), 0) t_sum2
                ,case when exists (select 1 from dpartyown_dbt where t_partykind = 29 and t_partyid = tick.t_partyid) then 'Эмитент' else 'Контрагент' end t_partykind
                ,nvl((select t_account from dmcaccdoc_dbt where t_catnum = 602 and t_docid = mcacc.t_docid), chr(1)) t_account3
                ,nvl((select rsb_account.restall (t_account, t_chapter, t_currency, (select t_dt from parm), 0) from dmcaccdoc_dbt where t_catnum = 602 and t_docid = mcacc.t_docid), 0) t_sum3
         from ddl_tick_dbt tick
         left join ddl_tick_ens_dbt tick_ens ON tick.t_dealid = tick_ens.t_dealid
         join dfininstr_dbt fin ON fin.t_fiid = case when rsb_secur.IsBasket(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) = 1
                                                     then tick_ens.t_fiid
                                                     else tick.t_pfi
                                                end
         join dmcaccdoc_dbt mcacc ON mcacc.t_catnum in (231, 233, 1298, 1237, 1299, 1246, 1245) and
                                     mcacc.t_iscommon <> chr(88) and
                                     rsb_account.restall (mcacc.t_account, mcacc.t_chapter, mcacc.t_currency, (select t_dt from parm), 0) != 0 and
                                     mcacc.t_docid = case when mcacc.t_dockind = 4620 /*Корзина*/
                                                          then tick_ens.t_id
                                                          when mcacc.t_dockind = 176 /*Часть сделки с ЦБ*/
                                                          then (select t_id from ddl_leg_dbt where t_dealid = tick.t_dealid and t_legid = 0 and t_legkind = 0 ) /*TODO:Возможно, туд придётся доработать логику*/
                                                          when mcacc.t_dockind = 101 /*Сделка с ЦБ*/
                                                          then tick.t_dealid
                                                     end and
                                     1 = case when mcacc.t_catnum = 231 /*Для этой КУ ожидаем признак БПП*/
                                              then (select t_value4 from dmctempl_dbt where t_number = mcacc.t_templnum and t_catid = mcacc.t_catid)
                                              else 1 end
         where     RSB_SECUR.IsRepo(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) = 1
               and RSB_SECUR.IsBuy(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) = 0 /*прямое РЕПО*/
               and t_clientid = -1
               and tick.t_bofficekind = 101
               and t_dealdate <= (select t_dt from parm) --открыты ранее отчетной даты
               and ( t_closedate = to_date('01.01.0001', 'dd.mm.yyyy') or --ещё не закрытые
                     t_closedate > (select t_dt from parm)) --открытые за отчетную дату
               order by 1
         ) t
      ];
  sql = ExecSqlSelect (StopCaptureOutput (), makeArray (sqlParam ("1", repdate)));

  while (sql.MoveNext() )
    MakeResult = false;
    if (sql.value("t_dealscount") > 1)
      MakeResult = true;
    end;

    rn = rn + 1;
    Rep.SetExecute("iBook.Sheets("+sheet+").Rows("+rn+").WrapText = true;");

    Rep.AddPrintCell(sql.value("t_dealcode"),                             13, null, format_line); //Номер сделки
    Rep.AddPrintCell(sql.value("t_contractorname"),                       13, null, format_line); //Наименование контрагента по сделке
    Rep.AddPrintCell(sql.value("t_issuername"),                           13, null, format_line); //Наименование эмитента ценной бумаги
    Rep.AddPrintCell(sql.value("t_isin"),                                 13, null, format_line); //ISIN/гос.номер бумаги
    Rep.AddPrintCell(sql.value("t_principal"),                            13, null, format_line); //Кол-во ценных бумаг по сделке
    Rep.AddPrintCell(sql.value("t_account"),                              13, null, format_line); //Номер лицевого счета по учету ЭРБ резерв-я по п.2.8.1 Положения 283-П
    Rep.AddPrintCell(sql.value("t_sum1"),                                 13, null, format_line); //Сумма ЭРБ резерв-я по п.2.8.1 Положения 283-П
    Rep.AddPrintCell(sql.value("t_categ"),                                13, null, format_line); //Категория качества по ЭРБ
    Rep.AddPrintCell(sql.value("t_percent"),                              13, null, format_line); //Процент резервирования
    Rep.AddPrintCell(sql.value("t_calcreserv"),                           13, null, format_line); //Расчетный резерв
    Rep.AddPrintCell(IfThenElse(MakeResult, "", sql.value("t_account2")), 13, null, format_line); //Счет по учету резерва по ЭРБ
    Rep.AddPrintCell(IfThenElse(MakeResult, "", sql.value("t_sum2")),     13, null, format_line); //Сумма резерва по ЭРБ
    Rep.AddPrintCell(IfThenElse(MakeResult, "", sql.value("t_sum4")),     13, null, format_line); //Сумма ЭРБ необеспеченной части сделки
    Rep.AddPrintCell(sql.value("t_partykind"),                            13, null, format_line); //Лицо, на которое оценивается риск по необеспеченная часть сделки (заполняется значениями контрагент / эмитент
    Rep.AddPrintCell(IfThenElse(MakeResult, "", sql.value("t_account3")), 13, null, format_line); //Номер счета по учету обеспеченной части сделки (счет по учету обязательств по возврату ден.ср-в
    Rep.AddPrintCell(IfThenElse(MakeResult, "", sql.value("t_sum3")),     13, null, format_line); //Сумма ЭРБ обеспеченной части сделки
    Rep.AddStr();

    if (MakeResult and (sql.value("t_dealscount") == sql.value("t_dealscount_num")))
      rn = rn + 1;
      Rep.SetExecute("iBook.Sheets("+sheet+").Rows("+rn+").WrapText = true;");

      Rep.AddPrintCell("Итог по сделке",               13, null, format_head); //Номер сделки
      Rep.AddPrintCell("",                             13, null, format_head); //Наименование контрагента по сделке
      Rep.AddPrintCell("",                             13, null, format_head); //Наименование эмитента ценной бумаги
      Rep.AddPrintCell("",                             13, null, format_head); //ISIN/гос.номер бумаги
      Rep.AddPrintCell("",                             13, null, format_head); //Кол-во ценных бумаг по сделке
      Rep.AddPrintCell("",                             13, null, format_head); //Номер лицевого счета по учету ЭРБ резерв-я по п.2.8.1 Положения 283-П
      Rep.AddPrintCell(sql.value("t_itog_sum1"),       13, null, format_head); //Сумма ЭРБ резерв-я по п.2.8.1 Положения 283-П
      Rep.AddPrintCell("",                             13, null, format_head); //Категория качества по ЭРБ
      Rep.AddPrintCell("",                             13, null, format_head); //Процент резервирования
      Rep.AddPrintCell(sql.value("t_itog_calcreserv"), 13, null, format_head); //Расчетный резерв
      Rep.AddPrintCell(sql.value("t_account2"),        13, null, format_head); //Счет по учету резерва по ЭРБ
      Rep.AddPrintCell(sql.value("t_sum2"),            13, null, format_head); //Сумма резерва по ЭРБ
      Rep.AddPrintCell(sql.value("t_sum4"),            13, null, format_head); //Сумма ЭРБ необеспеченной части сделки
      Rep.AddPrintCell("",                             13, null, format_head); //Лицо, на которое оценивается риск по необеспеченная часть сделки (заполняется значениями контрагент / эмитент
      Rep.AddPrintCell(sql.value("t_account3"),        13, null, format_head); //Номер счета по учету обеспеченной части сделки (счет по учету обязательств по возврату ден.ср-в
      Rep.AddPrintCell(sql.value("t_sum3"),            13, null, format_head); //Сумма ЭРБ обеспеченной части сделки
      Rep.AddStr();
    end;
  end;

  Rep.SetFileName("ВУ13_"+repdate);
  Rep.PrintWinRep("ВУ13 (Резервы РЕПО сд)");
  Rep.SetZoomType(ZOOM_TYPE_A4L);
  Rep.SetWinRepOutput();
  Rep.ShowWinRep();
End;

Start_vu13();
exit(1);