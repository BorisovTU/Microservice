/**
 @file f899_1_7.mac
 @brief Отчет "Реестр квал. инвесторов"
 
 Файл содержит функционал создания отчета "Реестр квал. инвесторов"
 
  # tag
 - functional_block:Отчетность_Клиент
 - code_type:Report
 - Ценные бумаги
 - Квалификация
 - КИ
 - Квалифицированный инвестор
 
  # menu
  БО ЦБ\Отчеты\РСХБ\Реестр квал. инвесторов
  
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |25.01.2024 |Топорков Д.В.|BIQ-16875 BOSS-194 BOSS-1778                              | Добавление колонки Дата подтв. статуса КИ - для ЮЛ
 |xx.xx.20xx |Дорохов А.Н. |                                                          | Создание
 */

import RsbFormsInter, Global, Календарь, or_rep_h, PTInter, or_tpl_h;
import "dbo_qi_func.mac";
import oralib, likepy, dlquery;



const FI_KIND_NOTE = 301; // Номер примечания "ФИ в отношении уоторых субъект признан квал. инвестором" для субъекта
//Значение для вывода в отчет при отсутствии заполненного примечания на субъекте:
const FI_NOTE_KIND_DEF = "В отношении акций, облигаций, ПИФ, иных ценных бумаг и Финансовых инструментов, " +
                         "а также в отношении банковских продуктов предназначенных для квалифицированных " +
                         "инвесторов в соответствии с Российским законодательством.";

const RUSSIAN_PASSPORT = 0;
const FOREIGN_PASSPORT = 11;
const RESIDENCE_PERMIT = 5; // Вид на жительство GAA:503893

/**
@brief Функция IIF
*/
private macro IIF(a, b, c)
  if (a)
    return b;
  end;
  
  return c;
end;

/**
@brief Функция проверки значения
*/
macro CheckValue(val:variant)
  private CONST V_SPECVAL = 26;

  if(ValType(val) == V_UNDEF)
    return "";
  end;

  if(ValType(val) == V_SPECVAL)
    return "";
  end;

  if(trim(string(val)) == "")
    return "";
  end;

  if(ValType(val) == V_DOUBLE)
    return Money(val);
  end;

  return String(val);
end;

/**
@brief Функция получения наименования документа по его виду
*/
macro GetDocName (doctype)
  var sql = ExecSqlSelect("select t_name from dpaprkind_dbt where t_paperkind = :t", MakeArray(SqlParam("t", doctype)), false);
  
  if (sql.MoveNext() ) 
    return sql.value("t_name");
  end;
  
  return "";
end;

/**
@brief Функция получения списка договоров
*/
private macro GetContrInfo(partyID: Integer): String
  var result: String = "";

  var rs = ExecSqlSelect("SELECT sf.t_number, " + 
                                 "REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(TO_CHAR(sf.t_dateBegin, 'DD month YYYY', 'NLS_DATE_LANGUAGE = RUSSIAN'), 'ь', 'я'), 'й', 'я'), 'т', 'та'), 'тая', 'тя'), '   ',  ' ') dateConverted " + 
                           "FROM dSfContr_dbt sf, dDlContr_dbt dl " +
                          "WHERE sf.T_PARTYID = :partyID " +
                            "AND sf.t_servkind = 0 " +
                            "AND dl.t_sfcontrid = sf.t_id " +
                            "AND EXISTS(SELECT 1 " +
                                         "FROM dObjAtCor_dbt ob " +
                                        "WHERE ob.t_objecttype = 207 " +
                                          "AND ob.t_groupid = 140 " +
                                          "AND ob.t_object IN (LPAD (dl.t_DlContrID, 34, '0')) " +
                                          "AND t_attrid = 2)",
           MakeArray(SqlParam("partyID", partyID)), false);
  
  while (rs.MoveNext())
    result = result + rs.Value("t_number") + "\n  "+ rs.Value("dateConverted") + " .; \n";
  end;
  
  return result;
end;

/**
@brief Функция поиска вида документа
*/
private macro GetPaperKind (partyid)
  var sql = " select t_paperkind kind from dpersnidc_dbt "
          + "where t_isnotvalid <> 'X' and t_personid = :partyid and rownum = 1 "
          + "order by t_paperkind ";
         
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("partyid", partyid)), false);
  
  if (sql.MoveNext() ) 
    return int(sql.value("kind"));
  end;
  
  return -1;
end;

/**
@brief Функция поиска документа "Вид на жительство"
*/
private macro GetPaperKind_ResidencePermit (PartyID)
  var sql = " select count(*) count from dpersnidc_dbt "
          + "where  t_personid = :partyid and t_paperkind  = "+RESIDENCE_PERMIT;
         
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("partyid", partyid)), false);
  
  if (sql.MoveNext() ) 
    return int(sql.value("count"));
  end;
  
  return -1;
end;

/**
@brief Функция получения параметров для субъекта (документ, адрес, fi_kind)
*/
private macro GetPartyAttr (PartyID:integer, pdate:date, doc:@string, Addr:@string, Fi_Kind:@string);
  var Party = RsbParty(PartyID);
  var PartyAddress;
  var day = "", mm = "", year = "", RegDoc, doctype;
  var docname: String = "";
  var docseries: String = "";
  var docnumber: String = "";
  var docissuer: String = "";
  var dociscode: String = "";
  var docisDate: String = "";
  var legaladdr: String = "";
  var realaddr: String  = "";
  var INN: String       = "";
  var Foreign_INN: String = "";    
  var PtNote : RsbPtNote;
  var doctype_residencepermit = 0; // Вид на жительство GAA: 503893 

  doc = "";
  Addr = "";
  FI_Kind = ""; 

  PtNote = Party.PartyNote();
  FI_kind = PtNote.ReadNote(FI_KIND_NOTE, pdate);
  
  if (not StrLen(FI_kind))
    FI_kind = FI_NOTE_KIND_DEF;
  end;
  
  if (Party.LegalForm == PTLEGF_INST) // юр. лицо
    Foreign_INN = ПолучитьКодСубъекта(PartyID, PTCK_FOREIGN_INN);
    if (Foreign_INN)
      doc = doc + "Код ин. орг.: " + Foreign_INN + "; ";
    end;
   
    INN = ПолучитьКодСубъекта(PartyID, PTCK_INN);
    if (INN)
      if(Foreign_INN) 
        doc = doc + " ";
      end;
      
      doc = doc + "ИНН: " + INN + "; ";
    end;

    PartyAddress = Party.Address(PTADDR_LEGAL);
    legaladdr = PartyAddress.Address;
    if (StrLen(legaladdr))
       Addr = " Юридический адрес: " + Legaladdr;
    end;

    PartyAddress = Party.Address(PTADDR_REAL);
    realaddr = PartyAddress.Address;
    if (StrLen(realaddr))
       if(StrLen(legaladdr))
         Addr = Addr + " ";
       end;
       
       Addr = Addr + " Фактический адрес: " + realaddr;
    end;
  elif (Party.LegalForm == PTLEGF_PERSN) // физ. лицо
    PartyAddress = Party.Address(PTADDR_LEGAL);
    legaladdr = PartyAddress.Address;

    PartyAddress = Party.Address(PTADDR_REAL);
    realaddr = PartyAddress.Address;
    
    if (StrLen(legaladdr))
       Addr = " Адрес регистрации: " + Legaladdr;
    end;

    if (StrLen(realaddr))
       if(StrLen(legaladdr))
         Addr = Addr + " ";
       end;
       Addr = Addr + " Фактический адрес: " + realaddr;
    end;
    
    if (Party.BirthDate == Date(0, 0, 0))
      Party.BirthDate = Date(01, 01, 1901);
    end;

    INN = ПолучитьКодСубъекта(PartyID, PTCK_INN);
    if (INN)
      doc = doc + "ИНН: " + INN + " ";
    end;

    // документ, удостоверяющий личность
    doctype = GetPaperKind (PartyID);
    
    if (doctype >= 0) 
      if (Party.isnotresident) 
        doctype = FOREIGN_PASSPORT;
      else 
        // Если вид ДУЛ != RUSSIAN_PASSPORT, проверить наличие вида на жительство и в случае наличия выводить в отчет информацию по иностр. паспорту GAA: 503893
        if(doctype != RUSSIAN_PASSPORT)
           doctype_residencepermit =  GetPaperKind_ResidencePermit (PartyID);
           if (doctype_residencepermit > 0)
              doctype = FOREIGN_PASSPORT;
           else
              doctype = RUSSIAN_PASSPORT;
           end;
        end;
      end;
           
      RegDoc = Party.PersonPaper(doctype);
      
      DateSplit(RegDoc.IssuedDate, day, mm, year);
      docseries = RegDoc.Series;
      docnumber = RegDoc.Number;
      docissuer = CheckValue(RegDoc.Issuer);
      dociscode = CheckValue(RegDoc.IssuerCode);
      docisDate = CheckValue(string(RegDoc.IssuedDate));

      if ((docnumber != "") or (docissuer != "") or (dociscode != "") or (docisDate != ""))
        docname = GetDocName(doctype);
      end;
    end;

    docname   = IIF(docname   != "", docname, "паспорт ");
    docseries = IIF(docseries != "", docseries, " ");
    docnumber = IIF(docnumber != "", docnumber, " ");
      
    doc = doc + docname + " " + docseries + " " + docnumber + " " + dociscode + " " ;
    doc = doc + IIF(docissuer != "", " Выдан: " + docissuer + "", " ");
    doc = doc + IIF(docisDate != "", "  Дата выдачи: " + docisDate + "", " ");
    doc = doc + IIF(docisCode != "", "  Код подр.: " + docisCode + "", " ");
  end;   
end;



private macro findObj(str, flag) 
  var str2 = ". ";
if( flag == 1)
  str2 = " "; 
end;

  var i = 1;
  while(i != 0 )
    var l1 = " ";
    var l2 = " ";
      if( index(str, ";") != 0)
            l1 = substr(str, 1, index(str, ";") - 1);
            l2 = substr(str, index(str, ";") + 1);
            str = l1 + str2 + l2;
       else 
          i = 0;
      end;
  end;
return str;
end;


/**
@brief Класс формирования отчета
*/
class PrintRep(pDAte)

  var vDAte   = pDAte;
    var Num = 0;
  var csvFile;
  var csvTable;
  var printEngine;
  var fname;
  private macro ПечатьБлокаДанных(_rs);
    var doc, addr, fi_kind;
    var contrInfo: String;
    if(ValType(_rs) != v_undef)

      while(_rs.MoveNext())
        doc = ""; addr = "";


        GetPartyAttr(_rs.value("t_partyid"),this.vdate, @doc, @addr, @fi_kind);
        
        
        csvFile.AddCell(findObj(_rs.value("t_number"))); 
        csvFile.AddCell( findObj(_rs.value("t_name")));

        if(not GetRegValueSwitchQI())//NEW PARAM

          contrInfo = _rs.value("t_sfcontrQI");
          if (contrInfo == "limit")
            contrInfo = GetContrInfo(_rs.value("t_partyid"));
          end;
          csvFile.AddCell(findObj(contrInfo, 1));
        else 
        end;
        
        csvFile.AddCell( findObj(addr));
        csvFile.AddCell( findObj(doc));  
        csvFile.AddCell( findObj(_rs.value("t_regdate")));  
        csvFile.AddCell( findObj(fi_kind));
        csvFile.AddCell( findObj(_rs.value("t_changedate")));
        csvFile.AddCell( findObj(_rs.value("T_cause")));
        csvFile.AddCell( findObj(_rs.value("t_stateconfirmationdate")));
        csvFile.AddCell(findObj(_rs.value("t_excdate")));
        csvFile.AddCell( findObj(_rs.value("T_cause"))); 
        csvFile.AddRow();
        UseProgress( Num = Num + 1 );
      end;
    end;
  end;

  /**
  @brief Процедура запуска формирования отчета
  */
  macro run()
    var SQL, cmd, rs;
    printEngine = CTemplateSXLSX(NULL);

    if(printEngine.UsePoi())
      printEngine.SetXLSXFormat();
    end;

    printEngine.CreateTotalBook();
    if(not GetRegValueSwitchQI())//NEW PARAM
      printEngine.openTemplate("userQualiInvestorList_template.xlsx", true);
    else 
      printEngine.openTemplate("userQualiInvestorList_template_withoutNumber.xlsx", true);
    end;
    printEngine.SheetChange(1);
    csvTable = printEngine.RegisterTable("templateTable", "templateTableHeader", true);
    csvFile  = C_CSVFile();

    if (printEngine.UsePoi())
        csvFile.SetLimitRow(FLUSH_COUNT_XLSX);
    end;

    fName = csvFile.CreateFullFileName("userQualiInvestorList_csv.txt");
    csvFile.FileOpen(fName, true);
    printEngine.CopyAllSheetInTotalBook(null, false, "templateHeader", 0, "Реестр квал. инвесторов");

    SQL = "       SELECT ROW_NUMBER () OVER ( order by to_date(t.t_regdate,'dd.mm.yyyy') ) t_number,t.* ";
    SQL = SQL + " FROM ( SELECT qinv.t_partyid, "                                                      ;
    SQL = SQL + "         NVL (qinv.t_code, CHR (0)) t_code, "                                         ;
    SQL = SQL + "         P.T_NAME "                                                                   ;
    SQL = SQL + "         || CASE "                                                                    ;
    SQL = SQL + "               WHEN    p.t_shortname IS NOT NULL "                                    ;
    SQL = SQL + "                    OR p.t_shortname != CHR (0) "                                     ;
    SQL = SQL + "                    OR p.t_shortname != CHR (1) "                                     ;
    SQL = SQL + "               THEN "                                                                 ;
    SQL = SQL + "                  ' (' || p.t_shortname || ')' "                                      ;
    SQL = SQL + "               ELSE "                                                                 ;
    SQL = SQL + "                  '' "                                                                ;
    SQL = SQL + "            END "                                                                     ;
    SQL = SQL + "            t_name, "                                                                 ;

    if(not GetRegValueSwitchQI())//NEW PARAM
      SQL = SQL + "     CASE WHEN (SELECT COUNT(1) FROM dsfcontr_dbt sf "                                ;
      SQL = SQL + "                 WHERE sf.t_partyid = qinv.t_partyid AND sf.t_servkind = 0) < 100 "   ;
      SQL = SQL + "          THEN NVL ( "                                                                      ;
      SQL = SQL + "            (SELECT TRIM ( "                                                          ;
      SQL = SQL + "                       RTRIM ( "                                                      ;
      SQL = SQL + "                          XMLAGG ( "                                                  ;
      SQL = SQL + "                             XMLELEMENT ( "                                           ;
      SQL = SQL + "                                E, "                                                  ;
      SQL = SQL + "                                t_number || ' \nот ' "                                ;
      SQL = SQL + "                                || REPLACE ( "                                        ;
      SQL = SQL + "                                      REPLACE ( "                                     ;
      SQL = SQL + "                                         REPLACE ( "                                  ;
      SQL = SQL + "                                            REPLACE ( "                               ;
      SQL = SQL + "                                               REPLACE ( "                            ;
      SQL = SQL + "                                                  TO_CHAR ( "                         ;
      SQL = SQL + "                                                     t_datebegin, "                   ;
      SQL = SQL + "                                                     'DD month YYYY', "               ;
      SQL = SQL + "                                                     'NLS_DATE_LANGUAGE = RUSSIAN'), ";
      SQL = SQL + "                                                  'ь', "                              ;
      SQL = SQL + "                                                  'я'), "                             ;
      SQL = SQL + "                                               'й', "                                 ;
      SQL = SQL + "                                               'я'), "                                ;
      SQL = SQL + "                                            'т', "                                    ;
      SQL = SQL + "                                            'та'), "                                  ;
      SQL = SQL + "                                         'тая', "                                     ;
      SQL = SQL + "                                         'тя'), "                                     ;
      SQL = SQL + "                                      '   ', "                                        ;
      SQL = SQL + "                                      ' ') "                                          ;
      SQL = SQL + "                                || ' г.', "                                           ;
      SQL = SQL + "                                ';\n').EXTRACT ('//text()')), "                       ;
      SQL = SQL + "                          '; ')) "                                                    ;
      SQL = SQL + "               FROM dsfcontr_dbt sf, ddlcontr_dbt dl "                                ;
      SQL = SQL + "              WHERE     sf.T_PARTYID = qinv.t_partyid "                               ;
      SQL = SQL + "                    AND sf.t_servkind = 0 "                                           ;
      SQL = SQL + "                    AND dl.t_sfcontrid = sf.t_id "                                    ;
      SQL = SQL + "                    AND exists(SELECT ob.* "                                          ;
      SQL = SQL + "  FROM dobjatcor_dbt ob "                                                             ;
      SQL = SQL + " WHERE     ob.t_objecttype = 207 "                                                    ;
      SQL = SQL + "       AND ob.t_groupid = 140 "                                                       ;
      SQL = SQL + "       AND ob.t_object IN (LPAD (dl.t_DlContrID, 34, '0')) "                          ;
      SQL = SQL + "       AND t_attrid = 2)        ), "                                                  ;
      SQL = SQL + "            CHR (0)) "                                                                ;
      SQL = SQL + "   ELSE 'limit' END "                                                                 ;
      SQL = SQL + "            t_sfcontrQI, "                                                            ;
    end;
    
    SQL = SQL + "         NVL (TO_CHAR (qinv.t_regdate, 'dd.mm.yyyy'), CHR (0)) t_regdate, "           ;
    SQL = SQL + "         NVL ( "                                                                      ;
    SQL = SQL + "            (SELECT TRIM ( "                                                          ;
    SQL = SQL + "                       RTRIM ( "                                                      ;
    SQL = SQL + "                          XMLAGG ( "                                                  ;
    SQL = SQL + "                             XMLELEMENT (e, avrkind.t_name, '; '). "                  ;
    SQL = SQL + "                              EXTRACT ('//text()')), "                                ;
    SQL = SQL + "                          '; ')) "                                                    ;
    SQL = SQL + "               FROM    dscqinvfi_dbt qinvfi "                                         ;
    SQL = SQL + "                    LEFT JOIN "                                                       ;
    SQL = SQL + "                       davrkinds_dbt avrkind "                                        ;
    SQL = SQL + "                    ON avrkind.t_fi_kind = 2 "                                        ;
    SQL = SQL + "                       AND qinvfi.t_avoirkind = avrkind.t_avoirkind "                 ;
    SQL = SQL + "              WHERE QINVFI.T_PARTYID = qinv.t_partyid), "                             ;
    SQL = SQL + "            CHR (0)) "                                                                ;
    SQL = SQL + "            t_fi_kinds, "                                                             ;
    SQL = SQL + "         CASE "                                                                       ;
    SQL = SQL + "            WHEN qinv.t_state = 0 "                                                   ;
    SQL = SQL + "                 AND (qinv.t_changedate > qinv.t_regdate "                            ;
    SQL = SQL + "                      AND qinv.t_changedate <= :dt1) "                                ;
    SQL = SQL + "            THEN "                                                                    ;
    SQL = SQL + "               NVL (TO_CHAR (qinv.t_changedate, 'dd.mm.yyyy'), CHR (0)) "             ;
    SQL = SQL + "            ELSE "                                                                    ;
    SQL = SQL + "               CHR (0) "                                                              ;
    SQL = SQL + "         END "                                                                        ;
    SQL = SQL + "            t_excdate, "                                                              ;
    SQL = SQL + "         CASE "                                                                       ;
    SQL = SQL + "            WHEN qinv.t_state = 0 "                                                   ;
    SQL = SQL + "                 AND (qinv.t_changedate > qinv.t_regdate "                            ;
    SQL = SQL + "                      AND qinv.t_changedate <= :dt1) "                                ;
    SQL = SQL + "            THEN "                                                                    ;
    SQL = SQL + "               NVL (QINV.T_CAUSE, CHR (0)) "                                          ;
    SQL = SQL + "            ELSE "                                                                    ;
    SQL = SQL + "               CHR (0) "                                                              ;
    SQL = SQL + "         END "                                                                        ;
    SQL = SQL + "            t_cause, "                                                                ;
    SQL = SQL + "         CASE "                                                                       ;
    SQL = SQL + "            WHEN qinv.t_state = 0 "                                                   ;
    SQL = SQL + "                 AND (qinv.t_changedate > qinv.t_regdate "                            ;
    SQL = SQL + "                      AND qinv.t_changedate <= :dt1) "                                ;
    SQL = SQL + "            THEN "                                                                    ;
    SQL = SQL + "               NVL (TO_CHAR (qinv.t_changedate, 'dd.mm.yyyy'), CHR (0)) "             ; // "Дата внесения изменений в реестр" PDV 520193
    SQL = SQL + "            ELSE "                                                                    ;
    SQL = SQL + "               CHR (0) "                                                              ;
    SQL = SQL + "         END "                                                                        ;
    SQL = SQL + "            t_changedate, "                                                           ;
    SQL = SQL + "         CASE "                                                                       ;
    SQL = SQL + "            WHEN qinv.t_state = 1 "                                                   ;
    SQL = SQL + "                 AND p.t_legalForm = 1 "                                              ;
    SQL = SQL + "            THEN "                                                                    ;
    SQL = SQL + "                 CASE "                                                               ;
    SQL = SQL + "                    WHEN qinv.t_changedate > to_date('01.01.0001', 'DD.MM.YYYY') AND ";
    SQL = SQL + "                         qinv.t_changedate <= :dt1 "                                  ;
    SQL = SQL + "                    THEN "                                                            ;
    SQL = SQL + "                       NVL (TO_CHAR (qinv.t_changedate, 'dd.mm.yyyy'), CHR (0)) "     ;
    SQL = SQL + "                    WHEN qinv.t_regdate <= :dt1 "                                     ;
    SQL = SQL + "                    THEN "                                                            ;
    SQL = SQL + "                       NVL (TO_CHAR (qinv.t_regdate, 'dd.mm.yyyy'), CHR (0)) "        ;
    SQL = SQL + "                    ELSE "                                                            ;
    SQL = SQL + "                       CHR (0) "                                                      ;
    SQL = SQL + "                 END "                                                                ;
    SQL = SQL + "            ELSE "                                                                    ;
    SQL = SQL + "               CHR (0) "                                                              ;
    SQL = SQL + "         END "                                                                        ;
    SQL = SQL + "            t_stateconfirmationdate "                                                 ;
    SQL = SQL + "    FROM    dscqinv_dbt qinv "                                                        ;
    SQL = SQL + "         LEFT JOIN "                                                                  ;
    SQL = SQL + "            dparty_dbt p "                                                            ;
    SQL = SQL + "         ON qinv.t_partyid = p.t_partyid "                                            ;
    
    if(GetRegValueSwitchQI())//NEW PARAM
      SQL = SQL + "        WHERE EXISTS (SELECT 1 "                                                      ;
      SQL = SQL + "                        FROM dsfcontr_dbt sf  "                                       ;
      SQL = SQL + "                       WHERE sf.t_partyid = p.t_partyid  "                            ;
      SQL = SQL + "                         AND sf.t_servkind = 0  "                                     ;
      SQL = SQL + "                         AND (sf.t_dateclose = to_date('01.01.0001','dd.mm.yyyy') "   ;                                          
      SQL = SQL + "                           OR sf.t_dateclose >= :dt1)) "                              ;
    end;

    SQL = SQL + "ORDER BY qinv.t_regdate, qinv.t_partyid) t "                                          ; // Добавил сортировку по дате Golovkin 18.07.2019 ID : 489269 (IM2871416)
    SQL = SQL + "ORDER BY to_date(t.t_regdate,'dd.mm.yyyy') asc, t.t_partyid "                         ; 

    
    cmd = DL_RSDCommand( SQL, false /*NullConversion*/ );
    cmd.addparam( this.vDate );
    var iCount = cmd.GetCount();

    if( iCount > 0 )
        InitProgress( iCount, "", "Вывод клиентов в реестр ");
    end;

    BegAction(20, "Сбор данных...");
    rs = cmd.Execute( SQL ); 
    EndAction();
    ПечатьБлокаДанных(rs);
    RemProgress();

    csvFile.FileClose();
    csvTable.FillTabelFromCSV(csvFile.GetlsFileName());
    csvTable.EndTable();
    printEngine.CopyAllSheetInTotalBook (null, false, csvTable.GetTableRange(), 0, "Реестр квал. инвесторов");
    printEngine.Close();
    printEngine.SaveTotalBook(null, false);
    printEngine.SaveAsTotalbook ("Реестр квал. инвесторов_ " + {curdate}, true);
  end;
  
end; // class PrintRep

/**
@brief Класс панели параметров отчета
*/
class (TRsbPanel) Pan()
  initTRsbPanel();

  const Delta = 50;

  const K_Enter = 13;
  const K_F3    = 317;
  const K_F2    = 316;

  const BeginCalcDate_ID = 1;

  var curstr = 1;
  Caption = "Реестр квалифицированных инвесторов.";
  Status = "~ESC~Выход ~F2~Выполнить";
  SetSize(37,5);
  SetPosition(30,10);
  var BeginCalcDate;
    
  curstr = curstr + 1;
  BeginCalcDate = TRsbEditField(V_DATE);
  BeginCalcDate.setPosition(16, curstr);
  BeginCalcDate.setSize(8, 1);
  BeginCalcDate.name = "begincalcdate";
  BeginCalcDate.value = {curdate};
  AddControl(BeginCalcDate);
    
  var m_LabelCD: TRsbLabel = TRsbLabel(2, curstr, "Отчетная дата:");
  AddLabel(m_LabelCD);

  AddEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "onKeyPressed"));
    
  macro onKeyPressed(ev)
    if (ev.KeyCode == K_F3)
      if (ev.id == BeginCalcDate_ID)
        BeginCalcDate.value = GetDateByCalendar({CurDate});
        this.Redraw(); 
      end;
    elif (ev.KeyCode == K_Enter)
      this.Redraw(); 
    elif(ev.keyCode == K_F2)
      var myRep = PrintRep(BeginCalcDate.value);
      myRep.Run();
      msgbox("Отчет сформирован !");
    end;
  end; 
end;

// Точка входа в макрос
var myPanel:TRsbPanel = Pan();

myPanel.run();

exit(1);