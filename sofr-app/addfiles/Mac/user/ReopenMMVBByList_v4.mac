/*
$Name:           ReopenMMVBByList_v4.mac
$Module:         БО ЦБ
$Description:    Макрос открытия ранее закрытой площадки на ММВБ
*/

import RSD;
import OprInter, PTInter, PaymInter, CTInter, SPInter, sp_class, "dpimp.mac" , "spRepFun.mac";
import RsbDataSet;
import sfinter, rsd, sp_car, activex, or_rep_h;
import "dlcontrmsg.mac";

// -------------------------------------------------------------------------
// Поиск ID ДБО по его номеру
// [in]      DBO_NUM - номер ДБО
// [in,out]  DBO_ID  - ID ДБО
// -------------------------------------------------------------------------
private macro FindDBO(DBO_NUM:string, DBO_ID:@integer):integer 
  var sqls = String( "select dlc.t_dlcontrid   \n"
                    ,"  from ddlcontr_dbt dlc, dsfcontr_dbt sfc   \n"
                    ," where sfc.t_id = dlc.t_sfcontrid   \n"
                    ,"   and sfc.t_number = ?   and sfc.t_dateclose = to_date('01010001','ddmmyyyy')\n");
  var cmd = RSDCommand(sqls);
  cmd.AddParam("", RSDBP_IN, DBO_NUM);
  var rs = RSDRecordset(cmd, 1, 1);
    if(rs.movenext())
      DBO_ID = rs.value("t_dlcontrid");
      return 1;
    end;
  return 0;
end;

// -------------------------------------------------------------------------
// Поиск последнего онлайн сообщения о регистрации на ММВБ и переворд его в статус 103	Ошибка вызова серв получ статуса заявки
// [in]      dboid - ID ДБО
// [in,out]  MsgID  - ID сообщения
// -------------------------------------------------------------------------
private macro FindResendMSG(dboid:integer, MsgID:@integer):integer
  var sqls = String("select max(t_id)as t_id   \n"
                   ,"  from ddlcontrmsg_dbt   \n"
                   ," where t_dlcontrid = ?   \n"
                   ,"   and t_marketid = 2   \n"
                   ,"   and t_kind = 1  and T_ISONLINESENDMES = chr(88) \n");
  var cmd = RSDCommand(sqls);
  cmd.addParam("", RSDBP_IN, dboid);
  var rs = RSDRecordset(cmd, 1,1);
    if(rs.movenext())
      MsgID = rs.value("t_id");
    else
      return 0;
    end;

  cmd =RSDCommand(" update ddlcontrmsg_dbt set t_sendmesstate = 103 where t_id = ?");
  cmd.AddParam("", rsdbp_in, MsgID);
  cmd.execute();
  return 1;
  onerror(er)
    return 0;
end;

private macro ResendMSG(MsgID:integer):integer
  return DlRepeatSendWithAddCode(MsgID);
  onerror(er)
    return 1;
end;

var err :integer   = 0; 

var FullNameFile:string = "", 
  pFileName:string = "", 
  ExtName:string = "", 
  DirName:string = "", 
  Sheet:object = Null, 
  i:integer = 1, 
  j:integer = 1, 
  SF_NUM:string = "", 
  InputDir:string = "", 
  dboid:integer = 0;

  InputDir = "..\\Import\\";
  if(not SelectFile(FullNameFile, InputDir + "CheckMMVB_*.xls*", "Выберите файл для загрузки", 0, TRUE))
    MsgBox("Отказ от загрузки");
    return FALSE;
  else
    SplitFile(FullNameFile, pFileName, ExtName);
    pFileName = pFileName + ExtName;
    PrintLn ("Обработка файла: " + pFileName);
    DirName = SubStr(FullNameFile, 1, Index(FullNameFile, pFileName) - 1);

    if(not OpenExcelFile(pFileName, false, TRUE, DirName))
      MsgBox("Ошибка открытия файла \"" + FullNameFile + "\"");
      return FALSE;
    end;

    j = 1;
    while(j <= ExcelApplication.Sheets.Count)

      Sheet = ExcelApplication.Sheets(j);
      i = 1;

        while (ValType(Sheet.Cells(i, 1).Value) != V_UNDEF)

          SF_NUM = string(Sheet.Cells(i, 1).Value:0:0);
          dboid = 0;
          if (FindDBO(SF_NUM, @dboid))
            [Обработка договора: #############  : #################################################################################################]( SF_NUM,"Договор найден");
            err = 0;
          else 
            err = 1;
          end;


          var MsgID = 0;
          if(not err)
            if(not FindResendMSG(dboid, @MsgID))
              [                    #############  : #################################################################################################]( SF_NUM,"Ошибка. Не найдено сообщение для переотправки");
              err = 1;
            else
              err = ResendMSG(MsgID);
              if(err)
                [                    #############  : #################################################################################################]( SF_NUM,"Ошибка отправки сообщения");
              else
                [                    #############  : #################################################################################################]( SF_NUM,"ОК. Переотправка сообщений на биржу");
              end;
            end;
          end;

          [                    #############  : #################################################################################################]( SF_NUM,"Обработка завершена");

          i = i + 1;
        end;
        j = j+1;
    end;

  end; 
  




