/*
$Name:         vtb_LoadLot2.mac
$Module:       Ценные бумаги
$Description:  Депозитарные зачисления - загрузка из *.csv
*/

/*----------------------------------------------------------*/
/* Формат:
   RecNo|Client|DogBO|Market|DogDEPO|ISIN|RegNumber|Qty|Coef|Prec|OperType|ResourceSOFR|ResourceDEPO|TimeStamp|Descr|ClientType|GUID|PRICE|ISRELATIVE|CURPRICE|NKD|CMS|REPLACEMENT|GUIDLNK
*/  
/*                                                          */
/* Автор: Белохонов П.                                      */
/*----------------------------------------------------------*/
import rsd, rsexts, dlcontrfunc, rsbformsinter;
import "usr_open_files.mac";
import BankInter, Fiinter, SPInter;
import "vtb_createenroll.mac";
import "loadRUData.mac";

private CONST ENCODE = "rsansi"; //"utf8" "rsoem";
private CONST OUTDIR = "..\\Export"; 

private const NOTEKIND_LINKED_DEALID = 37; //Примечание "Номер связанной сделки"
private const NOTEKIND_GUID = 404; //Примечание "GUID депозитарного зачисления"

private const CHECK_GUID = true; //проверка на уникальность по гуид

//private const UPDATE_REST = true; //проверка на уникальность по гуид

private CONST FN_RECID       ="RECNO";
private CONST FN_CLIENT      ="CLIENT";
private CONST FN_CONTRNUM    ="DOGBO";
private CONST FN_CONTRMP     ="MARKET";
private CONST FN_DEPONUM     ="DOGDEPO";
private CONST FN_ISIN        ="ISIN";
private CONST FN_LSIN        ="REGNUMBER";
private CONST FN_AMNT        ="QTY";
private CONST FN_SCALE       ="COEF";
private CONST FN_POINT       ="PREC";
private CONST FN_INOUT       ="OPERTYPE";
private CONST FN_INACC       ="RESOURCESOFR";
private CONST FN_DIASACC     ="RESOURCEDEPO";
private CONST FN_TM          ="TIMESTAMP";
private CONST FN_GROUND      ="DESCR";
private CONST FN_ISVTB       ="CLIENTTYPE";
private CONST FN_GUID        ="GUID";
private CONST FN_PRICE       ="PRICE";
private CONST FN_ISRELATIVE  ="ISRELATIVE";
private CONST FN_CURPRICE    ="CURPRICE";
private CONST FN_NKD         ="NKD";
private CONST FN_CMS         ="CMS";
private CONST FN_REPLACEMENT ="REPLACEMENT";
private CONST FN_GUIDLNK     ="GUIDLNK";

private var _MMVB_ID = MMVB_ID();
private var _SPB_ID = SPB_ID();

private class WrtEnrLinks()

   private macro nvl(value, defaultValue)
      if (value == null) 
         return defaultValue;
      end;
      return value;
   end;

   private macro Insert(dealIDWrt:integer, guidWrt:string, dealIDenr:integer, guidEnr:string)
      var params = TArray();
      var query = "insert into uddl_wrtenr_links (link_id, "
                + "                               wrtoff_deal_id, "
                + "                               wrtoff_guid, "
                + "                               enroll_deal_id, "
                + "                               enroll_guid) "
                + "values (uddl_wrtenr_links_seq.nextval, "
                + "        nullif(:dealIDWrt, 0), "
                + "        nullif(:guidWrt,   chr(1)), "
                + "        nullif(:dealIDenr, 0), "
                + "        nullif(:guidEnr,   chr(1))) ";
      params(params.Size()) = SqlParam("dealIDWrt", nvl(dealIDWrt, 0));
      params(params.Size()) = SqlParam("guidWrt",   guidWrt);
      params(params.Size()) = SqlParam("dealIDenr", nvl(dealIDenr, 0));
      params(params.Size()) = SqlParam("guidEnr",   guidEnr);

      ExecSql(query, params);
   end;

   private macro GetDealIDEnr(guidEnr:string, guidWrt:string):integer
      var query = "select l.enroll_deal_id "
                + "  from uddl_wrtenr_links l "
                + " where l.enroll_guid = :guidenr "
                + "   and l.wrtoff_guid = :guidwrt "
                + "   and l.enroll_deal_id is not null ";
      var sql = ExecSQLselectPrmDyn(query, guidEnr, guidWrt);
      if (sql.MoveNext())
         return sql.value("enroll_deal_id");
      end;
      return 0;
   end;

   private macro GetDealIDWrt(guidWrt:string, guidEnr:string)
      var query = "select l.wrtoff_deal_id "
                + "  from uddl_wrtenr_links l "
                + " where l.wrtoff_guid = :guidwrt "
                + "   and l.enroll_guid = :guidenr "
                + "   and l.wrtoff_deal_id is not null ";
      var sql = ExecSQLselectPrmDyn(query, guidWrt, guidEnr);
      if (sql.MoveNext())
         return sql.value("wrtoff_deal_id");
      end;
      return 0;
   end;

   private macro UpdateByEnr(dealIDEnr:integer, dealIDWrt:integer, guidWrt:string)
      var params = TArray();
      var query = "update uddl_wrtenr_links l "
                + "   set l.wrtoff_deal_id = :dealIDWrt, "
                + "       l.wrtoff_guid = :guidWrt "
                + " where l.enroll_deal_id = :dealIDEnr ";
      params(params.Size()) = SqlParam("dealIDWrt", dealIDWrt);
      params(params.Size()) = SqlParam("guidWrt",   guidWrt);
      params(params.Size()) = SqlParam("dealIDEnr", dealIDEnr);
      ExecSql(query, params);
   end;

   private macro UpdateByWrt(dealIDWrt:integer, dealIDEnr:integer, guidEnr:string)
      var params = TArray();
      var query = "update uddl_wrtenr_links l "
                + "   set l.enroll_deal_id = :dealIDEnr, "
                + "       l.enroll_guid = :guidEnr "
                + " where l.wrtoff_deal_id = :dealIDWrt ";
      params(params.Size()) = SqlParam("dealIDEnr", dealIDEnr);
      params(params.Size()) = SqlParam("guidEnr",   guidEnr);
      params(params.Size()) = SqlParam("dealIDWrt", dealIDWrt);
      ExecSql(query, params);
   end;

   private macro SaveNote(dealID:integer, dealIdLinked:string)
     AddNoteForObject(OBJTYPE_SECDEAL, string(dealID:o:34) , NOTEKIND_LINKED_DEALID, dealIdLinked);
   end;

   //Возможны 3 состояния:
   //  1. enr с таким guid уже есть в таблице
   //    1.1 заполнена сторона wrt
   //    1.2 не заполнена сторона wrt
   //  => игнорируем два подпункта (1.1 и 1.2), просто обновляем данные по wrt

   //  2. wrt с таким guid уже есть в таблице
   //    2.1 заполнена сторона enr
   //    2.2 не заполнена сторона enr
   //  => игнорируем два подпункта (2.1 и 2.2) и ничего не делаем
   //!!Если будет два dealid с одним гуид, то в таблице останется тот dealid, который первый обработался. Потенциальное пространство для ошибок

   //  3. нет ни wrt ни enr части
   //  => создаём новую запись, заполняя только одну из сторон
   //
   // if работает как:
   //   if(1)
   //   elif(3)
   //   end;
   macro SaveWrt(dealIdWrt:integer, guidWrt:string, guidEnr:string)
      if ((guidWrt == "") or (guidEnr == "") or (dealIdWrt == 0))
         return;
      end;
      var dealIDEnr:integer = GetDealIDEnr(guidEnr, guidWrt);

      if (dealIDEnr > 0)
         UpdateByEnr(dealIDEnr, dealIdWrt, guidWrt);
         SaveNote(dealIdWrt, dealIDEnr);
         SaveNote(dealIDEnr, dealIdWrt);
      elif (GetDealIDWrt(guidWrt, guidEnr) == 0)
         Insert(dealIdWrt, guidWrt, null, guidEnr);
      end;
   end;

   //Тоже самое, что и SaveWrt, только "зеркально"
   macro SaveEnr(dealidEnr:integer, guidEnr:string, guidWrt:string)
      if ((guidWrt == "") or (guidEnr == "") or (dealidEnr == 0))
         return;
      end;
      var dealIdWrt:integer = GetDealIDWrt(guidWrt, guidEnr);

      if (dealIdWrt > 0)
         UpdateByWrt(dealIdWrt, dealidEnr, guidEnr);
         SaveNote(dealIdWrt, dealIDEnr);
         SaveNote(dealIDEnr, dealIdWrt);
      elif (GetDealIDEnr(guidEnr, guidWrt) == 0)
         Insert(null, guidWrt, dealidEnr, guidEnr);
      end;
   end;
end;

private class WrtEnrLinksReporter()
   macro Header()
      var str;
      str = str+"\n";
      str =     "                            Список несвязанных операций  \n";
      str = str+"\n";
      str = str+"┌──────────────────────┬─────────────────────┬────────────────────────────────────────────────┬────────────────────────────────────────────────┐\n";
      str = str+"│ Код операции         │Дата операции        │GUID                                            │GUID связанной операции                         │\n";
      str = str+"├──────────────────────┼─────────────────────┼────────────────────────────────────────────────┼────────────────────────────────────────────────┤";
      println(str);                                                                   
   end;                                                                               
                                                                                       
   macro Footer(count:integer)                                            
      var str = "└──────────────────────┴─────────────────────┴────────────────────────────────────────────────┴────────────────────────────────────────────────┘\n";
      str = str+"Количество несвязанных операций: "+count+"\n";
      println(str);
   end;

   macro AddStr(dealCode:string, dealDate:date, guid:string, linkedGUid:string)
      var str = "│"
              + string(dealCode   :22)   + "│"
              + string(dealDate   :21:l) + "│"
              + string(guid       :48:l) + "│"
              + string(linkedGUid :48:l) + "│";
      return println(str);
   end;

   //Можно использовать как статичный метод
   macro Print ()
      StartCaptureOutput();
      [ 
         with unlinked_deals as (
                 select l.wrtoff_deal_id deal_id
                       ,l.wrtoff_guid guid
                       ,l.enroll_guid linked_guid
                   from uddl_wrtenr_links l
                  where l.enroll_deal_id is null
                 union all
                 select l.enroll_deal_id deal_id
                       ,l.enroll_guid guid
                       ,l.wrtoff_guid linked_guid
                   from uddl_wrtenr_links l
                  where l.wrtoff_deal_id is null
               )
             select t.t_dealcode
                   ,t.t_dealdate
                   ,d.guid
                   ,d.linked_guid
               from unlinked_deals d
               join ddl_tick_dbt t on t.t_dealid = d.deal_id
      ];
      var query = EndCaptureOutput();
      var sql = ExecSqlSelect(query);
      var count:integer = 0;
      Header();
      while (sql.MoveNext())
         AddStr(sql.value("t_dealcode"), sql.value("t_dealdate"), sql.value("guid"), sql.value("linked_guid"));
         count = count + 1;
      end;
      Footer(count);
   end;
end;

/*СОВУ*/
Private macro SC_InAccountingExecOp(commdate, opersubkind, documentid:@integer)
  var cmdD, rsD;
  var comm = TRecHandler("dl_comm.dbt"), stat = 0, nextnumb = 0;

  documentid = 0;

  GenerateReference(227, nextnumb);
  
  comm.rec.dockind = DL_INACCSRVOP/*4613*/;
  comm.rec.oper = "1";
  comm.rec.division = 1;
  comm.rec.commstatus = 0;
  comm.rec.PartyId = -1;
  comm.rec.issuerID = -1;
  comm.rec.newFIID = -1;//код ФИ заполнять если ц/б, иначе -1
  comm.rec.commcode = "DEPO"+nextnumb;
  comm.rec.commdate = commdate;//Дата операции
  comm.rec.operationkind = 0;//Вид действия
  comm.rec.opersubkind = opersubkind;//Вид операции
  comm.rec.fiid = -1;// код ФИ заполнять если не ц/б, иначе -1
  comm.rec.contractkind = 1;
  comm.rec.clientid = -1;

  stat = SC_CreateDlCommOperation(comm);

  if(stat == 0)
     documentid = Int(comm.rec.DocumentID);
     stat = SC_ServOperExecuteSilent(DL_INACCSRVOP, Int(comm.rec.DocumentID));
  end;

  if(stat == 0)
    StartCaptureOutput();
    [ 
      select dl_comm.t_commstatus
      from ddl_comm_dbt dl_comm
      where dl_comm.t_documentid = ? and
            dl_comm.t_commstatus = 2
    ];
    cmdD = RSDCommand(EndCaptureOutput());
    cmdD.AddParam("documentid", RSDBP_IN, Int(comm.rec.DocumentID));
    cmdD.Execute();
    rsD = TRsbDataSet(cmdD);
    if(not rsD.movenext)
      return 1;
    end;
  end;

  return stat;
end;


/*СОБУ*/
Private macro SC_AccountingExecOp(commdate, dockind, documentid:@integer)
  var cmdD, rsD;
  var comm = TRecHandler("dl_comm.dbt"), stat = 0, nextnumb = 0;
  documentid = 0;



  GenerateReference(230, nextnumb);

  comm.rec.dockind = DL_SCACCOUNTING/*4615*/;
  comm.rec.oper = "1";
  comm.rec.division = 1;
  comm.rec.commstatus = 0;
  comm.rec.PartyId = -1;
  comm.rec.issuerID = -1;
  comm.rec.FIID = -1;//Выпуск ц/б
  comm.rec.commcode = "DEPO" + nextnumb;
  comm.rec.commdate = commdate;//Дата операции
  comm.rec.contractID = 0;
  comm.rec.clientid = -1;
  comm.rec.fiid = -1;
  comm.rec.avoirkind = 0;//Вид ц/б
  comm.rec.currency = -1;// Валюта номинала
  comm.rec.flag1 = " ";//искл.
  comm.rec.flag2 = " ";//Неттинг
  comm.rec.flag3 = " ";//Клиентские комиссии за обороты
  comm.rec.flag4 = " ";//Рассчеты на бирже

  comm.rec.flag6 = "X";

  comm.rec.createreport = StrFor(0);

  comm.rec.FlagSecur = "X";
  if (ValType(dockind) != V_UNDEF)
     if (dockind == 117)
        comm.rec.opersubkind = 12021;
     else
        comm.rec.opersubkind = 0;
     end;
  else
     comm.rec.opersubkind = 0;
  end;
  comm.rec.contractkind = "3";

  stat = SC_CreateDlCommOperation(comm);

  if(stat == 0)
     documentid = Int(comm.rec.DocumentID);
     stat = SC_ServOperExecuteSilent(DL_SCACCOUNTING, Int(comm.rec.DocumentID));
  end;

  if(stat == 0)
    StartCaptureOutput();
    [ 
      select dl_comm.t_commstatus
      from ddl_comm_dbt dl_comm
      where dl_comm.t_documentid = ? and
            dl_comm.t_commstatus = 2
    ];
    cmdD = RSDCommand(EndCaptureOutput());
    cmdD.AddParam("documentid", RSDBP_IN, Int(comm.rec.DocumentID));
    cmdD.Execute();
    rsD = TRsbDataSet(cmdD);
    if(not rsD.movenext)
      return 1;
    end;
  end;

  return stat;
end;


private class FileFields()
   private var fields = TArray();

   private class FileField(_name, _id)
      var name, id;
      name = _name;
      id   = _id;
   end;

   macro FieldsCount()
      return fields.size;
   end;

   macro addField(_name, _id)
      fields(fields.size) = FileField(_name, _id);
   end;

   macro GetId(_name)
      var i=0;
      for (i, fields) 
         if (StrUpr(i.name) == StrUpr(_name))
            return i.id;
         end;
      end;
      return -1;
   end;

end;

private class Logger()

    macro RepHeader(dt, tm)
       var str;
       str =     "                             ОТЧЕТ о загрузке  \n";
       str = str+"\n";
       str = str+"Дата загрузки  : "+dt+"\n";
       str = str+"Время загрузки : "+tm+"\n";
       str = str+"┌─┬────────────────┬───────────────┬────────┬─────────────┬─────────────┬───────────┬──────────────────────┬─────────────┬───────────────────────────────────────────────────────────────────────────────────────────┐\n";
       str = str+"│ │Код АБС         │Договор        │Площадка│ISIN         │госрег       │ Кол-во    │ Дата-время           │Статус       │Комментарий                                                                                │\n";
       str = str+"├─┼────────────────┼───────────────┼────────┼─────────────┼─────────────┼───────────┼──────────────────────┼─────────────┼───────────────────────────────────────────────────────────────────────────────────────────┤";
       println(str);                                                                   
    end;                                                                               
                                                                                       
    macro RepFooter(suc, er, upd, tot, outfname)                                            
       var str = "└─┴────────────────┴───────────────┴────────┴─────────────┴─────────────┴───────────┴──────────────────────┴─────────────┴───────────────────────────────────────────────────────────────────────────────────────────┘\n";
       str = str+"Загружено успешно: "+suc+"\n";
       str = str+"Не загружено     : "+er+"\n";
       str = str+"Обновлено        : "+upd+"\n";
       str = str+"Всего            : "+tot+"\n";
       str = str+"Файл протокол    : "+outfname+"\n";
      println(str);
    end;

    macro RepStr(_err,_code, _num, _mp,  _isin, _lsin, _amnt, _dt, _stat,_comm)
        var status;
        _comm = strsubst(_comm,strfor(10)," ");
        if (_stat == 0)
            status = "Загружено";
        elif (_stat == 1)
            status = "Не загружено";
        elif (_stat == 2)
            status = "Обновлено";
        end;
        var str = "│"+string(_err  :1)   +"│"+
                  string(_code     :16:l)+"│"+
                  string(_num      :15:l)+"│"+
                  string(_mp       :8:l)+"│"+
                  string(_isin     :13:l)+"│"+
                  string(_lsin     :13:l)+"│"+
                  string(_amnt     :11:l)+"│"+
                  string(_dt       :22:l)+"│"+
                  string(status    :13:l)+"│"+
                  string(_comm          );
       return println(str);
    end;
    macro AddStr(_str)
       println(_str);
    end;
end;

private macro GetLastValue(DealDate)
   var yyyy = 0, num = 0;
   DateSplit(DealDate,null,null,yyyy);
   var dt = TRsbDataSet(
         "  SELECT num \n" +
         "    FROM (SELECT REPLACE (REPLACE (REGEXP_SUBSTR (t_dealcode, '/\d*/ДЕПО'), '/'), 'ДЕПО') \n" +
         "                    num \n" +
         "            FROM ddl_tick_dbt t \n" +
         "           WHERE     t_bofficekind = 127 \n" +
         "                 AND t_dealtype IN (2011, 2010) \n" +
         "                 AND t_dealcode LIKE '%/ДЕПО' \n" +
         "                 AND t_dealdate >= TO_DATE ('0101"+string(yyyy)+"', 'ddmmyyyy') \n" +
         "                 AND t_dealdate <= TO_DATE ('3112"+string(yyyy)+"', 'ddmmyyyy')) \n" +
         "   WHERE num IS NOT NULL \n" +
         "ORDER BY LENGTH (num) DESC, num DESC \n" );
   if (Dt.movenext())
      num = int(dt.num);
   else
      num = 0;
   end;
   return num;
end;

private macro GetNextValue(DealDate)
   var yyyy = 0;
   DateSplit(DealDate,null,null,yyyy);
   var Dt = TRsbDataSet(" select vtb_loadlot_"+string(yyyy)+"_seq.nextval n from dual");
   if (Dt.movenext())
      return dt.n;
   end;
OnError(e)
   var l = GetLastValue(DealDate)+1;
   DateSplit(DealDate,null,null,yyyy);
   var cmd = rsdcommand(" CREATE SEQUENCE vtb_loadlot_"+string(yyyy)+"_seq START WITH "+string(l)+" MAXVALUE 9999999999999999999999999999  NOCYCLE NOCACHE");
   cmd.execute();
   Dt = TRsbDataSet(" select vtb_loadlot_"+string(yyyy)+"_seq.nextval n from dual");
   if (Dt.movenext())
      return dt.n;
   end;
end;

private macro GetDealCode(DealDate, ContrID, ContrNum, num:@integer)

   var cmd = RsdCommand("select t_code from ddlobjcode_dbt where t_objecttype = 207 and t_codekind = 1 and t_bankclosedate = to_date('01010001','ddmmyyyy') and t_objectid = ( " + 
                         "  select t_dlcontrid from ddlcontrmp_dbt where t_sfcontrid = ? )");
   cmd.addParam("", RSDBP_IN, ContrID);
   var Dt = RsdRecordSet(cmd);
   var ClientCode = "", RetVal = "";
   if (Dt.movenext())
      ClientCode = Dt.Value("t_code");
   else
      return "-1";
   end;

/*   if (num == -1)
      num = GetLastValue(DealDate)+1;
   end;*/
   num = GetNextValue(DealDate);
   RetVal = ClientCode+"/"+string(num)+"/ДЕПО";
   cmd = RsdCommand("select 1 from ddl_tick_dbt where t_bofficekind = 127 and t_dealcode = ?");
   cmd.addParam("",RSDBP_IN,RetVal);
   Dt = RsdRecordSet(cmd);
   while (Dt.movenext())
      num = num + 1;
      RetVal = ClientCode+"/"+string(num)+"/ДЕПО";
      cmd = RsdCommand("select 1 from ddl_tick_dbt where t_bofficekind = 127 and t_dealcode = ?");
      cmd.addParam("",RSDBP_IN,RetVal);
      Dt = RsdRecordSet(cmd);
   end;
   return RetVal;
end;

private macro GetDealCodeRet(ContrID)

   var cmd = RsdCommand("select t_code from ddlobjcode_dbt where t_objecttype = 207 and t_codekind = 1 and t_bankclosedate = to_date('01010001','ddmmyyyy') and t_objectid = ( " + 
                         "  select t_dlcontrid from ddlcontrmp_dbt where t_sfcontrid = ? )");
   cmd.addParam("", RSDBP_IN, ContrID);
   var Dt = RsdRecordSet(cmd);
   var ClientCode = "", RetVal = "", num = "";
   if (Dt.movenext())
      ClientCode = Dt.Value("t_code");
   else
      return "-1";
   end;

   if (  generatereference(42,num) != 0 )
      return "-1";
   end;
   RetVal = ClientCode+"/"+string(num)+"/ДЕПО";
   cmd = RsdCommand("select 1 from ddl_tick_dbt where t_bofficekind = 117 and t_dealcode = ?");
   cmd.addParam("",RSDBP_IN,RetVal);
   Dt = RsdRecordSet(cmd);
   while (Dt.movenext())
      if (  generatereference(42,num) != 0 )
         return "-1";
      end;
      RetVal = ClientCode+"/"+string(num)+"/ДЕПО";
      cmd = RsdCommand("select 1 from ddl_tick_dbt where t_bofficekind = 117 and t_dealcode = ?");
      cmd.addParam("",RSDBP_IN,RetVal);
      Dt = RsdRecordSet(cmd);
   end;
   return RetVal;
end;


private macro GetAvoiriss(s_Isin, s_Lsin, Avoirkind:@integer)
   var cmd = RsdCommand(" select f.t_fiid, f.t_avoirkind from davoiriss_dbt a, dfininstr_dbt f where f.t_fiid = a.t_fiid and a.t_isin = ?");
   cmd.addParam("", RSDBP_IN, s_Isin);
   var Dt = RsdRecordSet(cmd);
   if (Dt.movenext)
      avoirkind = Dt.Value("t_AvoirKind");
      return Dt.Value("t_fiid");
   else
      Dt = cmd = null;
      cmd = RsdCommand(" select f.t_fiid, f.t_avoirkind from davoiriss_dbt a, dfininstr_dbt f where f.t_fiid = a.t_fiid and a.t_isin = ?");
      cmd.addParam("", RSDBP_IN, s_Lsin);
      Dt = RsdRecordSet(cmd);
      if (Dt.movenext)
         avoirkind = Dt.Value("t_AvoirKind");
         return Dt.Value("t_fiid");
      end;
   end;
   avoirkind = -1;
   return -1;
end;

private macro GetContract(s_contrNum, s_ContrMP, ContrDateBegin:@date)
   var cmd = RsdCommand("select t_dlcontrid from ddlcontr_dbt d, dsfcontr_Dbt sf where d.t_sfcontrid = sf.t_id and sf.t_number = ? ");
       cmd.addParam("",RSDBP_IN, s_ContrNum);
   var Dt = RsdRecordSet(cmd);
   var dlcontrid = 0, contrid = 0, sql = "";
   if (Dt.movenext())
      dlcontrid = Dt.Value("t_dlcontrid");
      if (s_ContrMP == "OTC")
         sql = "select sf.t_id, sf.t_datebegin from dsfcontr_dbt sf, ddlcontrmp_dbt mp where sf.t_servkind = 1 and sf.t_servkindsub = 9 " + 
                          " and sf.t_id = mp.t_sfcontrid and mp.t_dlcontrid = ?";
      else
         sql = "select sf.t_id, sf.t_datebegin from dsfcontr_dbt sf, ddlcontrmp_dbt mp where sf.t_servkind = 1 and sf.t_servkindsub = 8 " + 
                          " and sf.t_id = mp.t_sfcontrid and mp.t_dlcontrid = ? and mp.t_marketid = ?";
      end;
      cmd = RsdCommand(sql);
      cmd.addParam("",RSDBP_IN, dlcontrid);
      if (s_ContrMP == "MOEX")
         cmd.addParam("",RSDBP_IN, _MMVB_ID);
      elif(s_ContrMP == "SPB")
         cmd.addParam("",RSDBP_IN, _SPB_ID);
      end;

      Dt = RsdRecordSet(cmd);
      if (Dt.movenext())
         ContrId = Dt.Value("t_id");
         ContrDateBegin = Dt.Value("t_datebegin");
      else
         ContrId = -2;
      end;
   else
      Contrid = -1;
   end;
   return contrid;
end;

private macro CheckExistsBoardByGUID(s_GUID, ClientID, ClientContrID, Fiid, DocKind, BoardID:@integer, DealCode:@string, DealDate:@date, Qty:@double)
   var cmd = RsdCommand(" SELECT t.t_DealID, t.t_dealcode, t.t_dealdate, rq.t_amount  " +
             "   FROM dnotetext_dbt n, ddl_tick_dbt t, ddlrq_dbt rq " +
             "  WHERE     n.t_documentid = LPAD (t.t_dealid, 34, '0') " +
             "        AND n.t_notekind = ? "
             "        AND t_objecttype = ?                          " +
             "        AND t.t_bofficekind = ?                       " +
             "        AND t.t_clientid = ?                       " +
             "        AND t.t_clientcontrid = ?                       " +
             //"        AND t.t_pfi = ?                       " +
             "        and rq.t_dockind = t.t_bofficekind  " +
             "        and rq.t_docid = t.t_dealid   and rq.t_type=8 and rq.t_fiid = t.t_pfi and rq.t_dealpart = 1 " +
             "        AND REPLACE (rsb_struct.GetString (t_text), CHR (0)) = ?");
   cmd.addParam("",RSDBP_IN, NOTEKIND_GUID);
   cmd.addParam("",RSDBP_IN, IIF(DocKind == 127, 101, DocKind));
   cmd.addParam("",RSDBP_IN, DocKind);
   cmd.addParam("",RSDBP_IN, ClientID);
   cmd.addParam("",RSDBP_IN, ClientContrID);
//   cmd.addParam("",RSDBP_IN, Fiid);
   cmd.addParam("",RSDBP_IN, s_GUID);
   var Dt = RsdRecordSet(cmd);
   if (Dt.movenext())
      DealCode = Dt.Value("t_DealCode");
      DealDate = Dt.Value("t_DealDate");
      BoardID = Dt.Value("t_DealID");
      Qty = Dt.Value("t_Amount");
      return true;
   end;
   DealCode = "";
   BoardID = -1;
   Qty = -1;
   return false;
end;

private macro GetDt(s_DateTime)
   var s = s_DateTime, dd = "", mm = "", yyyy = "";
   if (index(s,"-") > 0)/*дата есть*/
      if (index(s," ") > 0)/*и время наверное есть*/
         s = trim(SubStr(s_DateTime, 1, index(s_DateTime," ")-1));
      end;
      yyyy = int(trim(SubStr(s, 1, index(s,"-")-1)));
      s = trim(SubStr(s, index(s,"-")+1));
      mm = int(trim(SubStr(s, 1, index(s,"-")-1)));
      s = trim(SubStr(s, index(s,"-")+1));
      dd = int(trim(SubStr(s, 1)));
      return date(dd,mm,yyyy);
   else
      return "";
   end;
end;

//На вход дата в формате строки: 2024-10-22 16:41
//На выходе время 16:41
private macro GetTm(s_DateTime)
   var s = s_DateTime, hh = "", mm = "", ss = "";
   if (index(s,":") > 0)/*время есть*/
      if (index(s," ") > 0)/*и дата наверное есть*/
         s = trim(SubStr(s_DateTime, index(s_DateTime," "))); //отрезается дата. остаётся только 16:41
      end;
      hh = int(trim(SubStr(s, 1, index(s,":")-1)));
      s = trim(SubStr(s, index(s,":")+1));

      if (index(s, ":") == 0) //остались только минуты (41)
         mm = int(s);
      else
         //иначе. после минут есть ещё секунды.
         mm = int(trim(SubStr(s, 1, index(s,  ":") - 1)));
         s = trim(SubStr(s, index(s,  ":") + 1));
         ss = int(s);
      end;
      
      return time(hh,mm,ss);
   else
      return "";
   end;
end;

private macro InsertOutStr(outfile, strfile, delim, fieldcount, fieldnum, msg)
   var c = 0, str = "";
   msg = strsubst(msg,strfor(10)," ");
   while(c < fieldcount)
      if ((ValType(fieldnum) != V_UNDEF) and (c == fieldnum))
         if (c == 0)
            str = msg;
         else
            str = str + delim + msg;
         end;
      else
         if (c == 0)
            str = strfile(c);
         else
            str = str + delim + strfile(c);
         end;
      end;
      c = c + 1;
   end;
   outfile.str = str;
   insert(outfile)
end;

private macro ResetSeq()
   var cmd = RsdCommand(" BEGIN " +
                        " FOR i IN (SELECT * " +
                        "             FROM USER_SEQUENCES " +
                        "            WHERE sequence_name LIKE UPPER ('vtb_loadlot%')) " +
                        "  LOOP                                                       " +
                        "    EXECUTE IMMEDIATE 'drop SEQUENCE ' || i.sequence_name;   " +
                        "  END LOOP;                                                  " +
                        " END;");
   cmd.execute();
end;

class (TRsbPanel) loadPanel()
    InitTRsbPanel();
    setSize(30,5);
    setPosition(35,15);

    var ExternalOper = false;
    var TaxEns = false;
    var TaxWriteOffs = false;
    var UpdateCounter = true;

    class (TRsbCheckBox) CheckBox(x:integer,y:integer,state:bool)
       initTRsbCheckBox();

       setPosition(x,y);
       checked = state;
    end;

    addLabel(TRsbLabel(6, 1, "Установить признак Внешняя операция"));
    var m_ExternalOper = CheckBox(3, 1, ExternalOper);
    addControl(m_ExternalOper);

    addLabel(TRsbLabel(6, 2, "Учитывать зачисления в НУ"));
    var m_TaxEns = CheckBox(3, 2, TaxEns);
    addControl(m_TaxEns);

    addLabel(TRsbLabel(6, 3, "Учитывать списания в НУ"));
    var m_TaxWriteOffs = CheckBox(3, 3, TaxWriteOffs);
    addControl(m_TaxWriteOffs);

    addLabel(TRsbLabel(6, 4, "Обновить счетчик нумерации операций"));
    var m_UpdateCounter = CheckBox(3, 4, UpdateCounter);
    addControl(m_UpdateCounter);

    addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "KEY_PRESSED"));

    macro KEY_PRESSED(RsbEvent)
       if((RsbEvent.keyCode == 13) or (RsbEvent.keyCode == 316))
          ExternalOper  = m_ExternalOper.checked; 
          TaxEns        = m_TaxEns.checked; 
          TaxWriteOffs  = m_TaxWriteOffs.checked; 
          UpdateCounter = m_UpdateCounter.checked;

          close(1);
       elif(RsbEvent.keyCode == 27)
          close(0);
       end;
    end; 
end;

private macro GetFiidByISO(iso:string):integer
   var query = "select f.t_fiid "
             + "  from dfininstr_dbt f "
             + " where f.t_fi_kind = 1 "
             + "   and f.t_ccy = :iso ";
   var sql = ExecSQLselectPrmDyn(query, iso);
   if (sql.MoveNext() )
      return sql.value("t_fiid");
   end;
   return null;
end;

private macro GetCurrIdByFaceValue(fiid:integer):integer
   var query = "select f.t_facevaluefi "
             + "  from dfininstr_dbt f "
             + " where f.t_fiid = :fiid ";
   var sql = ExecSQLselectPrmDyn(query, fiid);
   if (sql.MoveNext())
      return sql.value("t_facevaluefi");
   end;
   return null;
end;

private macro GetFacevalueOndate(fiid:integer, dt:date):double
   var query = "select nvl(rsi_rsb_fiinstr.FI_GetNominalOnDate(pFIID => :fiid, pDate => :dt), 0) as facevalue from dual";
   var sql = ExecSQLselectPrmDyn(query, fiid, dt);
   if (sql.MoveNext() )
      return sql.value("facevalue");
   end;
   return 0;
end;

private macro CalcNkd(fiid:integer, dt:date, qty:integer):double
   var query = "select rsi_rsb_fiinstr.CalcNKD_Ex(FIID     => :fiid, "
             + "                                  CalcDate => :dt, "
             + "                                  Amount   => :qty, "
             + "                                  LastDate => 0) nkd "
             + "  from dual ";
   var sql = ExecSQLselectPrmDyn(query, fiid, dt, qty);
   if (sql.MoveNext() )
      return sql.value("nkd");
   end;
   return 0;
end;

//true = субъект физ. лицо и не ИП
private macro isIndividual(partyid:integer):bool
   var query = "select 1 "
             + "  from dparty_dbt p "
             + " where p.t_partyid = :partyid "
             + "   and p.t_legalform = 2 "
             + "   and not exists (select 1 "
             + "                     from dpersn_dbt pers "
             + "                    where pers.t_personid = p.t_partyid "
             + "                      and pers.t_isemployer = 'X') ";
   return ExecSQLselectPrmDyn(query, partyid).MoveNext();
end;

//Из текстовой цены 123.456 возвращает количество знаков после точки
private macro getPointLength(price:string):integer
   var separatorIndex:integer = index(price, ".");
   if (separatorIndex == 0)
      return 0;
   end;
   
   return StrLen(trim(SubStr(price, separatorIndex + 1)));
end;

macro runLoadLot2(UPDATE_REST, errp:@string , result_protocol:@variant)
   var ErrStr = "",ErrStat = 0;
   var row = 0, c = 0;
   var suc = 0, err = 0, upd = 0, nd = 0;
   var log = Logger();
   var dt = StrSubSt(string(Date()),".","")+StrSubSt(string(Time()),":","");
   var InfoLogFileName = GetTxtFileName( "loadlot2_"+dt);
   var DataSet;
   var Qty = 0.0;
   var LoadAvoirArr = TArray();

   var cur_file_mask = "*.csv";
   file datafile() txt;
   file outfile() txt WRITE;
   SetDelim("|");

   var DealCode, DealDate, DealTime, ClientID, ContrID, CustodyID, BoardID, msg = "", Fiid, Amount, ContrDateBegin, Price, CurPriceFiid, nkd, cms;
      
   var c_file = c_txt_file();
   var dealsBinder = WrtEnrLinks();
   var FullFileName;
   var stat;
   var replacementAttrID:integer;

   var s_Client = "",
       s_ContrNum = "",
       s_ContrMp = "",
       s_ClientPrev = "",  
       s_ContrNumPrev = "",
       s_ContrMpPrev = "", 
       s_Isin = "",
       s_Lsin = "",
       s_Amount = "",
       s_Guid = "",
       s_DateTime = "",
       s_Scale = "",
       s_OperType = "",
       s_Point = "",
       s_price = "",
       s_isrelative = "",
       s_curprice = "",
       s_nkd = "",
       s_cms = "",
       s_replacement = "",
       s_guidlnk = "";
              
   var AvoirKind = -1, num = -1;

   var sql_str, cmd, rs;
   var panel = LoadPanel;
   panel.setStatus(" ~F2~,~Enter~ Выполнить  ~Esc~ Выход");
   panel.setCaption("Загрузка депозитарных зачислений");
   var panelStat = panel.run;

   if(panelStat == 0)/** Нажали Esc */
      errp = "Отказ от обработки";
      return false;
   end;

   var SetOuter = panel.ExternalOper;
   var UseInTaxEns = panel.TaxEns;
   var UseInTaxWriteOffs = panel.TaxWriteOffs;
   var UpdateCounter = panel.UpdateCounter;

   stat = c_file.openFile(datafile, cur_file_mask, true, false, FullFileName, ENCODE);

   if ( (valtype(stat) == V_UNDEF) or (stat == false) )
      errp = "Ошибка открытия файла " + FullFileName;
      return false;
   end;

   /*создаем лог файл*/
   var namf="", extf="";
   SplitFile(c_file.localFileName, namf, extf);
   var outfname = OUTDIR+"\\" + namf + "_out_"+dt + extf;
   if (not Open(outfile, outfname, encode)) 
     errp = "Ошибка открытия файла лога " + outfname;
     return false;
   end;

   /*Открываем файл*/
   BegAction(1, "Обработка файла \"" + c_file.localFileName + "\". Ждите...");

   /* формат файла, первая строка - всегда заголовок, данные со второй строки
   запись в файле|клиент|договор брокерского обслуживания|площадка|договор депо|идентификатор ценной бумаги 1|идентификатор ценной бумаги 2|количество ЦБ в штуках|дробный коэффициент|дробная точность|флаг направления операции|ссылка на ВУ счет ЦБ в СОФРе|ссылка на счет учета в Диасе|отметка времени операции в диасе/софре|текст - основание|признак клиента ВТБ|guid операции
1	запись в файле
2	клиент
3	договор брокерского обслуживания 
4	площадка
5	договор депо
6	идентификатор ценной бумаги 1
7	идентификатор ценной бумаги 2
8	количество ЦБ в штуках
9	дробный коэффициент
10	дробная точность
11	флаг направления операции
12	ссылка на ВУ счет ЦБ в СОФРе
13	ссылка на счет учета в Диасе
14	отметка времени операции в диасе/софре
15	текст - основание 
16	признак клиента ВТБ
17	операции
18  Цена - Только для внебиржевых  
19  Признак относительной цены - Только для внебиржевых  
20  Валюта цены - Только для внебиржевых  
21  НКД в валюте цены - Только для внебиржевых
22  Сумма комиссии
23  Замещение облигаций 1 - Да 2 - Нет 3 - Да, МинФин
24  GUID связанной сделки

     */

   if(UpdateCounter)
      ResetSeq();
   end;

   var tmstart = time();

   next(datafile);


  /* есть требование о произвольном порядке полей в файле. Собираем заголовок*/
   var Fields = FileFields();
   c = 0;
   while(datafile(c) != "")
      Fields.AddField(datafile(c),c);
      c = c + 1;
   end;

   SetOutput( InfoLogFileName, true ); //Пишем лог в файл. Начало
   log.RepHeader(date(),time());

   /*Проверяем обязательные поля*/
   stat = 0;
   if (Fields.GetID(FN_CLIENT) == -1)
      log.RepStr("X", "", "", "", "", "", "Отсутствует обязательная колонка: \""+FN_CLIENT+"\"" );
      stat = 1;
   end;
   if (Fields.GetID(FN_CONTRNUM) == -1)
      log.RepStr("X", "", "", "", "", "", "Отсутствует обязательная колонка: \""+FN_CONTRNUM+"\"" );
      stat = 1;
   end;
   if (Fields.GetID(FN_CONTRMP) == -1)
      log.RepStr("X", "", "", "", "", "", "Отсутствует обязательная колонка: \""+FN_CONTRMP+"\"" );
      stat = 1;
   end;
   if ((Fields.GetID(FN_ISIN) == -1) and (Fields.GetID(FN_LSIN) == -1))
      log.RepStr("X", "", "", "", "", "", "Отсутствует обязательная колонка: \""+FN_ISIN+"\"  или \""+FN_LSIN+"\"" );
      stat = 1;
   end;
   if (Fields.GetID(FN_AMNT) == -1)
      log.RepStr("X", "", "", "", "", "", "Отсутствует обязательная колонка: \""+FN_AMNT+"\"" );
      stat = 1;
   end;
   if (Fields.GetID(FN_GUID) == -1)
      log.RepStr("X", "", "", "", "", "", "Отсутствует обязательная колонка: \""+FN_GUID+"\"" );
      stat = 1;
   end;
   if (Fields.GetID(FN_TM) == -1)
      log.RepStr("X", "", "", "", "", "", "Отсутствует обязательная колонка: \""+FN_TM+"\"" );
      stat = 1;
   end;

   if ( not stat)

      row = 2;
      //Бежим по файлу
      InitProgress(-1);
      SP_BeginInsertBoardLot ();
      outfile.str = datafile.str;
      Insert(outfile);
      while (next(datafile))
         if (StrLen(trim(datafile.str)) == 0)
            continue;
         end;

         s_ClientPrev = s_Client;
         s_ContrNumPrev = s_ContrNum;
         s_ContrMpPrev = s_ContrMp;
         s_Client = s_ContrNum = s_ContrMp = s_Isin = s_Lsin = s_Amount = s_Guid = s_DateTime = s_Scale = s_Point = s_OperType = "";
         s_price = s_isrelative = s_curprice = s_nkd = s_cms = s_replacement = s_guidlnk = "";
         BoardID = 0;

         s_Client      = trim(datafile(Fields.GetID(FN_CLIENT)));
         s_ContrNum    = trim(datafile(Fields.GetID(FN_CONTRNUM)));
         s_ContrMp     = trim(datafile(Fields.GetID(FN_CONTRMP)));
         s_Isin        = trim(datafile(Fields.GetID(FN_ISIN)));
         s_Lsin        = trim(datafile(Fields.GetID(FN_LSIN)));
         s_Amount      = trim(datafile(Fields.GetID(FN_AMNT)));
         s_Guid        = trim(datafile(Fields.GetID(FN_GUID)));
         s_DateTime    = trim(datafile(Fields.GetID(FN_TM)));
         s_OperType    = trim(datafile(Fields.GetID(FN_INOUT)));
         s_price       = "";
         s_isrelative  = "";
         s_curprice    = "";
         s_nkd         = "";
         s_cms         = "";
         s_replacement = "";
         s_guidlnk     = "";

         if (Fields.GetID(FN_PRICE) > 0)
            s_price = trim(datafile(Fields.GetID(FN_PRICE)));
         end;

         if (Fields.GetID(FN_ISRELATIVE) > 0)
            s_isrelative = trim(datafile(Fields.GetID(FN_ISRELATIVE)));
         end;

         if (Fields.GetID(FN_CURPRICE) > 0)
            s_curprice = trim(datafile(Fields.GetID(FN_CURPRICE)));
         end;

         if (Fields.GetID(FN_NKD) > 0)
            s_nkd = trim(datafile(Fields.GetID(FN_NKD)));
         end;

         if (Fields.GetID(FN_CMS) > 0)
            s_cms = trim(datafile(Fields.GetID(FN_CMS)));
         end;

         if (Fields.GetID(FN_REPLACEMENT) > 0)
            s_replacement = trim(datafile(Fields.GetID(FN_REPLACEMENT)));
         end;

         if (Fields.GetID(FN_GUIDLNK) > 0)
            s_guidlnk = trim(datafile(Fields.GetID(FN_GUIDLNK)));
         end;

         if (StrLen(s_OperType) == 0)
            ErrStat = 1;
            ErrStr = "Не задано поле \""+FN_INOUT+"\", строка "+row;
            log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
            InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
            row = row + 1;
            UseProgress(row);
            err = err + 1;
            continue;
         end;
         if (StrLen(s_Client) == 0)
            ErrStat = 1;
            ErrStr = "Не задано поле \""+FN_CLIENT+"\", строка "+row;
            log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
            InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
            row = row + 1;
            UseProgress(row);
            err = err + 1;
            continue;
         end;
         if (StrLen(datafile(Fields.GetID(FN_CONTRNUM))) == 0)
            ErrStat = 1;
            ErrStr = "Не задано поле \""+FN_CONTRNUM+"\", строка "+row;
            log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
            InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
            row = row + 1;
            UseProgress(row);
            err = err + 1;
            continue;
         end;
         if (StrLen(datafile(Fields.GetID(FN_CONTRMP))) == 0)
            ErrStat = 1;
            ErrStr = "Не задано поле \""+FN_CONTRMP+"\", строка "+row;
            log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
            InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
            row = row + 1;
            UseProgress(row);
            err = err + 1;
            continue;
         end;
         if ((StrLen(datafile(Fields.GetID(FN_ISIN))) == 0)
            and (StrLen(datafile(Fields.GetID(FN_LSIN))) == 0))
            ErrStat = 1;
            ErrStr = "Не заданы поля \""+FN_ISIN+"\" и \""+FN_LSIN+"\", строка "+row;
            log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
            InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
            row = row + 1;
            UseProgress(row);
            err = err + 1;
            continue;
         end;
         if (StrLen(datafile(Fields.GetID(FN_AMNT))) == 0)
            ErrStat = 1;
            ErrStr = "Не задано поле \""+FN_AMNT+"\", строка "+row;
            log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
            InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
            row = row + 1;
            UseProgress(row);
            err = err + 1;
            continue;
         end;
         if (StrLen(datafile(Fields.GetID(FN_TM))) == 0)
            ErrStat = 1;
            ErrStr = "Не задано поле \""+FN_TM+"\", строка "+row;
            log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
            InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
            row = row + 1;
            UseProgress(row);
            err = err + 1;
            continue;
         end;
         if (StrLen(datafile(Fields.GetID(FN_GUID))) == 0)
            ErrStat = 1;
            ErrStr = "Не задано поле \""+FN_GUID+"\", строка "+row;
            log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
            InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
            row = row + 1;
            UseProgress(row);
            err = err + 1;
            continue;
         end;
         if ((s_OperType != "3") and  (StrLen(s_price) > 0))
            if ((StrLen(s_curprice) == 0) and (StrLen(s_isrelative) == 0) )
               ErrStat = 1;
               ErrStr = "Не задана валюта цены, строка "+row;
               log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
               InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
               row = row + 1;
               UseProgress(row);
               err = err + 1;
               continue;
            end;
         end;

         DealDate = GetDt(s_DateTime);
         if (DealDate == "")
            ErrStat = 1;
            ErrStr = "Не удалось получить дату из \""+s_DateTime+"\", строка "+row;
            log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
            InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
            row = row + 1;
            UseProgress(row);
            err = err + 1;
            continue;
         end;
         DealTime = GetTm(s_DateTime);
         if (DealTime == "")
            ErrStat = 1;
            ErrStr = "Не удалось получить время из \""+s_DateTime+"\", строка "+row;
            log.RepStr("X",s_Client,s_ContrNum,s_Isin,s_Amount, ErrStat, ErrStr);
            log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
            row = row + 1;
            UseProgress(row);
            err = err + 1;
            continue;
         end;

         if ( ( s_ClientPrev == "") or (s_Client != s_ClientPrev) or ErrStat)
            ClientID = GetCodeParty(s_Client, 101);
            if (ClientID <= 0)
               ErrStat = 1;
               ErrStr = "Не найден клиент с видом кода 101 = \""+s_Client+"\", строка "+row;
               log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
               InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
               row = row + 1;
               UseProgress(row);
               err = err + 1;
               continue;
            end;
         end;

         if ( ( s_ContrMpPrev == "") or (s_ContrMp != s_ContrMpPrev) or ErrStat /*ошибка на предыдущей строке*/)
            if (s_ContrMp == "SPB")
               CustodyID = GetCodeParty("00#Б#239654",1); //1450;
            elif (s_ContrMp == "MOEX")
               CustodyID = GetCodeParty("НРД",1); //4;
            elif (s_ContrMp == "OTC")
               CustodyID = {OurBank};
            else
               ErrStat = 1;
               ErrStr = "Неизвестная торговая площадка \""+datafile(Fields.GetID(FN_CONTRMP))+"\", строка "+row;
               log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
               InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
               row = row + 1;
               UseProgress(row);
               err = err + 1;
               continue;
            end;
         end;

         if ( (s_ContrMpPrev == "") or (s_ContrNumPrev == "") or (s_ContrNum != s_ContrNumPrev) or (s_ContrMp != s_ContrMpPrev) or ErrStat )
            ContrID = GetContract(s_ContrNum, s_ContrMp, @ContrDateBegin);
            if (ContrID < 0)
               ErrStat = 1;
               if (ContrID == -1)
                  ErrStr = "Не найден ДБО по номеру \""+s_contrNum+"\", строка "+row;
               elif (ContrID == -2)
                  ErrStr = "Не найден субдоговор по номеру \""+s_contrNum+"\" для площадки \""+s_ContrMp+"\", строка "+row;
               else
                  ErrStr = "Не найден субдоговор по номеру \""+s_contrNum+"\" для площадки \""+s_ContrMp+"\", строка "+row;
               end;
               log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
               InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
               row = row + 1;
               UseProgress(row);
               err = err + 1;
               continue;
            else
               cmd = RsdCommand("update dclient_dbt c set c.t_startdate = ? where C.T_PARTYID = ?  and ? < c.t_startdate and c.t_servicekind = ? and c.t_startdate <> to_date('01010001','ddmmyyyy')");
               cmd.addParam("", RSDBP_IN, ContrDateBegin);
               cmd.addParam("", RSDBP_IN, ClientID);
               cmd.addParam("", RSDBP_IN, ContrDateBegin);
               cmd.addParam("", RSDBP_IN, 1);
               cmd.Execute();
            end;
         end;

         Fiid = GetAvoiriss(s_Isin, s_Lsin, @AvoirKind);
         if (Fiid < 0 )
            Message("Загрузка "+s_ISIN+" из RU DATA");
            if (SOFR_LoadRuDataByID( StrUpr( s_Isin ), 1, @msg, true ))
               LoadAvoirArr[LoadAvoirArr.size] = s_isin;
            else
               LoadAvoirArr[LoadAvoirArr.size] = s_isin + "  " + msg;
            end;
            Message(" ");
            Fiid = GetAvoiriss(s_Isin, s_Lsin, @AvoirKind);
            if (Fiid < 0 )
               ErrStat = 1;
               ErrStr = "Не найдена ценная бумага по ISIN \""+s_Isin+"\" и гос.рег \""+s_lsin+"\", строка "+row;
               log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
               InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
               row = row + 1;
               UseProgress(row);
               err = err + 1;
               continue;
            end;
         else
            cmd = RsdCommand("update davoiriss_dbt a set A.T_TSISCLOSED = chr(0),A.T_SPISCLOSED = chr(0) where t_fiid = ? and (( A.T_TSISCLOSED = chr(88)) or (A.T_SPISCLOSED = chr(88)))");
            cmd.addParam("", RSDBP_IN, Fiid);
            cmd.Execute();
            cmd = RsdCommand("update dfininstr_dbt set t_expirydate = to_date('01010001','ddmmyyyy') where t_fiid = ?");
            cmd.addParam("", RSDBP_IN, Fiid);
            cmd.Execute();
         end;

         if (StrLen(datafile(Fields.GetID(FN_SCALE))) == 0)
            s_Scale = 1;
         else
            s_Scale     = trim(datafile(Fields.GetID(FN_SCALE)));
         end;

         if (StrLen(datafile(Fields.GetID(FN_POINT))) == 0)
            s_Point = 0;
         else
            s_Point = trim(datafile(Fields.GetID(FN_POINT)));
         end;

         cmd = RsdCommand("update dfininstr_dbt set t_sumprecision = ? where t_fiid = ? and t_sumprecision < ? ");
         cmd.addParam("", RSDBP_IN, int( s_Point));
         cmd.addParam("", RSDBP_IN, Fiid);
         cmd.addParam("", RSDBP_IN, int( s_Point));
         cmd.Execute();

         Amount = double(s_Amount) * int(s_Scale) * pow(10,-int(s_Point));

         if (CHECK_GUID)
            if (CheckExistsBoardByGUID(s_Guid, ClientID, ContrID, Fiid, IIF(s_OperType == "3", 117, 127), @BoardID, @DealCode, @DealDate, @Qty))
               if ((UPDATE_REST) and (round(Amount,11) != round(Qty,11)) and (Qty > 0))
                  if ( UpdateRest(BoardID, round(Qty,11), round(Amount,11), int(s_Scale), int(s_Point), @msg) != 0)
                     ErrStat = 1;
                     ErrStr = "Ошибка обновления количества по операции \""+DealCode+"\" \""+DealDate+"\", строка "+row+"  "+msg;
                     log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
                     InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
                     row = row + 1;
                     UseProgress(row);
                     err = err + 1;
                     continue;
                  else
                     ErrStat = 2;
                     ErrStr = "Обновлено количество по операции: \""+DealCode+"\" \""+DealDate+"\": с "+Qty+" на "+Amount+", строка "+row;
                     log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
                     InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
                     row = row + 1;
                     UseProgress(row);
                     upd = upd + 1;
                     continue;
                  end;
               else
                  ErrStat = 1;
                  ErrStr = "Зачисление с GUID = \""+s_Guid+"\" уже загружалось: \""+DealCode+"\" \""+DealDate+"\", строка "+row;
                  log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
                  InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
                  row = row + 1;
                  UseProgress(row);
                  err = err + 1; nd = nd + 1;
                  continue;
               end;
            end;
         end;

         if (s_OperType == 3)
            DealCode = GetDealCodeRet(ContrID);
         else
            if (num != -1)
               num = num + 1;
            end;

            DealCode = GetDealCode(DealDate, ContrID, s_ContrNum, @num);
         end;
         if (DealCode == "-1")
            ErrStat = 1;
            ErrStr = "Код операции не сформирован. Не найден ЕКК по договору \""+s_ContrNum+"\", строка "+row;
            log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
            InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
            row = row + 1;
            UseProgress(row);
            err = err + 1;
            continue;
         end;

         if (DealDate < ContrDateBegin)
            DealDate = ContrDateBegin;
         end;
         DealDate = GetDateAfterWorkDays(DealDate,0);

         if (s_OperType != "3") 
            nkd          = iif(StrLen(s_nkd) == 0,      null,                       double(s_nkd));
            cms          = iif(StrLen(s_cms) == 0,      null,                       double(s_cms));
            CurPriceFiid = iif(StrLen(s_curprice) == 0, GetCurrIdByFaceValue(Fiid), GetFiidByISO(s_curprice));
            Price        = iif(StrLen(s_price) == 0,    null,                       double(s_price));

            if ( (Price != null) and (s_isrelative == "X") )
               Price = GetFacevalueOndate(Fiid, DealDate) * Price / 100;
            end;

            if ( (Price != null) and (nkd == null) and FI_IsBond(Fiid))
               nkd = CalcNkd(Fiid, DealDate, Amount);
            end;
         end;

         var UseInTax = strFor(0);
         if (isIndividual(ClientID)) //только физики
            if(s_OperType == "2")
               if(UseInTaxWriteOffs)
                  UseInTax = "X";
               end;
            elif(s_OperType == "3")
               UseInTax = StrFor(0); 
            elif(UseInTaxEns) 
               UseInTax = "X";
            end;
         end;

         stat = CreateDepoBoard (DealCode,
                                 DealDate,
                                 DealTime,
                                 ClientID,
                                 ContrID,
                                 Fiid,
                                 Amount,
                                 getPointLength(s_price),
                                 CustodyID,
                                 false /*IsSingle*/,
                                 s_OperType,
                                 UseInTax,
                                 Price,
                                 CurPriceFiid,
                                 nkd,
                                 cms,
                                 @msg,
                                 @BoardID
                                );

         if (stat == 0)
            AddNoteForObject(IIF(s_OperType == "3", 117, OBJTYPE_SECDEAL), string(BoardID:o:34) , NOTEKIND_GUID, s_Guid);

            if ( (StrLen(s_guidlnk) > 0) and (s_OperType != "3"))
               if (s_OperType == "1")
                 dealsBinder.SaveEnr(BoardID, s_Guid, s_guidlnk);
               elif (s_OperType == "2")
                 dealsBinder.SaveWrt(BoardID, s_Guid, s_guidlnk);
               end;
            end;

            if (StrLwr(s_replacement) == "да")
               replacementAttrID = 1;
            elif (StrLwr(s_replacement) == "нет")
               replacementAttrID = 2;
            else
               replacementAttrID = 0;
            end;

            if ( (replacementAttrID > 0) and (s_OperType != "3") and not ConnectObjAttr(stat, OBJTYPE_SECDEAL, string(BoardID:o:34), OBJGROUP_TICKREPLACEMENT, replacementAttrID, null, null, DealDate) )
               ErrStat = 0;
               ErrStr = "Ошибка при установке Категории \"Сделка замещения\" для операции \"" + DealCode + "\"";
               log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
               InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
            end;

            if (SetOuter and (s_OperType != "3") and (not ConnectObjAttr(stat, OBJTYPE_SECDEAL, string(BoardID:o:34), OBJGROUP_TICKEXTERNALOP, 2, null, null, DealDate)))
               ErrStat = 0;
               ErrStr = "Ошибка при установке Категории \"Внешняя операция\" для операции \"" + DealCode + "\"";
               log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
               InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
            else
               ErrStat = 0;
               ErrStr = DealCode;
               log.RepStr("",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
               InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), string(BoardID));
            end;

            suc = suc + 1;
         else 
            ErrStat = 1;
            ErrStr = msg;
            log.RepStr("X",s_Client,s_ContrNum,s_ContrMP, s_Isin, s_Lsin, s_Amount, s_DateTime, ErrStat, ErrStr);
            InsertOutStr(outfile, datafile, "|", Fields.FieldsCount, Fields.GetID(FN_GROUND), ErrStr);
            row = row + 1;
            UseProgress(row);
            err = err + 1;
            continue;
         end;

         row = row+1;
         UseProgress(row);
         ErrStr = "";
         ErrStat = false;
      end;
      RemProgress();
      SP_EndInsertBoardLot ();

   else
      log.AddStr("Неверный формат файла");
   end;

   log.RepFooter(suc,err-nd, upd, suc+err+upd, outfname);
   WrtEnrLinksReporter.Print();

   c_file.closeFile(datafile);
   close(outfile);



   errp = "";
   result_protocol.addString("Обработано " + suc+err+upd + " строк " );
   result_protocol.addString("Не загружено " + (err-nd) + " записей " );
   result_protocol.addString("Обновлено " + upd + " записей " );
   result_protocol.addString("Загружено " + suc + " новых записей " );
   result_protocol.addString("Файл протокол: " + outfname );
   result_protocol.addString("");
   log.AddStr("");
   if (LoadAvoirArr.size > 0)
      result_protocol.addString("Загружены ценные бумаги:");
      log.AddStr("Загружены ценные бумаги:");
      var i_l;
      for (i_l, LoadAvoirArr)
         result_protocol.addString(i_l);
         log.AddStr(i_l);
      end;
   result_protocol.addString("");
   end;
   result_protocol.addString("Старт " + tmstart);
   result_protocol.addString("Окончание " + time() );


   SetOutput( NULL, true ); //Пишем лог в файл. Конец
   ViewFile( InfoLogFileName );

   return true;


end;

macro runProcessLot2(errp:@string , result_protocol:@variant)
   Var cmd, DataSet, er, cmd1, , dt1, OperDateAr = TArray(), item , sql;
   var ArrDeal = tarray(),recs = 0, errrecs = 0, RunDate = {curdate}, documentid, i = 0, j = 0;

   var ar_menu = TArray();
   ar_menu(0) = "Выполнить зачисления/списания";
   ar_menu(1) = "Выполнить погашения";
   var choice = menu(ar_menu);  
   var dockind;
   if (choice == 0)
      dockind = 127;
   else
      dockind = 117;
   end;


   if (choice == 0)
      Initprogress(3, "Проведение операций списания и зачисления цб", "Этапы проведения операций списания и зачисления цб");
      cmd1 = RSDCommand(  " update doprostep_dbt set t_notinuse = 'X' where T_BLOCKID in(201000, 201100) and t_number_step = 40 " );
      cmd1.execute();
   end;


   cmd = RsdCommand(" select tick.* from ddl_tick_dbt tick, dnotetext_dbt text where tick.t_bofficekind = ? and tick.t_dealstatus !=20 and tick.t_dealdate <= ? "+
       				  " AND text.t_objecttype = ? and text.t_documentid = LPAD(tick.t_dealid, 34, '0') and text.t_notekind = 404  ");

   cmd.addParam("",RSDBP_IN, dockind);
   cmd.addParam("",RSDBP_IN, RunDate);
   if (choice == 0)
      cmd.addParam("",RSDBP_IN, 101);
   else
      cmd.addParam("",RSDBP_IN, dockind);
   end;
   DataSet = RsdRecordSet(cmd);
   
   while (DataSet.movenext())
      ArrDeal[recs] = DataSet.value("t_dealid");
      recs = recs + 1;
   end;
     
   result_protocol.addString("Старт проведения операций  " + time() );

   if(SP_ExecDeal(ArrDeal, false, RunDate) != 0)
      result_protocol.AddString("Ошибка при проведении операций  '");
      errrecs = errrecs + 1;
   else
      result_protocol.AddString("проведение операций выполнено " + time());
   end;
   UseProgress(i = i + 1);

   if (choice == 0)
      cmd1 = RSDCommand(  " update doprostep_dbt set t_notinuse = chr(0) where T_BLOCKID in(201000, 201100) and t_number_step = 40 " );
      cmd1.execute();
   end;

   /* СОБУ*/

   if (choice == 0)
      /*закрытие графика Фиксация комиссии*/
      cmd1 = RsdCommand(
                       "update ddlgracc_dbt set t_state = 2 where t_id in ( \n" +
                       "select a.t_id from ddlgrdeal_dbt  gr, ddl_tick_dbt t, ddlgracc_dbt a, dnotetext_dbt text where gr.t_dockind = t.t_bofficekind and gr.t_docid = t.t_dealid and GR.T_TEMPLNUM = 100  \n" +
                       "and A.T_GRDEALID = gr.t_id \n" +
                       "AND text.t_objecttype = 101 and text.t_documentid = LPAD(t.t_dealid, 34, '0') and text.t_notekind = 404 and t.t_dealstatus = 10 \n" +
                       "and a.t_accnum = 2 and a.t_state = 1) \n" );
      cmd1.execute();
   else
      /*закрытие графика Оплата и Фиксация комиссии*/
      cmd1 = RsdCommand(
                       "update ddlgracc_dbt set t_state = 2 where t_id in ( \n" +
                       "select a.t_id from ddlgrdeal_dbt  gr, ddl_tick_dbt t, ddlgracc_dbt a, dnotetext_dbt text where gr.t_dockind = t.t_bofficekind and gr.t_docid = t.t_dealid  and GR.T_TEMPLNUM in (15, 100)   \n" +
                       "and A.T_GRDEALID = gr.t_id  \n" +
                       "AND text.t_objecttype = 117 and text.t_documentid = LPAD(t.t_dealid, 34, '0') and text.t_notekind = 404 and t.t_dealstatus = 10  \n" +
                       "and a.t_accnum = 2  and a.t_state = 1 \n" +
                       ")  \n" );                                                                    
      cmd1.execute();
      cmd1 = RsdCommand(
                      "UPDATE dfiwarnts_dbt \n" +
                      "   SET t_spisclosed = CHR (88) \n" +
                      " WHERE t_spisclosed = CHR (0) \n" +
                      "       AND t_fiid IN \n" +
                      "              (SELECT tick.t_pfi \n" +
                      "                 FROM ddl_tick_dbt tick, dnotetext_dbt text \n" +
                      "                WHERE     tick.t_bofficekind = 117 \n" +
                      "                      AND tick.t_dealstatus = 0 \n" +
                      "                      AND tick.t_dealdate <= ? \n" +
                      "                      AND text.t_objecttype = 117 \n" +
                      "                      AND text.t_documentid = LPAD (tick.t_dealid, 34, '0') \n" +
                      "                      AND text.t_notekind = 404) \n" );
      cmd1.addParam("",RSDBP_IN, RunDate);
      cmd1.Execute();

   end;

   sql = " select tick.t_dealdate from ddl_tick_dbt tick, dnotetext_dbt text where tick.t_bofficekind = "+dockind+" and tick.t_dealstatus =10 and tick.t_dealdate <=  " + GetSQLDate(RunDate) +
                    " AND text.t_objecttype = case when tick.t_bofficekind = 127 then 101 else tick.t_bofficekind end "
                    "  and text.t_documentid = LPAD(tick.t_dealid, 34, '0') and text.t_notekind = 404 "+
                    " group by tick.t_dealdate "+
                    " order by tick.t_dealdate asc";
   cmd = RsdCommand(sql);
   DataSet = RsdRecordSet(cmd);
   InitProgress(SQL_GetNRecs(sql), "Проведение СОБУ");
   while (DataSet.movenext())
      cmd1 = RsdCommand("select 1 from dcurdate_dbt where t_curdate = ? and t_isclosed = chr(88)");
      cmd1.addParam("", RSDBP_IN, sql_convtypedate(DataSet.value("t_dealdate")));
      cmd1 = RsdRecordSet(cmd1);
      if (cmd1.movenext())
          cmd1 = RsdCommand("update dcurdate_dbt  \n" +
              "set t_phase = 1, t_minphase = 1, t_maxphase = 1, t_isclosed = chr(0) where t_branch = 1 and  \n" +
              "t_curdate  =  ? \n" +
              "and t_isclosed = chr(88); " );
          cmd1.addParam("", RSDBP_IN, sql_convtypedate(DataSet.value("t_dealdate")));
          cmd1.execute();
          result_protocol.AddString(" Открыт опердень " + sql_convtypedate(DataSet.value("t_dealdate")));
          OperDateAr[OperDateAr.size] = sql_convtypedate(DataSet.value("t_dealdate"));
      end;

      if(SC_AccountingExecOp(sql_convtypedate(DataSet.value("t_dealdate")), iif(dockind == 117,dockind,null), @documentid) != 0)
         result_protocol.AddString("Ошибка создания СОБУ в дату " + sql_convtypedate(DataSet.value("t_dealdate")));
         errrecs = errrecs + 1;
      else
         result_protocol.AddString("Проведена СОБУ в дату " + sql_convtypedate(DataSet.value("t_dealdate")) + " время " + time());
      end; 
      UseProgress(j=j+1);
   end;
   RemProgress;
   UseProgress(i = i + 1);
   /* СОВУ*/
   result_protocol.AddString("Начало формирования СОВУ" + time());

   sql = " select tick.t_dealdate from ddl_tick_dbt tick, dnotetext_dbt text where tick.t_bofficekind = "+dockind+" and tick.t_dealstatus =10 and tick.t_dealdate <= " + GetSQLDate(RunDate) +
                    " AND text.t_objecttype = case when tick.t_bofficekind = 127 then 101 else tick.t_bofficekind end and text.t_documentid = LPAD(tick.t_dealid, 34, '0') and text.t_notekind = 404 "+
                    " group by tick.t_dealdate "+
                    " order by tick.t_dealdate asc";
   cmd = RsdCommand(sql);
   DataSet = RsdRecordSet(cmd);
   InitProgress(SQL_GetNRecs(sql), "Проведение СОВУ");
   j = 0;
   while (DataSet.movenext())
      if (choice == 0)
         if(SC_InAccountingExecOp(sql_convtypedate(DataSet.value("t_dealdate")),"2011", @documentid) != 0)
            result_protocol.AddString("Ошибка создания СОВУ по зачислениям в дату " + sql_convtypedate(DataSet.value("t_dealdate")));
            errrecs = errrecs + 1;
         else
            result_protocol.AddString("Проведена СОВУ по зачислениям в дату " + sql_convtypedate(DataSet.value("t_dealdate")) + " время " + time());
         end;
         if(SC_InAccountingExecOp(sql_convtypedate(DataSet.value("t_dealdate")),"2010", @documentid) != 0)
            result_protocol.AddString("Ошибка создания СОВУ по списаниям в дату " + sql_convtypedate(DataSet.value("t_dealdate")));
            errrecs = errrecs + 1;
         else
            result_protocol.AddString("Проведена СОВУ по списаниям в дату " + sql_convtypedate(DataSet.value("t_dealdate")) + " время " + time());
         end;
      else
         if(SC_InAccountingExecOp(sql_convtypedate(DataSet.value("t_dealdate")),"12021", @documentid) != 0)
            result_protocol.AddString("Ошибка создания СОВУ по погашениям в дату " + sql_convtypedate(DataSet.value("t_dealdate")));
            errrecs = errrecs + 1;
         else
            result_protocol.AddString("Проведена СОВУ по погашениям в дату " + sql_convtypedate(DataSet.value("t_dealdate")) + " время " + time());
         end;
      end;
      UseProgress(j=j+1);
   end;
   Remprogress();
   UseProgress(i = i + 1);
   Remprogress();

   for (item, OperDateAr)
      cmd1 = RsdCommand("update dcurdate_dbt  \n" +
          "set t_phase = 9999, t_minphase = 9999, t_maxphase = 9999, t_isclosed = chr(88) where t_branch = 1 and  \n" +
          "t_curdate  =  ? \n" +
          "and t_isclosed = chr(0); " );
      cmd1.addParam("", RSDBP_IN, item);
      cmd1.execute();
      result_protocol.AddString(" Закрыт опердень " + item);
   end;

   
   return true;

OnError(er)
      result_protocol.AddString("Ошибка при проведении сделок зачисления ' " + er.Message);
      return false;

End;
//runLoadLot2();