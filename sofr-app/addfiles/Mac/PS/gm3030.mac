/*
$Name:             gm3030.mac
$Module:           РКО юридических лиц
$Description:      Операция - 3030 - "Открытие лицевого счета"  
                   Шаг      - 90   - "Формирование сообщения об открытии счета" 
*/

IMPORT reqinter, PSInter, InsCarryDoc, CTInter;

var ReqOpenAccObj : RsbReqOpenAcc;

RECORD req( reqopena );
var TB_ReqOpenAcc = TBFile("reqopena.dbt",  "R", 0, "reqopena.dbt",  "bank.def");

MACRO ExecuteStep(doc, pmdoc)
  var stat : integer = 0;
  TB_ReqOpenAcc.rec.RequestID = ReqOpenAccObj.RequestID;

  if( TB_ReqOpenAcc.GetEQ() )
    Copy( req, TB_ReqOpenAcc);
    req.Account = ReqOpenAccObj.Account; /*Номер счета еще может быть не сохранен в базе*/
  end;

  stat = InsertOpenAccountMessage( req );

  if( stat == ОтказОтГенерацииСообщения )
    return 1;
  end;

  if( stat != 0 )
    DisplayError();
    MsgBox( "Ошибка при создании сообщения об открытии счета " + req.Account );
    return  1;
  end;

  return stat;
END;

MACRO PostStep( message,     /* данный параметр может принимать следующие       */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                FirstDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                ID_Step,     /* внутренний идентификатор шага операции          */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep )   /* вид шага операции                               */

  var stat = 0;

  Array Text, Buttons;
  Text(0) = "Печатать сообщения?";
  Buttons( 0 ) = "Да";
  Buttons( 1 ) = "Нет";
  var CopyCount = 0;

  if( ( errTrn == 0 ) and ( message == 1 ) )// на выполнении шага
    if( not IsOprMultiExec and 
        (ConfWin( Text, Buttons, 1 ) == 0) and 
        CheckTypeAccPSRegVal(ReqOpenAccObj.Type_Account, "ТИПЫ_СЧЕТОВ") 
      )
      SetKindRegOrgan( REG_ORGAN_KIND_FNS );
      stat = ReqAccPrintMessage( ReqOpenAccObj.RequestID,
                                 OBJTYPE_REQOPENA );
      if( stat == 0 )
        SetKindRegOrgan( REG_ORGAN_KIND_PFR );
        stat = ReqAccPrintMessage( ReqOpenAccObj.RequestID,
                                   OBJTYPE_REQOPENA );
      end;
      if( stat == 0 )
        SetKindRegOrgan( REG_ORGAN_KIND_FSS );
        stat = ReqAccPrintMessage( ReqOpenAccObj.RequestID,
                                   OBJTYPE_REQOPENA );
      end;
      SetKindRegOrgan( REG_ORGAN_KIND_NON );
    end;
  end;
  return stat;
END;