/* ╔══════════════════════════════════════════════════════════════════╗ */
/*         Автоматизированная банковская система RS-Bank v5.10          */
/*                 Copyright (c) R-Style Software Lab 2001              */
/*                                                                      */
/*  Имя файла        : bcall.mac                                        */
/*                                                                      */
/*  Описание         : Общие функции для операция с валютой             */
/*                                                                      */
/*  Программист      : Литворенко А.А.                                  */
/*                                                                      */
/*  Создан           : 10.08.2001                                       */
/*                                                                      */
/* ╚══════════════════════════════════════════════════════════════════╝ */
import PTInter, календарь, FIInter, PSBCInter;

/* Для связок */ 
/* может быть привязка или к поручению на покупку валюты */
var KindDoc_BuyCurrency = 200;
/* или может быть привязка или к ответному платежу */
var KindDoc_Pm_Paym     = 322;

macro PSBC_GetAcc( ID, FIID, Symb, Account )
     var acc:TBFile;
     acc = Tbfile ("account.dbt", "R", 2, "account.dbt");

     acc.rec.Chapter    = 1;
     acc.rec.Open_Close = "";
     acc.rec.Client     = ID;

     if (acc.GetGE())
        if( (Index ( acc.rec.Type_Account, Symb)) and 
            (acc.rec.Code_Currency == FIID) and 
            (acc.rec.Client     == ID) and
            (acc.rec.Open_Close == "") and
            (acc.rec.Chapter    == 1 ) )
          SetParm (3, acc.rec.Account);
          return 1;
        end;

        if( (acc.rec.Client     != ID) )
          return 0;
        end;

        while ( acc.next () )
           if( (Index ( acc.rec.Type_Account, Symb)) and 
               (acc.rec.Code_Currency == FIID) and 
               (acc.rec.Client     == ID) and
               (acc.rec.Open_Close == "") and
               (acc.rec.Chapter    == 1 ) )
             SetParm (3, acc.rec.Account);
             return 1;
           end;

           if( (acc.rec.Client     != ID) )
             return 0;
           end;

        end;
     end;

     return 0;
end;

/* получить текущий валютный счет клиента */
macro ПолучитьРС_Клиента (Ид_Клиента, Cчет)
    var _Cчет = "";
    var stat = PSBC_GetAcc(Ид_Клиента, 0, "Ч", _Cчет);
    if (stat == 0)
       msgbox ("Не найден расчетный счет клиента");
    end;
    SetParm (1, _Cчет);
    return stat;
end;

/* получить текущий валютный счет клиента */
macro ПолучитьТкВС_Клиента (Ид_Клиента, Валюта, Cчет)
    var _Cчет = "";
    var stat = PSBC_GetAcc(Ид_Клиента, Валюта, "X", _Cчет);
    if (stat == 0)
       msgbox ("Не найден текущий валютный счет клиента в валюте ", ПолучитьКодФинИн(Валюта));
    end;
    SetParm (2, _Cчет);
    return stat;
end;

/* получить транзитный валютный счет клиента */
macro ПолучитьТВС_Клиента (Ид_Клиента, Валюта, Cчет)
    var _Cчет = "";
    var stat = PSBC_GetAcc(Ид_Клиента, Валюта, "Y", _Cчет);
    if (stat == 0)
       msgbox ("Не найден транзитный валютный счет клиента в валюте ", ПолучитьКодФинИн(Валюта));
    end;
    SetParm (2, _Cчет);
    return stat;
end;

/* получить спецтранзитный валютный счет клиента */
macro ПолучитьСТВС_Клиента (Ид_Клиента, Валюта, Cчет)
    var _Cчет = "";
    var stat = PSBC_GetAcc(Ид_Клиента, Валюта, "Z", _Cчет);
    if (stat == 0)
       msgbox ("Не найден спецтранзитный валютный счет клиента в валюте ", ПолучитьКодФинИн(Валюта));
    end;
    SetParm (2, _Cчет);
    return stat;
end;

/* получить тип счет (спец)транзитный */
macro ПолучитьТипСчета (Cчет, Валюта, Глава)
   file acc( "account.dbt" ) key 0;

   acc.Chapter = Глава;
   acc.Account = Cчет;
   acc.Code_Currency  = Валюта;

   if (GetEQ(acc))
      if( (Index ( acc.Type_Account, "Y")) )
          return 1;
      end;  
      if( (Index ( acc.Type_Account, "Z")) )
          return 2;
      end;  
      msgbox("Счет ", Cчет," не является транзитным или спецтранзитным");
   else
      msgbox("Счет ", Cчет," не найден");
   end;
   return 0;
end;

/* узнать резидент или нет ? */
macro Клиент_Резидент (Ид_Клиента)
   var resident = true;
   file party ("party.dbt") key 0;
   party.PartyID = Ид_Клиента;
   if (GetEQ(party))
     if (party.NotResident == "X")
        resident = false;
     end;
   else 
     msgbox("Клиент не найден");
   end;
   return resident;
end;


macro ПолучитьПредыдущиеРасходыЗадепонированныхСредств (Ид_поручения)
   file psbc_execute ("ps_bcexe.dbt") key 1;
   psbc_execute.OrderID = Ид_поручения;
   psbc_execute.BCexeID = 0;
   var Расход = 0;

   if ((GetGE(psbc_execute)) and (psbc_execute.OrderID == Ид_поручения))
      Расход = Расход + psbc_execute.UsedAmount;
      while ( next (psbc_execute))
          if (psbc_execute.OrderID != Ид_поручения)
             return Расход;
          end;
          Расход = Расход + psbc_execute.UsedAmount;
      end;
   end;

   return Расход;
end;

macro ПолучитьВладельцаСчета (Глава, Счет, Валюта)
   file account ("account.dbt") key 0;

   account.Chapter = Глава;
   account.Account = Счет;
   account.Code_Currency = Валюта;

   if (GetEQ(account))
      return account.Client;
   end;

   return 0;
end;

macro ПолучитьДатуПоследнегоИсполнениеПоПоручению (Ид_поручения, Дата)
   file psbc_execute ("ps_bcexe.dbt") key 1;

   psbc_execute.OrderID = Ид_поручения;
   psbc_execute.BCexeID = 2147483646; /*По идее масимальный ид исполнения*/

   if ((GetLE(psbc_execute)) and (psbc_execute.OrderID == Ид_поручения))
      SetParm(1, psbc_execute.ExecDate);
      return 1;
   else
      msgbox("Исполнение не найдено");
   end;
   return 0;
end;

macro ПолучитьСуммуВозврата( DocID, DocKind, удовлетворенная_сумма )
   return удовлетворенная_сумма - CalculatePSBCKivtSum( DocKind, DocID );
end;

/* получить хоть какую-нибудь торговую площадку */
macro ПолучитьТорговуюПлощадку ( Ид_ТорговойПлощадки )
   file partyown ("partyown.dbt") key 1;

   partyown.PartyKind = PTK_MARKETPLASE;
   partyown.PartyID = 0;

   if (GetGE(partyown))
       setparm(0, partyown.PartyID);
   end;
end;

