 /*
 $Name: psindchg.mac
 $Module: Ядро Banking
 $Description: Макрос шага 10 - "Изменение реквизитов"
 */


//-----------------------------------------------------------------------------
// Блок      : 29021 - "Изменение реквизитов"
// Шаг       : 10    - "Изменение реквизитов"
// Назначение: Макрос шага
// Описание  : Макрос шага
//-----------------------------------------------------------------------------
import PaymInter, pm_categ, PayInter, "SetPaymTrnDef.mac";

var IsMassProc:bool; // Исполняется из процедуры?


private macro СчетСвязанС_КУ_ОжидИсполн( Account:string, FIID:integer, Chapter:integer )
  
  var query:string = "SELECT 1        " +
                     "  FROM DUAL     " +
                     " WHERE EXISTS ( " +
                                      "SELECT /*+FIRST_ROWS(1)*/ 1                    " +
                                      "  FROM dmccateg_dbt cat, dmcaccdoc_dbt doc     " +
                                      " WHERE cat.t_number   =  113 /*Ожид. Исполн.*/ " +
                                      "   AND doc.t_catid    =  cat.t_id              " +
                                      "   AND doc.t_account  = :Account               ";
  if( FIID and FIID != 0 )
    query = query + " AND doc.t_currency = :FIID ";
  end;
    
  if( Chapter and Chapter != 0 )
    query = query + " AND doc.t_chapter = :Chapter ";
  end;     

  query = query + "   AND ROWNUM <= 1 ) " ;

  var params:TArray = makeArray( SQLParam( "Account", Account ) );
        
  if( FIID and FIID != 0 )
    params[ params.size ] = SQLParam( "FIID", FIID );
  end;
        
  if( Chapter and Chapter != 0 )
    params[ params.size ] = SQLParam( "Chapter", Chapter );
  end;
          
  var rs:RsdRecordset = execSQLselect( query, params, true );

  if( rs and rs.moveNext() )
    return true;
  else
    return false;
  end;

end;

PRIVATE CLASS (TRsdRecordReader) TMassReMakeIndexCarryData

  var PaymentID:integer, 
      ID_Operation:integer,
      ID_Step:integer,
      OldPayerAccount:string,
      NewPayerAccount:string,
      PaymStatus:integer,
      ValueDate:date,
      NumberPack:integer,
      Number:string,
      Department:integer,
      FIID_FuturePayAcc:integer,
      FuturePayerAmount:money,
      FutureReceiverAmount:money,
      LinkOldAccount:string,
      LinkNewAccount:string,
      SfContrID:integer,
      DocKind:integer,
      PayerFIID:integer,
      Payer:integer,
      PayDate:date,
      BaseFIID:integer,
      FutureBaseAmount:money;
END;

// Проводки шага "Изменение реквизитов"
macro PM_MassReMakeIndexCarry()  
                                  
  var query:string = 
     " select pm.t_PaymentID                                                                                              "
     "       ,t.t_ID_Operation                                                                                            "
     "       ,t.t_ID_Step                                                                                                 "
     "       ,pm.t_PayerAccount as t_OldPayerAccount                                                                      "
     "       ,h.t_PayerAccount  as t_NewPayerAccount                                                                      "
     "       ,pm.t_PaymStatus                                                                                             "
     "       ,pm.t_ValueDate                                                                                              "
     "       ,pm.t_NumberPack                                                                                             "+
     "       ,rm.t_Number                                                                                                 "
     "       ,pm.t_Department                                                                                             "
     "       ,pm.t_FIID_FuturePayAcc                                                                                      "
     "       ,pm.t_FuturePayerAmount                                                                                      "
     "       ,pm.t_FutureReceiverAmount                                                                                   "
     "       ,nvl((select oliwp.t_AttrID                                                                                  "+
     "               from dobjlink_dbt oliwp                                                                              "
     "               where oliwp.t_GroupID = decode(pm.t_PaymStatus, 2100, 49, 2)                                         "                                     
     "                 and oliwp.t_ObjectType = 4                                                                         "
     "                 and oliwp.t_ObjectID = lpad(pm.t_Chapter, 2, '0') || lpad(pm.t_FIID, 7, '0') || pm.t_PayerAccount  "
     "                 and oliwp.t_ValidFromDate <= RsbSessionData.curdate                                                "
     "                 and oliwp.t_ValidToDate >= RsbSessionData.curdate                                                  "
     "                 and oliwp.t_AttrType = 4                                                                           "
     "                 and ROWNUM = 1), chr(1)) as t_LinkOldAccount                                                       "          
     "       ,nvl((select oliwp.t_AttrID                                                                                  "
     "               from dobjlink_dbt oliwp                                                                              "
     "               where oliwp.t_GroupID(+) = decode(pm.t_PaymStatus, 2100, 49, 2)                                      "                                         
     "                 and oliwp.t_ObjectType(+) = 4                                                                      "+
     "                 and oliwp.t_ObjectID = lpad(pm.t_Chapter, 2, '0') || lpad(pm.t_FIID, 7, '0') || h.t_PayerAccount   "
     "                 and oliwp.t_ValidFromDate <= RsbSessionData.curdate                                                "
     "                 and oliwp.t_ValidToDate >= RsbSessionData.curdate                                                  "
     "                 and oliwp.t_AttrType = 4                                                                           "
     "                 and ROWNUM = 1), chr(1)) as t_LinkNewAccount                                                       "             
     "       ,case                                                                                                        "
     "          when pm.t_PaymStatus = 2100 then                                                                          "
     "            nvl((select sf.t_ID                                                                                     "
     "                   from dsfcontr_dbt sf                                                                             "
     "                   where sf.t_ServKind = 3                                                                          "+
     "                     and sf.t_ObjectType = 1                                                                        "
     "                     and sf.t_FIID = pm.t_FIID_FuturePayAcc                                                         "
     "                     and sf.t_Object = h.t_PayerAccount                                                             "
     "                     and ROWNUM = 1), 0)                                                                            "
     "          else                                                                                                      "
     "            0                                                                                                       "
     "        end as t_SfContrID                                                                                          "
     "       ,pm.t_DocKind                                                                                                "
     "       ,pm.t_FIID as t_PayerFIID                                                                                    "
     "       ,pm.t_Payer                                                                                                  "
     "       ,rm.t_PayDate                                                                                                "
     "       ,pm.t_BaseFIID                                                                                               "
     "       ,pm.t_FutureBaseAmount                                                                                       "
     " from V_PMMASSOPFOREXE t,                                                                                           "
     "      dpmpaym_dbt pm,                                                                                               "+
     "      dpmrmprop_dbt rm,                                                                                             "
     "      dpminhist_tmp h                                                                                               "
     " where pm.t_PaymentID = t.t_PaymentID                                                                               "
     "   and rm.t_PaymentID = t.t_PaymentID                                                                               "
     "   and pm.t_PayerAccount != h.t_PayerAccount                                                                        "
     "   and h.t_PaymentID = t.t_PaymentID                                                                                "
     "   and pm.t_PaymStatus in( 1000, 2100, 2000 )                                                                             ";

  var ErrorMessage:string = "";
  var CarryData:TMassReMakeIndexCarryData = TMassReMakeIndexCarryData();
  var Carry:RsbAccTransaction;
  var NotBalCorrAcc:string = ""; // Внебалансовый счет картотек
  var LinkOldAccount:string = ""; // Связанный внебалансовый счет картотеки для старого счета плательщика
  var LinkNewAccount:string = ""; // Связанный внебалансовый счет картотек для нового счета плательщика
  var IWPCategNewAccount:string = ""; // Счет КУ "Карт ОР", полученный для нового счета
  var NatCurSum:money = $0;
  var accTrnData : RsbAccTransactionData;
  var batchTrn:RsbBatchPaymTrn;

  var Status_After = ACCTRN_STATUS_DOCUMENT;
  var ErrorStatus = 0;
  
  // Для всех документов К2 проверим наличие связи нового счета и внебалансового счета К2
  // если связь не найдена, то открываем новый счет и связываем его с новым
  if(not MassCreateNotBalAccountIndex2ForChDoc())
    ErrorStatus = 1;
  end;
  
  if( not ErrorStatus )  

    var rs:RsdRecordset = execSQLselect( query );

    while( rs.moveNext() )
     
      CarryData.Read( rs );

      ErrorMessage = "";
      ErrorStatus = 0;

      LinkOldAccount = SubStr( CarryData.LinkOldAccount, 10 );
      LinkNewAccount = SubStr( CarryData.LinkNewAccount, 10 );

      // Данный счет не зависит от платежа, поэтому получаем его один раз
      if( NotBalCorrAcc == "")
        NotBalCorrAcc = NotBalCorrAcc_FirstDoc( "П" ).FindAndOpenSysAccount( "ВнебалСчетКорресп", IsOprMultiExec() );
      end;

      if( CarryData.PaymStatus == PM_IWPPLACED )

        if( (ErrorMessage == "") and (CarryData.LinkOldAccount == "") )
          ErrorMessage = "Не найден внебалансовый счет КОР для " + CarryData.OldPayerAccount;
          ErrorStatus = 1;
        end;

        if( (ErrorMessage == "") and (CarryData.SfContrID == 0) )
          ErrorMessage = "Не найден договор обслуживания нового счета плательщика.";
          ErrorStatus = 1;
        end;

        if( ErrorMessage == "" )
          IWPCategNewAccount = TIndexWPPrimDocMass( CarryData.PaymentID, 
                                                    CarryData.DocKind, 
                                                    CarryData.PayerFIID, 
                                                    CarryData.Payer, 
                                                    {OperDprtNode},//CarryData.Department, 
                                                    CarryData.PayDate, 
                                                    CarryData.SfContrID, 
                                                    CarryData.BaseFIID ).FindAndOpenSysAccount( "Карт ОР", IsOprMultiExec() );
        end;

        if( (ErrorMessage == "") and (LinkNewAccount != "") and (LinkNewAccount != IWPCategNewAccount) )
          ErrorMessage = "Счет КУ \"Карт ОР\" " + IWPCategNewAccount + " для нового счета плательщика " + CarryData.NewPayerAccount + " отличается от связанного " + LinkNewAccount;
          ErrorStatus = 1;
        end;

        if( ErrorMessage == "" )
          
          accTrnData.Chapter         = 3;                                            
          accTrnData.Date_Carry      = CarryData.ValueDate;  
          accTrnData.Number_Pack     = CarryData.NumberPack;
          accTrnData.Numb_Document   = CarryData.Number;
          accTrnData.ResultCarry     = 1;
          accTrnData.Kind_Oper       = " 1";
          accTrnData.Department      = CarryData.Department;
          accTrnData.AccountPayer    = NotBalCorrAcc;
          accTrnData.AccountReceiver = LinkOldAccount;
          accTrnData.FIID            = CarryData.FIID_FuturePayAcc;
          accTrnData.Sum             = CarryData.FuturePayerAmount;
          accTrnData.Ground          = "Перенос остатка";
          
          if( ErrorMessage == "" )
            ErrorStatus = SetPaymTransactionDef(accTrnData, CarryData.PaymentID, 0, CarryData );
          end;

          if(ErrorStatus)
           ErrorMessage = GetErrMsg();

           if(not ErrorMessage)
             ErrorMessage = "Ошибка при установке атрибутов проводки по платежу";
           end;
          end;
        
          if( batchTrn.AddTransaction( CarryData.PaymentID, accTrnData, Status_After, PM_CARRY_REASON_EXECUTION, 0, CarryData.ID_Operation, CarryData.ID_Step ) < 0 )
            ErrorMessage = "Ошибка проводки по списанию с внебаланса КОР";
            ErrorStatus = 1;
          end;
        
        end;
                      
        if( ErrorMessage == "" )
        
          accTrnData = RsbAccTransactionData();

          accTrnData.Chapter         = 3;                                            
          accTrnData.Date_Carry      = CarryData.ValueDate;
          accTrnData.Number_Pack     = CarryData.NumberPack;
          accTrnData.Numb_Document   = CarryData.Number;
          accTrnData.ResultCarry     = 1;
          accTrnData.Kind_Oper       = " 1";
          accTrnData.Department      = CarryData.Department;
          accTrnData.AccountPayer    = IWPCategNewAccount;
          accTrnData.AccountReceiver = NotBalCorrAcc;
          accTrnData.FIID            = CarryData.FIID_FuturePayAcc;
          accTrnData.Sum             = CarryData.FuturePayerAmount;
          accTrnData.Ground          = "Перенос остатка";

          if( ErrorMessage == "" )
            ErrorStatus = SetPaymTransactionDef(accTrnData, CarryData.PaymentID, 0, CarryData );
          end;

          if(ErrorStatus)
            ErrorMessage = GetErrMsg();

            if(not ErrorMessage)
              ErrorMessage = "Ошибка при установке атрибутов проводки по платежу";
            end;
          end;  
          
          if( batchTrn.AddTransaction( CarryData.PaymentID, accTrnData, Status_After, PM_CARRY_REASON_EXECUTION, 0, CarryData.ID_Operation, CarryData.ID_Step ) < 0 )
            ErrorMessage = "Ошибка проводки по зачислению на внебаланс КОР";
            ErrorStatus = 1;
          end;

        end;

        // Если счет КОР для нового счета не связан, то вставляем связь
        if((ErrorMessage == "") and (CarryData.LinkNewAccount == ""))
          SetLinkedAccountTrn( CarryData.NewPayerAccount, CarryData.FIID_FuturePayAcc, 1, IWPCategNewAccount, CarryData.FIID_FuturePayAcc, 3, OBJROLE_ACC_IWPOBACC );
        end;

      elif( CarryData.PaymStatus == PM_I2PLACED )
        
        if( (ErrorMessage == "") and (CarryData.LinkOldAccount == "") )
          ErrorMessage = "Не найден внебалансовый счет К2 для " + CarryData.OldPayerAccount;
          ErrorStatus = 1;
        end;
        
        if( (ErrorMessage == "") and (CarryData.LinkNewAccount == "") )
          ErrorMessage = "Не найден внебалансовый счет К2 для " + CarryData.NewPayerAccount;
          ErrorStatus = 1;
        end;

        if( ConvSum( NatCurSum, CarryData.FutureBaseAmount, CarryData.ValueDate, CarryData.BaseFIID, NATCUR ) )
          ErrorMessage = "Ошибка конвертации суммы";          
          ErrorStatus = 1;
        end;
        
        if( ErrorMessage == "" )
        
          accTrnData = RsbAccTransactionData();

          accTrnData.Chapter         = 3;                                            
          accTrnData.Date_Carry      = CarryData.ValueDate;
          accTrnData.Number_Pack     = CarryData.NumberPack;
          accTrnData.Numb_Document   = CarryData.Number;
          accTrnData.ResultCarry     = 1;
          accTrnData.Kind_Oper       = " 1";
          accTrnData.Shifr_Oper      = "09";
          accTrnData.Department      = CarryData.Department;
          accTrnData.AccountPayer    = NotBalCorrAcc;
          accTrnData.AccountReceiver = LinkOldAccount;

          accTrnData.FIIDPayer       = NATCUR; 
          accTrnData.FIIDReceiver    = CarryData.BaseFIID;
           
          accTrnData.SumPayer    = NatCurSum;
          accTrnData.SumReceiver = CarryData.FutureBaseAmount;

          accTrnData.Ground          = "Перенос остатка";

          if( ErrorMessage == "" )
            ErrorStatus = SetPaymTransactionDef(accTrnData, CarryData.PaymentID, 0, CarryData );            
          end;

          if(ErrorStatus)
            ErrorMessage = GetErrMsg();

            if(not ErrorMessage)
              ErrorMessage = "Ошибка при установке атрибутов проводки по платежу";              
            end;
          end;  
          
          if( batchTrn.AddTransaction( CarryData.PaymentID, accTrnData, Status_After, PM_CARRY_REASON_EXECUTION, 0, CarryData.ID_Operation, CarryData.ID_Step ) < 0 )
            ErrorMessage = "Ошибка проводки по списанию с внебаланса К2";
            ErrorStatus = 1;
          end;
        end;

        if( ErrorMessage == "" )
          
          accTrnData = RsbAccTransactionData();

          accTrnData.Chapter         = 3;                                            
          accTrnData.Date_Carry      = CarryData.ValueDate;
          accTrnData.Number_Pack     = CarryData.NumberPack;
          accTrnData.Numb_Document   = CarryData.Number;
          accTrnData.ResultCarry     = 1;
          accTrnData.Kind_Oper       = " 1";
          accTrnData.Shifr_Oper      = "09";
          accTrnData.Department      = CarryData.Department;
          accTrnData.AccountPayer    = LinkNewAccount;
          accTrnData.AccountReceiver = NotBalCorrAcc;
          
          accTrnData.FIIDPayer       = CarryData.BaseFIID; 
          accTrnData.FIIDReceiver    = NATCUR;

          accTrnData.SumPayer = CarryData.FutureBaseAmount;
          accTrnData.SumReceiver = NatCurSum;
          accTrnData.Ground          = "Перенос остатка";

          if( ErrorMessage == "" )
            ErrorStatus = SetPaymTransactionDef(accTrnData, CarryData.PaymentID, 0, CarryData );
          end;

          if(ErrorStatus)
            ErrorMessage = GetErrMsg();

            if(not ErrorMessage)
              ErrorMessage = "Ошибка при установке атрибутов проводки по платежу";
            end;
          end;  
          
          if( batchTrn.AddTransaction( CarryData.PaymentID, accTrnData, Status_After, PM_CARRY_REASON_EXECUTION, /*CarryData.PmAddPIID*/0, CarryData.ID_Operation, CarryData.ID_Step ) < 0 )
            ErrorMessage = "Ошибка проводки по зачислению на внебаланс К2";
            ErrorStatus = 1;
          end;
        end;

      elif( CarryData.PaymStatus == PM_READIED )

        var selectQuery = " SELECT T_ACCTRNID, T_PAYERAMOUNT, T_PAYERACCOUNT " + 
                          " FROM TABLE( PM_CARFUN.GetPaymentCarries( :PaymentID ) ) " +
                          " ORDER BY T_ACCTRNID DESC ";
        var selectParams = makeArray(SQLParam( "PaymentID", CarryData.PaymentID ));
        var selectRs:RsdRecordset = execSQLselect( selectQuery, selectParams, true );

        if( selectRs and selectRs.MoveNext() )

          if( СчетСвязанС_КУ_ОжидИсполн( selectRs.value(2/*"T_PAYERACCOUNT"*/) ) )

            if( ErrorMessage == "" )

              if( ConvSum( NatCurSum, selectRs.value(1/*"T_PAYERAMOUNT"*/), CarryData.ValueDate, CarryData.BaseFIID, NATCUR ) )
                ErrorMessage = "Ошибка конвертации суммы";
                ErrorStatus = 1;
              end;

              accTrnData = RsbAccTransactionData();

              accTrnData.Chapter         = 3;                                            
              accTrnData.Date_Carry      = {curdate};
              accTrnData.Number_Pack     = CarryData.NumberPack;
              accTrnData.Numb_Document   = CarryData.Number;
              accTrnData.ResultCarry     = 1;
              accTrnData.Kind_Oper       = " 1";
              accTrnData.Shifr_Oper      = "09";

              accTrnData.Department      = CarryData.Department;

              accTrnData.AccountPayer    = NotBalCorrAcc;
              accTrnData.AccountReceiver = selectRs.value(2/*"T_PAYERACCOUNT"*/);

              accTrnData.FIIDPayer       = NATCUR; 
              accTrnData.FIIDReceiver    = CarryData.BaseFIID;
               
              accTrnData.SumPayer        = NatCurSum;
              accTrnData.SumReceiver     = selectRs.value(1/*"T_PAYERAMOUNT"*/);

              accTrnData.Ground          = "Перенос средств по внебалансу при изменении счета плательщика";

              if( ErrorMessage == "" )
                ErrorStatus = SetPaymTransactionDef(accTrnData, CarryData.PaymentID, 0, CarryData );
              end; 

              if(ErrorStatus)
                ErrorMessage = GetErrMsg();

                if(not ErrorMessage)
                  ErrorMessage = "Ошибка при установке атрибутов проводки по платежу";
                end;
              end;  
              
              if( batchTrn.AddTransaction( CarryData.PaymentID, accTrnData, Status_After, PM_CARRY_REASON_EXECUTION, /*CarryData.PmAddPIID*/0, CarryData.ID_Operation, CarryData.ID_Step ) < 0 )
                ErrorMessage = "Ошибка проводки при переносе средств по внебалансу при изменении счета плательщика";
                ErrorStatus = 1;
              end;
            end;

            if( ErrorMessage == "" )

              if( ConvSum( NatCurSum, CarryData.FutureBaseAmount, {curdate}, CarryData.BaseFIID, NATCUR ) )
                ErrorMessage = "Ошибка конвертации суммы";
                ErrorStatus = 1;
              end;

              accTrnData = RsbAccTransactionData();

              accTrnData.Chapter         = 3;                                            
              accTrnData.Date_Carry      = {curdate};
              accTrnData.Number_Pack     = CarryData.NumberPack;
              accTrnData.Numb_Document   = CarryData.Number;
              accTrnData.ResultCarry     = 1;
              accTrnData.Kind_Oper       = " 1";
              accTrnData.Shifr_Oper      = "09";

              accTrnData.Department      = CarryData.Department;

              accTrnData.AccountPayer    = TIndexWaitExec(CarryData.PaymentID).FindAndOpenSysAccount( "Ожид. Исполн.", IsOprMultiExec() );
              accTrnData.AccountReceiver = NotBalCorrAcc;                  

              accTrnData.FIIDPayer       = CarryData.BaseFIID; 
              accTrnData.FIIDReceiver    = NATCUR;
               
              accTrnData.SumPayer        = CarryData.FutureBaseAmount;
              accTrnData.SumReceiver     = NatCurSum;

              accTrnData.Ground          = "Перенос средств по внебалансу при изменении счета плательщика";
              
              if( ErrorMessage == "" )
                ErrorStatus = SetPaymTransactionDef(accTrnData, CarryData.PaymentID, 0, CarryData );
              end; 

              if(ErrorStatus)
                ErrorMessage = GetErrMsg();

                if(not ErrorMessage)
                  ErrorMessage = "Ошибка при установке атрибутов проводки по платежу";
                end;
              end;  
              
              if( batchTrn.AddTransaction( CarryData.PaymentID, accTrnData, Status_After, PM_CARRY_REASON_EXECUTION, /*CarryData.PmAddPIID*/0, CarryData.ID_Operation, CarryData.ID_Step ) < 0 )
                ErrorMessage = "Ошибка проводки при переносе средств по внебалансу при изменении счета плательщика";
                ErrorStatus = 1;
              end;
            end;

          end;  
        end;
      end;
    
      if( ErrorStatus )
        SetErrorOprTemp( CarryData.PaymentID, ErrorStatus, ErrorMessage );
      end; 
    
    end;
    
    if( not batchTrn.Execute( false ) )
      batchTrn = NULL;      
    end;

    var errPaymentID:TArray;
    var errCode:TArray;
    var errMsg:TArray;

    if(not batchTrn.GetErrors(errPaymentID, errCode, errMsg) )
      batchTrn = NULL;
    end;   
    
    if( not batchTrn )
      SetErrorsOprTemp(errPaymentID, errCode, errMsg);  
    end;

  end;
  
  return ErrorStatus;
   
end;



/* Предтранзакционные действия */
macro PrepMassExecuteStep() 

  var stat:integer = execStoredFunc( "PM_CHDOC_STEP.MassStepPrepare", V_INTEGER );

  // Если изменение происходит через процедуру массового изменения,
  // то необходимо выполнить макрос скроллинга для каждого платежа :(
  if( not stat and IsMassProc )
    stat = RunScMacroForChangeDoc();
  end;

  return stat;

onerror(x)
  msgbox( x.Message );
  return 1;
end;

/* Транзакционные действия */
macro MassExecuteStep()

  /* Серверная часть до проводки */
  var ODBalance = {curdate}, ODBankServiceBalance = {curdate};
  var query = "select pm.t_Department " +
              "  from V_PMMASSOPFOREXE t, dpmpaym_dbt pm " +
              " where pm.t_PaymentID = t.t_PaymentID ";
  var rs = execSQLselect(query);
  if(rs and rs.moveNext)
    ODBalance = PM_GetOperDay_Balance( rs.value(0) );
    ODBankServiceBalance = PM_GetOperDay_BankServiceBalance( rs.value(0) );
  end;
  var stat:integer = execStoredFunc( "PM_CHDOC_STEP.MassStepExecute1", 
                                     V_INTEGER, 
                                     makeArray( SQLParam("p_ODBalance", ODBalance),
                                                SQLParam("p_ODBankServiceBalance", ODBankServiceBalance)
                                              )
                                   );
  // Проводки шага "Изменение реквизитов"
  // Сами проводки можно делать только в макросе, на сервере такой возможности нет
  if( not stat )
    stat = PM_MassReMakeIndexCarry();
  end;
  
  /* Серверная часть после проводки */
  if( not stat )
    stat = execStoredFunc( "PM_CHDOC_STEP.MassStepExecute2", V_INTEGER );
  end;

  return stat;

onerror(x)
  msgbox( x.Message );
  return 1;
end;

