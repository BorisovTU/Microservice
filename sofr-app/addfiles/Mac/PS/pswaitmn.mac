/*

 $Name:             pswaitmn.mac
 $Module:           РКО
 $Description:      Макрос шага "Ожидание поступлений"

*/

//-----------------------------------------------------------------------------
// Блок     : 29042 - "Ожидание поступлений"
// Шаг      : 10    - "Ожидание поступлений"
// Описание : Макрос шага
//-----------------------------------------------------------------------------
import PaymInter, cbsttls, OprInter, pm_common, pm_setst;

var PaymentObj:RsbPayment;
var InsInd2:bool;

// Выполнение шага
MACRO ExecuteStep( doc, payorder, DocKind:integer, ID_Operation:integer, ID_Step:integer )
 
  var DO_Segment;
  
  if(CheckDateStartOpr(ID_Operation))
    return 1;
  end;

  // Установить дату начала операции равной дате операционного дня пользователя
  SetOprDate(29000000, {curdate});
  PaymentObj.ValueDate = {curdate};
  PaymentObj.OutTransferDate = PmGetDefaultOutTransferDate( PaymentObj );

  if( InsInd2 )
  
    if( (PaymentObj.DocKind == PS_INRQ) and IsSetPsBCNumber( PaymentObj ) )
      DO_Segment = OPR_PM_ST_EXEC_IPVS;
    else
      DO_Segment = OPR_PM_ST_ENTER;
    end;

    if( УстановитьСтатусыПлатежа( OPR_PAYM_INDEX, OPR_PAYM_ST_INDEX_2 , 
                                  OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_CONTROL,
                                  OPR_PAYM_DO, DO_Segment) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;

  else

    PaymentObj.PlaceToIndex = "";
    
    if( PM_NeedRejectControl() == true )

      PaymentObj.PaymStatus = PM_REJECTED;
      PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_REJECTED );

      if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_REJECT ) )
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;

      if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, "Недостаточно средств на счете плательщика", {curdate} ) != 0 )
        msgbox( "Ошибка при вставке примечания платежа" );
        return 1;
      end;
    
    else
      var IndexStopDate : date = date(0, 0, 0); // Дата приостановления в картотеке
      if( IsPaymentStoppedInIndex(PaymentObj, @IndexStopDate) and
          (IndexStopDate > {curdate})
        )
        msgbox("Действие документа приостановлено с " + DDpMMpYYYY(IndexStopDate) + ". " +
               "Документ может быть перемещен в картотеку №2 или отвергнут");
        return 1;
      end;

      if( УстановитьСтатусыПлатежа( OPR_PAYM_MANUAL, OPR_PAYM_ST_MANUAL_NOTNEED ) )
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
    end;

  end;

  return 0;

END;
