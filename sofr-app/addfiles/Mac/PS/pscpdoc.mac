/*
$Name:             pscpdoc.mac
$Module:           РКО юридических лиц
$Description:      Макрос скроллинга валютных платежных документов в РКО
*/
/***********************************************************************
          Автоматизированная банковская система RS-Bank v6.0           
                  Copyright (c) R-Style Software Lab                   
   Имя файла        : psrqdoc.mac
   Описание         : Макродействия валютных клиентских платежей
   Создан           : 01.03.2011                                       
***********************************************************************/

import pmbuff, globals, PaymInter, PTInter, lnpaym, check117, "pmchoper.mac","pm_tools.mac", "pm_opr.mac", pm_chksave, "pm_syscont.mac", "pmedit.mac";


/*номера полей в панели*/
const pscp_fld_Number      = 1,
      pscp_fld_Date        = 2,
      pscp_fld_DateValue   = 3,
      pscp_fld_BaseAmount  = 4,
      pscp_fld_ReceiverINN = 30,
      pscp_fld_Details     = 34,
      pscp_fld_ReceiverBankName = 23,
      pscp_fld_PayerBankName    = 18,
      pscp_fld_ReceiverName     = 29,
      pscp_fld_PayerName        = 12;
const pscp_fld_kzPayerCode    = 13,
      pscp_fld_kzReceiverCode = 30;

macro PS_CurPayOrderNewDoc()
  return 0;
end;

macro PS_CurPayOrderUserFunc( Режим:integer )
  return 1;
end;

macro PS_CurPayOrderCheckDoc( Режим )
  
  var stat     :integer = 0 ,
      note     :string  = "",
      save_note:string  = "",
      ErrStr   :string  = "",
      ret_val  :bool,
      OldDoc   :integer = 0;

  /*Проверяем номер для всех документов, в том числе и отложенных.*/           
  if ( (Режим == 2 ) or (Режим == 3) or (Режим == 8) ) 
    if( needUseKZpm() )
      if( CompareStrWithMasks("10-29&??", r_pmkz.PayerCode) )
        MsgBox("Некорректное значение КОд");
        return pscp_fld_kzPayerCode;
      end;
      if( CompareStrWithMasks("10-29&??", r_pmkz.ReceiverCode) )
        MsgBox("Некорректное значение Кбе");
        return pscp_fld_kzReceiverCode;
      end;
    end;
           
    if( (not IsUFEBS(r_credit, r_debet)) and (CheckINN(r_pmrmprop.ReceiverINN) > 0) )
      msgBox ("Ошибка в ИНН получателя");
      return pscp_fld_ReceiverINN;
    end;
    if( StrLen( r_pmrmprop.Ground ) == 0 )
       MsgBox("Не заданы детали платежа");
       return pscp_fld_Details;
    end;
    if( StrLen( r_pmrmprop.Number ) == 0 )
      MsgBox("Не задан номер документа");
      return pscp_fld_Number;
    end;

    if( r_pmpaym.BaseAmount < 0.0 )
      msgbox("Нельзя задавать отрицательную сумму");
      return pscp_fld_BaseAmount;
    end;

    if( PM_CheckPayments( r_pmpaym, NULL, r_credit, r_pmrmprop, 1 ) )
      return 1;
    end;

    if( StrLen( r_pmrmprop.Ground )>210 )                
      if ( RsbGetTrue( False,True,"Основание платежа превышает 210 символов.|Продолжить?")==False )                   
        return pscp_fld_Details;
      end;
    end;

    if( StrLen( r_pmrmprop.PartyInfo)>210-33 )
      if ( RsbGetTrue( False,True,"Информация участнику превышает 177 символов.|Продолжить?")==False )
        return 1;
      end;
    end;

    if( StrLen( r_pmrmprop.ReceiverBankName)>140 )
      if ( RsbGetTrue( False,True,"Наименование банка получателя превышает 140 символов.|Продолжить?")==False )
        return pscp_fld_ReceiverBankName;
      end;
    end;

    if( StrLen( r_pmrmprop.PayerBankName)>140 )
      if ( RsbGetTrue( False,True,"Наименование банка плательщика превышает 140 символов.|Продолжить?")==False )
        return pscp_fld_PayerBankName;
      end;
    end;

    if( StrLen( r_pmrmprop.ReceiverName)>160 )
      if ( RsbGetTrue( False,True,"Наименование получателя превышает 160 символов.|Продолжить?")==False )
        return pscp_fld_ReceiverName;
      end;
    end;

    if( StrLen( r_pmrmprop.PayerName)>160 )
      if ( RsbGetTrue( False,True,"Наименование плательщика превышает 160 символов.|Продолжить?")==False )
        return pscp_fld_PayerName;
      end;
    end;

    ErrStr = PM_CheckPaymAccounts( r_pmpaym, NULL, r_credit, r_pmrmprop, 1 );

    if( strlen(ErrStr) > 0 )
      msgbox( ErrStr );
      return 1;
    end;

    /* Общие проверки по списку */
    stat = PSCP_ScrolMacroCommonChecks( TPanelFields(), r_pmpaym, r_debet, r_credit, r_pmrmprop, r_pmserv );
    if( stat != NOTERROR )
      return stat;
    end;

    /* Проверить по 117-И */
    if( CheckOnSave_117( r_pmpaym, NULL, r_credit, NULL, r_pmrmprop ) )
      return pscp_fld_Number;
    end;

    /*проверка реквизитов валютной операции*/
    if(PM_CheckCO(r_pmpaym,r_pmrmprop,0,r_credit))
       return 1;
    end;
    
    if( stat ) return stat; end;

  end;
  
  if( (Режим == 2 ) or (Режим == 3) or (Режим == 8) or (Режим == 9) )
    if( ClientHasServKind( r_pmpaym.Payer, PTSK_PAY ))
      return 1;
    end;
  end;

  if( Режим == 1 ) /*УДАЛЕНИЕ ДОКУМЕНТА*/

    if(not isDLMRuning())
      if( r_pscpord.Origin == CP_OR_SF )
        msgbox("Документ является платой за обслуживание.|Удаление запрещено.");
        stat = 1;
      end;
      /* Документ из Клиент-Банк'а (или БОУРМ) */
      if( r_pscpord.Origin == CP_OR_CLB )
        save_note = note = readNoteForObject(OBJTYPE_PSCPORDER, makeObjectID(OBJTYPE_PSCPORDER, NULL, r_pscpord), NOTEKIND_PSCP_DELETEGROUND);
        ret_val = GetString(note, "Основание для удаления документа, полученного по системе \"Клиент-Банк\"", 68);
        if( ret_val and (save_note != note) )
          stat = addNoteForObject(OBJTYPE_PSCPORDER, makeObjectID(OBJTYPE_PSCPORDER, null, r_pscpord), NOTEKIND_PSCP_DELETEGROUND, note);
          if( stat )
            MsgBox("Ошибка добавления примечания к документу");
            return stat;
          end;
        end;
        if( not note )
          MsgBox("Документ получен по системе \"Клиент-Банк\".|Удаление без ввода основания запрещено.");
          return 1;
        end;
      end;
    end;

    if(CheckDeletePayment(r_pmpaym.PaymentID))
      return 1;
    end;
  elif( Режим == 2 )  /* ВВОД ДОКУМЕНТА */
  elif( Режим == 3 )  /* РЕДАКТИРОВАНИЕ ДОКУМЕНТА */
    if (StandartCheckUpdate(TCheckUpdateParam(r_pmpaym, r_debet, r_credit, r_pmrmprop), 
        TCheckUpdateParam(r_pmpaym_old, r_debet_old, r_credit_old, r_pmrmprop_old)) == CHANG_NOTKEEP)
      return CHANG_NOTKEEP;
    end;
  elif( Режим == 7 ) /*ПОМЕЩЕНИЕ ДОКУМЕНТА В ОТЛОЖЕННЫЕ, ОТКАТ ОПЕРАЦИИ*/
  elif( Режим == 8 )  /*ВВОД В ОТЛОЖЕННЫЕ*/
  elif(Режим == OBJ_AFTEREDIT) /* Проверка важности изменений влияющей на необходимость смены опера документа
                                  (изменения в буферах не сохраняются)*/
    return IsImportantChangeForOperPsCpOrder(r_pmpaym, r_pmpaym_old, r_pmrmprop, r_pmrmprop_old, r_credit, r_credit_old);
  end;

  return stat;
end;

