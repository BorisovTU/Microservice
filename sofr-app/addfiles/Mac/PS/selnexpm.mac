/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "РКО"                                        */
/*  Условия отбора неисполненных платежей по требованиям ФНС                */
/*                                                                          */
/*  Имя файла: selnexpm.mac                                                 */
/*  Создан:  02.08.11                                        Чукина Т.А.    */
/****************************************************************************/
import BankInter, MesInter, CTInter;

private const CHOICE_BUTTON_YES  = 0; // "Да"
private const CHOICE_BUTTON_NO = 1; // "Нет"

// Возвращает строку с условием WHERE для отбора платежей по требованию wlregdec (или ошибку)
// Остальные параметры - псевдонимы таблиц, которые используются при построении 
// списка платежей. В строке с условием должны использоваться именно эти псевдонимы
// Псевдоним "t" использовать нельзя
macro УсловиеПоискаНеисполненногоПлатежа
(
  wlregdec, 
  pmpaymAlias,    // псевдоним для таблицы dpmpaym_dbt
  pmrmpropAlias,  // псевдоним для таблицы dpmrmprop_dbt
  propDbAlias,    // псевдоним для таблицы dpmprop_dbt ( t_DebetCredit = PRT_Debet )
  propCrAlias,    // псевдоним для таблицы dpmprop_dbt ( t_DebetCredit = PRT_Credit )
  oproperAlias    // псевдоним для таблицы doproper_dbt
)
  record rgd (wlregdec);
  setbuff(rgd, wlregdec);

  record mes(wlmes);
  var NumF = "";

  if( (rgd.RecipientID <= 0) and 
      (ПолучитьСообщение( rgd.DecisionID, OBJTYPE_FNSDEMAND, 0, mes, NULL )) and
      (ПолучитьПоле( mes.MesID, "НомФ", NumF )) )
    msgbox( "По БИК банка ", rgd.RecipientCode, " и номеру филиала ", NumF, " не удалось определить филиал, в котором введен неисполненный документ" );
    return 1;
  end;
    
  array Text;
  array Buttons;
  if (rgd.ClientID <= 0)
    Text(0) = "Не найден плательщик по неисполненному документу. Искать платежное поручение по номеру документа и дате?";
    Buttons( CHOICE_BUTTON_YES ) = " Да "; 
    Buttons( CHOICE_BUTTON_NO  ) = " Нет ";
    if ( ConfWin( Text, Buttons ) != CHOICE_BUTTON_YES )
      return 1;
    end;
  end;

  var s = "     " + propDbAlias   + ".t_BankCode = '" + rgd.RecipientCode + "'" +
          " AND " + propDbAlias   + ".t_CodeKind = " + rgd.RecipientCodeKind;
  if (rgd.ClientID > 0)
    s = s + " AND " + pmpaymAlias + ".t_Payer = " + rgd.ClientID;
  end;
  s = s + " AND " + pmrmpropAlias + ".t_ShifrOper IN ('01', '06', '16') " +
          " AND " + pmrmpropAlias + ".t_TaxAuthorState <> CHR (1) " +
          " AND " + pmrmpropAlias + ".t_Number = '" + rgd.LnkDocNumber + "'" +
          " AND " + pmrmpropAlias + ".t_Date = '" + rgd.LnkDocDate + "'";
  return s;
end;