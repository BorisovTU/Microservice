/*
$Name:             reqclosa.mac
$Module:           РКО юридических лиц 
$Description:      Инициализация и проверки заявлений на закрытие счета
*/
import BankInter, PSInter, reqinter, "pr_oppmclac.mac", "fnsaccmsg_lib.mac";

RECORD ReqCloseAcc(reqclosa);
RECORD OldReqCloseAcc(reqclosa);


var accbuf  :TRecHandler = TRecHandler( "account.dbt"  , "bank.def" );

const Fld_Account = 5,
      Fld_Series   = 17,
      Fld_PackNumb = 19,
      Fld_Client = 25;

/*Для платежей*/

/*Хинт для списков по дате заявления*/
private const Hint_ByDate     :string = "/*+FIRST_ROWS LEADING(t oproper rq_code rq_pt) INDEX(t dreqclosa_dbt_idx5) USE_NL(t oproper rq_code rq_pt)*/";
/*Хинт для списков по дате закрытия*/
private const Hint_ByCloseDate:string = "/*+FIRST_ROWS LEADING(t oproper rq_code rq_pt) INDEX(t dreqclosa_dbt_idx4) USE_NL(t oproper rq_code rq_pt)*/";
/*Хинт для списков по статусу первички*/
private const Hint_ByStatus   :string = "/*+FIRST_ROWS LEADING(t oproper rq_code rq_pt) INDEX(t dreqclosa_dbt_idx3) USE_NL(t oproper rq_code rq_pt)*/";
/*Хинт для списков по шагу*/
private const Hint_ByStep     :string = "/*+FIRST_ROWS LEADING(t oproper reqclosa rq_code rq_pt) INDEX(t doprstep_dbt_idx10) INDEX(reqclosa dreqclosa_dbt_idx0) USE_NL(t oproper reqclosa rq_code rq_pt)*/";

/* Установка подсказки для скролингов из макроса */
MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStates:integer ):string
  /*  Возможные значения ScrolStates:
      0  - Все                      
      1  - Отложенные               
      2  - Открытые                 
      3  - Закрытые                 
      4  - Отвергнутые              
      5  - Подготовленные к шагу */

  if( ScrolStates == 0 ) //Все

    return Hint_ByDate;

  elif( ScrolStates == 3 ) //Закрытые

    return Hint_ByCloseDate;  

  elif(    ( ScrolStates == 1 )   //Отложенные
        or ( ScrolStates == 2 )   //Открытые
        or ( ScrolStates == 4 ) ) //Отвергнутые
  
    return Hint_ByStatus;    

  elif( ScrolStates == 5 ) //Подготовленные к шагу

    return Hint_ByStep;

  end;

  return DefaultHint;
END;

MACRO Новое_Заявление()
  return 0;
END;

private const REQCLOSA_ST_CLOSED = 30;

private macro CheckOther()
  var select = "select t_CurrentState, t_Date, t_RequestID from dreqclosa_dbt " +
                      " where t_Code_Currency = " + ReqCloseAcc.Code_Currency +
                      "   and t_Account = '" + ReqCloseAcc.Account + "'" +
                      "   and t_RequestID <> " + ReqCloseAcc.RequestID;
  var rs = execSQLselect( select );
  if (rs and rs.MoveNext())
    if (rs.value("t_CurrentState") != REQCLOSA_ST_CLOSED)
      var res = ConfWin( makeArray("Заявление на закрытие счета " + ReqCloseAcc.Account + " от " + date(rs.value("t_Date")) +
              " уже существует и находится в стадии обработки. Сохранить заявление?"), 
              makeArray("Сохранить", "Продолжить редактирование", "Отказаться"));
      return res;  
    else
      var err:integer = 0;
      var locale:string = "";
      GetRegistryValue("CB\\PAYMENTS\\PANEL\\LOCALE", V_STRING, locale, err);
      var y1, y2: integer;
      datesplit(ReqCloseAcc.Date, NULL, NULL, y1);
      datesplit(date(rs.value("t_Date")), NULL, NULL, y2);
      if ( (locale == "RU") and (y2 >= y1) )
        var rs2 = execSQLselect( "select wlmes.t_Trn, wlmes.t_Date from dwlmes_dbt wlmes join dwlmeslnk_dbt wlml using(t_MesID) " +
            "where wlml.t_objKind = 451 " +/*OBJTYPE_REQCLOSA*/
              "and wlmes.t_State >= 30 " +/*WLD_STATUS_MES_SEND*/
              "and wlml.t_objID = " + rs.value("t_RequestID"));
        if (rs2 and rs2.MoveNext())
          res = ConfWin( makeArray("Заявление на закрытие счета " + ReqCloseAcc.Account + " от " + date(rs.value("t_Date")) +
              " уже существует. Заявление обработано и по нему отправлено сообщение в ФНС №" + rs2.value("t_Trn") +
              " от " + date(rs2.value("t_Date")) + ". Сохранить заявление?"), 
              makeArray("Сохранить", "Продолжить редактирование", "Отказаться"));
          return res;
        end;
      end;
    end;
  end;
end;

MACRO Проверить_Заявление(Режим)

  var BuffAccPaym:string = "";
  var Certifs = TArray;
  var Cert    = TArray;
  var stat = 0;
  Array Text, Buttons;
  Text(0) = "Есть неисполненные документы к счету " + ReqCloseAcc.Account + ". Показать? ";
  Buttons( 0 ) = "     Да     ";
  Buttons( 1 ) = "    Нет     ";
  
  // ввод/редактирование/сохранение в отложенных
  if ( (Режим == 2 ) or (Режим == 3) or (Режим == 8) )
    var errList = "";
    var IsIndividEmployer;
    var AttrPartyType; 
    var NotResident;
    var LegalForm;
    var RegValDeposit;
    GetRegistryValue( "PS\\REQOPENACC\\OPERATION\\365-П\\ВКЛАД_ФИЗЛИЦА", V_STRING, RegValDeposit);
    var RegValAccount;
    GetRegistryValue( "PS\\REQOPENACC\\OPERATION\\365-П\\СЧЕТ_ФИЗЛИЦА", V_STRING, RegValAccount);

    if( ReqCloseAcc.ClientID > 0 )
      if( not GetTaxPayerPartyData(ReqCloseAcc.ClientID, @LegalForm, @IsIndividEmployer, @AttrPartyType, @NotResident) )
        RunError("Не найден субъект " + ReqCloseAcc.ClientID);
      end;
    end;

    if(((ReqCloseAcc.ClientType311 == 1) or (ReqCloseAcc.ClientType311 == 6)) or (LegalForm == PTLEGF_INST))
      
      if(ReqCloseAcc.ClientName == "")
        errList = errList + "Наименование";
      end;

      if(ReqCloseAcc.ClientOGRN == "")
        if(errList != "") errList = errList + ", "; end;
        errList = errList + "ОГРН";
      end;

    end;
   
    if(((ReqCloseAcc.clientType311 == 2) or
        (ReqCloseAcc.clientType311 == 3) or
        (ReqCloseAcc.clientType311 == 4) or
        (ReqCloseAcc.clientType311 == 5)) or 
        ((LegalForm == PTLEGF_PERSN) and ((AttrPartyType == PARTY_ATTR_PTTYPE_NOTARIUS) or
                                       (AttrPartyType == PARTY_ATTR_PTTYPE_ADVOCAT) or
                                       (AttrPartyType == PARTY_ATTR_PTTYPE_MANAGPARTNER )) or
        (IsIndividEmployer == true))) 
      if(ReqCloseAcc.ClientName == "")
        errList = errList + "Наименование";
      end;

      if(ReqCloseAcc.ClientOGRN == "")
        if(errList != "") errList = errList + ", "; end;
        errList = errList + "ОГРН";
      end;

    end;
    
    if((ReqCloseAcc.clientType311 == 7) or
       ((LegalForm == PTLEGF_PERSN) and ((AttrPartyType != PARTY_ATTR_PTTYPE_NOTARIUS) AND
                                      (AttrPartyType != PARTY_ATTR_PTTYPE_ADVOCAT) AND
                                      (AttrPartyType != PARTY_ATTR_PTTYPE_MANAGPARTNER )) AND
       (IsIndividEmployer == false)) or
       ((CompareStrWithMasks( RegValDeposit, ReqCloseAcc.Account ) == 0) or
        (CompareStrWithMasks( RegValAccount, ReqCloseAcc.Account ) == 0))) 
      
      if(ReqCloseAcc.ClientName == "")
        errList = errList + "Наименование";
      end;
      
    end;
    
    if(errList != "")
      MsgBox("Не заполнены следующие реквизиты клиента: " + errList + ". Укажите необходимые реквизиты в панели, вызываемой по клавише Alt-R");  
      return Fld_Client;
    end;                                  

    if( ReqCloseAcc.Department != {OperDprt})
      MsgBox("Запрещено редактирование заявлений других филиалов");
      return CHANG_NOTKEEP;
    end;  
    if( ReqCloseAcc.Dummy != "X" )
      // Счет, закрываемый по заявлению, является открытым (account.open_close = UNSET_CHAR)
      var select = "select 1 from daccount_dbt " +
                   " where t_Account = '" + ReqCloseAcc.Account + "'" +
                   "   and t_chapter = 1 " +
                   "   and t_Code_Currency = " + ReqCloseAcc.Code_Currency +
                   "   and t_Open_Close <> chr(0) ";
      var rs = execSQLselect( select );
      if (rs and rs.MoveNext())
        MsgBox("Заданный счет уже закрыт");
        return Fld_Account;
      end;
      var res = CheckOther();
      if (res == 1)
        return -9;//Продолжить редактирование
      elif( res == 2)
        return -8;//Отказаться
      end;
    end;
  end;

         if(Режим == 3)  /* РЕДАКТИРОВАНИЕ ДОКУМЕНТА */
               /*При редактировании производим проверку важности внесенных изменений */
               /* Константы важности внесенных изменений:           */
               /* CHANG_NOTIMPORTANT   - изменения неважные (сохранение без отката операции)*/
               /* CHANG_IMPORTANT      - изменения важны (сохранение изменений возможно только при откате) */
               /* CHANG_NOTKEEP        - не сохранять изменения */
               /* Если возвращаемое значение  > 0, то это оно интерпритируется как номер поля с ошибочным параметром*/
               /* Если возвращаемое значение  = 0, то cчитается, что проверка прошла успешно
                  и сохранение изменений можно производить без отката операции      */
               if( (ReqCloseAcc.Account       != OldReqCloseAcc.Account) or
                   (ReqCloseAcc.Code_Currency != OldReqCloseAcc.Code_Currency)
                 ) stat = CHANG_IMPORTANT;
               end;
         
         elif( Режим == 9 )/* Старт операции */
           if( ReqCloseAcc.Dummy != "X" )  
             stat = CheckAllPayments( ReqCloseAcc.Account, ReqCloseAcc.Code_Currency, BuffAccPaym );

             if( stat )
               if( GetDialogFlag() )
                 if( ConfWin( Text, Buttons ) == 0/*Да*/ )
                   PS_PrintOpenPaymsForAccount( ReqCloseAcc.Account );
               return 1;
                 else
                   return 1;
                 end;
               else
                 msgbox("Есть неисполненные документы к счету " + ReqCloseAcc.Account + ". Показать? ");
                 return 1;
               end;  
             end;

             stat = CheckSertifForAccount( ReqCloseAcc.Account, ReqCloseAcc.Code_Currency, Certifs );
             if( stat != -1 )
               if( 
                   ( ReqCloseAcc.Series      == "" ) and 
                   ( ReqCloseAcc.NumberFirst == "" ) and
                   ( ReqCloseAcc.NumberLast  == "" )
                 )
  
                 if( stat > 0) 
                 msgbox( "К закрываемому счету имеется чековая книжка с неиспользованными бланками" );
                 return 1;
                 end;
               else
                 
                 if( stat == 0 )
                   msgbox( "В заявлении неверно указаны реквизиты бланков чековой книжки" );
                   return 1;
                 end;

                 Cert = Certifs[0];
                 if(
                     ( ReqCloseAcc.Series      != Cert[0] ) or 
                     ( ReqCloseAcc.NumberFirst != Cert[1] ) or
                     ( ReqCloseAcc.NumberLast  != Cert[2] )
                   )
                   msgbox( "В заявлении неверно указаны реквизиты бланков чековой книжки" );
                   return 1;
                 end;
               end;
               stat = 0; /* с чековыми книжками к счету все в порядке */
               
               // Проверка клиента на банкротство
               var ClientID = 0;
               rs = execSQLSelect( " select t_Client from daccount_dbt "
                                   " where t_Account = :Account ", makeArray( SQLParam( "Account", ReqCloseAcc.Account )));
               if( rs.MoveNext() )
                 ClientID = rs.value(0);
               end;
               var resCheck = PSCheckBankruptStatus( ClientID, 1 );
               if ( resCheck )
                 return resCheck;
               end;
               
             else
               MsgBox( "Не задан финансовый инструмент для бланков" );
               return 1;
             end;                              
           end;
         end;

         if( (ReqCloseAcc.NumberPack < 0) and (ReqCloseAcc.Dummy != "X") )
            MsgBox("Номер пачки не может быть отрицательным");
            return Fld_PackNumb;
         end;

  return stat;
END;

MACRO Функция_Пользователя( Режим:integer )
 /*
 Возможные значения Режим:
  UFN_PANEL_INPUT(1) - функция вызвана из панели ввода объекта;
  UFN_PANEL_EDIT(2) - функция вызвана из панели корректировки объекта;
  UFN_SCROL(3) - функция вызвана из панели скролинга, единичный вызов, любая корректировка объекта запрещена;
  UFN_SCROL_FMASS(4) - функция вызвана из панели скролинга, работа по нескольким записям, вызов до начала обработки, любая корректировка объекта запрещена;
 
// Пример работы: 
 if( Режим == UFN_SCROL )
   return UPDTPAGE;    // Обновить страницу записей и область скролинга
   //return UPDTREC;     // Обновить текущую запись, загрузив из файла
 end;
 */
  return 0;
END;


