/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2001              */
/*                                                                      */
/*  Имя файла        : prn_bcord.mac                                    */
/*                                                                      */
/*  Описание         : Печатные формы поручений на п\п валюты           */
/*  Программист      : Бурак В.Ю.                                       */
/*                                                                      */
/*  Создан           : 12.04.2001                                        */
/*                                                                      */
/************************************************************************/

import bcprncom;

/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/

macro DrawReport_BuyOrder( nameb, number_order, date_order,
                           Name_Client, Address_Client, FIO, Post, Phone,
                           RequiredAccount, Name_Bank, ISO, Name_FIID,
                           SourceAmount, str_SourceAmount, RequiredAmount, str_RequiredAmount,
                           TypeName, NameMarketPlace, SourceAccount, SurplusAccount,
                           Cause, Ground, DocGround, ScanObject )
  if(MassCopy == 0)
    if(ncopy == 0)
      ncopy = 1;
      if ( not GetInt(ncopy,"Введите количество копий.",3))
        ncopy = 0;
      end;
    end;
  else ncopy = MassCopy;
  end;

  if(ncopy == 0) /*return false;*/ exit(1); end;
                 
  ARRAY SNB, SG, SDG, SMP;

  strsplit( nameb, SNB, 19,14,2 );
  strsplit( Ground,    SG,  93,38,4 );
  strsplit( DocGround, SDG, 93,59,2 );
  strsplit( NameMarketPlace, SMP, 59,59,2 );
  
while(ncopy != 0)
[
                                                                            Банк ##############
                                                                                 --------------
                                                                            ###################
                                                                            -------------------
                                                           
                         Поручение на покупку иностранной валюты
                            № ############### от  ############
                              ---------------    --------------

  Наименование клиента: #
                       ------------------------------------------------------------------------
  Адрес: #
        ---------------------------------------------------------------------------------------
  Ф.И.О. уполномоченного сотрудника: #
                                   ------------------------------------------------------------
  Должность #                                Тел./факс:#
           ---------------------------------           ----------------------------------------

  Просим  #                                                          купить для нас безналичную 
        ------------------------------------------------------------
  иностранную валюту с зачислением на наш специальный транзитный валютный счет

  № ######################## в #
    ------------------------   ----------------------------------------------------------------
  на следующих условиях:

  +------------------------------+------------------------------------------------------------+
  |Наименование покупаемой валюты|(###) #                                                     |
  +------------------------------+------------------------------------------------------------+
  |Сумма рублей, перечисленная на|#                                                           |
  |покупку валюты                |#                                                           |
  +------------------------------+------------------------------------------------------------+
  |Сумма покупаемой валюты       |#                                                           |
  |                              |#                                                           |
  +------------------------------+------------------------------------------------------------+
  |Курс                          |#                                                           |
  +------------------------------+------------------------------------------------------------+
  |Торговая площадка             |#                                                           |
  |                              |#                                                           |
  +------------------------------+------------------------------------------------------------+

  Сумму покрытия просим списать с нашего расчетного счета №  ########################
                                                            -----------------------------------
  Остаток неиспользованных средств просим вернуть на счет №  ########################
                                                            -----------------------------------
  Основание покупки валюты (код и наименование ): ####   #
                                                 ------  --------------------------------------
  #
  ---------------------------------------------------------------------------------------------
  #
  ---------------------------------------------------------------------------------------------
  #
  ---------------------------------------------------------------------------------------------
  Документы, обосновывающие покупку: #
                                    -----------------------------------------------------------
  #
  ---------------------------------------------------------------------------------------------



       М.П.                                      Руководитель 
                                                                  -----------------------------
                                                
                                                 Главный бухгалтер 
                                                                  -----------------------------


  ------Заполняется банком---------------------------------------------------------------------

  +--------------------------------------+----------------------------------------------------+
  |Отметка о наличии средств на расчетном|                                                    |
  |счете                                 |                                                    |
  +--------------------------------------+----------------------------------------------------+
  |Отметка отдела валютного контроля об  |                                                    |
  |обоснованности покупки валюты         |                                                    |
  +--------------------------------------+----------------------------------------------------+
]                                                                                           
 ( SNB(0):l, SNB(1):l, number_order:l, date_order:f:c,
   Name_Client:l, Address_Client:l, FIO:l, Post:l, Phone:l,
   {Name_Bank}:l, RequiredAccount:l, Name_Bank:l, ISO:c, Name_FIID:l,
   SourceAmount:l:f, str_SourceAmount:l, RequiredAmount:l:f, str_RequiredAmount:l,
   TypeName:l, SMP(0):l, SMP(1):l, SourceAccount:l, SurplusAccount:l, Cause:c, 
   SG(0):l, SG(1):l, SG(2):l, SG(3):l,
   SDG(0):l, SDG(1):l
 );

 ScanObject.Do();
 ncopy = ncopy - 1;

 end;

 return true;

end;

/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/

macro DrawReport_ObligPayOrder( nameb, number_order, date_order,
                                Name_Client, Address_Client, FIO, Post, Phone,
                                InAmount, str_InAmount, 
                                DateIn, Contract, DateContract, CodePaym, SourceAccount,
                                SourceAmount, str_SourceAmount, Percent, SurplusAccount,
                                SurplusAmount, str_SurplusAmount,
                                RequiredAccount, Name_Bank, INN_Bank, BankCode, ScanObject )
  if(MassCopy == 0)
    if(ncopy == 0)
      ncopy = 1;
      if ( not GetInt(ncopy,"Введите количество копий.",3))
        ncopy = 0;
      end;
    end;
  else ncopy = MassCopy;
  end;

  if(ncopy == 0) /*return false;*/ exit(1); end;

  ARRAY SNB, SIM;

  strsplit( nameb,    SNB, 19,14,2 ); 
  strsplit( str_InAmount, SIM, 93,42,2 ); 

while(ncopy != 0)
[
                                                                            Банк ##############
                                                                                 --------------
                                                                            ###################
                                                                            -------------------
                                                           
                     Поручение на обязательную продажу иностранной валюты
                            № ############### от  ############
                              ---------------    --------------

  Наименование клиента: #
                       ------------------------------------------------------------------------
  Адрес: #
        ---------------------------------------------------------------------------------------
  Ф.И.О. уполномоченного сотрудника: #
                                   ------------------------------------------------------------
  Должность #                                Тел./факс:#
           ---------------------------------           ----------------------------------------

  Номер и дата контракта, паспорта сделки, учетной карточки и ГТД
  
  ---------------------------------------------------------------------------------------------
  Из общей суммы экспортной выручки #               (#
                                    ---------------- ------------------------------------------
  #                                                                                           
  ---------------------------------------------------------------------------------------------
  поступившей ############ по контракту № ############### от  ############ .
             --------------               ---------------    --------------
  Код вида полученного плaтежа #
                               --------

  Просим списать с нашего транзитного счета № #
                                              -------------------------------------------------
  с перечислением на счет № 40705 суммы  #
                                        ----------------------
  (#                                                                                          
  ---------------------------------------------------------------------------------------------
  для обязательной продажи #       % экспортной валютной выручки
                           -------
  с зачислением на текущий валютный счет #
                                         ------------------------------------------------------
  суммы #                   (#                                                                
        ---------------------------------------------------------------------------------------

  Полученные от продажи рублевые средства просим перечислить на наш расчетный

  счет № #                            в #
         -----------------------------  -------------------------------------------------------
  ИНН #                БИК #
     -----------------     ------------------

       М.П.                                      Руководитель 
                                                                  -----------------------------
                                                
                                                 Главный бухгалтер 
                                                                  -----------------------------


  ------Заполняется банком---------------------------------------------------------------------

  +--------------------------------------+----------------------------------------------------+
  |Отметка о наличии средств на расчетном|                                                    |
  |счете                                 |                                                    |
  +--------------------------------------+----------------------------------------------------+
  |Отметка отдела валютного контроля об  |                                                    |
  |обоснованности покупки валюты         |                                                    |
  +--------------------------------------+----------------------------------------------------+
]
 ( SNB(0):l, SNB(1):l, number_order:l, date_order:f:c,
   Name_Client:l, Address_Client:l, FIO:l, Post:l, Phone:l,
   InAmount:l:f, SIM(0):l, SIM(1):l,
   DateIn:l:f, Contract:l, DateContract:l:f, CodePaym:c, SourceAccount:l,
   SourceAmount:l:f, str_SourceAmount:l, Percent:l, SurplusAccount:l,
   SurplusAmount:l:f, str_SurplusAmount:l, RequiredAccount:l, Name_Bank:l,
   INN_Bank:l, BankCode:l
 );


 ScanObject.Do();
 ncopy = ncopy - 1;

 end;

 return true;

end;

/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/

macro DrawReport_FreeRevPayOrder( nameb, number_order, date_order, kind_pay,
                                Name_Client, Address_Client, FIO, Post, Phone,
                                KindCurrAccount, SourceAccount, RequiredAccount,
                                Name_Bank, INN_Bank, BankCode, ISO, Name_FIID,
                                SourceAmount, str_SourceAmount, RequiredAmount, str_RequiredAmount,
                                TypeName, NameMarketPlace, ScanObject )
  if(MassCopy == 0)
    if(ncopy == 0)
      ncopy = 1;
      if ( not GetInt(ncopy,"Введите количество копий.",3))
        ncopy = 0;
      end;
    end;
  else ncopy = MassCopy;
  end;

  if(ncopy == 0) /*return false;*/ exit(1); end;

  ARRAY SNB, SMP;

  strsplit( nameb, SNB, 19,14,2 ); 
  strsplit( NameMarketPlace, SMP, 59,59,2 ); 

while(ncopy != 0)
[
                                                                            Банк ##############
                                                                                 --------------
                                                                            ###################
                                                                            -------------------
                                                           
                          Поручение на продажу иностранной валюты
                            № ############### от  ############
                              ---------------    --------------
                                  ##############  продажа
                                 ----------------  

  Наименование клиента: #
                       ------------------------------------------------------------------------
  Адрес: #
        ---------------------------------------------------------------------------------------
  Ф.И.О. уполномоченного сотрудника: #
                                   ------------------------------------------------------------
  Должность #                                Тел./факс:#
           ---------------------------------           ----------------------------------------

  Номер и дата контракта, паспорта сделки, учетной карточки и ГТД
  
  ---------------------------------------------------------------------------------------------

  ############### валютный счет для списания валютных средств №  #
  ---------------                                              ------------------------------
  Расчетный счет клиента для зачисления рублевых средств №  #                                 в
                                                           ---------------------------------- 
  #                              ИНН #              БИК #
  -------------------------------   ---------------    ----------------

  Просим #                                         продать наши валютные средства на
        ------------------------------------------ 
  нижеследующих условиях:
  +------------------------------+------------------------------------------------------------+
  |Наименование продаваемой      |(###) #                                                     |
  |валюты                        |                                                            |
  +------------------------------+------------------------------------------------------------+
  |Сумма продаваемой валюты      |#                                                           |
  |                              |#                                                           |
  +------------------------------+------------------------------------------------------------+
  |Сумма рублей, полученная от   |#                                                           |
  |продажи валюты                |#                                                           |
  +------------------------------+------------------------------------------------------------+
  |Курс                          |#                                                           |
  +------------------------------+------------------------------------------------------------+
  |Торговая площадка             |#                                                           |
  |                              |#                                                           |
  +------------------------------+------------------------------------------------------------+



       М.П.                                      Руководитель 
                                                                  -----------------------------
                                                
                                                 Главный бухгалтер 
                                                                  -----------------------------



  ------Заполняется банком--------------------------------------------------------------------

  +--------------------------------------+---------------------------------------------------+
  |Отметка о наличии средств на расчетном|                                                   |
  |счете                                 |                                                   |
  +--------------------------------------+---------------------------------------------------+
  |Отметка отдела валютного контроля об  |                                                   |
  |обоснованности покупки валюты         |                                                   |
  +--------------------------------------+---------------------------------------------------+
]
 ( SNB(0):l, SNB(1):l, number_order:l, date_order:f:c, kind_pay:c,
   Name_Client:l, Address_Client:l, FIO:l, Post:l, Phone:l, KindCurrAccount:c,
   SourceAccount:l, RequiredAccount:l, Name_Bank:l, INN_Bank:l, BankCode:l, {Name_Bank}:l,
   ISO:c, Name_FIID:l, SourceAmount:l:f, str_SourceAmount:l, 
   RequiredAmount:l:f, str_RequiredAmount:l, TypeName:l, SMP(0):l, SMP(1):l
 );

 ScanObject.Do();
 ncopy = ncopy - 1;

 end;

 return true;

end;

/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/

macro DrawReport_ConvOrder( nameb             , /*( 1)*/
                            number_order      , /*( 2)*/
                            date_order        , /*( 3)*/
                            Name_Client       , /*( 4)*/
                            Address_Client    , /*( 5)*/
                            FIO               , /*( 6)*/
                            Post              , /*( 7)*/
                            Phone             , /*( 8)*/
                            CurrentAccountTo  , /*( 9)*/
                            Name_Bank         , /*(10)*/
                            ISO_To            , /*(11)*/
                            Name_FIID_To      , /*(12)*/
                            RequiredAmount    , /*(13)*/
                            str_RequiredAmount, /*(14)*/
                            ISO_From          , /*(15)*/
                            Name_FIID_From    , /*(16)*/
                            SourceAmount      , /*(17)*/
                            str_SourceAmount  , /*(18)*/
                            TypeName          , /*(19)*/
                            NameMarketPlace   , /*(20)*/
                            CurrentAccountFrom, /*(21)*/
                            Ground            , /*(22)*/
                            ValueDate         , /*(23)*/
                            ScanObject          /*(24)*/
                          )

  if( MassCopy == 0 )
    if( ncopy == 0 )
      ncopy = 1;
      if( not GetInt( ncopy,"Введите количество копий.", 3) )
        ncopy = 0;
      end;
    end;
  else 
     ncopy = MassCopy;
  end;

  if( ncopy == 0 ) exit(1); end;
                 
  ARRAY SNB, SG, SMP;

  strsplit( nameb          , SNB, 19, 14, 2 );
  strsplit( Ground         ,  SG, 93, 38, 3 );
  strsplit( NameMarketPlace, SMP, 59, 59, 2 );


while( ncopy != 0 )
[
                                                                            Банк ##############
                                                                                 --------------
                                                                            ###################
                                                                            -------------------
                                                           
                         Поручение на покупку иностранной валюты
                            № ############### от  ############
                              ---------------    --------------

  Наименование клиента: #
                       ------------------------------------------------------------------------
  Адрес: #
        ---------------------------------------------------------------------------------------
  Ф.И.О. уполномоченного сотрудника: #
                                   ------------------------------------------------------------
  Должность #                                Тел./факс:#
           ---------------------------------           ----------------------------------------

  Просим  #                                                            на сумму валюты покрытия   
        ---------------------------------------------------------------
  приобрести безналичную иностранную валюту с зачислением на наш валютный счет

  № ######################## в #
    ------------------------   ----------------------------------------------------------------
  на следующих условиях:

  +------------------------------+------------------------------------------------------------+
  |Сумма и код покупаемой валюты |#                                                           |
  |                              |(###) #                                                     |
  |                              |#                                                           |
  +------------------------------+------------------------------------------------------------+
  |Сумма и код валюты покрытия   |#                                                           |
  |                              |(###) #                                                     |
  |                              |#                                                           |
  +------------------------------+------------------------------------------------------------+
  |Курс                          |#                                                           |
  +------------------------------+------------------------------------------------------------+
  |Дата валютирования            |##########                                                  |
  +------------------------------+------------------------------------------------------------+
  |Торговая площадка             |#                                                           |
  |                              |#                                                           |
  +------------------------------+------------------------------------------------------------+

  Сумму покрытия просим списать с нашего счета №  ########################
                                                  ---------------------------------------------
  Основание операции : #
                       ------------------------------------------------------------------------
  #
  ---------------------------------------------------------------------------------------------
  #
  ---------------------------------------------------------------------------------------------



       М.П.                                      Руководитель 
                                                                  -----------------------------
                                                
                                                 Главный бухгалтер 
                                                                  -----------------------------


  ------Заполняется банком---------------------------------------------------------------------

  +--------------------------------------+----------------------------------------------------+
  |Отметка о наличии средств на текущем  |                                                    |
  |валютном счете                        |                                                    |
  +--------------------------------------+----------------------------------------------------+
  |Отметка отдела валютного контроля об  |                                                    |
  |обоснованности покупки валюты         |                                                    |
  +--------------------------------------+----------------------------------------------------+
  |Счет расчета по конверсионным сделкам |№                                                   |
  +--------------------------------------+----------------------------------------------------+
]                                                                                           
 ( SNB(0):l, SNB(1):l, number_order:l, date_order:f:c,

   Name_Client:l   , 
   Address_Client:l, 
   FIO:l           , 
   Post:l          , 
   Phone:l         ,
   {Name_Bank}:l   , 

   CurrentAccountTo:l, Name_Bank:l,  

   RequiredAmount:l:f, ISO_To:c  , Name_FIID_To:l  , str_RequiredAmount:l, 
   SourceAmount:l:f  , ISO_From:c, Name_FIID_From:l, str_SourceAmount:l  ,

   TypeName:l, ValueDate, SMP(0):l, SMP(1):l,

   CurrentAccountFrom:l, 
   SG(0):l, SG(1):l, SG(2):l
 );

 ScanObject.Do();
 ncopy = ncopy - 1;

end;

 return true;

end;

/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/

class VScanOrderExec( DocID )

   var ID = DocID, exec_file = TBfile( "ps_bcexe.dbt", "R", 1 );

   /*Печатать одну запись ---------------------------------------------------*/
   macro PrintOneExec( exec_rec )
   end;
   /*------------------------------------------------------------------------*/

   /*Запустить сканирование -------------------------------------------------*/
   macro Do
      var found = 0;

      exec_file.clear();
      exec_file.rec.OrderID = ID;

      found = exec_file.GetGE();
      while( (found) and (exec_file.rec.OrderID == ID) )
         PrintOneExec( exec_file.rec );
         found = exec_file.next();
      end;
   end;
   /*------------------------------------------------------------------------*/

end;

/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/

class (VScanOrderExec) VBasePrintExec( _Order )
   InitVScanOrderExec( _Order.OrderID );

   var bcOrder = _Order, fromISO, formFIName, toISO, toFIName, convISO, convFIName, backSum;

   GetFIIDProperty( bcOrder.SourceFIID  , fromISO, formFIName );
   GetFIIDProperty( bcOrder.RequiredFIID, toISO  , toFIName   );
   GetFIIDProperty( bcOrder.ExchangeFIID, convISO, convFIName );

   backSum = bcOrder.SourceAmount;

   /*пересчет backSum -------------------------------------------------------*/
   macro recalcBackSum( exec_rec )
      if( backSum > 0 ) backSum = backSum - exec_rec.UsedAmount end;
   end;
   /*------------------------------------------------------------------------*/

   /*получить курс ----------------------------------------------------------*/
   macro GetRate( exec_rec )
      var inv = " "; if( exec_rec.IsInverse ) inv = "X" end;

      if( bcOrder.BCOrdKind != PSBCKIND_PAY  )
         return ConvSumRate( exec_rec.UsedAmount     , exec_rec.SatisfiedAmount, exec_rec.Scale, exec_rec.Point, inv ) / pow( 10, exec_rec.Point );
      else
         return ConvSumRate( exec_rec.SatisfiedAmount, exec_rec.UsedAmount     , exec_rec.Scale, exec_rec.Point, inv ) / pow( 10, exec_rec.Point );
      end; 
   end;
   /*------------------------------------------------------------------------*/

end;

/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/

class (VBasePrintExec) VPrintBuyOrderExec( _Order )
   InitVBasePrintExec( _Order );

   /*Печатать одну запись ---------------------------------------------------*/
   macro PrintOneExec( exec_rec )

      recalcBackSum( exec_rec );

[
  +--------------------------------------+----------------------------------------------------+
  |Дата исполнения сделки                |#                                                   |
  +--------------------------------------+----------------------------------------------------+
  |Курс сделки                           |###/###   #                                         |
  |                                      |                                                    |
  +--------------------------------------+----------------------------------------------------+
  |Кросс-курс сделки                     |###/###   #                                         |
  |                                      |                                                    |
  +--------------------------------------+----------------------------------------------------+
  |Сумма валюты к зачислению             |#                                                   |
  +--------------------------------------+----------------------------------------------------+
  |Сумма рублей к списанию               |#                                                   |
  +--------------------------------------+----------------------------------------------------+
  |Сумма неиспользованных руб. средств   |#                                                   |
  +--------------------------------------+----------------------------------------------------+

  Исполнитель                               Дата  ############
              --------------------------         --------------
]( exec_rec.ExecDate, 
   fromISO, toISO, GetRate( exec_rec ):l, 
   fromISO, convISO, exec_rec.ConvRate:l, 
   exec_rec.SatisfiedAmount:l,
   exec_rec.UsedAmount:l,
   backSum:l,
   {curdate} );

   end;
   /*------------------------------------------------------------------------*/

end;

/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/

class (VBasePrintExec) VPrintPayOrderExec( _Order )
   InitVBasePrintExec( _Order );

   /*Печатать одну запись ---------------------------------------------------*/
   macro PrintOneExec( exec_rec )

[
  +--------------------------------------+----------------------------------------------------+
  |Дата исполнения сделки                |#                                                   |
  +--------------------------------------+----------------------------------------------------+
  |Курс сделки                           |###/###   #                                         |
  |                                      |                                                    |
  +--------------------------------------+----------------------------------------------------+
  |Кросс-курс сделки                     |###/###   #                                         |
  |                                      |                                                    |
  +--------------------------------------+----------------------------------------------------+
  |Сумма рублей к зачислению             |#                                                   |
  +--------------------------------------+----------------------------------------------------+

  Исполнитель                               Дата  ############
              --------------------------         --------------
]( exec_rec.ExecDate, 
   fromISO, toISO, GetRate( exec_rec ):l, 
   fromISO, convISO, exec_rec.ConvRate:l, 
   exec_rec.SatisfiedAmount:l,
   {curdate} );

   end;
   /*------------------------------------------------------------------------*/

end;

/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/

class (VBasePrintExec) VPrintConvOrderExec( _Order )
   InitVBasePrintExec( _Order );

   /*Печатать одну запись ---------------------------------------------------*/
   macro PrintOneExec( exec_rec )

      recalcBackSum( exec_rec );

[
  +--------------------------------------+----------------------------------------------------+
  |Дата исполнения сделки                |#                                                   |
  +--------------------------------------+----------------------------------------------------+
  |Курс сделки                           |###/###   #                                         |
  |                                      |                                                    |
  +--------------------------------------+----------------------------------------------------+
  |Кросс-курс сделки                     |###/###   #                                         |
  |                                      |                                                    |
  +--------------------------------------+----------------------------------------------------+
  |Сумма валюты к зачислению             |#                                                   |
  +--------------------------------------+----------------------------------------------------+
  |Сумма валюты покрытия к списанию      |#                                                   |
  +--------------------------------------+----------------------------------------------------+
  |Сумма неиспользованных средств        |#                                                   |
  +--------------------------------------+----------------------------------------------------+

  Исполнитель                               Дата  ############
              --------------------------         --------------
]( exec_rec.ExecDate, 
   fromISO, toISO, GetRate( exec_rec ):l, 
   fromISO, convISO, exec_rec.ConvRate:l, 
   exec_rec.SatisfiedAmount:l,
   exec_rec.UsedAmount:l,
   backSum:l,
   {curdate} );

   end;
   /*------------------------------------------------------------------------*/
end;

/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/

macro Print_BuyOrder( aa )

  var stat, Name_Client, Address_Client, Phone, Name_Bank, ISO, Name_FIID,
      str_SourceAmount, str_RequiredAmount, TypeName, NameMarketPlace, DocGround;

  setbuff( prn_bcord, aa );
  ncopy = КоличествоКопий;

  GetClientPartyProperty( prn_bcord.ClientID, Name_Client, Address_Client, Phone );
  GetBankProperty( prn_bcord.RequiredCodeKind, prn_bcord.RequiredBankCode, Name_Bank, 0 );
  GetFIIDProperty( prn_bcord.RequiredFIID, ISO, Name_FIID );
  str_SourceAmount   = rubtostralt( prn_bcord.SourceAmount );
  str_RequiredAmount = curtostralt( prn_bcord.RequiredAmount, 0, 0, 
                                   ПолучитьISOКод( prn_bcord.RequiredFIID ) );
  GetRateTypeName( prn_bcord.RateType, TypeName, prn_bcord.Rate, prn_bcord.Point );
  GetNameMarketPlace( prn_bcord.MarketPlace, NameMarketPlace);
  GetDocGround( prn_bcord.OrderID, prn_bcord.BCOrdKind, DocGround );

  var ScanObject = VPrintBuyOrderExec( prn_bcord );

  stat =  DrawReport_BuyOrder( GetNameBank(), prn_bcord.Number, prn_bcord.OrderDate, 
                              Name_Client, Address_Client, prn_bcord.FIO, prn_bcord.Post, Phone,
                              prn_bcord.RequiredAccount, Name_Bank, ISO, Name_FIID,
                              prn_bcord.SourceAmount,   str_SourceAmount, 
                              prn_bcord.RequiredAmount, str_RequiredAmount,
                              TypeName, NameMarketPlace, prn_bcord.SourceAccount, prn_bcord.SurplusAccount,
                              prn_bcord.Cause, prn_bcord.Ground, DocGround, ScanObject );
  if(not stat) return 1;
  else return 0;
  end;
end;


/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/


macro Print_ObligPayOrder( aa )

  var stat, Name_Client, Address_Client, Phone, str_InAmount, DateIn, Contract, DateContract,
      CodePaym, str_SourceAmount, SurplusAmount, str_SurplusAmount, Percent, Name_Bank, INN_Bank;

  setbuff( prn_bcord, aa );
  ncopy = КоличествоКопий;

  GetClientPartyProperty( prn_bcord.ClientID, Name_Client, Address_Client, Phone );
  str_InAmount = curtostralt( prn_bcord.InAmount, 0, 0, ПолучитьISOКод( prn_bcord.SourceFIID ) ) + ")";
  DateIn = Date(0,0,0000);
  Contract = ""; 
  DateContract = Date(0,0,0000);
  CodePaym = ""; 
  str_SourceAmount = curtostralt( prn_bcord.SourceAmount, 0, 0, 
                                  ПолучитьISOКод( prn_bcord.SourceFIID ) ) + ")";

  SurplusAmount = prn_bcord.InAmount - prn_bcord.SourceAmount;
  str_SurplusAmount = curtostralt( SurplusAmount, 0, 0, 
                                  ПолучитьISOКод( prn_bcord.SourceFIID ) ) + ")";

  if( prn_bcord.InAmount )
    Percent = (prn_bcord.SourceAmount/prn_bcord.InAmount)*100;
  else
    Percent = 0.00;
  end;

  GetBankProperty( prn_bcord.RequiredCodeKind, prn_bcord.RequiredBankCode, Name_Bank, INN_Bank );

  var ScanObject = VPrintPayOrderExec( prn_bcord );

  stat =  DrawReport_ObligPayOrder( GetNameBank(), prn_bcord.Number, prn_bcord.OrderDate,
                                   Name_Client, Address_Client, prn_bcord.FIO, prn_bcord.Post, Phone,
                                   prn_bcord.InAmount, str_InAmount, DateIn, Contract, DateContract,
                                   CodePaym, prn_bcord.SourceAccount, prn_bcord.SourceAmount,
                                   str_SourceAmount, Percent, 
                                   prn_bcord.SurplusAccount, 
                                   SurplusAmount, str_SurplusAmount, prn_bcord.RequiredAccount,
                                   Name_Bank, INN_Bank, prn_bcord.RequiredBankCode, ScanObject );
  if(not stat) return 1;
  else return 0;
  end;
end;


/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/


macro Print_FreeRevPayOrder( aa )

  var stat, kind_pay, Name_Client, Address_Client, Phone, KindCurrAccount, Name_Bank, INN_Bank, 
      ISO, Name_FIID, str_SourceAmount, str_RequiredAmount, TypeName, NameMarketPlace;

  setbuff( prn_bcord, aa );
  ncopy = КоличествоКопий;

  if( prn_bcord.BCOrdSubKind == PSBC_FREE ) 
    kind_pay = "Необязательная"; 
  elif( prn_bcord.BCOrdSubKind == PSBC_REV )  
    kind_pay = "Обратная";       
  end;

  GetClientPartyProperty( prn_bcord.ClientID, Name_Client, Address_Client, Phone );
  GetKindCurrAccount( prn_bcord.SourceAccount, prn_bcord.SourceFIID, KindCurrAccount );
  GetBankProperty( prn_bcord.RequiredCodeKind, prn_bcord.RequiredBankCode, Name_Bank, INN_Bank );
  GetFIIDProperty( prn_bcord.SourceFIID, ISO, Name_FIID );
  str_SourceAmount = curtostralt( prn_bcord.SourceAmount, 0, 0, 
                                  ПолучитьISOКод( prn_bcord.SourceFIID ) );
  str_RequiredAmount = rubtostralt( prn_bcord.RequiredAmount );

  GetRateTypeName( prn_bcord.RateType, TypeName, prn_bcord.Rate, prn_bcord.Point );
  GetNameMarketPlace( prn_bcord.MarketPlace, NameMarketPlace);

  var ScanObject = VPrintPayOrderExec( prn_bcord );

  stat =  DrawReport_FreeRevPayOrder( GetNameBank(), prn_bcord.Number, prn_bcord.OrderDate, kind_pay,
                                   Name_Client, Address_Client, prn_bcord.FIO, prn_bcord.Post, Phone,
                                   KindCurrAccount, prn_bcord.SourceAccount, prn_bcord.RequiredAccount,
                                   Name_Bank, INN_Bank, prn_bcord.RequiredBankCode, ISO, Name_FIID,
                                   prn_bcord.SourceAmount, str_SourceAmount, 
                                   prn_bcord.RequiredAmount, str_RequiredAmount,
                                   TypeName, NameMarketPlace, ScanObject );
  if(not stat) return 1;
  else return 0;
  end;
end;


/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/


macro Print_ConvOrder( aa )

  var stat, Name_Client, Address_Client, Phone, Name_Bank, ISO_To, Name_FIID_To, str_SourceAmount, 
      ISO_From, Name_FIID_From, str_RequiredAmount, TypeName, NameMarketPlace;

  setbuff( prn_bcord, aa );
  ncopy = КоличествоКопий;

  GetClientPartyProperty( prn_bcord.ClientID        , Name_Client               , Address_Client, Phone );
  GetBankProperty       ( prn_bcord.RequiredCodeKind, prn_bcord.RequiredBankCode, Name_Bank     , 0     );
  GetRateTypeName       ( prn_bcord.RateType        , TypeName, prn_bcord.Rate  , prn_bcord.Point       );
  GetNameMarketPlace    ( prn_bcord.MarketPlace     , NameMarketPlace                                   );

  GetFIIDProperty( prn_bcord.RequiredFIID, ISO_To  , Name_FIID_To   );
  GetFIIDProperty( prn_bcord.SourceFIID  , ISO_From, Name_FIID_From );

  str_RequiredAmount = curtostralt( prn_bcord.RequiredAmount, 0, 0, ПолучитьISOКод( prn_bcord.RequiredFIID ) );
  str_SourceAmount   = curtostralt( prn_bcord.SourceAmount  , 0, 0, ПолучитьISOКод( prn_bcord.SourceFIID   ) );

  var ScanObject = VPrintConvOrderExec( prn_bcord );

  stat = DrawReport_ConvOrder( GetNameBank()            , /*( 1)*/ 
                               prn_bcord.Number         , /*( 2)*/ 
                               prn_bcord.OrderDate      , /*( 3)*/ 
                               Name_Client              , /*( 4)*/ 
                               Address_Client           , /*( 5)*/ 
                               prn_bcord.FIO            , /*( 6)*/ 
                               prn_bcord.Post           , /*( 7)*/ 
                               Phone                    , /*( 8)*/ 
                               prn_bcord.RequiredAccount, /*( 9)*/ 
                               Name_Bank                , /*(10)*/ 
                               ISO_To                   , /*(11)*/ 
                               Name_FIID_To             , /*(12)*/ 
                               prn_bcord.RequiredAmount , /*(13)*/ 
                               str_RequiredAmount       , /*(14)*/ 
                               ISO_From                 , /*(15)*/ 
                               Name_FIID_From           , /*(16)*/ 
                               prn_bcord.SourceAmount   , /*(17)*/ 
                               str_SourceAmount         , /*(18)*/ 
                               TypeName                 , /*(19)*/ 
                               NameMarketPlace          , /*(20)*/ 
                               prn_bcord.SourceAccount  , /*(21)*/ 
                               prn_bcord.Ground         , /*(22)*/ 
                               prn_bcord.ValueDate      , /*(23)*/ 
                               ScanObject                 /*(24)*/
                          );

  if( not stat ) return 1;
  else return 0;
  end;
end;


/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/


macro BC_MassPrint( Num, /* число печатаемых копий, если 0, считается первым вызовом */
                    aa )
  var err = 0;
  setbuff( prn_bcord, aa );

  if(Num) MassCopy = Num;
  else    MassCopy = КоличествоКопий;
  end;

  if(MassCopy == 0)
    MassCopy = 1;
    if ( not GetInt(MassCopy,"Введите количество копий.",3))
      MassCopy = 0;
    end;
  end;

  if( MassCopy == 0 ) exit(1);  end;


  if( prn_bcord.BCOrdKind == 1 )

    err = print_buyorder( aa );

  elif ( prn_bcord.BCOrdKind == 3 )

    err = Print_ConvOrder( aa );

  elif( (prn_bcord.BCOrdKind == 2) and (prn_bcord.BCOrdSubKind == 1) )

    err = print_obligpayorder( aa );

  else 

    err = Print_FreeRevPayOrder( aa );

  end;

  if( err ) return false; end;

  return MassCopy;

end;


/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/


/* Вызов происходит перед печатью 1-го документа из списка */
MACRO BC_HeaderMassPrint
  MassCopy = 1;
  if ( not GetInt(MassCopy,"Введите количество копий.",3))
    MassCopy = 0;
  end;
  return MassCopy;
END;


/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/
/*---------------------------------------------------------------------------*/


/* Вызов происходит после печати последнего документа */
MACRO BC_ItogMassPrint
END;
