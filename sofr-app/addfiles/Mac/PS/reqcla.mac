/*
$Name:           reqcla.mac
$Module:         РКО
$Description:    Закрытие лицевого счета
*/
/* -------------------------------------------------------------------- */
/*         Автоматизированная банковская система RS-Bank v6.0           */
/*                 Copyright (c) R-Style Software Lab                   */
/*                                                                      */
/*  Имя файла  : reqcla.mac                                             */
/*                                                                      */
/*  Описание   : Операция - 3035 - "Закрытие лицевого счета"            */
/*               Шаг      - 60   - "Закрытие счета"                     */
/*                                                                      */
/*  Программист: Андреева Е.П.                                          */
/*                                                                      */
/*  Создан     : 13.01.2005                                             */
/*                                                                      */
/* -------------------------------------------------------------------- */
import reqinter, InsCarryDoc, PaymInter, PSInter, FMInter, pmfm;

RECORD req( reqclosa );

var TB_ReqClosAcc = TBFile("reqclosa.dbt",  "R", 0, "reqclosa.dbt",  "bank.def");


var ReqCloseAccObj:RsbReqCloseAcc;

PRIVATE MACRO GetMainRegDocDate( ClientID:integer, RegDate:date ):integer

var select = "SELECT RGDOC.T_DOCDATE"+
            " FROM DOBJRGDOC_DBT RGDOC"+
            " WHERE RGDOC.T_OBJECTID = :ClientID"+
              " AND RGDOC.T_ISMAIN = 'X'"+
              " AND RGDOC.T_ISCLOSED = CHR(0)";


var params:TArray = makeArray( SQLParam( "ClientID", ClientID ) );

var rset:RsdRecordset = execSQLselect( select, params, TRUE );

if( rset and rset.moveNext() )
  SetParm( 1, date( rset.value(0) ) );
end;     

return 0;

ONERROR(x)
  MsgBox( x.Message );
  return 1;

END;

private macro CheckPaymentValueDate( ID_Operation ):integer

  var select = "SELECT NVL( pm.t_ValueDate, to_date('01010001', 'ddmmyyyy') )"+
              " FROM dpmpaym_dbt pm, doprdocs_dbt opr "+
              " WHERE opr.t_id_operation = :id "+
                " AND opr.t_dockind in( 16, 27 )"+/*платеж банка*/
                " AND opr.t_origin = 0"/*DOC_CREATION*/
                " AND pm.t_paymentid = to_number( opr.t_documentid )";

  var params:TArray = makeArray( SQLParam( "id", ID_Operation ) );

  var rset:RsdRecordset = execSQLselect( select, params, true );

  if( rset and rset.moveNext() )
    var PaymentValueDate = date( rset.value(0) );
    if( PaymentValueDate != date( 0, 0, 0 ) )
      if( not ( ( PaymentValueDate <= {curdate} ) and
                ( {curdate}        <= PaymentValueDate + 1 ) ) )
        msgbox( "Закрытие счета должно быть выполнено не позднее дня, следующего за днем перевода остатка.|Остаток со счета был переведен " + PaymentValueDate + 
                ".|Перейдите в операционный день, совпадающий с датой перевода остатка или следующий за ней, и повторите закрытие счета" );
        return 1;
      end;
    end;
  end;     
  return 0;

ONERROR(x)
  MsgBox( x.Message );
  return 1;
end;

private macro CheckReqAccCloseDate():integer

  if( ( ReqCloseAccObj.AccCloseDate != date( 0, 0, 0 ) ) and
      ( ReqCloseAccObj.AccCloseDate != {curdate} ) )
    msgbox( "Счет должен быть закрыт " + ReqCloseAccObj.AccCloseDate + ".|Перейдите в операционный день, совпадающий с указанной датой закрытия, и повторите закрытие счета" );
    return 1;
  end;

  if( ReqCloseAccObj.AccCloseDate == date( 0, 0, 0 ) )
    ReqCloseAccObj.AccCloseDate = {curdate};
  end;
  return 0;
end;

PRIVATE VAR UR_PERS = 1;
PRIVATE VAR PR_PERS = 2;
/* Выполнение шага */
MACRO ExecuteStep( doc, reqacc, DocKind, ID_Operation )


  var stat:integer = 0;
  var RegDate:date = date( 0, 0, 0);
  var IPDate:date = date( 1, 1, 2004 );
  var URDate:date = date( 1, 7, 2002 );
  
  var ClientID = 0;
  var rsAcc = execSQLSelect( " select t_Client from daccount_dbt "
                             " where t_Account = :Account ", makeArray( SQLParam( "Account", ReqCloseAccObj.Account )));
  if( rsAcc and rsAcc.MoveNext() )
    ClientID = rsAcc.value(0);
  end;
  
  if( PSCheckBankruptStatus( ClientID, 1 ) )
    return 1;
  end;

  if( CheckPaymentValueDate( ID_Operation ) )
    return 1;
  end;

  if( CheckReqAccCloseDate() )
    return 1;
  end;

  var Series = "", Number = "";// серия и номер сообщения в фонды 
  var TypeAccount = GetTypeAccount( 1/*CHAPT1*/, ReqCloseAccObj.Account, ReqCloseAccObj.Code_Currency );

  var KindClientFlag = -1;
  
  stat = GetMainRegDocDate( Req_GetAccClient( ReqCloseAccObj.Code_Currency, ReqCloseAccObj.Account ), RegDate ); 

  if( PM_FindBalanceInReg_117( "PS\\REQOPENACC\\Счета клиентов", ReqCloseAccObj.Account, 1 ) )
      KindClientFlag = UR_PERS;
  elif( Index( substr(ReqCloseAccObj.Account, 1, 5 ), "40802" ) )
      KindClientFlag = PR_PERS;
  end;

  if( stat == 0 )

    if( CheckTypeAccPSRegVal( TypeAccount, "ТИПЫ_СЧЕТОВ" ) )    
      // Заполнить статусы сегментов операции
      if( not InsertOprStatus( REQCLOSA_ST_KIND_FS, REQCLOSA_ST_FS_NEED ) )
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
    else
      if( not InsertOprStatus( REQCLOSA_ST_KIND_DO, REQCLOSA_ST_DO_CLOSED ) )
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
    end;

    if (ReqCloseAccObj.Orderkind == 2)

      var ErrorMessage:string = "";
      var terminateAccountObj = RsbFMRejectServiceMessage(OBJTYPE_REQCLOSA, String(ReqCloseAccObj.RequestID:34:o));
      var ErrorStatus = terminateAccountObj.GetLastError(ErrorMessage);
       
      if( ErrorStatus == 1 )
         terminateAccountObj = RsbFMRejectServiceMessage( RJSRVMSG_MSGKIND_TERM_ACC_CNTR );
         ErrorStatus = FillTerminateAccount( ReqCloseAccObj, @ErrorMessage, @terminateAccountObj ); 
       end;

      if(ErrorStatus)
        MsgBox(ErrorMessage);
        return 1;
      end;
    end;

  end;

  return 0;
END;

MACRO CheckStepAction( mes:integer ):integer

  var stat = 0;

  if( mes == OP_BACKOUT_STEP )
    stat = FM_Backout_RjSrvMsg(RJSRVMSG_MSGKIND_TERM_ACC_CNTR, String(ReqCloseAccObj.RequestID:34:o), OBJTYPE_REQCLOSA );

    if((stat != 0) and (stat != 4))
      var bankMsg = AL_GetErrorInfo();
      MsgBox("Ошибка отката передачи сведений об отказе от заключения договора счета в ФСФМ: " + bankMsg.Message);      
    else
      stat = 0;
    end;
  end;
 
  return stat;
END;

