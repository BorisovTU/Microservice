/*
 $Name: fns_not.mac
 $Module: MBR
 $Description: Макрос скроллинга исходящих уведомлений в ФНС
 */

//-----------------------------------------------------------------------------
// Блок      : Вне блока
// Шаг       : Вне шага
// Назначение: Макрос скроллинга
// Описание  : Макрос скроллинга исходящих уведомлений в ФНС
//-----------------------------------------------------------------------------
import WldInter, Календарь, MesInter, "bbcpinit.mac", CTInter;

RECORD r_wlnfns    ("wlnotfns.dbt");
RECORD r_wlnfns_old("wlnotfns.dbt");

macro Новое_Уведомление()
  r_wlnfns.OriginatorCodeKind = 3;
  r_wlnfns.OriginatorID = {OurBank};
  r_wlnfns.RecipientCodeKind = 3;
  var query:string = "SELECT pt.t_PartyID"
                     " FROM dparty_dbt pt, dobjcode_dbt oc, dbankdprt_dbt bdpt"
                     " WHERE oc.t_ObjectType = 3 and oc.t_CodeKind = 3 and oc.t_ObjectID = pt.t_PartyID"
                     " and oc.t_State = 0 and OC.T_CODE = bdpt.T_BIC_RCC and bdpt.T_PARTYID = :PARTYID ";
  var params:TArray = makeArray( SQLParam( "PARTYID", {OurBank}  ) );
  var rs:RsdRecordset = execSQLselect( query, params, TRUE );
  if( rs and rs.moveNext() )
    r_wlnfns.RecipientID = rs.value(0);//bankdprt.BIC_RCC;
  else
    MsgBox("Не найден РКЦ нашего банка.");
  end;
  r_wlnfns.Date = date();
  return 0;
END;

/* Проверка на числовой номер */
private macro IsDigit( NumberStr ):bool

  var OK = true, i = 1, ch = "", DigitString = "0123456789";

      while( (OK) and (i <= strlen(NumberStr)) )
        ch = SubStr( NumberStr, i, 1 );
        if( not Index( DigitString, ch ))
          OK = false;
        end;
        i = i + 1;
      end;

  return OK;

end;

private macro isFormatXML():bool

  var FormID = 0;

  if( not WldDefineRlsForm( 528/*OBJTYPE_WLD_FNSNOTICE*/, r_wlnfns.RecipientID, WLD_MES_KIND_MNS, 0, 0, FormID, 8/*WLD_SUBKIND_MNS_NOTICE*/ ) )
    MsgBox("Не найден релиз уведомления.");
  else
    var query:string = "SELECT schem.T_NAME "
                       " FROM dwltpshem_dbt schem, dwltpparm_dbt parm "
                       " WHERE parm.t_rlsformid = :FormID "
                       " AND parm.t_tpshemid = schem.t_tpshemid ";
    var params:TArray = makeArray( SQLParam( "", FormID  ) );
    var rs:RsdRecordset = execSQLselect( query, params, TRUE );
    var format = "";
    if( rs and rs.moveNext() )
      format = rs.value(0);
      if( index(format,"xml") or index(format,"XML") )
        return TRUE;
      end;
    end;
  end;

  return FALSE;

end;

// Отправляли сегодня уведомление?
private macro existsNoticeToday()

  var query:string = "SELECT 1 "
                       "FROM DUAL "
                      "WHERE EXISTS "
                               "(SELECT 1 "
                                  "FROM dwlnotfns_dbt nfns, "
                                       "dwlmeslnk_dbt lnk, "
                                       "dwlmes_dbt mes, "
                                       "dwlsess_dbt ses "
                                 "WHERE     nfns.t_State + 0  >= :State " 
                                       "AND nfns.t_NoticeID = lnk.t_ObjID "
                                       "AND lnk.t_ObjKind = :ObjKind "
                                       "AND mes.t_MesID = lnk.t_MesID "
                                       "AND ses.t_SessionID = mes.t_SessionID "
                                       "AND ses.t_SysDate = :Today "
                                       "AND ses.t_TpID = :TpID ) ";

  var params:TArray = makeArray( SQLParam( "State", WLD_STATUS_FNSNOTICE_SEND ),
                                 SQLParam( "ObjKind", OBJTYPE_FNSNOTICE ),
                                 SQLParam( "Today", date() ),
                                 SQLParam( "TpID", TRANSP_MNS  )
                               );


  var rs:RsdRecordset = execSQLselect( query, params, TRUE );

  if( rs and rs.moveNext() )
    return true;
  end;

  return false;
end;

MACRO Проверить_Уведомление( Режим )
 /*
 Возможные значения Режим:
 1  - удаление справки ,
 2  - вставка справки  ,
 3  - обновление справки
*/

  const NOTFNS_PAN_NUMFLD_DATE = 11;
  const NOTFNS_PAN_NUMFLD_CONTRACT_DATE = 9;
  const NOTFNS_PAN_NUMFLD_ACTION = 16;
  var res;
  var isXML = isFormatXML();
  var CodeBUV = IfThenElse( isXML==true , "", 
                                          " о прекращении, временном приостановлении обмена или изменении перечня филиалов" );

  if( ( Режим == 2 ) or ( Режим == 3 ) )

    if( existsNoticeToday() )
      res = ConfWin( makeArray("Не допускается направлять более одного уведомления в течение дня. За дату "+ date() +" уже отправлено уведомление. Вы уверены, что хотите сохранить документ?"), 
                     makeArray("Да","Нет"));
      if(res == 1)
        return 1;
      end;
    end;

    if (r_wlnfns.Action == "")
      MsgBox("Заполните код уведомления.");
      return NOTFNS_PAN_NUMFLD_ACTION;
    end;
    if ((r_wlnfns.Action == "11") or (r_wlnfns.Action == "12"))
      if ( (isXML==false) AND (r_wlnfns.Date < date()) )
        MsgBox("Уведомления о начале и возобновлении обмена отправляются в день начала, возобновления обмена. Нельзя отправить уведомление прошлой датой.");
        return NOTFNS_PAN_NUMFLD_DATE;
      end;
      if ( (isXML==false) AND (r_wlnfns.Date > date()) )
        if (r_wlnfns.State == WLD_STATUS_FNSNOTICE_DEFER)
          return CHANG_IMPORTANT;
        end;
        if (r_wlnfns.State == WLD_STATUS_FNSNOTICE_PREPARE)
          res = ConfWin( makeArray("Уведомления о начале и возобновлении обмена отправляются в день начала, возобновления обмена. "+
              "Вы ввели уведомление будущей датой, отложить его?"), 
              makeArray("Отложить","Отказаться","Продолжить"));
          if (res == 0) return CHANG_IMPORTANT;
          elif (res == 1) return CHANG_NOTKEEP;
          else return NOTFNS_PAN_NUMFLD_DATE;
          end;
        end;
      end;
    elif ( InList(r_wlnfns.Action, "21", "22", "31" ) AND (isXML==false) )
      if (r_wlnfns.Date > GetDateAfterWorkDays(date(), -1))
        MsgBox("Уведомления"+ CodeBUV + 
            " отправляются не позднее чем за один рабочий день до даты вступления изменения в действие. Нельзя отправить уведомление датой "+
            "больше чем "+GetDateAfterWorkDays(date(), -1));
        return NOTFNS_PAN_NUMFLD_DATE;
      end;
      if (r_wlnfns.Date < GetDateAfterWorkDays(date(), -3))
        if (r_wlnfns.State == WLD_STATUS_FNSNOTICE_DEFER)
          return CHANG_IMPORTANT;
        end;
        if (r_wlnfns.State == WLD_STATUS_FNSNOTICE_PREPARE)
          res = ConfWin( makeArray("Уведомления" + CodeBUV + 
              " отправляется не ранее чем за три рабочих дня до даты вступления изменения в действие. " +
              "Вы пытаетесь отправить сообщение датой "+r_wlnfns.Date+", отложить его?"),
              makeArray("Отложить","Отказаться","Продолжить"));
          if (res == 0) return CHANG_IMPORTANT;
          elif (res == 1) return CHANG_NOTKEEP;
          else return NOTFNS_PAN_NUMFLD_DATE;
          end;
        end;
      end;
    end;

    var NDLnk = RsbWlNDLnk(r_wlnfns.NoticeID);
    var lnk = TRecHandler("wlndlnk.dbt");
    var query:string = "SELECT count(*)"+
                       " FROM ddp_dep_dbt"+
                       " WHERE t_code = :CODE and t_partyID = :PARTYID";
    
    while ( NDLnk.Next(lnk) == 0 )
      var params:TArray = makeArray( SQLParam( "CODE", lnk.rec.department ), SQLParam( "PARTYID", r_wlnfns.OriginatorID ) );
      var rs:RsdRecordset = execSQLselect( query, params, TRUE );
      if( rs and rs.moveNext() and (rs.value(0) > 0) )
        MsgBox("В список филиалов не следует включать банк (филиал банка), заключивший соглашение об обмене " +
               "и направляющий уведомление в ФНС. Удалите банк (филиал) " + r_wlnfns.OriginatorCode + " из списка филиалов");
        return 1;
      elif( (strlen(lnk.rec.name) > 60) and (isXML==false) )
        MsgBox("Наименование филиала не должно содержать больше 60 символов.");
        return 1;  
      end;
    end;
    
    if (   (r_wlnfns.Action != "11") and (r_wlnfns.Action != "31") and (isXML==false))
      if (NDLnk.Size > 0)
        MsgBox("Перечень филиалов заполняется только для уведомлений с кодом 11 или 31. "+
               "Введенный список филиалов не будет включен в сообщение.");
      end;
    end;

    CodeBUV = "";
    var this = "";
    if( isXML == TRUE ) // Проверки для уведомлений в xml-формате
      var dateStr = "";
      var isValidDate = false;
      if( InList( r_wlnfns.Action, "11", "12" ) )
        if(r_wlnfns.Action == "11")
          CodeBuv = "о начале обмена";
        elif(r_wlnfns.Action == "12")
          CodeBuv = "об изменении условий договора";
        end;

        this = "Начало (возобновление) обмена";
        var date1 : date = GetDateAfterWorkDays( r_wlnfns.Date, -5 ),
            date2 : date = ifThenElse((r_wlnfns.Action == "11"),r_wlnfns.Date, GetDateAfterWorkDays( r_wlnfns.Date, -1 ));
        if( ( date() >= date1 ) AND ( date() <= date2 ) )
          isValidDate = true;
        else
          dateStr = "в срок от " + date1 + " до " + date2; 
        end;
      elif( r_wlnfns.Action == "13" )
        CodeBuv = "о возобновлении обмена";
        if( r_wlnfns.Date == date() )
          isValidDate = true;
        else
          dateStr = "в дату " + date();
        end;
      end;

      if( isValidDate == false )
        res = ConfWin( MakeArray("Уведомление " + CodeBUV + " с указанной датой начала действия должно быть отправлено " + dateStr + ". Сохранить?"),
                       makeArray("Да","Нет","Продолжить редактирование") );
        if (res == 1) return CHANG_NOTKEEP;
        elif(res == 2) return NOTFNS_PAN_NUMFLD_DATE;
        end;
      end;

      if( r_wlnfns.ContractDate == date(0,0,0) )
        MsgBox("Не заполнена дата заключения договора"); // must be the system message
        return NOTFNS_PAN_NUMFLD_CONTRACT_DATE;
      end;

    end;
  end;
      
  return 0;
END;



macro Функция_Пользователя( Режим:integer )
 /*
 Возможные значения Режим:
  UFN_PANEL_INPUT(1) - функция вызвана из панели ввода объекта;
  UFN_PANEL_EDIT(2) - функция вызвана из панели корректировки объекта;
  UFN_SCROL(3) - функция вызвана из панели скролинга, единичный вызов, любая корректировка объекта запрещена;
  UFN_SCROL_FMASS(4) - функция вызвана из панели скролинга, работа по нескольким записям, вызов до начала обработки, любая корректировка объекта запрещена;
 
// Пример работы: 
 if( Режим == UFN_SCROL )
   return UPDTPAGE;    // Обновить страницу записей и область скролинга
   //return UPDTREC;     // Обновить текущую запись, загрузив из файла
 end;
  MsgBox("dfsdf");
  var wld = RsbWlNDLnk(r_wlnfns.NoticeID);
  MsgBox(wld.Size);
 */
  return  0;
end;

