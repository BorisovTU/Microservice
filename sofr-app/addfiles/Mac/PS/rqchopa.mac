/* -------------------------------------------------------------------- */
/*         Автоматизированная банковская система RS-Bank v6.0           */
/*                 Copyright (c) R-Style Software Lab                   */
/*                                                                      */
/*  Имя файла  : rqchopa.mac                                            */
/*                                                                      */
/*  Описание   : Операция - 3036 - "Изменение номера счета"             */
/*               Шаг      - 10   - "Открытие нового счета"              */
/*                                                                      */
/*  Программист: Андреева Е.П.                                          */
/*                                                                      */
/*  Создан     : 02.11.2004                                             */
/*                                                                      */
/* -------------------------------------------------------------------- */
IMPORT InsCarryDoc, reqinter, OprInter, PSInter, pm_common;

var ReqChangeAccObj:RsbReqChangeAcc;

RECORD RecordAcc    ( account ); 
RECORD ab           ( accblnc );

PRIVATE CONST CHOICE_CONTINUE = 0; // "Продолжить"
PRIVATE CONST CHOICE_BREAK    = 1; // "Прервать"

// ----------------------------------------------------------------------------
// Обертка для вызова вертикального меню
// ----------------------------------------------------------------------------
PRIVATE MACRO ShowMenu():integer

  ARRAY m;
  m(0) = " Продолжить";
  m(1) = " Прервать";

  return Menu( m, "Enter Выбор Esc Отмена", "" );

END;


/* Выполнение шага */ 
MACRO ExecuteStep( doc, reqacc )

  var stat:integer = 0;
  var Choice:integer = 0;
  var CloseOper:bool = false;
  var AccBuff  :TRecHandler = TRecHandler( "account.dbt"  , "bank.def" );
  record LinkAccRec("account.dbt");
  var Rest;

  if( ReqChangeAccObj.OldAccountFIID != ReqChangeAccObj.NewAccountFIID )

    AccBuff = GetAccount( 1, ReqChangeAccObj.OldAccount, ReqChangeAccObj.OldAccountFIID );

    if( ( RestA( AccBuff.rec.Account, NULL, NULL, 1/*CHAPT1*/, AccBuff.rec.Code_Currency ) == 0) )
      if( GetLinkedObject( OBJROLE_ACC_TRANSIT, OBJTYPE_ACCOUNT, UniID( AccBuff, OBJTYPE_ACCOUNT ), OBJTYPE_ACCOUNT, LinkAccRec) == 0 );
        Rest = ПолучитьОстаток( LinkAccRec.Account, LinkAccRec.Chapter, LinkAccRec.Code_Currency );
        if( Rest != 0 )
          CloseOper = true;
        end;
    end;
    else
      CloseOper = true;
    end;

     
    if( CloseOper )    
    msgbox( "Валюта открываемого счета не совпадает с валютой закрываемого счета.|"+
            "Операция может быть выполнена только при нулевых остатках на закрываемых|"+
           " счетах или в случае смены кода у одноименной валюты." );

      return 1;
    end;

  end;

  ClearRecord( RecordAcc );

  stat = CheckKey( 1, ReqChangeAccObj.NewAccount );

  if( stat )
    DisplayError();
    return stat;
  end;

  RecordAcc.Account       = ReqChangeAccObj.NewAccount;
  RecordAcc.Code_Currency = ReqChangeAccObj.NewAccountFIID;
  RecordAcc.Balance       = ReqChangeAccObj.Balance(0);
  RecordAcc.Open_Date     = {curdate};
  RecordAcc.Oper          = ReqChangeAccObj.NewAccountOper;
  RecordAcc.Client        = Req_GetAccClient( ReqChangeAccObj.OldAccountFIID, ReqChangeAccObj.OldAccount );
  RecordAcc.Department    = ReqChangeAccObj.NewAccountDep;
  RecordAcc.Branch        = ReqChangeAccObj.NewAccountBranch;
  RecordAcc.Chapter       = 1;
  RecordAcc.NameAccount   = ReqChangeAccObj.NewAccountName;
  RecordAcc.Type_Account  = ReqChangeAccObj.NewAccountType; /* Арест на дебет */
  RecordAcc.Kind_Account  = ПолучитьВидСчета( RecordAcc.Balance, RecordAcc.Chapter, RecordAcc.Code_Currency );
  RecordAcc.UserField1    = "Открыт по распоряжению на изменение номера счета";

  ClearRecord( ab );
  ab.Account       = RecordAcc.Account;
  ab.Code_Currency = RecordAcc.Code_Currency;
  ab.Chapter       = RecordAcc.Chapter;
  ab.Balance0      = ReqChangeAccObj.Balance(0);
  ab.Balance1      = ReqChangeAccObj.Balance(1);
  ab.Balance2      = ReqChangeAccObj.Balance(2);
  ab.Balance3      = ReqChangeAccObj.Balance(3);
  ab.Balance4      = ReqChangeAccObj.Balance(4);
  ab.Balance5      = ReqChangeAccObj.Balance(5);
  ab.Balance6      = ReqChangeAccObj.Balance(6);
  ab.Balance7      = ReqChangeAccObj.Balance(7);
  ab.Balance8      = ReqChangeAccObj.Balance(8);
  ab.Balance9      = ReqChangeAccObj.Balance(9);
  ab.Balance10     = ReqChangeAccObj.Balance(10);
  ab.Balance11     = ReqChangeAccObj.Balance(11);
 
  if( not InsertAccount( RecordAcc, ab, NULL, true ) )
    MsgBox( "Ошибка при открытии счета " + ReqChangeAccObj.NewAccount );
    return  1;
  end;

  return stat;

END;
