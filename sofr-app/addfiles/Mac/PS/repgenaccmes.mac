 /*
 $Name: repgenaccmes.mac
 $Module: РКО
 $Description: Протокол выполнения процедуры формирования сообщений в ФНС
 */

// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//  Описание   : Протокол выполнения процедуры формирования сообщений в ФНС
//
//  Программист: Чукина Т.А.
//
//  Создан     : 23.03.2015
//
// --------------------------------------------------------------------

import globals, "bnk_ptlib.mac", likepy, FIInter;

private macro GetMesKind(ObjKind : integer, DateChange : date) : string
  var MesKindStr : string = "";

  if(ObjKind == OBJTYPE_REQOPENA)
    MesKindStr = "об открытии счета";
  elif(ObjKind == OBJTYPE_REQCHANGE)
    MesKindStr = string("об изменении счета с ", DateChange:f);
  elif(ObjKind == OBJTYPE_REQCLOSA)
    MesKindStr = "о закрытии счета";
  end;

  return MesKindStr;
end;

private macro GetDetails
(
  ObjKind : integer,
  AccountChange : bool,
  Account2 : string,
  ClientChange : bool,
  ClientID : integer,
  BankChange : bool,
  OldBankName : string,
  ReqSubKindName : string
) : string

  var Details : string = "";

  if(ObjKind == OBJTYPE_REQCHANGE)
    var ChKind : TArray = TArray();

    if(AccountChange)
      ChKind[ ChKind.size ] = "изменился номер счета на номер " + Account2;
    end;

    if(ClientChange)
      ChKind[ ChKind.size ] = "изменились реквизиты клиента " + Bnk_GetPartyShortName(ClientID);
    end;

    if(BankChange)
      ChKind[ ChKind.size ] = "счет передан из филиала (банка) " + OldBankName;
    end;

    Details = "характер изменений: " + join(ChKind, "; ");

  elif(ObjKind == OBJTYPE_REQCLOSA)
    Details = "вид документа: " + ReqSubKindName;
  end;

  return Details;
end;

MACRO PrintReportHeader
(
  Accounts : string,
  ObjKind : integer,
  ChangeDate : date,
  AccountChange : bool,
  Account2 : string,
  ClientChange : bool,
  ClientID : integer,
  BankChange : bool,
  OldBankName : string,
  ReqSubKindName : string
)

  var Details : string = GetDetails
    ( ObjKind, AccountChange, Account2, ClientChange, ClientID, 
      BankChange, OldBankName, ReqSubKindName );

  [
    Протокол выполнения процедуры формирования сообщений в ФНС 

    #
    Дата выполнения:  #
    Исполнитель:      #

    Параметры формируемых сообщений: 
      счет(а): #
      вид сообщений: #
      #
    ┌─────────────────────────┬────────┬───────────┬──────────────────────────────────────────────────────────────────────┐
    │    Номер счета          │ Валюта │ Тип счета │     Результат                                                        │
  ]
  ( {Name_Bank}, {curdate}:f, GetNameOper({oper}), 
    Accounts, GetMesKind(ObjKind, ChangeDate), Details );

END;

MACRO PrintReportLine( addrAccount, ResultMsg : string )
  record account(account);
  SetBuff( account, addrAccount );

  [ ├─────────────────────────┼────────┼───────────┼──────────────────────────────────────────────────────────────────────┤
    │#########################│ #####  │###########│######################################################################│
  ]
  ( account.Account, ПолучитьКодФинИн(account.Code_Currency):c, account.Type_Account:c, ResultMsg:w );

END;

MACRO PrintReportFooter()
  [ └─────────────────────────┴────────┴───────────┴──────────────────────────────────────────────────────────────────────┘
  ];
END;
