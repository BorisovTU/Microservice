/*
 $Name: pr_fnsmes.mac
 $Module: РКО
 $Description: Сверка принятых и отправленных сообщений ФНС
 */

/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  Created     : 13.02.2014
  Programmer  : Polyakov E
  Description : Сверка принятых и отправленных сообщений ФНС

└───────────────────────────────────────────────────────────────────────────*/
import BankInter, WldInter, oralib, likepy;

private macro FillViewParams( ObjectType, ObjectID )
  
  execStoredFunc( "WLD_FNSANS.clearViewParam", V_UNDEF ) ;
  execStoredFunc( "WLD_FNSANS.addObjectID", V_UNDEF, makeArray( SQLParam( "p_ObjectID", ObjectID )) );
  execStoredFunc( "WLD_FNSANS.addObjectType", V_UNDEF, makeArray( SQLParam( "p_ObjectType", ObjectType )) );

end;
  
  

private macro printHeader(Dprt, Tp, DS, DE)
  var Num, Name, TpName, Desc;
  file f(wltransp) key 0;
  f.TpID = Tp;
  if (GetEQ(f))
    TpName = f.Name;
    Desc = f.Description;
  else
    TpName = "";
    Desc = "";
  end;
  CB_GetDepartmentCodeAndName(Dprt, Num, Name);
[                            СВЕРКА ПРИНЯТЫХ И ОТПРАВЛЕННЫХ СООБЩЕНИЙ ФНС

    Филиал           ####### #
    Транспорт        ####### #
    За период        с ########## по ##########
 ](Num, Name, TpName, Desc, DS, DE);
end;

private macro printNotProcessed(rs)
[Сообщения ФНС, обработка которых не завершена:
 ┌────────────────────────────────────────────────────────┬───────────────────────────────────────────────────────────────────────────────────────────────────────────────┐
 │              Принятые сообщения                        │                                           Результат обработки                                                 │
 ├─────┬─────┬──────┬─────────────────────────┬───────────┼────────────────────────────┬──────┬────────────────────┬──────────────────────────────────────────────────────┤
 │Форма│Релиз│Код НО│Референс                 │Дата сеанса│       Вид документа        │  ID  │ Реквизиты объекта  │                    Комментарий                       │
 ├─────┼─────┼──────┼─────────────────────────┼───────────┼────────────────────────────┼──────┼────────────────────┼──────────────────────────────────────────────────────┤];
  var MesID = 0;
  var item;

  for (item, rs)
    if(item[0] != MesID)
      [│#####│#####│######│#########################│########## │############################│######│####################│######################################################│]
          (item[1], item[2], item[3], item[4], item[5], item[6]:w:20, item[7], item[8], item[9]);
      MesID = item[0];
    else
      [│     │     │      │                         │           │############################│######│####################│######################################################│]
          (item[6]:w:20, item[7], item[8], item[9]);
    end;
  end;

[└─────┴─────┴──────┴─────────────────────────┴───────────┴────────────────────────────┴──────┴────────────────────┴──────────────────────────────────────────────────────┘
];
end;

private macro printClosedWithoutAnswer(rs)
[Сообщения ФНС, обработанные без отправки ответных сообщений:
 ┌─────┬─────┬──────┬─────────────────────────┬───────────┐
 │Форма│Релиз│Код НО│Референс                 │Дата сеанса│
 ├─────┼─────┼──────┼─────────────────────────┼───────────┤];
  var MesID = 0;
  var item;

  for (item, rs)
    if(item[0] != MesID)
      [│#####│#####│######│#########################│########## │]
          (item[1], item[2], item[3], item[4], item[5]);
      MesID = item[0];
    end;
  end;

[└─────┴─────┴──────┴─────────────────────────┴───────────┘
];
end;

private macro printSendedAnswer(rs)
[Сообщения ФНС, по которым отправлены ответные сообщения:
 ┌────────────────────────────────────────────────────────┬──────────────────────────────────────────────┐
 │              Принятые сообщения                        │           Отправленные сообщения             │
 ├─────┬─────┬──────┬─────────────────────────┬───────────┼─────┬─────┬────────────────────┬─────────────┤
 │Форма│Релиз│Код НО│Референс                 │Дата сеанса│Форма│Релиз│Референс            │Дата доставки│
 ├─────┼─────┼──────┼─────────────────────────┼───────────┼─────┼─────┼────────────────────┼─────────────┤];
  var MesID = 0;
  var item;

  for (item, rs)
    if(item[0] != MesID)
      [│#####│#####│######│#########################│########## │#####│#####│####################│##########   │]
          (item[1], item[2], item[3], item[4], item[5], item[6], item[7], item[8], item[9]);
      MesID  =item[0];
    else
      [│     │     │      │                         │           │#####│#####│####################│##########   │]
          (item[6], item[7], item[8], item[9]);
    end;
  end;

[└─────┴─────┴──────┴─────────────────────────┴───────────┴─────┴─────┴────────────────────┴─────────────┘
];
end;

macro PrintFNSMes(Dprt, Tp, Form, Rls, DS, DE, Trn, Num, Oper, NOCode)
  printHeader(Dprt, Tp, DS, DE);
  var query: string = "select wlmes.*, case when wlmrls.t_FormID in (183, 184) then substr(wlmes.t_trn, 8, 4) else substr(wlmes.t_trn, 5, 4) end t_NOCode, "
                      "       wlmesfrm.t_Name t_FrmName, wlmrls.t_Name t_RlsName, wlsess.t_BankDate, NVL(mlnk.t_ObjID, 0) t_ObjID, NVL(mlnk.t_ObjKind, 0) t_ObjKind, "
                      "       decode(NVL(mlnk.t_ObjKind, 0), 517, decode(NVL(resprm.t_State, 0), 50, 1, 2), 505, decode(NVL(req.t_State, 0), 70, 1, 2), 519, decode(NVL(rgd.t_State, 0), 50, 1, 2), 524, decode(NVL(rgd.t_State, 0), 50, 1, 2), 0) t_OState, "
                      "       NVL(decode(NVL(mlnk.t_ObjKind, 0), 517, resprm.t_CreateDate, 505, req.t_PmDateValue, 501, rmp.t_Date, rgd.t_Date), to_date('01010001', 'DDMMYYYY')) t_ODate, "
                      "       NVL(decode(NVL(mlnk.t_ObjKind, 0), 517, to_char(resprm.t_ResultID), 505, req.t_Trn, 501, rmp.t_Number, rgd.t_Number), '0') t_ONumber "
                      " from dwlmes_dbt wlmes, dwltpshem_dbt wltpsh, dwlmesfrm_dbt wlmesfrm, dwlmesrls_dbt wlmrls, dwlsess_dbt wlsess, dwlmeslnk_dbt mlnk, "
                      "      dwlregdec_dbt rgd, dwlreq_dbt req, dpmrmprop_dbt rmp, dwlresprm_dbt resprm "
                      " where wlmes.t_Direct = 'X' "
                      " and wlmes.t_Kind = 13 "
                      " and wlmes.t_SessionID = wlsess.t_SessionID "
                      " and wlmes.t_Department = ? "
                      " and wlsess.t_BankDate between ? and ? "
                      " and wlmes.t_TpSchemID = wltpsh.t_TpShemID "
                      " and wlmesfrm.t_FormID = wlmrls.t_FormID "
                      " and wlmes.t_RlsFormID = wlmrls.t_RlsFormID "
                      " and mlnk.t_MesID(+) = wlmes.t_MesID "
                      " and rgd.t_DecisionID(+) = mlnk.t_ObjID "
                      " and req.t_ReqID(+) = mlnk.t_ObjID "
                      " and rmp.t_PaymentID(+) = mlnk.t_ObjID "
                      " and resprm.t_ResultID(+) = mlnk.t_ObjID ";
  var Param = MakeArray(SQLParam("", Dprt), SQLParam("", DS), SQLParam("", DE));
  if (Tp != 0)
    query = query + " and wltpsh.t_TpID = ? ";
    Param[Param.size] = SQLParam("", Tp);
  end;

  if (Form != 0)
    query = query + " and wlmrls.t_FormID = ? ";
    Param[Param.size] = SQLParam("", Form);
  end;

  if (Rls != 0)
    query = query + " and wlmes.t_RlsFormID = ? ";
    Param[Param.size] = SQLParam("", Rls);
  end;

  if (Trn != "")
    query = query + " and wlmes.t_Trn = ? ";
    Param[Param.size] = SQLParam("", Trn);
  end;

  if (Num != 0)
    query = query + " and wlsess.t_Number = ? ";
    Param[Param.size] = SQLParam("", Num);
  end;

  if (Oper != 0)
    query = query + " and wlmes.t_UserID = ? ";
    Param[Param.size] = SQLParam("", Oper);
  end;

  if (NOCode != "")
    query = query + " and ((wlmrls.t_FormID in (183, 184) and substr(wlmes.t_trn, 8, 4) = ?) or substr(wlmes.t_trn, 5, 4) = ?) ";
    Param[Param.size] = SQLParam("", NOCode);
    Param[Param.size] = SQLParam("", NOCode);
  end;
  
  query = query + " order by wlmes.t_mesID";
  var query2: string;
  var query3 = "select wlans.*, NVL(wlmes.t_State, -1) mesState, wlmes.t_Trn mesTrn, wlmes.t_OutsideAbonentDate, rls.t_Name rlsName, frm.t_Name frmName "
           "  from dwlfnsans_dbt wlans, dwlmeslnk_dbt mlnk2, dwlmes_dbt wlmes, dwlmesrls_dbt rls, dwlmesfrm_dbt frm "
           " where mlnk2.t_ObjID(+) = wlans.t_AnswerID and mlnk2.t_ObjKind(+) = wlans.t_AnswerType "
           " and wlmes.t_MesID(+) = mlnk2.t_MesID "
           " and rls.t_RlsFormID(+) = wlmes.t_RlsFormID "
           " and frm.t_FormID(+) = rls.t_FormID";
  var rs = execSQLSelect(query, Param);
  var rsNP = TArray(); //NotProcessed
  var rsSA = TArray(); //SendedAnswer
  var rsCWA = TArray();//ClosedWithoutAnswer
  var LastMesId = 0;
  var AsNP = False, AsCWA = False;
  var rsTmp, mesState;
  var rr;
  if (rs)
    while (rs.moveNext())
      if (rs.value("t_State") != 60)
        if (LastMesId != rs.value("t_MesID"))
          LastMesId = rs.value("t_MesID");
          rsNP[rsNP.size] = MakeArray(LastMesId, rs.value("t_FrmName"), rs.value("t_RlsName"), rs.value("t_NOCode"), rs.value("t_trn"), date(rs.value("t_BankDate")), 
                                      "", "", "", "Необработано принятое сообщение");
          AsNP = AsCWA = True;
        end;
      elif (rs.value("t_ObjID") == 0)
        LastMesId = rs.value("t_MesID");
        rsNP[rsNP.size] = MakeArray(LastMesId, rs.value("t_FrmName"), rs.value("t_RlsName"), rs.value("t_NOCode"), rs.value("t_trn"), date(rs.value("t_BankDate")), 
                                    "", "", "", "Не сформирован учетный объект по принятому сообщению");
        AsNP = AsCWA = True;
      else
        if (LastMesId != rs.value("t_MesID"))
          LastMesId = rs.value("t_MesID");
          /*query2 = "select kk, count(kk) from (select case when wlmes.t_State < 30 then 0 else 1 end kk from dwlfnsans_dbt wlans, dwlmeslnk_dbt mlnk, dwlmeslnk_dbt mlnk2, dwlmes_dbt wlmes "
                   " where mlnk.t_MesID = ? and ((wlans.t_ObjectID = mlnk.t_ObjID and wlans.t_ObjectType = mlnk.t_ObjKind) or (wlans.t_ObjectID = ? and wlans.t_ObjectType = 502)";
          var TmpArr = MakeArray(SQLParam("", LastMesId), SQLParam("", LastMesId));
          query2 = query2 + string(") "
                   " and mlnk2.t_ObjID = wlans.t_AnswerID and mlnk2.t_ObjKind = wlans.t_AnswerType "
                   " and wlmes.t_MesID = mlnk2.t_MesID) group by kk");
          rsTmp = execSQLSelect(query2, TmpArr);
          var Ans = MakeArray(0, 0);
          //[0] - колво неотправленных ответов
          //[1] - колво отправленных ответов
          while (rsTmp.moveNext())
            Ans[rsTmp.value(0)] = rsTmp.value(1);
          end;
          AsNP = ((Ans[0] > 0)// есть неотправленные ответы
              or ((Ans[0] == 0) and (Ans[1] == 0)));// ответов вообще нет
          AsCWA = (Ans[0] == 0) and (Ans[1] == 0);*/
           
          FillViewParams(502, LastMesId);
          rr = execSQLSelect(query3);
          while( true )
            if( rr and rr.moveNext() )
              mesState = rr.value("mesState");
            else
              mesState = -2;
            end;
            if (mesState == -1)// нет сообщений
              rsNP[rsNP.size] = MakeArray(LastMesId, rs.value("t_FrmName"), rs.value("t_RlsName"), rs.value("t_NOCode"), rs.value("t_trn"), date(rs.value("t_BankDate")),
                                          "Сообщение ФНС", int(rr.value("t_AnswerID")), rs.value("t_MesID"), "Не сформировано ответное сообщение по учетному объекту");
              AsNP = true;
            elif ((mesState >= 0) and (mesState < 30))// есть неотправленные ответы
              rsNP[rsNP.size] = MakeArray(LastMesId, rs.value("t_FrmName"), rs.value("t_RlsName"), rs.value("t_NOCode"), rs.value("t_trn"), date(rs.value("t_BankDate")), 
                                          "Сообщение ФНС", int(rr.value("t_AnswerID")), rs.value("t_MesID"), "Не отправлено ответное сообщение по учетному объекту");
              AsNP = true;
            elif (mesState >= 30)
            else // ответов вообще нет
            end;
            if( mesState == -2 )
              break;
            end;
          end;
        end;
        //rr = CheckAnswer(rs.value("t_ObjID"), rs.value("t_ObjKind"));
          FillViewParams(rs.value("t_ObjKind"), rs.value("t_ObjID"));
          rr = execSQLSelect(query3);
        while( true )
          if( rr and rr.moveNext() )
            mesState = rr.value("mesState");
          else
            mesState = -2;
          end;
          if (mesState == -1)// нет сообщений
            rsNP[rsNP.size] = MakeArray(LastMesId, rs.value("t_FrmName"), rs.value("t_RlsName"), rs.value("t_NOCode"), rs.value("t_trn"), date(rs.value("t_BankDate")), 
                                        rr.value("t_AnswerSubType"), int(rr.value("t_AnswerID")), string("№", int(rs.value("t_ONumber")), " от ", date(rs.value("t_ODate"))), 
                                        "Не сформировано ответное сообщение по учетному объекту");
          elif ((mesState >= 0) and (mesState < 30))// есть неотправленные ответы
            rsNP[rsNP.size] = MakeArray(LastMesId, rs.value("t_FrmName"), rs.value("t_RlsName"), rs.value("t_NOCode"), rs.value("t_trn"), date(rs.value("t_BankDate")), 
                                        rr.value("t_AnswerSubType"), int(rr.value("t_AnswerID")), string("№", int(rs.value("t_ONumber")), " от ", date(rs.value("t_ODate"))), 
                                        "Не отправлено ответное сообщение по учетному объекту");
          else
            if (rs.value("t_OState") == 1)
              if (mesState >= 30)
                rsSA[rsSA.size] = MakeArray(LastMesId, rs.value("t_FrmName"), rs.value("t_RlsName"), rs.value("t_NOCode"), rs.value("t_trn"), date(rs.value("t_BankDate")), 
                                            rr.value("rlsName"), rr.value("frmName"), rr.value("mesTrn"), date(rr.value("t_OutsideAbonentDate")));
              else
                rsCWA[rsCWA.size] = MakeArray(LastMesId, rs.value("t_FrmName"), rs.value("t_RlsName"), rs.value("t_NOCode"), rs.value("t_trn"), date(rs.value("t_BankDate")));
              end;
            elif (mesState >= 30)
              rsSA[rsSA.size] = MakeArray(LastMesId, rs.value("t_FrmName"), rs.value("t_RlsName"), rs.value("t_NOCode"), rs.value("t_trn"), date(rs.value("t_BankDate")), 
                                          rr.value("rlsName"), rr.value("frmName"), rr.value("mesTrn"), date(rr.value("t_OutsideAbonentDate")));
            else
              var Name = "";
              if ((rs.value("t_ObjKind") == 519) or (rs.value("t_ObjKind") == 524))
                var tmp = execSQLSelect("select LLV.T_NOTE from  DWLREGDEC_DBT WLREGDEC, DLLVALUES_DBT LLV "
                                        " where WLREGDEC.t_DecisionID = ? AND LLV.T_ELEMENT = WLREGDEC.T_TYPE AND LLV.T_LIST = 2046", 
                                        MakeArray(SQLParam("", rs.value("t_ObjID"))));
                if (tmp and tmp.MoveNext)
                  Name = tmp.value(0);
                end;
              elif (rs.value("t_ObjKind") == 517)
                Name = "Подтверждение банка";
              elif (rs.value("t_ObjKind") == 505)
                Name = "Запрос ФНС";
              elif (rs.value("t_ObjKind") == 502)
                Name = "Сообщение ФНС";
              else
                Name = "Платеж";
              end;
              rsNP[rsNP.size] = MakeArray(LastMesId, rs.value("t_FrmName"), rs.value("t_RlsName"), rs.value("t_NOCode"), rs.value("t_trn"), date(rs.value("t_BankDate")), 
                                          Name, int(rs.value("t_ObjID")), string("№", int(rs.value("t_ONumber")), " от ", date(rs.value("t_ODate")))
                                          /* + string(rs.value("t_ObjID"), " ", rs.value("t_ObjKind"))*/, "Не обработан учетный объект");
            end;
          end;
          if( mesState == -2 ) 
            break; 
          end;
        end;
      end;
    end;
  end;
  if (rsNP.size > 0)
    printNotProcessed(rsNP);
  end;
  if (rsCWA.size > 0)
    printClosedWithoutAnswer(rsCWA);
  end;
  if (rsSA.size > 0)
    printSendedAnswer(rsSA);
  end;
  return true;
end;