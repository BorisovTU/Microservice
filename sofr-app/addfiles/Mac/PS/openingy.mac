/* -------------------------------------------------------------------- */
/*         Автоматизированная банковская система RS-Bank v6.0           */
/*                 Copyright (c) R-Style Software Lab                   */
/*                                                                      */
/*  Имя файла  : openingy.mac                                           */
/*                                                                      */
/*  Описание   : Операция - 3030 - "Открытие лицевого счета"            */
/*               Шаг      - 50   - "Открытие транзитного счета"         */
/*                                                                      */
/*  Программист: Андреева Е.П.                                          */
/*                                                                      */
/*  Создан     : 15.11.2004                                             */
/*                                                                      */
/* -------------------------------------------------------------------- */
IMPORT InsCarryDoc, reqinter, OprInter, PSInter;

var ReqOpenAccObj:RsbReqOpenAcc;
record RecordLinkAcc( account  );
record ab           ( accblnc  );
record RecordAcc    ( account  );

RECORD req( reqopena );
var TB_ReqOpenAcc = TBFile("reqopena.dbt",  "R", 0, "reqopena.dbt",  "bank.def");

/* Выполнение шага */
MACRO ExecuteStep( doc, reqacc )
   var stat:integer = 0;
   var ID_Link;

   var RecAcc  :TRecHandler = TRecHandler( "account.dbt", "bank.def" );

   var reqlinka:TRecHandler = TRecHandler( "reqlinka.dbt", "bank.def");
   reqlinka = GetReqLinkAcc( ReqOpenAccObj.RequestID, 230 );

   if( reqlinka.rec.RequestID != 0 )

     stat = CheckKey( 1, reqlinka.rec.Account );

     if( stat )
       DisplayError();
       return stat;
     end;

     if( not Req_IsAccExist( reqlinka.rec.Code_Currency, reqlinka.rec.Account ) )
       
       if( not GetString( reqlinka.rec.Account, "Счет уже существует:", 20 ) )
           return 1;
       end;

     end;  

     ClearRecord( RecordLinkAcc );

     RecordLinkAcc.Account = reqlinka.rec.Account;

     RecordLinkAcc.Code_Currency = reqlinka.rec.Code_Currency;
     RecordLinkAcc.Balance       = reqlinka.rec.Balance0;

     if( RecordLinkAcc.Balance == "" )
       RecordLinkAcc.Balance = ReqOpenAccObj.Balance(0);
     end;

     RecAcc = GetAccount( 1, ReqOpenAccObj.Account, ReqOpenAccObj.Code_Currency );

     RecordLinkAcc.Open_Date     = RecAcc.rec.Open_Date;
     RecordLinkAcc.Oper          = RecAcc.rec.Oper;
     RecordLinkAcc.Client        = RecAcc.rec.Client;
     RecordLinkAcc.Department    = RecAcc.rec.Department;
     RecordLinkAcc.Branch        = RecAcc.rec.Branch;
     RecordLinkAcc.Chapter       = reqlinka.rec.Chapter;
     RecordLinkAcc.Type_Account  = "Y"; /* Транзитный счет */
     RecordLinkAcc.NameAccount   = "Транзитный счет для зачисления валютных поступлений к текущему счету " + RecAcc.rec.Account;
     RecordLinkAcc.Kind_Account  = ПолучитьВидСчета( RecordLinkAcc.Balance, RecordLinkAcc.Chapter, RecordLinkAcc.Code_Currency );
     RecordLinkAcc.UserField1    = "Открыт по заявлению на открытие счета";

     ClearRecord( ab );
     ab.Account       = RecordLinkAcc.Account;
     ab.Code_Currency = RecordLinkAcc.Code_Currency;
     ab.Chapter       = RecordLinkAcc.Chapter;
     ab.Balance0      = reqlinka.rec.Balance0;
     ab.Balance1      = reqlinka.rec.Balance1;
     ab.Balance2      = reqlinka.rec.Balance2;
     ab.Balance3      = reqlinka.rec.Balance3;
     ab.Balance4      = reqlinka.rec.Balance4;
     ab.Balance5      = reqlinka.rec.Balance5;
     ab.Balance6      = reqlinka.rec.Balance6;
     ab.Balance7      = reqlinka.rec.Balance7;
     ab.Balance8      = reqlinka.rec.Balance8;
     ab.Balance9      = reqlinka.rec.Balance9;
     ab.Balance10     = reqlinka.rec.Balance10;
     ab.Balance11     = reqlinka.rec.Balance11;

     if( not InsertAccount( RecordLinkAcc, ab, ID_Link, true ) )
       MsgBox( "Ошибка при открытии транзитного счета " + RecordLinkAcc.Account );
       return  1;
     end;

     Copy( RecordAcc, RecAcc );
     /* Осуществляем привязку транзитного счета */
     if( not InsertObjectLink( OBJTYPE_ACCOUNT, reqlinka.rec.GroupID,
                               UniID( RecordAcc, OBJTYPE_ACCOUNT ), false,
                               OBJTYPE_ACCOUNT, ID_Link, true ) )
       MsgBox( "Ошибка при привязке транзитного счета " + RecordLinkAcc.Account );
       return  1;
     end;

     /* Создаем сообщение в МНС */
     TB_ReqOpenAcc.rec.RequestID = ReqOpenAccObj.RequestID;

     if( TB_ReqOpenAcc.GetEQ() )
       Copy( req, TB_ReqOpenAcc);
     end;

   else
     MsgBox("В списке связанных счетов не задан номер транзитного счета");
     return 1;
   end;

   return 0;
END;
