import FIInter, pralcash, prcsord;


MACRO PrintCBCashOrderHeader(numorder, dateorder, rate, НомерФормы, НомерЭкземпляра, ВсегоЭкземпляров)

  var НомерФормыЭкземпляра = ПолучитьНомерФормыСБ(НомерФормы,НомерЭкземпляра,ВсегоЭкземпляров);

[                                                                     #########################](НомерФормыЭкземпляра:r);
[                                                                                 ######## экз.](НомерЭкземпляра:r);
[
 ########################################################                         #############
 ________________________________________________________             Курс валюты _____________
   наименование филиала Сбербанка России
]({Name_Bank}:c,rate:c);

[
 ##############################################################################################


 ##############################################################################################


](numorder:c,GetPrDateCB(dateorder):c);

END;

MACRO PrintCBCashOrder(ground,debitAcc,creditAcc,sumCur,sumNatCur,Code_Currency,ВсегоЭкземпляров)
 var счетчик;
 ARRAY СОДЕРЖАНИЕ_ОПЕРАЦИИ, SUM_CURRENCY, SUM_NATCUR;

 var SumString       = CurToStrAlt(sumCur,    null, null, GetISOCode(Code_Currency));
 var SumNatCurString = CurToStrAlt(sumNatCur, null, null, GetISOCode(NATCUR));


 StrSplit(ground,         СОДЕРЖАНИЕ_ОПЕРАЦИИ, 74);
 StrSplit(SumString,      SUM_CURRENCY,        82);
 StrSplit(SumNatCurString,SUM_NATCUR,          94, 36);

 счетчик = 0;
 while(ValType(СОДЕРЖАНИЕ_ОПЕРАЦИИ(счетчик)) != V_UNDEF)
[                    ##########################################################################]
(СОДЕРЖАНИЕ_ОПЕРАЦИИ(счетчик));
счетчик = счетчик + 1;
 end;
[Содержание операции __________________________________________________________________________
];
[

 Основание ____________________________________________________________________________________

];
[┌─────────────────────────┬─────────────────────────┬────────────────┬─────────────────────────┐
 │     Дебет счета №       │    Кредит счета №       │  Сумма валюты  │ Сумма валюты в рублевом │
 │                         │                         │    (цифрами)   │   эквиваленте по курсу  │
 │                         │                         │                │  Банка России (цифрами) │
 ├─────────────────────────┼─────────────────────────┼────────────────┼─────────────────────────┤
 │#########################│#########################│################│#########################│
 └─────────────────────────┴─────────────────────────┴────────────────┴─────────────────────────┘
](debitAcc:f, creditAcc:f, sumCur:r:a, sumNatCur:r:a);

[
 Приложение на ## листe(ах)

](ВсегоЭкземпляров);

 счетчик = 0;
 while(ValType(SUM_CURRENCY(счетчик)) != V_UNDEF)
[             ##################################################################################]
(SUM_CURRENCY(счетчик));
счетчик = счетчик + 1;
 end;
[Сумма валюты __________________________________________________________________________________

];

[                                                          #####################################]
(SUM_NATCUR(0));
[Сумма валюты в рублевом эквиваленте по курсу Банка России _____________________________________];
 счетчик = 1;
 while(ValType(SUM_NATCUR(счетчик)) != V_UNDEF)
[
 ###############################################################################################
 _______________________________________________________________________________________________]
(SUM_NATCUR(счетчик));
счетчик = счетчик + 1;
 end;


END;

MACRO PrintOutCashDocCB( order, payment )

 ncopy    = GetNumCopy();
 var rate = ПолучитьКурсОрдера(payment);
 var НомерЭкземпляра = 1;
 var ВсегоЭкземпляров = ncopy;
 var НаименованиеКлиента = "";
 var sumNatCur = ПолучитьСуммуВНациональнойВалюте(payment);

 var счетчик;
 ARRAY SUM_CURRENCY;
 var SumString = CurToStrAlt(payment.Amount,    null, null, GetISOCode(payment.FIID));
 StrSplit(SumString, SUM_CURRENCY, 50);

 while(ncopy != 0)
   PrintCBCashOrderHeader("РАСХОДНЫЙ КАССОВЫЙ ОРДЕР № " + order.Numb_Document,
                          payment.ValueDate,
                          rate,
                          229,
                          НомерЭкземпляра,
                          ВсегоЭкземпляров);

[       #######################################################################################
 Выдать _______________________________________________________________________________________
               (Ф.И.О получателя)

](order.FIOClient:l);

[######################### № ############### выдан ############################################
 ______________________________________________________________________________________________
 (данные документа, удостоверяющие личность)

](GetPaperKindName(order.PaperKind), order.PaperSeries + " " + order.PaperNumber,
  order.PaperIssuer + " " + string(order.PaperIssuedDate:f));

   PrintCBCashOrder(order.Ground,payment.ReceiverAccount,payment.PayerAccount,payment.Amount,sumNatCur,payment.FIID,ВсегоЭкземпляров);

[

 Указанную валюту в сумме:
];
 счетчик = 0;
 while(ValType(SUM_CURRENCY(счетчик)) != V_UNDEF)
[##################################################
 __________________________________________________](SUM_CURRENCY(счетчик));
   if(счетчик == 0)
[           (прописью)];
   end;
счетчик = счетчик + 1;
 end;

[                                                     Кассир __________________________________
 получил: _________________________
            (подпись получателя)                      Контролер _______________________________

                                                      Главный бухгалтер _______________________

];


   ncopy = ncopy - 1;
   НомерЭкземпляра = НомерЭкземпляра + 1;
 end;

END;

MACRO PrintOutCashDoc(ФормаПечати, ord, pm, sb )

 setbuff( payment, pm  );
 setbuff( order,   ord );
 setbuff( symb_rec, sb );

 if( (order.IsCurrency != "X") and (ФормаПечати == ФормаПечати_РасходныйКассовыйОрдерСБ) )
   MsgBox("Форма №229 не предназначена для печати рублевых ордеров");
   exit(1);
 end;

 if(ФормаПечати == ФормаПечати_РасходныйКассовыйОрдерСБ)
   return PrintOutCashDocCB(order, payment);
 else
   return PrintOutgoCashOrder(payment, order, MassCopy);
 end;

END;
