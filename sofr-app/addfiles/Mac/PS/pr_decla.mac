/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : pr_decla.mac
  Created     : 27.02.2007
  Programmer  : Осмаковский Е.В.
  Description : Печать распоряжения на закрытие счета

└───────────────────────────────────────────────────────────────────────────*/
import reqinter;

PRIVATE
macro DrawReport(AdressDep,
                 NameBranch,
                 DateOpenAcc,
                 NameClient,
                 StrRest,
                 Rest,
                 РАСПОРЯЖЕНИЕ_ДОЛЖ,
                 РАСПОРЯЖЕНИЕ_ФИО,
                 pr_reqclosa)

     var text;
     var point:string = "";

     if( pr_reqclosa.rec.OrderKind == 1 )
       point = "1";
     else
       point = "2";
     end;

     text = "   На основании ст.859 п.1." + point + " части второй Гражданского Кодекса произвести закрытие счета № " +
            pr_reqclosa.rec.Account + ", открытого  " + string(DateOpenAcc:m:f) + " " + NameClient + 
            ". Остаток средств на счете " + Rest + " " + StrRest + " " +  
             "Уведомление о закрытии счета отправлено в адрес клиента " + 
             string(date(ReadNoteForAccount( pr_reqclosa.rec.Account, pr_reqclosa.rec.Code_Currency, 24/*ACC_NOTE_KIND_DATE_CLOSED_ACC*/ )):m:f); 

[                                                                                             ];
[ ##########################################################################                  ]({Name_Bank}:w);
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[ #############################################################         ##################### ](AdressDep,{curdate}:f:m:r);
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[                                                                                 в отдел РКО ];
[                                                         ################################### ](NameBranch:w:r);
[                                                                                             ];
[                                         Распоряжение                                        ]; 
[                                                                                             ];
[                                                                                             ];
[ ########################################################################################### ](text:w);
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[ ######################## _________________ / ############################# /                  ](РАСПОРЯЖЕНИЕ_ДОЛЖ , РАСПОРЯЖЕНИЕ_ФИО:c);
[                                                                                             ];


end;

macro PrintDirectionClAcc(pr_reqclosa:TRecHandler):bool

   var AdressDep,
       NameBranch,
       DateOpenAcc,
       NameClient,
       Rest,
       StrRest,
       РАСПОРЯЖЕНИЕ_ДОЛЖ,
       РАСПОРЯЖЕНИЕ_ФИО,

       Client,
       TR_account:TRecHandler = TRecHandler( "account.dbt"  , "bank.def" ),
       TR_party:TRecHandler = TRecHandler( "party.dbt"  , "bank.def" ),
       err;

   AdressDep = GetRealAdressDep({OperDprtNode});
   NameBranch = GetNameBranch({OperDprtNode});

   TR_account = GetAccount( 1 , pr_reqclosa.rec.Account, pr_reqclosa.rec.Code_Currency);
   DateOpenAcc = TR_account.rec.Open_Date;
   Client = TR_account.rec.Client;

   if(GetPartyRec(Client,TR_party))
     NameClient = TR_party.rec.Name;
   else
     NameClient = "";
   end;

   //Остаток на счете
   if( pr_reqclosa.rec.Code_Currency == NATCUR )
      Rest = RestA(pr_reqclosa.rec.Account , {curdate});
      StrRest = string(Rest:a) +" ( " + RubToStrAlt(Rest) + " ).";
   else
      Rest = RestAC(pr_reqclosa.rec.Account, pr_reqclosa.rec.Code_Currency, {curdate});
      StrRest = string(Rest:a) +" ( " + CurToStrAlt(Rest, NULL, NULL, GetISOCode(pr_reqclosa.rec.Code_Currency)) + " ).";
   end;

  if( GetRegValForOPENAC( "РАСПОРЯЖЕНИЕ_ДОЛЖ", V_STRING, РАСПОРЯЖЕНИЕ_ДОЛЖ ) )
     РАСПОРЯЖЕНИЕ_ДОЛЖ = "";
  end;

  if( GetRegValForOPENAC( "РАСПОРЯЖЕНИЕ_ФИО", V_STRING, РАСПОРЯЖЕНИЕ_ФИО ) )
     РАСПОРЯЖЕНИЕ_ФИО = "";
  end;

      DrawReport(AdressDep,
                 NameBranch,
                 DateOpenAcc,
                 NameClient,
                 StrRest,
                 Rest,
                 РАСПОРЯЖЕНИЕ_ДОЛЖ,
                 РАСПОРЯЖЕНИЕ_ФИО,
                 pr_reqclosa);

   return true;
   
end;
