//-----------------------------------------------------------------------------
// Блок      : Вне блока
// Шаг       : Вне шага
// Назначение: Макрос скроллинга
// Описание  : Макрос скроллинга заранее данных акцептов
//-----------------------------------------------------------------------------

import BankInter, likepy, oralib, globals, PSInter;

var PreAcptObj:RsbPreAccept;

record          Pmpreacpt( pmpreacpt );
record          OldPmpreacpt( pmpreacpt );

/*номера полей в панели*/
const fld_Account = 1,               /*Счет плательщика*/
      fld_DateFrom = 3,              /*Дата начала действия акцепта*/
      fld_ReceiverName = 6,
      fld_ReceiverBankName = 7,
      fld_ReceiverAccount = 9,       /*Счет получателя*/
      fld_CountCondition = 10,
      fld_GroundCondition = 14,
      fld_AmountCondition = 17,
      fld_AutoAcceptCondition = 20;

CONST SET_CHAR = "X", 
      UNSET_CHAR = "";

/* Установка подсказки для скролингов из макроса */
MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStates:integer ):string
   return DefaultHint;
END;

macro    Новый_Документ( )
   return 0;
end;

/* Выдать сообщение о незаполненности поля FieldName */
macro PrintErrMsg( FieldName )
  msgbox( "Не заполнено поле \'" + FieldName + "\'");
end;

macro PrintCondErrMsg( Text )
  msgbox( "В системе уже зарегистрирован заранее данный акцепт с противоположным признаком\'" + Text + ". Эти два акцепта являются взаимоисключающими\'");
end;

macro    Проверить_Документ( Режим )
  var stat:integer = 0;
  var PreObj:object;
  PreObj = GenObject( "RsbPreAccept", Pmpreacpt.PreacptID );  

  if( ( Режим == 2 ) or ( Режим == 3 ) or ( Режим == 8 ) )
                                     
  
    if( StrLen( Pmpreacpt.Account ) == 0 )
      PrintErrMsg( "Владелец счета" );
      return fld_Account;
    end;

    if( Pmpreacpt.DateFrom == date(0, 0, 0) )
      PrintErrMsg( "Дата " );
      return fld_DateFrom;
    end;

    if( StrLen( Pmpreacpt.ReceiverName ) == 0 )
      PrintErrMsg( "Получатель" );
      return fld_ReceiverName;
    end;

    if( StrLen( Pmpreacpt.ReceiverBankName ) == 0 )
      PrintErrMsg( "Банк получателя" );
      return fld_ReceiverBankName;
    end;

    if( StrLen( Pmpreacpt.ReceiverAccount ) == 0 )
      PrintErrMsg( "Счет получателя" );
      return fld_ReceiverAccount;
    end;
  end;
  
  if( Режим == 3 and (PreObj.GetCountDemand() > 0) )
    stat = CHANG_IMPORTANT;
  end; 
  
  if( ( Режим == 2 ) OR ( Режим == 3 ) )
    var rs:RsdRecordset,
    Query: string = " SELECT d.t_CountCondition, d.t_GroundCondition, d.t_AmountCondition, d.t_AutoAcceptCondition "+
                  " FROM dpmpreacpt_dbt d "+
                  " WHERE ( d.t_Client         = :Client           "+
                  "    OR d.t_Account          = :Account          "+
                  "   AND d.t_FIID             = :FIID             "+                       
                  "   AND d.t_Chapter          = :Chapter        ) "+                 
                  "   AND d.t_DateFrom         = :DateFrom         "+               
                  "   AND d.t_DateTo           = :DateTo           "+                   
                  "   AND d.t_ReceiverCode     = :ReceiverCode     "+        
                  "   AND d.t_ReceiverName     = :ReceiverName     "+       
                  "   AND d.t_ReceiverAccount  = :ReceiverAccount  "+ 
                  "   AND d.t_ReceiverBankCode = :ReceiverBankCode "+
                  "   AND d.t_ReceiverBankName = :ReceiverBankName "+
                  "   AND d.t_PreAcptID <> :PreAcptID ",

              
    params: TArray = makeArray( SQLParam( "Client"           , Pmpreacpt.Client           ), 
                                SQLParam( "Account"          , Pmpreacpt.Account          ),
                                SQLParam( "FIID"             , Pmpreacpt.FIID             ),
                              SQLParam( "Chapter"          , Pmpreacpt.Chapter          ),
                              SQLParam( "DateFrom"         , Pmpreacpt.DateFrom         ),
                              SQLParam( "DateTo"           , Pmpreacpt.DateTo           ),
                              SQLParam( "ReceiverCode"     , Pmpreacpt.ReceiverCode     ),
                              SQLParam( "ReceiverName"     , Pmpreacpt.ReceiverName     ),
                              SQLParam( "ReceiverAccount"  , Pmpreacpt.ReceiverAccount  ),
                              SQLParam( "ReceiverBankCode" , Pmpreacpt.ReceiverBankCode ),
                              SQLParam( "ReceiverBankName" , Pmpreacpt.ReceiverBankName ),
                              SQLParam( "PreAcptID" , Pmpreacpt.PreAcptID ) );
                                  
    rs = execSQLselect( Query, params , true  );
    while( rs.MoveNext() and rs )
      if( Pmpreacpt.CountCondition != rs.Value(0) )
           PrintCondErrMsg( "по количеству акцептуемых требований" ); 
           return fld_CountCondition;
      end;
      if( Pmpreacpt.GroundCondition != rs.Value(1)  )
            PrintCondErrMsg( "проверки основания платежа" ); 
           return fld_GroundCondition;
      end;
      if( Pmpreacpt.AmountCondition != rs.Value(2) )
           PrintCondErrMsg( "по сумме исполнения требования" ); 
            return fld_AmountCondition;
      end;
      if( Pmpreacpt.AutoAcceptCondition != rs.Value(3) )
          PrintCondErrMsg( "по необходимости немедленного акцепта" ); 
          return fld_AutoAcceptCondition;
      end;
    end;
  end;                                                                    
  return stat;

end;

macro    Функция_Пользователя( Режим:integer )        
 return  0;
end;