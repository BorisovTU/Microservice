/*
$Name:             reqopena.mac
$Module:           РКО юридических лиц
$Description:      Инициализация и проверки заявлений на открытие счета
*/
IMPORT reqinter, reqchecks, PSInter, "fnsaccmsg_lib.mac";

/* Проверяемые поля в панели заявления */
private const fld_type_account = 22,
              fld_name_account = 23,
              fld_pack_numb    = 27;
private const  ReqOpenAcc_Fld_Acc      = 18,
               ReqOpenAcc_Fld_LinkAcc  =  5,
               ReqOpenAcc_Fld_LinkABal =  4,
               ReqOpenAcc_Fld_LinkAFI  =  3,
               ReqOpenAcc_Fld_ClientButton = 25;
/*Для платежей*/

/*Хинт для списков по дате заявления*/
private const Hint_ByDate     :string = "/*+FIRST_ROWS LEADING(t oproper partcode) INDEX(t dreqopena_dbt_idx6) USE_NL(t oproper partcode)*/";
/*Хинт для списков по дате закрытия*/
private const Hint_ByCloseDate:string = "/*+FIRST_ROWS LEADING(t oproper partcode) INDEX(t dreqopena_dbt_idx5) USE_NL(t oproper partcode)*/";
/*Хинт для списков по статусу первички*/
private const Hint_ByStatus   :string = "/*+FIRST_ROWS LEADING(t oproper partcode) INDEX(t dreqopena_dbt_idx3) USE_NL(t oproper partcode)*/";
/*Хинт для списков по шагу*/
private const Hint_ByStep     :string = "/*+FIRST_ROWS LEADING(t oproper reqopena partcode) INDEX(t doprstep_dbt_idx10) INDEX(reqopena dreqopena_dbt_idx0) USE_NL(t oproper reqopena partcode)*/";

/* Установка подсказки для скролингов из макроса */
MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStates:integer ):string
  /*  Возможные значения ScrolStates:
      0  - Все                      
      1  - Отложенные               
      2  - Открытые                 
      3  - Закрытые                 
      4  - Отвергнутые              
      5  - Подготовленные к шагу */

  if( ScrolStates == 0 ) //Все

    return Hint_ByDate;

  elif( ScrolStates == 3 ) //Закрытые

    return Hint_ByCloseDate;  

  elif(    ( ScrolStates == 1 )   //Отложенные
        or ( ScrolStates == 2 )   //Открытые
        or ( ScrolStates == 4 ) ) //Отвергнутые
  
    return Hint_ByStatus;    

  elif( ScrolStates == 5 ) //Подготовленные к шагу

    return Hint_ByStep;

  end;

  return DefaultHint;
END;

/* Макрос генерации номера открываемого счета, для которого было введено заявление */
MACRO НомерСчетаЗаявления()
  ReqOpenAcc.Account=ПолучитьНомерСчетаЗаявления(ReqOpenAcc.Account, ReqOpenAcc.Balance0, ReqOpenAcc.Code_Currency, ReqOpenAcc.SpecialKind,  ReqOpenAcc.AccountDepartment);
  return 0;
END;

/* Макрос генерации номера связанного счета */
MACRO НомерСвязанногоСчетаЗаявления()
  ReqLinkAcc.Account=ПолучитьНомерСвязанногоСчетаЗаявления(ReqLinkAcc, ReqOpenAcc.Account, ReqOpenAcc.Balance0, ReqOpenAcc.AccountDepartment);
  return 0;
END;

// Функция предназначена для проверки номера основного счета в заявлении
MACRO ПроверитьНомерСчета():integer
  
  var AccKey, CodeKind, error;
  
  if( not IsStepWasExecuted( ReqOpenAcc.RequestID, PS_REQOPENA, 10/*открытие счета*/ ) )
    if ( (ReqOpenAcc.Account != "") and (ReqOpenAcc.Dummy != "X") )
      if ( ( NOT Req_IsAccExist(ReqOpenAcc.Code_Currency, ReqOpenAcc.Account) ) and ( SubStr(ReqOpenAcc.Account, 1, 1) != "L") )
        msgbox("Счет с таким номером уже существует");
        return ReqOpenAcc_Fld_Acc;
      end;

      if( ( ReqOpenAcc.DenialOpen == "" ) and ( SubStr(ReqOpenAcc.Account, 1, 5) != LZ( ReqOpenAcc.Balance0, 5 ) ) )
        msgbox("Заданный счет не может быть открыт на заданном балансовом счете");
        return ReqOpenAcc_Fld_Acc;
      end;

      if ( NOT Req_IsFIExist( SubStr(ReqOpenAcc.Account, 6, 3) ) )      
        msgbox("Код валюты счета не найден в справочнике");
        return ReqOpenAcc_Fld_Acc;
      end;
      
      if ( SubStr(ReqOpenAcc.Account, 6, 3) != LZ(ПолучитьКодФинИнДляСчета( ReqOpenAcc.Code_Currency ), 3) )
        msgbox("Валюта открываемого счета не соответствует заданной");
        return ReqOpenAcc_Fld_Acc;
      end;

      var PartyID = 0, BIC = "";
      getDeptPartyIDandBIC(ReqOpenAcc.AccountDepartment, @PartyID, @BIC);

      GetREgistryValue("CB/PAYMENTS/DEPCODEKIND", V_INTEGER, CodeKind, error);
      if ( SubStr(ReqOpenAcc.Account, 10, 4) != ПолучитьКодСубъекта(PartyID, CodeKind, error) )
        msgbox("Неверно задан код филиала в номере счета");
        return ReqOpenAcc_Fld_Acc;
      end;

      AccKey = GetKey( ReqOpenAcc.Account, BIC );
      if ( AccKey != ReqOpenAcc.Account )
        msgbox("Неверный ключ счета. Должен быть " + SubStr(AccKey, 9, 1));
        return ReqOpenAcc_Fld_Acc;
      end;

    end;
  end;
  
  return 0;
END;

// Функция предназначена для проверки номеров связанных счетов (в дистрибутиве реализуется только проверка транзитного счета)
MACRO ПроверитьНомерСвязанногоСчета():integer

  var AccKey, CodeKind, error;

  if( not IsStepWasExecuted( ReqOpenAcc.RequestID, PS_REQOPENA, 50/*открытие транзитного счета*/ ) )

    if ( (ReqLinkAcc.GroupID == OBJROLE_ACC_TRANSIT) and (ReqOpenAcc.Dummy != "X") )
      if ( NOT Req_IsAccExist(ReqLinkAcc.Code_Currency, ReqLinkAcc.Account) )
        msgbox("Счет с таким номером уже существует");
        return ReqOpenAcc_Fld_LinkAcc;
      end;

      if ( ReqOpenAcc.Balance0 != ReqLinkAcc.Balance0 ) 
        msgbox("Балансовые счета основного и транзитного счетов не совпадают");
        return ReqOpenAcc_Fld_LinkABal;
      end;

      if ( ReqOpenAcc.Code_Currency != ReqLinkAcc.Code_Currency ) 
        msgbox("Валюты основного и транзитного счетов не совпадают");
        return ReqOpenAcc_Fld_LinkAFI;
      end;

      if ( SubStr(ReqLinkAcc.Account, 1, 5) != LZ( ReqLinkAcc.Balance0, 5 ) )
        msgbox("Заданный счет не может быть открыт на заданном балансовом счете");
        return ReqOpenAcc_Fld_LinkAcc;
      end;

      if ( NOT Req_IsFIExist( SubStr(ReqLinkAcc.Account, 6, 3) ) )
        msgbox("Код валюты счета не найден в справочнике");
        return ReqOpenAcc_Fld_LinkAcc;
      end;
      
      if ( SubStr(ReqLinkAcc.Account, 6, 3) != LZ(ПолучитьКодФинИнДляСчета( ReqLinkAcc.Code_Currency ), 3) )
        msgbox("Валюта открываемого счета не соответствует заданной");
        return ReqOpenAcc_Fld_LinkAcc;
      end;
   
      var PartyID = 0, BIC = "";
      getDeptPartyIDandBIC(ReqOpenAcc.AccountDepartment, @PartyID, @BIC);

      GetREgistryValue("CB/PAYMENTS/DEPCODEKIND", V_INTEGER, CodeKind, error);
      if ( SubStr(ReqLinkAcc.Account, 10, 4) != ПолучитьКодСубъекта(PartyID, CodeKind, error) )
        msgbox("Неверно задан код филиала в номере счета");
        return ReqOpenAcc_Fld_LinkAcc;
      end;
    
      if ( SubStr(ReqLinkAcc.Account, 14, 1) != "1" )
        msgbox("В номере счета отсутствует признак транзитного счета");
        return ReqOpenAcc_Fld_LinkAcc;
      end;

      AccKey = GetKey( ReqLinkAcc.Account, BIC );
      if( AccKey != ReqLinkAcc.Account )
        msgbox("Неверный ключ счета. Должен быть " + SubStr(AccKey, 9, 1));
        return ReqOpenAcc_Fld_LinkAcc;
      end;

    end;

  end;

  return 0;
 
END;

MACRO Новое_Заявление()
  return 0;
END;

/* Номер ошибки заведомо больше числа полей, чтобы никуда не позиционироваться */
private const REQOPENA_JUSTERROR:integer = 1024;

MACRO Проверить_Заявление(Режим)

  var stat = 0;
  file Субъекты( party ) key 0;
  if( (ReqOpenAcc.Dummy != "X") and (StrLen(ReqOpenAcc.Type_Account) != 0) and
      ( not CheckTypeAccount( ReqOpenAcc.Type_Account ) ) )
         return fld_type_account;
  end;
                                        
  if ( (Режим == 2 ) or (Режим == 3) or (Режим == 8) )
    var errList = "";
    var IsIndividEmployer;
    var AttrPartyType; 
    var NotResident;
    var LegalForm;
    var RegValDeposit;
    GetRegistryValue( "PS\\REQOPENACC\\OPERATION\\365-П\\ВКЛАД_ФИЗЛИЦА", V_STRING, RegValDeposit);
    var RegValAccount;
    GetRegistryValue( "PS\\REQOPENACC\\OPERATION\\365-П\\СЧЕТ_ФИЗЛИЦА", V_STRING, RegValAccount);
    
    if( ReqOpenAcc.Department != {OperDprt})
      MsgBox("Запрещено редактирование заявлений других филиалов");
      return CHANG_NOTKEEP;
    end;  
  
    if(ReqOpenAcc.Dummy != "X")
    
      if( StrLen( ReqOpenAcc.NameAccount ) == 0 )
        MsgBox("Введите наименование счета");
        return fld_name_account;
      end;

      if( ReqOpenAcc.NumberPack < 0 )
        MsgBox("Номер пачки не может быть отрицательным");
        return fld_pack_numb;
      end;

      stat = PS_CheckReqopena( ReqOpenAcc ); // может вернуть CHANG_IMPORTANT=-11
      if( stat > 0 )
        return REQOPENA_JUSTERROR;
      end;

    end;

    if( ReqOpenAcc.ClientID > 0 )
      if( not GetTaxPayerPartyData(ReqOpenAcc.ClientID, @LegalForm, @IsIndividEmployer, @AttrPartyType, @NotResident) )
        RunError("Не найден субъект " + ReqOpenAcc.ClientID);
      end;
    end;

    if(((ReqOpenAcc.ClientType311 == 1) or (ReqOpenAcc.ClientType311 == 6)) or (LegalForm == PTLEGF_INST))
      
      if(ReqOpenAcc.ClientName == "")
        errList = errList + "Наименование";
      end;

      if(ReqOpenAcc.ClientINN == "")
        if(errList != "") errList = errList + ", "; end;
        errList = errList + "ИНН";
      end;

      if(ReqOpenAcc.ClientOGRN == "")
        if(errList != "") errList = errList + ", "; end;
        errList = errList + "ОГРН";
      end;
      
    end;
   
    if(((ReqOpenAcc.clientType311 == 2) or
        (ReqOpenAcc.clientType311 == 3) or
        (ReqOpenAcc.clientType311 == 4) or
        (ReqOpenAcc.clientType311 == 5)) or 
        ((LegalForm == PTLEGF_PERSN) and ((AttrPartyType == PARTY_ATTR_PTTYPE_NOTARIUS) or
                                       (AttrPartyType == PARTY_ATTR_PTTYPE_ADVOCAT) or
                                       (AttrPartyType == PARTY_ATTR_PTTYPE_MANAGPARTNER )) or
        (IsIndividEmployer == true))) 
      if(ReqOpenAcc.ClientName == "")
        errList = errList + "Наименование";
      end;

      if(ReqOpenAcc.ClientINN == "")
        if(errList != "") errList = errList + ", "; end;
        errList = errList + "ИНН";
      end;

      if(ReqOpenAcc.ClientOGRN == "")
        if(errList != "") errList = errList + ", "; end;
        errList = errList + "ОГРН";
      end;
      
    end;

    if((ReqOpenAcc.clientType311 == 7) or
       ((LegalForm == PTLEGF_PERSN) and ((AttrPartyType != PARTY_ATTR_PTTYPE_NOTARIUS) AND
                                      (AttrPartyType != PARTY_ATTR_PTTYPE_ADVOCAT) AND
                                      (AttrPartyType != PARTY_ATTR_PTTYPE_MANAGPARTNER )) AND
       (IsIndividEmployer == false)) or
       ((CompareStrWithMasks( RegValDeposit, ReqOpenAcc.Account ) == 0) or
        (CompareStrWithMasks( RegValAccount, ReqOpenAcc.Account ) == 0))) 
      
      if(ReqOpenAcc.ClientName == "")
        errList = errList + "Имя или Фамилия";
      end;

      if(ReqOpenAcc.ClientCardType == "")
        if(errList != "") errList = errList + ", "; end;
        errList = errList + "Код документа";
      end;

      if(ReqOpenAcc.ClientCardNum == "")
        if(errList != "") errList = errList + ", "; end;
        errList = errList + "Серия и номер";
      end;

      if( (ReqOpenAcc.ClientCardType != "14") and (ReqOpenAcc.ClientCardDate == zerodate) )
        if(errList != "") errList = errList + ", "; end;
        errList = errList + "Дата выдачи";
      end;

      if( (ReqOpenAcc.ClientBirthDate == zerodate) or (ReqOpenAcc.ClientBirthDate == " 1.01.0001"))
        if(errList != "") errList = errList + ", "; end;
        errList = errList + "Дата рождения";
      end;

      if(ReqOpenAcc.ClientBirthPlace == "")
        if(errList != "") errList = errList + ", "; end;
        errList = errList + "Место рождения";
      end;
    end;

    if(errList != "")
      MsgBox("Не заполнены следующие реквизиты клиента: " + errList + ". Укажите необходимые реквизиты в панели, вызываемой по клавише Alt-R");  
      return ReqOpenAcc_Fld_ClientButton;
    end;   
  end;
  
  if( Режим == 9 )
    if( ReqOpenAcc.Dummy != "X" )
      stat = PSCheckBankruptStatus( ReqOpenAcc.ClientID , 1);
    end;
  end;                                
  return stat;
END;

MACRO Функция_Пользователя( Режим:integer )
/*
 Возможные значения Режим:
  UFN_PANEL_INPUT(1) - функция вызвана из панели ввода объекта;
  UFN_PANEL_EDIT(2) - функция вызвана из панели корректировки объекта;
  UFN_SCROL(3) - функция вызвана из панели скролинга, единичный вызов, любая корректировка объекта запрещена;
  UFN_SCROL_FMASS(4) - функция вызвана из панели скролинга, работа по нескольким записям, вызов до начала обработки, любая корректировка объекта запрещена;
 
// Пример работы: 
 if( Режим == UFN_SCROL )
   return UPDTPAGE;    // Обновить страницу записей и область скролинга
   //return UPDTREC;     // Обновить текущую запись, загрузив из файла
 end;
 */  return 0;
END;

/* Макрос генерации кода клиента для заявление на открытие счета */
MACRO КодКлиента()

  FILE code(partcode) key 1;
  VAR ClientCode = "";
  VAR CodeLen;

  code.CodeKind = PTCK_CONTR;
  code.Code = SetCode(9,35);
  if( (GetLE(code)) and (code.CodeKind == PTCK_CONTR) )
    ClientCode = code.Code;
  end;
  if( (ClientCode != "") and (AllDigits(ClientCode)) )
    CodeLen = strlen(ClientCode);
    ClientCode = String(Int(ClientCode) + 1);
    ClientCode = LZ(ClientCode,CodeLen);
    PtCode.Code = ClientCode;
  end;
  return 0;

END;
