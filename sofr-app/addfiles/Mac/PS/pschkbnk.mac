/*
 $Name: pschkbnk.mac
 $Module: Ядро Banking
 $Description: Проверка клиента на банкротство
 */
import PaymInter, OprInter, CTInter, oralib, globals, "pm_common.mac", "pm_chkrst.mac";

private const BANKRUPT_STOP = 2,
              BANKRUPT_REJECT = 0,
              BANKRUPT_CONTINUE = 1;

private macro CheckSingleClient( Payment : RsbPayment, ClientID : Integer, Reject : bool )
  var RegVal = "";
  var Mes = TArray();
  var Buttons = TArray();
  var BankruptStatus = 0;
  var stat = 0;
  
  stat = GetPartyBankruptStatus( ClientID, date(), BankruptStatus );
  if( (stat == 19023) or (BankruptStatus == "0") )
    InitError();
    // Если не найден клиент или статус банкротства, то пропускаем проверку
    return 1;
  end;
  GetRegistryValue( "CB\\PAYMENTS\\SKIPBANKRUPTSTATUS", V_STRING, RegVal );
  var BStatus : String = BankruptStatus;
  
  if( CompareStrWithMasks( RegVal, BStatus, 4) and ( BankruptStatus != 10 ) )
    var select = " select t_Name "
                 " from   dllvalues_dbt "
                 " where  t_List = 3564 "
                 " and t_Element = :Element ";
    var rs = execSQLSelect( select, makeArray( SQLParam( "Element", BankruptStatus ) ));
    if( rs.MoveNext() )
      BStatus = rs.value(0);
    end;
    
    Mes(Mes.size) = "ВНИМАНИЕ! Физическое лицо " + ClientID + " " + Payment.PayerName 
    + " имеет по состоянию на " + date() + " статус банкротства \"" + BStatus + "\"";
    Mes(Mes.size) = "Убедитесь в правомерности списания средств";
    Buttons(Buttons.size) = "Отвергнуть";
    Buttons(Buttons.size) = "Продолжить";
    Buttons(Buttons.size) = "Прервать";
    var ret = 0;
    if( GetDialogFlag() and (Reject == false ))
      ret = ConfWin( Mes, Buttons, 0 );
    else
      var Action = "ОТ";
      GetRegistryValue( "CB\\PAYMENTS\\ОБРАБОТКА ПО УМОЛЧАНИЮ\\PAYMENTEXE\\BankruptDebetAcc", V_STRING, Action );
      if( Action == "ОТ")
        ret = 0;
      elif( Action == "ОП" )
        ret = 1;
      elif( Action == "НП" )
        ret = 2;
      end;
    end;
    if( Reject )
      ret = 0;
    end;
    if( ret == 0 )
      // Заполнить примечание и отвергнуть
      if( Payment.PaymStatus == PM_REJECTED )
        var Note = Payment.Notes.ReadNote( PM_NOTEKIND_DENIALGROUND, {curdate} );
        if( strlen(Note) > 0 )
          Note = Note + ". " + Mes(0);
        end;
        Payment.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, Note );
      else
        RejectPayment( Payment, Mes(0) );
      end;
      return 0;
    elif( ret == 1)
      // Продолжить проверку
      return 1;
    elif( ret == 2 )
      // Прервать
      return 2;
    end;
  end;
  return -1;
end;

macro ПроверитьСтатусБанкротства( Payment : RsbPayment )
  var ClientID = 0;
  var addpi = false; // Флаг наличия разноски по дебету
  var ret = 0;
    
  if( Payment.Payer > 0 )
    ClientID = Payment.Payer;
  end;
  
  var rs = execSQLSelect( " select t_AccountClient from dpmaddpi_dbt "
                          " where t_PaymentID = :PaymentID "
                          " and t_DebetCredit = 0 "
                          " order by t_AccountClient ", 
                          makeArray( SQLParam( "PaymentID", Payment.PaymentID)) );
  if( (rs and rs.MoveNext()) and (not ClientID) )
    addpi = true;
    ClientID = rs.value(0);
  end;
  
  // Если не задан плательщик и нет разноски по дебету, то выходим из проверки
  if( (ClientID <= 0) and ( addpi == false ) )
    return 0;
  end;
  
  ret = CheckSingleClient( Payment, ClientID, false );
  
  // Если есть разноска, то проверяем каждого клиента-плательщика
  if( addpi )
    while( rs.MoveNext() )
      if( ret == BANKRUPT_STOP )
        break;
      end;
      if( ClientID == rs.value(0) )
        continue;
      end;
      ClientID = rs.value(0);
      var Reject = false;
      if( ret == 0 )
        Reject = true;
      end;
      ret = CheckSingleClient( Payment, ClientID, Reject );
    end;
  end;
  if( ret == BANKRUPT_REJECT )
    // Отвергнуть
    if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_REJECT ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return -1;
    end;
    return 1;
  elif( (ret == BANKRUPT_CONTINUE) )
    // Продолжить
    return 0;
  elif( ret == BANKRUPT_STOP )
    // Прервать
    return -1;
  end;
  return 0;
end;