/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2001              */
/*                                                                      */
/*  Имя файла        : pr_bcrst.mac                                     */
/*                                                                      */
/*  Описание         : Реестр поручений резидентов на покупку           */
/*  Программист      : Бурак В.Ю.                                       */
/*                                                                      */
/*  Создан           : 13.04.2001                                       */
/*                                                                      */
/************************************************************************/

import bcprncom, PSInter, oralib;

const КомуПредставляется = "";

macro PrintHeader( )

 var CodeOKATO = "Код", Code13, err;
 ARRAY SNB, SP;

  Code13 = ПолучитьКодСубъекта( {OurBank}, PTCK_BANKREGNUM, err );

  strsplit( {Name_Bank},        SNB, 65,27,2 );
  strsplit( КомуПредставляется, SP,  65,42,2 );

[
+-------------------------+----------+-----------------------+
| Код территории по ОКАТО | Код ОКПО | Регистрационный номер |
+-------------------------+----------+-----------------------+
|           ###           | ######## |     ############      |
+-------------------------+----------+-----------------------+

                            Наименование уполномоченного банка    ###########################
                            #################################################################
                            Кому представляется    ########################################## 
                            #################################################################

             РЕЕСТР ПОРУЧЕНИЙ РЕЗИДЕНТОВ НА ПОКУПКУ 
]
( CodeOKATO:c, GetOKPO({OurBank}):c, Code13:c, SNB(0), SNB(1), SP(0), SP(1) );

end;

macro PrintNewCurrency( ISO, Name_FIID, RepDate )
 
[
                    ###  #########################
                    ------------------------------
                      (наименование и код валюты)


                          НА ВАЛЮТНОМ РЫНКЕ
                        На #

+------+----------+------------------------------------------+----------------------------------------------------------+--------------+
|  №   | Код  ОКПО|         Наименование резидента           |           Поручение резидента на покупку                 |Регистрацион- |
| п/п  | резидента|                                          |                иностранной валюты                        |ный номер     |
|      |          |                                          +------------------+------------+--------------------+-----+Исполняющего  |
|      |          |                                          |       Номер      |    Дата    |   Сумма покупки    |Код  |банка         |
|      |          |                                          |                  |            |                    |осно-|              |
|      |          |                                          |                  |            |                    |вания|              |
+------+----------+------------------------------------------+------------------+------------+--------------------+-----+--------------+
|  1   |    2     |                     3                    |        4         |     5      |         6          |  7  |      8       |
+------+----------+------------------------------------------+------------------+------------+--------------------+-----+--------------+]
( ISO:c, Name_FIID:l, RepDate:m:l );

end;
macro PrintDocument( DocCurrency, CodeOKPO, Name_Client, Number, OrderDate, Amount, Cause, Code13 )

[| #### | ######## | ######################################## | ################ | ########## | ################## | ##  | ############ |
+------+----------+------------------------------------------+------------------+------------+--------------------+-----+--------------+ 
]
( DocCurrency:c, CodeOKPO:c, Name_Client:l, Number:c, OrderDate:f:c, Amount:f:c, Cause:c, Code13:c );
end;

macro PrintItogCurrency( Sum )
[|Итого                                                                                       | ################## |                    |
+--------------------------------------------------------------------------------------------+--------------------+--------------------+ 
]( Sum:f:c );
end;

macro PrintItog()
[
Ответственное лицо
Уполномоченного банка__________________________________________________ ( Ф.И.О., должность )


М.П


Исполнитель____________________________________________________________ ( Ф.И.О., должность )

]
end;

MACRO PrintBcrst ( MapVal:PSRepMap )
  PrintHeader();

  var RepDate:date     = MapVal.Value( "RepDate"          );
  var FIID             = MapVal.Value( "FIID"             );
  var MarketPlace_Cond = MapVal.Value( "MarketPlace_Cond" );
  var MarketPlace      = MapVal.Value( "MarketPlace"      );
  var DepNum           = MapVal.Value( "DepNum"           );
  var BankFunds_Cond   = MapVal.Value( "BankFunds_Cond"   );
  var rs;
  var params:TArray = TArray();

  var selectc = "SELECT COUNT(1)";
  var selectq = "SELECT party.T_OKPO, party.T_NAME, pmrmprop.T_NUMBER, pmrmprop.T_DATE,"    +
                " pmpaym.T_PAYAMOUNT, objcode.T_CODE, fininstr.T_NAME, fininstr.T_FI_CODE," +
                " ps_bcord.T_CAUSE";

  var query  = " FROM dps_bcord_dbt ps_bcord, dpmpaym_dbt pmpaym, dfininstr_dbt fininstr," +
                    " dpmrmprop_dbt pmrmprop, dparty_dbt party, dobjcode_dbt objcode"      +
               " WHERE     ps_bcord.T_BCORDKIND = 1"                                       +
                     " AND ps_bcord.T_STATE = 10"                                          +
                     " AND ps_bcord.T_PAYMENTID = pmpaym.T_PAYMENTID"                      +
                     " AND pmrmprop.T_PAYMENTID = pmpaym.T_PAYMENTID"                      +
                     " AND party.T_PARTYID = pmpaym.T_PAYER"                               +
                     " AND party.T_NOTRESIDENT <> 'X'"                                     +
                     " AND pmrmprop.T_DATE = :RepDate"                                     +
                     " AND objcode.T_OBJECTID(+) = pmpaym.T_PAYER"                         +
                     " AND objcode.T_CODEKIND(+) = 13"                                     +
                     " AND objcode.T_OBJECTTYPE(+) = 3"                                    +
                     " AND pmpaym.t_Department = :DepNum"                                  +
                     " AND fininstr.T_FIID = pmpaym.T_PAYFIID";


  params[params.size] = ( SQLParam( "RepDate"   , repdate ));
  params[params.size] = ( SQLParam( "DepNum"    , DepNum  ));

  if( FIID != -1)
    query = query + " AND pmpaym.T_PAYFIID = :FIID";
    params[params.size] = SQLParam( "FIID", FIID );
  end;

  /* Если через биржу и за счет банка*/
  if( (BankFunds_Cond!=0) AND (MarketPlace_Cond!=0) )
    if( MarketPlace != -1 )
      query = query + " AND (ps_bcord.T_BANKFUNDS = 'X' OR (ps_bcord.T_BANKFUNDS = CHR(0) AND ps_bcord.T_EXCHANGEID = :MarketPlace))";
      params[params.size] = SQLParam( "MarketPlace", MarketPlace );
    end;    
  /* Если только за счет средств банка */
  elif( BankFunds_Cond!=0 )
    query = query +  " AND ps_bcord.T_BANKFUNDS = 'X'";
  /* Если только через биржу */
  else
    query = query +  " AND ps_bcord.T_BANKFUNDS = CHR(0)";
    if(  (MarketPlace_Cond != 0) AND (MarketPlace != -1 ) )
      query = query + " AND ps_bcord.T_EXCHANGEID = :MarketPlace";
      params[params.size] = SQLParam( "MarketPlace", MarketPlace );
    end;    
  end;
  
  var ISO, Name_FIID, CodeOKPO, 
      Name_Client, Number, Amount,
      Cause, Code13, OrderDate:date;

  var ch = 1, SumAmount = 0;
  
  selectc  = selectc + query;
  var rsc = execSQLselect( selectc, params, FALSE );
  rsc.moveNext();
  var count:integer = rsc.value(0); 
  InitProgress( count, null, "Поручения реестра" );

  selectq = selectq + query;
  rs = execSQLselect( selectq, params, FALSE );

  if( rs )     
    while( rs.MoveNext() )
      CodeOKPO    = rs.value(0);
      Name_Client = rs.value(1);
      Number      = rs.value(2);
      OrderDate   = rs.value(3);
      Amount      = rs.value(4);
      Code13      = rs.value(5);      
      
      if( ch == 1 )
        if( FIID != -1 )
          Name_FIID   = rs.value(6);
          ISO         = rs.value(7);        
          PrintNewCurrency( ISO, Name_FIID, RepDate );
        else
          PrintNewCurrency( "", "по всем валютам", RepDate );
        end;
      end;

      Cause       = rs.value(8);
      
      PrintDocument( ch, CodeOKPO, Name_Client, Number, OrderDate, Amount, Cause, Code13 );
      SumAmount = SumAmount + Amount;
      UseProgress( ch );
      ch = ch + 1;
    end;

    PrintItogCurrency( SumAmount );
    RemProgress();
  else
  msgbox( "Нет записей" );
  end;
END;

