import FIInter, PTInter, pralcash, pr_oo, prcsord, "adress.mac";


PRIVATE file a("account.dbt");
PRIVATE file pt(party);

macro Print_PKO_CB( order, payment )

 ncopy    = GetNumCopy();
 var rate = ПолучитьКурсОрдера(payment);
 var НомерЭкземпляра = 1;
 var ВсегоЭкземпляров = ncopy;
 var НаименованиеКлиента = "";
 var sumNatCur = ПолучитьСуммуВНациональнойВалюте(payment);

 a.Chapter       = 1;
 a.Account       = payment.ReceiverAccount;
 a.Code_Currency = payment.FIID;

 if(getEQ(a))
   pt.PartyID = a.Client;
   if(getEQ(pt))
     НаименованиеКлиента = pt.Name;
   end;
 end;

 while(ncopy != 0)
   PrintCBCashOrderHeader("ПРИХОДНЫЙ КАССОВЫЙ ОРДЕР № " + order.Numb_Document,
                          payment.ValueDate,
                          rate,
                          230,
                          НомерЭкземпляра,
                          ВсегоЭкземпляров);

[        ######################################################################################
 От кого ______________________________________________________________________________________
               (наименование клиента)

](НаименованиеКлиента:l);

   PrintCBCashOrder(order.Ground,payment.PayerAccount,payment.ReceiverAccount,payment.Amount,sumNatCur,payment.FIID,ВсегоЭкземпляров);

[

 Указанную валюту внес:__________________________
                          (подпись вносителя)
                                                      Кассир __________________________________

                                                      Контролер _______________________________

                                                      Главный бухгалтер _______________________

];


   ncopy = ncopy - 1;
   НомерЭкземпляра = НомерЭкземпляра + 1;
 end;

end;


MACRO GetPartyProperty(PartyID,Place,Code)
  record rec(party);
  record RecordAdress ( adress );
  file Коды_Субъектов(partcode);
  file Параметры_Банков (bankdprt);

  if ( ПолучитьСубъекта (PartyID,rec) != 0 )
    msgbox("Не найден банк-получатель");
    return 1;
  else
    Коды_Субъектов.PartyID  = rec.PartyID;
    Коды_Субъектов.CodeKind = 3;
    If (GetEQ(Коды_Субъектов)) /* Если есть БИК у самого банка, то возвращаем его */
      SetParm(2, Коды_Субъектов.Code);
    end;
    Параметры_Банков.PartyID = rec.PartyID;
    if (GetEQ(Параметры_Банков))
      ClearRecord(RecordAdress);
      НайтиЮридическийАдресСубъекта(rec.PartyID,RecordAdress);
      SetParm(1, Параметры_Банков.place + " " + RecordAdress.Place );
    else
      SetParm(1, "");
    end;
  end;
END;

/*--------------------------------------*/
/* Печать объявления на взнос наличными */
/*--------------------------------------*/
MACRO Print_OVN( order, payment )

  FILE account  ("account.dbt" ) KEY 0;
  FILE party    ("party.dbt"   ) KEY 0;

  array StringSumArray,    /*сумма прописью по строкам    */
        ReceiverNameArray, /*получатель по строкам        */
        GroundArray;       /*назначение платежа по строкам*/

  var ReceiverBankBIK    :string, /*БИК банка получателя     */
      ReceiverBankName   :string, /*Название банка получателя*/
      ReceiverBankAddress:string, /*Адрес банка получателя   */
      ReceiverName       :string, /*Название получателя      */
      StringSum          :string, /*Сумма прописью           */
      StringKop          :string; /*Кол-во копеек в сумме    */

  /* число копий */
  if(MassCopy == 0) ncopy = GetNumCopy();
  else              ncopy = MassCopy;
  end;
  
  /* свойства банка получателя */
  GetPartyProperty( {OurBank}, ReceiverBankAddress, ReceiverBankBIK );
  ReceiverBankName = {Name_Bank} + " " + ReceiverBankAddress;
  
  /* счет получателя */
  party.PartyID = 0;
  account.Chapter = 1;
  account.Code_Currency = payment.FIID;
  account.Account = payment.ReceiverAccount;
  if(getEQ(account))
    party.PartyID = account.Client;
  end;

  /* получатель (клиент счета) */
  
  if((party.PartyID != 0) and getEQ(party))
    ReceiverName = party.Name;
  else
    ReceiverName = "";
  end;
  StrSplit(ReceiverName, ReceiverNameArray, 31, 31, 2);
  
  /* сумма пpописью  */
  if(order.IsCurrency == "X") 
    StringSum = CurToStrAlt(payment.Amount, NULL, NULL, GetISOCode(payment.FIID));
    StrSplit( StringSum, StringSumArray, 72, 52, 3 );
  else
    StringSum = RubToStrAlt(payment.Amount, NULL, StringKop);
    if( Int(StringKop) == 0 )
      if(Index(StringSum, "коп") != 0)
        StringSum = SubStr(StringSum, 1, Index(StringSum, "коп")-4);
      end;
    end;
  end;
  StrSplit(StringSum, StringSumArray, 72, 52, 3);
  StrSplit(order.Ground, GroundArray, 72, 52, 2);
  
  while( ncopy > 0 )
    [                                 +----------+                   +-------+
                   Объявление №       |##########|                   |0402001|
                   на взнос наличными +----------+                   +-------+

                   ##########
     От кого      ------------        Для зачисл. +--------------------------+
     ################################ На счет №   |##########################|
     ---------------------------------------------+--------------------------+
     Банк получателя ############################ |##########################|
     ---------------------------------------------+                          |
     Получатель ################################# |      Сумма цифрами       |
                                                  +--------------------------+

     Сумма прописью      ####################################################
     ########################################################################
     ########################################################################
     -------------------------------------------------------------------------
     Источник взноса     ####################################################
     ########################################################################
     -------------------------------------------------------------------------
     Подпись вносителя                          Бухгалтер
                                                Деньги принял кассир

     -------------------------------------------------------------------------
                                      +----------+                   +-------+
                   Квитанция №        |##########|                   |0402001|
                                      +----------+                   +-------+

                   ##########
     От кого      ------------        Для зачисл. +--------------------------+
     ################################ На счет №   |##########################|
     ---------------------------------------------+--------------------------+
     Банк получателя ############################ |##########################|
     ---------------------------------------------+                          |
     Получатель ################################# |      Сумма цифрами       |
                                                  +--------------------------+

     Сумма прописью      ####################################################
     ########################################################################
     ########################################################################
     -------------------------------------------------------------------------
     Источник взноса     ####################################################
     ########################################################################
     -------------------------------------------------------------------------
      М.П.              Бухгалтер               Деньги принял кассир
     -------------------------------------------------------------------------
                                       +----------+                  +-------+
                   Ордер №             |##########|                  |0402001|
                                       +----------+                  +-------+
                                         ДЕБЕТ
                   ##########          +----------+--------------------------+
     От кого      ------------         |   Счет № |##########################|
                                       +----------+                          |
     ###############################     КРЕДИТ   |                          |
     ----------------------------------+----------+                          |
     Банк получателя          |  Код   |   Счет № |##########################|
     ######################## |########|          |           Сумма          |
     ----------------------------------+----------+           общая          |
     Получатель   ############################### |##########################|
                  ############################### +-------------+------------+
                                                  |   частные   |  символы   |
                                                  |--------------------------|
                                                  |--------------------------|
                                                  |--------------------------|
                                                  |--------------------------|
     ---------------------------------------------+--------------------------+
     Источник взноса     ####################################################
     ########################################################################
     
     Бухгалтер                        Кассир
    
    ]
    ( order.Numb_Document:l, order.DocumentDate:f:c, order.FIOClient:l, 
      payment.ReceiverAccount:l, ReceiverBankName:l, payment.Amount:l:f, ReceiverName:l,
      StringSumArray(0):l, StringSumArray(1):l, StringSumArray(2),
      GroundArray(0):l, GroundArray(1):l,
      
      order.Numb_Document:l, order.DocumentDate:f:c, order.FIOClient:l, 
      payment.ReceiverAccount:l, ReceiverBankName:l, payment.Amount:l:f, ReceiverName:l,
      StringSumArray(0):l, StringSumArray(1):l, StringSumArray(2),
      GroundArray(0):l, GroundArray(1):l,
      
      order.Numb_Document:l, order.DocumentDate:f:c, payment.PayerAccount:l,
      order.FIOClient:l, payment.ReceiverAccount:l,
      ReceiverBankName:l, ReceiverBankBIK:l,
      ReceiverNameArray(0):l, payment.Amount:l:f, ReceiverNameArray(1):l,
      GroundArray(0):l, GroundArray(1):l
    );

    ncopy = ncopy - 1;
  end;

  return TRUE;

end;


macro PrintPSIncCashDoc(ФормаПечати, ord, pm, sb )

 setbuff( payment , pm  );
 setbuff( order   , ord );
 setbuff( symb_rec, sb  );
 var symbol = "", count = 0, stat = 0;

 if( (order.IsCurrency != "X") and (ФормаПечати == ФормаПечати_ПриходныйКассовыйОрдерСБ) )
   MsgBox("Форма №230 не предназначена для печати рублевых ордеров");
   exit(1);
 end;

 if(symb_rec.Symbol == "" )
   symbol = "";
   rewind(symb_file);
   count = 0;
   symb_file.DocKind        = symb_rec.DocKind;
   symb_file.ApplicationKey = symb_rec.ApplicationKey;
   symb_file.Kind           = symb_rec.Kind;

   stat = getGE(symb_file);
   while(stat)
      if((symb_file.DocKind == symb_rec.DocKind) and
         (symb_file.Kind    == symb_rec.Kind) and
         (symb_file.ApplicationKey == symb_rec.ApplicationKey))
          count = count + 1;
          if( count == 1) symbol = symb_file.Symbol; end;
          stat = next(symb_file);
      else stat = 0;
      end;
   end;
   if(count != 1) symbol = ""; end;
 else
   symbol = symb_rec.Symbol;
 end;

 if(ФормаПечати == ФормаПечати_ОбъявлениеНаВзносНаличными)
   stat = Print_OVN( order, payment );
 elif(ФормаПечати == ФормаПечати_ПриходныйКассовыйОрдер)
   stat = PrintIncomeCashOrder(payment, order, MassCopy);
 elif(ФормаПечати == ФормаПечати_ПриходныйКассовыйОрдерСБ)
   stat = Print_PKO_CB( order, payment );
 end;

 return stat;
end;


macro Cash_MassPrint( Num, /* число печатаемых копий, если 0, считается первым вызовом */
                      dd, aa, bb, cc )
  var err, Форма;
  setbuff( order, aa );

  if(Num) MassCopy = Num;
  else    MassCopy = КоличествоКопий;
  end;

  if( dd ) Форма = dd;
  else     Форма = ФормаПечати_ПриходныйКассовыйОрдер;
  end;

  if(MassCopy == 0)
    MassCopy = 1;
    if ( not GetInt(MassCopy,"Введите количество копий.",3))
      MassCopy = 0;
    end;
  end;

  if(MassCopy == 0) return false;end;

  if( order.DocKind == 420 )   err = PrintOutCashDoc  (ФормаПечати_РасходныйКассовыйОрдер, aa, bb, cc);
  elif( order.DocKind == 410 ) err = PrintPsIncCashDoc(Форма, aa, bb, cc);
  end;

  if(err) return true; end;

  return MassCopy;

end;

/* Вызов происходит перед печатью 1-го документа из списка */
MACRO Cash_HeaderMassPrint
  MassCopy = 1;
  if ( not GetInt(MassCopy,"Введите количество копий.",3))
    MassCopy = 0;
  end;
  return MassCopy;
END;

/* Вызов происходит после печати последнего документа */
MACRO Cash_ItogMassPrint
END;
