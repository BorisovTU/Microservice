/*
$Name:             psrqdoc.mac
$Module:           РКО юридических лиц
$Description:      Макродействия ИПВС
*/


import BankInter, PaymInter, globals, pm_opr, pmchoper, pm_tools, pm_common, pmbuff, pm_chksave, "pm_syscont", "pmedit.mac";

/* номера полей в панели */
const fld_Number = 1,  /* номер */
      fld_PaymKind = 3,  /* вид платежа*/
      fld_NumberPay = 7, /* номер поля поручения на продажу*/
      fld_Ground = 24; /* основание */

macro Проверить_Номер(number:string, str_docname:string):integer

  if( StrLen( Number ) == 0 )
    MsgBox("Не задан номер "+str_docname);
    return 1;
  end;

  if( not StrIsNumber( Number ) )
    MsgBox("Номер "+str_docname+" нечисловой");
    return 1;
  else
     if( int( Number ) == 0 )
        MsgBox("Номер "+str_docname+" не может быть нулевым");
        return 1;
     end;
  end;

  return 0;

end;

macro PS_ReqOrderCheckDoc( Режим )
  var stat:integer = 0,
      OldDoc:integer = 0;

  if ( (Режим == 2 ) or (Режим == 3) or (Режим == 8) )

    if (Проверить_Номер(r_pmrmprop.Number, "документа"))
          return fld_Number;
       end;

    if( StrLen( r_pmrmprop.Ground ) == 0 )
      MsgBox("Введите назначение платежа");
      return fld_Ground;
    end;

    if( PM_CheckPayments( r_pmpaym, NULL, r_credit, r_pmrmprop, 1 ) )
      return 1;
    end;

    /* Общие проверки по списку */
    stat = INRQ_ScrolMacroCommonChecks( TPanelFields(), r_pmpaym, r_debet, r_credit, r_pmrmprop, r_pmserv );
    if( stat != NOTERROR )
      return stat;
    end;

    /*проверка реквизитов валютной операции*/
    if( PM_CheckCO(r_pmpaym,r_pmrmprop,0,0))
       return 1;
    end;

  end;

  /* Вид платежа "Срочно" запрещено устанавливать для ИПВС */
  if( r_pmrmprop.PaymentKind == "С" )
     MsgBox("Недопустимый вид платежа");
     return fld_PaymKind;
  end;
  
  if( (Режим == 2 ) or (Режим == 3) or (Режим == 8) or (Режим == 9) )
    if( ClientHasServKind( r_pmpaym.Payer, PTSK_PAY ))
      return 1;
    end;
  end;

  if(Режим == 3)  /* РЕДАКТИРОВАНИЕ ДОКУМЕНТА */
    if ( not EditFromHistScrol )
       /*При редактировании производим проверку важности внесенных изменений */
       /* Константы важности внесенных изменений:           */
       /* CHANG_NOTIMPORTANT   - изменения неважные (сохранение без отката операции)*/
       /* CHANG_IMPORTANT      - изменения важны (сохранение изменений возможно только при откате) */
       /* CHANG_NOTKEEP        - не сохранять изменения */
       /* Если возвращаемое значение  > 0, то это оно интерпритируется как номер поля с ошибочным параметром*/
       /* Если возвращаемое значение  = 0, то cчитается, что проверка прошла успешно
          и сохранение изменений можно производить без отката операции      */
      if (StandartCheckUpdate(TCheckUpdateParam(r_pmpaym, r_debet, r_credit, r_pmrmprop), 
          TCheckUpdateParam(r_pmpaym_old, r_debet_old, r_credit_old, r_pmrmprop_old)) == CHANG_NOTKEEP)
        return CHANG_NOTKEEP;
      end;
    end;
  elif(Режим == 2)  /* ВВОД ДОКУМЕНТА */
  elif(Режим == 1)  /* УДАЛЕНИЕ ДОКУМЕНТА */
    if(CheckDeletePayment(r_pmpaym.PaymentID))
      return 1;
    end;
  elif(Режим == 7) /*ПОМЕЩЕНИЕ ДОКУМЕНТА В ОТЛОЖЕННЫЕ, ОТКАТ ОПЕРАЦИИ*/
  elif(Режим == 8)  /*ВВОД В ОТЛОЖЕННЫЕ*/
  elif(Режим == OBJ_AFTEREDIT) /* Проверка важности изменений влияющей на необходимость смены опера документа
                                  (изменения в буферах не сохраняются)*/
    return IsImportantChangeForOperRequestOrder(r_pmpaym, r_pmpaym_old, r_pmrmprop, r_pmrmprop_old, r_credit, r_credit_old);
  end;

  return stat;
end;

macro PS_ReqOrderNewDoc()
  return 0;
end;

macro PS_ReqOrderUserFunc( Режим:integer )
  return 1;
end;
