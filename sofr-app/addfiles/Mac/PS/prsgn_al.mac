/*
  $Name:         prsgn_al.mac
  $Module:       РКО
  $Description:  Печать карточек с образцами подписей
*/
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.10                                         R-Style Software Lab

  File Name   : prsgn_al.mac
  Created     : 01.11.03
  Description : Печать карточек с образцами подписей

└───────────────────────────────────────────────────────────────────────────*/
IMPORT globals, PaymInter, PTInter, prreqbuf, "adress.mac", "pt_lib.mac", CtInter;
import oralib, likepy;
import "bnk_dttmfrmt.mac";

VAR         Oper:Integer = {oper};  /* выбранный опер                       */ 

PRIVATE VAR CopyCount = 1,          /* Кол-во копий                         */
            bTemp:bool,             /* "Временная"  карточка                */
            strOwner = "",          /* Владелец счета                       */
            strBithDate = "",       /* Дата рождения владельца              */
            strType = "",           /* Предприниматель/адвокат/нотариус     */
            strShortName = "",      /* Краткое наименование владельца счета */
            strAccount   = "",      /* Номер счета                          */
            strOperPost,            /* Должность опера                      */
            strOperName,            /* Имя опера                            */
            strAddress   = "",      /* Адрес клиента                        */
            strTelNum    = "",      /* Телефон клиента                      */
            aNAME  = TArray,        /* Имена доверенных лиц                 */
            aDate  = TArray,        /* Сроки полномочий доверенных лиц      */
            ;

private macro FillDateString(DateParam):string

  return  string( "\"", 
                  SubStr( string(DateParam:m:f), 1, 2 ), 
                  "\"", 
                  SubStr( string(DateParam:m:f), 3, strlen(string(DateParam:m:f) ) - 5 )
                 ) ;
end;
// Получить должность операциониста
private macro GetOperPost( OperID : integer ) 
  var SelectStr:string;
  var params:TArray;
  var rs:object;
  var tmpOperPost:string = "";

  SelectStr = "select officer.t_Post " +
              "from   dofficer_dbt officer " +
              "where  officer.t_PartyID   = :PartyID " + 
              "  and  officer.t_PersonID  = :OperID  ";

  params = makeArray( SQLParam( "PartyID" , {OurBank} ),
                      SQLParam( "PersonID", OperID   ));

  rs = execSQLselect( SelectStr, params, FALSE );

  if( rs and rs.moveNext() )
    tmpOperPost = rs.value(0);
  end;

  if( strlen(tmpOperPost) <= 0 )
    tmpOperPost = "Ответственный сотрудник";
  end;

  return tmpOperPost;
end;

// Получить ID договора обслуживания
// Если договор не найден - то возвращать 0
private macro GetSfContrID( FIID:integer, Account:string )

  var SelectStr:string;
  var params:TArray;
  var rs:object;
  var tmpID:integer = 0;

  SelectStr = "select sfcontr.t_ID " +
              "from   dsfcontr_dbt sfcontr " +
              "where  sfcontr.t_ServKind   = 3        " + // РКО
              "  and  sfcontr.t_ObjectType = 1        " + // SF_ACCOUNT
              "  and  sfcontr.t_FIID       = :FIID    " +
              "  and  sfcontr.t_Object     = :Account " ;

  params = makeArray( SQLParam( "FIID"   , FIID    ),
                      SQLParam( "Account", Account ));

  rs = execSQLselect( SelectStr, params, FALSE );

  if( rs and rs.moveNext() )
    tmpID = rs.value(0);
  end;

  return tmpID;

end;

// Преобразовать нулевую sql-дату в нулевую rsl-дату
private macro ConvertDate( dt ):date
  var d, m, y;

  if( dt == NULL )
    return Date( 0, 0, 0 );
  end;

  if( ValType( dt ) == V_DATE )
    DateSplit( dt, d, m, y );
    if( ( d == 1 ) and ( m == 1 ) and ( y == 1 ) )
      return Date( 0, 0, 0 );
    end;
  end;

  return dt;
end;

MACRO FillData ()
    var fPerson     = Tbfile ("person.dbt");
    var fPaprKind   = Tbfile ("paprkind.dbt");
    var bContinue   :bool;
    var bIsAny      :bool = false;
    var bIsAny2     :bool = false;
    var i           :Integer;
    var operID      :Integer;
    var aMenu = TArray (2);

    var SelectStr:string;
    var params1:TArray;
    var params2:TArray;
    var rs1:object;
    var rs2:object;
    var Agent          :integer;
    var AgentName      :string  = "";
    var ProxyStartDate :date    = date(0,0,0);
    var ProxyTermDate  :date    = date(0,0,0);
    var IsTempProxy    :string  = "";
    var SfContrID      :integer = 0;
    
    record buf_oper (person); 
    record persnidc ("persnidc.dbt");   
    record RecordAdress ( adress );
             
    i = 0;

    // Выбор лица, удостоверяющего подлинность подписей
    aMenu (0) = "Текущий         ";
    aMenu (1) = "Другой          ";
    bContinue = true;
    while( bContinue )
        if( GetDialogFlag() )
          i = Menu (aMenu, "Лицо, удостоверяющее подлинность подписей", "Операционист");
        else //при массовой печати всегда текущий опер
          i = 0;
        end;
        if( i == 0 )
          // поиск PartyID текущего пользователя
          ClearRecord( fPerson.rec );
          fPerson.rec.Oper = Oper;
          if( not fPerson.getEQ () )
            MsgBox ("Не найден текущий пользователь");
            return false;
            end;
          operID      = fPerson.rec.PartyID;
          strOperName = fPerson.rec.Name;
          bContinue = false;
        elif( i == 1 ) 
          if( ListOper( buf_oper , true ) )
            Oper        = buf_oper.Oper;
            operID      = buf_oper.PartyID;
            strOperName = buf_oper.Name;
            bContinue = false;
          end;
        else
          return false;            
        end;
    end;
    
    // Номер счета
    strAccount = pr_reqopena.rec.Account;

    // Получить должность операциониста
    strOperPost = GetOperPost( operID );

    // Владелец счета
    strOwner = pr_party.rec.Name;

    // Для физического лица не печатаем временную карточку
    if( bTemp and (pr_party.rec.LegalForm == 2) )
      MsgBox ("Отчет для физического лица не формируется");
      return false;            
    end;

    // Договор обслуживания ищем для всех
    SfContrID = GetSfContrID( pr_reqopena.rec.Code_Currency, pr_reqopena.rec.Account );

    // Если договор обслуживания не найден
    if( SfContrID == 0 )
      // Временную карточку при отсутствии договора не печатаем
      if( bTemp )
         MsgBox ("Не найден договор обслуживания");
         return false;
      end;

      // Печатаем пустую карточку
      strAccount   = "";
      strOwner     = "";
      strShortName = "";
      strAddress   = "";
      strTelNum    = "";

      return true;
    end;

    // Найти адрес и телефон клиента
    ClearRecord(RecordAdress);
    НайтиАдресСубъекта( pr_party.rec.PartyID, PTADDR_REAL, RecordAdress );

    var PhoneNumber : string = "";

    if( strlen( RecordAdress.Adress ) == 0 )
      ClearRecord( RecordAdress );
      НайтиЮридическийАдресСубъекта( pr_party.rec.PartyID, RecordAdress );
      FindAdressContactValue(pr_party.rec.PartyID, PTADDR_LEGAL, CNTADR_PHONENUMBER, PhoneNumber);
    else
      FindAdressContactValue(pr_party.rec.PartyID, PTADDR_REAL, CNTADR_PHONENUMBER, PhoneNumber);
    end;
    
    strAddress = RecordAdress.Adress;
    strTelNum  = PhoneNumber;

    if(pr_party.rec.LegalForm == 1) 
      // Юридическое лицо

      // Если краткое наименование не задано - то берем полное
      if( strlen(pr_party.rec.ShortName) > 0 )
        strShortName = pr_party.rec.ShortName;
      else
        strShortName = strOwner;
      end;

    else 
      // Физическое лицо
      strOwner = pr_persn.rec.Name1;

      if( pr_persn.rec.Name2 )
        strOwner = strOwner + " " + pr_persn.rec.Name2;
      end;

      if( pr_persn.rec.Name3 )
        strOwner = strOwner + " " + pr_persn.rec.Name3;
      end;

      strShortName = strOwner;

      // Если дата рождения задана - прицепить ее к наименованию
      if( pr_persn.rec.Born > date(0,0,0) )
        strBithDate = "Дата рождения " + pr_persn.rec.Born;
      end;

      ClearRecord (fPaprKind.rec);
      GetPersonIdc( pr_persn.rec.PersonID, persnidc );
      fPaprKind.rec.PaperKind = persnidc.PaperKind;

      var ObjCat = RsbObjCategories( 3/*OBJTYPE_PARTY*/, UniID( pr_persn, 3/*OBJTYPE_PARTY*/ ) );
      if( ObjCat.IsAttrPresense( OBJ_PAYMENT_GROUP_INITIATOR_PYMENT, 3, NULL, NULL, false, {curdate} ) )
        strType = "адвокат "; 
      elif ( ObjCat.IsAttrPresense( OBJ_PAYMENT_GROUP_INITIATOR_PYMENT, 2, NULL, NULL, false, {curdate} ) )
        strType = "нотариус "; 
      elif( pr_persn.rec.IsEmployer == "X" )
        strType = " Индивидуальный предприниматель";
      end;
    end;
    SelectStr = " select ptprxsauth.t_TermTo, persn.t_Name1, persn.t_Name2, persn.t_Name3 " +
                  " from   dptprxsauth_dbt ptprxsauth, dptproxy_dbt ptproxy, dpersn_dbt persn " +
                  " where ptproxy.t_PartyID = :clientID           " + 
                  "  and  ptprxsauth.t_PartyID = ptproxy.t_PartyID " + 
                  "  and  ptproxy.t_ProxyID = ptprxsauth.t_ProxyID " + 
                  "  and  ptprxsauth.t_Account = :account          " + 
                  "  and  ptprxsauth.t_TermTo >= :curdate          " + 
                  "  and  ptproxy.t_ProxyID = persn.t_personID     " ; 
                  

      params1 = makeArray( SQLParam( "clientID" , pr_reqopena.rec.ClientID ),
                           SQLParam( "account"  , pr_reqopena.rec.Account  ),
                           SQLParam( "curdate"  , {curdate}                ));      

      rs1 = execSQLselect( SelectStr, params1, FALSE );

      i = 0;
      if( rs1 and rs1.moveNext() )
        aDate (i) = rs1.value(0);
        aName (i) = rs1.value(1) + " " + rs1.value(2) + " " + rs1.value(3);
        while( rs1.moveNext() )
          i = i + 1;
          aDate (i) = rs1.value(0);
          aName (i) = rs1.value(1) + " " + rs1.value(2) + " " + rs1.value(3);
        end;
      else 
        MsgBox ("Не заданы сроки полномочий");
        return false;
      end;
    
    return true;
END;

MACRO Draw ()

    var aOwner     = strsplit2 (strOwner,     37, 13, 6),  // Владелец счета
        aPlace     = strsplit2 (strAddress,   37, 37, 7),  // Место нахождения
        aBank      = strsplit2 ({Name_Bank},  37, 32, 9),  // Банк
        aShortName = strsplit2 (strShortName, 35, 35, 2);  // Краткое наименование владельца

    var str = "";
    var i : Integer;
    var DateStr = DDpMMpYYYY({curdate});

    if (bTemp) str = "Временная"; end;

    while (CopyCount)

[                                                    #########         ] (str);
[                                                                      ];
[                                                    ┌────────────┐    ];
[                                                    │ Код формы  │    ];
[                                                    │документа по│    ];
[                                                    │    ОКУД    │    ];
[                                                    ├────────────┤    ];
[                                                    │  0401026   │    ];
[                                                    └────────────┘    ];
[                                                                      ];
[              Карточка                                                ];
[с образцами подписей и оттиска печати                                 ];
[                                                                      ];
[─────────────────────────────────────┬───────────────────────────┐    ];
[Клиент (владелец счета)############# │   Отметка банка           │    ] (aOwner (0));
[─────────────────────────────────────│                           │    ];    
[#####################################│                           │    ] (aOwner (1));
[─────────────────────────────────────│                           │    ];
[#####################################│   ______________________  │    ] (aOwner (2));
[─────────────────────────────────────│         (подпись)         │    ];
[#####################################│ ######################### │    ] (strBithDate, FillDateString({curdate}):c);
[─────────────────────────────────────│                           │    ];
[#####################################│                           │    ] (strType/*aOwner (4)*/);
[─────────────────────────────────────│                           │    ];

[Место нахождения (место жительства)  │                           │    ];
[─────────────────────────────────────│───────────────────────────│    ];
[#####################################│                           │    ] (aPlace (0));
[─────────────────────────────────────│───────────────────────────│    ];
[#####################################│                           │    ] (aPlace (1));
[─────────────────────────────────────│───────────────────────────│    ];
[#####################################│                           │    ] (aPlace (2));
[─────────────────────────────────────│───────────────────────────│    ];
[#####################################│                           │    ] (aPlace (3));
[─────────────────────────────────────│                           │    ];
[#####################################│                           │    ] (aPlace (4));
[─────────────────────────────────────│                           │    ];

  i = 5;
  while( (strlen(aPlace(i)) > 14) or (strlen(aPlace(i+1)) > 0) ) 
    if( strlen(aPlace(i+1)) <= 0 )
      aPlace(i+1) = "";
    end;
[#####################################│                           │    ] (aPlace (i));
    i = i+ 1;
  end;

[############## тел. N ###############│                           │    ] (aPlace (i), strTelNum);
[─────────────────────────────────────│───────────────────────────│    ];
[                                     │                           │    ];
[Банк ################################│                           │    ] (aBank (0));
[─────────────────────────────────────│───────────────────────────│    ];
[#####################################│      Прочие отметки       │    ] (aBank (1));
[─────────────────────────────────────│                           │    ];
[#####################################│                           │    ] (aBank (2));
[─────────────────────────────────────│───────────────────────────│    ];
[#####################################│                           │    ] (aBank (3));
[─────────────────────────────────────│───────────────────────────│    ];
[#####################################│                           │    ] (aBank (4));
[─────────────────────────────────────│───────────────────────────│    ];
[#####################################│                           │    ] (aBank (5));
[─────────────────────────────────────│───────────────────────────│    ];
[#####################################│                           │    ] (aBank (6));
[─────────────────────────────────────│───────────────────────────│    ];
[#####################################│                           │    ] (aBank (7));
[─────────────────────────────────────│───────────────────────────│    ];

  i = 8;
  while( strlen(aBank(i)) > 0 ) 
[#####################################│___________________________│    ] (aBank (i));
    i = i+ 1;
  end;

[                                     │                           │    ];
[─────────────────────────────────────┘───────────────────────────┘    ];
[];
[                                                                      ];
 i = 0;
 while( strlen(aShortName(i+1)) > 0 )
[###################################                                   ] (aShortName(i));
   i = i + 1;
 end;
[###################################     N счета ####################  ] (aShortName(i), strAccount);
[###################################](strType);
[ (сокращенное наименование клиента                                    ];
[        (владельца счета))                                            ];
[┌──────────────────────────────┬───────┬────────────────────────────  ];
[│    Фамилия, имя, отчество    │Образец│      Срок полномочий         ];
[│                              │подписи│                              ];
[├──────────────────────────────┼───────┼────────────────────────────  ];

    i = 0;
    while (i < aName.Size)
[│ #############################│       │###########################   ]( aName (i) :w, DDpMMpYYYY( aDate (i) ) :w:c);
        i = i + 1;

        if (i != aName.Size)
[├──────────────────────────────┼───────┤                              ];
        end;
    end;
[├──────────────────────────────┴───────┼────────────────────────────  ];
[│Дата заполнения  ###################  │  Образец оттиска печати      ](DateStr:c);
[├──────────────────────────────────────┤                              ];
[│Подпись клиента(владельца счета)      │                              ];
[├──────────────────────────────────────┼───────────────────────────┐  ];
[│ Место для удостоверительной надписи  │    Выданы денежные чеки   │  ];
[│   о свидетельствовании подлинности   ├────┬───┬────┬────┬───┬────┤  ];
[│              подписей                │Дата│с N│по N│Дата│с N│по N│  ];
[├──────────────────────────────────────┼────┼───┼────┼────┼───┼────┤  ];
[│                                      │    │   │    │    │   │    │  ];
[│                                      ├────┼───┼────┼────┼───┼────┤  ];
[│                                      │    │   │    │    │   │    │  ];
[│                                      ├────┼───┼────┼────┼───┼────┤  ];
[│                                      │    │   │    │    │   │    │  ];
[│                                      ├────┼───┼────┼────┼───┼────┤  ];
[│                                      │    │   │    │    │   │    │  ];
[│                                      ├────┼───┼────┼────┼───┼────┤  ];
[│                                      │    │   │    │    │   │    │  ];
[└──────────────────────────────────────┴────┴───┴────┴────┴───┴────┘  ];
[####################################################################  ] (strOperName);
[#######################        #############                          ] (strOperPost, DateStr:c);
[];                            
        CopyCount = CopyCount - 1;
    end;
    return;
END;

MACRO PrintSignatureCard (bTmp: bool, ncopy: integer) :bool;
    CopyCount = ncopy;
    if (CopyCount <= 0) return false; end;

    bTemp = bTmp;

    if (FillData ())
        Draw ();
    end;

    return true;
END;
