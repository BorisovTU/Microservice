/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : psii2sc.mac
  Created     : 15.10.2007
  Programmer  : Осмаковский Е.В.
  Description : Печать отчета о групповом помещении в к2

└───────────────────────────────────────────────────────────────────────────*/
IMPORT  PaymInter, OprInter;
IMPORT  "gstname.mac";

const Ошибка   = 0,
      Первый   = 1,
      Последний= 2;

PRIVATE MACRO GetDocument( PaymentID:integer, DocKind:integer, Number:string, Amount, PayAmount, OpDate:date ):bool
  var query:string = " SELECT pmpaym.t_paymentid, pmpaym.t_dockind, pmrmprop.t_number, pmpaym.t_amount, pmpaym.t_payamount, pmrmprop.t_date, pmpaym.t_valuedate " +
                       " FROM dpmpaym_dbt pmpaym, dpmrmprop_dbt pmrmprop  " +
                      " WHERE pmpaym.t_paymentid = :PaymentID" + 
                        " AND pmpaym.t_paymentid = pmrmprop.t_paymentid";                                

  var params:TArray = TArray();
  params[params.size] = SQLParam( "PaymentID", PaymentID );
  var rset:RsdRecordset = execSQLselect( query, params, true );

  if( rset and rset.moveNext() )
    SetParm(1, rset.value(1));
    SetParm(2, rset.value(2));
    SetParm(3, rset.value(3));
    SetParm(4, rset.value(4));
    if ( rset.value(1) != DLDOC_MEMORIALORDER )
      SetParm(5, rset.value(5));
    else
      SetParm(5, rset.value(6)); 
    end;
    return true;
  end;

  return false;
END; 


MACRO Печать_ОтчетСканирования( Вызов, firstdoc, Статус, Сообщение )

  var PaymentID:integer = GetLastDocumentID(), DocKind:integer = 0;
  var Number:string = "";
  var OpDate:date = date(0,0,0);
  var Amount = 0,
      PayAmount = 0;

  if( Вызов == Первый )
[              Отчет о групповом помещении документов в картотеку №2.              ];
[┌─────────┬─────────────┬──────────┬──────┬──────────────────────────────────────┐];
[│  Номер  │   Сумма     │   Дата   │Номер │                Сообщение             │];
[│документа│             │          │ошибки│                                      │];
[├─────────┼─────────────┼──────────┼──────┼──────────────────────────────────────┤];

  elif( Вызов == Ошибка )
    
    if( Статус == 0 )
      Сообщение = "Шаг успешно выполнен";
    end;

    if( NOT GetDocument( PaymentID, DocKind, Number, Amount, PayAmount, OpDate ) )
      Сообщение = "Не найдено платежное поручение";
    else 
      Сообщение = Сообщение + ". " + GetStepNameExecuteRead( PaymentID, DocKind );
    end;

[│#########│#############│##########│######│######################################│]
( Number, Amount, OpDate, Статус, StrSubst(Сообщение, "|", " " ):w );
  elif( Вызов == Последний )
[└─────────┴─────────────┴──────────┴──────┴──────────────────────────────────────┘];
  end;

END;
