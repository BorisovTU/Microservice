/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : pr_inosa.mac
  Created     : 13.02.2007
  Programmer  : Осмаковский Е.В.
  Description : Печать справки об открытии накопительного счета

└───────────────────────────────────────────────────────────────────────────*/

import reqinter;

macro DrawReport(AdressDep,     
                 Open_DateAcc,  
                 PartyName,
                 FI_Name,
                 ClientID,
                 DepName,
                 РАСПОРЯЖЕНИЕ_ДОЛЖ,
                 РАСПОРЯЖЕНИЕ_ФИО,
                 Account,
                 StrRest
                 )

  var text:string;


  text = {Name_Bank} + " ( БИК " + {MFO_Bank} + ", к\с " + {CORAC_Bank} + " ) подтверждает, что " + PartyName + " " +
         string(Open_DateAcc:m) +" открыт временный накопительный счет в " +
         FI_Name + " № " + Account + " в " + DepName + ". Уставный капитал внесен в размере " +
         StrRest;
[                                                                                         ];
[ ###############################################################                         ]({Name_Bank});
[                                                                                         ];
[                                                                                         ];
[                                                                                         ];
[                                                                                         ];
[                                                                                         ];
[                                                                                         ];
[ ##############################################################      ####################](AdressDep , {curdate}:m);
[                                                                                         ];
[                                                                                         ];
[                                                                                         ];
[                                                                     по месту требования ];
[                                                                                         ];
[                                                                                         ];
[                                       СПРАВКА                                           ]; 
[                                                                                         ];
[                                                                                         ];
[ ####################################################################################### ](text:w);  
[                                                                                         ];
[                                                                                         ];
[ ################### _______________ / ############################## /                  ]( РАСПОРЯЖЕНИЕ_ДОЛЖ , РАСПОРЯЖЕНИЕ_ФИО:c );
[                                                                                         ];
[                                                                                         ];
[ М.П.                                                                                    ];
[                                                                                         ];
end;

macro PrintDoc(Code_Currency,Account,nCopy, TR_account)
                                                                                       
  var params:TArray = TArray(),
      select, rs,
      
      AdressDep, 
      Open_DateAcc,
      PartyName,
      FI_Name,
      ClientID,
      DepName,
      Rest,
      StrRest,
      РАСПОРЯЖЕНИЕ_ДОЛЖ,
      РАСПОРЯЖЕНИЕ_ФИО;

  select = "select dParty_dbt.t_Name, dFininstr_dbt.t_Name, dReqopnac_dbt.t_ClientID, dDp_dep_dbt.t_Name "+ 
             "from dReqopnac_dbt, dParty_dbt, dFininstr_dbt, dDp_dep_dbt "+
             "where "+ 
               "dReqopnac_dbt.t_Account = :acc and "+
               "dReqopnac_dbt.t_AccountFIID = :Code_Currency and "+
               "dReqopnac_dbt.t_ClientID = dParty_dbt.t_PartyID and "+
               "dReqopnac_dbt.t_AccountFIID = dFininstr_dbt.t_FIID and "+
               "dReqopnac_dbt.t_Department = dDp_dep_dbt.t_Code";
      params[params.size] = ( SQLParam( "acc"   , Account ) );
      params[params.size] = ( SQLParam( "Code_Currency"  , Code_Currency ) );

      rs = execSQLselect( select, params, FALSE );
      if( rs and rs.MoveNext())

        PartyName = rs.value(0);
        FI_Name = rs.value(1);
        ClientID = rs.value(2);
        DepName = rs.value(3);

        Open_DateAcc = TR_account.rec.Open_Date;

        AdressDep = GetRealAdressDep({OperDprtNode});

        if( GetRegValForOPENAC( "РАСПОРЯЖЕНИЕ_ДОЛЖ", V_STRING, РАСПОРЯЖЕНИЕ_ДОЛЖ ) )
           РАСПОРЯЖЕНИЕ_ДОЛЖ = "";
        end;

        if( GetRegValForOPENAC( "РАСПОРЯЖЕНИЕ_ФИО", V_STRING, РАСПОРЯЖЕНИЕ_ФИО ) )
           РАСПОРЯЖЕНИЕ_ФИО = "";
        end;

        //Остаток на счете
        if( Code_Currency == NATCUR )
            Rest = RestA(Account , {curdate});
            StrRest = string(Rest:a) +" ( " + RubToStrAlt(Rest) + " ).";
        else
            Rest = RestAC(Account , Code_Currency, {curdate});
            StrRest = string(Rest:a) +" ( " + CurToStrAlt(Rest, NULL, NULL, GetISOCode(Code_Currency)) + " ).";
        end;

        while(nCopy)
          DrawReport(AdressDep,      
                   Open_DateAcc,   
                   PartyName,
                   FI_Name,
                   ClientID,
                   DepName,
                   РАСПОРЯЖЕНИЕ_ДОЛЖ,
                   РАСПОРЯЖЕНИЕ_ФИО,
                   Account,
                   StrRest
                  );
          nCopy = nCopy - 1;
        end;

      else
        msgbox("Ошибка создания данных справки.");
        return false;
      end;

      return true;
end;

macro PrintInqOpSAcc(Code_Currency,Account):bool

   var Tb_Fininstr, Tb_Account, Tb_Reqopnac,nCopy,    
       TR_account:TRecHandler = TRecHandler( "account.dbt"  , "bank.def" );

   if( Code_Currency == -1 )
       msgbox("Не задана валюта");
       return false;
   end;
         
    if( not strlen(Account) )
        msgbox("Не задан счёт");
        return false;
    end;
         
    Tb_Fininstr = Tbfile("Fininstr.dbt", "r");

    Tb_Fininstr.rec.FIID = Code_Currency;

    if( not GetEQ(Tb_Fininstr) )
        msgbox("Неверное значение валюты");
        return false;
    end;

    TR_account = GetAccount( 1 , Account, Code_Currency);
    if( not TR_account.rec.Account )
       msgbox("Неверное значение счёта");
       return false;
    end;

    if(TR_account.rec.Department != {OperDprt})
       msgbox("Счёт не принадлежит текущему филиалу");
       return false;
    end;

    if( SubStr(TR_account.rec.Type_Account, 1, 1) != "L" )
       msgbox("Счёт не является накопительным");
       return false;
    end;

    Tb_Reqopnac = Tbfile("reqopnac.dbt","r",5);

    Tb_Reqopnac.rec.AccountFIID = Code_Currency;
    Tb_Reqopnac.rec.Account = Account;

    if(not getEQ(Tb_Reqopnac))
      msgbox("Не найдено заявление на открытие накопительного счёта");
      return false;
    end;
    
    nCopy = GetNumCopy();

    if(not nCopy)
      return false;
    end;
  
   return PrintDoc(Code_Currency,Account,nCopy,TR_account);
end;