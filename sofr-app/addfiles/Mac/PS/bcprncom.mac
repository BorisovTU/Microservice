/*
  $Name:         bcprncom.mac
  $Module:       
  $Description:  
*/
/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2001              */
/*                                                                      */
/*  Имя файла        : ps_buycr.mac                                     */
/*                                                                      */
/*  Описание         : Печатные формы поручений на п\п валюты           */
/*  Программист      : Бурак В.Ю.                                       */
/*                                                                      */
/*  Создан           : 12.04.2001                                       */
/*                                                                      */
/************************************************************************/

import BankInter, PTInter, FIInter, globals, "adress.mac";

record prn_bcord (ps_bcord);
record rep_bcord (ps_bcord);
record rep_bcexe (ps_bcexe);

const КоличествоКопий = 0,
      ALG_PS_BCKINDPAY = 2997,
      PSBCKIND_BUY = 1, PSBCKIND_PAY = 2, PSBCKIND_CONV = 3,
      PSBC_FREE = 0, PSBC_REV = 2;

var MassCopy = 0, ncopy;     /* Число копий при массовой печати из списка */

macro GetClientPartyProperty( PartyID, Name_Client, Address_Client, Phone )

file Субъекты (party) key 0;
record RecordAdress ( adress );
var PhoneNumber : string = "";

  if ( ПолучитьСубъекта (PartyID, Субъекты ) != 0 )
      msgbox("Не найден субъект");
      return 1;
  else
      ClearRecord(RecordAdress);
      НайтиЮридическийАдресСубъекта(Субъекты.PartyID,RecordAdress);
      FindAdressContactValue(Субъекты.PartyID, PTADDR_LEGAL, CNTADR_PHONENUMBER, PhoneNumber);
      SetParm( 1, Субъекты.Name );            /*Заполним имя клиента*/
      SetParm( 2, RecordAdress.Adress );      /*Заполним почтовый адрес*/
      SetParm( 3, PhoneNumber ); /*Заполним телефон*/
  end;

end;

macro GetBankProperty( CodeKind, Code, Name_Bank, INN )

file Коды_Субъектов(partcode) key 1;
file Субъекты (party) key 0;

     Коды_Субъектов.CodeKind = CodeKind;
     Коды_Субъектов.Code     = Code;
     if(  getEQ(Коды_Субъектов) )
       if( ПолучитьСубъекта (Коды_Субъектов.PartyID, Субъекты ) == 0 )
         SetParm( 2, Субъекты.Name );      /*Заполним имя банка*/
         SetParm( 3, Субъекты.OKPO );      /*Заполним ИНН банка*/
       else
         SetParm( 2, "" );      /*Заполним имя банка*/
         SetParm( 3, "" );      /*Заполним ИНН банка*/
       end;
     else
       SetParm( 2, "" );      /*Заполним имя банка*/
       SetParm( 3, "" );      /*Заполним ИНН банка*/
     end;
end;

macro GetFIIDProperty( FIID, ISO, Name_FIID )
record fi ( fininstr );

  if( FIID != ALLFININSTR )
    if( ПолучитьФинИн( FIID, fi ) == 0 )
      SetParm( 1, fi.Ccy);
      SetParm( 2, fi.Name);
    else 
      SetParm( 1, ""); 
      SetParm( 2, ""); 
    end;
  else
    SetParm( 1, ""); 
    SetParm( 2, ""); 
  end;

end;

macro ПолучитьISOКод( FIID )
record fi ( fininstr );

  if( FIID != ALLFININSTR )
    if( ПолучитьФинИн( FIID, fi ) == 0 )
      return int(fi.ISO_Number);
    else
      return "";
    end;
  else
    return "";
  end;

end;

macro GetRateTypeName( RateType, TypeName, Rate, Point )

file type( ratetype ) key 0;

  type.Type = RateType;
  if( getEQ( type) )
    SetParm(1, type.TypeName );
  else
    SetParm(1, Rate/pow(10,Point) );
  end;
end;

macro GetRate( TypeName, SourceAmount, RequiredAmount, Scale, Point, IsInverse )

  if( IsInverse == "X")
    IsInverse = "";
  else
    IsInverse = "X";
  end;
  TypeName = ConvSumRate( SourceAmount, RequiredAmount, Scale, Point, IsInverse );
  SetParm(0, TypeName/pow(10,Point) );

end;

macro GetNameMarketPlace( MarketPlace, NameMarketPlace )

file Субъекты (party) key 0;

  if( ПолучитьСубъекта(MarketPlace, Субъекты ))
      SetParm( 1, "За счет средств банка" );        
  else
      SetParm( 1, Субъекты.Name );        
  end;

end;

macro GetDocGround( OrderID, DocKind, DocGround );

   file ps_ordoc(ps_ordoc) key 0;

   var result    = "";
   var delimiter = "";

   ps_ordoc.DocKind = DocKind;
   ps_ordoc.OrderID = OrderID;
   ps_ordoc.OrderDocType = 0;

   var stat = getGE( ps_ordoc );

   while( stat and (ps_ordoc.DocKind == DocKind) and (ps_ordoc.OrderID == OrderID) )
      result = result + delimiter + ps_ordoc.Comment;
      delimiter = "; ";
      stat = Next( ps_ordoc );
   end;

   SetParm( 2, result );

end;

macro GetKindCurrAccount( SourceAccount, SourceFIID, KindCurrAccount )

file acc( "account.dbt" ) key 0;
file type( typeac )  key 0;

  acc.Chapter = 1;
  acc.Account = SourceAccount;
  acc.Code_Currency = SourceFIID;

  if( getEQ(acc) )
    type.iNumType    = 4;
    if( Index( acc.Type_Account, "X"))
      type.Type_Account = "X";
      if( getEQ(type) )  
        SetParm( 2, type.Name_Type );
      else
        SetParm( 2, "" );
      end;
    elif( Index( acc.Type_Account, "Y"))
      type.Type_Account = "Y";
      if( getEQ(type) )
        SetParm( 2, type.Name_Type );
      else
        SetParm( 2, "" );
      end;
    elif( Index( acc.Type_Account, "Z"))
      type.Type_Account = "Z";
      if( getEQ(type) )
        SetParm( 2, type.Name_Type );
      else
        SetParm( 2, "" );
      end;
    end;
  else
    SetParm( 2, "" );
  end;

end;

macro GetPartyPropertyforBCReectr( PartyID, CodeKind, Code, CodeOKPO, Name_Client, Code13 )
var err;
file Субъекты (party) key 0;
file Коды_Субъектов(partcode) key 1;

  if( not ПолучитьСубъекта (PartyID, Субъекты ) )
    SetParm( 3, Субъекты.OKPO );         /*Заполним код ОКПО*/
    SetParm( 4, Субъекты.Name );         /*Заполним имя клиента*/
  else
    SetParm( 3, "" );         /*Заполним код ОКПО*/
    SetParm( 4, "" );         /*Заполним имя клиента*/
  end;
  
  Коды_Субъектов.CodeKind = CodeKind;
  Коды_Субъектов.Code     = Code;
  if(  getEQ(Коды_Субъектов) )
    SetParm( 5, ПолучитьКодСубъекта( Коды_Субъектов.PartyID, PTCK_BANKREGNUM, err ) );
  else
    SetParm( 5, ПолучитьКодСубъекта( {OurBank}, PTCK_BANKREGNUM, err ));
  end;


end;

macro GetOKPO( PartyID )
file Субъекты (party) key 0;
  if( not ПолучитьСубъекта (PartyID, Субъекты ) )
    return Субъекты.OKPO;
  end;
end;  

macro GetNameKindOrder( BCOrdKind )
  if( BCOrdKind == PSBCKIND_BUY )
    return "покупку";
  elif( BCOrdKind == PSBCKIND_PAY )
    return "продажу";
  elif( BCOrdKind == PSBCKIND_CONV )
    return "конверсию";
  else
    return "";
  end;
end;

macro GetNameBank()

file bankdprt( bankdprt ) key 0;

   bankdprt.PartyID = {OurBank};
   if( getEQ(bankdprt) )
     return ( {Name_Bank} );
   else
    return "";
   end;
end;