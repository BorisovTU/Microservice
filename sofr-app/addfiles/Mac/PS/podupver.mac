/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.10                                         R-Style Software Lab

  File Name   : podupver.mac
  Created     : 25.12.2002
  Programmer  : Андреева Е.П.
  Description : Печать отчета сканирования массовой верификации
                дубликатов платежных документов

└───────────────────────────────────────────────────────────────────────────*/
IMPORT PaymInter;
IMPORT oralib, likepy;
RECORD pspo_firstdoc (psdupaor);

const   Ошибка   = 0,
        Первый   = 1,
        Последний= 2;

MACRO Печать_ОтчетСканирования (Вызов,firstdoc,Статус,Сообщение)
   var stat;
   array StringArray;
   var i = 1;

   var select:string;
   var params:TArray;
   var rs:object;
   var num;

   record rmprop (pmdurmpp);
        setbuff (pspo_firstdoc,firstdoc);

        if (Вызов==Первый)
[        Отчет о групповой верификации дубликатов платежных документов          ];
[+---------+------+------------------------------------------------------------+];
[|  Номер  |Номер |                 Сообщение                                  |];
[|дубликата|ошибки|                                                            |];
[+---------+------+------------------------------------------------------------+];
        elif (Вызов==Ошибка)

                if (Статус == 0)
                        Сообщение = "Верификация прошла успешно";
                end;
                if (Статус == 531)
                        Статус    = 1834;
                        Сообщение = "Для дубликата найдено несколько оригиналов";
                end;

            select = "SELECT t_Number "                         + 
                     "FROM dpmdupaym_dbt pm, dpmdurmpp_dbt rm " + 
                     "WHERE pm.t_DocKind    = :dockind "        +  
                     "  AND pm.t_DocumentID = :docid "          + 
                     "  AND pm.t_Purpose    = :purp "           + 
                     "  AND pm.t_SubPurpose = :subpurp "        +
                     "  AND pm.t_PaymentID  = rm.t_PaymentID; " ;
                
            params = makeArray( SQLParam("dockind", 261),
                                SQLParam("docid",   pspo_firstdoc.OrderID),
                                SQLParam("purp",    PM_PURP_POPRIMARY),
                                SQLParam("subpurp", 0) );
            rs = execSQLselect( select, params, FALSE );
                
            if( rs AND rs.moveNext() )
               num = rs.value(0) ;
            end;

         StrSplit(StrSubst(Сообщение, "|", " " ), StringArray, 56);
[|#########|######|############################################################|](num/*rmprop.Number, pspo_firstdoc.OrderID*/,Статус,StringArray(0));
         while ( StrLen(StringArray(i) ) > 0)
[|         |      |############################################################|](StringArray(i));
           i = i + 1;
         end;

        elif (Вызов==Последний)
[+---------+------+------------------------------------------------------------+];
        end;
END;