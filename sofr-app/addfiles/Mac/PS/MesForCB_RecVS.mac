/*
  $Name:        MesForCB_RecVS.mac
  $Module:      РКО
  $Description: Блок Rec для сообщения в ЦБ об операциях по счетам
*/
//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : РКО
//
// Описание     : Блок Rec для сообщения в ЦБ об операциях по счетам
//
// Программист  : Чукина Т.А.
//
// Создан       : 16.10.2015
//
//-----------------------------------------------------------------------------

import "MesForCB_lib.mac", PaymInter, BankInter, PSInter, "MesForCB_protocol.mac", FIInter,
       "bnk_dttmfrmt.mac";

private const PathFillExtAccount : string = "PS\\СООБЩЕНИЯ В ЦБ\\FILL_EXT_ACCOUNT";

class (TXmlNodesAdd) TBlockRecVS(p_nodeRec : object, p_xml : object)
  InitTXmlNodesAdd(p_nodeRec, p_xml);

  private var Protocol = MesForCB_GetProtocolObj();
  private var 
    RegFillExtAccount : string = Bnk_GetRegistryValue(PathFillExtAccount, V_INTEGER, 1);

  private macro GetSymbolsFromRs(rs : RsdRecordset) : string
    var Symbols : string = "";

    var IsFirst : bool = true;
    while(rs and rs.moveNext())
      if(not IsFirst)
        Symbols = Symbols + ",";
      end;

      Symbols = Symbols + rs.value(0);
      IsFirst = false;
    end;

    return Symbols;
  end;

  private macro GetAcctrnSymbols(AccTrnID : integer) : string
    var rs : RsdRecordset = execSQLselectPrm
      ( "select t_Symbol "
        "  from dsymbcash_dbt "
        " where t_AccTrnID = :AccTrnID ",
        SQLParam("AccTrnID", AccTrnID)
      );

    return GetSymbolsFromRs(rs);
  end;

  private macro GetPaymentSymbols(PaymentID : integer) : string
    var rs : RsdRecordset = execSQLselectPrm
      ( "select t_Symbol "
        "  from dsymbcash_dbt "
        " where t_ApplicationKey = LPAD( :PaymentID, 34, '0') ",
        SQLParam("PaymentID", PaymentID)
      );

    return GetSymbolsFromRs(rs);
  end;

  private macro Fill_SCO(rs : RsdRecordset, IsGK : bool)
    var CashSymbols : string = rs.value("t_CashSymbols");

    if(IsGK)
      var AccTrnID : integer = int( rs.value("t_UKey") ),
          PaymentID : integer = rs.value("t_PaymentID");

      CashSymbols = GetAcctrnSymbols(AccTrnID);
      if(PaymentID and not CashSymbols)
        CashSymbols = GetPaymentSymbols(PaymentID);
      end;
    end;

    if(CashSymbols)
      CashSymbols = StrSubst(CashSymbols, " ", "");
      AddText( "SCO", StrNormalization( CashSymbols ) );
    end;
  end;

  private macro GetCountryCode(BankID : integer) : string
    var Code : string = "";

    var rs : RsdRecordset = execSQLselectPrm
      ( "select country.t_CodeLat2 "
        "  from dadress_dbt adr, dcountry_dbt country "
        " where adr.t_PartyID = :PartyID "
        "   and adr.t_Type = :PTADDR_LEGAL "
        "   and country.t_CodeLat3 = adr.t_Country ",
        SQLParam("PartyID", BankID),
        SQLParam("PTADDR_LEGAL", PTADDR_LEGAL)
      );

    if(rs and rs.moveNext())
      Code = rs.value(0);
    end;

    return Code;
  end;

  private macro Fill_EXT_KSM(SidePrefix : string, BankID : integer)
    var Code : string = GetCountryCode(BankID);

    if(Code)
      AddText( SidePrefix + "_EXT_KSM", Code );
    end;
  end;

  private macro Fill_EXT_BIK(SidePrefix : string, BankID : integer)
    var Code : string = ПолучитьКодСубъекта(BankID, PTCK_BIC);

    if(Code)
      AddText( SidePrefix + "_EXT_BIK", StrNormalization( Code ) );
    end;
  end;

  private macro Fill_EXT_SWIFT(SidePrefix : string, BankID : integer)
    var Code : string = ПолучитьКодСубъекта(BankID, PTCK_SWIFT);

    if(Code)
      AddText( SidePrefix + "_EXT_SWIFT", StrNormalization( Code ) );
    end;
  end;

  private macro IsFindExtClient : bool
    var RegPath : string = "PS\\СООБЩЕНИЯ В ЦБ\\FIND_EXTCLIENT";
    return Bnk_GetRegistryValue(RegPath, V_BOOL, false);
  end;

  private macro Fill_EXT_INNKPP
  ( SidePrefix : string,
    ClientID : integer, 
    ClientINN : string, 
    ClientKPP : string
  )
    if(ClientID > 0)
      var INNKPP : string = ПолучитьКодСубъекта(ClientID, PTCK_INN);

      if(INNKPP)
        var INN : string = RemoveKPP(INNKPP);
        var KPP : string = RemoveINN(INNKPP);

        if(INN)
          ClientINN = INN;
        end;

        if(KPP)
          ClientKPP = KPP;
        end;
      end;
    end;
  
    ClientINN = DeleteNotDigitSymbols( ClientINN, true );
    
    if(ClientINN)
      AddText( SidePrefix + "_EXT_INN", StrNormalization( ClientINN ) );
    end;

    if( ClientKPP )
      if( MakeRightKPP( @ClientKPP ) )
        AddText( SidePrefix + "_EXT_KPP", ClientKPP );
      end;
    end;
  end;

  private macro Fill_EXT_OGRN(SidePrefix : string, ClientID : integer, ClientOGRN : string)
    if(ClientID > 0)
      var Code : string = ПолучитьКодСубъекта(ClientID, PTCK_OGRN);

      if(Code)
        ClientOGRN = Code;
      end;
    end;

    if(ClientOGRN)
      AddText( SidePrefix + "_EXT_OGRN", StrNormalization( ClientOGRN ) );
    end;
  end;

  private macro Fill_EXT_TNAME
  ( SidePrefix : string, 
    ClientID : integer, 
    ClientName : string, 
    ClientAccount : string, 
    UIN : string,
    BankID : integer
  )
    if(ClientID > 0)
      var s : string = GetClientInfoForCB(ClientID, UIN, ClientAccount, BankID);

      if(s)
        ClientName = s;
      end;
    end;

    if(ClientName)
      AddText( SidePrefix + "_EXT_TNAME", StrNormalization( ClientName ) );
    end;
  end;

  private macro CorrectClientData
  ( ClientID : @integer, 
    ClientINN : @string, 
    ClientKPP : @string, 
    ClientName : @string, 
    IsBO : bool
  )

    if(not IsBO)
      ClientKPP = RemoveINN(ClientINN);
      ClientINN = RemoveKPP(ClientINN);
    end;

    if( (ClientID <= 0) and IsFindExtClient() )
      var ClientType : integer = PTLEGF_PERSN;
      if( InList( StrLen(ClientINN), 10, 5 ) )
        ClientType = PTLEGF_INST;
      end;

      var stat : integer = PSFNSCheckClientData
        ( ClientINN,
          ClientKPP,
          ClientName,
          "", // ClientAddress
          ClientID,
          ClientType,
          true // IsMassMode
        );

    end;

  end;

  private macro Fill_EXTCLIENT
  ( SidePrefix : string,
    BankID : integer,
    BankAccount : string,
    ClientID : integer,
    ClientINN : string,
    ClientKPP : string,
    ClientOGRN : string,
    ClientName : string,
    ClientAccount : string,
    UIN : string,
    NeedFillExtAccount : bool
  )
    if( (BankID <= 0) or PM_IsBankInTS(BankID, true) )
      return;
    end;

    Fill_EXT_KSM(SidePrefix, BankID);

    if(NeedFillExtAccount)
      var ExtAccount : string = IfThenElse(RegFillExtAccount == 2, ClientAccount, BankAccount);
      if(ExtAccount)
        AddText( SidePrefix + "_EXT_ACCOUNT", StrNormalization( ExtAccount ) );
      end;
    end;

    Fill_EXT_BIK(SidePrefix, BankID);
    Fill_EXT_SWIFT(SidePrefix, BankID);
    Fill_EXT_INNKPP(SidePrefix, ClientID, ClientINN, ClientKPP);
    Fill_EXT_OGRN(SidePrefix, ClientID, ClientOGRN);
    Fill_EXT_TNAME(SidePrefix, ClientID, ClientName, ClientAccount, UIN, BankID);
  end;

  private macro GetNeedFillAccounts
  ( SidePrefix : string,
    IsBO : bool,
    rs : RsdRecordset,
    ClientID : integer,
    NeedFillAccount : @bool,
    NeedFillExtAccount : @bool
  )
    const FldPrefix : string = IfThenElse(SidePrefix == "DT", "t_Payer", "t_Receiver");

    const AccountFld : string = FldPrefix + IfThenElse(IsBO, "Account", "AccountPaym"),
          FiidFld : string = FldPrefix + IfThenElse(IsBO, "FIID", "FIIDPaym"),
          BankIdFld : string = FldPrefix + "BankID";

    macro IsDocInternal : bool
      if( (rs.value("t_PayerGroup") == PAYMENTS_GROUP_INTERNAL) and
          (rs.value("t_ReceiverGroup") == PAYMENTS_GROUP_INTERNAL) 
        )
        return TRUE;
      end;

      return FALSE;
    end;

    macro IsSideInternal : bool
      if( rs.value(FldPrefix + "Group") == PAYMENTS_GROUP_INTERNAL )
        return TRUE;
      end;

      if(IsBO)
        if( PM_IsBankInTS(rs.value(BankIdFld), true) )
          return TRUE;
        end;
      end;

      return FALSE;
    end;

  //begin
    NeedFillAccount = false;
    NeedFillExtAccount = false;

    if( IsDocInternal() )
      NeedFillAccount = true;
    elif( IsSideInternal() )
      NeedFillAccount = true;
    else
      if(RegFillExtAccount == 2)
        NeedFillAccount = true;
        NeedFillExtAccount = true;
      elif( not PM_IsBankAccount(rs.value(AccountFld), rs.value(FiidFld)) )
        NeedFillAccount = true;

        if( not БанкУБР(ClientID) and not БанкУБР(rs.value(BankIdFld)) )
          NeedFillExtAccount = true;
        end;
      else
        NeedFillExtAccount = true;
      end;
    end;
  end;

  macro Fill(rs : RsdRecordset)
    var RegBranchBO : string = rs.value("t_RegBranchBO");

    var UKEY : string = rs.value("t_UKey"),
        DT_ACCOUNT : string = "",
        KT_ACCOUNT : string = "",
        SUM : money = rs.value("t_Sum");

    var IsBO : bool = false;
    if(RegBranchBO)
      IsBO = true;
    end;

    var PayerID : integer = rs.value("t_Payer"),
        PayerINN : string = rs.value("t_PayerINN"),
        PayerKPP : string = rs.value("t_PayerKPP"),
        PayerName : string = rs.value("t_PayerName"),
        ReceiverID : integer = rs.value("t_Receiver"),
        ReceiverINN : string = rs.value("t_ReceiverINN"),
        ReceiverKPP : string = rs.value("t_ReceiverKPP"),
        ReceiverName : string = rs.value("t_ReceiverName");
    CorrectClientData(@PayerID, @PayerINN, @PayerKPP, @PayerName, IsBO);
    CorrectClientData(@ReceiverID, @ReceiverINN, @ReceiverKPP, @ReceiverName, IsBO);

    var NeedFillDtAccount : bool, NeedFillKtAccount : bool,
        NeedFillDtExtAccount : bool, NeedFillKtExtAccount : bool;
    GetNeedFillAccounts("DT", IsBO, rs, PayerID, @NeedFillDtAccount, @NeedFillDtExtAccount);
    GetNeedFillAccounts("KT", IsBO, rs, ReceiverID, @NeedFillKtAccount, @NeedFillKtExtAccount);

    AddText( "UKEY", StrNormalization( UKEY ) );
    AddText( "DATA", StrNormalization( DDpMMpYYYY(rs.value("t_Date")) ) );
    AddText( "TIME", StrNormalization( TimeToStr(rs.value("t_Time"), "hh:mi:ss") ) );
    AddText( "DCM_DATE", StrNormalization( DDpMMpYYYY(rs.value("t_DCM_Date")) ) );
    AddText( "DCM_NUM", StrNormalization( rs.value("t_DCM_Num") ) );

    if( NeedFillDtAccount )
      DT_ACCOUNT = rs.value("t_PayerAccount");
      AddText( "DT_ACCOUNT", StrNormalization( DT_ACCOUNT ) );
    end;

    if( NeedFillKtAccount )
      KT_ACCOUNT = rs.value("t_ReceiverAccount");
      AddText( "KT_ACCOUNT", StrNormalization( KT_ACCOUNT ) );
    end;

    AddText( "SUM", StrNormalization( SUM ) );
    if( rs.value("t_PayerFIID") != NATCUR )
      AddText( "DT_SUMC", StrNormalization( rs.value("t_DT_SumC") ) );
    end;
    if( rs.value("t_ReceiverFIID") != NATCUR )
      AddText( "KT_SUMC", StrNormalization( rs.value("t_KT_SumC") ) );
    end;

    if( rs.value("t_Ground") != "" )
      AddText( "APPOINT", StrNormalization( SubStr(rs.value("t_Ground"), 1, 210) ) );
    end;

    Fill_SCO(rs, not IsBO);
    Fill_EXTCLIENT
      ( "DT",
        rs.value("t_PayerBankID"),
        rs.value("t_PayerBankAccount"),
        PayerID,
        PayerINN,
        PayerKPP,
        rs.value("t_PayerOGRN"),
        PayerName,
        IfThenElse(IsBO, rs.value("t_PayerAccount"), rs.value("t_PayerAccountPaym")),
        rs.value("t_UIN"),
        NeedFillDtExtAccount
      );
    Fill_EXTCLIENT
      ( "KT",
        rs.value("t_ReceiverBankID"),
        rs.value("t_ReceiverBankAccount"),
        ReceiverID,
        ReceiverINN,
        ReceiverKPP,
        rs.value("t_ReceiverOGRN"),
        ReceiverName,
        IfThenElse(IsBO, rs.value("t_ReceiverAccount"), rs.value("t_ReceiverAccountPaym")),
        rs.value("t_UIN"),
        NeedFillKtExtAccount
      );

    Protocol.PrintTableRow(UKEY, DT_ACCOUNT, KT_ACCOUNT, SUM);
  end;
end;
