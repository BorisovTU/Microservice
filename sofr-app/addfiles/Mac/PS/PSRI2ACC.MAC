/* Исправление расхождения сумм платежных документов и счетов картотеки N2 */

import PSInter,OprInter,PaymInter;

FILE psindacc("psindacc") write;
FILE Ps_payord(pspayord) key 4;
FILE Pm_paym  (pmpaym) key 1;
FILE Pm_rmprop( pmrmprop );

VAR
      stat,
      Number,Account,
      ОстатокКартотеки,
      ОстатокКартотеки_ИТОГО,
      СуммаДокументов,
      СуммаДокументов_ИТОГО,
      ErrText,ercode,text,
      RepKind,
      success,
      StatusText,
      ProcessedAll = 0,
      ProcessedOK = 0,
      ProcessedWithErrors = 0;

ARRAY m;
m(0) = " без внесения изменений ";
m(1) = " с внесением изменений ";

RepKind = Menu(m,"Режим работы", "Режим работы:");

if( (RepKind == 1) or (RepKind == 0))
  [                    Список счетов в которых обнаружена ошибка ];
  if(RepKind == 1)
  [+-------------------------+--------------------+--------------------+--------------------+---------------------------------------------------+];
  [|     Счет картотеки      |  Сумма находящихся |  Текущий остаток   |    Расхождение     |Исправлен|  Ошибка (при исправлении суммы)         |];
  [|                         |докум.на счете карт.|  по счету в карт.  |  остатка и суммы   |         |                                         |];
  [+-------------------------+--------------------+--------------------+--------------------+---------------------------------------------------+];
  else
  [+-------------------------+--------------------+--------------------+--------------------+-----------------------------------------+];
  [|     Счет картотеки      |  Сумма находящихся |  Текущий остаток   |    Расхождение     |  Ошибка (при поиске)                    |];
  [|                         |докум.на счете карт.|  по счету в карт.  |  остатка и суммы   |                                         |];
  [+-------------------------+--------------------+--------------------+--------------------+-----------------------------------------+];
  end;

  ОстатокКартотеки_ИТОГО  = $0;
  СуммаДокументов_ИТОГО   = $0;

  Rewind(psindacc);

  while( next( psindacc ) )
      ProcessedAll = ProcessedAll + 1;
      ErrText = "";

          ОстатокКартотеки = psindacc.Sum;
          ОстатокКартотеки_ИТОГО = ОстатокКартотеки_ИТОГО + ОстатокКартотеки;

          stat = GetInd2AccDocSum(psindacc.Account,СуммаДокументов);
          if(stat == 0) СуммаДокументов_ИТОГО = СуммаДокументов_ИТОГО + СуммаДокументов;
          else ErrText = "Ошибка при определении суммы документов в картотеке";
          end;

          if((stat == 0) and (ОстатокКартотеки != СуммаДокументов))
            if(RepKind == 1) /* с внесением изменений */
              psindacc.Sum = СуммаДокументов;
              if ( not Update ( psindacc ) )
                ercode = status(text);
                ErrText = "Ошибка в файле pspayord.dbt" + "("+ ercode +"):" + text;
                success = " ";
              else
                success = "X";
              end;
              [| ########################| ################## | ################## | ################## |    #    | #########################################]
              (psindacc.Account:f,СуммаДокументов,ОстатокКартотеки,(ОстатокКартотеки - СуммаДокументов),success,ErrText);
            end;
          end;

          if((stat != 0) or (RepKind == 0) )
              [| ########################| ################## | ################## | ################## | #########################################]
              (psindacc.Account:f,СуммаДокументов:a:r,ОстатокКартотеки:a:r,(ОстатокКартотеки - СуммаДокументов):a:r,ErrText);
          end;

          if(ErrText == "") ProcessedOK = ProcessedOK + 1;
          else              ProcessedWithErrors = ProcessedWithErrors + 1;
          end;

      message( "Обработано записей:", ProcessedAll, ".");
  end;

  [+----------------------------------------------------------------------------------------+];
  [ ];
  [ Успешно обработано записей  : ###### ] ( ProcessedOK );
  [ Сумма всех находящихся докум. в карт.: ##################] (СуммаДокументов_ИТОГО);
  [ Сумма в картотеке по всем счетам:      ##################] (ОстатокКартотеки_ИТОГО);
  [ Расхождение:                           ##################] (ОстатокКартотеки_ИТОГО - СуммаДокументов_ИТОГО);
  if(RepKind == 1) /* с внесением изменений */
  [ Ошибки поиска/корректировки записи : ###### ] ( ProcessedWithErrors );
  end;
end;

END;