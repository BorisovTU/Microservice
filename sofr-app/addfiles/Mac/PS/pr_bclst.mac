/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2001              */
/*                                                                      */
/*  Имя файла        : pr_bclst.mac                                     */
/*                                                                      */
/*  Описание         : Ведомость поручений на покупку/продажу           */
/*  Программист      : Бурак В.Ю.                                       */
/*                                                                      */
/*  Создан           : 13.04.2001                                       */
/*                                                                      */
/************************************************************************/

import bcprncom, oralib, PSInter;

macro PrintDayHeader( BCOrdKind, Day )

 var NameKind = GetNameKindOrder( BCOrdKind );

[
                      Ведомость поручений на #
                              за ########## г.
]
( NameKind:l, Day:c );

end;

macro PrintPerHeader( BCOrdKind, Day_In, Day_Out )

 var NameKind = GetNameKindOrder( BCOrdKind );

[
                          Ведомость поручений на #
                          c ########## г. по ########## г.
]
( NameKind:l, Day_In:c, Day_Out:c );

end;

macro PrintRequestHeader( )
[                           Заявленные данные  ];
end;
macro PrintExecuteHeader( )
[                         Результаты исполнения  ];
end;

macro PrintBPNewCurrency( ISO, Name_FIID )

[ 
Валюта: ###  ######################### 
+------+----------------------+----------------------+--------------------------------------------------------------------------------------+ 
|  №   |         Счет         |  Название клиента    |                                 Параметры поручения                                  |
|      |                      |                      +------------------+--------------------+--------------------+------------+------------+
|      |                      |                      |       Номер      |     Сумма валюты   |     Сумма рублей   |    Курс    |    Дата    |
+------+----------------------+----------------------+------------------+--------------------+--------------------+------------+------------+ 
]
( ISO:c, Name_FIID:l );

end;

macro PrintCNewCurrency( ISO, Name_FIID )
[ 
Валюта: ###  ######################### 
+------+----------------------+----------------------+--------------------------------------------------------------------------------------------------+ 
|  №   |         Счет         |  Название клиента    |                                      Параметры поручения                                         |
|      |                      |                      +------------------+-----+--------------------+-----+--------------------+------------+------------+
|      |                      |                      |       Номер      | Код |    Сумма валюты    | Код |     Сумма валюты   |    Курс    |    Дата    |
|      |                      |                      |                  |     |      списания      |     |      зачисления    |            |            |
+------+----------------------+----------------------+------------------+-----+--------------------+-----+--------------------+------------+------------+ 
]
( ISO:c, Name_FIID:l );
end;

macro PrintBDocument( DocCurrency, Account, Name_Client, Number, CurAmount, RubAmount, Rate, DateDoc)


[| #### | #################### | #################### | ################ | ################## | ################## | ########## | ########## |
+------+----------------------+----------------------+------------------+--------------------+--------------------+------------+------------+ 
]
( DocCurrency:c, Account:c, Name_Client:c, Number:c, CurAmount:f:c, RubAmount:f:c, Rate:c, DateDoc:f );

 end;

macro PrintPDocument( DocCurrency, Account, Name_Client, Number, CurAmount, RubAmount, Rate, DateDoc )


[| #### | #################### | #################### | ################ | ################## | ################## | ########## | ########## |
+------+----------------------+----------------------+------------------+--------------------+--------------------+------------+------------+ 
]
( DocCurrency:c, Account:c, Name_Client:c, Number:c, CurAmount:f:c, RubAmount:f:c, Rate:c, DateDoc:f );

end;

macro PrintCDocument( DocCurrency, Account, Name_Client, Number, FIID, Amount1, PayFIID, Amount2, Rate, DateDoc )

[| #### | #################### | #################### | ################ | ### | ################## | ### | ################## | ########## | ########## |
+------+----------------------+----------------------+------------------+-----+--------------------+-----+--------------------+------------+------------+ 
]
( DocCurrency:c, Account:c, Name_Client:c, Number:c, 
  FIID:c, Amount1:f:c,
  PayFIID:c, Amount2:f:c, Rate:c, DateDoc:f );
end;


macro PrintBPDayItog( Sum1, Sum2 )
[| Итого за день                                                         |                    |                    |                         |
|                                                                       | ################## | ################## |                         |
|                                                                       |                    |                    |                         |
+------+----------------------+----------------------+------------------+--------------------+--------------------+-------------------------+ 
]( Sum1:f:c, Sum2:f:c );
end;

macro PrintCDayItog( Sum1 )
[| Итого за день                                                         |     |                    |     |                    |            |            |
|                                                                       |     | ################## |     |                    |            |            |
|                                                                       |     |                    |     |                    |            |            |
+------+----------------------+----------------------+------------------+-----+--------------------+-----+--------------------+------------+------------+ 
]( Sum1:f:c );
end;

macro PrintBPPerItog( Sum1, Sum2 )
[| Итого за период                                                       |                    |                    |                         |
|                                                                       | ################## | ################## |                         |
|                                                                       |                    |                    |                         |
+-----------------------------------------------------------------------+--------------------+--------------------+-------------------------+ 
]( Sum1:f:c, Sum2:f:c );
end;

macro PrintCPerItog( Sum1 )
[| Итого за период                                                       |     |                    |     |                    |            |            |
|                                                                       |     | ################## |     |                    |            |            |
|                                                                       |     |                    |     |                    |            |            |
+------+----------------------+----------------------+------------------+-----+--------------------+-----+--------------------+------------+------------+ 
]( Sum1:f:c );
end;

macro PrBPReq( Date_In, Date_Out, FIID, DepNum, DocKind, Close_Cond )
      var params:TArray = TArray();
      var select, rs, ch = 0, OldDate:date, dateF=0;

      var Account, Name, Number, CurAmount, RubAmount, Rate,
      OrderDate:date, FIName, FICode, SumCur = 0, SumRub = 0, SumDayCur=0,SumDayRub=0,Scale, Point, IsInverse;
      
      select = "select pmpaym.t_PayerAccount, pmpaym.t_ReceiverAccount, pmrmprop.t_PayerName, pmrmprop.T_NUMBER,"+
                     " pmpaym.t_PayAmount, pmpaym.t_Amount, pmpaym.t_Rate,  pmpaym.T_VALUEDATE,"+
                     " fininstr.T_NAME, fininstr.T_FI_CODE, pmpaym.t_Scale,"+
                     " pmpaym.t_Point, pmpaym.t_IsInverse"+ 
               " from dps_bcord_dbt ps_bcord,"+
                    " dpmpaym_dbt pmpaym,"+
                    " dpmrmprop_dbt pmrmprop,"+
                    " dfininstr_dbt fininstr"+
               " where pmpaym.T_PAYMENTID = ps_bcord.T_PAYMENTID"+
                " and pmrmprop.T_PAYMENTID = ps_bcord.T_PAYMENTID"+
                " and ps_bcord.T_BCORDKIND = :DocKind"+
                " and pmpaym.T_VALUEDATE between :Date_In and :Date_Out"
                ;

      if( Close_Cond )
        select = select + " and ps_bcord.T_STATE = 20";
      else
        select = select + " and (ps_bcord.T_STATE = 10 or ps_bcord.T_STATE = 20)";
      end;        


      
      params[params.size] = ( SQLParam( "DocKind"  , DocKind ));
      params[params.size] = ( SQLParam( "Date_In"  , Date_In ));
      params[params.size] = ( SQLParam( "Date_Out" , Date_Out ));

      if( DepNum != -1 )
        select = select + " and pmpaym.T_DEPARTMENT = :DepNum";
        params[params.size] = ( SQLParam( "DepNum"   , DepNum ));
      end;

      if(  FIID != -1  )
        if( DocKind == 1 )
          select = select + 
          " AND pmpaym.T_PAYFIID      = :FIID"+
          " AND fininstr.T_FIID(+) = pmpaym.T_PAYFIID";
          params[params.size] = ( SQLParam( "FIID" , FIID ));
        end;
        if( DocKind == 2 )
          select = select + 
          " AND pmpaym.T_FIID      = :FIID"+
          " AND fininstr.T_FIID(+) = pmpaym.T_FIID";
          params[params.size] = ( SQLParam( "FIID" , FIID ));
        end;
      else
        if( DocKind == 1 )
          select = select + " AND fininstr.T_FIID(+) = pmpaym.T_PAYFIID";
        end;
        if( DocKind == 2 ) 
          select = select + " AND fininstr.T_FIID(+) = pmpaym.T_FIID";
        end;
      end;

      select = select + " ORDER BY pmpaym.T_VALUEDATE";
      if ( DocKind == 1 )
        select = select+",pmpaym.t_ReceiverAccount ASC";
      else
        select = select+",pmpaym.T_PayerAccount  ASC";
      end;

      rs = execSQLselect( select, params, FALSE );      
     
      if( Date_In == Date_Out )
        if( DocKind == 1) PrintDayHeader( PSBCKIND_BUY, Date_In ); end;
        if( DocKind == 2) PrintDayHeader( PSBCKIND_PAY, Date_In ); end;        
      else
        if( DocKind == 1) PrintPerHeader( PSBCKIND_BUY, Date_In, Date_Out ); end;
        if( DocKind == 2) PrintPerHeader( PSBCKIND_PAY, Date_In, Date_Out ); end;        
      end;

      PrintRequestHeader();
       
      if( rs )
        while( rs.MoveNext())
          ch = ch + 1;

          Name      = rs.value(2);
          Number    = rs.value(3);
  
          if( Dockind == 1 )
            Account   = rs.value(1);
            CurAmount = rs.value(4);
            RubAmount = rs.value(5);
          end;
          
          if( Dockind == 2 )
            Account   = rs.value(0);
            CurAmount = rs.value(5);
            RubAmount = rs.value(4);
          end;
                    
          OrderDate = rs.value(7);
          Rate      = rs.value(6);
          FIName    = rs.value(8);
          FICode    = rs.value(9);
          Scale     = rs.value(10);
          Point     = rs.value(11);
          IsInverse = rs.value(12);
          
          if (Scale == 0)
            Rate = 0;
          else
            if (IsInverse !="X")
              Rate    = ( Rate / Scale )/pow( 10, Point );
            else
              Rate    = Scale / (Rate /pow( 10, Point ));
            end;
          end;
          
          if( ch == 1 )
            if(  FIID != -1  )
              PrintBPNewCurrency( FICode, FIName );
            else
              PrintBPNewCurrency( "", "по всем валютам" );
            end;
          end; 

          if( (Date_In != Date_Out) and (OrderDate!=OldDate) and (DateF))
            PrintBPDayItog( SumDayCur, SumDayRub ); 
            SumDayCur=0;
            SumDayRub=0;
          else
            DateF=1;
          end;
                             
          OldDate=OrderDate;

          if( DocKind == 1 )
            PrintBDocument( ch, Account, Name, Number, CurAmount, RubAmount, Rate, OrderDate, NULL, NULL );
          end;

          if( DocKind == 2 )
            PrintPDocument( ch, Account, Name, Number, CurAmount, RubAmount, Rate, OrderDate, NULL, NULL );
          end;
          
          SumCur = SumCur + CurAmount;
          SumRub = SumRub + RubAmount;
          SumDayCur = SumDayCur + CurAmount;
          SumDayRub = SumDayRub + RubAmount;

        end;      
      end;
      
      if( ch == 0 )
        PrintBPNewCurrency( 1, "", "" );
      end;

      if( Date_In != Date_Out)
        PrintBPDayItog( SumDayCur, SumDayRub );
        PrintBPPerItog( SumCur, SumRub );
      else
        PrintBPPerItog( SumCur, SumRub );
      end;
end;

macro PrBPExe( Date_In, Date_Out, FIID, DepNum, DocKind, Close_Cond )
      var params:TArray = TArray();
      var select, rs, ch = 0,DateF=0, OldDate:date;

      var Account, Name, Number, CurAmount, RubAmount, 
      ExecDate:date, FIName, FICode, Scale, Point, IsInverse, Rate, BankFunds;

      var SumCur = 0, SumRub = 0, SumDayCur = 0,SumDayRub = 0;

      select = "select pmpaym.t_PayerAccount, pmpaym.t_ReceiverAccount, pmrmprop.t_PayerName, pmrmprop.T_NUMBER,"+
                     " ps_bcexe.t_SatisfiedAmount, ps_bcexe.t_WriteOffAmount,"+
                     " ps_bcexe.t_ExecDate, fininstr.T_NAME, fininstr.T_FI_CODE,"+
                     " pmpaym.t_Scale, pmpaym.t_Point, ps_bcexe.t_RateInv,"+ 
                     " pmpaym.t_Rate,"+
                     " ps_bcord.T_BANKFUNDS,"+
                     " psbcexch.t_RateScale, psbcexch.t_RatePoint, ps_bcexe.t_RateInv,"+ 
                     " psbcexch.t_Rate"+
               " from dps_bcord_dbt ps_bcord,"+
                    " dpmpaym_dbt pmpaym,"+
                    " dpmrmprop_dbt pmrmprop,"+
                    " dps_bcexe_dbt ps_bcexe,"+
                    " dpsbcexch_dbt psbcexch,"+
                    " dfininstr_dbt fininstr"+
               " where pmpaym.T_PAYMENTID = ps_bcord.T_PAYMENTID"+
                " and pmrmprop.T_PAYMENTID = ps_bcord.T_PAYMENTID"+
                " and ps_bcexe.T_PAYMENTID = ps_bcord.T_PAYMENTID"+
                " and psbcexch.t_RequestID(+) = ps_bcexe.t_RequestID"+
                " and ps_bcord.T_BCORDKIND = :DocKind"+
                " and ps_bcexe.t_ExecDate between :Date_In and :Date_Out"+
                " and ps_bcord.T_STATE = 20";

      
      params[params.size] = ( SQLParam( "DocKind"  , DocKind ));
      params[params.size] = ( SQLParam( "Date_In"  , Date_In ));
      params[params.size] = ( SQLParam( "Date_Out" , Date_Out ));

      if( DepNum != -1 )
        select = select + " and pmpaym.T_DEPARTMENT = :DepNum";
        params[params.size] = ( SQLParam( "DepNum"   , DepNum ));
      end;

      if(  FIID != -1  )
        if( DocKind == 1 )
          select = select + 
          " AND pmpaym.T_PAYFIID      = :FIID"+
          " AND fininstr.T_FIID(+) = pmpaym.T_PAYFIID";
          params[params.size] = ( SQLParam( "FIID" , FIID ));
        end;
        if( DocKind == 2 )
          select = select + 
          " AND pmpaym.T_FIID      = :FIID"+
          " AND fininstr.T_FIID(+) = pmpaym.T_FIID";
        params[params.size] = ( SQLParam( "FIID" , FIID ));
      end;
      else
        if( DocKind == 1 )
          select = select + " AND fininstr.T_FIID(+) = pmpaym.T_PAYFIID";
        end;
        if( DocKind == 2 ) 
          select = select + " AND fininstr.T_FIID(+) = pmpaym.T_FIID";
        end;
      end;

      select = select + " ORDER BY ps_bcexe.T_ExecDate";
      if ( DocKind == 1 )
        select = select+",pmpaym.t_ReceiverAccount ASC";
      else
        select = select+",pmpaym.T_PayerAccount  ASC";
      end;

      rs = execSQLselect( select, params, FALSE );      
      
      if( Date_In == Date_Out )
        if( DocKind == 1 ) PrintDayHeader( PSBCKIND_BUY, Date_In ); end;
        if( DocKind == 2 ) PrintDayHeader( PSBCKIND_PAY, Date_In ); end;  
      else
        if( DocKind == 1 ) PrintPerHeader( PSBCKIND_BUY, Date_In, Date_Out );end;
        if( DocKind == 2 ) PrintPerHeader( PSBCKIND_PAY, Date_In, Date_Out );end;
      end;

      PrintExecuteHeader();
       
      if( rs )
        while( rs.MoveNext())
          ch = ch + 1;

          Name           = rs.value(2);
          Number         = rs.value(3);
          if( DocKind == 1)
            Account        = rs.value(1);
            CurAmount      = rs.value(4);
            RubAmount      = rs.value(5);
          end;
          if( DocKind == 2)
            Account        = rs.value(0);
            CurAmount      = rs.value(5);
            RubAmount      = rs.value(4);
          end;
          ExecDate       = rs.value(6);
          FIName         = rs.value(7);
          FICode         = rs.value(8);
          BankFunds      = rs.value(13);

          if( BankFunds == "X")
            IsInverse      = rs.value(11);
          Scale          = rs.value(9);
          Point          = rs.value(10);
            if (Scale == 0)
              Rate = 0;
            else
              if (IsInverse !="X")  
                Rate           = (rs.value(12) / Scale )/pow( 10, Point );
              else
                Rate           =  Scale / (rs.value(12) /pow( 10, Point ));
              end;
            end;
          else
            IsInverse      = rs.value(16);
            Scale          = rs.value(14);
            Point          = rs.value(15);
            if (Scale == 0)
              Rate = 0;
            else
              if (IsInverse !="X")
                Rate           = (rs.value(17) / Scale )/pow( 10, Point );
              else
                Rate           =  Scale / (rs.value(17) /pow( 10, Point ));
              end;
            end;
          end;
          
          if( ch == 1 )
            if( FIID != -1 )
              PrintBPNewCurrency( FICode, FIName );
            else
              PrintBPNewCurrency( "", "по всем валютам" );
            end;
          end;          

          if( (Date_In != Date_Out) and (ExecDate!=OldDate) and (DateF))
            PrintBPDayItog( SumDayCur, SumDayRub ); 
            SumDayCur=0;
            SumDayRub=0;
          else
            DateF=1;
          end;
                  
          OldDate=ExecDate;
           
          
          if( DocKind == 1 )
            PrintBDocument( ch, Account, Name, Number, CurAmount, RubAmount, Rate, ExecDate);
          end;
          
          if( DocKind == 2 )
            PrintPDocument( ch, Account, Name, Number, CurAmount, RubAmount, Rate, ExecDate);
          end;
          
          SumCur = SumCur + CurAmount;
          SumRub = SumRub + RubAmount;
          SumDayCur = SumDayCur + CurAmount;
          SumDayRub = SumDayRub + RubAmount;
                  
        end;
      end;
      
      if( ch == 0 )
        PrintBPNewCurrency( 0, "", "" );
      end;

      if( Date_In != Date_Out )
        PrintBPDayItog( SumDayCur, SumDayRub );
        PrintBPPerItog( SumCur, SumRub );
      else
        PrintBPPerItog( SumCur, SumRub );
      end;
end;



macro PrCReq( Date_In, Date_Out, FIID, DepNum, Close_Cond )
      var params:TArray = TArray();
      var select, rs, ch = 0;

      var Account, Name, Number, CurAmount, CurFIName, CurFICode, 
          RubAmount, FIName, FICode, Rate, OrderDate:date,
          Scale, Point, IsInverse;

      var SumCur = 0, SumDayCur=0, DateF=0,OldDate:date;
      
      select = "select pmpaym.t_PayerAccount, pmrmprop.t_PayerName, pmrmprop.T_NUMBER,"+
                     " pmpaym.t_Amount, fininstr.T_NAME, fininstr.T_FI_CODE,"+
                     " pmpaym.t_PayAmount, fininstrPay.T_NAME, fininstrPay.T_FI_CODE,"+
                     " pmpaym.t_Rate,  pmpaym.T_VALUEDATE,  pmpaym.t_Scale,"+
                     " pmpaym.t_Point, pmpaym.t_IsInverse"+ 
               " from dps_bcord_dbt ps_bcord,"+
                    " dpmpaym_dbt pmpaym,"+
                    " dpmrmprop_dbt pmrmprop,"+
                    " dfininstr_dbt fininstr,"+
                    " dfininstr_dbt fininstrPay"+
               " where pmpaym.T_PAYMENTID = ps_bcord.T_PAYMENTID"+
                " and pmrmprop.T_PAYMENTID = ps_bcord.T_PAYMENTID"+
                " and ps_bcord.T_BCORDKIND = 3"+
                " and pmpaym.T_VALUEDATE between :Date_In and :Date_Out"+
                " and fininstr.T_FIID(+) = pmpaym.T_FIID"+
                " and fininstrPay.T_FIID(+) = pmpaym.T_PAYFIID";
      
      if( Close_Cond )
        select = select + " and ps_bcord.T_STATE = 20";
      else
        select = select + " and (ps_bcord.T_STATE = 10 or ps_bcord.T_STATE = 20)";
      end;        
      
      params[params.size] = ( SQLParam( "Date_In"  , Date_In ));
      params[params.size] = ( SQLParam( "Date_Out" , Date_Out ));

      if( DepNum != -1 )
        select = select + " and pmpaym.T_DEPARTMENT = :DepNum";
        params[params.size] = ( SQLParam( "DepNum"   , DepNum ));
      end;

      if(  FIID != -1  )        
          select = select + " AND pmpaym.T_FIID = :FIID";          
          params[params.size] = ( SQLParam( "FIID" , FIID ));
      end;

      select = select + " ORDER BY pmpaym.T_ValueDate,pmpaym.T_PayerAccount  ASC";
            
      rs = execSQLselect( select, params, FALSE );      
      
      if( Date_In == Date_Out )
        PrintDayHeader( PSBCKIND_CONV, Date_In );
      else
        PrintPerHeader( PSBCKIND_CONV, Date_In, Date_Out );
      end;

      PrintRequestHeader();
       
      if( rs )
        while( rs.MoveNext())
          ch = ch + 1;
          Account   = rs.value(0);
          Name      = rs.value(1);
          Number    = rs.value(2);
          CurAmount = rs.value(3);
          CurFIName = rs.value(4);
          CurFICode = rs.value(5);
          RubAmount = rs.value(6);
          FIName    = rs.value(7);
          FICode    = rs.value(8);
          Rate      = rs.value(9);
          OrderDate = rs.value(10);
          Scale     = rs.value(11);
          Point     = rs.value(12);
          IsInverse = rs.value(13);
          
          if (Scale == 0)
            Rate = 0;
          else
            if (IsInverse !="X")
              Rate    =  (Rate / Scale) /pow( 10, Point );
            else
              Rate    =  Scale /( Rate /pow( 10, Point ));
            end;
          end;
          
          if( ch == 1 )
            if( FIID != -1 )
              PrintCNewCurrency( CurFICode, CurFIName );
            else
              PrintCNewCurrency( "", "по всем валютам" );
            end;
          end; 

          if( (Date_In != Date_Out) and (OrderDate!=OldDate) and (DateF))
            PrintCDayItog( SumDayCur ); 
            SumDayCur=0;
          else
            DateF=1;
          end;
                   
          
          OldDate=OrderDate;

          
          PrintCDocument( ch, Account, Name, Number, CurFICode, CurAmount, FICode, RubAmount, Rate, OrderDate );
          SumCur = SumCur + CurAmount;
          SumDayCur = SumDayCur + CurAmount;

          
        end;
      end;
      
      if( ch == 0 )
        PrintCNewCurrency( "", "" );
      end;

      if( Date_In != Date_Out )
        PrintCDayItog( SumDayCur );
        PrintCPerItog( SumCur );
      else
        PrintCPerItog( SumCur );
      end;
end;

macro PrCExe( Date_In, Date_Out, FIID, DepNum, DocKind, Close_Cond )
      var params:TArray = TArray();
      var select, rs, ch = 0;

      var Account, Name, Number, CurAmount, CurFIName, CurFICode, 
          Amount, FIName, FICode, OrderDate:date, Rate, RatePoint,RateScale,RateInv,BankFunds;

      var SumCur = 0, SumDayCur=0,OldDate:date,DateF=0;

      select = "select pmpaym.t_PayerAccount, pmrmprop.t_PayerName, pmrmprop.T_NUMBER,"+
                     " ps_bcexe.t_WriteOffAmount, fininstr.T_NAME, fininstr.T_FI_CODE,"+          
                     " ps_bcexe.t_SatisfiedAmount, fininstrPay.T_NAME, fininstrPay.T_FI_CODE,"+ 
                     " ps_bcexe.t_ExecDate, psbcexch.t_Rate, psbcexch.t_RatePoint,"+
                     " psbcexch.t_RateScale, psbcexch.t_RateInv,"+
                     " pmpaym.t_Rate, pmpaym.t_Point, pmpaym.t_Scale, pmpaym.t_IsInverse, ps_bcord.t_BankFunds "+
               " from dps_bcord_dbt ps_bcord,"+
                    " dpmpaym_dbt pmpaym,"+
                    " dpmrmprop_dbt pmrmprop,"+
                    " dps_bcexe_dbt ps_bcexe,"+
                    " dpsbcexch_dbt psbcexch,"+
                    " dfininstr_dbt fininstr,"+
                    " dfininstr_dbt fininstrPay"+
               " where pmpaym.T_PAYMENTID = ps_bcord.T_PAYMENTID"+
                " and pmrmprop.T_PAYMENTID = ps_bcord.T_PAYMENTID"+
                " and ps_bcexe.T_PAYMENTID = ps_bcord.T_PAYMENTID"+
                " and psbcexch.t_RequestID(+) = ps_bcexe.t_RequestID"+
                " and ps_bcord.T_BCORDKIND = 3"+
                " and ps_bcexe.t_ExecDate between :Date_In and :Date_Out"+
                " and ps_bcord.T_STATE = 20"+
                " and fininstr.T_FIID(+) = pmpaym.T_FIID"+
                " and fininstrPay.T_FIID(+) = pmpaym.T_PAYFIID";      
      
      params[params.size] = ( SQLParam( "Date_In"  , Date_In ));
      params[params.size] = ( SQLParam( "Date_Out" , Date_Out ));

      if( DepNum != -1 )
        select = select + " and pmpaym.T_DEPARTMENT = :DepNum";
        params[params.size] = ( SQLParam( "DepNum"   , DepNum ));
      end;

      if(  FIID != -1  )
        select = select + " AND pmpaym.T_FIID = :FIID";          
        params[params.size] = ( SQLParam( "FIID" , FIID ));
      end;
      
      select = select + " ORDER BY ps_bcexe.T_ExecDate,pmpaym.T_PayerAccount  ASC";
      
      rs = execSQLselect( select, params, FALSE );      
      
      if( Date_In == Date_Out )
        PrintDayHeader( PSBCKIND_CONV, Date_In );
      else
        PrintPerHeader( PSBCKIND_CONV, Date_In, Date_Out );
      end;


      PrintExecuteHeader();

       
      if( rs )
        while( rs.MoveNext())
          ch =ch + 1;

          Account    = rs.value(0);                    
          Name       = rs.value(1);                    
          Number     = rs.value(2);                    
          CurAmount  = rs.value(3);                    
          CurFIName  = rs.value(4);                    
          CurFICode  = rs.value(5);                    
          Amount     = rs.value(6);                    
          FIName     = rs.value(7);                    
          FICode     = rs.value(8);                    
          OrderDate  = rs.value(9);                    
          BankFunds  = rs.value(18);
          
          if( BankFunds == "X")
            RateInv      = rs.value(17);
            RateScale    = rs.value(16);
            RatePoint    = rs.value(15);
            Rate         = rs.value(14); 
            if (RateScale == 0)
              Rate = 0;
            else
              if (RateInv !="X")  
                Rate           = (Rate / RateScale)/pow( 10, RatePoint );
              else
                Rate           = RateScale / (Rate/pow( 10, RatePoint ));
              end;
            end;
          else
            RateInv            = rs.value(13);
          RatePoint  = rs.value(11);
            RateScale  = rs.value(12);
            Rate               = rs.value(10);
            if (RateScale == 0)
              Rate = 0;
          else
              if (RateInv !="X") 
                Rate           = (Rate/ RateScale)/pow( 10, RatePoint );
              else
                Rate           = RateScale / (Rate /pow( 10, RatePoint ));
              end;
            end;
          end;

          if( (Date_In != Date_Out) and (OrderDate!=OldDate) and (DateF))
            PrintCDayItog( SumDayCur ); 
            SumDayCur=0;
          else 
            DateF=1;
          end;         
          
          OldDate=OrderDate;
          
          
          if( ch == 1 )
            if( FIID != -1 )
              PrintCNewCurrency( CurFICode, CurFIName );
            else
              PrintCNewCurrency( "", "по всем валютам" );
            end;
          end;
          PrintCDocument( ch, Account, Name, Number, CurFICode, CurAmount, FICode, Amount, Rate, OrderDate );
          SumCur = SumCur + CurAmount;
          SumDayCur = SumDayCur + CurAmount;

        end;
      end;
      
      if( ch == 0 )
        PrintCNewCurrency( "", "" );
      end;

      if(Date_In != Date_Out)
        PrintCDayItog( SumDayCur );
        PrintCPerItog( SumCur );
      else
        PrintCPerItog( SumCur );
      end;
end;



MACRO PrintBclst ( MapVal:PSRepMap )
  
  
  var Date_In:date   = MapVal.Value( "Date_In"       );
  var Date_Out:date  = MapVal.Value( "Date_Out"      );
  var FIID           = MapVal.Value( "FIID"          );
  var DepNum         = MapVal.Value( "DepNum"        );
  var KindBuy_Cond   = MapVal.Value( "KindBuy_Cond"  );
  var KindPay_Cond   = MapVal.Value( "KindPay_Cond"  );
  var KindConv_Cond  = MapVal.Value( "KindConv_Cond" );
  var Request_Cond   = MapVal.Value( "Request_Cond"  );
  var Exec_Cond      = MapVal.Value( "Exec_Cond"     );
  var Close_Cond     = MapVal.Value( "Close_Cond"    );

  if( KindBuy_Cond )
    if( Request_Cond )
      PrBPReq( Date_In, Date_Out, FIID, DepNum, PSBCKIND_BUY, Close_Cond );     
    end;                                                      
    if( Exec_Cond )                                           
      PrBPExe( Date_In, Date_Out, FIID, DepNum, PSBCKIND_BUY, Close_Cond );
    end;
  end;

  if( KindPay_Cond )
    if( Request_Cond )
      PrBPReq( Date_In, Date_Out, FIID, DepNum, PSBCKIND_PAY, Close_Cond );           
    end;
    if( Exec_Cond )
      PrBPExe( Date_In, Date_Out, FIID, DepNum, PSBCKIND_PAY, Close_Cond );     
    end;
  end;

  if( KindConv_Cond )
    if( Request_Cond )
      PrCReq( Date_In, Date_Out, FIID, DepNum, Close_Cond ); 
    end;
    if( Exec_Cond )
      PrCExe( Date_In, Date_Out, FIID, DepNum, Close_Cond ); 
    end;
  end;
END;

  