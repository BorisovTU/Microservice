/*
 $Name: prreqcl.mac
 $Module: РКО
 $Description: Печатная форма заявления о закрытии счета
 */
import  PSInter,prreqbuf,pmprops,reqinter;

macro DrawReport( ShortNameSelf,
                Typeac,
                dateBegin,
                number,
                БанкОстаткаСредств,
                StrRest,
                FI_Name,
                РАЗРЕШЕНИЕ_ДОЛЖ,
                РАЗРЕШЕНИЕ_ФИО,
                ПРОВЕРКА_ДОЛЖ,
                ПРОВЕРКА_ФИО,
                PartyName,   
                DateClose
               )
  var ФИОРуководителя, ФИОБухгалтера, Остаток;
  var Name;
  var DelegatePost = "";

  if( pr_party.rec.Name )
    Name = pr_party.rec.Name;
  else
    Name = pr_reqclosa.rec.ClientName;
  end;
  
  if( pr_reqclosa.rec.ClientType == PTLEGF_INST )
    DelegatePost = pr_reqclosa.rec.DelegatePost;
    ФИОРуководителя = pr_reqclosa.rec.DelegateName1 + " " +
                    pr_reqclosa.rec.DelegateName2 + " " +
                    pr_reqclosa.rec.DelegateName3;
    ФИОБухгалтера = pr_reqclosa.rec.Name_Book;
  elif( pr_reqclosa.rec.ClientType == PTLEGF_PERSN )
    ФИОРуководителя = pr_reqclosa.rec.ClientName;
    ФИОБухгалтера = pr_reqclosa.rec.ClientName;
  end;


[                                                                                                    ];
[                                            З А Я В Л Е Н И Е                                       ];
[                                            на закрытие счета                                       ];
if({OperDprtNode} != {OperDprt})
[                                                                 В ################################ ](PartyName);
else
[                                                                                                    ];
end;
[                                                                   ################################ ](ShortNameSelf);
[                                                                                                    ];
[                                                                                                    ];      
[ #################################################################################################  ](pr_party.rec.Name);
[ ─────────────────────────────────────────────────────────────────────────────────────────────────  ];                                                                                                                                                                                                                                                             
[                     наименование организации  (полное и точное в соответствии с Уставом)           ]; 
[                                                                                                    ];
[ Просим Вас                                                                                         ];
[ 1. Закрыть наш ############################################# счет № ############################## ](Typeac , pr_reqclosa.rec.Account:f);
[                ─────────────────────────────────────────────                                       ];
[ и считать договор банковского счета № ######### от ################## расторгнутым со дня закрытия счета.](number,dateBegin:m:f);
[                                                                                                    ];
[ 2. Остаток средств (за вычитом комиссии за перевод средств) перечислить на наш счет по следующим реквизитам:];
[ Счет № ######################## в ################################################################ ](pr_reqclosa.rec.RestAccount:f,БанкОстаткаСредств);
[ ################################################################################################## ](StrRest:w);
[                                                                                                    ];
[ 3. Все вновь поступающие денежные средства:                                                        ];
[ ┌───┐                                                                                              ];
[ │ # │ возвращать отправителю                                                                       ]( pr_reqclosa.item("Return") );
[ ├───┤                                                                                              ];
[ │ # │ перечислять на наш счет по следующим реквизитам:                                             ](pr_reqclosa.rec.Transf);
[ └───┘                                                                                              ];
if(pr_reqclosa.rec.Transf == "X")
[  счет № ######################## в ############################################################### ](pr_reqclosa.rec.TransfAccount , БанкОстаткаСредств);
else
[  счет №                          в                                                                 ];
end;
[         ────────────────────────                                                                   ];
[ 4. Принять, выданную денежную чековую книжку с неиспользованными чеками с № ###################### по]( pr_reqclosa.rec.Series + " " + pr_reqclosa.rec.NumberFirst );
[                                                                             ────────────────────── ];
[ № ######################                                                                           ]( pr_reqclosa.rec.Series + " " + pr_reqclosa.rec.NumberLast );
[   ──────────────────────                                                                           ];
[       Претензий к банку не имеем.                                                                  ];
[                                                                                                    ];
[                                                                                                    ];
[ Руководитель: ####################                        ( #################################### ) ](DelegatePost,ФИОРуководителя);
[               ──────────────────── ───────────────────────                                         ];
[                   (должность)             (подпись)                         (ФИО)                  ];
[                                                                                                    ];                      
[                                                                                                    ];
[ Главный бухгалтер                                         ( #################################### ) ]( ФИОБухгалтера );
[                                    ───────────────────────                                         ];
[                                          (подпись)                          (ФИО)                  ];
[                                                                                                    ];                      
[                                                                                                    ];
[ М.П.                                                                                               ];                  
[                                                                          ######################### ](pr_reqclosa.rec.Date:m:f);
[________________________________________________________________________________                    ];
[                                        ОТМЕТКИ БАНКА                                               ];
[                                                                                                    ];
[                               Распоряжение на закрытие счета                                       ];
[  Закрыть                                                                                           ];
if(pr_reqclosa.rec.CurrentState == REQCLOSA_ST_CLOSED)
[  ####################################################################  счет в ###################  ]( Typeac , FI_Name);
else
[                                                                                                    ];
end;
[  ────────────────────────────────────────────────────────────────────         ───────────────────  ];
[ (расчетный, текущий, бюджетный, специальный банковский счет, др. счета)         (валюта счета)     ];                                      
[                                                                                                    ];
if(pr_reqclosa.rec.CurrentState == REQCLOSA_ST_CLOSED)
[ ################################################################################################## ](pr_party.rec.Name);
else
[                                                                                                    ];
end;
[ ────────────────────────────────────────────────────────────────────────────────────────────────── ];
[                 наименование организации  (полное и точное в соответствии с Уставом)               ];
[                                                                                                    ];
[  РАЗРЕШАЮ                                                       Документы на оформление            ];
[ #####################################                           закрытия счета проверил.           ](РАЗРЕШЕНИЕ_ДОЛЖ);
[ #####################################                           Оплата услуг банка по расчетно-    ](PartyName);
[                                                                 кассовому обслуживанию произведена ];
[                                                                 полностью                          ];
[                                                                                                    ];
[                                                                                                    ];
[                                                                                                    ];              
[              ( ###################### ) ##################                ( ##################### )]( РАЗРЕШЕНИЕ_ФИО, ПРОВЕРКА_ДОЛЖ, ПРОВЕРКА_ФИО);
[ ─────────────                                             ────────────────                         ];
[ ###################                                                                                ](DateClose:f:m);

end;

macro PrintDocument(ncopy:integer):bool

  var PartyName,
      ShortNameSelf,
      Typeac,
      dateBegin,
      number,
      БанкОстаткаСредств,
      Rest,
      StrRest,
      FI_Name,
      РАЗРЕШЕНИЕ_ДОЛЖ,
      РАЗРЕШЕНИЕ_ФИО,
      ПРОВЕРКА_ДОЛЖ,
      ПРОВЕРКА_ФИО,
      TR_party:TRecHandler = TRecHandler( "party.dbt"  , "bank.def" ),
      DateClose;

  var БИК, err, errCorr,PartyID, PartyIDCorr,SWIFT,SWIFTCorr,TR_account:TRecHandler;  
  
  TR_account = TRecHandler( "account.dbt"  , "bank.def" );

  TR_account = GetAccount( 1 , pr_reqclosa.rec.Account, pr_reqclosa.rec.Code_Currency);

  Typeac = GetTypeAcc(pr_reqclosa.rec.Code_Currency, TR_account.rec.type_Account);
  
  //Дата закрытия счета
  DateClose = TR_account.rec.Close_Date;
  //Наименование филиала/подразделения
  PartyName = GetNameBranch({OperDprtNode});

  if(GetPartyRec({OurBank},TR_party))
     ShortNameSelf = TR_party.rec.ShortName;
  else
     ShortNameSelf = "";
  end;

  GetNumAndDateContr(pr_reqclosa.rec.Code_Currency, pr_reqclosa.rec.Account,dateBegin,number);
  //Банк для перечисления остатка средств

  if( pr_reqclosa.rec.Code_Currency == 0 )
    PartyID = ПолучитьКодСубъекта(pr_reqclosa.rec.RestBankCode, pr_reqclosa.rec.RestBankCodeKind, err);
    if(not err)
        err = GetPartyCodeEx( PartyID, 3, @БИК );
      if(not err)
        БанкОстаткаСредств = pr_reqclosa.rec.RestBankName + " БИК " + БИК + " к/с " + pr_reqclosa.rec.RestBankCorrAcc;
      else
        БанкОстаткаСредств = "";
      end;
    else
      БанкОстаткаСредств = "";
    end;

  else

    PartyID = ПолучитьКодСубъекта(pr_reqclosa.rec.RestBankCode, pr_reqclosa.rec.RestBankCodeKind, err);
    PartyIDCorr = ПолучитьКодСубъекта(pr_reqclosa.rec.RestCorrCode, pr_reqclosa.rec.RestCorrCodeKind , errCorr);

    if((not err) and (not errCorr))
        err = GetPartyCodeEx( PartyID, 6, @SWIFT );
        errCorr = GetPartyCodeEx( PartyIDCorr, 6, @SWIFTCorr );
      if((not err) and (not errCorr))
        БанкОстаткаСредств = "Банк посредник: " + SWIFT + " "+ pr_reqclosa.rec.RestCorrName +   
                             "Банк бенефициара:" + SWIFTCorr + " " + pr_reqclosa.rec.RestBankName;
      else
        БанкОстаткаСредств = "";
      end;
    else
      БанкОстаткаСредств = "";
    end;

  end;
     
     //Остаток на счете
  if( pr_reqclosa.rec.Code_Currency == NATCUR )
     Rest = RestA(pr_reqclosa.rec.Account , {curdate});
     StrRest = string(Rest:a) +" ( " + RubToStrAlt(Rest) + " ).";
  else
     Rest = RestAC(pr_reqclosa.rec.Account, pr_reqclosa.rec.Code_Currency, {curdate});
     StrRest = string(Rest:a) +" ( " + CurToStrAlt(Rest, NULL, NULL, GetISOCode(pr_reqclosa.rec.Code_Currency)) + " ).";
  end;

  //Наименование валюты закрываемого  счета
  FI_Name = GetNameCurrencyAcc(pr_reqclosa.rec.Code_Currency);
  
  if( GetRegValForOPENAC( "РАЗРЕШЕНИЕ_ДОЛЖ", V_STRING, РАЗРЕШЕНИЕ_ДОЛЖ ) )
     РАЗРЕШЕНИЕ_ДОЛЖ = "";
  end;

  if( GetRegValForOPENAC( "РАЗРЕШЕНИЕ_ФИО", V_STRING, РАЗРЕШЕНИЕ_ФИО ) )
     РАЗРЕШЕНИЕ_ФИО = "";
  end;

  if( GetRegValForOPENAC( "ПРОВЕРКА_ДОЛЖ", V_STRING, ПРОВЕРКА_ДОЛЖ ) )
     ПРОВЕРКА_ДОЛЖ = "";
  end;

  if( GetRegValForOPENAC( "ПРОВЕРКА_ФИО", V_STRING, ПРОВЕРКА_ФИО ) )
     ПРОВЕРКА_ФИО = "";
  end;
  
  while( ncopy )
    DrawReport( ShortNameSelf,
                Typeac,
                dateBegin,
                number,
                БанкОстаткаСредств,
                StrRest,
                FI_Name,
                РАЗРЕШЕНИЕ_ДОЛЖ,
                РАЗРЕШЕНИЕ_ФИО,
                ПРОВЕРКА_ДОЛЖ,
                ПРОВЕРКА_ФИО,
                PartyName,   
                DateClose
               );
    ncopy = ncopy - 1;
  end;

  return true;

end;
