/*
  $Name:         ps_cert.mac
  $Module:       РКО
  $Description:  Инициализация и проверки ценных бланков
*/
/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 1998              */
/*                                                                      */
/*  Имя файла        : ps_cert.mac                                      */
/*                                                                      */
/*  Описание         : Инициализация и проверки ценных бланков          */
/*  Программист      : Дроздов И.Е.                                     */
/*                                                                      */
/*  Создан           : 08.09.2005                                       */
/*                                                                      */
/************************************************************************/

import globals, CtInter, FIInter, pm_common;

record Certif(certif);
record OldCertif(certif);

// номера полей в панели. нумерация с 1
const fld_FIID          = 1,  
      fld_Series        = 3,
      fld_NumbrFirst    = 4, 
      fld_NumbrLast     = 5, 
      fld_Account       = 6,
      fld_State         = 7;

/* Проверка на числовой номер */
private macro IsDigitNumber( Number )

  var stat = 0, i = 1, ch, DigitString = "0123456789";

      while( (not stat) and (i <= strlen(Number)) )
        ch = SubStr( Number, i, 1 );
        if( not Index( DigitString, ch ))
          stat = 1;
        end;
        i = i + 1;
      end;

  return stat == 0;

end;

macro Новый_Документ( )
  return 0;
end;

// Проверка связанного счета
private macro CheckConnAcc( Account:string, AccountFIID:integer, err:string )

  var Department:integer = 0;
  var Type_Account   :string  = "";
  var FIName:string = ПолучитьИмяФинИн( AccountFIID );

  if( not СчетСуществуетИОткрыт( AccountFIID, Account, 1/*CHAPT1*/, Department, Type_Account ) )
    SetParm( 2, "Счет " + Account + " в валюте " + FIName + " не открыт" );
    return 1;
  elif( Department != {OperDprt} )
    SetParm( 2, "Счет " + Account + " в валюте " + FIName + " принадлежит другому филиалу" );
    return 1;
  elif( StrBrk( Type_Account,"ПНМУ" ) )
    SetParm( 2, "Связанный счет не должен быть счетом покрытия" );
    return 1;
  end;
  return 0;
end;

macro Проверить_Документ( Режим )
  record fi(fininstr); fi.FIID = Certif.FIID;
  record cert(certif); 
  var ObjCat = RsbObjCategories( 22/*OBJTYPE_ARTICLE*/, UniID( fi, 22/*OBJTYPE_ARTICLE*/ ) );
  var err:string = "";

  if( (Режим == 2 ) or (Режим == 3) or (Режим == 8) ) 

    /* Вид бланка содержится в справочнике артикулов с установленным значением категории "Является бланком чековой книжки" = Да*/
    if( not ObjCat.IsAttrPresense( OBJ_PAYMENT_GROUP_COMM, 1, NULL, NULL, false, {curdate} ) )
      MsgBox("Неверно задан вид бланка");
      return fld_FIID; 
    end;

    /* Проверка связанного счета */
    if( Certif.ConnAccount == "" )
      MsgBox("Счет не задан");
      return fld_Account;
    elif( CheckConnAcc( Certif.ConnAccount, Certif.ConnAccountFIID, err ) )
      MsgBox( err );
      return fld_Account;
    elif( PM_IsBankAccount( Certif.ConnAccount, Certif.ConnAccountFIID ) )
      MsgBox( "Счет не является клиентским" );
      return fld_Account;
    end;

    /* Серия задана */
    if( Certif.Series == "" )
      MsgBox("Не задана серия"); 
      return fld_Series;
    end;

    /* Номер задан. Является цифровым */
    if( Certif.NumberFirst == "" )
      MsgBox("Не задан первый номер");
      return fld_NumbrFirst;
    end;

    if( not IsDigitNumber( Certif.NumberFirst ) )
      MsgBox("Номер должен быть цифровым");
      return fld_NumbrFirst;
    end;

    if( Certif.NumberLast == "" )
      MsgBox("Не задан последний номер");
      return fld_NumbrLast;
    end;

    if( not IsDigitNumber( Certif.NumberLast ) )
      MsgBox("Номер должен быть цифровым");
      return fld_NumbrLast;
    end;

    if( int(Certif.NumberLast) < int(Certif.NumberFirst) ) 
      InitError();
      MemoryError( 831 );/* Неверный диапазон номеров сертификатов */
      MsgBox( GetErrMsg() );
      InitError();
      return fld_NumbrFirst;
    end;

    if( ( not FindCERTforCheck( Certif.FIID, Certif.Department, Certif.Series, Certif.NumberFirst, Certif.NumberLast, 
                                Certif.ConnAccount, Certif.ConnAccountFIID, Certif.Status, cert )          ) and 
        ( cert.Autokey != Certif.Autokey ) )
      InitError();
      MemoryError( 10113 );/* Некорректный номер бланка */
      MsgBox( GetErrMsg() );
      InitError();
      return fld_NumbrFirst;
    end;
  end;

  return 0;
end;

macro Функция_Пользователя( Режим:integer )
 /*
 Возможные значения Режим:
  UFN_PANEL_INPUT(1) - функция вызвана из панели ввода объекта;
  UFN_PANEL_EDIT(2) - функция вызвана из панели корректировки объекта;
  UFN_SCROL(3) - функция вызвана из панели скролинга, единичный вызов, любая корректировка объекта запрещена;
  UFN_SCROL_FMASS(4) - функция вызвана из панели скролинга, работа по нескольким записям, вызов до начала обработки, любая корректировка объекта запрещена;
 
// Пример работы: 
 if( Режим == UFN_SCROL )
   return UPDTPAGE;    // Обновить страницу записей и область скролинга
   //return UPDTREC;     // Обновить текущую запись, загрузив из файла
 end;
 */
  return 0;
end;