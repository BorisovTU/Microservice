/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : prcssb.mac                                         */
/*                                                                      */
/*  Описание       : Общие функции для печати приходного и расходного   */
/*                   кассового ордера в форме СБ РФ из РКО.             */
/*                   Перенес из pralcash и переделал под новые реалии.  */
/*                                                                      */
/*  Программист    : Смирнов С.В. SMR PRN                               */
/*                                                                      */
/*  Создан         : 09.10.02                                           */
/*                                                                      */
/************************************************************************/

import globals, PaymInter, FIInter, PTInter, prpmbuff, prcs;

MACRO ПолучитьКурсОрдера(payment:TRecHandler):variant
  var stat   :integer = 0;
  var ratedef:TRecHandler = TRecHandler("ratedef.dbt", "bank.def");
  var rate   :variant;
  stat = ПолучитьКурс( ratedef, NATCUR, payment.rec.FIID );
  if(stat)
    MsgBox("Ошибка при определении курса");
    Exit(1);
  else
    rate = ratedef.rec.Rate / pow( 10, ratedef.rec.Point );
  end;
  return rate;
END;

MACRO ПолучитьСуммуВНациональнойВалюте(payment:TRecHandler):moneyl
  var sumNatCur:moneyl;

  if ( payment.rec.PaymStatus == PM_FINISHED )
     ConvSum( sumNatCur, payment.rec.Amount, payment.rec.ValueDate, payment.rec.FIID );
  else
     ConvSum( sumNatCur, payment.rec.Amount, {curdate},             payment.rec.FIID );
  end;

  return sumNatCur;
END;

MACRO ПолучитьНомерФормыСБ(НомерФормы:integer, НомерЭкземпляра:integer, ВсегоЭкземпляров:integer):string
  var Номер:string = "Форма № ";

  Номер = Номер + string(НомерФормы);
  if(НомерЭкземпляра <= 26)
    Номер = Номер + "-" + StrFor(CodeFor("a") + НомерЭкземпляра - 1);
  end;
  return Номер;
end;

macro GetPrDateCB(_date:date):string
  var dateString = "\"";
  var tmp = string(_date:m:f:l);
  dateString = dateString + SubStr(tmp,1,2) + "\"" + SubStr(tmp,3);
  return dateString;
end;

MACRO PrintCBCashOrderHeader(numorder:string, dateorder:date, rate:variant, 
                             НомерФормы:integer, НомерЭкземпляра:integer, 
                             ВсегоЭкземпляров:integer)

  var НомерФормыЭкземпляра = ПолучитьНомерФормыСБ(НомерФормы, НомерЭкземпляра, ВсегоЭкземпляров);

  [                                                                     #########################](НомерФормыЭкземпляра:r);
  [                                                                                 ######## экз.](НомерЭкземпляра:r);
  [
   ########################################################                         #############
   ________________________________________________________             Курс валюты _____________
     наименование филиала Сбербанка России
  ]({Name_Bank}:c,rate:c);

  [
   ##############################################################################################


   ##############################################################################################


  ](numorder:c,GetPrDateCB(dateorder):c);

END;

MACRO PrintCBCashOrder(ground:string, debitAcc:string, creditAcc:string, sumCur:moneyl, sumNatCur:moneyl, Code_Currency, ВсегоЭкземпляров:integer)
  var i:integer = 0;
  ARRAY СОДЕРЖАНИЕ_ОПЕРАЦИИ, SUM_CURRENCY, SUM_NATCUR;

  var SumString      :string = CurToStrAlt(sumCur,    null, null, GetISOCode(Code_Currency));
  var SumNatCurString:string = CurToStrAlt(sumNatCur, null, null, GetISOCode(NATCUR));

  StrSplit(ground,         СОДЕРЖАНИЕ_ОПЕРАЦИИ, 74);
  StrSplit(SumString,      SUM_CURRENCY,        82);
  StrSplit(SumNatCurString,SUM_NATCUR,          94, 36);

  while(ValType(СОДЕРЖАНИЕ_ОПЕРАЦИИ(i)) != V_UNDEF)
    [                    ##########################################################################]
    (СОДЕРЖАНИЕ_ОПЕРАЦИИ(i));
    i = i + 1;
  end;
  [Содержание операции __________________________________________________________________________
   
   Основание ____________________________________________________________________________________
   
   ┌─────────────────────────┬─────────────────────────┬────────────────┬─────────────────────────┐
   │     Дебет счета №       │    Кредит счета №       │  Сумма валюты  │ Сумма валюты в рублевом │
   │                         │                         │    (цифрами)   │   эквиваленте по курсу  │
   │                         │                         │                │  Банка России (цифрами) │
   ├─────────────────────────┼─────────────────────────┼────────────────┼─────────────────────────┤
   │#########################│#########################│################│#########################│
   └─────────────────────────┴─────────────────────────┴────────────────┴─────────────────────────┘
   
   Приложение на ## листe(ах)
  ](debitAcc:f, creditAcc:f, sumCur:r:a, sumNatCur:r:a, ВсегоЭкземпляров);

  i = 0;
  while(ValType(SUM_CURRENCY(i)) != V_UNDEF)
    [             ##################################################################################]
    (SUM_CURRENCY(i));
    i = i + 1;
  end;
  [Сумма валюты __________________________________________________________________________________

  ];
  [                                                          #####################################]
  (SUM_NATCUR(0));
  [Сумма валюты в рублевом эквиваленте по курсу Банка России _____________________________________];
  i = 1;
  while(ValType(SUM_NATCUR(i)) != V_UNDEF)
    [
     ###############################################################################################
     _______________________________________________________________________________________________]
    (SUM_NATCUR(i));
    i = i + 1;
  end;

END;

