IMPORT globals, PTInter, CTInter, OprInter, PSInter, likepy;
import prpm, SFInter, oralib;

FILE ExpFile () txt write;

PRIVATE VAR REG_REQOPENACC = "PS\\REQOPENACC\\";
PRIVATE VAR REG_OPENAC     = "NOTICEFIID";
PRIVATE VAR REG_CLOSEAC    = "CLOSENOTICEFIID";
PRIVATE VAR REG_CHNGAC     = "CHNGNOTICEFIID";

VAR PS_REQCHNGA = 233; // Фиктивный, токого DocKind'а нет


macro GetRegReqFIID( DocKind, FIID ):integer
   var stat:integer = 0;
   var RegPath:string = "";
   var RegVal;

   if  ( DocKind == PS_REQOPENA  )
     RegPath = REG_REQOPENACC + REG_OPENAC;
   elif( DocKind == PS_REQCLOSEA )
     RegPath = REG_REQOPENACC + REG_CLOSEAC;
   elif( DocKind == PS_REQCHNGA  )
      RegPath = REG_REQOPENACC + REG_CHNGAC;
   end;

   GetRegistryValue( RegPath, V_INTEGER, RegVal, stat );
   
   if( stat == 0 )
     SetParm( 1, RegVal );
   else
     SetParm( 1, -1 );
   end;

   return stat;
end;

// Получить сертификат для счета
private macro GetCertNumAndSeries( FIID:integer, Account:string, Ser:string, Num:string ):integer

  var SerBuf:string = "";
  var select = "SELECT CERT.T_NUMBERFIRST, CERT.T_SERIES"+
              " FROM   DCERTIF_DBT CERT"+
              " WHERE  CERT.T_FIID = :FIID"+
                " AND  CERT.T_CONNACCOUNT = :ACCOUNT"+
                " ORDER BY CERT.T_AUTOKEY DESC";


  var params = makeArray( SQLParam( "FIID"    , FIID    ),
                          SQLParam( "ACCOUNT" , Account ) 
                        );

  var rs = execSQLselect( select, params, FALSE );
  
  if( rs AND rs.moveNext() ) 
    
    SerBuf = rs.value(1);
    
    if( ( strlen( SerBuf ) == 19 ) and 
        ( ( SubStr( SerBuf, 17, 3 ) == "cls" ) or 
          ( SubStr( SerBuf, 17, 3 ) == "opn" ) ) )
        SerBuf = SubStr( SerBuf, 1, 16 );
    end;


    SetParm( 3, rs.value(0) );
    SetParm( 2, SerBuf      );
    return 0;
  end;

  return 1;

ONERROR(x)
  MsgBox( x.Message );
  return -1;

end;

private macro StrIsNumber( Str:string):integer
  
  var len = strlen( Str );
  var Code:integer = 0;
  var i = 1;
  
  while( i <= len )
     Code = CodeFor( SubStr( Str, i ) );
     if( ( Code < 48 ) and ( Code > 57 ) )
       return 1;
     end;
     i = i + 1;
  end;

  return 0;
end;

private macro CheckSeriesAndNumber( Series:string, Number:string ):integer

  if( ( strlen( Series) == 16  ) and
      ( strlen( Number) == 3 ) )
      if( ( StrIsNumber( Series ) == 0 ) and ( StrIsNumber( Number ) == 0 ) )
        return 0;
      end;
  end;
  return 1;
end;

// Возвращает обрезанную до Num, или дополненную до Num строку Str
macro TrimAndFillStr( Str:string, Num:integer ):string
   
   var len:integer = 0; 
   var RetStr:string = Str;

   if( RetStr != "" )
     len = strlen( RetStr );
     
     if( len < Num )
       while( len < Num)
          RetStr = "0" + RetStr;
          len = strlen( RetStr );
       end;
     elif( len > Num )
       RetStr = SubStr( RetStr, 1, Num );
     end;
   end;

   return RetStr;
end;

private macro CreateSeiesAndNumber( Ser:string, Num:string ):integer
   var Series:string = "";
   var Number:string = "";
   var stat:integer = 0;
   var FullRegNom:string = "";
   var RegSer:string = "";
   var RegNum:string = "";
   var CurYear:integer = 0;
   var Refer:string = "";
   
   DateSplit( {curdate}, null, null, CurYear );

   var GG = SubStr( string( CurYear ), 3, 2 );
   
   FullRegNom = ПолучитьКодСубъекта( {OurBank}, PTCK_BANKREGNUM, stat );

   if( stat != 0 )
     msgbox( "У субъекта ", {OurBank}, " не найден код вида ", PTCK_BANKREGNUM );
     return stat;
   end;

   SplitFullINN( FullRegNom, RegSer, RegNum );
   
   if( strlen(RegSer) )
     RegSer = TrimAndFillStr( RegSer, 4 );
   else
     RegSer = "0000";
   end;

   if( strlen(RegNum) )
     RegNum = TrimAndFillStr( RegNum, 4 );
   else
     RegNum = "0000";
   end;
   
   if( GenerateReference( 111, Refer ) != 0 )
     MsgBox( "Ошибка при генерации референса по описателю 111" );
     return 1;
   end;

   Series = "";
   Number = "";
   Series = RegSer + RegNum + GG + Refer;
   Number = "001";

   SetParm( 0, Series );
   SetParm( 1, Number );
   return stat;

end;

private macro FindCheckAndSetSert( DocKind, Account, Ser, Num ):integer

  var stat:integer         =  0;
  var FIID:integer    = -1;
  var Series = "", Number:string = "";

  stat = GetRegReqFIID( DocKind, FIID );

  if( stat == 0 )
    stat = GetCertNumAndSeries( FIID, Account, Series, Number );
  else
    msgbox( "Не заданы настройки ФИ для сообщений МНС" );
    stat = -1;
  end;

  if( stat == 0 )
    if( CheckSeriesAndNumber( Series, Number ) == 0 )
      SetParm( 2, Series );
      SetParm( 3, Number );
    else
      stat = 1;
    end;
  end;

  return stat;

end;

// по счету находит чековую книжку  
// для измения номера счета параметр Account - AccountNew
private MACRO GetMessageNumber( DocKind, Account, AccountOld, Ser, Num ):integer


   var Series = "", Number:string = "";
   var stat:integer = 0;
   var FIID:integer    = -1;

   stat = GetRegReqFIID( DocKind, FIID );

   if( stat == 0 )
  
     if( DocKind == PS_REQCLOSEA )

       stat = FindCheckAndSetSert( PS_REQCLOSEA, Account, Series, Number );

       if( stat == 1 )

         stat = FindCheckAndSetSert( PS_REQCHNGA, Account, Series, Number );

         if( stat == 1 )
            stat = FindCheckAndSetSert( PS_REQOPENA, Account, Series, Number );
         end;

         if( stat == 1 )
           stat = CreateSeiesAndNumber( Series, Number );
         elif( stat == 0 )
           Number = TrimAndFillStr( string( ( int( Number ) + 1 ) ), 3 );
         end;

         if( stat == 0 )
           stat = SetAccNoteNumber( FIID, Account, Series, Number, true );
         end;

       end;

     elif( DocKind == PS_REQCHNGA )

       stat = FindCheckAndSetSert( PS_REQCHNGA, Account, Series, Number );
       
       if( stat == 1 )
         
         stat = FindCheckAndSetSert( PS_REQCHNGA, AccountOld, Series, Number );

         if( stat == 0 )
         
           stat = SetAccNoteNumber( FIID, Account, Series, Number );
           if( stat != 0 )
             stat = -1;
           end;

         elif( stat == 1)

           stat   = FindCheckAndSetSert( PS_REQOPENA, AccountOld, Series, Number );

           if( stat == 1 )
             stat = CreateSeiesAndNumber( Series, Number );
           elif( stat == 0 )
             Number = TrimAndFillStr( string( ( int( Number ) + 1 ) ), 3 );
           end;

           if( stat == 0 )
             stat = SetAccNoteNumber( FIID, Account, Series, Number );
           
             if( stat != 0 )
               stat = -1;
             end;
           end;

           if( stat == 0 )
             stat = SetAccNoteNumber( FIID, AccountOld, Series, Number, true );
           
             if( stat != 0 )
               stat = -1;
             end;
           end;
         
         end;

       end;


     elif( DocKind == PS_REQOPENA )
    
       stat = FindCheckAndSetSert( PS_REQOPENA, Account, Series, Number );
 
       if( stat == 1 )
       
         stat = CreateSeiesAndNumber( Series, Number );
    
         if( stat == 0 )
         stat = SetAccNoteNumber( FIID, Account, Series, Number );
         end;

       end;

     end;

   else
     msgbox( "Не заданы настройки ФИ для сообщений МНС" );
     stat = -1;
   end;

   if( stat == 0 )
     SetParm( 3, Series );
     SetParm( 4, Number );
   end;


  return stat;

END;

CLASS  ExportFile( DocKind:integer, TypeMess:integer, MesSeries:string, MesNumber:string )

  record bankparty ( "party.dbt", "bank.def" );

  var stat:integer = 0;

  var NameExpFile = "SBC";

  var FOpen:bool = false;

  var CodeGNI = "", d = "", m = "", y = "";
  DateSplit( {curdate}, d, m, y );

  macro PutStr( Str:string )
     if( FOpen )
       Insert( ExpFile, Str );
     end;
  end;

  macro OpenFile( )

     var TxtDir:string = "";

     if( NameExpFile != "" )

        GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, TxtDir, stat );
        if(  TxtDir != "" )
          Open( ExpFile, TxtDir + "\\" + NameExpFile );
          FOpen = true;
        else
          msgbox( "Не задано значение настройки BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR");
        end;
     end;
  end;

  macro Destructor()
    if( FOpen )
      Close( ExpFile );
    end;
  end;

  if  ( DocKind == PS_REQOPENA  )
      NameExpFile = NameExpFile + "0";
  elif( DocKind == PS_REQCLOSEA )
      NameExpFile = NameExpFile + "2";
  elif( DocKind == PS_REQCHNGA  )
      NameExpFile = NameExpFile + "1";
  end;

  NameExpFile = NameExpFile + string( TypeMess );

  NameExpFile = NameExpFile + SubStr( {MFO_Bank}, 3, 9 ) + "_";

  if( ПолучитьСубъекта( {OurBank}, bankparty ) == 0 )

    if((bankparty.TaxInstitution != 0) and (bankparty.TaxInstitution != -1))
      
      CodeGNI = ПолучитьКодСубъекта( bankparty.TaxInstitution, PTCK_MNS, stat );
      
      if( not stat )
        NameExpFile = NameExpFile + TrimAndFillStr( CodeGNI, 4 );
        NameExpFile = NameExpFile + string( y:o:4, m:o:2, d:o:2 ) + "_" + MesSeries + "_" + MesNumber + ".txt";

        OpenFile();
      else
        msgbox("Не задан код налогового органа по месту регистрации банка (филиала)");
      end;

    else
      msgbox("Не задан налоговый орган, в котором зарегистрирован банк (филиал)");
      stat = 1;
    end;

  end;



END;

macro GetExpMessNameBank()

  var Name   = "";
  var select = "";
  var rs;

    if( {OperDprt} )
    select = " select decode( dp.t_partyid, 0, '', dp.t_Name ) as Name"+
             " from ddp_dep_dbt dp"+
             " where dp.t_code = " + {OperDprt};

    rs = execSQLselect( select, null, FALSE );
   
    if( rs AND rs.moveNext() ) 
      return string( ", филиал ", rs.value(0) );
    end;

  else
    return {Name_Bank};
  end;

end;

//В середине строки заменяет точки на пробелы
//если точки встречаются в начале или в конце строки, то удаляет их
private macro StrDelPeriod( Str )
  //Заменяем все точки на пробелы
  Str = replace( Str, ".", " " );
  //Удаляем лишние пробелы в начале и в конце
  return Trim( Str );
end;

//Получить краткое название по номеру кода для адреса
private macro GetShortNameCode( Code, ShortName )
  var Tb_admterr = Tbfile("admterr.dbt", "r");
  Tb_admterr.Clear();
  Tb_admterr.rec.NumberPlace = Code;
  if(Tb_admterr.GetEQ())
    SetParm( 1, Tb_admterr.rec.Place);
    return true;
  end;
  SetParm( 1, "");
  return false;
end;

private macro ЗаполнитьРайон(Tb_adress:object)
  var ShortNameType = "";
  if((Tb_adress.rec.CodeDistrict == 9) or //"р-н"
     (Tb_adress.rec.CodeDistrict == 15) or //"волость"
     (Tb_adress.rec.CodeDistrict == 13) or //"с/о"
     (Tb_adress.rec.CodeDistrict == 7) )//"тер."
     GetShortNameCode( Tb_adress.rec.CodeDistrict, ShortNameType );
     return Tb_adress.rec.District + " " + StrDelPeriod( ShortNameType ); 
  end;
  return "";
end;

private macro ЗаполнитьГород(Tb_adress:object)
  var ShortNameType = "";
  if(Tb_adress.rec.CodeProvince == 6) //"г."
    GetShortNameCode( Tb_adress.rec.CodeProvince, ShortNameType );
    return Tb_adress.rec.Province + " " + StrDelPeriod( ShortNameType );
  elif(Tb_adress.rec.CodeDistrict == 6) //"г."
    GetShortNameCode( Tb_adress.rec.CodeDistrict, ShortNameType );
    return Tb_adress.rec.District + " " + StrDelPeriod( ShortNameType );
  elif(Tb_adress.rec.CodePlace == 6) //"г."
    GetShortNameCode( Tb_adress.rec.CodePlace, ShortNameType );
    return Tb_adress.rec.Place + " " + StrDelPeriod( ShortNameType );
  end;
  return "";
end;

private macro ЗаполнитьНаселенныйПункт(Tb_adress:object)
  var ShortNameType = "";
  if(Tb_adress.rec.CodePlace == 6)//"г."
    return "";
  end;
  GetShortNameCode( Tb_adress.rec.CodePlace, ShortNameType );
  return Tb_adress.rec.Place + " " + StrDelPeriod( ShortNameType );
end;

MACRO GetClientAdress( PartyID, Adress:string, BName )

  var Tb_adress = Tbfile("adress.dbt", "r");
  Tb_adress.Clear();
  Tb_adress.rec.PartyID = PartyID;
  Tb_adress.rec.Type = 1; //PTADDR_LEGAL

  var msg = "", ShortNameType = "";

  var индекс = "", 
      КодРегиона = "", 
      район = "", 
      город = "", 
      НаселенныйПункт = "", 
      улица = ""; 

  if(Tb_adress.GetEQ())
    
    if((Tb_adress.rec.Country != "") and (Tb_adress.rec.Country != "RUS"))
      КодРегиона = "00";
    else
      КодРегиона = Tb_adress.rec.Region;
    end;

    if((КодРегиона == "") and (Tb_adress.rec.PostIndex == ""))
      msgbox("В адресе " +  BName +" не указаны обязательные элементы: индекс, код региона");
      return false;
    elif(КодРегиона == "")
      msgbox("В адресе " +  BName +" не указан обязательный элемент: код региона");
      return false;
    elif(Tb_adress.rec.PostIndex == "")
      msgbox("В адресе " +  BName +" не указан обязательный элемент: индекс");
      return false;
    end;     

    индекс = Tb_adress.rec.PostIndex;
    
    if( (КодРегиона == "77") or (КодРегиона == "78"))
      район = "";
      город = "";
      НаселенныйПункт = "";
    else
      район = ЗаполнитьРайон(Tb_adress);
      город = ЗаполнитьГород(Tb_adress);
      НаселенныйПункт = ЗаполнитьНаселенныйПункт(Tb_adress);
    end;


    GetShortNameCode( Tb_adress.rec.CodeStreet, ShortNameType );
    улица = Tb_adress.rec.Street + " " + StrDelPeriod( ShortNameType );

    Adress = индекс +","+ КодРегиона +","+ район +","+ город +","+ НаселенныйПункт +","+ улица +","
            + Tb_adress.rec.House +","+ Tb_adress.rec.NumCorps +","+ Tb_adress.rec.Flat;
    //Преобразуем всю строку в верхний регистр
    Adress = StrUpr(Adress);
    SetParm(1, Adress);
    return true;
  end;

  msgbox("Не найден юридический адрес " + BName);        
  return false;

END;

//Создает ФИО разделенное запятыми
macro TransformFIO(FIO, newFIO)
  var arr:TArray;
  FIO = join( filter( split( FIO, " " ) ), "," );
  arr = split( FIO, "," );
  if( (arr.size > 3) or (arr.size < 2))
    msgbox("Неверное значение настройки PS\REQOPENACC\OPERATION\ИМНС_ФИО");
    return false;
  elif(arr.size == 2)
    FIO = FIO + ",";
  end;
  SetParm(1, FIO);
  return true;
end;

/* Получить полное имя физ.лица */
macro ПолучитьИмяФизЛица( PersonID )

  file persn   ("persn.dbt");
  var Name = "";

  persn.PersonID = PersonID;
  if( GetEQ( persn ) )
    Name = Trim(persn.Name1) + "," + Trim(persn.Name2) + "," + Trim(persn.Name3);
  end;

  return Name;

end;
