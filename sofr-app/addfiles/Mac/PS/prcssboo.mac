/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : prcssboo.mac                                       */
/*                                                                      */
/*  Описание       : Печать расходного кассового ордера в форме СБ РФ   */
/*                   из РКО                                             */
/*                                                                      */
/*  Программист    : Смирнов С.В. SMR PRN                               */
/*                                                                      */
/*  Создан         : 09.10.02                                           */
/*                                                                      */
/************************************************************************/

import prcssb;

/* Печать приходного кассового ордера в форме СБ РФ */
MACRO PrintDocument(ncopy:integer):bool
  if( pr_pscshdoc.rec.IsCurrency != "X" )
    MsgBox("Форма №229 не предназначена для печати рублевых ордеров");
    return FALSE;
  end;

  var DocKind:integer = pr_pmpaym.rec.DocKind;

  if( ( DocKind != 420/*CASH_PS_OUTORDER*/ ) )
    MsgBox("Данный вид документа не поддерживается макросом");
    return FALSE;
  end;
 
  var rate               :variant = ПолучитьКурсОрдера(pr_pmpaym);
  var НомерЭкземпляра    :integer = 1;
  var ВсегоЭкземпляров   :integer = ncopy;
  var НаименованиеКлиента:string  = "";
  var sumNatCur          :moneyl  = ПолучитьСуммуВНациональнойВалюте(pr_pmpaym);

  var i:integer=0;
  var SumString:string = CurToStrAlt(pr_pmpaym.rec.Amount,    null, null, GetISOCode(pr_pmpaym.rec.FIID));
  ARRAY SUM_CURRENCY;
  StrSplit(SumString, SUM_CURRENCY, 50);

  while(ncopy != 0)
    PrintCBCashOrderHeader("РАСХОДНЫЙ КАССОВЫЙ ОРДЕР № " + pr_pmrmprop.rec.Number,
                           pr_pmpaym.rec.ValueDate,
                           rate,
                           229,
                           НомерЭкземпляра,
                           ВсегоЭкземпляров);

    [       #######################################################################################
     Выдать _______________________________________________________________________________________
                   (Ф.И.О получателя)
    
    ](pr_pscshdoc.rec.FIOClient:l);

    [######################### № ############### выдан ############################################
     ______________________________________________________________________________________________
     (данные документа, удостоверяющие личность)

    ](GetPaperKindName(pr_pscshdoc.rec.PaperKind), pr_pscshdoc.rec.PaperSeries + " " + pr_pscshdoc.rec.PaperNumber,
    pr_pscshdoc.rec.PaperIssuer + " " + string(pr_pscshdoc.rec.PaperIssuedDate:f));

    PrintCBCashOrder(pr_pmrmprop.rec.Ground,pr_pmpaym.rec.ReceiverAccount,pr_pmpaym.rec.PayerAccount,pr_pmpaym.rec.Amount,sumNatCur,pr_pmpaym.rec.FIID,ВсегоЭкземпляров);

    [

     Указанную валюту в сумме:
    ];
    i = 0;
    while(ValType(SUM_CURRENCY(i)) != V_UNDEF)
      [##################################################
       __________________________________________________](SUM_CURRENCY(i));
      if(i == 0)
        [           (прописью)];
      end;
      i = i + 1;
    end;

    [                                                     Кассир __________________________________
     получил: _________________________
                (подпись получателя)                      Контролер _______________________________

                                                          Главный бухгалтер _______________________
    
    ];


    ncopy = ncopy - 1;
    НомерЭкземпляра = НомерЭкземпляра + 1;
  end;
  
  return TRUE;
end;

