 /*
 $Name: pr_corep.mac
 $Module: АРМ позиционера
 $Description: Протокол контроля по базе данных ВО
 */
 
/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2007              */
/*                                                                      */
/*  Имя файла        : pr_coctr.mac                                     */
/*                                                                      */
/*  Описание         : Протокол контроля по базе данных ВО              */
/*                                                                      */
/*  Создан           : 28.03.2007                                       */
/*                                                                      */
/************************************************************************/

import bcprncom, PSInter, oralib, pm_common, pm_tools;
import prpm, gnd120p;
import "or_rep_h.mac", "BnkTemplReport.mac";

/* получить значение из результата запроса */
private macro getValue( rs : RsdRecordset, fld, nullValue : variant ) : variant

  var val = rs.value(fld);
  
  if( val == nullVal )

    if( nullValue != null ) val = nullValue;
    else                    val = "";
    end;

  end;
  
  return val;

onerror(x)
  msgbox("Ошибка при чтении поля " + fld);
end;

macro GetRegDate( ID )
 
  var rs:object;
  var select:string;
  var d:date;
  select = " select rg.t_StartDate"+
           " from dobjrgdoc_dbt rg"+
           " where rg.t_ObjectType = 3"+
             " and rg.t_RegDocKind = 2"+
             " and rg.t_ObjectID =" + ID;
  rs = execSQLselect( select, NULL, FALSE );
  if(rs.MoveNext())
    return d = rs.value(0);
  end;
  return "";
end;

private macro GetCoutry( ID, Country:@string )
  
  FILE adr(adress) key 0;
  FILE party(party) key 0;
  var AttrID = 0;
  party.PartyID = ID;
  if( not getEQ(party) )
    return 1;
  end;
  ClearRecord(adr);
  adr.PartyID = ID;
  adr.Type    = 1;
  if( getEQ(adr) )
    if( (adr.Country == "") and (party.NotResident == "X") )
       Country = "997";
    else
       GetMainObjAttr( null, OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 32, AttrID, NULL, NULL, NULL );
       if( AttrID == 1 )
         Country = "998";
       else
         Country = adr.Country;
       end;
    end;
  end;
end;

private macro GetMoneyStringType( sourceStr : @string)
  if ( Index(sourceStr, ".") > 0 )
    sourceStr = StrSubst(sourceStr, ".", ",");
  else
    sourceStr = sourceStr + ",00";
  end;
end; 

class (BnkTemplReport)VOReport()
  var BegDate, EndDate, BankName, OpSumm, OpSummAgr;
  var rs1, rs2, rsRepatr;
  
  macro Create()
      /* Работа с первой таблицей */
    var table:object = RegisterTable("StrTable");
    
    var PayerINN, PayerKPP, NameContr, NameCl, Num = 0, Country = "";
    
    macro addStrTable(a1,a2,a3,a4,a5,a6,a7,a8,a9,a10,a11,a12,a13,a14,a15,a16,a17,a18,a19,a20,a21,a22,a23,a24,a25)
      table.SetValueCell("Nnm", a1);
      table.SetValueCell("OpDate", a2);
      table.SetValueCell("OpSumm", a3);
      table.SetValueCell("Direction", a4);
      table.SetValueCell("OpVCode", a5);
      table.SetValueCell("ContrNum", a6);
      table.SetValueCell("ContrDate", a7);
      table.SetValueCell("UnicNum", a8);
      table.SetValueCell("RegDate", a9);
      table.SetValueCell("CurCodeAgr", a10);
      table.SetValueCell("OpSummAgr", a11);
      table.SetValueCell("NameCl", a12);
      table.SetValueCell("INNCl", a13);
      table.SetValueCell("KPPCl", a14);
      table.SetValueCell("Country", a15);
      table.SetValueCell("AccCl", a16);
      table.SetValueCell("NameContr", a17);
      table.SetValueCell("INNcontr", a18);
      table.SetValueCell("BankName1", a19);
      table.SetValueCell("BankCode", a20);
      table.SetValueCell("BankCountry", a21);
      table.SetValueCell("BankCountryName", a22);
      table.SetValueCell("RelDocFlag", a23);
      table.SetValueCell("Partner", a24);
      table.SetValueCell("PCountry", a25);
      table.AddStr();
    end;
         
    SetValues(  "BegDate"             , BegDate,
                "EndDate"             , EndDate, 
                "BankName"            , BankName
             );
    
    while(rs1.MoveNext())
      if(((strlen(rs1.value("t_ReceiverName"))>0) or (strlen(rs1.value("t_PMCOReceiverName"))>0)) and (strlen(rs1.value("t_ReceiverBankName"))>0) and AddCheckVOForReport(rs1.value("t_PaymentID")))  
        if (strlen(rs1.value("t_PMCOReceiverName"))>0)
          NameContr = rs1.value("t_PMCOReceiverName");
        else
          NameContr = rs1.value("t_ReceiverName");
        end;
        if (strlen(rs1.value("t_PMCOReceiverINN")>0))
          splitINN_KPP( getValue(rs1, "t_PMCOReceiverINN"), PayerINN, PayerKPP );
        else
          splitINN_KPP( getValue(rs1, "t_ReceiverINN"), PayerINN, PayerKPP );
        end;
        if(strlen(rs1.value("t_PMCOPayerName"))>0)
          NameCl = rs1.value("t_PMCOPayerName");
        elif(rs1.value("t_existAddPI") == 1)     
          NameCl = rs1.value("t_PayerBankName");
        else
          NameCl = rs1.value("t_PayerName");
        end;

        Num = Num + 1;
        Country = "";
        GetCoutry( IfThenElse(rs1.value("t_existAddPI") == 1, rs1.value("t_PayerBankID") , rs1.value("t_Payer") ), @Country );
        OpSumm = getValue(rs1, "Amount");
        GetMoneyStringType(@OpSumm);
        OpSummAgr = getValue(rs1, "ContractSum");
        GetMoneyStringType(@OpSummAgr);
        
        addStrTable( Num, 
                       getValue(rs1, "t_ValueDate")/*OpDate*/, 
                       OpSumm /*OpSumm*/, 
                       2          /*Direction*/, 
                       getValue(rs1, "t_VO_CodeStr")/*OpVCode*/, 
                       getValue(rs1, "t_ContractNumber")/*ContNum*/,
                       getValue(rs1, "t_ContractDate")/*ContDate*/, 
                       getValue(rs1, "t_ContractUIN")/*UnicNum*/,
                       getValue(rs1, "t_ContractRegDate")/*RegDate*/,
                       getValue(rs1, "t_FI_Code")/*CurCodeAgr*/,
                       OpSummAgr /*OpSummAgr*/,
                       NameCl /*NameCl*/,
                       PayerINN /*INNCl*/,
                       PayerKPP /*KPPCl*/, 
                       Country    /*Country*/,
                       getValue(rs1, "t_Account")/*AccCl*/,
                       NameContr/*NameContr*/, 
                       getValue(rs1, "t_ReceiverINN")/*INNcontr*/, 
                       getValue(rs1, "t_ReceiverBankName")/*BankContrName*/, 
                       getValue(rs1, "t_ReceiverBankCode")/*BankContrCode*/,
                       getValue(rs1, "t_CountryCodeNum3")/*BankCountryCode*/, 
                       getValue(rs1, "t_CountryName")/*BankCountryName*/,
                       getValue(rs1, "t_LnkDocs")/*RelDocFlag*/,
                       getValue(rs1, "t_ContragentName")/*Partner*/,
                       getValue(rs1, "t_ContragentCountry")/*PCountry*/); 
      end;
    end;
    
    while(rs2.MoveNext())
      if(((strlen(rs2.value("t_PayerName"))>0) or (strlen(rs2.value("t_PMCOPayerName"))>0))  and (strlen(rs2.value("t_PayerBankName" ))>0) and AddCheckVOForReport(rs2.value("t_PaymentID")))  
        if (strlen(rs2.value("t_PMCOPayerName"))>0)
          NameContr = rs2.value("t_PMCOPayerName");
        else
          NameContr = rs2.value( "t_PayerName");
        end;
        if (strlen(rs2.value("t_PMCOPayerINN")>0))
          splitINN_KPP( getValue(rs2, "t_PMCOPayerINN"), @PayerINN, @PayerKPP );
        else                       
          splitINN_KPP( getValue(rs2, "t_PayerINN"), @PayerINN, @PayerKPP );
        end;
        if(strlen(rs2.value("t_PMCOReceiverName"))>0)
          NameCl = rs2.value("t_PMCOReceiverName");
        elif(rs2.value("t_existAddPI") == 1)     
          NameCl = rs2.value("t_ReceiverBankName");
        else
          NameCl = rs2.value("t_ReceiverName");
        end;
      
        Num = Num + 1;
        Country = "";
        GetCoutry( IfThenElse(rs2.value("t_existAddPI") == 1, rs2.value("t_ReceiverBankID") , rs2.value("t_Receiver") ), @Country );
        OpSumm = getValue(rs2, "Amount");
        GetMoneyStringType(@OpSumm);
        OpSummAgr = getValue(rs2, "ContractSum");
        GetMoneyStringType(@OpSummAgr);
        
        addStrTable( Num, 
                       getValue(rs2,  "t_ValueDate")/*OpDate*/, 
                       OpSumm /*OpSumm*/, 
                       1          /*Direction*/, 
                       getValue(rs2, "t_VO_CodeStr")/*OpVCode*/, 
                       getValue(rs2, "t_ContractNumber")/*ContNum*/,
                       getValue(rs2, "t_ContractDate")/*ContDate*/, 
                       getValue(rs2, "t_ContractUIN")/*UnicNum*/,
                       getValue(rs2, "t_ContractRegDate")/*RegDate*/,
                       getValue(rs2, "t_FI_Code")/*CurCodeAgr*/,
                       OpSummAgr /*OpSummAgr*/,
                       NameCl /*NameCl*/,
                       PayerINN /*INNCl*/,
                       PayerKPP /*KPPCl*/, 
                       Country    /*Country*/,
                       getValue(rs2, "t_Account")/*AccCl*/,
                       NameContr/*NameContr*/, 
                       getValue(rs2, "t_PayerINN")/*INNcontr*/, 
                       getValue(rs2, "t_PayerBankName")/*BankContrName*/, 
                       getValue(rs2, "t_PayerBankCode")/*BankContrCode*/,
                       getValue(rs2, "t_CountryCodeNum3")/*BankCountryCode*/, 
                       getValue(rs2, "t_CountryName")/*BankCountryName*/,
                       getValue(rs2, "t_LnkDocs")/*RelDocFlag*/,
                       getValue(rs2, "t_ContragentName")/*Partner*/,
                       getValue(rs2, "t_ContragentCountry")/*PCountry*/); 
      end;
    end;
    
    table.EndTable();
    
    /*Работа со второй таблицей*/
    var tableRep:object = NULL;
    var numberBeginMergeCell;
    var numberEndMergeCell;
    var lastContract = "";
    var isDelete:bool = true;
    var RepAmount;
    
    macro addStrInRepTable( Contract, repDate, repAmount, typeAmount )
      
      if ( lastContract == "" )
      
        tableRep.SetValueCell( "ContrUniqNum", Contract );
        tableRep.SetValueCell( "RepAmount1", "" );
        tableRep.SetValueCell( "RepAmount2", "" );
        tableRep.SetValueCell( "RepAmount3", "" );
        lastContract = Contract;
        
      elif ( lastContract != Contract )
        
        lastContract = Contract;
        //объединяем ячеки предыдущего контракта
        SetDiapazon(string(numberBeginMergeCell + ":" + numberEndMergeCell));
        Merge();
        
        tableRep.SetValueCell( "ContrUniqNum", Contract );
        
        numberBeginMergeCell = tableRep.GetCurAddress("ContrUniqNum");
        numberEndMergeCell = tableRep.GetCurAddress("ContrUniqNum");//если у контракта одна репатриация
      else
        numberEndMergeCell = tableRep.GetCurAddress("ContrUniqNum");
      end;
      
      tableRep.SetValueCell( "RepDate", repDate );
      if   (typeAmount == 1)
        tableRep.SetValueCell( "RepAmount1", repAmount );
      elif (typeAmount == 2 )
        tableRep.SetValueCell( "RepAmount2", repAmount );
      else
        tableRep.SetValueCell( "RepAmount3", repAmount );
      end;
       
      tableRep.AddStr(); 
      
    end;
        
    SheetChange( "Сроки репатриации", true );
    tableRep = RegisterTable("TableRep");//ContrUniqNum RepDate RepAmount1 RepAmount2 RepAmount3
    
    numberBeginMergeCell = tableRep.GetCurAddress("ContrUniqNum");//A5
    numberEndMergeCell = tableRep.GetCurAddress("ContrUniqNum");
    
    while (rsRepatr.MoveNext())
      
      repAmount = getValue(rsRepatr,"RepAmount");
      GetMoneyStringType(@repAmount);

      addStrInRepTable(getValue(rsRepatr,"ContrUnicNum"),
                       getValue(rsRepatr,"RepDate"),
                       repAmount,
                       getValue(rsRepatr,"RepAmountType"));
      isDelete = false;
      
    end;
    
    if ( isDelete )
      SheetDelete(2);
    else
      SetDiapazon(string(numberBeginMergeCell + ":" + numberEndMergeCell));
      Merge();
      tableRep.EndTable();
    end;    
    
    SheetChange( "База данных ВО", true );
    
  end;
  
  initBnkTemplReport("vo_report.xls");
end;

private const sizeMoney = 20;

MACRO PrintCORep ( MapVal:PSRepMap )
  var BegDate:date     = MapVal.Value( "BegDate"          );
  var EndDate:date     = MapVal.Value( "EndDate"          );
  var FIID             = MapVal.Value( "FIID"             );
  var DepNum           = MapVal.Value( "DepNum"           );
  var Mode             = MapVal.Value( "Mode"             );
  var ClientID         = MapVal.Value( "ClientID"         );
  var BalanceAcc       = MapVal.Value( "BalanceAcc"       );
  var Account          = MapVal.Value( "Account"          );
  var ContrUIN         = MapVal.Value( "ContractUIN"      );
  
  var Nodes:TArray = TArray();
  GetNodeList(DepNum,Mode,Nodes);
  var s = Nodes.size, i = 0;
 
  var params:TArray = TArray();
  var paramsRep:TArray = TArray();
  var queryPay, queryRec; 
  var selectVO, from, where;
  var selectRep, fromRep, whereRep;
  var selectSubRep, fromSubRep, whereSubRep1, whereSubRep2; 

  selectVO = " SELECT pmpaym.t_ValueDate, "+     
                  " CAST(pmco.T_Amount as VARCHAR2(" + sizeMoney + ")) as Amount, "+ 
                  " pmco.t_VO_CodeStr, "+
                  " pmco.t_ContractNumber,"+
                  " pmco.t_ContractDate,"  +
                  " pmco.t_ContractUIN,"+  
                  " pmco.t_ContractRegDate,"+   
                  " fi.t_FI_Code, "+
                  " CAST(pmco.T_ContractSum as VARCHAR2(" + sizeMoney + ")) as ContractSum, "+ 
                  " acc.t_Account,"+
                  " pmrmprop.t_PayerName, "+     
                  " pmrmprop.t_PayerINN , "+ 
                  " pmrmprop.t_ReceiverName, "+ 
                  " pmrmprop.t_ReceiverINN," +
                  " pc.t_CodeNum3 as t_CountryCodeNum3,"+
                  " pc.t_Name as t_CountryName, "+                
                  " pmpaym.t_Payer, "+
                  " pmpaym.t_Receiver, "+
                  " pmpaym.t_PayerBankID, "+
                  " pmpaym.t_ReceiverBankID, "+
                  " pmpaym.t_PaymentID, " +            
                  " pmrmprop.t_ReceiverBankName, "+
                  " curtr.t_LnkDocs, "+
                  " cr.t_BankCode as t_ReceiverBankCode, "+  
                  " pmrmprop.t_PayerBankName, " +
                  " db.t_BankCode as t_PayerBankCode, "+
                  " pmco.t_PayerName as t_PMCOPayerName, "+ 
                  " pmco.t_PayerINN as t_PMCOPayerINN, "+ 
                  " pmco.t_ReceiverName as t_PMCOReceiverName, "+ 
                  " pmco.t_ReceiverINN as  t_PMCOReceiverINN," +     
                  " NVL( ( select 1 from dpmaddpi_dbt pi where pi.t_PaymentID = pmpaym.T_PAYMENTID and pi.t_DebetCredit = :DebetCredit AND ROWNUM = 1 ), 0) as t_existAddPI, " +
                  " pmco.t_ContragentName, " +
                  " pmco.t_ContragentCountry ";
   
  from   = " FROM dpmpaym_dbt pmpaym, dpmrmprop_dbt pmrmprop, dpmco_dbt pmco, "
                " dpmprop_dbt cr, dpmprop_dbt db, daccount_dbt acc, "+    
                " dfininstr_dbt fi, dpmcurtr_dbt curtr, dcountry_dbt pc " ;

  where  = " WHERE pmpaym.T_PAYMENTID = pmrmprop.T_PAYMENTID" +
             " and pmpaym.T_PAYMENTID = pmco.T_PAYMENTID" +
             " and pmpaym.T_PAYMENTID = cr.T_PAYMENTID" +
             " and curtr.t_PaymentID = pmpaym.T_PAYMENTID" +
             " and cr.T_DEBETCREDIT = 1" +
             " and pmpaym.T_PAYMENTID = db.T_PAYMENTID" +
             " and db.T_DEBETCREDIT = 0" +
             " and curtr.t_IsVO = 'X'" +
             " and pmpaym.T_VALUEDATE BETWEEN :BegDate and :EndDate" +
             " and pmpaym.T_PAYMSTATUS = 32000" +
             " and acc.T_Chapter = 1 " +
             " and fi.t_FIID (+) = pmco.t_ContractFIID";
  
  
  selectRep = " SELECT NVL(select1.ContrUnicNum,select2.ContrUnicNum) as ContrUnicNum, " +
                    "NVL(select1.RepDate,select2.RepDate) as RepDate, " +
                    "CAST(NVL(select1.RepAmount,select2.RepAmount) as VARCHAR2(" + sizeMoney + ")) as RepAmount, " +
                    "CAST(NVL(select1.RepAmountType,select2.RepAmountType) as NUMBER(10)) as RepAmountType ";
              
  whereRep = " WHERE select1.ContrUnicNum = select2.ContrUnicNum (+) " +
                  "and select1.RepDate = select2.RepDate (+) " +
                  "and select1.RepAmount = select2.RepAmount (+) " +
                  "and select1.RepAmountType = select2.RepAmountType (+) " +
          " ORDER BY ContrUnicNum ";
                 
  selectSubRep = " SELECT NVL (curcontr.t_ContractUIN, curcontr.t_ContractNumber) as ContrUnicNum, " +
                        "ccrepatr.t_Date as RepDate, " +
                        " ccrepatr.t_Amount as RepAmount, " +
                        "ccrepatr.t_AmountType as RepAmountType ";               
   
  fromSubRep = from + ", dccrepatr_dbt ccrepatr, dcurcontr_dbt curcontr ";               
                 
  where = where + " and acc.t_Branch in(";
  while( i < s )
    if( i != 0 )
      where = where + ", ";
    end;
    where = where + Nodes[i] ;
    i = i + 1;
  end;
  where = where + ")";

  var DebetCreditPos = params.size; // Запомним порядковый номер DebetCredit для изменения его при исполнении второго запроса
  params[DebetCreditPos] = ( SQLParam( "DebetCredit"   , 0 ));

  params[params.size] = ( SQLParam( "BegDate"   , BegDate ));
  paramsRep[paramsRep.size] = ( SQLParam( "BegDate"   , BegDate ));
  params[params.size] = ( SQLParam( "EndDate"   , EndDate ));
  paramsRep[paramsRep.size] = ( SQLParam( "EndDate"   , EndDate ));  
  
  if( FIID >= 0 )                                               
    where = where + " and pmpaym.T_BASEFIID = :FIID";
    params[params.size] = ( SQLParam( "FIID", FIID ));
    paramsRep[paramsRep.size] = ( SQLParam( "FIID", FIID ));
  elif( FIID == -2 )
    where = where + " and pmpaym.T_BASEFIID <> 0";
  end;
  
  queryPay = " and acc.T_Account(+) = pmpaym.T_PayerAccount "+
             " and acc.T_Code_Currency(+) = pmpaym.T_FIID "+
             " and pc.t_CodeNum3(+) = curtr.t_PayerBankCountry";
  queryRec = " and acc.T_Account(+) = pmpaym.T_ReceiverAccount "+
             " and acc.T_Code_Currency(+) = pmpaym.T_PayFIID "+
             " and pc.t_CodeNum3(+) = curtr.t_ReceiverBankCountry";             
  if( Account != "" ) 
    queryPay = queryPay + " and " + ConvertMaskToSQLFormat( Account, "pmpaym.T_PayerAccount" );
    queryRec = queryRec + " and " + ConvertMaskToSQLFormat( Account, "pmpaym.T_ReceiverAccount" );
  end;
  
  if( ContrUIN != "" )
    where = where + " and pmco.T_ContractUIN = :ContractUIN";
    params[params.size] = ( SQLParam( "ContractUIN", ContrUIN ));
    paramsRep[paramsRep.size] = ( SQLParam( "ContractUIN", ContrUIN ));
  end;
  
  if( ClientID >= 0 )
    queryPay = queryPay + " and pmpaym.t_Payer = :ClientID";
    queryRec = queryRec + " and pmpaym.t_Receiver = :ClientID";
    params[params.size] = ( SQLParam( "ClientID", ClientID ));
    paramsRep[paramsRep.size] = ( SQLParam( "ClientID", ClientID ));
  end;
  
  if( BalanceAcc != "" )
    where = where + " and " + ConvertMaskToSQLFormat( BalanceAcc, "acc.T_BALANCE" );
  end;  
  

  var select1 = selectVO + from + where + queryPay;
  var select2 = selectVO + from + where + queryRec; 
  
  
  whereSubRep1 = where + queryPay + " AND ccrepatr.t_ContractID = curcontr.t_ContractID " +
                                     "AND ccrepatr.t_ContractID = pmco.t_ContractID "; 
  whereSubRep2 = where + queryRec + " AND ccrepatr.t_ContractID = curcontr.t_ContractID " +
                                     "AND ccrepatr.t_ContractID = pmco.t_ContractID ";       

  fromRep = " FROM (" + selectSubRep + fromSubRep + whereSubRep1 + ") select1, (" +
                        selectSubRep + fromSubRep + whereSubRep2 + ") select2 ";

  var selectRepMain = selectRep + fromRep + whereRep;
  
  var rs:RsdRecordset = execSQLselect(select1,params,true);
    
  var report = VOReport();
  report.rs1 = rs;
  
  params[DebetCreditPos] = ( SQLParam( "DebetCredit"   , 1 ));
  rs = execSQLselect(select2,params,true);
  report.rs2 = rs;
  
  
  rs = execSQLselect(selectRepMain,paramsRep,true);
  report.rsRepatr = rs;
  
  report.BegDate = BegDate;
  report.EndDate = EndDate;
  report.BankName = {Name_Bank};
  
  report.Print();
END;

