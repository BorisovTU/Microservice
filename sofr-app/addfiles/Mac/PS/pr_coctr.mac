/*
  $Name:        pr_coctr.mac
  $Module:      РКО
  $Description: Протокол контроля по базе данных ВО
*/

/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2007              */
/*                                                                      */
/*  Имя файла        : pr_coctr.mac                                     */
/*                                                                      */
/*  Описание         : Протокол контроля по базе данных ВО              */
/*                                                                      */
/*  Создан           : 28.03.2007                                       */
/*                                                                      */
/*  Изменён          : 29.04.2016                                       */
/************************************************************************/

import bcprncom, PSInter, oralib, pm_common, pm_tools;
import "or_rep_h.mac", "BnkTemplReport.mac";

const ErrContract = "Не заполнены реквизиты контракта: ";

macro ControlVO( PaymentID, ErrStr:@string)
  
  var PaymentObj:RsbPayment = RsbPayment(PaymentID);
  var PmCOObj:RsbPmCO = PaymentObj.PmCO;
  var pmco:TRecHandler = TRecHandler("pmco.dbt", "bank.def");
  var IsNext = 0;

  if(PmCOObj.IsEmpty)
    ErrStr = "Для платежа по валютной операции не определен код вида валютной операции";
    return 1;
  else
    IsNext = PmCOObj.First();
    
    while((IsNext == 0) and (not PmCOObj.Current(pmco)))     
      
      if((pmco.rec.ContractUIN != "") and ( pmco.rec.ContractRegDate != date(0,0,0)))
        ErrStr = ErrContract;
        if(pmco.rec.ContractNumber == "")
          ErrStr = ErrStr + "номер";
        end;
        if(pmco.rec.ContractDate == date(0,0,0))
          if( strlen(ErrStr)>strlen(ErrContract) )
            ErrStr = ErrStr + ","
          end;
          ErrStr = ErrStr + "дата";
        end;
        if(pmco.rec.ContractFIID < 0)
          if( strlen(ErrStr)>strlen(ErrContract) )
            ErrStr = ErrStr + ","
          end;
          ErrStr = ErrStr + "валюта";
        end;
        
        if(strlen(ErrStr)>strlen(ErrContract))
          return 1;
        end;
      end;

      IsNext = PmCOObj.Next();
    end;

  end;

  var IsOK_VO_Code = false;

  AddCheckVOForReport(PaymentID, IsOK_VO_Code);
  
  if(not IsOK_VO_Code) 
    ErrStr = "Код ВО в назначении платежа не соответствует проставленному коду валютной операции";
    return 1;
  end;

  return 0;
end;

class (BnkTemplReport)СontrolVOReport()
  var BegDate, EndDate, SysDate, SysTime, OperCode, OperName,
    OpDate, Summ, CurCode, Acc_N, Acc_N_Ben, Comment;
  var rs;
  
  macro Create()
    var i1 = 0;
    var ErrStr:string;
    var table:object = RegisterTable("StrTable");
    
    macro addStrTable(a1,a2,a3,a4,a5,a6)
      table.SetValueCell("OpDate", a1);
      table.SetValueCell("Summ", a2);
      table.SetValueCell("CurCode", a3);
      table.SetValueCell("Acc_N", a4);
      table.SetValueCell("Acc_N_Ben", a5);
      table.SetValueCell("Comment", a6);
      table.AddStr();
    end;
    
    SetValues(  "BegDate"             , BegDate,
                "EndDate"             , EndDate, 
                "SysDate"             , SysDate,
                "SysTime"             , SysTime,
                "OperCode"            , OperCode,
                "OperName"            , OperName
             );
    
    while( rs.MoveNext() )
      if(ControlVO(rs.value(0), @ErrStr))
        addStrTable(rs.value(1), rs.value(2), rs.value(3), rs.value(5), rs.value(4), ErrStr);
        i1 = i1 + 1;
      end;
    end;
    table.EndTable();
    if (i1 == 0)
      SetVal("ControlOk", "Контроль произведен успешно, ошибок в отчете не обнаружено");
      HideRange("TotalErrors");
      HideRange("HeaderTable");
    else
      SetVal("ErrQuantity", i1);
    end;
  end;
  
  initBnkTemplReport("сontrol_vo.xls");
end;

MACRO PrintCOCtrlRep ( MapVal:PSRepMap )
  var BegDate:date     = MapVal.Value( "BegDate" );
  var EndDate:date     = MapVal.Value( "EndDate" );
  var FIID             = MapVal.Value( "FIID"    );
  var DepNum           = MapVal.Value( "DepNum"  );
  var Mode             = MapVal.Value( "Mode"    );
  var ErrStr:string;
  var params:TArray = TArray();  
  var select = " select DISTINCT paym.t_PaymentID, to_char(paym.t_ValueDate, 'DD.MM.YYYY'), paym.t_BaseAmount, fi.t_FI_Code, paym.t_ReceiverAccount, paym.T_PAYERACCOUNT";
  var from   = " from dpmpaym_dbt paym, dpmrmprop_dbt rm, dpmcurtr_dbt curtr, daccount_dbt acc, dfininstr_dbt fi";
  var where  = " where paym.T_PaymentID = rm.T_PaymentID"+
                 " and paym.T_PaymentID = curtr.T_PaymentID"+
                 " and curtr.t_IsVO = 'X'"+
                 " and paym.t_valuedate between :BegDate and :EndDate"+
                 " and paym.t_paymstatus = 32000"+
                 " and fi.t_FIID = paym.t_BaseFIID"+
                 " and ((acc.t_account = paym.t_ReceiverAccount and acc.t_Code_Currency = paym.t_payfiid)"+
                   " or (acc.t_account = paym.t_PayerAccount and acc.t_Code_Currency = paym.t_FIID))"+
                 " and acc.t_chapter = 1"+
                 " and acc.t_Branch in(";

  var Nodes:TArray = TArray();
  GetNodeList(DepNum,Mode,Nodes);
  var s = Nodes.size, i = 0;
  while( i < s )
    if( i != 0 )
      where = where + ", ";
    end;
    where = where + Nodes[i] ;
    i = i + 1;
  end;
  where = where + ")";

  params[params.size] = ( SQLParam( "BegDate" , BegDate ));
  params[params.size] = ( SQLParam( "EndDate" , EndDate  ));
    
  if( FIID >= 0 )                                               
    where = where + " and paym.T_BASEFIID = :FIID";
    params[params.size] = ( SQLParam( "FIID", FIID ));
  elif( FIID == -2 )
    where = where + " and paym.T_BASEFIID <> 0";
  end;
  select = select + from + where;
  var rs:RsdRecordset = execSQLselect(select,params,true);
  
  var report = СontrolVOReport();
  rs = execSQLselect(select,params,true);
  i = 0;
  report.BegDate = BegDate;
  report.EndDate = EndDate;
  report.SysDate = Date();
  report.SysTime = Time();
  report.OperCode = {Oper};
  report.OperName = {Name_Oper};
  report.rs = rs;
  
  report.Print();
END;


