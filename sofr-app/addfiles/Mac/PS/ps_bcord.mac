/*
  $Name:             ps_bcord.mac
  $Module:           РКО юридических лиц
  $Description:      Инициализация и проверки заявок на продажу/покупку валюты
*/

import BankInter, CTInter, PaymInter, globals, "payconst.mac", "pmchoper.mac","pm_tools.mac", likepy, pm_opr, "pmedit.mac", pmbuff;

/* Режимы функции проверки: */
CONST BOMAC_DELETE    = 1; /* - удаление */
CONST BOMAC_INSERT    = 2; /* - ввод     */
CONST BOMAC_UPDATE    = 3; /* - обновление */

macro Новый_Документ( )
  return 0;
end;

macro Проверить_Документ( Режим )

  var stat     :integer = 0 ,
      note     :string  = "",
      save_note:string  = "",
      ret_val  :bool;
  var fld_Number:TArray = makeArray( 1, 1, 1 );
  var fld_Date:TArray = makeArray( 2, 2, 2 );
  var fld_DateValue:TArray = makeArray( 21, 20, 26 );
  var fld_Amount:TArray = makeArray( 18, 15, 13 );
  var msg_Amount:TArray = makeArray( "Не задана сумма обеспечения", "Не задана сумма валюты", "Не задана сумма продажи" );
  var fld_PayAmount:TArray = makeArray( 15, 18, 14 );
  var msg_PayAmount:TArray = makeArray( "Не задана сумма валюты", "Не задана сумма к зачислению", "Не задана сумма покупки" );
  var fld_Rate:TArray = makeArray( 11, 11, 15 );
  var fld_Exchange:TArray = makeArray( 19, 19, 21 );
  var fld_LimRate:TArray = makeArray( 22, 21, 22 );
  var msg_LimRate:TArray = makeArray( "Поле \"Курс не выше\" должно быть не заполнено",
                                      "Поле \"Курс не ниже\" должно быть не заполнено",
                                      "Поле \"Лимитированный курс\" должно быть не заполнено" );
  var errors:TArray = TArray();

  if( (Режим == BOMAC_INSERT) or (Режим == BOMAC_UPDATE) or (Режим == 8) )
    
    if( StrLen( r_pmrmprop.Number ) == 0 )
      MsgBox("Не задан номер документа");
      return fld_Number[(r_ps_bcord.BCOrdKind-1)];
    end;
    if( r_pmpaym.ValueDate < {curdate} )
      msgbox("Дата валютирования не должна быть|меньше текущей операционной даты");
      return fld_DateValue[(r_ps_bcord.BCOrdKind-1)];
    end;
    if( r_pmpaym.ValueDate < r_pmrmprop.Date )
      msgbox("Некорректно задана дата документа");
      return fld_Date[(r_ps_bcord.BCOrdKind-1)];
    end;

    if( (r_pmpaym.Amount <= 0) AND (r_pmpaym.PayAmount <= 0) )
      if( r_pmpaym.Amount <= 0 )
        msgbox(msg_Amount[(r_ps_bcord.BCOrdKind-1)]);
        return fld_Amount[(r_ps_bcord.BCOrdKind-1)];
      else
        msgbox(msg_PayAmount[(r_ps_bcord.BCOrdKind-1)]);
        return fld_PayAmount[(r_ps_bcord.BCOrdKind-1)];
      end;
    end;
    if( r_ps_bcord.BankFunds == "X" )
      if( r_pmpaym.Rate == 0 )
        msgbox("Не задан курс" );
        return fld_Rate[(r_ps_bcord.BCOrdKind-1)];
      end;
    else
      if( r_ps_bcord.ExchangeID < 0 )
        msgbox("Не задана торговая плoщадка");
        return fld_Exchange[(r_ps_bcord.BCOrdKind-1)];
      end;
      if( (r_pmpaym.Amount > 0) AND (r_pmpaym.PayAmount > 0) AND (r_pmpaym.PayFIID != r_ps_bcord.ExchangeFIID) AND (r_ps_bcord.LimRate != 0) )
        msgbox(msg_LimRate[(r_ps_bcord.BCOrdKind-1)]);
        return fld_LimRate[(r_ps_bcord.BCOrdKind-1)];
      end;
    end;
    
    /*проверка реквизитов валютной операции*/
    stat = PM_CheckCO(r_pmpaym,r_pmrmprop,0,r_credit);
    if( stat ) return stat; end;

    // проверка корректности фактического курса
    if( CheckCorrectRateTypeOnDate( r_pmpaym, errors ) )
      msgbox( errors[0].name );
      return 1;
    end;
  end;

  if( Режим == BOMAC_UPDATE )  /* РЕДАКТИРОВАНИЕ ДОКУМЕНТА */
    /*При редактировании производим проверку важности внесенных изменений */
    /* Константы важности внесенных изменений:           */
    /* CHANG_NOTIMPORTANT   - изменения неважные (сохранение без отката операции)*/
    /* CHANG_IMPORTANT      - изменения важны (сохранение изменений возможно только при откате) */
    /* CHANG_NOTKEEP        - не сохранять изменения */
    /* Если возвращаемое значение  > 0, то это оно интерпритируется как номер поля с ошибочным параметром*/
    /* Если возвращаемое значение  = 0, то cчитается, что проверка прошла успешно
       и сохранение изменений можно производить без отката операции      */
    if (StandartCheckUpdate(TCheckUpdateParam(r_pmpaym, r_credit, NULL, r_pmrmprop), 
        TCheckUpdateParam(r_pmpaym_old, r_credit_old, NULL, r_pmrmprop_old)) == CHANG_NOTKEEP)
      return CHANG_NOTKEEP;
    end;

  elif( Режим == BOMAC_DELETE )
    if(not isDLMRuning())
      if( r_ps_bcord.Origin == PSBCORD_OR_AUTO )
        MsgBox( "Нельзя удалять документы, созданные системой автоматически" );
        return 1;
      elif( r_ps_bcord.Origin == PSBCORD_OR_INRQ )
        msgbox("Документ порожден инкассовым поручением.|Удаление запрещено.");
        return 1;
      /* Документ из Клиент-Банк'а (или БОУРМ) */
      elif( r_ps_bcord.Origin == PSBCORD_OR_CLB )
        save_note = note = readNoteForObject(OBJTYPE_PSBCORDER, makeObjectID(OBJTYPE_PSBCORDER, NULL, r_ps_bcord), NOTEKIND_PSBC_DELETEGROUND);
        ret_val = GetString(note, "Основание для удаления документа, полученного по системе \"Клиент-Банк\"", 68);
        if( ret_val and (save_note != note) )
          stat = addNoteForObject(OBJTYPE_PSBCORDER, makeObjectID(OBJTYPE_PSBCORDER, null, r_ps_bcord), NOTEKIND_PSBC_DELETEGROUND, note);
          if( stat )
            MsgBox("Ошибка добавления примечания к документу");
            return stat;
          end;
        end;
        if( not note )
          MsgBox("Документ получен по системе \"Клиент-Банк\".|Удаление без ввода основания запрещено.");
          return 1;
        end;
      end;
    end;
  elif(Режим == OBJ_AFTEREDIT) // Проверка важности изменений влияющей на необходимость смены опера документа
                               // (изменения в буферах не сохраняются)
    return IsImportantChangeForOperBuyCurOrder(r_pmpaym, r_pmpaym_old, r_pmrmprop, r_pmrmprop_old, r_ps_bcord, r_ps_bcord_old, r_credit, r_credit_old);
  end;

  return stat;
end;

macro    Функция_Пользователя( Режим:integer )
 /*
 Возможные значения Режим:
  UFN_PANEL_INPUT(1) - функция вызвана из панели ввода объекта;
  UFN_PANEL_EDIT(2) - функция вызвана из панели корректировки объекта;
  UFN_SCROL(3) - функция вызвана из панели скролинга, единичный вызов, любая корректировка объекта запрещена;
  UFN_SCROL_FMASS(4) - функция вызвана из панели скролинга, работа по нескольким записям, вызов до начала обработки, любая корректировка объекта запрещена;
 
// Пример работы: 
 if( Режим == UFN_SCROL )
   return UPDTPAGE;    // Обновить страницу записей и область скролинга
   //return UPDTREC;     // Обновить текущую запись, загрузив из файла
 end;
 */ return 0;
end;

/* Установка подсказки для скролингов из макроса */

private const Hint_ByValueDate:string = "/*+FIRST_ROWS LEADING(pmpaym t pmrmprop oproper oprcurst) INDEX(pmpaym dpmpaym_dbt_idx11) USE_NL(pmpaym t pmrmprop oproper oprcurst)*/";
private const Hint_ByCloseDate:string = "/*+FIRST_ROWS LEADING(pmpaym t pmrmprop oproper oprcurst) INDEX(pmpaym dpmpaym_dbt_idx15) USE_NL(pmpaym t pmrmprop oproper oprcurst)*/";
private const Hint_ByStatus   :string = "/*+FIRST_ROWS LEADING(t pmpaym pmrmprop oproper oprcurst) INDEX(t dps_bcord_dbt_idx2) INDEX(pmpaym dpmpaym_dbt_idx0) USE_NL(t pmpaym pmrmprop oproper oprcurst)*/";
private const Hint_ByStep     :string = "/*+FIRST_ROWS LEADING(t oproper ps_bcord pmpaym pmrmprop oprcurst) INDEX(t doprstep_dbt_idx10) INDEX(ps_bcord dps_bcord_dbt_idx0) INDEX(pmpaym dpmpaym_dbt_idx0) USE_NL(t oproper ps_bcord pmpaym pmrmprop oprcurst)*/";
private const Hint_ByRequest  :string = "/*+FIRST_ROWS LEADING(ps_bcexe t pmpaym pmrmprop oproper oprcurst) INDEX(ps_bcexe dps_bcexe_dbt_idx1) INDEX(t dps_bcord_dbt_idx0) INDEX(pmpaym dpmpaym_dbt_idx0) USE_NL(ps_bcexe t pmpaym pmrmprop oproper oprcurst)*/";

MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStates:integer ):string
  /* Возможные значения ScrolStates:
     0 - Все
     1 - Отложенные
     2 - Открытые
     3 - Закрытые
     4 - Отвергнутые
     5 - Подготовленные к шагу
     +1000 - По конкретной биржевой заявке */
  
  /* Все */
  if( ScrolStates == 0 ) 
    return Hint_ByValueDate;
  /* Отложенные, Открытые, Отвергнутые */
  elif( ( ScrolStates == 1 ) or 
        ( ScrolStates == 2 ) or 
        ( ScrolStates == 4 ) )
    return Hint_ByStatus;
  /* Закрытые */
  elif( ScrolStates == 3 )
    return Hint_ByCloseDate;
  /* Подготовленные к шагу */
  elif( ScrolStates == 5 )
    return Hint_ByStep;
  /* По конкретной биржевой заявке */
  elif( ScrolStates >= 1000 )
    return Hint_ByRequest;
  end;

  return DefaultHint;
END;
