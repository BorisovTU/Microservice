/*
$Name:             esid003_10.mac
$Module:           РКО юридических лиц
$Description:      Операция 4004. Шаг <Контроль>
*/

import pm_tools, pm_chkrst;

var PaymentObj:RsbPayment;

private macro PM_GetDenialGround():string

  var Ground:string = "";
  var DialogFlag = TSetDialogFlag(1);
  if( SetReasonReject( Ground ) )
      Ground = "Контроль не выполнен";
  end;    

  return Ground;
end;


private macro ПринятьДокумент():integer

  var stat : integer = 0;
  var DenialGround = "";
  
  // Очистить значение примечания "Причина отказа (возврата)"
  if(not stat)
    stat = PaymentObj.Notes.DelNote(PM_NOTEKIND_DENIALGROUND);
  end;  
  
  if ( GetOprStatus(OPR_CLAIM_DIRECT) == OPR_CL_ST_DIR_IN) 
    if( УстановитьСтатусыПлатежа( OPR_CLAIM_DO, OPR_CL_ST_PREP ) ) 
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
  end;  
  
  if( УстановитьСтатусыПлатежа( OPR_CLAIM_CONTROL, OPR_CL_ST_CONTROLLED ) ) 
    msgbox("Ошибка при установке сегментов статуса экземпляра операции");
    return 1;
  end;
  
  if (PaymentObj.Notes.ReadNote(PM_NOTEKIND_PAYM_ACCEPTUATEDATE, {curdate}) == "") 
    if (PaymentObj.Notes.AddNote(PM_NOTEKIND_PAYM_ACCEPTUATEDATE, {curdate}))
      msgbox( "Ошибка при вставке примечания платежа" );
      return 1;
    end;
  end;
  return 0;
end;

private macro ОтозватьДокумент():integer

  var stat : integer = 0;
  var DenialGround = "";
  
  var DialogFlag = TSetDialogFlag(1);

  DenialGround = GetCachedVar( "CachedDenialGround", "PM_GetDenialGround" );
   
  // Установить примечание "Причина отказа" = сформированное сообщение
  if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, DenialGround ) != 0 )
    msgbox( "Ошибка при вставке примечания платежа" );
    return 1;
  end;
  
  if( УстановитьСтатусыПлатежа( OPR_CLAIM_STATE, OPR_CL_ST_REJECT ) ) 
    msgbox("Ошибка при установке сегментов статуса экземпляра операции");
    return 1;
  end;

  PaymentObj.PaymStatus = PM_REJECTED;

  return 0;
end;


MACRO ExecuteStep()

  if( PM_NeedRejectControl() == true )
    return ОтозватьДокумент();
  else
    return ПринятьДокумент();
  end;
  return 1;
end;