/* -------------------------------------------------------------------- */
/*         Автоматизированная банковская система RS-Bank v6.0           */
/*                 Copyright (c) R-Style Software Lab                   */
/*                                                                      */
/*  Имя файла  : rqchopy.mac                                            */
/*                                                                      */
/*  Описание   : Операция - 3036 - "Изменение номера счета"             */
/*               Шаг      - 50   - "Создание договора для нового        */
/*                                  транзитного счета"                  */
/*                                                                      */
/*  Программист: Андреева Е.П.                                          */
/*                                                                      */
/*  Создан     : 02.11.2004                                             */
/*                                                                      */
/* -------------------------------------------------------------------- */
IMPORT InsCarryDoc, reqinter, SFInter, PSInter;

private file SfCntrOld( "sfcontr.dbt" );

var ReqChangeAccObj:RsbReqChangeAcc;

/* Существует ли договор обслуживания для данного счета */
macro ExistAndFillSfForAccount( FIID, Account )

  var OldKey, result = false;

  OldKey = KeyNum( SfCntrOld, 1 );

  SfCntrOld.ServKind    = PTSK_PAY;
  SfCntrOld.objectType  = SF_ACCOUNT; 
  SfCntrOld.FIID        = FIID;       
  SfCntrOld.object      = Account; 

  if( getEQ( SfCntrOld ) )
    result = true;
  end;

  keyNum( SfCntrOld, OldKey );

  return result;
end;


/* Выполнение шага */
MACRO ExecuteStep( doc, reqacc )

   var stat:integer  = 0;
   var   existSfOld  = false;
   var   existSfMain = false;
   var   OldSfID     = 0;
   var   sfssidl     = TArray;
   var   SfPlanID    = 0;

   record SfCntr  ( "sfcontr.dbt", "bank.def" );

   var TrAccOldLnk = GetObjLinkAcc( ReqChangeAccObj.OldAccount, ReqChangeAccObj.OldAccountFIID, OBJROLE_ACC_TRANSIT );
   
   if( TrAccOldLnk != "" )
     existSfOld = ExistAndFillSfForAccount( ReqChangeAccObj.OldAccountFIID, TrAccOldLnk );
   end;

   var reqlinka:TRecHandler = TRecHandler( "reqlinka.dbt", "bank.def");
   
   reqlinka = GetReqLinkAcc( ReqChangeAccObj.RequestID, 232 );

   if( reqlinka.rec.RequestID != 0 )

     stat = CheckKey( 1, reqlinka.rec.Account );
  
     ClearRecord( SfCntr );

     /* 1. Найти договор обслуживания для основного счета */
     if( not existSfMain )
       existSfMain = ExistAndFillSfForAccount( ReqChangeAccObj.NewAccountFIID, ReqChangeAccObj.NewAccount );
     end;
     
     if( existSfMain )

       /* 2. Если нашли - скопировать для транзитного */
       Copy( SfCntr, SfCntrOld );
       SfCntr.ID      = 0;               /* Идентификатор     */
       SfCntr.AccCode = "";
       SfCntr.FIID    = reqlinka.rec.Code_Currency; /* Валюта объекта начисления */
       SfCntr.object  = reqlinka.rec.Account; /* Объект начисления */
       OldSfID        = SfCntrOld.ID;
        /* Филиал и ВСП заполняем по транзитному счету */
        FillDepartmentAndBranch( reqlinka.rec.Account, reqlinka.rec.Code_Currency, SfCntr.Department, SfCntr.Branch );
       /* 3. Заполнить СПИ */
       sfssidl[0] = TRecHandler("sfssidl.str", "bank.def" );
       sfssidl[0].rec.BankID    = {OurBank};
       sfssidl[0].rec.Chapter   = 1;
       sfssidl[0].rec.FIID      = ReqChangeAccObj.NewAccountFIID; /* валюта плательщика */
       sfssidl[0].rec.Account   = ReqChangeAccObj.NewAccount;     /* счет плательщика   */
       sfssidl[0].rec.FeeType   = 0;
       sfssidl[0].rec.FeeNumber = 0;

       if( existSfOld )
         SfPlanID = FindSfPlanIDByContract( OldSfID, {curdate} );
       elif( not existSfOld )
         SfPlanID = FindSfPlanIDByClient( SfCntr.partyID, {curdate} );
       end;

       if( not InsertSfContract( SfCntr, ReqChangeAccObj.NewComissGroup, sfssidl, OldSfID, SfPlanID, NULL, 0 ) )
         MsgBox( "Ошибка при открытии договора обслуживания " + ReqChangeAccObj.NewContractNumber );
         return  1;
       end;
     else
       MsgBox( "Не найден договор обслуживания для основного счета" );
       return  1;
     end;
    
    end;

  return 0;

END;
