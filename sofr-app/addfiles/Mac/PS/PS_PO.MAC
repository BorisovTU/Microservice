/*
$Name:             PS_PO.MAC
$Module:           РКО юридических лиц
$Description:      Макрос скроллинга рублевых платежных документов в РКО
*/

import BankInter, PTInter, PSInter, CTInter, PaymInter, globals, frscan, lnpaym, check117, pm_common, pm_chksave,
       "pm_opr.mac", "pmchoper.mac","pm_tools.mac", "pmbuff.mac", "pm_syscont.mac", "pmedit.mac";
import "cb_FillFactura.mac";
import "pmchangeprop.mac";


FILE   fmo_account    ("account.dbt") ;
FILE   fmo_party      ("party.dbt");
FILE   fmo_partcode   ("partcode.dbt");
FILE   fmo_pspaydem   ("pspaydem.dbt");

/*номера полей в панели*/
const fld_Number = 1,
      fld_paym_kind = 3,              /*Вид документа*/
      fld_dem_bankaccr = 16,
      fld_req_bankaccr = 15,
      fld_pay_bankaccr = 18,        /*счет получателя*/
      fld_akkr_bankaccr = 15,
      fld_dem_Ground = 22,          /*основание требования*/
      fld_req_Ground = 20,          /*основание требования-поручения*/
      fld_pay_Ground = 19,          /*основание поручения*/
      fld_csrq_Ground = 12, /*основание Заявление на выдачу наличных денежных средств*/
      fld_akkr_Ground = 22,         /*основание аккредитива*/
      fld_akkr_AccRealReceiver = 23,/*конечный получатель денег по аккредитиву*/
      fld_akkr_PayerBankCode = 13,  /*Код банка плательщика*/
      fld_akkr_Date = 3,            /*Срок действия аккредитива*/
      fld_akkr_Type = 1,            /*Вид аккредитива*/
      fld_akkr_PayCondition = 2,    /*Условие оплаты аккредитива*/

      fld_pay_PayerINN    = 6,  /*инн получателя*/
      fld_pay_ReceiverINN = 16, /*инн плательщика*/
      fld_akkr_PayerINN   = 8,
      fld_akkr_ReceiverINN= 16,
      fld_dem_PayerINN    = 10,
      fld_dem_ReceiverINN = 19,
      fld_req_PayerINN    = 8,
      fld_req_ReceiverINN = 17;
const fld_kzPayerCode    = 7,
      fld_kzReceiverCode = 18;

const CASH_DEFERRED = 1700,
      CASH_READY = 1750;

private const strContinueEdit = "Продолжить редактирование", ContinueEdit = 0;
private const strSaveWithoutSymbol = "Сохранить без символа", SaveWithoutSymbol = 1;
/*Для платежей*/

/*Хинт для списков по дате значения*/
private const Hint_ByValueDate:string = "/*+FIRST_ROWS LEADING(pmpaym t pmprop pmrmprop oproper oprcurst) INDEX(pmpaym dpmpaym_dbt_idx11) INDEX(t dpspayord_dbt_idx0) INDEX(pmprop dpmprop_dbt_idx0) USE_NL(pmpaym t pmprop pmrmprop oproper oprcurst)*/";
/*Хинт для списков по дате закрытия*/
private const Hint_ByCloseDate:string = "/*+FIRST_ROWS LEADING(pmpaym t pmprop pmrmprop oproper oprcurst) INDEX(pmpaym dpmpaym_dbt_idx15) INDEX(pmprop dpmprop_dbt_idx0) USE_NL(pmpaym t pmprop pmrmprop oproper oprcurst)*/";
/*Хинт для списков по статусу первички*/
private const Hint_ByStatus   :string = "/*+FIRST_ROWS LEADING(t pmpaym pmprop pmrmprop oproper oprcurst) INDEX(t dpspayord_dbt_idx2) INDEX(pmpaym dpmpaym_dbt_idx0) INDEX(pmprop dpmprop_dbt_idx0) USE_NL(t pmpaym pmprop pmrmprop oproper oprcurst)*/";
/*Хинт для списков по шагу*/
private const Hint_ByStep     :string = "/*+FIRST_ROWS LEADING(t oproper pmpaym pspayord pmprop pmrmprop oprcurst) INDEX(t doprstep_dbt_idx10) INDEX(pspayord dpspayord_dbt_idx0) INDEX(pmpaym dpmpaym_dbt_idx0) INDEX(pmprop dpmprop_dbt_idx0) USE_NL(t oproper pmpaym pspayord pmprop pmrmprop oprcurst)*/";
/*Хинт для списка К1*/
private const Hint_ForK1      :string = "/*+FIRST_ROWS LEADING(t pmpaym pspayord pmprop pmrmprop oproper oprcurst) INDEX(t dpspaydem_dbt_idx1) INDEX(pspayord dpspayord_dbt_idx0) INDEX(pmpaym dpmpaym_dbt_idx0) INDEX(pmprop dpmprop_dbt_idx0) USE_NL(t pmpaym pspayord pmprop pmrmprop oproper oprcurst)*/";
/*Хинт для списка К2*/
private const Hint_ForK2idx0  :string = "/*+FIRST_ROWS LEADING(t) FULL(t)*/";
private const Hint_ForK2idx1  :string = "/*+FIRST_ROWS LEADING(t) INDEX( t dpsinprop_dbt_idx1)*/";
/*Хинт для списка документов КОР*/
private const Hint_ForIWPDocs :string = "/*+FIRST_ROWS LEADING(t pmpaym oproper oprstep pmprop pmrmprop dp_dep pmnd partcode) INDEX(t dpspayord_dbt_idx2) INDEX(pmpaym dpmpaym_dbt_idx0) INDEX(oproper doproper_dbt_idx1) INDEX(oprstep doprstep_dbt_idx0) INDEX(pmprop dpmprop_dbt_idx0) USE_NL(t pmpaym oproper oprstep pmprop pmrmprop dp_dep pmnd partcode)*/";
/*Хинт для списка невыясненных*/
private const Hint_ForUnknown :string = "/*+FIRST_ROWS LEADING(oprstep oproper t pmrmprop rminprop debet credit) INDEX(t doprstep_dbt_idx10) INDEX(oproper doproper_dbt_idx0) INDEX(pmpaym dpmpaym_dbt_idx0) INDEX(rminprop drminprop_dbt_idx8) INDEX(debet dpmprop_dbt_idx0) INDEX(credit dpmprop_dbt_idx0) USE_NL(t oproper pmpaym pmrmprop rminprop debet credit)*/";

/*Для заявлений на выдачу наличных денежных средств*/

/*Хинт для списков по статусу первички*/
private const Hint_ByStatus_ForCashReq   :string = "/*+FIRST_ROWS LEADING(t pmpaym pmrmprop oproper) INDEX(t dpspayord_dbt_idx2) INDEX(pmpaym dpmpaym_dbt_idx0) USE_NL(t pmpaym pmrmprop oproper)*/";
/*Хинт для списков по дате закрытия*/
private const Hint_ByCloseDate_ForCashReq:string = "/*+FIRST_ROWS LEADING(t pspayord pmprop pmrmprop oproper) INDEX(t dpmpaym_dbt_idx15) USE_NL(t pspayord pmprop pmrmprop oproper)*/";
/*Хинт для списков по дате значения*/
private const Hint_ByValueDate_ForCashReq:string = "/*+FIRST_ROWS LEADING(t pspayord pmprop pmrmprop oproper ) INDEX(t dpmpaym_dbt_idx11) INDEX(pspayord dpspayord_dbt_idx0) USE_NL(t pspayord pmprop pmrmprop oproper)*/";
/*Хинт для списков по шагу*/
private const Hint_ByStep_ForCashReq     :string = "/*+FIRST_ROWS LEADING(t oproper pspayord pmpaym pmrmprop) INDEX(t doprstep_dbt_idx10) INDEX(pspayord dpspayord_dbt_idx0) INDEX(pmpaym dpmpaym_dbt_idx0) USE_NL(t oproper pspayord pmpaym pmrmprop)*/";

/* Установка подсказки для скролингов из макроса */
MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStates:integer ):string

  /* Все, Закрытые */
  if( ( ScrolStates == PSSK_ALL ) or ( ScrolStates == PSSK_CLOSED ) )

    var dtflt:PMScrolDateFltr = PMScrolDateFltr();

    if( (GetBankCaseItem >= CASH_DEFERRED) and (GetBankCaseItem <= CASH_READY) )

      if( dtflt.IsSet( DTFL_CLOSEDATE ) )
        return Hint_ByCloseDate_ForCashReq;    
      elif( dtflt.IsSet( DTFL_VALUEDATE ) )
        return Hint_ByValueDate_ForCashReq;
      else
        return Hint_ByStatus_ForCashReq;
      end;

    else

      if( dtflt.IsSet( DTFL_CLOSEDATE ) )
        return Hint_ByCloseDate;
      elif( dtflt.IsSet( DTFL_VALUEDATE ) )
        return Hint_ByValueDate;
      else
        return Hint_ByStatus;
      end;
    
    end;

  /* Отложенные, Открытые, Отвергнутые */
  elif(    ( ScrolStates == PSSK_DEFER )
        or ( ScrolStates == PSSK_OPEN )
        or ( ScrolStates == PSSK_REJECTED_WORK ) )
  
    if( (GetBankCaseItem >= CASH_DEFERRED) and (GetBankCaseItem <= CASH_READY) )
      return Hint_ByStatus_ForCashReq;    
    else
      return Hint_ByStatus;
    end;

  /* Подготовленные к шагу */
  elif( ScrolStates == PSSK_STEP )

    if( (GetBankCaseItem >= CASH_DEFERRED) and (GetBankCaseItem <= CASH_READY) )
      return Hint_ByStep_ForCashReq;    
    else
      return Hint_ByStep;
    end;

  /* Документов из картотеки 1 */
  elif( ScrolStates == PSSK_INDEX_1 )

    return Hint_ForK1;

  /* Документов из картотеки 2 */
  elif( ScrolStates == PSSK_INDEX_2 )
    if( IndexNum == 0 )
      return Hint_ForK2idx0;
     else
      return Hint_ForK2idx1;
    end;
  /* Счета картотеки ОР */
  elif( ScrolStates == 9999 )

    return "/*+FIRST_ROWS*/";

  /* Документы картотеки ОР */
  elif( ScrolStates == PSSK_INDEX_WP )

    return Hint_ForIWPDocs;

  /* Невыясненные */
  elif( ScrolStates == PSSK_INDEX_UNKNOWN )

    return Hint_ForUnknown;

  end;

  return DefaultHint;
END;

/* Просто ищем Party по PartyId*/
macro FindPartyByPartyID( PartyID )
   var retval, OldKey;
   OldKey = KeyNum (0, fmo_party);

   fmo_party.PartyId = PartyId;
   retval = getEQ(fmo_party);

   keyNum (OldKey, fmo_party);
   return retval;
end;

/* Ищем имеем ли мы БИК*/
macro FindBIC ( PartyID )
   var retval, OldKey;
   OldKey = KeyNum (0, fmo_partcode);

   fmo_partcode.PartyId = PartyId;
   fmo_partcode.CodeKind = PTCK_BIC;

   retval = getEQ(fmo_partcode);

   keyNum (OldKey, fmo_partcode);
   return retval;
end;

/* Возвращает код головного банка, если таковой есть, для банка заданного ID*/
macro GetHeadBankCode (BankID)
   if( not FindBIC( BankID ) )
     FindPartyByPartyID(BankID);
     if(fmo_party.Superior != -1 )
       if( FindPartyByPartyID( fmo_party.Superior ) )
         return fmo_party.PartyId;
       else return -1;
       end;
     else return -1;
     end;
   else return -1;
   end;
end;

macro CheckBankID( BankID )
   var stat;
   return 0;
   if ( (BankID == {OurBank}) or (BankID == GetHeadBankCode({OurBank})) )
      /* ищем среди наших счетов */
      fmo_account.Chapter        = 1;
      fmo_account.Account        = r_pmpaym.ReceiverAccount ;
      if ( GetEQ ( fmo_account ) == TRUE )
        if ( RsbGetTrue (false, true, "Проверьте реквизиты получателя. | Указанный счет есть в \"нашем банке\". | Отправить \"внешний\" платеж? ") == false )
           if  ( r_pspayord.DocKind == PSPOKIND_DEMAND ) stat = fld_dem_bankaccr;
           elif( r_pspayord.DocKind == PSPOKIND_REQUEST) stat = fld_req_bankaccr;
           elif( r_pspayord.DocKind == PSPOKIND_ORDER  ) stat = fld_pay_bankaccr;
           elif( r_pspayord.DocKind == PSPOKIND_AKKREDITIV ) stat = fld_akkr_bankaccr;
           end;
           return stat;
        end;
      end;
   end;

   if ( r_pspayord.DocKind == PSPOKIND_AKKREDITIV )
      if ( (BankID == {OurBank}) or (BankID == GetHeadBankCode({OurBank})) )
         /* ищем среди наших счетов */
         fmo_account.Chapter        = 1;
         fmo_account.Account        = r_pmakkr.AccRealReceiver ;
         if ( GetEQ ( fmo_account ) == TRUE )
           if ( RsbGetTrue (false, true, "Проверьте реквизиты получателя. | Указанный счет есть в \"нашем банке\". | Отправить \"внешний\" платеж? ") == false )
                 stat = fld_akkr_AccRealReceiver;
                 return stat;
           end;
         end;
      end;
   end;

   return 0;
end;

macro    Новый_Документ( )
        return 0;
end;

/*проверка на то , что порождён от ИПВС*/
macro  Порожден_ИПВС()
  var rs             :RsdRecordset,
      Query: string = " SELECT * "+
                      " FROM doprdocs_dbt d , doproper_dbt o "+
                      " WHERE d.t_DocKind = : DocKind AND "+
                      "       d.t_DocumentID = : DocumentID AND "+
                      "       d.t_ID_Operation = o.t_ID_Operation AND "+
                      "       o.t_DocKind = : DocKindOper ",
      params: TArray = makeArray( SQLParam( "DocKind"    , r_pmpaym.DocKind    ),
                                  SQLParam( "DocumentID" , string( r_pmpaym.DocumentID:34:o ) ),
                                  SQLParam( "DocKindOper", PS_INRQ           ) );

  rs = execSQLselect( Query, params , true  );
  return rs.MoveNext();

end;

/*проверка на то, что входит в ТС*/
private macro Входит_в_ТС():bool
  var rs : RsdRecordset,
  Query: string = " SELECT * "+
                    " FROM ddp_dep_dbt dp "+
                   " WHERE dp.t_PartyID = :PayerBankID "+
                     " AND dp.t_AccessMode = 1 ",
  params: TArray = makeArray( SQLParam( "PayerBankID", r_pmpaym.PayerBankID ) );
  rs = execSQLselect( Query, params , true  );
  return  rs.MoveNext();
end;
/* Возвращает номер поля ИНН/КПП получателя(если параметр = false) или плательщика*/
MACRO FieldINN(isPayer)
    var field;
    if(isPayer)
        if  ( r_pspayord.DocKind == PSPOKIND_DEMAND )
            field = fld_dem_PayerINN;
        elif( r_pspayord.DocKind == PSPOKIND_REQUEST)
            field =  fld_req_PayerINN;
        elif( r_pspayord.DocKind == PSPOKIND_ORDER  )
            field = fld_pay_PayerINN;
        elif( r_pspayord.DocKind == PSPOKIND_AKKREDITIV )
            field = fld_akkr_PayerINN;
        else
            field = 1;
        end;
    else
        if  ( r_pspayord.DocKind == PSPOKIND_DEMAND )
            field = fld_dem_ReceiverINN;
        elif( r_pspayord.DocKind == PSPOKIND_REQUEST)
            field = fld_req_ReceiverINN;
        elif( r_pspayord.DocKind == PSPOKIND_ORDER  )
            field = fld_pay_ReceiverINN;
        elif( r_pspayord.DocKind == PSPOKIND_AKKREDITIV ) 
            field = fld_akkr_ReceiverINN;
        else
            field = 1;
        end;
    end;
    return field;
END;

// проверка на допустимость расходного кассового символа
private macro PM_CheckCashSymbolCredit( r_pmrmprop )

  var RegVal : string = "",
      RegValCHECK_SYMBOL : integer = 0,
      err    : integer = 0;
  var TypeVal;

  Array Text, Buttons;
  Text(0) = "В документе не указан кассовый символ";
  Buttons( ContinueEdit ) = strContinueEdit;
  Buttons( SaveWithoutSymbol ) = strSaveWithoutSymbol;

  //Проверка разноски по кассовым символам для рублевых ордеров
  TypeVal = GetRegistryValue( "PS\\CASHORDER\\CHECK_SYMBOL", V_INTEGER, RegValCHECK_SYMBOL, err );
  if ( (TypeVal == V_INTEGER) and ( err == 0 ) //настройка определена  
     and ( (RegValCHECK_SYMBOL == 1) or (RegValCHECK_SYMBOL == 2) ) 
     )

    if ( not Trim(r_pmrmprop.CashSymbolCredit) ) //если не задан кассовый символ
      if (RegValCHECK_SYMBOL == 2)
        msgbox( "В документе не указан кассовый символ" );
        return 1;
      else //RegValCHECK_SYMBOL == 1
        if ( ConfWin( Text, Buttons ) == ContinueEdit )
          return 1;
        end;
      end;         
    end;
    
  end;

  // Получить значение настройки "PS\CASHORDER\REQPAYCASH\PermisExpsSymbol"
  TypeVal = GetRegistryValue( "PS\\CASHORDER\\REQPAYCASH\\PERMISEXPSSYMBOL", V_STRING, RegVal, err ); 

  if( (TypeVal == V_STRING) and ( err == 0 ) )
    if ( CompareStrWithMasks( RegVal, Trim(r_pmrmprop.CashSymbolCredit) ) == 0 )
      return 0; // OK
    else
      msgbox( "Недопустимый расходный кассовый символ" );
      return 1;
    end;
  else
    msgbox( "Ошибка при определении настройки: |PS\\CASHORDER\\REQPAYCASH\\PERMISEXPSSYMBOL" );
    return 1;
  end;
             
end;

macro    Проверить_Документ( Режим )

  var stat           :integer = 0 ,
      NeedCheckBankID:integer = 0 ,
      note           :string  = "",
      save_note      :string  = "",
      ret_val        :bool        ,
      res            :integer     ,   
      Sh_Oper        :string      ,
      ErrStr         :string  = "",
      editdem        :integer = 0 ,
      OldDoc         :integer = 0 ;

         /*Проверяем номер для всех документов, в том числе и отложенных.*/
         if ( (Режим == 2) or (Режим == 3) or (Режим == 8) )
              if( needUseKZpm() )
                if( CompareStrWithMasks("10-29&??", r_pmkz.PayerCode) )
                  MsgBox("Некорректное значение КОд");
                  return fld_kzPayerCode;
                end;
                if( CompareStrWithMasks("10-29&??", r_pmkz.ReceiverCode) )
                  MsgBox("Некорректное значение Кбе");
                  return fld_kzReceiverCode;
                end;
              end;

           if( PM_CheckPayments( r_pmpaym, NULL, r_credit, r_pmrmprop, 1 ) )
             return 1;
           end;

           /* Общие проверки по списку */
           stat = PSPO_ScrolMacroCommonChecks( TPanelFields(), r_pmpaym, r_debet, r_credit, r_pmrmprop, r_pmserv, r_pspayord, r_pspaydem );
           if( stat != NOTERROR )
             return stat;
           end;

           // проверка ИНН плательщика и получателя
           if( (not IsUFEBS(r_credit, r_debet)) and CheckINN( r_pmrmprop.PayerINN, ErrStr)  )
             MsgBox( ErrStr + "плательщика" );
             return FieldINN(true);
           elif( (not IsUFEBS(r_credit, r_debet)) and CheckINN( r_pmrmprop.ReceiverINN, ErrStr) )
             MsgBox( ErrStr + "получателя" );
             return FieldINN(false);
           end;
           
           if( StrLen( r_pmrmprop.Number ) == 0 )
             MsgBox("Не задан номер документа");
             return fld_Number;
           end;

           if( not StrIsNumber(r_pmrmprop.Number) )
             MsgBox("Номер документа нечисловой");
             return fld_Number;
           else
              if( int(r_pmrmprop.Number) == 0 )
                 MsgBox("Номер документа не может быть нулевым");
                 return fld_Number;
              end;
           end; 

           if( StrLen( r_pmrmprop.Ground ) == 0 )
             if (r_pspayord.DocKind == PSPOKIND_AKKREDITIV)
                 MsgBox("Не заполнено поле 'Наименование товаров (работ, услуг), № и дата договора, срок отгрузки товаров (выполнения работ, оказания услуг), грузополучатель и место назначения'");
             else
                 MsgBox("Введите Назначение платежа");
             end;
             if  ( r_pspayord.DocKind == PSPOKIND_DEMAND ) stat = fld_dem_Ground;
             elif( r_pspayord.DocKind == PSPOKIND_REQUEST) stat = fld_req_Ground;
             elif( r_pspayord.DocKind == PSPOKIND_ORDER  ) stat = fld_pay_Ground;
             elif( r_pspayord.DocKind == PSPOKIND_AKKREDITIV ) stat = fld_akkr_Ground;
             elif( r_pspayord.DocKind == PSPOKIND_CASH_REQUEST ) stat = fld_csrq_Ground;
             end;
             return stat;
           end;

           if( StrLen( r_pmrmprop.Ground )>210 )                
             if ( RsbGetTrue( False,True,"Основание платежа превышает 210 символов.|Продолжить?")==False )
                if  ( r_pspayord.DocKind == PSPOKIND_DEMAND ) stat = fld_dem_Ground;
                elif( r_pspayord.DocKind == PSPOKIND_REQUEST) stat = fld_req_Ground;
                elif( r_pspayord.DocKind == PSPOKIND_ORDER  ) stat = fld_pay_Ground;
                elif( r_pspayord.DocKind == PSPOKIND_AKKREDITIV ) stat = fld_akkr_Ground;
                elif( r_pspayord.DocKind == PSPOKIND_CASH_REQUEST ) stat = fld_csrq_Ground;
                end;
                return stat;
             end;
           end;

           if( StrLen( r_pmrmprop.PartyInfo)>210-33 )
             if ( RsbGetTrue( False,True,"Информация участнику превышает 177 символов.|Продолжить?")==False )
                return 1;
             end;
           end;

           if( StrLen( r_pmrmprop.ReceiverBankName)>140 )
             if ( RsbGetTrue( False,True,"Наименование банка получателя превышает 140 символов.|Продолжить?")==False )
                return 1;
             end;
           end;

           if( StrLen( r_pmrmprop.PayerBankName)>140 )
             if ( RsbGetTrue( False,True,"Наименование банка плательщика превышает 140 символов.|Продолжить?")==False )
                return 1;
             end;
           end;

           if( r_pspayord.DocKind == PSPOKIND_CASH_REQUEST )
             if( StrLen( r_pmrmprop.ReceiverName)>160 )
               if ( RsbGetTrue( False,True,"Наименование получателя превышает 160 символов.|Продолжить?")==False )
                  return 1;
               end;
             end;

             if( StrLen( r_pmrmprop.PayerName)>160 )
               if ( RsbGetTrue( False,True,"Наименование плательщика превышает 160 символов.|Продолжить?")==False )
                  return 1;
               end;
             end;
           end;

           if ( r_pspayord.DocKind == PSPOKIND_AKKREDITIV )
              
              if ( NOT Входит_в_ТС() )
                 msgbox("Банк плательщика не входит в ТС (on-line)");
                 stat = fld_akkr_PayerBankCode;        
                 return stat;
              end;
              
              if ( (r_pmpaym.FIID != r_pmpaym.PayFIID) or (r_pmpaym.BaseFIID != r_pmpaym.PayFIID) or (r_pmpaym.BaseFIID != NATCUR) )
                 msgbox("Аккредитив может быть только в рублях");
                 return 1;
              end; 
              
              if ( (r_credit.DebetCredit == 0) ) 
                 msgbox("Аккредитив может быть только кредитовым");
                 return 1;
              end;
                 
              if ( r_pmakkr.Date < {curdate} )
                 msgbox("Срок действия аккредитива должен быть больше|либо равен дате операционного дня"); 
                 stat = fld_akkr_Date;
                 return stat;
              end;

              if ( r_pmakkr.Type == "" )
                 msgbox("Не задан вид аккредитива");
                 stat = fld_akkr_Type; 
                 return stat;
              end;                    
           end;

           if ( r_pspayord.DocKind != PSPOKIND_CASH_REQUEST )
             ErrStr = PM_CheckPaymAccounts( r_pmpaym, NULL, r_credit, r_pmrmprop, 1 );  // SCR #106452
           end;

           if( strlen(ErrStr) > 0 )
             msgbox( ErrStr );
             return 1;
           end;

           if( CheckOnSave_117( r_pmpaym, NULL, r_credit, NULL, r_pmrmprop ) )
             return fld_Number;
           end;

           /*  Если код банка равен нашему банку или код банка равен коду нашего головного банка */
           if( strlen(r_credit.BankCode) and
               ((Режим != 3) or ((Режим == 3) and
                                 ((r_credit.BankCode != r_credit_old.BankCode) or
                                  (r_pmpaym.ReceiverAccount != r_pmpaym_old.ReceiverAccount)))))
             NeedCheckBankID = 1;
           end;
           /*платеж внешний*/
           if( NeedCheckBankID and (Режим != 3) and (r_pspayord_old.OrderID == 0) ) 
             stat = CheckBankID( r_pmpaym.ReceiverBankID );
             if( stat ) return stat; end;
           end;

           /*проверка реквизитов валютной операции*/
           stat = PM_CheckCO(r_pmpaym,r_pmrmprop,0,r_credit);
           if( stat ) return stat; end;

           /*проверка на допустимость расходного кассового символа*/
           if ( r_pspayord.DocKind == PSPOKIND_CASH_REQUEST/*Заявление на выдачу наличных*/ )
             stat = PM_CheckCashSymbolCredit( r_pmrmprop );
             if( stat ) return stat; end;
           end;

         end;
          /* Вид "Срочно" разрешен только для платежных поручений */
         if( (r_pmrmprop.PaymentKind == "С"           ) and
             (r_pspayord.DocKind     != PSPOKIND_ORDER) and
             (r_pspayord.DocKind     != PSPOKIND_DEMAND) and
             (r_pspayord.DocKind     != PSPOKIND_REQUEST) )
                 MsgBox( "Недопустимый вид платежа" );
                 return fld_paym_kind;
         end;

         if((Режим == 3) and ( not EditFromHistScrol ))  /* РЕДАКТИРОВАНИЕ ДОКУМЕНТА */
               /*При редактировании производим проверку важности внесенных изменений */
               /* Константы важности внесенных изменений:           */
               /* CHANG_NOTIMPORTANT   - изменения неважные (сохранение без отката операции)*/
               /* CHANG_IMPORTANT      - изменения важны (сохранение изменений возможно только при откате) */
               /* CHANG_NOTKEEP        - не сохранять изменения */
               /* Если возвращаемое значение  > 0, то это оно интерпритируется как номер поля с ошибочным параметром*/
               /* Если возвращаемое значение  = 0, то cчитается, что проверка прошла успешно
                  и сохранение изменений можно производить без отката операции      */
            if (StandartCheckUpdate(TCheckUpdateParam(r_pmpaym, r_debet, r_credit, r_pmrmprop, r_pspaydem, r_pspayord), 
                TCheckUpdateParam(r_pmpaym_old, r_debet_old, r_credit_old, r_pmrmprop_old, r_pspaydem_old, r_pspayord_old)) == CHANG_NOTKEEP)
              return CHANG_NOTKEEP;

            end;
         elif(Режим == 2)  /* ВВОД ДОКУМЕНТА */
           if(r_pspayord.Kind_Operation) /* Вставка документа одновременно с началом операции */
             /*Дата валютирования*/
             /* if(r_pmpaym.ValueDate < {curdate})
               r_pmpaym.ValueDate = {curdate};
             end; */
           end;
         elif(Режим == 1)  /* УДАЛЕНИЕ ДОКУМЕНТА */
          
           if(not isDLMRuning())
             if( /*(r_pspayord.Origin == PSPO_OR_SV) or */(r_pspayord.Origin == PSPO_OR_SF) )
               msgbox("Документ является платой за обслуживание.|Удаление запрещено.");
               stat = 1;
             elif(r_pspayord.Origin == PSPO_OR_PAYEEBANK)
               var numberPrim:integer = 0;
               var datePrim:date = date( 0, 0, 0);
               var select = "select pmrmprop.t_Number, pmrmprop.t_Date " +
                            "  from dpmrmprop_dbt pmrmprop, dpmlink_dbt pmlink " +
                            " where pmlink.t_PurposePayment = :PaymID " +
                            "   and pmlink.t_InitialPayment = pmrmprop.t_PaymentID " +
                            "   and pmlink.t_LinkKind       = :LinkKind "; 
               var params = makeArray( SQLParam( "PaymID" , r_pmpaym.PaymentID ),
                             SQLParam( "LinkKindID" , PMLINK_KIND_ESIDPAYMENT ) );
      
               var rs = execSQLselect( select, params, TRUE );
               if(rs and rs.moveNext())
                 numberPrim = rs.value(0);
                 datePrim = rs.value(1);
               end;             
               msgbox("Платежный документ создан в результате исполнения распоряжения на оплату N " + numberPrim + " от " + datePrim + ". Удаление запрещено. Следует отвергнуть документ и завершить его обработку из отвернутых");
               stat = 1;
             elif(r_pspayord.Origin == PSPO_OR_LOANS)
               if(not Index( "Ц", StrFor(GetIdentProgram())))
                 msgbox("Документ порожден п/с \"Кредитование\".|Удаление запрещено.");
                 stat = 1;
               end;
             elif(r_pspayord.Origin == PSPO_OR_DEPOSIT)
               if(not Index( "Д", StrFor(GetIdentProgram())))
                 msgbox("Документ порожден п/с \"Депозиты\".|Удаление запрещено.");
                 stat = 1;
               end;
             elif(r_pspayord.Origin == PSPO_OR_RETAIL)
               if(not Index( "ВБD", StrFor(GetIdentProgram())))
                 msgbox("Документ порожден п/с \"Обслуж.физ.лиц\".|Удаление запрещено.");
                 stat = 1;
               end;
             /* Документ из Клиент-Банк'а (или БОУРМ) */
             elif(r_pspayord.Origin == PSPO_OR_CLB)
               save_note = note = readNoteForObject(OBJTYPE_PSPAYORD, makeObjectID(OBJTYPE_PSPAYORD, NULL, r_pspayord), NOTEKIND_PSPO_DELETEGROUND);
               ret_val = GetString(note, "Основание для удаления документа, полученного по системе \"Клиент-Банк\"", 68);
               if( ret_val and (save_note != note) )
                 stat = addNoteForObject(OBJTYPE_PSPAYORD, makeObjectID(OBJTYPE_PSPAYORD, null, r_pspayord), NOTEKIND_PSPO_DELETEGROUND, note);
                 if(stat)
                   MsgBox("Ошибка добавления примечания к документу");
                   return stat;
                 end;
               end;
               if( not note )
                 MsgBox("Документ получен по системе \"Клиент-Банк\".|Удаление без ввода основания запрещено.");
                 return 1;
               end;
             end;
           end;

           if(CheckDeletePayment(r_pmpaym.PaymentID))
             return 1;
           end;         
         elif(Режим == 7) /*ПОМЕЩЕНИЕ ДОКУМЕНТА В ОТЛОЖЕННЫЕ, ОТКАТ ОПЕРАЦИИ*/

         elif(Режим == 8)  /*ВВОД В ОТЛОЖЕННЫЕ*/
         
         elif(Режим == OBJ_AFTEREDIT) // Проверка важности изменений влияющей на необходимость смены опера документа
                                      // (изменения в буферах не сохраняются)
           return IsImportantChangeForOperPayOrder(r_pspayord.DocKind, r_pmpaym, r_pmpaym_old, r_pmrmprop, r_pmrmprop_old, r_credit, r_credit_old, 
                                                   r_pmakkr, r_pmakkr_old, r_pspaydem, r_pspaydem_old)
         end;
  
  return stat;

end;

macro    Проверить_Счет_В_Документе( поле ) /*0-счет плательщика, 1-счет получателя*/
        var новое_значение_счета;

        if(поле) новое_значение_счета = r_pmpaym.ReceiverAccount;
        else новое_значение_счета = r_pmpaym.PayerAccount;
        end;
        return новое_значение_счета;
end;

macro    Функция_Пользователя( Режим:integer )
 

 
 /*
 Возможные значения Режим:
  UFN_PANEL_INPUT(1) - функция вызвана из панели ввода объекта;
  UFN_PANEL_EDIT(2) - функция вызвана из панели корректировки объекта;
  UFN_SCROL(3) - функция вызвана из панели скролинга, единичный вызов, любая корректировка объекта запрещена;
  UFN_SCROL_FMASS(4) - функция вызвана из панели скролинга, работа по нескольким записям, вызов до начала обработки, любая корректировка объекта запрещена;
 
// Пример работы: 
 if( Режим == UFN_SCROL )
   return UPDTPAGE;    // Обновить страницу записей и область скролинга
   //return UPDTREC;     // Обновить текущую запись, загрузив из файла
 end;
 */
         
 return  0;
end;

macro    СканироватьДокументы( )

        return FRScan();
end;


macro    ЗапроситьРеквизиты()
  return ChoicePaymentPropertiesFromScrol();
end;

macro    УстановитьРеквизиты( PaymentID )
  return ChangePaymentPropertiesFromScrol( PaymentID );
end;



