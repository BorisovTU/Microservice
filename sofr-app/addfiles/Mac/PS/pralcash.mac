/*
 *  Общие функции для печати кассовых ордеров
 *
 *  04.08.99
 *
 */
import globals, PaymInter, FIInter;

record payment ( pmpaym );
record order   ( pscshdoc );
record symb_rec ( symbcash );
file   symb_file( symbcash ) key 0;
file   fi      ( fininstr );
file   accunt  ( account ) key 0;
file   pt      ( party ) key 0;

var MassCopy = 0;
var ncopy = 0;

CONST КоличествоКопий = 0;

/*CONST NATCUR = 0;*/

CONST ФормаПечати_ПриходныйКассовыйОрдер     = 0;
CONST ФормаПечати_ОбъявлениеНаВзносНаличными = 1;
CONST ФормаПечати_ПриходныйКассовыйОрдерСБ   = 2; /* Приходный кассовый ордер СБ РФ */

CONST ФормаПечати_РасходныйКассовыйОрдер     = 0;
CONST ФормаПечати_РасходныйКассовыйОрдерСБ   = 1; /* Расходный кассовый ордер СБ РФ */

private record RateDef(ratedef);

MACRO ПолучитьСуммуВНациональнойВалюте(payment)
  var sumNatCur;

  if ( payment.PaymStatus == PM_FINISHED )
     ConvSum( sumNatCur , payment.Amount, payment.ValueDate, payment.FIID );
  else
     ConvSum( sumNatCur , payment.Amount, {curdate}, payment.FIID );
  end;

  return sumNatCur;
END;

MACRO ПолучитьКурсОрдера(payment)

 var rate;
 if(ПолучитьКурс( RateDef, NATCUR, payment.FIID ))
   MsgBox("Ошибка при определении курса");
   exit(1);
 end;
 rate = RateDef.Rate / pow( 10, RateDef.Point );

 return rate;

END;

/* Пролучить номер формы кассового ордера по СБ РФ, в зависимости от номера экземпляра */
MACRO ПолучитьНомерФормыСБ(НомерФормы, НомерЭкземпляра, ВсегоЭкземпляров)

 var Номер = "Форма № ";

 if(ValType(НомерФормы) == V_INTEGER) Номер = Номер + string(НомерФормы);
 else                                Номер = Номер + "???";
 end;

 if((ValType(ВсегоЭкземпляров) == V_INTEGER) and (ВсегоЭкземпляров > 1) and
    (ValType(НомерЭкземпляра)  == V_INTEGER) and (НомерЭкземпляра != 0))
   Номер = Номер + "-";
   if(НомерЭкземпляра <= 26)
     Номер = Номер + StrFor(CodeFor("a") + НомерЭкземпляра - 1);
   end;
 end;
 return Номер;
end;

/* Получить число копий для печати */
MACRO GetNumCopy()

  var NumCopy = КоличествоКопий;

  if (NumCopy <= 0)
    NumCopy = 1;
    if( ( not GetInt(NumCopy,"Введите количество копий.",3)) or (NumCopy <= 0) )
      exit(1);
    end;
  end;
  return NumCopy;
END;


macro GetISOCode( code )
 fi.FIID = code;
 if ( GetEQ( fi ) )
    return int(fi.ISO_Number);
 else
    return 0;
 end;
end;



macro GetStrSum( sum )
 var sumstr = string(MoneyL(sum));
 StrSet(sumstr, Index(sumstr, "."), "-");
 return sumstr;
end;



macro GetPrDate( date )
 ARRAY month;
 var str, dd, mm, yy;

 month(1) = " января ";
 month(2) = " февраля ";
 month(3) = " марта ";
 month(4) = " апреля ";
 month(5) = " мая ";
 month(6) = " июня ";
 month(7) = " июля ";
 month(8) = " августа ";
 month(9) = " сентября ";
 month(10) = " октября ";
 month(11) = " ноября ";
 month(12) = " декабря ";

 DateSplit( date, dd , mm , yy );
 str = String( dd, month(mm), yy, " г." );
 return str;
end;

macro GetPrDateCB(_date)

  var dateString = "\"";
  var tmp = string(_date:m:f:l);
  dateString = dateString + SubStr(tmp,1,2) + "\"" + SubStr(tmp,3);
  return dateString;

end;




macro GetAccountClientName( account , currency )
  var ptID = 0;
  accunt.Account = account;
  accunt.Code_Currency = currency;
  accunt.Chapter = 1;
  if ( GetEQ( accunt ) )
     ptID = accunt.Client;
  end;
  if ( ptID != 0 )
     pt.PartyID = ptID;
     if ( GetEQ( pt ) )
        return pt.Name;
     end;
  end;
  return "";

end;


/* Выводим длинную строку Str в поле длиной LineWidth - StrLen( EmptyFlds ) */
/* EmptyFlds - предыдущие пустые колонки                                    */
/* LineWidth - общая длина строки отчета                                    */
macro WriteLongStr( Str, EmptyFlds, LineWidth, EndSymb )
  var i, FieldWidth = LineWidth - StrLen( EmptyFlds ) - 1, NumItems;
  array Strokes;

  macro AppendSpace( Str, Len, EndSymb )
    var slen = strlen( Str ), rstr;
    return Str + MkStr( " ", Len-slen ) + EndSymb;
  end;

  StrSplit( Str, Strokes, FieldWidth );
  println( AppendSpace( Strokes( 0 ), FieldWidth, EndSymb ) );
  i = 1;
  NumItems = Asize( Strokes );
  while( i < NumItems )
    println( AppendSpace( EmptyFlds + Strokes( i ), LineWidth - 1, EndSymb ) );
    i = i + 1;
  end;
end;
