/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 1998              */
/*                                                                      */
/*  Имя файла        : checkiss.mac                                     */
/*                                                                      */
/*  Описание         : Инициализация и проверки  заявления              */
/*                     на выдачу чековой книжки                         */
/*  Программист      : Зайцев А.В.                                      */
/*                                                                      */
/*  Создан           : 12.12.98                                         */
/*                                                                      */
/************************************************************************/

import BankInter,PSInter, globals, "pm_opr.mac", "chisfun.mac";

record          Pscheckiss( checkiss );

record          OldPscheckiss( checkiss );

macro    Новый_Документ( )
        return 0;
end;

macro    Проверить_Документ( Режим )

  var stat = 0;
  if( ( Режим == 2 ) or ( Режим == 3 ) or ( Режим == 8 ) )/* ввод/редактирование документа */
    if( FindCHISSwithRange( Pscheckiss.Series, Pscheckiss.NumberFirst, Pscheckiss.NumberLast, Pscheckiss.Account, Pscheckiss.FormKind, Pscheckiss.OrderID  ) )
      MsgBox( "Заявление с такой серией и пересекающимся| диапазоном номеров ценных бланков уже введено" );
      return 1;
    end;
  end;
         if( (Режим == 3) and (Pscheckiss.CurrentState != CHECKISS_ST_POST) and (Pscheckiss.CurrentState != CHECKISS_ST_REJECTED))  /* РЕДАКТИРОВАНИЕ ДОКУМЕНТА */
               /*При редактировании производим проверку важности внесенных изменений */
               /* Константы важности внесенных изменений:           */
               /* CHANG_NOTIMPORTANT   - изменения неважные (сохранение без отката операции)*/
               /* CHANG_IMPORTANT      - изменения важны (сохранение изменений возможно только при откате) */
               /* CHANG_NOTKEEP        - не сохранять изменения */
               /* Если возвращаемое значение  > 0, то это оно интерпритируется как номер поля с ошибочным параметром*/
               /* Если возвращаемое значение  = 0, то cчитается, что проверка прошла успешно
                  и сохранение изменений можно производить без отката операции      */
                 
                 if((Pscheckiss.SheetsInBook != OldPscheckiss.SheetsInBook) or
                    (Pscheckiss.Account != OldPscheckiss.Account) or
                    (Pscheckiss.BooksAmount != OldPscheckiss.BooksAmount) or
                    (Pscheckiss.Series != OldPscheckiss.Series) or
                    (Pscheckiss.NumberFirst != OldPscheckiss.NumberFirst) or
                    (Pscheckiss.NumberLast != OldPscheckiss.NumberLast) or
                    (Pscheckiss.ValueDate != OldPscheckiss.ValueDate)
                   ) 
                 stat = CHANG_IMPORTANT;
               else
                 stat = CHANG_NOTIMPORTANT;
               end;

              if( Opr_IsStepExecuteSymb( OldPscheckiss.OrderID, PS_CHECKISS, "К" ))
                stat = CHANG_NOTKEEP;
              end;
                
         end;

  return stat;
end;

macro    Функция_Пользователя( Режим:integer )
/*
 Возможные значения Режим:
  UFN_PANEL_INPUT(1) - функция вызвана из панели ввода объекта;
  UFN_PANEL_EDIT(2) - функция вызвана из панели корректировки объекта;
  UFN_SCROL(3) - функция вызвана из панели скролинга, единичный вызов, любая корректировка объекта запрещена;
  UFN_SCROL_FMASS(4) - функция вызвана из панели скролинга, работа по нескольким записям, вызов до начала обработки, любая корректировка объекта запрещена;
 
// Пример работы: 
 if( Режим == UFN_SCROL )
   return UPDTPAGE;    // Обновить страницу записей и область скролинга
   //return UPDTREC;     // Обновить текущую запись, загрузив из файла
 end;
*/
  return 0;
end;


/*Хинт для списков по дате заявления*/
private const Hint_ByDate     :string = "/*+FIRST_ROWS LEADING(t oproper) INDEX(t dcheckiss_dbt_idx6) INDEX(oproper doproper_dbt_idx1) USE_NL(t oproper)*/";
/*Хинт для списков по дате закрытия*/
private const Hint_ByCloseDate:string = "/*+FIRST_ROWS LEADING(t oproper) INDEX(t dcheckiss_dbt_idx5) INDEX(oproper doproper_dbt_idx1) USE_NL(t oproper)*/";;
/*Хинт для списков по статусу первички*/
private const Hint_ByStatus   :string = "/*+FIRST_ROWS LEADING(t oproper) INDEX(t dcheckiss_dbt_idx2) INDEX(oproper doproper_dbt_idx1) USE_NL(t oproper)*/";
/*Хинт для списков по шагу*/
private const Hint_ByStep     :string = "/*+FIRST_ROWS LEADING(t oproper checkiss) INDEX(t doprstep_dbt_idx11) INDEX(oproper doproper_dbt_idx0) INDEX(t dcheckiss_dbt_idx2) USE_NL(t oproper checkiss)*/";

/* Установка подсказки для скролингов из макроса */
MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStates:integer ):string
  /*  Возможные значения ScrolStates:
      0  - Все                      
      1  - Отложенные               
      2  - Открытые                 
      3  - Закрытые                 
      4  - Отвергнутые              
      5  - Подготовленные к шагу */

  if( ScrolStates == 0 ) //Все

    return Hint_ByDate;

  elif( ScrolStates == 3 ) //Закрытые
    
    return Hint_ByCloseDate;

  elif(    ( ScrolStates == 1 )   //Отложенные
        or ( ScrolStates == 2 )   //Открытые
        or ( ScrolStates == 4 ) ) //Отвергнутые
  
    return Hint_ByStatus;

  elif( ScrolStates == 5 ) //Подготовленные к шагу

    return Hint_ByStep;

  end;

  return DefaultHint;
END;
