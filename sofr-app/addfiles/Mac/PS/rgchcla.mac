/*
$Name:             rgchcla.mac
$Module:           РКО юридических лиц
$Description:      Операция - 3036, Шан 90 - Закрытие старого счета
*/
/* -------------------------------------------------------------------- */
/*         Автоматизированная банковская система RS-Bank v6.0           */
/*                 Copyright (c) R-Style Software Lab                   */
/*                                                                      */
/*  Имя файла  : rqchcla.mac                                            */
/*                                                                      */
/*  Описание   : Операция - 3036 - "Изменение номера счета"             */
/*               Шаг      - 90   - "Закрытие старого счета"             */
/*                                                                      */
/*  Программист: Копачев Д.В.                                           */
/*                                                                      */
/*  Создан     : 14.09.2005                                             */
/*                                                                      */
/* -------------------------------------------------------------------- */
import reqinter, InsCarryDoc, BankInter, OprInter, PSInter;

RECORD req( reqchnga );

// Класс ПД для определения дополнительных счетов (пока это здесь, больше ничего и не надо)
class PrimREQCHANGE( DocKind : integer, DocID : integer )
  
  var Kind : integer;
  var Id   : integer;
  var Error : integer;

  // Вернуть параметр второго рода
  macro GetParametr( ParmKind, OperDate, CatCode, FIRole )
    return -1;
  end;

  // вернуть параметр первого рода
  macro GetParametrTemplate( ObjectID, Classificator, OperDate, FIRole )         
    return -1;
  end;
  // Основная роль ФИ
  macro GetBasisFIRole(FIRole)
    return FIROLE_UNDEF;
  end;
  // Корректировка счета
  macro CorrectAccount( account, accblnc, ORScheme, categ, templ, accdoc, OperDate )
    return 0;
  end;
  // Конструктор
  Kind = DocKind;
  Id   = DocID;
  Error = 0;
end;

// Выполним проводку
PRIVATE MACRO InsertCarryDocument( SumCarry, StatusCarry:integer ):integer
  var Carry:RsbAccTransaction = RsbAccTransaction;
  var obj_reqchahge : PrimREQCHANGE = PrimREQCHANGE( PS_REQCHANGE, req.REQUESTID );

  Carry.Chapter         = 1;
  Carry.FIIDPayer       = req.OldAccountFIID;
  Carry.FIIDReceiver    = req.NewAccountFIID;
  Carry.SumPayer        = SumCarry;
  Carry.AccountPayer    = req.OldAccount;
  Carry.AccountReceiver = req.NewAccount;
  Carry.Numb_Document   = req.Number;
  Carry.Date_Carry      = {curdate};
  Carry.Ground          = req.ChangeGround;
  Carry.Status_After    = StatusCarry;
  Carry.Date_Rate       = {curdate};
  Carry.PrimaryDoc      = obj_reqchahge;

  if( not Carry.Carry() )
    msgbox( "Ошибка при вставке проводки" );
    return 1;
  end;
  return 0;
END;

// Выполнение шага
MACRO ExecuteStep( doc, reqacc )
  var Rest:money, PlanRest:money;
  var stat:integer = 0;

  SetBuff( req, reqacc );
  
  var ClientID = 0;
  var rsAcc = execSQLSelect( " select t_Client from daccount_dbt "
                             " where t_Account = :Account ", makeArray( SQLParam( "Account", req.OldAccount )));
  if( rsAcc and rsAcc.MoveNext() )
    ClientID = rsAcc.value(0);
  end;
  
  stat = PSCheckBankruptStatus( ClientID, 1 );
  if( stat )
    return stat;
  end;
  
  // Получить остаток по счету за дату
  stat = CB_GetAccountRest(1, req.OldAccount, req.oldaccountfiid, {curdate}, Rest, PlanRest);
  if( stat == 0 )
    if( Rest ) // Выполним фактическую проводку
      stat = InsertCarryDocument( Rest, ACCTRN_STATUS_DATE_CARRY );
    end;
    if( (stat == 0) and ((PlanRest - Rest) > 0) ) // выполним плановую проводку
      stat = InsertCarryDocument( (PlanRest - Rest), ACCTRN_STATUS_PLAN );
    end;
  else 
    MsgBox("Ошибка получения остатка по счету " + req.OldAccount + " за дату " + {curdate});
  end;
  return stat;
END;

