 /*
 $Name: psbcpre1.mac
 $Module: РКО юридических лиц
 $Description: Предобработка поручений на покупку/продажу/конвертацию валюты
 */

//-----------------------------------------------------------------------------
// Блок     : "Предобработка п/п/к"
// Шаг      : "Предобработка"
// Описание : Макрос шага
//-----------------------------------------------------------------------------

import PaymInter, PSInter, BankInter, psbccomn, FIInter, cbsttls, PTInter, pm_const,pm_common, pm_tools, pm_reserve;

var PaymentObj:RsbPayment;

CONST SET_CHAR = "X", 
      UNSET_CHAR = "";


//-----------------------------------------------------------------------------
// Является ли субъект резидентом
//-----------------------------------------------------------------------------
PRIVATE MACRO IsResident( BankID )
  private FILE fparty ( "party.dbt" );

  var OldKey, result = true;

  OldKey = KeyNum( fparty, 0 );

  fparty.PartyId = BankID;

  if( getEQ( fparty ) )
    if( fparty.NotResident == "X" )
      result = false;
    end;
  end;

  keyNum( fparty, OldKey );

  return result;
END;


//-----------------------------------------------------------------------------
// Проверка внешних платежей
//-----------------------------------------------------------------------------
PRIVATE MACRO CheckExternalPayment( Payment:RsbPayment, err:string ):integer

  var stat   :integer = 0;
  var err_str:string  = "";
  var retval:integer = 0;
  var ExtAcc :string  = "", ExtAccNew :string  = "", BankCode:string = Payment.ReceiverBankCode;;
  var params:TArray = NULL;

  // Если банк российский
  if( ( Payment.ReceiverBankID > 0 ) and ( IsResident( Payment.ReceiverBankID )) )
    
    // Проверить длину счета
    if( ( stat == 0 ) and ( strlen( Payment.ReceiverAccount ) > 0 ) )
      // Проверить ключ счета
      if( Payment.ReceiverBankCodeKind != PTCK_BIC )
        params = makeArray( SQLParam( "p_PartyID"    , Payment.ReceiverBankID ),
                            SQLParam( "p_CodeKind"   , PTCK_BIC               ),
                            SQLParam( "p_Code"       , V_STRING , RSDBP_OUT   ),
                            SQLParam( "p_CodeOwnerID", V_INTEGER, RSDBP_OUT   )
                          );
        retval = execStoredFunc( "RSBPARTY.PT_GetPartyCodeEx", V_INTEGER, params );
        if( retval == 0 )
          BankCode = params.Value(2).value;
        end;
      end;
      ExtAccNew = GetKey( ExtAcc, BankCode );
      if( ExtAccNew != ExtAcc )
        err_str = "В номере счета " + ExtAcc + " неверно значение ключа.|Должно быть " + ExtAccNew;
        stat = 1;
      end;
    end;
  end;
  
  SetParm( 1, err_str );
  return stat;
END;

//-----------------------------------------------------------------------------
// Системные проверки при выполнении шага
// Возвращает ошибку
// Если заполняет строку ошибки, то возвращает 1
//-----------------------------------------------------------------------------
PRIVATE MACRO SystemControl_CommonCheck( Payment:RsbPayment, err_msg:string ):integer
  var stat   :integer = 0;
  var obj    :object  = GenObject( "RsbBuyCurrencyOrder", Payment.DocumentID );
  var RateObj:object  = NULL;

  //проверка "дата валютирования==дата опердня"
  if( Payment.ValueDate != {curdate} )
    err_msg = "Дата валютирования должна совпадать с датой опердня,| в котором проводится операция";
    SetParm( 1, err_msg );
    return 1;
  end;
  
  // Проверить валюты
  if( CheckFIID( Payment.PayerFIID ) != 0 )
    err_msg = "Неверно задана валюта дебета";
    SetParm( 1, err_msg );
    return 1;
  end;  
  if( CheckFIID( Payment.ReceiverFIID ) != 0 )
    err_msg = "Неверно задана валюта кредита";
    SetParm( 1, err_msg );
    return 1;
  end;
  if( Payment.PayerFIID == Payment.ReceiverFIID )
    err_msg = "Валюты дебета и кредита должны быть различны";
    SetParm( 1, err_msg );
    return 1;
  end;
  if( obj.BankFunds )
    if( Payment.PayerAmount <= 0 )
      err_msg = "Не задана сумма по дебету";
      SetParm( 1, err_msg );
      return 1;
    end;
    if( Payment.ReceiverAmount <= 0 )
      err_msg = "Не задана сумма по кредиту";
      SetParm( 1, err_msg );
      return 1;
    end;
  end;
  if( (obj.ExchangeFIID > -1 ) AND (CheckFIID( obj.ExchangeFIID ) != 0) )
    err_msg = "Неверно задана валюта торгов";
    SetParm( 1, err_msg );
    return 1;
  end;
  if( Payment.NumberPack < 0 )
    err_msg = "Неверно задан номер пачки";
    SetParm( 1, err_msg );
    return 1;
  end;
  if( Payment.PayerAccount == "" )
    err_msg = "Не задан счет плательщика";
    SetParm( 1, err_msg );
    return 1;
  end;
  if( not AccountExistAndOpen( Payment.PayerFIID, Payment.PayerAccount, CHAPT1 ) )
    return ERR_INVALID_PAYERACC;
  end;
 
  // Счет дебета является клиентским. Иначе - "Плательщик не является клиентом. Ошибка выбора операции."
  if( not IsClientPayerAccount( Payment ) )
     err_msg = "Плательщик не является клиентом. Ошибка выбора операции.";
     SetParm( 1, err_msg );
      return 1;
  end;
    
  if( Payment.ReceiverGroup != PAYMENTS_GROUP_EXTERNAL )
    if( Payment.ReceiverAccount == "" )
      err_msg = "Не задан счет получателя";
      SetParm( 1, err_msg );
      return 1;
    end;
    if( not AccountExistAndOpen( Payment.ReceiverFIID, Payment.ReceiverAccount, CHAPT1 ) )
      return ERR_INVALID_RECEIVERACC;
    end;
  else
    // Проверка для внешних платежей
    if( ( Payment.PayerGroup == PAYMENTS_GROUP_EXTERNAL ) or ( Payment.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL ) )
      if( CheckExternalPayment( Payment, err_msg ) )
        SetParm( 1, err_msg );
        return 1;
      end;
    end;
  end;
  
  return 0;
END;

PRIVATE MACRO ExecuteSysControlStep( Payment:RsbPayment ):integer
  var err:integer = 0;
  var err_mes:string = "";
  var obj;

  err = SystemControl_CommonCheck( Payment, err_mes );

  if( err != 0 )
    if( StrLen( err_mes ) == 0 )
      InitError();
      MemoryError( err );
      err_mes = GetErrMsg();
    end;

    InitError();
    // Установить статус платежа
    Payment.PaymStatus = PM_REJECTED;

    // Установить статус первички
    BC_SetPrimDocumentState( Payment, PSBCORD_ST_REJECTED );

    // Заполнить статусы сегментов операции
    if( УстановитьСтатусыПлатежа( OPR_BC_CONTROL, OPR_BC_ST_CTRL_REJECTED ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return -1;
    end;

    // Заполнить примечание
    if( Payment.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, err_mes ) != 0 )
      msgbox( "Ошибка при вставке примечания платежа" );
      return -1;
    end;
    msgbox( err_mes );
    return 1;
  end;
  
  return 0;

END;

//-----------------------------------------------------------------------------
// Общие проверки остатков по счетам
//-----------------------------------------------------------------------------
PRIVATE MACRO CheckAccRestCommon( Payment:RsbPayment, err_mes:string ):integer
  var Sfree = 0, SumR = 0, SumC = 0;
  var КоэфД:double = 1.0;
  var err_msg:string = "";
  var BcOrder:object  = GenObject( "RsbBuyCurrencyOrder", Payment.DocumentID );

  if( NOT DefineCoefficientDepos( {OperDprt}, КоэфД ) )
    err_msg = "";
    SetParm( 1, err_msg );
    return -1;
  elif( КоэфД < 1.0 )
    КоэфД = 1.0;
  end;
  
  if( Payment.PayerAmount == 0 )

    if( BcOrder.BCOrdKind == PSBCKIND_BUY ) // покупка
      if( Payment.ReceiverFIID == BcOrder.ExchangeFIID )
        if( BcOrder.LimRate != 0 )
          Payment.PayerAmount = ConvertSum( Payment.ReceiverAmount, BcOrder.LimRate, BcOrder.LimRateScale, BcOrder.LimRatePoint, BcOrder.LimRateInv );
        else
          if( ConvSum(SumR, Payment.ReceiverAmount, {curdate}, Payment.ReceiverFIID, 0/*NATCUR*/) )
            err_msg = "Ошибка при конвертации валюты";
            SetParm( 1, err_msg );
            return -1;
          end;
          Payment.PayerAmount = SumR * КоэфД;
        end;
      else
        if( BcOrder.LimRate != 0 )
          if( ConvSum(SumR, Payment.ReceiverAmount, {curdate}, Payment.ReceiverFIID, 0/*NATCUR*/) )
            err_msg = "Ошибка при конвертации валюты";
            SetParm( 1, err_msg );
            return -1;
          end;
          if( ConvSum(SumC, SumR, {curdate}, 0/*NATCUR*/, BcOrder.ExchangeFIID) )
            err_msg = "Ошибка при конвертации валюты";
            SetParm( 1, err_msg );
            return -1;
          end;
          Payment.PayerAmount = ConvertSum( SumC * КоэфД, BcOrder.LimRate, BcOrder.LimRateScale, BcOrder.LimRatePoint, false );
        else
          if( ConvSum(SumR, Payment.ReceiverAmount, {curdate}, Payment.ReceiverFIID, 0/*NATCUR*/) )
            err_msg = "Ошибка при конвертации валюты";
            SetParm( 1, err_msg );
            return -1;
          end;
          Payment.PayerAmount = SumR * КоэфД * КоэфД;
        end;
      end;

    elif( BcOrder.BCOrdKind == PSBCKIND_PAY ) // продажа
      
      if( BcOrder.LimRate != 0 )
        Payment.PayerAmount = ConvertSum( Payment.ReceiverAmount, BcOrder.LimRate, BcOrder.LimRateScale, BcOrder.LimRatePoint, true );
      else
        if( ConvSum(SumC, Payment.ReceiverAmount, {curdate}, 0/*NATCUR*/, PaymentObj.PayerFIID) )
          err_msg = "Ошибка при конвертации валюты";
          SetParm( 1, err_msg );
          return -1;
        end;
        Payment.PayerAmount = SumC * КоэфД;
      end;

    else //конверсия
      if( BcOrder.LimRate != 0 )
        if( BcOrder.ConvOper == KINDOPCONV_BUY )
          Payment.PayerAmount = ConvertSum( Payment.ReceiverAmount, BcOrder.LimRate, BcOrder.LimRateScale, BcOrder.LimRatePoint, false );
        elif( BcOrder.ConvOper == KINDOPCONV_PAY )
          Payment.PayerAmount = ConvertSum( Payment.ReceiverAmount, BcOrder.LimRate, BcOrder.LimRateScale, BcOrder.LimRatePoint, true );
        end;
      else
        if( ConvSum(SumR, Payment.ReceiverAmount, {curdate}, Payment.ReceiverFIID, 0/*NATCUR*/) )
          err_msg = "Ошибка при конвертации валюты";
          SetParm( 1, err_msg );
          return -1;
        end;
        if( ConvSum(SumC, SumR, {curdate}, 0/*NATCUR*/, Payment.PayerFIID) )
          err_msg = "Ошибка при конвертации валюты";
          SetParm( 1, err_msg );
          return -1;
        end;
        Payment.PayerAmount = SumC * КоэфД;
      end;
    end;

  end;

  if( CheckRestAndMakeReserve(Payment, false, true, true, true, GetOprStatus(OPR_PAYM_PERMISSION)) )
    err_msg = "Недостаточно средств на счете плательщика";
    SetParm( 1, err_msg );
    return 1;
  end;
  
  return 0;
END;

//-----------------------------------------------------------------------------
// Проверка остатков по счетам
//-----------------------------------------------------------------------------
PRIVATE MACRO CheckAccRest( Payment:RsbPayment )
  var err_mes : string  = "";
  var stat    : integer = CheckAccRestCommon( Payment, err_mes );
  var BcOrder:object  = GenObject( "RsbBuyCurrencyOrder", PaymentObj.DocumentID );

  if( stat != 0 )
    if( stat > 0 )
      // Установить статус платежа
      Payment.PaymStatus = PM_REJECTED;

      // Установить статус первички
      BC_SetPrimDocumentState( Payment, PSBCORD_ST_REJECTED );

      // Заполнить статусы сегментов операции
      if( УстановитьСтатусыПлатежа( OPR_BC_CONTROL, OPR_BC_ST_CTRL_REJECTED ) )
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return -1;
      end;

      // Заполнить примечание
      if( Payment.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, err_mes ) != 0 )
        msgbox( "Ошибка при вставке примечания платежа" );
        return -1;
      end;
      msgbox( err_mes );
      return 1;
    else
      if( err_mes != "" )
        msgbox( err_mes );
      end;
      return -1;
    end;
  else
    
    /*if( BcOrder.Origin != PSBCORD_OR_INRQ )
      if( Payment.MakeReserve( Payment.PayerAccount, CHAPT1, Payment.PayerFIID, Payment.PayerAmount ) )
        msgbox("Ошибка при создании резерва на счете плательщика");
        return -1;
      end;
    end;*/

    // Заполнить статусы сегментов операции
    if( УстановитьСтатусыПлатежа( OPR_BC_DO, OPR_BC_ST_EXECUTION ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return -1;
    end;

    return 1;
  end;
  
  return 0;

END;

MACRO ExecuteStep( doc, paymDoc )
  var stat:integer = 0;
  var obj:object;

  if( stat == 0 )
    
    obj = GenObject( "RsbBuyCurrencyOrder", PaymentObj.DocumentID );
    
    //Контроль
    stat = УстановитьСтатусыПлатежа( OPR_BC_CONTROL, OPR_BC_ST_CTRL_NOTCONTROL );

    // Исполнение
    if( stat == 0 )
      if( obj.BankFunds == SET_CHAR )
        stat = УстановитьСтатусыПлатежа( OPR_BC_EXECUTION, OPR_BC_ST_BANKFUNDS );
      else
        stat = УстановитьСтатусыПлатежа( OPR_BC_EXECUTION, OPR_BC_ST_EXCHANGE );
      end;
    end;      

    // Состояние средств
    if( stat == 0 )
      if( obj.BankFunds == UNSET_CHAR )
        stat = УстановитьСтатусыПлатежа( OPR_BC_STATEFUNDS, OPR_BC_ST_FUNDS_NONDEPOSITION );
      end;
    end;      
    
    if( stat != 0 )      
      msgbox("Ошибка при установке статуса платежа");
      return 1;
    end;
  end;

  // Системный контроль
  stat = ExecuteSysControlStep( PaymentObj );
  if( stat < 0 )
    return 1;
  elif( stat > 0 )
    return 0;
  end;

  if( stat == 0 ) // Проверка остатков по счетам
    stat = CheckAccRest( PaymentObj );
    if( stat < 0 )
      return 1;
    elif( stat > 0 )
      return 0;
    end;
  end;

  return stat;
END;
