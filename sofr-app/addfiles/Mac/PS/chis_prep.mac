/* ╔══════════════════════════════════════════════════════════════════╗ */
/* ║        Автоматизированная банковская система RS-Bank v5.0        ║ */
/* ║                Copyright (c) R-Style Software Lab 1998           ║ */
/* ║                                                                  ║ */
/* ║ Имя файла    : chiss_categ.mac                                   ║ */
/* ║                                                                  ║ */
/* ║ Описание     : Шаг "Предобработка"                               ║ */
/* ║                заяв. на выд. цен. бл.                            ║ */
/* ║                                                                  ║ */
/* ║                                                                  ║ */
/* ║ Программист  : Локтюшин В.Ю.                                     ║ */
/* ║                                                                  ║ */
/* ║ Создан       : 14.09.2006                                        ║ */
/* ║                                                                 ║ */
/* ╚══════════════════════════════════════════════════════════════════╝ */

import PSInter, chisfun, InsCarryDoc, CashInter;

var CheckIssObj:RsbCheckIss;

CONST SET_CHAR = "X", 
      UNSET_CHAR = "";

//-----------------------------------------------------------------------------
// Системные проверки при выполнении шага
// Возвращает ошибку
// Если заполняет строку ошибки, то возвращает 1
//-----------------------------------------------------------------------------
PRIVATE MACRO SystemControl_CommonCheck( CheckIss:RsbCheckIss, err_msg:string ):integer
  var stat   :integer = 0;
  var intbuff:integer = 0;
  var strbuff:string  = UNSET_CHAR;
   
  if( CheckIss.Account == UNSET_CHAR )
    err_msg = "Счет не задан";
    SetParm( 1, err_msg );
    return 1;
  end;

  if( AccountExistAndOpenChiss( CheckIss.Account, CheckIss.FIID, CheckIss.Department, "Счет", err_msg ) == false )
      SetParm( 1, err_msg );
      return 1;
  end;
  
  if( AccountExistAndOpenChiss( CheckIss.ComissAccount, CheckIss.ComissFIID, CheckIss.Department, "Счет комиссии", err_msg ) == false )
      SetParm( 1, err_msg );
      return 1;
  end;


  intbuff = CheckIss.BooksAmount;
  if( intbuff <= 0 )
    err_msg = "Не задано количество книжек";
    SetParm( 1, err_msg );
    return 1875;
  end;

  intbuff = CheckIss.SheetsInBook;
  if( intbuff <= 0 )
    err_msg = "Не задано количество бланков к выдаче";
    SetParm( 1, err_msg );
    return 1876;
  end;

  strbuff = CheckIss.Series;
  if( strbuff == UNSET_CHAR )
    err_msg = "Не задана серия бланков";
    SetParm( 1, err_msg );
    return 1877;
  end;
   
  strbuff = CheckIss.NumberFirst;
  if( strbuff == UNSET_CHAR )
    err_msg = "Не задан начальный номер";
    SetParm( 1, err_msg );
    return 1878;
  end;

  strbuff = CheckIss.NumberLast;
  if( strbuff == UNSET_CHAR )
    err_msg = "Не задана конечный номер";
    SetParm( 1, err_msg );
    return 1879;
  end;
  
  if( FindCHISSwithRange( CheckIss.Series, CheckIss.NumberFirst, CheckIss.NumberLast, CheckIss.Account, CheckIss.FormKind, CheckIss.OrderID ) )
    err_msg = "Заявление с такой серией и пересекающимся| диапазоном номеров ценных бланков уже введено";
    SetParm( 1, err_msg );
    return 1;
  end;

  stat = FindCERTforCheck( -1, CheckIss.Department, CheckIss.Series, CheckIss.NumberFirst, CheckIss.NumberLast );
  if( ( stat == 831 /*NumberLast<NumberFirst*/) or 
      ( stat == 0   /*ценные бланки найдены */) )
    err_msg = "Неверный диапазон номеров сертификатов";
    SetParm( 1, err_msg );
    return 831;
  end;
    

  return 0;

END;


MACRO ExecuteStep( doc, paymDoc )
   var stat:integer = 0;
   var err_msg:string = UNSET_CHAR;

   stat = SystemControl_CommonCheck( CheckIssObj, err_msg );

   if( stat != 0 )

     msgbox( err_msg );

     CheckIssObj.CurrentState = CHECKISS_ST_REJECTED;

     // Заполнить статусы сегментов операции
     if( not InsertOprStatus( CHECKISS_KIND_ST_STATE, CHECKISS_ST_STAT_REJECTED ) )
       msgbox("Ошибка при установке сегментов статуса экземпляра операции");
       return -1;
     end;

     // Заполнить примечание
     if( InsertNoteForCheckIss( CheckIssObj.OrderID, CHECKISS_NOTES_CAUSE_OF_FAILUR,err_msg ) != 0 )
       msgbox( "Ошибка при вставке примечания платежа" );
       return -1;
     end;

     return 0;
   else
     
     if( not InsertOprStatus( CHECKISS_KIND_ST_DO, CHECKISS_ST_DO_REG ) )
       msgbox("Ошибка при установке сегментов статуса экземпляра операции"); 
       return -1;                                                            
     end;                                                                    

     if( not InsertOprStatus( CHECKISS_KIND_ST_PZO, CHECKISS_ST_PZO_NEED ) )
       msgbox("Ошибка при установке сегментов статуса экземпляра операции"); 
       return -1;                                                            
     end;                                                                    

     if( WorkWithRetail() )
       if( not InsertOprStatus( CHECKISS_KIND_ST_ACCEPT, CHECKISS_ST_ACCEPT_NEED ) )
         msgbox("Ошибка при установке сегментов статуса экземпляра операции");
         return -1;                                                           
       end;                                                                   
     else
       if( not InsertOprStatus( CHECKISS_KIND_ST_ACCEPT, CHECKISS_ST_ACCEPT_NOTNEED ) )
         msgbox("Ошибка при установке сегментов статуса экземпляра операции");
         return -1;                                                           
       end;                                                                   
     end;
   end;
   return stat;
END;