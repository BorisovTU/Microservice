/*
 $Name: fns_info.mac
 $Module: РКО
 $Description: Макрос скроллинга исходящих справок в ФНС
 */
//-----------------------------------------------------------------------------
// Блок      : Вне блока
// Шаг       : Вне шага
// Назначение: Макрос скроллинга
// Описание  : Макрос скроллинга исходящих справок в ФНС
//-----------------------------------------------------------------------------
import MesInter, BankInter, CTInter, PTInter, oralib, likepy;

RECORD r_wlregdec    ("wlregdec.dbt");
RECORD r_wlregdec_old("wlregdec.dbt");
record clientRec     ("party.dbt");

macro Новая_Справка()
  return 0;
END;

/* Проверка на числовой номер */
private macro IsDigit( NumberStr ):bool

  var OK = true, i = 1, ch = "", DigitString = "0123456789";

      while( (OK) and (i <= strlen(NumberStr)) )
        ch = SubStr( NumberStr, i, 1 );
        if( not Index( DigitString, ch ))
          OK = false;
        end;
        i = i + 1;
      end;

  return OK;

end;

MACRO Проверить_Справку( Режим )
 /*
 Возможные значения Режим:
 1  - удаление справки ,
 2  - вставка справки  ,
 3  - обновление справки
*/
  if( ( Режим == 2 ) or ( Режим == 3 ) )

    if( r_wlregdec.OriginatorCodeKind == 0 )
      msgbox( "Не заполнено поле 'Вид кода создателя'");
      return 1;
    elif( r_wlregdec.OriginatorCode == "" )
      msgbox( "Не заполнено поле 'Код создателя'");
      return 1;
    elif( r_wlregdec.OriginatorName == "" )
      msgbox( "Не заполнено поле 'Наименование создателя'");
      return 1;
    elif( r_wlregdec.RecipientCodeKind == 0 )
      msgbox( "Не заполнено поле 'Вид кода отправителя'");
      return 1;
    elif( r_wlregdec.RecipientCode == "" )
      msgbox( "Не заполнено поле 'Код отправителя'");
      return 1;
    elif( r_wlregdec.RecipientName == "" )
      msgbox( "Не заполнено поле 'Наименование отправителя'");
      return 1;
    elif( r_wlregdec.Date == date( 0, 0, 0 ) )
      if( r_wlregdec.Type == WLD_TYPE_REGDEC_IVS )
        msgbox( "Не заполнено поле 'Дата начала периода'");
      else
        msgbox( "Не заполнено поле 'Дата справки'");
      end;
      return 1;
    elif( ( r_wlregdec.EndDate == date( 0, 0, 0 ) ) and ( r_wlregdec.Type == WLD_TYPE_REGDEC_IVS ) )
      msgbox( "Не заполнено поле 'Дата окончания периода'");
      return 1;
    elif( r_wlregdec.ClientName == "" )
      msgbox( "Не заполнено поле 'Наименование клиента'");
      return 1;
    elif( r_wlregdec.ClientINN == "" )
      msgbox( "Не заполнено поле 'ИНН клиента'");
      return 1;
    elif( (r_wlregdec.Number == "") and (r_wlregdec.RegPartyKind == 7 ) )
      msgbox( "Не заполнено поле 'Номер справки/выписки'" );
      return 1;
    elif( (strlen(r_wlregdec.Number) > 6) or not IsDigit(r_wlregdec.Number) )
      msgbox( "Номер справки/выписки должен быть не длиннее 6 символов и содержать только цифры" );
      return 1;
    elif( r_wlregdec.LnkDocType <= 0 )
      msgbox( "Не заполнено поле 'Вид связанного объекта'" );
      return 1;
    elif( r_wlregdec.LnkDocNumber == "" )
      msgbox( "Не заполнено поле 'Номер связанного объекта'" );
      return 1;
    elif( (strlen(r_wlregdec.LnkDocNumber) > 6) or not IsDigit(r_wlregdec.LnkDocNumber) )
      msgbox( "Номер связанного объекта должен быть не длиннее 6 символов и содержать только цифры" );
      return 1;
    elif( r_wlregdec.LnkDocDate == date( 0, 0, 0 ) )
      msgbox( "Не заполнено поле 'Дата связанного объекта'" );
      return 1;
    elif( (r_wlregdec.RegPartyKind == 11) or (r_wlregdec.RegPartyKind == 52))
      var RegVal = true;
      GetRegistryValue("PS\\ФОНДЫ_СПРАВКИ_ВЫПИСКИ\\РАЗРЕШИТЬ_ФИЗЛИЦ", V_BOOL, RegVal);
      if( (not RegVal) )
        if( (r_wlregdec.ClientID > 0) and (r_wlregdec.ClientType == 2) )
          var AttrNum : String;
          ПолучитьСубъекта(r_wlregdec.ClientID, clientRec);
          GetMainObjAttr( null, OBJTYPE_PARTY, UniID(clientRec, OBJTYPE_PARTY), 16, null, null, AttrNum);
          var IsEmployer = "";
          var rs = execSQLSelect( " Select pers.t_IsEmployer "
                                  "   From dpersn_dbt pers "
                                  "  Where pers.t_PersonID = :ClientID ",
                                  MakeArray( SQLParam("ClientID", r_wlregdec.ClientID)));
          if( rs and rs.MoveNext())
            IsEmployer = rs.value("t_IsEmployer");
          end;
          // Если НЕ (persn.IsEmployer == SET_CHAR или категория "Тип субъекта" имеет значение адвокат, нотариус, управляющий товарищ), ТО ошибка
          if( not((IsEmployer == "X") or ((AttrNum == "2") or (AttrNum == "3") or (AttrNum == "8"))) )
            msgbox( "Согласно установленным настройкам запрещена отправка данных в ПФР и ФСС по счетам фзических лиц, не являющихся предпринимателями");
            return 1;
          end;
        end;
      end;
      RegVal = true;
      GetRegistryValue("PS\\ФОНДЫ_СПРАВКИ_ВЫПИСКИ\\РАЗРЕШИТЬ_ДЕПОЗИТЫ", V_BOOL, RegVal);
      if( (not RegVal))
        if( r_wlregdec.LnkTypeInfo == 8 )
          msgbox( "Согласно установленным настройкам запрещена отправка данных в ПФР и ФСС по вкладам (депозитам)");
          return 1;
        end;
      end;
      RegVal = true;
      GetRegistryValue("PS\\ФОНДЫ_СПРАВКИ_ВЫПИСКИ\\РАЗРЕШИТЬ_КЭСП", V_BOOL, RegVal);
      if( (not RegVal))
        if( r_wlregdec.LnkTypeInfo == 5 )
          msgbox( "Согласно установленным настройкам запрещена отправка данных в ПФР и ФСС по КЭСП");
          return 1;
        end;
      end;
    elif( (r_wlregdec.LnkDocExtID != "") and not RegExMatch(r_wlregdec.LnkDocExtID, "[[:alnum:]А-Яа-я]{12}_\\d{6}") )
      msgbox( "Идентификатор запроса должен соответствовать шаблону ККККDDDDDDDD_NNNNNN" );
      return 1;
    end;
    
  end;

  return 0;
END;



macro Функция_Пользователя( Режим:integer )
 /*
 Возможные значения Режим:
  UFN_PANEL_INPUT(1) - функция вызвана из панели ввода объекта;
  UFN_PANEL_EDIT(2) - функция вызвана из панели корректировки объекта;
  UFN_SCROL(3) - функция вызвана из панели скролинга, единичный вызов, любая корректировка объекта запрещена;
  UFN_SCROL_FMASS(4) - функция вызвана из панели скролинга, работа по нескольким записям, вызов до начала обработки, любая корректировка объекта запрещена;
 
// Пример работы: 
 if( Режим == UFN_SCROL )
   return UPDTPAGE;    // Обновить страницу записей и область скролинга
   //return UPDTREC;     // Обновить текущую запись, загрузив из файла
 end;
 */
 return  0;
end;

