//-----------------------------------------------------------------------------
// Блок      : Вне блока
// Шаг       : Вне шага
// Назначение: Макрос скроллинга
// Описание  : Макрос скроллинга кассовых документов в РКО
//-----------------------------------------------------------------------------

import check117, CashInter, "pm_opr.mac", "pmchoper.mac", "pm_tools.mac", pm_chksave, "pmedit.mac", pmbuff;

// номера полей в панели
const fld_Number    = 1,  
      fld_Date      = 3,
      fld_DateValue = 4, 
      fld_Ground    = 22;

class (TPanelFields)TCashDocPanelFields()
  InitTPanelFields();
  Number = fld_Number;
  rmDate = fld_Date;
  ValueDate = fld_DateValue;
  Ground = fld_Ground;
end;

MACRO Новый_Документ( )
  return 0;
END;

MACRO Проверить_Документ( Режим )
  
  var stat = 0,
      CHANG_IMPORTANT = -11,
      CHANG_NOTIMPORTANT = -10;

  var ErrStr:string = "";
  record debet_rec ( "pmprop.dbt" );
  record credit_rec ( "pmprop.dbt" );
  ClearRecord( debet_rec );
  debet_rec.PaymentID = credit_rec.PaymentID = r_pmpaym.PaymentID;
  debet_rec.Group     = credit_rec.Group     = PAYMENTS_GROUP_INTERNAL;
  debet_rec.IsSender  = credit_rec.IsSender  = "X";

  // Проверяем номер для всех документов, в том числе и отложенных           
  if( (Режим == 2 ) or (Режим == 3) or (Режим == 8) ) 

    if( r_pmrmprop.Date == date(0,0,0) )
      msgbox("Дата документа должна быть задана");
      return fld_Date;
    end;
    if( r_pmpaym.ValueDate < r_pmrmprop.PayDate )
      msgbox( "Дата валютирования не должна быть|меньше даты платежа" );
      return fld_DateValue;
    end;

    if( StrLen( r_pmrmprop.Number ) == 0 ) 
      MsgBox("Не задан номер документа");
      return fld_Number;
    end;

    if( not r_pscshdoc.IsCurrency ) /* Проверки для рублевых */
       if( not StrIsNumber( r_pmrmprop.Number ) )
         MsgBox("Номер документа нечисловой");
         return fld_Number;
       else
         if( int( r_pmrmprop.Number ) == 0 )
           MsgBox("Номер документа не может быть нулевым");
           return fld_Number;
         end;
       end;
    end;
            
    if( StrLen( r_pmrmprop.Ground ) == 0 )
      MsgBox("Введите Назначение платежа");
      return fld_Ground;
    end;

    ErrStr = PM_CheckPaymAccounts( r_pmpaym, NULL, NULL, NULL, 1 );
    if( strlen(ErrStr) > 0 )
      msgbox( ErrStr );
      return 1;
    end;

    /* Общие проверки по списку */
    var fld : TCashDocPanelFields = TCashDocPanelFields();
    stat = CS_ScrolMacroCommonChecks( fld, r_pmpaym, debet_rec, credit_rec, r_pmrmprop );
    if( stat != NOTERROR )
      return stat;
    end;

    /*проверка реквизитов валютной операции*/
    if( PM_CheckCO(r_pmpaym,r_pmrmprop,0,0) )
      return 1;
    end;

    /* Проверить по 117-И */
    if( CheckOnSave_117( r_pmpaym, NULL, NULL, NULL ) )
      return fld_Number;
    end;

  end;
  
  if( ( Режим == 2 ) or ( Режим == 3 ) or ( Режим == 8 ) or ( Режим == 9 ))
    var PartyID = 0;
    if (r_pmpaym.DocKind == CASH_PS_INCORDER)
      PartyID = r_pmpaym.Receiver;
    else
      PartyID = r_pmpaym.Payer;
    end;
    if( (PartyID > 0) and ClientHasServKind( PartyID, PTSK_PAY ))
      return 1;
    end;
  end;

  if( Режим == 1 ) /*УДАЛЕНИЕ ДОКУМЕНТА*/
    if(CheckDeletePayment(r_pmpaym.PaymentID))
      return 1;
    end;
  elif( Режим == 3) /*РЕДАКТИРОВАНИЕ ДОКУМЕНТА*/
         /*При редактировании производим проверку важности внесенных изменений */
         /* Константы важности внесенных изменений:           */
         /* CHANG_NOTIMPORTANT   - изменения неважные (сохранение без отката операции)*/
         /* CHANG_IMPORTANT      - изменения важны (сохранение изменений возможно только при откате) */
         /* CHANG_NOTKEEP        - не сохранять изменения */
         /* Если возвращаемое значение  > 0, то это оно интерпритируется как номер поля с ошибочным параметром*/
         /* Если возвращаемое значение  = 0, то cчитается, что проверка прошла успешно
            и сохранение изменений можно производить без отката операции      */
                 
      stat = StandartCheckUpdate(TCheckUpdateParam(r_pmpaym, NULL, NULL, r_pmrmprop),
                                 TCheckUpdateParam(r_pmpaym_old, NULL, NULL, r_pmrmprop_old));

  elif( Режим == 7 ) /*ПОМЕЩЕНИЕ ДОКУМЕНТА В ОТЛОЖЕННЫЕ, ОТКАТ ОПЕРАЦИИ*/
  elif( Режим == 8 ) /*ВВОД В ОТЛОЖЕННЫЕ*/
  elif(Режим == OBJ_AFTEREDIT) /* Проверка важности изменений влияющей на необходимость смены опера документа
                                  (изменения в буферах не сохраняются)*/
    return IsImportantChangeForOperPSCashOrder(r_pscshdoc, r_pscshdoc_old, r_pmpaym, r_pmpaym_old, r_pmrmprop, r_pmrmprop_old);
  end;

  return stat;
end;

/* Установка подсказки для оптимизатора ORACLE */

private const Hint_ByValueDate:string = 
"/*+FIRST_ROWS LEADING(pmpaym t pmrmprop oproper oprcurst) INDEX(pmpaym dpmpaym_dbt_idx11) INDEX(t dpscshdoc_dbt_idx0) USE_NL(pmpaym t pmrmprop oproper oprcurst)*/";

private const Hint_ByCloseDate:string = 
"/*+FIRST_ROWS LEADING(pmpaym t pmrmprop oproper oprcurst) INDEX(pmpaym dpmpaym_dbt_idx15) INDEX(t dpscshdoc_dbt_idx0) USE_NL(pmpaym t pmrmprop oproper oprcurst)*/";

private const Hint_ByStatus   :string = 
"/*+FIRST_ROWS LEADING(t pmpaym pmrmprop oproper oprcurst) INDEX(t dpscshdoc_dbt_idx4) INDEX(pmpaym dpmpaym_dbt_idx0) USE_NL(t pmpaym pmrmprop oproper oprcurst)*/";

private const Hint_ByStep     :string = 
"/*+FIRST_ROWS LEADING(oprstep oproper t pmpaym pmrmprop) INDEX(oprstep doprstep_dbt_idx10) INDEX(t dpscshdoc_dbt_idx0) INDEX(pmpaym dpmpaym_dbt_idx0) USE_NL(oprstep oproper t pmpaym pmrmprop)*/";

MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStates:integer ):string

  // Возможные значения ScrolStates:
  //   0 Все
  //   1 Отложенные
  //   2 Открытые
  //   3 Закрытые
  //   4 Отвергнутые
  //   5 Подготовленные
  
  if(  ( ScrolStates == 0 )   // Все
    or ( ScrolStates == 3 ) ) // Закрытые

    var dtflt:PMScrolDateFltr = PMScrolDateFltr();

    if( dtflt.IsSet( DTFL_CLOSEDATE ) )
      return Hint_ByCloseDate;
    elif( dtflt.IsSet( DTFL_VALUEDATE ) )
      return Hint_ByValueDate;
    else
      return Hint_ByCloseDate;
    end;

  elif(  ( ScrolStates == 1 )   // Отложенные
      or ( ScrolStates == 2 )   // Открытые
      or ( ScrolStates == 4 ) ) // Отвергнутые

    return Hint_ByStatus;

  elif ( ScrolStates == 5 ) // Подготовленные

    return Hint_ByStep;

  end;

  return DefaultHint;

END;

MACRO Проверить_Счет_В_Документе( поле ) /*0-счет кассы, 1-счет клиента*/
  var новое_значение_счета:string = "";

  if( поле ) /* счет клиента */
     if( r_pscshdoc.DocKind == 410 ) /* Объявление на взнос наличными в РКО */
       новое_значение_счета = r_pmpaym.ReceiverAccount;
     end;
     if( r_pscshdoc.DocKind == 420 ) /* Чек в РКО */
       новое_значение_счета = r_pmpaym.PayerAccount;
     end;
  else 
     if( r_pscshdoc.DocKind == 410 ) /* Объявление на взнос наличными в РКО */
       новое_значение_счета = r_pmpaym.PayerAccount;
     end;
     if( r_pscshdoc.DocKind == 420 ) /* Чек в РКО */
       новое_значение_счета = r_pmpaym.ReceiverAccount;
     end;
  end;
  return новое_значение_счета;
END;

MACRO Функция_Пользователя( Режим:integer )
 /*
 Возможные значения Режим:
  UFN_PANEL_INPUT(1) - функция вызвана из панели ввода объекта;
  UFN_PANEL_EDIT(2) - функция вызвана из панели корректировки объекта;
  UFN_SCROL(3) - функция вызвана из панели скролинга, единичный вызов, любая корректировка объекта запрещена;
  UFN_SCROL_FMASS(4) - функция вызвана из панели скролинга, работа по нескольким записям, вызов до начала обработки, любая корректировка объекта запрещена;
 
// Пример работы: 
 if( Режим == UFN_SCROL )
   return UPDTPAGE;    // Обновить страницу записей и область скролинга
   //return UPDTREC;     // Обновить текущую запись, загрузив из файла
 end;
 */
 return  0;
END;


