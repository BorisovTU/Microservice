/*
  $Name:        MesForCB_protocol.mac
  $Module:      РКО
  $Description: Протокол процедуры формирования сообщений ЦБ
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : РКО
//
// Описание     : Протокол процедуры формирования сообщений ЦБ
//
// Программист  : Чукина Т.А.
//
// Создан       : 09.12.2015
//
//-----------------------------------------------------------------------------

import "lib_lang.mac", "bnk_ptlib.mac", "likepy.mac", PSInter, BankInter;

private class TTable
  macro PrintHeader
    PureVirtualFunc();
  end;

  macro PrintRow(Params : TArray)
    PureVirtualFunc();
  end;

  macro PrintFooter
    PureVirtualFunc();
  end;
end;

private class (TTable) TTableNS
  macro PrintHeader
  [
    ┌─────────────────────────┬────────────┬──────────────┐
    │ ACCOUNT                 │ OPEN       │ CLOSE        │
  ];
  end;

  macro PrintRow(Params : TArray)
    var Account : string = Params[0],
        DateOpen : date = Params[1],
        DateClose : date = Params[2];
  [ ├─────────────────────────┼────────────┼──────────────┤
    │#########################│ ########## │ ##########   │
  ](Account, DateOpen:f, DateClose:f);
  end;

  macro PrintFooter
  [ └─────────────────────────┴────────────┴──────────────┘
  ];
  end;
end;

private class (TTable) TTableOS
  macro PrintHeader
  [
    ┌─────────────────────────┐
    │ ACCOUNT                 │
  ];
  end;

  macro PrintRow(Params : TArray)
    var Account : string = Params[0];
  [ ├─────────────────────────┤
    │#########################│
  ](Account);
  end;

  macro PrintFooter
  [ └─────────────────────────┘
  ];
  end;
end;

private class (TTable) TTableVS
  macro PrintHeader
  [
    ┌────────────────┬─────────────────────────┬─────────────────────────┬───────────────────┐
    │ UKEY           │ DT_ACCOUNT              │ KT_ACCOUNT              │ SUM               │
  ];
  end;

  macro PrintRow(Params : TArray)
    var UKey : string = Params[0], 
        DtAccount : string = Params[1],
        KtAccount : string = Params[2],
        Sum : money = Params[3];
  [ ├────────────────┼─────────────────────────┼─────────────────────────┼───────────────────┤
    │################│#########################│#########################│###################│
  ](UKey, DtAccount, KtAccount, Sum);
  end;

  macro PrintFooter
  [ └────────────────┴─────────────────────────┴─────────────────────────┴───────────────────┘
  ];
  end;
end;

private macro GetTable(MesType : string) : TTable
  if(MesType == "NS")
    return TTableNS();
  elif(MesType == "OS")
    return TTableOS();
  elif(MesType == "VS")
    return TTableVS();
  end;

  // такой ситуации не должно быть
  RunError("Ошибка программирования: неверное значение параметра MesType");
end;

private macro AppendArray(Dest : TArray, Tail : TArray)
  for(var val : variant, Tail)
    Dest[ Dest.size ] = val;
  end;
end;

private FILE TmpFile() txt;

private class TProtocolMesForCB
  private var 
    IsExtProtocol : bool,
    Warnings : TArray,
    Table : TTable,
    LastRowParams : TArray,
    LastWarnings : TArray;

  private var
    OldOutput,
    IsTempOutput : bool;

  private macro Init(p_IsExtProtocol : bool)
    IsExtProtocol = p_IsExtProtocol;

    Warnings = TArray();
    Table = null;
    LastRowParams = TArray();
    LastWarnings = TArray();

    IsTempOutput = false;
  end;

  private macro PrintFltParm(FltParmName : string, F : string, FltVal : variant)

    if(F)
      var S : string = FltParmName + " " +
                       IfThenElse(F == "=", F, "!=") + " " +
                       FltVal;
  [   #
  ](S);
    end;

  end;

  private macro PrintReportHeader(MapVal : PSRepMap)
    var ReqNum : string = MapVal.Value("ReqNum"),
        ReqDate : date = MapVal.Value("ReqDate"),
        DepStr : string = "",
        Sub : string = IfThenElse( MapVal.Value("SubBranch"), "с подчиненными", "" ),
        DateBeg : date = MapVal.Value("DateBeg"),
        DateEnd : date = MapVal.Value("DateEnd");
  [
    Протокол процедуры формирования сообщений по заявке ЦБ РФ
  ];

    if( ReqNum or (ReqDate != date(0,0,0)) )
  [
    Данные заявки ЦБ РФ
  ];
    end;

    if(ReqNum)
  [   Номер: #
  ](ReqNum);
    end;

    if(ReqDate != date(0,0,0))
  [   Дата: #
  ](ReqDate : f);
    end;

    CB_GetDepartmentCodeAndName( MapVal.Value("Dep"), DepStr, null );
    DepStr = DepStr + " " + Sub;
  [
    Параметры фильтрации данных
      Узел ТС: #
      Период: с ########## по ##########
  ](DepStr, DateBeg:f, DateEnd:f);

    if(MapVal.Value("IsOperRest"))
  [   Только счета, по которым выполнялись операции или числился остаток
  ];
    end;

    if( MapVal.Value("IncludeAccGK") )
  [
    Счета Главной книги:
  ];
      PrintFltParm("Глава", MapVal.Value("GKFltByChapter"), MapVal.Value("GKChapter"));
      PrintFltParm("Валюта", MapVal.Value("GKFltByFIID"), MapVal.Value("GKFICode"));
      PrintFltParm("Счет", MapVal.Value("GKFltByAcc"), MapVal.Value("GKAccMask"));
      PrintFltParm("Клиент", MapVal.Value("GKFltByClient"), MapVal.Value("GKClientCode"));
    end;

    if( MapVal.Value("IncludeAccBO") )
  [
    Счета подсистем(ы) #
  ]( join( MapVal.Value("RegBranchesBO"), ", " ) + ":" );

      PrintFltParm("Валюта", MapVal.Value("BOFltByFIID"), MapVal.Value("BOFICode"));
      PrintFltParm("Счет", MapVal.Value("BOFltByAcc"), MapVal.Value("BOAccMask"));
      PrintFltParm("Клиент", MapVal.Value("BOFltByClient"), MapVal.Value("BOClientCode"));
    end;

  [
    Дата выполнения процедуры: #
    Исполнитель: #

    Сформированы электронные файлы:
  ]( date():f, string({oper}, " ", GetNameOper({oper})) );
  end;

  private macro SetMesType(NewMesType : string)
    Table = GetTable(NewMestype);
  end;

  private macro PrintTableHeader
    if(IsExtProtocol)
      Table.PrintHeader();
    end;
  end;

  private macro PrintTableFooter
    if(IsExtProtocol)
      Table.PrintFooter();
    end;
  end;

  private macro PrintWarnings
    if( Warnings and ( Warnings.size > 0 ) )
      println("Некритичные ошибки и предупреждения:");
      for(var s : string, Warnings)
        println(s);
      end;
    end;
  end;

  macro PrintProcError( s : string )
    println( "Данные выгружены частично. При выполнении процедуры возникла ошибка: " + s );
  end;

  macro PrintTableRow(/*...*/)
    if(IsExtProtocol)

      var parm : variant;
      var i : integer = 1;

      LastRowParams = TArray();
      while( GetParm(i, parm) )
        LastRowParams[ LastRowParams.size ] = parm;
        i = i + 1;
      end;
      
    end;
  end;

  macro CommitLastRow

    if(IsExtProtocol and LastRowParams.size)
      Table.PrintRow(LastRowParams);
      LastRowParams = TArray();
    end;

    AppendArray(Warnings, LastWarnings);
    LastWarnings = TArray();
  end;

  macro RollbackLastRow

    if(IsExtProtocol)
      LastRowParams = TArray();
    end;

    LastWarnings = TArray();
  end;

  macro AddLogMessage(Msg : string)
    LastWarnings[ LastWarnings.size ] = Msg;
  end;

  macro Start(MapVal : PSRepMap)
    Init(MapVal.Value("IsExtProtocol"));

    PrintReportHeader(MapVal);
  end;

  macro NewFile(NewMesType : string, NewFileName : string)
    SetMesType(NewMesType);

  [
    #
  ](NewFileName);

    PrintTableHeader();
  end;

  macro FinishCurrentFile
    PrintTableFooter();

    PrintWarnings();
    Warnings = TArray();
  end;

  /*Перенаправляем вывод текста во временный файл*/
  macro SetTempOutput
    var TmpFileName = GetTxtFileName("tmpmscbprotocol");
    OldOutput = SetOutPut( TmpFileName );

    IsTempOutput = true;

    if(not open( TmpFile, TmpFileName ) )
      RunError( String("Файл не открыт:", "|", TmpFileName) );
    end;
  end;

  /*Перенаправляем вывод обратно*/
  macro RestoreOutput
    if(IsTempOutput)
      close(TmpFile);
      SetOutPut( OldOutput, true );
      IsTempOutput = false;
    end;
  end;

  /*Выводим данные из временного файла в первоначальный поток вывода*/
  macro TransferFromTempOutput
    RestoreOutput();

    rewind(TmpFile);
    while( next(TmpFile) )
      println(TmpFile.str);
    end;
  end;

end;

private var Protocol : TProtocolMesForCB = TProtocolMesForCB();

macro MesForCB_GetProtocolObj : TProtocolMesForCB
  return Protocol;
end;
