// ----------------------------------------------------------------------------
// Печать протокола работы процедуры "Массовая оплата картотеки №2"
// ----------------------------------------------------------------------------
import globals, oralib, gstname;

private macro MakeAccountState( I2PaidSum:money, HasArrest:string, FreeAmount:money )
  var Result:string = "",
      Delim:string = "";

  if( I2PaidSum )
    Result = string( Result, Delim, "Выполнена оплата на сумму ", I2PaidSum:0:2 );
    Delim  = "\n";
  end;

  if( HasArrest == "X" )
    Result = string( Result, Delim, "На счет наложено ограничение операций" );
    Delim  = "\n";
  elif( not FreeAmount )
    Result = string( Result, Delim, "Остаток на счете исчерпан" );
    Delim  = "\n";
  end;

  return Result;
end;

/* Печать документов пока не дойдём в выборке до нового счёта или до конца */
private macro PS_I2PayLogPrintOneAccount( rs:RsdRecordset, IsCurIndex:bool ):bool

  var PartPaymReqv:string;
  var LastPayerAccount:string;
  var PaymentID:integer;
  var PaymStatus;
  var DocKind:integer = 0;
  var AccSum:money = $0;
  var PayerAccount:string = rs.Value( "t_MainPayerAccount" );
  var Result:bool = false;
  var saveMainPaynentID:integer = 0;
  var savePartPaynentID:integer = 0;

  [
   ####################### ####################################################### ]
  ( PayerAccount, rs.Value( "t_MainPayerName" ):w );
  
  while( true )
    
    if( saveMainPaynentID != rs.Value("t_MainPaymentID") )
  [------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------];
    end;

    AccSum = AccSum + money( rs.Value( "t_PayerAmount" ) );

    PartPaymReqv = "";
    if( rs.Value("t_PartPaymentID") )
      PartPaymReqv = string( rs.Value("t_PartNumber"), "/", 
                             sqlDate2date( rs.Value("t_PartValueDate") ), "/", 
                             int( rs.Value("t_PartNumberPack") ) );
      PaymentID = rs.Value("t_PartPaymentID");
      DocKind = rs.Value("t_PartDocKind");
      PaymStatus = rs.Value( "t_PartPaymStatus" );
    else
      PaymentID = rs.Value("t_MainPaymentID");
      DocKind = rs.Value("t_MainDocKind");
      PaymStatus = rs.Value( "t_MainPaymStatus" );
    end;

    if( rs.Value("t_MainPaymentID") != saveMainPaynentID ) // Если первая строка по документу, то печатаем все реквизиты
      if( IsCurIndex )
  [############ #### ########## ############### ################ ####### ####################### ##################### ############# ############################### ########################]
     ( rs.Value( "t_MainNumber"    ),
       rs.Value( "t_MainShifrOper" ),
       sqlDate2date( rs.Value( "t_MainDate" ) ),
       money( rs.Value( "t_PayerAmount" ) ):10:2,
       money( rs.Value( "t_BaseAmount" ) ):10:2,
       int( rs.Value( "t_MainPriority" ) ),
       rs.Value( "t_MainReceiverAccount"  ),
       rs.Value( "t_MainReceiverBankName" ):w,
       rs.Value( "t_MainBankCode"         ),
       PartPaymReqv,
       GetStepNameExecuteRead( PaymentID, DocKind, PaymStatus ):w );
      else
  [############ #### ########## ############### ####### ####################### ##################### ############# ############################### ########################]
     ( rs.Value( "t_MainNumber"    ),
       rs.Value( "t_MainShifrOper" ),
       sqlDate2date( rs.Value( "t_MainDate" ) ),
       money( rs.Value( "t_PayerAmount" ) ):10:2,
       int( rs.Value( "t_MainPriority" ) ),
       rs.Value( "t_MainReceiverAccount"  ),
       rs.Value( "t_MainReceiverBankName" ):w,
       rs.Value( "t_MainBankCode"         ),
       PartPaymReqv,
       GetStepNameExecuteRead( PaymentID, DocKind, PaymStatus ):w );
      end;
    else // Если есть по платежу вторая строка, то => есть разноска у основного платежа
      
      if( rs.Value("t_PartPaymentID") ) // Есть частичная оплата
        if( rs.Value("t_PartPaymentID") != savePartPaynentID ) 
        // частичная оплата ордерами
          if( IsCurIndex )
  [                             ############### ################         #######################                                     ############################### ########################]
     ( money( rs.Value( "t_PayerAmount" ) ):10:2,
       money( rs.Value( "t_BaseAmount" ) ):10:2,
       rs.Value( "t_MainReceiverAccount"  ),
       PartPaymReqv,
       GetStepNameExecuteRead( PaymentID, DocKind, PaymStatus ):w );
          else
  [                             ###############         #######################                                     ############################### ########################]
     ( money( rs.Value( "t_PayerAmount" ) ):10:2,
       rs.Value( "t_MainReceiverAccount"  ),
       PartPaymReqv,
       GetStepNameExecuteRead( PaymentID, DocKind, PaymStatus ):w );
          end;
        else
        // Частичная оплата одним ордером с разноской
          if( IsCurIndex )
  [                             ###############                          ####################### ]
     ( money( rs.Value( "t_PayerAmount" ) ):10:2,
       rs.Value( "t_MainReceiverAccount"  ) );
          else
  [                             ###############         ####################### ]
     ( money( rs.Value( "t_PayerAmount" ) ):10:2,
       rs.Value( "t_MainReceiverAccount"  ) );
          end;
        end;
      else 
      // Нет частичной оплаты => это полная оплата платежа с разноской
        if( IsCurIndex )
  [                             ###############                          ####################### ]
     ( money( rs.Value( "t_PayerAmount" ) ):10:2,
       rs.Value( "t_MainReceiverAccount"  ) );
        else
  [                             ###############         ####################### ]
     ( money( rs.Value( "t_PayerAmount" ) ):10:2,
       rs.Value( "t_MainReceiverAccount"  ) );
        end;
      end;
    end;
    
    saveMainPaynentID = rs.Value("t_MainPaymentID");
    savePartPaynentID = rs.Value("t_PartPaymentID");

    if( not rs.moveNext() )
      break;
    end;

    if( rs.Value( "t_MainPayerAccount" ) != PayerAccount )
      Result = true;
      break;
    end;
  end;

  [------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
   Итого:                  ####################
  ] ( AccSum:0:2 );

  return Result;

end;

/* Печать лога массовой оплаты К2 */
macro PS_I2PayLogPrint( IsCurIndex:bool )

  var query_documents:string = 
  "select * "
    "from( select docs1.*, "
               "decode(docs1.t_PartPayerAmount, 0, decode(nvl(mainpi.t_PmAmount, 0), 0, docs1.t_MainPMPayerAmount, mainpi.t_PmAmount), docs1.t_PartPayerAmount) as t_PayerAmount, "
               "decode(docs1.t_PartPMBaseAmount, 0, docs1.t_MainPMBaseAmount, docs1.t_PartPMBaseAmount) as t_BaseAmount, "
               "nvl(docs1.t_PartReceiverAccount, nvl(mainpi.t_Account, docs1.t_MainPMReceiverAccount)) as t_MainReceiverAccount "
          "from ( select mainpm.t_PaymentID as t_MainPaymentID, "
                        "mainrm.t_Number    as t_MainNumber, "
                        "mainrm.t_PayerName as t_MainPayerName, "
                        "mainrm.t_ShifrOper as t_MainShifrOper, "
                        "mainrm.t_Date      as t_MainDate, "
                        "decode( nvl(partpi.t_PmAmount, 0), 0, nvl( partpm.t_Amount, 0), partpi.t_PmAmount ) as t_PartPayerAmount, "
                        "nvl( partpm.t_BaseAmount, 0 ) as t_PartPMBaseAmount, "
                        "mainrm.t_Priority  as t_MainPriority, "
                        "mainpm.t_PayerAccount  as t_MainPayerAccount, "
                        "mainpm.t_DocKind as t_MainDocKind, "
                        "nvl( partpm.t_DocKind, 0 ) as t_PartDocKind, "
                        "nvl( partpi.t_Account, partpm.t_ReceiverAccount ) as t_PartReceiverAccount, "
                        "nvl( recbnk.t_Name, mainrm.t_ReceiverBankName ) as t_MainReceiverBankName, "
                        "nvl( recbnkcod.t_Code, maincr.t_BankCode ) as t_MainBankCode, "
                        "nvl( partpm.t_PaymentID, 0 ) as t_PartPaymentID, "
                        "nvl( partrm.t_Number, CHR(1) ) as t_PartNumber, "
                        "nvl( partpm.t_ValueDate, to_date( '01010001', 'ddmmyyyy' ) ) as t_PartValueDate, "
                        "nvl( partpm.t_NumberPack, 0 ) as t_PartNumberPack, "
                        "decode(partpi.t_PaymentID, null, decode(partpm.t_PaymentID, null, 1, -1), -1) as t_PartPIDebetCredit, "
                        "mainpm.t_BaseAmount as t_MainPMBaseAmount, "
                        "mainpm.t_Amount as t_MainPMPayerAmount, "
                        "mainpm.t_ReceiverAccount as t_MainPMReceiverAccount, "  +
                        "mainpm.t_PaymStatus  as t_MainPaymStatus, "
                        "partpm.t_PaymStatus  as t_PartPaymStatus "
                   "from table( PS_I2PAYLOG.GetDocuments() ) docs "
                  "inner join dpmpaym_dbt   mainpm on mainpm.t_PaymentID = docs.t_MainPaymentID "
                  "inner join dpmrmprop_dbt mainrm on mainrm.t_PaymentID = docs.t_MainPaymentID "
                  "inner join dpmprop_dbt   maincr on maincr.t_PaymentID = docs.t_MainPaymentID "
                                                 "and maincr.t_DebetCredit = 1 "
                   "left join dpmpaym_dbt   partpm on partpm.t_PaymentID = docs.t_PartPaymentID "
                   "left join dpmrmprop_dbt partrm on partrm.t_PaymentID = docs.t_PartPaymentID "
                   "left join dpmaddpi_dbt  partpi on partpi.t_PaymentID = docs.t_PartPaymentID "
                                                 "and partpi.t_DebetCredit = 1 "
                   "left join dparty_dbt    recbnk on recbnk.t_PartyID = mainpm.t_ReceiverBankID "
                   "left join dpartcode_dbt recbnkcod on recbnkcod.t_PartyID = recbnk.t_PartyID "
                                                 "and recbnkcod.t_CodeKind = 3 "
                 "order by mainpm.t_PayerAccount, mainrm.t_Priority, mainpm.t_ValueDate, mainpm.t_PaymentID) docs1, dpmaddpi_dbt  mainpi "
          "where mainpi.t_PaymentID(+) = docs1.t_MainPaymentID "
            "and mainpi.t_DebetCredit(+) = docs1.t_PartPIDebetCredit) docs2 "
    "where docs2.t_BaseAmount > 0 ";

  [Протокол работы процедуры "Массовая оплата документов картотеки №2"

   #
   Дата выполнения:  ##########
   Исполнитель:      #
   
   Выполнена оплата документов:]
   ({Name_Bank}, {curdate}, {Name_Oper});
   
   if( IsCurIndex )
  [------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
   № документа |Шифр|  Дата    |Сумма в валюте | Сумма в валюте |Очеред-| Счет получателя       | Банк получателя     | БИК банка   |  Реквизиты частичной оплаты   |  Находится на шаге       
               |док |          |сч. плательщ.  |    документа   |ность  |                       |                     | получателя  |  (номер/дата значения/пачка)  |                          
   ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------];
  else
  [-------------------------------------------------------------------------------------------------------------------------------------------------------------------------
   № документа |Шифр|  Дата    |Сумма в валюте |Очеред-| Счет получателя       | Банк получателя     | БИК банка   |  Реквизиты частичной оплаты   |  Находится на шаге       
               |док |          |сч. плательщ.  |ность  |                       |                     | получателя  |  (номер/дата значения/пачка)  |                          
   -------------------------------------------------------------------------------------------------------------------------------------------------------------------------];
  end;

  var rs:RsdRecordset = execSQLselect( query_documents );

  if( rs.moveNext() )
    while( PS_I2PayLogPrintOneAccount( rs, IsCurIndex ) )
      ;
    end;
  end;
  
  [
  
   Состояние счетов картотеки №2
   -----------------------------------------------------------------------------------------------------------------------------------------------
   Счет плательщика       |  Наименование          | Сумма доступная | Сумма документов   |     Состояние счета                                   
                          |                        | для списания    | в картотеке 2      |                                                       
   -----------------------------------------------------------------------------------------------------------------------------------------------  ];

  var query_accounts :string = 
  "select logac.t_Account, pt.t_Name, logac.t_FreeAmount, logac.t_I2DocsSum, logac.t_HasArrest, logac.t_I2PaidSum "
  "from table( PS_I2PAYLOG.GetAccounts() ) logac, "
       "daccount_dbt ac, "
       "dparty_dbt pt "
  "where ac.t_Code_Currency = logac.t_FIID "
    "and ac.t_Chapter = logac.t_Chapter "
    "and ac.t_Account = logac.t_Account "
    "and pt.t_PartyID = ac.t_Client "
  "order by logac.t_Account";

  rs = execSQLselect( query_accounts );
  while( rs.moveNext() )
    [####################### ######################## ################# #################### #######################################################]
    (
      rs.Value( "t_Account" ):m,
      rs.Value( "t_Name"    ):w,
      money( rs.Value( "t_FreeAmount" ) ):14:2,
      money( rs.Value( "t_I2DocsSum"  ) ):14:2,
      MakeAccountState( rs.Value( "t_I2PaidSum" ), rs.Value( "t_HasArrest" ), rs.Value( "t_FreeAmount" ) ):w
    );
  end;

  [-----------------------------------------------------------------------------------------------------------------------------------------------];

end;