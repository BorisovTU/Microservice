/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : exprq.mac                                          */
/*                                                                      */
/*  Описание       : Формирование файла с сообщением в ГНИ.             */
/*                                                                      */
/*  Программист    : Дроздов И.Е.                                       */
/*                                                                      */
/*  Создан         : 18.10.04                                           */
/*                                                                      */
/************************************************************************/

IMPORT globals, PTInter, CTInter, OprInter, PSInter, likepy;
import rslx, prpm, gnd120p, SFInter;

RECORD tax(party);
FILE   reg(objrgdoc);
FILE   acc1("account");   
FILE   cert("certif") key 2;
FILE   sc(sfcontr);
FILE   pc(partcode);
FILE   client(party);
FILE   adr (adress);

var prpart   :TRecHandler = TRecHandler("party.dbt",    "bank.def");
var ro       :TRecHandler = TRecHandler("reqopena.dbt", "bank.def");
var rc       :TRecHandler = TRecHandler("reqclosa.dbt", "bank.def");

var ExportFileName:string;
var TxtDir:string;
var refer = "";
var BankNameOfficer:string, Officer:string, PhoneNumber:string;
FILE fileexport () txt write;

/*-----------------------------------------------*/
/* Класс для добавления SQL-фильтра к таблице    */
/* и последующего гарантированного его удаления  */
/* (скопирован из MC_lib.mac  :) )               */
/*-----------------------------------------------*/
PRIVATE CLASS TAddFilter( bfile:TBFile, filterStr:string )
  private var m_bfile:TBFile = bfile;
  m_bfile.AddFilter( filterStr );
  MACRO destructor()
    m_bfile.DropFilter();
  END;
END;

// поля файла для заполнения
var ИдФайл:string, ТипИнф:string, ИдПол:string,
    КолДок:string, ВерсФорм:string, ИННКО:string,
    КППКО:string, БИК:string, НаимКО:string,
    АдрКО:string, ТелОтпр:string, ДолжнОтпр:string,
    ФИООтпр:string, ИдДок:string, ИНННП:string,
    КППНП:string, ОГРН:string, КИО:string,
    КодНОНП:string, СвидНУ:string, НомСооб:string,
    ДатаСооб:string, НомСч:string, ДатаОткрСч:string,
    КодВСч:string, КодСостСч:string, /*ДатаЗакрСч:string,*/
    НомерДог:string, ДатаДог:string, СрокДеп:string;

// по счету находит чековую книжку
MACRO ПолучитьНомерИзвещения(Code_Currency,Chapter,Account)
   VAR ercode, stat = false;
    cert.FIID  = AccNoteFIID;
    cert.ConnAccount  = Account;
    cert.Series = cert.NumberFirst = "";

    stat = GetGE(cert);
    if( (not stat) )
      ercode = status();
      if( (ercode != 4) and (ercode!=9) )
        MsgBox("Ошибка при определении номера извещения");
        cert.Series = cert.NumberFirst = "";
        return stat;
      end;
    end;

    if((not stat)or(cert.FIID != AccNoteFIID)or(cert.ConnAccount != Account))

      acc1.Chapter = Chapter;
      acc1.Code_Currency = Code_Currency;
      acc1.Account = Account;
      if(not GetEQ(acc1))
        cert.Series = cert.NumberFirst = "";
        ercode = status();
        if( (ercode != 4) and (ercode!=9) )
          MsgBox("Ошибка при определении счета");
          return stat;
        end;
      else
        if(SetAccNoteNumber(Account, cert.Series, cert.NumberFirst ) == 0)  
          stat = true;
        end;
      end;
    end;
  return stat;
END;
// просто SQL-запрос не пронимает обычную дату 
macro strDateForSQL(dt:date):string
  return "TO_DATE('" + StrSubst(string(dt:o), ".", "-") + "', 'DD-MM-YYYY')";
end;

MACRO ПолучитьРеквизитыРегистрации(PARTYID,DK:integer, reg)
  VAR ercode;
  VAR OK:bool;
  var reg1:TBFile = TBFile("objrgdoc.dbt","R",0,"objrgdoc.dbt","bank.def");
  ClearRecord(reg);
  ClearRecord(reg1);
  reg1.rec.RegPartyID   = PARTYID;    
  reg1.rec.IsClosed = "";
  reg1.rec.RegPartyKind = 7;
  var AddFltr:TAddFilter = TAddFilter(reg1,string("t_StartDate < ",strDateForSQL({curdate})," and t_FinishDate > ",strDateForSQL({curdate}) ) );
  if(reg1.GetGE())
        Copy(reg,reg1);
        return true;
      end;
END;

//заполнение данных для каждого счета
MACRO ПолучитьГНИ(RP, tax) //PARTY
  FILE party1(party) key 0;
  ClearRecord(party1);
  ClearRecord(tax);
  if(RP)
    party1.PartyID = RP;
    if(not GetEQ(party1))
      return false;
    else
      COPY(tax,party1);
      return true;
    end;
  end;
END;

MACRO ПолучитьДоговор(Account,Code_Currency)  //SFCONTR
  FILE sc1(sfcontr) key 1;
  var d,m,y;
  ClearRecord(sc); 
  ClearRecord(sc1);
  sc1.Object = Account;
  sc1.FIID = Code_Currency;
  sc1.ObjectType = SF_ACCOUNT;
  sc1.ServKind = PTSK_PAY;
  sc1.ServKindSub = 0;
  if(GetEQ(sc1));
    COPY(sc,sc1);
  end;
END;

MACRO НайтиАдресс(PARTYID) //ADRESS
  ClearRecord(adr);
  adr.PartyID = PARTYID;
  adr.Type = 1; //ищем юридический адресс
  АдрКО = "";
  if(GetEQ(adr))
    АдрКО = string(adr.Country,",",adr.PostIndex,",",adr.Region,",,",adr.Place,",,",adr.Street,",",adr.House,",",adr.NumCorps,",",adr.Flat);
  end;
END;

MACRO ОткрытьФайл()
  var d,m,y,;
  var BINN,BKPP;
  DateSplit({curdate},d,m,y);
  splitINN_KPP(GetPartyINN({OurBank},1), BINN, BKPP);
  GenerateReference(84, refer);
  //полное имя файла с директорией
  ExportFileName = string(TxtDir,"\\SBC",BINN,"__",BKPP,y:o:4,m:o:2,d:o:2,refer,".txt");
  Open(fileexport, ExportFileName);
END;

MACRO ЗакрытьФайл()
  Close(fileexport);
END;

MACRO ЗаполнитьДанные(КОД, СЧЕТ)
  var d,mo,y,h,mi,s;
  var BINN,BKPP;
  DateSplit({curdate},d,mo,y);
  splitINN_KPP(GetPartyINN({OurBank},1), BINN, BKPP);
  TimeSplit(Time(),h,mi,s);
  ИдФайл = string(BINN,"**",BKPP,y:o:4,mo:o:2,d:o:2,h:o:2,mi:o:2,s:o:2,refer);
  ТипИнф = "СООБЩЕНИЕБАНКА";
  ПолучитьРеквизитыРегистрации({OurBank},14, reg);
  //ДатаЗакрСч = acc1.Close_Date;
  ПолучитьНомерИзвещения(КОД,1,СЧЕТ);
  НомСооб = string(cert.Series,cert.NumberFirst);
  ИдПол = ПолучитьКодСубъекта(reg.PtRegParty,28);
  КолДок = "1";
  ВерсФорм = "2.01";
  ИННКО = BINN;
  КППКО = BKPP;
  БИК = {MFO_Bank};
  НаимКО = {Name_Bank};
  НайтиАдресс(prpart.rec.PartyID);   
  ТелОтпр = PhoneNumber;
  ДолжнОтпр = Officer;
  ФИООтпр = BankNameOfficer;
  ИдДок = string(BINN,"**",BKPP,y:o:4,"000001");
  splitINN_KPP(ПолучитьКодСубъекта(prpart.rec.PartyID,PTCK_INN),ИНННП,КППНП);
  ОГРН = ПолучитьКодСубъекта(prpart.rec.PartyID,PTCK_OGRN);
  КИО = "";
  ПолучитьГНИ(reg.PtRegParty, tax);
  ПолучитьРеквизитыРегистрации(prpart.rec.PartyID,14, reg);
  КодНОНП = ПолучитьКодСубъекта(reg.PtRegParty,PTCK_MNS);
  СвидНУ = string(reg.Series,",",reg.Number,",",reg.RegDate);
  ДатаСооб = string(d:o:2,".",mo:o:2,".",y:o:4);
  НомСч = string(СЧЕТ);
  ДатаОткрСч = string(acc1.Open_Date);
  if(КОД==NATCUR)
    КодВСч = "0";
  else
    КодВСч = "1";
  end;
  КодСостСч = "0";
  ПолучитьДоговор(СЧЕТ,КОД);
  НомерДог = sc.Number;
  ДатаДог = sc.dateBegin;
  СрокДеп = "";
END;

MACRO ЗаполнитьФайл()
[│####################│##################################################### │
](НомСч,ExportFileName);
//служебная часть
  //общие сведения служебной части 
  Insert(fileexport,"ИдФайл: "+ИдФайл);
  Insert(fileexport,"ТипИнф: "+ТипИнф);
  Insert(fileexport,"ИдПол: "+ИдПол);
  Insert(fileexport,"КолДок: "+КолДок);
  Insert(fileexport,"ВерсФорм: "+ВерсФорм);
  Insert(fileexport,"###EOL");
  //сведения об отправителе
  Insert(fileexport,"ИННКО: "+ИННКО);
  Insert(fileexport,"КППКО: "+КППКО);
  Insert(fileexport,"БИК: "+БИК);
  Insert(fileexport,"НаимКО: "+НаимКО);
  Insert(fileexport,"АдрКО: "+АдрКо);
  Insert(fileexport,"ТелОтпр: "+ТелОтпр);
  Insert(fileexport,"ДолжнОтпр: "+ДолжнОтпр);
  Insert(fileexport,"ФИООтпр: "+ФИООтпр);
  Insert(fileexport,"###EOL");
  Insert(fileexport,"@@@EOL");
//информационная часть
  //общие сведения о налогоплательщике
  Insert(fileexport,"ИдДок: "+ИдДок);
  Insert(fileexport,"ИНННП: "+ИНННП);
  Insert(fileexport,"КППНП: "+КППНП);
  Insert(fileexport,"ОГРН: "+ОГРН);
  Insert(fileexport,"КИО: "+КИО);
  Insert(fileexport,"КодНОНП: "+КодНОНП);
  Insert(fileexport,"СвидНУ: "+СвидНУ);
  Insert(fileexport,"###EOL: ");
  //сведения о счете
  Insert(fileexport,"НомСооб: "+НомСооб);
  Insert(fileexport,"ДатаСооб: "+ДатаСооб);
  Insert(fileexport,"НомСч: "+НомСч);
  Insert(fileexport,"ДатаОткрСч: "+ДатаОткрСч);
  Insert(fileexport,"КодВСч: "+КодВСч);
  Insert(fileexport,"КодСостСч: "+КодСостСч);
  //Insert(fileexport,"ДатаЗакрСч: "+ДатаЗакрСч);
  Insert(fileexport,"НомерДог: "+НомерДог);
  Insert(fileexport,"ДатаДог: "+ДатаДог);
  Insert(fileexport,"СрокДеп: "+СрокДеп);
  Insert(fileexport,"###EOL");
  Insert(fileexport,"@@@EOL");
  Insert(fileexport,"===EOL");
END;

MACRO НапечататьСвязанныйСчет(Code_Currency,Account)
  ОткрытьФайл();
  ЗаполнитьДанные(Code_Currency,Account);
  ЗаполнитьФайл();
  ЗакрытьФайл();
  return true;
END;

MACRO ImportTXTFile(DocKind:integer, G:integer)
  FILE LinkAcc(reqlinka);
  var stat,flag;
  var errcode;  
  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, TxtDir, errcode);
  stat = PutPersData(BankNameOfficer, Officer, PhoneNumber);
  if(stat == -27)
    return true;
  end;
  [┌────────────────────┬──────────────────────────────────────────────────────┐
   │ НОМЕР СЧЕТА        │ ИМЯ ФАЙЛА                                            │                    │
   ├────────────────────┼──────────────────────────────────────────────────────┤
  ];
  //для открываемых
  if(DocKind == 230)
    if(G == -1)//для всех
      ОткрытьФайл();
      ЗаполнитьДанные(ro.rec.Code_Currency,ro.rec.Account);
      ЗаполнитьФайл();
      ЗакрытьФайл();
      //файлы для связанных счетов
      LinkAcc.DocKind   = PS_REQOPENA;
      LinkAcc.RequestID = ro.rec.RequestID;
      LinkAcc.GroupID   = 0;
      flag = GetGE(LinkAcc);
      while( (flag) and 
             (LinkAcc.RequestID == ro.rec.RequestID) and 
             (LinkAcc.DocKind == PS_REQOPENA) )
        if(not НапечататьСвязанныйСчет(LinkAcc.Code_Currency, LinkAcc.Account)) 
          return false; 
        end;
        flag = next(LinkAcc);
      end;
    end;
    if(G > 0)
      LinkAcc.DocKind   = PS_REQOPENA;
      LinkAcc.RequestID = ro.rec.RequestID;
      LinkAcc.GroupID   = G;
      if(not GetEQ(LinkAcc))
        MsgBox("Не найден связанный счет");
        return false;
      else
        if(not НапечататьСвязанныйСчет(LinkAcc.Code_Currency, LinkAcc.Account)) 
          return false; 
        end;
      end;      
    end;
  end;
  //для закрытых
  if((G == 0) and (DocKind == 231))
    ОткрытьФайл();
    ЗаполнитьДанные(rc.rec.Code_Currency,rc.rec.Account);
    ЗаполнитьФайл();
    ЗакрытьФайл();
  end;
[└────────────────────┴──────────────────────────────────────────────────────┘
]
END;