/*
$Name:             pm067_20.mac
$Module:           Платежи
$Description:      Макрос шага
*/

//-----------------------------------------------------------------------------
// Блок      : 29067 - "Ожидание даты исполнения"
// Шаг       : 20    - "Ожидание даты исполнения"
//-----------------------------------------------------------------------------

import "globals.mac", PaymInter, OprInter, "pm_categ.mac", "cbsttls.mac", "pm_reserve.mac";

var PaymentObj: RsbPayment;

macro ConfWin2(Msg, Default, Buttons)
  array text;
  text(0) = Msg;
  var parm;
  array button;
  var i = 2;
  while( GetParm(i, parm) )
    button(asize(button)) = parm;
    i = i + 1;
  end;

  return ConfWin(text, button, Default);

end;
    
macro NeedToWaitEx()
  var value;
  var stat = 0;
  GetRegistryValue("PS\\FUTURE_DATE_PAYMENT\\USEINDWAITEXEC", V_BOOL, value);
  if( ((PaymentObj.DocKind == PS_PAYORDER) or 
      (PaymentObj.DocKind == PS_CPORDER) or  
      (PaymentObj.DocKind == PS_INRQ)) and
      (PaymentObj.ValueDate > {curdate}) and
      (value == true))
    stat = 1;

  end;

  return stat;
    
end;

// ----------------------------------------------------------------------------
// Выполнение шага
// ----------------------------------------------------------------------------
macro ExecuteStep( Kind_Operation, first, KindDoc, ID_Operation )

  if (PaymentObj.ValueDate != {curdate})
    var val = 0;
    if (IsOprMultiExec())
      var regval;
      if (GetRegistryValue("PS\\FUTURE_DATE_PAYMENT\\WITHDRAWNOEQDATE", V_STRING, regval) != V_UNDEF)
        if (regval == "ДЗ")
          if(not NeedToWaitEx())
            val = 2;
          else 
            msgbox("Дата значения не наступила. Списание с очереди ожидающих исполнения датой значения невозможно");
            return 1;
          end;
        elif (regval == "НП")
          msgbox("Дата значения не совпадает. Укажите дату списания с очереди ожидающих исполнения в единичном режиме");
          val = 1;
        else
          msgbox("Введено не корректное значение настройки");
          val = 1;
        end;
      end;
    else
      if(not NeedToWaitEx())
        val = ConfWin2("Дата значения платежа " + PaymentObj.ValueDate + " не совпадает с текущим операционным днем", 0,
                       "Провести текущим днем",
                       "Прервать",
                       "Провести датой значения");      
      else     
        val = ConfWin2("Дата значения платежа " + PaymentObj.ValueDate + " не совпадает с текущим операционным днем", 0,
                       "Провести текущим днем",
                       "Прервать");  
      end;
    end;
    if (val == 1)
      return 1;
    end;
    if (val == 0)
      PaymentObj.ValueDate = {curdate};
    end;
  end;

  if( PaymentObj.Actuate() )
    MsgBox("Ошибка при актуализации платежа");
    return 1;
  end;

  // Выполнить проводку
  var paymtr = PaymentObj.MakeTransaction();

  if( paymtr == NULL )
    MsgBox("Ошибка при создании проводки по платежу");
    return 1;
  end;

  paymtr.Chapter         = 3;                                            
  paymtr.Date_Carry      = PaymentObj.ValueDate;
  paymtr.Number_Pack     = PaymentObj.NumberPack;
  paymtr.Numb_Document   = PaymentObj.Number;
  paymtr.ResultCarry     = OBIWEOUTCARRY;
  paymtr.Kind_Oper       = " 1";
  paymtr.Ground          = "Снятие с ожидания исполнения распоряжения №" + PaymentObj.Number + " от " + PaymentObj.ValueDate + " к счету " + PaymentObj.PayerAccount + ".";
  paymtr.Department      = PaymentObj.Department;

  paymtr.FIIDPayer       = NATCUR; 
  paymtr.FIIDReceiver    = NATCUR;
  paymtr.AccountPayer    = NotBalCorrAcc_FirstDoc( "П" ).FindAndOpenSysAccount( "ВнебалСчетКорресп", 0, paymtr.Date_Carry );
  paymtr.AccountReceiver = TIndexWaitExec(PaymentObj).FindAndOpenSysAccount( "Ожид. Исполн.", 0, paymtr.Date_Carry );

  if( ConvSum( paymtr.SumPayer, PaymentObj.FutureBaseAmount, paymtr.Date_Carry, PaymentObj.BaseFIID, NATCUR ) )
    msgbox("Ошибка конвертации суммы");
    return 1;
  end;
  paymtr.SumReceiver     = paymtr.SumPayer;

  if( not paymtr.Carry )
    MsgBox("Ошибка при актуализации платежа");
    return 1;
  end;

  var ReasonID: integer;
  var DialogFlag = SetDialogFlag(0);
  var stat        :integer = CheckRestAndMakeReserve(PaymentObj, true, true, true, false, null, false, false, true, @ReasonID, true, true, false);

  if (stat)
    var msg = "";
    if( stat == MR_INVALIDACCOUNT )
      msg = "Неверный счет плательщика";
    elif( (stat == MR_NOFREEAMOUNT) and (ReasonID > 0) )
      msg = "На счет плательщика наложено ограничение операций";
    elif( stat == MR_NOFREEAMOUNT )
      msg = "Недостаточно средств на счете плательщика";
    end;
    PaymentObj.Notes.AddNote(PM_NOTEKIND_DENIALGROUND, "Исполнение документа, принятого будущей датой: " + msg);
    msg = msg + ". Документ будет направлен в отвергнутые.";
    CreateWarning( null , PAYMERR_WEXWARNING , msg );
    if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_REJECT ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
    PaymentObj.PaymStatus = PM_REJECTED;
    PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_REJECTED );
  else
    if( УстановитьСтатусыПлатежа( OPR_PAYM_DO, OPR_PM_ST_PREP, OPR_PAYM_INDEX, OPR_PAYM_ST_INDEX_NO ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
  end;
  SetDialogFlag(DialogFlag);
  return 0;
end;