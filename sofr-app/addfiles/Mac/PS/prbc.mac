/*
  $Name:         prbc.mac
  $Module:       Banking
  $Description:  Печать поручения на покупку валюты. Буферы и вспомогательные функции.  
*/
/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2006              */
/*                                                                      */
/*  Имя файла      : prbc.mac                                           */
/*                                                                      */
/*  Описание       : Печать поручения на покупку валюты.                */
/*                   Буферы и вспомогательные функции.                  */
/*                                                                      */
/*  Программист    : Локтюшин В.Ю.                                      */
/*                                                                      */
/*  Создан         : 17.05.06                                           */
/*                                                                      */
/************************************************************************/

import prpm, adress, bcprncom, PSInter, prmc;
const  NOTEKIND_KDEP    :integer = 46;  /* NoteKind коэфицента депонирования */

/*валютные платежные документы клиентов*/
var pr_ps_bcord:TRecHandler = TRecHandler( "pscpord.dbt", "bank.def" );

/*
MACRO GetDepartmentName(d)  
  var dep_name = "";
  CB_GetDepartmentCodeAndName (d.Code, NULL, dep_name);
  return dep_name;
END;
  */
CLASS  PrintBuyCost(pr_pmrmprop:TRecHandler, pr_pmpaym:TRecHandler, pr_ps_bcord:TRecHandler, pr_credit:TRecHandler )
       var PmrmNumber, PmrmDate, PrpmPayerName,  PrpmPayerINN, BossFIO, BossPost, PhoneNumber = "" ,
           PayerAccount, DepositRest, RecAccount, RecBankName, Cause, AdressFull:string,
           NameBank:string,     BankCode:string,  CorrAcc:string,    DocComment, DocGround,
           BankFundsF:string,   BankFundsS,       ShoteName:string,  FinistrExName:string,
           FinistrPayName:string, FinistrName:string, LimRate:string,   ISOCode:integer, PayISOCode:integer,
           Amount:string, AmountS:string,      PayAmount:string, PayAmountS:string, TypeRate:string,
           KoefDepD:double,    KoefDepS:string,  ExecDate, FIIDF, FIIDS, FIID_RF, FIID_RS,
           FIID_FF, FIID_FS, 
           Rate, SatAmount, RateS, SellBankName = "", ConvOper,
           DepRest, ValueDate; 

       const BankFunds = pr_ps_bcord.rec.BankFunds;
       const IsInverse = pr_pmpaym.rec.IsInverse;

       FILE ps_bcexe  ( "ps_bcexe.dbt"  );
       ps_bcexe.PaymentID = pr_pmpaym.rec.PaymentID;
       if( not (GetEQ( ps_bcexe )) )/* MsgBox("Ошибка базы!"); return 0;*/ end;
             
       FILE psbcexch ( "psbcexch.dbt" );
       psbcexch.RequestID = ps_bcexe.RequestID;
       if( not (GetEQ( psbcexch )) )/* MsgBox("Ошибка базы!"); return 0;*/ end;
       
       const RateInv = ps_bcexe.RateInv;

       RECORD RecordAdress ( adress );
       ClearRecord( RecordAdress );
       НайтиЮридическийАдресСубъекта( pr_pmpaym.rec.Payer, RecordAdress );
              
       FindAdressContactValue( pr_pmpaym.rec.Payer, PTADDR_LEGAL, CNTADR_PHONENUMBER, PhoneNumber);
       
       AdressFull = RecordAdress.Adress;

       MACRO CreateNameBank( pr_pmpaym:TRecHandler ):string
         var query : string = " select party.t_shortname, adress.t_codedistrict, adress.t_district, adress.t_codeplace, adress.t_place " + 
                                          " from dparty_dbt party, dadress_dbt adress " + 
                                          " where party.t_partyID = :partyID and " + 
                                          " adress.t_partyID = party.t_partyID and adress.t_type = :adrType "; 
         var params : TArray = makeArray( SQLParam( "partyID", pr_pmpaym.rec.PayerBankID ),
                                          SQLParam( "adrType", PTADDR_LEGAL ) 
                                        );
         var rset : RsdRecordset = execSQLselect( query, params, true );
         var tmpBankName = "";

         while( rset and rset.MoveNext() )
           tmpBankName = rset.Value( "t_shortname" );
             if( rset.Value( "t_codedistrict" ) != "" )
               tmpBankName = tmpBankName  + " " +  rset.Value( "t_codedistrict" ) + " " + rset.Value( "t_district" );
             elif( rset.Value( "t_codeplace" ) != "" )
               tmpBankName = tmpBankName + " " +  rset.Value( "t_codeplace" ) + " " + rset.Value( "t_place" );
             end;
         end;

         return tmpBankName;
       END;

       MACRO GetBankCode( pr_pmpaym:TRecHandler )  
             var params:TArray = NULL;
             var retval:integer = 0;
             var BankID :integer  = pr_pmpaym.rec.PayerBankID;
             params = makeArray( SQLParam( "p_PartyID"    , BankID               ),
                                 SQLParam( "p_CodeKind"   , PTCK_BIC             ),
                                 SQLParam( "p_Code"       , V_STRING , RSDBP_OUT ),
                                 SQLParam( "p_CodeOwnerID", V_INTEGER, RSDBP_OUT )
                               );
             retval = execStoredFunc( "RSBPARTY.PT_GetPartyCodeEx", V_INTEGER, params );
             if( retval == 0 )
               return params.Value(2).value;
             end;

       END;

       MACRO GetCorAcc( pr_pmpaym:TRecHandler )
             FILE bankdprt ( "bankdprt.dbt" );
             bankdprt.PartyID = pr_pmpaym.rec.PayerBankID;
             if( GetEQ( bankdprt ) )
               return  bankdprt.CorAcc;
             else return "";
             end;            
       END;

       MACRO GetComment( PaymentID ); 
             FILE ps_ordoc ( "ps_ordoc.dbt" ) key 0;
             var result    = "";
             var delimiter = "";

             ps_ordoc.PaymentID = PaymentID;             
             var stat = getGE( ps_ordoc );

             while( stat and (ps_ordoc.PaymentID == PaymentID) )
                result = result + delimiter + ps_ordoc.Comment;
                delimiter = "; ";
                stat = Next( ps_ordoc );
             end;  
             return result; 
       END;

       MACRO GetShotName( ps_bcord:TRecHandler ):string
             FILE party    ( "party.dbt"    );
             party.PartyID = ps_bcord.rec.ExchangeID;
             if( GetEQ( party ) )
               return party.ShortName;            
             else return "";
             end;
       END;
       
       MACRO GetFinistrName( FIID:integer ):string 
             FILE fininstr ( "fininstr.dbt" );
             fininstr.FIID = FIID;
             if( GetEQ( fininstr ) )
               return fininstr.Name;
               else return "";
             end;
       END;
       
       MACRO GetTypeRate( RType:integer ):string
             FILE ratetype ( "ratetype.dbt" );
             ratetype.Type = RType;             
             if( GetEQ( ratetype ))
               return ratetype.TypeName;
             else return "";              
             end;
       END;

       MACRO GetFIIDF( pr_pmpaym:TRecHandler, psbcexch )
         if( BankFunds )
           if( IsInverse )
             return pr_pmpaym.rec.PayFIID;
           else
             return pr_pmpaym.rec.FIID;
           end;
         else           
             return psbcexch.FIID;                         
         end;
       END;
       
       MACRO GetFIIDS( pr_pmpaym:TRecHandler, psbcexch )
         if( BankFunds )
           if( IsInverse )
             return pr_pmpaym.rec.FIID;
           else
             return pr_pmpaym.rec.PayFIID;
           end;
         else         
             return psbcexch.ContrFIID;    
         end;
       END;
       
       MACRO GetFIID_FF( pr_pmpaym:TRecHandler, psbcexch )
         if( BankFunds )
           if( IsInverse == pr_pmpaym.rec.IsFixAmount )
             return pr_pmpaym.rec.FIID;
           else
             return pr_pmpaym.rec.PayFIID;
           end;
         else           
             return psbcexch.FIID;                         
         end;
       END;
       
       MACRO GetFIID_FS( pr_pmpaym:TRecHandler, psbcexch )
         if( BankFunds )
           if( IsInverse == pr_pmpaym.rec.IsFixAmount )
             return pr_pmpaym.rec.PayFIID;
           else
             return pr_pmpaym.rec.FIID;
           end;
         else         
             return psbcexch.ContrFIID;    
         end;
       END;

       MACRO GetFIID_RF( pr_pmpaym:TRecHandler, psbcexch )
         if( not BankFunds )
           if( RateInv )
             return pr_pmpaym.rec.PayFIID;
           else
             return psbcexch.FIID;
           end;         
         else return 0;
         end;
       END;

       
       MACRO GetFIID_RS( pr_pmpaym:TRecHandler, psbcexch )
         if( not BankFunds )
           if( RateInv )
             return psbcexch.FIID;
           else
             return pr_pmpaym.rec.PayFIID;
           end;
         else return 0;         
         end;
       END;
                  
       
       
       if( pr_ps_bcord.rec.ConvOper == 1)
         ConvOper = 0;
       else
         ConvOper = 1;
       end;
       
       PmrmNumber     = pr_pmrmprop.rec.Number;
       PmrmDate       = pr_pmrmprop.rec.Date;
       PrpmPayerName  = pr_pmrmprop.rec.PayerName;
       PrpmPayerINN   =  pr_pmrmprop.rec.PayerINN;
       BossFIO        = pr_ps_bcord.rec.BossFIO;
       BossPost       = pr_ps_bcord.rec.BossPost;
       PayerAccount   = pr_pmpaym.rec.PayerAccount;
       NameBank       = CreateNameBank( pr_pmpaym );
       
       if( pr_pmpaym.rec.ReceiverBankID == pr_pmpaym.rec.PayerBankID )
         SellBankName = IfThenElse(NameBank, NameBank, CreateNameBank( pr_pmpaym ));
       else
         SellBankName =   pr_pmrmprop.rec.ReceiverBankName + " БИК " +
                  pr_credit.rec.BankCode + " корсчет " + pr_pmrmprop.rec.ReceiverCorrAccNostro;
       end;
       BankCode       = GetBankCode   ( pr_pmpaym );
       CorrAcc        = GetCorAcc     ( pr_pmpaym );
       RecAccount     = pr_pmpaym.rec.ReceiverAccount;
       RecBankName    = pr_pmrmprop.rec.ReceiverBankName;
       Cause          = pr_ps_bcord.rec.Cause;
       DocGround      = pr_pmrmprop.rec.Ground;
       DocComment     = GetComment( pr_ps_bcord.rec.PaymentID );
       DocComment     = IfThenElse( DocComment, DocComment, ""  );       
       FinistrPayName = GetFinistrName( pr_pmpaym.rec.PayFIID );
       FinistrName    = GetFinistrName( pr_pmpaym.rec.FIID );       
       ISOCode        = getISOCode( pr_pmpaym.rec.FIID );
       PayISOCode     = getISOCode( pr_pmpaym.rec.PayFIID );
       Amount         = string(MoneyL( pr_pmpaym.rec.Amount ));
       AmountS        = CurToStrAlt( pr_pmpaym.rec.Amount , null, null, getISOCode(pr_pmpaym.rec.FIID) );
       PayAmount      = string(MoneyL( pr_pmpaym.rec.PayAmount ));
       PayAmountS     = CurToStrAlt( pr_pmpaym.rec.PayAmount , null, null, getISOCode(pr_pmpaym.rec.PayFIID) );              
       ExecDate       = ps_bcexe.ExecDate;
       FIIDF          = GetISOCode( GetFIIDF( pr_pmpaym, psbcexch ));
       FIIDS          = GetISOCode( GetFIIDS( pr_pmpaym, psbcexch ));       
       FIID_FF        = GetISOCode( GetFIID_FF( pr_pmpaym, psbcexch ));
       FIID_FS        = GetISOCode( GetFIID_FS( pr_pmpaym, psbcexch ));       
       SatAmount      = ps_bcexe.SatisfiedAmount;
       DepositRest    = ps_bcexe.DepositRest;
       ValueDate      = pr_pmpaym.rec.ValueDate;

        if( pr_ps_bcord.rec.BCOrdKind == 1 )
          
          if( NOT DefineCoefficientDepos( {OperDprt}, KoefDepD ) )
            KoefDepD = 0;
          elif( KoefDepD < 1.0 )
            KoefDepD = 1.0;         
          end; 
          KoefDepS       = IfThenElse( KoefDepD, KoefDepD:string, "");
        end;
        FinistrExName = LimRate = FIID_RF = FIID_RS = RateS = ShoteName = TypeRate = "";
        BankFundsF    = "X";
        BankFundsS    = "_";       

       if( BankFunds )
         Rate          = pr_pmpaym.rec.Rate / pow( 10, pr_pmpaym.rec.Point );
         TypeRate      = GetTypeRate( pr_pmpaym.rec.RateType );
       else
         FinistrExName = GetFinistrName( pr_ps_bcord.rec.ExchangeFIID );
         LimRate       = pr_ps_bcord.rec.LimRate / pow( 10, pr_ps_bcord.rec.LimRatePoint );
         BankFundsF    = "_";
         BankFundsS    = "X";
         FIID_RF       = GetISOCode( GetFIID_RF( pr_pmpaym, psbcexch ));
         FIID_RS       = GetISOCode( GetFIID_RS( pr_pmpaym, psbcexch ));
         Rate          = psbcexch.Rate / pow( 10, psbcexch.RatePoint );
         RateS         = ps_bcexe.Rate / pow( 10, ps_bcexe.RatePoint );
         ShoteName     = GetShotName( pr_ps_bcord );
       end;
     
       if( pr_ps_bcord.rec.State != PSBCORD_ST_CLOSED )
         ExecDate = FIIDF = FIID_FF = FIIDS = FIID_FS =     
         Rate = SatAmount = FIID_RF = Amount =  FIID_RS =
         DepositRest = RateS = "";
       end;

     
END;                           

/*  поручение на покупку/продажу/конверсию валюты - для печати сводного мемордера */
CLASS (TDocument)TPSBcorDoc()

  InitTDocument("ps_bcord.dbt");

  /*получить универсальный строковый идентификатор*/ 
  MACRO getUniID():string
    return string( rec.PaymentID:34:o );
  END;

  /*получить поручение*/
  MACRO get():bool
    var fpsbcdoc:TBFile = TBFile("ps_bcord.dbt", 0, "ps_bcord.dbt", "bank.def");
  
    carries().clear();
    
    if(rec.PaymentID)
      fpsbcdoc.rec.PaymentID = rec.PaymentID;
      if(fpsbcdoc.GetEQ())
        Copy(_extObj, fpsbcdoc);
        return TRUE;
      else
        return FALSE;
      end;
    else
      return FALSE;
    end;
  END;  

END;
  
/*  отчет по поручениям на п/п/к валюты   */
CLASS (ConsolidatedMemorialOrder)BC_ConsolidatedMemorialOrder(ps_bcord:TRecHandler, payment:TRecHandler, rmpayment:TRecHandler, print_ocp:bool)

  var m_psdoc:TPSBcorDoc = TPSBcorDoc();
  m_psdoc.init(ps_bcord, payment, rmpayment);

  InitConsolidatedMemorialOrder(m_psdoc, print_ocp);

END;


/**/
/*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/
/**/



















