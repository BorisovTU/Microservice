/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : psporjsc.mac
  Created     : 11.01.11
  Description : Печать отчета об отвержении документов

└───────────────────────────────────────────────────────────────────────────*/

IMPORT "gstname.mac";
IMPORT  PaymInter, OprInter;

RECORD pspo_firstdoc (pspayord);

const Ошибка   = 0,
      Первый   = 1,
      Последний = 2;


macro Печать_ОтчетСканирования ( Вызов, firstdoc, Статус, Сообщение )

  record pm_rmprop (pmrmprop);
  record pm_paym   (pmpaym);

  setbuff (pspo_firstdoc,firstdoc);

  if( Вызов == Первый )


[              Отчет о групповом помещении документов в отвергнутые.               ];
[┌─────────┬─────────────┬──────────┬──────┬──────────────────────────────────────┐];
[│  Номер  │   Сумма     │   Дата   │Номер │                Сообщение             │];
[│документа│             │          │ошибки│                                      │];
[├─────────┼─────────────┼──────────┼──────┼──────────────────────────────────────┤];

  elif( Вызов == Ошибка )
 
    if( Статус == 0 )
      Сообщение = "Документ успешно помещен в отвергнутые";
    end;

    if ( FindPayment(
            0,      /* Индентификатор неизвестен    */
            PM_PURP_POPRIMARY,      /* Основной по клиентской платежке */
            0,      /* Subpurpose                   */
            PS_PAYORDER, /* Вид первичного документа */ 
            pspo_firstdoc.OrderID,
            true,   /* Поиск по реальной базе       */
            pm_paym,
            null,
            null,
            pm_rmprop
            )       !=      0 )

            Статус = 1;
            Сообщение = "Не найдено платежное поручение";
    else 
      Сообщение = Сообщение + ". " + GetStepNameExecuteRead( pm_paym.PaymentID, pm_paym.DocKind );
    end;

[│#########│#############│##########│######│######################################│]
( pm_rmprop.Number, pm_paym.Amount, pm_rmprop.Date, Статус, StrSubst(Сообщение, "|", " " ):w );


  elif( Вызов == Последний )

[└─────────┴─────────────┴──────────┴──────┴──────────────────────────────────────┘];

  end;


end;