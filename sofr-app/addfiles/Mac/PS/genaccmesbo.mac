/*
  $Name:        genaccmesbo.mac
  $Module:      РКО
  $Description: Формирование сообщений по счетам бэк-офисов
*/
//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : РКО
//
// Описание     : Формирование сообщений по счетам бэк-офисов
//
// Программист  : Чукина Т.А.
//
// Создан       : 18.07.2014
//
//-----------------------------------------------------------------------------

import globals, likepy, oralib, RsbDataSet, PSInter, "wlmnstls.mac", FIInter, 
       "pm_tools.mac";

private const ISCANCELLED_NO : integer = 0;
private const ISCANCELLED_YES : integer = 1;

private macro ExistsParty(PartyID : integer) : bool
  var q : string = 
    "select 1 "
    "  from dparty_dbt "
    " where t_PartyID = :PartyID ";
  var rs : RsdRecordset = execSQLselectPrm( q, SQLParam("PartyID", PartyID) );

  if(rs and rs.moveNext())
    return TRUE;
  end;

  return FALSE;
end;

private macro CheckRegAndGetViewName(KindBO : string, ViewName : @string)
  var regHp = "PS\\СООБЩЕНИЯ ФНС\\ДАННЫЕ ВНЕШНИХ СИСТЕМ\\" + KindBO + "\\ХП";
  var regView = "PS\\СООБЩЕНИЯ ФНС\\ДАННЫЕ ВНЕШНИХ СИСТЕМ\\" + KindBO + "\\VIEW";

  var SPName : string = "";
  GetRegistryValue(reghp, V_STRING, SPName);
  GetRegistryValue(regView, V_STRING, ViewName);

  if( not ViewName or
      ( not SPName and (StrUpr(KindBO) != "RETAIL") )
    )
    var ErrMsg : string = "Не удалось определить источник данных по счетам для формирования сообщений. Проверьте корректность настроек в ветке реестра PS\\СООБЩЕНИЯ ФНС\\ДАННЫЕ ВНЕШНИХ СИСТЕМ";
    RunError(ErrMsg, ErrMsg);
  end;
end;

private class TBackOffice ( p_KindBO : string, MapVal : PSRepMap )
  var KindBO : string = p_KindBO;

  var DateBegin   : date   = MapVal.Value( "DateBegin" ),
      DateEnd     : date   = MapVal.Value( "DateEnd" ),
      Accounts    : string = MapVal.Value( "Accounts" ),
      Deps        : string = MapVal.Value( "Deps" ),
      IsSub       : bool   = MapVal.Value( "IsSub" ),
      IsInfOpDone : bool   = MapVal.Value( "IsInfOpDone" ),
      IsInfOpAnnul: bool   = MapVal.Value( "IsInfOpAnnul" );

  if( DateBegin == date(0, 0, 0) )
    DateBegin = date( 01, 07, 2014 );
  end;

  private var ViewName : string = "", 
              ReqOrigin : integer = 0,
              RepBOKind : string = "",
              CurrDprt  : integer = null,
              CurrIsCancelled : integer = null;

  // конструктор
  
  CheckRegAndGetViewName(KindBO, @ViewName);

  if( StrUpr(KindBO) == StrUpr(GENACCMES_KINDBO_RETAIL) )
    ReqOrigin = REQ_ACC_ORIGIN_RETAIL;
  elif( StrUpr(KindBO) == StrUpr(GENACCMES_KINDBO_LOANS) )
    ReqOrigin = REQ_ACC_ORIGIN_LOANS;
  end;
  
  private macro GetDateStr() : string
    return " case "
           "   when nvl(t.t_PrevAccount, chr(1)) <> chr(1) or "
           "        t.t_AccStatus = '99' or "
           "        t.t_Department_Old > 0 or "
           "        nvl(t.t_DepartmentBIC_Old, chr(1)) <> chr(1) then "
           "     t.t_ChangeDate "
           "   when nvl(t.t_CloseDate, to_date('01010001', 'ddmmyyyy')) = to_date('01010001', 'ddmmyyyy') then "
           "     t.t_OpenDate "
           "   else "
           "     t.t_CloseDate "
           " end ";
  end;

  private macro GetOpKind(view /*: TRsbDataSet*/) : integer
    var OpKind : integer;

    if( view.PrevAccount or (view.AccStatus == "99") or (view.Department_Old > 0) or view.DepartmentBIC_Old )
      OpKind = ACCMSG_KIND_CHNG;
    elif( (ValType(view.CloseDate) == V_UNDEF) or (date(view.CloseDate) == date(0, 0, 0)) )
      OpKind = ACCMSG_KIND_OPEN;
    else
      OpKind = ACCMSG_KIND_CLOSE;
    end;

    return OpKind;
  end;

  private macro GetListInSingleQuotes(src : string) : string
    var res : string = "";

    var arr : TArray = StrCut(src);
    for(var s : string, arr)
      if(res)
        res = res + ",";
      end;

      res = res + "'" + s + "'";
    end;

    return res;
  end;

  private macro GetRecords() : RsdCommand
    var cmd : RsdCommand = RsdCommand();

    var query : string = "select * from " + ViewName + " t where ";

    var condDo : string = "";
    if(IsInfOpDone)
      condDo = "t.t_IsCancelled = 0 and nvl(t.t_MessageID, 0) = 0";
    end;

    var condAn : string = "";
    if(IsInfOpAnnul)
      condAn = "t.t_IsCancelled = 1 and nvl(t.t_MessageID, 0) != 0";
    end;

    query = query + " (" + condDo + 
                           IfThenElse(condDo and condAn, " or ", "") + 
                           condAn + ") ";

    if(DateBegin != date(0, 0, 0))
      query = query + " and :DateBegin <= " + GetDateStr();
      cmd.AddParam("DateBegin", RSDBP_IN, DateBegin);
    end;

    if(DateEnd != date(0, 0, 0))
      query = query + " and :DateEnd >= " + GetDateStr();
      cmd.AddParam("DateEnd", RSDBP_IN, DateEnd);
    end;

    if(Accounts and (Accounts != "*"))
      query = query + " and rsb_mask.CompareStringWithMask( :AccountMask, t.t_Account ) = 1 ";
      cmd.AddParam("AccountMask", RSDBP_IN, Accounts);
    end;

    if(Deps)
      Deps = GetListInSingleQuotes(Deps);

      query = query + " and t.t_TCBranch in " +
                      "       ( select dp.t_Code " +
                      "           from ddp_dep_dbt dp ";

      if(IsSub)
        query = query + "        start with dp.t_Name in (" + Deps + ") " +
                        "        connect by prior dp.t_Code = dp.t_ParentCode ";

      else
        query = query + "        where dp.t_Name in (" + Deps + ") ";
      end;

      query = query +   "     ) ";
    end;

    query = query + " order by t.t_Department, t.t_IsCancelled, t.t_ClientID ";
    cmd.CmdText = query;

    return cmd;
  end;

  private macro PrintReportHeader
    [

      Протокол выполнения процедуры формирования сообщений в ФНС #

      #
      Дата выполнения:  #
      Исполнитель:      #

      За период с ########## по ##########
    ]
    (RepBOKind, {Name_Bank}, {curdate}:f, {Name_Oper}, DateBegin:f, DateEnd:f);
  end;

  private macro PrintStartDep(NewDprt : integer)
    CurrDprt = NewDprt;
    CurrIsCancelled = null;

    var DepCode : string = "", DepName : string = "";
    CB_GetDepartmentCodeAndName(CurrDprt, DepCode, DepName);

    [
      Счета филиала ############ #
     ┌─────────────────────────┬──────┬─────────────────────────┬────────────────────────────┬────────────────────────────┬──────────────────────────────────────────────────────────────┐
     │ Номер счета             │Валюта│        Тип счета        │         Клиент             │        Вид операции        │      Результат                                               │
    ]
    (DepCode, DepName);
  end;

  private macro PrintFinishDep

    [└─────────────────────────┴──────┴─────────────────────────┴────────────────────────────┴────────────────────────────┴──────────────────────────────────────────────────────────────┘
    ];
  end;

  private macro PrintChangeDep(NewDprt : integer)
    if(CurrDprt)
      PrintFinishDep();
    end;

    PrintStartDep(NewDprt);
  end;

  private macro PrintChangeIsCancelled(NewIsCancelled : integer)
    CurrIsCancelled = NewIsCancelled;

    var Title : string = IfThenElse( CurrIsCancelled == ISCANCELLED_YES,
                                     "Сообщения об отмененных операциях:",
                                     "Сообщения о выполненных операциях:"
                                   );

    [└─────────────────────────┴──────┴─────────────────────────┴────────────────────────────┴────────────────────────────┴──────────────────────────────────────────────────────────────┘
      #
      ──────────────────────────────────
    ]
    (Title);
  end;

  private macro GetOpKindName(OpKind : integer, ClsAccSubKind : integer) : string
    var OpKindName : string = "";

    if( OpKind == ACCMSG_KIND_OPEN )
      OpKindName = "Открытие счета";

    elif( OpKind == ACCMSG_KIND_CHNG )
      OpKindName = "Изменение реквизитов счета";

    elif( OpKind == ACCMSG_KIND_CLOSE )
      var SubKindStr : string = "";
      if(ClsAccSubKind == REQCLSAC_REQUEST)
        SubKindStr = "по заявлению клиента";
      elif(ClsAccSubKind == REQCLSAC_ORDER)
        SubKindStr = "по распоряжению банка";
      elif(ClsAccSubKind == REQCLSAC_JUDGEMENT)
        SubKindStr = "по решению суда";
      end;

      OpKindName = "Закрытие счета" + " " + SubKindStr;

    else
      RunError("Неверный значение параметра OpKind");
    end;

    return OpKindName;
  end;

  private macro PrintChangeRecord
    [├─────────────────────────┼──────┼─────────────────────────┼────────────────────────────┼────────────────────────────┼──────────────────────────────────────────────────────────────┤
    ];
  end;

  private macro PrintRecord
  ( view /*: TRsbDataSet*/, 
    ClsAccSubKind : integer, 
    ResultStr : string
  )
    var OperKind : string = GetOpKindName( GetOpKind(view), ClsAccSubKind );

    if( view.Department != CurrDprt )
      PrintChangeDep(view.Department);
    end;

    if( view.IsCancelled != CurrIsCancelled )
      PrintChangeIsCancelled(view.IsCancelled);
    else
      PrintChangeRecord();
    end;

    [│#########################│######│#########################│############################│############################│##############################################################│
    ](view.Account, string(view.CurrCode:0:0):c, view.AccKind, Bnk_GetPartyShortName(view.ClientID):w, OperKind:w, ResultStr:w);

  end;

  private macro GetRequestID(BOOperationID : string, TableName : string) : integer
    var RequestID : integer = 0;

    var rs : RsdRecordset = execSQLselectPrm
    ( "select t_RequestID " +
      "  from " + TableName +
      " where t_BOOperationID = :BOOperationID ",
      SQLParam("BOOperationID", BOOperationID)
    );

    if( rs and rs.moveNext() )
      RequestID = rs.value("t_RequestID");
    end;

    return RequestID;
  end;

  private macro GetReqClosaSubKind(ContractStatus : integer) : integer
    var SubKind : integer = 0;

    if( InList(ContractStatus, 0, 1) )
      SubKind = REQCLSAC_REQUEST;
    elif( ContractStatus == 2 )
      SubKind = REQCLSAC_ORDER;
    elif( ContractStatus == 3 )
      SubKind = REQCLSAC_JUDGEMENT;
    /*else
      RunError("Не удалось определить вид документа закрытия счета");*/
    end;

    return SubKind;
  end;

  private macro GetReqOpenAcc(view /*: TRsbDataSet*/) : RsbReqOpenAcc
    var RequestID : integer = GetRequestID( view.ID, "dreqopena_dbt" );

    var ReqObj : RsbReqOpenAcc = RsbReqOpenAcc(RequestID);
    
    ReqObj.Date = view.OpenDate;
    ReqObj.Balance(0) = substr( view.Account, 1, 5 );
    ReqObj.Code_Currency = ПолучитьКодФинИн(view.CurrCode, null, FICK_ISONUMBER);
    ReqObj.Account = view.Account;
    ReqObj.Type_Account = view.TypeAccount;
    ReqObj.AccountDepartment = view.Department;
    ReqObj.AccountBranch = view.TCBranch;
    ReqObj.AccOpenDate = view.OpenDate;
    ReqObj.SfContrNum = view.Contract;
    ReqObj.SfContrDate = view.ContractDate;
    ReqObj.Oper = {oper};
    ReqObj.OperAcc = {oper};
    
    if( ReqObj.Notes.AddNote(REQOPENA_NOTE_KIND_ACCKIND, view.AccKind) != 0 )
      RunError("Не удалось записать примечание \"Вид счета\"");
    end;

    return ReqObj;
  end;

  private macro GetReqChangeAcc(view /*: TRsbDataSet*/) : RsbReqChangeAcc
    var RequestID : integer = GetRequestID( view.ID, "dreqchnga_dbt" );

    var ReqObj : RsbReqChangeAcc = RsbReqChangeAcc(RequestID);

    ReqObj.Date = view.ChangeDate;
    ReqObj.Balance(0) = substr( view.Account, 1, 5 );
    ReqObj.OldAccountFIID = Bnk_GetAccountFIID( view.PrevAccount );
    ReqObj.OldAccount = view.PrevAccount;
    ReqObj.NewAccountFIID = ПолучитьКодФинИн(view.CurrCode, null, FICK_ISONUMBER);
    ReqObj.NewAccount = view.Account;
    ReqObj.NewAccountDep = view.Department;
    ReqObj.NewAccountBranch = view.TCBranch;
    ReqObj.NewContractNumber = view.Contract;
    ReqObj.SfContrDate = view.ContractDate;
    ReqObj.Branch = view.TCBranch;
    ReqObj.Oper = {oper};
    ReqObj.NewAccountOper = {oper};

    if( ReqObj.Notes.AddNote(REQCHNGA_NOTE_KIND_ACCKIND, view.AccKind) != 0 )
      RunError("Не удалось записать примечание \"Вид счета\"");
    end;

    if(view.OpenDate != null)
      if( ReqObj.Notes.AddNote(REQCHNGA_NOTE_KIND_ACC_OPENDATE, view.OpenDate) != 0 )
        RunError("Не удалось записать примечание \"Дата открытия счета\"");
      end;
    end;

    if( ReqObj.Notes.AddNote(REQCHNGA_NOTE_KIND_ACC_CHNGDATE, view.ChangeDate) != 0 )
      RunError("Не удалось записать примечание \"Дата изменения счета\"");
    end;

    if( view.AccStatus == "99" )
      ReqObj.ClientChange = "X";
      ReqObj.ClientChangeDate = view.ChangeDate;
      ReqObj.ClientChangeGround = CHANGE_CLIENT_GROUND_PERSN;

      if( view.PrevAccount == "" )
        ReqObj.OldAccount = ReqObj.NewAccount;
        ReqObj.OldAccountFIID = ReqObj.NewAccountFIID;
      end;
            
    elif( (view.Department_Old > 0) or view.DepartmentBIC_Old )
      ReqObj.BankChange = "X";
      ReqObj.BankChangeDate = view.ChangeDate;

      // ReqObj.BankIDChange
      if( (view.Department_Old > 0) and ExistsParty(view.Department_Old) )
        ReqObj.BankIDChange = view.Department_Old;
      elif( view.DepartmentBIC_Old )
        var BIC : string = ""; // БИК
        var DeptNum : string = ""; // номер филиала

        var ind : integer = index(view.DepartmentBIC_Old, ",");
        if( ind > 0 ) 
          BIC = substr(view.DepartmentBIC_Old, 1, ind - 1);
          DeptNum = substr(view.DepartmentBIC_Old, ind + 1);
        else
          BIC = view.DepartmentBIC_Old;
        end;

        if(DeptNum) // номер филиала задан
          ReqObj.BankIDChange = getPartyIDFromBICandDeptNum(BIC, DeptNum, true);
        else // только БИК
          ReqObj.BankIDChange = GetCodeOwnerID(PTCK_BIC, BIC);
        end;
      end;

      if(not view.PrevAccount)
        ReqObj.OldAccountFIID = ReqObj.NewAccountFIID;
        ReqObj.OldAccount = ReqObj.NewAccount;
      end;

      ReqObj.ClientName = view.ClientName;
      ReqObj.ClientINN = view.ClientINN + "/" + view.ClientKPP;
      ReqObj.ClientOGRN = view.ClientOGRN;
      ReqObj.ClientBirthDate = view.ClientBirthDate;
      ReqObj.ClientBirthPlace = view.ClientBirthPlace;
      ReqObj.ClientCardType = view.ClientIdCardType;
      ReqObj.ClientCardNum = view.ClientIdCardNum;
      ReqObj.ClientCardDate = view.ClientIdCardDate;
    else
      ReqObj.AccountChange = "X";
    end;

    return ReqObj;
  end;

  private macro GetReqCloseAcc(view /*: TRsbDataSet*/) : RsbReqCloseAcc
    var RequestID : integer = GetRequestID( view.ID, "dreqclosa_dbt" );

    var ReqObj : RsbReqCloseAcc = RsbReqCloseAcc(RequestID);
    ReqObj.Date = view.CloseDate;
    ReqObj.SubKind = GetReqClosaSubKind(view.ContractStatus);
    ReqObj.Code_Currency = ПолучитьКодФинИн(view.CurrCode, null, FICK_ISONUMBER);
    ReqObj.Account = view.Account;
    ReqObj.Branch = view.TCBranch;
    ReqObj.AccCloseDate = view.CloseDate;
    ReqObj.Oper = {oper};

    if( ReqObj.Notes.AddNote(REQCLOSA_NOTE_KIND_ACCKIND, view.AccKind) != 0 )
      RunError("Не удалось записать примечание \"Вид счета\"");
    end;

    if(view.OpenDate != null)
      if( ReqObj.Notes.AddNote(REQCLOSA_NOTE_KIND_ACC_OPENDATE, view.OpenDate) != 0 )
        RunError("Не удалось записать примечание \"Дата открытия счета\"");
      end;
    end;

    if( ReqObj.Notes.AddNote(REQCLOSA_NOTE_KIND_CONTRACT_NUMBER, view.Contract) != 0 )
      RunError("Не удалось записать примечание \"Номер договора\"");
    end;

    if(view.ContractDate != null)
      if( ReqObj.Notes.AddNote(REQCLOSA_NOTE_KIND_CONTRACT_CLOSEDATE, view.ContractDate) != 0 )
        RunError("Не удалось записать примечание \"Дата расторжения счета\"");
      end;
    end;

    return ReqObj;
  end;

  private macro SetCommonAttrs(view /*: TRsbDataSet*/, ReqObj)
    ReqObj.InputDate = {curdate};
    ReqObj.Department = view.Department;

    ReqObj.Dummy = "X";
    ReqObj.Origin = ReqOrigin;
    ReqObj.BOOperationID = view.ID;
    ReqObj.ClientID = view.ClientID;

    ReqObj.Origin = ReqOrigin;
    ReqObj.BackOffice = KindBO;
    ReqObj.ClientName = view.ClientName;
    ReqObj.ClientINN = view.ClientINN + IfThenElse(view.ClientKPP, "/" + view.ClientKPP, "");
    ReqObj.ClientOGRN = view.ClientOGRN;
    ReqObj.ClientBirthDate = view.ClientBirthDate;
    ReqObj.ClientBirthPlace = view.ClientBirthPlace;
    ReqObj.ClientCardType = view.ClientIdCardType;
    ReqObj.ClientCardNum = view.ClientIdCardNum;
    ReqObj.ClientCardDate = view.ClientIdCardDate;
    ReqObj.ClientSertif = view.ClientCertif;
    ReqObj.ClientSertifReg = view.ClientCertifReg;

    if(not view.ClientType and (view.ClientID > 0))
      ReqObj.ClientType = IfThenElse(IsPerson(view.ClientID), PTLEGF_PERSN, PTLEGF_INST);
    elif( InList(view.ClientType, "1", "6") )
      ReqObj.ClientType = PTLEGF_INST;
    else
      ReqObj.ClientType = PTLEGF_PERSN;
    end;

    ReqObj.ClientType311 = view.ClientType;
  end;

  private macro GetReqObj(view /*: TRsbDataSet*/) : object
    var ReqObj : object = null;

    var OpKind : integer = GetOpKind(view);

    if(OpKind == ACCMSG_KIND_OPEN)
      ReqObj = GetReqOpenAcc(view);
    elif(OpKind == ACCMSG_KIND_CHNG)
      ReqObj = GetReqChangeAcc(view);
    else
      ReqObj = GetReqCloseAcc(view);
    end;

    SetCommonAttrs(view, ReqObj);

    return ReqObj;
  end;

  private macro CreateReqOperation(ReqObj : object) : integer
    var stat : integer = 0;

    if( IsEqClass("RsbReqOpenAcc", ReqObj) )
      stat = CreateReqOpenAOperation(ReqObj.RequestID);
    elif( IsEqClass("RsbReqChangeAcc", ReqObj) )
      stat = CreateReqChngAOperation(ReqObj.RequestID);
    elif( IsEqClass("RsbReqCloseAcc", ReqObj) )
      stat = CreateReqClosAOperation(ReqObj.RequestID);
    else
      RunError("Неверный тип параметра ReqObj");
    end;

    return stat;
  end;

  private macro GetResultStr(view /*: TRsbDataSet*/, Err : string)
    var ResultStr : string = "";

    var ProcKind : string = IfThenElse( view.IsCancelled == ISCANCELLED_NO, 
                                        "сформировано",
                                        "аннулировано"
                                      );
    var OpKindStr : string = "";

    var OpKind : integer = GetOpKind(view);
    if(OpKind == ACCMSG_KIND_OPEN)
      OpKindStr = "об открытии счета";
    elif(OpKind == ACCMSG_KIND_CHNG)
      OpKindStr = "об изменении номера счета";
    else
      OpKindStr = "о закрытии счета";
    end;

    if(not Err)
      ProcKind = StrUpr( SubStr(ProcKind, 1, 1) ) + SubStr(ProcKind, 2);
      ResultStr = ProcKind + " сообщение " + OpKindStr;
    else
      ProcKind = StrLwr( SubStr(ProcKind, 1, 1) ) + SubStr(ProcKind, 2);
      ResultStr = "Сообщение " + OpKindStr + " не может быть " + ProcKind + ": \n" + Err;
    end;

    var WarnList : TArray = GetWarnings();
    for(var Warning, WarnList)
      ResultStr = ResultStr + "\n" + Warning.stat + " " + Warning.message;
    end;

    return ResultStr;
  end;

  private macro GetErr(stat : integer, DefaultErr : string) : string
    var ErrMsg : string = "";

    if(stat)
      MemoryError(stat);
    end;

    ErrMsg = GetErrMsg();
    
    if(  ErrMsg != "" )
      ErrMsg = StrSubst( ErrMsg, "|", "" );
    end;

    if(not ErrMsg)
      ErrMsg = DefaultErr;
    end;

    if(stat and (stat > 1))
      ErrMsg = string(stat, ": ", ErrMsg);
    end;

    return ErrMsg;
  end;

  private macro GenOneMessage(rs /*: TRsbDataSet*/)
    var ClsAccSubKind : integer = 0;

    var ReqObj = GetReqObj(rs);

    if( IsEqClass("RsbReqCloseAcc", ReqObj) )
      ClsAccSubKind = ReqObj.SubKind;
    end;

    var ErrMsg : string = "";
    
    var ReqKind = GetOpKind(rs);
    
    if( ReqKind == ACCMSG_KIND_CHNG )
      ReqObj.FillReqChngClientData();
    elif( ReqKind == ACCMSG_KIND_OPEN )
      ReqObj.SetDefaults();
    elif( ReqKind == ACCMSG_KIND_CLOSE )
      ReqObj.SetDefaults();
    end;
    
    if( ReqObj.SaveChanges() )
      var stat : integer = CreateReqOperation(ReqObj);
      if(stat)
        ErrMsg = GetErr(stat, "Ошибка при запуске операции по заявлению");
      end;
    else
      ErrMsg = GetErr(null, "Ошибка при сохранении заявления");
    end;

    PrintRecord( rs, ClsAccSubKind, GetResultStr(rs, ErrMsg) );

  OnError(er)
    PrintRecord( rs, ClsAccSubKind, GetResultStr(rs, RsbGetError(er)) );
  end;

  private macro GetClsAccSubKindByMesID(MesID : integer) : integer
    var SubKind : integer = 0;

    var query : string = 
      "select t_SubKind "
      "  from dreqclosa_dbt req, dwlmeslnk_dbt lnk "
      " where lnk.t_MesID = :MesID "
      "   and lnk.t_Direct = chr(0) "
      "   and lnk.t_ObjKind = :OBJTYPE_REQCLOSA "
      "   and req.t_RequestID = lnk.t_ObjID ";

    var rs : RsdRecordset = execSQLselectPrm( query, 
                                              SQLParam("MesID", MesID),
                                              SQLParam("OBJTYPE_REQCLOSA", OBJTYPE_REQCLOSA)
                                            );
    if( rs and rs.moveNext() )
      SubKind = rs.value("t_SubKind");
    end;

    return SubKind;
  end;

  private macro AnnulOneMes(rs /*: TRsbDataSet*/)
    var MesID : integer = rs.MessageID;

    var ClsAccSubKind : integer = 0;
    if( GetOpKind(rs) == ACCMSG_KIND_CLOSE )
      ClsAccSubKind = GetClsAccSubKindByMesID(MesID);
    end;

    var stat : integer = AnnulMes(MesID);

    var ErrMsg : string = "";
    if(stat)
      ErrMsg = GetErr(stat, "Ошибка при аннулировании сообщения");
    end;

    PrintRecord( rs, ClsAccSubKind, GetResultStr(rs, ErrMsg) );

  OnError(er)
    PrintRecord( rs, ClsAccSubKind, GetResultStr(rs, RsbGetError(er)) );
  end;

  macro GenMessages()
    PrintReportHeader();
    
    var cmd : RsdCommand = GetRecords();
    var rs = TRsbDataSet(cmd);

    var DialogFlag = TSetDialogFlag(0);

    while( rs.MoveNext() )
      if( rs.IsCancelled == ISCANCELLED_NO )
        GenOneMessage(rs);
      elif( rs.IsCancelled == ISCANCELLED_YES )
        AnnulOneMes(rs);
      end;
    end;

    if(CurrDprt)
      PrintFinishDep();
    end;
  end;

end;

private macro GetBOCodes(KindBO: string) : TArray
  var BOCodes : TArray = TArray();

  if(KindBO != "")
    
    BOCodes = split(KindBO, ",");
    
    var i : integer = 0;
    while(i < BOCodes.size)
      BOCodes[i] = trim(BOCodes[i]);
      i = i + 1;
    end;

    if(BOCodes.size > 0)
      RETURN BOCodes;
    end;

  end;

  RunError("Не удалось определить источник данных по счетам для формирования сообщений. Проверьте корректность параметров выполнения системного модуля");  
end;

macro GenAccMesBO(MapVal : PSRepMap)
  var KindBO     : string = MapVal.Value( "KindBO" );

  var mode_multi : bool = Opr_SetMultiExec( true ); // генерить будем в групповом режиме

  var BackOffice : TBackOffice = null;

  var BOCodes : TArray = GetBOCodes(KindBO);

  for(var BOCode : string, BOCodes )
    BackOffice = TBackOffice(BOCode, MapVal);
    BackOffice.GenMessages();
  end;

  Opr_SetMultiExec( mode_multi ); // восстанавливаем прежний режим

OnError(er)
  Opr_SetMultiExec( mode_multi ); // восстанавливаем прежний режим
  ExeptionMessage(er);
end;
