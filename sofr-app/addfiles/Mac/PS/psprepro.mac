 /*
 $Name: psprepro.mac
 $Module: РКО юридических лиц
 $Description: Предобработка клиентского платежа
 */

//-----------------------------------------------------------------------------
// Блок     : 29016 - "Предобработка клиентского платежа"
// Шаг      : 10    - "Системный контроль"
// Описание : Макрос шага
//-----------------------------------------------------------------------------
import PaymInter, pmdefbo, pm_syscont, pmpurp, pmchk117, pschkrst, pmfm, pmprepromass,
       MesInter, pschkbnk, "pm_allowtransfer.mac";

var PaymentObj:RsbPayment;

PRIVATE MACRO RevokePayment() : integer

  var stat : integer = УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_CLOSE );
  if(stat)
    msgbox("Ошибка при установке статуса платежа");
    stat = 1;
  end;

  PaymentObj.PaymStatus = PM_REJECTED;
  PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_REJECTED );
  
  return stat;
END;

MACRO ExecuteStep( doc, paymDoc, DocKind:integer, ID_Operation:integer, ID_Step:integer )
  var stat:integer = 0;
  var obj:object;
  var RegVal, error;

  if( stat == 0 )
    
    if( DocKind == PS_PAYORDER )
      obj = GenObject( "RsbPsPayOrder", PaymentObj.DocumentID );
    elif( DocKind == PS_CPORDER )
      obj = GenObject( "RsbPsCpOrder", PaymentObj.DocumentID );
    elif( DocKind == PS_INRQ )
      obj = GenObject( "RsbRequestOrder", PaymentObj.DocumentID );
    end;

    if( (DocKind == PS_PAYORDER) and IsRevokedByInitiator(PaymentObj) )
      return RevokePayment();
    end;

    //Контроль
    if( not IsSetStatusKind( ID_Operation, OPR_PAYM_CONTROL ) )
      if( PaymentObj.Origin == PAYMENT_OR_AUTO )
        stat = УстановитьСтатусыПлатежа( OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_CONTROL );
      else
        stat = УстановитьСтатусыПлатежа( OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_NOTCONTROL );
      end;
    end;
    //Направление
    if( stat == 0 )
      if( PaymentObj.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL )
        stat = УстановитьСтатусыПлатежа( OPR_PAYM_DIRECT, OPR_PM_ST_DIR_OUT );
      else
        stat = УстановитьСтатусыПлатежа( OPR_PAYM_DIRECT, OPR_PM_ST_DIR_INTERNAL );
      end;
    end;      
          
    //Уточнение причастности к терроризму - Не требуется
    if( stat == 0 )
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_TERR, OPR_PAYM_ST_TERR_NOTNEED );
    end;  
          
    //Квитовка - не сквитован
    if( stat == 0 )
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_OUT_KVIT, OPR_PM_ST_UNKVIT );
    end;

    //Квитанция - не сквитован
    if( stat == 0 )
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_KVT, OPR_PM_ST_UNKVIT );
    end;  
    
    //ЦАБС
    if( stat == 0 )
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_CABS, OPR_PM_ST_MFR_NO );
    end;
    
    //Обработка в БО - нет
    if( stat == 0 )
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_BO_PROCESS, OPR_PAYM_ST_BO_NO );
    end;  
    
    // ПЗО
    if( stat == 0 )
      if( ((DocKind == PS_PAYORDER) and (obj.Origin != PSPO_OR_SF) and (obj.Origin != PSPO_OR_RETACCUM)) or
          ((DocKind == PS_CPORDER ) and (obj.Origin != CP_OR_SF  ) and (obj.Origin != CP_OR_RETACCUM  )) or
           (DocKind == PS_INRQ    )
        )
        stat = УстановитьСтатусыПлатежа( OPR_PAYM_PZO, OPR_PAYM_ST_PZO_NEED );
      else
        stat = УстановитьСтатусыПлатежа( OPR_PAYM_PZO, OPR_PAYM_ST_PZO_NOTNEED );
      end;
    end;      

    // РО - не требуется
    if( stat == 0 )
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_MANUAL, OPR_PAYM_ST_MANUAL_NOTNEED );
    end;  

    // МФ
    if( stat == 0 )
      if( PaymentObj.StartDepartment != PaymentObj.EndDepartment )
        stat = УстановитьСтатусыПлатежа( OPR_PAYM_BRANCH, OPR_PAYM_ST_BRANCH_YES );
      else
        stat = УстановитьСтатусыПлатежа( OPR_PAYM_BRANCH, OPR_PAYM_ST_BRANCH_NO );
      end;
    end;      
          
    // КАРТ - нет
    if( stat == 0 )
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_INDEX, OPR_PAYM_ST_INDEX_NO );
    end;
    
    if( stat != 0 )      
      msgbox("Ошибка при установке статуса платежа");
      return 1;
    end;
  end;

  // Выбор бэк-офиса
  stat = ОпределениеБэкОфиса( PaymentObj );
  
  if( stat == 0 ) // Системный контроль
    stat = ExecuteSysControlStep( PaymentObj );
    // Проверить, не является ли платеж целевым финансированием
    if( stat == 0 )
      if( ( PaymentObj.IsPurpose == "" ) and ( IsPurposefulPayment( PaymentObj ) ) )
        PaymentObj.IsPurpose = "X";
      end;
    elif( stat < 0 )
      return 1;
    elif( stat > 0 )
      return 0;
    end;
  end;
  
  // Проверка платежей, позиционированных на корсхему, в которой  корреспондент corschem.CorrID является ПБР
  if( (stat == 0) and ПлатежВнешний( PaymentObj ) )
    stat = CheckPmPSBR( PaymentObj );
    if( stat < 0 )
      return 1;
    elif( stat > 0 )
      return 0;
    end;
  end;
  
  if( stat == 0 ) // Проверка на причастность к терроризму
    stat = ПроверкаПричастностиТерроризму( PaymentObj );
    if( stat < 0 )
      return 1;
    elif( stat > 0 )
      return 0;
    end;
  end;
  
  if( stat == 0 ) // Проверка на банкротство
    stat = ПроверитьСтатусБанкротства( PaymentObj );
    if( stat < 0 )
      return 1;
    elif( stat > 0 )
      return 0;
    end;
  end;

  if( stat == 0 ) // Контроль по 117-И
    stat = КонтрольПо117И( PaymentObj );
    if( stat < 0 )
      return 1;
    elif( stat > 0 )
      return 0;
    end;
  end;
  
  if( stat == 0 ) // Проверка остатков по счетам
    stat = PS_CheckAccRest( PaymentObj, ID_Operation, ID_Step );
  end;

  return stat;
END;


/* -----------------------------------------------------------------------------
   Массовое выполнение шага "Предобработка клиентского платежа"
   ----------------------------------------------------------------------------- */

/* -----------------------------------------------------------------------------
   Предтранзакционные действия 
   Все проверки отражаются только на временной таблице dpmprepro_tmp,
   никакие изменения в БД не делаются
   ----------------------------------------------------------------------------- */
macro PrepMassExecuteStep() 

  var stat:integer = execStoredFunc( "PS_POPREPRO.MassPreprocessPrepare", V_INTEGER );

  if( not stat )
    stat = PM_MassPreproPrepare();
  end;

  if( stat )
    MemoryError( stat );
  end;

  return stat;

OnError(er)
  ExeptionMessage( er );
  return 1;
end;

/* -----------------------------------------------------------------------------
   Транзакционные действия 
   Всё, что напроверяли, отражается на статусах документа, платежа, операции 
   ----------------------------------------------------------------------------- */
macro MassExecuteStep()

  var stat:integer = execStoredFunc( "PS_POPREPRO.MassPreprocessExecute", V_INTEGER );

  if( not stat )
    stat = PM_MassPreproExecute();
  end;

  if( stat )
    MemoryError( stat );
  end;

  return stat;

OnError(er)
  ExeptionMessage( er );
  return 1;
end;

/* -----------------------------------------------------------------------------
   Действия после транзакции
   Заполняем лог обработки платежей для отчета 
   ----------------------------------------------------------------------------- */
macro PostMassExecuteStep()

  var stat:integer = execStoredFunc( "PM_PREPRO.MassFillLog", V_INTEGER );

  if( stat )
    MemoryError( stat );
  end;

  return stat;

OnError(er)
  ExeptionMessage( er );
  return 1;
end;
