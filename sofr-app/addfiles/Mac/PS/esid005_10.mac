/*
$Name:             esid005_10.mac
$Module:           РКО юридических лиц
$Description:      Операция 4004. Шаг <Выгрузка в МБР>
*/
import PaymInter, InsCarryDoc, WldInter, MesInter, "rmtools.mac", "multypm.mac", "pm_opr.mac", "cbsttls.mac";
import pmsummo;

var PaymentObj:RsbPayment;

private macro ТранспортСообщения( ID_Kind, MesID )
  var TpID:integer;
  var rs:object;
  var select:string;
  var params:TArray;

  if( ID_Kind == OPR_ID_REAL)
    select = "select tpschem.t_TpID " +
              "from dwlmes_dbt wlmes, dwltpshem_dbt tpschem "; 
  else
    select = "select tpschem.t_TpID " +
              "from dwlmes_tmp wlmes, dwltpshem_dbt tpschem "; 
  end;

  select = select + "where wlmes.t_MesID = :MesID and wlmes.t_TpSchemID = tpschem.t_TpShemID ";
  params = makeArray( SQLParam("MesID", MesID) );

  rs = execSQLselect( select, params, FALSE );

  if( rs.MoveNext() )
    TpID = rs.value(0);
  else
    TpID = -1;
  end;

  return TpID;
end;

//-----------------------------------------------------------------------------
// Выгрузка в МБР
//-----------------------------------------------------------------------------
macro ExecuteStep( doc, first, KindDoc, ID_Operation, ID_Step )
  var stat:integer = 0;                                                                                                            
  var ID_Kind, MesID, Context, str, depName = "";
  var errMes : string = "";

  stat = CheckExternalPaymentForRls( PaymentObj.PaymentID, @errMes, true );

  if( not stat )
    stat = InsertPaymentMessage( PaymentObj.PaymentID, ID_Kind, MesID );
  end;

  if( stat )
    if( errMes == "" )
      errMes = "Ошибка при генерации сообщения по платежу";
    end;
    РасширенноеСообщениеОбОшибке( errMes );
  else
    // наложение ЭЦП на сообщение
    Context = "ТранспортМБР|" + ТранспортСообщения(ID_Kind, MesID) + "|Выгрузка платежа в МБР";
    InsertMesSign( ID_Kind, MesID, Context );
    FiscLogDischDoc(PaymentObj.PaymentID);
  end;

  return stat;
end;

macro CheckStepAction( mes )
   var MultyPaymID;
   RECORD wldmes( wlmes  );

   if( ПолучитьСообщение( PaymentObj.PaymentID, OBJTYPE_PAYMENT, 0, wldmes, NULL ) )
      if(wldmes.State > WLD_STATUS_MES_DEFECT)
         msgbox("По распоряжению сформировано и выгружено сообщение, откат операции запрещен. Откатите выгрузку сообщения в п/с Межбанковские расчеты и повторите откат");
         return 1;
      end;
   end;
     
   return 0;
end;