/*
  $Name:         prn_req.mac
  $Module:       РКО
  $Description:  Печать платежного требования-поручения
*/
/* prn_req.mac                                   Дудин В.А.             */
/* ============= Печать платежного требования-поручения ==================


 =========================================================================*/
 import BankInter, PTInter,OprInter,PaymInter,rslx,payconst,PSInter,"adress.mac", "prpm.mac", "psprncom.mac";

/* ---------------------------- Вывод отчета ---------------------------- */
macro DrawReport_Request(date_in, number, date_,dptch,payerINN,
                payer, deb_acc, deb_corracc, bank_payer,  payerMFO,
                sum,receiverINN,
                receiver, kred_acc, kred_corracc,bank_receiver, receiverMFO,
                ground, queue, paydate, kindoper,AcceptPeriod,
                ReqSum,DocumentID,I2_Date )

  var kopeck,rub,req_rub,
      divide_kopeck,
      sumstr,reqsumstr,/*meetsumstr,*/Rest,/*reststr,*/
      pospoint,copy,SubPurp,ErrMsg;

  divide_kopeck = РазделительКопеек;
  copy = КоличествоКопий;

  if(MassCopy == 0)
    if(copy == 0)
      copy = 1;
      if ( not GetInt(copy,"Введите количество копий.",3))
        copy = 0;
      end;
    end;
  else copy = MassCopy;
  end;

  if(copy == 0) return false;end;

  ARRAY SS,SG,SBP,SBR,SP,SR,SQ;

  sumstr = string(MoneyL(sum));
  rub = RubToStrAlt(sum);
  pospoint = Index(sumstr, ".");
  if(Int(SubStr(sumstr, pospoint + 1, 2)) == 0 )
    rub = SubStr(rub,1,Index(rub,"коп")-4);
  end;

  reqsumstr = string(MoneyL(ReqSum));
  req_rub = RubToStrAlt(ReqSum);
  pospoint = Index(reqsumstr, ".");
  if(Int(SubStr(reqsumstr, pospoint + 1, 2)) == 0 )
    req_rub = SubStr(req_rub,1,Index(req_rub,"коп")-4);
  end;

  ground = InterpretGround( ground );

  strsplit( payer ,             SP,    44,44,4 ); /* Плательщик      */
  strsplit( bank_payer,         SBP,   44,44,3 ); /* Банк-плательщик */
  strsplit( rub,                SS,    52,52,4 ); /* Сумма  к оплате */
  strsplit( ground,             SG,    68,68,3 ); /* Основание       */
  strsplit( bank_receiver,      SBR,   44,44,3 ); /* Банк-получатель */
  strsplit( receiver,           SR,    44,44,4 ); /* Получатель      */
  strsplit( req_rub,            SQ,    54,54,4 ); /* Сумма           */

  date_ = string( date_ );
  paydate = string( paydate );
  if( I2_Date != Date(0,0,0000) )
     I2_Date = string(I2_Date);
  else
     I2_Date = "";
  end;

while(copy != 0)
[
                                                                    +---------+
                                                                    | 0401064 |
     ############       ##########                                  +---------+
  -------------------  ------------
  Посту. в банк плат.  Срок платежа

  ПЛАТЕЖНОЕ                               ##########  #############
  ТРЕБОВАНИЕ-ПОРУЧЕНИЕ N ###############  ----------  -------------
                                            (Дата)    (Вид платежа)
 -------------------------------------------------------------------------------
  Сумма   |######################################################|        |
  прописью|######################################################|Срок для|###
          |######################################################|акцепта |
          |######################################################|        |
 -------------------------------------------------------------------------------
  ИНН ########################################|СУММА  |#########################
                                              |       |
  ############################################|       |
  ############################################|-------+-------------------------
  ############################################| Сч.N. |#########################
  ############################################|       |
  ПЛАТЕЛЬЩИК                                  |       |
 ---------------------------------------------+-------|
  ############################################| БИК   |#########################
  ############################################|-------|
  ############################################| Сч.N. |#########################
  БАНК ПЛАТЕЛЬЩИКА                            |       |
 ---------------------------------------------+-------+-------------------------
  ############################################| БИК   |#########################
  ############################################|-------|
  ############################################| Сч.N. |#########################
  БАНК ПОЛУЧАТЕЛЯ                             |       |
 ---------------------------------------------+-------|
  ИНН ########################################| Сч.N. |#########################
                                              |       |
                                              |-------+-------------------------
                                              |Вид Оп.|####|        |
  ############################################+-------+    |Очер.пл.| ###
  ############################################|Наз.пл.|    |        |
  ############################################+-------+    +--------+
  ############################################| Код   |    |Рез.поле|
  ПОЛУЧАТЕЛЬ                                  |       |    |        |
 -------------------------------------------------------------------------------
  Назначение платежа
  #############################################################################
  #############################################################################
  #############################################################################

 -------------------------------------------------------------------------------

                           Подписи получателя         Отметки банка получателя

        М.П.
                     -------------------------------

                     -------------------------------


 -------------------------------------------------------------------------------
  Сумма к оплате прописью                             | Сумма к оплате
  ####################################################|#########################
  ####################################################|
  ####################################################|
  ####################################################|
 -------------------------------------------------------------------------------

                           Подписи плательщика

        М.П.                                                                    Дата помещения
                     -------------------------------                             в картотеку
                                                                                 ##########
                     -------------------------------


         -------------------------------------------------------------------     Отметки банка
   N ч.  |   N плат.  |  Дата      |  Сумма       |  Сумма      | Подпись
   плат. |   ордера   |  плат.     |  частичного  |  остатка    |
         |            |  ордера    |  платежа     |  платежа    |
  -------+------------+------------+--------------+-------------+-----------]
 (date_in:c,paydate:c,
  date_:c,dptch:c,number:l,
  SQ(0):l,SQ(1):l,AcceptPeriod,SQ(2),SQ(3),
  payerINN,ReqSum:l:f,SP(0):l,SP(1):l,SP(2):l,
  deb_acc:l,SP(3):l,
  SBP(0):l,payerMFO:l,SBP(1):l,SBP(2):l,deb_corracc:l,
  SBR(0):l,receiverMFO:l,SBR(1):l,SBR(2):l,kred_corracc:l,receiverINN,
  kred_acc:l,
  kindoper:l,SR(0):l,queue:l,SR(1):l,SR(2):l,SR(3):l,
  SG(0):l,SG(1):l,SG(2):l,

  SS(0):l, sum:l:f,
  SS(1), SS(2), SS(3),
  I2_Date
  );

  SubPurp = 1;
  while(SubPurp != 0 )
    pm_key.DocKind = PS_PAYORDER;
    pm_key.DocumentID = DocumentID;
    pm_key.Purpose = PM_PURP_POPRIMARY;
    pm_key.SubPurpose = SubPurp;
    if (not GetEQ(pm_key))
      SubPurp = 0;
      if(Status() != 4) MsgBox("Ошибка Btrieve :", Status());end;
    end;
    if(SubPurp != 0)
      rm_key.PaymentID = pm_key.PaymentID;
      if (not GetEQ(rm_key))
        SubPurp = 0;
        if(Status() != 4) MsgBox("Ошибка Btrieve :", Status());end;
      end;
    end;

    if(SubPurp != 0)
      /* Получить сумму остатка платежного документа для заданной частичной оплаты */
      Rest = RsbPayment( pm_key.PaymentID ).PartPaymRestAmountMain;
    end;

    if(SubPurp != 0)

[  ##### | ########## | ########## | ############ | ########### |
  -------+------------+------------+--------------+-------------+-----------

]
 (
  SubPurp:l,rm_key.Number:l,rm_key.Date:c,pm_key.Amount:l:f,Rest:l:f
 );
    SubPurp = SubPurp + 1;
    end;
 end;

 copy = copy - 1;
 end;

 return true;

end;


/* ================= Печать платежного поручения ============================= */
 macro PSPO_print_request ( aa, bb, cc, dd, ee )

 record pt  (party);
 record RecordAdress ( adress );
 file pknd(pmpopknd) key 0;

 VAR
   dptch,
   payer, deb_acc, deb_corracc, bank_payer,  payerMFO,
   sum,
   receiver, kred_acc, kred_corracc,bank_receiver, receiverMFO, Ground,
   I2_Date, /* Дата помещения в картотеку N2 */
   MFO,CorAc,
   rub, divide_kopeck,sumstr,req_rub, req_sumstr,
   Use_VRSL, err, VRSL, report,
   stat, pospoint,
   SubPurp, Rest, ErrMsg, reststr, meetsumstr;

   setbuff( payord,   aa );
   setbuff( pm_paym,  bb );
   setbuff( pm_prop,  cc );
   setbuff( pm_rmprop,dd );
   setbuff( paydem,   ee );

   GetRegistryValue( ПутьНастройкиИспользованияVRSLТребованииПоручении, V_INTEGER, Use_VRSL, err );
   if ( err != REG_OK )
      MsgBox( "Не определен ключ " ,ПутьНастройкиИспользованияVRSLТребованииПоручении, " в настройках банка" );
      return 1;
   end;

   /*Реквизиты плательщика*/
   deb_acc     = pm_paym.PayerAccount;
   bank_payer  = {Name_Bank} + ", " + {Post_Addr};
   payerMFO    = {MFO_Bank};

   deb_corracc = {CORAC_Bank};

   /*Реквизиты получателя*/
   kred_acc = pm_paym.ReceiverAccount;
   if ( pm_prop.BankCode == "" ) /* Внутренний документ банка */
     bank_receiver = pm_rmprop.ReceiverBankName;
     if(bank_receiver == "") bank_receiver = {Name_Bank}; end;
     bank_receiver = bank_receiver + ", " + {Post_Addr};
     receiverMFO   = {MFO_Bank};
     kred_corracc  = {CORAC_Bank};
   else
     if ( ПолучитьСубъекта (pm_paym.ReceiverBankID,pt) != 0 )
       msgbox("Не найден банк-получатель");
       return 1;
     end;
     GetPartyProperty(pm_paym.ReceiverBankID,pt,MFO,CorAc);
     ClearRecord(RecordAdress);
     НайтиЮридическийАдресСубъекта(pt.PartyID,RecordAdress);
     bank_receiver = pm_rmprop.ReceiverBankName + ", " + RecordAdress.Adress;
     receiverMFO   = MFO;
     kred_corracc  = CorAc;
  /*    receiverMFO   = pm_prop.BankCode;
     kred_corracc  = pm_rmprop.ReceiverCorrAccNostro;*/
   end;

   pknd.PaymentKind = pm_rmprop.PaymentKind;
   if (GetEQ(pknd))
     dptch = pknd.Name;
   else
     msgbox("Не найден вид платежа");
   end;
     dptch = pknd.Name;

   if( not Use_VRSL)
     stat = DrawReport_Request( pm_rmprop.Date,
            pm_rmprop.Number, pm_rmprop.Date, dptch,
            pm_rmprop.PayerINN,
            pm_rmprop.PayerName, deb_acc, deb_corracc,
            bank_payer, payerMFO,
            pm_paym.Amount,
            pm_rmprop.ReceiverINN,
            pm_rmprop.ReceiverName, kred_acc, kred_corracc,
            bank_receiver, receiverMFO,
            pm_rmprop.Ground, pm_rmprop.Priority, pm_rmprop.PayDate, pm_rmprop.ShifrOper,
            paydem.AcceptPeriod,paydem.ReqSum,paydem.OrderID,payord.I2PlaceDate);

     if(not stat) return 1;
     else return 0;
     end;
   else
      VRSL = ActiveX("RSGENOLE.VRslRun.1");
      report = VRSL.RunRpt("ПлатежноеТребованиеПоручение");

      divide_kopeck = РазделительКопеек;

      sumstr = string(MoneyL(pm_paym.Amount));
      rub = RubToStrAlt(pm_paym.Amount);
      pospoint = Index(sumstr, ".");
      if(Int(SubStr(sumstr, pospoint + 1, 2)) == 0 )
        rub = SubStr(rub,1,Index(rub,"коп")-4);
      end;

      req_sumstr = string(MoneyL(paydem.ReqSum));
      req_rub = RubToStrAlt(paydem.ReqSum);
      pospoint = Index(req_sumstr, ".");
      if(Int(SubStr(req_sumstr, pospoint + 1, 2)) == 0 )
        req_rub = SubStr(rub,1,Index(req_rub,"коп")-4);
      end;

      report.DataPostup.text       = string(pm_rmprop.Date);
      report.Number.text           = pm_rmprop.Number;
      report.Data.text             = string(pm_rmprop.Date);
      report.Dispatch.text         = dptch;
      report.SrokAkcepta.text      = string(paydem.AcceptPeriod);
      report.INN_Payer.text        = pm_rmprop.PayerINN;
      report.Sum.text              = string(paydem.ReqSum:f);
      report.SumText.text          = req_rub;
      report.Sum_1.text            = string(pm_paym.Amount:f);
      report.SumText_1.text        = rub;
      report.Payer.text            = pm_rmprop.PayerName;
      report.Account_Payer.text    = deb_acc;
      report.Bank_Payer.text       = bank_payer;
      report.MFO_Payer.text        = payerMFO;
      report.Corac_Payer.text      = deb_corracc;
      report.Receiver.text         = pm_rmprop.ReceiverName;
      report.Account_Receiver.text = kred_acc;
      report.MFO_Receiver.text     = receiverMFO;
      report.Bank_Receiver.text    = bank_receiver;
      report.Corac_Receiver.text   = kred_corracc;
      report.INN_Receiver.text     = pm_rmprop.ReceiverINN;
      report.Ground.text           = InterpretGround( pm_rmprop.Ground) ;
      report.Kind_Oper.text        = pm_rmprop.ShifrOper;
      report.Pay_Date.text         = string(pm_rmprop.PayDate);
      report.Payment.text          = string(pm_rmprop.Priority);

      if( payord.I2PlaceDate != Date(0,0,0000) )
          report.Data_I2.text      = string(payord.I2PlaceDate);
      else
          report.Data_I2.text      = " ";
      end;

      SubPurp = 1;
      while(SubPurp != 0 )
         pm_key.DocKind = PS_PAYORDER;
         pm_key.DocumentID = paydem.OrderID;
         pm_key.Purpose = PM_PURP_POPRIMARY;
         pm_key.SubPurpose = SubPurp;
         if (not GetEQ(pm_key))
           SubPurp = 0;
           if(Status() != 4) MsgBox("Ошибка Btrieve :", Status());end;
         end;
         if(SubPurp != 0)
           rm_key.PaymentID = pm_key.PaymentID;
           if (not GetEQ(rm_key))
             SubPurp = 0;
             if(Status() != 4) MsgBox("Ошибка Btrieve :", Status());end;
           end;
         end;

         if(SubPurp != 0)
           /* Получить сумму остатка платежного документа для заданной частичной оплаты */
           Rest = RsbPayment( pm_key.PaymentID ).PartPaymRestAmountMain;
         end;

         if(SubPurp != 0)

           report.TablePaym1.listtext.at(0).text = string(SubPurp);
           report.TablePaym2.listtext.at(0).text = rm_key.Number;
           report.TablePaym3.listtext.at(0).text = string(rm_key.Date);
           report.TablePaym4.listtext.at(0).text = string(pm_key.Amount:f);
           report.TablePaym5.listtext.at(0).text = string(Rest:f);
           report.PutElement(report.TablePaym1);
           report.PutElement(report.TablePaym2);
           report.PutElement(report.TablePaym3);
           report.PutElement(report.TablePaym4);
           report.PutElement(report.TablePaym5);

         SubPurp = SubPurp + 1;
         end;
      end;

      report.runReport ();
      if (IsStandAlone)
       ViewRep (report);
      else
       TransferRep (report);
      end;
   end;

  end;

 END;
