/* prreqopa.mac                                   Дудин В.А.              */
/* ============= Печать заявления на открытие счета ======================*/

/* ---------------------------- Вывод отчета ---------------------------- */
IMPORT reqinter, OprInter, PSInter, "adress.mac";

CONST РазделительКопеек   = "="; /* "-" или "="*/
VAR   КоличествоКопий     = 1 ;

VAR АдресГНИ = "", ИНН_БАНКА = "", ИНН_Клиента = "", РеквизитыРегистрации = "", ВидДокументаРегистрации = "";

RECORD tax(party);
RECORD ReqCloseAccount(reqclosa);
FILE   reg(objrgdoc);
FILE   acc("account");
FILE   cert("certif") key 2;
FILE   pr_party("party");

MACRO DrawReport(ПризнакЗакрытия,Дата,НомерСчета,НаименованиеТипаСчета)

  VAR НомерКопии = 1;

  ИНН_БАНКА = GetPartyINN({OurBank},1);

  if(valtype(НаименованиеТипаСчета) != V_STRING) НаименованиеТипаСчета = ""; end;
  if(НаименованиеТипаСчета == "") НаименованиеТипаСчета = "расчетный"; end;

while(НомерКопии <= КоличествоКопий)
[                                                                              ];
[ В Госналогинспекцию по ####################                                  ](tax.ShortName);
[ Почтовый адрес         ######################################################](АдресГНИ);
[                                                                              ];
[   1.СООБЩЕНИЕ БАНКА НАЛОГОВОМУ ОРГАНУ                         Бланк N        ];
if(ПризнакЗакрытия == 0)
[     ОБ ОТКРЫТИИ БАНКОВСКОГО СЧЕТА                         ######  ########   ](cert.Series, cert.NumberFirst);
else
[     О ЗАКРЫТИИ БАНКОВСКОГО СЧЕТА                          ######  ########   ](cert.Series, cert.NumberFirst);
end;
[                                                                              ];
[ Банк ######### ИНН ####################################                      ]({MFO_Bank},ИНН_БАНКА);
[        (БИК)             ИНН           КПП                                   ];
[ Сообщает, что клиенту, имеющему ИНН: ####################################    ](ИНН_Клиента);
[                                            ИНН           КПП                 ];
[ по предъявленному ########################################                   ](ВидДокументаРегистрации);
[                                  ############################################](РеквизитыРегистрации);
if(ПризнакЗакрытия == 0)
[ После заключения договора банк.счета от ########################             ](ReqOpenAcc.Date :m);
else
[ После расторжения договора банк.счета от ########################            ](ReqCloseAccount.Date :m);
end;
if(ПризнакЗакрытия == 0)
[ N ############### Открыт ####################################################](ReqOpenAcc.Number,НаименованиеТипаСчета);
else
[ N ############### Закрыт ####################################################](ReqCloseAccount.Number,НаименованиеТипаСчета);
end;
[ Счет ################################                                        ](НомерСчета:f);
[ Должностное лицо банка                                                       ];
[                                                 Дата ########################]({curdate} :m);
[--------------------Линия отрыва (для госналогинспекции)----------------------];
[ В Госналогинспекцию по ####################                                  ](tax.ShortName);
[                                                                              ];
if(ПризнакЗакрытия == 0)
[   2.УВЕДОМЛЕНИЕ О ПОЛУЧЕНИИ СООБЩЕНИЯ БАНКА ОБ ОТКРЫТИИ БАНКОВСКОГО СЧЕТА    ];
else
[   2.УВЕДОМЛЕНИЕ О ПОЛУЧЕНИИ СООБЩЕНИЯ БАНКА О ЗАКРЫТИИ БАНКОВСКОГО СЧЕТА     ];
end;
[ Госналогинспекция           , тел            уведомляет, что                 ];
[ Получила сообщение N ###### ######## о том, что ######################## банк](cert.Series, cert.NumberFirst,Дата:m);
if(ПризнакЗакрытия == 0)
[ ######### (ИНН:####################################) открыл лицу             ]({MFO_Bank},ИНН_БАНКА);
else
[ ######### (ИНН:####################################) закрыл лицу             ]({MFO_Bank},ИНН_БАНКА);
end;
[ ИНН: ######################################                                  ](ИНН_Клиента);
[          ИНН           КПП                                                   ];
[ по предъявленному ########################################                   ](ВидДокументаРегистрации);
[                                 #############################################](РеквизитыРегистрации);
[ ############################################# Счет ##########################](НаименованиеТипаСчета, НомерСчета:f);
[        (наименование(вид,тип) счета)                                         ];
[ Должностное лицо налогового органа                                           ];
[                                                 Дата                         ];
[--------------------------Линия отрыва (для банка)----------------------------];
if(ПризнакЗакрытия == 0)
[ 3.КОРЕШОК СООБЩЕНИЯ   ###### ########           ОБ ОТКРЫТИИ БАНКОВСКОГО СЧЕТА](cert.Series, cert.NumberFirst);
else
[ 3.КОРЕШОК СООБЩЕНИЯ   ###### ########            О ЗАКРЫТИИ БАНКОВСКОГО СЧЕТА](cert.Series, cert.NumberFirst);
end;
[ В Госналогинспекцию по ####################                                  ](tax.ShortName);
[                         направлено сообщение                                 ];
[ банк ####################################                                    ](ИНН_БАНКА);
if(ПризнакЗакрытия == 0)
[ Открыл клиенту ####################################                          ](ИНН_Клиента);
else
[ Закрыл клиенту ####################################                          ](ИНН_Клиента);
end;
[                          ИНН           КПП                                   ];
[ по предъявленному ###########################################################](ВидДокументаРегистрации);
[ #############################################################################](РеквизитыРегистрации);
[ ############################################# Счет ##########################](НаименованиеТипаСчета,НомерСчета:f);
[ Должностное лицо банка                                                       ];
[                                                 Дата ########################]({curdate} :m);
[--------------------------Линия отрыва (для банка)----------------------------];
if(ПризнакЗакрытия == 0)
[ 4.УВЕДОМЛЕНИЕ N  ###### ########          БАНКА ОБ ОТКРЫТИИ БАНКОВСКОГО СЧЕТА](cert.Series, cert.NumberFirst);
else
[ 4.УВЕДОМЛЕНИЕ N  ###### ########           БАНКА О ЗАКРЫТИИ БАНКОВСКОГО СЧЕТА](cert.Series, cert.NumberFirst);
end;
[ Банк ######### ИНН:####################################                      ]({MFO_Bank},ИНН_БАНКА);
[                          ИНН           КПП                                   ];
[ корр/cчет ##########################                                         ]({CORAC_Bank});
if(ПризнакЗакрытия == 0)
[ Уведомляет, что ####################### открыл клиенту, имеющему             ](Дата:m);
else
[ Уведомляет, что ####################### закрыл клиенту, имеющему             ](Дата:m);
end;
[ ИНН: ####################################                                    ](ИНН_Клиента);
[             ИНН           КПП                                                ];
[ по предъявленному ########################################                   ](ВидДокументаРегистрации);
[                             #################################################](РеквизитыРегистрации);
[ ############################################# Счет ##########################](НаименованиеТипаСчета,НомерСчета:f);
[ Должностное лицо банка                                                       ];
[                                                 Дата ########################]({curdate} :m);
[                                                                              ];
[];

НомерКопии = НомерКопии + 1;
end;
  return;
END;


MACRO ПолучитьНомерИзвещения(Code_Currency,Chapter,Account)
   VAR ercode, stat = false;
    cert.FIID  = AccNoteFIID;
    cert.ConnAccount  = Account;
    cert.Series = cert.NumberFirst = "";

    stat = GetGE(cert);
    if( (not stat) )
      ercode = status();
      if( (ercode != 4) and (ercode!=9) )
        MsgBox("Ошибка при определении номера извещения");
        cert.Series = cert.NumberFirst = "";
        return stat;
      end;
    end;

    if( (not stat) or (cert.FIID  != AccNoteFIID) or (cert.ConnAccount  != Account))

      acc.Chapter = Chapter;
      acc.Code_Currency = Code_Currency;
      acc.Account = Account;
      if(not GetEQ(acc))
        cert.Series = cert.NumberFirst = "";
        ercode = status();
        if( (ercode != 4) and (ercode!=9) )
          MsgBox("Ошибка при определении счета");
          return stat;
        end;
      else
        if(SetAccNoteNumber(Account, cert.Series, cert.NumberFirst ) == 0)  stat = true;
        end;
      end;
    end;
  return stat;
END;


MACRO ДатаОперацииНадСчетом(ПризнакЗакрытия,Code_Currency,Chapter,Account,Дата)

  VAR ercode, stat = false;

  Дата = Date(0,0,0);

  acc.Chapter = Chapter;
  acc.Code_Currency = Code_Currency;
  acc.Account = Account;
  if(not GetEQ(acc))
    ercode = status();
    if( (ercode != 4) and (ercode!=9) )
      MsgBox("Ошибка при определении счета");
      return stat;
    end;
  else
    if(ПризнакЗакрытия == 0) Дата = acc.Open_Date;
    else                     Дата = acc.Close_Date;
    end;
  end;

  setparm(4,Дата);
  return true;
END;

MACRO ПолучитьАдресГНИ()
  record RecordAdress ( adress );
  ClearRecord(tax);
  ClearRecord(RecordAdress);
  if((pt.PartyID != -1) and (pt.TaxInstitution != -1) and (ПолучитьСубъекта(pt.TaxInstitution,tax) != 0) )
    msgbox("Не найдена налоговая инспекция");
    return true;
  end;
  НайтиЮридическийАдресСубъекта(tax.PartyID,RecordAdress);
  АдресГНИ = RecordAdress.PostIndex+" "+RecordAdress.District+" "+RecordAdress.Region+" "+RecordAdress.Place+" "+RecordAdress.Adress;

  return true;
END;

MACRO ПолучитьРеквизитыРегистрации()

  VAR OK:bool;
  FILE dk_reg(objkdoc);
  VAR ercode;

  РеквизитыРегистрации = "";
  if(pr_party.rec.PartyID != -1)
    reg.RegPartyID   = pr_party.rec.PartyID;
    reg.IsClosed = "";
    reg.RegPartyKind = 7;
    if(not GetEQ(reg))
      ercode = status();
      if( (ercode != 4) and (ercode!=9) )
        MsgBox("Ошибка при определении данных о регистрации клиента");
        return false;
      end;
    else
      dk_reg.RegDocKind = reg.RegDocKind;
      if(getEQ(dk_reg)) ВидДокументаРегистрации = dk_reg.Name; end;
      РеквизитыРегистрации = "серия "+reg.Series+ " N "+reg.Number+" от "+string(reg.StartDate:f);
    end;
  end;

  return true;

END;

MACRO НапечататьСвязанныйСчет(ПризнакЗакрытия,GroupID,Code_Currency,Chapter,Account)
  VAR ДатаОткрытия,НаименованиеТипаСчета = "";

  if(not ДатаОперацииНадСчетом(0,Code_Currency,Chapter,Account,ДатаОткрытия)) return false; end;

  ПолучитьНомерИзвещения(Code_Currency,Chapter,Account);

  if(GroupID == OBJROLE_ACC_SPECTRANSIT) НаименованиеТипаСчета = "расчетный спецтранзитный";
  elif(GroupID == OBJROLE_ACC_TRANSIT)   НаименованиеТипаСчета = "расчетный транзитный";
  end;

  DrawReport(ПризнакЗакрытия,ДатаОткрытия,Account,НаименованиеТипаСчета);

  return true;
END;

/* Макрос печати заявления на открытие счета */
/* Возвращает false при отказе от печати */
MACRO ReqOpenAcc_Print(GroupID, Num)
/* GroupID - номер группы привязанного к заявлению счета
   Если GroupID == -1, то печаются формы для счета из заявления и для всех связанных счетов.
   Если GroupID ==  0, то печатается только форма для счета из заявления.
   Если GroupID  >  0, то печатается только форма для связанного счета с заданным GroupID.
*/
  FILE LinkAcc(reqlinka);
  VAR stat = false, ДатаОткрытия, flag;

  if(not ПолучитьАдресГНИ()) return false; end;

  if(pt.PartyID != -1) ИНН_Клиента = GetPartyINN(pt.PartyID,1); end;

  if(not ПолучитьРеквизитыРегистрации()) return false; end;

  if( Num ) КоличествоКопий = Num; end;
  if(КоличествоКопий == 0) КоличествоКопий = 1; end;
  if(not GetInt(КоличествоКопий,"Введите количество копий.",3)) КоличествоКопий = 0; end;

  if(КоличествоКопий <= 0) return false; end;

  /* Печать формы для счета из заявления */
  if(GroupID <= 0)
    if(not ДатаОперацииНадСчетом(0,ReqOpenAcc.Code_Currency,1,ReqOpenAcc.Account,ДатаОткрытия)) return false; end;
    ПолучитьНомерИзвещения(ReqOpenAcc.Code_Currency,1,ReqOpenAcc.Account);
    DrawReport(0,ДатаОткрытия,ReqOpenAcc.Account);
  end;

  if(GroupID > 0)   /* Печать формы для связанного счета c номером группы счета GroupID */
    LinkAcc.DocKind   = PS_REQOPENA;
    LinkAcc.RequestID = ReqOpenAcc.RequestID;
    LinkAcc.GroupID   = GroupID;
    LinkAcc.Action    = 0;

    if(not GetEQ(LinkAcc))
      MsgBox("Не найден связанный счет");
      return false;
    else
      if(not НапечататьСвязанныйСчет(0,LinkAcc.GroupID,LinkAcc.Code_Currency,LinkAcc.Chapter,LinkAcc.Account)) return false; end;
    end;
  elif(GroupID < 0)   /* Печать формы для всех связанных счетов */
    LinkAcc.DocKind   = PS_REQOPENA;
    LinkAcc.RequestID = ReqOpenAcc.RequestID;
    LinkAcc.GroupID   = 0;
    LinkAcc.Action    = 0;
    flag = GetGE(LinkAcc);
    while( (flag) and (LinkAcc.RequestID == ReqOpenAcc.RequestID) and (LinkAcc.DocKind == PS_REQOPENA) and (LinkAcc.Action == 0))
      if(not НапечататьСвязанныйСчет(0,LinkAcc.GroupID,LinkAcc.Code_Currency,LinkAcc.Chapter,LinkAcc.Account)) return false; end;
      flag = next(LinkAcc);
    end;
  end;

  return true;
END;

/* Макрос печати заявления на закрытие счета */
/* Возвращает false при отказе от печати */
MACRO ReqCloseAcc_Print(Num)
  FILE dk_reg(dk_reg);
  VAR stat = false, ДатаЗакрытия, flag;

  if( Num ) КоличествоКопий = Num; end;

  if(not ПолучитьАдресГНИ()) return false; end;

  if(pt.PartyID != -1) ИНН_Клиента = GetPartyINN(pt.PartyID,1); end;

  if(not ПолучитьРеквизитыРегистрации()) return false; end;

  if(КоличествоКопий == 0) КоличествоКопий = 1; end;
  if(not GetInt(КоличествоКопий,"Введите количество копий.",3)) КоличествоКопий = 0; end;

  if(КоличествоКопий <= 0) return false; end;
  /* Печать формы для счета из заявления */

  if(not ДатаОперацииНадСчетом(1,ReqCloseAccount.Code_Currency,1,ReqCloseAccount.Account,ДатаЗакрытия)) return false; end;
  ПолучитьНомерИзвещения(ReqCloseAccount.Code_Currency,1,ReqCloseAccount.Account);
  DrawReport(1,ДатаЗакрытия,ReqCloseAccount.Account);

  return true;
END;

/* Вызов происходит перед печатью 1-го документа из списка */
MACRO ReqC_HeaderMassPrint
  var MassCopy = 1;
  if ( not GetInt(MassCopy,"Введите количество копий.",3))
    MassCopy = 0;
  end;
  return MassCopy;
END;

/* Вызов происходит после печати последнего документа */
MACRO ReqC_ItogMassPrint
END;
