/* grnd_prn.mac                                   Дудин В.А.             */
/* ============= Печать справочника оснований документов ================*/

 file ground(pspogrnd) key 1;

MACRO print_gr(Oper,ground)
 ARRAY SS;
 var len = 0, num = 0, j = 0;
 if((Oper == ground.Oper) or (Oper == -1))
   len = strlen(ground.GroundText);
   num = (len/84);
   strsplit(ground.GroundText,SS, 84,84,num );
   if(Oper == -1 )
     [| ####### | #### | #################################################################################### |]
     (ground.GroundCode,ground.Oper,SS(0));
   else
     [| ####### | ####################################################################################        |]
     (ground.GroundCode,SS(0));
   end;
   j = 1;
   while(num)
     if(Oper == -1 )
       [|         |      | #################################################################################### |]
       (SS(j));
     else 
       [|         | ####################################################################################        |]
       (SS(j));
     end;
     num = num - 1;
     j = j + 1;
   end;
   [|-------------------------------------------------------------------------------------------------------|];
 end;
END;

/* ==== Печать справочника оснований рублевых платежных документов ======= */
 macro PSPO_GROUND ( Oper )
      var Title = "";
      Rewind( ground );
      if(Oper == -1) /* Печать справочника для всех операционистов */
        Title = "СПРАВОЧНИК ОСНОВАНИЙ";
      elif(Oper == 0) Title = "ОБЩИЙ СПРАВОЧНИК ОСНОВАНИЙ";
      else            Title = "СПРАВОЧНИК ОСНОВАНИЙ ОПЕРАЦИОНИСТА \"" + string(Oper) +"\"";
      end;
      if(not next(ground))
        [Справочник оснований пуст]
      else
        [
                          ############################################](Title);
        [/-------------------------------------------------------------------------------------------------------\];
        if(Oper == -1 )
        [|   Код   | Опер | Наименование основания                                                               |];
        else
        [|   Код   | Наименование основания                                                                      |];
        end;
        [|-------------------------------------------------------------------------------------------------------|];
        print_gr(Oper,ground);
        while(next(ground)) print_gr(Oper,ground); end;
      end;
      return 0;
 END;
