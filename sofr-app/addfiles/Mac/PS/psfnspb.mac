/*
$Name:           psfnspb.mac
$Module:         РКО
$Description:    Макрос скроллинга входящих подтверждений банка
*/
//-----------------------------------------------------------------------------
// Блок      : Вне блока
// Шаг       : Вне шага
// Назначение: Макрос скроллинга
// Описание  : Макрос скроллинга входящих подтверждений банка
//-----------------------------------------------------------------------------
import globals, PaymInter, PTInter, WldInter, oralib, likepy, wlmnstls;

RECORD r_wlresprm    ("wlresprm.dbt");
RECORD r_wlresprm_old("wlresprm.dbt");
var FnsPB_IsFormatTxt : bool;

macro Новый_Документ()
  return 0;
end;

/* Поля панели подтверждения банка - для позиционирования*/
private const FNSPB_PNFLD_ORIGINATORCODEKIND = WLDFNSPB_NUMFLD_ORIGCODEKINDNAME + 1;
private const FNSPB_PNFLD_ORIGINATORCODE     = WLDFNSPB_NUMFLD_ORIGCODE + 1;
private const FNSPB_PNFLD_ORIGINATORNAME     = WLDFNSPB_NUMFLD_ORIGNAME + 1;
private const FNSPB_PNFLD_RECIPIENTCODEKIND  = WLDFNSPB_NUMFLD_RECCODEKINDNAME + 1;
private const FNSPB_PNFLD_RECIPIENTCODE      = WLDFNSPB_NUMFLD_RECCODE + 1;
private const FNSPB_PNFLD_RECIPIENTNAME      = WLDFNSPB_NUMFLD_RECNAME + 1;
private const FNSPB_PNFLD_DATE               = WLDFNSPB_NUMFLD_DATE + 1;
private const FNSPB_PNFLD_TPNAME             = WLDFNSPB_NUMFLD_TRANSPORT + 1;
private const FNSPB_PNFLD_TIME               = WLDFNSPB_NUMFLD_TIME + 1;
private const FNSPB_PNFLD_TPSCHEMNAME        = WLDFNSPB_NUMFLD_TPSCHEM + 1;
private const FNSPB_PNFLD_TRN                = WLDFNSPB_NUMFLD_INITMNESTRN + 1;
private const FNSPB_PNFLD_FORMNAME           = WLDFNSPB_NUMFLD_FORM + 1;
private const FNSPB_PNFLD_BEGMESFORMNAME     = WLDFNSPB_NUMFLD_INITMESFORM + 1;
private const FNSPB_PNFLD_RLSFORMNAME        = WLDFNSPB_NUMFLD_RELEASE + 1;
private const FNSPB_PNFLD_BEGMESDATE         = WLDFNSPB_NUMFLD_INITMESDATE + 1;
private const FNSPB_PNFLD_FILENAME           = WLDFNSPB_NUMFLD_FILENAME + 1;
private const FNSPB_PNFLD_PARTPAYMAMOUNT     = WLDFNSPB_NUMFLD_PARTPAYMAMOUNT + 1;
private const FNSPB_PNFLD_ERRCODE            = WLDFNSPB_NUMFLD_ERRCODE + 1;
private const FNSPB_PNFLD_ERRDESCRIPTION     = WLDFNSPB_NUMFLD_ERRDESCRIPTION + 1;

private const FNSPB_PNFLD_LISTERRCODE        = WLDFNSPB_NUMFLD_LAST + 1;
private const FNSPB_PNFLD_LISTERRDESCR       = WLDFNSPB_NUMFLD_LAST + 2;
private const FNSPB_PNFLD_LISTERRCOUNT       = WLDFNSPB_NUMFLD_LAST + 3;


private macro FindResPrmEQErrCode( ErrCode )
  return existsSQLselect( "select 1 "
                            "from dwlresprm_dbt "
                           "where t_ResultID    != :ResID "
                             "and t_InitialMesID = :MesID "
                             "and t_ErrCode      = :ErrCode ", 
     makeArray( SQLParam( "ResID", r_wlresprm.ResultID ), SQLParam( "MesID", r_wlresprm.InitialMesID ), SQLParam( "ErrCode", ErrCode ) ) );
end;

macro Проверить_Документ( Режим )
 /*
 Возможные значения Режим:
 1  - удаление справки ,
 2  - вставка справки  ,
 3  - обновление справки
*/
  var initmes = TbFile("wlmes.dbt");
  var errrec  = TRecHandler( "wlerror.dbt" );
  var ErrList = NULL;
  /* Проверки при сохранении подтверждения */
  if( ( Режим == 2 ) or ( Режим == 3 ) )

    if( r_wlresprm.InitialMesID != 0 )
      initmes.rec.MesID = r_wlresprm.InitialMesID;
      if( initmes.getEQ() )

        if( FnsPB_IsFormatTxt )
          if( FindResPrmEQErrCode( r_wlresprm.ErrCode ) )
            msgbox( "По сообщению ", initmes.rec.TRN, " от ", initmes.rec.SysDate, "|уже существует подтверждение с тем же результатом обработки.|",
                    "Ввод второго подтверждения не допускается." );
            return FNSPB_PNFLD_TRN;
          end;
        end;

        if( ( r_wlresprm.ErrCode == 0 ) and ( FindResPrmEQErrCode( 8 ) ) )
          msgbox( "Нельзя по одному сообщению отправить два подтверждения|с порядковым номером 1.|",
                  "По сообщению ", initmes.rec.TRN, " от ", initmes.rec.SysDate, " уже cформировано сообщение об отрицательном результате проверки формата." );
          return FNSPB_PNFLD_ERRCODE;
        end;

        if( r_wlresprm.ErrCode == 8 )
          if( FindResPrmEQErrCode( 0 ) )
            msgbox( "Нельзя по одному сообщению отправить два подтверждения|с порядковым номером 1.|",
                    "По сообщению ", initmes.rec.TRN, " от ", initmes.rec.SysDate, " уже cформировано сообщение о положительном результате проверки формата." );
            return FNSPB_PNFLD_ERRCODE;
          end;

          if( FindResPrmEQErrCode( 9 ) )
            msgbox( "Нельзя по одному сообщению отправить подтверждение об отрицательном результате проверки формата (порядковый номер 1) и о невозможности исполнения (порядковый номер 2).|",
                    "По сообщению ", initmes.rec.TRN, " от ", initmes.rec.SysDate, " уже cформировано сообщение о невозможности его исполнения." );
            return FNSPB_PNFLD_ERRCODE;
          end;
        end;

        if( r_wlresprm.ErrCode == 9 )
          if( FindResPrmEQErrCode( 8 ) )
            msgbox( "Нельзя по одному сообщению отправить подтверждение об отрицательном результате проверки формата (порядковый номер 1) и о невозможности исполнения (порядковый номер 2).|",
                    "По сообщению ", initmes.rec.TRN, " от ", initmes.rec.SysDate, " уже cформировано сообщение об отрицательном результате проверки формата." );
            return FNSPB_PNFLD_ERRCODE;
          end;

          if( FindResPrmEQErrCode( 9 ) )
            msgbox( "Нельзя по одному сообщению отправить подтверждение об отрицательном результате проверки формата (порядковый номер 1) и о невозможности исполнения (порядковый номер 2).|",
                    "По сообщению ", initmes.rec.TRN, " от ", initmes.rec.SysDate, " уже cформировано сообщение о невозможности его исполнения." );
            return FNSPB_PNFLD_ERRCODE;
          end;
        end;
      end;
    end;

    if( ( r_wlresprm.ErrCode == 8 ) or ( r_wlresprm.ErrCode == 9 ) )

      ErrList = RsbWlError( r_wlresprm.ResultID );

      if( ErrList.Size() == 0 )
        msgbox( "Введите код и текстовое пояснение, объясняющее причину,|по которой сообщение налогового органа не было принято банком или не может быть исполнено." );
        return FNSPB_PNFLD_LISTERRCODE;
      end;

      if( FnsPB_IsFormatTxt and ( r_wlresprm.ErrCode == 8 ) and ( ErrList.Size() > 1 ) ) 

        if( ( ErrList.First( errrec ) == 0 ) and ( errrec.rec.Code != "15" ) )
          /* предупреждение */
          msgbox( "Регламентом \"Налог\" допускается наличие нескольких ошибок в подтверждении об отрицательном результате проверки формата (порядковый номер 1), только если все они имеют код 15." );
        end;
        while( ErrList.Next( errrec ) == 0 )
          if( errrec.rec.Code != "15" )
            /* предупреждение */
            msgbox( "Регламентом \"Налог\" допускается наличие нескольких ошибок в подтверждении об отрицательном результате проверки формата (порядковый номер 1), только если все они имеют код 15." );
            break;
          end;
        end;

      end;

      if( FnsPB_IsFormatTxt and ( r_wlresprm.ErrCode == 9 ) and ( ErrList.Size() > 1 ) ) 
        /* предупреждение */
        msgbox( "Регламентом \"Налог\" не допускается наличие нескольких ошибок в подтверждении о невозможности исполнения (порядковый номер 2)." );
      end;

    end;/*if( ( r_wlresprm.ErrCode == 8 ) or ( r_wlresprm.ErrCode == 9 ) )*/

    if( InList(r_wlresprm.ErrCode, 10, 11, 12, 13) )

      if (r_wlresprm.PaymentID <= 0 )
        msgbox("Для отправки сообщения о неисполнении (частичном исполнении) необходимо указать связанный платеж");
        return 1;
      end;

      if (r_wlresprm.Number == "")
        //сформировать его с использованием описателя референса "Номера сообщений в ФНС о неисполнении (частичном исполнении)"
        GenerateReference(257, r_wlresprm.Number);
      end;

      ErrList = RsbWlError( r_wlresprm.ResultID );

      if( ErrList.Size() > 1 )
        msgbox( "Регламентом формат сообщения допускает передачу только одной причины неисполнения (частичного исполнения) поручения. Отправлена будет только одна из введенных причин" );
        //Это предупреждение, а не ошибка! //return 1;
      end;
    else
      if (r_wlresprm.FileName == "")
        msgbox("Не задано имя файла исходного сообщения");
        return FNSPB_PNFLD_FILENAME;
      end;
    end;/*if( ( r_wlresprm.ErrCode == 10 ) or ( r_wlresprm.ErrCode == 11 ) )*/
  
  end;/*if( ( Режим == 2 ) or ( Режим == 3 ) )*/

  return 0;
end;

macro ЗаполнитьИмяФайла( )
  if (r_wlresprm.FileName != "")
    return 0;
  end;

  if( r_wlresprm.InitialMesID > 0 )
    var select = "select REGEXP_SUBSTR(sess.t_filename, '([^\\\]*)\\.[^\\.]*$', 1, 1, NULL, 1) t_ShortFileName"+
                  " from dwlmes_dbt mes, dwlsess_dbt sess"+ 
                 " where mes.t_MesID = :MesID"+
                   " and mes.t_sessionid = sess.t_sessionid";

    var params = makeArray( SQLParam("MesID", r_wlresprm.InitialMesID) );

    var rs = execSQLSelect( select, params, true );

    if( rs.MoveNext() )
      r_wlresprm.FileName = rs.value("t_ShortFileName");
      return 0;
    end;
  end;

  record p(party);
  var fn = "";
  var paym = RsbPayment(r_wlresprm.PaymentID);
  if (paym.TaxAuthorState == "04")
    fn = "PBN";
  else
    fn = "PNP";
  end;

  fn = fn + "1";
  fn = fn + substr(ПолучитьКодСубъекта( r_wlresprm.OriginatorID, PTCK_BIC ), 3);

  fn = fn + "_";
  var tmp = substr(paym.Notes.ReadNote(PM_NOTEKIND_PAYM_TAXORDER, {curdate}), 1, 4);

  if (StrIsNumber(tmp))
    fn = fn + tmp;
  else
    ПолучитьСубъекта(r_wlresprm.OriginatorID, p);
    fn = fn + ПолучитьКодСубъекта(p.TaxInstitution, PTCK_MNS);
  end;
  fn = fn + YYYYMMDD(paym.GetPM_PAYM().rec.CreationDate);
  var Ref: string = "";

  fn = fn + "_";
  GenerateReference(WLD_REFERENCE_ANSWER_FOR_FNS_DAY, Ref);
  fn = fn + Ref;
  r_wlresprm.FileName = fn;
  return 0;
end;

macro Функция_Пользователя( Режим )
 /*
 Возможные значения Режим:
  UFN_PANEL_INPUT(1) - функция вызвана из панели ввода объекта;
  UFN_PANEL_EDIT(2) - функция вызвана из панели корректировки объекта;
  UFN_SCROL(3) - функция вызвана из панели скролинга, единичный вызов, любая корректировка объекта запрещена;
  UFN_SCROL_FMASS(4) - функция вызвана из панели скролинга, работа по нескольким записям, вызов до начала обработки, любая корректировка объекта запрещена;
 
// Пример работы: 
 if( Режим == UFN_SCROL )
   return UPDTPAGE;    // Обновить страницу записей и область скролинга
   //return UPDTREC;     // Обновить текущую запись, загрузив из файла
 end;
 */
  return 0;
end;
