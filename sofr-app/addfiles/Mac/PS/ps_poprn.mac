/*
  $Name:         ps_poprn.mac
  $Module:       РКО
  $Description:  П
*/
/* ps_poprn.mac                                   Дудин В.А.             */
/* ============= Печать платежного поручения ==============================


 =========================================================================*/
 import "prn_dem.mac", "prn_req.mac", "adress.mac", "prpm.mac", "psprncom.mac";

/* ---------------------------- Вывод отчета ---------------------------- */
macro DrawReport_Order( date_inbp, number, date_,dptch,payerINN,
                payer, deb_acc, deb_corracc, bank_payer,  payerMFO,
                sum,receiverINN,
                receiver, kred_acc, kred_corracc,bank_receiver, receiverMFO,
                ground, queue, paydate, kindoper )

  var kopeck,rub,
      divide_kopeck,
      sumstr,shortsum,
      pospoint,copy,Плательщик,Получатель;


  Плательщик = payerINN + " " + payer;
  Получатель = receiverINN + " " + receiver;
  divide_kopeck = РазделительКопеек;
  copy = КоличествоКопий;

  if(MassCopy == 0)
    if(copy == 0)
      copy = 1;
      if ( not GetInt(copy,"Введите количество копий.",3))
        copy = 0;
      end;
    end;
  else copy = MassCopy;
  end;

  if(copy == 0) return false;end;

  ARRAY SS,SG,SBP,SBR,SP,SR;

  sumstr = string(MoneyL(sum));
  rub = RubToStrAlt(sum);
  pospoint = Index(sumstr, ".");
  if(Int(SubStr(sumstr, pospoint + 1, 2)) == 0 )
    rub = SubStr(rub,1,Index(rub,"коп")-4);
  end;

  ground = InterpretGround( ground );

  strsplit( Плательщик,         SP,    50,46,5 ); /* Плательщик      */
  strsplit( bank_payer,         SBP,   50,50,2 ); /* Банк-плательщик */
  strsplit( rub,                SS,    76,76,3 ); /* Сумма пpописью  */
  strsplit( ground,             SG,    85,85,3 ); /* Основание       */
  strsplit( bank_receiver,      SBR,   50,50,2 ); /* Банк-получатель */
  strsplit( Получатель,         SR,    50,50,5 ); /* Получатель      */

  date_ = string( date_ );
  paydate = string( paydate );
while(copy != 0)
[                                                                                         +---------+
                                                                                          | 0401060 |
                                                                                          +---------+
        ############
   ----------------------
   Поступило в банк плат.
                                                ############     #############
   Платежное поручение N ###############        ------------     -------------
                                                   (Дата)        (Вид платежа)

   Сумма   | ####################################################################################
   прописью| ####################################################################################
           | ####################################################################################
  ---------+------------------------------------------+-----------+--------------------------------
   ИНН ############################################## | Сумма     | #############################
   ################################################## |           |
   ################################################## |           |
   ################################################## +-----------+
   ################################################## | Сч.N.     | #############################
                                                      |           |
   Плательщик                                         |           |
  ----------------------------------------------------+-----------+--------------------------------
   ################################################## | БИК       | #############################
   ################################################## +-----------+
                                                      | Сч.N.     | #############################
   Банк плательщика                                   |           |
  ----------------------------------------------------+-----------+--------------------------------
   ################################################## | БИК       | #############################
   ################################################## +-----------+
                                                      | Сч.N.     | #############################
   Банк получателя                                    |           |
  ----------------------------------------------------+-----------+
   ИНН ############################################## | Сч.N.     | #############################
   ################################################## |           |
   ################################################## +-----------+------+------------+------------
   ################################################## |  Вид оп.  | #### | Срок плат. | ##########
   ################################################## +-----------+      +------------+
                                                      | Наз. пл.  |      | Очер плат. | ##########
                                                      +-----------+      +------------+
   Получатель                                         | Код       |      | Рез. поле  |
  ----------------------------------------------------+-----------+------+------------+------------
   Назначение платежа:
   ###############################################################################################
   ###############################################################################################
   ###############################################################################################

  -------------------------------------------------------------------------------------------------
                                            Подписи                        Отметки банка

        М.П.
                                            -------------------------------

                                            -------------------------------



]
 (date_inbp:c,date_:c,dptch:c,number:l,
  SS(0):l,SS(1):l,SS(2),
  SP(0):l,sum:l:f,SP(1):l,SP(2):l,SP(3),SP(4),
  deb_acc:l,
  SBP(0):l,payerMFO:l,SBP(1):l,deb_corracc:l,
  SBR(0):l,receiverMFO:l,SBR(1):l,kred_corracc:l,SR(0),
  kred_acc:l,SR(1),SR(2),SR(3),
  kindoper:l, paydate:l,SR(4), queue:l,
  SG(0):l,SG(1):l,SG(2):l
 );
 copy = copy - 1;
 end;

 return true;

end;





/* ================= Печать платежного поручения ============================= */
 macro PSPO_print_order ( aa, bb, cc, dd )

 record pt  (party);
 record RecordAdress ( adress );
 file pknd(pspopknd) key 0;

 VAR
   dptch,
   payer, deb_acc, deb_corracc, bank_payer,  payerMFO,
   sum,
   receiver, kred_acc, kred_corracc,bank_receiver, receiverMFO, Ground,
   MFO,CorAc,
   rub, divide_kopeck,sumstr,
   stat, pospoint,
   Use_VRSL, err, VRSL, report;

   setbuff( payord,   aa );
   setbuff( pm_paym,  bb );
   setbuff( pm_prop,  cc );
   setbuff( pm_rmprop,dd );

   GetRegistryValue( ПутьНастройкиИспользованияVRSLПоручении, V_INTEGER, Use_VRSL, err );
   if ( err != REG_OK )
      MsgBox( "Не определен ключ " ,ПутьНастройкиИспользованияVRSLПоручении, " в настройках банка" );
      return 1;
   end;

   /*Реквизиты плательщика*/
   deb_acc     = pm_paym.PayerAccount;
   bank_payer  = {Name_Bank} + ", " + {Post_Addr};

   if ({MFO_Bank} != "")
         payerMFO    = {MFO_Bank};
         deb_corracc = {CORAC_Bank};
   else
         GetPartyProperty({OurBank},pt,MFO,CorAc);
         payerMFO    = MFO;
         deb_corracc = Corac;
   end;

   /*Реквизиты получателя*/
   kred_acc = pm_paym.ReceiverAccount;
   if( (pm_prop.PaymentID <= 0) or (pm_prop.Group != PAYMENTS_GROUP_EXTERNAL) ) /* Внутренний документ банка */
     bank_receiver = pm_rmprop.ReceiverBankName;
     if(bank_receiver == "") bank_receiver = {Name_Bank}; end;
     bank_receiver = bank_receiver + ", " + {Post_Addr};
     receiverMFO   = {MFO_Bank};
     kred_corracc  = {CORAC_Bank};
   else
     if ( ПолучитьСубъекта (pm_paym.ReceiverBankID,pt) != 0 )
       msgbox("Не найден банк-получатель");
       return 1;
     end;
     GetPartyProperty(pm_paym.ReceiverBankID,pt,MFO,CorAc);
     ClearRecord(RecordAdress);
     НайтиЮридическийАдресСубъекта(pt.PartyID,RecordAdress);
     bank_receiver = pm_rmprop.ReceiverBankName + ", " + RecordAdress.Adress;
     receiverMFO   = MFO;
     kred_corracc  = CorAc;
  /*    receiverMFO   = pm_prop.BankCode;
     kred_corracc  = pm_rmprop.ReceiverCorrAccNostro;*/
   end;

   pknd.PaymentKind = pm_rmprop.PaymentKind;
   if (GetEQ(pknd))
     dptch = pknd.Name;
   else
     msgbox("Не найден вид платежа");
   end;
     dptch = pknd.Name;

   if( not Use_VRSL)
      stat = DrawReport_Order( pm_rmprop.Date, pm_rmprop.Number, pm_rmprop.Date, dptch,
                pm_rmprop.PayerINN,
                pm_rmprop.PayerName, deb_acc, deb_corracc,
                bank_payer, payerMFO,
                pm_paym.Amount,
                pm_rmprop.ReceiverINN,
                pm_rmprop.ReceiverName, kred_acc, kred_corracc,
                bank_receiver, receiverMFO,
                pm_rmprop.Ground, pm_rmprop.Priority, pm_rmprop.PayDate, pm_rmprop.ShifrOper);

      if(not stat) return 1;
      else return 0;
      end;
   else

      VRSL = ActiveX("RSGENOLE.VRslRun.1");
      report = VRSL.RunRpt("ПлатежноеПоручение");

      divide_kopeck = РазделительКопеек;

      sumstr = string(MoneyL(pm_paym.Amount));
      rub = RubToStrAlt(pm_paym.Amount);
      pospoint = Index(sumstr, ".");
      if(Int(SubStr(sumstr, pospoint + 1, 2)) == 0 )
        rub = SubStr(rub,1,Index(rub,"коп")-4);
      end;


      report.AccC.text             =  kred_acc;
      report.AccD.text             =  deb_acc;
      report.Amm.text              =  string(pm_paym.Amount:f);
      report.Ammstr.text           =  rub;
      report.BICC.text             =  receiverMFO;
      report.BICD.text             =  payerMFO;
      report.Get_Date.text         =  string(pm_rmprop.Date);
      report.Down.text             =  string(pm_rmprop.Date);
      report.ground.text           =  InterpretGround( pm_rmprop.Ground) ;
      report.INNC.text             =  pm_rmprop.ReceiverINN;
      report.INND.text             =  pm_rmprop.PayerINN;
      report.KindOper.text         =  pm_rmprop.ShifrOper;
      report.Nown.text             =  pm_rmprop.Number;
      report.payDate.text          =  string(pm_rmprop.PayDate);
      report.payerBankName.text    =  bank_payer;
      report.payerCorAcc.text      =  deb_corracc;
      report.payerName.text        =  pm_rmprop.PayerName;
      report.payKind.text          =  dptch;
      report.payQueue.text         =  string(pm_rmprop.Priority);
      report.receiverBankName.text =  bank_receiver;
      report.receiverCorAcc.text   =  kred_corracc;
      report.receiverName.text     =  pm_rmprop.ReceiverName;

      report.runReport ();
      if (IsStandAlone)
       ViewRep (report);
      else
       TransferRep (report);
      end;
   end;


 END;

/* Массовая печать платежных документов из списка */
/* Возвращает false при отказе от печати,  */
/* или цифру - число печатаемых копий */
MACRO PSPO_MassPrint( Num, /* число печатаемых копий, если 0, считается первым вызовом */
                      aa, bb, cc, dd, ee )

  var err;
  setbuff( payord, aa );

  if(Num) MassCopy = Num;
  else    MassCopy = КоличествоКопий;
  end;

  if(MassCopy == 0)
    MassCopy = 1;
    if ( not GetInt(MassCopy,"Введите количество копий.",3))
      MassCopy = 0;
    end;
  end;

  if(MassCopy == 0) return false;end;

  if  (payord.DocKind == PSPOKIND_ORDER)   err = PSPO_print_order  (aa, bb, cc, dd);
  elif(payord.DocKind == PSPOKIND_DEMAND)  err = PSPO_print_demand (aa, bb, cc, dd, ee);
  elif(payord.DocKind == PSPOKIND_REQUEST) err = PSPO_print_request(aa, bb, cc, dd, ee);
  end;

  if(err) return true; end;

  return MassCopy;

END;
