/*
$Name:             pr_svk.mac
$Module:           РКО юридических лиц 
$Description:      Сводная карточка по картотеке №2
*/

import PSInter, globals, FIInter, OprInter, pm_common, "oralib.mac";

class SVKReport( MapVal : PSRepMap )
  // Входные данные
  var DateIn  = MapVal.Value( "DateIn" );
  var DateOut = MapVal.Value( "DateOut" );
  var DepNum  = MapVal.Value( "DepNum" );
  var NodeNum = MapVal.Value( "NodeNum" );
  var FIID    = MapVal.Value( "FIID" );
  var OperNum = MapVal.Value( "Oper" );

  // Параметры выборки
  const Chapter : Integer  = 3;
  const Balance : String   = "90902";

  macro PrintHeader()
    var DepCode= "", DepName = "";
    CB_GetDepartmentCodeAndName( DepNum, DepCode, NULL, DepName );

    [
                                     Сводная карточка по картотеке №2
                                   за период с ########## по ##########

       Филиал         ##########       ############################################################]
      ( DateIn, DateOut, DepCode, DepName );
  end;

  macro PrintNodeHeader()
    if ( NOT (NodeNum > 0) )
      return;
    end;
    var NodeCode= "", NodeName = "";
    CB_GetDepartmentCodeAndName( NodeNum, NodeCode, NULL, NodeName );
    if( NodeName != "" )
    [  ВСП            ##########       ############################################################]
    ( NodeCode, NodeName );
    end;
  end;

  macro PrintCurrHeader()
    var FICode = "", FIName = "";
    FILE fininstr ( "fininstr.dbt" );
    fininstr.FIID = FIID;
    if( GetEQ( fininstr ) )
       FICode = fininstr.FI_Code;
       FIName = fininstr.Name;
     end;

    if( FIName != "" )
    [  Валюта счетов   ############### #########################]
    ( FICode, FIName );
    end;
  end;

  macro PrintOper()
    if( NOT (OperNum > 0) )
      return;
    end;

    var NameOper : string = GetNameOper(OperNum);

    if( NameOper != "" )
      [  
         Операционист   #####   # ]
      ( OperNum, NameOper );
    end;
  end;

  macro PrintTable(Debet : Money, Credit : Money, RestBegin : Money, RestEnd : Money)
    [
    ┌───────────────────┬───────────────────┬───────────────────┬───────────────────┐
    │  Остаток на       │     Приход        │     Расход        │  Остаток на       │
    │начало периода     │                   │                   │на конец периода   │
    │                   │                   │                   │                   │
    ├───────────────────┼───────────────────┼───────────────────┼───────────────────┤
    │                   │                   │                   │                   │
    │###################│###################│###################│###################│
    │                   │                   │                   │                   │
    └───────────────────┴───────────────────┴───────────────────┴───────────────────┘
    ](RestBegin, Debet, Credit, RestEnd);
    
  end;

  macro PrintExecutor()

    [
      Ответственный исполнитель: #####  #
    ]({Oper}, {Name_Oper})
  end;

  // Получение списка параметров для daccount_dbt
  macro GetAccountParams()
    var params = TArray();
    params[params.size] = SQLParam( "DepNum", DepNum );
    params[params.size] = SQLParam( "FIID", FIID );
    params[params.size] = SQLParam( "Chapter", Chapter );
    params[params.size] = SQLParam( "Balance", Balance );
    if ( NodeNum > 0 )
      params[params.size] = SQLParam( "NodeNum", NodeNum );
    end;
    if ( OperNum > 0 )
      params[params.size] = SQLParam( "OperNum", OperNum );
    end;
    
    return params;
  end;


  // Сумма зачисления
  macro GetDebetSum( selectAccount : String )

    var Sum : Money = 0;
    var selectDebet : String = " SELECT NVL (sum(acctrn.t_Sum_Payer), 0.0)" + 
                               " FROM dacctrn_dbt acctrn," + " (" + selectAccount + " ) account"    
                               ;
    var whereCond : String =   " WHERE acctrn.t_State = 1" + 
                               " AND   acctrn.t_Result_carry = 50" +
                               " AND   acctrn.t_Date_Carry between :DateIn AND :DateOut" +
                               " AND   acctrn.t_Account_Payer = account.t_Account"
                               ;
    var params = GetAccountParams();
     
    params[params.size] = SQLParam( "DateIn", DateIn );
    params[params.size] = SQLParam( "DateOut", DateOut );
    selectDebet = selectDebet + whereCond;
    var rs : RsdRecordSet = execSQLselect( selectDebet, params, FALSE );
    
    if (rs.MoveNext())
      Sum = rs.Value(0);
    end;
    
    return Sum;
    
  end;

  // Сумма списания
  macro GetCreditSum( selectAccount : String )
    
    var Sum : Money = 0;
    var selectCredit : String = " SELECT NVL(sum(acctrn.t_Sum_Receiver), 0.0)" + 
                                " FROM dacctrn_dbt acctrn," + " (" + selectAccount + " ) account"    
                                ;
    var whereCond : String =    " WHERE acctrn.t_State = 1" + 
                                " AND   acctrn.t_Result_carry in (51, 52, 53, 74)" +
                                " AND   acctrn.t_Date_Carry between :DateIn AND :DateOut" +
                                " AND   acctrn.t_Account_Receiver = account.t_Account"
                                ;
    var params = GetAccountParams();
    
    params[params.size] = SQLParam( "DateIn", DateIn );
    params[params.size] = SQLParam( "DateOut", DateOut );
    selectCredit = selectCredit + whereCond;
    var rs : RsdRecordSet = execSQLselect( selectCredit, params, FALSE );
    
    if( rs.MoveNext())
      Sum = rs.Value(0);
    end;
    
    return Sum;
    
  end;
  
end;

macro PrintSVKReport( MapVal : PSRepMap )
  
  var stat = 0;
  
  var report = SVKReport( MapVal );
  
  var Debet     : Money  = 0.0;
  var Credit    : Money  = 0.0;
  var RestBegin : Money  = 0.0;
  var RestEnd   : Money  = 0.0;
  var Check     : Integer = 0;
  var paramsAccount = TArray();
  
        
  var selectAccount : String = " SELECT account.t_Account" +
                               " FROM daccount_dbt account "                   
                        ;
  var whereCond : String = " WHERE account.t_department = :DepNum" +
                           " AND   account.t_Code_Currency = :FIID" +
                           " AND   account.t_Chapter = :Chapter" +
                           " AND   account.t_Balance = :Balance" +
                           // account имеет связь по виду "Внебалансовый Счет картотеки 2"
                           " AND EXISTS (select 1 " +
                           "               from dobjlink_dbt ol " +
                           "              where ol.t_groupid = 2 " /*OBJROLE_ACC_I2OBACC*/ +
                           "                and ol.t_objecttype = 4 " /*OBJTYPE_ACCOUNT*/ +
                           "                and ol.t_attrtype = 4 " /*OBJTYPE_ACCOUNT*/ +
                           "                and ol.t_attrid = LPAD(account.t_Chapter, 2, '0') || " +
                           "                                  LPAD(account.t_Code_Currency, 7, '0') || " +
                           "                                  account.t_account ) "
                           ;
                           
  if ( report.NodeNum > 0 )
    whereCond = whereCond + " AND account.t_Branch = :NodeNum";
  else
    var privFROM : string = ""; //дополнительная строка для проверки привилегии 0008
    var privWHERE : string = ""; //дополнительное условие для проверки привилегии 0008
    if( GetObjectRestriction(privWHERE, 8, {Oper}, "dt", 0,"dp_dep.dbt", privFROM) )
      if(privFROM != "")
        selectAccount = selectAccount + ", " + privFROM + " ";
      else
        selectAccount = selectAccount + ", ddp_dep_dbt dt ";
        whereCond = whereCond + " AND dt.t_Code = account.t_Branch ";
      end;
      if(privWHERE != "")
        whereCond = whereCond + " AND " + privWHERE;
      end;
    end;
  end;
  if ( report.OperNum > 0 )
    whereCond = whereCond + " AND account.t_Oper = :OperNum";
  end;
  
  selectAccount = selectAccount + whereCond;
  
    
  paramsAccount = report.GetAccountParams();
  
  var rsAccount : RsdRecordSet = execSQLselect( selectAccount, paramsAccount, FALSE );
  // Получение входящего и исходящего остатков  
  while( rsAccount.MoveNext() )
    Check = Check + 1;
    RestBegin = RestBegin + ПолучитьОстатокНаДату( rsAccount.Value(0), report.Chapter, report.FIID, report.DateIn - 1 );
    RestEnd   = RestEnd   + ПолучитьОстатокНаДату( rsAccount.Value(0), report.Chapter, report.FIID, report.DateOut    );
  end;
  // Если счета не найдены, выводить сообщение
  if(Check == 0)
    msgbox( "Нет счетов, удовлетворяющих заданным условиям" );
    return 1;
  end;
  
  Debet = report.GetDebetSum( selectAccount );
  Credit = report.GetCreditSum( selectAccount );
  
  // Печать отчета
  report.PrintHeader();
  report.PrintNodeHeader();
  report.PrintCurrHeader();
  report.PrintOper();
  report.PrintTable(Debet, Credit, RestBegin, RestEnd);
  report.PrintExecutor();
  
    
end;