//------------------------------------------------------------------------------
// Перевод ИПВС на категории учета
//------------------------------------------------------------------------------
import BankInter, PaymInter, PSInter, CTInter, globals, "payconst.mac", likepy;

//------------------------------------------------------------------------------
// Первичный документ для категорий
//------------------------------------------------------------------------------
CLASS (RsbPayment) IR_FirstDoc( IDpayment )

//------------------------------------------------------------------------------
// данные класса
//------------------------------------------------------------------------------
  // Допустимые базис ролей
  PRIVATE VAR FIRoleB:TArray = makeArray( FIROLE_FIREQ //ФИ требования по сделке
                                        );

  VAR Error = 0, kind, id;

//------------------------------------------------------------------------------
// вернуть параметр второго рода
//------------------------------------------------------------------------------
  MACRO GetParametr( ParmKind, OperDate, CatCode, FIRole )
     VAR Parametr = -1;

       if( ParmKind == MC_TYPE_PARAMETR_DOCKIND       )
        return DocKind;
     elif( ParmKind == MC_TYPE_PARAMETR_DOCID         )
        return PaymentID;
     elif( ParmKind == MC_TYPE_PARAMETR_DEPARTMENT    )
       return Department;
     elif( ParmKind == MC_TYPE_PARAMETR_OWNER         ) 
        return Payer;
     elif( ParmKind == MC_TYPE_PARAMETR_CONTRACTOR    )
        return /*Payer*/{OurBank};
     elif( (ParmKind == MC_TYPE_PARAMETR_FIID) OR
           (ParmKind == MC_TYPE_PARAMETR_PAYCURRENCY) )
         return ReceiverFIID;
     end;

     return Parametr;
  END;

//------------------------------------------------------------------------------
// параметры шаблона
//------------------------------------------------------------------------------
    MACRO GetParametrTemplate( ObjectID, Classificator, OperDate, FIRole )         
       VAR Parametr = -1;
       return Parametr;
    END;

//------------------------------------------------------------------------------
// основная роль фИ
//------------------------------------------------------------------------------
    MACRO GetBasisFIRole( FIRole )
       return FIRole;
    END;

//------------------------------------------------------------------------------
// вернуть весь базис ролей
//------------------------------------------------------------------------------
    MACRO GetFIRoleBArray()
       return FIRoleB;
    END;

//------------------------------------------------------------------------------
// найти/открыть счет
//------------------------------------------------------------------------------
   MACRO FindAndOpenAccount( Category, isMass, Open_Date, FIRole )
      var opendate:Date = {curDate};
      
      if ( ValType(Open_Date) == V_DATE )
         opendate = Open_Date;
      end;

      return MC_FindAndOpenAccount( Category, this, opendate, isMass, MC_OPENACC_CREATE, null, null, null, null, null, FIRole );
   END; 

//------------------------------------------------------------------------------
// Корректировка счета
//------------------------------------------------------------------------------
   MACRO CorrectAccount( account, accblnc, ORScheme, categ, templ, accdoc, OperDate )
      return true;
   END;

//------------------------------------------------------------------------------
// конструктор
//------------------------------------------------------------------------------
    InitRsbPayment( IDpayment );
    Kind = DocKind;
    Id   = IDpayment;
END;
//------------------------------------------------------------------------------
