/*
$Name:             rqchms.mac
$Module:           РКО юридических лиц
$Description:      Операция - 3036 - "Изменение номера счета"  
                   Шаг      - 90   - "Формирование сообщения в МНС" 
*/
import prpm,reqinter, InsCarryDoc, PSInter, wlglobal;

RECORD req( reqchnga );

var ReqChangeAccObj:RsbReqChangeAcc;

/* Выполнение шага */
MACRO ExecuteStep( doc, reqacc )
  var TB_Req = TBFile("reqchnga.dbt",  "R", 0, "reqchnga.dbt",  "bank.def");

  var stat:integer = 0;

  TB_Req.rec.RequestID = ReqChangeAccObj.RequestID;

  if( TB_Req.GetEQ() )
    Copy( req, TB_Req );
  end;

  stat = InsertChangeAccountMessage( req );

  if( stat == ОтказОтГенерацииСообщения )
    return 1;
  end;

  if( stat != 0 )
    DisplayError();
    MsgBox( "Ошибка при создании сообщения об изменении счета " + req.OldAccount );
    return  1;
  end;

  return stat;
END;

//Заполняет буфер первым попавшимся сообщением документа
private macro FillWlmes( ObjID, ObjKind, mes:TRecHandler)
  var rs:object;
  var select:string;
  var params:TArray;
  var TB_Wlmes = TBFile("wlmes.dbt",  "R", 0, "wlmes.dbt",  "bank.def");

  select = "select lnk.t_MesID from dwlmeslnk_dbt lnk where "+
                          "lnk.t_ObjID =:ObjID and " +
                          "lnk.t_ObjKind =:ObjKind ";
  params = makeArray( SQLParam("ObjID", ObjID),
                      SQLParam("ObjKind", ObjKind) );

  rs = execSQLselect( select, params, FALSE );

  if( rs.MoveNext() ) /* Найдена связь уч.объекта с сообщением */
    TB_Wlmes.Clear();
    TB_Wlmes.rec.MesID = rs.value(0);
    if( TB_Wlmes.GetEQ() )
      copy(mes, TB_Wlmes);
      return true;
    else       
      MsgBox("Не найдена запись в файле сообщений");
    end;
  end;

  return false;
end;

macro PostStep( message,     /* данный параметр может принимать следующие       */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                FirstDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                ID_Step,     /* внутренний идентификатор шага операции          */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep )   /* вид шага операции                               */

    var stat = 0;

    Array Text, Buttons;
    Text(0) = "Печатать сообщения?";
    Buttons( 0 ) = "Да";
    Buttons( 1 ) = "Нет";
    var CopyCount = 0;

    if( ( errTrn == 0 ) and ( message == 1 ) )// на выполнении шага
      if( not IsOprMultiExec and 
          (ConfWin( Text, Buttons, 1 ) == 0)
        )
        SetKindRegOrgan( REG_ORGAN_KIND_FNS );
        stat = ReqChangePrintMessage(ReqChangeAccObj.RequestID,
                                    ReqChangeAccObj.NewAccountFIID,
                                    ReqChangeAccObj.NewAccount);
        if( stat == 0 )
          SetKindRegOrgan( REG_ORGAN_KIND_PFR );
          stat = ReqChangePrintMessage(ReqChangeAccObj.RequestID,
                                      ReqChangeAccObj.NewAccountFIID,
                                      ReqChangeAccObj.NewAccount);
          SetKindRegOrgan( REG_ORGAN_KIND_FSS );
          stat = ReqChangePrintMessage(ReqChangeAccObj.RequestID,
                                      ReqChangeAccObj.NewAccountFIID,
                                      ReqChangeAccObj.NewAccount);
        end;
        SetKindRegOrgan( REG_ORGAN_KIND_NON );
      end;
    end;
    return stat;
end;
                