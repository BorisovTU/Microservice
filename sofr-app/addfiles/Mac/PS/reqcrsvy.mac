/* -------------------------------------------------------------------- */
/*         Автоматизированная банковская система RS-Bank v6.0           */
/*                 Copyright (c) R-Style Software Lab                   */
/*                                                                      */
/*  Имя файла  : reqcrsvy.mac                                           */
/*                                                                      */
/*  Описание   : Операция - 3030 - "Открытие лицевого счета"            */
/*               Шаг      - 60   - "Создание договора обслудивания для  */
/*                                  транзитного счета"                  */
/*                                                                      */
/*  Программист: Андреева Е.П.                                          */
/*                                                                      */
/*  Создан     : 15.11.2004                                             */
/*                                                                      */
/* -------------------------------------------------------------------- */
IMPORT InsCarryDoc, reqinter, SFInter, PSInter;

private file SfCntrOld( "sfcontr.dbt" );

var ReqOpenAccObj:RsbReqOpenAcc;

/* Существует ли договор обслуживания для данного счета */
macro ExistAndFillSfForAccount( FIID, Account )

  var OldKey, result = false;

  OldKey = KeyNum( SfCntrOld, 1 );

  SfCntrOld.ServKind    = PTSK_PAY;
  SfCntrOld.objectType  = SF_ACCOUNT; 
  SfCntrOld.FIID        = FIID;       
  SfCntrOld.object      = Account; 

  if( getEQ( SfCntrOld ) )
    result = true;
  end;

  keyNum( SfCntrOld, OldKey );

  return result;
end;

/* Выполнение шага */
MACRO ExecuteStep( doc, reqacc )
  var   existSfOld  = false;
  var   existSfMain = false;
  var   OldSfID     = 0;
  var   sfssidl     = TArray;

  record SfCntr  ( "sfcontr.dbt", "bank.def" );

  var reqlinka:TRecHandler = TRecHandler( "reqlinka.dbt", "bank.def");
  reqlinka = GetReqLinkAcc( ReqOpenAccObj.RequestID, 230 );

  if( reqlinka.rec.RequestID != 0 )

    ClearRecord( SfCntr );

    existSfOld = ExistSfForAccount( reqlinka.rec.Code_Currency, reqlinka.rec.Account );

    if( not existSfOld )

      /* 1. Найти договор обслуживания для основного счета */
      existSfMain = ExistAndFillSfForAccount( ReqOpenAccObj.Code_Currency, ReqOpenAccObj.Account );

      if( existSfMain )

        /* 2. Если нашли - скопировать для транзитного */
        Copy( SfCntr, SfCntrOld );
        /* Заполняем только отличные от ДО по основному счету поля */
        SfCntr.ID     = 0;               /* Идентификатор     */
        SfCntr.FIID    = reqlinka.rec.Code_Currency;             /* Валюта объекта начисления */
        SfCntr.object = reqlinka.rec.Account; /* Объект начисления */
        SfCntr.AccCode = "";
        OldSfID       = SfCntrOld.ID;
        /* Филиал и ВСП заполняем по транзитному счету */
        FillDepartmentAndBranch( reqlinka.rec.Account, reqlinka.rec.Code_Currency, SfCntr.Department, SfCntr.Branch );
        /* 3. Заполнить СПИ */
        sfssidl[0] = TRecHandler("sfssidl.str", "bank.def" );
        sfssidl[0].rec.BankID    = {OurBank};
        sfssidl[0].rec.Chapter   = 1;
        sfssidl[0].rec.FIID      = ReqOpenAccObj.Code_Currency; /* валюта плательщика */
        sfssidl[0].rec.Account   = ReqOpenAccObj.Account;       /* счет плательщика   */
        sfssidl[0].rec.FeeType   = 0;
        sfssidl[0].rec.FeeNumber = 0;

        if( ReqOpenAccObj.SfPlanID != 0 )
          ReqOpenAccObj.SfComGroup = 0;
        end;
              
        if( not InsertSfContract( SfCntr, ReqOpenAccObj.SfComGroup, sfssidl, OldSfID, ReqOpenAccObj.SfPlanID, NULL, 0 ) )
          MsgBox( "Ошибка при открытии договора обслуживания " + ReqOpenAccObj.SfContrNum );
          return  1;
        end;
      else
        MsgBox( "Не найден договор обслуживания для основного счета" );
        return  1;
      end;
    end;

  else
    MsgBox( "Не найден транзитный счет" );
    return  1;
  end;

  return 0;

END;
