/*
  $Name: psfnsfileallocation.mac
  $Module: PS
  $Description: Распределение файлов по необходимым каталогам для транспорта ФНС
*/

import likepy, rsexts, xmlmestools, wlfnsimx;


// Процедура распределения файлов по указанным каталогам.
// Каталоги не создаются! Необходимо указывать уже имеющиеся каталоги. Если каталога нет, то файл не будет перенесен
// Каталоги, в которые копируются файлы могут дублироваться (т.е., например, для ИП и ЮЛ могут быть указаны одинаковые каталоги).
// Параметры: InitialDirPath - каталог из которого копируются файлы,
//            IndividualDirPath - каталог для файлов с данными по физическим лицам,
//            IndividualEntrepreneurDirPath - каталог для файлов с данными по индивидуальным предпринимателям,
//            EntityDirPath - каталог для файлов с данными по юридическим лицам.
macro FilesToDirsAllocation( InitialDirPath : string, IndividualDirPath : string, IndividualEntrepreneurDirPath : string, EntityDirPath : string )
  var RegOrgKind : integer = 0, FormName : string = "";
  InitialDirPath = InitialDirPath + IfThenElse( SubStr( InitialDirPath, strlen( InitialDirPath ) ) == "\\", "", "\\" );
  IndividualDirPath = IndividualDirPath + IfThenElse( SubStr( IndividualDirPath, strlen( IndividualDirPath ) ) == "\\", "", "\\" );
  IndividualEntrepreneurDirPath = IndividualEntrepreneurDirPath + IfThenElse( SubStr( IndividualEntrepreneurDirPath, strlen( IndividualEntrepreneurDirPath ) ) == "\\", "", "\\" );
  EntityDirPath = EntityDirPath + IfThenElse( SubStr( EntityDirPath, strlen( EntityDirPath ) ) == "\\", "", "\\" );
  var list = TDirList( InitialDirPath + "*.xml", "f" );
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var IndividualNodeObj = null;
  var IndividualEntrepreneurNodeObj = null;
  var EntityNodeObj = null;
  var ImportFileName = "";
  var itr = 0;
  while( itr < list.Count )
    ImportFileName = "";
    IndividualNodeObj = null;
    IndividualEntrepreneurNodeObj = null;
    EntityNodeObj = null;
    ImportFileName = InitialDirPath + list.name(itr);
    if( xml.load( ImportFileName ) )
      FormName = ОпределитьИмяФормыФНС( ImportFileName, @RegOrgKind );      
      IndividualNodeObj = GetChildNodeConsideringHierarchy( xml.DocumentElement, "ПФЛ" );
      IndividualEntrepreneurNodeObj = GetChildNodeConsideringHierarchy( xml.DocumentElement, "ПлИП" );
      EntityNodeObj = GetChildNodeConsideringHierarchy( xml.DocumentElement, "ПлЮЛ" );

      var selTpParams = SelectTpParameters( FormName, IndividualNodeObj, IndividualEntrepreneurNodeObj, EntityNodeObj );

      if( selTpParams.Individual )
        if( CopyFile( ImportFileName, IndividualDirPath + list.name(itr) ) )
          RemoveFile( ImportFileName );
          //println( list.name(itr) + " transfered from " + InitialDirPath + " to " + IndividualDirPath );
        end;
      elif( selTpParams.IndividualEntrepreneur )
        if (CopyFile( ImportFileName, IndividualEntrepreneurDirPath + list.name(itr) ) );
          RemoveFile( ImportFileName );
          //println( list.name(itr) + " transfered from " + InitialDirPath + " to " + IndividualEntrepreneurDirPath );
        end;
      elif( selTpParams.Entity )
        if( CopyFile( ImportFileName, EntityDirPath + list.name(itr) ) );
          RemoveFile( ImportFileName );
          //println( list.name(itr) + " transfered from " + InitialDirPath + " to " + EntityDirPath );
        end;
      end;
    end;    
    itr = itr + 1;
  end;

end;

/*
//Пример вызова макропроцедуры 
FilesToDirsAllocation( "E:\\BANK\\Source3146_17\\import\\fnsxml\\", "E:\\BANK\\Source3146_17\\import\\fnsxml\\FL", 
                      "E:\\BANK\\Source3146_17\\import\\fnsxml\\IP", "E:\\BANK\\Source3146_17\\import\\fnsxml\\YL" );

*/