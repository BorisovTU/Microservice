/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.10                                         R-Style Software Lab

  File Name   : psposcop.mac
  Created     : 11.03.01
  Description : Печать отчета сканирования открытых платежных документов

└───────────────────────────────────────────────────────────────────────────*/
IMPORT  PaymInter, OprInter, BankInter;
IMPORT  "gstname.mac";
RECORD pspo_firstdoc (pspayord);

private const OBJTYPE_PAYMENT = 501;

const   Ошибка   = 0,
        Первый   = 1,
        Последний= 2;

MACRO Печать_ОтчетСканирования (Вызов,firstdoc,Статус,Сообщение)
   
        record pm_rmprop (pmrmprop);
        record pm_paym   (pmpaym);

        setbuff (pspo_firstdoc,firstdoc);

        if (Вызов==Первый)
[      Отчет о групповом выполнении операций для открытых платежных документов.    ];
[┌─────────┬─────────────┬──────────┬──────┬──────────────────────────────────────┐];
[│  Номер  │   Сумма     │   Дата   │Номер │                Сообщение             │];
[│документа│             │          │ошибки│                                      │];
[├─────────┼─────────────┼──────────┼──────┼──────────────────────────────────────┤];
        elif (Вызов==Ошибка)

                if (Статус == 0)
                        Сообщение = "Операция выполнена успешно";
                end;
                if ( FindPayment(
                        0,      /* Индентификатор неизвестен    */
                        PM_PURP_POPRIMARY,      /* Основной по клиентской платежке */
                        0,      /* Subpurpose                   */
                        PS_PAYORDER, /* Вид первичного документа */ 
                        pspo_firstdoc.OrderID,
                        true,   /* Поиск по реальной базе       */
                        pm_paym,
                        null,
                        null,
                        pm_rmprop
                        )       !=      0 )

                        Статус = 1;
                        Сообщение = "Не найдено платежное поручение";
                else
                  if( pm_paym.PaymStatus == PM_REJECTED )
                    Сообщение = ReadNoteForObject( OBJTYPE_PAYMENT, makeObjectID(OBJTYPE_PAYMENT, NULL, pm_paym), PM_NOTEKIND_DENIALGROUND );
                  end;
                  Сообщение = Сообщение+". "+GetStepNameExecuteRead(pm_paym.PaymentID, pm_paym.DocKind, pm_paym.PaymStatus);
                end;
[│#########│#############│##########│######│######################################│](pm_rmprop.Number,pm_paym.Amount,pm_rmprop.Date,Статус,StrSubst(Сообщение, "|", " " ):w);
                
        elif (Вызов==Последний)
[└─────────┴─────────────┴──────────┴──────┴──────────────────────────────────────┘];
        end;
END;