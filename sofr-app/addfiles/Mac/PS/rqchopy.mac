/* -------------------------------------------------------------------- */
/*         Автоматизированная банковская система RS-Bank v6.0           */
/*                 Copyright (c) R-Style Software Lab                   */
/*                                                                      */
/*  Имя файла  : rqchopy.mac                                            */
/*                                                                      */
/*  Описание   : Операция - 3036 - "Изменение номера счета"             */
/*               Шаг      - 40   - "Открытие нового транзитного счета"  */
/*                                                                      */
/*  Программист: Андреева Е.П.                                          */
/*                                                                      */
/*  Создан     : 02.11.2004                                             */
/*                                                                      */
/* -------------------------------------------------------------------- */
IMPORT InsCarryDoc, "reqinter.mac", OprInter, PSInter;

var ReqChangeAccObj:RsbReqChangeAcc;

record RecordAcc    ( account  );
record RecordLinkAcc( account  );
record ab           ( accblnc  );

file   ReqLink      ( reqlinka );

/* Выполнение шага */
MACRO ExecuteStep( doc, reqacc )

  var ID_Link;
  var stat;

  file acc    ( "account"  );

  acc.Chapter       = 1;
  acc.Code_Currency = ReqChangeAccObj.NewAccountFIID;
  acc.Account       = ReqChangeAccObj.NewAccount;

  if( GetEQ( acc ) )
    Copy( RecordAcc, acc );
  else
    MsgBox("Ошибка при поиске основного счета");
    return 1;
  end;
  ReqLink.DocKind   = PS_REQCHANGE;
  ReqLink.RequestID = ReqChangeAccObj.RequestID;
  ReqLink.GroupID   = OBJROLE_ACC_TRANSIT;
  ReqLink.Action    = 0;

  if( GetEQ( ReqLink ) )

    ClearRecord( RecordLinkAcc );

    RecordLinkAcc.Account = ReqLink.Account;

    if( RecordLinkAcc.Account == "" )
      RecordLinkAcc.Account = ПолучитьНомерСвязанногоСчетаЗаявления( ReqLink, ReqChangeAccObj.NewAccount, ReqChangeAccObj.Balance0 );
    end;

    stat = CheckKey( 1, RecordLinkAcc.Account );

    if( stat )
      DisplayError();
      return stat;
    end;

    if( not Req_IsAccExist( ReqLink.Code_Currency, RecordLinkAcc.Account ) )
       if( not GetString( RecordLinkAcc.Account, "Счет уже существует:", 20 ) )
           return 1;
       end;
    end;

    RecordLinkAcc.Code_Currency = ReqLink.Code_Currency;
    RecordLinkAcc.Balance       = ReqLink.Balance0;

    if( RecordLinkAcc.Balance == "" )
      RecordLinkAcc.Balance = ReqChangeAccObj.Balance(0);
    end;

    RecordLinkAcc.Open_Date     = {curdate};
    RecordLinkAcc.Oper          = ReqChangeAccObj.NewAccountOper;
    RecordLinkAcc.Client        = Req_GetAccClient( ReqChangeAccObj.NewAccountFIID, ReqChangeAccObj.NewAccount );
    RecordLinkAcc.Department    = ReqChangeAccObj.NewAccountDep;
    RecordLinkAcc.Branch        = ReqChangeAccObj.NewAccountBranch;
    RecordLinkAcc.Chapter       = ReqLink.Chapter;
    RecordLinkAcc.Type_Account  = "Y"; /* Транзитный счет */
    RecordLinkAcc.NameAccount   = "Транзитный счет для зачисления валютных поступлений к текущему счету " + RecordAcc.Account;
    RecordLinkAcc.Kind_Account  = ПолучитьВидСчета( RecordLinkAcc.Balance, RecordLinkAcc.Chapter, RecordLinkAcc.Code_Currency );
    RecordLinkAcc.UserField1    = "Открыт по распоряжению на изменение номера счета";

    ClearRecord( ab );
    ab.Account       = RecordLinkAcc.Account;
    ab.Code_Currency = RecordLinkAcc.Code_Currency;
    ab.Chapter       = RecordLinkAcc.Chapter;
    ab.Balance0      = ReqLink.Balance0;
    ab.Balance1      = ReqLink.Balance1;
    ab.Balance2      = ReqLink.Balance2;
    ab.Balance3      = ReqLink.Balance3;
    ab.Balance4      = ReqLink.Balance4;
    ab.Balance5      = ReqLink.Balance5;
    ab.Balance6      = ReqLink.Balance6;
    ab.Balance7      = ReqLink.Balance7;
    ab.Balance8      = ReqLink.Balance8;
    ab.Balance9      = ReqLink.Balance9;
    ab.Balance10     = ReqLink.Balance10;
    ab.Balance11     = ReqLink.Balance11;

    if( not InsertAccount( RecordLinkAcc, ab, ID_Link, true ) )
      MsgBox( "Ошибка при открытии транзитного счета " + RecordLinkAcc.Account );
      return  1;
    end;

    /* Осуществляем привязку транзитного счета */
    if( not InsertObjectLink( OBJTYPE_ACCOUNT, ReqLink.GroupID,
                              UniID( RecordAcc, OBJTYPE_ACCOUNT ), false,
                              OBJTYPE_ACCOUNT, ID_Link, true ) )
      MsgBox( "Ошибка при привязке транзитного счета " + RecordLinkAcc.Account );
      return  1;
    end;
  else
     MsgBox("В списке связанных счетов не задан номер транзитного счета");
     return 1;
  end;

  return 0;

END;
