 /*
 $Name: Idxwp10.mac
 $Module: РКО
 $Description: Макрос шага
 */

//-----------------------------------------------------------------------------
// Блок     : 29052 - "Картотека ожидания разрешения"
// Шаг      : 610    - "Постановка на картотеку ОР"
// Описание : Макрос шага
//-----------------------------------------------------------------------------

import  InsCarryDoc, OprInter, FIInter, BankInter, payconst, payinter, catfdoc, 
        pm_common, pm_setst, cbsttls, "sf_lib.mac", "pm_answerret.mac";

var PaymentObj:RsbPayment;

//Получить счет дебета последней проводки по платежу (смотрим только рублевые проводки)
//если она была внебалансовой (глава 3)
private macro GetAccount_PayerDocChapt3(PaymentID, Account_Payer)

  var select = "select d.t_account_payer, d.t_chapter "+
               "from dacctrn_dbt d, dpmdocs_dbt pmd "+   
               "where pmd.t_paymentid = :PaymentID "+
                 "and d.t_acctrnid = pmd.t_acctrnid "+
                 "order by d.t_acctrnid desc ";

  var params:TArray = TArray();
  params[params.size] = SQLParam( "PaymentID", PaymentID );
  
  var rset = execSQLselect( select, params, false );
  
  if( rset and rset.moveNext() and  (rset.value(1) == 3))
    SetParm(1, rset.value(0));
    return true;
  end;

  return false;
end;

private macro УдалитьПроводкуШагаСписаниеИзК1( Payment:RsbPayment, ID_Operation:integer )
  var carrydocument:TBFile = TBFile("acctrn.dbt",  "R", 0, "acctrn.dbt", "bank.def");

  var query:string = "SELECT DOCS.T_ACCTRNID                             " +
                     "  FROM DOPRSTEP_DBT STEP,                          " +
                     "       DOPRDOCS_DBT DOCS                           " +
                     " WHERE DOCS.T_DOCKIND      = 1                     "/*DLDOC_CARRY*/+
                     "   AND DOCS.T_ID_OPERATION = :ID_OPERATION         " +
                     "   AND STEP.T_ID_OPERATION = DOCS.T_ID_OPERATION   " +
                     "   AND STEP.T_ID_STEP      = DOCS.T_ID_STEP        " +
                     "   AND STEP.T_ISEXECUTE    = 'X'                   " +
                     "   AND STEP.T_KIND_ACTION  = 205                   "/*PM_OPR_KA_ACCEPTI1*/+
                     " ORDER BY DOCS.T_ID_STEP DESC                      ";

  var params:TArray = makeArray( SQLParam( "ID_OPERATION", ID_Operation ) );

  var rs:RsdRecordset = execSQLselect( query, params, true );
  while( rs and rs.moveNext() )

    if( carrydocument )
      carrydocument.rec.AccTrnID = rs.value(0);
      if( carrydocument.GetEQ() and  ( carrydocument.rec.Result_Carry == 48 ) )
        // Удалить внебалансовую проводку, выполненную на шаге "Списание из картотеки 1"
        if( not Opr_DeleteCarry( carrydocument.rec.AccTrnID ) )
          MsgBox("Ошибка при удалении проводки");
          return 1;                             
        end;
        return 0;
      end;
    end;
  end;
  return 0;

ONERROR(x)
  
  MsgBox( "Ошибка получения шага операции|" + x.Message );
  return FALSE;


end;


private macro ValueDateForPlaceToIWP(Paym : RsbPayment) : date
  var ValueDate : date = {curdate};
  var DocKind = Paym.PrimDocKind;

  if( GetParentOrEqualDocKindFromList(DocKind, PMDOC_CLIENTPAYMENT) )
    ValueDate = PM_GetOperDay_BankServiceBalance(Paym.Department);
  elif( GetParentOrEqualDocKindFromList(DocKind, DLDOC_BANKORDER) )
    ValueDate = PM_GetOperDay_Balance(Paym.Department);
  end;

  return ValueDate;
end;

private macro СчетДебетаСвязанС_КУ_Картотека1( PaymentID )

  var select1 = "select d.t_account_payer, d.t_FIID_payer, d.t_chapter "+
               "from dacctrn_dbt d, dpmdocs_dbt pmd "+   
               "where pmd.t_paymentid = :PaymentID "+
                 "and d.t_acctrnid = pmd.t_acctrnid "+
                 "order by d.t_acctrnid desc ";

  var params1:TArray = TArray();
  params1[params1.size] = SQLParam( "PaymentID", PaymentID );
  
  var rs1 = execSQLselect( select1, params1, false );
  
  if( rs1 and rs1.moveNext() )
    var select2 = "SELECT 1        " +
                  "  FROM DUAL     " +
                  " WHERE EXISTS ( " +
                                   "SELECT /*+FIRST_ROWS(1)*/ 1                " +
                                   "  FROM dmccateg_dbt cat, dmcaccdoc_dbt doc " +
                                   " WHERE cat.t_number   =  101               " +
                                   "   AND doc.t_catid    =  cat.t_id          " +
                                   "   AND doc.t_account  = :Account           " +
                                   "   AND doc.t_currency = :FIID              " +
                                   "   AND doc.t_chapter  = :Chapter           " +
                                   "   AND ROWNUM <= 1 )                       " ;
    
    var params2:TArray = makeArray( SQLParam( "Account", rs1.value(0) ),
                                    SQLParam( "FIID"   , rs1.value(1) ),
                                    SQLParam( "Chapter", rs1.value(2) ) );
    
    var rs2 = execSQLselect( select2, params2, TRUE );
    
    if( rs2 and rs2.moveNext() )
      return true;
    else
      return false;
    end;
  end;
  
  return false;

end;

macro ExecuteStep( doc, payorder )
  
  var ID_Operation = 0;
  // Для документов ПЗО вызовем функцию начисления комиссий
  if( IsSfCommPayment( PaymentObj ) )
    if( not chargeComission( PaymentObj.PaymentID ) )
      MsgBox("Ошибка при начислении комиссии");
      return 1;
    end;
  end;
  
  var category:TRecHandler = TRecHandler("objattr");
    if( PaymentObj.Categories.GetFirst(OBJ_PAYMENT_GROUP_EXECUTE_ACC_STOP, {curdate},  category) )
      
      var IndPlace_ExecAlthoughTaxClaim:integer = Bnk_GetRegistryValue( "CB\\PAYMENTS\\DepartmentalInfo\\IndPlace_ExecAlthoughTaxClaim", V_INTEGER, 0 );

      if( ((IndPlace_ExecAlthoughTaxClaim == 3) and ((PaymentObj.PayType == BPT_TAX) or (PaymentObj.PayType == BPT_INSURANCE))) or
          ((IndPlace_ExecAlthoughTaxClaim == 1) and (PaymentObj.PayType == BPT_TAX)) or
          ((IndPlace_ExecAlthoughTaxClaim == 2) and (PaymentObj.PayType == BPT_INSURANCE)))
        
        if(not ConnectCategory( OBJTYPE_PAYMENT,
                                OBJ_PAYMENT_GROUP_EXECUTE_ACC_STOP,
                                PaymentObj.PaymentID,
                                false,
                                1, "", "" ))
          MsgBox("Ошибка при установке категории платежа");
          return 1;
        end;

      end;
    end;

  if( getParm( 3, ID_Operation ) )
    УдалитьПроводкуШагаСписаниеИзК1( PaymentObj, ID_Operation );
  end;
  
  var SfContrID = PM_GetSfContrID(PaymentObj);

  if( SfContrID == 0 )
    msgbox("Не найден договор обслуживания счета плательщика.");
    return 1;
  end;

  PaymentObj.ValueDate = ValueDateForPlaceToIWP(PaymentObj);
  // Установить дату начала операции равной дате операционного дня пользователя
  SetOprDate(29000000, PaymentObj.ValueDate);
  
  //Получим счета для внебалансовой проводки
  var FD_Index1: PaymIndex1_FirstDoc  = PaymIndex1_FirstDoc (PaymentObj.PaymentID);
  
  var СчетДт;
  if ( not GetAccForPlaceToIWP(PaymentObj,@СчетДт) ) 
    СчетДт = TIndexWPPrimDoc(SfContrID, PaymentObj.PaymentID).FindAndOpenSysAccount( "Карт ОР", IsOprMultiExec() );
  end;

  var СчетКт;                                  
  if( GetAccount_PayerDocChapt3(PaymentObj.PaymentID, СчетКт) )
    if( СчетДебетаСвязанС_КУ_Картотека1(PaymentObj.PaymentID) ) /* Платеж из К1 */
      СчетКт = FD_Index1.FindAndOpenAccount( "Картотека 1", 0, PaymentObj.PayerBankEnterDate );
    elif ( PaymentObj.PaymStatus != PM_I2PLACED) /* Платеж не из К2 */
      СчетКт = NotBalCorrAcc_FirstDoc( "П" ).FindAndOpenSysAccount( "ВнебалСчетКорресп", 0, {curdate} );
    end;
  else
    СчетКт = NotBalCorrAcc_FirstDoc( "П" ).FindAndOpenSysAccount( "ВнебалСчетКорресп", 0, {curdate} );
  end;

  // Выполнить проводку по внебалансу
  var paymtr:RsbAccTransaction = PaymentObj.MakeTransaction();

  if( paymtr == NULL )
    MsgBox("Ошибка при создании проводки по платежу");
    return 1;
  end;

  paymtr.Chapter         = 3;                                            
  paymtr.Date_Carry      = {curdate};
//  paymtr.Date_Document   = PaymentObj.ValueDate;
  paymtr.Number_Pack     = PaymentObj.NumberPack;
  paymtr.Numb_Document   = PaymentObj.Number;
  paymtr.ResultCarry     = OBIWPINCARRY;
  paymtr.Kind_Oper       = " 1";
  paymtr.Shifr_Oper      = "09";
  paymtr.Department      = PaymentObj.Department;
  paymtr.AccountPayer    = СчетДт;
  paymtr.AccountReceiver = СчетКт;
  paymtr.FIID            = NATCUR;
  if( PaymentObj.DocKind == PS_INRQ )
    paymtr.Sum           = PaymentObj.FutureReceiverAmount;
  else
    paymtr.Sum           = PaymentObj.FuturePayerAmount;
  end;
  paymtr.Ground          = "Постановка в Картотеку ОР документа № " + string(PaymentObj.Number) + 
                           " от "                                   + string(PaymentObj.Date)   + 
                           " к счету "                              + string(PaymentObj.PayerAccount);

  if( not paymtr.Carry )
    MsgBox("Ошибка при актуализации платежа");
    return 1;
  end;
        
   //Свяжем счет плательщика и картотеки ОР
  if(not SetLinkedAccount( PaymentObj.PayerAccount, PaymentObj.PayerFIID, 1, СчетДт, 
        0/*NATCUR*/, 3/*CHAPT3*/, 49/*OBJROLE_ACC_IWPOBACC*/ ))
    return 1;
  end;
  
  // Установить статус платежа
  PaymentObj.PaymStatus = PM_IWPPLACED;

  // Установить статус первички
  PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_IWP );
  
  if( УстановитьСтатусыПлатежа(OPR_PAYM_INDEX, OPR_PAYM_ST_INDEX_WP_MEET) )
    msgbox("Ошибка при установке сегментов статуса экземпляра операции");
    return 1;
  end;

  return 0;
end;

// Сообщить о неисполнении
private macro InformAboutNonExecution()
  if( ДокументПорожденВходящимЭСИДNew(PaymentObj.PaymentID) )
    
    // Параметры создаваемого подтверждения банка
    var ErrCode : integer = FNS_RP_PAY_NO_EXEC_ACC_CLAIM;
    var ErrDescription : string = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_MNS, ErrCode);

    var ErrList : RsbWlError = RsbWlError(0);
    var wlerr_buf : TRecHandler = TRecHandler("wlerror.dbt");
    wlerr_buf.rec.Code = "00";
    wlerr_buf.rec.Description  = string("поручение налогового органа № ",
        PaymentObj.Number, " от ", PaymentObj.Date,
        " на сумму ", PaymentObj.BaseAmount, 
        " не может быть исполнено в установленный срок в связи с наличием ограничений на распоряжение денежными средствами на счете ", 
        PaymentObj.PayerAccount, " клиента ", PaymentObj.PayerName, " банка (филиала банка)");

    if( ErrList.Insert( wlerr_buf ) != 0 )
      msgbox("Ошибка при вставке подтверждения банка");
      return;
    end;

    // Вставка подтверждения банка
    if( not ВставитьПодтверждениеБанкаФНС( NULL, ErrCode, ErrDescription, ErrList, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, PaymentObj.PaymentID, 2 ) )
      msgbox( "Ошибка при вставке подтверждения банка" );
    end;
  end;
end;

macro PostStep( message,     /* данный параметр может принимать следующие       */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                FirstDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                Num_Step,    /* Номер шага операции (из настроек)               */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep,    /* вид шага операции                               */
                ID_Step )    /* внутренний идентификатор шага операции          */

  var stat = 0;

  if( ( errTrn == 0 ) and ( message == 1 ) )// на выполнении шага
    if((PaymentObj.DocKind == PS_PAYORDER) or (PaymentObj.DocKind == PS_INRQ))
      var DocKind, Origin;
      var rs = execSQLselect("select nvl(ps.t_DocKind, " + PSPOKIND_REQUEST + "), nvl(ps.t_Origin, inr.t_Origin) from dpspayord_dbt ps, dpsinrq_dbt inr, dpmpaym_dbt pm " +
                             " where pm.t_PaymentID = ? and ps.t_OrderID(+) = pm.t_PaymentID and inr.t_PaymentID(+) = pm.t_PaymentID",
                             makeArray(SQLParam("", PaymentObj.PaymentID)));
      if (rs and rs.MoveNext() and (rs.value(1) == PSPO_OR_PAYEEBANK))
        DocKind = rs.value(0);
        Origin = rs.value(1);
        var Queries : string = "InfoCode:8";

        var Narrative : string = "";
        var Rec_Account : TRecHandler = TRecHandler("account");
        PM_GetAccountRecord( PaymentObj.FuturePayerAccount, PaymentObj.FuturePayerFIID, PaymentObj.Chapter, Rec_Account );
        if( Index( Rec_Account.rec.Type_Account, "Т" ) // Если запрет на дебет
            or
           (Rec_Account.rec.DateNoChange >= PaymentObj.ValueDate) // Запрет проводок
          )
          Narrative = "На счет плательщика " + PaymentObj.FuturePayerAccount + " " +
                      "наложен запрет на проведение операций";
        else
          Narrative = "К счету плательщика " + PaymentObj.FuturePayerAccount + " " +
                      "есть картотека ожидающих разрешения";
        end;

        Narrative = 
          "Положительные результаты всех процедур приема к исполнению " +
          "(включая положительный результат проверки достаточности денежных средств " +
          "на счете плательщика), уведомляем о приеме " + 
          IfThenElse( DocKind == PSPOKIND_REQUEST,
                      "инкассового поручения", "платежного требования" ) + 
          " к исполнению. " + Narrative;

        CreateED274(PaymentObj.PaymentID, Queries, Narrative, ID_Oper, ID_Step);
      end;
    end;

    // Сообщить о неисполнении
    InformAboutNonExecution();
  end;

  return stat;
end;
