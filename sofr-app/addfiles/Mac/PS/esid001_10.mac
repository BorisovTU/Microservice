/*
$Name:             esid001_10.mac
$Module:           РКО юридических лиц
$Description:      Операция 4004. Шаг <Предобработка выставленного распоряжения>
*/

import pm_tools, pm_chkrst, cbsttls, pmtax, pm_const;

var PaymentObj:RsbPayment;
var DenialGroundStr:string = "";

//-----------------------------------------------------------------------------
// Является ли субъект резидентом
//-----------------------------------------------------------------------------
PRIVATE MACRO IsResident( BankID )
  private FILE fparty ( "party.dbt" );

  var OldKey, result = true;

  OldKey = KeyNum( fparty, 0 );

  fparty.PartyId = BankID;

  if( getEQ( fparty ) )
    if( fparty.NotResident == "X" )
      result = false;
    end;
  end;

  keyNum( fparty, OldKey );

  return result;
END;
//------------------------------------------------------------------------------
// Получить БИК субъекта
//------------------------------------------------------------------------------
private macro ПолучитьБИК( PartyID:integer ):string

  var select:string = "select t_Code from dpartcode_dbt where t_CodeKind=:CodeKind and t_PartyID =:PartyID";
  if( PartyID == 0 )
      PartyID = {OurBank};
  end;
  var params:TArray = makeArray( SQLParam( "CodeKind", 3 ),
                                 SQLParam( "PartyID" , PartyID ) );
  var rset:RsdRecordset = execSQLselect( select, params, FALSE );
  if( rset and rset.moveNext() )
    return rset.Value( 0 );
  end;

  return "";
end;

//-----------------------------------------------------------------------------
// Получить сокращенное наименование банка
//-----------------------------------------------------------------------------
private macro ПолучитьСокращенноеНаименование(PartyID:integer)
  RECORD party(party);

  if(ПолучитьСубъекта(PartyID, party) == 0)
    return party.shortname; 
  end;     
  
  return "";
end;

//-----------------------------------------------------------------------------
// Сформировать новую строку примечания
//-----------------------------------------------------------------------------
private macro AddGroundStr(ground)
  DenialGroundStr = DenialGroundStr + ground + "\n"
end;

//-----------------------------------------------------------------------------
// Проверить, открыт ли операционный день в подразделении
//-----------------------------------------------------------------------------
private macro IsOpenOperDay( Department , ValDate )
  var select:string = " select t_IsClosed " +
                      " from dcurdate_dbt " +
                      " where t_Branch = :Department " +
                      " and t_CurDate = :ValDate ";

  var params:TArray = makeArray( SQLParam( "Department", Department),
                                 SQLParam( "ValDate"      , ValDate ));
  var rset:RsdRecordset = execSQLselect( select, params, TRUE );
  if( rset and rset.moveNext() and (rset.value("t_IsClosed") != "X"))
    return true;
  end;
  return false;
end;

//-----------------------------------------------------------------------------
// Проверить, существует ли субъект в базе
//-----------------------------------------------------------------------------
private macro СубъектСуществует( PartyID:integer, IsLocked:bool ):bool
  
  var IsExist:bool = false;
  
  IsLocked = false;

  RECORD party(party);

  if(ПолучитьСубъекта(PartyID, party) == 0)
    IsExist = true;
    IsLocked = party.locked == "X";
  end;     
  
  SetParm(1, IsLocked);
  return IsExist;
end;

private macro CheckDoc( )

  file account( account );
  var AccountMaxLen;
  var ClaimWithoutAccept:bool = false; 
  var OldDoc:integer = 0;
  var CheckBankEnterDate;
  var err:integer = 0;
  var Direct     = GetOprStatus(OPR_CLAIM_DIRECT); //Направление
  var AccNew :string  = "";
  var IsLocked:bool = false;
  var err_tmp:string  = "";  
  DenialGroundStr = "";

//+1 Номер документа    
  if(PaymentObj.Number == "")
    AddGroundStr("Номер документа должен быть задан");
  elif( int( GetLastSymbols(PaymentObj.Number, PM_DOCNO_NONZERO_LEN) ) == 0 )
    AddGroundStr("Шесть последних разрядов номера должны быть отличны от '000000'");
  end;

//+2 Сумма платежа (сумма основного базового актива  
  if(PaymentObj.BaseAmount == 0.0)
    AddGroundStr("Сумма документа должна быть задана");
  elif (PaymentObj.BaseAmount < 0.0)
    AddGroundStr("Сумма документа не должна быть отрицательной");
  end;
  
//+3 Очередность платежа 
  if( CheckPriority(PaymentObj.Priority, PaymentObj.DocKind ) != 0 )
    AddGroundStr("Очередность платежа должна иметь значение в диапазоне от 0 до " + PM_DefaultMaxPriority());
  end;
  
//+4 Дата поступления в банк получателя   
  if (PaymentObj.ReceiverBankMarkdate == date(0,0,0) )
    AddGroundStr("Дата поступления в банк получателя должна быть задана");
  else 
    GetRegistryValue("CB\\PAYMENTS\\CHECKRECBANKENTERDATE", V_BOOL, CheckBankEnterDate, err);
    if ( CheckBankEnterDate and not IsOpenOperDay(PaymentObj.StartDepartment,PaymentObj.ReceiverBankMarkdate ))
      AddGroundStr("Не найден операционный день с датой, соответствующей дате поступления распоряжения в банк получателя");
    end;
  end;

//+5 Дата документа   
  if (PaymentObj.Date == date(0,0,0) )
    AddGroundStr("Дата составления документа должна быть задана");
  elif (PaymentObj.Date > PaymentObj.ReceiverBankMarkdate)
    AddGroundStr("Дата составления документа не должна быть больше даты поступления документа в банк получателя");
  else
    OldDoc = GetRegValueOldDoc( PaymentObj.DocKind );
    if ((OldDoc != null) and (OldDoc > 0) and ((PaymentObj.ReceiverBankMarkdate - PaymentObj.Date) > OldDoc ))
      AddGroundStr("Дата составления документа меньше даты поступления в банк получателя более чем на " + OldDoc + " дней. Документ устарел и не может быть обработан");
    end;
  end;

//+6 Счет плательщика
  if ( Direct != OPR_CL_ST_DIR_OUT)   
    if ( PaymentObj.PIList( PRT_Debet ).Size > 0)  
      AddGroundStr("В распоряжениях, выставляемых банком получателя, не должно быть уточняющих записей по стороне дебета");
    elif ((PaymentObj.FuturePayerAccount == "") or AccountIsMask( PaymentObj.FuturePayerAccount ))
      AddGroundStr("Счет плательщика должен быть задан");
    else
      ClearRecord( account );
      account.Chapter = PaymentObj.Chapter;
      account.Account = PaymentObj.FuturePayerAccount;
      account.Code_currency = PaymentObj.PayerFIID;
      if ( not geteq(account) or account.Department != PaymentObj.EndDepartment )
        AddGroundStr("Счет плательщика не найден в списке счетов филиала " + ПолучитьБИК(PaymentObj.PayerBankID) + " " +  ПолучитьСокращенноеНаименование(PaymentObj.PayerBankID));
      elif ( PaymentObj.Chapter != 1 )
        AddGroundStr("Счет плательщика должен быть открыт в главе А <Балансовые счета>");
      elif ( (account.Open_Close == "З" ) and (account.Close_Date < {curdate}) )
        AddGroundStr("Счет плательщика закрыт на дату " + {curdate});
      elif (PaymentObj.FuturePayerAccount == PaymentObj.FutureReceiverAccount)
        AddGroundStr("Номер счета плательщика не должен совпадать со счетом получателя");
      end;
    end;
  end;

//+7 Счет плательщика 
  if ( (Direct == OPR_CL_ST_DIR_OUT) and IsResident(PaymentObj.Payer) )       
    GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ОТОБРАЖЕНИЕ ПОЛЕЙ\\ACCOUNTMAXLEN", V_STRING, AccountMaxLen, err);
    if ( strlen(PaymentObj.FuturePayerAccount) > int(AccountMaxLen) )  
      AddGroundStr("Длина счета плательщика не должна быть больше " + AccountMaxLen + " символов");
    else 
      AccNew = GetKey( PaymentObj.FuturePayerAccount, ПолучитьБИК({OurBank}) );
      if( AccNew != PaymentObj.FuturePayerAccount )
//        AddGroundStr("В номере счета " + PaymentObj.FuturePayerAccount + " неверно значение ключа. Должно быть " + SubStr(AccNew, 9, 1) );
     AddGroundStr("В номере счета " + PaymentObj.FuturePayerAccount + " неверно значение ключа. Должно быть " + AccNew );
      end;  
    end;  
  end;

//+8 Счет получателя  
  if ( PaymentObj.PIList( PRT_Credit ).Size > 0)  
    AddGroundStr("В распоряжениях, выставляемых банком получателя, не должно быть уточняющих записей по стороне кредита");
  elif ((PaymentObj.FutureReceiverAccount == "") or AccountIsMask( PaymentObj.FutureReceiverAccount ))
    AddGroundStr("Счет получателя должен быть задан");
  else
    ClearRecord( account );
    account.Chapter = PaymentObj.Chapter;
    account.Account = PaymentObj.FutureReceiverAccount;
    account.Code_currency = PaymentObj.ReceiverFIID;
    if ( not geteq(account) or account.Department != PaymentObj.Startdepartment )
      AddGroundStr("Счет получателя не найден в списке счетов филиала " + ПолучитьБИК({OurBank}) + " " +  ПолучитьСокращенноеНаименование({OurBank}));
    elif ( PaymentObj.Chapter != 1 )
      AddGroundStr("Счет получателя должен быть открыт в главе А <Балансовые счета>");
    elif ( (account.Open_Close == "З" ) and (account.Close_Date < {curdate}) )
      AddGroundStr("Счет получателя закрыт на дату <дата текущего опердня>");
    elif ( not PM_FindBalanceInReg_117( "PS\\REQOPENACC\\СЧЕТА КЛИЕНТОВ", PaymentObj.FutureReceiverAccount, 1 ) )  
      AddGroundStr("Счет получателя должен принадлежать клиенту банка. Счет считается клиентским, если он попадает под одну из масок счетов в настройке реестра PS\REQOPENACC\СЧЕТА КЛИЕНТОВ или владельцем счета является клиент банка. ");
    end;
  end;  

//+9 Банк плательщика 
  if (PaymentObj.PayerBankID <= 0 )
    AddGroundStr("Банк плательщика должен быть задан");
  elif (not СубъектСуществует( PaymentObj.PayerBankID, IsLocked ) or IsLocked )
    AddGroundStr("Банк плательщика " + PaymentObj.BankCode + " не найден в справочнике среди открытых банков");
  end;

//+10 Банк получателя 
  if (PaymentObj.ReceiverBankID <= 0 )
    AddGroundStr("Банк плательщика должен быть задан");
  elif (PaymentObj.ReceiverGroup != PAYMENTS_GROUP_INTERNAL)
    AddGroundStr("Банк получателя должен входить в территориальную структуру <нашего> банка с режимом доступа к данным ЦАБС <On-Line>");
  end;

//11 Условие оплаты 
  if (PaymentObj.DocKind == DLDOC_OUT_PMCLAIM )
    GetRegistryValue("PS\\PAYORDER\\DEMAND\\CLAIMWITHOUTACCEPT", V_BOOL, ClaimWithoutAccept, err);
    var ClaimOrder = GenObject( "RsbOutClaimOrder", PaymentObj.PaymentID );
    if ( not ClaimWithoutAccept and ClaimOrder.AcceptTerm == PSPAYDEM_TERM_WITHOUTACCEPT )
      AddGroundStr("В соответствии с Положением ЦБ РФ №383-П требования с условием оплаты <без акцепта> больше не применяются");
    end;
  end;

 
//+12 Налоговые реквизиты
  if( PM_CheckTaxPropForStep( PaymentObj.PaymentID ) )
    AddGroundStr("Ошибка при проверке налоговых реквизитов");
  end;
  

  //13 Реквизиты сообщения
  if ( Direct == OPR_CL_ST_DIR_OUT)
    if (PaymentObj.OutRlsForm <= 0 )
      AddGroundStr("Не определен релиз исходящего сообщения. Задайте параметры транспортного сообщения в панели позиционирования");
    elif (CheckExternalPaymentForRls(PaymentObj.PaymentID,err_tmp))
     AddGroundStr(err_tmp);
    end;  
  end;

//+14 БИК банка плательщика 
  if (PaymentObj.OutTransport == TRANSP_UFBS )
    if (ПолучитьКодСубъекта(PaymentObj.PayerBankID, PTCK_BIC) == "" )
      AddGroundStr("Для банка плательщика не задан код вида 3 <БИК (ЦБ РФ)>, необходимый для отправки сообщения по транспорту УФЭБС");
    end;  
  end;

//+15 БИК банка получателя 
  if (PaymentObj.OutTransport == TRANSP_UFBS )
    if (ПолучитьКодСубъекта(PaymentObj.ReceiverBankID , PTCK_BIC) == "" )
      AddGroundStr("Для банка получателя не задан код вида 3 <БИК (ЦБ РФ)>, необходимый для отправки сообщения по транспорту УФЭБС");
    end;  
  end;

//+16 Правомерность, выполняется только для инкассового поручения
  if( ( PaymentObj.DocKind == DLDOC_OUT_PMCOL ) and ( PaymentObj.GetPM_PAYM().rec.FIID == NATCUR ) )
    if( ( PaymentObj.Legality == PSRQ_REL_NOTSETTED ) or ( ( PaymentObj.Legality == PSRQ_REL_BYCONTRACT ) and ( PaymentObj.LegalityReason == "" ) ) )
      if( PaymentObj.Legality == PSRQ_REL_NOTSETTED )  
        AddGroundStr("Не установлена правомерность выставления инкассового поручения");      
      else
        AddGroundStr("Не обоснована правомерность выставления инкассового поручения");            
      end;
    end;
  end;
  
  if ( DenialGroundStr != "" )
    // Заполнить примечание
    if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, DenialGroundStr ) != 0 )
      msgbox( "Ошибка при вставке примечания платежа" );
      return 1;
    end;
  end;
  return 0;
end;

MACRO ExecuteStep()

  var stat:integer = 0;
  var RejectGround:string = "";
  var Direct     = GetOprStatus(OPR_CLAIM_DIRECT); //Направление
  
  PaymentObj.Notes.DelNote( PM_NOTEKIND_DENIALGROUND );
  
  stat = CheckDoc( );

  if ( not stat )
    RejectGround = PaymentObj.Notes.ReadNote( PM_NOTEKIND_DENIALGROUND, {curdate} );
  end;
  
  if( not stat and RejectGround == "" )  
    if ( Direct == OPR_CL_ST_DIR_OUT)          
      if( УстановитьСтатусыПлатежа( OPR_CLAIM_DO, OPR_CL_ST_DISCHARGE ) ) 
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
    else
      if( УстановитьСтатусыПлатежа( OPR_CLAIM_DO, OPR_CL_ST_EXEC_CL ) ) 
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
    end;  
  else
    if( УстановитьСтатусыПлатежа( OPR_CLAIM_STATE, OPR_CL_ST_REJECT ) ) 
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
    PaymentObj.paymstatus = PM_REJECTED;

    if (RejectGround != "")
      if (IsOprMultiExec())   
        CreateWarning( null, PAYMERR_PREPESIDWARNING, RejectGround );
      else
        msgbox( RejectGround );
      end;    
    end;  
  end;
  return 0;
end;