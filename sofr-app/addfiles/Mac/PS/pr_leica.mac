/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : pr_leica.mac
  Created     : 18.02.2007
  Programmer  : Осмаковский Е.В.
  Description : Печать письма в ИМНС о закрытии счета по сроку

└───────────────────────────────────────────────────────────────────────────*/

import prreqbuf,reqinter;

//Получить основной регистрационный документ
private macro GetMainDoc( ObjectID,Objrgdoc:object ):bool

   var Tb_Objrgdoc;
   Tb_Objrgdoc = Tbfile("Objrgdoc.dbt","r",2);
   Tb_Objrgdoc.rec.objectID = ObjectID;
   Tb_Objrgdoc.rec.ObjectType = 3;
   Tb_Objrgdoc.rec.ismain = "X";

   if( GetEQ(Tb_Objrgdoc) )
      SetParm(1,Tb_Objrgdoc);
      return true;
   else
      return false;
   end;
end;

//Получить фактический адрес (если нет фактического - то юридический, если нет юридического - почтовый)
private macro GetAdress( PartyID ):string
  var params:TArray  = TArray(),
      select, rs, Adress, stat = true;
   select = "select adr.t_PostIndex, adr.t_CodePlace, adr.t_Place, adr.t_Adress "+ 
             "from dAdress_dbt adr " +
             "where "+ 
               "adr.t_PartyID = :PartyID and "+
               "adr.t_Type = :TypeAdress "; 

   params[params.size] = ( SQLParam( "PartyID" , PartyID ));
   params[params.size] = ( SQLParam( "TypeAdress" , 2 )); //PTADDR_REAL

   rs = execSQLselect( select, params, FALSE );
   if( (not rs) or (not rs.MoveNext()) )
      params[1] = ( SQLParam( "TypeAdress" , 1 )); //PTADDR_LEGAL
      rs = execSQLselect( select, params, FALSE );

      if( (not rs) or (not rs.MoveNext()) )
         params[1] = ( SQLParam( "TypeAdress" , 3 )); //PTADDR_POST
         rs = execSQLselect( select, params, FALSE );

         if( (not rs) or (not rs.MoveNext()))
            stat = false;
         end;

      end;

   end;

   if(stat)
      Adress = string(rs.value(0)) + " " + string(rs.value(1)) + " " +
               string(rs.value(2)) + " " + string(rs.value(3));
   else
      Adress = ""; 
   end;

   return Adress;
end;

macro DrawReport( ОГРН_НашегоБанка,
                  CloseDateAcc,
                  NameClient,
                  TaxAdress,
                  DateBeginCon, 
                  NumberCon,
                  TaxName,
                  ClientAdress,
                  INNClient,
                  RegNum,
                  RegBodyName,
                  StrCur,
                  Account,
                  ИМНС_ДОЛЖ,
                  ИМНС_ФИО,
                  MainRgDocDateCl,
                  MainRgSeriesCl,  
                  MainRgNumberCl,
                  AdvicDate
                  )

  var text:string, StrDateBeginCon:string, StrAdvicDate:string, StrBank:string;
  
  if(DateBeginCon == date(0,0,0))
     StrDateBeginCon = "00.00.0000 г.";
  else
     StrDateBeginCon = string(DateBeginCon) + " г.";
  end;

  if(AdvicDate == date(0,0,0))
     StrAdvicDate = "00.00.0000 г.";
  else
     StrAdvicDate = string(AdvicDate) + " г.";
  end;

  if(strlen(NumberCon) == 0)
     NumberCon = "б/н";
  end;

  var INN, KPP;

  SplitFullINN ({INN_Bank}, INN, KPP);

  StrBank = "ИНН " + INN + "   КПП " + KPP + "  ОГРН " + ОГРН_НашегоБанка;

  text = "  Настоящим сообщаем , что в соответствии с пунктом 1.1 статьи 859 Гражданского Кодекса РФ, " + CloseDateAcc +
         " с клиентом " + NameClient + " расторгнут договор банковского счета от " + StrDateBeginCon +
         " № " + NumberCon + " и закрыт " + StrCur + " счет № " + Account + 
         ". О расторжении договора клиент был уведомлен " + StrAdvicDate;

[                                                                                                ];
[ ################################################################                               ]({Name_Bank});
[                                                                                                ];
[ БИК ###########################                                                                ]({MFO_Bank});
[ ############################################################################################## ]( StrBank:w );
[                                                                                                ];
[                                                                                                ];
[                                                                                                ];
[                                                                                                ];
[ ##########################                                                                     ]({curdate}:m:l);
[  Исх.ном.                                                                                      ];
[                                                                                                ];
[                                                    ########################################### ](("В "+TaxName):r);
[                                                    ########################################### ](TaxAdress:r:w);
[                                                                                                ];
[                                      Сообщение о закрытии счета                                ];
[                                                                                                ];
[                                                                                                ];
[ ############################################################################################## ](text:w);
[                                                                                                ];
[           Сведения о клиенте:                                                                  ];
[           место нахождение юридического лица (место жительство ИП)                             ];
[              ############################################################################      ](ClientAdress:w);
[           ИНН/КПП                  ####################                                        ](INNClient);
[           регистрационный номер    ######                                                      ](RegNum);
[           дата регистрации              ##########                                             ](MainRgDocDateCl);
[           свидетельство            серия ## номер ######                                       ](MainRgSeriesCl,MainRgNumberCl);
[           регистрирующий орган     ###########################################                 ](RegBodyName);
[                                                                                                ];
[                                                                                                ];
[                                                                                                ];
[ ############################### _______________  / ############################### /           ](ИМНС_ДОЛЖ,ИМНС_ФИО:c);
[                                                                                                ];
[                                                                                                ];
[ М.П.                                                                                           ];
[                                                                                                ];


end;

macro PrintDoc( MainRgDocCl , TR_account , nCopy)
                                                                                       
  var params:TArray = TArray(),
      select, rs,err,
      
      ОГРН_НашегоБанка,
      NameClient,
      TaxAdress,
      DateBeginCon,
      NumberCon,
      TaxName,
      ClientAdress,
      INNClient,
      RegNum,
      RegBodyName,
      StrCur,
      ИМНС_ДОЛЖ,
      ИМНС_ФИО,
      
      Client,
      RegID,
      TaxID,
      TR_party:TRecHandler = TRecHandler( "party.dbt"  , "bank.def" ),
      Tb_Objkrgpt;

   ОГРН_НашегоБанка = ПолучитьКодСубъекта({OurBank}, 27);//PTCK_OGRN      
   Client = TR_account.rec.Client;

   NameClient = pr_party.rec.Name;
   select = "select t_RegPartyID " +
               "from dobjrgdoc_dbt " +
                  "where t_ObjectID = :Client " +
                  "and t_ObjectType = 3 " + //OBJTYPE_PARTY
                  "and t_RegPartyKind = 7 " + // REGPT_TAXINSTITUTE
                  "and t_RegDocKind = 14 "; // OBJKDOC_EVIDENCE_REGISTERED
    
   params[params.size] = ( SQLParam( "Client" , Client ) );

   rs = execSQLselect( select, params, FALSE );

   if( rs and rs.MoveNext())
         
      TaxID = rs.value(0);

      if(not GetPartyRec(TaxID,TR_party))
         TaxName = "";
      elif( TR_party.rec.Name )
         TaxName = TR_party.rec.Name;
      elif( TR_party.rec.ShortName )
         TaxName = TR_party.rec.ShortName;
      else
         TaxName = "";
      end;
      
      TaxAdress = GetAdress( TaxID );
      GetNumAndDateContr(pr_reqclosa.rec.Code_Currency,pr_reqclosa.rec.Account, DateBeginCon, NumberCon);
      ClientAdress = GetAdress( Client );
      INNClient = ПолучитьКодСубъекта(Client, 16);//PTCK_INN
      RegNum = ПолучитьКодСубъекта(MainRgDocCl.rec.ObjectID, MainRgDocCl.rec.CodeKind);
      
      Tb_Objkrgpt = Tbfile("Objkrgpt.dbt");
      Tb_Objkrgpt.rec.RegPartyKind = MainRgDocCl.rec.RegPartyKind;
      if(GetEQ(Tb_Objkrgpt))
         RegBodyName = Tb_Objkrgpt.rec.Name;
      else
         RegBodyName = "";
      end;

      if( GetRegValForOPENAC( "ИМНС_ДОЛЖ", V_STRING, ИМНС_ДОЛЖ ) )
         ИМНС_ДОЛЖ = "";
      end;

      if( GetRegValForOPENAC( "ИМНС_ФИО", V_STRING, ИМНС_ФИО ) )
         ИМНС_ФИО = "";
      end;

      if( pr_reqclosa.rec.Code_Currency )
         StrCur = "рублевый";
      else
         StrCur = "валютный";
      end;

      while(nCopy)
         DrawReport( ОГРН_НашегоБанка,
                     TR_account.rec.Close_Date,
                     NameClient,
                     TaxAdress,
                     DateBeginCon, 
                     NumberCon,
                     TaxName,
                     ClientAdress,
                     INNClient,
                     RegNum,
                     RegBodyName,
                     StrCur,
                     pr_reqclosa.rec.Account,
                     ИМНС_ДОЛЖ,
                     ИМНС_ФИО,
                     MainRgDocCl.rec.DocDate,
                     MainRgDocCl.rec.Series,  
                     MainRgDocCl.rec.Number,
                     pr_reqclosa.rec.date
                  );
          nCopy = nCopy - 1;
      end;

   else
      msgbox("Не найдена ИФНС.");
      return false;
   end;

   return true;
end;

macro PrintDocument(nCopy:integer):bool

    var Tb_Objrgdoc:object, MainRgDocCl, StrWithMasks, stat = true,err, 
        TR_account:TRecHandler = TRecHandler( "account.dbt"  , "bank.def" );

    if((pr_reqclosa.rec.SubKind == 1) or (pr_reqclosa.rec.SubKind == 3))// 1 - заявление, 3 - решение суда
       msgbox("Счет был закрыт на основании заявления клиента или решения суда");
       return false;
    end;

    TR_account = GetAccount( 1 , pr_reqclosa.rec.Account, pr_reqclosa.rec.Code_Currency);

    if( GetMainDoc( TR_account.rec.Client , Tb_Objrgdoc ) and (Tb_Objrgdoc.rec.isclosed != "X") )

      MainRgDocCl = TRecHandler("Objrgdoc.dbt");
      Copy( MainRgDocCl, Tb_Objrgdoc );

      GetRegistryValue("PS\\REQOPENACC\\СЧЕТА КЛИЕНТОВ", V_STRING, StrWithMasks, err);
      if( err )
         StrWithMasks = "";
      end;

      if( ( MainRgDocCl.rec.docdate != date(0,0,0) ) 
            and (   (( CompareStrWithMasks( "40802*" , pr_reqclosa.rec.Account ) == 0 ) and (MainRgDocCl.rec.docdate < date(01,01,2004)))
                 or (( CompareStrWithMasks( StrWithMasks , pr_reqclosa.rec.Account ) == 0 ) and (MainRgDocCl.rec.docdate < date(01,07,2002)))
                ) 
        )            
        stat = PrintDoc( MainRgDocCl,TR_account, nCopy);
      else
        msgbox("По данному счету сообщение в ИФНС оформляется на бланке");
      end;

    else

      msgbox("Не найден основной регистрационный документ");
      return false;

    end;

   return stat;
end;
