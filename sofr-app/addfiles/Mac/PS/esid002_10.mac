/*
$Name:             esid002_10.mac
$Module:           РКО юридических лиц
$Description:      Операция 4004. Шаг <Предобработка предъявленного распоряжения>
*/

import pm_tools, pm_chkrst, cbsttls, rmcmptl;

var PaymentObj:RsbPayment;
var DenialGroundStr:string = "";


private macro NeedCheckInComRequest():bool
  var NeedCheck:bool = false;
  var err:integer = 0;

  GetRegistryValue( "PS\\REQUESTRELEVANCY\\CHECK_INCOMREQUEST", V_BOOL, NeedCheck, err );
  if( err != 0 )
    msgbox("Ошибка чтения настройки PS\\REQUESTRELEVANCY\\CHECK_INCOMREQUEST");
    return false;
  end;

  return NeedCheck;
end;

//------------------------------------------------------------------------------
// Получить БИК субъекта
//------------------------------------------------------------------------------
private macro ПолучитьБИК( PartyID:integer ):string

  var select:string = "select t_Code from dpartcode_dbt where t_CodeKind=:CodeKind and t_PartyID =:PartyID";
  if( PartyID == 0 )
      PartyID = {OurBank};
  end;
  var params:TArray = makeArray( SQLParam( "CodeKind", 3 ),
                                 SQLParam( "PartyID" , PartyID ) );
  var rset:RsdRecordset = execSQLselect( select, params, FALSE );
  if( rset and rset.moveNext() )
    return rset.Value( 0 );
  end;

  return "";
end;

//-----------------------------------------------------------------------------
// Получить сокращенное наименование банка
//-----------------------------------------------------------------------------
private macro ПолучитьСокращенноеНаименование(PartyID:integer)
  RECORD party(party);

  if(ПолучитьСубъекта(PartyID, party) == 0)
    return party.shortname; 
  end;     
  
  return "";
end;

//-----------------------------------------------------------------------------
// Сформировать новую строку примечания
//-----------------------------------------------------------------------------
private macro AddGroundStr(ground)
  DenialGroundStr = DenialGroundStr + ground + "\n"
end;

//-----------------------------------------------------------------------------
// Проверить, существует ли субъект в базе
//-----------------------------------------------------------------------------
private macro СубъектСуществует( PartyID:integer, IsLocked:bool ):bool
  
  var IsExist:bool = false;
  
  IsLocked = false;

  RECORD party(party);

  if(ПолучитьСубъекта(PartyID, party) == 0)
    IsExist = true;
    IsLocked = party.locked == "X";
  end;     
  
  SetParm(1, IsLocked);
  return IsExist;
end;

private macro CheckDoc( )

  file account( account );
  var OldDoc:integer = 0;
  var CheckBankEnterDate;
  var err:integer = 0;
  var Direct     = GetOprStatus(OPR_CLAIM_DIRECT); //Направление
  var AccNew :string  = "";
  var IsLocked:bool = false;
  DenialGroundStr = "";
  
//+1 Банк плательщика  
  if (not СубъектСуществует( PaymentObj.PayerBankID, IsLocked ))
    AddGroundStr("Не удалось определить банк плательщика " + PaymentObj.PayerBankCode);
  elif (IsLocked)
    AddGroundStr("Банк плательщика " + PaymentObj.PayerBankCode + " не найден в справочнике среди открытых банков");
  elif (PaymentObj.PayerGroup != PAYMENTS_GROUP_INTERNAL)
    AddGroundStr("Банк плательщика " + PaymentObj.PayerBankCode + " не входит в территориальную структуру <нашего> банка с режимом доступа к данным ЦАБС <On-Line>");
  end;

//+2 Сумма платежа (сумма основного базового актива  
  if(PaymentObj.BaseAmount == 0.0)
    AddGroundStr("Сумма документа должна быть задана");
  elif (PaymentObj.BaseAmount < 0.0)
    AddGroundStr("Сумма документа не должна быть отрицательной");
  end;

//+3 Счет плательщика + 4 Плательщик 
  if ( PaymentObj.PayerAccount == "" )
    AddGroundStr("Не задан счет плательщика");
  else
    ClearRecord( account );
    account.Chapter = PaymentObj.Chapter;
    account.Account = PaymentObj.PayerAccount;
    account.Code_currency = PaymentObj.PayerFIID;
    if ( (not geteq(account)) or (account.Department != PaymentObj.EndDepartment) )
      AddGroundStr("Счет плательщика не найден в списке счетов филиала " + ПолучитьБИК(PaymentObj.PayerBankID) + " " +  ПолучитьСокращенноеНаименование(PaymentObj.PayerBankID));
    else
      if ( (account.Open_Close == "З" ) and (account.Close_Date < {curdate}) )
        AddGroundStr("Счет плательщика закрыт на дату " + {curdate});
      end;
      if ( CheckPaymPayerName( PaymentObj.PaymentID ) )
        AddGroundStr("Наименование плательщика, указанное в платеже, не совпадает с наименованием клиента по счету " + PaymentObj.PayerAccount);
      end;
    end;
  end;

//+5 Правомерность, выполняется только для предъявленного инкассового поручения при включенной настройке PS\RequestRelevancy\CHECK_INCOMREQUEST
  if( ( PaymentObj.DocKind == DLDOC_IN_PMCOL ) and ( PaymentObj.GetPM_PAYM().rec.FIID == NATCUR ) and ( NeedCheckInComRequest() ) )
    if( ( PaymentObj.Legality == PSRQ_REL_NOTSETTED ) or ( ( PaymentObj.Legality == PSRQ_REL_BYCONTRACT ) and ( PaymentObj.LegalityReason == "" ) ) )
      if( PaymentObj.Legality == PSRQ_REL_NOTSETTED )  
        AddGroundStr("Не установлена правомерность выставления инкассового поручения");      
      else
        AddGroundStr("Не обоснована правомерность выставления инкассового поручения");            
      end;
    end;
  end;
  
  if ( DenialGroundStr != "" )
    // Заполнить примечание
    if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, DenialGroundStr ) != 0 )
      msgbox( "Ошибка при вставке примечания платежа" );
      return 1;
    end;
  end;
  return 0;
end;

MACRO ExecuteStep()

  var stat:integer = 0;
  var RejectGround:string = "";
  
  if( IsRevokedByInitiator(PaymentObj) )
    return RevokeEsidOrder(PaymentObj);
  end;

  stat = CheckDoc( );

  if ( not stat )
    RejectGround = PaymentObj.Notes.ReadNote( PM_NOTEKIND_DENIALGROUND, {curdate} );
  end;
  
  if( not stat and RejectGround == "" )  
    if( УстановитьСтатусыПлатежа( OPR_CLAIM_DO, OPR_CL_ST_EXEC_CL ) ) 
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
  else
    msgbox( RejectGround );
    if( УстановитьСтатусыПлатежа( OPR_CLAIM_STATE, OPR_CL_ST_REJECT ) ) 
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
    PaymentObj.paymstatus = PM_REJECTED; 

    if (RejectGround != "")
      if (IsOprMultiExec())   
        CreateWarning( null, PAYMERR_PREPESIDWARNING, RejectGround );
      else
        msgbox( RejectGround );
      end;    
    end;  
  end;  
  
  return 0;
end;