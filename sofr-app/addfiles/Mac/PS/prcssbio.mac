/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : prcssbio.mac                                       */
/*                                                                      */
/*  Описание       : Печать приходного кассового ордера в форме СБ РФ   */
/*                   из РКО                                             */
/*                                                                      */
/*  Программист    : Смирнов С.В. SMR PRN                               */
/*                                                                      */
/*  Создан         : 09.10.02                                           */
/*                                                                      */
/************************************************************************/

import prcssb;

/* Печать приходного кассового ордера в форме СБ РФ */
MACRO PrintDocument(ncopy:integer):bool
  if( pr_pscshdoc.rec.IsCurrency != "X" )
    MsgBox("Форма №230 не предназначена для печати рублевых ордеров");
    return FALSE;
  end;

  var rate               :variant = ПолучитьКурсОрдера(pr_pmpaym);
  var НомерЭкземпляра    :integer = 1;
  var ВсегоЭкземпляров   :integer = ncopy;
  var НаименованиеКлиента:string  = "";
  var sumNatCur          :moneyl  = ПолучитьСуммуВНациональнойВалюте(pr_pmpaym);
  var receiver           :TRecHandler = TRecHandler("party.dbt", "bank.def");

  var DocKind:integer = pr_pmpaym.rec.DocKind;

  if( ( DocKind != 410/*CASH_PS_INCORDER*/ ) )
    MsgBox("Данный вид документа не поддерживается макросом");
    return FALSE;
  end;

  GetAccountClient(pr_pmpaym.rec.ReceiverAccount, pr_pmpaym.rec.FIID, receiver);
  НаименованиеКлиента = receiver.rec.Name;

  while(ncopy != 0)
    PrintCBCashOrderHeader("ПРИХОДНЫЙ КАССОВЫЙ ОРДЕР № " + pr_pmrmprop.rec.Number,
                           pr_pmpaym.rec.ValueDate,
                           rate,
                           230,
                           НомерЭкземпляра,
                           ВсегоЭкземпляров);

    [        ######################################################################################
     От кого ______________________________________________________________________________________
                   (наименование клиента)

    ](НаименованиеКлиента:l);

    PrintCBCashOrder(pr_pmrmprop.rec.Ground, pr_pmpaym.rec.PayerAccount, 
                     pr_pmpaym.rec.ReceiverAccount, pr_pmpaym.rec.Amount, 
                     sumNatCur, pr_pmpaym.rec.FIID, ВсегоЭкземпляров);

    [

     Указанную валюту внес:__________________________
                              (подпись вносителя)
                                                          Кассир __________________________________

                                                          Контролер _______________________________

                                                          Главный бухгалтер _______________________
    
    ];


    ncopy = ncopy - 1;
    НомерЭкземпляра = НомерЭкземпляра + 1;
  end;
  
  return TRUE;

end;
