/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : pr_decsa.mac
  Created     : 30.02.2007
  Programmer  : Осмаковский Е.В.
  Description : Печать распоряжения на закрытие накопительного счета

└───────────────────────────────────────────────────────────────────────────*/

import pmprops,reqinter;

PRIVATE
macro DrawReport( AdressDep,
                  NameBranch,
                  DateOpenAcc,
                  NameClient,
                  StrRest,
                  DateReqOpenAcc,
                  ReceiverName,
                  ReceiverAccount,
                  ReceiverBank,
                  РАСПОРЯЖЕНИЕ_ДОЛЖ,
                  РАСПОРЯЖЕНИЕ_ФИО,
                  pr_reqclsac )

   var text,StrDateReqOpenAcc,StrReceiver;
   if(DateReqOpenAcc == date(0,0,0))
      StrDateReqOpenAcc = "00.00.0000 г."
   else
      StrDateReqOpenAcc = string(DateReqOpenAcc:m);
   end;
 
   text = "   В связи с не предоставлением в банк в течение 6 месяцев со дня открытия накопительного счета " +  
          "заявления клиента на его закрытие, произвести закрытие накопительного счета № " + pr_reqclsac.rec.Account +
          ", открытого " + string(DateOpenAcc:m) + " " + NameClient + ". Остаток средств на счете в размере " +
          " "+ StrRest + " перечислить по реквизитам, указанным в" + 
          " заявлении на открытие накопительного счета  от " + StrDateReqOpenAcc + ":";
  StrReceiver = "Счет № " + ReceiverAccount +" в " + ReceiverBank;

[                                                                                             ];
[ ##########################################################################                  ]({Name_Bank}:w);
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[ #############################################################         ##################### ](AdressDep,{curdate}:m:r);
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[                                                                                 в отдел РКО ];
[                                                         ################################### ](NameBranch:w:r);
[                                                                                             ];
[                                         Распоряжение                                        ]; 
[                                                                                             ];
[                                                                                             ];
[ ########################################################################################### ](text:w);
[                                                                                             ];
[ Получатель: ############################################################################### ](ReceiverName);
[ ########################################################################################### ](StrReceiver:w);
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[ ########################## _________________ / ############################# /              ](РАСПОРЯЖЕНИЕ_ДОЛЖ , РАСПОРЯЖЕНИЕ_ФИО:c);
[                                                                                             ];


end;

macro PrintDirectionClStAcc(pr_reqclsac:TRecHandler):bool

   var AdressDep,
       NameBranch,
       DateOpenAcc,
       NameClient,
       Rest,
       StrRest,
       DateReqOpenAcc,
       ReceiverName,
       ReceiverAccount,
       ReceiverBank,
       РАСПОРЯЖЕНИЕ_ДОЛЖ,
       РАСПОРЯЖЕНИЕ_ФИО,

       TR_party:TRecHandler = TRecHandler( "party.dbt"  , "bank.def" ),
       TR_account:TRecHandler = TRecHandler( "account.dbt"  , "bank.def" ),
       params:TArray  = TArray(),

       
       PartyID,
       PartyIDCorr,
       err,
       errCorr,
       SWIFT,
       SWIFTCorr,
       БИК,
       ReceAcc:ProfitFromAccAccum,
       select,
       rs;

   AdressDep = GetRealAdressDep({OperDprtNode});
   NameBranch = GetNameBranch({OperDprtNode});
   
   TR_account = GetAccount( 1 , pr_reqclsac.rec.Account, pr_reqclsac.rec.AccountFIID);
   DateOpenAcc = TR_account.rec.Open_Date;

   if(GetPartyRec(pr_reqclsac.rec.ClientID,TR_party))
     NameClient = TR_party.rec.Name;
   else
     NameClient = "";
   end;

   //Остаток на счете
   if( pr_reqclsac.rec.AccountFIID == NATCUR )
      Rest = RestA(pr_reqclsac.rec.Account , {curdate});
      StrRest = string(Rest:a) +" ( " + RubToStrAlt(Rest) + " ).";
   else
      Rest = RestAC(pr_reqclsac.rec.Account, pr_reqclsac.rec.AccountFIID, {curdate});
      StrRest = string(Rest:a) +" ( " + CurToStrAlt(Rest, NULL, NULL, GetISOCode(pr_reqclsac.rec.AccountFIID)) + " ).";
   end;

   select = "select * "+
            "from dreqopnac_dbt req " +
            "where " +
            "req.t_ClientID = :ClientID and "+
            "req.t_Account = :Account and " +
            "req.t_AccountFIID = :AccountFIID and "+
            "req.t_CurrentState = 30 ";

   params[params.size] = ( SQLParam( "ClientID" , pr_reqclsac.rec.ClientID ));
   params[params.size] = ( SQLParam( "Account" , pr_reqclsac.rec.Account ));
   params[params.size] = ( SQLParam( "AccountFIID" , pr_reqclsac.rec.AccountFIID ));
   
   rs = execSQLselect( select, params, FALSE );

   if( ( not rs ) or ( not rs.MoveNext()) or ( rs.value("t_ToBankProfit") == "X") )
      ReceiverName = {Name_Bank};

      if( pr_reqclsac.rec.AccountFIID == NATCUR ) 
         ReceAcc = ProfitFromAccAccum( NATCUR );
         ReceiverAccount = ReceAcc.FindAndOpenSysAccount( "Доходы с накоп. счетов", 0, {curdate} );
      else
         ReceAcc = ProfitFromAccAccum( pr_reqclsac.rec.AccountFIID );
         ReceiverAccount = ReceAcc.FindAndOpenSysAccount( "Доходы с накоп. счетов", 0, {curdate} );
      end;
      //ReceiverAccount = "";
      ReceiverBank = {Name_Bank} + " БИК " + {MFO_Bank} + " к/с " + {CORAC_Bank};
   else
      ReceiverName = rs.value("t_ReceiverName");
      ReceiverAccount = rs.value("t_ReceiverAccount");
  
      //Банк для перечисления остатка средств
      if(rs.value("t_AccountFIID") == 0)

         PartyID = ПолучитьКодСубъекта(rs.value("t_ReceiverBankCode"), rs.value("t_ReceiverBankCodeKind"), err);
         if(not err)
            err = GetPartyCodeEx( PartyID, 3, @БИК );
            if(not err)
               ReceiverBank = rs.value("t_ReceiverBankName") + " БИК " 
                                    + БИК +" к/с " + rs.value("t_ReceiverBankCorrAcc");
            else
               ReceiverBank = "";
            end;
         else
           ReceiverBank = "";
         end;

      else
         PartyID = ПолучитьКодСубъекта(rs.value("t_ReceiverBankCode"), rs.value("t_ReceiverBankCodeKind"), err);
         PartyIDCorr = ПолучитьКодСубъекта(rs.value("t_ReceiverCorrCode"), rs.value("t_ReceiverCorrCodeKind") , errCorr);
         if((not err) and (not errCorr))
            err = GetPartyCodeEx( PartyID, 6, @SWIFT );
            errCorr = GetPartyCodeEx( PartyIDCorr, 6, @SWIFTCorr );
            if((not err) and (not errCorr))
               ReceiverBank = "Банк посредник: " + SWIFT + " "+ rs.value("t_ReceiverCorrName")+   
                                   "Банк бенефициара:" + SWIFTCorr + " " + rs.value("t_ReceiverBankName");
            else
               ReceiverBank = "";
            end;
         else
            ReceiverBank = "";
         end;
      end;

   end;

   if(rs)
      DateReqOpenAcc = date(rs.value("t_Date"));
   end;

  if( GetRegValForOPENAC( "РАСПОРЯЖЕНИЕ_ДОЛЖ", V_STRING, РАСПОРЯЖЕНИЕ_ДОЛЖ ) )
     РАСПОРЯЖЕНИЕ_ДОЛЖ = "";
  end;

  if( GetRegValForOPENAC( "РАСПОРЯЖЕНИЕ_ФИО", V_STRING, РАСПОРЯЖЕНИЕ_ФИО ) )
     РАСПОРЯЖЕНИЕ_ФИО = "";
  end;

  
      DrawReport( AdressDep,
                  NameBranch,
                  DateOpenAcc,
                  NameClient,
                  StrRest,
                  DateReqOpenAcc,
                  ReceiverName,
                  ReceiverAccount,
                  ReceiverBank,
                  РАСПОРЯЖЕНИЕ_ДОЛЖ,
                  РАСПОРЯЖЕНИЕ_ФИО,
                  pr_reqclsac);
   return true;
end;