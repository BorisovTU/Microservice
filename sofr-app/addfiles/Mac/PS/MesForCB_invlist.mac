/*
  $Name:        MesForCB_invlist.mac
  $Module:      РКО
  $Description: Формирование описи выборок информации
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : РКО
//
// Описание     : Формирование описи выборок информации
//
// Программист  : Чукина Т.А.
//
// Создан       : 11.12.2015
//
//-----------------------------------------------------------------------------

import rsexts, PSInter, likepy, "rsberror.mac", "bnk_dttmfrmt.mac", "MesForCB_lib.mac",
       BankInter, "bnk_dirfile.mac";

private const TextEditorName : string = "Любой текстовый редактор";

private var CreatedFiles : TArray;

private macro InitCreatedFiles
  CreatedFiles = TArray();
end;

private macro CheckDirExists(Directory : string)
  var ErrMsg : string = "";

  var CheckDirResult : integer = ExistDir(Directory);

  if( CheckDirResult == 2 ) // каталог с указанным именем не существует
    RunError("Неверно указана директория");
  elif( CheckDirResult == 1 )
    ErrMsg = "Каталог " + Directory + " недоступен для записи";
    RunError(ErrMsg, ErrMsg);
  end;

end;

private macro CheckDirectory(Directory : string)
  if(not Directory)
    RunError("Укажите директорию");
  end;

  CheckDirExists(Directory);
end;

private macro CheckFileName(FName : string) : bool
  var s : string = SubStr(FName, 4, 1);
  if(not InList(s, "Z", "s", "p"))
    return FALSE;
  end;

  s = SubStr(FName, 6, 4) + SubStr(FName, 11, 4) + SubStr(FName, 17, 8) + 
      SubStr(FName, 37, 8) + SubStr(FName, 46, 3);

  if(not StrIsNumber(s))
    return FALSE;
  end;

  return TRUE;
end;

private macro AddFileToGroup(FName : string, FileGroups : PSRepMap)
  var NBf_NFf : string = SubStr(FName, 6, 9);

  // Ищем в кэше группу файлов для данного филиала
  var FileGroup = FileGroups.Value( NBf_NFf );

  // Если не нашли, создаем такую группу
  if( FileGroup == NULL )
    FileGroup = TArray();
    FileGroups.Add( NBf_NFf, FileGroup );
  end;

  // Добавляем файл в свою группу
  FileGroup[ FileGroup.size ] = FName;
end;

private macro InitFileGroups
( Directory : string, 
  FileGroups : PSRepMap
)

  var FileMask : string = "LSO?_????_????_F????????_L????????_C????????_???.xml";

  var DirList : TDirList = TDirList(Directory + "\\" + FileMask, "F");

  if(not DirList.count)
    RETURN;
  end;

  for(var i : integer, 0, DirList.count - 1)
    var FName : string = DirList.name(i);

    if( CheckFileName(FName) )
      AddFileToGroup(FName, FileGroups);
    end;
  end;

end;

private macro CheckFilesExist(FileGroups : PSRepMap)
  if(not FileGroups.size)
    var ErrMsg : string = "Указанная директория не содержит файлов с информацией об учетно-операционной деятельности";
    RunError(ErrMsg, ErrMsg);
  end;
end;

macro CheckAndInit(Directory : string, FileGroups : PSRepMap) : bool

  CheckDirectory(Directory);

  InitFileGroups(Directory, FileGroups);

  CheckFilesExist(FileGroups);

  InitCreatedFiles();

  return TRUE;

OnError(er)
  ExeptionMessage(er);
  return FALSE;
end;

private macro GetInvListFileName(Directory : string, NBf_NFf : string) : string
  var whom : string = "";
  if( substr(NBf_NFf, 6) == "0000" )
    whom = substr(NBf_NFf, 1, 4);
  else
    whom = NBf_NFf;
  end;

  var FName : string = Directory + "\\Опись выборок информации по " + whom + ".txt";

  return FName;
end;

private macro CheckFileExists(FileName : string)
    
  if(FileExists(FileName))
    var UserChoice : integer =
      ConfWin( makeArray("Файл " + FileName + " уже существует"), // Text
               makeArray("Перезаписать", "Прервать"), // Buttons
               0 // Default
             );

    if(UserChoice == 1) // "Прервать"
      RunError("Выполнение процедуры прервано пользователем");
    end;
  end;
end;

private class TFileList(FirstFileName : string)
  var Str : string = FirstFileName;

  macro Add(FName : string)
    Str = Str + "\n" + FName;
  end;
end;

private macro GroupFilesByKindAndDateInterval(FileGroup : TArray, FileMap : PSRepMap)
  for(var FName : string, FileGroup)
    var FileKey : string = SubStr(FName, 1, 34);

    if( FileMap.Exists(FileKey) )
      FileMap.Value(FileKey).Add(FName);
    else
      var FileList : TFileList = TFileList(FName);
      FileMap.Add(FileKey, FileList);
    end;
  end;
end;

private class TInventory(MapVal : PSRepMap, p_NBf_NFf : string, p_FileGroup : TArray)
  private var 
    Directory : string = MapVal.Value("Directory"),
    MediaName : string = MapVal.Value("MediaName"),
    MediaNumber : string = MapVal.Value("MediaNumber"),
    AntiVirusName : string = MapVal.Value("AntiVirusName"),
    AntiVirusVersion : string = MapVal.Value("AntiVirusVersion"),
    AntiVirusDate : date = MapVal.Value("AntiVirusDate"),

    NBf_NFf : string = p_NBf_NFf,
    FileGroup : TArray = p_FileGroup,

    InvListFileName = GetInvListFileName(Directory, NBf_NFf),
    OldOutput;

  private macro constructor
    CheckFileExists(InvListFileName);

    // Перенаправляем вывод текста в файл
    OldOutput = SetOutPut( InvListFileName );

    CreatedFiles[ CreatedFiles.size ] = InvListFileName;
  end;

  constructor();

  macro destructor
    // Перенаправляем вывод обратно
    SetOutPut( OldOutput, true );
  end;

  private macro GetPartyIdByBankRegNum(NBf : string, NFf : string) : integer
    var PartyID : integer = 0;

    var select : string = 
      " select t_PartyID "
      "   from dpartcode_dbt "
      "  where t_CodeKind = :PTCK_BANKREGNUM "
      "    and Instr(t_Code, '/') > 0 "
      "    and LPad( Substr(t_Code, 1, Instr(t_Code, '/') - 1), 4, '0') = :NBf "
      "    and LPad( Substr(t_Code, Instr(t_Code, '/') + 1), 4, '0') = :NFf ";

    var params : TArray = makeArray
      ( SQLParam("PTCK_BANKREGNUM", PTCK_BANKREGNUM),
        SQLParam("NBf", NBf),
        SQLParam("NFf", NFf)
      );

    var rs = execSQLselect(select, params);
    if(rs and rs.moveNext())
      PartyID = rs.value("t_PartyID");
    end;

    return PartyID;
  end;

  private macro GetDepNameStr : string
    var HeadPartyID : integer = GetHeadDprtPartyID();
    var HeadName : string = Bnk_GetPartyName(HeadPartyID);

    var NBf : string = substr(NBf_NFf, 1, 4),
        NFf : string = substr(NBf_NFf, 6);

    var PartyName : string = "";
    if(NFf != "0000")
      var PartyID : integer = GetPartyIdByBankRegNum(NBf, NFf);//ПолучитьКодСубъекта(NBf + "/" + NFf, PTCK_BANKREGNUM);
      if(PartyID and (PartyID > 0))
        PartyName = Bnk_GetPartyName(PartyID);
      end;
    end;

    var DepNameStr : string = HeadName + ", регистрационный номер " + NBf;
    if(PartyName)
      DepNameStr = DepNameStr + ", " + PartyName + ", порядковый номер " + NFf;
    end;

    return DepNameStr;
  end;

  private macro GetMediaProp : string
    var MediaProp : string = "";

    if(MediaName)
      MediaProp = MediaName;
    end;

    if(MediaNumber)
      MediaProp = MediaProp + " N " + MediaNumber;
    end;

    return MediaProp;
  end;

  private macro PrintHeader
    var DepNameStr : string = GetDepNameStr();
    var DepName : TArray = StrSplit2(DepNameStr, 103, 103, 3);
    [

                                                                                                     РУКОВОДИТЕЛЮ РАБОЧЕЙ ГРУППЫ
                                                                                                          (ЧЛЕНУ РАБОЧЕЙ ГРУППЫ)

                                                            ОПИСЬ
                                              ВЫБОРОК ИНФОРМАЦИИ (НАБОРОВ ЗАПИСЕЙ),
                                      ПРЕДОСТАВЛЕННЫХ КРЕДИТНОЙ ОРГАНИЗАЦИЕЙ (ЕЕ ФИЛИАЛОМ)
                                                      от #
              #######################################################################################################
              ───────────────────────────────────────────────────────────────────────────────────────────────────────
         (полное фирменное наименование кредитной организации; основной государственный регистрационный номер кредитной орга- 
              #######################################################################################################
              ───────────────────────────────────────────────────────────────────────────────────────────────────────
         низации; регистрационный номер кредитной организации; полное наименование и порядковый номер филиала кредитной орга- 
              #######################################################################################################
              ───────────────────────────────────────────────────────────────────────────────────────────────────────
         низации)
    ](date():m, DepName[0], DepName[1], DepName[2]);

    var i : integer = 3;
    while(i < DepName.size)
    [         #######################################################################################################
              ───────────────────────────────────────────────────────────────────────────────────────────────────────
    ](DepName[i]);
    end;

    var MediaProp : string = GetMediaProp();
    if(MediaProp)
    [
      Настоящим представляем опись выборок информации (наборов записей) на отчуждаемом (съемном) машинном носителе информации 
      однократной записи #
    ](MediaProp);
    end;
  end;

  private macro GetFileKind(FileKey : string) : string
    var Kind : string = SubStr(FileKey, 4, 1);

    if(Kind == "Z")
      return "открытых и закрытых счетах";
    elif(Kind == "s")
      return "остатках на счетах";
    elif(Kind == "p")
      return "операциях по счетам";
    else
      // Такой ситуации не должно быть
      RunError("Имя файла не удовлетворяет требованиям к имени файла");
    end;
  end;

  private macro GetBankRegNum(FileKey : string) : string
    var BankRegNum : string = SubStr(FileKey, 6, 4);

    var NFf : string = SubStr(FileKey, 11, 4);
    if(NFf != "0000")
      BankRegNum = BankRegNum + "/" + NFf;
    end;

    return BankRegNum;
  end;

  private macro ConvertDateFromYYYYMMDD(YYYYMMDD : string) : string
    var dt : date = ToDate_YYYYMMDD(YYYYMMDD);
    return DDpMMpYYYY(dt);
  end;

  private macro GetBegDate(FileKey : string) : string
    var YYYYMMDD : string = SubStr(FileKey, 17, 8);

    return ConvertDateFromYYYYMMDD(YYYYMMDD);
  end;

  private macro GetEndDate(FileKey : string) : string
    var YYYYMMDD : string = SubStr(FileKey, 27, 8);

    return ConvertDateFromYYYYMMDD(YYYYMMDD);
  end;

  private macro PrintTableLine(LineNum : integer, LineKey : string, FileListStr : string)
    var FileDescription : string = 
      "Информация об " + GetFileKind(LineKey) + 
      " филиала " + GetBankRegNum(LineKey) + 
      " за период с " + GetBegDate(LineKey) + " по " + GetEndDate(LineKey);

    [ ├───────┼─────────────────────────────────┼──────────────────────────────┼───────────────────┤
      │#######│#################################│##############################│###################│
    ]( LineNum : c, FileDescription : w, FileListStr : w, TextEditorName : w );
  end;

  private macro PrintTableBody
    /*
    3.3.1.  Сгруппировать файлы с данными филиала по:
      3.3.1.1.  Виду информации (4-тый символ в наименовании файла);
      3.3.1.2.  Периоду запрашиваемых данных (символы с 16 по 34 включительно).
    3.3.2.  Данные каждой полученной группы выводить в первой таблице описи отдельной строкой.
    */

    var TableLines : PSRepMap = PSRepMap(V_STRING);
    GroupFilesByKindAndDateInterval(FileGroup, TableLines);

    var HasElements : bool = TableLines.First();
    var i : integer = 0;
    while(HasElements)
      i = i + 1;

      PrintTableLine(i, TableLines.Key(), TableLines.Value().Str);

      HasElements = TableLines.Next();
    end;
  end;

  private macro PrintTable
    [
      ┌───────┬─────────────────────────────────┬──────────────────────────────┬───────────────────┐
      │ N п/п │ Наименование (описание) выборки │   Состав (перечень) файлов   │ Название и версия │
      │       │   информации (наборов записей)  │ электронной выборки информа- │    программного   │
      │       │                                 │    ции (наборов записей)     │    обеспечения    │
      ├───────┼─────────────────────────────────┼──────────────────────────────┼───────────────────┤
      │   1   │               2                 │               3              │          4        │
    ];

    PrintTableBody();

    [ └───────┴─────────────────────────────────┴──────────────────────────────┴───────────────────┘
    ];
  end;

  private macro PrintHashFunctions
    [
      Результат вычисления хэш-функций для каждого файла, записанного на отчуждаемый (съемный) машинный носитель информации 
      однократной записи, реализованный в соответствии с ГОСТ Р 34.11-2012: 
    ];

    for(var FName : string, FileGroup)
      var HashValue : string = "";
      var stat : integer = AL_GetHashValueFile_3411_2012(Directory + "\\" + FName, HashValue);
      if( stat != 0 )
        HashValue = "Ошибка " + stat + ": " + GetErrMsg();
      end;

      [ #################################################### - #
      ](FName, HashValue);
    end;
  end;

  private macro GetAntiVirusProp : string
    var Prop : string = AntiVirusName;

    if(AntiVirusVersion)
      Prop = Prop + ", версия " + AntiVirusVersion;
    end;

    return Prop;
  end;

  private macro PrintFooter
    var AntiVirusProp : string = GetAntiVirusProp();

    if(AntiVirusProp)
    [
        Проверка отчуждаемого (съемного) машинного носителя информации однократной записи программным средством защиты от воздействия вредоносного кода 
                                                        #
      ────────────────────────────────────────────────────────────────────────────────────────────────────────
                            (название и версия программного средства защиты)
      с использованием актуальной по состоянию на ################### сигнатурной базы не выявила признаков присутствия вредоносного кода.
    ](GetAntiVirusProp() : c, AntiVirusDate : m);
    end;

    [
      Руководитель кредитной организации (ее филиала)
                                                     ──────────────────────────
                                                            (подпись)                   (должность, Ф.И.О.)
      Главный бухгалтер кредитной организации (ее
      филиала)                                       ──────────────────────────
                                                            (подпись)                   (должность, Ф.И.О.)
        #
                                м.п. кредитной организации
                                      (ее филиала)
    ]( date() : m);
  end;

  private macro PrintAttachmentBody
    var FileDesc : string = "Описание форматов файлов приведено в Приложении к Указанию Банка России от 30 ноября 2014 года № 3462-У \"О составе и форматах представления учетно-операционной и иной информации кредитной организации (ее филиала) в электронном виде\"";
    var DescLines : TArray = StrSplit2(FileDesc, 31, 31, FileGroup.size);

    for(var i : integer, 1, FileGroup.size, 1)

    [ │#######│####################################################│###############################│
    ](i : c, FileGroup[i - 1], DescLines[i - 1]);
    end;

    while(i < DescLines.size)
      i = i + 1;

    [ │#######│####################################################│###############################│
    ]("" : c, "", DescLines[i - 1]);
    end;

  end;

  private macro PrintAttachment
    [
      Приложение к описи выборок информации (наборов записей), предоставленных кредитной организацией (ее филиалом), необходимых для проведения проверки
      ┌───────┬────────────────────────────────────────────────────┬───────────────────────────────┐
      │ N п/п │                 Имя и тип файла                    │        Описание файла         │
      ├───────┼────────────────────────────────────────────────────┼───────────────────────────────┤
      │   1   │                          2                         │               3               │
      ├───────┼────────────────────────────────────────────────────┼───────────────────────────────┤
    ];

    PrintAttachmentBody();

    [ └───────┴────────────────────────────────────────────────────┴───────────────────────────────┘

      Сверка описи выборок информации (наборов записей) с заявкой на предоставление документов (информации) и отчуждаемым 
      (съемным) машинным носителем информации однократной записи произведена

      Ответственный работник кредитной организации
      (ее филиала)                                ──────────────────────────
                                                            (подпись)                   (должность, Ф.И.О.)
      Руководитель рабочей группы или член рабочей
      группы                                       ──────────────────────────
                                                            (подпись)                         (Ф.И.О.)

    ];
  end;

  macro CreateFile : string

    PrintHeader();

    PrintTable();

    PrintHashFunctions();

    PrintFooter();

    PrintAttachment();
  
    return InvListFileName;
  end;
end;

macro PrintInventory
( MapVal : PSRepMap, 
  NBf_NFf : string, 
  FileGroup : TArray,
  InvListFileName : string // возвращаем из макроса в сишник
) : bool

  var Inventory : TInventory = TInventory(MapVal, NBf_NFf, FileGroup);
  InvListFileName = Inventory.CreateFile();

  SetParm(3, InvListFileName);

  return TRUE;

OnError(er)
  ExeptionMessage(er);
  DeleteCreatedFiles(CreatedFiles);
  return FALSE;
end;
