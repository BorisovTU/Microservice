/* -------------------------------------------------------------------- */
/*         Автоматизированная банковская система RS-Bank v6.0           */
/*                 Copyright (c) R-Style Software Lab                   */
/*                                                                      */
/*  Имя файла  : rqchopy.mac                                            */
/*                                                                      */
/*  Описание   : Операция - 3037 - "Изменение номера счета"             */
/*               Шаг      - 85   - "Перенос остатка старого             */
/*                                  счета"                              */
/*                                                                      */
/*  Программист: Локтюшин В.Ю.                                          */
/*                                                                      */
/*  Создан     : 12.02.2007                                             */
/*                                                                      */
/* -------------------------------------------------------------------- */

IMPORT reqinter, PSInter, PaymInter, InsCarryDoc;

var ReqChangeAccObj:RsbReqChangeAcc;

var AccBuff  :TRecHandler = TRecHandler( "account.dbt"  , "bank.def" );

MACRO ExecuteStep( doc, reqacc )

  var stat:integer      = 0, 
      rset:RsdRecordset = NULL;

  var Memorial:object;
  var MemPaym:RsbMOPayment;

  var OldAcc = GetObjLinkAcc( ReqChangeAccObj.OldAccount, ReqChangeAccObj.OldAccountFIID, -1 );

  if( OldAcc == "" )

    // Перенести претензии "Арест на сумму" и "Целевое использование" со старого счета на новый
    if( ReqChangeAccObj.OldAccountFIID == ReqChangeAccObj.NewAccountFIID )

      if( not BorrowAccountClaim( 1/*CHAPT1*/, ReqChangeAccObj.OldAccountFIID, ReqChangeAccObj.OldAccount, ReqChangeAccObj.NewAccount ) )
        msgbox( "Ошибка при переносе претензий со счета ",  ReqChangeAccObj.OldAccount, " на счет ", ReqChangeAccObj.NewAccount );
      end;

    else // Если меняется валюта и есть претензии, нужно предупредить о необходимости ручного переноса претензий
      rset = RSetSelectArestSpecialClaimAccount( "*", ReqChangeAccObj.OldAccount, ReqChangeAccObj.OldAccountFIID );
      if( rset and rset.moveNext() )
        msgbox("Перенос претензий на счёт в другой валюте невозможен.|Ввести претензии для нового номера счёта можно в Главной книге.");
      end;
    end;

    AccBuff = GetAccount( 1, ReqChangeAccObj.OldAccount, ReqChangeAccObj.OldAccountFIID );

    // Перенести Арест на дебет со старого счета на новый
    if( Index( AccBuff.rec.Type_Account, "Т" ) )
      if( ChangeAccountType( 1/*CHAPT1*/, ReqChangeAccObj.OldAccountFIID, ReqChangeAccObj.OldAccount, ACCTYPE_CHANGE_MODE_DEL, "Т" ) or
          ChangeAccountType( 1/*CHAPT1*/, ReqChangeAccObj.NewAccountFIID, ReqChangeAccObj.NewAccount, ACCTYPE_CHANGE_MODE_ADD, "Т" ) )
        msgbox( "Ошибка при переносе ареста на дебет со счета ", ReqChangeAccObj.OldAccount, " на счет ", ReqChangeAccObj.NewAccount );
      end;
    end;

    var RestAcc = RestA( AccBuff.rec.Account, NULL, NULL, 1/*CHAPT1*/, AccBuff.rec.Code_Currency );
    if( RestAcc > 0 )

      Memorial = GenObject("RsbMemorialOrder", 0 );
      MemPaym  = Memorial.Payment();

      Memorial.State             = 0;/*CB_DOC_STATE_DEFERRED*/;
      Memorial.Oper              = {oper};
      Memorial.Chapter           = 1;
      Memorial.Code_Currency     = ReqChangeAccObj.NewAccountFIID;
      Memorial.Origin            = MEMORDER_FDOC_AUTO;

      MemPaym.DocKind            = 70;
      MemPaym.Purpose            = PM_PURP_MEMORDER;
      MemPaym.Number             = 1;
      MemPaym.PayerAmount        = RestAcc;
      MemPaym.PayerFIID          = ReqChangeAccObj.OldAccountFIID;
      MemPaym.ReceiverFIID       = ReqChangeAccObj.NewAccountFIID;
      MemPaym.Ground             = "Перевод остатка с закрываемого счета";
      MemPaym.ClientDate         =
      MemPaym.Date               =
      MemPaym.PayerBankEnterDate =
      MemPaym.ValueDate          = {curdate};
                           

      MemPaym.SetPayerPI( PAYMENTS_GROUP_UNDEF, 
                          {OurBank}, 
                          0, 
                          "", 
                          "",
                          "",
                          ReqChangeAccObj.OldAccountFIID, 
                          1/*CHAPT1*/, 
                          ReqChangeAccObj.OldAccount, 
                          0, 
                          "", 
                          "" );

      MemPaym.SetReceiverPI( PAYMENTS_GROUP_UNDEF, 
                          {OurBank}, 
                          0, 
                          "", 
                          "",
                          "",
                          ReqChangeAccObj.NewAccountFIID, 
                          1/*CHAPT1*/, 
                          ReqChangeAccObj.NewAccount, 
                          0, 
                          "", 
                          "" );
                  
      // Установить мемориальному ордеру признак автозапуска операции.
      Memorial.LaunchOper    = true;
      Memorial.ConnectToOper = true;
      MemPaym.CryptoAction( string("Автоматическое_формирование_платежей") );
    end;

  else
    msgbox( "Нельзя закрыть счет|Есть связанные счета" );
    return 1;
  end;

  return stat;

END;