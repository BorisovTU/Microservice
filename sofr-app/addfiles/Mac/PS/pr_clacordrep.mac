/*
$Name:             pr_clacordrep.mac
$Module:           РКО юридических лиц 
$Description:      Макрос печати протокола выполнения процедуры формирования распоряжений на закрытие счета
*/

import FIInter, PSInter, "pmlib.mac", pm_tools, pr_oppmclac;

private macro GetGround( ground:integer):string

  var llval  = TBfile ("llvalues");

  llval.KeyNum = 0;
  llval.rec.List = 1683;
  llval.rec.Element = ground;

  if(llval.GetEQ)
    return llval.rec.Name;
  end;
  return "";
end;

private macro TableHeader( ground:string );


[
  Протокол выполнения процедуры формирования распоряжений на закрытие счета

  ################################################################################################################################################################################################################################################################################################################################
  Дата выполнения: ##########
  Исполнитель: #

  Основание распоряжений: ##################################################################################################################################################################################################################

  Обработанные счета:

                      │    Дата     │  Тип     │               
       Номер счета    │  открытия   │  счета   │   Результат операции закрытия счета
  ────────────────────┴─────────────┴──────────┴─────────────────────────────────────────────────────────────────────────────────────────────────────]
( PM_GetBankName( {OurBank} ), {curdate}, {Name_Oper}, ground );
end;

private macro TableRow( Account:string, OpenDate:date, TypeAc:string, Result:string )

  array ResultArray;
  var j = 0;

  StrSplit( StrSubst(IfThenElse(Result!="",Result," ") , "|", " " ), ResultArray, 100 );

[ ####################│ ##########  │##########│####################################################################################################]
( Account, OpenDate, TypeAc, ResultArray(j) );
  j = j +1;
  while ( ( StrLen( ResultArray(j) ) > 0 ) )
[                     │             │          │####################################################################################################]
  ( IfThenElse( StrLen(ResultArray(j)) > 0 ,ResultArray(j)," " ) );
    j = j + 1;
  end;

end;

private macro TableFooter()  

[ ───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────

];
end;

macro PrintRepCloseAccOrder( Account:TArray, Ground:string, Result:TArray )

  TableHeader( Ground );
  var Comment:string = "";
  var idx = 0;
  while( idx < Account.Size )
    var select = " select t_Open_Date, t_Type_Account " +
                 "   from daccount_dbt                " +                         
                 "  where t_Account = :Account        " ;
  
    var params:TArray = makeArray( SQLParam( ":Account", Account[idx] ) );
           
    var rs = execSQLselect( select, params, FALSE );
    if( rs.MoveNext() ) 
      TableRow( Account[idx], rs.Value(0), rs.Value(1), Result[idx] );
    end;
    idx = idx +1;
  end;
  TableFooter();
  
  idx = 0;
  while( idx < Account.Size )
    
    select = " select 1 " +
             "    from dpmpaym_dbt pm " +                         
             " where ( pm.t_futurepayeraccount = :Account1 " +
             "     or  pm.t_futurereceiveraccount = :Account2 ) " +
             "   and pm.t_CloseDate = TO_DATE('01-01-0001','DD-MM-YYYY') ";
  
    params = makeArray( SQLParam( ":Account1", Account[idx] ), SQLParam( ":Account2", Account[idx] ) );
            
    rs = execSQLselect( select, params, FALSE );
    if( rs.MoveNext() ) 
      PrintOpenPaymsForAccount( Account[idx] );
    end;
    idx = idx + 1;
  end;
end;