/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.0                                          R-Style Software Lab

  File Name   : psdemscn.mac
  Created     : 23.06.99
  Programmer  : Дудин В.А.
  Description : Печать отчета сканирования картотеки N1

└───────────────────────────────────────────────────────────────────────────*/
IMPORT  PaymInter, OprInter;

RECORD payord (pspayord);

const   Ошибка   = 0,
        Первый   = 1,
        Последний= 2;

MACRO Печать_ОтчетСканирования (Вызов,firstdoc,Статус,Сообщение)
   array StringArray;
   var i = 1;
   var stat;
        record pm_rmprop (pmrmprop);
        record pm_paym   (pmpaym);


        setbuff (payord,firstdoc);


        if (Вызов==Первый)
[                        Отчет о сканировании картотеки N1.                      ];
[+---------+------+------------------------------------------------------------+];
[|  Номер  |Номер |                 Сообщение                                  |];
[|документа|ошибки|                                                            |];
[+---------+------+------------------------------------------------------------+];
        elif (Вызов==Ошибка)

                if (Статус == 0)
                        Сообщение = "Документ изъят из картотеки N1";
                end;
                if ( FindPayment(
                        0,      /* Индентификатор неизвестен    */
                        PM_PURP_POPRIMARY,      /* Основной по клиентской платежке */
                        0,      /* Subpurpose                   */
                        PS_PAYORDER, /* Вид первичного документа */
                        payord.OrderID,
                        true,   /* Поиск по реальной базе       */
                        pm_paym,
                        null,
                        null,
                        pm_rmprop
                        )       !=      0 )

                        Статус = 1;
                        Сообщение = "Не найден платежный документ";
                end;
         StrSplit(StrSubst(Сообщение, "|", " " ), StringArray, 56);                        
[|#########|######|############################################################|](pm_rmprop.Number,Статус,StringArray(0));
         while ( StrLen(StringArray(i) ) > 0)
[|         |      |############################################################|](StringArray(i));
           i = i + 1;
         end;

        elif (Вызов==Последний)
[+---------+------+------------------------------------------------------------+];
        end;
END;