/*
$Name:           pcfmclsa.mac
$Module:         РКО
$Description:    Процедура "Формирование распоряжений на закрытие счетов"
*/

import PSInter, ReqInter, BankInter, pr_decla, pr_decsa, pr_adcla, pr_clacordrep;

PRIVATE CONST NOTHING      = -1;
PRIVATE CONST YES          = 0; // " Да";          
PRIVATE CONST YES_FOR_ALL  = 1; // " Да для всех"; 
PRIVATE CONST NO           = 2; // " Нет";         
PRIVATE CONST NO_FOR_ALL   = 3; // " Нет для всех";

// ----------------------------------------------------------------------------
// Обертка для вызова вертикального меню
// ----------------------------------------------------------------------------
PRIVATE MACRO ShowMenu( title:string ):integer
  
  Array Msg;
  Msg(0) = title;

  Array Box;
  Box(0) = " Да";
  Box(1) = " Да для всех";
  Box(2) = " Нет";
  Box(3) = " Нет для всех";

  return ConfWin( Msg, Box );

END;

class RestPanelParm()
  
  var RestCodeKind:integer,
      RestCode:string,
      RestName:string,
      RestAccount:string,
      RestINN:string,
      RestBankCodeKind:integer,
      RestBankCode:string,
      RestBankName:string,
      RestBankCorrAcc:string,
      RestCorrCodeKind:integer,
      RestCorrCode:string,
      RestCorrName:string;
end;

private macro GetStepName( ReqID:integer ):string
      
  var select:string = " select ost.t_Name " +
                      "   from doproper_dbt op, doprstep_dbt st, doprostep_dbt ost" +
                      "  where op.t_DocKind      = 231 " + //PS_REQCLOSEA
                      "    and op.t_DocumentID   = LPAD(:DocumentID, 34, '0') " +
                      "    and st.t_ID_Operation = op.t_ID_Operation " +
                      "    and st.t_IsExecute    = 'R' " +
                      "    and ost.t_BlockID     = st.t_BlockID " +
                      "    and ost.t_Number_Step     = st.t_Number_Step ";

  var params:TArray = makeArray( SQLParam( "DocumentID", ReqID ) );
  var rs:RsdRecordset = execSQLselect( select, params, false );

  if( rs and rs.moveNext() )
    return rs.Value(0);
  end;
  return "Закрытие договора обслуживания";
end; 
               
private macro GetCloseDate( RequestID ):date

  var select = " select reqcl.t_CloseDate " +
               "   from dreqclosa_dbt reqcl " +
               "  where reqcl.t_RequestID = :RequestID ";

  var params = makeArray( SQLParam( "RequestID", RequestID ) );
  var rset = execSQLselect( select, params, TRUE );
        
  if( rset and rset.moveNext() )
    return rset.Value(0);
  end;

  return date(0,0,0);
end; 
               
MACRO ProcForClosAc( Account, FIID, Dep, Branch, KindOrder, CloseDate, Client, Ground, Operation, StartOp, restParm )

  var stat     = 0;
  var RegVal   = 0;
  var TAcc     = TArray();
  var TAccRep  = TArray();
  var TFIID    = TArray();
  var TResult  = TArray();
  var TReady   = TArray();
  var idx = 0;
  var Final_Date:date;
  var Note_Date:date;
  var Date_Buff:date;
  var Note_Date_Text:string;
  var TRecAcc:TRecHandler = TRecHandler("account.dbt", "bank.def");

  var Key0closa:TBFile      = TBFile( "reqclosa.dbt", "R", 0, "reqclosa.dbt", "bank.def" );
  var RecClosA :TRecHandler = TRecHandler( "reqclosa.dbt", "bank.def" );

  var day, mon, year;
  var AccountRest;
  var Choice1 = NOTHING;
  var Choice2 = NOTHING;
  var StrBuff = "";
  var ReqClosAcc;
  var AccountType = "";
  var ErrMsg = "";

  var rset:RsdRecordset = NULL;

  if( KindOrder != 2 ) /*ПОД/ФТ*/
    stat = GetRegValForOPENAC( "СРОК_ЗНС", V_INTEGER, RegVal );
    if( RegVal == 0 )
      msgbox( "В настройках операций открытия/закрытия счета не предусмотрено закрытие |"+
               "расчетных/текущих счетов клиентов по инициативе банка" );
      return 1;
    end;
  end;

  if( KindOrder == 2 ) /*ПОД/ФТ*/
    GetRegistryValue( "PS\\REQOPENACC\\OPERATION\\ТИПЫ_СЧЕТОВ_ФМ", V_STRING, AccountType, stat );
    if( Index( AccountType, "X" ) <= 0 )
      AccountType = AccountType + "X";
    end;
    if( Index( AccountType, "Ч" ) <= 0 )
      AccountType = AccountType + "Ч";
    end;
    else
    AccountType = "XЧ";
  end;

  if( stat == 0 ) 

    GetAccounts( Dep, Branch, FIID, AccountType, Account, TAcc, TFIID, Client );

    if( TAcc.Size > 0 )

      while( idx < TAcc.Size )
        var select = " select reqcl.t_SubKind, reqcl.t_OrderKind, reqcl.t_InputDate, reqcl.t_RequestID " +
                     "   from dreqclosa_dbt reqcl " +
                     "  where reqcl.t_Account = :Account " +
                     "    and reqcl.t_Code_Currency = :Code_Currency ";

        var params = makeArray( SQLParam( "Account", TAcc[idx] ),
                                SQLParam( "Code_Currency", TFIID[idx] ) );

        rset = execSQLselect( select, params, TRUE );
        
        if( rset and rset.moveNext() )
          if( rset.Value(0) == 2 /*Распоряжение банка*/ and ( rset.Value(1) == KindOrder ) )
            if( StartOp == "X" )
              // запустить операцию по найденному распоряжению так же как и для созданных в данной процедуре
              TReady[idx] = 1;
              SetDialogFlag(0);
              CreateReqClosAOperation( rset.Value(3) );
              ErrMsg = GetErrMsg();
              SetDialogFlag(1);
              if( GetCloseDate( rset.Value(3) ) != date(0,0,0) )
                TResult[idx] = "Счет закрыт по распоряжению банка от " + GetCloseDate( rset.Value(3) );
                TAccRep[idx] = TAcc[idx];
              elif( ErrMsg )
                TResult[idx] = "Операция по закрытию счета прервана с сообщением: " + ErrMsg;
                TAccRep[idx] = TAcc[idx];
              else
                TResult[idx] = "Распоряжение банка от " + rset.Value(2) + " о закрытии счета подготовлено к выполнению шага " + GetStepName( rset.Value(3) ); 
                TAccRep[idx] = TAcc[idx];
              end; 
            else
              TResult[idx] = "Распоряжение банка на закрытие счета " + TAcc[idx] + " создано " + rset.Value(2);
              TAccRep[idx] = TAcc[idx];
              TReady[idx] = 1;
            end;
          else
            TResult[idx] = "Уже существует одно или несколько заявлений/распоряжений на закрытие счета " + TAcc[idx] + 
                           ". Если все созданные ранее заявления некорректны, удалите их из системы. В противном случае закройте счет по одному из существующих заявлений ";
            TAccRep[idx] = TAcc[idx];
            TReady[idx] = 1;
          end;
        end;
        idx = idx + 1;
      end;

      idx = 0;
      
      while( ( idx < TAcc.Size ) and ( TReady[idx] != 1 ) )
          
        Note_Date_Text = RemoveStr2FormStr1( ReadNoteForAccount( TAcc[idx], TFIID[idx], 24 ) );
        Note_Date = date( Note_Date_Text );
        TRecAcc = GetAccount( 1, TAcc[idx], TFIID[idx] );
        Final_Date = date( TRecAcc.rec.Final_Date );
  
        if( Final_Date == date( 0, 0, 0 ) )
          Final_Date = date( TRecAcc.rec.Open_Date );
        end;
  
        if( KindOrder != 2 ) /*ПОД/ФТ*/    
          rset = RSetSelectArestSpecialClaimAccount( "1", TAcc[idx], TFIID[idx] );
          stat = IfThenElse( rset and rset.moveNext(), 1, 0 );

          if( stat == 0 and Note_Date_Text )
            if( Final_Date >= Note_Date )
              stat = InsertNoteForAccount( TAcc[idx], TFIID[idx], 24, date(0,0,0) );
              Note_Date = date( 0, 0, 0);
              Note_Date_Text = "";
            end;
          end;
        end;
              
        datesplit( Final_Date, day, mon, year );
        Date_Buff = date( day, mon, ( year + 2 ) );
                
        if( (( KindOrder != 2 and ( {curdate} > Date_Buff ) ) or KindOrder == 2) and ( Note_Date == date(0,0,0) ) )
          if( ( Choice1 == NOTHING ) or ( ( Choice1 != NO_FOR_ALL ) and  ( Choice1 != YES_FOR_ALL ) ) )
            if( KindOrder != 2 )
              StrBuff = "По счету "+ TRecAcc.rec.NameAccount + " №"+TRecAcc.rec.Account +" не было движений с "+ string( Final_Date ) +". Печатать уведомление о закрытии счета?";
              Choice1 = ShowMenu( StrBuff );
            else
             StrBuff = "Счет" + TRecAcc.rec.Account + " будет закрыт по основаниям ПОД/ФТ. Печатать уведомление о закрытии счета?";
             Choice1 = ShowMenu( StrBuff );
           end;
         end;
       end;

       if( ( Choice1 == YES ) or ( Choice1 == YES_FOR_ALL ) )
         PrintAdviceClAcc( TAcc[idx], TFIID[idx] );
         Note_Date = {curdate};
         datesplit( Note_Date, day, mon, year );
         Note_Date_Text = string( day ) + "." + string( mon ) + "." + string( year );
         stat = InsertNoteForAccount( TAcc[idx], TFIID[idx], 24, Note_Date );
       end;

       datesplit( Date_Buff, day, mon, year );
       mon = mon + 2; 
       if( mon > 12 ) year = year + 1; mon = mon - 12;  end;
       if( day == 31 )
         if( mon == 4 or ( mon == 6 ) or ( mon == 9 ) or ( mon == 11 ) )
           day = 1; mon = mon + 1;
         end;
       end;
       Date_Buff = date( day, mon, year );
 
       if( ( KindOrder != 2 ) and ( {curdate} > Date_Buff ) or ( KindOrder == 2 ) )
                  
         ReqClosAcc = RsbReqCloseAcc();
                 
         ReqClosAcc.SubKind = 2;
         ReqClosAcc.OrderKind = KindOrder;
         ReqClosAcc.Date = {curdate};
         ReqClosAcc.InputDate = {curdate};
         ReqClosAcc.Oper = {Oper};
         ReqClosAcc.Department = {OperDprt};
         ReqClosAcc.Branch = {OperDprtNode};
         ReqClosAcc.Code_Currency = TRecAcc.rec.Code_Currency;
         ReqClosAcc.Account = TRecAcc.rec.Account;
                  
         stat = GetRegValForOPENAC( "РАСПОРЯЖЕНИЕ_ДОЛЖ", V_STRING, RegVal );
         if( stat != 0 ) RegVal = ""; end;
         ReqClosAcc.DelegatePost = RegVal;
  
         stat = GetRegValForOPENAC( "РАСПОРЯЖЕНИЕ_ФИО", V_STRING, RegVal );
         if( stat != 0 ) RegVal = ""; end;
 
         ReqClosAcc.DelegateName1 = RegVal;
         ReqClosAcc.Ground = "Распоряжение на закрытие счета";
         ReqClosAcc.CloseLinkAcc = "X";
         ReqClosAcc.Rest = false;
         ReqClosAcc.Returnes = true; 
         ReqClosAcc.AccCloseDate = CloseDate;
         ReqClosAcc.Kind_Operation = Operation;
         ReqClosAcc.RestCodeKind = restParm.RestCodeKind;
         ReqClosAcc.RestCode = restParm.RestCode;
         ReqClosAcc.RestName = restParm.RestName;
         ReqClosAcc.RestAccount = restParm.RestAccount;
         ReqClosAcc.RestINN = restParm.RestINN;
         ReqClosAcc.RestBankCodeKind = restParm.RestBankCodeKind;
         ReqClosAcc.RestBankCode = restParm.RestBankCode;
         ReqClosAcc.RestBankName = restParm.RestBankName;
         ReqClosAcc.RestBankCorrAcc = restParm.RestBankCorrAcc;
         ReqClosAcc.RestCorrCodeKind = restParm.RestCorrCodeKind;
         ReqClosAcc.RestCorrCode = restParm.RestCorrCode;
         ReqClosAcc.RestCorrName = restParm.RestCorrName;
 
         var select_c = " select certif.t_Series " + 
                        "   from dcertif_dbt certif, dobjatcor_dbt objatcor     " +
                        "  where certif.t_ConnAccount = :Account                " +
                        "    and certif.t_ConnAccountFIID = :Code_Currency      " +
                        "    and certif.t_Status = 32000                        " +
                        "    and Lpad(certif.t_FIID, 10, 0) = objatcor.t_object " +
                        "    and objatcor.t_ObjectType = 22                     " +
                        "    and objatcor.t_GroupID = 1                         ";

         var params_c = makeArray( SQLParam( "Account", TRecAcc.rec.Account ),
                                   SQLParam( "Code_Currency", TRecAcc.rec.Code_Currency ) );
 
         var rset_c = execSQLselect( select_c, params_c, TRUE );
  
         if( rset_c and rset_c.moveNext() )
      
           ReqClosAcc.Series = rset_c.Value(0);
   
           var select_cm = " select min(certif.t_NumberFirst), max(certif.t_NumberLast) " + 
                           "   from dcertif_dbt certif, dobjatcor_dbt objatcor     " +
                           "  where certif.t_ConnAccount = :Account                " +
                           "    and certif.t_ConnAccountFIID = :Code_Currency      " +
                           "    and certif.t_Status = 32000                        " +
                           "    and Lpad(certif.t_FIID, 10, 0) = objatcor.t_Object " +
                           "    and objatcor.t_ObjectType = 22                     " +
                           "    and objatcor.t_GroupID = 1                         " +
                           "    and certif.t_Series = :Series                      ";

           var params_cm = makeArray( SQLParam( "Account", TRecAcc.rec.Account ),
                                      SQLParam( "Code_Currency", TRecAcc.rec.Code_Currency ),
                                      SQLParam( "Series", ReqClosAcc.Series ) );

           var rset_cm = execSQLselect( select_cm, params_cm, TRUE );
           if( rset_cm and rset_cm.moveNext() )
             ReqClosAcc.NumberFirst = rset_cm.Value(0);
             ReqClosAcc.NumberLast = rset_cm.Value(1);
           end;
         end;

         stat = IfThenElse( ReqClosAcc.SaveChanges(), 0, 1 );
         if( StartOp == true )
           SetDialogFlag(0);
           CreateReqClosAOperation( ReqClosAcc.RequestID );
           ErrMsg = GetErrMsg();
           SetDialogFlag(1);
           if( ReqClosAcc.CloseDate != date(0,0,0) )
             TResult[idx] = "Счет закрыт по распоряжению банка от " + ReqClosAcc.Date;
             TAccRep[idx] = TAcc[idx];
           elif( ErrMsg )
             TResult[idx] = "Операция по закрытию счета прервана с сообщением: " + ErrMsg;
             TAccRep[idx] = TAcc[idx];
           else
             TResult[idx] = "Распоряжение банка от " + ReqClosAcc.Date + " о закрытии счета подготовлено к выполнению шага " + GetStepName( ReqClosAcc.RequestID ); 
             TAccRep[idx] = TAcc[idx];
           end;
         else
           TResult[idx] = "Распоряжение банка от " + ReqClosAcc.Date + " о закрытии счета подготовлено к выполнению шага " + GetStepName( ReqClosAcc.RequestID ); 
           TAccRep[idx] = TAcc[idx];
         end;
       end;

       idx = idx + 1;
     end;                                                         

     if( TAccRep.Size > 0 )
       PrintRepCloseAccOrder( TAccRep, Ground, TResult ); 
     end;

   end;
    
 end;
  
  return stat;
END;


MACRO ProcForClsAccum( Account, FIID, Dep, Branch, Client, Ground, Operation, StartOp )

  var stat   = 0;
  var RegVal = 0;
  var TAcc   = TArray();
  var TFIID  = TArray();
  var Choice = NOTHING;
  var idx = 0;
  var TRecAcc:TRecHandler  = TRecHandler("account.dbt", "bank.def");
  var reqopnac:TRecHandler = TRecHandler( "reqopnac.dbt", "bank.def" );
  var clsac:TBFile         = TBFile( "reqclsac.dbt", "R", 5, "reqclsac.dbt", "bank.def" );
  var Key0clsac:TBFile     = TBFile( "reqclsac.dbt", "R", 0, "reqclsac.dbt", "bank.def" );
  var RecClsAc:TRecHandler = TRecHandler( "reqclsac.dbt", "bank.def" );
  var ReqClsAccum;
  var FD_CorrAcc:ProfitFromAccAccum;
  var СчетДоходов:string;


  stat = GetRegValForOPENAC( "СРОК_ЗНС", V_INTEGER, RegVal );

  if( stat == 0 )    
    
    if( RegVal == 0 )
      msgbox( "В настройках операций открытия/закрытия счета не определен |"+
              "предельный срок предоставления заявлений на закрытие накопительного счета" );
      return 1;
    
    else

      if( FIID == -1 )
        stat = GetAccounts( Dep, Branch, -1, "L", Account, TAcc, TFIID, Client );
      else
        stat = GetAccounts( Dep, Branch, FIID, "L", Account, TAcc, TFIID, Client );
      end;

      if( TAcc.Size > 0 )
        
        while( idx < TAcc.Size )

        TRecAcc = GetAccount( 1, TAcc[idx], TFIID[idx] );

          if( ( Choice == NOTHING ) or ( ( Choice != NO_FOR_ALL ) and  ( Choice != YES_FOR_ALL ) ) )
            Choice = ShowMenu( "Накопительный счет " + TRecAcc.rec.NameAccount + " №"+ TRecAcc.rec.Account +" открыт " + TRecAcc.rec.Open_Date + ". Сформировать распоряжение на закрытие?" );
          end;

          if( ( Choice == YES ) or ( Choice == YES_FOR_ALL ) )

            clsac.rec.Account      = TAcc[idx];
            clsac.rec.AccountFIID  = TFIID[idx];
            
            if( not clsac.GetEQ() )

              ReqClsAccum = RsbReqCloseAccum();

              ReqClsAccum.SubKind     = 2;
              ReqClsAccum.Date        = {curdate};
              ReqClsAccum.InputDate   = {curdate};
              ReqClsAccum.AccountFIID = TFIID[idx];
              ReqClsAccum.Account     = TAcc[idx];
              ReqClsAccum.Ground      = Ground;
              ReqClsAccum.ClientID    = TRecAcc.rec.Client;
              ReqClsAccum.PersonName  = "";
              ReqClsAccum.Department  = {OperDprt};
              ReqClsAccum.Oper        = {Oper};
              ReqClsAccum.Kind_Operation = Operation;

              reqopnac = GetREQOPNAC( TAcc[idx], stat );
              
              if( stat == 0 )
                
                if( reqopnac.rec.ToBankProfit )
                  
                  ReqClsAccum.RestFIID = NATCUR;
          
                  if( TFIID[idx] == NATCUR )
                    FD_CorrAcc = ProfitFromAccAccum( NATCUR );
                    СчетДоходов = FD_CorrAcc.FindAndOpenSysAccount( "Доходы с накоп. счетов", 0, {curdate} );
                  else
                    FD_CorrAcc = ProfitFromAccAccum( TFIID[idx] );
                    СчетДоходов = FD_CorrAcc.FindAndOpenSysAccount( "Доходы с накоп. счетов", 0, {curdate} );
                  end;

                  ReqClsAccum.ReceiverAccount =  СчетДоходов;


                else
                  
                  ReqClsAccum.ReceiverFIID         = reqopnac.rec.ReceiverFIID;
                  ReqClsAccum.ReceiverCodeKind     = reqopnac.rec.ReceiverCodeKind;
                  ReqClsAccum.ReceiverCode         = reqopnac.rec.ReceiverCode;
                  ReqClsAccum.ReceiverINN          = reqopnac.rec.ReceiverINN;
                  ReqClsAccum.ReceiverName         = reqopnac.rec.ReceiverName;
                  ReqClsAccum.ReceiverAccount      = reqopnac.rec.ReceiverAccount;
                  ReqClsAccum.ReceiverBankCodeKind = reqopnac.rec.ReceiverBankCodeKind;
                  ReqClsAccum.ReceiverBankCode     = reqopnac.rec.ReceiverBankCode;
                  ReqClsAccum.ReceiverBankName     = reqopnac.rec.ReceiverBankName;
                  ReqClsAccum.ReceiverBankCorrAcc  = reqopnac.rec.ReceiverBankCorrAcc;
                  ReqClsAccum.ReceiverCorrCodeKind = reqopnac.rec.ReceiverCorrCodeKind;
                  ReqClsAccum.ReceiverCorrCode     = reqopnac.rec.ReceiverCorrCode;
                  ReqClsAccum.ReceiverCorrName     = reqopnac.rec.ReceiverCorrName;

                end;

              end;

              if( StartOp == true )
                ReqClsAccum.LaunchOper = true;
              end;
              stat = IfThenElse( ReqClsAccum.SaveChanges(), 0, 1 );

              if( stat != 0 )
                msgbox( "Распоряжение на закрытие счета " + TRecAcc.rec.Account + " не сформировано" );
              else

                Key0clsac.rec.RequestID = ReqClsAccum.RequestID;
                Key0clsac.GetEQ();
                Copy( RecClsAc, Key0clsac );

                PrintDirectionClStAcc( RecClsAc );
              end;

            end;

          elif( Choice == NO_FOR_ALL )
            return 1;
          end;

          idx =idx + 1;

        end;

      else

       msgbox( "Накопительных счетов к закрытию по сроку нет" );
       return 1;

      end;

    end;

  end;

  return stat;
END;




MACRO ProcGenReqClsAc( MapVal :PSRepMap )
  
  var stat = 0;

  var Account:string     = MapVal.Value( "Account" );
  var FIID:integer       = MapVal.Value( "FIID"    );
  var Department:integer = MapVal.Value( "Dep"     );
  var Branch:integer     = MapVal.Value( "Branch"  );
  var Accum:integer      = MapVal.Value( "Accum"   );
  var KindOrder:integer  = MapVal.Value( "KindOrder" );
  var CloseDate:date     = MapVal.Value( "CloseDate" );
  var Client:integer     = MapVal.Value( "Client"    );
  var Ground:string      = MapVal.Value( "Ground"    );
  var Operation:integer  = MapVal.Value( "Operation" );
  var StartOp:string     = MapVal.Value( "StartOp"   );

  if( Accum > 0 )
    stat = ProcForClsAccum( Account, FIID, Department, Branch, Client, Ground, Operation, StartOp );
  else
    var restParm:RestPanelParm = RestPanelParm();
    restParm.RestCodeKind      = MapVal.Value( "RestCodeKind"     );
    restParm.RestCode          = MapVal.Value( "RestCode"         );
    restParm.RestName          = MapVal.Value( "RestName"         );
    restParm.RestAccount       = MapVal.Value( "RestAccount"      );
    restParm.RestINN           = MapVal.Value( "RestINN"          );
    restParm.RestBankCodeKind  = MapVal.Value( "RestBankCodeKind" );
    restParm.RestBankCode      = MapVal.Value( "RestBankCode"     );
    restParm.RestBankName      = MapVal.Value( "RestBankName"     );
    restParm.RestBankCorrAcc   = MapVal.Value( "RestBankCorrAcc"  );
    restParm.RestCorrCodeKind  = MapVal.Value( "RestCorrCodeKind" );
    restParm.RestCorrCode      = MapVal.Value( "RestCorrCode"     );
    restParm.RestCorrName      = MapVal.Value( "RestCorrName"     );

    stat = ProcForClosAc( Account, FIID, Department, Branch, KindOrder, CloseDate, Client, Ground, Operation, StartOp, restParm );
  end;

  return stat;
END;