/*
$Name:           pr_adcla.mac
$Module:         РКО
$Description:    Печать уведомления о закрытии счета
*/
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : pr_adcla.mac
  Created     : 23.02.2007
  Programmer  : Осмаковский Е.В.
  Description : Печать уведомления о закрытии счета

└───────────────────────────────────────────────────────────────────────────*/
import Календарь, prreqbuf, reqinter;

PRIVATE
macro DrawReport(AdressDep,
                 DateOpenAcc,
                 DateClose,
                 NameClient,
                 NameFirstFace, 
                 PostFirstFace,
                 NameBranch,
                 РАСПОРЯЖЕНИЕ_ДОЛЖ,
                 РАСПОРЯЖЕНИЕ_ФИО,
                 СРОК_ЗС,
                 Rest,
                 StrRest,
                 Account,
                 Code_Currency,
                 OrderKind)
     var text;
     var ZeroMoney: money = Money(0);
     
     text = "\tУведомляем Вас, что согласно ст.859 п.1." + IfThenElse(OrderKind == 1, "1", "2") + " части второй Гражданского Кодекса РФ счет, " + 
            "принадлежащий Вашей организации, № " + Account +" будет закрыт " + string( DateClose:m );
     if (OrderKind == 1)
       text = text + ", если до указанной даты по нему не будет выполнено ни одной операции.\n\tСчет  № " + Account +
            " был открыт " + string(DateOpenAcc:m) + " " + NameBranch + ", и  в течение последних " +
              СРОК_ЗС + " месяцев остаток денежных средств на нем равен " + Rest + " " + StrRest;
     else
       if (OrderKind == 2)
        text = text + " на основании п.5.2 ст.7 федерального закона № 115-ФЗ \"О противодействии легализации (отмыванию) доходов, полученных преступным путем, и финансированию терроризма\"";
       end;
     end;
     
     if (Rest != ZeroMoney)
       text = text + "\n\tДля получения остатка денежных средств или перевода остатка на другой счет обратитесь с заявлением в обслуживающее отделение банка в срок до " + string( DateClose:m ) + " По истечении указанного срока остаток будет перечислен на специальный счет в Банке России. Для его возврата Вам необходимо будет обратиться в обслуживающее отделение банка с требованием о возврате денежных средств. Возврат остатка в этом случае станет возможным только после перевода средств Банком России в обслуживающий банк."
     end;

[                                                                                             ];
[ ##########################################################################                  ]({Name_Bank}:w);
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[ #############################################################         ##################### ](AdressDep,{curdate}:f:m:r);
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[                                  ########################################################## ](PostFirstFace:r:w);
[                                  ########################################################## ](NameClient:r:w);
[                                                                                             ];
[                                         Уведомление                                         ];
[                                                                                             ];
[                                                                                             ];
[ ########################################################################################### ]( ("Уважаемый, " + NameFirstFace + "!"):c );
[ ########################################################################################### ](text:w);
[                                                                                             ];
[                                                                                             ];
[                                                                                             ];
[ ######################## _________________ / ############################# /                  ](РАСПОРЯЖЕНИЕ_ДОЛЖ , РАСПОРЯЖЕНИЕ_ФИО:c);
[                                                                                             ];

     var Note_Date:date;
     Note_Date = date(ReadNoteForAccount( Account, Code_Currency, 24/*ACC_NOTE_KIND_DATE_CLOSED_ACC*/));
     if ( Note_Date == date(0,0,0))
       Note_Date = {curdate};
       InsertNoteForAccount( Account, Code_Currency, 24, Note_Date );
     end;

end;


macro PrintDocument(ncopy:integer):bool

   var AdressDep,
       DateOpenAcc,
       DateClose,
       NameClient,
       NameFirstFace, 
       PostFirstFace,
       NameBranch,
       РАСПОРЯЖЕНИЕ_ДОЛЖ,
       РАСПОРЯЖЕНИЕ_ФИО,
       СРОК_ЗС,
       Rest,
       StrRest,
       SubKind,
       Client,
       Branch,
       dd, mm, yyyy,
       TR_account:TRecHandler = TRecHandler( "account.dbt"  , "bank.def" ),
       TR_party:TRecHandler = TRecHandler( "party.dbt"  , "bank.def" ),
       err;

  if ( pr_reqclosa.rec.SubKind != 2 )
    MsgBox("Уведомление о закрытии счета формируется только для распоряжений банка");
    return false;
  end;

   AdressDep = GetRealAdressDep({OperDprtNode});
   TR_account = GetAccount( 1 , pr_reqclosa.rec.Account, pr_reqclosa.rec.Code_Currency);
   DateOpenAcc = TR_account.rec.Open_Date;
   Client = TR_account.rec.Client;
   Branch = TR_account.rec.Branch;
   
   if(GetPartyRec(Client,TR_party))
     NameClient = TR_party.rec.Name;
   else
     NameClient = "";
   end;

   NameBranch = GetNameBranch( Branch );

   if( not GetPostAndNameFirstFace( Client, NameFirstFace, PostFirstFace ))
      NameFirstFace = "  ";
      PostFirstFace = "  "
   end;

   DateClose = DateAfterCalenMonths ({curdate}, 2);

  if( GetRegValForOPENAC( "РАСПОРЯЖЕНИЕ_ДОЛЖ", V_STRING, РАСПОРЯЖЕНИЕ_ДОЛЖ ) )
     РАСПОРЯЖЕНИЕ_ДОЛЖ = "";
  end;

  if( GetRegValForOPENAC( "РАСПОРЯЖЕНИЕ_ФИО", V_STRING, РАСПОРЯЖЕНИЕ_ФИО ) )
     РАСПОРЯЖЕНИЕ_ФИО = "";
  end;

  if( GetRegValForOPENAC( "СРОК_ЗС", V_INTEGER, СРОК_ЗС ) )
     СРОК_ЗС = 0;
  end;

  //Остаток на счете
  if( pr_reqclosa.rec.Code_Currency == NATCUR )
     Rest = RestA(pr_reqclosa.rec.Account , {curdate});
     StrRest = string(Rest:a) +" ( " + RubToStrAlt(Rest) + " ).";
  else
     Rest = RestAC(pr_reqclosa.rec.Account, pr_reqclosa.rec.Code_Currency, {curdate});
     StrRest = string(Rest:a) +" ( " + CurToStrAlt(Rest, NULL, NULL, GetISOCode(pr_reqclosa.rec.Code_Currency)) + " ).";
  end;

  
  while( ncopy )
    DrawReport(  AdressDep,
                 DateOpenAcc,
                 pr_reqclosa.rec.AccCloseDate,
                 NameClient,
                 NameFirstFace, 
                 PostFirstFace,
                 NameBranch,
                 РАСПОРЯЖЕНИЕ_ДОЛЖ,
                 РАСПОРЯЖЕНИЕ_ФИО,
                 СРОК_ЗС,
                 Rest,
                 StrRest,
                 pr_reqclosa.rec.Account,
                 pr_reqclosa.rec.Code_Currency,
                 pr_reqclosa.rec.OrderKind);
    ncopy = ncopy - 1;
  end;

  return true;

end;

macro PrintAdviceClAcc(Account , Code_Currency):bool

   var AdressDep,
       DateOpenAcc,
       DateClose,
       NameClient,
       NameFirstFace, 
       PostFirstFace,
       NameBranch,
       РАСПОРЯЖЕНИЕ_ДОЛЖ,
       РАСПОРЯЖЕНИЕ_ФИО,
       СРОК_ЗС,
       Rest,
       StrRest,

       Client,
       Branch,
       dd, mm, yyyy,
       TR_account:TRecHandler = TRecHandler( "account.dbt"  , "bank.def" ),
       TR_party:TRecHandler = TRecHandler( "party.dbt"  , "bank.def" ),
       err;

   AdressDep = GetRealAdressDep({OperDprtNode});
   TR_account = GetAccount( 1 , Account, Code_Currency);
   DateOpenAcc = TR_account.rec.Open_Date;
   Client = TR_account.rec.Client;
   Branch = TR_account.rec.Branch;
   
   if(GetPartyRec(Client,TR_party))
     NameClient = TR_party.rec.Name;
   else
     NameClient = "";
   end;

   NameBranch = GetNameBranch( Branch );

   if( not GetPostAndNameFirstFace( Client, NameFirstFace, PostFirstFace ))
      NameFirstFace = "  ";
      PostFirstFace = "  "
   end;

   DateClose = DateAfterCalenMonths ({curdate}, 2);

  if( GetRegValForOPENAC( "РАСПОРЯЖЕНИЕ_ДОЛЖ", V_STRING, РАСПОРЯЖЕНИЕ_ДОЛЖ ) )
     РАСПОРЯЖЕНИЕ_ДОЛЖ = "";
  end;

  if( GetRegValForOPENAC( "РАСПОРЯЖЕНИЕ_ФИО", V_STRING, РАСПОРЯЖЕНИЕ_ФИО ) )
     РАСПОРЯЖЕНИЕ_ФИО = "";
  end;

  if( GetRegValForOPENAC( "СРОК_ЗС", V_INTEGER, СРОК_ЗС ) )
     СРОК_ЗС = 0;
  end;

  //Остаток на счете
  if( Code_Currency == NATCUR )
     Rest = RestA(Account , {curdate});
     StrRest = string(Rest:a) +" ( " + RubToStrAlt(Rest) + " ).";
  else
     Rest = RestAC(Account, Code_Currency, {curdate});
     StrRest = string(Rest:a) +" ( " + CurToStrAlt(Rest, NULL, NULL, GetISOCode(Code_Currency)) + " ).";
  end;

      DrawReport(AdressDep,
                 DateOpenAcc,
                 DateClose,
                 NameClient,
                 NameFirstFace, 
                 PostFirstFace,
                 NameBranch,
                 РАСПОРЯЖЕНИЕ_ДОЛЖ,
                 РАСПОРЯЖЕНИЕ_ФИО,
                 СРОК_ЗС,
                 Rest,
                 StrRest,
                 Account,
                 Code_Currency,
                 2);
   return true;
end;
