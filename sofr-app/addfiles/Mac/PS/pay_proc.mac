import  FIInter, globals;

/******************************************************************************
Служебная процедура, расширяет значение num до строки длиной len,
свободные позиции слева заполняет _нулями_ (а не пробелами)
******************************************************************************/
macro LZ( num, len )
     var str1, len1;
     str1 = trim( string( num ) );
     len1 = strlen( str1 );
     if ( len1 >= len ) return str1;
     else   return  mkstr("0", len-len1 ) + str1;
     end;
end;


/******************************************************************************
Процедура генерации номера лицевого счета, привязанного к заданному
******************************************************************************/
macro MakeAccNumber_Link(
   Account,     /* Cчет, на основании которого происходит генерация */
   Bal,         /* Номер балансового счета */
   FIID         /* Идентификатор (не код !) финансового инструмента */
)

     var CodeFI, res_string;
     record finstr(fininstr);

     if (valtype (FIID) != V_INTEGER )
        CodeFI= LZ (FIID, 3);
     else
          CodeFI = LZ (ПолучитьКодФинИнДляСчета ( FIID ),3);
     end;

        res_string =    string (Bal) +
                        CodeFI + "0" + SubStr(Account,10,11);

        return GetKey(res_string,{MFO_Bank});

end;

/* -----------------------------------------------------------------------------

Класс для поверки ключа счета

------------------------------------------------------------------------------ */

CLASS VAccountKeyChecker

   PRIVATE VAR
      List = TArray, obchaptr = TBfile( "obchaptr.dbt" );

   PRIVATE MACRO key_mode( Chapter )
      if( List[Chapter] == NULL )
         obchaptr.rec.Chapter = Chapter;
         if( obchaptr.GetEQ ) 
            List[Chapter] = obchaptr.rec.AccountKey; 
         else
            msgbox( "Главы счетов № " + Chapter + " не существует" );
         end;
      end;
      return List[Chapter];
   END;

   PRIVATE MACRO without_control( Account )
      return Account;
   END;

   PRIVATE MACRO with_control( Account )
      var needAccount = GetKey( Account, {MFO_Bank} );
      var Text;

      if( needAccount != Account )
         Text = "В номере счета " + Account + " неверное значение ключа.|" +
                "Должно быть " + needAccount + "|" + "Изменить ?";
         if( GetTrue( True, Text ) )
            Account = needAccount;
         end;
      end;
      return Account;
   END;

   PRIVATE MACRO necessarily( Account )
      var needAccount = GetKey( Account, {MFO_Bank} );
      var Text;

      if( needAccount != Account )
         Text = "В номере счета " + Account + " неверное значение ключа.|" +
                "Будет вставлен счет " + needAccount ;
         msgbox( Text );
      end;
      return needAccount;
   END;

   MACRO Do( Chapter, Account );
      var mode = key_mode( Chapter );

      if( mode != NULL )
         if  ( mode == 0 ) /* без контроля */
            return without_control( Account );
         elif( mode == 1 ) /* с контролем  */
            return with_control( Account );
         elif( mode == 2 ) /* обязательное */
            return necessarily( Account );
         end;
      end;
   END;

END;

