
/*
$Name:             esid006_10.mac
$Module:           РКО юридических лиц
$Description:      Операция 4004. Шаг <Обработка отвергнутого>
*/

import pm_tools, pm_chkrst,"pm_answerret.mac";

var PaymentObj: RsbPayment,
    Direct: integer;

const ActionStringContext:string = "Action_esid006_10";
    
private macro PM_ChooseAction():integer

  Array Text;
  Array Buttons;
  var SelectAction: integer;
  var DialogFlag = TSetDialogFlag(1);

  Text(0) = "Какое действие следует произвести с отвергнутым документом?";                                                       

  if ( Direct == OPR_CL_ST_DIR_IN)  
    Buttons(0) = " Исполнить ";
    Buttons(1) = " Аннулировать ";
    Buttons(2) = " Отменить "; 
  else 
    Buttons(0) = " Перезапустить ";
    Buttons(1) = " Аннулировать ";
    Buttons(2) = " Отменить "; 
  end;

  SelectAction = ConfWin(Text, Buttons, 0) ;

  return SelectAction;
end;

private macro PM_GetDenialGround():string

  var Ground:string = "";
  var DialogFlag = TSetDialogFlag(1);
  if( SetReasonReject( Ground ) )
    return "";
  end;    

  return Ground;
end;
   
//-----------------------------------------------------------------------------
// Выполнение шага
//-----------------------------------------------------------------------------
MACRO ExecuteStep()
  if( IsRevokedByInitiator(PaymentObj) )
    return RevokeEsidOrder(PaymentObj);
  end;

  Array Text;
  Array Buttons;

  Direct     = GetOprStatus(OPR_CLAIM_DIRECT); //Направление
  var Action:integer = GetCachedVar(STEP_PARM_EXEC_REJECTED, "PM_ChooseAction");
  
  // Нам нужно значение Action в PostStep-е, но GetCachedVar сохраняет
  // только в массовом режиме, поэтому сохраняем значение вручную

  if( not IsOprMultiExec() )
    SetCachedVar(ActionStringContext, Action);
  end;
                            
  if (Action == REJESID_PROCKIND_EXECUTE)//Перезапустить/Исполнить

    if( УстановитьСтатусыПлатежа( OPR_CLAIM_STATE, OPR_CL_ST_OPEN ) ) 
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
  
    if ( Direct == OPR_CL_ST_DIR_IN)  
      if( УстановитьСтатусыПлатежа( OPR_CLAIM_DO, OPR_CL_ST_EXEC_CL ) ) 
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
    else
      if( УстановитьСтатусыПлатежа( OPR_CLAIM_DO, OPR_CL_ST_PREP ) ) 
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
      PaymentObj.PaymStatus = PM_READIED;
      PaymentObj.PropStatus = PM_PROP_PREPARING;
      if( UnvalidPaymentMessage( PaymentObj.PaymentID ) )
        msgbox( "Ошибка при смене статуса сообщения по платежу" );
        return 1;
      end;
    end;    
    
  elif(Action == REJESID_PROCKIND_REFUSE) //Аннулировать
    
    var DenialGround:string = GetCachedVar( REASON_REJECT_DESCRIPTION, "PM_GetDenialGround" );     

    if(ValType(DenialGround) != V_STRING)    
      DenialGround = "";
    end;

    if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, DenialGround ) != 0 )
      msgbox( "Ошибка при вставке примечания платежа" );
      return 1;
    end;

    if( УстановитьСтатусыПлатежа( OPR_CLAIM_STATE, OPR_CL_ST_CLOSE ) ) 
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
    
    PaymentObj.PropStatus = PM_PROP_CLOSED;    
    PaymentObj.PayerBankEnterDate = {curdate};
    
//  elif(Action == REJESID_PROCKIND_FINISH )
  else
    return 1;
  end;

  return 0;
  
END;

private macro PostTrn(IsExecStep : bool)
  if( IsExecStep and not IsOprMultiExec() )
    PM_SetRejectProcessKind(PM_REJECT_PROCKIND_NONE);
  end;                                                             
end;

macro PostStep( message,     /* данный параметр может принимать следующие       */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                FirstDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                Num_Step,    /* Номер шага операции (из настроек)               */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep,    /* вид шага операции                               */
                ID_Step )    /* внутренний идентификатор шага операции          */
  var stat = 0;

  var Action:integer;
  
  // В единичном режиме нужно получить и удалить значение переменной в любом случае
  if( not IsOprMultiExec() )    
    Action = GetCachedVar(ActionStringContext);      
    EraseCachedVar(ActionStringContext);
  else
    Action = GetCachedVar(STEP_PARM_EXEC_REJECTED);
  end;

  Direct = GetOprStatus(OPR_CLAIM_DIRECT);
  if( ( errTrn == 0 ) and ( message == 1 ) )// на выполнении шага
    if(Direct == OPR_CL_ST_DIR_IN) //входящие      
      if(Action == REJESID_PROCKIND_REFUSE) // Если действие - аннулировать
        var Queries : string = "InfoCode:7";
        var Narrative : string = PaymentObj.Notes.ReadNote( PM_NOTEKIND_DENIALGROUND, {curdate} );
        var ind = Index(Narrative,";");
        if (ind) 
          var NumQueries: string = Trim(SubStr(Narrative,1,ind-1));
          if (StrIsNumber(NumQueries))
            Queries = "InfoCode:" + NumQueries;
          end;
 
          Narrative = Trim(SubStr(Narrative,ind+1));
        end;
        Narrative = "Уведомляем об аннулировании " + 
                    IfThenElse( PaymentObj.DocKind == DLDOC_IN_PMCOL, 
                    "инкассового поручения", "платежного требования" ) + ". " + Narrative;
     
        CreateED274(PaymentObj.PaymentID, Queries, Narrative, ID_Oper, ID_Step);     
      end;
    end;
  end;
  PostTrn(message == 1);
  return stat;
end;
