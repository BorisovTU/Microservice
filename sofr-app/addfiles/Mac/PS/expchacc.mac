/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : exprq.mac                                          */
/*                                                                      */
/*  Описание       : Формирование файла с сообщением в ГНИ.             */
/*                                                                      */
/*  Программист    : Дроздов И.Е.                                       */
/*                                                                      */
/*  Создан         : 18.10.04                                           */
/*                                                                      */
/************************************************************************/

IMPORT globals, PTInter, CTInter, OprInter, PSInter, likepy;
import rslx, prpm, gnd120p, SFInter;

var accold1   :TRecHandler = TRecHandler("account.dbt",    "bank.def");
var accold2   :TRecHandler = TRecHandler("account.dbt",    "bank.def");
var accnew1   :TRecHandler = TRecHandler("account.dbt",    "bank.def");
var accnew2   :TRecHandler = TRecHandler("account.dbt",    "bank.def");
var ptClient  :TRecHandler = TRecHandler("party.dbt",      "bank.def");
var prClient  :TRecHandler = TRecHandler("objrgdoc.dbt",   "bank.def");
var person    :TRecHandler = TRecHandler("persn.dbt",      "bank.def");
var officer   :TRecHandler = TRecHandler("officer.dbt",    "bank.def");

FILE   acc1("account");
FILE   cert("certif") key 2;
FILE   reg(objrgdoc);
FILE   adr (adress);

FILE fileexport () txt write;

var ExportFileName:string;
var TxtDir:string;
var refer = "";

PRIVATE VAR ИдФайл:string, ТипИнф:string, ИдПол:string,
            КолДок:string, ВерсФорм:string, КИО1:string,КИО2:string,
            ИННКО:string, КППКО:string, БИК:string,
            НаимКО:string, АдрКО:string, ТелОтпр:string,
            ДолжнОтпр:string, ФИООтпр:string, ИдДок:string,
            НомИз:string, ДатаПодИз:string, ФамДЛ:string,
            ДатаПодписи:string, ИНННП1:string, ИНННП2:string,
            КППНП1:string, КППНП2:string, ОГРН1:string, 
            ОГРН2:string, КодНОНП1:string, КодНОНП2:string,
            НомСч1:string, НомСч2:string, НомСчИз1:string,
            НомСчИз2:string, ДатаСчИз1:string, ДатаСчИз2:string;

/*-----------------------------------------------*/
/* Класс для добавления SQL-фильтра к таблице    */
/* и последующего гарантированного его удаления  */
/*-----------------------------------------------*/
PRIVATE CLASS TAddFilter( bfile:TBFile, filterStr:string )
  private var m_bfile:TBFile = bfile;
  m_bfile.AddFilter( filterStr );
  MACRO destructor()
    m_bfile.DropFilter();
  END;
END;

MACRO ОткрытьФайл()
  var d,m,y,;
  var BINN,BKPP;
  DateSplit({curdate},d,m,y);
  splitINN_KPP(GetPartyINN({OurBank},1), BINN, BKPP);
  GenerateReference(84, refer);
  //полное имя файла с директорией
  ExportFileName = string(TxtDir,"\\SMN",BINN,"__",BKPP,y:o:4,m:o:2,d:o:2,refer,".txt");
  Open(fileexport, ExportFileName);
END;

MACRO ЗакрытьФайл()
  Close(fileexport);
END;

MACRO ЗаполнитьФайл()
//служебная часть
  //общие сведения служебной части 
  Insert(fileexport,"ИдФайл: "+ИдФайл);
  Insert(fileexport,"ТипИнф: "+ТипИнф);
  Insert(fileexport,"ИдПол: "+ИдПол);
  Insert(fileexport,"КолДок: "+КолДок);
  Insert(fileexport,"ВерсФорм: "+ВерсФорм);
  Insert(fileexport,"###EOL");
  //сведения об отправителе
  Insert(fileexport,"ИННКО: "+ИННКО);
  Insert(fileexport,"КППКО: "+КППКО);
  Insert(fileexport,"БИК: "+БИК);
  Insert(fileexport,"НаимКО: "+НаимКО);
  Insert(fileexport,"АдрКО: "+АдрКО);
  Insert(fileexport,"ТелОтпр: "+ТелОтпр);
  Insert(fileexport,"ДолжнОтпр: "+ДолжнОтпр);
  Insert(fileexport,"ФИООтпр: "+ФИООтпр);
  Insert(fileexport,"###EOL");
  Insert(fileexport,"@@@EOL");
//информационная часть
  //общие сведения извещения
  Insert(fileexport,"ИдДок: "+ИдДок);
  Insert(fileexport,"НомИз: "+НомИз);
  Insert(fileexport,"ДатаПодИз: "+ДатаПодИз);
  Insert(fileexport,"ФамДЛ: "+ФамДЛ);
  Insert(fileexport,"ДатаПодписи: "+ДатаПодписи);
  Insert(fileexport,"###EOL: ");
  //сведения об изменении счета
  if(strlen(accold1.rec.Account))
    Insert(fileexport,"ИНННП: "+ИНННП1);
    Insert(fileexport,"КППНП: "+КППНП1);
    Insert(fileexport,"ОГРН: "+ОГРН1);
    Insert(fileexport,"КИО: "+КИО1);
    Insert(fileexport,"КодНОНП: "+КодНОНП1);
    Insert(fileexport,"НомСч: "+НомСч1);
    Insert(fileexport,"НомСчИз: "+НомСчИз1);
    Insert(fileexport,"ДатаСчИз: "+ДатаСчИз1);
    Insert(fileexport,"###EOL: ");
  end;
  if(strlen(accold2.rec.Account))
    Insert(fileexport,"ИНННП: "+ИНННП2);
    Insert(fileexport,"КППНП: "+КППНП2);
    Insert(fileexport,"ОГРН: "+ОГРН2);
    Insert(fileexport,"КИО: "+КИО2);
    Insert(fileexport,"КодНОНП: "+КодНОНП2);
    Insert(fileexport,"НомСч: "+НомСч2);
    Insert(fileexport,"НомСчИз: "+НомСчИз2);
    Insert(fileexport,"ДатаСчИз: "+ДатаСчИз2);
    Insert(fileexport,"###EOL: ");
  end;
  Insert(fileexport,"@@@EOL");
  Insert(fileexport,"===EOL");
END;


MACRO ПолучитьНомерИзвещения(Code_Currency,Chapter,Account)
   VAR ercode, stat = false;
    cert.FIID  = AccNoteFIID;
    cert.ConnAccount  = Account;
    cert.Series = cert.NumberFirst = "";

    stat = GetGE(cert);
    if( (not stat) )
      ercode = status();
      if( (ercode != 4) and (ercode!=9) )
        MsgBox("Ошибка при определении номера извещения");
        cert.Series = cert.NumberFirst = "";
        return stat;
      end;
    end;

    if((not stat)or(cert.FIID != AccNoteFIID)or(cert.ConnAccount != Account))

    acc1.Chapter = Chapter;
    acc1.Code_Currency = Code_Currency;
    acc1.Account = Account;
    if(not GetEQ(acc1))
      cert.Series = cert.NumberFirst = "";
      ercode = status();
      if( (ercode != 4) and (ercode!=9) )
        MsgBox("Ошибка при определении счета");
        return stat;
      end;
    else
      if(SetAccNoteNumber(Account, cert.Series, cert.NumberFirst ) == 0)  
        stat = true;
      end;
    end;
    end;
  return stat;
END;

macro strDateForSQL(dt:date):string
  return "TO_DATE('" + StrSubst(string(dt:o), ".", "-") + "', 'DD-MM-YYYY')";
end;

MACRO ПолучитьРеквизитыРегистрации(ID:integer, DK:integer, reg)
  VAR ercode;
  VAR OK:bool;
  var reg1:TBFile = TBFile("objrgdoc.dbt","R",0,"objrgdoc.dbt","bank.def");
  ClearRecord(reg);
  ClearRecord(reg1);
  reg1.rec.RegPartyID   = ID;    
  reg1.rec.IsClosed = "";
  reg1.rec.RegPartyKind = 7;
  var AddFltr:TAddFilter = TAddFilter(reg1,string("t_StartDate < ",strDateForSQL({curdate})," and t_FinishDate > ",strDateForSQL({curdate}) ) );
  if(reg1.GetGE())
        Copy(reg,reg1);
        return true;
      end;
END;

MACRO НайтиАдресс(PARTYID) //ADRESS
  ClearRecord(adr);
  adr.PartyID = PARTYID;
  adr.Type = 1; //ищем юридический адресс
  АдрКО = "";
  if(GetEQ(adr))
    АдрКО = string(adr.Country,",",adr.PostIndex,",",adr.Region,",,",adr.Place,",,",adr.Street,",",adr.House,",",adr.NumCorps,",",adr.Flat);
  end;
END;

MACRO ЗаполнитьДанные()
  var d,mo,y,h,mi,s;
  var BINN,BKPP;
  RECORD pt (party);
  DateSplit({curdate},d,mo,y);
  splitINN_KPP(GetPartyINN({OurBank},1), BINN, BKPP);
  TimeSplit(Time(),h,mi,s);
  ИдФайл = string(BINN,"**",BKPP,y:o:4,mo:o:2,d:o:2,h:o:2,mi:o:2,s:o:2,refer);
  ТипИнф = "ИЗВЕЩЕНИЕИЗМСЧЕТ";
  ПолучитьСубъекта({OurBank},pt);
  ПолучитьРеквизитыРегистрации({OurBank},4, reg);
  ИдПол = ПолучитьКодСубъекта(reg.PtRegParty,28);
  КолДок = "1";
  ВерсФорм = "2.01";
  splitINN_KPP(GetPartyINN({OurBank},1), ИННКО, КППКО);
  БИК = {MFO_Bank};
  НаимКО = {Name_Bank};
  ПолучитьСубъекта({OurBank},pt);
  НайтиАдресс({OurBank});
  ТелОтпр = officer.rec.PhoneNumber;
  ДолжнОтпр = officer.rec.Post;
  ФИООтпр = string(person.rec.Name1," ",person.rec.Name2," ",person.rec.Name3);
  ИдДок = string(BINN,"**",BKPP,y:o:4,"000001");
  if(strlen(accold1.rec.Account))                                  
    ПолучитьНомерИзвещения(accold1.rec.Code_Currency,1,accold1.rec.Account);
    НомИз = cert.NumberFirst;
  elif(strlen(accold2.rec.Account))                                  
    ПолучитьНомерИзвещения(accold2.rec.Code_Currency,1,accold2.rec.Account);
    НомИз = cert.NumberFirst;
  elif(strlen(accnew1.rec.Account))                                  
    ПолучитьНомерИзвещения(accnew1.rec.Code_Currency,1,accnew1.rec.Account);
    НомИз = cert.NumberFirst;
  elif(strlen(accnew2.rec.Account))                                  
    ПолучитьНомерИзвещения(accnew2.rec.Code_Currency,1,accnew2.rec.Account);
    НомИз = cert.NumberFirst;
  end;
  ДатаПодИз = string(d:o:2,".",mo:o:2,".",y:o:4);
  ФамДЛ = string(person.rec.Name1);
  ДатаПодписи = string(d:o:2,".",mo:o:2,".",y:o:4);
  ПолучитьРеквизитыРегистрации(ptClient.rec.PartyID,14, reg);
  if(strlen(accold1.rec.Account))
    splitINN_KPP(GetPartyINN(ptClient.rec.PartyID,1), ИНННП1, КППНП1);
    ОГРН1 = ПолучитьКодСубъекта(ptClient.rec.PartyID,PTCK_OGRN);
    КИО1 = "";
    КодНОНП1 = ПолучитьКодСубъекта(reg.PtRegParty,PTCK_MNS);
    НомСч1 = accold1.rec.Account;
    НомСчИз1 = accnew1.rec.Account;
    ДатаСчИз1 = string(d:o:2,".",mo:o:2,".",y:o:4);         
  end;
  if(strlen(accold2.rec.Account))
    splitINN_KPP(GetPartyINN(ptClient.rec.PartyID,1), ИНННП2, КППНП2);
    ОГРН2 = ПолучитьКодСубъекта(ptClient.rec.PartyID,PTCK_OGRN);
    КИО2 = "";
    КодНОНП2 = ПолучитьКодСубъекта(reg.PtRegParty,PTCK_MNS);
    НомСч2 = accold2.rec.Account;
    НомСчИз2 = accnew2.rec.Account;
    ДатаСчИз2 = string(d:o:2,".",mo:o:2,".",y:o:4);        
  end;
END;

MACRO PrintChangeAcc()
  var errcode;
  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, TxtDir, errcode);
  ОткрытьФайл();
[┌────────────────────────────────────────────────────────────────────────────┐
 │  ИМЯ ФАЙЛА                                                                 │
 ├────────────────────────────────────────────────────────────────────────────┤
 │ #######################################################################    │
 └────────────────────────────────────────────────────────────────────────────┘
](ExportFileName);
  ЗаполнитьДанные();
  ЗаполнитьФайл();
  ЗакрытьФайл();

END;