/*

 $Name:             openinga.mac
 $Module:           РКО
 $Description:      Макрос шага "Открытие счета"

*/

/* -------------------------------------------------------------------- */
/*         Автоматизированная банковская система RS-Bank v6.0           */
/*                 Copyright (c) R-Style Software Lab                   */
/*                                                                      */
/*  Имя файла  : openinga.mac                                           */
/*                                                                      */
/*                                                                      */
/*  Программист: Локтюшин В.Ю.                                          */
/*  Переписан  : 19.01.2007                                             */
/*  Описание   : Операция - 3030 - "Открытие лицевого счета"            */
/*               Шаг      - 10   - "Открытие счета"                     */
/*                                                                      */
/* -------------------------------------------------------------------- */

import PSInter, bcall, InsCarryDoc, OprInter, PaymInter, PTInter, pmprops, reqinter;

var ReqOpenAccObj:RsbReqOpenAcc;

RECORD req( reqopena );
var TB_ReqOpenAcc = TBFile("reqopena.dbt",  "R", 0, "reqopena.dbt",  "bank.def");

private const RegPath_Client    = "PS\\REQOPENACC\\Счета клиентов";

private const KindDocOrder = 2;

private var ID_Link;
private var IsInsertAccount = 0;

RECORD RecordAcc    ( account ); 
RECORD RecMainAcc   ( account ); 
RECORD ab           ( accblnc );

PRIVATE CONST CHOICE_CONTINUE = 0; // "Продолжить"
PRIVATE CONST CHOICE_BREAK    = 1; // "Прервать"

var accbuf  :TRecHandler = TRecHandler( "account.dbt"  , "bank.def" );
var ReqCloseAccum:RsbReqCloseAccum;
var reqopnac:TRecHandler = TRecHandler( "reqopnac.dbt" , "bank.def" );


// ----------------------------------------------------------------------------
// Обертка для вызова вертикального меню
// ----------------------------------------------------------------------------
PRIVATE MACRO ShowMenu():integer

  ARRAY m;
  m(0) = " Продолжить";
  m(1) = " Прервать";

  return Menu( m, "Enter Выбор Esc Отмена", "" );

END;

MACRO InsertOpenAccount()
     
      if( not IsInsertAccount )
      
        ClearRecord( RecordAcc );             

        RecordAcc.Account       = ReqOpenAccObj.Account;
        RecordAcc.Code_Currency = ReqOpenAccObj.Code_Currency;
        RecordAcc.Balance       = ReqOpenAccObj.Balance(0);
        RecordAcc.Open_Date     = {curdate};
        RecordAcc.Oper          = ReqOpenAccObj.OperAcc;
        RecordAcc.Client        = ReqOpenAccObj.ClientID;
        RecordAcc.Department    = ReqOpenAccObj.AccountDepartment;
        RecordAcc.Branch        = ReqOpenAccObj.AccountBranch;
        RecordAcc.Chapter       = 1;
        RecordAcc.NameAccount   = ReqOpenAccObj.NameAccount;
        RecordAcc.Type_Account  = ReqOpenAccObj.Type_Account;
        RecordAcc.Kind_Account  = GenKindAccount( ReqOpenAccObj.Balance(0), 1, ReqOpenAccObj.Code_Currency );
        RecordAcc.UserField1    = "Открыт по заявлению на открытие счета";

        if( RecordAcc.NameAccount == "" )
          RecordAcc.NameAccount = "Счет клиента " + GetCodeParty( ReqOpenAccObj.ClientID, PTCK_CONTR );
        end;

        ClearRecord( ab );
        ab.Account       = RecordAcc.Account;
        ab.Code_Currency = RecordAcc.Code_Currency;
        ab.Chapter       = RecordAcc.Chapter;
        ab.Balance0      = ReqOpenAccObj.Balance(0);
        ab.Balance1      = ReqOpenAccObj.Balance(1); 
        ab.Balance2      = ReqOpenAccObj.Balance(2); 
        ab.Balance3      = ReqOpenAccObj.Balance(3); 
        ab.Balance4      = ReqOpenAccObj.Balance(4); 
        ab.Balance5      = ReqOpenAccObj.Balance(5); 
        ab.Balance6      = ReqOpenAccObj.Balance(6); 
        ab.Balance7      = ReqOpenAccObj.Balance(7); 
        ab.Balance8      = ReqOpenAccObj.Balance(8); 
        ab.Balance9      = ReqOpenAccObj.Balance(9); 
        ab.Balance10     = ReqOpenAccObj.Balance(10);
        ab.Balance11     = ReqOpenAccObj.Balance(11);

        if(not InsertAccount( RecordAcc, ab, ID_Link, true ) ) 
          MsgBox( "Ошибка при открытии счета " + ReqOpenAccObj.Account );
          return  1;
        else
          IsInsertAccount = 1;
        end;

      end;
      return 0;
END;

private macro getClientNameForMemorder( reqopnac, accbuf ):string

  var ClientName = "";
  var params = makeArray( SQLParam( "p_account"         , accbuf.rec.Account ),
                          SQLParam( "p_chapter"         , accbuf.rec.Chapter ),
                          SQLParam( "p_fiid"            , accbuf.rec.Code_Currency ),
                          SQLParam( "p_date"            , {curdate} ),
                          SQLParam( "p_Name"            , V_STRING, RSDBP_OUT ) );

  var stat = execStoredFunc( "PM_NAMES.PM_SetNameClientForReqOpNac", V_INTEGER, params, false );
  if( stat == 0 )
    ClientName = string( params.Value(4).value );
  end;
  return ClientName;
end;

private macro getBankNameForMemorder( AccDep ):string

  var BankName = "";
  var dp_dep = TBfile("dp_dep.dbt", "r");
  dp_dep.rec.Code = AccDep;
  if( not getEQ( dp_dep ) )
    msgbox( "Не найден филиал" );
  else
    var params:TArray = makeArray( SQLParam( "p_Context"  , execStoredFunc( "PM_NAMES.GetContextForPrimaryDoc", V_STRING, 
                                                                             makeArray( SQLParam( "p_DocKind", DLDOC_MEMORIALORDER ), 
                                                                                        SQLParam( "p_SubDocKind", 0 ) ) ) ), 
                                   SQLParam( "p_PartyID"  , dp_dep.rec.PartyID ),
                                   SQLParam( "p_Date"     , {curdate} ),
                                   SQLParam( "p_BankName" , V_STRING, RSDBP_OUT )  );
    var stat = execStoredFunc( "PM_NAMES.PM_SetBankName", V_INTEGER, params );
    if( stat == 0 )
      BankName = string( params.Value(3).value );
    end;
  end;
  return BankName;
end;

MACRO ExecuteStep( doc, pmDoc )

  

  
  var stat:integer = 0;

  var Choice:integer = 0;

  var RegVal;
  var Series = "", Number = "";// серия и номер сообщения в фонды 

  var FD_CorrAcc:ProfitFromAccAccum;
  var СчетДоходов:string;

  if(( not stat ) and ( ReqOpenAccObj.Account == "" ))
    ReqOpenAccObj.Account=ПолучитьНомерСчетаЗаявления(ReqOpenAccObj.Account, ReqOpenAccObj.Balance(0), ReqOpenAccObj.Code_Currency, ReqOpenAccObj.SpecialKind );
  end;
  /* Проверка даты открытия счета */
  if( ( ReqOpenAccObj.AccOpenDate  != date( 0, 0, 0 ) ) and ( ReqOpenAccObj.AccOpenDate != {curdate} ) )
    msgbox( "Счет должен быть открыт " + ReqOpenAccObj.AccOpenDate + ".|Перейдите в операционный день, совпадающий с указанной датой открытия, и повторите открытие счета" );
    return 1;
  end;

  if(( not stat ) and ( ReqOpenAccObj.Account != "" ))
    stat = CheckKey( 1, ReqOpenAccObj.Account );
  end;

  /*------------------------------------*/
  /* Проверка ареста на счетах слиента  */
  if( not stat )
    stat = CheckRestFromAccount( ReqOpenAccObj.ClientID );
  end;
  
  if( stat )
    msgbox( "Клиенту ", ReqOpenAccObj.NameAccount," новый счет открыть нельзя.|Имеются счета с приостановленными операциями по счету");
    return stat;
  end;
  /*------------------------------*/

   var ClientAccounts = TArray;
   var CltAccFIID     = TArray;

   var I:integer;

   var NeedOpenAccount = true;/* нужно ли открывать счет */

  ClientAccounts = TArray;
  CltAccFIID     = TArray;

  stat = GetClientAccounts( ReqOpenAccObj.ClientID, ALLFININSTR, "L", ClientAccounts, CltAccFIID );

  // Добавить вид обслуживания РКО, если его ещё нет
  if( PT_CheckClientService( ReqOpenAccObj.ClientID, PTSK_PAY, {curdate}, {OperDprt} ) != 0 )
    if( not PT_BindClientWithBranch( ReqOpenAccObj.ClientID, PTSK_PAY, {OperDprt}, {oper} ) )
      MsgBox( "Ошибка постановки клиента на обслуживание в РКО" );
      return stat;
    end;
  end;
  
  if( stat == 0 ) /* Если накопительный счет имеется */
    
    msgbox( "У клиента " + GetNameClient( "", ClientAccounts(0), 1, CltAccFIID(0) ) + 
             " есть накопительный счет в валюте ", ПолучитьИмяФинИн( CltAccFIID(0) ) + 
             ". | Накопительный счет подлежит закрытию! | " +
             " Проверьте наличие заявления на закрытие накопительного счета" );

    if( IsOprMultiExec() ) /* при массовом прервать выполнение шага */
      return 1;
    end;

    Choice = ShowMenu();
    
    if( Choice == CHOICE_CONTINUE ) /* Выбрали продолжить*/
        
        stat = GetRegValForOPENAC( "МЕТОД_ОТКР", V_INTEGER, RegVal );
        
        if( ( stat == 0 ) and ( RegVal == 0 ) )
          
          if( ( SubStr( ClientAccounts(0), 1, 5) == SubStr( ReqOpenAccObj.Account, 1, 5) ) and 
              ( CltAccFIID(0) == ReqOpenAccObj.Code_Currency ) and 
              ( stat == 0 )
            )
              ReqOpenAccObj.Account = ClientAccounts(0);
              NeedOpenAccount = false; /*будем использовать накопительный*/
              InsertNoteForAccount(ReqOpenAccObj.Account, ReqOpenAccObj.Code_Currency, 42, {curdate}); //42 = ACC_NOTE_KIND_DATE_CHANGE_TO_SETTLEMENT
          end;

        end;
        
       if( NeedOpenAccount )
          accbuf = GetAccount( 1, ClientAccounts(0), CltAccFIID(0) );

          ReqCloseAccum.ConnectToOper = true;
          ReqCloseAccum.LaunchOper    = true;
          ReqCloseAccum.SubKind       = KindDocOrder;
          ReqCloseAccum.Date          = {curdate};
          ReqCloseAccum.InputDate     = {curdate};
          ReqCloseAccum.Account       = accbuf.rec.Account;
          ReqCloseAccum.Ground        = 1;
          ReqCloseAccum.ClientID      = accbuf.rec.Client;
          
          if( not ( GetRegValForOPENAC( "РАЗРЕШЕНИЕ_ФИО", V_STRING, RegVal ) ) )
            ReqCloseAccum.PersonName  = RegVal;
          else
            ReqCloseAccum.PersonName  = "";
          end;
          
          ReqCloseAccum.Department = {OperDprt};
          ReqCloseAccum.Oper = {Oper};

          reqopnac = GetREQOPNAC( accbuf.rec.Account, stat );

          if( ( not stat) and ( reqopnac.rec.ToBankProfit == "X" ) or stat )
            
            ReqCloseAccum.RestFIID = NATCUR;
            
            if( accbuf.rec.Code_Currency == NATCUR )
              FD_CorrAcc = ProfitFromAccAccum( NATCUR );
              СчетДоходов = FD_CorrAcc.FindAndOpenSysAccount( "Доходы с накоп. счетов", 0, {curdate} );
            else
              FD_CorrAcc = ProfitFromAccAccum( accbuf.rec.Code_Currency );
              СчетДоходов = FD_CorrAcc.FindAndOpenSysAccount( "Доходы с накоп. счетов", 0, {curdate} );
            end;
            
            if( СчетДоходов != "" )
              ReqCloseAccum.RestAccount = СчетДоходов;
            end;

          else
            
            ReqCloseAccum.ReceiverFIID          = reqopnac.rec.ReceiverFIID;
            ReqCloseAccum.ReceiverCodeKind      = reqopnac.rec.ReceiverCodeKind;
            ReqCloseAccum.ReceiverCode          = reqopnac.rec.ReceiverCode;
            ReqCloseAccum.ReceiverINN           = reqopnac.rec.ReceiverINN;
            ReqCloseAccum.ReceiverName          = getClientNameForMemorder( reqopnac, accbuf );
            ReqCloseAccum.ReceiverAccount       = reqopnac.rec.ReceiverAccount;
            ReqCloseAccum.ReceiverBankCodeKind  = reqopnac.rec.ReceiverBankCodeKind;
            ReqCloseAccum.ReceiverBankCode      = reqopnac.rec.ReceiverBankCode;
            ReqCloseAccum.ReceiverBankName      = getBankNameForMemorder( accbuf.rec.Department );
            ReqCloseAccum.ReceiverBankCorrAcc   = reqopnac.rec.ReceiverBankCorrAcc;
            ReqCloseAccum.ReceiverCorrCodeKind  = reqopnac.rec.ReceiverCorrCodeKind;
            ReqCloseAccum.ReceiverCorrCode      = reqopnac.rec.ReceiverCorrCode;
            ReqCloseAccum.ReceiverCorrName      = reqopnac.rec.ReceiverCorrName;

          end;
       end;
    else
      return 1;
    end;
  
  end;

  if( ( GenKindAccount( ReqOpenAccObj.Balance(0), 1, ReqOpenAccObj.Code_Currency ) == "П" ) and
      PM_FindBalanceInReg_117( RegPath_Client, ReqOpenAccObj.Account, 1 ) )
    if( NeedOpenAccount )
       stat = InsertOpenAccount();
    end;
  else
    stat = InsertOpenAccount();
  end;
  return stat;

END;

