//-----------------------------------------------------------------------------
// Блок     : 29019 - "Картотека 1"
// Шаг      : 10    - "Помещение в картотеку 1"
// Описание : Макрос шага
//-----------------------------------------------------------------------------
import pm_common, pm_setst, payconst, payinter, "catfdoc.mac", "sf_lib.mac";
var PaymentObj:RsbPayment;

MACRO ExecuteStep( doc, paymDoc )

  // Для документов ПЗО вызовем функцию начисления комиссий
  if( IsSfCommPayment( PaymentObj ) )
    if( not chargeComission( PaymentObj.PaymentID ) )
      MsgBox("Ошибка при начислении комиссии");
      return 1;
    end;
  end;

  // Объекты для КУ
  var FD_Index1 :PaymIndex1_FirstDoc    = PaymIndex1_FirstDoc( PaymentObj.PaymentID );
  var FD_CorrAcc:NotBalCorrAcc_FirstDoc = NotBalCorrAcc_FirstDoc( "П" );

  // Счета КУ
  var СистемныйСчетДляКартотек:string = FD_CorrAcc.FindAndOpenSysAccount( "ВнебалСчетКорресп", 0, {curdate} );
  var ВнебалСчетКартотеки1    :string = FD_Index1.FindAndOpenAccount( "Картотека 1", 0, PaymentObj.PayerBankEnterDate );

  // Выполнить проводку по внебалансу
  var paymtr:RsbAccTransaction = PaymentObj.MakeTransaction();

  if( paymtr == NULL )
    MsgBox("Ошибка при создании проводки по платежу");
    return 1;
  end;

  paymtr.Chapter         = 3;                                            
  paymtr.Date_Carry      = PaymentObj.PayerBankEnterDate;
  paymtr.Number_Pack     = PaymentObj.NumberPack;
  paymtr.Numb_Document   = PaymentObj.Number;
  paymtr.ResultCarry     = 47;
  paymtr.Kind_Oper       = " 1";
  paymtr.Shifr_Oper      = "09";
  paymtr.Department      = PaymentObj.Department;
  paymtr.AccountPayer    = ВнебалСчетКартотеки1;
  paymtr.AccountReceiver = СистемныйСчетДляКартотек;
  paymtr.FIID            = PaymentObj.PayerFIID;
  paymtr.Sum             = PaymentObj.FuturePayerAmount;
  paymtr.Ground          = "Постановка в Картотеку 1 документа № " + string(PaymentObj.Number)   + 
                           " от "                                  + string(FD_Index1.GetDate()) + 
                           " к счету "                             + string(PaymentObj.PayerAccount);

  if( not paymtr.Carry )
    MsgBox("Ошибка при актуализации платежа");
    return 1;
  end;  

  // Здесь можно вставлять дополнительные проводки для шага

  return 0;

END;
