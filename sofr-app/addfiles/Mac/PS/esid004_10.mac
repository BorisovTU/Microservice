/*
$Name:             esid004_10.mac
$Module:           РКО юридических лиц
$Description:      Операция 4004. Шаг <Исполнение распоряжения на оплату>
*/

import PaymInter, BankInter, PSInter, CTInter, OprInter, "wltools.mac", cbsttls, pm_common,
       "pm_setst.mac";

private CONST SET_CHAR   = "X";
private CONST UNSET_CHAR = "";

// CalcFromCurDateDepartment если true, то считаем от текущего опердня филиала, 
// который передали, иначе считаем от текущего опердня пользователя
private CONST CalcFromCurDateDepartment = true; 

var PaymentObj:RsbPayment;

//-----------------------------------------------------------------------------
// Найти текущий операционный день в подразделении
//-----------------------------------------------------------------------------
private macro GetCurOperDay( Department ):date
  var select:string = " SELECT t_CurDate " + 
                        " FROM dcurdate_dbt " + 
                        " WHERE t_Branch = :Department " + 
                          " and t_IsMain = :IsMain ";
                          
  var params:TArray = makeArray( SQLParam( "Department", Department),
                                 SQLParam( "IsMain"      , "X" ));
  var rset:RsdRecordset = execSQLselect( select, params, TRUE );
  if( rset and rset.moveNext())
    return rset.value("t_CurDate");
  end;
  return date(0,0,0);
end;
// ----------------------------------------------------------------------------
// Выполнение шага
// ----------------------------------------------------------------------------
macro ExecuteStep( Kind_Operation, first, KindDoc, ID_Operation )
  if( IsRevokedByInitiator(PaymentObj) )
    return RevokeEsidOrder(PaymentObj);
  end;

  var Obj = null;
  var Direct = GetOprStatus(OPR_CLAIM_DIRECT); //Направление

  /* Создать дочерний документ */
  var NewPayOrderObj = null;
  if (PaymentObj.PayerFIID == NATCUR)
    NewPayOrderObj = RsbPSPayOrder();
    if ( (PaymentObj.DocKind == DLDOC_OUT_PMCLAIM) or (PaymentObj.DocKind == DLDOC_IN_PMCLAIM))
   
      if ( PaymentObj.DocKind == DLDOC_OUT_PMCLAIM)
        Obj = GenObject( "RsbOutClaimOrder", PaymentObj.DocumentID ); 
      else
        Obj = GenObject( "RsbInClaimOrder", PaymentObj.DocumentID ); 
      end;
    
      NewPayOrderObj.ReqSum            = Obj.ReqSum;
      NewPayOrderObj.AcceptTerm        = Obj.AcceptTerm;
      NewPayOrderObj.AcceptPeriod      = Obj.AcceptPeriod;
      NewPayOrderObj.DocKind           = PSPOKIND_DEMAND;// Рублевое платежное требование
      NewPayOrderObj.AcceptDate        = GetDateAfterWorkDays( {curdate}, NewPayOrderObj.AcceptPeriod );

    elif ( (PaymentObj.DocKind == DLDOC_OUT_PMCOL) or (PaymentObj.DocKind == DLDOC_IN_PMCOL))

      NewPayOrderObj.DocKind           = PSPOKIND_REQUEST;// Рублевое инкассовое поручение

    end;
    NewPayOrderObj.ReqSum = PaymentObj.BaseAmount;
    NewPayOrderObj.Origin        = PSPO_OR_PAYEEBANK;
    NewPayOrderObj.ConnectToOper = true;
  else
    NewPayOrderObj = RsbRequestOrder();
    NewPayOrderObj.Origin        = CP_OR_PAYEEBANK;
    NewPayOrderObj.PSBCDate      = PaymentObj.Date;
    NewPayOrderObj.ConnectToOper = true;
  end;

  var NewPaymentObj:RsbPayment = NewPayOrderObj.Payment;

  /* Заполнить общие поля платежа */
  if (PaymentObj.PayerFIID == NATCUR)
    NewPaymentObj.DocKind              =
    NewPaymentObj.Primdockind          = PS_PAYORDER;
  end;
  NewPaymentObj.Number               = PaymentObj.Number;
  NewPaymentObj.reference            = PaymentObj.reference;
  NewPaymentObj.Date                 = PaymentObj.Date;
  NewPaymentObj.ValueDate            = PM_GetDefaultValueDate(PaymentObj.endDepartment, NewPaymentObj.PrimDocKind, CalcFromCurDateDepartment);
  NewPaymentObj.PaymentKind          = PaymentObj.PaymentKind;
  NewPaymentObj.BaseFIID             = PaymentObj.BaseFIID;
  NewPaymentObj.ReceiverAmount       = 
  NewPaymentObj.BaseAmount           = PaymentObj.BaseAmount;
//  NewPaymentObj.PayerCorrAccNostro   = PaymentObj.PayerCorrAccNostro;

  NewPaymentObj.ReceiverCorrAccNostro= PaymentObj.ReceiverCorrAccNostro;
  NewPaymentObj.ShifrOper            = PaymentObj.ShifrOper;
  NewPaymentObj.Ground               = PaymentObj.Ground;
//  NewPaymentObj.PayerIsSender        = SET_CHAR;
  NewPaymentObj.Priority             = PaymentObj.Priority;
  NewPaymentObj.I2placedate          = date(0,0,0);
  NewPaymentObj.PayerBankEnterDate   = GetCurOperDay(PaymentObj.endDepartment); 
  NewPaymentObj.Origin               = PAYMENT_OR_AUTO;
    
  if ( (PaymentObj.DocKind == DLDOC_OUT_PMCOL) or (PaymentObj.DocKind == DLDOC_IN_PMCOL))
    NewPaymentObj.TaxAuthorState       = PaymentObj.TaxAuthorState;
    NewPaymentObj.BTTTICode            = PaymentObj.BTTTICode;
    NewPaymentObj.OKATOCode            = PaymentObj.OKATOCode;
    NewPaymentObj.TaxPmGround          = PaymentObj.TaxPmGround;
    NewPaymentObj.TaxPmPeriod          = PaymentObj.TaxPmPeriod;
    NewPaymentObj.TaxPmNumber          = PaymentObj.TaxPmNumber;
    NewPaymentObj.TaxPmDate            = PaymentObj.TaxPmDate;
    NewPaymentObj.TaxPmType            = PaymentObj.TaxPmType;
    NewPaymentObj.PayDate              = NewPaymentObj.ValueDate;

    NewPaymentObj.Legality             = PaymentObj.Legality;
    NewPaymentObj.LegalityReason       = PaymentObj.LegalityReason;
    NewPaymentObj.LegalityEdit         = PaymentObj.LegalityEdit;
  end;    
    
  /* Скопировать ПИ по дебету */
  NewPaymentObj.SetPayerPI( PaymentObj.PayerGroup,                                /*Group*/
                            PaymentObj.PayerBankID,                               /*BankID*/
                            PaymentObj.PayerBankCodeKind,                         /*BankCodeKind*/
                            PaymentObj.PayerBankCode,                             /*+BankCode*/
                            PaymentObj.PayerBankName,                             /*BankName*/
                            "",                                                   /*CorrAcc*/
                            PaymentObj.PayerFIID,                                 /*FIID*/
                            PaymentObj.Chapter,                                   /*Chapter*/
                            PaymentObj.PayerAccount,                              /*+Account*/
                            PaymentObj.Payer,                                     /*Client*/
                            PaymentObj.PayerName,                                 /*+ClientName*/
                            PaymentObj.PayerINN,                                  /*+ClientINN*/
                            PaymentObj.PayerCodeKind,                             /*+ClientCodeKind*/
                            PaymentObj.PayerCode,                                 /*ClientCode*/
                            -1,                                                   /*Corschem*/
                            0  );                                                 /*CorrPosType*/
  
  /* Скопировать ПИ по кредиту */
  NewPaymentObj.SetReceiverPI( PaymentObj.ReceiverGroup,                           /*Group*/               
                               PaymentObj.ReceiverBankID,                          /*BankID*/              
                               PaymentObj.ReceiverBankCodeKind,                    /*BankCodeKind*/        
                               PaymentObj.ReceiverBankCode,                        /*+BankCode*/            
                               PaymentObj.ReceiverBankName,                        /*BankName*/            
                               PaymentObj.ReceiverBankCorrAcc,                     /*CorrAcc*/             
                               PaymentObj.ReceiverFIID,                            /*FIID*/                
                               PaymentObj.Chapter,                                 /*Chapter*/             
                               PaymentObj.ReceiverAccount,                         /*+Account*/             
                               PaymentObj.Receiver,                                /*Client*/              
                               PaymentObj.ReceiverName,                            /*+ClientName*/          
                               PaymentObj.ReceiverINN,                             /*+ClientINN*/           
                               PaymentObj.ReceiverCodeKind,                        /*+ClientCodeKind*/      
                               PaymentObj.ReceiverCode,                            /*ClientCode*/
                               -1,                                                 /*Corschem*/
                               0  );                                               /*CorrPosType*/

  if ( (Direct == OPR_CL_ST_DIR_IN) and not IsOprMultiExec())       
    /* Вывести панель созданного платежа на экран */
    if( PaymentObj.PayerFIID == NATCUR )
      if( PS_ShowPsPayOrder( NewPaymentObj.PaymentID ) )
        msgbox( "Выполнение процедуры прервано пользователем" );
        return 1;
      end;
    else
      if( PS_ShowRequestOrder( NewPaymentObj.PaymentID ) )
        msgbox( "Выполнение процедуры прервано пользователем" );
        return 1;
      end;
    end;
  end;  

  /* Связать созданный платеж с обрабатываемым по виду связи "Исполнение распоряжения"*/ 
  if( PaymentObj.LinkPayment( NewPaymentObj, PMLINK_KIND_ESIDPAYMENT ) )
    msgbox(GetErrMsg());
    return 1;
  end;
  
  // Дочерний платеж должен идти на контроль
  if( NewPayOrderObj.AddOprState( OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_NOTCONTROL) != 0 )
    msgbox("Ошибка при установке сегментов статуса экземпляра операции");
    return 1;
  end;

  if ( Direct == OPR_CL_ST_DIR_INTERNAL)
    PaymentObj.PayerBankEnterDate  = NewPaymentObj.PayerBankEnterDate; 
  end;  
  
  PaymentObj.PaymStatus = PM_FINISHED;
  PaymentObj.PropStatus = PM_PROP_CLOSED;
    
  return 0;
end;                                                               
                          
