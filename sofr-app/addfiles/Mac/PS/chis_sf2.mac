/* ╔══════════════════════════════════════════════════════════════════╗ */
/* ║        Автоматизированная банковская система RS-Bank v5.0        ║ */
/* ║                Copyright (c) R-Style Software Lab 1998           ║ */
/* ║                                                                  ║ */
/* ║ Имя файла    : chiss_categ.mac                                   ║ */
/* ║                                                                  ║ */
/* ║ Описание     : Шаг "Единовременные комиссии - исполнение"        ║ */
/* ║                     заяв. на выд. цен. бл.                       ║ */
/* ║                                                                  ║ */
/* ║                                                                  ║ */
/* ║ Программист  : Локтюшин В.Ю.                                     ║ */
/* ║                                                                  ║ */
/* ║ Создан       : 14.09.2006                                        ║ */
/* ║                                                                 ║ */
/* ╚══════════════════════════════════════════════════════════════════╝ */


import PSInter, chis_categ, chisfun, InsCarryDoc, OprInter, payconst, payinter, pm_categ,
FIInter, SFInter, oralib, likepy, cbctuncs;

var CheckIssObj:RsbCheckIss;

//Возможные исходы событий
PRIVATE CONST PMSF_ACTION_OK    :integer = 0, //Ok
              PMSF_ACTION_ERROR :integer = 1; //Возникла ошибка

PRIVATE MACRO CheckSiCredit( sicredit:TRecHandler ):integer

  // Проверить наличие валюты в sicredit
  if( sicredit.rec.FIID < 0 )
    msgbox("Не задана валюта в ПИ ПЗО");
    return PMSF_ACTION_ERROR;
  end;
  // проверить, чтобы получатель ПЗО находился в нашем банке (в том же банке, что и плательщик);
  if( sicredit.rec.BankCode != "" )
    msgbox("При оплате комиссий проводками счет получателя комиссий должен быть в нашем банке");
    return PMSF_ACTION_ERROR;
  end;
  if( sicredit.rec.Account == "" )
    msgbox("Не задан получатель в ПИ ПЗО");
    return PMSF_ACTION_ERROR;
  end;

  return PMSF_ACTION_OK;
END;


PRIVATE MACRO CheckFee( action:integer, oprcom ):integer

  if( action != PMSF_ACTION_OK )
    return action;
  end;

  record contr(sfcontr);
  if( not SfGetContr(oprcom.SfcontrID, contr ) )
    msgbox( "Не найден договор обслуживания" );
    return PMSF_ACTION_ERROR;
  end;

  var sfcomiss:TBFile = TBFile( "sfcomiss.dbt", "R", 0 );
  
  sfcomiss.rec.FeeType = oprcom.FeeType;    
  sfcomiss.rec.Number  = oprcom.CommNumber;
  if( not sfcomiss.GetEQ() )
    msgbox( "Не найдена комиссия" );
    return PMSF_ACTION_ERROR;
  end;

  var sidebet:TRecHandler = TRecHandler("sfsi.dbt");
  var sicredit:TRecHandler = TRecHandler("sfsi.dbt");
  
  if( SfGetSI( OBJTYPE_OPRSFCOM, oprcom, sidebet, sicredit ) )
    msgbox("Ошибка при взятии платежных инструкций");
    return PMSF_ACTION_ERROR;
  end;
  
  var Status:integer = CheckSiCredit(sicredit);
  if( Status != PMSF_ACTION_OK )
    return PMSF_ACTION_ERROR;
  end;

  var paymtr:object    = RsbAccTransaction();//PaymentObj.MakeTransaction();
  var paymtrNDS:object = RsbAccTransaction();//PaymentObj.MakeTransaction();
  
  if( ( paymtr == NULL ) and ( paymtrNDS == NULL ) )
    MsgBox("Ошибка при создании проводки по платежу");
    return PMSF_ACTION_ERROR;
  end;

  var sum_pay = $0;
  if( CheckIssObj.ComissFIID != sicredit.rec.FIID )
    ConvSumCross( sum_pay, oprcom.Sum, {curdate}, sicredit.rec.FIID, CheckIssObj.ComissFIID );
  else
    sum_pay = oprcom.Sum;
  end;

  paymtr.Date_Carry      = {curdate};
  paymtr.Department      = CheckIssObj.Department;
  paymtr.ResultCarry     = SFPAY_CARRY;
  paymtr.FIIDPayer       = CheckIssObj.ComissFIID;
  paymtr.FIIDReceiver    = sicredit.rec.FIID;
  paymtr.SumPayer        = sum_pay;
  paymtr.SumReceiver     = oprcom.Sum;
  paymtr.AccountPayer    = CheckIssObj.ComissAccount;
  paymtr.AccountReceiver = sicredit.rec.Account;
  paymtr.Ground          = "Заявление на выдачу ценных бланков №"+ string(CheckIssObj.Number) + 
                           " от "                                + string(CheckIssObj.Date)   + 
                           ". Комиссия за оформление.";

  if( not paymtr.Carry )
    MsgBox("Ошибка при актуализации платежа");
    return PMSF_ACTION_ERROR;
  end;  

  var sum_pay_nds = $0;
  if( oprcom.SumNDS > 0 )
  
    if( CheckIssObj.ComissFIID != sicredit.rec.FIID )
      ConvSumCross( sum_pay_nds, oprcom.SumNDS, {curdate}, sicredit.rec.FIID, CheckIssObj.ComissFIID );
    else
      sum_pay_nds = oprcom.SumNDS;
    end;
  
    paymtrNDS.Date_Carry      = {curdate};
    paymtrNDS.Department      = CheckIssObj.Department;
    paymtrNDS.ResultCarry     = SFPAY_CARRY;
    paymtrNDS.FIIDPayer       = CheckIssObj.ComissFIID;
    paymtrNDS.FIIDReceiver    = sicredit.rec.FIID;
    paymtrNDS.SumPayer        = sum_pay_nds;
    paymtrNDS.SumReceiver     = oprcom.SumNDS;
    paymtrNDS.AccountPayer    = CheckIssObj.ComissAccount;
    paymtrNDS.AccountReceiver = sicredit.rec.Account;
    paymtrNDS.Ground          = "Заявление на выдачу ценных бланков №"+ string(CheckIssObj.Number) +
                             " от "                                + string(CheckIssObj.Date)   +
                             ". НДС по комиссии за оформление.";

    if( not paymtrNDS.Carry )
      MsgBox("Ошибка при актуализации платежа");
      return PMSF_ACTION_ERROR;
    end;  

  end;
  return PMSF_ACTION_OK;
END;


/* ╔═════════════════════════════════╗ */
/* ║        Выполнение шага          ║ */
/* ╚═════════════════════════════════╝ */
MACRO ExecuteStep( doc, paymDoc, DocKind:integer, ID_Operation:integer, ID_Step:integer )
  var stat:bool = true;
  var Action:integer;

  var OprComList:TArray = TArray();
  RECORD oprcom( oprsfcom );
  
    
  while( SfGetOperCommission(ID_Operation, -1, oprcom) )
    OprComList[OprComList.size] = oprcom;
  end;

  if( OprComList.size > 0 )

    Action = reduce( OprComList, @CheckFee, PMSF_ACTION_OK );
    if( Action == PMSF_ACTION_ERROR )
      stat = false;
    end;

  end;

  if( stat )
    if( not InsertOprStatus( CHECKISS_KIND_ST_PZO, CHECKISS_ST_PZO_NOTNEED ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return -1;
    end;
    return 0;
  else
    return 1;
  end;

END;
