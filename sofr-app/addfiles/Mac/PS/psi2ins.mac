/*
$Name:           psi2ins.mac
$Module:         РКО
$Description:    Макрос шага
*/
/*
 Блок     : 29020 - "Картотека 2"
 Шаг      : 10    - "Помещение в картотеку 2"
*/

import pm_common, pm_setst, payinter, oralib, likepy, catfdoc, cbsttls, "sf_lib.mac", 
       "pm_answerret.mac", WldInter, MesInter, "wltools.mac", "pmlib.mac", "pm_answerret.mac";

var PaymentObj:RsbPayment;

private const PM_OPR_KA_ACCEPTI1    = 205;
private const PM_OPR_KA_OUTPLACEIWP = 235;
private const PM_OPR_KA_PLACEIWP    = 234; 

PRIVATE MACRO ПлатежИзКартотеки1илиКОР( Payment:RsbPayment, ID_Operation:@integer, ID_Step:@integer, Kind_Action:@integer ):bool

  if( Payment.DocKind != PS_PAYORDER )
    return false;
  end;

  var sDocumentID:string = PM_MakeDocumentID( Payment.DocKind, Payment.DocumentID );

  var query:string = "SELECT STEP.T_ID_OPERATION, STEP.T_ID_STEP, STEP.T_KIND_ACTION    " +
                     "  FROM DOPROPER_DBT OPR,                        " +
                     "       DOPRSTEP_DBT STEP                        " +
                     " WHERE OPR.T_DOCKIND       = :DOCKIND           " +
                     "   AND OPR.T_DOCUMENTID    = :DOCUMENTID        " +
                     "   AND STEP.T_ID_OPERATION = OPR.T_ID_OPERATION " +
                     "   AND STEP.T_ISEXECUTE    = 'X'                " +
                     "   AND STEP.T_KIND_ACTION  IN(:KIND_ACTION_K1, :KIND_ACTION_IWP, :KIND_ACTION_IWP2)  "+
                     "ORDER BY STEP.T_ID_STEP DESC" ;

  var params:TArray = makeArray( SQLParam( "DOCKIND"    , Payment.DocKind           ), 
                                 SQLParam( "DOCUMENTID" , sDocumentID               ),
                                 SQLParam( "KIND_ACTION_K1" , PM_OPR_KA_ACCEPTI1        ),
                                 SQLParam( "KIND_ACTION_IWP", PM_OPR_KA_OUTPLACEIWP     ),
                                 SQLParam( "KIND_ACTION_IWP2", PM_OPR_KA_PLACEIWP       ));

  var rs:RsdRecordset = execSQLselect( query, params, true );
  if( rs and rs.moveNext() )
    ID_Operation = rs.value(0);
    ID_Step      = rs.value(1);
    Kind_Action  = rs.value(2);
    return TRUE;
  else
    return FALSE;
  end;

ONERROR(x)
  
  MsgBox( "Ошибка получения шага операции|" + x.Message );
  return FALSE;

END;

PRIVATE MACRO DeleteCarryPrevStep( Payment:RsbPayment, ID_Operation:integer, ID_Step:integer ):integer

  // Получить проводку
  MACRO getCarry( AccTrnID:integer ):TBFile
    var fdocument:TBFile = TBFile("acctrn.dbt",  "R", 0, "acctrn.dbt", "bank.def");
      
    if( fdocument and AccTrnID )
      fdocument.rec.AccTrnID = AccTrnID;
      if( fdocument.GetEQ() )
        return fdocument;
      end;
    end;
    return fdocument;
   
  END;

  var carrydocument:TBFile;
  // привязка проводки к найденному шагу
  var query:string = "SELECT OPRDOCS.T_ACCTRNID " +
                     "  FROM DOPRDOCS_DBT OPRDOCS " +
                     " WHERE OPRDOCS.T_DOCKIND      = :DOCKIND " + 
                     "   AND OPRDOCS.T_ID_OPERATION = :ID_OPERATION " +
                     "   AND OPRDOCS.T_ID_STEP      = :ID_STEP; ";
   
  var params:TArray = makeArray( SQLParam( "DOCKIND"     , DLDOC_CARRY  ),
                                 SQLParam( "ID_OPERATION", ID_Operation ),
                                 SQLParam( "ID_STEP"     , ID_Step      ) );
     
  var rs:RsdRecordset = execSQLselect( query, params, true );
  while( rs and rs.moveNext() )

    carrydocument = getCarry( rs.value(0) );

    // Удалить внебалансовую проводку, выполненную на шаге "Изъятие из картотеки №1" или "Списание из КОР"
    if( ( carrydocument.rec.Result_Carry == OBI1CARRY ) or ( carrydocument.rec.Result_Carry == OBIWPOUTCARRY ) )
      if( not Opr_DeleteCarry( carrydocument.rec.AccTrnID ) )
        MsgBox("Ошибка при удалении проводки");
        return 1;
      else
        return 0;
      end; 
    end;
  end; 

ONERROR(x)
  
  MsgBox( "Ошибка получения привязки проводки к шагу операции|" + x.Message );
  return 1;

END;

MACRO ExecuteStep( doc, payorder, Kind, ID_Operation )

  var ВнебалСчетКартотеки2:string = "";
  var paymtr:RsbAccTransaction = NULL;

  var IDOperation:integer = 0;
  var IDStep:integer = 0;
  var Kind_Action:integer = 0;

  var ПлатежПришелИзКартотеки1:bool = ПлатежИзКартотеки1илиКОР( PaymentObj, @IDOperation, @IDStep, @Kind_Action ) and 
                                      ( Kind_Action == PM_OPR_KA_ACCEPTI1    );
  var ПлатежПришелИзКОР       :bool = ( Kind_Action == PM_OPR_KA_OUTPLACEIWP ) or (Kind_Action == PM_OPR_KA_PLACEIWP);
 
  if( PaymentObj.PaymStatus != PM_I2PLACED )
  
    // Для документов ПЗО вызовем функцию начисления комиссий
    if( IsSfCommPayment( PaymentObj ) )
      if( not chargeComission( PaymentObj.PaymentID ) )
        MsgBox("Ошибка при начислении комиссии");
        return 1;
      end;
    end;
    
    var category:TRecHandler = TRecHandler("objattr");
    if( PaymentObj.Categories.GetFirst(OBJ_PAYMENT_GROUP_EXECUTE_ACC_STOP, {curdate},  category) )
      
      var IndPlace_ExecAlthoughTaxClaim:integer = Bnk_GetRegistryValue( "CB\\PAYMENTS\\DepartmentalInfo\\IndPlace_ExecAlthoughTaxClaim", V_INTEGER, 0 );

      if( ((IndPlace_ExecAlthoughTaxClaim == 3) and ((PaymentObj.PayType == BPT_TAX) or (PaymentObj.PayType == BPT_INSURANCE))) or
          ((IndPlace_ExecAlthoughTaxClaim == 1) and (PaymentObj.PayType == BPT_TAX)) or
          ((IndPlace_ExecAlthoughTaxClaim == 2) and (PaymentObj.PayType == BPT_INSURANCE)))
        
        if(not ConnectCategory( OBJTYPE_PAYMENT,
                                OBJ_PAYMENT_GROUP_EXECUTE_ACC_STOP,
                                PaymentObj.PaymentID,
                                false,
                                1, "", "" ))
          MsgBox("Ошибка при установке категории платежа");
          return 1;
        end;

      end;
    end;
    
    if( GetAccount90902( PaymentObj.PayerAccount,
                         PaymentObj.PayerFIID,
                         PaymentObj.BaseFIID,
                         ВнебалСчетКартотеки2,  /* В случае успеха, сюда вернется номер "привязанного" счета */
                         PaymentObj.ValueDate ) )
      return 1;
    end;
    
    if( ПлатежПришелИзКартотеки1 or ПлатежПришелИзКОР ) 

       // Удалить внебалансовую проводку, выполненную на шаге "Изъятие из картотеки №1"/"Списание платежа с карт.ОР"
       if( DeleteCarryPrevStep( PaymentObj, IDOperation, IDStep ) )
         return 1;
       end;

    end;

    // Выполнить проводку по внебалансу

    paymtr = PaymentObj.MakeTransaction();
    if( PaymentObj.I2PlaceDate == date(0,0,0) )
      PaymentObj.I2PlaceDate = {curdate};
    end;

    if( paymtr == NULL )
      MsgBox("Ошибка при создании проводки по платежу");
      return 1;
    end;

    paymtr.Chapter         = 3;                                            
    paymtr.Date_Carry      = PaymentObj.ValueDate;
    paymtr.Number_Pack     = PaymentObj.NumberPack;
    paymtr.Numb_Document   = PaymentObj.Number;
    paymtr.ResultCarry     = 50;
    paymtr.Kind_Oper       = " 1";
    paymtr.Shifr_Oper      = "09";
    paymtr.Department      = PaymentObj.Department;
    paymtr.AccountPayer    = ВнебалСчетКартотеки2;
    paymtr.FIIDPayer       = PaymentObj.BaseFIID;

    if( ПлатежПришелИзКартотеки1 ) 
      paymtr.AccountReceiver = PaymIndex1_FirstDoc( PaymentObj.PaymentID ).FindAndOpenAccount( "Картотека 1", 0, {curdate} );
    elif( ПлатежПришелИзКОР ) 
      paymtr.AccountReceiver = TIndexWPPrimDoc( PM_GetSfContrID(PaymentObj), PaymentObj.PaymentID ).FindAndOpenSysAccount( "Карт ОР", IsOprMultiExec() );
    else
      paymtr.AccountReceiver = NotBalCorrAcc_FirstDoc( "П" ).FindAndOpenSysAccount( "ВнебалСчетКорресп", 0, {curdate} );
    end;
    
    paymtr.FIIDReceiver = NATCUR;

    paymtr.SumPayer = PaymentObj.FutureBaseAmount;

    if( ConvSum( paymtr.SumReceiver, PaymentObj.FutureBaseAmount, PaymentObj.ValueDate, PaymentObj.BaseFIID, NATCUR ) )
      msgbox("Ошибка конвертации суммы");
      return 1;
    end;


    paymtr.Ground          = "Постановка в Картотеку 2 документа № " + string(PaymentObj.Number) + 
                             " от "                                  + string(PaymentObj.Date)   + 
                             " к счету "                             + string(PaymentObj.PayerAccount);

    if( not paymtr.Carry )
      MsgBox("Ошибка при актуализации платежа");
      return 1;
    end;  

    // Установить статус платежа
    PaymentObj.PaymStatus = PM_I2PLACED;

    // Установить статус первички
    PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_I2 );

    if ( IsDocKindChild(PaymentObj.DocKind, 283/*PMDOC_CLIENTPAYMENT*/) // платеж клиентский
        and (PaymentObj.Notes.ReadNote(PM_NOTEKIND_PAYM_ACCEPTUATEDATE, {curdate}) == "") )//примечание не заполнено
      if (PaymentObj.Notes.AddNote(PM_NOTEKIND_PAYM_ACCEPTUATEDATE, GetCurrentDateTimeUTC({curdate})))
        msgbox( "Ошибка при вставке примечания платежа" );
        return 1;
      end;
    end;

    // Здесь можно вставлять дополнительные проводки для шага
  end;
  return 0;

END;

macro PostStepAction( message,     /* данный параметр может принимать следующие       */
                                   /* значения: 1 - выполнение шага; 2 - откат шага;  */
                      errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                                   /* произошла ошибка                                */
                      FirstDoc,    /* указатель на первичный документ                 */
                      ID_Oper,     /* внутренний идентификатор операции               */
                      Num_Step,    /* Номер шага операции (из настроек)               */
                      KindOper,    /* номер вида операции                             */
                      KindDoc,     /* номер вида первичного документа                 */
                      KindStep,    /* вид шага операции                               */
                      ID_Step )    /* внутренний идентификатор шага операции          */
  if (message != OP_EXECUTE_STEP)
    return 0;
  end;                                   

  record wlmes( wlmes );
   
  if( ДокументПорожденВходящимЭСИДNew(PaymentObj.PaymentID) )

    var initialPaymentID = getInitialPaymentID(PaymentObj.PaymentID, PMLINK_KIND_EXECORDER);
    
    if( initialPaymentID > 0 )
      // Связанное входящее сообщение платежа 
      if( not ПолучитьСообщение( initialPaymentID, OBJTYPE_PAYMENT, 1 /*WLD_MES_IN*/, wlmes, NULL ) )  
          ClearRecord( wlmes );
      end;
    end;

    var ErrElement;
    var ErrDescription;
    var ErrList = RsbWlError(0);
    var wlerr_buf = TRecHandler("wlerror.dbt");

    if( (initialPaymentID > 0) and (wlmes.MesID > 0) and (GetMesFormatType(wlmes.MesID) == WLD_FORMAT_TXT) )

      wlerr_buf.rec.Code = "35";
      wlerr_buf.rec.Description  = string("поручение налогового органа № ",
          PaymentObj.GetPMRMPROP().rec.Number, " от ", PaymentObj.GetPMRMPROP().rec.Date,
          " на сумму ", PaymentObj.BaseAmount, " не может быть исполнено в установленный срок в  связи с отсутствием ",
          "(недостаточностью) денежных средств на счете ", PaymentObj.PayerAccount,
          " клиента ", PaymentObj.GetPMRMPROP().rec.PayerName, " банка (филиала банка)");

      ErrList.Insert( wlerr_buf );

      // Параметры создаваемого подтверждения банка 
      ErrElement = FNS_RP_CANNOT_EXECUTED_BANK;
      ErrDescription = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_MNS, ErrElement);

      if( not ВставитьПодтверждениеБанкаФНС( wlmes, ErrElement, ErrDescription, ErrList ) )
        msgbox( "Ошибка при вставке подтверждения банка" );
      end;

    else

      wlerr_buf.rec.Code = "00";

      wlerr_buf.rec.Description  = string("поручение налогового органа № ",
          PaymentObj.GetPMRMPROP().rec.Number, " от ", PaymentObj.GetPMRMPROP().rec.Date,
          " на сумму ", PaymentObj.BaseAmount, " не может быть исполнено в установленный срок в  связи с отсутствием ",
          "(недостаточностью) денежных средств на счете ", PaymentObj.PayerAccount,
          " клиента ", PaymentObj.GetPMRMPROP().rec.PayerName, " банка (филиала банка)");

      ErrList.Insert( wlerr_buf );
      
      // Параметры создаваемого подтверждения банка 
      ErrElement = FNS_RP_PAY_NO_EXEC_NO_MONEY;
      ErrDescription = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_MNS, ErrElement);

      if( not ВставитьПодтверждениеБанкаФНС( NULL, ErrElement, ErrDescription, ErrList, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, PaymentObj.PaymentID, 2 ) )
        msgbox( "Ошибка при вставке подтверждения банка" );
      end;
    end;
  end;


  if ((PaymentObj.DocKind == PS_PAYORDER) or (PaymentObj.DocKind == PS_INRQ))
    var DocKind, Origin;
    var rs = execSQLselect("select nvl(ps.t_DocKind, " + PSPOKIND_REQUEST + "), nvl(ps.t_Origin, inr.t_Origin) from dpspayord_dbt ps, dpsinrq_dbt inr, dpmpaym_dbt pm " +
                           " where pm.t_PaymentID = ? and ps.t_OrderID(+) = pm.t_PaymentID and inr.t_PaymentID(+) = pm.t_PaymentID",
                           makeArray(SQLParam("", PaymentObj.PaymentID)));
    if (rs and rs.MoveNext())
      var Doc : string = IfThenElse( DocKind == PSPOKIND_REQUEST, 
                                     "инкассового поручения", "платежного требования" );
      var Narrative : string = 
        "Недостаточность денежных средств на счете плательщика " + PaymentObj.PayerAccount +
        ", уведомляем о помещении " + Doc + " в очередь не исполненных в срок распоряжений " + 
        "(в соответствии с нормативными актами Банка России) и о приеме " + Doc + 
        " к исполнению. ";
      var Queries : string = "InfoCode:6";
      CreateED274(PaymentObj.PaymentID, Queries, Narrative, ID_Oper, ID_Step);
    end;
  end;

  return 0;
end;