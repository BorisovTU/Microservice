/* ps_i2prn.mac                                                                  Дудин В.А.             
   ──────────────────────────── Печать платежного ордера ──────────────────────────────────
   Modified: 13.09.01 LCh 
     Вывод картинки для общей доступности перенесен в CB\prpocl.mac 
       Макрофункции:  DrawBankPayOrd, _PSPO_print_i2 
   ──────────────────────────────────────────────────────────────────────────────────────── */
 import  prpocl,  payconst;

 file PrimPmpaym  ( pmpaym   ); /* ─┐                                                     */
 file PrimPmprop  ( pmprop   ); /*  ├─ параметры основного платежа по клиентской платежке */
 file PrimPmrmprop( pmrmprop ); /* ─┘                                                     */
 file PartPmpaym  ( pmpaym   ); /* ─┐                                           */
 file PartPmprop  ( pmprop   ); /*  ├─ параметры платежного документа           */
 file PartPmrmprop( pmrmprop ); /* ─┘  последней частичной оплаты картотеки №2  */


/* ──────────────────────────── Печать платежного ордера ────────────────────────────────── */

 MACRO PSPO_print_i2( GroupPrint, Date_In:Date, Date_Out:Date, Oper:integer, NumDprt:integer ) //GroupPrint - признак массовой печати 

   var Use_VRSL, err,  ErrStr;
   GetRegistryValue( ПутьНастройкиИспользованияVRSLПлатежномОрдере, V_INTEGER, Use_VRSL, err );
   if ( err != REG_OK )
      MsgBox( "Не определен ключ " ,ПутьНастройкиИспользованияVRSLПлатежномОрдере, " в настройках банка" );
      return 1;
   end;

   var select:string = "SELECT pmpaym.t_documentid, pmlink.t_initialpayment " +
                         "FROM dpmpaym_dbt pmpaym, dpmrmprop_dbt rmprop, dpmlink_dbt pmlink " +
                        "WHERE pmpaym.t_valuedate BETWEEN :Date_In AND :Date_Out " + 
                          "AND rmprop.t_shifroper = '16' " +
                          "AND rmprop.t_paymentid = pmpaym.t_paymentid "+
                          "AND pmlink.t_purposepayment = pmpaym.t_paymentid "+
                          "AND pmlink.t_linkkind  = :LinkKind " +
                          "AND pmlink.t_purposepayment <> pmlink.t_initialpayment "; 
   
   var params:TArray = makeArray( SQLParam( "Date_In"  , Date_In  ),
                                  SQLParam( "Date_Out" , Date_Out ),
                                  SQLParam( "LinkKind" , PMLINK_KIND_KVITING ) );
   if( Oper != 0 )
     select = select + "AND pmpaym.t_oper = :Oper ";
     params[params.size] = SQLParam( "Oper" , Oper );
   end;
   if( NumDprt != 0 )
     select = select + "AND pmpaym.t_startdepartment = :NumDprt ";
     params[params.size] = SQLParam( "NumDprt" , NumDprt );
   end;
   select = select + "ORDER BY pmpaym.t_oper, pmpaym.t_paymentid ";

   var rs:RsdRecordset = execSQLselect( select, params, TRUE );
   while( rs and rs.moveNext() )
     PartPmpaym.PaymentID = PartPmprop.PaymentID = PartPmrmprop.PaymentID = rs.value(0);
     PrimPmpaym.PaymentID = PrimPmprop.PaymentID = PrimPmrmprop.PaymentID = rs.value(1);
     GetEq( PrimPmpaym );  
     GetEq( PrimPmprop );  
     GetEq( PrimPmrmprop );
     GetEq( PartPmpaym );  
     GetEq( PartPmprop );  
     GetEq( PartPmrmprop );
     
     if ( _PSPO_print_i2 ( GroupPrint, Use_VRSL, ErrStr,
                           PrimPmpaym,
                           PrimPmprop,
                           PrimPmrmprop,
                           PartPmpaym, 
                           PartPmprop, 
                           PartPmrmprop  ) != 0 )
       msgbox(ErrStr);
     end;
   end;
 END;
