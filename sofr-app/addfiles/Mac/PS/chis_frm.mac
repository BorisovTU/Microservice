/* ╔══════════════════════════════════════════════════════════════════╗ */
/* ║        Автоматизированная банковская система RS-Bank v5.0        ║ */
/* ║                Copyright (c) R-Style Software Lab 1998           ║ */
/* ║                                                                  ║ */
/* ║ Имя файла    : chiss_categ.mac                                   ║ */
/* ║                                                                  ║ */
/* ║ Описание     : Шаг "Оформление балнка" заяв. на выд. цен. бл.    ║ */
/* ║                                                                  ║ */
/* ║                                                                  ║ */
/* ║ Программист  : Локтюшин В.Ю.                                     ║ */
/* ║                                                                  ║ */
/* ║ Создан       : 14.09.2006                                        ║ */
/* ║                                                                 ║ */
/* ╚══════════════════════════════════════════════════════════════════╝ */


import PSInter, chis_categ, chisfun, InsCarryDoc, OprInter, payconst, payinter, pm_categ;

var CheckIssObj:RsbCheckIss;

/* ╔═════════════════════════════════╗ */
/* ║        Выполнение шага          ║ */
/* ╚═════════════════════════════════╝ */
MACRO ExecuteStep( doc, paymDoc )
   
   CheckIssObj.RegistrationDate = {curdate};

   var FD:CheckissFirstDoc = CheckissFirstDoc( CheckIssObj.OrderID );
   var ВнебалСчетУчетаЧековыхКнижек:string = FD.FindSysAccount( "СчетБланков", 0 );
   var ВнебалСчетУчетаВыданныхКнижек:string = FD.FindAndOpenSysAccount( "СчетБланкОформ", 0 );

   var FD_CorrAcc:NotBalCorrAcc_FirstDoc = NotBalCorrAcc_FirstDoc( "П" );
   var СистемныйСчетКорреспонденцииЧековыхКнижек:string = FD_CorrAcc.FindAndOpenSysCurAccount( "ВнебалСчетКорресп", 0, {curdate}, FD.CalcFIID );

   if( strlen(ВнебалСчетУчетаЧековыхКнижек) == 0 )
     msgbox("Не задан внебалансовый счет|учета чековых книжек");
     return 1;
   end;

   if( strlen(ВнебалСчетУчетаВыданныхКнижек) == 0 )
     msgbox("Не задан внебалансовый счет|учета выданных оформленных книжек");
     return 1;
   end;

/*****************************************************
Списание с внебаланса
*****************************************************/

    var paymtr1:object     = RsbAccTransaction();
    var paymtr2:object     = RsbAccTransaction();

    if( (paymtr1 == NULL) OR (paymtr2 == NULL) )
      MsgBox("Ошибка при создании проводки по платежу");
      return 1;
    end;

    paymtr1.Chapter         = 3;                                            
    paymtr1.FIID            = FD.CalcFIID;
    paymtr1.AccountPayer    = СистемныйСчетКорреспонденцииЧековыхКнижек;
    paymtr1.AccountReceiver = ВнебалСчетУчетаЧековыхКнижек;
    paymtr1.Sum             = FD.BooksAmount;
    paymtr1.Ground          = "Выдача из хранилища "          + string(GetOperName(FD.Oper)) + 
                             " бланков чековых книжек серии " + string(FD.Series)   + 
                             ", номера с "                   + string(FD.NumberFirst) + 
                             " по "                           + string(FD.NumberLast);
    if( not paymtr1.Carry )
      MsgBox("Ошибка при актуализации платежа");
      return 1;
    end;  

    paymtr2.Chapter         = 3;                                            
    paymtr2.FIID            = FD.CalcFIID;
    paymtr2.AccountPayer    = ВнебалСчетУчетаВыданныхКнижек;
    paymtr2.AccountReceiver = СистемныйСчетКорреспонденцииЧековыхКнижек;
    paymtr2.Sum             = FD.BooksAmount;
    paymtr2.Ground          = "Оформленная чековая книжка к счету " + string(FD.Account) + 
                                 " " + GetClientName( FD.Account ); 
    if( not paymtr2.Carry )    
      MsgBox("Ошибка при актуализации платежа");
      return 1;
    end;  

     if( not InsertOprStatus( CHECKISS_KIND_ST_DO, CHECKISS_ST_DO_REGDISP ) )
       msgbox("Ошибка при установке сегментов статуса экземпляра операции"); 
       return -1;                                                            
     end;                                                                    


     return 0;

END;
