/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : psiwpdsc.mac
  Created     : 19.02.2008
  Programmer  : Осмаковский Е.В.
  Description : Печать отчета сканирования о массвом изъятии/исполнении 
                платежа картотеки ОР

└───────────────────────────────────────────────────────────────────────────*/
IMPORT  PaymInter, OprInter, pm_opr;

const   ПечатьЗаписи = 0,
        Первый       = 1,
        Последний    = 2;

MACRO Печать_ОтчетСканирования (Вызов,firstdoc,Статус,Сообщение)
   
        record pm_rmprop (pmrmprop);
        record pm_paym   (pmpaym);

        var PaymIndexStatusValue = -1; 

        if (Вызов==Первый)
[              Отчет о выполнении массового изъятия/исполнения платежа             ];
[                        картотеки ожидания разрешений                             ];
[┌─────────┬─────────────┬──────────┬──────┬──────────────────────────────────────┐];
[│  Номер  │    Сумма    │   Дата   │Номер │              Сообщение               │];
[│документа│             │          │ошибки│                                      │];
[├─────────┼─────────────┼──────────┼──────┼──────────────────────────────────────┤];
        elif (Вызов == ПечатьЗаписи)

          if ( FindPayment( GetLastDocumentID(), 
                            0,    
                            0,    
                            0,    
                            0,
                            true,   /* Поиск по реальной базе       */
                            pm_paym,
                            null,
                            null,
                            pm_rmprop
                            ) != 0 )

            Статус = 1;
            Сообщение = "Не найден платеж";
          end;

          if (Статус == 0)
            if( PM_GetOprStatus( pm_paym.DocKind, pm_paym.PaymentID, OPR_PAYM_INDEX, @PaymIndexStatusValue) )
              if( ( PaymIndexStatusValue == OPR_PAYM_ST_INDEX_NO ) or
                  ( PaymIndexStatusValue == OPR_PAYM_ST_INDEX_WAIT ) )
                Сообщение = "Документ успешно списан с картотеки";
              elif( ( PaymIndexStatusValue == OPR_PAYM_ST_INDEX_2 ) or
                    ( PaymIndexStatusValue == OPR_PAYM_ST_INDEX_I2MEET ) )
                Сообщение = "Документ на неоплаченную сумму помещен в картотеку №2";
              elif( PaymIndexStatusValue == OPR_PAYM_ST_INDEX_WP_MEET )
                Сообщение = "Документ частично оплачен. Неоплаченная сумма осталась на картотеке ОР";
              end;
            end;           
          end;
[│#########│#############│##########│######│######################################│](pm_rmprop.Number,pm_paym.Amount,pm_rmprop.Date,Статус,StrSubst(Сообщение, "|", " " ):w);
                
        elif (Вызов==Последний)
[└─────────┴─────────────┴──────────┴──────┴──────────────────────────────────────┘];
        end;

END;
