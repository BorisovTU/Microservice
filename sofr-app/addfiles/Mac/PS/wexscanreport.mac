/*
$Name:           wexscanreport.mac
$Module:         РКО юридических лиц
$Description:    Протокол сканирования картотеки ОИ
*/                                                                            

import payinter, "d_inter.mac";

macro WexScanReportPrintHeader( Oper:integer )

[Протокол процедуры сканирования очереди распоряжений, ожидающих даты исполнения

 # 
 Дата отчета: ##########                                                                    ]
( {Name_Bank}, {curdate} );

[Операционист: #### # ]( {oper}, {Name_Oper} );
end;

macro WexScanReportPrintHeadOk()
[Распоряжения, исполненные успешно:];
[  ────────────┬──────────┬──────────────────┬─────────────────────────┬──────────────────────────────────────────┬─────────────────────────┬─────────────────────────────────────────┬──────────────
       №       │  Дата    │   Сумма          │Счет плательщика         │        Плательщик                        │ Счет получателя         │  Банк получателя                        │   БИК банка    
   Распоряжения│ значения │ Распоряжения     │                         │                                          │                         │                                         │   получателя   
   ────────────┴──────────┴──────────────────┴─────────────────────────┴──────────────────────────────────────────┴─────────────────────────┴─────────────────────────────────────────┴──────────────];
end;

macro WexScanReportOk( PaymentID:integer )
  var query :string = " select rm.t_number, pm.t_valuedate, pm.t_FutureBaseAmount, pm.t_PayerAccount,"
                      " rm.t_PayerName, pm.t_ReceiverAccount, rm.t_ReceiverBankName, pr.t_BankCode   "
                      " from dpmrmprop_dbt rm, dpmpaym_dbt pm, dpmprop_dbt pr"
                      " where rm.t_paymentid = :paymentID "
                      " AND rm.t_paymentid = pm.t_paymentid        "
                      " AND rm.t_paymentid = pr.t_paymentid        "
                      " and pr.t_issender <> 'X'                   ";
  var params:TArray = makeArray( SQLParam( "paymentid", PaymentID ));
  
  var rs:RsdRecordset = execSQLselect( query, params );
  while( rs.moveNext() )
  
[  ##########   ########## ##################  #########################  ######################################### ######################### ######################################## ########## ]
( rs.Value(0),
  date(rs.Value(1)), 
  rs.Value(2), 
  rs.Value(3),
  rs.Value(4),
  rs.Value(5),
  rs.Value(6),
  rs.Value(7));
  end;  
end;  

macro WexScanReportPrintHeadErr()
[
Распоряжения, исполненные с ошибками:];
[  ────────────┬──────────┬──────────────────┬─────────────────────────┬──────────────────────────────────────────┬───────────────────────────────────────────────────
       №       │  Дата    │   Сумма          │Счет плательщика         │Плательщик                                │ Результат обработки         
   Распоряжения│ значения │ Распоряжения     │                         │                                          │                                                   
   ────────────┴──────────┴──────────────────┴─────────────────────────┴──────────────────────────────────────────┴───────────────────────────────────────────────────];
end;

macro WexScanReportErr(PaymentID:integer, Msg:string)
  var query :string = " select rm.t_number, pm.t_valuedate, pm.t_FutureBaseAmount, pm.t_PayerAccount,  rm.t_PayerName, pm.t_dockind"
                      " from dpmrmprop_dbt rm, dpmpaym_dbt pm"                     
                      " where rm.t_paymentid = :paymentID "
                      " AND rm.t_paymentid = pm.t_paymentid ";

  var paramserr:TArray = makeArray( SQLParam( "paymentid", PaymentID ));
  
  var rserr:RsdRecordset = execSQLselect( query, paramserr );
  while( rserr.moveNext() )
  
[  ##########   ########## ##################  #########################  ######################################### ################################################]
( rserr.Value(0),
  date(rserr.Value(1)), 
  rserr.Value(2), 
  rserr.Value(3),
  rserr.Value(4),
  StrSubst(Msg +". "+ GetStepNameExecuteRead(PaymentID, rserr.Value(5)), "|", " " ):w ) ;
  end;  
end;

macro WexScanReportPrintCountOk(count:integer)
[
Итого успешно исполненных: #######](count);
end;

macro WexScanReportPrintCountErr(count:integer)
[
Итого с ошибками: #######](count);
end;














