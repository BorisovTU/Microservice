/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.10                                         R-Style Software Lab

  File Name   : pspoprn.mac
  Created     : 26.04.01
  Description : Печать отчета сканирования открытых платежных документов

└───────────────────────────────────────────────────────────────────────────*/
IMPORT  PaymInter, OprInter;

RECORD pspo_firstdoc (pspayord);

const   Ошибка   = 0,
        Первый   = 1,
        Последний= 2;

MACRO Печать_ОтчетСканирования (Вызов,firstdoc,Статус,Сообщение)
   var stat;
   array StringArray;
   var i = 1;

        record pm_rmprop (pmrmprop);
        record pm_paym   (pmpaym);

        setbuff (pspo_firstdoc,firstdoc);

        if (Вызов==Первый)
[    Отчет о групповой печати платежных документов.   ];
[+---------+------+------------------------------------------------------------+];
[|  Номер  |Номер |                 Сообщение                                  |];
[|документа|ошибки|                                                            |];
[+---------+------+------------------------------------------------------------+];
        elif (Вызов==Ошибка)

                if (Статус == 0)
                        Сообщение = "Печать выполнена успешно";
                end;
                if ( FindPayment(
                        0,      /* Индентификатор неизвестен    */
                        PM_PURP_POPRIMARY,      /* Основной по клиентской платежке */
                        0,      /* Subpurpose                   */
                        PS_PAYORDER, /* Вид первичного документа */ 
                        pspo_firstdoc.OrderID,
                        true,   /* Поиск по реальной базе       */
                        pm_paym,
                        null,
                        null,
                        pm_rmprop
                        )       !=      0 )

                        Статус = 1;
                        Сообщение = "Не найдено платежное поручение";
                end;
         StrSplit(StrSubst(Сообщение, "|", " " ), StringArray, 56);                
[|#########|######|############################################################|](pm_rmprop.Number,Статус,StringArray(0));
         while ( StrLen(StringArray(i) ) > 0)
[|         |      |############################################################|](StringArray(i));
           i = i + 1;
         end;

        elif (Вызов==Последний)
[+---------+------+------------------------------------------------------------+];
        end;
END;