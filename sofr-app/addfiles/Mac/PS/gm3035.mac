/*
$Name:             gm3035.mac
$Module:           РКО юридических лиц
$Description:      Операция - 3035 - "Закрытие лицевого счета"
                   Шаг      - 70   - "Формирование сообщения о закрытии счета" 
*/

IMPORT reqinter, PSInter, InsCarryDoc, CTInter;

RECORD req( reqclosa );

var TB_ReqClosAcc = TBFile("reqclosa.dbt",  "R", 0, "reqclosa.dbt",  "bank.def");


var ReqCloseAccObj:RsbReqCloseAcc;

MACRO ExecuteStep(doc, pmdoc)
  var stat : integer = 0;
  TB_ReqClosAcc.rec.RequestID = ReqCloseAccObj.RequestID;

  if( TB_ReqClosAcc.GetEQ() )
    Copy( req, TB_ReqClosAcc);
  end;

  stat = InsertCloseAccountMessage( req );

  if( stat == ОтказОтГенерацииСообщения )
    return 1;
  end;

  if( stat != 0 )
    DisplayError();
    MsgBox( "Ошибка при создании сообщения о закрытии счета " + ReqCloseAccObj.Account );
    return 1;
  end;

  return stat;
END;

MACRO PostStep( message,     /* данный параметр может принимать следующие       */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                FirstDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                ID_Step,     /* внутренний идентификатор шага операции          */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep )   /* вид шага операции                               */

  var stat = 0;

  Array Text, Buttons;
  Text(0) = "Печатать сообщения?";
  Buttons( 0 ) = "Да";
  Buttons( 1 ) = "Нет";
  var CopyCount = 0;

  if( ( errTrn == 0 ) and ( message == 1 ) )// на выполнении шага
    if( not IsOprMultiExec and 
        (ConfWin( Text, Buttons, 1 ) == 0)
      )
      SetKindRegOrgan( REG_ORGAN_KIND_FNS );
      stat = ReqAccPrintMessage(ReqCloseAccObj.RequestID,
                                OBJTYPE_REQCLOSA );
      if( stat == 0 )
        SetKindRegOrgan( REG_ORGAN_KIND_PFR );
        stat = ReqAccPrintMessage(ReqCloseAccObj.RequestID,
                                  OBJTYPE_REQCLOSA
                                 );
      end;
      if( stat == 0 )
        SetKindRegOrgan( REG_ORGAN_KIND_FSS );
        stat = ReqAccPrintMessage(ReqCloseAccObj.RequestID,
                                  OBJTYPE_REQCLOSA
                                 );
      end;
      SetKindRegOrgan( REG_ORGAN_KIND_NON );
    end;
  end;
  return stat;
END;