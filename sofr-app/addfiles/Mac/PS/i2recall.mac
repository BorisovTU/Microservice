/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : i2recall.mac
  Created     : 13.05.2004
  Description : Печать отчета сканирования массового отзыва
                платежных документов из картотеки 2

└───────────────────────────────────────────────────────────────────────────*/
IMPORT  PaymInter, OprInter, BankInter;

//RECORD pspo_firstdoc (pspayord);
RECORD ps_psinprop (psinprop);

const   Ошибка   = 0,
        Первый   = 1,
        Последний= 2;

MACRO Печать_ОтчетСканирования (Вызов,firstdoc,Статус,Сообщение)
   record pm_rmprop (pmrmprop);
   record pm_paym   (pmpaym);
   var stat;
   array StringArray;
   var i = 1;
   var PayOrderObj:RsbPsPayOrder;

        setbuff (ps_psinprop,firstdoc);

        if (Вызов==Первый)
[        Отчет о групповом отзыве платежных документов из картотеки 2        ];
[+---------+------+------------------------------------------------------------+];
[|  Номер  |Номер |                 Сообщение                                  |];
[|документа|ошибки|                                                            |];
[+---------+------+------------------------------------------------------------+];
        elif (Вызов==Ошибка)

                if (Статус == 0)
                        Сообщение = "Документ успешно отозван";
                end;
                if ( FindPayment(
                        0,      /* Индентификатор неизвестен    */
                        PM_PURP_POPRIMARY,      
                        0,      /* Subpurpose                   */
                        PS_PAYORDER, /* Вид первичного документа */ 
                        ps_psinprop.PaymentID,
                        true,   /* Поиск по реальной базе       */
                        pm_paym,
                        null,
                        null,
                        pm_rmprop
                        )  !=  0 )

                        Статус = 1;
                        Сообщение = "Не найден платежный документ";
                end;
                // Ошибки не было, но документ не отозван - значит его пропустили
                if( ( Статус == 0 ) and ( pm_paym.FuturePayerAmount != 0) and ( pm_paym.PaymentID != 0 ) )
                  PayOrderObj = GenObject( "RsbPsPayOrder", pm_paym.PaymentID );
                  if( PayOrderObj.Origin == PSPO_OR_SF )
                    Сообщение = "Документ на оплату комиссий банка пропущен пользователем";
                end;
                end;
         StrSplit(StrSubst(Сообщение, "|", " " ), StringArray, 56);
[|#########|######|############################################################|](pm_rmprop.Number,Статус,StringArray(0));
         while ( StrLen(StringArray(i) ) > 0)
[|         |      |############################################################|](StringArray(i));
           i = i + 1;
         end;

        elif (Вызов==Последний)
[+---------+------+------------------------------------------------------------+];
        end;
END;