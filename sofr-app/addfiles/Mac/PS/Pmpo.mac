/*
  $Name:         pmpo.mac
  $Module:       Banking
  $Description:  Зачисление платежным ордером
*/

//-----------------------------------------------------------------------------
// Блок      : 29058 - "Зачисление платежным ордером"
// Шаг       : 10    - "Зачисление платежным ордером"
// Назначение: Макрос шага
// Описание  : Макрос шага
//-----------------------------------------------------------------------------
import PaymInter, pm_categ, pm_common, pm_setst, cbsttls, catfdoc, "mpckvit.mac", payinter;

var PaymentObj : RsbPayment;

macro ExecuteStep( doc, paymDoc, DocKind:integer, ID_Operation:integer, ID_Step:integer )

  var ClaimID    :integer =  0; /* ID претензии      */
  var ClaimAmount:money   = $0; /* сумма претензии   */
  var ClaimStatus:integer =  0; /* статус претензии  */

  if( not MakePartOrders( PaymentObj.PaymentID, PaymentObj.DocKind, PaymentObj.FuturePayerAmount ) )
    msgbox("Ошибка создания частичного платежа");
    return 1;
  end;
  
  var PartPayments:TArray = PaymentObj.PartPayments(true);
  var NewPayOrderObj = GenObject( "RsbPsPayOrder", PartPayments[0].PaymentID );

  // это будет платежный ордер
  NewPayOrderObj.Payment.ShifrOper = "16";
  NewPayOrderObj.Payment.NumberPack = 0;

  // Автоматический запуск операции и привязка к шагу
  NewPayOrderObj.LaunchOper    = true;
  NewPayOrderObj.ConnectToOper = true;

  // Перепривязка резерва родительского платежа
  if( GetReserveInfo(PaymentObj.DocKind, PaymentObj.PaymentID, PaymentObj.ValueDate, @ClaimID, @ClaimAmount, @ClaimStatus) == 0 )
    if( NewPayOrderObj.Payment.BindReserve( ClaimID, NewPayOrderObj.Payment.Number, true ) )
      msgbox( "Ошибка при перепривязке резерва к дочернему документу" );
      return 1;
    end;
  end;

  // Если основной платеж из модуля "Проценты" - свяжем с ним планируемую оплату
  if( IsPrcPayment( PaymentObj ) )
    if( PrcKvitLinkPayments( PaymentObj.PaymentID, NewPayOrderObj.PaymentID ) )
      return 1;
    end;
  end;

  PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_CLOSED );
  PaymentObj.PaymStatus = PM_FINISHED; 
  if( УстановитьСтатусыПлатежа(OPR_PAYM_STATE, OPR_PM_ST_CLOSE) )
    msgbox("Ошибка при установке сегментов статуса экземпляра операции");
    return 1;
  end;

  return 0;

end;

