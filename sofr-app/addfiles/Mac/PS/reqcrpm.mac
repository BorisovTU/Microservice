/* -------------------------------------------------------------------- */
/*         Автоматизированная банковская система RS-Bank v6.0           */
/*                 Copyright (c) R-Style Software Lab                   */
/*                                                                      */
/*  Имя файла  : rqchcs.mac                                             */
/*                                                                      */
/*  Описание   : Операция - 3035 - "Закрытие лицевого счета"            */
/*               Шаг      - 10   - "Перевод остатка"                    */
/*                                                                      */
/*  Программист  : Дудин В.А.                                           */
/*                                                                      */
/*  Создан       : 20.07.00                                             */
/*                                                                      */
/*  SMR 18.11.02 : Формирование платежа заменено на формирование        */
/*                 отложенного платежа ББ                               */
/* -------------------------------------------------------------------- */
IMPORT globals, CurrInter, InsCarryDoc, PaymInter, OprInter, PSInter, reqinter;

var ReqCloseAccObj:RsbReqCloseAcc;

/* ╔═════════════════════════════════╗ */
/* ║        Выполнение шага          ║ */
/* ╚═════════════════════════════════╝ */
MACRO ExecuteStep(doc, reqacc)
  var stat:integer = 0,
      rest:moneyl,
      facc:TBFile;
  var   Refer      = "";
  var   RefID = 0;

  var Payment:RsbPayment   = NULL;
  var BankPaym:object      = NULL;

  /*запрос на закрытие счета с арестом на дебет*/
  Array Text, Buttons;
  Text(0) = "На счет " + ReqCloseAccObj.Account + " наложен арест на дебет.|Продолжить операцию закрытия счета?";
  Buttons( 0 ) = "     Да     ";
  Buttons( 1 ) = "    Нет     ";

  var rset:RsdRecordset = NULL;

  /* Получение остатка на счете */
  if( ReqCloseAccObj.Code_Currency )
    rest = RestAC( ReqCloseAccObj.Account, ReqCloseAccObj.Code_Currency );
  else
    rest = RestA( ReqCloseAccObj.Account );
  end;

  if( not stat and rest )
    rset = RSetSelectArestSpecialClaimAccount( "acclaim.t_ClaimKind", ReqCloseAccObj.Account, ReqCloseAccObj.Code_Currency );
    if( rset and rset.moveNext() )
      if( rset.value(0) == 1 )    /*ACCLAIM_KIND_ARREST*/
        MsgBox("На счет ", ReqCloseAccObj.Account, " наложен арест.|Закрытие счета невозможно.");
        return 1;
      elif( rset.value(0) == 2 )  /*ACCLAIM_KIND_SPECIAL*/
        MsgBox("На счете ", ReqCloseAccObj.Account, " есть средства,|подлежащие целевому использованию.|Закрытие счета невозможно.");
        return 1;
      end;                                                                                  
    end;
    if( Index( GetTypeAccount( 1/*CHAPT1*/, ReqCloseAccObj.Account, ReqCloseAccObj.Code_Currency ), "Т" ) )
      if( GetDialogFlag() )
        if( ConfWin( Text, Buttons ) == 0/*Да*/ )
          ChangeAccountType( 1/*CHAPT1*/, ReqCloseAccObj.Code_Currency, ReqCloseAccObj.Account, ACCTYPE_CHANGE_MODE_DEL, "Т" );
        else
          return 1;
        end;
      else 
        MsgBox( "На счет ", ReqCloseAccObj.Account, " наложен арест на дебет");
        return false;
      end;
    end;
  end;

  if( ( not stat ) and rest and ReqCloseAccObj.Rest ) 
    
    if( ReqCloseAccObj.Code_Currency == 0 )
    
      BankPaym              = GenObject( "RsbBankPayment", 0 );
      Payment               = BankPaym.Payment();
      
      BankPaym.Origin       = MEMORDER_FDOC_CLOSTRANS;
      BankPaym.Oper         = {oper}; 
      BankPaym.Status       = 1/*MEMORDER_STATUS_POST*/;

      Payment.DocKind       = DLDOC_BANKPAYMENT;
      Payment.Purpose       = PM_PURP_BANKPAYMENT;
      Payment.PayerFIID     = 
      Payment.ReceiverFIID  = ReqCloseAccObj.Code_Currency;
    else
      BankPaym              = GenObject( "RsbBbCpOrder", 0 );
      Payment               = BankPaym.Payment();

      BankPaym.Origin       = CP_OR_CLOSTRANS;
      BankPaym.Oper         = {oper}; 
      BankPaym.CurrentState = 0/*CP_ST_DEFERRED*/;
                                  
      Payment.BaseFIID      =
      Payment.PayerFIID     = 
      Payment.ReceiverFIID  = ReqCloseAccObj.Code_Currency;
      Payment.DocKind       = BBANK_CPORDER;
      Payment.Purpose       = PM_PURP_BANKPAYMENT;
      Payment.ComissCharges = PM_CHRG_SHA;
      end;


    if( GetReferenceIDByType( 
          OBJTYPE_BANKPAYMENT, /*ObjectType = 600; платеж банка*/
          REFOBJ_BANKPAYMENT,  /*RefType = 1;*/
          RefID ) != 0 )
       MsgBox( "Ошибка при генерации платеж банка" );
       return 1;
    end;

    if( GenerateReference( RefID, Payment.Number ) != 0 )
       MsgBox( "Ошибка при генерации платеж банка" );
       return 1;
    end;
    
    Payment.PaymentKind              = "Э";
    Payment.ShifrOper                = "01";

    Payment.BaseAmount               =
    Payment.PayerAmount              =
    Payment.ReceiverAmount           = rest;

    Payment.PayDate                  =
    Payment.ClientDate               = {curdate};

    if( ReqCloseAccObj.AccCloseDate != date( 0, 0, 0 ) )
      Payment.Date                     =
      Payment.ValueDate                = ReqCloseAccObj.AccCloseDate;
    else
      Payment.Date                     =
      Payment.ValueDate                = {curdate};
    end;    
    
    stat = Payment.SetPayerPI( PAYMENTS_GROUP_UNDEF, 
                               {OurBank}, 
                               0, 
                               "", 
                               "",
                               "",
                               ReqCloseAccObj.Code_Currency, 
                               1/*CHAPT1*/, 
                               ReqCloseAccObj.Account, 
                               0, 
                               "", 
                               "" );
    if( stat )
       MemoryError( stat );
       msgbox( GetErrMsg() );
       return 1;
    end;

    stat = Payment.SetReceiverPI( PAYMENTS_GROUP_UNDEF, 
                                  0, 
                                  ReqCloseAccObj.RestBankCodeKind,   
                                  ReqCloseAccObj.RestBankCode, 
                                  ReqCloseAccObj.RestBankName,
                                  ReqCloseAccObj.RestBankCorrAcc,
                                  ReqCloseAccObj.Code_Currency, 
                                  1/*CHAPT1*/, 
                                  ReqCloseAccObj.RestAccount, 
                                  0, 
                                  ReqCloseAccObj.RestName, 
                                  ReqCloseAccObj.RestINN );
    if( stat )
       MemoryError( stat );
       msgbox( GetErrMsg() );
       return 1;
    end;
    
    Payment.Ground                   = "Перевод остатка с закрываемого счета";

    // Установить признак автозапуска операции.
    BankPaym.LaunchOper              = true;
    BankPaym.ConnectToOper           = true;
    Payment.CryptoAction( string("Автоматическое_формирование_платежей") );

  end;

  return stat;

END;

