/*
 $Name: psbcexe1.mac
 $Module: PS 
 $Description: Макрос шага. Исполнение за счет собств. средств. Исполнение
*/

//-----------------------------------------------------------------------------
// Блок     : "Исполнение за счет собств. средств"
// Шаг      : "Исполнение"
// Описание : Макрос шага
//-----------------------------------------------------------------------------

import PaymInter, PSInter, OprInter, BankInter, psbccomn, bc_categ, pm_opr, pm_common, pm_setst, pm_tools;

var PaymentObj:RsbPayment;

//-----------------------------------------------------------------------------
// Создать мемордер
//-----------------------------------------------------------------------------
PRIVATE MACRO CreateMemorial( buyorder:object, ReceiverAccount:string )

  var Memorial:object = GenObject("RsbMemorialOrder", 0 );
  var Payment:RsbMOPayment = Memorial.Payment();
  var SkipArest, Status;

  Memorial.State             = 0;/*CB_DOC_STATE_DEFERRED*/;
  Memorial.Oper              = {oper};
  Memorial.Chapter           = 1;
  Memorial.Code_Currency     = PaymentObj.PayerFIID;

  Memorial.Kind_Oper         = " 4";
  Memorial.Origin            = CB_DOC_ORIGIN_MANUAL;

  Payment.ClientDate         =
  Payment.Date               =
  Payment.PayerBankEnterDate =
  Payment.ValueDate          = {curdate};
                       
  Payment.DocKind            = DLDOC_MEMORIALORDER;
  Payment.Purpose            = PM_PURP_MEMORDER;

  Payment.Number             = PaymentObj.Number;
  Payment.ShifrOper          = "09";

  Payment.PayerAmount        = PaymentObj.PayerAmount;
  Payment.PayerFIID          = 
  Payment.ReceiverFIID       = PaymentObj.PayerFIID;

  Payment.SetPayerPI( PAYMENTS_GROUP_UNDEF, 
                      PaymentObj.PayerBankID, 
                      PaymentObj.PayerBankCodeKind, 
                      PaymentObj.PayerBankCode, 
                      PaymentObj.PayerBankName,
                      PaymentObj.PayerBankCorrAcc,
                      PaymentObj.PayerFIID, 
                      1/*CHAPT1*/, 
                      PaymentObj.PayerAccount,                                
                      PaymentObj.Payer, 
                      PaymentObj.PayerName, 
                      PaymentObj.PayerINN );

  Payment.SetReceiverPI( PAYMENTS_GROUP_UNDEF, 
                         {OurBank}, 
                         0, 
                         "", 
                         "",
                         "",
                         PaymentObj.PayerFIID, 
                         1/*CHAPT1*/, 
                         ReceiverAccount, 
                         0, 
                         "", 
                         "" );
  // Передача очередности дочернему документу
  Payment.Priority = PaymentObj.Priority;
                    
  Payment.Ground             = BC_KindOrderName(buyorder.BCOrdKind) + " № " + PaymentObj.Number + " от " + PaymentObj.Date + ". Списание средств для исполнения поручения.";
  
  // Заполнение кода вида валютной операции
  BC_SetVOCode( buyorder, Payment, PSBCACTION_CONV );

  // Установить мемориальному ордеру признак автозапуска операции.
  Memorial.LaunchOper = true;

  // Претензию резервирования поручения на п/п/к перепривязать к созданному мемориальному ордеру
  Payment.BindReserve( GetClaimID( PaymentObj, PaymentObj.PayerAccount, PaymentObj.Chapter, PaymentObj.PayerFIID ), 
                       PaymentObj.Number, true );

  if( PM_GetOprStatus( Payment.DocKind, Payment.DocumentID, OPR_PAYM_PERMISSION, @Status ) and
      (Status == OPR_PAYM_ST_PERMISSION_YES)
    )
    SkipArest = true;
  else
    SkipArest = false;
  end;

  Payment.CryptoAction( string("Автоматическое_формирование_платежей") );
END;

MACRO ExecuteStep( doc, paymDoc )
  
  var BuyOrder:object         = GenObject("RsbBuyCurrencyOrder", PaymentObj.DocumentID);
 
  var FD:BuyCurOrder_FirstDoc = BuyCurOrder_FirstDoc(PaymentObj.DocumentID);
  
  var МинусСчетКонвКл:string = FD.FindAndOpenAccount( "-СчетКонвКл", 0, {curdate}, FIROLE_FICOM );
  var ПлюсСчетКонвКл:string  = FD.FindAndOpenAccount( "+СчетКонвКл", 0, {curdate}, FIROLE_FIREQ );

  var paymtr:RsbPaymTransaction = PaymentObj.MakeTransaction();
  if( paymtr == NULL )
    MsgBox("Ошибка при создании проводки по платежу");
    return 1;
  end;

  // Сформировать мультивалютную проводку
  paymtr.Chapter         = PaymentObj.Chapter;
  paymtr.Date_Carry      = PaymentObj.ValueDate;
  paymtr.Number_Pack     = PaymentObj.NumberPack;
  paymtr.Numb_Document   = PaymentObj.Number;
  paymtr.ResultCarry     = 1;
  paymtr.Kind_Oper       = " 1";
  paymtr.Department      = PaymentObj.Department;

  paymtr.FIIDPayer       = PaymentObj.PayerFIID;
  paymtr.AccountPayer    = МинусСчетКонвКл;
  paymtr.SumPayer        = PaymentObj.PayerAmount;

  paymtr.FIIDReceiver    = Paymentobj.ReceiverFIID;
  paymtr.AccountReceiver = ПлюсСчетКонвКл;
  paymtr.SumReceiver     = Paymentobj.ReceiverAmount;

  paymtr.Ground          = BC_KindOrderName(BuyOrder.BCOrdKind) + " № " + PaymentObj.Number + " от " + PaymentObj.Date + ". Формирование требований/обязательств.";

  if( not paymtr.Carry() )
    msgbox("Ошибка при актуализации платежа");
    return 1;
  end;

  // Сформировать мемориальный ордер
  CreateMemorial( BuyOrder, МинусСчетКонвКл );
  
  BuyOrder.WriteOffAmount = PaymentObj.PayerAmount;

  //PaymentObj.FuturePayerAccount = ПлюсСчетКонвКл;

  return 0;

END;