/*
$Name:             psi1recl.mac
$Module:           РКО юридических лиц
$Description:      Блок     : 29019 - "Картотека 1" 
                   Шаг      : 10    - "Изъятие из картотеки 1"
                   Описание : Макрос шага
*/
import pm_common, pm_setst, payconst, payinter, "catfdoc.mac", cbsttls, "pm_answerret.mac", "mpckvit.mac";
var PaymentObj:RsbPayment;

var OpenBalansAccountK2 = 0;

private macro ValueDateForExtractFromInd1(Paym : RsbPayment) : date
  var ValueDate : date = {curdate};
  var DocKind = Paym.PrimDocKind;

  if( GetParentOrEqualDocKindFromList(DocKind, PMDOC_CLIENTPAYMENT) )
    ValueDate = PM_GetOperDay_BankServiceBalance(Paym.Department);
  end;

  return ValueDate;
end;

MACRO ExecuteStep( doc, paymDoc )

  // Объекты для КУ
  var FD_Index1 :PaymIndex1_FirstDoc    = PaymIndex1_FirstDoc( PaymentObj.PaymentID );
  var FD_CorrAcc:NotBalCorrAcc_FirstDoc = NotBalCorrAcc_FirstDoc( "П" );
  
  // Счета КУ
  var СистемныйСчетДляКартотек:string = FD_CorrAcc.FindAndOpenSysAccount( "ВнебалСчетКорресп", 0, {curdate} );
  var ВнебалСчетКартотеки1    :string = FD_Index1.FindAndOpenAccount( "Картотека 1", 0, PaymentObj.PayerBankEnterDate );

  var obj:RsbPsPayOrder = RsbPsPayOrder( PaymentObj.DocumentID );
  var DenialAmount = PaymentObj.DenialAmount;
  var AcceptAmount = PaymentObj.PayerAmount - PaymentObj.DenialAmount;
  var СостояниеАкцепта: integer;
  file inhist(pminhist) key 1;

  inhist.PaymentID = PaymentObj.PaymentID;
  inhist.HistoryNum = 0;
 
  if( (AcceptAmount == PaymentObj.PayerAmount) and (not GetEQ(inhist)) )
    СостояниеАкцепта = 0; //акцепт
    PaymentObj.PayerAmount    = PaymentObj.FuturePayerAmount;
    PaymentObj.ReceiverAmount = PaymentObj.PayerAmount;
  elif( (AcceptAmount > 0) and ((DenialAmount > 0) or (GetEQ(inhist))) )
    СостояниеАкцепта = 1; //частичный акцепт
    PaymentObj.PayerAmount    = AcceptAmount;
    PaymentObj.ReceiverAmount = AcceptAmount;
  else
    СостояниеАкцепта = 2; //отказ
    PaymentObj.PayerAmount    = 0;
  end;

  PaymentObj.ValueDate = ValueDateForExtractFromInd1(PaymentObj);
  PaymentObj.PayDate = PaymentObj.ValueDate;
  PaymentObj.OutTransferDate = PmGetDefaultOutTransferDate( PaymentObj );

  if( DenialAmount != 0 )// Заполнить примечание причиной отказа
    if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, PaymentObj.DenialGround) != 0 )
      MsgBox("Ошибка при вставке примечания платежа");
    end;
  end;

  if( СостояниеАкцепта == 1 )
    obj.Accept = PSPAYDEM_ST_ACCEPT;
    //PaymentObj.PayerAmount = PaymentObj.FuturePayerAmount;
    var PartPayments: TArray = PaymentObj.PartPayments(true);
    for(var i, 0, PartPayments.size() - 1, 1)
      // Если основной платеж из модуля "Проценты" - свяжем с ним планируемую оплату
      if( IsPrcPayment( PaymentObj ) )
        if( PrcKvitLinkPayments( PaymentObj.PaymentID, PartPayments[i].PaymentID ) )
          return 1;
        end;
      end;
    
      if(PaymentObj.CheckTerror != CHT_NOTNEED)
        PartPayments[i].CheckTerror = CHT_NOTCHECK;
      end;
    end;
  end;

  // Выполнить проводку по внебалансу для акцепта
  if( СостояниеАкцепта < 2 )
    var paymtr_acc = PaymentObj.MakeTransaction( NULL, NULL, AcceptAmount, PaymentObj.PayerFIID );

    if( paymtr_acc == NULL )
      MsgBox("Ошибка при создании проводки по платежу");
      return 1;
    end;

    paymtr_acc.Chapter         = 3;                                            
    paymtr_acc.Date_Carry      = {curdate};;
    paymtr_acc.Number_Pack     = PaymentObj.NumberPack;
    paymtr_acc.Numb_Document   = PaymentObj.Number;
    paymtr_acc.ResultCarry     = 48;
    paymtr_acc.Kind_Oper       = " 1";
    paymtr_acc.Shifr_Oper      = "09";
    paymtr_acc.Department      = PaymentObj.Department;
    paymtr_acc.AccountPayer    = СистемныйСчетДляКартотек;
    paymtr_acc.AccountReceiver = ВнебалСчетКартотеки1;
    paymtr_acc.FIID            = PaymentObj.PayerFIID;
    paymtr_acc.Sum             = AcceptAmount;

    if (СостояниеАкцепта == 0 )
      paymtr_acc.Ground = "Списание с картотеки акцептованной суммы " + string(paymtr_acc.Sum) + 
                        " по документу № " + string(PaymentObj.Number) + 
                        " от "             + string(FD_Index1.GetDate()) + 
                        " к счету "        + string(PaymentObj.PayerAccount);
    else
      paymtr_acc.Ground = "Списание с картотеки акцептованной суммы " + string(paymtr_acc.Sum) + " из " + string(obj.ReqSum) + 
                        " по документу № " + string(PaymentObj.Number) + 
                        " от "             + string(FD_Index1.GetDate()) + 
                        " к счету "        + string(PaymentObj.PayerAccount);
    end;

    if( not paymtr_acc.Carry )
      MsgBox("Ошибка при актуализации платежа");
      return 1;
    end;  

  end;

  if( СостояниеАкцепта == 0 )
    // СП - открыт, ДО - предобработка, КАРТ - нет
    if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_OPEN, OPR_PAYM_DO, OPR_PM_ST_PREP, OPR_PAYM_INDEX, OPR_PAYM_ST_INDEX_NO ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;

    if( PM_WasChanged( PaymentObj.PaymentID ) )
      if( УстановитьСтатусыПлатежа( OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_NOTCONTROL ) )
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
    end;
    PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_WORKING );
    PaymentObj.PaymStatus = PM_READIED; 
    
    if(PaymentObj.CheckTerror != CHT_NOTNEED)
      PaymentObj.CheckTerror = CHT_NOTCHECK;
    end;
  else
    //Общая часть для частичного акцепта и отказа
    //Установить статус первичного документа родительского документа = $(закрыт)
    PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_CLOSED );
    //Установить статус платежа = PM_FINISHED
    PaymentObj.PaymStatus = PM_FINISHED;
    //Установить сегмент <Состояние платежа> = $(Закрыт)
    if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_CLOSE ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
  end;

  // Выполнить проводку по внебалансу для отказа
  var paymtr_den:object = NULL;

  if( DenialAmount != 0 )
    paymtr_den = PaymentObj.MakeTransaction( NULL, NULL, DenialAmount, PaymentObj.PayerFIID );
    if( paymtr_den == NULL )
      MsgBox("Ошибка при создании проводки по платежу");
      return 1;
    end;

    paymtr_den.Chapter         = 3;                                            
    paymtr_den.Date_Carry      = {curdate};;
    paymtr_den.Number_Pack     = PaymentObj.NumberPack;
    paymtr_den.Numb_Document   = PaymentObj.Number;
    paymtr_den.ResultCarry     = 49;
    paymtr_den.Kind_Oper       = " 1";
    paymtr_den.Shifr_Oper      = "09";
    paymtr_den.Department      = PaymentObj.Department;
    paymtr_den.AccountPayer    = СистемныйСчетДляКартотек;
    paymtr_den.AccountReceiver = ВнебалСчетКартотеки1;
    paymtr_den.FIID            = PaymentObj.PayerFIID;
    paymtr_den.Sum             = DenialAmount;

    paymtr_den.Ground = "Списание с картотеки неакцептованной суммы " + string(DenialAmount) + 
                        " из "                          + string(obj.ReqSum) + 
                        " по документу № "              + string(PaymentObj.Number) + 
                        " от "                          + string(FD_Index1.GetDate())  + 
                        " к счету "                     + string(PaymentObj.PayerAccount);

    if( not paymtr_den.Carry )
      MsgBox("Ошибка при актуализации платежа");
      return 1;
    end;  

  end;

  var PreObj:object;
  PreObj = GenObject( "RsbPreAccept", obj.PreacptID );
  if( obj.PreacptID != 0 )
    if( СостояниеАкцепта == 0 )
      PreObj.ActuateState();
    elif( СостояниеАкцепта == 1 )
      if( DenialAmount > 0 )
        PreObj.ActuateState( DenialAmount );
      else
        PreObj.ActuateState();
      end;        
    else
      PreObj.ActuateState( DenialAmount );
    end;
  end;

  // Установить дату начала операции равной дате операционного дня пользователя
  SetOprDate(29000000, PaymentObj.ValueDate);

  var PsOrder : RsbPsPayOrder = RsbPsPayOrder(PaymentObj.PaymentID);
  
  if ((DenialAmount == PsOrder.ReqSum) // отказ полный
       and
      (PsOrder.Origin == PSPO_OR_PAYEEBANK))
    var PaymStatusClaim, DocKindClaim, PaymentIDClaim;
    if ( GetESIDPrimPaym(PaymentObj.PaymentID,PaymStatusClaim, DocKindClaim, PaymentIDClaim ))
       if (ChoiseESIDProcesDoc(PaymStatusClaim, DocKindClaim, PaymentIDClaim))
         return 1;
       end;
    end;    
  end;
  
  //Здесь можно вставлять дополнительные проводки для шага

  return 0;
END;

macro PostStep( message,     /* данный параметр может принимать следующие       */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                FirstDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                Num_Step,    /* Номер шага операции (из настроек)               */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep,    /* вид шага операции                               */
                ID_Step )    /* внутренний идентификатор шага операции          */

  var stat = 0;

  if( ( errTrn == 0 ) and ( message == 1 ) )// на выполнении шага
    var DenialAmount = $0, DenialGround = "";
    GetDenialAmountGround(PaymentObj.PaymentID, PAYMENTS_INDEX_1, @DenialAmount, @DenialGround);
    if(DenialAmount > $0)
      var PsOrder : RsbPsPayOrder = RsbPsPayOrder(PaymentObj.PaymentID);
      var Narrative : string = "", Queries : string = "";
      if(DenialAmount == PsOrder.ReqSum) // отказ полный
        Queries = "InfoCode:3";
        Narrative = 
          "Отказ от акцепта плательщиком, уведомляем об отказе от акцепта плательщика, " +
          "и об аннулировании платежного требования. Основание отказа " + DenialGround;
      else // частичный акцепт
        Queries = "InfoCode:4";
        Narrative = 
          "Частичный отказ от акцепта платежного требования плательщиком, " +
          "уведомляем о получении частичного акцепта плательщика. " +
          "Сумма отказа от акцепта " + DenialAmount + ". Основание отказа: " + DenialGround;                    
      end;
      CreateED274(PaymentObj.PaymentID, Queries, Narrative, ID_Oper, ID_Step);
    end; // Если сумма отказа > 0
  end;

  return stat;
end;
