/* ╔══════════════════════════════════════════════════════════════════╗ */
/*         Автоматизированная банковская система RS-Bank v5.10          */
/*                 Copyright (c) R-Style Software Lab 2001              */
/*                                                                      */
/*  Имя файла        : psbcppay.mac                                     */
/*                                                                      */
/*  Описание         : Выполнение шага по формированию продажи валюты   */
/*                                                                      */
/*  Операция         : Ответный валютный платеж в РКО                   */
/*                                                                      */
/*  Программист      : Литворенко А.А.                                  */
/*                                                                      */
/*  Создан           : 03.10.2001                                       */
/*                                                                      */
/* ╚══════════════════════════════════════════════════════════════════╝ */
import InsCarryDoc, payinter, "bcall.mac";

record  d    ( arhdoc );
record  back ( ps_bcord );
record  Pmpaym_Part  ( pmpaym   ); 
record  Pmprop_Part  ( pmprop   ); 
record  Pmrmprop_Part( pmrmprop ); 

/**********************************/
/* Выполнение шага                */
/**********************************/
MACRO ExecuteStep( doc, pm_paym )

   const PSBCKIND_BUY = 1, PSBCKIND_PAY = 2, PSBCKIND_CONV = 3,
         PSBC_FREE = 0, PSBC_REV = 2;
   var СуммуВозврата, tmp_date;

   setbuff( back, doc );
   setbuff( Pmpaym_Part, pm_paym );

   ClearRecord( back );

   if( Pmrmprop_Part.KindPayCurrency == 0 )
      return 0;
   end;

   if( Pmrmprop_Part.KindPayCurrency == 3 )
      PSBC_AddMessageToLog( "Валюта не подлежит продаже" );
      return 0;
   end;

   if( not PossiblePSBCPayCurrency( KindDoc_Pm_Paym, Pmpaym_Part.paymentID ) )
      msgbox("Невозможно сформировать продажу, поскольку есть открытые сквитованные документы");
      return 1;
   end;

   СуммуВозврата = ПолучитьСуммуВозврата( Pmpaym_Part.paymentID, KindDoc_Pm_Paym, Pmpaym_Part.Amount );

   if( СуммуВозврата > 0.0 )

/* Последовательность полей по ТЗ*/ 

/* обратная продажа валюты */

/* 1  Номер поручения   Следующий по порядку номер среди поручений, 
автоматически сформированных системой. */
      back.Number = Pmrmprop_Part.Number; 

/* 2  Дата поручения Дата последнего исполнения поручения на покупку 
(или дата зачисления средств на счет клиента по ответному платежу) плюс 8 дней. 
Если полученная дата является выходным днем, то проставляется дата первого рабочего дня, 
следующего за этим выходным (использовать календарь Главной Книги). */
      tmp_date = {curdate}; /* пока пусть будет так */

      back.OrderDate    = DateAfterCalenDays(tmp_date,  8);  /*Дата оформления заявки*/
      if (IsHoliday(back.OrderDate))
         back.OrderDate = DateAfterWorkDays(back.OrderDate, 1);  /*Дата оформления заявки*/
      end;

/* 3  Вид продажи Обратная*/
      back.BCOrdKind    = PSBCKIND_PAY; 
      back.BCOrdSubKind = PSBC_REV;     

/* 4  Клиент - код     Значение реквизита "Клиент - код" поручения на покупку валюты 
Код клиента счета получателя ответного платежа.*/
      back.ClientID   = ПолучитьВладельцаСчета( 1, pmpaym_Part.ReceiverAccount, Pmpaym_Part.FIID );
      back.ClientName = psbc_GetNameClient( back.ClientID );
      back.ClientINN  = GetCodeParty( back.ClientID, PTCK_INN );

/* 5  Клиент - наименование   Наименование клиента, код которого указан в реквизите (4) 
из справочника клиентов банка.*/
      /* не нужно подкачает скроллинг */

/* 6  В лице - должность     Значение реквизита "В лице - должность"поручения на покупку   
Пусто (для ответного платежа)*/
      /* пусто */

/* 7  В лице - ФИО     Значение реквизита "В лице - ФИО" поручения на покупку  
Пусто (для ответного платежа)*/
      /* пусто */

/* 8  Заявка - валюта - код     Значение реквизита "Заявка - валюта - код" поручения на покупку  
Код валюты счета - получателя ответного платежа.*/
      back.SourceFIID   = Pmpaym_Part.FIID;
      back.RequiredFIID =  0;
/* 9  Заявка - валюта - наименование   Наименование валюты, соответствующей реквизиту (8) 
из справочника валют банка.*/
      /* скроллинг сам справиться */

/* 10 Заявка - дата валютирования   Значение реквизита (2) плюс 3 рабочих дня.*/
      back.ValueDate    = DateAfterCalenDays(back.OrderDate, 3); 
      if (IsHoliday(back.ValueDate))
         back.OrderDate = DateAfterWorkDays(back.ValueDate, 1);  
      end;

/* 11 Заявка - торговая площадка Значение "ЕТС"*/
     /* ЕТС - такого в справочнике нет, по этому подставляем первый попавшийся */
       ПолучитьТорговуюПлощадку(back.MarketPlace);

/* 12 Заявка - валюта торгов - код    Для валют, приведенных в справочнике стран - 
участниц Европейского Валютного союза (п.4)  - код "978"   Для остальных валют - код "840"*/
      /* пусто */

/* 13 Заявка - валюта торгов - наименование  Наименование валюты, к соответствующей 
реквизиту (12) из справочника валют банка.*/
      /* пусто */

/* 14 Заявка - поступившая сумма   Значение реквизита "Заявка - сумма валюты" 
поручения на покупку минус сумма сквитованных с поручением на покупку документов по 
переводу средств со счета.  Сумма ответного платежа.*/
      back.SourceAmount = Pmpaym_Part.Amount;

/* 15 Заявка - процент продажи   Значение "100".*/
      /* вычислится само */

/* 16 Заявка - сумма валюты   Значение реквизита (14) умножить на значение реквизита (15) 
разделить на 100.*/
      back.InAmount = Pmpaym_Part.Amount;

/* 17 Заявка - сумма обеспечения Пусто*/
      /* пусто */

/* 18 Заявка - вид курса - код   Пусто*/
      /* пусто */

/* 19 Заявка - вид курса - название Пусто*/
      /* пусто */

/* 20 Заявка - курс - значение   Пусто*/
      /* пусто */

/* 21 Счет списания валюты   Значение реквизита "Зачисление купленной валюты - счет" 
поручения на покупку.   Номер счета - получателя ответного платежа.*/
      ПолучитьСТВС_Клиента( ПолучитьВладельцаСчета( 1, Pmpaym_Part.ReceiverAccount, Pmpaym_Part.FIID ), Pmpaym_Part.FIID, back.SourceAccount );

/* 22 Текущий валютный счет   Валютный счет клиента (4), имеющий системный тип "Текущий", 
для которого счет (21) указан в качестве связанного счета (панель корректировки валютного 
лицевого счета).*/
      ПолучитьТкВС_Клиента( back.ClientID, back.SourceFIID, back.CurrentAccount );

/* 23 Зачисление полученных средств - счет   Рублевый счет клиента, имеющий системный тип 
"Расчетный" (первый из найденных).*/
      ПолучитьРС_Клиента( back.ClientID, back.RequiredAccount );

/* 24 Зачисление полученных средств - банк   Пусто*/
      /* пусто */

/* 25 Основание операции   Обратная продажа валюты, поступившей по поручению N 
<номер поручения на покупку (или номер первичного документа ответного платежа)>  
от <дата поручения на покупку (или дата первичного документа ответного платежа)>.*/
      back.Ground = "Обратная продажа валюты, поступившей по платежу № " + Pmrmprop_Part.Number + " от " + Pmrmprop_Part.Date;

/* 26 Номер пачки Ноль*/
      back.NumberPack   = 0;   

/* 27 Филиал   Значение реквизита "Филиал" поручения на покупку (или ответного платежа).*/
      back.Department   = Pmpaym_Part.Department;

/* 28 Операция - код Значение "3011"*/
      /* подставиться автоматически */

/* 29 Операция - наименование Наименование операции, указанной в реквизите (28) 
из справочника операций ГК.*/
      /* подставиться автоматически */

/* Обязательная */ 
      if( Pmrmprop_Part.KindPayCurrency == 1 )

         ПолучитьТВС_Клиента( ПолучитьВладельцаСчета( 1, Pmpaym_Part.ReceiverAccount, Pmpaym_Part.FIID ), Pmpaym_Part.FIID, back.SourceAccount );

/* 14 Заявка - поступившая сумма Сумма ответного платежа минус сумма сквитованных с ним 
документов по переводу средств со счета.*/
        back.InAmount  = СуммуВозврата;
/* 15 Заявка - процент продажи   Значение "50".*/
        /* скроллинг сам всё подсчитает */
/* 16 Заявка - сумма валюты   Значение реквизита (14) умножить на значение реквизита (15) 
разделить на 100.*/
        back.SourceAmount = back.InAmount * 0.5;

/* 25 Основание операции   Обязательная продажа валюты, поступившей по поручению N <номер
 первичного документа ответного платежа> от <дата первичного документа ответного платежа>.*/

        back.Ground = "Обязательная продажа валюты, поступившей по платежу № " + Pmrmprop_Part.Number + " от " + Pmrmprop_Part.Date;
        back.BCOrdSubKind = 1;

      end;

/* 30. Происхождение документа */
      back.Origin = PSBCORD_OR_AUTO;

/* 31. Операционист */
      back.Oper = {oper};

/* количество знаков для курса */
      back.Point  = 4;

      if( not InsertPSBCOrder( NULL ) )
        msgbox("Ошибка при вставке поручения на продажу валюты");
        return 1;
      else
         if( Pmrmprop_Part.KindPayCurrency == 1 )
            PSBC_AddMessageToLog( "Сформированно поручение на обязательную продажу № " + back.Number + ", дата " + back.OrderDate );
         else
            PSBC_AddMessageToLog( "Сформированно поручение на обратную продажу № " + back.Number + ", дата " + back.OrderDate );
         end;
      end;

   else
      PSBC_AddMessageToLog( "Средства были полностью израсходованы" );
   end;

   return 0;
end;

