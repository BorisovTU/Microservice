/*
  $Name:        MesForCB_RecNS.mac
  $Module:      РКО
  $Description: Блок Rec для сообщения в ЦБ об открытых и закрытых счетах
*/
//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : РКО
//
// Описание     : Блок Rec для сообщения в ЦБ об открытых и закрытых счетах
//
// Программист  : Чукина Т.А.
//
// Создан       : 16.10.2015
//
//-----------------------------------------------------------------------------

import "MesForCB_lib.mac", PTInter, "wlmnstls.mac", "MesForCB_protocol.mac",
       "fnsaccmsg_lib.mac", "reqinter.mac";

private macro GetRequestID(Account : string, FIID : integer, AccMsgKind : integer) : integer
  var RequestID : integer = 0;

  var TableName : string = "";
  if(AccMsgKind == ACCMSG_KIND_OPEN)
    TableName = "dreqopena_dbt";
  elif(AccMsgKind == ACCMSG_KIND_CLOSE)
    TableName = "dreqclosa_dbt";
  else
    // такой ситуации не должно быть
    RunError("Ошибка программирования: неверное значение параметра AccMsgKind");
  end;

  var rs : RsdRecordset = execSQLselectPrm
    ( "select req.t_RequestID "
      "  from " + TableName + " req "
      " where req.t_Account = :Account "
      "   and req.t_Code_Currency = :FIID ",
      SQLParam("Account", Account),
      SQLParam("FIID", FIID)
    );

  if(rs and rs.moveNext())
    RequestID = rs.value("t_RequestID");
  end;

  return RequestID;
end;

private macro GetAccChngData
( Account : string, 
  FIID : integer,
  OldAccount : @string, 
  OldFIID : @integer
) : bool

  var RequestID : integer = 0;

  var rs : RsdRecordset = execSQLselectPrm
    ( "select req.t_OldAccount, req.t_OldAccountFIID "
      "  from dreqchnga_dbt req "
      " where req.t_NewAccount = :Account "
      "   and req.t_NewAccountFIID = :FIID "
      "   and req.t_NewAccount <> req.t_OldAccount ",
      SQLParam("Account", Account),
      SQLParam("FIID", FIID)
    );

  if(rs and rs.moveNext())
    OldAccount = rs.value("t_OldAccount");
    OldFIID = rs.value("t_OldAccountFIID");
    return true;
  end;

  return false;
end;

private macro GetAccOpenRequestIdCheckChng(Account : string, FIID : integer) : integer
  var RequestID : integer = 0;

  while(not RequestID)

    RequestID = GetRequestID(Account, FIID, ACCMSG_KIND_OPEN);

    if(not RequestID)
      var OldAccount : string = "", OldFIID : integer = -1;

      if( GetAccChngData(Account, FIID, @OldAccount, @OldFIID) )
        Account = OldAccount;
        FIID = OldFIID;
      else
        break;
      end;
    end;

  end;

  return RequestID;
end;

private macro GetReqMesData
( RequestID : integer, 
  AccMsgKind : integer, 
  MesState : @integer,
  MesDate : @date
) : bool

  var ObjType : integer = 0;
  if(AccMsgKind == ACCMSG_KIND_OPEN)
    ObjType = OBJTYPE_REQOPENA;
  elif(AccMsgKind == ACCMSG_KIND_CLOSE)
    ObjType = OBJTYPE_REQCLOSA;
  else
    // такой ситуации не должно быть
    RunError("Ошибка программирования: неверное значение параметра AccMsgKind");
  end;

  var rs : RsdRecordset = execSQLselectPrm
    ( "select mes.t_State, mes.t_Date "
      "  from dwlmeslnk_dbt lnk, dwlmes_dbt mes "
      " where lnk.t_ObjID = :RequestID "
      "   and lnk.t_ObjKind = :ObjType "
      "   and lnk.t_Direct = chr(0) "
      "   and mes.t_MesID = lnk.t_MesID "
      "   and mes.t_IsAnnul = chr(0) "
      " order by mes.t_MesID ",
      SQLParam("RequestID", RequestID),
      SQLParam("ObjType", ObjType)
    );

  if(rs and rs.moveNext())
    MesState = rs.value("t_State");
    MesDate = rs.value("t_Date");
    return true;
  end;

  return false;
end;

private macro GetOpenCloseAccFnsMesDate(Account : string, FIID : integer, AccMsgKind : integer) : date
  var MesDate : date = date(0,0,0), MesState : integer = 0;

  var Protocol = MesForCB_GetProtocolObj();
  var RequestID : integer = 0;
  var MsgKindStr : string = "";

  if(AccMsgKind == ACCMSG_KIND_OPEN)
    RequestID = GetAccOpenRequestIdCheckChng(Account, FIID);
    MsgKindStr = "об открытии";
  elif(AccMsgKind == ACCMSG_KIND_CLOSE)
    RequestID = GetRequestID(Account, FIID, AccMsgKind);
    MsgKindStr = "о закрытии";
  else
    // такой ситуации не должно быть
    RunError("Ошибка программирования: неверное значение параметра AccMsgKind");
  end;

  if( (RequestID > 0) and
      GetReqMesData(RequestID, AccMsgKind, @MesState, @MesDate)
    )
    if( MesState != WLD_STATUS_MES_CLOSED )
      Protocol.AddLogMessage("Не обработано сообщение " + MsgKindStr + " счета " + Account + " в ИФНС");
    end;
  else
    Protocol.AddLogMessage("Не сформировано сообщение " + MsgKindStr + " счета " + Account + " в ИФНС");
    MesDate = date(0,0,0);
  end;

  return MesDate;
end;

class (TXmlNodesAdd) TBlockRecNS(p_nodeRec : object, p_xml : object)
  InitTXmlNodesAdd(p_nodeRec, p_xml);
  private var Protocol = MesForCB_GetProtocolObj();

  private macro FillAgreeFields(rs : RsdRecordset, IsGK : bool)
    // Если счет банковский, элемент отсутствует
    if( PM_IsBankAccount(rs.value("t_Account"), rs.value("t_Code_Currency")) )
      return;
    end;

    var ContractDate : date = rs.value("t_ContractDate"),
        ContractNumber : string = rs.value("t_ContractNumber");

    if( IsGK )
      var rs_sfcontr : RsdRecordset = execSQLselectPrm
      ( "SELECT t_DateConc, t_Number "
        "  FROM dsfcontr_dbt "
        " WHERE t_ServKind = :PTSK_PAY "
        "   AND t_ObjectType = :SF_ACCOUNT "
        "   AND t_FIID = :FIID "
        "   AND t_Object = :Account",
        SQLParam("PTSK_PAY", PTSK_PAY),
        SQLParam("SF_ACCOUNT", SF_ACCOUNT),
        SQLParam("FIID", rs.value("t_Code_Currency")),
        SQLParam("Account", rs.value("t_Account"))
      );

      if(rs_sfcontr and rs_sfcontr.moveNext())
        ContractDate = date( rs_sfcontr.value("t_DateConc") );
        ContractNumber = rs_sfcontr.value("t_Number");
      else
        Protocol.AddLogMessage("Не найден договор, на основании которого открыт счет " + rs.value("t_Account"));
      end;
    end;

    var DateStr : string = "";
    if(ContractDate != date(0,0,0))
      DateStr = DDpMMpYYYY(ContractDate);
    end;

    AddText("AGREE_DATE", StrNormalization( DateStr ));
    AddText("AGREE_NUM", StrNormalization( ContractNumber ));
  end;

  private macro Fill_TNAME(rs : RsdRecordset, IsBO : bool)

    macro GetClientTypeDescr(ClientType : string) : string
      var Descr : string = "";

      if(ClientType == "2")
        Descr = "индивидуальный предприниматель";
      elif(ClientType == "3")
        Descr = "нотариус";
      elif(ClientType == "4")
        Descr = "адвокат";
      elif(ClientType == "5")
        Descr = "управляющий товарищ инвестиционного товарищества";
      end;

      return Descr;
    end;

    var TNAME : string = "";

    var ClientID : integer = rs.value("t_ClientID");
    if(ClientID > 0)
      TNAME = GetClientInfoForCB(ClientID);
    elif(IsBO)
      var ClientName : string = rs.value("t_ClientName"),
          ClientTypeDescr : string = GetClientTypeDescr(rs.value("t_ClientType")),
          ClientAddress : string = rs.value("t_ClientAddress");

      TNAME = JoinClientInfoForCB( makeArray(ClientName, ClientTypeDescr, ClientAddress) );
    end;

    AddText("TNAME", StrNormalization( SubStr(TNAME, 1, 160) ));
  end;

  private macro GetAccountName(AccountID : integer, Account : string) : string
    var rs : RsdRecordset = execSQLselectPrm
    ( "select t_NameAccount "
      "  from daccount_dbt "
      " where t_AccountID = :AccountID ",
      SQLParam("AccountID", AccountID)
    );

    if(rs and rs.moveNext())
      return rs.value("t_NameAccount");
    end;

    RunError("Не найден счет " + IfThenElse(Account, Account, "с идентификатором " + AccountID));
  end;

  private macro Fill_TNAME_2(rs : RsdRecordset, IsBO : bool)
    var TNAME_2 : string = "";

    if(IsBO)
      TNAME_2 = rs.value("t_AccKind");
    else
      var Type_Account : string = rs.value("t_AccKind");
      TNAME_2 = GetAccKindNameGK(rs.value("t_ClientID"), Type_Account);
  
      if(TNAME_2 == "Иной")
        TNAME_2 = GetTypeAcc(rs.value("t_Code_Currency"), Type_Account);
        if( TNAME_2 == "" )
          TNAME_2 = GetAccountName(rs.value("t_GK_AccountID"));
        end;    
      end;  
               
      if(TNAME_2 == "Неустановленный")
        TNAME_2 = GetAccountName(rs.value("t_GK_AccountID"));
      end;   
    end;
           
    AddText("TNAME_2", StrNormalization( SubStr(TNAME_2, 1, 4000) ));
  end;

  private macro GetIsAccClosed(rs : RsdRecordset, IsGK : bool) : bool

    macro IsSetOpenCloseFlag(AccountID : integer) : bool
      var OpenCloseFlag : string = "";

      var rs : RsdRecordset = execSQLselectPrm
        ( "select t_Open_Close "
          "  from daccount_dbt "
          " where t_AccountID = :AccountID ",
          SQLParam("AccountID", AccountID) );

      if(rs and rs.moveNext())
        OpenCloseFlag = rs.value(0);
      end;

      return (OpenCloseFlag != "");
    end;

    if( date(rs.value("t_CloseDate")) == date(0,0,0) )
      return false;
    end;

    if( IsGK and not IsSetOpenCloseFlag(rs.value("t_GK_AccountID")) )
      return false;
    end;

    return true;
  end;

  private macro Fill_OPEN_NALOG(rs : RsdRecordset, IsGK : bool)
    var DateStr : string = "";

    var NalogMesDate : date = date(0,0,0);

    var Type_Account : string = "";
    if(IsGK)
      Type_Account = rs.value("t_AccKind");
    else
      Type_Account = GetAccountTypeSymbolByAccKindName( rs.value("t_AccKind") );
    end;

    if( not CheckTypeAccPSRegVal(Type_Account, "ТИПЫ_СЧЕТОВ") )
      DateStr = "99.99.9999";
    else
      NalogMesDate = GetOpenCloseAccFnsMesDate
        ( rs.value("t_Account"), 
          rs.value("t_Code_Currency"),
          ACCMSG_KIND_OPEN );

      DateStr = DDpMMpYYYY(NalogMesDate);
    end;

    AddText( "OPEN_NALOG", StrNormalization( DateStr ) );
  end;

  private macro Fill_CLOSE_NALOG(rs : RsdRecordset)
    var DateStr : string = "";

    var NalogMesDate : date = GetOpenCloseAccFnsMesDate
      ( rs.value("t_Account"), 
        rs.value("t_Code_Currency"),
        ACCMSG_KIND_CLOSE );

    DateStr = DDpMMpYYYY(NalogMesDate);

    AddText( "CLOSE_NALOG", StrNormalization( DateStr ) );
  end;

  private macro FillBankCodes(ClientID : integer)
    var Code : string = "";

    if( IsPartyOwned(ClientID, PTK_BANK) )
      Code = ПолучитьКодСубъекта(ClientID, PTCK_BIC);
      if(Code)
        AddText( "BIK", StrNormalization( Code ) );
      end;
    end;

    if( IsPartyOwned(ClientID, PTK_SWIFT) )
      Code = ПолучитьКодСубъекта(ClientID, PTCK_SWIFT);
      if(Code)
        AddText( "SWIFT", StrNormalization( Code ) );
      end;
    end;
  end;

  private macro FillINNKPP(rs : RsdRecordset, IsBO : bool)
    var INN : string = "", KPP : string = "";

    var ClientID : integer = rs.value("t_ClientID");
    if(ClientID > 0)
      var FullINN : string = ПолучитьКодСубъекта(ClientID, PTCK_INN);
      INN = RemoveKPP(FullINN);
      KPP = RemoveINN(FullINN);
    elif(IsBO)
      INN = rs.value("t_ClientINN");
      KPP = rs.value("t_ClientKPP");
    end;

    INN = DeleteNotDigitSymbols( INN, true );

    AddText( "INN", StrNormalization( INN ) );
    if( KPP )
      if( MakeRightKPP( @KPP ) )
        AddText( "KPP", KPP );
      end;
    end;
  end;

  macro FillOGRN(ClientID : integer, ClientOGRN : string)
    if(ClientID > 0)
      ClientOGRN = ПолучитьКодСубъекта(ClientID, PTCK_OGRN);
    end;

    if(ClientOGRN)
      AddText( "OGRN", ClientOGRN );
    end;
  end;

  macro Fill(rs : RsdRecordset)
    var RegBranchBO : string = rs.value("t_RegBranchBO");

    var Account : string = rs.value("t_Account"),
        OpenDate : date = rs.value("t_OpenDate"),
        CloseDate : date = rs.value("t_CloseDate");

    var IsBO : bool = false;
    if(RegBranchBO)
      IsBO = true;
    end;

    var IsBankAccount : bool = PM_IsBankAccount(Account, rs.value("t_Code_Currency"));
    var IsAccClosed : bool = GetIsAccClosed(rs, not IsBO);
    var ClientID : integer = rs.value("t_ClientID");

    AddText( "OPEN", StrNormalization( DDpMMpYYYY(OpenDate) ) );
    FillAgreeFields(rs, not IsBO);

    if(not IsBankAccount)
      Fill_TNAME(rs, IsBO);
    end;

    Fill_TNAME_2(rs, IsBO);
    AddText( "ACCOUNT", StrNormalization( Account ) );

    if(not IsBankAccount)
      Fill_OPEN_NALOG(rs, not IsBO);
    end;

    if(IsAccClosed)
      if(CloseDate != date(0,0,0))
        AddText( "CLOSE", StrNormalization( DDpMMpYYYY(CloseDate) ) );
      end;
    end;

    if(not IsBankAccount)
      if(IsAccClosed)
        Fill_CLOSE_NALOG(rs);
      end;

      if(ClientID > 0) 
        FillBankCodes(ClientID);
      end;

      FillINNKPP(rs, IsBO);
    end;

    AddText("PR_KONS", StrNormalization( rs.value("t_PrKons") ));
    if( rs.value("t_AccountKons") != "" )
      AddText("ACCOUNT_KONS", StrNormalization( rs.value("t_AccountKons") ));
    end;

    if(not IsBankAccount)
      FillOGRN(ClientID, StrNormalization( rs.value("t_ClientOGRN") ));
    end;

    Protocol.PrintTableRow(Account, OpenDate, CloseDate);
  end;

end;
