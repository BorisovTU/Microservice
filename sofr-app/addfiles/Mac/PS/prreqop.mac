/*
 $Name: prreqop.mac
 $Module: РКО
 $Description: Печатная форма заявления об открытии счета
 */
import PSInter,prreqbuf,reqinter;

macro DrawReport( ShortNameSelf:string,
                  AccNameType:string, 
                  FI_Name:string,
                  PartyName:string,   
                  Open_Date:date,
                  number:string,
                  dateBegin:date,
                  РАЗРЕШЕНИЕ_ДОЛЖ:string,
                  РАЗРЕШЕНИЕ_ФИО:string,
                  ПРОВЕРКА_ДОЛЖ:string,
                  ПРОВЕРКА_ФИО:string
                  )
                                     
  var ФИОРуководителя, 
      ФИОБухгалтера,
      ДатаЗаявления, 
      ДатаОткрытияСчета,
      ДатаДоговора;
  var DelegatePost = "";

  if(pr_reqopena.rec.date != date(0,0,0))
    ДатаЗаявления = string(pr_reqopena.rec.date) + " г."; 
  else
    ДатаЗаявления = "";
  end;

  if(Open_Date != date(0,0,0))
    ДатаОткрытияСчета = string(Open_Date) + " г.";
  else
    ДатаОткрытияСчета = "";
  end;

  if(dateBegin != date(0,0,0))
    ДатаДоговора = string(dateBegin) + " г.";
  else
    ДатаДоговора = "";
  end;

  Array Name;
  if( pr_party.rec.Name )
    StrSplit(pr_party.rec.Name , Name,77,77,2 );
  else
    StrSplit(pr_reqopena.rec.ClientName , Name,77,77,2 );
  end;
  
  if( pr_reqopena.rec.ClientType == PTLEGF_INST )
    DelegatePost = pr_reqopena.rec.DelegatePost;
    ФИОРуководителя = pr_reqopena.rec.DelegateName1 + " " +
                    pr_reqopena.rec.DelegateName2 + " " +
                    pr_reqopena.rec.DelegateName3;
    ФИОБухгалтера = pr_reqopena.rec.Name_Book;
  elif( pr_reqopena.rec.ClientType == PTLEGF_PERSN )
    ФИОРуководителя = pr_reqopena.rec.ClientName;
    ФИОБухгалтера = pr_reqopena.rec.ClientName;
  end;
  
  
  

[                                                                                                    ];
[                                            З А Я В Л Е Н И Е                                       ];
[                                            на открытие счета                                       ];  
[                                                                                ┌──────────────────┐];
[ #####################                                                          │      0401025     │](ShortNameSelf);
[                                                                                └──────────────────┘];
[                                                                                                    ];
[                                                                                ┌──────────────────┐];
[ Наименование организации  (полное и точное в соответствии с Уставом)           │       Коды       │];
[ #############################################################################  ├──────────────────┤](Name(0));
[ ─────────────────────────────────────────────────────────────────────────────  │                  │];
[ #############################################################################  │                  │](Name(1));
[ ─────────────────────────────────────────────────────────────────────────────  │                  │];
[ Просим открыть ##############################################################  │                  │](AccNameType);                         
[               ───────────────────────────────────────────────────────────────  │                  │];
[              (расчетный, текущий, бюджетный, специальный банковский, др.счета) │                  │];
[    в ########################################################################  │                  │](FI_Name);
[      ────────────────────────────────────────────────────────────────────────  │                  │];
[                            (указать валюту счета)                              └──────────────────┘];
[ счет на основании инструкций Банка России, нам известных и имеющих                                 ];
[ обязательную для нас силу.                                                                         ];
[                                                                                                    ];
[                                                                                                    ];
[                                                                                                    ];
[ Руководитель: ####################                   ( ######################################## )  ]( DelegatePost, ФИОРуководителя );
[               ──────────────────── ────────────────  ────────────────────────────────────────────  ];
[                  (должность)         (подпись)                          (ФИО)                      ];
[                                                                                                    ];
[                                                                                                    ];
[ Главный бухгалтер                                    ( ######################################## )  ]( ФИОБухгалтера );
[                                   ─────────────────  ────────────────────────────────────────────  ];
[                                      (подпись)                          (ФИО)                      ];
[                                                                                                    ];
[                                                                                                    ];
[ М.П.                                                                                               ];
[                                                                       #############                ](ДатаЗаявления);
[ ________________________________________________________________________________                   ];
[                                            ОТМЕТКИ БАНКА                                           ]; 
[                                                                                                    ];
[                                   Распоряжение на открытие счета                                   ]; 
[         Открыть                                                                                    ];
if(pr_reqopena.rec.CurrentState == REQOPENA_ST_CLOSED)
[ ######################################################################  счет в ################### ](AccNameType,FI_Name);
else
[                                                                         счет в                     ];
end;
[ ──────────────────────────────────────────────────────────────────────         ─────────────────── ];
[ (расчетный, текущий, бюджетный, специальный банковский счет, др. счета)         (валютасчета)      ];
[                                                                                                    ];
if(pr_reqopena.rec.CurrentState == REQOPENA_ST_CLOSED)                                                                                                    
[ #################################################################################################  ](pr_party.rec.name);
else
[                                                                                                    ];
end;
[ ────────────────────────────────────────────────────────────────────────────────────────────────── ];                                                                                                                       
[                    наименование организации  (полное и точное в соответствии с Уставом)            ];
[                                                                                                    ];
[  РАЗРЕШАЮ                                                            Документы на оформление       ];
[ #####################################                                открытия счета и совершение   ](РАЗРЕШЕНИЕ_ДОЛЖ);
[ #####################################                                операций по счету проверил    ](PartyName);
[                                                                                                    ];
[                                                                                                    ];
[              ( ###################### ) ##################                ( ##################### )]( РАЗРЕШЕНИЕ_ФИО, ПРОВЕРКА_ДОЛЖ, ПРОВЕРКА_ФИО);
[ ─────────────                                             ────────────────                         ];
[                                                                                                    ];
[ #############                                                                                      ](ДатаОткрытияСчета);
[                                                                                                    ];
[                                                                                                    ];
[        Счет открыт:                                                                                ];
[ ╔══════════════════╤═════════════════════════════╗                                                 ];
[ ║  № бал. счета    │  № лиц. Счета               ║                                                 ];
[ ╟──────────────────┼─────────────────────────────╢                                                 ];
[ ║ ################ │   ########################  ║             ](pr_reqopena.rec.Balance0,pr_reqopena.rec.account:f);
[ ╚══════════════════╧═════════════════════════════╝                                                 ];
[ ┌────────────────────────────────────────────┐                                                     ];
[ │ Договор банковского счета                  │                                                     ];
[ │ № ############# от #############           │                                                     ](number, ДатаДоговора);
[ └────────────────────────────────────────────┘                                                     ];

end;

macro PrintDocument(ncopy:integer):bool

  var errcode,
      TR_account:TRecHandler = TRecHandler( "account.dbt"  , "bank.def" ),
      TR_party:TRecHandler = TRecHandler( "party.dbt"  , "bank.def" ),
      ShortNameSelf,
      AccNameType:string, 
      FI_Name:string,
      PartyName:string,   
      Open_Date:date,
      number:string,
      dateBegin:date,
      РАЗРЕШЕНИЕ_ДОЛЖ:string,
      РАЗРЕШЕНИЕ_ФИО:string,
      ПРОВЕРКА_ДОЛЖ:string,
      ПРОВЕРКА_ФИО:string;
  
  AccNameType = GetTypeAcc(pr_reqopena.rec.Code_Currency , pr_reqopena.rec.type_Account); 
  FI_Name = GetNameCurrencyAcc(pr_reqopena.rec.Code_Currency);
  PartyName = GetNameBranch({OperDprtNode});

  TR_account = GetAccount( 1 , pr_reqopena.rec.Account, pr_reqopena.rec.Code_Currency);
  Open_Date = TR_account.rec.Open_Date;
   
  if(GetPartyRec({OurBank},TR_party))
    ShortNameSelf = TR_party.rec.ShortName;
  else
    ShortNameSelf = "";
  end;

  GetNumAndDateContr(pr_reqopena.rec.Code_Currency, pr_reqopena.rec.Account,dateBegin,number);

  if( GetRegValForOPENAC( "РАЗРЕШЕНИЕ_ДОЛЖ", V_STRING, РАЗРЕШЕНИЕ_ДОЛЖ ) )
     РАЗРЕШЕНИЕ_ДОЛЖ = "";
  end;

  if( GetRegValForOPENAC( "РАЗРЕШЕНИЕ_ФИО", V_STRING, РАЗРЕШЕНИЕ_ФИО ) )
     РАЗРЕШЕНИЕ_ФИО = "";
  end;

  if( GetRegValForOPENAC( "ПРОВЕРКА_ДОЛЖ", V_STRING, ПРОВЕРКА_ДОЛЖ ) )
     ПРОВЕРКА_ДОЛЖ = "";
  end;

  if( GetRegValForOPENAC( "ПРОВЕРКА_ФИО", V_STRING, ПРОВЕРКА_ФИО ) )
     ПРОВЕРКА_ФИО = "";
  end;

  while( ncopy )
    DrawReport( ShortNameSelf,
                AccNameType, 
                FI_Name,
                PartyName,   
                Open_Date,
                number,
                dateBegin,
                РАЗРЕШЕНИЕ_ДОЛЖ,
                РАЗРЕШЕНИЕ_ФИО,
                ПРОВЕРКА_ДОЛЖ,
                ПРОВЕРКА_ФИО
               );
    ncopy = ncopy - 1;
  end;

  return true;
end;