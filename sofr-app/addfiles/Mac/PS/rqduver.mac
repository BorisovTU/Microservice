/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                         R-Style Software Lab

  File Name   : rqduver.mac
  Created     : 08.02.2006
  Programmer  : Копачев Д.В.
  Description : Печать отчета сканирования массовой верификации
                дубликатов ИПВС

└───────────────────────────────────────────────────────────────────────────*/
IMPORT PaymInter;
IMPORT oralib, likepy;
RECORD rq_firstdoc (psduinrq);

const  Ошибка   = 0,
       Первый   = 1,
       Последний= 2;

MACRO Печать_ОтчетСканирования (Вызов,firstdoc,Статус,Сообщение)

   var select:string;
   var params:TArray;
   var rs:object;
   var num;

   record rmprop (pmdurmpp);
   setbuff(rq_firstdoc, firstdoc);

   if( Вызов == Первый )
[        Отчет о групповой верификации дубликатов платежных документов          ];
[┌─────────┬──────┬────────────────────────────────────────────────────────────┐];
[│  Номер  │Номер │                        Сообщение                           │];
[│дубликата│ошибки│                                                            │];
[├─────────┼──────┼────────────────────────────────────────────────────────────┤];
   elif( Вызов == Ошибка )

     if( Статус == 0 )
       Сообщение = "Верификация прошла успешно";
     end;

     if( Статус == 531 )
       Статус = 1834;
       Сообщение = "Для дубликата найдено несколько оригиналов";
     end;

     select = "SELECT t_Number "                         + 
              "FROM dpmdupaym_dbt pm, dpmdurmpp_dbt rm " + 
              "WHERE pm.t_DocKind    = :dockind "        +  
              "  AND pm.t_DocumentID = :docid "          + 
              "  AND pm.t_Purpose    = :purp "           + 
              "  AND pm.t_SubPurpose = :subpurp "        +
              "  AND pm.t_PaymentID  = rm.t_PaymentID; " ;
         
     params = makeArray( SQLParam("dockind", 263),
                         SQLParam("docid",   rq_firstdoc.PaymentID),
                         SQLParam("purp",    PM_PURP_POPRIMARY),
                         SQLParam("subpurp", 0) );
     rs = execSQLselect( select, params, FALSE );
         
     if( rs AND rs.moveNext() )
       num = rs.value(0) ;
     end;

[│#########│######│############################################################│](num, Статус, StrSubst(Сообщение, "|", " " ):w);
   elif( Вызов == Последний )
[└─────────┴──────┴────────────────────────────────────────────────────────────┘];
        end;
END;