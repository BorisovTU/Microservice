import BankInter, InsCarryDoc, PSInter, PaymInter, OprInter, reqinter, "bnk_common.mac";

var ReqCloseAccumObj:RsbReqCloseAccum;

//Сформировать документ вида "Расходный кассовый ордер ББ" (DocKind = 440)
private macro CreateBBOutCashOrder( ReqCloseAccumObj:RsbReqCloseAccum, AccBuff, RestAcc ):integer
  
  var CashOrder:object = GenObject( "RsbBBOutCashOrder" );
  var Payment:RsbPayment = CashOrder.Payment();

  CashOrder.Status          = 1;//STAT_CASH_ORDER_POST;
  CashOrder.ClientAccount   = ReqCloseAccumObj.Account;
  CashOrder.FIOClient       = ReqCloseAccumObj.ReceiverFIO;
  CashOrder.PaperKind       = ReqCloseAccumObj.PaperKind;
  CashOrder.PaperSeries     = ReqCloseAccumObj.PaperSeries;
  CashOrder.PaperNumber     = ReqCloseAccumObj.PaperNumber;
  CashOrder.PaperIssuedDate = ReqCloseAccumObj.PaperIssuedDate;
  CashOrder.PaperIssuer     = ReqCloseAccumObj.PaperIssuer;
  CashOrder.Oper            = {oper}; 
  CashOrder.Origin          = CASH_FDOC_RETACCUM;

  if( ReqCloseAccumObj.AccountFIID != NATCUR )
    CashOrder.IsCurrency = "X";
  end;

  Payment.DocKind        = CASH_BOF_OUTORDER;
  Payment.Purpose        = PM_PURP_CASHBAL;
  Payment.BaseFIID       =
  Payment.PayerFIID      =
  Payment.ReceiverFIID   = ReqCloseAccumObj.AccountFIID;
  Payment.BaseAmount     =
  Payment.PayerAmount    =
  Payment.ReceiverAmount = RestAcc;
  Payment.PayDate        =
  Payment.ClientDate     =
  Payment.Date           =
  Payment.ValueDate      = {curdate};
  Payment.Department     = ReqCloseAccumObj.Department;
  Payment.NumberPack     = ReqCloseAccumObj.NumberPack;
  Payment.Ground         = "Возврат уставного капитала. Основание: " + Bnk_GetLLValuesName( 1683, ReqCloseAccumObj.Ground ) ;
  Payment.Number         = 1;

  var stat:integer = Payment.SetPayerPI( PAYMENTS_GROUP_UNDEF,        
                                         {OurBank},                    
                                         0,                           
                                         "",                          
                                         "",                          
                                         "",                          
                                         ReqCloseAccumObj.AccountFIID,
                                         1/*CHAPT1*/,                 
                                         ReqCloseAccumObj.Account,    
                                         0,                           
                                         "",                          
                                         "" );
  if( stat )
    MemoryError( stat );
    msgbox( GetErrMsg() );
    return 1;
  end;

  stat = Payment.SetReceiverPI( PAYMENTS_GROUP_UNDEF,       
                                {OurBank},                      
                                0,                             
                                "",                            
                                "",                            
                                "",                            
                                ReqCloseAccumObj.AccountFIID,  
                                1/*CHAPT1*/,                   
                                ReqCloseAccumObj.CashAccount,  
                                0,                             
                                "",                            
                                "" );
  if( stat )
    MemoryError( stat );
    msgbox( GetErrMsg() );
    return 1;
  end;

  // Установить мемориальному ордеру признак автозапуска операции.
  CashOrder.LaunchOper = true;

  Payment.CryptoAction( string("Автоматическое_формирование_платежей") );

  return 0;
end;

//Сформировать документ вида "Клиентский платеж". Если reqclsac.AccountFIID  - рубли, то 
//сформировать рублевый документ (DocKind = 201), иначе валютный (DocKind = 202)
private macro CreatePsOrder( ReqCloseAccumObj:RsbReqCloseAccum, AccBuff, RestAcc ):integer

  var PsOrder:object = null;

  if( ReqCloseAccumObj.AccountFIID == NATCUR )
    PsOrder = GenObject( "RsbPsPayOrder",0 ); 
    PsOrder.DocKind = PSPOKIND_ORDER;
    PsOrder.Origin = PSPO_OR_RETACCUM; 
  else
    PsOrder = GenObject( "RsbPsCpOrder",0 );
    PsOrder.Origin = CP_OR_RETACCUM;  
  end;
  PsOrder.Oper  = {oper}; 

  var Payment:RsbPayment = PsOrder.Payment();
   
  if( ReqCloseAccumObj.AccountFIID == NATCUR )
    Payment.DocKind = PS_PAYORDER;
    Payment.PaymentKind = "Э";
  else
    Payment.DocKind = PS_CPORDER;
  end;

  Payment.Purpose        = PM_PURP_POPRIMARY;
  Payment.Department     = ReqCloseAccumObj.Department;
  Payment.NumberPack     = ReqCloseAccumObj.NumberPack;
  Payment.Number         = 1;  
  Payment.BaseFIID       =
  Payment.PayerFIID      =
  Payment.ReceiverFIID   = ReqCloseAccumObj.AccountFIID;
  Payment.BaseAmount     =
  Payment.PayerAmount    =
  Payment.ReceiverAmount = RestAcc;
  Payment.PayDate        =
  Payment.ClientDate     =
  Payment.Date           =
  Payment.ValueDate      = {curdate};
  Payment.Ground         = "Перевод средств по заявлению/распоряжению на закрытие накопительного счета. НДС не облагается.";

  var stat:integer = Payment.SetPayerPI( PAYMENTS_GROUP_UNDEF,        
                                         {OurBank},                    
                                         0,                           
                                         "",                          
                                         "",                          
                                         "",                          
                                         ReqCloseAccumObj.AccountFIID,
                                         1/*CHAPT1*/,                 
                                         ReqCloseAccumObj.Account,    
                                         0,                           
                                         "",                          
                                         "" );
  if( stat )
    MemoryError( stat );
    msgbox( GetErrMsg() );
    return 1;
  end;

  stat = Payment.SetReceiverPI( PAYMENTS_GROUP_UNDEF,       
                                0,                      
                                ReqCloseAccumObj.ReceiverBankCodeKind,                             
                                ReqCloseAccumObj.ReceiverBankCode,                            
                                ReqCloseAccumObj.ReceiverBankName,                            
                                ReqCloseAccumObj.ReceiverBankCorrAcc,                            
                                ReqCloseAccumObj.AccountFIID,  
                                1/*CHAPT1*/,                   
                                ReqCloseAccumObj.ReceiverAccount,  
                                0,                             
                                ReqCloseAccumObj.ReceiverName,                            
                                ReqCloseAccumObj.ReceiverINN,
                                ReqCloseAccumObj.ReceiverCodeKind,
                                ReqCloseAccumObj.ReceiverCode );
  if( stat )
    MemoryError( stat );
    msgbox( GetErrMsg() );
    return 1;
  end;

  // Установить мемориальному ордеру признак автозапуска операции.
  PsOrder.LaunchOper = true;

  Payment.CryptoAction( string("Автоматическое_формирование_платежей") );

  return 0;
end;

//Сформировать дочерний документ типа "Мемориальный ордер" по переводу средств с одного счета на другой 
private macro CreateMemorialOrder( ReqCloseAccumObj:RsbReqCloseAccum, AccBuff, RestAcc ):integer

  var Memorial:object      = GenObject( "RsbMemorialOrder", 0 );
  var Payment:RsbMOPayment = Memorial.Payment();
    
  if( ReqCloseAccumObj.RestAccount == "" )
    msgbox( "Не задан счет для перевода остатка" );
    return 1;
  end;
  
  Memorial.Origin        = CB_DOC_ORIGIN_AUTO;
  Memorial.State         = 0;/*CB_DOC_STATE_DEFERRED*/
  Memorial.Oper          = {oper};
  Memorial.Chapter       = 1;
  Memorial.Code_Currency = ReqCloseAccumObj.AccountFIID;

  Payment.Number         = 1;
  Payment.BaseAmount     =
  Payment.PayerAmount    =
  Payment.ReceiverAmount = RestAcc;
  Payment.BaseFIID       =
  Payment.PayerFIID      =
  Payment.ReceiverFIID   = ReqCloseAccumObj.AccountFIID;
  Payment.Ground         = "Перевод средств по заявлению/распоряжению на закрытие накопительного счета.";
  Payment.PayDate        =
  Payment.ClientDate     =
  Payment.Date           =
  Payment.ValueDate      = {curdate};
  Payment.DocKind        = DLDOC_MEMORIALORDER;
  Payment.Purpose        = PM_PURP_MEMORDER;
  
  var stat:integer = Payment.SetPayerPI( PAYMENTS_GROUP_UNDEF, 
                                         {OurBank}, 
                                         0, 
                                         "", 
                                         "",
                                         "",
                                         ReqCloseAccumObj.AccountFIID, 
                                         1/*CHAPT1*/, 
                                         ReqCloseAccumObj.Account, 
                                         0, 
                                         "", 
                                         "" );
  if( stat )
    MemoryError( stat );
    msgbox( GetErrMsg() );
    return 1;
  end;

  stat = Payment.SetReceiverPI( PAYMENTS_GROUP_UNDEF, 
                                {OurBank}, 
                                0, 
                                "", 
                                "",
                                "",
                                ReqCloseAccumObj.AccountFIID, 
                                1/*CHAPT1*/, 
                                ReqCloseAccumObj.RestAccount, 
                                0, 
                                "", 
                                "" );
  if( stat )
    MemoryError( stat );
    msgbox( GetErrMsg() );
    return 1;
  end;

  // Установить мемориальному ордеру признак автозапуска операции.
  Memorial.LaunchOper = true;

  Payment.CryptoAction( string("Автоматическое_формирование_платежей") );

  return 0;
end;

MACRO ExecuteStep( doc, reqacc )
  
  var stat:integer = 0;
  var RestAcc = RestA( ReqCloseAccumObj.Account, NULL, NULL, 1/*CHAPT1*/, ReqCloseAccumObj.AccountFIID, ReqCloseAccumObj.AccountFIID );

  if( RestAcc == $0 )
    return 0;
  end;
  
  var AccBuff:TRecHandler = GetAccount( 1, ReqCloseAccumObj.Account, ReqCloseAccumObj.AccountFIID );
  if( ReqCloseAccumObj.Ground != 1 )
    if( ReqCloseAccumObj.RestToCash )  
      //Сформировать документ вида "Расходный кассовый ордер ББ" (DocKind = 440)
      stat = CreateBBOutCashOrder( ReqCloseAccumObj, AccBuff, RestAcc );
    else
      //Иначе сформировать документ вида "Клиентский платеж". 
      //Если reqclsac.AccountFIID  - рубли, то сформировать рублевый документ 
      //(DocKind = 201), иначе валютный (DocKind = 202)
      stat = CreatePsOrder( ReqCloseAccumObj, AccBuff, RestAcc );
    end;
  else
    //Сформировать дочерний документ типа "Мемориальный ордер" 
    // по переводу средств с одного счета на другой 
    stat = CreateMemorialOrder( ReqCloseAccumObj, AccBuff, RestAcc );
  end;

  return stat;

END;