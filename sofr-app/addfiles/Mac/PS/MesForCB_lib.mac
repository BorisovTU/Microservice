/*
  $Name:        MesForCB_lib.mac
  $Module:      РКО
  $Description: Функции и классы для формирования сообщений по запросу ЦБ
*/
//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : РКО
//
// Описание     : Функции и классы для формирования сообщений по запросу ЦБ
//
// Программист  : Чукина Т.А.
//
// Создан       : 21.09.2015
//
//-----------------------------------------------------------------------------

import globals, likepy, oralib, PSInter, "rsberror.mac", "bnk_common.mac", OprInter,
       "pm_const.mac", rsexts, "lib_str.mac", "adress.mac", "bnk_ptlib.mac", PaymInter;

macro GetRegPathMesCbExtSyst(RegBranchBO : string, RegName : string) : string
  var RegPath : string = "PS\\СООБЩЕНИЯ В ЦБ\\ДАННЫЕ ВНЕШНИХ СИСТЕМ\\" + 
                         RegBranchBO + "\\" + RegName;

  return RegPath;
end;

macro GetTSNodeField(IsSubBranch : bool, SplitByBranch : bool) : string
  if(IsSubBranch and SplitByBranch)
    return "t_Branch";
  end;

  return "t_Department";
end;

// Параметры текущего сообщения, которое сейчас формируем
class TParmCurrMesForCB(p_MesType : string, p_DateBeg : date, p_DateEnd : date)
  var MesType : string = p_MesType,
      DateBeg : date = p_DateBeg,
      DateEnd : date = p_DateEnd;
end;

class TMesDataSelectForCB(MapVal : PSRepMap, CurrMes : TParmCurrMesForCB)

  private var
    // формировать сообщения по счетам из Главной книги
    IncludeAccGK : bool = MapVal.Value( "IncludeAccGK" ), 
    // формировать сообщения по счетам из внешних систем (бэк-офисов)
    IncludeAccBO : bool = MapVal.Value( "IncludeAccBO" ), 
    // Формировать файл о наличии счетов
    NeedOpenAndClosedAccounts : bool = ( CurrMes.MesType == "NS" ), 
    // Формировать файл об остатках
    NeedAccRests : bool = ( CurrMes.MesType == "OS" ), 
    // Формировать файл об операциях
    NeedAccTrns : bool = ( CurrMes.MesType == "VS" ), 
    // Узел ТС
    Branch : integer = MapVal.Value( "Dep" ),
    // C подчиненными
    IsSubBranch : bool = MapVal.Value( "SubBranch" ), 
    // Название поля Узел ТС - филиал или ВСП
    TSNodeField : string = GetTSNodeField( IsSubBranch, MapVal.Value( "SplitByBranch" ) ), 
    // по которым были операции или числился остаток
    IsOperRest : bool = MapVal.Value( "IsOperRest" ), 
    // дата начала периода
    DateBeg : date = CurrMes.DateBeg,
    // дата окончания периода
    DateEnd : date = CurrMes.DateEnd,

    // Фильтрация счетов из Главной книги по главе
    GKFltByChapter : string = MapVal.Value( "GKFltByChapter" ), 
    GKChapter : integer = MapVal.Value( "GKChapter" ), 

    // Фильтрация счетов из Главной книги по валюте
    GKFltByFIID : string = MapVal.Value( "GKFltByFIID" ), 
    GKFIID : integer = MapVal.Value( "GKFIID" ), 

    // Фильтрация счетов из Главной книги по маске счета
    GKFltByAcc : string = MapVal.Value( "GKFltByAcc" ), 
    GKAccMask : string = MapVal.Value( "GKAccMask" ), 

    // Фильтрация счетов из Главной книги по клиенту - владельцу счета
    GKFltByClient : string = MapVal.Value( "GKFltByClient" ), 
    GKClientID : integer = MapVal.Value( "GKClientID" ), 

    // Фильтрация счетов из внешних систем по валюте
    BOFltByFIID : string = MapVal.Value( "BOFltByFIID" ), 
    BOFIID : integer = MapVal.Value( "BOFIID" ), 

    // Фильтрация счетов из внешних систем по маске счета
    BOFltByAcc : string = MapVal.Value( "BOFltByAcc" ), 
    BOAccMask : string = MapVal.Value( "BOAccMask" ), 

    // Фильтрация счетов из внешних систем по клиенту - владельцу счета
    BOFltByClient : string = MapVal.Value( "BOFltByClient" ), 
    BOClientID : integer = MapVal.Value( "BOClientID" ), 

    // Исключить синтетические счета
    IsExSynt : bool = MapVal.Value( "IsExSynt" ),

    // Вьюхи со счетами и операциями по счетам внешних систем
    AccViews : TArray = TArray(),
    OperViews : TArray = TArray(),
    RestStoredProcs : TArray = TArray(),
    RegBranchesBO : TArray = MapVal.Value( "RegBranchesBO" ),

    DepListStr : string = "";

  var
    rsOpenAndClosed = null,
    rsAccRests = null,
    rsAccRestsGK = null,//optimize
    rsOperations = null;

  private macro Constructor
    if(GKFltByChapter == "!")
      GKFltByChapter = "<>";
    end;

    if(GKFltByFIID == "!")
      GKFltByFIID = "<>";
    end;

    if(GKFltByClient == "!")
      GKFltByClient = "<>";
    end;

    if(BOFltByFIID == "!")
      BOFltByFIID = "<>";
    end;

    if(BOFltByClient == "!")
      BOFltByClient = "<>";
    end;

    if(IncludeAccBO)
      for(var RegBranchName : string, RegBranchesBO)
        var RegPath : string = GetRegPathMesCbExtSyst(RegBranchName, "VIEW_СЧЕТА");
        AccViews[ AccViews.size ] = Bnk_GetRegistryValue(RegPath, V_STRING, "");

        RegPath = GetRegPathMesCbExtSyst(RegBranchName, "VIEW_ОПЕРАЦИИ");
        OperViews[ OperViews.size ] = Bnk_GetRegistryValue(RegPath, V_STRING, "");

        RegPath = GetRegPathMesCbExtSyst(RegBranchName, "ХП_Остаток");
        RestStoredProcs[ RestStoredProcs.size ] = Bnk_GetRegistryValue(RegPath, V_STRING, "");
      end;
    end;

    if(IsSubBranch)
      DepListStr = GetDepListStr(Branch);
    end;
  end;

  // Вызов конструктора
  Constructor();

  private macro GetSelectFieldsForAccountsGK( MesType : string ) : string
    if(MesType == "NS")
      return 
      "       acc.t_Account as t_Account, "
      "       acc.t_Code_Currency as t_Code_Currency, "
      "       acc.t_AccountID as t_GK_AccountID, "
      "       acc.t_Branch as t_Branch, "
      "       acc.t_Department as t_Department, "
      "       acc.t_Open_Date as t_OpenDate, "
      "       acc.t_Close_Date as t_CloseDate, "
      "       to_date('01010001', 'ddmmyyyy') as t_ContractDate, "
      "       chr(1) as t_ContractNumber, "
      "       acc.t_Type_Account as t_AccKind, "
      "       case "
      "         when acc.t_Type_Account like '%И%' then "
      "           '1' "
      "         else "
      "           '0' "
      "       end as t_PrKons, "
      "       case "
      "         when acc.t_Type_Account like '%И%' then "
      "           acc.t_Account "
      "         else "
      "           chr(1) "
      "       end as t_AccountKons, "
      "       acc.t_Client as t_ClientID, "
      "       chr(1) as t_ClientINN, "
      "       chr(1) as t_ClientKPP, "
      "       chr(1) as t_ClientOGRN, "
      "       chr(1) as t_ClientName, "
      "       chr(1) as t_ClientAddress, "
      "       cast( chr(0) as char(1) ) as t_ClientType, "
      "       cast( chr(1) as varchar2(2000) ) as t_RegBranchBO ";

    elif(MesType == "OS")
      return 
      "       acc.t_Account as t_Account, "
      "       acc.t_Code_Currency as t_Code_Currency, "
      "       acc.t_Branch as t_Branch, "
      "       acc.t_Department as t_Department, "
      "       acc.t_Kind_Account as t_Kind_Account, "
      "       acc.t_Client as t_ClientID, "
      "       cast( chr(1) as varchar2(2000) ) as t_RegBranchBO ";
    else
      RunError("Ошибка программирования: неверное значение параметра MesType");
    end;
  end;

  private macro AttachWhereCondForAccountsGK
  ( MesType : string,
    query : @string, 
    params : TArray
  )
    if(IsSubBranch)
      query = query + 
      "       acc.t_Branch in (" + DepListStr + ") ";
    else
      query = query + 
      "       acc.t_Branch = :Dep ";

      params[params.size] = SQLParam("", Branch);
    end;

    if(MesType == "NS") // отбираем данные для сообщения об открытых и закрытых счетах
      query = query + 
      "   and ( acc.t_Open_Date between :DateBeg and :DateEnd or "
      "         acc.t_Close_Date between :DateBeg1 and :DateEnd1 ) ";

      params[ params.size ] = SQLParam("", DateBeg);
      params[ params.size ] = SQLParam("", DateEnd);
      params[ params.size ] = SQLParam("", DateBeg);
      params[ params.size ] = SQLParam("", DateEnd);
    end;

    if(IsOperRest)
      query = query +
      "   and ( rsb_account.restall(acc.t_Account, acc.t_Chapter, acc.t_Code_Currency, :DateBeg2) <> 0 or "
      "         rsb_account.restall(acc.t_Account, acc.t_Chapter, acc.t_Code_Currency, :DateEnd2) <> 0 or "
      "         exists ( select 1 from drestdate_dbt restdate "
      "                   where restdate.t_AccountID = acc.t_AccountID "
      "                     and restdate.t_RestDate between :DateBeg3 and :DateEnd3 ) "
      "       ) ";

      params[ params.size ] = SQLParam("", DateBeg);
      params[ params.size ] = SQLParam("", DateEnd);
      params[ params.size ] = SQLParam("", DateBeg);
      params[ params.size ] = SQLParam("", DateEnd);
    end;

    if(GKFltByChapter)
      query = query +
      "   and acc.t_Chapter " + GKFltByChapter + " :GKChapter ";

      params[ params.size ] = SQLParam("", GKChapter);
    end;

    if(GKFltByFIID)
      query = query +
      "   and acc.t_Code_Currency " + GKFltByFIID + " :GKFIID ";

      params[ params.size ] = SQLParam("", GKFIID);
    end;

    if(GKFltByAcc)
      var CmpResult : string = IfThenElse(GKFltByAcc == "=", "1", "0");
      query = query + 
      "   and rsb_mask.CompareStringWithMask( :GKAccMask, acc.t_Account ) = " + CmpResult;

      params[ params.size ] = SQLParam("", GKAccMask);
    end;

    if(GKFltByClient)
      query = query +
      "   and acc.t_Client " + GKFltByClient + " :GKClientID ";

      params[ params.size ] = SQLParam("", GKClientID);
    end;

    if( IsExSynt )
      query = query +
      "   and acc.t_Type_Account not like '%И%' ";
    end;      

  end;

  private macro GetQueryParamsForAccountsGK
  ( MesType : string,
    query : @string, 
    params : TArray
  )
    query = 
      "select " + GetSelectFieldsForAccountsGK(MesType) +
      ", count(*) over() as t_RowCount " +
      "  from daccount_dbt acc "
      " where ";

    AttachWhereCondForAccountsGK(MesType, @query, params);
  end;

  private macro GetSelectFieldsForAccountsBO
  ( MesType : string, 
    AccViewName : string,
    RegBranchBO : string
  )
    if(MesType == "NS")
    return 
      "       acc.t_Account as t_Account, "
      "       PM_SCRHLP.GetFiidByIsoNumber(acc.t_CurrCode) as t_Code_Currency, "
      "       0 as t_GK_AccountID, "
      "       acc.t_TCBranch as t_Branch, "
      "       acc.t_Department as t_Department, "
      "       case "
      "         when acc.t_AccStatus = '1' then "
      "           acc.t_OpenDate "
      "         when acc.t_AccStatus = '0' then "
      "           Nvl( ( select acc_open.t_OpenDate "
      "                    from " + AccViewName + " acc_open "
      "                   where acc_open.t_Account = acc.t_Account "
      "                     and acc_open.t_CurrCode = acc.t_CurrCode "
      "                     and acc_open.t_TCBranch = acc.t_TCBranch "
      "                     and acc_open.t_AccStatus = '1' "
      "                ), "
      "                to_date('01010001', 'ddmmyyyy') "
      "              ) "
      "       end as t_OpenDate, "
      "       case "
      "         when acc.t_AccStatus = '0' then "
      "           acc.t_CloseDate "
      "         when acc.t_AccStatus = '1' then "
      "           Nvl( ( select acc_close.t_CloseDate "
      "                    from " + AccViewName + " acc_close "
      "                   where acc_close.t_Account = acc.t_Account "
      "                     and acc_close.t_CurrCode = acc.t_CurrCode "
      "                     and acc_close.t_TCBranch = acc.t_TCBranch "
      "                     and acc_close.t_AccStatus = '0' "
      "                ), "
      "                to_date('01010001', 'ddmmyyyy') "
      "              ) "
      "       end as t_CloseDate, "
      "       Nvl( acc.t_ContractDate, to_date('01010001', 'ddmmyyyy') ) as t_ContractDate, "
      "       Nvl( acc.t_Contract, chr(1) ) as t_ContractNumber, "
      "       acc.t_AccKind as t_AccKind, "
      "       acc.t_PrKons as t_PrKons, "
      "       acc.t_AccountKons as t_AccountKons, "
      "       acc.t_ClientID as t_ClientID, "
      "       acc.t_ClientINN as t_ClientINN, "
      "       acc.t_ClientKPP as t_ClientKPP, "
      "       acc.t_ClientOGRN as t_ClientOGRN, "
      "       acc.t_ClientName as t_ClientName, "
      "       acc.t_ClientAddress as t_ClientAddress, "
      "       acc.t_ClientType as t_ClientType, "
      "       cast( '" + RegBranchBO + "' as varchar2(2000) ) as t_RegBranchBO ";

    elif(MesType == "OS")
      return 
      "       acc.t_Account as t_Account, "
      "       PM_SCRHLP.GetFiidByIsoNumber(acc.t_CurrCode) as t_Code_Currency, "
      "       acc.t_TCBranch as t_Branch, "
      "       acc.t_Department as t_Department, "
      "       acc.t_TypeAccount as t_Kind_Account, "
      "       acc.t_ClientID as t_ClientID, "
      "       cast( '" + RegBranchBO + "' as varchar2(2000) ) as t_RegBranchBO ";

    else
      RunError("Ошибка программирования: неверное значение параметра MesType");
    end;
  end;

  private macro GetQueryParamsForAccountsBO
  ( MesType : string,
    RegBranchBO : string,
    AccViewName : string, 
    OperViewName : string, 
    RestSP : string, 
    query : @string, 
    params : TArray
  )
    query = 
      "select distinct " + 
      GetSelectFieldsForAccountsBO(MesType, AccViewName, RegBranchBO) +
      ", count(*) over() as t_RowCount " +
      "  from " + AccViewName + " acc ";

    if(IsSubBranch)
      query = query + 
      " where "
      "     ( acc.t_Department in (" + DepListStr + ") "
      "     or "
      "       acc.t_TCBranch in (" + DepListStr + ") "
      "     ) ";
    else
      query = query + 
      " where ( acc.t_Department = :Dep or acc.t_TCBranch = :Dep1 ) ";

      params[params.size] = SQLParam("", Branch);
      params[params.size] = SQLParam("", Branch);
    end;

    if(MesType == "NS") // отбираем данные для сообщения об открытых и закрытых счетах
      query = query + 
      "   and ( acc.t_AccStatus = '1' and acc.t_OpenDate between :DateBeg and :DateEnd or "
      "         acc.t_AccStatus = '0' and acc.t_CloseDate between :DateBeg1 and :DateEnd1 ) ";

      params[ params.size ] = SQLParam("", DateBeg);
      params[ params.size ] = SQLParam("", DateEnd);
      params[ params.size ] = SQLParam("", DateBeg);
      params[ params.size ] = SQLParam("", DateEnd);
    end;

    if(IsOperRest)
      if(not OperViewName)
        RunError("Не задан источник данных по операциям подсистемы " + RegBranchBO);
      end;

      if(not RestSP)
        RunError("Не задана хранимая процедура получения остатка по счету п/с " + RegBranchBO);
      end;

      query = query +
      "   and ( " + RestSP + "( acc.t_Account, "
      "                         acc.t_CurrCode, " +
      "                         acc.t_Department, "
      "                         acc.t_ClientID, "
      "                         :DateBeg2, "
      "                         :ACCREST_KIND_IN ) <> 0 " +
      "         or "
      "         " + RestSP + "( acc.t_Account, "
      "                         acc.t_CurrCode, " +
      "                         acc.t_Department, "
      "                         acc.t_ClientID, "
      "                         :DateBeg2, "
      "                         :ACCREST_KIND_OUT ) <> 0 " +
      "         or "
      "         exists ( select 1 from " + OperViewName + " op "
      "                   where op.t_ClientAcc = acc.t_Account "
      "                     and op.t_AccCurr = acc.t_CurrCode "
      "                     and op.t_Department = acc.t_Department "
      "                     and op.t_OperDate between :DateBeg3 and :DateEnd3 "
      "                     and op.t_Amount <> 0 ) "
      "       ) ";

      params[ params.size ] = SQLParam("", DateBeg);
      params[ params.size ] = SQLParam("", ACCREST_KIND_IN);
      params[ params.size ] = SQLParam("", DateEnd);
      params[ params.size ] = SQLParam("", ACCREST_KIND_OUT);
      params[ params.size ] = SQLParam("", DateBeg);
      params[ params.size ] = SQLParam("", DateEnd);
    end;

    if(BOFltByFIID)
      query = query +
      "   and acc.t_CurrCode " + BOFltByFIID + " :BOFI ";

      params[ params.size ] = SQLParam("", GetISO_Number(BOFIID));
    end;

    if(BOFltByAcc)
      var CmpResult : string = IfThenElse(BOFltByAcc == "=", "1", "0");
      query = query + 
      "   and rsb_mask.CompareStringWithMask( :BOAccMask, acc.t_Account ) = " + CmpResult;

      params[ params.size ] = SQLParam("", BOAccMask);
    end;

    if(BOFltByClient)
      query = query +
      "   and acc.t_ClientID " + BOFltByClient + " :BOClientID ";

      params[ params.size ] = SQLParam("", BOClientID);
    end;
  end;

  private macro GetQueryParamsForOperationsGK
  ( query : @string, 
    params : TArray
  )
    query = 
      "select to_char(trn.t_AccTrnID) as t_UKey, "
      "       Nvl(pmd.t_PaymentID, 0) as t_PaymentID, "
      "       trn.t_Date_Carry as t_Date, "
      "       trn.t_SystemTime as t_Time, "
      "       Nvl(rm.t_Date, trn.t_Date_Carry) as t_DCM_Date, "
      "       Nvl(rm.t_Number, trn.t_Numb_Document) as t_DCM_Num, "
      "       trn.t_Account_Payer as t_PayerAccount, "
      "       trn.t_FIID_Payer as t_PayerFIID, "
      "       Nvl(pm.t_PayerAccount, chr(1)) as t_PayerAccountPaym, "
      "       Nvl(pm.t_FIID, -1) as t_PayerFIIDPaym, "
      "       trn.t_Account_Receiver as t_ReceiverAccount, "
      "       trn.t_FIID_Receiver as t_ReceiverFIID, "
      "       Nvl(pm.t_ReceiverAccount, chr(1)) as t_ReceiverAccountPaym, "
      "       Nvl(pm.t_PayFIID, -1) as t_ReceiverFIIDPaym, "
      "       trn.t_Sum_NatCur as t_Sum, "
      "       trn.t_Sum_Payer as t_DT_SumC, "
      "       trn.t_Sum_Receiver as t_KT_SumC, "
      "       Nvl(rm.t_Ground, chr(1)) as t_Ground, "
      "       chr(1) as t_CashSymbols, "
      "       Nvl(pm.t_PayerBankID, 0) as t_PayerBankID, "
      "       Nvl(rm.t_PayerCorrAccNostro, chr(1)) as t_PayerBankAccount, "
      "       Nvl(rm.t_PayerINN, chr(1)) as t_PayerINN, "
      "       chr(1) as t_PayerKPP, "
      "       chr(1) as t_PayerOGRN, "
      "       Nvl(rm.t_PayerName, chr(1)) as t_PayerName, "
      "       Nvl(pm.t_ReceiverBankID, 0) as t_ReceiverBankID, "
      "       Nvl(rm.t_ReceiverCorrAccNostro, chr(1)) as t_ReceiverBankAccount, "
      "       Nvl(rm.t_ReceiverINN, chr(1)) as t_ReceiverINN, "
      "       chr(1) as t_ReceiverKPP, "
      "       chr(1) as t_ReceiverOGRN, "
      "       Nvl(rm.t_ReceiverName, chr(1)) as t_ReceiverName, "
      "       decode(trn.t_Branch, 0, accdb.t_Branch, trn.t_Branch) as t_Branch, "
      "       trn.t_Department as t_Department, "
      "       Nvl(pm.t_Payer, 0) as t_Payer, "
      "       Nvl(pm.t_Receiver, 0) as t_Receiver, "
      "       Nvl(rm.t_UIN, chr(1)) as t_UIN, "
      "       CAST( Nvl(dbprop.t_Group, :PAYMENTS_GROUP_INTERNAL) AS NUMBER(5) ) as t_PayerGroup, "
      "       CAST( Nvl(crprop.t_Group, :PAYMENTS_GROUP_INTERNAL) AS NUMBER(5) ) as t_ReceiverGroup, "
      "       cast( chr(1) as varchar2(2000) ) as t_RegBranchBO, " +
      "       count(*) over() as t_RowCount " +
      "  from dacctrn_dbt trn, dpmdocs_dbt pmd, dpmrmprop_dbt rm, dpmpaym_dbt pm, "
      "       dpmprop_dbt dbprop, dpmprop_dbt crprop, "
      "       daccount_dbt accdb, daccount_dbt acccr "
      " where pmd.t_AccTrnID(+) = trn.t_AccTrnID "
      "   and rm.t_PaymentID(+) = pmd.t_PaymentID "
      "   and pm.t_PaymentID(+) = pmd.t_PaymentID "
      "   and dbprop.t_PaymentID(+) = pmd.t_PaymentID "
      "   and dbprop.t_DebetCredit(+) = 0 /*PRT_Debet*/ "
      "   and crprop.t_PaymentID(+) = pmd.t_PaymentID "
      "   and crprop.t_DebetCredit(+) = 1 /*PRT_Credit*/ "
      "   and accdb.t_AccountID = trn.t_AccountID_Payer "
      "   and acccr.t_AccountID = trn.t_AccountID_Receiver "
      "   and trn.t_State = 1 /*TRN_STATE_FACT*/ "
      "   and trn.t_Date_Carry between :DateBeg and :DateEnd ";

    params[ params.size ] = SQLParam("", PAYMENTS_GROUP_INTERNAL);
    params[ params.size ] = SQLParam("", PAYMENTS_GROUP_INTERNAL);
    params[ params.size ] = SQLParam("", DateBeg);
    params[ params.size ] = SQLParam("", DateEnd);

    if(IsSubBranch)
      query = query + 
      "   and ( trn.t_Department in (" + DepListStr + ") "
      "       or "
      "         accdb.t_Branch in (" + DepListStr + ") "
      "       ) ";
    else
      query = query + 
      "   and ( trn.t_Department = :Dep or accdb.t_Branch = :Dep1 ) ";

      params[params.size] = SQLParam("", Branch);
      params[params.size] = SQLParam("", Branch);
    end;

    if(GKFltByChapter)
      query = query +
      "   and trn.t_Chapter " + GKFltByChapter + " :GKChapter ";

      params[ params.size ] = SQLParam("", GKChapter);
    end;

    if(GKFltByFIID)
      query = query +
      "   and ( trn.t_FIID_Payer " + GKFltByFIID + " :GKFIID or "
      "         trn.t_FIID_Receiver " + GKFltByFIID + " :GKFIID ) ";

      params[ params.size ] = SQLParam("", GKFIID);
      params[ params.size ] = SQLParam("", GKFIID);
    end;

    if(GKFltByAcc)
      var CmpResult : string = IfThenElse(GKFltByAcc == "=", "1", "0");
      query = query + 
      "   and ( rsb_mask.CompareStringWithMask( :GKAccMask, "
      "                                         trn.t_Account_Payer ) = " + CmpResult +
      "         or "
      "         rsb_mask.CompareStringWithMask( :GKAccMask, "
      "                                         trn.t_Account_Receiver ) = " + CmpResult + " ) ";

      params[ params.size ] = SQLParam("", GKAccMask);
      params[ params.size ] = SQLParam("", GKAccMask);
    end;

    if(GKFltByClient)
      query = query +
      "   and ( accdb.t_Client " + GKFltByClient + " :GKClientID or "
      "         acccr.t_Client " + GKFltByClient + " :GKClientID ) ";

      params[ params.size ] = SQLParam("", GKClientID);
      params[ params.size ] = SQLParam("", GKClientID);
    end;

    if( IsExSynt )
      query = query +
      "   and ( accdb.t_Type_Account not like '%И%' and "
      "         acccr.t_Type_Account not like '%И%' ) ";
    end;
           
  end;

  private macro GetQueryParamsForOperationsBO
  ( RegBranchBO : string,
    AccViewName : string, 
    OperViewName : string, 
    query : @string, 
    params : TArray
  )
    query = 
      "select distinct "
      "       Nvl(db.t_OperID, cr.t_OperID) as t_UKey, "
      "       0 as t_PaymentID, "
      "       Nvl(db.t_OperDate, cr.t_OperDate) as t_Date, "
      "       Nvl( Nvl(db.t_OperTime, cr.t_OperTime), :ZeroTime ) as t_Time, "
      "       Nvl(db.t_DocDate, cr.t_DocDate) as t_DCM_Date, "
      "       Nvl(db.t_DocNumber, cr.t_DocNumber) as t_DCM_Num, "
      "       Nvl(db.t_ClientAcc, cr.t_ContrAcc) as t_PayerAccount, "
      "       PM_SCRHLP.GetFiidByIsoNumber( Nvl(db.t_AccCurr, chr(1)) ) as t_PayerFIID, "
      "       chr(1) as t_PayerAccountPaym, "
      "       -1 as t_PayerFIIDPaym, "
      "       Nvl(cr.t_ClientAcc, db.t_ContrAcc) as t_ReceiverAccount, "
      "       PM_SCRHLP.GetFiidByIsoNumber( Nvl(cr.t_AccCurr, chr(1)) ) as t_ReceiverFIID, "
      "       chr(1) as t_ReceiverAccountPaym, "
      "       -1 as t_ReceiverFIIDPaym, "
      "       cast( Nvl(db.t_AmountNatCur, cr.t_AmountNatCur) as NUMBER(32,12) ) as t_Sum, "
      "       cast( Nvl(db.t_Amount, cr.t_AmountContrAcc) as NUMBER(32,12) ) as t_DT_SumC, "
      "       cast( Nvl(cr.t_Amount, db.t_AmountContrAcc) as NUMBER(32,12) ) as t_KT_SumC, "
      "       Nvl(db.t_Ground, cr.t_Ground) as t_Ground, "
      "       Nvl( Nvl(db.t_CashSymbols, cr.t_CashSymbols), chr(1) ) as t_CashSymbols, "
      "       Nvl(cr.t_BankID, 0) as t_PayerBankID, "
      "       Nvl(cr.t_BankAcc, chr(1)) as t_PayerBankAccount, " +
      "       case "
      "         when cr.t_ContrINN is null or cr.t_ContrINN = chr(1) then "
      "           Nvl(accdb.t_ClientINN, chr(1)) "
      "         else "
      "           cr.t_ContrINN "
      "       end as t_PayerINN, "
      "       case "
      "         when cr.t_ContrKPP is null or cr.t_ContrKPP = chr(1) then "
      "           Nvl(accdb.t_ClientKPP, chr(1)) "
      "         else "
      "           cr.t_ContrKPP "
      "       end as t_PayerKPP, "
      "       Nvl(accdb.t_ClientOGRN, chr(1)) as t_PayerOGRN, "
      "       Nvl(cr.t_ContrName, chr(1)) as t_PayerName, "
      "       Nvl(db.t_BankID, 0) as t_ReceiverBankID, "
      "       Nvl(db.t_BankAcc, chr(1)) as t_ReceiverBankAccount, " +
      "       case "
      "         when db.t_ContrINN is null or db.t_ContrINN = chr(1) then "
      "           Nvl(acccr.t_ClientINN, chr(1)) "
      "         else "
      "           db.t_ContrINN "
      "       end as t_ReceiverINN, "
      "       case "
      "         when db.t_ContrKPP is null or db.t_ContrKPP = chr(1) then "
      "           Nvl(acccr.t_ClientKPP, chr(1)) "
      "         else "
      "           db.t_ContrKPP "
      "       end as t_ReceiverKPP, "
      "       Nvl(acccr.t_ClientOGRN, chr(1)) as t_ReceiverOGRN, "
      "       Nvl(db.t_ContrName, chr(1)) as t_ReceiverName, "
      "       Nvl(accdb.t_TCBranch, acccr.t_TCBranch) as t_Branch, "
      "       Nvl(db.t_Department, cr.t_Department) as t_Department, "
      "       Nvl(accdb.t_ClientID, 0) as t_Payer, "
      "       Nvl(acccr.t_ClientID, 0) as t_Receiver, "
      "       chr(1) as t_UIN, "
      "       CAST( Decode( db.t_OperID, "
      "                     null, :PAYMENTS_GROUP_EXTERNAL, "
      "                     :PAYMENTS_GROUP_INTERNAL ) "
      "             AS NUMBER(5) ) as t_PayerGroup, "
      "       CAST( Decode( cr.t_OperID, "
      "                     null, :PAYMENTS_GROUP_EXTERNAL, "
      "                     :PAYMENTS_GROUP_INTERNAL ) "
      "             AS NUMBER(5) ) as t_ReceiverGroup, "
      "       cast( '" + RegBranchBO + "' as varchar2(2000) ) as t_RegBranchBO "
      "     , count(*) over() as t_RowCount " +
      "  from (select * from " + OperViewName + " where t_DK = 'D') db "
      "       full outer join " 
      "       (select * from " + OperViewName + " where t_DK = 'K') cr "
      "         on ( cr.t_OperID = db.t_OperID ) "
      "       left outer join " + AccViewName + " accdb "
      "         on ( accdb.t_Account = db.t_ClientAcc and "
      "              accdb.t_CurrCode = db.t_AccCurr and "
      "              accdb.t_Department = db.t_Department ) "
      "       left outer join " + AccViewName + " acccr "
      "         on ( acccr.t_Account = cr.t_ClientAcc and "
      "              acccr.t_CurrCode = cr.t_AccCurr and "
      "              acccr.t_Department = cr.t_Department ) "
      " where ( db.t_OperDate between :DateBeg and :DateEnd or "
      "         cr.t_OperDate between :DateBeg1 and :DateEnd1 ) ";

    params[ params.size ] = SQLParam("", time(0,0,0,0));
    params[ params.size ] = SQLParam("", PAYMENTS_GROUP_EXTERNAL);
    params[ params.size ] = SQLParam("", PAYMENTS_GROUP_INTERNAL);
    params[ params.size ] = SQLParam("", PAYMENTS_GROUP_EXTERNAL);
    params[ params.size ] = SQLParam("", PAYMENTS_GROUP_INTERNAL);
    params[ params.size ] = SQLParam("", DateBeg);
    params[ params.size ] = SQLParam("", DateEnd);
    params[ params.size ] = SQLParam("", DateBeg);
    params[ params.size ] = SQLParam("", DateEnd);

    if(IsSubBranch)
      query = query + 
      " and ( db.t_Department in (" + DepListStr + ") "
      "     or "
      "       cr.t_Department in (" + DepListStr + ") "
      "     ) ";
    else
      query = query + 
      " and ( db.t_Department = :Dep or cr.t_Department = :Dep1 or "
      "       accdb.t_TCBranch = :Dep2 or acccr.t_TCBranch = :Dep3 ) ";

      params[params.size] = SQLParam("", Branch);
      params[params.size] = SQLParam("", Branch);
      params[params.size] = SQLParam("", Branch);
      params[params.size] = SQLParam("", Branch);
    end;

    if(BOFltByFIID)
      query = query +
      "   and ( db.t_AccCurr " + BOFltByFIID + " :BOFI or "
      "         cr.t_AccCurr " + BOFltByFIID + " :BOFI ) ";

      params[ params.size ] = SQLParam("", GetISO_Number(BOFIID));
      params[ params.size ] = SQLParam("", GetISO_Number(BOFIID));
    end;

    if(BOFltByAcc)
      var CmpResult : string = IfThenElse(BOFltByAcc == "=", "1", "0");
      query = query + 
      "   and ( rsb_mask.CompareStringWithMask( :BOAccMask, "
      "                                         db.t_ClientAcc ) = " + CmpResult +
      "         or "
      "         rsb_mask.CompareStringWithMask( :BOAccMask, "
      "                                         cr.t_ClientAcc ) = " + CmpResult + " ) ";

      params[ params.size ] = SQLParam("", BOAccMask);
      params[ params.size ] = SQLParam("", BOAccMask);
    end;

    if(BOFltByClient)
      query = query +
      "   and ( accdb.t_ClientID " + BOFltByClient + " :BOClientID or "
      "         acccr.t_ClientID " + BOFltByClient + " :BOClientID ) ";

      params[ params.size ] = SQLParam("", BOClientID);
      params[ params.size ] = SQLParam("", BOClientID);
    end;
  end;

  private macro GetQueriesAndParametersForEachDataSource
  ( MesType : string,
    Queries : TArray,
    Parameters : TArray,
    OrderByFlds : @string
  )
    var query : string, params : TArray;

    if( IncludeAccGK
        and (MesType != "OS")//optimize
      )
      query = "";
      params = TArray();

      if(MesType == "VS")
        GetQueryParamsForOperationsGK(@query, params);
      else
        GetQueryParamsForAccountsGK(MesType, @query, params);
      end;

      Queries[Queries.size] = query;
      Parameters[Parameters.size] = params;
    end;

    if(IncludeAccBO)
      for(var i : integer, 0, AccViews.size - 1)
        query = "";
        params = TArray();

        if(MesType == "VS")
          GetQueryParamsForOperationsBO
          ( RegBranchesBO[i], AccViews[i], OperViews[i], 
            @query, params );
        else
          GetQueryParamsForAccountsBO
          ( MesType, RegBranchesBO[i], AccViews[i], OperViews[i], RestStoredProcs[i], 
            @query, params );
        end;

        Queries[Queries.size] = query;
        Parameters[Parameters.size] = params;
      end;
    end;

    if(MesType == "VS")
      OrderByFlds = TSNodeField;
    else
      OrderByFlds = TSNodeField + ", t_Account, t_Code_Currency";
    end;
  end;

  private macro GetUnitedQueryAndParams
  ( Queries : TArray,
    Parameters : TArray,
    query : @string,
    params : TArray,
    OrderByFlds : string
  )
    query =
      "select * from " +
      " ( " +
      "   ( " + Queries[0] + " ) ";
    
    var i : integer = 1;
    while(i < Queries.size)

      query = query + 
      "   union all " +
      "   ( " + Queries[i] + " ) ";

      i = i + 1;
    end;

    query = query + 
      " ) " +
      " order by " + OrderByFlds;

    for(var p : TArray, Parameters)
      for(var OneParam : SQLParam, p)
        params[ params.size ] = OneParam;
      end;
    end;
  end;

  private macro ExecuteQuery(MesType : string, query : string, params : TArray)
    if(MesType == "NS")
      rsOpenAndClosed = execSQLselect(query, params);
    elif(MesType == "OS")
      rsAccRests = execSQLselect(query, params);
    elif(MesType == "VS")
      rsOperations = execSQLselect(query, params);
    else
      RunError("Ошибка программирования: неверное значение параметра MesType");
    end;
  end;

  private macro GetRecords(MesType : string)
    var Queries : TArray = TArray(), 
        Parameters : TArray = TArray(), 
        OrderByFlds : string = "";
    GetQueriesAndParametersForEachDataSource(MesType, Queries, Parameters, @OrderByFlds);

    var query : string = "", params : TArray = TArray();
    GetUnitedQueryAndParams(Queries, Parameters, @query, params, OrderByFlds);
    
    ExecuteQuery(MesType, query, params);
  end;

  private macro GetPrevOperDay(dt : date) : date
    var rs : RsdRecordset = execSQLselectPrm
      ( "select Nvl( Max(t_RestDate), to_date('01010001', 'ddmmyyyy') ) "
        "  from drestdate_dbt "
        " where t_RestDate < :p_Date ",
        SQLParam("p_Date", dt)
      );

    if(rs and rs.moveNext())
      return rs.value(0);
    end;

    return date(0,0,0);
  end;

  private macro GetRecordsForAccRests
    var PrevBegOperDay : date = GetPrevOperDay(DateBeg);
    if(PrevBegOperDay == date(0,0,0))
      PrevBegOperDay = DateBeg;
    end;

    var query : string =
      "select Nvl(rest.t_RestDate, to_date('01010001', 'ddmmyyyy')) as t_RestDate, "
      "       rest.t_Rest as t_Rest, "
      "       case "
      "         when acc.t_Code_Currency = 0 /*NATCUR*/ then 0 "
      "         else ( select restC.t_Rest "
      "                  from drestdate_dbt restC "
      "                 where restC.t_AccountID = rest.t_AccountID "
      "                   and restC.t_RestDate = rest.t_RestDate "
      "                   and restC.t_RestCurrency = acc.t_Code_Currency ) "
      "       end as t_RestC, "
      "       acc.t_AccountID as t_AccountID, "
      "       acc.t_Account as t_Account, "
      "       acc.t_Code_Currency as t_AccFIID, "
      "       acc.t_Branch as t_Branch, "
      "       acc.t_Department as t_Department, "
      "       acc.t_Kind_Account as t_Kind_Account, "
      "       acc.t_Client as t_ClientID, "
      "       acc.t_Open_Date as t_AccOpenDate, "
      "       acc.t_Close_Date as t_AccCloseDate, " +
      "       ( select Nvl( Sum(t_Sum_NatCur), 0 ) "
      "           from dacctrn_dbt "
      "          where t_AccountID_Payer = acc.t_AccountID "
      "            and t_Date_Carry = rest.t_RestDate "
      "                   and t_Sum_Payer <> 0 "
      "       ) as t_TurnDb, "
      "       case "
      "         when acc.t_Code_Currency = 0 /*NATCUR*/ then 0 "
      "         else ( select Nvl( Sum(t_Sum_Payer), 0 ) "
      "                  from dacctrn_dbt "
      "                 where t_AccountID_Payer = acc.t_AccountID "
      "                   and t_Date_Carry = rest.t_RestDate "
      "                   and t_Sum_Payer <> 0 "
      "              ) "
      "       end as t_TurnDbC, " +
      "       ( select Nvl( Sum(t_Sum_NatCur), 0 ) "
      "           from dacctrn_dbt "
      "          where t_AccountID_Receiver = acc.t_AccountID "
      "            and t_Date_Carry = rest.t_RestDate "
      "                   and t_Sum_Receiver <> 0 "
      "       ) as t_TurnCr, "
      "       case "
      "         when acc.t_Code_Currency = 0 /*NATCUR*/ then 0 "
      "         else ( select Nvl( Sum(t_Sum_Receiver), 0 ) "
      "                  from dacctrn_dbt "
      "                 where t_AccountID_Receiver = acc.t_AccountID "
      "                   and t_Date_Carry = rest.t_RestDate "
      "                   and t_Sum_Receiver <> 0 "
      "              ) "
      "       end as t_TurnCrC " +
      ", count(*) over() as t_RowCount " +      
      "  from drestdate_dbt rest, daccount_dbt acc "
      " where rest.t_AccountID(+) = acc.t_AccountID "
      "   and rest.t_RestCurrency(+) = 0 /*NATCUR*/ "
      "   and rest.t_RestDate(+) between :PrevBegOperDay and :DateEnd "
      "   and acc.t_Open_Date <= :DateEnd "
      "   and ( acc.t_Close_Date >= :DateBeg or "
      "         acc.t_Close_Date = to_date('01010001', 'ddmmyyyy') ) ";

    var params : TArray = makeArray( SQLParam("", PrevBegOperDay),
                                     SQLParam("", DateEnd),
                                     SQLParam("", DateEnd),
                                     SQLParam("", DateBeg) );

    query = query +
      "   and ";
    AttachWhereCondForAccountsGK("OS", @query, params);

    query = query +
      " order by acc." + TSNodeField + ", acc.t_AccountID, rest.t_RestDate ";

    rsAccRestsGK = execSQLselect(query, params);
  end;

  macro SelectData
    if(NeedOpenAndClosedAccounts)
      GetRecords("NS");
    end;
    
    if(NeedAccRests)
      //optimize
      if(IncludeAccGK)
        GetRecordsForAccRests();
      end;

      if(IncludeAccBO)
        GetRecords("OS");
      end;
    end;

    if(NeedAccTrns)
      GetRecords("VS");
    end;

  end;

  macro GetDateBeg : date
    return DateBeg;
  end;

  macro GetDateEnd : date
    return DateEnd;
  end;
end;


class TXmlNodesAdd(p_node : object, p_xml : object)
  private var 
    node : object = p_node,
    xml : object = p_xml;

  if(not xml)
    xml = ActiveX("Microsoft.XMLDOM");
  end;

  macro AddText(TextNodeName : string, Text : string)
    var elem : object = xml.createElement(TextNodeName);

    elem.appendChild( xml.createTextNode(Text) );

    node.appendChild(elem);
  end;
end;


macro JoinClientInfoForCB(Elements : TArray) : string
  var NonEmptyElements : TArray = TArray();
  for(var s : string, Elements)
    if(s)
      NonEmptyElements[ NonEmptyElements.size ] = s;
    end;
  end;

  var ClientInfo : string = join(NonEmptyElements, "//");

  if(ClientInfo)
    ClientInfo = "//" + ClientInfo + "//";
  end;

  return ClientInfo;
end;

private macro GetShortOrFullName(PartyID : integer) : string
  var Name : string = Bnk_GetPartyShortName(PartyID);

  if(not Name)
    Name = Bnk_GetPartyName(PartyID);
  end;

  return Name;
end;

private macro GetLegalOrPostAddress(PartyID : integer) : string
  var AddrStr : string = "";

  RECORD adress(adress);
  if( НайтиЮридическийАдресСубъекта(PartyID, adress) )
    AddrStr = adress.adress;
  end;

  if( not AddrStr and НайтиПочтовыйАдресСубъекта(PartyID, adress) )
    AddrStr = adress.adress;
  end;

  return AddrStr;
end;

private macro GetAnyAddress(PartyID : integer) : string
  var AddrStr : string = "";

  var rs : RsdRecordset = execSQLselectPrm
    ( "select t_Adress "
      "  from dadress_dbt "
      " where t_PartyID = :PartyID "
      "   and t_Adress <> chr(1) ",
      SQLParam("PartyID", PartyID)
    );

  if(rs and rs.moveNext())
    AddrStr = rs.value("t_Adress");
  end;

  return AddrStr;
end;

private macro GetActivityKindForClientInfo(ClientID : integer) : string
  var ActivityKind : string = "";

  var AttrID = GetPartyMainAttr_PartyType(ClientID);
  if(AttrID == PARTY_ATTR_PTTYPE_NOTARIUS)
    ActivityKind = "нотариус";
  elif(AttrID == PARTY_ATTR_PTTYPE_ADVOCAT)
    ActivityKind = "адвокат";
  elif(AttrID == PARTY_ATTR_PTTYPE_KFH)
    ActivityKind = "глава крестьянско-фермерского хозяйства";
  elif(AttrID == PARTY_ATTR_PTTYPE_TRUSTMANAGER)
    ActivityKind = "доверительный управляющий";
  elif(AttrID == PARTY_ATTR_PTTYPE_MANAGPARTNER)
    ActivityKind = "управляющий товарищ инвестиционного товарищества";
  elif(AttrID == PARTY_ATTR_PTTYPE_ARBITRMANAGER)
    ActivityKind = "арбитражный управляющий";
  elif( IsIndividualEmployer(ClientID) )
    ActivityKind = "индивидуальный предприниматель";
  end;

  return ActivityKind;
end;

private macro GetAccStrForClientInfo(Account : string, BankID : integer) : string
  var AccInfo : string = "р/с " + Account;

  if(BankID and (BankID > 0))
    AccInfo = AccInfo + " в " + GetShortOrFullName(BankID);

    var AddrStr : string = GetLegalOrPostAddress(BankID);
    if(not AddrStr)
      AddrStr = GetAnyAddress(BankID);
    end;
    if(AddrStr)
      AccInfo = AccInfo + " " + AddrStr;
    end;
  end;

  return AccInfo;
end;

macro GetClientInfoForCB
( ClientID : integer,
  OperationID : string,
  Account : string,
  BankID : integer
  
) : string

  var Info : string = "";

  var InfoElements : TArray;

  var Name : string = "", AddrStr : string = "";
  RECORD adress(adress);

  if( IsPerson(ClientID) ) // физ.лицо
    Name = Bnk_GetPartyName(ClientID);

    var ActivityKind : string = GetActivityKindForClientInfo(ClientID);

    if( НайтиПочтовыйАдресСубъекта(ClientID, adress) )
      AddrStr = adress.adress;
    end;
    if( not AddrStr )
      if( НайтиАдресСубъекта(ClientID, PTADDR_REAL, adress) )
        AddrStr = adress.adress;
      end;
    end;

    InfoElements = makeArray(Name, OperationID, ActivityKind, AddrStr);
  else // юр.лицо
    Name = GetShortOrFullName(ClientID);

    AddrStr = GetLegalOrPostAddress(ClientID);

    InfoElements = makeArray(Name, AddrStr);
  end;

  if(Account)
    var AccInfo : string = GetAccStrForClientInfo(Account, BankID);
    InfoElements[ InfoElements.size ] = AccInfo;
  end;

  Info = JoinClientInfoForCB(InfoElements);

  return Info;
end;


class TRecInfoOS(p_IsAccRestsGK)
  var Account : string = "",
      AccFIID : integer = -1,
      AccKind : string = "",
      AccountID : integer = 0,
      AccTSNode : integer = 0,

      InRest : money = $0,
      InRestC : money = $0,
      
      OutRest : money = $0,
      OutRestC : money = $0,

      ObDt : money = $0,
      ObDtC : money = $0,
      ObKt : money = $0,
      ObKtC : money = $0;

  var IsAccRestsGK : bool = true;
  if( ValType(p_IsAccRestsGK) == V_BOOL )
    IsAccRestsGK = p_IsAccRestsGK;
  end;
  
end;

macro DeleteCreatedFiles(CreatedFiles : TArray)
  var Msg : string = "";

  for(var FName : string, CreatedFiles)
    if( not RemoveFile(FName) )
      Msg = Msg + FName + ", ";
    end;
  end;

  if(Msg)
    Msg = SubStr(Msg, 1, StrLen(Msg) - 2);
    Msg = "Не удалось удалить файлы: " + Msg + ". Удалите их вручную";
    msgboxEx(Msg, 0);
  end;
end;

macro DeleteLastWrongFile(CreatedFiles : TArray)
  var Msg : string = "";
  var Fname : string = "";

  FName = CreatedFiles[ CreatedFiles.size - 1 ];

  if( not RemoveFile(FName) )
    Msg = Msg + FName;
  end;

  if(Msg)
    Msg = SubStr(Msg, 1, StrLen(Msg) - 2);
    Msg = "Не удалось удалить файл: " + Msg + ". Удалите его вручную";
    msgboxEx(Msg, 0);
  end;
end;

macro MakeRightKPP( KPP:@string ):bool
  var tmpStr = KPP;
  
  // 1. Удаляем начальные и конечные нечисловые символы
  tmpStr = DeleteNotDigitSymbols( tmpStr );
  // 2. Приводим к верхнему регистру
  tmpStr = StrUpr( tmpStr ); 
  // 3. Проверка на соответствие формату "\d{4}[0-5A-Z]{2}\d{3}" по ТЗ 2424
  if( tmpStr != "0" )    
    if( RegExMatch( tmpStr, "\\d{4}[0-5A-Z]{2}\\d{3}" ) )
      KPP = tmpStr;
      return true;
    end;
  end;

  return false;

end;