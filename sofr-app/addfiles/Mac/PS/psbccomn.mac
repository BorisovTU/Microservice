import PaymInter, PSInter, OprInter, likepy, oralib, globals, PSBCInter;

//-----------------------------------------------------------------------------
// Название вида документа
//-----------------------------------------------------------------------------
MACRO BC_KindOrderName( KindOrder:integer ):string

  var query:string = "SELECT llv.T_NAME " +
                     "  FROM dllvalues_dbt llv " +
                     " WHERE llv.T_LIST = :lst " +
                     "   AND llv.T_ELEMENT = :elm;";

  var params:TArray = makeArray( SQLParam( "lst", 1632      ), 
                                 SQLParam( "elm", KindOrder ) );

  var rs:RsdRecordset = execSQLselect( query, params, true );
  if( rs and rs.moveNext() )
    return rs.value(0);
  else
    return "";
  end;

ONERROR(x)
  
  MsgBox( "Ошибка получения названия вида документа|" + x.Message );
  return "";

END;

//-----------------------------------------------------------------------------
// Установить статус первичного документа
//-----------------------------------------------------------------------------
MACRO BC_SetPrimDocumentState( Payment:RsbPayment, state:integer )
  var obj:object;

  if( Payment.DocKind == PS_BUYCURORDER )
    obj = GenObject( "RsbBuyCurrencyOrder", Payment.DocumentID );
    obj.State = state;
  end;
END;

//-----------------------------------------------------------------------------
//  Проверить, существует ли счет в базе
//-----------------------------------------------------------------------------
MACRO AccountExistAndOpen( FIID:integer, Account:string, Chapter:integer ):bool
  
  VAR select:string = " select acc.t_department, acc.t_type_account, acc.t_client " +
                      " from daccount_dbt acc " +
                      " where acc.T_CHAPTER = :chapter " +
                      "   and acc.T_CODE_CURRENCY = :fiid " +
                      "   and acc.T_ACCOUNT = :account "+
                      "   and acc.T_Open_Close = chr(0)";
  VAR params:TArray = makeArray(SQLParam( "chapter", Chapter ),     
                                SQLParam( "fiid", FIID ),     
                                SQLParam( "account", Account ));
  
  VAR rset:RsdRecordset = execSQLselect( select, params, TRUE );
  if( rset and rset.moveNext() )
    return true;
  end;     
  return false;
  
ONERROR(x)
  MsgBox( x.Message );
  return false;
END;


//-----------------------------------------------------------------------------
// Заполнение кода вида валютной операции
//-----------------------------------------------------------------------------
MACRO BC_SetVOCode( BuyOrder:object, PaymentChild:RsbPayment, Action:integer )

  // субъект резидентом
  private macro isResident( PartyID:integer ) : integer
    FILE fparty ("party.dbt" );
    fparty.PartyId = PartyId;

    if( getEQ( fparty ) )
      if( fparty.NotResident == "X" )
        return 2;
      end;
    end;
    return 1;
  end;

  var Resident:integer    = isResident( BuyOrder.Payment.Payer);
  var AccountSide:integer = 0;
  var llvalues:TBFile = TBFile( "llvalues.dbt", "R", 0 );

  if( CompareStrWithMasks( "401-408", PaymentChild.PayerAccount ) == 0 )
    AccountSide = 1;
  elif( CompareStrWithMasks( "401-408", PaymentChild.ReceiverAccount ) == 0 )
    AccountSide = 2;
  end;
  
  // найти запись dpsbcvo_dbt
  var query:string = "SELECT VO.T_VOCODE " +
                      " FROM DPSBCVO_DBT VO " +
                      "WHERE VO.T_BCORDKIND = :BCORDKIND " +
                       " AND VO.T_RESIDENT = :RESIDENT " +
                       " AND VO.T_BCORDACTION = :BCORDACTION " +
                       " AND VO.T_ACCOUNTSIDE = :ACCOUNTSIDE " +
                       " AND VO.T_DATEFROM <= :CRDATE " +
                       " AND VO.T_DATETO > :CRDATE ";

  var params:TArray = makeArray( SQLParam( "BCORDKIND"  , BuyOrder.BCOrdKind ), 
                                 SQLParam( "RESIDENT"   , Resident           ),
                                 SQLParam( "BCORDACTION", Action             ),
                                 SQLParam( "ACCOUNTSIDE", AccountSide        ),
                                 SQLParam( "CRDATE"     , {curdate}          ) );

  var rs:RsdRecordset = execSQLselect( query, params, true );
  if( rs and rs.moveNext() )

    if(PaymentChild.BaseFIID == 0)
      llvalues.Clear();
      llvalues.rec.List = 1805 /*OBJTYPE_PURPOSE117*/; 
      llvalues.rec.Element = rs.value(0);

      if( llvalues.GetEQ() )
        PaymentChild.Ground = "{VO" + llvalues.rec.Code + "} " + llvalues.rec.Name + ". " + PaymentChild.Ground;
      end;
    end;

    PM_FillPropVOOnDefault(PaymentChild.PaymentID, rs.value(0));
  end;
  return 0;
END;

/* биржевая заявка */
CLASS (TRecHandler)TExchangeClaimRec( RequestID:integer )

  /*конструктор*/
  InitTRecHandler("psbcexch.dbt", "bank.def");

  /*получить проводку*/
  MACRO get():bool
    var fdocument:TBFile = TBFile("psbcexch.dbt", "R", 0, "psbcexch.dbt", "bank.def");
    if( fdocument and rec.RequestID )
      fdocument.rec.RequestID = rec.RequestID;
      if( fdocument.GetEQ() )
        Copy( _extObj, fdocument );
        return TRUE;
      else
        return FALSE;
      end;
    else
      return FALSE;
    end;

  END;
  rec.RequestID = RequestID;

END;
