/*
  $Name:        lc005_20.mac
  $Module:      Документарные аккредитивы
  $Description: Макрос шага
*/

// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//  Назначение  : Макрос шага
//
//  Описание    : Операция - 3411   - "Аккредитив"
//                Блок     - 341005 - "Обработка выплаты"
//                Шаг      - 20     - "Открытие счетов требований/обязательств"
//
//  Программист : Чукина Т.А.
//
//  Создан      : 23.05.2013
//
// --------------------------------------------------------------------

IMPORT LCInter, oralib, likepy, globals, "lc_categ.mac";

var LCDocObj:RsbLetterOfCredit;

MACRO ExecuteStep()
  var needOpenAccount : bool = false;

  if(LCDocObj.LCaccount == "")
    needOpenAccount = true; // не задан счет lcdoc.Lcaccount 
  else
    var rs : RsdRecordset = execSQLselect 
                              ( "select t_Close_Date "
                                "  from daccount_dbt "
                                " where t_Chapter = :Chapter "
                                "   and t_Account = :Account "
                                "   and t_Code_Currency = :FIID ",
                                makeArray( SQLParam("Chapter", 1/*CHAPT1*/       ),
                                           SQLParam("Account", LCDocObj.LCaccount),
                                           SQLParam("FIID"   , LCDocObj.FIID     ) )
                              );
    if(rs and rs.moveNext)
      var CloseDate : date = rs.value("t_Close_Date");
      if( (CloseDate != date(0, 0, 0)) and (CloseDate <= {curdate}) )
        needOpenAccount = true; // счет закрыт в валюте lcdoc.FIID (глава 1) на дату curdate
      end;
    else
      needOpenAccount = true;
    end;
  end;

  if(needOpenAccount)
    var LCaccount : string = LetterOfCredit_FD(LCDocObj).FindAndOpenAccount(CATEG_CODE_LC);
    if(LCaccount)
      LCDocObj.LCaccount = LCaccount;
    else
      msgbox("Не определен счет обязательств");
      return 1;
    end;
  end;

  return 0;
END;
