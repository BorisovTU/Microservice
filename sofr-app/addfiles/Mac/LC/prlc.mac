/*
 $Name:        prlc.mac
 $Module:      Торговое финансирование
 $Description: Печать документарного аккредитива
*/
// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//  Описание    : Печать документарного аккредитива
//
//  Программист : Чукина Т.А.
//
//  Создан      : 22.07.2013
//
// --------------------------------------------------------------------

import "Or_Rep_h.mac";
import "oralib.mac", "likepy.mac", CTInter, LCInter, "get_mfo.mac";

var pr_lcdoc:TRecHandler = TRecHandler( "lcdoc.dbt", "bank.def" );
var pr_lcreimb:TRecHandler = TRecHandler( "lcreimb.dbt", "bank.def" );

// Приложения к аккредитиву (BLOB)
var pr_lc_descgoods : string;
var pr_lc_addcond : string;
var pr_lc_docreq : string;

private class LCDocReport()

  var DepName : string = "", OrdLatName : string = "", OrdName : string = "",
      BenName : string = "", IsLcAvz : string = "", IsLcConfirm : string = "",
      AvzName : string = "", AvzCode : string = "", ExeName = "", ExeCode = "",
      CnfName = "", CnfCode = "";

  private var rep = CMakeReport( "", SEP_DEFAULT, 99999 );

  private macro init()
    
    CB_GetDepartmentCodeAndName(pr_lcdoc.rec.Department, null, DepName);

    var LcObj : RsbLetterOfCredit = RsbLetterOfCredit(pr_lcdoc.rec.LcdocID),
        Lcparty : TRecHandler = TRecHandler("lcparty"), 
        IsFound : bool = false;

    IsFound = (LcObj.Parties.First(Lcparty) == 0);
    while(IsFound)
      if(Lcparty.rec.Kind == LCPARTY_KIND_APPLICANT)
        OrdLatName = Lcparty.rec.LatName;
        OrdName = Lcparty.rec.Name;
      elif(Lcparty.rec.Kind == LCPARTY_KIND_BENEFICIARY)
        BenName = Lcparty.rec.Name;
      elif(Lcparty.rec.Kind == LCPARTY_KIND_ADVISING_BANK)
        IsLcAvz = "X";
        AvzName = Lcparty.rec.Name;
        AvzCode = Lcparty.rec.Code;
      elif(Lcparty.rec.Kind == LCPARTY_KIND_CONFIRMING_BANK)
        IsLcConfirm = "X";
        CnfName = Lcparty.rec.Name;
        CnfCode = Lcparty.rec.Code;
      elif(Lcparty.rec.Kind == LCPARTY_KIND_EXEC_BANK)
        ExeName = Lcparty.rec.Name;
        ExeCode = Lcparty.rec.Code;
      end;

      IsFound = (LcObj.Parties.Next(Lcparty) == 0);
    end;

  end;

  private macro makeReport(file_name:string)
    RECORD lcdoc("lcdoc");
    Copy(lcdoc, pr_lcdoc);

    rep.RegisterForm
             ( "LC", "LetterOfCredit.dot", file_name,
               "Number",
               "DepName",
               "OrdLatName",
               "OrdName",
               "BenName",
               "CreationDate",
               "ValidDate",
               "DocsPlace",
               "IsLcAvz", 
               "IsLcTransfer", 
               "IsLcReserve", 
               "IsLcRevolve", 
               "IsLcConfirm", 
               "Amount", 
               "AmountStr", 
               "PercentTolerance", 
               "AvzName", 
               "AvzCode", 
               "ExeName", 
               "ExeCode", 
               "CnfName", 
               "CnfCode", 
               "IsByPayment", 
               "IsByDefPayment", 
               "IsByAccept", 
               "IsByNeg", 
               "IsNegAnyBank",
               "IsByMixPayment", 
               "IsPartShip", 
               "IsNotPartShip", 
               "IsOverShip", 
               "IsNotOverShip", 
               "ShippingPlace", 
               "DeparturePort", 
               "EndPlace", 
               "DestinationPort", 
               "ShippingDatePeriod", 
               "DescGoods", 
               "AddCond", 
               "DocReq", 
               "PayerAccount", 
               "DirectBill"
             );


    rep.AddDoc("LC",
               /*"Number"*/ lcdoc.Number,
               /*"DepName"*/ DepName,
               /*"OrdLatName"*/ OrdLatName,
               /*"OrdName"*/ OrdName,
               /*"BenName",*/ BenName,
               /*"CreationDate",*/ string(lcdoc.CreationDate:f),
               /*"ValidDate",*/ string(lcdoc.ValidDate:f),
               /*"DocsPlace",*/ lcdoc.DocsPlace,
               /*"IsLcAvz",*/ IsLcAvz,
               /*"IsLcTransfer",*/ substr(lcdoc.Form, 2, 1),
               /*"IsLcReserve",*/ substr(lcdoc.Form, 3, 1),
               /*"IsLcRevolve",*/ "",
               /*"IsLcConfirm",*/ IsLcConfirm,
               /*"Amount",*/ string( moneyL(lcdoc.Amount) ),
               /*"AmountStr",*/ CurToStrAlt(lcdoc.Amount, null, null, GetISOCode(lcdoc.FIID)),
               /*"PercentTolerance",*/ IfThenElse(lcdoc.MisCalc == LCMISCALC_KIND_DEVIATION, string("+", lcdoc.ErrPlus, "/-", lcdoc.ErrMinus), ""),
               /*"AvzName",*/ AvzName,
               /*"AvzCode",*/ AvzCode,
               /*"ExeName",*/ ExeName,
               /*"ExeCode",*/ ExeCode,
               /*"CnfName",*/ CnfName,
               /*"CnfCode",*/ CnfCode,
               /*"IsByPayment",*/ IfThenElse(lcdoc.PayCond == LCDOC_PAYCOND_KIND_BY_PAYMENT, "X", ""),
               /*"IsByDefPayment",*/ IfThenElse(lcdoc.PayCond == LCDOC_PAYCOND_KIND_BY_DEF_PAYMENT, "X", ""),
               /*"IsByAccept",*/ IfThenElse(lcdoc.PayCond == LCDOC_PAYCOND_KIND_BY_ACCEPTANCE, "X", ""),
               /*"IsByNeg",*/ IfThenElse(lcdoc.PayCond == LCDOC_PAYCOND_KIND_BY_NEGOTIATION, "X", ""),
               /*"IsNegAnyBank",*/ "",
               /*"IsByMixPayment",*/ IfThenElse(lcdoc.PayCond == LCDOC_PAYCOND_KIND_BY_MIXED_PYMT, "X", ""),
               /*"IsPartShip",*/ IfThenElse(lcdoc.PartShipping == "ALLOWED", "X", ""),
               /*"IsNotPartShip",*/ IfThenElse(lcdoc.PartShipping != "ALLOWED", "X", ""),
               /*"IsOverShip",*/ IfThenElse(lcdoc.OverShipping == "ALLOWED", "X", ""),
               /*"IsNotOverShip",*/ IfThenElse(lcdoc.OverShipping != "ALLOWED", "X", ""),
               /*"ShippingPlace",*/ lcdoc.ShippingPlace,
               /*"DeparturePort",*/ lcdoc.DeparturePort,
               /*"EndPlace",*/ lcdoc.EndPlace,
               /*"DestinationPort",*/ lcdoc.DestinationPort,
               /*"ShippingDatePeriod",*/ IfThenElse(lcdoc.ShippingDateCond == "X", string(lcdoc.ShippingDate:f), lcdoc.ShippingPeriod),
               /*"DescGoods",*/ pr_lc_descgoods,
               /*"AddCond",*/ pr_lc_addcond,
               /*"DocReq",*/ pr_lc_docreq,
               /*"PayerAccount",*/ lcdoc.PayerAccount,
               /*"DirectBill",*/ lcdoc.DirectBill
              );

    rep.CreateTemplateData();
  end;

  macro Print():bool
    var FileNameOffic = "";

    init();

    FileNameOffic = "LC" + pr_lcdoc.rec.Number + "."; // для or_exl_h.mac, функция ExistFile (для 3х звенки GetFileInfo) не находит файл без точки в конце имени
                                        
    makeReport(FileNameOffic);

    rep.ShowTemplateRep();

    return true;
  end;

end;

MACRO PrintDocument():bool
  return LCDocReport().Print();
END;
