/*
  $Name:        lc011_20.mac
  $Module:      Документарные аккредитивы
  $Description: Макрос шага
*/

// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//  Назначение  : Макрос шага
//
//  Описание    : Операция - 3411   - "Аккредитив"
//                Блок     - 341011 - "Учет привлеченных средств по МБК"
//                Шаг      - 20     - "Ожидание зачисления привлеченных средств"
//
//  Программист : Чукина Т.А.
//
//  Создан      : 15.07.2013
//
// --------------------------------------------------------------------

IMPORT LCInter, lc_common;

var LCDocObj:RsbLetterOfCredit;

private class (TLcpayPaymChecker) TPaymChecker(p_Pays : RsbLCPay)
  InitTLcpayPaymChecker(p_Pays);

  private var 
    OffBalanceAccounting : integer = LC_GetOffBalanceAccounting(),
    CreateDoc : bool = InList(OffBalanceAccounting, 1, 3),
    CreateGuaranty : bool = InList(OffBalanceAccounting, 2, 3);

  private macro NeedCheckPayment(RecLcpay : TRecHandler) : bool
    return (RecLcpay.rec.State == LCPAY_COMPENSATION_STATUS_TOTRANSFER); // К перечислению
  end;

  private macro GetPaymentID(RecLcpay : TRecHandler) : integer
    return RecLcpay.rec.PaymentID;
  end;

  private macro GetPaymPurpose() : string
    return "по перечислению возмещения";
  end;

  private macro GetContinueCond() : string
    return "только после того, как возмещение будет перечислено";
  end;

  private macro ProcessFinishedPayment(RecLcpay : TRecHandler) : integer
    RecLcpay.rec.State = LCPAY_COMPENSATION_STATUS_TRANSFERRED; // возмещение перечислено
    if( LCDocObj.Pays().Update(RecLcpay) != 0 )
      msgbox("Ошибка при изменении статуса возмещения выплаты");
      return 1;
    end;

    var stat : integer = 0;
    if( CreateDoc )
      stat = CreateMultiDoc_NonBalance_ForLcpay(LCDocObj, RecLcpay);
      if(stat)
        return stat;
      end;
    end;

    if( CreateGuaranty )
      stat = CreateMultiDoc_Guaranty_ForLcpay(LCDocObj, RecLcpay);
      if(stat)
        return stat;
      end;
    end;

    return 0;
  end;
end;

MACRO ExecuteStep()

  // Проверить состояние платежей
  var PaymChecker : TPaymChecker = TPaymChecker( LCDocObj.Pays() );
  if( PaymChecker.Check() != 0 )
    return 1;
  end;

  return 0;

OnError(er);
  MsgBox( RsbGetError(er) );
  return 1;
END;
