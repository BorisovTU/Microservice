/*
  $Name:        lc003_30.mac
  $Module:      Документарные аккредитивы
  $Description: Макрос шага
*/

// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//  Назначение  : Макрос шага
//
//  Описание    : Операция - 3411   - "Аккредитив"
//                Блок     - 341003 - "Выпуск"
//                Шаг      - 30     - "Выгрузка аккредитива в МБР"
//
//  Программист : Чукина Т.А.
//
//  Создан      : 17.05.2013
//
// --------------------------------------------------------------------

IMPORT LCInter, InsCarryDoc, BankInter;

var LCDocObj:RsbLetterOfCredit;

private const Field45 = 0,
              Field46 = 1,
              Field47 = 2,
              CountArrangedFields = 3; // количество полей, которые требуется распределить по сообщениям

// Подсчитывается ориентировочная длина первого (основного) сообщения аккредитива без полей 45A, 46A и 47A
private macro GetApproxLengthOfFirstMes : integer
  var Len : integer = strlen(LCDocObj.AddAmountCond ) +
                      strlen(LCDocObj.DirectBill    ) +
                      strlen(LCDocObj.MixPayDescr   ) +
                      strlen(LCDocObj.DeferPayDescr ) +
                      strlen(LCDocObj.ShippingPeriod);

  var RecLccost : TRecHandler = TRecHandler("lccost");
  if( LCDocObj.Costs.Find(LCCOST_KIND_OTHER, RecLccost) == 0 )
    Len = Len + strlen(RecLccost.rec.Info);
  end;

  var RecLcparty : TRecHandler = TRecHandler("lcparty");
  if( LCDocObj.Parties.Find(LCPARTY_KIND_EXEC_BANK, RecLcparty) == 0 )
    Len = Len + strlen(RecLcparty.rec.PartyInfo);
  end;

  if( LCDocObj.Parties.Find(LCPARTY_KIND_CONFIRMING_BANK, RecLcparty) == 0 )
    Len = Len + strlen(RecLcparty.rec.PartyInfo);
  end;

  Len = Len + 2200;

  return Len;
end;

// Распределить поля по сообщениям
private macro ArrangeFieldsInMessages(Flags : TArray) : integer
  var FlagItem : bool = false, 
      MesLen   : integer = 0,
      MaxLen   : integer = 0,
      CountMes : integer = 0;

  var needArrange : TArray = TArray(); 
  needArrange[Field45] = (LCDocObj.DescGoods != "");
  needArrange[Field46] = (LCDocObj.DocReq != "");
  needArrange[Field47] = (LCDocObj.AddCond != "");

  var IsArranged : bool = true; // все поля разместили?
  for(FlagItem, needArrange)
    if(FlagItem)
      IsArranged = false;
      break;
    end;
  end;

  macro AddNextMesItems
    Flags[Flags.size] = TArray();
    Flags[Flags.size - 1].size = CountArrangedFields;
    for(FlagItem, Flags[Flags.size - 1])
      FlagItem = false;
    end;
  end;

  macro ArrangeField(flagnum : integer, fieldvalue : string) : bool
    if(needArrange[flagnum])
      MesLen = MesLen + strlen(fieldvalue);
      if(MesLen <= MaxLen)
        Flags[CountMes - 1][flagnum] = true;
        needArrange[flagnum] = false;
      else
        return false;
      end;
    end;

    return true;
  end;

  // По крайней мере одно сообщение формируем всегда
  CountMes = 1; 
  AddNextMesItems();

  while(not IsArranged)
    if(CountMes == 1)
      MaxLen = 10000;
      MesLen = GetApproxLengthOfFirstMes();
    else
      AddNextMesItems();
      MaxLen = 9500;
      MesLen = 0;
    end;

    if(not ArrangeField(Field45, LCDocObj.DescGoods))
      CountMes = CountMes + 1;
      continue;
    end;

    if(not ArrangeField(Field46, LCDocObj.DocReq))
      CountMes = CountMes + 1;
      continue;
    end;

    if(not ArrangeField(Field47, LCDocObj.AddCond))
      CountMes = CountMes + 1;
      continue;
    end;

    // Раз нигде не сработало continue, значит, все разместили
    IsArranged = true;
  end;

  return CountMes;
end;

private macro BoolToChar(Flag : bool) : string
  if(Flag)
    return "X";
  else
    return "";
  end;
end;

private macro GenMessages(Flags : TArray) : integer
  var stat = 0;

  var Cnf : TRecHandler = TRecHandler("lcparty");
  LCDocObj.Parties.Find(LCPARTY_KIND_CONFIRMING_BANK, Cnf);

  // Первое сообщение формируется по выбранной основной форме аккредитива
  var RlsFormID = LCDocObj.GenRlsFormID;

  for(var i, 0, LCDocObj.CountMes - 1)
    LCDocObj.Flag45 = BoolToChar(Flags[i][Field45]);
    LCDocObj.Flag46 = BoolToChar(Flags[i][Field46]);
    LCDocObj.Flag47 = BoolToChar(Flags[i][Field47]);
    LCDocObj.NumMes = i + 1;

    stat = InsertLCMessage(LCDocObj.LcdocID, RlsFormID, LCDocObj.TpSchemID, Cnf);
    if(stat)
      DisplayError();
      break;
    end;

    // Второе сообщение и все последующие сообщения 
    // формируются по выбранной дополнительной форме аккредитива
    RlsFormID = LCDocObj.AddRlsFormID;
  end;

  return stat;
end;

MACRO ExecuteStep()
  var stat : integer = 0;
  var ShowRlsPanel : bool = true;

  if(LCDocObj.GenRlsFormID <= 0)
    stat = DefineRlsFormLCDOC_Basic(LCDocObj, 0, LCDocObj.TpSchemID, 0, LCDocObj.GenRlsFormID, ShowRlsPanel);
    if(stat)
      msgbox("Не выбрана форма сообщения для аккредитива");
      return stat;
    end;
  end;

  var Flags : TArray = TArray();
  LCDocObj.CountMes = ArrangeFieldsInMessages(Flags);
    
  if( (LCDocObj.CountMes > 1) and (LCDocObj.AddRlsFormID <= 0) )
    stat = DefineRlsFormLCDOC_Add(LCDocObj, 0, LCDocObj.TpSchemID, 0, LCDocObj.AddRlsFormID, ShowRlsPanel);
    if(stat)
      msgbox("Не выбрана дополнительная форма сообщения для аккредитива");
      return stat;
    end;
  end;

  stat = GenMessages(Flags);

  return stat;
END;
