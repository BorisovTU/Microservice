/*
  $Name:        lc007_10.mac
  $Module:      Документарные аккредитивы
  $Description: Макрос шага
*/

// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//  Назначение  : Макрос шага
//
//  Описание    : Операция - 3411   - "Аккредитив"
//                Блок     - 341007 - "Перечисление покрытия со счета клиента"
//                Шаг      - 10     - "Перечисление покрытия со счета клиента"
//
//  Программист : Чукина Т.А.
//
//  Создан      : 24.05.2013
//
// --------------------------------------------------------------------

IMPORT LCInter, lc_common;

var LCDocObj:RsbLetterOfCredit;

// Сформировать платежи покрытия на шаге
// LCDocObj - аккредитив, Ord - приказодатель по аккредитиву
private macro CreateCoverPayments(LCDocObj : RsbLetterOfCredit, Ord : TRecHandler) : integer
  var RecLcpay : TRecHandler = TRecHandler("lcpay");
  var BankOrder : RsbBankOrder;
  var BankID : integer = 0, BODetail : string = "";
  var BankOrderID : integer = 0;

  var IsFoundPay : bool = ( LCDocObj.Pays().First(RecLcpay) == 0 );
  if(IsFoundPay)
    BankID = GetDprtPartyID(LCDocObj.Department);
    BODetail = GetBODetailRegVal();
  end;

  while(IsFoundPay)
    if( (RecLcpay.rec.CoverState == LCPAY_COVER_STATUS_TOTRANSFER) // к перечислению
        and (RecLcpay.rec.ContrID == 0) 
      )
      var stat : integer = 0;
      // Сформировать платеж вида "банковский ордер"
      BankOrder = RsbBankOrder();
      BankOrder.Number     = RecLcpay.rec.Trn;
      BankOrder.Date       = BankOrder.ValueDate = {curdate};
      BankOrder.BaseFIID   = LCDocObj.FIID;
      BankOrder.BaseAmount = RecLcpay.rec.ClaimAmount;
      BankOrder.Ground     = BODetail + " " + LCDocObj.Number;

      if(not stat)
        stat = BankOrder.SetPayerPI
                ( 
                  PAYMENTS_GROUP_UNDEF,
                  BankID,
                  0,
                  "",
                  "",
                  "",
                  LCDocObj.FIID,
                  1/*CHAPT1*/,
                  LCDocObj.PayerAccount,
                  Ord.rec.PartyID,
                  Ord.rec.Name,
                  Ord.rec.PartyINN,
                  Ord.rec.CodeKind,
                  Ord.rec.Code
                );
      end;

      if(not stat)
        stat = BankOrder.SetReceiverPI
                ( 
                  PAYMENTS_GROUP_UNDEF,
                  BankID,
                  0,
                  "",
                  "",
                  "",
                  LCDocObj.FIID,
                  1/*CHAPT1*/,
                  LCDocObj.LCAccount,
                  BankID
                );
      end;

      if(not stat)
        stat = BankOrder.GetPermanentID(BankOrderID);
      end;

      if(stat)
        msgbox("Ошибка при формировании платежа покрытия");
        return stat;
      end;

      // Установить lcpay.CoverPaymentID = pmpaym.paymentID
      RecLcpay.rec.CoverPaymentID = BankOrderID;

      // Изменить в записи статус покрытия lcpay.CoverState = $(Покрытие перечислено)
      RecLcpay.rec.CoverState = LCPAY_COVER_STATUS_TRANSFERRED;

      if( LCDocObj.Pays().Update(RecLcpay) != 0 )
        msgbox("Ошибка при сохранении изменений в выплате");
        return 1;
      end;

      // Стартовать по созданному ордеру операцию его обработки
      BankOrder.LaunchOper = true;
      BankOrder.PrimDocOrigin = PD_OR_AUTO;
    end;

    IsFoundPay = ( LCDocObj.Pays().Next(RecLcpay) == 0 );
  end;

  return 0;
end;

MACRO ExecuteStep()

  var Ord : TRecHandler = TRecHandler("lcparty"); // приказодатель
  if( LCDocObj.Parties().Find(LCPARTY_KIND_APPLICANT, Ord) != 0 )
    msgbox("Не указан приказодатель по аккредитиву");
    return 1;
  end;

  if(not LCDocObj.PayerAccount)
    if(not Ord.rec.Account)
      msgbox("Не указан счет списания покрытия для аккредитива");
      return 1;
    end;

    if(GetAccDprt(Ord.rec.Account, LCDocObj.FIID, 1/*CHAPT1*/) != LCDocObj.Department)
      msgbox("Не указан счет списания покрытия для аккредитива");
      return 1;
    end;

    LCDocObj.PayerAccount = Ord.rec.Account;
  else
    if(GetAccDprt(LCDocObj.PayerAccount, LCDocObj.PayerFIID, 1/*CHAPT1*/) != LCDocObj.Department)
      msgbox("Не указан счет списания покрытия для аккредитива");
      return 1;
    end;
  end;

  return CreateCoverPayments(LCDocObj, Ord);
END;

