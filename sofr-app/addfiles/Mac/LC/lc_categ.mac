/*
  $Name:        lc_categ.mac
  $Module:      Документарные аккредитивы
  $Description: Класс первичного документа для категорий учета аккредитивов
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Описание     : Аккредитивы. Класс первичного документа для категорий учета
//
// Программист  : Чукина Т.А.
//
// Создан       : 14.05.2013
//
//-----------------------------------------------------------------------------

import "cat_acc.mac", LCInter, OprInter, "lclib.mac", CTInter;

const CATEG_CODE_LC : string = "Аккредитивы",
      CATEG_CODE_LC_NONBALANCE : string = "ВнебалАккредитив",
      CATEG_CODE_LC_GUARANTY : string = "ГарантАккредитивы";

//-----------------------------------------------------------------------------
// Первичный документ для счета КУ "Счета учета аккредитивов"
//-----------------------------------------------------------------------------
CLASS (TCommonFirstDoc) LetterOfCredit_FD( LCDoc )

  var Doc: RsbLetterOfCredit;

  var IsOrdNotResident  : bool = false, // приказодатель является нерезидентом?
      IsBOrdNotResident : bool = false, // банк приказодателя является нерезидентом?
      IsBenNotResident  : bool = false, // бенефициар является нерезидентом?
      IsBBenNotResident : bool = false, // банк бенефициара является нерезидентом?
      IsReiNotResident  : bool = false, // рамбурсирующий банк является нерезидентом?
      IsFinNotResident  : bool = false, // финансирующий банк является нерезидентом?
      IsCnfNotResident  : bool = false; // подтверждающий банк является нерезидентом?

  MACRO InitParmArray
    var Contragent = -1; // ALLPARTY

    var RecLcparty: TRecHandler = TRecHandler("lcparty");
    if( Doc.Parties().Find(LCPARTY_KIND_APPLICANT, RecLcparty) == 0 )
      IsOrdNotResident  = IsPtNotResident(RecLcparty.rec.PartyID);
    end;
    if( Doc.Parties().Find(LCPARTY_KIND_APPLICANT_BANK, RecLcparty) == 0 )
      IsBOrdNotResident = IsPtNotResident(RecLcparty.rec.PartyID);
    end;
    if( Doc.Parties().Find(LCPARTY_KIND_BENEFICIARY, RecLcparty) == 0 )
      Contragent = RecLcparty.rec.PartyID;
      IsBenNotResident  = IsPtNotResident(RecLcparty.rec.PartyID);
    end;
    if( Doc.Parties().Find(LCPARTY_KIND_BENEFICIARY_BANK, RecLcparty) == 0 )
      IsBBenNotResident  = IsPtNotResident(RecLcparty.rec.PartyID);
    end;
    if( Doc.Parties().Find(LCPARTY_KIND_REIMBURSING_BANK, RecLcparty) == 0 )
      IsReiNotResident  = IsPtNotResident(RecLcparty.rec.PartyID);
    end;
    if( Doc.Parties().Find(LCPARTY_KIND_FIN_BANK, RecLcparty) == 0 )
      IsFinNotResident  = IsPtNotResident(RecLcparty.rec.PartyID);
    end;
    if( Doc.Parties().Find(LCPARTY_KIND_CONFIRMING_BANK, RecLcparty) == 0 )
      IsCnfNotResident  = IsPtNotResident(RecLcparty.rec.PartyID);
    end;

    ParmA[regList.registration(MC_TYPE_PARAMETR_FIID  )     ] = Doc.FIID;
    ParmA[regList.registration(MC_TYPE_PARAMETR_CONTRACTOR) ] = Contragent;
    ParmA[regList.registration(MC_TYPE_PARAMETR_DOCID     ) ] = Id   = Doc.LcdocID;
    ParmA[regList.registration(MC_TYPE_PARAMETR_DOCKIND   ) ] = Kind = DLDOC_LCDOC;

    ParmA[regList.registration(MC_TYPE_PARAMETR_DEPARTMENT) ] = Doc.Department;
    //???ParmA[regList.registration(MC_TYPE_PARAMETR_PAYCURRENCY)] = Doc.FIID;

    FIRoleBArray[0] = 0;
  END;

  // вернуть параметр первого рода
  MACRO GetParametrTemplate ( ObjectID, Classificator, OperDate, FIRole )
    if   ( (ObjectID == OBJTYPE_LC_NU) and (Classificator == 1800) )
      return 2; // Требования
    elif ( (ObjectID == OBJTYPE_LC_REZ) and (Classificator == 1801) )
      if(IsOrdNotResident or IsBenNotResident or IsBOrdNotResident or IsBBenNotResident)
        return 1; // С участием нерезидентов
      else
        return 2; // Только резиденты
      end;
    /*elif ( (ObjectID == OBJTYPE_LC_RR) and (Classificator == 1802) )
      // До реализации аккредитивов по 2-П
      return 1; // Международный
      /*
      // После реализации аккредитивов по 2-П
      if( IsBOrdNotResident or IsReiNotResident or IsFinNotResident or 
          IsCnfNotResident or IsBBenNotResident )
        return 1; // Международный
      else
        return 2; // Внутри страны
      end;*/
      */
    end;

    return -1;
  END;

  //------------------------------------------------------------------------------
  // найти/открыть счет
  //------------------------------------------------------------------------------
  MACRO FindAndOpenAccount( CategCode : string, Open_Date : date )
    var opendate:Date = {curDate};
      
    if ( ValType(Open_Date) == V_DATE )
      opendate = Open_Date;
    end;

    return MC_FindAndOpenAccount( CategCode, this, opendate, IsOprMultiExec(), MC_OPENACC_CREATE, null, Doc.FIID, null, null, null, null );
  END;

  // Конструктор
  if   ( ValType(LCDoc) == V_INTEGER )
    Doc = RsbLetterOfCredit(LCDoc);
  elif ( (ValType(LCDoc) == V_GENOBJ) and IsEqClass("RsbLetterOfCredit", LCDoc) )
    Doc = LCDoc;
  else
    RunError("Неверный тип параметра в классе первичного документа аккредитива");
  end;

  InitTCommonFirstDoc();
  InitParmArray();
  
END;
