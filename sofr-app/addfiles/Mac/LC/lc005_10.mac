/*
  $Name:        lc005_10.mac
  $Module:      Документарные аккредитивы
  $Description: Макрос шага
*/

// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//  Назначение  : Макрос шага
//
//  Описание    : Операция - 3411   - "Аккредитив"
//                Блок     - 341005 - "Обработка выплаты"
//                Шаг      - 10     - "Обработка выплаты"
//
//  Программист : Чукина Т.А.
//
//  Создан      : 23.05.2013
//
// --------------------------------------------------------------------

IMPORT LCInter, InsCarryDoc, BankInter, CTInter, likepy;

var LCDocObj:RsbLetterOfCredit;

private macro IsOnRegFlag(RegName : string) : bool
  var RegFlag : bool = false;
  GetRegistryValue( "LC\\" + RegName, V_BOOL, RegFlag);
  return RegFlag;
end;

MACRO ExecuteStep()
  var IsCoverNotProc : bool = false, // есть хотя бы одна запись со статусом lcpay.CoverState = $(не обработана)
      IsCompensationNotProc : bool = false; // есть хотя бы одна запись со статусом возмещения lcpay.State = $(не обработана)

  var RecLcpay : TRecHandler = TRecHandler("lcpay");
  var IsFoundPay : bool = ( LCDocObj.Pays().First(RecLcpay) == 0 );
  while(IsFoundPay)
    if(RecLcpay.rec.CoverState == LCPAY_COVER_STATUS_NOTPROC)
      IsCoverNotProc = true;

      if( InList( LCDocObj.Cover, 
                  LCDOC_COVER_KIND_APPLICANT, // Со счета приказодателя
                  LCDOC_COVER_KIND_BANK       // Со счета банка
                )
        )
        RecLcpay.rec.CoverState = LCPAY_COVER_STATUS_TOTRANSFER;

        if( LCDocObj.Pays().Update(RecLcpay) != 0 )
          msgbox("Ошибка при установке статуса покрытия в выплате");
          return 1;
        end;
      end;

      // Если у выплаты заполнено примечание "Причина ошибки", очистить его
      var sLcpayID : string = UniID(RecLcpay, OBJTYPE_LCPAY);
      if( ReadNoteForObject(OBJTYPE_LCPAY, sLcpayID, LCPAY_NOTEKIND_ERROR_REASON) )
        if( addNoteForObject(OBJTYPE_LCPAY, sLcpayID, LCPAY_NOTEKIND_ERROR_REASON, "") != 0 )
          msgbox("Ошибка при очистке примечания");
          return 1;
        end;
      end;
    end;

    if(RecLcpay.rec.State == LCPAY_COMPENSATION_STATUS_NOTPROC)
      IsCompensationNotProc = true;
    end;

    IsFoundPay = ( LCDocObj.Pays().Next(RecLcpay) == 0 );
  end;

  var StatusValue : integer = 0;

  if(IsCoverNotProc)
    // сегмент статуса $(Покрытие)
    if(LCDocObj.Cover == LCDOC_COVER_KIND_APPLICANT_CREDIT) // за счет кредита приказодателю
      StatusValue = LCDOC_ST_PO_NOTPROC; // Не обработано
    else
      StatusValue = LCDOC_ST_PO_TO_TRANSFER; // К перечислению
    end;
    if( not InsertOprStatus(LCDOC_ST_KIND_PO, StatusValue) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;

    // сегмент статуса $(Оплата клиентом)
    if(LCDocObj.Cover == LCDOC_COVER_KIND_APPLICANT_CREDIT) // за счет кредита приказодателю
      if( IsOnRegFlag("CREDITRSLOANS") )
        StatusValue = LCDOC_ST_OK_LOAN_RSLOANS; // Кредит RS-Loans
      else
        StatusValue = LCDOC_ST_OK_LOAN; // Кредит
      end;
    else
      StatusValue = LCDOC_ST_OK_ACCOUNT; // Со счета
    end;
    if( not InsertOprStatus(LCDOC_ST_KIND_OK, StatusValue) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
  end;

  if(IsCompensationNotProc)
    // сегмент статуса $(Возмещение) = $(Не обработано)
    if( not InsertOprStatus(LCDOC_ST_KIND_VO, LCDOC_ST_VO_NOTPROC) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;

    // сегмент статуса $(Оплата банком)
    if(LCDocObj.Compensation == LCDOC_COMPENSATION_KIND_FIN_BANK) // финансирующим банком
      if( IsOnRegFlag("DEALRSDEALING") )
        StatusValue = LCDOC_ST_OB_DEALMBK_RSDEALING; // Сделка МБК RS-Dealing
      else
        StatusValue = LCDOC_ST_OB_DEALMBK; // Сделка МБК
      end;
    else
      StatusValue = LCDOC_ST_OB_TRANSFER; // Перечисление
    end;
    if( not InsertOprStatus(LCDOC_ST_KIND_OB, StatusValue) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;

    // сегмент статуса $(Документооборот) - обработка
    StatusValue = LCDOC_ST_DO_PROCESS;
    if( not InsertOprStatus(LCDOC_ST_KIND_DO, StatusValue) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
  end;

  return 0;
END;
