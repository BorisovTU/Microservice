/*
  $Name:        lc003_20.mac
  $Module:      Документарные аккредитивы
  $Description: Макрос шага
*/

// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//  Назначение  : Макрос шага
//
//  Описание    : Операция - 3411   - "Аккредитив"
//                Блок     - 341003 - "Выпуск"
//                Шаг      - 20     - "Выпуск аккредитива"
//
//  Программист : Чукина Т.А.
//
//  Создан      : 15.05.2013
//
// --------------------------------------------------------------------

IMPORT LCInter, PTInter, BankInter, globals, "rsberror.mac", "lc_categ.mac", 
       "lc_common.mac";

var LCDocObj:RsbLetterOfCredit;

// класс для проверки участников аккредитива
private class TLcpartyChecker(p_Kind : integer)
  private var Kind : integer = p_Kind;

  // для переопределения в наследниках
  private macro CheckRec(Lcparty : TRecHandler) : bool
    return true;
  end;

  // для переопределения в наследниках
  private macro GetErrStr() : string
    return "";
  end;

  macro Check(errmsg : @string) : bool
    var Lcparty: TRecHandler = TRecHandler("lcparty");

    if( (LCDocObj.Parties().Find(Kind, Lcparty) != 0) or
        not CheckRec(Lcparty) 
      )
      errmsg = GetErrStr();
      return false;
    end;

    return true;
  end;

end;

private class (TLcpartyChecker) OrdChecker
  InitTLcpartyChecker(LCPARTY_KIND_APPLICANT);

  private macro CheckRec(Lcparty : TRecHandler) : bool
    return ( (Lcparty.rec.Name != "") or (Lcparty.rec.LatName != "") );
  end;

  private macro GetErrStr() : string
    return "Не заполнено наименование приказодателя";
  end;
end;

private class (TLcpartyChecker) BOrdChecker
  InitTLcpartyChecker(LCPARTY_KIND_APPLICANT_BANK);

  private macro CheckRec(Lcparty : TRecHandler) : bool
    return ( (Lcparty.rec.Code != "") and (Lcparty.rec.Name != "") );
  end;

  private macro GetErrStr() : string
    return "Не заполнен код или наименование банка приказодателя";
  end;
end;

private class (TLcpartyChecker) BenChecker
  InitTLcpartyChecker(LCPARTY_KIND_BENEFICIARY);

  private macro CheckRec(Lcparty : TRecHandler) : bool
    return (Lcparty.rec.Name != "");
  end;

  private macro GetErrStr() : string
    return "Не заполнено наименование бенефициара";
  end;
end;

private class (TLcpartyChecker) CnfChecker
  InitTLcpartyChecker(LCPARTY_KIND_CONFIRMING_BANK);

  private macro CheckRec(Lcparty : TRecHandler) : bool
    return ( (Lcparty.rec.CodeKind == PTCK_SWIFT) and (Lcparty.rec.Code != "") );
  end;

  private macro GetErrStr() : string
    return "Не заполнены реквизиты подтверждающего банка";
  end;
end;

private macro CheckLCDoc(errmsg : @string) : bool
  if(LCDocObj.ValidDate == date(0, 0, 0))
    errmsg = "Не заполнена дата действия";
  elif(LCDocObj.Amount == $0)
    errmsg = "Не заполнена сумма аккредитива";
  elif(LCDocObj.FIID == -1 /*ALLFININSTR*/)
    errmsg = "Не заполнена валюта аккредитива";
  elif(not LCDocObj.DocReq)
    errmsg = "Не заполнены требуемые документы";
  elif(not LCDocObj.DescGoods)
    errmsg = "Не заполнено описание товаров/услуг";
  else
    return true; // все проверки прошли
  end;

  return false; // сюда попадем, если какую-то проверку не прошли 
end;

// Сформировать мультивалютный мемориальный ордер для отражения 
// суммы аккредитива по внебалансу
private macro CreateMultiDoc_NonBalance : integer

  var Ground : string = "Открытие аккредитива № " + LCDocObj.Number;

  var stat : integer = CreateMultiDocOffBalance( LCDocObj, LCDocObj.Number, Ground, true, 
                                                 LCDocObj.Amount, CATEG_CODE_LC_NONBALANCE );

  if(stat)
    msgbox("Ошибка при формировании платежа для отражения суммы аккредитива по внебалансу");
  end;

  return stat;
end;

// Сформировать мультивалютный мемориальный ордер для отражения 
// суммы аккредитива по счету выданных гарантий
private macro CreateMultiDoc_Guaranty : integer

  var Ground : string = "Предоставленные гарантии по аккредитиву № " + LCDocObj.Number;

  var stat : integer = CreateMultiDocOffBalance( LCDocObj, LCDocObj.Number, Ground, false, 
                                                 LCDocObj.Amount, CATEG_CODE_LC_GUARANTY );

  if(stat)
    msgbox("Ошибка при формировании платежа для отражения суммы аккредитива по счету выданных гарантий");
  end;

  return stat;
end;

MACRO ExecuteCaseStep(Kind_Operation, Number_Step)
  var stat : integer = 0;
  var errmsg : string = "";
  if( not OrdChecker.Check(@errmsg) or not BOrdChecker.Check(@errmsg) or 
      not BenChecker.Check(@errmsg) or not CnfChecker.Check(@errmsg) or 
      not CheckLCDoc(@errmsg) 
    )
    msgbox(errmsg);
    return 1;
  end;

  LCDocObj.OpenDate = {curdate};
  if( (stat = LCDocObj.ChangeState(WLD_STATUS_LC_ISSUE)) != 0 )
    DisplayError();
    return stat;
  end;

  var OffBalanceAccounting : integer = LC_GetOffBalanceAccounting();
  if( InList(OffBalanceAccounting, 1, 3) )
    stat = CreateMultiDoc_NonBalance();
    if(stat)
      return stat;
    end;
  end;

  if( InList(OffBalanceAccounting, 2, 3) )
    stat = CreateMultiDoc_Guaranty();
    if(stat)
      return stat;
    end;
  end;

  if( LCDocObj.ManualTransferCond == "X" )
    return ""; // Завершить выполнение блока
  else
    return "30"; // выполняется следующий шаг 
  end;

ONERROR(er)
  ExeptionMessage(er);
  return 1;
END;
