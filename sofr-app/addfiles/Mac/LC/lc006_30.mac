/*
  $Name:        lc006_30.mac
  $Module:      Документарные аккредитивы
  $Description: Макрос шага
*/

// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//  Назначение  : Макрос шага
//
//  Описание    : Операция - 3411   - "Аккредитив"
//                Блок     - 341006 - "Выдача кредита RS-Loans"
//                Шаг      - 30     - "Ожидание завершения выдачи кредита"
//
//  Программист : Чукина Т.А.
//
//  Создан      : 23.05.2013
//
// --------------------------------------------------------------------

IMPORT LCInter, lc_common, PaymInter, OprInter;

var LCDocObj : RsbLetterOfCredit;

private macro GetLastStepFactDate() : date
  var rs : RsdRecordset = execSQLselectPrm
    ( "select step.t_Fact_Date "
      "  from doproper_dbt opr, doprstep_dbt step "
      " where opr.t_DocKind = :DLDOC_LCDOC "
      "   and opr.t_DocumentID = Lpad(:LcdocID, 34, '0') "
      "   and step.t_ID_Operation = opr.t_ID_Operation "
      "   and step.t_IsExecute = 'X' "
      " order by step.t_ID_Step desc ",
      SQLParam("DLDOC_LCDOC", DLDOC_LCDOC),
      SQLParam("LcdocID", LCDocObj.LcdocID)
    );

  if( rs and rs.moveNext() )
    return rs.value(0);
  end;

  RunError("Ошибка при получении операционной даты выполнения предыдущего шага");
end;

private macro IsPaymentInCurrentLcdocObj(PaymentID : integer) : bool
  var Pays : RsbLCPay = LCDocObj.Pays(),
      RecLcpay : TRecHandler = TRecHandler("lcpay");

  var IsFoundPay : bool = ( Pays.First(RecLcpay) == 0 );
  while(IsFoundPay)
    if(RecLcpay.rec.LoanPaymentID == PaymentID)
      return TRUE;
    end;

    IsFoundPay = ( Pays.Next(RecLcpay) == 0 );
  end;

  return FALSE;
end;

private macro GetLoanPmByLcpay(RecLcpay : TRecHandler, PaymCount : @integer) : integer
  PaymCount = 0;
  var PaymentID : integer = 0;

  var ObligationID : integer = IfThenElse( RecLcpay.rec.ObligationID > 0, 
                                           RecLcpay.rec.ObligationID,
                                           LCDocObj.ObligationID );
  var ContrID : integer = 0;
  if(ObligationID <= 0)
    ContrID = IfThenElse( RecLcpay.rec.ContrID > 0,
                          RecLcpay.rec.ContrID,
                          LCDocObj.ContrID );
  end;

  // В SQL-запросе исключаем из проверки выплаты текущего аккредитива, т.к.
  // с ними работаем через сервис ввода, и там могут быть изменения, еще 
  // не отраженные в БД. Поэтому проверку выплат текущего аккредитива
  // делаем отдельно по сервису ввода (IsPaymentInCurrentLcdocObj)
  var SelectFrom : string = 
    "select pm.t_PaymentID "
    "  from dpmpaym_dbt pm, dpmrmprop_dbt rm ";

  var Where : string = 
    " where pm.t_BaseAmount = :BaseAmount "
    "   and pm.t_Purpose = :Purpose "
    "   and rm.t_PaymentID = pm.t_PaymentID "
    "   and rm.t_Date <= :curdate "
    "   and rm.t_Date >= :PrevStepFactDate "
    "   and not exists ( select 1   " +
    "                      from dlcpay_dbt lcp "
    "                     where lcp.t_LcdocID <> :CurLcdocID "
    "                       and lcp.t_LoanPaymentID = pm.t_PaymentID ) ";

  var LnkObjID : integer = 0;
  if(ObligationID > 0)
    LnkObjID = ObligationID;
    SelectFrom = SelectFrom + ", dcrd_op_dbt crdop, ddoc_acc_dbt doc ";
    Where = Where + 
    " and crdop.t_ObjectTypeID_ref = 6 /* LO_DUTY */ "
    " and crdop.t_ObjectID_ref = :LnkObjID "
    " and doc.t_CredOperID_ref = crdop.t_CredOperID "
    " and pm.t_DocumentID in (doc.t_OutDocID, doc.t_InDocID) ";

  elif(ContrID > 0)
    LnkObjID = ContrID;
    SelectFrom = SelectFrom + ", ddoc_acc_dbt doc ";
    Where = Where +
    " and doc.t_CreditNumber_ref = :LnkObjID "
    " and pm.t_DocumentID in (doc.t_OutDocID, doc.t_InDocID) ";
  end;

  var rs : RsdRecordset = execSQLselectPrm
    ( SelectFrom + Where,
      SQLParam("BaseAmount", GetSumForLoanPaym(RecLcpay)),
      SQLParam("Purpose", PM_PURP_LCCOVER),
      SQLParam("curdate", {curdate}),
      SQLParam("PrevStepFactDate", GetLastStepFactDate()),
      SQLParam("CurLcdocID", RecLcpay.rec.LcdocID),
      SQLParam("LnkObjID", LnkObjID)
    );

  while(rs and rs.moveNext())
    if( not IsPaymentInCurrentLcdocObj(rs.value(0)) )
      PaymCount = PaymCount + 1;
      PaymentID = rs.value(0);
    end;
  end;

  if(PaymCount != 1)
    PaymentID = 0;
  end;

  return PaymentID;
end;

private macro GetLoanPaymentID(RecLcpay : TRecHandler) : integer
  if(RecLcpay.rec.LoanPaymentID > 0)
    return RecLcpay.rec.LoanPaymentID;
  end;

  // Если в выплате не указан платеж по выдаче кредита lcdoc.LoanPaymentID, попытаться найти его

  var PaymentID : integer = 0;
  var PaymCount : integer = 0;

  // искать платеж по значению примечания (примечание платежа = LcpayID)
  var rs : RsdRecordset = execSQLselectPrm
    (
      "select pm.t_PaymentID "
      "  from dnotetext_dbt nt, dpmpaym_dbt pm "
      " where nt.t_ObjectType = :OBJTYPE_PAYMENT "
      "   and nt.t_NoteKind   = :NOTEKIND_PAYM_LCPAYID "
      "   and rsb_kernel.ExtractText(nt.t_text) = :LcpayID "
      "   and pm.t_PaymentID = to_number(nt.t_DocumentID) ",
      SQLParam("OBJTYPE_PAYMENT", OBJTYPE_PAYMENT),
      SQLParam("NOTEKIND_PAYM_LCPAYID", NOTEKIND_PAYM_LCPAYID),
      SQLParam("LcpayID", RecLcpay.rec.LcpayID)
    );
  while(rs and rs.moveNext())
    PaymentID = rs.value(0);
    PaymCount = PaymCount + 1;
  end;

  // Если найдено 0 платежей, искать платеж по параметрам выплаты
  if(PaymCount == 0)
    PaymentID = GetLoanPmByLcpay(RecLcpay, @PaymCount);
  end;

  // Если найден 1 платеж, записать его в Lcpay.LoanPaymentID
  if(PaymCount == 1)
    RecLcpay.rec.LoanPaymentID = PaymentID;

    if( LCDocObj.Pays().Update(RecLcpay) != 0 )
      RunError("Ошибка при записи идентификатора платежа в выплату");
    end;
  else
    PaymentID = 0;
  end;

  return PaymentID;
end;

private class (TLcpayPaymChecker) TPaymChecker(p_Pays : RsbLCPay)
  InitTLcpayPaymChecker(p_Pays);

  private macro NeedCheckPayment(RecLcpay : TRecHandler) : bool
    if(RecLcpay.rec.CoverState == LCPAY_COVER_STATUS_LOAN) // Выдача кредита
      return TRUE;
    end;

    return FALSE;
  end;

  private macro GetPaymentID(RecLcpay : TRecHandler) : integer
    return RecLcpay.rec.LoanPaymentID;
  end;

  private macro GetPaymPurpose() : string
    return "по выдаче кредита";
  end;

  private macro GetContinueCond() : string
    return "только после зачисления выданного кредита на расчетный счет клиента";
  end;

  private macro PreCheck(PaymentID : integer) : integer
    // Проверить привязку платежа к выплате
    if(PaymentID <= 0)
      msgbox("Не определен платеж, на основании которого производится перечисление выданного кредита на счет клиента, по одной или нескольким выплатам. Выполнение шага невозможно");
      return 1;
    end;

    return 0;
  end;

  private macro ProcessFinishedPayment(RecLcpay : TRecHandler) : integer
    RecLcpay.rec.CoverState = LCPAY_COVER_STATUS_TOTRANSFER; // К перечислению
    if( LCDocObj.Pays().Update(RecLcpay) != 0 )
      msgbox("Ошибка при изменении статуса покрытия выплаты");
      return 1;
    end;

    return 0;
  end;
end;

MACRO ExecuteStep()
  var RecLcpay : TRecHandler = TRecHandler("lcpay"),
      PaymentID : integer = 0, 
      NeedPaymLinkManual : bool = false, 
      LcpayIDs : TArray = TArray(); // выплаты, обрабатываемые на текущем шаге операции

  // Привязать платежи к выплатам (где они еще не привязаны)
  var IsFoundPay : bool = ( LCDocObj.Pays().First(RecLcpay) == 0 );
  while(IsFoundPay)

    if(RecLcpay.rec.CoverState == LCPAY_COVER_STATUS_LOAN) // Выдача кредита
      LcpayIDs[ LcpayIDs.size ] = RecLcpay.rec.LcpayID;

      PaymentID = GetLoanPaymentID(RecLcpay);

      if(PaymentID <= 0)
        if(not IsOprMultiExec())
          NeedPaymLinkManual = true;
        else
          msgbox("Не определен платеж, на основании которого производится перечисление выданного кредита на счет клиента, по одной или нескольким выплатам. Запустите шаг в индивидуальном режиме и выполните привязку платежей вручную");
          return 1;
        end;
      end;

    end;

    IsFoundPay = ( LCDocObj.Pays().Next(RecLcpay) == 0 );
  end;

  if(NeedPaymLinkManual)
    LcpayLinkPaymScrol(LCDocObj, LcpayIDs, LCPAY_PAYMENTS_SCR_MODE_WAIT_CREDIT);
  end;

  // Проверить привязку и состояние платежей
  var PaymChecker : TPaymChecker = TPaymChecker( LCDocObj.Pays() );
  if( PaymChecker.Check() != 0 )
    return 1;
  end;

  return 0;

OnError(er);
  MsgBox( RsbGetError(er) );
  return 1;
END;
