/*
  $Name:        lc006_20.mac
  $Module:      Документарные аккредитивы
  $Description: Макрос шага
*/

// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//  Назначение  : Макрос шага
//
//  Описание    : Операция - 3411   - "Аккредитив"
//                Блок     - 341006 - "Выдача кредита RS-Loans"
//                Шаг      - 20     - "Выдача кредита в RS-Loans"
//
//  Программист : Чукина Т.А.
//
//  Создан      : 23.05.2013
//
// --------------------------------------------------------------------

IMPORT LCInter, "bnk_common.mac", CTInter, "lettercredit.mac", InsCarryDoc, OprInter,
       "lc_const.mac", "lc_common.mac";

var LCDocObj:RsbLetterOfCredit;

MACRO ExecuteStep()
  var NameCRLINNO : string = Bnk_GetNameAlg(LIST_TYPECRD, CF_CRLINNO);
  var NeedWarnErr : bool = false;

  var RecLcpay : TRecHandler = TRecHandler("lcpay");

  var IsFoundPay : bool = ( LCDocObj.Pays().First(RecLcpay) == 0 );
  while(IsFoundPay)
    if(RecLcpay.rec.CoverState == LCPAY_COVER_STATUS_NOTPROC) // не обработано
      // Определить кредитный договор
      var ContrID : integer = 0, CreditType : string = "";
      if(RecLcpay.rec.ContrID > 0)
        ContrID = RecLcpay.rec.ContrID;
        CreditType = RecLcpay.rec.CreditType;
      elif(LCDocObj.ContrID > 0)
        ContrID = LCDocObj.ContrID;
        CreditType = LCDocObj.CreditType;
      else
        msgbox("Не задан кредитный договор, за счет которого производится выплата. Укажите кредитный договор в закладке выплаты \"Покрытие\" или условиях финансирования аккредитива");
        return 1;
      end;

      // Определить СО, если CreditType = $(Невозобновляемая КЛ)
      var ObligationID : integer = 0;
      if(CreditType == NameCRLINNO)
        if(RecLcpay.rec.ObligationID > 0)
          ObligationID = RecLcpay.rec.ObligationID;
        elif(LCDocObj.ObligationID > 0)
          ObligationID = LCDocObj.ObligationID;
        else
          msgbox("Для невозобновляемой кредитной линии не задано срочное обязательство, за счет которого производится выплата. Укажите срочное обязательство в закладке выплаты \"Покрытие\" или условиях финансирования аккредитива");
          return 1;
        end;
      end;

      var sLcpayID : string = UniID(RecLcpay, OBJTYPE_LCPAY);
      var Sum : money = GetSumForLoanPaym(RecLcpay);
      var ErrorMsg : string = "";

      var stat : integer = CheckPayCredit
        ( ContrID, 
          Sum,
          RecLcpay.rec.FIID,
          LCDocObj.Department, 
          ObligationID, 
          @ErrorMsg
        );

      if(stat)
        if( addNoteForObject(OBJTYPE_LCPAY, sLcpayID, LCPAY_NOTEKIND_ERROR_REASON, ErrorMsg) != 0 )
          msgbox("Ошибка при добавлении примечания к выплате");
          return 1;
        end;
      end;

      if(not stat)
        if( not PayLoanForLetterOfCredit(ContrID, Sum, ObligationID) )
          if( addNoteForObject(OBJTYPE_LCPAY, sLcpayID, LCPAY_NOTEKIND_ERROR_REASON, "Ошибка при выдаче кредита") != 0 )
            msgbox("Ошибка при добавлении примечания к выплате");
            return 1;
          end;
        else
          RecLcpay.rec.CoverState = LCPAY_COVER_STATUS_LOAN; // Выдача кредита
          if( LCDocObj.Pays().Update(RecLcpay) != 0 )
            msgbox("Ошибка при изменении статуса покрытия выплаты");
            return 1;
          end;
        end;
      end;

      if(RecLcpay.rec.CoverState == LCPAY_COVER_STATUS_NOTPROC) // не обработано
        NeedWarnErr = true;
      end;
    end;

    IsFoundPay = ( LCDocObj.Pays().Next(RecLcpay) == 0 );
  end;

  if(NeedWarnErr)
    CreateWarning(null, LC_ERR_LCPAY_NOTPROC);
    if(not IsOprMultiExec())
      DisplayError();
    end;
  end;

  return 0;
END;
