/*
  $Name:        lc008_10.mac
  $Module:      Документарные аккредитивы
  $Description: Макрос шага
*/

// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//  Назначение  : Макрос шага
//
//  Описание    : Операция - 3411   - "Аккредитив"
//                Блок     - 341008 - "Выдача кредита"
//                Шаг      - 10     - "Перечисление кредита с ссудного счета"
//
//  Программист : Чукина Т.А.
//
//  Создан      : 12.07.2013
//
// --------------------------------------------------------------------

IMPORT LCInter, lc_common;

var LCDocObj:RsbLetterOfCredit;

// Сформировать платеж вида "банковский ордер"
private macro CreateLoanPayment
( RecLcpay : TRecHandler, 
  LoanAccount : string, 
  CreditAccount : string, 
  BankID : integer, 
  BODetail : string,
  LoanPaymentID : @integer
) : integer

  var stat : integer = 0;

  var BankOrder : RsbBankOrder = RsbBankOrder();
  BankOrder.Number     = RecLcpay.rec.Trn;
  BankOrder.Date       = BankOrder.ValueDate = {curdate};
  BankOrder.BaseFIID   = LCDocObj.FIID;
  BankOrder.BaseAmount = GetSumForLoanPaym(RecLcpay);
  BankOrder.Ground     = BODetail + " " + LCDocObj.Number;

  if(not stat)
    stat = BankOrder.SetPayerPI
            ( 
              PAYMENTS_GROUP_UNDEF,
              BankID,
              0,
              "",
              "",
              "",
              LCDocObj.FIID,
              1/*CHAPT1*/,
              LoanAccount
            );
  end;

  if(not stat)
    stat = BankOrder.SetReceiverPI
            ( 
              PAYMENTS_GROUP_UNDEF,
              BankID,
              0,
              "",
              "",
              "",
              LCDocObj.FIID,
              1/*CHAPT1*/,
              CreditAccount,
              BankID
            );
  end;

  if(not stat)
    stat = BankOrder.GetPermanentID(LoanPaymentID);
  end;

  if(stat)
    msgbox("Ошибка при формировании платежа по выдаче кредита");
    return stat;
  end;

  // Стартовать по созданному ордеру операцию его обработки
  BankOrder.LaunchOper = true;
  BankOrder.PrimDocOrigin = PD_OR_AUTO;

  return stat;
end;

private macro GetLoanCredAccounts
( RecLcpay : TRecHandler, 
  LoanAccount : @string, 
  CreditAccount : @string
) : integer

  if(RecLcpay.rec.LoanAccount and RecLcpay.rec.CreditAccount)
    LoanAccount = RecLcpay.rec.LoanAccount;
    CreditAccount = RecLcpay.rec.CreditAccount;
  else
    LoanAccount = LCDocObj.LoanAccount;
    CreditAccount = LCDocObj.CreditAccount;
  end;

  if(not LoanAccount)
    msgbox("Не указан ссудный счет для выдачи кредита. Задайте номер ссудного счета в поле \"ссудный счет\" панели \"Финансирование аккредитива\" или закладке выплаты \"Покрытие\"");
    return 1;
  end;

  if(not CreditAccount)
    msgbox("Не указан расчетный счет для зачисления выданного кредита. Задайте номер расчетного счета в поле \"расчетный счет для выдачи\" панели \"Финансирование аккредитива\" или закладке выплаты \"Покрытие\"");
    return 1;
  end;

  if(GetAccDprt(LoanAccount, LCDocObj.FIID, 1/*CHAPT1*/) != LCDocObj.Department)
    msgbox("Для формирования банковского ордера валюта и филиал ссудного счета должны совпадать с валютой и филиалом аккредитива");
    return 1;
  end;

  if(GetAccDprt(CreditAccount, LCDocObj.FIID, 1/*CHAPT1*/) != LCDocObj.Department)
    msgbox("Для формирования банковского ордера валюта и филиал расчетного счета должны совпадать с валютой и филиалом аккредитива");
    return 1;
  end;

  return 0;
end;

MACRO ExecuteStep()
  var RecLcpay : TRecHandler = TRecHandler("lcpay");
  var BankID : integer = 0, BODetail : string = "";
  var LoanPaymentID : integer = 0;

  var IsFoundPay : bool = ( LCDocObj.Pays().First(RecLcpay) == 0 );
  if(IsFoundPay)
    BankID = GetDprtPartyID(LCDocObj.Department);
    BODetail = GetBODetailRegVal();
  end;

  while(IsFoundPay)
    if(RecLcpay.rec.CoverState == LCPAY_COVER_STATUS_NOTPROC) // не обработано
      var LoanAccount : string = "", CreditAccount : string = "";
      if( GetLoanCredAccounts(RecLcpay, @LoanAccount, @CreditAccount) != 0 )
        return 1;
      end;

      // Сформировать платеж вида "банковский ордер"
      if( CreateLoanPayment(RecLcpay, LoanAccount, CreditAccount, BankID, BODetail, @LoanPaymentID) != 0 )
        return 1;
      end;

      // Установить lcpay.LoanPaymentID = pmpaym.paymentID
      RecLcpay.rec.LoanPaymentID = LoanPaymentID;

      // Изменить в записи статус покрытия lcpay.CoverState = $(Выдача кредита)
      RecLcpay.rec.CoverState = LCPAY_COVER_STATUS_LOAN;

      if( LCDocObj.Pays().Update(RecLcpay) != 0 )
        msgbox("Ошибка при сохранении изменений в выплате");
        return 1;
      end;
    end;

    IsFoundPay = ( LCDocObj.Pays().Next(RecLcpay) == 0 );
  end;

  return 0;
END;
