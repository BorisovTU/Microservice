Import DealsInter, globals, RSD;

var str, rs, cmd, CalcDate = date(31,12,2018), i = 0;

macro Create_ROVU(fiid, amount, client, clientcontr)

  var v_stat = 0;
  var m_opertype       = 6, //Прием от клиента денежных средств
      m_fikind         = 1, //Вид ФИ валюта
      m_fiid           = fiid, //фининструмент рубли
      m_old_place_kind = 1,
      m_old_place      = 1, 
      m_new_place_kind = 2, // вид субъекта
      m_new_place      = 1, //наш банк
      m_amount         = money(amount), //сумма
      m_date           = CalcDate, //дата
      m_run            = 0, //признак запуска
      m_client         = client, //client
      m_clientcontr    = clientcontr;


  v_stat = CreateDL_ACC(m_opertype,       //OPER_TYPE
                        m_fikind,         //FIKIND
                        m_fiid,           //FIID
                        m_old_place_kind, //OLD_PLACE_KIND
                        m_old_place ,     //OLD_PLACE
                        m_new_place_kind, //NEW_PLACE_KIND
                        m_new_place,      //NEW_PLACE
                        m_amount,         //SUM
                        m_date,           //DATE
                        m_date,           //VALUE_DATE
                        m_run,            //RUN
                        -1,               //SPGROUND_ID
                        -1,               //SPGROUND_KIND,
                        "",               //SPGOUND_CODE
                        m_date,           //SPGROUND_DATE
                 		 	  m_client,         //CLIENT
	                      m_clientcontr     //CLIENTCONTR
                       ); 

  if (v_stat != 0)
    [+------------------+------------------+------------------+---------+-------------+];
    [|##################|##################|##################|#########|#############|]
    (client, clientcontr, amount, fiid, string("Ошибка"));
  else 
    [+------------------+------------------+------------------+---------+-------------+];
    [|##################|##################|##################|#########|#############|]
    (client, clientcontr, amount, fiid, string("Удачно"));
  end;

end;

macro Correct_RoVU
  var str, rs, cmd;
  str = "Update DDL_ACC_DBT SET t_oldplace = 0, t_oldplacekind = 0, t_department = 1, t_oper = 1 WHERE t_date = ?";
  cmd = RsdCommand(str); 
  cmd.addParam("", RSDBP_IN, CalcDate);  
  cmd.Execute();
end;

str = " SELECT t.*                                                                                             "
    + "   FROM (SELECT ac.t_accountid,                                                                         "
    + "                ac.t_code_currency,                                                                     "
    + "                ac.t_account,                                                                           "
    + "                CASE ac.t_code_currency                                                                 "
    + "                  WHEN 0 THEN RSB_ACCOUNT.RESTA (ac.t_account, ?, ac.t_chapter, 0)                      "
    + "                         ELSE RSB_ACCOUNT.RESTAC (ac.t_account, ac.t_code_currency, ?, ac.t_chapter, 0) "
    + "                END AS t_rest,                                                                          " 
    + "                ac.t_client,  pt.t_name, mc.t_catnum, mc.t_id, mc.t_clientcontrid                       "
    + "           FROM daccount_dbt ac                                                                         "
    + "           JOIN dparty_dbt pt ON (AC.T_CLIENT = pt.t_partyid)                                           "
    + "           LEFT JOIN dmcaccdoc_dbt mc ON (mc.t_account = ac.t_account                                   "
    + "                                      AND mc.t_iscommon = CHR (88)                                      "
    + "                                      AND mc.t_owner = ac.t_client                                      "
    + "                                      AND mc.t_currency = ac.t_code_currency)                           "
    + "          WHERE ac.t_account LIKE '30601%' OR ac.t_account LIKE '30606%') t                             "
    + "  WHERE t.t_rest != 0      ";

cmd = RSdCommand(str);
cmd.addParam("1", RSDBP_IN, CalcDate);
cmd.addParam("2", RSDBP_IN, CalcDate);

rs = RSdRecordSet(cmd);

[|    ID Клиента    |    ID Договора   |      Сумма       |  Валюта |];

while(rs and rs.MoveNext())
  i = i + 1;
  Create_ROVU(rs.value("t_code_currency"), rs.value("t_rest"), rs.value("t_client"), rs.value("t_clientcontrid"));
  message(string("Обрабатываю РОВУ номер "), i);
end; 

Correct_RoVU
