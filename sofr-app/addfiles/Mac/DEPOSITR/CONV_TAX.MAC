/************************************************************
*  ---------------------                                    *
*  R-Style SoftWare Lab.                                    *
*  ---------------------                                    *
*  RS-Retail                                                *
*                                                           *
*  Определение суммы конвертации и налога на конвертацию    *
*  при проведении операций по валютным вкладам и            *
*  валютным налично-кассовым операциям,                     *
*  а также валютно-обменным операциям                       *
*                                                           *
*  Лебедев С.В.                                             *
*  22.06.99                                                 *
************************************************************/

import DeprIntr, DeskInter, IsSrvDoc, ExchangeInter, "define.mac", "office.mac", "cur_rate.mac";

/* Первичные документы */
record pay_doc     (pay_doc);
record CNVsbdepdoc (sbdepdoc);
record exoperat    ("exoperat.dbt","exchange.def");

/* Дополнительные документы, формируемые при частичной конвертации */
record depdoc_cnv  (sbdepdoc);
record depdoc_cnv2 (sbdepdoc);
record taxdoc      ("pay_doc.com" );
record convdoc     ("pay_doc.cnv");
record pay_doc_com ("pay_doc.com");
record auxinfo11   ("auxinfo.11" );
record exoperat_cnv("exoperat.dbt","exchange.def");
record cashdoc     (sb_casdc);

file tarif             (sb_tarif) key 0;
file tarif_plus        (sb_tarpl) key 0;
file depositr          (depositr) key 8;
file sb_dtyp           (sb_dtyp ) key 2;
file sbdepdoc_conv_tax (sbdepdoc) key 3;
file auxinfo_conv_tax  (auxinfo ) key 0;
file pay_kind          (pay_kind) key 0;
file sb_algop          (sb_algop) key 0;
file exopset           ("exopset.dbt","exchange.def") key 0;
file cashval           (sb_casvl) key 0;
file sb_casvl_1        (sb_casvl) key 1;
private file currency  (currency) key 0;

var depclnt        = TClientList;
var depclnt_depdoc = TClientList;

const ResTax              =   3; /* Номер тарифа - налог на резидента                  */
const NonresTax           =   4; /* Номер тарифа - налог на нерезидента                */
const ResTax_New          =   6; /* Номер тарифа - налог на резидента (с 01.01.2001)   */
const NonresTax_New       =   7; /* Номер тарифа - налог на нерезидента (с 01.01.2001) */
const CNV_SB_DEPOSIT      =   1; /* Документ по вкладной операции - для Application    */
const CNV_SB_CASHOPERT    = 200; /* Документ по кассовой операции - для Application    */
const CO_Comission        =  21; /* Кассовая операция - Комиссия                       */
const CO_Conversion       =  31; /* Кассовая операция - Конверсия                      */
const COMM_CONVERSION_TAX =   2; /* Вид комиссии - Налог на конвертацию                */
const CVT_NONCASH_DEP     =   2; /* Вид конверсии - Б/конверсия для вкладов          */
const CVT_NONCASH_NALPER  =   4; /* Вид конверсии - Б/конверсия для перечислений     */
const CVT_NONCASH_PAYED   =   5; /* Вид конверсии - Б/конверсия для выплат           */
const CVT_NONCASH_COMM    = Office.ComCnvCoinsKind; /*6 Вид конверсии - Б/конверсия по комиссии          */
const CVT_NONCASH_EXCH_IN = Office.InCnvCoinsKind;  /*7 Вид конверсии - Б/конверсия для приходных ВОО    */
const CVT_NONCASH_EXCH_OUT= Office.OutCnvCoinsKind; /*8 Вид конверсии - Б/конверсия для расходных ВОО    */
const OP_GETCON           =  62; /* Операция по вкладам - Простое списание             */
const OP_GETCONV          =  11; /* Подвид простого списания - Безналичная конверсия */
private const D_GET       =   3; /* Операция - Перечисление                            */
private const D_PUT       =   5;
const CI_CREDIT           =   1;
const CI_DEBET            =   2;
const STAND_CASHSERV_OPER =   1;

const NEED_PAYRATE  = 0;
const NEED_SALERATE = 1;

const TYPERES_CURRENCY = 1; /* Валюта */
const TYPE_RES_PAY     = 0; /* Платежеспособные ресурсы */

const
  CASHOP_DEBIT     = 5,  /* Расход */
  CASHOP_CREDIT    = 6;  /* Приход */

const
  ACCOUTN_OWNER = StrFor( 0 );

private const K_F9 = 323;

var SumExchangeForRUR     = $0L;
var SumFromExchangeForRUR = $0L;
var ConvSumForExchangeRUR = $0L;
var RestForAccount        = $0L;

var firstCall = true;

private var {curdate};


/**************************************************************************\
|    Определение суммы вклады, которая была приобретена за счет обмена     |
\**************************************************************************/
macro DefSumExchangeForRUR

  var ExchangeSum = $0L;

  auxinfo_conv_tax.Reference = CNVsbdepdoc.Referenc;
  auxinfo_conv_tax.InfoType  = 13;
  auxinfo_conv_tax.Date      = Date(0,0,0);

  if (GetEQ(auxinfo_conv_tax))
    SetRecordAddr(auxinfo11,auxinfo_conv_tax,0,0,TRUE);
    ExchangeSum = auxinfo11.SumIn - auxinfo11.SumOut;
  end;

  return ExchangeSum;

end;


/**************************************************************************\
|    Определение суммы вклады, которая была приобретена за счет обмена     |
\**************************************************************************/
macro DefRestForAccount

  var RestAccount = $0L;
  var stat;

  var cmd, rs;
  cmd = RsdCommand ( "select DEPDOC.T_APPLICATIONKEY from dsbdepdoc_dbt DEPDOC " +
                     "where DEPDOC.T_REFERENC = :Ref " +
                     "ORDER BY T_DATE_DOCUMENT DESC , T_NUMDAYDOC DESC");
                     
  rs = RsdRecordset (cmd);
  cmd.addParam ( "Ref", RSDBP_IN, CNVsbdepdoc.Referenc); 
  cmd.execute;
  
  stat = rs.moveNext();
  
  while (stat)
    sbdepdoc_conv_tax.applicationkey = rs.value("t_applicationkey");
    getEQ(sbdepdoc_conv_tax);
    if (IsServDoc(sbdepdoc_conv_tax) AND (sbdepdoc_conv_tax.FlagStorn == ""))
      RestAccount = sbdepdoc_conv_tax.Rest;
      stat = FALSE;
    else
      stat = rs.moveNext();
    end;
  end;

  return RestAccount;

end;


/**************************************************************************\
|                            Поиск вида вклада                             |
\**************************************************************************/
macro FindDepType (IsCur, Kind)

  sb_dtyp.FlagCur = IsCur;
  sb_dtyp.Kind    = Kind;

  return GetEQ(sb_dtyp);

end;


/**************************************************************************\
|                               Поиск тарифа                               |
\**************************************************************************/
macro FindTarif( Num, _Date, _Sum )

  var stat;

  tarif.IsCur = 0;
  tarif.NumTarif = Num;

  stat = GetEQ(tarif);

  if ( stat  AND  ( tarif.TarifPlus ) )
    tarif_plus.IsCur    = 0;
    tarif_plus.NumTarif = Num;
    tarif_plus.Date     = _Date;
    tarif_plus.SumOper  = _Sum;
    if ( GetGE( tarif_plus )            AND
         ( tarif_plus.IsCur    == 0 )   AND
         ( tarif_plus.NumTarif == Num ) AND
         ( tarif_plus.Date     == _Date ) )
      tarif.PerSent = tarif_plus.Percent;
      tarif.MinSum  = tarif_plus.MinSum;
      tarif.MaxSum  = tarif_plus.MaxSum;
      tarif.Summa   = tarif_plus.Summa;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                             Поиск вкладчика                              |
\**************************************************************************/
macro FindClient ( CodClnt )

  return depclnt.GetRecord( CodClnt );

end;


/**************************************************************************\
|                          Поиск счета вкладчика                           |
\**************************************************************************/
macro FindDepositr (Referenc)

  depositr.Referenc = Referenc;

  return GetEQ(depositr);

end;


/**************************************************************************\
|     Определение номера операции по для конвертации прочего документа     |
\**************************************************************************/
macro DefNumOperConvForPayDoc

  var NumOper = 0;

  pay_kind.IsCur      = pay_doc.IsCur;
  pay_kind.GroupOpert = pay_doc.GroupOpert;
  pay_kind.NumOperat  = pay_doc.NumOpert;

  if (GetEQ(pay_kind))
    NumOper = pay_kind.NumOpertConv;
  end;

  return NumOper;

end;


/**************************************************************************\
|    Определение номера операции по для конвертации вкладного документа    |
\**************************************************************************/
macro DefNumOperConvForDepDoc

  var NumOper = 0;

  sb_algop.IsCur      = CNVsbdepdoc.IsCur;
  sb_algop.Kind       = CNVsbdepdoc.Type_Account;
  sb_algop.NumOpert   = CNVsbdepdoc.TypeOper;
  sb_algop.NumStepAlg = 267;
  sb_algop.BegDate    = CNVsbdepdoc.Date_Document;
  if ( GetLE(sb_algop)                                  AND
      (sb_algop.IsCur      == CNVsbdepdoc.IsCur       ) AND
      (sb_algop.Kind       == CNVsbdepdoc.Type_Account) AND
      (sb_algop.NumOpert   == CNVsbdepdoc.TypeOper    ) AND
      (sb_algop.NumStepAlg == 267                     )    )
    NumOper = sb_algop.prmL;
  end;

  if (NumOper == 0)
    sb_algop.IsCur      = CNVsbdepdoc.IsCur;
    sb_algop.Kind       = "Шаблонный";
    sb_algop.NumOpert   = CNVsbdepdoc.TypeOper;
    sb_algop.NumStepAlg = 267;
    sb_algop.BegDate    = CNVsbdepdoc.Date_Document;
    if ( GetLE(sb_algop)                              AND
        (sb_algop.IsCur      == CNVsbdepdoc.IsCur   ) AND
        (sb_algop.Kind       == "Шаблонный"         ) AND
        (sb_algop.NumOpert   == CNVsbdepdoc.TypeOper) AND
        (sb_algop.NumStepAlg == 267                 )    )
      NumOper = sb_algop.prmL;
    end;
  end;

  return NumOper;

end;


/**************************************************************************\
|  Находит настройки ВОО                                                   |
\**************************************************************************/
macro FindExopset( Branch, Kind_Oper, SubOper )
  
  ClearRecord( exopset );
  exopset.Branch    = Branch;
  exopset.Kind_Oper = Kind_Oper;
  exopset.SubOper   = SubOper;
  
  return GetEQ( exopset );

end;


/**************************************************************************\
|  Определяет код ценности 10, 10+11 или 11 (по сумме и валюте)            |
\**************************************************************************/
macro DefineCoins( Sum, CodeCurrency )
  
  var scale;
  var Up, Down;
  var HasCoins;
  
  if( CodeCurrency == {NationalCur} )
    scale = 10;
  else
    scale = 1;
  end;

  Sum  = double( Sum ) / scale;

  Up   = -1 * floor( -1 * Sum );
  Down =      floor(      Sum );

  if( Up == Down )
    HasCoins = 0; /* купюры */
  elif( Down == 0 )
    HasCoins = 2; /* монеты */
  else
    HasCoins = 1; /* купюры и монеты */
  end;

  return HasCoins;

end;


/**************************************************************************\
|  Формирует пустую строку нужной длины. Полезно для бинарных данных.      |
\**************************************************************************/
macro MkClearField( DB, FieldName );

  var n = fldindex( DB, FieldName );
  var len;

  if( n != fldnumber(DB)-1 )
    len = FldOffset ( DB, n+1 ) - FldOffset ( DB, n );
  else
    len = GetRecordSize( DB ) - FldOffset ( DB, n );
  end;

  return MkStr( 0, len-1 );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro FindSB_CasVl_1 ( codCur )

  var stat;

  ClearRecord( sb_casvl_1 );
  sb_casvl_1.TypeValue = TYPERES_CURRENCY;
  sb_casvl_1.CodIntValue = codCur;
  sb_casvl_1.Type = TYPE_RES_PAY;

  return GetEQ( sb_casvl_1 );

end;


/**************************************************************************\
|  Конверсия мелочи. Проводится как валютно-обменная операция.             |
\**************************************************************************/
macro exchop_conv
(
 DocAddr,       /* Адрес первичного документа                          */
 TypeDoc,       /* Тип первичного документа (1 - вклады, 2 - кассовый, */
                /*   3 - ВОО (сумма прихода), 4 - ВОО (сумма расхода)) */
 AddToListOper, /* Добавить в список документов                        */
 NeedCalcSum,   /* Считать суммы или они пришли подготовленными извне  */
 ConvSum,       /* Сумма конверсии - здесь менять не рекомендуется     */
 ConvSumRUR,    /* Сумма конверсии в рублях                            */
 TaxSum         /* Сумма налога на конверсию                           */
)

  var Kind_Oper;
  var SubOper = 1;
  var CodeCurrency;
  var dateOper;
  var numBranch;
  var CBRate; 
  var BuyRate;
  var SellRate;
  var Kurs;
  var dtRate, tmRate;
  var NeedRate = NEED_PAYRATE;
  var refValNat;

  if ( not FindSB_CasVl_1 ( {NationalCur} ) )
    MsgBox( "Не найден вид ресурса для валюты с кодом " + String( {NationalCur} ) );
    return 1;
  else
    refValNat = sb_casvl_1.RefValue;
  end;

  ClearRecord( exoperat_cnv );

  /* Вкладные операции ================================================ */
  if   ( TypeDoc == 1 ) 
    SetBuff( CNVsbdepdoc, DocAddr );
    Kind_Oper    = CDF_OperCurrBuy;
    CodeCurrency = CNVsbdepdoc.Code_Currency;
    dateOper = CNVsbdepdoc.Date_Document;
    numBranch = CNVsbdepdoc.FNCash;
    NeedRate = NEED_PAYRATE;

    if ( not FindSB_CasVl_1 ( CNVsbdepdoc.Code_Currency ) )
      MsgBox( "Не найден вид ресурса для валюты с кодом " + String( CNVsbdepdoc.Code_Currency ) );
      return 1;
    end;

    exoperat_cnv.Branch = CNVsbdepdoc.FNCash;
    exoperat_cnv.Brigade = CNVsbdepdoc.Brigade;
    exoperat_cnv.CurDate = CNVsbdepdoc.Date_Document;
    exoperat_cnv.Oper = CNVsbdepdoc.oper;
    exoperat_cnv.Date = Date( );
    exoperat_cnv.Time = Time( );
    exoperat_cnv.ClientCode = CNVsbdepdoc.CodClient;
    if ( depclnt_depdoc.GetRecord( CNVsbdepdoc.CodClient ) )
      exoperat_cnv.Sname = depclnt_depdoc.CurRec.rec.Name1;
      exoperat_cnv.Name = depclnt_depdoc.CurRec.rec.Name2;
      exoperat_cnv.Pname = depclnt_depdoc.CurRec.rec.Name3;
      exoperat_cnv.Resident = depclnt_depdoc.CurRec.rec.NotResident;
      exoperat_cnv.ClientDocType = depclnt_depdoc.CurRec.rec.PaperKind ;
      exoperat_cnv.ClientDocNumber = depclnt_depdoc.CurRec.rec.PaperNumber;
      exoperat_cnv.ClientDocSeria = depclnt_depdoc.CurRec.rec.PaperSeries;
      exoperat_cnv.ClientDocInfo = depclnt_depdoc.CurRec.rec.PaperIssuer;
      exoperat_cnv.ClientAddress = depclnt_depdoc.CurRec.rec.Address;
    end;
  /* Прочие операции ================================================== */
  elif ( TypeDoc == 2 )
    SetBuff( pay_doc, DocAddr );
    CodeCurrency = pay_doc.CodCur;
    dateOper = pay_doc.Date_Document;
    numBranch = pay_doc.FNCash;
    if (pay_doc.GroupOpert == 41) /* Выплаты */
      Kind_Oper    = CDF_OperCurrBuy;
      NeedRate = NEED_PAYRATE;
    else
      Kind_Oper = CDF_OperCurrSale;
      NeedRate = NEED_SALERATE;
    end;

    if ( not FindSB_CasVl_1 ( pay_doc.CodCur ) )
      MsgBox( "Не найден вид ресурса для валюты с кодом " + String( pay_doc.CodCur ) );
      return 1;
    end;

    exoperat_cnv.Branch = pay_doc.FNCash;
    exoperat_cnv.Brigade = pay_doc.Brigade;
    exoperat_cnv.CurDate = pay_doc.Date_Document;
    exoperat_cnv.Oper = pay_doc.oper;
    exoperat_cnv.Date = Date( );
    exoperat_cnv.Time = Time( );
    exoperat_cnv.ClientCode = pay_doc.CodClient;
    exoperat_cnv.Sname = pay_doc.ClientLastName;
    exoperat_cnv.Name = pay_doc.ClientFirstName;
    exoperat_cnv.Pname = pay_doc.ClientSecondName;
    exoperat_cnv.ClientDocType = pay_doc.PersIDType;
    exoperat_cnv.ClientDocNumber = pay_doc.PassportNumb;
    exoperat_cnv.ClientDocSeria = pay_doc.PassportSer;
    exoperat_cnv.ClientDocInfo = pay_doc.PassportIssuer;
    exoperat_cnv.ClientAddress = pay_doc.ClientAddress;
    if ( pay_doc.GroupOpert == 21 )
      SetBuff( pay_doc_com, DocAddr );
      exoperat_cnv.Resident = pay_doc_com.Citizenship;
    else
      exoperat_cnv.Resident = pay_doc.FlagRez;
    end;
  /* Обменно-валютные операции (сумма прихода) ======================== */
  elif ( TypeDoc == 3 )
    SetBuff( exoperat, DocAddr );
    Kind_Oper = CDF_OperCurrSale;
    CodeCurrency = FindCodCurByRefVal( exoperat.IncomeRefVal );
    dateOper = exoperat.CurDate;
    numBranch = exoperat.Branch;
    NeedRate = NEED_SALERATE;

    exoperat_cnv.Branch = exoperat.Branch;
    exoperat_cnv.Brigade = exoperat.Brigade;
    exoperat_cnv.CurDate = exoperat.CurDate;
    exoperat_cnv.Oper = exoperat.Oper;
    exoperat_cnv.Date = exoperat.Date;
    exoperat_cnv.Time = exoperat.Time;
    exoperat_cnv.ClientCode = exoperat.ClientCode;
    exoperat_cnv.Sname = exoperat.Sname;
    exoperat_cnv.Name = exoperat.Name;
    exoperat_cnv.Pname = exoperat.Pname;
    exoperat_cnv.Resident = exoperat.Resident;
    exoperat_cnv.ClientDocType = exoperat.ClientDocType;
    exoperat_cnv.ClientDocNumber = exoperat.ClientDocNumber;
    exoperat_cnv.ClientDocSeria = exoperat.ClientDocSeria;
    exoperat_cnv.ClientDocInfo = exoperat.ClientDocInfo;
    exoperat_cnv.ClientAddress = exoperat.ClientAddress;
  /* Обменно-валютные операции (сумма расхода) ======================== */
  elif ( TypeDoc == 4 )
    SetBuff( exoperat, DocAddr );
    Kind_Oper    = CDF_OperCurrBuy;
    CodeCurrency = FindCodCurByRefVal( exoperat.OutRefVal );
    dateOper = exoperat.CurDate;
    numBranch = exoperat.Branch;
    NeedRate = NEED_PAYRATE;
  
    exoperat_cnv.Branch = exoperat.Branch;
    exoperat_cnv.Brigade = exoperat.Brigade;
    exoperat_cnv.CurDate = exoperat.CurDate;
    exoperat_cnv.Oper = exoperat.Oper;
    exoperat_cnv.Date = exoperat.Date;
    exoperat_cnv.Time = exoperat.Time;
    exoperat_cnv.ClientCode = exoperat.ClientCode;
    exoperat_cnv.Sname = exoperat.Sname;
    exoperat_cnv.Name = exoperat.Name;
    exoperat_cnv.Pname = exoperat.Pname;
    exoperat_cnv.Resident = exoperat.Resident;
    exoperat_cnv.ClientDocType = exoperat.ClientDocType;
    exoperat_cnv.ClientDocNumber = exoperat.ClientDocNumber;
    exoperat_cnv.ClientDocSeria = exoperat.ClientDocSeria;
    exoperat_cnv.ClientDocInfo = exoperat.ClientDocInfo;
    exoperat_cnv.ClientAddress = exoperat.ClientAddress;
  end;

  if ( ( not FindExopset( numBranch, Kind_Oper, SubOper ) ) and
       ( not FindExopset( 0        , Kind_Oper, SubOper ) )    )
    MsgBox( "Не найдены настройки операции:|" +
            "филиал "   + numBranch + " или 0 |" +
            "операция " + Kind_Oper              + "|" +
            "подвид "   + SubOper );
    return 1;
  end;

  PrepareDateTimeForRates( true, dateOper, null, dtRate, tmRate );

  var RateChangeID;

  if ( not FindCurrRate( CodeCurrency, exopset.RateKind, dtRate, CBRate, BuyRate, SellRate, tmRate, ConvSum, RateChangeID ) )
    MsgBox( "Не найден курс для валюты с кодом " + String( CodeCurrency ) + 
            "|за дату " + String( dateOper ) + " по виду " + String( exopset.RateKind ) );
    return 1;
  end;

/*
  if  ( TypeDoc == 3 )  
    Kurs = SellRate;
  elif( TypeDoc == 4 )
    Kurs = BuyRate;
  end;
*/

  if ( NeedRate == NEED_PAYRATE )
    Kurs = BuyRate;
  else
    Kurs = SellRate;
  end;

  ConvSumRUR = Round( Money( ConvSum * Kurs ), 2 );

  exoperat_cnv.Kind_Oper = exopset.Kind_Oper;
  exoperat_cnv.SubOper   = exopset.SubOper;
  exoperat_cnv.Rate_Ref  = RateChangeID;
  exoperat_cnv.Kurs      = Kurs;
  exoperat_cnv.CBRate    = CBRate;
  exoperat_cnv.Scale     = 1;
  exoperat_cnv.Point     = 4;

  /* Вкладные операции ================================================ */
  if   ( TypeDoc == 1 ) 
    exoperat_cnv.IncomeRefVal      = sb_casvl_1.RefValue;
    exoperat_cnv.IncomeTypeValue   = TYPERES_CURRENCY;
    exoperat_cnv.IncomeCodIntValue = CNVsbdepdoc.Code_Currency;
    exoperat_cnv.IncomeType        = TYPE_RES_PAY;
    exoperat_cnv.IncomeCoins       = DefineCoins( ConvSum, CodeCurrency );
    exoperat_cnv.IncomeAmount      = ConvSum;
    exoperat_cnv.OutRefVal         = refValNat;
    exoperat_cnv.OutTypeValue      = TYPERES_CURRENCY;
    exoperat_cnv.OutCodIntValue    = {NationalCur};
    exoperat_cnv.OutType           = TYPE_RES_PAY;
    exoperat_cnv.OutCoins          = DefineCoins( ConvSumRUR, {NationalCur} );
    exoperat_cnv.OutAmount         = ConvSumRUR;
  /* Прочие операции ================================================== */
  elif ( TypeDoc == 2 )
    if ( Kind_Oper == CDF_OperCurrBuy )
      exoperat_cnv.IncomeRefVal      = sb_casvl_1.RefValue;
      exoperat_cnv.IncomeTypeValue   = TYPERES_CURRENCY;
      exoperat_cnv.IncomeCodIntValue = pay_doc.CodCur;
      exoperat_cnv.IncomeType        = TYPE_RES_PAY;
      exoperat_cnv.IncomeCoins       = DefineCoins( ConvSum, CodeCurrency );
      exoperat_cnv.IncomeAmount      = ConvSum;
      exoperat_cnv.OutRefVal         = refValNat;
      exoperat_cnv.OutTypeValue      = TYPERES_CURRENCY;
      exoperat_cnv.OutCodIntValue    = {NationalCur};
      exoperat_cnv.OutType           = TYPE_RES_PAY;
      exoperat_cnv.OutCoins          = DefineCoins( ConvSumRUR, {NationalCur} );
      exoperat_cnv.OutAmount         = ConvSumRUR;
    else
      exoperat_cnv.IncomeRefVal      = refValNat;
      exoperat_cnv.IncomeTypeValue   = TYPERES_CURRENCY;
      exoperat_cnv.IncomeCodIntValue = {NationalCur};
      exoperat_cnv.IncomeType        = TYPE_RES_PAY;
      exoperat_cnv.IncomeCoins       = DefineCoins( ConvSumRUR, {NationalCur} );
      exoperat_cnv.IncomeAmount      = ConvSumRUR;
      exoperat_cnv.OutRefVal         = sb_casvl_1.RefValue;
      exoperat_cnv.OutTypeValue      = TYPERES_CURRENCY;
      exoperat_cnv.OutCodIntValue    = pay_doc.CodCur;
      exoperat_cnv.OutType           = TYPE_RES_PAY;
      exoperat_cnv.OutCoins          = DefineCoins( ConvSum, CodeCurrency );
      exoperat_cnv.OutAmount         = ConvSum;
    end;
  /* Обменно-валютные операции (сумма прихода) ======================== */
  elif ( TypeDoc == 3 )
    exoperat_cnv.IncomeRefVal      = refValNat;
    exoperat_cnv.IncomeTypeValue   = TYPERES_CURRENCY;
    exoperat_cnv.IncomeCodIntValue = {NationalCur};
    exoperat_cnv.IncomeType        = TYPE_RES_PAY;
    exoperat_cnv.IncomeCoins       = DefineCoins( ConvSumRUR, {NationalCur} );
    exoperat_cnv.IncomeAmount      = ConvSumRUR;
    exoperat_cnv.OutRefVal         = exoperat.IncomeRefVal;
    exoperat_cnv.OutTypeValue      = TYPERES_CURRENCY;
    exoperat_cnv.OutCodIntValue    = exoperat.IncomeCodIntValue;
    exoperat_cnv.OutType           = TYPE_RES_PAY;
    exoperat_cnv.OutCoins          = DefineCoins( ConvSum, CodeCurrency );
    exoperat_cnv.OutAmount         = ConvSum;
  /* Обменно-валютные операции (сумма расхода) ======================== */
  elif ( TypeDoc == 4 )
    exoperat_cnv.IncomeRefVal      = exoperat.OutRefVal;
    exoperat_cnv.IncomeTypeValue   = TYPERES_CURRENCY;
    exoperat_cnv.IncomeCodIntValue = exoperat.OutCodIntValue;
    exoperat_cnv.IncomeType        = TYPE_RES_PAY;
    exoperat_cnv.IncomeCoins       = DefineCoins( ConvSum, CodeCurrency );
    exoperat_cnv.IncomeAmount      = ConvSum;
    exoperat_cnv.OutRefVal         = refValNat;
    exoperat_cnv.OutTypeValue      = TYPERES_CURRENCY;
    exoperat_cnv.OutCodIntValue    = {NationalCur};
    exoperat_cnv.OutType           = TYPE_RES_PAY;
    exoperat_cnv.OutCoins          = DefineCoins( ConvSumRUR, {NationalCur} );
    exoperat_cnv.OutAmount         = ConvSumRUR;
  end;
  
  if ( not InsertExchDocToGDDList( exoperat_cnv ) )
    return 1;
  end;

  StoreValue("doc:Сумма@КонвМелИсходная",ConvSum );
  StoreValue("doc:Сумма@КонвМелВыдача",ConvSumRUR );
  
  SetParm( 5, ConvSumRUR );
  SetParm( 6, TaxSum );

  return 0;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindCashVal( ValueRef )

  cashval.RefValue = ValueRef;

  return GetEQ( cashval );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetCurrCodeForCashDoc( cashdoc )

  if ( not FindCashVal( cashdoc.RefValue ) )
    MsgBox( "Не найден ресурс кассы с кодом ", cashdoc.RefValue );
    Exit( 1 );
  end;

  return cashval.CodIntValue;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro adjustAmounts ( DocAddr, TypeDoc, SumOper, ConvSum, Key )

  var codeCurr = -1;
  var stat = 0;

  if ( TypeDoc == 1 )
    SetBuff( CNVsbdepdoc, DocAddr );
    codeCurr = CNVsbdepdoc.Code_Currency;
  elif ( TypeDoc == 2 )
    SetBuff( pay_doc, DocAddr );
    codeCurr = pay_doc.CodCur;
  elif ( ( TypeDoc == 3 ) or ( TypeDoc == 4 ) )
    SetBuff( exoperat, DocAddr );
    if ( TypeDoc == 3 )
      codeCurr = FindCodCurByRefVal( exoperat.IncomeRefVal );
    else
      codeCurr = FindCodCurByRefVal( exoperat.OutRefVal );
    end;
  elif ( TypeDoc == 100 )
    SetBuff( cashdoc, DocAddr );
    codeCurr = GetCurrCodeForCashDoc( cashdoc );
  end;

  if ( codeCurr > 0 )
    KeyNum( currency, 0 );
    ClearRecord( currency );

    currency.Code_Currency = codeCurr;

    if ( GetEQ( currency ) )
      if ( currency.ExternalCode == "840" ) // Алгоритм для долларов
        if ( ( Key == K_F9 )  AND  ( ( SumOper - Floor( DoubleL( SumOper ) ) ) == 0 ) )
          stat = 1;
        end;
      elif ( currency.ExternalCode == "978" ) // Алгоритм для евро
        /*
        ConvSum = SumOper - MoneyL( Floor( DoubleL( SumOper / 5 ) ) * 5 );
        if ( ( Key == K_F9 )  AND  ( ConvSum == 0 ) )
          stat = 1;
        end;
        */
        if ( ( Key == K_F9 )  AND  ( ( SumOper - Floor( DoubleL( SumOper ) ) ) == 0 ) )
          stat = 1;
        end;
      elif ( currency.ExternalCode == "826" ) // Алгоритм для фунтов
        if ( ( Key == K_F9 )  AND  ( ( SumOper - Floor( DoubleL( SumOper ) ) ) == 0 ) )
          stat = 1;
        end;
      end;
    end;
  else
    if ( codeCurr == 0 )
      stat = 1;
    end;
  end;

  SetParm( 3, ConvSum );

  return stat;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/
macro conv_tax
(
 DocAddr,       /* Адрес первичного документа                          */
 TypeDoc,       /* Тип первичного документа (1 - вклады, 2 - кассовый, */
                /*   3 - ВОО (сумма прихода), 4 - ВОО (сумма расхода)) */
 AddToListOper, /* Добавить в список документов                        */
 NeedCalcSum,   /* Считать суммы или они пришли подготовленными извне  */
 ConvSum,       /* Сумма конверсии - здесь менять не рекомендуется     */
 ConvSumRUR,    /* Сумма конверсии в рублях                            */
 TaxSum,        /* Сумма налога на конверсию                           */
 SumOper,       /* Сумма операции                                      */
 Key            /* Клавиша вызова                                      */
)

  var CodeCurrency;
  var CodeOper;
  var NumOperConv = 0;
  var DateDoc;
  var dtRate, tmRate;
  var CodeClient  = 0;
  var TarifNum;
  var NeedRate    = NEED_PAYRATE;
  var Rate        = $0.0L;
  var WasFindClnt = FALSE;
  var stat        = 0;
  var flag;
  var FNCash      = NumFNCash();
  var RealFNCash  = NumRealFNcash();
  var FlagCur     = NumFlagCur();
  var DebCred;
  var RateKind    = 0;
  var CBRate, BuyRate, SellRate;
  var NumOpert;
  var numBranch;
  var Kind_Oper;
  var SubOper = 1;
  var convToExOpert = false;

  if ( ( AddToListOper == 0 ) and ( NeedCalcSum == 1 ) and firstCall )
    if ( adjustAmounts( DocAddr, TypeDoc, SumOper, ConvSum, Key ) != 0 )
      SetParm( 4, ConvSum );
      SetParm( 5, ConvSumRUR );
      SetParm( 6, TaxSum );
      return 1;
    end;
    firstCall = false;
  end;

  if ( ( ( TypeDoc == 1 ) or ( TypeDoc == 2 ) or ( TypeDoc == 3 ) or ( TypeDoc == 4 ) ) and
       ( GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\КОНВЕРСИЯ_ВАЛ_МЕЛОЧИ\\ЧЕРЕЗ_ВОО" ) == 1 )       )
    convToExOpert = true;
  end;

  if ( convToExOpert and ConvSum and AddToListOper )
    stat = exchop_conv( DocAddr, TypeDoc, AddToListOper, NeedCalcSum, ConvSum, ConvSumRUR, TaxSum );

    if ( stat == 0 )
      SetParm( 4, ConvSum );
      SetParm( 5, ConvSumRUR );
      SetParm( 6, TaxSum );
    end;

    return stat;
  end;

  /* Вклады */
  if ( TypeDoc == 1 )
    SetBuff( CNVsbdepdoc, DocAddr );
    CodeCurrency = CNVsbdepdoc.Code_Currency;
    CodeOper     = CNVsbdepdoc.oper;
    DateDoc      = CNVsbdepdoc.Date_Document;
    numBranch    = CNVsbdepdoc.FNCash;
    Kind_Oper    = CDF_OperCurrSale;
    NumOperConv  = DefNumOperConvForDepDoc( );
    if ( FindDepositr( CNVsbdepdoc.Referenc ) )
      CodeClient = depositr.CodClient;
    end;
    if ( CNVsbdepdoc.OutSum != 0 )
      NeedRate = NEED_PAYRATE;
      DebCred = CI_DEBET;
    else
      NeedRate = NEED_SALERATE;
      DebCred = CI_CREDIT;
    end;
  /* Налично-кассовые операции */
  elif ( TypeDoc == 2 )
    SetBuff( pay_doc, DocAddr );
    CodeCurrency = pay_doc.CodCur;
    CodeOper     = pay_doc.oper;
    DateDoc      = pay_doc.Date_Document;
    numBranch    = pay_doc.FNCash;
    Kind_Oper    = CDF_OperCurrSale;
    NumOperConv  = DefNumOperConvForPayDoc();
    if ( pay_doc.GroupOpert == 41 ) /* Выплаты */
      NeedRate = NEED_PAYRATE;
      DebCred = CI_DEBET;
    else
      NeedRate = NEED_SALERATE;
      DebCred = CI_CREDIT;
    end;
  /* Валютнообменные операции */
  elif ( ( TypeDoc == 3 ) or ( TypeDoc == 4 ) )
    SetBuff( exoperat, DocAddr );
    if ( TypeDoc == 3 )
      CodeCurrency = FindCodCurByRefVal( exoperat.IncomeRefVal );
      NeedRate     = NEED_SALERATE;
      DebCred      = CI_CREDIT;
      Kind_Oper    = CDF_OperCurrSale;
    else
      CodeCurrency = FindCodCurByRefVal( exoperat.OutRefVal );
      NeedRate     = NEED_PAYRATE;
      DebCred      = CI_DEBET;
      Kind_Oper    = CDF_OperCurrBuy;
    end;
    CodeOper     = exoperat.oper;
    DateDoc      = exoperat.curdate;
    CodeClient   = exoperat.ClientCode;
    numBranch    = exoperat.Branch;
  /* Кассовый документ */
  elif ( TypeDoc == 100 )
    SetBuff( cashdoc, DocAddr );
    CodeCurrency = GetCurrCodeForCashDoc( cashdoc );
    CodeOper     = cashdoc.oper;
    DateDoc      = cashdoc.CashDateDoc;
    NeedRate     = NEED_PAYRATE;
    DebCred      = CI_DEBET;
  else
    stat = 1;
    MsgBox(" Не определен тип документа ");
  end;

  if ( convToExOpert )
    if ( ( not FindExopset( numBranch, Kind_Oper, SubOper ) ) and
         ( not FindExopset( 0        , Kind_Oper, SubOper ) )    )
      MsgBox( "Не найдены настройки операции:|" +
              "филиал "   + numBranch + " или 0 |" +
              "операция " + Kind_Oper              + "|" +
              "подвид "   + SubOper );
      return 1;
    end;
  end;

  PrepareDateTimeForRates( true, DateDoc, null, dtRate, tmRate );

  /* Определение суммы конверсии в рублях */
  if ( ConvSum )

    /* Определение номера операции конвертации */
    NumOpert = NumOperConv;
    if ( NumOpert == 0 )
      if   ( TypeDoc == 1 )
        NumOpert   = CVT_NONCASH_DEP;
      elif ( TypeDoc == 2 )
        if   ( pay_doc.GroupOpert == 11 ) /* Прочие приходные -------- */
          NumOpert = CVT_NONCASH_NALPER;
        elif ( pay_doc.GroupOpert == 21 ) /* Комиссия ---------------- */
          NumOpert = CVT_NONCASH_COMM;
        else                              /* Прочие расходные -------- */
          NumOpert = CVT_NONCASH_PAYED;
        end;
      elif ( TypeDoc == 3 )
        NumOpert = CVT_NONCASH_EXCH_IN;
      elif ( TypeDoc == 4 )
        NumOpert = CVT_NONCASH_EXCH_OUT;
      elif ( TypeDoc == 100 )
        NumOpert = STAND_CASHSERV_OPER;
      end;
    end;

    pay_kind.IsCur      = 1/*FlagCur*/;
    pay_kind.GroupOpert = CO_Conversion;
    pay_kind.NumOperat  = NumOpert;

    /* Определение вида курсов для конвертации */
    if ( DateDoc == Date( 0, 0, 0 ) )
      DateDoc = {curdate};
    end;
    if ( GetEQ( pay_kind ) )
      RateKind = pay_kind.RateKind_Ref;
    end;
    if ( convToExOpert )
      RateKind = exopset.RateKind;
    end;
    if ( RateKind == 0 )
      RateKind = MAINRATEKIND; /* Рытик- по умолчанию- основной вид курсов */
    end;

    if ( FindCurrRate( CodeCurrency, RateKind, dtRate, CBRate, BuyRate, SellRate, tmRate, ConvSum ) )
      if ( NeedRate == NEED_PAYRATE )
        Rate = BuyRate;
      else
        Rate = SellRate;
      end;
      if ( NeedCalcSum )
        ConvSumRUR = Round( Money( ConvSum * Rate ), 2 );
      end;
    else
      MsgBox( "Не найден курс для валюты с кодом " + String( CodeCurrency ) + "|за дату " + String( DateDoc ) + " по виду " + String( RateKind ) );
      stat = 1;
    end;
  else
    ConvSumRUR = Money( 0 );
  end;

  /* Поиск клиента */
  if ( ( stat == 0 ) AND ( CodeClient != 0 ) )
    WasFindClnt = FindClient( CodeClient );
    if ( not WasFindClnt )
      depclnt.CurRec.Clear( );
    end;
  end;

  if( ( TypeDoc == 3 ) or ( TypeDoc == 4 ) )
    SetBuff( exoperat, DocAddr );
    if( not FindExopset( exoperat.Branch, exoperat.Kind_Oper, exoperat.SubOper ) )
      FindExopset( 0, exoperat.Kind_Oper, exoperat.SubOper  )
    end;
  end;

  /* Определение суммы налога */
  if ( ( stat == 0 ) and ( ConvSum ) and ( NeedCalcSum ) )
    if ( ( NeedRate == NEED_PAYRATE) and ( ConvSum ) and (exopset.IncTaxFlag != "") and ( Rate > CBRate ) )
      if (WasFindClnt)
        if ( depclnt.CurRec.rec.NotResident != StrFor( 0 ) )
          if ( DateDoc >= Date( 1, 1, 2001 ) )
            TarifNum = NonresTax_New;
          else
            TarifNum = NonresTax;
          end;
        else
          if ( DateDoc >= Date( 1, 1, 2001 ) )
            TarifNum = ResTax_New;
          else
            TarifNum = ResTax;
          end;
        end;
      else
        if ( DateDoc >= Date( 1, 1, 2001 ) )
          TarifNum = ResTax_New;
        else
          TarifNum = ResTax;
        end;
      end;
      if ( FindTarif( TarifNum, DateDoc, ConvSum ) )
        TaxSum = Money( ( ConvSum * ( Rate - CBRate ) ) * tarif.Persent / 1000000 );
        TaxSum = Round( TaxSum, 2 );
      else
        MsgBox( " Не найден тариф с кодом " + String( TarifNum ) + " " );
        stat = 1;
      end;
    end;
  end;

  /* При проводке сохраняем документы по конверсии и комиссии в списке GDDList */
  if ( ( stat == 0 ) and ( AddToListOper ) )
    /* Уменьшим сумму первичного документа ============================ */
    if ( ConvSum and ConvSumRUR )
      if (TypeDoc == 1)
        if (CNVsbdepdoc.OutSum)
          CNVsbdepdoc.OutSum = CNVsbdepdoc.OutSum - ConvSum;
        else
          CNVsbdepdoc.InSum = CNVsbdepdoc.InSum - ConvSum;
        end;
      elif (TypeDoc == 2)
        pay_doc.InSum = pay_doc.InSum - ConvSum;
      elif (TypeDoc == 3)
        exoperat.IncomeAmount = exoperat.IncomeAmount - ConvSum;
      elif (TypeDoc == 4)
        exoperat.OutAmount = exoperat.OutAmount - ConvSum;
      end;
    end;
    /* Вставка документа по вкладам на сумму конвертации ============== */
    if ((stat == 0) AND ConvSum AND ConvSumRUR AND (TypeDoc == 1))
      if ((CNVsbdepdoc.IsCur == 1) AND CNVsbdepdoc.OutSum)
        SumExchangeForRUR = DefSumExchangeForRUR();
        if (SumExchangeForRUR > 0)
          RestForAccount = DefRestForAccount();
          SumFromExchangeForRUR = (CNVsbdepdoc.OutSum + ConvSum) - (RestForAccount - SumExchangeForRUR);
          if ((SumFromExchangeForRUR >  CNVsbdepdoc.OutSum          ) AND
              (SumFromExchangeForRUR <= CNVsbdepdoc.OutSum + ConvSum)    )
            ConvSumForExchangeRUR = SumFromExchangeForRUR - CNVsbdepdoc.OutSum;
          end;
        end;
      end;
      ClearRecord(depdoc_cnv);
      depdoc_cnv.Referenc         = CNVsbdepdoc.Referenc;
      depdoc_cnv.IsCur            = CNVsbdepdoc.IsCur;
      depdoc_cnv.FNCash           = CNVsbdepdoc.FNCash;
      depdoc_cnv.RealFNCash       = CNVsbdepdoc.RealFNCash;
      depdoc_cnv.Type_Account     = CNVsbdepdoc.Type_Account;
      depdoc_cnv.Account          = CNVsbdepdoc.Account;
      depdoc_cnv.Code_Currency    = CNVsbdepdoc.Code_Currency;
      depdoc_cnv.CodClient        = CNVsbdepdoc.CodClient;
      depdoc_cnv.Oper             = CodeOper;
      depdoc_cnv.YesSbook         = "X";
      depdoc_cnv.Date_Document    = CNVsbdepdoc.Date_Document;
      depdoc_cnv.DepDate_Document = CNVsbdepdoc.DepDate_Document;
      depdoc_cnv.TypeComplexOper  = CNVsbdepdoc.TypeComplexOper;
      if (CNVsbdepdoc.TypeComplexOper == 54)
        depdoc_cnv.TypeOper       = 54;
        depdoc_cnv.ApplType       = 2;
        depdoc_cnv.KindOp         = D_PUT;
        depdoc_cnv.InSum          = ConvSum;
      else
        depdoc_cnv.TypeOper         = OP_GETCON;
        depdoc_cnv.ApplType         = OP_GETCONV;
        depdoc_cnv.KindOp           = D_GET;
        depdoc_cnv.OutSum           = ConvSum;
      end;
      depdoc_cnv.Author           = ACCOUTN_OWNER;
      depdoc_cnv.iApplicationKind = CNV_SB_DEPOSIT;
      depdoc_cnv.ApplicationKey   = FormApplicationKey(CNV_SB_DEPOSIT);
      depdoc_cnv.Ground           = "Б/п дробной части по курсу " + Trim(String(Double(Rate/*/100*/))) + " ";
      if (ConvSumForExchangeRUR > 0)
        if (depdoc_cnv.OutSum - ConvSumForExchangeRUR <= 0)
          depdoc_cnv.Source = 13;
        else
          Copy(depdoc_cnv2,depdoc_cnv);
          depdoc_cnv.OutSum          = depdoc_cnv.OutSum - ConvSumForExchangeRUR;
          depdoc_cnv2.OutSum         = ConvSumForExchangeRUR;
          depdoc_cnv2.Source         = 13;
          depdoc_cnv2.ApplicationKey = FormApplicationKey(CNV_SB_DEPOSIT);
          if (NOT InsertDocToGDDList(depdoc_cnv2))
            stat = 1;
          end;
        end;
      end;
      if (stat == 0)
        if (NOT InsertDocToGDDList(depdoc_cnv))
          stat = 1;
        end;
      end;
    end;

    /* Вставка документа на сумму конвертации ========================= */
    if ( ( stat == 0 ) and ConvSum )
      ClearRecord(convdoc);
      convdoc.FNCash              = RealFNCash;
      convdoc.IsCur               = 1/*FlagCur*/;
      convdoc.GroupOpert          = CO_Conversion;
      convdoc.Oper                = CodeOper;
      convdoc.InSum               = ConvSumRUR;
      convdoc.VydachSum           = ConvSumRUR;
      convdoc.KonvertSum          = ConvSum;
      convdoc.ApplType            = DebCred;
      if ( NeedRate == NEED_PAYRATE )
        convdoc.Yield             = Round( Money( ConvSum * CBRate ), 2 ) - ConvSumRUR;
      else
        convdoc.Yield             = ConvSumRUR - Round( Money( ConvSum * CBRate ), 2 );
      end;
      convdoc.Rate                = Rate;
      convdoc.CBRate              = CBRate;
      convdoc.FlagCredit          = StrFor( NeedRate );
      convdoc.DebetCredit         = DebCred;

      if (TypeDoc == 1)
        convdoc.Date_Document     = CNVsbdepdoc.Date_Document;
        if (NumOperConv > 0)
          convdoc.NumOpert        = NumOperConv;
        else
          convdoc.NumOpert        = CVT_NONCASH_DEP;
        end;
        convdoc.KonvertCodCur     = CNVsbdepdoc.Code_Currency;
        convdoc.Kind              = CNVsbdepdoc.Type_Account;
        if ( WasFindClnt )
          convdoc.FlagRez         = depclnt.CurRec.rec.NotResident; /* Резидентность владельца счета */
          convdoc.SpecialAccess   = depclnt.CurRec.rec.SpecialAccess;
        end;

        if ( depclnt_depdoc.GetRecord( CNVsbdepdoc.CodClient ) )
          convdoc.ClientLastName  = depclnt_depdoc.CurRec.rec.Name1;
          convdoc.ClientFirstName = depclnt_depdoc.CurRec.rec.Name2;
          convdoc.ClientSecondName= depclnt_depdoc.CurRec.rec.Name3;
          depclnt_depdoc.CurRec.GetPersnCard();
          convdoc.SocialNumber    = depclnt_depdoc.CurRec.rec.SocialNumber;
          convdoc.PersIDType      = depclnt_depdoc.CurRec.rec.PaperKind ;
          convdoc.PassportSer     = depclnt_depdoc.CurRec.rec.PaperSeries;
          convdoc.PassportNumb    = depclnt_depdoc.CurRec.rec.PaperNumber;
          convdoc.PassportIssuer  = depclnt_depdoc.CurRec.rec.PaperIssuer;
          convdoc.PassportDate    = depclnt_depdoc.CurRec.rec.PaperIssuedDate;
          convdoc.FlagRezid       = depclnt_depdoc.CurRec.rec.NotResident;
          convdoc.CodClient       = depclnt_depdoc.CurRec.rec.CodClient;
        end;

        if (convdoc.SpecialAccess)
          if (FindDepType(convdoc.IsCur,convdoc.Kind))
            convdoc.Ground        = "Счет вклада " + sb_dtyp.Name;
          else
            convdoc.Ground        = "Счет вклада " + convdoc.Kind;
          end;
        else
          convdoc.Ground          = "Счет " + Acc_Form(CNVsbdepdoc.Account);
        end;

      elif (TypeDoc == 2)
        convdoc.Date_Document     = pay_doc.Date_Document;
        if (NumOperConv > 0)
          convdoc.NumOpert        = NumOperConv;
        elif (pay_doc.GroupOpert == 11) /* Прочие приходные -------- */
          convdoc.NumOpert        = CVT_NONCASH_NALPER;
        elif (pay_doc.GroupOpert == 21) /* Комиссия ---------------- */
          setbuff(pay_doc_com,DocAddr);
          convdoc.NumOpert        = CVT_NONCASH_COMM;
        else                            /* Прочие расходные -------- */
          convdoc.NumOpert        = CVT_NONCASH_PAYED;
        end;
        convdoc.KonvertCodCur     = pay_doc.CodCur;

        convdoc.ClientLastName    = pay_doc.ClientLastName;
        convdoc.ClientFirstName   = pay_doc.ClientFirstName;
        convdoc.ClientSecondName  = pay_doc.ClientSecondName;
        convdoc.SocialNumber      = pay_doc.SocialNumber;
        convdoc.PersIDType        = pay_doc.PersIDType;
        convdoc.PassportSer       = pay_doc.PassportSer;
        convdoc.PassportNumb      = pay_doc.PassportNumb;
        convdoc.PassportIssuer    = pay_doc.PassportIssuer;
        convdoc.PassportDate      = pay_doc.PassportDate;
        convdoc.FlagRezid         = pay_doc.FlagRez;
        if (pay_doc.GroupOpert == 21)
          convdoc.FlagRez         = pay_doc_com.Citizenship;
        else
          convdoc.FlagRez         = pay_doc.FlagRez;
        end;

        convdoc.Ground            = "Безналичная конверсия на операцию " + String(pay_doc.GroupOpert) + "/" + String(pay_doc.NumOpert);

      elif ( (TypeDoc == 3 ) or ( TypeDoc == 4 ) )
        if( DebCred == CI_CREDIT )  /* Для ОП - именно так! */
          convdoc.FlagCredit      = StrFor( 1 );
        elif( DebCred == CI_DEBET )
          convdoc.FlagCredit      = StrFor( 0 );
        end;
        convdoc.Date_Document     = exoperat.CurDate;
        if(TypeDoc == 3)
          convdoc.NumOpert        = CVT_NONCASH_EXCH_IN;
          convdoc.KonvertCodCur   = FindCodCurByRefVal(exoperat.IncomeRefVal);
        else
          convdoc.NumOpert        = CVT_NONCASH_EXCH_OUT;
          convdoc.KonvertCodCur   = FindCodCurByRefVal(exoperat.OutRefVal);
        end;
        if (WasFindClnt)
          convdoc.ClientLastName  = depclnt.CurRec.rec.Name1;
          convdoc.ClientFirstName = depclnt.CurRec.rec.Name2;
          convdoc.ClientSecondName= depclnt.CurRec.rec.Name3;
          depclnt.CurRec.GetPersnCard();
          convdoc.SocialNumber    = depclnt.CurRec.rec.SocialNumber;
          convdoc.PersIDType      = depclnt.CurRec.rec.PaperKind;
          convdoc.PassportSer     = depclnt.CurRec.rec.PaperSeries;
          convdoc.PassportNumb    = depclnt.CurRec.rec.PaperNumber;
          convdoc.PassportIssuer  = depclnt.CurRec.rec.PaperIssuer;
          convdoc.PassportDate    = depclnt.CurRec.rec.PaperIssuedDate;
          convdoc.SpecialAccess   = depclnt.CurRec.rec.SpecialAccess;
          convdoc.FlagRez         = depclnt.CurRec.rec.NotResident;
          convdoc.FlagRezid       = depclnt.CurRec.rec.NotResident;
        end;
        convdoc.Ground            = "Безналичная конверсия на операцию "
              + GetNameExchOperation(exoperat.Branch,exoperat.Kind_Oper,exoperat.SubOper);
      elif (TypeDoc == 100)
        convdoc.Date_Document     = cashdoc.CashDateDoc;
        convdoc.NumOpert          = STAND_CASHSERV_OPER;

        convdoc.KonvertCodCur     = CodeCurrency;

/* Надо научиться определять код клиента! Это будет легко сделать,
   когда его добавят в ret_op.dbt

        convdoc.ClientLastName    = pay_doc.ClientLastName;
        convdoc.ClientFirstName   = pay_doc.ClientFirstName;
        convdoc.ClientSecondName  = pay_doc.ClientSecondName;
        convdoc.SocialNumber      = pay_doc.SocialNumber;
        convdoc.PersIDType        = pay_doc.PersIDType;
        convdoc.PassportSer       = pay_doc.PassportSer;
        convdoc.PassportNumb      = pay_doc.PassportNumb;
        convdoc.PassportIssuer    = pay_doc.PassportIssuer;
        convdoc.PassportDate      = pay_doc.PassportDate;
        convdoc.FlagRezid         = pay_doc.FlagRez;
        if (pay_doc.GroupOpert == 21)
          convdoc.FlagRez         = pay_doc_com.Citizenship;
        else
          convdoc.FlagRez         = pay_doc.FlagRez;
        end;
*/

        convdoc.Ground            = "Безналичная частичная конверсия выдаваемой суммы";

      end;

      convdoc.iApplicationKind    = CNV_SB_CASHOPERT;
      convdoc.ApplicationKey      = FormApplicationKey(CNV_SB_CASHOPERT);
      if ( ConvSum and ConvSumRUR )
        flag = InsertPayDocToGDDList( convdoc );
        if ( not flag )
          stat = 1;
        end;

        StoreValue("doc:Сумма@КонвМелИсходная",ConvSum );
        StoreValue("doc:Сумма@КонвМелВыдача",ConvSumRUR );

      end;
    end;

    /* Вставка документа на сумму налога ============================== */
    if ( ( stat == 0 ) and TaxSum )
      ClearRecord(taxdoc);
      taxdoc.FNCash           = RealFNCash;
      taxdoc.IsCur            = 1;
      taxdoc.GroupOpert       = CO_Comission;
      taxdoc.NumOpert         = COMM_CONVERSION_TAX;
      taxdoc.CodCur           = 0;
      taxdoc.Oper             = CodeOper;
      taxdoc.InSum            = TaxSum;
      taxdoc.iApplicationKind = CNV_SB_CASHOPERT;
      taxdoc.ApplicationKey   = FormApplicationKey( CNV_SB_CASHOPERT );
      taxdoc.ClientLastName   = convdoc.ClientLastName;
      taxdoc.ClientFirstName  = convdoc.ClientFirstName;
      taxdoc.ClientSecondName = convdoc.ClientSecondName;
      taxdoc.SocialNumber     = convdoc.SocialNumber;
      taxdoc.PersIDType       = convdoc.PersIDType;
      taxdoc.PassportSer      = convdoc.PassportSer;
      taxdoc.PassportNumb     = convdoc.PassportNumb;
      taxdoc.PassportIssuer   = convdoc.PassportIssuer;
      taxdoc.PassportDate     = convdoc.PassportDate;
      taxdoc.Citizenship      = convdoc.FlagRez;
      taxdoc.FlagRez          = convdoc.FlagRezid;

      taxdoc.DepNumOper = CO_Conversion; /* Налог на частичную конвертацию */ 

      if ( TypeDoc == 1 )
        taxdoc.Date_Document  = CNVsbdepdoc.Date_Document;
        if ( convdoc.SpecialAccess )
          if ( FindDepType( taxdoc.IsCur, CNVsbdepdoc.Type_Account ) )
            taxdoc.Ground     = "Налог на конвертацию по счету вклада " + sb_dtyp.Name;
          else
            taxdoc.Ground     = "Налог на конвертацию по счету вклада " + taxdoc.Kind;
          end;
        else
          taxdoc.Ground       = "Налог на конвертацию по счету " + Acc_Form(CNVsbdepdoc.Account);
        end;
      elif ( TypeDoc == 2 )
        taxdoc.Date_Document  = pay_doc.Date_Document;
        taxdoc.Ground         = "Налог на конвертацию по операции " + pay_doc.GroupOpert + "/" + pay_doc.NumOpert;
      end;

      flag = InsertPayDocToGDDList( taxdoc );
      if ( not flag )
        stat = 1;
      end;
    end;
  end;

  if ( stat == 0 )
    SetParm( 4, ConvSum );
    SetParm( 5, ConvSumRUR );
    SetParm( 6, TaxSum );
  end;

  return stat;

end;
