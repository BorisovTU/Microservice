/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Справка о состоянии вклада(ов).

  Заявление об изменении контрольной информации.

*****************************************************************/

import DeprIntr, ExchangeInter, Alg_Vars, OperCode, IsSrvDoc, GetDocs, Form_36b;

file opr ( person   ) key 0;
file dep ( depositr ) key 8;

file aux_ ( auxinfo ) key 0;

var clnt_l = TClientList; /* Справочник вкладчиков */

/* Константы названий для коммерческих банков */
const cTypeBank = "Коммерческий банк",
      cNumForm  = "",
      cKontr    = "Проверяющий",
      cZav      = "Упр.филиалом";

const NullDate = date( 0, 0, 0 );

private var {Name_Bank};

/*****************************************************************
                     Строковый формат даты
******************************************************************/
macro DateToStr( pDate )

  var d, m, y, Day, Month, Year;

  DateSplit( pDate, d, m, y );
  Day = String( d );

  if   (m ==  1) Month = " января ";
  elif (m ==  2) Month = " февраля ";
  elif (m ==  3) Month = " марта ";
  elif (m ==  4) Month = " апреля ";
  elif (m ==  5) Month = " мая ";
  elif (m ==  6) Month = " июня ";
  elif (m ==  7) Month = " июля ";
  elif (m ==  8) Month = " августа ";
  elif (m ==  9) Month = " сентября ";
  elif (m == 10) Month = " октября ";
  elif (m == 11) Month = " ноября ";
  elif (m == 12) Month = " декабря ";
  else           Month = "  ";
  end;

  Year = String(y);

  return Day + Month + Year + " г.";

end;

/*****************************************************************
                   Преобразование к Ф.И.О.
******************************************************************/
macro StrFIO( fio )
  var n, s, str;

  s = Trim( fio );
  n = Index( s, " " );

  if ( n == 0 )
    str = s;
  else
    str = StrUpr( SubStr( s, 1, n ), 1 );
    s = SubStr( s, n+1 );
    str = str + StrUpr(SubStr( s, 1, 1 )) + ".";
    n = Index( s, " " );
    if ( n != 0 )
      str = str + StrUpr(SubStr( s, n+1, 1 )) + ".";
    end;
  end;

  return str;
end;

/*****************************************************************
                  Основная процедура выписки
******************************************************************/
macro CBK_Main( Ref, Date1, Date2, sClient, sFIO, CodClnt, Standart,
                Archive, Period, sControlInfo )

  var sFilial, sOper;

  var sClntAddr, sClntPapers;

  var sAcc_OwnerFIO;

  var stat_ = true, posfile, count = 0;

  array aControlInfo;
  var i;

  var sTypeBank, sNumForm, sKontr, sZav;

  rewind( aux_ );
  clearrecord( aux_ );
  aux_.reference = ref;
  aux_.infotype  = 20;

  stat_ = GetGe( aux_ );
  while ( stat_ and ( aux_.reference == ref ) and ( aux_.infotype == 20 ) )
    count = count + 1;
    stat_ = next( aux_ );
  end;

  if ( count > 0 )

    PrintForm_36( Ref, CodClnt, "" );
    println( strfor(12) );

    /* Подстановка названий, согласно стандарту работы банка */
    if ( Standart == 0 )
    /* Сбербанк РФ */
      sTypeBank = "Сбербанк России";
      sNumForm  = "Ф.№299";
      sKontr    = "Контролер";
      sZav      = "Зав.филиалом";
    else
    /* Коммерческий банк */
      sTypeBank = cTypeBank;
      sNumForm  = cNumForm;
      sKontr    = cKontr;
      sZav      = cZav;
    end;

    /* Данные по заявителю */
    if ( NOT clnt_l.GetRecord( CodClnt ) )
      MsgBox( "Не найдена запись вкладчика с CodClient = ", CodClnt );
      Exit( 0 );
    end;
    sClntAddr = "проживающего по адресу: " +
                clnt_l.CurRec.MakeFullStrAddress();
    sClntPapers = "паспортные данные: " + clnt_l.CurRec.MakeDocStr();

    /* Чтение записи счета */
    dep.Referenc = Ref;
    if ( NOT GetEQ( dep ) )
      MsgBox( "Не найдена запись счета с Reference = ", Ref );
      Exit( 0 );
    end;

    /* Чтение записи вкладчика */
    if ( NOT clnt_l.GetRecord( dep.CodClient ) )
      MsgBox( "Не найдена запись вкладчика с CodClient = ", dep.CodClient );
      Exit( 0 );
    end;

    /* Чтение записи Операциониста */
    opr.Oper = {oper};
    if ( GetEQ( opr ) )
      sOper = opr.Name;
    else
      sOper = "???";
    end;
    /* Чтение записи о Филиале */
    if ( not findDepartment(dep.FNCash, sFilial) )
      sFilial = "???";
    end;

    /* Строка параметров счета (номер счета и Ф.И.О. вкладачика) */
    sAcc_OwnerFIO = "№ " + String( Acc_Form(dep.Account):f ) +
                    ", открытому(ым) на имя: " +
                    clnt_l.CurRec.rec.Name1 + " " +
                    clnt_l.CurRec.rec.Name2 + " " +
                    clnt_l.CurRec.rec.Name3;

    i = 0;
    while ( i < 30 )
      aControlInfo( i ) = " ";
      i = i + 1;
    end;

    i = 0;
    while ( i < StrLen( sControlInfo ) )
      aControlInfo( i ) = SubStr( sControlInfo, i+1, 1 );
      i = i + 1;
    end;

    /* Печать заголовка выписки */
    [
       #                                                                #
       #
                                      В подразделение #________________________
                                      #########################################
                                      #########################################
                                      #########################################


                    ЗАЯВЛЕНИЕ ОБ ИЗМЕНЕНИИ КОНТРОЛЬНОЙ ИНФОРМАЦИИ


         Прошу изменить контрольную информацию по счету(ам)
       ########################################################################

         Новая контрольная информация:
       +-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+
       |#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|
       +-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+
                           (не более 30 знаков)


                                                                        ___________________________
                                                                                  (Подпись)

                                                                        ###########################



       ОТМЕТКА КОНТРОЛЕРА


       Заявление принято. Контрольная информация по счету(ам) № ###################################
       в базе данных изменена.


                                         ___________________________/ ############################
                                                   (Подпись)

                                                                        ###########################]
    ( sTypeBank,
      sNumForm,
      {Name_Bank},
      sFilial,
      ( "от клиента: " + sFIO + "," ):w,
      sClntAddr:w,
      sClntPapers:w,
      sAcc_OwnerFIO:w,
      aControlInfo(  0 ),
      aControlInfo(  1 ),
      aControlInfo(  2 ),
      aControlInfo(  3 ),
      aControlInfo(  4 ),
      aControlInfo(  5 ),
      aControlInfo(  6 ),
      aControlInfo(  7 ),
      aControlInfo(  8 ),
      aControlInfo(  9 ),
      aControlInfo( 10 ),
      aControlInfo( 11 ),
      aControlInfo( 12 ),
      aControlInfo( 13 ),
      aControlInfo( 14 ),
      aControlInfo( 15 ),
      aControlInfo( 16 ),
      aControlInfo( 17 ),
      aControlInfo( 18 ),
      aControlInfo( 19 ),
      aControlInfo( 20 ),
      aControlInfo( 21 ),
      aControlInfo( 22 ),
      aControlInfo( 23 ),
      aControlInfo( 24 ),
      aControlInfo( 25 ),
      aControlInfo( 26 ),
      aControlInfo( 27 ),
      aControlInfo( 28 ),
      aControlInfo( 29 ),
      DateToStr( Date() ),
      Acc_Form(dep.Account):f,
      StrFIO( sOper ) + " /",
      DateToStr( Date() ) );

  end;

end;
