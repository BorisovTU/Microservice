/*
$Name:             ddr_decl.mac
$Module:           RS-Retail
$Description:      Справка о состоянии вкладов
*/

/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Справка о состоянии вклада(ов).

  Заявление на выдачу Справки о состоянии вклада.

*****************************************************************/

import DeprIntr, OperCode, IsSrvDoc, GetDocs, ExchangeInter, RSD;

file opr      ( person   ) key 0;
file dep      ( depositr ) key 8;
file op_obj   ( opobject ) key 0;
file decl     ( ddr_decl ) key 0;

var sb_casln = TBFile( "sb_casln.dbt", "r", 0 );

var depclnt = TClientList; /* Справочник вкладчиков */

const LogSC_DDRDecl = 510;  /* "Номер файла" ddr_decl - таблица заявлений */

/* Константы названий для коммерческих банков */
const cTypeBank = "Коммерческий банк",
      cNumForm  = "",
      cKontr    = "Проверяющий",
      cZav      = "Упр.филиалом";

const NullDate = date( 0, 0, 0 );

var Bank_Standard = GetBankStandart( );  /* Стандарт работы банка */

private var {Name_Bank};

/*****************************************************************
         Получение "Номера файла" из ALG_NFOPR.objectID
******************************************************************/
macro get_ID_FileNum( pID )
 var p = Index( pID, StrFor( 47 ) );  /* Позиция косой черты в ID */
 var s = SubStr( pID, 1, p-1 );
 return Int( s );
end;

/*****************************************************************
                     Строковый формат даты
******************************************************************/
macro DateToStr( pDate )

  var d, m, y, Day, Month, Year;

  DateSplit( pDate, d, m, y );
  Day = String( d );

  if   (m ==  1) Month = " января ";
  elif (m ==  2) Month = " февраля ";
  elif (m ==  3) Month = " марта ";
  elif (m ==  4) Month = " апреля ";
  elif (m ==  5) Month = " мая ";
  elif (m ==  6) Month = " июня ";
  elif (m ==  7) Month = " июля ";
  elif (m ==  8) Month = " августа ";
  elif (m ==  9) Month = " сентября ";
  elif (m == 10) Month = " октября ";
  elif (m == 11) Month = " ноября ";
  elif (m == 12) Month = " декабря ";
  else           Month = "  ";
  end;

  Year = String(y);

  return Day + Month + Year + " г.";

end;

/*****************************************************************
                   Преобразование к Ф.И.О.
******************************************************************/
macro StrFIO( fio )
  var n, s, str;

  s = Trim( fio );
  n = Index( s, " " );

  if ( n == 0 )
    str = s;
  else
    str = StrUpr( SubStr( s, 1, n ), 1 );
    s = SubStr( s, n+1 );
    str = str + StrUpr(SubStr( s, 1, 1 )) + ".";
    n = Index( s, " " );
    if ( n != 0 )
      str = str + StrUpr(SubStr( s, n+1, 1 )) + ".";
    end;
  end;

  return str;
end;

/*****************************************************************
                  Основная процедура выписки
******************************************************************/
macro DDR_Decl_Report()

  var sFilial, sOper;

  var sClntAddr, sClntPapers;

  var sAcc_OwnerFIO;

  var sFIO;
  var sControlInfo = decl.Key;

  var sPeriod03 = " ";
  var sPeriod06 = " ";
  var sPeriod12 = " ";

  array aControlInfo;
  var i;

  var ret;

  var sTypeBank, sNumForm, sKontr, sZav;

  var cmd, rs;

  /* Подстановка названий, согласно стандарту работы банка */
  if ( Bank_Standard == 0 )
  /* Сбербанк РФ */
    sTypeBank = "Сбербанк России";
    sNumForm  = "Ф.№298";
    sKontr    = "Контролер";
    sZav      = "Зав.филиалом";
  else
  /* Коммерческий банк */
    sTypeBank = cTypeBank;
    sNumForm  = cNumForm;
    sKontr    = cKontr;
    sZav      = cZav;
  end;

  /* Данные по заявителю */
  if ( NOT depclnt.GetRecord( decl.ClientCode ) )
    MsgBox( "Не найдена запись вкладчика с CodClient = ", decl.ClientCode );
    Exit( 0 );
  end;

  sFIO = depclnt.CurRec.rec.Name1 + " " +
         depclnt.CurRec.rec.Name2 + " " +
         depclnt.CurRec.rec.Name3;
  sClntAddr = "проживающего по адресу: " +
              depclnt.CurRec.MakeFullStrAddress();
  sClntPapers = "паспортные данные: " + depclnt.CurRec.MakeDocStr();

  /* Чтение записи Операциониста */
  opr.Oper = {oper};
  if ( GetEQ( opr ) )
    sOper = opr.Name;
  else
    sOper = "???";
  end;
  /* Чтение записи о Филиале */
  if ( not findDepartment(decl.Branch, sFilial) )
    sFilial = "???";
  end;

  /* Данные по владельцу счета(ов) */
  if ( NOT depclnt.GetRecord( decl.AccOwner ) )
    MsgBox( "Не найдена запись владельца счетов с CodClient = ",
            decl.AccOwner );
    Exit( 0 );
  end;

  /* Строка параметров счета (номер счета и Ф.И.О. вкладачика) */
  sAcc_OwnerFIO = "открытого(ых) на имя: " +
                  depclnt.CurRec.rec.Name1 + " " +
                  depclnt.CurRec.rec.Name2 + " " +
                  depclnt.CurRec.rec.Name3;

  if   ( decl.Period == 1 )
    sPeriod03 = "X";
  elif ( decl.Period == 2 )
    sPeriod06 = "X";
  elif ( decl.Period == 3 )
    sPeriod12 = "X";
  end;

  i = 0;
  while ( i < 30 )
    aControlInfo( i ) = " ";
    i = i + 1;
  end;

  i = 0;
  while ( i < StrLen( sControlInfo ) )
    aControlInfo( i ) = SubStr( sControlInfo, i+1, 1 );
    i = i + 1;
  end;

  /* Печать заголовка выписки */
  [
     #                                                                #
     #
                                    В подразделение #________________________
                                    #########################################
                                    #########################################
                                    #########################################


                                   ЗАЯВЛЕНИЕ


       Прошу предоставлять детализированную справку о состоянии вклада(ов)]

    ( sTypeBank,
      sNumForm,
      {Name_Bank},
      sFilial,
      ( "от клиента: " + sFIO + "," ):w,
      sClntAddr:w,
      sClntPapers:w );

    cmd = RsdCommand( "select link.t_accref as ref " +
                        "from dddr_link_dbt link "
                          "where link.t_declid = " + String( decl.ID ) );

    cmd.execute;

    rs = RsdRecordset( cmd );

    while ( rs.moveNext )
      /* Чтение записи счета */
      dep.Referenc = rs.value( "ref" );
      if ( NOT GetEQ( dep ) )
        println( "Не найдена запись счета с Reference = ", rs.value( "ref" ) );
        Exit( 0 );
      end;
      [                  ########################################################################]
      ( "№ " + String( Acc_Form(dep.Account):f ) + "," );
    end;

    [   ########################################################################

       Данную справку прошу выдавать начиная с даты подачи данного заявления
     +---+----------------------+  +---+----------------------+  +---+----------------------+
     | # |   Каждые три месяца  |  | # | Каждые шесть месяцев |  | # |      Каждый год      |
     +---+----------------------+  +---+----------------------+  +---+----------------------+
     при моем посещении структурного подразделения Сбербанка России, либо предоставлять
     содержащуюся в ней информацию по моему телефонному звонку при сообщении следующей
     контрольной информации:
     +-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+
     |#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|
     +-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+
     (укажите любое ключевое слово (не более 30 знаков) для идентификации Вас как вкладчика
     и запомните его).
        Подтверждаю, что не буду предъявлять претензий к банку, если сведения о состоянии моего
     вклада(ов) станут доступны  третьим лицам,  представившимся от моего имени  по телефону  и
     сообщившим контрольную информацию. В этом случае банк  не несет ответственности за разгла-
     шение сведений, составляющих банковскую тайну.

               С содержанием информации, включаемой в данную справку, ознакомлен(а).


                                                                      ___________________________
                                                                                (Подпись)

                                                                      ###########################



     ОТМЕТКА КОНТРОЛЕРА


     Заявление принято. Соответствующие отметки по счету(ам)]
  ( sAcc_OwnerFIO:w,
    sPeriod03,
    sPeriod06,
    sPeriod12,
    aControlInfo(  0 ),
    aControlInfo(  1 ),
    aControlInfo(  2 ),
    aControlInfo(  3 ),
    aControlInfo(  4 ),
    aControlInfo(  5 ),
    aControlInfo(  6 ),
    aControlInfo(  7 ),
    aControlInfo(  8 ),
    aControlInfo(  9 ),
    aControlInfo( 10 ),
    aControlInfo( 11 ),
    aControlInfo( 12 ),
    aControlInfo( 13 ),
    aControlInfo( 14 ),
    aControlInfo( 15 ),
    aControlInfo( 16 ),
    aControlInfo( 17 ),
    aControlInfo( 18 ),
    aControlInfo( 19 ),
    aControlInfo( 20 ),
    aControlInfo( 21 ),
    aControlInfo( 22 ),
    aControlInfo( 23 ),
    aControlInfo( 24 ),
    aControlInfo( 25 ),
    aControlInfo( 26 ),
    aControlInfo( 27 ),
    aControlInfo( 28 ),
    aControlInfo( 29 ),
    DateToStr( Date() ) );

  cmd = RsdCommand( "select link.t_accref as ref " +
                      "from dddr_link_dbt link "
                        "where link.t_declid = " + String( decl.ID ) );

  cmd.execute;

  rs = RsdRecordset( cmd );

  while ( rs.moveNext )
    /* Чтение записи счета */
    dep.Referenc = rs.value( "ref" );
    if ( NOT GetEQ( dep ) )
      println( "Не найдена запись счета с Reference = ", rs.value( "ref" ) );
      Exit( 0 );
    end;
    [                  ########################################################################]
    ( "№ " + String( Acc_Form(dep.Account):f ) + "," );
  end;

  [в базе данных проставлены.


                                       ___________________________/ ############################
                                                 (Подпись)

                                                                      ###########################]
  ( StrFIO( sOper ) + " /", DateToStr( Date() ) );

end;

/*************************************************************/
/*           Оперделение первого документа в связке          */
/*************************************************************/
macro IsFirstLog

  var ret_flag       = false;
  var stat;
  var kind_main      = 0;
  var key_main       = "";
  var key_current    = "";
  var numDprt        = String( ALG_NFOPR.numDprt );
  var recID          = String( ALG_NFOPR.recID );

  while ( StrLen( numDprt ) < 5 )
    numDprt = "0" + numDprt;
  end;
  while ( StrLen( recID ) < 10 )
    recID = "0" + recID;
  end;
  key_current = "rtnfopr#" + numDprt + recID + "00";

  sb_casln.KeyNum = 1;
  sb_casln.rec.ApplicationKind2 = 3001;
  sb_casln.rec.ApplicationKey2  = key_current;
  sb_casln.rec.RefValue2        = 0;

  stat = sb_casln.GetEQ( );

  if ( stat )
    ;
  else
    numDprt = String( ALG_NFOPR.numDprt );

    while ( StrLen( numDprt ) < 3 )
      numDprt = "0" + numDprt;
    end;

    key_current = "rtnfopr#" + numDprt + recID + "00";

    sb_casln.KeyNum = 1;
    sb_casln.rec.ApplicationKind2 = 3001;
    sb_casln.rec.ApplicationKey2  = key_current;
    sb_casln.rec.RefValue2        = 0;

    stat = sb_casln.GetEQ( );
  end;

  if ( stat )
    kind_main = sb_casln.rec.ApplicationKind1;
    key_main  = sb_casln.rec.ApplicationKey1;

    sb_casln.KeyNum = 0;
    sb_casln.rec.ApplicationKind1 = kind_main;
    sb_casln.rec.ApplicationKey1  = key_main;
    sb_casln.rec.NumLinkDoc       = 0;

    stat = sb_casln.GetGE();
    while ( stat )
      if ( ( sb_casln.rec.ApplicationKind1 == kind_main ) and
           ( sb_casln.rec.ApplicationKey1  == key_main  )    )
        if ( sb_casln.rec.ApplicationKind2 == 3001 )
          if ( sb_casln.rec.ApplicationKey2 == key_current )
            ret_flag = true;
          end;
          stat = false;
        else
          stat = sb_casln.Next();
        end;
      else
        stat = false;
      end;
    end;
  end;

  return ret_flag;

end;

/*************************************************************/
/*  Печать отчета. Подготовка данных для DDR_Decl_Report().  */
/*************************************************************/
macro Print_Report()
 var pID, pFIO, pCodClnt, pStandart, pPeriod, pControlInfo;

 ClearRecord( op_obj );
 op_obj.OpAppKind = ALG_RET_OP.ApplicationKind;
 op_obj.OpAppKey  = ALG_RET_OP.ApplicationKey;

 if ( GetGE( op_obj )  AND
      ( op_obj.OpAppKind == ALG_RET_OP.ApplicationKind )  AND
      ( op_obj.OpAppKey  == ALG_RET_OP.ApplicationKey ) )
   pID = Int( op_obj.ObjectRef );
 else
   println( "Запись связи операция->объект не найдена. ApplicationKey = ",
            ALG_RET_OP.ApplicationKey );
   Exit( 0 );
 end;

 ClearRecord( decl );
 decl.ID = pID;
 if ( NOT GetEQ( decl ) )
   println( "Не найдена запись заявления с ID = ", pID );
   Exit( 0 );
 end;

 DDR_Decl_Report();
end;

/**************************************************************/
/*  Головная процедура: выделение первого документа в связке  */
/**************************************************************/
 ALG_RESULT = OK_RES;

 if ( IsFirstLog()  AND
      ( get_ID_FileNum( ALG_NFOPR.objectID ) == LogSC_DDRDecl ) )
     Print_Report();
 else
   Exit( 1 );
 end;

end;
