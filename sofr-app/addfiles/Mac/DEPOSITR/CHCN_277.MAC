/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Прочие расходные операции                                               *
*                                                                          *
*  Проведение операции по выплате вкладов бывшего ЧСБ                      *
*                                                                          *
*  Лебедев С.В.                                                            *
*  18.02.2004                                                              *
\**************************************************************************/

import DeprIntr, issrvdoc, sqlconv;

record OC_PAY_DOC  ( "pay_doc.dbt"  );
record OC_PAY_KIND ( "pay_kind.dbt" );

private var sb_casln = TBFile( "sb_casln.dbt", "r", 0 );
private var sbdepdoc = TBFile( "sbdepdoc.dbt", "r", 3 );
private var sb_dtyp  = TBFile( "sb_dtyp.dbt",  "r", 2 );
private var chcn_pay = TBFile( "chcn_pay.dbt", "r", 1 );
private var chcnrgst = TBFile( "chcnrgst.dbt", "r", 0 );
private var currency = TBFile( "currency.dbt", "r", 0 );

var DepClient = TClientList;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro IsOperPaymentChcnDep ( recDoc )

  var retValue = false;

  if ( ( recDoc.TypeComplexOper ==  4 ) or  // Расход
       ( recDoc.TypeComplexOper == 63 ) or  // Списание по разовому поручению
       ( recDoc.TypeComplexOper == 64 ) or  // Списание по длительному поручению
       ( recDoc.TypeComplexOper == 65 ) or  // Списание переводом вклада
       ( recDoc.TypeComplexOper == 66 ) or  // Списание в свой филиал
       ( recDoc.TypeComplexOper == 67 ) or  // Списание в чужой филиал
       ( recDoc.TypeComplexOper == 80 ) or  // Конвертация валюты
       ( recDoc.TypeComplexOper == 10 ) or  // Закрытие наличными
       ( recDoc.TypeComplexOper == 81 ) or  // Закрытие безналичными
       ( recDoc.TypeComplexOper == 96 )   ) // Закрытие переводом
    if ( (   recDoc.TypeOper  ==  4      ) or // Расход
         (   recDoc.TypeOper  == 63      ) or // Списание по разовому поручению
         (   recDoc.TypeOper  == 64      ) or // Списание по длительному поручению
         ( ( recDoc.TypeOper  == 65 ) and     // Списание переводом вклада
           ( recDoc.KindOp    !=  5 )    ) or
         (   recDoc.TypeOper  == 66      ) or // Списание в свой филиал
         (   recDoc.TypeOper  == 67      ) or // Списание в чужой филиал
         ( ( recDoc.TypeOper  == 80 ) and     // Конвертация валюты
           ( recDoc.KindOp    !=  5 )    )   )
      retValue = true;
    end;
  end;

  return retValue;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindRecsInBase

  var applKind        = 0;
  var applKey         = "";
  var retValue        = false;
  var stat;
  var wasFindDepDoc   = false;
  var wasFindChcnRgst = false;

  sb_casln.KeyNum = 1;
  sb_casln.Clear;
  sb_casln.rec.ApplicationKind2 = OC_PAY_DOC.iApplicationKind;
  sb_casln.rec.ApplicationKey2  = OC_PAY_DOC.ApplicationKey;
  sb_casln.addFilter( "t.t_ApplicationKind2 = " + String( OC_PAY_DOC.iApplicationKind ) + " and " +
                      "t.t_ApplicationKey2 = " + GetSQLString( OC_PAY_DOC.ApplicationKey )         );

  if (   sb_casln.GetGE                              and
       ( sb_casln.rec.ApplicationKind2 == OC_PAY_DOC.iApplicationKind ) and
       ( sb_casln.rec.ApplicationKey2  == OC_PAY_DOC.ApplicationKey   )    )
    applKind = sb_casln.rec.ApplicationKind1;
    applKey = sb_casln.rec.ApplicationKey1;
  end;
  sb_casln.dropFilter;

  if ( applKey != "" )

    sb_casln.KeyNum = 0;
    sb_casln.Clear;
    sb_casln.rec.ApplicationKind1 = applKind;
    sb_casln.rec.ApplicationKey1  = applKey;
    sb_casln.addFilter( "t.t_ApplicationKind1 = " + String( applKind ) + " and " +
                        "t.t_ApplicationKey1 = " + GetSQLString( applKey )        );

    stat = sb_casln.GetGE;
    while ( stat )
      if ( ( sb_casln.rec.ApplicationKind1 == applKind ) and
           ( sb_casln.rec.ApplicationKey1  == applKey  )    )
        if ( sb_casln.rec.ApplicationKind2 == 1 ) // Вкладной документ
          sbdepdoc.Clear;
          sbdepdoc.rec.iApplicationKind = sb_casln.rec.ApplicationKind2;
          sbdepdoc.rec.ApplicationKey   = sb_casln.rec.ApplicationKey2;

          if ( sbdepdoc.GetEQ )
            if ( IsServDoc( sbdepdoc.rec ) and ( sbdepdoc.rec.FlagStorn == "" ) and 
                 IsOperPaymentChcnDep( sbdepdoc.rec )                              )
              wasFindDepDoc = true;
            end;
          end;
        end;
        if ( wasFindDepDoc )
          stat = false;
        else
          stat = sb_casln.Next;
        end;
      else
        stat = false;
      end;
    end;
    sb_casln.dropFilter;
  end;

  if ( wasFindDepDoc )
    chcn_pay.Clear;
    chcn_pay.rec.iApplicationKind = sbdepdoc.rec.iApplicationKind;
    chcn_pay.rec.ApplicationKey   = sbdepdoc.rec.ApplicationKey;

    if ( chcn_pay.GetEQ )
      chcnrgst.Clear;
      chcnrgst.rec.RgtpID = chcn_pay.rec.RgtpID;
      chcnrgst.rec.RgstID = chcn_pay.rec.RgstID;

      wasFindChcnRgst = chcnrgst.GetEQ;
    end;
  end;

  if ( wasFindDepDoc and wasFindChcnRgst )
    retValue = true;
  end;

  return retValue;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DenomSum ( Sum )

  if ( Sum < 0 )
    return -MoneyL( Floor( DoubleL( -Sum * 100 ) / 1000 + 0.5 ) ) / 100;
  else
    return MoneyL( Floor( DoubleL( Sum * 100 ) / 1000 + 0.5 ) ) / 100;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro PrintForm277

  var numAccount     = "";
  var nameClient     = "";
  var partRest       = DenomSum( chcn_pay.rec.PartRest );
  var partPercent    = chcn_pay.rec.PartPercent - chcn_pay.rec.PartTax;
  var partCompens    = chcn_pay.rec.PartCompens1 + chcn_pay.rec.PartCompens2;
  var partCompens3x  = chcn_pay.rec.PartCompens3x;
  var partTotal      = partRest + partPercent + partCompens + partCompens3x;
  var sumStr         = RubToStr( partTotal );
  var sPartCompens3x = "";
  var sAccCompens3x  = "";

  if ( chcnrgst.rec.Account != "" )
    numAccount = chcnrgst.rec.Account;
  else
    numAccount = chcnrgst.rec.RgstAccount;
  end;

  if ( DepClient.GetRecord( sbdepdoc.rec.CodClient ) )
    if ( Trim( DepClient.CurRec.rec.Name1 ) != "" )
      nameClient = Trim( DepClient.CurRec.rec.Name1 );
      if ( Trim( DepClient.CurRec.rec.Name2 ) != "" )
        nameClient = nameClient + " " + SubStr( Trim( DepClient.CurRec.rec.Name2 ), 1, 1 ) + ".";
        if ( Trim( DepClient.CurRec.rec.Name3 ) != "" )
          nameClient = nameClient + SubStr( Trim( DepClient.CurRec.rec.Name3 ), 1, 1 ) + "." ;
        end;
      end;
    end;
  end;

  if ( sbdepdoc.rec.Code_Currency > 0 )
    currency.Clear;
    currency.rec.Code_Currency = sbdepdoc.rec.Code_Currency;
    if ( currency.GetEQ and ( Int( currency.rec.ExternalCode ) > 0 ) )
      sumStr = CurToStrAlt( partTotal, NULL, NULL, Int( currency.rec.ExternalCode ) );
    end;
  end;

  if ( chcn_pay.rec.PartCompens3x > 0 )
    sPartCompens3x = String( chcn_pay.rec.PartCompens3x:18:2:a );
    sAccCompens3x  = "40313.03";
  end;

[
                          МЕМОРИАЛЬНЫЙ НОМЕР №                     ф.277

                             за ______________

   Выплата средств по счету _#_______________________ вкладчику бывшего 

 Чеченского банка Сбербанка России _#______________________ (ФИО Вкладчика)

 |-------------------------|-------------------------|--------------------|
 |       Дебет счета       |      Кредит счета       |       Сумма        |
 |-------------------------|-------------------------|--------------------|
 |   47422.99              |   30220                 | ################## |
 |-------------------------|-------------------------|--------------------|
 |   70203                 |                         | ################## |
 |-------------------------|-------------------------|--------------------|
 |   40313.03              |                         | ################## |
 |-------------------------|-------------------------|--------------------|
 |   #                     |                         |                  # |
 |-------------------------|-------------------------|--------------------|
 |                         |                         |                    |
 |-------------------------|-------------------------|--------------------|
 |                         |                         |                    |
 |-------------------------|-------------------------|--------------------|
 |                         |                         |                    |
 |-------------------------|-------------------------|--------------------|
 |                         |                         |                    |
 |-------------------------|-------------------------|--------------------|
 | Итого:                  |                         | ################## |
 |-------------------------|-------------------------|--------------------|


  Сумма #

  Приложение на ___ листах

  ______________________________________________________
  Подписи                                  Отметка банка

  ______________________________________________________


  ______________________________________________________
]
  ( Acc_Form(numAccount), nameClient,
    partRest:18:2:a, partPercent:18:2:a, partCompens:18:2:a,
    sAccCompens3x, sPartCompens3x:r,
    partTotal:18:2:a,
    sumStr                                                                    );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro PrintInquire

  array aNameClient;

  var nameClient       = "";
  var numberChcnDepart = Trim( chcnrgst.rec.ChcnDepart ) + "/" + Trim( chcnrgst.rec.ChcnBranch );
  var kindAccount      = "";
  var numAccount       = "";
  var nameBranch       = "";
  var dep_name;
  var partPercent      = chcn_pay.rec.PartPercent;
  var partCompens3x    = chcn_pay.rec.PartCompens3x;
  var partCompens      = chcn_pay.rec.PartCompens1 + chcn_pay.rec.PartCompens2;
  var partTax          = chcn_pay.rec.PartTax;
  var partTotal        = sbdepdoc.rec.OutSum - chcn_pay.rec.PartTax;

  if ( DepClient.GetRecord( sbdepdoc.rec.CodClient ) )
    if ( Trim( DepClient.CurRec.rec.Name1 ) != "" )
      nameClient = Trim( DepClient.CurRec.rec.Name1 );
      if ( Trim( DepClient.CurRec.rec.Name2 ) != "" )
        nameClient = nameClient + " " + Trim( DepClient.CurRec.rec.Name2 );
        if ( Trim( DepClient.CurRec.rec.Name3 ) != "" )
          nameClient = nameClient + " " + Trim( DepClient.CurRec.rec.Name3 );
        end;
      end;
    end;
  end;
  StrSplit( nameClient, aNameClient, 33, 33, 2 );

  sb_dtyp.Clear;
  sb_dtyp.rec.FlagCur = sbdepdoc.rec.IsCur;
  sb_dtyp.rec.Kind    = sbdepdoc.rec.Type_Account;
  if ( sb_dtyp.GetEQ )
    kindAccount = sb_dtyp.rec.Name;
  else
    kindAccount = sbdepdoc.rec.Type_Account;
  end;

  if ( chcnrgst.rec.Account != "" )
    numAccount = chcnrgst.rec.Account;
  else
    numAccount = chcnrgst.rec.RgstAccount;
  end;

  if ( findDepartment(sbdepdoc.rec.FNCash, dep_name) )
    nameBranch = Trim( dep_name );
  end;
  if ( nameBranch == "" );
    nameBranch = "_";
  end;

[
                                 Справка

                    о произведенных выплатах по вкладу,
            внесенному в бывший Чеченский банк Сбербанка России

 |-----------------------------------|-----------------------------------|
 | Фамилия, имя, отчество вкладчика  | ################################# |
 |                                   | ################################# |
 |-----------------------------------|-----------------------------------|
 | Номер структурного подразделения  | #                                 |
 | Чеченского банка Сбербанка России |                                   |
 |-----------------------------------|-----------------------------------|
 | Вид вклада                        | ################################# |
 |-----------------------------------|-----------------------------------|
 | № счета                           | #                                 |
 |-----------------------------------|-----------------------------------|
 | Остаток вклада на 20.06.1991      |              #################### |
 |-----------------------------------|-----------------------------------|
 | Рассчитанная сумма (руб.коп.)     |                                   |
 |-----------------------------------|-----------------------------------|
 |       Вклада                      |              #################### |
 |-----------------------------------|-----------------------------------|
 |       причисленных процентов      |              #################### |
 |-----------------------------------|-----------------------------------|
 |       трехкратной компенсации     |              #################### |
 |-----------------------------------|-----------------------------------|
 |       предварительной компенсации |              #################### |
 |-----------------------------------|-----------------------------------|
 | Удержана сумма налога с дохода    |              #################### |
 | по вкладу                         |                                   |
 |-----------------------------------|-----------------------------------|
 | ИТОГО выплачено (руб.коп.)        |              #################### |
 |-----------------------------------|-----------------------------------|


  ________________     № _#______________________________________________
        Дата             Структурное подразделение, осуществившее выплату


  _________________  _________________  /_______________________________/
      Должность           Подпись            Ф.И.О. сотрудника банка
]
  ( aNameClient( 0 ), aNameClient( 1 ), numberChcnDepart, kindAccount, Acc_Form(numAccount),
    chcnrgst.rec.Rest200691:20:2:a, sbdepdoc.rec.OutSum:20:2:a, 
    partPercent:20:2:a, partCompens3x:20:2:a, partCompens:20:2:a, partTax:20:2:a,
    partTotal:20:2:a, nameBranch                                                  );
 
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  if ( OC_PAY_DOC.ApplType != 6 )
    Exit( 1 );
  end;

  if ( not FindRecsInBase( ) )
    Exit( 1 );
  end;

  // Безналичная операция - печать формы 277
  if ( OC_PAY_DOC.NumOpert == 44 )
    PrintForm277( );
    [#]( StrFor( 12 ) );
  end;

  // Печать справки о произведенных выплатах
  PrintInquire( );
  [#]( StrFor( 12 ) );
  PrintInquire( );

end;
