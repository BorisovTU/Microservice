//
//
//

import Rep_Page, Acc_Form;

//  Класс формы 210.
class TGroupForm210

  private var vPaginator : object;

  private var vKind = "";

  private var vSumPage  = $0;
  private var vSumTotal = $0;

  // Начало страницы.
  macro onPageStart()
    [ --- Список-ордер формы 210 ----------------- ########## ------ ######## --- #

       Сбербанк России ОАО                                      Ф. N 210-c
       ___________________

                         Мемориальный список-ордер N _____
                           "___"_______________ 20__г.

                                                                   Лист #

       Назначение платежа (содержание операции) #
       +---------------------------------------------------------------------------------------------------+
       | N п/п |     Дебет счета N    |     Кредит счета N   |           Сумма           |    Примечание   |
       |       |                      |                      |------------+--------------| (дополнительные |
       |       |                      |                      | в номинале |   в рублях/  |    реквизиты)   |
       |       |                      |                      | иностранной|   рублевом   |                 |
       |       |                      |                      |   валюты   |  эквиваленте |                 |
       |-------+----------------------+----------------------+------------+--------------+-----------------|]
    ( Date(), Time(), vPaginator.getPageNo(), vPaginator.getPageNo(), vKind );
  end;

  // Конец страницы.
  macro onPageEnd()
    [
       Подписи:
                ______________________

                ______________________

      ------ RS-Bank v.6 --------------------------------------- RS-Retail --
    ];
  end;

  // Начать печать формы (напечатать шапку формы).
  macro beginForm( pKind )
    vKind = pKind;
  end;

  // Завершить печать формы (напечатать подвал формы с итогами).
  macro endForm

    if ( NOT vPaginator.addLines( 5 ) )
      vPaginator.addEmpty();
    end;

    [  |-------+----------------------+----------------------+------------+--------------+-----------------|
       |  Итого по листу:                                    |            | ############ |                 |
       |---------------------------------------------------------------------------------------------------|
       |  Итого:                                             |            | ############ |                 |
       |---------------------------------------------------------------------------------------------------|]
    ( vSumPage:12:2, vSumTotal:12:2 );
    vSumPage  = $0;
    vSumTotal = $0;
    vPaginator.Finish();
  end;

  // Напечатать строку отчета.
  macro putLine( pNum:integer, pAcc:string, pSum:money )
    vPaginator.addLine();
    [  | ##### | #################### |                      |            | ############ |                 |]
    ( pNum:5, Acc_Form( pAcc ), pSum:12:2 );
    vSumPage  = vSumPage  + pSum;
    vSumTotal = vSumTotal + pSum;
    if ( vPaginator.getLineNo() == vPaginator.getLinesPerPage() - 10 )
      vPaginator.addLine();
      vPaginator.addLine();
      vPaginator.addLine();
      [  |-------+----------------------+----------------------+------------+--------------+-----------------|
         |  Итого по листу:                                    |            | ############ |                 |
         |---------------------------------------------------------------------------------------------------|]
      ( vSumPage:12:2 );
      vSumPage = $0;
    end;
  end;

  macro beginPart( id : string, text : string )
    beginForm( text );
    vPaginator.newReport();
  end;

  macro endPart( id : string, putTotals : bool, totalsLabel : string )
    endForm();
    println( cFormFeed );  // Перевод страницы.
  end;

  //
  // Конструктор класса TGroupForm.
  // Начальная инициализация свойств класса.
  //

  vPaginator = TPaginator( this, 18, 7 );

end;
