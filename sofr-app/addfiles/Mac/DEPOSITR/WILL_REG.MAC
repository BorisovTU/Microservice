/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Книга регистрации завещательных распоряжений ( ф. N 310 )

*****************************************************************/

 import DeprIntr, rsd;

 file opr  ( person   ) key 0;
 file tpac ( typeac   ) key 0;
 file acc  ( depositr ) key 8;
 file wreg ( rtwillrg ) key 0;

 var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
 var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

 var DepClnt = TClientList;  /* Справочник вкладчиков */

 var FNCash = NumFNCash();

 var ErrCodeWR, ErrTextWR;

 var stat;

 /*************************************************************************\
 |        Получение наименования и номера структурного подразделения.      |
 \*************************************************************************/
 macro get_Name_Branch()
   var sBranch = "";
   var ddep = TRecHandler( "i_dp_dep.rec" );
   var party = TRecHandler( "i_party.rec" );
   
   if ( findDepartment(FNCash, null, ddep, party) )
     sBranch = "\"" + party.rec.Name + "\"  " + ddep.rec.Name;
   else
     sBranch = "?????";
     println( " *** Ошибка при чтении номера структурного подразделения с FNCash = ",
              FNCash );
     ErrCodeWR = Status( ErrTextWR );
     println( "  Ошибка " + String( ErrCodeWR ) + ": " + ErrTextWR );
   end;
   return sBranch;
 end;

 /*************************************************************************\
 |               Получение даты оформления/изменения завещания.            |
 \*************************************************************************/
 macro get_Will_Date()
   var sDate = "";
   sDate = "  " + wreg.Date;
   return sDate;
 end;

 /*************************************************************************\
 |                          Получение ФИО клиента.                         |
 \*************************************************************************/
 macro get_FIO_Client()
   var SpecialAccount = "";
   var cmd, rs;
   cmd = RSDCommand("select dep.t_SpecialAccess as SpecialAccess from ddepositr_dbt dep where dep.t_Referenc = "+String(wreg.AccRef));
   cmd.Execute;
   rs = RSDRecordset(cmd);

   if(rs.MoveNext)
      SpecialAccount = rs.value("SpecialAccess");
   end;
   var sFIO = "";
   if ( DepClnt.GetRecord( wreg.ClientCode ) )
      if(  (not SpecialAccess) and OperMayListSpecialAccounts and SpecialAccount)
        sFIO = "XXXXXX X.X.";
      else      
         sFIO = DepClnt.CurRec.rec.Name1 + " " +
                DepClnt.CurRec.rec.Name2 + " " +
                DepClnt.CurRec.rec.Name3;
      end;   
   else
     sFIO = "?????";
     println( " *** Не найдена запись вкладчика с CodClient = ", wreg.ClientCode );
   end;
   return sFIO;
 end;

 /*************************************************************************\
 |                      Получение номера счета вкладчика.                  |
 \*************************************************************************/
 macro get_Account()
   var sAcc = "";
   ReWind( acc );
   ClearRecord( acc );
   acc.Referenc = wreg.AccRef;
   if ( getEQ( acc ) )
     sAcc = acc.Account;
   else
     sAcc = "?????";
     println( " *** Ошибка при чтении номера счета по Referenc = ", wreg.AccRef );
     ErrCodeWR = Status( ErrTextWR );
     println( "  Ошибка " + String( ErrCodeWR ) + ": " + ErrTextWR );
   end;
   return sAcc;
 end;

 /*************************************************************************\
 |                     Получение должности операциониста.                  |
 \*************************************************************************/
 macro GetNameTypePerson()
   var sWork = "";
   ReWind( tpac );
   ClearRecord( tpac );
   tpac.iNumType = 14;  /* Уровни доступов. */
   tpac.Type_Account = opr.cTypePerson;
   if ( GetEQ( tpac ) )
     sWork = tpac.Name_Type;
   else
     sWork = "?????";
     println( " *** Ошибка при чтении должности операциониста с кодом = ", opr.cTypePerson );
     ErrCodeWR = Status( ErrTextWR );
     println( "  Ошибка " + String( ErrCodeWR ) + ": " + ErrTextWR );
   end;
   return sWork;
 end;

 /*************************************************************************\
 |                   Получение ФИО, должности операциониста.               |
 \*************************************************************************/
 macro get_FIO_Oper()
   var sOper = "";
   ReWind( opr );
   ClearRecord( opr );
   opr.Oper = wreg.Oper;
   if ( GetEQ( opr ) )
     sOper = opr.Name + ", " + GetNameTypePerson();
   else
     sOper = "?????";
     println( " *** Ошибка при чтении записи по операционисту с кодом = ", wreg.Oper );
     ErrCodeWR = Status( ErrTextWR );
     println( "  Ошибка " + String( ErrCodeWR ) + ": " + ErrTextWR );
   end;
   return sOper;
 end;

 /*************************************************************************\
 |                            Основная процедура.                          |
 \*************************************************************************/

 [
                                                                                                                                     Ф. N 310

                                                   КНИГА РЕГИСТРАЦИИ ЗАВЕЩАТЕЛЬНЫХ РАСПОРЯЖЕНИЙ

   ###########################################################################################################################################

   |----------+----------------+--------------------------+--------------------------------+------------------------+------------------------|
   | Регистра |      Дата      |      Фамилия, имя и      |        Номера счетов, по       |     ФИО, должность     |       Примечание       |
   | ционный  |   совершения   |    отчество вкладчика    |             которым            |       работника,       |                        |
   |   номер  |   (изменения/  |                          |            составлено          |     удостоверившего    |                        |
   |          |     отмены)    |                          |          завещательное         |      завещательное     |                        |
   |          | завещательного |                          |           распоряжение         |       распоряжение     |                        |
   |          |  распоряжения  |                          |                                |                        |                        |
   |----------+----------------+--------------------------+--------------------------------+------------------------+------------------------|
   |     1    |        2       |             3            |                 4              |             5          |            6           |
   |----------+----------------+--------------------------+--------------------------------+------------------------+------------------------|]
 (
   get_Name_Branch():c
 );

 ReWind( wreg );
 ClearRecord( wreg );

 wreg.Branch = FNCash;

 stat = GetGE( wreg );

 while ( stat  AND  ( wreg.Branch == FNCash ) )

   [ | ######## | ############## | ######################## | ############################## | ###################### | ###################### |]
   (
     wreg.Number:8,
     get_Will_Date():12,
     get_FIO_Client():w:20,
     Acc_Form(get_Account()):f:30,
     get_FIO_Oper():w:20,
     wreg.Comment:w:20
   );

   [ |----------+----------------+--------------------------+--------------------------------+------------------------+------------------------|];

   stat = Next( wreg );

 end;
