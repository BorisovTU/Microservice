/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Операция по утере сберкнижки                                            *
*                                                                          *
*  Панель операции по утере сберкнижки                                     *
*                                                                          *
\**************************************************************************/

 import DeposInter, Acc_Form;

 file fnc ( listfdep ) key 0;
 file dep ( depositr ) key 8;

 record dlg_lstb ( "LOSTBOOK" ) dialog;

 /* Коды клавиш. */
 const KEY_TAB   =   9;  /* Код нажатия клавиши Tab. */
 const KEY_ENT   =  13;  /* Код нажатия клавиши Enter. */
 const KEY_ESC   =  27;  /* Код нажатия клавиши Esc. */
 const KEY_SPACE =  32;  /* Код нажатия клавиши "Пробел". */
 const KEY_F2    = 316;  /* Код нажатия клавиши F2. */
 const KEY_F3    = 317;  /* Код нажатия клавиши F3. */
 const KEY_F9    = 323;  /* Код нажатия клавиши F9. */
 const KEY_UP    = 328;  /* Код нажатия клавиши Up. */
 const KEY_LEFT  = 331;  /* Код нажатия клавиши Left. */
 const KEY_RIGHT = 333;  /* Код нажатия клавиши Right */
 const KEY_DOWN  = 336;  /* Код нажатия клавиши Down. */
 const KEY_CTRLLEFT  = 371;  /* Код нажатия клавиши Ctrl+Left. */
 const KEY_CTRLRIGHT = 372;  /* Код нажатия клавиши Ctrl+Right */

 const DateZero = Date( 0, 0, 0 );

 /* Значения, устанавливаемые в панели операции "Утеря сбер.книжки". */
 var panDateReg  = DateZero;
 var panCloseNal = FALSE;
 var panNewAcc   = FALSE;
 var panKindAcc  = "";
 var panAcc      = "";
 var panRef      = 0;
 var panDubl     = FALSE;
 var panEscape   = FALSE;

 var cCurCode = 0;
 var cClientCode = 0;

 var ErrCodeLB, ErrTextLB;

/****************************************************************/
/*                    Поиск счета по номеру.                    */
/****************************************************************/
macro FindAccount( pAcc )
 var nBranch = 1;
 var vFind = FALSE;
 KeyNum( dep, 7 );
 ReWind( fnc );
 while ( ( NOT vFind )  AND  Next( fnc ) )
   ClearRecord( dep );
   dep.FNCash  = fnc.FNCash;
   dep.Account = pAcc;
   if ( GetEQ( dep ) )
     vFind = TRUE;
   end;
 end;
 if ( vFind )
    return dep.Referenc;
 else
   return 0;
 end;
end;

/****************************************************************/
/*                   Чтение записи счета.                       */
/****************************************************************/
macro getAccount( pRef )
 var sAcc = "";
 var sStr = "";
 KeyNum( dep, 8 );
 ClearRecord( dep );
 dep.Referenc = pRef;
 if ( GetEQ( dep ) )
   sAcc = dep.Account;
 else
   sStr = "Не найдена запись счета с Reference = " + String( pRef ) + "|";
   ErrCodeLB = Status( ErrTextLB );
   sStr = sStr + "  Ошибка " + String( ErrCodeLB ) + ": " + ErrTextLB;
   MsgBox( sStr );
 end;
 return sAcc;
end;

/****************************************************************/
/*                   Обработка событий панели.                  */
/****************************************************************/
macro EventProcPr( dlg, cmd, id, key )

 var vKindAcc = "";
 var vAccRef = 0;

 if ( cmd == DLG_KEY )
   /* msgbox(key); */
   if ( ( key == KEY_TAB       )  OR
        ( key == KEY_ENT       )  OR
        ( key == KEY_UP        )  OR
        ( key == KEY_LEFT      )  OR
        ( key == KEY_RIGHT     )  OR
        ( key == KEY_DOWN      )  OR
        ( key == KEY_CTRLLEFT  )  OR
        ( key == KEY_CTRLRIGHT ) )
     if ( StrLen( dlg.Acc ) > 0 )
       if ( (vAccRef=FindAccount( dlg.Acc ))>0 )
         panRef  = vAccRef;
         /* Отмена альтенатив. */
         dlg.CloseNal = "";
         dlg.NewAcc   = "";
         dlg.KindAcc  = "";
         dlg.Dubl     = "";
         UpdateFields( dlg );
       else
         MsgBox( "Счет " + Acc_Form(dlg.Acc) + " не найден." );
         SetFocus( dlg, 4 );
         return CM_IGNORE;
       end;
     end;
   elif ( key == KEY_F2 )
     if ( StrLen( dlg.Acc )>0 )
        if ( (vAccRef=FindAccount( dlg.Acc ))>0 )
          panRef  = vAccRef;
          /* Отмена альтенатив. */
          dlg.CloseNal = "";
          dlg.NewAcc   = "";
          dlg.KindAcc  = "";
          dlg.Dubl     = "";
          UpdateFields( dlg );
        else
          MsgBox( "Счет " + Acc_Form(dlg.Acc) + " не найден." );
          SetFocus( dlg, 4 );
          return CM_IGNORE;
        end;
     end;

     if ( dlg.DateReg == Date( 0, 0, 0 ) )
       MsgBox( "Дата не должна быть нулевой!" );
       SetFocus( dlg, 0 );
       return CM_IGNORE;
     end;
     if ( ( StrLen( dlg.CloseNal ) == 0 )  AND
          ( StrLen( dlg.NewAcc   ) == 0 )  AND
          ( StrLen( dlg.KindAcc  ) == 0 )  AND
          ( StrLen( dlg.Acc      ) == 0 )  AND
          ( StrLen( dlg.Dubl     ) == 0 ) )
       MsgBox( "Выберите вид обработки заявления." );
       SetFocus( dlg, 1 );
       return CM_IGNORE;
     end;
     if ( ( StrLen( dlg.NewAcc   ) != 0 )  AND
          ( StrLen( dlg.KindAcc  ) == 0 ) )
       MsgBox( "Выберите вид вклада." );
       SetFocus( dlg, 3 );
       return CM_IGNORE;
     end;
     return CM_SAVE;
   elif ( key == KEY_F3 )
     if ( id == 3 )
       if ( dlg.NewAcc == "X" )
         if ( SelectDepType( vKindAcc ) )
           dlg.KindAcc = vKindAcc;
         end;
       end;
     end;
     if ( id == 4 )
       if ( SelectAccount( "", cCurCode, cClientCode, vAccRef, 0 ) )
         dlg.Acc = getAccount( vAccRef );
         panRef  = vAccRef;
         /* Отмена альтенатив. */
         dlg.CloseNal = "";
         dlg.NewAcc   = "";
         dlg.KindAcc  = "";
         dlg.Dubl     = "";
         UpdateFields( dlg );
       end;
     end;
   elif ( key == KEY_F9 )
     return CM_IGNORE;
   elif ( key == KEY_ESC )
     panEscape = TRUE;
     return CM_SAVE;
   elif ( key == KEY_SPACE )
     /* Закрыть наличными. */
     if ( id == 1 )
       if ( dlg.CloseNal == "X" )
         dlg.CloseNal = "";
       else
         dlg.CloseNal = "X";
         /* Отмена альтенатив. */
         dlg.NewAcc  = "";
         dlg.KindAcc = "";
         dlg.Acc     = "";
         dlg.Dubl    = "";
       end;
     end;
     /* Перечислить на НОВЫЙ счет. */
     if ( id == 2 )
       if ( dlg.NewAcc == "X" )
         dlg.NewAcc  = "";
         dlg.KindAcc = "";
       else
         dlg.NewAcc = "X";
         /* Отмена альтенатив. */
         dlg.CloseNal = "";
         dlg.Acc      = "";
         dlg.Dubl     = "";
       end;
     end;
     /* Выдать дубликат сб\кн. */
     if ( id == 5 )
       if ( dlg.Dubl == "X" )
         dlg.Dubl = "";
       else
         dlg.Dubl = "X";
         /* Отмена альтенатив. */
         dlg.CloseNal = "";
         dlg.NewAcc   = "";
         dlg.KindAcc  = "";
         dlg.Acc      = "";
       end;
     end;
     UpdateFields( dlg );
   end;
 end;

end;

/****************************************************************/
/*              Панель операции "Утеря сбер.книжки".            */
/****************************************************************/
macro PanelLostBook( pCurCode, pClientCode )

 cCurCode    = pCurCode;
 cClientCode = pClientCode;

 dlg_lstb.DateReg  = {curdate};
 dlg_lstb.CloseNal = "";
 dlg_lstb.NewAcc   = "";
 dlg_lstb.KindAcc  = "";
 dlg_lstb.Acc      = "";
 dlg_lstb.Dubl     = "";

 panEscape = FALSE;

 /* Работа с панелью */
 RunDialog( dlg_lstb, "EventProcPr" );

 /* Значения, устанавливаемые в панели операции "Утеря сбер.книжки". */
 panDateReg  = dlg_lstb.DateReg;
 panKindAcc  = dlg_lstb.KindAcc;
 panAcc      = dlg_lstb.Acc;

 if ( StrLen( dlg_lstb.CloseNal ) == 0 )
   panCloseNal = FALSE;
 else
   panCloseNal = TRUE;
 end;
 if ( StrLen( dlg_lstb.NewAcc ) == 0 )
   panNewAcc = FALSE;
 else
   panNewAcc = TRUE;
 end;
 if ( StrLen( dlg_lstb.Dubl ) == 0 )
   panDubl = FALSE;
 else
   panDubl = TRUE;
 end;

end;

/* PanelLostBook( 0, 8093 ); */

