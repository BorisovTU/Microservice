/**************************************************************************\
*  --------------                                                          *
*  R-Style Soflab                                                          *
*  --------------                                                          *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Операция по утере сберкнижки                                            *
*                                                                          *
*  Лебедев С.В.                                                            *
*  12.07.2000                                                              *
\**************************************************************************/

import DeprIntr, cur_rate, alg_vars;

file depositr   ( depositr     );
file currency   ( currency     ) key 0;
file tarif      ( sb_tarif     ) key 0;
file tarif_plus ( sb_tarpl     ) key 0;
file msg        ( "sbbank.msg" ) key 0;

var depclnt = TClientList;

const UPDATE_ALL  = 1;
const UPDATE_DEP  = 2;
const UPDATE_DOC  = 4;

const TarifSbook = 17;  /* Код тарифа за утерю сберкнижки */

var ComisSum  = $0L;
var MinRest   = $100L;
var ScaleRate = 1;
var Rate      = $0.0L;
var ErrCode62 = 0;


/**************************************************************************\
|                               Поиск тарифа                               |
\**************************************************************************/
macro FindTarif( isCur, Num, _Date, _Sum )

  var stat;

  tarif.IsCur = isCur;
  tarif.NumTarif = Num;

  stat = GetEQ(tarif);

  if ( stat  AND  ( tarif.TarifPlus ) )
    tarif_plus.IsCur    = isCur;
    tarif_plus.NumTarif = Num;
    tarif_plus.Date     = _Date;
    tarif_plus.SumOper  = _Sum;
    if ( GetGE( tarif_plus )           AND
         ( tarif_plus.IsCur    == 0 )  AND
         ( tarif_plus.NumTarif == Num ) AND
         ( tarif_plus.Date     == _Date ) )
      tarif.PerSent = tarif_plus.Percent;
      tarif.MinSum  = tarif_plus.MinSum;
      tarif.MaxSum  = tarif_plus.MaxSum;
      tarif.Summa   = tarif_plus.Summa;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro NewDepDoc ( Doc )

  ClearRecord(Doc);
  Doc.Referenc         = ALG_DEPOSITOR.Referenc;
  Doc.IsCur            = ALG_DEPOSITOR.IsCur;
  Doc.FNCash           = ALG_DEPOSITOR.FNCash;
  Doc.RealFNCash       = NumRealFNCash( );
  Doc.Account          = ALG_DEPOSITOR.Account;
  Doc.Oper             = {oper};
  Doc.Type_Account     = ALG_DEPOSITOR.Type_Account;
  Doc.Code_Currency    = ALG_DEPOSITOR.Code_Currency;
  Doc.CodClient        = ALG_DEPOSITOR.CodClient;
  Doc.YesSbook         = "X";
  Doc.Date_Document    = {curdate};
  Doc.DepDate_Document = {curdate};
  Doc.iApplicationKind = 1;
  Doc.ApplicationKey   = FormApplicationKey(1);

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro TrnProc

  record pay_doc  (pay_doc);
  record sbdepdoc (sbdepdoc);

  var stat = TRUE;

  if (stat)
    ALG_DEPOSITOR.UserTypeAccount = ALG_DEPOSITOR.UserTypeAccount + "Y";
    stat = UpdateAlgRecords(UPDATE_DEP,TRUE);
  end;

  if ( stat AND ( ComisSum > 0 ) )
    ClearRecord(pay_doc);
    pay_doc.FNCash           = NumRealFNCash( );
    pay_doc.IsCur            = ALG_DEPOSITOR.IsCur;
    pay_doc.NumOpert         = 5;
    pay_doc.GroupOpert       = 21; /* Комиссия */
    pay_doc.Date_Document    = {curdate};
    pay_doc.CodCur           = 0;
    pay_doc.Oper             = {oper};
    pay_doc.InSum            = ComisSum;
    pay_doc.ClientLastName   = depclnt.CurRec.rec.Name1;
    pay_doc.ClientFirstName  = depclnt.CurRec.rec.Name2;
    pay_doc.ClientSecondName = depclnt.CurRec.rec.Name3;
    pay_doc.FlagRez          = depclnt.CurRec.rec.NotResident;
    pay_doc.CodClient        = depclnt.CurRec.rec.CodClient;
    pay_doc.iApplicationKind = 200;
    pay_doc.ApplicationKey   = FormApplicationKey(200);
    pay_doc.Ground           = "Плата за утерю сберкнижки по счету " + 
                               Acc_Form(ALG_DEPOSITOR.Account);
    stat = InsertPayDocToGDDList(pay_doc);
  end;

  if (stat)
    NewDepDoc(sbdepdoc);
    sbdepdoc.TypeComplexOper  = 501;
    sbdepdoc.TypeOper         = 62;
    sbdepdoc.ApplType         = 30;
    sbdepdoc.DepDate_Document = {curdate};
    sbdepdoc.IsControl        = StrFor(1); /* Признак главного документа */
    if (StrFor(GetProgramID()) != "П")
      if (ComisSum > 0)
        sbdepdoc.Ground       = "Установка признака потери сберкнижки со взятием комиссии";
      else
        sbdepdoc.Ground       = "Установка признака потери сберкнижки без взятия комиссии";
      end;
    else
      if (ALG_SBDEPDOC.Ground != "")
        sbdepdoc.Ground       = ALG_SBDEPDOC.Ground;
      else
        sbdepdoc.Ground       = "Установка признака потери сберкнижки";
      end;
    end;

    ErrCode62 = Выполнить_операцию(sbdepdoc.Account,sbdepdoc.Type_Account,
                                   sbdepdoc.Code_Currency,62,sbdepdoc     );
    stat = NOT ErrCode62;
  end;

  if (NOT stat)
    AbortTrn();
  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  var SizeField = 5;
  var ComisStr  = "";

  ALG_RESULT = OK_RES;

  if ( ( ALG_RESULT == OK_RES ) and ( StrFor( GetProgramID( ) ) != "П" ) )
    MsgBox( "Данная операция закрыта|Воспользуйтесь нефинансовой операцией 205");
    ALG_RESULT = END_RES;
  end;

  if (ALG_RESULT == OK_RES)
    if (Index(ALG_DEPOSITOR.UserTypeAccount,"Y"))
      if (StrFor(GetProgramID()) != "П")
        MsgBox("У счета сберкнижка уже утеряна");
      end;
      ALG_RESULT = SKIP_RES;
    end;
  end;

  if ((ALG_RESULT == OK_RES) AND (StrFor(GetProgramID()) != "П"))
    if (NOT GetTrue(TRUE,"Провести операцию по утере сберкнижки ?"))
      ALG_RESULT = SKIP_RES;
    end;
  end;

  if ( ALG_RESULT == OK_RES )
    if ( not depclnt.GetRecord( ALG_DEPOSITOR.CodClient ) )
      if ( StrFor( GetProgramID( ) ) != "П" )
        MsgBox( "Не найден клиент с кодом " + String( ALG_DEPOSITOR.CodClient ) );
      end;
      ALG_RESULT = ERR_RES;
    end;
  end;

  if ( ALG_RESULT == OK_RES )
    if ( ALG_DEPOSITOR.Code_Currency != 0 )
      if ( GetAllCurrRate( {curdate}, ALG_DEPOSITOR.Code_Currency, Rate ) and ( Rate != 0 ) )
        MinRest = Money( MinRest / Rate );
      else
        if ( StrFor( GetProgramID( ) ) != "П" )
          MsgBox( "Не найден курс валюты с кодом " + String( ALG_DEPOSITOR.Code_Currency ) );
        end;
        ALG_RESULT = ERR_RES;
      end;
    end;
  end;

  /* Чтение тарифа платы за утерю сберкнижки */
  if ( ALG_RESULT == OK_RES )
    if ( NOT FindTarif( ALG_DEPOSITOR.IsCur, TarifSbook,
                        ALG_SBDEPDOC.DepDate_Document, ComisSum ) )
      MsgBox("Не найден тариф за утерю сберкнижки");
      ALG_RESULT = ERR_RES;
    end;
  end;

  if (ALG_RESULT == OK_RES)
    if ( ( depclnt.CurRec.rec.DateDeath == Date( 0, 0, 0 ) ) and ( ALG_DEPOSITOR.Sum_Rest > MinRest ) )
      ComisSum = tarif.MaxSum;
      depclnt.CurRec.GetPersnCard();
      if ( ( depclnt.CurRec.rec.Kategor == 3 ) OR ( depclnt.CurRec.rec.SocialNumber != "" ) )
        ComisSum = tarif.MinSum;
      else
        if ( StrFor( GetProgramID( ) ) != "П" )
          /* А вдруг всё-таки пенсионер? */
          if (GetTrue(False,"Вкладчик принадлежит к категории \"ПЕНСИОНЕР\"?"))
            ExecMacroFile("setpensn.mac");
            if (ALG_RESULT == OK_RES)
              ComisSum = tarif.MinSum;
            end;
          end;
        end;
      end;
    end;
  end;

  if ( ( ALG_RESULT == OK_RES ) and ( ComisSum > 0 ) )
    if ( StrFor( GetProgramID( ) ) != "П" )
      ComisStr = String( ComisSum:0:2 );
      if ( not GetTrue( true, "Вы берете плату за услуги наличными в размере " + ComisStr + " рублей ?" ) )
        ComisSum = $0L;
      end;
    else
      ComisSum = $0L; /* Чтоб не формировался PAY_DOC.DBT на комиссию */
    end;
  end;

  if (ALG_RESULT == OK_RES)
    if (ProcessTrn(1,"TrnProc",depositr))
      ALG_RESULT = SKIP_RES;
    else
      if (StrFor(GetProgramID()) != "П")
        if (ErrCode62 > 0)
          msg.Number = ErrCode62;
          if (GetEQ(msg))
            MsgBox("Ошибка " + String(ErrCode62) + " во время проведения операции 62|" + msg.Contents);
          else
            MsgBox("Ошибка " + String(ErrCode62) + " во время проведения операции 62");
          end;
        else
          MsgBox("Ошибка во время проведения операции");
        end;
      end;
      ALG_RESULT = ERR_RES;
    end;
  end;


end;
