/*
$Name:             DI_EXEEL.mac
$Module:           RS-Retail
$Description:      Страхование частных вкладов.  Выпуск Реестра обязательств в электронной форме.
*/

 import DeprIntr, Define, DI_Def, DI_PapTp, RSD, SQLConv, SQLExec, commclnt;

 private file dem ( depimake ) key 0;
 private file des ( depisum  ) key 0;
 private file dpl ( depiprev ) key 1;
 private file dps ( depiprev ) key 1;

 private var elDepClnt = TClientList; /* Справочник вкладчиков */

 private FILE f_bnk ( "BNK" ) txt 512 write;
 private FILE f_fil ( "FIL" ) txt 512 write;
 private FILE f_inv ( "INV" ) txt 512 write;
 private FILE f_dep ( "DEP" ) txt 512 write;
 private FILE f_crd ( "CRD" ) txt 512 write;
 private FILE f_ctr ( "CTR" ) txt 512 write;

 private const ArcCmd = "zip ";  /* Имя команды архиватора стандарта ZIP. */
 private const ArcExt = ".zip";

 private const ENCODING = "rsansi";

 private var ExportDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\EXPORTMESDIR", TRUE );
 private var ToolDir   = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TOOLDIR", false );
 private var ComSpec   = GetEnv( "COMSPEC" );
 private var ComArc    = "";

 private var vReg_Num_Bank = "";
 private var lReg_Num_Bank = 0;

 private var ArcName = "r0000";

 private var FN_BNK = ToolDir + "\\bnk.txt";
 private var FN_FIL = ToolDir + "\\fil.txt";
 private var FN_INV = ToolDir + "\\inv.txt";
 private var FN_DEP = ToolDir + "\\dep.txt";
 private var FN_CRD = ToolDir + "\\crd.txt";
 private var FN_CTR = ToolDir + "\\ctr.txt";

 private var {Name_Bank};
 private var {Post_Addr};

 private var save_CodClient;

 private var stat_l;

 private var rAddr = TRecHandler( "i_addr.rec" );
 private var tCountry  = TBFile( "country.dbt" , "R", 2, null, "bank.def" );

 /********************************************************************/
 /*          Представление строчного формата даты ДД.ММ.ГГГГ         */
 /********************************************************************/
 private macro DateStrDD( pDate )
   var sDate = String( pDate );
   if ( SubStr( sDate, 1, 1 ) == " " )
     sDate = "0" + Trim( sDate );
   end;
   return sDate;
 end;

 /********************************************************************/
 /*           Получение регистрационного номера банка                */
 /********************************************************************/
 private macro getRegNumBank( Str )
   var ret = "";
   var flagNext = TRUE;
   var i = 1;
   while ( flagNext  AND  ( i < 5 ) )
     if ( ( SubStr( Str, i, 1 ) < "0" )  OR  ( SubStr( Str, i, 1 ) > "9" ) )
       flagNext = FALSE;
     else
       ret = ret + SubStr( Str, i, 1 );
     end;
     i = i + 1;
   end;
   return ret;
 end;

/**************************************************************************\
|                        Адрес субъекта по 4120-У.                        |
\**************************************************************************/

private macro strSection( _code, _name )
  if ( ( ValType( _name ) == V_STRING )  AND  ( StrLen( _name ) > 0 ) )
    return _code + " " + _name + ", ";
  else
    return "";
  end;
end;

private macro strHouse( _code, _name )
  if ( ( ValType( _name ) == V_STRING )  AND  ( StrLen( _name ) > 0 ) )
    return ", " + _code + " " + _name;
  else
    return "";
  end;
end;

private macro strFlat( _name )
  if ( ( ValType( _name ) == V_STRING )  AND  ( StrLen( _name ) > 0 ) )
    return ", кв. " + _name;
  else
    return "";
  end;
end;

private macro addrOurBankRUS_Fields()
  var vPlace = FALSE;
  if   ( ( ValType( rAddr.rec.Region ) == V_STRING )  AND  ( StrLen( rAddr.rec.Region ) > 0 ) )
    vPlace = TRUE;
  elif ( ( ValType( rAddr.rec.Place ) == V_STRING )  AND  ( StrLen( rAddr.rec.Place ) > 0 ) )
    vPlace = TRUE;
  elif ( ( ValType( rAddr.rec.District ) == V_STRING )  AND  ( StrLen( rAddr.rec.District ) > 0 ) )
    vPlace = TRUE;
  elif ( ( ValType( rAddr.rec.Province ) == V_STRING )  AND  ( StrLen( rAddr.rec.Province ) > 0 ) )
    vPlace = TRUE;
  end;
  if ( vPlace )
    return rAddr.rec.PostIndex + ", " + 
           strSection( rAddr.rec.Region, rAddr.rec.CodeRegion ) +
           strSection( rAddr.rec.Province, rAddr.rec.lCodeProvince ) +
           strSection( rAddr.rec.lCodeDistrict, rAddr.rec.District ) +
           strSection( rAddr.rec.lCodePlace, rAddr.rec.Place ) +
           rAddr.rec.lCodeStreet + " " + 
           rAddr.rec.StreetName +
           strHouse( "д.", rAddr.rec.House ) +
           strHouse( "корп.", rAddr.rec.NumCorp ) +
           strFlat( rAddr.rec.Flat );
  else
    return rAddr.rec.Address;
  end;
end;

private macro addrRUS( _id )
  var vFind = FALSE;
  var strAddr = "";
  var retAddr = "";
  if ( Addr_Str( _id, I_PTADDR_POST, strAddr )  AND  ( StrLen( strAddr ) > 0 ) )
    vFind = TRUE;
  else
    if ( Addr_Str( _id, I_PTADDR_REAL, strAddr )  AND  ( StrLen( strAddr ) > 0 ) )
      vFind = TRUE;
    else
      if ( Addr_Str( _id, I_PTADDR_LEGAL, strAddr )  AND  ( StrLen( strAddr ) > 0 ) )
        vFind = TRUE;
      end;
    end;
  end;
  if ( vFind )
    retAddr = strAddr;
  else
    retAddr = addrOurBankRUS_Fields();
  end;
  return retAddr;
end;

private macro addrRUS_Client( _id, _type )
  var vFind = FALSE;
  var strAddr = "";
  var retAddr = "";
  if ( Addr_Str( _id, _type, strAddr )  AND  ( StrLen( strAddr ) > 0 ) )
    vFind = TRUE;
  end;
  if ( vFind )
    retAddr = strAddr;
  else
    retAddr = addrOurBankRUS_Fields();
  end;
  return retAddr;
end;

private macro countryNumCode( _CodeLat3 )
 tCountry.clear();
 tCountry.rec.CodeLat3 = _CodeLat3;
 if ( tCountry.getGE() )
   if ( tCountry.rec.CodeLat3 == _CodeLat3 )
     return tCountry.rec.Name + ", ";
     return "";
   end;
 end;
 return "";
end;

private macro addrForeign()
  var vPlace = "";
  if   ( ( ValType( rAddr.rec.Region ) == V_STRING )  AND  ( StrLen( rAddr.rec.Region ) > 0 ) )
    vPlace = strSection( rAddr.rec.CodeRegion, rAddr.rec.Region );
  elif ( ( ValType( rAddr.rec.Place ) == V_STRING )  AND  ( StrLen( rAddr.rec.Place ) > 0 ) )
    vPlace = strSection( rAddr.rec.lCodePlace, rAddr.rec.Place );
  elif ( ( ValType( rAddr.rec.District ) == V_STRING )  AND  ( StrLen( rAddr.rec.District ) > 0 ) )
    vPlace = strSection( rAddr.rec.lCodeDistrict, rAddr.rec.District );
  elif ( ( ValType( rAddr.rec.Province ) == V_STRING )  AND  ( StrLen( rAddr.rec.Province ) > 0 ) )
    vPlace = strSection( rAddr.rec.lCodeProvince, rAddr.rec.Province );
  end;
  if ( StrLen( vPlace ) > 0 ) 
    return vPlace +
           rAddr.rec.lCodeStreet + " " + 
           rAddr.rec.StreetName +
           strHouse( "д.", rAddr.rec.House ) +
           strHouse( "корп.", rAddr.rec.NumCorp ) +
           strFlat( rAddr.rec.Flat );
  else
    return rAddr.rec.Address;
  end;
end;

private macro addrOurBank()
  var sAddr = {Post_Addr};
  var vFind = FALSE;
  if ( FindAddress( {OurBank}, I_PTADDR_POST,  rAddr ) )
    vFind = TRUE;
  else
    if ( FindAddress( {OurBank}, I_PTADDR_REAL,  rAddr ) )
      vFind = TRUE;
    else
      if ( FindAddress( {OurBank}, I_PTADDR_LEGAL, rAddr ) )
        vFind = TRUE;
      end;
    end;
  end;
  if ( vFind )
    if ( ( ValType( rAddr.rec.Country ) == V_STRING )  AND  ( rAddr.rec.Country == "RUS" ) )
      sAddr = addrRUS( {OurBank} );
    else
      sAddr = addrForeign();
    end;
  end;
  return sAddr;
end;

private macro address_Client( _id, _type )
  var sAddr = "";
  if ( FindAddress( _id, _type, rAddr ) )
    if ( ( ValType( rAddr.rec.Country ) == V_STRING )  AND  ( rAddr.rec.Country == "RUS" ) )
      sAddr = addrRUS_Client( _id );
    else
      sAddr = addrForeign();
    end;
  end;
  return sAddr;
end;

 /********************************************************************/
 /*        Заполнение сведений об обязательствах и требованиях.      */
 /********************************************************************/
 private macro Client_Summs( sDateMake )

   var s_Stat;

   ReWind( dps );
   ClearRecord( dps );
   dps.DateMake  = sDateMake;
   dps.CodClient = save_CodClient;
   s_Stat = GetGE( dps );

   while ( s_Stat  AND
           ( dps.DateMake  == sDateMake )  AND
           ( dps.CodClient == save_CodClient ) )

     if ( dps.Type == TYPE_DEPOS )  /* Обязательства. */
       Insert( f_dep, String( save_CodClient ) + "^" +
                      reg_Num_Bank()           + "^" +
                      String( dps.NumDoc )     + "^" +
                      DateStrDD( dps.DateDoc ) + "^" +
                      Acc_Form( dps.Account )  + "^" +
                      String( dps.SummaRest:19:2 ) );
     else                           /* Требования. */
       if ( ( dps.Type == TYPE_CR_LOAN )  OR
            ( dps.Type == TYPE_CR_PERC )  OR
            ( dps.Type == TYPE_CR_OVER )  OR
            ( dps.Type == TYPE_CR_OVERPERC ) )
         Insert( f_crd, String( save_CodClient ) + "^" +
                        reg_Num_Bank()           + "^" +
                        String( dps.NumDoc )     + "^" +
                        DateStrDD( dps.DateDoc ) + "^" +
                        Acc_Form( dps.Account )  + "^" +
                        String( dps.SummaRest:19:2 ) );
       end;
     end;

     s_Stat = Next( dps );

   end;

 end;

 // =====================================================
 //                  Номер телефона клиента.
 // =====================================================
 private macro get_E_Mail( _id )
   var cmd, rs;
   var vEmail = "";
   cmd = RsdCommand( "select T_VALUE as rPhone "
                     "from DCONTACT_DBT "
                     "where T_PARTYID = ? and T_ADDRESSTYPE = 1 and T_CONTACTKIND = 5" );
   rs = RsdRecordset( cmd );
   cmd.addParam( "idpt", RSDBP_IN );
   cmd.value( "idpt" ) = _id;
   cmd.execute;
   if ( rs.moveNext() )
     vEmail = rs.value( "rPhone" );
   end;
   return vEmail;
 end;

 private macro delSpace( str )
    var str2="", i=1;
    while( i<=strlen(str) )
       if ( substr(str,i,1)!=" " )
          str2 = str2 + substr(str,i,1);
       end;
       i = i + 1;
    end;
    
    return str2;
 end;

 /********************************************************************/
 /*                Формирование записи о клиенте.                    */
 /********************************************************************/
 private macro Client_Line( rDateMake )

   /* Атрибуты клиента. */
   var elName3;
   var elFIO;
   var elAddress;
   var elAddressPost;
   var elE_Mail;
   var elPapers;

   if ( ( save_CodClient != 0 )  AND  ( elDepClnt.GetRecord( save_CodClient ) ) )
     if ( StrLen( elDepClnt.CurRec.rec.Name3 ) > 0 )
       elName3 = elDepClnt.CurRec.rec.Name3;
     else
       elName3 = "Отсутствует";
     end;
     elFIO = elDepClnt.CurRec.rec.Name1 + "^" +
             elDepClnt.CurRec.rec.Name2 + "^" +
             elName3;

     elAddress = commclnt_Получить_Поле_Address_Заданного_Типа( elDepClnt, COMMCLNT_PTADDR_LEGAL );

     elE_Mail = get_E_Mail( save_CodClient );

     if ( StrLen( elAddress ) == 0 )
       elAddress = "000000 Отсутствует";
     end;

     elAddressPost = commclnt_Получить_Поле_Address_Заданного_Типа( elDepClnt, COMMCLNT_PTADDR_POST );  // Почтовый адрес

     if ( StrLen( elAddressPost ) == 0 )
       elAddressPost = "000000 Отсутствует";
     end;

     if ( ( StrLen( elDepClnt.CurRec.rec.PaperSeries ) != 0 )  AND
          ( StrLen( elDepClnt.CurRec.rec.PaperNumber ) != 0 )  AND
          ( StrLen( elDepClnt.CurRec.rec.PaperIssuer ) != 0 ) )
       elPapers = DI_PaperTypes1417( elDepClnt.CurRec.rec.PaperKind,
                                     elDepClnt.CurRec.rec.NotResident ) + "^" +
                  elDepClnt.CurRec.rec.PaperSeries + " № " +
                  elDepClnt.CurRec.rec.PaperNumber + ", " +
                  elDepClnt.CurRec.rec.PaperIssuer + ", " +
                  DateStrDD( elDepClnt.CurRec.rec.PaperIssuedDate );
     else
       elPapers = "14^00 № 000000 выдан \"00.00.0000\" подразделение отсутствует";
     end;
   else
     elFIO     = "^^";
     elAddress = "000000 Отсутствует";
     elPapers  = "14^00 № 000000 выдан \"00.00.0000\" подразделение отсутствует";
   end;

   Insert( f_inv, String( save_CodClient ) + "^" +
                  elFIO                    + "^" +
                  elAddress                + "^" +
                  elAddressPost            + "^" +
                  elE_Mail                 + "^" +
                  elPapers );

   Client_Summs( rDateMake );

 end;

 /********************************************************************/
 /*                        Основная процедура.                       */
 /********************************************************************/

 macro DI_CB_Electronic( pDateMake )

   var cmd, rs;

   var f_Find;        /* Флаг: записи найдены \ не найдены. */
   var f_End = TRUE;  /* Флаг: штатное завершение работы процедуры. */

   var vCountRec;
   var vRec = 0;

   if ( pDateMake == Date( 0, 0, 0 ) )
     pDateMake = {CurDate};
     if ( NOT GetDate( pDateMake, "Укажите дату выпуска Реестра" ) )
       Exit( 1 );
     end;
   end;

   if ( pDateMake == Date( 0, 0, 0 ) )
     println( "Не задана дата." );
     Exit();
   end;

   /*** Сведения о банке. ***/

   if ( ExistFile( FN_BNK, 0 ) )
     Close( f_bnk );
     DelFile( FN_BNK );
   end;

   if ( NOT Open( f_bnk, FN_BNK, ENCODING ) )
     println( "Ошибка открытия файла: ", FN_BNK );
     Exit();
   end;

   Insert( f_bnk, {Name_Bank} + "^" + reg_Num_Bank() );

   Close( f_bnk );

   /*** Сведения о филиале. ***/

   if ( ExistFile( FN_FIL, 0 ) )
     Close( f_fil );
     DelFile( FN_FIL );
   end;

   if ( NOT Open( f_fil, FN_FIL, ENCODING ) )
     println( "Ошибка открытия файла: ", FN_FIL );
     Exit();
   end;

   Insert( f_fil, reg_Num_Bank() + "^" +
                  {Name_Bank} + "^" +
                  addrOurBank() );

   Close( f_fil );

   /*** Сведения о клиентах, обязательства, требования. ***/

   if ( ExistFile( FN_INV, 0 ) )
     Close( f_inv );
     DelFile( FN_INV );
   end;

   if ( NOT Open( f_inv, FN_INV, ENCODING ) )
     println( "Ошибка открытия файла: ", FN_INV );
     Exit();
   end;

   if ( ExistFile( FN_DEP, 0 ) )
     Close( f_dep );
     DelFile( FN_DEP );
   end;

   if ( NOT Open( f_dep, FN_DEP, ENCODING ) )
     println( "Ошибка открытия файла: ", FN_DEP );
     Exit();
   end;

   if ( ExistFile( FN_CRD, 0 ) )
     Close( f_crd );
     DelFile( FN_CRD );
   end;

   if ( NOT Open( f_crd, FN_CRD, ENCODING ) )
     println( "Ошибка открытия файла: ", FN_CRD );
     Exit();
   end;

   cmd = RsdCommand( "select t_CodClient as Client " +
                     "from ddepiprev_dbt " +
                     "where t_DateMake = " + sqlDateToStr( pDateMake ) + " " +
                     "order by t_CodClient" );

   cmd.execute;

   rs = RsdRecordset( cmd );

   vCountRec = NRecordsSQL( "depiprev.dbt", "t_DateMake = " + sqlDateToStr( pDateMake ) );

   if ( vCountRec > 0 )

     InitProgress( vCountRec, " Ж Д И Т Е ...",
                              "Формирование электронного Реестра ..." );

     vRec = 0;

     f_Find = TRUE;

     while ( rs.moveNext )

       if ( vRec == 0 )
         save_CodClient = rs.value( "Client" );
       end;

       UseProgress( vRec );
       vRec = vRec + 1;

       if ( save_CodClient != rs.value( "Client" ) )
         Client_Line( pDateMake );
         save_CodClient = rs.value( "Client" );
       end;

     end;

     Client_Line( pDateMake );

     RemProgress();

   else

     f_Find = FALSE;

   end;

   /*
   Close( f_crd );
   Close( f_dep );
   */

   Close( f_inv );
   Close( f_dep );
   Close( f_crd );

   /* Контрольная информация. */
   if ( ExistFile( FN_CTR, 0 ) )
     Close( f_ctr );
     DelFile( FN_CTR );
   end;

   if ( NOT Open( f_ctr, FN_CTR, ENCODING ) )
     println( "Ошибка открытия файла: ", FN_CTR );
     Exit();
   end;

   Insert( f_ctr, reg_Num_Bank() );
   Insert( f_ctr, DateStrDD( pDateMake ) );

   ReWind( dem );
   ClearRecord( dem );
   dem.DateMake = pDateMake;
   if ( GetEQ( dem ) )
     Insert( f_ctr, String( dem.NumBranch ) );
     Insert( f_ctr, String( dem.NumClient ) );
     Insert( f_ctr, String( dem.NumDebet  ) );
     Insert( f_ctr, String( dem.NumCredit ) );
   else
     println( " *** Не найдена сводная запись за ", pDateMake );
     Exit();
   end;

   ReWind( des );
   ClearRecord( des );
   des.DateMake = pDateMake;
   stat_l = GetGE( des );

   while ( stat_l  AND  ( des.DateMake == pDateMake ) )
     Insert( f_ctr, GetCurrNumericalCode( des.Currency ) + "^" +
                    String( des.SumDebet:18:2  ) + "^" +
                    String( des.SumCredit:18:2 ) );
     stat_l = Next( des );
   end;

   Close( f_ctr );

   /*** Архивирование. ***/

   vReg_Num_Bank = Trim( reg_Num_Bank() );

   ArcName = "r" + getRegNumBank( vReg_Num_Bank );

   ComArc = ArcCmd + ExportDir + ArcName + ArcExt;

   if ( Run( ComSpec, "/C " + ComArc +
                      " bnk.txt" + " fil.txt" + " inv.txt" +
                      " dep.txt" + " crd.txt" + " ctr.txt" ) != 0 )

     println( " *** Не удалось выполнить команду архивирования: " +
              ComArc + " ..." );
     f_End = FALSE;

   end;

   /* Удаление текстовых файлов после архивирования. */
   if ( ExistFile( FN_BNK, 0 ) )
     Close( f_bnk );
     DelFile( FN_BNK );
   end;

   if ( ExistFile( FN_FIL, 0 ) )
     Close( f_fil );
     DelFile( FN_FIL );
   end;

   if ( ExistFile( FN_INV, 0 ) )
     Close( f_inv );
     DelFile( FN_INV );
   end;

   if ( ExistFile( FN_DEP, 0 ) )
     Close( f_dep );
     DelFile( FN_DEP );
   end;

   if ( ExistFile( FN_CRD, 0 ) )
     Close( f_crd );
     DelFile( FN_CRD );
   end;

   if ( ExistFile( FN_CTR, 0 ) )
     Close( f_ctr );
     DelFile( FN_CTR );
   end;

   /* Протокол результатов работы. */
   if ( f_End )
     if ( f_Find )
       PrintLn( "" );
       PrintLn( "   Выпуск Реестра обязательств в электронном формате за ",
                pDateMake );
       PrintLn( "" );
       PrintLn( "   Файл архива: ", ExportDir + ArcName + ArcExt );
     else
       PrintLn( "" );
       PrintLn( "   Не найдено записей за ", pDateMake );
     end;
   end;

 end;
