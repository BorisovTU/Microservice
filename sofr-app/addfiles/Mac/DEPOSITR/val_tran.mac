/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Журнал учета валютных переводов по счетам физических лиц                *
*                                                                          *
\**************************************************************************/

import DeprIntr, Cur_Rate, CommonInter;

file cur ( "currency.dbt" ) key 0;  /* Справочник валют */
private var sbdepdoc = TBFile( "sbdepdoc.dbt", "r", 5 );

file srt ( "sbdepdoc.t00" ) key 0 write; /* Сортировка */

record doc1 ( "sbdepdoc.1" );

// Операции валютных переводов
const OP_GETPOR = 63; // Списание по поручению (разовому)
const OP_PUTCON = 71; // Зачисление средств
const appRussia = 32; // Подвид "из-в Россию

const cUSD = 1;        // Код базовой валюты (USD)
const LimSum = $2000.; // Граничная сумма

private var Branch = NumFNCash( );

var {curdate};
var {Name_Bank};
var {CNum};


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetCurr ( code )

  var str;

  cur.Code_Currency = code;
  if ( GetEQ( cur ) )
    str = cur.Short_Name;
  else
    str = "???";
  end;

  return str;

end;


/**************************************************************************\
|                 Нахождение эквивалента суммы в долларах                  |
\**************************************************************************/
macro EQU_USD ( curr, sum, cur_date, cur_time )

  var CBRateUSD, BuyRateUSD, SellRateUSD;
  var CBRateVal, BuyRateVal, SellRateVal;
  var equ;
 
  if ( curr == cUSD )
    equ = sum / $1.0;
  else
    if ( not GetAllCurrRate( cur_date, 1,
                             CBRateUSD, BuyRateUSD, SellRateUSD, cur_time ) )
      MsgBox( "Ошибка при определении курса USD" );
      Exit( 1 );
    end;
 
    if ( curr == 0 )
      equ = sum / ( CBRateUSD );
    else
      if ( not GetAllCurrRate( cur_date, curr,
                               CBRateVal, BuyRateVal, SellRateVal, cur_time ) )
        MsgBox( "Ошибка при определении курса валюты = ", curr );
        Exit( 1 );
      end;
 
      equ = sum * CBRateVal / ( CBRateUSD );
    end;
  end;
 
  return equ;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ValLogProc ( date1, date2, p_iso, p_client, p_group, p_sum, p_filial )

  array iSumO; // Итоговые суммы переводов из России
  array iSumI; // Итоговые суммы переводов в  Россию
  array iCur;  // Коды валют для итоговых сумм

  var curCur = -1; // Текущий код валюты
  var indCur = -1; // Текущий индекс массивов итоговых сумм 
  var numCur;      // Максимальное значение индекса массивов

  var stat = true;
  var nrec = 0;
  var nsrt = 0;
  var rezid = "";
  var group = "";
  var sum = $0;
  var equivSum;
  var corrAcc = "";
  var bankName = {Name_Bank};
  var tm;
  var needProcess = true;
  var errCode = 0;
  var errText;

  var TxtDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true );
  var FNdt;
  var NameBranch;

  // Определение имени рабочего файла
  if ( StrLen( TxtDir ) > 0 )
    if ( SubStr( TxtDir, StrLen( TxtDir ), 1 ) != "\\" )
      TxtDir = TxtDir + "\\";
    end;
  end;
  FNdt = TxtDir + "valtrsrt." + String( {CNum} );

  // Создадим временный файл
  if ( ExistFile( FNdt, 0 ) )
    Close( srt );
    DelFile( FNdt );
  end;
  Create( srt, FNdt, true );
  Open( srt, FNdt, 1, true );

  // Определим название филиала
  if (findDepartment(Branch, NameBranch))
    if ( StrLen( NameBranch ) > 0 )
      bankName = bankName + " (Филиал " + NameBranch + ")";
    end;
  end;

  // Цикл по документам, удовлетворяющим заданным условиям
  Message( " Идет перебор документов в заданном интервале дат. Ждите..." );
  sbdepdoc.addFilter( "t.t_IsCur = 1 and " +
                      "( t.t_TypeOper = " + String( OP_GETPOR ) + " or " +
                      "  t.t_TypeOper = " + String( OP_PUTCON ) + " ) and " +
                      "t.t_ApplType = " + String( appRussia )                );
  sbdepdoc.Clear;
  sbdepdoc.rec.Date_Document = date1;
  stat = sbdepdoc.GetGE;
  while ( stat )
    if ( ( sbdepdoc.rec.Date_Document >= date1 ) and
         ( sbdepdoc.rec.Date_Document <= date2 )    )

      needProcess = true;

      // Обрабатываем только валютные операции
      if ( sbdepdoc.rec.IsCur == 0 )
        needProcess = false;
      end;

      // Обрабатываем только некоторые операции с подоперациями
      if ( ( ( sbdepdoc.rec.TypeOper == OP_GETPOR ) or
             ( sbdepdoc.rec.TypeOper == OP_PUTCON )   ) and
           (   sbdepdoc.rec.ApplType == appRussia     )    )
        ;
      else
        needProcess = false;
      end;

      // Фильтр на валюту
      if ( ( p_iso == -1                         ) or // Для всех валют
           ( p_iso == sbdepdoc.rec.Code_Currency )   )
        ;
      else
        needProcess = false;
      end;

      // Фильтр на резидентность клиента
      if ( ( p_client               == 0                    ) or // Все клиенты
           ( sbdepdoc.rec.FlagRezid == StrFor( p_client-1 ) )   )
        ;
      else
        needProcess = false;
      end;

      // Фильтр на тип операции
      if ( (   p_group == 0                            ) or // Переводы и зачисления
           ( ( p_group == 1                       ) and  
             ( sbdepdoc.rec.TypeOper == OP_PUTCON )    ) or
           ( ( p_group == 2                       ) and
             ( sbdepdoc.rec.TypeOper == OP_GETPOR )    )   )
        ;
      else
        needProcess = false;
      end;

      // Фильтр на подразделение
      if ( ( p_filial            == 0      ) or // По всем подразделениям
           ( sbdepdoc.rec.FNCash == Branch )   )
        ;
      else
        needProcess = false;
      end;

      // Фильтр на сумму операции
      if ( needProcess and ( p_sum > 0 ) )
        if ( sbdepdoc.rec.TypeOper == OP_GETPOR )
          sum = sbdepdoc.rec.OutSum;
        else
          sum = sbdepdoc.rec.InSum;
        end;

        tm = null;
        if ( sbdepdoc.rec.ApplicationKey != "" )
          tm = Time( Int( SubStr( sbdepdoc.rec.ApplicationKey, 11, 2 ) ),
                     Int( SubStr( sbdepdoc.rec.ApplicationKey, 13, 2 ) ),
                     Int( SubStr( sbdepdoc.rec.ApplicationKey, 15, 2 ) ) );
        end;

        equivSum = $1.* EQU_USD( sbdepdoc.rec.Code_Currency,
                                 sum,
                                 sbdepdoc.rec.Date_Document,
                                 tm );

        if ( ( ( p_sum == 1 ) AND ( equivSum <= LimSum ) ) or
             ( ( p_sum == 2 ) AND ( equivSum >  LimSum ) )   )
          ;
        else
          needProcess = false;
        end;
      end;

      // Помещение документа во временный файл
      if ( needProcess )
        Copy( srt, sbdepdoc );
        stat = Insert( srt );
        if ( stat )
          nsrt = nsrt + 1; // Число записей во временном файле
        else
          println( " Ошибка при записи во временный файл" );
          errCode = Status( errText );
          println( " Ошибка СУБД  ", errCode, ": ", errText );
        end;
      end;

      if ( stat )
        stat = sbdepdoc.Next;
      end;
    else
      stat = false;
    end;
  end;
  sbdepdoc.dropFilter;

  // Формирование журнала учета переводов
  if ( errCode != 0 )
    ;
  elif ( nsrt != 0 )

    [
       #


                                                                    ЖУРНАЛ
                                               УЧЕТА ВАЛЮТНЫХ ПЕРЕВОДОВ ПО СЧЕТАМ ФИЗИЧЕСКИХ ЛИЦ
                                                     за период с ########## по ##########

      +---------------------------------------------------------------------------------------------------------------------------------+
      | N/N |    Дата    |                            | Резидент-  |   Группа   | Код |                    |           Номер            |
      | п/п | совершения |         Номер счета        | ность      |  валютных  | вал-|        Сумма       |         корр.счета         |
      |     |  операции  |                            |            |  операций  | юты |                    |                            |
      +-----+------------+----------------------------+------------+------------+-----+--------------------+----------------------------+
    ]
    (
      bankName,
      date1,
      date2
    );

    InitProgress( nsrt,
                  " Идет формирование журнала учета переводов. Ждите..."
                  "Формирование журнала учета переводов" );
    nrec = 0;

    KeyNum( srt, 0 );
    ReWind( srt );
    while (  Next( srt ) )
      nrec = nrec + 1;
      /* Переход на новый элемент массивов итоговых сумм */
      if ( curCur != srt.Code_Currency )
        indCur = indCur + 1;
        curCur = srt.Code_Currency;
        iCur( indCur ) = curCur;
        iSumO( indCur ) = $0;
        iSumI( indCur ) = $0;
      end;

      if ( srt.FlagRezid == StrFor( 0 ) )
        rezid = "Резидент";
      else
        rezid = "Нерезидент";
      end;

      if ( srt.TypeOper == OP_GETPOR )
        group = "Перевод";
        sum = srt.OutSum;
        iSumO( indCur ) = iSumO( indCur ) + sum;
        SetRecordAddr( doc1, srt, 0, 0, true );
        corrAcc = doc1.CorAcc_Receiver;
      else
        group = "Зачисление";
        sum = srt.InSum;
        iSumI( indCur ) = iSumI( indCur ) + sum;
        corrAcc = "";
      end;

      [ | ### | ########## | ########################## | ########## | ########## | ### | ################## | ########################## |]
      ( nrec:3,
        srt.Date_Document:f,
        Acc_Form(srt.Account):f,
        rezid,
        group,
        GetCurr( srt.Code_Currency ),
        sum:a:18:2,
        Acc_Form(corrAcc):f );

      UseProgress( nrec );
    end;

    [ +---------------------------------------------------------------------------------------------------------------------------------+];
    numCur = indCur;
    indCur = 0;
    while ( indCur <= numCur )
      if ( ( p_group == 0 ) or ( p_group == 2 ) )
        [ |                                                  Итого по: | Перевод    | ### | ################## |                            |]
        ( GetCurr( iCur( indCur ) ), iSumO( indCur ):a:18:2 );
      end;
      if ( ( p_group == 0 ) or ( p_group == 1 ) )
        [ |                                                  Итого по: | Зачисление | ### | ################## |                            |]
        ( GetCurr( iCur( indCur ) ), iSumI( indCur ):a:18:2 );
      end;
      indCur = indCur + 1;
    end;
    [ +---------------------------------------------------------------------------------------------------------------------------------+


            Исполнитель ___________________________________
    ];

    RemProgress( );
  else
    [
          При заданных значениях валютных переводов не найдено];
  end;

end;
