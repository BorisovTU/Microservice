import wf_log;

class TParameter(_name : String, _value)
  var Name : String = _name;
  var Value = _value;
end; 

class TParameters(paramStr : String)
  private var params = TArray;

  macro add(name : String, value)
    params[params.size] = TParameter(name, value); 
  end;

  macro get(name : String)
    var param;
    for (param, params)
      if (param.name == name)
        return param.Value;
      end;
    end;
    return null;
  end;

  macro set(name : String, value)
    var param;
    for (param, params)
      if (param.name == name)
        param.Value = value;
        return;
      end;
    end;
    add(name, value);
  end;

  macro asArray
    return params;
  end;

  macro setArray(a : TArray)
    params = a;
  end;

  private macro paramToString(param)
    var rslType = valType(param.Value);
    var ftType;
    var valueStr = string(param.Value);

    if (rslType == V_INTEGER)
      ftType = "FT_INT32";
    elif (rslType == V_DOUBLE)
      ftType = "FT_DOUBLE";
    elif (rslType == V_STRING)
      ftType = "FT_STRING";
      valueStr = strSubst(strSubst(valueStr, "\\", "\\\\"), "\n", "\\n");
    elif (rslType == V_DATE)
      ftType = "FT_DATE";
      valueStr = StrSubst(valueStr, " ", "0");
    elif (rslType == V_NUMERIC)
      ftType = "FT_NUMERIC";
    elif (rslType == V_BOOL)
      ftType = "FT_BOOLEAN";
    else
      runError(logError("Тип данных " + rslType + " не поддерживается"));
    end;
    
    return param.name + "=" + ftType + " " + valueStr;
  end;

  macro toString
    var param, result = "";
    for(param, params)
      result = result + paramToString(param) + "\n";
    end; 
    return result;
  end;

  private macro parseValue(type : String, valueStr : String)
    if (type == "FT_INT32")
      return int(valueStr);
    elif (type == "FT_DOUBLE")
      return double(valueStr);
    elif (type == "FT_STRING")
      return strSubst(strSubst(valueStr, "\\n", "\n"), "\\\\", "\\");
    elif (type == "FT_DATE")
      return date(valueStr);
    elif (type == "FT_NUMERIC")
      return money(valueStr);
    elif (type == "FT_BOOLEAN")
      return strLwr(valueStr) == "true";
    end;
  end;

  private macro parseParam(s : String) : TParameter
    if (trim(s) == "")
      return;
    end;

    var pos1 = index(s, "=");
    if (pos1 == 0)
      runError(logError("Неверный формат параметра: " + s));
    end;   

    var pos2 = index(s, " ", pos1 + 1); 
    if (pos2 == 0)
      runError(logError("Неверный формат параметра: " + s));
    end;   

    var name = subStr(s, 1, pos1 - 1);
    var type = subStr(s, pos1 + 1, pos2 - pos1 - 1);
    var valueStr = subStr(s, pos2 + 1);

    add(name, parseValue(type, valueStr));
  end;
  
  private macro parse(paramStr : String) : TArray
    var pos;
    var line;
    while((pos = index(paramStr, "\n")) != 0)
       line = subStr(paramStr, 1, pos - 1);
       paramStr = subStr(paramStr, pos + 1);
       parseParam(line);
    end; 
    parseParam(paramStr);
  end;

  if (paramStr != null)
    parse(paramStr);
  end;
end;

/*
var p = TParameters;

p.add("i", 1);
p.add("d", 1.1);
p.add("s", "fuck off\nand die! \\lamo");
p.add("dt", date);
p.add("amount", $1.12);
p.add("b1", true);
p.add("b2", false);

var s = p.toString;
printLn(s);

var p1 = TParameters(s);
printLn(p1.toString);
println(p.get("s"));
*/