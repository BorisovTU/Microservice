/*
$Name:             will_com.mac
$Module:           RS-Retail
$Description:      Завещательное распоряжение
*/

/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Завещательное распоряжение
  (выпускается при операции 232 "Ввод завещания".)

*****************************************************************/

 import DeprIntr, Alg_Vars, rsd;

 record ALG_TRAST ( sbtrast  );
 record ALG_WREG  ( rtwillrg );

 file opr     ( person   ) key 0;
 file tpac    ( typeac   ) key 0;
 file casln   ( sb_casln ) key 0;
 file rtnfopr ( rtnfopr  ) key 1;

 var dpdep = TRecHandler( "i_dp_dep.rec" );
 var party = TRecHandler( "i_party.rec" );
 var adr   = TRecHandler( "i_addr.rec"  );

 record will_r ( sbtrast );

 var DepClnt  = TClientList;  /* Справочник вкладчиков - для завещателя */
 var DepClntH = TClientList;  /* Справочник вкладчиков - для наследника */
 var DepClnt1 = TClientList;  /* Справочник вкладчиков - для наследника 1-ой очереди */
 var DepClnt2 = TClientList;  /* Справочник вкладчиков - для наследника 2-ой очереди */

 /* var {Name_Bank}; */
 /* var {Post_Addr}; */
 /* var {oper}; */

 // Атрибуты структурного подразделения.
 var sBranchCode = "";
 var sBranchName = "";
 var sBranchAddr = "";

 var FNCash = NumFNCash();

 var ErrCodeWR, ErrTextWR;

 /**************************************************************************\
 |                 Дополнение строки символами подчеркивания.               |
 \**************************************************************************/
 macro Str_Line( pStr, pLen )
   var i = 0;
   var l = StrLen( pStr );
   var vStr = "";

   if ( pLen > l )
     i = pLen - l;
     vStr = pStr;
     while ( i )
       vStr = vStr + "_";
       i = i - 1;
     end;
   else
     vStr = SubStr( pStr, 1, pLen );
   end;

   return vStr;
 end;

 /**************************************************************************\
 |      Проверка: первый вызов макроса из связки документов по операции.    |
 \**************************************************************************/
 macro IsFirstLog()

   var ret_flag       = false;
   var key_current    = "";
   var numDprt        = String( ALG_NFOPR.numDprt );
   var recID          = String( ALG_NFOPR.recID );
   var query;
   var rs;

   while ( StrLen( numDprt ) < 5 )
     numDprt = "0" + numDprt;
   end;
   while ( StrLen( recID ) < 10 )
     recID = "0" + recID;
   end;
   key_current = String("rtnfopr#" + numDprt + recID + "00");

   query = String(" select * from dsb_casln_dbt " +
            " where T_APPLICATIONKIND2=3001 and T_APPLICATIONKEY2=\'" + key_current +
            "\' and T_REFVALUE2=0 and T_NUMLINKDOC = 10");

   rs = RsdRecordset( query );

   if( rs.moveNext() ) 
     ret_flag = true;
   else
     numDprt = String( ALG_NFOPR.numDprt );
     while ( StrLen( numDprt ) < 3 )
       numDprt = "0" + numDprt;
     end;
     key_current = String("rtnfopr#" + numDprt + recID + "00");
     
     query = String(" select * from dsb_casln_dbt " +
              " where T_APPLICATIONKIND2=3001 and T_APPLICATIONKEY2=\'" + key_current +
              "\' and T_REFVALUE2=0 and T_NUMLINKDOC = 10");

     rs = RsdRecordset( query );

     if( rs.moveNext() ) 
       ret_flag = true;
     end;
   end;

   return ret_flag;

 end;

 /*************************************************************************\
 |               Получение атрибутов структурного подразделения.           |
 \*************************************************************************/
 macro get_Branch_Attributs()
   if ( findDepartment( FNCash, NULL, dpdep, party) )
     sBranchCode = dpdep.rec.Name;
     if ( FindAddress( party.rec.partyid, I_PTADDR_LEGAL, adr ) )
       sBranchAddr = adr.rec.Address;
     else
       sBranchAddr = "";
     end;
     if ( dpdep.rec.NodeType == 1 )  // Филиал.
       sBranchName = party.rec.Name;
     else   // Если ВСП, то берется вышестоящее подразделение.
       while ( dpdep.rec.NodeType != 1 )
         if ( findDepartment( dpdep.rec.ParentCode, NULL, dpdep, party) )
           sBranchName = party.rec.Name;
         else
           println( " *** Ошибка при чтении атрибутов структурного подразделения с номером = ",
                    dpdep.rec.ParentCode );
           ErrCodeWR = Status( ErrTextWR );
           println( "  Ошибка " + String( ErrCodeWR ) + ": " + ErrTextWR );
           dpdep.rec.NodeType = 1;  // Для заверешения цикла.
         end;
       end;
     end;
   else
     println( " *** Ошибка при чтении атрибутов структурного подразделения с номером = ",
              FNCash );
     ErrCodeWR = Status( ErrTextWR );
     println( "  Ошибка " + String( ErrCodeWR ) + ": " + ErrTextWR );
   end;
 end;

 /*************************************************************************\
 |                          Получение ФИО клиента.                         |
 \*************************************************************************/
 macro get_FIO_Client()
   var sFIO = "";
   if ( DepClnt.GetRecord( ALG_DEPOSITOR.CodClient ) )
     sFIO = DepClnt.CurRec.rec.Name1 + " " +
            DepClnt.CurRec.rec.Name2 + " " +
            DepClnt.CurRec.rec.Name3;
   else
     sFIO = "?????";
     println( " *** Не найдена запись вкладчика с CodClient = ", ALG_DEPOSITOR.CodClient );
   end;
   return sFIO;
 end;

 /*************************************************************************\
 |                       Строчное наименование дроби.                      |
 \*************************************************************************/
 macro Str_Fraction( pM, pN )
   var sFract = NumToStr( pM, "", "", "", TRUE ) + " / " + NumToStr( pN, "", "", "", TRUE );
   return sFract;
 end;

 /*************************************************************************\
 |                       Получение ФИО и доли наследника.                  |
 \*************************************************************************/
 macro get_Heir()
   var sHeir;
   if ( will_r.CodClient != 0 )
     if ( DepClntH.GetRecord( will_r.CodClient ) )
       /* Формирование ФИО */
       sHeir = DepClntH.CurRec.ConvertFIOFull();
       /* Формирование доли */
       if ( ( will_r.QPart_m != 0 ) and ( will_r.QPart_n != 0 ) )
         sHeir = sHeir + ", доля " + String( will_r.QPart_m ) + "/" +
                                     String( will_r.QPart_n ) + " ( " +
                                     Str_Fraction( will_r.QPart_m,
                                                   will_r.QPart_n ) + " )";
       end;
     else
       sHeir = "????????";
     end;
   else
     sHeir = "????????";
   end;
   return sHeir;
 end;

  macro get_HeirByID(CodClient, QPart_m, QPart_n)
   var sHeir;
   if ( CodClient != 0 )
     if ( DepClntH.GetRecord( CodClient ) )
       /* Формирование ФИО */
       sHeir = DepClntH.CurRec.ConvertFIOFull();
       /* Формирование доли */
       if ( ( QPart_m != 0 ) and ( QPart_n != 0 ) )
         sHeir = sHeir + ", доля " + String( QPart_m ) + "/" +
                                     String( QPart_n ) + " ( " +
                                     Str_Fraction( QPart_m,
                                                   QPart_n ) + " )";
       end;
     else
       sHeir = "????????";
     end;
   else
     sHeir = "????????";
   end;
   return sHeir;
 end;

 /*************************************************************************\
 |       Получение ФИО наследника при наличии дополнительных условий.      |
 \*************************************************************************/
 macro get_Heir1_ReWill()
   var sHeir1;

   if ( will_r.ConditionClient != 0 )
     if ( DepClnt1.GetRecord( will_r.CodClient ) )
       sHeir1 = DepClnt1.CurRec.ConvertFIOFull();
     else
       sHeir1 = "????????";
     end;
   else
     sHeir1 = "";
   end;

   return sHeir1;
 end;

 macro get_Heir1_ReWillByID(CodClient, ConditionClient)
   var sHeir1;

   if ( ConditionClient != 0 )
     if ( DepClnt1.GetRecord( CodClient ) )
       sHeir1 = DepClnt1.CurRec.ConvertFIOFull();
     else
       sHeir1 = "????????";
     end;
   else
     sHeir1 = "";
   end;

   return sHeir1;
 end;

 /*************************************************************************\
 |     Получение ФИО наследника второй очереди при наличии доп.условий.    |
 \*************************************************************************/
 macro get_Heir2_ReWill()
   var sHeir2;

   if ( will_r.ConditionClient != 0 )
     if ( DepClnt2.GetRecord( will_r.ConditionClient ) )
       sHeir2 = DepClnt2.CurRec.ConvertFIOFull();
     else
       sHeir2 = "????????";
     end;
   else
     sHeir2 = "";
   end;

   return sHeir2;
 end;

 macro get_Heir2_ReWillByID(CodClient, ConditionClient)
   var sHeir2;

   if ( ConditionClient != 0 )
     if ( DepClnt2.GetRecord( ConditionClient ) )
       sHeir2 = DepClnt2.CurRec.ConvertFIOFull();
     else
       sHeir2 = "????????";
     end;
   else
     sHeir2 = "";
   end;

   return sHeir2;
 end;

 /*************************************************************************\
 |                     Получение должности операциониста.                  |
 \*************************************************************************/
 macro GetNameTypePerson()
   var sWork = "";
   ReWind( tpac );
   ClearRecord( tpac );
   tpac.iNumType = 14;  /* Уровни доступов. */
   tpac.Type_Account = opr.cTypePerson;
   if ( GetEQ( tpac ) )
     sWork = tpac.Name_Type;
   else
     sWork = "?????";
     println( " *** Ошибка при чтении должности операциониста с кодом = ", opr.cTypePerson );
     ErrCodeWR = Status( ErrTextWR );
     println( "  Ошибка " + String( ErrCodeWR ) + ": " + ErrTextWR );
   end;
   return sWork;
 end;

 /*************************************************************************\
 |                   Получение ФИО, должности операциониста.               |
 \*************************************************************************/
 macro get_FIO_Oper()
   var sOper = "";
   ReWind( opr );
   ClearRecord( opr );
   opr.Oper = {oper};
   if ( GetEQ( opr ) )
     sOper = opr.Name;
   else
     sOper = "?????";
     println( " *** Ошибка при чтении записи по операционисту с кодом = ", {oper} );
     ErrCodeWR = Status( ErrTextWR );
     println( "  Ошибка " + String( ErrCodeWR ) + ": " + ErrTextWR );
   end;
   return sOper;
 end;

 /**************************************************************************\
 |                  Найти нефинансовую операцию по завещанию.               |
 \**************************************************************************/
 macro Find_NF_Opr()

   var numDprt = 0;
   var recID   = 0;

   if ( StrLen( casln.ApplicationKey2 ) >= 12 )
     numDprt = Int( SubStr( casln.ApplicationKey2,  9, 3 ) );
     recID   = Int( SubStr( casln.ApplicationKey2, 12    ) );
   end;

   rtnfopr.numDprt = numDprt;
   rtnfopr.recID   = recID;

   return GetEQ ( rtnfopr );

 end;

 /**************************************************************************\
 |               Получение записи по завещанию из sbtrast.dbt.              |
 \**************************************************************************/
 macro Get_Will_Record()

   var key_current = "";
   var numDprt     = String( ALG_NFOPR.numDprt );
   var recID       = String( ALG_NFOPR.recID );
   var kind_main, key_main;
   var stat;
   var rs1,rs2,rs3;
   var dprt, rid;
   var query1, query2, query3;

   while ( StrLen( numDprt ) < 5 )
     numDprt = "0" + numDprt;
   end;
   while ( StrLen( recID ) < 10 )
     recID = "0" + recID;
   end;
   key_current = String("rtnfopr#" + numDprt + recID + "00");


   query1 = string(" select T_APPLICATIONKIND1, T_APPLICATIONKEY1 " +
             " from dsb_casln_dbt " +
             " where T_APPLICATIONKIND2=3001 and T_APPLICATIONKEY2=\'" + key_current +
             "\' and T_REFVALUE2=0 ");

   rs1 = RsdRecordset( query1 );

   stat = rs1.moveNext();

   if ( stat )
     ;
   else
     numDprt = String( ALG_NFOPR.numDprt );
     while ( StrLen( numDprt ) < 3 )
       numDprt = "0" + numDprt;
     end;
     key_current = String("rtnfopr#" + numDprt + recID + "00");

     query1 = string(" select T_APPLICATIONKIND1, T_APPLICATIONKEY1 " +
               " from dsb_casln_dbt " +
               " where T_APPLICATIONKIND2=3001 and T_APPLICATIONKEY2=\'" + key_current +
               "\' and T_REFVALUE2=0 ");

     rs1 = RsdRecordset( query1 );

     stat = rs1.moveNext();
   end;

   if( stat )
     kind_main = rs1.value(0);
     key_main  = rs1.value(1);

     query2 = String(" select SUBSTR(T_APPLICATIONKEY2,9,3) , SUBSTR(T_APPLICATIONKEY2,12) " +
             " from  dsb_casln_dbt " +
             " where T_APPLICATIONKIND1=" + kind_main + 
             " and T_APPLICATIONKEY1=\'"  + key_main  + "\'");

     rs2 = RsdRecordset( query2 );
     stat = true;
     while( stat and rs2.moveNext() )
       dprt  = int(rs2.value(0));
       rid   = int(rs2.value(1));

       query3 = String(" select * from drtnfopr_dbt "+
             " where T_NUMDPRT=" + dprt+ " and T_RECID=" +
             rid+ " and T_TABLENAME=\'sbtrast.dbt\'");

       rs3 = RsdRecordset( query3 );
       if( rs3.moveNext() )

         rtnfopr.numDprt = rs3.value("T_NUMDPRT");
         rtnfopr.recID   = rs3.value("T_RECID");

         GetEQ ( rtnfopr );

         SetRecordAddr( will_r, rtnfopr, 0, 2, false );
         stat = false;
       end;
     end;
   else
     println( "*** Не найдена запись связки по нефин.операции с ApplKey = ",
              key_current );
     Exit;
   end;

 end;
