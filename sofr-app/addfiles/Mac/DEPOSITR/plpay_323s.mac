/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Реестр ф.323 для Сбербанка РФ

  Программист: Стольников С.В.

*****************************************************************/

import CommonInter, DeprIntr, OperCode, rsd;

var {curdate};

    // настраиваемые параметры
    var LinePerPage = 20; /* количество записей на один лист реестра. МОЖНО НАСТРОИТЬ */
    var sPlace = "Должность (настройте изменив константу sPlace в файле plpay_323s.mac)"; /* Должность которая будет написана в подвале отчёта. МОЖНО НАСТРОИТЬ */

var ReestrSum = $0;   // Сумма по реестру
var PageSum   = $0;     // Сумма по листу
var RecordsCount    = 0; // Количество записей по текущему получателю
var CurRecordInPage = 0; // Текущая запись по листу
var CurPage   = 0; // текущая страница реестра

file f_oper   ( person   ) key 0;
var sOper = "";     // Операционист

var sWhere = "";      // куда платёж (наименование организации)
var sFullRecv = "";   // полные реквизиты получателя платежа

var FromDate=date;
var ToDate=date;
var rs_period:RsdRecordSet;

var sQuery = "";

var i = 0; // счётчик иттератора

var party;

const  /* Периодичность планового перечисления */
  TERM_DAY   = 1,  /* в днях */
  TERM_MONTH = 2,  /* в месяцах */
  TERM_MON3  = 3,  /* в кварталах */
  TERM_YEAR  = 4;  /* в годах */

var Day, Month, Year; // для хранения даты в разобранном виде при вычислении периода платежа

// целочисленное деление
macro MATH_DIV (what,base)
    RETURN abs(int(what/base));    
end;

// взятие по модулю
macro MATH_MOD (what,base)
    var temp_div = MATH_DIV(what, base);
    RETURN what - (temp_div * base);
end;

// печать одной строки отчёта
macro PRINT_LINE(rs:RsdRecordSet, CurRecNum)
    /* Вычиляем период платежа */
    /* В соответствие с решением проектироващика:
       Логика формирования периода платежа следующая - ищем есть ли предыдущие платежи, но отбираем только один,
       т.е. ближайший предыдущий. Если предыдущий есть то левую границу периода берём из него. Если предыдущего нет,
       то левую границу берём из поля start_datepay ДП.
       Правая гарница периода барётся из обрабатываемого платежа.
    */
    sQuery = "";
    sQuery = sQuery + " SELECT dep.T_DATE_DOCUMENT ";
    sQuery = sQuery + " FROM dsbdepdoc_dbt dep ";
    sQuery = sQuery + " WHERE dep.T_LISTTRANSFER = " + String(rs.value("t_ref"));
    sQuery = sQuery + "       AND (dep.T_DATE_DOCUMENT < TO_DATE('" + String({curdate}) + "','DD MM YYYY') OR (dep.T_DATE_DOCUMENT = TO_DATE('"+String({curdate})+"','DD MM YYYY') AND dep.T_NUMDAYDOC < "+rs.value("t_numdaydoc")+"))";
    sQuery = sQuery + "       AND rownum<2";
    rs_period = RsdRecordSet(sQuery);

    if (rs_period.MoveNext) // если есть предыдущий
        DtTmSplit(rs_period.value(0), FromDate, 0);
    else // если нет предыдущего, то это первый платёж по ДП
        DtTmSplit(rs.value("t_start_datepay"), FromDate, 0);
    end;
    DtTmSplit(rs.value("t_date_document"),ToDate,0);    
   
    // Печатаем
    println("│",CurRecNum:5:c,"│", rs.value("t_name1")+" "+rs.value("t_name2")+" "+rs.value("t_name3"):74:c,"│", rs.value("T_SZNAMEALG"):30:c,"│", "c "+Trim(String(FromDate))+" по "+Trim(String(ToDate)):26:c,"│", Acc_Form(String(rs.value("t_account"))):25:c,"│", rs.value("t_outsum"):25:c,"│");
    if (CurRecNum<RecordsCount)
        println("├─────┼──────────────────────────────────────────────────────────────────────────┼──────────────────────────────┼──────────────────────────┼─────────────────────────┼─────────────────────────┤");        
    else
        println("├─────┴──────────────────────────────────────────────────────────────────────────┴──────────────────────────────┴──────────────────────────┴─────────────────────────┼─────────────────────────┤");        
    end;
end; // end macro PRINT_LINE

/* Чтение записи Операциониста */
f_oper.Oper = GetOper;
if ( GetEQ( f_oper ) )
    sOper = f_oper.Name;
else
    sOper = "???";
end;

var sBranchName:string;
var sDepartment;
var sFullBranchName:string;

/* Поиск записи о подразделении */
if (findDepartment(NumFNcash, sBranchName, sDepartment, party) )
    sFullBranchName = Trim( party.rec.Name ) + " №" +  Trim(String(NumFNcash)) + "/" + Trim( sBranchName );
else
    sFullBranchName = "???";
    sBranchName = "???";
end;

// главная процедура
macro PlanPay_323s()
    var NumOfPages  = 0;

    // выбираем список получателей по которым нужно составлять реестры
    var sQuery =       "SELECT DISTINCT pp.T_CP_RECIP, payrecv.T_SZRECIPIENTNAME, payrecv.T_BANKNAME, payrecv.T_MFO, payrecv.T_CORRACCOUNT, payrecv.T_ACCOUNT";
    sQuery = sQuery + " FROM ddplanpay_dbt pp, dsbdepdoc_dbt dep, dpay_recv_dbt payrecv";
    sQuery = sQuery + " WHERE pp.T_ACCOUNT = dep.T_ACCOUNT AND dep.T_LISTTRANSFER  = pp.T_REF";
    sQuery = sQuery +       " AND dep.T_DATE_DOCUMENT = TO_DATE('"+String({curdate})+"','DD MM YYYY')";
    sQuery = sQuery +       " AND payrecv.t_recipientid = pp.T_CP_RECIP AND pp.T_CP_RECIP <> 0;";
    
    var rs_recip = RsdRecordSet(sQuery);
    var rs_pp: RsdRecordSet;
    var rs_tmp:RsdRecordSet;

    while (rs_recip.MoveNext)
        // Выбираем список платежей
        sQuery = "";    /* выборка   Фамилия      Имя          Отчество    Вид платежа       счёт     сумма по док-ам   */
        sQuery =           "SELECT per.t_name1, per.t_name2, per.t_name3, pk.T_SZNAMEALG, pp.T_ACCOUNT, dep.T_OUTSUM, pp.t_ref, dep.t_numdaydoc, pp.t_start_datepay, dep.t_date_document";
        sQuery = sQuery + " FROM ddplanpay_dbt pp, dsbdepdoc_dbt dep, dpay_recv_dbt payrecv, dpersn_dbt per, dpay_kind_dbt pk ";
        sQuery = sQuery + " WHERE pp.T_ACCOUNT = dep.T_ACCOUNT AND dep.T_LISTTRANSFER  = pp.T_REF ";
        sQuery = sQuery +       " AND dep.T_DATE_DOCUMENT = TO_DATE ('"+String({curdate})+"','DD MM YYYY')";
        sQuery = sQuery +       " AND payrecv.t_recipientid = pp.T_CP_RECIP AND pp.T_CP_RECIP <> 0";
        sQuery = sQuery +       " AND per.T_PERSONID = pp.T_CODCLIENT";
        sQuery = sQuery +       " AND pp.T_CP_RECIP = "+String(rs_recip.value("T_CP_RECIP"));
        sQuery = sQuery +       " AND pk.T_NUMOPERAT = pp.T_CP_KIND AND pp.T_ISCUR = pk.T_ISCUR AND pk.T_GROUPOPERT = 1;";
        
        rs_pp = RsdRecordSet(sQuery);
        // считаем сумму по реестру, а так же количество записей
        while (rs_pp.MoveNext)
            RecordsCount = RecordsCount + 1;
            ReestrSum = ReestrSum + rs_pp.value("t_outsum");
        end; // end of while (!rs_pp.MoveNext)
        rs_pp = RsdRecordSet(sQuery);
                
        // вычисляем количество листов в реестре
        NumOfPages = MATH_DIV(RecordsCount,LinePerPage);   // кол-во полных листов
        if (MATH_MOD(RecordsCount,LinePerPage)!=0)        // есть ли неполный лист?
            NumOfPages = NumOfPages + 1;             // 
        end;

        while (CurPage<NumOfPages)  // Начинаем новую страницу?
            CurPage = CurPage + 1;
            // Выводим шапку
            sWhere = "в "+rs_recip.value("T_SZRECIPIENTNAME");
            sFullRecv = rs_recip.value("t_bankname")+", БИК "+rs_recip.value("t_mfo")+", корр/сч "+Acc_Form(String(rs_recip.value("T_CORRACCOUNT")))+", счёт№ "+Acc_Form(String(rs_recip.value("T_ACCOUNT")));

            // Сообщаем об ошибках
            i = 0;
            while( i<RslDefEnv.ErrorCount )
                [error № ##: #####################################](i+1, RslDefEnv.error(i).descr);
                i=i+1;
            end;

            [   
                                                                                                                                                                            ф.№323-с
                                                                                                                        
                                                                                    РЕЕСТР-ИЗВЕЩЕНИЕ О ПЕРЕЧИСЛЕННЫХ ПЛАТЕЖАХ
            ####################################################################################################################################################################################
            ####################################################################################################################################################################################                  
            
            ]
            (sWhere:c,
            sFullRecv:c);
            
            println(sFullBranchName, {curdate}:120:r:m);
            
            if (NumOfPages>1)
                [                                                                                                                                                                           Лист: ### Листов: ###]
                (String(CurPage),String(NumOfPages));
            end;
            [
            ┌─────┬──────────────────────────────────────────────────────────────────────────┬──────────────────────────────┬──────────────────────────┬─────────────────────────┬─────────────────────────┐
            │  №  │                                 Фамилия, Имя                             │           Вид платежа        │      Период платежа      │     Номер лицевого      │         Сумма           │
            │ п/п │                                   Отчество                               │                              │                          │        счёта            │                         │
            ├─────┼──────────────────────────────────────────────────────────────────────────┼──────────────────────────────┼──────────────────────────┼─────────────────────────┼─────────────────────────┤
            ];
           /*##### ########################################################################## ############################## ########################## ######################### ######################### */ // примерочная :)
            
            //Начинаем цикл вывода записей по листу
            while ((CurRecordInPage < LinePerPage) AND rs_pp.MoveNext) // можем вывести ещё запись и она влезет на страницу?       
                CurRecordInPage = CurRecordInPage + 1;
                
                PRINT_LINE(rs_pp, (CurPage-1)*LinePerPage+CurRecordInPage);
 
                PageSum = PageSum + rs_pp.value("t_outsum");
            end;
            //Пишем сумму по листу и по реестру
            [│                                                                                                                                                    Итого по листу: │#########################│
            ├────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┼─────────────────────────┤
            │                                                                                                                                                  Итого по реестру: │#########################│
            └────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┴─────────────────────────┘                                                                                
            
        
            ](String(PageSum):c, String(ReestrSum):c); 

            println(sPlace+", "+sOper+"_________________________":190:r);
            println(                            "Подпись        ":190:r);
            println();
            println(              "            М.П.         ":190:r);
            print("");
            CurRecordInPage = 0;
            PageSum = $0;
        end; // end of while (CurPage<NumOfPages) 
        
        ReestrSum = $0;
        RecordsCount = 0;
        NumOfPages = 0;    
    end; // end of while (!rs_recip.MoveNext)

END; //end of macros