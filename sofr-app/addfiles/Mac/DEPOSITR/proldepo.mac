/*****************************************************
* ---------------------                              *
* R-Style SoftWare Lab.                              *
* ---------------------                              *
* RS-Retail (СберБанк)                               *
*                                                    *
* Проверка количества пролонгаций счета              *
*                                                    *
* 13.07.99                                           *
*****************************************************/
import deprintr;

  record ALG_DEPOSITOR("depositr.dbt");
  var QualProl = 9999;  /* Максимальное число пролонгаций счета */
  var DateProl;
  if ((ALG_DEPOSITOR.Type_Account == "Ср.пенс.6мес")
   OR (ALG_DEPOSITOR.Type_Account == "СБЕРЕГ.6МЕС."))
    QualProl = 2;
  elif ((ALG_DEPOSITOR.Type_Account == "Особ.н.1г_1м")
     OR (ALG_DEPOSITOR.Type_Account == "Ср.пен.1г1м")
     OR (ALG_DEPOSITOR.Type_Account == "СБ-501"))
    QualProl = 1;
  end;

/*************************************************************
                    Последний день месяца
*************************************************************/
macro LastDay
(
 m,     /* Месяц        */
 y      /* Год          */
)
  var d;
  m = m + 1;
  if (m > 12)
    m = 1;
    y = y + 1;
  end;
  DateSplit (Date(1,m,y)-1,d);
  return d;
end;

/*************************************************************
                   Определение даты
*************************************************************/
const R_DAY    = 1;        /* Дней         */
const R_MON    = 2;        /* Месяцев      */
const R_QW     = 3;        /* Кварталов    */
const R_YEAR   = 4;        /* Лет          */
const R_CALDAY = 5;        /* Календарных дней */

macro SumDate
(
 BegDate,       /* Дата, к которой добавляем            */
 Term,          /* Добавляемое значение (>0 или <0)     */
 KindTerm       /* Вид срока                            */
)

  var ResDate = Date(0,0,0);
  var d,m,y;

  if (KindTerm == R_QW)
    Term = Term * 3;
    KindTerm = R_MON;
  end;
  DateSplit (BegDate,d,m,y);
  if (KindTerm == R_YEAR)
    y = y + Term;
    if ((m == 2) AND (d > 28))
      d = LastDay (m,y);
    end;
    ResDate = Date(d,m,y);
  elif (KindTerm == R_DAY)
    ResDate = BegDate + Term;
  elif (KindTerm == R_CALDAY)
    ResDate = BegDate + Term;
  elif (KindTerm == R_MON)
    m = m + Term;
    while ((m < 1) OR (m > 12))
      if (m < 1)
        m = m + 12;
        y = y - 1;
      elif (m > 12)
        m = m - 12;
        y = y + 1;
      end;
    end;
    while (d > LastDay(m,y))
      d = d - 1;
    end;
    ResDate = Date(d,m,y);
  end;

  return ResDate;

end;


/*************************************************************
             Г Л А В Н А Я   П Р О Г Р А М М А
*************************************************************/

  var i = 0;

  DateProl = ALG_DEPOSITOR.Start_DateDep;

  while ( i <= QualProl )
/*
    DateProl = SumDate( DateProl,
                        ALG_DEPOSITOR.Term_Prol,
                        ALG_DEPOSITOR.KindTerm_Prol );
*/
    GetDateOff (DateProl,
                ALG_DEPOSITOR.KindTerm_Prol,
                ALG_DEPOSITOR.Term_Prol,
                DateProl);
    i = i + 1;
  end;

  if ( ALG_DEPOSITOR.End_DateDep >= DateProl )
    ALG_DEPOSITOR.FormContr = 20;  /* Депозитный */
    ALG_UPDATE = 1;
  end;

  ALG_RESULT = 1;

end;
