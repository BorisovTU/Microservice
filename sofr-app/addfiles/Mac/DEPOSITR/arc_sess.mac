/*
   Для загрузки сессий со 190 сборки помимо стандартной постановки
   нужно дополнительно:
   - Каталог ..\DBFILE\OLD_DEFS, содержащий словари SBBANK.DEF, SPCARD.DEF
     и EXCHANGE.DEF сборки 2.01.190
   - Каталог ..OBJ\CONVERT, содержащий BTCODER.EXE и BTDECIM.EXE (не старше
     патча 18 к сборке 5.10.068 или патча 6 к сборке 5.10.071), а также DLL
     для конвертации некоторых файлов из конвертора на новые деньги -
     DEPDOC.DLL, PAY_DOC.DLL, AUXINFO.DLL, SCACC.DLL и т.п.
*/


import deprintr;
import "issrvdoc.mac";
import archiver;
import cryptdlm;

/* Параметр, настраиваемый Вами. 
   Если TRUE, то будет выдаваться запрос о необходимости удаления архива сессии
   Если FALSE, то удаление будет производиться без запроса
*/
var ЗАПРАШИВАТЬ_ПОДТВЕРЖДЕНИЕ_НА_УДАЛЕНИЕ_АРХИВА = TRUE;

const
  BankVersion = BankVer(),
  Archiver = TArchiver, 
  ArcExt = "." + Archiver.GetArchiveExt,
  ArcMask = "*" + ArcExt,
  ReplExt = ".xml";

var
  {CNum},
  {SpoolerIsRunning},
  {BatchMode}, 
  TextDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", false ),
  ImportDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\IMPORTMESDIR", false ),
  ExportDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\EXPORTMESDIR", false ),
  writeESign = true,
  ComSpec = GetEnv( "COMSPEC" );


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro DefShortFileName ( fname )

  var outFileName = fname;
  var posDelit = Index( outFileName, "\\" );

  while ( posDelit > 0 )
    outFileName = SubStr( outFileName, posDelit + 1 );
    posDelit = Index( outFileName, "\\" );
  end;

  outFileName = StrUpr( outFileName );

  return outFileName;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro ESignFiles ( pathFiles, nameFile, toArchive )

  var cryptoAPI = RsCryptoAPI( );
  var lenPath = StrLen( pathFiles );
  var context;
  var retValue = true;

  if (toArchive)
    context = "RS-Retail|Транспорт|ПередачаФайла";
  else
    context = "RS-Retail|Транспорт|ПриемФайла";
  end;
  
  if (writeESign)
    writeESign = cryptoAPI.IsCryptoActionNeeded(context, 0);
  end;
  
  if ( not writeESign )
    return retValue;
  end;

  if ( lenPath > 0 )
    if ( SubStr( pathFiles, lenPath ) != "\\" )
      pathFiles = pathFiles + "\\";
    end;
  end;

  if ( not ExistFile( pathFiles + nameFile ) )
    return retValue;
  end;

  if ( toArchive )
    if ( cryptoAPI.SignFile( context, pathFiles + nameFile ) )
      ;
    else
      retValue = false;
    end;
  else
    if ( cryptoAPI.CheckFileSign( context, pathFiles + nameFile ) )
      ;
    else
      retValue = false;
    end;
  end;

  return retValue;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ArcSession( Direction, Branch, SessionNum, expDir)

var
  Mask,
  ArcName = SubStr( Direction, 1, 2 ) + "00000000",
  BrStr = String( Branch ),
  NumStr = String( SessionNum ),
  Pwd = String( SessionNum * Branch ),
	UserExportDir;
  
  if (expDir=="")
  UserExportDir = ExportDir;
  else
  	 UserExportDir = expDir;
  end;

  StrSet( ArcName, 7 - StrLen( BrStr ), BrStr );
  StrSet( ArcName, 11 - StrLen( NumStr ), NumStr );

  Mask = ArcName + ReplExt;

  if ( ( not {SpoolerIsRunning} ) and ( not {BatchMode} ) )
     GetString( UserExportDir, "Путь для выгрузки" );
  end;

  if ( not ESignFiles( ExportDir, Mask, true ) )
    MsgBox( "Ошибка при простановке ЭЦП|Сессия будет удалена" );
    DelFile( ExportDir + "\\" + Mask );
    return false; /* Не прошла ЭЦП - не архивируем */
  end;

  Archiver.SetArchiveDir( UserExportDir );
  Archiver.SetDataDir( ExportDir );
  Archiver.SetLogFileName( TextDir + "\\archlog." + String( {CNum} ) );
  if ( Archiver.ArchiveFiles( Mask, ArcName, Pwd ) )
    DelFile( ExportDir + "\\" + Mask );
  end;

  expDir=UserExportDir;
  return true;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ExtrSess( Fname )

  var
    Branch,
    SessionNum,
    Name,
    NewMoney = false,
    nameSess = Fname,
    Pwd;

  Branch = Int( SubStr( Fname, 3, 4 ) );
  SessionNum = Int( SubStr( Fname, 7, 4 ) );
  Pwd = String( Branch * SessionNum );

  /* Распаковываем */
  Archiver.SetArchiveDir( ImportDir);
  Archiver.SetDataDir( ImportDir);
  Archiver.SetLogFileName( TextDir + "\\archlog." + String( {CNum} ) );
  Archiver.ExtractAll( Fname, Pwd );

  if ( Index( nameSess, "." ) > 1 )
    nameSess = SubStr( nameSess, 1, Index( nameSess, "." ) - 1 );
  end;
  nameSess = StrUpr( nameSess + ReplExt );
  if ( not ESignFiles( ImportDir, nameSess, false ) )
    DelFile( ImportDir + "\\" + nameSess );
    MsgBox( "Ошибка при проверке ЭЦП|Сессия " + nameSess + " будет удалена, оставлен только архив" );
    return; /* Не прошла ЭЦП - не архивируем */
  end;

  if( {SpoolerIsRunning} or {BatchMode} )
    ЗАПРАШИВАТЬ_ПОДТВЕРЖДЕНИЕ_НА_УДАЛЕНИЕ_АРХИВА = FALSE;
  end;
  if( not ЗАПРАШИВАТЬ_ПОДТВЕРЖДЕНИЕ_НА_УДАЛЕНИЕ_АРХИВА 
      or GetTrue( False, "Удалять архив сессии |" + Fname ) )
    DelFile( ImportDir + "\\" + Fname );
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ExtrSessions

  var
    Stat = true,
    Done = false,
    UserImportDir = ImportDir;
  var listFiles = TRSLListFiles;
  var flagListFiles;

  if ( ( not {SpoolerIsRunning} ) and ( not {BatchMode} ) )
    GetString( UserImportDir, "Путь к файлам загрузки" );
    Run( ComSpec, "/C cls " );
    Run( ComSpec, "/C copy " + UserImportDir + "\\" + "*.* " + ImportDir );
  end;

  listFiles.MaskFile = ImportDir + "\\" + ArcMask;
  listFiles.DirInList = false;

  flagListFiles = listFiles.First;
  while ( flagListFiles )
    ExtrSess( DefShortFileName( listFiles.FileName ) );
    flagListFiles = listFiles.Next;
  end;

  if ( not Stat )
    if ( ( not {SpoolerIsRunning} ) and ( not {BatchMode} ) )
      MsgBox( "Ошибка при разархивации" );
    end;
  end;

end;

end;
