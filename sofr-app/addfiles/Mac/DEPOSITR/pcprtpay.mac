/*               

  RS-Retail
  Обслуживание физических лиц
  Модуль # 3302  (=== Макет ===)

   Операция # 72 (Причисление процентов)
   Шаг # 11 (Проверка перед зачислением %%)
   Частичная капитализация процентов- 
 определение суммы процентов и налога к оплате
------------------------------------------------------------
  Входные параметры:
    ALG_SBDEPDOC.DepDate_Document - дата проведения операции
  Выходные параметры:
    ALG_PARAM.prmI - признак того, что заполнена структура процентов к оплате
                     не 0- структура заполнена 
                     Параметры налога
    ALG_PARAM.prmI2   - условия, по которым расчитан налог (по умолчанию- 2001)
    ALG_PARAM.prmD    - сумма налога к оплате
    ALG_PARAM.prmDate - дата конца периода налоговой карточки
                        (в этот период попадет сумма оплаты prmD)
    ALG_SBDEPDOC - структура sbdepdoc.2 
         Record sbdepdoc2 ("sbdepdoc.2");
         file   TempFile   ("sbdepdoc.dbt");
         Copy (TempFile,ALG_SBDEPDOC);
         SetRecordAddr (sbdepdoc2,TempFile,0,0,TRUE);
          . . .
         Copy (ALG_SBDEPDOC,TempFile);
      Заполняются следующие поля (сумма и дата конца периода по каждому условию):
                             - по основным условиям
      sbdepdoc2.GlobPerc     - сумма оплачиваемых процентов
      sbdepdoc2.GlobDatePay  - дата конца периода по PcCalc
                             - по альтернативным условиям
      sbdepdoc2.AltPerc
      sbdepdoc2.AltDatePay
                             - по овердрафту
      sbdepdoc2.OverPerc
      sbdepdoc2.OverDatePay
                             - по дополнительным взносам
      sbdepdoc2.DopPerc
      sbdepdoc2.DopDatePay
  
  Если ALG_PARAM.prmI = 0, проценты причисляются полность в соответствии
 с данными из PcCalc. При причислении процентов идет расчет налога на
 мат.выгоду и причисление налога.
  Если ALG_PARAM.prmI = 1, проценты причисляются в соответствии со структурой
 сумм к оплате. При причислении процентов налог не считается (расчет налога
 необходимо вызвать из макроса функцией "Расчет_Налога_По_Счету_Вкладчика").
 При этом, если задана сумма prmD, списывается налог только на эту сумму,
 в противном случае налог не списывается (необходимо в макросе
 вставить документы на списание налога в список DDList,
 используя функцию InsertDocToGDDList.
------------------------------------------------------------
  30.05.2001
  Ромодин Александр Васильевич
*/
import alg_vars,DeprIntr;

const PartPerc = 20.;  /* Доля оплаты (в процентах от суммы к оплате) */

ALG_RESULT = OK_RES;  /* Оптимистично считаем, что все будет хорошо */

File PcCalc (pc__calc) key 0; /* График расчета     */
File PcTax  (pc_tax)   key 0; /* Налоговая карточка */

Record sbdepdoc2 ("sbdepdoc.2");  /* Структура сумм процентов к оплате */
file   TempFile  ("sbdepdoc.dbt");
Copy (TempFile,ALG_SBDEPDOC);
SetRecordAddr (sbdepdoc2,TempFile,0,0,TRUE);

/**********************************************************/

macro Сумма_процентов_к_оплате ()
/*
  Определение суммы процентов к оплате и заполнение структуры
 к оплате
*/
  var ObjectType = 2001; /* Сумму к оплате определяем по всем условиям */
  var st;
  var NumStep;
  var SumPay:money;
  while (ObjectType <= 2004)
    NumStep = 0;
    PcCalc.Referenc   = ALG_DEPOSITOR.Referenc;
    PcCalc.ObjectType = ObjectType;
    PcCalc.TypeRecord = 1;
    PcCalc.EndDate    = sbdepdoc2.DepDate_Document;
    st = GetLE(PcCalc);
    while (st
     AND (PcCalc.Referenc   == ALG_DEPOSITOR.Referenc)
     AND (PcCalc.ObjectType == ObjectType)
     AND (PcCalc.TypeRecord == 1) 
     AND (PcCalc.SumPay     == 0) ) /* Не было оплаты за период */
      NumStep = NumStep + 1;
      SumPay = PcCalc.SumCalc * PartPerc / 100.; /* Сумма к оплате- процент от расчитанного */
      if (ObjectType == 2001)
        if (NumStep == 1)
          sbdepdoc2.GlobPerc    = Money(SumPay);
          sbdepdoc2.GlobDatePay = PcCalc.EndDate;
        else
          sbdepdoc2.GlobPerc    = sbdepdoc2.GlobPerc + SumPay;
        end;
      elif (ObjectType == 2002)
        if (NumStep == 1)
          sbdepdoc2.AltPerc    = SumPay;
          sbdepdoc2.AltDatePay = PcCalc.EndDate;
        else
          sbdepdoc2.AltPerc    = sbdepdoc2.AltPerc + SumPay;
        end;
      elif (ObjectType == 2003)
        if (NumStep == 1)
          sbdepdoc2.OverPerc    = SumPay;
          sbdepdoc2.OverDatePay = PcCalc.EndDate;
        else
          sbdepdoc2.OverPerc    = sbdepdoc2.OverPerc + SumPay;
        end;
      elif (ObjectType == 2004)
        if (NumStep == 1)
          sbdepdoc2.DopPerc    = SumPay;
          sbdepdoc2.DopDatePay = PcCalc.EndDate;
        else
          sbdepdoc2.DopPerc    = sbdepdoc2.DopPerc + SumPay;
        end;
      end;
      st = Prev (PcCalc);
    end;
    ObjectType = ObjectType + 1;
  end;
end;
/**********************************************************/

macro Сумма_налога_к_оплате
/*
  Определение суммы налога на материальную выгоду к оплате
*/
  var st;
  var NumStep;
  var SumPay:moneyl;

  ALG_PARAM.prmI2 = 2001; /* По основным условиям */
  NumStep = 0;
  PcTax.CodeModule    = 2;
  PcTax.ObjectTax     = String(ALG_DEPOSITOR.Referenc);
  PcTax.ObjectTax_Add = 0;
  PcTax.DateTaxPay    = sbdepdoc2.DepDate_Document;
  st = GetLE(PcTax);
  while (st
     AND (PcTax.CodeModule    == 2)
     AND (PcTax.ObjectTax     == String(ALG_DEPOSITOR.Referenc))
     AND (PcTax.ObjectTax_Add == 0)
     AND (PcTax.SummaTaxPay   == 0)   /* Не было оплаты за период  */
     AND (PcTax.Year_InAll    == 0) ) /* Не итоговая за год запись */
    NumStep = NumStep + 1;
    SumPay = PcTax.SummaTaxCalc * PartPerc / 100.; /* Сумма к оплате- процент от расчитанного */
    if (NumStep == 1)
      ALG_PARAM.prmD    = SumPay;
      ALG_PARAM.prmDate = PcTax.DateTaxPay;
    else
      ALG_PARAM.prmD    = ALG_PARAM.prmD + SumPay;
    end;
    st = Prev (PcTax);
  end;
end;
/***********************************************************
 Точка входа в макрос 
***********************************************************/

  var stat;
  var TaxSum = $0l;

  ALG_PARAM.prmI = 1;  /* Признак того, что суммы определяем сами */
/*
  Определение суммы процентов к оплате
*/
  Сумма_процентов_к_оплате ();
/* 
  Расчет налога на материальную выгоду
*/
  if (ALG_RESULT == OK_RES)
    stat = Расчет_Налога_По_Счету_Вкладчика (
      ALG_DEPOSITOR.Referenc,     /* Счет                           */
      2001,                       /* По основным условиям           */ 
      Date(0,0,0),                /* От последней капитализации     */
      sbdepdoc2.DepDate_Document, /* До даты операции               */
      True,                       /* Заполнять налоговую карточку   */
      False,                      /* Без чистки значений за периоды */
      TaxSum,                     /* Выход- расчитанный налог       */
      False, False,               /* Уточнять даты начала и конца   */ 
      False,                      /* По данным Филиала              */
      False );                    /* Только если по виду вклада надо считать налог */
    if (stat != 0)
      MsgBox ("При расчете налога допущена ошибка "+String(stat));
      ALG_RESULT = ERR_RES;
    end;
  end;
/* 
  Определение суммы налога к оплате (к списанию)
*/
  if (ALG_RESULT == OK_RES)
    Сумма_налога_к_оплате ();
  end;

  if (ALG_RESULT == OK_RES)
    Copy (ALG_SBDEPDOC,TempFile);
    ALG_UPDATE = 1;  /* Чтобы суммы, вставленные в sbdepdoc2 дошли до программы */
  end;
end; /* Конец головного макроса */
/* ===== Конец файла =====*/
