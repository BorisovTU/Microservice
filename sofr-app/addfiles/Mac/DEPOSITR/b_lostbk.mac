/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Книга регистрации утраченных сберкнижек ф.39-с.

*****************************************************************/

import DeprIntr, RSD, SQLConv, SQLExec;

var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

/* Состояние заявления об утрате сбер.книжки. */
const LBDS_ANY         = 0,  /* Любое      */
      LBDS_INITIALIZED = 1,  /* Введено    */
      LBDS_PROCESSED   = 2,  /* Обработано */
      LBDS_CANCELLED   = 3;  /* Отменено   */

/* Вид обработки заявления об утрате сбер.книжки. */
const LBDP_UNDEF     = 0,  /* Не определен                */
      LBDP_CASHCLOSE = 1,  /* Закрыть счет наличными      */
      LBDP_TRANSFER  = 2,  /* Перечислить остаток на счет */
      LBDP_REISSUE   = 3;  /* Выдать дубликат сб/кн       */

var opr = TBFile( "person.dbt",   "r", 0 );
var dep = TBFile( "depositr.dbt", "r", 8 );

var DepClnt = TClientList; /* Справочник вкладчиков */

/**************************************************************************/
/*                   Получение номера подразделения.                      */
/**************************************************************************/
macro BranchNumber( pBranch )
 var sFilial;
 if ( not findDepartment(pBranch, sFilial) )
   sFilial = "???";
 end;
 return sFilial;
end;

/**************************************************************************/
/*                   Чтение записи счета по Reference.                    */
/**************************************************************************/
macro FindAccount( pReference )  
 var stat = true;  
 dep.Clear();
 dep.rec.Referenc = pReference;
 stat = dep.getEQ();
 return stat;
end;

/**************************************************************************/
/*                   Получение Ф.И.О. клиента по коду.                    */
/**************************************************************************/
macro getFIO( pClient )
 var sFIO_Declarer = "???";
 if ( DepClnt.GetRecord( pClient ) )
   sFIO_Declarer = DepClnt.CurRec.rec.Name1 + " " +
                   DepClnt.CurRec.rec.Name2 + " " +
                   DepClnt.CurRec.rec.Name3;
 end;
 return sFIO_Declarer;
end;

/**************************************************************************/
/*               Атрибуты удостоверения личности клиента.                 */
/**************************************************************************/
macro getPaperClient()
 return GetNameOfPAPRKIND( DepClnt.CurRec.rec.PaperKind ) + " " +
        DepClnt.CurRec.rec.PaperSeries + " № " +
        DepClnt.CurRec.rec.PaperNumber + ", выдан " +
        DepClnt.CurRec.rec.PaperIssuer + ", " +
        DepClnt.CurRec.rec.PaperIssuedDate;
end;

/**************************************************************************/
/*                   Чтение записи операциониста.                         */
/**************************************************************************/
macro OperFIO( pNum )
 var sOper = "???";
 if ( pNum > 0 )
   opr.rec.Oper = pNum;
   if ( opr.GetEQ )
     sOper = String( pNum ) + " " + opr.rec.Name;
   end;
 else
   sOper = "";
 end;
 return sOper;
end;

/**************************************************************************/
/*     Выпуск отчета "Книга регистрации утраченных сберкнижек ф.39-с".    */
/**************************************************************************/
macro printReport( Branch, minDate, maxDate, ClientCode )

 var cmd, rs;

 var Branch1, Branch2;
 var ClientCode1, ClientCode2;

 var vDateClose, vRest;
 var vDateProc,  vNumDubl;
 var vOperProc;
 var vDateCanc,  vOperCanc;

 var vFIO_Client;
 var InfoClient;

 var vCountRec;
 var vRec = 0;
 var Account = "???";

 if ( Branch < 0 )
   Branch1 =     0;
   Branch2 = 32766;
 else
   Branch1 = Branch;
   Branch2 = Branch;
 end;

 if ( ClientCode < 0 )
   ClientCode1 =          0;
   ClientCode2 = 2147483646;
 else
   ClientCode1 = ClientCode;
   ClientCode2 = ClientCode;
 end;

 cmd = RsdCommand( "select t_Number as Num, " +
                          "t_DateRegistered as DateReg, " +
                          "t_Branch as Branch, " +
                          "t_Reference as RefAcc, " +
                          "t_ClientCodeAcc as Client, " +
                          "t_RegisteredByOper as OperReg, " +
                          "t_DateProcessed as DatePrc, " +
                          "t_ProcessedByOper as OperPrc, " +
                          "t_Rest as RestAcc, " +
                          "t_BookCopyNo as NumDbl, " +
                          "t_DateCancelled as DateCncl, " +
                          "t_CancelledByOper as OperCncl, " +
                          "t_State as State, " +
                          "t_Processing as Proc " +
                   "from dlostbook_dbt " +
                   "where t_Branch >= ?  and  t_Branch <= ?  and " +
                         "t_DateRegistered between " + sqlDateToStr( minDate ) +
                           " and " + sqlDateToStr( maxDate ) + "  and " +
                         "t_ClientCodeAcc >= ?  and  t_ClientCodeAcc <= ? " +
                   "order by t_Number" );

 cmd.addParam( "branch1", RSDBP_IN );
 cmd.addParam( "branch2", RSDBP_IN );

 cmd.addParam( "client1", RSDBP_IN );
 cmd.addParam( "client2", RSDBP_IN );

 cmd.value( "branch1" ) = Branch1;
 cmd.value( "branch2" ) = Branch2;

 cmd.value( "client1" ) = ClientCode1;
 cmd.value( "client2" ) = ClientCode2;

 cmd.execute;

 rs = RsdRecordset( cmd );

 [                                                                                                                                                                                                                                       Ф.№ 39-с

                                                                                                  КНИГА ДЛЯ РЕГИСТРАЦИИ УТРАЧЕННЫХ СБЕРЕГАТЕЛЬНЫХ КНИЖЕК

   +----------------------+-----------+------------+-----------------------------+----------------------------+-----------------------------+--------------+------------+----------------+------------+--------------+--------------+--------------+-------------+
   |        Рег. №        |   Дата    |  № структ. |         Номер счета         |            Ф.И.О.          |            Данные           | Ф.И.О. и код |    Дата    |     Сумма,     |    Дата    |  Порядк. №   | Ф.И.О. и код |  Дата подачи | Ф.И.О. и код|
   |        заявл.        |  поступ-  | подраздел.,|                             |          вкладчика         |           паспорта          |  контролера, |  закрытия  |    выплач.     |   выдачи   |  выданного   |  контролера, | заявления об |  контролера,| 
   |         об           |   ления   |    где     |                             |                            |           вкладчика         |  принявшего  |   счета    |      нал.      |  дублика-  |  дубликата   |  оформившего | отмене ранее |  принявшего |
   |       утрате         |  заявл.   | зарегистр. |                             |                            |                             |   заявление  |            |    деньгами,   |  та сб/кн  |    сб/кн     |  операцию по |  составлен.  |  заявл. об  |
   |        сб/кн         |           | заявл. об  |                             |                            |                             |              |            |      либо      |            |              |закрытию счета|   заявл. об  | отмене ранее|
   |                      |           |утрате сб/кн|                             |                            |                             |              |            |    перечисл.   |            |              |     либо     | утрате сб/кн |   составл.  |
   |                      |           |            |                             |                            |                             |              |            |   на др. счет  |            |              |   выдавшего  |   ввиду ее   |  заявл. об  |
   |                      |           |            |                             |                            |                             |              |            |                |            |              |дубликат сб/кн|  обнаружения | утрате сб/кн|
   |                      |           |            |                             |                            |                             |              |            |                |            |              |              |              |   ввиду ее  |
   |                      |           |            |                             |                            |                             |              |            |                |            |              |              |              | обнаружения |
   |----------------------+-----------+------------+-----------------------------+----------------------------+-----------------------------+--------------+------------+----------------+------------+--------------+--------------+--------------+-------------|
   |          1           |     2     |      3     |               4             |              5             |                6            |       7      |      8     |        9       |     10     |       11     |       12     |       13     |      14     |
   |----------------------+-----------+------------+-----------------------------+----------------------------+-----------------------------+--------------+------------+----------------+------------+--------------+--------------+--------------+-------------|];

 vCountRec = NRecordsSQL( "lostbook.dbt", "t.t_Branch >= " + String(Branch1) + " and " +
                                          "t.t_Branch <= " + String(Branch2) + " and " +
                                          "t.t_DateRegistered between " + sqlDateToStr( minDate ) + " and " +
                                                                          sqlDateToStr( maxDate ) + " and " +
                                          "t.t_ClientCodeAcc >= " + String(ClientCode1) + " and " +
                                          "t.t_ClientCodeAcc <= " + String(ClientCode2) );

 InitProgress( vCountRec, " Ж Д И Т Е ...", "Формирование отчета ..." );

 vRec = 0;

 while ( rs.moveNext )

   UseProgress( vRec );
   vRec = vRec + 1;

   if ( ( rs.value( "State" ) == LBDS_PROCESSED )  AND
        ( ( rs.value( "Proc" ) == LBDP_CASHCLOSE )  OR
          ( rs.value( "Proc" ) == LBDP_TRANSFER ) ) )
     vDateClose = String( rslDate( rs.value( "DatePrc" ) ) );
     vRest = String( rs.value( "RestAcc" ):a );
   else 
     vDateClose = "";
     vRest = "";
   end;

   if ( ( rs.value( "State" ) == LBDS_PROCESSED )  AND
        ( rs.value( "Proc" ) == LBDP_REISSUE ) )
     vDateProc = String( rslDate( rs.value( "DatePrc" ) ) );
     vNumDubl = rs.value( "NumDbl" );
   else
     vDateProc = "";
     vNumDubl = "";
   end;

   if ( rs.value( "State" ) == LBDS_PROCESSED )
     vOperProc = OperFIO( rs.value( "OperPrc" ) );
   else
     vOperProc = "";
   end;

   if ( rs.value( "State" ) == LBDS_CANCELLED )
     vDateCanc = String( rslDate( rs.value( "DateCncl" ) ) );
     vOperCanc = OperFIO( rs.value( "OperCncl" ) );
   else
     vDateCanc = "";
     vOperCanc = "";
   end;
   vFIO_Client = getFIO( rs.value( "Client" ) );
   InfoClient = getPaperClient();
   if(FindAccount( rs.value( "RefAcc" ) ))
      if(  (not SpecialAccess) and OperMayListSpecialAccounts and dep.rec.SpecialAccess)
         vFIO_Client = "XXXXXX X.X.";
         InfoClient = "XXXXXX";
      end;
      Account = String( Acc_Form(dep.rec.Account):f );
   end;

   [ | #################### | ######### | ########## | ########################### | ########################## | ########################### | ############ | ########## | ############## | ########## | ############ | ############ | ############ | ########### |]
   ( rs.value( "Num" ),
     String( rslDate( rs.value( "DateReg" ) ) ),
     BranchNumber( rs.value( "Branch" ) ),
     Account,
     vFIO_Client:w,
     InfoClient:w, 
     OperFIO( rs.value( "OperReg" ) ):w,
     vDateClose,
     vRest:r,
     vDateProc,
     vNumDubl,
     vOperProc:w,
     vDateCanc, 
     vOperCanc:w );
   [ |----------------------+-----------+------------+-----------------------------+----------------------------+-----------------------------+--------------+------------+----------------+------------+--------------+--------------+--------------+-------------|];
 end;

 RemProgress();

end;
