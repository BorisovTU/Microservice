/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Страхование частных вкладов.

  Выпуск реестра обязательств в печатной форме.

  Панель параметров выпуска реестра обязательств в печатной форме.

*****************************************************************/

import DeprIntr, DI_Def;

record dlg_prnt ( "DIL_PRNT", "rtusrmac" ) dialog;

var clnt = TClientList;

var {DI_LimitSum};

/* Переменные параметров для предварительного формирования реестра */
var panDateMake    = {curdate};      /* Дата формирования */
var panClient      = 0;              /* Код клиента */
var panLimitSum    = {DI_LimitSum};  /* Лимит обязательства */
var panNeedExcerpt = FALSE;          /* Флаг выписки */
var panEscape      = FALSE;          /* Флаг отказа от работы (нажата клавиша ESC) */

var vCodClient = 0;

/****************************************************************/
/*                   Обработка событий панели.                  */
/****************************************************************/
macro EventProcPr( dlg, cmd, id, key )

  if ( cmd == DLG_KEY )
    /* msgbox(key); */
    if   ( key == KEY_F2 )
      if ( dlg.DateMake == Date( 0, 0, 0 ) )
        MsgBox( "Дата не должна быть нулевой!" );
        SetFocus( dlg, 0 );
        /* return CM_IGNORE; */
      else
        return CM_SAVE;
      end;
    elif ( key == KEY_F3 )
      if ( id == 1 )
        clnt.Select_Search();
        vCodClient = clnt.CurRec.Rec.CodClient;
        dlg.CodClient = clnt.CurRec.ConvertFIO();
        /* dlg.CodClient = clnt.CurRec.ConvertFIOFull(); */
      end;
    elif ( key == KEY_F9 )
      return CM_IGNORE;
    elif ( key == KEY_ESC )
      panEscape = TRUE;
      return CM_SAVE;
    elif ( key == KEY_SPACE )
      if ( id == 3 )
        if ( dlg.NeedExcerpt == "X" )
          dlg.NeedExcerpt = "";
        else
          dlg.NeedExcerpt = "X";
        end;
      end;
      UpdateFields( dlg );
    end;
  end;

end;

/****************************************************************/
/*            Панель параметров формирования реестра.           */
/****************************************************************/
macro DI_Panel_Print()

  /* Установка начальных значений полей панели */
  dlg_prnt.DateMake  = panDateMake;
  dlg_prnt.CodClient = "";
  dlg_prnt.LimitSum  = panLimitSum;
  if ( panNeedExcerpt )
    dlg_prnt.NeedExcerpt = "X";
  else
    dlg_prnt.NeedExcerpt = "";
  end;
  panEscape = FALSE;

  /* Работа с панелью */
  RunDialog( dlg_prnt, "EventProcPr" );

  /* Установка параметров для предварительного формирования реестра */
  panDateMake = dlg_prnt.DateMake;
  panClient   = vCodClient;
  panLimitSum = dlg_prnt.LimitSum;
  if ( dlg_prnt.NeedExcerpt == "X" )
    panNeedExcerpt = TRUE;
  else
    panNeedExcerpt = FALSE;
  end;

end;

/* DI_Panel_Print(); */
