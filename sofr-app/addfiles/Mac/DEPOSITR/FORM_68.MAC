/*
  RS-Bank
  Частные вклады для Сбербанка

  Формирование Учетной карточки (форма 68 - ежедневная)

  24.09.96
  Ромодин Александр Васильевич
*/
import deprintr, BankInter, cur_rate;

var  doc = TBFile( "sbdepdoc.dbt", "r", 6 );
file crn   ("currency.dbt");
file dr    ("depositr.dbt");
file dt    ("sb_dtyp.dbt");

const FlagCur = NumFlagCur();
const FNcash  = NumFNcash();

var Kol;
var KolClosed;
var RestBegin;
var InSum;
var OutSum;
var RestEnd;
var KolAll;
var KolClosedAll;
var RestBeginAll;
var InSumAll;
var OutSumAll;
var RestEndAll;
var DateDoc = Date (0,0,0);

var TotKolAll;
var TotRestBeginAll;
var TotInSumAll;
var TotKolClosedAll;
var TotOutSumAll;
var TotKolAll_KolClosedAll;
var TotRestEndAll;

private var cbRateDate;
private var cbRatePrev;


/***********************************************************
***********************************************************/

macro Разделитель_1
  [|-------------------------|                                   |---------------------------------------------------------------------------------------------------------------------------------------------------|]
end;

macro Разделитель_2
  [===================================================================================================================================================================================================================]
end;

/**********************************************************/

macro Заголовок_отчета
  [ ];
  [                     Учетная карточка (форма 68 - ежедневная)];
  [                     ========================================];
  [                                    ВКЛАДЫ];
  [                                #](DateDoc:m);
  Разделитель_2 ();
  [|       Курс валюты       |Код| Наименование ценностей  |Кол. |    Остатки на начало дня        |         Приход                  |              Расход                   |          Остатки на конец дня         |];
  Разделитель_1 ();
  [| Текущий    | Предыдущий |вал|                         |сч.  | номинал    | рубли              | номинал    | рубли              |З.сч.| номинал    | рубли              |К.сч.| номинал    | рубли              |];
  Разделитель_2 ();
end;

macro Строка_отчета
  var Code = String (crn.Code_Currency);
  var ScaleRate = 1;

  if (crn.ScaleOfc != 0)
    ScaleRate = crn.ScaleOfc;
  end;

  while (StrLen(Code) < 3)
    Code = "0" + Code;
  end;
  [ ############ ############ ### ######################### ##### ############ #################### ############ #################### ##### ############ #################### ##### ############ ####################]
  ( cbRateDate, cbRatePrev, Code, crn.Name_Currency, Kol,
   RestBegin, Money(RestBegin * cbRateDate),
   InSum,     Money(InSum * cbRateDate),
   KolClosed,
   OutSum,    Money(OutSum * cbRateDate),
   Kol - KolClosed,
   RestEnd,   Money(RestEnd * cbRateDate) );

end;

macro Тип_вклада
 [ ];
 [                             #](dt.Name);
 [ ];
end;

/**********************************************************/
macro Обнуление_Всего
  TotKolAll = 0;
  TotRestBeginAll = $0.00;
  TotInSumAll = $0.00;
  TotKolClosedAll = 0;
  TotOutSumAll = $0.00;
  TotKolAll_KolClosedAll = 0;
  TotRestEndAll = $0.00;

end;

/**********************************************************/
macro Суммирование_Всего
  TotKolAll              = TotKolAll + KolAll;
  TotRestBeginAll        = TotRestBeginAll + Money(RestBeginAll);
  TotInSumAll            = TotInSumAll + Money(InSumAll);
  TotKolClosedAll        = TotKolClosedAll + KolClosedAll;
  TotOutSumAll           = TotOutSumAll + Money(OutSumAll);
  TotKolAll_KolClosedAll = TotKolAll_KolClosedAll + (KolAll - KolClosedAll);
  TotRestEndAll          = TotRestEndAll + Money(RestEndAll);
end;

/**********************************************************/
macro Всего
  [ ];
  [ ];
  [                              В С Е Г О                  #####              ####################              #################### #####              #################### #####              ####################]
  (TotKolAll, TotRestBeginAll, TotInSumAll, TotKolClosedAll,
   TotOutSumAll, TotKolAll_KolClosedAll, TotRestEndAll);

end;

/***********************************************************/
macro Итого
  [ ];
  [                              И Т О Г О                  #####              ####################              #################### #####              #################### #####              ####################]
  (KolAll, Money(RestBeginAll), Money(InSumAll), KolClosedAll,
   Money(OutSumAll), KolAll - KolClosedAll, Money(RestEndAll));

end;
/***********************************************************/

macro Остаток_на_начало_дня
/*
  Определяет входящий остаток по счету

  24.09.96
*/
  var Rest = 0;
  var stat;

  doc.Clear;
  doc.rec.Referenc = dr.Referenc;
  doc.rec.Date_Document = DateDoc;
  doc.rec.NumDayDoc = 1;

  doc.addFilter( "t.t_Referenc = " + String( dr.Referenc ) );

  stat = doc.GetLE;

  if ( stat and ( doc.rec.Referenc == dr.Referenc ) )
    if ( doc.rec.Date_Document < DateDoc )
      Rest = doc.rec.Rest;
    else
      stat = doc.Prev;
      if ( stat and ( doc.rec.Referenc == dr.Referenc ) )
        Rest = doc.rec.Rest;
      end;
    end;
  end;

  doc.dropFilter;

  return Rest;

end;
/***********************************************************/

macro Цикл_по_счетам
/*
  Организует цикл по счетам данного типа вклада
  и данной валюты

  24.09.96
*/
  var stat;
  var wr = FALSE;

  KeyNum (dr,1);
  dr.IsCur = FlagCur;
  dr.FNCash = FNcash;
  dr.Type_Account = dt.Kind;
  dr.Code_Currency = crn.Code_Currency;

  Kol = 0;
  KolClosed = 0;
  RestBegin = 0;
  RestEnd = 0;
  InSum = 0;
  OutSum = 0;

  stat = getGE (dr);
  while (stat)
    if ((dr.IsCur == FlagCur) AND (dr.FNCash == FNcash) AND
     (dr.Type_Account == dt.Kind) AND (dr.Code_Currency == crn.Code_Currency))
      Kol = Kol + 1;
      RestBegin = RestBegin + Остаток_на_начало_дня();
      RestEnd = RestEnd + dr.Sum_Rest;
      InSum = InSum + dr.Sum_In;
      OutSum = OutSum + dr.Sum_Out;
      if (dr.Close_Date == DateDoc)
        KolClosed = KolClosed + 1;
      end;
      wr = TRUE;
      stat = Next (dr);
    else
      stat = FALSE;
    end;
  end;
  if (wr)
    Строка_отчета ();
  end;
end;
/***********************************************************/

macro Цикл_по_валютам
/*
  Организует цикл по валютам для данного типа вклада

  24.09.96
*/
  var stat;
  var ScaleRate = 1;

  KolAll = 0;
  KolClosedAll = 0;
  RestBeginAll = 0;
  InSumAll = 0;
  OutSumAll = 0;
  RestEndAll = 0;

  KeyNum (crn,0);
  crn.Code_Currency = 0;
  stat = getGE (crn);

  while (stat)
    if (crn.ScaleOfc != 0)
      ScaleRate = crn.ScaleOfc;
    end;

    GetAllCurrRate( DateDoc, crn.Code_Currency, cbRateDate );
    GetAllCurrRate( DateDoc - 1, crn.Code_Currency, cbRatePrev );
    if ( cbRatePrev == 0 )
      cbRatePrev = cbRateDate;
    end;

    Цикл_по_счетам ();
    KolAll       = KolAll       + Kol;
    KolClosedAll = KolClosedAll + KolClosed;
    RestBeginAll = RestBeginAll + Money( RestBegin * cbRateDate );
    RestEndAll   = RestEndAll   + Money( RestEnd   * cbRateDate );
    InSumAll     = InSumAll     + Money( InSum     * cbRateDate );
    OutSumAll    = OutSumAll    + Money( OutSum    * cbRateDate );
    stat = Next (crn);
  end;
end;
/**********************************************************/

macro Цикл_по_типам_вкладов
/*
  Организует цикл по типам вклада

  24.09.96
*/
  var stat;

  KeyNum (dt,1);
  ReWind (dt);

  dt.FlagCur = FlagCur;
  dt.Priorit = 0;

  stat = getGE (dt);

  while (stat)
    if (dt.FlagCur == FlagCur)
     if((dt.Kind != "Шаблонный") AND (dt.Kind != "Служебный"))
      Тип_вклада ();
      Цикл_по_валютам ();
      Суммирование_Всего ();
      Итого ();
     end;
      stat = Next (dt);
    else
      stat = FALSE;
    end;
  end;
end;
/**********************************************************/

macro Начало_Конец_отчета
  [ ------ RS-Bank -------- Сбер.банк ------- ########## ------ ######## ------
    ](Date(),Time());
end;
/***********************************************************
***********************************************************/

/*
  Головной макрос
*/
  var {curdate};

  if (DateDoc == Date(0,0,0))
    DateDoc = {curdate};
  end;
  Начало_Конец_отчета ();
  Обнуление_Всего ();
  Заголовок_отчета (DateDoc);
  Цикл_по_типам_вкладов ();
  Всего ();
  Разделитель_2 ();
  Начало_Конец_отчета ();
end;
