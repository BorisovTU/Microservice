/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Обработка контрольной точки при работе макропрограмм                    *
*                                                                          *
*  Лебедев С.В.                                                            *
*  23.03.2000                                                              *
\**************************************************************************/

file breakpnt (breakpnt) key 0 write;

var  FileON = FALSE;


/**************************************************************************\
|             Заполнение контрольной точки обработанной записи             |
\**************************************************************************/
macro FillBreakPoint (ProgIdent, PointNumber, RecIdent)

  var stat = TRUE;

  if (NOT FileON)
    stat = Open(breakpnt);
    if (NOT stat)
      stat = Create(breakpnt);
    end;
    if (stat)
      FileON = TRUE;
      ClearRecord(breakpnt);
    end;
  end;

  if (stat)
    breakpnt.ProgIdent   = ProgIdent;
    breakpnt.PointNumber = PointNumber;
    stat = GetEQ(breakpnt);
    if (stat)
      breakpnt.RecIdent = RecIdent;
      stat = Update(breakpnt);
    else
      ClearRecord(breakpnt);
      breakpnt.ProgIdent   = ProgIdent;
      breakpnt.PointNumber = PointNumber;
      breakpnt.RecIdent    = RecIdent;
      stat = Insert(breakpnt);
    end; 
  end;

  return stat;

end;


/**************************************************************************\
|                Поиск контрольной точки обработанной записи               |
\**************************************************************************/
macro FindBreakPoint (ProgIdent, PointNumber, RecIdent)

  var stat = TRUE;

  RecIdent = 0; 

  if (NOT FileON)
    stat = Open(breakpnt);
    if (NOT stat)
      stat = Create(breakpnt);
    end;
    if (stat)
      FileON = TRUE;
      ClearRecord(breakpnt);
    end;
  end;

  if (stat)
    breakpnt.ProgIdent   = ProgIdent;
    breakpnt.PointNumber = PointNumber;
    stat = GetEQ(breakpnt);
  end;

  if (stat)
    RecIdent = breakpnt.RecIdent;
  end;

  SetParm(2,RecIdent);

  return stat;

end;


/**************************************************************************\
|              Удаление контрольной точки обработанной записи              |
\**************************************************************************/
macro DeleteBreakPoint (ProgIdent, PointNumber)

  var stat = TRUE;

  if (NOT FileON)
    stat = Open(breakpnt);
    if (NOT stat)
      stat = Create(breakpnt);
    end;
    if (stat)
      FileON = TRUE;
      ClearRecord(breakpnt);
    end;
  end;

  if (stat)
    breakpnt.ProgIdent   = ProgIdent;
    breakpnt.PointNumber = PointNumber;
    stat = GetEQ(breakpnt);
    if (stat)
      stat = Delete(breakpnt);
    end;
  end;

  return stat;

end;
