/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Печать отчета по журналу операций                                       *
*                                                                          *
*  Лебедев С.В. (за основу принят журнал RS-Bank)                          *
*  08.06.2000                                                              *
\**************************************************************************/
import DeprIntr;

const OLinsrec   =    1;  /* Ввод записи                    */
const OLdelrec   =    2;  /* Удаление записи                */
const OLchgrec   =    3;  /* Обновление записи              */
const OLproc     =    4;  /* Процедура                      */

const iOLentry   = 1000;  /* Вход в программу               */
const iOLexit    = 1001;  /* Выход из программы             */
const iOLstrproc = 1002;  /* Начало выполнения процедуры    */
const iOLfinproc = 1003;  /* Окончание выполнения процедуры */
const iOLbadpwd  = 1007;  /* Неверный пароль                */
const iOLblock   = 1101;  /* Блокировка пользователя        */
const iOLsessins    = 9000; /* Загрузка сессии: вставка записи    */
const iOLsessupd    = 9001; /* Загрузка сессии: обновление записи */
const iOLsessinserr = 9002; /* Загрузка сессии: ошибка вставки    */
const iOLsessupderr = 9003; /* Загрузка сессии: ошибка обновления */

var   Counter    =  0;
var   WriteStep  =  0;
var   BoardStep  = 10;


/**************************************************************************\
|                         Печать заголовка отчета                          |
\**************************************************************************/
macro PrintHeader (SingleRecord)
/*
   - Параметры функции:
     SingleRecord:  0 - отчет по всем записям списка
                    1 - отчет по выбранной записи
   - Возвращаемое значение:
     TRUE  нормальное выполнение
     FALSE прервать выпуск отчета
*/
 [Отчет по журналу];
 [------------------------------------------------------------------------------];
 [ ];

 return TRUE;

end;


/**************************************************************************\
|                         Печать окончания отчета                          |
\**************************************************************************/
macro PrintFooting (SingleRecord)
/*
   - Параметры функции:
     SingleRecord:  0 - отчет по всем записям списка
                    1 - отчет по выбранной записи
   - Возвращаемое значение: не имеет значения
*/
 [ ];
 [------------------------------------------------------------------------------];
 [Конец отчета ];

 return TRUE;

end;

 
/**************************************************************************\
|           Печать отчета по конкретной записи в журнале операций          |
\**************************************************************************/
macro PrintRecord (SingleRecord, BufferPtr, RecordType, VarRecord, ProcName, rec1, rec2)
/*
   - Параметры функции:
     SingleRecord:  0 - отчет по всем записям списка
                    1 - отчет по выбранной записи
     BufferPtr   :  адрес буфера с копией записи в журнале операций
                    (структура operlog.dbt)
     RecordType  :  одно из значений OLinsrec, OLdelrec, OLchgrec, OLproc
                    (соответственно ввод, удаление, обновление записи в
                     btrieve-файле или запись о выполнении некой процедуры)
     VarRecord   :  адрес буфера с переменной частью записи журнала
     ProcName    :  строка с названием выполненной процедуры
     rec1        :  Либо объект TRecHandler, содержащий структуру введенной, удаленной
                    записи или записи до изменения, либо V_UNDEF
     rec2        :  Либо объект TRecHandler, содержащий структуру записи после изменения,
                    либо V_UNDEF
   - Возвращаемое значение:
     TRUE  нормальное выполнение
     FALSE прервать выпуск отчета
*/
  record oplog ( "operlog.old" );
  record iolog ( "tLogIO.str", "bank.def" );
  var i              = 0;
  var FieldNameOpLog = "";
  var FileNameOpLog  = "";
  var FieldChanged   = "";
  var Field1;
  var Field2;

  /* Счетчик в статусной строке */
  Counter   = Counter   + 1;
  WriteStep = WriteStep + 1;
  if (WriteStep >= BoardStep)
    if (SingleRecord == 0)
      Message(" Выводится в отчет ",Counter:6," запись ...");
    end;
    WriteStep = 0;
  end;

  SetBuff(oplog,BufferPtr);

  [ ];

  /* Удаление, ввод или обновление записи */
  if ((RecordType == OLinsrec) OR
      (RecordType == OLdelrec) OR
      (RecordType == OLchgrec)   )

    if ((oplog.iOperation == iOLsessins   ) OR
        (oplog.iOperation == iOLsessupd   ) OR
        (oplog.iOperation == iOLsessinserr) OR
        (oplog.iOperation == iOLsessupderr)   )

      if (oplog.iOperation == iOLsessins) 
          println(" Загрузка сессии: вставка записи");
      elif (oplog.iOperation == iOLsessupd)
          println(" Загрузка сессии: обновление записи");
      elif (oplog.iOperation == iOLsessinserr)
          println(" Загрузка сессии: ошибка вставки");
      elif (oplog.iOperation == iOLsessupderr)
          println(" Загрузка сессии: ошибка обновления");
      end;
      [-- ########## ########  Филиал: ####  Номер сессии: ####  Файл: ############ ]
      (oplog.bdDateRec,oplog.btTimeRec,oplog.iAppcicationKind,oplog.iOper,oplog.szFileName);
    else
      [-- ########## ########  Операционист: ####  Файл: ############ ]
      (oplog.bdDateRec,oplog.btTimeRec,oplog.iOper,oplog.szFileName);
    end;

    FileNameOpLog = FileCommentRt(oplog.szFileName);
    if (StrLen(FileNameOpLog) > 0)
      [ #](FileNameOpLog);
    end;

    if (ValType(rec1) != V_UNDEF)

      [ +--------------------+--+---------------------------+---------------------------+];
      [ |       Поле         |Из|  Значение после операции  |   Значение до операции    |];
      [ +--------------------+--+---------------------------+---------------------------+];
  
      while (i < rec1.FldNumber)
  
        if (RecordType == OLinsrec)
          FieldChanged = "";
          Field1       = rec1.(i);
          Field2       = "";
        elif (RecordType == OLdelrec)
          FieldChanged = "";
          Field1       = "";
          Field2       = rec1.(i);
        elif (ValType(rec2) != V_UNDEF)
          Field1 = rec1.(i);
          Field2 = rec2.(i);
          if ( Field2 != Field1 )
            FieldChanged = "X";
          else
            FieldChanged = " ";
          end;
        else
          FieldChanged = "?";
          Field1       = "?";
          Field2       = rec1.(i);
        end;

        FieldNameOpLog = FldCommentRt(rec1.TblName,i);
        if (FieldNameOpLog == "")
          FieldNameOpLog = "\"" + rec1.FldName(i) + "\"";
        end;
  
        [ |####################|##|###########################|###########################|]
        (FieldNameOpLog:w,FieldChanged,Field1,Field2);
  
        i = i + 1;
  
      end;
      [ +--------------------+--+-------------------------+-------------------------+];
    else
      [ * Неверная запись в журнале или ошибка формирования структуры ];
    end;
  else
    /* Запись о выполнении той или иной процедуры */
    [-- ########## ########  Операционист: ####  Операция: #### ]
    (oplog.bdDateRec, oplog.btTimeRec, oplog.iOper, oplog.iOperation);

    if ((oplog.iOperation == iOLentry ) OR
        (oplog.iOperation == iOLexit  ) OR
        (oplog.iOperation == iOLbadpwd) OR
        (oplog.iOperation == iOLblock )   )

      if (oplog.iOperation == iOLentry) 
        if (StrLen(ProcName) < 50)
          println("   Вход в программу. ",ProcName);
        else
          println("   Вход в программу");
          println("   ",ProcName);
        end;
      elif (oplog.iOperation == iOLbadpwd)
        println("   Неверный ввод пароля");
        println("   ",ProcName);
      elif (oplog.iOperation == iOLblock)
        println("   Блокировка пользователя");
        println("   ",ProcName);
      else
        if (StrLen(ProcName) < 50)
          println("   Выход из программы. ",ProcName);
        else
          println("   Выход из программы");
          println("   ",ProcName);
        end; 
      end;

      SetBuff( iolog, VarRecord );
      if (iolog.Mark == "\x01")
        println("   Путь к исполняемому модулю      : ", iolog.ExePath);
        println("   Название исполняемого модуля    : ", iolog.ExeName);
        println("   Размер исполняемого модуля      : ", iolog.ExeLen);
        println("   Дата и время исполняемого модуля: ", iolog.ExeDate,", ",iolog.ExeTime);
        println("   Версия программы                : ", iolog.verHi,".",iolog.verLo,".",iolog.Build);
        println("   Статус                          : ", iolog.Name_Type);
      end;

    elif (oplog.iOperation == iOLstrproc)
      if (StrLen(ProcName) < 45)
        println("   Начало выполнения процедуры \"",ProcName,"\"");
      else
        println("   Начало выполнения процедуры");
        println("   \"",ProcName,"\"");
      end;
    elif (oplog.iOperation == iOLfinproc)
      if (StrLen(ProcName) < 45)
        println("   Завершение выполнения процедуры \"", ProcName, "\"");
      else
        println("   Завершение выполнения процедуры");
        println("   \"",ProcName,"\"");
      end;
    end;

  end;

  return TRUE;

end;
