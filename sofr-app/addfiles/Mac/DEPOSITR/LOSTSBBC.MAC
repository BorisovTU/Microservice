/*
$Name:             lostsbbc.mac
$Module:           RS-Retail
$Description:      Операция "Отмена утери сбер.книжки"
*/

/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Операция "Отмена утери сбер.книжки".

*****************************************************************/

import DeprIntr, alg_vars;

file depositr ( depositr ) key 8 write;
file lostbook ( lostbook ) key 5 write;

const UPDATE_ALL = 1;
const UPDATE_DEP = 2;
const UPDATE_DOC = 4;

/* Состояние заявления об утрате сбер.книжки. */
const LBDS_ANY         = 0,  /* Любое      */
      LBDS_INITIALIZED = 1,  /* Введено    */
      LBDS_PROCESSED   = 2,  /* Обработано */
      LBDS_CANCELLED   = 3;  /* Отменено   */

/**************************************************************************\
|       Поиск записи в таблице регистр.утрач. сб\кн для данного счета.     |
\**************************************************************************/
macro LB_find_Ref()
  KeyNum( lostbook, 5 );
  ClearRecord( lostbook );
  lostbook.Reference = ALG_DEPOSITOR.Referenc;
  lostbook.State     = LBDS_INITIALIZED;  /* Введено */
  return getGE( lostbook );
end;

/**************************************************************************\
|      Изменение записи в таблице регистрации утраченных сбер.книжек.      |
\**************************************************************************/
macro LB_Update()
  /* Состояние заявления */
  lostbook.State = LBDS_CANCELLED;  /* Отменено   */
  /* Дата отмены заявления */
  lostbook.DateCancelled = {curdate};
  /* Номер операциониста->отмена утери */
  lostbook.CancelledByOper = {oper};
  return Update( lostbook );
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro T_UpdateRecsForUnLostSBBook

  record save_depositr ( depositr );
  record save_lostbook ( lostbook );

  record WriteLog_rec  ( "WriteLog.rec" );
  record op_parm_rec   ( "op_parm.rec"  );
  record op_link_rec   ( "op_link.rec"  );
  record ret_op_dep    ( "ret_op.dep"   );

  var stat = true;
  var appl_key;
  var str_tmp;
  var indY;

  /* Изменение записи по счету */
  if ( stat )
    GetPos( depositr );
    stat = GetDirect( depositr );
  end;

  if ( stat )
    /* Удаление признака утери с/к из поля UserTypeAccount. */
    indY = Index( ALG_DEPOSITOR.UserTypeAccount, "Y" );
    if ( indY > 0 )
      if ( indY > 1 )
        ALG_DEPOSITOR.UserTypeAccount =
          SubStr( ALG_DEPOSITOR.UserTypeAccount, 1, indY - 1 ) +
          SubStr( ALG_DEPOSITOR.UserTypeAccount, indY + 1 );
      else
        ALG_DEPOSITOR.UserTypeAccount =
          SubStr( ALG_DEPOSITOR.UserTypeAccount, indY + 1 );
      end;
    end;
    // нужно будет установливать эти признаки для всех счетов по контракту
    ALG_DEPOSITOR.Givebook   = "X";
    ALG_DEPOSITOR.NumSession = 0;
    ALG_DEPOSITOR.Action     = 1;

    ALG_CONTRACT.BookGiven   = "X";
    ALG_CONTRACT.Clear_AccountsFlagCh("Y");

    Copy( save_depositr, depositr );
    Copy( depositr, ALG_DEPOSITOR );

    stat = Update( depositr );
  end;

  /* Журнализация изменений в файл нефинансовых операций */
  if ( stat )
    ClearRecord( WriteLog_rec );
    WriteLog_rec.NumOperation = 3;
    WriteLog_rec.ObjectType   = ALG_RET_OP.Operation;
    WriteLog_rec.ObjectID     = String( 501 ) + "/" + String( depositr.Referenc );

    if ( WriteChangesToLog( WriteLog_rec, depositr, save_depositr ) != 0 )
      stat = false;
    end;
  end;

  /* Формирование операции */
  if ( stat )
    appl_key = "rtnfopr#";
    str_tmp = String( WriteLog_rec.NumDprt );
    while ( StrLen( str_tmp ) < 5 )
      str_tmp = "0" + str_tmp;
    end;
    appl_key = appl_key + str_tmp;
    str_tmp = String( WriteLog_rec.RecID );
    while ( StrLen( str_tmp ) < 10 )
      str_tmp = "0" + str_tmp;
    end;
    appl_key = appl_key + str_tmp;
    appl_key = appl_key + "00";

    if ( not InterDesk_IsDocBunchActive )
      ClearRecord( op_parm_rec );
      op_parm_rec.Date      = {curdate};
      op_parm_rec.Operation = ALG_RET_OP.Operation;
      op_parm_rec.SubOp     = ALG_RET_OP.SubOp;
      op_parm_rec.ObjectRef = depositr.Type_Account;
      op_parm_rec.CodCur    = depositr.Code_Currency;
      op_parm_rec.Flags     = 1 /*ROF_NONFINANCIAL*/;

      ClearRecord( ret_op_dep );
      ret_op_dep.accRef = depositr.Referenc;

      stat = InterDesk_InitDocBunch( 3001, appl_key, op_parm_rec, ret_op_dep );
    end;
  end;

  if ( stat )
    stat = CashLink_AddRec( 3001, appl_key );
  end;

  if ( stat )
    stat = UpdateAlgRecords( UPDATE_DEP, true, false );
  end;

  /* Формирование связи операции и объекта */
  if ( stat )
    ClearRecord( op_link_rec );
    op_link_rec.Date         = {curdate};
    op_link_rec.ObjectType   = 1;
    op_link_rec.ObjectRef    = String( depositr.Referenc );

    stat = LinkOpToObject( op_link_rec );
  end;

  /* Установка позиции в таблице регистрации утраченных сбер.книжек. */
  if ( stat )
    GetPos( lostbook );
    stat = GetDirect( lostbook );
  end;

  /* Отметка отмены в таблице регистрации утраченных сбер.книжек. */
  if ( stat )
    Copy( save_lostbook, lostbook );
    stat = LB_Update();
  end;

  /* Журнализация обновления в lostbook.dbt в файл нефинансовых операций */
  if ( stat )
    ClearRecord( WriteLog_rec );
    WriteLog_rec.NumOperation = 3;  /* Update */
    WriteLog_rec.ObjectType   = ALG_RET_OP.Operation;
    WriteLog_rec.ObjectID     = String( 517 ) + "/" + String( lostbook.ID );

    if ( WriteChangesToLog( WriteLog_rec, lostbook, save_lostbook ) != 0 )
      stat = false;
    end;
  end;

  /* Формирование операции */
  if ( stat )
    appl_key = "rtnfopr#";
    str_tmp = String( WriteLog_rec.NumDprt );
    while ( StrLen( str_tmp ) < 5 )
      str_tmp = "0" + str_tmp;
    end;
    appl_key = appl_key + str_tmp;
    str_tmp = String( WriteLog_rec.RecID );
    while ( StrLen( str_tmp ) < 10 )
      str_tmp = "0" + str_tmp;
    end;
    appl_key = appl_key + str_tmp;
    appl_key = appl_key + "00";

    if ( not InterDesk_IsDocBunchActive )
      ClearRecord( op_parm_rec );
      op_parm_rec.Date      = {curdate};
      op_parm_rec.Operation = ALG_RET_OP.Operation;
      op_parm_rec.SubOp     = ALG_RET_OP.SubOp;
      op_parm_rec.ObjectRef = lostbook.Number;
      op_parm_rec.CodCur    = depositr.Code_Currency;
      op_parm_rec.Flags     = 1 /*ROF_NONFINANCIAL*/;

      ClearRecord( ret_op_dep );
      ret_op_dep.accRef = depositr.Referenc;

      stat = InterDesk_InitDocBunch( 3001, appl_key, op_parm_rec, ret_op_dep );
    end;
  end;

  if ( stat )
    stat = CashLink_AddRec( 3001, appl_key );
  end;

  if ( not stat )
    AbortTrn( );
  end;

end;

 ALG_RESULT = OK_RES;

 /* Проверка признака утери сбер.книжки в записи счета. */
 if ( ALG_RESULT == OK_RES )
    if ( NOT Index( ALG_DEPOSITOR.UserTypeAccount, "Y" ) )
      if ( StrFor( GetProgramID( ) ) != "П" )
        MsgBox( "Сберкнижка по счету не была утеряна." );
      end;
      ALG_RESULT = SKIP_RES;
    end;
  end;

 /* Поиск записи в таблице регистр. утрач. сб\кн для данного счета. */
 if ( ALG_RESULT == OK_RES )
   if ( NOT LB_find_Ref() )
     MsgBox( "Для счета нет необработанного заявления об утрате сбер.книжки." );
     ALG_RESULT = ERR_RES;
   end;
 end;

 /* Запрос на выполнение операции. */
 if ( ( ALG_RESULT == OK_RES )  AND  ( StrFor( GetProgramID() ) != "П" ) )
   if ( NOT GetTrue( true, "Провести операцию по отмене утери сберкнижки ?" ) )
     ALG_RESULT = SKIP_RES;
   end;
 end;

 /* Поиск счета. */
 if ( ALG_RESULT == OK_RES )
   ClearRecord( depositr );
   depositr.Referenc = ALG_DEPOSITOR.Referenc;
   if ( not GetEQ( depositr ) )
     if ( StrFor( GetProgramID( ) ) != "П" )
       MsgBox( "Не найден счет с референсом " + String( ALG_DEPOSITOR.Referenc ) );
     end;
     ALG_RESULT = ERR_RES;
   end;
 end;

 /* Транзакция операции отмены утери сберкнижки. */
 if ( ALG_RESULT == OK_RES )
   if ( ProcessTrn( 0, "T_UpdateRecsForUnLostSBBook", depositr ) )
     AI_SBook_Num_Rest( ALG_DEPOSITOR.Referenc );
     /*
     if ( AI_SBook_Num( ALG_DEPOSITOR.Referenc, "", 0 ) != 0 )
       MsgBox( "Ошибка при сбросе серии\номера сбер.книжки" );
     end;
     */
     ALG_RESULT = SKIP_RES;
   else
     if ( StrFor( GetProgramID( ) ) != "П" )
       MsgBox( "Ошибка при проводке по отмене утери сберкнижки" );
     end;
     ALG_RESULT = ERR_RES;
     InterDesk_EndDocBunch( );
   end;
 end;

end;
