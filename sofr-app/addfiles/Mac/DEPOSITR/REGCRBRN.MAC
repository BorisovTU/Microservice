/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Последконтроль                                                          *
*                                                                          *
*                                                                          *
*  Печать Формы 68 по Филиалу                                              *
*                                                                          *
*  Лебедев С.В.                                                            *
*  05.03.98                                                                *
\**************************************************************************/
import deprintr;

file sb_dtyp  (sb_dtyp );       /* Типы вкладов           */
file sb_depst (sb_depst);       /* Статистика по вкладам  */
file currency (currency) key 0; /* Справочник валют       */

const ПЕЧАТАТЬ_ПУСТЫЕ_СТРОКИ = TRUE; /* Печать вкладов у которых не было оборотов */

const NationalCodCur =  0; /* Код национальной валюты в currency.dbt */
const ConstLenBalAcc =  5; /* Длина Балансового счета для сравнения  */
const LengthReport   = 88; /* Длина отчета                           */

array ABalAccs; /* Массив балансовых счетов */

var FlagCur    = NumFlagCur();     /* Валютность           */
var FNCash     = NumFNCash();      /* Номер филиала        */
var Mode       = GetCurrentMode(); /* Текущий режим работы */
var {curdate};                     /* Текущая дата         */
var DateReport = {curdate};        /* Дата отчета          */

/* Подсчет сумм по балансовому счету */
var SumBAColIn       = 0;
var SumBAColOut      = 0;
var SumBAColFinDay   = 0;
var SumBASummaIn     = $0L;
var SumBASummaOut    = $0L;
var SumBASummaFinDay = $0L;
var BACount          = 0;
/* Подсчет сумм по валюте */
var SumCurColIn       = 0;
var SumCurColOut      = 0;
var SumCurColFinDay   = 0;
var SumCurSummaIn     = $0L;
var SumCurSummaOut    = $0L;
var SumCurSummaFinDay = $0L;


/**************************************************************************\
|                         Начало-Окончание отчета                          |
\**************************************************************************/
macro BeginEndReport

  [ ------- #################### ------------------ ########## ------ ######## ---------]
  (GetRetailVersion(),Date(),Time());

end;


/**************************************************************************\
|                               Шапка отчета                               |
\**************************************************************************/
macro TopReport

  var str        = "";
  var dep_name;

  if (NOT findDepartment(FNCash, dep_name))
    dep_name = "";
  end;

  [ ];
  str = "Учетная карточка по учреждению банка № " + dep_name + " за " + String(DateReport);
  [########################################################################################]
  (str:c);
  [ ];
  str = "Валюта: " + currency.ExternalCode + " (" + currency.Name_Currency + ")";
  [ #](str);
  [ ];
  [#](MkStr("-",LengthReport));
  [    N б/с   | Вид вклада |       Приход       |       Расход       |Остатки на конец дня];
  [            |            |--------------------|--------------------|--- по 68 форме ----];
  [            |            | л/с |    Сумма     | л/с |    Сумма     | л/с |    Сумма     ];
  [#](MkStr("-",LengthReport));

end;


/**************************************************************************\
|                             Окончание отчета                             |
\**************************************************************************/
macro BottomReport

  [   Всего                  ##### ############## ##### ############## ##### ##############]
  (SumCurColIn,SumCurSummaIn:14:2:a,SumCurColOut,SumCurSummaOut:14:2:a,
   SumCurColFinDay,SumCurSummaFinDay:14:2:a                            );
  [#](MkStr("=",LengthReport));
  [ ];
  [ ];

end;


/**************************************************************************\
|                          Печать строки в отчет                           |
\**************************************************************************/
macro WriteToReport

  var stat;
  var BalAccount  = "";
  var ColIn       = sb_depst.ColAccounts;
  var ColOut      = sb_depst.ColClosedAcc;
  var ColFinDay   = sb_depst.ColOpenedAcc;
  var SummaIn     = $0L;
  var SummaOut    = $0L;
  var SummaFinDay = sb_depst.Rest;
  var FlagRez     = sb_depst.FlagRez;

  if (sb_depst.Date < DateReport)
    ColIn = ColOut = 0;
  else
    KeyNum(sb_depst,0);
    sb_depst.FNCash  = FNCash;
    sb_depst.IsCur   = FlagCur;
    sb_depst.Kind    = sb_dtyp.Kind;
    sb_depst.CodCur  = currency.Code_Currency;
    sb_depst.FlagRez = FlagRez;
    sb_depst.Date    = DateReport;
    sb_depst.Mode    = 0;
    stat = GetGE(sb_depst);
    while (stat)
      if ((sb_depst.FNCash  == FNCash                ) AND
          (sb_depst.IsCur   == FlagCur               ) AND
          (sb_depst.Kind    == sb_dtyp.Kind          ) AND
          (sb_depst.CodCur  == currency.Code_Currency) AND
          (sb_depst.FlagRez == FlagRez               ) AND
          (sb_depst.Date    == DateReport            )    )
        SummaIn  = SummaIn  + sb_depst.KredSum;
        SummaOut = SummaOut + sb_depst.DebSum;
        stat = Next(sb_depst);
      else
        stat = FALSE;
      end;
    end;

    KeyNum(sb_depst,0);
    sb_depst.FNCash  = FNCash;
    sb_depst.IsCur   = FlagCur;
    sb_depst.Kind    = sb_dtyp.Kind;
    sb_depst.CodCur  = currency.Code_Currency;
    sb_depst.FlagRez = FlagRez;
    sb_depst.Date    = DateReport - 1;
    sb_depst.Mode    = 1;
    if ((GetLE(sb_depst)                           ) AND
        (sb_depst.FNCash  == FNCash                ) AND
        (sb_depst.IsCur   == FlagCur               ) AND
        (sb_depst.Kind    == sb_dtyp.Kind          ) AND
        (sb_depst.CodCur  == currency.Code_Currency) AND
        (sb_depst.FlagRez == FlagRez               )    )
      ColIn  = ColIn  - sb_depst.ColAccounts;
      ColOut = ColOut - sb_depst.ColClosedAcc;
    end;
  end;

  if (FlagRez)
    BalAccount = Trim(sb_dtyp.BalAccNotRez);
  else
    BalAccount = Trim(sb_dtyp.BalAcc);
  end;
  if (StrLen(BalAccount) > ConstLenBalAcc)
    BalAccount = SubStr(BalAccount,1,ConstLenBalAcc);
  end;

  /* Печатаем только если за день были какие-то движения по вкладу */
  if ( ПЕЧАТАТЬ_ПУСТЫЕ_СТРОКИ          OR
      (ColIn  != 0) OR (SummaIn  != 0) OR
      (ColOut != 0) OR (SummaOut != 0)   )
    [############ ############ ##### ############## ##### ############## ##### ##############]
    (BalAccount,sb_dtyp.Kind,ColIn,SummaIn:14:2:a,ColOut,SummaOut:14:2:a,ColFinDay,SummaFinDay:14:2:a);
    if (FlagRez)
      [             (нерезид.) ];
    end;
    BACount = BACount + 1;
  end;

  /* Подсчет сумм по балансовому счету */
  SumBAColIn       = SumBAColIn       + ColIn;
  SumBAColOut      = SumBAColOut      + ColOut;
  SumBAColFinDay   = SumBAColFinDay   + ColFinDay;
  SumBASummaIn     = SumBASummaIn     + SummaIn;
  SumBASummaOut    = SumBASummaOut    + SummaOut;
  SumBASummaFinDay = SumBASummaFinDay + SummaFinDay;
  /* Подсчет сумм по валюте */
  SumCurColIn       = SumCurColIn       + ColIn;
  SumCurColOut      = SumCurColOut      + ColOut;
  SumCurColFinDay   = SumCurColFinDay   + ColFinDay;
  SumCurSummaIn     = SumCurSummaIn     + SummaIn;
  SumCurSummaOut    = SumCurSummaOut    + SummaOut;
  SumCurSummaFinDay = SumCurSummaFinDay + SummaFinDay;

end;


/**************************************************************************\
|                     Печать сумм по балансовому счету                     |
\**************************************************************************/
macro PrintSumBalAcc (NumBalAcc)

  /* Печатаем только если за день были какие-то движения по балансовому счету */
  if (BACount > 0)
    if (BACount > 1)
      [#](MkStr("-",LengthReport));
      [ Итого по счету ######### ##### ############## ##### ############## ##### ##############]
      (NumBalAcc,SumBAColIn,SumBASummaIn:14:2:a,SumBAColOut,
       SumBASummaOut:14:2:a,SumBAColFinDay,SumBASummaFinDay:14:2:a);
    end;
    [#](MkStr("=",LengthReport));
  end;

end;


/**************************************************************************\
|                        Цикл по балансовым счетам                         |
\**************************************************************************/
macro DoReport

  var i = 0;
  var stat;
  var CurBalAcc;

  /* Обнуляем суммы по валюте */
  SumCurColIn       = 0;
  SumCurColOut      = 0;
  SumCurColFinDay   = 0;
  SumCurSummaIn     = $0L;
  SumCurSummaOut    = $0L;
  SumCurSummaFinDay = $0L;

  while (i < ASize(ABalAccs))
    /* Обнуляем суммы по балансовому счету */
    SumBAColIn       = 0;
    SumBAColOut      = 0;
    SumBAColFinDay   = 0;
    SumBASummaIn     = $0L;
    SumBASummaOut    = $0L;
    SumBASummaFinDay = $0L;
    BACount          = 0;

    Message(" Обрабатывается балансовый счет ",ABalAccs(i)," ...");

    KeyNum(sb_dtyp,4);
    sb_dtyp.FlagCur = FlagCur;
    sb_dtyp.BalAcc  = "";
    stat = GetGE(sb_dtyp);
    while (stat)
      if (sb_dtyp.FlagCur == FlagCur)
        /* Проверяем счет для резидента */
        CurBalAcc = Trim(sb_dtyp.BalAcc);
        if (StrLen(CurBalAcc) > ConstLenBalAcc)
          CurBalAcc = SubStr(CurBalAcc,1,ConstLenBalAcc);
        end;
        if (ABalAccs(i) == CurBalAcc)
          KeyNum(sb_depst,0);
          sb_depst.FNCash  = FNCash;
          sb_depst.IsCur   = FlagCur;
          sb_depst.Kind    = sb_dtyp.Kind;
          sb_depst.CodCur  = currency.Code_Currency;
          sb_depst.FlagRez = "\x00";
          sb_depst.Date    = DateReport;
          sb_depst.Mode    = 1;
          if ((GetLE(sb_depst)                           ) AND
              (sb_depst.FNCash  == FNCash                ) AND
              (sb_depst.IsCur   == FlagCur               ) AND
              (sb_depst.Kind    == sb_dtyp.Kind          ) AND
              (sb_depst.CodCur  == currency.Code_Currency) AND
              (sb_depst.FlagRez == "\x00"                )    )
            if (ПЕЧАТАТЬ_ПУСТЫЕ_СТРОКИ OR (sb_depst.Date == DateReport))
              WriteToReport();
            end;
          end;
        end;
        /* Проверяем счет для нерезидента */
        CurBalAcc = Trim(sb_dtyp.BalAccNotRez);
        if (StrLen(CurBalAcc) > ConstLenBalAcc)
          CurBalAcc = SubStr(CurBalAcc,1,ConstLenBalAcc);
        end;
        if (ABalAccs(i) == CurBalAcc)
          KeyNum(sb_depst,0);
          sb_depst.FNCash  = FNCash;
          sb_depst.IsCur   = FlagCur;
          sb_depst.Kind    = sb_dtyp.Kind;
          sb_depst.CodCur  = currency.Code_Currency;
          sb_depst.FlagRez = "\x01";
          sb_depst.Date    = DateReport;
          sb_depst.Mode    = 1;
          if ((GetLE(sb_depst)                           ) AND
              (sb_depst.FNCash  == FNCash                ) AND
              (sb_depst.IsCur   == FlagCur               ) AND
              (sb_depst.Kind    == sb_dtyp.Kind          ) AND
              (sb_depst.CodCur  == currency.Code_Currency) AND
              (sb_depst.FlagRez == "\x01"                )    )
            if (ПЕЧАТАТЬ_ПУСТЫЕ_СТРОКИ OR (sb_depst.Date == DateReport))
              WriteToReport();
            end;
          end;
        end;
        stat = Next(sb_dtyp);
      else
        stat = FALSE;
      end;
    end;

    PrintSumBalAcc(ABalAccs(i));
    i = i + 1;
  end;

end;


/*************************************************************************\
|     Определение всех возможных балансовых счетов - резидентов и нет     |
\*************************************************************************/
macro DefBalAccs

  var stat;
  var FindBA;
  var FindBA_NotRez;
  var CurBalAcc;
  var CurBalAcc_NotRez;
  var TmpVal;
  var i = 0;
  var j = 0;

  Message(" Определение балансовых счетов ...");

  /* Получение массива всех возможных балансовых счетов */
  KeyNum(sb_dtyp,2);
  sb_dtyp.FlagCur = FlagCur;
  sb_dtyp.Kind    = "";
  stat = GetGE(sb_dtyp);
  while (stat)
    if (sb_dtyp.FlagCur == FlagCur)

      CurBalAcc = Trim(sb_dtyp.BalAcc);
      if (StrLen(CurBalAcc) > ConstLenBalAcc)
        CurBalAcc = SubStr(CurBalAcc,1,ConstLenBalAcc);
      end;
      CurBalAcc_NotRez = Trim(sb_dtyp.BalAccNotRez);
      if (StrLen(CurBalAcc_NotRez) > ConstLenBalAcc)
        CurBalAcc_NotRez = SubStr(CurBalAcc_NotRez,1,ConstLenBalAcc);
      end;

      if (ASize(ABalAccs) == 0)
        ABalAccs(0) = CurBalAcc;
      end;
      FindBA        = FALSE;
      FindBA_NotRez = FALSE;
      i = 0;
      while (i < ASize(ABalAccs))
        if (ABalAccs(i) == CurBalAcc)
          FindBA = TRUE;
        end;
        if (ABalAccs(i) == CurBalAcc_NotRez)
          FindBA_NotRez = TRUE;
        end;
        i = i + 1;
      end;
      if (FindBA == FALSE)
        ABalAccs(ASize(ABalAccs)) = CurBalAcc;
      end;
      if (FindBA_NotRez == FALSE)
        if (CurBalAcc != CurBalAcc_NotRez)
          ABalAccs(ASize(ABalAccs)) = CurBalAcc_NotRez;
        end;
      end;
      stat = Next(sb_dtyp);
    else
      stat = FALSE;
    end;
  end;

  /* Отсортируем полученный массив балансовых счетов в порядке возрастания */
  if (ASize(ABalAccs) > 0)
    i = 0;
    while (i < ASize(ABalAccs))
      j = i;
      while (j < ASize(ABalAccs))
        if (ABalAccs(j) < ABalAccs(i))
          TmpVal = ABalAccs(i);
          ABalAccs(i) = ABalAccs(j);
          ABalAccs(j) = TmpVal;
        end;
        j = j + 1;
      end;
      i = i + 1;
    end;
  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  var stat;

  BeginEndReport();

  /* Определение всех возможных балансовых счетов - резидентов и нет */
  DefBalAccs();
  /* ---------- Цикл по всем иностранным валютам ----------- */
  if (FlagCur)
    currency.Code_Currency = 0;
    stat = GetGE(currency);
    while (stat)
      if (currency.Code_Currency != NationalCodCur)
        TopReport();
        DoReport();
        BottomReport();
      end;
      stat = Next(currency);
    end;
  /* ------------ Отчет по национальной валюте ------------- */
  else
    currency.Code_Currency = NationalCodCur;
    stat = GetEQ(currency);
    if (stat)
      TopReport();
      DoReport();
      BottomReport();
    else
      [ Не найдена валюта с кодом ###](NationalCodCur);
    end;
  end;

  BeginEndReport();

end;
