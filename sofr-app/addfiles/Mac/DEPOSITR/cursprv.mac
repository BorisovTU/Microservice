/*****************************************************************************\
| Общие ф-и для печати справки 0406007                                        |
\*****************************************************************************/

import ExchangeInter;

private const /* Типы ценнностей (ресурсов кассы) в Retail */
      TYPERES_CURRENCY    =   1, /* Деньги (из справочника валют)           */
      TYPERES_VALFORM     =   2, /* Ценные бланки                           */
      TYPERES_VALUABLES   =   3, /* Др. ценности (из справочника ценностей) */
      TYPERES_PAYDOC      =   4, /* Платежные документы ОП                  */
      TYPERES_CARD        =  10, /* Пластиковые карточки                    */
      TYPERES_MINCAPISSUE = 500; /* Минимально возможный код ЦБ             */
private const
      TRS_CODE_DELIM      = "-";
var
      natCurency = {NationalCur};
/*--- Определение типа ценности (10, 10+11, 11 и т.п. ) ---------------------*/
macro TreasTypePanel( cvrefIn,   /* ref ресурса кассы полученной ценности         */
                      sumIn,     /* полученная сумма                              */
                      cvrefOut,  /* ref ресурса кассы выданной ценности           */
                      sumOut,    /* выданная сумма                                */
                      trsTypeIn, /* тип полученной ценности (default и на выходе) */
                      trsTypeOut /* тип выданной ценности (default и на выходе)   */ )

  macro toDouble( sum )                   /* сконвертировать money -> double, если нужно */
    if( ( valtype( sum ) == V_DOUBLE ) or ( valtype( sum ) == V_DOUBLEL ) )
      return sum;
    elif( valtype( sum ) == V_MONEY )
      return double( Sum );
    end;
    return sum;
  end;

  macro getDefaultTreasType( cvref, sum, trsCode, cvTrs ) /* получить значения по-умолчанию */

    var cashValue : TBFile,
        hasCoins,
        retValue;

    cvTrs.clear();
    sum = toDouble( sum );
    
    hasCoins   = false;
    cashValue  = TBFile( "sb_casvl.dbt" );
    if( ( trsCode == null ) or ( trsCode == "" ) )
      retValue = "XXX";
    else
      retValue = trsCode;
    end;

    cashValue.rewind();
    cashValue.clear();
    cashValue.rec.RefValue = cvref;
    if( cashValue.getEQ() )
      copy( cvTrs, cashValue );
      if( ( trsCode == null ) or ( trsCode == "" ) )
        if  ( cashValue.rec.TypeValue == TYPERES_CURRENCY    ) /* Деньги (из справочника валют)           */
          if( cashValue.rec.CodIntValue == natCurency )
            sum = sum / 10;
          end;
          if  ( floor( sum ) == sum )
            retValue = "10";
          elif( floor( sum ) == $0  )
            retValue = "11";
          else
            retValue = "10" + TRS_CODE_DELIM + "11";
          end;
        elif( cashValue.rec.TypeValue == TYPERES_VALFORM     ) /* Ценные бланки                           */
        elif( cashValue.rec.TypeValue == TYPERES_VALUABLES   ) /* Др. ценности (из справочника ценностей) */
        elif( cashValue.rec.TypeValue == TYPERES_PAYDOC      ) /* Платежные документы ОП                  */
          retValue = "20";
        elif( cashValue.rec.TypeValue == TYPERES_CARD        ) /* Пластиковые карточки                    */
        elif( cashValue.rec.TypeValue == TYPERES_MINCAPISSUE ) /* Минимально возможный код ЦБ             */
        else
          retValue = "36";
        end;
      end;
    end;
    return retValue;
  end;

  macro panelHandler( dlg, cmd, id, key )
    if( cmd == DLG_KEY )
      if( ( key == 32 ) and
          ( ( fldName( dlg, id ) == "InTrsCode"  ) or
            ( fldName( dlg, id ) == "OutTrsCode" )   ) )
        if  ( dlg.item( id ) == "10" )
          dlg.item( id ) = "10" + TRS_CODE_DELIM + "11";
        elif( dlg.item( id ) == "10" + TRS_CODE_DELIM + "11" )
          dlg.item( id ) = "11";
        elif( dlg.item( id ) == "11" )
          dlg.item( id ) = "10";
        end;
      elif( key == 316 /* F2 */ )
        return CM_SAVE;
      end;
    elif( cmd == DLG_INIT )
      if( ( dlg.item( fldIndex( dlg, "InTrsSum"  ) ) == $0 ) or
          ( ( dlg.item( fldIndex( dlg, "InTrsCode" ) ) != "10"    ) and
            ( dlg.item( fldIndex( dlg, "InTrsCode" ) ) != "10" + TRS_CODE_DELIM + "11" ) and
            ( dlg.item( fldIndex( dlg, "InTrsCode" ) ) != "11"    )    ) )
        disableFields( dlg, fldIndex( dlg, "InTrsCode" ) );
      end;
      if( ( dlg.item( fldIndex( dlg, "OutTrsSum"  ) ) == $0 ) or
          ( ( dlg.item( fldIndex( dlg, "OutTrsCode" ) ) != "10"    ) and
            ( dlg.item( fldIndex( dlg, "OutTrsCode" ) ) != "10" + TRS_CODE_DELIM + "11" ) and
            ( dlg.item( fldIndex( dlg, "OutTrsCode" ) ) != "11"    )    ) )
        disableFields( dlg, fldIndex( dlg, "OutTrsCode" ) );
      end;
    end;
    return CM_DEFAULT;
  end;

  var needPanel = true, /* !!! ДЛЯ ТОГО ЧТОБЫ НЕ ВЫВОДИТЬ ПАНЕЛЬ ПОСТАВИТЬ FALSE !!! */
      P_TRSCOD  = TRecHandler( "P_TRSCOD", null, true ),
      cvInTrs   = TRecHandler( "sb_casvl.dbt" ),
      cvOutTrs  = TRecHandler( "sb_casvl.dbt" ),
      retValue  = true;

  /* значения по-умолчанию */
  trsTypeIn  = getDefaultTreasType( cvrefIn,  sumIn,  trsTypeIn,  cvInTrs  );
  trsTypeOut = getDefaultTreasType( cvrefOut, sumOut, trsTypeOut, cvOutTrs );
  /* панель, если нужно */
  if( needPanel )
    clearRecord( P_TRSCOD );
    P_TRSCOD.rec.InTrsCashValue  = cvInTrs.rec.NameValue;
    P_TRSCOD.rec.InTrsSum        = sumIn;
    P_TRSCOD.rec.InTrsCode       = trsTypeIn;
    P_TRSCOD.rec.OutTrsCashValue = cvOutTrs.rec.NameValue;
    P_TRSCOD.rec.OutTrsSum       = sumOut;
    P_TRSCOD.rec.OutTrsCode      = trsTypeOut;
    retValue = RunDialog( P_TRSCOD, "panelHandler" );
    if( retValue )
      trsTypeIn  = P_TRSCOD.rec.InTrsCode;
      trsTypeOut = P_TRSCOD.rec.OutTrsCode;
    end;
  end;

  setParm( 4, trsTypeIn  );
  setParm( 5, trsTypeOut );

  return retValue;

end;