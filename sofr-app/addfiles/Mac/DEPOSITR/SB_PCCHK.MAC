/***************************************************************************\
| SB_PCCHK.MAC - макрос шага Проверка перед причислением процентов" операции|
| "Причисление процентов" для вида вклада "СБЕРЕГАТЕЛЬНЫЙ"                  |
|                                                     Зубов В.Ю. 14-08-98   |
\***************************************************************************/
import deprintr;


/*---------- Инициализируемые структуры ---------------------------------*/
 record ALG_DEPOSITOR("depositr.dbt");
 record ALG_SBDEPDOC("sbdepdoc.dbt");
 record ALG_PARAM("sb_algop.dbt");
 record dt("sb_dtyp.dbt");
 record alg ("pc_alg.dbt");
/*---------- Коды возврата макроса --------------------------------------*/
 const  OK_RES   =  0,  /* продолжать проводку */
        SKIP_RES =  1,  /* пропустить выполнение предопределенного модуля */
        END_RES  =  2,  /* завершить проводку */
        ERR_RES  = -1;  /* аваpийно завершить проводку */
 var    {curdate}, GrafDate;

/*--- MACRO WorkWithDate -------------------------------------------------*/
macro WorkWithDate
(
 Cur_Date,      /* Дата, от которой идет расчет (текущая) */
 Graf,          /* Стратегия расчета (оплаты) */
 Day,           /* День расчета (если стратегия- заданного числа каждого месяца */
 GrafDate       /* Выход- дата следующего расчета (оплаты) */
)
/*
  Определение даты текущего расчета (оплаты) процентов
*/
  var stat = TRUE;
  var d,m,y;

  DateSplit (Cur_Date,d,m,y);
  GrafDate = Date(0,0,0);
  if (Graf == 1)	/* В конце дня	*/
    GrafDate = Cur_Date;
  elif (Graf == 2)	/* В конце месяца */
    if (m == 12)
      m = 1;
      y = y + 1;
    else
      m = m + 1;
    end;
    GrafDate = Date(1,m,y) - 1;
  elif (Graf == 3)	/* В конце квартала	*/
    if (m > 9)
      m = 1;
      y = y + 1;
    elif (m > 6)
      m = 10;
    elif (m > 3)
      m = 7;
    else
      m = 4
    end;
    GrafDate = Date(1,m,y) - 1;
  elif (Graf == 4)	/* В конце года	*/
    GrafDate = Date(31,12,y);
  elif (Graf == 5)	/* Day числа каждого месяца */
    if (Day > 0)
      if (d > Day)
        m = m + 1;
        if (m > 12)
          m = 1;
          y = y + 1;
        end;
      end;
      GrafDate = Date(Day,m,y);
    end;
  end;
  SetParm (3,GrafDate);
  return stat;
end;

/*--- MACRO GetDepType --------------------------------------------------*/
MACRO GetDepType
  file dtyp("sb_dtyp.dbt") key 2;
  dtyp.FlagCur = ALG_DEPOSITOR.IsCur;
  dtyp.Kind    = ALG_DEPOSITOR.Type_Account;
  if (not GetEQ(dtyp))
    [Не найден вид вклада #](ALG_DEPOSITOR.Type_Account);
    return False;
  end;
  Copy(dt,dtyp);
  return True;
END;/*of MACRO*/
/**********************************************************/

macro GetLastPcAlg
  var stat;
  file pc_alg ("pc_alg.dbt");
  pc_alg.FlagCur    = dt.FlagCur;
  pc_alg.Referenc   = dt.Kind;
  pc_alg.ObjectType = 1003;
  pc_alg.BegDate    = {curdate};
  stat = (GetLE(pc_alg) AND (pc_alg.FlagCur    == dt.FlagCur)
                        AND (pc_alg.Referenc   == dt.Kind)
                        AND (pc_alg.ObjectType == 1003) );
  if (stat)
    Copy (alg,pc_alg);
  end;
  return stat;
end;

/*--- MACRO DiffLessMonth -----------------------------------------------*/
MACRO DiffLessMonth(date1,date2)
  Var MinDate,MaxDate,TmpDate,
      day,mon,year,delta;
  if(date1 < date2)
    MinDate=date1;
    MaxDate=date2;
  else
    MinDate=date2;
    MaxDate=date1;
  end;/*IF*/
  /*Вычисляем число дней в месяце,в котоый попадает первая дата*/
  DateSplit(MinDate,day,mon,year);
  mon = mon+1;
  if(mon > 12)
   mon=1;year=year+1;
  end;
  TmpDate = Date(1,mon,year)-1;/*Последний день нужного месяца*/
  DateSplit(TmpDate,day,mon,year);/*в day - число дней в месяце*/
  delta = MaxDate - MinDate;
  if (delta < day)
    return True;
  else
    return False;
  end; /*IF*/
END;/*of MACRO*/
 /*=== Точка входа в макрос =============================================*/
 ALG_RESULT = ERR_RES; /* Заранее инициализируем плохой код возврата */
 if(not GetDepType)
   /*Exit(1);*/
   return;
 end;
 if (not GetLastPcAlg())
   return;
 end;
 /* Если задан график причисления ...*/
 if( alg.GrafPay )
 /* ... и это причисления по графику причисления ...*/
   if ( not WorkWithDate({curdate},alg.GrafPay, alg.DayPay, GrafDate))
     /*Exit(1);*/
     return;
   end;
   if (GrafDate == {curdate})
 /* ... то: */
 /* ... если есть дата пролонгации... */
     if ((ALG_DEPOSITOR.Prol_DateDep != Date(0,0,0)) AND
 /* ... и она отстоит от текущей даты, менее чем на месяц... */
         DiffLessMonth(ALG_DEPOSITOR.Prol_DateDep-1,{curdate}))
 /* ... то завершать выполнение операции без ошибки.*/
       ALG_RESULT = END_RES;
       /*Exit(1);*/
       return;
     end; /* IF */
   end; /*IF*/
 end; /*IF(alg.GrafPay )*/
 ALG_RESULT = OK_RES;
 /*Exit(1);*//* Не выводить на экран */
/*------------------------- конец тела макроса --------------------------*/