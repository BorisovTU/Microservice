
/* Сохранение/восстановление базы для процедуры завершения года */

import DeprIntr;

record ALG_PARAM( "sb_algop.dbt" );

const
  OK_RES   =  0,  /* продолжать проводку */
  SKIP_RES =  1,  /* пропустить выполнение предопределенного модуля */
  END_RES  =  2,  /* завершить проводку */
  ERR_RES  = -1;  /* аваpийно завершить проводку */

var
  {curdate},    
  {ForwardState},
  ComSpec = GetEnv( "COMSPEC" ),
  Archiver =  GetRegVal( "RS-RETAIL\\INI\\ПЕРЕМЕННЫЕ\\ВНЕШНИЕ_ПРОГРАММЫ\\АРХИВАТОР\\ARCHIVER" ),
  ArchSwitches = GetRegVal( "RS-RETAIL\\INI\\ПЕРЕМЕННЫЕ\\ВНЕШНИЕ_ПРОГРАММЫ\\АРХИВАТОР\\ARCHSWITCHES" ),
  ExtrSwitches = GetRegVal( "RS-RETAIL\\INI\\ПЕРЕМЕННЫЕ\\ВНЕШНИЕ_ПРОГРАММЫ\\АРХИВАТОР\\EXTRSWITCHES" ),
  DBDir,
  ArchDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\ARHFILEDIR", true ),
  Save = ALG_PARAM.PrmC,
  ArchiveName;

array FilesToSave;

FilesToSave( 0 ) = "depositr.dbt";
FilesToSave( 1 ) = "sbdepdoc.dbt";
FilesToSave( 2 ) = "depclnt.dbt";
FilesToSave( 3 ) = "sb_congt.dbt";
FilesToSave( 4 ) = "auxinfo.dbt";
FilesToSave( 5 ) = "sb_depst.dbt";
FilesToSave( 6 ) = "trn_payf.dbt";
FilesToSave( 7 ) = "trn_doc.dbt";
FilesToSave( 8 ) = "accreg.dbt";
FilesToSave( 9 ) = "compens.dbt";
FilesToSave( 10 ) = "sb_casln.dbt";
FilesToSave( 11 ) = "pc_drest.dbt";
FilesToSave( 12 ) = "pc__calc.dbt";
FilesToSave( 13 ) = "pc_rdate.dbt";
FilesToSave( 14 ) = "pcestim.dbt";
FilesToSave( 15 ) = "pc_tax.dbt";

if ( {ForwardState} )
  DBDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\FORWFILEDIR", true );
else
  DBDir = GetIniString( "DATABASEDIR" );
end;

macro SetArchiveName

  var
    Dd, Mm, Yy;

  DateSplit( {curdate}, Dd, Mm, Yy );
  if ( Mm != 12 )
    Yy = Yy - 1;
  end; 
  ArchiveName = "eoy" + Yy;
end;

macro SaveDB

  var 
    StatStr,
    Stat = true,
    i = 0;

  PrintLn( "Сохранение базы" );
  PrintLn( "---------------" );
  PrintLn;
  [------------------------------];
  [| Файл        | Статус       |];
  [------------------------------];
  while ( Stat and ( i < ASize ( FilesToSave ) ) )
    Message( "Сохранение файла ", FilesToSave( i ), " ..." );
    Stat = not Run( ComSpec, " /C " +
      Archiver + " " + 
      ArchSwitches + " " + 
      ArchDir + ArchiveName + " " + 
      DBDir + FilesToSave( i ) + 
      " >nul" );
    if ( Stat )
      StatStr = "Нормально";
    else 
      StatStr = "Ошибка";
    end;
    [ ############# ############## ]
    (
      FilesToSave( i ),
      StatStr
    );
    i = i + 1;
  end;
  PrintLn;
  if ( Stat )
    ALG_RESULT = OK_RES;
    PrintLn( "Операция прошла успешно" );
  else
    PrintLn( "Ошибка при сохранении базы" );
    ALG_RESULT = ERR_RES;
  end;   
  return Stat;
end;

macro RestoreDB

  var
    Stat;

  Message( "Восстановление базы ..." );
  PrintLn( "Восстановление базы" );
  PrintLn( "-------------------" );
  Stat = not Run( ComSpec, " /C " +
    Archiver + " " + 
    ExtrSwitches + " " + 
    ArchDir + ArchiveName + " " + 
    DBDir +
    " >nul" );
     
  PrintLn;
  if ( Stat )
    PrintLn( "Операция прошла успешно" );
  else
    PrintLn( "Ошибка при восстановлении базы" );
  end;   
  return stat;
end;

  var
    Stat;

  SetArchiveName;
  if ( Save )
    Stat = SaveDB;
  else
    Stat = RestoreDB;
  end;

  if ( Stat )
    ALG_RESULT = OK_RES;
  else
    ALG_RESULT = ERR_RES;
  end;   
  
  ALG_UPDATE = 0;
end;




