import CommonInter, BrParm;

/* Виды отчетных периодов */

const
  cbPt_day     = 1,      	/* День */
  cbPt_week    = 2,             /* Неделя */
  cbPt_10days  = 3,             /* Декада */
  cbPt_month   = 4,             /* Месяц */
  cbPt_quarter = 5,             /* Квартал */
  cbPt_year    = 6,             /* Год */
  cbPt_irreg   = 7;		/* Нерегулярно */

/* Схемы предоставления */

const
  cbSch_branchToItself = 1,     /* Филиал (отделение) предоставляет данные себе */
  cbSch_branchToBank   = 2,     /* Филиал (отделение) предоставляет данные вышестоящей организации */
  cbSch_bankToBranch   = 3;     /* Вышестоящая организация предоставляет данные филиалу (отделению) */

/* Единицы измерения */

const
  cbMu_quantity    = 0, 	/* Количество */
  cbMu_wholeRubs   = 1,         /* Целые рубли */
  cbMu_roubles     = 2,         /* Рубли и копейки */
  cbMu_wholeThsR   = 3,         /* Целые тысячи рублей */
  cbMu_thousands2R = 4,         /* Тысячи рублей с 2-мя знаками после точки */
  cbMu_thousands3R = 5,         /* Тысячи рублей с 3-мя знаками после точки */
  cbMu_wholeMlnR   = 6,         /* Целые миллионы рублей */
  cbMu_millions2R  = 7,         /* Миллионы рублей с 2-мя знаками после точки */
  cbMu_millions3R  = 8,         /* Миллионы рублей с 3-мя знаками после точки */
  cbMu_wholeCUs     = 11,       /* Целые единицы валюты */
  cbMu_currUnits    = 12,       /* Единицы валюты с 2-мя знаками после точки*/
  cbMu_wholeThsCU   = 13,       /* Целые тысячи едининиц валюты */
  cbMu_thousands2CU = 14,       /* Тысячи едининиц валюты с 2-мя знаками после точки */
  cbMu_thousands3CU = 15,       /* Тысячи едининиц валюты с 3-мя знаками после точки */
  cbMu_wholeMlnCU   = 16,       /* Целые миллионы едининиц валюты */
  cbMu_millions2CU  = 17,       /* Миллионы едининиц валюты с 2-мя знаками после точки */
  cbMu_millions3CU  = 18;       /* Миллионы едининиц валюты с 3-мя знаками после точки */

/* Тип значения */

const
  cbFmt_numeric     = 1, 	/* Число */
  cbFmt_percent     = 2,        /* Процентная ставка */
  cbFmt_amount      = 3;        /* Сумма или количество штук */

private var
  {curdate};

private class TParameter( _id, _value, _valFmt )

  var
    id = _id, 
    value = _value, 
    valFmt = _valFmt;
end;

class TCBReportFile( _formId, _schema, _periodType, _branch, _date )

  private const
    delim = "=", 
    nSpacesInFirstLine = 100;

  private const
    qf_money = 1, 
    qf_quantity = 2;

  private var
    params = TArray, 
    nParams = 0, 
    formId = _formId, 
    schema = _schema, 
    periodType = _periodType, 
    branch = _branch, 
    branchName, 
    repDate = {curdate}, 
    periodStartDate, 
    periodEndDate, 
    measUnit = cbMu_roubles, 
    fName;

  private macro formatValue( p )
     
    var
      tmp,
      valFmt = p.valFmt;

    if ( valFmt != null )
      if ( valFmt == cbFmt_numeric ) 
        return string( p.value );
      elif ( valFmt == cbFmt_percent )
        tmp = string( p.value:0:4 );
        return mkStr( "0", 9 - strLen( tmp ) ) + tmp;
      end;
    end;

    if ( measUnit == cbMu_quantity )
      return string( p.value );
    elif ( ( measUnit == cbMu_roubles ) or ( measUnit == cbMu_currUnits ) )
      return string( p.value:0:2 );
    elif ( ( measUnit == cbMu_wholeThsR ) 
        or ( measUnit == cbMu_wholeMlnR )
        or ( measUnit == cbMu_wholeThsCU )
        or ( measUnit == cbMu_wholeMlnCU ) )
      return string( p.value:0:0 );
    elif ( ( measUnit == cbMu_thousands2R ) 
        or ( measUnit == cbMu_millions2R )
        or ( measUnit == cbMu_thousands2CU )
        or ( measUnit == cbMu_millions2CU ) )
      return string( p.value:0:2 );
    elif ( ( measUnit == cbMu_thousands3R ) 
        or ( measUnit == cbMu_millions3R )
        or ( measUnit == cbMu_thousands3CU )
        or ( measUnit == cbMu_millions3CU ) )
      return string( p.value:0:3 );
    end;
    runError( "Некорректно задана единица измерения" );
  end;

  private macro int2nChars( i, nChars )

    var
      str = string( i );
     
    nChars = nChars - strLen( str );
    while ( nChars > 0 )
      str = "0" + str;
      nChars = nChars - 1;
    end;

    return str;
  end;

  private macro int2char( i )

    var
      chars = "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ0";

    return subStr( chars, i + 1, 1 );
  end;

  private macro date2str( dd, type )

    var
      d, m, y;

    dateSplit( dd, d, m, y );  
    return int2nChars( d, 2 ) + int2nChars( m, 2 ) + int2nChars( mod( y, 1000 ), 3 ) + int2nChars( type, 1 ); 
  end; 

  private macro time2str( tt )

    var
      h, m;

    timeSplit( tt, h, m );
    return int2nChars( h, 2 ) + int2nChars( m, 2 );
  end;

  macro setReportDate( dd )

    repDate = dd;
  end;

  macro setPeriodStart( dd )

    periodStartDate = dd; 
  end;

  macro setPeriodEnd( dd )
    
    periodEndDate = dd; 
  end;

  macro setMeasUnit( mu )

    measUnit = mu;
  end;

  macro addParameter( id, val, valFmt )

    if ( not val )
      return;
    end;

    params[nParams] = TParameter( id, val, valFmt );
    nParams = nParams + 1;
  end;

  macro makeFileName

    var
      d, m, y, s, br;

    dateSplit( repDate, d, m, y );

    if ( branch )
      br = int2nChars( branch, 3 );
    else
      if ( schema == cbSch_branchToBank )
       schema = cbSch_bankToBranch;
      end;
      br = "BNK";
    end;

    return "#" 
         + int2char( formId ) 
         + int2char( m )
         + int2char( d )
         + subStr( string( y ), 3, 2 )
         + int2nChars( periodType, 1 )
         + int2char( schema + 9 )
         + "."
         + br;
  end;

  macro writeHead

    var
      br = branchName, 
      i = 0;

    while ( i < nSpacesInFirstLine )
      print( " " );
      i = i + 1;
    end;
    printLn;
    
    while ( strLen( br ) < 9 )
      br = " " + br;
    end;

    printLn( int2char( schema + 9 ) + delim  
           + br + delim
           + date2str( repDate, 1 ) + delim
           + date2str( periodStartDate, 2 ) + delim 
           + date2str( periodEndDate, 3 ) + delim 
           + date2str( date, 4 ) + delim 
           + time2str( time ) + delim 
           + int2nChars( nParams, 9 ) + delim
           + fName );
    
    printLn;
  end;

  macro write

    var
      i = 0; 

    fName = makeFileName;

    setOutput( getRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true ) + fName );
    writeHead;

    while ( i < nParams )
      printLn( params[i].id + delim + formatValue( params[i] ) );
      i = i + 1;
    end;
    
    setOutput( null, true );
  end;

  if ( branch == null )
    branch = numFNCash;
  end;

  getBranchParams( branch, branchName );

  if ( _date != null )
    repDate = _date;
  end;

  periodStartDate = periodEndDate = repDate;
end;