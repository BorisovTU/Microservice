import CommonInter, Archiver, cryptdlm;

var                                                         
  {CNum},
  ComSpec = GetEnv( "COMSPEC" ),
  Archiver = TArchiver, 
  ArcExt = Archiver.GetArchiveExt,
  TextDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", false ),
  writeESignDocs = true,
  writeESignRefs = true;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro CreatePwd( mask )

  var retval = Int( ( CodeFor( SubStr( mask, 1, 1)  ) * CodeFor( SubStr( mask,2,1 ) ) * 
	      	   CodeFor( SubStr( mask, 3, 1 ) ) * CodeFor( SubStr( mask, 4, 1 ) ) ) / 128 );

  return String( retval );
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro TrimPath( Path )
var Len = StrLen( Path );

  if ( SubStr( Path, Len ) == "\\" )
    return SubStr( Path, 1, Len - 1 );
  end;
  return Path;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro ESignFiles ( pathFiles, maskFiles, toArchive )

  var cryptoAPI = RsCryptoAPI( );
  var writeESign = writeESignRefs;
  var lenPath = StrLen( pathFiles );
  var listFiles = TRSLListFiles;
  var stat;
  var context;
  var retValue = true;

  if (toArchive)
    context = "RS-Retail|Транспорт|ПередачаФайла";
  else
    context = "RS-Retail|Транспорт|ПриемФайла";
  end;
  
  if ( StrLen( maskFiles ) >= 2 )
    if ( StrUpr( SubStr( maskFiles, 1, 2 ) ) == "DA" )
      writeESign = writeESignDocs;
    end;
  end;

  if (writeESign)
    writeESign = cryptoAPI.IsCryptoActionNeeded(context, 0);
  end;
  
  if ( not writeESign )
    return retValue;
  end;

  if ( lenPath > 0 )
    if ( SubStr( pathFiles, lenPath ) != "\\" )
      pathFiles = pathFiles + "\\";
    end;
  end;

  listFiles.MaskFile = pathFiles + maskFiles;
  listFiles.DirInList = false;

  stat = listFiles.First;
  while ( stat )
    if ( toArchive )
      if ( cryptoAPI.SignFile( context, listFiles.FileName ) )
        ;
      else
        stat = false;
        retValue = false;
      end;
    else
      if ( cryptoAPI.CheckFileSign( context, listFiles.FileName ) )
        ;
      else
        stat = false;
        retValue = false;
      end;
    end;
    if ( stat )
      stat = listFiles.Next;
    end;
  end;

  return retValue;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro CompressFiles( Path, Mask, Archived )
var Pwd = CreatePwd( Mask ),
    ArcName = SubStr(Mask,1,4),
    ArcPathName = Path + "\\" + ArcName + "." + ArcExt;

  Archiver.SetArchiveDir( Path );
  Archiver.SetDataDir( Path );
  Archiver.SetLogFileName( TextDir + "\\archlog." + String( {CNum} ) );

  if( Archived )
/* Архивирование */
    if ( ESignFiles( Path, Mask, Archived ) )
      if ( Archiver.ArchiveFiles( Mask, ArcName, Pwd ) )
        Run( ComSpec, " /C del " + Path + "\\" + Mask + " >nul" );
      end;
    else
      MsgBox( "Ошибка при простановке ЭЦП" );
    end;
  else
/* Разархивирование */
    if ( ExistFile( ArcPathName ) )
      if ( Archiver.ExtractAll( ArcName, Pwd ) )
        if ( ESignFiles( Path, Mask, Archived ) )
          DelFile( ArcPathName );
        else
          Run( ComSpec, " /C del " + Path + "\\" + Mask + " >nul" );
          MsgBox( "Ошибка при проверке ЭЦП" );
        end;
      else
        msgbox("Ошибка при работе с архивом!");
      end;
    end; 
  end;

end;
