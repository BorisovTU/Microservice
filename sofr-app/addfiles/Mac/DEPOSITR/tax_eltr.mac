/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Выписки по требованию налоговых органов.

  Справка о переводах электронных денежных средств клиента.

*****************************************************************/

import DeprIntr, RSD, SQLConv, SQLExec;

const FNCash = NumFNCash();

// Данные проводки
private var gDatePay;
private var gType;
private var gNumOper;
private var gDateTrans;
private var gSumDt;
private var gSumKt;

private var gCurrency = "643";

private const PTCK_BIC = 3;   // БИК (РФ).
private const PTCK_INN = 16;  // ИНН нашего банка.

private const DateNull = Date( 0, 0, 0 );

private var OurBank_BIC     = Trim( getRegNumOurBank( PTCK_BIC ) );
private var OurBank_Name    = "";
private var OurBank_INN     = Trim( getRegNumOurBank( PTCK_INN ) );
private var OurBank_KPP     = "";

private var {Bank_INN};
private var {Name_Bank};
private var {curdate};
private var {oper};

private var clientList = TClientList;

private var prsn = TBFile( "person.dbt",   "bank.def", "r", 0 );
private var tpac = TBFile( "typeac.dbt",   "bank.def", "r", 0 );
private var tcur = TBFile( "currency.dbt", "bank.def", "r", 0 );

var ErrCodeWR, ErrTextWR;

// ----------------------------------------
//    Строковый формат сумм с черточкой.
// ----------------------------------------
private macro Sum_Format( pSum )
  var rSum = "";
  var vSum = String( pSum:0:2:l );
  var lSum = StrLen( vSum );
  rSum = SubStr( vSum, 1, lSum-3 ) + "-" + SubStr( vSum, lSum-1, 2 );
  return rSum;
end;

// ----------------------------------------
//    Строковый формат сумм с черточкой.
// ----------------------------------------
private macro Sum_Format_Z( pSum )
  var rSum = "";
  var vSum = String( pSum:0:2:l );
  var lSum = StrLen( vSum );
  if ( pSum == 0.00 )
    rSum = ""; 
  else
    rSum = SubStr( vSum, 1, lSum-3 ) + "-" + SubStr( vSum, lSum-1, 2 );
  end;
  return rSum;
end;

// ----------------------------------------
//          Цифровой код валюты.
// ----------------------------------------
private macro Currency_Code( pCurr )
  if ( pCurr > 0 )
    tcur.Clear();
    tcur.rec.Code_Currency = pCurr;
    if ( tcur.GetEQ() )
      gCurrency = tcur.rec.EXTERNALCODE;
    end;
  end;
end;

// ----------------------------------------
//    Получение должности операциониста.
// ----------------------------------------
private macro GetNameTypePerson()
 var sWork = "";

 tpac.ReWind();
 tpac.Clear();
 tpac.rec.iNumType = 14;  /* Уровни доступов. */
 tpac.rec.Type_Account = prsn.rec.cTypePerson;
 if ( tpac.GetEQ() )
   sWork = tpac.rec.Name_Type;
 else
   sWork = "?????";
   println( " *** Ошибка при чтении должности операциониста с кодом = ", prsn.rec.cTypePerson );
   ErrCodeWR = prsn.Status( ErrTextWR );
   println( "  Ошибка " + String( ErrCodeWR ) + ": " + ErrTextWR );
 end;
 return sWork;
end;

// ----------------------------------------
//         Найти карточную связку.
// ----------------------------------------
private macro find_scLinkRec( cardFNC, cardRef )
  var cmd, rs;
  var retLink = -1;

  cmd = RsdCommand( "select LNK.T_ACCCARDLINKREF as cardLink " +
                      "from dsclink_dbt lnk " +
                        "where LNK.T_FNCASH = ? AND LNK.T_CARDREF = ? " +
                          "order by LNK.T_ACCCARDLINKOPENCLOSE, LNK.T_MAINACC desc" );

  cmd.addParam( "fncash", RSDBP_IN );
  cmd.addParam( "ref", RSDBP_IN );
  cmd.value( "fncash" ) = cardFNC;
  cmd.value( "ref" ) = cardRef;

  cmd.execute;

  rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    retLink = rs.value( "cardLink" );
  end;

  return retLink;
end;

// ----------------------------------------
//            Найти главную карту.
// ----------------------------------------
private macro find_MainCard( cardFNC, cardRef )

  var cmd, rs;
  var retLink = -1;

  cmd = RsdCommand( "select CR.T_CARDREF as cardRef " +
                      "from dsccard_dbt cr " +
                        "where CR.T_FNCASH = ? and CR.T_CONTRACTID = ? " +
                          "order by CR.T_CARDOPENDATE desc" );

  cmd.addParam( "fncash", RSDBP_IN );
  cmd.addParam( "ref", RSDBP_IN );
  cmd.value( "fncash" ) = cardFNC;
  cmd.value( "ref" ) = cardRef;

  cmd.execute;

  rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    retLink = rs.value( "cardRef" );
  end;

  return retLink;
end;

// ----------------------------------------
//    Найти ссылку на документы карты.
// ----------------------------------------
private macro find_scLink( cardRef )

  var cmd, rs;
  var retLink = -1;
  var vMain = -1;
  var vBranch = -1;
  var vAdd = -1;
  var card_Ref = cardRef;

  cmd = RsdCommand( "SELECT CNTR.T_MAINCARDCNTRID as main_card, CNTR.T_BRANCH as branch, CNTR.T_ADDCARDOWNACC as add_card " +
                      "FROM drtcardcontract_dbt cntr, dsccard_dbt sccard " +
                        "WHERE CNTR.T_BRANCH = sccard.t_fncash AND CNTR.T_ID = sccard.t_contractID AND " +
                          "sccard.T_FNCASH=? AND sccard.T_CARDREF=?" );

  cmd.addParam( "fncash", RSDBP_IN );
  cmd.addParam( "ref", RSDBP_IN );
  cmd.value( "fncash" ) = FNCash;
  cmd.value( "ref" ) = cardRef;

  cmd.execute;

  rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    vMain   = rs.value( "main_card" );
    vBranch = rs.value( "branch" );
    vAdd    = rs.value( "add_card" );
  end;

  if ( vMain > -1 )

    if ( ( vMain > 0 )  AND  ( vAdd != "X" ) )
      card_Ref = find_MainCard( vBranch, vMain );
    end;

    if ( card_Ref > -1 )
      retLink = find_scLinkRec( FNCash, card_Ref );
    end;

  end;

  return retLink;
end;

// ----------------------------------------
//         Поиск типа транзакции.
// ----------------------------------------
private macro find_TransType( type, psref )
  var ret = "???";
  var cmd, rs;

  cmd = RsdCommand( "select cd.T_CODUSERCODE as card_code " +
                      "from dsccodif_dbt cd " +
                        "where CD.T_CODTYPE = 20 AND cd.T_CODCODE = ? AND (CD.T_PSREF = 0 OR CD.T_PSREF = ?) " +
                          "order by CD.T_PSREF desc" );

  cmd.addParam( "type", RSDBP_IN );
  cmd.addParam( "ref", RSDBP_IN );
  cmd.value( "type" ) = type;
  cmd.value( "ref" ) = psref;

  cmd.execute;

  rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    ret = rs.value( "card_code" );
  end;
  return ret;
end;

// ----------------------------------------
//     Получить транзакцию по карте.
// ----------------------------------------
private macro get_CardTransact( auth )
  var ret = FALSE;
  var cmd, rs;

  cmd = RsdCommand( "select T.T_AUTHDATE as adate, T.T_AUTHTYPECODE as atype, T.T_PSREF as psref, T.T_AUTHCODE as aCode, T.T_AUTHNUMCODE as aNum, T.T_AUTHDATE as aDate " +
                      "from dsctran_dbt t where T.T_FNCASH=? AND T.T_AUTHREF=?" );

  cmd.addParam( "fncash", RSDBP_IN );
  cmd.addParam( "ref", RSDBP_IN );
  cmd.value( "fncash" ) = FNCash;;
  cmd.value( "ref" ) = auth;

  cmd.execute;

  rs = RsdRecordset( cmd );

  if ( rs.moveNext )

    gType = find_TransType( rs.value( "atype" ), rs.value( "psref" ) );
    gNumOper = rs.value( "aCode" ) + "/" + SubStr( String( (10000 + rs.value( "aNum" )):5 ), 4, 2 );
    gDateTrans = rslDate( rs.value( "aDate" ) );

    // println( "Нашли транзакцию по карте=", gType, "=", gNumOper, "=", gDateTrans );
    ret = TRUE;
  end;

  return ret;
end;

// ----------------------------------------
//   Получить вкладной документ по карте.
// ----------------------------------------
private macro get_CardDepDoc( kind, key )
  var ret = FALSE;
  var cmd, rs;

  cmd = RsdCommand( "select T.T_TYPEOPER as toper, T.T_DEPDATE_DOCUMENT as depDate, T.T_INSUM as sumKt, T.T_OUTSUM as sumDt " +
                      "from dsbdepdoc_dbt t where T.T_APPLICATIONKEY=?" );

  cmd.addParam( "ref", RSDBP_IN );
  cmd.value( "ref" ) = key;

  cmd.execute;

  rs = RsdRecordset( cmd );

  if ( rs.moveNext )

    gType = String( rs.value( "toper" ):5:l );
    gNumOper = "-";
    gDateTrans = rslDate( rs.value( "depDate" ) );
    gSumKt   = rs.value( "sumKt" );
    gSumDt   = rs.value( "sumDt" );

    // println( "Нашли вкладной документ по карте=", gType, "=", gNumOper, "=", gDateTrans, "=", gSumKt, "=", gSumDt );
    ret = TRUE;
  end;

  return ret;
end;

// ----------------------------------------
//     Получить данные по карте.
// ----------------------------------------
private macro get_CardData( auth, kind, key )
  var ret  = TRUE;
  var find = FALSE;
  // Транзакция.
  if ( auth > 0 )
    find = get_CardTransact( auth );
  end;
  // Вкладной документ.
  if ( NOT find )
    if ( ( kind != 0 )  AND  ( strlen( key ) > 0 ) )
      find = get_CardDepDoc( kind, key );
    end;
  end;
  // Карточный документ.
  if ( NOT find )
    ret = TRUE;
  end;
  return ret;
end;

// ----------------------------------------
private macro MakeReport( CodeClient, cardRef, CardNumber, Date1, Date2 )

  var cmd, rs;
  var vLink = find_scLink( cardRef );
  var vRestIn = 0.0;
  var vRestOt = 0.0;
  var vTurnDt = 0.0;
  var vTurnKt = 0.0;
  var nn = 1;
  var vKod = 1;
  var flagTitle = TRUE;
  var vCurr = 0;
  var vNo = "";
  var sOper = "";
  var sWork = "";

  if ( ( ValType( codeClient ) != V_INTEGER )  OR  ( codeClient == 0 )  OR  ( NOT clientList.GetRecord( codeClient ) ) )
    println( " " );
    println( "  Клиент с кодом " + CodeClient + " не найден." );
  end;

  prsn.Clear();
  prsn.rec.Oper = {oper};

  if ( prsn.GetEQ() )
    sOper = prsn.rec.Name; 
  else
    println( " " );
    println( "  Пользователь с кодом " + {oper} + " не найден." );
  end;

  sWork = GetNameTypePerson();

  OurBank_Name = {Name_Bank};

  [
                                                                                     Форма по КНД 1114310

                                                             

                                                             ____________________________________________
                                                                   (наименование налогового органа)


                                                             ____________________________________________
                                                                               (адрес)



                                                    Справка 
                                    о переводах электронных денежных средств


     ####################################################################################################
     ----------------------------------------------------------------------------------------------------
        (полное наименование (сокращенное наименование) банка в соответствии с Книгой государственной
                                     регистрации кредитных организаций)


             +-------------------+                    +-------------------+
     ИНН     |#|#|#|#|#|#|#|#|#|#|            КПП     | | | | | | | | | | |
             +-------------------+                    +-------------------+

             +-----------------+
     БИК     |#|#|#|#|#|#|#|#|#|
             +-----------------+

           В соответствии с запросом налогового органа от _________________   № ____________________
           в отношении
      ####################################################################################################
      ----------------------------------------------------------------------------------------------------
                                          (Ф.И.О. физического лица)

             +-----------------------+            +-----------------+
     ИНН/КИО |#|#|#|#|#|#|#|#|#|#|#|#|        КПП | | | | | | | | | |
             +-----------------------+            +-----------------+]
  (
    ( "Банк " + OurBank_Name ):w,
    SubStr( OurBank_INN,  1, 1 ),
    SubStr( OurBank_INN,  2, 1 ),
    SubStr( OurBank_INN,  3, 1 ),
    SubStr( OurBank_INN,  4, 1 ),
    SubStr( OurBank_INN,  5, 1 ),
    SubStr( OurBank_INN,  6, 1 ),
    SubStr( OurBank_INN,  7, 1 ),
    SubStr( OurBank_INN,  8, 1 ),
    SubStr( OurBank_INN,  9, 1 ),
    SubStr( OurBank_INN, 10, 1 ),
    SubStr( OurBank_BIC,  1, 1 ),
    SubStr( OurBank_BIC,  2, 1 ),
    SubStr( OurBank_BIC,  3, 1 ),
    SubStr( OurBank_BIC,  4, 1 ),
    SubStr( OurBank_BIC,  5, 1 ),
    SubStr( OurBank_BIC,  6, 1 ),
    SubStr( OurBank_BIC,  7, 1 ),
    SubStr( OurBank_BIC,  8, 1 ),
    SubStr( OurBank_BIC,  9, 1 ),
    ( ClientList.CurRec.Rec.Name1 + " " + ClientList.CurRec.Rec.Name2 + " " + ClientList.CurRec.Rec.Name3 ):w:c,
    SubStr( ClientList.CurRec.Rec.INN,  1, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  2, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  3, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  4, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  5, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  6, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  7, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  8, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  9, 1 ),
    SubStr( ClientList.CurRec.Rec.INN, 10, 1 ),
    SubStr( ClientList.CurRec.Rec.INN, 11, 1 ),
    SubStr( ClientList.CurRec.Rec.INN, 12, 1 )
  );

  if ( vLink < 0 )
    println( " " );
    println( " " );
    println( " " );
    println( "    Не найдна ссылка на документы карты по FNCash = ", FNCash, ", CardRef = ", cardRef );
    return;
  end;

  cmd = RsdCommand( "select  T.T_AUTHREF as auth, T.T_DEPDOCAPPKIND as kind, T.T_DEPDOCAPPKEY as key, T.T_DATE as cardDate, T.T_INSUM as sumKt, T.T_OUTSUM as sumDt, " +
                            "T.T_NUMOPER as operType, T.T_CURRCODE as curr, T.T_REST as rest " +
                      "from dSccrddoc_dbt t " +
                        "where T.T_FNCASH=? and T.T_ACCCARDLINKREF=? and T.T_ACTION NOT IN (2,11) and T.T_DATE>=? and T.T_DATE<=? " +
                          "order by T.T_DATE, T.T_NUMDAYDOC" );

  cmd.addParam( "fncash", RSDBP_IN );
  cmd.addParam( "ref",    RSDBP_IN );
  cmd.addParam( "date1",  RSDBP_IN );
  cmd.addParam( "date2",  RSDBP_IN );
  cmd.value( "fncash" ) = FNCash;
  cmd.value( "ref"    ) = vLink;
  cmd.value( "date1"  ) = Date1;
  cmd.value( "date2"  ) = Date2;

  cmd.execute;

  rs = RsdRecordset( cmd );

  while ( rs.moveNext )

    vCurr = rs.value( "curr" );
    Currency_Code( vCurr );

    vTurnDt = vTurnDt + rs.value( "sumDt" );
    vTurnKt = vTurnKt + rs.value( "sumKt" ); 

    if ( flagTitle )

      vRestIn = rs.value( "rest" ) - rs.value( "sumKt" ) + rs.value( "sumDt" );
      
      [
             представляет справку о переводах электронных денежных средств в отношении электронного средства платежа:
                +-------------------------------------------------+
             №  |#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|
                +-------------------------------------------------+

                +-----+
                |#|#|#|
                +-----+
             (цифровой код валюты (в соответствии с Общероссийским классификатором валют)

             за период с ########### по ###########.]
        (
          SubStr( CardNumber,  1, 1 ),
          SubStr( CardNumber,  2, 1 ),
          SubStr( CardNumber,  3, 1 ),
          SubStr( CardNumber,  4, 1 ),
          SubStr( CardNumber,  5, 1 ),
          SubStr( CardNumber,  6, 1 ),
          SubStr( CardNumber,  7, 1 ),
          SubStr( CardNumber,  8, 1 ),
          SubStr( CardNumber,  9, 1 ),
          SubStr( CardNumber, 10, 1 ),
          SubStr( CardNumber, 11, 1 ),
          SubStr( CardNumber, 12, 1 ),
          SubStr( CardNumber, 13, 1 ),
          SubStr( CardNumber, 14, 1 ),
          SubStr( CardNumber, 15, 1 ),
          SubStr( CardNumber, 16, 1 ),
          SubStr( CardNumber, 17, 1 ),
          SubStr( CardNumber, 18, 1 ),
          SubStr( CardNumber, 19, 1 ),
          SubStr( CardNumber, 20, 1 ),
          SubStr( CardNumber, 21, 1 ),
          SubStr( CardNumber, 22, 1 ),
          SubStr( CardNumber, 23, 1 ),
          SubStr( CardNumber, 24, 1 ),
          SubStr( CardNumber, 25, 1 ),

          SubStr( gCurrency,  1, 1 ),
          SubStr( gCurrency,  2, 1 ),
          SubStr( gCurrency,  3, 1 ),

          Date1:r,
          Date2:r
        );

      [
          +-------+---------------+-------------------------------------------------------------+---------------------------------------+------------+
          |   №   | Дата проводки |             Реквизиты распоряжения, на основании            |       Сумма перевода, влекущего в     |    Код     |
          |  п/п  |  (дд.мм.гггг) |               которого был осуществлен перевод              |      отношении остатка электронных    |  перевода  |
          |       |               |                                                             |             денежных средств          |            |
          |       |               +-------+---------------------------------------+-------------+-------------------+-------------------+------------+
          |       |               |  Тип  |                Номер                  |Дата операции|     уменьшение    |     увеличение    |            |
          +-------+---------------+-------+---------------------------------------+-------------+-------------------+-------------------+------------+];
      flagTitle = FALSE;
    end;

    gDatePay = rslDate( rs.value( "cardDate" ) );
    gType    = String( rs.value( "operType" ):5:l );
    gNumOper = "-";
    gDateTrans = gDatePay;
    gSumKt   = rs.value( "sumKt" );
    gSumDt   = rs.value( "sumDt" );

    if ( get_CardData( rs.value( "auth" ), rs.value( "kind" ), rs.value( "key" ) ) )

      if ( gSumDt > 0.00 )
        vKod = 1;
      else
        vKod = 2;
      end;
      [   | ##### |  ############ | ##### | ##################################### | ########### | ################# | ################# |  ######### |]
      (
        nn:5,
        gDatePay,
        gType:5,
        gNumOper:37,
        gDateTrans,
        Sum_Format_Z( gSumDt ):15:r,
        Sum_Format_Z( gSumKt ):15:r,
        vKod:c
      );
      [   +-------+---------------+-------+---------------------------------------+-------------+-------------------+-------------------+------------+];

      nn = nn + 1;

    end;
  end;

  if ( flagTitle )
    [
           представляет справку о переводах электронных денежных средств в отношении электронного средства платежа:
              +-------------------------------------------------+
           №  |#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|
              +-------------------------------------------------+

           +-----+
           |6|4|3|
           +-----+
           (цифровой код валюты (в соответствии с Общероссийским классификатором валют)

           за период с ########### по ###########.

    ]
      (
        SubStr( CardNumber,  1, 1 ),
        SubStr( CardNumber,  2, 1 ),
        SubStr( CardNumber,  3, 1 ),
        SubStr( CardNumber,  4, 1 ),
        SubStr( CardNumber,  5, 1 ),
        SubStr( CardNumber,  6, 1 ),
        SubStr( CardNumber,  7, 1 ),
        SubStr( CardNumber,  8, 1 ),
        SubStr( CardNumber,  9, 1 ),
        SubStr( CardNumber, 10, 1 ),
        SubStr( CardNumber, 11, 1 ),
        SubStr( CardNumber, 12, 1 ),
        SubStr( CardNumber, 13, 1 ),
        SubStr( CardNumber, 14, 1 ),
        SubStr( CardNumber, 15, 1 ),
        SubStr( CardNumber, 16, 1 ),
        SubStr( CardNumber, 17, 1 ),
        SubStr( CardNumber, 18, 1 ),
        SubStr( CardNumber, 19, 1 ),
        SubStr( CardNumber, 20, 1 ),
        SubStr( CardNumber, 21, 1 ),
        SubStr( CardNumber, 22, 1 ),
        SubStr( CardNumber, 23, 1 ),
        SubStr( CardNumber, 24, 1 ),
        SubStr( CardNumber, 25, 1 ),
        Date1:r,
        Date2:r
      );
    println( "    У клиента не было перевода электронных средств" );
  else
    [
        +---------------------------+---------------------------+---------------------------+---------------------------+
        |    Остаток электронных    | Сумма переводов, влекущих | Сумма переводов, влекущих |    Остаток электронных    |
        |    денежных средств на    |    уменьшение остатка     |    увеличение остатка     | денежных средств на конец |
        |       начало периода      |   электронных денежных    |   электронных денежных    |         периода           |
        |                           |    средств, за период     |    средств, за период     |                           |
        +---------------------------+---------------------------+---------------------------+---------------------------+
        |      ###############      |      ###############      |      ###############      |      ###############      |
        +---------------------------+---------------------------+---------------------------+---------------------------+
    ]
    (
      Sum_Format( vRestIn ):15:r,
      Sum_Format( vTurnDt ):15:r,
      Sum_Format( vTurnKt ):15:r,
      Sum_Format( vRestIn - vTurnDt + vTurnKt ):15:r
    );

  end;

  [

          Представитель банка   ##############################  ########################################                       ##########
                                ------------------------------  ----------------------------------------  -------------------  ----------
                                          (должность)                          (Ф.И.О.)                        (подпись)         (дата)



                                                                        М.П.]
  (
    sWork:30:c,
    sOper:42:c,
    {curdate}
  );

end;


// ----------------------------------------
macro TaxTransReport( CodeClient, cardRef, CardNumber, Date1, Date2 )

  if ( Date1 == DateNull )
    Date1 = Date( 1, 1, 2001 );
  end;
  if ( Date2 == DateNull )
    Date2 = {curdate};
  end;

  MakeReport( CodeClient, cardRef, CardNumber, Date1, Date2 ); 

end;
