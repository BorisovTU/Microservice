/* Расчет дохода по сертификату 1999 г. ( срок обращения в днях ) */

import CommonInter;

file CapIssue("capissue.dbt") key 0;
file CertRate("certrate.dbt") key 0;
file DemandRate("dem_rate.dbt") key 0;
file Cert("cert.dbt") key 0;

const
  CI_CERT=3;

/* Флаги настройки */

const
  IncludeSellDate=false,      /* Включать дату выдачи                      */ 
  NoYieldAfterEndOfTerm=TRUE; /* Не считать доход после конца срока обращ. */	

var
  IncludeDemandDate=false,    /* Включать дату возврата                    */
  ClientList = TClientList, 
  CBRFRate,
  SaveCBRFRate : TRecHandler,
  CurrentCBRFRate : TRecHandler;

var
  TreatLeapness=true,         /* Учитывать високосность                    */
  TreatLeapnessDem=true;      /* Учитывать високосность после конца срока  */

/* "Возвращаемое" значение */

var
  Tax = $0, 
  Yield;

/* Параметры сертификата */

var
  TreatLeap,
  C_Issue,
  C_Par,
  C_Duration,
  C_IncludeDemandDate,
  C_SellDate,
  C_DemandDate, 
  C_ClientCode;

var Presched = false;

array
  DaysInMon;

DaysInMon(  1 ) = 31;
DaysInMon(  2 ) = 28;
DaysInMon(  3 ) = 31;
DaysInMon(  4 ) = 30;
DaysInMon(  5 ) = 31;
DaysInMon(  6 ) = 30;
DaysInMon(  7 ) = 31;
DaysInMon(  8 ) = 31;
DaysInMon(  9 ) = 30;
DaysInMon( 10 ) = 31;
DaysInMon( 11 ) = 30;
DaysInMon( 12 ) = 31;

macro MyMin(a,b)

  if(a<b)
    return a;
  else
    return b;
  end;
end;


macro DaysInYear( Year )

  if ( Year / 4.0 == Int( Year / 4 ) )   /* Ламерство, знаю, но если этот макрос */
    return 366;                          /* доживет до 2100 года, править его уже */
  else                                   /* явно буду не я */
    return 365;
  end;
end;

macro DateDiff(Date1,Date2)

  var
    Diff=Date2-Date1,
    Day1,Day2,
    Mon1,Mon2,
    Year1,Year2,
    Feb29;

  if ( not TreatLeap )
    DateSplit(Date1,Day1,Mon1,Year1);
    DateSplit(Date2,Day2,Mon2,Year2);
    
    if(Year1/4.0==Int(Year1/4))
      Feb29=Date(29,2,Year1);
      if((Date1<Feb29) and (Date2>=Feb29))
        Diff=Diff-1;
      end;
    elif(Year2/4.0==Int(Year2/4))
      Feb29=Date(29,2,Year2);
      if((Date1<Feb29) and (Date2>=Feb29))
        Diff=Diff-1;
      end;
    end;
    return Diff;
  else
    return Diff;
  end;
end;

macro PartOfYear( Date1, Date2 )

  var
    Day,
    Mon,
    Year,
    YearStart;

  if ( ( Date1 != C_SellDate ) or ( not IncludeSellDate ) )
    Date1 = Date1 + 1;
  end;

  if ( ( Date2 != C_DemandDate ) or C_IncludeDemandDate )
    Date2 = Date2 + 1;
  end;
	              
  if ( Date1 >= Date2 )
    return 0;
  end;

  if ( TreatLeap )
    DateSplit( Date1, Day, Mon, Year );
    return Double( DateDiff( Date1, Date2 ) ) / DaysInYear( Year );
  else       
    return Double( DateDiff( Date1, Date2 ) ) / 365;
  end;
end;


macro OutHead( SellDate, DemandDate )

  [ ];
  [#################################################################]
  ( "Протокол расчета дохода по сертификату":c );
  [#################################################################]
  ( Cert.Desc:c ); 
  [ ];
  [ Начало периода:  ########## ]( SellDate );
  [ Конец периода:   ########## ]( DemandDate );
  [ ];
  [-----------------------------------------------------------------];
  [|       Период        |    |           |            |           |];
  [|---------------------|Дней| Ставка %% | Расч. база |   Доход   |];
  [|    c     |    по    |    |           |            |           |];       
  [-----------------------------------------------------------------];
end;

macro OutLine( Date1, Date2, Rate, Yld )
                            
  if ( ( Date1 != C_SellDate ) or ( not IncludeSellDate ) )
    Date1 = Date1 + 1;
  end;

  if ( ( Date2 != C_DemandDate ) or C_IncludeDemandDate )
    Date2 = Date2 + 1;
  end;

  if ( Date1 >= Date2 )
    return;
  end;

  [ ########## ########## #### ########### ############ ########### ]
  (
    Date1,
    Date2 - 1,
    DateDiff( Date1, Date2 ),
    Rate / 100:0:2,
    C_Par,
    Money( Yld )
  ); 
end;

macro OutTotalYield

  [-----------------------------------------------------------------];
  [                                                     ########### ]
  ( Money( Yield ) );
end;

macro GetCertParms

  CapIssue.Type=CI_CERT;
  if(not GetEQ(CapIssue))
    MsgBox("Тип ЦБ 'Сберегательные сертификаты' не найден");
    Exit;
  end;
end;

macro GetRate
  
  return CertRate.YearRate;
end;

macro GetYield( BegDate, EndDate, Date1, Date2, Rate, Report )

  var
    POY,
    Day1, Day2,
    Mon1, Mon2,
    Year1, Year2,
    YearEnd,
    Tmp,
    Yld = $0;

  DateSplit( Date1, Day1, Mon1, Year1 );
  DateSplit( Date2, Day1, Mon2, Year2 );
  if ( ( not TreatLeapness ) or ( DaysInYear( Year1 ) == DaysInYear( Year2 ) ) )
    POY = PartOfYear( Date1, Date2 );
    Yld=Round( Money( Double( C_Par )*Rate*POY/10000 ) ); 
    if ( ( ValType( Report ) != V_BOOL ) or Report )
      OutLine( Date1, Date2, Rate, Yld );
    end; 
  else
    YearEnd = Date( 31, 12, Year1 );
    POY = PartOfYear( Date1, YearEnd );
    if ( POY )
      Yld=Round( Money( Double( C_Par )*Rate*POY/10000 ) ); 
      if ( ( ValType( Report ) != V_BOOL ) or Report )
        OutLine( Date1, YearEnd, Rate, Yld );
      end;   
    end;
    POY = PartOfYear( YearEnd, Date2 );
    if ( POY )
      Tmp=Round( Money( Double( C_Par )*Rate*POY/10000 ) );
      Yld=Yld+Tmp; 
      if ( ( ValType( Report ) != V_BOOL ) or Report )
        OutLine( YearEnd, Date2, Rate, Tmp );
      end;
    end;
  end;
  return Yld;
end;

macro GetTaxRate


  if ( not ClientList.GetRecord( C_ClientCode ) )
    MsgBox( "Не найден клиент" );
    Exit( 1 );
  end;

  if ( ClientList.CurRec.Rec.NotResident == StrFor( 0 ) )
    return CurrentCBRFRate.Rec.TaxRate;
  else
    return CurrentCBRFRate.Rec.TaxRateForeigh;
  end;
end;

macro CalcTaxForSpan( BegDate, EndDate, Date1, Date2, Rate )

  var                               
    Tmp1,
    Tmp2,
    LimRate,
    PartialTax;

  if ( ( SaveCBRFRate.Rec.QPart_m == 0 ) and ( SaveCBRFRate.Rec.QPart_n == 0 ) )
    SaveCBRFRate.Rec.QPart_m = 1;
    SaveCBRFRate.Rec.QPart_n = 1;
  end;

  LimRate = SaveCBRFRate.Rec.Rate * 100 * SaveCBRFRate.Rec.QPart_m / SaveCBRFRate.Rec.QPart_n;

  if ( Rate > LimRate )
    Tmp1 = GetYield( BegDate, EndDate, Date1, Date2, Rate, false );
    Tmp2 = GetYield( BegDate, EndDate, Date1, Date2, LimRate, false );
    PartialTax = Round( Money( Double( Tmp1 - Tmp2 ) * GetTaxRate / 100 ) );
    Tax = Tax + PartialTax;
  end;
end;

macro CalcTax( BegDate, EndDate, Date1, Date2, Rate )

  var
    SpanStart, 
    RecFound;

  if ( Date1 > Date2 )
    return;
  end;                    

  RecFound = CBRFRate.GetRecForDate( Date1 );

  if ( not RecFound )
    return;
  end;

  SpanStart = Date1;
  Copy( SaveCBRFRate, CBRFRate.RecHandler );
  while ( CBRFRate.Next
    and ( CBRFRate.Rec.EffectiveDate <= Date2 ) )
    CalcTaxForSpan( BegDate, EndDate, SpanStart, CBRFRate.Rec.EffectiveDate, Rate );
    SpanStart = CBRFRate.Rec.EffectiveDate - 1;
    Copy( SaveCBRFRate, CBRFRate.RecHandler );
  end;

  CalcTaxForSpan( BegDate, EndDate, SpanStart, Date2, Rate );
end;

macro CalcForPeriod(BegDate,EndDate)

  var
    RetVal=$0,
    Rate;

  TreatLeap = TreatLeapness;

  if(BegDate<EndDate)
    Rate=GetRate;
    EndDate=MyMin(C_DemandDate,EndDate);
    RetVal=GetYield( BegDate, EndDate, BegDate, EndDate, Rate );
    CalcTax( BegDate, EndDate, BegDate, EndDate, Rate );
  end;   
  return RetVal;
end;

macro CalcPresched( BegDate, EndDate )

  TreatLeap = TreatLeapnessDem;

  DemandRate.StartDate = EndDate;

  if ( not GetLE( DemandRate ) )
    MsgBox( "Ставка до востреб. на " + String( BegDate ) + " не задана" );
    Exit;
  end;

  return GetYield( BegDate, EndDate, BegDate, C_DemandDate, DemandRate.Rate );
end;

macro CheckDates(SellDate,DemandDate)

  if(SellDate>DemandDate)
    return false;
  else
    ClearRecord( CertRate );
    CertRate.Issue=C_Issue;

    if ( GetGE( CertRate ) )
      if ( SellDate < CertRate.Date )
        return false;
      else
        return true; 
      end;
    else
      return false;
    end;
  end;
end;

macro FindCert(Issue)
  
  Cert.Type=CI_CERT;
  Cert.Issue=Issue;
  if(not GetEQ(Cert))
    MsgBox("Выпуск сертификата не найден");
    Exit;
  end;
end;

macro PeriodEndDate( Year )

  var
    Y2 = Year + 1;

  if ( Date( 31, 12, Year ) == C_SellDate )
    Y2 = Y2 + 1;
  end;

  if ( not TreatLeapness )
    return C_DemandDate;
  end;

  while ( DaysInYear( Y2 ) == DaysInYear( Year ) )
    Y2 = Y2 + 1;
  end;

  return MyMin( C_DemandDate, Date( 31, 12, Y2 - 1 ) );
end;

macro CalcYield(Issue,Par,Duration,SellDate,DemandDate,DurTermType,ClientCode)

  var
    BegDate,
    EndDate,
    EndOfTerm,
    Day, Mon, Year,   
    Stat = true;

  EndOfTerm = SellDate + Duration;

  if((SellDate > Date( 4, 04, 2005)) and (EndOfTerm == DemandDate + 1))
      EndOfTerm =  EndOfTerm - 1;
      IncludeDemandDate = true;
  end;

  if ( DemandDate < EndOfTerm )
    Presched = true;
  end;

  CBRFRate = TCbRfRateList;
  SaveCBRFRate = TRecHandler( "cbrfrate.rec" );
  CurrentCBRFRate = TRecHandler( "cbrfrate.rec" );
  CBRFRate.SetFilter( 0 );
  if ( CBRFRate.GetRecForDate( DemandDate ) )
    Copy( CurrentCBRFRate, CBRFRate.RecHandler );
  else
    Stat = false;
  end;

  if ( not Stat )
    MsgBox( "Не найдена ставка рефинансирования на дату востребования сертификата" );
    Exit( 1 );
  end;                                     

  Copy( CurrentCBRFRate,CBRFRate.RecHandler );

  FindCert(Issue);
  TreatLeapness = Cert.TreatLeapness;
  TreatLeapnessDem = Cert.TreatLeapnessDem;
  OutHead(SellDate,DemandDate);

  C_IncludeDemandDate = IncludeDemandDate;
  C_Issue=Issue;
  C_Par=Par;
  C_Duration=Duration;
  C_SellDate = SellDate;
  C_ClientCode = ClientCode;
  if(not CheckDates(SellDate,DemandDate))
    return false;
  end; 
  if(NoYieldAfterEndOfTerm)
    /* Не считаем доход после конца срока обращения */
    C_DemandDate=MyMin(DemandDate,EndOfTerm);
/*    C_IncludeDemandDate = true;*/
  else
    C_DemandDate=DemandDate;
  end;                       

  GetCertParms;

  CertRate.Issue=C_Issue;
  CertRate.Date=C_SellDate;
  if(not (GetLE(CertRate) and (CertRate.Issue==C_Issue)))
    MsgBox("Ставка на "+String(C_SellDate)+" не задана"); 
    Exit;
  end;
 
  Yield=$0;
  DateSplit( SellDate, Day, Mon, Year );
  BegDate=SellDate;
  EndDate = PeriodEndDate( Year );
  while( BegDate != EndDate )
    if ( Presched )
      Yield=Yield+CalcPresched(BegDate,EndDate);
    else
      Yield=Yield+CalcForPeriod(BegDate,EndDate);
    end;
/*    C_Par=Par+Yield; */
    BegDate=EndDate;
    Year = Year + 1;
    EndDate=PeriodEndDate( Year );
  end;

  OutTotalYield;
  return true;
end;  

end;