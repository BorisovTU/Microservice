/*
  СБЕРБАНК 1.0
  R-Style SoftWare Lab.

  Печать Кассового ордера ф.53

  Лебедев С.В.
  16.12.97
*/

IMPORT deprintr, ci_ord_b, ci_ord_s, brparm, coin, InputRN, ChkPrn, rsd;

var {ReissuingReports};

file CIDoc  ("ci_doc.dbt") key 3 write;
file CIOper ("ci_oper.dbt")  key 0;
file CISubOp("ci_subop.dbt") key 0;
file CICode ("ci_code.dbt")  key 0;
file CashDoc("sb_casdc.dbt") key 7;
file CashLink("sb_casln.dbt") key 0;
file CashVal("sb_casvl.dbt") key 0;
file PayDoc( "pay_doc.dbt" ) key 2;
file GroupMem( "groupmem.dbt" ) key 0;

private const
  SB_CAPISSUE = 300;

private const
  CG__MATERIAL  = 2, 
  SB_NOTBALANCE = 1000;

private const
  CI_OGSZ          = 1,
  CI_LOTTERY       = 2,
  CI_CERT          = 3,
  CI_COIN          = 4,
  CI_INGOT         = 5,
  CI_ORVVZ         = 6,
  CI_MISC          = 10,

  O_OGSZ_SELL      = 1,
  O_OGSZ_BUY       = 2,
  O_OGSZ_PAYWARR   = 3,
  O_OGSZ_KEEP      = 4,
  O_OGSZ_RETURN    = 5,
  O_OGSZ_SELL_ACC  = 6,

  O_LOTTERY_SELL   = 1,
  O_LOTTERY_PAYWIN = 2,
  O_LOTTERY_BUY    = 3,
  O_LOTTERY_KEEP   = 4,
  O_LOTTERY_RETURN = 5,
  O_LOTTERY_SELL_ACC  = 6,

  O_CERT_SELL      = 1,
  O_CERT_PAY       = 2,
  O_CERT_PAYALN    = 3,
  O_CERT_KEEP      = 4,
  O_CERT_RETURN    = 5,
  O_CERT_SELL_ACC  = 6,

  O_MISC_SELL      = 1,
  O_MISC_BUY       = 2,
  O_MISC_KEEP      = 3,
  O_MISC_RETURN    = 4,
  O_MISC_SELL_ACC  = 6,

  O_INGOT_SELL     = 1,
  O_INGOT_BUY      = 2,
  O_INGOT_KEEP     = 3,
  O_INGOT_RETURN   = 4,
  O_INGOT_SELL_ACC = 5;

private const
  FLAGDEBCRED_IN   = 1,  /* Кредит */
  FLAGDEBCRED_OUT  = 2;  /* Дебет */

private const
  TYPERES_CURRENCY    = 1,   /* Деньги (из справочника валют)      */
  TYPERES_VALFORM     = 2,   /* Ценные бланки                      */
  TYPERES_VALUABLES   = 3,   /* Др. ценности (из справочника ценностей) */
  TYPERES_MINCAPISSUE = 500; /* Минимально возможный код ЦБ        */

private const
  RESMESUNIT_POINT = 1,  /* Поштучно   */
  RESMESUNIT_MONEY = 2;  /* Стоимость  */

private const 
  CASHOP_DEBET    = 5, 
  VALFORM_SPR31   = 31;

private const
  SB_CASHOPERT   =  200;   /* Документ для сервисно-кассовых операций */

private const        
  CK_NEW = 0,
  CK_OLD = 1;

private var
  Branch = NumFNCash,
  OperBrigade = GetOperBrigade,
  {curdate},
  IsCredit,
  TotalSum, 
  NDS = 0, 
  SalesTax = 0, 
  ValSum = 0, 
  F31Ser, 
  F31Num, 
  MaterialName, 
  Hallmark,  
  Ground, 
  CoinBunch, 
  Operation, 
  SubOp, 
  NumItems, 
  TotalPar, 
  BalCost, 
  CIMisc;

macro FindCICode( MaterialRef )

  CICode.AutoKey = MaterialRef;
  return GetEQ( CICode );
end;

macro FindCashVal( ValueRef )

  CashVal.RefValue = ValueRef;
  return GetEQ( CashVal );
end;

macro CIOperIsCreditM(Type, Number)

  if((Number == O_MISC_SELL) or (Number == O_MISC_RETURN) or (Number == O_MISC_SELL_ACC))
    return true;
  else
    return false;
  end;
end;

macro PrintList

  var 
    Hallmark,
    Delim = "-";

  if ( F31Ser == "" )
    Delim = "";
  end;

  FindCICode( CIMisc.Rec.HallmarkRef );
  Hallmark = CICode.Double;
    

  [                                                                         Приложение к квитанции ф.31         ];
  [                                                                         #                                   ]
  ( String(RegNumToStr(F31Ser,F31Num) + " от " + String( CoinBunch.Date:m ) ) );
  [                                             О П И С Ь                                                       ];
  [                              приобретаемых памятных монет по квитанции                                      ];    
  [ ];
  [-------------------------------------------------------------------------------------------------------------];
  [| №№  | Наименование монет     |  Металл, проба  | Номинал | Кол-во |Кач-во| Цена продажи с | Общая стом-ть |];
  [| п/п | и год выпуска          |                 |         | шт.    |чекан.| учетом НДС,руб | в руб         |];
  [|-----|------------------------|-----------------|---------|--------|------|----------------|---------------|];
  [     1 ######################## ################# ######### ######## ###### ################ ############### ]
  (
    ( CIMisc.Rec.Name + ", " + CIMisc.Rec.IssueYear ):w, 
    ( MaterialName + ", " + Hallmark ):w, 
    CIMisc.Rec.Par, 
    NumItems, 
    CIMisc.Rec.Quality, 
    TotalSum / NumItems, 
    TotalSum
  );
  [-------------------------------------------------------------------------------------------------------------];
  [ Общая стоимость монет                                                                       ############### ]
  ( TotalSum );
  [ ];
  [ ];
  [                                                                          Клиент _______________________     ];
  [                                                                                     (подпись)               ];
  [       * Примечание: с учетом налога с продажи                                                               ];
  [                                                                          Проверено ____________________     ];
  [                                                                                    (подпись контролера)     ];
  PrintLn( StrFor( 12 ) );       
end;

macro DoPrintForm31

  array
    Months,
    PurposeArr;

  var
    i,
    Purpose, 
    ClientName, 
    RubSum, CopSum, 
    Day, Month, Year;

  Months(1) = "января";
  Months(2) = "февряля";
  Months(3) = "марта";
  Months(4) = "апреля";
  Months(5) = "мая";
  Months(6) = "июня";
  Months(7) = "июля";
  Months(8) = "августа";
  Months(9) = "сентября";
  Months(10) = "октября";
  Months(11) = "ноября";
  Months(12) = "декабря";

  DateSplit( CoinBunch.Date, Day, Month, Year ); 

  RubSum = SubStr( String( TotalSum ), 1, Index( String( TotalSum ), "." ) - 1 );
  CopSum = SubStr( String( TotalSum ), Index( String( TotalSum ), "." ) + 1 );

  ClientName = CoinBunch.GetClientDesc;

  Purpose = CIOper.Name + " монет";
  if ( CoinBunch.HeadSubOp != 0 )
    Purpose = Purpose + " (" + CISubOp.Name + ")";
  end;
  if ( NumItems > 1 )  
    Purpose = Purpose + ",  " + NumItems + " шт. в соответствии с прилагаемой описью";
  else
    Purpose = Purpose + " " + CIMisc.Rec.Name + " (" + MaterialName + ") ";
  end;           

  if ( NDS or SalesTax )
    Purpose = Purpose + "\n В том числе: ";
  end;                                     
  if ( NDS )
    Purpose = Purpose + "НДС - " + NDS + " ";
  end;
  if ( SalesTax )
    Purpose = Purpose + "налог с продаж - " + SalesTax + " ";
  end;
  
  StrSplit( Purpose, PurposeArr, 50 );
  i = 0;
  while ( i < 3 )
    if ( ValType( PurposeArr( i ) ) != V_STRING )
      PurposeArr( i ) = ""; 
    end;
    i = i + 1;
  end;

  print("\x1b0"); /* Режим печати - 8 строк на дюйм */
  [

                    #   #              #

      #
    #
    #

    #


                #                     #

    #
  ]
  ( 
    Day:l,
    Months(Month):l,
    Year:l,
    ClientName:l, 
    PurposeArr(0):l,
    PurposeArr(1):l,
    PurposeArr(2):l,
    RubSum:l,
    CopSum:l,
    RubToStr( TotalSum ):l
  );
  printLn(""); /* End of page */
end;

macro PrintForm31

  var
    stat, 
    ExitFlag = false;

  record Parm( "CashParm.dbt" );

  Parm.Type             = 0;       /* Платежеспосбные ресурсы */
  Parm.TypeValue        = TYPERES_VALFORM;
  Parm.CodIntValue      = VALFORM_SPR31;
  Parm.Oper             = CoinBunch.Oper;
  Parm.CashDateDoc      = CoinBunch.Date;
  Parm.NumOper          = CASHOP_DEBET;
  Parm.ValueCol         = 1;
  Parm.NumbDocumCash    = CoinBunch.TokenID;
  Parm.ApplicationKind  = CoinBunch.HeadAppKind;
  Parm.ApplicationKey   = CoinBunch.HeadAppKey;
  Parm.Series           = "";
  Parm.MinRegistrNumber = 0;
  Parm.OperPatern       = CoinBunch.Oper;
  while (not ExitFlag)
    stat = InputRegNum(CoinBunch.Oper,
                       Parm.Oper,
                       TYPERES_VALFORM,
                       VALFORM_SPR31,
                       "Введите номер справки ф.31:",
                       Parm.Series,
                       Parm.MinRegistrNumber); 
    if (stat)
      stat = InitCashDoc(Parm);
    end;

    if (stat)
      ExitFlag = TRUE;
    else
      if ( GetTrue(true, "Ошибка при расходе. Повторить ?") )
        Parm.MinRegistrNumber = 0;
      else
        ExitFlag = true;
      end;
    end;
  end;
  if (stat)
    F31Ser = Parm.Series;
    F31Num = Parm.MinRegistrNumber;
    DoPrintForm31();
  else
    MsgBox("Ошибка при расходе справки ф.31");
    Exit( 1 );
  end;
end;

macro CheckPrn

  var
    RegPath = "RS-RETAIL\\ПЕЧАТИ\\ЦЕННЫЕ_БУМАГИ_И_ЦЕННОСТИ\\ОРДЕРА";

  CheckPrinter(RegPath, StrFor( 0 ) );
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro IsFirstCIOperat( )

  var cmd = RsdCommand( "select 0 from dsb_casln_dbt c1, dsb_casln_dbt c2 "
                        "where c1.t_applicationKind1 = c2.t_applicationKind1 "
                        "  and c1.t_applicationKey1 = c2.t_applicationKey1 "
                        "  and c1.t_numLinkDoc < c2.t_numLinkDoc "
                        "  and c1.t_applicationKind2 = c2.t_applicationKind2 "
                        "  and c1.t_refValue2 = 0 "
                        "  and c2.t_refValue2 = 0 "
                        "  and c2.t_applicationKind2 = ? "
                        "  and c2.t_applicationKey2 = ? " );

  cmd.addParam( "p_appKind", RsdBp_In );
  cmd.value( "p_appKind" ) = CIDoc.ApplicationKind;
  cmd.addParam( "p_appKey", RsdBp_In );
  cmd.value( "p_appKey" ) = CIDoc.ApplicationKey;

  cmd.execute;
  var rs = RsdRecordset( cmd );

  return not rs.moveNext;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro PrintOrd( DocAddr )
               
  record RCIDoc( "ci_doc.dbt" );

  SetBuff(RCIDoc, DocAddr);
  Copy(CIDoc, RCIDoc);
  GetEQ(CIDoc); 

  if ( not IsFirstCIOperat( ) )
    Exit( 1 );
  end;

  CoinBunch = TCoinDocBunch( CIDoc.ApplicationKind, CIDoc.ApplicationKey );

  if ( not CoinBunch.IsSingleCoinTypeOp )
    RunError( "Режим работы с несколькими видами монет на операцию не реализован" );
  end; 

  CIMisc = CoinBunch.GetCoinType;

  if ( not CIMisc )
    RunError( "Вероятно, касса выключена либо не настроен ресурс по операции. Ордера не будут выпущены" );
  end;

  if ( not FindCICode( CIMisc.Rec.MaterialRef ) )
    MsgBox( "Не найден металл" );
    Exit( 1 );
  end;

  MaterialName = CICode.String;

  Operation = CoinBunch.HeadOperation;
  SubOp     = CoinBunch.HeadSubOp;
  NumItems  = CoinBunch.GetNumItems;
  TotalPar  = CoinBunch.GetTotalPar;
  BalCost   = CoinBunch.GetBalCost;
  NDS       = CoinBunch.GetOutgoingNDS; 
  SalesTax  = CoinBunch.GetSalesTax;

  IsCredit = CIOperIsCreditM(CI_COIN, Operation);
  CIOper.Type = CI_COIN;
  CIOper.Number = Operation;
  if ( not GetEQ(CIOper))
    CIOper.Name = "???";
  end;
  if (SubOp > 0)
    CISubOp.Type = CI_COIN;
    CISubOp.Operation = Operation;
    CISubOp.SubOpNum = SubOp;
    if ( not GetEQ(CISubOp))
      CISubOp.Name = "???";
    end;
  end;

  TotalSum = CoinBunch.GetCiOpSum + NDS + SalesTax;

  CheckPrn;

  if ( (Operation == O_MISC_SELL) AND ({ReissuingReports}==false) )
    PrintForm31; 
    if ( NumItems > 1 )
      PrintList();
    end;
  else
    EXIT( 1 );
  end;

  PrintOutput;
end;

end;
