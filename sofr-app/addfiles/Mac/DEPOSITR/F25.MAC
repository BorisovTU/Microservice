
/*
   Макрос выпуска ведомости пересчета остатков.
   Не путать с настоящей формой 25!
*/

import CommonInter, BankInter, deprintr, ChkPrn, ExchangeInter,
       DeskInter,   "cur_rate.mac", rsd;

file F68Tmp     ("f68.tmp")      key 1 write;
file DepType    (sb_dtyp)  key 2;
file Curr       (currency) key 0;
file DepStat    (sb_depst) key 0;
var ddep = TRecHandler( "i_dp_dep.rec" );
file cv         (sb_casvl) key 1;  /* Справочник ресурсов кассы */

record CurStat ("sb_depst.dbt");
record Tot     ("f68.tmp");
record TotTot  ("f68.tmp");

var
  {curdate},
  {oper};

const
  NonRez = "(нерезид.)",
  IsCur = NumFlagCur,
  Branch = NumFNCash,
  Mode = GetCurrentMode,
  TmpFileName = "f68" + String({oper}) + ".dbt";

var
  CurRate,
  PrevRate,
  Citizenship,
  DepTypeDisplayed,
  SmthDisplayed;

macro GetExtCode(Code_Currency)
  file Curr(currency) key 0;
  Var ExternCode = 0;
  Curr.Code_Currency = Code_Currency;
  if(GetEQ(Curr))
    ExternCode = Curr.ExternalCode;
  end;
  return ExternCode;
end;

macro ClearTmpFile
  var cmd;

  cmd = RsdCommand( NULL, string("truncate table df68_tmp") );
  cmd.execute();
end;

macro GetDepStat

  ClearRecord(DepStat);
  ClearRecord(CurStat);
  DepStat.FNCash = Branch;
  DepStat.IsCur = 1;
  DepStat.Kind = DepType.Kind;
  DepStat.CodCur = Curr.Code_Currency;
  DepStat.FlagRez = Citizenship;
  DepStat.Date = {curdate};
  DepStat.Mode = Mode;
  if(GetLT(DepStat)
    and (DepStat.FNCash == Branch)
    and (DepStat.IsCur == 1)
    and (DepStat.Kind == DepType.Kind)
    and (DepStat.CodCur == Curr.Code_Currency)
    and (DepStat.FlagRez == Citizenship))
    if( ( ( Mode == 0 ) and ( DepStat.Date == {curdate}-1) )
      or ( DepStat.Date == {curdate} ) )
      Copy(CurStat, DepStat);
     /*
      if(Prev(DepStat)
        and (DepStat.FNCash == Branch)
        and (DepStat.IsCur == 1)
        and (DepStat.Kind == DepType.Kind)
        and (DepStat.CodCur == Curr.Code_Currency)
        and (DepStat.FlagRez == Citizenship))
        Copy(PrevStat, DepStat);
      end;
     */
    else
      /*Copy(PrevStat, DepStat);*/
      Copy(CurStat, DepStat);
      CurStat.DebSum = $0;
      CurStat.KredSum = $0;
      CurStat.MemDebSum = $0;
      CurStat.MemKredSum = $0;
      CurStat.CurOpenAc = 0;
      CurStat.CurClosAc = 0;
    end;
  end;
end;

/*         RA 07-03-01 нужно пользоваться справочником Retail
macro FindExchCurr

  ExchCurr.ID = Int(Curr.ExternalCode);
  return GetEQ(ExchCurr);
end;
*/

macro TreatDepType


  GetDepStat;
  ClearRecord(F68Tmp);
  F68Tmp.AccTypeName = DepType.Name;
  F68Tmp.CurrCode = Curr.Code_Currency;
  F68Tmp.CurrISO = Curr.Short_Name;
  F68Tmp.Citizenship = Citizenship;
  F68Tmp.CurrName = Curr.Name_Currency;
  F68Tmp.CurrPrevRate = PrevRate;
  F68Tmp.CurrCurRate = CurRate;
  F68Tmp.InRest = CurStat.Rest;
  F68Tmp.InRestR = Money(CurStat.Rest * PrevRate);
  F68Tmp.OutRest = CurStat.Rest;
  F68Tmp.OutRestR = Money(CurStat.Rest * CurRate);
  return Insert(F68Tmp);
end;

macro TreatCurrency

  var
    RecFound;

  /*FindExchCurr;*/
  copy( RtlCurr, Curr );
  PrevRate = GetCurrRate({curdate} - 1);
  CurRate = GetCurrRate({curdate});
  ClearRecord(DepType);
  DepType.FlagCur = 1;
  RecFound = GetGE(DepType);
  while(RecFound and (DepType.FlagCur == 1))
    if((DepType.Kind != "Служебный")
      and (DepType.Kind != "Неподвижный")
      and (DepType.Kind != "Объединенный"))
      TreatDepType;
    end;
    RecFound = Next(DepType);
  end;
end;

macro TreatAllCurrs

  var
    RecFound;

  Curr.Code_Currency = 1;
  RecFound = GetGE(Curr);
  while(RecFound)
    if( Curr.CrDifrRate ) /* валюта используется */
      TreatCurrency;
    end;
    RecFound = Next(Curr);
  end;
end;

macro FillTmpFile

  Citizenship = StrFor(0);
  TreatAllCurrs;
  Citizenship = StrFor(1);
  TreatAllCurrs;
end;

macro FindBranch

  return findDepartment(Branch, null, ddep);
end;
/*
macro OutHead1

  [ ];
  [Раздел 1. Обороты и остатки наличной валюты ];
  [ ];
  [----------------------------------------------------------------------------------------------------------------------];
  [|Код  |       Наименование      |          Остаток на начало дня    |  Пересчет  |        Остаток на начало дня      |];
  [|ва-  |          валюты         |-----------------------------------|            |-----------------------------------|];
  [|люты |                         |     в валюте    |     в рублях    |    руб.    |     в валюте    |     в рублях    |];
  [----------------------------------------------------------------------------------------------------------------------];
  [ ];
  [ Итого в рублях :];
  [ ];
  [ ];
end;
*/

macro OutHead2

  [ ];
  [ ];
  [Остатки по валютным счетам ];
  [---------------------------------------------------------------------------------------------------------------------------------];
  [|Код  |     Н а и м е н о в а н и е        |          Остаток на начало дня    |  Пересчет  |         Остаток на начало дня     |];
  [|ва-  |           с ч е т о в,             |-----------------------------------|            |-----------------------------------|];
  [|люты |       в и д  в а л ю т ы           |     в валюте    |     в рублях    |    руб.    |     в валюте    |     в рублях    |];
  [---------------------------------------------------------------------------------------------------------------------------------];
end;


/*
macro OutHead3

  [ ];
  [ ];
  [----------------------------------------------------------------------------------------------------------------------];
  [Раздел 3. Наличие платежных средств в валюте, купленных(проданных) у населения ];
  [----------------------------------------------------------------------------------------------------------------------];
  [|Код  |       Наименование      |          Остаток на начало дня    |  Пересчет  |          Остаток на начало дня    |];
  [|ва-  |          валюты         |-----------------------------------|            |-----------------------------------|];
  [|люты |                         |     в валюте    |     в рублях    |    руб.    |     в валюте   |     в рублях     |];
  [----------------------------------------------------------------------------------------------------------------------];
  [ ];
  [ Итого в рублях :];
  [ ];
  [ ];
end;
*/

macro OutDepType

  [ ];
  [  ######################################################### ]
  (F68Tmp.AccTypeName);
  [ ];
end;

macro OutLine

  var
    CurrName;

  if((F68Tmp.InRest != 0)
    and (F68Tmp.OutRest != 0))
    if(not DepTypeDisplayed)
      OutDepType;
      DepTypeDisplayed = true;
    end;
    if(F68Tmp.Citizenship)
      CurrName = F68Tmp.CurrName + NonRez;
    else
      CurrName = F68Tmp.CurrName;
    end;
  [ ###              ### ##################### ################# ################# ############ ################# ################# ]
    (
      GetExtCode(F68Tmp.CurrCode),
      F68Tmp.CurrISO,
      CurrName,
      F68Tmp.InRest:z,
      F68Tmp.InRestR:z,
      (F68Tmp.OutRestR-F68Tmp.InRestR):z,
      F68Tmp.OutRest:z,
      F68Tmp.OutRestR:z
    );
    Tot.InRestR = Tot.InRestR + F68Tmp.InRestR;
    Tot.OutRestR = Tot.OutRestR + F68Tmp.OutRestR;
    SmthDisplayed = true;
  end;
end;

macro OutTotals(str)

  [ ];
  [  ######### :                                                 ################# ############                   ################# ]
  (
    str,
    Tot.InRestR,
    (Tot.OutRestR - Tot.InRestR),
    Tot.OutRestR
  );
  if(str != "В с е г о ")
    TotTot.InRestR = TotTot.InRestR + Tot.InRestR;
    TotTot.OutRestR = TotTot.OutRestR + Tot.OutRestR;
  end;
end;

macro OutTotTotals

  Copy(Tot, TotTot);
  [ ];
  OutTotals("В с е г о ");
end;

macro OutLines

  var
    PrevAccTypeName = "";

  Rewind(F68Tmp);
  SmthDisplayed = false;
  while(Next(F68Tmp))
    if((F68Tmp.AccTypeName != PrevAccTypeName))
      if(SmthDisplayed)
        OutTotals("И т о г о ");
        SmthDisplayed = false;
      end;
      ClearRecord(Tot);
      PrevAccTypeName = F68Tmp.AccTypeName;
      DepTypeDisplayed = false;
    end;
    OutLine;
  end;
end;


  if(not IsCur)
    MsgBox("Форма выпускается только в валютном режиме");
    Exit;
  end;

  if ( not UpdateStatistics( true ) )
    Exit;
  end;

  Message("Заполнение промежуточного файла ...");
  ClearTmpFile;
  FillTmpFile;
  Message;
  FindBranch;
  [Филиал #                                                              ]
  (ddep.rec.Name);
  [ ];
  [                          Пересчет остатков при изменении курса валют ];
  [ ];
  [############]
  (String({curdate}));
  [ ];
  /*OutHead1;*/
  OutHead2;
  OutLines;
  OutTotTotals;
  /*OutHead3;*/
end;
