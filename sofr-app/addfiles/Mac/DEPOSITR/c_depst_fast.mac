/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Заполнение файла статистики                                             *
*                                                                          *
*  Лебедев С.В.                                                            *
*  17.03.1997                                                              *
\**************************************************************************/

import DeprIntr, IsSrvDoc, sqlconv, OperCode, rsd;

var currency  = TBFile( "currency.dbt", "r", 0 ); // Справочник валют
var sb_dtyp   = TBFile( "sb_dtyp.dbt",  "r", 2 ); // Справочник видов вкладов
var sb_depst  = TBFile( "sb_depst.dbt", "w", 0 ); // Статистика по вкладам

var BegDate = {curdate};
var EndDate = {curdate};
var CurDate;

const FNCash  = NumFNCash( );
const FlagCur = NumFlagCur( );

var NeedFormRest1 = false;

var BeginLastQuart  = Date(0,0,0);

var Mode;

var SumColAccounts  = 0;
var SumColOpenedAcc = 0;
var SumColClosedAcc = 0;
var SumRest         = $0L;
var SumKredSum      = $0L;
var SumDebSum       = $0L;
var SumColOper      = 0;

/**************************************************************************\
|                         Начало-Окончание отчета                          |
\**************************************************************************/
macro BeginEndReport

  [ ----- #################### ---------------- ########## ----- ######## -------]
  ( GetRetailVersion( ), Date( ), Time( ) );

end;


/**************************************************************************\
|                                 Поиск валюты                             |
\**************************************************************************/
macro FindCurrency ( CodeCur )

  currency.Clear;
  currency.rec.Code_Currency = CodeCur;

  return currency.GetEQ;

end;


/**************************************************************************\
|                       Определение названия филиала                       |
\**************************************************************************/
macro DefNameBranch

  var NameBranch = "";

  if ( not findDepartment(FNCash, NameBranch) )
    NameBranch = "";
  end;

  return NameBranch;

end;


/**************************************************************************\
|                         Чистка файла статистики                          |
\**************************************************************************/
macro DelStat ( Date1, Date2 )

  var cmd = RsdCommand( "delete from dsb_depst_dbt "
                        " where t_fncash = ? "
                        "   and t_date between ? and ?" );

  Message( " Чистка файла статистики с ", Date1, " по ", Date2, " ..." );

  cmd.addParam( "p_branch", RSDBP_IN, FNCash );
  cmd.addParam( "p_date1", RSDBP_IN, Date1 );
  cmd.addParam( "p_date2", RSDBP_IN, Date2 );

  cmd.execute;
end;


/**************************************************************************\
|                            Печать шапки отчета                           |
\**************************************************************************/
macro TopReport ( CodeCurrency, NeedFindCur )

  var stat = true;

  [ ];
  [              Статистика по вкладам по филиалу ]( DefNameBranch( ) + " за " + String( EndDate ) );
  [ ];
  if ( NeedFindCur )
    stat = FindCurrency( CodeCurrency );
  end;
  if ( stat )
    [  Валюта: #]( currency.rec.Short_Name + " - " + currency.rec.Name_Currency );
    [ ];
  end;
  [ ╓────────────╥────────────┬────────────┬─────────────╥─────────────────╥─────╖];
  [ ║            ║ Кредитовые │ Дебетовые  │ Остаток на  ║Количество счетов║Кол. ║];
  [ ║ Вид вклада ║ обороты    │ обороты    │ конец дня   ╟─────┬─────┬─────╢опер.║];
  [ ║            ║ дня        │ дня        │             ║всего│откр.│закр.║     ║];
  [ ╟────────────╫────────────┼────────────┼─────────────╫─────┼─────┼─────╫─────╢];

end;


/**************************************************************************\
|                          Печать окончания отчета                         |
\**************************************************************************/
macro EndReport

  [ ╙────────────╨────────────┴────────────┴─────────────╨─────┴─────┴─────╨─────╜];
  [ ];
  [  Итого        ############ ############ ############# ##### ##### ##### #####]
  ( SumKredSum:12:2:a, SumDebSum:12:2:a, SumRest:13:2:a, SumColAccounts:5,
    SumColOpenedAcc:5, SumColClosedAcc:5, SumColOper:5 );
  [ ];

end;


/**************************************************************************\
|                           Печать строки отчета                           |
\**************************************************************************/
macro WriteString

  var RepKredSum = $0L;
  var RepDebSum  = $0L;
  var ColOper    = 0;

  if ( sb_depst.rec.Date == EndDate )
    RepKredSum = sb_depst.rec.KredSum;
    RepDebSum  = sb_depst.rec.DebSum;
  end;

  if ( sb_depst.rec.Date > BeginLastQuart )
    ColOper = sb_depst.rec.ColOper;
  end;

  [ ║############║############│############│#############║#####│#####│#####║#####║]
  ( sb_depst.rec.Kind, RepKredSum:12:2:a, RepDebSum:12:2:a,
    sb_depst.rec.Rest:13:2:a, sb_depst.rec.ColAccounts:5,
    sb_depst.rec.ColOpenedAcc:5, sb_depst.rec.ColClosedAcc:5, ColOper:5 );
  if ( sb_depst.rec.FlagRez != "" )
    [ ║(нерез.)    ║            │            │             ║     │     │     ║     ║];
  end;

  /* Подсчет сумм */
  SumColAccounts  = SumColAccounts  + sb_depst.rec.ColAccounts;
  SumColOpenedAcc = SumColOpenedAcc + sb_depst.rec.ColOpenedAcc;
  SumColClosedAcc = SumColClosedAcc + sb_depst.rec.ColClosedAcc;
  SumRest         = SumRest         + sb_depst.rec.Rest;
  SumKredSum      = SumKredSum      + RepKredSum;
  SumDebSum       = SumDebSum       + RepDebSum;
  SumColOper      = SumColOper      + ColOper;

end;


/**************************************************************************\
|     Цикл по файлу статистики вкладов за последний день - для отчета      |
\**************************************************************************/
macro WriteReportForLastDay ( CodeCurrency )

  var stat;
  var i;
  array AFlagRez;
    AFlagRez( 0 ) = "\x00";
    AFlagRez( 1 ) = "\x01";

  sb_dtyp.Clear;
  sb_dtyp.rec.FlagCur = FlagCur;
  sb_dtyp.rec.Kind    = "";
  stat = sb_dtyp.GetGE;
  while ( stat )
    if ( sb_dtyp.rec.FlagCur == FlagCur )
      i = 0;
      while ( i < ASize( AFlagRez ) )
        sb_depst.KeyNum = 0;
        sb_depst.Clear;
        sb_depst.rec.FNCash  = FNCash;
        sb_depst.rec.IsCur   = FlagCur;
        sb_depst.rec.Kind    = sb_dtyp.rec.Kind;
        sb_depst.rec.CodCur  = CodeCurrency;
        sb_depst.rec.FlagRez = AFlagRez( i );
        sb_depst.rec.Date    = EndDate;
        sb_depst.rec.Mode    = 1;
        sb_depst.addFilter( "t.t_FNCash = " + string( FNCash ) + " and " +
                            "t.t_IsCur = " + string( FlagCur ) + " and " +
                            "t.t_Kind = " + GetSQLString( sb_dtyp.rec.Kind ) + " and " +
                            "t.t_CodCur = " + string( CodeCurrency ) + " and " +
                            "t.t_FlagRez = " + sqlChar( AFlagRez( i ) ) + " and " +
                            "t.t_Date between " + sqlDateToStr( Date( 0, 0, 0 ) ) + " and " +
                                                  sqlDateToStr( EndDate )                    );
        if ( ( sb_depst.GetLE                           ) and
             ( sb_depst.rec.FNCash  == FNCash           ) and
             ( sb_depst.rec.IsCur   == FlagCur          ) and
             ( sb_depst.rec.Kind    == sb_dtyp.rec.Kind ) and
             ( sb_depst.rec.CodCur  == CodeCurrency     ) and
             ( sb_depst.rec.FlagRez == AFlagRez( i )    )    )
          WriteString( );
        end;
        sb_depst.dropFilter;
        i = i + 1;
      end;
      stat = sb_dtyp.Next;
    else
      stat = false;
    end;
  end;

end;

/**************************************************************************\
|         Определение данных по вкладам за текущую дату и режим            |
\**************************************************************************/
macro CreateStat

  var cmd = RsdCommand( "begin RSU_RTLDEPOSIT.UpdateStatistics( ?, ?, ?, ?, ? ); end;" );

  cmd.addParam( "p_branch", RSDBP_IN, FNCash );
  cmd.addParam( "p_isCur", RSDBP_IN, FlagCur );
  cmd.addParam( "p_date", RSDBP_IN, CurDate );
  cmd.addParam( "p_mode", RSDBP_IN,Mode );
  cmd.addParam( "p_depType", RSDBP_IN, "" );

  cmd.execute;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro CreateInitialStat

  var cmd = RsdCommand( "begin RSU_RTLDEPOSIT.CreateInitialStatistics( ?, ?, ?, ?, ? ); end;" );

  cmd.addParam( "p_branch", RSDBP_IN, FNCash );
  cmd.addParam( "p_isCur", RSDBP_IN, FlagCur );
  cmd.addParam( "p_date", RSDBP_IN, CurDate );
  cmd.addParam( "p_mode", RSDBP_IN, Mode );
  cmd.addParam( "p_depType", RSDBP_IN, "" );

  cmd.execute;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DepDocsForModeExist

  var cmd = RsdCommand( "select 0 from dsbdepdoc_dbt where t_iscur = ? and t_fncash = ? and t_date_document = ? and t_mode = ?" );
  var rs;

  cmd.addParam( "p_isCur", RSDBP_IN, FlagCur );
  cmd.addParam( "p_branch", RSDBP_IN, FNCash );
  cmd.addParam( "p_date", RSDBP_IN, CurDate );
  cmd.addParam( "p_mode", RSDBP_IN, Mode );

  cmd.execute;

  rs = RsdRecordset( cmd );
  if ( rs.moveNext )
    return true;
  else
    return false;
  end;
end;

/**************************************************************************\
|                      Формирование файла статистики                       |
\**************************************************************************/
macro DoReport

  var d,m,y;
  var flag;

  DateSplit( EndDate, d, m, y );
  if   ( ( m >= 1 ) and ( m <= 3 ) )
    m = 1;
  elif ( ( m >= 4 ) and ( m <= 6 ) )
    m = 4;
  elif ( ( m >= 7 ) and ( m <= 9 ) )
    m = 7;
  else
    m = 10;
  end;
  BeginLastQuart = Date( 1, m, y );

  CurDate = BegDate;
  while ( CurDate <= EndDate )
    Message( " Формирование статистики за " + String( CurDate ) );
    Mode = 0;
    while ( Mode <= 1 )
      if ( (CurDate == BegDate ) and ( Mode == 0 ) )
        if ( not NeedFormRest1 )
          DelStat( BegDate, BegDate );
        end;
        Message( " Формирование статистики за " + String( CurDate ) );
        CreateInitialStat();
      else
        if ( DepDocsForModeExist )
          CreateStat( );
        end;
      end;
      Mode = Mode + 1;
    end;
    CurDate = CurDate + 1;
  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  var WasError = false;
  var stat;

  // Формирование статистики
  if ( GetTrue( NULL, "Заполнять файл статистики?" ) )
    if ( GetDate( BegDate, "Введите начальную дату" ) )
      if ( GetDate( EndDate, "Введите конечную дату" ) )
        if ( BegDate <= EndDate )
          NeedFormRest1 = GetTrue( NeedFormRest1, "Формировать остатки за первый день?" );
          if ( NeedFormRest1 )
            DelStat( BegDate, EndDate );
          else
            DelStat( BegDate + 1, EndDate );
          end;
          DoReport( );
        else
          WasError = true;
          [ ];
          [ Начальная дата не может быть больше конечной];
        end;
      end;
    end;
  end;

  Mode = GetCurrentMode( );

  // Вывод отчета
  if ( not WasError )

    Message( " Выводится отчет..." );

    BeginEndReport( );

    if ( FlagCur )
      currency.Clear;
      currency.rec.Code_Currency = 1;
      stat = currency.GetGE;
      while ( stat )
        SumColAccounts  = 0;
        SumColOpenedAcc = 0;
        SumColClosedAcc = 0;
        SumRest         = $0;
        SumKredSum      = $0;
        SumDebSum       = $0;
        SumColOper      = 0;
        TopReport( currency.rec.Code_Currency, false );
        WriteReportForLastDay( currency.rec.Code_Currency );
        EndReport( );
        [ ];
        [ ];
        stat = currency.Next;
      end;
    else // Отчет по национальной валюте
      TopReport( 0, true );
      WriteReportForLastDay( 0 );
      EndReport( );
    end;

    BeginEndReport( );

  end;

end;
