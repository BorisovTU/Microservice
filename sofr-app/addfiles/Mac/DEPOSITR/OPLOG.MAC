/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Отчет изменений по вкладным файлам в Журнале корректировок              *
*                                                                          *
*  Шилов А.                                                                *
*  Лебедев С.В.                                                            *
\**************************************************************************/

import DeprIntr, rsd;

file F_SC_Log     (sc_log,"spcard.def") key 1;
var F_DepClient   = TClientList;
file F_Depositor  (depositr           ) key 8;
file typeac       (typeac, "bank.def" ) key 0;
file namealg      (namealg,"bank.def" ) key 0;  /* Названия алгоритмов */

record R_Depositor     (depositr);
record R_Old_Depositor (depositr);
record R_TrastWill     (sbtrast );
record R_Old_TrastWill (sbtrast );
record R_AccGet        (sb_congt);
record R_Old_AccGet    (sb_congt);
record R_PlanPay       (dplanpay);
record R_Old_PlanPay   (dplanpay);
record R_DepClient     (depclnt );
record R_Old_DepClient (depclnt );

const Act_Insert = 2;
const Act_Update = 3;
const Act_Delete = 4;

const FN_Depositor = 501;
const FN_TrastWill = 502;
const FN_AccGet    = 503;
const FN_PlanPay   = 504;
const FN_DepClient = 505;

const SecondRecOfs = 4096;

const Get_Type    = 1;
const Get_Account = 2;

const MaxLineLen = 60;

const  TRAST_DOC       = StrFor( 0 ),   /* Доверенность        */
       WILL_DOC        = StrFor( 1 ),   /* Завещание - выдачи  */
       WILL_PAY        = StrFor( 2 ),   /* Завещание - выплаты */
       UNDER_TRIAL_DOC = StrFor( 3 ),   /* Судебные взыскания  */
       UNDER_TRIAL_PAY = StrFor( 4 );   /* Выплаты взысканий   */

const  ACCOUTN_OWNER       = StrFor( 0 ),    /* Владелец счета      */
       ACCOUTN_PROXY       = StrFor( 1 ),    /* Доверенное лицо     */
       ACCOUTN_HEIR        = StrFor( 2 ),    /* Наследник           */
       ANONIMUS_CLIENT     = StrFor( 3 ),    /* Анонимный клиент    */
       ACCOUNT_IMPORTER    = StrFor( 4 ),    /* Вноситель           */
       ACCOUNT_TUTOR       = StrFor( 5 ),    /* Опекун              */
       ACCOUNT_OBLIGATOR   = StrFor( 6 ),    /* Под обязательство   */
       ACCOUTN_HEIR_PAY    = StrFor( 7 ),    /* Наследник - выплаты */
       ACCOUNT_UNDER_TRIAL = StrFor( 8 ),    /* По суду             */
       ACCOUNT_PARENT      = StrFor( 9 );    /* Родитель            */

var Branch = NumFNCash();
var {curdate};
var Cdate = {curdate};

array ActionNames;
  ActionNames(0) = "";
  ActionNames(2) = "Ввод";
  ActionNames(3) = "Изменение";
  ActionNames(4) = "Удаление";


/*************************************************************************\
|             Подкачка названий из файла названий алгоритмов              |
\*************************************************************************/
macro FindName( TypeAlg, NumberAlg )

  var str;

  KeyNum( namealg, 0 );
  ReWind( namealg );
  namealg.iTypeAlg   = TypeAlg;
  namealg.iNumberAlg = NumberAlg;
  if ( GetEQ( namealg ) )
    str = Trim( namealg.szNameAlg );
  else
    str = "";
  end;

  return str;

end;


/**************************************************************************\
|                           Получить номер счета                           |
\**************************************************************************/
macro GetAccount (Reference)

  var str = "";

  F_Depositor.Referenc = Reference;

  if (NOT GetEQ(F_Depositor))
    str = "[Не найден]";
  else
    str = F_Depositor.Account;
  end;

  return str;

end;


/**************************************************************************\
|              Формирование ФИО клиента на основании его кода              |
\**************************************************************************/
macro GetClientName (CodClient)

  var str = "";

  if (NOT F_DepClient.GetRecord(CodClient))
    str = "[Не найден]";
  else
    if (StrLen(F_DepClient.CurRec.rec.Name1) > 0)
      str = F_DepClient.CurRec.rec.Name1 + " ";
      if (F_DepClient.CurRec.rec.Name2 != "")
        str = str + SubStr(F_DepClient.CurRec.rec.Name2,1,1) + ".";
        if (StrLen(F_DepClient.CurRec.rec.Name3) > 0)
          str = str + SubStr(F_DepClient.CurRec.rec.Name3,1,1) + ".";
        end;
      end;
    else
      str = "[Фамилия не задана]";
    end;
  end;

  str = Trim(str);

  return str;

end;


/**************************************************************************\
|                   Определение типа счета по его символу                  |
\**************************************************************************/
macro NameTypeAccount (SymbolAccount)

  var OutString = "";

  typeac.iNumType     = 11;
  typeac.Type_Account = SymbolAccount;
  if (GetEQ(typeac))
    OutString = typeac.Name_Type;
  else
    typeac.iNumType     = 50;
    typeac.Type_Account = SymbolAccount;
    if (GetEQ(typeac))
      OutString = typeac.Name_Type;
    end;
  end;

  return OutString;

end;


private macro IsBookGiven(branch, number)
  var given = false;
  var cmd = RsdCommand("select dc.t_BookGiven "
                        " from drtdepcontract_dbt dc, drt_contract_dbt c "
                        " where dc.t_Branch = c.t_Branch "
                        "   and dc.t_Id = c.t_id "
                        "   and c.t_Type = 3 "
                        "   and c.t_Branch = ? "
                        "  and c.t_Number = ? ");

  cmd.addParam("Branch", RSDBP_IN, branch);
  cmd.addParam("Number", RSDBP_IN, number);
  cmd.execute();
  var rs = RsdRecordset(cmd);

  if (rs.moveNext() and rs.value(0))
    given = true;
  end;

  return given;
end;


/**************************************************************************\
|                               Шапка отчета                               |
\**************************************************************************/
macro OutHeader

  [ ];
  [                                    Хронологический отчет за ##########](Cdate);
  [ ];
  [-------------------------------------------------------------------------------------------------------------------------];
  [   Время  |           Счет           |  Действие  | Опер |                          Комментарий                         |];
  [-------------------------------------------------------------------------------------------------------------------------];

end;


/**************************************************************************\
|                               Шапка отчета                               |
\**************************************************************************/
macro OutBottom

  [-------------------------------------------------------------------------------------------------------------------------];

end;


/**************************************************************************\
|                              Строка отчета                               |
\**************************************************************************/
macro OutReportLine (Account, CommentInfo)

  [ ######## |#                         | #          | #### | #############################################################|]
  (
    String(F_SC_Log.TimeRec),
    Acc_Form(Account):l:f,
    ActionNames(F_SC_Log.NumOperation):l,
    F_SC_Log.Oper,CommentInfo:l:w
  );

end;


/**************************************************************************\
|                  Разборка записи файла счетов вкладчика                  |
\**************************************************************************/
macro MCI_Depositor

  var str = "";
  var i;

  SetRecordAddr(R_Depositor,F_SC_Log);

  str = "Открыт счет " + R_Depositor.Type_Account + "\nРасчет процентов по ";
  if (R_Depositor.UseAlternate)
    str = str + "альтернативным ";
  else
    str = str + "основным ";
  end;
  str = str + "ставкам";
  if (R_Depositor.UserTypeAccount != "")
    str = str + "\nТип счета:";
    i = 1;
    while (i <= StrLen(R_Depositor.UserTypeAccount))
      str = str + " '" + SubStr(R_Depositor.UserTypeAccount,i,1) + "'-" + NameTypeAccount(SubStr(R_Depositor.UserTypeAccount,i,1));
      i = i + 1;
    end;
  end;
  if (IsBookGiven(R_Depositor.FNCash, R_Depositor.SvodAccount)) // if (ALG_CONTRACT.BookGiven)
    str = str + "\nПо счету выдана сберкнижка";
  end;

  return str;

end;

/**************************************************************************/
macro MCU_Depositor

  var str = "";
  var i;

  SetRecordAddr(R_Old_Depositor, F_SC_Log                 );
  SetRecordAddr(R_Depositor,     F_SC_Log, 0, SecondRecOfs);

  if (R_Depositor.UserTypeAccount != R_Old_Depositor.UserTypeAccount)
    i = 1;
    while (i <= StrLen(R_Depositor.UserTypeAccount))
      if (Index(R_Old_Depositor.UserTypeAccount,SubStr(R_Depositor.UserTypeAccount,i,1)) == 0)
        if (str != "")
          str = str + "\n";
        end;
        str = str + "Добавлен тип счета '" + SubStr(R_Depositor.UserTypeAccount,i,1) + "'-" + NameTypeAccount(SubStr(R_Depositor.UserTypeAccount,i,1));
      end;
      i = i + 1;
    end;
    i = 1;
    while (i <= StrLen(R_Old_Depositor.UserTypeAccount))
      if (Index(R_Depositor.UserTypeAccount,SubStr(R_Old_Depositor.UserTypeAccount,i,1)) == 0)
        if (str != "")
          str = str + "\n";
        end;
        str = str + "Убран тип счета '" + SubStr(R_Old_Depositor.UserTypeAccount,i,1) + "'-" + NameTypeAccount(SubStr(R_Old_Depositor.UserTypeAccount,i,1));
      end;
      i = i + 1;
    end;
  end;
  if (R_Depositor.Open_Date != R_Old_Depositor.Open_Date)
    if (str != "")
      str = str + "\n";
    end;
    str = str + "Дата открытия счета изменена с " + String(R_Old_Depositor.Open_Date) +
                                           " на " + String(R_Depositor.Open_Date);
  end;
  if (R_Depositor.Close_Date != R_Old_Depositor.Close_Date)
    if (str != "")
      str = str + "\n";
    end;
    str = str + "Дата закрытия счета изменена с " + String(R_Old_Depositor.Close_Date) +
                                           " на " + String(R_Depositor.Close_Date);
  end;
  if (R_Depositor.CodClient != R_Old_Depositor.CodClient)
    if (str != "")
      str = str + "\n";
    end;
    str = str + "Заменен владелец счета с " + GetClientName(R_Old_Depositor.CodClient) + " на " + GetClientName(R_Depositor.CodClient);
  end;
  if ((NOT R_Old_Depositor.FlagTrast) AND R_Depositor.FlagTrast)
    if (str != "")
      str = str + "\n";
    end;
    str = str + "Выдана доверенность на вклад";
  elif (R_Old_Depositor.FlagTrast AND (NOT R_Depositor.FlagTrast))
    if (str != "")
      str = str + "\n";
    end;
    str = str + "Удалена доверенность на вклад";
  end;
  if ((NOT R_Old_Depositor.FlagWill) AND R_Depositor.FlagWill)
    if (str != "")
      str = str + "\n";
    end;
    str = str + "Выдано завещание на вклад";
  elif (R_Old_Depositor.FlagWill AND (NOT R_Depositor.FlagWill))
    if (str != "")
      str = str + "\n";
    end;
    str = str + "Удалено завещение на вклад";
  end;
  if ((NOT R_Old_Depositor.UseAlternate) AND R_Depositor.UseAlternate)
    if (str != "")
      str = str + "\n";
    end;
    str = str + "Ставка процентов изменена на альтернативную";
  elif(R_Old_Depositor.UseAlternate and (not R_Depositor.UseAlternate))
    if (str != "")
      str = str + "\n";
    end;
    str = str + "Ставка процентов изменена на основную";
  end;
  if ((NOT R_Old_Depositor.Open_Close) AND R_Depositor.Open_Close)
    if (str != "")
      str = str + "\n";
    end;
    str = str + "Счет открыт";
  elif (R_Old_Depositor.Open_Close AND (NOT R_Depositor.Open_Close))
    if (str != "")
      str = str + "\n";
    end;
    str = str + "Счет закрыт";
  end;
  if ((NOT R_Old_Depositor.GiveBook) AND IsBookGiven(R_Depositor.FNCash, R_Depositor.SvodAccount)) // if ((NOT R_Old_Depositor.GiveBook) AND ALG_CONTRACT.BookGiven)
    if (str != "")
      str = str + "\n";
    end;
    str = str + "По счету выдана сбер. книжка";
  end;

  return str;

end;

/**************************************************************************/
macro MCD_Depositor

  var str = "Счет удален";

  return str;

end;

/**************************************************************************/
macro OutRec_Depositor

  var Comment = "";
  var Account = "";

  SetRecordAddr(R_Old_Depositor, F_SC_Log                 );
  SetRecordAddr(R_Depositor,     F_SC_Log, 0, SecondRecOfs);

  if   (F_SC_Log.NumOperation == Act_Insert)
    Comment = MCI_Depositor();
    Account = R_Depositor.Account;
  elif (F_SC_Log.NumOperation == Act_Update)
    Comment = MCU_Depositor();
    Account = R_Depositor.Account;
  elif (F_SC_Log.NumOperation == Act_Delete)
    Comment = MCD_Depositor();
    Account = R_Old_Depositor.Account;
  end;

  OutReportLine(Account,Comment);

end;


/**************************************************************************\
|               Разборка записи файла доверенности/завещания               |
\**************************************************************************/
macro MCI_TrastWill

  var str = "";

  SetRecordAddr(R_TrastWill,F_SC_Log);

  if (R_TrastWill.VidDoc == TRAST_DOC )
    str = "Введена доверенность";
  end;
  if (R_TrastWill.VidDoc == WILL_DOC )
    str = "Введено завещание";
  end;
  if (R_TrastWill.VidDoc == WILL_PAY )
    str = "Введена разовая выплата по наследству";
  end;
  if (R_TrastWill.VidDoc == UNDER_TRIAL_DOC )
    str = "Введено судебное взыскание";
  end;
  if (R_TrastWill.VidDoc == UNDER_TRIAL_PAY )
    str = "Введена выплата взысканий";
  end;

  str = str + " на клиента " + GetClientName(R_TrastWill.CodClient);
  if ( R_TrastWill.VidDoc == WILL_DOC )
    str = str +" на долю " +
          String(R_TrastWill.QPart_m) + "/" +
          String(R_TrastWill.QPart_n) +
          " вклада";
  end;

  if ( R_TrastWill.VidDoc == WILL_PAY )
    str = str +" на сумму " + String(Money(R_TrastWill.Part));
  end;

  if(R_TrastWill.Type == ACCOUTN_PROXY)
    str = str + " (По доверенности)";
  end;
  if(R_TrastWill.Type == ACCOUTN_HEIR)
    str = str + " (По завещанию)";
  end;
  if(R_TrastWill.Type == ANONIMUS_CLIENT)
    str = str + " (Вноситель)";
  end;
  if(R_TrastWill.Type == ACCOUNT_IMPORTER)
    str = str + " (Вноситель)";
  end;
  if(R_TrastWill.Type == ACCOUNT_TUTOR)
    str = str + " (Опекун)";
  end;
  if(R_TrastWill.Type == ACCOUNT_OBLIGATOR)
    str = str + " (Под обязательство)";
  end;
  if(R_TrastWill.Type == ACCOUTN_HEIR_PAY)
    str = str + " (Наследник)";
  end;
  if(R_TrastWill.Type == ACCOUNT_UNDER_TRIAL)
    str = str + " (По суду)";
  end;
  if(R_TrastWill.Type == ACCOUNT_PARENT)
    str = str + " (Родитель)";
  end;

  return str;

end;

/**************************************************************************/
macro MCU_TrastWill

  var str = "";

  SetRecordAddr(R_Old_TrastWill,F_SC_Log);
  SetRecordAddr(R_TrastWill,F_SC_Log,0,SecondRecOfs);

  str = "По ";
  if (NOT R_TrastWill.VidDoc)
    str = str + "доверенности ";
  else
    str = str + "завещанию ";
  end;
  str = str + "на клиента " + GetClientName(R_TrastWill.CodClient) + ":";

  if (R_TrastWill.VidDoc AND (R_Old_TrastWill.Part != R_TrastWill.Part))
    str = str + "\nизменено долевое участие с " + String(R_Old_TrastWill.Part:0:2) +
                                         " на " + String(R_TrastWill.Part:0:2);
  end;
  if (R_Old_TrastWill.DateOpen != R_TrastWill.DateOpen)
    str = str + "\nизменена дата открытия с " + String(R_Old_TrastWill.DateOpen) +
                                       " на " + String(R_TrastWill.DateOpen);
  end;
  if (R_Old_TrastWill.CloseDate != R_TrastWill.CloseDate)
    str =str + "\nизменена дата открытия с " + String(R_Old_TrastWill.CloseDate) +
                                      " на " + String(R_TrastWill.CloseDate);
  end;
  if (NOT R_TrastWill.VidDoc)
    if ((NOT R_Old_TrastWill.BlockFlag) AND R_TrastWill.BlockFlag)
      str = str + "\nустановлена блокировка";
    elif (R_Old_TrastWill.BlockFlag AND (NOT R_TrastWill.BlockFlag))
      str = str +"\nснята блокировка";
    end;
  end;

  return str;

end;

/**************************************************************************/
macro MCD_TrastWill

  var str = "";

  SetRecordAddr(R_TrastWill,F_SC_Log);

  if (NOT R_TrastWill.VidDoc)
    str = "Доверенность ";
  else
    str = "Завещание ";
  end;
  str = str + "на клиента " + GetClientName(R_TrastWill.CodClient);
  if (NOT R_TrastWill.VidDoc)
    str = str + " удалена";
  else
    str = str + " удалено"
  end;

  return str;

end;

/**************************************************************************/
macro OutRec_TrastWill

  var Comment = "";

  if   (F_SC_Log.NumOperation == Act_Insert)
    Comment = MCI_TrastWill();
  elif (F_SC_Log.NumOperation == Act_Update)
    Comment = MCU_TrastWill();
  elif (F_SC_Log.NumOperation == Act_Delete)
    Comment = MCD_TrastWill();
  end;

  OutReportLine(GetAccount(R_TrastWill.RefID),Comment);

end;


/**************************************************************************\
|        Разборка записи файла первода вклада по окончании договора        |
\**************************************************************************/
macro MCI_AccGet

  var str  = "";
  var Type = "";

  SetRecordAddr(R_AccGet,F_SC_Log);

  if (R_AccGet.GetType == Get_Type)
    Type = "тип вклада";
  else
    Type = "счет";
  end;

  str = "Часть вклада " + String(R_AccGet.PartAccount:0:2) +
        " по окончании договора переводится на " + Type + " " + R_AccGet.Ac_Type_Get;

  return str;

end;

/**************************************************************************/
macro MCU_AccGet()

  var str = "";

  SetRecordAddr(R_Old_AccGet,F_SC_Log);
  SetRecordAddr(R_AccGet,F_SC_Log,0,SecondRecOfs);

  if (R_Old_AccGet.PartAccount != R_AccGet.PartAccount)
    str = "Изменена часть перевода с " + String(R_Old_AccGet.PartAccount:0:2) +
                                " на " + String(R_AccGet.PartAccount:0:2);
  end;

  return str;

end;

/**************************************************************************/
macro MCD_AccGet

  var str = "Удален перевод вклада по окончании срока";

  return str;

end;

/**************************************************************************/
macro OutRec_AccGet

  var Comment = "";

  if   (F_SC_Log.NumOperation == Act_Insert)
    Comment = MCI_AccGet();
  elif (F_SC_Log.NumOperation == Act_Update)
    Comment = MCU_AccGet();
  elif (F_SC_Log.NumOperation == Act_Delete)
    Comment = MCD_AccGet();
  end;

  OutReportLine(GetAccount(R_AccGet.ReferencAcc),Comment);

end;


/**************************************************************************\
|               Разборка записи файла планового перечисления               |
\**************************************************************************/
macro MCI_PlanPay

  var str = "";

  SetRecordAddr(R_PlanPay,F_SC_Log);

  str = "По счету введено перечисление на сумму " + Trim(String(R_PlanPay.Sum:15:2:a));
  if (R_PlanPay.Account_Receiver != "")
    str = str + "\nСчет получателя: " + Acc_Form(R_PlanPay.Account_Receiver);
  end;
  if (R_PlanPay.BankAccountReceiver != "" )
    str = str + "\nСчет получателя в банке: " + R_PlanPay.BankAccountReceiver;
  end;
  if (R_PlanPay.MFO_Receiver != "")
    str = str + "\nМФО получателя: " + R_PlanPay.MFO_Receiver;
  end;
  if (R_PlanPay.CorAcc_Receiver != "")
    str = str + "\nКорсчет получателя: " + Acc_Form(R_PlanPay.CorAcc_Receiver);
  end;
  str = str + "\nДата начала платежа: " + String(R_PlanPay.Start_DatePay) +
              "\nДата конца платежа: " + String(R_PlanPay.End_DatePay);

  return str;

end;

/**************************************************************************/
macro MCU_PlanPay()

  var str = "";

  SetRecordAddr(R_Old_PlanPay,F_SC_Log                 );
  SetRecordAddr(R_PlanPay,    F_SC_Log, 0, SecondRecOfs);

  if (R_Old_PlanPay.Sum != R_PlanPay.Sum)
    if (str != "")
      str = str + "\n";
    end;
    str = str + "Изменена сумма перечисления с " + Trim(String(R_Old_PlanPay.Sum:15:2:a)) +
                                          " на " + Trim(String(R_PlanPay.Sum:15:2:a));
  end;
  if (R_Old_PlanPay.Account_Receiver != R_PlanPay.Account_Receiver)
    if (str != "")
      str = str + "\n";
    end;
    if (R_Old_PlanPay.Account_Receiver != "")
      str = str + "Изменен счет получателя с " + Acc_Form(R_Old_PlanPay.Account_Receiver) + " на ";
    else
      str = str + "Введен счет получателя ";
    end;
    str = str + Acc_Form(R_PlanPay.Account_Receiver);
  end;
  if (R_Old_PlanPay.BankAccountReceiver != R_PlanPay.BankAccountReceiver)
    if (str != "")
      str = str + "\n";
    end;
    if (R_Old_PlanPay.BankAccountReceiver != "")
      str = str + "Изменен счет получателя в банке с " + R_Old_PlanPay.BankAccountReceiver + " на ";
    else
      str = str + "Введен счет получателя в банке ";
    end;
    str = str + R_PlanPay.BankAccountReceiver;
  end;
  if (R_Old_PlanPay.MFO_Receiver != R_PlanPay.MFO_Receiver)
    if (str != "")
      str = str + "\n";
    end;
    if (R_Old_PlanPay.MFO_Receiver != "")
      str = str + "Изменено МФО получателя с " + R_Old_PlanPay.MFO_Receiver + " на ";
    else
      str = str + "Введено МФО получателя ";
    end;
    str = str + R_PlanPay.MFO_Receiver;
  end;
  if (R_Old_PlanPay.CorAcc_Receiver != R_PlanPay.CorAcc_Receiver)
    if (str != "")
      str = str + "\n";
    end;
    if (R_Old_PlanPay.CorAcc_Receiver != "")
      str = str + "Изменен корсчет получателя с " + Acc_Form(R_Old_PlanPay.CorAcc_Receiver) + " на ";
    else
      str = str + "Введен корсчет получателя ";
    end;
    str = str + Acc_Form(R_PlanPay.CorAcc_Receiver);
  end;
  if (R_Old_PlanPay.Start_DatePay != R_PlanPay.Start_DatePay)
    if (str != "")
      str = str + "\n";
    end;
    str = str + "Изменена дата начала платежа с " + String(R_Old_PlanPay.Start_DatePay) + " на " + String(R_PlanPay.Start_DatePay);
  end;
  if (R_Old_PlanPay.End_DatePay != R_PlanPay.End_DatePay)
    if (str != "")
      str = str + "\n";
    end;
    str = str + "Изменена дата конца платежа с " + String(R_Old_PlanPay.End_DatePay) + " на " + String(R_PlanPay.End_DatePay);
  end;

  return str;

end;

/**************************************************************************/
macro MCD_PlanPay

  var str = "Удалено плановое перечисление";

  return str;

end;

/**************************************************************************/
macro OutRec_PlanPay

  var Comment = "";

  if   (F_SC_Log.NumOperation == Act_Insert)
    Comment = MCI_PlanPay();
  elif (F_SC_Log.NumOperation == Act_Update)
    Comment = MCU_PlanPay();
  elif (F_SC_Log.NumOperation == Act_Delete)
    Comment = MCD_PlanPay();
  end;

  OutReportLine(GetAccount(R_PlanPay.Reference),Comment);

end;


/**************************************************************************\
|                     Разборка записи файла вкладчиков                     |
\**************************************************************************/
macro MCI_DepClient

  var str         = "";
  var Citizenship = "";

  SetRecordAddr(R_DepClient,F_SC_Log);

  if (R_DepClient.Citizenship != "")
    Citizenship = "нерезидент";
  else
    Citizenship = "резидент";
  end;

  str = "Введен клиент "        + R_DepClient.Sname + " " +
                                  R_DepClient.Name  + " " +
                                  R_DepClient.Pname +
        "\nДата рождения: "     + String(R_DepClient.Borndate) +
        "\nДокумент:\n"         + MakePersIDStr(R_DepClient) +
        "\nАдрес прописки:\n"   + R_DepClient.Adress +
        "\nАдрес проживания:\n" + R_DepClient.Adress2 +
        "\nГражданство: "       + Citizenship +
        "\nСоциальный номер: "  + R_DepClient.SocialNumber;

  return str;

end;

/**************************************************************************/
macro MCU_DepClient

  var str = "";

  SetRecordAddr(R_Old_DepClient,F_SC_Log                 );
  SetRecordAddr(R_DepClient,    F_SC_Log, 0, SecondRecOfs);

  str = "Клиент " + R_Old_DepClient.Sname + " " +
                    R_Old_DepClient.Name  + " " +
                    R_Old_DepClient.Pname;

  if (R_Old_DepClient.Sname != R_DepClient.Sname)
    str = str + "\nИзменена фамилия с " + R_Old_DepClient.Sname + " на " + R_DepClient.Sname;
  end;
  if (R_Old_DepClient.Name != R_DepClient.Name)
    str =str + "\nИзменено имя с " + R_Old_DepClient.Name + " на " + R_DepClient.Name;
  end;
  if (R_Old_DepClient.Pname != R_DepClient.Pname)
    str = str + "\nИзменено отчество с " + R_Old_DepClient.Pname + " на " + R_DepClient.Pname;
  end;
  if (R_Old_DepClient.BornDate != R_DepClient.BornDate)
    str = str + "\nИзменена дата рождения с "+ String(R_Old_DepClient.BornDate) + " на " + String(R_DepClient.BornDate);
  end;
  if (R_Old_DepClient.DateDeath != R_DepClient.DateDeath)
    str = str + "\nИзменена дата смерти с "+ String(R_Old_DepClient.DateDeath) + " на " + String(R_DepClient.DateDeath);
  end;
  if (R_Old_DepClient.Country != R_DepClient.Country)
    str = str + "\nИзменен код страны с "+ String(R_Old_DepClient.Country) + " на " + String(R_DepClient.Country);
  end;
  if (R_Old_DepClient.SpecialAccess != R_DepClient.SpecialAccess)
    str = str + "\nИзменен спецдоступ с "+ String(R_Old_DepClient.SpecialAccess) + " на " + String(R_DepClient.SpecialAccess);
  end;
  if (R_Old_DepClient.GroupType != R_DepClient.GroupType)
    str = str + "\nИзменена категория населения с "+ String(R_Old_DepClient.GroupType) + " на " + String(R_DepClient.GroupType);
  end;
  if (R_Old_DepClient.Kategor != R_DepClient.Kategor)
    str = str + "\nИзменена категория населения с "+ FindName(713,R_Old_DepClient.Kategor) + " на " + FindName(713,R_DepClient.Kategor);
  end;
  if (R_Old_DepClient.RegistrDate != R_DepClient.RegistrDate)
    str = str + "\nИзменена дата регистрации с "+ String(R_Old_DepClient.RegistrDate) + " на " + String(R_DepClient.RegistrDate);
  end;
  if (R_Old_DepClient.IsLiterate != R_DepClient.IsLiterate)
    str = str + "\nИзменен признак граммотности с "+ String(R_Old_DepClient.IsLiterate) + " на " + String(R_DepClient.IsLiterate);
  end;
  if (R_Old_DepClient.TakeIncomTax != R_DepClient.TakeIncomTax)
    str = str + "\nИзменен признак взятия налога с "+ String(R_Old_DepClient.TakeIncomTax) + " на " + String(R_DepClient.TakeIncomTax);
  end;
  if (MakePersIDStr(R_Old_DepClient) != MakePersIDStr(R_DepClient))
    str = str + "\nИзменен документ с\n" + MakePersIDStr(R_Old_DepClient) +
                                "\nна\n" + MakePersIDStr(R_DepClient);
  end;
  if (R_Old_DepClient.Adress != R_DepClient.Adress)
    str = str + "\nИзменен адрес прописки с\n" + R_Old_DepClient.Adress +
                                      "\nна\n" + R_DepClient.Adress;
  end;
  if (R_Old_DepClient.Adress2 != R_DepClient.Adress2)
    str = str + "\nИзменен адрес проживания с\n" + R_Old_DepClient.Adress2 +
                                        "\nна\n" + R_DepClient.Adress2;
  end;
  if ((R_Old_DepClient.Citizenship == "") AND (R_DepClient.Citizenship != ""))
    str = str + "\nИзменено гражданство с 'резидент' на 'нерезидент'";
  elif ((R_Old_DepClient.Citizenship != "") AND (R_DepClient.Citizenship == ""))
    str = str + "\nИзменено гражданство с 'нерезидент' на 'резидент'";
  end;
  if (R_Old_DepClient.SocialNumber != R_DepClient.SocialNumber)
    str = str + "\nИзменен социальный номер с " + R_Old_DepClient.SocialNumber +
                                         " на " + R_DepClient.SocialNumber;
  end;

  return str;

end;

/**************************************************************************/
macro MCD_DepClient

  var str = "";

  SetRecordAddr(R_DepClient,F_SC_Log);

  str = "Удален клиент " + R_DepClient.Sname + " " +
                           R_DepClient.Name  + " " +
                           R_DepClient.Pname        ;

  return str;

end;

/**************************************************************************/
macro OutRec_DepClient

  var Comment = "";

  if   (F_SC_Log.NumOperation == Act_Insert)
    Comment = MCI_DepClient();
  elif (F_SC_Log.NumOperation == Act_Update)
    Comment = MCU_DepClient();
  elif (F_SC_Log.NumOperation == Act_Delete)
    Comment = MCD_DepClient();
  end;

  OutReportLine("",Comment);

end;


/**************************************************************************\
|                  Разборка записи Журнала корректировок                   |
\**************************************************************************/
macro OutCurRec

  var FileNumber=F_SC_Log.NumFile;

  if   (FileNumber == FN_Depositor)
    OutRec_Depositor();
  elif (FileNumber == FN_TrastWill)
    OutRec_TrastWill();
  elif (FileNumber == FN_AccGet   )
    OutRec_AccGet();
  elif (FileNumber == FN_PlanPay  )
    OutRec_PlanPay();
  elif (FileNumber == FN_DepClient)
    OutRec_DepClient();
  end;

end;


/**************************************************************************\
|                  Цикл по записям Журнала корректировок                   |
\**************************************************************************/
macro OutLogRecs

  var stat;

  ClearRecord(F_SC_Log);
  F_SC_Log.FNCash      = Branch;
  F_SC_Log.DateOperDay = Cdate;

  stat = GetGE(F_SC_Log);

  while (stat)
    if ((F_SC_Log.FNCash      == Branch   ) AND
        (F_SC_Log.DateOperDay == Cdate)    )
      OutCurRec();
      stat = Next(F_SC_Log);
    else
      stat = FALSE;
    end;
  end;

end;


/**************************************************************************\
|    Выпуск отчета изменений по вкладным файлам в Журнале корректировок    |
\**************************************************************************/
macro OutLog

  GetDate( CDate, "Ведомость за..." );
  OutHeader();
  OutLogRecs();
  OutBottom();

end;


/**************************************************************************\
|                    Г Л А В Н А Я    П Р О Г Р А М М А                    |
\**************************************************************************/

  OutLog();

end;
