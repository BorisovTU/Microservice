/*
  RS-Retail
  Обслуживание физических лиц
----------------------------------------------------------------------
  Операция # 34 (закрытие счета (операции 10, 81))
  Шаг # 20 (проверка при закрытии)
----------------------------------------------------------------------
   Условное закрытие счета
   =======================
  Если счет был открыт до заданной даты (20.06.1991), то он может
  быть закрыт условно, т.е. остаться в числе открытых с нулевым
  остатком.
----------------------------------------------------------------------
  Сделано по запросу 10031
----------------------------------------------------------------------
  Ромодин Александр Васильевич
  08.01.2003
*/
import deprintr,alg_vars;

const UPDATE_DEP   = 2,  /* Обновить запись по счету */
      UPDATE_DOC   = 4;  /* Обновить документ   */

const TestDate   = Date (20,06,1991);

/*********************************************************************
 Точка входа в макрос
*/
  var cod, len;
  ALG_RESULT = OK_RES;   /* Выполнить операцию 80 стандартным образом */

  if ( (ALG_DEPOSITOR.Open_Date <= TestDate) // Открыт давно
   AND (ALG_SBDEPDOC.Author != StrFor(15)) )         // Не прогноз процентов
    cod = Index (ALG_DEPOSITOR.UserTypeAccount, "Z");
    if (cod != 0) /* Счет условно закрыт */
      if (GetTrue (True,"Счет условно закрыт.|Закрыть его реально?"))
        /* Снять со счета признак условного закрытия */
        len = StrLen (ALG_DEPOSITOR.UserTypeAccount);
        if (cod == 1)
          if (len > 1)
            ALG_DEPOSITOR.UserTypeAccount = SubStr (ALG_DEPOSITOR.UserTypeAccount,2);
          else
            ALG_DEPOSITOR.UserTypeAccount = "";
          end;
        else
          ALG_DEPOSITOR.UserTypeAccount = SubStr (ALG_DEPOSITOR.UserTypeAccount,1,(cod-1)) +
                                          SubStr (ALG_DEPOSITOR.UserTypeAccount,(cod+1));
        end;
        ALG_UPDATE = UPDATE_DEP;
      else
        ALG_RESULT = 6058; /* Отказались от выпролнения операции */
      end;
    else
      if (GetTrue (False,"Оставить счет после закрытия в действующих?"))
        /* Условное закрытие счета- ради чего вводится этот шаг */
        ALG_SBDEPDOC.ApplType = 3;
        ALG_DEPOSITOR.UserTypeAccount = ALG_DEPOSITOR.UserTypeAccount + "Z";
        ALG_UPDATE = UPDATE_DOC + UPDATE_DEP;
      end;
    end;
  end;
end;
/* --- Конец макроса --- */
