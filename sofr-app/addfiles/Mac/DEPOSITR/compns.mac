
/* Компенсации за день */
import deprintr, rsd;

file Compens("compens.dbt") key 1;

var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/
var FNCash   = NumFNCash();

macro ToDate( ss )
   var dt;
   DtTmSplit( ss, dt );
   return dt;
end;

macro OutHeader(TheDate);

  println("                                                 Компенсации за "+
    String(TheDate));
  println;
  [┌─────────────────────────┬────────────────────────┬──────────┬──────────┬───────────────────┬─────────────┬───────────────────┬─────────────────────────┐];
  [│                         │                        │   Дата   │   Дата   │   Сумма вклада,   │ Коэффициент │       Сумма       │ Номер счета, на который │];
  [│  Н о м е р   с ч е т а  │ Фамилия И.О. вкладчика │ рождения │ закрытия │    подлежащая     │ компенсации │    компенсации    │ зачисляется компенсация │];
  [│                         │                        │ вкладчика│  счета   │    компенсации    │             │                   │                         │];
  [├─────────────────────────┼────────────────────────┼──────────┼──────────┼───────────────────┼─────────────┼───────────────────┼─────────────────────────┤];
end;

macro OutLine(RS);
  var BirthDate, AccClsDate;
  AccClsDate = String( ToDate(RS.value("AccClsDate")));
  BirthDate = String( ToDate(RS.value("BirthDate")));

  var LastNameWithInitials; 
  if(  (not SpecialAccess) and OperMayListSpecialAccounts and RS.value("SpecialAccess"))
     LastNameWithInitials = "XXXXXX X.X.";
  else
     LastNameWithInitials = RS.value("LastName")+" "+
         SubStr(RS.value("FirstName"),1,1)+"."+
         SubStr(RS.value("PatrName"),1,1)+".";
  end;

  if ( BirthDate == " 1.01.0001" )
     BirthDate = " - ";
  end;
  if ( AccClsDate == " 1.01.0001" )
     AccClsDate = " - ";
  end;

  [│#########################│########################│##########│##########│###################│#############│###################│#########################│]
  (
    Acc_Form(String(RS.value("Account"))),
    LastNameWithInitials,
    BirthDate, 
    AccClsDate,
    RS.value("CompensedSum"),
    RS.value("Rate"),
    RS.value("Sum"),
    Acc_Form(String(RS.value("DestAccount")))
  );
end;

macro OutBottom;

  [└─────────────────────────┴────────────────────────┴──────────┴──────────┴───────────────────┴─────────────┴───────────────────┴─────────────────────────┘];
end;

macro OutBody(TheDate)
   var SqlQuery, cmd, RS, LastNameWithInitials;
   SqlQuery = string("select c.t_account as Account, c.t_lastName as LastName, c.t_firstName as FirstName, c.t_patrName as PatrName, ",
                        "c.t_birthDate as BirthDate, c.t_accClsDate as AccClsDate, c.t_compensedSum as CompensedSum, c.t_Rate as Rate, ", 
                        "c.t_sum as Sum, c.t_destAccount as DestAccount, d.t_specialAccess as SpecialAccess", 
                     " from ddepositr_dbt d, dcompens_dbt c ", 
                     " where c.t_Date = '", TheDate,"'",
                        " and c.t_FNCash= ", FNCash,
                        " and c.T_DESTACCOUNT = d.t_Account ",
                        " and c.t_FNCash = d.t_FNCash"
                     );
   cmd = RsdCommand(SqlQuery);
   cmd.execute;
   RS = RsdRecordset(cmd);

   while (RS.MoveNext)
       OutLine(RS);
   end;
end;

  var
  TheDate,{curdate};

  TheDate={curdate};
  GetDate(TheDate,"Введите дату");
  OutHeader(TheDate);
  OutBody(TheDate);
  OutBottom;
end;




