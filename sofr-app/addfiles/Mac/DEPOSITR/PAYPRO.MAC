/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Налично-кассовые операции                                               *
*                                                                          *
*  Проведение операции по снятию/пополнению наличных по пластиковым картам *
*                                                                          *
*  Лебедев С.В.                                                            *
*  24.07.2001                                                              *
\**************************************************************************/

import DeprIntr;

record OC_PAY_DOC  ("pay_doc.n36" ); /* Глобальная переменная */
record OC_PAY_KIND ("pay_kind.dbt"); /* Глобальная переменная */

record pay_doc     ("pay_doc.dbt" );

file scPaySys      (scPaySys,"spcard.def");      /* Справочник платежных систем  */
file pay_csop      (pay_csop              ) key 0;
file currency      (currency              ) key 0;
file issuer        (issuer, "exchange.def") key 0;
file scCodif       (scCodif,"spcard.def"  );

var depclnt = TClientList;

var FlagCur = NumFlagCur( );

var NumTarif     = 0;
var WasPressedF5 = FALSE;
var SaveValue;

const NameOurBankOnCard = "ПРОБИЗНЕСБАНК";

/* Номера карточек, эмитентом которых является наш банк */
array AOurCardNumbers;
  AOurCardNumbers(0) = "545421";
  AOurCardNumbers(1) = "554538";
  AOurCardNumbers(2) = "676479";

/* Номера тарифов для типов карточек, которые отличаются от настройки в БД */
array ACardTypesForTarif;
array ATarifNumbers;
  ACardTypesForTarif(0) = "D"; ATarifNumbers(0) = 61; /* На D начинаются карточки Diners Club */

record InputPanelOut ("PAYOUT36") dialog;
record InputPanelIn  ("PAYIN36" ) dialog;
  const ne_APPL_NUM        =  0;
  const ne_APPL_NAME       =  1;
  const ne_DATE_OPERAT     =  2;
  const ne_CARD_NUM        =  3;
  const ne_PAYSYS_CODE     =  4;
  const ne_PAYSYS_NAME     =  5;
  const ne_CARD_TYPE_CODE  =  6;
  const ne_CARD_TYPE_NAME  =  7;
  const ne_CARD_BANK_NAME  =  8;
  const ne_SUMMA_OPERAT    =  9;
  const ne_CURRNAME_SUMMA  = 10;
  const ne_SUMMA_COMM      = 11;
  const ne_CURRNAME_PLATA  = 12;
  const ne_SUMMA_CLIENT    = 13;
  const ne_CURRNAME_OPERAT = 14;
  const ne_GROUND          = 15;


/**************************************************************************\
|    Определение номера тарифа для расчета платы за выдачу по карточке     |
\**************************************************************************/
macro DefTarifForCalc (InputNumTarif)

  var OutNumTairf     = InputNumTarif;
  var i               = 0;
  var count_dif_types = 0;
  var length_type;

  /* Если по подоперации положена плата, уточним ее значение в зависимости */
  /* от типа карточки */
  if (OutNumTairf > 0)
    count_dif_types = ASize(ACardTypesForTarif);
    while (i < count_dif_types)
      length_type = StrLen(ACardTypesForTarif(i));
      if (length_type > 0)
        if (StrUpr(SubStr(OC_PAY_DOC.cardTypeCode,1,length_type)) == StrUpr(ACardTypesForTarif(i)))
          OutNumTairf = ATarifNumbers(i);
        end;
      end;
      i = i + 1;
    end;
  end;

  return OutNumTairf;

end;


/**************************************************************************\
|             Сохранение полей записи налично-кассовой операции            |
\**************************************************************************/
macro SaveFields

  if (OC_PAY_KIND.GroupOpert != 11)
    Copy(InputPanelIn,InputPanelOut);
  end;

  OC_PAY_DOC.Ground       = InputPanelIn.GROUND;
  OC_PAY_DOC.cardNumber   = InputPanelIn.CARD_NUM;
  OC_PAY_DOC.cardBankName = InputPanelIn.CARD_BANK_NAME;

end;


/**************************************************************************\
|    Проверка заполнения полей  панели ввода дополнительной информации     |
\**************************************************************************/
macro CheckFields

  var stat       = TRUE;
  var i;
  var OurCardNum;

  if (OC_PAY_KIND.GroupOpert != 11)
    Copy(InputPanelIn,InputPanelOut);
  end;

  if (NOT WasPressedF5)
    WasPressedF5 = TRUE;
    PanelClientForPayDoc();
  end;

  if ( OC_PAY_DOC.CodClient != 0 )
    if ( depclnt.GetRecord( OC_PAY_DOC.CodClient ) )
      if ( ( depclnt.CurRec.rec.Name1 != OC_PAY_DOC.ClientLastName   ) or
           ( depclnt.CurRec.rec.Name2 != OC_PAY_DOC.ClientFirstName  ) or
           ( depclnt.CurRec.rec.Name3 != OC_PAY_DOC.ClientSecondName )   )
        OC_PAY_DOC.CodClient = 0;
      end;
    else
      OC_PAY_DOC.CodClient = 0;
    end;
  end;

  if (stat AND (InputPanelIn.SUMMA_CLIENT == $0L))
    stat = FALSE;
    MsgBox("Не задана сумма операции|Сумма операции является обязательным параметром");
  end;

  if (stat AND (OC_PAY_DOC.ApplType == 0))
    stat = FALSE;
    MsgBox("Не задан подвид операции|Подвид операции является обязательным параметром");
  end;

  if (stat AND (Trim(InputPanelIn.CARD_NUM) == ""))
    stat = FALSE;
    MsgBox("Не задан номер карточки|Номер карточки является обязательным параметром");
  end;

  if (stat AND (Trim(InputPanelIn.PAYSYS_CODE) == ""))
    stat = FALSE;
    MsgBox("Не задана платежная система|Платежная система является обязательным параметром");
  end;

  if (stat AND (Trim(InputPanelIn.CARD_TYPE_CODE) == ""))
    stat = FALSE;
    MsgBox("Не задан тип карточки|Тип карточки является обязательным параметром");
  end;

  if (stat AND (Trim(InputPanelIn.CARD_TYPE_CODE) == "???"))
    stat = FALSE;
    MsgBox("Не определен тип карточки в справочнике");
  end;

  if (stat AND (OC_PAY_KIND.GroupOpert == 11))
    i = 0;
    OurCardNum = FALSE;
    while (i < ASize(AOurCardNumbers))
      if (StrLen(AOurCardNumbers(i)) > 0)
        if (SubStr(Trim(InputPanelIn.CARD_NUM),1,StrLen(AOurCardNumbers(i))) == AOurCardNumbers(i))
          OurCardNum = TRUE;
          i = ASize(AOurCardNumbers);
        end;
      end;
      i = i + 1;
    end;
    if (NOT OurCardNum)
      stat = FALSE;
      MsgBox("Карточка не принадлежит \"нашему\" банку");
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                 Выбор эмитента для пластиковой карточки                  |
\**************************************************************************/
macro SelectIssuerForPayDoc

  array APaySysName;
  array APaySysCode;

  var was_choose = FALSE;
  var stat;
  var i;

  scPaySys.paySysId = 0;
  stat = GetGE(scPaySys);
  while (stat)
    i = ASize(APaySysCode);
    APaySysCode(i) = scPaySys.paySysId;
    APaySysName(i) = " " + scPaySys.paySysName + " ";
    stat = Next(scPaySys);
  end;

  if (ASize(APaySysCode) > 0)
    i = Menu(APaySysName," ~ENTER~ Выбор  ~ESC~ Отмена","Платежные системы");
    if (i >= 0)
      OC_PAY_DOC.cardIssuerCode = APaySysCode(i);
      OC_PAY_DOC.cardTypeCode    = "";
      OC_PAY_DOC.cardTypeCodePos = 0;
      InputPanelIn.CARD_TYPE_CODE = "";
      InputPanelIn.CARD_TYPE_NAME = "";
      was_choose = TRUE;
    end;
  end;

  return was_choose;

end;


/**************************************************************************\
|                     Выбор типа пластиковой карточки                      |
\**************************************************************************/
macro SelectCardTypeForPayDoc

  array ACardTypeName;
  array ACardTypeCode;
  array ACardTypePSRef;

  var was_choose = FALSE;
  var stat;
  var i;

  if(OC_PAY_DOC.cardIssuerCode > 0)
  KeyNum(scCodif,4);
  scCodif.codType     = 7;
  scCodif.paySysId = OC_PAY_DOC.cardIssuerCode;
  scCodif.codCode = "";
  stat = GetGE(scCodif);
  while (stat)
      if ((scCodif.codType == 7) AND (scCodif.paySysId == OC_PAY_DOC.cardIssuerCode) )
      i = ASize(ACardTypeCode);
      ACardTypeCode (i) = scCodif.codCode;
      ACardTypePSRef(i) = scCodif.psref;
      ACardTypeName (i) = " " + SubStr(scCodif.codUserCode + MkStr(" ",6),1,6) + " " + scCodif.codName + " ";
      stat = Next(scCodif);
    else
      stat = FALSE;
    end;
  end;

  if (ASize(ACardTypeCode) > 0)
    i = Menu(ACardTypeName," ~ENTER~ Выбор  ~ESC~ Отмена","Типы карт");
    if (i >= 0)
      OC_PAY_DOC.cardTypeCode    = ACardTypeCode (i);
      OC_PAY_DOC.cardTypeCodePos = ACardTypePSRef(i);
      was_choose = TRUE;
    end;
  end;
  else
    MsgBox("Не задана платежная система");
  end;

  return was_choose;

end;


/**************************************************************************\
|                          Обработчик для панели                           |
\**************************************************************************/
macro GetInfoFields (num_field)

  var NumTarifForCalc,
      dtRate, tmRate;

  PrepareDateTimeForRates( true, null, null, dtRate, tmRate );

  /* Название подоперации */
  if ((num_field == -1) OR (num_field == ne_APPL_NAME))
    NumTarif = 0;
    pay_csop.IsCur     = OC_PAY_DOC.IsCur;
    pay_csop.GroupType = OC_PAY_DOC.GroupOpert;
    pay_csop.NumOperat = OC_PAY_DOC.NumOpert;
    pay_csop.ApplType  = OC_PAY_DOC.ApplType;
    if (GetEQ(pay_csop))
      InputPanelIn.APPL_NAME = pay_csop.NameCompType;
      NumTarif = pay_csop.NumTarif;
    end;
  end;

  /* Тип карточки */
  if ((num_field == -1) OR (num_field == ne_CARD_TYPE_CODE))
    InputPanelIn.CARD_TYPE_CODE = "";
    InputPanelIn.CARD_TYPE_NAME = "";
    if (OC_PAY_DOC.cardTypeCode != "")
      KeyNum(scCodif,1);
      scCodif.codType = 7;
      scCodif.codCode = OC_PAY_DOC.cardTypeCode;
      scCodif.psRef   = OC_PAY_DOC.cardTypeCodePos;
      if (GetEQ(scCodif))
        InputPanelIn.CARD_TYPE_CODE  = scCodif.codUserCode;
        InputPanelIn.CARD_TYPE_NAME  = scCodif.codName;
      else
        InputPanelIn.CARD_TYPE_CODE  = "???";
        InputPanelIn.CARD_TYPE_NAME  = "";
      end;
    end;
  end;

  /* Эмитент карточки */
  if ((num_field == -1) OR (num_field == ne_PAYSYS_CODE))
    InputPanelIn.PAYSYS_CODE = "";
    if (OC_PAY_DOC.cardIssuerCode > 0)
      scPaySys.paySysId = OC_PAY_DOC.cardIssuerCode;
      if (GetEQ(scPaySys))
        InputPanelIn.PAYSYS_CODE = scPaySys.paySysCode;
        InputPanelIn.PAYSYS_NAME = scPaySys.paySysName;
      else
        InputPanelIn.PAYSYS_CODE = "???";
      end;
    end;
  end;

  /* Название валюты */
  if ((num_field == -1) OR (num_field == ne_CURRNAME_OPERAT))
    currency.Code_Currency = OC_PAY_DOC.CodCur;
    if (GetEQ(currency))
      InputPanelIn.CURRNAME_OPERAT = currency.Short_Name;
    else
      InputPanelIn.CURRNAME_OPERAT = "???";
    end;
    InputPanelIn.CURRNAME_SUMMA = InputPanelIn.CURRNAME_PLATA = InputPanelIn.CURRNAME_OPERAT;
  end;

  /* Расчет суммы комиссии */
  if ((num_field == -1                ) OR
      (num_field == ne_APPL_NAME      ) OR
      (num_field == ne_CURRNAME_OPERAT) OR
      (num_field == ne_SUMMA_CLIENT   ) OR
      (num_field == ne_CARD_TYPE_CODE )   )
    if ((NumTarif > 0) AND (OC_PAY_KIND.GroupOpert == 41) AND (OC_PAY_DOC.cardTypeCode != ""))
      NumTarifForCalc = DefTarifForCalc(NumTarif);
      if ( CalculateTariff( NumTarifForCalc, OC_PAY_DOC.CodCur,
                            dtRate,
                            InputPanelIn.SUMMA_CLIENT,
                            InputPanelIn.SUMMA_COMM,
                            false, tmRate ) != 0 )
        OC_PAY_DOC.CommSum = InputPanelIn.SUMMA_COMM   = $0L;
        OC_PAY_DOC.InSum   = InputPanelIn.SUMMA_OPERAT = InputPanelIn.SUMMA_CLIENT;
        MsgBox("Ошибка при расчете тарифа");
      else
        OC_PAY_DOC.CommSum = InputPanelIn.SUMMA_COMM;
        OC_PAY_DOC.InSum   = InputPanelIn.SUMMA_OPERAT = InputPanelIn.SUMMA_COMM + InputPanelIn.SUMMA_CLIENT;
      end;
    else
      OC_PAY_DOC.CommSum = InputPanelIn.SUMMA_COMM   = $0L;
      OC_PAY_DOC.InSum   = InputPanelIn.SUMMA_OPERAT = InputPanelIn.SUMMA_CLIENT;
    end;
  end;

  Copy(InputPanelOut,InputPanelIn);

end;


/**************************************************************************\
|                          Обработчик для панели                           |
\**************************************************************************/
macro WorkDialog ( IP, cmd, id, key )

  var stat = CM_DEFAULT;
  var NumSubOper;
  var CodCur;

  /* ================================================================== */
  if ( cmd == DLG_KEY )
    /* ENTER -------------------------------------------------------- */
    if ( key == 13 )
      if ( id == ne_GROUND )
        SetFocus( IP, ne_APPL_NAME );
        stat = CM_IGNORE;
      end;
    /* F2 ----------------------------------------------------------- */
    elif ( key == 316 )
      if ( id == ne_SUMMA_CLIENT )
        if ( OC_PAY_KIND.GroupOpert != 11 )
          Copy( InputPanelIn, InputPanelOut );
        end;
        if ( SaveValue != InputPanelIn.SUMMA_CLIENT )
          GetInfoFields( id );
          UpdateFields( IP );
        end;
      end;
      if ( CheckFields( ) )
        SaveFields( );
        stat = CM_SAVE;
      else
        stat = CM_IGNORE;
      end;
    /* F3 ----------------------------------------------------------- */
    elif ( key == 317 )
      if ( id == ne_APPL_NAME )
        NumSubOper = ChoosePaySubOper( OC_PAY_DOC.GroupOpert, OC_PAY_DOC.NumOpert );
        if ( NumSubOper >= 0 )
          InputPanelIn.APPL_NUM = OC_PAY_DOC.ApplType = NumSubOper;
          GetInfoFields( id );
          UpdateFields( IP );
        end;
      elif ( id == ne_CARD_TYPE_CODE )
        if ( SelectCardTypeForPayDoc( ) )
          GetInfoFields( id );
          UpdateFields( IP );
        end;
      elif ( id == ne_PAYSYS_CODE )
        if ( SelectIssuerForPayDoc( ) )
          GetInfoFields( id );
          UpdateFields( IP );
        end;
      elif ( id == ne_CURRNAME_OPERAT )
        if ( SelectCurrency( CodCur, TRUE, TRUE ) )
          OC_PAY_DOC.CodCur = OC_PAY_DOC.CommCodCur = CodCur;
          GetInfoFields( id );
          UpdateFields( IP );
        end;
      end;
    /* F5 ----------------------------------------------------------- */
    elif ( key == 319 )
      if ( id == ne_SUMMA_CLIENT )
        if ( OC_PAY_KIND.GroupOpert != 11 )
          Copy( InputPanelIn, InputPanelOut );
        end;
        if ( SaveValue != InputPanelIn.SUMMA_CLIENT )
          GetInfoFields( id );
          UpdateFields( IP );
        end;
      end;
      WasPressedF5 = TRUE;
      PanelClientForPayDoc( );
    /* ESC ---------------------------------------------------------- */
    elif ( key == 27 )
      if ( id == ne_SUMMA_CLIENT )
        if ( OC_PAY_KIND.GroupOpert != 11 )
          Copy( InputPanelIn, InputPanelOut );
        end;
        if ( SaveValue != InputPanelIn.SUMMA_CLIENT )
          GetInfoFields( id );
          UpdateFields( IP );
        end;
      end;
      if ( GetTrue( TRUE, "Вы хотите отменить проведение операции?" ) )
        SaveFields( );
        stat = CM_CANCEL;
      else
        stat = CM_IGNORE;
      end;
    /* AltF11 ------------------------------------------------------- */
    elif ( key == 395 )
      FlagCur = 1 - FlagCur;
      SetFlagCur( FlagCur );
    end;
  /* ================================================================== */
  elif ( cmd == DLG_SETFOCUS )
    if ( OC_PAY_KIND.GroupOpert != 11 )
      Copy( InputPanelIn, InputPanelOut );
    end;
    if ( id == ne_SUMMA_CLIENT )
      SaveValue = InputPanelIn.SUMMA_CLIENT;
    end;
  /* ================================================================== */
  elif ( cmd == DLG_REMFOCUS )
    if ( OC_PAY_KIND.GroupOpert != 11 )
      Copy( InputPanelIn, InputPanelOut );
    end;
    if ( id == ne_SUMMA_CLIENT )
      if ( SaveValue != InputPanelIn.SUMMA_CLIENT )
        GetInfoFields( id );
        UpdateFields( IP );
      end;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                    Г Л А В Н А Я    П Р О Г Р А М М А                    |
\**************************************************************************/

  OC_RESULT = 0;
  OC_VARLEN = GetRecordSize(OC_PAY_DOC) - GetRecordSize(pay_doc);

  if (OC_RESULT == 0)
    ClearRecord(InputPanelIn);
    InputPanelIn.APPL_NUM        = OC_PAY_DOC.ApplType;
    InputPanelIn.DATE_OPERAT     = OC_PAY_DOC.Date_Document;
    InputPanelIn.CARD_NUM        = "";
    if (OC_PAY_KIND.GroupOpert == 11)
      InputPanelIn.CARD_BANK_NAME  = NameOurBankOnCard;
    else
      InputPanelIn.CARD_BANK_NAME  = "";
    end;
    InputPanelIn.SUMMA_CLIENT    = $0L;
    InputPanelIn.GROUND          = OC_PAY_DOC.Ground;

    GetInfoFields(-1);

    if (OC_PAY_KIND.GroupOpert == 11)
      if (RunDialog(InputPanelIn,"WorkDialog"))
        ;
      else
        OC_RESULT = 1;
      end;
    else
      if (RunDialog(InputPanelOut,"WorkDialog"))
        ;
      else
        OC_RESULT = 1;
      end;
    end;
  end;

  /* Если есть комиссия - вставим документ на сумму комиссии */
  if ((OC_RESULT == 0) AND (OC_PAY_DOC.CommSum > 0))
    ClearRecord(pay_doc);
    pay_doc.FNCash           = OC_PAY_DOC.FNCash;
    pay_doc.IsCur            = OC_PAY_DOC.IsCur;
    pay_doc.NumOpert         = OC_PAY_KIND.NumOperatComm;
    pay_doc.GroupOpert       = 21;
    pay_doc.Date_Document    = OC_PAY_DOC.Date_Document;
    pay_doc.CodCur           = OC_PAY_DOC.CodCur;
    pay_doc.Oper             = OC_PAY_DOC.Oper;
    pay_doc.InSum            = OC_PAY_DOC.CommSum;
    pay_doc.ClientLastName   = OC_PAY_DOC.ClientLastName;
    pay_doc.ClientFirstName  = OC_PAY_DOC.ClientFirstName;
    pay_doc.ClientSecondName = OC_PAY_DOC.ClientSecondName;
    pay_doc.ClientAddress    = OC_PAY_DOC.ClientAddress;
    pay_doc.ClientPhone      = OC_PAY_DOC.ClientPhone;
    pay_doc.SocialNumber     = OC_PAY_DOC.SocialNumber;
    pay_doc.PersIDType       = OC_PAY_DOC.PersIDType;
    pay_doc.PassportSer      = OC_PAY_DOC.PassportSer;
    pay_doc.PassportNumb     = OC_PAY_DOC.PasportNumb;
    pay_doc.PassportIssuer   = OC_PAY_DOC.PassportIssuer;
    pay_doc.PassportDate     = OC_PAY_DOC.PassportDate;
    pay_doc.FlagRez          = OC_PAY_DOC.FlagRez;
    pay_doc.CodClient        = OC_PAY_DOC.CodClient;
    pay_doc.iApplicationKind = 200;
    pay_doc.ApplicationKey   = FormApplicationKey(200);
    pay_doc.Ground           = "За выдачу наличных по пластиковой карте " + OC_PAY_DOC.cardNumber;

    if (NOT InsertPayDocToGDDList(pay_doc))
      OC_RESULT = 1;
      MsgBox("Ошибка при вставке документа на комиссию");
    end;
  end;

  OC_VARLEN = GetRecordSize( OC_PAY_DOC ) - GetRecordSize( pay_doc ); /* Теряется */

end;
