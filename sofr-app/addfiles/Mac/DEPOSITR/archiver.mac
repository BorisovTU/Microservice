
//
//  Класс для архивации/разархивации файлов
//

import CommonInter;

class TArchiver

  private const 
    VAR_SYS_ARCDIR : string = "{ДирАрхивов}", 
    VAR_SYS_DATADIR : string = "{ДирДанных}", 
    VAR_FILENAMES : string = "{ИменаФайлов}", 
    VAR_ARCNAME : string  = "{ИмяАрхива}", 
    VAR_PASSWORD : string  = "{Пароль}";

  private const 
    REG_PATH : string  = "RS-RETAIL\\INI\\ПЕРЕМЕННЫЕ\\ВНЕШНИЕ_ПРОГРАММЫ\\АРХИВАТОР\\";

  private var
    comSpec : string  = getEnv( "COMSPEC" );

  private var 
    archiver : string  = getRegVal( REG_PATH + "ARCHIVER" ), 
    arcSwitches : string  = getRegVal( REG_PATH + "ARCHSWITCHES" ), 
    extrSwitches : string  = getRegVal( REG_PATH + "EXTRSWITCHES" ), 
    arcExt : string  = getRegVal( REG_PATH + "ARCHIVEEXT" );

  private var
    arcDir : string = ".", 
    dataDir : string = ".", 
    logFileName : string = "nul" ;

  private macro substVar( str : string, varName : string, value : string )

    var
      varPos : integer, 
      openBrcPos : integer, 
      closeBrcPos : integer, 
      len : integer = strLen( str ),
      i : integer; 

    if ( value != null )
      varPos = index( str, varName );
      if ( varPos > 0 )
        i = varPos - 1;
        while ( ( i > 0 ) and ( subStr( str, i, 1 ) != "[" ) and ( subStr( str, i, 1 ) != "]" ) )
          i = i - 1;
        end;
        if ( ( i > 0 ) and ( subStr( str, i, 1 ) != "]" ) )
          openBrcPos = i;
          i = varPos + strLen( varName );
          while ( ( i <= len ) and ( subStr( str, i, 1 ) != "]" ) )
            i = i + 1;
          end;
          closeBrcPos = i;

          if ( closeBrcPos > len )
            msgBox( "Не закрыта [" );
            exit( 1 );
          end;
          str = subStr( str, 1, openBrcPos - 1 ) 
              + subStr ( str, openBrcPos + 1, closeBrcPos - openBrcPos - 1 )
              + subStr( str, closeBrcPos + 1 );
        end;   
        str = strSubst( str, varName, value );
      end;
    end;
    setParm( 1, str );
  end;

  private macro removeUnsubstituted( str : string )
    
    var
      brcPos : integer, 
      subCmdStart : integer, subCmdEnd : integer;


    // Убрать неподставленные переменные вместе с подкомандами

    while ( ( brcPos = index( str, "{" ) ) != 0 )
      subCmdStart = index( str, "[" );
      if ( ( subCmdStart != 0 ) and ( subCmdStart < brcPos ) )
        subCmdEnd = index( str, "]" );
      else
        subCmdStart = brcPos;
        subCmdEnd = index( str, "}" );
      end;
      str = subStr( str, 1, subCmdStart - 1 ) + subStr( str, subCmdEnd + 1 );
    end;
    setParm( 1, str );
  end;

  private macro preprocessFileNames( fileNames : string ) 

    var
      res : string = "", 
      len = strLen( fileNames ),
      pos;

    fileNames = trim( fileNames );
    while ( ( pos = index( fileNames, " " ) ) != 0 )
      res = res + dataDir + "\\" + subStr( fileNames, 1, pos - 1 ) + " ";
      fileNames = trim( subStr( fileNames, pos + 1 ) );
    end;
    if ( fileNames != "" )
      res = res + dataDir + "\\" + fileNames;
    end;

    setParm( 1, res );
  end;

  private macro substSysVars( str : string )
    substVar( str, VAR_SYS_ARCDIR, arcDir );
    substVar( str, VAR_SYS_DATADIR, dataDir );
    setParm( 1, str );
  end;

  private macro execCommand( cmd : string ) : bool
    return run( comSpec,  " /C " + cmd + " >>" + logFileName ) == 0;
  end;

  // Интерфейс

  macro setArchiveDir( dir : string )
    arcDir = dir;
  end;

  macro getArchiveDir : string
    return arcDir;
  end;

  macro setDataDir( dir : string )
    dataDir = dir;
  end;

  macro getDataDir : string
    return dataDir;
  end;

  macro setLogFileName( fn : string )
    logFileName = fn;
  end;

  macro getLogFileName : string
    return logFileName;
  end;

  macro getArchiveExt : string
    return arcExt;
  end; 

  macro archiveFiles( fileNames : string, arcName : string, password : string ) : bool

    var
      switches : string = arcSwitches;

    preprocessFileNames( fileNames );

    substSysVars( switches );
    substVar( switches, VAR_FILENAMES, fileNames );
    substVar( switches, VAR_ARCNAME, arcDir + "\\" + arcName );
    substVar( switches, VAR_PASSWORD, password );
    removeUnsubstituted( switches );

    return execCommand( archiver + " " + switches );
  end;

  macro extractFiles( fileNames : string, arcName : string, password : string ) : bool

    var
      switches : string = extrSwitches;

    substSysVars( switches );
    substVar( switches, VAR_FILENAMES, fileNames );
    substVar( switches, VAR_ARCNAME, arcDir + "\\" + arcName );
    substVar( switches, VAR_PASSWORD, password );
    removeUnsubstituted( switches );

    return execCommand( archiver + " " + switches );
  end;

  macro extractAll( arcName : string, password : string ) : bool
    return extractFiles( null, arcName, password );
  end;
end;