/* Вспомогательные функции и переменные для отчетов по задолженностям (prj937) */
import deprintr, rsd;

const NullDate = date( 0, 0, 0 );
/* имеет ли операционист спецдоступ к счетам*/
private var SpecialAccess = GetSpecialAccess();
/* может ли операционист просматривать спец счета ,не имея спец. доступа*/
private var OperSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart();

private var depclnt = TClientList; /* Справочник вкладчиков */

file sccard(sccard,"spcard.def") key 0;
record sbCard("scCard.sb","sc_user.def");
private file pers(person,"bank.def") key 0;
private file subop(suboper,"bank.def") key 0;
private file sccodif(sccodif,"spcard.def") key 1;
private var {oper};

class IndebtReportData(_Branch, _RepDate, _DateFrom, _DateTo, _NeedStructFile)
 var Branch = _Branch,
     RepDate = _RepDate,
     DateFrom = _DateFrom,
     DateTo = _DateTo,
     NeedStructFile = _NeedStructFile;
 var NameBranch = "",
     RepNumber = 0;
end;


/* Поиск ФИО операциониста */
macro GetOperatorName()
  pers.Oper = {oper};
  if( GetEQ(pers) )
    return pers.Name;
  else
    return "";
  end;
end;

/* Поиск польз. названия типа */
macro GetUserCodeName( PSRef, CodType, CodCode )
  sccodif.PSRef = PSRef;
  sccodif.CodType = CodType;
  sccodif.CodCode = CodCode;
  if( GetEQ(sccodif) )
    return sccodif.CodUserCode;
  else
    return "";
  end;
end;

/* Поиск названия платы */
macro GetIndebtName( isCur, Type, Kind )
  subop.isCur = isCur;
  subop.OperType = 89;
  subop.Type = Type;
  subop.Kind = Kind;
  if( GetEQ(subop) )
    return subop.Name;
  else
    return "";
  end;
end;

/* Поиск ФИО клиента */
macro GetClientName( ClientID, SpecialAccess )
  var ClientName = "";

  if( (not SpecialAccess) and OperSpecialAccounts and SpecialAccess )
     ClientName = "XXXXXX X.X.";
  else
    if( depclnt.GetRecord( ClientID ) )
      ClientName = depclnt.CurRec.rec.name1 + " " + SubStr( depclnt.CurRec.rec.name2, 1, 1 ) + "." + SubStr( depclnt.CurRec.rec.name3, 1, 1 ) + ".";
    end;
  end;
  return ClientName;
end;

/* Поиск суммы задолженности */
macro GetIndebtSum( Branch, IndebtID )
  var cmdstr = "";
  var cmd, rs;
  var IndebtSum = 0.0;

  cmdstr  = "SELECT t_InSum FROM dindebdoc_dbt WHERE t_Branch = ? AND t_IndebtID = ? AND t_NumOpert = ?";
  cmd = RsdCommand(cmdstr);

  cmd.addParam("Branch", RsdBp_in);
  cmd.addParam("IndebtID", RsdBp_in);
  cmd.addParam("NumOpert", RsdBp_in);
  cmd.value("Branch") = Branch;
  cmd.value("IndebtID") = IndebtID;
  cmd.value("NumOpert") = 89; /* оп. Возникновение задолженности */
  cmd.execute();
  rs = RsdRecordset(cmd);

  if( rs.moveNext() )
    IndebtSum = rs.value(0);
  end;
  return IndebtSum;
end;

END;