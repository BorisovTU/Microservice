import rsd;

private class CErrHandler
  private var Errors = TArray;

  private const MessageMark = "`";

  private macro EncodeMessage(msg)
    return MessageMark + msg;
  end;
  
  private macro DecodeMessage(msg)
    return substr(msg, index(msg, MessageMark) + 1);
  end;

  private macro IsDecorated(msg)
    if (index(msg, MessageMark) != 0)
      return true;
    end;
    return false;
  end;

  macro Clear()
    Errors.Size = 0;
  end;
  
  macro AddMessage(msg_var)
    var msg = msg_var;
    var ext_msg = "";
    
    if (IsEqClass("TrslError", msg_var))
      msg = msg_var.Message;
      ext_msg = "| Модуль:" + msg_var.Module + "| строка:" + msg_var.Line;
    end;
    
    if ((msg != null) and (msg != ""))
      if (not IsDecorated(msg))
        Errors(Errors.Size) = msg + ext_msg;
      end;
    end;
  end;

  macro Error(msg)
    AddMessage(msg);
    RunError(EncodeMessage(msg));
  end;

  macro GetMessageText()
    var msg = "";
    
    var i = Errors.Size;
    while (i > 0)
      i = i - 1;
      msg = msg + "|" + Errors(i);
    end;
    
    msg = substr(msg, 2);

    return msg;
  end;
  
  macro ShowError(new_msg)
    AddMessage(new_msg);
    
    var msg = GetMessageText();
    if (msg != "")
      msgbox(msg);
    end;

    Clear();
  end;
  
end;


private var ErrHandler = CErrHandler;

macro GetMessage(code)
  var msg = "";
  
  var cmd = RsdCommand(
      "select t_Contents from dsbbank_msg "
      "where t_Number = ? "
  );
  cmd.addParam("Number", RSDBP_IN, code);
  cmd.execute();

  var rs = RsdRecordset(cmd);
  if (rs.moveNext())
    msg = rs.value(0);
  end;

  return msg;
end;

private macro ErrMessage(err_code)
  return GetMessage(err_code) + " (" + string(err_code) + ")";
end;


//----------------------------------------------------------------------------
// проверка результата выполнения функции
// в случае ошибки бросает исключение с заданным текстом сообщения err_msg
//----------------------------------------------------------------------------
macro CheckError(result, err_msg)
  var error = false;
  var err_code = 0, err_msg2 = "";

  if (ValType(result) == V_BOOL)
    error = (not result);
    if (error)
      err_code = status(err_msg2);
    end;
  elif (ValType(result) == V_INTEGER)
    error = (result != 0);
    err_code = result;
    if (error and (err_code != 0))
      err_msg2 = ErrMessage(err_code);
    end;
  end;
  
  if (error)
    if (err_msg == null)
      err_msg = "Ошибка";
    end;
    if (err_msg2 != "")
      if (err_msg != "")
        err_msg = err_msg + ". ";
      end;
      err_msg = err_msg + err_msg2;
    end;
    ErrHandler.Error(err_msg);
  end;
end;


//----------------------------------------------------------------------------
// бросить исключение с текстом сообщения msg
//----------------------------------------------------------------------------
macro DoError(msg)
  if (ValType(msg) == V_INTEGER)
    msg = ErrMessage(msg);
  end;
  
  ErrHandler.Error(msg);
end;


//----------------------------------------------------------------------------
// добавить в стек сообщение об ошибке, исключение не бросается
// обычно используется в обработчике OnError функции транзакции
//----------------------------------------------------------------------------
macro AddErrMessage(msg)
  ErrHandler.AddMessage(msg);
end;


//----------------------------------------------------------------------------
// очистить стек сообщений об ошибках
//----------------------------------------------------------------------------
macro ClearError()
  ErrHandler.Clear();
end;


//----------------------------------------------------------------------------
// показать сообщения об ошибках
//----------------------------------------------------------------------------
macro ShowError(new_msg)
  ErrHandler.ShowError(new_msg);
end;


//----------------------------------------------------------------------------
// получить сообщения об ошибках
//----------------------------------------------------------------------------
macro GetErrMessageText()
  return ErrHandler.GetMessageText();
end;


/*
macro Test1
  CheckError(false, "CheckError");
  return true;
  
OnError(err)
  ErrHandler.AddMessage(err.Message);
  return false;
end;

macro Test0
  if (not Test1())
    ErrHandler.Error();
  end;
  
OnError(err)
  ErrHandler.AddMessage(err.Message);
end;

Test0();

ErrHandler.ShowError();

OnError(err)
  ErrHandler.ShowError(err.Message);
  */
