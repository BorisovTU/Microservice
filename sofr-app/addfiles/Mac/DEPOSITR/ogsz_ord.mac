/*
  СБЕРБАНК 1.0
  R-Style SoftWare Lab.

  Печать Кассового ордера ф.53

  Лебедев С.В.
  16.12.97
*/

IMPORT deprintr, brparm;

record CapIssueDoc("ci_doc.dbt");  
file CIDoc("ci_doc.dbt");
record OGSZDocInfo("ci_doc.1");
file CIOper("ci_oper.dbt") key 0;
file CISubOp("ci_subop.dbt") key 0;
file OGSZIssue("ogsz_iss.dbt") key 0;

const
  CI_OGSZ          = 1,
  CI_LOTTERY       = 2,
  CI_CERT          = 3,
  CI_COIN          = 4,
  CI_INGOT         = 5,
  CI_ORVVZ         = 6,
  CI_MISC          = 10,

  O_OGSZ_SELL      = 1,
  O_OGSZ_BUY       = 2,
  O_OGSZ_PAYWARR   = 3,
  O_OGSZ_KEEP      = 4,
  O_OGSZ_RETURN    = 5,
  O_OGSZ_SELL_ACC  = 6,

  O_LOTTERY_SELL   = 1,
  O_LOTTERY_PAYWIN = 2,
  O_LOTTERY_BUY    = 3,
  O_LOTTERY_KEEP   = 4,
  O_LOTTERY_RETURN = 5,
  O_LOTTERY_SELL_ACC  = 6,

  O_CERT_SELL      = 1,
  O_CERT_PAY       = 2,
  O_CERT_PAYALN    = 3,
  O_CERT_KEEP      = 4,
  O_CERT_RETURN    = 5,
  O_CERT_SELL_ACC  = 6,

  O_MISC_SELL      = 1,
  O_MISC_BUY       = 2,
  O_MISC_KEEP      = 3,
  O_MISC_RETURN    = 4,
  O_MISC_SELL_ACC  = 6,

  O_INGOT_SELL     = 1,
  O_INGOT_BUY      = 2,
  O_INGOT_KEEP     = 3,
  O_INGOT_RETURN   = 4,
  O_INGOT_SELL_ACC = 5;

const
  SUM_BALCOST      = 1,
  SUM_WARRYIELD    = 2;

macro CIOperIsCreditM(Type, Number)

  if(Type == CI_OGSZ)
    if((Number == O_OGSZ_SELL) or (Number == O_OGSZ_RETURN))
      return true; 
    else
      return false;
    end;
  elif((Type == CI_LOTTERY) or (Type == CI_ORVVZ))
    if((Number == O_LOTTERY_SELL) or (Number == O_LOTTERY_RETURN))
      return true; 
    else
      return false;
    end;
  elif(Type == CI_CERT)
    if((Number == O_CERT_SELL) or (Number == O_CERT_RETURN))
      return true; 
    else
      return false;
    end;
  elif(Type == CI_MISC)
    if((Number == O_MISC_SELL) or (Number == O_MISC_RETURN))
      return true; 
    else
      return false;
    end;
  end;
  return false;
end;

/*******************************************************************
			Печать ордера
*******************************************************************/
macro PrintOneOrder(WhichSum)

  var    str;
  var    StrTmp,StrTmp2;
  var    d,m,y;
  var    CurStr   = 0;
  const  StrCount = 4;
  var    Summa    = $0.0;
  var    IsCredit;
  var    RubSum,CopSum;
  var
    BranchStrNum = "",
    DepartNum = "",
    DepartName = "";

  GetBranchParams( NumFNCash, BranchStrNum, DepartNum, DepartName );

  if(WhichSum == SUM_WARRYIELD)
    Summa = OGSZDocInfo.WarrantYield;
  else
    Summa = Money(OGSZIssue.BalCost * OGSZDocInfo.NumItems);
  end;
  
  IsCredit = CIOperIsCreditM(CIDoc.Type, CIDoc.Operation);

  DateSplit(CIDoc.Date,d,m,y);
  if (not IsCredit)
    str = "Учреждение                                 Ф.No54";
  else
    str = "Учреждение                                 Ф.No53";
  end;
  [ #](str);
  str = "Сбербанка No " + BranchStrNum;
  [ #](str);
  str = "";
  [ #](str);
  str = "                   КАССОВЫЙ  ОРДЕР";
  [ #](str);
  str = "                \"";
  StrTmp = String(d);
  while (StrLen(StrTmp) < 2)
    StrTmp = "0" + StrTmp;
  end;
  str = str + StrTmp + "\" ";
  if (m == 1)
    StrTmp = "января";
  elif (m == 2)
    StrTmp = "февраля";
  elif (m == 3)
    StrTmp = "марта";
  elif (m == 4)
    StrTmp = "апреля";
  elif (m == 5)
    StrTmp = "мая";
  elif (m == 6)
    StrTmp = "июня";
  elif (m == 7)
    StrTmp = "июля";
  elif (m == 8)
    StrTmp = "августа";
  elif (m == 9)
    StrTmp = "сентября";
  elif (m == 10)
    StrTmp = "октября";
  elif (m == 11)
    StrTmp = "ноября";
  elif (m == 12)
    StrTmp = "декабря";
  else
    StrTmp = "???";
  end;
  StrTmp = "_" + StrTmp;
  while (StrLen(StrTmp) < 12)
    StrTmp = StrTmp + "_";
  end;
  str = str + StrTmp + " " + String(y) + " г.";
  [ #](str);
  if (not IsCredit)
    str = "     ВЫДАТЬ наличными     │    ПРИНЯТЬ ценности";
  else
    str = "    ПРИНЯТЬ наличными     │     ВЫДАТЬ ценности";
  end;
  [ #](str);

  StrTmp = Trim(String(Summa:16:2));
  StrTmp2 = "_" + SubStr(StrTmp,Index(StrTmp,".") + 1);
  while (StrLen(StrTmp2) < 2)
    StrTmp2 = "0" + StrTmp2;
  end;
  while (StrLen(StrTmp2) < 4)
    StrTmp2 = StrTmp2 + "_";
  end;
  StrTmp = "_" + SubStr(StrTmp,1,Index(StrTmp,".") - 1);
  while (StrLen(StrTmp) < 11)
    StrTmp = StrTmp + "_";
  end;
  str = " руб." + StrTmp + "коп." + StrTmp2 + "  │ руб.___________коп.___";
  [ #](str);
  str = "";
  [ #](str);
  str = "_" + RubToStr(Summa);
  [ ##################################################](str:w);
  str = "                   (сумма прописью)";
  [ #](str);
  /* Напечать операцию с подоперацией */

  CIOper.Type = CIDoc.Type;
  CIOper.Number = CIDoc.Operation;
  if (GetEQ(CIOper))
    str = "_" + CIOper.Name;
  else
    str = "_";
  end;
  if (CIDoc.SubOp > 0)
    CISubOp.Type = CIDoc.Type;
    CISubOp.Operation = CIDoc.Operation;
    CISubOp.SubOpNum = CIDoc.SubOp;
    if (GetEQ(CISubOp))
      str = str + "_(" + CISubOp.Name + ")";
    end;
  end;
  while (StrLen(str) < 50)
    str = str + "_";
  end;
  [ #](str);
  str = "";
  /* Основание */
  str = str + "ОГСЗ серия " + OGSZDocInfo.Series + ", номинал " + 
    String(OGSZDocInfo.Par);
  while (StrLen(str) < 50)
    str = str + "_";
  end;
  [ #](str);
  CurStr = CurStr + 1;
  while (CurStr < StrCount)
    str = "__________________________________________________";
    [ #](str);
    CurStr = CurStr + 1;
  end;
  str = "Контролер__________________ Кассир________________";
  [ #](str);
  if(IsCredit)
    str = "Ценности";
  else
    str = "Деньги";
  end;
  [ #](str);
  str = "на сумму__________________________________________";
  [ #](str);
  str = "                        (прописью)";
  [ #](str);
  str = "";
  [ #](str);
  str = "          Получил_________________________________";
  [ #](str);
  str = "         Дебет счет   │  Кредит счета  │ Расход по";
  [ #](str);
  str = "                      │                │   счету";
  [ #](str);
  str = "                      │                │";
  [ #](str);
  str = "         No__________ │ No____________ │ No_______";
  [ #](str);
  str = "";
  [ #](str);
  str = "                   Гл.(ст.) бухгалтер ____________";
  [ #](str);
end;

macro FindOGSZIssue

  OGSZIssue.Type = CIDoc.Type;
  OGSZIssue.IssueID = CIDoc.Issue;
  if(not GetEQ(OGSZIssue))
    MsgBox("Ошибка при печати ордера: выпуск ОГСЗ не найден");
    Exit;
  end;
end;
 
macro PrintOrd(DocAddr)

  SetBuff(CapIssueDoc, DocAddr);     
  Copy(CIDoc, CapIssueDoc);
  SetRecordAddr(OGSZDocInfo,CIDoc,0,FldOffset(CIDoc,"Info"),true);

  if ( CIDoc.Operation == O_OGSZ_SELL_ACC )
    return;
  end;

  FindOGSZIssue;

  if((CIDoc.Operation == O_OGSZ_KEEP) 
    or ((CIDoc.Operation == O_OGSZ_PAYWARR) 
    and ( OGSZDocInfo.WarrantNumber != OGSZIssue.NumWarrants ) ) )
    return;
  end;
  PrintOneOrder(SUM_BALCOST);
  if(CIDoc.Operation != O_OGSZ_RETURN)
    PrintLn;
    PrintLn;
    PrintOneOrder(SUM_WARRYIELD);
  end;
end;

end;
