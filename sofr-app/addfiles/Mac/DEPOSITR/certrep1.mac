import BrParm;

record CertDocInfo("ci_doc.3");
record CertRecInfo("ci_rec.3");

private file DepClient( "depclnt.dbt" ) key 0;

const
  CI_CERT=3;

const
  O_CERT_SELL=1,
  O_CERT_PAY=2,
  O_CERT_PAYALN=3,
  O_CERT_SELL_ACC=6;

var
  C_TotNumItems=0,
  C_TotYield=$0,
  C_TotComm=$0,
  C_TotSum=$0,
  C_TotPar=$0,
  C_CopyCode;

private var
  BranchStrNum = "",
  DepartNum = "",
  DepartName = "";

private macro GetClientInfo

  DepClient.CodClient = CapIssueDoc.ClientCode;
  if ( GetEQ( DepClient ) )
    return DepClient.Sname + " " + DepClient.Name + " " + DepClient.Pname + 
   "\nДокумент: " +  MakePersIDStr( DepClient ) + 
   "\nАдрес: " + DepClient.Adress;
  else
    return "";
  end;
end;

private macro GetCertInfo

  if ( ( CertDocInfo.Series != "" ) and ( CertDocInfo.Number != 0 ) )
    return "№ " + CertDocInfo.Series + "-" + CertDocInfo.Number;
  elif ( CertDocInfo.Number != 0 )
    return "№ " + CertDocInfo.Number;
  else
    return "";
  end;
end;

macro C_Find;

  KeyNum( Cert, 0 );
  Cert.Type=CI_CERT;
  Cert.Issue=CapIssueRec.Issue;
  GetEQ(Cert);
  KeyNum( Cert, 1 );
end;

private macro PrintOrd63

  array
    A_Op;

  var    str;
  var    StrTmp,StrTmp2;
  var    CurStr   = 0;
  var    Summa    = $0L;
  var    RubSum,CopSum;
  var    i;

  SetRecordAddr(CertDocInfo,CapIssueDoc,0,
    FldOffset(CapIssueDoc,"Info"),true);

  StrSplit( "Налог на оплату сертификата " + Cert.Desc 
    + " " + GetCertInfo + "\n" 
    + GetClientInfo, A_Op, 70, 70, 6 );

  Summa = CertDocInfo.Tax;

  if (Summa == 0)
    return;
  end;

  [
                                                                   Ф. № 63

                               В___________________________________________
                                 наименование и № учреждения Сбербанка РФ
                               Адрес_______________________________________
       _ _ _ _ _ _ _ _ _ _ _    ___________________________________________
         место для штампа       (заполняется при межбанковских операциях)
            учреждения

                                   О Р Д Е Р                         

  ];

  [                             #

  ]
  (CapIssueDoc.Date:m);


  StrTmp = Trim(String(Summa:16:2));
  StrTmp2 = "_" + SubStr(StrTmp,Index(StrTmp,".") + 1);
  while (StrLen(StrTmp2) < 2)
    StrTmp2 = "0" + StrTmp2;
  end;
  while (StrLen(StrTmp2) < 4)
    StrTmp2 = StrTmp2 + "_";
  end;
  StrTmp = "_" + SubStr(StrTmp,1,Index(StrTmp,".") - 1);
  while (StrLen(StrTmp) < 11)
    StrTmp = StrTmp + "_";
  end;
  str = "На сумму:   " + StrTmp + " руб." +  StrTmp2 + "коп.";
  [    #](str);
  str = "";
  [ #](str);
  str = "_" + RubToStr(Summa);
  [    #####################################################################](str:w);
  str = "                            (сумма прописью)";
  [ #](str);

  str = "\n";
  [ #](str);

  i = 0;
  while ( i < ASize( A_Op ) )
    str = A_Op( i );
    while (StrLen(str) < 70)
      str = str + "_";
    end;
    [ #](str);
    i = i + 1;
  end;

  [
      Управляющий отделением:            Главный бухгалтер:
      _____________________________________________________________________
            Начальная проводка:         |        Ответная проводка:
      ---------------------------------------------------------------------
      Дебет сч.  № _____________________| Дебет  сч. № ____________________
      Кредит сч. № _____________________| Кредит сч. № ____________________
                                        |           '  ' ___________ ____ г.
      Гл.бухгалтер_____________________ | Гл.бухгалтер_____________________

                                                                               ];
end;

macro OutOrdersForRec

  var
    RecFound;

  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;

  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    if ( CheckCIDoc )
      PrintOrd63;
    end;
    RecFound=Next(CapIssueDoc);
  end;
end;

macro OutOrdersForOp( Op )

  var
    RecFound, 
    SK = KeyNum( CapIssueDoc, 0 );

  ClearRecord( CapIssueDoc );
  CapIssueDoc.Branch = Branch;
  CapIssueDoc.IsCur = 0;
  CapIssueDoc.Type = CI_CERT;
  RecFound = GetGE( CapIssueDoc );
  while ( RecFound
    and ( CapIssueDoc.Branch == Branch )
    and ( CapIssueDoc.IsCur == 0 ) 
    and ( CapIssueDoc.Type == CI_CERT ) )
    if ( ( CapIssueDoc.Brigade == OperBrigade ) and CheckCIDoc )
      C_Find;
      PrintOrd63;
    end;
    RecFound = Next( CapIssueDoc );
  end;

  KeyNum( CapIssueDoc, SK );
end;
  
macro C_OutHead;
  
  [
                                                                 Филиал №
                                                                 отделения
                                                                 Сбербанка РФ
  
  
           Отчет по операциям по сберегательным сертификатам за ##########
  
  ]
  (
    {curdate}
  );
end;
  
macro C_OutSellHead;
  
  [ ];
  [Продажа сертификатов];
  [ ];
  [+----------+-----------+-----------------+--------+-------------------+];
  [| Год вып. |  Номинал  | Срок обр. (лет) | Колич. |     С у м м а     |];
  [+----------+-----------+-----------------+--------+-------------------+];
end; 
  
macro C_OutPayHead;
  
  [ ];
  [Оплата сертификатов];
  [ ];
  [+----------+-----------+-----------------+--------+-------------------+------------+-------------------+];
  [| Год вып. |  Номинал  | Срок обр. (лет) | Колич. |     Д о х о д     |  Комиссия  |     С у м м а     |];
  [+----------+-----------+-----------------+--------+-------------------+------------+-------------------+];
end; 

macro C_GetNumItems

  var
    RecFound,
    NumItems=0;

  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;

  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    if ( CheckCIDoc )
      NumItems=NumItems+1;
    end;
    RecFound=Next(CapIssueDoc);
  end;
  return NumItems;
end;

macro C_OutSellLine;

  SetRecordAddr(CertRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [|   ####   | ######### |            #### | ###### | ################# |]
  (
    CertRecInfo.Year,
    CertRecInfo.Par,
    CertRecInfo.Duration,
    C_GetNumItems, 
    CapIssueRec.Sum
  );
end;

macro C_OutPayLine;

  SetRecordAddr(CertRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [|   ####   | ######### |            #### | ###### | ################# | ########## | ################# |]
  (
    CertRecInfo.Year,
    CertRecInfo.Par,
    CertRecInfo.Duration,
    C_GetNumItems, 
    CertRecInfo.YieldSum,
    CertRecInfo.CommissionSum,
    CapIssueRec.Sum
  );
end;

macro C_OutSellTotals;

  [+----------+-----------+-----------------+--------+-------------------+];
  [|  И т о г о :                           | ###### | ################# |]
  (
    C_TotNumItems,
    C_TotSum
  );
end;

macro C_OutPayTotals;

  [+----------+-----------+-----------------+--------+-------------------+------------+-------------------+];
  [|  И т о г о :                           | ###### | ################# | ########## | ################# |]
  (
    C_TotNumItems,
    C_TotYield,
    C_TotComm,
    C_TotSum
  );
end;

macro C_SellReport

  var
    RecFound;

  C_TotNumItems=0;
  C_TotSum=$0;

  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_CERT;

  C_OutSellHead;
  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_CERT))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Operation==O_CERT_SELL)
      and (CapIssueRec.Brigade==OperBrigade))
      C_OutSellLine; 
      SetRecordAddr(CertRecInfo,CapIssueRec,0,
        FldOffset(CapIssueRec,"Info"),true);
      C_TotNumItems=C_TotNumItems+C_GetNumItems;
      C_TotSum=C_TotSum+CapIssueRec.Sum;
    end;
    RecFound=Next(CapIssueRec);
  end;
  C_OutSellTotals;
end;
    
macro C_PayReport

  var
    RecFound;

  C_TotNumItems=0;
  C_TotComm=$0;
  C_TotYield=$0;
  C_TotSum=$0;

  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_CERT;

  C_OutPayHead;
  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_CERT))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Brigade==OperBrigade)
      and ((CapIssueRec.Operation==O_CERT_PAY) 
         or (CapIssueRec.Operation==O_CERT_PAYALN)))
      C_OutPayLine; 
      SetRecordAddr(CertRecInfo,CapIssueRec,0,
        FldOffset(CapIssueRec,"Info"),true);
      C_TotNumItems=C_TotNumItems+C_GetNumItems;
      C_TotComm=C_TotComm+CertRecInfo.CommissionSum;
      C_TotYield=C_TotYield+CertRecInfo.YieldSum;
      C_TotSum=C_TotSum+CapIssueRec.Sum;
    end;
    RecFound=Next(CapIssueRec);
  end;
  C_OutPayTotals;
end;
    
macro CertOperReport;

  C_OutHead;         
  C_SellReport;         
  [ ];
  C_PayReport;
end;

macro C_OutSellRecHead;

  GetBranchParams( Branch, BranchStrNum, DepartNum, DepartName );
  SetRecordAddr(CertRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [                                                                                Ф. № 56-#
     ##################################                                                                          
                                                                         Филиал № ##########
     Сберегательного банка № ##########         

                                      
                                       КОНТРОЛЬНАЯ  ВЕДОМОСТЬ

                            на продажу и покупку облигаций Российского
                                  внутреннего выигрышного займа
                                             1992 года
                             ########################################
                                           за ##########


     Смена № #####

  ]
  (
    DepartName,
    C_CopyCode,
    BranchStrNum,
    DepartNum,
    "(Сберегательные сертификаты "+Cert.Year+" года)":c,
    {curdate},
    OperBrigade
  );
  [+-----------+--------------------------------------+-----------------+-------------------+];
  [| Номерной  |             П р о д а н о            |  К у п л е н о  |   П о д п и с и   |];
  [|  талон,   |-------------------+------------------|-----------------|---------+---------|];
  [| выданный  | Номинальная стои- | Получено за      | Получено за     |         |         |];
  [| клиенту   | мость проданных   | проданные        | купленные       |контролер| кассир  |];
  [|           | облигаций         | облигации        | облигации       |         |         |];
  [+-----------+-------------------+------------------+-----------------+---------+---------+];
end;

macro C_OutSellRecLine;
 
  SetRecordAddr(CertDocInfo,CapIssueDoc,0,
    FldOffset(CapIssueDoc,"Info"),true);
  [|   ####    | ################# | ################ |                 |         |         |]
  (
    CapIssueDoc.DocNumber,
    CertDocInfo.Par,
    CapIssueDoc.Sum
  );
  C_TotPar=C_TotPar+CertDocInfo.Par;
  C_TotSum=C_TotSum+CapIssueDoc.Sum;
end;

macro C_CertSellRecTotals;

  var
    SellCredAcc,
    SellDebAcc;
 
  GetBookAcc(O_CERT_SELL, SellDebAcc, SellCredAcc);
  SetRecordAddr(CertDocInfo,CapIssueDoc,0,
    FldOffset(CapIssueDoc,"Info"),true);
  [+-----------+-------------------+------------------+-----------------+---------+---------+];
  [| Всего     |                   | ################ |                 |         |         |]
  (
    C_TotSum
  );
  [ ];
  [                                                    Д-т сч. ];
  [                                                    _________________ ];
  [ ];
  [                                 Д-т сч. ];
  [                                 ########################## ]
  (SellDebAcc);
  [ ];
  [             Расход по сч.                          К-т сч. ];
  [             ___________________                    _________________ ];
  [ ];
  [                                 К-т сч. ];
  [                                 ########################## ]
  (SellCredAcc);
  [ ];
  [                                                    Приход по сч.];
  [                                                    _________________ ];
end;

macro C_OutPayRecHead;

  var
    DebAcc,
    CredAcc;

  GetBranchParams( Branch, BranchStrNum, DepartNum, DepartName );
  GetBookAcc(CapIssueRec.Operation, DebAcc, CredAcc);
  SetRecordAddr(CertRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [
     ###################################                                                                       Ф. № 92
                                                                                                    Филиал № #########
     Сберегательного банка № ############         


  
                                                 КОНТРОЛЬНАЯ  ВЕДОМОСТЬ                    Отметки бухгалтерии 

                                                 на оплату ценных бумаг              Дебет сч.  ######################
                                        #######################################      Кредит сч. ######################
                                                     за ##########                   Приход сч. ______________________
                                                                                             
                                                                                     

  ]
  (
    DepartName,
    BranchStrNum,
    DepartNum,
    DebAcc,
    "(Сберегательные сертификаты "+Cert.Year+" года)":c,
    CredAcc,
    {curdate}
  );
  [+------------------------------+-------+------------------+------+----------------+------------+-------------------+
   |                              |       |                  |      |                |            |                   |
   |    Название ценной бумаги    | Серия |     Номер(а)     |Кол-во|     Доход      |  Комиссия  |  Итого выплатить  |
   |                              |       |                  |      |                |            |                   |
   +------------------------------+-------+------------------+------+----------------+------------+-------------------+];
end;

macro C_OutPayRecLine;

  var
    Numbers;

  SetRecordAddr(CertDocInfo,CapIssueDoc,0,
    FldOffset(CapIssueDoc,"Info"),true);

  if ( Abs( CertDocInfo.NumItems ) == 1 )
    Numbers = String( CertDocInfo.Number );
  else
    Numbers = "с " + String( CertDocInfo.Number ) + " по " + String( CertDocInfo.Number + Abs( CertDocInfo.NumItems ) - 1 );
  end;

  [| ############################ | ##### | ################ | #### | ############## | ########## | ################# |]
  (
    Cert.Desc:w,
    CertDocInfo.Series,
    Numbers:w,
    CertDocInfo.NumItems,
    CertDocInfo.Yield,
    CertDocInfo.Commission:z,
    CapIssueDoc.Sum
  );
  C_TotNumItems = C_TotNumItems + CertDocInfo.NumItems;
  C_TotSum=C_TotSum+CapIssueDoc.Sum;
  C_TotYield=C_TotYield+CertDocInfo.Yield;
  C_TotComm=C_TotComm+CertDocInfo.Commission;
end;

macro C_CertPayRecTotals;
 
  SetRecordAddr(CertDocInfo,CapIssueDoc,0,
    FldOffset(CapIssueDoc,"Info"),true);
  [+------------------------------+-------+------------------+------+----------------+------------+-------------------+];
  [| В с е г о                    |       |                  | #### | ############## | ########## | ################# |]
  (
    C_TotNumItems,
    C_TotYield,
    C_TotComm,
    C_TotSum
  );
end;

macro C_OutRecTail;

  [



                             Ст. бухгалтер ___________                                
  ];
end;

macro C_CertSellRecBody;

  var
    RecFound;

  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;

  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    C_OutSellRecLine;
    RecFound=Next(CapIssueDoc);
  end;
end;

macro C_CertSellRecCopy;
 
  C_Find;
  if ( Cert.Year == 1999 )
    return;
  end;
  C_OutSellRecHead; 
  C_CertSellRecBody;
  C_CertSellRecTotals;
  C_OutRecTail;
end;

macro C_CertSellRec

  C_CopyCode = "A";
  C_CertSellRecCopy;
  C_CopyCode = "Б";
  C_CertSellRecCopy;
end;

macro C_CertPayRecBody;

  var
    RecFound;

  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;
  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    C_OutPayRecLine;
    RecFound=Next(CapIssueDoc);
  end;
end;

macro C_CertPayRecCopy;
 
  C_TotNumItems = 0;
  C_TotPar=$0;
  C_TotSum=$0;
  C_TotYield=$0;
  C_Find;
  if ( Cert.Year == 1999 )
    return;
  end;
  C_OutPayRecHead; 
  C_CertPayRecBody;
  C_CertPayRecTotals;
  C_OutRecTail;
end;

macro C_CertPayRec

  C_CopyCode = "A";
  C_CertPayRecCopy;
/*
  C_CopyCode = "Б";
  C_CertPayRecCopy;
*/
  OutOrdersForRec;
end;

macro CertRec(ApplicationKind,ApplicationKey);
      
  KeyNum(CapIssueRec,2);                                        
  CapIssueRec.ApplicationKind=ApplicationKind;
  CapIssueRec.ApplicationKey=ApplicationKey;
  if(not GetEQ(CapIssueRec))
    MsgBox("Ошибка при печати ведомости");
    return;
  end;
  if((CapIssueRec.Operation==O_CERT_SELL) or (CapIssueRec.Operation==O_CERT_SELL_ACC))
/*    C_CertSellRec; */
    Exit( 1 );
  elif((CapIssueRec.Operation==O_CERT_PAY) 
    or (CapIssueRec.Operation==O_CERT_PAYALN))
    C_CertPayRec;
  end;
end;

macro OutRecsForOp( Op )

  var
    Pos = 0,
    PrevYear = 0,
    HeadPrinted = false,
    RecFound;

  if ( ( Op == O_CERT_SELL ) or ( Op == O_CERT_SELL_ACC ) )
    C_CopyCode = "А";
    Rewind( Cert );
    while ( Next( Cert ) )
      if ( Cert.Year != PrevYear )
        if ( HeadPrinted )
          if ( ( Op == O_CERT_SELL ) or ( Op == O_CERT_SELL_ACC ) )
            C_CertSellRecTotals;
          else
            C_CertPayRecTotals; 
          end;
          C_OutRecTail;
          if ( C_CopyCode == "А" )
            if ( Pos )
              GetDirect( Cert, Pos );
            end;
            C_CopyCode = "Б";
          else
            Pos = GetPos( Cert );
            C_CopyCode = "А";
          end;
        else
          Pos = GetPos( Cert );
          C_CopyCode = "А";
        end;
        HeadPrinted = false;
        PrevYear = Cert.Year;
        C_TotNumItems = 0;
        C_TotPar=$0;
        C_TotSum=$0;
        C_TotYield=$0;
      end;
  
      ClearRecord(CapIssueRec);
      CapIssueRec.Branch=Branch;
      CapIssueRec.IsCur=0;
      CapIssueRec.Type=CI_CERT;
      CapIssueRec.Issue = Cert.Issue;
    
      RecFound=GetGE(CapIssueRec);
      while(RecFound
        and (CapIssueRec.Branch==Branch)
        and (CapIssueRec.IsCur==0)
        and (CapIssueRec.Type==CI_CERT)
        and (CapIssueRec.Issue==Cert.Issue))
        if((CapIssueRec.Date=={curdate})
          and (CapIssueRec.Brigade==OperBrigade))
          if(CapIssueRec.Operation==Op) 
            if ( ( Op == O_CERT_SELL ) or ( Op == O_CERT_SELL_ACC ) )
              if ( not HeadPrinted )
                C_OutSellRecHead;
                HeadPrinted = true;
              end;
              C_CertSellRecBody;
            else
              if ( Cert.Year != 1999 ) 
                if ( not HeadPrinted )
                  C_OutPayRecHead;
                  HeadPrinted = true;
                end;
                C_CertPayRecBody;
              end;
            end;
          end;
        end;
        RecFound=Next(CapIssueRec);
      end;
    end;
  else
    C_CopyCode = "А";
    Rewind( Cert );
    while ( Next( Cert ) )
      if ( Cert.Year != PrevYear )
        if ( HeadPrinted )
          if ( ( Op == O_CERT_SELL ) or ( Op == O_CERT_SELL_ACC ) )
            C_CertSellRecTotals;
          else
            C_CertPayRecTotals; 
          end;
          C_OutRecTail;
        end;
        HeadPrinted = false;
        PrevYear = Cert.Year;
        C_TotNumItems = 0;
        C_TotPar=$0;
        C_TotSum=$0;
        C_TotYield=$0;
      end;
  
      ClearRecord(CapIssueRec);
      CapIssueRec.Branch=Branch;
      CapIssueRec.IsCur=0;
      CapIssueRec.Type=CI_CERT;
      CapIssueRec.Issue = Cert.Issue;
    
      RecFound=GetGE(CapIssueRec);
      while(RecFound
        and (CapIssueRec.Branch==Branch)
        and (CapIssueRec.IsCur==0)
        and (CapIssueRec.Type==CI_CERT)
        and (CapIssueRec.Issue==Cert.Issue))
        if((CapIssueRec.Date=={curdate})
          and (CapIssueRec.Brigade==OperBrigade))
          if(CapIssueRec.Operation==Op) 
            if ( ( Op == O_CERT_SELL ) or ( Op == O_CERT_SELL_ACC ) )
              if ( not HeadPrinted )
                C_OutSellRecHead;
                HeadPrinted = true;
              end;
              C_CertSellRecBody;
            else
              if ( Cert.Year != 1999 ) 
                if ( not HeadPrinted )
                  C_OutPayRecHead;
                  HeadPrinted = true;
                end;
                C_CertPayRecBody;
              end;
            end;
          end;
        end;
        RecFound=Next(CapIssueRec);
      end;
    end;
  end;

  if ( HeadPrinted )
    if ( ( Op == O_CERT_SELL ) or ( Op == O_CERT_SELL_ACC ) )
      C_CertSellRecTotals;
    else
      C_CertPayRecTotals; 
    end;
    C_OutRecTail;
  end;
end;

macro CertReport;

/*  
  OutRecsForOp( O_CERT_SELL );
  OutRecsForOp( O_CERT_SELL_ACC );
*/
  OutRecsForOp( O_CERT_PAY );
  OutOrdersForOp( O_CERT_PAY );
  OutRecsForOp( O_CERT_PAYALN );
  OutOrdersForOp( O_CERT_PAYALN );
end;               
