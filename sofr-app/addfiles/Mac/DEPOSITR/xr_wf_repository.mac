/*
  $Name:         xr_wf_repository.mac
  $Module:       АРМ бухгалтера
  $Description:  Сервисы реестра приложений BPMN
*/

import rsd, bankInter;

private class TListElem(_id, _name)
  var id = _id;
  var name = _name;
end;

private class TFormalParameter
  var id;          
  var name;
  var description;
  var mode;
  var dataType;
  var isArray;
  var initialValue;
  var length;
  var readOnly;
  var domain;
  var domainParams;
end;

private class TApplication
  var id;          
  var name;
  var description;
  var formalParameters;
  var isUIForm;
end;

private class TParameterDomain
  var dataType;
  var domainName;
  var enumValues;
end;

private macro toBoolean(v)
  return (valType(v) == V_STRING) and (v != strFor(0))
end;

private macro toInteger(v)
  if (valType(v) == V_STRING) 
    return int(v);
  else
    return 0;
  end;
end;

macro packageList
  var rv = TArray;
  var cmd = RsdCommand("select t_externalId, t_name from dwf_package_dbt order by t_externalId");
  cmd.nullConversion = true;
  cmd.execute;
  var rs = RsdRecordset(cmd);
  while (rs.moveNext) 
    rv(rv.size) = TListElem(rs.value("t_externalId"), rs.value("t_name"));
  end;
  return rv;
end;

macro retrievePackage(externalId : String)
  const CHUNK_SIZE = 10000;
  var cmd = RsdCommand("select 0 from dwf_package_dbt where t_externalId = ?");
  cmd.addParam("p_externalId", RSDBP_IN, externalId);
  cmd.nullConversion = true;
  cmd.execute;
  var rs = RsdRecordset(cmd);
  if (rs.moveNext) 
    var stream = TStream("..\\XML\\" + externalId + ".xpdl");
    var source = "";
    var chunk;
    while(stream.read(chunk, V_STRING, CHUNK_SIZE))
      source = source + chunk;
    end;
    return RslBinaryData(source).asBase64;
  end;
end;

macro storePackage(externalId, name, description, author, version, source)
  var stream = TStream("..\\XML\\" + externalId + ".xpdl", "C");
  if (not stream.write(RslBinaryData(source, true).asASCIIZ))
    runError("Ошибка записи XPDL-файла");
  end;
  var cmdSave;
  var isUpdating = false;
  var cmd = RsdCommand("select 0 from dwf_package_dbt where t_externalId = ?");
  cmd.nullConversion = true;
  cmd.addParam("p_externalId", RSDBP_IN, externalId);
  cmd.execute;
  var rs = RsdRecordset(cmd);
  if (rs.moveNext) 
    cmdSave = RsdCommand("update dwf_package_dbt set t_name = ?, t_description = ?, t_author = ?, t_version = ? where t_externalId = ?");
    isUpdating = true;
  else
    cmdSave = RsdCommand("insert into dwf_package_dbt(t_externalId, t_name, t_description, t_author, t_version) values (?, ?, ?, ?, ?)");
  end;
  cmdSave.nullConversion = true;
  if (not isUpdating)
    cmdSave.addParam("p_externalId", RSDBP_IN, externalId);
  end;
  cmdSave.addParam("p_name", RSDBP_IN, name);
  cmdSave.addParam("p_description", RSDBP_IN, description);
  cmdSave.addParam("p_author", RSDBP_IN, author);
  cmdSave.addParam("p_version", RSDBP_IN, version);
  if (isUpdating)
    cmdSave.addParam("p_externalId", RSDBP_IN, externalId);
  end;
  cmdSave.execute;
end;

macro applicationList
  var rv = TArray;
  var cmd = RsdCommand("select t_externalId, t_name from dwf_application_dbt order by t_externalId");
  cmd.nullConversion = true;
  cmd.execute;
  var rs = RsdRecordset(cmd);
  while (rs.moveNext) 
    rv(rv.size) = TListElem(rs.value("t_externalId"), rs.value("t_name"));
  end;
  return rv;
end;

macro retrieveApplication(externalId : String)
  macro getFormalParams(appId)
    var rv = TArray; 
    var cmd = RsdCommand("select * from dwf_appparam_dbt where t_applicationId = ? order by t_number");
    cmd.nullConversion = true;
    cmd.addParam("p_applicationId", RSDBP_IN, appId);
    cmd.execute;
    var rs = RsdRecordset(cmd);
    while (rs.moveNext)
      var fp = TFormalParameter;
      fp.id = rs.value("t_externalId");
      fp.name = rs.value("t_name");
      fp.description = rs.value("t_description");
      fp.mode = rs.value("t_mode");
      fp.dataType = rs.value("t_dataType");
      fp.isArray = toBoolean(rs.value("t_isArray"));     
      fp.initialValue = rs.value("t_initialValue");
      fp.length = toInteger(rs.value("t_length"));      
      fp.readOnly = toBoolean(rs.value("t_readOnly"));
      fp.domain = rs.value("t_domain");
      fp.domainParams = rs.value("t_domainParams");
      rv(rv.size) = fp;
    end;
    return rv;
  end;

  var cmd = RsdCommand("select * from dwf_application_dbt where t_externalId = ?");
  cmd.nullConversion = true;
  cmd.addParam("p_externalId", RSDBP_IN, externalId);
  cmd.execute;
  var rs = RsdRecordset(cmd);
  if (rs.moveNext)
    var rv = TApplication;
    rv.id = rs.value("t_externalId");
    rv.name = rs.value("t_name");
    rv.isUIForm = toBoolean(rs.value("t_isUIForm"));
    rv.description = rs.value("t_description");
    rv.formalParameters = getFormalParams(rs.value("t_id"));
    return rv;
  end; 
end;

macro getEnumValues(enumId)
  var cmdSql = RsdCommand("select t_sql from dwf_enum_dbt where t_id = ?");
  cmdSql.nullConversion = true;
  cmdSql.addParam("p_id", RSDBP_IN, enumId);
  cmdSql.execute;
  var rsSql = RsdRecordset(cmdSql);
  if (rsSql.moveNext)
    var cmdItems = RsdCommand(rsSql.value("t_sql"));
    cmdItems.nullConversion = true;
    cmdItems.execute;
    var rsItems = RsdRecordset(cmdItems);
    var items = TArray;
    while (rsItems.moveNext)
      items(items.size) = TListElem(string(rsItems.value(0)), string(rsItems.value(1)));
    end;
    return items;
  end; 
end;

macro getEnums
  var rv = TArray;
  var cmd = RsdCommand("select t_id, t_name from dwf_enum_dbt order by t_id");
  cmd.nullConversion = true;
  cmd.execute;
  var rs = RsdRecordset(cmd);
  while (rs.moveNext) 
    rv(rv.size) = TListElem(rs.value("t_id"), rs.value("t_name"));
  end;
  return rv;
end;

/*
var a = retrieveApplication("CalculateCommission");

var domains = getParameterDomains(a.id);
printprops(domains(0));

var ev;
for(ev, domains(0).enumValues)
  println(ev.id, " ", ev.name);
end;
*/
