/*
  Макрос по инициализации номеров валютных счетов по видам вкладов
  "Пополняемый" и "Особый"
  Автор: Зубов В. 15-04-98

  Структура номера счета такова
  ┌────┬───────────────────────────────────────────────────┐
  │ С  │Значение поля                                      │
  ├────┼───────────────────────────────────────────────────┤
  │  2 │Индекс террбанка                                   │
  ├────┼───────────────────────────────────────────────────┤
  │  5 │Балансовый счет                                    │
  ├────┼───────────────────────────────────────────────────┤
  │  3 │Код валюты                                         │
  ├────┼───────────────────────────────────────────────────┤
  │  2 │Код вида вклада                                    │
  │    │29 - Пополняемый,30 - особый                       │
  ├────┼───────────────────────────────────────────────────┤
  │  4?│Номер счета                                        │
  └────┴───────────────────────────────────────────────────┘
Может быть вариант, когда код валюты идет в конце номера после '/'
*/


/*---------- Инициализируемые структуры ---------------------------------*/
 record ALG_DEPOSITOR("depositr.dbt");
 record ALG_SBDEPDOC("sbdepdoc.dbt");
 record ALG_PARAM("sb_algop.dbt");

/*---------- Коды возврата макроса --------------------------------------*/
 const  OK_RES   =  0,  /* продолжать проводку */
	SKIP_RES =  1,  /* пропустить выполнение предопределенного модуля */
	END_RES  =  2,  /* завершить проводку */
	ERR_RES  = -1;  /* аваpийно завершить проводку */

 Const LastCodeCur = False; /* True - код валюты идет в конце номера после '/' */
 Const NumberWidth = 4;/*Количество символов в номере счета под порядковый номер*/
 Var FullCur,Ident,Number,BalAc;

MACRO GetCurFullCode(Code_Currency)
  file Curr(currency) key 0;
  Var ExternCode = 0;
  Curr.Code_Currency = Code_Currency;
  if(GetEQ(Curr))
    ExternCode = Curr.ExternalCode;
  end; /* IF */
  return ExternCode;
END; /* MACRO */

MACRO GetTypeInfo(BalAcc,IdentChars)
  file sb_dtyp(sb_dtyp) key 2;
  IdentChars = "";
  BalAcc = "";
  sb_dtyp.FlagCur = ALG_DEPOSITOR.IsCur;
  sb_dtyp.Kind = ALG_DEPOSITOR.Type_Account;
  if (GetEQ(sb_dtyp))
    SetParm(0,sb_dtyp.BalAcc);
    SetParm(1,sb_dtyp.IdentChar);
  end; /* IF */
END; /* MACRO */

/* Убираем вывод на экран*/
SetOutput("NUL");

FullCur  = GetCurFullCode(ALG_DEPOSITOR.Code_Currency);
GetTypeInfo(BalAc,Ident);
Number = Trim(String(ALG_DEPOSITOR.Number));
while (StrLen(Number) < NumberWidth)
  Number = "0" + Number;
end;/*While*/
if(LastCodeCur == False)
ALG_DEPOSITOR.Account = SubStr(ALG_DEPOSITOR.Account,1,2) /*Индекс террбанка*/
                      + SubStr(BalAc,1,5)                 /*Балансовый счет */
                      + SubStr(FullCur,1,3)               /*Код валюты*/
                      + SubStr(Ident,1,2)                  /*Код вида вклада*/
                      + Number;                             /*Номер счета*/
else
ALG_DEPOSITOR.Account = SubStr(ALG_DEPOSITOR.Account,1,2) /*Индекс террбанка*/
                      + SubStr(BalAc,1,5)                 /*Балансовый счет */
                      + SubStr(Ident,1,2)                  /*Код вида вклада*/
                      + Number                              /*Номер счета*/
                      + "/" + SubStr(FullCur,1,3)         /*Код валюты*/
end; /*IF*/

ALG_RESULT = OK_RES;
ALG_UPDATE = 1;

return;
