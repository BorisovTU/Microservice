/******************************************************************************
  Проверка перед выполнением отмены безнадежной платы 
  (отнесением платы на резерв)
******************************************************************************/
import RSD, alg_vars;

  private var {curdate};
  var BankStandart = GetBankStandart();
  var cmd, rs;
  var CardRef = 0;
  var CardMaturityDate = date(0,0,0);
  var CardStateCode = "";

  ALG_RESULT = OK_RES;

  if( (ALG_PARAM.NumOpert == 97) and (ALG_PARAM.prmNumOper == 3) and (BankStandart == 0))

    cmd = RsdCommand("SELECT t_CardRef FROM ddpindebt_dbt WHERE t_Branch=? AND t_IndebtID=?");
    cmd.addParam("t_Branch", RsdBp_in);
    cmd.value("t_Branch") = ALG_PARAM.prmI;
    cmd.addParam("t_IndebtID", RsdBp_in);
    cmd.value("t_IndebtID") = ALG_PARAM.prmL;

    cmd.execute;
    rs = RsdRecordset(cmd); 
    if( rs.moveNext() )
      CardRef = rs.value(0);
    end;

    if( CardRef <= 0 )
      msgbox("Отнесение на резерв возможно только для плат по картам");
      ALG_RESULT = ERR_RES;
    end;

    if( ALG_RESULT == OK_RES )

      cmd = RsdCommand("SELECT t_CardMaturityDate, t_CardStateCode FROM dsccard_dbt WHERE t_FNcash=? AND t_CardRef=?");
      cmd.addParam("t_FNcash", RsdBp_in);
      cmd.value("t_FNcash") = ALG_PARAM.prmI;
      cmd.addParam("t_CardRef", RsdBp_in);
      cmd.value("t_CardRef") = CardRef;

      cmd.execute;
      rs = RsdRecordset(cmd); 
      if( rs.moveNext() )
        CardMaturityDate = date(rs.value(0));
        CardStateCode = rs.value(1);
      else 
        msgbox("Карта не найдена");
        ALG_RESULT = ERR_RES;
      end;
    end;

    if( (ALG_RESULT == OK_RES) and (CardMaturityDate >= {curdate}) )
      msgbox("У карты должен закончится срок действия|для отнесения платы на резерв");
      ALG_RESULT = ERR_RES;
    end;

    if( (ALG_RESULT == OK_RES) and (CardStateCode != "BLKJ_") )
      msgbox("Карта не находится в требуемом состоянии|для отнесения платы на резерв");
      ALG_RESULT = ERR_RES;
    end;

  end;

end;
