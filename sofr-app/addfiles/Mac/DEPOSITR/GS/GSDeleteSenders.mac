/*
$Name:             GSDeleteSenders.mac
$Module:           RS-Retail
$Description:      Операция "Удалить отправителя Global Stream"
*/


import GSOperation, GSDialogs;


private const DEBUG = false;


//============================================================================
class (TGSOperation) TGSDeleteSenders(opNum, subOpNum, depRef)

  private var dbSenders = TBFile("gssender.dbt", "W");

  //----------------------------------------------------------------------------
  private macro posOnDbSender(id: integer)
    dbSenders.KeyNum = 0;
    
    ClearRecord(dbSenders);
    dbSenders.rec.id = id;

    CheckError(dbSenders.GetEQ());
  end;

  //----------------------------------------------------------------------------
  private macro debugCallService(accountNumber: string, cardHolderCountry: string, cardHolderPersId: string, payeeId: string): bool
    var result = true;
    return result;
  end;
  
  //----------------------------------------------------------------------------
  private macro callService(accountNumber: string, cardHolderCountry: string, cardHolderPersId: string, payeeId: string): bool
    message("Удаление отправителя Global Stream...");
    var result = proxy.deleteSenderByPayeeID(accountNumber, cardHolderCountry, cardHolderPersId, payeeId);
    message(" ");

    return result.isReturn();

  OnError(err)
    //AddErrMessage(err);
    DoError("Ошибка при вызове сервиса удаления отправителей Global Stream");
  end;

  //----------------------------------------------------------------------------
  private macro deleteSender()
    if (dbSenders.rec.payeeId == "")
      return;
    end;

    // создаем обертку для вызова сервиса
    createGSProxy();

    var accountNumber = ALG_DEPOSITOR.account;
    var cardHolderCountry = getCountryCode2(clientList.CurRec.rec.Country);
    var cardHolderPersId = ownerId;
    var payeeId = dbSenders.rec.payeeId;

    var result;    
    if (DEBUG)
      result = debugCallService(accountNumber, cardHolderCountry, cardHolderPersId, payeeId);
    else
      result = callService(accountNumber, cardHolderCountry, cardHolderPersId, payeeId);
    end;

    if (not result)
      DoError("Ошибка при удалении отправителя");
    end;
  end;

  //----------------------------------------------------------------------------
  private macro deleteDbSender()
    record saveDbSender ("gssender.dbt");

    Copy(saveDbSender, dbSenders);

    GetPos(dbSenders);
    CheckError(GetDirect(dbSenders));
    CheckError(Delete(dbSenders));

    writeSenderToLogAndLinkToOp(4, dbSenders, saveDbSender);  /* Delete */
  end;

  //----------------------------------------------------------------------------
  macro runPanel(): bool
    var id = showDeleteSendersPanel();
    if (id == 0)
      return cancel();
    end;

    if (not GetTrue(true, "Вы действительно желаете произвести удаление выбранного отправителя Global Stream?"))
      return cancel();
    end;

    posOnDbSender(id);

    return true;

  OnError(err)
    return handleError(err);
  end;

  //----------------------------------------------------------------------------
  macro check(): bool
    deleteSender();
    
    return true;

  OnError(err)
    return handleError(err);
  end;

  //----------------------------------------------------------------------------
  macro trnFunc(): bool
    deleteDbSender();
    
    return true;

  OnError(err)
    return handleError(err);
  end;

  //----------------------------------------------------------------------------
  InitTGSOperation(opNum, subOpNum, depRef);

end;


//----------------------------------------------------------------------------
private macro createInstance(opNum, subOpNum, depRef)
  var op = TGSDeleteSenders(opNum, subOpNum, depRef); 
  return op;

OnError(err)
  AddErrMessage(err.Message);
  PushErrorMessage(GetErrMessageText());
end;


