/*
$Name:             GSRegistration.mac
$Module:           RS-Retail
$Description:      Операция "Регистрация клиента в Global Stream"
*/


import GSOperation, GSDialogs;


private const DEBUG = false;


//============================================================================
class (TGSOperation) TGSRegistration(opNum, subOpNum, depRef)

  //private var agreementTemplateId = 0;
  private var response;   //: com.gstreams.efts.back.ws.registration.RegisterClientResponse

  //----------------------------------------------------------------------------
  private macro getClientAddress(): string
    /*clientList.CurRec.AdressType = 1;
    if (clientList.CurRec.rec.Address == "")
      clientList.CurRec.AdressType = 2;
      if (clientList.CurRec.rec.Address == "")
        clientList.CurRec.AdressType = 3;
      end;
    end;*/
    return clientList.CurRec.rec.Address;
  end;

  //----------------------------------------------------------------------------
  private macro fillOwnerInfo()
    var client = jvm.createJavaObject("com.gstreams.efts.back.ws.registration.ClientInfo");

    client.actualAddress = getClientAddress();
    client.address = client.actualAddress;
    client.birthDate = proxy.dateFromString(string(clientList.CurRec.rec.Born), "dd.MM.yyyy");
    client.cntry = getCountryCode2(clientList.CurRec.rec.Country);
    client.firstName = clientList.CurRec.rec.Name2;
    client.lastName = clientList.CurRec.rec.Name1;
    client.personalNo = ownerId;
    client.registrationDate = proxy.dateFromString(string({curdate}), "dd.MM.yyyy");

    var agrInfo = jvm.createJavaObject("com.gstreams.efts.back.ws.registration.AgreementInfo");
    //agrInfo.agreementTemplateId = agreementTemplateId;
    
    client.agreementInfo = agrInfo;

    return client;
  end;

  //----------------------------------------------------------------------------
  private macro debugCallService(regInfo)
    var result = jvm.createJavaObject("com.gstreams.efts.back.ws.registration.RegisterClientResponse");

    var n = 1;
    var list = regInfo.payees;   // List<ClientInfo>
    var iterator = list.iterator;
    var addOwner = regInfo.cardHolderIsSender;
    while (addOwner or iterator.hasNext())
      var client;
      if (addOwner)
        client = regInfo.cardHolder;
        addOwner = false;
      else
        client = iterator.next();
      end;

      var sender = jvm.createJavaObject("com.gstreams.efts.back.ws.registration.SenderDataInfo");
      sender.senderCountryID = client.cntry;
      sender.senderPersID = client.personalNo;
      sender.telCode = client.telCode;
      sender.payeeID = "GS" + string(n);

      result.senderDataList.add(sender);
    
      n = n + 1;
    end;
    
    return result;
  end;
  
  //----------------------------------------------------------------------------
  private macro callService(regInfo) //: com.gstreams.efts.back.ws.registration.RegisterClientResponse
    message("Регистрация клиента в Global Stream...");
    var result = proxy.registerClient(regInfo);
    message(" ");

    return result;

  OnError(err)
    //AddErrMessage(err);
    DoError("Ошибка при вызове сервиса регистрации клиента Global Stream");
  end;

  //----------------------------------------------------------------------------
  private macro registerClient() //: com.gstreams.efts.back.ws.registration.RegisterClientResponse
    var bankId = getGSRegVal("Код_Банка");
    if (bankId == "")
      DoError("Не указан код Нашего Банка в Global Stream");
    end;

    // создаем обертку для вызова сервиса
    createGSProxy();

    // входящие параметры для регистрации клиента
    var regInfo = jvm.createJavaObject("com.gstreams.efts.back.ws.registration.RegistrationInformation");
    var accInfo = jvm.createJavaObject("com.gstreams.efts.back.ws.registration.AccountInfo");

    accInfo.accountNo = ALG_DEPOSITOR.account;
    accInfo.bankId = bankId;
    accInfo.ccy = getCurrShortName(ALG_DEPOSITOR.code_currency);

    regInfo.accountInfo = accInfo;

    var senders = TBFile("gssender.tmp");

    var stat = senders.next();
    while (stat)
      var client = fillClientInfo(senders.rec);

      if (client.personalNo == ownerId)
        regInfo.cardHolder = client;
        regInfo.cardHolderIsSender = true;
      else
        regInfo.payees.add(client);
      end;

      stat = senders.next();
    end;

    if (not regInfo.cardHolderIsSender)
      regInfo.cardHolder = fillOwnerInfo();
    end;

    debugPringObj(regInfo);
    if (DEBUG)
      return debugCallService(regInfo);
    end;
    return callService(regInfo);
  end;

  //----------------------------------------------------------------------------
  macro runPanel(): bool
    if (not DEBUG)
      loadCardAgreementTemplateInfos();
    end;

    if (showRegisterClientPanel() != F2)
      return cancel();
    end;

    return true;

  OnError(err)
    return handleError(err);
  end;

  //----------------------------------------------------------------------------
  macro check(): bool
    checkSendersForDuplicate();
    
    response = registerClient();
    debugPringObj(response);
    
    return true;

  OnError(err)
    return handleError(err);
  end;

  //----------------------------------------------------------------------------
  macro trnFunc(): bool
    saveSenders(response.senderDataList);
    
    return true;

  OnError(err)
    return handleError(err);
  end;

  //----------------------------------------------------------------------------
  macro showReport(): bool
    printSenders(response.senderDataList);
    
    return true;

  OnError(err)
    return handleError(err);
  end;

  //----------------------------------------------------------------------------
  InitTGSOperation(opNum, subOpNum, depRef);

end;


//----------------------------------------------------------------------------
private macro createInstance(opNum, subOpNum, depRef)
  var op = TGSRegistration(opNum, subOpNum, depRef); 
  return op;

OnError(err)
  AddErrMessage(err.Message);
  PushErrorMessage(GetErrMessageText());
end;

