/*
$Name:             GSPayment.mac
$Module:           RS-Retail
$Description:      On-line пополнение текущего счета на основании платежа из Qiwi
*/

import GSOperation, CommonInter, /*RtXmlFunc,*/ rsd, rsexts;


// максимальное количество попыток выполнить операцию при конкурентном изменении счёта
private const ConcurentRepCount = 100;

private const LOG_ERRORS = false;

private var {cnum};
private file ErrLogFile() txt append;


//============================================================================
private class TGSMessage

  class A
    var JMSCorrelationID: string = "";
    var AcqTxn: string = "";
    var AcqSysId: string = "";
    var TransactionDate: string = "00000000000000";
    var Cban: string = "";
    var BaseAmount: money = $0;
    var BaseCcy: string = "";
  end;

  var attr = A;

  //----------------------------------------------------------------------------
  private macro splitDateTimeStr(dt: string, d: @date, t: @time)
    // YYYYMMDDHHmmss
    var year = int(SubStr(dt, 1, 4));
    var mon = int(SubStr(dt, 5, 2));
    var day = int(SubStr(dt, 7, 2));
    var hour = int(SubStr(dt, 9, 2));
    var min = int(SubStr(dt, 11, 2));
    var sec = int(SubStr(dt, 13, 2));

    d = date(day, mon, year);
    t = time(hour, min, sec);
  end;

  //----------------------------------------------------------------------------
  macro fillRtExternMsg(rec)
    rec.messageId = attr.JMSCorrelationID;
    rec.systemType = "GS";
    rec.transactionId = attr.AcqTxn;
    rec.transactionAddId = attr.AcqSysId;
    rec.owner = "OSMP_MSK";
    splitDateTimeStr(attr.TransactionDate, @rec.transactionDate, @rec.transactionTime);
    rec.receivingDate = getCurDate( );
    rec.account = attr.Cban;
    if ( ValType( attr.BaseAmount ) != V_UNDEF )
      rec.amount = attr.BaseAmount;
    else
      rec.amount = $0;
    end;
    rec.isCreditOperaion = "X";
    rec.currency = attr.BaseCcy;
  end;
  
end;


//============================================================================
private class TGSPayment
  var result = "00";
  private var msg = TGSMessage;
  private var dep = TBFile("depositr", "R", 8);
  private const LogDir = GetRegVal("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR") + "\\GS\\";

  //----------------------------------------------------------------------------
  private macro error(errCode: string)
    result = errCode;
    RunError(errCode);
  end;

  //----------------------------------------------------------------------------
  private macro makeLogLine(): string
    var i = 0;
    var p;
    var str = "";

    while (GetParm(i + 1, p))
      if (i != 0)
        str = str + ",\t";
      end;
      
      str = str + p;
      i = i + 1;
    end;

    return str;
  end;

  //----------------------------------------------------------------------------
  private macro logError(stat: integer)
    if (not LOG_ERRORS)
      return;
    end;
    
    //if (not FileName(ErrLogFile))
      MakeDir(LogDir);

      var y, m, d;
      DateSplit(date(), d, m, y);
      var fileName = "log_" + string(y:o:4) + string(m:o:2) + string(d:o:2) + ".";
      if ({cnum})
        fileName = fileName + {cnum};
      else
        fileName = fileName + "txt";
      end;
      var fullName = LogDir + fileName;

      var exists = ExistFile(fullName);
      
      Open(ErrLogFile, fullName);

      if (not exists)
        var header = makeLogLine(
            "JMSCorrelationID",
            "AcqTxn",
            "AcqSysId",
            "TransactionDate",
            "Cban",
            "BaseAmount",
            "BaseCcy",
            "stat");
        
        ErrLogFile.str = header;
        Insert(ErrLogFile);
      end;
    //end;

    var line = makeLogLine(
        msg.attr.JMSCorrelationID,
        msg.attr.AcqTxn,
        msg.attr.AcqSysId,
        msg.attr.TransactionDate,
        msg.attr.Cban,
        msg.attr.BaseAmount,
        msg.attr.BaseCcy,
        stat);
    
    ErrLogFile.str = line;
    Insert(ErrLogFile);

    var errMsg = "";
    var errStat = GetErrorMessage(errMsg);
    if ((errStat != 0) or errMsg)
      line = errStat + "\n" + errMsg;
      ErrLogFile.str = line;
      Insert(ErrLogFile);
    end;

    Close(ErrLogFile);
    
  onError
    return;
  end;

  //----------------------------------------------------------------------------
  private macro readMessage(message)
    /*if (RtXml2Rsl(message, msg) != 0)
      error("01");
    end;*/

    msg.attr.JMSCorrelationID = message.JMSCorrelationID;
    msg.attr.AcqTxn = message.AcqTxn;
    msg.attr.AcqSysId = message.AcqSysId;
    msg.attr.TransactionDate = message.TransactionDate;
    msg.attr.Cban = message.Cban;

    if ( ValType( message.BaseAmount ) != V_UNDEF )
      msg.attr.BaseAmount = message.BaseAmount;
    else
      msg.attr.BaseAmount = $0;
    end;

    if ( ValType( message.BaseCcy ) != V_UNDEF )
      msg.attr.BaseCcy = message.BaseCcy;
    end;

    msg.attr.BaseAmount = msg.attr.BaseAmount / 100;
    
    if (msg.attr.BaseCcy == "RUB")
      msg.attr.BaseCcy = "RUR";
    end;

    // Проверка присланного сообщения
    if ( ValType( message.BaseCcy ) == V_UNDEF )
      error("01");
    end;

    if ( ( ValType( message.BaseAmount ) == V_UNDEF ) or ( msg.attr.BaseAmount <= 0 ) )
      error("01");
    end;

  OnError
    error("01");
  end;

  //----------------------------------------------------------------------------
  private macro findAccount()
    var account = msg.attr.Cban;
    
    var cmd = RsdCommand(
        "SELECT t_Referenc " +
        "FROM ddepositr_dbt " +
        "WHERE t_Account = ? ");

    cmd.addParam("account", RSDBP_IN, account);
    cmd.execute();
    var rs = RsdRecordset(cmd);
    rs.NullConversion = true;

    if (not rs.moveNext())
      error("04");  // Счет не найден
    end;
    
    var depRef = rs.value("t_referenc");

    if (rs.moveNext())
      error("04");
    end;

    dep.rec.Referenc = depRef;
    if (not dep.GetEQ())
      error("04");  // Счет не найден
    end;
    
    if (dep.rec.Open_Close)
      error("03");
    end;

    if (getCurrShortName(dep.rec.code_currency) != msg.attr.BaseCcy)
      error("01");
    end;
  end;

  //----------------------------------------------------------------------------
  private macro insertRtExternMsg(rec)
    var cmd = RsdCommand(
        "INSERT INTO dRtExternMsg_dbt " +
        "(t_messageId, t_systemType, t_transactionId, t_transactionAddId, t_owner, " +
        " t_transactionDate, t_transactionTime, t_receivingDate, t_account, " +
        " t_amount, t_isCreditOperaion, t_currency, t_result) " +
        "VALUES (?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?) ");

    cmd.addParam("messageId", RSDBP_IN, rec.messageId);
    cmd.addParam("systemType", RSDBP_IN, rec.systemType);
    cmd.addParam("transactionId", RSDBP_IN, rec.transactionId);
    cmd.addParam("transactionAddId", RSDBP_IN, rec.transactionAddId);
    cmd.addParam("owner", RSDBP_IN, rec.owner);
    cmd.addParam("transactionDate", RSDBP_IN, rec.transactionDate);
    cmd.addParam("transactionTime", RSDBP_IN, rec.transactionTime);
    cmd.addParam("receivingDate", RSDBP_IN, rec.receivingDate);
    cmd.addParam("account", RSDBP_IN, rec.account);
    cmd.addParam("amount", RSDBP_IN, rec.amount);
    cmd.addParam("isCreditOperaion", RSDBP_IN, rec.isCreditOperaion);
    cmd.addParam("currency", RSDBP_IN, rec.currency);
    cmd.addParam("result", RSDBP_IN, rec.result);

    cmd.NullConversion = true;
    cmd.execute();
  end;

  //----------------------------------------------------------------------------
  private macro execOperation()
    record operParm("oper_prm.1");
    record tmpDoc("sbdepdoc");

    var repCount = 0;

    while (true)
      ClearRecord(tmpDoc);
      if (dep.rec.code_currency != 0)
        tmpDoc.IsCur = 1;
      end;

      var saveFNcash = SetAllFNcash(dep.rec.FNCash, dep.rec.FNCash);
      var saveFlagCur = SetFlagCur(tmpDoc.IsCur);

      tmpDoc.Referenc = dep.rec.referenc;
      tmpDoc.FNCash = dep.rec.FNCash;
      tmpDoc.Account = dep.rec.Account;
      tmpDoc.Code_Currency = dep.rec.code_currency;
      tmpDoc.Type_Account = dep.rec.Type_Account;
      tmpDoc.CodClient = dep.rec.CodClient;
      tmpDoc.Date_Document = getCurDate( );
      tmpDoc.DepDate_Document = getCurDate( );
      tmpDoc.Oper = {oper};
      tmpDoc.YesSbook = "X";
      tmpDoc.IsControl = StrFor(1);
      tmpDoc.InSum = msg.attr.BaseAmount;
      tmpDoc.TypeOper = 71;
      tmpDoc.ApplType = 54;
      tmpDoc.TypeComplexOper = tmpDoc.TypeOper;
      
      ClearRecord(operParm);
      operParm.Account = tmpDoc.Account;
      operParm.Type_Account = tmpDoc.Type_Account;
      operParm.Code_Currency = tmpDoc.Code_Currency;
      operParm.TypeComplexOper = tmpDoc.TypeComplexOper;
      operParm.Type_Oper = tmpDoc.TypeComplexOper;
      operParm.Show_Panel = 0;
      operParm.UseMaxDate = 1;
      operParm.NoPrint = 1;
      operParm.Branch = tmpDoc.FNCash;
      var stat = Выполнение_Операции(operParm, tmpDoc);

      InterDesk_EndDocBunch();

      SetAllFNcash(saveFNcash, saveFNcash);
      SetFlagCur(saveFlagCur);

      if (stat == 0)
        break;
      elif (stat == 70)
        repCount = repCount + 1;
        if  (repCount < ConcurentRepCount)
          stat = 0;
        end;
      end;        

      if (stat != 0)
        logError(stat);
        
        if ( stat == 4678 ) // Ошибка проверки лимита на операцию
          error("03");
        else
          error("05");
        end;
      end;
    end;
  end;
  
  //----------------------------------------------------------------------------
  private macro execute(message)
    readMessage(message);
    findAccount();
    execOperation();

  OnError(err)
    if (result == "00")
      result = "05";
    end;
  end;

  //----------------------------------------------------------------------------
  private macro saveMessage()
    var messages = TBFile("RtExternMsg", "W");
    
    ClearRecord(messages);
    msg.fillRtExternMsg(messages.rec);
    //messages.rec.currency = dep.rec.code_currency;
    messages.rec.result = result;

    //messages.Insert();
    insertRtExternMsg(messages.rec);
  end;

  //----------------------------------------------------------------------------
  macro process(message)
    execute(message);
    saveMessage();
  end;
  
end;


macro onMessage(message)
  var payment = TGSPayment;
  payment.process(message);
  if (payment.result != "00")
     RunError(payment.result);
  end;
end;

