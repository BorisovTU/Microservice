/*
$Name:             GSReport.mac 
$Module:           RS-Retail
$Description:      Отчет о результатах сверки реестров платежей Global Stream
*/

import GSOperation, rsd, rsexts;


private file RegFile() txt;
private file FtpCfgFile() txt write;


//============================================================================
private class GSHeader
  var count: integer = 0;
  var sum: money = $0;
  var fee: money = $0;
  var totalSum: money = $0;
  var comment: string = "";

  //----------------------------------------------------------------------------
  macro setFields(arr: TArray)
    if (arr.size == 4)
      count = arr[1];
      totalSum = arr[2];
      comment = arr[3];
    elif (arr.size = 6)
      count = arr[1];
      sum = arr[2];
      fee = arr[3];
      totalSum = arr[4];
      comment = arr[5];
    else
      DoError("Ошибка формата файла ответного реестра Global Stream");
    end;

    sum = sum / 100;
    fee = fee / 100;
    totalSum = totalSum / 100;
  end;
  
end;


//============================================================================
private class GSData
  var txn: string = "";
  var AcqInstID: string = "";
  var AcqSysId: string = "";
  var AcqTxn: string = "";
  var CBAN: string = "";
  var BaseCCY: string = "";
  var BaseAmount: money = $0;
  var Fee: money = $0;
  var TransmissionDate: string = "";
  var ActionCode: string = "";
  var CBAN2: string = "";
  var BaseCCY2: string = "";
  var BaseAmount2: money = $0;
  var ActionCode2: string = "";

  //----------------------------------------------------------------------------
  private macro checkArraySize(arr: TArray, size: integer)
    if (arr.size < size)
      DoError("Ошибка формата файла ответного реестра Global Stream");
    end;
  end;

  //----------------------------------------------------------------------------
  macro setFields(sectionNumber: integer, arr: TArray)
    checkArraySize(arr, 7);

    txn = arr[0];
    AcqInstID = arr[1];
    AcqSysId = arr[2];
    AcqTxn = arr[3];
    CBAN = arr[4];
    BaseCCY = arr[5];
    BaseAmount = arr[6];

    if (sectionNumber == 1)
      checkArraySize(arr, 10);

      Fee = arr[7];
      TransmissionDate = arr[8];
      ActionCode = arr[9];
    else
      checkArraySize(arr, 9);

      TransmissionDate = arr[7];
      ActionCode = arr[8];
    end;
    
    if (sectionNumber == 2)
      checkArraySize(arr, 13);

      CBAN2 = arr[9];
      BaseCCY2 = arr[10];
      BaseAmount2 = arr[11];
      ActionCode2 = arr[12];
    end;

    BaseAmount = BaseAmount / 100;
    Fee = Fee / 100;
    BaseAmount2 = BaseAmount2 / 100;
  end;
  
end;


//============================================================================
private class GSReport
  private const WorkDir = GetRegVal("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\WORKDIR") + "\\GS\\";
  private const RegDate = {curdate} - 1;

  //----------------------------------------------------------------------------
  private macro makeCmdLine(): string
    var params = TArray;
    var p;

    while (GetParm(params.size + 1, p))
      params(params.size) = p;
    end;

    return strCat(params, " ");
  end;

  //----------------------------------------------------------------------------
  private macro insertFtpCfgLine(line: string): string
    FtpCfgFile.str = line;
    Insert(FtpCfgFile);
  end;

  //----------------------------------------------------------------------------
  // YYYYMMDD
  private macro makeDateString(d: date): string
    var year, mon, day;
    DateSplit(d, day, mon, year);
    var s = string(year:4:o) + string(mon:2:o) + string(day:2:o);
    return s;
  end;

  //----------------------------------------------------------------------------
  // YYYYMMDDHHmmss -> ДД.ММ.ГГГГ чч:мм:сс
  private macro formatDateTimeString(dt: string): string
    var year = SubStr(dt, 1, 4);
    var mon = SubStr(dt, 5, 2);
    var day = SubStr(dt, 7, 2);
    var hour = SubStr(dt, 9, 2);
    var min = SubStr(dt, 11, 2);
    var sec = SubStr(dt, 13, 2);
    var s = day + "." + mon + "." + year + " " + hour + ":" + min + ":" + sec;
    return s;
  end;

  //----------------------------------------------------------------------------
  private macro makeFileName(): string
    var bankId = getGSRegVal("Код_Банка");
    if (bankId == "")
      DoError("Не указан код Нашего Банка в Global Stream");
    end;

    var name = "GS_" + bankId + "_" + makeDateString(RegDate);
    return name;
  end;

  //----------------------------------------------------------------------------
  private macro splitString(str: string, delim: string): TArray
    var arr = TArray;
    var idx;
    while (str != "")
      idx = Index(str, delim);
      if (idx == 0)
        arr[arr.size] = str;
        return arr;
      end;
      arr[arr.size] = substr(str, 1, idx - 1);
      str = substr(str, idx + 1);
    end;
    return arr;
  end;

  //----------------------------------------------------------------------------
  private macro copyFromFtp(fileName: string)
    var ftpUrl = getGSRegVal("FTP");
    if (ftpUrl == "")
      DoError("Не указан адрес FTP для обмена реестрами с Global Stream");
    end;

    var cfgFile = WorkDir + "ftp.cfg";
    Open(FtpCfgFile, cfgFile);

    var localPath = SubStr(WorkDir, 1, StrLen(WorkDir) - 1);

    var server;
    var serverPath;
    parseFtpUrl(ftpUrl, @server, @serverPath);

    if (FTP_READ_DIR != "")
      if (serverPath != "")
        serverPath = serverPath + "/";
      end;
      serverPath = serverPath + FTP_READ_DIR;
    end;
    
    var openPath = "ftp://" + FTP_USER + ":" + FTP_PASS + "@" + server;    
    insertFtpCfgLine(makeCmdLine("open", openPath, /*"-hostkey=\"*\"",*/ "-explicit"));
    //insertFtpCfgLine(makeCmdLine("user", FTP_USER, FTP_PASS));
    insertFtpCfgLine(makeCmdLine("lcd", localPath));
    if (serverPath != "")
      insertFtpCfgLine(makeCmdLine("cd", serverPath));
    end;
    insertFtpCfgLine(makeCmdLine("get", fileName));
    //insertFtpCfgLine("quit");
    insertFtpCfgLine("exit");

    Close(FtpCfgFile);

    //var cmd = "/c ftp -n -s:" + cfgFile;
    var cmd = "/c " + FTP_CLIENT + " /script=" + cfgFile;
    //var res = run(GetEnv("COMSPEC"), cmd, ">", "done");
    var res = run(GetEnv("COMSPEC"), cmd);
    if (res != 0)
      DoError("Ошибка при загрузке реестра с FTP Global Stream");
    end;

    RemoveFile(cfgFile);
  end;

  //----------------------------------------------------------------------------
  private macro printReportHeader()
    [                                     ОТЧЕТ О РЕЗУЛЬТАТАХ СВЕРКИ РЕЕСТРОВ ПЛАТЕЖЕЙ QIWI];
    [                                                    ЗА ########## г.                  ](RegDate);
    [ ];
  end;

  //----------------------------------------------------------------------------
  private macro printSectionHeader(sectionNumber: integer, header)
    if (sectionNumber == 1)
      [I. Успешно сквитованные платежи:];
    elif (sectionNumber == 2)
      [II. Платежи с обнаруженными расхождениями в реквизитах];
    elif (sectionNumber == 3)
      [III. Платежи Qiwi, отсутствующие в АБС банка];
    else
      [IV. Платежи, отсутствующие в Global Stream];
    end;

      [ ];
      [Количество платежей:  #](header.count:l);

    if (sectionNumber == 1)
      [Общая сумма успешно выполненных платежей:    #](header.sum:l);
      [Общая сумма комиссии за выполненные платежи: #](header.fee:l);
      [Общая сумма успешно выполненных платежей с учетом комиссии: #](header.totalSum:l);
    else
      [Общая сумма платежей:    #](header.totalSum:l);
    end;

    if (sectionNumber == 1)
      [ ];
      [┌──────────┬──────────┬────────────────────┬────────────────────┬──────────┬────────────┬────────────┬───────────────────┬──────────┐];
      [│ ID       │Владелец  │                    │                    │Валюта    │  Сумма     │  Сумма     │Дата и время       │Код       │];
      [│ платежа  │системы   │   ID транзакции    │        Счет        │зачисления│  платежа   │  комиссии  │совершения платежа │результата│];
      [│ в GS     │терминалов│                    │                    │          │            │            │                   │операции  │];
      [├──────────┼──────────┼────────────────────┼────────────────────┼──────────┼────────────┼────────────┼───────────────────┼──────────┤];
    elif (sectionNumber == 2)
      [ ];
      [┌──────────┬──────────┬────────────────────┬────────────────────┬──────────┬────────────┬───────────────────┬──────────┬────────────────────┬──────────┬────────────┬──────────┐];
      [│ ID       │Владелец  │                    │                    │Валюта    │  Сумма     │Дата и время       │Код       │                    │Валюта    │  Сумма     │Код       │];
      [│ платежа  │системы   │   ID транзакции    │      Счет в GS     │зачисления│  платежа   │совершения платежа │результата│      Счет в АБС    │зачисления│  платежа   │результата│];
      [│ в GS     │терминалов│                    │                    │в GS      │  в GS      │                   │операции  │                    │в АБС     │  в АБС     │операции  │];
      [│          │          │                    │                    │          │            │                   │в GS      │                    │          │            │в АБС     │];
      [├──────────┼──────────┼────────────────────┼────────────────────┼──────────┼────────────┼───────────────────┼──────────┼────────────────────┼──────────┼────────────┼──────────┤];
    else
      [ ];
      [┌──────────┬──────────┬────────────────────┬────────────────────┬──────────┬────────────┬───────────────────┬──────────┐];
      [│ ID       │Владелец  │                    │                    │Валюта    │  Сумма     │Дата и время       │Код       │];
      [│ платежа  │системы   │   ID транзакции    │        Счет        │зачисления│  платежа   │совершения платежа │результата│];
      [│ в GS     │терминалов│                    │                    │          │            │                   │операции  │];
      [├──────────┼──────────┼────────────────────┼────────────────────┼──────────┼────────────┼───────────────────┼──────────┤];
    end;
  end;

  //----------------------------------------------------------------------------
  private macro printSectionData(sectionNumber: integer, data)
    if (sectionNumber == 1)
      [│##########│##########│####################│####################│   ###    │############│############│###################│    ##    │]
      (data.txn, data.AcqInstID, data.AcqSysId + "/" + data.AcqTxn, data.CBAN, data.BaseCCY,
       data.BaseAmount, data.Fee, formatDateTimeString(data.TransmissionDate), data.ActionCode);
    elif (sectionNumber == 2)
      [│##########│##########│####################│####################│   ###    │############│###################│    ##    │####################│   ###    │############│    ##    │]
      (data.txn, data.AcqInstID, data.AcqSysId + "/" + data.AcqTxn, data.CBAN, data.BaseCCY,
       data.BaseAmount, formatDateTimeString(data.TransmissionDate), data.ActionCode, data.CBAN2, 
       data.BaseCCY2, data.BaseAmount2, data.ActionCode2);
    else
      [│##########│##########│####################│####################│   ###    │############│###################│    ##    │]
      (data.txn, data.AcqInstID, data.AcqSysId + "/" + data.AcqTxn, data.CBAN, data.BaseCCY,
       data.BaseAmount, formatDateTimeString(data.TransmissionDate), data.ActionCode);
    end;
  end;

  //----------------------------------------------------------------------------
  private macro printSectionFooter(sectionNumber: integer)
    if (sectionNumber == 1)
      [└──────────┴──────────┴────────────────────┴────────────────────┴──────────┴────────────┴────────────┴───────────────────┴──────────┘];
      [ ];
    elif (sectionNumber == 2)
      [└──────────┴──────────┴────────────────────┴────────────────────┴──────────┴────────────┴───────────────────┴──────────┴────────────────────┴──────────┴────────────┴──────────┘];
      [ ];
    else
      [└──────────┴──────────┴────────────────────┴────────────────────┴──────────┴────────────┴───────────────────┴──────────┘];
      [ ];
    end;
  end;

  //----------------------------------------------------------------------------
  private macro readRegFileLine(): string
    if (not next(RegFile))
      DoError("Ошибка формата файла ответного реестра Global Stream");
    end;

    var line = RegFile.str;
    return line;
  end;

  //----------------------------------------------------------------------------
  private macro parseSectionHeader(sectionNumber: integer)
    var line = readRegFileLine();
    var header = GSHeader;
    header.setFields(splitString(line, "\t"));
    return header;
  end;

  //----------------------------------------------------------------------------
  private macro parseSectionData(sectionNumber: integer)
    var line = readRegFileLine();
    var data = GSData;
    data.setFields(sectionNumber, splitString(line, "\t"));
    return data;
  end;

  //----------------------------------------------------------------------------
  private macro parseSection(sectionNumber: integer)
    var header = parseSectionHeader(sectionNumber);
    printSectionHeader(sectionNumber, header);
    
    var i = 0;
    while (i < header.count)
      var data = parseSectionData(sectionNumber);
      printSectionData(sectionNumber, data);

      i = i + 1;
    end;

    printSectionFooter(sectionNumber);
  end;

  //----------------------------------------------------------------------------
  private macro parseFile(fileName: string)
    RemoveFile(WorkDir + fileName + ".txt");
    RenameFile(WorkDir + fileName, WorkDir + fileName + ".txt");
    if (not Open(RegFile, WorkDir + fileName))
      DoError("Не найден файл ответного реестра Global Stream");
    end;

    printReportHeader();

    var i;
    for (i, 1, 4)
      parseSection(i);
    end;

  OnError(err)
    Close(RegFile);
    DoError(err);
  end;

  //----------------------------------------------------------------------------
  macro showReport()
    MakeDir(WorkDir);
    var fileName = makeFileName();

    copyFromFtp(fileName);
    parseFile(fileName);
  end;

end;


//----------------------------------------------------------------------------
var rep = GSReport;
rep.showReport();

OnError(err)
  ShowError(err);
  exit(1);

