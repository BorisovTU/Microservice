/*
$Name:             GSOperation.mac
$Module:           RS-Retail
$Description:      Базовый класс операции Global Stream
*/


import alg_vars, rcw, ErrHandler, rsd;


// Код страны по умолчанию
private const OUR_COUNTRY_CODE2 = "TJ"; // Таджикистан

// логин и пароль для доступа к сервису регистрации отправителей Global Stream
private const USER_NAME = "IMONB";
private const PASSWORD = ""; //SVI убран пароль

// логин и пароль для доступа к FTP реестра платежей Global Stream
const FTP_USER = "IMONB";
const FTP_PASS = ""; //SVI убран пароль

const FTP_CLIENT = "..\\WinSCP\\WinSCP.com";

const FTP_WRITE_DIR = "write";
const FTP_READ_DIR = "read";


//----------------------------------------------------------------------------
private const DEBUG_PRINT = false;

private record WriteLog_rec("WriteLog.rec");
private file cntr ( country  ) Key 2;  /* список стран */
private file curr("currency.dbt") key 0;

private const GSRegPath = "RS-RETAIL\\СЕРВИСНЫЕ\\ИНТЕГРАЦИЯ\\QIWI_GlobalStream\\";
private const ZERO_DATE = date(0,0,0);


//--------------------------------------------------------------------------
macro getCurrShortName(codeCurr)
  rewind(curr);
  clearRecord(curr);
  curr.Code_Currency = codeCurr;
  getEQ(curr);
  return curr.Short_Name;
end;

//----------------------------------------------------------------------------
macro getCountryCode2(codeLat3: string): string
  if (codeLat3 == "")
    return OUR_COUNTRY_CODE2;
  end;

  ClearRecord(cntr);
  cntr.CodeLat3 = codeLat3;

  if (not GetGE(cntr) or (cntr.CodeLat3 != codeLat3))
    DoError("Не найдена страна с кодом " + codeLat3);
  end;
  
  return cntr.CodeLat2;
end;

//----------------------------------------------------------------------------
macro strCat(strings: TArray, delim: string): string
  var str = "";
  var p;
  for (p, strings)
    if (str != "")
      str = str + delim;
    end;
    str = str + p;
  end;

  return str;
end;

//----------------------------------------------------------------------------
macro getGSRegVal(name: string)
  var val = GetRegVal(GSRegPath + name);
  return trim(val);
end;

//----------------------------------------------------------------------------
macro parseFtpUrl(url: string, server: @string, serverPath: @string)
  var protocol = "ftp://";

  if (Index(StrLwr(url), protocol) == 1)
    url = SubStr(url, StrLen(protocol) + 1);
  end;

  server = url;
  serverPath = "";

  var pos = Index(url, "/");
  if (pos >= 1)
    server = SubStr(url, 1, pos - 1);
    serverPath = SubStr(url, pos);
  end;

  if (server == "")
    DoError("Ошибка в адресе FTP для обмена реестрами с Global Stream");
  end;
end;


//============================================================================
class (TGlobalStreamOperation) TGSOperation(opNum, subOpNum, depRef)
  private var operation = opNum;
  private var jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
  private var proxy;
  private var clientList = TClientList;
  private var ownerId = "";


  //----------------------------------------------------------------------------
  private macro debugPringObj(obj)
    if (DEBUG_PRINT)
      ExecMacroFile("printObj.mac", "PrintObj", obj, true);
    end;
  OnError
  end;

  //----------------------------------------------------------------------------
  private macro handleError(err): bool
    message(" ");
    
    AddErrMessage(err);
    errMessage = GetErrMessageText();
    return false;
  end;

  //----------------------------------------------------------------------------
  private macro getRegDate(d: date): date
    if (d == ZERO_DATE)
      d = {curdate};//date();
    end;
    return d;
  end;

  //----------------------------------------------------------------------------
  private macro fillClientInfo(sender)
    var client = jvm.createJavaObject("com.gstreams.efts.back.ws.registration.ClientInfo");

    client.telCode = sender.telCode;
    client.actualAddress = sender.actualAddress;
    client.address = sender.address;
    client.birthDate = proxy.dateFromString(string(sender.birthDate), "dd.MM.yyyy");
    client.cntry = sender.cntry;
    client.firstName = sender.firstName;
    client.lastName = sender.lastName;
    client.fixedPhone = sender.fixedPhone;
    client.mobilePhone = sender.mobilePhone;
    client.personalNo = sender.personalNo;
    client.registrationDate = proxy.dateFromString(string(getRegDate(sender.registrationDate)), "dd.MM.yyyy");

    var agrInfo = jvm.createJavaObject("com.gstreams.efts.back.ws.registration.AgreementInfo");
    agrInfo.agreementTemplateId = sender.agrTemplateId;
    
    client.agreementInfo = agrInfo;

    return client;
  end;

  //----------------------------------------------------------------------------
  // создаем обертку для вызова сервиса
  private macro createGSProxy()
    if (proxy)
      return proxy;
    end;
    
    var url = getGSRegVal("Регистрация");
    if (trim(url) == "")
      DoError("Не указан URL сервиса Global Stream, используемого при регистрации клиентов в Qiwi");
    end;

    // создаем обертку для вызова сервиса
    proxy = jvm.createJavaObject("ru.softlab.rsbank.GSClient");
    proxy.endpoint = url;
    proxy.username = USER_NAME;
    proxy.password = PASSWORD;

    return proxy;
  end;
  
  //----------------------------------------------------------------------------
  private macro callServiceForAgreementTemplateInfos()  //: com.gstreams.efts.back.ws.registration.GetFamilyCardAgreementTemplateInfosResponse
    message("Загрузка списка соглашений Global Stream...");
    var result = proxy.getFamilyCardAgreementTemplateInfos();
    message(" ");

    return result;

  OnError(err)
    //AddErrMessage(err);
    DoError("Ошибка при вызове сервиса загрузки списка соглашений Global Stream");
  end;

  //----------------------------------------------------------------------------
  private macro loadCardAgreementTemplateInfos()
    var agreements = TBFile("GSAgrTempl.tmp", "W", -1);

    agreements.rewind();
    if (agreements.next())
      return;
    end;

    // создаем обертку для вызова сервиса
    createGSProxy();
    var result = callServiceForAgreementTemplateInfos();
    
    var iterator = result.familyCardAgreementTemplate.iterator;
    while (iterator.hasNext())
      var agr = iterator.next();

      ClearRecord(agreements);
      agreements.rec.id = agr.id;
      agreements.rec.name  = agr.name;
      agreements.rec.periodLimit = agr.periodLimit;
      agreements.rec.periodAmountLimit = money(string(agr.periodAmountLimit)) / 100;
      agreements.rec.tranAmountLimit = money(string(agr.tranAmountLimit)) / 100;
      agreements.rec.tranLimit = agr.tranLimit;

      CheckError(agreements.insert());
    end;
  end;

  //----------------------------------------------------------------------------
  private macro checkSendersForDuplicate()
    var cmd = RsdCommand(
        "SELECT d.t_PersonalNo " +
        "FROM dgssender_dbt d " +
        "WHERE d.t_AccountId = ? " +
        "  AND EXISTS ( " +
        "    SELECT 1  " +
        "    FROM dgssender_tmp t " +
        "    WHERE t.t_AccountId = d.t_AccountId " +
        "      AND t.t_PersonalNo = d.t_PersonalNo " +
        "  ) "
    );

    cmd.addParam("account", RSDBP_IN, ALG_DEPOSITOR.referenc);
    cmd.execute();
    var rs = RsdRecordset(cmd);
    rs.NullConversion = true;

    if (rs.moveNext())
      DoError("Клиент с номером документа " + rs.value("t_PersonalNo") + " уже зарегистрирован в списке отправителей");
    end;
  end;

  //----------------------------------------------------------------------------
  private macro makeNFOpApplKey(numDprt: integer, recId: integer): string
    var applKey = "rtnfopr#";
    var strTmp = String(numDprt);
    while (StrLen(strTmp) < 3)
      strTmp = "0" + strTmp;
    end;
    applKey = applKey + strTmp;
    strTmp = String(recID);
    while (StrLen(strTmp) < 10)
      strTmp = "0" + strTmp;
    end;
    applKey = applKey + strTmp;
    applKey = applKey + "00";
    return applKey;
  end;

  //----------------------------------------------------------------------------
  private macro writeSenderToLogAndLinkToOp(action, dbSenders, saveDbSender)
    ClearRecord(WriteLog_rec);
    WriteLog_rec.NumOperation = action;
    WriteLog_rec.ObjectType   = operation;
    WriteLog_rec.ObjectID     = string(50) + "/" + String(dbSenders.rec.id);

    CheckError(WriteChangesToLog(WriteLog_rec, dbSenders, saveDbSender));

    var applKey = makeNFOpApplKey(WriteLog_rec.NumDprt, WriteLog_rec.RecID);

    CheckError(CashLink_AddRec(3001, applKey));
  end;

  //----------------------------------------------------------------------------
  private macro getSenderInfo(senderDataList /*: List<SenderDataInfo>*/, personalNo: string)  //: com.gstreams.efts.back.ws.registration.SenderDataInfo
    var iterator = senderDataList.iterator;
    while (iterator.hasNext())
      var sender = iterator.next();
      if (sender.senderPersID == personalNo)
        return sender;
      end;
    end;

    DoError("Клиент с номером документа " + personalNo + " не найден в списке зарегистрированных отправителей");
  end;

  //----------------------------------------------------------------------------
  private macro saveSenders(senderDataList /*: List<SenderDataInfo>*/)
    var senders = TBFile("gssender.tmp");
    var dbSenders = TBFile("gssender.dbt", "W");
    record saveDbSender ("gssender.dbt");

    senders.rewind();
    var stat = senders.next();
    while (stat)
      var senderInfo = getSenderInfo(senderDataList, senders.rec.personalNo);

      ClearRecord(dbSenders);
      Copy(dbSenders, senders);
      dbSenders.rec.id = 0;
      dbSenders.rec.accountId  = ALG_DEPOSITOR.referenc;
      dbSenders.rec.registrationDate = getRegDate(senders.rec.registrationDate);//{curdate};
      dbSenders.rec.payeeId = senderInfo.payeeID;
      dbSenders.rec.telCode = senderInfo.telCode;
      dbSenders.rec.action = 0;

      CheckError(dbSenders.insert());

      writeSenderToLogAndLinkToOp(2, dbSenders, saveDbSender);  /* Insert */

      stat = senders.next();
    end;
  end;

  //----------------------------------------------------------------------------
  private macro cancel(): bool
    errCode = -27;
    return false;
  end;

  //----------------------------------------------------------------------------
  private macro printSenders(senderDataList /*: List<SenderDataInfo>*/)
    var senders = TBFile("gssender.tmp");

[ ];
[                                  Зарегистрированы отправители GLOBAL STREAM ];
[ ];
[ ];
[+------------------------------+------------------------------+----------------------+-------------------+];
[|           Фамилия            |             Имя              |    Номер документа   |     Уникальный    |];
[|                              |                              |                      | Global Stream-код |];
[+------------------------------+------------------------------+----------------------+-------------------+];
    senders.rewind();
    var stat = senders.next();
    while (stat)
      var senderInfo = getSenderInfo(senderDataList, senders.rec.personalNo);

[| ############################ | ############################ | #################### |     ##########    |]
(senders.rec.lastName:w, senders.rec.firstName:w, senders.rec.personalNo, senderInfo.payeeID);

      stat = senders.next();
    end;
[+------------------------------+------------------------------+----------------------+-------------------+];

  end;

  //----------------------------------------------------------------------------
  private macro init(depRef)
    var dep = TBFile("depositr", "R", 8);
    
    dep.rec.Referenc = depRef;
    if (not dep.GetEQ())
      DoError(8524);  // Счет не найден
    end;
    Copy(ALG_DEPOSITOR, dep);

    if (clientList.getRecord(ALG_DEPOSITOR.CodClient))
      ownerId = clientList.CurRec.rec.PaperSeries + " " + clientList.CurRec.rec.PaperNumber;
    end;
    if (trim(ownerId) == "")
      DoError(4713);
    end;
  end;

  //----------------------------------------------------------------------------
  init(depRef);

end;

