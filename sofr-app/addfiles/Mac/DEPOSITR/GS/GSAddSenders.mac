/*
$Name:             GSAddSenders.mac
$Module:           RS-Retail
$Description:      Операция "Добавить отправителей Global Stream"
*/


import GSOperation, GSDialogs;


private const DEBUG = false;

//============================================================================
class (TGSOperation) TGSAddSenders(opNum, subOpNum, depRef)

  private var response;  //: com.gstreams.efts.back.ws.registration.AddSendersResponse  

  //----------------------------------------------------------------------------
  private macro debugCallService(request)
    var result = jvm.createJavaObject("com.gstreams.efts.back.ws.registration.AddSendersResponse");

    var n = 1;
    var list = request.senders;   // List<ClientInfo>
    var iterator = list.iterator;
    while (iterator.hasNext())
      var client = iterator.next();

      var sender = jvm.createJavaObject("com.gstreams.efts.back.ws.registration.SenderDataInfo");
      sender.senderCountryID = client.cntry;
      sender.senderPersID = client.personalNo;
      sender.telCode = client.telCode;
      sender.payeeID = "GS" + string(n);

      result.senderDataList.add(sender);
    
      n = n + 1;
    end;
    
    return result;
  end;
  
  //----------------------------------------------------------------------------
  private macro callService(request)  //: com.gstreams.efts.back.ws.registration.AddSendersResponse
    message("Добавление отправителей Global Stream...");
    var result = proxy.addSenders(request);
    message(" ");

    return result;

  OnError(err)
    //AddErrMessage(err);
    DoError("Ошибка при вызове сервиса добавления отправителей Global Stream");
  end;

  //----------------------------------------------------------------------------
  private macro addSenders()  //: com.gstreams.efts.back.ws.registration.AddSendersResponse
    // создаем обертку для вызова сервиса
    createGSProxy();

    var request = jvm.createJavaObject("com.gstreams.efts.back.ws.registration.AddSendersRequest");

    request.accountNo = ALG_DEPOSITOR.account;
    request.cardHolderCountry = getCountryCode2(clientList.CurRec.rec.Country);
    request.cardHolderPersId = ownerId;

    var senders = TBFile("gssender.tmp");

    var stat = senders.next();
    while (stat)
      var client = fillClientInfo(senders.rec);

      if (client.personalNo == ownerId)
        request.cardHolderIsSender = true;
        request.agreementInfo = client.agreementInfo;
      else
        request.senders.add(client);
      end;

      stat = senders.next();
    end;

    if (not request.cardHolderIsSender)
      var agrInfo = jvm.createJavaObject("com.gstreams.efts.back.ws.registration.AgreementInfo");
      //agrInfo.agreementTemplateId = 0;//agreementTemplateId;
      request.agreementInfo = agrInfo;
    end;
    
    if (DEBUG)
      return debugCallService(request);
    end;
    return callService(request);
  end;

  //----------------------------------------------------------------------------
  macro runPanel(): bool
    if (not DEBUG)
      loadCardAgreementTemplateInfos();
    end;

    if (showAddSendersPanel() != F9)
      return cancel();
    end;

    return true;

  OnError(err)
    return handleError(err);
  end;

  //----------------------------------------------------------------------------
  macro check(): bool
    checkSendersForDuplicate();
    
    response = addSenders();
    
    return true;

  OnError(err)
    return handleError(err);
  end;

  //----------------------------------------------------------------------------
  macro trnFunc(): bool
    saveSenders(response.senderDataList);
    
    return true;

  OnError(err)
    return handleError(err);
  end;

  //----------------------------------------------------------------------------
  macro showReport(): bool
    printSenders(response.senderDataList);
    
    return true;

  OnError(err)
    return handleError(err);
  end;

  //----------------------------------------------------------------------------
  InitTGSOperation(opNum, subOpNum, depRef);

end;


//----------------------------------------------------------------------------
private macro createInstance(opNum, subOpNum, depRef)
  var op = TGSAddSenders(opNum, subOpNum, depRef); 
  return op;

OnError(err)
  AddErrMessage(err.Message);
  PushErrorMessage(GetErrMessageText());
end;

