/*
$Name:             GSRegister.mac
$Module:           RS-Retail
$Description:      Формирование реестра платежей Global Stream
*/

import GSOperation, rsd, rsexts;


private file RegFile() txt write;
private file FtpCfgFile() txt write;


//============================================================================
private class GSRegister
  private const WorkDir = GetRegVal("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\WORKDIR") + "\\GS\\";
  private const RegDate =  Date( ) - 1;

  private var count = 0;
  private var sum = $0;

  //----------------------------------------------------------------------------
  private macro makeRegisterLine(): string
    var params = TArray;
    var p;

    while (GetParm(params.size + 1, p))
      params(params.size) = p;
    end;

    return strCat(params, "\t");
  end;

  //----------------------------------------------------------------------------
  private macro makeCmdLine(): string
    var params = TArray;
    var p;

    while (GetParm(params.size + 1, p))
      params(params.size) = p;
    end;

    return strCat(params, " ");
  end;

  //----------------------------------------------------------------------------
  private macro insertRegisterLine(line: string): string
    RegFile.str = line;
    Insert(RegFile);
  end;

  //----------------------------------------------------------------------------
  private macro insertFtpCfgLine(line: string): string
    FtpCfgFile.str = line;
    Insert(FtpCfgFile);
  end;

  //----------------------------------------------------------------------------
  private macro countMessages()
    var cmd = RsdCommand(
        "select count(*) cnt " +
        "from dRtExternMsg_dbt " +
        "where t_SystemType = ? "
        "  and t_TransactionDate = ? ");

    cmd.addParam("SystemType", RSDBP_IN, "GS");
    cmd.addParam("TransactionDate", RSDBP_IN, RegDate);
    cmd.execute();
    var rs = RsdRecordset(cmd);
    rs.NullConversion = true;

    rs.moveNext();
    count = int(rs.value("cnt"));
  end;

  //----------------------------------------------------------------------------
  private macro countSums()
    var cmd = RsdCommand(
        "select sum(t_Amount) s " +
        "from dRtExternMsg_dbt " +
        "where t_SystemType = ? "
        "  and t_TransactionDate = ? "
        "  and t_Result = ? ");

    cmd.addParam("SystemType", RSDBP_IN, "GS");
    cmd.addParam("TransactionDate", RSDBP_IN, RegDate);
    cmd.addParam("Result", RSDBP_IN, "00");
    cmd.execute();
    var rs = RsdRecordset(cmd);
    rs.NullConversion = true;

    rs.moveNext();
    sum = money(rs.value("s"));
  end;

  //----------------------------------------------------------------------------
  private macro makeMoneyString(m: money): string
    var s = string((m * 100):0:0);
    return s;
  end;

  //----------------------------------------------------------------------------
  // YYYYMMDD
  private macro makeDateString(d: date): string
    var year, mon, day;
    DateSplit(d, day, mon, year);
    var s = string(year:4:o) + string(mon:2:o) + string(day:2:o);
    return s;
  end;

  //----------------------------------------------------------------------------
  // HHmmss
  private macro makeTimeString(t: time): string
    var hour, min, sec;
    TimeSplit(t, hour, min, sec);
    var s = string(hour:2:o) + string(min:2:o) + string(sec:2:o);
    return s;
  end;

  //----------------------------------------------------------------------------
  // YYYYMMDDHHmmss
  private macro makeDateTimeString(d: date, t: time): string
    return makeDateString(d) + makeTimeString(t);
  end;

  //----------------------------------------------------------------------------
  private macro makeFileName(): string
    var bankId = getGSRegVal("Код_Банка");
    if (bankId == "")
      DoError("Не указан код Нашего Банка в Global Stream");
    end;

    var name = bankId + "_GS_" + makeDateString(RegDate);
    return name;
  end;

  //----------------------------------------------------------------------------
  private macro makeRegister(): string
    countMessages();
    if (count != 0)
      countSums();
    end;

    MakeDir(WorkDir);
    var fileName = makeFileName();
    RemoveFile(WorkDir + fileName);
    Open(RegFile, WorkDir + fileName);

    var header = makeRegisterLine(count, makeMoneyString(sum));
    insertRegisterLine(header);

    if (count > 0)
      var messages = TBFile("RtExternMsg", "R", 1);
      
      ClearRecord(messages);
      messages.rec.SystemType = "GS";
      messages.rec.TransactionDate = RegDate;

      var stat = messages.getGE();
      while (stat and (messages.rec.SystemType == "GS") and (messages.rec.TransactionDate == RegDate))
        var line = makeRegisterLine(
            messages.rec.messageId,
            messages.rec.owner,
            messages.rec.transactionAddId,
            messages.rec.transactionId,
            messages.rec.account,
            messages.rec.currency,
            makeMoneyString(messages.rec.amount),
            makeDateTimeString(messages.rec.transactionDate, messages.rec.transactionTime),
            messages.rec.result);

        insertRegisterLine(line);
        
        stat = messages.next();
      end;
    end;
    
    Close(RegFile);
    RenameFile(WorkDir + fileName + ".txt", WorkDir + fileName);

    return fileName;
  end;

  //----------------------------------------------------------------------------
  private macro copyToFtp(fileName: string)
    var ftpUrl = getGSRegVal("FTP");
    if (ftpUrl == "")
      DoError("Не указан адрес FTP для обмена реестрами с Global Stream");
    end;

    var cfgFile = WorkDir + "ftp.cfg";
    Open(FtpCfgFile, cfgFile);

    var localPath = SubStr(WorkDir, 1, StrLen(WorkDir) - 1);

    var server;
    var serverPath;
    parseFtpUrl(ftpUrl, @server, @serverPath);

    if (FTP_WRITE_DIR != "")
      if (serverPath != "")
        serverPath = serverPath + "/";
      end;
      serverPath = serverPath + FTP_WRITE_DIR;    
    end;

    var openPath = "ftp://" + FTP_USER + ":" + FTP_PASS + "@" + server;    
    insertFtpCfgLine(makeCmdLine("open", openPath, /*"-hostkey=\"*\"",*/ "-explicit"));
    //insertFtpCfgLine(makeCmdLine("user", FTP_USER, FTP_PASS));
    insertFtpCfgLine(makeCmdLine("lcd", localPath));
    if (serverPath != "")
      insertFtpCfgLine(makeCmdLine("cd", serverPath));
    end;
    insertFtpCfgLine(makeCmdLine("put", fileName));
    //insertFtpCfgLine("quit");
    insertFtpCfgLine("exit");

    Close(FtpCfgFile);

    //var cmd = "/c ftp -n -s:" + cfgFile;
    var cmd = "/c " + FTP_CLIENT + " /script=" + cfgFile;
    //var res = run(GetEnv("COMSPEC"), cmd, ">", "done");
    var res = run(GetEnv("COMSPEC"), cmd);
    if (res != 0)
      DoError("Ошибка при выгрузке реестра на FTP Global Stream");
    end;

    RemoveFile(cfgFile);
  end;

  //----------------------------------------------------------------------------
  macro sendRegister()
    var fileName = makeRegister();
    copyToFtp(fileName);

  OnError(err)
    ShowError(err);
  end;

end;


//----------------------------------------------------------------------------
var reg = GSRegister;
reg.sendRegister();
exit(1);

