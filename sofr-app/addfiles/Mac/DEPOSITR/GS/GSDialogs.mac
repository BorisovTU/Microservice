/*
$Name:             GSDialogs.mac
$Module:           RS-Retail
$Description:      Интерфейс для интеграции с Global Stream 
*/


/**********************************************************************
      Продукт: Ритейл
         Файл: ImonRegClient.mac
   Назначение: Интерфейс регистрации клиента в Global Stream 
     Операция: 

 28.01.2016 

***********************************************************************/


import rsd, CommonInter;
import alg_vars;
import GSOperation;

CONST       
      F3 = 317,
      F5 = 319,
      F8 = 322,
      F2 = 316, 
      F9 = 323,
      ESC = 27,
      ENTER = 13,
      SPACE = 32,
      KEY_DEL = 339;

                        

RECORD dlg ("REGCLNT", "rtusrmac") DIALOG;
RECORD dlgSender ("SEND_GS", "rtusrmac") DIALOG;
RECORD dlgCountry ("COUNTRY", "rtusrmac") DIALOG;
                                              

var cmd_scroll= "", rs_scroll= "";
var Columns_oper = TArray(), NumColumns_oper = 0;
var global_name_country = "";
var global_payeeid = "";
var global_type_card = 0;
var global_tc_name   = "";
var OTP = "";
var T_id_cur_rec = 0;
var global_t_id = 0;
var result = 0;


/*Обновить вид соглашения для  владельца  счета */
Macro update_TMP (type_card_, dul_, accid_ );
var rs_up, cmd_up;
 
  rs_up = " UPDATE dGSSender_tmp SET T_AGRTEMPLATEID = ?  WHERE T_PERSONALNO = ?  and T_ACCOUNTID = ? " ;
 
  cmd_up = RSDCommand(rs_up);
  cmd_up.AddParam("", RSDBP_IN, type_card_);
  cmd_up.AddParam("", RSDBP_IN, dul_);
  cmd_up.AddParam("", RSDBP_IN, accid_);
  cmd_up.NullConversion = true;
  cmd_up.execute;

end;


/* Очищаем  временную  таблицу */

Macro Trunc_Sender()
 var rs_tr, cmd_tr;
    cmd_tr = "TRUNCATE TABLE dgssender_tmp" ;
    rs_tr = RsdCommand(cmd_tr);
    rs_tr.execute;  
end; 

/* Вставка во временную таблицу отправителей */

Macro Insert_in_TMP(t_id_, acc_id_, aggr_id_, tel_code_, lname_, fname_, dul_, birth_, cntry_, act_adr_, fixph_, mobph_, addr_, regdate_, payid_, action_ )
 private var  ins_query, ins ;
  
    ins_query = "INSERT INTO   dGSSender_tmp (   " +
              "     T_ID,                 " +
              "     T_ACCOUNTID,                 " +
              "     T_AGRTEMPLATEID,             " +
              "     T_TELCODE,                   " +
              "     T_LASTNAME,                  " +
              "     T_FIRSTNAME,                 " +
              "     T_PERSONALNO,                " +
              "     T_BIRTHDATE,                 " +
              "     T_CNTRY,                     " +
              "     T_ACTUALADDRESS,             " +
              "     T_FIXEDPHONE,                " +
              "     T_MOBILEPHONE,               " +
              "     T_ADDRESS,                   " +
              "     T_REGISTRATIONDATE,          " +
              "     T_PAYEEID,                   " +
              "     T_ACTION )                   " +
   
              " VALUES (?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?)";
        
       ins =  RSDCommand(ins_query);   
       ins.NullConversion = true; 
       ins.AddParam("", RSDBP_IN, t_id_);
       ins.AddParam("", RSDBP_IN, acc_id_);
       ins.AddParam("", RSDBP_IN, aggr_id_);
       ins.AddParam("", RSDBP_IN, tel_code_);
       ins.AddParam("", RSDBP_IN, lname_);    
       ins.AddParam("", RSDBP_IN, fname_);  
       ins.AddParam("", RSDBP_IN, dul_);  
       ins.AddParam("", RSDBP_IN, birth_);                                             
       ins.AddParam("", RSDBP_IN, cntry_);
       ins.AddParam("", RSDBP_IN, act_adr_);
       ins.AddParam("", RSDBP_IN, fixph_);
       ins.AddParam("", RSDBP_IN, mobph_);
       ins.AddParam("", RSDBP_IN, addr_);
       ins.AddParam("", RSDBP_IN, regdate_);
       ins.AddParam("", RSDBP_IN, payid_);
       ins.AddParam("", RSDBP_IN, action_);
       ins.execute;
end; 

Macro Del_from_TMP(dul_, accid_);

 private var  del_query, del ;

  del_query =  "DELETE FROM dGSSender_tmp WHERE T_PERSONALNO = ? and T_ACCOUNTID = ? " ;
 
  del =  RSDCommand(del_query);
  del.AddParam("", RSDBP_IN, dul_);
  del.AddParam("", RSDBP_IN, accid_);

  del.execute;

end;
 
/*Обработка заголовков столбцов для скролига отправителя на  форме регистрации*/

macro AddColumn_oper( ar,ind, fld, head, wdth, edit )
  ar.value( ind * 6 + 0 ) = fld;  
  ar.value( ind * 6 + 1 ) = head;
  ar.value (ind * 6 + 2) = wdth;
  ar.value (ind * 6 + 3 ) = 0;   // fldType
  ar.value (ind * 6 + 4 ) = -1;  // decPoint
  ar.value (ind * 6 + 5 ) = 0;   // reserv
  NumColumns_oper = NumColumns_oper + 1;
end;

Macro Scroll_operation_new(dlg)
                                                  
   /* Строим скроллинг  */
                                    
    cmd_scroll =  "SELECT *  FROM DGSSENDER_tmp  WHERE  t_action <> 2   AND t_action <> 11  and  t_accountId = " + ALG_DEPOSITOR.referenc ;    
    rs_scroll = RsdRecordSet(RsdCommand(cmd_scroll), RSDVAL_CLIENT, RSDVAL_STATIC);

    NumColumns_oper=0;
    AddColumn_oper( Columns_oper, NumColumns_oper, "t_payeeid",  "Global_Stream Код", 20, 2);
    AddColumn_oper( Columns_oper, NumColumns_oper, "t_lastname","Фамилия", 25,2 );
    AddColumn_oper( Columns_oper, NumColumns_oper, "t_firstname", "Имя",    15, 2 );
    AddColumn_oper( Columns_oper, NumColumns_oper, "T_PERSONALNO", "№ документа",  8, 2 );
                    
    AddScroll(dlg, rs_scroll, NumColumns_oper, Columns_oper, "EvProc_operScrlReg", true, true, 0);
    
end;

/////////////////////////////////////////////////////////////////////////////////////
/* Листалка  Виды  соглашений */                                                  //
                                                                                 //
//////////////////////////////////////////////////////////////////////////////////

var NumColumns_aggr, Columns_aggr = TArray();
var querry, ins_querry;

 /*Обработка клавиш в скролинге операций*/
macro EvProc_oper_type (rs, cmd, id, key )
private var clnt = TClientList,
            dul;

  clnt.GetRecord(ALG_DEPOSITOR.CodClient);
  dul  = clnt.CurRec.rec.PaperSeries + " " + clnt.CurRec.rec.PaperNumber ;


   if(cmd == DLG_KEY)                                                      
     if (key == Enter)
        global_type_card = rs.fld("t_id").value;
        global_tc_name   = rs.fld("t_name").value;
        update_TMP (global_type_card, dul, ALG_DEPOSITOR.referenc );      // обновить в отправителях  вид  соглашения
        return CM_CANCEL;

     elif (key == Esc)
        return  CM_cancel;

     end;
    end;
  end;
   
 /*Обработка заголовков столбцов*/
Macro AddColumn_aggr(ar,ind, fld, head, wdth, edit )
  
  ar.value( ind * 6 + 0 ) = fld;  
  ar.value( ind * 6 + 1 ) = head;
  NumColumns_aggr =  NumColumns_aggr + 1;

end;

 
Macro ScrollCardAggrList() 

  NumColumns_aggr = 0;
    
  private var cmd_aggr = "", rs_aggr = "";
  private var RunScroll_Title      = "Виды  соглашений";
  private var RunScroll_StatusLine = "~Enter~ Выбор  ~Esc~ Выход";

  cmd_aggr = "SELECT *  FROM dGSAgrTempl_tmp"; 
  rs_aggr = RsdRecordSet(RsdCommand(cmd_aggr), RSDVAL_CLIENT, RSDVAL_STATIC);
 
    /* Строим скроллинг  */
    
    AddColumn_aggr( Columns_aggr, NumColumns_aggr, "t_id",  "ID шаблона", 10,2);
    AddColumn_aggr( Columns_aggr, NumColumns_aggr, "t_name","Наименование",10 ,2 );
    AddColumn_aggr( Columns_aggr, NumColumns_aggr, "t_periodLimit", "Период (Сутки) ",6, 2 );
    AddColumn_aggr( Columns_aggr, NumColumns_aggr, "t_periodAmountLimit", "Общая сумма за период", 5, 2 );
    AddColumn_aggr( Columns_aggr, NumColumns_aggr, "t_tranAmountLimit", "MAX сумма платежа",  5, 2 );
    AddColumn_aggr( Columns_aggr, NumColumns_aggr, "t_tranLimit", "MAX кол-во платежей за период", 5, 2 );  

    RunScroll (rs_aggr, NumColumns_aggr, Columns_aggr, null, "EvProc_oper_type", RunScroll_Title, RunScroll_StatusLine, true, 0, 1, 75, 15)
 
end;  

//////////////////////////////////////////////////////////////////
 /* Обработчик скроллинга стран для указания гражданства */    //
                                                              //
///////////////////////////////////////////////////////////////

var  NumColumns_doc,  Columns_doc = TArray();

Macro AddColumn_doc( ar,ind, fld, head, wdth, edit )
  ar.value( ind * 6 + 0 ) = fld;  
  ar.value( ind * 6 + 1 ) = head;
  ar.value (ind * 6 + 2) = wdth;
  ar.value (ind * 6 + 3 ) = 0;   // fldType
  ar.value (ind * 6 + 4 ) = -1;  // decPoint
  ar.value (ind * 6 + 5 ) = 0;   // reserv
  NumColumns_doc = NumColumns_doc + 1;
end;



 Macro evProc_country(rs, cmd, id, key)
  private VAR PRM_INF;
  private VAR str, urs;
   if(cmd == DLG_KEY)                                                      
     if (key == Enter)
        global_name_country = rs.fld("t_codelat2").value;
        return CM_CANCEL;
     elif (key == Esc)
       return  CM_DEFAULT;
      
     end;

   end;
 end;

 /* Скроллинг видов документов */
 Macro ScrollCountryList()

  NumColumns_doc = 0;
    
  private var cmd = "", rs = "";
  private var RunScroll_Title      = "Страны";
  private var RunScroll_StatusLine = "~Enter~ Выбор  ~Esc~ Выход";

 
  AddColumn_doc( Columns_doc, NumColumns_doc, "t_codelat3", "Код"          , 3, 2 );
  AddColumn_doc( Columns_doc, NumColumns_doc, "t_name",     "Наименование" , 15, 2 );
  
  cmd = RsdCommand ("select * from dcountry_dbt");
  rs  = RsdRecordSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC );
 
  RunScroll (rs, NumColumns_doc, Columns_doc, null, "evProc_country", RunScroll_Title, RunScroll_StatusLine)
      
 end;     


private macro CheckAddSender(dlg): bool
  var ok = false;
  var field = 0;
  var msg = "";

  if (not dlg.type_card)
    field = 0;
    msg = "Выберите вид соглашения!";
  elif (not dlg.last_name)
    field = 3;
    msg = "Укажите фамилию отправителя!";
  elif (not dlg.first_name)
    field = 4;
    msg = "Укажите имя отправителя!";
  elif (not dlg.dul)
    field = 5;
    msg = "Заполните Номер документа!";
  elif (dlg.birthday == date(0, 0, 0))
    field = 6;
    msg = "Укажите дату рождения отправителя!";
  elif (not dlg.country)
    field = 7;
    msg = "Укажите гражданство отправителя!";
  /*elif (not dlg.adres)
    field = 8;
    msg = "Укажите адрес проживания отправителя!";*/
  else
    ok = true;
  end;

  if (not ok)
    msgbox(msg);
    SetFocus(dlg, field); 
  end;

  return ok;
end;


/////////////////////////////////////////////////////////////////////////////
                                                                          //
/* Обработчик Форма  отправители*/                                       //
                                                                        //
/////////////////////////////////////////////////////////////////////////

macro EvProcParam_Sender(dlg, cmd, id, key )

 private var text, ins ;
 private var  cmd_enter = "", rs_enter = "";
 private var date_b :date;

  macro IsRecCreate(dul_)
    private var  rs_rec, cmd_rec;
       cmd_rec = RsdCommand ("SELECT * from dGSSender_tmp  where T_PERSONALNO = ?");
       cmd_rec.AddParam("", RSDBP_IN, dul_);
       cmd_rec.NullConversion = true;
       rs_rec  = RsdRecordSet(cmd_rec, RSDVAL_CLIENT, RSDVAL_STATIC );
       if(rs_rec and rs_rec.movenext)
          return  true ;
        else
          return  false ;
       end;
  end;

  date_b = "00.00.0000" ;     // 	Обулить при  повторном  открытии фомы  на  Ф9 
  
  Message("~F3~ - Список  ~F9~ - Сохранить   ~Del~  - Очистить   ~Esc~ - Выход"); 

  if (cmd == DLG_PREINIT)
    
    dlg.date_reg = {curdate};
        
    cmd_enter = RsdCommand ("SELECT * from dGSSender_tmp  where T_ID = ?");
    cmd_enter.NullConversion = true;

    cmd_enter.AddParam("", RSDBP_IN, T_id_cur_rec);
    
    rs_enter  = RsdRecordSet(cmd_enter, RSDVAL_CLIENT, RSDVAL_STATIC );
   
    if(rs_enter and  rs_enter.Movenext)
         dlg.type_card  = rs_enter.value("T_AGRTEMPLATEID");
         dlg.name_card  = global_tc_name;
         dlg.code_auto  = rs_enter.value("T_TELCODE");
         dlg.last_name  = rs_enter.value("T_LASTNAME");
         dlg.first_name = rs_enter.value("T_FIRSTNAME");
         dlg.dul        = rs_enter.value("T_PERSONALNO");
         dlg.birthday   = rs_enter.value("T_BIRTHDATE");
         dlg.country    = rs_enter.value("T_CNTRY");
         dlg.adres      = rs_enter.value("T_ACTUALADDRESS");
         dlg.home_phone = rs_enter.value("T_FIXEDPHONE");
         dlg.mobile_phone = rs_enter.value("T_MOBILEPHONE");
         dlg.adres_prop   = rs_enter.value("T_ADDRESS");
         dlg.date_reg  =   rs_enter.value("T_REGISTRATIONDATE");
         dlg.payeeid   =   rs_enter.value("T_PAYEEID");
      
         disablefields(dlg,5); 
    else 
         dlg.type_card  = "";
         dlg.name_card  = "";
         dlg.code_auto  = "";
         dlg.last_name  = "";
         dlg.first_name = "";
         dlg.dul        = "";
         dlg.birthday   = date_b;
         dlg.country    = "";
         dlg.adres      = "";
         dlg.home_phone = "";
         dlg.mobile_phone = "";
         dlg.adres_prop = "";
         dlg.payeeid   =  "";

    end;
        UpdateFields(dlg);
      
  elif ((cmd == DLG_KEY) and (key == F3))     
 
    if(id == 7)
      ScrollCountryList(); 
      dlg.country = global_name_country;

    elif(id == 0)
       ScrollCardAggrList(); 
       dlg.type_card =  global_type_card;
       dlg.name_card  = global_tc_name;
       updatefields(dlg);
    end; 

  elif ((cmd == DLG_KEY) and (key == KEY_DEL)) 
    if(id==7)
      dlg.country = "";
    end;
  elif ((cmd == DLG_KEY) and (key == F9))  
      if (not CheckAddSender(dlg))
         return cm_ignore;
      end;
     
   var cmd_ent, rs_ent;
    cmd_ent = RsdCommand ("SELECT * from dGSSender_tmp  where T_ID = ?");
    cmd_ent.NullConversion = true;

    cmd_ent.AddParam("", RSDBP_IN, T_id_cur_rec);
    
    rs_ent  = RsdRecordSet(cmd_ent, RSDVAL_CLIENT, RSDVAL_STATIC );
   
    if(rs_ent and  rs_ent.Movenext)
      if ( rs_ent.value("T_PERSONALNO") == dlg.dul ) 
           msgbox("Запись уже введена, нажмите Esc");
           return cm_ignore;
      end;
    else
      Insert_in_TMP (0,ALG_DEPOSITOR.referenc, dlg.type_card, dlg.code_auto, dlg.last_name, dlg.first_name, dlg.dul,
                     date(dlg.birthday), dlg.country,dlg.adres, dlg.home_phone, dlg.mobile_phone, dlg.adres_prop, date(dlg.date_reg),
                     dlg.payeeid, 0); 
    
    end; 
  elif ((cmd == DLG_KEY) and (key == Esc))
       if (GetTRUE (TRUE," Запись была введена/изменена. Сохранить?"))
       
         if (CheckAddSender(dlg))
           if(IsRecCreate (dlg.dul))
              /* добавить проверку на  заполненность полей */
              var rs_up, cmd_up;                                    
                                                                 
               rs_up =    " UPDATE dGSSender_tmp        " +               
                          " SET T_AGRTEMPLATEID   =  ?, " +     
                          "     T_TELCODE         =  ?, " +      
                          "     T_LASTNAME        =  ?, " +      
                          "     T_FIRSTNAME       =  ?, " +      
                          "     T_PERSONALNO      =  ?, " +      
                          "     T_BIRTHDATE       =  ? ," +            
                          "     T_CNTRY           =  ?, " +      
                          "     T_ACTUALADDRESS   =  ?, " +      
                          "     T_FIXEDPHONE      =  ?, " +      
                          "     T_MOBILEPHONE     =  ?, " +        
                          "     T_ADDRESS         =  ?, " +  
                          "     T_REGISTRATIONDATE=  ?, " +
                          "     T_PAYEEID         =  ?  " +  
                          " WHERE   T_PERSONALNO = ? " ;

               cmd_up =  RSDCommand(rs_up);
               cmd_up.NullConversion = true;
               cmd_up.AddParam("", RSDBP_IN, dlg.type_card);
               cmd_up.AddParam("", RSDBP_IN, dlg.code_auto); 
               cmd_up.AddParam("", RSDBP_IN, dlg.last_name);         
               cmd_up.AddParam("", RSDBP_IN, dlg.first_name);
               cmd_up.AddParam("", RSDBP_IN, dlg.dul);       
               cmd_up.AddParam("", RSDBP_IN, dlg.birthday);     
               cmd_up.AddParam("", RSDBP_IN, dlg.country);                      
               cmd_up.AddParam("", RSDBP_IN, dlg.adres);                        
               cmd_up.AddParam("", RSDBP_IN, dlg.home_phone);                   
               cmd_up.AddParam("", RSDBP_IN, dlg.mobile_phone);                 
               cmd_up.AddParam("", RSDBP_IN, dlg.adres_prop);            
               cmd_up.AddParam("", RSDBP_IN, dlg.date_reg);                
               cmd_up.AddParam("", RSDBP_IN, dlg.payeeid); 
               cmd_up.AddParam("", RSDBP_IN, dlg.dul);
               cmd_up.execute;              
                            
              return CM_Cancel;
           else
             Insert_in_TMP(0, ALG_DEPOSITOR.referenc, dlg.type_card, dlg.code_auto, dlg.last_name, dlg.first_name, dlg.dul,
                     date(dlg.birthday), dlg.country,dlg.adres, dlg.home_phone, dlg.mobile_phone, dlg.adres_prop, date(dlg.date_reg),
                     dlg.payeeid, 0 );
           end;
         else
            return cm_ignore;
         end;
       end;
    
  end;
end;


 /*Обработка клавиш в скролинге отправители на  форме  Регистрация */

macro EvProc_operScrlReg (rs, cmd, id, key )

 var CM_FLAG = CM_DEFAULT; 

   Message("~F9~ - Ввод   ~Enter~ - Редактировать   ~F8~ - Удалить   ~F5~ - Переход   ~Esc~ - Выход"); 
   
   if ((cmd == DLG_KEY) and (key == F9) )
     
      T_id_cur_rec  = 0;

      RunDialog (dlgSender, "EvProcParam_Sender");
      
      CM_FLAG = CM_UPDATE_ADDSCROLL;

   elif ((cmd == DLG_KEY) and (key == Enter)) 
      T_id_cur_rec = rs.value("T_ID");
     
      RunDialog (dlgSender, "EvProcParam_Sender");
           
   end;
   return CM_FLAG;

end;   


/*Проверка на наличие владельца счета в таблице отправителей */

macro IsInsertOnSender()

private var  cmd, rs , str_personalNo = "";
private var clntcode = TClientList;
  
clntcode.GetRecord(ALG_DEPOSITOR.CodClient);
str_personalNo = clntcode.CurRec.rec.PaperSeries +" "+ clntcode.CurRec.rec.PaperNumber;


 cmd = RsdCommand (" SELECT 1 FROM DUAL WHERE EXISTS       " +
            "  (SELECT *                            " +
            "   FROM dgssender_tmp	            " +
            "   WHERE t_accountid = ?               " +
            "   AND t_personalNo  = ?               " + 
            "   AND t_cntry = ?                     " +
            "   AND t_action <> 2                   " +
            "   AND t_action <> 11)                 " );


 cmd.AddParam("", RSDBP_IN, ALG_DEPOSITOR.account);
 cmd.AddParam("", RSDBP_IN, str_personalNo); 
 cmd.AddParam("", RSDBP_IN, getCountryCode2(clntcode.CurRec.rec.Country));

 
 rs  = RsdRecordSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC );

 if(rs and rs.movenext) // если вернул 1  владелец счета уже был добавлен 
   return true;
   else
   return false;   // разрешить установку признака
         
 end;
end;

//////////////////////////////////////////////////
/* Обработчик Регистрации  клиента              */
//////////////////////////////////////////////////

macro EvProcParam (dlg, cmd, id, key )

private var stat = CM_DEFAULT;
private var clnt = TClientList; 
private var t_id, dul;


 if(cmd == DLG_PREINIT)
 
   Scroll_operation_new(dlg);
 
 elif(cmd == DLG_INIT)

   dlg.account = ALG_DEPOSITOR.account;
   dlg.otp = OTP;
   dlg.type_card = global_type_card;
   dlg.name_card = global_tc_name;


   if (clnt.GetRecord(ALG_DEPOSITOR.CodClient))  // поиск  кода  клиента  для  заполнения ФИО
     dlg.fio =  clnt.CurRec.rec.Name1 + " " 
               +clnt.CurRec.rec.Name2 + " "  
               +clnt.CurRec.rec.Name3;    
   end;
   
   if( IsInsertOnSender())  //   если  клиент введен  в базу  отправителей
       disablefields(dlg,2);
   end;

   UpdateFields(dlg);
  
 elif ((cmd == DLG_KEY) and (key == F3))

     if ((id == 0 ) and (dlg.otp == "X"))
       
       ScrollCardAggrList(); 
    
       dlg.type_card = global_type_card;
       dlg.name_card = global_tc_name;
       UpdateFields(dlg);

     end; 
  
 elif ((cmd == DLG_KEY) and (key == F2))
     
      return CM_SAVE;
   
 elif ((cmd == DLG_KEY) and (key == SPACE))
        	
     if (id == 2)
       clnt.GetRecord(ALG_DEPOSITOR.CodClient);
       
       dul  = clnt.CurRec.rec.PaperSeries + " " + clnt.CurRec.rec.PaperNumber ;
        
        if (dlg.otp == "")
           OTP =  "X";
           dlg.otp = OTP;
           
           ScrollCardAggrList(); 

           Insert_in_TMP (0, ALG_DEPOSITOR.referenc, global_type_card, "", clnt.CurRec.rec.Name1, clnt.CurRec.rec.Name2, dul, clnt.CurRec.rec.Born,
                          getCountryCode2(clnt.CurRec.rec.Country), clnt.CurRec.rec.Address,"", "", clnt.CurRec.rec.Address, {curdate}, "", 0 ); 
                    
           
               
           dlg.type_card = global_type_card;
           dlg.name_card = global_tc_name;
           UpdateFields(dlg);
        
           return  CM_UPDATE_ADDSCROLL;
             
        else
           OTP =  "";
           dlg.otp = OTP;
           
           Del_from_TMP(dul, ALG_DEPOSITOR.referenc);  // если пробел сняли, то  удалить из  скроллинга владельца счета

           global_type_card = 0;
           dlg.type_card = global_type_card;
           global_tc_name = "";
           dlg.name_card = global_tc_name;
        

           return CM_UPDATE_ADDSCROLL;
        end; 
      end;   
      
  elif ((cmd == DLG_KEY) and (key == KEY_DEL))
     if(id == 0)
       dlg.type_card = "";
       dlg.name_card = "";
     end;

  elif ((cmd == DLG_KEY) and (key == Esc))
    
    return  cm_cancel;
  end;

   UpdateFields(dlg);
  
end;  
/////////////////////////////////////////////////////////////////////////////////////

 /*Обработка клавиш в скролинге операций*/
macro EvProc_ScrSender (rs, cmd, id, key )

   if(cmd == DLG_KEY)  
                                                   
     if (key == f8)
        global_t_id = rs.value("t_id");
        result = global_t_id;
        return CM_cancel;

     elif (key == Esc)
        result = 0;
        return  CM_cancel;
     
     end;
   end;
end;

Macro  main_proc_del()

  NumColumns_oper = 0;
    
  private var cmd_sender = "", rs_sender = "";
  private var RunScroll_Title      = "Отправители GLOBAL STREAM ";
  private var RunScroll_StatusLine = "~Enter~  Просмотр  ~F8~  Удалить  ~Esc~  Выход";

  cmd_sender = "SELECT *  FROM DGSSENDER_DBT  WHERE  t_action <> 2   AND t_action <> 11 and  t_accountId = " + ALG_DEPOSITOR.referenc ;

  rs_sender = RsdRecordSet(RsdCommand(cmd_sender), RSDVAL_CLIENT, RSDVAL_STATIC);

  NumColumns_oper=0;
  AddColumn_oper( Columns_oper, NumColumns_oper, "t_payeeid",  "Global_Stream Код", 20, 2);
  AddColumn_oper( Columns_oper, NumColumns_oper, "t_lastname","Фамилия", 25,2 );
  AddColumn_oper( Columns_oper, NumColumns_oper, "t_firstname", "Имя",    15, 2 );
  AddColumn_oper( Columns_oper, NumColumns_oper, "T_PERSONALNO", "№ документа",  8, 2 );   

  RunScroll (rs_sender, NumColumns_oper, Columns_oper, null, "EvProc_ScrSender", RunScroll_Title, RunScroll_StatusLine, true, 0, 1, 100, 15);
 
end;

macro EvProcParam_AddSender(dlg, cmd, id, key )

 Message("~F3~ - Список  ~F9~ - Сохранить   ~Del~  - Очистить   ~Esc~ - Выход"); 

 if (cmd == DLG_INIT)
     dlg.date_reg = {curdate};
     UpdateFields(dlg);
 
 elif ((cmd == DLG_KEY) and (key == F3))     
 
    if(id == 7)
      ScrollCountryList(); 
      dlg.country = global_name_country;

    elif(id == 0)
       ScrollCardAggrList(); 
       dlg.type_card =  global_type_card;
       dlg.name_card  = global_tc_name;
       updatefields(dlg);
    end; 

  elif ((cmd == DLG_KEY) and (key == KEY_DEL)) 
    if(id==7)
      dlg.country = "";
    end;
  elif ((cmd == DLG_KEY) and (key == F9))
     if (GetTRUE (TRUE,"Сохранить текущую запись ?"))
      if (CheckAddSender(dlg))
        Insert_in_TMP(0, ALG_DEPOSITOR.referenc, dlg.type_card, dlg.code_auto, dlg.last_name, dlg.first_name, dlg.dul,
                     date(dlg.birthday), dlg.country,dlg.adres, dlg.home_phone, dlg.mobile_phone, dlg.adres_prop, date(dlg.date_reg),
                     dlg.payeeid, 0 );
      else
         return cm_ignore;
      end;
     end;
     return cm_save;
  end;
end;
 
////////////////////////////////////////////
// ТОЧКА ВЫЗОВА ПАНЕЛИ РЕГИСТРАЦИИ       //
//////////////////////////////////////////

Macro showRegisterClientPanel() 
 Trunc_Sender();
 if (RunDialog (dlg, "EvProcParam", F5))
   return F2; 
 else
   return Esc;
 end;
end;

/////////////////////////////////////////////////////////
// ТОЧКА ВЫЗОВА СКРОЛА ОТПРАВИТЕЛЕЙ НА  УДАЛЕНИЕ      //
///////////////////////////////////////////////////////

macro showDeleteSendersPanel()
   main_proc_del();
   return result;
end;

/////////////////////////////////////////////////////////
// ТОЧКА ВЫЗОВА ФОРМЫ ОТПРАВИТЕЛЕЙ НА ВСТАВКУ         //
///////////////////////////////////////////////////////

macro showAddSendersPanel()
 Trunc_Sender();
 if(RunDialog (dlgSender, "EvProcParam_AddSender"))
   return F9;
 else
   return Esc;
 end;
end;

