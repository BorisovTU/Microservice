/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Определение курса валюты за число                                       *
*                                                                          *
*  Лебедев С.В.                                                            *
*  18.07.2006                                                              *
\**************************************************************************/

import DeprIntr, cur_rate, RSD, sqlconv;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro defineResidentPayDoc( payDoc )

  var ClientList = TClientList;
  var retValue = payDoc.FlagRez;

  if ( payDoc.CodClient > 0 )
    if ( ClientList.GetRecord( payDoc.CodClient ) )
      retValue = ClientList.CurRec.Rec.NotResident;
    else
      retValue = "";
    end;
  end;

  return retValue;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro defineSumma ( inSumma, codeCurInSumma, dateDoc )

  var cbRate = 1.0;
  var usdCbRate = 1.0;
  var retSumma = $0L;

  if ( codeCurInSumma == 0 )
    FindCurrRate( 1, 1 /* Основной курс*/, dateDoc, usdCbRate, null, null, null );
    retSumma = Round( Money( inSumma / usdCbRate ), 2 );
  elif ( codeCurInSumma == 1 )
    retSumma = inSumma;
  elif ( codeCurInSumma > 1 )
    FindCurrRate( codeCurInSumma, 1 /* Основной курс*/, dateDoc, cbRate, null, null, null );
    FindCurrRate( 1, 1 /* Основной курс*/, dateDoc, usdCbRate, null, null, null );
    retSumma = Round( Money( inSumma * cbRate / usdCbRate ), 2 );
  end;

  return retSumma;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro CheckOverOther5000 ( payDoc )

  var retValue = true;
  var summaOut = $0L;
  var cmd;
  var rs;
  var summaDoc;
  var codCurDoc;

//  if ( ( payDoc.IsCur == 1 ) and ( payDoc.GroupOpert == 11 ) )
  if (  payDoc.GroupOpert == 11  )
     if ( (payDoc.NumOpert == 20) or (payDoc.NumOpert == 42) )
       if ( defineResidentPayDoc( payDoc ) == "" )
         summaOut = defineSumma( payDoc.InSum, payDoc.CodCur, payDoc.Date_Document );

         if ( payDoc.CodClient > 0 )
           cmd = RsdCommand( "select pay_doc.t_InSum InSum, pay_doc.t_CodCur CodCur " + 
                             "from dpay_doc_dbt pay_doc " + 
                             //"where pay_doc.t_IsCur = 1 and pay_doc.t_GroupOpert = 11 and pay_doc.t_NumOpert in ( 20, 42 ) and pay_doc.t_Date_Document = ? and pay_doc.t_CodClient = ?" );
                             "where pay_doc.t_GroupOpert = 11 and pay_doc.t_NumOpert in ( 20, 42 ) and pay_doc.t_Date_Document = ? and pay_doc.t_CodClient = ?" );

           cmd.AddParam( "dateDoc", RSDBP_IN, payDoc.Date_Document );
           cmd.AddParam( "codeClient", RSDBP_IN, payDoc.CodClient );

           rs  = RsdRecordSet( cmd );

           while ( rs.MoveNext )
             codCurDoc = rs.Value( "CodCur" );
             summaDoc = rs.Value( "InSum" );

             summaOut = summaOut + defineSumma( summaDoc, codCurDoc, payDoc.Date_Document );
           end;
         end;
       end;
     end;
  end;

  if ( summaOut > 5000 )
    retValue = false;
  end;

  return retValue;

end;
