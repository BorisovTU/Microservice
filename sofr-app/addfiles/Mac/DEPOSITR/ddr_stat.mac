/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Справка о состоянии вклада(ов).

*****************************************************************/

import DeprIntr, OperCode, IsSrvDoc, GetDocs, ExchangeInter, Календарь, SqlConv;

var
/*  fil = TBFile( "listfdep.dbt", "r", 0 ),*/
  dtp = TBFile( "sb_dtyp.dbt",  "r", 2 ),
  opr = TBFile( "person.dbt",   "r", 0 ),
  cur = TBFile( "currency.dbt", "r", 0 ),
  dep = TBFile( "depositr.dbt", "r", 8 ),
  doc = TBFile( "sbdepdoc.dbt", "r", 6 ),
  sop = TBFile( "suboper.dbt",  "r", 1 ),
  trs = TBFile( "sbtrast.dbt",  "r", 0 ),
  dln = TBFile( "ddr_link.dbt", "r", 0 );

var depclnt = TClientList; /* Справочник вкладчиков */

/* Константы названий для коммерческих банков */
const cTypeBank = "Коммерческий банк",
      cNumForm  = "",
      cKontr    = "Проверяющий",
      cZav      = "Упр.филиалом";

const cRub = 0;  /* Код Российского рубля */
const NatISO = "рублях";  /* Название национальной валюты */
const NatRub = " руб. ", NatKop = " коп.";

/* Значения doc->TypeOper для операций открытия счета */
const OP_OPENCAS =  1; /* наличными */
const OP_OPENNCS = 51; /* безналичными */
const OP_OPENTRN = 58; /* переводом */

/* Значения doc->TypeOper для операций закрытия счета - выплаченная сумма */
const OP_OUT         =  4;  /* Расход */
const OP_GETCON      = 62;  /* -------- простое списание */
const OP_GETPOR      = 63;  /* -------- по поручению (разовому) */
const OP_GETLPR      = 64;  /* -------- по поручению длительному */
const OP_GETIN       = 65;  /* -------- переводом вклада */
const OP_GETOWN      = 66;  /* -------- в свой филиал */
const OP_GETALIEN    = 67;  /* -------- в чужой филиал */
const OP_CONVERT     = 80;  /* -------- б\н покупка или конверсия валют */
const OP_CHDTYPE     = 78;  /* Перевод в другой вид вклада */
const OP_WILL_GIVE   = 99;  /* Выдача наследства */

const TRAST_DOC = "\x00";  /* Признак доверенности в sbtrast.dbt */

const DateNull = date( 0, 0, 0 );

private var {Name_Bank};

var Branch = NumFnCash();

macro GetQuart

  var
    Q,
    Year,
    Mon;

  DateSplit( doc.rec.Date_Document, null, Mon, Year );

  if ( Mon == 3 )
    Q = 1;
  elif ( Mon == 6 )
    Q = 2;
  elif ( Mon == 9 )
    Q = 3;
  else
    Q = 4;
  end;
  return Q + "-й квартал " + Year + " года";
end;

/*****************************************************************
               Определение кода национальной валюты
*****************************************************************/
macro NatCur()

  var retCur;

  cur.KeyNum = 1;
  cur.rec.ExternalCode = {NationalCur};
  if ( cur.getEQ )
    retCur = cur.rec.Code_Currency;
  else
    retCur = -1;
  end;

  cur.KeyNum = 0;

  return retCur;

end;

/*****************************************************************
                    Получение подоперации
******************************************************************/
macro GetSubOper
  var SubOp;
  if ( ( doc.rec.TypeOper != 0 )  AND  ( doc.rec.ApplType != 0 ) )
    sop.rec.IsCur    = doc.rec.IsCur;
    sop.rec.Kind     = doc.rec.Type_Account;
    sop.rec.OperType = doc.rec.TypeOper;
    sop.rec.Type     = doc.rec.ApplType;
    if(sop.GetEQ)
      SubOp= " (" + sop.rec.Name + ")";
    else
      SubOp= "";
    end;
  else
    SubOp="";
  end;
  return SubOp;
end;

/*****************************************************************
                     Строковый формат даты
******************************************************************/
macro DateToStr( pDate )

  var d, m, y, Day, Month, Year;

  DateSplit( pDate, d, m, y );
  Day = String( d );

  if   (m ==  1) Month = " января ";
  elif (m ==  2) Month = " февраля ";
  elif (m ==  3) Month = " марта ";
  elif (m ==  4) Month = " апреля ";
  elif (m ==  5) Month = " мая ";
  elif (m ==  6) Month = " июня ";
  elif (m ==  7) Month = " июля ";
  elif (m ==  8) Month = " августа ";
  elif (m ==  9) Month = " сентября ";
  elif (m == 10) Month = " октября ";
  elif (m == 11) Month = " ноября ";
  elif (m == 12) Month = " декабря ";
  else           Month = "  ";
  end;

  Year = String(y);

  return Day + Month + Year + " г.";

end;

/*****************************************************************
                   Преобразование к Ф.И.О.
******************************************************************/
macro StrFIO( fio )
  var n, s, str;

  s = Trim( fio );
  n = Index( s, " " );

  if ( n == 0 )
    str = s;
  else
    str = StrUpr( SubStr( s, 1, n ), 1 );
    s = SubStr( s, n+1 );
    str = str + StrUpr(SubStr( s, 1, 1 )) + ".";
    n = Index( s, " " );
    if ( n != 0 )
      str = str + StrUpr(SubStr( s, n+1, 1 )) + ".";
    end;
  end;

  return str;
end;

/*****************************************************************
                        Фильтр по записям
******************************************************************/
macro Filter( dc )
  var mustBePrinted = TRUE;
  if ( ( dc.rec.FlagStorn != StrFor(0) )  OR
       ( ( dc.rec.TypeOper == Перевод_В_Другой_Вид_Вклада )  AND
         dc.rec.ApplType ) )
    mustBePrinted = FALSE;
  end;
  return mustBePrinted;
end;

/*****************************************************************
            Вычисление числа дней с текущей даты
         до даты окончания хранения текущего срока.
*****************************************************************/
macro Calc_Days_Dep( vDateDep )

  var days;

  if ( vDateDep > {curdate} )
    days = NDays( {curdate}, vDateDep );
  else
    days = 0;
  end;

  /*
  var day1, month1, year1;
  var day2, month2, year2;
  var days, months, years;

  DateSplit( { curdate }, day1, month1, year1 );
  DateSplit( vDateDep,    day2, month2, year2 );

  years = year2 - year1;
  */

  return days;

end;

/*****************************************************************
    1. Информация о статусе вклада: для депозитных договоров
*****************************************************************/
macro DepositState( pDepDateEnd, pDepDaysEnd, pDepPcRate )
  var vDateDep;
  var vDaysDep;
  var vProcDep;
  var vObjectType;

  /*
  if ( dep.rec.FormContr == 0 )
    vDateDep = DateNull;
    vDaysDep = 0;
    vProcDep = 0.0;
  else */

    if ( dep.rec.End_DateDep != DateNull )
      vDateDep = dep.rec.End_DateDep;
    else
      if ( dep.rec.Prol_DateDep != DateNull )
        vDateDep = dep.rec.Prol_DateDep - 1;
      else
        vDateDep = DateNull;
      end;
    end;

    if ( ( vDateDep != DateNull )  AND  ( vDateDep > {curdate} ) )
      vDaysDep = Calc_Days_Dep( vDateDep );
      if ( dep.rec.UseAlternate )
        vObjectType = 2002;
      else
        vObjectType = 2001;
      end;

      vProcDep = PercRateAL( dep.rec.Referenc, vObjectType, {curdate} );
    else
      vDaysDep = 0;
      vProcDep = 0.0;
    end;

  /* end; */

  SetParm( 0, String( vDateDep ) );
  SetParm( 1, String( vDaysDep ) );
  SetParm( 2, String( vProcDep:a:0:2 ) );
end;

/*****************************************************************
       Получение суммы, выплаченной по вкладу при закрытии
*****************************************************************/
macro Get_Close_Sum()
  var vCloseSum = 0.0;
  var stat;

  doc.Clear;
  doc.rec.Referenc      = dep.rec.Referenc;
  doc.rec.Date_Document = Date(31,12,9999);
  doc.rec.NumDayDoc     = 32000;

  doc.addFilter( "t.t_referenc = " + string( dep.rec.Referenc ) +
                 " and t.t_typeoper in ( " + string( OP_OUT ) + ", " +
                                             string( OP_GETCON ) + ", " +
                                             string( OP_GETPOR ) + ", " +
                                             string( OP_GETLPR ) + ", " +
                                             string( OP_GETIN ) + ", " +
                                             string( OP_GETOWN ) + ", " +
                                             string( OP_GETALIEN ) + ", " +
                                             string( OP_CONVERT ) + ", " +
                                             string( OP_CHDTYPE ) + ", " +
                                             string( OP_WILL_GIVE ) + " )" );

  stat = doc.getLE;

  while ( stat  AND  ( doc.rec.Referenc == dep.rec.Referenc ) )
    if ( ( doc.rec.TypeOper == OP_OUT       )  OR  /* Расход */
         ( doc.rec.TypeOper == OP_GETCON    )  OR  /* -------- простое списание */
         ( doc.rec.TypeOper == OP_GETPOR    )  OR  /* -------- по поручению (разовому) */
         ( doc.rec.TypeOper == OP_GETLPR    )  OR  /* -------- по поручению длительному */
         ( doc.rec.TypeOper == OP_GETIN     )  OR  /* -------- переводом вклада */
         ( doc.rec.TypeOper == OP_GETOWN    )  OR  /* -------- в свой филиал */
         ( doc.rec.TypeOper == OP_GETALIEN  )  OR  /* -------- в чужой филиал */
         ( doc.rec.TypeOper == OP_CONVERT   )  OR  /* -------- б\н покупка или конверсия валют */
         ( doc.rec.TypeOper == OP_CHDTYPE   )  OR  /* Перевод в другой вид вклада */
         ( doc.rec.TypeOper == OP_WILL_GIVE ) )    /* Выдача наследства */
      vCloseSum = doc.rec.OutSum;
      stat = FALSE;
    else
      stat = doc.Prev;
    end;
  end;

  doc.dropFilter;

  return vCloseSum;
end;

/*****************************************************************
   2. Сведения о закрытии вклада
      ( если вклад закрыт в указанном периоде )
*****************************************************************/
macro CloseAccountInfo( pCloseDate, pCloseSum )
  var vCloseDate;
  var vCloseSum;

  vCloseDate = dep.rec.Close_Date;

  if ( dep.rec.Close_Date == DateNull )
    vCloseSum = 0.0;
  else
    vCloseSum = Get_Close_Sum();
  end;

  SetParm( 0, String( vCloseDate ) );
  SetParm( 1, String( vCloseSum:a:0:2 ) );
end;

/*****************************************************************
                      Цикл по доверенностям
*****************************************************************/
macro Get_Trasts( pDateStartStat, pDateFinishStat )
  var vTrastFIO, vTrastDate, vTrastDays;
  var vNumTrast = 0;
  var stat;

  trs.Clear;
  trs.rec.Referenc  = dep.rec.Referenc;
  trs.rec.VidDoc    = TRAST_DOC;

  trs.addFilter( "t.t_Referenc = " + string( dep.rec.Referenc ) +
                 " and t.t_VidDoc = " + sqlChar( TRAST_DOC ) );


  stat = trs.GetGE;

  if ( ( NOT stat )  OR
       ( trs.rec.Referenc != dep.rec.Referenc )  OR
       ( trs.rec.VidDoc   != TRAST_DOC ) )
    [ |                                |                           |                              |];
    return;
  end;

  while ( stat  AND
          ( trs.rec.Referenc == dep.rec.Referenc )  AND
          ( trs.rec.VidDoc   == TRAST_DOC    ) )

    if ( depclnt.GetRecord( trs.rec.CodClient ) )
      vTrastFIO = Trim( depclnt.CurRec.rec.Name1 ) + " " +
                  SubStr( Trim( depclnt.CurRec.rec.Name2 ), 1, 1 ) + "." +
                  SubStr( Trim( depclnt.CurRec.rec.Name3 ), 1, 1 ) + ".";
    else
      vTrastFIO = "";
    end;

    vTrastDate = String( trs.rec.CloseDate );

    if ( trs.rec.CloseDate > {curdate} )
      vTrastDays = NDays( {curdate}, trs.rec.CloseDate );
    else
      vTrastDays = 0;
    end;

    if ( ( trs.rec.DateOpen  <= pDateFinishStat )  AND
         ( trs.rec.CloseDate >= pDateStartStat ) )
      [ | ############################## | ######################### | ############################ |]
      ( vTrastFIO, vTrastDate, vTrastDays );
      vNumTrast = vNumTrast + 1;
    end;

    stat = trs.Next;

  end;

  trs.dropFilter;

  if ( vNumTrast == 0 )
    [ |                                |                           |                              |];
  end;

end;

/*****************************************************************
                  Основная процедура справки
******************************************************************/
macro CBK_Run( DeclID, Date1, Date2, sClient, sFIO_Receiv, sFIO_Owner,
               CodClnt, Standart, Archive )

  var Ref;

  var stat, rub, kop, sOpenSum, sFilial, sISO, sVklad, sPeriod,
      Sum, sDate, sOper, sOpen;

  var sTypeBank, sNumForm, sKontr, sZav;

  var ret;

  /* 1. Информация о статусе вклада: для депозитных договоров */
  var sDepDateEnd, sDepDaysEnd, sDepPcRate;

  /* 2. Сведения о закрытии вклада ( если вклад закрыт в указанном периоде ) */
  var sCloseDate, sCloseSum;

  /* 4. Информация о наличии доверенности */
  /* var sTrastFIO, sTrastDate, sTrastDays; */

  /* Определение кода национальной валюты */
  var cNatCur = NatCur();

  /* Подстановка названий, согласно стандарту работы банка */
  if ( Standart == 0 )
  /* Сбербанк РФ */
    sTypeBank = "Сбербанк России";
    sNumForm  = "Ф.№297";
    sKontr    = "Контролер";
    sZav      = "Зав.филиалом";
  else
  /* Коммерческий банк */
    sTypeBank = cTypeBank;
    sNumForm  = cNumForm;
    sKontr    = cKontr;
    sZav      = cZav;
  end;

  /* Чтение записи вкладчика */
  if ( NOT depclnt.GetRecord( CodClnt ) )
    MsgBox( "Не найдена запись вкладчика с CodClient = ", CodClnt );
    Exit( 0 );
  end;

  /* Чтение записи Операциониста */
  opr.rec.Oper = {oper};
  if ( opr.GetEQ )
    sOper = opr.rec.Name;
  else
    sOper = "???";
  end;
  /* Чтение записи о Филиале */
  if ( not findDepartment(Branch, sFilial) )
    sFilial = "???";
  end;

  /* Период справки */
  sPeriod = DateToStr( Date1 ) + " по " + DateToStr( Date2 );

  /* Печать общего заголовка выписки */
  [
     #                                                                #
     #
     Подразделение № #

                                 CПРАВКА О СОСТОЯНИИ ВКЛАДА(ОВ)
                   ##########################################################
                   ----------------------------------------------------------
                                 (указывается Ф.И.О. вкладчика)

                          за период с #

  ]
  ( sTypeBank,
    sNumForm,
    {Name_Bank},
    sFilial,
    sFIO_Owner:c,
    sPeriod );

  dln.rec.DeclID = DeclID;
  dln.rec.AccRef = 0;
  ret = dln.GetGE;
  if ( ( NOT ret ) OR  ( dln.rec.DeclID != DeclID ) )
    MsgBox( "Не найдена запись связи счета с заявлением ID = ", DeclID );
    Exit( 0 );
  end;

  /********************************************************/
  /*         Цикл по счетам, указанным в заявлении.       */
  /********************************************************/
  while ( ret  AND  ( dln.rec.DeclID == DeclID ) )

    Ref = dln.rec.AccRef;  /* Референс счета */

    /* Чтение записи счета */
    dep.rec.Referenc = Ref;
    if ( NOT dep.GetEQ )
      MsgBox( "Не найдена запись счета с Reference = ", Ref );
      Exit( 0 );
    end;

    /* Подключение архивов */
    /*
    if ( Archive and ( dep.rec.Limit_Date != DateNull ) and dep.rec.ArhFlag )
      GetDocsAndReopenTemp( dep.rec.Referenc, dep.rec.Open_Date, doc );
    end;
    */

    /* Чтение записи вида вклада */
    dtp.rec.FlagCur = dep.rec.IsCur;
    dtp.rec.Kind    = dep.rec.Type_Account;
    if ( NOT dtp.GetEQ )
      dtp.rec.Name = "???";
    end;
    /* Чтение названия валюты */
    if ( ( dep.rec.IsCur )  OR  ( cNatCur != cRub ) )
      cur.rec.Code_Currency = dep.rec.Code_Currency;
      if ( cur.GetEQ )
        sISO = cur.rec.Short_Name;
      else
        sISO = "???";
      end;
    else
      sISO = NatISO;  /* Национальная валюта */
    end;
    /* Поиск документа открытия счета */
    doc.Clear;
    doc.rec.Referenc = Ref;
    doc.rec.Date_Document = dep.rec.Open_Date;

    doc.addFilter( "t.t_referenc = " + string( Ref ) +
                   " and t.t_date_document = " + sqlDateToStr( dep.rec.Open_Date ) +
                   " and t.t_typeoper in ( " + string( OP_OPENCAS ) + ", " +
                                               string( OP_OPENNCS ) + ", " +
                                               string( OP_OPENTRN ) + " )" );


    stat = doc.getGE;
    if ( stat                                    AND
         ( doc.rec.Referenc == Ref )                 AND
         ( doc.rec.Date_Document >= dep.rec.Open_Date )  AND
         ( ( doc.rec.TypeOper == OP_OPENCAS )  OR
           ( doc.rec.TypeOper == OP_OPENNCS )  OR
           ( doc.rec.TypeOper == OP_OPENTRN ) ) )
      if ( ( dep.rec.IsCur == 0 )  AND  ( cNatCur == cRub ) )
        RubToStr( doc.rec.InSum, rub, kop );
        sOpenSum = String((Int(DoubleL(doc.rec.InSum))/100):a) +
                   NatRub + kop + NatKop;
      else
        sOpenSum = String(doc.rec.InSum:a) + " " + sISO;
      end;
    else
      sOpenSum = "_________________________";
    end;

    doc.dropFilter;

    sVklad = "\"" + dtp.rec.Name + "\" в " + sISO;
    sOpen  = DateToStr( dep.rec.Open_Date ) + " на сумму: " + sOpenSum;

    /* Печать заголовка выписки по отдельному счету */
    [
       Счет № ############################### по вкладу: #

    ]
    ( Acc_Form(dep.rec.Account):f, sVklad );

    /* 1. Информация о статусе вклада: для депозитных договоров */
    DepositState( sDepDateEnd, sDepDaysEnd, sDepPcRate );

    [
         1. Информация о статусе вклада
      +-------------------------+-----------------------+---------------------------+
      | Дата окончания хранения | Количество дней       | Годовая процентная ставка |
      | основного               | до истечения          | по вкладу, действующая    |
      | (пролонгированного)     | основного             | в течение текущего        |
      | срока                   | (пролонгированного)   | срока                     |
      |                         | срока хранения вклада |                           |
      +-------------------------+-----------------------+---------------------------+
      | ####################### | ##################### | ######################### |
      +-------------------------+-----------------------+---------------------------+]
    ( sDepDateEnd:c, sDepDaysEnd:c, sDepPcRate:c );

    /* 2. Сведения о закрытии вклада ( если вклад закрыт в указанном периоде ) */
    CloseAccountInfo( sCloseDate, sCloseSum );

    [
         2. Сведения о закрытии вклада (указываются, если вклад был закрыт за указанный период)
      +-----------------------------------------------+-----------------------------------------------+
      |                 Дата закрытия                 |                 Выплаченная сумма             |
      +-----------------------------------------------+-----------------------------------------------+
      | ############################################# | ############################################# |
      +-----------------------------------------------+-----------------------------------------------+]
    ( sCloseDate:c, sCloseSum:c );

    [
         3. Сведения о текущем состоянии вклада
      +-----------------------+-------------------------------------+-----------------+-----------------+
      |     Дата операции     |             Наименование            |  Сумма операции |     Остаток     |
      |                       |               операции              |                 |      вклада     |
      +-----------------------+-------------------------------------+-----------------+-----------------+];

    doc.Clear;
    doc.rec.Referenc = Ref;
    doc.rec.Date_Document = Date1;

    doc.addFilter( "t.t_referenc = " + string( Ref ) +
                   " and t.t_date_document between " + sqlDateToStr( Date1 ) +
                                             " and " + sqlDateToStr( Date2 ) );

    stat = doc.getGE;
    if ( stat  AND
         ( doc.rec.Referenc == Ref )         AND
         ( doc.rec.Date_Document >= Date1 )  AND
         ( doc.rec.Date_Document <= Date2 ) )

      while ( stat  AND
              ( doc.rec.Referenc == Ref )         AND
              ( doc.rec.Date_Document >= Date1 )  AND
              ( doc.rec.Date_Document <= Date2 ) )
        if ( IsServDoc( doc.rec )  AND  Filter( doc ) )
          Sum = doc.rec.InSum - doc.rec.OutSum;
          if ( doc.rec.Date_Document == doc.rec.DepDate_Document )
            sDate = String( doc.rec.Date_Document );
          else
            sDate = String( doc.rec.Date_Document ) + "/" +
                    String( doc.rec.DepDate_Document );
          end;
          if ( ( doc.rec.Mode == 1 ) and ( doc.rec.TypeOper == Зачисление_Процентов ) )
            [ | ########################################################### |                 |                 |]
            ( "Проценты за " + GetQuart + ":" );
          end;
          [ | ##################### | ################################### | ############### | ############### |]
          ( sDate, doc.rec.Ground+GetSubOper():w, Sum:a:15:2, doc.rec.Rest:a:15:2 );
        end;
        stat = doc.Next;
      end;
      [ +-----------------------+-------------------------------------+-----------------+-----------------+];

    else
      [ |                       |                                     |                 |                 |
        +-----------------------+-------------------------------------+-----------------+-----------------+];

      println( "  \n    Документов за период с ", Date1,
                                          " по ", Date2, " не найдено." );
    end;

    [
         4. Информация о наличии доверенности (указывается при ее оформлении )
      +--------------------------------+---------------------------+------------------------------+
      |    Ф.И.О. лица, на которого    |  Дата окончания действия  | Количество дней до истечения |
      |     оформлена доверенность     |         доверенности      | срока действия доверенности  |
      +--------------------------------+---------------------------+------------------------------+];

    Get_Trasts( Date1, Date2 );

    [ +--------------------------------+---------------------------+------------------------------+];

    [
         5. Дополнительная информация
      +---------------------------------------------------------------------------------------+
      |                                                                                       |
      |                                                                                       |
      +---------------------------------------------------------------------------------------+

    ];

    ret = dln.Next();

  end;

  [
     Выписка составлена по запросу #

     -------------------------------------------------------------------------------------

                                           #_________________________ #



    #

                                   М. П.
  ]
  ( "(" + StrLwr( sClient ) + "): " + sFIO_Receiv,  /* StrFIO(sFIO_Receiv), */
    sKontr, StrFIO(sOper),
    /* sZav, StrFIO(fil.rec.BranchChief), */
    DateToStr(Date()) );

end;

/*****************************************************************
             Финальная подпись при получении справки.
*****************************************************************/
macro FinishSign( Date1, Date2 )
  [


         Справку за период с ########## по ########## по вкладам,
         указанным в заявлении ф.№ 298, получил.


                                                     -------------------
                                                          (Подпись)

                                                 ############################]
  ( String( Date1 ), String( Date2 ), String( {curdate}:m ):c );
end;

/*****************************************************************
          Вызываемая из исполняемого модуля процедура.
*****************************************************************/
macro CBK_Main( DeclID, Date1, Date2, sClient, sFIO_Receiv, sFIO_Owner,
                CodClnt, Standart, Archive, Period, sControlInfo )

  if ( OpenDepFiles() )
    CBK_Run( DeclID, Date1, Date2, sClient, sFIO_Receiv, sFIO_Owner,
             CodClnt, Standart, Archive );
    printLn( strFor( 12 ) );
    CBK_Run( DeclID, Date1, Date2, sClient, sFIO_Receiv, sFIO_Owner,
             CodClnt, Standart, Archive );
    FinishSign( Date1, Date2 );
    CloseDepFiles();
  else
    MsgBox( "Ошибка при открытии файлов для работы" );
  end;

end;
