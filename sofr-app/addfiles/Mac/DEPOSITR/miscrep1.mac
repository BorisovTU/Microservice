import BrParm, acc_form;

record MiscDocInfo("ci_doc.2");
record MiscRecInfo("ci_rec.2");

var
  M_TotNumItems=0,
  M_TotSum=$0,
  M_TotSumSold=$0,
  M_TotItemsSold=0,
  M_TotSumBought=$0,
  M_TotItemsBought=0;

const
  CI_MISC=10;

const
  O_MISC_SELL=1,
  O_MISC_BUY=2;

macro M_OutHead;

  [
                                                                               Филиал №
                                                                               отделения
                                                                               Сбербанка РФ


                 Отчет по операциям по облигациям целевого займа за ##########

  ]
  (
    {curdate}
  );
end;

macro M_OutCommonHead;

  [+-------+----------------------------------------------+--------+-------------------+];
  [|   №   |               Н а з в а н и е                | Колич. |     С у м м а     |];
  [+-------+----------------------------------------------+--------+-------------------+];
end; 

macro M_OutSellHead;

  [ ];
  [Продажа облигаций целевого займа];
  [ ];
  M_OutCommonHead;
end;

macro M_GetNumItems

  var
    RecFound,
    NumItems=0;

  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;

  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    if ( CheckCIDoc )
      SetRecordAddr(MiscDocInfo,CapIssueDoc,0,
        FldOffset(CapIssueDoc,"Info"),true);
      NumItems=NumItems+MiscDocInfo.NumItems;
    end;
    RecFound=Next(CapIssueDoc);
  end;
  return NumItems;
end;

macro M_OutBuyHead;

  [ ];
  [Покупка облигаций целевого займа];
  [ ];
  M_OutCommonHead;
end;

macro M_OutLine;

  SetRecordAddr(MiscRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [| ##### | ############################################ | ###### | ################# |]
  (
    CapIssueRec.Issue,
    MiscRecInfo.Name,
    M_GetNumItems,
    CapIssueRec.Sum
  );
end;

macro M_OutTotals;

  SetRecordAddr(MiscRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [+------------------------------------------------------+--------+-------------------+];
  [|  И т о г о :                                         | ###### | ################# |]
  (
    M_TotNumItems,
    M_TotSum
  );
end;

macro M_SellReport

  var
    RecFound;

  M_TotNumItems=0;
  M_TotSum=$0;

  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_MISC;

  M_OutSellHead;
  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_MISC))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Brigade==OperBrigade)
      and (CapIssueRec.Operation==O_MISC_SELL))
      M_OutLine; 
      SetRecordAddr(MiscRecInfo,CapIssueRec,0,
        FldOffset(CapIssueRec,"Info"),true);
      M_TotNumItems=M_TotNumItems+M_GetNumItems;
      M_TotSum=M_TotSum+CapIssueRec.Sum;
    end;
    RecFound=Next(CapIssueRec);
  end;
  M_OutTotals;
end;
    
macro M_BuyReport

  var
    RecFound;

  M_TotNumItems=0;
  M_TotSum=$0;

  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_MISC;

  M_OutBuyHead;
  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_MISC))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Brigade==OperBrigade)
      and (CapIssueRec.Operation==O_MISC_BUY))
      M_OutLine; 
      SetRecordAddr(MiscRecInfo,CapIssueRec,0,
        FldOffset(CapIssueRec,"Info"),true);
      M_TotNumItems=M_TotNumItems+M_GetNumItems;
      M_TotSum=M_TotSum+CapIssueRec.Sum;
    end;
    RecFound=Next(CapIssueRec);
  end;
  M_OutTotals;
end;
    
macro MiscReport;

  M_OutHead;         
  M_SellReport;
  M_BuyReport;
end;

macro M_Find;

  CIMisc.Type=CI_MISC;
  CIMisc.IssueID=CapIssueRec.Issue;
  GetEQ(CIMisc);
end;

macro M_OutSellBuyRecHead;

  var
    BranchStrNum = "",
    DepartNum = "",
    DepartName = "";

  GetBranchParams( Branch, BranchStrNum, DepartNum, DepartName );
  SetRecordAddr(MiscRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [
     ##################################
                                                                                           Ф. № 56
     Сберегательного банка № ##########                                         Филиал № #########                                     


                                      
                                       КОНТРОЛЬНАЯ ВЕДОМОСТЬ

                            на продажу и покупку облигаций Российского
                                  внутреннего выигрышного займа
                                             1992 года
                        ##################################################
                                           за ##########


  ]
  (
    DepartName,
    DepartNum,
    BranchStrNum,
    "("+MiscRecInfo.Name+")":c,
    {curdate}
  );
  [+-----------+----------------------------------------+--------------------+-------------------+];
  [| Номерной  |              П р о д а н о             |   К у п л е н о    |   П о д п и с и   |];
  [|  талон,   |------+-----------------+---------------|------+-------------|---------+---------|];
  [| выданный  |Кол-во|Номинальная стои-| Получено за   |Кол-во| Получено за |         |         |];
  [| клиенту   |облиг.|мость проданных  | проданные     |облиг.| купленные   |контролер| кассир  |];
  [|           | штук |облигаций        | облигации     | штук | облигации   |         |         |];
  [+-----------+------+-----------------+---------------+------+-------------+---------+---------+];
end;

macro M_OutSellBuyRecLine;

  var
    ParRepr="",
    SumSoldRepr="",
    SumBoughtRepr="",
    ItemsSoldRepr="",
    ItemsBoughtRepr="";
 
  SetRecordAddr(MiscDocInfo,CapIssueDoc,0,
    FldOffset(CapIssueDoc,"Info"),true);
  if(CapIssueDoc.Operation==O_MISC_SELL) 
    M_TotSumSold=M_TotSumSold+CapIssueDoc.Sum;
    M_TotItemsSold=M_TotItemsSold+MiscDocInfo.NumItems;
    ParRepr=MiscDocInfo.Par;
    SumSoldRepr=CapIssueDoc.Sum;
    ItemsSoldRepr=MiscDocInfo.NumItems;
  elif(CapIssueDoc.Operation==O_MISC_BUY) 
    M_TotSumBought=M_TotSumBought+CapIssueDoc.Sum;
    M_TotItemsBought=M_TotItemsBought+MiscDocInfo.NumItems;
    SumBoughtRepr=CapIssueDoc.Sum;
    ItemsBoughtRepr=MiscDocInfo.NumItems;
  end;
  
  [|   ####    |######|#################|###############|######|#############|         |         |]
  (
    CapIssueDoc.DocNumber,
    ItemsSoldRepr,
    ParRepr,
    SumSoldRepr,
    ItemsBoughtRepr,
    SumBoughtRepr
  );
end;

macro M_MiscSellBuyRecTotals;
 
  var
    SellCredAcc,
    SellDebAcc,
    BuyCredAcc,
    BuyDebAcc;
 
  GetBookAcc(O_MISC_SELL, SellDebAcc, SellCredAcc);
  GetBookAcc(O_MISC_BUY, BuyDebAcc, BuyCredAcc);
  SetRecordAddr(MiscDocInfo,CapIssueDoc,0,
    FldOffset(CapIssueDoc,"Info"),true);
  [+-----------+------+-----------------+---------------+------+-------------+---------+---------+];
  [| Всего     |######|                 |###############|######|#############|         |         |]
  (
    M_TotItemsSold,
    M_TotSumSold,
    M_TotItemsBought,
    M_TotSumBought
  );
  [ ];
  [                                                    Д-т сч. ];
  [                                                    ########################## ]
  (Acc_Form(BuyDebAcc));
  [ ];
  [                                 Д-т сч. ];
  [                                 ########################## ]
  (Acc_Form(SellDebAcc));
  [ ];
  [             Расход по сч.                          К-т сч. ];
  [             ___________________                    ########################## ]
  (Acc_Form(BuyCredAcc));
  [ ];
  [                                 К-т сч. ];
  [                                 ########################## ]
  (Acc_Form(SellCredAcc));
  [ ];
  [                                                    Приход по сч.];
  [                                                    _________________ ];
end;

macro M_OutRecTail;

  [



                             Ст. бухгалтер ___________                                
  ];
end;

macro M_MiscSellBuyRecBody;

  var
    RecFound;

  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;

  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    if ( CheckCIDoc )
      M_OutSellBuyRecLine;
    end;
    RecFound=Next(CapIssueDoc);
  end;
end;

macro M_MiscSellBuyRec;
 
  M_OutSellBuyRecHead; 
  M_MiscSellBuyRecBody;
  M_MiscSellBuyRecTotals;
  M_OutRecTail;
end;

macro MiscRec(ApplicationKind,ApplicationKey);
      
  KeyNum(CapIssueRec,2);                                        
  CapIssueRec.ApplicationKind=ApplicationKind;
  CapIssueRec.ApplicationKey=ApplicationKey;
  if(not GetEQ(CapIssueRec))
    MsgBox("Ошибка при печати ведомости");
    return;
  end;
  M_MiscSellBuyRec;
end;

macro MiscRecReport;
  
  var
    NotEmpty = false,
    PrevIssue = 0,
    RecFound;
 
  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_MISC;

  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_MISC))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Brigade==OperBrigade))
      if(CapIssueRec.Issue!=PrevIssue)
        if(PrevIssue)
          M_MiscSellBuyRecTotals;
        end;
        PrevIssue=CapIssueRec.Issue;
        M_TotSumSold=$0;
        M_TotSumBought=$0;
        M_OutSellBuyRecHead;
        NotEmpty=true;
      end; 
      M_MiscSellBuyRecBody;
    end;
    RecFound=Next(CapIssueRec);
  end;
  if(NotEmpty)
    M_MiscSellBuyRecTotals;
  end;
end;               
