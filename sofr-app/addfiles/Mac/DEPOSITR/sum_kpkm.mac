/*
  RS-Retail
  Филиал Сбербанка

  Итоговые суммы остатков по рублевым видам вкладов
  (для всех счетов по всем филиалам
  без формирования файла статистики)

  Ромодин Александр Васильевич
  29.01.98
*/
/*
  Общие файлы
*/
import BankInter,deprintr,sqlconv;

Private var DEPOSITR = Tbfile ("depositr.dbt","r",1), /* Счета вкладчиков */
            SB_DTYP  = Tbfile ("sb_dtyp.dbt" ,"r",1), /* Справочник видов вкладов */
                                                      /* FlagCur + Prior */
            CURR     = Tbfile ("currency.dbt","r",0), /* Справочник валют*/
            LISTFDEP = Tbfile ("listfdep.dbt","r",0);

const
  DD_DELETE        =  2;

/***********************************************************
        Настройки
***********************************************************/
const MinPrior = 0;             /* Приоритеты видов вкладов */
const MaxPrior = 999;

var FNcash   = NumFNcash();           /* Номер филиала        */
const FlagCur  = NumFlagCur();

var KolAccKind = 0;
var KolAccKindNR = 0;

/***********************************************************
        Переменные
***********************************************************/
var {curdate};
var DateSum = {curdate}; /* Дата */

array KolAcc,           /* Количество счетов по виду вклада и сумма остатков */
      KolAccNR,
      KolAccOpen,
      KolAccOpenNR,
      KolAccClose,
      KolAccCloseNR,
      KolAccAll,
      KolAccAllOpen,
      KolAccAllClose,
      SumAcc,
      SumAccNR,
      SumAccOpen,
      SumAccOpenNR,
      SumAccClose,
      SumAccCloseNR,
      SumAccAll,
      SumAccAllOpen,
      SumAccAllClose,
      KolFin,           /* Количество завершенных договоров и сумма остатков */
      KolFinNR,
      KolFinAll,
      SumFin,
      SumFinNR,
      SumFinAll,
      KolCont,          /* Количество не завершенных договоров и сумма остатков */
      KolContNR,
      KolContAll,
      SumCont,
      SumContNR,
      SumContAll;
array Currency;                     /* Код валюты- Рубли    */
var   KolCur = 0;
var   ClientList = TClientList;

/***********************************************************
***********************************************************/

macro GetCur (Cur)
/*
  Получение имени валюты
*/
  var Name = "";
  CURR.rec.Code_Currency = Cur;
  if (CURR.GetEQ())
    Name = CURR.rec.Short_Name;
  end;
  return Name;
end;
/**********************************************************/

macro LineRep
/*
  Разделитель
*/
  [----------------------------------------------------------------------------------------------------------------------------------------];
end;

macro BeginReport
/*
  Начало отчета
*/
  [ ];
  [                        Итоговые суммы по видам вкладов на ##########](DateSum);
  [                                          по филиалу ##](FNcash);
  [ ];
  LineRep();
  [| Вид вклада        | Договор    |     Всего счетов                |     Завершенных договоров       |     Незавершенных договоров     |];
  LineRep();
end;

macro StringReport
/*
  Строка отчета
*/
  var Form="";
  var i = 0;
    if (SB_DTYP.rec.FormContr == 1)
      Form = "Срочные";
    elif (SB_DTYP.rec.FormContr == 20)
      Form = "Депозиты";
    elif (SB_DTYP.rec.FormContr == 30)
      Form = "Пролонгир.";
    end;
    if ((Asize(KolAcc)>0) OR (Asize(KolAccNR)>0))
      while (i < KolCur)
        if ((KolAcc(i) != 0) OR (SumAcc(i) != 0))
          [| ############ (###)| ########## | ##### | ####################### | ##### | ####################### | ##### | ####################### |]
          (SB_DTYP.rec.Kind,GetCur(Currency(i)),Form,KolAcc(i),SumAcc(i):a,
          KolFin(i),SumFin(i):a,KolCont(i),SumCont(i):a);
          [| Открытые          |            | ##### | ####################### |       |                         |       |                         |]
          (KolAccOpen(i),SumAccOpen(i):a);
          [| Закрытые          |            | ##### | ####################### |       |                         |       |                         |]
          (KolAccClose(i),SumAccClose(i):a);
        end;
        if ((KolAccNR(i) != 0) OR (SumAccNR(i) != 0))
          [| ############ (###)| ########## | ##### | ####################### | ##### | ####################### | ##### | ####################### |]
          (SB_DTYP.rec.Kind,GetCur(Currency(i)),Form,KolAccNR(i),SumAccNR(i):a,
          KolFinNR(i),SumFinNR(i):a,KolContNR(i),SumContNR(i):a);
          [| Открытые (нерез.) |            | ##### | ####################### |       |                         |       |                         |]
          (KolAccOpenNR(i),SumAccOpenNR(i):a);
          [| Закрытые (нерез.) |            | ##### | ####################### |       |                         |       |                         |]
          (KolAccCloseNR(i),SumAccCloseNR(i):a);
        end;
        i = i + 1;
      end;
    end;
end;

macro EndRep;
/*
  Конец отчета
*/
  var i = 0;
  LineRep();
  while (i < KolCur)
    [| Итого:                   (###) | ##### | ####################### | ##### | ####################### | ##### | ####################### |]
    (GetCur(Currency(i)),KolAccAll(i),SumAccAll(i):a,KolFinAll(i),
    SumFinAll(i):a,KolContAll(i),SumContAll(i):a);
    i = i + 1;
  end;
end;
/**********************************************************/

macro DefCur
(
 Cur
)
/*
  Определение валюты или добавление в список новой
*/
  var i = 0;
  var NumCur = 0;
  var stat = TRUE;
  while (stat AND (i < KolCur))
    if (Currency(i) == Cur)
      stat = FALSE;
      NumCur = i;
    else
      i = i + 1;
    end;
  end;
  if (stat)
    Currency (KolCur) = Cur;
    KolAcc(KolCur) = 0;
    KolAccNR(KolCur) = 0;
    KolAccAll(KolCur) = 0;
      KolAccOpen(KolCur) = 0;
      KolAccOpenNR(KolCur) = 0;
      KolAccClose(KolCur) = 0;
      KolAccCloseNR(KolCur) = 0;
      KolAccAllOpen(KolCur) = 0;
      KolAccAllClose(KolCur) = 0;
    SumAcc(KolCur) = 0;
    SumAccNR(KolCur) = 0;
    SumAccAll(KolCur) = 0;
      SumAccOpen(KolCur) = 0;
      SumAccOpenNR(KolCur) = 0;
      SumAccClose(KolCur) = 0;
      SumAccCloseNR(KolCur) = 0;
      SumAccAllOpen(KolCur) = 0;
      SumAccAllClose(KolCur) = 0;
    KolFin(KolCur) = 0;
    KolFinNR(KolCur) = 0;
    KolFinAll(KolCur) = 0;
    SumFin(KolCur) = 0;
    SumFinNR(KolCur) = 0;
    SumFinAll(KolCur) = 0;
    KolCont(KolCur) = 0;
    KolContNR(KolCur) = 0;
    KolContAll(KolCur) = 0;
    SumCont(KolCur) = 0;
    SumContNR(KolCur) = 0;
    SumContAll(KolCur) = 0;

    NumCur = KolCur;
    KolCur = KolCur + 1;
  end;
  return NumCur;
end;
/**********************************************************/

macro AccForKind
/*
  Цикл по счетам
*/
  var   stat;
  array OpenClose;
  var   NumCur;
  var   IsRez;
  var   i = 0;
  var   strFilter;
  OpenClose(0) = "";
  OpenClose(1) = "З";
  while (i < 2)
    DEPOSITR.rec.IsCur         = FlagCur;
    DEPOSITR.rec.FNCash        = FNcash;
    DEPOSITR.rec.Open_Close    = OpenClose(i);
    DEPOSITR.rec.Type_Account  = SB_DTYP.rec.Kind;
    DEPOSITR.rec.Code_Currency = -1;
    DEPOSITR.rec.Number = 0;
    /*DEPOSITR.Account = "";*/
    strFilter = "t.T_ISCUR = "        + String(FlagCur) +             
           " AND t.T_FNCASH = "       + String(FNcash)  +             
           " AND t.T_OPEN_CLOSE = "   + sqlChar(OpenClose(i)) +       
           " AND t.T_TYPE_ACCOUNT = " + GetSQLString(SB_DTYP.rec.Kind);  
    DEPOSITR.addFilter (strFilter);   
    stat = DEPOSITR.GetGE ();         
    while (stat AND                   
     (DEPOSITR.rec.IsCur        == FlagCur) AND
     (DEPOSITR.rec.FNCash       == FNcash)  AND
     (DEPOSITR.rec.Open_Close   == OpenClose(i)) AND
     (DEPOSITR.rec.Type_Account == SB_DTYP.rec.Kind) AND
     ((DEPOSITR.rec.Code_Currency == 0) OR (FlagCur != 0)) )
      if(DEPOSITR.rec.Action != DD_DELETE)
         IsRez = TRUE;
         if (DEPOSITR.rec.CodClient != 0)
         if ( ClientList.GetRecord( DEPOSITR.rec.CodClient ) )
           if (ClientList.CurRec.Rec.NotResident != "")
               IsRez = FALSE;
             end;
           end;
         end;

         NumCur = DefCur (DEPOSITR.rec.Code_Currency);

         if (IsRez)
           KolAccKind = KolAccKind + 1;
         else
           KolAccKindNR = KolAccKindNR + 1;
         end;

         if (IsRez)
           KolAcc(NumCur) = KolAcc(NumCur) + 1;
           SumAcc(NumCur) = SumAcc(NumCur) + DEPOSITR.rec.Sum_Rest;
         else
           KolAccNR(NumCur) = KolAccNR(NumCur) + 1;
           SumAccNR(NumCur) = SumAccNR(NumCur) + DEPOSITR.rec.Sum_Rest;
         end;
         KolAccAll(NumCur) = KolAccAll(NumCur) + 1;
         SumAccAll(NumCur) = SumAccAll(NumCur) + DEPOSITR.rec.Sum_Rest;
         if (i==0) /* Открые счета */
           if (IsRez)
             KolAccOpen(NumCur) = KolAccOpen(NumCur) + 1;
             SumAccOpen(NumCur) = SumAccOpen(NumCur) + DEPOSITR.rec.Sum_Rest;
           else
             KolAccOpenNR(NumCur) = KolAccOpenNR(NumCur) + 1;
             SumAccOpenNR(NumCur) = SumAccOpenNR(NumCur) + DEPOSITR.rec.Sum_Rest;
           end;
           KolAccAllOpen(NumCur) = KolAccAllOpen(NumCur) + 1;
           SumAccAllOpen(NumCur) = SumAccAllOpen(NumCur) + DEPOSITR.rec.Sum_Rest;
           if (SB_DTYP.rec.FormContr != 0)   /* Договоры */
             if (DEPOSITR.rec.End_DateDep != Date(0,0,0))
               if (IsRez)
                 KolCont(NumCur) = KolCont(NumCur) + 1;
                 SumCont(NumCur) = SumCont(NumCur) + DEPOSITR.rec.Sum_Rest;
               else
                 KolContNR(NumCur) = KolContNR(NumCur) + 1;
                 SumContNR(NumCur) = SumContNR(NumCur) + DEPOSITR.rec.Sum_Rest;
               end;
               KolContAll(NumCur) = KolContAll(NumCur) + 1;
               SumContAll(NumCur) = SumContAll(NumCur) + DEPOSITR.rec.Sum_Rest;
             else
               if (IsRez)
                 KolFin(NumCur) = KolFin(NumCur) + 1;
                 SumFin(NumCur) = SumFin(NumCur) + DEPOSITR.rec.Sum_Rest;
               else
                 KolFinNR(NumCur) = KolFinNR(NumCur) + 1;
                 SumFinNR(NumCur) = SumFinNR(NumCur) + DEPOSITR.rec.Sum_Rest;
               end;
               KolFinAll(NumCur) = KolFinAll(NumCur) + 1;
               SumFinAll(NumCur) = SumFinAll(NumCur) + DEPOSITR.rec.Sum_Rest;
             end;
           end;
         else
           if (IsRez)
             KolAccClose(NumCur) = KolAccClose(NumCur) + 1;
             SumAccClose(NumCur) = SumAccClose(NumCur) + DEPOSITR.rec.Sum_Rest;
           else
             KolAccCloseNR(NumCur) = KolAccCloseNR(NumCur) + 1;
             SumAccCloseNR(NumCur) = SumAccCloseNR(NumCur) + DEPOSITR.rec.Sum_Rest;
           end;
           KolAccAllClose(NumCur) = KolAccAllClose(NumCur) + 1;
           SumAccAllClose(NumCur) = SumAccAllClose(NumCur) + DEPOSITR.rec.Sum_Rest;
         end;
      end;
      Message ("Филиал ",FNcash,": По виду вклада \"",SB_DTYP.rec.Kind,"\" обработано ",KolAccKind," счетов");
      stat = DEPOSITR.Next ();
    end;
    DEPOSITR.dropFilter();
    i = i + 1;
  end;
  return TRUE;
end;
/**********************************************************/

macro ForTypeAccount
/*
  Цикл по видам вкладов
*/
  var stat;
  var i = 0;
  var strFilter;
  SB_DTYP.rec.FlagCur = FlagCur;
  SB_DTYP.rec.Priorit = MinPrior;
  strFilter = "t.T_FLAGCUR = "      + String(FlagCur)+
              " AND t.T_PRIORIT <= "+ String(MaxPrior);
  SB_DTYP.addFilter (strFilter);
  stat = SB_DTYP.GetGE();
  while (stat AND (SB_DTYP.rec.FlagCur == FlagCur) AND
   (SB_DTYP.rec.Priorit <= MaxPrior))
    i = 0;
    while (i < KolCur)
      KolAcc(i) = 0;      KolAccNR (i) = 0;
      SumAcc(i) = 0;      SumAccNR (i) = 0;
      KolAccOpen(i) = 0;  KolAccOpenNR (i) = 0;
      SumAccOpen(i) = 0;  SumAccOpenNR (i) = 0;
      KolAccClose(i) = 0; KolAccCloseNR(i) = 0;
      SumAccClose(i) = 0; SumAccCloseNR(i) = 0;
      KolFin(i) = 0;      KolFinNR(i) = 0;
      SumFin(i) = 0;      SumFinNR(i) = 0;
      KolCont(i) = 0;     KolContNR(i) = 0;
      SumCont(i) = 0;     SumContNR(i) = 0;
      KolAccKind = 0;     KolAccKindNR = 0;
      i = i + 1;
    end;
    stat = AccForKind();
    if (stat)
      StringReport ();
      stat = SB_DTYP.Next ();
    end;
  end;
  SB_DTYP.dropFilter();
end;
/**********************************************************/

macro sum_k_pk( Quiet )
/*
  Головной макрос
*/
  var stat = TRUE;
  var OneFil = FALSE;
  var dpDepList = TdpDepList;

    if ( Quiet or GetInt (FNcash, " Введите номер филиала (две цифры)",2));
      if (FNcash != "00")
        OneFil = TRUE;
      end;
      dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
      while (stat AND dpDepList.next())
        if (OneFil)
          stat = FALSE;    /* Для одного филиала */
        else
          FNcash = dpDepList.rec.Code;
          KolCur = 0;
        end;
        BeginReport();
        ForTypeAccount();
        EndRep();
      end;
    end;
end;
/*********/
