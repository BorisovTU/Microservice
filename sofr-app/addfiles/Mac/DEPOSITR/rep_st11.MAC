/**********************************************************
*  ---------------------                                  *
*  R-Style SoftWare Lab.                                  *
*  ---------------------                                  *
*                                                         *
*  СБЕРБАНК 1.0                                           *
*                                                         *
*  Отчет по структуре вкладов по срокам востребования     *
*                                                         *
*  Лебедев С.В.                                           *
*  13.03.98                                               *
**********************************************************/
import deprintr, IsSrvDoc;

var dpDepList = TdpDepList; /* Справочник филиалов */
file depositr (depositr); /* Счета вкладчиков    */
file sbdepdoc (sbdepdoc); /* Проводки вкладчиков */

var {curdate};
var FNCash = NumFNCash();
var NumBranch;
var NameBranch;
var DateReport;
private var dep_found;

var SumSum = $0.0; /* Общая сумма для всех периодов */

/* Граница даты и сумма за период дат */
array RightDate;
array SumForDate;
array NameDate;

/* Если граница даты до 1000 включительно - то это количество дней, */
/* если больше, то (RightDate - 1000) - количество лет               */
  RightDate(0) =    0; SumForDate(0) = $0.0; NameDate (0) = "             до   0 дней";
  RightDate(1) =    1; SumForDate(1) = $0.0; NameDate (1) = " от   1 дня  до   1 дня";
  RightDate(2) =    7; SumForDate(2) = $0.0; NameDate (2) = " от   2 дней до   7 дней";
  RightDate(3) =   30; SumForDate(3) = $0.0; NameDate (3) = " от   8 дней до  30 дней";
  RightDate(4) =   90; SumForDate(4) = $0.0; NameDate (4) = " от  31 дня  до  90 дней";
  RightDate(5) =  180; SumForDate(5) = $0.0; NameDate (5) = " от  91 дней до 180 дней";
  RightDate(6) =  365; SumForDate(6) = $0.0; NameDate (6) = " от 181 дней до 365 дней";
  RightDate(7) = 1003; SumForDate(7) = $0.0; NameDate (7) = " от 366 дней до   3 лет";
                       SumForDate(8) = $0.0; NameDate (8) = " от   3 лет";



/********************************************************************
                           Шапка отчета
********************************************************************/
macro HeadReport

  var day,month,year;
  var d,m,y;
  var dep_code;

  DateSplit(DateReport,d,m,y);
  day = String(d);
  if (StrLen(day) == 1)
    day = "0" + day;
  end;
  if   (m ==  1) month = "января";
  elif (m ==  2) month = "февраля";
  elif (m ==  3) month = "марта";
  elif (m ==  4) month = "апреля";
  elif (m ==  5) month = "мая";
  elif (m ==  6) month = "июня";
  elif (m ==  7) month = "июля";
  elif (m ==  8) month = "августа";
  elif (m ==  9) month = "сентября";
  elif (m == 10) month = "октября";
  elif (m == 11) month = "ноября";
  elif (m == 12) month = "декабря";
  else month = "";
  end;
  year = String(y);

  NameBranch = "";
  if (NumBranch == -1)
    dep_code = FNCash;
  else
    dep_code = NumBranch;
  end;
  if (not findDepartment(dep_code, NameBranch))
    NameBranch = "";
  end;

  [ ];
  [  Справка 1];
  [ ];
  [     Информация о структуре рублевых вкладов населения];
  [     по срокам востребования на #  #        #    года.](day,month,year);
  [                  Оперчасть #](NameBranch);
  [  ─────┬───────────────────────────────┬────────────────────];
  [  N п/п│   СРОК ВОСТРЕБОВАНИЯ ВКЛАДА   │       СУММА];
  [       │         (по договору)         │       (руб.)];
  [  ─────┼───────────────────────────────┼────────────────────];

end;


/********************************************************************
                          Окончание отчета
********************************************************************/
macro BottomReport

  [  ─────┼───────────────────────────────┼────────────────────];
  [  ИТОГО│ (бал.счета 42301,42303,42306) │ ################## ](String(SumSum:18:2:a));
  [  ─────┴───────────────────────────────┴────────────────────];
  [ ];
  [  Исполнитель:];
  [ ];
  [  #](String({curdate}));

end;


/********************************************************************
                           Подсчет суммы
********************************************************************/
macro CalcSum

  var i =  0;
  var j = -1;
  var RightDateForYear;
  var d,m,y;
  var stat;
  var AccRestForDate = $0.0;

  while (i < ASize(RightDate))
    if (RightDate(i) > 1000)
      DateSplit(DateReport,d,m,y);
      RightDateForYear = Date(d,m,y + RightDate(i) - 1000);
      if (depositr.End_DateDep <= RightDateForYear)
        j = i;
        i = ASize(RightDate) + 1; /* Чтобы выйти из цикла */
      end;
    else
      if ((depositr.End_DateDep - DateReport) <= RightDate(i))
        j = i;
        i = ASize(RightDate) + 1; /* Чтобы выйти из цикла */
      end;
    end;
    i = i + 1;
  end;

  /* Не попала в диапазоны, ограниченные справа, значит попала в свыше последнего */
  if (j == -1)
    j = ASize(RightDate);
  end;

  /* Определим остаток на счете на дату отчета */
  KeyNum(sbdepdoc,6);
  sbdepdoc.Referenc      = depositr.Referenc;
  sbdepdoc.Date_Document = DateReport + 1;
  sbdepdoc.NumDayDoc     = 0;
  stat = GetLT(sbdepdoc);
  while (stat)
    if (sbdepdoc.Referenc == depositr.Referenc)
      if ((sbdepdoc.Date_Document <= DateReport     ) AND
          IsServDoc(sbdepdoc) )
        AccRestForDate = sbdepdoc.Rest;
        stat = FALSE;
      else
        stat = Prev(sbdepdoc);
      end;
    else
      stat = FALSE;
    end;
  end;

  SumForDate(j) = SumForDate(j) + AccRestForDate;
  SumSum        = SumSum        + AccRestForDate;

end;


/********************************************************************
                 Цикл по открытым счетам вкладчиков
********************************************************************/
macro DoReport

  var stat;
  var i = 0;
  var AccCounter = 0;
  var DateEnd;

  KeyNum(depositr,1);
  ReWind(depositr);
  depositr.IsCur         = 0;
  if (NumBranch == -1)
    depositr.FNCash      = 0;
  else
    depositr.FNCash      = NumBranch;
  end;
  depositr.Open_Close    = "";
  depositr.Type_Account  = "";
  depositr.Code_Currency = 0;
  depositr.Number        = 0;
  /*depositr.Account       = "";*/
  stat = GetGE(depositr);
  while (stat)
    if (depositr.Close_Date != Date(0,0,0))
      DateEnd = depositr.Close_Date;
    else
      DateEnd = DateReport + 1; /* Счет не закрыт */
    end;
    if ((depositr.IsCur == 0) AND
        ((NumBranch == -1) OR ((NumBranch > -1) AND (depositr.FNCash == NumBranch))))
      if ((depositr.Type_Account == "Служебный") OR
          ((depositr.Open_Date > DateReport) AND (DateEnd <= DateReport)))
        ; /* Отбрасываем служебные счета и счета, которые не действовали на дату отчета */
      else
        AccCounter = AccCounter + 1;
        Message(" Обработано ",AccCounter," счетов");
        CalcSum();
      end;
      stat = Next(depositr);
    else
      stat = FALSE;
    end;
  end;

  while (i < ASize(SumForDate))
    [  #### │###############################│ ##################]
    ((i + 1):r,NameDate(i),SumForDate(i):18:2:a);
    i = i + 1;
  end;

end;


/********************************************************************
                Г Л А В Н А Я   П Р О Г Р А М М А
********************************************************************/

  if (GetString(NameBranch,"Введите номер филиала",12))
    if (StrLen(Trim(NameBranch)) == 0)
      NumBranch = -1;
    else
      dep_found = false;
      dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
      while (not dep_found and dpDepList.next())
        if (dpDepList.rec.Name == Trim(NameBranch))
          dep_found = true;
        end;
      end;
      if (dep_found)
        NumBranch = dpDepList.rec.Code;
      else
        [ ];
        [ В списке филиалов нет филиала #](NameBranch);
        NumBranch = -2;
      end;
    end;
    if (NumBranch >= -1)
      DateReport = {curdate};
      if (GetDate(DateReport,"Введите дату отчета"))
        HeadReport();
        DoReport();
        BottomReport();
      else
        [ ];
        [ Отказались от выпуска отчета];
      end;
    end;
  else
    [ ];
    [ Отказались от выпуска отчета];
  end;

end;
