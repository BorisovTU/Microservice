/*---------- Сбеpбанк ------------ Начало файла Load_CR.Mac ------------------*/
/*--10:41 27.02.1997 Зубов В.Ю.<VZ> ------------------------------------------*/
/*--- Макpос загpузки пpинятых коммунальных платежей из кассового аппаpата ---*/
/*
  Структура файла:
  ---------------------------------------------------------
  Номер поля | Тип, длина | Комментарий
  ---------------------------------------------------------
        1       N          Код получателя платежа
        2       N          Общая сумма платежа(В КОПЕЙКАХ)
  ---------------------------------------------------------
  ВНИМАНИЕ! Разделители полей задаются в массиве Separate
*/

import BankInter,deprintr,DeskInter,"rc_file.mac";

var {curdate};
var {oper};
var Branch = NumFNCash;
var OperBrigade = GetOperBrigade;

file InputFile() txt 120;       /* Исходный текстовый файл      */
file CP_Doc("pay_doc.dbt") write;       /* Файл коммунальных платежей   */
file CP_Recip("rtcp_recipient.dbt");    /* Файл получателей платежей    */
/*
  Разделители
*/
Array Separate;
Separate(0) = " ";
Separate(1) = ":";
Separate(2) = ";";
Separate(3) = ",";
const NumberSeparate = ASize(Separate);
/*
  Константы
*/
const SB_CASHOPERT = 200;
const CO_CommPay = 1;
const TYPERES_CURRENCY = 1;
const CASHOP_CREDIT = 6;
const PatternFile = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true ) + "*.cpd"; /* Маска исходного текстового файла */

const FNcash  = NumFNcash();    /* Номер филиала        */
/*
  Глобальные переменные
*/
var NameInputFile      = "";    /* Имя Операционного файла      */
var StrFile            = "";    /* Строка Операционного файла   */
var KolStrFile;                 /* Номер строки и количество
                                   строк в Операционном файле   */
var KolStrInp;                  /* Количество введенных записей */
var KolStrErr;                  /* Количество записей с ошибками*/
var Sum_Credit         = 0;     /* Сумма по кредиту             */


macro Начало_Конец_отчета
  [ ------ Сбербанк ------------------- ########## ------ ######## ---
    ](Date(),Time());
end;

macro Разделитель_1
  [ ---------------------------------------------------------------------------];
end;

macro Разделитель_2
  [ ===========================================================================];
end;

/***********************************************************
***********************************************************/

macro Описание_ошибки (ErrCode)
/* Строка описания ошибки */
  var StrErr = "";

  if (ErrCode == 0)
    StrErr = "Нет ошибки";
  elif (ErrCode == 1)
    StrErr = "Неверно задан получатель платежа";
  elif (ErrCode == 2)
    StrErr = "Не найден получатель платежа";
  elif (ErrCode == 3)
    StrErr = "Неверно задана сумма";
  end;
  return StrErr;
end;
/***********************************************************
        Макросы обслуживания
***********************************************************/

/**********************************************************/

macro FindRecipient (RecipientID)
/* Ищет, существует ли получатель с данным кодом */
  var stat = FALSE;
  KeyNum (CP_Recip,0);
  CP_Recip.ID = RecipientID;
  stat = getEQ (CP_Recip);
  return stat;
end;
/**********************************************************/

macro GetSeparateStr
/*
  Отделяет от StrFile начало строки до первого разделителя
  и смещает начало StrFile на следующий после разделителя символ

  27.01.97
*/
  var Str = "";
  var Sym = "";
  var Cont = TRUE;
  var StepBack = 0;
  var i = 1,
      j = 0;
  var Len = StrLen(StrFile);

  while ((i <= Len) AND Cont)
    Sym = SubStr (StrFile,i,1);
    j = 0;
    while (j < NumberSeparate)
      if (Sym == Separate(j))
        Cont = FALSE;
        StepBack = 1;
      end;
      j = j + 1;
    end;
    i = i + 1;
  end;
  if (i > 1)
    Str = SubStr (StrFile,1,i-1-StepBack);
    StrFile = SubStr (StrFile,i);
  end;
  return Str;
end;
/**********************************************************/

macro IsInt (Str)
/*
  Записано ли в строке целое число (без знака)?

  27.01.97
*/
  var stat = TRUE;
  var Len = StrLen(Str);
  var i = 1;
  var Sym;
  var EndSpace = FALSE;

  while ((i <= Len) AND stat)
    Sym = SubStr(Str,i,1);
    if (Sym == " ")
      if (EndSpace)
        stat = FALSE;
      end;
    elif ((Sym < "0") OR (Sym > "9"))
      stat = FALSE;
    else
      EndSpace = TRUE;
    end;
    i = i + 1;
  end;
  return stat;
end;
/***********************************************************
***********************************************************/

macro Запись_в_отчет ()
/*
  Заносит информацию по обработанной строке в отчет

  27.01.97
*/
  [  #####################  ######################## ]
  (CP_Recip.Name, CP_Doc.InSum);
end;
/**********************************************************/
macro Разделитель_1_1
  [ ----------------------------------------------------------];
end;
/**********************************************************/

macro Заголовок_отчета
  Начало_Конец_отчета ();
  [                                                                ];
  [        Загрузка информации о принятых коммунальных платежах из ];
  [                      текстового файла обмена                   ];
  [                                                                ];
  [ Обработан файл: #](NameInputFile);
  [ Дата обработки: #]({curdate});
  [ Операционист:   ####]({oper});
  [ ];
  Разделитель_1_1 ();
  [   Получатель          |    Сумма зачисления         ];
  [                       |                             ];
  Разделитель_1_1 ();
end;
/***********************************************************
***********************************************************/

macro Конец_отчета
/*
  Вывод итоговых строк в отчет
*/
  var i = 0;

  Разделитель_1_1 ();
  [  Итого:                 ########################]
   (Money(Double(Sum_Credit)));
  [ ];
  [ Общее число обработанных записей: #](KolStrFile);
  [ ];
  [ Введено в базу данных ##### документов ](KolStrInp);
  [ ];
  [ Отвергнуто из-за ошибки ##### записей ](KolStrErr);
  [ ];
  Начало_Конец_отчета();
end;
/***********************************************************
***********************************************************/

macro Инициализация_Документа
/*
  Разбирает строку исходного файла и инициализирует документ
*/
  var i = 0;
  var InputString;
  var stat = 0;

/* Обнуление записи */
  ClearRecord(CP_Doc);
  CP_Doc.Date_Document = {curdate};
  CP_Doc.Oper = {oper};
/* Код получателя платежа */
  if (stat == 0)

    InputString = GetSeparateStr ();
    CP_Doc.FNCash=FNcash;
    CP_Doc.GroupOpert=CO_CommPay;
    CP_Doc.ReceiverCod = Int(InputString);
    if (NOT ((InputString != "") AND IsInt(InputString)))
      stat = 1;
    elif(NOT FindRecipient (CP_Doc.ReceiverCod))
      stat = 2;
    end;
  end;

/*   Сумма   */
  if (stat == 0)
    InputString = GetSeparateStr ();
    CP_Doc.InSum  = Money(Double(InputString));
    if (NOT (InputString != "" AND IsInt(InputString)))
      stat = 3;
    end;
  end;

  if(stat == 0)
    CP_Doc.iApplicationKind=SB_CASHOPERT;
    CP_Doc.ApplicationKey=FormApplicationKey(CP_Doc.iApplicationKind);
  end;
  return stat;
end;
/**********************************************************/

/*
  Поиск связного кассира для Oper
*/

 macro GetPatern(Oper)

   file GroupMem( "groupmem.dbt" ) key 0;

   GroupMem.Branch = NumRealFNCash;
   GroupMem.Date = {curdate};
   GroupMem.Group = OperBrigade;
   GroupMem.Oper = Oper;
   if ( GetEQ( GroupMem ) )
     return GroupMem.Linked;
   else
     return 0;
   end;
 end;

macro Обработка_строк_исходного_файла
/*
  Организует цикл по строкам файла
  Вызывает макрос инициализации отложенного
  Записывает очередной отложенный документ
*/
  record Parm(CashParm);
  var stat = 0;

  ReWind (InputFile);
  KolStrFile = 0;
  KolStrErr = 0;
  KolStrInp = 0;

  while (Next(InputFile))
    StrFile = InputFile.str;
    KolStrFile = KolStrFile + 1;
    stat = Инициализация_Документа();
    Запись_в_отчет ();
    if (stat == 0)
      if (Insert (CP_Doc))
        Parm.Type=0;           /* Платежеспосбные ресурсы */
        Parm.TypeValue=TYPERES_CURRENCY;
        Parm.CodIntValue=CP_Doc.CodCur;
        Parm.Oper=GetPatern(CP_Doc.Oper);
        Parm.CashDateDoc=CP_Doc.Date_Document;
        Parm.NumOper=CASHOP_CREDIT;
        Parm.ValueMoney=CP_Doc.InSum;
        Parm.NumbDocumCash=CP_Doc.NDoc;
        Parm.ApplicationKind=CP_Doc.iApplicationKind;
        Parm.ApplicationKey=CP_Doc.ApplicationKey;
        Parm.CashPatern=CP_Doc.Oper;
        if(not InitCashDoc(Parm))
          [ Ошибка при формировании кассового документа ];
          KolStrErr = KolStrErr + 1;
        else
          KolStrInp = KolStrInp + 1;
          Sum_Credit = Sum_Credit + CP_Doc.InSum;
        end;
      else
        [ Ошибка при занесении документа в файл отложенных ];
        KolStrErr = KolStrErr + 1;
      end;
    else
      KolStrErr = KolStrErr + 1;
      [ # ](Описание_ошибки (stat));
    end;
  end;
  Конец_отчета ();
end;
/**********************************************************/

macro Обработка_исходного_файла
/*
  - Отделяет имя исходного файла от пути
  - Вызывает макрос копирования информации из исходного файла
    в файл принятых коммунальных платежей
*/
  Заголовок_отчета ();
  Обработка_строк_исходного_файла ();
end;

/*----------------------------- MAIN BODY ------------------------------------*/
  var PathName = "";
  var len;

  if (SelectFile(NameInputFile,PatternFile,"Выберите исходный файл"))
      Open (InputFile,NameInputFile);
      Обработка_исходного_файла ();
    else
      [ Файл не был выбpан ];
    end;
  end;
end;
/*---------- Сбеpбанк ------------ Конец  файла Load_CR.Mac ------------------*/
