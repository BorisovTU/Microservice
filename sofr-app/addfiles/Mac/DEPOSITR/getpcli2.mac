/***************************************************************************\
| GETPCLI2.MAC - Библиотека макрофункций  для вида вклада "СБЕРЕГАТЕЛЬНЫЙ"  |
|                                                     Зубов В.Ю. 08-09-97   |
\***************************************************************************/
import deprintr,opercode,issrvdoc,Календарь,alg_vars;

/***************************************************************************\
| Число_Дней_В_Месяце                                                       |
\***************************************************************************/
MACRO Число_Дней_В_Месяце(Дата)
Var tmpDate,begDate,endDate,день,месяц,год;
 DateSplit(Дата,день,месяц,год);
 begDate = Date(1,месяц,год);
 месяц = месяц + 1;
 if( месяц > 12)
   месяц = 1;
   год = год +1;
 end;/* IF */
 tmpDate = Date(1,месяц,год);
 endDate = tmpDate - 1;
 день = endDate - begDate + 1;
/*
 [ Начало #
   Конец  #
   Дней   #](begDate:l,endDate:l,день:l);
*/
 return день;
end; /* of MACRO */


/***************************************************************************\
| Последний_День_Предыдущего_Месяца                                         |
\***************************************************************************/
MACRO Последний_День_Предыдущего_Месяца(Дата)
Var tmpDate,day,mon,year;
 DateSplit(Дата,day,mon,year);
 tmpDate = Date(1,mon,year);
 tmpDate = tmpDate - 1;
 return tmpDate;
end; /* of MACRO */

/***************************************************************************\
| Расчет процентов,начисл. на доп.взносы, не пролежавшие к сегодня месяца   |
\***************************************************************************/
MACRO Расчет_Излишков_Процентов(Referenc, doc)
Var Дата_последнего_расчета,
    MaxDate,MinDate,
    Проценты,ПроцентыВсего,Сумма,
    day,mon,year,found,
    Результат;

Дата_последнего_расчета = Последний_День_Предыдущего_Месяца({curdate}); /* Если они закрывают счет предыдущим месяцем */
KeyNum(doc,2);
doc.Referenc = Referenc;
MaxDate = Дата_Последнего_расчета;
MinDate = DateAfterCalenMonths(ALG_SBDEPDOC.DepDate_Document, -1);
doc.DepDate_Document = MinDate;
doc.NumDayDoc = 0;
Проценты = $0L;
ПроцентыВсего = $0L;
Сумма = $0L;
if(GetGE(doc))
  found=True;
  while((doc.Referenc == Referenc) AND
        (doc.DepDate_Document <= MaxDate) AND
        (found == True))
    if( /*(doc.TypeOper == Зачисление_процентов) OR */
        /*(doc.TypeOper == Выдача_процентов) OR     */
        IsServDoc(doc) AND
        ((doc.TypeOper == Открытие_Наличными) OR
        (doc.TypeOper == Открытие_Безналичными) OR
        (doc.TypeOper == Дополнительный_Взнос) OR
        /*(doc.TypeOper == Списание_переводом) OR   */
        (doc.TypeOper == Зачисление_В_Пак_Режиме)))
      /*[#](doc.InSum:l);*/
      Сумма = Сумма + doc.InSum;
      Результат = Проценты_На_Операцию(doc.Account,doc.Type_Account,doc.Code_Currency,
               doc.InSum, Проценты, doc.DepDate_Document, Дата_Последнего_расчета);
      if(Результат)
        MsgBox("Ошибка при расчете %% = "+String(Результат));
        return 0;
      end; /* IF */
      ПроцентыВсего=ПроцентыВсего + Проценты;
    end; /* IF */
    if (not Next(doc))
      found=False;
    end;/* IF */
  end; /* WHILE */
end; /* IF */
return ПроцентыВсего;
end; /* of MACRO */

