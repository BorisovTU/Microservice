import deprintr, commclnt, alg_vars;

const FlagComment36 = TRUE;  /* Флаг ввода комментария */

private Var sCheckInfo = "";

const AI_CBK_CONTROL_INFO = 20;  /* Тип auxinf: контрольная информ.по счету */

/*************************************************************************
               Получение наименования подразделения.
*************************************************************************/
macro get_Branch_Name()
  var s_Branch_Name = getFilialName();
  /*
  if ( FindDepartment( Branch, NULL, i_dp_dep ) )
    s_Branch_Name = i_dp_dep.rec.Name;
  else
    s_Branch_Name = String( Branch );
  end;
  */
  return s_Branch_Name;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro Print_nf_36_data(Account, CodClient, Info)

  file opr      ( person   ) key 0;
  var  clnt = TClientList;
  var v_FIO;
  var sOper = "";

  if ( clnt.GetRecord( CodClient ) )
    v_FIO = clnt.CurRec.ConvertFIOFull();
  end;

  opr.Oper = {oper};
  if ( GetEQ( opr ) )
    sOper = "/" + opr.Name + "/";
  else
    sOper = "";
  end;

  [
     Сбербанк России                                 ИЗВЕЩЕНИЕ ф.№ 36

    ##################################################################
    --------------------------------
      (наименование подразделения
          Сбербанка России)

    ПО СЧЕТУ № #######################################################

    ##################################################################
    __________________________________________________________________
            (фамилия, имя, отчество вкладчика/владельца счета)

    ##################################################################

    __________________________________________________________________

    __________________________________________________________________

               +-------------------------------------------+
               |                                           |
               |                                           |
               |                                           |
               +-------------------------------------------+

    ##################################################################

     Контролер _______________________ ################################################################## ]
  (
    get_Branch_Name(),
    Acc_Form(Account):f,
    v_FIO:c:w,
    Info:c:w,
    {curdate}:m,
    sOper
  );

end;


/****************************************************************
                  Поиск записи в auxinfo.dbt
****************************************************************/
macro find_Aux_Info( _Ref )

  file aux      ( auxinfo  ) key 0;  /* доп.информ. о вкладе */
  record aux_21 ( "auxinfo.21" );  /* Для контр.информ.по счету */
  var stat;

  ReWind( aux );
  ClearRecord( aux );
  aux.Reference = _Ref;
  aux.InfoType  = AI_CBK_CONTROL_INFO;
  aux.Date      = {curdate};
  stat = GetLE( aux );  /* Последняя запись по контр.информ.по счету */

  while ( stat  AND
          ( aux.Reference == _Ref )  AND
          ( aux.InfoType  == AI_CBK_CONTROL_INFO )  AND
          ( aux.Date      == {curdate} ) )
    SetRecordAddr( aux_21, aux, 0, 0, TRUE );
    if ( StrLen( aux_21.Reserve ) > 0 )
      sCheckInfo = aux_21.Reserve;
    end;

    stat = Next( aux );
  end;

end;

/***************************************************************************\
|  PrintForm_36 Печать формы ф.№ 36                                         |
\***************************************************************************/
MACRO PrintForm_36(Referenc, CodClient)
 file depositr ( depositr ) key 8;  /* файл счетов */
 var  depclnt = TClientList;     /* файл вкладчиков */
 var Comment36 = "";

 /* Встаем на счет */
 depositr.Referenc = Referenc;
 if(not GetEQ(depositr))
   [Счет с референсом #               не найден](Referenc:l);
   return;
 end; /* IF */
 /* Запрос комментария */
 if ( FlagComment36 )
   GetStringM(Comment36,"Введите комментарий ф.№36", 35);
 end;
 find_Aux_Info( Referenc );
 if ( StrLen( sCheckInfo ) > 0 )
   Comment36 = Comment36 + "\nКонтр.инфо.: \"" + Trim( sCheckInfo ) + "\"";
 end;
 Print_nf_36_data(depositr.Account, CodClient, Comment36);
END;/*of MACRO PrintForm_36*/

