/*****************************************************************
*  ---------------------                                         *
*  R-Style SoftWare Lab.                                         *
*  ---------------------                                         *
*  RS-Retail                                                     *
*                                                                *
*  Ведомость остатков вкладов                                    *
*  (для валют с пересчетом по кросс-курсам)                      *
*                                                                *
*  Лебедев С.В.                                                  *
*  02.04.99                                                      *
*****************************************************************/

import DeprIntr, "cur_rate.mac", "issrvdoc.mac", sqlconv, rsd;

/* Файлы для работы макрофайла ================================ */
var
  currency = TBFile( "currency.dbt", "r", 0 ),  /* Справочник валют         */
  typgrp = TBFile( "sbtypgrp.dbt", "r", 0 ),    /* Справочник видов вкладов */
  sbnumgrp = TBFile( "sbnumgrp.dbt", "r", 0 ),
  depositr = TBFile( "depositr.dbt", "r", 1 ),  /* Счета вкладчиков         */
/*  listfdep = TBFile( "listfdep.dbt", "r", 0 ),*/  /* Список учреждений        */
  operdep = TBFile( "operdep.dbt", "r" ),       /* Учреждения операциониста */
  sbdepdoc = TBFile( "sbdepdoc.dbt", "r", 6 );  /* Вкладные документы       */

var depclnt = TClientList;      /* Справочник вкладчиков    */

/* Переменные для работы макрофайла ===========================           */
var   Mode          = GetCurrentMode(); /* Текущий режим работы */
var   FlagCur       = NumFlagCur(); /* Валютность                          */
var   FNCash        = NumFNCash();  /* Номер филиала                       */
var   NameBranch    = "";           /* Название филиала                    */
var   StatusBranch  = 0;            /* Статус филиала                      */
var   DateReport    = {curdate};    /* Дата отчета                         */
var   CurRate;                      /* Курс за день отчета                 */
var   PrevRate;                     /* Курс за предыдущий день             */
var   FlagRezid;                    /* Резидентность при обработке вклада  */
var   FirstRecForDT;                /* Первая строка по виду вклада        */
var   FirstRecForBA;                /* Первая строка по юалансовому счету  */
array ABalAccounts;                 /* Массив балансовых счетов            */
array Branches;                        /* Массив филиалов для отчета          */

/* Итого по балансовому счету ================================= */
/* ------------------ */
var   CountAllAccBA;    /* Всего счетов по балансовому счету           */
var   CountOpenAccBA;   /* Всего открытых счетов                       */
var   CountCloseAccBA;  /* Всего закрытых счетов                       */
var   RestAllBA;        /* Остаток по балансовому счету                */
/* ------------------ */
var   CountAllForDT;    /* Всего счетов по виду вклада                 */
var   CountOpenForDT;   /* Всего открытых счетов по виду вклада        */
var   CountCloseForDT;  /* Всего закрытых счетов по виду вклада        */
var   RestForDT;        /* Остаток в эквиваленте по виду вклада        */
/* ------------------ */
array ACurrBA;          /* Валюты по балансовому счету                 */
array ANameCurrBA;      /* Название валюты по балансовому счету        */
array ACountAllAccBA;   /* Всего счетов по балансовому счету по валюте */
array ACountOpenAccBA;  /* Всего открытых счетов по валюте             */
array ACountCloseAccBA; /* Всего закрытых счетов по валюте             */
array ARestBA;          /* Остаток по балансовому счету по валюте      */
array ARestEqvBA;       /* Остаток по балансовому счету по валюте      */
/* ------------------ */
array ABoardDT;         /* Граница остатка по виду вклада              */
array ACountAllAccDT;   /* Всего счетов по виду вклада                 */
array ACountOpenAccDT;  /* Всего открытых счетов по виду вклада        */
array ACountCloseAccDT; /* Всего закрытых счетов по виду вклада        */
array ARestDT;          /* Остаток по виду вклада                      */
array ARestEqvDT;       /* Остатаок в эквиваленте по виду вклада       */
var   CountAllAccDT;    /* Всего счетов по виду вклада                 */
var   CountOpenAccDT;   /* Всего открытых счетов по виду вклада        */
var   CountCloseAccDT;  /* Всего закрытых счетов по виду вклада        */
var   RestDT;           /* Остаток по виду вклада                      */
var   RestEqvDT;        /* Остатаок в эквиваленте по виду вклада       */
/* ------------------ */
var   CountAllAccAll;   /* Всего счетов                                */
var   CountOpenAccAll;  /* Всего открытых счетов                       */
var   CountCloseAccAll; /* Всего закрытых счетов                       */
var   RestAllAll;       /* Остаток                                     */
array ACodCurrencyAll;  /* Код валюты                                  */
array ANameCurrencyAll; /* Название валюты                             */
array ACountAllAccAll;  /* Всего счетов по валюте                      */
array ACountOpenAccAll; /* Всего открытых счетов                       */
array ACountCloseAccAll;/* Всего закрытых счетов                       */
array ARestAllAll;      /* Остаток                                     */
array ARestEqvAllAll;   /* Остаток в эквиваленте                       */

/* Тип отчета для валюты ====================================== */
const REPORT_RubRate   = 1; /* С пересчетом по национальной валюте */
const REPORT_CrossRate = 2; /* С пересчетом по долларам            */
var Current_REPORT = REPORT_RubRate;

/********************************************************************
                    Выбор филиала для работы
********************************************************************/
macro ChooseBranchAndDateForWork

  array ACash,ANameBr,ANameMenu,AStatBr;

  var stat;
  var i        = 0;
  var FullName = "";
  var RetCode  = FALSE;
  var NumBranch;
  var ddep = TRecHandler( "i_dp_dep.rec" );
  var dpDepList = TdpDepList;

  ACash    (i) = -1;
  ANameBr  (i) = "";
  ANameMenu(i) = " По всем учреждениям отделения ";
  AStatBr  (i) = 1;

  operdep.KeyNum = 0;
  operdep.rec.Oper   = {oper};
  operdep.rec.FNCash = 0;
  stat = operdep.GetGE;
  while (stat)
    if (operdep.rec.Oper == {oper})
      if ( findDepartment(operdep.rec.FNCash, null, ddep)                  /*AND
         ((listfdep.rec.StatusBranch == 0) OR 
          (listfdep.rec.StatusBranch == 3)   ) */  )
        i = i + 1;
        ACash  (i) = operdep.rec.FNCash;
        ANameBr(i) = ddep.rec.Name;
        AStatBr(i) = ddep.rec.NodeType;
        FullName   = "";
        if (FullName == "")
          if (ddep.rec.NodeType == I_DEPARTMENT_TYPE_FILIAL /*3*/)
            FullName = "Оперчасть";
          elif (ddep.rec.NodeType == I_DEPARTMENT_TYPE_VSP/*0*/)
            FullName = "Филиал";
          end;
        end;
        ANameMenu(i) = " " + String(ACash(i):2) + "  " + SubStr(ANameBr(i) + MkStr(" ",12),1,12) + "  " + FullName + " ";
      end;
      stat = operdep.Next;
    else
      stat = FALSE;
    end;
  end;

  i = Menu(ANameMenu," ~ENTER~ Печать отчета  ~ESC~ Выход","Выберите учреждение");
  if (i < 0)
    [ ];
    [ Отказались от выпуска отчета];
  else
    NumBranch    = ACash(i);
    NameBranch   = ANameBr(i);
    StatusBranch = AStatBr(i);
    DateReport = {curdate};
    if (GetDate(DateReport,"Введите дату отчета"))
      RetCode = TRUE;
      if (NumBranch > -1)
        Branches(0) = NumBranch;
      else
        dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
        dpDepList.rewind();
        i = 0;
        while (dpDepList.next())
          if (dpDepList.rec.NodeType == I_DEPARTMENT_TYPE_FILIAL/*1*/)
            NameBranch = dpDepList.rec.Name;
          end;
/*          if ((listfdep.rec.StatusBranch == 0) OR 
              (listfdep.rec.StatusBranch == 3)   )*/
            Branches(i) = dpDepList.rec.Code;
            i = i + 1;
/*          end;*/
        end;
      end;
    else
      [ ];
      [ Отказались от выпуска отчета];
    end;
  end;

  return RetCode;

end;


/*****************************************************************
           Определение всех возможных балансовых счетов
*****************************************************************/
macro DefBalAccounts

  var stat;
  var FindBA;
  var FindBA_NotRez;
  var TmpVal;
  var i = 0;
  var j = 0;

  Message(" Определение балансовых счетов ...");

  /* Получение массива всех возможных балансовых счетов */

  sbnumgrp.Clear;
  sbnumgrp.Rewind;
  stat = sbnumgrp.Next;

  while ( stat )
    if ( ASize( ABalAccounts ) == 0 )
      ABalAccounts( 0 ) = Trim( sbnumgrp.rec.BalAcc2 );
    end;

    FindBA        = FALSE;
    FindBA_NotRez = FALSE;

    i = 0;
    while ( i < ASize( ABalAccounts ) )
      if ( ABalAccounts( i ) == Trim( sbnumgrp.rec.BalAcc2 ) )
        FindBA = TRUE;
      end;
      if ( ABalAccounts( i ) == Trim( sbnumgrp.rec.BalAcc2NotResid ) )
        FindBA_NotRez = TRUE;
      end;
      i = i + 1;
    end;

    if ( FindBA == FALSE )
      ABalAccounts( ASize( ABalAccounts ) ) = Trim( sbnumgrp.rec.BalAcc2 );
    end;

    if ( FindBA_NotRez == FALSE )
      if ( Trim( sbnumgrp.rec.BalAcc2 ) != Trim( sbnumgrp.rec.BalAcc2NotResid ) )
        ABalAccounts( ASize( ABalAccounts ) ) = Trim( sbnumgrp.rec.BalAcc2NotResid );
      end;
    end;

    stat = sbnumgrp.Next;
  end;

  /* Отсортируем полученный массив балансовых счетов в порядке возрастания */
  if ( ASize( ABalAccounts ) > 0 )
    i = 0;
    while ( i < ASize( ABalAccounts ) )
      j = i;
      while ( j < ASize( ABalAccounts ) )
        if ( ABalAccounts( j ) < ABalAccounts( i ) )
          TmpVal = ABalAccounts( i );
          ABalAccounts( i ) = ABalAccounts( j );
          ABalAccounts( j ) = TmpVal;
        end;
        j = j + 1;
      end;
      i = i + 1;
    end;
  end;

end;


/*****************************************************************
                Определение курса валюты за дату
*****************************************************************/
macro GetCurrRate1 (DateRate)

  var Rate;

  copy( RtlCurr, currency );
  Rate = GetCurrRate(DateRate);
  if ((Rate != 0) AND (Current_REPORT == REPORT_CrossRate))
    Rate = CurrRate.CrossRate;
  end;

  return Rate;

end;


/*****************************************************************
                     Печать заголовка отчета
*****************************************************************/
macro WriteHeadForReport

  var str;
  var RecFound;

  macro WriteStrHead (WrStr)
    if (FlagCur)
      [ ###################################################################################################](WrStr:c);
    else
      [ ##############################################################################](WrStr:c);
    end;
  end;

  [ ];

  WriteStrHead("Ведомость остатков вкладов");

  if (FlagCur)
    if (Current_REPORT == REPORT_RubRate)
      str = "с пересчетом прочих валют в национальную валюту";
    else
      str = "с пересчетом прочих валют в доллары по кросс-курсам";
    end;
    WriteStrHead(str);
  end;

  if (StatusBranch == 1)
    str = "по учреждениям отделения";
  elif (StatusBranch == 3)
    str = "по оперчасти";
  elif (StatusBranch == 0)
    str = "по филиалу";
  else
    str = "по учреждению";
  end;
  str = str + " " + NameBranch + " за " + String(DateReport) + "г.";
  WriteStrHead(str);

  /* Печать таблицы курсов валют к рублю или доллару */
  if (FlagCur)
    [ ];
    if (Current_REPORT == REPORT_RubRate)
      [ -------------------------------------------];
      [ |   Курсы валюты   |   |                  |];
      [ |------------------|Вал|  Наимен. валюты  |];
      [ | Текущий | Предыд.|   |                  |];
      [ -------------------------------------------];
    else
      [ -------------------------------------------];
      [ |Кросс-курсы валюты|   |                  |];
      [ |------------------|Вал|  Наимен. валюты  |];
      [ | Текущий | Предыд.|   |                  |];
      [ -------------------------------------------];
    end;
    currency.rec.Code_Currency = 1;

    currency.addFilter( "t.t_CrDifrRate != chr( 0 )" );

    RecFound = currency.GetGE;
    while(RecFound)
      if( currency.rec.CrDifrRate ) /* валюта используется */
        PrevRate = GetCurrRate1(DateReport - 1);
        CurRate  = GetCurrRate1(DateReport);
        [ |#########|########|###|##################|]
        (Double(CurRate):9:4,Double(PrevRate):8:4,currency.rec.Short_Name,currency.rec.Name_Currency:w);
      end;
      RecFound = currency.Next;
    end;

    currency.dropFilter;

    [ -------------------------------------------];
  end;

  [ ];

  if (FlagCur)
    if (Current_REPORT == REPORT_RubRate)
      str = "Остаток в рублях";
    else
      str = "Остаток в долларах";
    end;
    [ ----------------------------------------------------------------------------------------------------];
    [ |Номер|                        |    | Количество счетов  |                    |                    |];
    [ |счета|       Вид вклада       |Вал.|--------------------|  Остаток в валюте  |####################|]
    (str:c);
    [ |     |                        |    |всего | откр.| закр.|                    |                    |];
    [ ----------------------------------------------------------------------------------------------------];
  else
    [ -------------------------------------------------------------------------------];
    [ |Номер|                        |    | Количество счетов  |                    |];
    [ |счета|       Вид вклада       |Вал.|--------------------|  Остаток в рублях  |];
    [ |     |                        |    |всего | откр.| закр.|                    |];
    [ -------------------------------------------------------------------------------];
  end;

end;


/*****************************************************************
     Расчет итоговых сумм по всем балансовым счетам и вкладам
*****************************************************************/
macro CalcItogAll (CodCurrency,AllAccount,OpenAccount,CloseAccount,Rest,RestEqv)

  var i = 0;

  CountAllAccAll   = CountAllAccAll + AllAccount;
  CountOpenAccAll  = CountOpenAccAll + OpenAccount;
  CountCloseAccAll = CountCloseAccAll + CloseAccount;
  RestAllAll       = RestAllAll + RestEqv;

  if (FlagCur)
    while (i < ASize(ACodCurrencyAll))
      if (CodCurrency == ACodCurrencyAll(i))
        ACountAllAccAll(i)   = ACountAllAccAll(i) + AllAccount;
        ACountOpenAccAll(i)  = ACountOpenAccAll(i) + OpenAccount;
        ACountCloseAccAll(i) = ACountCloseAccAll(i) + CloseAccount;
        ARestAllAll(i)       = ARestAllAll(i) + Rest;
        ARestEqvAllAll(i)    = ARestEqvAllAll(i) + RestEqv;
        i = ASize(ACodCurrencyAll);
      end;
      i = i + 1;
    end;
  end;

end;


/*****************************************************************
             Вывод информации по виду вклада в отчет
*****************************************************************/
macro WriteStringToReport (NumBA,ColAccounts,ColOpenedAcc,ColClosedAcc,Rest)

  var BalAcc   = "";
  var Kind;
  var SumEqviv = $0.0L;
  var i;
  var NumEl    = -1;

  if (FirstRecForBA)
    FirstRecForBA = FALSE;
  elif (FirstRecForDT)
    if (FlagCur)
      [ |--------------------------------------------------------------------------------------------------|];
    else
      [ |-----------------------------------------------------------------------------|];
    end;
  end;

  /* Номер балансового счета */
  if (FirstRecForDT)
    BalAcc = ABalAccounts(NumBA);
    FirstRecForDT = FALSE;
  end;

  /* Вид вклада */
  Kind = Trim( typgrp.rec.Kind );
  if (NOT FlagRezid)
    Kind = Kind + " (н/р)";
  end;

  if (FlagCur)
    /*if (FindExchCurr())*/
      CurRate  = GetCurrRate1(DateReport);
      SumEqviv = Money(Rest * Double(CurRate));
    /*end;*/
  else
    SumEqviv = Rest;
  end;

  if (FlagCur)
    [ |#####|########################|### |######|######|######|####################|####################|]
    (BalAcc,Kind:w,currency.rec.Short_Name,ColAccounts:6,ColOpenedAcc:6,ColClosedAcc:6,Rest:20:2:a,SumEqviv:20:2:a);
  else
    [ |#####|########################|### |######|######|######|####################|]
    (BalAcc,Kind:w,currency.rec.Short_Name,ColAccounts:6,ColOpenedAcc:6,ColClosedAcc:6,Rest:20:2:a);
  end;

  /* Подсчет итога общеотчетного */
  CalcItogAll(currency.rec.Code_Currency,ColAccounts,ColOpenedAcc,ColClosedAcc,Rest,SumEqviv);

  /* Подсчет итоговых сумм по балансовому счету с учетом эквивалента */
  CountAllAccBA   = CountAllAccBA   + ColAccounts;
  CountOpenAccBA  = CountOpenAccBA  + ColOpenedAcc;
  CountCloseAccBA = CountCloseAccBA + ColClosedAcc;
  RestAllBA       = RestAllBA       + SumEqviv;

  /* Итоговая сумма по балансовому счету и по валюте */
  if (FlagCur)
    if (ASize(ACurrBA))
      i = 0;
      while (i < ASize(ACurrBA))
        if (ACurrBA(i) == currency.rec.Code_Currency)
          NumEl = i;
          i = ASize(ACurrBA); /* Чтобы быстрее выйти из цикла */
        end;
        i = i + 1;
      end;
    end;

    if (NumEl == -1)
      i = ASize(ACurrBA);
      ACurrBA(i)          = currency.rec.Code_Currency;
      ANameCurrBA(i)      = currency.rec.Short_Name;
      ACountAllAccBA(i)   = ColAccounts;
      ACountOpenAccBA(i)  = ColOpenedAcc;
      ACountCloseAccBA(i) = ColClosedAcc;
      ARestBA(i)          = Rest;
      ARestEqvBA(i)       = SumEqviv;
    else
      ACountAllAccBA(NumEl)   = ACountAllAccBA(NumEl)   + ColAccounts;
      ACountOpenAccBA(NumEl)  = ACountOpenAccBA(NumEl)  + ColOpenedAcc;
      ACountCloseAccBA(NumEl) = ACountCloseAccBA(NumEl) + ColClosedAcc;
      ARestBA(NumEl)          = ARestBA(NumEl)          + Rest;
      ARestEqvBA(NumEl)       = ARestEqvBA(NumEl)       + SumEqviv;
    end;
  end;

  /* Итоговая сумма по виду вклада с учетом эквивалента */
  if (FlagCur)
    CountAllForDT   = CountAllForDT + ColAccounts;
    CountOpenForDT  = CountOpenForDT + ColOpenedAcc;
    CountCloseForDT = CountCloseForDT + ColClosedAcc;
    RestForDT       = RestForDT + SumEqviv;
  end;

end;


/*****************************************************************
    Вывод информации по виду вклада в отчет для сложной ставки
*****************************************************************/
macro WriteStringToReport2 (NumBA)

  var BalAcc = "";
  var Kind;
  var Name;
  var i = 0;

  if (CountAllAccDT OR RestDT)
    if (FirstRecForBA)
      FirstRecForBA = FALSE;
    elif (FirstRecForDT)
      if (FlagCur)
        [ |--------------------------------------------------------------------------------------------------|];
      else
        [ |-----------------------------------------------------------------------------|];
      end;
    end;

    /* Номер балансового счета */
    if (FirstRecForDT)
      BalAcc = ABalAccounts(NumBA);
      FirstRecForDT = FALSE;
    end;

    /* Вид вклада */
    Kind = Trim(typgrp.rec.Kind);
    if (NOT FlagRezid)
      Kind = Kind + " (н/р)";
    end;

    if (FlagCur)
      [ |#####|########################|### |######|######|######|####################|####################|]
      (BalAcc,Kind:w,currency.rec.Short_Name,CountAllAccDT:6,CountOpenAccDT:6,CountCloseAccDT:6,RestDT:20:2:a,RestEqvDT:20:2:a);
    else
      [ |#####|########################|### |######|######|######|####################|]
      (BalAcc,Kind:w,currency.rec.Short_Name,CountAllAccDT:6,CountOpenAccDT:6,CountCloseAccDT:6,RestDT:20:2:a);
    end;

    i = 0;
    while (i < ASize(ABoardDT))
      if (ACountAllAccDT(i) OR ARestDT(i))
        if (i == ASize(ABoardDT) - 1)
          Name = "свыше " + String(ABoardDT(i):10:2:a);
        else
          Name = String(ABoardDT(i):10:2:a) + " - " + String(ABoardDT(i + 1):10:2:a);
        end;
        if (FlagCur)
          [ |     | #######################|    |######|######|######|####################|####################|]
          (Name,ACountAllAccDT(i):6,ACountOpenAccDT(i):6,ACountCloseAccDT(i):6,ARestDT(i):20:2:a,ARestEqvDT(i):20:2:a);
        else
          [ |     | #######################|    |######|######|######|####################|]
          (Name,ACountAllAccDT(i):6,ACountOpenAccDT(i):6,ACountCloseAccDT(i):6,ARestDT(i):20:2:a);
        end;
      end;
      i = i + 1;
    end;
  end;

end;


/*****************************************************************
          Проверка вида ставки для вида вклада по валюте
*****************************************************************/
macro RateDependFromRest

  var stat = FALSE;
  var i;
  
  var
    cmd_getPcRate = RsdCommand( "select * " +
                                "from ( select t_RestCount, t_Rest1, t_Rest2, t_Rest3, t_Rest4, t_Rest5, t_Rest6, t_Rest7, t_Rest8 " + 
                                "       from dpc_rate_dbt " +
                                "       where t_FlagCur = ? " +
                                "         and t_ObjectType = 1003 " +
                                "         and t_Referenc = ? " +
                                "         and t_Code_Currency = ? " +
                                "         and t_BeginDate <= ? " + 
                                "         and t_PercAlg = 2 " +
                                "       order by t_FlagCur, t_ObjectType, t_Referenc, t_Code_Currency, t_BeginDate, t_DatePeriodType, t_DatePeriod )" +
                                "where rownum = 1" );

  var rs = RsdRecordset( cmd_getPcRate );

  cmd_getPcRate.addParam( "flagCur", RSDBP_IN );
  cmd_getPcRate.addParam( "referenc", RSDBP_IN );
  cmd_getPcRate.addParam( "code_currency", RSDBP_IN );
  cmd_getPcRate.addParam( "beginDate", RSDBP_IN );

  ASize(ABoardDT,0);
  ASize(ACountAllAccDT,0);
  ASize(ACountOpenAccDT,0);
  ASize(ACountCloseAccDT,0);
  ASize(ARestDT,0);
  ASize(ARestEqvDT,0);

  cmd_getPcRate.value( "flagCur" ) = typgrp.rec.FlagCur;
  cmd_getPcRate.value( "referenc" ) = typgrp.rec.Kind;
  cmd_getPcRate.value( "code_currency" ) = currency.rec.Code_Currency;
  cmd_getPcRate.value( "beginDate" ) = DateReport + 1;

  cmd_getPcRate.execute;

  if( rs.moveNext )
    stat = TRUE;
    i = 0;
    while ( i < rs.value( "t_RestCount" ) )
      ABoardDT(i) = rs.value( "t_Rest" + string( i + 1 ) );
      ACountAllAccDT(i)   = 0;
      ACountOpenAccDT(i)  = 0;
      ACountCloseAccDT(i) = 0;
      ARestDT(i)          = $0.0L;
      ARestEqvDT(i)       = $0.0L;
      i = i + 1;
    end;
  end;

  return stat;
end;


/*****************************************************************
                Определение резидентности клиента
*****************************************************************/
macro IsNeedRezidAccount


  var stat    = TRUE;
  var RezAcc  = "";
  var FlagRez = "";

  if (NOT FlagRezid)
    FlagRez = StrFor(1);
  end;

  if ( depclnt.GetRecord( depositr.rec.CodClient ) )
    RezAcc = depclnt.CurRec.rec.NotResident;
  end;

  if (RezAcc != FlagRez)
    stat = FALSE;
  end;

  return stat;

end;


/*****************************************************************
      Определение остатка по счету вкладчика на дату отчета
*****************************************************************/
macro DefRestForAccountAtDateReport


  var stat;
  var Delta = -1;
  var Rest  = $0.0L;

  if ((depositr.rec.Close_Date == Date(0,0,0)) OR
      (depositr.rec.Close_Date >  DateReport )   )
    sbdepdoc.rec.Referenc         = depositr.rec.Referenc;
    sbdepdoc.rec.Date_Document = Date(0,0,0);
    sbdepdoc.rec.NumDayDoc        = 0;

    sbdepdoc.addFilter( "t.t_Referenc = " + string( depositr.rec.Referenc ) );

    stat = sbdepdoc.GetGE;
    while (stat)
      if (sbdepdoc.rec.Referenc == depositr.rec.Referenc)
        if (IsServDoc(sbdepdoc.rec))
          if (sbdepdoc.rec.Date_Document <= DateReport)
            if ((Delta < 0) OR (DateReport - sbdepdoc.rec.Date_Document <= Delta))
              Delta = DateReport - sbdepdoc.rec.Date_Document;
              Rest = sbdepdoc.rec.Rest;
            end;
          end;
        end;
        stat = sbdepdoc.Next;
      else
        stat = FALSE;
      end;
    end;
  end;

  sbdepdoc.dropFilter;

  return Rest;

end;


/*****************************************************************
        Подсчет остатка по виду вклада для сложных ставок
*****************************************************************/
macro CalcCountAndRestFromDepDoc

  var   flag;
  var   i;
  var   j;
  var   l;
  var   Rest;
  var   SumEqviv;
  var   NumEl = -1;
  var   NumRate;
  array AOpCl;
    AOpCl(0) = "";
    AOpCl(1) = "З";

  CountAllAccDT   = 0;
  CountOpenAccDT  = 0;
  CountCloseAccDT = 0;
  RestDT          = $0.0L;
  RestEqvDT       = $0.0L;

  i = 0;
  while (i < ASize(AOpCl))
    l = 0;
    while (l < ASize(Branches))
      depositr.rec.IsCur         = FlagCur;
      depositr.rec.FNCash        = Branches(l);
      depositr.rec.Open_Close    = AOpCl(i);
      depositr.rec.Type_Account  = typgrp.rec.Kind;
      depositr.rec.Code_Currency = currency.rec.Code_Currency;
      depositr.rec.Number        = 0;

      depositr.addFilter( "t.t_IsCur = " + string( FlagCur ) +
                          " and t.t_FNCash = " + string( Branches(l) ) +
                          " and t.t_Open_Close = " + sqlChar( AOpCl(i) ) +
                          " and t.t_Type_Account = " + GetSQLString( typgrp.rec.Kind ) +
                          " and t.t_Code_Currency = " + string( currency.rec.Code_Currency ) + 
                          " and t.t_Open_Date <= " + sqlDateToStr( DateReport ) );

      /* depositr.Account       = ""; */
      flag = GetGE(depositr);
      while (flag)
        if ((depositr.rec.IsCur         == FlagCur               ) AND
            (depositr.rec.FNCash        == Branches(l)           ) AND
            (depositr.rec.Open_Close    == AOpCl(i)              ) AND
            (depositr.rec.Type_Account  == typgrp.rec.Kind       ) AND
            (depositr.rec.Code_Currency == currency.rec.Code_Currency)    )
          if (depositr.rec.Open_Date <= DateReport)
            if (IsNeedRezidAccount())
              Rest = DefRestForAccountAtDateReport();
              SumEqviv = $0.0L;
              if (FlagCur)
                /*if (FindExchCurr())*/
                  CurRate  = GetCurrRate1(DateReport);
                  SumEqviv = Money(Rest * Double(CurRate));
                /*end;*/
              else
                SumEqviv = Rest;
              end;
              j = 0;
              NumRate = 0;
              while (j < ASize(ABoardDT))
                if (j == ASize(ABoardDT) - 1)
                  if (Rest > ABoardDT(j))
                    NumRate = j;
                  end;
                else
                  if ((Rest > ABoardDT(j)) AND (Rest <= ABoardDT(j + 1)))
                    NumRate = j;
                  end;
                end;
                j = j + 1;
              end;
              if (NumRate >= 0)
                ACountAllAccDT(NumRate)   = ACountAllAccDT(NumRate) + 1;
                if ((depositr.rec.Close_Date <= DateReport) AND (depositr.rec.Close_Date != Date(0,0,0)))
                  ACountCloseAccDT(NumRate) = ACountCloseAccDT(NumRate) + 1;
                else
                  ACountOpenAccDT(NumRate)  = ACountOpenAccDT(NumRate) + 1;
                end;
                ARestDT(NumRate)          = ARestDT(NumRate) + Rest;
                ARestEqvDT(NumRate)       = ARestEqvDT(NumRate) + SumEqviv;
              end;
              /* Подсчет общеитоговых отчетных сумм */
              if ((depositr.rec.Close_Date <= DateReport) AND (depositr.rec.Close_Date != Date(0,0,0)))
                CalcItogAll(currency.rec.Code_Currency,1,0,1,Rest,SumEqviv);
              else
                CalcItogAll(currency.rec.Code_Currency,1,1,0,Rest,SumEqviv);
              end;
              /* Итого по виду вклада по валюте */
              CountAllAccDT   = CountAllAccDT + 1;
              RestDT          = RestDT + Rest;
              RestEqvDT       = RestEqvDT + SumEqviv;
              if ((depositr.rec.Close_Date <= DateReport) AND (depositr.rec.Close_Date != Date(0,0,0)))
                CountCloseAccDT = CountCloseAccDT + 1;
              else
                CountOpenAccDT  = CountOpenAccDT + 1;
              end;
              /* Подсчет итоговых сумм по балансовому счету */
              CountAllAccBA   = CountAllAccBA + 1;
              RestAllBA       = RestAllBA     + SumEqviv;
              if ((depositr.rec.Close_Date <= DateReport) AND (depositr.rec.Close_Date != Date(0,0,0)))
                CountCloseAccBA = CountCloseAccBA + 1;
              else
                CountOpenAccBA  = CountOpenAccBA  + 1;
              end;
              /* Итоговая сумма по виду вклада с учетом эквивалента */
              if (FlagCur)
                CountAllForDT   = CountAllForDT + 1;
                RestForDT       = RestForDT + SumEqviv;
                if ((depositr.rec.Close_Date <= DateReport) AND (depositr.rec.Close_Date != Date(0,0,0)))
                  CountCloseForDT = CountCloseForDT + 1;
                else
                  CountOpenForDT  = CountOpenForDT  + 1;
                end;
              end;
              /* Итоговая сумма по балансовому счету и по валюте */
              if (FlagCur)
                if (ASize(ACurrBA))
                  j = 0;
                  while (j < ASize(ACurrBA))
                    if (ACurrBA(j) == currency.rec.Code_Currency)
                      NumEl = j;
                      j = ASize(ACurrBA); /* Чтобы быстрее выйти из цикла */
                    end;
                    j = j + 1;
                  end;
                end;

                if (NumEl == -1)
                  j = ASize(ACurrBA);
                  ACurrBA(j)        = currency.rec.Code_Currency;
                  ANameCurrBA(j)    = currency.rec.Short_Name;
                  ACountAllAccBA(j) = 1;
                  ARestBA(j)        = Rest;
                  ARestEqvBA(j)     = SumEqviv;
                  if ((depositr.rec.Close_Date <= DateReport) AND (depositr.rec.Close_Date != Date(0,0,0)))
                    ACountOpenAccBA(j)  = 0;
                    ACountCloseAccBA(j) = 1;
                  else
                    ACountOpenAccBA(j)  = 1;
                    ACountCloseAccBA(j) = 0;
                  end;
                else
                  ACountAllAccBA(NumEl)   = ACountAllAccBA(NumEl)   + 1;
                  if ((depositr.rec.Close_Date <= DateReport) AND (depositr.rec.Close_Date != Date(0,0,0)))
                    ACountCloseAccBA(NumEl) = ACountCloseAccBA(NumEl) + 1;
                  else
                    ACountOpenAccBA(NumEl)  = ACountOpenAccBA(NumEl)  + 1;
                  end;
                  ARestBA(NumEl)          = ARestBA(NumEl)          + Rest;
                  ARestEqvBA(NumEl)       = ARestEqvBA(NumEl)       + SumEqviv;
                end;
              end;
            end;
          end;
          flag = depositr.Next;
        else
          flag = FALSE;
        end;
      end;

      depositr.dropFilter;

      l = l + 1;
    end;
    i = i + 1;
  end;

end;


/*****************************************************************
         Печать итога по виду вклада с учетом эквивалента
*****************************************************************/
macro WriteSummaryForDT

  if (FlagCur)
    if (CountAllForDT OR RestForDT) /* Если по данному вкладу есть счета */
      [ |--------------------------------------------------------------------------------------------------|];
      [ |Итого ########################|    |######|######|######|                    |####################|]
      (typgrp.rec.Kind,CountAllForDT:6,CountOpenForDT:6,CountCloseForDT:6,RestForDT:20:2:a);
    end;
  end;

end;


/*****************************************************************
                   Обработка одного вида вклада
*****************************************************************/
macro WorkWithDType (NumBA)

  var stat;
  var flag;
  var FlagRez = "";
  var i = 0;
  var CountAccStat;
  var CountOpAccStat;
  var CountClAccStat;
  var SumRestStat;
  var rs;

  var
    cmd_getDepStat = RsdCommand( "select * " +
                                 "from ( select t_ColAccounts, t_ColOpenedAcc, t_ColClosedAcc, t_Rest " +
                                 "       from dsb_depst_dbt " +
                                 "       where t_FNCash = ? " +
                                 "         and t_IsCur = ? " +
                                 "         and t_Kind = ? " +
                                 "         and t_CodCur = ? " +
                                 "         and t_FlagRez = chr( ? ) " +
                                 "         and ( t_Date < ? or ( t_Date = ? and t_Mode <= ? ) ) " +
                                 "       order by t_Date desc, t_Mode desc )" +
                                 "where rownum = 1" );  

  cmd_getDepStat.addParam( "fnCash", RSDBP_IN );
  cmd_getDepStat.addParam( "isCur", RSDBP_IN );
  cmd_getDepStat.addParam( "kind", RSDBP_IN );
  cmd_getDepStat.addParam( "codCur", RSDBP_IN );
  cmd_getDepStat.addParam( "flagRez", RSDBP_IN );
  cmd_getDepStat.addParam( "date", RSDBP_IN );
  cmd_getDepStat.addParam( "date2", RSDBP_IN );
  cmd_getDepStat.addParam( "mode", RSDBP_IN );

  if (FlagCur)
    if (Current_REPORT == REPORT_RubRate)
      Message(" Часть 1. Обработка счета " + Trim(ABalAccounts(NumBA)) + " вклад " + Trim(typgrp.rec.Kind) + " ...");
    else
      Message(" Часть 2. Обработка счета " + Trim(ABalAccounts(NumBA)) + " вклад " + Trim(typgrp.rec.Kind) + " ...");
    end;
  else
    Message(" Обработка счета " + Trim(ABalAccounts(NumBA)) + " вклад " + Trim(typgrp.rec.Kind) + " ...");
  end;

  /* Обнуление итоговых параметров по виду вклада */
  CountAllForDT   = 0;
  CountOpenForDT  = 0;
  CountCloseForDT = 0;
  RestForDT       = $0.0L;
  FirstRecForDT   = TRUE;

  if (NOT FlagRezid)
    FlagRez = StrFor(1);
  end;

  ReWind(currency);
  if (FlagCur)
    currency.rec.Code_Currency = 1;
    currency.addFilter( "t.t_CrDifrRate != chr( 0 )" );
    stat = currency.GetGE;
  else
    currency.rec.Code_Currency = 0;
    currency.addFilter( "t.t_CrDifrRate != chr( 0 )" );
    stat = currency.GetEQ;
  end;

  while (stat)
    if( currency.rec.CrDifrRate ) /* валюта используется */
      /* Если ставка сложная - идем по всем счетам данного вклада в данной валюте */
      if (RateDependFromRest())
        CalcCountAndRestFromDepDoc();
        WriteStringToReport2(NumBA);
      /* Если ставка простая - идем по файлу статистики */
      else
        i              = 0;
        CountAccStat   = 0;
        CountOpAccStat = 0;
        CountClAccStat = 0;
        SumRestStat    = $0.0L;
        while (i < ASize(Branches))
          cmd_getDepStat.value( "fnCash" ) = Branches(i);
          cmd_getDepStat.value( "isCur" ) = FlagCur;
          cmd_getDepStat.value( "kind" ) = typgrp.rec.Kind;
          cmd_getDepStat.value( "codCur" ) = currency.rec.Code_Currency;
          cmd_getDepStat.value( "flagRez" ) = codeFor( flagRez );
          cmd_getDepStat.value( "date" ) = DateReport;
          cmd_getDepStat.value( "date2" ) = DateReport;
          cmd_getDepStat.value( "mode" ) = Mode;
                                 
          cmd_getDepStat.execute;
          rs = RsdRecordset( cmd_getDepStat );

          if ( rs.moveNext )
            CountAccStat   = CountAccStat + rs.value( "t_ColAccounts" );
            CountOpAccStat = CountOpAccStat + rs.value( "t_ColOpenedAcc" );
            CountClAccStat = CountClAccStat + rs.value( "t_ColClosedAcc" );
            SumRestStat    = SumRestStat + rs.value( "t_Rest" );
          end;
          i = i + 1;
        end;
        if (CountAccStat OR SumRestStat)
          WriteStringToReport(NumBA,CountAccStat,CountOpAccStat,CountClAccStat,SumRestStat);        
        end;
      end;
    end;

    if (FlagCur)
      stat = currency.Next;
    else
      stat = FALSE;
    end;

  end;

  currency.dropFilter;

  if (FlagCur)
    WriteSummaryForDT();
  end;

end;


/*****************************************************************
                Печать итога по балансовому счету
*****************************************************************/
macro WriteSummaryForBA (NumBA)

  var i = 0;

  if (CountAllAccBA OR RestAllBA) /* Если по данному балансовому счету есть счета */
    if (FlagCur)
      [ |==================================================================================================|];
    else
      [ |=============================================================================|];
    end;
    if (FlagCur)
      while (i < ASize(ACurrBA))
        [ |ИТОГО по бал.счету ###########|### |######|######|######|####################|####################|]
        (ABalAccounts(NumBA),ANameCurrBA(i),ACountAllAccBA(i):6,ACountOpenAccBA(i):6,
         ACountCloseAccBA(i):6,ARestBA(i):20:2:a,ARestEqvBA(i):20:2:a);
        i = i + 1;
      end;
      [ |--------------------------------------------------------------------------------------------------|];
      [ |ИТОГО по бал.счету ###########|    |######|######|######|                    |####################|]
      (ABalAccounts(NumBA),CountAllAccBA:6,CountOpenAccBA:6,CountCloseAccBA:6,RestAllBA:20:2:a);
    else
      [ |ИТОГО по бал.счету ###########|    |######|######|######|####################|]
      (ABalAccounts(NumBA),CountAllAccBA:6,CountOpenAccBA:6,CountCloseAccBA:6,RestAllBA:20:2:a);
    end;
    if (FlagCur)
      [ |==================================================================================================|];
    else
      [ |=============================================================================|];
    end;
  end;

end;


/*****************************************************************
                  Печать общеотчетного итога
*****************************************************************/
macro WriteSummaryAll

  var i = 0;
  var Name;
  var First = TRUE;

  if (CountAllAccAll OR RestAllAll) /* Если есть счета */
    if (FlagCur)
      while (i < ASize(ACodCurrencyAll))
        if (ACountAllAccAll(i) OR ARestAllAll(i))
          if (First)
            Name = "ИТОГО";
            First = FALSE;
          else
            Name = "";
          end;
          [ |#                             |### |######|######|######|####################|####################|]
          (Name,ANameCurrencyAll(i),ACountAllAccAll(i):6,ACountOpenAccAll(i):6,
           ACountCloseAccAll(i):6,ARestAllAll(i):20:2:a,ARestEqvAllAll(i):20:2:a);
        end;
        i = i + 1;
      end;
      [ |--------------------------------------------------------------------------------------------------|];
      [ |ИТОГО                         |    |######|######|######|                    |####################|]
      (CountAllAccAll:6,CountOpenAccAll:6,CountCloseAccAll:6,RestAllAll:20:2:a);
    else
      [ |ИТОГО                         |    |######|######|######|####################|]
      (CountAllAccAll:6,CountOpenAccAll:6,CountCloseAccAll:6,RestAllAll:20:2:a);
    end;
    if (FlagCur)
      [ |==================================================================================================|];
    else
      [ |=============================================================================|];
    end;
  end;

end;


/*****************************************************************
                Обнуление общеотчетных параметров
*****************************************************************/
macro ZeroAllParam

  var i = 0;
  var RecFound;

  CountAllAccAll   = 0;
  CountOpenAccAll  = 0;
  CountCloseAccAll = 0;
  RestAllAll       = $0.0L;
  ASize(ACodCurrencyAll,0);
  ASize(ANameCurrencyAll,0);
  ASize(ACountAllAccAll,0);
  ASize(ACountOpenAccAll,0);
  ASize(ACountCloseAccAll,0);
  ASize(ARestAllAll,0);
  ASize(ARestEqvAllAll,0);
  if (FlagCur)
    currency.rec.Code_Currency = 1;
    currency.addFilter( "t.t_CrDifrRate != chr( 0 )" );
    RecFound = currency.GetGE;
    while(RecFound)
      if( currency.rec.CrDifrRate ) /* валюта используется */
        ACodCurrencyAll(i)   = currency.rec.Code_Currency;
        ANameCurrencyAll(i)  = currency.rec.Short_Name;
        ACountAllAccAll(i)   = 0;
        ACountOpenAccAll(i)  = 0;
        ACountCloseAccAll(i) = 0;
        ARestAllAll(i)       = $0.0L;
        ARestEqvAllAll(i)    = $0.0L;
        i = i + 1;
      end;
      RecFound = currency.Next;
    end;
    currency.dropFilter;
  end;

end;


/*****************************************************************
               Обнуление параметров по виду вклада
*****************************************************************/
macro ZeroDTParam

  CountAllAccBA   = 0;
  CountOpenAccBA  = 0;
  CountCloseAccBA = 0;
  RestAllBA       = $0.0L;
  ASize(ACurrBA,0);
  ASize(ANameCurrBA,0);
  ASize(ACountAllAccBA,0);
  ASize(ACountOpenAccBA,0);
  ASize(ACountCloseAccBA,0);
  ASize(ARestEqvBA,0);
  FirstRecForBA = TRUE;

end;


/*****************************************************************
             Цикл по видам вкладов и вывод их в отчет
*****************************************************************/
macro WriteDTypeToReport

  var i = 0;
  var stat;
  var stat2;

  ZeroAllParam( );

  while ( i < ASize( ABalAccounts ) )

    ZeroDTParam( );

    sbnumgrp.Clear;
    sbnumgrp.Rewind;
    stat = sbnumgrp.Next;

    while ( stat )

      if ( ABalAccounts( i ) == Trim( sbnumgrp.rec.BalAcc2 ) )

        typgrp.Clear;
        typgrp.rec.FlagCur = FlagCur;
        typgrp.Rewind( );
        stat2 = typgrp.GetGE;

        while ( stat2 )
          if ( typgrp.rec.FlagCur == FlagCur )
            if ( typgrp.rec.GroupID == sbnumgrp.rec.GroupID )
              FlagRezid = true;
              WorkWithDType( i );
            end;
            stat2 = typgrp.Next;
          else
            stat2 = false;
          end;
        end;
      end;

      if ( ABalAccounts( i ) == Trim( sbnumgrp.rec.BalAcc2NotResid ) )

        typgrp.Clear;
        typgrp.rec.FlagCur = FlagCur;
        typgrp.Rewind( );
        stat2 = typgrp.GetGE;

        while ( stat2 )
          if ( typgrp.rec.FlagCur == FlagCur )
            if ( typgrp.rec.GroupID == sbnumgrp.rec.GroupID )
              FlagRezid = false;
              WorkWithDType( i );
            end;
            stat2 = typgrp.Next;
          else
            stat2 = false;
          end;
        end;
      end;

      stat = sbnumgrp.Next;
    end;

    WriteSummaryForBA( i );

    i = i + 1;
  end;

  WriteSummaryAll( );

end;


/*****************************************************************
                Г Л А В Н А Я    П Р О Г Р А М М А
*****************************************************************/

  if ( ChooseBranchAndDateForWork( ) )
    if ( DateReport == {curdate} )
      if ( not UpdateStatistics( true ) )
        Exit;
      end;
    end;
    DefBalAccounts( );
    WriteHeadForReport( );
    WriteDTypeToReport( );
    if ( FlagCur )

      Current_REPORT = REPORT_CrossRate;
      [#](StrFor(12));

      WriteHeadForReport( );
      WriteDTypeToReport( );
    end;
  end;

end;
