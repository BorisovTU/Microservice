/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Страхование частных вкладов.

  Выписка из Реестра обязательств.

*****************************************************************/

  import DeprIntr, DI_Def, DI_PapTp, commclnt;

  file dpe ( depiprev ) key 1;

  var eDepClnt = TClientList; /* Справочник вкладчиков */

  /* Глобализмы. */
  /*var {Name_Bank};*/
  /*var {Post_Addr};*/

  /* Атрибуты клиента. */
  var veFIO;
  var veAddress;
  var vePhone;
  var vePapers;

  private var rAddr = TRecHandler( "i_addr.rec" );
  private var tCountry  = TBFile( "country.dbt" , "R", 2, null, "bank.def" );

/**************************************************************************\
|                               Получение телефона                         |
\**************************************************************************/

macro GetPhoneNumberRelex(ClntCode)
  var DepClnt = TClientList;
  var AddrTypes = TArray;
  var i = 0;
  var PhoneNumber = "";
  var PhoneNumber2 = "";
  
  if (DepClnt.GetRecord (clntCode))
    AddrTypes(0) = COMMCLNT_PTADDR_LEGAL;
    AddrTypes(1) = COMMCLNT_PTADDR_REAL;
    AddrTypes(2) = COMMCLNT_PTADDR_POST;

    while ((i < AddrTypes.Size) and (PhoneNumber == ""))
      PhoneNumber = commclnt_GetAddressField(DepClnt, AddrTypes(i), "PhoneNumber");
      PhoneNumber2 = commclnt_GetAddressField(DepClnt, AddrTypes(i), "PhoneNumber2");
      if ((PhoneNumber != "") and (PhoneNumber2 != ""))
        PhoneNumber = PhoneNumber + ", ";
      end;
      PhoneNumber = PhoneNumber + PhoneNumber2;
      i = i + 1;
    end;
  end;
  return PhoneNumber;
end;


private macro strSection( _code, _name )
  if ( ( ValType( _name ) == V_STRING )  AND  ( StrLen( _name ) > 0 ) )
    return _code + " " + _name + ", ";
  else
    return "";
  end;
end;

private macro strHouse( _code, _name )
  if ( ( ValType( _name ) == V_STRING )  AND  ( StrLen( _name ) > 0 ) )
    return ", " + _code + " " + _name;
  else
    return "";
  end;
end;

private macro strFlat( _name )
  if ( ( ValType( _name ) == V_STRING )  AND  ( StrLen( _name ) > 0 ) )
    return ", кв. " + _name;
  else
    return "";
  end;
end;

private macro addrOurBankRUS_Fields()
  var vPlace = FALSE;
  if   ( ( ValType( rAddr.rec.Region ) == V_STRING )  AND  ( StrLen( rAddr.rec.Region ) > 0 ) )
    vPlace = TRUE;
  elif ( ( ValType( rAddr.rec.Place ) == V_STRING )  AND  ( StrLen( rAddr.rec.Place ) > 0 ) )
    vPlace = TRUE;
  elif ( ( ValType( rAddr.rec.District ) == V_STRING )  AND  ( StrLen( rAddr.rec.District ) > 0 ) )
    vPlace = TRUE;
  elif ( ( ValType( rAddr.rec.Province ) == V_STRING )  AND  ( StrLen( rAddr.rec.Province ) > 0 ) )
    vPlace = TRUE;
  end;
  if ( vPlace )
    return rAddr.rec.PostIndex + ", " + 
           strSection( rAddr.rec.Region, rAddr.rec.CodeRegion ) +
           strSection( rAddr.rec.Province, rAddr.rec.lCodeProvince ) +
           strSection( rAddr.rec.lCodeDistrict, rAddr.rec.District ) +
           strSection( rAddr.rec.lCodePlace, rAddr.rec.Place ) +
           rAddr.rec.lCodeStreet + " " + 
           rAddr.rec.StreetName +
           strHouse( "д.", rAddr.rec.House ) +
           strHouse( "корп.", rAddr.rec.NumCorp ) +
           strFlat( rAddr.rec.Flat );
  else
    return rAddr.rec.Address;
  end;
end;

private macro addrRUS( _id )
  var vFind = FALSE;
  var strAddr = "";
  var retAddr = "";
  if ( Addr_Str( _id, I_PTADDR_POST, strAddr )  AND  ( StrLen( strAddr ) > 0 ) )
    vFind = TRUE;
  else
    if ( Addr_Str( _id, I_PTADDR_REAL, strAddr )  AND  ( StrLen( strAddr ) > 0 ) )
      vFind = TRUE;
    else
      if ( Addr_Str( _id, I_PTADDR_LEGAL, strAddr )  AND  ( StrLen( strAddr ) > 0 ) )
        vFind = TRUE;
      end;
    end;
  end;
  if ( vFind )
    retAddr = strAddr;
  else
    retAddr = addrOurBankRUS_Fields();
  end;
  return retAddr;
end;

private macro addrRUS_Client( _id, _type )
  var vFind = FALSE;
  var strAddr = "";
  var retAddr = "";
  if ( Addr_Str( _id, _type, strAddr )  AND  ( StrLen( strAddr ) > 0 ) )
    vFind = TRUE;
  end;
  if ( vFind )
    retAddr = strAddr;
  else
    retAddr = addrOurBankRUS_Fields();
  end;
  return retAddr;
end;

private macro countryNumCode( _CodeLat3 )
 tCountry.clear();
 tCountry.rec.CodeLat3 = _CodeLat3;
 if ( tCountry.getGE() )
   if ( tCountry.rec.CodeLat3 == _CodeLat3 )
     return tCountry.rec.Name + ", ";
     return "";
   end;
 end;
 return "";
end;

private macro addrForeign()
  var vPlace = "";
  if   ( ( ValType( rAddr.rec.Region ) == V_STRING )  AND  ( StrLen( rAddr.rec.Region ) > 0 ) )
    vPlace = strSection( rAddr.rec.CodeRegion, rAddr.rec.Region );
  elif ( ( ValType( rAddr.rec.Place ) == V_STRING )  AND  ( StrLen( rAddr.rec.Place ) > 0 ) )
    vPlace = strSection( rAddr.rec.lCodePlace, rAddr.rec.Place );
  elif ( ( ValType( rAddr.rec.District ) == V_STRING )  AND  ( StrLen( rAddr.rec.District ) > 0 ) )
    vPlace = strSection( rAddr.rec.lCodeDistrict, rAddr.rec.District );
  elif ( ( ValType( rAddr.rec.Province ) == V_STRING )  AND  ( StrLen( rAddr.rec.Province ) > 0 ) )
    vPlace = strSection( rAddr.rec.lCodeProvince, rAddr.rec.Province );
  end;
  if ( StrLen( vPlace ) > 0 ) 
    return vPlace +
           rAddr.rec.lCodeStreet + " " + 
           rAddr.rec.StreetName +
           strHouse( "д.", rAddr.rec.House ) +
           strHouse( "корп.", rAddr.rec.NumCorp ) +
           strFlat( rAddr.rec.Flat );
  else
    return rAddr.rec.Address;
  end;
end;

private macro addrOurBank()
  var sAddr = {Post_Addr};
  var vFind = FALSE;
  if ( FindAddress( {OurBank}, I_PTADDR_POST,  rAddr ) )
    vFind = TRUE;
  else
    if ( FindAddress( {OurBank}, I_PTADDR_REAL,  rAddr ) )
      vFind = TRUE;
    else
      if ( FindAddress( {OurBank}, I_PTADDR_LEGAL, rAddr ) )
        vFind = TRUE;
      end;
    end;
  end;
  if ( vFind )
    if ( ( ValType( rAddr.rec.Country ) == V_STRING )  AND  ( rAddr.rec.Country == "RUS" ) )
      sAddr = addrRUS( {OurBank} );
    else
      sAddr = addrForeign();
    end;
  end;
  return sAddr;
end;

private macro address_Client( _id, _type )
  var sAddr = "";
  if ( FindAddress( _id, _type, rAddr ) )
    if ( ( ValType( rAddr.rec.Country ) == V_STRING )  AND  ( rAddr.rec.Country == "RUS" ) )
      sAddr = addrRUS_Client( _id );
    else
      sAddr = addrForeign();
    end;
  end;
  return sAddr;
end;

/*****************************************************************/
/*         Процедура "Выписка из Реестра обязательств".          */
/*****************************************************************/
macro DI_Client_Excerpt( eDateMake, eCodClient, eNameFileExcerpt, eNumClient )

  var eLine;
  var eCommonSumR   = $0.0;
  var eCommonSumL   = $0.0;
  var eCommonSum    = $0.0;
  var eCommonSumLim = $0.0;
  var vNameOurBank = get_Bank_Name( {OurBank} );
  var eStat;

  if ( StrLen( vNameOurBank ) < 1 )
    vNameOurBank = {Name_Bank};
  end;

  ReWind( dpe );
  ClearRecord( dpe );
  dpe.DateMake  = eDateMake;
  dpe.CodClient = eCodClient;
  eStat = GetGE( dpe );

  if ( eStat  AND
       ( dpe.DateMake  == eDateMake )  AND
       ( dpe.CodClient == eCodClient ) )

    if ( ( dpe.CodClient != 0 )  AND  ( eDepClnt.GetRecord( dpe.CodClient ) ) )
      veFIO = eDepClnt.CurRec.rec.Name1 + " " +
             eDepClnt.CurRec.rec.Name2 + " " +
             eDepClnt.CurRec.rec.Name3;
      vePhone = GetPhoneNumberRelex(eDepClnt.CurRec.rec.CodClient);
      if ( StrLen( vePhone ) != 0 )
        vePhone = ", тел. " + vePhone;
      end;
      veAddress = eDepClnt.CurRec.rec.Address + vePhone;

      if ( ( StrLen( eDepClnt.CurRec.rec.PaperSeries ) != 0 )  AND
           ( StrLen( eDepClnt.CurRec.rec.PaperNumber ) != 0 )  AND
           ( StrLen( eDepClnt.CurRec.rec.PaperIssuer ) != 0 ) )
        vePapers = DI_PaperTypes1417( eDepClnt.CurRec.rec.PaperKind,
                                      eDepClnt.CurRec.rec.NotResident ) + ": " +
                  eDepClnt.CurRec.rec.PaperSeries + " № " +
                  eDepClnt.CurRec.rec.PaperNumber + ", " +
                  eDepClnt.CurRec.rec.PaperIssuer + ", " +
                  eDepClnt.CurRec.rec.PaperIssuedDate;
      else
        vePapers = "00 № 000000 выдан \"00.00.0000\" подразделение отсутствует";
      end;
    else
      veFIO     = "";
      veAddress = "";
      vePhone   = "";
      vePapers  = "";
    end;

    [


                                               ВЫПИСКА
                           ИЗ РЕЕСТРА ОБЯЗАТЕЛЬСТВ БАНКА ПЕРЕД ВКЛАДЧИКАМИ

     Сведения о банке
     Наименование банка: #
     Почтовый адрес: #
     Регистрационный номер: #

     +------------------------------------------------------------------------------------------+
     |   Номер   |                                |       Адрес ре-      |      Код вида и      |
     |   вклад-  |             Ф.И.О.             |       гистрации,     |      реквизиты       |
     |    чика   |                                |       для почто-     |      документа,      |
     |   по ре-  |                                |        вых уве-      |      удостове-       |
     |   естру   |                                |       домлений,      |       ряющего        |
     |   обяза-  |                                |       телефон,       |       личность       |
     |  тельств  |                                |       электрон-      |                      |
     |           |                                |       ная почта      |                      |
     +-----------+--------------------------------+----------------------+----------------------+
     | ######### | ############################## | #################### | #################### |
     +-----------+--------------------------------+----------------------+----------------------+]
     (
       vNameOurBank,
       addrOurBank(),
       reg_Num_Bank(),
       eNumClient:9,
       veFIO:w,
       veAddress:w,
       vePapers:w
     );

    /* Обязательства. */

    [
                                      Сведения о подлежащих страхованию
                                    обязательствах банка перед вкладчиком
     +--------------------------------------------------------------------------------------------------------------------+
     |  N  |   Номер  | Дата доку- |   Порядковый   |         Номер ли-         |       Сумма в      |       Сумма в      |
     | п/п |   доку-  | мента, на  |   номер фили-  |           цевого          |       валюте       |      рублях по     |
     |     |  мента,  | основании  |   ала, заклю-  |         счета для         |       обяза-       |      курсу Бан-    |
     |     |  на ос-  |  которого  |   чившего до-  |         учета обя-        |      тельств       |      ка России     |
     |     | новании  |   принят   |     говор      |         зательств         |                    |                    |
     |     |  которо- |   вклад    |                |                           |                    |                    |
     |     |  го при- |            |                |                           |                    |                    |
     |     |    нят   |            |                |                           |                    |                    |
     |     |   вклад  |            |                |                           |                    |                    |
     +-----+----------+------------+----------------+---------------------------+--------------------+--------------------+];

    eLine = 1;
    eCommonSumR = $0.0;

    while ( eStat  AND
            ( dpe.DateMake  == eDateMake )   AND
            ( dpe.CodClient == eCodClient )  AND
            ( dpe.Type == TYPE_DEPOS ) )

      [| ### | ######## | ########## | ############## | ######################### | ################## | ################## |]
      (
        eLine:3,
        dpe.NumDoc:8,
        dpe.DateDoc:10,
        find_OrdNumBank( dpe.Branch ):14,
        Acc_Form(dpe.Account):25,
        dpe.SummaRest:a:15:2,
        dpe.SummaRestRUR:a:15:2
      );

      eLine = eLine + 1;
      eCommonSumR = eCommonSumR + dpe.SummaRestRUR;

      /* Результирующие суммы. */
      eCommonSum    = eCommonSum    + dpe.SummaFullRUR;
      eCommonSumLim = eCommonSumLim + dpe.SummaFinalRUR;

      eStat = Next( dpe );

    end;

    [|     |          |            |                |                           |                    |                    |
     +-----+----------+------------+----------------+---------------------------+--------------------+--------------------+
     |  ИТОГО обязательств (руб.)  ################################################################# |                    |
     +--------------------------------------------------------------------------------------------------------------------+]
    ( eCommonSumR:l:a:15:2 );

    /* Требования. */

    [
                             Сведения о встречных требованиях банка к вкладчику
     +--------------------------------------------------------------------------------------------------------------------+
     |  N  |   Номер  | Дата доку- |   Порядковый   |         Номер ли-         |       Сумма в      |       Сумма в      |
     | п/п |   доку-  | мента, на  |   номер фили-  |           цевого          |       валюте       |      рублях по     |
     |     |  мента,  | основании  |   ала, заклю-  |         счета для         |       требо-       |      курсу Бан-    |
     |     |  на ос-  |  которого  |   чившего до-  |           учета           |       вания        |      ка России     |
     |     | новании  |  возникло  |     говор      |         встречных         |                    |                    |
     |     |  которо- | требование |                |         требований        |                    |                    |
     |     |  го воз- |            |                |                           |                    |                    |
     |     |   никло  |            |                |                           |                    |                    |
     |     |   требо- |            |                |                           |                    |                    |
     |     |   вание  |            |                |                           |                    |                    |
     +-----+----------+------------+----------------+---------------------------+--------------------+--------------------+];

    eLine = 1;
    eCommonSumL = $0.0;

    ReWind( dpe );
    ClearRecord( dpe );
    dpe.DateMake  = eDateMake;
    dpe.CodClient = eCodClient;
    dpe.Type      = TYPE_CR_LOAN;
    eStat = GetGE( dpe );

    while ( eStat  AND
            ( dpe.DateMake  == eDateMake )   AND
            ( dpe.CodClient == eCodClient )  AND
            ( dpe.Type >= TYPE_CR_LOAN ) )

      [| ### | ######## | ########## | ############## | ######################### | ################## | ################## |]
      (
        eLine:3,
        dpe.NumDoc:8,
        dpe.DateDoc:10,
        find_OrdNumBank( dpe.Branch ):14,
        Acc_Form(dpe.Account):25,
        dpe.SummaRest:a:15:2,
        dpe.SummaRestRUR:a:15:2
      );

      eLine = eLine + 1;
      eCommonSumL = eCommonSumL + dpe.SummaRestRUR;

      /* Результирующие суммы. */
      eCommonSum    = eCommonSum    + dpe.SummaFullRUR;
      eCommonSumLim = eCommonSumLim + dpe.SummaFinalRUR;

      eStat = Next( dpe );

    end;

    [|     |          |            |                |                           |                    |                    |
     +-----+----------+------------+----------------+---------------------------+--------------------+--------------------+
     |  ИТОГО встречных требований (руб.)  ######################################################### |                    |
     +--------------------------------------------------------------------------------------------------------------------+]
    ( eCommonSumL:l:a:15:2 );

    [   Сумма обязательств по вкладам за вычетом суммы встречных
     требований к вкладчику (руб.)  ##############################################
        Сумма, подлежащая страховому возмещению Агентством по
     страхованию вкладов (руб.)  ##############################################

     Руководитель банка              (Агентство по страхованию вкладов)

     ______________/____________/     _______________________________________

     Главный бухгалтер банка

     ______________/____________/


     М.П.     Дата составления выписки #######################    Дата подписания выписки _______________________]
     (
       eCommonSum:l:a:15:2,
       eCommonSumLim:l:a:15:2,
       eDateMake:l
     );

  end;

end;

/* DI_Client_Excerpt( Date(1,2,2003) , 2000003, "" ); */
