/* Тест на соответствие Документов по операциям (sbdepdoc) и Счетов вкладчиков
   (depositr).
   ∙ для всех ли записей в файле sbdepdoc есть соответствующие записи в файле
     depositr и наоборот;
   ∙ соответствуют ли номера счетов, уникальные первичные идентификаторы и
     номера клиентов;
   ∙ соответствуют ли данные по последней операции в depositr (дата, приход,
     расход, остаток и т.д.) данным последнего документа в sbdepdoc
     (по каждому счету);
   ∙ соответствуют ли данные о том, что счет закрыт в depositr соответствующим
     данным в sbdepdoc и наоборот.
   Автор Зубов В. 22-03-98
*/
import opercode,issrvdoc,acc_form;
/*- Блок констант --------------------------------------------------------*/
const
  D_DELETE            = 2,
  ACCOUTN_OWNER       = StrFor(0),    /* Владелец счета      */
  ACCOUTN_PROXY       = StrFor(1),    /* Доверенное лицо     */
  ACCOUTN_HEIR        = StrFor(2),    /* Наследник           */
  ANONIMUS_CLIENT     = StrFor(3),    /* Анонимный клиент    */
  ACCOUNT_IMPORTER    = StrFor(4),    /* Вноситель           */
  ACCOUNT_TUTOR       = StrFor(5),    /* Опекун              */
  ACCOUNT_OBLIGATOR   = StrFor( 6 ),  /* Под обязательство   */
  ACCOUTN_HEIR_PAY    = StrFor( 7 ),  /* Наследник - выплаты */
  ACCOUNT_UNDER_TRIAL = StrFor( 8 ),  /* По суду             */
  ACCOUNT_PARENT      = StrFor( 9 );  /* Родитель            */

/*- Объявление переменных ------------------------------------------------*/
file doc(sbdepdoc);
file dr(depositr) write;
file trast(sbtrast);

Var found, count; 
Var retVal = true;

macro IsServDr()
  return ( dr.Action == D_DELETE);
end; /* orcam */

macro GetFirstRef()
Var ff;
  InitProgress(Nrecords(doc),"Шаг 1. Проверка наличие счетов для каждого документа");
  count = 0;
  KeyNum(doc, 6);
  Rewind(doc);
  /*doc.Referenc = "";*/
  ff = GetGE(doc);
  While (ff AND (not IsServDoc(doc)))
     ff = Next(doc);
     UseProgress(count);
     count = count + 1;
  end; /* elihw */
  if ( not ff)
    RemProgress();
  end; /*fi*/
  return ff;
end; /*orcam*/

macro GetNextRef()
Var f,oldRef;
  oldRef = doc.Referenc;
  f = True;
  while( f AND ((oldRef == doc.Referenc) OR (not IsServDoc(doc))))
     f = Next(doc);
     UseProgress(count);
     count = count + 1;
  end; /*elihw*/
  if (not f)
    RemProgress();
  end;
  return f;
end; /*orcam*/

macro PrepareFindSameDepositr()
  KeyNum(dr,8);
  Rewind(dr);
end; /*orcam*/

macro FindSameDepositr(Referenc)
  dr.Referenc = Referenc;
  if ( GetEQ(dr) AND (not IsServDr()))
    return True;
  else
    return False;
  end; /* if */
end; /*orcam*/

macro PrintDocInfo()
  [ По документу   Referenc:###############    Account:#########################
    Cur:### Фил:## Type_Account:############   CodClient:########
                  Date_Document:##########   NumDayDoc:########]
  (doc.Referenc, Acc_Form(doc.Account),
   doc.Code_Currency, doc.FNCash, doc.Type_Account, doc.CodClient,
   doc.Date_Document, doc.NumDayDoc);
end; /*orcam*/

macro PrintDrInfo()
  [ По счету       Referenc:###############    Account:#########################
    Cur:### Фил:## Type_Account:############   CodClient:########]
  (dr.Referenc, Acc_Form(dr.Account),
   dr.Code_Currency, dr.FNCash, dr.Type_Account, dr.CodClient);
end; /*orcam*/

macro PrintSeparator()
 [-------------------------------------------];
end; /*orcam*/

macro GetNextDepositr()
Var f;
  f = Next(dr);
  UseProgress(count);
  count = count + 1;
  while (f and IsServDr())
    f = Next(dr);
    UseProgress(count);
    count = count + 1;
  end; /* elihw */
  if (not f)
    RemProgress();
  end;
  return f;
end; /*orcam*/

macro GetFirstDepositr()
Var ff;
  InitProgress(Nrecords(dr),"Шаг 2. Проверка истории по каждому счету");
  count = 0;
  KeyNum(dr, 8);
  Rewind(dr);
  dr.Referenc = "";
  ff = GetGE(dr);
  While (ff AND IsServDr())
     GetNextDepositr();
  end; /* elihw */
  return ff;
end; /*orcam*/

macro GetLastDoc()
Var ff, WasError = False;
  KeyNum(doc,6);
  Rewind(doc);
  doc.Referenc = dr.Referenc;
  doc.Date_Document = Date(31,12,2999);
  ff = GetLE(doc);
  /* пропускаем служебные записи */
  while (ff AND (doc.Referenc == dr.Referenc) AND (not IsServDoc(doc)))
     ff = Prev(doc);
  end; /*elihw*/
  if (doc.Referenc != dr.Referenc)
    ff = False;
  end; /* fi */
  if( not ff)
    [Ошибка : не найден ни один документ по счету];
    PrintDrInfo();
    PrintSeparator();
    return False;
  end; /*fi*/
  /* Проверки, специфичные для последнего документа */
  if (((doc.TypeComplexOper == Закрытие_Наличными ) OR
       (doc.TypeComplexOper == Закрытие_Безналичными)) AND
      (dr.Open_Close == ""))
    [Ошибка : счет не закрыт, а последняя документ из состава операции закрытия];
    WasError = True;
  end;/* fi */
  if ((doc.TypeComplexOper != Закрытие_Наличными )   AND
      (doc.TypeComplexOper != Закрытие_Безналичными) AND
      (dr.Open_Close == "З"))
    [Ошибка : счет закрыт, а последняя документ не из состава операции закрытия];
    WasError = True;
  end;/* fi */
  if(dr.Sum_Rest != doc.Rest )
    [Ошибка : Остаток по счету ########### не совпадает с остатком по послед.док-ту ###########]
    (dr.Sum_Rest , doc.Rest );
    dr.Sum_Rest = doc.Rest;
    update(dr);
    WasError = True;
  end; /*fi*/
  if(dr.Final_Date  != doc.Date_Document ) /* Дата в счете берется из Date_Document*/
    [Ошибка : Дата последней операции по счету ########## не совпадает с документом ##########]
    (dr.Final_Date, doc.Date_Document);
    WasError = True;
  end; /*fi*/
  if (WasError)
    PrintDrInfo();
    PrintDocInfo();
    PrintSeparator();
    RetVal = false;
  end; /*fi*/
  return True;
end; /*orcam*/

macro GetPrevDoc()
  Var ff;
  ff = Prev(doc);
  /* пропускаем служебные записи */
  while (ff AND (doc.Referenc == dr.Referenc) AND (not IsServDoc(doc)))
     ff = Prev(doc);
  end; /*elihw*/
  if (ff AND (doc.Referenc != dr.Referenc))
    ff = False;
  end; /* fi */
  return ff;
end; /*orcam*/

macro CheckClient()
Var f;
  if (doc.Author == ACCOUTN_OWNER)
    if (dr.CodClient != doc.CodClient)
      [Ошибка : Клиент, указанный в счете (########) не совпадает с документом (########)]
       (dr.CodClient, doc.CodClient);
      return False;
    else
      return True;
    end; /*fi*/
  else
    /*ищем клиента в доверенных лицах и т.п.*/
    KeyNum(trast,0);
    Rewind(trast);
    trast.Referenc  = dr.Referenc;
    trast.VidDoc    = "";
    trast.CodClient = doc.CodClient;
    f = GetGE(trast);
    While (f and (trast.Referenc  == dr.Referenc))
      if((trast.Action != D_DELETE) AND
        (trast.CodClient == doc.CodClient))
        return True;
      end; /* fi */
      f = Next(trast);
    end; /*elihw*/
    [Ошибка : Указанный в документе клиент не найден в доверенностях(завещаниях)];
    return False;
  end; /* fi */
end; /*arcam*/

macro CheckDoc()
Var S,S1,S2, IsError = True;
  Var WasError = False;
  if((doc.Account != dr.Account) AND
     (doc.Account != dr.PrevAccount))
    /* Проверяем случай, когда не совпадают только 2 последних символа в
       номере счета(идентификационные символы)*/
    if( StrLen(doc.Account) >= 2)
      S  = SubStr(doc.Account,1,StrLen(doc.Account)-2);
      S1 = SubStr(dr.Account,1,StrLen(doc.Account)-2);
      S2 = SubStr(dr.PrevAccount,1,StrLen(doc.Account)-2);
      if( (S == S1) OR (S == S2))
        IsError = False;
      end; /*fi*/
    end; /*fi*/
    if (IsError)
      [Предупреждение: Номер счета в документе не совпадает ни с текущим, ни с предыдущим из счета];
      WasError = True;
    end; /*fi*/
  end; /* fi */
  if (not CheckClient())
    WasError = True;
  end; /* fi */
  if (WasError)
    PrintDrInfo();
    PrintDocInfo();
    PrintSeparator();
    retVal = false;
  end; /*fi*/
end; /*orcam*/

macro CheckHistory()
  Var f;
  f = GetLastDoc();
  while(f)
    CheckDoc();
    f = GetPrevDoc();
  end; /*elihw*/
end; /*orcam*/

/*- Точка входа в макрос -------------------------------------------------*/
macro RunCheck(mode, addr)
[ Тест на соответствие файлов счетов и файлов документов ];
/* Шаг 1. Проверяем для всех ли записей в SBDEPDOC есть записи в DEPOSITR
   Алгоритм проверки - для каждого оригинального doc.Referenc ищем запись с
   тем же dr.Referenc */
   PrepareFindSameDepositr();
   found = GetFirstRef();
   while(found)
     if (not FindSameDepositr(doc.Referenc))
       [Ошибка: есть документ в sbdepdoc, нет соответствующей записи в depositr];
       PrintDocInfo;
       PrintSeparator;
       retVal = false;
     end; /* if */
     found = GetNextRef();
   end; /*elihw*/

/* Шаг 2. Проверяем историю по всем счетам в DEPOSITR
   По всем документам данного счета
   ∙ Проверить клиента
   По последнему документу
   ∙ Проверить соответствие состояния счета
   ∙ Проверить остаток по счету, дату совершения операции */
   found = GetFirstDepositr();
   while(found)
     CheckHistory();
     found = GetNextDepositr();
   end; /*elihw*/
   return retVal;
end;/*orcam*/
