
/* Переоценка слитков драгметаллов */

import deprintr, ingot;

file CIRec( "ci_rec.dbt" ) write;
file CIMisc( "ci_misc.dbt" ) key 0;
file DeskAux( "desk_aux.dbt" ) key 0 write;
record IngotAux( "desk_aux.1" );
record IngotRec( "ci_rec.2" );

const
  O_INGOT_REVALUE_POS = 6,
  O_INGOT_REVALUE_NEG = 7;

const
  SB_CAPISSUE_REC =  350;		   /* Ценные бумаги */

const
  TYPERES_MINCAPISSUE = 500;

const
  CI_INGOT = 5;

var
  {curdate},
  {oper},
  Branch = NumFNCash,
  DebetSum = $0,
  Rate,  
  CreditSum = $0;

macro MakeCIRec( Operation, Sum )

  SetRecordAddr( IngotRec, CIRec, 0, FldOffset( CIRec, "Info" ), true );
  ClearRecord( CIRec );
  CIRec.Type = CIMisc.Type;
  CIRec.Issue = CIMisc.IssueID;
  CIRec.Branch = Branch;
  CIRec.Operation = Operation;
  CIRec.Sum = Sum;
  CIRec.Oper = {oper};
  CIRec.Date = {curdate};
  CIRec.ApplicationKind = SB_CAPISSUE_REC;
  CIRec.ApplicationKey = FormApplicationKey( SB_CAPISSUE_REC );
  CIRec.Brigade = GetOperBrigade;
  IngotRec.Name = CIMisc.Name;
  return Insert( CIRec );
end;


macro TreatCashVal( ValueType, ValueCode )

  var                      
    Stat = true,
    Diff,
    RecFound, 
    NewBalCost,
    Weight;

  SetRecordAddr( IngotAux, DeskAux, 0, FldOffset( DeskAux, "Info" ), true );
  ClearRecord( DeskAux );
  DeskAux.ValueType = ValueType;  
  DeskAux.ValueCode = ValueCode;  
  RecFound = GetGE( DeskAux );
  while ( RecFound and Stat 
    and ( DeskAux.ValueType == ValueType)  
    and ( DeskAux.ValueCode == ValueCode) )
    if ( CIMisc.UseWeightType == IWT_PURE )
      Weight = IngotAux.ChemicalWeight;
    else
      Weight = IngotAux.RealWeight;
    end;             
    NewBalCost = Money( Weight * Rate / 10000 )/100;
    Diff = NewBalCost - IngotAux.BalCost;
    if ( Diff > 0 ) 
      CreditSum = CreditSum + Diff;
    elif ( Diff < 0 )
      DebetSum = DebetSum - Diff;
    end;    
    if ( Diff != 0 ) 
      IngotAux.BalCost = NewBalCost;
      Stat = Update( DeskAux );
    end;
    RecFound = Next( DeskAux );
  end;          
  return Stat;
end;


macro TreatIngots

  var
    Stat = true,
    RecFound;

  ClearRecord( CIMisc );
  CIMisc.Type = CI_INGOT;
  RecFound = GetGE( CIMisc );
  while ( RecFound and Stat and ( CIMisc.Type == CI_INGOT ) )
    Message( "Переоценка: ", CIMisc.Name );
    Rate = ing_getRubRate( CIMisc.IssueID, {curdate} );
    if ( Rate != $0 )
      CreditSum = $0;
      DebetSum = $0;
      Stat = TreatCashVal( TYPERES_MINCAPISSUE + CI_INGOT, CIMisc.IssueID );
      if ( Stat ) 
        if ( CreditSum > 0 )
          Stat = MakeCIRec( O_INGOT_REVALUE_POS, CreditSum );
        elif ( DebetSum > 0 )
          Stat = MakeCIRec( O_INGOT_REVALUE_NEG, DebetSum );
        end;
      end;
    end; 
    RecFound = Next( CIMisc );
  end;
  if ( not Stat )
    MsgBox( "Ошибка при переоценке" );
  end;
end;