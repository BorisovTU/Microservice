import RslUnit, dep_utils, wf_params, wf_localExecutor, wf_mockUi;

class (TTest) TWfDepOperationsTest
  private var ui = TMockWebUI;

  macro hnd_additionalDeposit_paramsForm(fields : TParameters)
    fields.set("amount", $12.09);
    fields.set("tokenNumber", "75");
    return fields;
  end;

  macro hnd_withdraw_paramsForm(fields : TParameters)
    fields.set("amount", $9.75);
    fields.set("tokenNumber", "75");
    return fields;
  end;

  macro hnd_internalTransfer_paramsForm(fields : TParameters)
    fields.set("receiverAccountId", 10000001);
    fields.set("amount", $12.75);
    return fields;
  end;

  macro hnd_commission_paramsForm(fields : TParameters)
    println("commAccountId = ", fields.get("commAccountId"));
    println("commAmount = ", fields.get("commAmount"));
    println("commVatAmount = ", fields.get("commVatAmount"));
    return fields;
  end;

  macro hnd_partialConversion_paramsForm(fields : TParameters)
    const amountToConvert = $0.75;
    const rate = 30.0;

    fields.set("amountToConvert", amountToConvert);
    fields.set("convertedAmount", amountToConvert * rate);
    fields.set("conversionRate", rate);
    return fields;
  end;

  macro hnd_issueReports(fields : TParameters)
    println("reportText = \n", strSubst(fields.get("reportText"), "{nl}", "\n"));
    return fields;
  end;

  macro execOutputHandler(msg : String, pd : TProcessData)
    println("---");
    println(msg);
    printProps(pd);
    println("---");
  end;

  macro test_additionalDeposit
    TLocalExecutor(ui, r2m(this, "execOutputHandler")).executeProcess("AdditionalDeposit", 10000002, 14, "V");
  end;

  macro test_withdraw
    TLocalExecutor(ui, r2m(this, "execOutputHandler")).executeProcess("Withdraw", 10000003, 14, "V");
  end;

  macro test_internalTransfer
    TLocalExecutor(ui, r2m(this, "execOutputHandler")).executeProcess("InternalTransfer", 10000002, 14, "V");
  end;

  InitTTest;
  ui.forms.add("DepositApi.additionalDeposit.paramsForm", r2m(this, "hnd_additionalDeposit_paramsForm"));
  ui.forms.add("DepositApi.withdraw.paramsForm", r2m(this, "hnd_withdraw_paramsForm"));
  ui.forms.add("DepositApi.internalTransfer.paramsForm", r2m(this, "hnd_internalTransfer_paramsForm"));
  ui.forms.add("DepositApi.commission.paramsForm", r2m(this, "hnd_commission_paramsForm"));
  ui.forms.add("DepositApi.partialConversion.paramsForm", r2m(this, "hnd_partialConversion_paramsForm"));
  ui.forms.add("OperationApi.issueReports", r2m(this, "hnd_issueReports"));
end;

TWfDepOperationsTest.run;