/*
$Name:             r_card.mac 
$Module:           RS-Retail
$Description:      Выписка по счету.
*/

import deprintr, opercode, issrvdoc, getdocs;

file dr       (depositr) key 8;
file depdoc   (sbdepdoc) key 6;
file currency (currency) key 0;
file namealg  (namealg,"bank.def")  key 0;
file suboper  (suboper ) key 1;

var clntL = TClientList;

var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

var
  LookForUnloaded;

/***********************************************************/
macro GetCurrSortName( Code )

  var shortName = String(Code);

  currency.Code_Currency = Code;
  if(GetEQ(currency))
    shortName = currency.Short_Name;
  end;
  return shortName;
end;

/***********************************************************/
macro FindClient( void )
  if ( NOT clntL.GetRecord( dr.CodClient ) )
    clntL.CurRec.rec.Name1 = "Клиент не найден!";
  end;
end;

/***********************************************************/
macro Filter( doc )

  var mustBePrinted = true;

  if(not LookForUnloaded)
    if(    (doc.FlagStorn != StrFor(0))
        or ((doc.TypeOper == Перевод_В_Другой_Вид_Вклада) and doc.ApplType)    )
      mustBePrinted = false;
    end;
  end;
  return mustBePrinted;
end;

/***********************************************************/
macro Начало_Конец_отчета

[-------####################------------------ ########## ------ ######## ---------
] (GetRetailVersion(),Date(),Time());
end;

/***********************************************************/
macro Заголовок_отчета

  var fullName = "";

  if(  (not SpecialAccess) and OperMayListSpecialAccounts and dr.SpecialAccess)
    fullName = "XXXXXX X.X.";
  else
    FindClient();
    fullName = clntL.CurRec.rec.Name1 + " " +
               clntL.CurRec.rec.Name2 + " " +
               clntL.CurRec.rec.Name3;
  end;

  [
           Счет ######################## ###   Вид вклада ############

    Владелец счета: #################################################################################
    Дата рождения:  ##########
    Адрес:          ####################################################################################################
    Документ:       ####################################################################################################
   ------------------------------------------------------------------------------------------------------------------------------
                        :                                            Вклад
           Дата         :--------------------------------------------------------------------------------------------------------
                        :    Приход    :    Расход    :   Остаток     : Начис.% : Остаток % :            Основание
   ---------------------:--------------:--------------:---------------:---------:-----------:------------------------------------
  ](Acc_Form(dr.Account), GetCurrSortName(dr.Code_Currency), dr.Type_Account,
    fullName, String(clntL.CurRec.rec.Born), clntL.CurRec.rec.Address,
    clntL.CurRec.MakeDocStr()
   );
end;

/***********************************************************/
macro BottomLine

[------------------------------------------------------------------------------------------------------------------------------
];
end;

/***********************************************************/
macro Тело_Отчета

  var found, dateStr, NullDate = date(0, 0, 0);

/* Отключено до лучших времен
  if(LookForUnloaded and (dr.Limit_Date != NullDate) and dr.ArhFlag)
    GetDocsAndReopenTemp(dr.Referenc, dr.Open_Date, depdoc);
  end;
*/
  ClearRecord(depdoc);
  depdoc.Referenc = dr.Referenc;
  found = GetGE(depdoc);
  while(found and (depdoc.Referenc == dr.Referenc))
    if(IsServDoc(depdoc) and Filter(depdoc))
      dateStr = String(depdoc.Date_Document);
      if(depdoc.Date_Document != depdoc.DepDate_Document)
        dateStr = dateStr + "/" + String(depdoc.DepDate_Document);
      end;
[#####################: ############ : ############ : ############# : ####### : ######### :################################### ]
      (dateStr, depdoc.InSum, depdoc.OutSum, depdoc.Rest,
       depdoc.PercOprSum, depdoc.PercRest, depdoc.Ground:w);
    end;
    found = Next(depdoc);
  end;
end;

/***********************************************************/
macro PrintCard( Referenc, unloaded )

  LookForUnloaded = unloaded;
  dr.Referenc = Referenc;
  if(GetEQ(dr))
    Начало_Конец_отчета();
    [ ];
    Заголовок_отчета();
    Тело_Отчета();
    BottomLine();
    [ ];
    Начало_Конец_отчета();
  end;
end;