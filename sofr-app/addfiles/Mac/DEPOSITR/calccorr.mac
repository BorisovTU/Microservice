/*
   Сложная капитализация по альтернативным условиям для Целевых
  вкладов на детей
   Вычисляет сумму не причисленных процентов на заданую дату
*/
import DeprIntr,RSD;

record PC_ACC_REC ("pc_alg.dbt");
record DEP_ACC_REC("depositr.dbt");

const
  PC_ACCOUNT_DEP  = 2001,
  PC_ACCOUNT_ALT  = 2002;

var
  SumCorRest,
  NextCalc = 0;

/**********************************************************/

macro CalcDelta (CalcDate, ObjectType)

  var
    Delta  = $0l,
    SumPay = $0l;

  Проценты_Налог_К_Оплате_Учет_Конв (
       DEP_ACC_REC.Referenc,
       ObjectType,
       CalcDate,
       Delta, SumPay );

  return Delta+SumPay;
end;
/* *********************************************************
 Т.к. настройку "Причислять проценты при завершении договора"
 могут включить в любое время, то в первый квартал не подойдет
 ни старый, ни новый макрос - часть счетов надо будет 
 обрабатывать по старому, а часть - по новому.
 Как обрабатывать конкретный счет - решит функция 
 isNewDirection
*/
macro isNewDirection
//
// Возвращает true, если Детский счет (конкретный)
// обрабатывался по-новому, т.е. проценты были причислены
// во время выполнения 82 операции.
// Функция ищет операцию 72/82 (причисление процентов 
// при обработке договора). Если операция найдена, ее
// DepDate_Document равен Prol_DateDep-1 и ее сумма не 0,
// то счет обработан по-новому.
// 
// Проверка имеет смысл только в том квартале, когда сменили 
// условия причисления процентов при обработке договоров.
// В следующем квартале допустимо возвращать true и не париться.
// Но в макросе момент смены параметра я не определю - нет историчности.
// 
  var ret = true;
  var InSum;
  var cmd,rs;
  var strcmd = "SELECT t_InSum FROM dsbdepdoc_dbt " +
               " WHERE t_Referenc = ? "             +
               "   AND t_TypeOper = 72 AND t_TypeComplexOper = 82 ";

  if ( DEP_ACC_REC.Prol_DateDep != Date( 0, 0, 0 ) )
    strcmd = strcmd + " AND t_DepDate_Document = ? ";
    cmd = RsdCommand( strcmd );
    cmd.addParam("t_Referenc", RsdBp_in);
    cmd.value   ("t_Referenc") = DEP_ACC_REC.Referenc;
    cmd.addParam("t_DepDate_Document", RsdBp_in);
    cmd.value   ("t_DepDate_Document") = DEP_ACC_REC.Prol_DateDep - 1;
  else
    cmd = RsdCommand( strcmd );
    cmd.addParam("t_Referenc", RsdBp_in);
    cmd.value   ("t_Referenc") = DEP_ACC_REC.Referenc;
  end;
  cmd.execute;
  rs = RsdRecordset( cmd );
  if ( rs.moveNext )
    InSum = rs.value( "t_InSum" );
    if ( InSum != 0 )
      ret = true;
    else
      ret = false;
    end;
  else
    ret = false;
  end;
  return ret;
end;
// *********************************************************

macro CalcCorSum_Old( CalcDate, Rest, PrevDelta )

  var DepEndYear,
      DepEndMon,
      DepEndDate;
  var DateBeginQuarter,
      DateEndQuarter;

  if ( DEP_ACC_REC.End_DateDep == Date( 0, 0, 0 ) )
    if (DEP_ACC_REC.Prol_DateDep != Date( 0, 0, 0 ))
      DepEndDate = DEP_ACC_REC.Prol_DateDep - 1;
      DateSplit (DepEndDate, null, DepEndMon, DepEndYear);
      if (CalcDate < Date(1,1,2000)) /* Ежегодный расчет процентов */
        DateBeginQuarter = Date( 1, 1, DepEndYear);
        DateEndQuarter   = Date(31,12, DepEndYear);
      else /* Ежеквартальный расчет процентов */
        if (DepEndMon <= 3)   /* 1 квартал */
          DateBeginQuarter = Date( 1, 1, DepEndYear);
          DateEndQuarter   = Date(31, 3, DepEndYear);
        elif (DepEndMon <= 6) /* 2 квартал */
          DateBeginQuarter = Date( 1, 4, DepEndYear);
          DateEndQuarter   = Date(30, 6, DepEndYear);
        elif (DepEndMon <= 9) /* 3 квартал */
          DateBeginQuarter = Date( 1, 7, DepEndYear);
          DateEndQuarter   = Date(30, 9, DepEndYear);
        else                  /* 4 квартал */
          DateBeginQuarter = Date( 1,10, DepEndYear);
          DateEndQuarter   = Date(31,12, DepEndYear);
        end;
      end;
      if ( ( CalcDate > DepEndDate )
        and ( CalcDate >= Date( 1, 1, 1998 ) )
        and ( CalcDate >= DateBeginQuarter )
        and ( CalcDate <= DateEndQuarter ) )
        SumCorRest = CalcDelta(DateBeginQuarter-1, PC_ACCOUNT_DEP);
        NextCalc = 0;
      else
        SumCorRest = $0;
        if ( CalcDate >= Date( 31, 12, DepEndYear ) )
          SumCorRest = CalcDelta( CalcDate, PC_ACCOUNT_ALT ); /* РАВ 22.12.99
          на случай ошибки в расчете в прошлом году и пересчета без причисления
          Например, договор завершен 17.03.98, но 31.12.98 были во-время
          причислены не все проценты- корректирующая сумма причислилась
          только 31.12.99. В этом случае нужна капитализация */
          NextCalc = 0;
        else
          NextCalc = 1;
        end;
      end;
    else
      SumCorRest = $0; /* Чтобы убрать ошибку- дата конца договора выключена, дата пролонгации еще не взведена */
    end;
  else
    if ( DEP_ACC_REC.UseAlternate )
      SumCorRest = CalcDelta( CalcDate, PC_ACCOUNT_ALT );
    else
      SumCorRest = $0;
    end;
    NextCalc = 0;
  end;
end;
// *********************************************************

macro CalcCorSum_New( CalcDate, Rest, PrevDelta )

  if ( DEP_ACC_REC.End_DateDep == Date( 0, 0, 0 ) )   // Договор завершен 
    SumCorRest = $0; // По альтернативу не капитализируем
  else // Договор не завершен - закрытие с нарушением
    if ( DEP_ACC_REC.UseAlternate )
      SumCorRest = CalcDelta( CalcDate, PC_ACCOUNT_ALT );
    else
      SumCorRest = $0;
    end;
  end;
end;
                                                    
/***********************************************************
 Головной макрос (вызывается из программы для капитализации)
***********************************************************/

macro CalcCorSum( CalcDate, Rest, PrevDelta )

  if ( isNewDirection() )
    CalcCorSum_New( CalcDate, Rest, PrevDelta );
  else
    CalcCorSum_Old( CalcDate, Rest, PrevDelta );
  end;

end;
