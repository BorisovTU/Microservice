/*
  RS-Bank 5.1
  RS-Retail

  Операция № 99 - "Выдача наследства"
  Шаг № 593 - "Проверки перед операцией"
  Виды вкладов:
   - "Ср.пен.1г1м"  - Срочный пенсионный на 1 год 1 месяц
   - "Ср.пен.на 2г" - Срочный пенсионный на 2 года

  Дополнительное действие при первой выдаче процентов - 
 вставка в график расчета процентов (pc__calc) периода
 с границей, равной дате выдачи процентов. Это необходимо для
 того, чтобы после выдачи первого наследства не пересчитывались
 проценты до даты выдачи.

  SCR # 12583
  Ромодин Александр Васильевич
  25 ноября 2003 г.
*/
import alg_vars;

private var PcCalc = TDepFile ("pc__calc.dbt","w",0);

/* ##### Точка входа ##### */
  private var st,
              NextEndDate = Date(0,0,0);

  ALG_RESULT = OK_RES;

  if (ALG_DEPOSITOR.UseAlternate == 0) // Только для первой выдачи (выплаты)
    PcCalc.rec.Referenc    = ALG_DEPOSITOR.Referenc;
    PcCalc.rec.ObjectType  = 2002;
    PcCalc.rec.TypeRecord  = 1;
    PcCalc.rec.EndDate     = ALG_SBDEPDOC.DepDate_Document;
    st = PcCalc.GetGE();
//  Сужение периода до даты операции
    if ( st
     AND (PcCalc.rec.Referenc    == ALG_DEPOSITOR.Referenc)
     AND (PcCalc.rec.ObjectType  == 2002)
     AND (PcCalc.rec.TypeRecord  == 1) )
      if (PcCalc.rec.EndDate > ALG_SBDEPDOC.DepDate_Document)
        NextEndDate = PcCalc.rec.EndDate;
        PcCalc.rec.EndDate = ALG_SBDEPDOC.DepDate_Document;
        st = PcCalc.Update();
        if (NOT st)
          MsgBox ("Ошибка 1 при обработке графика расчета процентов");
        end;
      else
        NextEndDate = Date(0,0,0);
      end;
    else
      NextEndDate = ALG_SBDEPDOC.DepDate_Document;
    end;
//  Добавление периода
    if (st AND (NextEndDate != Date(0,0,0)))
      PcCalc.rec.Referenc    = ALG_DEPOSITOR.Referenc;
      PcCalc.rec.ObjectType  = 2002;
      PcCalc.rec.TypeRecord  = 1;
      PcCalc.rec.EndDate     = ALG_SBDEPDOC.DepDate_Document;
      st = PcCalc.GetLE();
      if ( st
       AND (PcCalc.rec.Referenc    == ALG_DEPOSITOR.Referenc)
       AND (PcCalc.rec.ObjectType  == 2002)
       AND (PcCalc.rec.TypeRecord  == 1) )
        PcCalc.rec.BeginDate    = PcCalc.rec.EndDate + 1;
        PcCalc.rec.EndDate      = NextEndDate;
        PcCalc.rec.SumCalc      = 0;
        PcCalc.rec.SumPay       = 0;
        PcCalc.rec.NumSession   = 0;
        PcCalc.rec.TaxSumCalc   = 0;
        PcCalc.rec.TaxSumPay    = 0;
        PcCalc.rec.SumCalcDelta = 0;
        st = PcCalc.Insert();
        if (NOT st)
          MsgBox ("Ошибка 2 при обработке графика расчета процентов");
        end;
      end;
    end;
  end;
end;
/* ----- Конец файла ----- */
