/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Безналичная конвертация с валютного вклада                              *
*                                                                          *
*  Шилов А.                                                                *
*  10.11.98                                                                *
\**************************************************************************/

import deprintr, OperCode, cur_rate, alg_vars;

file Curr       (currency) key 0;
file Clnt       (depclnt ) key 0;
file Trst       (sbtrast ) key 0;
file Tarif      (sb_tarif) key 0;
file tarif_plus (sb_tarpl) key 0;
file Dep        (depositr) key 8 write;
file Dox        (sbdepdoc) key 3;
file Lynx       (sb_casln) key 1;
file PSDEPDOC   (psdepdoc) write;

const
  SB_DEPOSIT   =   1,
  SB_PSDEPOS   =   5,  /* Отложенный документ Вкладов */
  SB_CASHOPERT = 200;

const
  OP_BUY_CURR      = 101,
  CV_SELL_BUY_CURR =   3,
  CO_Conversion    =  31;
/* Ставки налога (номера тарифов) */
const
  ResTax    = 3,
  NonresTax = 4;
const
  ResTax2001    = 6, /* Для резидента   */
  NonresTax2001 = 7; /* Для нерезидента */


const
  DateNull = Date(0,0,0);

const
  ACCOUTN_PROXY    = 1, /* Доверенное лицо */
  ACCOUNT_IMPORTER = 4; /* Вноситель       */

var Branch       = NumFNCash();
var Sum;
var SumForGet    = $0L;
var SumForTax    = $0L;
var SumForPut    = $0L;
var SumForOut    = $0L;
var DestBranch   = "";
var DestAccRef   = "";
var DestAccType  = "";
var DestAcc;
var DestCurrCode;
var ConvertedSum;
var PureSum;
var NewAcc       = false;
var GiveMoney    = false;
var TrnStat      = true;
var DepDate      = {curdate};
var CBRate       = 0.0;
var BuyRate      = 0.0;
var SellRate     = 0.0;
var DestCBRate   = 0.0;
var DestBuyRate  = 0.0;
var DestSellRate = 0.0;
var FindPayDocFC = False;
var NewAccount   = "";
var CanWorkWithCurr;

/**************************************************************************\
|               Проверка даты рождения для установки возраста              |
\**************************************************************************/
macro CheckBornDate

 var bDay, bMon, bYear;  /* День, месяц, год рождения клиента */
 var cDay, cMon, cYear;  /* День, месяц, год текущей даты */
 var ClientYears = 20;   /* Возраст клиента */

 if ( Clnt.BornDate != DateNull )

   DateSplit( Clnt.BornDate, bDay, bMon, bYear );
   DateSplit( {curdate},     cDay, cMon, cYear );

   ClientYears = cYear - bYear;
   if ( ClientYears > 0 )
     if ( bMon > cMon )
       ClientYears = ClientYears - 1;
     else
       if ( ( bMon == cMon )  AND  ( bDay > cDay ) )
         ClientYears = ClientYears - 1;
       end;
     end;
   else
     MsgBox( "Дата рождения клиента задана неправильно" );
     ALG_RESULT = ERR_RES;
     return;
   end;

 end;

 return ClientYears;

end;

/**************************************************************************\
|                              Список валют                                |
\**************************************************************************/
macro List_Currency (CurCode)

  array Code;
  array MenuElem;
  var   i    = 0;
  var   stat = FALSE;

  SetParm(0,0);

  ReWind(Curr);
  while(Next(Curr))
    if (Curr.Code_Currency != ALG_DEPOSITOR.Code_Currency)
      Code(i)     = Curr.Code_Currency;
      MenuElem(i) = " " + SubStr(Curr.ExternalCode + MkStr(" ",3),1,3) + " " +
                          SubStr(Curr.Short_Name   + MkStr(" ",3),1,3) + " " +
                          Curr.Name_Currency                           + " "  ;
      i = i + 1;
    end;
  end;

  if (ASize(Code) > 0)
    i = Menu(MenuElem,NULL,"Выберите валюту выдачи");
    if (i >= 0)
      stat = TRUE;
      SetParm(0,Code(i));
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindCurr (CurrCode)

  Curr.Code_Currency = CurrCode;

  return GetEQ(Curr);

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindTarif( Num, _Date, _Sum )

  var stat;

  Tarif.IsCur    = 0;
  Tarif.NumTarif = Num;

  stat = GetEQ(tarif);

  if ( stat  AND  ( tarif.TarifPlus ) )
    tarif_plus.IsCur    = 0;
    tarif_plus.NumTarif = Num;
    tarif_plus.Date     = _Date;
    tarif_plus.SumOper  = _Sum;
    if ( GetGE( tarif_plus )            AND
         ( tarif_plus.IsCur    == 0 )   AND
         ( tarif_plus.NumTarif == Num ) AND
         ( tarif_plus.Date     == _Date ) )
      tarif.Percent = tarif_plus.Percent;
      tarif.MinSum  = tarif_plus.MinSum;
      tarif.MaxSum  = tarif_plus.MaxSum;
      tarif.Summa   = tarif_plus.Summa;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro CalcTaxPercent( _Sum, TaxPercent )

  var TarifNum;

  if (SellRate > CBRate)
    if (ALG_SBDEPDOC.DepDate_Document < Date(1,1,2001))
      if (ALG_COMDEPCLNT.rec.NotResident != StrFor(0))
        TarifNum = NonresTax;
      else
        TarifNum = ResTax;
      end;
    else
      if (ALG_COMDEPCLNT.rec.NotResident != StrFor(0))
        TarifNum = NonresTax2001;
      else
        TarifNum = ResTax2001;
      end;
    end;
    if ( FindTarif( TarifNum, ALG_SBDEPDOC.DepDate_Document, _Sum ) )
      SetParm(0,Tarif.Persent);
    else
      TrnStat = false;
    end;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindAcc (Ref)

  Dep.Referenc = Ref;

  return GetEQ(Dep);

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindAccByNum (IsCur, DepType, CurrCode, Account)

  var SaveKey = KeyNum(Dep,7);
  var Stat;

  Dep.FNCash  = Branch;
  Dep.Account = Account;
  Stat = GetEQ(Dep);
  KeyNum(Dep,SaveKey);

  return Stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro NewDepDoc (Doc)

  ClearRecord(Doc);
  Doc.Referenc         = Dep.Referenc;
  Doc.IsCur            = Dep.IsCur;
  Doc.FNCash           = Dep.FNCash;
  Doc.Account          = Dep.Account;
  Doc.Oper             = {oper};
  Doc.Type_Account     = Dep.Type_Account;
  Doc.Code_Currency    = Dep.Code_Currency;
  Doc.CodClient        = Dep.CodClient;
  Doc.YesSbook         = "X";
  Doc.Date_Document    = {curdate};
  Doc.DepDate_Document = {curdate};
  Doc.iApplicationKind = SB_DEPOSIT;
  Doc.ApplicationKey   = FormApplicationKey( SB_DEPOSIT );
  Doc.Ground = "Безналичная покупка валюты";

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetSum (Ref, Summa, ApplType)

  record TmpDoc ("sbdepdoc.dbt");

  TrnStat = FindAcc(Ref);
  if (TrnStat)
    NewDepDoc(TmpDoc);
    TmpDoc.TypeComplexOper  = OP_BUY_CURR;
    TmpDoc.TypeOper         = Списание_простое;
    TmpDoc.ApplType         = ApplType;
    TmpDoc.OutSum           = Summa;
    TmpDoc.DepDate_Document = DepDate;
    TmpDoc.IsControl        = StrFor(1); /* Признак главного документа */
    TrnStat = not Выполнить_операцию(Dep.Account,Dep.Type_Account,Dep.Code_Currency,
                                     Списание_простое,TmpDoc );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro PutSum (Ref, Summa)

  record TmpDoc ("sbdepdoc.dbt");

  if (Ref)
    TrnStat = FindAcc(Ref);
  end;

  if (CanWorkWithCurr)
/*
    Если операционист может работать в другой валюте,
    необходимые проводки выполняются сразу
*/
    if (TrnStat)
      NewDepDoc(TmpDoc);
      TmpDoc.TypeComplexOper  = OP_BUY_CURR;
      TmpDoc.TypeOper         = Зачисление_По_Данным_ПК;
      TmpDoc.InSum            = Summa;
      TmpDoc.DepDate_Document = DepDate;
    end;

    if (Ref)
      TrnStat = not Выполнить_операцию(Dep.Account,Dep.Type_Account,Dep.Code_Currency,
                                       Зачисление_По_Данным_ПК,TmpDoc);
    else
      ClearRecord(ALG_OPERPRM);
      ALG_OPERPRM.Account         = DestAcc;
      ALG_OPERPRM.Type_Account    = DestAccType;
      ALG_OPERPRM.Code_Currency   = DestCurrCode;
      ALG_OPERPRM.Type_Oper       = Открытие_Безналичными;
      ALG_OPERPRM.Show_Panel      = 0;
      ALG_OPERPRM.UseMaxDate      = 1;
      ALG_OPERPRM.NoPrint         = 1;
      ALG_OPERPRM.NonCashCommission = 0;
      ALG_OPERPRM.TypeComplexOper = Открытие_Безналичными;
      if (DestCurrCode == 0)
        TmpDoc.IsCur = 0;
      else
        TmpDoc.IsCur = 1;
      end;
      TmpDoc.Code_Currency = DestCurrCode;
      TmpDoc.DepDate_Document = DepDate;
      TrnStat = not Выполнение_Операции(ALG_OPERPRM, TmpDoc);
      NewAcc  = true;
      if (TrnStat)
        TrnStat = FindAccByNum(NumFlagCur,DestAccType,DestCurrCode,ALG_OPERPRM.Account);
      end;
      if (TrnStat)
        SetParm(0,Dep.Referenc);
        NewAccount = Dep.Account;
      end;
    end;
  else
/*
    Если операционист не может работать в другой валюте,
    заполняется отложенный
*/
    if (TrnStat)
      NewDepDoc(PSDEPDOC);
      PSDEPDOC.InSum            = Summa;
      PSDEPDOC.DepDate_Document = DepDate;
      PSDEPDOC.KindOp           = 5;
      PSDEPDOC.iApplicationKind = SB_PSDEPOS;
      PSDEPDOC.ApplicationKey   = FormApplicationKey(SB_PSDEPOS);
      if (NOT Ref)  /* Открытие нового счета */
        PSDEPDOC.Referenc = "";
        if (DestCurrCode == 0)
          PSDEPDOC.IsCur = 0;
        else
          PSDEPDOC.IsCur = 1;
        end;
        PSDEPDOC.Account          = DestAcc;
        PSDEPDOC.Type_Account     = DestAccType;
        PSDEPDOC.Code_Currency    = DestCurrCode;
        PSDEPDOC.TypeOper         = Открытие_Безналичными;
        PSDEPDOC.TypeComplexOper  = OP_BUY_CURR;
      else
        PSDEPDOC.TypeComplexOper  = OP_BUY_CURR;
        PSDEPDOC.TypeOper         = Зачисление_По_Данным_ПК;
      end;
      TrnStat = Insert (PSDEPDOC);
      if (TrnStat)
        TrnStat = CashLink_AddRec (PSDEPDOC.iApplicationKind,
                                   PSDEPDOC.ApplicationKey);
      end;
    end;
  end;

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro SumOut (Ref, Summa)

  record TmpDoc ("sbdepdoc.dbt");

  var Op;

  if (NewAcc)
    Op = Закрытие_Наличными;
  else
    Op = Расход;
  end;

  NewDepDoc(TmpDoc);
  TmpDoc.OutSum   = Summa;
  TmpDoc.TypeOper = Расход;
  TrnStat = FindAcc(Ref);
  if (TrnStat)
    if (NewAcc)
      TmpDoc.TypeComplexOper = Закрытие_Наличными;
    else
      TmpDoc.TypeComplexOper = Расход;
    end;
    TmpDoc.DepDate_Document = DepDate;
    TmpDoc.IsControl        = StrFor(1); /* Признак главного документа */
    TrnStat = not Выполнить_операцию(Dep.Account,Dep.Type_Account,Dep.Code_Currency,Op,TmpDoc);
  end;

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetInfoFromDox

  file PayDoc (pay_doc) key 2;

  var Found;
  var AppKind;
  var AppKey;
  var stat = false;

  /* В контексте Последконтроля открываем файл явно! */
  if (Not Open(Dox,"sbdepdoc.dbt"))
    MsgBox("Ошибка открытия файла");
    Exit(1);
  end;
  Lynx.ApplicationKind2 = ALG_SBDEPDOC.iApplicationKind;
  Lynx.ApplicationKey2  = ALG_SBDEPDOC.ApplicationKey;
  Found = GetEQ(Lynx);
  if (Found)
    AppKind = Lynx.ApplicationKind1;
    AppKey  = Lynx.ApplicationKey1;
    KeyNum(Lynx,0);
    Found = GetGE(Lynx);
    while( Found                             and
          (Lynx.ApplicationKind1 == AppKind) and
          (Lynx.ApplicationKey1  == AppKey )    )
      if (Lynx.ApplicationKind2 == SB_DEPOSIT)
        Dox.iApplicationKind = Lynx.ApplicationKind2;
        Dox.ApplicationKey   = Lynx.ApplicationKey2;
        if (GetEQ(Dox))
          if (Dox.TypeOper == Списание_простое)
            if (Dox.ApplType == Безналичная_Комиссия)
              SumForTax  = Dox.OutSum;
            elif (Dox.ApplType == Без_Подоперации)
              SumForGet  = Dox.OutSum;
            end;
            DepDate = Dox.DepDate_Document;
          elif (Dox.TypeOper == Зачисление_По_Данным_ПК)
            DestAccRef   = Dox.Referenc;
            DestAccType  = Dox.Type_Account;
            DestCurrCode = Dox.Code_Currency;
            SumForPut    = Dox.InSum;
          elif (Dox.TypeOper == Открытие_Безналичными)
            DestAcc      = Dox.Account;
            DestAccType  = Dox.Type_Account;
            DestCurrCode = Dox.Code_Currency;
            SumForPut    = Dox.InSum;
            NewAcc       = True;
          elif (Dox.TypeComplexOper == Расход)
            SumForOut = Dox.OutSum;
            GiveMoney = True;
          elif( Dox.TypeComplexOper == Закрытие_Наличными )
            SumForOut = Dox.OutSum;
            GiveMoney = True;
            /* Открываем счёт */
            if (FindAcc(Dox.Referenc))
              Dep.Open_Close = StrFor(0);
              Update(Dep);
            end;
          end;
        end;
      elif (Lynx.ApplicationKind2 == SB_CASHOPERT)
        PayDoc.iApplicationKind = Lynx.ApplicationKind2;
        PayDoc.ApplicationKey   = Lynx.ApplicationKey2;
        if (GetEQ(PayDoc))
          FindPayDocFC = True;
        end;
      end;
      Found = Next(Lynx);
    end;
  end;

  if (DestAccType)
    stat = True;
  end;

  return stat;

end;

/**************************************************************************\
|                             Поиск вкладчика                              |
\**************************************************************************/
macro FindClient( code )

  ClearRecord( Clnt );
  Clnt.CodClient = code;

  if ( NOT GetEQ( Clnt ) )
    MsgBox( "Не найден вкладчик с кодом " + String(code) );
    Exit(1);
  end;

end;

/**************************************************************************\
|                             Поиск вкладчика                              |
\**************************************************************************/
macro FindTrast( ref, code )

  ClearRecord( Trst );
  Trst.Referenc  = ref;
  Trst.VidDoc    = StrFor(0);  /* Доверенность */
  Trst.CodClient = code;
  return GetEQ( Trst );

end;

/**************************************************************************\
|                        Проверки для нерезидента                          |
\**************************************************************************/
macro CheckNoResident

  if ( Dep.Referenc == 0 )
    return;
  end;

  FindClient(Dep.CodClient);

  if ( ( Clnt.CitizenShip != StrFor(0) )  AND   /* Нерезидент */
       ( Dep.CodClient != ALG_DEPOSITOR.CodClient )  AND  /* Не владелец счета */
       ( ( NOT FindTrast(Dep.Referenc, ALG_DEPOSITOR.CodClient) ) OR
       ( Trst.Type != StrFor(ACCOUTN_PROXY) ) ) )  /* Не доверенное лицо */
    MsgBox("На счет нерезидента|валюту могут зачислять|только вкладчик и доверен.лицо");
    Exit(1);
  end;

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetSumCurrAndDestAccount

  if ((StrFor(GetProgramID()) == "П") and (GetInfoFromDox()))
    ;
  else
    if (not (List_Currency(DestCurrCode)))
      Exit(1);
    end;

    if (NOT GetAllCurrRate({curdate},ALG_DEPOSITOR.Code_Currency,CBRate,BuyRate,SellRate))
      MsgBox("Ошибка при определении курса валюты");
      Exit(1);
    else
      CBRate   = CBRate   * 100.0;
      BuyRate  = BuyRate  * 100.0;
      SellRate = SellRate * 100.0;
    end;

    if (DestCurrCode == 0)
       DestCBRate = DestBuyRate = DestSellRate = 100.0;
    else
      if (NOT GetAllCurrRate({curdate},DestCurrCode,DestCBRate,DestBuyRate,DestSellRate))
        MsgBox("Ошибка при определении курса валюты");
        Exit(1);
      else
        DestCBRate   = DestCBRate   * 100.0;
        DestBuyRate  = DestBuyRate  * 100.0;
        DestSellRate = DestSellRate * 100.0;
      end;
    end;

    GetMoney(Sum,"Введите сумму снятия со счета");
    if (ValType(Sum) == V_UNDEF)
      Exit(1);
    end;

    MsgBox("Выберите вид вклада и счет,|на которые будет зачислена сконвертированная сумма");
    if (DestCurrCode == 0)
      SetFlagCur(0,1);
    end;
    if (not (SelectDepType(DestAccType)))
      SetFlagCur(1,1);
      Exit(1);
    end;
    SetFlagCur(1,1);

    GetString(DestAcc,"Введите номер счета для зачисления",25);
    if (ValType(DestAcc) == V_UNDEF)
      if (not SelectAccount(DestAccType,DestCurrCode,ALG_COMDEPCLNT.rec.CodClient,DestAccRef))
        Exit(1);
      end;
    end;
    if (DestAccRef)
      if (not FindAcc(DestAccRef))
        MsgBox("Счет не найден");
        Exit(1);
      end;
      DestAcc = Dep.Account;
    elif ((DestBranch == "") and (DestAcc != ""))
      if (not FindAccByNum(1,DestAccType,0,DestAcc))
        MsgBox("Счет не найден");
        Exit(1);
      end;
      DestAccRef = Dep.Referenc;
    end;

    /* Проверки для нерезидента */
    CheckNoResident();

    if (CanWorkWithCurr and (DestCurrCode == 0) and GetTrue(true,"Выдать деньги клиенту ?"))
      GiveMoney = true;
    end;

    if (CanWorkWithCurr OR (DestCurrCode != 0))
      GetDate(DepDate,"Введите дату учета по процентам");
    end;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ConvertSum (SrcSum, DestSum)

  record PayDoc ("pay_doc.cnv");

  ClearRecord(PayDoc);
  PayDoc.FNCash           = Branch;
  PayDoc.IsCur            = 1;
  PayDoc.NumOpert         = CV_SELL_BUY_CURR;
  PayDoc.GroupOpert       = CO_Conversion;
  PayDoc.Date_Document    = {curdate};
  PayDoc.Oper             = {oper};
  PayDoc.ClientLastName   = ALG_COMDEPCLNT.rec.Name1;
  PayDoc.ClientFirstName  = ALG_COMDEPCLNT.rec.Name2;
  PayDoc.ClientSecondName = ALG_COMDEPCLNT.rec.Name3;
  PayDoc.FlagRez          = ALG_COMDEPCLNT.rec.NotResident;
  PayDoc.iApplicationKind = SB_CASHOPERT;
  PayDoc.ApplicationKey   = FormApplicationKey(SB_CASHOPERT);
  PayDoc.Ground           = "Безналичная покупка валюты ";
  PayDoc.KonvertCodCur    = ALG_DEPOSITOR.Code_Currency;
  PayDoc.KonvertSum       = SrcSum;
  PayDoc.VydachCodCur     = DestCurrCode;
  if (DestCurrCode == 0)
    PayDoc.CBRate         = CBRate;
    PayDoc.Rate           = SellRate;
  else
    PayDoc.CBRate         = 100.0 * CBRate / DestCBRate;
    PayDoc.Rate           = 100.0 * CBRate / DestCBRate;
  end;

  TrnStat = PerformConversion(PayDoc);

  SetParm(1,PayDoc.VydachSum);

  if (TrnStat/* and PayDoc.Yield  По просьбе Д.Зембатова - Банк ВДНХ */)
    TrnStat = InsertPayDoc(PayDoc);
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro TrnProc

  var TaxPerc = 0;
  var Tax     = 0;

  TrnStat = FindCurr(ALG_DEPOSITOR.Code_Currency);

  if (TrnStat)
    if ((StrFor(GetProgramID()) == "П") and SumForGet)
      GetSum(ALG_DEPOSITOR.Referenc,SumForGet, Без_Подоперации);
    else
      GetSum(ALG_DEPOSITOR.Referenc,Sum, Без_Подоперации);
    end;
  end;

  if (TrnStat)
    if ((StrFor(GetProgramID()) == "П"))
      if (NOT FindPayDocFC)
        ConvertSum(SumForGet,SumForPut);
      end;
    else
      ConvertSum(Sum,ConvertedSum);
    end;
  end;

  if (DestCurrCode == 0)
    SetFlagCur(0,1);
  end;

  if (TrnStat)
    PureSum = ConvertedSum;
    if ((StrFor(GetProgramID()) == "П") and SumForPut)
      PutSum(DestAccRef,SumForPut);
    else
      PutSum(DestAccRef,ConvertedSum);
    end;
  end;

  if (TrnStat and (DestCurrCode == 0))
    CalcTaxPercent( Sum, TaxPerc );
    if (TrnStat and TaxPerc)
      Tax = Money(Sum * (SellRate - CBRate) * TaxPerc / 1000000);
      if (Tax)
        if ((StrFor( GetProgramID() ) == "П") and SumForTax)
          GetSum(DestAccRef,SumForTax,Безналичная_Комиссия);
        else
          GetSum(DestAccRef,Tax,Безналичная_Комиссия);
        end;
        PureSum = ConvertedSum - Tax;
      end;
    end;
  end;

  if (TrnStat and GiveMoney)
    if ((StrFor( GetProgramID() ) == "П") and SumForOut)
      SumOut(DestAccRef,SumForOut);
    else
      SumOut(DestAccRef,PureSum);
    end;
  end;

  SetFlagCur(1,1);

  if (not TrnStat)
    AbortTrn();
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DoTrn

  ProcessTrn(1,"TrnProc",Dep,Curr,Tarif);

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  ALG_RESULT = SKIP_RES;

  CanWorkWithCurr = GetChFlagCur(); /* Разрешение смены валюты */

  FindClient(ALG_DEPOSITOR.CodClient);

  if ( ( ALG_COMDEPCLNT.rec.CodClient != ALG_DEPOSITOR.CodClient )  AND
         FindTrast(ALG_DEPOSITOR.Referenc, ALG_COMDEPCLNT.rec.CodClient)  AND
       ( Trst.Type == StrFor(ACCOUNT_IMPORTER) )  AND  /* Вноситель */
       ( CheckBornDate() > 17 ) )
    MsgBox("Вноситель не может выполнять эту операцию|для совершеннолетнего вкладчика");
    ALG_RESULT = SKIP_RES;
    return;
  end;

  GetSumCurrAndDestAccount();

  if (DestCurrCode != 0)
    CanWorkWithCurr = 1;   /* Фактически работаем только в валюте */
  end;

  DoTrn();

  if (TrnStat)
    if (GiveMoney)
      if (ValType(Sum) != V_UNDEF)
        MsgBox("Сумма к выдаче:|",PureSum);
      end;
    else
      if (CanWorkWithCurr)
        if ((StrFor( GetProgramID() ) != "П") AND NewAcc)
          MsgBox("Открыт счет:|",NewAccount);
        end;
      else
        MsgBox ("Рублевый документ помещен в отложенные");
      end;
    end;
    ALG_RESULT = OK_RES;
  else
    MsgBox("Ошибка при конвертации");
    ALG_RESULT = ERR_RES;
  end;

  if (FindAcc(ALG_DEPOSITOR.Referenc))
    Copy(ALG_DEPOSITOR,Dep);
    ALG_UPDATE = 1;
  else
    ALG_UPDATE = 0;
  end;

end;
