/*
  ---------------
  R-Style Softlab
  ---------------
  RS-Retail
  ---------------

  Макрос простановки счета кассы ресурса.

  Примечание: символ '0' (ноль) в 9-ой позиции примеров подставляемых номеров счетов - 
              символ ключа, который вычисляется процедурой ключевания.

*/

import DeprIntr;

private file ci_misc ( "ci_misc.dbt" ) key 0;
private file ci_doc_file ( "ci_doc.dbt" );

private record ci_doc ( "ci_doc.dbt" );
private record ci_doc3 ( "ci_doc.3" );

private file cr  ( "currency.dbt" ) key 0;  // Справочник валют.
private file cv  ( "sb_casvl.dbt" ) key 0;  // Справочник видов ресурсов кассы.
private file mi  ( "ci_misc.dbt"  ) key 0;
private file fca ( "sb_casac.dbt" ) key 1;  // Таблица счетов кассы.
private file fda ( "dbc_acc.dbt"  ) key 0;  // Таблица счетов бал.стоимостей.
private file par ( par ) key 0;

private var Group = TBFile( "rtgroup.dbt", "r", 2 );

private record ca ( "sb_casac.dbt" );      // Запись таблицы счетов кассы.
private record da ( "dbc_acc.dbt" );       // Запись таблицы счетов бал. стоимостей.
private record reg_num ( reg_num );

private var userAttrs;
private var varUserAttrs;

private var {curdate};

private const cFNCash  = NumFNCash();           // Код подразделения.
private const cOperBrigade = GetOperBrigade();  // Смена.

private const CI_CERT  = 3;  // Сертификаты.
private const CI_COIN  = 4;  // Монеты.
private const CI_INGOT = 5;  // Драг.металлы.

private const TYPERES_CURRENCY =   1;  // Тип ресурса кассы: деньги (из справочника валют).
private const CM_COIN          = 504;  // Монеты.
private const CM_INGOT         = 505;  // Драг.металлы.
private const TYPERES_PAYDOC   =   4;  // Платежные документы ОП.

private const TYPERES_MINCAPISSUE = 500;  // Минимально возможный код ЦБ.

private const TYPE_RES_PAY    = 0;  // Платежеспособные ресурсы.
private const TYPE_RES_NOPAY  = 1;  // Неплатежеспособные ресурсы.

private const CK_NEW = 0;  // Новые  монеты.
private const CK_OLD = 1;  // Старые монеты.

/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro FindCiMisc ( type, issue )

  ClearRecord( ci_misc );
  ci_misc.Type = type;
  ci_misc.IssueID = issue;

  return GetEQ( ci_misc );

end;

// ======================================================
//       Найти кассовый ресурс для документа ЦБиЦ.
// ======================================================
private macro findCashVal()
  ClearRecord( cv );
  cv.TypeValue   = ci_doc.Type + TYPERES_MINCAPISSUE;
  cv.CodIntValue = ci_doc.Issue;
  cv.Type        = 0;  // Платежеспособные.
  return GetEQ( cv );
end;

// ======================================================
//           Найти запись по сейфу бригады.
// ======================================================
private macro findBrigade( pBranch )
  Group.Rec.Branch = pBranch;
  Group.Rec.Date   = Date( 0, 0, 0 );
  Group.Rec.ID     = cOperBrigade;
  return Group.GetEQ();
end;

// ======================================================
//    Поиски счета подкрепления кассы для новых монет.
// ======================================================
private macro getNewCoinAcc()
  var vFind = FALSE;
  var vSaveKeyNum = KeyNum( cv, 1 );
  var vSafeID;
  if ( findCashVal()  AND  findBrigade( ci_doc.Branch ) )
    vSafeID = Group.Rec.SafeID;
    ClearRecord( fca );
    fca.Branch   = ci_doc.Branch;
    fca.RefValue = cv.RefValue;
    fca.Date     = {curdate};
    fca.CashOper = vSafeID;
    if ( GetGE( fca )  AND
         ( fca.Branch   == ci_doc.Branch )  AND
         ( fca.RefValue == cv.RefValue   )  AND
         ( fca.Date     == {curdate}     )  AND
         ( fca.CashOper == vSafeID       ) )
      vFind = TRUE;
    end;
  end;
  KeyNum( cv, vSaveKeyNum );
  return vFind;
end;

// =======================================================
//    Поиски счета подкрепления кассы для старых монет.
// =======================================================
private macro getOldCoinAcc()
  var vFind = FALSE;
  var vSaveKeyNum = KeyNum( cv, 1 );
  var vSafeID;
  if ( findCashVal()  AND  findBrigade( ci_doc.Branch ) )
    vSafeID = Group.Rec.SafeID;
    ClearRecord( fda );
    fda.Branch   = ci_doc.Branch;
    fda.Date     = {curdate};
    fda.Subj     = vSafeID;
    fda.ValueRef = cv.RefValue;
    if ( GetGE( fda )  AND
         ( fda.Branch   == ci_doc.Branch )  AND
         ( fda.Date     == {curdate}     )  AND
         ( fda.Subj     == vSafeID       )  AND
         ( fda.ValueRef == cv.RefValue   ) )
      vFind = TRUE;
    end;
  end;
  KeyNum( cv, vSaveKeyNum );
  return vFind;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro SetAccounts ( ciDocAddr )

  var stat = 0;

  SetBuff( ci_doc, ciDocAddr );

  /**********************************************************************/
  if ( ci_doc.Type == CI_CERT )
    Copy( ci_doc_file, ci_doc );
    SetRecordAddr( ci_doc3, ci_doc_file, 0, FldOffset( ci_doc_file, "Info" ), true );
    /*MsgBox( ci_doc3.Number, " ", ci_doc3.Series );*/

    userAttrs = getDocUserAttrPool( );
    userAttrs.clear( );
    userAttrs.rec.Branch           = NumFNCash( );
    userAttrs.rec.iApplicationKind = ci_doc.ApplicationKind;
    userAttrs.rec.ApplicationKey   = ci_doc.ApplicationKey;
    userAttrs.rec.AttrID           = "ODB_ACCOUNTS";

    varUserAttrs = userAttrs.overlayRecord( "cvodbacc.rec", 0, false );
    varUserAttrs.clear( );
    varUserAttrs.rec.Account1 = "52205810КТТОО00SSННННН";
    varUserAttrs.rec.Account2 = "90704810КТТОО00ПSSННННН";
    varUserAttrs.rec.Account3 = "52404810КТТОО00SSННННН";
    varUserAttrs.rec.Account4 = "52405810КТТОО01SSННННН";
    varUserAttrs.rec.Account5 = "52501810КТТОО01SSННННН";

    if ( userAttrs.insert( varUserAttrs.recSize ) )
      ;
    else
      stat = 9999;
    end;
  /**********************************************************************/
  elif ( ci_doc.Type == CI_COIN )
    userAttrs = getDocUserAttrPool( );
    userAttrs.clear( );
    userAttrs.rec.Branch           = NumFNCash( );
    userAttrs.rec.iApplicationKind = ci_doc.ApplicationKind;
    userAttrs.rec.ApplicationKey   = ci_doc.ApplicationKey;
    userAttrs.rec.AttrID           = "ODB_ACCOUNTS";

    varUserAttrs = userAttrs.overlayRecord( "cvodbacc.rec", 0, false );
    varUserAttrs.clear( );

    if ( ci_doc.SubOp == 1 ) /* Документ по "новой" монете */
      if ( getNewCoinAcc() )
        varUserAttrs.rec.Account1 = fca.ODBDeskAcc;
      else
        varUserAttrs.rec.Account1 = "47415810КТТОО00MMЦММЦЦ";
      end;
      varUserAttrs.rec.Account2 = "20202ВВВКТТОО003MMММ";

      if ( userAttrs.insert( varUserAttrs.recSize ) )
        ;
      else
        stat = 9999;
      end;
    elif ( ci_doc.SubOp == 2 ) /* Документ по "старой" монете */
      if ( getOldCoinAcc() )
        varUserAttrs.rec.Account1 = fda.Account;
      else
        varUserAttrs.rec.Account1 = "20308810001";
      end;
      varUserAttrs.rec.Account2 = varUserAttrs.rec.Account1;

      if ( userAttrs.insert( varUserAttrs.recSize ) )
        ;
      else
        stat = 9999;
      end;
    end;
  /**********************************************************************/
  elif ( ci_doc.Type == CI_INGOT )
    if ( FindCiMisc( ci_doc.Type, ci_doc.Issue ) )
      userAttrs = getDocUserAttrPool( );
      userAttrs.clear( );
      userAttrs.rec.Branch           = NumFNCash( );
      userAttrs.rec.iApplicationKind = ci_doc.ApplicationKind;
      userAttrs.rec.ApplicationKey   = ci_doc.ApplicationKey;
      userAttrs.rec.AttrID           = "ODB_ACCOUNTS";

      varUserAttrs = userAttrs.overlayRecord( "cvodbacc.rec", 0, false );
      varUserAttrs.clear( );

      if ( ci_misc.MaterialRef == 8 ) /* Документ для золота */
        varUserAttrs.rec.Account1 = "20302А98КТТОО0100ППП";
        varUserAttrs.rec.Account2 = "20302А98КТТОО02ППППП";
        varUserAttrs.rec.Account3 = "20302810КТТОО0100ППП";
        varUserAttrs.rec.Account4 = "20302810КТТОО02ППППП";
      else
        varUserAttrs.rec.Account1 = "20303ВВВКТТОО0100ППП";
        varUserAttrs.rec.Account2 = "20303ВВВКТТОО02ППППП";
        varUserAttrs.rec.Account3 = "20303810КТТОО0100ППП";
        varUserAttrs.rec.Account4 = "20303810КТТОО02ППППП";
      end;

      if ( userAttrs.insert( varUserAttrs.recSize ) )
        ;
      else
        stat = 9999;
      end;
    end;
  end;

  return stat;

end;

//==========================
//   Найти ресурс кассы.
//==========================
private macro findCasValue( pRefValue )
  ReWind( cv );
  ClearRecord( cv );
  cv.RefValue = pRefValue;
  return getEQ( cv );
end;

//=============================
//   Найти запись по валюте.
//=============================
private macro getCurr( pCode )
 ReWind( cr );
 ClearRecord( cr );
 cr.Code_Currency = pCode;
 return getEQ( cr );
end;

//================================
//   Вид монет: новые \ старые.
//================================
private macro CoinType( pIssueID )
  var retType = -1;
  mi.Type    = CI_COIN;
  mi.IssueID = pIssueID;
  if ( GetEQ( mi ) )
    retType = mi.KindOfCoin;
  end;
  return retType;
end;

//====================================================================
//   Пример процедуры установки счета кассы в таблице счетов кассы.
//====================================================================
macro SetCashAccount( addr )
  var vCurrCode = "ВВВ";

  SetBuff( ca, addr );

  if ( NOT findCasValue( ca.RefValue ) )  // Не нашли ресурс кассы.
    return 0;
  end;

  if ( cv.TypeValue == TYPERES_CURRENCY )  // Валюты.
    if ( getCurr( cv.CodIntValue ) )
      vCurrCode = String( cr.ExternalCode:3 );
    else
      vCurrCode = "ВВВ";
    end;
    if ( cv.Type == TYPE_RES_PAY )         // Платежеспособные.
      ca.ODBDeskAcc = "20202" + vCurrCode + "001";
    else
      ca.ODBDeskAcc = "20202" + vCurrCode + "002";
    end;
  elif ( cv.TypeValue == CM_COIN )         // Монеты.
    if ( CoinType( cv.CodIntValue ) == CK_NEW )
      ca.ODBDeskAcc = "20202810003";
    else
      ca.ODBDeskAcc = "20308810001";
    end;
  elif ( cv.TypeValue == CM_INGOT )        // Драг.металлы.
    ca.ODBDeskAcc = "20302VVV001";
  elif ( cv.TypeValue == TYPERES_PAYDOC )  // Дорожные чеки.
    ca.ODBDeskAcc = "20203VVV001";
  end;

  return 0;

end;

//==============================================================================
//   Пример процедуры установки счета кассы в таблице счетов бал. стоимостей.
//==============================================================================
macro SetCashAccountForBalCost( addr )
  var vCurrCode = "ВВВ";

  SetBuff( da, addr );

  if ( NOT findCasValue( da.ValueRef ) )  // Не нашли ресурс кассы.
    return 0;
  end;

  if ( cv.TypeValue == TYPERES_CURRENCY )  // Валюты.
    if ( getCurr( cv.CodIntValue ) )
      vCurrCode = String( cr.ExternalCode:3 );
    else
      vCurrCode = "ВВВ";
    end;  
    if ( cv.Type == TYPE_RES_PAY )         // Платежеспособные
      da.Account = "20202" + vCurrCode + "001";
    else
      da.Account = "20202" + vCurrCode + "002";
    end;
  elif ( cv.TypeValue == CM_COIN )         // Монеты.
    if ( CoinType( cv.CodIntValue ) == CK_NEW )
      da.Account = "20202810003";
    else
      da.Account = "20308810001";
    end;
  elif ( cv.TypeValue == CM_INGOT )        // Драг.металлы.
    da.Account = "20302VVV001";
  elif ( cv.TypeValue == TYPERES_PAYDOC)   // Дорожные чеки.
    da.Account = "20203VVV001";
  end;

  return 0;

end;


//==============================================================================
//                                                                           
//==============================================================================
macro SetNotBalAccountForNominal( addr )

  SetBuff( reg_num, addr );

  if ( findCasValue( reg_num.ValueRef ) )
    if ( cv.TemplateValue != 0 )
      ClearRecord( par );
      par.ValueRef = cv.TemplateValue;
      par.Par = reg_num.Nominal;

      if ( GetEQ( par ) )
        reg_num.Account = par.Account;
      end; 
    end;
  end;

  return 0;

end;
