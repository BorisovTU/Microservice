
/*
**  Отчет для аудита сторнирований и удалений
**  документов
**
*/

import CommonInter, DeskInter, ExchangeInter, "define.mac", Acc_Form;

file SCLog         ( "sc_log.dbt", "spcard.def" ) key 3;
file CIDoc         ( "ci_doc.dbt"   );
file Currency      ( "currency.dbt" ) key 0;
file CIOper        ( "ci_oper.dbt"  ) key 0;
file pay_kind      ( "pay_kind.dbt" );             /* Виды платежей       */
file pay_csop      ( "pay_csop.dbt" );             /* Подвиды платежей    */
file dtyp          ( "sb_dtyp.dbt"  );
file dr            ( "depositr.dbt" );
file OpTypes       ( "sb_typop.dbt" ) key 0;
file SubOper       ( "suboper.dbt"  ) key 1;
file RtlCurr       ( "currency.dbt" ) key 1;

record DepDoc      ( "sbdepdoc.dbt" );
record PayDoc      ( "pay_doc.dbt"  );
file PmntProp	   ( "RTPaymentProp.dbt") key 0;
record rCIDoc      ( "ci_doc.dbt"   );
record ExchOperat  ( "exoperat.dbt", "exchange.def" );
record Dates       ( "sd_dates", "rtusrmac") dialog;
/* структуры для доступа к полю ExtInfo структуры операции */
record paydocop    ( "paydocop.str", "exchange.def" );
record incassop    ( "incasso.str" , "exchange.def" );
record retincassop ( "retincas.str", "exchange.def" );

const
  K_F3 = 317;

const
  LogSC_sb_depdoc   = 506,
  LogSC_ci_doc      = 601,
  LogSC_pay_doc     = 701,
  LogSC_exch_operat = 801;

const
  LogSC_Insert  = 2,   /* вставка новой записи              */
  LogSC_Update  = 3,   /* корректировка существующей записи */
  LogSC_Delete  = 4,   /* удаление существующей записи      */
  LogSC_Storn   = 100; /* сторнирование существующей записи */

const
  BS_BRANCH  = 0,  /* Филиал */
  BS_DEP     = 1,  /* Отделение */
  BS_EXCH    = 2,  /* Обменный пункт */
  BS_OPERDEP = 3;  /* Оперчасть */

const
  CI_OGSZ          = 1,
  CI_LOTTERY       = 2,
  CI_CERT          = 3,
  CI_COIN          = 4,
  CI_INGOT         = 5,
  CI_ORVVZ         = 6,
  CI_MISC          = 10;

var
  {curdate},
  Branch = NumFNCash,
  BranchState = GetBranchStatus,
  StartDate = {curdate},
  EndDate = {curdate},
  ClientList = TClientList,
  SubHeadPrinted,
  G_BPName;

var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

macro GetCurrShortName( Curr )

  Currency.Code_Currency = Curr;
  if ( GetEQ( Currency ) )
    return Currency.Short_Name;
  else
    return "";
  end;
end;

/* преобразует код валюты ОП в код Retail */
/* RA 26-03-01 Больше не требуется. В ОП теперь ретейловский код валюты.
macro ExchCurrToRtl( Exch_CodeCurr )

  var OldKeyNum = KeyNum( RtlCurr, 1 );

  RtlCurr.ExternalCode = string(Exch_CodeCurr);
  while (StrLen(RtlCurr.ExternalCode) < 3)
    RtlCurr.ExternalCode = "0" + RtlCurr.ExternalCode;
  end;
  if( not GetEQ( RtlCurr ) )
    MsgBox("Ошибка при определении курса валюты ", RtlCurr.ExternalCode );
    Exit(1);
  end;
  KeyNum( RtlCurr, OldKeyNum );
  return RtlCurr.Code_Currency;
end;
*/

macro Find_PayKind

  var stat;

  KeyNum(pay_kind,0);
  ReWind(pay_kind);
  pay_kind.IsCur      = PayDoc.IsCur;
  pay_kind.GroupOpert = PayDoc.GroupOpert;
  pay_kind.NumOperat  = PayDoc.NumOpert;
  stat = GetEQ(pay_kind);

  return stat;

end;

macro Find_PayCsOp

  var stat = FALSE;

  if (PayDoc.ApplType > 0)
    KeyNum(pay_csop,0);
    ReWind(pay_csop);
    pay_csop.IsCur     = PayDoc.IsCur;
    pay_csop.GroupType = PayDoc.GroupOpert;
    pay_csop.NumOperat = PayDoc.NumOpert;
    pay_csop.ApplType  = PayDoc.ApplType;
    stat = GetEQ(pay_csop);
  end;

  return stat;

end;

macro Pay_MakeDesc

  var Ground = "";

  /* ------------------- Коммунальные платежи ------------------- */
  if (PayDoc.GroupOpert == 1)
    /* Название получателя */
    Ground = PmntProp.Name;
  /* ------------- Наличные перечисления и выплаты -------------- */
  elif ((PayDoc.GroupOpert == 11) OR (PayDoc.GroupOpert == 41))
    /* Вид платежа */
    if (Find_PayKind())
      Ground = pay_kind.SzNameAlg + " ";
    end;
    /* Подвид платежа */
    if (Find_PayCsOp())
      Ground = Ground + "(" + pay_csop.NameCompType + ") ";
    end;
    /* Счет */
    Ground = Ground + Acc_Form(PmntProp.Account);
  /* ------------------------- Комиссии ------------------------- */
  elif (PayDoc.GroupOpert == 21)
    /* Вид платежа */
    if (Find_PayKind())
      Ground = pay_kind.SzNameAlg;
    end;
  /* ------------------------ Конверсии ------------------------- */
  elif (PayDoc.GroupOpert == 31)
    /* Вид платежа */
    if (Find_PayKind())
      Ground = pay_kind.SzNameAlg;
    end;
  end;

  /* Для всех кассовых операций добавляем основание */
  Ground = Trim(Ground + "  " + PayDoc.Ground) + "  ";

  /* Для всех кассовых операций добавляем ФИО, если оно есть */
  if (Trim(PayDoc.ClientLastName) != "")
    Ground = Ground + PayDoc.ClientLastName;
  end;
  if (Trim(PayDoc.ClientFirstName) != "")
    Ground = Ground + " " + SubStr(PayDoc.ClientFirstName,1,1) + ".";
  end;
  if (Trim(PayDoc.ClientSecondName) != "")
    if (Trim(PayDoc.ClientFirstName) != "")
      Ground = Ground + SubStr(PayDoc.ClientSecondName,1,1) + ".";
    else
      Ground = Ground + " " + SubStr(PayDoc.ClientSecondName,1,1) + ".";
    end;
  end;
  Ground = Trim(Ground);

  return Ground;

end;

macro Dep_GetClientData

  if( DepDoc.CodClient != 0)
    ClientList.GetRecord( DepDoc.CodClient );
  else
    KeyNum (dtyp, 2);
    dtyp.FlagCur = DepDoc.IsCur;
    dtyp.Kind = DepDoc.Type_Account;
    if ( getEQ (dtyp) )
      ClientList.CurRec.Rec.Name1 = SubStr(dtyp.Name,1,25);
    else
      ClientList.CurRec.Rec.Name1 = "";
    end;
    ClientList.CurRec.Rec.Name2 = "";
    ClientList.CurRec.Rec.Name3 = "";
  end;
end;

macro Dep_MakeDesc

  var Ground = "";
  var FIO;

  Dep_GetClientData;

  if (((DepDoc.TypeOper ==  4) AND (DepDoc.TypeComplexOper == 10)) OR
      ((DepDoc.TypeOper == 63) AND (DepDoc.TypeComplexOper == 81))   )
    KeyNum(OpTypes,0);
    ReWind(OpTypes);
    OpTypes.IsCur    = DepDoc.IsCur;
    OpTypes.Kind     = DepDoc.Type_Account;
    OpTypes.NumOpert = DepDoc.TypeComplexOper;
    if (GetEQ(OpTypes))
      Ground = OpTypes.czNameAlg;
    end;
  else
    KeyNum(OpTypes,0);
    OpTypes.IsCur    = DepDoc.IsCur;
    OpTypes.Kind     = DepDoc.Type_Account;
    OpTypes.NumOpert = DepDoc.TypeOper;
    if (GetEQ(OpTypes))
      Ground = OpTypes.czNameAlg;
    end;
    if (DepDoc.ApplType > 0)
      KeyNum(SubOper,1);
      SubOper.IsCur    = DepDoc.IsCur;
      SubOper.Kind     = DepDoc.Type_Account;
      SubOper.OperType = DepDoc.TypeOper;
      SubOper.Type     = DepDoc.ApplType;
      if (GetEQ(SubOper))
        Ground = Ground + " " + SubOper.Name;
      end;
    end;
  end;

  Ground = Acc_Form(DepDoc.Account) + " " + Ground;

  KeyNum(dr,8);
  dr.Referenc = DepDoc.Referenc;
  if ( not GetEQ(dr) )
    ClearRecord(dr);
  end;

  /*
  if ( ClientList.CurRec.Rec.SpecialAccess OR dr.SpecialAccess)
    FIO = dtyp.Kind;
  else
    FIO = ClientList.CurRec.ConvertFIO;
  end; */

  if ( (not SpecialAccess) and OperMayListSpecialAccounts and dr.SpecialAccess)  
     FIO = "XXXXXX X.X.";
  else
     FIO = ClientList.CurRec.ConvertFIO;
  end; 

  return Ground + " " + FIO;
end;


macro ТипОперацииОП( КодОперации, Покупка, Продажа, Конверсия )

  Покупка   = false;
  Продажа   = false;
  Конверсия = false;

  if   (   КодОперации == CDF_OperCurrConversion      )
    Конверсия = true;
  elif ( ( КодОперации == CDF_OperCurrBuy             ) or
         ( КодОперации == CDF_OperPayDocBuy           ) or
         ( КодОперации == CDF_OperPayDocBuyCurr       ) or
         ( КодОперации == CDF_OperPayDocBuyAcc        ) or
         ( КодОперации == CDF_OperPayDocBuyCurrAcc    ) or
         ( КодОперации == CDF_OperChange              ) or
         ( КодОперации == CDF_OperCardTake            ) or
         ( КодОперации == CDF_OperSplit               ) or
         ( КодОперации == CDF_OperUnpayCurrBuy        ) or
         ( КодОперации == CDF_OperAvance              ) or
         ( КодОперации == CDF_OperIncasso             ) or
         ( КодОперации == CDF_OperExpert              ) or
         ( КодОперации == CDF_OperPayDocIncasso       ) or
         ( КодОперации == CDF_OperPayDocExpert        ) or
         ( КодОперации == CDF_OperCheck               ) or
         ( КодОперации == CDF_OperDetectOverplus      ) or
         ( КодОперации == CDF_OperCompensateDeficit   )
       )
    Покупка = true;
  elif ( ( КодОперации == CDF_OperCurrSale            ) or
         ( КодОперации == CDF_OperPayDocSale          ) or
         ( КодОперации == CDF_OperPayDocSaleCurr      ) or
         ( КодОперации == CDF_OperPayDocSaleAcc       ) or
         ( КодОперации == CDF_OperPayDocSaleCurrAcc   ) or
         ( КодОперации == CDF_OperCardGive            ) or
         ( КодОперации == CDF_OperTransfer            ) or
         ( КодОперации == CDF_OperCashOut             ) or
         ( КодОперации == CDF_OperReturnIncasso       ) or
         ( КодОперации == CDF_OperReturnExpert        ) or
         ( КодОперации == CDF_OperPayDocReturnIncasso ) or
         ( КодОперации == CDF_OperPayDocReturnExpert  ) or
         ( КодОперации == CDF_OperGiveOutOverplus     ) or
         ( КодОперации == CDF_OperDetectDeficit       )
       )
    Продажа = true;
  end;
  SetParm( 1, Покупка   );
  SetParm( 2, Продажа   );
  SetParm( 3, Конверсия );
end;

macro Exch_MakeDesc()

  var Desc = "";

  /* название операции */
  /*Desc = Desc + GetNameCodif( ExchOperat.Oper_Ref, CDF_Oper );  RA 26-03-01 */
  Desc = Desc + GetNameExchOperation( ExchOperat.Branch, ExchOperat.Kind_Oper, ExchOperat.SubOper );

  /* название ПД, если есть */
  if  ( ( ExchOperat.Kind_Oper == CDF_OperPayDocBuy           ) or
        ( ExchOperat.Kind_Oper == CDF_OperPayDocBuyCurr       ) or
        ( ExchOperat.Kind_Oper == CDF_OperPayDocSale          ) or
        ( ExchOperat.Kind_Oper == CDF_OperPayDocSaleCurr      ) or
        ( ExchOperat.Kind_Oper == CDF_OperPayDocBuyAcc        ) or
        ( ExchOperat.Kind_Oper == CDF_OperPayDocBuyCurrAcc    ) or
        ( ExchOperat.Kind_Oper == CDF_OperPayDocSaleAcc       ) or
        ( ExchOperat.Kind_Oper == CDF_OperPayDocSaleCurrAcc   )   
      )
    SetRecordAddr( paydocop, ExchOperat, 0, FldOffset( ExchOperat, "ExtInfo" ), True );
    Desc = Desc + " " + GetNamePayDoc( paydocop.PayDoc_Ref );

  elif( ( ExchOperat.Kind_Oper == CDF_OperIncasso             ) or
        ( ExchOperat.Kind_Oper == CDF_OperExpert              ) or
        ( ExchOperat.Kind_Oper == CDF_OperPayDocIncasso       ) or
        ( ExchOperat.Kind_Oper == CDF_OperPayDocExpert        )
      )
    SetRecordAddr( incassop, ExchOperat, 0, FldOffset( ExchOperat, "ExtInfo" ), True );
    Desc = Desc + " " + incassop.TrsName;

  elif( ( ExchOperat.Kind_Oper == CDF_OperReturnIncasso       ) or
        ( ExchOperat.Kind_Oper == CDF_OperReturnExpert        ) or
        ( ExchOperat.Kind_Oper == CDF_OperPayDocReturnIncasso ) or
        ( ExchOperat.Kind_Oper == CDF_OperPayDocReturnExpert  )
      )
    SetRecordAddr( retincassop, ExchOperat, 0, FldOffset( ExchOperat, "ExtInfo" ), True );
    if( retincassop.PayDoc_Ref != 0 )
      Desc = Desc + " " + GetNamePayDoc( retincassop.PayDoc_Ref );
    end;
  end;

  return Desc;
end;

macro Dep_GetDocInfo( Desc, DocDate, Oper, Sum, Curr, Ground )

  SetRecordAddr( DepDoc, SCLog );

  Desc = Dep_MakeDesc;
  DocDate = DepDoc.Date_Document;
  Oper = DepDoc.Oper;
  if ( DepDoc.InSum )
    Sum = DepDoc.InSum;
  else
    Sum = DepDoc.OutSum;
  end;
  Curr = DepDoc.Code_Currency;
  Ground = DepDoc.Ground;
  SetParm( 0, Desc );
  SetParm( 1, DocDate );
  SetParm( 2, Oper );
  SetParm( 3, Sum );
  SetParm( 4, Curr );
  SetParm( 5, Ground );
end;

macro CI_GetDocInfo( Desc, DocDate, Oper, Sum, Curr, Ground )

  record OGSZInfo( "ci_doc.1" );
  record MiscInfo( "ci_doc.2" );
  record CertInfo( "ci_doc.3" );
  record IngotInfo( "ci_doc.4" );

  SetRecordAddr( rCIDoc, SCLog );
  Copy( CIDoc, rCIDoc );

  DocDate = CIDoc.Date;
  Oper = CIDoc.Oper;
  Sum = CIDoc.Sum;
  Curr = 0;
  Ground = "";
  Desc = "";
  CIOper.Type = CIDoc.Type;
  CIOper.Number = CIDoc.Operation;
  if ( GetEQ( CIOper ) )
    Desc = Desc + CIOper.Name + ": ";
  end;
  if ( CIDoc.Type == CI_OGSZ )
    SetRecordAddr( OGSZInfo, CIDoc, 0, FldOffset( CIDoc, "Info" ), true );
    Desc = Desc + "ОГСЗ серии " + OGSZInfo.Series +
      ", номинал " + String(OGSZInfo.Par);
  elif ( ( CIDoc.Type == CI_LOTTERY  ) or ( CIDoc.Type == CI_ORVVZ ) )
    SetRecordAddr(MiscInfo, CIDoc, 0, FldOffset( CIDoc, "Info" ), true );
    Desc = Desc + MiscInfo.Name;
  elif ( CIDoc.Type == CI_CERT )
    SetRecordAddr( CertInfo, CIDoc, 0, FldOffset( CIDoc, "Info" ), true );
    Desc = Desc + "Сбер.сертификат " + CertInfo.Year + "г, номинал " +
      CertInfo.Par;
  elif( ( CIDoc.Type == CI_MISC ) or ( CIDoc.Type == CI_COIN ) )
    SetRecordAddr( MiscInfo, CIDoc, 0, FldOffset( CIDoc, "Info" ), true );
    Desc = Desc + MiscInfo.Name;
  elif ( CIDoc.Type == CI_INGOT )
    SetRecordAddr( IngotInfo, CIDoc, 0, FldOffset( CIDoc, "Info" ), true );
    Desc = Desc + IngotInfo.Name;
    Ground = IngotInfo.Ground;
  end;
  SetParm( 0, Desc );
  SetParm( 1, DocDate );
  SetParm( 2, Oper );
  SetParm( 3, Sum );
  SetParm( 4, Curr );
  SetParm( 5, Ground );
end;

macro Pay_GetDocInfo( Desc, DocDate, Oper, Sum, Curr, Ground )

  SetRecordAddr( PayDoc, SCLog );
  PmntProp.DocApplicationKind = PayDoc.iApplicationKind;
  PmntProp.DocApplicationKey  = PayDoc.ApplicationKey;
  if (not GetEQ(PmntProp))
	ClearRecord(PmntProp);
  end;
  Desc = Pay_MakeDesc;
  DocDate = PayDoc.Date_Document;
  Oper = PayDoc.Oper;
  Sum = PayDoc.InSum;
  Curr = PayDoc.CodCur;
  Ground = PayDoc.Ground;
  SetParm( 0, Desc );
  SetParm( 1, DocDate );
  SetParm( 2, Oper );
  SetParm( 3, Sum );
  SetParm( 4, Curr );
  SetParm( 5, Ground );
end;

macro Exch_GetDocInfo( Desc, DocDate, Oper, Sum, Curr, Ground )

  var Покупка, Продажа, Конверсия;

  SetRecordAddr( ExchOperat, SCLog );
  ТипОперацииОП( ExchOperat.Kind_Oper, Покупка, Продажа, Конверсия );
  Desc    = Exch_MakeDesc();
  DocDate = ExchOperat.CurDate;
  Oper    = ExchOperat.Oper;
  if( Покупка )
    Sum     = ExchOperat.IncomeAmount;
    /*Curr    = ExchCurrToRtl( ExchOperat.IncomeCurr_Ref );    RA 26-03-01 */
    Curr    = FindCodCurByRefVal( ExchOperat.IncomeRefVal );
  end;
  if( Продажа )
    Sum     = ExchOperat.OutAmount;
    /*Curr    = ExchCurrToRtl( ExchOperat.OutCurr_Ref );       RA 26-03-01 */
    Curr    = FindCodCurByRefVal( ExchOperat.OutRefVal );
  end;
  Ground  = "";
  SetParm( 0, Desc );
  SetParm( 1, DocDate );
  SetParm( 2, Oper );
  SetParm( 3, Sum );
  SetParm( 4, Curr );
  SetParm( 5, Ground );
end;

macro GetDocInfo( Desc, DocDate, Oper, Sum, Curr, Ground )

  if ( SCLog.NumFile == LogSC_sb_depdoc )
    Dep_GetDocInfo( Desc, DocDate, Oper, Sum, Curr, Ground );
  elif ( SCLog.NumFile == LogSC_ci_doc )
    CI_GetDocInfo( Desc, DocDate, Oper, Sum, Curr, Ground );
  elif ( SCLog.NumFile == LogSC_pay_doc )
    Pay_GetDocInfo( Desc, DocDate, Oper, Sum, Curr, Ground );
  elif ( SCLog.NumFile == LogSC_exch_operat )
    Exch_GetDocInfo( Desc, DocDate, Oper, Sum, Curr, Ground );
  end;
  SetParm( 0, Desc );
  SetParm( 1, DocDate );
  SetParm( 2, Oper );
  SetParm( 3, Sum );
  SetParm( 4, Curr );
  SetParm( 5, Ground );
end;

macro GetBranchName

  var name;

  if (not findDepartment(Branch, name))
    name = "";
  end;
  return name;
end;

macro EventHandler( Dlg, Cmd, Id, Key )

  if ( Cmd == DLG_KEY )
    if ( ( Key == K_F3 ) and ( Id == 0 ) and ( BranchState == BS_DEP ) )
      SelectFNCash( Branch );
      Dlg.BrName = GetBranchName( Branch );
      UpdateFields( Dlg );
    end;
  end;
  return CM_DEFAULT;
end;

macro InputBranchAndDates

  Dates.BrName = GetBranchName( Branch );
  Dates.StartDate = StartDate;
  Dates.EndDate = EndDate;

  if ( RunDialog( Dates, "EventHandler" ) )
    StartDate = Dates.StartDate;
    EndDate = Dates.EndDate;
  else
    Exit( 1 );
  end;
end;

macro OutHead

  [ ];
  [##################################################################################################################################]
  ( "Отчет по рискованным проводкам":c);
  [##################################################################################################################################]
  ( ( "за период с " + String( StartDate:m ) + " по " + String( EndDate:m ) ):c );
  [ ];
end;

macro OutSubHead;

  [ ];
  [ ];
  [#]( G_BPName );
  [#]( MkStr( "=", StrLen( G_BPName ) ) );
  [ ];
  [----------------------------------------------------------------------------------------------------------------------------------];
  [    Дата и время    |          |                                   |   Дата   |    |                   |                          ];
  [      проводки      | Действие | О п и с а н и е   о п е р а ц и и | операции |Опер|       Сумма       |    Основание операции    ];
  [                    |          |                                   |          |    |                   |                          ];
  [----------------------------------------------------------------------------------------------------------------------------------];
end;

macro OutLine

  var
    Action = "", Desc = "", DocDate = Date( 0, 0, 0 ), Oper = 0, Sum = $0L, Curr = -2, Ground = "";

  if ( not SubHeadPrinted )
    OutSubHead;
    SubHeadPrinted = true;
  end;

  if ( SCLog.NumOperation == LogSC_Storn )
    Action = "Сторниров.";
  else
    Action = "Удаление";
  end;

  GetDocInfo( Desc, DocDate, Oper, Sum, Curr, Ground );

  [ ########## ######## ########## ################################### ########## #### ############### ### ######################### ]
  (
    SCLog.DateOperDay,
    SCLog.TimeRec,
    Action,
    Desc:w,
    DocDate,
    Oper,
    Sum,
    GetCurrShortName( Curr ),
    Ground:w
  );
end;

macro ReportOnBP( FileNo, BPName )

  var
    RecFound;

  SubHeadPrinted = false;
  G_BPName = BPName;
  ClearRecord( SCLog );
  SCLog.FNCash = Branch;
  SCLog.NumFile = FileNo;
  SCLog.DateOperDay = StartDate;
  RecFound = GetGE( SCLog );
  while ( RecFound
    and ( SCLog.FNCash == Branch )
    and ( SCLog.NumFile == FileNo )
    and ( SCLog.DateOperDay <= EndDate ) )
    if ( ( SCLog.NumOperation == LogSC_Storn  ) or
         ( SCLog.NumOperation == LogSC_Delete )   )
      OutLine( );
    end;
    RecFound = Next( SCLog );
  end;
end;

  InputBranchAndDates;
  OutHead;
  ReportOnBP( LogSC_sb_depdoc,   "Вклады" );
  ReportOnBP( LogSC_pay_doc,     "Коммунальные платежи и налично-кассовые операции" );
  ReportOnBP( LogSC_ci_doc,      "Ценные бумаги и ценности" );
  ReportOnBP( LogSC_exch_operat, "Валюто-обменные операции" );
end;