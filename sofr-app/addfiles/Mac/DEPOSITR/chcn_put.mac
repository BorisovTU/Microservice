/*
  RS-Bank 6.0
  RS-Retail

  Выплата вкладов Чеченского банка

  Прием данных в реестр вкладчиков из файла
 заданного формата (в таблице Exel)
 -----------------------------------------------------------
  ВНИМАНИЕ!
 Рекомендую не использовать русских букв в имени исходного файла
 -----------------------------------------------------------
  Макрос работает в двузвенке.
  В трехзвенке Exel должен быть установлен на сервере,
  исходный файл - также на сервере.
 -----------------------------------------------------------
  Ромодин Александр Васильевич
  05.01.2004
*/
import RslX,DeprIntr,rcw;

// ##### Параметры #####
// Шаблон файла (*.xls) - можно с путем, например, "e:\\work\\*.xls"
private var PatternSourceFile = "*.xls";
// Раздел данных  в таблице Exel
private var Sheet = 1;
// Необходимость подробного отчета (по каждой занесенной записи реестра)
private var NeedFullReport = true;
// ##### Конец параметров #####

private var chcnrgtp = TBFile ("chcnrgtp.dbt", "w", 1);
private var chcnrgst = TBFile ("chcnrgst.dbt", "w", 0);

private var RgtpID = 0; // Идентификатор вида принимаемого реестра
private var LastRgstID = 0; // Последний идентификатор реестра
private var NumErr = 0, // Количество ошибок
            NumAll = 0, // Количество просмотренных записей реестров
            NumW   = 0; // Количество занесенных записей реестров
private var NumAccBreak     = "",  // Номер счета из файла прерывания
            ChcnDepartBreak = "",  // Номер отделения ЧСБ из файла прерывания
            ChcnBranchBreak = "";  // Номер подразделения ЧСБ из файла прерывания
private var ChcnDepart = "",       // Номер обрабатываемого отделения ЧСБ 
            ChcnBranch = "";       // Номер обрабатываемого подразделения ЧСБ
private var NameSourceFile = "",   // Имя выбранного исходного файла
            NameWorkBook   = "";   // Имя выбранного исходного файла без пути
private var NumCurRow = 0, // Номер текущей строки в исходной таблице
            NumRows   = 0; // Количество строк в исходной таблице

private var {curdate}, {oper};
//
// Доступ к Exel
//
private var xl,
            startxl;

//if (isStandAlone())  // Если двузвенка
  xl = ActiveX ("Excel.Application");
//else                 // Если трехзвенка              
//  startxl = CreateObject("rsax", "TRsAxServer", "RetailAxServer", isStandAlone());
//  xl = startxl.CreateComObject("Excel.Application");
//end;

// ****************************************************** 
// ****************************************************** 
// ##### Сообщение об ошибке ############################
// ****************************************************** 

macro ErrorMes (str,NeedSt)
  var ErrCod, ErrStr;
  
  NumErr = NumErr + 1;
  println (" ");
  println (" !!! ОШИБКА: "+str);
  if (NeedSt)
    ErrCod = Status (ErrStr);
    println (" Код ошибки: "+String(ErrCod));
    println (ErrStr);
  end;
  println (" ");
end;

// ****************************************************** 
// ##### Файл контрольной точки #########################
// ****************************************************** 
file TXTBREAKW() txt 300 write;
file TXTBREAKR() txt 300;
var  wrkDir    = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\WORKDIR", true );
var  NameBreak = wrkDir + "chcn_put.brk"; // Файл контрольной точки 

// ******************************************************

macro ДобавитьПробелыДоДлины (str,len)
  var ret = str,
      num = StrLen(ret);
  while (num < len)
    ret = ret + " ";
    num = num + 1;
  end;
  return ret;
end;

macro WriteToBreakFile
(
 tID,             // Вид реестра             (до 6 цифр)
 ChcnDepart,      // Номер отделения ЧСБ     (до 5 цифр)
 ChcnBranch,      // Номер подразделения ЧСБ (до 5 цифр)
 NumAcc,          // Номер счета             (до 25 символов)
 NameSF           // Имя исходного файла     (до 200 символов)
)
/*
  Запись информации в файл прерывания
*/
  var stat = True;
  var str;

  if (Open(TXTBREAKW,NameBreak))
    str = String(tID);
    str = ДобавитьПробелыДоДлины(str,6);
    str = str + ChcnDepart;
    str = ДобавитьПробелыДоДлины(str,11);
    str = str + ChcnBranch;
    str = ДобавитьПробелыДоДлины(str,16);
    str = str + Acc_Form(String(NumAcc));
    str = ДобавитьПробелыДоДлины(str,41);
    str = str + String(NameSF);
    stat = Insert (TXTBREAKW,str);
    Close (TXTBREAKW);
  else
    stat = False;
  end;
  return stat;
end;
// ******************************************************

macro ReadFromBreakFile
(
 tID,             // Возвращает вид реестра  (до 6 цифр)
 ChcnDepart,      // Номер отделения ЧСБ     (до 5 цифр)
 ChcnBranch,      // Номер подразделения ЧСБ (до 5 цифр)
 NumAcc,          // Возвращает номер счета  (до 25 символов)
 NameSF           // Имя исходного файла     (до 200 символов)
)
  var stat = True;
  var str;

  tID             = 0;
  ChcnDepart      = "";
  ChcnBranch      = "";
  NumAcc          = "";
  NameSF          = "";
  if (ExistFile(NameBreak))
    if (NOT Open(TXTBREAKR,NameBreak))
      if (NOT GetTrue(TRUE,"Ошибка при чтении файла прерываний. Начать работу с начала?"))
        stat = False;
      end;
    else
      ReWind (TXTBREAKR);
      Next (TXTBREAKR);
      str = TXTBREAKR.str;
      if (StrLen(str) > 0)
        tID             =  Int(SubStr(str, 1,  6));
        ChcnDepart      = Trim(SubStr(str, 7,  5));
        ChcnBranch      = Trim(SubStr(str,12,  5)); 
        NumAcc          = Trim(SubStr(str,17, 25));
        NameSF          = Trim(SubStr(str,42,200));
      end;
      if (tID != 0)
        if (NOT GetTrue(TRUE,"Продолжить загрузку реестра с прерванного места?"))
          if (GetTrue(TRUE,"Начать работу с начала?"))
            tID = 0;
            ChcnDepart      = "";
            ChcnBranch      = "";
            NumAcc = "";
            NameSF = "";
          else
            MsgBox ("Вы отказались от ввода реестра");
            stat = False;
          end;
        end;
      end;
      Close (TXTBREAKR);
    end;
  end;
  SetParm (0,tID);
  SetParm (1,ChcnDepart);
  SetParm (2,ChcnBranch);
  SetParm (3,NumAcc);
  SetParm (4,NameSF);
  return stat;
end;
// ****************************************************** 
// ##### Протокол #######################################
// ****************************************************** 

macro HeadLog ()
  println (" ----- RS-Retail ----- Реестр вкладчиков бывшего ЧСБ ----- "+String(Date())+" ----- "+String(Time())+" -----");
  println (" ");
  println ("      Прием реестра вкладчиков бывшего Чеченского банка");
  println ("      =================================================");
  println ("      Исх.файл: "+NameSourceFile);
  println ("      Вид реестра: "+chcnrgtp.rec.UserID);
  println (" ");
  if ((ChcnDepartBreak == "") AND (ChcnBranchBreak == ""))
    println (" Обработка с начала файла");
  else
    println (" Продолжение обработки:");
    println ("    Подразделение # "+ ChcnDepartBreak +"/"+ChcnBranchBreak);
    if (NumAccBreak != "")
      println ("    Счет "+NumAccBreak);
    end;
  end;
  if (NeedFullReport)
    println (" ");
    println ("  # пп   Идент. Подразделение    Номер счета      ФИО");
  end;
  println (" ");
end;
// ******************************************************

macro FinishLog
  println (" ");
  println (" ");
  println (" Работа завершена");
  println (" Всего просмотрено "+String(NumAll)+" записей");
  println (" Внесено в реестр "+String(NumW)+" записей");
  println (" Допущено "+String(NumErr)+" ошибок");
  println (" ");
  println (" ----- RS-Retail ----- Реестр вкладчиков бывшего ЧСБ ----- "+String(Date())+" ----- "+String(Time())+" -----");
end;
// ******************************************************

macro ПробелВперед (par,len)
  var str = String(par);
  var l   = StrLen(str);

  while (l < len)
    str = " " + str;
    l = l + 1;
  end;
  return str;
end;

macro СтрокаВОтчет
  var str = " "+ПробелВперед(NumW,5)+" "+ПробелВперед(chcnrgst.rec.RgstID,7)+": "+
            ПробелВперед(chcnrgst.rec.ChcnDepart,5)+"/"+ПробелВперед(chcnrgst.rec.ChcnBranch,5)+"   "+
            Acc_Form(chcnrgst.rec.RgstAccount);
  Message (str);
  if (NeedFullReport)
    str = str + "  " +
          chcnrgst.rec.RgstName1+" "+chcnrgst.rec.RgstName2+" "+
          chcnrgst.rec.RgstName3;
    println (str);
  end;
end;
// *********************************************************
// ##### ОбработкаИсходногоФайла ###########################
// *********************************************************

macro ЧтениеЯчейки (Row,Col)
  return xl.workbooks(NameWorkBook).worksheets(Sheet).UsedRange.cells(Row,Col).value;
end;             

macro ЧислоСтрокВТаблице
  return xl.workbooks(NameWorkBook).worksheets(Sheet).UsedRange.Rows.Count;
end;
// *********************************************************

macro ПропускСтрокБезДанных
//
// Установка текущего номера строки (NumCurRow) 
// на очередную запись с данными
//
  var st = true;
  var str = NULL;

  while ((str == NULL)
     AND (NumCurRow < NumRows))
    NumCurRow = NumCurRow + 1;
    str = ЧтениеЯчейки (NumCurRow,1);
  end;
  if (str == NULL)
    st = false;
  end;
  return st;
end;
// *********************************************************

macro ДостигнутКонецТаблицы
  var st;
  if (NumCurRow > NumRows)
    st = true;
  else
    st = false;
  end;
  return st;
end;
// *********************************************************

macro ПропускСтрокСДанными
//
// Установка текущего номера строки (NumCurRow) 
// на очередную запись без данных
//
  var st   = true;
  var cont = true;
  var str  = "";

  while (cont AND (NOT ДостигнутКонецТаблицы()))
    str = ЧтениеЯчейки (NumCurRow,1);
    if (str != NULL)
      NumAll = NumAll + 1;
      NumCurRow = NumCurRow + 1;
    else
      cont = false;
    end;
  end;
  st = NOT ДостигнутКонецТаблицы();
  return st;
end;
// *********************************************************

macro СтрокаЦифр (str)
//
// Из исходной строки выделяется строка, состоящая из цифр 
// (символы выбираются от начала строки,
// выбор завершается, как только встречается не цифра
//
  var ret = "";
  var Num = 1,
      Len = StrLen(str);
  var cont = true;
  var symb;

  while (cont AND (Num<=Len))
    symb = SubStr(str,Num,1);
    if ((symb == "0") OR (symb == "1") OR (symb == "2") OR (symb == "3")
     OR (symb == "4") OR (symb == "5") OR (symb == "6") OR (symb == "7")
     OR (symb == "8") OR (symb == "9"))
      ret = ret + symb;
    else
      cont = false;
    end; 
    Num = Num + 1;
  end;
  return ret;
end;
// *********************************************************

macro ЧтениеНомераПодразделения
  var st = true;
  var Num;
  var str;

  st = ПропускСтрокБезДанных ();
  if (st)
    Num = NumCurRow - 1;  // Строка с номером подразделения
    str = ЧтениеЯчейки (Num,2);
    if (str != NULL)
      Num = Index (str,"№");
      str = SubStr(str,Num+1);
      Num = Index (str,"/");
      if (Num == 0)
        ChcnDepart = СтрокаЦифр(str);
        ChcnBranch = "";
      else
        ChcnDepart = Trim(SubStr(str,1,Num-1));
        str = SubStr(str,Num+1);
        ChcnBranch = СтрокаЦифр(str);
      end;
    else
      ErrorMes ("Чтение номера подразделения ЧСБ",false);
      st = false;
    end;
  end;
  return st;
end;
// *********************************************************

macro ПрочитатьФИО (Name1 ,Name2 ,Name3)
  var str = Trim(ЧтениеЯчейки(NumCurRow,4));
  var Num;
  Num = Index(str," ");
  if (Num != 0)
    Name1 = SubStr(str,1,Num-1);
    str = Trim(SubStr(str,Num+1));
    Num = Index(str," ");
    if (Num != 0)
      Name2 = SubStr(str,1,Num-1);
      Name3 = Trim(SubStr(str,Num+1));
    else
      Name2 = Trim(str);
      Name3 = "";
    end;
  else
    Name1 = Trim(str);
    Name2 = "";
    Name3 = "";
  end;
  SetParm (0,Name1);
  SetParm (1,Name2);
  SetParm (2,Name3);
end;      
// *********************************************************

macro ЧтениеДаты (col)
  var ret;
  var str = ЧтениеЯчейки (NumCurRow,col);
  var d = 0, m = 0, y = 0;
  var Num;

  if (ValType(str) == V_DATE)
    DateSplit(str,d,m,y);
    if (y > 2000)
      y = 1900 + y - 2000;
    end;
  else
    if (ValType(str) == V_STRING)
      Num = Index(str,".");
      if (Num == 0)
        Num = Index(str,",");  // Возможна опечатка при вводе даты
      end;  
      if (Num != 0)
        d = Int(SubStr(str,1,Num-1));
        str = SubStr(str,Num+1);
        Num = Index(str,".");
        if (Num == 0)
          Num = Index(str,",");  // Возможна опечатка при вводе даты
        end;  
        if (Num != 0)
          m = Int(SubStr(str,1,Num-1));
          y = Int(SubStr(str,Num+1));
          if (y < 100)
            y = y + 1900;
          end;
        end;
      end;
    end;
  end;
  ret = Date (d,m,y);
  return ret;
end;
// *********************************************************

macro ТипДокументаВыплаты
  var ret = 0;
  var str = Trim(ЧтениеЯчейки (NumCurRow,7));
  if (str == "1")
    ret = 0;  // Сберкнижка 
  else
    str = Trim(ЧтениеЯчейки (NumCurRow,8));
    if (str == "1")
      ret = 1;  // Форма 31
    end;
  end;
  return ret;
end;
// *********************************************************

macro ЧтениеСуммы (col)
  var ret;
  var str = ЧтениеЯчейки (NumCurRow, col);

  if (ValType(str) == V_MONEY)
    ret = str;
  elif (ValType(str) == V_DOUBLE)
    ret = str;
  else
    ret = moneyL(str);
  end;
  return ret;
end;
// *********************************************************

macro ОбработкаПодразделения
  var st = true;
  var cont = true;
  var str;
  var Acc;

  while (cont AND (NOT ДостигнутКонецТаблицы()))
    str = ЧтениеЯчейки (NumCurRow,1);
    if (str != NULL)
      NumAll = NumAll + 1;
      Acc = ЧтениеЯчейки (NumCurRow,3);
      if ((NumAccBreak == "") OR (NumAccBreak == Acc))
        WriteToBreakFile (RgtpID,ChcnDepart,ChcnBranch,Acc,NameSourceFile);
        chcnrgst.Clear();
        chcnrgst.rec.RgtpID                 = RgtpID;
        chcnrgst.rec.RgstID                 = LastRgstID + 1;
        LastRgstID = LastRgstID + 1;
        chcnrgst.rec.ChcnDepart             = ChcnDepart;
        chcnrgst.rec.ChcnBranch             = ChcnBranch;
        chcnrgst.rec.RgstAccount            = Acc;
        chcnrgst.rec.RgstCodeCurrency       = 0;
        ПрочитатьФИО (chcnrgst.rec.RgstName1 ,chcnrgst.rec.RgstName2 ,chcnrgst.rec.RgstName3);
        chcnrgst.rec.RgstBorn               = ЧтениеДаты ( 5);
        chcnrgst.rec.RgstDateLastOperation  = ЧтениеДаты (10);
        chcnrgst.rec.RgstPaperIssuer        = ЧтениеЯчейки (NumCurRow,6);
        chcnrgst.rec.RgstPaperKindPay                = ТипДокументаВыплаты ();
        chcnrgst.rec.RgstRestAfterLastOpert = ЧтениеСуммы ( 9);
        chcnrgst.rec.RgstRest200691         = ЧтениеСуммы (11);
        chcnrgst.rec.RgstRest010192         = ЧтениеСуммы (12);
        chcnrgst.rec.RgstRest010793         = ЧтениеСуммы (13);
        chcnrgst.rec.RgstRest010195         = ЧтениеСуммы (14);
        st = chcnrgst.Insert();
        if (st)
          NumW = NumW + 1;
          СтрокаВОтчет ();
        else
          ErrorMes ("Вставка в реестр: "+ChcnDepart,"/",ChcnBranch+": Acc",true);
        end;
        NumAccBreak = "";
      end;
      NumCurRow = NumCurRow + 1;
    else
      cont = false;
    end;
  end;
  return st;
end;
// *********************************************************

macro ОбработкаИсходногоФайла
//
// Начало обработки исходного файла
//
  var st;
  var str;
  var Num = 1;
// Открытие Exel-файла и определение имен рабочих областей
  st = xl.workbooks.open (NameSourceFile);
  if (st)
    NameWorkBook = NameSourceFile;
    while (Num != 0)
      Num = Index(NameWorkBook,"\\");
      if (Num != 0)
        NameWorkBook = SubStr (NameWorkBook,Num+1);
      end;
    end;
  else
    ErrorMes ("Открытие исходного файла",false);
  end;
  if (st)
    NumRows = ЧислоСтрокВТаблице ();
    while (st AND (NOT ДостигнутКонецТаблицы()))
      st = ЧтениеНомераПодразделения ();
      if (st)
        if ( ((ChcnDepartBreak == "") OR (ChcnDepartBreak == ChcnDepart))
         AND ((ChcnBranchBreak == "") OR (ChcnBranchBreak == ChCnBranch)) )
          WriteToBreakFile (RgtpID,ChcnDepart,ChcnBranch,"",NameSourceFile);
          st = ОбработкаПодразделения ();
          ChcnDepartBreak = "";
          ChcnBranchBreak = "";
        else
          st = ПропускСтрокСДанными ();
        end;
      end;
    end;
    xl.workbooks(NameWorkBook).close ();
  end;
  return st;
end;
// *********************************************************

macro СправочникРеестров
//
// Обработка справочника реестров
//
  var st = false;
  var cont = true;
  var UserID = "";

  if (RgtpID == 0)
    while (cont)
//    1. Ввод пользовательского вида реестра
      GetString (UserID,"Введите вид принимаемого реестра",9);
      if (UserID == "")
        MsgBox ("Вы отказались от ввода реестра");
        cont = false;
      else
        chcnrgtp.KeyNum = 1;
        chcnrgtp.rec.UserID = UserID;
        if (chcnrgtp.GetEQ())
          if (GetTrue (True,"Вы выбрали существующий вид реестра?"))
            RgtpID = chcnrgtp.rec.RgtpID;
            st = true;
            cont = false;
          end;
        else
          chcnrgtp.KeyNum = 0;
          chcnrgtp.rec.RgtpID = 1000000000;
          if (chcnrgtp.GetLT())
            RgtpID = chcnrgtp.rec.RgtpID + 1;
          else
            RgtpID = 1;
          end;
          chcnrgtp.Clear();
          chcnrgtp.rec.RgtpID  = RgtpID;
          chcnrgtp.rec.UserID  = UserID;
          chcnrgtp.rec.RcvDate = {curdate};
          chcnrgtp.rec.RcvOper = {oper};
          cont = false;
          st = chcnrgtp.Insert();
          if (NOT st)
            ErrorMes ("Ввод вида реестра",true);
          end;
        end;
      end;
    end;
  else
    chcnrgtp.KeyNum = 0;
    chcnrgtp.rec.RgtpID = RgtpID;
    st = chcnrgtp.GetEQ();
    if (NOT st)
      ErrorMes ("Поиск вида реестра с кодом "+String(RgtpID),true);
    end;
  end;
  if (st)
//  2. Определение последнего номера реестра выбранного вида в базе данных
    chcnrgst.rec.RgtpID = RgtpID;
    chcnrgst.rec.RgstID = 1000000000;
    if (chcnrgst.GetLE() 
     AND (chcnrgst.rec.RgtpID == RgtpID))
      LastRgstID = chcnrgst.rec.RgstID;
    else
      LastRgstID = 0;
    end;
  end;
  return st;
end;
// *********************************************************

macro ВыборИсходногоФайла
//
// Выбор исходного файла (*.xls)
//
  var st = true;

  if (NameSourceFile == "")
    st = SelectFile(NameSourceFile,PatternSourceFile,"Выберите исходный файл реестра");
    if (NOT st)
      MsgBox ("Вы отказались от ввода реестра");
    end;
  end;
  return st;
end;
// *********************************************************
// ##### Точка входа #####
  var st = true;
// 1. Контрольная точка
  st = ReadFromBreakFile (RgtpID,ChcnDepartBreak,ChcnBranchBreak,
                          NumAccBreak,NameSourceFile);
// 2. Выбор исходного файла
  if (st)
    st = ВыборИсходногоФайла ();
  end;
// 3. Ввод записи в справочник реестров или выбор типа реестра
  if (st)
    st = СправочникРеестров();
    if (st)
      WriteToBreakFile (RgtpID,"","","",NameSourceFile);
      HeadLog ();
    end;
  end;
// 4. Прием данных и их занесение в файл реестра
  if (st)
    ОбработкаИсходногоФайла ();
    FinishLog ();
    WriteToBreakFile (0,"","","","");
  end;
end;
/* ----- Конец файла ----- */
