
/* Расчет цены продажи для ОРВВЗ */

file CIMisc( "ci_misc.dbt" ) key 0;

/* Массивы периодов. Заполняются ручками. 
   Начало и конец периода включаются.
   Год не учитывается и должен быть равным 1900 */

array
  StartDate, EndDate, Marge;

StartDate(0)=Date(01,01,1900); EndDate(0)=Date(31,05,1900); Marge(0)=$0.30;
StartDate(1)=Date(01,06,1900); EndDate(1)=Date(31,12,1900); Marge(1)=$0.1;

const
  O_LOTTERY_SELL = 1;

var
  Price = $0;

macro FindCIMisc( Type, Issue )

  CIMisc.Type = Type;
  CIMisc.IssueID = Issue;
  return GetEQ( CIMisc );
end;

macro MargeForDate( Dd )

  var
    Dd1, Day, Mon, Year, i;

  DateSplit( Dd, Day, Mon, Year );
  Dd1 = Date( Day, Mon, 1900 );
  i = 0;
  while ( i < ASize( StartDate ) )
    if ( ( Dd1 >= StartDate( i ) ) and ( Dd1 <= EndDate( i ) ) )
      return Marge( i );
    end;
    i = i + 1;
  end;
  return $0;
end;

macro CalcPrice( Type, Issue, Operation, SubOp, Dd )

  if ( FindCIMisc( Type, Issue ) )
    Price = CIMisc.Price;
    if ( Operation == O_LOTTERY_SELL )
      Price = Price + MargeForDate( Dd );
    end;
  end;
end;