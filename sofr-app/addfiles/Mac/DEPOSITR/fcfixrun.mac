import deprintr,alg_vars;
file dd(sbdepdoc) key 6 write;
private var depclnt_tmp = TClientList;
var TrnStatus=true;
macro TrnProc
record OpParm("op_parm.rec");
record PayDoc("pay_doc.np");
var
  NumDayDoc=100;
  dd.Referenc=ALG_SBDEPDOC.Referenc;
  dd.Date_Document=ALG_SBDEPDOC.Date_Document;
  dd.NumDayDoc=32000;
  TrnStatus=GetLE(dd);
  if(TrnStatus)
    if(dd.Date_Document==ALG_SBDEPDOC.Date_Document)
      NumDayDoc=dd.NumDayDoc+100;
    end;
    Copy(dd,ALG_SBDEPDOC);
    dd.ApplicationKey=FormApplicationKey(dd.iApplicationKind);
    dd.NumDayDoc=NumDayDoc;
    NumDayDoc=NumDayDoc+100;
    if(ALG_SBDEPDOC.TypeOper==49)
      dd.InSum=ALG_SBDEPDOC.InSum;
      dd.OutSum=0;
    else
      dd.OutSum=ALG_SBDEPDOC.OutSum;
      dd.InSum=0;
    end;
    dd.Rest=0;
    TrnStatus=Insert(dd);
    if(TrnStatus and (not InterDesk_IsDocBunchActive))
      ClearRecord(OpParm);
      OpParm.Date={curdate};
      OpParm.Operation=ALG_SBDEPDOC.TypeOper;
      OpParm.ObjectRef=ALG_DEPOSITOR.Referenc;
      TrnStatus=InterDesk_InitDocBunch(dd.iApplicationKind,dd.ApplicationKey,OpParm);
    end; 
    if(TrnStatus)
      TrnStatus=CashLink_AddRec(dd.iApplicationKind,dd.ApplicationKey);
    end;
    if(TrnStatus)
      ClearRecord(PayDoc);
      PayDoc.FNCash=ALG_SBDEPDOC.FNCash;
      PayDoc.IsCur=ALG_SBDEPDOC.IsCur;
      PayDoc.NumOpert=90;
      if(ALG_SBDEPDOC.TypeOper==49)
        PayDoc.GroupOpert=11;
      else
        PayDoc.GroupOpert=41;
      end;
      PayDoc.ApplType=ALG_SBDEPDOC.ApplType;
      PayDoc.InSum=ALG_SBDEPDOC.InSum;
      PayDoc.Date_Document={curdate};
      PayDoc.Oper={oper};
      if (depclnt_tmp.GetRecord (ALG_SBDEPDOC.CodClient))
        PayDoc.ClientLastName   = depclnt_tmp.CurRec.rec.Name1;
        PayDoc.ClientFirstName  = depclnt_tmp.CurRec.rec.Name2;
        PayDoc.ClientSecondName = depclnt_tmp.CurRec.rec.Name3;
        PayDoc.FlagRez          = depclnt_tmp.CurRec.rec.NotResident;
        depclnt_tmp.CurRec.GetPersnCard();
        PayDoc.SocialNumber     = depclnt_tmp.CurRec.rec.SocialNumber;
        PayDoc.PersIDType       = depclnt_tmp.CurRec.rec.PaperKind;
        PayDoc.PassportSer      = depclnt_tmp.CurRec.rec.PaperSeries;
        PayDoc.PasportNumb      = depclnt_tmp.CurRec.rec.PaperNumber;
        PayDoc.CodClient        = depclnt_tmp.CurRec.rec.CodClient;
      end;
      PayDoc.iApplicationKind=200;
      PayDoc.ApplicationKey=FormApplicationKey(PayDoc.iApplicationKind);
      if(ALG_SBDEPDOC.TypeOper==49)
        PayDoc.Ground="Возмещ.переплата вклада по сч.№ "+Acc_Form(ALG_SBDEPDOC.Account)+
        " на имя "+ALG_COMDEPCLNT. ConvertFIO();
      else
        PayDoc.Ground="Выдана недоплата вклада по сч.№ "+Acc_Form(ALG_SBDEPDOC.Account)+
        " на имя "+ALG_COMDEPCLNT.ConvertFIO();
      end;
      PayDoc.Kind=ALG_SBDEPDOC.Type_Account;
      TrnStatus=InsertPayDocToGDDList(PayDoc);
      if(TrnStatus)
        TrnStatus=CarryGDDList;
      end;
    end;
  end;
end;
/* entry point */
  SetOutput("NUL");
  if(((ALG_SBDEPDOC.TypeOper==49) and (ALG_SBDEPDOC.ApplType==2) or (ALG_SBDEPDOC.ApplType==3)))
    ;
  elif((ALG_SBDEPDOC.TypeOper==48) and (ALG_SBDEPDOC.ApplType==2))
    ;
  else
    ALG_RESULT=OK_RES;
    return;
  end;
  if(TrnStatus)
    ProcessTrn(1,"TrnProc",dd);
  end;
  if(TrnStatus)
    ALG_RESULT=END_RES;
  else
    ALG_RESULT=ERR_RES;
  end;
end;