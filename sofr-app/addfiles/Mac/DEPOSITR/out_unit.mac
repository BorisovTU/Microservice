
/* Выдача объединенного вклада (Оперчасть) */

import deprintr, DeskInter, OperCode;

record ALG_SBDEPDOC("sbdepdoc.1");
record ALG_DEPOSITOR("depositr.dbt");
file Dep("depositr.dbt") key 7 write;
file DepType("sb_dtyp.dbt") key 2;

/*---------- Коды возврата макроса --------------------------------------*/
const
  OK_RES   =  0,  /* продолжать проводку */
  SKIP_RES =  1,  /* пропустить выполнение предопределенного модуля */
  END_RES  =  2,  /* завершить проводку */
  ERR_RES  = -1;  /* аваpийно завершить проводку */

var
  Stat = true,
  {curdate},
  {oper},
  Op,
  PercSum;

const
  Date1 = Date(1, 7, 1936),
  Date2 = Date(1, 1, 1955),
  Date3 = Date(1, 1, 1984);

macro FindDepType

  DepType.FlagCur = ALG_DEPOSITOR.IsCur;
  DepType.Kind = ALG_DEPOSITOR.PrevAccount;
  if(not GetEQ(DepType))
    MsgBox("Вид вклада не найден: ", ALG_DEPOSITOR.PrevAccount);
    Exit(1);
  end;
end;

macro DateMax(a, b)

  if(a > b)
    return a;
  else
    return b;
  end;
end;

macro SelectRate(dem_rate, term_rate)

  if(DepType.FormContr == 0)
    return dem_rate;
  else
    return term_rate;
  end;
end;

macro FindPrimaryAcc

  Dep.FNCash = ALG_DEPOSITOR.FNCash;
  Dep.Account = ALG_SBDEPDOC.Account_Receiver;
  return GetEQ(Dep);
end;

macro GetPercSum;

  record UnitInfo(UnitInfo) dialog;

  var
    TmpDate,
    Sum = 0;

  FindDepType;
  if(ALG_SBDEPDOC.Account_Receiver != "")
    if(not FindPrimaryAcc)
      MsgBox("Счет не найден: ", ALG_SBDEPDOC.Account_Receiver);
      Exit(1);
    end;
  else
    ClearRecord(UnitInfo);
    RunDialog(UnitInfo);
    if((UnitInfo.LatestOpDate != Date(0, 0, 0)) and (UnitInfo.Rest != 0))
      Dep.Final_Date = UnitInfo.LatestOpDate;
      ALG_SBDEPDOC.OutSum = Dep.StaticRest = Money(UnitInfo.Rest);
    else
      Exit(0);
    end;
  end;
  if(Dep.Final_Date < Date1)
    Sum = Sum + Money((Date1 - Dep.Final_Date) / 365.0 *
      SelectRate(8, 5) * Dep.StaticRest / 100.0);
  end;
  if(Dep.Final_Date < Date2)
    TmpDate = DateMax(Dep.Final_Date, Date1);
    Sum = Sum + Money((Date2 - TmpDate) / 365.0 *
      SelectRate(3, 5) * Dep.StaticRest / 100.0);
  end;
  if(Dep.Final_Date < Date3)
    TmpDate = DateMax(Dep.Final_Date, Date2);
    Sum = Sum + Money((Date3 - TmpDate) / 365.0 *
      SelectRate(2, 3) * Dep.StaticRest / 100.0);
  end;
  return Sum;
end;

macro T_OutUnited

  record tmpDoc("sbdepdoc.dbt");

  if(PercSum > 0)
    ClearRecord(tmpDoc);
    tmpDoc.Referenc = ALG_DEPOSITOR.Referenc;
    tmpDoc.IsCur=ALG_DEPOSITOR.IsCur;
    tmpDoc.FNCash = ALG_DEPOSITOR.FNCash;
    tmpDoc.Account = ALG_DEPOSITOR.Account;
    tmpDoc.Oper = {oper};
    tmpDoc.Type_Account = ALG_DEPOSITOR.Type_Account;
    tmpDoc.Code_Currency = ALG_DEPOSITOR.Code_Currency;
    tmpDoc.CodClient = ALG_DEPOSITOR.CodClient;
    tmpDoc.YesSbook = "X";
    tmpDoc.Date_Document = {curdate};
    tmpDoc.DepDate_Document = {curdate};
    tmpDoc.InSum = PercSum;
    tmpDoc.Ground = "Выплата объединенного вклада " +
      ALG_SBDEPDOC.Account_Receiver +
      " причисление %%";
    Stat = not Выполнить_Операцию(ALG_DEPOSITOR.Account,
      ALG_DEPOSITOR.Type_Account, ALG_DEPOSITOR.Code_Currency,
      Зачисление_По_Данным_ПК, tmpDoc, 0);
  end;
  if(Stat)
    ClearRecord(tmpDoc);
    tmpDoc.Referenc = ALG_DEPOSITOR.Referenc;
    tmpDoc.IsCur=ALG_DEPOSITOR.IsCur;
    tmpDoc.FNCash = ALG_DEPOSITOR.FNCash;
    tmpDoc.Account = ALG_DEPOSITOR.Account;
    tmpDoc.Oper = {oper};
    tmpDoc.Type_Account = ALG_DEPOSITOR.Type_Account;
    tmpDoc.Code_Currency = ALG_DEPOSITOR.Code_Currency;
    tmpDoc.CodClient = ALG_DEPOSITOR.CodClient;
    tmpDoc.YesSbook = "X";
    tmpDoc.Date_Document = {curdate};
    tmpDoc.DepDate_Document = {curdate};
    tmpDoc.OutSum = ALG_SBDEPDOC.OutSum + PercSum;
    tmpDoc.Ground = "Выплата объединенного вклада " +
      ALG_SBDEPDOC.Account_Receiver + " ";
    tmpDoc.IsControl = StrFor(1); /* Признак главного документа */
    Stat = not Выполнить_Операцию(ALG_DEPOSITOR.Account,
      ALG_DEPOSITOR.Type_Account, ALG_DEPOSITOR.Code_Currency, Op,
      tmpDoc, 0);
  end;
  if(Stat and (ALG_SBDEPDOC.Account_Receiver != ""))
    Stat = FindPrimaryAcc;
    if(Stat)
      Dep.StaticPaid = "X";
      Stat = Update(Dep);
    end;
  end;
  InterDesk_EndDocBunch;
  if(not Stat)
    AbortTrn;
  end;
end;

  var
    Choice;

  Op = ALG_SBDEPDOC.TypeOper;

/*
  GetMoney(PercSum, "Введите сумму %%");
  if(ValType(PercSum) == V_UNDEF)
    ALG_RESULT = END_RES;
    Exit;
  end;
*/
  ALG_RESULT = ERR_RES;
  PercSum = GetPercSum;
  GetMoney(PercSum, "Сумма причисляемых процентов");
  if(ValType(PercSum) == V_UNDEF)
    Exit;
  end;
  ProcessTrn(1, "T_OutUnited", Dep);
  if(Stat)
    ALG_RESULT = END_RES;
  else
    ALG_RESULT = ERR_RES;
  end;
end;