/*
  RS-Retail 6.0
  RSD
  Обслуживаний физических лиц
  Вклады бывшего Чеченского Сбербанка
  Отчет "Ведомость выплаченных сумм, причитающихся вкладчикам бывшего Чеченского банка Сбербанка России"
  Отчет "Отчет о произведенных выплатах вкладчикам бывшего Чеченского банка Сбербанка России" (за текущую дату)

  Модуль № 3044 (chcn_psm.mac)

  ТЗ_997_ВЧБ  Приложение 1, Приложение 2

  Ромодин Александр Васильевич
  05.02.2004
*/
Import DeprIntr, RSD, sqlconv;

var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

var {BatchMode};  /* Признак выполнения в пакетном режиме */
var {curdate};                    
var {oper};
var BeginDate = {curdate};
var EndDate   = {curdate};
var branch    = NumFNCash();

var DepClient = TClientList;

// Выходные файлы
file outf()  txt 300;        // Выходной файл отчета
file outf1() txt 300 write;  // Первая часть отчета
file outf2() txt 300 write;  // Вторая часть отчета
var  txtDir    = GetRegVal("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR",true);
var  NameOut  = txtDir + "chcn_psm."+String(branch); 
var  NameOut1 = txtDir + "chcn1psm."+String(branch); 
var  NameOut2 = txtDir + "chcn2psm."+String(branch); 

// #########################################################
var NumRec = 0;
// #########################################################

var chcnDepart;
var chcnBranch;
var Account;
var SpecialAccount;
var DateLastOperation;
var RestAfterLastOperation;
var Rest200691;
var Rest010192;
var Percent;
var Tax;
var Compens1;
var Compens2;
var Compens3x;
var RgstAccount;
var RgstName1;
var RgstName2;
var RgstName3;
var drCodClient;
var OutSum;
var docCodClient;
var DatePay;
var FIO;
var FIOtrast;
var Born;
var PaperSeries;
var PaperNumber;
var PaperIssuer;
var PartRest;
var PartCompens1;
var PartCompens2;
var PartCompens3x;
var PartPercent;
var PartTax;
var payDepart;
var payBranch;
// Итого:
var AllRestAfterLastOperation = 0;
var AllRest200691             = 0;
var AllRest010192             = 0;
var AllOutSum                 = 0;
var AllPercent                = 0;
var AllTax                    = 0;
var AllCompens1               = 0;
var AllCompens2               = 0;
var AllCompens3x              = 0;

// #########################################################

var Title1 = "+----+----+-----+-------------------------+------------------------+----------+------------------+--------------------+------------------------+----------+----------------+----------------+----------------+";
var Title2 = "+----+-------------------------+----------------+----------------+------------+------------+------------+------------+------------+----+-----+-------------------------+------------------------+";
var VR = " "; // Вертикальный разделитель
var VG = "|"; // Вертикальная граница таблицы

// #########################################################

macro ИмяТекущегоФилиала
  var namebranch = "";

  var i_dp_dep = TRecHandler( "i_dp_dep.rec" );

  if ( FindDepartment( branch, NULL, i_dp_dep ) )
    nameBranch = i_dp_dep.rec.Name;
  end;

  return namebranch;
end;
// ---------------------------------------------------------

macro Заголовок
  var str1,str2,str3,str4,str5,str6;
  var namebranch = "";
  if (BeginDate == EndDate)
    str1 = "       Отчет о произведенных выплатах бывшего Чеченского банка Сбербанка России";
    str2 = "                                     за "+String(EndDate);
    str3 = "";
  else
    str1 = "                                          ВЕДОМОСТЬ";
    str2 = "       выплаченных сумм, причитающихся вкладчикам бывшего Чеченского банка Сбербанка России";
    str3 = "                              за период "+String(BeginDate)+" - "+String(EndDate);
  end;
  namebranch = ИмяТекущегоФилиала ();
  str4 = "    Подразделение "+namebranch;
  Insert (outf1,str1);
  Insert (outf1,str2);
  Insert (outf1,str3);
  Insert (outf1,str4);     
  Insert (outf1,"");
// Шапки таблиц 
// Первая таблица
  str1 = "|# пп|Номер подр|       Номер счета       |      ФИО вкладчика     |   Дата   |              Данные документа,        |    ФИО представителя   |Дата посл.|        Остаток вклада неденоминированный         |";
  str2 = "|    |в ЧСБ     |     по предъявленной    |                        | рождения |         удостоверяющего личность      | (наследника) вкладчика |операции  +----------------+----------------+----------------+";
  str3 = "|    +----+-----+        сберкнижке       |                        |          +------------------+--------------------+                        |по с/к    | На дату посл.  | На 20.06.1991  |  На 01.01.1992 |";
  str4 = "|    |Отд |Подр |                         |                        |          |  Серия, номер    | Кем и когда выдан  |                        |уточненная|операции (уточн)| (уточненный)   |  (уточненный)  |";
  str5 = "|  1 | 2  |  3  |             4           |            5           |     6    |        7         |        8           |            9           |    10    |       11       |       12       |       13       |";
  Insert (outf1,Title1);
  Insert (outf1,str1);
  Insert (outf1,str2);
  Insert (outf1,str3);
  Insert (outf1,str4);     
  Insert (outf1,Title1);
  Insert (outf1,str5);
  Insert (outf1,Title1);
// Вторая таблица
  str1 = "|# пп|       Номер счета       |  Выплаченная   |                              В том числе                                        |Номер подр|        Примечание (при несовпадении данных)      |";
  str2 = "|    |     по предъявленной    |     сумма      +----------------+-------------------------+------------+------------+------------+выплаты   +-------------------------+------------------------+";
  str3 = "|    |        сберкнижке       |                | Остаток вклада | Сумма выплаченных проц. |Сумма выплач| Доп.выпл.  |Сумма выплач+----+-----+ Номер счета по реестру  |     ФИО вкладчика      |";
  str4 = "|    |                         |                |                +------------+------------+предварит.  | предварит. |3-х кратной |Отд |Подр |                         |     по реестру         |";
  str5 = "|    |                         |                |                |Начислен. % |Удерж.налог |компенсации | компенсация|компенсации |    |     |                         |     (или сообщение)    |";
  str6 = "|  1 |          4              |       14       |       15       |      16    |     17     |     18     |     19     |     20     | 21 | 22  |           23            |          24            |";
  Insert (outf2,"");
  Insert (outf2,Title2);
  Insert (outf2,str1);
  Insert (outf2,str2);
  Insert (outf2,str3);
  Insert (outf2,str4);     
  Insert (outf2,str5);     
  Insert (outf2,Title2);
  Insert (outf2,str6);     
  Insert (outf2,Title2);
end;
// *********************************************************

macro ДоДлиныСправа (str,len)
  var l = StrLen(str);
  var retstr = str;
  while (l < len)
    retstr = retstr + " ";
    l = l + 1;
  end;
  return retstr;
end;
macro ДоДлиныСлева (str,len)
  var l = StrLen(str);
  var retstr = str;
  while (l < len)
    retstr = " " + retstr;
    l = l + 1;
  end;
  return retstr;
end;
// *********************************************************

Array aFIO,
      aPaperNumber,
      aPaperIssuer,
      aFIOtrast,
      aRgstName;
var   sFIO, 
      sPaperNumber,
      sPaperIssuer,
      sFIOtrast,   
      sRgstName;   
var   mFIO         = 24,
      mPaperNumber = 18,
      mPaperIssuer = 20,
      mFIOtrast    = 24,
      mRgstName    = 24;   
// ---------------------------------------------------------

macro РазбиениеПолей
// Разбивает поля, не помещающиеся в одну строку, на несколько строк
  sFIO         = 0;
  sPaperNumber = 0;
  sPaperIssuer = 0;
  sFIOtrast    = 0;
  sRgstName    = 0;

  StrSplit (FIO,aFIO,mFIO);
  sFIO = aSize(aFIO);
  StrSplit (PaperSeries + "  № " + PaperNumber,aPaperNumber,mPaperNumber);
  sPaperNumber = aSize(aPaperNumber);
  StrSplit (PaperIssuer,aPaperIssuer,mPaperIssuer);
  sPaperIssuer = aSize(aPaperIssuer);
  StrSplit (FIOtrast,aFIOtrast,mFIOtrast);
  sFIOtrast = aSize(aFIOtrast);
  StrSplit (RgstName1+" "+RgstName2+" "+RgstName3,aRgstName,mRgstName);
  sRgstName = aSize(aRgstName);
end;
// ---------------------------------------------------------

macro GetMax (i1,i2,i3,i4,i5)
  var rmax = 0;
  if (rmax < i1)
    rmax = i1;
  end;
  if (rmax < i2)
    rmax = i2;
  end;
  if (rmax < i3)
    rmax = i3;
  end;
  if (rmax < i4)
    rmax = i4;
  end;
  if (rmax < i5)
    rmax = i5;
  end;
  return rmax;
end;
// ---------------------------------------------------------

macro ДобавитьЭлементы (arr,marr)
  var sarr = aSize(arr),
      i;
  i = sarr;
  while (i < marr)
    arr(i) = "";
    i = i + 1;
  end;
  return arr;
end;
// ---------------------------------------------------------

macro ВставкаДопСтрок
// Для полей, не поместившихся в одну строку, 
// вставляет дополнительные строки
  var NumStr = GetMax(sFIO,sPaperNumber,sPaperIssuer,sFIOtrast,sRgstName);
  var str1, str2;
  var i = 1;
  ДобавитьЭлементы (aFIO        , NumStr);
  ДобавитьЭлементы (aPaperNumber, NumStr);
  ДобавитьЭлементы (aPaperIssuer, NumStr);
  ДобавитьЭлементы (aFIOtrast   , NumStr);
  while (i < NumStr)
    str1 = VG                                          +
           ДоДлиныСлева ("",4)                         + VR +
           ДоДлиныСправа("",4)                         + VR +
           ДоДлиныСправа("",5)                         + VR +
           ДоДлиныСправа("",25)                        + VR +
           ДоДлиныСправа(aFIO(i)        ,mFIO)         + VR +
           ДоДлиныСправа("",10)                        + VR +
           ДоДлиныСправа(aPaperNumber(i),mPaperNumber) + VR +
           ДоДлиныСправа(aPaperIssuer(i),mPaperIssuer) + VR +
           ДоДлиныСправа(aFIOtrast(i)   ,mFIOtrast   ) + VR +
           ДоДлиныСправа("",10)                        + VR +
           ДоДлиныСлева ("",16)                        + VR +
           ДоДлиныСлева ("",16)                        + VR +
           ДоДлиныСлева ("",16)                        + VG;
    Insert (outf1,str1);
    i = i + 1;
  end;
  i = 1;
  while (i < sRgstName)
    str2 = VG                                     +
           ДоДлиныСлева ("",4)                    + VR +
           ДоДлиныСправа("",25)                   + VR +
           ДоДлиныСлева ("",16)                   + VR +
           ДоДлиныСлева ("",16)                   + VR +
           ДоДлиныСлева ("",12)                   + VR +
           ДоДлиныСлева ("",12)                   + VR +
           ДоДлиныСлева ("",12)                   + VR +
           ДоДлиныСлева ("",12)                   + VR +
           ДоДлиныСлева ("",12)                   + VR +
           ДоДлиныСправа("",4)                    + VR +
           ДоДлиныСправа("",5)                    + VR +
           ДоДлиныСлева ("",25)                   + VR +
           ДоДлиныСправа(aRgstName(i), mRgstName) + VG;
    Insert (outf2,str2);
    i = i + 1;
  end;
end;
// ---------------------------------------------------------

macro ОбнулениеМассивов
  aSize (aFIO        , 0);
  aSize (aPaperNumber, 0);
  aSize (aPaperIssuer, 0);
  aSize (aFIOtrast   , 0);
  aSize (aRgstName   , 0);
end;
// ---------------------------------------------------------

macro ПерваяСтрока (arr,sarr,len)
  var str;
  if (sarr > 0)
    str = ДоДлиныСправа (arr(0),len);
  else
    str = ДоДлиныСправа ("",len);
  end;
  return str;  
end;
// ---------------------------------------------------------

macro СтрокаОтчета
  var str1, str2;

  РазбиениеПолей ();
  str1 = VG                                                    +
         ДоДлиныСлева (String(NumRec),4)                       + VR +
         ДоДлиныСправа(String(chcnDepart),4)                   + VR +
         ДоДлиныСправа(String(chcnBranch),5)                   + VR +
         ДоДлиныСправа(String(Acc_Form(Account)),25)           + VR +
         ПерваяСтрока (aFIO,sFIO,mFIO)                         + VR +
         ДоДлиныСправа(String(Born),10)                        + VR +
         ПерваяСтрока (aPaperNumber,sPaperNumber,mPaperNumber) + VR +
         ПерваяСтрока (aPaperIssuer,sPaperIssuer,mPaperIssuer) + VR +
         ПерваяСтрока (aFIOtrast,   sFIOtrast,   mFIOtrast)    + VR +
         ДоДлиныСправа(String(DateLastOperation),10)           + VR +
         ДоДлиныСлева (String(RestAfterLastOperation),16)      + VR +
         ДоДлиныСлева (String(Rest200691),16)                  + VR +
         ДоДлиныСлева (String(Rest010192),16)                  + VG;
  str2 = VG                                               +
         ДоДлиныСлева (String(NumRec),4)                  + VR +
         ДоДлиныСправа(String(Acc_Form(Account)),25)      + VR +
         ДоДлиныСлева (String(OutSum),16)                 + VR +
         ДоДлиныСлева (String(RestAfterLastOperation),16) + VR +
         ДоДлиныСлева (String(PartPercent),12)            + VR +
         ДоДлиныСлева (String(PartTax),12)                + VR +
         ДоДлиныСлева (String(PartCompens1),12)           + VR +
         ДоДлиныСлева (String(PartCompens2),12)           + VR +
         ДоДлиныСлева (String(PartCompens3x),12)          + VR +
         ДоДлиныСправа(String(payDepart),4)               + VR +
         ДоДлиныСправа(String(payBranch),5)               + VR +
         ДоДлиныСлева (String(Acc_Form(RgstAccount)),25)  + VR +
         ПерваяСтрока (aRgstName,sRgstName,mRgstName)     + VG;
  Insert (outf1,str1);
  Insert (outf2,str2);
  ВставкаДопСтрок ();
  ОбнулениеМассивов ();
  AllRestAfterLastOperation = AllRestAfterLastOperation + RestAfterLastOperation;
  AllRest200691             = AllRest200691             + Rest200691;
  AllRest010192             = AllRest010192             + Rest010192;
  AllOutSum                 = AllOutSum                 + OutSum;
  AllPercent                = AllPercent                + PartPercent;
  AllTax                    = AllTax                    + PartTax;
  AllCompens1               = AllCompens1               + PartCompens1;
  AllCompens2               = AllCompens2               + PartCompens2;
  AllCompens3x              = AllCompens3x              + PartCompens3x;
end;
// *********************************************************

macro ФИОоперациониста
  var NameOper = "";
  var Cmd = RsdCommand ("select t_name from dperson_dbt where t_oper = ?");
  var Rs;

  Cmd.AddParam("t_oper", RSDBP_IN,{oper});
  Rs  = RsdRecordSet (Cmd);
  if (Rs.MoveNext)
    NameOper = Rs.Value("t_name");
  end;
  return NameOper;
end;
// ---------------------------------------------------------

macro Итого
  var str1 = "", str2 = "", 
      str3;
  var NameOper = "";
  str1 = VG + ДоДлиныСправа("",143) + "  Итого:  " + VR +
          ДоДлиныСлева (String(AllRestAfterLastOperation),16) + VR +
          ДоДлиныСлева (String(AllRest200691),16)             + VR +
          ДоДлиныСлева (String(AllRest010192),16)             + VG;
  str2 = VG + ДоДлиныСправа("",4)                             + VR +
         "                  Итого: "                    + VR +
         ДоДлиныСлева (String(AllOutSum),16)                 + VR +
         ДоДлиныСлева (String(AllRestAfterLastOperation),16) + VR +
         ДоДлиныСлева (String(AllPercent),12)                + VR +
         ДоДлиныСлева (String(AllTax),12)                    + VR +
         ДоДлиныСлева (String(AllCompens1),12)               + VR +
         ДоДлиныСлева (String(AllCompens2),12)               + VR +
         ДоДлиныСлева (String(AllCompens3x),12)              + VR +
         ДоДлиныСправа("",4)                                 + VR +
         ДоДлиныСправа("",5)                                 + VR +
         ДоДлиныСправа("",25)                                + VR +
         ДоДлиныСправа("",24)                                + VG;

  Insert (outf1,Title1);
  Insert (outf1,str1);
  Insert (outf1,Title1);
  Insert (outf1," ");

  Insert (outf2,Title2);
  Insert (outf2,str2);
  Insert (outf2,Title2);
  Insert (outf2," ");

  NameOper = ФИОоперациониста ();
  str3 = "         ____________________          "+NameOper;
  Insert (outf2,str3);
end;

// #########################################################

macro ПоискКлиента
  var stat = true;

  FIO         = "";
  FIOtrast    = "";
  Born        = Date(0,0,0);
  PaperSeries = "";
  PaperNumber = "";
  PaperIssuer = "";
  stat = DepClient.GetRecord (drCodClient);
  if (stat)
    FIO = DepClient.CurRec.ConvertFIOFull();
    Born        = DepClient.CurRec.rec.Born;
    PaperSeries = DepClient.CurRec.rec.PaperSeries;
    PaperNumber = DepClient.CurRec.rec.PaperNumber;
    PaperIssuer = DepClient.CurRec.rec.PaperIssuer;
    if ( (docCodClient != 0)
     AND (docCodClient != drCodClient))
      stat = DepClient.GetRecord (docCodClient);
      if (stat)
        FIOtrast = DepClient.CurRec.ConvertFIOFull();
      end;
    end;
  end;
  return stat;
end;
// #########################################################

macro ПоправитьДату (dd)
  var d1 = String(dd);
  var d = "";
  var i = 0;
  while (i < 10)
    i = i + 1;
    d = d + SubStr(d1,i,1);
  end;
  return d;
end;
// ---------------------------------------------------------

macro ОбработкаВыплаты (Rs:RsdRecordSet)

  chcnDepart              = rslString( Rs.Value("chcnDepart") );
  chcnBranch              = rslString( Rs.Value("chcnBranch") );
  Account                 = rslString( Rs.Value("Account") );
  SpecialAccount          = rslString( Rs.Value("SpecialAccess") );
  DateLastOperation       = ПоправитьДату(Rs.Value("DateLastOperation"));
  RestAfterLastOperation  = Rs.Value("RestAfterLastOperation");
  Rest200691              = Rs.Value("Rest200691");
  Rest010192              = Rs.Value("Rest010192");
  Percent                 = Rs.Value("Percent");
  Tax                     = Rs.Value("Tax");
  Compens1                = Rs.Value("Compens1");
  Compens2                = Rs.Value("Compens2");
  Compens3x               = Rs.Value("Compens3x");
  RgstAccount             = rslString( Rs.Value("RgstAccount") );
  RgstName1               = rslString( Rs.Value("RgstName1") );
  RgstName2               = rslString( Rs.Value("RgstName2") );
  RgstName3               = rslString( Rs.Value("RgstName3") );
  drCodClient             = Rs.Value("drCodClient");
  OutSum                  = Rs.Value("OutSum");
  docCodClient            = Rs.Value("docCodClient");
  DatePay                 = Rs.Value("DatePay");
  PartRest                = Rs.Value("PartRest");
  PartCompens1            = Rs.Value("PartCompens1");
  PartCompens2            = Rs.Value("PartCompens2");
  PartCompens3x           = Rs.Value("PartCompens3x");
  PartPercent             = Rs.Value("PartPercent");
  PartTax                 = Rs.Value("PartTax");
  payDepart               = rslString( Rs.Value("nameBranch") );
  payBranch               = "";

  if ( Index( payDepart ,"/" ) > 0 )
    payBranch = SubStr( payDepart, Index( payDepart ,"/" ) + 1 );
    payDepart = SubStr( payDepart, 1, Index( payDepart ,"/" ) - 1 );
  end;

  if (Account == "")
    Account = RgstAccount;
  end;
  if (ПоискКлиента())
    if(  (not SpecialAccess) and OperMayListSpecialAccounts and SpecialAccount )
       FIO = "XXXXXX X.X.";
       Born = "XXXXXX";
       PaperSeries = "XXXXXX";
       PaperNumber = "XXXXXX";
       PaperIssuer = "XXXXXX";
    end;
    СтрокаОтчета ();
  else
    println ("Не найдены вкладчики для "+RgstName1+" "+RgstName2+" "+RgstName3);
  end;
end;
// *********************************************************

macro Выполнение
  var Cmd = RsdCommand (
               "select rgst.t_chcnDepart"         +" chcnDepart,"
                      "rgst.t_chcnBranch"         +" chcnBranch,"
                      "rgst.t_Account"            +" Account,"
                      "rgst.t_DateLastOperation"  +" DateLastOperation,"
                      "rgst.t_RestAfterLastOperation RestAfterLastOperation,"
                      "rgst.t_Rest200691"         +" Rest200691,"
                      "rgst.t_Rest010192"         +" Rest010192,"
                      "rgst.t_Percent"            +" Percent,"
                      "rgst.t_Tax"                +" Tax,"
                      "rgst.t_Compens1"           +" Compens1,"
                      "rgst.t_Compens2"           +" Compens2,"
                      "rgst.t_Compens3x"          +" Compens3x,"
                      "rgst.t_RgstAccount"        +" RgstAccount,"
                      "rgst.t_RgstName1"          +" RgstName1,"
                      "rgst.t_RgstName2"          +" RgstName2,"
                      "rgst.t_RgstName3"          +" RgstName3,"

                      "dr.t_CodClient"            +" drCodClient,"
                      "dr.t_SpecialAccess"        +" SpecialAccess,"
                      "doc.t_OutSum"              +" OutSum,"
                      "doc.t_CodClient"           +" docCodClient,"

                      "pay.t_DatePay"             +" DatePay,"
                      "pay.t_PartRest"            +" PartRest,"
                      "pay.t_PartCompens1"        +" PartCompens1,"
                      "pay.t_PartCompens2"        +" PartCompens2,"
                      "pay.t_PartCompens3x"       +" PartCompens3x,"
                      "pay.t_PartPercent"         +" PartPercent,"
                      "pay.t_PartTax"             +" PartTax,"

                     "dp_dep.t_Name"              +" nameBranch"

             " from dchcnrgst_dbt rgst,"
                   "dchcn_pay_dbt pay,"
                   "ddepositr_dbt dr,"
                   "dsbdepdoc_dbt doc,"
                   "ddp_dep_dbt dp_dep"

             " where rgst.t_RgstID"        +" = pay.t_RgstID"
               " and rgst.t_RgtpID"        +" = pay.t_RgtpID"
               " and rgst.t_Referenc"      +" = dr.t_Referenc"
               " and doc.t_iApplicationKind   = pay.t_iApplicationKind"
               " and doc.t_ApplicationKey     = pay.t_ApplicationKey"
               " and dp_dep.t_Code "       +" = pay.t_Branch"
               
               " and (pay.t_DatePay BETWEEN ? and ?)"
               " and pay.t_Branch = ?"

             " order by rgst.t_chcnDepart,rgst.t_chcnBranch");
  var Rs;

  Cmd.AddParam("MinDate",RSDBP_IN,BeginDate);                                  
  Cmd.AddParam("MaxDate",RSDBP_IN,EndDate);
  Cmd.AddParam("Branch", RSDBP_IN,branch);
  Rs  = RsdRecordSet (Cmd);

  while (Rs.MoveNext)
    NumRec = NumRec + 1;
    Message (" Обрабатывается оплата номер "+String(NumRec));
    ОбработкаВыплаты (Rs);
  end;
end;

// #########################################################

macro Результат 
  var ComSpec = GetEnv("COMSPEC");

  if (NOT Run(ComSpec, "/C copy /B " + NameOut1 + "+" + NameOut2 + " " +NameOut +">nul"))
    if (Open (outf,NameOut))
      ViewFile (outf);
      Close (outf);
    else
      println ("Не открыт файл вывода "+NameOut);
    end;
  else
    println ("Ошибка при слиянии рабочих файлов");
  end;
end;
// #########################################################
// ##### Точка входа #######################################
// #########################################################

  var cont = true;

  if (Open(outf1,NameOut1))
    if (Open(outf2,NameOut2))
      if (NOT {BatchMode})
        if ( GetDate(BeginDate,"Введите начальную дату интервала")
         AND GetDate(EndDate,  "Введите конечную дату интервала"))
          cont = true;
        else
          MsgBox ("Вы отказались от ввода. Работа прекращена.");
          cont = false;
        end;
      end;
      if (cont)
        Заголовок ();
        Выполнение ();
        Итого ();
      end;
      Close (outf2);
    else
      println ("Не открыт файл вывода "+NameOut2);
    end;
    Close (outf1);
  else
    println ("Не открыт файл вывода "+NameOut1);
  end;
  if (cont)
    Результат ();
  end;
end;
