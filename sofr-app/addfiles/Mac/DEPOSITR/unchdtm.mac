/*
$Name:           unchdtm.mac
$Module:         RETAIL
$Description:    Откат (удаление\сторнирование) операции перевода счета на другой вид вклада
*/

/*
  RS-Retail 2.01
  Откат (удаление\сторнирование) операции перевода счета на
  другой вид вклада (# 78)
  (откат операции перевода вклада с "До востребования" на
  "Пенсионный"
  ==========================================================
  Переработка макроса unchdt1.mac, который вызывается при
  откате зачисления пенсии на счет "До востребования",
  в результате которого счет стал "Пенсионным"
  (Замечание 21 аттестации в центр.аппарате СБ РФ
  ==========================================================
  Вызывается из макросов шага "Сторнирование" для Пенсионных:
  - unchdt1.mac (операция 78 "Смена вида вклада")
  - unchdt2.mac (операция 73 "Зачисление в пакетном режиме")
  ==========================================================
  Доработал Ромодин Александр Васильевич 26.05.00
*/
import deprintr,DeskInter,OperCode,alg_vars;

file CDTDep       (depositr) key 8 write;
file CDTDepDoc    (sbdepdoc) key 6 write;
file SBDepDoc     (sbdepdoc) key 6 write;
file CDTDepRest   (pc_drest) key 0 write;
file CDTPcCalc    (pc__calc) key 0 write;
file dtyp         (sb_dtyp ) key 2;
file casln        (sb_casln) key 1;
file CDTpcestim   (pcestim ) key 0;
file accreg       (accreg  ) key 1 write;

record Template ("pcacc.dbt");

const
  CDT_SB_DEPOSIT = 1;

const
  ACCOUTN_OWNER = StrFor( 0 );

const
  NewType  = "ДО ВОСТРЕБ.";

const
  MINDATE = Date(01, 01, 1900),
  MAXDATE = Date(31, 12, 2999);

const
  UPDATE_ALL  = 1,
  UPDATE_DEP  = 2,
  UPDATE_DOC  = 4;

const
  Binsert = 2;

const
  D_IN = 1,         /* Приход */
  D_OUT = 2,        /* Расход */
  D_GET = 3,        /* Списание */
  D_PUT = 5;        /* Зачисление */

private var
  {curdate},
  {oper},
  curdat = {curdate},
  operman = {oper},
  OperBrigade = GetOperBrigade,
  Branch = NumFNCash,
  CDTClientList = TClientList, 
  OldType,
  TrnStatus = true;
var OldPosDepDoc;
var HeadAppKind,
    HeadAppKey;
var GlobNeedChangeDepType = False;


macro IsResident

  TrnStatus = CDTClientList.GetRecord( ALG_DEPOSITOR.CodClient );
  if(TrnStatus)
    return CDTClientList.CurRec.Rec.NotResident;
  else
    return -1;
  end;

end;


macro GetDepTypeIdentChars

  rewind(dtyp);
  dtyp.FlagCur = ALG_DEPOSITOR.IsCur;
  dtyp.Kind = NewType;
  if(getEQ(dtyp))
    return dtyp.NewIdentChar;
  end;

  return "XX";

end;

macro ChangeAccReg

  accreg.IsCur         = ALG_DEPOSITOR.IsCur;
  accreg.FNCash        = ALG_DEPOSITOR.FNCash;
  accreg.GroupNumb     = ALG_DEPOSITOR.GroupNumb;
  accreg.Code_Currency = ALG_DEPOSITOR.Code_Currency;
  accreg.Number        = ALG_DEPOSITOR.Number;
  if(getEQ(accreg))
    accreg.Account = ALG_DEPOSITOR.Account;
    update(accreg);
  end;

end;

macro DeleteSBDepDocs

  var
    RecFound;

  Close(SBDepDoc);
  TrnStatus = Open(SBDepDoc, "sbdepdoc.dbt");
  if ( TrnStatus )
    ClearRecord( SBDepDoc );
    SBDepDoc.Referenc = ALG_DEPOSITOR.Referenc;
    RecFound = GetGE(SBDepDoc);
    while(RecFound and (SBDepDoc.Referenc == ALG_DEPOSITOR.Referenc))
      if ( (SBDepDoc.DepDate_Document == curdat) and
           (SBDepDoc.TypeOper == Перевод_В_Другой_Вид_Вклада) and
           (SBDepDoc.ApplType != 0) )   /* саму смену вида вклада (без подвида) */
         KeyNum (casln,1);
         ClearRecord(casln);            /* удалять НЕ НАДО! Только спис. и зач. */
         casln.ApplicationKind2 = SBDepDoc.iApplicationKind;
         casln.ApplicationKey2 = SBDepDoc.ApplicationKey;
         if(GetEQ(casln))
           TrnStatus = Delete(casln);
         end;
         if(TrnStatus)
           TrnStatus = Delete(SBDepDoc);
         end;
      end;
      if(TrnStatus)
        RecFound = Next(SBDepDoc);
      else
        RecFound = false;
      end;
    end;
  end;
end;
/**********************************************************/

macro ChangeObjectType2002To2001

  var
    RecFound,
    OldKey;
  if(TrnStatus)
    ClearRecord(CDTDepRest);
    CDTDepRest.Referenc = ALG_DEPOSITOR.Referenc;
    CDTDepRest.Type_Object = 2001;
    RecFound = GetGE(CDTDepRest);
    while(TrnStatus and RecFound
      and (CDTDepRest.Referenc == ALG_DEPOSITOR.Referenc)
      and (CDTDepRest.Type_Object == 2001))
      TrnStatus = Delete(CDTDepRest);
      if(TrnStatus)
/*        RecFound = Next(CDTDepRest); РАВ 20.10.98 Изменяемое поле Type_Object входит в ключ!!! */
        CDTDepRest.Referenc = ALG_DEPOSITOR.Referenc;
        CDTDepRest.Type_Object = 2001;
        CDTDepRest.Date_Rest = Date(0,0,0);
        RecFound = GetGE(CDTDepRest);
      end;
    end;
    ClearRecord(CDTDepRest);
    CDTDepRest.Referenc = ALG_DEPOSITOR.Referenc;
    CDTDepRest.Type_Object = 2002;
    RecFound = GetGE(CDTDepRest);
    while(TrnStatus and RecFound
      and (CDTDepRest.Referenc == ALG_DEPOSITOR.Referenc)
      and (CDTDepRest.Type_Object == 2002))
      CDTDepRest.Type_Object = 2001;
      CDTDepRest.NumSession = 0;
      TrnStatus = Update(CDTDepRest);
      if(TrnStatus)
/*        RecFound = Next(CDTDepRest); РАВ 20.10.98 Изменяемое поле Type_Object входит в ключ!!! */
        CDTDepRest.Referenc = ALG_DEPOSITOR.Referenc;
        CDTDepRest.Type_Object = 2002;
        CDTDepRest.Date_Rest = Date(0,0,0);
        RecFound = GetGE(CDTDepRest);
      end;
    end;
  end;
/* РАВ 20.10.98 Проценты на операцию */
  if(TrnStatus)
    OldKey = KeyNum(CDTDepDoc,2);
    ClearRecord(CDTDepDoc);
    CDTDepDoc.Referenc = ALG_DEPOSITOR.Referenc;
    RecFound = GetGE(CDTDepDoc);
    while(TrnStatus and RecFound
      and (CDTDepDoc.Referenc == ALG_DEPOSITOR.Referenc))
      if ((CDTDepDoc.DepDate_Document != curdat) OR
       (CDTDepDoc.TypeOper != Перевод_В_Другой_Вид_Вклада))
        CDTDepDoc.ObjectPerc = 2001; /* Для новых уже поставлено */
        TrnStatus = Update(CDTDepDoc);
      end;
      if(TrnStatus)
        RecFound = Next(CDTDepDoc);
      end;
    end;
    KeyNum(CDTDepDoc,OldKey);
  end;

/* Перенос pc__calc */

  if ( TrnStatus )
    ClearRecord( CDTPcCalc );
    CDTPcCalc.Referenc = ALG_DEPOSITOR.Referenc;
    CDTPcCalc.ObjectType = 2001;
    RecFound = GetGE( CDTPcCalc );
    while ( RecFound and TrnStatus
      and ( CDTPcCalc.Referenc == ALG_DEPOSITOR.Referenc )
      and ( CDTPcCalc.ObjectType == 2001 ) )
      TrnStatus = Delete( CDTPcCalc );
      RecFound = Next( CDTPcCalc );
    end;
  end;
  if ( TrnStatus )
    ClearRecord( CDTPcCalc );
    CDTPcCalc.Referenc = ALG_DEPOSITOR.Referenc;
    CDTPcCalc.ObjectType = 2002;
    RecFound = GetGE( CDTPcCalc );
    while ( RecFound and TrnStatus
      and ( CDTPcCalc.Referenc == ALG_DEPOSITOR.Referenc )
      and ( CDTPcCalc.ObjectType == 2002 ) )
      CDTPcCalc.ObjectType = 2001;
      TrnStatus = Update( CDTPcCalc );
      ClearRecord( CDTPcCalc );
      CDTPcCalc.Referenc = ALG_DEPOSITOR.Referenc;
      CDTPcCalc.ObjectType = 2002;
      RecFound = GetGE( CDTPcCalc );
    end;
  end;
end;
/**********************************************************/

macro UpdateDepRecord

  UpdateAlgRecords (UPDATE_DEP, false);
  ALG_DEPOSITOR.Type_Account = NewType;
  ALG_DEPOSITOR.UseAlternate = 0;
  UpdateAlgRecords (UPDATE_DEP, true);
end;


/**************************************************************************\
|           Поиск последнего документа по накопленным процентам            |
\**************************************************************************/
macro GetLastPcEstim

  var stat;
  var find_last = FALSE;

  CDTpcestim.Referenc   = ALG_DEPOSITOR.Referenc;
  CDTpcestim.DateOperat = Date(31,12,9999);
  CDTpcestim.NumDayDoc  = 32000;
  stat = GetLE(CDTpcestim);
  while (stat)
    if (CDTpcestim.Referenc != ALG_DEPOSITOR.Referenc)
      stat = FALSE;
    else
      if (CDTpcestim.Action < 2)
        find_last = TRUE;
        stat = FALSE;
      else
        stat = Prev(CDTpcestim);
      end;
    end;
  end;

  return find_last;
  
end;


/**************************************************************************\
|                     Обновление статистики по вкладам                     |
\**************************************************************************/
private macro UpdateStatistics

  record StatParm ("StatParm.rec");

  var SummaPay = $0L;

  if (GetLastPcEstim())
    SummaPay = CDTpcestim.SummaPay_Rest;
  end;
  
  if (SummaPay != 0)
    ClearRecord(StatParm);
    StatParm.Op           = Binsert;
    StatParm.Type_Account = OldType;
    StatParm.CodCur       = ALG_DEPOSITOR.Code_Currency;
    StatParm.Date         = curdat;
    StatParm.IsCur        = ALG_DEPOSITOR.IsCur;
    StatParm.FlagRez      = IsResident;
    StatParm.EstimIn      = -SummaPay;
    if (Index(ALG_DEPOSITOR.UserTypeAccount,"Н") > 0)
      StatParm.Branch     = NumRealFNCash();
    else
      StatParm.Branch     = ALG_DEPOSITOR.FNCash;
    end;
    if (TrnStatus)
      TrnStatus = UpdateDepStats(StatParm);
    end;
    if (TrnStatus)
      StatParm.Type_Account = NewType;
      StatParm.EstimOverOut = -SummaPay;
      StatParm.EstimIn      = $0L;
      TrnStatus = UpdateDepStats(StatParm);
    end;
  end;

end;


/**********************************************************/
macro MoveToAltState

  TrnStatus = not moveAccountIntoAnotherState(
    Перевод_В_Альт_Сост,
    ALG_DEPOSITOR.Referenc,
    curdat,
    Date(1, 1, 9999),NULL,NULL,0
    );
end;
/**********************************************************/

macro TrnProcedure
/*
  Процедура в транзакции
*/
  var Storn;

  if (ALG_PARAM.PrmC)
    Storn = true;
  else
    Storn = false;
  end;
/*
  Перевод в альтернативное состояние для отката
*/
  if (TrnStatus AND GlobNeedChangeDepType)
/*    if (NOT storn)
      CashLink_AddRec(HeadAppKind,HeadAppKey);
    end;*/
/*    MoveToAltState; */
/*    if (NOT storn)
      InterDesk_EndDocBunch ();
    end;*/
  end;
/*
  Выполнение сторнирования\удаления
*/
  if (TrnStatus)
      TrnStatus = DeleteDocument( ALG_SBDEPDOC.iApplicationKind,
                                  ALG_SBDEPDOC.ApplicationKey, Storn );
  end;
  OldType = ALG_DEPOSITOR.Type_Account;
/*
  Зачистка при переводе на другой вид вклада
*/
  if (TrnStatus AND GlobNeedChangeDepType)
    UpdateDepRecord;
  end;
  if (TrnStatus AND GlobNeedChangeDepType)
    ChangeObjectType2002To2001;
  end;
  if (TrnStatus AND GlobNeedChangeDepType)
    UpdateStatistics();
  end;

  if (NOT TrnStatus)
    AbortTrn();
  end;

end;
/***********************************************************
############### Точка входа- основной макрос ###############
***********************************************************/

macro MakeChangeDepType
(
 NeedChange /* True  - со сменой вида вклада
               False - вызвать только DeleteDocument, без
                       организации смены вида вклада */
)
/*
  Организация смены вида вклада
*/
  var OldKey, RecFound, ErrText;

  GlobNeedChangeDepType = NeedChange;
  SetOutput("NUL");
/*
  Для смены вида вклада ищем операцию смены- она в голове списка
*/
  if(TrnStatus AND GlobNeedChangeDepType)
    TrnStatus = false;
    OldKey = KeyNum(CDTDepDoc,2);
    ClearRecord(CDTDepDoc);
    CDTDepDoc.Referenc = ALG_DEPOSITOR.Referenc;
    RecFound = GetGE(CDTDepDoc);
    while(RecFound and (CDTDepDoc.Referenc == ALG_DEPOSITOR.Referenc))
      if ( (CDTDepDoc.Date_Document == curdat) and
           (CDTDepDoc.TypeOper == Перевод_В_Другой_Вид_Вклада) and
          ((CDTDepDoc.ApplType == 0) or (CDTDepDoc.ApplType == 101)) )
        TrnStatus = true;
        HeadAppKind = CDTDepDoc.iApplicationKind;
        HeadAppKey  = CDTDepDoc.ApplicationKey;
      end;
      if (NOT TrnStatus)
        RecFound = Next (CDTDepDoc);
      else
        RecFound = false;
      end;
    end;
    KeyNum (CDTDepDoc,OldKey);
  end;
  if (TrnStatus)
    if (ProcessTrn(1, "TrnProcedure",
      CDTDep, CDTDepDoc, CDTDepRest, CDTPcCalc))

      if(GetDepTypeIdentChars != "XX")
        StrSet(ALG_DEPOSITOR.Account, 21, GetDepTypeIdentChars);
        ChangeAccReg();
      end;

      if (GlobNeedChangeDepType)
        ALG_UPDATE = UPDATE_DEP;
      end;
    else
      Status(ErrText);
      MsgBox ("Транзакция прервана! " + ErrText);
    end;
  end;

  if(TrnStatus)
    ALG_RESULT = END_RES;
  else
    ALG_RESULT = ERR_RES;
  end;
end;
