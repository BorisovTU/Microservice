/*
$Name:               RtEsInfo.mac
$Module:             DEPOSITR
$Description:        функции выборки данных RS-Retail во временные таблицы для передачи их в ФНС, ЦБ и другим получателям, определенным в RS-Banking
*/


import RtXmlFunc, rsd;


private const 
    PARAM_ERROR = -1,
    EXTRA_PARAM = -2,
    NO_CLIENT = -3,
    NO_PARAM = -4;

private const ZERO_DATE = date(0, 0, 0);


//-----------------------------------------------------------------------------
private macro GetRegIntValue(key: string): integer
  var value = 0;

  var cmd = RsdCommand("SELECT NVL(rsb_common.GetRegIntValue(?), 0) AS val FROM dual");

  cmd.addParam("key", RSDBP_IN, key);
  cmd.execute();

  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;

  if (rs.moveNext())
    value = rs.value("val");
  end;

  return value;
end;


private const METAL_ACC = GetRegIntValue("RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ОБМЕН_С_ФНС\\ОМС");


//-----------------------------------------------------------------------------
private macro errMessage(errCode: integer): string
  var msg;
  if (errCode == EXTRA_PARAM)
    msg = "Список клиентов и список счетов не могут быть определены одновременно.";
  elif (errCode == NO_CLIENT)
    msg = "Для выборки сведений о наличии счетов не задан клиент, по счетам которого надо делать выборку";
  elif (errCode == NO_PARAM)
    msg = "Не определены списки клиентов и счетов";
  else
    msg = "Заданы не все обязательные параметры. Выборка данных невозможна.";  
  end;
  return msg;
end;


//-----------------------------------------------------------------------------
private macro doError(errCode: integer)
  RunError(errMessage(errCode), errCode);
end;


//-----------------------------------------------------------------------------
private macro makeXsltList(tag: string): string
  var header = tag + "s";
  // однотипные элементы переносим в список под общим заголовком
  return
      "      <" + header + ">" +
      "        <xsl:for-each select=\"" + tag + "\">" +
      "            <xsl:copy-of select=\".\" />" +
      "        </xsl:for-each> " +
      "      </" + header + "> ";
end;


//-----------------------------------------------------------------------------
private macro makeXsltLists(arr: TArray): string
  var list = "";

  var tag;
  for (tag, arr)
    list = list + makeXsltList(tag);
  end;

  return list;
end;


//-----------------------------------------------------------------------------
private macro makeIgnoreTagList(arr: TArray): string
  var list = "";

  var i = 0;
  while (i < arr.size())
    if (i > 0)
      list = list + " | ";
    end;

    list = list + arr[i];
    
    i = i + 1;
  end;

  return list;
end;


//-----------------------------------------------------------------------------
// преобразование исходного XML в формат, пригодный для автоматического парсинга в RSL-объекты
private macro preprocessParam(param: string, listTags: TArray): string
  var xml = CreateXMLParser();
  xml.validateOnParse = true;
  xml.async = false;

  var stat = xml.loadXML(param);
  if (not stat)
    doError(PARAM_ERROR);
  end;

  //println(xml.xml);

  var xslt = CreateXMLParser();
  xslt.validateOnParse = true;
  xslt.async = false;

  stat = xslt.loadXML(
      "<?xml version=\"1.0\"?> " +
      "<xsl:stylesheet version=\"1.0\" xmlns:xsl=\"http://www.w3.org/1999/XSL/Transform\"> " +
      "  <xsl:output method=\"xml\" indent=\"yes\" omit-xml-declaration=\"yes\"/> " +
      "  <xsl:template match=\"PARAM\"> " +
      "    <PARAM> " +
      // создаём списки
      makeXsltLists(listTags) +
      // рекурсивная обработка шаблонов
      "      <xsl:apply-templates /> " +
      "    </PARAM> " +
      "  </xsl:template> " +
      " " +
      // эти элементы уже перенесены в списки, поэтому с ними больше ничего не делаем
      "  <xsl:template match=\"" + makeIgnoreTagList(listTags) + "\" /> " +
      " " +
      // оставшиеся элементы копируем как есть
      "  <xsl:template match=\"*\"> " +
      "    <xsl:copy-of select=\".\" /> " +
      "  </xsl:template> " +
      " " +
      "</xsl:stylesheet>"        
  );
  
  if (not stat)
    doError(PARAM_ERROR);
  end;

  //println(xslt.xml);

  var output = xml.transformNode(xslt);
  //println(output);

  return output;
end;


//-----------------------------------------------------------------------------
// DD.MM.YYYY
private macro ParseDateParam(value)
  return Date(value);
end;


//-----------------------------------------------------------------------------
private macro metalAccCondition(accStr: string, depStr: string): string
  var cond;
  if (METAL_ACC == 1)
    cond = " WHEN '203' THEN '" + accStr +"'";
  elif (METAL_ACC == 2)
    cond = " WHEN '203' THEN '" + depStr +"'";
  else
    cond = "";
  end;
  return cond;
end;


//-----------------------------------------------------------------------------
private macro selectAccountsQuery(): string
  return
      "SELECT SUBSTR(dr.t_Account, 1, 20) AS t_Account, dr.t_Referenc AS t_AccountID,\n" +
      "       CASE WHEN dr.t_CodClient = dr.t_OpenClient THEN dr.t_CodClient\n" +
      "            ELSE NVL((SELECT dr.t_CodClient \n" +
      "                      FROM dual\n" +
      "                      WHERE EXISTS (SELECT 1\n" +
      "                                          FROM dsbdepdoc_dbt d \n" +
      "                                          WHERE d.t_Referenc = dr.t_Referenc\n" +
      "                                            AND d.t_Date_Document <= prm.EndDate\n" +
      "                                            AND d.t_CodClient = dr.t_CodClient\n" +
      "                                            AND d.t_TypeOper NOT IN (72, 35, 38, 39)\n" +
      "                                            AND d.t_TypeComplexOper NOT IN (72, 82)\n" +
      "                                            AND d.t_KindOp NOT IN (8, 9, 14, 15, 16)\n" +
      "                                            AND d.t_Action <> 2\n" +
      "                                            AND d.t_FlagStorn = CHR(0)\n" +
      "                                            AND BITAND (d.t_Flags, 131072) = 0)), dr.t_OpenClient)\n" +
      "             END AS t_ClientID,\n" +
      "       rsu_rtlcommon.getDepFNCash(dr.t_FNCash) AS t_Department,\n" +
      "       CHR(1) AS t_DepartmentBIC,\n" +
      "       CHR(1) AS t_DepartmentNum,\n" +
      "       CASE dr.t_Code_Currency WHEN 0 THEN '643' ELSE (\n" +
      "           SELECT NVL(f.t_ISO_Number, c.t_ExternalCode)\n" +
      "           FROM dcurrency_dbt c \n" +
      "             LEFT OUTER JOIN dfininstr_dbt f\n" +
      "               ON c.t_ExternalCode IN (f.t_FI_Code, f.t_CodeInAccount)\n" +
      "           WHERE c.t_Code_Currency = dr.t_Code_Currency\n" +
      "             AND ROWNUM = 1\n" +
      "         ) END AS t_CurrCode,\n" +
      "       CASE SUBSTR(dr.t_Account, 1, 3) \n" +
      "               WHEN '408' THEN 'текущий счет' WHEN '423' THEN 'вклад' WHEN '426' THEN 'вклад'" +
          metalAccCondition("текущий счет", "вклад") + " ELSE CHR(1)\n" +
      "         END AS t_AccType,\n" +
      "       CASE SUBSTR(dr.t_Account, 1, 3) \n" +
      "               WHEN '408' THEN 'Текущий' WHEN '423' THEN 'Счет по вкладу' WHEN '426' THEN 'Счет по вкладу'" +
          metalAccCondition("Текущий", "Счет по вкладу") + " ELSE CHR(1)\n" +
      "         END AS t_AccKind,\n" +
      "       NVL((SELECT MIN(d.t_Date_Document)\n" +
      "            FROM dsbdepdoc_dbt d\n" +
      "            WHERE d.t_Referenc = dr.t_Referenc\n" +
      "              AND d.t_TypeOper IN (31, 1, 51, 58)), dr.t_Open_Date) AS t_OpenDate,\n" +
      "       dr.t_Close_Date AS t_CloseDate,\n" +
      "       CASE WHEN dr.t_Open_Date NOT BETWEEN prm.BeginDate AND EndDate THEN CHR(1)\n" +
      "            ELSE NVL((SELECT MAX(SUBSTR(prev.t_Account, 1, 20)) KEEP (DENSE_RANK LAST ORDER BY prev.t_Open_Date)\n" +
      "                  FROM ddepositr_dbt prev\n" +
      "                  WHERE prev.t_FNCash = dr.t_FNCash\n" +
      "                    AND prev.t_SvodAccount = dr.t_SvodAccount\n" +
      "                    AND prev.t_Code_Currency = dr.t_Code_Currency\n" +
      "                    AND prev.t_Open_Close = 'З'\n" +
      "                    AND prev.t_Open_Date < dr.t_Open_Date\n" +
      "                  GROUP BY prev.t_FNCash, prev.t_SvodAccount), CHR(1)) END AS t_AccountChange,\n" +
      "       'RETAIL' AS t_ABSID,\n" +
      "       CHR(1) AS t_AccNote,\n" +
      "       dr.t_Account AS FullAccount\n" +
      "FROM ddepositr_dbt dr, \n" +
      "     (SELECT ? AS BeginDate, ? AS EndDate,\n" +
      "      TO_DATE('01010001', 'DDMMYYYY') AS NullDate FROM DUAL) prm\n";
end;


//-----------------------------------------------------------------------------
private macro TArrayOf(sample): TArray
  var arr = TArray;
  arr[0] = sample;
  return arr;
end;


//-----------------------------------------------------------------------------
//
//-----------------------------------------------------------------------------
class TAccount
  var AccID: integer = 0;
  var Account: string = "";
  var AccFI: string = "";
  var ClientID: integer = 0;


  //-----------------------------------------------------------------------------
  macro check()
    if ((Account == "") or (AccFI == ""))
      doError(PARAM_ERROR);
    end;
  end;


  //-----------------------------------------------------------------------------
  macro checkForRest()
    check();
    
    if (ClientID == 0)
      doError(PARAM_ERROR);
    end;
  end;
end;


//-----------------------------------------------------------------------------
//
//-----------------------------------------------------------------------------
class TDataPeriod
  var BeginDate: date = ZERO_DATE;
  var EndDate: date = ZERO_DATE;


  //-----------------------------------------------------------------------------
  macro check()
    if ((BeginDate == ZERO_DATE) and (EndDate == ZERO_DATE))
      doError(PARAM_ERROR);
    end;

    if (BeginDate > EndDate)
      doError(PARAM_ERROR);
    end;
  end;
end;


//-----------------------------------------------------------------------------
//
//-----------------------------------------------------------------------------
class TAccRestParam
  var AccRestPeriod = TDataPeriod;
  var AccLists = TArrayOf(TAccount);


  //-----------------------------------------------------------------------------
  macro check()
    AccRestPeriod.check();
    
    if (AccLists.size() == 0)
      doError(NO_PARAM);
    end;

    var acc;
    for (acc, AccLists)
      acc.checkForRest();
    end;
  end;
end;


//-----------------------------------------------------------------------------
//
//-----------------------------------------------------------------------------
private class TAccRestProcessor

  private var prm = TAccRestParam;
  private var query;
  private var queryParams = TArray;


  //-----------------------------------------------------------------------------
  private macro addQueryParam(param)
    queryParams[queryParams.size()] = param;
  end;


  //-----------------------------------------------------------------------------
  private macro baseQuery()
    query =
        "SELECT * FROM (\n" +
        selectAccountsQuery() +
        ") acc\n";

    addQueryParam(prm.AccRestPeriod.BeginDate);
    addQueryParam(prm.AccRestPeriod.EndDate);
  end;


  //-----------------------------------------------------------------------------
  private macro addAccounts()
    var accountCond = "(";
    
    var i = 0;
    while (i < prm.AccLists.size())
      if (i > 0)
        accountCond = accountCond + "   OR ";
      end;

      var acc = prm.AccLists[i];

      accountCond = accountCond + "(";
      if (acc.AccID != 0)
        accountCond = accountCond + "acc.t_AccountID = ? AND ";
        addQueryParam(acc.AccID);
      end;
      accountCond = accountCond + "acc.t_Account = ? AND acc.t_CurrCode = ? AND acc.t_ClientID = ?)\n";
      
      addQueryParam(acc.Account);
      addQueryParam(acc.AccFI);
      addQueryParam(acc.ClientID);
      
      i = i + 1;
    end;

    accountCond = accountCond + "  )\n";

    query = query +
        "WHERE " + accountCond;
  end;


  //-----------------------------------------------------------------------------
  private macro selectRestQuery(cmp: string): string
    return
        "       NVL((SELECT t_Rest from (\n" +
        "          SELECT * FROM dsbdepdoc_dbt doc\n" +
        "          WHERE doc.t_IsSuspended = CHR(0)\n" +
        "            AND doc.t_FlagStorn = CHR(0)\n" +
        "            AND doc.t_KindOp NOT IN (8, 9, 14, 15, 16) \n" +
        "            AND doc.t_Action <> 2\n" +
        "          ORDER BY doc.t_Referenc DESC, doc.t_Date_Document DESC, doc.t_NumDayDoc DESC\n" +
        "        ) d\n" +
        "        WHERE d.t_Referenc = a.t_AccountID\n" +
        "          AND d.t_Date_Document " + cmp + " p.DateRest\n" +
        "          AND ROWNUM = 1 \n" +
        "       ), 0)";
  end;


  //-----------------------------------------------------------------------------
  private macro insert(dateRest: date)
    var qry = 
        "INSERT INTO desaccrest_tmp\n" +
        "(t_Account, t_AccountID, t_CurrCode, t_Department, t_DepartmentBIC,\n" +
        " t_DepartmentNum, t_DateRest, t_InRest, t_OutRest)\n" +
        "SELECT t_Account, t_AccountID, t_CurrCode, t_Department, t_DepartmentBIC, \n" +
        "       t_DepartmentNum, p.DateRest AS t_DateRest,\n" +
                        selectRestQuery("<") + " AS t_InRest,\n" +
                        selectRestQuery("<=") + " AS t_OutRest\n" +
        "FROM (\n" +
        query +
        ") a,\n" +
        "  (SELECT ? AS DateRest FROM DUAL) p\n";
    
    var cmd = RsdCommand(qry);

    var i = 0;
    while (i < queryParams.size())
      cmd.addParam("prm" + string(i), RSDBP_IN, queryParams[i]);
      i = i + 1;
    end;

    cmd.addParam("prm" + string(i), RSDBP_IN, dateRest);

    cmd.NullConversion = true;
    cmd.execute();
  end;


  //-----------------------------------------------------------------------------
  private macro parseParam(param: string)
    var listTags = TArray;
    listTags[listTags.size] = "AccList";

    param = preprocessParam(param, listTags);

    ReplaceParseDateMacro(@ParseDateParam);
    var stat = RtXml2Rsl(param, prm);
    ReplaceParseDateMacro();
    if (stat != 0)
      doError(PARAM_ERROR);
    end;

    //printObj(prm, true);
  end;


  //-----------------------------------------------------------------------------
  macro processAccounts(accRestPeriod: TDataPeriod, accQuery: string, accQueryParams: TArray)
    prm.AccRestPeriod = accRestPeriod;
    query = accQuery;
    queryParams = TArray(accQueryParams);

    insert(prm.AccRestPeriod.BeginDate);
    if (prm.AccRestPeriod.BeginDate != prm.AccRestPeriod.EndDate)
      insert(prm.AccRestPeriod.EndDate);
    end;
  end;


  //-----------------------------------------------------------------------------
  macro process(param: string)
    parseParam(param);

    prm.check();

    baseQuery();
    addAccounts();

    insert(prm.AccRestPeriod.BeginDate);
    if (prm.AccRestPeriod.BeginDate != prm.AccRestPeriod.EndDate)
      insert(prm.AccRestPeriod.EndDate);
    end;
  end;

end;


//-----------------------------------------------------------------------------
//
//-----------------------------------------------------------------------------
class TAccountParam
  var DataPeriod = TDataPeriod;
  var OperTypes = TArrayOf(0);
  var AccTypes = TArray;
  var ClientIDs = TArrayOf(0);
  var AccDeps = TArrayOf(0);
  var AccLists = TArrayOf(TAccount);
  var IncAccRest: bool = false;
  var AccMask: string = "";
  var InCloseAcc: bool = false;
  var AccountFI: string = "";
  var AccChapters = TArray;


  //-----------------------------------------------------------------------------
  macro check()
    DataPeriod.check();
    
    if (OperTypes.size() == 0)
      doError(PARAM_ERROR);
    end;

    if ((ClientIDs.size() > 0) and (AccLists.size() > 0))
      doError(EXTRA_PARAM);
    end;

    if ((ClientIDs.size() == 0) and (AccLists.size() == 0))
      doError(NO_PARAM);
    end;

    if ((OperTypes.size() == 1) and (OperTypes[0] == 1) and (ClientIDs.size() == 0))
      doError(NO_CLIENT);
    end;

    var acc;
    for (acc, AccLists)
      acc.check();
    end;
  end;
end;


//-----------------------------------------------------------------------------
//
//-----------------------------------------------------------------------------
private class TAccountProcessor

  private var prm = TAccountParam;
  private var query;
  private var accCond = "";
  private var queryParams = TArray;


  //-----------------------------------------------------------------------------
  private macro findAccChapter(chapter: string): bool
    var c;
    for (c, prm.AccChapters)
      if (c == chapter)
        return true;
      end;
    end;
    
    return false;
  end;


  //-----------------------------------------------------------------------------
  private macro addQueryParam(param)
    queryParams[queryParams.size()] = param;
  end;


  //-----------------------------------------------------------------------------
  private macro addAccCondition(cond: string)
    if (accCond == "")
      accCond = "WHERE ";
    else
      accCond = accCond + "  AND ";
    end;
    
    accCond = accCond + cond;
  end;


  //-----------------------------------------------------------------------------
  private macro makeParamList(arr: TArray): string
    var list = "";

    var i = 0;
    while (i < arr.size())
      if (i > 0)
        list = list + ", ";
      end;

      list = list + "?";
      addQueryParam(arr[i]);
      
      i = i + 1;
    end;

    return list;
  end;


  //-----------------------------------------------------------------------------
  private macro baseQuery()
    query =
        selectAccountsQuery() +
        "WHERE dr.t_Open_Date <= prm.EndDate\n";

    addQueryParam(prm.DataPeriod.BeginDate);
    addQueryParam(prm.DataPeriod.EndDate);

    if ((prm.ClientIDs.size() == 0) or (not prm.InCloseAcc))
      query = query +
          "  AND (dr.t_Close_Date = prm.NullDate OR dr.t_Close_Date >= prm.BeginDate)\n";
    end;
  end;


  //-----------------------------------------------------------------------------
  private macro addAccDeps()
    query = query +
        "  AND dr.t_FNCash IN (\n"
        "    SELECT t_Code\n" +
        "    FROM DDP_DEP_DBT\n" +
        "    WHERE t_NodeType <> 1\n" +
        "      AND t_Code IN (" + makeParamList(prm.AccDeps) + ")\n" +
        "    UNION\n" +
        "    SELECT t_Code\n" +
        "    FROM DDP_DEP_DBT\n" +
        "    CONNECT BY t_ParentCode = PRIOR t_Code AND t_NodeType <> 1 \n" +
        "    START WITH t_Code IN (" + makeParamList(prm.AccDeps) + ") AND t_NodeType = 1\n" +
        "  )\n";
  end;


  //-----------------------------------------------------------------------------
  private macro addAccMask()
    // PM_COMMON.CompareAccWithMask()
    
    query = query +
        "  AND REGEXP_LIKE(dr.t_Account, ?)\n";

    var mask = "^(" + StrSubst(
                      StrSubst(
                      StrSubst(
                      StrSubst(
                      StrSubst(
                      prm.AccMask,
                      " ", ""),
                      ",", "|"),
                      ";", "|"),
                      "?", "."),
                      "*", ".*") + ")$";

    addQueryParam(mask);
  end;


  //-----------------------------------------------------------------------------
  private macro addClients()
    addAccCondition("acc.t_ClientID IN (" + makeParamList(prm.ClientIDs) + ")\n");
  end;


  //-----------------------------------------------------------------------------
  private macro addAccTypes()
    addAccCondition("acc.t_AccType IN (" + makeParamList(prm.AccTypes) + ")\n");
  end;


  //-----------------------------------------------------------------------------
  private macro addAccounts()
    var accountCond = "(";
    
    var i = 0;
    while (i < prm.AccLists.size())
      if (i > 0)
        accountCond = accountCond + "   OR ";
      end;

      accountCond = accountCond + "(acc.t_Account = ? AND acc.t_CurrCode = ?)\n";
      
      var acc = prm.AccLists[i];
      addQueryParam(acc.Account);
      addQueryParam(acc.AccFI);
      
      i = i + 1;
    end;

    accountCond = accountCond + "  )\n";
    addAccCondition(accountCond);
  end;


  //-----------------------------------------------------------------------------
  private macro insert()
    var qry =
        "INSERT INTO desaccount_tmp\n" +
        "(t_Account, t_AccountID, t_ClientID, t_Department, t_DepartmentBIC, t_DepartmentNum,\n" +
        " t_CurrCode, t_AccType, t_AccKind, t_OpenDate, t_CloseDate, t_AccountChange, t_ABSID, t_AccNote)\n" +
        "SELECT t_Account, t_AccountID, t_ClientID, t_Department, t_DepartmentBIC, t_DepartmentNum,\n" +
        "       t_CurrCode, t_AccType, t_AccKind, t_OpenDate, t_CloseDate, t_AccountChange, t_ABSID, t_AccNote\n" +
        "FROM (\n" +
        query +
        ") acc\n" +
        accCond;

    var cmd = RsdCommand(qry);

    var i = 0;
    while (i < queryParams.size())
      cmd.addParam("prm" + string(i), RSDBP_IN, queryParams[i]);
      i = i + 1;
    end;

    cmd.NullConversion = true;
    cmd.execute();
  end;


  //-----------------------------------------------------------------------------
  private macro processRest()
    var qry =
        "SELECT * FROM (\n" +
        query +
        ") acc\n" +
        accCond;

    var restProcessor = TAccRestProcessor;
    restProcessor.processAccounts(prm.DataPeriod, qry, queryParams);
  end;


  //-----------------------------------------------------------------------------
  private macro parseParam(param: string)
    var listTags = TArray;
    listTags[listTags.size] = "OperType";
    listTags[listTags.size] = "AccType";
    listTags[listTags.size] = "ClientID";
    listTags[listTags.size] = "AccDep";
    listTags[listTags.size] = "AccList";
    listTags[listTags.size] = "AccChapter";

    param = preprocessParam(param, listTags);

    ReplaceParseDateMacro(@ParseDateParam);
    var stat = RtXml2Rsl(param, prm);
    ReplaceParseDateMacro();
    if (stat != 0)
      doError(PARAM_ERROR);
    end;

    //printObj(prm, true);
  end;


  //-----------------------------------------------------------------------------
  macro process(param: string)
    parseParam(param);

    prm.check();

    baseQuery();

    if (prm.ClientIDs.size() > 0)
      if ((prm.AccChapters.size() > 0) and (not findAccChapter("А")))
        return;
      end;
      
      if (prm.AccDeps.size() > 0)
        addAccDeps();
      end;

      if (prm.AccMask != "")
        addAccMask();
      end;
      
      addClients();
      
      if (prm.AccTypes.size() > 0)
        addAccTypes();
      end;
    elif (prm.AccLists.size() > 0)
      addAccounts();
    end;

    insert();

    if (prm.IncAccRest)
      processRest();
    end;
  end;

end;


//-----------------------------------------------------------------------------
//
//-----------------------------------------------------------------------------
class TAccTrnParam
  var AccOperPeriod = TDataPeriod;
  var AccLists = TArrayOf(TAccount);


  //-----------------------------------------------------------------------------
  macro check()
    AccOperPeriod.check();
    
    if (AccLists.size() == 0)
      doError(NO_PARAM);
    end;

    var acc;
    for (acc, AccLists)
      acc.checkForRest();
    end;
  end;
end;


//-----------------------------------------------------------------------------
//
//-----------------------------------------------------------------------------
private class TAccTrnProcessor

  private var prm = TAccTrnParam;
  private var query;
  private var queryParams = TArray;


  //-----------------------------------------------------------------------------
  private macro addQueryParam(param)
    queryParams[queryParams.size()] = param;
  end;


  //-----------------------------------------------------------------------------
  private macro baseQuery()
    query =
        "SELECT * FROM (\n" +
        selectAccountsQuery() +
        ") acc\n";

    addQueryParam(prm.AccOperPeriod.BeginDate);
    addQueryParam(prm.AccOperPeriod.EndDate);
  end;


  //-----------------------------------------------------------------------------
  private macro addAccounts()
    var accountCond = "(";
    
    var i = 0;
    while (i < prm.AccLists.size())
      if (i > 0)
        accountCond = accountCond + "   OR ";
      end;

      var acc = prm.AccLists[i];

      accountCond = accountCond + "(";
      if (acc.AccID != 0)
        accountCond = accountCond + "acc.t_AccountID = ? AND ";
        addQueryParam(acc.AccID);
      end;
      accountCond = accountCond + "acc.t_Account = ? AND acc.t_CurrCode = ? AND acc.t_ClientID = ?)\n";
      
      addQueryParam(acc.Account);
      addQueryParam(acc.AccFI);
      addQueryParam(acc.ClientID);
      
      i = i + 1;
    end;

    accountCond = accountCond + "  )\n";

    query = query +
        "WHERE " + accountCond;
  end;


  //-----------------------------------------------------------------------------
  private macro selectDocs1()
    return
        "SELECT LPAD(rtd.t_DocumentId, 10, '0') AS t_DocId,\n" +
        "       a.t_Account, a.t_CurrCode, rtd.t_Date, rtd.t_Ground,\n" +
        "       rtd.t_OperationCipher, rtd.t_DocumentNumber, rtd.t_ValueDate,\n" +
        "       CASE a.t_CurrCode WHEN '643' THEN 3 ELSE 6 END AS t_BankCodeKind,\n" +
        "       'D' AS t_DK,\n" +
        "       rtd.t_ReceiverBankPartyId AS t_BankId,\n" +
        "       rtd.t_ReceiverBankTempPartyId AS t_BankTempId,\n" +
        "       rtd.t_ReceiverPartyId AS t_ContrId,\n" +
        "       rtd.t_ReceiverTempPartyId AS t_ContrTempId,\n" +
        "       rtd.t_ReceiverName AS t_ContrName,\n" +
        "       rtd.t_ReceiverAccount AS t_ContrAcc,\n" +
        "       CASE WHEN rtd.t_ReceiverPartyId IS NOT NULL\n" +
        "            THEN rsi_rsbparty.PT_GetPartyCode(rtd.t_ReceiverPartyId, 16)\n" +
        "       END AS t_ContrInnKpp,\n" +
        "       rtd.t_DebitAmount AS t_Amount\n" +
        "FROM (\n" +
        query +
        ") a, drtdocument_dbt rtd\n" +
        "WHERE a.FullAccount = rtd.t_PayerAccount\n";
  end;


  //-----------------------------------------------------------------------------
  private macro selectDocs2()
    return
        "SELECT LPAD(rtd.t_DocumentId, 10, '0') AS t_DocId,\n" +
        "       a.t_Account, a.t_CurrCode, rtd.t_Date, rtd.t_Ground,\n" +
        "       rtd.t_OperationCipher, rtd.t_DocumentNumber, rtd.t_ValueDate,\n" +
        "       CASE a.t_CurrCode WHEN '643' THEN 3 ELSE 6 END AS t_BankCodeKind,\n" +
        "       'K' t_DK,\n" +
        "       rtd.t_PayerBankPartyId t_BankId,\n" +
        "       rtd.t_PayerBankTempPartyId t_BankTempId,\n" +
        "       rtd.t_PayerPartyId t_ContrId,\n" +
        "       rtd.t_PayerTempPartyId t_ContrTempId,\n" +
        "       rtd.t_PayerName t_ContrName,\n" +
        "       rtd.t_PayerAccount t_ContrAcc,\n" +
        "       CASE WHEN rtd.t_PayerPartyId IS NOT NULL\n" +
        "            THEN rsi_rsbparty.PT_GetPartyCode(rtd.t_PayerPartyId, 16)\n" +
        "       END AS t_ContrInnKpp,\n" +
        "       rtd.t_CreditAmount t_Amount\n" +
        "FROM (\n" +
        query +
        ") a, drtdocument_dbt rtd\n" +
        "WHERE a.FullAccount = rtd.t_ReceiverAccount\n";
  end;


  //-----------------------------------------------------------------------------
  private macro selectDocs3()
    return
        "SELECT LPAD(rtd.t_DocumentId, 10, '0') || '/' || LPAD(dac.t_LineNumber, 6, '0') AS t_DocId,\n" +
        "       a.t_Account, a.t_CurrCode, rtd.t_Date, rtd.t_Ground,\n" +
        "       rtd.t_OperationCipher, rtd.t_DocumentNumber, rtd.t_ValueDate,\n" +
        "       CASE a.t_CurrCode WHEN '643' THEN 3 ELSE 6 END AS t_BankCodeKind,\n" +
        "       'D' AS t_DK,\n" +
        "       rtd.t_ReceiverBankPartyId AS t_BankId,\n" +
        "       rtd.t_ReceiverBankTempPartyId AS t_BankTempId,\n" +
        "       rtd.t_ReceiverPartyId AS t_ContrId,\n" +
        "       rtd.t_ReceiverTempPartyId AS t_ContrTempId,\n" +
        "       rtd.t_ReceiverName AS t_ContrName,\n" +
        "       rtd.t_ReceiverAccount AS t_ContrAcc,\n" +
        "       CASE WHEN rtd.t_ReceiverPartyId IS NOT NULL\n" +
        "            THEN rsi_rsbparty.PT_GetPartyCode(rtd.t_ReceiverPartyId, 16)\n" +
        "       END AS t_ContrInnKpp,\n" +
        "       dac.t_Amount\n" +
        "FROM (\n" +
        query +
        ") a, drtdocument_dbt rtd, drtdocaccount_dbt dac\n" +
        "WHERE a.FullAccount = dac.t_InternalAccount\n" +
        "  AND rtd.t_DocumentId = dac.t_DocumentId\n" +
        "  AND rtd.t_FoldSide = 1\n";
  end;


  //-----------------------------------------------------------------------------
  private macro selectDocs4()
    return
        "SELECT LPAD(rtd.t_DocumentId, 10, '0') || '/' || LPAD(dac.t_LineNumber, 6, '0') AS t_DocId,\n" +
        "       a.t_Account, a.t_CurrCode, rtd.t_Date, rtd.t_Ground,\n" +
        "       rtd.t_OperationCipher, rtd.t_DocumentNumber, rtd.t_ValueDate,\n" +
        "       CASE a.t_CurrCode WHEN '643' THEN 3 ELSE 6 END AS t_BankCodeKind,\n" +
        "       'K' t_DK,\n" +
        "       rtd.t_PayerBankPartyId t_BankId,\n" +
        "       rtd.t_PayerBankTempPartyId t_BankTempId,\n" +
        "       rtd.t_PayerPartyId t_ContrId,\n" +
        "       rtd.t_PayerTempPartyId t_ContrTempId,\n" +
        "       rtd.t_PayerName t_ContrName,\n" +
        "       rtd.t_PayerAccount t_ContrAcc,\n" +
        "       CASE WHEN rtd.t_PayerPartyId IS NOT NULL\n" +
        "            THEN rsi_rsbparty.PT_GetPartyCode(rtd.t_PayerPartyId, 16)\n" +
        "       END AS t_ContrInnKpp,\n" +
        "       dac.t_Amount\n" +
        "FROM (\n" +
        query +
        ") a, drtdocument_dbt rtd, drtdocaccount_dbt dac\n" +
        "WHERE a.FullAccount = dac.t_InternalAccount\n" +
        "  AND rtd.t_DocumentId = dac.t_DocumentId\n" +
        "  AND rtd.t_FoldSide = 2\n" +
        "  AND dac.t_InternalAccount <> rtd.t_ReceiverAccount\n";
  end;
  

  //-----------------------------------------------------------------------------
  private macro insert(selDoc: string)
    var qry = 
        "INSERT INTO desacctrn_tmp\n" +
        "(t_OperID, t_Account, t_AccCurr, t_OperDate, t_Ground,\n" +
        " t_CodeOper, t_DocNumber, t_DocDate, t_BankID,\n" +
        " t_BankCodeKind,\n" +
        " t_BankCode,\n" +
        " t_BankName,\n" +
        " t_BankAcc,\n" +
        " t_ContrName,\n" +
        " t_ContrINN,\n" +
        " t_ContrKPP,\n" +
        " t_ContrAcc,\n" +
        " t_Amount, t_DK)\n" +
        "SELECT doc.t_DocId, doc.t_Account, doc.t_CurrCode, doc.t_Date, doc.t_Ground, \n" +
        "       SUBSTR(doc.t_OperationCipher, 1, 2), doc.t_DocumentNumber, doc.t_ValueDate, NVL(doc.t_BankId, 0),\n" +
        "       CASE WHEN doc.t_BankId > 0 THEN doc.t_BankCodeKind \n" +
        "            ELSE NVL(bnkTemp.t_BankCodeKind, 0)\n" +
        "       END AS t_BankCodeKind, \n" +
        "       CASE WHEN doc.t_BankId > 0\n" +
        "            THEN rsi_rsbparty.PT_GetPartyCode(doc.t_BankId, doc.t_BankCodeKind) \n" +
        "            ELSE NVL(bnkTemp.t_BankCode, CHR(1))\n" +
        "       END AS t_BankCode,\n" +
        "       SUBSTR(NVL(bnk.t_Name, NVL(bnkTemp.t_Name, CHR(1))), 1, 160) AS t_BankName,\n" +
        "       SUBSTR(NVL(bnkDep.t_CorAcc, NVL(bnkTemp.t_BankCorrAccount, CHR(1))), 1, 34) AS t_BankAcc,\n" +
        "       SUBSTR(CASE WHEN doc.t_ContrName <> CHR(1) THEN doc.t_ContrName\n" +
        "                   ELSE NVL(cntr.t_Name, NVL(cntrTemp.t_Name, CHR(1))) \n" +
        "              END, 1, 160) AS t_ContrName,\n" +
        "       SUBSTR(CASE WHEN doc.t_ContrInnKpp IS NOT NULL\n" +
        "                   THEN CASE WHEN INSTR(doc.t_ContrInnKpp, '/') = 0 \n" +
        "                             THEN doc.t_ContrInnKpp \n" +
        "                             ELSE SUBSTR(doc.t_ContrInnKpp, 1, INSTR(doc.t_ContrInnKpp, '/') - 1)\n" +
        "                        END\n" +
        "                   ELSE NVL(cntrTemp.t_Inn, CHR(1))\n" +
        "              END, 1, 12) AS t_ContrINN,\n" +
        "       SUBSTR(CASE WHEN doc.t_ContrInnKpp IS NOT NULL\n" +
        "                   THEN CASE WHEN INSTR(doc.t_ContrInnKpp, '/') = 0 \n" +
        "                             THEN CHR(1) \n" +
        "                             ELSE SUBSTR(doc.t_ContrInnKpp, INSTR(doc.t_ContrInnKpp, '/') + 1)\n" +
        "                        END\n" +
        "                   ELSE NVL(cntrTemp.t_Kpp, CHR(1))\n" +
        "              END, 1, 9) AS t_ContrKPP,\n" +
        "       SUBSTR(doc.t_ContrAcc, 1, CASE WHEN doc.t_BankId > 0 THEN 20 ELSE 34 END) AS t_ContrAcc,\n" +
        "       doc.t_Amount, doc.t_DK\n" +
        "FROM (\n" +
        selDoc +
        ") doc,\n" +
        "  dparty_dbt bnk, dbankdprt_dbt bnkDep, dtemporaryparty_dbt bnkTemp,\n" +
        "  dparty_dbt cntr, dtemporaryparty_dbt cntrTemp\n" +
        "WHERE bnk.t_PartyId(+) = doc.t_BankId\n" +
        "  AND bnkDep.t_PartyId(+) = doc.t_BankId\n" +
        "  AND bnkTemp.t_TempPartyId(+) = doc.t_BankTempId\n" +
        "  AND cntr.t_PartyId(+) = doc.t_ContrId\n" +
        "  AND cntrTemp.t_TempPartyId(+) = doc.t_ContrTempId\n" +
        "  AND doc.t_Date BETWEEN ? AND ? ";
    
    var cmd = RsdCommand(qry);

    var i = 0;
    while (i < queryParams.size())
      cmd.addParam("prm" + string(i), RSDBP_IN, queryParams[i]);
      i = i + 1;
    end;

    cmd.addParam("prm" + string(i), RSDBP_IN, prm.AccOperPeriod.BeginDate);
    cmd.addParam("prm" + string(i + 1), RSDBP_IN, prm.AccOperPeriod.EndDate);

    cmd.NullConversion = true;
    cmd.execute();
  end;


  //-----------------------------------------------------------------------------
  private macro parseParam(param: string)
    var listTags = TArray;
    listTags[listTags.size] = "AccList";

    param = preprocessParam(param, listTags);

    ReplaceParseDateMacro(@ParseDateParam);
    var stat = RtXml2Rsl(param, prm);
    ReplaceParseDateMacro();
    if (stat != 0)
      doError(PARAM_ERROR);
    end;

    //printObj(prm, true);
  end;


  //-----------------------------------------------------------------------------
  macro process(param: string)
    parseParam(param);

    prm.check();

    baseQuery();
    addAccounts();

    insert(selectDocs1());
    insert(selectDocs2());
    insert(selectDocs3());
    insert(selectDocs4());
  end;


end;


//-----------------------------------------------------------------------------
private macro Process(param: string, processor, message: string): integer
  var state = 0;
  processor.process(param);

  SetParm(2, "");
  return state;

onError(err)
  if (ValType(err.err) == V_INTEGER)
    state = err.err;
  else
    state = PARAM_ERROR;
  end;

  message = errMessage(state);
  SetParm(2, message);
  return state;
end;


//-----------------------------------------------------------------------------
macro GetESAccount(param: string, message: string): integer
  var processor = TAccountProcessor;
  var state = Process(param, processor, message);

  SetParm(1, message);
  return state;
end;


//-----------------------------------------------------------------------------
macro GetESAccRest(param: string, message: string): integer
  var processor = TAccRestProcessor;
  var state = Process(param, processor, message);

  SetParm(1, message);
  return state;
end;


//-----------------------------------------------------------------------------
macro GetESAccTrn(param: string, message: string): integer
  var processor = TAccTrnProcessor;
  var state = Process(param, processor, message);

  SetParm(1, message);
  return state;
end;

