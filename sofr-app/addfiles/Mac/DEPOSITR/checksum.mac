/***************************************************************************\
| CHECKSUM.MAC - ограничения на выдаваемую сумму для вклада "НОМЕРНОЙ"      |
|                                                     Зубов В.Ю. 18-09-97   |
\***************************************************************************/
/* Данный макрос служит для шага алгоритма "Ограничение на сумму расхода"  */
/* операций "Расход" и "Выдача %%". Реализует условие - при хранении счета */
/* меньше одного месяца нельзя выдавать причисленные %% */

import deprintr,/*getpclib,*/opercode, Календарь;

/*---------- Инициализируемые структуры ---------------------------------*/
 record ALG_DEPOSITOR("depositr.dbt");
 record ALG_SBDEPDOC("sbdepdoc.dbt");
 record ALG_PARAM("sb_algop.dbt");

/*---------- Коды возврата макроса --------------------------------------*/
 const  OK_RES   =  0,  /* продолжать проводку */
        SKIP_RES =  1,  /* пропустить выполнение предопределенного модуля */
        END_RES  =  2,  /* завершить проводку */
        ERR_RES  = -1;  /* аваpийно завершить проводку */

file       doc("sbdepdoc.dbt"); /* файл документов */
/*------------------------- тело макроса --------------------------------*/
Var
     MaxDate   = Date(0,0,0),
     tmpDate, Summa, found, Результат, С_Панелью,
     day, mon, year,
     Причислено,Остаток;

ALG_RESULT = ERR_RES; /* Инициализируем код возврата ошибкой */
/* Проверяем кода операций */
if((ALG_SBDEPDOC.TypeOper != Выдача_Процентов) AND
   (ALG_SBDEPDOC.TypeOper != Расход))
     MsgBox("Неправильно использован макрос");
   return;
end; /* IF */
/* П.№1 Вычисляем дату = сегодня - месяц */
tmpDate = DateAfterCalenMonths({curdate}, -1);
/*MsgBox(String(tmpDate));*/
/* П.№2 Проверяем, пролежал ли счет месяц*/
if(ALG_DEPOSITOR.Open_Date > tmpDate)
  if((ALG_SBDEPDOC.TypeOper == Выдача_Процентов))
     MsgBox("Вклад не пролежал 1 месяц. | Cумма причисленных %% не будут выдана");
     return;
  end; /* IF */
  /* Далее все делается только для Расхода */
  /* П.№3 Находим сумму причисленных процентов */
  Rewind(doc);
  KeyNum(doc,1);
  doc.Referenc = ALG_DEPOSITOR.Referenc;
  doc.TypeOper = Зачисление_Процентов;
  doc.DepDate_Document = ALG_DEPOSITOR.Open_Date;
  Summa = $0L;

  if(GetGE(doc))
    /*MSGBOX("нашли!");*/
    found=True;
    while((doc.Referenc == ALG_DEPOSITOR.Referenc) AND
          (doc.TypeOper == Зачисление_Процентов) AND
          (found == True))
        Summa = Summa + doc.InSum;
        if (not Next(doc))
          found=False;
        end;/* IF */
    end; /* WHILE */
  end; /* IF */
  /*MsgBox(String(Summa));*/
  Причислено = Summa;

  /*П.№4 Ищем сумму остатока                                               */
  Rewind(doc);
  KeyNum(doc,2);
  doc.Referenc = ALG_DEPOSITOR.Referenc;
  MaxDate = {curdate}+1;
  doc.DepDate_Document = MaxDate;
  doc.NumDayDoc = 0;
  Summa = $0;
  if(GetLE(doc))
    found=True;
    while((doc.Referenc == ALG_DEPOSITOR.Referenc) AND
          (doc.DepDate_Document <= MaxDate) AND
          (found == True))
      Summa = doc.Rest;
      found = False;
      if(found)
        if (not Next(doc))
          found=False;
        end;/* IF */
      end; /* IF */
    end; /* WHILE */
  end; /* IF */
  Остаток = Summa;
  if(ALG_SBDEPDOC.OutSum > (Остаток - Причислено))
    MsgBox("Вклад не пролежал 1 месяц. | Cумма причисленных %% не может быть выдана");
    ALG_RESULT = ERR_RES;
    return;
  end; /* IF */
end; /* IF */
ALG_RESULT = SKIP_RES;

Exit(1); /* Убрать вывод на экран */

return;
/*------------------------- конец тела макроса --------------------------*/
