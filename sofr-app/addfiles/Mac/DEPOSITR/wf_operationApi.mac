/*
$Name:               wf_operationApi.mac
$Module:             ┼╬
$Description:        wf_operationApi.mac
*/

import commonInter, rsexts, rsd, wf_log, wf_appUtils, order_const, ws_file;

/*************************************************
 *  Вннутренние функции
 ************************************************/

private macro isResident(partyId : Integer)
  var cmd = RsdCommand("select t_notResident from dparty_dbt where t_partyId = ?");
  cmd.addParam("p_partyId", RsdBp_In, partyId);
  cmd.execute;

  var rs = RsdRecordset(cmd);
  if (rs.moveNext)
    return rs.value(0) == StrFor(0);  
  end;
  return true;
end;

private macro setCommonVars(params : TParameters)
  var v;

  if (params.get("Нерезидент@Вып") == null)
    v = params.get("Клиент@Вып");
    if (v != null)
      var partyId = int(v);
      params.add("Нерезидент@Вып", not isResident(partyId)); 
    end;
  end;

  if (params.get("Валюта@НДС") == null)
    v = params.get("Валюта@Комиссия");
    if (v != null)
      params.add("Валюта@НДС", v);    
    end;
  end;
end;

private macro storeVars(params : TParameters)
  var p;
  for (p, params.asArray)
    storeValue("doc:" + p.Name, p.Value);
  end;
end;

private macro toHtml(text : String)
  return "<html>\n" + 
         "  <head>\n" +
         "    <meta http-equiv=\"Content-type\" CONTENT=\"text/html; charset=cp866\">\n" +
         "  </head>\n" +
         "  <body>\n" +
         "    <pre>\n" +
         text +
         "    </pre>\n" +
         "  </body>\n" +
         "</html>";
end;

/*************************************************
 *  createAccountingDocuments
 *  Создание РДД
 *
 *  [in]    workflowProcess
 *  [in]    operationReference
 *  [in]    applicationKind
 *  [in]    applicationKey
 *
 ************************************************/

macro createAccountingDocuments(paramStr : String) : String
  var inParams = TParameters(paramStr);

  makeDocBunchCurrent(inParams);
  setCommonVars(inParams); 
  storeVars(inParams);

  var ok = createAccountingDocs(inParams.get("applicationKind"), inParams.get("operationReference"));
  if (not ok)
    var errMessage;
    var errCode = topErrorMessage(errMessage);
    return handleResult(errCode, errMessage).toString;
  end;
  interDesk_endDocBunch;
  return handleResult(0).toString;
onError(e)
  interDesk_endDocBunch;
  return handleAppError(e, INTERNAL_ERROR).toString;     
end;

/*************************************************
 *  issueReports
 *  Выпуск отчетов
 *
 *  [in]    applicationKind
 *  [in]    applicationKey
 *
 ************************************************/

macro issueReports_prepare(paramStr : String) : String
  var inParams = TParameters(paramStr);
  var appKind = inParams.get("applicationKind");
  var appKey = inParams.get("applicationKey");
  
  var data = toHtml(issueReports_CreateData(appKind, appKey), true);
  var fc = CreateFileContainer("report.html", false, data);

  inParams.add("storageId", fc.storageId);
  inParams.add("reportText", fc.body);

  return uiCreateSession("OperationApi.issueReports", inParams).toString;
onError(e)
  //rtSetOutputPrefix(oldPrefix);
  //rtSetUniqueOutputFiles(oldUnique);
  return handleAppError(e, INTERNAL_ERROR).toString;     
end;

macro issueReports_inputReady(paramStr : String) : String
  return uiSimpleCheckInputReady(paramStr, "reportText");
end;

macro issueReports_getInput(paramStr : String) : String
  var sessId = uiGetSessionId(paramStr);
  var sessParams = uiGetSessionParams(sessId);
  if (sessParams != null)
    uiFormManager.reset;
    return handleResult(0).toString;
  end;
  return null;
end;

macro issueReports(paramStr : String) : String
  var inParams = TParameters(paramStr);
  var appKind = inParams.get("applicationKind");
  var appKey = inParams.get("applicationKey");
  
  var data = toHtml(issueReports_CreateData(appKind, appKey), true);
  var fc = CreateFileContainer("report.html", false, data);

  inParams.add("storageId", fc.storageId);
  inParams.add("reportText", fc.body);

  return inParams.toString;
onError(e)
  //rtSetOutputPrefix(oldPrefix);
  //rtSetUniqueOutputFiles(oldUnique);
  return handleAppError(e, INTERNAL_ERROR).toString;     
end;
