import DeprIntr, issrvdoc, sqlconv, rsd;

var ddep = TRecHandler( "i_dp_dep.rec" );
file rp( "rep_93i.dbt"  ) key 0 write;
file dr( "depositr.dbt" ) key 8;

var dd = TBfile( "sbdepdoc.dbt", "r", 5 );

var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

var depclnt = TClientList; /* Справочник вкладчиков */

const
  D_IN = 1,         /* Приход */
  D_OUT = 2,        /* Расход */
  D_GET = 3,        /* Списание */
  D_PUT = 5;        /* Зачисление */

const
  BS_BRANCH  = 0,  /* Филиал */
  BS_DEP     = 1,  /* Отделение */
  BS_EXCH    = 2,  /* Обменный пункт */
  BS_OPERDEP = 3;  /* Оперчасть */

var
  branch = numfncash,
  statusBranch,
  startDate, endDate;

private var 
    {Name_Bank},
    {NumberBranch},
    {Bank_OKPO};

macro define_brtyp;

  if( not findDepartment(branch, null, ddep) )
    exit( 1 );
  else
    statusBranch = ddep.rec.NodeType;
  end;
end;

macro filterBranch( num );

  if( statusBranch == I_DEPARTMENT_TYPE_FILIAL/*BS_DEP*/ )
    return true;
  else
    if( branch == num )
      return true;
    end;
  end;
  return false;
end;

macro isFType;

  dr.Referenc = dd.rec.Referenc;
  if( getEQ( dr ) )
    if( index( dr.UserTypeAccount, "Ф" ) )
      return true;
    end;
  end;
  return false;
end;

macro GetDiscountMROT;

  return $100.0;
end;

/* Определение резидентности клиента */
macro getClientCS( code );

  if ( depclnt.GetRecord( code ) )
    return depclnt.CurRec.rec.NotResident;
  else
    return "";
  end;
end;

macro getClientName( code );

  if ( depclnt.GetRecord( code ) )
    return depclnt.CurRec.rec.Name1 + " " +
           depclnt.CurRec.rec.Name2 + " " +
           depclnt.CurRec.rec.Name3;
  else
    return "";
  end;
end;

macro PrintDonos( Date, FIO );

  [








      код ОКПО кредитной организации:
      № #################### и дата
      ##############################







                                                    УВЕДОМЛЕНИЕ


      Настоящим уведомляем, что нерезидентом #
      ___________________________________________________________________________
      в нарушение пункта 4.5 Инструкции Банка России "О порядке открытия уполномоченными банками счетов
      нерезидентов в валюте Российской Федерации и проведения операций по этим счетам" совершена приходная
      операция с превышением установленного минимума в три тысячи минимальных размеров оплаты труда.



      Главный бухгалтер         __________________________ (Ф.И.О.)

                                  М. П.

      Исполнитель               ___________________________ (Ф.И.О.)

      Телефон                   ____________________________

  ]
  ( {Bank_OKPO}, Date:m:c, FIO:w );
end;

macro PrintReport( StartDate, EndDate );

var ClientName;

 var cmd, rs;  
  var namestr = "";
 cmd = RsdCommand("select rep.t_Date as tDate, rep.t_account as Account, rep.t_FIO as FIO,"+
                  " rep.t_TypeOper as TypeOper, rep.t_Sum1 as Sum1, dep.t_SpecialAccess as SpecialAccess "+
                  " from drep_93i_dbt rep, ddepositr_dbt dep where "+
                  " rep.T_ACCOUNT = dep.T_ACCOUNT and dep.t_fncash = "+String(Branch)
                  );
 cmd.execute();
 rs = RsdRecordset(cmd);


  namestr = {Name_Bank} + " " + {NumberBranch};
  if( statusBranch != I_DEPARTMENT_TYPE_FILIAL/*BS_DEP*/ )
    namestr = namestr + " Филиал " + ddep.rec.Name;
  end;

  [
      #
                                   Зачисления по счетам типа "Ф" на сумму свыше 3000 МРОТ
                                             за период с ########## по ##########

    -----------------------------------------------------------------------------------------------------------
    |     Дата   |         № счета        |           ФИО нерез.           | Операция(код) |      Сумма       |
    -----------------------------------------------------------------------------------------------------------
  ]
  ( namestr, StartDate, EndDate );
  rewind( rp );
  while  ( rs.moveNext() )
   if(  (not SpecialAccess) and OperMayListSpecialAccounts and rs.value("SpecialAccess"))
      ClientName = "XXXXXX X.X.";
   else
      ClientName = rs.value("FIO");
   end;   

    [ | ########## | ###################### | ############################## |      ###      | ################ |]
    ( Date(rs.value("tDate")), Acc_Form(String(rs.value("Account"))), ClientName,
      rs.value("TypeOper"), rs.value("Sum1") );
    [ |------------|------------------------|--------------------------------|---------------|------------------|];
  end;
  [

                                                                             Контролер_________________________
  ];
end;

macro FillRepFile( StartDate, EndDate );

  var stat, Num = 1, Str = "";
  var cmd;
  if( not open( rp ) )
     msgbox( "Ошибка при открытии файла rep_93i.dbt" );
     exit( 1 );
  end;
  cmd = RSDCommand("truncate table drep_93i_dbt");
  cmd.execute;
  Str = "t.T_Date_Document >= "      + sqlDateToStr( StartDate ) +
        " AND t.T_Date_Document <= " + sqlDateToStr( EndDate ) +
        " AND t.T_FNcash = "         + String( Branch ) +
        " AND t.T_KindOp = 5";
  dd.addFilter( Str );
  dd.rec.Date_Document = StartDate;
  stat = getGE( dd );
  while( stat and ( dd.rec.Date_Document <= EndDate ) )
    Str = "  " + String(Num:7) + ": Документ по счету " + Acc_Form( dd.rec.Account );
    Message( Str );
    Num = Num + 1;
    if( IsServDoc( dd.rec )            AND
        filterBranch( dd.rec.FNcash )  AND
        isFType()                      AND        /* Счет типа "Ф" */
        ( getClientCS( dr.CodClient ) != "" ) )   /* Клиент - нерезидент */
      if ( ( dd.rec.KindOp == D_PUT )  AND
           ( dd.rec.InSum > 3000 * GetDiscountMROT() ) )
        clearrecord( rp );
        rp.Date = dd.rec.Date_Document;
        rp.Account = dd.rec.Account;
        rp.FIO = getClientName( dd.rec.CodClient );
        rp.TypeOper = dd.rec.KNFCode;
        rp.Sum1 = dd.rec.InSum;
        stat = insert( rp );
        if( not stat )
          msgbox( "Ошибка при вставке в файл rep_93i.dbt" );
          exit( 1 );
        end;
      end;
    end;
    stat = next( dd );
  end;
  dd.dropFilter();
end;
