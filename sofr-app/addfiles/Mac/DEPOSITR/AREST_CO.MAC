/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Операция по снятию ареста со счета вкладчика                            *
*                                                                          *
\**************************************************************************/

import deprintr, alg_vars;

file depo (depositr);

record depositr_pre (depositr);

const UPDATE_ALL  = 1;
const UPDATE_DEP  = 2;

var FlagPlan         = false,
    FlagPlanFinished = false;

const LogSC_Update      = 3;
const LogSC_sb_depositr = 501;


/**************************************************************************\
| Бегает по dplanpay и вызывает макросы                                    |
\**************************************************************************/
private macro forAllPPays( macroName )

  var stat,
      macroStat = true,
      dppay = TBFile( "dplanpay.dbt", "W" );

  dppay.rewind(); dppay.clear();

  dppay.rec.IsCur      = ALG_DEPOSITOR.IsCur;
  dppay.rec.FNCash     = ALG_DEPOSITOR.FNCash;
  dppay.rec.Open_Close = "A";
  dppay.rec.Account    = ALG_DEPOSITOR.Account;
  stat = dppay.getGE();
  while( ( stat                 == true                  ) and
         ( dppay.rec.IsCur      == ALG_DEPOSITOR.IsCur   ) and
         ( dppay.rec.FNCash     == ALG_DEPOSITOR.FNCash  ) and
         ( dppay.rec.Open_Close == "A"                   ) and
         ( dppay.rec.Account    == ALG_DEPOSITOR.Account )    )
    /* ПП закрыто по аресту */
    if( dppay.rec.Code_Currency == ALG_DEPOSITOR.Code_Currency )
      if( not execMacro2( macroName, dppay, macroStat ) )
        return macroStat;
      end;
    else
      stat = dppay.next();
    end;
  end;

  return macroStat;

end;

/**************************************************************************\
| Устанавливает флаги FlagPlan/FlagPlanFinished                            |
\**************************************************************************/
private macro setFlags( dppay, macroStat )

  FlagPlan = true;
  if( ( dppay.rec.End_DatePay != date( 0, 0, 0 ) ) and
      ( dppay.rec.End_DatePay <  {curdate}       )    )
    FlagPlanFinished = true;
  end;

  if( ( FlagPlan == true ) and ( FlagPlanFinished == true ) )
    return false;
  else
    return dppay.next();
  end;

end;

/**************************************************************************\
| Открывает плановое перечисление                                          |
\**************************************************************************/
private macro openPPay( dppay, macroStat )

  var stat = true;
  
  macroStat = true;

  if( ( dppay.rec.End_DatePay == date( 0, 0, 0 ) ) or
      ( dppay.rec.End_DatePay >= {curdate}       ) or
      ( FlagPlanFinished      == true            )   )
      dppay.rec.Open_Close = strFor( 0 );
      macroStat = dppay.update();
      if( macroStat )
        dppay.rec.IsCur      = ALG_DEPOSITOR.IsCur;
        dppay.rec.FNCash     = ALG_DEPOSITOR.FNCash;
        dppay.rec.Open_Close = "A";
        dppay.rec.Account    = ALG_DEPOSITOR.Account;
        stat                 = dppay.getGE();
      end;
  else
    stat = dppay.next();
  end;

  setParm( 1, macroStat );
  if( not macroStat )
    return false;
  else
    return stat;
  end;

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro askForPPays

  forAllPPays( @setFlags );

  if( ( FlagPlan                                                       == true ) and
      ( GetTrue( true, "Открыть арестованные плановые перечисления?" ) == true )    )
    if( ( FlagPlanFinished                                                                      == true ) and
        ( GetTrue( TRUE, "Сроки некоторых плановых перечислений уже закончились. Открыть их?" ) == true )    )
    else
      FlagPlanFinished = false;
    end;
  else
    FlagPlan         = false;
    FlagPlanFinished = false;
  end;

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro TrnProc

  var stat            = TRUE;
  var ret             = TRUE;
  var UserTypeAccount = "";
  var Symb            = "";
  var i               = 1;

  if (stat)
    Copy(depositr_pre,ALG_DEPOSITOR);
  end;
  
  if (stat)
    i = 1;
    while (i <= StrLen(ALG_DEPOSITOR.UserTypeAccount))
      Symb = SubStr(ALG_DEPOSITOR.UserTypeAccount,i,1);
      if (Symb != "Т")
        UserTypeAccount = UserTypeAccount + Symb;
      end;
      i = i + 1;
    end;
    ALG_DEPOSITOR.UserTypeAccount = UserTypeAccount;
    ALG_DEPOSITOR.NumSession      = 0;
    if ( ALG_DEPOSITOR.Action == 0 )
      ALG_DEPOSITOR.Action        = 1;
    end;
    stat = UpdateAlgRecords(UPDATE_DEP,TRUE);
  end;

  if (stat)
    WriteChronologicalLog(LogSC_Update,LogSC_sb_depositr,ALG_DEPOSITOR,0,depositr_pre,0);
  end;

  /* Открытие арестованных плановых перечислений */
  if ( FlagPlan  AND  stat )
    stat = forAllPPays( @openPPay );
  end;

  if (NOT stat)
    AbortTrn();
  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  ALG_RESULT = OK_RES;

  if (ALG_RESULT == OK_RES)
    if ( Index( ALG_DEPOSITOR.UserTypeAccount, "Т" ) == 0 )
      MsgBox("Счет не арестован");
      ALG_RESULT = END_RES;
    end;
  end;

  if (ALG_RESULT == OK_RES)
    askForPPays();
  end;

  if (ALG_RESULT == OK_RES)
    if (ProcessTrn(1,"TrnProc",depo))
      ALG_RESULT = SKIP_RES;
      [
        Операция завершилась успешно.
        Арест снят со счета.
      ];
    else
      MsgBox("Ошибка во время проведения операции");
      ALG_RESULT = ERR_RES;
    end;
  end;

end;
