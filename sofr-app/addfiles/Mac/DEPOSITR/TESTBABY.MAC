/*
  R-Style Software Lab.

  RS-Bank 6.0
  RS-Retail

  Макрос шага "Проверки" операций закрытия "Целевого на детей".

  Ромодин Александр Васильевич
  Доработан 10.02.2004
*/
Import DeprIntr,alg_vars;

//rivate record ALG_DEPOSITOR (depositr);
//private record ALG_SBDEPDOC  (sbdepdoc);

private const DateNull = Date (0, 0, 0);

private const ACCOUTN_OWNER    = StrFor(0); // Владелец счета
private const ACCOUTN_PROXY    = StrFor(1); // Доверенное лицо
private const ACCOUNT_IMPORTER = StrFor(4); // Вноситель
private const ACCOUNT_TUTOR    = StrFor(5); // Опекун
private const ACCOUNT_PARENT   = StrFor(9); // Родитель

private const ACCOUTN_HEIR        = StrFor( 2); // Наследник - выдачи
private const ACCOUTN_HEIR_PAY    = StrFor( 7); // Наследник - выплаты
private const ACCOUNT_PARENT_WILL = StrFor(11); // Родитель наследника
private const ACCOUNT_TUTOR_WILL  = StrFor(12); // Опекун наследника
private const ACCOUTN_BURIAL_PAY  = StrFor(14); // Выплата "На достойные похороны"


  const UPDATE_DEP  = 2,  /* Обновить счет   */
        UPDATE_DOC  = 4;  /* Обновить документ  24 мая 2006 _ Макашов  */


var DepClient = TClientList;

// #########################################################

macro ПолныхЛет (d1:date,d2:date)
// Разность d1 - d2  в полных годах
  private var day1,mon1,year1,
              day2,mon2,year2;
          var Res;
  if ((d1 != DateNull) AND (d2 != DateNull))
    DateSplit (d1,day1,mon1,year1);
    DateSplit (d2,day2,mon2,year2);
    Res = year1 - year2;
    if (Res > 0)
      if (mon2 > mon1)
        Res = Res - 1;
      elif ((mon2 == mon1) AND (day2 > day1))
        Res = Res - 1;
      end;
    end;
  else
    Res = 1000; // Фикция - считаем, что договор завершен
  end;
  return Res;
end;
// #########################################################

  private var ВкладчикуПолныхЛет    = 0;

  if ((ALG_SBDEPDOC.Author == ACCOUTN_HEIR       )
   OR (ALG_SBDEPDOC.Author == ACCOUTN_HEIR_PAY   )
   OR (ALG_SBDEPDOC.Author == ACCOUNT_PARENT_WILL)
   OR (ALG_SBDEPDOC.Author == ACCOUNT_TUTOR_WILL )
   OR (ALG_SBDEPDOC.Author == ACCOUTN_BURIAL_PAY ))
//  Выплата наследникам- вкладчик должен числиться умершим
    if (DepClient.GetRecord (ALG_DEPOSITOR.CodClient))
      if (DepClient.CurRec.rec.DateDeath != DateNull)
        ALG_RESULT = OK_RES;  // Раз вкладчик умер- получают напследники 
      else
        MsgBox ("Вкладчик не числится умершим!");
        ALG_RESULT = ERR_RES;
      end;
    else
      MsgBox ("Не найден вкладчик в базе данных");
      ALG_RESULT = ERR_RES;
    end;
  else
//  Вкладчик жив
    if (ALG_DEPOSITOR.End_DateDep == DateNull)  // Договор завершен 
      if ((ALG_SBDEPDOC.Author == ACCOUTN_OWNER)
       OR (ALG_SBDEPDOC.Author == ACCOUTN_PROXY))
        ALG_RESULT = OK_RES;  // Закрывать может только владелец 
      else
        MsgBox ("Закрыть счет может только вкладчик (владелец счета)");
        ALG_RESULT = ERR_RES;
      end;
    else  // Условия договора не выполнены 
          // Здесь получатель зависит от многих условий

         SetBitFlag( ALG_SBDEPDOC.Flags, 9, 1 );  /* Макашов  */
         ALG_UPDATE = UPDATE_DOC;                 /* Макашов  */


      if (DepClient.GetRecord (ALG_DEPOSITOR.CodClient))
        ВкладчикуПолныхЛет    = ПолныхЛет (ALG_SBDEPDOC.DepDate_Document,DepClient.CurRec.rec.Born);
        if (ВкладчикуПолныхЛет < 14)
          if ((ALG_SBDEPDOC.Author == ACCOUNT_IMPORTER)
           OR (ALG_SBDEPDOC.Author == ACCOUNT_TUTOR   )
           OR (ALG_SBDEPDOC.Author == ACCOUNT_PARENT  ))
            ALG_RESULT = OK_RES;
          else
            MsgBox ("До 14 лет закрыть счет может только вноситель или родитель");
            ALG_RESULT = ERR_RES;
          end;
        elif (ВкладчикуПолныхЛет < 16)
          if ((ALG_SBDEPDOC.Author == ACCOUNT_IMPORTER)
           OR (ALG_SBDEPDOC.Author == ACCOUNT_TUTOR   )
           OR (ALG_SBDEPDOC.Author == ACCOUNT_PARENT  ))
            ALG_RESULT = OK_RES;
          elif (ALG_SBDEPDOC.Author == ACCOUTN_OWNER)
            if (GetTrue(False, "Подтвердите наличие согласия родителей"))
              ALG_RESULT = OK_RES;
            else
              MsgBox ("До 16 лет должно быть согласие родителей");
              ALG_RESULT = ERR_RES;
            end;
          else
            MsgBox ("До 16 лет закрыть счет может только вноситель, родитель или вкладчик");
            ALG_RESULT = ERR_RES;
          end;
        elif (ВкладчикуПолныхЛет < 18)
          if ((ALG_SBDEPDOC.Author == ACCOUTN_OWNER)
           OR (ALG_SBDEPDOC.Author == ACCOUTN_PROXY))
            if (GetTrue(False, "Подтвердите наличие согласия родителей"))
              ALG_RESULT = OK_RES;
            else
              MsgBox ("До 18 лет должно быть согласие родителей");
              ALG_RESULT = ERR_RES;
            end;
          else
            MsgBox ("Закрыть счет может только вкладчик или доверенное лицо");
            ALG_RESULT = ERR_RES;
          end;
        else  // Получает сам вкладчик
          if ((ALG_SBDEPDOC.Author == ACCOUTN_OWNER)
           OR (ALG_SBDEPDOC.Author == ACCOUTN_PROXY))
            ALG_RESULT = OK_RES;  // Закрывать может только владелец 
          else
            MsgBox ("Закрыть счет может только вкладчик (владелец счета)");
            ALG_RESULT = ERR_RES;
          end;
        end;
      else
        MsgBox ("Не найден вкладчик в базе данных");
        ALG_RESULT = ERR_RES;
      end;
    end;
  end;
end;  
