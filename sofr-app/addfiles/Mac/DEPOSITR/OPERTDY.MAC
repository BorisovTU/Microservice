/*
  RS-Bank
  Частные вклады для Сбербанка

  Формирование Операционного дневника

  23.09.96
  Ромодин Александр Васильевич  ... и прочие ребяты - Клепиков С.А.("Масдай")
*/
import deprintr,BankInter,IsSrvDoc,cur_rate;

file DepParm ("depparm.dbt")  key 0;
file doc     ("sbdepdoc.dbt");
file prs     ("person.dbt", "bank.def" );
file GroupMember ("groupmem.dbt") key 0;
file dr      ("depositr.dbt");
file cr      ("currency.dbt");
file drest   ("sb_drest.dbt");
file dtyp    ("sb_dtyp.dbt");

file CashDoc ("sb_casdc.dbt") key 1;

const
  D_AR            = 8,    /* проводка в архиве      */
  D_ARC           = 9,    /* корректировка в архиве */
  D_AUDIT_STORN   = 14,   /* Сторнирование операции */
  D_AUDIT_DELETE  = 15,   /* Удаление операции      */
  D_AUDIT_CORRECT = 16,   /* Корректировка операции */
  D_DELETE        = 2;

const FlagCur = NumFlagCur();
const FNcash  = NumFNcash();
const Mode    = GetCurrentMode();
const MaxLenFIO = 30;
const ServAcntName = "Служебный";
const FictOper = -1;

var CountCur;             /* Количество валют в currency.dbt */
array CodeCur,ShortNmCur; /* Здесь хранятся коды валют и их названия */
array RateCBCur;
var CurCurrency;          /* Индекс текущей валюты в массиве валют */
var CurCurrencyCode;      /* Код текущей валюты */
var CurCurrencyName;      /* Название текущей валюты */
var CurCurrencyRateCB;
var Branch = NumFNCash;
var ClientList = TClientList;


array SumOperInTAcc;      /* Итого по операционисту Наличные.Приход */
array SumOperOutTAcc;     /* по типам вклада        Наличные.Расход */
array SumBeznInTAcc;      /*                        Безналичные.Зачисление */
array SumBeznOutTAcc;     /*                        Безналичные.Списание */
array SumPercRestTAcc;    /*                        Остаток %% */
array SumPercTAcc;        /*                        %% по операции */
array SumRestTAcc;        /*                        Остаток вклада */

array SumOperIn;          /* Итого по операционисту Наличные.Приход */
array SumOperOut;         /*                        Наличные.Расход */
array SumBeznIn;          /*                        Безналичные.Зачисление */
array SumBeznOut;         /*                        Безналичные.Списание */
array SumPercRest;        /*                        Остаток %% */
array SumPerc;            /*                        %% по операции */
array SumRest;            /*                        Остаток вклада */

array SumSumOperIn;       /* Сумма по операционистам Наличные.Приход */
array SumSumOperOut;      /*                         Наличные.Расход */
array SumSumBeznIn;       /*                         Безналичные.Зачисление */
array SumSumBeznOut;      /*                         Безналичные.Списание */
array SumSumPercRest;     /*                        Остаток %% */
array SumSumPerc;         /*                        %% по операции */
array SumSumRest;         /*                        Остаток вклада */

var c_cas_sch;
var c_ink_sch;
var cas_sch;
var ink_sch;
var Isx_Ost;              /* Исходящий остаток */
var Vx_Ost;               /* Входящий остаток */
var Sum_KO_Pr;            /* Сумма кассовых операций приходная */
var Sum_KO_Ras;           /*                         расходная */

array opName;

macro GetBranchName

  var
    RetVal;

  if(not findDepartment(Branch, RetVal))
    RetVal=String(Branch);
  end;
  return RetVal;
end;

macro Cur2Rub(Cur,Sum)

  return CurCurrencyRateCB * Sum;

end;

macro GetDiaryNumber();

  DepParm.FNCash = Branch;
  DepParm.FlagCur = 0;
  if ( not ( GetGE( DepParm )
    and ( DepParm.FNCash == Branch )
    and ( DepParm.FlagCur == 0 ) ) )
    MsgBox("Ошибка чтения параметров филиала");
    return 0;
  else
    return DepParm.NumOperDy;
  end;
end;


/***********************************************************/
macro Типы_Валют(DateDoc);

/* Определение количества и типа валют, а также номера кассового
   счета в зависимости от FlagCur */

  var stat;
  var rateCB = 0.0;

  if (FlagCur == 0)
    c_cas_sch = "ССС000001";
    c_ink_sch = "ССС000004";
  else
    c_cas_sch = "0099999S00001/";
    c_ink_sch = "0099999S00004/";
  end;

  CountCur = 0;
  KeyNum(cr,0);
  ReWind(cr);

  if (FlagCur == 0)
    cr.Code_Currency = 0;
    stat = getEQ(cr);
    while (stat)
      if (cr.Code_Currency == 0)
        CountCur = CountCur + 1;
        CodeCur(CountCur) = cr.Code_Currency;
        ShortNmCur(CountCur) = cr.Short_Name;
        RateCBCur( CountCur ) = 1.0;
      end;
      stat = Next(cr);
    end
  else
    cr.Code_Currency = 1;
    stat = getGE(cr);
    while (stat)
      if ( GetAllCurrRate( DateDoc, cr.Code_Currency, rateCB ) )
        CountCur = CountCur + 1;
        CodeCur(CountCur) = cr.Code_Currency;
        ShortNmCur(CountCur) = cr.Short_Name;
        RateCBCur( CountCur ) = rateCB;
      end;
      stat = Next(cr);
    end;
  end;
end;

/***********************************************************/
macro Обнуление_Сумм_Сумм

  var i;

  i = 1;
  while (i<=CountCur)
    SumSumOperIn(i)  = $0.00;
    SumSumOperOut(i) = $0.00;
    SumSumBeznIn(i)  = $0.00;
    SumSumBeznOut(i) = $0.00;
    SumSumPercRest(i) = $0.00;
    SumSumPerc(i) = $0.00;
    SumSumRest(i) = $0.00;
    i = i + 1;
  end;
end;

/***********************************************************/
macro Расчет_Сумм_Сумм

  var i;

  i = 1;
  while (i<=CountCur)
    SumSumOperIn(i)  = SumSumOperIn(i) + SumOperIn(i);
    SumSumOperOut(i) = SumSumOperOut(i) + SumOperOut(i);
    SumSumBeznIn(i)  = SumSumBeznIn(i) + SumBeznIn(i);
    SumSumBeznOut(i) = SumSumBeznOut(i) + SumBeznOut(i);
    SumSumPercRest(i) = SumSumPercRest(i) + SumPercRest(i);
    SumSumPerc(i) = SumSumPerc(i) + SumPerc(i);
    SumSumRest(i) = SumSumRest(i) + SumRest(i);
    i = i + 1;
  end;
end;

/***********************************************************/
macro Обнуление_Сумм_по_Типу_Вклада

  var i;

  i = 1;
  while (i<=CountCur)
    SumOperInTAcc(i)  = $0.00;
    SumOperOutTAcc(i) = $0.00;
    SumBeznInTAcc(i)  = $0.00;
    SumBeznOutTAcc(i) = $0.00;
    SumPercRestTAcc(i) = $0.00;
    SumPercTAcc(i) = $0.00;
    SumRestTAcc(i) = $0.00;
    i = i + 1;
  end
end;

/***********************************************************/
macro Обнуление_Сумм

  var i;

  i = 1;
  while (i<=CountCur)
    SumOperIn(i)  = $0.00;
    SumOperOut(i) = $0.00;
    SumBeznIn(i)  = $0.00;
    SumBeznOut(i) = $0.00;
    SumPercRest(i) = $0.00;
    SumPerc(i) = $0.00;
    SumRest(i) = $0.00;
    i = i + 1;
  end
end;

/***********************************************************/
macro Разделитель_1
  if (FlagCur == 0)
    [---------------------------------------------------------------------------------------------------------------------------------------];
  else
    [-------------------------------------------------------------------------------------------------------------------------------------------------------------];
  end;
end;

/***********************************************************/
macro Заголовок_по_операционисту( Oper )
  [ ];
  [ ];
  if( Oper )
  [     Операционист #### #](prs.Oper, prs.Name);
  end;
  Разделитель_1 ();
  if (FlagCur == 0)
    [     № лицевого       |        Содержание операции       |        Наличные     |Мемориальные обороты |      Проценты       | Остаток   |];
    [       счета          |                                  |  Приход  |   Расход |Зачисление| Списание |  Сумма   | Остаток  |  вклада   |];
  else
    [     № лицевого       |     Содержание    |    Наличные в рублях    | Мемор. обор. в рублях   |       Наличные      |Мемориальные обороты |    Проценты     |];
    [       счета          |      операции     |  Приход    |   Расход   | Зачисление |  Списание  |  Приход  |  Расход  |Зачисление| Списание | Сумма  |Остаток |];
  end;
  Разделитель_1 ();
end;


/***********************************************************/
macro OutOpNameTail

  var i=1;

  while(i<asize(opName))
    if (FlagCur == 0)
    [                       ##################################](opName(i));
    else
    [                       ###################](opName(i));
    end;
    i=i+1;
  end;
end;
/***********************************************************/
macro FormGround

  var Ground = "";
  file OpTypes ("sb_typop.dbt");
  file SubOper ("suboper.dbt");

  if (StrLen(Trim(doc.ground)) > 0)
    if (((doc.TypeOper ==  4) AND (doc.TypeComplexOper == 10)) OR
        ((doc.TypeOper == 63) AND (doc.TypeComplexOper == 81))   )
      KeyNum(OpTypes,0);
      ReWind(OpTypes);
      OpTypes.IsCur    = doc.IsCur;
      OpTypes.Kind     = doc.Type_Account;
      OpTypes.NumOpert = doc.TypeComplexOper;
      if (GetEQ(OpTypes))
        Ground = OpTypes.czNameAlg;
      end;
    else
      Ground = "";
    end;
    Ground = doc.ground + " " + Ground;
  else
    KeyNum(OpTypes,0);
    ReWind(OpTypes);
    OpTypes.IsCur    = doc.IsCur;
    OpTypes.Kind     = doc.Type_Account;
    OpTypes.NumOpert = doc.TypeOper;
    if (GetEQ(OpTypes))
      Ground = OpTypes.czNameAlg + " ";
    end;
    if (doc.ApplType > 0)
      KeyNum(SubOper,1);
      ReWind(SubOper);
      SubOper.IsCur    = doc.IsCur;
      SubOper.Kind     = doc.Type_Account;
      SubOper.OperType = doc.TypeOper;
      SubOper.Type     = doc.ApplType;
      if (GetEQ(SubOper))
        Ground = Ground + SubOper.Name;
      end;
    end;
  end;

  if (StrLen(Ground) == 0)
    Ground = " ";
  end;

  return Ground;

end;

/***********************************************************/
macro Строка_отчета

  var FIO;
  var Ground;

  if ( ClientList.CurRec.Rec.SpecialAccess )
    FIO = dtyp.Kind;
  else
    FIO = ClientList.CurRec.ConvertFIO;
  end;

  Ground = FormGround();
  Ground = Ground + " " + FIO;
  if (FlagCur == 0)
    StrSplit(Ground,opName,35);
  else
    StrSplit(Ground,opName,18);
  end;

  if (FlagCur == 0)
    if (doc.InSum != 0) /* Приход       */
      if (doc.VidDoc != 0)/* Наличные   */
    [####################### ################################# ##########                                  ########## ########## ###########]
         (Acc_Form(doc.Account),opName(0), /*FIO,*/ doc.InSum:10:2:z, doc.PercOprSum:z, doc.PercRest:z, doc.Rest:z);
      else              /* Безналичные  */
    [####################### #################################                       ##########            ########## ########## ###########]
         (Acc_Form(doc.Account),opName(0), /*FIO,*/ doc.InSum:10:2:z, doc.PercOprSum:z, doc.PercRest:z, doc.Rest:z);
      end;
    else                        /* Расход       */
      if (doc.VidDoc != 0)/* Наличные   */
    [###################### ##################################            ##########                       ########## ########## ###########]
         (Acc_Form(doc.Account),opName(0), /*FIO,*/ doc.OutSum:10:2:z, doc.PercOprSum:z, doc.PercRest:z, doc.Rest:z);
      else              /* Безналичные  */
    [###################### ##################################                                  ########## ########## ########## ###########]
         (Acc_Form(doc.Account),opName(0), /*FIO,*/ doc.OutSum:10:2:z, doc.PercOprSum:z, doc.PercRest:z, doc.Rest:z);
      end;
    end;
  else
    if (doc.InSum != 0) /* Приход       */
      if (doc.VidDoc != 0)/* Наличные   */
    [###################### ##################  ############                                        ##########                                  ######## ########]
         (Acc_Form(doc.Account),opName(0), /*FIO,*/Cur2Rub(CurCurrency,doc.InSum):12:2:z, doc.InSum:10:2:z, doc.PercOprSum:z, doc.PercRest:z);
      else              /* Безналичные  */
    [###################### ##################                            ############                                    ##########            ######## ########]
         (Acc_Form(doc.Account),opName(0), /*FIO,*/Cur2Rub(CurCurrency,doc.InSum):12:2:z, doc.InSum:10:2:z, doc.PercOprSum:z, doc.PercRest:z);
      end;
    else                        /* Расход       */
      if (doc.VidDoc != 0)/* Наличные   */
    [###################### ##################               ############                                      ##########                       ######## ########]
         (Acc_Form(doc.Account),opName(0), /*FIO,*/Cur2Rub(CurCurrency,doc.OutSum):12:2:z, doc.OutSum:10:2:z, doc.PercOprSum:z, doc.PercRest:z);
      else              /* Безналичные  */
    [###################### ##################                                         ############                                  ########## ######## ########]
         (Acc_Form(doc.Account),opName(0), /*FIO,*/Cur2Rub(CurCurrency,doc.OutSum):12:2:z, doc.OutSum:10:2:z, doc.PercOprSum:z, doc.PercRest:z);
      end;
    end;
  end;
  OutOpNameTail;
end;

/***********************************************************/
macro Подкачка_по_счету
/*
  Находит информацию по данному счету и по клиенту

  23.09.96
*/
  var stat = TRUE;

  /*VZ 22.11.96 изменил ключ: не попадали закpытые счета*/
/*  KeyNum (dr,8);
  dr.IsCur = FlagCur;
  dr.FNCash = FNcash;
  dr.Type_Account = doc.Type_Account;
  dr.Code_Currency = doc.Code_Currency;
  dr.Account = doc.Account;
  stat = getEQ (dr);
  if (stat)   */
  if(doc.Type_Account == ServAcntName)
    /*прокидываем данные по служебным счетам*/
     stat=FALSE;
  end;
  if(stat)
    if( doc.CodClient != 0)
      stat = ClientList.GetRecord( doc.CodClient );
    else
     KeyNum (dtyp, 2);
     dtyp.FlagCur = FlagCur;
     dtyp.Kind = doc.Type_Account;
     stat = getEQ (dtyp);
     if(stat)
       ClientList.CurRec.Rec.Name1 = SubStr(dtyp.Name,1,25);
       ClientList.CurRec.Rec.Name2 = "";
       ClientList.CurRec.Rec.Name3 = "";
     end;
    end;
  end;
  return stat;
end;

/***********************************************************/
macro Печать_Сумм_Сумм


  if (  (SumSumOperIn(CurCurrency)  > $0.00)
     OR (SumSumOperOut(CurCurrency) > $0.00)
     OR (SumSumBeznIn(CurCurrency)  > $0.00)
     OR (SumSumBeznOut(CurCurrency) > $0.00)
     OR (SumSumPercRest(CurCurrency)  > $0.00)
     OR (SumSumPerc(CurCurrency) > $0.00)
     OR (SumSumRest(CurCurrency) > $0.00)
     )
    if (FlagCur == 0)
    [ Сумма общая                                              ########## ########## ########## ########## ##########]/* ########## ###########]*/
       (SumSumOperIn(CurCurrency):10:2,
        SumSumOperOut(CurCurrency):10:2,
        SumSumBeznIn(CurCurrency):10:2,
        SumSumBeznOut(CurCurrency):12:2,
        SumSumPerc(CurCurrency):8:0);
/*
        SumSumPercRest(CurCurrency):8:0,
        SumSumRest(CurCurrency):9:0);
*/
    else
    [ Сумма общая                             ############## ############ ############ ############ ########## ########## ########## ########## ########]/* ########]*/
      (Cur2Rub(CurCurrency,SumSumOperIn(CurCurrency)):14:2,
       Cur2Rub(CurCurrency,SumSumOperOut(CurCurrency)):12:2,
       Cur2Rub(CurCurrency,SumSumBeznIn(CurCurrency)):12:2,
       Cur2Rub(CurCurrency,SumSumBeznOut(CurCurrency)):12:2,
       SumSumOperIn(CurCurrency):10:2,
       SumSumOperOut(CurCurrency):10:2,
       SumSumBeznIn(CurCurrency):10:2,
       SumSumBeznOut(CurCurrency):10:2,
       SumSumPerc(CurCurrency):8:0);
/*
       SumSumPercRest(CurCurrency):8:0);
*/
    end;
    Разделитель_1;
  end;

end;

/***********************************************************/
macro Печать_Сумм_по_Операционисту

  if ((SumOperIn(CurCurrency) > $0.00) OR (SumOperOut(CurCurrency) > $0.00) OR
     (SumBeznIn(CurCurrency) > $0.00) OR (SumBeznOut(CurCurrency) > $0.00) OR
     (SumPercRest(CurCurrency) > $0.00) OR (SumPerc(CurCurrency) > $0.00)  OR
     (SumRest(CurCurrency) > $0.00)
  )
    if (FlagCur == 0)
    [ Сумма по операционисту                                   ########## ########## ########## ########## ########]/* ######## ###########]*/
      (SumOperIn(CurCurrency):10:2,
       SumOperOut(CurCurrency):10:2,
       SumBeznIn(CurCurrency):10:2,
       SumBeznOut(CurCurrency):10:2,
       SumPerc(CurCurrency):8:0);
/*
       SumPercRest(CurCurrency):8:0,
       SumRest(CurCurrency):9:0);
*/
    else
    [ Сумма по операционисту                  ############## ############ ############ ############ ########## ########## ########## ########## ########]/* ########]*/
      (
       Cur2Rub(CurCurrency,SumOperIn(CurCurrency)):14:2,
       Cur2Rub(CurCurrency,SumOperOut(CurCurrency)):12:2,
       Cur2Rub(CurCurrency,SumBeznIn(CurCurrency)):12:2,
       Cur2Rub(CurCurrency,SumBeznOut(CurCurrency)):12:2,
       SumOperIn(CurCurrency):10:2,
       SumOperOut(CurCurrency):10:2,
       SumBeznIn(CurCurrency):10:2,
       SumBeznOut(CurCurrency):10:2,
       SumPerc(CurCurrency):8:0);
/*
       SumPercRest(CurCurrency):8:0);
*/
    end;
    Разделитель_1;
  end;
end;

/***********************************************************/
macro Печать_Сумм_по_Типу_Вклада
/*VZ 19.11.96 Не печатать сумму остатков вкладов и %%*/


  if(   (SumOperInTAcc(CurCurrency) > $0.00)
    OR (SumOperOutTAcc(CurCurrency) > $0.00)
    OR (SumBeznInTAcc(CurCurrency)  > $0.00)
    OR (SumBeznOutTAcc(CurCurrency)  > $0.00)
    OR (SumPercRestTAcc(CurCurrency) > $0.00)
    OR (SumPercTAcc(CurCurrency) > $0.00)
    OR (SumRestTAcc(CurCurrency) > $0.00)
    )
    Разделитель_1;
    if (FlagCur == 0)
    [ Сумма по виду вклада                                     ########## ########## ########## ########## ##########] /* ########## ###########]*/
      (SumOperInTAcc(CurCurrency):10:2,
       SumOperOutTAcc(CurCurrency):10:2,
       SumBeznInTAcc(CurCurrency):10:2,
       SumBeznOutTAcc(CurCurrency):10:2,
       SumPercTAcc(CurCurrency):8:0);
/*
       SumPercRestTAcc(CurCurrency):8:0,
       SumRestTAcc(CurCurrency):9:0);
*/
    else
    [ Сумма по виду вклада                    ############## ############ ############ ############ ########## ########## ########## ########## ########] /* ########]*/
      (
       Cur2Rub(CurCurrency,SumOperInTAcc(CurCurrency)):14:2,
       Cur2Rub(CurCurrency,SumOperOutTAcc(CurCurrency)):12:2,
       Cur2Rub(CurCurrency,SumBeznInTAcc(CurCurrency)):12:2,
       Cur2Rub(CurCurrency,SumBeznOutTAcc(CurCurrency)):12:2,
       SumOperInTAcc(CurCurrency):10:2,
       SumOperOutTAcc(CurCurrency):10:2,
       SumBeznInTAcc(CurCurrency):10:2,
       SumBeznOutTAcc(CurCurrency):10:2,
       SumPercTAcc(CurCurrency):8:0);
/*
       SumPercRestTAcc(CurCurrency):8:0);
*/
    end;
  Разделитель_1();
  end;


end;

/***********************************************************/
macro FindOper
(
 Oper
)

  var stat;

  KeyNum (prs,0);
  ReWind (prs);

  prs.Oper = Oper;

  stat = GetEQ(prs);
  if ( not stat )
    prs.Name = "";
  end;

  return stat;

end;


macro CheckOper( Oper )
     if( doc.Oper == Oper )
       return true;
     end;
  return false;
end;

/***********************************************************/
macro Цикл_по_документам (DateDoc, Oper)
/*
  Организует цикл по документам

  23.09.96
  VZ 03.06.98 Глобальная переделка - группировка по операционисту ведется
              внутри данной макрофункции
*/
  var stat;
  var i;
  var Type_Account_Pred,Oper_Pred, MaxOperNum;
  var NamTyp = TRUE;      /*        FALSE;   */

  Обнуление_Сумм();
  Обнуление_Сумм_по_Типу_Вклада();
  KeyNum (doc,0);
  ReWind(doc);
  doc.IsCur = FlagCur;
  doc.FNCash = FNcash;
  doc.Date_Document = DateDoc;
  doc.Type_Account = "";
  doc.Code_Currency = 0;
  doc.TypeOper = 0;
  stat = getGE (doc);
  Type_Account_Pred = doc.Type_Account;
  if( Oper ) /* задан конкретный операционист*/
    Oper_Pred = FictOper;
    MaxOperNum = Oper;
  else
    Заголовок_по_операционисту(Oper);
  end;
  while (stat
         AND (doc.IsCur==FlagCur)
         AND (doc.FNCash==FNcash)
         AND (doc.Date_Document==DateDoc))
    if( NOT Oper OR (doc.Oper == Oper) )
     /*Исключаем служебные счета и архивные "следы"*/
      if( (doc.Mode == Mode)
        AND Подкачка_по_счету()
        AND (Int(doc.Code_Currency)==CurCurrencyCode)
        AND IsServDoc(doc)
        )
        if ( (Type_Account_Pred == doc.Type_Account)
             AND ( NOT Oper OR (Oper_Pred == doc.Oper) ) )
          /***********************************************/
          SumPercRestTAcc(CurCurrency) = SumPercRestTAcc(CurCurrency) + doc.PercRest;
          SumPercTAcc(CurCurrency) = SumPercTAcc(CurCurrency) + doc.PercOprSum;
          SumRestTAcc(CurCurrency) = SumRestTAcc(CurCurrency) + doc.Rest;
          if (doc.VidDoc != 0)
            SumOperInTAcc(CurCurrency) = SumOperInTAcc(CurCurrency) + doc.InSum;
            SumOperOutTAcc(CurCurrency) = SumOperOutTAcc(CurCurrency) + doc.OutSum
          else
            SumBeznInTAcc(CurCurrency) = SumBeznInTAcc(CurCurrency) + doc.InSum;
            SumBeznOutTAcc(CurCurrency) = SumBeznOutTAcc(CurCurrency) + doc.OutSum
          end;
        else
          /***********************************************/
          Печать_Сумм_по_Типу_Вклада();
          if( Oper )
            if (Oper_Pred != doc.Oper)
             /*Печать итога по старому и шапки по новому операционистам*/
              if(Oper_Pred != FictOper)
                Печать_Сумм_по_Операционисту;
              end; /*IF*/
              FindOper(doc.Oper);
              Заголовок_по_операционисту( Oper );
              Oper_Pred = doc.Oper;
              Расчет_Сумм_Сумм;
              Обнуление_Сумм;
            end; /* IF */
          end;

          Обнуление_Сумм_по_Типу_Вклада();
          Type_Account_Pred = doc.Type_Account;
          NamTyp = TRUE;
          SumPercRestTAcc(CurCurrency) = SumPercRestTAcc(CurCurrency) + doc.PercRest;
          SumPercTAcc(CurCurrency) = SumPercTAcc(CurCurrency) + doc.PercOprSum;
          SumRestTAcc(CurCurrency) = SumRestTAcc(CurCurrency) + doc.Rest;
          if (doc.VidDoc != 0)
            SumOperInTAcc(CurCurrency) = SumOperInTAcc(CurCurrency) + doc.InSum;
            SumOperOutTAcc(CurCurrency) = SumOperOutTAcc(CurCurrency) + doc.OutSum
          else
            SumBeznInTAcc(CurCurrency) = SumBeznInTAcc(CurCurrency) + doc.InSum;
            SumBeznOutTAcc(CurCurrency) = SumBeznOutTAcc(CurCurrency) + doc.OutSum
          end;
        end;
        if(NamTyp)
          KeyNum (dtyp, 2);
          dtyp.FlagCur = FlagCur;
          dtyp.Kind = doc.Type_Account;
          if(getEQ (dtyp) == TRUE);
           [ ];
           [Вид вклада  #]( dtyp.Name);
           Разделитель_1();
           NamTyp = FALSE;
          end;
        end;
        if( (doc.InSum != 0) OR (doc.OutSum != 0) OR (doc.PercRest != 0) OR (doc.PercOprSum != 0) )
          Строка_отчета();
        end;
        if (doc.VidDoc != 0)
          SumOperIn(CurCurrency) = SumOperIn(CurCurrency) + doc.InSum;
          SumOperOut(CurCurrency) = SumOperOut(CurCurrency) + doc.OutSum
        else
          SumBeznIn(CurCurrency) = SumBeznIn(CurCurrency) + doc.InSum;
          SumBeznOut(CurCurrency) = SumBeznOut(CurCurrency) + doc.OutSum
        end;
        SumPercRest(CurCurrency) = SumPercRest(CurCurrency) + doc.PercRest;
        SumPerc(CurCurrency) = SumPerc(CurCurrency) + doc.PercOprSum;
        SumRest(CurCurrency) = SumRest(CurCurrency) + doc.Rest;
      end;
    end;
    stat = Next (doc);
  end;
  Расчет_Сумм_Сумм;
  Печать_Сумм_по_Типу_Вклада;
  if( Oper )
    Печать_Сумм_по_Операционисту;
  end;
end;


/***********************************************************/
macro IsDocForOper
(
 DateDoc
)

  var stat = FALSE;
  var flag;

  KeyNum (doc,0);
  ReWind(doc);
  doc.IsCur         = FlagCur;
  doc.FNCash        = FNcash;
  doc.Date_Document = DateDoc;
  doc.Oper          = prs.Oper;
  doc.Type_Account  = "";
  doc.TypeOper      = 0;
  doc.Code_Currency = 0;
  doc.Account       = "";

  flag = getGE (doc);
  while (flag)
    if ((doc.IsCur         == FlagCur ) AND
        (doc.FNCash        == FNcash  ) AND
        (doc.Date_Document == DateDoc ) AND
        (doc.Oper          == prs.Oper)    )
     /*Исключаем служебные счета и архивные "следы"*/
      if((Int(doc.Code_Currency) == CurCurrencyCode) AND
         IsServDoc(doc))
        stat = TRUE;
        flag = FALSE;
      else
        flag = Next(doc);
      end;
    else
      flag = FALSE;
    end;
  end;


  return stat;

end;

/***********************************************************/
macro Цикл_по_операционистам (DateDoc, Oper)
/*
  Организует цикл по операционистам,
  если Oper == 0

  23.09.96
*/
  var
    stat;

  ClearRecord(GroupMember);
  GroupMember.Branch = Branch;
  GroupMember.Date = DateDoc;
  stat = GetGE(GroupMember);
  while(stat
    and (GroupMember.Branch == Branch)
    and (GroupMember.Date == DateDoc))
    FindOper(GroupMember.Oper);
    if (IsDocForOper(DateDoc))
      Заголовок_по_операционисту (Oper);
      Цикл_по_документам (DateDoc);
    end;
    stat = Next (GroupMember);
  end;
end;

/***********************************************************/
macro Начало_Конец_отчета
  [ ------- #################### ------------------ ########## ------ ######## ---------
  ] (GetRetailVersion(),Date(),Time());
end;

/***********************************************************/
macro Заголовок_отчета (DateDoc)
  [ Филиал № #                                                           Ф. №24]
  ( GetBranchName );
  [                                                              Последконтроль];
  [ ];
  PrintLn("                   Операционный дневник № ",GetDiaryNumber()," за ",DateDoc);
  PrintLn("                                        Вклады");
  [ ];
end;

/***********************************************************/
macro Заголовок_отчета_по_виду_валюты (CurrencyName)
  [ ];
  [                       Операционный дневник по вкладам ###](CurrencyName);
  [ ];
end;

/***********************************************************/
macro Определение_Вход_Исход_Остаток (Date)
/*VZ 19.11.96 исправлено*/
  var stat;

  Vx_Ost = $0.00;
  Isx_Ost = $0.00;
  KeyNum(dr,7);
  ReWind(dr);
  dr.FNcash = FNcash;
  dr.Account = cas_sch;
  stat = getEQ(dr);
  if (stat)
    KeyNum(drest,0);
    drest.Referenc = dr.Referenc;
    drest.Type_Object = 2001;
    drest.Date_Rest = Date;
    stat = getLE(drest);
    if (stat AND (drest.Referenc == dr.Referenc)
             AND (drest.Type_Object == 2001))
      /*запись с датой <= заданной найдена*/
      if( drest.Date_Rest == Date)
       /*дата = заданной*/
       Isx_Ost = drest.Rest * (-1);
       stat = Prev(drest);
       if(stat AND (drest.Referenc == dr.Referenc)
               AND (drest.Type_Object == 2001))
        /* Есть предыдущая*/
        Vx_Ost = drest.Rest * (-1);
       else
        /* Нет предыдущей записи*/
        Vx_Ost = $0.00;
       end;
      else
       /* дата < заданной */
       Isx_Ost = drest.Rest * (-1);
       Vx_Ost = drest.Rest * (-1);
       return TRUE;
      end;
    else
      /*запись не найдена*/
      Vx_Ost = $0.00;
      Isx_Ost = $0.00;
      return TRUE;
    end;
  else
    return FALSE;
  end;
end;

/***********************************************************/
macro Цикл_по_кассовым_операциям (Date, Oper)

  var stat;

  Sum_KO_Pr = $0.00;
  Sum_KO_Ras = $0.00;
  KeyNum(doc,0);
  ReWind(doc);
  doc.IsCur = FlagCur;
  doc.FNCash = FNcash;
  doc.DepDate_Document = Date;
  stat = getGE(doc);
  while(stat)
    if ((doc.IsCur == FlagCur) AND (doc.FNCash == FNcash) AND
        (doc.DepDate_Document == Date))
      if((Oper == 0) OR (doc.Oper == Oper))
       Sum_KO_Pr = Sum_KO_Pr + doc.InSum;
       Sum_KO_Ras = Sum_KO_Ras + doc.OutSum;
       [ ############################################################# |###############|###############|]
       (doc.Ground, String(doc.InSum:15:2), String(doc.OutSum:15:2));
      end;
    else
      stat = FALSE;
    end;
    stat = Next(doc);
  end;
end;

/***********************************************************/
macro Дневник_по_Кассе (Date, Oper)

  var i;

  [ ];
  [ ];
  [   Операционный дневник по кассе];
  [ ];
  [ ];
  if (FlagCur == 0)
    cas_sch = c_cas_sch;
    ink_sch = c_ink_sch;
  else
    cas_sch = c_cas_sch + SubStr("00" + String(CurCurrencyCode),StrLen("00" + String(CurCurrencyCode))-2);
    ink_sch = c_ink_sch + SubStr("00" + String(CurCurrencyCode),StrLen("00" + String(CurCurrencyCode))-2);
  end;
  Определение_Вход_Исход_Остаток(Date);
  if(Oper == 0) /*по всем операционистам*/
      [ Входящий остаток:                 #########################](String(Vx_Ost:25:2));
  end;
  [   Кассовые документы];
  [------------------------------------------------------------------------------------------------];
  [                       Название операции                       |  Приход       |  Расход       |];
  [------------------------------------------------------------------------------------------------];
  Цикл_по_кассовым_операциям(Date, Oper);
  [------------------------------------------------------------------------------------------------];
  [ Общая сумма кассовых операций                                 |###############|###############|]
  (String(Sum_KO_Pr:15:2),String(Sum_KO_Ras:15:2));
  [------------------------------------------------------------------------------------------------];
  [ Итоговая сумма кассовых операций: #########################](String((Sum_KO_Pr - Sum_KO_Ras):25:2));
  [ Итоговая сумма вкладных операций: #########################](String((SumSumOperIn(CurCurrency) - SumSumOperOut(CurCurrency)):25:2));
  if(Oper == 0) /*по всем операционистам*/
    [ Исходящий остаток:                #########################](String(Isx_Ost:25:2));
  end;
end;

/***********************************************************/
macro имя_операции(код_операции)

 var имя_операции = "";

 if(код_операции == D_AR           ) имя_операции = "Арх.проводка";  end;
 if(код_операции == D_ARC          ) имя_операции = "Арх.корр-ка";   end;
 if(код_операции == D_AUDIT_STORN  ) имя_операции = "Сторнирование"; end;
 if(код_операции == D_AUDIT_DELETE ) имя_операции = "Удаление";      end;
 if(код_операции == D_AUDIT_CORRECT) имя_операции = "Корректировка"; end;
 return имя_операции;
end;

/***********************************************************/
macro Рискованные_проводки(DateDoc)

 var записьНайдена;
 var NameOper;

 [
                Рискованные проводки,
 ---------------------------------------------------------------------------
 :         Счет          :  Дата проводки : Операционист : Действие        :
 ---------------------------------------------------------------------------
 ];


 KeyNum(doc,0);
 ClearRecord(doc);
 doc.IsCur         = FlagCur;
 doc.FNCash        = FNCash;
 doc.Date_Document = DateDoc;
 записьНайдена     = getGE(doc);
 while(   (записьНайдена                 )
      AND (doc.Date_Document == DateDoc)
      AND (doc.IsCur == FlagCur)
      AND (doc.FNCash == FNCash)
      )
      if(  (doc.KindOp == D_AR           )
        OR (doc.KindOp == D_ARC          )
        OR (doc.KindOp == D_AUDIT_STORN  )
        OR (doc.KindOp == D_AUDIT_DELETE )
        OR (doc.KindOp == D_AUDIT_CORRECT)
        )
        NameOper = имя_операции(doc.KindOp);
        [:#######################: ############## : ############ : ################:]
        (
        Acc_Form(doc.Account),
        doc.ArDate,
        doc.Oper,
        NameOper
        );
      end;
      записьНайдена = Next(doc);
 end;

[-------------------------------------------------------------------------
 ];
end;
/***********************************************************/

/***********************************************************/
macro Цикл_по_валютам (DateDoc, Oper)

  CurCurrency=1;
  while (CurCurrency<=CountCur)
    CurCurrencyCode=CodeCur(CurCurrency);
    CurCurrencyName=ShortNmCur(CurCurrency);
    CurCurrencyRateCB=RateCBCur(CurCurrency);
    Обнуление_Сумм_Сумм();
    if(FlagCur)
      Заголовок_отчета_по_виду_валюты (CurCurrencyName);
    end;
    /*Цикл_по_операционистам (DateDoc, Oper);*/
    Цикл_по_документам (DateDoc,Oper);
    Печать_Сумм_Сумм();
/*    Дневник_по_Кассе(DateDoc, Oper);   */
    [ ];
    CurCurrency=CurCurrency+1;
  end;
  Рискованные_проводки(DateDoc);
end;

macro opertdy
(
  DateDoc,      /* Дата документа */
  Oper          /* Операционист */
)
/*
  Головной макрос
*/

  LogProcStartEnd(0,"Инициализирована процедура формирования дневника");

  if (DateDoc == Date(0,0,0))
    DateDoc = {curdate};
  end;
  Типы_Валют(DateDoc);
  Начало_Конец_отчета ();
  Заголовок_отчета (DateDoc);
  Цикл_по_валютам (DateDoc, Oper);
  Начало_Конец_отчета ();

  LogProcStartEnd(1,"Завершена процедура формирования дневника");
end;

/***********************************************************/
