/*

     Информационная справка, выпускаемая совместно с договором
                   при открытии счетов типа "Ф"

*/
Import commclnt, acc_form;

file  country  ( country, "rsrfdb.def" ) key 2;
file  auxinfo  ( auxinfo  ) key 0;

record auxinfo_f  ( "auxinfo.4" );

private var {MFO_Bank};


macro getCountry( code )

  var ret_name  = "";

  Country.CodeLat3 = code;
  if ( GetEQ( Country ) )
    ret_name = Country.Name;
  else
    if ( code == "" )
      ret_name = "Россия";
    end;
  end;

  return ret_name;

end;


macro Info_F( dep, client )

  Var Stat;
  Var Visa = "", Source = "", MaxSum = $0.;
  Var BankName = getBankName();
  Var NumBranch;
  Var MFO = "";
  Var FIO = client.ConvertFIOFull ();
  Var Pasport = client.MakeDocStr();

  auxinfo.Reference = dep.Referenc;
  auxinfo.InfoType  = 14;
  auxinfo.Date      = {curdate};
  stat = GetGE(auxinfo);
  while (stat)
    if ( ( auxinfo.Reference == dep.Referenc) )
      if   ( auxinfo.InfoType == 14 )
        Visa = auxinfo.Info;
      elif ( auxinfo.InfoType == 15 )
        Source = auxinfo.Info;
      elif ( auxinfo.InfoType == 16 )
        SetRecordAddr( auxinfo_f, auxinfo, 0, 0, TRUE );
        MaxSum = auxinfo_f.SummaArest;
      end;
      stat = Next(auxinfo);
    else
      stat = FALSE;
    end;
  end;

  MFO = {MFO_Bank};

  if ( not findDepartment(NumFNCash, NumBranch) )
    NumBranch = "";
  end;

  /* print( "\f\n\n" ); */

[
                              ИНФОРМАЦИОННАЯ СПРАВКА
                 о банковском счете физического лица-нерезидента,
                           открытом в уполномоченном банке

  #                                             ###############################
  БИК кредитной организации: #
  Тип счета:  "Ф"   № счета #

                            А. Сведения о владельце счета
  +----+--------------------------------+------------------------------------+
  |  # | #                              | ################################## |
  +----+--------------------------------+------------------------------------+
  |  # | #                              | ################################## |
  +----+--------------------------------+------------------------------------+
  |  # | #############################  | ################################## |
  +----+--------------------------------+------------------------------------+
  |  # | #############################  | ################################## |
  +----+--------------------------------+------------------------------------+
  |  # | #############################  | ################################## |
  +----+--------------------------------+------------------------------------+
  |  # | #############################  | ################################## |
  +----+--------------------------------+------------------------------------+

                 Б. Сведения о предполагаемых операциях по счету
                и источниках получения валюты Российской Федерации
  +----+--------------------------------+------------------------------------+
  |  # | #############################  | ################################## |
  +----+--------------------------------+------------------------------------+
  |  # | #############################  | ################################## |
  +----+--------------------------------+------------------------------------+

                          В. Обязательства владельца счета
  +----+--------------------------------+------------------------------------+
  |  # | #############################  | ################################## |
  +----+--------------------------------+------------------------------------+

                                 Г. Иные сведения
  +----+--------------------------------+------------------------------------+
  | 10.| Иные сведения, в том числе     |                                    |
  |    | полученные со слов физического |                                    |
  |    | лица-нерезидента               |                                    |
  |    |                                |                                    |
  +----+--------------------------------+------------------------------------+


  Представленные документы мною проверены,
  Ответственное лицо уполномоченного банка __________________ ________________
                                                (подпись)          (ф.и.о.)


  Подпись физического лица-нерезидента     __________________ ________________
                                                (подпись)          (ф.и.о.)

]
  ( "Наименование кредитной организации (филиала):",
    ( BankName + ", филиал " + NumBranch ):w,
    MFO,
    Acc_Form(dep.Account):f,
    "1.", "Фамилия, имя, отчество",
    FIO:w,
    "2.", "Гражданство (подданство)",
    getCountry( client.rec.Country ):w,
    "3.", ( "Сведения о паспорте или ином действительном документе, " +
            "удостоверяющем личность физического лица - нерезидента" ):w,
    Pasport:w,
    "4.", "Место жительства за пределами Российской Федерации":w,
    client.rec.Address:w,
    "5.", "Место пребывания на территории Российской Федерации":w,
    commclnt_Получить_Поле_Address_Фактического_Адреса(client):w,
    "6.", "Сведения о въездной визе, включая срок действия визы":w,
    Visa:w,
    "7.", ( "Предполагаемый источник получения валюты Российской Федерации " +
            "(в том числе наличной валюты Российской Федерации)" ):w,
    Source:w,
    "8.", ( "Предполагаемый максимальный объем поступлений валюты " +
            "Российской Федерации (в том числе наличной) на счет " +
            "по вкладу в течение календарного месяца" ):w,
    String( MaxSum:a ),
    "9.", ( FIO + " (фамилия, имя, отчество физического лица-нерезидента) " +
          "обязуется не использовать счет типа \"Ф\" для осуществления " +
          "операций, связанных с предпринимательской деятельностью" ):w,
    "\n\n" +
    "__________________________________ (подпись физического лица-нерезидента, " +
    "являющегося владельцем счета типа \"Ф\")":w );

end;
