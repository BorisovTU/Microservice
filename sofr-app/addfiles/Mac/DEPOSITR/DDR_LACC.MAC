/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Справка о состоянии вклада(ов).

  Ведомость счетов, по которым предоставлялась справка
  о состоянии вкладов, закрытых другими дополнительными офисами.

*****************************************************************/

import DeprIntr, OperCode, SqlConv, RSD, CommonInter;

var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

var doc = TBFile( "sbdepdoc.dbt", "r", 6 ),
    dcl = TBFile( "ddr_decl.dbt", "r", 0 ),
    lnk = TBFile( "ddr_link.dbt", "r", 0 );

var depclnt = TClientList; /* Справочник вкладчиков */

const FNCash = NumFNCash();

private var {CurDate};

var sFIO_Declarer = "";
var sDeclare_Attr = "";

var nn = 1;

/****************************************************************
                    Чтение записи о Филиале
****************************************************************/
macro find_Branch( _FNCash )
  var sFilial;

  if (not findDepartment(_FNCash, sFilial))
    sFilial = "???";
  end;

  return sFilial;

end;

/*****************************************************************
        Поиск номера подразделения, в котором закрыт счет
*****************************************************************/
macro getBranchClose( _Ref )

 var ret = TRUE;

 doc.Clear;
 doc.rec.Referenc      = _Ref;
 doc.rec.Date_Document = Date( 31, 12, 2999 );
 doc.rec.NumDayDoc     = 32000;

 doc.addFilter( "t.t_Referenc = " + string( _Ref ) );

 if ( ( NOT doc.getLE() )  OR  ( doc.rec.Referenc != _Ref ) )
   println( " *** Не найдены документы по счету с Referenc = ", _Ref );
   doc.dropFilter;
   return -1;
 end;

 while ( ret  AND  ( doc.rec.Referenc == _Ref ) )
   if ( ( doc.rec.TypeComplexOper == Закрытие_Наличными )     OR
        ( doc.rec.TypeComplexOper == Закрытие_Безналичными )  OR
        ( doc.rec.TypeComplexOper == Закрытие_Переводом    ) )
     doc.dropFilter;
     return doc.rec.RealFNcash;
   end;
   ret = doc.Prev();
 end;

 println( " *** Не найдены документы закрытия счета с Referenc = ", _Ref );
 doc.dropFilter;
 return -1;

end;

/*****************************************************************
    Формирование строк по закрытым счетам для данного заявления
*****************************************************************/
macro Acc_DDR_Decl( _declID )

 var acmd, ars;
 var dcmd, drs;

 /* Выборка всех счетов по заявлению для проверки на закрытие. */
 acmd = RsdCommand( "select acc.t_open_close as oc " +
                    "from dddr_link_dbt lnk, ddepositr_dbt acc " +
                    "where lnk.t_declid = " + String( _declID ) + " " +
                      "and acc.t_referenc = lnk.t_accref" );

 acmd.execute;

 ars = RsdRecordSet( acmd );

 while ( ars.moveNext )
   if ( ars.value( "oc" ) == StrFor(0) )  /* Найден открытый счет по заявлению */
     return;
   end;
 end;

 /* Выборка параметров для строки Ведомости. */
 dcmd = RsdCommand( "select acc.t_Referenc   as ref, " +
                           "acc.t_Account    as account, " +
                           "acc.t_Close_Date as close, " +
                           "acc.t_SpecialAccess as SpecialAccess " +
                      "from dddr_link_dbt lnk, ddepositr_dbt acc " +
                        "where lnk.t_declid = " + String( _declID ) + " and " +
                              "acc.t_Referenc = lnk.t_accref" );

 dcmd.execute;

 drs = RsdRecordSet( dcmd );

 dcl.Clear;
 dcl.rec.ID = _declID;

 if ( NOT dcl.getEQ() )
   println( "Не найдено заявление с ID = ", _declID );
   return;
 else
   if ( depclnt.GetRecord( dcl.rec.ClientCode ) )
      if(  (not SpecialAccess) and OperMayListSpecialAccounts and string(drs.value("SpecialAccess")) )
        sFIO_Declarer = "XXXXXX X.X.";
      else
        sFIO_Declarer = depclnt.CurRec.rec.Name1 + " " +
                        depclnt.CurRec.rec.Name2 + " " +
                        depclnt.CurRec.rec.Name3;
      end;
   else
     sFIO_Declarer = "???";
   end;
   sDeclare_Attr = String( ( "\"" + dcl.rec.Number + "\", " ):14 ) +
                   String( dcl.rec.OpenDate );
 end;

 while ( drs.moveNext )
   [ | ### | ############################### | ############################ | ########## | ######### | ######################### | ]
   ( nn:3,
     sFIO_Declarer:30:w,
     Acc_Form(String(drs.value( "account" ))):f:28,
     String( drs.value( "close" ) ),
     find_Branch( getBranchClose( drs.value( "ref" ) ) ),
     sDeclare_Attr:26:w );
   nn = nn + 1;
 end;

 [ +-----+---------------------------------+------------------------------+------------+-----------+---------------------------+]

end;

/****************************************************************
                        Головная процедура
*****************************************************************/

 /*
    Алгоритм формирования "Ведомости счетов":
    - перебор документов данного филиала за указанную дату,
      у которых doc.TypeComplexOper == "Закрытие счета",
      с признаком "Закрыт в другом структурном подразделении";
    - для каждого такого документа ищем запись в auxinfo.dbt
      "Заявление на выдачу справки" - если такая запись есть -
      формируем строку "Ведомости".
 */

 var Date1 = {CurDate};

 var cmd, rs;
 var stat;

 var sDateClose = "";
 var sOurBranch = find_Branch( FNCash );

 if ( NOT GetDate( Date1, "Укажите дату, за которую выпускается ведомость" ) )
   Exit( 1 );
 end;

 [
   Дополнительный офис N #########



                        ВЕДОМОСТЬ СЧЕТОВ, ПО КОТОРЫМ ПРЕДОСТАВЛЯЛАСЬ СПРАВКА О СОСТОЯНИИ ВКЛАДА(ОВ) Ф.N 297,
                                                ЗАКРЫТЫХ ДРУГИМИ ДОПОЛНИТЕЛЬНЫМИ ОФИСАМИ

                                                          ЗА  ##########

   +---------------------------------------------------------------------------------------------------------------------------+
   | N N |      Фамилия, Имя, Отчество     |           Номер счета        |    Дата    |   Номер   |  Номер и дата оформления  |
   | п/п |            вкладчика            |                              |  закрытия  | дополни-  |     заявления ф. N 298    |
   |     |                                 |                              |  счета по  | тельного  |                           |
   |     |                                 |                              |   вкладу   | офиса, в  |                           |
   |     |                                 |                              |            |  котором  |                           |
   |     |                                 |                              |            | совершена |                           |
   |     |                                 |                              |            | операция  |                           |
   |     |                                 |                              |            | по закры- |                           |
   |     |                                 |                              |            | тию счета |                           |
   |     |                                 |                              |            | по вкладу |                           |
   +-----+---------------------------------+------------------------------+------------+-----------+---------------------------+
   |  1  |                2                |                 3            |      4     |     5     |               6           |
   +-----+---------------------------------+------------------------------+------------+-----------+---------------------------+]
 ( sOurBranch, Date1 );


 cmd = RsdCommand( "select dcl.t_id as declID " +
                   "from  dddr_decl_dbt dcl, dddr_link_dbt lnk, ddepositr_dbt acc " +
                   "where dcl.t_branch = " + String( FNCash ) + " and " +
                         "lnk.t_declid = dcl.t_id and " +
                         "acc.t_referenc = lnk.t_accref and " +
                         "dcl.t_LastClosedAccRef = acc.t_referenc and " +
                         "exists ( select 0 from dsbdepdoc_dbt doc " +
                                  "where doc.t_referenc = acc.t_referenc and " +
                                        "doc.t_Date_Document = " + sqlDateToStr( Date1 ) + " and " +
                                        "doc.t_typeComplexOper in ( " + string( Закрытие_Наличными    ) + ", " +
                                                                        string( Закрытие_Безналичными ) + ", " +
                                                                        string( Закрытие_Переводом    ) + " ) and " +
                                        "doc.t_realFnCash != doc.t_fnCash )" );

 cmd.execute;

 rs = RsdRecordset( cmd );

 while ( rs.moveNext )
   Acc_DDR_Decl( rs.value( "declID" ) );
 end;

end;
