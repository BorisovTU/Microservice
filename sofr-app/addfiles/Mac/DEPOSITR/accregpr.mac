
/* Печать книги регистрации открытых счетов */

import deprintr, rsd, sqlconv, CommonInter;

var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

const
  flagTitle_ISO_BAL = FALSE;  /* Флаг печати заголовка по валюте и баланс.счету */

const
  page_Lines = 50;  /* Число строк отчета на странице */

var
  first_Page = TRUE,  /* Флаг первой страницы */
  num_Line_of_Page,   /* Номер строки на странице */
  num_Page = 0;       /* Номер страницы */

var
  currency = TBFile( "currency.dbt", "r", 0 ),      /* Справочник валют */
  sbdepdoc = TBFile( "sbdepdoc.dbt", "r", 0 );      /* Документы по счету */

var    {UseNewNumbering},
       {curdate},
       {CNum},
       StartDate = {curdate},
       EndDate = {curdate},
       ClientList = TClientList,
       Stat = true,
       flagFloor = FALSE,
       BStandart = GetBankStandart( ),
       ErrCode, ErrText;
const  FlagCur = NumFlagCur(),
       FNCash  = NumFNCash();
const
  D_DELETE = 2;


macro Colontitle
  num_Page = num_Page + 1;
  [ ------- #################### ------------------ ########## ------ ######## --------------------------------------  страница ####
  ] ( GetRetailVersion(), Date(), Time(), num_Page:l );
end;

/****************************************************************************
                        Написать заголовок по валюте
****************************************************************************/
macro PrintHeadCurrency( vCurr )

  currency.rec.Code_Currency = vCurr;
  if ( currency.GetEQ )
    [ Валюта: #](currency.rec.ExternalCode + " (" + currency.rec.Name_Currency + ")");
    [ ];
  end;

end;

/*************************************************************************
               Получение наименования подразделения.
*************************************************************************/
macro get_Branch_Name( pCode )
  var name;

  if (not findDepartment(pCode, name))
    name = String(pCode);
  end;
  return name;
end;

/****************************************************************************
                        Найти документ закрытия счета
****************************************************************************/
macro findDocAccClose( pRef )
  var cmd, rs;
  var sRet = "";

  cmd = RsdCommand( "select dep.t_RealFNCash as RealFN from dsbdepdoc_dbt dep " +
                    "where dep.t_Referenc = :ref and " +
                    "      dep.t_Date_Document = :dateDoc and " +
                    "      dep.t_NumDayDoc = :NumDay and " +
                    "      dep.t_TypeComplexOper in (10, 81, 96) and " +
                    "      dep.t_FNCash!=dep.t_RealFNCash " +
                    "order by dep.t_Referenc desc, dep.t_Date_Document desc, dep.t_NumDayDoc desc"  );

  rs = RsdRecordset( cmd );

  cmd.addParam( "ref", RSDBP_IN, pRef );
  cmd.addParam( "dateDoc", RSDBP_IN, Date(31, 12, 2999) );
  cmd.addParam( "NumDay", RSDBP_IN, 32000 );

  cmd.execute;

  if ( rs.moveNext() )
     sRet = "Закрыт в " + get_Branch_Name( rs.value("RealFN") );
  end;
  return sRet;
end;

macro FindDepClient(ClientCode)

  return ClientList.GetRecord( ClientCode );
end;

macro Hand1er (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Esc - Отказ от выпуска отчета ----------------------- */
    if (key == 27)
      stat = CM_CANCEL;
    /* F7 - Выпуск отчета ---------------------------------- */
    elif (key == 321)
      stat = CM_SAVE;
    end;
  end;

  return stat;

end;

macro SetupPeriod()

  record
    Period("SC_PRD", "rtusrmac") dialog;

  Period.BeginDate = StartDate;
  Period.EndDate = EndDate;
  Stat = RunDialog(Period, "Hand1er");
  if(Stat)
    StartDate = Period.BeginDate;
    EndDate = Period.EndDate;
  end;
end;

macro PrintTitle()

  if ( BStandart )
    [ +--------------------------------------------------------------------------------------------------------------------------------------+];
    [ |№ №|Дата откр.|   Номер    |   Фамилия, имя, отчество  | Вид вклада | Номер лицевого счета |Дата сообщ. |Дата закр.|    Примечание    |];
    [ |п/п|  счета   |  договора  |          вкладчика        |            |                      |налог.орган.|  счета   |                  |];
    [ |---|----------|------------|---------------------------|------------|----------------------|------------|----------|------------------|];
  else
    [ +------------------------------------------------------------------------------------------------------------------------------+];
    [ |№ № |Номер счета по вкладу |     Фамилия, имя, отчество    |     Дата    |Дата сообщ.|     Дата    |                          |];
    [ |п/п |                      |             вкладчика         |   открытия  |налог.орг. |   закрытия  |         Примечание       |];
    [ |    |                      |                               |     счета   |об откр.сч.|     счета   |                          |];
    [ |----|----------------------|-------------------------------|-------------|-----------|-------------|--------------------------|];
  end;
  flagFloor = TRUE;
end;

macro PrintFloor()
  if ( flagFloor )
    if ( BStandart )
      [ +--------------------------------------------------------------------------------------------------------------------------------------+];
    else
      [ +------------------------------------------------------------------------------------------------------------------------------+];
    end;
  end;
end;

/********************************************************************
            Процедура перехода со страницы на страницу.
********************************************************************/
macro next_Page( pBalAcc, vCurr )
  PrintFloor();
  [ ];
  if ( NOT first_Page )
    printLn( strFor( 12 ) );  /* Управляющий символ конца страницы */
    Colontitle();
    [ ];
  end;
  if ( BStandart  OR  flagTitle_ISO_BAL )
    [ ];
    PrintHeadCurrency(vCurr);
    [ Балансовый счет 2-го порядка: #] ( pBalAcc );
    [ ];
  end;
  PrintTitle();
  num_Line_of_Page = 0;
  first_Page = FALSE;
end;

macro ToStr( val )
   if ( string(val)=="" )
      return "";
   end;

   return string(val);
end;

macro PrintRegisteredAccounts()

  var specAccount; 
  var stat;
  var NN = 1;
  var nr = 0;
  var str, ClientName = "";
  var sSB, sF294;
  var NameBranch = "";
  var vBalAcc = "";
  var codeCurr;
  var vCurr = -1;
  var cmd, rs;
  var cmdCount, rsCount, BalAcc;
  var Open_Date, Close_Date, FiscalDate;

  if ( FlagCur == 0 )
    codeCurr = 0;
  else
    codeCurr = 1;
  end;

  if ( BStandart )
    sSB   = "";
    sF294 = "";
  else
    sSB   = "Сбербанк Росии";
    sF294 = "Ф.N 294";

  end;

  NameBranch = get_Branch_Name( FNCash );

  cmdCount = RsdCommand( "select count(1) as CountRec " +
                        "from ddepositr_dbt d, daccreg_dbt a, dsb_dtyp_dbt t " +
                        "where d.t_IsCur = ? " +
                        "  and d.t_FNCash = ? " +
                        "  and d.t_Open_Date between ? and ? " +
                        "  and d.t_Code_Currency = ?"+
                        "  and a.t_IsCur = d.t_IsCur " +
                        "  and a.t_FNCash = d.t_FNCash " +
                        "  and a.t_GroupNumb = d.t_GroupNumb " +
                        "  and a.t_Code_Currency = d.t_Code_Currency " +
                        "  and a.t_Number = d.t_Number " +
                        "  and t.t_FlagCur = d.t_IsCur " +
                        "  and t.t_Kind = d.t_Type_Account " );

  rsCount = RsdRecordset( cmdCount );

  cmdCount.addParam( "isCur", RSDBP_IN );
  cmdCount.addParam( "branch", RSDBP_IN );
  cmdCount.addParam( "minDate", RSDBP_IN );
  cmdCount.addParam( "maxDate", RSDBP_IN );
  cmdCount.addParam( "Code_Currency", RSDBP_IN );

  cmdCount.value( "isCur" ) = FlagCur;
  cmdCount.value( "branch" ) = FNCash;
  cmdCount.value( "minDate" ) = StartDate;
  cmdCount.value( "maxDate" ) = EndDate;
  cmdCount.value( "Code_Currency" ) = codeCurr;
  cmdCount.execute;

  rsCount.moveNext();    

  cmd = RsdCommand( "select d.t_CodClient as clientCode, " +
                        "  d.t_Code_Currency as currCode, " +
                        "  d.t_Open_Date as openDate, " +
                        "  d.t_Close_Date as closeDate, " +
                        "  d.t_GroupNumb as numGroup, " +
                        "  d.t_Number as num, " +
                        "  d.t_Type_Account as accType, " +
                        "  d.t_Account as account, " +
                        "  d.t_Referenc as referenc, " +
                        "  d.t_SpecialAccess as SpecialAccess, " +
                        "  a.t_FiscalDate as fiscalDate, " +
                        "  a.t_Remark as remark, " +
                        "  g.t_BalAcc2 as balAcc, " +
                        "  g.T_BalAcc2NotResid as balAccNR " +
                        "from ddepositr_dbt d, daccreg_dbt a, dsb_dtyp_dbt t, dsbnumgrp_dbt g " +
                        "where d.t_IsCur = ? " +
                        "  and d.t_FNCash = ? " +
                        "  and d.t_Open_Date between ? and ? " +
                        "  and d.t_Code_Currency = ?" +
                        "  and a.t_IsCur = d.t_IsCur " +
                        "  and a.t_FNCash = d.t_FNCash " +
                        "  and a.t_GroupNumb = d.t_GroupNumb " +
                        "  and a.t_Code_Currency = d.t_Code_Currency " +
                        "  and a.t_Number = d.t_Number " +
                        "  and t.t_FlagCur = d.t_IsCur " +
                        "  and t.t_Kind = d.t_Type_Account " +
                        "  and g.t_GroupID = a.t_GroupNumb " +
                        "  order by d.t_IsCur, d.t_FNCash, d.t_Code_Currency" );
  rs = RsdRecordset( cmd );

  cmd.addParam( "isCur", RSDBP_IN );
  cmd.addParam( "branch", RSDBP_IN );
  cmd.addParam( "minDate", RSDBP_IN );
  cmd.addParam( "maxDate", RSDBP_IN );
  cmd.addParam( "Code_Currency", RSDBP_IN );

  cmd.value( "isCur" ) = FlagCur;
  cmd.value( "branch" ) = FNCash;
  cmd.value( "minDate" ) = StartDate;
  cmd.value( "maxDate" ) = EndDate;
  cmd.value( "Code_Currency" ) = codeCurr;

  cmd.execute;

  InitProgress( int(rsCount.value("CountRec")), " Ж Д И Т Е ...",
                 "Формирование отчета" );

  [ ];
  [ ####################################################                                                   ########]
  ( sSB, sF294 );
  [ Филиал № #](NameBranch);
  [ ];
  if ( BStandart == 0 )
    str = "                                        КНИГА РЕГИСТРАЦИИ ОТКРЫТЫХ СЧЕТОВ";
  else
    str = "                                        КНИГА регистрации открытых счетов";
  end;
  [#](str);
  if ( BStandart == 0 )
    str = "                                     (лицевых счетов клиентов-физических лиц)";
    [#](str);
  end;
  str = "                                            с " + String(StartDate) + " по " + String(EndDate);
  [#](str);
  [ ];

  num_Line_of_Page = 0;

  while ( rs.moveNext() )

    specAccount = rs.value( "SpecialAccess" ) ; 
      
    if ( FindDepClient( rs.value( "clientCode" ) ) )      
      if(  (not SpecialAccess) and OperMayListSpecialAccounts and specAccount)
         ClientName = "XXXXXX X.X.";
      else
  	 ClientName = ClientList.CurRec.ConvertFioFull;
      end;
    else
      ClientName = "";
    end;

    DtTmSplit( rs.value( "openDate" ), Open_Date );
    DtTmSplit( rs.value( "closeDate" ), Close_Date );
    DtTmSplit( rs.value( "fiscalDate" ), FiscalDate );

    if ( Trim(string(Open_Date))=="1.01.0001" )
       Open_Date = " ";
    end;

    if ( Trim(string(Close_Date))=="1.01.0001" )
       Close_Date= " ";
    end;

    if ( Trim(string(FiscalDate))=="1.01.0001" )
       FiscalDate= " ";
    end;

    if ( ClientList.CurRec.Rec.NotResident == StrFor( 0 ) )  /* Резидент */
      BalAcc = rs.value( "balAcc" );
    else                                         /* Нерезидент */
      BalAcc = rs.value( "balAccNR" );
    end;

    num_Line_of_Page = num_Line_of_Page + 1;

    if ( ( vCurr   != rs.value( "currCode" ) )  OR
         ( vBalAcc != BalAcc ) )
      vCurr   = rs.value("currCode");
      vBalAcc = BalAcc;
      next_Page( vBalAcc, vCurr );
    else
      if ( num_Line_of_Page >= page_Lines )
        next_Page( vBalAcc, vCurr );
      end;
    end;

    if ( BStandart )

      [ |###|##########|############|###########################|############|######################|############|##########|##################|]
      ( String(NN),
        String(Open_Date),
        String(SubStr(Trim(ToStr(rs.value("numGroup"))),1,4):r:4) + "/" + String(rs.value( "num" )),
        String(ClientName),
        String(rs.value( "accType" )),
        Acc_Form(String(ToStr(rs.value( "account" )))),
        String(FiscalDate),
        // "не требуется",
        String(Close_Date),
        String(ToStr(rs.value( "remark" ))):w );

    else

      [ |####|######################|###############################|#############|###########|#############|##########################|]
      ( String(NN),
        Acc_Form(String(ToStr(rs.value( "account" )))),
        String(ClientName),
        String(Open_Date),
        " ",
        String(Close_Date),
        String(ToStr(rs.value( "remark" ))):w );

    end;
    NN = NN + 1;

    UseProgress( nr );
    nr = nr + 1;
  end;

  PrintFloor();
  RemProgress();

  if ( BStandart == 0 )
    [
      Главный бухгалтер

      "____" ________________________________ 20____ г.]
  end;

end;

macro main(void)

  if ( {UseNewNumbering} )
    SetupPeriod();
    if(Stat)
      /* SetOutput("PRN"); */
      Colontitle();
      [ ];
      PrintRegisteredAccounts();
      [ ];
      /* Colontitle(); */
    end;
  end;
end;

main();
End...