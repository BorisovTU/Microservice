
/* Общие функции для ордера ф.187 */

import deprintr, BankInter, ChkPrn, rtpm_lib, cur_rate, rsd;
import PmPropUtil;

const
        ACCOUTN_OWNER       = StrFor( 0 ),    /* Владелец счета      */
        ACCOUTN_PROXY       = StrFor( 1 ),    /* Доверенное лицо     */
        ACCOUTN_HEIR        = StrFor( 2 ),    /* Наследник           */
        ANONIMUS_CLIENT     = StrFor( 3 ),    /* Анонимный клиент    */
        ACCOUNT_IMPORTER    = StrFor( 4 ),    /* Вноситель           */
        ACCOUNT_TUTOR       = StrFor( 5 ),    /* Опекун              */
        ACCOUNT_OBLIGATOR   = StrFor( 6 ),    /* Под обязательство   */
        ACCOUTN_HEIR_PAY    = StrFor( 7 ),    /* Наследник - выплаты */
        ACCOUNT_UNDER_TRIAL = StrFor( 8 ),    /* По суду             */
        ACCOUNT_PARENT      = StrFor( 9 );    /* Родитель            */


/*---------- Панель диалога ---------------------------------------------*/
 record dlg(form187, "rtusrmac") dialog;

file SubOper  (suboper);
file currency (currency);
file recip    (rtcp_recipient);  /* Получатели */
file plan     (dplanpay) key 3;  /* Плановые перечисления */

var DocDate,Day,Month,Month1,Year;
var Filial,Filial_;
var AccDep;
var sPlan;
var FIO = "" ,FIO_ = "";
var MFO = "", RecName = "", RecName1 = "", INN = "_", sKNF = "";
var CorAcc = "", Account = "", BankName = "";
var Comment = "";
var Ground;
var SumRub,TotSumRub;
var SumCop,TotSumCop;
var PennyRub = 0,PennyCop = 0;
var OutPercR;
var OutPercC;
var RateFlag = TRUE;
var USD_rate;
var NRub,NRubS,NCop,NCopS;
var G_Dep, G_Doc;
var ClientList   = TClientList;

var str;

const LengthMonth    = 10;
const LengthYear     =  4;
const LengthFilial   = 15;
const LengthAccDep   = 10;
const LengthFIO      = 55;
const LengthMFO      = 15;
const LengthCorAcc   = 25;
const LengthRecName  = 20;
const LengthAccount  = 52;
const LengthGround   = 55;
const LengthRub      = 10;
const LengthCop      =  3;
const LengthOutPercR =  7;
const LengthOutPercC =  3;
const CO_CommPay     =  1;

/******************************************************************\
\******************************************************************/
macro FindRecipient(id)
/* Ищет, существует ли получатель с данным кодом */
  var stat = FALSE;
  KeyNum (recip, 0);
  recip.ID = id;
  stat = getEQ (recip);
  return stat;
end;



/******************************************************************\
\******************************************************************/
macro SumRubCop(sum,sumrub,sumcop)

  /* Рубли и копейки */
  str = Trim(String(sum));
  SumRub = str;
  SumCop = "00";
  if (Index(str,".") > 0)
    SumRub = SubStr(str,1,Index(str,".") - 1);
    SumCop = SubStr(str,Index(str,".") + 1);
  end;
  if (StrLen(SumRub) == 0)
    SumRub = "0";
  end;
  if (StrLen(SumRub) == 0)
    sumrub = "0";
  end;

  SetParm(1, SumRub);
  SetParm(2, SumCop);

end;


/******************************************************************\
\******************************************************************/
macro OutPercRubCop(sum)

  /* Рубли и копейки */
  if (sum < 0)
    sum = -1 * Sum;
  end;
  str = Trim(String(sum));
  OutPercR = "";
  OutPercC = "00";
  if (Index(str,".") > 0)
    OutPercR = SubStr(str,1,Index(str,".") - 1);
    OutPercC = SubStr(str,Index(str,".") + 1);
  end;
  if (StrLen(OutPercR) == 0)
    OutPercR = "0";
  end;
  /*
  while (StrLen(OutPercR) < LengthOutPercR)
    OutPercR = OutPercR + "_";
  end;
  while (StrLen(OutPercC) < LengthOutPercC)
    OutPercC = OutPercC + "_";
  end; */

end;

/******************************************************************
                    Чтение планового перечисления
******************************************************************/
macro readPlanPay( ref )

  ClearRecord( plan );
  plan.Ref = ref;
  return GetEQ( plan );

end;

/******************************************************************
                Определение планового перечисления
******************************************************************/
macro getPlanPay()
  var str = "";

  if ( ( GetBitFlag( G_Doc.Flags, 29 ) == 1 )  AND
       ( readPlanPay( G_Doc.ListTransfer ) ) )
    str = "по план.перечисл. " + plan.NumberContract +
          " за " + plan.Start_DatePay;
  end;

  return str;
end;

/******************************************************************
                  Определение выходных полей
******************************************************************/
macro DefOutputFields

  var d,m,y;
  var curCBRate;
  var ScaleRate = 1;

  /* День, месяц и год платежа */
  DocDate = G_Doc.Date_Document;
  DateSplit(G_Doc.Date_Document,d,m,y);
  Day = string(d);
  while (StrLen(Day) < 2)
    Day = "0" + Day;
  end;
  if   (m ==  1) Month = "января";
  elif (m ==  2) Month = "февраля";
  elif (m ==  3) Month = "марта";
  elif (m ==  4) Month = "апреля";
  elif (m ==  5) Month = "мая";
  elif (m ==  6) Month = "июня";
  elif (m ==  7) Month = "июля";
  elif (m ==  8) Month = "августа";
  elif (m ==  9) Month = "сентября";
  elif (m == 10) Month = "октября";
  elif (m == 11) Month = "ноября";
  elif (m == 12) Month = "декабря";
  else           Month = "";
  end;
  if   (m ==  1) Month1 = "январь";
  elif (m ==  2) Month1 = "февраль";
  elif (m ==  3) Month1 = "март";
  elif (m ==  4) Month1 = "апрель";
  elif (m ==  5) Month1 = "май";
  elif (m ==  6) Month1 = "июнь";
  elif (m ==  7) Month1 = "июль";
  elif (m ==  8) Month1 = "август";
  elif (m ==  9) Month1 = "сентябрь";
  elif (m == 10) Month1 = "октябрь";
  elif (m == 11) Month1 = "ноябрь";
  elif (m == 12) Month1 = "декабрь";
  else           Month1 = "";
  end;
  Year = String(y);
  while (StrLen(Year) < LengthYear)
    Year = Year + "_";
  end;
  /* Филиал */
  if (not findDepartment(G_Doc.FNCash, Filial))
    Filial = "_";
  end;
  Filial_ = Filial;
  /* Счет вкладчика */
  AccDep = G_Doc.Account;
  sPlan = getPlanPay();
  /* ФИО */
  FIO = "";
  G_Dep.CodClient;
  if (G_Dep.CodClient != 0)
    if (ClientList.GetRecord( G_Dep.CodClient ))
      FIO = ClientList.CurRec.ConvertFioFull;
    end;
  else
    FIO = "";
  end;
  if (G_Doc.Author != ACCOUTN_OWNER)
    if (G_Doc.CodClient > 0)
      if (ClientList.GetRecord( G_Doc.CodClient ))
        FIO = ClientList.CurRec.ConvertFioFull;
      end;
    else
      FIO = "";
    end;
  end;
  if (G_Doc.Author == ACCOUTN_PROXY)
    FIO = "По доверенности " + FIO;
  end;
  if (G_Doc.Author == ACCOUTN_HEIR)
    FIO = "По завещанию "+ FIO;
  end;
  if (G_Doc.Author == ANONIMUS_CLIENT)
   FIO = "Вноситель";
  end;
  if (G_Doc.Author == ACCOUNT_IMPORTER)
    FIO = "Вноситель " + FIO;
  end;
  if (G_Doc.Author == ACCOUNT_TUTOR)
    FIO = "Опекун " + FIO;
  end;
  if (G_Doc.Author == ACCOUNT_OBLIGATOR)
    FIO = "Под обязательство "+ FIO;
  end;
  if (G_Doc.Author == ACCOUTN_HEIR_PAY)
    FIO = "Наследник "+ FIO;
  end;
  if (G_Doc.Author == ACCOUNT_UNDER_TRIAL)
    FIO = "По суду "+ FIO;
  end;
  if (G_Doc.Author == ACCOUNT_PARENT)
    FIO = "Родитель "+ FIO;
  end;
  FIO_ = FIO;

                                /* МФО */
  MFO = "_";
  if (StrLen(PMNT_PROP_REC_BUF.BankCode) > 0)
    MFO = PMNT_PROP_REC_BUF.BankCode;
  end;
/*  while (StrLen(MFO) < LengthMFO)
    MFO = MFO + "_";
  end;
*/
                               /* Корсчет */
  CorAcc = "_";
  if (StrLen(PMNT_PROP_REC_BUF.CorrAccount) > 0)
    CorAcc = PMNT_PROP_REC_BUF.CorrAccount;
  end;

                               /* Счет */
  Account = "";
  if (StrLen(PMNT_PROP_REC_BUF.Account) > 0)
    Account =  PMNT_PROP_REC_BUF.Account;
  end;

                             /* Вид платежа */
  Ground = "Вид платежа " + G_Doc.Ground;

  if (G_Doc.ApplType >= 0)
    KeyNum(SubOper,1);
    SubOper.IsCur    = G_Doc.IsCur;
    SubOper.Kind     = G_Doc.Type_Account;
    SubOper.OperType = G_Doc.TypeOper;
    SubOper.Type     = G_Doc.ApplType;
    if (GetEQ(SubOper))
      Ground = Ground + " (" + Trim(SubOper.Name) + ")";
    end;
  end;

  SumRubCop( G_Doc.OutSum, SumRub, SumCop );

  OutPercRubCop( G_Doc.PercOprSum );

  /* Название рублей и копеек */
  NRub  = "руб.";
  NRubS = " руб. ";
  NCop  = "коп.";
  NCopS = " коп.";
  if (G_Doc.IsCur != 0)
    KeyNum(currency,0);
    ReWind(currency);
    currency.Code_Currency = G_Doc.Code_Currency;
    if (GetEQ(currency))
      NRub  = currency.Short_Name;
      NRubS = currency.Short_Name;
      NCop  = " ";
      NCopS = " ";
      if (NRub == "USD")
        NCop = "цен.";
        NCopS = "ц.";
      elif (NRub == "DM")
        NCop = "пф.";
        NCopS = "п.";
      end;
    end;
  end;

  if ( G_Doc.IsCur != 0 )
    USD_rate = "_";
    KeyNum( currency, 0 );
    ReWind( currency );
    if ( RateFlag )
      currency.Code_Currency = G_Doc.Code_Currency;
      if(GetEQ(currency))
        if ( GetAllCurrRate( G_Doc.Date_Document, G_Doc.Code_Currency, curCBRate ) )
          USD_rate = "Курс ЦБ " + String( curCBRate ) +
                     " Руб./"   + currency.Short_Name;
        end;
      end;
    end;
  else
    USD_rate = "_";
  end;
end;

/******************************************************************
                       Вывод ордера 187
******************************************************************/
macro PrintOrder;

  var oldAccDep = AccDep;

  TotSumRub = String (Int(SumRub) + Int(PennyRub) );
  TotSumCop = String (Int(SumCop) + Int(PennyCop) );

  if ( Dlg.Dogovor == "" )
    Dlg.Dogovor = "_";
  end;
  if ( Dlg.Operman == "" )
    Dlg.Operman = "_";
  end;

  /* 1-я страница */
  [+----------------------------------------------------------+
   |                                                Ф. No 187 |
   +----------------------------+-----------------------------+
   |      Отделение (филиал)    |       Сбербанк России       |
   |  Сберегательного банка     |                             |
   |  No #                      |                             |
   |                            |      ПОРУЧЕНИЕ ВКЛАДЧИКА    |
   | #   #            #   г.    |          по счету           |
   |                            |                             |
   |                            |№ #                          |
   +----------------------------+-----------------------------+
   |            ##################################            |
   +----------------------------------------------------------+
   | о перечислении в #                                       |
   +----------------------------------------------------------+
   | #                                                        |
   | Р/с #                      БИК #                         |
   | К/с #                      ИНН #                         |
   +----------------------------------------------------------+
   | #                        в уплату за #            #   г. |
   | ######################################################## |
   +----------------------------------------------------------+
   | N лицевого счета, обязательства и т.д.                   |
   | #                                                        |
   | ######################################################## |
   +----------------------------------------------------------+
   | Сумма #           #    #  #                              |
   |                                                          |
   | Подпись вкладчика _____________________________          |
   +----------------------------------------------------------+
  ]
  (Filial_,Day,Month,Year,Acc_Form(AccDep):f,FIO_:30:c,RecName1,BankName,
   Acc_Form(Account),MFO,CorAcc,INN,sKNF,Month1,Year,Ground:w,Acc_Form(AccDep):f,sPlan:w,
   /* dlg.Dogovor,dlg.DateDogovor,dlg.Operman,USD_rate, */
   SumRub,NRub,SumCop,NCop);
   /* PennyRub, NRub,PennyCop, NCop,
      TotSumRub,NRub,TotSumCop,NCop); */
  /* Переход к новой странице */
  [#](MkStr(12,1));


  /* 2-я страница */
  [+----------------------------------------------------------+
   |                                                          |
   | Отчислены %%     #                                       |
   |                                                          |
   |                                                          |
   |                                                          |
   |           Счет № #                                       |
   |                                                          |
   |                                                          |
   +------------------------+---------------------------------+
   |        СПИСАНО         |   Остатки после списания        |
   |                        |  вклада           %%            |
   | #                      |  #                #             |
   |                                                          |
   |                                                          |
   |Контролер (оператор) _____________________________        |
   |                                                          |
   |                                                          |
   |Проверено Бухгалтер  _____________________________        |
   |                                                          |
   +----------------------------------------------------------+
  ](OutPercR+" "+NRubS+" "+OutPercC+" "+NCopS,Acc_Form(AccDep):f,
    Trim(String(G_Doc.OutSum:14:2:a)),
    Trim(String(G_Doc.Rest:14:2:a)),
    Trim(String(G_Doc.PercRest:14:2:a)));
  /* Переход к новой странице */
  [#](MkStr(12,1));

  if( ( G_Doc.TypeOper == 63 ) and
      ( G_Doc.ApplType ==  1 ) )
    oldAccDep = AccDep;
    AccDep = "";
  end;

  /* 3-я страница */
  [+----------------------------------------------------------+
   |                                                Ф. No 187 |
   +----------------------------+-----------------------------+
   |      Отделение (филиал)    |       Сбербанк России       |
   |  Сберегательного банка     |                             |
   |  No #                      |                             |
   |                            |      И З В Е Щ Е Н И Е      |
   | #   #            #   г.    |          по счету           |
   |                            |                             |
   |                            |№ #                          |
   +----------------------------+-----------------------------+
   |            ##################################            |
   +----------------------------------------------------------+
   | о перечислении в #                                       |
   +----------------------------------------------------------+
   | #                                                        |
   | Р/с #                      БИК #                         |
   | К/с #                      ИНН #                         |
   +----------------------------------------------------------+
   | #                        в уплату за #            #   г. |
   | ######################################################## |
   +----------------------------------------------------------+
   | N лицевого счета, обязательства и т.д.                   |
   | #                                                        |
   | ######################################################## |
   +----------------------------------------------------------+
   | Сумма #           #    #  #                              |
   |                                                          |
   |Подпись вкладчика ________  Подпись контролера __________ |
   +----------------------------------------------------------+
  ](Filial_,Day,Month,Year,Acc_Form(AccDep):f,FIO_:30:c,RecName1,BankName,
   Acc_Form(Account),MFO,CorAcc,INN,sKNF,Month1,Year,Ground:w,Acc_Form(AccDep):f,sPlan:w,
   SumRub,NRub,SumCop,NCop);
  /*
  (Filial_,Day,Month,Year,(FIO_):30:c,Acc_Form(Account),BankName,
   MFO,CorAcc,RecName,INN,Comment,Ground,dlg.Dogovor,dlg.DateDogovor,
   dlg.Operman,USD_rate,SumRub,NRub,SumCop,NCop); */
  AccDep = oldAccDep;

end;


macro CheckPrn

  var
    RegPath = "RS-RETAIL\\ПЕЧАТИ\\ВКЛАДЫ\\ОРДЕР_187";

  CheckPrinter(RegPath, StrFor( 0 ) );
end;

macro Init( Dep, Doc )
  G_Dep = Dep;
  G_Doc = Doc;
end;


// Ордер не печатается для 64 операции (списание по коммунальному платежу),
// если у получателя установлен флаг "Готовить реестр"
macro NeedPrintOrder187(document)
  if (not (document.TypeOper == 64))
    return true;
  end;
  
  var cmd = RsdCommand(" select pr.t_IsReestrPreparing " 
                       " from drtcp_recipient_dbt pr, drtcp_link_dbt pl, dpay_doc_cp_dbt pd "
                       " where pr.t_Id = pl.t_RecipientId "
                       "   and pl.t_Id = pd.t_CPLinkId "
                       "   and pd.t_ApplKind = ? "
                       "   and pd.t_ApplKey = ? ");

  cmd.addParam("ApplicationKind", RSDBP_IN, document.iApplicationKind);
  cmd.addParam("ApplicationKey",  RSDBP_IN, document.ApplicationKey);

  cmd.execute();
  var rs = RsdRecordset(cmd);

  if (rs.moveNext() and rs.value(0) == "X")
    return false;
  end;

  return true;                    
end;


