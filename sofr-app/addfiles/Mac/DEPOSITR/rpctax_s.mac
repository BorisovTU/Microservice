/*
$Name:               rpctax_s.mac
$Module:             DEPOSITR
$Description:        Формирование налоговых карточек и справок о доходах физического лица для налоговых органов
*/

/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Формирование налоговых карточек и справок о доходах
  физического лица для налоговых органов.

  Форма "Справка о доходах" в формате для печати.

*****************************************************************/

Import rPcTax1c,rtkladr;

private const PageLen = 70;  // Число строк на одной странице.

private const cDebug = FALSE;  // Флаг вывода сообщений об ошибках.

private var {CNum}, {oper};
private var {CurDate};

private var txtDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true );
private var fTxt_PCTax = "f_rpctax.";
private file ft_PCTax () txt;

private var opr = TBFile ( "person.dbt", "r", 0 );
private var obj_code  = TBFile ( "objcode.dbt",  "r", 0 );
private var obj_atcor = TBFile ( "objatcor.dbt", "r", 0 );

private var sOper = "";
private var sINNcitizen = "";
private var gStatNalog = -1;

private var saveIdentClient = -1;

/***********************************************************
***********************************************************/

macro Заголовок_страницы( pSeqVal, pPage )

[                                                  Стр № #                                                  ]
( pPage );
[                  СПРАВКА О ДОХОДАХ ФИЗИЧЕСКОГО ЛИЦА за #### год № ########## от ##########                ]
( InputPanel_Year_InAll, pSeqVal:c, {curdate} );
[ ];

end;

/***********************************************************
***********************************************************/

macro Подвал_страницы( pPage )

[
  Налоговый агент (1 - налоговый агент (правопреемник налогового агента), 2 - представитель 
  налогового агента (правопреемника налогового агента))                                             1

  ############################################################################      __________________      ]
( sOper );
[                               (ФИО)                                                    (подпись)          ];
if ( pPage == 2 )
[  ___________________________
   * Отчество указывается при наличии
                                                                                                            ];
end;

end;

/***********************************************************
***********************************************************/

macro Шапка_по_клиенту( pSeqVal, pStatus )

  var stat = True;
  var Citizenship;
  var sIsMale;
  var sCountryRF = "643";
  var sCountry1  = "643";
  var sCountry2  = "643";
  var sCountry   = "";
  var IDType = "",
      IDCode = "";
  var db,mb,yb;
  var sCorpus = "", iCorpus = 0;
  var sPostIndex = "";
  var sRegion = "";
  var sProvince = "";
  var sDistrict = "";
  var sPlaceType = "";
  var sPlace = "";
  var sStreet = "";
  var sHouse = "";
  var sFlat = "";
  var vAddrType = 1;
  var vAddrTypeRF  = 1;
  var vAddrTypeRez = 1;
  var sAddressNR = "";
  var vSeqVal = 1, numSeq = 1;
  var vBankINN = "", vBankKPP = "", vIndKPP = 0;
  var vObjStr = "";

  COMCLNT.GetRecord( COMCLNT.CurRec.rec.CodClient );

  vObjStr = SubStr( String( 10000000000 + COMCLNT.CurRec.rec.CodClient ), 2 );

  /* Чтение записи ИНН в стране гражданства */
  sINNcitizen = "";
  obj_code.Clear();
  obj_code.rec.ObjectType = 3;  
  obj_code.rec.ObjectID = COMCLNT.CurRec.rec.CodClient;
  obj_code.rec.CodeKind = 33;
  if ( obj_code.GetGE() )
    if ( ( obj_code.rec.ObjectType == 3 )  AND 
         ( obj_code.rec.ObjectID == COMCLNT.CurRec.rec.CodClient )  AND 
         ( obj_code.rec.CodeKind == 33 ) )
      sINNcitizen = obj_code.rec.Code;
    end;
    while ( obj_code.Next()  AND 
            ( obj_code.rec.ObjectType == 3 )  AND 
            ( obj_code.rec.ObjectID == COMCLNT.CurRec.rec.CodClient )  AND 
            ( obj_code.rec.CodeKind == 33 ) )
      sINNcitizen = obj_code.rec.Code;
    end;
  end;

  /* Чтение записи статуса налогоплательщика */
  gStatNalog = -1;
  obj_atcor.Clear();
  obj_atcor.rec.ObjectType = 3;  
  obj_atcor.rec.Object = vObjStr;
  obj_atcor.rec.GroupID = 42;
  if ( obj_atcor.GetGE() )
    if ( ( obj_atcor.rec.ObjectType == 3 )  AND
         ( obj_atcor.rec.Object == vObjStr ) AND
         ( obj_atcor.rec.GroupID == 42 ) )
      gStatNalog = obj_atcor.rec.AttrID;
    end;
    while ( obj_atcor.Next()  AND 
            ( obj_atcor.rec.ObjectType == 3 )  AND
            ( obj_atcor.rec.Object == vObjStr ) AND
            ( obj_atcor.rec.GroupID == 42 ) )
      gStatNalog = obj_atcor.rec.AttrID;
    end;
  end;

  if ( gStatNalog < 0 )
    if ( COMCLNT.CurRec.Rec.NotResident == StrFor( 0 ) )
      gStatNalog = 1;  // Резидент
    else                                         
      gStatNalog = 2;  // Нерезидент
    end;
  end;

  if ( pStatus == 1 )
    Citizenship = 1;
    vAddrTypeRF  = 1;
    vAddrTypeRez = 0;
    sCountry2  = "";
    sCountry   = "";
    sAddressNR = "";
  else
    Citizenship = 2;
    vAddrTypeRF  = 2;
    vAddrTypeRez = 1;
  end;

  if ( StrLen( COMCLNT.CurRec.rec.NRCountry ) != 0 )
    sCountry1 = COMCLNT.CurRec.rec.NRCountry;
  else
    sCountry1 = "RUS";
  end;

  // Построение формата гражданства РФ.
  ClearRecord( cntr );
  cntr.CodeLat3 = sCountry1;
  if ( getGE( cntr )  AND
       ( cntr.CodeLat3 == sCountry1 ) )
    sCountryRF = cntr.CodeNum3;
  else
    sCountryRF = sCountry1;
  end;

  if ( COMCLNT.CurRec.rec.IsMale != StrFor(0) )
    sIsMale = "1";
  else
    sIsMale = "2";
  end;

  if (GetPaprKind(COMCLNT.CurRec.rec.PaperKind))
    IDType = PAPRKIND.Name;
    IDCode = PAPRKIND.CodeDocum;
  end;
  DateSplit(COMCLNT.CurRec.rec.Born,db,mb,yb);

  // Заполнение полей адреса.
  if ( pStatus == 1 )
    vAddrType = 1;
  else
    vAddrType = 2;
  end;

  while ( ( vAddrType < 4 )  AND
          // ( StrLen( sAddressRF ) == 0 )  AND
          ( StrLen( sPostIndex ) == 0 )  AND
          ( StrLen( sRegion    ) == 0 )  AND
          ( StrLen( sPlaceType ) == 0 )  AND
          ( StrLen( sPlace     ) == 0 )  AND
          ( StrLen( sProvince  ) == 0 )  AND
          ( StrLen( sDistrict  ) == 0 )  AND
          ( StrLen( sStreet    ) == 0 ) )

    COMCLNT.AdressType = vAddrType;

    if ( COMCLNT.AdressType == vAddrType )
      // sAddressRF = COMCLNT.CurRec.rec.Address;
      sPostIndex = COMCLNT.CurRec.rec.PostIndex;
      sRegion    = getRegionNumber( COMCLNT.CurRec );

      sPlaceType = Тип_населенного_пункта();
      sPlace = TRIM( COMCLNT.CurRec.rec.Place );
      sProvince = COMCLNT.CurRec.rec.Province;
      sDistrict = COMCLNT.CurRec.rec.District;

      sStreet = COMCLNT.CurRec.rec.StreetName;
      sHouse  = COMCLNT.CurRec.rec.House;
      sFlat   = COMCLNT.CurRec.rec.Flat;

      /* Обработка строки "Корпус" */
      iCorpus = Index( StrUpr( COMCLNT.CurRec.rec.NumCorp ), "КОРПУС" );
      if ( iCorpus > 0 )
        if ( StrLen(COMCLNT.CurRec.rec.NumCorp) > ( iCorpus + 5 ) )
          sCorpus = SubStr( COMCLNT.CurRec.rec.NumCorp, iCorpus + 6 );
        else
          sCorpus = "";
        end;
      else
        sCorpus = COMCLNT.CurRec.rec.NumCorp;
      end;
    end;

    vAddrType = vAddrType + 1;

  end;

  if ( Citizenship == 2 )
    COMCLNT.AdressType = 1;
    if ( StrLen( COMCLNT.CurRec.rec.Country ) != 0 )
      sCountry2 = COMCLNT.CurRec.rec.Country;
    else
      sCountry2 = "RUS";
    end;
    // Построение формата гражданства в стране проживания.
    ClearRecord( cntr );
    cntr.CodeLat3 = sCountry2;
    if ( getGE( cntr )  AND
       ( cntr.CodeLat3 == sCountry2 ) )
      sCountry = cntr.CodeNum3;
    else
      sCountry = sCountry2;
    end;
    sAddressNR = SubStr( COMCLNT.CurRec.rec.Address, 1, 255 );
    COMCLNT.AdressType = 2;
  end;

  vIndKPP = Index( Bank_INN, "/" );

  if ( vIndKPP == 0 )
    vBankINN = Bank_INN;
  else
    vBankINN = SubStr( Bank_INN, 1, vIndKPP-1 );
    vBankKPP = SubStr( Bank_INN, vIndKPP+1 );
  end;

  if ( saveIdentClient == COMCLNT.CurRec.rec.CodClient )
    PrintLn( StrFor( 17 ) );
  else
    saveIdentClient = COMCLNT.CurRec.rec.CodClient;
  end;

[                                                                                            Приложение № 1 ];
[ Штрих-код                     СПРАВКА О ДОХОДАХ ФИЗИЧЕСКОГО ЛИЦА                     к приказу ФНС России ];
[ 3990 9015                                                                           от 30 октября 2015 г. ];                    
[                             за #### год N ########## от ##########                        № ММВ-7-11/485@ ]
( InputPanel_Year_InAll, pSeqVal:l, {curdate} );
[                                                                             (в ред. от 17 января 2018 г.) ];
[                                                                                                           ];
[ Форма по КНД 1151078         Признак 1        номер корректировки ##          в ИФНС (код) N ####         ]
( SubStr( String( InputPanel_Correct + 100 ), 2 ), CodeTaxAgency );
[                                                                                                           ];
[                                                                                              Форма 2-НДФЛ ];
[ 1. Данные о налоговом агенте                                                                              ];
[ Код по ОКТМО #####################  Телефон ########################  ИНН ###########  КПП #              ]
( CodeOKATO, Bank_Phone, vBankINN, vBankKPP );
[ ######################################################################################################### ]
( ( "Налоговый агент " + Name_Bank ):w );
[ Форма реорганизации (ликвидации) (код)                                                                    ];
[ ИНН/КПП реорганизованной организации       /                                                              ];
[ ];
[ 2. Данные о физическом лице - получателе дохода                                                           ];
[ ИНН в Российской Федерации ###############  ИНН в стране гражданства #                                    ]
( SubStr( COMCLNT.CurRec.rec.INN, 1, 12 ), sINNcitizen );
[ ######################################################################################################### ]
( ( "Фамилия " + COMCLNT.CurRec.rec.Name1 + "  Имя " + COMCLNT.CurRec.rec.Name2 + "  Отчество* " + COMCLNT.CurRec.rec.Name3 ):w );
[ Статус налогоплательщика #   Дата рождения ##.##.####   Гражданство (код страны) #                        ]
( gStatNalog, db, mb, yb, sCountryRF );
[ ######################################################################################################### ]
( ( "Код документа, удостоверяющего личность " + IDCode + "   Серия и номер документа " +
    SubStr( make_Paper_Series_Number( COMCLNT.CurRec.rec.PaperKind, COMCLNT.CurRec.rec.PaperSeries, COMCLNT.CurRec.rec.PaperNumber ), 1, 25 ) ):w );

  if ( NOT stat )
    ErrCode = Status( ErrText );
    if ( ( ErrCode == 0 )  OR  ( ErrCode == 4 )  OR  ( ErrCode == 9 ) )
     stat = TRUE;
    end;
  end;

  return stat;
end;

/***********************************************************
***********************************************************/

macro Таблица_доходов_по_ставке( pStatus )

  var stat = True;

  var vCommonTaxPayGet = $0.0;
  var vCommonTaxPayPut = $0.0;

  var vCommonTaxPayPay = "               0";

  var vDuty            = $0.0;
  var vDutyPayer       = $0.0;
  var vDutyInspection  = $0.0;

  var vMon1 = "", vCod1 = "", vSum1 = "", vDCod1 = "", vDSum1 = "";
  var vMon2 = "", vCod2 = "", vSum2 = "", vDCod2 = "", vDSum2 = "";

[ ];
PrintLn( StrFor( 7 ) + " " + "15" );
[ 3. Доходы, облагаемые по ставке #                                            ]
( String(mPcRate.rec.TaxRate:0:2) + " %" );
[ ];
[+----+--------+--------------+--------+-------------+ +----+--------+--------------+--------+-------------+];
[| Ме-|  Код   |    Сумма     |  Код   |    Сумма    | | Ме-|  Код   |    Сумма     |  Код   |    Сумма    |];
[|сяц | дохода |    дохода    | вычета |   вычета    | |сяц | дохода |    дохода    | вычета |   вычета    |];
[+----+--------+--------------+--------+-------------+ +----+--------+--------------+--------+-------------+];

  mPcTax.Clear();
  mPcTax.rec.TaxPayerStatus = pStatus;
  mPcTax.rec.TaxRate = mPcRate.rec.TaxRate;
  stat = mPcTax.GetGE();

  while ( stat  AND  
          ( mPcTax.rec.TaxPayerStatus == pStatus )  AND
          ( mPcTax.rec.TaxRate == mPcRate.rec.TaxRate ) )

    vMon1 = "";
    vCod1 = "";
    vSum1 = "";
    vDCod1 = "";
    vDSum1 = "";

    // Пропускаем записи с нулевой суммой для левой части таблицы.
    while ( stat  AND  
            ( mPcTax.rec.TaxPayerStatus == pStatus )  AND
            ( mPcTax.rec.TaxRate == mPcRate.rec.TaxRate )  AND
            ( mPcTax.rec.IncomeRUR == 0.0 ) )
      stat = mPcTax.Next();
    end;

    if ( stat  AND  
         ( mPcTax.rec.TaxPayerStatus == pStatus )  AND
         ( mPcTax.rec.TaxRate == mPcRate.rec.TaxRate )  AND
         ( mPcTax.rec.IncomeRUR != 0.0 ) )
      vMon1 = SubStr( String( (mPcTax.rec.Mon + 100):3 ), 2, 2 );
      vCod1 = String( mPcTax.rec.CodIncome:4 );
      vSum1 = String( mPcTax.rec.IncomeRUR:12:2 );
      vDCod1 = "0000";
      vDSum1 = "00.00";

      stat = mPcTax.Next();
    end;

    vMon2 = "";
    vCod2 = "";
    vSum2 = "";
    vDCod2 = "";
    vDSum2 = "";

    // Пропускаем записи с нулевой суммой для правой части таблицы.
    while ( stat  AND  
            ( mPcTax.rec.TaxPayerStatus == pStatus )  AND
            ( mPcTax.rec.TaxRate == mPcRate.rec.TaxRate )  AND
            ( mPcTax.rec.IncomeRUR == 0.0 ) )
      stat = mPcTax.Next();
    end;

    if ( stat  AND  
         ( mPcTax.rec.TaxPayerStatus == pStatus )  AND
         ( mPcTax.rec.TaxRate == mPcRate.rec.TaxRate )  AND
         ( mPcTax.rec.IncomeRUR != 0.0 ) )
      vMon2 = SubStr( String( (mPcTax.rec.Mon + 100):3 ), 2, 2 );
      vCod2 = String( mPcTax.rec.CodIncome:4 );
      vSum2 = String( mPcTax.rec.IncomeRUR:12:2 );
      vDCod2 = "0000";
      vDSum2 = "00.00";

      stat = mPcTax.Next();
    end;

    if ( ( StrLen( vMon1 ) > 0 )  OR
         ( StrLen( vCod1 ) > 0 )  OR
         ( StrLen( vSum1 ) > 0 ) )
      [| ## |  ####  | ############ |  ####  |     #####   | | ## |  ####  | ############ |  ####  |     #####   |]
      ( vMon1, vCod1, vSum1, vDCod1, vDSum1, vMon2, vCod2, vSum2, vDCod2, vDSum2 )
    end;
  end;

[+----+--------+--------------+--------+-------------+ +----+--------+--------------+--------+-------------+];
[ ];

PrintLn( StrFor( 7 ) + " " + "7" );
[4. Стандартные, социальные и имущественные налоговые вычеты                                                ];
[+-----------+-------------+ +----------+-------------+ +----------+------------+ +-----------+------------+];
[│    Код    │    Сумма    │ │   Код    │    Сумма    │ │   Код    │   Сумма    │ │    Код    │    Сумма   │];
[│   вычета  │    вычета   │ │  вычета  │    вычета   │ │  вычета  │   вычета   │ │   вычета  │   вычета   │];
[+-----------+-------------+ +----------+-------------+ +----------+------------+ +-----------+------------+];
[│           │             │ │          │             │ │          │            │ │           │            │];
[+-----------+-------------+ +----------+-------------+ +----------+------------+ +-----------+------------+];
[Уведомление, подтверждающее право на социальный налоговый вычет:          №       Дата           Код ИФНС  ];
[Уведомление, подтверждающее право на имущественный налоговый вычет:       №       Дата           Код ИФНС  ];
[ ];

 if ( mPcRate.rec.CommonTaxPay > $0.0 )
   vCommonTaxPayGet = mPcRate.rec.CommonTaxPay;
   vCommonTaxPayPut = $0.0;
 else
   vCommonTaxPayGet = $0.0;
   vCommonTaxPayPut = mPcRate.rec.CommonTaxPay;
 end;

 if ( InputPanel_Year_InAll > 2010 )
   vCommonTaxPayPay = String( ( RoundToRuble(vCommonTaxPayGet) ):16:0:a:r );
 else
   vCommonTaxPayPay = "               0";
 end;

  // Расчет задолженностей.
  vDuty = mPcRate.rec.CommonTaxCalc - mPcRate.rec.CommonTaxPay;
  if ( vDuty > $0.0 )
    vDutyPayer      = vDuty;
    vDutyInspection = $0.0;
  else
    vDutyPayer      = $0.0;
    vDutyInspection = -vDuty;
  end;

PrintLn( StrFor( 7 ) + " " + "12" );
[5. Общие суммы дохода и налога                                                                             ];
[┌──────────────────────────────────┬─────────────────┬──────────────────────────────────┬─────────────────┐];
[│ Общая сумма дохода               │ ################│ Сумма налога удержанная          │ ################│]
( mPcRate.rec.CommonIncome:16:2:a:r, ( RoundToRuble(vCommonTaxPayGet) ):16:0:a:r );
[├──────────────────────────────────┼─────────────────┼──────────────────────────────────┼─────────────────┤];
[│ Налоговая база                   │ ################│ Сумма налога перечисленная       │ ################│]
( mPcRate.rec.CommonIncome:16:2:a:r, vCommonTaxPayPay );
[├──────────────────────────────────┼─────────────────┼──────────────────────────────────┼─────────────────┤];
[│ Сумма налога исчисленная         │ ################│ Сумма налога, излишне удержанная │ ################│]
( ( RoundToRuble(mPcRate.rec.CommonTaxCalc) ):16:0:a:r, ( RoundToRuble( vDutyInspection ) ):16:0:a:r );
[│                                  │                 │ налоговым агентом                │                 │];
[├──────────────────────────────────┼─────────────────┼──────────────────────────────────┼─────────────────┤];
[│ Сумма фиксированных              │                0│ Сумма налога, не удержанная      │ ################│]
( ( RoundToRuble( vDutyPayer ) ):16:0:a:r );
[│ авансовых платежей               │                 │ налоговым агентом                │                 │];
[└──────────────────────────────────┴─────────────────┴──────────────────────────────────┴─────────────────┘];
[Уведомление, подтверждающее право                                                                          ];
[на уменьшение налога на фиксированные авансовые платежи:              №         Дата             Код ИФНС  ];
[ ];

  if ( NOT stat )
    ErrCode = Status( ErrText );
    if ( ( ErrCode == 0 )  OR  ( ErrCode == 4 )  OR  ( ErrCode == 9 ) )
     stat = TRUE;
    end;
  end;

  return stat;
end;

/***********************************************************
***********************************************************/

macro Подвал_по_клиенту()

[
  Налоговый агент (1 - налоговый агент (правопреемник налогового агента), 2 - представитель
  налогового агента (правопреемника налогового агента))                                             1

  ############################################################################      __________________      ]
( sOper );
[                               (ФИО)                                                    (подпись)          
  
   Наименование и реквизиты документа, подтверждающего полномочия 
   представителя налогового агента (правопреемника налогового агента)                                                                      
                                                                                                            ];

end;

/***********************************************************
***********************************************************/

macro Справка_по_клиенту()
/*
  Разнесение данных по вкладчику в поля справки
*/
  var vFindTax = FALSE;
  var vFindSum = FALSE;
  var vReport  = FALSE;
  var vSeqVal = 1, numSeq = 1;
  var vFileStrName = txtDir + fTxt_PCTax + String( {CNum} );
  var iStr = 0, NumPage = 1, vDelta = 7;
  var flagReport = FALSE;
  var stat = True;
  var vStatus = 1;
  var st;

  SetOutPut( vFileStrName );

  mPcRate.KeyNum = 1;

  while ( vStatus < 7 )

    mPcRate.Clear();
    mPcRate.rec.Zero = 0;
    mPcRate.rec.TaxPayerStatus = vStatus;

    st = mPcRate.GetGE();

    if ( st  AND  
         ( mPcRate.rec.Zero == 0 )  AND
         ( mPcRate.rec.TaxPayerStatus == vStatus ) )
      if ( getCountUseSeq( 183, vSeqVal ) )  // Номер из последовательности.
        numSeq = vSeqVal;
      end;
      vReport = TRUE;
      Шапка_по_клиенту( numSeq, vStatus );
    end;

    while ( st  AND  
            ( mPcRate.rec.Zero == 0 )  AND
            ( mPcRate.rec.TaxPayerStatus == vStatus ) )

      vFindTax = FALSE;
      vFindSum = FALSE;

      mPcTax.Clear();
      mPcTax.rec.TaxPayerStatus = vStatus;
      mPcTax.rec.TaxRate = mPcRate.rec.TaxRate;
      stat = mPcTax.GetGE();

      while ( stat  AND
              ( mPcTax.rec.TaxPayerStatus == vStatus )  AND
              ( mPcTax.rec.TaxRate == mPcRate.rec.TaxRate ) )
        vFindTax = TRUE;
        if ( mPcTax.rec.IncomeRUR != 0.0 )
          vFindSum = TRUE;
        end;
        stat = mPcTax.Next();
      end;

      if ( vFindSum )
        Таблица_доходов_по_ставке( vStatus );
      else
        if ( vFindTax  AND  cDebug )
          PrintLn( " " );
          PrintLn( "*** Ошибка в исходных данных по налогам." );
          COMCLNT.GetRecord( COMCLNT.CurRec.rec.CodClient );
          PrintLn( "*** Клиент " + COMCLNT.CurRec.rec.Name1 + " " +
                                   COMCLNT.CurRec.rec.Name2 + " " +
                                   COMCLNT.CurRec.rec.Name3 );
          PrintLn( "*** Ставка = " + String( Round( Money( mPcRate.rec.TaxRate ) ):0:0 ) + " %" );
          if ( ( mPcRate.rec.CommonTaxCalc != 0.0 )  OR
               ( mPcRate.rec.CommonTaxPay != 0.0 ) )
            PrintLn( "*** Несоответствие данных: суммы доходов нулевые, а суммы налога не нулевые" );
          else
            PrintLn( "*** Справка не выпущена - суммы нулевые" );
          end;
        end;
      end;

      st = mPcRate.Next();

    end;

    vStatus = vStatus + 1;
  end;

  if ( vReport )
    Подвал_по_клиенту();
  else
    PrintLn( "Клиент: " + COMCLNT.CurRec.rec.Name1 + " " +
                          COMCLNT.CurRec.rec.Name2 + " " +
                          COMCLNT.CurRec.rec.Name3 + "\n" +
             "Данных по 2-НДФЛ за указанный период нет." );
  end;

  stat = TRUE;

  SetOutPut( NULL, TRUE );

  Open( ft_PCTax, vFileStrName );

  // Вывод мемориального ордера на печать.
  rewind( ft_PCTax );
  iStr = 0;
  NumPage = 2;
  while( Next( ft_PCTax ) )
    flagReport = TRUE;
    iStr = iStr + 1;
    if ( ( SubStr( ft_PCTax.Str, 1, 1 ) == StrFor( 17 ) )  OR  ( SubStr( ft_PCTax.Str, 1, 1 ) == StrFor( 7 ) ) )
      if ( SubStr( ft_PCTax.Str, 1, 1 ) == StrFor( 17 ) )
        vDelta = -1;
        Подвал_по_клиенту();
        PrintLn( strFor( 12 ) );
        NumPage = NumPage + 1;
        iStr = 0;
      elif ( ( iStr + Int( SubStr( ft_PCTax.Str, 3 ) ) ) > ( PageLen + vDelta ) )
        vDelta = -1;
        Подвал_страницы( NumPage );
        PrintLn( strFor( 12 ) );
        Заголовок_страницы( numSeq, NumPage );
        NumPage = NumPage + 1;
        iStr = 0;
      end;
    else
      PrintLn( ft_PCTax.Str );
    end;
    if ( iStr > PageLen + vDelta )
      vDelta = -1;
      Подвал_страницы( NumPage );
      PrintLn( strFor( 12 ) );
      Заголовок_страницы( numSeq, NumPage );
      NumPage = NumPage + 1;
      iStr = 0;
    end;
  end;

if ( ( NumPage == 2 )  AND  ( iStr > 5 ) )
[  ___________________________
   * Отчество указывается при наличии
                                                                                                            ];
end;

  Close( vFileStrName );
  DelFile( vFileStrName );

  if ( flagReport )
    printLn( strFor( 12 ) );
  end;

  if ( NOT stat )
    ErrCode = Status( ErrText );
    if ( ( ErrCode == 0 )  OR  ( ErrCode == 4 )  OR  ( ErrCode == 9 ) )
     stat = TRUE;
    end;
  end;

  return stat;
end;
/***********************************************************
***********************************************************/

macro Выпуск_справки_о_доходах
/*
  Цикл по вкладчикам
*/
  var stat = ERR_OK;
  var st;
  var NumCycle = 0;

  while ((stat == ERR_OK)
      OR (stat == ERR_SKIP))
    NumCycle = NumCycle + 1;
    stat = Выбрать_Данные_По_Очередному_клиенту( NumCycle);
    if (stat == ERR_OK)
      st = Справка_по_клиенту();
      if ( ( NOT st )  AND  cDebug )
        [ ];
        [ !!! ОШИБКА !!! при заполнении таблицы по вкладчику с кодом ##########](COMCLNT.CurRec.rec.CodClient);
        [ ];
      end;
    end;
  end;

  if (stat == ERR_REPEAT)
    st = False; /* Требуется повторить ввод данных */
  else
    st = True;
  end;
  return st;
end;
/***********************************************************
************************************************************
***********************************************************/

macro rpc_MakeIncomeInform
/*
  Головной макрос
*/
  var stat = False;

  /* Чтение записи Операциониста */
  opr.Clear();
  opr.rec.Oper = {Oper};
  if ( opr.GetEQ() )
    sOper = opr.rec.Name;
  else
    sOper = "???";
  end;

  while (NOT stat)
    if (Ввод_исходных_данных ("Выпуск справки о доходах"))
      Bank_Attr_NDFL();
      stat = Выпуск_справки_о_доходах();
    else
      [ Отказались от выпуска справки о доходах ];
      stat = True;
    end;
  end;

end;

/*************************************************************************
                             Головная процедура.
**************************************************************************/

rpc_MakeIncomeInform ();

/* ----- Конец файла ----- */
