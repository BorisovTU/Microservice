/*
  RS-Retail
  Инициализация счета
  Определение номера рублевого счета по правилам Ларос
  (макрос описывается в cbbank.ini)

  Ромодин Александр Васильевич
  20.02.98
*/
import deprintr;

/*---------- Инициализируемые структуры ---------------------------------*/
record dr("depositr.dbt");

file DEPOSITR (depositr) key 0;
file SB_DTYP (sb_dtyp) key 2;

file PROMF() txt 20 write; /* Промежуточные файлы */
file PROMF1() txt 20;

/*---------- Коды возврата макроса --------------------------------------*/
const  OK_RES   =  0,  /* продолжать проводку */
       SKIP_RES =  1,  /* пропустить выполнение предопределенного модуля */
       END_RES  =  2,  /* завершить проводку */
       ERR_RES  = -1;  /* аваpийно завершить проводку */
/*---------- Код филиала и валюта ---------------------------------------*/
var FNcash  = NumFNcash();
var FlagCur = NumFlagCur();
var strFNcash = String(FNcash);
if (StrLen(strFNcash)<2)
  strFNcash = "0" + strFNcash;
end;
/*---------- Интересный видывкладов -------------------------------------*/
const pension = "Пенсионный";
const uslov = "Условн.";

/*---------- Промежуточный файл -----------------------------------------*/
var namefile = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\WORKDIR", true ) + "in_laros.txt";
/**********************************************************/

var KindInLaros = "";

/***********************************************************
***********************************************************/


macro LetterLaros (TypeAccount)
/*
  Определение соответствия видов вкладов Ларос и Retail
*/
  var Kind = "";
  if (TypeAccount == "СРОЧНЫЙ")
    Kind = "0";
  elif (TypeAccount == "СР.ДЕП. 4 МП")
    Kind = "Г";
  elif (TypeAccount == "СР.ДЕП. 6 МП")
    Kind = "Г";
  elif (TypeAccount == "СР.ДЕП. 12МП")
    Kind = "Г";
  elif (TypeAccount == "Пенсионный")
    Kind = "Т";
  elif (TypeAccount == "Условн.")
    Kind = "Т";
  elif (TypeAccount == "ДО ВОСТРЕБ.")
    Kind = "Т";
  elif (TypeAccount == "Ср.1мес.выпл")
    Kind = "6";
  elif (TypeAccount == "Фикс-3")
    Kind = "И";
  elif (TypeAccount == "Фикс-6")
    Kind = "И";
  elif (TypeAccount == "Ср.пенсион.")
    Kind = "Л";
  elif (TypeAccount == "Целев. дети")
    Kind = "Н";
  elif (TypeAccount == "СБЕРЕГ.1МЕС.")
    Kind = "С";
  elif (TypeAccount == "СБЕРЕГ.2МЕС.")
    Kind = "С";
  elif (TypeAccount == "СБЕРЕГ.3МЕС.")
    Kind = "С";
  elif (TypeAccount == "ШКОЛЬНЫЙ")
    Kind = "Ш";
  elif (TypeAccount == "ЮБИЛЕЙНЫЙ")
    Kind = "Б";
  elif (TypeAccount == "НОМЕРНОЙ")
    Kind = "Ц";
  elif (TypeAccount == "РОЖДЕСТВЕН.")
    Kind = "Э";
  elif (TypeAccount == "РОЖДЕСТВЕН.+")
    Kind = "Щ";
  elif (TypeAccount == "НОВОДЕНЕЖНЫЙ")
    Kind = "Ь";
  elif (TypeAccount == "Компнес. 2")
    Kind = "Й";
  elif (TypeAccount == "ВЫИГРЫШНЫЙ")
    Kind = "А";
  elif (TypeAccount == "ВЕСЕННИЙ")
    Kind = "Э";
  elif (TypeAccount == "ВЕСЕННИЙ+")
    Kind = "Щ";
  elif (TypeAccount == "СБ-100 дней")
    Kind = "Ы";
  elif (TypeAccount == "СБ-60 дней")
    Kind = "Ъ";
  elif (TypeAccount == "Особый номер")
    Kind = "О";
  end;
  return Kind;
end;
/*********************************************************/

macro GetLastNumber
/*
  Получение последнего номера счета
*/
  var stat;
  var ErrCode;

  KeyNum(SB_DTYP,2);
  SB_DTYP.FlagCur = FlagCur;
  SB_DTYP.Kind = dr.Type_Account;
  stat = GetEQ (SB_DTYP);
  if (stat)
    KeyNum (DEPOSITR,0);
    DEPOSITR.FNCash = FNcash;
    DEPOSITR.GroupNumb = SB_DTYP.GroupNumb;
    DEPOSITR.Code_Currency = 0;
    DEPOSITR.Number = 999999;
    ReWind (DEPOSITR);
    if (GetLE(DEPOSITR) AND
     (DEPOSITR.FNCash == FNcash) AND
     (DEPOSITR.GroupNumb == SB_DTYP.GroupNumb) AND
     (DEPOSITR.Code_Currency == 0))
      if (Prev(DEPOSITR) AND
       (DEPOSITR.FNCash == FNcash) AND
       (DEPOSITR.GroupNumb == SB_DTYP.GroupNumb) AND
       (DEPOSITR.Code_Currency == 0))
        stat = True;
      else
        stat = False;
      end;
    else
      stat = False;
    end;
    if (NOT stat)
      ErrCode = Status ();
      if ((ErrCode == 0) OR (ErrCode == 4) OR (ErrCode == 9))
        DEPOSITR.Account = "А"+strFNcash+KindInLaros; /* Нет счетов */
        stat = True;
      end;
    end;
  end;
  return stat;
end;
/*********************************************************/

macro GetNextNumberLaros
/*
  Определение следующего номера по правилам Ларос
  (вызов функции Ларос)
*/
  var noml = DEPOSITR.Account;
  var stat;
  var st = False;

  if (StrLen(noml) < 10)
    noml = noml + MkStr(" ",10-StrLen(noml));
  end;
  if (Open(PROMF,namefile))
    if (Insert (PROMF,noml))
      Close (PROMF);
      stat = Run ("in_laros.exe",namefile);
      if (stat != 0)
        MsgBox ("Ошибка пуска доп.программы "+String(stat));
      else
        if (Open(PROMF1,namefile))
          ReWind (PROMF1);
          Next (PROMF1);
          dr.Account = PROMF1.str;
          st = True;
          Close (PROMF1);
        else
          MsgBox ("Ошибка при открытии временного файла");
        end;
      end;
    else
      MsgBox ("Ошибка занесения информации о счете в файл");
    end;
  else
    MsgBox (" Ошибка создания временного файла");
  end;
  return st;
end;
/**********************************************************
##### Головной макрос #####################################
**********************************************************/

macro inlaros1    /* !!! Имя макропроцедуры должно соответствовать !!! */
(                 /* !!! имени файла                               !!! */
 depos
)

  var NumAccount = "";
  var MaxStr = "Я"+strFNcash;
  var stat;

  setbuff(dr, depos);

  if (FlagCur > 0)
    return;
/*    MsgBox ("Инициализация работает только для рублей"); */
  else
    SetOutput("NUL");       /* Убираем вывод на экран*/

/*
    1. Определение вида вклада по Ларос
*/
    KindInLaros = LetterLaros (dr.Type_Account);
    if (KindInLaros == "")
      MsgBox ("Не определен вид вклада по Ларос");
    else
/*
      2. Определение номера счета
*/
/*      NumAccount = strFNcash + KindInLaros + String(ALG_DEPOSITOR.Number); */
      stat = GetLastNumber ();
      if (stat)
        stat = GetNextNumberLaros ();
      end;
      if (NOT stat)
        MsgBox ("Ошибка при определении следующего номера счета");
      elif ((dr.Type_Account == pension) OR
       (dr.Type_Account == uslov))
        dr.UseAlternate = 1;
      end;
    end;
  end;

  return;
end;
