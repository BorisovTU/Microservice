/*
**  Операционный журнал.
**  Продолжение
*/

import BankInter, deprintr, DeskInter, ChkPrn, ExchDef, "cur_rate.mac","issrvdoc.mac", Pcs2Mon, CVSettng, Ingot;

file DepParm  ("depparm.dbt")  key 0;
file Curr     ("currency.dbt")  key 0;
file CIDoc    ("ci_doc.dbt")    key 6;
file CashDoc  ("sb_casdc.dbt")  key 0;
file CashLink ("sb_casln.dbt")  key 1;
file BagDoc   ("bag_doc.dbt")  key 1;
file PayDoc   ("pay_doc.dbt")   key 5;
file CashVal  ("sb_casvl.dbt")  key 1;
file CashAcc  ("sb_casac.dbt")  key 2;
file CIOper   ("ci_oper.dbt")   key 0;
file pay_kind ("pay_kind.dbt");             /* Виды платежей       */
file pay_csop ("pay_csop.dbt");             /* Подвиды платежей    */
file doc      ("sbdepdoc.dbt") key 0;
file dr       ("depositr.dbt");
file drest    ("sb_drest.dbt");
file dtyp     ("sb_dtyp.dbt");
file Alg      ("namealg.dbt","bank.def")   key 0;
file DeskSubj ("desksubj.dbt") key 0;
file GroupMember ("groupmem.dbt") key 0;
file prs      ("person.dbt","bank.def") key 0;
file DskSubOp ("dsuboper.dbt") key 0;
file Bag      ("bag.dbt") key 1;
file CIMisc("ci_misc.dbt") key 0;
file IngRate  ("ing_rate.dbt") key 1;

record TmpCashVal ("sb_casvl.dbt");

const
  BS_BRANCH       = 0,    /* Филиал                 */
  BS_DEP          = 1,    /* Отделение              */
  BS_EXCH         = 2,    /* Обменный пункт         */
  BS_OPERDEP      = 3;    /* Оперчасть              */

const
  DST_IO          = 1,    /* Инкассация */
  DST_SAFE        = 2,    /* Сейф */
  DST_WORKDESK    = 3,    /* Рабочий стол кассира */
  DST_RDESK       = 4,    /* Касса пересчета */
  DST_PANTRY      = 5,    /* Кладовая отделения */
  DST_EDESK       = 6;    /* Вечерняя касса */

const
  B_RDESK         = 0,
  B_EDESK         = 1;

var
  {DeskOpen},
  {EDeskOpen},
  {RprtDSubj},
  Safe,
  SafeType,
  OperBrigade      = -1,
  Branch           = NumFNCash,
  BranchStatus     = GetBranchStatus,
  IsCur            = NumFlagCur,
  IsConv           = 0,
  AreIngotsPresent,
  AreCurrValsPresent,
  AccLen;

const
  PrintIfNoOps     = true,   /* Если true, печатать вх. остатки, даже если не было ни одной операции */
  BreakLines       = true;

const
  ShowBagInfo      = true;   /* Если true, печатать название клиента КО */

const
  RDesk            = "К. пересчета",
  EDesk            = "Вечер. касса";

var
  OPCONT_FLD_LEN;

const
  CI_OGSZ          = 1,
  CI_LOTTERY       = 2,
  CI_CERT          = 3,
  CI_COIN          = 4,
  CI_INGOT         = 5,
  CI_MISC          = 10,

  TYPERES_CURRENCY = 1,  /* Деньги (из справочника валют) */
  TYPERES_MINCAPISSUE = 500,

  TYPEDOC_CARRYOFF = 1,  /* Отложен (не подтвержден) */
  TYPEDOC_CARRYON  = 2,  /* Проведен (подтвержден) */

  FLAGDEBCRED_IN   = 1,  /* Кредит */
  FLAGDEBCRED_OUT  = 2,  /* Дебет */

  CASHOP_REINFORCE = 1,  /* Подкрепление кассира */
  CASHOP_RETURN    = 2,  /* Возврат средств в кладовую */
  CASHOP_COLLECT   = 3,  /* Инкассация */
  CASHOP_ADVANCE   = 4,  /* Аванс */
  CASHOP_DEBIT     = 5,  /* Расход */
  CASHOP_CREDIT    = 6,  /* Приход */
  CASHOP_REMOVE    = 7,  /* Внутреннее подкрепление (Расход) */
  CASHOP_REMOVE_1  = 8,  /* Внутреннее подкрепление (Приход) */
  CASHOP_CHVALUE   = 9,  /* Перевод на другой ресурс той же группы (Расход) */
  CASHOP_CHVALUE_1 = 10, /* Перевод на другой ресурс той же группы (Приход) */
  CASHOP_REMOVE_SAFE   = 11,  /* Внутреннее подкрепление сейфа (Расход) */
  CASHOP_REMOVE_SAFE_1 = 12,  /* Внутреннее подкрепление сейфа (Приход) */
  CASHOP_PANT_RI   = 20, /* Подкрепление филиала */
  CASHOP_PANT_RET  = 21, /* Возврат из филиала */
  CASHOP_PANT_RIO  = 22, /* Подкрепление оперчасти */
  CASHOP_PANT_RETO = 23, /* Возврат из оперчасти */
  CASHOP_PANT_COL  = 24, /* Инкассация */
  CASHOP_PANT_ADV  = 25, /* Аванс */
  CASHOP_PANT_RDEB = 30, /* Расход по кассе пересчета */
  CASHOP_PANT_RCRD = 31, /* Приход по кассе пересчета */
  CASHOP_PANT_RDRT = 32, /* Сдача ресурса в кладовую из Кассы пересчета */
  CASHOP_PANT_ECRD = 41, /* Приход по вечерней кассе */
  CASHOP_PANT_EDRT = 42, /* Сдача ресурса из вечерней кассы */

  CO_CONVERSION    = 31,
  CO_PAYED         = 41,
  CVT_NONCASH      = 2,

  SB_INCASH        = 100,
  SB_EXTCASH       = 150;

const
  CV_Currency = 1,
  CV_ValForm = 2,
  CV_PayDoc = 4,
  CV_MinCI = 500;

const                    /* Виды строк */
  LT_MAIN = 0,
  LT_WEIGHT = 1,
  LT_CBRATE = 2,
  LT_REVALUE = 3,
  LT_NTYPES = 4;

const
  TYPE_RES_PAY = 0;

const
  RESMESUNIT_POINT = 1,  /* Поштучно   */
  RESMESUNIT_MONEY = 2;  /* Стоимость  */

const
  CK_NEW          = 0, 
  CK_OLD          = 1;

array
  TotCashDebet,
  TotCashCredit,
  TotMemDebet,
  TotMemCredit,
  TotDebetV,
  TotCreditV;

var
  CurrNameDisplayed = false,
  HeaderDisplayed = false,
  SafeIDDisplayed = false,
  SomethingDisplayed = false,
  ShowSecondRefVal = true;

array
  OpContArr;

var CBRate;
var BuyRate;
var SellRate;
var ScaleRate;

macro ZeroTotals

  var
    i = 0; 

  while ( i < LT_NTYPES )
    TotCashDebet( i ) = $0;
    TotCashCredit( i ) = $0;
    TotMemDebet( i ) = $0;
    TotMemCredit( i ) = $0;
    TotDebetV( i ) = $0;
    TotCreditV( i ) = $0;
    i = i + 1;
  end;
end;

macro IsShown

  if ( not FindCVSettings( TmpCashVal ) )
    return true;
  end;
  return CVSettings.Form24 != StrFor( 0 );
end;

macro IsKept

  return CVSettings.KeepRests != StrFor( 0 );
end;

macro ShouldAddRest( IsPrev )

  var
    Shown,
    Kept;

  if ( not FindCVSettings( TmpCashVal ) )
    return true;
  end;

  Shown = CVSettings.Form24 != StrFor( 0 );
  Kept  = CVSettings.KeepRests != StrFor( 0 ); 

  if ( not Shown )
    return false;
  end;

  if ( Kept )
    return true;
  else
    if ( IsPrev )
      return false;
    else
      return true;
    end; 
  end;
end;              

macro FindCashValue( ValueRef )

  var
    Stat,
    OldKey = KeyNum( CashVal, 0 );

  CashVal.RefValue = ValueRef;
  Stat = GetEQ( CashVal );
  KeyNum( CashVal, OldKey );
  return Stat;
end;  

macro FindCIMisc( Type, IssueID )

  CIMisc.Type = Type;
  CIMisc.IssueID = IssueID;
  return GetEQ( CIMisc );
end;

macro GetIngRate( Type, Issue, Dd )

  ClearRecord( IngRate );
  IngRate.Type = Type;
  IngRate.Issue = Issue;
  IngRate.Date = Dd + 1;
  if ( GetLT( IngRate ) and ( IngRate.Type == Type ) and ( IngRate.Issue == Issue ) )
    return IngRate.CBRate;
  else
    return 0;
  end;
end;

macro CoinMoney( Issue, NumItems )

  FindCIMisc( CI_COIN, Issue );
  return Money( NumItems * CIMisc.Par );
end;

macro FindBag

  CashLink.ApplicationKind2 = CashDoc.ApplicationKind;
  CashLink.ApplicationKey2 = CashDoc.ApplicationKey;
  if ( GetGE( CashLink )
    and ( CashLink.ApplicationKind2 == CashDoc.ApplicationKind )
    and ( CashLink.ApplicationKey2 == CashDoc.ApplicationKey ) )
    BagDoc.ApplicationKind = CashLink.ApplicationKind1;
    BagDoc.ApplicationKey = CashLink.ApplicationKey1;
    if ( GetEQ( BagDoc ) )
      Bag.ApplicationKind = BagDoc.BagAppKind;
      Bag.ApplicationKey = BagDoc.BagAppKey;
      return GetEQ( Bag );
    end;
  end;
  return false;
end;

macro GetBagInfo

  var
    Info = "",
    ClientName = "";

  if ( not ShowBagInfo )
    return "";
  end;

  if ( FindBag )
    GetClientInfo( Bag.ClientType, Bag.ClientID, ClientName );
  end;
  if ( ClientName != "" )
    Info = " (" + Trim( ClientName ) + ")";
  end;
  return Info;
end;

macro GetCurrRate1

  ScaleRate = 1;
      
  CBRate   = Double(0);
  BuyRate  = Double(0);
  SellRate = Double(0);

  if (Curr.ScaleOfc != 0)
    ScaleRate = Curr.ScaleOfc;
  end;
/*
  ExchCurr.ID = Int(Curr.ExternalCode);
  if (GetEQ(ExchCurr))
*/
    copy( RtlCurr, Curr );
    CBRate = GetCurrRate({curdate});
    if (CBRate != 0)
      BuyRate  = CurrRate.BuyRate;
      SellRate = CurrRate.SellRate;
      if ( CurrRate.ChangeNom != 0 )
        BuyRate  = CurrRate.BuyRate / CurrRate.ChangeNom;
        SellRate = CurrRate.SellRate / CurrRate.ChangeNom;
      end;
    end;
/*  end;*/
end;

macro GetDiaryNumber();

  DepParm.FNCash = Branch;
  DepParm.FlagCur = 0;
  if ( not ( GetGE( DepParm )
    and ( DepParm.FNCash == Branch )
    and ( DepParm.FlagCur == 0 ) ) )
    MsgBox("Ошибка чтения параметров филиала");
    return 0;
  else
    return DepParm.NumOperDy;
  end;
end;

macro OutSubTitle(Unit)
  [ ];
  PrintLn("                                         Операционный дневник № ", GetDiaryNumber(), " за ", {curdate});
  PrintLn("                                                              ", Unit);
  [ ];
end;

macro PrintnNameCur
var rate, tmp, tmp1 = "";
 rate = CBRate;
 if ( ScaleRate != 1 )
   tmp1 = String( ScaleRate ) + " ";
 end;
 tmp =   "Курс ЦБ: " + Trim(String(rate:15:4)) + " Руб/" + tmp1 + Curr.Short_Name;
 PrintLn("Валюта:  ",Curr.Name_Currency);
 if(Curr.Code_Currency)
   PrintLn(tmp);
 end;
end;

macro GetAlgName(Type,Number)

  Alg.iTypeAlg = Type;
  Alg.iNumberAlg = Number;
  if(GetEQ(Alg))
    return Alg.szNameAlg;
  else
    return "";
  end;
end;

macro GetBranchName

  var
    RetVal;

  if(not findDepartment(Branch, RetVal))
    RetVal=String(Branch);
  end;
  return RetVal;
end;

macro Cur2Rub(Sum)

  return Money( CBRate / ScaleRate * Sum);
end;

macro CVGetNativeCurr;

  var
    Pos,
    NativeCurr = -1;

  if ( CashVal.TypeValue == TYPERES_CURRENCY )
    NativeCurr = CashVal.CodIntValue;
  else
    Pos = GetPos( CashVal );
    FindCashValue( CashVal.GroupValue );
    if ( CashVal.TypeValue == TYPERES_CURRENCY )
      NativeCurr = CashVal.CodIntValue;
    end;
    GetDirect( CashVal, Pos );
    if ( NativeCurr == -1 )
      if ( CashVal.RefValueReport < 2 )
        NativeCurr = CashVal.RefValueReport;
      else
        NativeCurr = IsCur;
      end;
    end;
  end;
  return NativeCurr;
end;

macro GetPrevRests(PrevRestM, PrevRestV, date, IsPrev)

  var
    i,
    Rt,
    RecFound,
    ValueRef = CashVal.RefValue,
    Pos,
    IngRub,
    Tmp, 
    IngotAcc;

  array
    PrRstM,
    PrRstV;

  i = 0;
  while ( i < LT_NTYPES )
    PrRstM(i) = PrRstV(i) = $0;
    i = i + 1;
  end;

  Copy( TmpCashVal, CashVal );

  if ( ShouldAddRest( IsCur ) )
    CashAcc.Branch = Branch;
    CashAcc.CashOper = Safe;
    CashAcc.RefValue = CashVal.RefValue;
    CashAcc.Date = date;
    if(GetLT(CashAcc) and
      (CashAcc.Branch == Branch) and
      (CashAcc.RefValue == CashVal.RefValue) and
      (CashAcc.CashOper == Safe))
      PrRstM(0) = CashAcc.RestMoney;
    end;

    /* Прибавим обороты по ВК */
  
    if ( {DeskOpen} )
      CashAcc.Branch = Branch;
      CashAcc.CashOper = Safe;
      CashAcc.RefValue = CashVal.RefValue;
      CashAcc.Date = date;
      if(GetEQ(CashAcc) and
        (CashAcc.Branch == Branch) and
        (CashAcc.RefValue == CashVal.RefValue) and
        (CashAcc.CashOper == Safe))
        PrRstM(0) = PrRstM(0) + CashAcc.EKredMoney - CashAcc.EDebMoney;
      end;
    end;
  else
    PrRstM(0) = $0;
  end;

  Pos = GetPos(CashVal);
  CashVal.TypeValue = 0;
  CashVal.CodIntValue = 0;
  RecFound = GetGE(CashVal);
  while(RecFound)
    Copy( TmpCashVal, CashVal );
    if ( ( CVGetNativeCurr == Curr.Code_Currency )
      and ( ( CashVal.TypeValue != CV_Currency ) or ( CashVal.Type != TYPE_RES_PAY ) ) 
      and ShouldAddRest( IsPrev ) )
      CashAcc.Branch = Branch;
      CashAcc.CashOper = Safe;
      CashAcc.RefValue = CashVal.RefValue;
      CashAcc.Date = date;
      if(GetLT(CashAcc) and
        (CashAcc.Branch == Branch) and
        (CashAcc.RefValue == CashVal.RefValue) and
        (CashAcc.CashOper == Safe))
        if(CashVal.UnitMeas == RESMESUNIT_POINT)
          if ( ( CashVal.TypeValue == TYPERES_MINCAPISSUE + CI_COIN ) 
            and ( CashVal.Type == TYPE_RES_PAY )
            and FindCIMisc( CI_COIN, CashVal.CodIntValue ) 
            and ( CIMisc.KindOfCoin == CK_NEW ) )
            PrRstM(0) = PrRstM(0) + CoinMoney( CashVal.CodIntValue, CashAcc.RestCol );
          else
            PrRstV(0) = PrRstV(0) + 
              Pieces2Money( 
                CashVal.TypeValue, 
                CashVal.CodIntValue,
                CashVal.Type,
                CashAcc.RestCol );
          end;
          if ( CashVal.TypeValue == TYPERES_MINCAPISSUE + CI_INGOT )
            IngotAcc = TIngotCashAcc( CashAcc.RefValue, CashAcc.CashOper );
            PrRstV(1) = PrRstV(1) + IngotAcc.GetTotalWeight;
            IngRub = IngotAcc.GetRubValue;
            PrRstV(2) = PrRstV(2) + IngRub;
            PrRstV(3) = PrRstV(3) + IngRub - IngotAcc.GetRubValue( {curdate} - 1 );
          end;
        else
          if ( CashVal.TypeValue == CV_PayDoc ) 
            PrRstM(0) = PrRstM(0) + CashAcc.RestMoney;
          else
            PrRstV(0) = PrRstV(0) + CashAcc.RestMoney;
          end;
        end;
      end;

      /* Прибавим обороты по ВК */

      if ( {DeskOpen} )
        CashAcc.Branch = Branch;
        CashAcc.CashOper = Safe;
        CashAcc.RefValue = CashVal.RefValue;
        CashAcc.Date = date;
        if(GetEQ(CashAcc) and
          (CashAcc.Branch == Branch) and
          (CashAcc.RefValue == CashVal.RefValue) and
          (CashAcc.CashOper == Safe))
          if(CashVal.UnitMeas == RESMESUNIT_POINT)
            Tmp = CashAcc.EKredCol - CashAcc.EDebCol;
            if ( ( CashVal.TypeValue == TYPERES_MINCAPISSUE + CI_COIN ) 
              and ( CashVal.Type == TYPE_RES_PAY )
              and FindCIMisc( CI_COIN, CashVal.CodIntValue ) 
              and ( CIMisc.KindOfCoin == CK_NEW ) )
              PrRstM(0) = PrRstM(0) + CoinMoney( CashVal.CodIntValue, Tmp );
            else
              PrRstV(0) = PrRstV(0) + Money(Tmp);
            end;
          else
            Tmp = CashAcc.EKredMoney - CashAcc.EDebMoney;
            if ( CashVal.TypeValue == CV_PayDoc ) 
              PrRstM(0) = PrRstM(0) + Tmp;
            else
              PrRstV(0) = PrRstV(0) + Tmp;
            end;
          end;
        end;
      end;
    end;
    RecFound = Next(CashVal);
  end;
  GetDirect(CashVal, Pos);

  if ( IsCur )
    PrRstM(1) = Cur2Rub( PrRstM(0) );
    PrRstV(1) = Cur2Rub( PrRstV(0) );
  end;

  SetParm(0, PrRstM);
  SetParm(1, PrRstV);
end;

macro AreCVPresent( date )

  array
    PrevRestM,
    PrevRestV;

  GetPrevRests( PrevRestM, PrevRestV, date + 1, false );
  
  if ( PrevRestV( 0 ) != $0 )
    return true;
  else
    return false;
  end;
end;

macro AreIngsPresent( date )

  var
    i,
    RecFound,
    AIP;

  AIP = false;

  GetPos(CashVal);
  CashVal.TypeValue = CV_MinCI + CI_INGOT;
  CashVal.CodIntValue = 0;
  RecFound = GetGE(CashVal);
  while(RecFound)
    CashAcc.Branch = Branch;
    CashAcc.CashOper = Safe;
    CashAcc.RefValue = CashVal.RefValue;
    CashAcc.Date = date;
    if(GetLE(CashAcc) and
      (CashAcc.Branch == Branch) and
      (CashAcc.RefValue == CashVal.RefValue) and
      (CashAcc.CashOper == Safe) and
      (CashAcc.Date == date) and
      CashAcc.RestCol)
      AIP = true; 
    end;
    RecFound = Next(CashVal);
  end;
  GetDirect(CashVal);

  return AIP;
end;

macro OutDelim
  if(not IsCur)
    [------------------------------------------------------------------------------------------------------------------------------]
    (GetBranchName);
  else
    [-------------------------------------------------------------------------------------------------------------------------------];
  end;
end;

macro OutTotalsHead

  [ ];
  [ ];
  PrintLn("                                  Итоги операционного дневника № ", GetDiaryNumber(), " за ", {curdate});
  [ ];
end;

macro OutSignForm

  [ ];
  [ ];
  [------------------------------------------------------------------------------------------------------------------------------];
  [Ст. контролер(контролер) ________________________________________________ Кассир _____________________________________________];
  [ ];
  [Отчет проверил "___" ________________ _______ г.                          Бухгалтер __________________________________________];
  [ ];
  PrintLn(StrFor(12));
end;

macro OutTotalsForCurr1

  var
    i;

  array
    PrevRestM,
    PrevRestV;

  if ( IsCur )
    {curdate} = {curdate} - 1;
    GetCurrRate1;
    {curdate} = {curdate} + 1;
  end;
  GetPrevRests(PrevRestM, PrevRestV,{curdate},true);

  if ( IsCur ) 
    GetCurrRate1;
    PrevRestM(2) = Cur2Rub( PrevRestM( 0 ) ) - PrevRestM( 1 );    /* Переоценка */
    PrevRestV(2) = Cur2Rub( PrevRestV( 0 ) ) - PrevRestV( 1 );    /* Переоценка */
  end;

  if (IsCur == 0)
/*
    OutDelim;
    [                                               |           Наличные          |   Безналичные   |           Ценности          |];
    [                                               |    Приход    |    Расход    | Зачисл.|  Спис. |    Приход    |    Расход    |];
    OutDelim;
*/
    [Остаток на начало дня                                    ##############                          ##############                 ]
    (PrevRestM(0), PrevRestV(0));
    if ( AreIngotsPresent )
      [  - на массу                                             ##############                          ##############                ]
      (PrevRestM(1):0:4, PrevRestV(1):0:4);
      [  - в рублях по курсу ЦБ                                 ##############                          ##############                ]
      (PrevRestM(2), PrevRestV(2));
      if ( ( PrevRestM(3) > 0 ) or ( PrevRestV(3) > 0 ) )
        [  - переоценка                                                              ##############       ##############                ]
        (PrevRestM(3):z, PrevRestV(3):z);
      elif ( ( PrevRestM(3) < 0 ) or ( PrevRestV(3) < 0 ) )
        [  - переоценка                                                                     ##############               ############## ]
        (-PrevRestM(3):z, -PrevRestV(3):z);
      end;
    end;
  else
/*
    OutDelim;
    [                 |      Наличные в рублях      |Безн.руб.|          Наличные           |Безналич.|          Ценности           |];
    [                 |    Приход    |    Расход    |Зач.|Спис|    Приход    |    Расход    |Зач.|Спис|    Приход    |    Расход    |];
    OutDelim;
*/
    [Ост.на начало дня                                              ##############                        ##############            ]
    (PrevRestM(0), PrevRestV(0));
    [  - в рублях              ##############                                                             ##############            ]
    (PrevRestM(1), PrevRestV(1));
    if ( ( PrevRestM(2) > 0 ) or  ( PrevRestV(2) > 0 ) )
      [  - переоценка                              ##############                                           ##############            ]
      (PrevRestM(2), PrevRestV(2));
    elif ( ( PrevRestM(2) < 0 ) or  ( PrevRestV(2) < 0 ) )
      [  - переоценка                                    ##############                                                ############## ]
      (-PrevRestM(2), -PrevRestV(2));
    end;
  end;
  OutDelim;
end;

macro OutTotalsForCurr2

  var
    i;

  array
    PrevRestM,
    PrevRestV,
    PrevRestME,
    RestM,
    RestME,
    RestV,
    CurRestM,
    CurRestV;

  GetPrevRests(PrevRestM, PrevRestV,{curdate},true);

  i = 0;
  while ( i < LT_NTYPES )
    RestM(i) = PrevRestM(i) + TotCashCredit(i) - TotCashDebet(i);
    RestV(i) = PrevRestV(i) + TotCreditV(i) - TotDebetV(i);
    i = i + 1;
  end;

  if (IsCur == 0)
    OutDelim;
    [                                               |           Наличные          |   Безналичные   |           Ценности          |];
    [                                               |    Приход    |    Расход    | Зачисл.|  Спис. |    Приход    |    Расход    |];
    OutDelim;
    [Итого по операциям за день                      ############## ##############                   ############## ############## ]
    (TotCashCredit(0):12:2:z,
     TotCashDebet(0):12:2:z,
     TotCreditV(0):12:0:z,
     TotDebetV(0):12:0:z);
    if ( AreIngotsPresent )
      [  - на массу                                    ############## ##############                   ############## ############## ]
      (TotCashCredit(1):12:4:z,
       TotCashDebet(1):12:4:z,
       TotCreditV(1):12:4:z,
       TotDebetV(1):12:4:z);
      [  - в рублях по курсу ЦБ                        ############## ##############                   ############## ############## ]
      (TotCashCredit(2):12:2:z,
       TotCashDebet(2):12:2:z,
       TotCreditV(2):12:0:z,
       TotDebetV(2):12:0:z);
    end;
    [Остаток на конец дня                            ##############                                   #############                ]
    (RestM(0), RestV(0));
    if ( AreIngotsPresent )
      [  - на массу                                    ##############                                   #############                ]
      (RestM(1):0:4, RestV(1):0:4);
      [  - в рублях по курсу ЦБ                        ##############                                   #############                ]
      (RestM(2), RestV(2));
    end;
    GetPrevRests(CurRestM, CurRestV,{curdate}+1,false);
    if((CurRestM(0) != RestM(0)) OR (CurRestV(0) != RestV(0)))
      [ВНИМАНИЕ!!!! ОСТАТОК НА КОНЕЦ ДНЯ НЕ СОВПАДАЕТ С ОСТАТКОМ КАССЫ];
    end;
    OutDelim;
  else
    OutDelim;
    [                 |      Наличные в рублях      |Безн.руб.|          Наличные           |Безналич.|          Ценности           |];
    [                 |    Приход    |    Расход    |Зач.|Спис|    Приход    |    Расход    |Зач.|Спис|    Приход    |    Расход    |];
    OutDelim;
    [Итого по опер-ям                                          ############## ##############           ############## ############## ]
    (TotCashCredit(0):10:2:z,
     TotCashDebet(0):10:2:z,
     TotCreditV(0):12:0:z,
     TotDebetV(0):12:0:z);
    [  - В рублях      ############## ##############                                                   ############## ############## ]
    (TotCashCredit(1):10:2:z,
     TotCashDebet(1):10:2:z,
     TotCreditV(1):12:0:z,
     TotDebetV(1):12:0:z);
    [Ост. на конец дня                                         ##############                          ##############                ]
    (RestM(0), RestV(0));
    [  - В рублях      ##############                                                                  ##############                ]
    (RestM(1), RestV(1));
    OutDelim;
  end;
end;

macro GetCashVal(RefValue)

  var
    Pos,
    SaveKey = KeyNum(CashVal, 0);

  Pos = GetPos(CashVal);
  CashVal.RefValue = RefValue;
  if(GetEQ(CashVal))
    Copy(TmpCashVal, CashVal);
  else
    ClearRecord(TmpCashVal);
  end;
  GetDirect(CashVal, Pos);
  KeyNum(CashVal, SaveKey);
end;

macro OutTitle

  var
    FormNo;

  if ( IsCur )
    FormNo = "Ф. 24-в"
  else
    FormNo = "Ф. 24"
  end;

  [ Филиал № ###################                                                ##########]
  (GetBranchName, FormNo:r );
end;

macro OutHead

  OutDelim();
  if (not IsCur)
    [      № лицевого      |      Содержание операции       |        Наличные           | Мемор. обор.|          Ценности         |];
    [        счета         |                                |     Приход    |  Расход   | Зач. | Спис.|   Приход    |    Расход   |];
  else
    [ № лицевого | Содержание |      Наличные в рублях   |Мем.об.руб.|        Наличные        |Мемор.обор|           Ценности     |];
    [ счета      |  операции  |    Приход    |   Расход  | Зач.|Спис.|   Приход   |   Расход  |Зач.|Спис.|    Приход    |  Расход |];
  end;
  OutDelim();
end;

macro OutOpContentTail

  var
    i = 1;

  while(i < ASize(OpContArr))
    if ( not IsCur )
      [                       ################################](OpContArr(i));
    else
      [  #######################](OpContArr(i));
    end;
    i = i + 1;
  end;
end;

macro OutLine(OpContent, CreditM, DebetM, CreditV, DebetV, 
  IsCash, IsConv, Brk, LineType)

  var
    CashDebet = $0,
    CashCredit = $0,
    MemDebet = $0,
    MemCredit = $0,
    CashDebetR = $0,
    CashCreditR = $0,
    MemDebetR = $0,
    MemCreditR = $0;

  if ( not IsCur )
    StrSplit(OpContent, OpContArr, OPCONT_FLD_LEN, OPCONT_FLD_LEN + AccLen);
  else
    StrSplit(OpContent, OpContArr, OPCONT_FLD_LEN + AccLen - 4, OPCONT_FLD_LEN + AccLen);
  end;

  if(not IsCur)
    if(IsCash)
      CashDebet = DebetM;
      CashCredit = CreditM;
    else
      MemDebet = DebetM;
      MemCredit = CreditM;
    end;
    if ( CashCredit or MemCredit or CreditV )
      [ ###################################################### ###############             #######      ##############                ]
      (
        OpContArr(0),
        CashCredit:z,
        MemCredit:z,
        CreditV:z
      );
    else
      [ ######################################################              ##############        ######               ############## ]
      (
        OpContArr(0),
        CashDebet:z,
        MemDebet:z,
        DebetV:z
      );
    end;
  else
    if ( not IsConv )
      if(IsCash)
        CashDebet = DebetM;
        CashCredit = CreditM;
        CashCreditR = Cur2Rub(CreditM);
        CashDebetR = Cur2Rub(DebetM);
      else
        MemDebet = DebetM;
        MemCredit = CreditM;
        MemCreditR = Cur2Rub(CreditM);
        MemDebetR = Cur2Rub(DebetM);
      end;
    else
      MemCreditR = CreditM;
      MemDebetR = DebetM;
    end;
    if ( CashCredit or MemCredit or CreditV )
      [######################### ##############             #####      #############             ####       ##############            ]
      (
        OpContArr(0),
        CashCreditR:z,
        MemCreditR:z,
        CashCredit:z,
        MemCredit:z,
        CreditV:z
      );
    else
      [#########################             ##############       #####           ##############      #####            ############## ]
      (
        OpContArr(0),
        CashDebetR:z,
        MemDebetR:z,
        CashDebet:z,
        MemDebet:z,
        DebetV:z
      );
    end;
  end;
  OutOpContentTail;
  if ( Brk and BreakLines )
    OutDelim;
  end;

  if ( ValType( LineType ) == V_UNDEF )
    LineType = LT_MAIN;
  end;

  if ( not IsCur )
    TotCashCredit( LineType ) = TotCashCredit( LineType ) + CashCredit;
    TotCashDebet( LineType ) = TotCashDebet( LineType ) + CashDebet;
    TotMemCredit( LineType ) = TotMemCredit( LineType ) + MemCredit;
    TotMemDebet( LineType ) = TotMemDebet( LineType ) + MemDebet;
    TotCreditV( LineType ) = TotCreditV( LineType ) + CreditV;
    TotDebetV( LineType ) = TotDebetV( LineType ) + DebetV;
  else
    TotCashCredit( 0 ) = TotCashCredit( 0 ) + CashCredit;
    TotCashCredit( 1 ) = TotCashCredit( 1 ) + CashCreditR;
    TotCashDebet( 0 ) = TotCashDebet( 0 ) + CashDebet;
    TotCashDebet( 1 ) = TotCashDebet( 1 ) + CashDebetR;
    TotMemCredit( 0 ) = TotMemCredit( 0 ) + MemCredit;
    TotMemCredit( 1 ) = TotMemCredit( 1 ) + MemCreditR;
    TotMemDebet( 0 ) = TotMemDebet( 0 ) + MemDebet;
    TotMemDebet( 1 ) = TotMemDebet( 1 ) + MemDebetR;
    TotCreditV( LineType ) = TotCreditV( LineType ) + CreditV;
    TotDebetV( LineType ) = TotDebetV( LineType ) + DebetV;
  end;
end;

macro OutTotals

  if ( not BreakLines )
    OutDelim;
  end; 
  if(not IsCur)
    [ Итого:                                               ############## ##############              ############## ############## ]
    (
      TotCashCredit(0),
      TotCashDebet(0),
      TotCreditV(0),
      TotDebetV(0)
    );
    if ( AreIngotsPresent )
      [   - по массе                                         ############## ##############              ############## ############## ]
      (
        TotCashCredit(1),
        TotCashDebet(1),
        TotCreditV(1),
        TotDebetV(1)
      );
      [   - в рублях по курсу ЦБ                             ############## ##############              ############## ############## ]
      (
        TotCashCredit(2),
        TotCashDebet(2),
        TotCreditV(2),
       TotDebetV(2)
      );
    end;
  else
    [ Итого:                                                     ############## ##############          ############ ############## ]
    (
      TotCashCredit(0),
      TotCashDebet(0),
      TotCreditV(0),
      TotDebetV(0)
    );
    [   - в рублях          ############## ##############                                               ############ ############## ]
    (
      TotCashCredit(1),
      TotCashDebet(1),
      TotCreditV(1),
      TotDebetV(1)
    );
  end;
end;

macro GetDskSubOp

  DskSubOp.Branch = Branch;
  DskSubOp.OperType = CashDoc.NumOper;
  DskSubOp.Type = CashDoc.SubOper;
  if ( GetEQ( DskSubOp ) )
    if ( ( CashDoc.NumOper != CASHOP_PANT_RDEB )
      and ( CashDoc.NumOper != CASHOP_PANT_RCRD )
      and ( CashDoc.NumOper != CASHOP_PANT_ECRD ) )
      return ", подвид " + DskSubOp.Name;
    else
      return DskSubOp.Name;
    end;
  else
    return "";
  end;
end;

macro CVGetOpContent

  var
    NativeCurr,
    Str = "";

  if ( not IsShown )
    return "";
  end;

  if ( TmpCashVal.TypeValue == TYPERES_CURRENCY )
    NativeCurr = TmpCashVal.CodIntValue;
  else
    GetPos( CashVal );
    FindCashValue( TmpCashVal.GroupValue );
    if ( CashVal.TypeValue == TYPERES_CURRENCY )
      NativeCurr = CashVal.CodIntValue;
    else
      if ( TmpCashVal.RefValueReport < 2 )
        NativeCurr = TmpCashVal.RefValueReport;
      elif ( ShowSecondRefVal )
        NativeCurr = IsCur;
      else
        NativeCurr  =0;
      end;
    end;
    GetDirect( CashVal );
  end;

  Str = Str + TmpCashVal.NameValue + " ";
  while ( StrLen( Str ) < AccLen )
    Str = Str + " ";
  end;
  if ( ( CashDoc.NumOper != CASHOP_PANT_RDEB )
    and ( CashDoc.NumOper != CASHOP_PANT_RCRD )
    and ( CashDoc.NumOper != CASHOP_PANT_ECRD ) )
    Str = Str + GetAlgName(803, CashDoc.NumOper);
  end;
  Str = Str + GetDskSubOp;
  if((CashDoc.NumOper == CASHOP_PANT_RI)
    or (CashDoc.NumOper == CASHOP_PANT_RET))
    Str = Str + " " + CashDoc.CashOper;
  elif ( ( CashDoc.NumOper == CASHOP_PANT_RDEB )
    or ( CashDoc.NumOper == CASHOP_PANT_RCRD )
    or ( CashDoc.NumOper == CASHOP_PANT_EDRT )
    or ( CashDoc.NumOper == CASHOP_PANT_ECRD ) )
    Str = Str + " " + GetBagInfo;
  end;
  if ( ( CashDoc.NumOper != CASHOP_PANT_RDEB )
    and ( CashDoc.NumOper != CASHOP_PANT_RCRD )
    and ( CashDoc.NumOper != CASHOP_PANT_ECRD ) )
    Str = Str + ": ";
  end;
  if ( Curr.Code_Currency == NativeCurr )
    if(CashDoc.Ground)
      Str = Str + " (" + CashDoc.Ground + ")";
    end;
  else
    Str = "";
  end;
  return Str;
end;

macro ReportOnCashDoc

  var
    OpContent,
    Rt,
    NoWeightCoin = false,
    IngotDoc, 
    CreditM = $0,
    DebetM = $0,
    CreditMC = $0,
    DebetMC = $0,
    CreditV = $0,
    DebetV = $0,
    CreditW = 0,
    DebetW = 0;

  GetCashVal(CashDoc.RefValue);
  if( ( TmpCashVal.TypeValue == TYPERES_MINCAPISSUE + CI_COIN ) and ( CashVal.Type == TYPE_RES_PAY ) )
    if ( FindCIMisc( CI_COIN, TmpCashVal.CodIntValue ) and ( CIMisc.KindOfCoin == CK_NEW ) )
      NoWeightCoin = true;
    end;
  end;
  OpContent = CVGetOpContent;
  if(OpContent != "")
    if(((CashDoc.FlagDebCredOper == FLAGDEBCRED_OUT)
      and (CashDoc.CashPatern == Safe)
      or ((CashDoc.FlagDebCredOper == FLAGDEBCRED_IN)
      and (CashDoc.CashPatern != Safe))))
      if(((TmpCashVal.TypeValue == CV_Currency) or (TmpCashVal.TypeValue == CV_PayDoc)) and ( TmpCashVal.Type == TYPE_RES_PAY ) )
        CreditM = CashDoc.ValueMoney;
      else
        if(TmpCashVal.UnitMeas == RESMESUNIT_POINT)
          if ( NoWeightCoin )
            CreditM = CoinMoney( TmpCashVal.CodIntValue, CashDoc.ValueCol );
          else
            CreditV = Pieces2Money( 
              TmpCashVal.TypeValue, 
              TmpCashVal.CodIntValue,
              TmpCashVal.Type,
              CashDoc.ValueCol );
          end;
        else
          CreditV = CashDoc.ValueMoney;
        end;
      end;
    else
      if(((TmpCashVal.TypeValue == CV_Currency) or (TmpCashVal.TypeValue == CV_PayDoc)) and ( TmpCashVal.Type == TYPE_RES_PAY ) )
        DebetM = CashDoc.ValueMoney;
      else
        if(TmpCashVal.UnitMeas == RESMESUNIT_POINT)
          if( NoWeightCoin )
            DebetM = CoinMoney( TmpCashVal.CodIntValue, CashDoc.ValueCol );
          else
            DebetV = Pieces2Money( 
              TmpCashVal.TypeValue, 
              TmpCashVal.CodIntValue,
              TmpCashVal.Type,
              CashDoc.ValueCol );
          end;
        else
          DebetV = CashDoc.ValueMoney;
        end;
      end;
    end;
    if( not SafeIDDisplayed )
      PrintLn;
      PrintLn;
      PrintLn( DeskSubj.Desc );
      PrintLn( MkStr( "=", StrLen( DeskSubj.Desc ) ) );
      PrintLn;
      SafeIDDisplayed = true;
      SomethingDisplayed = true;
    end;
    if(IsCur and (not CurrNameDisplayed))
      [ ];
      /*PrintLn(Curr.Name_Currency);*/
      PrintnNameCur;
      CurrNameDisplayed = true;
      SomethingDisplayed = true;
    end;
    if(not HeaderDisplayed)
      OutSubTitle("");
      OutHead;
      AreIngotsPresent = AreIngsPresent( {curdate} - 1);
      if ( not AreIngotsPresent )
        AreIngotsPresent = AreIngsPresent( {curdate} );
      end;
      AreCurrValsPresent = AreCVPresent( {curdate} );
      OutTotalsForCurr1;
      HeaderDisplayed = true;
      SomethingDisplayed = true;
    end;
    if ( ( TmpCashVal.TypeValue == TYPERES_MINCAPISSUE + CI_INGOT )
      and ( TmpCashVal.UnitMeas == RESMESUNIT_POINT ) )
      IngotDoc = TIngotCashDoc( CashDoc.ApplicationKind, CashDoc.ApplicationKey, CashDoc.RefValue );
      if ( CreditV )
        CreditW = IngotDoc.GetTotalWeight;
        CreditMC = IngotDoc.GetRubValue;
      elif ( DebetV )
        DebetW = IngotDoc.GetTotalWeight;
        DebetMC = IngotDoc.GetRubValue;
      end; 
      OutLine(OpContent, CreditM, DebetM, CreditV, DebetV, true, false, false, 0 );
      OutLine( "  - на массу", 0, 0, CreditW, DebetW, true, false, false, 1 );
      OutLine( "  - по курсу ЦБ", 0, 0, CreditMC, DebetMC, true, false, true, 2 );
    elif ( IsCur and ( TmpCashVal.TypeValue != TYPERES_CURRENCY ) )
      OutLine(OpContent, CreditM, DebetM, CreditV, DebetV, true, false, false, 0 );
      OutLine( "  - в рублях", 0, 0, Cur2Rub( CreditV ), Cur2Rub( DebetV ), true, false, true, 1 );
    else
      OutLine(OpContent, CreditM, DebetM, CreditV, DebetV, true, false, true, 0 );
    end;
  end;
end;

macro ReportOnCashValsForPantry

  var
    RecFound;

  KeyNum(CashDoc, 5);
  ClearRecord(CashDoc);
  CashDoc.Branch = Branch;
  CashDoc.CashDateDoc = {curdate};
  CashDoc.TypeDoc = TYPEDOC_CARRYON;
  CashDoc.CashPatern = Safe;
  RecFound = GetGE(CashDoc);
  while(RecFound
    and (CashDoc.Branch == Branch)
    and (CashDoc.CashDateDoc == {curdate})
    and (CashDoc.TypeDoc == TYPEDOC_CARRYON)
    and (CashDoc.CashPatern == Safe))
    if(CashDoc.Brigade == OperBrigade)
      ReportOnCashDoc;
    end;
    RecFound = Next(CashDoc);
  end;
end;

macro ReportOnCashValsForRDesk

  var
    RecFound;

  KeyNum(CashDoc, 5);
  ClearRecord(CashDoc);
  CashDoc.Branch = Branch;
  CashDoc.CashDateDoc = {curdate};
  CashDoc.TypeDoc = TYPEDOC_CARRYON;
  CashDoc.CashPatern = RDesk;
  RecFound = GetGE(CashDoc);
  while(RecFound
    and (CashDoc.Branch == Branch)
    and (CashDoc.CashDateDoc == {curdate})
    and (CashDoc.TypeDoc == TYPEDOC_CARRYON)
    and (CashDoc.CashPatern == RDesk))
    if(CashDoc.Brigade == OperBrigade)
      ReportOnCashDoc;
    end;
    RecFound = Next(CashDoc);
  end;

  KeyNum(CashDoc, 0);
  ClearRecord(CashDoc);
  CashDoc.Branch = Branch;
  CashDoc.CashDateDoc = {curdate};
  CashDoc.TypeDoc = TYPEDOC_CARRYON;
  CashDoc.CashOper = RDesk;
  RecFound = GetGE(CashDoc);
  while(RecFound
    and (CashDoc.Branch == Branch)
    and (CashDoc.CashDateDoc == {curdate})
    and (CashDoc.TypeDoc == TYPEDOC_CARRYON)
    and (CashDoc.CashOper == RDesk))
    if(CashDoc.Brigade == OperBrigade)
      ReportOnCashDoc;
    end;
    RecFound = Next(CashDoc);
  end;
end;

macro ReportOnCashValsForEDesk

  var
    RecFound;

  KeyNum(CashDoc, 5);
  ClearRecord(CashDoc);
  CashDoc.Branch = Branch;
  CashDoc.CashDateDoc = {curdate};
  CashDoc.TypeDoc = TYPEDOC_CARRYON;
  CashDoc.CashPatern = EDesk;
  RecFound = GetGE(CashDoc);
  while(RecFound
    and (CashDoc.Branch == Branch)
    and (CashDoc.CashDateDoc == {curdate})
    and (CashDoc.TypeDoc == TYPEDOC_CARRYON)
    and (CashDoc.CashPatern == EDesk))
    if(CashDoc.Brigade == OperBrigade)
      ReportOnCashDoc;
    end;
    RecFound = Next(CashDoc);
  end;
end;

macro FindCashValForCurr;
      
  var 
    Stat,
    PrevKey = KeyNum( CashVal, 1 );

  CashVal.Type = 0;
  CashVal.TypeValue = TYPERES_CURRENCY;
  CashVal.CodIntValue = Curr.Code_Currency;
  Stat =  GetEQ(CashVal);
  KeyNum( CashVal, PrevKey );

  return Stat;
end;

macro TreatCurrentSafe

  array
    PrevRstM,
    PrevRstV;

  var
    CurrCode = -1,
    RecFound;
                                    
  if ( ( ( not IsCur ) and ( Index( DeskSubj.Reports, "R" ) == 0 ) )
    or ( IsCur and ( Index( DeskSubj.Reports, "C" ) == 0 ) ) )
    return;
  end;      


  SomethingDisplayed = false;
  SafeIDDisplayed = false;

  if(not IsCur)
    Curr.Code_Currency = 0;
    if(GetEQ(Curr))
      GetCurrRate1();
      SomethingDisplayed = false;
      FindCashValForCurr;
      ZeroTotals;

      HeaderDisplayed = false;
      if ( SafeType == DST_PANTRY )
        GetPrevRests( PrevRstM, PrevRstV, {curdate}, true );
        if ( PrevRstM(0) or PrevRstV(0) )
          if( not SafeIDDisplayed )
             PrintLn;
             PrintLn;
             PrintLn( DeskSubj.Desc );
             PrintLn( MkStr( "=", StrLen( DeskSubj.Desc ) ) );
             PrintLn;
             SafeIDDisplayed = true;
             SomethingDisplayed = true;
           end;
           if(IsCur and (not CurrNameDisplayed))
             [ ];
              /*PrintLn(Curr.Name_Currency);*/
              PrintnNameCur;
              CurrNameDisplayed = true;
              SomethingDisplayed = true;
            end;
          if(not HeaderDisplayed)
            OutSubTitle("");
            OutHead;
            AreIngotsPresent = AreIngsPresent( {curdate} - 1);
            if ( not AreIngotsPresent )
              AreIngotsPresent = AreIngsPresent( {curdate} );
            end;
            AreCurrValsPresent = AreCVPresent( {curdate} );
            OutTotalsForCurr1;
            HeaderDisplayed = true;
            SomethingDisplayed = true;
          end;
        end;
        ReportOnCashValsForPantry;
      elif ( SafeType == DST_RDESK )
        ReportOnCashValsForRDesk;
      elif ( SafeType == DST_EDESK )
        ReportOnCashValsForEDesk;
      end;
      if(HeaderDisplayed)
        OutTotals;
      end;
      if(SomethingDisplayed)
        OutTotalsHead;
        OutTotalsForCurr2;
        OutSignForm;
      end;
    end;
  else
    if ( not SelectCurrency( CurrCode ) )
      if ( not GetTrue( true, "Печатать Опердневник по всем валютам?" ) )
        Exit( 1 );
      end;
    end;
    if ( CurrCode == 0 )
      MsgBox( "Опердневник по национальной валюте не печатается в режиме работы с инвалютой" );
      Exit( 1 );
    end;
    Curr.Code_Currency = 1;
    RecFound = GetGE(Curr);
    while(RecFound)
      if ( ( CurrCode < 0 ) or ( Curr.Code_Currency == CurrCode ) )
        GetCurrRate1();
        SomethingDisplayed = false;
        CurrNameDisplayed = false;
        if ( FindCashValForCurr )
          ZeroTotals;
          HeaderDisplayed = false;
          if ( SafeType == DST_PANTRY )
            GetPrevRests( PrevRstM, PrevRstV, {curdate}, true );
            if ( PrevRstM(0) or PrevRstV(0) )
              if( not SafeIDDisplayed )
                PrintLn;
                PrintLn;
                PrintLn( DeskSubj.Desc );
                PrintLn( MkStr( "=", StrLen( DeskSubj.Desc ) ) );
                PrintLn;
                SafeIDDisplayed = true;
                SomethingDisplayed = true;
              end;
              if(IsCur and (not CurrNameDisplayed))
                [ ];
                 /*PrintLn(Curr.Name_Currency);*/
                 PrintnNameCur;
                 CurrNameDisplayed = true;
                 SomethingDisplayed = true;
              end;
              if(not HeaderDisplayed)
                OutSubTitle("");
                OutHead;
                AreIngotsPresent = AreIngsPresent( {curdate} - 1);
                if ( not AreIngotsPresent )
                  AreIngotsPresent = AreIngsPresent( {curdate} );
                end;
                AreCurrValsPresent = AreCVPresent( {curdate} );
                OutTotalsForCurr1;
                HeaderDisplayed = true;
                SomethingDisplayed = true;
              end;
            end;
            ReportOnCashValsForPantry;
          elif ( SafeType == DST_RDESK )
            ReportOnCashValsForRDesk;
          elif ( SafeType == DST_EDESK )
            ReportOnCashValsForEDesk;
          end;
          if(HeaderDisplayed)
            OutTotals;
          end;                                         
          GetPrevRests(PrevRstM, PrevRstV,{curdate},true);
          if(SomethingDisplayed or PrevRstM(0) or PrevRstV(0))
            if ( ( not SomethingDisplayed ) and ( PrintIfNoOps ) )
              if( not SafeIDDisplayed )
                PrintLn;
                PrintLn;
                PrintLn( DeskSubj.Desc );
                PrintLn( MkStr( "=", StrLen( DeskSubj.Desc ) ) );
                PrintLn;
                SafeIDDisplayed = true;
              end; 
              PrintnNameCur;
              CurrNameDisplayed = true;
              OutSubTitle("");
              OutHead;
              OutTotalsForCurr1;
              HeaderDisplayed = true;
              SomethingDisplayed = true;
            end;
            OutTotalsHead;
            OutTotalsForCurr2;
            OutSignForm;
          end;
        end; 
      end; 
      if(Next(Curr))
        if(Curr.Code_Currency == 1)
          RecFound = false;
        else
          RecFound = true;
        end;
      else
        RecFound = false;
      end;
    end;
  end;
end;

  var
    RecFound;

  var
    RegPath = "RS-RETAIL\\ПЕЧАТИ\\ФОРМА_24",
    Type = V_STRING,
    Value,
    ErrCode;
  var
    RepeatCheckPrint = TRUE;

  if ( {EDeskOpen} )
    OperBrigade = B_EDESK;
  elif ( {DeskOpen} ) 
    OperBrigade = B_RDESK;
  end;

  if ( IsCur )
    AccLen = 13;
    OPCONT_FLD_LEN = 12;
  else
    AccLen = 22;
    OPCONT_FLD_LEN = 31;
  end;

  Rewind( CashVal );
  Next( CashVal );

  CheckPrinter( RegPath, StrFor( 0 ) );

  OutTitle;

  if ( {RprtDSubj} == "" )
    KeyNum( DeskSubj, 1 );
    ClearRecord( DeskSubj );
    DeskSubj.Branch = Branch;
    DeskSubj.Type = DST_PANTRY;
    RecFound = GetGE( DeskSubj );
    while ( RecFound
      and ( DeskSubj.Branch == Branch )
      and ( DeskSubj.Type == DST_PANTRY ) )
      Safe = DeskSubj.Name;
      SafeType = DeskSubj.Type;
      TreatCurrentSafe;
      RecFound = Next( DeskSubj );
    end;
    KeyNum( DeskSubj, 0 );
    DeskSubj.Branch = Branch;
    DeskSubj.Name = RDesk;
    if ( GetEQ( DeskSubj ) )
      Safe = DeskSubj.Name;
      SafeType = DeskSubj.Type;
      TreatCurrentSafe;
    end;
  else
    DeskSubj.Branch = Branch;
    DeskSubj.Name = {RprtDSubj};
    if ( GetEQ( DeskSubj ) )
      Safe = DeskSubj.Name;
      SafeType = DeskSubj.Type;
      TreatCurrentSafe;
    end;
  end;
  ExitMacro;
end;
