
/* Выполнение операций из файла ручного ввода */

import deprintr;

file ManualInputData(maninput) key 0;
file Accounts(depositr) key 7;
var
 BranchCode,
 FlagCur,
 CurrencyCode,
 AccountType,
 StartDate,
 EndDate;
var
 {curdate};

macro SelectCurrency

  file Currency(currency) key 0;
  array
    MenuItems,MenuItemCodes;
  var
    i,RecFound,Choice;

  i=0;
  Currency.Code_Currency=1; /* Отсекаем рубли */

  RecFound=GetGE(Currency);
  while(RecFound)
    MenuItems(i)=Currency.Name_Currency;
    MenuItemCodes(i)=Currency.Code_Currency;
    RecFound=Next(Currency);
    i=i+1;
  end;

  Choice=Menu(MenuItems,"Выбор валюты","Выберите валюту");
  return MenuItemCodes(Choice);
end;

macro SelectAccountType;

  file AccountTypes(sb_dtyp) key 2;
  array
    MenuItems;
  var
    RecFound,Choice,i;

  i=0;
  AccountTypes.FlagCur=FlagCur;
  AccountTypes.Kind="";

  RecFound=GetGE(AccountTypes);
  while(RecFound and (AccountTypes.FlagCur==FlagCur))
    MenuItems(i)=AccountTypes.Kind;
    RecFound=Next(AccountTypes);
    i=i+1;
  end;

  Choice=Menu(MenuItems,"Выбор вида вклада","Выберите вид вклада");
  return Choice;
end;

macro PerformOperation;

  record TmpDoc(sbdepdoc);
  var
    Result;

  Accounts.FNCash=BranchCode;
  Accounts.Account=ManualInputData.Account;
  if(not GetEQ(Accounts))
    MsgBox("Счет не найден: "+Acc_Form(ManualInputData.Account));
    return;
  end;

  TmpDoc.Referenc = Accounts.Referenc;
  if(ManualInputData.CurrencyCode!=0)
    TmpDoc.IsCur=1;
  else
    TmpDoc.IsCur=0;
  end;
  TmpDoc.FNCash = BranchCode;
  TmpDoc.Account = ManualInputData.Account;
  TmpDoc.Oper = 9999;
  TmpDoc.Type_Account = ManualInputData.AccountType;
  TmpDoc.Code_Currency = ManualInputData.CurrencyCode;
  TmpDoc.CodClient = Accounts.CodClient;
  TmpDoc.YesSbook = "X";
  TmpDoc.Date_Document = ManualInputData.Date;
  TmpDoc.DepDate_Document = ManualInputData.Date;
  TmpDoc.Author = 0;
  TmpDoc.OutSum = ManualInputData.Sum;
  TmpDoc.IsControl = StrFor(1); /* Признак главного документа */
  Result = Выполнить_Операцию(
    ManualInputData.AccountType,
    ManualInputData.Account,
    ManualInputData.CurrencyCode,
    ManualInputData.OperType,
    TmpDoc,
    0,
    1,
    1
  );
  if(Result)
    MsgBox("Ошибка при выполнении операции, код "+String(Result));
  end;
end;

macro TreatManualInput;

  var
    RecFound;

  ManualInputData.CurrencyCode=CurrencyCode;
  ManualInputData.AccountType=AccountType;
  ManualInputData.Date=Date(0,0,0);

  RecFound=GetGE(ManualInputData);
  while(RecFound and
    (ManualInputData.Date>=StartDate) and
    (ManualInputData.Date<=EndDate))
    PerformOperation;
    RecFound=Next(ManualInputData);
  end;
end;

  BranchCode=NumFNCash;
  FlagCur=NumFlagCur;

  if(not FlagCur)
    CurrencyCode=0;
  else
    CurrencyCode=SelectCurrency;
  end;
  AccountType=SelectAccountType;
  StartDate={curdate};
  GetDate(StartDate,"Введите начальную дату");
  EndDate={curdate};
  GetDate(EndDate,"Введите конечную дату");

  TreatManualInput;
end;