/* Отчеты по ОГСЗ */

import DeprInter;

file CapIssueDoc("ci_doc.dbt") key 2;
file CapIssueRec("ci_rec.dbt") key 0;
file OGSZIssue("ogsz_iss") key 0;
record OGSZDocInfo("ci_doc.1");
record OGSZRecInfo("ci_rec.1");

var
  Branch=NumFNCash,
  {curdate},
  RetiredSum,  
  WarrTotSum,
  TotNumItems,
  TotWarrYieldSum,
  TotCommissionSum,
  TotSum,
  TotTaxSum,
  TotRetiredSum,
  TotWarrTotSum;

array
  WYSumArr,
  TotWYSumArr;

const
  CI_OGSZ=1;

const
  O_OGSZ_SELL=1,
  O_OGSZ_BUY=2,
  O_OGSZ_PAYWARR=3,
  O_OGSZ_KEEP=4;


macro OutOperHead;

  [
                                                                               Филиал №
                                                                               отделения
                                                                               Сбербанка РФ


                                    Отчет по операциям за ##########

  ]
  (
    {curdate}
  );
end;

macro OutSellHead;

  [ ];
  [Продажа облигаций];
  [ ];
  [+------+------------------+------+------------------+------------------+------------------+------------------+]; 
  [|Серия |     Номинал      | Кол. |      Н К Д       |Курсовая надбавка |     Получено     |   В т.ч. налог   |];
  [+------+------------------+------+------------------+------------------+------------------+------------------+];
end;

macro GetNumItems

  var
    RecFound,
    NumItems=0;

  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;

  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    SetRecordAddr(OGSZDocInfo,CapIssueDoc,0,
      FldOffset(CapIssueDoc,"Info"),true);
    NumItems=NumItems+OGSZDocInfo.NumItems;
    RecFound=Next(CapIssueDoc);
  end;
  return NumItems;
end;

macro OutSellLine;

  SetRecordAddr(OGSZRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [| #### | ################ | #### | ################ | ################ | ################ | ################ |]
  (
    OGSZRecInfo.Series,
    OGSZRecInfo.Par,
    GetNumItems,
    OGSZRecInfo.WarrYieldSum,
    OGSZRecInfo.CommissionSum,
    CapIssueRec.Sum,
    OGSZRecInfo.TaxSum
  );
end;

macro OutBuyHead;

  [ ];
  [Покупка облигаций];
  [ ];
  [+------+------------------+------+------------------+------------------+------------------+]; 
  [|Серия |     Номинал      | Кол. |      Н К Д       |Курсовая надбавка |     Получено     |];
  [+------+------------------+------+------------------+------------------+------------------+];
end;

macro OutBuyLine;

  SetRecordAddr(OGSZRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [| #### | ################ | #### | ################ | ################ | ################ |]
  (
    OGSZRecInfo.Series,
    OGSZRecInfo.Par,
    GetNumItems,
    OGSZRecInfo.WarrYieldSum,
    OGSZRecInfo.CommissionSum,
    CapIssueRec.Sum
  );
end;

macro OutSellTotals;

  [+-------------------------+------+------------------+------------------+------------------+------------------+];
  SetRecordAddr(OGSZRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [| И т о г о :             | #### | ################ | ################ | ################ | ################ |]
  (
    TotNumItems,
    TotWarrYieldSum,
    TotCommissionSum,
    TotSum,
    TotTaxSum
  );
end;

macro OutBuyTotals;

  SetRecordAddr(OGSZRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [+-------------------------+------+------------------+------------------+------------------+];
  [| И т о г о :             | #### | ################ | ################ | ################ |]
  (
    TotNumItems,
    TotWarrYieldSum,
    TotCommissionSum,
    TotSum
  );
end;

macro OutPayWarrHead;

  [ ];
  [Оплата купонов и погашение облигаций];
  [ ];
  [+------+------------------+----------------------------------------------------------------------------------------------+------------------+------------------+];
  [|      |                  |                   С у м м а ,  в ы п л а ч е н н а я   п о   к у п о н а м                   | Выплачено по     |                  |];
  [|Серия |     Номинал      |------------------+------------------+------------------+------------------+------------------| погашенным       | Итого выплачено  |];
  [|      |                  |      1 - м у     |      2 - м у     |      3 - м у     |      4 - м у     |    В с е г о     | облигациям       |                  |];
  [+------+------------------+------------------+------------------+------------------+------------------+------------------+------------------+------------------+];
end;

macro GetOGSZIssue;

  OGSZIssue.Type=CI_OGSZ;
  OGSZIssue.IssueID=CapIssueRec.Issue;
  if(not GetEQ(OGSZIssue))
    MsgBox("Выпуск ОГСЗ не найден: #"+String(CapIssueRec.Issue));
  end; 
end;

macro GetWarrantYieldSums;

  var
    RecFound,
    i=1;
  
  while(i<=4)
    WYSumArr(i)=$0;
    i=i+1;
  end;
  RetiredSum=$0;                      
  WarrTotSum=$0;

  GetOGSZIssue;
  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;

  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    SetRecordAddr(OGSZDocInfo,CapIssueDoc,0,FldOffset(CapIssueDoc,"Info"),true);
    if(OGSZDocInfo.WarrantNumber!=OGSZIssue.NumWarrants)
       WYSumArr(OGSZDocInfo.WarrantNumber)=
         WYSumArr(OGSZDocInfo.WarrantNumber)+CapIssueDoc.Sum;
    else
       WYSumArr(OGSZDocInfo.WarrantNumber)=
         WYSumArr(OGSZDocInfo.WarrantNumber)+CapIssueDoc.Sum-
         OGSZIssue.Par*OGSZDocInfo.NumItems;
       RetiredSum=RetiredSum+OGSZIssue.Par*OGSZDocInfo.NumItems;
    end;
    RecFound=Next(CapIssueDoc);
  end;
         
  i=1;
  while(i<=4)
    WarrTotSum=WarrTotSum+WYSumArr(i);
    i=i+1;
  end;
end;

macro OutPayWarrLine

  SetRecordAddr(OGSZRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [| #### | ################ | ################ | ################ | ################ | ################ | ################ | ################ | ################ |]
  (
    OGSZRecInfo.Series,
    OGSZRecInfo.Par,
    WYSumArr(1),
    WYSumArr(2),
    WYSumArr(3),
    WYSumArr(4),
    WarrTotSum,
    RetiredSum,
    CapIssueRec.Sum
  );
end;

macro OutPayWarrTotals

  SetRecordAddr(OGSZRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [+------+------------------+------------------+------------------+------------------+------------------+------------------+------------------+------------------+];
  [| И т о г о :             | ################ | ################ | ################ | ################ | ################ | ################ | ################ |]
  (
    TotWYSumArr(1),
    TotWYSumArr(2),
    TotWYSumArr(3),
    TotWYSumArr(4),
    TotWarrTotSum,
    TotRetiredSum,
    TotSum
  );
end;

macro SellReport

  var
    RecFound;

  TotNumItems=0;
  TotWarrYieldSum=$0;
  TotCommissionSum=$0;
  TotSum=$0;
  TotTaxSum=$0;

  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_OGSZ;

  OutSellHead;
  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_OGSZ))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Operation==O_OGSZ_SELL))
      OutSellLine; 
      SetRecordAddr(OGSZRecInfo,CapIssueRec,0,
        FldOffset(CapIssueRec,"Info"),true);
      TotNumItems=TotNumItems+GetNumItems;
      TotWarrYieldSum=TotWarrYieldSum+OGSZRecInfo.WarrYieldSum;
      TotCommissionSum=TotCommissionSum+OGSZRecInfo.CommissionSum;
      TotSum=TotSum+CapIssueRec.Sum;
      TotTaxSum=TotTaxSum+OGSZRecInfo.TaxSum;
    end;
    RecFound=Next(CapIssueRec);
  end;
  OutSellTotals;
end;
    
macro BuyReport

  var
    RecFound;

  TotNumItems=0;
  TotWarrYieldSum=0;
  TotCommissionSum=0;
  TotSum=0;
  TotTaxSum=0;

  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_OGSZ;

  OutBuyHead;
  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_OGSZ))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Operation==O_OGSZ_BUY))
      OutBuyLine; 
      SetRecordAddr(OGSZRecInfo,CapIssueRec,0,
        FldOffset(CapIssueRec,"Info"),true);
      TotNumItems=TotNumItems+GetNumItems;
      TotWarrYieldSum=TotWarrYieldSum+OGSZRecInfo.WarrYieldSum;
      TotCommissionSum=TotCommissionSum+OGSZRecInfo.CommissionSum;
      TotSum=TotSum+CapIssueRec.Sum;
      TotTaxSum=TotTaxSum+OGSZRecInfo.TaxSum;
    end;
    RecFound=Next(CapIssueRec);
  end;
  OutBuyTotals;
end;

macro PayWarrReport

  var
    RecFound,
    i=1;

  while(i<=4)
    TotWYSumArr(i)=$0;
    i=i+1;
  end;
  TotRetiredSum=$0;
  TotWarrTotSum=$0;
  TotSum=$0;

  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_OGSZ;

  OutPayWarrHead;
  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_OGSZ))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Operation==O_OGSZ_PAYWARR))
      GetWarrantYieldSums;
      OutPayWarrLine; 
      SetRecordAddr(OGSZRecInfo,CapIssueRec,0,
        FldOffset(CapIssueRec,"Info"),true);
      i=1;
      while(i<=4)
        TotWYSumArr(i)=TotWYSumArr(i)+WYSumArr(i);
        i=i+1;
      end;
      TotRetiredSum=TotRetiredSum+RetiredSum;
      TotWarrTotSum=TotWarrTotSum+WarrTotSum;
      TotSum=TotSum+CapIssueRec.Sum;
    end;
    RecFound=Next(CapIssueRec);
  end;
  OutPayWarrTotals;
end;

macro OGSZOperReport;

  OutOperHead;
  SellReport;
  BuyReport;
  PayWarrReport;
end;

macro OutSellBuyRecHead;

  SetRecordAddr(OGSZRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [
     ____________________Отделение                                                                               Филиал №_______

     Сберегательного банка №______              


  
                                                          В Е Д О М О С Т Ь

                                           на покупку и продажу облигаций государственного
                                        сберегательного займа Российской Федерации #### займа
                                                           за ##########


  ]
  (
    OGSZRecInfo.Series,
    {curdate}
  );
  [+-----------+-------------------------------------------------------+------------------------------------+------------------+];
  [| Номерной  |                      П р о д а н о                    |            К у п л е н о           |   П о д п и с и  |];
  [|  талон,   |------------------+------------------------------------|------------------+-----------------|---------+--------|];
  [| выданный  |  Ном. стоимость  |  Получено за проданные облигации   |  Ном. стоимость  |  Выплачено за   |         |        |];
  [| клиенту   |    проданных     |------------------+-----------------|    купленных     |   купленные     | Контро- | Кассир |];
  [|           |    облигаций     |       Всего      |В том числе налог|    облигаций     |   облигации     |   лер   |        |];
  [+-----------+------------------+------------------+-----------------+------------------+-----------------+---------+--------+];
end;

macro OutSellBuyRecLine;
 
  var
    SoldSum=$0,
    BoughtSum=$0,
    SoldParSum=$0,
    BoughtParSum=$0,
    Tax=$0;

  SetRecordAddr(OGSZDocInfo,CapIssueDoc,0,
    FldOffset(CapIssueDoc,"Info"),true);
  if(CapIssueRec.Operation==O_OGSZ_SELL)
    SoldSum=CapIssueDoc.Sum;
    SoldParSum=OGSZDocInfo.Par*OGSZDocInfo.NumItems;
    Tax=OGSZDocInfo.Tax*OGSZDocInfo.NumItems;
  elif(CapIssueRec.Operation==O_OGSZ_BUY)
    BoughtSum=CapIssueDoc.Sum;
    BoughtParSum=OGSZDocInfo.Par*OGSZDocInfo.NumItems;
  end;

  [|   ####    | ################ | ################ | ############### | ################ | ############### |         |        |]
  (
    CapIssueDoc.DocNumber,
    SoldParSum,
    SoldSum,
    Tax,
    BoughtParSum,
    BoughtSum
  );
end;

macro OutPayWarrRecHead;

  SetRecordAddr(OGSZRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [
     ____________________Отделение                                                                                                                    Филиал №_______

     Сберегательного банка №______              


  
                                                                             В Е Д О М О С Т Ь

                                                          на оплату купонов и погашение облигаций государственного
                                                            сберегательного займа Российской Федерации #### займа
                                                                               за ##########


  ]
  (
    OGSZRecInfo.Series,
    {curdate}
  );
  [+-----------+------------------+--------------------------------------------------------------------------------------------+------------------+-----------------+];
  [| Номерной  |                  |                  С у м м а ,  в ы п л а ч е н н а я   п о   к у п о н а м                  |      Сумма,      |                 |];
  [|  талон,   |   Номинальная    |                                                                                            |    выплаченная   |      Итого      |];
  [| выданный  |                  |------------------+-----------------+------------------+-----------------+------------------|   по погашенным  |                 |];
  [| клиенту   |    стоимость     |     1 - м у      |     2 - м у     |     3 - м у      |    4 - м у      |    В с е г о     |    облигациям    |    выплачено    |];
  [|           |                  |                  |                 |                  |                 |                  |                  |                 |];
  [+-----------+------------------+------------------+-----------------+------------------+-----------------+------------------+------------------+-----------------+];
end;

macro OutPayWarrRecLine;

  var 
    i=1;

  while(i<=4)
    WYSumArr(i)=$0;
    i=i+1;
  end;
  RetiredSum=$0;                      
  
  GetOGSZIssue;
  SetRecordAddr(OGSZDocInfo,CapIssueDoc,0,FldOffset(CapIssueDoc,"Info"),true);
  if(OGSZDocInfo.WarrantNumber!=OGSZIssue.NumWarrants)
    WYSumArr(OGSZDocInfo.WarrantNumber)=CapIssueDoc.Sum;
  else
    WYSumArr(OGSZDocInfo.WarrantNumber)=CapIssueDoc.Sum-
      OGSZIssue.Par*OGSZDocInfo.NumItems;
    RetiredSum=OGSZIssue.Par*OGSZDocInfo.NumItems;
  end;
  WarrTotSum=WYSumArr(OGSZDocInfo.WarrantNumber);

  [|   ####    | ################ | ################ | ############### | ################ | ############### | ################ | ################ | ############### |]
  (
    CapIssueDoc.DocNumber,
    OGSZDocInfo.Par*OGSZDocInfo.NumItems,
    WYSumArr(1),
    WYSumArr(2),
    WYSumArr(3),
    WYSumArr(4),
    WarrTotSum,
    RetiredSum,
    CapIssueDoc.Sum
  );
end;

macro OutRecTail;

  [



                                                                                                       Ст. бухгалтер ___________                                
  ];
end;

macro OGSZSellBuyRec;
 
  var
    RecFound;

  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;

  OutSellBuyRecHead; 
  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    OutSellBuyRecLine;
    RecFound=Next(CapIssueDoc);
  end;
  OutRecTail;
end;

macro OGSZPayWarrRec;
 
  var
    RecFound;

  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;

  OutPayWarrRecHead; 
  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    OutPayWarrRecLine;
    RecFound=Next(CapIssueDoc);
  end;
  OutRecTail;
end;

macro OGSZRec(ApplicationKind,ApplicationKey);
                                              
  KeyNum(CapIssueRec,2);
  CapIssueRec.ApplicationKind=ApplicationKind;
  CapIssueRec.ApplicationKey=ApplicationKey;
  if(not GetEQ(CapIssueRec))
    MsgBox("Ошибка при печати ведомости");
    return;
  end;
  if((CapIssueRec.Operation==O_OGSZ_SELL) 
    or (CapIssueRec.Operation==O_OGSZ_BUY))
    OGSZSellBuyRec;
  elif(CapIssueRec.Operation==O_OGSZ_PAYWARR)
    OGSZPayWarrRec;
  end; 
end;

end;                            


    

  


