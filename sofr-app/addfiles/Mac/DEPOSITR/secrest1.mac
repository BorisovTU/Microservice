/*
  RS-Retail 5.1
  Шаг "Ограничение на сумму прихода" 
  операции "Открытие счета"
------------------------------------------------------------
  Вид вклада- "Особый номерной на 2 года"
------------------------------------------------------------
   Процентная ставка зависит от неснижаемого остатка, 
  который может поменяться в течение 30 дней.
   Макрос заносит в поле DEPOSITR.StaticRest возможное
  значение неснижаемого остатка.
------------------------------------------------------------
    Ромодин Александр Васильевич
    12 августа 2002 г.
*/
Import deprintr, alg_vars;

file PC_RATE_SecRest (pc_rate)  key 0; /* Справочник ставок  */
file DEP_secrest1    (depositr) key 8; /* Счета вкладчика    */

array Rest;

const ACCOUNT_IMPORTER  = StrFor(4);    /* Вноситель           */
const UPDATE_DEP        = 2;

var  TypeUpd = TRUE;

/* ****************************************************** */

macro GetRestArray
/*
  Получить список допустимых неснижаемых остатков
*/
  var stat = false;

  ClearRecord (PC_RATE_SecRest);

  PC_RATE_SecRest.FlagCur        = ALG_DEPOSITOR.IsCur;    
  PC_RATE_SecRest.ObjectType     = 1003;
  PC_RATE_SecRest.Referenc       = ALG_DEPOSITOR.Type_Account;
  PC_RATE_SecRest.Code_Currency  = ALG_DEPOSITOR.Code_Currency;
  PC_RATE_SecRest.BeginDate      = ALG_DEPOSITOR.Open_Date;
  PC_RATE_SecRest.DatePeriodType = 999;
  PC_RATE_SecRest.DatePeriod     = 999;
  stat = GetLE (PC_RATE_SecRest);
  if (stat)
    if ( (PC_RATE_SecRest.FlagCur        != ALG_DEPOSITOR.IsCur)
     OR  (PC_RATE_SecRest.ObjectType     != 1003)
     OR  (PC_RATE_SecRest.Referenc       != ALG_DEPOSITOR.Type_Account)
     OR  (PC_RATE_SecRest.Code_Currency  != ALG_DEPOSITOR.Code_Currency) )
      stat = false;
    elif (PC_RATE_SecRest.RestCount <= 0)
      stat = false;
    else
      Rest(1)     = PC_RATE_SecRest.Rest1;
      Rest(2)     = PC_RATE_SecRest.Rest2;
      Rest(3)     = PC_RATE_SecRest.Rest3;
      Rest(4)     = PC_RATE_SecRest.Rest4;
      Rest(5)     = PC_RATE_SecRest.Rest5;  
      Rest(6)     = PC_RATE_SecRest.Rest6;  
      Rest(7)     = PC_RATE_SecRest.Rest7;  
      Rest(8)     = PC_RATE_SecRest.Rest8;  
      Rest(9)     = PC_RATE_SecRest.Rest9;  
      Rest(10)    = PC_RATE_SecRest.Rest10;
      Rest(11)    = PC_RATE_SecRest.Rest11;
      Rest(12)    = PC_RATE_SecRest.Rest12;
      Rest(13)    = PC_RATE_SecRest.Rest13;
      Rest(14)    = PC_RATE_SecRest.Rest14;
      Rest(15)    = PC_RATE_SecRest.Rest15;
      Rest(16)    = PC_RATE_SecRest.Rest16;
      Rest(17)    = PC_RATE_SecRest.Rest17;
      Rest(18)    = PC_RATE_SecRest.Rest18;
      Rest(19)    = PC_RATE_SecRest.Rest19;
      Rest(20)    = PC_RATE_SecRest.Rest20;
    end;
  end;
  return stat;
end;
/* ****************************************************** */

macro RestInList (TestRest)
  var i = 1,
      stat = false;
  while ((NOT stat)
     AND (i < 21))
    if (TestRest == Rest(i))
      stat = true;
    end;
    i = i + 1;
  end;
  return stat;
end;
/* ****************************************************** */

macro GetNextRest 
/*
  Получить из массива Rest возможную величину неснижаемого остатка
*/
  var Limit:money   = 0;
  var SecRest:money = 0;
  var i = 20;
  var Cont;
  var MaxRest:money = 0;
/*
  Новый неснижаемый остаток
*/
  while (i > 0)
    if ( (MaxRest == 0)
     AND (Rest(i) > 0) )
      MaxRest = Rest(i);
    end; 
    if ( (Rest(i) > 0)
     AND (Rest(i) <= ALG_SBDEPDOC.InSum) )
      Limit = Rest(i);
      if (StrFor(GetProgramID) != "П")
        if (Limit != ALG_DEPOSITOR.Limit)
          Cont = ConfDlg ("Установить неснижаемый остаток "+Limit+"?",
                           NULL,
                          "Да","Нет");
          if (Cont == 0)
            ALG_DEPOSITOR.Limit = Limit;
            UpdateAlgRecords (UPDATE_DEP, TRUE, TypeUpd);
          else
            Limit = Rest(1);
            Cont = 1;
            while (Cont != 0)
              if (GetMoney (Limit, "Введите величину неснижаемого остатка")
               AND (Limit > 0))
                if (Limit > ALG_SBDEPDOC.InSum)
                  MsgBox ("Неснижаемый остаток не больше первоначального взноса!");
                  Limit = Rest(1);
                elif (RestInList(Limit))
                  Cont = 0;
                else
                  MsgBox ("Ввели значение неснижаемого остатка не из списка допустимых");
                end;
              else
                Cont = ConfDlg ("Установить минимально возможный неснижаемый остаток ("+String(Limit)+")?",
                                 NULL,
                                 "Да","Нет");
                if (Cont == 0)
                  Limit = Rest(1);
                end;
              end;
              if (Cont == 0)
                ALG_DEPOSITOR.Limit = Limit;
                UpdateAlgRecords (UPDATE_DEP, TRUE, TypeUpd);
              end;
            end;
          end;
        end;
      end;
      i = 0;
    else
      i = i - 1;
    end;
  end;
/*
  Неснижаемый остаток, который можно установить в течение 30 дней
*/
  if ( (MaxRest > 0)
   AND (MaxRest == ALG_DEPOSITOR.Limit))
    if (StrFor(GetProgramID) != "П")
      MsgBox ("Изменения неснижаемого остатка в течение 30 дней не будет:|",
              "Задано максимальное значение неснижаемого остатка");
    end;
  else
    i = 1;
    while (i < 21)
      if (Rest(i) > ALG_DEPOSITOR.Limit)
        SecRest = Rest(i);
        i = 21;
      else
        i = i + 1;
      end;
    end;
  end;
  return SecRest;
end;
      
/* ##### Точка входа ##### */

  var Cont; 
  var OldRest,
      NewRest;
  ALG_RESULT = OK_RES;
   
  if ((ALG_SBDEPDOC.TypeComplexOper == 80)
   OR (ALG_SBDEPDOC.TypeComplexOper == 65)
   OR (ALG_SBDEPDOC.TypeComplexOper == 78))
    TypeUpd = FALSE; /* 11141 Для открытия в рамках перечисления операции изменения заносим */
                     /* только в RecBuf, но не сохраняем в файле */
  end;
  if (ALG_SBDEPDOC.Author == ACCOUNT_IMPORTER)
    if (StrFor(GetProgramID) != "П")
      MsgBox ("Для вносителя операции запрещены");
    end;
    ALG_RESULT = ERR_RES;
  elif (ALG_SBDEPDOC.InSum < ALG_PARAM.prmD)
    if (StrFor(GetProgramID) != "П")
      MsgBox ("Минимальная сумма прихода- ",ALG_PARAM.prmD);
    end;
    ALG_RESULT = ERR_RES;
  elif (ALG_SBDEPDOC.InSum < ALG_DEPOSITOR.Limit)
    if (StrFor(GetProgramID) != "П")
      MsgBox ("Минимально допустимый остаток- ",ALG_DEPOSITOR.Limit);
    end;
    ALG_RESULT = ERR_RES;
  elif (StrFor(GetProgramID) != "П")
    ALG_RESULT = SKIP_RES;
    if (GetRestArray())
      OldRest = GetNextRest ();
      if (OldRest > 0)
        NewRest = OldRest;
        Cont = 1;
        while (Cont != 0)
          if (GetMoney (NewRest, "Введите величину неснижаемого остатка для изменения в течение 30 дней")
           AND (NewRest > 0))
            if (RestInList(NewRest))
              if (NewRest >= ALG_DEPOSITOR.Limit)
                ALG_DEPOSITOR.StaticRest = NewRest;
                UpdateAlgRecords (UPDATE_DEP, TRUE, TypeUpd);
                Cont = 0;
              else
                MsgBox ("Новое значение неснижаемого остатка должно быть больше текущего.|",
                        "Текущее значение неснижаемого остатка: ",ALG_DEPOSITOR.Limit);
              end;
            else
              MsgBox ("Ввели новое значение неснижаемого остатка не из списка допустимых");
            end;
          else
            Cont = ConfDlg ("Вы отказались от изменения неснижаемого остатка в течение 30 дней.",
                             NULL,
                            "Да","Нет");
          end;
        end;
      end;
    else
      Cont = ConfDlg ("Нет списка неснижаемых остатков.",
                      "Не будет задано изменение неснижаемого остатка в течение 30 дней.",
                      "Продолжать открытие счета?",
                       NULL,
                      "Да","Нет");
      if (Cont == 1)
        ALG_RESULT = ERR_RES;
      end;
    end;
  else /* Для Последующего контроля- простое копирование лимитов */
    if (Open (DEP_secrest1,"depositr.dbt"))
      DEP_secrest1.Referenc = ALG_DEPOSITOR.Referenc;
      if (GetEQ(DEP_secrest1))
        ALG_DEPOSITOR.Limit      = DEP_secrest1.Limit;
        ALG_DEPOSITOR.StaticRest = DEP_secrest1.StaticRest;
        UpdateAlgRecords (UPDATE_DEP, TRUE, TypeUpd);
      end;
      Close (DEP_secrest1);
    end;
  end;
end;
/* ----- Конец файла ----- */
