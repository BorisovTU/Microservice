/***************************************************************************\
| CLOSEDE3.MAC - макрос закрытия счета для вида вклада "НОМЕРНОЙ"           |
|                                                     Зубов В.Ю. 18-09-97   |
\***************************************************************************/
/* при хранении счета меньше одного месяца отчислять причисленные %%       */
/* и не считать %% за текущий месяц                                       */

import denomin,deprintr,IsSrvDoc,Календарь,alg_vars, PmPropUtil;

record FINISH_SBDEPDOC ("sbdepdoc.1"  );
file   fff             ("sbdepdoc.dbt");
private var doc = TDepFile( "sbdepdoc.dbt", "r" );
record tmpDoc          ("sbdepdoc.1"  );
file   dr              ("depositr.dbt") write;
record save_SBDEPDOC   (sbdepdoc      );
record save_DEPOSITOR  (depositr      );

const ОТКАТ_С_РЕЗУЛЬТАТОМ = -777; /* Откат с получением процентов */

const OnlyInputSum_Commission  = 1;
const OnlyInsertDoc_Commission = 2;

var IsFirst   = TRUE;
var Результат = 0;
var ErrMsg    = "";
var tmpDate   = Date(0,0,0);

var SummaComm = $0L;
var TypeComm  = "";
var NumOpert  = 0;

ALG_RESULT = OK_RES; /* заранее инициализируем код возврата */
ALG_UPDATE = 0;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ClearLimit

  KeyNum(dr,8);
  dr.Referenc = ALG_DEPOSITOR.Referenc;
  if ( GetGE(dr)                             AND
      (dr.Referenc == ALG_DEPOSITOR.Referenc)   )
    dr.Limit = $0L;
    Update(dr);
  else
    ErrMsg = "Cчет не найден";
    Результат = -1;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro CloseDep

  var
    MaxDate   = Date(0,0,0),
    Summa, found, С_Панелью, UseMaxDate, NoPrint,
    day, mon, year, PercSum3,
    Operation;
  Array NeedOperCode;
  var   NumOper = 0;

  /* Находим сумму причисленных процентов */
  NeedOperCode(0) = Зачисление_Процентов;
  NeedOperCode(1) = Переплата_Процентов;
  NeedOperCode(2) = Недоплата_Процентов;
  Summa = $0L;

  while (NumOper < ASize( NeedOperCode ) )
    doc.KeyNum = 1;
    doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) + " and " +
                   "T_TypeOper = " + String( NeedOperCode( NumOper ) )           );
    doc.Clear;
    doc.rec.Referenc         = ALG_DEPOSITOR.Referenc;
    doc.rec.TypeOper         = NeedOperCode( NumOper );
    doc.rec.DepDate_Document = ALG_DEPOSITOR.Open_Date;

    if ( doc.GetGE )
      found = TRUE;
      while ((doc.rec.Referenc == ALG_DEPOSITOR.Referenc  ) AND
             (doc.rec.TypeOper == NeedOperCode( NumOper ) ) AND
             (found        == TRUE                        )    )
        if ( IsServDoc( doc.rec ) )
          Summa = Summa + doc.rec.InSum - doc.rec.OutSum;
        end;
        if ( not doc.Next)
          found = FALSE;
        end;
      end;
    end;

    doc.dropFilter;

    NumOper = NumOper + 1;

  end;

  /* Учитываем сумму отчисленного налога на нерезидентов */
  PercSum3 = $0L;
  doc.KeyNum = 1;
  doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) + " and " +
                 "T_TypeOper = " + String( Списание_Простое )                  );
  doc.Clear;
  doc.rec.Referenc         = ALG_DEPOSITOR.Referenc;
  doc.rec.TypeOper         = Списание_Простое;
  doc.rec.DepDate_Document = ALG_DEPOSITOR.Open_Date;
  if ( doc.GetGE )
    found = TRUE;
    while ( found                                       AND
           (doc.rec.Referenc == ALG_DEPOSITOR.Referenc) AND
           (doc.rec.TypeOper == Списание_Простое      ) AND
           (doc.rec.DepDate_Document <= {curdate}     )    )
      if ( IsServDoc( doc.rec ) and ( doc.rec.ApplType == Налог_Нерезидентов ) )
        PercSum3 = PercSum3 + Den( doc.rec.OutSum, doc.rec.DepDate_Document );
      end;
      if ( not doc.Next )
        found = FALSE;
      end;
    end;
  end;
  doc.dropFilter;

  if ((PercSum3 != 0) AND (Summa != 0))
    Summa = Summa - PercSum3;
  end;

  /* Списываем сумму, вычисленную ранее */
  Результат = 0;
  if (Summa != 0)
    ClearRecord(tmpDoc);
    tmpDoc.Referenc         = ALG_SBDEPDOC.Referenc;
    tmpDoc.IsCur            = ALG_SBDEPDOC.IsCur;
    tmpDoc.FNCash           = ALG_SBDEPDOC.FNCash;
    tmpDoc.RealFNCash       = ALG_SBDEPDOC.RealFNCash;
    tmpDoc.Account          = ALG_DEPOSITOR.Account;
    tmpDoc.Oper             = {oper};
    tmpDoc.Type_Account     = ALG_SBDEPDOC.Type_Account;
    tmpDoc.Code_Currency    = ALG_SBDEPDOC.Code_Currency;
    tmpDoc.CodClient        = ALG_SBDEPDOC.CodClient;
    tmpDoc.YesSbook         = "X";
    tmpDoc.Date_Document    = ALG_SBDEPDOC.Date_Document;
    tmpDoc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
    tmpDoc.Author           = ALG_SBDEPDOC.Author;
    tmpDoc.TypeComplexOper  = ALG_PARAM.NumOpert;
    tmpDoc.InSum            = -Summa;

    С_Панелью  = 0;
    UseMaxDate = 1;
    NoPrint    = 1;

    Результат = Выполнить_Операцию(ALG_DEPOSITOR.Account,ALG_DEPOSITOR.Type_Account,
                                   ALG_DEPOSITOR.Code_Currency,Зачисление_Процентов,
                                   tmpDoc,С_Панелью,UseMaxDate,NoPrint,ALG_PARAM.NumOpert );

  end;

  if (Результат != 0)
    ErrMsg = "4. Ошибка при списании " + String(Результат);
    return;
  end;

  /* Выдаем весь остаток */
  ClearLimit( );
  doc.KeyNum = 2;
  doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) );
  doc.Clear;
  doc.rec.Referenc = ALG_DEPOSITOR.Referenc;
  MaxDate = {curdate}+1;
  doc.rec.DepDate_Document = MaxDate;
  doc.rec.NumDayDoc = 0;
  Summa = $0L;

  if ( doc.GetLE )
    found = TRUE;
    while ( ( doc.rec.Referenc == ALG_DEPOSITOR.Referenc ) AND
            ( doc.rec.DepDate_Document <= MaxDate        ) AND
            ( found == TRUE                              )    )
      if ( IsServDoc( doc.rec ) )
        Summa = doc.rec.Rest;
        found = False;
      end;

      if (found)
        if ( not doc.Next )
          found = FALSE;
        end;
      end;
    end;
  end;
  doc.dropFilter;

  if (Summa != 0)
    ClearRecord(tmpDoc);
    tmpDoc.Referenc         = ALG_SBDEPDOC.Referenc;
    tmpDoc.IsCur            = ALG_SBDEPDOC.IsCur;
    tmpDoc.FNCash           = ALG_SBDEPDOC.FNCash;
    tmpDoc.RealFNCash       = ALG_SBDEPDOC.RealFNCash;
    tmpDoc.Account          = ALG_DEPOSITOR.Account;
    tmpDoc.Oper             = {oper};
    tmpDoc.Type_Account     = ALG_SBDEPDOC.Type_Account;
    tmpDoc.Code_Currency    = ALG_SBDEPDOC.Code_Currency;
    tmpDoc.CodClient        = ALG_SBDEPDOC.CodClient;
    tmpDoc.YesSbook         = "X";
    tmpDoc.Date_Document    = ALG_SBDEPDOC.Date_Document;
    tmpDoc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
    tmpDoc.Author           = ALG_SBDEPDOC.Author;
    tmpDoc.OutSum           = Summa;
    tmpDoc.TypeComplexOper  = ALG_PARAM.NumOpert;
    tmpDoc.ApplType         = FINISH_SBDEPDOC.ApplType;
    //tmpDoc.Account_Receiver = FINISH_SBDEPDOC.Account_Receiver;
    //tmpDoc.CodeCur_Receiver = FINISH_SBDEPDOC.CodeCur_Receiver;
    //tmpDoc.MFO_Receiver     = FINISH_SBDEPDOC.MFO_Receiver;
    //tmpDoc.CorAcc_Receiver  = FINISH_SBDEPDOC.CorAcc_Receiver;
    tmpDoc.IsControl        = StrFor(1); /* Признак главного документа */
    tmpDoc.PercTrnRest      = FINISH_SBDEPDOC.PercTrnRest;
    tmpDoc.PercTrnAlt       = FINISH_SBDEPDOC.PercTrnAlt;
    tmpDoc.CurrencyBought   = FINISH_SBDEPDOC.CurrencyBought;
    tmpDoc.SummaTaxRub      = FINISH_SBDEPDOC.SummaTaxRub;

    if ((FINISH_SBDEPDOC.TypeOper != 0                 ) AND 
        (FINISH_SBDEPDOC.TypeOper != ALG_PARAM.NumOpert)    )
      Operation = FINISH_SBDEPDOC.TypeOper;
    elif (ALG_PARAM.NumOpert == Закрытие_Наличными)
      Operation = Расход;
    else
      Operation = Списание_По_Раз_Поруч;
    end;

    tmpDoc.TypeOper = Operation;

    if (Operation == Безналичная_Конверсия)
      /* Пересчет суммы с учетом подоходного налога */
      if (tmpDoc.SummaTaxRub != 0)
        tmpDoc.OutSum = tmpDoc.OutSum - tmpDoc.SummaTaxRub;
      end;
      /* Пересчет конвертируемой суммы с учетом процентов */
      if (tmpDoc.PercTrnRest != 0)
        tmpDoc.CurrencyBought = tmpDoc.OutSum / tmpDoc.PercTrnRest;
      end;
    end;

    if (IsFirst)
      Результат = ОТКАТ_С_РЕЗУЛЬТАТОМ;
      InterDesk_EndDocBunch();
      return;
    elif (SummaComm > 0)
      Результат = GetCommissionOnOper(tmpDoc,tmpDoc.OutSum,OnlyInsertDoc_Commission,SummaComm,TypeComm,NumOpert,ALG_PARAM.NumOpert);
      if (Результат != 0)
        ErrMsg = "Ошибка при вставке документа на комиссию " + String(Результат);
        return;
      end;
    end;
      
    С_Панелью  = 0;
    UseMaxDate = 1;
    NoPrint    = 1;

    ClearRecord(ALG_OPERPRM);
    ALG_OPERPRM.Account           = ALG_DEPOSITOR.Account;
    ALG_OPERPRM.Type_Account      = ALG_DEPOSITOR.Type_Account;
    ALG_OPERPRM.Code_Currency     = ALG_DEPOSITOR.Code_Currency;
    ALG_OPERPRM.Type_Oper         = Operation;
    ALG_OPERPRM.Show_Panel        = С_Панелью;
    ALG_OPERPRM.UseMaxDate        = UseMaxDate;
    ALG_OPERPRM.NoPrint           = NoPrint;
    ALG_OPERPRM.NonCashCommission = ALG_PARAM.prmI;
    ALG_OPERPRM.TypeComplexOper   = ALG_PARAM.NumOpert;
    ALG_OPERPRM.DocumentAuthor    = CodeFor(tmpDoc.Author);
    if (Operation == Списание_Переводом)
      ALG_OPERPRM.CorrAcc         = PMNT_PROP_REC_BUF.Account;
      ALG_OPERPRM.CorrType        = PMNT_PROP_REC_BUF.CorrAccount;
    end;

    Результат = Выполнение_Операции(ALG_OPERPRM,tmpDoc);

    if (Результат != 0)
      ErrMsg = "5. Ошибка при расходе "+String(Результат);
      return;
    end;

  else
    if (IsFirst)
      ClearRecord(tmpDoc);
      Результат = ОТКАТ_С_РЕЗУЛЬТАТОМ;
      InterDesk_EndDocBunch();
      return;
    end;
  end;

  ALG_RESULT = END_RES; /* Завершить операцию */

  if ((ALG_PARAM.prmC=="X") and (Результат==0))
    ALG_PARAM.prmD=PercSum3;
    Результат = ОТКАТ_С_РЕЗУЛЬТАТОМ;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro TrnProc

  CloseDep();

  if (Результат != 0)
    if (Результат == ОТКАТ_С_РЕЗУЛЬТАТОМ)
      ;
    else
      ALG_RESULT = ERR_RES;
    end;
    AbortTrn();
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  LoopInTrn(TRUE);

  ClearRecord(tmpDoc);

  Copy(save_DEPOSITOR,ALG_DEPOSITOR);
  Copy(save_SBDEPDOC,ALG_SBDEPDOC);
  Copy(fff,ALG_SBDEPDOC);
  SetRecordAddr(FINISH_SBDEPDOC,fff,0,0,true);

  /* Вычисляем дату = сегодня - месяц */
  tmpDate = DateAfterCalenMonths({curdate}, -1);
  /* Проверяем, пролежал ли счет месяц */
  if (ALG_DEPOSITOR.Open_Date > tmpDate)
    ;
  else
    ALG_RESULT = OK_RES; /* Продолжить операцию */
    ALG_UPDATE = 0;
    return;
  end;
      
  /* Нужно взять комиссию - для этого предварительно определим сумму расхода */
  IsFirst = TRUE;
  if ((ALG_PARAM.prmI2 >   0 ) AND 
      (ALG_PARAM.prmC  != "X")    )
    if ((StrFor(GetProgramID) == "П") AND (ALG_PARAM.prmI > 0))
      ;
    else
      if ( not ProcessTrn( 1, "TrnProc", dr ) )
        if (Результат != ОТКАТ_С_РЕЗУЛЬТАТОМ)
          ALG_RESULT = ERR_RES;
          Результат = 4714;
        end; 
      end;
      Copy(ALG_DEPOSITOR,save_DEPOSITOR);
      Copy(ALG_SBDEPDOC,save_SBDEPDOC);
      Copy(fff,ALG_SBDEPDOC);
      SetRecordAddr(FINISH_SBDEPDOC,fff,0,0,true);
    end;
  end;

  /* Проведем собственно операцию закрытия счета */
  IsFirst = FALSE;
  if (ALG_RESULT != ERR_RES)
    Результат = 0;
    if ((ALG_PARAM.prmI2 >   0 ) AND 
        (ALG_PARAM.prmC  != "X") AND
        (tmpDoc.OutSum   >   0 )    )
      if ((StrFor(GetProgramID) == "П") AND (ALG_PARAM.prmI > 0))
        ;
      else
        Результат = GetCommissionOnOper(tmpDoc,tmpDoc.OutSum,OnlyInputSum_Commission,SummaComm,TypeComm,NumOpert);
        if (Результат == 2)
          MsgBox("Отказались от проведения операции");
          ALG_RESULT = ERR_RES;
        elif (Результат != 0)
          ErrMsg = "Ошибка при взятии комиссии = " + String(Результат);
          ALG_RESULT = ERR_RES;
        end;
      end;
    end;
    if (Результат == 0)
      if ( not ProcessTrn( 1, "TrnProc", dr ) )
        if (Результат != ОТКАТ_С_РЕЗУЛЬТАТОМ)
          ALG_RESULT = ERR_RES;
          Результат = 4714;
        end;
      end;
    end;
  end;

  if ((ALG_PARAM.prmC == "X") AND (Результат == ОТКАТ_С_РЕЗУЛЬТАТОМ))
    ALG_RESULT = END_RES;
    ALG_UPDATE = 0;
  end;

  if ((ErrMsg != "") AND (NOT InFCGroupOpRun()))
    MsgBox(ErrMsg);
  end;

end;
