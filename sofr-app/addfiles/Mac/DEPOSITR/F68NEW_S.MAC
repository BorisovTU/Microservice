/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Последконтроль                                                          *
*                                                                          *
*  Выпуск формы 68 по учреждению                                           *
*                                                                          *
*  Лебедев С.В.                                                            *
*  23.08.2000                                                              *
\**************************************************************************/

import DeprIntr,"cur_rate.mac";

/* Файлы для работы макрофайла ========================================== */
file currency (currency          ) key 0; /* Справочник валют            */
/*file listfdep (listfdep          ) key 0;*/ /* Справочник филиалов         */
var ddep = TRecHandler( "i_dp_dep.rec" );
file person   (person, "bank.def") key 0; /* Пользователи системы        */
file namealg  (namealg,"bank.def") key 0; /* Названия алгоритмов         */
file sb_dtyp  (sb_dtyp           );       /* Справочник видов вкладов    */
file sb_depst (sb_depst          ) key 0; /* Статистика по видам вкладов */

record sb_depst_input  (sb_depst);
record sb_depst_cur    (sb_depst);
record sb_depst_output (sb_depst);

/* Панель ввода исходных данных ========================================= */
record InputPanel (FORMA68) dialog;
const ne_NameBranch   = 0;
const ne_StatusBranch = 1;
const ne_ShCur        = 2;
const ne_NameCur      = 3;
const ne_Month        = 4;
const ne_Year         = 5;
const ne_BegDate      = 6;
const ne_EndDate      = 7;

/* Переменные для работы программы ====================================== */
var DlgFNCash   = NumFNCash();
var DlgNumMonth = 1;
var DlgCodCur   = 0;
var FlagCur     = NumFlagCur();
var FirstPage   = TRUE;
var NamePerson  = "";
var DateRep;
var Rate        = 0;
var PrevRate    = 0;
var PrevRestEqv = $0L;
var CurCountAcc = 0;
var CurRestAcc  = $0L;
var PeriodOpen  = 0;
var PeriodClose = 0;
var PeriodKred  = $0L;
var PeriodDeb   = $0L;
var PrintDayRec = FALSE;
var {curdate};
var {oper};

var RestAll        = $0, ItogoRest     = $0;
var ColOpAll       = 0,  ItogoColOp    = 0;
var PeriodKredAll  = $0, ItogoKred     = $0;
var PeriodDebAll   = $0, ItogoDeb      = $0;
var PeriodOpenAll  = 0,  ItogoOpen     = 0;
var PeriodCloseAll = 0,  ItogoClose    = 0;
var RestOutputAll  = $0, ItogoRestOut  = $0;
var CountOutputAll = 0,  ItogoCountOut = 0;

/* Массивы для работы программы ========================================= */
array ABalAccs;
array AIdentChars;
array ABranches;

/* Настроечные параметры ================================================ */
var OutputZeroDay = FALSE; /* TRUE  - выводить данные о вкладе за день */
                           /*         даже при отсутствии оборотов (и  */
                           /*         смены курсов для валютных)       */
                           /* FALSE - не выводить                      */
var OutStandartRep = True; /* TRUE  - выводить стандартный отчет      */
                           /* False - выводить сводный отчет          */

/**************************************************************************\
|                          Последний день месяца                           |
\**************************************************************************/
macro LastDayInMonth (Month, Year)

  var OutputDay;
  var LastDateInMonth;
  var m,y;

  m = Month + 1;
  y = Year;
  if (m > 12)
    m = 1;
    y = y + 1;
  end;

  LastDateInMonth = Date(1,m,y) - 1;
  DateSplit(LastDateInMonth,OutputDay,NULL,NULL);

  return OutputDay;

end;


/**************************************************************************\
|                   Поиск названия алгоритма по его коду                   |
\**************************************************************************/
macro FindNameAlg (Type, Number)

  namealg.iTypeAlg   = Type;
  namealg.iNumberAlg = Number;

  return GetEQ(namealg);

end;


/**************************************************************************\
|                         Поиск филиала по его коду                        |
\**************************************************************************/
macro FindBranch (NumBranch)

  return findDepartment(NumBranch, null, ddep);

end;


/**************************************************************************\
|                         Поиск валюты по ее коду                          |
\**************************************************************************/
macro FindCurrency (CodCur)

  var stat = TRUE;

  if (CodCur == -1)
    ClearRecord(currency);
    currency.Code_Currency = -1;
    currency.Short_Name    = "Все";
  else
    currency.Code_Currency = CodCur;
    stat = GetEQ(currency);
  end;

  return stat;

end;


/**************************************************************************\
|                         Поиск валюты по ее коду                          |
\**************************************************************************/
macro DefCurRate (DateRate)

  var DefRate = 0;

  if (FlagCur)
    /*ClearRecord(ExchCurr);*/
    /*ExchCurr.ID = Int(Trim(currency.ExternalCode));*/
    copy( RtlCurr, currency );
    DefRate = GetCurrRate(DateRate);
  else
    DefRate = 1;
  end;

  return DefRate;

end;


/**************************************************************************\
|                       Определение названия месяца                        |
\**************************************************************************/
macro DefNameMonth (NumMonth)

  var NameMonth = "";

  if   (NumMonth ==  1)
    NameMonth = "января";
  elif (NumMonth ==  2)
    NameMonth = "февраля";
  elif (NumMonth ==  3)
    NameMonth = "марта";
  elif (NumMonth ==  4)
    NameMonth = "апреля";
  elif (NumMonth ==  5)
    NameMonth = "мая";
  elif (NumMonth ==  6)
    NameMonth = "июня";
  elif (NumMonth ==  7)
    NameMonth = "июля";
  elif (NumMonth ==  8)
    NameMonth = "августа";
  elif (NumMonth ==  9)
    NameMonth = "сентября";
  elif (NumMonth == 10)
    NameMonth = "октября";
  elif (NumMonth == 11)
    NameMonth = "ноября";
  elif (NumMonth == 12)
    NameMonth = "декабря";
  end;

  return NameMonth;

end;


/**************************************************************************\
|                  Определение идентификационных символов                  |
\**************************************************************************/
macro DefIdentChar (BalAcc)

  var stat;
  var FindIdentChar;
  var TmpVal;
  var i = 0;
  var j = 0;

  ASize(AIdentChars,0);

  KeyNum(sb_dtyp,4);
  sb_dtyp.FlagCur = FlagCur;
  sb_dtyp.BalAcc  = BalAcc;
  stat = GetGE(sb_dtyp);
  while (stat)
    if ((sb_dtyp.FlagCur == FlagCur) AND
        (sb_dtyp.BalAcc  == BalAcc )    )
      FindIdentChar = FALSE;
      i = 0;
      while (i < ASize(AIdentChars))
        if (AIdentChars(i) == Trim(sb_dtyp.NewIdentChar))
          FindIdentChar = TRUE;
          i = ASize(AIdentChars);
        end;
        i = i + 1;
      end;
      if (NOT FindIdentChar)
        i = ASize(AIdentChars);
        AIdentChars(i) = Trim(sb_dtyp.NewIdentChar);
      end;
      stat = Next(sb_dtyp);
    else
      stat = FALSE;
    end;
  end;

  KeyNum(sb_dtyp,5);
  sb_dtyp.FlagCur      = FlagCur;
  sb_dtyp.BalAccNotRez = BalAcc;
  stat = GetGE(sb_dtyp);
  while (stat)
    if ((sb_dtyp.FlagCur      == FlagCur) AND
        (sb_dtyp.BalAccNotRez == BalAcc )    )
      FindIdentChar = FALSE;
      i = 0;
      while (i < ASize(AIdentChars))
        if (AIdentChars(i) == Trim(sb_dtyp.NewIdentChar))
          FindIdentChar = TRUE;
          i = ASize(AIdentChars);
        end;
        i = i + 1;
      end;
      if (NOT FindIdentChar)
        i = ASize(AIdentChars);
        AIdentChars(i) = Trim(sb_dtyp.NewIdentChar);
      end;
      stat = Next(sb_dtyp);
    else
      stat = FALSE;
    end;
  end;

  /* Отсортируем полученный массив идентификационных символов в порядке возрастания */
  if (ASize(AIdentChars) > 0)
    i = 0;
    while (i < ASize(AIdentChars))
      j = i;
      while (j < ASize(AIdentChars))
        if (AIdentChars(j) < AIdentChars(i))
          TmpVal = AIdentChars(i);
          AIdentChars(i) = AIdentChars(j);
          AIdentChars(j) = TmpVal;
        end;
        j = j + 1;
      end;
      i = i + 1;
    end;
  end;

end;


/**************************************************************************\
|             Написать шапку о конкретном виде вклада в отчет              |
\**************************************************************************/
macro HeadReport (Citizenship)

  var str     = "";
  var RestEqv = $0.0L;
  var d1,m1,y1;
  var d2,m2,y2;

  if (FirstPage)
    FirstPage = FALSE;
  else
    [#](StrFor(12));
  end;

  [######################                                                                                          Ф.№68](ddep.rec.Name:c);
  [Наименование учреждения];
  [  Сбербанка России];
  [ ];
  [                                                           УЧЕТНАЯ КАРТОЧКА ];
  if (sb_depst_input.FlagRez)
    str = SubStr(Trim(sb_dtyp.BalAccNotRez) + MkStr(" ",5),1,5);
  else
    str = SubStr(Trim(sb_dtyp.BalAcc) + MkStr(" ",5),1,5);
  end;
  str = str + "/" + SubStr(Trim(sb_dtyp.NewIdentChar) + MkStr(" ",2),1,2) + " (" + sb_dtyp.Kind + ")";
  [                                         ОБОРОТОВ ПО БАЛАНСОВОМУ СЧЕТУ № #](str);
  [ ];
  DateSplit(InputPanel.BegDate,d1,m1,y1);
  DateSplit(InputPanel.EndDate,d2,m2,y2);
  d1 = String(d1);
  while (StrLen(d1) < 2)
    d1 = "0" + d1;
  end;
  d2 = String(d2);
  while (StrLen(d2) < 2)
    d2 = "0" + d2;
  end;
  m1 = DefNameMonth(m1);
  while (StrLen(m1) < 8)
    m1 = m1 + "_";
  end;
  m2 = DefNameMonth(m2);
  while (StrLen(m2) < 8)
    m2 = m2 + "_";
  end;
  [                                       за период с "##" ######## #### г. по "##" ######## #### г.](d1,m1,y1:4,d2,m2,y2:4);
  if (FlagCur)
    RestEqv = sb_depst_input.Rest * Rate;
    PrevRestEqv = RestEqv;
    [Валюта: #    #](currency.ExternalCode,currency.Name_Currency);
    [+-----------------+--------------------------------------------------------------------------------------------------+];
    [|                 |                    ##############                                                                |]
    (sb_depst_input.Rest:14:2:a);
    [|Входящий остаток |--------------------------------------------------------------------------------------------------+];
    [|                 | Курс RUR/#               ##########                                                              |]
    (currency.Short_Name,Rate:10:4);
    [|                 |              ####################                                                                |]
    (RestEqv:20:2:a);
    [+-----------------+--------------------------------------------------------------------------------------------------+];
    [| К-во лиц.счетов |                           #######                                                                |]
    (sb_depst_input.ColOpenedAcc);
    [+-----------------+--------------------------------------------------------------------------------------------------+];
    [|      Дата       |К-во лиц.сч.| Кредит (приход) |К-во лиц.сч.|  Дебет (расход) |К-во лиц.сч.|        Остаток        |];
    [+-----------------+------------+-----------------+------------+-----------------+------------+-----------------------+];
  else
    [ ];
    [+-----------------+---------------------------------------------------------------------------------------------------+];
    [|Входящий остаток | ####################                                                                              |]
    (sb_depst_input.Rest:20:2:a);
    [+-----------------+---------------------------------------------------------------------------------------------------+];
    [| К-во лиц.счетов |             ########                                                                              |]
    (sb_depst_input.ColOpenedAcc);
    [+-----------------+---------------------------------------------------------------------------------------------------+];
    [|      Дата       |К-во лиц.сч.| Кредит (приход) |К-во лиц.сч.|  Дебет (расход) |К-во лиц.сч.|        Остаток         |];
    [+-----------------+------------+-----------------+------------+-----------------+------------+------------------------+];
  end;

  CurCountAcc = sb_depst_input.ColOpenedAcc;
  CurRestAcc  = sb_depst_input.Rest;

end;


/**************************************************************************\
|           Написать хвостовик о конкретном виде вклада в отчет            |
\**************************************************************************/
macro BottomReport

  var RestOutput  = sb_depst_input.Rest         + PeriodKred - PeriodDeb;
  var CountOutput = sb_depst_input.ColOpenedAcc + PeriodOpen - PeriodClose;
  var RestEqv     = $0.0L;

  if (FlagCur)
    RestEqv = RestOutput * Rate;
    if (NOT PrintDayRec)
      [+-----------------+------------+-----------------+------------+-----------------+------------+-----------------------+];
    end;
    [|    Обороты      |   #######  |################ |   #######  |################ |                                    |]
    (PeriodOpen:7,PeriodKred:16:2:a,PeriodClose:7,PeriodDeb:16:2:a);
    [+-----------------+--------------------------------------------------------------------------------------------------+];
    [|                 |                    ##############                                                                |]
    (RestOutput:14:2:a);
    [|Исходящий остаток|--------------------------------------------------------------------------------------------------+];
    [|                 | Курс RUR/###             ##########                                                              |]
    (currency.Short_Name,Rate:10:4);
    [|                 |              ####################                                                                |]
    (RestEqv:20:2:a);
    [+-----------------+--------------------------------------------------------------------------------------------------+];
    [| К-во лиц.счетов |                       ########                                                                   |]
    (CountOutput:8);
    [+-----------------+--------------------------------------------------------------------------------------------------+];
  else
    if (NOT PrintDayRec)
      [+-----------------+------------+-----------------+------------+-----------------+------------+------------------------+];
    end;
    [|    Обороты      |  ########  |################ |  ########  |################ |                                     |]
    (PeriodOpen:8,PeriodKred:16:2:a,PeriodClose:8,PeriodDeb:16:2:a);
    [+-----------------+---------------------------------------------------------------------------------------------------+];
    [|Исходящий остаток| ####################                                                                              |]
    (RestOutput:20:2:a);
    [+-----------------+---------------------------------------------------------------------------------------------------+];
    [| К-во лиц.счетов |             ########                                                                              |]
    (CountOutput:8);
    [+-----------------+---------------------------------------------------------------------------------------------------+];
  end;
  [ ];
  [       Подписи:________________________   #](NamePerson);

  if (sb_depst_output.Rest != RestOutput)
    [ ];
    [WARNING  Суммы операций за день не соответствуют остатку в статистике];
  end;
  if (sb_depst_output.ColOpenedAcc != CountOutput)
    [ ];
    [WARNING  Количество открываемых и закрываемых счетов за день не соответствуют количеству открытых счетов в статистике];
  end;

end;


/**************************************************************************\
|            Написать данные об оборотах по виду вклада за день            |
\**************************************************************************/
macro DayRecReport

  var RestEqv     = $0.0L;
  var KredEqv     = $0.0L;
  var DebEqv      = $0.0L;
  var RestPer     = $0.0L;
  var KredPer     = $0.0L;
  var DebPer      = $0.0L;
  var DeltaPer    = $0.0L;
  var PrevRestAcc = CurRestAcc;
  var d,m,y;

  CurCountAcc = CurCountAcc + sb_depst_cur.CurOpenAc - sb_depst_cur.CurClosAc;
  CurRestAcc  = CurRestAcc  + sb_depst_cur.KredSum +  sb_depst_cur.HiddenKredSum  - sb_depst_cur.DebSum - sb_depst_cur.HiddenDebSum;

  if (FlagCur) /* Переоценка - предыдущий остаток на разницу курсов */
    KredPer = ( PrevRestAcc * Rate ) - ( PrevRestAcc * PrevRate );
    if (KredPer < 0)
      DebPer  = -KredPer;
      KredPer = $0.0L;
    end;
    RestPer = PrevRestEqv + KredPer - DebPer;
    DeltaPer = ( CurRestAcc * Rate ) - ( ( PrevRestAcc * PrevRate ) + ( ( sb_depst_cur.KredSum + sb_depst_cur.HiddenKredSum ) * Rate ) - ( ( sb_depst_cur.DebSum + sb_depst_cur.HiddenDebSum ) * Rate ) + KredPer - DebPer );
  end;

  DateSplit(DateRep,d,m,y);
  d = String(d);
  m = String(m);
  y = String(y);
  while (StrLen(d) < 2)
    d = "0" + d;
  end;
  while (StrLen(m) < 2)
    m = "0" + m;
  end;
  while (StrLen(y) < 4)
    y = "0" + y;
  end;

  if (FlagCur)
    RestEqv = CurRestAcc * Rate;
    KredEqv = ( sb_depst_cur.KredSum + sb_depst_cur.HiddenKredSum ) * Rate;
    DebEqv  = ( sb_depst_cur.DebSum + sb_depst_cur.HiddenDebSum )  * Rate;
    if( OutStandartRep )

    [|   ##.##.####    |    ######  |  ############## |    ######  |  ############## |  ########  |  #################### |]
    (d,m,y,sb_depst_cur.CurOpenAc:6,(sb_depst_cur.KredSum + sb_depst_cur.HiddenKredSum):14:2:a,sb_depst_cur.CurClosAc:6,(sb_depst_cur.DebSum + sb_depst_cur.HiddenDebSum):14:2:a,CurCountAcc:8,CurRestAcc:20:2:a);
    [|  Курс, RUR/###  |------------+-----------------+------------+-----------------+------------+-----------------------+]
    (currency.Short_Name);
    [|    #########    |                   ########## |                   ########## |               #################### |]
    (Rate:9:4,KredPer:10:2:a,DebPer:10:2:a,RestPer:20:2:a);
    [|                 |             ################ |             ################ | (######)      #################### |]
    (KredEqv:16:2:a,DebEqv:16:2:a,DeltaPer:6:2:a,RestEqv:20:2:a);
    [+-----------------+------------+-----------------+------------+-----------------+------------+-----------------------+];

    end;
    PrevRestEqv = RestEqv;
  else
    if( OutStandartRep )

    [|   ##.##.####    |    ######  |  ############## |    ######  |  ############## |  ########  |   #################### |]
    (d,m,y,sb_depst_cur.CurOpenAc:6,(sb_depst_cur.KredSum + sb_depst_cur.HiddenKredSum):14:2:a,sb_depst_cur.CurClosAc:6,(sb_depst_cur.DebSum + sb_depst_cur.HiddenDebSum):14:2:a,CurCountAcc:8,CurRestAcc:20:2:a);
    [+-----------------+------------+-----------------+------------+-----------------+------------+------------------------+];

    end;
  end;

  PrintDayRec = TRUE;

  PrevRate = Rate;

  PeriodOpen  = PeriodOpen  + sb_depst_cur.CurOpenAc;
  PeriodClose = PeriodClose + sb_depst_cur.CurClosAc;
  PeriodKred  = PeriodKred  + sb_depst_cur.KredSum + sb_depst_cur.HiddenKredSum;
  PeriodDeb   = PeriodDeb   + sb_depst_cur.DebSum + sb_depst_cur.HiddenDebSum;

end;


/**************************************************************************\
|           Определить параметры по виду вклада за текущий день            |
\**************************************************************************/
macro DefCurrentDayParams (Citizenship)

  var i = 0;
  var stat;

  ClearRecord(sb_depst_cur);
  sb_depst_cur.FNCash  = DlgFNCash;
  sb_depst_cur.IsCur   = FlagCur;
  sb_depst_cur.Kind    = sb_dtyp.Kind;
  sb_depst_cur.CodCur  = currency.Code_Currency;
  sb_depst_cur.FlagRez = Citizenship;
  sb_depst_cur.Date    = DateRep;

  i = 0;
  while (i < ASize(ABranches))
    sb_depst.FNCash  = ABranches(i);
    sb_depst.IsCur   = FlagCur;
    sb_depst.Kind    = sb_dtyp.Kind;
    sb_depst.CodCur  = currency.Code_Currency;
    sb_depst.FlagRez = Citizenship;
    sb_depst.Date    = DateRep;
    sb_depst.Mode    = 0;

    stat = GetGE(sb_depst);

    while (stat)
      if ((sb_depst.FNCash  == ABranches(i)          ) AND
          (sb_depst.IsCur   == FlagCur               ) AND
          (sb_depst.Kind    == sb_dtyp.Kind          ) AND
          (sb_depst.CodCur  == currency.Code_Currency) AND
          (sb_depst.FlagRez == Citizenship           ) AND
          (sb_depst.Date    == DateRep               )    )
        sb_depst_cur.CurOpenAc = sb_depst_cur.CurOpenAc + sb_depst.CurOpenAc;
        sb_depst_cur.CurClosAc = sb_depst_cur.CurClosAc + sb_depst.CurClosAc;
        sb_depst_cur.DebSum    = sb_depst_cur.DebSum    + sb_depst.DebSum;
        sb_depst_cur.KredSum   = sb_depst_cur.KredSum   + sb_depst.KredSum;
        sb_depst_cur.HiddenDebSum    = sb_depst_cur.HiddenDebSum    + sb_depst.HiddenDebSum;
        sb_depst_cur.HiddenKredSum   = sb_depst_cur.HiddenKredSum   + sb_depst.HiddenKredSum;
        stat = Next(sb_depst);
      else
        stat = FALSE;
      end;
    end;

    i = i + 1;
  end;

end;


/**************************************************************************\
|               Определить входящие параметры по виду вклада               |
\**************************************************************************/
macro DefInputParams (Citizenship)

  var i = 0;

  ClearRecord(sb_depst_input);
  sb_depst_input.FNCash  = DlgFNCash;
  sb_depst_input.IsCur   = FlagCur;
  sb_depst_input.Kind    = sb_dtyp.Kind;
  sb_depst_input.CodCur  = currency.Code_Currency;
  sb_depst_input.FlagRez = Citizenship;
  sb_depst_input.Date    = InputPanel.BegDate - 1;

  i = 0;
  while (i < ASize(ABranches))
    sb_depst.FNCash  = ABranches(i);
    sb_depst.IsCur   = FlagCur;
    sb_depst.Kind    = sb_dtyp.Kind;
    sb_depst.CodCur  = currency.Code_Currency;
    sb_depst.FlagRez = Citizenship;
    sb_depst.Date    = InputPanel.BegDate;
    sb_depst.Mode    = 0;
    if ((GetLT(sb_depst)                           ) AND
        (sb_depst.FNCash  == ABranches(i)          ) AND
        (sb_depst.IsCur   == FlagCur               ) AND
        (sb_depst.Kind    == sb_dtyp.Kind          ) AND
        (sb_depst.CodCur  == currency.Code_Currency) AND
        (sb_depst.FlagRez == Citizenship           ) AND
        (sb_depst.Date    <  InputPanel.BegDate    )    )
      if ((sb_depst.ColOpenedAcc != 0) OR (sb_depst.Rest != 0))
        sb_depst_input.ColOpenedAcc = sb_depst_input.ColOpenedAcc + sb_depst.ColOpenedAcc;
        sb_depst_input.Rest         = sb_depst_input.Rest         + sb_depst.Rest;
      end;
    end;
    i = i + 1;
  end;

end;


/**************************************************************************\
|               Определить входящие параметры по виду вклада               |
\**************************************************************************/
macro DefOutputParams (Citizenship)

  var i = 0;

  ClearRecord(sb_depst_output);
  sb_depst_output.FNCash  = DlgFNCash;
  sb_depst_output.IsCur   = FlagCur;
  sb_depst_output.Kind    = sb_dtyp.Kind;
  sb_depst_output.CodCur  = currency.Code_Currency;
  sb_depst_output.FlagRez = Citizenship;
  sb_depst_output.Date    = InputPanel.BegDate - 1;

  i = 0;
  while (i < ASize(ABranches))
    sb_depst.FNCash  = ABranches(i);
    sb_depst.IsCur   = FlagCur;
    sb_depst.Kind    = sb_dtyp.Kind;
    sb_depst.CodCur  = currency.Code_Currency;
    sb_depst.FlagRez = Citizenship;
    sb_depst.Date    = InputPanel.EndDate + 1;
    sb_depst.Mode    = 0;
    if ((GetLT(sb_depst)                           ) AND
        (sb_depst.FNCash  == ABranches(i)          ) AND
        (sb_depst.IsCur   == FlagCur               ) AND
        (sb_depst.Kind    == sb_dtyp.Kind          ) AND
        (sb_depst.CodCur  == currency.Code_Currency) AND
        (sb_depst.FlagRez == Citizenship           )    )
      if ((sb_depst.ColOpenedAcc != 0) OR (sb_depst.Rest != 0))
        sb_depst_output.ColOpenedAcc = sb_depst_output.ColOpenedAcc + sb_depst.ColOpenedAcc;
        sb_depst_output.Rest         = sb_depst_output.Rest         + sb_depst.Rest;
      end;
    end;
    i = i + 1;
  end;

end;

/* Вывод нестандартного заголовка */
macro OutNotStandartHeader()
   var stat = True;

  [######################                                                                                          Ф.№68](ddep.rec.Name:c);
  [Наименование учреждения];
  [  Сбербанка России];
  [ ];
  [                                                  ИТОГИ ПО БАЛАНСОВЫМ СЧЕТАМ ];
  [ ];
   [=======================================================================================================================================];
   [| Бал. |    Вид    |  К-во  |      Входящий      |  К-во  |       Кредит     |  К-во  |       Дебет     |  К-во  |       Исходящий    |];
   [| счет |   вклада  |  л/сч  |      остаток       |  л/сч  |      (приход)    |  л/сч  |      (расход)   |  л/сч  |        остаток     |];
   [=======================================================================================================================================];

   return stat;
end;

/* Вывод строки нестандартного отчета */
macro OutNotStandartLine()
   var stat = True;
   var bal:string;
   var RestOutput  = sb_depst_input.Rest         + PeriodKred - PeriodDeb;
   var CountOutput = sb_depst_input.ColOpenedAcc + PeriodOpen - PeriodClose;

   if (sb_depst_input.FlagRez)
      bal = Trim(sb_dtyp.BalAccNotRez);
   else
      bal = Trim(sb_dtyp.BalAcc);
   end;

   [|##### |###########|########|####################|########|##################|########|#################|########|####################|]
   ( bal, sb_dtyp.Kind, sb_depst_input.ColOpenedAcc:8, sb_depst_input.Rest:20:2:a, PeriodOpen:8, PeriodKred:18:2:a,
   PeriodClose:8, PeriodDeb:18:2:a, CountOutput:8, RestOutput:20:2:a );

   RestAll        = RestAll        + sb_depst_input.Rest;
   ColOpAll       = ColOpAll       + sb_depst_input.ColOpenedAcc;
   PeriodKredAll  = PeriodKredAll  + PeriodKred;
   PeriodDebAll   = PeriodDebAll   + PeriodDeb;
   PeriodOpenAll  = PeriodOpenAll  + PeriodOpen;
   PeriodCloseAll = PeriodCloseAll + PeriodClose;
   RestOutputAll  = RestOutputAll  + RestOutput;
   CountOutputAll = CountOutputAll + CountOutput;

   return stat;
end;

macro OutNotStandartBalItog( bal )
   var stat = True;

   [---------------------------------------------------------------------------------------------------------------------------------------];
   [| Итого            |        |                    |        |                  |        |                 |        |                    |];
   [| по счету  ###### |########|####################|########|##################|########|#################|########|####################|]
   ( bal,  ColOpAll:8, RestAll:20:2:a, PeriodOpenAll:8, PeriodKredAll:18:2:a,
   PeriodCloseAll:8, PeriodDebAll:18:2:a, CountOutputAll:8, RestOutputAll:20:2:a );
   [=======================================================================================================================================];

   ItogoRest     = ItogoRest     + RestAll;
   ItogoColOp    = ItogoColOp    + ColOpAll;
   ItogoKred     = ItogoKred     + PeriodKredAll;
   ItogoDeb      = ItogoDeb      + PeriodDebAll;
   ItogoOpen     = ItogoOpen     + PeriodOpenAll;
   ItogoClose    = ItogoClose    + PeriodCloseAll;
   ItogoRestOut  = ItogoRestOut  + RestOutputAll;
   ItogoCountOut = ItogoCountOut + CountOutputAll;

   return stat;
end;

macro OutNotStandartItogo()
   var stat = True;

   [|                  |        |                    |        |                  |        |                 |        |                    |];
   [| Итого по вкладам |########|####################|########|##################|########|#################|########|####################|]
   ( ItogoColOp:8, ItogoRest:20:2:a, ItogoOpen:8, ItogoKred:18:2:a,
   ItogoClose:8, ItogoDeb:18:2:a, ItogoCountOut:8, ItogoRestOut:20:2:a );
   [=======================================================================================================================================];

   return stat;
end;

/**************************************************************************\
|           Написать информацию о конкретном виде вклада в отчет           |
\**************************************************************************/
macro WriteDepTypeForDefCurrencyAndCitizenshipReport (Citizenship)

  var FindRecBeforeBegDate = FALSE;
  var FindRecInPeriod      = FALSE;
  var i                    = 0;

  PrintDayRec = FALSE;

  CurCountAcc = 0;
  CurRestAcc  = $0L;

  PeriodOpen  = 0;
  PeriodClose = 0;
  PeriodKred  = $0L;
  PeriodDeb   = $0L;

  PrevRestEqv = $0L;

  DefInputParams(Citizenship);
  if ((sb_depst_input.ColOpenedAcc != 0) OR (sb_depst_input.Rest != 0))
    FindRecBeforeBegDate = TRUE;
  end;

  if (NOT FindRecBeforeBegDate)
    i = 0;
    while (i < ASize(ABranches))
      sb_depst.FNCash  = ABranches(i);
      sb_depst.IsCur   = FlagCur;
      sb_depst.Kind    = sb_dtyp.Kind;
      sb_depst.CodCur  = currency.Code_Currency;
      sb_depst.FlagRez = Citizenship;
      sb_depst.Date    = InputPanel.BegDate;
      sb_depst.Mode    = 0;
      if ((GetGE(sb_depst)                           ) AND
          (sb_depst.FNCash  == ABranches(i)          ) AND
          (sb_depst.IsCur   == FlagCur               ) AND
          (sb_depst.Kind    == sb_dtyp.Kind          ) AND
          (sb_depst.CodCur  == currency.Code_Currency) AND
          (sb_depst.FlagRez == Citizenship           ) AND
          (sb_depst.Date    <= InputPanel.EndDate    )    )
        FindRecInPeriod = TRUE;
        i = ASize(ABranches);
      end;
      i = i + 1;
    end;
  end;

  if (FindRecBeforeBegDate OR FindRecInPeriod)
    Rate = DefCurRate(InputPanel.BegDate - 1);
    PrevRate = Rate;
    if( OutStandartRep )
       HeadReport();
    end;

    DateRep = InputPanel.BegDate;
    while (DateRep <= InputPanel.EndDate)
      DefCurrentDayParams(Citizenship);
      Rate = DefCurRate(DateRep);
      if ((sb_depst_cur.CurOpenAc != 0   ) OR
          (sb_depst_cur.CurClosAc != 0   ) OR
          (sb_depst_cur.DebSum    != 0   ) OR
          (sb_depst_cur.KredSum   != 0   ) OR
          (sb_depst_cur.HiddenDebSum    != 0   ) OR
          (sb_depst_cur.HiddenKredSum   != 0   ) OR
          (PrevRate               != Rate) OR
          (OutputZeroDay                 )   )
        DayRecReport();
      end;
      DateRep = DateRep + 1;
    end;

    DefOutputParams(Citizenship);
    if( OutStandartRep )
       BottomReport();
    else
       OutNotStandartLine();
    end;
  end;

end;

/**************************************************************************\
|                             Выпуск формы 68                              |
\**************************************************************************/
macro WorkWithStatForDepType (Citizenship)

  var stat = TRUE;

  if (DlgCodCur >= 0)
    stat = FindCurrency(DlgCodCur);
  else
    if (FlagCur)
      currency.Code_Currency = 1;
      stat = GetGE(currency);
    else
      currency.Code_Currency = 0;
      stat = GetEQ(currency);
    end;
  end;

  while (stat)

    /* Вывод отчета в выходной файл */
    WriteDepTypeForDefCurrencyAndCitizenshipReport(Citizenship);

    if ((DlgCodCur >= 0) OR (FlagCur == 0))
      stat = FALSE;
    else
      stat = Next(currency);
    end;
  end;

end;


/**************************************************************************\
|                             Выпуск формы 68                              |
\**************************************************************************/
macro DoReport

  var i = 0;
  var j = 0;
  var stat;


  if( not OutStandartRep )
     OutNotStandartHeader();
  end;

  while (i < ASize(ABalAccs))

    j = 0;

    /* Определим идентификационные символы для данного вида вклада */
    DefIdentChar(ABalAccs(i));

    RestAll        = $0;
    ColOpAll       = 0;
    PeriodKredAll  = $0;
    PeriodDebAll   = $0;
    PeriodOpenAll  = 0;
    PeriodCloseAll = 0;
    RestOutputAll  = $0;
    CountOutputAll = 0;


    while (j < ASize(AIdentChars))

      Message(" Обрабатываются вклады балансового счета ",Trim(ABalAccs(i)),"/",Trim(AIdentChars(j))," ...");

      KeyNum(sb_dtyp,1);
      sb_dtyp.FlagCur = FlagCur;
      sb_dtyp.Priorit = 0;
      stat = GetGE(sb_dtyp);
      while (stat)
        if (sb_dtyp.FlagCur == FlagCur)
          if ((sb_dtyp.BalAcc             == ABalAccs(i)   ) AND
              (Trim(sb_dtyp.NewIdentChar) == AIdentChars(j))    )
            WorkWithStatForDepType(StrFor(0));
          end;
          if ((sb_dtyp.BalAccNotRez       == ABalAccs(i)   ) AND
              (Trim(sb_dtyp.NewIdentChar) == AIdentChars(j))    )
            WorkWithStatForDepType(StrFor(1));
          end;
          stat = Next(sb_dtyp);
        else
          stat = FALSE;
        end;
      end;

      j = j + 1;
    end;

    if( not OutStandartRep and ( ( RestOutputAll != $0 ) or ( RestAll != $0 ) ) )
       OutNotStandartBalItog( ABalAccs( i ) );
    end;

    i = i + 1;
  end;
  if( not OutStandartRep and ( ( ItogoRest != $0 ) or ( ItogoRestOut != $0 ) ) )
     OutNotStandartItogo();
  end;


end;


/**************************************************************************\
|                      Определение балансовых счетов                       |
\**************************************************************************/
macro DefBalAccs

  var stat;
  var FindBA;
  var FindBA_NotRez;
  var TmpVal;
  var i = 0;
  var j = 0;

  Message(" Определение балансовых счетов ...");

  /* Получение массива всех возможных балансовых счетов */
  KeyNum(sb_dtyp,2);
  sb_dtyp.FlagCur = FlagCur;
  sb_dtyp.Kind    = "";
  stat = GetGE(sb_dtyp);
  while (stat)
    if (sb_dtyp.FlagCur == FlagCur)
      if (ASize(ABalAccs) == 0)
        ABalAccs(0) = Trim(sb_dtyp.BalAcc);
      end;
      FindBA        = FALSE;
      FindBA_NotRez = FALSE;
      i = 0;
      while (i < ASize(ABalAccs))
        if (ABalAccs(i) == Trim(sb_dtyp.BalAcc))
          FindBA = TRUE;
        end;
        if (ABalAccs(i) == Trim(sb_dtyp.BalAccNotRez))
          FindBA_NotRez = TRUE;
        end;
        i = i + 1;
      end;
      if (FindBA == FALSE)
        ABalAccs(ASize(ABalAccs)) = Trim(sb_dtyp.BalAcc);
      end;
      if (FindBA_NotRez == FALSE)
        if (Trim(sb_dtyp.BalAcc) != Trim(sb_dtyp.BalAccNotRez))
          ABalAccs(ASize(ABalAccs)) = Trim(sb_dtyp.BalAccNotRez);
        end;
      end;
      stat = Next(sb_dtyp);
    else
      stat = FALSE;
    end;
  end;

  /* Отсортируем полученный массив балансовых счетов в порядке возрастания */
  if (ASize(ABalAccs) > 0)
    i = 0;
    while (i < ASize(ABalAccs))
      j = i;
      while (j < ASize(ABalAccs))
        if (ABalAccs(j) < ABalAccs(i))
          TmpVal = ABalAccs(i);
          ABalAccs(i) = ABalAccs(j);
          ABalAccs(j) = TmpVal;
        end;
        j = j + 1;
      end;
      i = i + 1;
    end;
  end;

end;


/**************************************************************************\
|                     Определение филиалов для расчета                     |
\**************************************************************************/
macro DefBranches

  var stat;
  var i;
  var dpDepList = TdpDepList;

  if (FindBranch(DlgFNCash))
    if (ddep.rec.NodeType == I_DEPARTMENT_TYPE_FILIAL /*1*/)
      dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
      ddep = dpDepList.RecHandler;
      while (dpDepList.next())
/*        if ((listfdep.StatusBranch == 3) OR (listfdep.StatusBranch == 0))*/
          i = ASize(ABranches);
          ABranches(i) = ddep.rec.Code;
/*        end;*/
      end;
      if (NOT FindBranch(DlgFNCash))
        ddep.Clear;
        ddep.rec.Code = DlgFNCash;
      end;
    else
      ABranches(0) = DlgFNCash;
    end;
  else
    ddep.Clear;
    ddep.rec.Code = DlgFNCash;
    ABranches(0) = DlgFNCash;
  end;

end;


/**************************************************************************\
|                   Определение имени выпустившего отчет                   |
\**************************************************************************/
macro DefNamePerson

  var SName = "";
  var Name  = "";
  var PName = "";
  var i;

  person.Oper = {oper};

  if (GetEQ(person))
    SName = Trim(person.Name);
    i = Index(SName," ");
    if (i > 0)
      Name = Trim(SubStr(SName,i + 1));
      SName = Trim(SubStr(SName,1,i - 1));
      i = Index(Name," ");
      if (i > 0)
        PName = Trim(SubStr(Name,i + 1));
        Name = Trim(SubStr(Name,1,i - 1));
      end;
    end;
    if (StrLen(Name) > 2)
      Name = SubStr(Name,1,1) + ".";
    end;
    if (StrLen(PName) > 2)
      PName = SubStr(PName,1,1) + ".";
    end;
    NamePerson = Trim(SName + " " + Name + PName);
  end;

end;


/**************************************************************************\
|                       Справочный скролинг филиалов                       |
\**************************************************************************/
macro List_Branch (InputNum)

  array AName;
  array ACode;
  var   stat;
  var   i         = 0;
  var   OutputNum = InputNum;
  var dpDepList = TdpDepList;

  dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
  while (dpDepList.next())
/*    if (listfdep.StatusBranch != 2)*/
      i = ASize(AName);
      ACode(i) = dpDepList.rec.Code;
      AName(i) = " " + SubStr(dpDepList.rec.Name + MkStr(" ",12),1,12) + " ";
/*      if (FindNameAlg(731,listfdep.StatusBranch))
        AName(i) = AName(i) + namealg.szNameAlg + " ";
      end;*/
      if (dpDepList.rec.NodeType == I_DEPARTMENT_TYPE_FILIAL)
        AName(i) = AName(i) + "Филиал ";
      else
        AName(i) = AName(i) + "ВСП ";
      end;
/*    end;*/
  end;

  if (ASize(AName))
    i = Menu(AName,NULL,"Выберите учреждение");
    if (i >= 0)
      OutputNum = ACode(i);
    end;
  end;

  return OutputNum;

end;


/**************************************************************************\
|                        Справочный скролинг валют                         |
\**************************************************************************/
macro List_Currency (InputCur)

  array AName;
  array ACode;
  var   stat;
  var   i         = 0;
  var   OutputCur = InputCur;

  currency.Code_Currency = 1;
  stat = GetGE(currency);
  while (stat)
    i = ASize(AName);
    ACode(i) = currency.Code_Currency;
    AName(i) = " " + SubStr(currency.ExternalCode + MkStr(" ",3),1,3) + " " +
                     SubStr(currency.Short_Name   + MkStr(" ",3),1,3) + " " +
                     currency.Name_Currency                           + " ";
    stat = Next(currency);
  end;

  if (ASize(AName))
    i = Menu(AName,NULL,"Выберите валюту");
    if (i >= 0)
      OutputCur = ACode(i);
    end;
  end;

  return OutputCur;

end;


/**************************************************************************\
|                        Справочный скролинг месяцев                       |
\**************************************************************************/
macro List_Month (InputMonth)

  array AName;
  var   i            = 0;
  var   OutputMonth  = InputMonth;

  while (i < 12)
    AName(i) = " " + String((i + 1):2) + "   " + MonName(i + 1) + "    ";
    i = i + 1;
  end;

  if (ASize(AName))
    i = Menu(AName,NULL,"Выберите месяц");
    if (i >= 0)
      OutputMonth = i + 1;
    end;
  end;

  return OutputMonth;

end;


/**************************************************************************\
|               Обработка нажатия клавишей в панели диалога                |
\**************************************************************************/
var PrevVolume;

macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;
  var y, m, d;
  var y1,m1,d1;

  /**********************************************************************/
  if (cmd == DLG_KEY)
    /* ENTER -------------------------------------------------------- */
    if (key == 13)
      if (id == ne_EndDate)
        SetFocus(IP,ne_NameBranch);
        stat = CM_IGNORE;
      end;
    /* ESC ---------------------------------------------------------- */
    elif (key == 27)
      stat = CM_CANCEL;
    /* F7 ----------------------------------------------------------- */
    elif (key == 321)
      stat = CM_SAVE;
    /* F3 ----------------------------------------------------------- */
    elif (key == 317)
      if (id == ne_ShCur)
        if (FlagCur)
          PrevVolume = DlgCodCur;
          DlgCodCur = List_Currency(DlgCodCur);
          if (PrevVolume != DlgCodCur)
            if (FindCurrency(DlgCodCur))
              InputPanel.ShCur   = currency.Short_Name;
              InputPanel.NameCur = currency.Name_Currency;
            else
              InputPanel.ShCur   = "???";
              InputPanel.NameCur = "";
            end;
          end;
        end;
      elif (id == ne_NameBranch)
        PrevVolume = DlgFNCash;
        DlgFNCash = List_Branch(DlgFNCash);
        if (PrevVolume != DlgFNCash)
          InputPanel.NameBranch   = "";
          InputPanel.StatusBranch = "";
          if (FindBranch(DlgFNCash))
            InputPanel.NameBranch = ddep.rec.Name;
/*            if (FindNameAlg(731,listfdep.StatusBranch))
              InputPanel.StatusBranch = namealg.szNameAlg;
            end;*/
            if (ddep.rec.NodeType == I_DEPARTMENT_TYPE_FILIAL)
              InputPanel.StatusBranch = "Филиал ";
            else
              InputPanel.StatusBranch = "ВСП ";
            end;
          end;
        end;
      elif (id == ne_Month)
        PrevVolume = DlgNumMonth;
  DlgNumMonth = List_Month(DlgNumMonth);
        if (PrevVolume != DlgNumMonth)
          InputPanel.Month = MonName(DlgNumMonth);
          DateSplit(InputPanel.BegDate,d,m,y);
    InputPanel.BegDate = Date(1,DlgNumMonth,y);
    DateSplit(InputPanel.EndDate,d,m,y);
    InputPanel.EndDate = Date(LastDayInMonth(DlgNumMonth,y),DlgNumMonth,y);
  end;
      end;
      UpdateFields(IP);
    /* Пробел ------------------------------------------------------- */
    elif (key == 32)
      if (id == ne_ShCur)
        if (FlagCur)
          DlgCodCur = -1;
          if (FindCurrency(DlgCodCur))
            InputPanel.ShCur   = currency.Short_Name;
            InputPanel.NameCur = currency.Name_Currency;
          else
            InputPanel.ShCur   = "???";
            InputPanel.NameCur = "";
          end;
        end;
      end;
      UpdateFields(IP);
    /* CtrlF3 ------------------------------------------------------- */
    elif (key == 352)
      if (id == ne_BegDate)
        InputPanel.BegDate = {curdate};
      elif (id == ne_EndDate)
        InputPanel.EndDate = {curdate};
      end;
    end;
  /**********************************************************************/
  elif (cmd == DLG_SETFOCUS)
    if (id == ne_Year)
      PrevVolume = InputPanel.Year;
    elif (id == ne_BegDate)
      PrevVolume = InputPanel.BegDate;
    elif (id == ne_EndDate)
      PrevVolume = InputPanel.EndDate;
    end;
  /**********************************************************************/
  elif (cmd == DLG_REMFOCUS)
    if (id == ne_Year)
      if (PrevVolume != InputPanel.Year);
        DateSplit(InputPanel.BegDate,d,m,y);
        if (d > LastDayInMonth(m,InputPanel.Year))
          d = LastDayInMonth(m,InputPanel.Year);
        end;
        InputPanel.BegDate = Date(d,m,InputPanel.Year);
        DateSplit(InputPanel.EndDate,d,m,y);
        if (d > LastDayInMonth(m,InputPanel.Year))
          d = LastDayInMonth(m,InputPanel.Year);
        end;
        InputPanel.EndDate = Date(d,m,InputPanel.Year);
      end;
    elif (id == ne_BegDate)
      if (PrevVolume != InputPanel.BegDate)
        DateSplit(InputPanel.BegDate,d,m,y);
        DateSplit(InputPanel.EndDate,d1,m1,y1);
        if (m == m1)
          DlgNumMonth = m;
          InputPanel.Month = MonName(DlgNumMonth);
        else
          DlgNumMonth = 0;
          InputPanel.Month = MonName(DlgNumMonth);
        end;
        if (y == y1)
          InputPanel.Year = y;
        else
          InputPanel.Year = 0;
        end;
      end;
    elif (id == ne_EndDate)
      if (PrevVolume != InputPanel.EndDate)
        DateSplit(InputPanel.BegDate,d,m,y);
        DateSplit(InputPanel.EndDate,d1,m1,y1);
        if (m == m1)
          DlgNumMonth = m;
          InputPanel.Month = MonName(DlgNumMonth);
        else
          DlgNumMonth = 0;
          InputPanel.Month = MonName(DlgNumMonth);
        end;
        if (y == y1)
          InputPanel.Year = y;
        else
          InputPanel.Year = 0;
        end;
      end;
    end;
    UpdateFields(IP);
  end;

  return stat;

end;


/**************************************************************************\
|                           Ввод исходных данных                           |
\**************************************************************************/
macro InputDate

  var stat = TRUE;

  if (NOT RunDialog(InputPanel,"WorkDialog"))
    [ ];
    [ Отказались от выпуска формы 68 по учреждению банка];
    [ ];
    stat = FALSE;
  else
    if (InputPanel.BegDate > InputPanel.EndDate)
      [ ];
      [ Дата начала отчета не может быть больше даты окончания];
      [ ];
      stat = FALSE;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                       Инициализация полей диалога                        |
\**************************************************************************/
macro InitDialogFields

  var d,m,y;
  var stat;

  /* Определение дня и месяца отчета по умолчанию */
  DateSplit({curdate},d,m,y);
  if (d != LastDayInMonth(m,y))
    m = m - 1;
    if (m <= 0)
      m = 1;
      y = y - 1;
    end;
  end;
  DlgNumMonth      = m;
  InputPanel.Month = MonName(DlgNumMonth);
  InputPanel.Year  = y;

  InputPanel.BegDate = Date(1,m,y);
  InputPanel.EndDate = Date(LastDayInMonth(m,y),m,y);

  /* Определение номера филиала по умолчанию */
  InputPanel.NameBranch   = "";
  InputPanel.StatusBranch = "";
  if (FindBranch(DlgFNCash))
    InputPanel.NameBranch = ddep.rec.Name;
/*    if (FindNameAlg(731,listfdep.StatusBranch))
      InputPanel.StatusBranch = namealg.szNameAlg;
    end;*/
    if (ddep.rec.NodeType == I_DEPARTMENT_TYPE_FILIAL)
      InputPanel.StatusBranch = "Филиал ";
    else
      InputPanel.StatusBranch = "ВСП ";
    end;
  end;

  /* Определение кода валюты по умолчанию */
  if (FlagCur == 0)
    DlgCodCur = 0;
  else
    DlgCodCur = -1;
  end;
  if (FindCurrency(DlgCodCur))
    InputPanel.ShCur   = currency.Short_Name;
    InputPanel.NameCur = currency.Name_Currency;
  else
    InputPanel.ShCur   = "???";
    InputPanel.NameCur = "";
  end;

  return stat;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  InitDialogFields();
  if (InputDate())
    DefBalAccs(); /* Определение балансовых счетов */
    DefBranches();
    DefNamePerson();
    DoReport();   /* Выпуск формы                  */
  end;

end;
