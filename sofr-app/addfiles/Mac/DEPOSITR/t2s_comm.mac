/* Перевод счетов в неподвижные/объединенные - общая часть*/

import deprintr, OperCode, TrAc_Cmn, IsSrvDoc;

file DepType("sb_dtyp.dbt") key 2;
file Curr("currency.dbt") key 0;

const
  SA_STATIC = "Неподвижный",
  SA_UNITED = "Объединенный";

const
  Binsert = 2;

const
  D_IN            = 1,    /* Приход */
  D_OUT           = 2;    /* Расход */

const
  BS_BRANCH       = 0,    /* Филиал                 */
  BS_DEP          = 1,    /* Отделение              */
  BS_EXCH         = 2,    /* Обменный пункт         */
  BS_OPERDEP      = 3;    /* Оперчасть              */

const
  SB_DEPOSIT = 1,

private var 
  {curdate},
  {oper};

var
  OperBrigade = GetOperBrigade,
  Branch = NumFNCash,
  BranchStatus = GetBranchStatus,
  IsCur = NumFlagCur,
  StaticAccType,
  StaticAcc,
  IsFirstForType,
  IsFirstForCurr,
  FileExt,
  ASFirstTime,
  DocApplicationKind,
  TrnStat = true;

macro CreateDepDoc

  record OpParm( "op_parm.rec" );

  var
    PrevKey = KeyNum( DepDoc, 6 ),
    Date_Document,
    NumDayDoc,
    stat,
    MAXDATE = Date( 31, 12, 2099 ),
    Rest, PercRest;

  ClearRecord( DepDoc );
  DepDoc.Referenc = Dep.Referenc;
  DepDoc.Date_Document = MAXDATE;
  TrnStat = GetLE(DepDoc);
  if(TrnStat and(DepDoc.Referenc == Dep.Referenc))
    Rest = Dep.Sum_Rest;
    PercRest = DepDoc.PercRest;
    Date_Document = DepDoc.Date_Document;
    NumDayDoc = DepDoc.NumDayDoc;
    ClearRecord( DepDoc );
    DepDoc.FNCash = Dep.FNCash;
    DepDoc.Referenc = Dep.Referenc;
    DepDoc.IsCur = Dep.IsCur;
    DepDoc.Code_Currency = Dep.Code_Currency;
    DepDoc.CodClient = Dep.CodClient;
    DepDoc.Account = Dep.Account;
    DepDoc.Type_Account = Dep.Type_Account;
    DepDoc.TypeOper = 102;
    DepDoc.TypeComplexOper = 102;
    DepDoc.ApplType = 1;
    DepDoc.VidDoc = 0;
    DepDoc.iApplicationKind=DocApplicationKind;
    DepDoc.ApplicationKey=FormApplicationKey(DocApplicationKind);
    DepDoc.PercOprSum = $0;
    DepDoc.Rest = Rest;
    DepDoc.PercRest = PercRest;
    DepDoc.oper = {oper};
    DepDoc.Brigade = OperBrigade;
    DepDoc.Ground = "Перевод счета на неподвижный/объединенный";
    DepDoc.DepDate_Document = {curdate};
    if(Date_Document == {curdate})
      DepDoc.Date_Document = {curdate};
      DepDoc.NumDayDoc = NumDayDoc + 100;
    else
      DepDoc.Date_Document = {curdate};
      DepDoc.NumDayDoc = 100;
    end;
    TrnStat=Insert(DepDoc);
    if ( TrnStat and ( not InterDesk_IsDocBunchActive ) )
      ClearRecord( OpParm );
      OpParm.Date = DepDoc.Date_Document;
      OpParm.Operation = DepDoc.TypeComplexOper;
      OpParm.ObjectRef = DepDoc.Referenc;
  
      stat = InterDesk_InitDocBunch( DocApplicationKind, DepDoc.ApplicationKey, OpParm );
    end; 
    if(TrnStat)
      TrnStat = CashLink_AddRec(DocApplicationKind, DepDoc.ApplicationKey);
    end;
  end;
  KeyNum( DepDoc, PrevKey );
end;

macro UnloadAccount(Acc)

  var
    SK_Dep = KeyNum(Dep, 8),
    SK_DepDoc = KeyNum(DepDoc, 6);

  Direction = Unload;
  From(I_Dep)=Dep;
  To(I_Dep)=T_Dep;
  From(I_DepDoc)=DepDoc;
  To(I_DepDoc)=T_DepDoc;
  From(I_DepRest)=DepRest;
  To(I_DepRest)=T_DepRest;
  From(I_TrWill)=TrWill;
  To(I_TrWill)=T_TrWill;
  From(I_DepClient)=DepClient;
  To(I_DepClient)=T_DepClient;
  From(I_AuxInfo)=AuxInfo;
  To(I_AuxInfo)=T_AuxInfo;
  From(I_ConGet)=ConGet;
  To(I_ConGet)=T_ConGet;
  From(I_PcCalc)=PcCalc;
  To(I_PcCalc)=T_PcCalc;
  From(I_PcEstim)=PcEstim;
  To(I_PcEstim)=T_PcEstim;
  From(I_CashLink)=CashLink;
  To(I_CashLink)=T_CashLink;
  From(I_AccReg)=AccReg;
  To(I_AccReg)=T_AccReg;
  From(I_TextInfo)=TextInfo;
  To(I_TextInfo)=T_TextInfo;
  From(I_PcRDate)=PcRDate;
  To(I_PcRDate)=T_PcRDate;
  From(I_DepHistr)=DepHistr;
  To(I_DepHistr)=T_DepHistr;
  From(I_ScAcc)=ScAcc;
  To(I_ScAcc)=T_ScAcc;
  From(I_ScLink)=ScLink;
  To(I_ScLink)=T_ScLink;
  From(I_ScCard)=ScCard;
  To(I_ScCard)=T_ScCard;
  From(I_ScTran)=ScTran;
  To(I_ScTran)=T_ScTran;
  From(I_PcTax)=PcTax;
  To(I_PcTax)=T_PcTax;
  From(I_RetOp)=RetOp;
  To(I_RetOp)=T_RetOp;
  From(I_OpObject)=OpObject;
  To(I_OpObject)=T_OpObject;
  Account = Acc;
  ClearRecord(To(I_Dep));
  To(I_Dep).Referenc=Account;
  if(not GetEQ(To(I_Dep)))
    TrnStat = CopyDep;
  end;
  KeyNum(Dep, SK_Dep);
  KeyNum(DepDoc, SK_DepDoc);
end;

macro FormStaticAccountNumber(BinaryNumber)

  var
    AccNumber,
    Number,
    SaveDepKey = KeyNum(Dep, 0);

  GetPos(Dep);

  Dep.FNCash = Branch;
  Dep.GroupNumb = StaticAccType;
  Dep.Code_Currency = Curr.Code_Currency;
  Dep.Number = 32767;
  if(GetLE(Dep)
    and (Dep.FNCash == Branch)
    and (Dep.GroupNumb == StaticAccType)
    and (Dep.Code_Currency == Curr.Code_Currency))
    Number = Dep.Number + 1;
  else
    Number = 1;
  end;

  AccNumber = String(Number);
  while(StrLen(AccNumber) < 6)
    AccNumber = "0" + AccNumber;
  end;

  if(StaticAccType == SA_STATIC)
    AccNumber = "#Н" + AccNumber;
  else
    AccNumber = "#O" + AccNumber;
  end;

  if(IsCur)
    AccNumber = AccNumber + "/" + Curr.ExternalCode;
  end;

  GetDirect(Dep);
  KeyNum(Dep, SaveDepKey);
  SetParm(0, Number);
  return AccNumber;
end;

macro GetLatestOperDate;

  var
    SK_DepDoc = KeyNum(DepDoc, 6),
    RecFound,
    OpFound = false,
    RetVal = Dep.Open_Date;


  DepDoc.Referenc = Dep.Referenc;
  DepDoc.Date_Document = {curdate};
  DepDoc.NumDayDoc = 32767;
  RecFound = GetLE(DepDoc);
  while(RecFound and (not OpFound)
    and (DepDoc.Referenc == Dep.Referenc))
    if((DepDoc.TypeOper != Расчет_Процентов)
      and (DepDoc.TypeOper != Зачисление_Процентов)
      and IsServDoc(DepDoc))
      RetVal = DepDoc.Date_Document;
      OpFound = true;
    end;
    RecFound = Prev(DepDoc);
  end;
  KeyNum(DepDoc, SK_DepDoc);
  return RetVal;
end;

macro Convert(Sum)

  var ScaleRate = 1;

  if (Curr.ScaleOfc != 0)
    ScaleRate = Curr.ScaleOfc;
  end;

  if(IsCur)
    return Money(Sum * Curr.Cur_Rate / (100 * ScaleRate));
  else
    return Sum;
  end;
end;

macro CheckAndSetStaticAccType

  var
    RetVal = false;

  if(Convert(Dep.Sum_Rest) > $3.00)
    if({curdate} - GetLatestOperDate > 365 * 10)
      StaticAccType = SA_STATIC;
      RetVal = true;
    end;
  else
    if({curdate} - GetLatestOperDate > 365 * 5)
      StaticAccType = SA_UNITED;
      RetVal = true;
    end;
  end;
  return RetVal;
end;

macro FindOrCreateStaticAcc(AccType, Account);

  var
    Stat,
    AccNumber,
    Number,
    RecFound,
    T_AccFound = false,
    T_OldKeyNum,
    OldKeyNum = KeyNum( Dep, 1 ),
    AccFound = false;

  ClearRecord(Dep);
  Dep.IsCur = IsCur;
  Dep.FNCash = Branch;
  Dep.Type_Account = StaticAccType;
  Dep.Code_Currency = Curr.Code_Currency;
  RecFound = GetGE(Dep);
  while(RecFound and (not AccFound)
    and (Dep.IsCur == IsCur)
    and (Dep.FNCash == Branch)
    and (Dep.Open_Close == StrFor( 0 ))
    and (Dep.Type_Account == StaticAccType)
    and (Dep.Code_Currency == Curr.Code_Currency))
    if((Dep.Open_Date == {curdate}) and (Dep.PrevAccount == AccType))
      AccFound = true;
    end;
    if(not AccFound)
      RecFound = Next(Dep);
    end;
  end;
  KeyNum( Dep, OldKeyNum );

  if(AccFound)
    ASFirstTime = false;
    if(BranchStatus == BS_BRANCH)
      T_OldKeyNum = KeyNum( T_Dep, 8 );
      T_Dep.Referenc = Dep.Referenc;
      T_AccFound = GetEQ(T_Dep);
      KeyNum( T_Dep, T_OldKeyNum );
      if ( not T_AccFound )
        Copy(T_Dep, Dep);
        Stat = Insert(T_Dep);
        return Stat;
      else
        return true;
      end;
    else
      return true;
    end;
  else
    AccNumber = FormStaticAccountNumber(Number);
    ClearRecord(Dep);
    Dep.IsCur = IsCur;
    Dep.FNCash = Branch;
    Dep.Account = AccNumber;
    Dep.Referenc = FormReference;
    Dep.Number = Number;
    Dep.Type_Account = Dep.GroupNumb = StaticAccType;
    Dep.Oper = {oper};
    Dep.PrevAccount = AccType;
    Dep.Code_Currency = Curr.Code_Currency;
    Dep.Open_Date = {curdate};
    Copy(T_Dep, Dep);
    Stat = Insert(Dep);
    if(Stat and (BranchStatus == BS_BRANCH))
      Stat = Insert(T_Dep);
    end;
    ASFirstTime = true;
    return Stat;
  end;
end;

macro FindDepClient(ClientID)

  DepClient.CodClient = ClientID;
  return GetEQ(DepClient);
end;

macro AdjustStatisticsOut(Sum)

  record StatParm("StatParm.rec");

  ClearRecord(StatParm);
  StatParm.Op = Binsert;
  StatParm.KindOp = D_OUT;
  StatParm.Sum = Sum;
  StatParm.CloseFlag = StrFor(1);
  StatParm.Type_Account = Dep.PrevAccount;
  StatParm.CodCur = Dep.Code_Currency;
  StatParm.Date = {curdate};
  StatParm.IsCur = Dep.IsCur;
  StatParm.FlagRez = DepClient.Citizenship;
  if (Index(Dep.UserTypeAccount,"Н") > 0)
    StatParm.Branch = NumRealFNCash();
  else
    StatParm.Branch = Dep.FNCash;
  end;

  if ( UpdateDepStats(StatParm) == 0 )
    TrnStat = FALSE;
  else
    TrnStat = TRUE;
  end;
end;

macro AdjustStatisticsIn(Sum)

  record StatParm("StatParm.rec");

  ClearRecord(StatParm);
  StatParm.Op = Binsert;
  StatParm.KindOp = D_IN;
  StatParm.Sum = Sum;
  if(ASFirstTime or (Dep.Type_Account == SA_STATIC))
    StatParm.OpenFlag = StrFor(1);
    ASFirstTime = false;
  end;
  StatParm.Type_Account = Dep.Type_Account;
  StatParm.CodCur = Dep.Code_Currency;
  StatParm.Date = {curdate};
  StatParm.IsCur = Dep.IsCur;
  if (Index(Dep.UserTypeAccount,"Н") > 0)
    StatParm.Branch = NumRealFNCash();
  else
    StatParm.Branch = Dep.FNCash;
  end;

  if ( UpdateDepStats(StatParm) == 0 )
    TrnStat = FALSE;
  else
    TrnStat = TRUE;
  end;
end;

macro AdjustDepRest(Sum)

  ClearRecord(DepRest);
  DepRest.Referenc = Dep.Referenc;
  DepRest.Type_Object = 2001;
  DepRest.Date_Rest = Dep.Open_Date;
  if(not GetEQ(DepRest))
    DepRest.Rest = Sum;
    TrnStat = Insert(DepRest);
  else
    DepRest.Rest = DepRest.Rest + Sum;
    TrnStat = Update(DepRest);
  end;
end;

macro AdjustDepDocOut(Sum)

  ClearRecord(DepDoc);
  DepDoc.Referenc = Dep.Referenc;
  DepDoc.Date_Document = Dep.Open_Date;
  DepDoc.NumDayDoc = 0;
  if(GetEQ(DepDoc))
    DepDoc.OutSum = DepDoc.OutSum + Sum;
    TrnStat = Update(DepDoc);
  else
    DepDoc.IsCur = Dep.IsCur;
    DepDoc.FNCash = Dep.FNCash;
    DepDoc.Oper = {oper};
    DepDoc.Account = Dep.Account;
    DepDoc.Type_Account = Dep.PrevAccount;
    DepDoc.Code_Currency = Dep.Code_Currency;
    DepDoc.YesSbook = "X";
    DepDoc.Date_Document = Dep.Open_Date;
    DepDoc.DepDate_Document = Dep.Open_Date;
    DepDoc.OutSum = Sum;
    DepDoc.TypeOper = Списание_Простое;
    DepDoc.Brigade = OperBrigade;
    DepDoc.iApplicationKind = DocApplicationKind;
    DepDoc.ApplicationKey = FormApplicationKey(DocApplicationKind);
    DepDoc.Ground = "Перевод на " + Dep.Type_Account + " " + Dep.PrevAccount;
    TrnStat = Insert(DepDoc);
    if(TrnStat)
      TrnStat = CashLink_AddRec(DocApplicationKind, DepDoc.ApplicationKey);
    end;
  end;
end;

macro AdjustDepDocIn(Sum)

  ClearRecord(DepDoc);
  DepDoc.Referenc = Dep.Referenc;
  DepDoc.Date_Document = Dep.Open_Date;
  DepDoc.NumDayDoc = 1;
  if(GetEQ(DepDoc))
    DepDoc.InSum = DepDoc.InSum + Sum;
    DepDoc.Rest = DepDoc.Rest + Sum;
    TrnStat = Update(DepDoc);
  else
    DepDoc.IsCur = Dep.IsCur;
    DepDoc.FNCash = Dep.FNCash;
    DepDoc.Oper = {oper};
    DepDoc.Account = Dep.Account;
    DepDoc.Type_Account = Dep.Type_Account;
    DepDoc.Code_Currency = Dep.Code_Currency;
    DepDoc.YesSbook = "X";
    DepDoc.Date_Document = Dep.Open_Date;
    DepDoc.DepDate_Document = Dep.Open_Date;
    DepDoc.InSum = Sum;
    DepDoc.Rest = Sum;
    DepDoc.TypeOper = Зачисление_По_Данным_ПК;
    DepDoc.Brigade = OperBrigade;
    DepDoc.iApplicationKind = DocApplicationKind;
    DepDoc.ApplicationKey = FormApplicationKey(DocApplicationKind);
    DepDoc.Ground = "Перевод на " + Dep.Type_Account + " " + Dep.PrevAccount;
    TrnStat = Insert(DepDoc);
    if(TrnStat)
      TrnStat = CashLink_AddRec(DocApplicationKind, DepDoc.ApplicationKey);
    end;
  end;
end;

macro AdjustDep(Sum)

  Dep.Sum_Rest = Dep.Sum_Rest + Sum;
  Dep.S_NumAccounts = Dep.S_NumAccounts + 1;
  Dep.NumSession = 0;
  TrnStat = Update(Dep);
end;

macro StepByStepCarry(Sum)
  if( StrFor(GetProgramID) != "П" )
     AdjustDep(Sum);                                        /* Счет */
  else
     TrnStat = True;
  end;
  if(TrnStat and (Dep.Type_Account == SA_UNITED)
    and (BranchStatus == BS_OPERDEP))
    AdjustDepRest(Sum);                                  /* Остаток */
  end;
  if(TrnStat and ((Dep.Type_Account == SA_UNITED) or
    (BranchStatus == BS_BRANCH)))                        /* Док. на списание */
    AdjustDepDocOut(Sum);
  end;
  if(TrnStat and (Dep.Type_Account == SA_UNITED) and
    (BranchStatus == BS_OPERDEP))
    AdjustDepDocIn(Sum);                                 /* Док. на зачисл. */
  end;
  if(TrnStat/* and ((BranchStatus == BS_BRANCH)
    or (Dep.Type_Account == SA_UNITED))*/)
    AdjustStatisticsOut(Sum);                            /* Стат. по исх. в/в */
  end;
  if(TrnStat/* and (BranchStatus == BS_OPERDEP) and
    (Dep.Type_Account == SA_UNITED)*/)
    AdjustStatisticsIn(Sum);                             /* Стат. по св. в/в */
  end;
end;

macro T_TreatAccount

  var
    Rest = Dep.Sum_Rest,
    DepPos = GetPos(Dep),
    StaticAccPos, T_StaticAccPos;

    DocApplicationKind = SB_DEPOSIT;

  TrnStat = FindDepClient(Dep.CodClient);
  if(TrnStat)
    CreateDepDoc;
  end;
  if(TrnStat)
    TrnStat = FindOrCreateStaticAcc(Dep.Type_Account, Dep.Account);
  end;
  if(TrnStat)
    StaticAccPos = GetPos(Dep);
    if(BranchStatus == BS_BRANCH)
      T_StaticAccPos = GetPos(T_Dep);
    end;
    StaticAcc = Dep.Account;
    GetDirect(Dep, DepPos);
    if((StaticAccType == SA_UNITED) or (BranchStatus == BS_BRANCH))
      Dep.Open_Close = "З";
    end;
    Dep.Close_Date = {curdate};
/*    Dep.UserField1 = "Переведен на неподвижный/объединенный " + StaticAcc +
      " " + String({curdate}); */
    Dep.StaticRest = Rest;
    Dep.SvodAccount = StaticAcc;
    if(StaticAccType == SA_STATIC)
      Dep.UserTypeAccount = Dep.UserTypeAccount + "Н";
    else
      Dep.UserTypeAccount = Dep.UserTypeAccount + "С";
    end;
    Dep.NumSession = 0;
    TrnStat = Update(Dep);
  end;
  if(TrnStat and (BranchStatus == BS_BRANCH))
    UnloadAccount(Dep.Referenc);
  end;
  if(TrnStat)
    GetDirect(Dep, StaticAccPos);
    StepByStepCarry(Rest);
    if(TrnStat and (BranchStatus == BS_BRANCH))
      GetDirect(T_Dep, T_StaticAccPos);
      Copy(T_Dep, Dep);
      TrnStat = Update(T_Dep);
    end;
  end;
  GetDirect(Dep, DepPos);
  if(not TrnStat)
    AbortTrn;
  end;
  return TrnStat;
end;

macro TreatAccount;

  var
    DepPos;

  TrnStat = true;
  ProcessTrn(null, "T_TreatAccount",
    Curr,
    DepType,
    DepClient, T_DepClient,
    From(I_Dep),To(I_Dep),
    From(I_DepDoc),To(I_DepDoc),
    From(I_DepRest),To(I_DepRest),
    From(I_TrWill),To(I_TrWill),
    From(I_DepClient),To(I_DepClient),
    From(I_AuxInfo),To(I_AuxInfo),
    From(I_ConGet),To(I_ConGet),
    From(I_PcCalc),To(I_PcCalc),
    From(I_PcEstim),To(I_PcEstim),
    From(I_CashLink),To(I_CashLink),
    From(I_AccReg),To(I_AccReg),
    From(I_TextInfo),To(I_TextInfo),
    From(I_PcRDate),To(I_PcRDate),
    From(I_DepHistr),To(I_DepHistr),
    From(I_ScAcc),To(I_ScAcc),
    From(I_ScLink),To(I_ScLink),
    From(I_ScCard),To(I_ScCard),
    From(I_ScTran),To(I_ScTran),
    From(I_PcTax),To(I_PcTax), 
    From(I_RetOp),To(I_RetOp), 
    From(I_OpObject),To(I_OpObject));
  return TrnStat;
end;

macro CreateTrFiles;

  return (Clone(T_Dep,TrakDir+"depositr"+FileExt)
    and Clone(T_DepDoc,TrakDir+"sbdepdoc"+FileExt)
    and Clone(T_DepRest,TrakDir+"sb_drest"+FileExt)
    and Clone(T_TrWill,TrakDir+"sbtrast"+FileExt)
    and Clone(T_DepClient,TrakDir+"depclnt"+FileExt)
    and Clone(T_AuxInfo,TrakDir+"auxinfo"+FileExt)
    and Clone(T_ConGet,TrakDir+"sb_congt"+FileExt)
    and Clone(T_PcCalc,TrakDir+"pc__calc"+FileExt)
    and Clone(T_PcEstim,TrakDir+"pcestim"+FileExt)
    and Clone(T_CashLink,TrakDir+"sb_casln"+FileExt)
    and Clone(T_AccReg,TrakDir+"accreg"+FileExt)
    and Clone(T_TextInfo,TrakDir+"textinfo"+FileExt)
    and Clone(T_PcRDate,TrakDir+"pc_rdate"+FileExt)
    and Clone(T_DepHistr,TrakDir+"dephistr"+FileExt)
    and Clone(T_ScAcc,TrakDir+"scacc"+FileExt)
    and Clone(T_ScLink,TrakDir+"sclink"+FileExt)
    and Clone(T_ScCard,TrakDir+"sccard"+FileExt)
    and Clone(T_ScTran,TrakDir+"sctran"+FileExt)
    and Clone(T_PcTax,TrakDir+"pc_tax"+FileExt)
    and Clone(T_RetOp,TrakDir+"ret_op"+FileExt) 
    and Clone(T_OpObject,TrakDir+"opobject"+FileExt));
end;

macro OpenTrFiles;

  return (Open(T_Dep,TrakDir+"depositr"+FileExt)
    and Open(T_DepDoc,TrakDir+"sbdepdoc"+FileExt)
    and Open(T_DepRest,TrakDir+"sb_drest"+FileExt)
    and Open(T_TrWill,TrakDir+"sbtrast"+FileExt)
    and Open(T_DepClient,TrakDir+"depclnt"+FileExt)
    and Open(T_AuxInfo,TrakDir+"auxinfo"+FileExt)
    and Open(T_ConGet,TrakDir+"sb_congt"+FileExt)
    and Open(T_PcCalc,TrakDir+"pc__calc"+FileExt)
    and Open(T_PcEstim,TrakDir+"pcestim"+FileExt)
    and Open(T_CashLink,TrakDir+"sb_casln"+FileExt)
    and Open(T_AccReg,TrakDir+"accreg"+FileExt)
    and Open(T_TextInfo,TrakDir+"textinfo"+FileExt)
    and Open(T_DepHistr,TrakDir+"dephistr"+FileExt)
    and Open(T_ScAcc,TrakDir+"scacc"+FileExt)
    and Open(T_ScLink,TrakDir+"sclink"+FileExt)
    and Open(T_ScCard,TrakDir+"sccard"+FileExt)
    and Open(T_ScTran,TrakDir+"sctran"+FileExt)
    and Open(T_PcTax,TrakDir+"pc_tax"+FileExt)
    and Open(T_RetOp,TrakDir+"ret_op"+FileExt)
    and Open(T_OpObject,TrakDir+"opobject"+FileExt));
end;

macro OpenOrCreateTrFiles(BranchID)

  var
    BIDStr;

  FileExt = ".s00";
  BIDStr = String(BranchID);
  StrSet(FileExt, 5 - StrLen(BIDStr), BIDStr);
  if(not OpenTrFiles )
    if(not CreateTrFiles)
      MsgBox("Ошибка при создании транспортных файлов");
      Exit( -1 );
    end;
  end;
end;

