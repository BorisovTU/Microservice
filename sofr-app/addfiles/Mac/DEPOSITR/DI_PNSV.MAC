/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Страхование частных вкладов.

  Формирование реестра обязательств.

  Панель параметров формирования реестра по филиалу.

*****************************************************************/

import BankInter, DI_Def;

file did ( depiprev ) key 2 write;  /* Файл "Предварительный реестр". */

record dlg_svod ( "DIP_BRNC", "rtusrmac" ) dialog;

/* Переменные параметров для предварительного формирования реестра */
var panSvDateMake = {curdate};  /* Дата формирования */
var panSvNeedPrev = FALSE;      /* Флаг вызова "Предварит. формирования" */
var panSvLoansOn  = FALSE;      /* Флаг: Loans Online */
var panSvLoansOff = FALSE;      /* Флаг: Loans Offline */
var panSvNeedCalc = FALSE;      /* Флаг выполнения расчета */
var panSvEscape   = FALSE;      /* Флаг отказа от работы (нажата клавиша ESC) */

var RegV_LNS1 = 0, RegV_LNS2 = 0;

var ErrABC;

var save_DateMake;

/****************************************************************/
/*               Проверка наличия записей по депозитам.         */
/****************************************************************/
macro ExistDebet( dDateMake )

  var dRet;

  ReWind( did );
  ClearRecord( did );
  did.DateMake = dDateMake;
  did.Type     = TYPE_DEPOS;
  if ( GetGE( did )  AND
       ( did.DateMake == dDateMake )  AND
       ( did.Type     == TYPE_DEPOS ) )
    dRet = TRUE;
  else
    dRet = FALSE;
  end;

  return dRet;

end;

/****************************************************************/
/*               Проверка наличия записей по кредитам.          */
/****************************************************************/
macro ExistCredit( cDateMake )

  var cRet;

  ReWind( did );
  ClearRecord( did );
  did.DateMake = cDateMake;
  did.Type     = TYPE_CR_LOAN;
  if ( GetGE( did )  AND
       ( did.DateMake == cDateMake )  AND
       ( did.Type     >= TYPE_CR_LOAN ) )
    cRet = TRUE;
  else
    cRet = FALSE;
  end;

  return cRet;

end;

/****************************************************************/
/*           Установка значений полей флагов в панели.          */
/****************************************************************/
macro SetParmFields()

  if ( panSvNeedPrev )
    dlg_svod.NeedPrev = "X";
  else
    dlg_svod.NeedPrev = "";
  end;
  if ( panSvLoansOn )
    dlg_svod.LoansOn = "X";
  else
    dlg_svod.LoansOn = "";
  end;
  if ( panSvLoansOff )
    dlg_svod.LoansOff = "X";
  else
    dlg_svod.LoansOff = "";
  end;
  if ( panSvNeedCalc )
    dlg_svod.NeedCalc = "X";
  else
    dlg_svod.NeedCalc = "";
  end;

end;

/****************************************************************/
/*          Установка параметров формирования Реестра.          */
/****************************************************************/
macro SetParameters( pDateMake )

  var sExistDebet  = ExistDebet(  pDateMake );
  var sExistCredit = ExistCredit( pDateMake );

  /* Флаг вызова "Предварит. формирования" */
  panSvNeedPrev = ( NOT sExistDebet );

  /* Флаг: Loans Online */
  panSvLoansOn = ( ( ( RegV_LNS1 != 0 )  OR  ( RegV_LNS2 != 0 ) )  AND
                 ( NOT sExistCredit ) );

  /* Флаг: Loans Offline */
  panSvLoansOff = ( ( NOT panSvLoansOn )  AND
                  ( RegV_LNS1 == 0 )  AND  ( RegV_LNS2 == 0 )  AND
                  ( NOT sExistCredit ) );

  /* Флаг выполнения расчета */
  panSvNeedCalc = ( ( sExistDebet   OR  panSvNeedPrev )  AND
                  ( sExistCredit  OR  panSvLoansOn  OR  panSvLoansOff ) );

  /* Установка значений полей флагов в панели. */
  SetParmFields();

end;

/****************************************************************/
/*                   Обработка событий панели.                  */
/****************************************************************/
macro EventProcSvod( dlg, cmd, id, key )

  if ( cmd == DLG_KEY )
    /* msgbox(key); */
    if   ( key == KEY_F2 )
      if ( dlg.DateMake == Date( 0, 0, 0 ) )
        MsgBox( "Дата не должна быть нулевой!" );
        SetFocus( dlg, 0 );
        /* return CM_IGNORE; */
      else
        return CM_SAVE;
      end;
    elif ( key == KEY_F9 )
      return CM_IGNORE;
    elif ( key == KEY_ESC )
      panSvEscape = TRUE;
      return CM_SAVE;
    elif ( key == KEY_SPACE )
      if   ( id == 1 )
        if ( dlg.NeedPrev == "X" )
          dlg.NeedPrev = "";
        else
          dlg.NeedPrev = "X";
        end;
      elif ( id == 2 )
        if ( dlg.LoansOn == "X" )
          dlg.LoansOn = "";
        else
          dlg.LoansOn  = "X";
          dlg.LoansOff = "";
        end;
      elif ( id == 3 )
        if ( dlg.LoansOff == "X" )
          dlg.LoansOff = "";
        else
          dlg.LoansOff = "X";
          dlg.LoansOn  = "";
        end;
      elif ( id == 4 )
        if ( dlg.NeedCalc == "X" )
          dlg.NeedCalc = "";
        else
          dlg.NeedCalc = "X";
        end;
      end;
      UpdateFields( dlg );
    end;
  elif ( cmd == DLG_REMFOCUS )
    if ( id == 0 )  /* Поле даты формирования. */
      if ( dlg_svod.DateMake != save_DateMake )  /* Дата формирования изменилась. */
        save_DateMake = dlg_svod.DateMake;
        /* Переустановка параметров формирования Реестра. */
        SetParameters( save_DateMake );
        UpdateFields( dlg );
      end;
    end;
  end;

end;

/****************************************************************/
/*            Панель параметров формирования реестра.           */
/****************************************************************/
macro DI_Panel_Svod( pParm )

  if ( pParm )
    /* Чтение настроек регистра АБС. */
    if ( ( GetRegistryValue( RegP_LNS1, V_BOOL, RegV_LNS1, ErrABC ) != V_BOOL )  OR
         ( ErrABC != 0 ) )
      RegV_LNS1 = 0;
    end;

    if ( ( GetRegistryValue( RegP_LNS2, V_BOOL, RegV_LNS2, ErrABC ) != V_BOOL )  OR
         ( ErrABC != 0 ) )
      RegV_LNS2 = 0;
    end;
  end;

  /* Установка начальных значений полей панели */
  dlg_svod.DateMake = panSvDateMake;
  save_DateMake     = panSvDateMake;

  if ( pParm )
    SetParameters( panSvDateMake );
  else
    /* Установка значений полей флагов в панели. */
    SetParmFields();
  end;

  panSvEscape = FALSE;

  /* Работа с панелью */
  RunDialog( dlg_svod, "EventProcSvod" );

  /* Установка параметров для предварительного формирования реестра */
  panSvDateMake = dlg_svod.DateMake;
  if ( dlg_svod.NeedPrev == "X" )
    panSvNeedPrev = TRUE;
  else
    panSvNeedPrev = FALSE;
  end;
  if ( dlg_svod.LoansOn == "X" )
    panSvLoansOn = TRUE;
  else
    panSvLoansOn = FALSE;
  end;
  if ( dlg_svod.LoansOff == "X" )
    panSvLoansOff = TRUE;
  else
    panSvLoansOff = FALSE;
  end;
  if ( dlg_svod.NeedCalc == "X" )
    panSvNeedCalc = TRUE;
  else
    panSvNeedCalc = FALSE;
  end;

end;

/* DI_Panel_Svod(); */
