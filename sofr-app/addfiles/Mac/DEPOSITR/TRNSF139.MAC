/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вкладные операции                                                       *
*                                                                          *
*  Реестр запросов переводов (форма 139-р)                                 *
*                                                                          *
*  Лебедев С.В.                                                            *
*  08.10.2004                                                              *
\**************************************************************************/

import DeprIntr, RSD, sqlconv, CommonInter;

private file currency ( currency ) key 0;
private file rttrncat ( rttrncat ) key 0;
private file depositr ( depositr ) key 8;

private var ClientList = TClientList;

private var {curdate};
private var Branch = NumFNCash( );
private var NumRec = 0;

private record SC_PRD ( SC_PRD ) dialog;
const ne_BegDate = 0;
const ne_EndDate = 1;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefMonthName( numMonth );

  var retValue = "";

  if ( numMonth == 1 )
    retValue = "января";
  elif ( numMonth == 2 )
    retValue = "февраля";
  elif ( numMonth == 3 )
    retValue = "марта";
  elif ( numMonth == 4 )
    retValue = "апреля";
  elif ( numMonth == 5 )
    retValue = "мая";
  elif ( numMonth == 6 )
    retValue = "июня";
  elif ( numMonth == 7 )
    retValue = "июля";
  elif ( numMonth == 8 )
    retValue = "августа";
  elif ( numMonth == 9 )
    retValue = "сентября";
  elif ( numMonth == 10 )
    retValue = "октября";
  elif ( numMonth == 11 )
    retValue = "ноября";
  elif ( numMonth == 12 )
    retValue = "декабря";
  end;

  return retValue;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefLastDayMonth ( m, y )

  var d;

  m = m + 1;

  if ( m > 12 )
    m = 1;
    y = y + 1;
  end;

  DateSplit( ( Date( 1, m, y ) - 1 ), d, m, y );

  return d;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro AddMonth ( m, y, dm )

  m = m + dm;
  while ( m > 12 )
    m = m - 12;
    y = y + 1;
  end;

  SetParm( 0, m );
  SetParm( 1, y );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro SumDate ( BegDate, Term, KindTerm )

  var retDate = Date( 0, 0, 0 );
  var d, m, y;
  var dd;
  var nd, nm, ny;

  const R_DAY     = 1; // Дней
  const R_MON     = 2; // Месяцев
  const R_QW      = 3; // Кварталов
  const R_YEAR    = 4; // Лет
  const R_REALDAY = 5; // Календарных дней

  if ( KindTerm == R_QW )
    Term = Term * 3;
    KindTerm = R_MON;
  end;

  DateSplit( BegDate, d, m, y );

  // ---------------------------------------------------------------------
  if ( KindTerm == R_YEAR )
    y = y + Term;
    if ( ( m == 2 ) and ( d > 28 ) )
      d = DefLastDayMonth( m, y );
    end;
    retDate = Date( d, m, y );
  // ---------------------------------------------------------------------
  elif ( KindTerm == R_DAY )
    nm = Int( Term / 30 );
    nd = Term - nm * 30;
    AddMonth( m, y, nm );
    retDate = Date( d, m, y ) + nd;
  // ---------------------------------------------------------------------
  elif ( KindTerm == R_REALDAY )
    retDate = BegDate + Term;
  // ---------------------------------------------------------------------
  elif ( KindTerm == R_MON )
    AddMonth( m, y, Term );
    dd = DefLastDayMonth( m, y );
    if ( d > dd )
      d = dd;
    end;
    retDate = Date( d, m, y );
  end;

  return retDate;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro HeadReport

  var nameBranch = "";
  var dBeg, mBeg, yBeg, dEnd, mEnd, yEnd;

  if (not findDepartment(Branch, nameBranch))
    nameBranch = "";
  end;

  DateSplit( SC_PRD.BeginDate, dBeg, mBeg, yBeg );
  DateSplit( SC_PRD.EndDate, dEnd, mEnd, yEnd );

  dBeg = String( dBeg );
  while ( StrLen( dBeg ) < 2 )
    dBeg = "0" + dBeg;
  end;

  dEnd = String( dEnd );
  while ( StrLen( dEnd ) < 2 )
    dEnd = "0" + dEnd;
  end;

  mBeg = DefMonthName( mBeg );
  mEnd = DefMonthName( mEnd );

  [       Структурное подразделение № #                                                                                                                                                 Ф.№ 139-р]
  ( nameBranch );
  [                                                                Реестр принятых заявлений о переводе вкладов из других];
  [                                                                              филиалов Сбербанка России];
  [                                                                     с ## ######## ####г. по ## ######## ####г.]
  ( dBeg, mBeg, yBeg:4, dEnd, mEnd, yEnd:4);
  [ ];
  [----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------]; 
  [|Регистр.|   Дата   |  Фамилия, имя и   |  Фамилия, имя и   |Номер счета по вкладу | Остаток |Типограф-|    Сумма    |Наименов. |Код | Предпол. |   Дата   |Номер счета по вкладу,|   Примечание   |];
  [| номер  |  приема  |     отчество      |     отчество      |      с которого      | вклада  |  ский   |  перевода   |подраздел.|кат.|  срок    |зачисления| на который зачислена |                |];
  [|завления|заявления |     вкладчика     |обратившегося лица |    осуществляется    |   по    |  номер  |             |в котором |пер.| поступл. |  суммы   |    сумма перевода    |                |];
  [|о перев.|          |                   |                   |       перевод        |  сбер.  |  сбер.  |             | хранится |    |  суммы   | перевода |                      |                |];
  [| ф.№143 |          |                   |                   |                      | книжке  | книжки  |             |  вклад   |    | перевода | во вклад |                      |                |];
  [----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------]; 
  [|   1    |    2     |         3         |         4         |          5           |    6    |    7    |      8      |    9     | 10 |    11    |    12    |          13          |       14       |];
  [----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro BottomReport

  [----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro WorkWithOneRtTransf( Rs:RsdRecordSet )

  var InputDate     = rslDate( Rs.Value( "fInputDate" ) );
  var DateProcessed = rslDate( Rs.Value( "fDateProcessed" ) );
  var Number        = Rs.Value( "fNumber" );
  var RequesterCode = Rs.Value( "fRequesterCode" );
  var SrcAccOwner   = Rs.Value( "fSrcAccOwner" );
  var RequesterName = "";
  var SrcAccName    = "";
  var SrcAccount    = rslString( Rs.Value( "fSrcAccount" ) );
  var BookRegNumber = rslString( Rs.Value( "fBookRegNumber" ) );
  var SrcAccRest    = Rs.Value( "fSrcAccRest" );
  var CurrCode      = Rs.Value( "fCurrCode" );
  var CurrName      = "";
  var Sum           = Rs.Value( "fSum" );
  var SumDesc       = rslString( Rs.Value( "fSumDesc" ) );
  var SrcBranchNum  = rslString( Rs.Value( "fSrcBranchNum" ) );
  var Category      = Rs.Value( "fCategory" );
  var DatePut       = Date( 0, 0, 0 );
  var State         = Rs.Value( "fState" );
  var DestAccRef    = Rs.Value( "fDestAccRef" );
  var DestAcc       = "";
  var DocumentInfo  = rslString( Rs.Value( "fDocumentInfo" ) );
  var RequesterRole = rslString( Rs.Value( "fRequesterRole" ) );

  if ( ( RequesterCode != 0 ) and ClientList.GetRecord( RequesterCode ) )
    RequesterName = ClientList.CurRec.ConvertFio( );
  end;

  if ( ( SrcAccOwner != 0 ) and ClientList.GetRecord( SrcAccOwner ) )
    SrcAccName = ClientList.CurRec.ConvertFio( );
  end;

  if ( CurrCode >= 0 )
    ClearRecord( currency );
    currency.Code_Currency = CurrCode;
    if ( GetEQ( currency ) )
      CurrName = currency.Short_Name;
    end;
  end;

  if ( Category > 0 )
    ClearRecord( rttrncat );
    rttrncat.ID = Category;
    if ( GetEQ( rttrncat ) )
      if ( rttrncat.ProcessingTerm > 0 )
        DatePut = SumDate( InputDate, rttrncat.ProcessingTerm, rttrncat.ProcessingKindTerm );
      end;
    end;
  end;
  if ( DatePut != Date( 0, 0, 0 ) )
    DatePut = String( DatePut:f );
  else
    DatePut = "";
  end;

  if ( State == 1 )
    DateProcessed = String( DateProcessed:f );
  else
    DateProcessed = "";
  end;

  if ( State == 1 )
    ClearRecord( depositr );
    depositr.Referenc = DestAccRef;
    if ( GetEQ( depositr ) )
      DestAcc = depositr.Account;
    end;
  end;

  if ( RequesterRole == 0 )
    DocumentInfo = "";
  end;

  if ( Sum != 0 )
    [| ###### |##########|###################|###################|######################|#########|#########|######### ###|##########|### |##########|##########|######################|################|]
    ( Number:6, InputDate:f, SrcAccName:w, RequesterName:w, Acc_Form(SrcAccount), SrcAccRest:9:2, BookRegNumber, Sum:9:2, CurrName, SrcBranchNum, Category:3, DatePut, DateProcessed, Acc_Form(DestAcc), DocumentInfo:w );
  else
    [| ###### |##########|###################|###################|######################|#########|#########|#############|##########|### |##########|##########|######################|################|]
    ( Number:6, InputDate:f, SrcAccName:w, RequesterName:w, Acc_Form(SrcAccount), SrcAccRest:9:2, BookRegNumber, SumDesc:w, SrcBranchNum, Category:3, DatePut, DateProcessed, Acc_Form(DestAcc), DocumentInfo:w );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro WorkWithRtTransf

  var Rs;
  var Cmd = RsdCommand( "select rttransf.t_InputDate     fInputDate,"
                               "rttransf.t_DateProcessed fDateProcessed,"
                               "rttransf.t_Number        fNumber,"
                               "rttransf.t_RequesterCode fRequesterCode,"
                               "rttransf.t_RequesterRole fRequesterRole,"
                               "rttransf.t_DocumentInfo  fDocumentInfo,"
                               "rttransf.t_Category      fCategory,"
                               "rttransf.t_State         fState,"
                               "rttransf.t_CurrCode      fCurrCode,"
                               "rttransf.t_Sum           fSum,"
                               "rttransf.t_SumDesc       fSumDesc,"
                               "rttransf.t_DestAccRef    fDestAccRef,"
                               "rttransf.t_BookRegNumber fBookRegNumber,"
                               "rttransf.t_SrcBranchNum  fSrcBranchNum,"
                               "rttransf.t_SrcAccount    fSrcAccount,"
                               "rttransf.t_SrcAccOwner   fSrcAccOwner,"
                               "rttransf.t_SrcAccRest    fSrcAccRest"

                        " from drttransf_dbt rttransf"

                        " where rttransf.t_Branch = ?"
                          " and (rttransf.t_InputDate BETWEEN ? and ?)"
                          " and rttransf.t_Action < 2"
                        
                        " order by rttransf.t_InputDate, rttransf.t_Number" );

  Cmd.AddParam( "Branch",  RSDBP_IN, Branch );
  Cmd.AddParam( "MinDate", RSDBP_IN, SC_PRD.BeginDate );
  Cmd.AddParam( "MaxDate", RSDBP_IN, SC_PRD.EndDate );

  Cmd.execute;
  Rs = RsdRecordSet( Cmd );

  while ( Rs.MoveNext )
    NumRec = NumRec + 1;
    Message( " Обрабатывается перевод номер " + String( NumRec ) );

    WorkWithOneRtTransf( Rs );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DoReport

  HeadReport( );

  WorkWithRtTransf( );

  BottomReport( );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro WorkDialogSC_PRD ( IP, cmd, id, key )

  var stat = CM_DEFAULT;

  if ( cmd == DLG_KEY )
    // ENTER -------------------------------------------------------------
    if ( key == 13 )
      ;
    // ESC ---------------------------------------------------------------
    elif ( key == 27 )
      stat = CM_CANCEL;
    // CtrlF3 ------------------------------------------------------------
    elif ( key == 352 )
      if ( id == ne_BegDate )
        SC_PRD.BeginDate = {curdate};
      elif (id == ne_EndDate)
        SC_PRD.EndDate = {curdate};
      end;
    // F7 ----------------------------------------------------------------
    elif ( key == 321 )
      stat = CM_SAVE;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  var d, m, y;

  ClearRecord( SC_PRD );
  DateSplit( {curdate}, d, m, y );
  SC_PRD.BeginDate = Date( 1, m, y );
  SC_PRD.EndDate   = {curdate};

  if ( RunDialog( SC_PRD, "WorkDialogSC_PRD" ) )
    DoReport( );
  else
    Exit( 1 );
  end;

end;
