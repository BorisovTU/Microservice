/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Выписки по требованию налоговых органов.

  Выписка по операциям на счете физического лица.

*****************************************************************/

import DeprIntr, RSD, SQLConv, SQLExec, issrvdoc;

private const PTCK_BIC = 3;   // БИК (РФ).
private const PTCK_INN = 16;  // ИНН нашего банка.

private var OurBank_BIC     = Trim( getRegNumOurBank( PTCK_BIC ) );
private var OurBank_Name    = "";
private var OurBank_INN     = Trim( getRegNumOurBank( PTCK_INN ) );
private var OurBank_KPP     = "";

private var {Bank_INN};
private var {Name_Bank};
private var {curdate};
private var {oper};

private const DateNull = Date( 0, 0, 0 );

private var clientList = TClientList;

private var prsn = TBFile( "person.dbt",   "bank.def", "r", 0 );
private var tpac = TBFile( "typeac.dbt",   "bank.def", "r", 0 );
private var tcur = TBFile( "currency.dbt", "bank.def", "r", 0 );

var ErrCodeWR, ErrTextWR;

private var gBankName = "";
private var gBankCode = "";
private var gRecName  = "";
private var gRecINN   = "";
private var gRecKPP   = "";
private var gRecAcc   = "";

// ----------------------------------------
//    Строковый формат сумм с черточкой.
// ----------------------------------------
private macro Sum_Format( pSum )
  var rSum = "";
  var vSum = String( pSum:0:2:l );
  var lSum = StrLen( vSum );
  rSum = SubStr( vSum, 1, lSum-3 ) + "-" + SubStr( vSum, lSum-1, 2 );
  return rSum;
end;

// ----------------------------------------
//    Строковый формат сумм с черточкой.
// ----------------------------------------
private macro Sum_Format_Z( pSum )
  var rSum = "";
  var vSum = String( pSum:0:2:l );
  var lSum = StrLen( vSum );
  if ( pSum == 0.00 )
    rSum = ""; 
  else
    rSum = SubStr( vSum, 1, lSum-3 ) + "-" + SubStr( vSum, lSum-1, 2 );
  end;
  return rSum;
end;

// ----------------------------------------
//          Цифровой код валюты.
// ----------------------------------------
private macro Currency_Code( pCurr )
  var rCurr = "643";
  if ( pCurr > 0 )
    tcur.Clear();
    tcur.rec.Code_Currency = pCurr;
    if ( tcur.GetEQ() )
      rCurr = tcur.rec.EXTERNALCODE;
    end;
  end;
  return rCurr;
end;

// ----------------------------------------
//    Получение должности операциониста.
// ----------------------------------------
macro GetNameTypePerson()
 var sWork = "";

 tpac.ReWind();
 tpac.Clear();
 tpac.rec.iNumType = 14;  /* Уровни доступов. */
 tpac.rec.Type_Account = prsn.rec.cTypePerson;
 if ( tpac.GetEQ() )
   sWork = tpac.rec.Name_Type;
 else
   sWork = "?????";
   println( " *** Ошибка при чтении должности операциониста с кодом = ", prsn.rec.cTypePerson );
   ErrCodeWR = prsn.Status( ErrTextWR );
   println( "  Ошибка " + String( ErrCodeWR ) + ": " + ErrTextWR );
 end;
 return sWork;
end;

// ----------------------------------------
//    Получить доп.данные по документу.
// ----------------------------------------
private macro get_DocProp( kind, key )
  var ret = FALSE;
  var cmd, rs;

  cmd = RsdCommand( "select P.T_BANKNAME as bankName, P.T_BANKCODE as bankCode, P.T_NAME as rName, P.T_INN as rINN, P.T_KPP as rKPP, P.T_ACCOUNT as rAcc " + 
                      "from dRtpaymentprop_dbt p " +
                        "where P.T_DOCAPPLICATIONKIND=? AND P.T_DOCAPPLICATIONKEY=?" );

  cmd.addParam( "kind", RSDBP_IN );
  cmd.addParam( "key", RSDBP_IN );
  cmd.value( "kind" ) = kind;
  cmd.value( "key" )  = key;

  cmd.execute;

  rs = RsdRecordset( cmd );

  if ( rs.moveNext )

    gBankName = rs.value( "bankName" );
    gBankCode = rs.value( "bankCode" );
    gRecName  = rs.value( "rName" );
    gRecINN   = rs.value( "rINN" );
    gRecKPP   = rs.value( "rKPP" );
    gRecAcc   = rs.value( "rAcc" );

    if ( StrLen( gBankName ) < 2 )
      gBankName = "";
    end;
    if ( StrLen( gBankCode ) < 2 )
      gBankCode = ""; 
    end;
    if ( StrLen( gRecName ) < 2 )
      gRecName  = "";
    end;
    if ( StrLen( gRecINN ) < 2 )
      gRecINN   = "";
    end;
    if ( StrLen( gRecKPP ) < 2 )
      gRecKPP   = "";
    end;
    if ( StrLen( gRecAcc ) < 2 )
      gRecAcc   = "";
    end;

    ret = TRUE;
  else
    gBankName = "";
    gBankCode = ""; 
    gRecName  = "";
    gRecINN   = "";
    gRecKPP   = "";
    gRecAcc   = "";
  end;

  return ret;
end;

// ----------------------------------------
private macro Acc_Report( CodeClient, Referenc, Date1, Date2 )

  var cmd, rs;
  var flagTitle = TRUE;
  var flagYes   = FALSE;
  var sOper = "";
  var sWork = "";
  var nn = 1;
  var vAcc = "";
  var vCurr = "";
  var vRestIn = 0.0;
  var vRestOt = 0.0;
  var vTurnDt = 0.0;
  var vTurnKt = 0.0;

  if ( ( ValType( codeClient ) != V_INTEGER )  OR  ( codeClient == 0 )  OR  ( NOT clientList.GetRecord( codeClient ) ) )
    println( " " );
    println( "  Клиент с кодом " + CodeClient + " не найден." );
  end;

  prsn.Clear();
  prsn.rec.Oper = {oper};

  if ( prsn.GetEQ() )
    sOper = prsn.rec.Name; 
  else
    println( " " );
    println( "  Пользователь с кодом " + {oper} + " не найден." );
  end;

  sWork = GetNameTypePerson();

  OurBank_Name = {Name_Bank};

  [
                                                                          Утверждена
                                                             Приказом Федеральной налоговой службы
                                                             от 30 марта 2007 г. N ММ-3-06/178@


                                                             Наименование налогового органа
                                                             
                                                             ____________________________________________


                                                             Адрес ______________________________________



                              Выписка по операциям на счете физического лица


     ####################################################################################################
                  (полное или сокращенное наименование банка в соответствии с Книгой государственной
                                        регистрации кредитных организаций)

             +-------------------+                    +-------------------+
     ИНН     |#|#|#|#|#|#|#|#|#|#|            КПП     | | | | | | | | | | |
             +-------------------+                    +-------------------+

             +-----------------+
     БИК     |#|#|#|#|#|#|#|#|#|
             +-----------------+

           В соответствии с запросом налогового органа от _________________   № ____________________

           в отношении
      ####################################################################################################
                                          (ФИО физического лица)

             +-----------------------+            +-----------------+
     ИНН/КИО |#|#|#|#|#|#|#|#|#|#|#|#|        КПП | | | | | | | | | |
             +-----------------------+            +-----------------+]
  (
    ( "Банк " + OurBank_Name ):w,
    SubStr( OurBank_INN,  1, 1 ),
    SubStr( OurBank_INN,  2, 1 ),
    SubStr( OurBank_INN,  3, 1 ),
    SubStr( OurBank_INN,  4, 1 ),
    SubStr( OurBank_INN,  5, 1 ),
    SubStr( OurBank_INN,  6, 1 ),
    SubStr( OurBank_INN,  7, 1 ),
    SubStr( OurBank_INN,  8, 1 ),
    SubStr( OurBank_INN,  9, 1 ),
    SubStr( OurBank_INN, 10, 1 ),
    SubStr( OurBank_BIC,  1, 1 ),
    SubStr( OurBank_BIC,  2, 1 ),
    SubStr( OurBank_BIC,  3, 1 ),
    SubStr( OurBank_BIC,  4, 1 ),
    SubStr( OurBank_BIC,  5, 1 ),
    SubStr( OurBank_BIC,  6, 1 ),
    SubStr( OurBank_BIC,  7, 1 ),
    SubStr( OurBank_BIC,  8, 1 ),
    SubStr( OurBank_BIC,  9, 1 ),
    ( ClientList.CurRec.Rec.Name1 + " " + ClientList.CurRec.Rec.Name2 + " " + ClientList.CurRec.Rec.Name3 ):w:c,
    SubStr( ClientList.CurRec.Rec.INN,  1, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  2, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  3, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  4, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  5, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  6, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  7, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  8, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  9, 1 ),
    SubStr( ClientList.CurRec.Rec.INN, 10, 1 ),
    SubStr( ClientList.CurRec.Rec.INN, 11, 1 ),
    SubStr( ClientList.CurRec.Rec.INN, 12, 1 )
  );

  cmd = RsdCommand( "select T_ACCOUNT as acc, T_CODE_CURRENCY as curr, T_DATE_DOCUMENT as dateDoc, T_INSUM as sum_i, T_OUTSUM as sum_o, T_REST as rest, " +
                           "T_GROUND as ground, T_IAPPLICATIONKIND as appKind, T_APPLICATIONKEY as appKey " + 
                      "from dsbdepdoc_dbt " +
                        "where T_REFERENC=? and T_DATE_DOCUMENT>=? and T_DATE_DOCUMENT<=? and " +
                          IsServ_const +
                          "order by T_DATE_DOCUMENT, T_NUMDAYDOC" );

  cmd.addParam( "ref", RSDBP_IN );
  cmd.addParam( "date1", RSDBP_IN );
  cmd.addParam( "date2", RSDBP_IN );

  cmd.value( "ref" ) = Referenc;
  cmd.value( "date1" ) = Date1;
  cmd.value( "date2" ) = Date2;

  cmd.execute;

  rs = RsdRecordset( cmd );

  while ( rs.moveNext )

    vTurnDt = vTurnDt + rs.value( "sum_o" );
    vTurnKt = vTurnKt + rs.value( "sum_i" ); 

    if ( flagTitle )

      if ( ValType( rs.value( "rest" ) ) == V_NUMERIC )
        vRestIn = rs.value( "rest" ) - rs.value( "sum_i" ) + rs.value( "sum_o" );
      end;

      vAcc = rs.value( "acc" );
      vCurr = Currency_Code( rs.value( "curr" ) );

      [
                  представляет выписку по счету
                +-------------------------------------------------+
             №  |#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|#|
                +-------------------------------------------------+

                +-----+
                |#|#|#|
                +-----+
                цифровой код вида валюты счета (в соответствии с Общероссийским классификатором валют)

             за период с ########### по ###########.]
        (
          SubStr( vAcc,  1, 1 ),
          SubStr( vAcc,  2, 1 ),
          SubStr( vAcc,  3, 1 ),
          SubStr( vAcc,  4, 1 ),
          SubStr( vAcc,  5, 1 ),
          SubStr( vAcc,  6, 1 ),
          SubStr( vAcc,  7, 1 ),
          SubStr( vAcc,  8, 1 ),
          SubStr( vAcc,  9, 1 ),
          SubStr( vAcc, 10, 1 ),
          SubStr( vAcc, 11, 1 ),
          SubStr( vAcc, 12, 1 ),
          SubStr( vAcc, 13, 1 ),
          SubStr( vAcc, 14, 1 ),
          SubStr( vAcc, 15, 1 ),
          SubStr( vAcc, 16, 1 ),
          SubStr( vAcc, 17, 1 ),
          SubStr( vAcc, 18, 1 ),
          SubStr( vAcc, 19, 1 ),
          SubStr( vAcc, 20, 1 ),
          SubStr( vAcc, 21, 1 ),
          SubStr( vAcc, 22, 1 ),
          SubStr( vAcc, 23, 1 ),
          SubStr( vAcc, 24, 1 ),
          SubStr( vAcc, 25, 1 ),

          SubStr( vCurr,  1, 1 ),
          SubStr( vCurr,  2, 1 ),
          SubStr( vCurr,  3, 1 ),

          Date1:r,
          Date2:r
        );
      
      [
          +----+------------+---------------------------------------+-----------------------------------------------------------------------+-------------------------------------+---------------------------+
          |  № |    Дата    |             Реквизиты банка           |                   Реквизиты плательщика/получателя                    |           Сумма операции            |         Назначение        |
          | пп |  операции  |              плательщика /            |                                                                       |                                     |           платежа         |
          |    |            |               получателя              |                                                                       |                                     |                           |
          |    |            +---------------------------+-----------+----------------------+--------------+------------+--------------------+------------------+------------------+                           |
          |    |            |        Наименование       |    БИК    |      Наименование    |      ИНН     |     КПП    |     Номер счета    |     по дебету    |     по кредиту   |                           |
          +----+------------+---------------------------+-----------+----------------------+--------------+------------+--------------------+------------------+------------------+---------------------------+];

      flagYes = TRUE;
      flagTitle = FALSE;
    end;

    get_DocProp( rs.value( "appKind" ), rs.value( "appKey" ) );
    [   |####| ########## | ######################### | ######### | #################### | ############ | ########## |####################| ################ | ################ | ######################### |]
    (
      nn:4,
      String( rslDate( rs.value( "dateDoc" ) ) ),
      gBankName:25:w,
      gBankCode:9,
      gRecName:20:w,
      gRecINN:12,
      gRecKPP:10,
      gRecAcc:20,
      Sum_Format_Z( rs.value( "sum_o" ) ):16:r,
      Sum_Format_Z( rs.value( "sum_i" ) ):16:r,
      rs.value( "ground" ):25:w );
    [   +----+------------+---------------------------+-----------+----------------------+--------------+------------+--------------------+------------------+------------------+---------------------------+];

    nn = nn + 1;

  end;

  if ( flagYes )

    [
        +---------------------------+---------------------------+---------------------------+---------------------------+
        |    Остаток по счету на    | Сумма по дебету счета за  | Сумма по кредиту счета за |    Остаток по счету на    |
        |       начало периода      |           период          |           период          |        конец периода      |
        +---------------------------+---------------------------+---------------------------+---------------------------+
        |             1             |             2             |             3             |              4            |
        +---------------------------+---------------------------+---------------------------+---------------------------+
        |      ###############      |      ###############      |      ###############      |      ###############      |
        +---------------------------+---------------------------+---------------------------+---------------------------+
    ]
    (
      Sum_Format( vRestIn ):15:r,
      Sum_Format( vTurnDt ):15:r,
      Sum_Format( vTurnKt ):15:r,
      Sum_Format( vRestIn - vTurnDt + vTurnKt ):15:r
    );

  end;

  [

          Представитель
              банка      #################  ###################################   _______________    ##########
                            (должность)                 (Ф.И.О.)                     (подпись)         (дата)



                                                                        М.П.]
  (
    sWork:17:c,
    sOper:35:c,
    {curdate}
  );

end;

// ----------------------------------------
private macro MakeReport( CodeClient, Referenc, Date1, Date2 )
  var cmd, rs;

  if ( Referenc > 0 )
    Acc_Report( CodeClient, Referenc, Date1, Date2 );
  else
    cmd = RsdCommand( "select T_REFERENC as ref " +
                    "from ddepositr_dbt " +
                      "where T_CODCLIENT=? and T_OPEN_DATE>=? " +
                        "order by T_OPEN_CLOSE, T_CODE_CURRENCY, T_TYPE_ACCOUNT, T_ACCOUNT" );

    cmd.addParam( "clnt", RSDBP_IN );
    cmd.addParam( "open", RSDBP_IN );
    cmd.value( "clnt" ) = CodeClient;
    cmd.value( "open" ) = Date1;

    cmd.execute;

    rs = RsdRecordset( cmd );

    while ( rs.moveNext )
      Acc_Report( CodeClient, rs.value( "ref" ), Date1, Date2 );
      printLn( strFor( 12 ) );  /* Управляющий символ конца страницы */
    end;
  end;

end;

// ----------------------------------------
macro TaxOperReport( CodeClient, Referenc, Date1, Date2 )

  if ( Date1 == DateNull )
    Date1 = Date( 1, 1, 2001 );
  end;
  if ( Date2 == DateNull )
    Date2 = {curdate};
  end;

  MakeReport( CodeClient, Referenc, Date1, Date2 ); 

end;
