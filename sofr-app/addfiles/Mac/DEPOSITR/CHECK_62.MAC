/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Ввод дополнительных данных при безналичной покупки расчетного чека СБ РФ*                      *
*                                                                          *
*  Лебедев С.В.                                                            *
*  29.11.2000                                                              *
\**************************************************************************/

import DeprIntr, DeskInter, InputRN, alg_vars;

file sb_casvl ( sb_casvl ) key 3;
file groupmem ( groupmem ) key 0;

var depclnt = TClientList;

record Parm ( CashParm );

const CASHOP_DEBET = 5;

var InitFIO     = "";
var CodIntValue = 0;

record InputPanel ("RC_SELL") dialog;
const ne_IdentValue = 0;
const ne_NameValue  = 1;
const ne_FIO        = 2;
const ne_SummaNal   = 3;
const ne_SummaBNal  = 4;
const ne_SummaTotal = 5;


/**************************************************************************\
|                    Вставить документ на наличную оплату                  |
\**************************************************************************/
macro InsertPayDocOnNal

  var stat = TRUE;
  var i;
  var j;
  var str;
  var Symb;

  record pay_doc (pay_doc);

  ClearRecord(pay_doc);

  pay_doc.FNCash           = ALG_SBDEPDOC.FNCash;
  pay_doc.IsCur            = ALG_SBDEPDOC.IsCur;
  pay_doc.NumOpert         = 31;
  pay_doc.GroupOpert       = 11;
  pay_doc.Date_Document    = ALG_SBDEPDOC.Date_Document;
  pay_doc.CodCur           = ALG_SBDEPDOC.Code_Currency;
  pay_doc.Oper             = ALG_SBDEPDOC.Oper;
  pay_doc.InSum            = InputPanel.SummaNal;
  if (InputPanel.FIO == InitFIO)
    if ( depclnt.GetRecord( ALG_SBDEPDOC.CodClient ) )
      pay_doc.ClientLastName   = depclnt.CurRec.rec.Name1;
      pay_doc.ClientFirstName  = depclnt.CurRec.rec.Name2;
      pay_doc.ClientSecondName = depclnt.CurRec.rec.Name3;
      depclnt.CurRec.GetPersnCard();
      pay_doc.SocialNumber     = depclnt.CurRec.rec.SocialNumber;
      pay_doc.PersIDType       = depclnt.CurRec.rec.PaperKind;
      pay_doc.PassportSer      = depclnt.CurRec.rec.PaperSeries;
      pay_doc.PassportNumb     = depclnt.CurRec.rec.PaperNumber;
      pay_doc.CodClient        = depclnt.CurRec.rec.CodClient;
    end;
  end;
  if ((pay_doc.ClientLastName == "") AND (pay_doc.ClientFirstName == ""))
    str = Trim(InputPanel.FIO);
    i = 1;
    j = 1;
    while (i < StrLen(str))
      Symb = SubStr(str,i,1);
      if ((Symb == " ") OR (Symb == "."))
        j = j + 1;
        if ((Symb == " ") AND (i > 1))
          if ((SubStr(str,i - 1,1) == " ") OR (SubStr(str,i - 1,1) == "."))
            j = j - 1;
          end;
        end;
      else
        if   (j == 1)
          pay_doc.ClientLastName   = pay_doc.ClientLastName   + Symb;
        elif (j == 2)
          pay_doc.ClientFirstName  = pay_doc.ClientFirstName  + Symb;
        elif (j == 3)
          pay_doc.ClientSecondName = pay_doc.ClientSecondName + Symb;
        end;
      end;
      i = i + 1;
    end;
  end;
  pay_doc.iApplicationKind = 200;
  pay_doc.ApplicationKey   = FormApplicationKey(200);
  pay_doc.Ground = "Смешанная продажа р/чека " + InputPanel.IdentValue + " ";
  if (Trim(Parm.Series) != "")
    pay_doc.Ground = pay_doc.Ground + "сер." + Trim(Parm.Series) + " ";
  end;
  if (Parm.MinRegistrNumber != 0)
    pay_doc.Ground = pay_doc.Ground + "№" + Trim(String(Parm.MinRegistrNumber)) + " ";
  end;
  pay_doc.Ground = pay_doc.Ground + "Счет " + String(Acc_Form(ALG_SBDEPDOC.Account):f) + " сумма " + Trim(String(ALG_SBDEPDOC.OutSum:0:2:a));
  pay_doc.Ground = Trim(pay_doc.Ground);

  stat = InsertPayDocToGDDList(pay_doc);

  return stat;

end;


/**************************************************************************\
|                       Получить связанного кассира                        |
\**************************************************************************/
macro GetPatern (Oper)

   /*
   if ( ALG_DEPOSITOR.SpecialAccess != StrFor( 0 ) )
     return Oper;
   end; */

  groupmem.Branch = NumRealFNCash();
  groupmem.Date   = {curdate};
  groupmem.Group  = GetOperBrigade();
  groupmem.Oper   = Oper;
  if (GetEQ(groupmem))
    return groupmem.Linked;
  else
    return 0;
  end;

end;


/**************************************************************************\
|                 Формирование фамилии клиента по его коду                 |
\**************************************************************************/
macro InputSeriesAndNumber

  var stat     = TRUE;
  var ExitFlag = FALSE;
  var Retry    = FALSE;

  ClearRecord(Parm);
  Parm.Type             = 0;       /* Платежеспосбные ресурсы */
  Parm.TypeValue        = 2;
  Parm.CodIntValue      = CodIntValue;
  Parm.Oper             = ALG_SBDEPDOC.Oper;
  Parm.CashDateDoc      = ALG_SBDEPDOC.Date_Document;
  Parm.NumOper          = CASHOP_DEBET;
  Parm.ValueCol         = 1;
  Parm.NumbDocumCash    = ALG_SBDEPDOC.NDoc;
  Parm.ApplicationKind  = ALG_SBDEPDOC.iApplicationKind;
  Parm.ApplicationKey   = ALG_SBDEPDOC.ApplicationKey;
  Parm.Series           = "";
  Parm.MinRegistrNumber = 0;
  Parm.OperPatern       = ALG_SBDEPDOC.Oper;

  while (not ExitFlag)
    if (Parm.MinRegistrNumber == 0)
      stat = InputRegNum(ALG_SBDEPDOC.Oper,
                         Parm.Oper,
                         2,
                         CodIntValue,
                         "Введите номер расчетного чека " + InputPanel.IdentValue,
                         Parm.Series,
                         Parm.MinRegistrNumber);
    end;
    if (stat)
      stat = InitCashDoc(Parm);
    end;
    if (stat)
      stat = TRUE;
      ExitFlag = TRUE;
    else
      stat = FALSE;
      Retry = FALSE;
      GetTrue(Retry,"Ошибка при расходе. Повторить ?");
      if (Retry)
        Parm.MinRegistrNumber = 0;
      else
        ExitFlag = TRUE;
      end;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                 Формирование фамилии клиента по его коду                 |
\**************************************************************************/
macro FormFIOFromDepDoc

  InitFIO = "";

  if ( depclnt.GetRecord( ALG_SBDEPDOC.CodClient ) )
    if ( Trim( depclnt.CurRec.rec.Name1 ) != "" )
      InitFIO = Trim( depclnt.CurRec.rec.Name1 );
      if ( Trim( depclnt.CurRec.rec.Name2 ) != "" )
        InitFIO = InitFIO + " " + SubStr( Trim( depclnt.CurRec.rec.Name2 ), 1, 1 ) + ".";
        if ( Trim( depclnt.CurRec.rec.Name3 ) != "" )
          InitFIO = InitFIO + SubStr( Trim( depclnt.CurRec.rec.Name3 ), 1, 1 ) + ".";
        end;
      end;
    end;
  end;

end;


/**************************************************************************\
|    Проверка заполнения полей  панели ввода дополнительной информации     |
\**************************************************************************/
macro CheckFields

  var stat = TRUE;

  if (stat AND (InputPanel.IdentValue == ""))
    MsgBox("Вид расчетного чека не задан");
    stat = FALSE;
  end;

  if (stat AND (InputPanel.FIO == ""))
    MsgBox("ФИО владельца чека не задано");
    stat = FALSE;
  end;

  if (stat AND (InputPanel.SummaNal < 0))
    MsgBox("Сумма не может быть отрицательной");
    stat = FALSE;
  end;

  return stat;

end;


/**************************************************************************\
|      Обработчик для панели задания информации об оплачиваемом чеке       |
\**************************************************************************/
macro ChoiceValue

  array AIdentValue;
  array ACodIntValue;
  array ANameValue;
  array ANameMenu;

  var stat;
  var i = 0;

  sb_casvl.Type        = 0;
  sb_casvl.TypeValue   = 2;
  sb_casvl.CodIntValue = 71;
  stat = GetGE(sb_casvl);
  while (stat)
    if ((sb_casvl.Type        ==  0) AND
        (sb_casvl.TypeValue   ==  2) AND
        (sb_casvl.CodIntValue <= 79)    )
      AIdentValue (i) = Trim(sb_casvl.IdentValue);
      ACodIntValue(i) = sb_casvl.CodIntValue;
      ANameValue  (i) = Trim(sb_casvl.NameValue);
      ANameMenu   (i) = " " + SubStr(AIdentValue(i) + MkStr(" ",5),1,5) + SubStr(ANameValue(i),1,55) + " ";
      i = i + 1;
      stat = Next(sb_casvl);
    else
      stat = FALSE;
    end;
  end;

  if (ASize(ANameMenu) > 0)
    i = Menu(ANameMenu," ~ESC~ Отказ  ~ENTER~ Выбор","Виды чеков");
    if (i >= 0)
      InputPanel.IdentValue = AIdentValue (i);
      InputPanel.NameValue  = ANameValue  (i);
      CodIntValue           = ACodIntValue(i);
    end;
  end;

end;


/**************************************************************************\
|        Обработчик для панели задания информации о покупаемом чеке        |
\**************************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;
  var i;

  /* ================================================================== */
  if (cmd == DLG_KEY)
    /* ENTER -------------------------------------------------------- */
    if (key == 13)
      if (id == ne_SummaNal)
        SetFocus(IP,ne_IdentValue);
        stat = CM_IGNORE;
      end;
    /* F2 ----------------------------------------------------------- */
    elif (key == 316)
      if (CheckFields())
        stat = CM_SAVE;
      else
        stat = CM_IGNORE;
      end;
    /* F3 ----------------------------------------------------------- */
    elif (key == 317)
      if (id == ne_IdentValue)
        ChoiceValue();
        UpdateFields(IP);
      end;
    /* ESC ---------------------------------------------------------- */
    elif (key == 27)
      if (GetTrue(TRUE,"Вы хотите отменить проведение операции ?"))
        stat = CM_CANCEL;
      else
        stat = CM_IGNORE;
      end;
    end;
  /* ================================================================== */
  elif (cmd == DLG_REMFOCUS)
    if (id == ne_SummaNal)
      InputPanel.SummaTotal = InputPanel.SummaNal + InputPanel.SummaBNal;
      UpdateFields(IP);
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  ALG_RESULT = OK_RES;

  if (ALG_SBDEPDOC.ApplType == 20)

    if (ALG_SBDEPDOC.ApplicationKey == "")
      ALG_SBDEPDOC.iApplicationKind = 1;
      ALG_SBDEPDOC.ApplicationKey   = FormApplicationKey(1);
    end;

    FormFIOFromDepDoc();
    InputPanel.FIO        = InitFIO;
    InputPanel.IdentValue = "";
    InputPanel.NameValue  = "";
    InputPanel.SummaBNal  = ALG_SBDEPDOC.OutSum;
    InputPanel.SummaTotal = ALG_SBDEPDOC.OutSum;

    if (RunDialog(InputPanel,"WorkDialog"))
      if (NOT InputSeriesAndNumber())
        ALG_RESULT = ERR_RES;
      end;
    else
      ALG_RESULT = ERR_RES;
    end;

    if ((ALG_RESULT == OK_RES) AND (InputPanel.SummaNal > 0))
      if (NOT InsertPayDocOnNal())
        ALG_RESULT = ERR_RES;
      end;
    end;

    if (ALG_RESULT == OK_RES)
      ALG_SBDEPDOC.Ground = "Б/нал. продажа р/чека " + InputPanel.IdentValue + " ";
      if (Trim(Parm.Series) != "")
        ALG_SBDEPDOC.Ground = ALG_SBDEPDOC.Ground + "сер." + Trim(Parm.Series) + " ";
      end;
      if (Parm.MinRegistrNumber != 0)
        ALG_SBDEPDOC.Ground = ALG_SBDEPDOC.Ground + "№" + Trim(String(Parm.MinRegistrNumber)) + " ";
      end;
      if (InputPanel.FIO != InitFIO)
        ALG_SBDEPDOC.Ground = ALG_SBDEPDOC.Ground + InputPanel.FIO;
      end;
      ALG_SBDEPDOC.Ground = Trim(ALG_SBDEPDOC.Ground);
      ALG_UPDATE = 4;
    end;
  end;

end;
