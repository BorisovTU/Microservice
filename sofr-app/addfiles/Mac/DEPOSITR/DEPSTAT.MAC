/**********************************************************************
*                                                                     *
*  Статистика по вкладам                                              *
*                                                                     *
*                                                                     *
*  История файла                                                      *
*     - Лебедев С.В.   08.01.98   - создание                          *
*     - Палий Д.А.     23.02.12   - переделан на RSD                  *
**********************************************************************/

import deprintr,rsd;

var sb_depst; /* Статистика по вкладам */
var sb_dtyp;  /* Справочник вкладов    */
var currency; /* Справочник валют      */

var FlagCur = NumFlagCur();
var Mode = GetCurrentMode;

var NeedAllDepType  = true;    /* Статистику выпускать только за текущую дату или по всем вкладам */
var NeedWrkDepType  = true;    /* Статистику выпускать только за по вкладам по вкладам по которым было движение или есть остатки*/

var SumColAccounts  = 0;
var SumColOpenedAcc = 0;
var SumColClosedAcc = 0;
var SumRest         = $0;
var SumKredSum      = $0;
var SumDebSum       = $0;
var SumColOper      = 0;

array Rezidentnost;
  Rezidentnost(0) = 0;
  Rezidentnost(1) = 1;

/*Получаем номер балансового счёта 2-го порядка по виду вклада*/
macro GetDepBalAcc(kind,iscur,isrez)
      var q, cmd, rep;
          q= "select b.t_balacc2,b.t_balacc2notresid from dsbnumgrp_dbt b "+
             "  where b.t_groupid in (select g.t_groupid "+
             "           from dsbtypgrp_dbt g "+
             "          where g.t_kind = ? "+
             "            and g.t_flagcur = ?)";

     cmd = rsdCommand(q);
     cmd.addParam( "", RSDBP_IN, kind);
     cmd.addParam( "", RSDBP_IN, iscur);
     cmd.Execute();
     rep = rsdRecordSet(cmd);

     If(rep.MoveNext)	
        If(isrez!=0)	
            return rep.Value(1)
        else
            return rep.Value(0)
        end;
     end;
       return "Не определился!!!";

end;

/*******************************************************************
                           Поиск валюты
*******************************************************************/
macro FindCurrency
(
  CodeCur
)
 var cmd;

 cmd=RSDCommand(string("select t_Code_Currency, t_Short_Name, t_Name_Currency from dcurrency_dbt where t_Code_Currency=:pCur "));
 cmd.AddParam(":pCur", RSDBP_IN, CodeCur);
 currency=RSDRecordset(cmd);    
 return currency.MoveNext();

end;

/*******************************************************************
                     Начало-Окончание отчета
*******************************************************************/
macro BeginEndReport

  [ ----- #################### ---------------- ########## ----- ######## -------] 
  (GetRetailVersion(),Date(),Time());

end;


/*******************************************************************
                           Шапка отчета
*******************************************************************/
macro TopReport
(
  Branch,
  CodeCurrency,
  DateReport,
  NeedFindCur
)

  var str;
  var stat = TRUE;
  var name_branch;

  if (not findDepartment(Branch, name_branch))
    name_branch = String(Branch);
  end;

  str = "";
  [ #](str);
  str = "Статистика по вкладам по подразделению " + name_branch + " за " + String(DateReport);
  [           #](str);
  str = "";
  [ #](str);
  if (NeedFindCur)
    stat = FindCurrency(CodeCurrency);
  end;
  if (stat)
    str = "Валюта: " + currency.value("t_Short_Name") + " - " + currency.value("t_Name_Currency");
    [  #](str);
    str = "";
    [ #](str);
  end;
  str = "╓───────────────────╥────────────────┬────────────────┬──────────────────╥─────────────────╥──────╖";
  [ #](str);
  str = "║                   ║ Кредитовые     │ Дебетовые      │ Остаток на       ║Количество счетов║Кол.  ║";
  [ #](str);
  str = "║ Вид вклада        ║ обороты дня    │ обороты дня    │ конец дня        ╟─────┬─────┬─────╢опер. ║";
  [ #](str);
  str = "║                   ║                │                │                  ║всего│откр.│закр.║      ║";
  [ #](str);
  str = "╟───────────────────╫────────────────┼────────────────┼──────────────────╫─────┼─────┼─────╫──────╢";
  [ #](str);

end;


/*******************************************************************
                         Окончание отчета
*******************************************************************/
macro EndReport

  [ ╙───────────────────╨────────────────┴────────────────┴──────────────────╨─────┴─────┴─────╨──────╜];
  [ ];
  [  Итого               ################ ################ ################## ##### ##### ##### ######]
  (SumKredSum:16:2:a,SumDebSum:16:2:a,SumRest:18:2:a,
   SumColAccounts:5,SumColOpenedAcc:5,SumColClosedAcc:5,SumColOper:5);
  [ ];

end;


/*******************************************************************
                         Строка отчета
*******************************************************************/
macro WriteString (KredSum,DebSum)

  var name_kind = sb_depst.value("t_Kind");

  if (NeedWrkDepType and (KredSum==0) and (DebSum==0) and (sb_depst.value("t_Rest")==0))
    return;
  end;

  if (sb_depst.value("t_FlagRez") != StrFor(0))
    name_kind = name_kind + " (н/р)";
  end;

  [ ║###################║################│################│##################║#####│#####│#####║######║]
  (name_kind:w,KredSum:16:2:a,DebSum:16:2:a,sb_depst.value("t_Rest"):18:2:a,
   sb_depst.value("t_ColAccounts"):5,sb_depst.value("t_ColOpenedAcc"):5,sb_depst.value("t_ColClosedAcc"):5,
   sb_depst.value("t_ColOper"):6);

  /* Подсчет сумм */
  SumColAccounts  = SumColAccounts  + sb_depst.value("t_ColAccounts");
  SumColOpenedAcc = SumColOpenedAcc + sb_depst.value("t_ColOpenedAcc");
  SumColClosedAcc = SumColClosedAcc + sb_depst.value("t_ColClosedAcc");
  SumRest         = SumRest         + sb_depst.value("t_Rest");
  SumColOper      = SumColOper      + sb_depst.value("t_ColOper");
  SumKredSum      = SumKredSum      + KredSum;
  SumDebSum       = SumDebSum       + DebSum;

end;


/*******************************************************************
                  Цикл по файлу статистики вкладов
*******************************************************************/
macro DoReport
(
  Branch,
  CodeCurrency,
  DateReport
)

  var i, cmd, stat;

  /* Цикл по видам вкладов */
  cmd=RSDCommand(string("select t_Kind from dsb_dtyp_dbt where t_FlagCur=:pFlagCur order by t_priorit, t_kind"));
  cmd.AddParam(":pFlagCur", RSDBP_IN, FlagCur);
  sb_dtyp=RSDRecordset(cmd);    

  while (sb_dtyp.MoveNext())
    Message("Обрабатывается вклад ",sb_dtyp.value("t_Kind")," по валюте ",CodeCurrency," ...");

    /* Поиск записей в файле статистики для вида вклада */
    i = 0;
    while (i < ASize(Rezidentnost))
      cmd=RSDCommand(string(
      " select *                                      ",
      "   from (SELECT *                              ",
      "           FROM dsb_depst_dbt                  ",
      "          WHERE t_fnCash  = :pFNCash           ",
      "            AND t_isCur   = :pFlagCur          ",
      "            AND t_kind    = :pPKind            ",
      "            AND t_codCur  = :pCurCode          ",
      "            AND t_flagRez = chr(:pFlagRez)     ",
      "            AND t_balAcc  = :pBalAcc           ",
      "            AND t_mode    = :pMode             ",
      "            AND t_date   <= :pDate             ",
      "          ORDER BY t_date DESC, t_mode DESC)   ",
      "  WHERE ROWNUM = 1                             "
      ));
      cmd.AddParam(":pFNCash",  RSDBP_IN, Branch);
      cmd.AddParam(":pFlagCur", RSDBP_IN, FlagCur);
      cmd.AddParam(":pPKind",   RSDBP_IN, sb_dtyp.value("t_Kind"));
      cmd.AddParam(":pCurCode", RSDBP_IN, CodeCurrency);
      cmd.AddParam(":pFlagRez", RSDBP_IN, Rezidentnost(i));
      cmd.AddParam(":pBalAcc",  RSDBP_IN, GetDepBalAcc(sb_dtyp.value("t_Kind"),FlagCur,Rezidentnost(i)));
      cmd.AddParam(":pMode",    RSDBP_IN, Mode);
      cmd.AddParam(":pDate",    RSDBP_IN, DateReport);
      sb_depst=RSDRecordset(cmd);    
      
      if (sb_depst.MoveNext())
        if (( sb_depst.value("t_Date") == DateReport ) and ( sb_depst.value("t_Mode") == Mode ) )
          WriteString(sb_depst.value("t_KredSum"),sb_depst.value("t_DebSum"));
        elif (NeedAllDepType)
          WriteString($0.0L,$0.0L,0);
        end;
      end;
      i = i + 1;
    end;

  end;

end;


/*******************************************************************
                Г Л А В Н А Я   П Р О Г Р А М М А
*******************************************************************/
macro DepStat   /* !!! Имя этой макро-процедуры и параметры менять нельзя */
(
  Branch,        /* integer */
  CodeCurrency,  /* integer */
  DateReport     /* date    */
)

  var cmd;

  BeginEndReport();
  /* --------------- Валюта задана конкретно --------------- */
  if (CodeCurrency > -1)
    TopReport(Branch,CodeCurrency,DateReport,TRUE);
    DoReport(Branch,CodeCurrency,DateReport);
    EndReport();
  /* ---------- Цикл по всем иностранным валютам ----------- */
  elif (FlagCur)
    cmd=RSDCommand(string("select t_Code_Currency, t_Short_Name, t_Name_Currency from dcurrency_dbt where t_Code_Currency>=:pCur order by t_Code_Currency"));
    cmd.AddParam(":pCur", RSDBP_IN, 1);
    currency=RSDRecordset(cmd);    
    while (currency.MoveNext())
      SumColAccounts  = 0;
      SumColOpenedAcc = 0;
      SumColClosedAcc = 0;
      SumRest         = $0;
      SumKredSum      = $0;
      SumDebSum       = $0;
      SumColOper      = 0;
      TopReport(Branch,currency.value("t_Code_Currency"),DateReport,FALSE);
      DoReport(Branch,currency.value("t_Code_Currency"),DateReport);
      EndReport();
      [ ];
      [ ];
    end;
  /* ------------ Отчет по национальной валюте ------------- */
  else
    TopReport(Branch,0,DateReport,TRUE);
    DoReport(Branch,0,DateReport);
    EndReport();
  end;

  BeginEndReport();

end;
