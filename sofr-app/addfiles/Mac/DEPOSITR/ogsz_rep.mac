/* Отчеты по ОГСЗ */

import ci_defs, acc_form;
            
class ( TCIRecGenerator ) TOGSZRecGenerator( _oper, _makeReport, _observer )

  var
    ogszDocInfo = TRecHandler( "ci_doc.1" ),
    ogszRecInfo = TRecHandler( "ci_rec.1");
  
  var
    retiredSum,
    warrTotSum,
    totNumItems,
    totWarrYieldSum,
    totBoughtWarrYieldSum,
    totSoldItems,
    totBoughtItems,
    totCommissionSum,
    totSum,
    totTaxSum,
    totRetiredSum,
    totSoldSum,
    totBoughtSum,
    totWarrTotSum;
  
  var
    wySumArr = TArray,
    totWySumArr = TArray;
  
  
  macro f5_outOperHead;
  
    [
                                                                                 Филиал №
                                                                                 отделения
                                                                                 Сбербанка РФ
  
  
                                      Отчет по операциям по ОГСЗ за ##########
  
    ]
    (
      {curdate}
    );
  end;
  
  macro f5_outSellHead;
  
    [ ];
    [Продажа облигаций];
    [ ];
    [+------+------------------+------+------------------+------------------+------------------+------------------+];
    [|Серия |     Номинал      | Кол. |      Н К Д       |Курсовая надбавка |     Получено     |   В т.ч. налог   |];
    [+------+------------------+------+------------------+------------------+------------------+------------------+];
  end;
  
  macro getNumItems
  
    var
      RecFound,
      NumItems=0;
  
    capIssueDoc.rec.RecApplicKind=capIssueRec.rec.ApplicationKind;
    capIssueDoc.rec.RecApplicKey=capIssueRec.rec.ApplicationKey;
  
    recFound=capIssueDoc.getEQ;
    while(recFound
      and (capIssueDoc.rec.RecApplicKind==capIssueRec.rec.ApplicationKind)
      and (capIssueDoc.rec.RecApplicKey==capIssueRec.rec.ApplicationKey))
      if ( checkCIDoc )
        ogszDocInfo.setRecordAddr(capIssueDoc,0,
          capIssueDoc.fldOffset("Info"),true);
        numItems=numItems+ogszDocInfo.rec.NumItems;
      end;
      recFound=capIssueDoc.next;
    end;
    return numItems;
  end;
  
  macro f5_outSellLine;
  
    ogszRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);
    [| #### | ################ | #### | ################ | ################ | ################ | ################ |]
    (
      ogszRecInfo.rec.Series,
      ogszRecInfo.rec.Par,
      getNumItems,
      ogszRecInfo.rec.WarrYieldSum,
      ogszRecInfo.rec.CommissionSum,
      capIssueRec.rec.Sum,
      ogszRecInfo.rec.TaxSum
    );
  end;
  
  macro f5_outBuyHead;
  
    [ ];
    [Покупка облигаций];
    [ ];
    [+------+------------------+------+------------------+------------------+------------------+];
    [|Серия |     Номинал      | Кол. |      Н К Д       |Курсовая надбавка |     Получено     |];
    [+------+------------------+------+------------------+------------------+------------------+];
  end;
  
  macro f5_outBuyLine;
  
    ogszRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);
    [| #### | ################ | #### | ################ | ################ | ################ |]
    (
      ogszRecInfo.rec.Series,
      ogszRecInfo.rec.Par,
      getNumItems,
      ogszRecInfo.rec.WarrYieldSum,
      ogszRecInfo.rec.CommissionSum,
      capIssueRec.rec.Sum
    );
  end;
  
  macro f5_outSellTotals;
  
    ogszRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);
    [+-------------------------+------+------------------+------------------+------------------+------------------+];
    [| И т о г о :             | #### | ################ | ################ | ################ | ################ |]
    (
      totNumItems,
      totWarrYieldSum,
      totCommissionSum,
      totSum,
      totTaxSum
    );
  end;
  
  macro f5_outBuyTotals;
  
    ogszRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);
    [+-------------------------+------+------------------+------------------+------------------+];
    [| И т о г о :             | #### | ################ | ################ | ################ |]
    (
      totNumItems,
      totWarrYieldSum,
      totCommissionSum,
      totSum
    );
  end;
  
  macro f5_outPayWarrHead;
  
    [ ];
    [Оплата купонов и погашение облигаций];
    [ ];
    [+------+------------------+----------------------------------------------------------------------------------------------+------------------+------------------+];
    [|      |                  |                   С у м м а ,  в ы п л а ч е н н а я   п о   к у п о н а м                   | Выплачено по     |                  |];
    [|Серия |     Номинал      |------------------+------------------+------------------+------------------+------------------| погашенным       | Итого выплачено  |];
    [|      |                  |      1 - м у     |      2 - м у     |      3 - м у     |      4 - м у     |    В с е г о     | облигациям       |                  |];
    [+------+------------------+------------------+------------------+------------------+------------------+------------------+------------------+------------------+];
  end;
  
  macro getOGSZIssue;
  
    ogszIssue.rec.Type=CI_OGSZ;
    ogszIssue.rec.IssueID=capIssueRec.rec.Issue;
    if(not ogszIssue.getEQ)
      MsgBox("Выпуск ОГСЗ не найден: #"+string(capIssueRec.rec.Issue));
    end;
  end;
  
  macro getWarrantYieldSums;
  
    var
      recFound,
      i=1;
  
    while(i<=4)
      wySumArr[i]=$0;
      i=i+1;
    end;
    retiredSum=$0;
    warrTotSum=$0;
  
    getOGSZIssue;
    capIssueDoc.rec.RecApplicKind=capIssueRec.rec.ApplicationKind;
    capIssueDoc.rec.RecApplicKey=capIssueRec.rec.ApplicationKey;
  
    recFound=capIssueDoc.getEQ;
    while(recFound
      and (capIssueDoc.rec.RecApplicKind==capIssueRec.rec.ApplicationKind)
      and (capIssueDoc.rec.RecApplicKey==capIssueRec.rec.ApplicationKey))
      if ( checkCIDoc )
        ogszDocInfo.setRecordAddr(capIssueDoc,0,
          capIssueDoc.fldOffset("Info"),true);
        if(ogszDocInfo.rec.WarrantNumber!=ogszIssue.rec.NumWarrants)
           wySumArr[ogszDocInfo.rec.WarrantNumber]=
             wySumArr[ogszDocInfo.rec.WarrantNumber]+capIssueDoc.rec.Sum;
        else
           wySumArr[ogszDocInfo.rec.WarrantNumber]=
             wySumArr[ogszDocInfo.rec.WarrantNumber]+capIssueDoc.rec.Sum-
             ogszIssue.rec.Par*ogszDocInfo.rec.NumItems;
           retiredSum=retiredSum+ogszIssue.rec.Par*ogszDocInfo.rec.NumItems;
        end;
      end;
      recFound=capIssueDoc.next;
    end;
  
    i=1;
    while(i<=4)
      warrTotSum=warrTotSum+wySumArr(i);
      i=i+1;
    end;
  end;
  
  macro f5_outPayWarrLine
  
    ogszRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    [| #### | ################ | ################ | ################ | ################ | ################ | ################ | ################ | ################ |]
    (
      ogszRecInfo.rec.Series,
      ogszRecInfo.rec.Par,
      wySumArr[1],
      WYSumArr[2],
      WYSumArr[3],
      WYSumArr[4],
      warrTotSum,
      retiredSum,
      capIssueRec.rec.Sum
    );
  end;
  
  macro f5_outPayWarrTotals
  
    ogszRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);
    [+------+------------------+------------------+------------------+------------------+------------------+------------------+------------------+------------------+];
    [| И т о г о :             | ################ | ################ | ################ | ################ | ################ | ################ | ################ |]
    (
      totWYSumArr[1],
      totWYSumArr[2],
      totWYSumArr[3],
      totWYSumArr[4],
      totWarrTotSum,
      totRetiredSum,
      totSum
    );
  end;
  
  macro f5_sellReport
  
    var
      recFound;
  
    totNumItems=0;
    totWarrYieldSum=$0;
    totCommissionSum=$0;
    totSum=$0;
    totTaxSum=$0;
  
    ogszRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    capIssueRec.clear;
    capIssueRec.rec.Branch=branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_OGSZ;
  
    f5_outSellHead;
    recFound=capIssueRec.getEQ;
    while(recFound
      and (capIssueRec.rec.Branch==branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_OGSZ))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Brigade==operBrigade)
        and (capIssueRec.rec.Operation==O_OGSZ_SELL))
        f5_outSellLine;
        totNumItems=totNumItems+getNumItems;
        totWarrYieldSum=totWarrYieldSum+ogszRecInfo.rec.WarrYieldSum;
        totCommissionSum=totCommissionSum+ogszRecInfo.rec.CommissionSum;
        totSum=totSum+capIssueRec.rec.Sum;
        totTaxSum=totTaxSum+ogszRecInfo.rec.TaxSum;
      end;
      recFound=capIssueRec.next;
    end;
    f5_outSellTotals;
  end;
  
  macro f5_buyReport
  
    var
      recFound;
  
    totNumItems=0;
    totWarrYieldSum=0;
    totCommissionSum=0;
    totSum=0;
    totTaxSum=0;
  
    ogszRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    capIssueRec.clear;
    capIssueRec.rec.Branch=branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_OGSZ;
  
    f5_outBuyHead;
    recFound=capIssueRec.getGE;
    while(recFound
      and (capIssueRec.rec.Branch==branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_OGSZ))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Brigade==operBrigade)
        and (capIssueRec.rec.Operation==O_OGSZ_BUY))
        f5_outBuyLine;
        totNumItems=totNumItems+getNumItems;
        totWarrYieldSum=totWarrYieldSum+ogszRecInfo.rec.WarrYieldSum;
        totCommissionSum=totCommissionSum+ogszRecInfo.rec.CommissionSum;
        totSum=totSum+capIssueRec.rec.Sum;
        totTaxSum=totTaxSum+ogszRecInfo.rec.TaxSum;
      end;
      recFound=capIssueRec.next;
    end;
    f5_outBuyTotals;
  end;
  
  macro f5_payWarrReport
  
    var
      recFound,
      i=1;
  
    while(i<=4)
      totWySumArr(i)=$0;
      i=i+1;
    end;
    totRetiredSum=$0;
    totWarrTotSum=$0;
    totSum=$0;
  
    ogszRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    capIssueRec.clear;
    capIssueRec.rec.Branch=branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_OGSZ;
  
    f5_outPayWarrHead;
    recFound=capIssueRec.getGE;
    while(recFound
      and (capIssueRec.rec.Branch==branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_OGSZ))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Brigade==operBrigade)
        and (capIssueRec.rec.Operation==O_OGSZ_PAYWARR))
        getWarrantYieldSums;
        f5_outPayWarrLine;
        i=1;
        while(i<=4)
          totWySumArr[i]=totWySumArr[i]+wySumArr[i];
          i=i+1;
        end;
        totRetiredSum=totRetiredSum+retiredSum;
        totWarrTotSum=totWarrTotSum+warrTotSum;
        totSum=totSum+capIssueRec.rec.Sum;
      end;
      recFound=capIssueRec.next;
    end;
    f5_outPayWarrTotals;
  end;
  
  macro f5_report;
  
    if ( not makeReport )
      return;
    end;

    f5_outOperHead;
    f5_sellReport;
    f5_buyReport;
    f5_payWarrReport;
  end;
  
  macro outSellBuyRecHead;
  
    var
      branchStrNum = "",
      departNum = "",
      departName = "";
  
    if ( not makeReport )
      return;
    end;

    getBranchParams( branch, branchStrNum, departNum, departName );
    ogszRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);
    [                                                                                                                                   Ф. №201
       ##################################
                                                                                                                                 Филиал №  #########
       Сберегательного банка № ##########
  
       
       
                                                                 В Е Д О М О С Т Ь
       
                                                  на покупку и продажу облигаций государственного
                                               сберегательного займа Российской Федерации #### серии
                                                                  за ##########
       
  
       Смена № #####
  
    ]
    (
      departName,
      branchStrNum,
      departNum,
      ogszRecInfo.rec.Series,
      {curdate},
      operBrigade
    );
    [+--------+--------------------------------------------------------+-------------------------------------------------------+-------------------+];
    [|Номерной|                    П р о д а н о                       |                     К у п л е н о                     |   П о д п и с и   |];
    [| талон, |-----------+--------------+-----------------------------|-----------+--------------+----------------------------|---------+---------|];
    [|выданный|Количество |Ном. стоимость| Получ. за продан. облигации |Количество |Ном. стоимость| Выплач. за купл. облигации |         |         |];
    [|клиенту |-----+-----|  проданных   |---------------+-------------|-----+-----|  купленных   |--------------+-------------|контролер| кассир  |];
    [|        | шт  |д/шт |  облигаций   |      Всего    | В т.ч. НКД  | шт  |д/шт |  облигаций   |     Всего    | В т.ч  НКД  |         |         |];
    [+--------+-----+-----+--------------+---------------+-------------+-----+-----+--------------+--------------+-------------+---------+---------+];
  end;                                                                                                                                              
                                                                                                                                                    
  macro outSellBuyRecLine;                                                                                                                          
                                                                                                                                                    
    var                                                                                                                                             
      soldSum=$0,                                                                                                                                   
      boughtSum=$0,                                                                                                             
      soldParSum=$0,
      boughtParSum=$0,
      soldWarrYield=$0,
      boughtWarrYield=$0,
      soldItems=0,
      boughtItems=0;
  
    ogszDocInfo.setRecordAddr(capIssueDoc,0,
      capIssueDoc.fldOffset("Info"),true);

    if(capIssueRec.rec.Operation==O_OGSZ_SELL)
      soldSum=capIssueDoc.rec.Sum;
      soldParSum=ogszDocInfo.rec.Par*ogszDocInfo.rec.NumItems;
      soldWarrYield=ogszDocInfo.rec.WarrantYield;
      soldItems = ogszDocInfo.rec.NumItems;
    elif(capIssueRec.rec.Operation==O_OGSZ_BUY)
      boughtSum=capIssueDoc.rec.Sum;
      boughtParSum=ogszDocInfo.rec.Par*ogszDocInfo.rec.NumItems;
      boughtWarrYield=ogszDocInfo.rec.WarrantYield;
      boughtItems = ogszDocInfo.rec.NumItems;
    end;
  
    totSoldItems = totSoldItems + soldItems;
    totSoldSum=totSoldSum+soldSum;
    totWarrYieldSum=totWarrYieldSum+soldWarrYield;
    totBoughtItems = totBoughtItems + boughtItems;
    totBoughtWarrYieldSum=totBoughtWarrYieldSum+boughtWarrYield;
    totBoughtSum=totBoughtSum+boughtSum;

    updateValCounter( capIssueDoc.rec.ApplicationKind, capIssueDoc.rec.ApplicationKey );

    if ( not makeReport )
      return;
    end;

    [|########|#####|#####|##############|###############|#############|#####|#####|##############|##############|#############|         |         |]
    (
      capIssueDoc.rec.DocNumber,
      soldItems:z,
      soldItems:z,
      soldParSum:z,
      soldSum:z,
      soldWarrYield:z,
      boughtItems:z,
      boughtItems:z,
      boughtParSum:z,
      boughtSum:z,
      boughtWarrYield:z
    );
  end;
  
  macro outSellBuyRecTotals
  
    var
      opName, 
      sum, 
      sellCredAcc = "",
      sellDebAcc = "",
      buyCredAcc = "",
      buyDebAcc = "";
  
    ogszRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    if ( capIssueRec.rec.Operation == O_OGSZ_SELL )
      opName = "Продажа";  
      sum = totSoldSum;
    else
      opName = "Покупка";
      sum = totBoughtSum;
    end;  

    notifyObserver( CI_OGSZ, capIssueRec.rec.Issue, capIssueDoc.rec.Operation, opName + ": ОГСЗ серия " + 
                    ogszRecInfo.rec.Series + ", номинал " + ogszRecInfo.rec.Par, sum );
  
    if ( not makeReport )
      return;
    end;

    [+--------+-----+-----+--------------+---------------+-------------+-----+-----+--------------+--------------+-------------+---------+---------+];
    [| Итого: |#####|#####|              |###############|#############|#####|#####|              |##############|#############|         |         |]
    (
      totSoldItems,
      totSoldItems,
      totSoldSum,
      totWarrYieldSum,
      totBoughtItems,
      totBoughtItems,
      totBoughtSum,
      totBoughtWarrYieldSum
    );
    [ ];
    [                                                    Д-т сч. ];
    [                                                    ########################## ]
    (Acc_Form(buyDebAcc));
    [ ];
    [                                 Д-т сч. ];
    [                                 ########################## ]
    (Acc_Form(sellDebAcc));
    [ ];
    [             Расход по сч.                          К-т сч. ];
    [             ___________________                    ########################## ]
    (Acc_Form(buyCredAcc));
    [ ];
    [                                 К-т сч. ];
    [                                 ########################## ]
    (Acc_Form(sellCredAcc));
    [ ];
    [                                                    Приход по сч.];
    [                                                    _________________ ];
  end;
  
  macro outPayWarrRecHead;
  
    var
      branchStrNum = "",
      departNum = "",
      departName = "",
      debAcc = "",
      credAcc = "";
  
    if ( not makeReport )
      return;
    end;

    ogszRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    getBranchParams( branch, branchStrNum, departNum, departName );
    [  Сбербанк России                                                                                                                                    Ф. № 202
       ##################################
       Сберегательного банка № ##########
       Филиал № ##########
  
  
                                                   КОНТРОЛЬНАЯ  ВЕДОМОСТЬ                                                           Отметки бухгалтерии
  
                                      на оплату купонов и погашение облигаций государственного                                      Дебет  сч. ######################
                                        сберегательного займа Российской Федерации #### серии                                       Кредит сч. ######################
                                                       за ##########                                                                Дебет  сч. ______________________
                                                                                                                                    Кредит сч. ______________________
  
  
      Смена № #####
  
    ]
    (
      departName,
      departNum,
      branchStrNum,
      Acc_Form(debAcc),
      ogszRecInfo.rec.Series,
      Acc_Form(credAcc),
      {curdate},
      operBrigade
    );
    [+-----------+------------------+--------------------------------------------------------------------------------------------+------------------+-----------------+];
    [| Номерной  |                  |                  С у м м а ,  в ы п л а ч е н н а я   п о   к у п о н а м                  |      Сумма,      |                 |];
    [|  талон,   |   Номинальная    |                                                                                            |    выплаченная   |      Итого      |];
    [| выданный  |                  |------------------+-----------------+------------------+-----------------+------------------|   по погашенным  |                 |];
    [| клиенту   |    стоимость     |     1 - м у      |     2 - м у     |     3 - м у      |    4 - м у      |    В с е г о     |    облигациям    |    выплачено    |];
    [|           |                  |                  |                 |                  |                 |                  |                  |                 |];
    [+-----------+------------------+------------------+-----------------+------------------+-----------------+------------------+------------------+-----------------+];
  end;
  
  macro outPayWarrRecLine;
  
    var
      i=1;
  
    while(i<=4)
      wySumArr(i)=$0;
      i=i+1;
    end;
    retiredSum=$0;
  
    ogszDocInfo.setRecordAddr(capIssueDoc,0,
      capIssueDoc.fldOffset("Info"),true);
    getOGSZIssue;

    if(ogszDocInfo.rec.WarrantNumber!=ogszIssue.rec.NumWarrants)
      wySumArr[ogszDocInfo.rec.WarrantNumber]=capIssueDoc.rec.Sum;
    else
      wySumArr[ogszDocInfo.rec.WarrantNumber]=capIssueDoc.rec.Sum-
        ogszIssue.rec.Par*ogszDocInfo.rec.NumItems;
      retiredSum=ogszIssue.rec.Par*ogszDocInfo.rec.NumItems;
    end;
    warrTotSum=wySumArr[ogszDocInfo.rec.WarrantNumber];

    totWySumArr[1]=totWySumArr[1]+WySumArr[1];
    totWySumArr[2]=totWySumArr[2]+wySumArr[2];
    totWySumArr[3]=totWySumArr[3]+wySumArr[3];
    totWySumArr[4]=totWySumArr[4]+wySumArr[4];
    totNumItems = totNumItems + ogszDocInfo.rec.NumItems;
    totWarrTotSum=totWarrTotSum+warrTotSum;
    totRetiredSum=totRetiredSum+retiredSum;
    totSum=totSum+capIssueDoc.rec.Sum;

    updateValCounter( capIssueDoc.rec.ApplicationKind, capIssueDoc.rec.ApplicationKey );

    if ( not makeReport )
      return;
    end;

    [|   ####    | ################ | ################ | ############### | ################ | ############### | ################ | ################ | ############### |]
    (
      capIssueDoc.rec.DocNumber,
      ogszDocInfo.rec.Par*ogszDocInfo.rec.NumItems,
      wySumArr[1],
      wySumArr[2],
      wySumArr[3],
      wySumArr[4],
      warrTotSum,
      retiredSum,
      capIssueDoc.rec.Sum
    );
  end;
  
  macro outPayWarrRecTotals
  
    ogszRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    notifyObserver( CI_OGSZ, capIssueRec.rec.Issue, O_OGSZ_PAYWARR, "Оплата купонов: ОГСЗ серия " + 
                    ogszRecInfo.rec.Series + ", номинал " + ogszRecInfo.rec.Par, totSum );
  
    if ( not makeReport )
      return;
    end;

    [+-----------+------------------+------------------+-----------------+------------------+-----------------+------------------+------------------+-----------------+];
    [| Итого:    |                  | ################ | ############### | ################ | ############### | ################ | ################ | ############### |]
    (
      totWySumArr[1],
      totWySumArr[2],
      totWySumArr[3],
      totWySumArr[4],
      totWarrTotSum,
      totRetiredSum,
      totSum
    );
    [
  
    Сумма прописью: #
    ] ( rubToStr( totSum ) );
  end;
  
  macro outSellBuyRecTail;
  
    if ( not makeReport )
      return;
    end;

    [
  
  
  
                                                                                 Контролер _____________        Кассир____________
  
                                                                                                         Ст. бухгалтер ___________
  
                                                                                                 Работник депозитария ____________
    ];
    printLn( strFor( 12 ) );
  end;
  
  macro outPayWarrRecTail
  
    if ( not makeReport )
      return;
    end;

    [ ];
    [ ];
    [Контролер (оператор) ________________________________     Кассир ______________________________ ];
    [ ];
    [                     Отметки бухгалтерии ];
    [                     Дебет счета ____________________     Кредит счета ____________________ ];                     
    [                     Дебет счета ____________________     Кредит счета ____________________ ];                     
    [ ];
    [                     Гл. (ст.) бухгалтер _______________________ ];
    [ ];
    [ ];
    printLn( strFor( 12 ) );
  end;
  
  macro ogszSellBuyRecBody;
  
    var
      recFound;
  
    capIssueDoc.rec.RecApplicKind=capIssueRec.rec.ApplicationKind;
    capIssueDoc.rec.RecApplicKey=capIssueRec.rec.ApplicationKey;
  
    recFound=capIssueDoc.getEQ;
    while(recFound
      and (capIssueDoc.rec.RecApplicKind==capIssueRec.rec.ApplicationKind)
      and (capIssueDoc.rec.RecApplicKey==capIssueRec.rec.ApplicationKey))
      if ( checkCIDoc ) 
        outSellBuyRecLine;
      end;
      recFound=capIssueDoc.next;
    end;
  end;
  
  macro ogszSellBuyRec
  
    totSoldSum=$0;
    totBoughtSum=$0;
    totWarrYieldSum=$0;
    totBoughtWarrYieldSum=$0;
    totSoldItems = 0;
    totBoughtItems = 0;
    resetValCounter;

    outSellBuyRecHead;
    ogszSellBuyRecBody;
    outSellBuyRecTotals;
    outSellBuyRecTail;
  end;
  
  macro ogszPayWarrRecBody;
  
    var
      recFound;
  
    capIssueDoc.rec.RecApplicKind=capIssueRec.rec.ApplicationKind;
    capIssueDoc.rec.RecApplicKey=capIssueRec.rec.ApplicationKey;
  
    recFound=capIssueDoc.getEQ;
    while(recFound
      and (capIssueDoc.rec.RecApplicKind==capIssueRec.rec.ApplicationKind)
      and (capIssueDoc.rec.RecApplicKey==capIssueRec.rec.ApplicationKey))
      if ( checkCIDoc )
        outPayWarrRecLine;
      end;
      recFound=capIssueDoc.next;
    end;
  end;
  
  macro ogszPayWarrRec;
  
    totNumItems=0;
    totWySumArr[1]=$0;
    totWySumArr[2]=$0;
    totWySumArr[3]=$0;
    totWySumArr[4]=$0;
    totWarrTotSum=$0;
    totRetiredSum=$0;
    totSum=$0;
    resetValCounter;
    outPayWarrRecHead;
    ogszPayWarrRecBody;
    outPayWarrRecTotals;
    outPayWarrRecTail;
  end;
  
  macro printRec( applicationKind, applicationKey )
        
    if ( not makeReport )
      return;
    end;

    capIssueRec.keyNum = 2;
    capIssueRec.rec.ApplicationKind=applicationKind;
    capIssueRec.rec.ApplicationKey=applicationKey;
    if(not capIssueRec.getEQ)
      msgBox("Ошибка при печати ведомости");
      return;
    end;
    if((capIssueRec.rec.Operation==O_OGSZ_SELL)
      or (capIssueRec.rec.Operation==O_OGSZ_BUY))
      ogszSellBuyRec;
    elif(capIssueRec.rec.Operation==O_OGSZ_PAYWARR)
      ogszPayWarrRec;
    end;
  end;
  
  macro ogszReportPayWarr;
  
    var
      recFound;
  
    capIssueRec.clear;
    capIssueRec.rec.Branch=branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_OGSZ;
  
    recFound=capIssueRec.getGE;
    while(recFound
      and (capIssueRec.rec.Branch==branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_OGSZ))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Oper==oper)
        and (capIssueRec.rec.Brigade==operBrigade))
        if((capIssueRec.rec.Operation==O_OGSZ_SELL) or (capIssueRec.rec.Operation==O_OGSZ_BUY))
          ogszSellBuyRec;
        end;
      end;
      recFound=capIssueRec.next;
    end;
  end;
  
  macro ogszReportSellBuy
  
    var
      recFound;
  
    capIssueRec.clear;
    capIssueRec.rec.Branch=branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_OGSZ;
  
    recFound=capIssueRec.getGE;
    while(recFound
      and (capIssueRec.rec.Branch==branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_OGSZ))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Oper==oper)
        and (capIssueRec.rec.Brigade==operBrigade))
        if(capIssueRec.rec.Operation==O_OGSZ_PAYWARR)
          ogszPayWarrRec;
        end;
      end;
      recFound=capIssueRec.next;
    end;
  end;
  
  macro proceed

    ogszReportSellBuy;
    ogszReportPayWarr;     
  end;    

  macro isOpIncluded( issue, op )
    
    if ( ( op == O_OGSZ_SELL ) or ( op == O_OGSZ_BUY ) or ( op == O_OGSZ_PAYWARR ) )
      return true;
    end;   

    return false;
  end; 

  InitTCIRecGenerator( _oper, _makeReport, _observer );
end;

macro ogszReport

  var
    gen = TOGSZRecGenerator( {oper}, true );

  gen.proceed;
end;

macro ogszRec( applicationKind, applicationKey )

  var
    gen = TOGSZRecGenerator( {oper}, true );

  gen.printRec( applicationKind, applicationKey );
end;

macro ogszOperReport

  var
    gen = TOGSZRecGenerator( {oper}, true );

  gen.f5_report;
end;    

  


