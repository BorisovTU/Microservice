/*

  Печать титульного листа в сберкнижку

*/

 import DeprIntr, ExchangeInter, RSD;

 var clntL = TClientList;

 var ddep = TRecHandler( "i_dp_dep.rec" );
 file curr  ("currency.dbt") key 1;
 file dtyp  ("sb_dtyp.dbt")  key 2;
 file depost("depositr.dbt") key 8;

 var Bank_Standart = GetBankStandart();

 private const SB_2_2005 = 3;
 private const SB_2_c_2005 = 4;

/*************************************************************************
               Получение наименования подразделения.
*************************************************************************/
macro get_Branch_Name()
  var s_Branch_Name = getFilialName();
  return s_Branch_Name;
end;


/* Нужны ли данные по клиенту? */
macro Is_Need_Client(IsCur, Type_Account)
file dt("sb_dtyp.dbt") key 2;
  dt.FlagCur = IsCur;
  dt.Kind    = Type_Account;
  if (GetEQ(dt))
    if (dt.ShowCln == "X")
      return True;
    else
      return False;
    end;
  else
    return False;
  end;
end;

/* Найти запись по клиенту */
macro Find_Client
  if (Is_Need_Client(depost.IsCur, depost.Type_Account))
    if ( NOT clntL.GetRecord( depost.CodClient ) )
      MsgBox("Не найден клиент");
      Exit;
    end;
  end;
end;

/* Найти запись по филиалу */
macro Find_Fdep
  if ( NOT findDepartment(depost.FNCash, null, ddep) )
    MsgBox("Не найдена запись по филиалу");
    Exit;
  end;
end;

/* Найти запись по виду вклада */
macro Find_Dtyp
  dtyp.FlagCur = depost.IsCur;
  dtyp.Kind    = depost.Type_Account;
  if ( NOT getEQ(dtyp) )
    MsgBox("Не найдена запись по виду вклада");
    Exit;
  end;
end;

/* Найти запись по валюте */
macro Find_Curr_1( code )
  KeyNum( curr, 1 );
  curr.ExternalCode = code;
  if ( NOT getEQ(curr) )
    MsgBox("Не найдена запись по валюте");
    Exit;
  end;
end;

macro Find_Curr_0( code )
  KeyNum( curr, 0 );
  curr.Code_Currency = code;
  if ( NOT getEQ(curr) )
    MsgBox("Не найдена запись по валюте");
    Exit;
  end;
end;

/*******************************************************************
                Определение кода национальной валюты
*******************************************************************/
/*   RA 26-03-01 Больше не требуется. Нужно использовать {NationalCur}
macro NatCur()

  var retCur;

  KeyNum( curr, 1 );
  curr.ExternalCode = {NationalCur};
  if ( getEQ( curr ) )
    retCur = curr.Code_Currency;
  else
    retCur = -1;
  end;

  KeyNum( curr, 0 );

  return retCur;

end;
*/

/* Процедура печати титульного листа сбер.книжки */
macro BookTitle( Ref, Conf, Choice, Type)

  var ISO;

  var i, j;

  var familC, ioC;
  var lost_message="";

  var nc = {NationalCur}; /*NatCur();*/

  var sSB = "", NameOpenFncash="", AdressOpenfncash="", cmd, rs;
  var ddep_open = TRecHandler( "i_dp_dep.rec" );
  var party_open = TRecHandler( "i_party.rec" );

  if ( Conf )
    if ( NOT GetTrue( TRUE, "Печатать титульный лист сберкнижки?" ) )
      return;
    end;
  end;

  depost.Referenc = Ref;
  if ( NOT getEQ(depost) )
    MsgBox("Не найден счет с Referenc=",Ref);
    Exit;
  end;

  if ( depost.IsCur == 0 )
    if ( nc == 810 )
      ISO = "Руб";
    else
      if ( nc == 0 )
        Find_Curr_0(nc);
      else
        Find_Curr_1(nc);
      end;
      ISO = curr.Short_Name;
    end;
  else
    Find_Curr_0(depost.Code_Currency);
    ISO = curr.Short_Name;
  end;

  Find_Fdep();
  Find_Dtyp();
  Find_Client();


  if ( StrLen(ddep.rec.Name) > 3 )
    i = StrLen(ddep.rec.Name)-3;
    j = 4;
  else
    i = 1;
    j = StrLen(ddep.rec.Name);
  end;

  if ( depost.CodClient != 0 )  /* AND  ( NOT clntL.CurRec.rec.SpecialAccess ) ) */
    familC = clntL.CurRec.rec.Name1;
    ioC = clntL.CurRec.rec.Name2 + " " + clntL.CurRec.rec.Name3;
  else  /* Особый номерной и т.п. */
    familC = "";
    ioC = "";
  end;

  if ( depost.NumberLostSBBook > 0 )
    lost_message = "Дубликат № " + String( depost.NumberLostSBBook );
  elif (Choice == 0 )
     lost_message = "Пересено с исписанной сберкнижки"
  end;
  if ( Bank_Standart == 0 )  /* Сберегательный банк */
    sSB = " Сбербанка России";
  else
    sSB = "";
  end;

  if (depost.FNCash != NumRealFNCash())
     if (findDepartment(depost.FNCash, null, ddep_open, party_open))     
        NameOpenFncash = "счет откр. в ВСП № " + string(ddep_open.rec.Name);
        AdressOpenfncash = party_open.rec.Address;
     end;
/*GAA*/
  else
     if (findDepartment(depost.FNCash, null, ddep_open, party_open))     
        NameOpenFncash = get_Branch_Name;
        AdressOpenfncash = party_open.rec.Address;
     end;
  end;

  if (Type == SB_2_2005)
  [ 
                                                        #
                                                            #
                                                            #
                           
                                                                            #





                                                            #

                                                        #####################

                                                            #

                                                            #

  ] ( lost_message:c,
      NameOpenFncash:c,
      AdressOpenfncash:c,
/*    SubStr(fdep.NameBranch, i, j):c, */      string(ddep.rec.Name):r,
      Acc_Form(depost.Account):c:f,
      ( dtyp.Name + " (" + ISO + ")" + sSB ):w,
      familC:c,
      ioC:c
      );
  elif (Type == SB_2_c_2005)
  [ 
                                #
                                    #
                                    #
                           
                                                            #





           #

             #

  #

  ] ( lost_message:c,
      NameOpenFncash:c,
      AdressOpenfncash:c,
/*    SubStr(fdep.NameBranch, i, j):c, */      string(ddep.rec.Name):r,
      Acc_Form(depost.Account):l:f,
      ( dtyp.Name + " (" + ISO + ")" + sSB ):l,
      (familC + " " + ioC):l
      );
  else
  [ 
                                                        #
                                                            #
                                                            #
                           
                                                            #





                                                            #

                                                            #

                                                            #

                                            ########### #####################

  ] ( lost_message:c,
      NameOpenFncash:c,
      AdressOpenfncash:c,
/*    SubStr(fdep.NameBranch, i, j):c, */      string(ddep.rec.Name):c,
      Acc_Form(depost.Account):c:f,
      familC:c,
      ioC:c,
      "Вид вклада:",
      ( dtyp.Name + " (" + ISO + ")" + sSB ):w
      );
  end;
  print( "\f" );

end;
