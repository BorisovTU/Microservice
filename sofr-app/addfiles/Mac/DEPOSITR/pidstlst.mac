/********************************************************************\

	Загрузка стоп - листа из файла
	  

	  Программист: Крестьянинов Евгений Анатольевич
	  Дата создания: 13.02.2003

\********************************************************************/

/*     
		Формат файла 
Вида_Удостоверения|Серия_Удостоверения|Номер_Удостоверения|ФИО_Владельца
Например:
Удостоверение личности офицера|AAA|111|Krestyaninov

*/

import deprintr;

var  PatternFile = "..//*.txt";         /* Маска исходного файла */
var  NameInputFile = "";		/* Имя выбранного файла */
file InputFile  () txt;                 /* Исходный текстовый файл с записями  */
file Pid ("pidstlst.dbt") key 0 write;  /* База, куда надо заносить ;) */
var  NameForReport = "";          	/* Название файла без полного пути	*/
file PaprKind("paprkind.dbt") key 0;    /* Виды удостоверений */

var {curdate};

private macro GetCodOfPAPRKIND( name )
var stat,found = false;

  rewind( PaprKind );
  stat = next( PaprKind );
  while(stat and (not found))
    if( PaprKind.Name == name )
      found = true;
    else
      stat = next( PaprKind );
    end;
  end;

  if( found )
    stat = PaprKind.PaperKind;
  else
    stat = -1;
  end;
  return stat;
end;

private macro getFileName
var q = strlen(NameInputFile);

  while(substr(NameInputFile,q,1) != "\\")
    q = q - 1;
  end;
  NameForReport = substr(NameInputFile,q+1);

end;

private macro PrintHeader

  [				Загрузка стоп - листа удостоверений личности:			              ];
  [  Дата:#											              ]
  ({curdate});                                                          	
  [  Имя файла:############					        				      ]
  ( NameForReport );
  [ ];
  [+---------------------------------------------------------------------------------------------------------+];
  [| №  |   Вид документа   |    Серия     |        Номер       |     ФИО владельца    |       Статус        |];
  [+----+-------------------+--------------+--------------------+----------------------+---------------------+];

end;

/************************ Основная программа ************************/

 var stat,NameError = "Ок",Cols = 0,CodOfPaprKind,Series = "",Number = "";
  if( SelectFile( NameInputFile, PatternFile, "Выберите исходный файл" ) )
    if( Open( InputFile,  NameInputFile ) )
      SetDelim("|"); 				/*Разделитель полей*/
      getFileName();
      PrintHeader();
      rewind( InputFile );
      while( next(InputFile) )
	Cols = Cols + 1;
        CodOfPaprKind = GetCodOfPAPRKIND( InputFile(0) );
        Series = InputFile(1);
        Number = InputFile(2);
        if( CodOfPaprKind != -1 )
	  stat = CheckOnAddNewRecord(CodOfPaprKind,Series, Number);
	  if( not stat )
	    NameError = "Запись не прошла проверку";
	  end;
        else
          NameError = "Не найден вид документа либо не верный формат файла";
          stat = false;
        end;
        if( stat )
	  Pid.PaperKind = CodOfPaprKind;
	  Pid.Key = MakeUniKey(CodOfPaprKind,Series,Number);
	  if( getEQ( Pid ) )
	    Pid.DateEntered = {curdate};
	    Pid.PaperKind = CodOfPaprKind;
	    Pid.Series = Series;
	    Pid.Number = Number;
	    Pid.OwnerName = InputFile(3);
	    stat = Update( Pid );
            if( not stat )
              NameError = "Ошибка обновления записи в базе";
            end;
	  else
	    Pid.DateEntered = {curdate};
	    Pid.PaperKind = CodOfPaprKind;
	    Pid.Series = Series;
	    Pid.Number = Number;
	    Pid.OwnerName = InputFile(3);
	    stat = Insert( Pid );
            if( not stat )
              NameError = "Ошибка вставки записи в базу";
            end;
	  end;
        end;
	[|####|###################|##############|####################|######################|#####################|]
	( Cols,
	  InputFile(0) : w,
	  InputFile(1) : w,
	  InputFile(2) : w,
	  InputFile(3) : w,
	  NameError:w:c );
        [+----+-------------------+--------------+--------------------+----------------------+---------------------+];
      end;
    else
      println("Ошибка открытия текстового файла");
    end;
  end;
end;
