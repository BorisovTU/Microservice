/**********************************************************
*  ---------------------                                  *
*  R-Style SoftWare Lab.                                  *
*  ---------------------                                  *
*                                                         *
*  СБЕРБАНК 1.0                                           *
*                                                         *
*  Отчет по структуре вкладов по пасшифровке остатков по  *
*  балансовым счетам                                      *
*                                                         *
*  Лебедев С.В.                                           *
*  13.03.98                                               *
**********************************************************/
import deprintr;

file sb_dtyp  (sb_dtyp);  /* Справочник вкладов  */
file sb_depst (sb_depst); /* Статистика вкладов  */
file currency (currency); /* Справочник валют    */

var {curdate};
var {oper};
var FNCash  = NumFNCash();
var FlagCur = NumFlagCur();
var Mode    = GetCurrentMode();
var NumBranch;
var NameBranch = "";
var DateReport;

var SumSum = $0.0L;

const NationalCodCur = 0; /* Код валюты страны в файле currency.dbt */

array Branches;

array DateLeft;
array DateRight;
array SumForDate;
array NameR;   /* Номера бал.счетов по резидентам */
array NameN;   /* Номера бал.счетов по нерезидентам */

/* Левую границу исключаем, правую включаем */
DateLeft(0) =   -1; DateRight(0) =    0; SumForDate(0) = $0.0L; NameR(0) = "42301";
                                                                NameN(0) = "42601";
DateLeft(1) =    0; DateRight(1) =   30; SumForDate(1) = $0.0L; NameR(1) = "42302";
                                                                NameN(1) = "42602";
DateLeft(2) =   30; DateRight(2) =   90; SumForDate(2) = $0.0L; NameR(2) = "42303";
                                                                NameN(2) = "42603";
DateLeft(3) =   90; DateRight(3) =  180; SumForDate(3) = $0.0L; NameR(3) = "42304";
                                                                NameN(3) = "42604";
DateLeft(4) =  180; DateRight(4) = 1001; SumForDate(4) = $0.0L; NameR(4) = "42305";
                                                                NameN(4) = "42605";
DateLeft(5) = 1001; DateRight(5) = 1003; SumForDate(5) = $0.0L; NameR(5) = "42306";
                                                                NameN(5) = "42606";
DateLeft(6) = 1003; DateRight(6) = 9999; SumForDate(6) = $0.0L; NameR(6) = "42307";
                                                                NameN(6) = "42607";

const  R_DAY   = 1, /* Дней             */
       R_MONTH = 2, /* Месяцев          */
       R_QW    = 3, /* Кварталов        */
       R_YEAR  = 4, /* Лет              */
       R_CDAY  = 5; /* Календарных дней */


/********************************************************************
                           Шапка отчета
********************************************************************/
macro HeadReport

  var day,month,year;
  var d,m,y;
  var str1,str2,str3,str4;
  var i = 0;
  var StrTmp;

  DateSplit(DateReport,d,m,y);
  day = String(d);
  if (StrLen(day) == 1)
    day = "0" + day;
  end;
  if   (m ==  1) month = "января";
  elif (m ==  2) month = "февраля";
  elif (m ==  3) month = "марта";
  elif (m ==  4) month = "апреля";
  elif (m ==  5) month = "мая";
  elif (m ==  6) month = "июня";
  elif (m ==  7) month = "июля";
  elif (m ==  8) month = "августа";
  elif (m ==  9) month = "сентября";
  elif (m == 10) month = "октября";
  elif (m == 11) month = "ноября";
  elif (m == 12) month = "декабря";
  else month = "";
  end;
  year = String(y);

  [ ];
  if (currency.Code_Currency == NationalCodCur)
    [ РАСШИФРОВКА ОСТАТКОВ ОТДЕЛЬНЫХ БАЛАНСОВЫХ СЧЕТОВ ПО РУБЛЕВЫМ ВИДАМ ВКЛАДОВ];
  else
    [  РАСШИФРОВКА ОСТАТКОВ ОТДЕЛЬНЫХ БАЛАНСОВЫХ СЧЕТОВ ПО ВИДАМ ВКЛАДОВ В #](currency.Short_Name);
  end;
  [            Оперчасть ############ на #  #        #    года](NameBranch,day,month,year);
  [ ];

  str1 = "──────────────┬───┬";
  str2 = " Виды вкладов │Рез│";
  str3 = "              │ид.│";
  str4 = "──────────────┼───┼";
  while (i < ASize(NameR))
    StrTmp = "     " + NameR(i);
    while (StrLen(StrTmp) < 16)
      StrTmp = StrTmp + " ";
    end;
    str2 = str2 + StrTmp + "│";
    StrTmp = "     " + NameN(i);
    while (StrLen(StrTmp) < 16)
      StrTmp = StrTmp + " ";
    end;
    str3 = str3 + StrTmp + "│";
    StrTmp = "─";
    while (StrLen(StrTmp) < 16)
      StrTmp = StrTmp + "─";
    end;
    str1 = str1 + StrTmp + "┬";
    str4 = str4 + StrTmp + "┼";
    i = i + 1;
  end;
  str1 = str1 + "────────────────";
  str2 = str2 + "     Итого";
  str4 = str4 + "────────────────";

  [ #](str1);
  [ #](str2);
  [ #](str3);
  [ #](str4);

end;


/********************************************************************
                          Окончание отчета
********************************************************************/
macro BottomReport

  var str1,str2,str3;
  var StrTmp;
  var i = 0;

  str1 = "──────────────┴───┼";
  str2 = "    ИТОГО         │";
  str3 = "──────────────────┴";
  while (i < ASize(SumForDate))
    StrTmp = " " + String(SumForDate(i):14:2:a);
    while (StrLen(StrTmp) < 16)
      StrTmp = StrTmp + " ";
    end;
    str2 = str2 + StrTmp + "│";
    StrTmp = "─";
    while (StrLen(StrTmp) < 16)
      StrTmp = StrTmp + "─";
    end;
    str1 = str1 + StrTmp + "┼";
    str3 = str3 + StrTmp + "┴";
    i = i + 1;
  end;
  str1 = str1 + "────────────────";
  str2 = str2 + " " + String(SumSum:14:2:a);
  str3 = str3 + "────────────────";

  [ #](str1);
  [ #](str2);
  [ #](str3);
  [ ];
  [ ];

end;


/********************************************************************
               Расчет суммы и запись строки в отчет
********************************************************************/
macro CalcSumAndWriteToReport

  var DepTypeDate;
  var j = -1;
  var i = 0;
  var k = 0;
  var RestDTypeR = $0.0L;  /* Остаток по резидентам */
  var RestDTypeN = $0.0L;  /* Остаток по нерезидентам */
  var str;
  var StrTmp;
  array FlagRez;
    FlagRez(0) = "\x00";
    FlagRez(1) = "\x01";

  if (sb_dtyp.Term == 0)
    DepTypeDate = 0;
  elif ((sb_dtyp.KindTerm == R_DAY) OR (sb_dtyp.KindTerm == R_CDAY))
    if (sb_dtyp.Term < 365)
      DepTypeDate = sb_dtyp.Term;
    elif (sb_dtyp.Term == 365)
      DepTypeDate = 1001;
    else
      DepTypeDate = 1000 + sb_dtyp.Term/365.0;
    end;
  elif (sb_dtyp.KindTerm == R_MONTH)
    if (sb_dtyp.Term < 12)
      DepTypeDate = 30 * sb_dtyp.Term;
    elif (sb_dtyp.Term == 12)
      DepTypeDate = 1001;
    else
      DepTypeDate = 1000 + sb_dtyp.Term/12.0;
    end;
  elif (sb_dtyp.KindTerm == R_QW)
    if (sb_dtyp.Term < 4)
      DepTypeDate = 90 * sb_dtyp.Term;
    elif (sb_dtyp.Term == 4)
      DepTypeDate = 1001;
    else
      DepTypeDate = 1000 + sb_dtyp.Term/4.0;
    end;
  elif (sb_dtyp.KindTerm == R_YEAR)
    DepTypeDate = 1000 + sb_dtyp.Term;
  end;

  while (i < ASize(DateLeft))
    if ((DepTypeDate > DateLeft(i)) AND (DepTypeDate <= DateRight(i)))
      j = i;
      i = ASize(DateLeft) + 1; /* Чтобы скорее выйти из цикла */
    end;
    i = i + 1;
  end;

  if (j > -1)
    i = 0;
    while (i < ASize(Branches))
      k = 0;
      while (k < ASize(FlagRez))
        KeyNum(sb_depst,0);
        ReWind(sb_depst);
        sb_depst.FNCash  = Branches(i);
        sb_depst.IsCur   = FlagCur;
        sb_depst.Kind    = sb_dtyp.Kind;
        sb_depst.CodCur  = currency.Code_Currency;
        sb_depst.FlagRez = FlagRez(k);
        sb_depst.Date    = DateReport;
        sb_depst.Mode    = Mode;
        if (GetLE(sb_depst))
          if ((sb_depst.FNCash  == Branches(i)           ) AND
              (sb_depst.IsCur   == FlagCur               ) AND
              (sb_depst.Kind    == sb_dtyp.Kind          ) AND
              (sb_depst.CodCur  == currency.Code_Currency) AND
              (sb_depst.FlagRez == FlagRez(k)            ) AND
              (sb_depst.Date    <= DateReport            )    )
            if ( sb_depst.FlagRez == FlagRez(0) )
              RestDTypeR = RestDTypeR + sb_depst.Rest;
            else
              RestDTypeN = RestDTypeN + sb_depst.Rest;
            end;
          end;
        end;
        k = k + 1;
      end;
      i = i + 1;
    end;
    if (RestDTypeR > 0)
      SumSum = SumSum + RestDTypeR;
      SumForDate(j) = SumForDate(j) + RestDTypeR;

      str = sb_dtyp.Kind;
      while (StrLen(str) < 14)
        str = str + " ";
      end;
      str = str + "│ Р │";
      i = 0;
      while (i < ASize(NameR))
        StrTmp = "";
        if (i == j)
          StrTmp = " " + String(RestDTypeR:14:2:a);
        end;
        while (StrLen(StrTmp) < 16)
          StrTmp = StrTmp + " ";
        end;
        str = str + StrTmp + "│";
        i = i + 1;
      end;
      str = str + " " + String(RestDTypeR:14:2:a);
      [ #](str);
    end;
    if (RestDTypeN > 0)
      SumSum = SumSum + RestDTypeN;
      SumForDate(j) = SumForDate(j) + RestDTypeN;

      str = sb_dtyp.Kind;
      while (StrLen(str) < 14)
        str = str + " ";
      end;
      str = str + "│ Н │";
      i = 0;
      while (i < ASize(NameR))
        StrTmp = "";
        if (i == j)
          StrTmp = " " + String(RestDTypeN:14:2:a);
        end;
        while (StrLen(StrTmp) < 16)
          StrTmp = StrTmp + " ";
        end;
        str = str + StrTmp + "│";
        i = i + 1;
      end;
      str = str + " " + String(RestDTypeN:14:2:a);
      [ #](str);
    end;
  end;

end;


/********************************************************************
                 Цикл по открытым счетам вкладчиков
********************************************************************/
macro DoReport

  var stat;
  var i = 0;

  KeyNum(sb_dtyp,2);
  sb_dtyp.FlagCur = FlagCur;
  sb_dtyp.Kind    = "";
  stat = GetGE(sb_dtyp);
  while (stat)
    if (sb_dtyp.FlagCur == FlagCur)
      if ((sb_dtyp.Kind == "Шаблонный") OR (sb_dtyp.Kind == "Служебный"))
        ;
      else
        CalcSumAndWriteToReport();
      end;
      stat = Next(sb_dtyp);
    else
      stat = FALSE;
    end;
  end;

end;


/********************************************************************
                    Выбор филиала для работы
********************************************************************/
macro ChooseBranchAndDateForWork

  file operdep  (operdep);  /* С какими филиалами может работать операционист */
/*  file listfdep (listfdep);*/ /* Список филиалов                                */

  array ACash,ANameBr,ANameMenu;

  var stat;
  var i        = 0;
  var FullName = "";
  var RetCode  = FALSE;
  var dep_name;
  var dpDepList = TdpDepList;

  ACash    (i) = -1;
  ANameBr  (i) = "";
  ANameMenu(i) = " По всем филиалам";

  KeyNum(operdep,0);
  operdep.Oper   = {oper};
  operdep.FNCash = 0;
  stat = GetGE(operdep);
  while (stat)
    if (operdep.Oper == {oper})
      i = i + 1;
      ACash(i) = operdep.FNCash;
      ANameBr(i) = "Не найден";
      FullName = "";
      if (findDepartment(operdep.FNCash, dep_name))
        ANameBr(i) = dep_name;
        FullName   = ""; /*listfdep.Name;*/
      end;
      ANameMenu(i) = " " + String(ACash(i):2) + "  " + SubStr(ANameBr(i) + MkStr(" ",12),1,12) + "  " + FullName;
      stat = Next(operdep);
    else
      stat = FALSE;
    end;
  end;

  i = Menu(ANameMenu," ~ENTER~ Печать отчета  ~ESC~ Выход","Выберите филиал");
  if (i < 0)
    [ ];
    [ Отказались от выпуска отчета];
  else
    NumBranch  = ACash(i);
    NameBranch = ANameBr(i);
    DateReport = {curdate};
    if (GetDate(DateReport,"Введите дату отчета"))
      RetCode = TRUE;
      if (NumBranch > -1)
        Branches(0) = NumBranch;
      else
        dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
        i = 0;
        while (dpDepList.next())
          if (dpDepList.rec.Code == FNCash)
            NameBranch = dpDepList.rec.Name;
          end;
          Branches(i) = dpDepList.rec.Code;
          i = i + 1;
        end;
      end;
    else
      [ ];
      [ Отказались от выпуска отчета];
    end;
  end;
                      
  if ( RetCode )
    if ( DateReport >= {curdate} )
      i = 0;
      while ( i < ASize( Branches ) )
        if ( not UpdateStatistics( false, null, -1, Branches( i ) ) )
          Exit;
        end;
        i = i + 1;
      end;  
    end;
  end;

  return RetCode;

end;


/********************************************************************
                Г Л А В Н А Я   П Р О Г Р А М М А
********************************************************************/

  var i = 0;

  if (ChooseBranchAndDateForWork())
    KeyNum(currency,0);
    ReWind(currency);
    while (Next(currency))
      if (((FlagCur == 0) AND (currency.Code_Currency == NationalCodCur)) OR
          ((FlagCur == 1) AND (currency.Code_Currency != NationalCodCur))   )
        SumSum = $0.0L;
        i = 0;
        while (i < ASize(SumForDate))
          SumForDate(i) = $0.0L;
          i = i + 1;
        end;
        HeadReport();
        DoReport();
        BottomReport();
      end;
    end;
  end;

end;
