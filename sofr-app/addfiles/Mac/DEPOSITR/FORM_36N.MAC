/*
$Name:             form_36n.mac
$Module:           RS-Retail
$Description:      Печать формы 36
*/

import deprintr, commclnt, alg_vars, form_36b;

/****************************************************************/
/*                 Константы для пользователей                  */
/****************************************************************/
const PhoneType = 4;     /* Вид номера телефона для ф.0401026:  */
                         /*  4 - домашний телефон,              */
                         /*  5 - рабочий телефон.               */
const FlagDate = FALSE;  /* Флаг простановки даты операционного */
                         /* дня в поле "Дата заполнения".       */

file namealg  ( namealg  ) key 0;
file opr      ( person   ) key 0;
file sb_casln ( sb_casln ) key 0;
file ret_op   ( ret_op   ) key 0;
file depositr ( depositr ) key 8;
file sb_dtyp  ( sb_dtyp  ) key 2;
file sb_typop ( sb_typop ) key 0;
file rtnfopr  ( rtnfopr  ) key 1;
file aux      ( auxinfo  ) key 0;  /* доп.информ. о вкладе */

var i_dp_dep = TRecHandler( "i_dp_dep.rec" );

var  clnt = TClientList;

record ret_op_dep ( "ret_op.dep" );

private var {Name_Bank};

var Branch = NumFNCash( );

var sFIO_Trast = "";  // Ф.И.О. доверенного лица.

Var sCheckInfo = "";

/**************************************************************************\
|                               Получение телефона                         |
\**************************************************************************/

macro GetPhoneNumberF36N(ClntCode)
  var DepClnt = TClientList;
  var AddrTypes = TArray;
  var i = 0;
  var PhoneNumber = "";
  var PhoneNumber2 = "";
  
  if (DepClnt.GetRecord (clntCode))
    AddrTypes(0) = COMMCLNT_PTADDR_LEGAL;
    AddrTypes(1) = COMMCLNT_PTADDR_REAL;
    AddrTypes(2) = COMMCLNT_PTADDR_POST;

    while ((i < AddrTypes.Size) and (PhoneNumber == ""))
      PhoneNumber = commclnt_GetAddressField(DepClnt, AddrTypes(i), "PhoneNumber");
      PhoneNumber2 = commclnt_GetAddressField(DepClnt, AddrTypes(i), "PhoneNumber2");
      if ((PhoneNumber != "") and (PhoneNumber2 != ""))
        PhoneNumber = PhoneNumber + ", ";
      end;
      PhoneNumber = PhoneNumber + PhoneNumber2;
      i = i + 1;
    end;
  end;
  return PhoneNumber;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindNFOpr

  var numDprt = 0;
  var recID   = 0;

  if ( StrLen( sb_casln.ApplicationKey2 ) >= 12 )
    numDprt = Int( SubStr( sb_casln.ApplicationKey2,  9, 3 ) );
    recID   = Int( SubStr( sb_casln.ApplicationKey2, 12    ) );
  end;

  rtnfopr.numDprt = numDprt;
  rtnfopr.recID   = recID;

  return GetEQ ( rtnfopr );

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetAlg ( type, number )

  namealg.iTypeAlg   = type;
  namealg.iNumberAlg = number;

  return GetEQ ( namealg );

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro InfoForDemoData( )

  record depclnt_old ( "commclnt.rec" );
  record depclnt_new ( "commclnt.rec" );

  var retStr = "";

  if ( FindNFOpr( ) )
    if ( StrLwr( rtnfopr.tableName ) == "commclnt.rec" )
      SetRecordAddr( depclnt_new, rtnfopr, 0, 2, false );
      SetRecordAddr( depclnt_old, rtnfopr, 0, 4 + GetRecordSize( depclnt_new ), false );

      if ( (depclnt_new.Name1 != depclnt_old.Name1) or (depclnt_new.Name2 != depclnt_old.Name2) or (depclnt_new.Name3 != depclnt_old.Name3) )
        retStr = "Изменение ";
        if   (depclnt_new.Name1 != depclnt_old.Name1)
          retStr = retStr + "фамилии ";
        end;
        if   (depclnt_new.Name2 != depclnt_old.Name2)
          retStr = retStr + "имени ";
        end;
        if   (depclnt_new.Name3 != depclnt_old.Name3)
          retStr = retStr + "отчества ";
        end;
      end;

      if ( depclnt_new.Address != depclnt_old.Address )
        if ( StrLen( retStr ) > 0 )
          retStr = retStr + "\n";
        end;
        retStr = "Изменение ";
        if   ( depclnt_new.AdressType == 1 )
          retStr = retStr + "юридического ";
        elif ( depclnt_new.AdressType == 2 )
          retStr = retStr + "фактического ";
        elif ( depclnt_new.AdressType == 3 )
          retStr = retStr + "почтового ";
        end;
        retStr = retStr + "адреса";
      end;

      if ( depclnt_new.INN  != depclnt_old.INN )
        if ( StrLen( retStr ) > 0 )
          retStr = retStr + "\n";
        end;
        retStr = retStr + "Изменение ИНН " + depclnt_new.INN;
      end;

      if ( depclnt_new.Kategor != depclnt_old.Kategor )
        if ( StrLen( retStr ) > 0 )
          retStr = retStr + "\n";
        end;
        retStr = retStr + "Изменение категории населения";
        if ( ( GetAlg( 713, CodeFor( depclnt_new.Kategor ) ) ) and
             ( StrLen( namealg.szNameAlg ) > 0 ) )
          retStr = retStr + ": " + namealg.szNameAlg;
        end;
      end;

      if ( ( depclnt_new.SocialNumber != depclnt_old.SocialNumber ) or
           ( depclnt_new.SocialDate   != depclnt_old.SocialDate  )   )
        if ( StrLen( retStr ) > 0 )
          retStr = retStr + "\n";
        end;
        retStr = retStr + "Изменение данных пенсионного удостоверения";
        retStr = retStr + " № " + depclnt_new.SocialNumber;
        if ( depclnt_new.SocialDate != date( 0, 0, 0 ) )
          retStr = retStr + ", " + String( depclnt_new.SocialDate:m );
        end;
      end;

      if ( ( depclnt_new.PaperKind       != depclnt_old.PaperKind   ) or
           ( depclnt_new.PaperSeries     != depclnt_old.PaperSeries ) or
           ( depclnt_new.PaperNumber     != depclnt_old.PaperNumber ) or
           ( depclnt_new.PaperIssuedDate != depclnt_old.PaperIssuedDate ) or
           ( depclnt_new.PaperIssuer     != depclnt_old.PaperIssuer )   )
        if ( StrLen( retStr ) > 0 )
          retStr = retStr + "\n";
        end;
        retStr = retStr + "Изменение паспортных данных клиента: ";
        retStr = retStr + Trim(depclnt_new.PaperSeries) + " № " +
                          Trim(depclnt_new.PaperNumber) + ", выдан " +
                          Trim(depclnt_new.PaperIssuer) + ", " +
                          depclnt_new.PaperIssuedDate;
      end;

      if ( depclnt_new.NRCountry != depclnt_old.NRCountry )
        if ( StrLen( retStr ) > 0 )
          retStr = retStr + "\n";
        end;
        retStr = retStr + "Изменение гражданства";
      end;
    end;
  end;

  if ( ( StrLen( retStr ) > 0 ) and ( StrLen( depclnt_new.UserField3 ) > 0 ) )
    retStr = retStr + "\n на основании: " + depclnt_new.UserField3;
  end;

  return retStr;

end;

/**************************************************************************\
|                       Получение Ф.И.О. доверенного лица.                 |
\**************************************************************************/
macro FillFIOForTrast()
  record sbtrast_new ( sbtrast );
  if ( FindNFOpr( ) )
    SetRecordAddr( sbtrast_new, rtnfopr, 0, 2, false );
    if ( sbtrast_new.CodClient != 0 )
      if (clnt.GetRecord (sbtrast_new.CodClient))
        sFIO_Trast = clnt.CurRec.ConvertFIOFull();
      end;
    end;
  end;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro Print_nf_36

  Print_nf_36_data(ALG_DEPOSITOR.Account, ALG_DEPOSITOR.CodClient, InfoForDemoData());
  
end;

/**************************************************************************\
|               Проверка вида вклада: "Специальный счет" ?                 |
\**************************************************************************/
macro Check_Special_DepType()

 var ret_val = FALSE;

 ClearRecord( ret_op );
 ret_op.ApplicationKind = ALG_RET_OP.ApplicationKind;
 ret_op.ApplicationKey  = ALG_RET_OP.ApplicationKey;

 if ( GetEQ( ret_op ) )
   SetRecordAddr( ret_op_dep, ret_op, 0, FldOffset( ret_op, "Extra" ), true );
   if ( ret_op_dep.accRef > 0 )

     ClearRecord( depositr );
     depositr.Referenc = ret_op_dep.accRef;

     if ( GetEQ( depositr ) )
       if ( Index( depositr.UserTypeAccount, "Щ" ) > 0 )
         ret_val = TRUE;
       end;
     end;
   end;
 end;

 return ret_val;

end;

/**************************************************************************\
|                       Выпуск карточки формы 0401026                      |
\**************************************************************************/
macro Form_0401026()

 var
   v_FIO      = "",
   v_FIO_Sign = "",
   v_Owner    = "",
   v_Address  = "",
   v_Phone    = "",
   v_BankName = {Name_Bank},
   v_Account  = ALG_DEPOSITOR.Account,
   v_DateSet  = "";

   if ( clnt.GetRecord( depositr.CodClient ) )
     v_FIO   = clnt.CurRec.ConvertFIOFull();
     v_Owner = v_FIO + ", дата рождения: " +
       String( clnt.CurRec.rec.Born:m ) + ", " +
       clnt.CurRec.MakeDocStr();
     // v_Address  = clnt.CurRec.rec.AddressF;
     v_Address = commclnt_Получить_Поле_Address_Фактического_Адреса( clnt.CurRec );
     v_Phone   = GetPhoneNumberF36N(clnt.CurRec.rec.CodClient );
   end;

   if ( ( ALG_NFOPR.objectType == 222 )  OR  // Ввод новой доверенности.
        ( ALG_NFOPR.objectType == 224 )  OR  // Продление доверенности.
        ( ALG_NFOPR.objectType == 226 )  OR  // Ввод передоверия.
        ( ALG_NFOPR.objectType == 228 ) )    // Отмена передоверия.
     FillFIOForTrast();
     v_FIO_Sign = sFIO_Trast;
   else
     v_FIO_Sign = v_FIO;
   end;

   if ( FlagDate )  /* Проставить значение в поле "Дата заполнения". */
     v_DateSet  = String( {curdate}:m );
   end;

 [
                                                           ------------------
                                                           |    Код формы   |
                                                           |    документа   |
                                                           |     по ОКУД    |
                                                           ------------------
                                                           |     0401026    |
                                                           ------------------

                Карточка
   с образцами подписей и оттиска печати

                                               |----------------------------|
                                               |        Отметка банка       |
   ########################################### |                            |
                                               | -------------------------- |
                                               |          (подпись)         |
                                               | "___" ____________ 20___г. |
   ------------------------------------------- |                            |
        Место нахождения (место жительства)    |----------------------------|
    ########################################## |                            |
    ------------------------------------------ |----------------------------|
                                               |                            |
    ------------------------------------------ |----------------------------|
                                               |                            |
    ------------------------------------------ |----------------------------|
                                               |                            |
    _тел. N (_______) ######################## |                            |
     ######################################### |                            |
    ------------------------------------------ |----------------------------|
                                               |  Прочие отметки            |
    ------------------------------------------ |                            |
                                               |                            |
    ------------------------------------------ |----------------------------|
                                               |                            |
    ------------------------------------------ |----------------------------|
                                               |                            |
    ------------------------------------------ |----------------------------|
                                               |                            |
    ------------------------------------------ |----------------------------|
                                               |                            |
    -------------------------------------------------------------------------]
    (
      ("Владелец счета " + v_Owner):w,
      v_Address:w,
      v_Phone,
      ("Банк " + "\"" +  v_BankName + "\""):w
    );
 [#](MkStr(12,1));  /* Переход к новой странице */
 [
    #########################################################################
    -------------------------------------------------------------------------
      (краткое наименование владельца счета)
    N банковского счета #####################################################
    -------------------------------------------------------------------------
       Должность   |       Фамилия, имя,      | Образец |Срок полномочий лиц,
                   |          отчество        | подписи |      временно
                   |                          |         |пользующихся правом
                   |                          |         |      подписи
    -------------------------------------------------------------------------
    #######|       |##########################|         |
           |       |--------------------------|---------|
           |       |                          |         |
           |       |--------------------------|---------|
           |       |                          |         |
           |       |--------------------------|---------|
           |       |                          |         |
    -------------------------------------------------------------------------
    вторая |       |                          |         |
    подпись|       |--------------------------|---------|
           |       |                          |         |
           |       |--------------------------|---------|
           |       |                          |         |
           |       |--------------------------|---------|
           |       |                          |         |
    -------------------------------------------------------------------------
    Дата           |####################################| Образец оттиска
    заполнения     |                                    | печати
    ----------------------------------------------------|
    Подпись        |                                    |
    клиента        |                                    |
    -------------------------------------------------------------------------
                                  |                                         |
                                  |            Выданы денежные чеки         |
    ------------------------------|                                         |
    Место для удостоверительной   |-----------------------------------------|
    надписи о свидетельствовании  | Дата | с N  | по N | Дата | с N  | по N |
    подлинностей подписей         |------|------|------|------|------|------|
                                  |      |      |      |      |      |      |
                                  |------|------|------|------|------|------|
                                  |      |      |      |      |      |      |
                                  |------|------|------|------|------|------|
                                  |      |      |      |      |      |      |
                                  |------|------|------|------|------|------|
                                  |      |      |      |      |      |      |
                                  |------|------|------|------|------|------|
                                  |      |      |      |      |      |      |
    ------------------            |------|------|------|------|------|------|]
    (
      v_FIO,
      Acc_Form(v_Account):f,
      "первая подпись":w,
      v_FIO_Sign:w,
      v_DateSet
    );
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro PrintForm_nf_36

  if ( Check_Special_DepType() )  /* Если счет вида "Специальный счет", то ... */
    Form_0401026();                /* ... выпускается карточка ф. 0401026.  */
  else
    Print_nf_36();
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro IsFirstLog

  var ret_flag       = false;
  var stat;
  var kind_main      = 0;
  var key_main       = "";
  var key_current    = "";
  var numDprt        = String( ALG_NFOPR.numDprt );
  var recID          = String( ALG_NFOPR.recID );

  while ( StrLen( numDprt ) < 5 )
    numDprt = "0" + numDprt;
  end;
  while ( StrLen( recID ) < 10 )
    recID = "0" + recID;
  end;
  key_current = "rtnfopr#" + numDprt + recID + "00";

  KeyNum( sb_casln, 1 );
  sb_casln.ApplicationKind2 = 3001;
  sb_casln.ApplicationKey2  = key_current;
  sb_casln.RefValue2        = 0;

  stat = GetEQ( sb_casln );

  if ( stat )
    ;
  else
    numDprt = String( ALG_NFOPR.numDprt );

    while ( StrLen( numDprt ) < 3 )
      numDprt = "0" + numDprt;
    end;

    key_current = "rtnfopr#" + numDprt + recID + "00";

    KeyNum( sb_casln, 1 );
    sb_casln.ApplicationKind2 = 3001;
    sb_casln.ApplicationKey2  = key_current;
    sb_casln.RefValue2        = 0;

    stat = GetEQ( sb_casln );
  end;

  if ( stat )
    kind_main = sb_casln.ApplicationKind1;
    key_main  = sb_casln.ApplicationKey1;

    KeyNum( sb_casln, 0 );
    sb_casln.ApplicationKind1 = kind_main;
    sb_casln.ApplicationKey1  = key_main;
    sb_casln.NumLinkDoc       = 0;

    stat = GetGE( sb_casln );
    while ( stat )
      if ( ( sb_casln.ApplicationKind1 == kind_main ) and
           ( sb_casln.ApplicationKey1  == key_main  )    )
        if ( sb_casln.ApplicationKind2 == 3001 )
          if ( sb_casln.ApplicationKey2 == key_current )
            ret_flag = true;
          end;
          stat = false;
        else
          stat = Next( sb_casln );
        end;
      else
        stat = false;
      end;
    end;
  end;

  return ret_flag;

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/

  ALG_RESULT = OK_RES;

  if ( IsFirstLog( ) )
    PrintForm_nf_36( );
  else
    Exit( 1 );
  end;

end;
