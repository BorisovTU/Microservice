/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Открытие овердрафтной карты для овердрафтного карточного счета (507)    *
*                                                                          *
*  Лучко Сергей                                                            *
*  Лебедев С.В.                                                            *
*  30.11.2001                                                              *
\**************************************************************************/

import DeprIntr, rsd;

file currency_dep ( currency ) key 0;

var type_crd_dep : TBFile;
var credit_c_dep : TBFile;
var fininstr_dep : TBFile;

record ALG_DEPOSITOR ( depositr );

const
  OK_RES   =  0,  /* Продолжать проводку                            */
  SKIP_RES =  1,  /* Пропустить выполнение предопределенного модуля */
  END_RES  =  2,  /* Завершить проводку                             */
  ERR_RES  = -1;  /* Аваpийно завершить проводку                    */

var {GroupProcRunning};

private var rtWorkWithLoans = GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\РАБОТА_С_RS-LOANS" );
private var rtWorkWithRetail = GetRegVal( "COMMON\\WORK_MODE\\WORK_WITH_RETAIL" );


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro OpenOneBankFile ( id_file, path_file, name_file, num_key, dic_name )

  var stat      = true;

  id_file = Tbfile( name_file, "r", num_key );
  if ( id_file )
    ;
  else
    stat = false;
  end;

  if ( stat )
    SetParm( 0, id_file );
  end;

  return stat;

end;


/**************************************************************************\
|                          Открытие файлов RS-Bank                         |
\**************************************************************************/
macro OpenBankFiles

  var Path          = "";
  var PathDic       = "";
  var PathDicLoans  = "";
  var PathFile      = "";
  var stat          = true;
  var Len           = 0;

  if ( stat )
    stat = OpenOneBankFile( type_crd_dep, Path, "type_crd.dbt", 2, PathDicLoans );
  end;

  if ( stat )
    stat = OpenOneBankFile( credit_c_dep, Path, "credit_c.dbt", 8, PathDicLoans );
  end;

  if ( stat )
    stat = OpenOneBankFile( fininstr_dep, Path, "fininstr.dbt", 12, PathDic );
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindFininstr

   var stat;
   var cmd, rs;
   var fiid = 0;
   var codeCurr = "";

   currency_dep.Code_Currency = ALG_DEPOSITOR.Code_Currency;

   stat = GetEQ(currency_dep);
   IF (stat)
      IF (Trim( currency_dep.ExternalCode ) == "")
         stat = false;
      ELSE
         codeCurr = Trim( currency_dep.ExternalCode );

         /* Ищем валюту по коду фининструмента */
         fininstr_dep.KeyNum = 1;
         fininstr_dep.Clear;
         fininstr_dep.rec.FI_Code = codeCurr;
         stat = fininstr_dep.GetEQ;

         /* Ищем валюту по коду для счетов в фининструменте */
         if ( not stat )
           cmd = RsdCommand( "select T_FIID from DFININSTR_DBT where T_CODEINACCOUNT = '" + codeCurr + "'" );
           rs = RsdRecordSet( cmd );
           stat = rs.MoveNext( );

           if ( stat )
             fiid = rs.value( "T_FIID" );
           end;
         end;

         if ( not stat and ( StrLen( codeCurr ) < 3 ) )
           while ( StrLen( codeCurr ) < 3 )
             codeCurr = "0" + codeCurr;
           end;

           cmd = RsdCommand( "select T_FIID from DFININSTR_DBT where T_CODEINACCOUNT = '" + codeCurr + "'" );
           rs = RsdRecordSet( cmd );
           stat = rs.MoveNext( );

           if ( stat )
             fiid = rs.value( "T_FIID" );
           end;

         end;

         if ( fiid != 0 )
           fininstr_dep.KeyNum = 0;
           fininstr_dep.Clear;
           fininstr_dep.rec.FIID = fiid;
           stat = fininstr_dep.GetEQ;
         end;

      END;
   END;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindTypeCrd ( typeID )
  
  type_crd_dep.KeyNum = 0;
  type_crd_dep.Clear;
  type_crd_dep.rec.CreditTypeID = typeID;

  return type_crd_dep.GetEQ;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefOverCardId

  var retID = 0;
  var stat;

  const ovApplicCode = 1;

  credit_c_dep.Clear;
  credit_c_dep.rec.AccountKarta = ALG_DEPOSITOR.Account;

  stat = credit_c_dep.GetGE;

  while ( stat )
    if ( credit_c_dep.rec.AccountKarta == ALG_DEPOSITOR.Account )
      if ( FindTypeCrd( credit_c_dep.rec.CreditTypeID_Ref ) )
        if ( ( type_crd_dep.rec.Crd_kind == ovApplicCode ) and ( type_crd_dep.rec.CurCode == fininstr_dep.rec.FIID ) )
          retID = credit_c_dep.rec.CreditNumber;;
        end;
      end;
      if ( retID != 0 )
        stat = false;
      else
        stat = credit_c_dep.Next;
      end;
    else
      stat = false;
    end;
  end;

  return retID;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  var ovcardID = 0;

  SetOutput( "NUL" );

  ALG_RESULT = SKIP_RES;

  if ( StrFor( GetProgramID( ) ) == "П" )
    return;
  end;

  if ( BankVer( ) != 510 )
    return;
  end;

  if ( rtWorkWithLoans and rtWorkWithRetail )
    ;
  else
    return;
  end;

  if ( ( Index( ALG_DEPOSITOR.UserTypeAccount, "К" ) > 0 ) and
       ( Index( ALG_DEPOSITOR.UserTypeAccount, "О" ) > 0 )    )
    ;
  else
    return;
  end;

  if ( not OpenBankFiles( ) )
    if ( {GroupProcRunning} )
      ;
    else
      MsgBox( "Ошибка при открытии файлов Loans и RS-Bank" );
    end;
    return;
  end;

  if ( not FindFininstr( ) )
    if ( {GroupProcRunning} )
      ;
    else
      MsgBox( "Не найдена валюта для определения вида карты" );
    end;
    return;
  end;

  ovcardID = DefOverCardId( );

  if ( ovcardID != 0 )
    if ( DeleteOverCard( ovcardID ) )
      ;
    else
      ALG_RESULT = ERR_RES;
      if ( {GroupProcRunning} )
        ;
      else
        MsgBox( "Ошибка при удалении овердрафтной карты по счету" );
      end;
    end;
  end;

end;
