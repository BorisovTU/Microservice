import rcw, rsexts, commonInter;

class RtTemplateRepGenerator( pluginName : string, remoteMode : bool )

  private var m_remoteMode : bool = remoteMode;
  private var m_rcwHost, m_plugin;
  private var m_templatePath : string = "..\\mac\\template";

  macro closeDocument()
    m_plugin.closeDocument();
  end;

  macro getMarkerPos() : integer
    return m_plugin.getMarkerPos();
  end;

  macro setLeftMargin( pos : integer )
    m_plugin.setLeftMargin( pos );
  end;

  macro setRightMargin( pos : integer )
    m_plugin.setRightMargin( pos );
  end;

  macro setOverwriteMode( val : bool ) : bool
    return m_plugin.setOverwriteMode( val );
  end;

  macro insertText( text : string ) : bool
    return m_plugin.insertText( text );
  end;

  macro getMarkerValue( marker : string ) : string
  end;

  macro handleMarker( marker : string ) : bool

    var str = getMarkerValue( marker );

    if ( str == null )
      str = "";
    end;
    insertText( str );
    return true;
  end;

  macro createDocument( docFileName : string, templFileName : string ) : bool

    var marker;
    
    /* when in remote mode we should copy template file to terminal */
    if ( m_remoteMode )
      if ( not copyFile( m_templatePath + "\\" + templFileName, "$" + templFileName ) )
        return false;
      end;
    end;

    if ( not m_plugin.newDocument( docFileName, templFileName ) )
      return false;
    end;

    m_plugin.startGettingMarkers();

    while ( ( marker = m_plugin.getNextMarker() ) != null )
      if ( not handleMarker( marker ) )
        closeDocument();
        return false;
      end;
    end;

    closeDocument();
    return true;
  end;

  m_rcwHost = createObject( "rcwhost", "TRcwHost", null, not m_remoteMode );
  m_rcwHost.attachSite( TRcwSite );
  m_plugin = m_rcwHost.createRslObject( pluginName, "RtDocTemplateHandler" );
  if ( not m_remoteMode )
    m_plugin.setTemplatePath( m_templatePath );
    m_plugin.setOutputPath( getRegVal( "RS-RETAIL\\INI\\ÑàêÖäíéêàà\\TEXTDIR", false ) );
  end;
end;
