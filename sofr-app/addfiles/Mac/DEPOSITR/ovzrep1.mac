import BrParm, Acc_Form;

record ORVVZDocInfo("ci_doc.2");
record ORVVZRecInfo("ci_rec.2");

var
  V_CopyCode,
  V_TotNumItems=0,
  V_TotSoldItems=0,
  V_TotBoughtItems=0,
  V_TotSoldDepo=0,
  V_TotBoughtDepo=0,
  V_TotSum=$0,
  V_TotPrice=$0,
  V_TotSumSold=$0,
  V_TotSumBought=$0;

const
  CI_ORVVZ=6;

const
  _O_LOTTERY_SELL=1,
  _O_LOTTERY_PAYWIN=2,
  _O_LOTTERY_BUY=3;

macro V_FindCIMisc

  CIMisc.Type = CapIssueRec.Type;
  CIMisc.IssueID = CapIssueRec.Issue;
  return GetEQ( CIMisc );
end;

macro V_OutHead;

  [
                                                                               Филиал №
                                                                               отделения
                                                                               Сбербанка РФ


                               Отчет по операциям по лотереям за ##########

  ]
  (
    {curdate}
  );
end;

macro V_OutCommonHead;

  [+----------+----------------------------------------------+--------+-------------------+];
  [|    №     |        Н а з в а н и е                       | Колич. |     С у м м а     |];
  [+----------+----------------------------------------------+--------+-------------------+];
end;

macro V_OutSellHead;

  [ ];
  [Продажа лотерей];
  [ ];
  V_OutCommonHead;
end;

macro V_IsORVVZ( Name )

  return true;
end;

macro V_GetNumItems

  var
    RecFound,
    NumItems=0;

  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;

  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    if ( CheckCIDoc )
      SetRecordAddr(ORVVZDocInfo,CapIssueDoc,0,
        FldOffset(CapIssueDoc,"Info"),true);
      NumItems=NumItems+ORVVZDocInfo.NumItems;
    end;
    RecFound=Next(CapIssueDoc);
  end;
  return NumItems;
end;

macro V_OutPayWinHead;

  [ ];
  [Выплата выигрышей];
  [ ];
  V_OutCommonHead;
end;

macro V_OutLine;

  SetRecordAddr(ORVVZRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [|  #####   | ############################################ | ###### | ################# |]
  (
    CapIssueRec.Issue,
    ORVVZRecInfo.Name,
    V_GetNumItems,
    CapIssueRec.Sum
  );
end;

macro V_OutTotals;

  SetRecordAddr(ORVVZRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [+---------------------------------------------------------+--------+-------------------+];
  [|  И т о г о :                                            | ###### | ################# |]
  (
    V_TotNumItems,
    V_TotSum
  );
end;

macro V_SellReport

  var
    RecFound;

  V_TotNumItems=0;
  V_TotSum=$0;

  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_ORVVZ;

  V_OutSellHead;
  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_ORVVZ))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Brigade==OperBrigade)
      and (CapIssueRec.Operation==_O_LOTTERY_SELL))
      V_OutLine;
      SetRecordAddr(ORVVZRecInfo,CapIssueRec,0,
        FldOffset(CapIssueRec,"Info"),true);
      V_TotNumItems=V_TotNumItems+V_GetNumItems;
      V_TotSum=V_TotSum+CapIssueRec.Sum;
    end;
    RecFound=Next(CapIssueRec);
  end;
  V_OutTotals;
end;

macro V_PayWinReport

  var
    RecFound;

  V_TotNumItems=0;
  V_TotSum=$0;

  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_ORVVZ;

  V_OutPayWinHead;
  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_ORVVZ))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Brigade==OperBrigade)
      and (CapIssueRec.Operation==_O_LOTTERY_PAYWIN))
      V_OutLine;
      SetRecordAddr(ORVVZRecInfo,CapIssueRec,0,
        FldOffset(CapIssueRec,"Info"),true);
      V_TotNumItems=V_TotNumItems+V_GetNumItems;
      V_TotSum=V_TotSum+CapIssueRec.Sum;
    end;
    RecFound=Next(CapIssueRec);
  end;
  V_OutTotals;
end;

macro ORVVZReport;

  V_OutHead;
  V_SellReport;
  V_PayWinReport;
end;

macro V_OutSellRecHead;

  var
    BranchStrNum = "",
    DepartNum = "",
    DepartName = "",
    Comment = "";

  GetBranchParams( Branch, BranchStrNum, DepartNum, DepartName );
  SetRecordAddr(ORVVZRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);

  V_FindCIMisc;
  Comment = "номинальной стоимостью " + CIMisc.Price + " рублей";

  [  Сбербанк России                                                                                   Ф. № 56

     Филиал № ##########
     --------------------
     Наименование филиала
     Сбербанка России

        

                                             КОНТРОЛЬНАЯ  ВЕДОМОСТЬ

                                  на продажу и покупку облигаций Российского
                                        внутреннего выигрышного займа 1992 года
                              ##################################################
                                                 за ##########
        

     Смена № #####

  ]
  (
    BranchStrNum,
    Comment:c,
    {curdate},
    OperBrigade
  );
  [+--------+----------------------------------------------+-----------------------------+-------------------+];
  [|Номерной|                 П р о д а н о                |        К у п л е н о        |   П о д п и с и   |];
  [| талон, |-----------+-----------------+----------------|-----------+-----------------|---------+---------|];
  [|выданный|Количество |Номинальная стои-|Получено за     |Количество | Выплачено за    |         |         |];
  [|клиенту |-----+-----|мость облигаций  |облигации (руб.)|-----+-----| облигации       |контролер| кассир  |];
  [|        | шт  |д/шт |(руб.)           |                | шт  |д/шт | (руб.)          |         |         |];
  [+--------+-----+-----+-----------------+----------------+-----+-----+-----------------+---------+---------+];
end;                                                                                                                                                
                                                                                                                                                    
macro V_GetDepo( NumItems, Name )                                                                                                                   
                                                                                                                                                    
  if ( V_IsORVVZ( Name ) )                                                                                                                          
    return Int( Double( NumItems * CIMisc.Price * 2 ) );                                                                                      
  else                                                                                                                           
    return 0;
  end;
end;

macro V_OutSellRecLine;

  var
    PriceRepr=$0,
    BoughtPriceRepr=$0,
    SoldItems=0,
    BoughtItems=0,
    SoldDepo=0,
    BoughtDepo=0,
    SoldPar=$0,
    BoughtPar=$0,
    SumSoldRepr=$0,
    SumBoughtRepr=$0;

  V_FindCIMisc;

  SetRecordAddr(ORVVZDocInfo,CapIssueDoc,0,
    FldOffset(CapIssueDoc,"Info"),true);
  if(CapIssueDoc.Operation==_O_LOTTERY_SELL)
    SoldItems = ORVVZDocInfo.NumItems;
    V_TotSoldItems = V_TotSoldItems + SoldItems;
    SoldDepo = V_GetDepo( SoldItems, ORVVZDocInfo.Name );
    V_TotSoldDepo = V_TotSoldDepo + SoldDepo;
    V_TotSumSold=V_TotSumSold+CapIssueDoc.Sum;
    if ( V_IsORVVZ( ORVVZDocInfo.Name) )
      PriceRepr=Money( CIMisc.Price * ORVVZDocInfo.NumItems );
      SoldPar = CIMisc.Price;
    else
      PriceRepr=ORVVZDocInfo.Price;
    end;
    SumSoldRepr=CapIssueDoc.Sum;
  elif(CapIssueDoc.Operation==_O_LOTTERY_BUY)
    BoughtItems = ORVVZDocInfo.NumItems;
    V_TotBoughtItems = V_TotBoughtItems + BoughtItems;
    BoughtDepo = V_GetDepo( BoughtItems, ORVVZDocInfo.Name  );
    V_TotBoughtDepo = V_TotBoughtDepo + BoughtDepo;
    V_TotSumBought=V_TotSumBought+CapIssueDoc.Sum;
    if ( V_IsORVVZ( ORVVZDocInfo.Name ) )
      BoughtPriceRepr=Money( CIMisc.Price * ORVVZDocInfo.NumItems );
      BoughtPar = CIMisc.Price;
    else
      BoughtPriceRepr=ORVVZDocInfo.Price;
    end;
    SumBoughtRepr=CapIssueDoc.Sum;
  end;

  [|########|#####|#####|#################|################|#####|#####|#################|         |         |]
  (
    CapIssueDoc.DocNumber,
    SoldItems:z,
    SoldDepo:z,
    PriceRepr:z,
    SumSoldRepr:z,
    BoughtItems:z,
    BoughtDepo:z,
    SumBoughtRepr:z
  );
end;

macro V_ORVVZSellRecTotals;

  var
    SellCredAcc,
    SellDebAcc,
    BuyCredAcc,
    BuyDebAcc;

  GetBookAcc(_O_LOTTERY_SELL, SellDebAcc, SellCredAcc);
  GetBookAcc(_O_LOTTERY_BUY, BuyDebAcc, BuyCredAcc);
  SetRecordAddr(ORVVZDocInfo,CapIssueDoc,0,
    FldOffset(CapIssueDoc,"Info"),true);
  [+--------+-----+-----+-----------------+----------------+-----+-----+-----------------+---------+---------+];
  [| Всего  |#####|#####|                 |################|#####|#####|#################|         |         |]
  (
    V_TotSoldItems,
    V_TotSoldDepo,
    V_TotSumSold,
    V_TotBoughtItems,
    V_TotBoughtDepo,
    V_TotSumBought
  );
  [         |-----------|-----------------|----------------|-----------|-----------------| ];
  [         | Дебет     | Дебет           | Дебет          | Дебет     | Дебет           | ];
  [         | счета     | счета           | счета          | счета     | счета           | ];
  [         | #         |                 |                | #         |                 | ]
  ( Acc_Form(SellDebAcc), Acc_Form(BuyDebAcc) );                                          
  [         |-----------|-----------------|----------------|-----------|-----------------| ];
  [         | Кредит    | Кредит          | Кредит         | Кредит    | Кредит          | ];
  [         | счета     | счета           | счета          | счета     | счета           | ];
  [         | #         |                 |                | #         |                 | ]
  ( Acc_Form(SellCredAcc), Acc_Form(BuyCredAcc) );                                          
  [ ];
  [                        Гл. (ст.) бухгалтер _______________________ ];
  [ ];
  [ ];
end;

macro V_OutPayRecHead;

  var
    BranchStrNum = "",
    DepartNum = "",
    DepartName = "",
    DebAcc,
    CredAcc,
    Comment = "";

  GetBranchParams( Branch, BranchStrNum, DepartNum, DepartName );
  GetBookAcc(CapIssueRec.Operation, DebAcc, CredAcc);
  SetRecordAddr(ORVVZRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);

  if ( not V_IsORVVZ( ORVVZRecInfo.Name ) )
    Comment = "(Лотерея \""+ORVVZRecInfo.Name+"\")";
  end;

  [                                                                                                            Ф. № 92
     ##################################
                                                                                                   Филиал № ##########
     Сберегательного банка № ##########



                                                 КОНТРОЛЬНАЯ  ВЕДОМОСТЬ                    Отметки бухгалтерии

                                           на оплату выигравших ценных бумаг         Дебет сч.  ######################
                                        #######################################      Кредит сч. ######################
                                                     за ##########                   Приход сч. ______________________


     Смена № #####

  ]
  (
    DepartName,
    BranchStrNum,
    DepartNum,
    Acc_Form(DebAcc),
    Comment:c,
    Acc_Form(CredAcc),
    {curdate},
    OperBrigade
  );
  [+-------------------------------------+-------+-------+-----------+--------+--------+---------+-------------------+
   |                                     | Номер |       |           |        |Нарицат.|         |                   |
   |       Название ценной бумаги        |  тир. | Серия |   Номер   | Разряд |стоим-ть| Кол-во  |     Выплачено     |
   |                                     |       |       |           |        |        |         |                   |
   +-------------------------------------+-------+-------+-----------+--------+--------+---------+-------------------+];
end;

macro V_OutPayRecLine;

  SetRecordAddr(ORVVZDocInfo,CapIssueDoc,0,
    FldOffset(CapIssueDoc,"Info"),true);
  [| ################################### | ##### | ##### |###########| ###### | ###### | ####### | ################# |]
  (
    ORVVZDocInfo.Name,
    ORVVZDocInfo.Drawing,
    ORVVZDocInfo.Series,
    ORVVZDocInfo.Number,
    ORVVZDocInfo.Category:z,
    ORVVZDocInfo.Price,
    ORVVZDocInfo.NumItems,
    CapIssueDoc.Sum
  );
  V_TotPrice=V_TotPrice+ORVVZDocInfo.Price;
  V_TotSum=V_TotSum+CapIssueDoc.Sum;
  V_TotNumItems=V_TotNumItems+ORVVZDocInfo.NumItems;
end;

macro V_ORVVZPayRecTotals;

  SetRecordAddr(ORVVZDocInfo,CapIssueDoc,0,
    FldOffset(CapIssueDoc,"Info"),true);
  [+-------------------------------------+-------+-------+-----------+--------+--------+---------+-------------------+];
  [| В с е г о                           |       |       |           |        |        | ####### | ################# |]
  (
    V_TotNumItems,
    V_TotSum
  );
  [ ];
  [  # ]( RubToStr( V_TotSum ) );
  [  ---------------------------------------------------------------------- ];
  [                          (сумма прописью) ];
  [ ];
end;

macro V_OutRecTail;

  [



        Контролер (оператор) ___________                                Кассир ____________________
  ];
end;

macro V_ORVVZSellRecBody;

  var
    RecFound;

  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;

  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    if ( CheckCIDoc )
      V_OutSellRecLine;
    end; 
    RecFound=Next(CapIssueDoc);
  end;
end;

macro V_ORVVZSellRecCopy;

  var
    RecFound;

  V_TotNumItems=0;
  V_TotSoldItems=0;
  V_TotBoughtItems=0;
  V_TotSoldDepo=0;
  V_TotBoughtDepo=0;
  V_TotSum=$0;
  V_TotPrice=$0;
  V_TotSumSold=$0;
  V_TotSumBought=$0;

  SetRecordAddr(ORVVZRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);

  V_OutSellRecHead;
/*  if ( not V_IsORVVZ( ORVVZRecInfo.Name ) ) */
    V_ORVVZSellRecBody;
    V_ORVVZSellRecTotals;
/*  else
    GetPos( CapIssueRec );
    KeyNum( CapIssueRec, 0 );
    ClearRecord(CapIssueRec);
    CapIssueRec.Branch=Branch;
    CapIssueRec.IsCur=0;
    CapIssueRec.Type=CI_ORVVZ;

    RecFound=GetGE(CapIssueRec);
    while(RecFound
      and (CapIssueRec.Branch==Branch)
      and (CapIssueRec.IsCur==0)
      and (CapIssueRec.Type==CI_ORVVZ))
      if((CapIssueRec.Date=={curdate})
        and (CapIssueRec.Brigade==OperBrigade))
        if(((CapIssueRec.Operation==_O_LOTTERY_SELL)
          or (CapIssueRec.Operation==_O_LOTTERY_BUY))
          and V_IsORVVZ( ORVVZRecInfo.Name ))
          V_ORVVZSellRecBody;
        end;
      end;
      RecFound=Next(CapIssueRec);
    end;
    GetDirect( CapIssueRec );
    V_ORVVZSellRecTotals;
  end;
*/
  V_OutRecTail;
  PrintLn( StrFor( 12 ) );
end;

macro V_ORVVZSellRec

  V_ORVVZSellRecCopy;
end;

macro V_ORVVZPayRecBody;

  var
    RecFound;

  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;
  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    if ( CheckCIDoc )
      V_OutPayRecLine;
    end;
    RecFound=Next(CapIssueDoc);
  end;
end;

macro V_ORVVZPayRecCopy;

  V_TotNumItems=0;
  V_TotSum=$0;
  V_OutPayRecHead;
  V_ORVVZPayRecBody;
  V_ORVVZPayRecTotals;
  V_OutRecTail;
  PrintLn( StrFor( 12 ) );
end;

macro V_ORVVZPayRec

  V_CopyCode = "А";
  V_ORVVZPayRecCopy;
/*
  V_CopyCode = "Б";
  V_ORVVZPayRecCopy;
*/
end;

macro ORVVZRec(ApplicationKind,ApplicationKey);

  KeyNum(CapIssueRec,2);
  CapIssueRec.ApplicationKind=ApplicationKind;
  CapIssueRec.ApplicationKey=ApplicationKey;
  if(not GetEQ(CapIssueRec))
    MsgBox("Ошибка при печати ведомости");
    return;
  end;
  if((CapIssueRec.Operation==_O_LOTTERY_SELL) or (CapIssueRec.Operation==_O_LOTTERY_BUY))
    V_ORVVZSellRec;
  elif(CapIssueRec.Operation==_O_LOTTERY_PAYWIN)
    V_ORVVZPayRec;
  end;
end;

macro V_SellBuyReportCopy

  var
    NotEmpty = false,
    PrevIssue = 0,
    RecFound;

  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_ORVVZ;

  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_ORVVZ))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Brigade==OperBrigade))
      if((CapIssueRec.Operation==_O_LOTTERY_SELL)
        or (CapIssueRec.Operation==_O_LOTTERY_BUY))
        if(CapIssueRec.Issue!=PrevIssue)
          if(PrevIssue)
            V_ORVVZSellRecTotals;
          end;
          PrevIssue=CapIssueRec.Issue;
          V_TotNumItems=0;
          V_TotSoldItems=0;
          V_TotBoughtItems=0;
          V_TotSoldDepo=0;
          V_TotBoughtDepo=0;
          V_TotSum=$0;
          V_TotPrice=$0;
          V_TotSumSold=$0;
          V_TotSumBought=$0;
          V_OutSellRecHead;
          NotEmpty = true;
        end;
        V_ORVVZSellRecBody;
      end;
    end;
    RecFound=Next(CapIssueRec);
  end;
  if(NotEmpty)
    V_ORVVZSellRecTotals;
  end;

  PrintLn;
  PrintLn;
end;

macro V_SellBuyReport

  V_SellBuyReportCopy;
end;

macro ORVVZRecReport;

  var
    ORVVZFound,
    RecFound,
    saveKeyNumOGSZ = KeyNum(CIMisc);

  V_SellBuyReport;

  KeyNum(CIMisc, 1);  /* По дате тиража */
  ClearRecord(CIMisc);
  CIMisc.Type=CI_ORVVZ;

  ORVVZFound=GetGE(CIMisc);
  while(ORVVZFound
    and (CIMisc.Type==CI_ORVVZ))

    ClearRecord(CapIssueRec);
    CapIssueRec.Branch=Branch;
    CapIssueRec.IsCur=0;
    CapIssueRec.Type=CI_ORVVZ;
    CapIssueRec.Issue=CIMisc.IssueID;

    RecFound=GetGE(CapIssueRec);
    while(RecFound
      and (CapIssueRec.Branch==Branch)
      and (CapIssueRec.IsCur==0)
      and (CapIssueRec.Type==CI_ORVVZ)
      and (CapIssueRec.Issue==CIMisc.IssueID))
      if((CapIssueRec.Date=={curdate})
        and (CapIssueRec.Brigade==OperBrigade))
        if(CapIssueRec.Operation==_O_LOTTERY_PAYWIN)
          V_TotPrice=$0;
          V_TotSum=$0;
          V_ORVVZPayRec;
        end;
      end;
      RecFound=Next(CapIssueRec);
    end;
    ORVVZFound=Next(CIMisc);
  end;
  KeyNum(CIMisc, saveKeyNumOGSZ);
end;
