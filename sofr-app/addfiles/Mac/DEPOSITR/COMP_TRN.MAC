/*
*  ---------------------
*  R-Style Software Lab.
*  ---------------------
*  RS-Retail
*
*  Компенсации по вкладам
*
*  Ежедневный оборот по компенсации
*
*/

import CommonInter, Acc_Form;

var dpDepList = TdpDepList;
file acc     ("depositr.dbt"  ) key 7;        /* Счета вкладчиков */
file doc     ( "sbdepdoc.dbt" ) key 1;        /* Документы */
file compens ( "compens.dbt"  ) key 1;

const OP_OUTCOMP = 15;  /* Операция выдачи компенсации */

private var {curdate};
private var {Name_Bank};

           /* По филиалу: ..... */
var        /* Начислено: ...... */
 b_QAccI,  /* Количество счетов */
 b_SumI,   /* Сумма             */
           /* Выдано: ......... */
 b_QAccO,  /* Количество счетов */
 b_SumO;   /* Сумма             */

           /* Итого: .......... */
var        /* Начислено: ...... */
 i_QAccI,  /* Количество счетов */
 i_SumI,   /* Сумма             */
           /* Выдано: ......... */
 i_QAccO,  /* Количество счетов */
 i_SumO;   /* Сумма             */

var compDocSum;

/************************************************************************
             Поиск документов выплаченной компенсации
************************************************************************/
MACRO CompOutDocs( p_date )
  var ret = TRUE;
  compDocSum = $0;
  ClearRecord( acc );
  acc.FNCash  = compens.FNCash;
  acc.Account = compens.Account;
  ret = GetEQ( acc );
  if ( NOT ret )
    PrintLn( " *** Не найден счет ", Acc_Form(compens.Account) );
  end;
  if ( ret )
    ClearRecord( doc );
    doc.Referenc = acc.Referenc;
    doc.TypeOper = OP_OUTCOMP;  /* Выдача компенсации */
    if ( GetGE( doc ) )
      while ( ret                               AND
              ( doc.Referenc == acc.Referenc )  AND
              ( doc.TypeOper == OP_OUTCOMP ) )
        if ( doc.Date_Document == p_date )
          compDocSum = compDocSum + doc.OutSum;
        end;
        ret = Next( doc );
      end;
      ret = TRUE;
    end;
  end;
  return ret;
END;

/*****************************************************************
                 Строка оборота компенсации за дату
*****************************************************************/
MACRO CompensString( p_branch, p_date )
 var stat;
                /* По филиалу: ..... */
                /* Начислено: ...... */
 b_QAccI =  0;  /* Количество счетов */
 b_SumI  = $0;  /* Сумма             */
                /* Выдано: ......... */
 b_QAccO = 0;   /* Количество счетов */
 b_SumO = $0;   /* Сумма             */
 ReWind( compens );
 compens.FNcash = p_branch;
 compens.Date   = p_date;
 stat = GetGE( compens );
 while ( stat                            AND
         ( compens.FNcash == p_branch )  AND
         ( compens.Date   == p_date   ) )
   stat = CompOutDocs( p_date );
   if ( stat )
                                                /* По филиалу: ..... */
                                                /* Начислено: ...... */
     b_QAccI = b_QAccI + 1;                     /* Количество счетов */
     b_SumI  = b_SumI  + compens.Sum;           /* Сумма             */
                                                /* Выдано: ......... */
     b_QAccO = b_QAccO + 1;                     /* Количество счетов */
     b_SumO  = b_SumO  + compDocSum;            /* Сумма             */
     stat = Next( compens );
   else
     PrintLn( " *** Произошла ошибка в процедуре CompOutDocs()" );
   end;
 end;
 [ | ############ | ###### | ###################### | ###### | ###################### |]
 ( dpDepList.rec.Name:12,
   b_QAccI:6,
   b_SumI:20:a,
   b_QAccO:6,
   b_SumO:20:a );
                                /* Итого: .......... */
                                /* Начислено: ...... */
 i_QAccI = i_QAccI + b_QAccI;   /* Количество счетов */
 i_SumI  = i_SumI  + b_SumI;    /* Сумма             */
                                /* Выдано: ......... */
 i_QAccO = i_QAccO + b_QAccO;   /* Количество счетов */
 i_SumO  = i_SumO  + b_SumO;    /* Сумма             */
END;

/*****************************************************************
    Процедура выпуска отчета ежедневонго оборота по компенсации
*****************************************************************/
MACRO CompensationTurn( p_date )
 var nn  = 0;
 var ret = TRUE;
 var sDepartment = "???";
 var filialID;

 sDepartment = "\"" + {Name_Bank} + "\"";
                /* Итого: .......... */
                /* Начислено: ...... */
 i_QAccI =  0;  /* Количество счетов */
 i_SumI  = $0;  /* Сумма             */
                /* Выдано: ......... */
 i_QAccO =  0;  /* Количество счетов */
 i_SumO  = $0;  /* Сумма             */
 [

                                          Ежедневный оборот по компенсации

                                                За #


       Отделение #


   |--------------+--------+------------------------+--------+----------------------- |
   |    Филиал    |            Начислено            |              Выдано             |
   |              |--------+------------------------|--------+----------------------- |
   |              | Кол-во |         Сумма          | Кол-во |         Сумма          |
   |              | счетов |                        | счетов |                        |
   |--------------+--------+------------------------+--------+----------------------- |]
 ( p_date:m, sDepartment );
 filialID = dpDepList.getFilialID(NumFNCash());
 dpDepList.SetFilter(filialID);
/* InitProgress( NRecords( filials ), " Ж Д И Т Е ...",
               "Перебор видов вкладов" );*/
 while ( ret )
   nn = nn + 1;
/*   UseProgress( nn );*/
   message("Обработано ", nn,  " подразделений");
   if ( dpDepList.next() )
     CompensString( dpDepList.rec.Code, p_date );
   else
     ret = FALSE;
   end;
 end;
/* RemProgress();*/
 [ |--------------+--------+------------------------+--------+----------------------- |
   | Итого по ОСБ:| ###### | ###################### | ###### | ###################### |
   |--------------+--------+------------------------+--------+----------------------- |]
 ( i_QAccI:6,
   i_SumI:20:a,
   i_QAccO:6,
   i_SumO:20:a );
END;

CompensationTurn( {curdate} );
