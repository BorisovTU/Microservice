/*

  ---------------
  R-Style Softlab
  ---------------
  RS-Retail

  Список мерных слитков, принятых на обезличенные металлические счета

*/

import CommonInter, alg_vars, RSD, cur_rate;

file DeskAux( "desk_aux.dbt" ) key 0 write;
record IngotAux( "desk_aux.1" );
private file listfdep( listfdep );                      /* Справочник филиалов */
private file dpdep( dp_dep );                      /* Справочник филиалов */
private file party( party );                      /* Справочник филиалов */

private const ACT_COUNTER_NUMBER = 21;

/*Режимы печати*/
private const PRMODE_ACT_SPEC      = 1; // печатать только акт и спецификацию 
private const PRMODE_LIST          = 2; // печатать только список слитков
private const PRMODE_ACT_SPEC_LIST = 3; // печатать все
private const PRMODE_ACT           = 4; // печатать только акт


private const TYPERES_MINCAPISSUE = 500, 
      CI_INGOT = 5,
      TYPE_RES_PAY = 0;

private var ClientList = TClientList;

/* Панель параметров акта приема-передачи */

private var {oper};
private var {curdate};
private const fncash = NumFNCash();

record panel_actTake( "PACTTAKE", "sbbank.lbr" ) dialog;

// Макросы работы с исключениями.
class exception( whatMsg )
    var whatMsg_ = whatMsg;
end;

macro throw( exception )
    RunError("", exception );
end;

macro what( whatExceptionOwner )
    if( isEqClass("exception", whatExceptionOwner.err) )
        return whatExceptionOwner.err.whatMsg_;
    else
        RunError;
    end;
end;

macro CHECK( predicate : BOOL, whatMsg : STRING ) /* throw exception */

    if( not predicate )
        throw( exception(whatMsg) );
    end;
end;

// Макрос "условного оператора".
macro caseOp( predicate : BOOL, returnCaseTrue, returnCaseFalse )
    if( predicate )
        return returnCaseTrue;
    else
        return returnCaseFalse;
    end;
end;

// Присваивающий оператор сложения.
macro accumulate( dst, value )
    setparm(0, dst + value );
end;

// Добавляем в конец TARRAY новый элемент.
macro push_back( arr, value )
    arr[arr.size] = value;
end;

// Функция переводит целое число в строковое представление.
macro NumInWords( num : INTEGER ) : STRING
    var str = STRING(MONEY(num):m); 
    var pos = 0;
    
    pos = strbrk( str, "." );
    CHECK( pos > 0, "Неверный формат для MONEY" );
    str = substr(str, 1, pos - 5 );        
    
    return trim(str);
end;

// Разбиваем строку на массив по символу в строке.
macro splitBySpaces( src ) /* : TARRAY */
    const SPACE = " ";
    var parts = TARRAY;
    var pos = 0;
    var leftover = trim(src);
    var toAdd = "";
  
    while( strlen(leftover) > 0 )
        pos = strbrk( leftover, SPACE );
        if( pos == 0 )
            push_back( parts, leftover );
            return parts;
        end;
        push_back( parts, substr(leftover,1,pos-1) );
        leftover = trim(substr(leftover,pos+1));
    end;

    return parts;
end;

// Получаем имя операциониста.
macro getOperName( Oper : integer ) : string
  
    file person( "person.dbt" ) key 0;
    var parts = TARRAY, name = " ";

    person.Oper = Oper;
    CHECK( GetEQ(person), "Не найден операционист" );  

    parts = splitBySpaces( person.Name );
    if ( parts.size > 1 )
      name = parts[0] + " " + substr(parts[1],1,1) + ". ";
      if ( parts.size > 2 )
        name = name + substr(parts[2],1,1) + ". ";   
      end;
    else
      name = person.Name;
    end;

    return name;
end;

// Получаем окончание по количеству элементов.
macro makeEndingForMultipleCount( count : INTEGER ) : STRING
    var ending = "ков";
    var leftover = mod( count, 10 );

    if( leftover == 1 )
        ending = "ок";
    elif( (leftover == 2) or (leftover == 3) or (leftover == 4) )
        ending = "ка";
    end;

    return ending;
end;

// Обрезаем строку.
macro cut( src, len )
    return substr( string(src),1,len );
end;

// Номера полей панели panel_actTake
private 
CLASS ActTakePanelField
  var MeasuringDevice = 0;
  var LicenseNumber = 1;
  var LicenseDate = 2;
  var Consistency = 3;
  var SpecificationNumber = 4;
  var SpecificationDate = 5;
  var ReclamationNumber = 6;
  var ReclamationDate = 7;
  var inconsistentIngotBatch = 8;
END;
 
private const K_ESC=27, K_F2=316, K_SPACE=32;
private const field = ActTakePanelField;
private const SET_CHAR = "X";
private const UNSET_CHAR = "";

/****************************************************************************\
|                   Обработка реакции при нажатии пробела                    |
\****************************************************************************/
private
MACRO HandleSpaceBar( field )

    if(field == UNSET_CHAR)
        field = SET_CHAR;
    else
        field = UNSET_CHAR;
    end;
  
    SetParm( 0, field ); 
END;
 
/**************************************************************************\
|                                                                          |
\**************************************************************************/

private
MACRO OnChangeFieldConsistency

  if( panel_actTake.Consistency == UNSET_CHAR )
    EnableFields( panel_actTake, field.ReclamationNumber );
    EnableFields( panel_actTake, field.ReclamationDate );
    EnableFields( panel_actTake, field.inconsistentIngotBatch );
  else
    DisableFields( panel_actTake, field.ReclamationNumber );
    DisableFields( panel_actTake, field.ReclamationDate );
    DisableFields( panel_actTake, field.inconsistentIngotBatch );
  end; 
END;
 
private 
MACRO OnInit()

  panel_actTake.MeasuringDevice = UNSET_CHAR;
  panel_actTake.LicenseNumber = UNSET_CHAR;
  panel_actTake.LicenseDate = {curdate};
  
  panel_actTake.Consistency = SET_CHAR;
  OnChangeFieldConsistency();
  
  panel_actTake.SpecificationNumber = UNSET_CHAR;
  panel_actTake.SpecificationDate = {curdate};
  panel_actTake.ReclamationNumber = UNSET_CHAR;
  panel_actTake.ReclamationDate = {curdate};
  panel_actTake.inconsistentIngotBatch = UNSET_CHAR;
END;

private 
MACRO OnSave()
                    
  /* TODO: Add your code here (c) */
END;

/**************************************************************************\
|              Проверка заполнения всех обязательных полей                 |
\**************************************************************************/

private macro AllMandatoryFieldsFilled()
                                                               
    var allFilled = true;

    if( panel_actTake.MeasuringDevice == UNSET_CHAR )
        MsgBox( "Не заполнено поле \"Средство измерений\"" );
        setFocus( panel_actTake, field.MeasuringDevice );
        allFilled = false;
    elif( panel_actTake.LicenseNumber == UNSET_CHAR )
        MsgBox( "Не заполнено поле \"# свидетельства\"" );
        setFocus( panel_actTake, field.LicenseNumber );
        allFilled = false;  
    elif( panel_actTake.SpecificationNumber == UNSET_CHAR )
        MsgBox( "Не заполнено поле \"# спецификации\"" );
        setFocus( panel_actTake, field.SpecificationNumber );
        allFilled = false;     
    elif( panel_actTake.SpecificationDate == date(0,0,0) )
        MsgBox( "Не заполнено поле \"Дата спецификации\"" );
        setFocus( panel_actTake, field.SpecificationDate );
        allFilled = false; 
    end;

    return allFilled;
end;

private
MACRO HandleFieldConsistency

  HandleSpaceBar( panel_actTake(field.Consistency) );
  OnChangeFieldConsistency();
END;

private
MACRO HandleFieldinconsistentIngotBatch

  HandleSpaceBar( panel_actTake(field.inconsistentIngotBatch) );
END;
 
/**************************************************************************\
|                        Обработчик событий панели                         |
\**************************************************************************/

macro eventHandler( dlg, cmd, id, key )

   var stat = CM_DEFAULT;
 
   if( cmd == DLG_KEY )
       /* Обработчик K_ESC */
       if (key == K_ESC )
           stat = CM_CANCEL;

       /* Обработчик K_F2 */
       elif(key == K_F2 )
           if( AllMandatoryFieldsFilled() )
              stat = CM_SAVE;
           end;

       /* Обработчик K_SPACE */
       elif( key == K_SPACE )
          if( id == Field.Consistency )
            HandleFieldConsistency();
          elif( id == Field.inconsistentIngotBatch )
            HandleFieldinconsistentIngotBatch();
          end;
          UpdateFields( dlg );
       end;

   elif( cmd == DLG_INIT )
       OnInit();
       UpdateFields( dlg );

   elif( cmd == DLG_SAVE )
       OnSave();
       UpdateFields( dlg );
       stat = CM_SAVE;
   end;
 
   return stat;
end;

macro RunActTakePanel()
   return RunDialog( panel_actTake, "eventHandler" );
end;

private 
CLASS IngotReportRecord
    var metalName = "<metalName>";
    var ingotName = "<ingotName>";
    var ingotCount = "<ingotCount>";
    var ligatureWeight = "<ligatureWeight>";
    var chemicalWeight = "<chemicalWeight>";
    var etc = "<etc>";  
    var mark = "<mark>";
    var serial = "<serial>";
    var hallmark = "<hallmark>";
    var specs = "<specs>";
    var year = "<year>";
    var quality = "<quality>";
    var sellRate = "<sellRate>";
    var producer = "<producer>";
    var useWeightType = "<useWeightType>";
END;

private
CLASS IngotReportSummaryRecord
    var totalIngotCount = "<totalIngotCount>";
    var totalIngotCountInWords = "<totalIngotCountInWords>";
    var totalLigatureWeight = "<totalLigatureWeight>";
    var totalLigatureWeightInWords = "<totalLigatureWeightInWords>";
    var totalChemicalWeight = "<totalChemicalWeight>";
    var totalChemicalWeightInWords = "<totalChemicalWeightInWords>";
    var sum = "<sum>";
    var sumInWords = "<sumInWords>";
    var totalEtc = "<totalEtc>"; 
    var ending_1 = "<ending_1>";
END;

private
CLASS ActTake_data

    var useWeightType = false;
    var ingotNameInGenitiveCase = "";
    var actNumber = "<actNumber>";
    var actDate = "<actDate>";;
    var branch = "<branch>";
    var clientName = "<clientName>";
    var clientPassport = TArray;
    
    var ingotRecords = tarray;
    var standartIngotSummary = IngotReportSummaryRecord;
    var measuredIngotSummary = IngotReportSummaryRecord;

    var measuringDevice = "<measuringDevice>";
    var licenseNumber = "<licenseNumber>";
    var licenseDate = "<licenseDate>";
    var specificationNumber = "<specificationNumber>";
    var specificationDate = "<specificationDate>";
    var reclamationNumber = "<reclamationNumber>";
    var reclamationDate = "<reclamationDate>";
    var ending_1 = "<ending_1>";
    var ending_2 = "<ending_2>";

    var measuredIngotIndices = tarray;
    var standartIngotIndices = tarray;
    var currentIndex = 0;
    var operName = {oper};    

    private
    macro pumpActNumber( number ) : DOUBLEL
        var countValue = 0.0;
        CHECK( GetCountValue(number, fncash, countValue), "Ошибка регистрации номера акта" );
        return countValue;
    end;

    private
    macro pumpBranch( branch ) : STRING
        dpdep.code = branch;
        CHECK( getEQ(dpdep), "Не найдено подразделение банка" );
        party.partyid = dpdep.partyid;
        CHECK( getEQ(party), "Не найдено подразделение банка" );
        return party.name + " " + fncash;
    end;  

    private
    macro selectCasln() /* : rsdrecordset */
        var cmd, rs;   
        cmd = rsdcommand(
            "SELECT casln.t_REFVALUE2 refvalue2,"
            "       casln.t_APPLICATIONKEY2 appkey2,"
            "       casln.t_APPLICATIONKIND2 appkind2"
            "  FROM dsb_casln_dbt CASLN"  
            " WHERE  casln.t_APPLICATIONKEY1 in "
            "            (SELECT casln.t_APPLICATIONKEY1 "
            "             FROM   dsb_casln_dbt CASLN " 
            "             WHERE  casln.t_APPLICATIONKEY2 = :1 "  
            "             and CASLN.T_APPLICATIONKIND2 = :2 ) "
            "        AND casln.t_APPLICATIONKIND2 = :3 "
            "   AND casln.t_REFVALUE2 <> 0"
            "   AND (SELECT casvl.t_TYPEVALUE"
            "          FROM dsb_casvl_dbt CASVL"
            "             WHERE  casvl.t_refvalue = casln.t_refvalue2) = :refvalue2 " );
            
        cmd.addparam( ":1", rsdbp_in, ALG_SBDEPDOC.ApplicationKey );
        cmd.addparam( ":2", rsdbp_in, ALG_SBDEPDOC.iApplicationKind );        
        cmd.addparam( ":3", rsdbp_in, ALG_SBDEPDOC.iApplicationKind );        
        cmd.addparam( "refvalue2", rsdbp_in, TYPERES_MINCAPISSUE + CI_INGOT );
        cmd.execute();
        rs = rsdrecordset(cmd); 
        return rs;
    end;

    private
    macro selectBunch( appkey, appkind ) /* : rsdrecordset */
        var cmd, rs;
        cmd = rsdcommand(
            "SELECT bunch.T_SERIES series,"
            "       bunch.T_MIN min"
            "  FROM dbunch_dbt BUNCH"
            " WHERE bunch.T_BAGAPPKEY = :appkey"
            "   AND bunch.T_BAGAPPKIND = :appkind"
            "   AND bunch.T_TYPE = :type "
            );
            
        cmd.addparam( "appkey", rsdbp_in, appkey );
        cmd.addparam( "appkind", rsdbp_in, appkind );
        cmd.addparam( "type", rsdbp_in, TYPE_RES_PAY );
        cmd.execute();
        rs = rsdrecordset(cmd);
        return rs;
    end;

    private
    macro selectDeskAux( series, number, valueref )
        var cmd, rs;

        cmd = RsdCommand(
            "SELECT desk.t_BRANCH branch,"
            "       desk.t_VALUEREF valueref,"
            "       desk.t_DATE rsdate,"
            "       desk.t_ISGONE isgone,"
            "       desk.t_SERIES series,"
            "       desk.T_NUMBER rsnumber"
            "  FROM ddesk_aux_dbt DESK"
            " WHERE desk.T_SERIES = :series"
            "   AND desk.T_NUMBER = :parm_number"
            "   AND desk.T_VALUEREF = :valueref"
            );
        cmd.addparam( "series", rsdbp_in, series );
        cmd.addparam( "parm_number", rsdbp_in, number );
        cmd.addparam( "valueref", rsdbp_in, valueref );  
        cmd.execute();
        rs = rsdrecordset(cmd);
        return rs;
    end;

    private
    macro pumpDeskAuxWithBlob( branch, valueref, rsdatetime, isgone, series, number )
        var rsdate;
        DtTmSplit( rsdatetime, rsdate );
    
        DeskAux.branch = branch;
        DeskAux.valueref = valueref;
        DeskAux.Date = rsdate;
        DeskAux.IsGone = isgone;
        DeskAux.Series = series;
        DeskAux.Number = number;
        CHECK( getEQ(DeskAux), "Внутренняя ошибка. Не удалось подкачать доп. информацию кассы по рекордсету." );
        SetRecordAddr( IngotAux, DeskAux, 0, FldOffset( DeskAux, "Info" ), true );
    end;

    private
    macro selectCasvl( valueref ) /* : rsdrecordset */
        var cmd, rs;
        cmd = rsdcommand(
            "SELECT casvl.T_CODINTVALUE codintvalue, casvl.t_NAMEVALUE ingotName"
            "  FROM dsb_casvl_dbt CASVL"
            " WHERE casvl.t_REFVALUE = :valueref"
            "   AND casvl.T_TYPEVALUE = :typevalue"
            );
        cmd.addparam( "valueref", rsdbp_in, valueref );
        cmd.addparam( "typevalue", rsdbp_in, TYPERES_MINCAPISSUE + CI_INGOT );
        cmd.execute();
        rs = rsdrecordset( cmd );
        return rs;
    end;

    private
    macro selectCiMisc( issueID ) /* : rsdrecordset */
        var cmd, rs;
        cmd = rsdcommand(
            "SELECT misc.t_MATERIALREF materialref, misc.t_WEIGHT weight,"
            "       misc.t_QUALITYREF qualityref, misc.t_BOX isBox, misc.t_USEWEIGHTTYPE useWeightType"
            "  FROM dci_misc_dbt MISC"
            " WHERE misc.t_TYPE = :type"
            "   AND misc.t_ISSUEID = :issueid"
            );
        cmd.addparam( "type", rsdbp_in, CI_INGOT );
        cmd.addparam( "issueid", rsdbp_in, issueid );
        cmd.execute();
        rs = rsdrecordset( cmd );
        return rs;
    end;

    private
    macro selectCiCode( autokey ) /* : rsdrecordset */
        var cmd, rs;
        cmd = rsdcommand(
            "SELECT code.t_STRING name"
            "  FROM dci_code_dbt code"
            " WHERE code.t_AUTOKEY = :autokey"
            );
        cmd.addparam( "autokey", rsdbp_in, autokey );
        cmd.execute();
        rs = rsdrecordset( cmd );
        return rs;
    end;

    private /* Выбираем из БД признак станадартности слитка */
    macro selectStardartIngotIndication( materialref, weight ) : STRING
        var cmd, rs;
        cmd = rsdcommand(
            "SELECT code.t_STRING standartIngotIndication"
            "  FROM dci_code_dbt code"
            " WHERE code.t_group = 5"
            "   AND code.t_uplink = :materialref"
            "   AND code.t_double = :weight"
            );
        cmd.addparam( "materialref", rsdbp_in, materialref );
        cmd.addparam( "weight", rsdbp_in, weight );
        cmd.execute();
        rs = rsdrecordset( cmd );
        CHECK( rs.movenext, "Не найден признак стандартности слитка" );
        return rs.value("standartIngotIndication");
    end;

    private
    macro appendIngotIndices( materialref, weight )
       var standartIngotIndication = selectStardartIngotIndication(materialref, weight);
       
       if( standartIngotIndication == SET_CHAR )
           push_back( standartIngotIndices, currentIndex ); 
       elif( standartIngotIndication == strfor(1) )
           push_back( measuredIngotIndices, currentIndex ); 
       else
           throw( exception("Некорректный признак стандартности слитка") );
       end;
    end;

    private
    macro name_alg( type, item ) : STRING
        var cmd, rs;
        cmd = rsdcommand(
            "SELECT alg.t_SZNAMEALG nameAlg"
            "  FROM dnamealg_dbt ALG"
            " WHERE alg.t_iTypeAlg = :type"
            "   AND alg.t_iNumberAlg = :item"
            );
        cmd.addparam( "type", rsdbp_in, type );
        cmd.addparam( "item", rsdbp_in, item );
        cmd.execute();
        rs = rsdrecordset( cmd );
        CHECK( rs.movenext, "Не найдено качество слитка" );
        return rs.value("nameAlg");
    end;
    
    private
    macro getQuality( qualityRef, isBox )
        var quality = "";
        var ALG_QUALITY_TYPE = 1201;
        var ALG_EXTENDED_QUALITY_TYPE = 1202;
        
        quality = name_alg(ALG_QUALITY_TYPE, qualityRef);
        
        if( isBox == SET_CHAR )
            accumulate( quality, "; в футляре" );
        end;
        
        return quality;
    end;

    private
    macro selectIngRate( issueID ) /* : rsdrecordset */
        var cmd, rs;
        cmd = rsdcommand(
            "SELECT rate.t_sellrate sellrate"
            "  FROM ding_rate_dbt rate"
            " WHERE rate.t_type = :type"
            "   AND rate.t_issue = :issueid"
            " ORDER BY rate.t_date DESC, rate.t_time DESC, rate.t_autokey DESC"
            );
        cmd.addparam( "type", rsdbp_in, CI_INGOT );
        cmd.addparam( "issueid", rsdbp_in, issueid );
        cmd.execute();
        rs = rsdrecordset( cmd );
        return rs;        
    end;

    private /* Подкачиваем наименования металла, слитка и распределяем индекс слитка (мерный или стандартный) */
    macro pumpMaterialDescription( valueref, ingotName, metalName, quality, sellrate, producer, useWeightType )
        var casvlRs;
        var cimiscRs;
        var ingRateRs;
        var cicodeRs;
        var tmpflag;

        casvlRs = selectCasvl( valueref );
        CHECK( casvlRs.movenext, "Не найден ресурс кассы" );
        /* Подкачиваем название слитка драгметалла */
        setparm( 2, caseOp(casvlRs.value("ingotName") != strfor(1), casvlRs.value("ingotName"), "") );

        cimiscRs = selectCiMisc( casvlRs.value("codintvalue") );
        CHECK( cimiscRs.movenext, "Не найден вид ценности" );
        setparm( 7, cimiscRs.value("useWeightType") ); 

        /* Дополняем индексы записей слитков */
        appendIngotIndices( cimiscRs.value("materialref"), cimiscRs.value("weight") / 10000 );
        
        /* Подкачиваем наименование металла */
        cicodeRs = selectCiCode( cimiscRs.value("materialref") );
        CHECK( cicodeRs.movenext, "Не найдено наименование металла" );       
        setparm( 3, caseOp(cicodeRs.value("name") != strfor(1), cicodeRs.value("name"), "") ); 
        
        /* Подкачиваем качество металла */
        setparm( 4, getQuality(cimiscRs.value("qualityref"), cimiscRs.value("isBox")) );

        /* Подкачиваем курс продажи */
        ingRateRs = selectIngRate( casvlRs.value("codintvalue") );
        CHECK( ingRateRs.movenext, "Не найден курс драгметалла" );
        setparm( 5, ingRateRs.value("sellrate") );


        /* Подкачиваем производителя */
        cicodeRs = selectCiCode( IngotAux.produceRef );
        tmpflag = cicodeRs.movenext;
        producer =  caseOp(tmpflag, cicodeRs.value("name"), "");        
        setparm( 6,  producer );
    end;

    private
    macro pumpSingleIngotRecord() /* : IngotReportRecord */
        var ingreprec = IngotReportRecord;
        
        pumpMaterialDescription( DeskAux.valueref, ingreprec.ingotName, ingreprec.metalName, ingreprec.quality, ingreprec.sellrate, ingreprec.producer, ingreprec.useWeightType );
        ingreprec.ingotCount = 1;
        ingreprec.ligatureWeight = ingotAux.nominalWeight / 10000;
        ingreprec.chemicalWeight = ingotAux.chemicalWeight / 10000;
        ingreprec.etc = string( RegNumToStr(DeskAux.series, DeskAux.number) );

        ingreprec.mark = ingotAux.mark;
        ingreprec.serial = string( RegNumToStr(DeskAux.series, DeskAux.number) );
        ingreprec.hallmark = ingotAux.hallmark;
        ingreprec.specs = ingotAux.specs;
        ingreprec.year = ingotAux.year;

        return ingreprec;
    end;

    macro pumpIngotRecords()
        var caslnRs;
        var bunchRs;
        var deskAuxRs;

        /* 1 */
        caslnRs = selectCasln();

        /* 2 */
        while( caslnRs.movenext )
            bunchRs = selectBunch( caslnRs.value("appkey2"), caslnRs.value("appkind2") );

            /* 3 */
            while( bunchRs.movenext )
                deskAuxRs = selectDeskAux( bunchRs.value("series"), bunchRs.value("min"), caslnRs.value("refvalue2") );

                /* 4 */
                if( deskAuxRs.movenext )
                    /* 5 */
                    
pumpDeskAuxWithBlob( deskAuxRs.value("branch"), deskAuxRs.value("valueref"), deskAuxRs.value("rsdate"),
                        deskAuxRs.value("isgone"), deskAuxRs.value("series"), deskAuxRs.value("rsnumber") );
                    /* 6 */
                    currentIndex = ingotRecords.size;
                    ingotRecords[currentIndex] = pumpSingleIngotRecord();
                end;
            end;
        end;        
    end;
    
    private
    macro pumpIngotSummary( summary, indices )
      var pos = 0;
      var idxPos = 0;

      summary.totalIngotCount = 0;
      summary.totalIngotCountInWords = "";
      summary.totalLigatureWeight = 0.0;
      summary.totalLigatureWeightInWords = "";
      summary.totalChemicalWeight = 0.0;
      summary.totalChemicalWeightInWords = "";
      summary.sum = 0.0;
      summary.sumInWords = "";
      summary.totalEtc = "";
      
      while( idxPos < indices.size )
        pos = indices[idxPos];
        accumulate( summary.totalIngotCount, ingotRecords[pos].ingotCount );
        accumulate( summary.totalLigatureWeight, ingotRecords[pos].ligatureWeight );
        accumulate( summary.totalChemicalWeight, ingotRecords[pos].chemicalWeight );
        accumulate( summary.sum, ingotRecords[pos].sellrate * ingotRecords[pos].ingotCount );
        accumulate( idxPos, 1 );
      end;

      summary.totalIngotCountInWords = NumInWords(summary.totalIngotCount);
      summary.totalLigatureWeightInWords = NumInWords(summary.totalLigatureWeight);
      summary.totalChemicalWeightInWords = NumInWords(summary.totalChemicalWeight);
      summary.sumInWords = TRIM(STRING(MONEY( summary.sum ):m));
      
      summary.ending_1 = makeEndingForMultipleCount( summary.totalIngotCount );
    end;

    private
    macro pumpClientInfo( codclient )
        var clnt = TClientList;
        var dep = TDepClient;
        var cliPassStr = "";

        CHECK( clnt.GetRecord(codclient), "Клиент в базе данных не найден" );        
        dep = clnt.CurRec;
        
        clientName = dep.rec.Name1 + " " + dep.rec.Name2 + " " + dep.rec.Name3;
        cliPassStr = "";
        accumulate( cliPassStr, dep.makedocstr );          /* Вид_документа */
        accumulate( cliPassStr, " " + dep.rec.PaperSeries );    /* Серия_документа */
        accumulate( cliPassStr, " " + dep.rec.PaperNumber );    /* Номер_документа */
        accumulate( cliPassStr, " " + dep.rec.PaperIssuedDate );  /* Дата_выдачи_док */
        accumulate( cliPassStr, " " + dep.rec.PaperIssuer );    /* Организация_выд_док */

        clientPassport = strsplit2( cliPassStr, 66, 84, 2 );
        CHECK( clientPassport.size >= 2, "Внутренняя ошибка программы" );
    end;

    private
    macro getIngotNameInGenitiveCase( codecurr : INTEGER ) : STRING
        var str = "";

        if( codecurr == 98 )
            str = "золотых";
        elif( codecurr == 99 )
            str = "серебряных";
        elif( codecurr == 76 )
            str = "платиновых";
        elif( codecurr == 33 )
            str = "палладиевых";
        end;

        return str;
    end;

    macro ExistsIngotWithUseWeightType()
       var i = 0;    
       
       while ( i < ingotRecords.Size )
           if ( ingotRecords(i).useWeightType != 0 )
               return ingotRecords(i).useWeightType;       
           end;
           i = i + 1;            
       end;      
       
       return 0;
    end;
    
    macro pump()
        ingotNameInGenitiveCase = getIngotNameInGenitiveCase( ALG_SBDEPDOC.code_currency );
        operName = getOperName( {oper} );
    
        /* header */
        actNumber = pumpActNumber( ACT_COUNTER_NUMBER ); 
        actDate = {curdate};
        branch = pumpBranch( fncash );
        pumpClientInfo( ALG_SBDEPDOC.codclient );

        /* body */
        pumpIngotRecords();

        /* footer */
        pumpIngotSummary( standartIngotSummary, standartIngotIndices );
        pumpIngotSummary( measuredIngotSummary, measuredIngotIndices );
        measuringDevice = panel_actTake.measuringDevice;
        licenseNumber = panel_actTake.licenseNumber;
        licenseDate = panel_actTake.licenseDate;
        specificationNumber = panel_actTake.specificationNumber;
        specificationDate = panel_actTake.specificationDate;
        
        useWeightType = ExistsIngotWithUseWeightType();    
         
        if( panel_actTake.consistency == SET_CHAR )
            reclamationNumber = "";
            reclamationDate = "";
        else
            reclamationNumber = panel_actTake.reclamationNumber;
            reclamationDate = panel_actTake.reclamationDate;
        end;

        if( panel_actTake.inconsistentIngotBatch == SET_CHAR )
            ending_1 = "ов";
            ending_2 = "ых";
        else
            ending_1 = "а";
            ending_2 = "ого";
        end;
    end;
END;

private
class ActTakeReport ( _prmode )
    private var data = ActTake_data;
    private var prmode = _prmode; /*режим печати отчета*/

    private
    macro printActHeader( ingotTypeStr )
    [
                                                                    +--------------------+
                                                                    |     Код формы      |
                                                                    | документа по ОКУД  |
                                                                    +--------------------+
                                                                    |       0402047      |
                                                                    +--------------------+

                              Акт приема-передачи драгоценных металлов

         N ##################                                ################################

        Мы, нижеподписавшиеся ###############################################################
                           (полное фирменное или сокращенное фирменное наименование кредитной
                         организации (полное фирменное или сокращенное фирменное наименование
                                        кредитной организации и наименование филиала))
        __________________________________________________________________, с одной стороны,
             (фамилии и инициалы работников кредитной организации,
                        филиала кредитной организации)
        #####################################################################################
          (фирменное наименование юридического лица, фамилия, имя и (в случае если имеется)
           отчество представителя юридического лица, фамилия, имя и (в случае если имеется)
            отчество индивидуального предпринимателя, физического лица (их представителей)
        ####################################################################################
        ##################################################################, с другой стороны
        (серия, номер, дата выдачи, наименование органа и код подразделения (если
           имеется), выдавшего документ, удостоверяющий личность представителя
           юридического лица, индивидуального предпринимателя, физического лица
         (их представителей), данные доверенности представителя юридического лица,
                    индивидуального предпринимателя, физического лица)
        *
        составили настоящий  акт о том, что одни выдали, а другие получили следующие
        драгоценные металлы:


        +------------+------------+-------------+-------------------------+--------------------+
        |Наименование| Вид слитка |  Количество |       Масса, грамм      |        Иная        |
        |драгоценного|драгоценного|   слитков   +------------+------------+     Информация     |
        |   металла  |   металла  |драгоценного | Лигатурная |  Химически |                    |
        |            |            |металла, штук|            |   чистая   |                    |
        |            |            |             |            |(для золота)|                    |
        +------------+------------+-------------+------------+------------+--------------------+
        |      1     |     2      |      3      |      4     |     5      |          6         |
        +------------+------------+-------------+------------+------------+--------------------+]
        (data.actNumber, data.actDate, data.branch, data.clientName, data.clientPassport[0], data.clientPassport[1]); 
    end;

    private
    macro printActBody( indices /* : TARRAY */ )
        var idxPos = 0;
        var pos = 0;
        
        while( idxPos < indices.size )
            pos = indices[idxPos];
            [   |############|############|#############|############|############|####################|]
                (data.ingotRecords[pos].metalName, data.ingotRecords[pos].ingotName, data.ingotRecords[pos].ingotCount,
                 data.ingotRecords[pos].ligatureWeight, data.ingotRecords[pos].chemicalWeight, data.ingotRecords[pos].etc);
            [   +------------+------------+-------------+------------+------------+--------------------+];
            accumulate( idxPos, 1 );
        end;
    end;

    private
    macro actTake_Footer_Header( summary )
        [   | ИТОГО      |            |#############|############|############|####################|]
            (summary.totalIngotCount, summary.totalLigatureWeight, summary.totalChemicalWeight, summary.totalEtc);
        [   +------------+------------+-------------+------------+------------+--------------------+];

        [   Вышеперечисленные  драгоценные металлы подвергнуты визуальному контролю и
        пересчету.
            Контрольное    взвешивание    каждого   слитка   драгоценных   металлов
        осуществлялось   средствами   измерений   драгоценных   металлов  кредитной
        организации марки ##############. Свидетельство о поверке средств измерений
        драгоценных металлов от ########## № ##########.]
        (data.measuringDevice, data.licenseNumber, data.licenseDate);
    end;

    private
    macro actTake_Footer_ConsistencyBody()
    [
        В случае отсутствия расхождений и/или рекламаций
            Данные   визуального   контроля,  пересчета,  контрольного  взвешивания
        каждого слитка  драгоценного  металла соответствуют данным спецификации № ##########
        от ##########. (у физического лица - при ее наличии) и документа о качестве.]
        (data.specificationNumber, data.specificationDate);
    end;

    private
    macro actTake_Footer_InconsistencyBody()
    [
        В случае наличия расхождений и/или рекламаций
            В результате взвешивания и проверки ценностей обнаружены несоответствия,
        отраженные в Рекламационном акте № ######### от ##########. Прием-передача  
        производилась в соответствии со спецификацией  № ########## от ##########. 
        за исключением  слитк###, указанн### в Рекламационном  акте № ########## от ##########.]
        (data.reclamationNumber,  data.reclamationDate,
         data.specificationNumber, data.specificationDate,
         data.ending_1, data.ending_2,
         data.reclamationNumber,  data.reclamationDate);
    end;

    private
    macro actTake_Footer_Footer( summary )

    [
            Приложение: спецификация № ######### от ##########

            Примечание:



        Драгоценные металлы выдали:
        ____________________________  ________________  __________________________
          (наименование должности)    (личная подпись)      (фамилия и инициалы)
        ____________________________  ________________  ___________________________
          (наименование должности)    (личная подпись)      (фамилия и инициалы)
        ____________________________  ________________  ___________________________
          (наименование должности)    (личная подпись)      (фамилия и инициалы)

        Драгоценные металлы приняли:
        ____________________________  ________________  __________________________
          (наименование должности)    (личная подпись)      (фамилия и инициалы)
        ____________________________  ________________  ___________________________
          (наименование должности)    (личная подпись)      (фамилия и инициалы)
        ____________________________  ________________  ___________________________
          (наименование должности)    (личная подпись)      (фамилия и инициалы)
          
         
         Место печати]
         (data.actNumber, data.actDate);
    end;

    private
    macro printActFooter( summary )

        actTake_Footer_Header( summary );
        
        actTake_Footer_ConsistencyBody();

        if( panel_actTake.consistency != SET_CHAR )
            actTake_Footer_InconsistencyBody();
        end;
        
        /*if( panel_actTake.consistency == SET_CHAR )
            actTake_Footer_ConsistencyBody();
        else
            actTake_Footer_InconsistencyBody();
        end; */
        
        actTake_Footer_Footer();    
    end;
    macro pump()
        data.pump();
    end;

    macro formatSummaryData( summary )
        var pos = 0;

        summary.totalLigatureWeight = STRING(summary.totalLigatureWeight:11:1);
        summary.totalChemicalWeight = STRING(summary.totalChemicalWeight:11:1);
        summary.sum = STRING(summary.sum:11:2);
    end;

    macro formatData()
        var pos = 0;
    
        data.actNumber = INT(data.actNumber);
        data.actDate = STRING(data.actDate:m);

        pos = 0;
        while( pos < data.ingotRecords.size )
            /*data.ingotRecords[pos].etc = INT(data.ingotRecords[pos].etc);*/
            data.ingotRecords[pos].ligatureWeight = STRING( data.ingotRecords[pos].ligatureWeight:11:1 );
            data.ingotRecords[pos].chemicalWeight = STRING( data.ingotRecords[pos].chemicalWeight:11:1 );
            data.ingotRecords[pos].hallmark = STRING( data.ingotRecords[pos].hallmark:5:1 );
            accumulate( pos, 1 );
        end;

        formatSummaryData( data.standartIngotSummary );
        formatSummaryData( data.measuredIngotSummary );
    end;
    macro printSpecificationHeader( ingotTypeStr : STRING )
        [

                                                                                        Ф. № 259


             ПРИЛОЖЕНИЕ __________                            (ПРИЛОЖЕНИЕ ___________           
             к Акту приема-передачи                           к _____________________           
             драгоценных металлов № #######                   (вид договора/соглашения)         
             ##############################                   № ________ от____________200__г.)*



                                              СПЕЦИФИКАЦИЯ № #######
                                      ############## в ########### слитках
                                         (металл)         (вид)
             +-------+-----------+--------------------+------------+-----------+-------+----------------+------+---------------------------+
             |       |           |                    | Лигатурная | Химически |       |                |      |                           |
             |   №   |  Марка    |          №№        |   масса,   |   чистая  | Проба |  №№ заводских  |  Год |        Примечание         |
             |  п/п  |           |       слитков      |    грамм   |   масса,  |       |  спецификаций  |      |                           |
             |       |           |                    |            |  грамм**  |       |                |      |                           |
             +-------+-----------+--------------------+------------+-----------+-------+----------------+------+---------------------------+]
             (data.actNumber, data.actDate, data.actNumber, data.ingotRecords[0].metalName, ingotTypeStr);

    end;

    macro printSpecificationBody( indices /* : TARRAY */ )
        var idxPos = 0;
        var pos = 0;
        
        while( idxPos < indices.size )
            pos = indices[idxPos];
            [    | ##### |###########|####################| ###########|###########| ######| ############## | #### | ######################### |
                 +-------+-----------+--------------------+------------+-----------+-------+----------------+------+---------------------------+
            ]
            (idxPos+1, data.ingotRecords[pos].mark, data.ingotRecords[pos].serial, data.ingotRecords[pos].ligatureWeight, data.ingotRecords[pos].chemicalWeight,
             data.ingotRecords[pos].hallmark, data.ingotRecords[pos].specs, data.ingotRecords[pos].year,
             "" /*"Качество: " + data.ingotRecords[pos].quality*/);
            accumulate( idxPos, 1 );
        end;
    end;

    macro printSpecificationFooter( summary )

        var magicString = 
            "Итого: " + cut(summary.totalIngotCount, 2) + " (" + cut(summary.totalIngotCountInWords) + ")" +
            " слит" + cut(summary.ending_1, 3) +
            " общей лигатурной массой " +
            summary.totalLigatureWeight + " (" + cut(summary.totalLigatureWeightInWords, 100) + ") грамм" +
            caseOp( data.useWeightType, 
                ", массой в химической чистоте " +  summary.totalChemicalWeight + " (" + cut(summary.totalChemicalWeightInWords, 100) + ") грамм",
                ""
                ) +
            " на общую сумму " + substr( string(summary.sum), 1, index(string(summary.sum),".")-1 ) + " руб. " + 
              substr( string(summary.sum), index(string(summary.sum),".")+1 ) + " коп. (" + cut(summary.sumInWords, 100) + ")" +
            " и сопроводительных документов в количестве ___ (______________) лист__."
            ;
        var magicParts = strsplit2( magicString, 110 );
        var pos = 0;

        while( pos < magicParts.size )
            print( "\n    " + magicParts[pos] );
            accumulate( pos, 1 );
        end;
    
        [            

             Составили:

                                                                                 ################

             _______________________________

             * - графы заполняются при совершении операций с юридическими лицами 
             **  масса в чистоте проставляется только для золота]
       ( data.opername );
    end;

    private 
    macro printIngotListHeader( ingotTypeStr )   
        [
            Место для штампа
            учреждения СБ РФ

                                                    Список  ############ ############ слитков,
                                            принятых на обезличенные металлические счета физических лиц
                                                                ################## г.



            +----------------------------------------------------------------------------------------------------------------------------------------------------+
            |№ п/п|          ФИО клиента         |Масса слитка,|     Номер слитка        |         Качество, проба           |          Завод-изготовитель       |
            |     |                              |   грамм     |                         |                                   |                                   |
            +----------------------------------------------------------------------------------------------------------------------------------------------------+]
        ( data.ingotNameInGenitiveCase, ingotTypeStr, data.actDate );
    end;

    private 
    macro printIngotListBody( indices )
        var idxPos = 0;
        var pos = 0;
        
        while( idxPos < indices.size )
            pos = indices[idxPos];
            [   | ### |##############################|#############|#########################|###################################|###################################|
                +----------------------------------------------------------------------------------------------------------------------------------------------------+]
            ( idxPos, data.clientName,
              caseOp(data.useWeightType, data.ingotRecords[pos].chemicalWeight, data.ingotRecords[pos].ligatureWeight),
              data.ingotRecords[pos].serial,
              data.ingotRecords[pos].quality + " - " + data.ingotRecords[pos].hallmark,
              data.ingotRecords[pos].producer );
              
            accumulate( idxPos, 1 );
        end;    
    end;

    private 
    macro printIngotListFooter( summary )
        var sum = caseOp(data.useWeightType, summary.totalChemicalWeight, summary.totalLigatureWeight);
    
        [   |Итого|                              |#############|                         |                                   |                                   |
            +----------------------------------------------------------------------------------------------------------------------------------------------------+

            * при принятии от одного клиента нескольких слитков, они отражаются по отдельности, после чего по ним подводится итог:
            в графе "№ п/п" указывается: "Итого", графе "Масса слитка, грамм" указывается суммарный вес принятых слитков.


            Итого     ##################### граммов

            Контролер _____________________(######################)
                         (подпись)        (фамилия, инициалы)

            Кассир   _____________________(______________________)
                           (подпись)      (фамилия, инициалы)

            М.П.]
        ( sum, sum, data.opername );
    end;

    private
    macro printAct( indices /* TARRAY из IngotReportRecord */,
                     summary /* IngotReportSummaryRecord */,
                     ingotTypeStr /* стандартных или мерных */ )
        printActHeader( ingotTypeStr ); 
        printActBody( indices );
        printActFooter( summary );
    end;

    private
    macro printSpecification( indices /* TARRAY из IngotReportRecord */,
                                summary /* IngotReportSummaryRecord */,
                                ingotTypeStr /* стандартных или мерных */ )
        printSpecificationHeader( ingotTypeStr ); 
        printSpecificationBody( indices );
        printSpecificationFooter( summary );
    end;

    private
    macro printIngotList( indices /* TARRAY из IngotReportRecord */,
                            summary /* IngotReportSummaryRecord */,
                            ingotTypeStr /* стандартных или мерных */ )
        printIngotListHeader( ingotTypeStr ); 
        printIngotListBody( indices );
        printIngotListFooter( summary );        
    end;

    private
    macro printByIngotType( indices /* TARRAY из IngotReportRecord */,
                              summary /* IngotReportSummaryRecord */,
                              ingotTypeStr /* стандартных или мерных */
                          )
        
        if ( prmode != PRMODE_LIST ) 
            if( indices.size > 0 )  
                printAct( indices, summary, ingotTypeStr );
  
                if ( (prmode == PRMODE_ACT_SPEC_LIST) or (prmode == PRMODE_ACT_SPEC) )
                printSpecification( indices, summary, ingotTypeStr );         
            end; 
        end;
        end;

        if ( ( prmode == PRMODE_LIST ) or ( prmode == PRMODE_ACT_SPEC_LIST ) )
            if( indices.size > 0 ) 
                printIngotList( indices, summary, ingotTypeStr ); 
            end;
        end;
    end;
    
    macro print()
        printByIngotType( data.standartIngotIndices, data.standartIngotSummary, "стандартных" );
        printByIngotType( data.measuredIngotIndices, data.measuredIngotSummary, "мерных" );        
    end;
END;

MACRO MakeReport( prmode, /* режим печати*/
                  copies /* количество копий отчетных документов */ )
    /* по умолчанию печатается все подряд в одном экземпляре */  
    if ( (ValType( prmode ) == V_UNDEF) )
        prmode = PRMODE_ACT_SPEC_LIST;    
    end;
 
    if ( (ValType( copies ) == V_UNDEF) or (copies <= 0) )
        copies = 1;
    end;

    if ( (prmode != PRMODE_LIST) and not RunActTakePanel() )
        if ( (prmode == PRMODE_ACT_SPEC) or (prmode == PRMODE_ACT) )
            return;
        else
            prmode = PRMODE_LIST;  
        end;     
    end;     

    var report = ActTakeReport( prmode );    

    report.pump();
    report.formatData();

    while ( copies > 0 )
    report.print();
        copies = copies - 1;
    end;

onError( errObj );
    msgbox( what(errObj) );  
END;
     