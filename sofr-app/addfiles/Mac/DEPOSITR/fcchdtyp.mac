/* Перевод в другой вид вклада */

import deprintr,DeskInter,OperCode,alg_vars;

file CDTDep("depositr.dbt") key 8 write;
file SBDepDoc("sbdepdoc.dbt") key 2 write;

record sbdepdocChDTyp  ("sbdepdoc.3");

const
  CDT_SB_DEPOSIT = 1,
  CDT_SB_FCDEPDOC = 10;

const
  ACCOUTN_OWNER = StrFor( 0 );

const
  NewType  = "Пенсионный";

const
  MINDATE = Date(01, 01, 1900),
  MAXDATE = Date(31, 12, 2999);

const
  UPDATE_ALL  = 1,
  UPDATE_DEP  = 2,
  UPDATE_DOC  = 4;

const
  Binsert = 2;

const
  D_IN = 1,         /* Приход */
  D_OUT = 2,        /* Расход */
  D_GET = 3,        /* Списание */
  D_PUT = 5;        /* Зачисление */

var
  {oper},
  {curdate},
  OperBrigade = GetOperBrigade,
  Branch = NumFNCash,
  OldType,
  TrnStatus = true,
  TxtDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true );
var OldPosDepDoc;

macro CreateSBDepDocs

  record OpParm( "op_parm.rec" );

  var
    SBDepDoc_NumDayDoc, Rest, PercRest;

  if(TrnStatus)
    ClearRecord( SBDepDoc );
    SBDepDoc.Referenc = ALG_DEPOSITOR.Referenc;
    SBDepDoc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document; /*MAXDATE;*/
    SBDepDoc.Date_Document = MAXDATE;
    SBDepDoc.NumDayDoc = 32767;
    TrnStatus = GetLE(SBDepDoc);
  end;
  if(TrnStatus and (SBDepDoc.Referenc == ALG_DEPOSITOR.Referenc))
     if(SBDepDoc.DepDate_Document == ALG_SBDEPDOC.DepDate_Document)
       SBDepDoc_NumDayDoc = SBDepDoc.NumDayDoc + 100;
     else
       SBDepDoc_NumDayDoc = 100;
     end;
     Rest = SBDepDoc.Rest;
     PercRest = SBDepDoc.PercRest;
     if(TrnStatus)
       ClearRecord( SBDepDoc );
       SBDepDoc.FNCash = ALG_DEPOSITOR.FNCash;
       SBDepDoc.Referenc = ALG_DEPOSITOR.Referenc;
       SBDepDoc.IsCur = ALG_DEPOSITOR.IsCur;
       SBDepDoc.Code_Currency = ALG_DEPOSITOR.Code_Currency;
       SBDepDoc.CodClient = ALG_DEPOSITOR.CodClient;
       SBDepDoc.Author = ACCOUTN_OWNER;
       SBDepDoc.Account = ALG_DEPOSITOR.Account;
       SBDepDoc.Type_Account = OldType;
       SBDepDoc.TypeOper = Перевод_В_Другой_Вид_Вклада;
       SBDepDoc.TypeComplexOper = Перевод_В_Другой_Вид_Вклада;
       SBDepDoc.ApplType = 1;
       SBDepDoc.VidDoc = 0;
       SBDepDoc.KindOp = D_GET;
       SBDepDoc.iApplicationKind=CDT_SB_DEPOSIT;
       SBDepDoc.ApplicationKey=FormApplicationKey(CDT_SB_DEPOSIT);
       SBDepDoc.InSum = $0;
       SBDepDoc.OutSum = Rest;
       SBDepDoc.PercOprSum = $0;
       SBDepDoc.Rest = $0;
       SBDepDoc.PercRest = PercRest;
       SBDepDoc.oper = {oper};
       SBDepDoc.Brigade = OperBrigade;
       SBDepDoc.ObjectPerc = 2002;
       SBDepDoc.Ground = "Смена вида вклада с \"" + OldType + "\" на \"" +
         NewType + "\"";
       SBDepDoc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
       SBDepDoc.Date_Document = {curdate};
       SBDepDoc.NumDayDoc = SBDepDoc_NumDayDoc;
       TrnStatus=Insert(SBDepDoc);

       if ( TrnStatus and ( not InterDesk_IsDocBunchActive ) )
         ClearRecord( OpParm );
         OpParm.Date = {curdate};
         OpParm.Operation = Перевод_В_Другой_Вид_Вклада;
         OpParm.ObjectRef = ALG_DEPOSITOR.Referenc;
     
         TrnStatus = InterDesk_InitDocBunch( CDT_SB_DEPOSIT, SBDepDoc.ApplicationKey, OpParm );
       end; 
       if(TrnStatus)
         TrnStatus = CashLink_AddRec( CDT_SB_DEPOSIT, SBDepDoc.ApplicationKey );
       end;
       if(TrnStatus)
         SBDepDoc.Account = ALG_DEPOSITOR.Account;
         SBDepDoc.ApplType = 2;
         SBDepDoc.KindOp = D_PUT;
         SBDepDoc.InSum = Rest;
         SBDepDoc.OutSum = $0;
         SBDepDoc.Rest = Rest;
         SBDepDoc.ObjectPerc = 2001;
         SBDepDoc.Type_Account = NewType;
         SBDepDoc.NumDayDoc = SBDepDoc.NumDayDoc + 100;
         SBDepDoc.ApplicationKey=FormApplicationKey(CDT_SB_DEPOSIT);
         TrnStatus=Insert(SBDepDoc);
       end;
       if(TrnStatus)
         TrnStatus = CashLink_AddRec( CDT_SB_DEPOSIT, SBDepDoc.ApplicationKey );
       end;
     end;
  end;
end;

/*
  Головная процендура
*/
macro TrnProcedure;

  OldType = ALG_DEPOSITOR.Type_Account;
  CreateSBDepDocs;
  if(not TrnStatus)
    AbortTrn;
  end;
end;

macro Main

  ProcessTrn( 1, "TrnProcedure", CDTDep, SBDepDoc );

  if(TrnStatus)
    ALG_RESULT = OK_RES;
  else
    ALG_RESULT = ERR_RES;
  end;
end;

/***********************************************************
  Точка входа
***********************************************************/
Main
End.
