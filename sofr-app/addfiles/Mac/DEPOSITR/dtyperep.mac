
/* Отчет по настройкам вида вклада */

import deprintr, RSD;

file DepType  ("sb_dtyp.dbt" ) key 2;
file ApplType ("pc_apltp.dbt") key 0;
file Operation("sb_typop.dbt") key 0;
file SubOp    ("suboper.dbt" ) key 1;
file OpStep   ("sb_algop.dbt") key 0;
file PcAlg    ("pc_alg.dbt"  ) key 0;
file PcRate   ("pc_rate.dbt" ) key 0;
file PcMacro  ("pc_macro.dbt") key 0;
file PcRCalc  ("pc_rcalc.dbt") key 0;
file NAlg     ("namealg.dbt", "bank.def" ) key 0;
file Curr     ("currency.dbt") key 0;

const
  OBJ_ACCTYPE = 1003,
  OBJ_DEP     = 2001,
  OBJ_ALT     = 2002,
  OBJ_OVER    = 2003,
  OBJ_FURTHER = 2004;

var
  IsCur = NumFlagCur,
  HeaderSuffix = "";

array
  IsCurStr;

  IsCurStr( 0 ) = "рублевый";
  IsCurStr( 1 ) = "валютный";

array
  TermTypes;

  TermTypes( 0 ) = " ";
  TermTypes( 1 ) = "Усл.дней";
  TermTypes( 2 ) = "Месяцев";
  TermTypes( 3 ) = "Кварталов";
  TermTypes( 4 ) = "Лет";
  TermTypes( 5 ) = "Кал.дней";

array
  ApplTypeStr;

  ApplTypeStr( OBJ_DEP ) = "По основным условиям";
  ApplTypeStr( OBJ_ALT ) = "При нарушении условий";
  ApplTypeStr( OBJ_OVER ) = "По овердрафту";
  ApplTypeStr( OBJ_FURTHER ) = "По доп. взносам";

/* Печать заголовка раздела */

macro OutSubHeader( S )

  S = S + " " + HeaderSuffix;

  PrintLn;
  PrintLn( s );
  PrintLn( MkStr( "=", StrLen( S ) ) );
  PrintLn;
end;

/* Строка из namealg */

macro NameAlg( Type, ID )

  NAlg.iTypeAlg = Type;
  NAlg.iNumberAlg = ID;
  if ( GetEQ( NAlg ) )
    return NAlg.szNameAlg;
  else
    return "";
  end;
end;

/* Вид графика строкой */

macro GraphStr( Alg, Type, Day )

  var
    S = "";

  if ( Type == 5 )
    S = String( Day ) + 1;
  end;
  S = S + NameAlg( Alg, Type );
  return S;
end;

/* Общие настройки */

macro ListCommonSettings

  OutSubHeader( "Общие настройки" );
  [ Название вида вклада                 #                                        ]
  ( DepType.Name );
  [ Короткое имя                         #                                        ]
  ( DepType.Kind );
  [ Валютность                           #                                        ]
  ( IsCurStr( DepType.FlagCur ) );
  [ Приоритет                            #                                        ]
  ( DepType.Priorit );
  [ Идентификационные символы            ###                                      ]
  ( DepType.NewIdentChar );
  // [ Группа нумерации                     #                                        ]
  // ( DepType.NewGroupNumb );
  [ Показывать данные о клиенте          (#)                                      ]
  ( DepType.ShowCln );
  [ Форма договора                       #                                        ]
  ( NameAlg( 701, DepType.FormContr ) );
  [ Пользовательский тип счета           #                                        ]
  ( DepType.UserTypeAccount );
  [ Срок договора                        #                                        ]
  ( String( DepType.Term ) + " " + TermTypes( DepType.KindTerm ) );
  [ Срок пролонгации                     #                                        ]
  ( String( DepType.Term_Prol ) + " " + TermTypes( DepType.KindTerm_Prol ) );
  [ Вступление в силу параметров %       #                                        ]
  ( NameAlg( 324, DepType.FlagBeginParameter ) );
  [ Дата ближайшего расчета %            ##########                               ]
  ( DepType.DateCalc );
  [ Дата ближайшей оплаты %              ##########                               ]
  ( DepType.DatePay );
  [ Нестандартная нумерация              (#)                                      ]
  ( DepType.FlagNonStdNum );
  [ Спецдоступ                           (#)                                      ]
  ( StrFor( DepType.SpecialAccess ) );
  // [ Балансовый счет - резиденты          #                                        ]
  // ( DepType.BalAcc );
  // [ Балансовый счет - нерезиденты        #                                        ]
  // ( DepType.BalAccNotRez );
  [ Обрабат. догов. при явке вкладчика   (#)                                      ]
  ( DepType.FlagNotProlong );
  [ Причисление %% при заверш. догов.    (#)                                      ]
  ( DepType.PayPercEndCont );
  [ Погаш. %% за оверд. при прих. опер.  (#)                                      ]
  ( DepType.PayPercOverd );
  [ Отдельное прич. %% за прош. периоды  (#)                                      ]
  ( DepType.PayPercPastPer );
  [ Отчисляь %% при нарушении условий    (#)                                      ]
  ( DepType.SubPcOnContrFault );
  [ Рассчитывать наращенные %%           (#)                                      ]
  ( DepType.CalcAddPc );
  [ Запретить отрицательный остаток %%   (#)                                      ]
  ( DepType.PercNoLessZero );
  [ Расч./прич. %% при завершении года   (#)                                      ]
  ( DepType.CalcPcOnEoy );
  [ Обраб. договора при завершении года  (#)                                      ]
  ( DepType.ProlOnEoy );
  [ Метод расчета %%                     #                                        ]
  ( NameAlg( 332, DepType.PercRestOrOper ) );
end;

macro ReportOnApplType( AT )

  ApplType.IsCur = DepType.FlagCur;
  ApplType.TypeRec = OBJ_ACCTYPE;
  ApplType.Type = DepType.Kind;
  ApplType.ApplType = AT;
  if ( GetEQ( ApplType ) )
    [ #                                          #                        ]
    (
      ApplTypeStr( AT ),
      ApplType.ApType
    );
  end;
end;

macro ReportOnApplTypes

  OutSubHeader( "Ссылки на другие виды" );
  ReportOnApplType( OBJ_DEP );
  ReportOnApplType( OBJ_ALT );
  ReportOnApplType( OBJ_OVER );
  ReportOnApplType( OBJ_FURTHER );
end;

macro OutOpHead

  OutSubHeader( "Операции" );

  [-----------------------------------------------------------------------------------------------------------------------];
  [|       |                          |        |          |    |    | |     |      |      |        |          |     |    |];
  [|  Код  |     Н а з в а н и е      |  НП/В  | Дата нач |Мод.|Пан.|C|  I  |  I2  |   L  |    D   |   Date   |№ oп.|Рез.|];
  [|       |                          |        |          |    |    | |     |      |      |        |          |     |    |];
  [-----------------------------------------------------------------------------------------------------------------------];
end;

macro OutOpLine

  [ ####### ##########################    #                                                                               ]
  (
    Operation.NumOpert,
    Operation.czNameAlg:w,
    Operation.OperExe
  );
end;

macro OutSubOpLine

  [ ####### ##########################    #                                                                               ]
  (
    ( String( SubOp.OperType ) + "(" + String( SubOp.Type ) + ")" ):r,
    SubOp.Name:w,
    SubOp.HideFromOper
  );
end;

macro OutOpStepLine

  [ ####### ##########################    #     ########## #### #### # ##### ###### ###### ######## ########## ##### #### ]
  (
    ( String( OpStep.NumOpert ) + "." + String( OpStep.NumStepAlg ) ):r,
    OpStep.czNameAlg:w,
    OpStep.FlagEXE,
    OpStep.BegDate,
    OpStep.ModuleNumber,
    OpStep.TypPanel,
    OpStep.prmC,
    OpStep.prmI,
    OpStep.prmI2,
    OpStep.prmL,
    OpStep.prmD,
    OpStep.prmC1,
    OpStep.prmC2,
    OpStep.prmDate,
    OpStep.prmNumOper,
    OpStep.prmRez
  );
end;

macro ReportOnSubOps

  var
    HeadPrinted = false,
    RecFound;

  ClearRecord(SubOp);
  SubOp.IsCur    = Operation.IsCur;
  SubOp.Kind     = Operation.Kind;
  SubOp.OperType = Operation.NumOpert;
  SubOp.Type     = 0;
  RecFound = GetGE(SubOp);
  while (RecFound                                AND
         (SubOp.IsCur    == Operation.IsCur    ) AND
         (SubOp.Kind     == Operation.Kind     ) AND
         (SubOp.OperType == Operation.NumOpert )    )
    if (not HeadPrinted)
      PrintLn;
      PrintLn( "Подвиды операции" );
      PrintLn( "----------------" );
      PrintLn;
      HeadPrinted = true;
    end;
    OutSubOpLine;
    RecFound = Next( SubOp );
  end;
end;

macro ReportOnOpSteps

  var
    HeadPrinted = false,
    RecFound;

  ClearRecord( OpStep );
  OpStep.IsCur = Operation.IsCur;
  OpStep.Kind = Operation.Kind;
  OpStep.NumOpert = Operation.NumOpert;
  RecFound = GetGE( OpStep );
  while ( RecFound
    and ( OpStep.IsCur == Operation.IsCur )
    and ( OpStep.Kind == Operation.Kind )
    and ( OpStep.NumOpert == Operation.NumOpert ) )
    if ( not HeadPrinted )
      PrintLn;
      PrintLn( "Шаги операции" );
      PrintLn( "-------------" );
      PrintLn;
      HeadPrinted = true;
    end;
    OutOpStepLine;
    RecFound = Next( OpStep );
  end;
end;

macro ReportOnOperation

  OutOpLine;
  ReportOnSubOps;
  ReportOnOpSteps;
  [-----------------------------------------------------------------------------------------------------------------------];
  PrintLn;
end;

macro ReportOnOperations

  var
    RecFound;

  OutOpHead;
  ClearRecord( Operation );
  Operation.IsCur = DepType.FlagCur;
  Operation.Kind = DepType.Kind;
  RecFound = GetGE( Operation );
  while ( RecFound
    and ( Operation.IsCur == DepType.FlagCur )
    and ( Operation.Kind == DepType.Kind ) )
    ReportOnOperation;
    RecFound = Next( Operation );
  end;
end;

macro ListPcAlgFields

  var
    Head = "Дата начала: " + String( PcAlg.BegDate ) + " "  + HeaderSuffix;

  var PcMacroFilename, PcRateComplexPercMacroNum, cmd, rs, rsfindPcRateComplexPercMacroNum;
  
  var findPcRateComplexPercMacroNum = RsdCommand( "select t_complexpercmacronum from dpc_rate_dbt where t_FlagCur = ?" 
                                                    "and t_ObjectType = ?" 
                                                    "and t_Referenc = ?" );
    findPcRateComplexPercMacroNum.addParam( "t_FlagCur", RSDBP_IN );
    findPcRateComplexPercMacroNum.value( "t_FlagCur" ) = DepType.FlagCur;
    findPcRateComplexPercMacroNum.addParam( "t_ObjectType", RSDBP_IN );
    findPcRateComplexPercMacroNum.value( "t_ObjectType" ) = OBJ_ACCTYPE;   
    findPcRateComplexPercMacroNum.addParam( "t_Referenc", RSDBP_IN );
    findPcRateComplexPercMacroNum.value( "t_Referenc" ) = DepType.Kind;
  findPcRateComplexPercMacroNum.execute();
  rsfindPcRateComplexPercMacroNum = RsdRecordset(findPcRateComplexPercMacroNum);

  PrintLn( Head );
  PrintLn( MkStr( "-", StrLen( Head ) ) );
  PrintLn;
  [ Комментарий                          #                                        ]
  ( PcAlg.Comment );
  [ Вид графика                          #                                        ]
  ( NameAlg( 308, PcAlg.GraphType ) );
  [ Включение границ периода             #                                        ]
  ( NameAlg( 305, PcAlg.RangeAlg ) );
  [ Календарь расчета                    #                                        ]
  ( NameAlg( 303, PcAlg.CalendAlg ) );
  [ Учет високосности года               (#)                                      ]
  ( PcAlg.CheckLeapYear );
  [ Округление исходной величины         #                                        ]
  ( NameAlg( 9, PcAlg.RoundAlgCalc ) );
  [ Округление результата                #                                        ]
  ( NameAlg( 9, PcAlg.RoundAlgRes ) );
  [ Тип остатка                          #                                        ]
  ( NameAlg( 307, PcAlg.RestUseType ) );
  [ Капитализация                        #                                        ]
  ( NameAlg( 705, PcAlg.NumbAccountingType ) );
  [ Вид ставки                           #                                        ]
  ( NameAlg( 306, PcAlg.RateType ) );
  while (rsfindPcRateComplexPercMacroNum.moveNext)
  PcRateComplexPercMacroNum = rsfindPcRateComplexPercMacroNum.value("t_complexpercmacronum");
  cmd = RsdCommand( "select t_filename from dpc_macro_dbt where t_number = ?" );
  if (PcRateComplexPercMacroNum > 0)
    cmd.addParam( "p_number", RSDBP_IN );
    cmd.value( "p_number" ) = PcRateComplexPercMacroNum;
  else 
    cmd.addParam( "p_number", RSDBP_IN );
    cmd.value( "p_number" ) = PcAlg.ComplexPercMacroNum;
  end;
  cmd.execute();
  rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    PcMacroFilename = rs.value( "t_filename" );
  else
    PcMacroFilename = "";
  end;  
  [ Макрос корректировки для капит.      #                                        ]
  ( PcMacroFilename );
  end;
  [ Стратегия расчета                    #                                        ]
  ( NameAlg( 702, PcAlg.StratCalc ) );
  [ Вид графика расчета                  #                                        ]
  ( GraphStr( 703, PcAlg.GrafCalc, PcAlg.DayCalc ) );
  [ Вид графика оплаты                   #                                        ]
  ( GraphStr( 704, PcAlg.GrafPay, PcAlg.DayPay ) );
  [ Ограничения при оплате %%            #                                        ]
  ( NameAlg( 810, PcAlg.PercPayLim ) );
  [ Отсрочка расчета %%                  (#)                                      ]
  ( PcAlg.FlagNoCalc );
  [ Отсрочка расчета %%                  #                                        ]
  ( String( PcAlg.TermNoCalc ) + " " + TermTypes( PcAlg.KindNoCalc ) );
  PrintLn;
end;

macro ReportOnPcAlg

  var
    RecFound;

  OutSubHeader( "Алгоритмы %%" );
  ClearRecord( PcAlg );
  PcAlg.FlagCur = DepType.FlagCur;
  PcAlg.Referenc = DepType.Kind;
  PcAlg.ObjectType = OBJ_ACCTYPE;
  RecFound = GetGE( PcAlg );
  while ( RecFound
    and ( PcAlg.FlagCur == DepType.FlagCur )
    and ( PcAlg.Referenc == DepType.Kind )
    and ( PcAlg.ObjectType == OBJ_ACCTYPE ) )
    ListPcAlgFields;
    RecFound = Next( PcAlg );
  end;
end;

macro FindCurr( ID )

  Curr.Code_Currency = ID;
  return GetEQ( Curr );
end;

macro FindPcMacro( Number )

  PcMacro.Number = Number;
  return GetEQ( PcMacro );
end;

macro OutRateLine( Rest, NextRest, Rate )

  if ( Rest and NextRest )
    [ Остаток: ##############..############### Ставка: ##### %                      ]
    (
      Rest:a:r,
      Money( NextRest - 1 ):a:l,
      Rate:0:2
    );
  elif ( Rest )
    [ Остаток: >= ################             Ставка: ##### %                      ]
    (
      Rest:a:r,
      Rate:0:2
    );
  end;
end;

macro ListPcRateFields

  var
    Head;

  FindCurr( PcRate.Code_Currency );

  if ( not DepType.FlagCur )
    Head = "Дата начала: " + String( PcRate.BeginDate ) + " " + HeaderSuffix;
  else
    Head = "Валюта: " + Curr.Short_Name + " дата начала: " + String( PcRate.BeginDate ) + " " + HeaderSuffix;
  end;

  PrintLn( Head );
  PrintLn( MkStr( "-", StrLen( Head ) ) );
  PrintLn;
  [ Период                               #                                        ]
  ( String( PcRate.DatePeriod ) + " " + NameAlg( 321, PcRate.DatePeriodType ) );
  [ Тип вычисл. остатка для % ставки     #                                        ]
  ( NameAlg( 313, PcRate.PercRestCalc ) );
  [ Признак прогресс. % ставки           (#)                                      ]
  ( StrFor( PcRate.PercProgressFlag ) );
  [ Период расчета %                     #                                        ]
  ( String( 1 ) + " " + NameAlg( 321, PcRate.PercPeriodType ) );
  [ Алг. % числа                         #                                        ]
  ( NameAlg( 312, PcRate.PercRestCalc ) );
  [ Тип вычисл. остатка для % числа      #                                        ]
  ( NameAlg( 313, PcRate.PercRestCalc ) );
  [ Период расчета % числа               #                                        ]
  ( String( 1 ) + " " + NameAlg( 321, PcAlg.NumbPeriodType ) );
  FindPcMacro( PcRate.MacroNum );
  [ Макрос расчета %%                    #                                        ]
  ( PcMacro.FileName );
  [ Вид ставки                           #                                        ]
  ( NameAlg( 735, PcRate.PeriodRate ) );
  [ Дней в периоде (для дневной ставки)  #######                                  ]
  ( PcRate.DaysPeriod:l );
  [ Ставка только для новых счетов       (#)                                      ]
  ( PcRate.OnlyForNew );
  if ( PcRate.RestCount > 1 )
    [ Число ставок в сложной ставке        #######                                  ]
    ( PcRate.RestCount:l );
    if ( PcRate.RestCount >= 1 )
      OutRateLine( PcRate.Rest1, PcRate.Rest2, PcRate.Percent1 )
    end;
    if ( PcRate.RestCount >= 2 )
      OutRateLine( PcRate.Rest2, PcRate.Rest3, PcRate.Percent2 )
    end;
    if ( PcRate.RestCount >= 3 )
      OutRateLine( PcRate.Rest3, PcRate.Rest4, PcRate.Percent3 )
    end;
    if ( PcRate.RestCount >= 4 )
      OutRateLine( PcRate.Rest4, PcRate.Rest5, PcRate.Percent4 )
    end;
    if ( PcRate.RestCount >= 5 )
      OutRateLine( PcRate.Rest5, PcRate.Rest6, PcRate.Percent5 )
    end;
    if ( PcRate.RestCount >= 6 )
      OutRateLine( PcRate.Rest6, PcRate.Rest7, PcRate.Percent6 )
    end;
    if ( PcRate.RestCount >= 7 )
      OutRateLine( PcRate.Rest7, PcRate.Rest8, PcRate.Percent7 )
    end;
    if ( PcRate.RestCount >= 8 )
      OutRateLine( PcRate.Rest8, PcRate.Rest9, PcRate.Percent8 )
    end;
    if ( PcRate.RestCount >= 9 )
      OutRateLine( PcRate.Rest9, PcRate.Rest10, PcRate.Percent9 )
    end;
    if ( PcRate.RestCount >= 10 )
      OutRateLine( PcRate.Rest10, PcRate.Rest11, PcRate.Percent10 )
    end;
    if ( PcRate.RestCount >= 11 )
      OutRateLine( PcRate.Rest11, PcRate.Rest12, PcRate.Percent11 )
    end;
    if ( PcRate.RestCount >= 12 )
      OutRateLine( PcRate.Rest12, PcRate.Rest13, PcRate.Percent12 )
    end;
    if ( PcRate.RestCount >= 13 )
      OutRateLine( PcRate.Rest13, PcRate.Rest14, PcRate.Percent13 )
    end;
    if ( PcRate.RestCount >= 14 )
      OutRateLine( PcRate.Rest14, PcRate.Rest15, PcRate.Percent14 )
    end;
    if ( PcRate.RestCount >= 15 )
      OutRateLine( PcRate.Rest15, PcRate.Rest16, PcRate.Percent15 )
    end;
    if ( PcRate.RestCount >= 16 )
      OutRateLine( PcRate.Rest16, PcRate.Rest17, PcRate.Percent16 )
    end;
    if ( PcRate.RestCount >= 17 )
      OutRateLine( PcRate.Rest17, PcRate.Rest18, PcRate.Percent17 )
    end;
    if ( PcRate.RestCount >= 18 )
      OutRateLine( PcRate.Rest18, PcRate.Rest19, PcRate.Percent18 )
    end;
    if ( PcRate.RestCount >= 19 )
      OutRateLine( PcRate.Rest19, PcRate.Rest20, PcRate.Percent19 )
    end;
    if ( PcRate.RestCount >= 20 )
      OutRateLine( PcRate.Rest20, 0, PcRate.Percent20 )
    end;
  else
    [ Величина ставки, %%                  #######                                  ]
    ( PcRate.Percent1:0:2:l );
  end;
  PrintLn;
end;

macro ReportOnPcRates

  var
    RecFound;

  OutSubHeader( "Процентные ставки" );

  ClearRecord( PcRate );
  PcRate.FlagCur = DepType.FlagCur;
  PcRate.ObjectType = OBJ_ACCTYPE;
  PcRate.Referenc = DepType.Kind;
  RecFound = GetGE( PcRate );
  while ( RecFound
    and ( PcRate.FlagCur == DepType.FlagCur )
    and ( PcRate.ObjectType == OBJ_ACCTYPE )
    and ( PcRate.Referenc == DepType.Kind ) )
    ListPcRateFields;
    RecFound = Next( PcRate );
  end;
end;

macro OutPcRCalcLine

  [ Дата начала: ##########   Длина периода: #                                    ]
  (
    PcRCalc.BeginDate,
    ( String( PcRCalc.Period ) + " " + NameAlg( 315, PcRCalc.PeriodType ) )
  );
end;

macro ReportOnPcRCalc

  var
    RecFound;

  OutSubHeader( "График расчетов %%" );

  ClearRecord( PcRCalc );
  PcRCalc.FlagCur = DepType.FlagCur;
  PcRCalc.Referenc = DepType.Kind;
  PcRCalc.ObjectType = OBJ_ACCTYPE;
  RecFound = GetGE( PcRCalc );
  while ( RecFound
    and ( PcRCalc.FlagCur == DepType.FlagCur )
    and ( PcRCalc.Referenc == DepType.Kind )
    and ( PcRCalc.ObjectType == OBJ_ACCTYPE ) )
    OutPcRCalcLine;
    RecFound = Next( PcRCalc );
  end;
end;

macro ReportOnDepType( DType )

  DepType.FlagCur = IsCur;
  DepType.Kind = DType;
  if ( not GetEQ( DepType ) )
    MsgBox( "Вид вклада не найден: " + DType );
    Exit( -1 );
  end;

  ListCommonSettings;
  ReportOnApplTypes;
  ReportOnOperations;
  ReportOnPcAlg;
  ReportOnPcRates;
  ReportOnPcRCalc;
end;

macro ShowDepType( DType, HSuff )

  if ( ValType( HSuff ) == V_STRING )
    HeaderSuffix = HSuff;
  end;

  ReportOnDepType( DType );
end;

