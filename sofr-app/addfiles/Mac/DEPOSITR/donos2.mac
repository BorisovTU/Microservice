import deprintr, issrvdoc, sqlconv, rsd;

var ddep = TRecHandler( "i_dp_dep.rec" );
file rp( "rep_93i.dbt"  ) key 1 write;
file ai( "auxinfo.dbt"  ) key 0;

var dr = TBfile( "depositr.dbt", "r", 7 );
var dd = TBfile( "sbdepdoc.dbt", "r", 6 );

var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

var depclnt = TClientList; /* Справочник вкладчиков */

record auxinfo4( "auxinfo.4" );

const
  D_IN = 1,         /* Приход */
  D_OUT = 2,        /* Расход */
  D_GET = 3,        /* Списание */
  D_PUT = 5;        /* Зачисление */

const
  BS_BRANCH  = 0,  /* Филиал */
  BS_DEP     = 1,  /* Отделение */
  BS_EXCH    = 2,  /* Обменный пункт */
  BS_OPERDEP = 3;  /* Оперчасть */

var
  branch = numfncash,
  statusBranch,
  startDate, endDate;

private var
    {Name_Bank},
    {NumberBranch},
    {Bank_OKPO};

macro define_brtyp;

  if( not findDepartment(branch, null, ddep) )
    exit( 1 );
  else
    statusBranch = ddep.rec.NodeType;
  end;
end;

macro filterBranch( num );

  if( statusBranch == I_DEPARTMENT_TYPE_FILIAL/*BS_DEP*/ )
    return true;
  else
    if( branch == num )
      return true;
    end;
  end;
  return false;
end;

macro isFType;

  if( index( dr.rec.UserTypeAccount, "Ф" ) )
    return true;
  end;
  return false;
end;

/* Определение резидентности клиента */
macro getClientCS( code );

  if ( depclnt.GetRecord( code ) )
    return depclnt.CurRec.rec.NotResident;
  else
    return "";
  end;
end;

macro getClientName( code );

  if ( depclnt.GetRecord( code ) )
    return depclnt.CurRec.rec.Name1 + " " +
           depclnt.CurRec.rec.Name2 + " " +
           depclnt.CurRec.rec.Name3;
  else
    return "";
  end;
end;

macro PrintDonos( Date, FIO );

  [








      код ОКПО кредитной организации:
      № #################### и дата
      ##############################







                                                    УВЕДОМЛЕНИЕ


      Настоящим уведомляем, что нерезидентом #
      ___________________________________________________________________________
      в нарушение пункта 4.5 Инструкции Банка России "О порядке открытия уполномоченными банками
      счетов нерезидентов в валюте Российской Федерации и проведения операций по этим счетам" в
      течение календарного месяца совершены приходные операции на общую сумму, превышающую
      предполагаемый максимальный объём поступлений, указанный нерезидентом в информационной
      справке.



      Главный бухгалтер         __________________________ (Ф.И.О.)

                                  М. П.

      Исполнитель               ___________________________ (Ф.И.О.)

      Телефон                   ____________________________

  ]
  ( {Bank_OKPO}, Date:m:c, FIO:w );
end;

macro monthAndYear( dmy );

  var d, m, y, str = "";

  datesplit( dmy, d, m, y );
  if  ( m ==  1 ) str = "январь";
  elif( m ==  2 ) str = "февраль";
  elif( m ==  3 ) str = "март";
  elif( m ==  4 ) str = "апрель";
  elif( m ==  5 ) str = "май";
  elif( m ==  6 ) str = "июнь";
  elif( m ==  7 ) str = "июль";
  elif( m ==  8 ) str = "август";
  elif( m ==  9 ) str = "сентябрь";
  elif( m == 10 ) str = "октябрь";
  elif( m == 11 ) str = "ноябрь";
  elif( m == 12 ) str = "декабрь";
  else            str = "";
  end;
  str = str + " " + string( y );
  return str;
end;

macro PrintReport( StartDate, EndDate );
var ClientName;

 var cmd, rs;  
  var namestr = "";
 cmd = RsdCommand("select rep.t_Date as tDate, rep.t_account as Account, rep.t_FIO as FIO,"+
                  " rep.t_TypeOper as TypeOper, rep.t_Sum1 as Sum1, rep.t_Sum2 as Sum2, dep.t_SpecialAccess as SpecialAccess "+
                  " from drep_93i_dbt rep, ddepositr_dbt dep where "+
                  " rep.T_ACCOUNT = dep.T_ACCOUNT and dep.t_fncash = "+String(Branch)
                  );
 cmd.execute();
 rs = RsdRecordset(cmd);


  namestr = {Name_Bank} + " " + {NumberBranch};
  if( statusBranch != I_DEPARTMENT_TYPE_FILIAL/*BS_DEP*/ )
    namestr = namestr + " Филиал " + ddep.rec.Name;
  end;

  [
      #
                                   Максимальный объем поступлений по счетам типа "Ф"
                                                за ####################

    --------------------------------------------------------------------------------------------------------------
    |         № счета        |           ФИО нерез.           | Предполаг. макс. объем |     Объем поступления   |
    --------------------------------------------------------------------------------------------------------------
  ]
  ( namestr, monthAndYear( StartDate ) );
  while  ( rs.moveNext() )
   if(  (not SpecialAccess) and OperMayListSpecialAccounts and rs.value("SpecialAccess"))
      ClientName = "XXXXXX X.X.";
   else
      ClientName = rs.value("FIO");
   end;   

    [ |########################|################################|########################|#########################|]
    ( Acc_Form(String(rs.value("Account"))), ClientName, rs.value("Sum1"), rs.value("Sum2") );
    [ |------------------------|--------------------------------|------------------------|-------------------------|];
  end;
  [

                                                                             Контролер_________________________
  ];
end;

macro getMaxDeclaredSumForAccount;

  ai.Reference = dr.rec.Referenc;
  ai.InfoType = 16;
  ai.Date = dr.rec.Open_Date;
  if( getEQ( ai ) )
    SetRecordAddr( auxinfo4, ai, 0, 0, true );
    return auxinfo4.SummaArest;
  end;
  return $0.0L;
end;

macro FillRepFile( StartDate, EndDate );

  var stat, sum, max_sum;
  var Num = 1, Str = "";
  var cmd;
  if( not open( rp ) )
     msgbox( "Ошибка при открытии файла rep_93i.dbt" );
     exit( 1 );
  end;
  cmd = RSDCommand("truncate table drep_93i_dbt");
  cmd.execute;
  Str = "t.T_FNcash = " + String( Branch ) +
        " AND t.T_UserTypeAccount LIKE" + GetSQLString("%Ф%");
  dr.addFilter( Str );
  rewind( dr );
  while( next( dr ) )
    Str = "  " + String(Num:7) + ": счет " + Acc_Form( dr.rec.Account );
    Message( Str );
    Num = Num + 1;
    if( filterBranch( dr.rec.FNcash )  AND
        isFType()                  AND   /* Счет типа "Ф" */
        ( getClientCS( dr.rec.CodClient ) != "" ) )  /* Клиент - нерезидент */
      sum = $0.0L;
      Str = "t.T_Referenc = " + String( dr.rec.Referenc ) +
            " AND t.T_Date_Document >= " + sqlDateToStr( StartDate ) +
            " AND t.T_Date_Document <= " + sqlDateToStr( EndDate );
      dd.addFilter( Str );
      ClearRecord( dd );
      dd.rec.Referenc = dr.rec.Referenc;
      dd.rec.Date_Document = StartDate;
      stat = getGE( dd );
      while( stat  AND
             ( dd.rec.Referenc == dr.rec.Referenc )  AND
             ( dd.rec.Date_Document <= EndDate ) )
        if( IsServDoc( dd.rec ) )
          if( ( dd.rec.KindOp == D_IN ) or ( dd.rec.KindOp == D_PUT ) )
            sum = sum + dd.rec.InSum;
          end;
        end;
        stat = next( dd );
      end;
      dd.dropFilter();
      max_sum = getMaxDeclaredSumForAccount();
      if( sum > max_sum )
        clearrecord( rp );
        rp.Date = dd.rec.Date_Document;
        rp.Account = dr.rec.Account;
        rp.FIO = getClientName( dr.rec.CodClient );
        rp.Sum1 = max_sum;
        rp.Sum2 = sum;
        stat = insert( rp );
        if( not stat )
          msgbox( "Ошибка при вставке в файл rep_93i.dbt" );
          exit( 1 );
        end;
      end;
    end;
  end;
  dr.dropFilter();
end;
