/*

      R-Style SoftWare Lab.

      RS-Retail 2.1

      Отчет для Налоговой инспекции (ГНС) по операциям
      с превышением лимита ( $ 10,000 )

*/

import CommonInter, BankInter, DeprIntr, ExchangeInter, DeskInter, Cur_Rate, IsSrvDoc, commclnt, chkboss;

var clntL = TClientList;

file person   ( person,   "bank.def"     ) key 0; /* Пользователи Системы */
file accnt    ( depositr                 ) key 8; /* Счета вкладчиков Банка */
file country  ( country,  "rsrfdb.def"   ) key 2; /* Страны */

file paykind  ( pay_kind                 ) key 0; /* Виды платежей */
file pay_csop ( pay_csop                 ) key 0; /* Подвиды платежей */

file ddoc     ( sbdepdoc                 ) key 0; /* Документы по счетам вкладчиков */
file pdoc     ( pay_doc                  ) key 7; /* Документы платежей */
file edoc     ( exoperat, "exchange.def" ) key 4; /* Документы "Обменного пункта" */

const cUSD = 1;  /* Код базовой валюты (USD) */

const OP_GETLPR = 64;  /* Списание по коммунальному платежу */

const Client_Pol = 62;  /* Вид пользовательского поля: пол клиента */

/* Виды платежей */
const
 CO_CommPay     =   1,  /* Коммунальные платежи */
 CO_NalPer      =  11,  /* Наличный перевод */
 CO_Comission   =  21,  /* Комиссия */
 CO_Conversion  =  31,  /* Конверсия */
 CO_Payed       =  41,  /* Выплата */
 CO_BankPayed   =  51,  /* Банковские расчеты */
 CO_OverCred    =  61,  /* Документ овердрафтного кредитования */
 CO_Trn         = 100,  /* Безналичные платежи */
 CO_Treasure    = 101;  /* Ценности */

/* Вид операции в pay_doc.dbt */
const
 CI_CREDIT = 1,  /* Приходная операция */
 C__DEBET  = 2;  /* Расходная операция */

/* Виды валютно-обменных операций */
const
 CDF_OperCurrBuy             =  6,   /* Покупка платежеспособной валюты за рубли */
 CDF_OperCurrSale            =  7,   /* Продажа платежеспособной валюты за рубли */
 CDF_OperPayDocBuy           =  3,   /* Покупка платежного документа за рубли  */
 CDF_OperPayDocSale          =  4,   /* Продажа платежного документа за рубли  */
 CDF_OperPayDocBuyCurr       = 20,   /* Покупка платежного документа за валюту */
 CDF_OperPayDocSaleCurr      = 21,   /* Продажа платежного документа за валюту */
 CDF_OperCurrConversion      = 18,   /* Конверсия */
 CDF_OperChange              = 23,   /* Замена неплатежной валюты */
 CDF_OperUnpayCurrBuy        =  1,   /* Покупка неплатежеспособной валюты */
 CDF_OperIncasso             = 16,   /* Прием валюты на инкассо */
 CDF_OperExpert              = 17,   /* Прием валюты на экспертизу */
 CDF_OperReturnIncasso       = 24,   /* Возврат валюты с инкассо */
 CDF_OperReturnExpert        = 25,   /* Возврат валюты с экспертизы */
 CDF_OperPayDocIncasso       = 27,   /* Прием ПД на инкассо */
 CDF_OperPayDocExpert        = 28,   /* Прием ПД на экспертизу */
 CDF_OperPayDocReturnIncasso = 29,   /* Возврат ПД с инкассо */
 CDF_OperPayDocReturnExpert  = 30,   /* Возврат ПД с экспертизы */
 CDF_OperPayDocBuyAcc        = 38,   /* Покупка ПД за рубли через счет   */
 CDF_OperPayDocSaleAcc       = 39,   /* Продажа ПД за рубли через счет   */
 CDF_OperPayDocBuyCurrAcc    = 40,   /* Покупка ПД за валюту через счет  */
 CDF_OperPayDocSaleCurrAcc   = 41;   /* Продажа ПД за валюту через счет  */


var FlagCur = NumFlagCur();   /* Валютность */
var Branch  = NumFNCash();    /* Номер филиала */

var LimInSum  = $0.;          /* Сумма лимита для выбора документов прихода */
var LimOutSum = $0.;          /* Сумма лимита для выбора документов расхода */

private var {Name_Bank};
private var {Post_Addr};

var BankName_C = {Name_Bank},
    BankAddr_C = {Post_Addr};
var Curr_Name = "";

var Date1 = {curdate}, Date2 = {curdate};

var v_shrt, v_name, v_cop;
var markFirst = FALSE;
var stat;
var ErrCode;
var NDoc;

/*
   Получение ФИО операциониста
*/
macro get_Oper( oper )
 var str;
 person.Oper = oper;
 if ( getEQ( person ) )
   str = person.Name;
 else
   str = "";
 end;
 return str;
end;

/*
   Получение названия страны
*/
macro getCountry ( code )

  var ret_name = "";

  ClearRecord( country );
  country.CodeLat3 = code;

  if ( GetEQ( country ) )
    ret_name = country.Name;
  else
    if ( code == "" )
      ret_name = "Россия";
    end;
  end;

  return ret_name;

end;

/*
   Вспомогательная процедура для getCurr()
*/
macro byWords( sum )
 var tmp, words;
 RubToStr( sum, words, tmp );
 return words
END;

/*
   Получение названия валюты
*/
macro getCurr( sum, code )
var
    wholePart    = byWords( sum ),
    wholeEmpty   = ( Strlen(wholePart) == 0 ),
    handredsEmpty,
    handredsPart,
    tmp;

 Curr_Name = v_name;

 if ( v_shrt == "RUR" )
   v_shrt = "руб.";
 end;

 RubToStr(sum,tmp,handredsPart);
 handredsEmpty = (StrLen(handredsPart)==0);
 if( wholeEmpty)    wholePart    = "Ноль"; end;
 if( handredsEmpty) handredsPart = "00";   end;

 return wholePart + " " + v_shrt + " " + handredsPart + " " + v_cop;
end;

/*
   Формирование отчета для Налоговой инспекции
*/
macro FiscalReport(
                   type,
                   client,
                   sum,
                   curr,
                   operation,
                   sumSell,
                   currSell,
                   sumBuy,
                   currBuy,
                   oper,
                   cur_date
                  )

 var clntFIO, clntPasp, clntCount, clntAddr;
 var sSumSell, sCurrSell, sSumBuy, sCurrBuy;
 var iSumOper, sSumOper;
 var Pol_of_Client = "";

 if ( clntL.GetRecord( client ) )
   clntFIO = clntL.CurRec.rec.Name1 + " " +
             clntL.CurRec.rec.Name2 + " " +
             clntL.CurRec.rec.Name3;

   /* Чтение пользовательского поля "Пол клиента" */
   Pol_of_Client = clntL.CurRec.rec.IsMale;  /* Пол клиента */
   if ( Pol_of_Client != StrFor(88) )
     clntFIO = clntFIO + " (пол: женский)";
   else
     clntFIO = clntFIO + " (пол: мужской)";
   end;
   clntPasp  = clntL.CurRec.MakeDocStr();
   if ( clntL.CurRec.rec.NotResident == "" )
     clntCount = "";
     clntAddr  = clntL.CurRec.MakeFullStrAddress();
   else
     clntCount = getCountry( clntL.CurRec.rec.Country );
     clntAddr = "";
     clntCount = getCountry( clntL.CurRec.rec.Country );
     clntL.AdressType = COMMCLNT_PTADDR_REAL;
     if (clntL.AdressType != COMMCLNT_PTADDR_REAL)
       clntL.AdressType = COMMCLNT_PTADDR_POST;
     end;
     if (clntAddr != COMMCLNT_NO_DEFINED_ADRESS_TYPE)
//       clntAddr  = clntL.CurRec.MakeFullStrAddress(); // Можно только для Единого клиента
       if (Trim(clntAddr) == "")
         clntAddr = clntL.CurRec.rec.Address;
       end;
     end;
   end;
 else
   clntFIO   = "";
   clntPasp  = "";
   clntCount = "";
   clntAddr  = "";
 end;

 /* Атрибуты продажи и покупки валюты */
 if ( sumSell == $0 )
   sSumSell  = "";
   sCurrSell = "";
 else
   sSumSell  = String(sumSell:a:0:2) + " " + byWords(sumSell);
   sCurrSell = GetNameCurr( FindCodCurByRefVal( currSell ) );
 end;
 if ( sumBuy == $0 )
   sSumBuy  = "";
   sCurrBuy = "";
 else
   sSumBuy  = String(sumBuy:a:0:2) + " " + byWords(sumBuy);
   sCurrBuy = GetNameCurr( FindCodCurByRefVal( currBuy ) );
 end;

 if ( type == 0 )
   Get_Currency( FindCodCurByRefVal( curr ), v_shrt, v_name, v_cop );
   v_shrt = GetNameCurr( FindCodCurByRefVal( curr ) );
   Curr_Name = v_shrt;
 else
   Get_Currency( curr, v_shrt, v_name, v_cop );
 end;

 if ( sum == $0 )
   sSumOper = "";
   iSumOper = "";
 else
   sSumOper = getCurr( sum, curr );
   iSumOper = String(sum:a:0:2) + " " + v_shrt;
 end;

 if ( markFirst )
   [ ];
   [ ];
   [------------------------------------------------------------------------];
   [ ];
 else
   markFirst = TRUE;
 end;

 [
                                                           Приложение N 9
                                           к Порядку применения положений
                                    Указа Президента Российской Федерации
                                                 от 23 мая 1994 г. N 1006

                                   СВЕДЕНИЯ
                  ОБ ОПЕРАЦИИ, СВЯЗАННОЙ С ДВИЖЕНИЕМ ДЕНЕЖНЫХ
               СРЕДСТВ ФИЗИЧЕСКОГО ЛИЦА, НА СУММУ ЭКВИВАЛЕНТНУЮ
                          10 ТЫС. ДОЛЛАРОВ США И ВЫШЕ

       1. Наименование банка (кредитного учреждения) #_____________________
       ____________________________________________________________________]
 ( BankName_C );
 [     2. Почтовый индекс, адрес, телефон банка (кредитного учреждения)
       ####################################################################
       ____________________________________________________________________]
  ( BankAddr_C:w );
 [     3. Физическое лицо #________________________________________________
                                 (фамилия, имя, отчество, пол)            ]
 ( clntFIO );
 [     4. Документ, удостоверяющий личность физического лица ______________
       ####################################################################
       ____________________________________________________________________
                  (вид, серия и номер документа, кем и когда выдан)       ]
 ( clntPasp:w );
 [     5. Страна постоянного проживания нерезидента #______________________ ]
 ( clntCount );
 [     6. Адрес прописки резидента или адрес проживания нерезидента _______
       ####################################################################]
 ( clntAddr:w );

 [     7. Сумма операции в валюте счета #__________________________________
       ####################################################################
                                         (сумма цифрами и прописью,]
 ( iSumOper, sSumOper:w );
 if ( type == 0 )
   Curr_Name = " ";
 end;
 [     #___________________________________________________________________
                              наименование валюты)

            Операция, связанная с движением средств физического лица

       ####################################################################
       ____________________________________________________________________
       9. Дата операции #
 ]( Curr_Name, operation:w, cur_date:m );
 [
                  Валютно - обменная операция физического лица

       10. Наименование  иностранной  или национальной валюты,  проданной
       физическому лицу #__________________________________________________ ]
 ( sCurrSell );
 [     11. Сумма этой валюты #_____________________________________________
                                     (сумма цифрами и прописью)             ]
 ( sSumSell );
 [     12. Наименование иностранной или национальной валюты,  купленной у
       физического лица #__________________________________________________ ]
 ( sCurrBuy );
 [     13. Сумма этой валюты #_____________________________________________
                                     (сумма цифрами и прописью)             ]
 ( sSumBuy );
 [
       М.П.    ____________________________________   (#
               (должность работника банка) (подпись)  (фамилия, инициалы)

 ]( get_Oper( oper )+")" );
end;

/*
   Нахождение эквивалента суммы в долларах
*/
macro EQU_USD( curr, sum, cur_date, cur_time )
 var CBRateUSD, BuyRateUSD, SellRateUSD;
 var CBRateVal, BuyRateVal, SellRateVal;
 var equ;
 if ( curr == cUSD )  /* USD */
   equ = sum / $1.0;
 else
   if ( NOT GetAllCurrRate( cur_date, 1,
                            CBRateUSD, BuyRateUSD, SellRateUSD, cur_time ) )
       MsgBox("Ошибка при определении курса USD");
       Exit(1);
   end;

   if ( curr == 0 )
     equ = sum / ( CBRateUSD );
   else
     if ( NOT GetAllCurrRate( cur_date, curr,
                              CBRateVal, BuyRateVal, SellRateVal, cur_time ) )
         MsgBox("Ошибка при определении курса валюты=", curr);
         Exit(1);
     end;

     equ = sum * CBRateVal / ( CBRateUSD );
   end;
 end;
 return equ;
end;

/*
   Название операции для документов из sbdepdoc.dbt
*/
macro OperationName
 var str = "8. Вид операции: ";
 if ( ddoc.InSum > $0 )
   str = str + "зачисление средств на счет по вкладу физического лица " +
               "либо любая другая операция по поступлению денежных средств " +
               "в пользу физического лица";
 else
   str = str + "снятие средств со счета по вкладу лица";
 end;
 return str;
end;

/*
   Поиск кода клиента для документа вкладчика
*/
macro getCodClient()
  var code = 0;
  ClearRecord( accnt );
  accnt.Referenc = ddoc.Referenc;
  if ( getEQ( accnt ) )
    code = accnt.CodClient;
  else
    MsgBox( " Счет ", Acc_Form(ddoc.Account), " не найден." );
    code = ddoc.CodClient;
  end;
  return code;
end;

/*
   Выбор из файла sbdepdoc.dbt документов по счетам вкладчиков
*/
macro Select_SBdepDoc()
 var tm;

 InitProgress( 1000, " Ж Д И Т Е...", "Операции по счетам" );
 NDoc = 0;
 ClearRecord( ddoc );
 ddoc.IsCur         = FlagCur;
 ddoc.FNCash        = Branch;
 ddoc.Date_Document = Date1;
 stat = getGE( ddoc );
 while ( stat                               AND
         ( ddoc.IsCur         == FlagCur )  AND
         ( ddoc.FNCash        == Branch  )  AND
         ( ddoc.Date_Document >= Date1   )  AND
         ( ddoc.Date_Document <= Date2   ) )
   tm = null;
   if( ddoc.ApplicationKey != "" )
     tm = Time( int( substr( ddoc.ApplicationKey, 11, 2 ) ),
                int( substr( ddoc.ApplicationKey, 13, 2 ) ),
                int( substr( ddoc.ApplicationKey, 15, 2 ) ) );
   end;
   if ( ( ddoc.TypeOper != OP_GETLPR )  AND  /* Кроме коммунальных платежей */
        IsServDoc( ddoc )               AND
        ( ( $1. * EQU_USD( ddoc.Code_Currency,
                           ddoc.InSum,
                           ddoc.Date_Document,
                           tm ) >= LimInSum )  OR
          ( $1. * EQU_USD( ddoc.Code_Currency,
                           ddoc.OutSum,
                           ddoc.Date_Document,
                           tm ) >= LimOutSum ) ) )
     if ( $1. * EQU_USD( ddoc.Code_Currency,
                         ddoc.InSum,
                         ddoc.Date_Document,
                         tm ) >= LimInSum )
       FiscalReport( 1, getCodClient(), ddoc.InSum, ddoc.Code_Currency,
                     OperationName(), $0, 0, $0, 0, ddoc.Oper,
                     ddoc.Date_Document );
     end;
     if ( $1. * EQU_USD( ddoc.Code_Currency,
                         ddoc.OutSum,
                         ddoc.Date_Document,
                         tm ) >= LimOutSum )
       FiscalReport( 1, getCodClient(), ddoc.OutSum, ddoc.Code_Currency,
                     OperationName(), $0, 0, $0, 0, ddoc.Oper,
                     ddoc.Date_Document );
     end;
   end;
   NDoc = Ndoc + 1;
   if ( NDoc < 1001 )
     UseProgress( NDoc );
   end;
   stat = Next( ddoc );
 end;
 RemProgress();
end;

/*
   Вид операции платежа
*/
macro payNumOpert()
 var str;
 ClearRecord( paykind );
 paykind.IsCur = NumFlagCur;
 paykind.GroupOpert = pdoc.GroupOpert;
 paykind.NumOperat  = pdoc.NumOpert;
 if(GetEQ(paykind))
   str = " (" + paykind.SzNameAlg + ")";
 else
   str = "";
 end;
 return str;
end;

/*
   Подвид платежа
*/
macro payApplType()
 var str;
 if ( pdoc.ApplType > 0)
   ClearRecord( pay_csop );
   pay_csop.IsCur     = pdoc.IsCur;
   pay_csop.GroupType = pdoc.GroupOpert;
   pay_csop.NumOperat = pdoc.NumOpert;
   pay_csop.ApplType  = pdoc.ApplType;
   if ( GetEQ( pay_csop ) )
     str = " (" + pay_csop.NameCompType + ")";
   else
     str = "";
   end;
 end;
 return str;
end;

/*
   Наименование платежа для документов из pay_doc.dbt
*/
macro PayName()
 var str = "8. Вид операции: ";
 if ( pdoc.DebetCredit == CI_CREDIT )
   str = str + "зачисление средств на счет по вкладу физического лица " +
               "либо любая другая операция по поступлению денежных средств " +
               "в пользу физического лица";
 else
   str = str + "снятие средств со счета по вкладу лица";
 end;
 return str;
end;

/*
   Выбор из файла pay_doc.dbt документов платежей
*/
macro Select_Pay_Doc()
 var tm;

 InitProgress( 1000, " Ж Д И Т Е...", "Операции платежей" );
 NDoc = 0;
 ClearRecord( pdoc );
 pdoc.FNCash        = Branch;
 pdoc.Date_Document = Date1;
 stat = getGE( pdoc );
 while ( stat                              AND
         ( pdoc.FNCash        == Branch )  AND
         ( pdoc.Date_Document >= Date1  )  AND
         ( pdoc.Date_Document <= Date2  ) )
   tm = null;
   if( pdoc.ApplicationKey != "" )
     tm = Time( int( substr( pdoc.ApplicationKey, 11, 2 ) ),
                int( substr( pdoc.ApplicationKey, 13, 2 ) ),
                int( substr( pdoc.ApplicationKey, 15, 2 ) ) );
   end;
   if ( ( pdoc.GroupOpert != CO_Comission )  AND  /* Комиссия */
        ( pdoc.GroupOpert != CO_BankPayed )  AND  /* Банковские расчеты */
        ( pdoc.GroupOpert != CO_Trn       )  AND  /* Безналичные платежи */
        ( pdoc.GroupOpert != CO_Treasure  )  AND  /* Ценности */
        ( pdoc.VidDoc == StrFor( 0 )      )  AND  /* Наличный платеж */
        ( EQU_USD( pdoc.CodCur,
                   pdoc.InSum,
                   pdoc.Date_Document,
                   tm ) >= LimInSum ) )
     FiscalReport( 1, pdoc.CodClient, pdoc.InSum, pdoc.CodCur,
                   PayName(), $0, 0, $0, 0, pdoc.Oper, pdoc.Date_Document );
   end;
   NDoc = Ndoc + 1;
   if ( NDoc < 1001 )
     UseProgress( NDoc );
   end;
   stat = Next( pdoc );
 end;
 RemProgress();
end;

/*
   Наименование валютно-обменных операций
*/
macro ExchName()
 var name;
 name = "8. Вид операции: Валютно-обменная операция: ";
 if   ( edoc.Kind_Oper == CDF_OperCurrBuy        )
   name = name + "Покупка платежеспособной валюты за рубли";
 elif ( edoc.Kind_Oper == CDF_OperCurrSale       )
   name = name + "Продажа платежеспособной валюты за рубли";
 elif ( edoc.Kind_Oper == CDF_OperPayDocBuy      )
   name = name + "Покупка платежного документа за рубли";
 elif ( edoc.Kind_Oper == CDF_OperPayDocSale     )
   name = name + "Продажа платежного документа за рубли";
 elif ( edoc.Kind_Oper == CDF_OperPayDocBuyCurr  )
   name = name + "Покупка платежного документа за валюту";
 elif ( edoc.Kind_Oper == CDF_OperPayDocSaleCurr )
   name = name + "Продажа платежного документа за валюту";
 elif ( edoc.Kind_Oper == CDF_OperCurrConversion )
   name = name + "Конверсия валюты";
 elif ( edoc.Kind_Oper == CDF_OperChange )
   name = name + "Замена неплатежеспособной валюты";
 elif ( edoc.Kind_Oper == CDF_OperUnpayCurrBuy )
   name = name + "Покупка неплатежеспособной валюты за рубли";
 elif ( edoc.Kind_Oper == CDF_OperIncasso )
   name = name + "Прием иностранной валюты на инкассо";
 elif ( edoc.Kind_Oper == CDF_OperExpert )
   name = name + "Прием иностранной валюты на экспертизу";
 elif ( edoc.Kind_Oper == CDF_OperReturnIncasso )
   name = name + "Возврат иностранной валюты с инкассо";
 elif ( edoc.Kind_Oper == CDF_OperReturnExpert )
   name = name + "Возврат иностранной валюты с экспертизы";
 elif ( edoc.Kind_Oper == CDF_OperPayDocIncasso )
   name = name + "Прием платежного документа на инкассо";
 elif ( edoc.Kind_Oper == CDF_OperPayDocExpert )
   name = name + "Прием платежного документа на экспертизу";
 elif ( edoc.Kind_Oper == CDF_OperPayDocReturnIncasso )
   name = name + "Возврат платежного документа с инкассо";
 elif ( edoc.Kind_Oper == CDF_OperPayDocReturnExpert )
   name = name + "Возврат платежного документа с экспертизы";
 elif ( edoc.Kind_Oper == CDF_OperPayDocBuyAcc )
   name = name + "Покупка платежного документа за рубли через счет";
 elif ( edoc.Kind_Oper == CDF_OperPayDocSaleAcc )
   name = name + "Продажа платежного документа за рубли через счет";
 elif ( edoc.Kind_Oper == CDF_OperPayDocBuyCurrAcc )
   name = name + "Покупка платежного документа за валюту через счет";
 elif ( edoc.Kind_Oper == CDF_OperPayDocSaleCurrAcc )
   name = name + "Продажа платежного документа за валюту через счет";
 end;
 return name;
end;

/*
   Нахождение эквивалента суммы в долларах для валютно-обменных операций
*/
macro EQU_EXC( curr, sum, cur_date, cur_time )
 var CBRateUSD, BuyRateUSD, SellRateUSD;
 var CBRateVal, BuyRateVal, SellRateVal;
 var equ;
 if ( curr == 1 )  /* USD */
   equ = sum;
 else
   if ( NOT GetAllCurrRate( cur_date, 1,
                             CBRateUSD, BuyRateUSD, SellRateUSD, cur_time ) )
       MsgBox("Ошибка при определении курса USD");
       Exit(1);
   end;

   if ( curr == 0 )
     equ = sum / ( BuyRateUSD );
   else
     if ( NOT GetAllCurrRate( cur_date, curr,
                               CBRateVal, BuyRateVal, SellRateVal, cur_time ) )
         MsgBox("Ошибка при определении курса валюты=", curr);
         Exit(1);
     end;

     equ = sum * SellRateVal / ( BuyRateUSD );
   end;
 end;
 return equ;
end;

/*
   Выбор из файла exoperat.dbt документов операций "Обменного пункта"
*/
macro Select_ExchangeDoc()
 var tm;

 InitProgress( 1000, " Ж Д И Т Е...", "операции ОП" );
 NDoc = 0;
 ClearRecord( edoc );
 edoc.Branch  = Branch;
 edoc.CurDate = Date1;
 stat = getGE( edoc );
 while ( stat                        AND
         ( edoc.Branch  == Branch )  AND
         ( edoc.CurDate >= Date1  )  AND
         ( edoc.CurDate <= Date2  ) )
   tm = null;
   if( edoc.ApplicationKey != "" )
     tm = Time( int( substr( edoc.ApplicationKey, 11, 2 ) ),
                int( substr( edoc.ApplicationKey, 13, 2 ) ),
                int( substr( edoc.ApplicationKey, 15, 2 ) ) );
   end;
   if ( (edoc.Kind_Oper == CDF_OperCurrBuy          )  OR  /* Покупка платежеспособной валюты за рубли */
        (edoc.Kind_Oper == CDF_OperPayDocBuy        )  OR  /* Покупка платежного документа за рубли  */
        (edoc.Kind_Oper == CDF_OperPayDocBuyCurr    )  OR  /* Покупка платежного документа за валюту */
        (edoc.Kind_Oper == CDF_OperPayDocBuyAcc     )  OR  /* Покупка платежного документа за рубли через счет  */
        (edoc.Kind_Oper == CDF_OperPayDocBuyCurrAcc )  OR  /* Покупка платежного документа за валюту через счет */
        (edoc.Kind_Oper == CDF_OperUnpayCurrBuy     )  OR  /* Покупка неплатежеспособной валюты за рубли */
        (edoc.Kind_Oper == CDF_OperIncasso          )  OR  /* Прием валюты на инкассо */
        (edoc.Kind_Oper == CDF_OperExpert           )  OR  /* Прием валюты на экспертизу */
        (edoc.Kind_Oper == CDF_OperPayDocIncasso    )  OR  /* Прием ПД на инкассо */
        (edoc.Kind_Oper == CDF_OperPayDocExpert     )      /* Прием ПД на экспертизу */
      )
     if   ( $1. * EQU_EXC( FindCodCurByRefVal(edoc.IncomeRefVal),
                           edoc.IncomeAmount,
                           edoc.CurDate,
                           tm ) >= LimInSum )
       FiscalReport( 0, edoc.ClientCode, $0, edoc.IncomeRefVal,
                     ExchName( ),
                     $0, 0,
                     edoc.IncomeAmount, edoc.IncomeRefVal,
                     edoc.Oper, edoc.CurDate );
     end;
   end;
   if ( (edoc.Kind_Oper == CDF_OperCurrSale            )  OR  /* Продажа платежеспособной валюты за рубли */
        (edoc.Kind_Oper == CDF_OperPayDocSale          )  OR  /* Продажа платежного документа за рубли  */
        (edoc.Kind_Oper == CDF_OperPayDocSaleCurr      )  OR  /* Продажа платежного документа за валюту */
        (edoc.Kind_Oper == CDF_OperPayDocSaleAcc       )  OR  /* Продажа платежного документа за рубли через счет */
        (edoc.Kind_Oper == CDF_OperPayDocSaleCurrAcc   )  OR  /* Продажа платежного документа за валюту через счет*/
        (edoc.Kind_Oper == CDF_OperReturnIncasso       )  OR  /* Возврат валюты с инкассо */
        (edoc.Kind_Oper == CDF_OperReturnExpert        )  OR  /* Возврат валюты с экспертизы */
        (edoc.Kind_Oper == CDF_OperPayDocReturnIncasso )  OR  /* Возврат ПД с инкассо */
        (edoc.Kind_Oper == CDF_OperPayDocReturnExpert  )      /* Возврат ПД с экспертизы */
      )
     if ( $1. * EQU_EXC( FindCodCurByRefVal(edoc.OutRefVal),
                         edoc.OutAmount,
                         edoc.CurDate,
                         tm ) >= LimOutSum )
       FiscalReport( 0, edoc.ClientCode, $0, edoc.OutRefVal,
                     ExchName( ),
                     edoc.OutAmount, edoc.OutRefVal,
                     $0, 0,
                     edoc.Oper, edoc.CurDate );
     end;
   end;
   if( (edoc.Kind_Oper == CDF_OperCurrConversion )  OR /* Конверсия */
       (edoc.Kind_Oper == CDF_OperChange         )     /* Замена неплатежной валюты */
     )
     if( $1. * EQU_EXC( FindCodCurByRefVal(edoc.OutRefVal), edoc.OutAmount, edoc.CurDate, tm ) >= LimOutSum )
       FiscalReport( 0, edoc.ClientCode, $0, edoc.OutRefVal,
                     ExchName( ),
                     edoc.OutAmount, edoc.OutRefVal,
                     edoc.IncomeAmount, edoc.IncomeRefVal,
                     edoc.Oper, edoc.CurDate );
     elif( $1. * EQU_EXC( FindCodCurByRefVal(edoc.IncomeRefVal), edoc.IncomeAmount, edoc.CurDate, tm ) >= LimInSum )
       FiscalReport( 0, edoc.ClientCode, $0, edoc.IncomeRefVal,
                     ExchName( ),
                     edoc.OutAmount, edoc.OutRefVal,
                     edoc.IncomeAmount, edoc.IncomeRefVal,
                     edoc.Oper, edoc.CurDate );
     end;
   end;
   NDoc = Ndoc + 1;
   if ( NDoc < 1001 )
     UseProgress( NDoc );
   end;
   stat = Next( edoc );
 end;
 RemProgress();
end;

/*
    Основная процедура
*/

var RegPathIn  = "RS-RETAIL\\СЕРВИСНЫЕ\\ЛИМИТЫ\\ЛИМИТЫ_ГНС\\ПРИХОД_НА_Ф-Л_(USD)";
var RegPathOut = "RS-RETAIL\\СЕРВИСНЫЕ\\ЛИМИТЫ\\ЛИМИТЫ_ГНС\\РАСХОД_Ф-Л_(USD)";
var Value;

//ByeBoss(0); scr#82999 

/* Определение суммы лимита для выбора документов */
if ( GetRegistryValue( RegPathIn, V_INTEGER, Value, ErrCode ) == V_INTEGER )
  LimInSum = $1. * Value;
else
  LimInSum = $10000.;
end;
if ( GetRegistryValue( RegPathOut, V_INTEGER, Value, ErrCode ) == V_INTEGER )
  LimOutSum = $1. * Value;
else
  LimOutSum = $10000.;
end;

getDate( Date1, "Введите дату начала периода" );
getDate( Date2, "Введите дату конца периода" );

/* Выбор из файла sbdepdoc.dbt документов по счетам вкладчиков */
Select_SBdepDoc();
/* Выбор из файла pay_doc.dbt документов платежей */
if ( FlagCur == 0 )  /* Только для рублей */
  Select_Pay_Doc();
end;
/* Выбор из файла exoperat.dbt документов операций "Обменного пункта" */
Select_ExchangeDoc();

end;