/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Общие функции                                                           *
*                                                                          *
*  Определение параметров банка                                            *
*                                                                          *
*  Лебедев С.В.                                                            *
*  26.12.2005                                                              *
\**************************************************************************/

import DeprIntr, BankInter;

private var partcode = Tbfile( "partcode.dbt", "r", 1 );
private var party    = Tbfile( "party.dbt", "r", 0 );
private var clientList = TClientList;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro FindParty ( partyID, kindCode, bankCode )

  array aKindCode;

  var bankID = 0;
  var i;
  var retValue = false;

  if ( ( ValType( partyID ) == V_INTEGER ) and ( partyID != 0 ) )
    bankID = partyID;
  end;

  if ( bankID == 0 )
    if ( ( ValType( bankCode ) == V_STRING ) and ( bankCode != ""  ) )

      if ( ( ValType( kindCode ) == V_INTEGER ) and ( kindCode != 0 ) )
        aKindCode( 0 ) = kindCode;
      else
        aKindCode( 0 ) = 3; /* БИК */
        aKindCode( 1 ) = 6; /* SWIFT */
      end;

      i = 0;
      while ( i < ASize( aKindCode ) )
        partcode.Clear;
        partcode.rec.CodeKind = aKindCode( i );
        partcode.rec.Code     = bankCode;
        if ( partcode.GetEQ )
          bankID = partcode.rec.PartyID;
          i = ASize( aKindCode );
        end;

        i = i + 1;
      end;
    end;
  end;

  if ( bankID != 0 )
    party.Clear;
    party.rec.PartyID = bankID;

    retValue = party.GetEQ;
  end;

  return retValue;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro CheckNotResidentBank ( partyID, kindCode, bankCode, corrAccount, recipientAccount )

  /* Определение нерезидентности по счету получателя */
  if ( ValType( recipientAccount ) == V_STRING )
    if ( ( SubStr( recipientAccount, 1, 3 ) == "426"   ) or
         ( SubStr( recipientAccount, 1, 5 ) == "40820" ) or
         ( SubStr( recipientAccount, 1, 5 ) == "40804" ) or
         ( SubStr( recipientAccount, 1, 5 ) == "40805" ) or
         ( SubStr( recipientAccount, 1, 5 ) == "40807" )   )
      return true;
    end;
  end;

  /* Определение нерезидентности по банку корреспонденту - по коду или идентификатору */
  if ( FindParty( partyID, kindCode, bankCode ) )
    if ( party.rec.NotResident != "" )
      return true;
    end;
  end;

  /* Определение нерезидентности по корреспондентскому счету */
  if ( ValType( corrAccount ) == V_STRING )
    if ( ( SubStr( corrAccount, 1, 5 ) == "30122" ) or
         ( SubStr( corrAccount, 1, 5 ) == "30123" )   )
      return true;
    end;
  end;

  return false;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro NeedFillRtPaym1 ( partyID, kindCode, bankCode, corrAccount, recipientAccount, codeClient, notResidentClient )

  var flagNotResidentBank = CheckNotResidentBank ( partyID, kindCode, bankCode, corrAccount, recipientAccount );
  var flagNotResidentClient = false;
  var flagNeedFill = false;

  /* Определение нерезидентности клиента */
  if ( ( ValType( codeClient ) == V_INTEGER ) and ( codeClient != 0 ) and clientList.GetRecord( codeClient ) )
    if ( clientList.CurRec.Rec.NotResident != "" )
      flagNotResidentClient = true;
    end;
  elif ( ( ValType( notResidentClient ) == V_STRING ) and ( notResidentClient != "" ) )
    flagNotResidentClient = true;
  end;

  if ( flagNotResidentBank or flagNotResidentClient )
    flagNeedFill = true;
  end;
    
  return flagNeedFill;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefPaymentDirection ( partyID, kindCode, bankCode, flagIncomeOperation )

  var cisCountries;
  var errCode;
  var paymentDirection = 0;
  var flagIncome = true;

  if ( GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\СТРАНЫ_СНГ", V_STRING, cisCountries, errCode) == V_STRING)
    ;
  else
    cisCountries = "";
  end;

  if ( ValType( flagIncomeOperation ) == V_BOOL )
    flagIncome = flagIncomeOperation;
  end;

  if ( FindParty ( partyID, kindCode, bankCode ) )
    if ( ( Trim( party.rec.NRCountry ) == "" ) or ( StrUpr( Trim( party.rec.NRCountry ) ) == "RUS" ) )
      paymentDirection = 5; /* Из РФ в РФ */
    elif ( Index( StrUpr( cisCountries ), StrUpr( Trim( party.rec.NRCountry ) ) ) != 0 )
      if ( flagIncome )
        paymentDirection = 2; /* Из РФ в СНГ */
      else
        paymentDirection = 4; /* Из СНГ в РФ */
      end;
    else
      if ( flagIncome )
        paymentDirection = 1; /* Из РФ в дальнее зарубежье */
      else
        paymentDirection = 3; /* Из дальнего зарубежья в РФ */
      end;
    end;
  end;

  return paymentDirection;

end;
