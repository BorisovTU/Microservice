import BrParm, Acc_Form;

record LotteryDocInfo("ci_doc.2");
record LotteryRecInfo("ci_rec.2");

var
  L_CopyCode,
  L_TotNumItems=0,
  L_TotSoldItems=0,
  L_TotBoughtItems=0,
  L_TotSoldDepo=0,
  L_TotBoughtDepo=0,
  L_TotSum=$0,
  L_TotPrice=$0,
  L_TotSumSold=$0,
  L_TotSumBought=$0;

const
  CI_LOTTERY=2;

const
  O_LOTTERY_SELL=1,
  O_LOTTERY_PAYWIN=2,
  O_LOTTERY_BUY=3;

macro L_FindCIMisc

  CIMisc.Type = CapIssueRec.Type;
  CIMisc.IssueID = CapIssueRec.Issue;
  return GetEQ( CIMisc );
end;

macro L_OutHead;

  [
                                                                               Филиал №
                                                                               отделения
                                                                               Сбербанка РФ


                               Отчет по операциям по лотереям за ##########

  ]
  (
    {curdate}
  );
end;

macro L_OutCommonHead;

  [+----------+----------------------------------------------+--------+-------------------+];
  [|    №     |        Н а з в а н и е   л о т е р е и       | Колич. |     С у м м а     |];
  [+----------+----------------------------------------------+--------+-------------------+];
end; 

macro L_OutSellHead;

  [ ];
  [Продажа лотерей];
  [ ];
  L_OutCommonHead;
end;

macro L_IsORVVZ( Name )

  return Index( Name, "1992" );
end;

macro L_GetNumItems

  var
    RecFound,
    NumItems=0;

  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;

  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    if ( CheckCIDoc )
      SetRecordAddr(LotteryDocInfo,CapIssueDoc,0,
        FldOffset(CapIssueDoc,"Info"),true);
      NumItems=NumItems+LotteryDocInfo.NumItems;
    end;
    RecFound=Next(CapIssueDoc);
  end;
  return NumItems;
end;

macro L_OutPayWinHead;

  [ ];
  [Выплата выигрышей];
  [ ];
  L_OutCommonHead;
end;

macro L_OutLine;

  SetRecordAddr(LotteryRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [|  #####   | ############################################ | ###### | ################# |]
  (
    CapIssueRec.Issue,
    LotteryRecInfo.Name,
    L_GetNumItems,
    CapIssueRec.Sum
  );
end;

macro L_OutTotals;

  SetRecordAddr(LotteryRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [+---------------------------------------------------------+--------+-------------------+];
  [|  И т о г о :                                            | ###### | ################# |]
  (
    L_TotNumItems,
    L_TotSum
  );
end;

macro L_SellReport

  var
    RecFound;

  L_TotNumItems=0;
  L_TotSum=$0;

  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_LOTTERY;

  L_OutSellHead;
  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_LOTTERY))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Brigade==OperBrigade)
      and (CapIssueRec.Operation==O_LOTTERY_SELL))
      L_OutLine; 
      SetRecordAddr(LotteryRecInfo,CapIssueRec,0,
        FldOffset(CapIssueRec,"Info"),true);
      L_TotNumItems=L_TotNumItems+L_GetNumItems;
      L_TotSum=L_TotSum+CapIssueRec.Sum;
    end;
    RecFound=Next(CapIssueRec);
  end;
  L_OutTotals;
end;
    
macro L_PayWinReport

  var
    RecFound;

  L_TotNumItems=0;
  L_TotSum=$0;

  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_LOTTERY;

  L_OutPayWinHead;
  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_LOTTERY))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Brigade==OperBrigade)
      and (CapIssueRec.Operation==O_LOTTERY_PAYWIN))
      L_OutLine; 
      SetRecordAddr(LotteryRecInfo,CapIssueRec,0,
        FldOffset(CapIssueRec,"Info"),true);
      L_TotNumItems=L_TotNumItems+L_GetNumItems;
      L_TotSum=L_TotSum+CapIssueRec.Sum;
    end;
    RecFound=Next(CapIssueRec);
  end;
  L_OutTotals;
end;
    
macro LotteryReport;

  L_OutHead;         
  L_SellReport;
  L_PayWinReport;
end;

macro L_Find;

  CIMisc.Type=CI_LOTTERY;
  CIMisc.IssueID=CapIssueRec.Issue;
  GetEQ(CIMisc);
end;

macro L_OutSellRecHead;

  var
    BranchStrNum = "",
    DepartNum = "",
    DepartName = "",
    Comment = "";

  GetBranchParams( Branch, BranchStrNum, DepartNum, DepartName );
  SetRecordAddr(LotteryRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);

  if ( not L_IsORVVZ( LotteryRecInfo.Name ) )
    Comment = "(Лотерея \""+LotteryRecInfo.Name+"\")";
  end;

  [                                                                                                                                        Ф. № 56-#
     ##################################                                                                          
                                                                                                                                 Филиал № ##########
     Сберегательного банка № ##########         


                                                              
                                                               КОНТРОЛЬНАЯ  ВЕДОМОСТЬ
                        
                                                    на продажу и покупку облигаций Российского
                                                          внутреннего выигрышного займа
                                                                     1992 года
                                                ##################################################
                                                                   за ##########
          

     Смена № #####

  ]
  (
    L_CopyCode,
    DepartName,
    BranchStrNum,
    DepartNum,
    Comment:c,
    {curdate},
    OperBrigade
  );
  [+--------+--------------------------------------------------------+---------------------------------------------------------+-------------------+];
  [|Номерной|                     П р о д а н о                      |                       К у п л е н о                     |   П о д п и с и   |];
  [| талон, |---------+-----------+-----------------+----------------|---------+-----------+-----------------+-----------------|---------+---------|];
  [|выданный| Номинал |Количество |Номинальная стои-|Получено за     | Номинал |Количество |Номинальная стои-| Получено за     |         |         |];
  [|клиенту |облигации|-----+-----|мость проданных  |проданные       |облигации|-----+-----|мость купленных  | купленные       |контролер| кассир  |];
  [|        |         | шт  |д/шт |облигаций        |облигации       |         | шт  |д/шт |облигаций        | облигации       |         |         |];
  [+--------+---------+-----+-----+-----------------+----------------+---------+-----+-----+-----------------+-----------------+---------+---------+];
end;                                                                                                                                                
                                                                                                                                                    
macro L_GetDepo( NumItems, Name )                                                                                                                   
                                                                                                                                                    
  if ( L_IsORVVZ( Name ) )                                                                                                                          
    return Int( Double( NumItems * CIMisc.Price * 2 ) );                                                                                      
  else                                                                                                                          
    return 0;
  end;
end;

macro L_OutSellRecLine;

  var
    PriceRepr=$0,
    BoughtPriceRepr=$0,
    SoldItems=0,
    BoughtItems=0,
    SoldDepo=0,
    BoughtDepo=0,
    SoldPar=$0,
    BoughtPar=$0,
    SumSoldRepr=$0,
    SumBoughtRepr=$0;
 
  L_FindCIMisc;

  SetRecordAddr(LotteryDocInfo,CapIssueDoc,0,
    FldOffset(CapIssueDoc,"Info"),true);
  if(CapIssueDoc.Operation==O_LOTTERY_SELL) 
    SoldItems = LotteryDocInfo.NumItems;
    L_TotSoldItems = L_TotSoldItems + SoldItems;
    SoldDepo = L_GetDepo( SoldItems, LotteryDocInfo.Name );
    L_TotSoldDepo = L_TotSoldDepo + SoldDepo;
    L_TotSumSold=L_TotSumSold+CapIssueDoc.Sum;
    if ( L_IsORVVZ( LotteryDocInfo.Name) )
      PriceRepr=Money( CIMisc.Price * LotteryDocInfo.NumItems );
      SoldPar = CIMisc.Price;
    else
      PriceRepr=LotteryDocInfo.Price;
    end;
    SumSoldRepr=CapIssueDoc.Sum;
  elif(CapIssueDoc.Operation==O_LOTTERY_BUY) 
    BoughtItems = LotteryDocInfo.NumItems;
    L_TotBoughtItems = L_TotBoughtItems + BoughtItems;
    BoughtDepo = L_GetDepo( BoughtItems, LotteryDocInfo.Name  );
    L_TotBoughtDepo = L_TotBoughtDepo + BoughtDepo;
    L_TotSumBought=L_TotSumBought+CapIssueDoc.Sum;
    if ( L_IsORVVZ( LotteryDocInfo.Name ) )
      BoughtPriceRepr=Money( CIMisc.Price * LotteryDocInfo.NumItems );
      BoughtPar = CIMisc.Price;
    else
      BoughtPriceRepr=LotteryDocInfo.Price;
    end;
    SumBoughtRepr=CapIssueDoc.Sum;
  end;

  [|########|#########|#####|#####|#################|################|#########|#####|#####|#################|#################|         |         |]
  (
    CapIssueDoc.DocNumber,
    SoldPar:z,
    SoldItems:z,
    SoldDepo:z,
    PriceRepr:z,
    SumSoldRepr:z,
    BoughtPar:z,
    BoughtItems:z,
    BoughtDepo:z,
    BoughtPriceRepr:z,
    SumBoughtRepr:z
  );
end;

macro L_LotterySellRecTotals;
 
  var
    SellCredAcc,
    SellDebAcc,
    BuyCredAcc,
    BuyDebAcc;

  GetBookAcc(O_LOTTERY_SELL, SellDebAcc, SellCredAcc);
  GetBookAcc(O_LOTTERY_BUY, BuyDebAcc, BuyCredAcc);
  SetRecordAddr(LotteryDocInfo,CapIssueDoc,0,
    FldOffset(CapIssueDoc,"Info"),true);
  [+--------+---------+-----+-----+-----------------+----------------+---------+-----+-----+-----------------+-----------------+---------+---------+];
  [| Всего  |         |#####|#####|                 |################|         |#####|#####|                 |#################|         |         |]
  (
    L_TotSoldItems,
    L_TotSoldDepo,
    L_TotSumSold,
    L_TotBoughtItems,
    L_TotBoughtDepo,
    L_TotSumBought
  );
  [ ];
  [                                                    Д-т сч. ];
  [                                                    ########################## ]
  (Acc_Form(BuyDebAcc));
  [ ];
  [                                 Д-т сч. ];
  [                                 ########################## ]
  (Acc_Form(SellDebAcc));
  [ ];
  [             Расход по сч.                          К-т сч. ];
  [             ___________________                    ########################## ]
  (Acc_Form(BuyCredAcc));
  [ ];
  [                                 К-т сч. ];
  [                                 ########################## ]
  (Acc_Form(SellCredAcc));
  [ ];
  [                                                    Приход по сч.];
  [                                                    _________________ ];
end;

macro L_OutPayRecHead;

  var
    BranchStrNum = "",
    DepartNum = "",
    DepartName = "",
    DebAcc,
    CredAcc,
    Comment = "";

  GetBranchParams( Branch, BranchStrNum, DepartNum, DepartName );
  GetBookAcc(CapIssueRec.Operation, DebAcc, CredAcc);
  SetRecordAddr(LotteryRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);

  if ( not L_IsORVVZ( LotteryRecInfo.Name ) )
    Comment = "(Лотерея \""+LotteryRecInfo.Name+"\")";
  end;

  [                                                                                                          Ф. № 92-#
     ##################################                                                                          
                                                                                                   Филиал № ##########
     Сберегательного банка № ##########         


  
                                                 КОНТРОЛЬНАЯ  ВЕДОМОСТЬ                    Отметки бухгалтерии 

                                           на оплату выигравших ценных бумаг         Дебет сч.  ######################
                                        #######################################      Кредит сч. ######################
                                                     за ##########                   Приход сч. ______________________
                                                                                             
                                                                                     
     Смена № #####

  ]
  (
    L_CopyCode,
    DepartName,
    BranchStrNum,
    DepartNum,
    Acc_Form(DebAcc),
    Comment:c,
    Acc_Form(CredAcc),
    {curdate},
    OperBrigade
  );
  [+-------------------------------------+-------+-------+-----------+--------+--------+---------+-------------------+
   |                                     | Номер |       |           |        |Нарицат.|         |                   |
   |       Название ценной бумаги        |  тир. | Серия |   Номер   | Разряд |стоим-ть| Кол-во  |     Выплачено     |
   |                                     |       |       |           |        |        |         |                   |
   +-------------------------------------+-------+-------+-----------+--------+--------+---------+-------------------+];
end;

macro L_OutPayRecLine;

  SetRecordAddr(LotteryDocInfo,CapIssueDoc,0,
    FldOffset(CapIssueDoc,"Info"),true);
  [| ################################### | ##### | ##### | ######### |        | ###### | ####### | ################# |]
  (
    CIMisc.Name,
    LotteryDocInfo.Drawing,
    LotteryDocInfo.Series,
    LotteryDocInfo.Number,
    LotteryDocInfo.Price,
    LotteryDocInfo.NumItems,
    CapIssueDoc.Sum
  );
  L_TotPrice=L_TotPrice+LotteryDocInfo.Price;
  L_TotSum=L_TotSum+CapIssueDoc.Sum;
  L_TotNumItems=L_TotNumItems+LotteryDocInfo.NumItems;
end;

macro L_LotteryPayRecTotals;
 
  SetRecordAddr(LotteryDocInfo,CapIssueDoc,0,
    FldOffset(CapIssueDoc,"Info"),true);
  [+-------------------------------------+-------+-------+-----------+--------+--------+---------+-------------------+];
  [| В с е г о                           |       |       |           |        |        | ####### | ################# |]
  (
    L_TotNumItems,
    L_TotSum
  );
  [ ];
  [  # ]( RubToStr( L_TotSum ) );
  [  ---------------------------------------------------------------------- ];
  [                          (сумма прописью) ];
  [ ];
end;

macro L_OutRecTail;

  [



        Контролер (оператор) ___________                                Кассир ____________________
  ];
end;

macro L_LotterySellRecBody;

  var
    RecFound;

  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;

  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    if ( CheckCIDoc )
      L_OutSellRecLine;
    end;
    RecFound=Next(CapIssueDoc);
  end;
end;

macro L_LotterySellRecCopy;

  var
    RecFound;
 
  L_TotNumItems=0;
  L_TotSoldItems=0;
  L_TotBoughtItems=0;
  L_TotSoldDepo=0;
  L_TotBoughtDepo=0;
  L_TotSum=$0;
  L_TotPrice=$0;
  L_TotSumSold=$0;
  L_TotSumBought=$0;

  SetRecordAddr(LotteryRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);

  L_OutSellRecHead; 
  if ( not L_IsORVVZ( LotteryRecInfo.Name ) )
    L_LotterySellRecBody;
    L_LotterySellRecTotals;
  else
    GetPos( CapIssueRec );
    KeyNum( CapIssueRec, 0 );
    ClearRecord(CapIssueRec);
    CapIssueRec.Branch=Branch;
    CapIssueRec.IsCur=0;
    CapIssueRec.Type=CI_LOTTERY;
  
    RecFound=GetGE(CapIssueRec);
    while(RecFound
      and (CapIssueRec.Branch==Branch)
      and (CapIssueRec.IsCur==0)
      and (CapIssueRec.Type==CI_LOTTERY))
      if((CapIssueRec.Date=={curdate})
        and (CapIssueRec.Brigade==OperBrigade))
        if(((CapIssueRec.Operation==O_LOTTERY_SELL)
          or (CapIssueRec.Operation==O_LOTTERY_BUY))
          and L_IsORVVZ( LotteryRecInfo.Name )) 
          L_LotterySellRecBody;
        end;
      end;
      RecFound=Next(CapIssueRec);
    end; 
    GetDirect( CapIssueRec );
    L_LotterySellRecTotals;
  end;  

  L_OutRecTail;
  PrintLn( StrFor( 12 ) );
end;

macro L_LotterySellRec

  L_CopyCode = "А";
  L_LotterySellRecCopy;
  L_CopyCode = "Б";
  L_LotterySellRecCopy;
end;

macro L_LotteryPayRecBody;

  var
    RecFound;

  L_Find;
  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;
  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    if ( CheckCIDoc )
      L_OutPayRecLine;
    end;
    RecFound=Next(CapIssueDoc);
  end;
end;

macro L_LotteryPayRecCopy;
 
  L_TotNumItems = 0;
  L_TotSum = $0;
  L_OutPayRecHead; 
  L_LotteryPayRecBody;
  L_LotteryPayRecTotals;
  L_OutRecTail;
  PrintLn( StrFor( 12 ) );
end;

macro L_LotteryPayRec;
  
  L_CopyCode = "А";
  L_LotteryPayRecCopy;
  L_CopyCode = "Б";
  L_LotteryPayRecCopy;
end;

macro LotteryRec(ApplicationKind,ApplicationKey);
      
  KeyNum(CapIssueRec,2);                                        
  CapIssueRec.ApplicationKind=ApplicationKind;
  CapIssueRec.ApplicationKey=ApplicationKey;
  if(not GetEQ(CapIssueRec))
    MsgBox("Ошибка при печати ведомости");
    return;
  end;
  if(CapIssueRec.Operation==O_LOTTERY_SELL) 
    L_LotterySellRec;
  elif(CapIssueRec.Operation==O_LOTTERY_PAYWIN) 
    L_LotteryPayRec;
  end;
end;

macro L_SellBuyReportCopy

  var
    NotEmpty = false,
    PrevIssue = 0,
    RecFound;
 
  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_LOTTERY;

  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_LOTTERY))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Brigade==OperBrigade))
      if((CapIssueRec.Operation==O_LOTTERY_SELL)
        or (CapIssueRec.Operation==O_LOTTERY_BUY)) 
        if(CapIssueRec.Issue!=PrevIssue)
          if(PrevIssue)
            L_LotterySellRecTotals;
          end;
          PrevIssue=CapIssueRec.Issue;
          L_TotPrice=$0;
          L_TotSumSold=$0;
          L_TotSumBought=$0;
          L_OutSellRecHead;
          NotEmpty = true;
        end; 
        L_LotterySellRecBody;
      end;
    end;
    RecFound=Next(CapIssueRec);
  end;
  if(NotEmpty)
    L_LotterySellRecTotals;
  end;

  PrintLn;
  PrintLn;
end;

macro L_SellBuyReport

  Exit( 1 );

  L_CopyCode = "А";
  L_SellBuyReportCopy;
  L_CopyCode = "Б";
  L_SellBuyReportCopy;
end;

macro LotteryRecReport;
  
  var
    RecFound;

/*  L_SellBuyReport;*/

  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_LOTTERY;

  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_LOTTERY))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Brigade==OperBrigade))
      if(CapIssueRec.Operation==O_LOTTERY_PAYWIN) 
        L_TotPrice=$0;
        L_TotSum=$0;
        L_LotteryPayRec;
      end;
    end;
    RecFound=Next(CapIssueRec);
  end;
end;    
