/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Формирование налоговых карточек и справок о доходах
  физического лица для налоговых органов.

  Форма "Справка о доходах" в формате DBF.

*****************************************************************/

Import rPcTax1c,rtkladr;

file fPers ( "pers.dbf" ) dbf write;
file fDoxn ( "doxn.dbf" ) dbf write;

file cPers ( "pers.dbf" ) dbf write;
file cDoxn ( "doxn.dbf" ) dbf write;

const DateNULL = Date( 0, 0, 0 );

var TaxFileDir;

/***************************************************************************
***************************************************************************/
macro Дата_получения_дохода( pMon )
  var vDate;
  var vDay;
  if   ( pMon ==  1 )
    vDay = 31;
  elif ( pMon ==  2 )
    vDay = 28;
  elif ( pMon ==  3 )
    vDay = 31;
  elif ( pMon ==  4 )
    vDay = 30;
  elif ( pMon ==  5 )
    vDay = 31;
  elif ( pMon ==  6 )
    vDay = 30;
  elif ( pMon ==  7 )
    vDay = 31;
  elif ( pMon ==  8 )
    vDay = 31;
  elif ( pMon ==  9 )
    vDay = 30;
  elif ( pMon == 10 )
    vDay = 31;
  elif ( pMon == 11 )
    vDay = 30;
  elif ( pMon == 12 )
    vDay = 31;
  end;
  vDate = Date( vDay, pMon, InputPanel_Year_InAll );
  return vDate;
end;

/***************************************************************************
***************************************************************************/
macro Цикл_по_Доходам()
  var stat = TRUE;
  var vNalUd, vNalVoz;

  if ( mPcRate.rec.CommonTaxPay > $0.0 )
    vNalUd  = mPcRate.rec.CommonTaxPay;
    vNalVoz = $0.0;
  else
    vNalUd  = $0.0;
    vNalVoz = -mPcRate.rec.CommonTaxPay;
  end;

  mPcTax.Clear();
  mPcTax.rec.TaxRate = mPcRate.rec.TaxRate;
  stat = mPcTax.GetGE();

  while ( stat  AND  ( mPcTax.rec.TaxRate == mPcRate.rec.TaxRate ) )

    if ( ( mPcTax.rec.IncomeRUR       != 0.0 )  OR
         ( mPcTax.rec.SummaTaxCalcRUR != 0.0 )  OR
         ( mPcTax.rec.SummaTaxPayRUR  != 0.0 ) )

      ClearRecord( fDoxn );
      fDoxn.TabNom   = COMCLNT.CurRec.rec.CodClient;
      fDoxn.DateOper = Дата_получения_дохода( mPcTax.rec.Mon );
      fDoxn.KodNac   = mPcTax.rec.CodIncome;
      fDoxn.God      = InputPanel_Year_InAll;
      fDoxn.Mes      = mPcTax.rec.Mon;
      fDoxn.SumNac   = mPcTax.rec.IncomeRUR;
      fDoxn.KodVic   = "";
      fDoxn.SumVic   = $0.0;
      fDoxn.NalUd    = RoundToRuble( vNalUd );
      fDoxn.NalVoz   = RoundToRuble( vNalVoz );

      stat = Insert( fDoxn );

    end;

    stat = mPcTax.Next();
  end;

  return stat;
end;

/***************************************************************************
***************************************************************************/
macro DBF_Адрес( pAddressType )
  var sAddr;
  var sRegion   = "";
  var sProvince = "";
  var sDistrict = "";
  var sPlace    = "";
  var sCorpus, iCorpus;

  COMCLNT.AdressType = pAddressType;

  if ( StrLen( COMCLNT.CurRec.rec.Address ) == 0 )
    return "";
  end;

  if ( COMCLNT.CurRec.rec.Country != "" )
    sAddr = COMCLNT.CurRec.rec.Country;
  else
    sAddr = "RUS";
  end;
  sAddr = sAddr + "," + COMCLNT.CurRec.rec.PostIndex;
// Номер региона
  sRegion = getRegionNumber( COMCLNT.CurRec );
// Район.
  if ( COMCLNT.CurRec.rec.Province != "" )
      sProvince = COMCLNT.CurRec.rec.Province + " " 
                + Trim(String(COMCLNT.CurRec.rec.lCodeProvince));
  end;
// Город
  if ( COMCLNT.CurRec.rec.District != "" )
      sDistrict = COMCLNT.CurRec.rec.District + " "
                + Trim(String(COMCLNT.CurRec.rec.lCodeDistrict));
  end;
// Населенный пункт
  if ( COMCLNT.CurRec.rec.Place != "" )
      sPlace = Trim( COMCLNT.CurRec.rec.Place ) + " "
             + Trim(String(COMCLNT.CurRec.rec.lCodePlace));
  end;

  sAddr = sAddr + "," + sRegion;
  sAddr = sAddr + "," + sProvince;
  sAddr = sAddr + "," + sDistrict;
  sAddr = sAddr + "," + sPlace;
  sAddr = sAddr + "," + Trim( COMCLNT.CurRec.rec.StreetName ) + " "
                      + Trim(String(COMCLNT.CurRec.rec.lCodeStreet));

  sAddr = sAddr + "," + Trim( COMCLNT.CurRec.rec.House );

  /* Обработка строки "Корпус" */
  iCorpus = Index( StrUpr( COMCLNT.CurRec.rec.NumCorp ), "КОРПУС" );
  if ( iCorpus > 0 )
    if ( StrLen(COMCLNT.CurRec.rec.NumCorp) > ( iCorpus + 5 ) )
      sCorpus = SubStr( COMCLNT.CurRec.rec.NumCorp, iCorpus + 6 );
    else
      sCorpus = "";
    end;
  else
    sCorpus = COMCLNT.CurRec.rec.NumCorp;
  end;

  sAddr = sAddr + "," + sCorpus;
  sAddr = sAddr + "," + Trim( COMCLNT.CurRec.rec.Flat );

  return sAddr;
end;

/***************************************************************************
***************************************************************************/
macro Содержание_DBF_файла()
  var stat = TRUE;
  var vAddrReg;
  var vAddrFakt;
  var sIDCode;
  var sCountry;
  var vPol;
  var vRez;
  var vDuty;
  var vDolgNp;
  var vDolgNa;

  vAddrReg  = DBF_Адрес( 1 );

  vAddrFakt = DBF_Адрес( 2 );

  if ( ( COMCLNT.CurRec.rec.NotResident == StrFor(0) )  AND
       ( StrLen( vAddrFakt ) == 0 ) )
    vAddrFakt = vAddrReg;
  end;

  // Код удостоверения личности.
  if ( GetPaprKind( COMCLNT.CurRec.rec.PaperKind ) )
    sIDCode = PAPRKIND.CodeDocum;
  else
    sIDCode = COMCLNT.CurRec.rec.PaperKind;
  end;

  // Построение формата гражданства.
  ClearRecord( cntr );
  cntr.CodeLat3 = COMCLNT.CurRec.rec.NRCountry;
  if ( getEQ( cntr ) )
    sCountry = cntr.CodeNum3;
  else
    sCountry = COMCLNT.CurRec.rec.NRCountry
  end;

  if ( COMCLNT.CurRec.rec.IsMale != StrFor(0) )
    vPol = "М";
  else
    vPol = "Ж";
  end;

  if ( COMCLNT.CurRec.rec.NotResident == StrFor(0) )
    vRez = "1";
  else
    vRez = "2";
  end;

  vDuty = mPcRate.rec.CommonTaxCalc - mPcRate.rec.CommonTaxPay;
  if ( vDuty > $0.0 )
    vDolgNp = vDuty;
    vDolgNa = $0.0;
  else
    vDolgNp = $0.0;
    vDolgNa = -vDuty;
  end;

  ClearRecord( fPers );
  fPers.TabNom  = COMCLNT.CurRec.rec.CodClient;
  fPers.Fam     = COMCLNT.CurRec.rec.Name1;
  fPers.Name    = COMCLNT.CurRec.rec.Name2;
  fPers.Otc     = COMCLNT.CurRec.rec.Name3;
  fPers.DateRog = COMCLNT.CurRec.rec.Born;
  fPers.Adres   = vAddrReg;
  fPers.Dok     = sIDCode + "," + COMCLNT.CurRec.rec.PaperSeries + " " +
                                  COMCLNT.CurRec.rec.PaperNumber;
  fPers.Dvid    = COMCLNT.CurRec.rec.PaperIssuedDate;
  fPers.Kvid    = COMCLNT.CurRec.rec.PaperIssuer;
  fPers.InnFl   = COMCLNT.CurRec.rec.INN;
  fPers.Strax   = COMCLNT.CurRec.rec.SocialNumber;
  fPers.Mrog    = COMCLNT.CurRec.rec.RaionBorn + " " +
                  COMCLNT.CurRec.rec.PlaceBorn;
  fPers.Pol     = vPol;
  fPers.Grag    = sCountry;
  fPers.Afakt   = vAddrFakt;
  fPers.WStatus = vRez;
  fPers.Ted     = COMCLNT.CurRec.rec.PhoneNumber;
  fPers.Tes     = COMCLNT.CurRec.rec.PhoneNumber2;
  fPers.DateRab = DateNULL;
  fPers.DateUvo = DateNULL;
  fPers.PFR     = "-";
  fPers.Avia    = "";
  fPers.KodKat  = "";
  fPers.GrInv   = 0;
  fPers.Inseria = "";
  fPers.Innom   = "";
  fPers.Indata  = DateNULL;
  fPers.Insrok  = DateNULL;
  fPers.Tusl    = "";
  fPers.Koef    = 0.0;
  fPers.BlMes   = 0;
  fPers.BlDen   = 0;
  fPers.BsMes   = 0;
  fPers.BsDen   = 0;
  fPers.DolgNp  = RoundToRuble( vDolgNp );
  fPers.DolgNa  = RoundToRuble( vDolgNa );
  fPers.Podr    = "";
  fPers.Dolgn   = "";
  fPers.PredSum = $0.0;
  fPers.PredSkm = $0.0;

  stat = Insert( fPers );

  if ( stat )
    stat = Цикл_по_Доходам();
  end;

  return stat;
end;

/***************************************************************************
***************************************************************************/
macro Открытие_файлов( pRate )
  var stat = TRUE;
  var strNum;
  var fileName;
  var pathName;

  strNum = SubStr( String( Int( pRate ) + 100 ), 2, 2 );

  // Файл персональных данных.
  fileName = "PERS" + strNum;
  pathName = TaxFileDir + fileName + ".dbf";
  if ( NOT ExistFile( pathName ) )
    stat = Create( cPers, pathName );
    PrintLn( " Создан DBF-файл " + pathName );
  end;
  if ( stat )
    stat = Open( fPers, pathName );
  end;
  if ( NOT stat )
    PrintLn( " *** Ошибка при открытии файла ", pathName );
  end;

  // Файл сведений о доходах и налогах.
  fileName = "DOXN" + strNum;
  pathName = TaxFileDir + fileName + ".dbf";
  if ( NOT ExistFile( pathName ) )
    stat = Create( cDoxn, pathName );
    PrintLn( " Создан DBF-файл " + pathName );
  end;
  if ( stat )
    stat = Open( fDoxn, pathName );
  end;
  if ( NOT stat )
    printLn( " *** Ошибка при открытии файла ", pathName );
  end;

  return stat;
end;

/***************************************************************************
***************************************************************************/
macro Закрытие_файлов()
  Close( fPers );
  Close( fDoxn );
end;

/***************************************************************************
***************************************************************************/
macro Формирование_DBF_файла( pRate )
  var stat = Открытие_файлов( pRate );
  if ( stat )
    stat = Содержание_DBF_файла();
  end;
  Закрытие_файлов();
  return stat;
end;

/***********************************************************
***********************************************************/
macro DBF_файлы_по_клиенту()
  var stat = True;

  mPcRate.KeyNum = 1;
  mPcRate.ReWind();

  while ( mPcRate.Next()  AND  ( mPcRate.rec.Zero == 0 ) )
    Формирование_DBF_файла( mPcRate.rec.TaxRate );
  end;

  if ( NOT stat )
    ErrCode = Status( ErrText );
    if ( ( ErrCode == 0 )  OR  ( ErrCode == 4 )  OR  ( ErrCode == 9 ) )
     stat = TRUE;
    end;
  end;

  return stat;
end;

/***************************************************************************
***************************************************************************/
macro Выпуск_налоговой_карточки()
  var stat = ERR_OK;
  var st;
  var NumCycle = 0;
  var vChanel;

  while ( ( stat == ERR_OK )  OR  ( stat == ERR_SKIP ) )
    NumCycle = NumCycle + 1;
    stat = Выбрать_Данные_По_Очередному_клиенту( NumCycle );
    if ( stat == ERR_OK )
      COMCLNT.GetRecord( COMCLNT.CurRec.rec.CodClient );
      DBF_файлы_по_клиенту();
    end;
  end;

  if ( stat == ERR_REPEAT )
    st = False; /* Требуется повторить ввод данных */
  else
    st = True;
  end;
  return st;
end;

/***************************************************************************
***************************************************************************/
macro Удаление_DBF_файлов()
  var stat = TRUE;
  var fileName = "";
  var pathName = "";
  var strNum = "";
  var i = 100;

  while ( stat  AND  ( i < 200 ) )
    strNum = SubStr( String( i ), 2, 2 );

    // Файлы персональных данных.
    fileName = "PERS" + strNum;
    pathName = TaxFileDir + fileName + ".dbf";
    if ( ExistFile( pathName ) )
      stat = DelFile( pathName );
      if ( NOT stat )
        PrintLn( "Не удалось удалить файл ", pathName );
      end;
    end;

    // Файлы доходов и налогов.
    if ( stat )
      fileName = "DOXN" + strNum;
      pathName = TaxFileDir + fileName + ".dbf";
      if ( ExistFile( pathName ) )
        stat = DelFile( pathName );
        if ( NOT stat )
          PrintLn( "Не удалось удалить файл ", pathName );
        end;
      end;
    end;

    i = i + 1;
  end;

  return stat;
end;

/***************************************************************************
                            Головная процедура.
***************************************************************************/
macro rpc_MakeDBF_FileTaxCard()
  var stat     = False;
  var NumCycle = 1;

  TaxFileDir =
      GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\TAXATION_EXPORT", TRUE );

  while (NOT stat)
    if ( Ввод_исходных_данных( "Форм-ие DBF-файлов справки о доходах" ) )
      stat = Удаление_DBF_файлов();
      if ( stat )
        stat = Выпуск_налоговой_карточки();
      else
        println( "Не удалось удалить старые DBF-файлы." );
        stat = TRUE;
      end;
    else
      [ Отказались от Формирования файла для налоговых органов ];
      stat = TRUE;
    end;
    NumCycle = NumCycle + 1;
  end;
end;

/***************************************************************************
                         Вызов головной процедуры.
***************************************************************************/
rpc_MakeDBF_FileTaxCard();
