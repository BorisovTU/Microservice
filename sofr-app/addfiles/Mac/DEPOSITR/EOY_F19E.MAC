/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Филиал                                                                  *
*                                                                          *
*  Завершение года. Форма 19 (новая) (с учетом пересчета в Евро)           *
*                                                                          *
*  Лебедев С.В.                                                            *
*  28.12.2001                                                              *
\**************************************************************************/

import deprintr, IsSrvDoc, OperCode;

file currency   (currency) key 0;
file sb_dtyp    (sb_dtyp ) key 2;
file sb_dtyp_s  (sb_dtyp ) key 1;
file depositr   (depositr) key 1;
file sbdepdoc   (sbdepdoc) key 1;
/*file listfdep   (listfdep) key 0;*/
var ddep = TRecHandler( "i_dp_dep.rec" );

const DisplayAll   = FALSE;  /* Все счета */
const LinesPerPage = 59;

const SA_STATIC = "Неподвижный";
const SA_UNITED = "Объединенный";
const SA_CHILD1 = "Целев. дети";
const SA_CHILD2 = "Целевой на д";

const BS_BRANCH  = 0; /* Филиал */
const BS_DEP     = 1; /* Отделение */
const BS_EXCH    = 2; /* Обменный пункт */
const BS_OPERDEP = 3; /* Оперчасть */

var {curdate};
var IsCur        = NumFlagCur();
var Branch       = NumFNCash();
var BranchStatus = GetBranchStatus();
var BranchName   = "";
var Mode         = GetCurrentMode();
var CurBalAcc    = "";
var WasChild     = FALSE;
var ClientList   = TClientList;

var RecCount = 0;
var BrdCount = 0;

var RepDate;
var MonthRepDate;
var CurrSubHeadDisplayed;
var BANumbDisplayed;
var TANameDisplayed;
var BranchDisplayed;
var AtLeastOneAccountFound;
var N       = 1;
var NPage   = 0;
var CurLine = 1;
var CurPage = 0;

var CrossRateEURO = 0.0;

var BeforeP   = $0L;
var PercP     = $0L;
var CalcP     = $0L;
var BehindP   = $0L; 
var BehindPE  = $0L; 

var CountTA   = 0;
var BeforeTA  = $0L;
var PercTA    = $0L;
var CalcTA    = $0L;
var BehindTA  = $0L; 
var BehindTAE = $0L; 

var CountBA   = 0;
var BeforeBA  = $0L;
var PercBA    = $0L;
var CalcBA    = $0L;
var BehindBA  = $0L;
var BehindBAE = $0L;

var CountCr   = 0;
var BeforeCr  = $0L;
var PercCr    = $0L;
var CalcCr    = $0L;
var BehindCr  = $0L;
var BehindCrE = $0L;

var PrintSummary = FALSE;

array ABalAccounts;

array APage;
array ACount;
array ABefore;
array APerc;
array ACalc;
array ABehind;
array ABehindE;

array RatesEURO;
  RatesEURO( 0) = "ATS13.7603";
  RatesEURO( 1) = "BEF40.3399";
  RatesEURO( 2) = "GRD340.750";
  RatesEURO( 3) = "IEP0.787564";
  RatesEURO( 4) = "ESP166.386";
  RatesEURO( 5) = "ITL1936.27";
  RatesEURO( 6) = "LUF40.3399";
  RatesEURO( 7) = "DEM1.95583";
  RatesEURO( 8) = "NLG2.20371";
  RatesEURO( 9) = "PTE200.482";
  RatesEURO(10) = "FIM5.94573";
  RatesEURO(11) = "FRF6.55957";
  RatesEURO(12) = "FFR6.55957";


/**************************************************************************\
|          Определить курс ЕВРО по отношению к переводимой валюте          |
\**************************************************************************/
macro DefCrossRateEURO

  var i = 0;
    
  CrossRateEURO = 0.0;

  while (i < ASize(RatesEURO))
    if (SubStr(RatesEURO(i),1,3) == currency.Short_Name)
      CrossRateEURO = Double(SubStr(RatesEURO(i),4));
    end;
    i = i + 1;
  end;

end;


/**************************************************************************\
|                Найти филиал, по которому выводится отчет                 |
\**************************************************************************/
macro DefBranchName

  if (findDepartment(Branch, null, ddep))
    BranchName = ddep.rec.Name;
  else
    BranchName = "";
  end;

end;


/**************************************************************************\
|                       Поиск вкладчика по его коду                        |
\**************************************************************************/
macro FindDepClient (Code)

  return ClientList.GetRecord( Code );

end;


/**************************************************************************\
|               Определение даты, за которую выводится отчет               |
\**************************************************************************/
macro SetDates

  var d,m,y;
  var YesNoMes = "";

  RepDate = {curdate};

  if (RepDate != Date(0,0,0))
    DateSplit(RepDate,d,m,y);
    if (m <= 3)
      if ((m ==  3) AND (d >= 20))
        RepDate = Date(31, 3,y  );
      else
        RepDate = Date(31,12,y-1);
      end;
    elif (m <= 6)
      if ((m ==  6) AND (d >= 20))
        RepDate = Date(30, 6,y  );
      else
        RepDate = Date(31, 3,y  );
      end;
    elif (m <= 9)
      if ((m ==  9) AND (d >= 20))
        RepDate = Date(30, 9,y  );
      else
        RepDate = Date(30, 6,y  );
      end;
    else
      if ((m == 12) AND (d >= 20))
        RepDate = Date(31,12,y  );
      else
        RepDate = Date(30, 9,y  );
      end;
    end;
  end;

  DateSplit(RepDate,NULL,MonthRepDate,y);

  if (MonthRepDate == 3)
    YesNoMes = "Выпустить форму 19 по результатам завершения I квартала " + String(y) + " года ?";
  elif (MonthRepDate == 6)
    YesNoMes = "Выпустить форму 19 по результатам завершения II квартала " + String(y) + " года ?";
  elif (MonthRepDate == 9)
    YesNoMes = "Выпустить форму 19 по результатам завершения III квартала " + String(y) + " года ?";
  elif (MonthRepDate == 12)
    YesNoMes = "Выпустить форму 19 по результатам завершения " + String(y) + " года ?";
  end;

  if (YesNoMes != "")
    if (NOT GetTrue(TRUE,YesNoMes))
      Exit(1);
    end;
  end;

end;


/**************************************************************************\
|                            Заголовок отчета                              |
\**************************************************************************/
macro OutHead

  var Head1 = "Выписка остатков вкладов по лицевым счетам вкладчиков";
  var Head2 = "по состоянию на " + String(RepDate:m);

  if (NOT PrintSummary)
    [                                                               Приложение 1];
    [---------------------------------------------------------------------------];
    [                                                                 ##########]
    (RepDate);
    [ ОСБ № ______ Филиал № #                                         Форма № 19]
    (BranchName);
    [ ];
    [###########################################################################]
    (Head1:c);
    [###########################################################################]
    (Head2:c);
    [ ];
    CurLine = 8;

    if (IsCur)
      [#](currency.Name_Currency);
      [#](MkStr("=",StrLen(currency.Name_Currency)));
      [ ];
      CurLine = CurLine + 3;
    end;
  else
    Head1 = "Сводная постраничная выписка по вкладу";
    Head2 = "по состоянию на " + String(RepDate:m);
    [                                                               Приложение 1];
    [---------------------------------------------------------------------------];
    [                                                                 ##########]
    (RepDate);
    [ ОСБ № ______ Филиал № #                                         Форма № 19]
    (BranchName);
    [ ];
    [###########################################################################]
    (Head1:c);
    [###########################################################################]
    (Head2:c);
    [ ];
    CurLine = 8;
  end;
  
end;


/**************************************************************************\
|                            Подзаголовок отчета                           |
\**************************************************************************/
macro OutSubHead

  if (NOT PrintSummary)
    if ((sb_dtyp.Kind == SA_CHILD1) OR (sb_dtyp.Kind == SA_CHILD2))
      if (CrossRateEURO > 0.0)
        [+-------+-----------------------+---------------+-----------+-------------+-------------+];
        [|       |                       |Ост. на конец  |Причисл. %%|Ост. на конец|Ост. на конец|];
        [| № п/п |  Номер лицевого счета |отч. периода   |     /     |отч. периода |отч. периода |];
        [|       |                       |без причисл. %%|Начисл. %% |с причисл. %%|в EUR        |];
        [+-------+-----------------------+---------------+-----------+-------------+-------------+];
      else
        [+-------+-----------------------+---------------+-----------+-------------+];
        [|       |                       |Ост. на конец  |Причисл. %%|Ост. на конец|];
        [| № п/п |  Номер лицевого счета |отч. периода   |     /     |отч. периода |];
        [|       |                       |без причисл. %%|Начисл. %% |с причисл. %%|];
        [+-------+-----------------------+---------------+-----------+-------------+];
      end;
    else
      if (CrossRateEURO > 0.0)
        [+-------+-----------------------+---------------+-----------+-------------+-------------+];
        [|       |                       |Ост. на конец  |           |Ост. на конец|Ост. на конец|];
        [| № п/п |  Номер лицевого счета |отч. периода   |Причисл. %%|отч. периода |отч. периода |];
        [|       |                       |без причисл. %%|           |с причисл. %%|в EUR        |];
        [+-------+-----------------------+---------------+-----------+-------------+-------------+];
      else
        [+-------+-----------------------+---------------+-----------+-------------+];
        [|       |                       |Ост. на конец  |           |Ост. на конец|];
        [| № п/п |  Номер лицевого счета |отч. периода   |Причисл. %%|отч. периода |];
        [|       |                       |без причисл. %%|           |с причисл. %%|];
        [+-------+-----------------------+---------------+-----------+-------------+];
      end;
    end;
  else
    if ((sb_dtyp.Kind == SA_CHILD1) OR (sb_dtyp.Kind == SA_CHILD2))
      if (CrossRateEURO > 0.0)
        [+-------+--------------+---------------+--------------------+-------------+-------------+];
        [|       |  Количество  |Ост. на конец  |   Причислено %%    |Ост. на конец|Ост. на конец|];
        [| № стр.|лицевых счетов|отч. периода   |         /          |отч. периода |отч. периода |];
        [|       |  на странице |без причисл. %%|   Начислено %%     |с причисл. %%|в EUR        |];
        [+-------+--------------+---------------+--------------------+-------------+-------------+];
      else
        [+-------+--------------+---------------+--------------------+-------------+];
        [|       |  Количество  |Ост. на конец  |   Причислено %%    |Ост. на конец|];
        [| № стр.|лицевых счетов|отч. периода   |         /          |отч. периода |];
        [|       |  на странице |без причисл. %%|   Начислено %%     |с причисл. %%|];
        [+-------+--------------+---------------+--------------------+-------------+];
      end;
    else
      if (CrossRateEURO > 0.0)
        [+-------+-----------------------+---------------+-----------+-------------+-------------+];
        [|       |      Количество       |Ост. на конец  |           |Ост. на конец|Ост. на конец|];
        [| № стр.|    лицевых счетов     |отч. периода   |Причисл. %%|отч. периода |отч. периода |];
        [|       |      на странице      |без причисл. %%|           |с причисл. %%|в EUR        |];
        [+-------+-----------------------+---------------+-----------+-------------+-------------+];
      else
        [+-------+-----------------------+---------------+-----------+-------------+];
        [|       |      Количество       |Ост. на конец  |           |Ост. на конец|];
        [| № стр.|    лицевых счетов     |отч. периода   |Причисл. %%|отч. периода |];
        [|       |      на странице      |без причисл. %%|           |с причисл. %%|];
        [+-------+-----------------------+---------------+-----------+-------------+];
      end;
    end;
  end;

  CurLine = CurLine + 5;

end;


/**************************************************************************\
|                             Окончание отчета                             |
\**************************************************************************/
macro OutBottom

  [ ];
  [  Заведующий филиалом (оперчасти) _____________________________ ];

  CurLine = CurLine + 2;

end;
                                 

/**************************************************************************\
|                        Заголовок по виду вклада                          |
\**************************************************************************/
macro OutHeadTA

  var NameType = "";

  if (sb_dtyp.Kind != SA_STATIC)
    if (StrLen(CurBalAcc) > 0)
      NameType = sb_dtyp.Name + " (" + CurBalAcc + ")";
    else
      NameType = sb_dtyp.Name;
    end;
  else
    if (StrLen(CurBalAcc) > 0)
      NameType = sb_dtyp_s.Name + " (" +  sb_dtyp.Name + ") (" + CurBalAcc + ")";
    else
      NameType = sb_dtyp_s.Name + " (" +  sb_dtyp.Name + ")";
      NameType = sb_dtyp.Name;
    end;
  end;

  CurPage = CurPage + 1;

  if (NOT PrintSummary)
    if (NOT BANumbDisplayed)
      [ Б А Л А Н С О В Ы Й   С Ч Е Т   №   # ](CurBalAcc);
      [ ];
      CurLine = CurLine + 2;
      BANumbDisplayed = TRUE;
    end;
  end;
  [ #                                                                Стр. ####]
  (NameType,CurPage);
  CurLine = CurLine + 1;

  TANameDisplayed = TRUE;

  OutSubHead();

end;


/**************************************************************************\
|                               Строка отчета                              |
\**************************************************************************/
macro OutLine (Citizenship)

  var RecFound = FALSE;
  var Rest     = $0L;
  var RestE    = $0L;
  var InSum    = $0L;
  var OutSum   = $0L;
  var PcRest   = $0L;
  var TmpStr   = "";
  var stat;
  var RealDateEndPeriod = Date(0,0,0);

  KeyNum(sbdepdoc,2);
  sbdepdoc.Referenc         = depositr.Referenc;
  sbdepdoc.DepDate_Document = RepDate;        
  stat = GetEQ(sbdepdoc);
  while (stat)
    if ((sbdepdoc.Referenc         == depositr.Referenc) AND
        (sbdepdoc.DepDate_Document == RepDate          )    )
      if (sbdepdoc.Mode != 0)
        RealDateEndPeriod = sbdepdoc.Date_Document;
        stat = FALSE;
      else
        stat = Next(sbdepdoc);
      end;
    else
      stat = FALSE;
    end;
  end;

  if (RealDateEndPeriod != Date(0,0,0))
    KeyNum(sbdepdoc,6);
    sbdepdoc.Referenc      = depositr.Referenc;
    sbdepdoc.Date_Document = RealDateEndPeriod;
    sbdepdoc.NumDayDoc     = 0;
    stat = GetGE(sbdepdoc);
    while (stat)
      if ((sbdepdoc.Referenc      == depositr.Referenc) AND
          (sbdepdoc.Date_Document == RealDateEndPeriod)    )
        if ((IsServDoc(sbdepdoc)         ) AND 
            (sbdepdoc.FlagStorn == ""    ) AND 
            (sbdepdoc.Mode      != 0     ) AND 
           ((sbdepdoc.InSum     != 0 ) OR
            (sbdepdoc.OutSum    != 0 )   )   )
          RecFound = TRUE;
          Rest   = sbdepdoc.Rest;
          InSum  = InSum  + sbdepdoc.InSum;
          OutSum = OutSum + sbdepdoc.OutSum;
        end;
        stat = Next(sbdepdoc);
      else
        stat = FALSE;
      end;
    end;
  end;

  if (DisplayAll OR (MonthRepDate == 12) OR RecFound)
    ;
  else
    return;
  end;

  /* Документ на завершение периода не нашли, ищем любой другой документ */
  if (RecFound == FALSE)
    KeyNum(sbdepdoc,6);
    sbdepdoc.Referenc      = depositr.Referenc;
    sbdepdoc.Date_Document = RepDate + 1;
    sbdepdoc.NumDayDoc     = 0;
    stat = GetLE(sbdepdoc);
    while (stat)
      if (sbdepdoc.Referenc == depositr.Referenc)
        if ((IsServDoc(sbdepdoc)     ) AND 
            (sbdepdoc.FlagStorn == "")    )
          Rest = sbdepdoc.Rest;
          RecFound = TRUE;
          stat = FALSE;
        else
          stat = Prev(sbdepdoc);
        end;
      else
        stat = FALSE;
      end;
    end;
  end;

  /* Для целевого на детей определяем начисленный процент вместо причисленного */
  if (RecFound)
    if (((sb_dtyp.Kind == SA_CHILD1) OR (sb_dtyp.Kind == SA_CHILD2)) AND (depositr.UseAlternate == 0))
      if (Проценты_Налог_К_Оплате_Учет_Конв(depositr.Referenc,0,RepDate,PcRest) != 0)
        PcRest = $0L;
        [Ошибка в функции "Проценты_Налог_К_Оплате_Учет_Конв"];
      end;
    end;
  end;

  if (RecFound)
    if (NOT CurrSubHeadDisplayed)
      OutHead();
      CurrSubHeadDisplayed = TRUE;
    end;
    if (NOT TANameDisplayed)
      OutHeadTA();
    end;
    if (sb_dtyp.Kind == SA_STATIC)
      if (NOT BranchDisplayed)
        [ Учреждение #](ddep.rec.Name);
        CurLine = CurLine + 1;
        BranchDisplayed = TRUE;
      end;
    end;

    if (CrossRateEURO > 0.0)
      RestE = MoneyL(Rest / CrossRateEURO);
    end;

    if ((sb_dtyp.Kind == SA_CHILD1) OR (sb_dtyp.Kind == SA_CHILD2))
      if ((InSum - OutSum == 0) AND (PcRest != 0))
        if (CrossRateEURO > 0.0)
          [ ####### ####################### ############### 0/######### ############# #############]
          (N,Acc_Form(depositr.Account),(Rest - InSum + OutSum):0:2:z,PcRest:0:2:z,Rest:0:2:z,RestE:0:2:z);
        else
          [ ####### ####################### ############### 0/######### #############]
          (N,Acc_Form(depositr.Account),(Rest - InSum + OutSum):0:2:z,PcRest:0:2:z,Rest:0:2:z);
        end;
      elif ((InSum - OutSum != 0) AND (PcRest == 0))
        if (CrossRateEURO > 0.0)
          [ ####### ####################### ############### #########/0 ############# #############]
          (N,Acc_Form(depositr.Account),(Rest - InSum + OutSum):0:2:z,(InSum - OutSum):0:2:z,Rest:0:2:z,RestE:0:2:z);
        else
          [ ####### ####################### ############### #########/0 #############]
          (N,Acc_Form(depositr.Account),(Rest - InSum + OutSum):0:2:z,(InSum - OutSum):0:2:z,Rest:0:2:z);
        end;
      else
        TmpStr = String((InSum - OutSum):0:2:z) + "/" + String(PcRest:0:2:z);
        if (CrossRateEURO > 0.0)
          [ ####### ####################### ############### ########### ############# #############]
          (N,Acc_Form(depositr.Account),(Rest - InSum + OutSum):0:2:z,TmpStr,Rest:0:2:z,RestE:0:2:z);
        else
          [ ####### ####################### ############### ########### #############]
          (N,Acc_Form(depositr.Account),(Rest - InSum + OutSum):0:2:z,TmpStr,Rest:0:2:z);
        end;
      end;
    elif ((sb_dtyp.Kind == SA_STATIC) AND (sb_dtyp_s.CalcPcOnEoy))
      if (CrossRateEURO > 0.0)
        [ ####### ####################### ############### ########### ############# #############]
        (N,Acc_Form(depositr.Account),(Rest - InSum + OutSum):0:2:z,(InSum - OutSum):0:2:z,Rest:0:2:z,RestE:0:2:z);
      else
        [ ####### ####################### ############### ########### #############]
        (N,Acc_Form(depositr.Account),(Rest - InSum + OutSum):0:2:z,(InSum - OutSum):0:2:z,Rest:0:2:z);
      end;
    elif ((sb_dtyp.CalcPcOnEoy) OR (InSum - OutSum != 0))
      if (CrossRateEURO > 0.0)
        [ ####### ####################### ############### ########### ############# #############]
        (N,Acc_Form(depositr.Account),(Rest - InSum + OutSum):0:2:z,(InSum - OutSum):0:2:z,Rest:0:2:z,RestE:0:2:z);
      else
        [ ####### ####################### ############### ########### #############]
        (N,Acc_Form(depositr.Account),(Rest - InSum + OutSum):0:2:z,(InSum - OutSum):0:2:z,Rest:0:2:z);
      end;
    else
      if (CrossRateEURO > 0.0)
        [ ####### ####################### ###############                           #############]
        (N,Acc_Form(depositr.Account),(Rest - InSum + OutSum):0:2:z,RestE:0:2:z);
      else
        [ ####### ####################### ###############]
        (N,Acc_Form(depositr.Account),(Rest - InSum + OutSum):0:2:z);
      end;
    end;

    AtLeastOneAccountFound = TRUE;
    CurLine = CurLine + 1;
    N = N + 1;
    NPage = NPage + 1;

    BeforeP   = BeforeP   + (Rest - InSum + OutSum);
    PercP     = PercP     + InSum - OutSum;
    CalcP     = CalcP     + PcRest;
    BehindP   = BehindP   + Rest; 
    BehindPE  = BehindPE  + RestE; 

    CountTA   = CountTA   + 1;
    BeforeTA  = BeforeTA  + (Rest - InSum + OutSum);
    PercTA    = PercTA    + InSum - OutSum;
    CalcTA    = CalcTA    + PcRest;
    BehindTA  = BehindTA  + Rest; 
    BehindTAE = BehindTAE + RestE; 

    CountBA   = CountBA   + 1;
    BeforeBA  = BeforeBA  + (Rest - InSum + OutSum);
    PercBA    = PercBA    + InSum - OutSum;
    CalcBA    = CalcBA    + PcRest;
    BehindBA  = BehindBA  + Rest;
    BehindBAE = BehindBAE + RestE;

    CountCr   = CountCr   + 1;
    BeforeCr  = BeforeCr  + (Rest - InSum + OutSum);
    PercCr    = PercCr    + InSum - OutSum;
    CalcCr    = CalcCr    + PcRest;
    BehindCr  = BehindCr  + Rest;
    BehindCrE = BehindCrE + RestE;
  
  end;

end;


/**************************************************************************\
|                         Печать итогов по вкладу                          |
\**************************************************************************/
macro OutTotalsTA (Citizenship)

  if (CrossRateEURO > 0.0)
    [-----------------------------------------------------------------------------------------];
  else
    [---------------------------------------------------------------------------];
  end;

  if ((sb_dtyp.Kind == SA_CHILD1) OR (sb_dtyp.Kind == SA_CHILD2))
    if (CrossRateEURO > 0.0)
      [ Итого по вкладу ###### ############### #########/########## ############# #############]
      (CountTA:6,BeforeTA:0:2:z,PercTA:0:2:z,CalcTA:0:2:z,BehindTA:0:2:z,BehindTAE:0:2:z);
    else
      [ Итого по вкладу ###### ############### #########/########## #############]
      (CountTA:6,BeforeTA:0:2:z,PercTA:0:2:z,CalcTA:0:2:z,BehindTA:0:2:z);
    end;
  elif ((sb_dtyp.Kind == SA_STATIC) AND (sb_dtyp_s.CalcPcOnEoy))
    if (CrossRateEURO > 0.0)
      [ Итого по вкладу ######          ############### ########### ############# #############]
      (CountTA:6,BeforeTA:0:2:z,PercTA:0:2:z,BehindTA:0:2:z,BehindTAE:0:2:z);
    else
      [ Итого по вкладу ######          ############### ########### #############]
      (CountTA:6,BeforeTA:0:2:z,PercTA:0:2:z,BehindTA:0:2:z);
    end;
  elif ((sb_dtyp.CalcPcOnEoy) OR (PercTA != 0))
    if (CrossRateEURO > 0.0)
      [ Итого по вкладу ######          ############### ########### ############# #############]
      (CountTA:6,BeforeTA:0:2:z,PercTA:0:2:z,BehindTA:0:2:z,BehindTAE:0:2:z);
    else
      [ Итого по вкладу ######          ############### ########### #############]
      (CountTA:6,BeforeTA:0:2:z,PercTA:0:2:z,BehindTA:0:2:z);
    end;
  else
    if (CrossRateEURO > 0.0)
      [ Итого по вкладу ######          ###############                           #############]
      (CountTA:6,BeforeTA:0:2:z,BehindTAE:0:2:z);
    else
      [ Итого по вкладу ######          ###############]
      (CountTA:6,BeforeTA:0:2:z);
    end;
  end;

  CurLine = CurLine + 2;

  BeforeP   = $0L;
  PercP     = $0L;
  CalcP     = $0L;
  BehindP   = $0L; 
  BehindPE  = $0L; 

  CountTA   = 0;
  BeforeTA  = $0L;
  PercTA    = $0L;
  CalcTA    = $0L;
  BehindTA  = $0L; 
  BehindTAE = $0L; 

  ASize(APage,   0);
  ASize(ACount,  0);
  ASize(ABefore, 0);
  ASize(APerc,   0);
  ASize(ACalc,   0);
  ASize(ABehind, 0);
  ASize(ABehindE,0);

end;


/**************************************************************************\
|                         Печать итогов по вкладу                          |
\**************************************************************************/
macro OutTotalsBA

  var Head1 = "";
  var Head2 = "";

  CurPage = CurPage + 1;

  Head1 = "Итоговая выписка по БАЛАНСОВОМУ СЧЕТУ № " + CurBalAcc;
  Head2 = "по состоянию на " + String(RepDate:m);
  [                                                               Приложение 1];
  [---------------------------------------------------------------------------];
  [                                                                 ##########]
  (RepDate);
  [ ОСБ № ______ Филиал № #                                         Форма № 19]
  (BranchName);
  [ ];
  [###########################################################################]
  (Head1:c);
  [###########################################################################]
  (Head2:c);
  [ ];
  [                                                                  Стр. ####]
  (CurPage);
  if (WasChild)
    if (CrossRateEURO > 0.0)
      [+-------+-----------------------+---------------+-----------+-------------+-------------+];
      [|       |      Количество       |Ост. на конец  |Причисл. %%|Ост. на конец|Ост. на конец|];
      [|       |    лицевых счетов     |отч. периода   |     /     |отч. периода |отч. периода |];
      [|       |                       |без причисл. %%|Начисл. %% |с причисл. %%|в EUR        |];
      [+-------+-----------------------+---------------+-----------+-------------+-------------+];
      [                #######          ############### ########### ############# #############]
      (CountBA:7,BeforeBA:0:2:z,PercBA:0:2,BehindBA:0:2:z,BehindBAE:0:2:z);
    else
      [+-------+-----------------------+---------------+-----------+-------------+];
      [|       |      Количество       |Ост. на конец  |Причисл. %%|Ост. на конец|];
      [|       |    лицевых счетов     |отч. периода   |     /     |отч. периода |];
      [|       |                       |без причисл. %%|Начисл. %% |с причисл. %%|];
      [+-------+-----------------------+---------------+-----------+-------------+];
      [                #######          ############### ########### #############]
      (CountBA:7,BeforeBA:0:2:z,PercBA:0:2,BehindBA:0:2:z);
    end;
    [                                                 ###########]
    (CalcBA:0:2);
  else
    if (CrossRateEURO > 0.0)
      [+-------+-----------------------+---------------+-----------+-------------+-------------+];
      [|       |      Количество       |Ост. на конец  |           |Ост. на конец|Ост. на конец|];
      [|       |    лицевых счетов     |отч. периода   |Причисл. %%|отч. периода |отч. периода |];
      [|       |                       |без причисл. %%|           |с причисл. %%|в EUR        |];
      [+-------+-----------------------+---------------+-----------+-------------+-------------+];
      [                #######          ############### ########### ############# #############]
      (CountBA:7,BeforeBA:0:2:z,PercBA:0:2:z,BehindBA:0:2:z,BehindBAE:0:2:z);
    else
      [+-------+-----------------------+---------------+-----------+-------------+];
      [|       |      Количество       |Ост. на конец  |           |Ост. на конец|];
      [|       |    лицевых счетов     |отч. периода   |Причисл. %%|отч. периода |];
      [|       |                       |без причисл. %%|           |с причисл. %%|];
      [+-------+-----------------------+---------------+-----------+-------------+];
      [                #######          ############### ########### #############]
      (CountBA:7,BeforeBA:0:2:z,PercBA:0:2:z,BehindBA:0:2:z);
    end;
  end;

  if (CrossRateEURO > 0.0)
    [-----------------------------------------------------------------------------------------];
  else
    [---------------------------------------------------------------------------];
  end;

  OutBottom();
  PrintLn(StrFor(12));

  CountBA   = 0;
  BeforeBA  = $0L;
  PercBA    = $0L;
  CalcBA    = $0L;
  BehindBA  = $0L;
  BehindBAE = $0L;
  
end;


/**************************************************************************\
|                         Печать итогов по вкладу                          |
\**************************************************************************/
macro OutTotalsCr

  var Head1 = "";
  var Head2 = "";

  CurPage = CurPage + 1;

  Head1 = "Итоговая выписка по ВАЛЮТЕ " + currency.Name_Currency + " (" + currency.ExternalCode + ")";
  Head2 = "по состоянию на " + String(RepDate:m);
  [                                                               Приложение 1];
  [---------------------------------------------------------------------------];
  [                                                                 ##########]
  (RepDate);
  [ ОСБ № ______ Филиал № #                                         Форма № 19]
  (BranchName);
  [ ];
  [###########################################################################]
  (Head1:c);
  [###########################################################################]
  (Head2:c);
  [ ];
  [                                                                  Стр. ####]
  (CurPage);
  if (CrossRateEURO > 0.0)
    [+-------+-----------------------+---------------+-----------+-------------+-------------+];
    [|       |      Количество       |Ост. на конец  |           |Ост. на конец|Ост. на конец|];
    [|       |    лицевых счетов     |отч. периода   |Причисл. %%|отч. периода |отч. периода |];
    [|       |                       |без причисл. %%|           |с причисл. %%|в EUR        |];
    [+-------+-----------------------+---------------+-----------+-------------+-------------+];
    [                #######          ############### ########### ############# #############]
    (CountCr:7,BeforeCr:0:2:z,PercCr:0:2:z,BehindCr:0:2:z,BehindCrE:0:2:z);
    [-----------------------------------------------------------------------------------------];
  else
    [+-------+-----------------------+---------------+-----------+-------------+];
    [|       |      Количество       |Ост. на конец  |           |Ост. на конец|];
    [|       |    лицевых счетов     |отч. периода   |Причисл. %%|отч. периода |];
    [|       |                       |без причисл. %%|           |с причисл. %%|];
    [+-------+-----------------------+---------------+-----------+-------------+];
    [                #######          ############### ########### #############]
    (CountCr:7,BeforeCr:0:2:z,PercCr:0:2:z,BehindCr:0:2:z);
    [---------------------------------------------------------------------------];
  end;

  OutBottom();
  PrintLn(StrFor(12));
  
end;


/**************************************************************************\
|                        Печать итогов по странице                         |
\**************************************************************************/
macro OutTotalsP

  var i;

  if (NPage != 0)
    if (CrossRateEURO > 0.0)
      [-----------------------------------------------------------------------------------------];
    else
      [---------------------------------------------------------------------------];
    end;

    if ((sb_dtyp.Kind == SA_CHILD1) OR (sb_dtyp.Kind == SA_CHILD2))
      if (CrossRateEURO > 0.0)
        [ Итого по странице ##### ############### #########/######### ############# #############]
        (CurPage:5,BeforeP:0:2:z,PercP:0:2:z,CalcP:0:2:z,BehindP:0:2:z,BehindPE:0:2:z);
      else
        [ Итого по странице ##### ############### #########/######### #############]
        (CurPage:5,BeforeP:0:2:z,PercP:0:2:z,CalcP:0:2:z,BehindP:0:2:z);
      end;
    elif ((sb_dtyp.Kind == SA_STATIC) AND (sb_dtyp_s.CalcPcOnEoy))
      if (CrossRateEURO > 0.0)
        [ Итого по странице #####         ############### ########### ############# #############]
        (CurPage:5,BeforeP:0:2:z,PercP:0:2:z,BehindP:0:2:z,BehindPE:0:2:z);
      else
        [ Итого по странице #####         ############### ########### #############]
        (CurPage:5,BeforeP:0:2:z,PercP:0:2:z,BehindP:0:2:z);
      end;
    elif ((sb_dtyp.CalcPcOnEoy) OR (PercP != 0))
      if (CrossRateEURO > 0.0)
        [ Итого по странице #####         ############### ########### ############# #############]
        (CurPage:5,BeforeP:0:2:z,PercP:0:2:z,BehindP:0:2:z,BehindPE:0:2:z);
      else
        [ Итого по странице #####         ############### ########### #############]
        (CurPage:5,BeforeP:0:2:z,PercP:0:2:z,BehindP:0:2:z);
      end;
    else
      if (CrossRateEURO > 0.0)
        [ Итого по странице #####         ###############                           #############]
        (CurPage:5,BeforeP:0:2:z,BehindPE:0:2:z);
      else
        [ Итого по странице #####         ###############]
        (CurPage:5,BeforeP:0:2:z);
      end;
    end;

    CurLine = CurLine + 2;

    i = ASize(ABefore);
    APage   (i) = CurPage;
    ACount  (i) = NPage;
    ABefore (i) = BeforeP;
    APerc   (i) = PercP;
    ACalc   (i) = CalcP;
    ABehind (i) = BehindP;
    ABehindE(i) = BehindPE;

    NPage    = 0;
    BeforeP  = $0L;
    PercP    = $0L;
    CalcP    = $0L;
    BehindP  = $0L; 
    BehindPE = $0L; 
  end;

end;


/**************************************************************************\
|                        Переход на новую страницу                         |
\**************************************************************************/
macro CheckFormFeed (EOTA, Citizenship)

  var Delta = 4;

  if (PrintSummary)
    Delta = 2;
  elif (sb_dtyp.Kind == SA_STATIC)
    if (NOT BranchDisplayed)
      Delta = 5;
    end;
  end;
            
  if ((CurrSubHeadDisplayed) AND (CurLine >= LinesPerPage - Delta))
    CurLine = 0;
    if (TANameDisplayed)
      OutTotalsP();
    end;
    if (AtLeastOneAccountFound)
      if (NOT TANameDisplayed)
        OutHeadTA();
        OutTotalsP();
      end;
      if (ValType(EOTA) == V_BOOL)
        if (EOTA)
          OutTotalsTA(Citizenship);
        end;
      end;                       
    end;
    OutBottom();
    CurrSubHeadDisplayed = FALSE;
    TANameDisplayed      = FALSE;
    PrintLn(StrFor(12));
  end;

end;


/**************************************************************************\
|                           Обработка одного счета                         |
\**************************************************************************/
macro TreatAccount (Citizenship)

  if (depositr.Open_Date < RepDate)

    if (NOT FindDepClient(depositr.CodClient))
      ClientList.CurRec.Clear;
    end;

    if (Citizenship == ClientList.CurRec.Rec.NotResident)
      RecCount = RecCount + 1;
      BrdCount = BrdCount + 1;
      if (BrdCount >= 50)
        BrdCount = 0;
        Message(" Обрабатывается ",RecCount:6," счет ...");
      end;
      CheckFormFeed(FALSE,Citizenship);
      OutLine(Citizenship);
      CheckFormFeed(FALSE,Citizenship);
    end;

  end;

end;


/**************************************************************************\
|                   Строка отчета для итого по страницам                   |
\**************************************************************************/
macro OutLinesForPagesTA (Citizenship)

  var i = 0;

  CurLine = LinesPerPage + 1;
  CheckFormFeed(FALSE,Citizenship);

  PrintSummary = TRUE;

  while (i < ASize(ABefore))
    if (NOT CurrSubHeadDisplayed)
      OutHead();
      CurrSubHeadDisplayed = TRUE;
    end;
    if (NOT TANameDisplayed)
      OutHeadTA();
    end;
    if ((sb_dtyp.Kind == SA_CHILD1) OR (sb_dtyp.Kind == SA_CHILD2))
      if (CrossRateEURO > 0.0)
        [ #######       ###      ############### #########/########## ############# #############]
        (APage(i):7,ACount(i):3,ABefore(i):0:2:z,APerc(i):0:2:z,ACalc(i):0:2:z,ABehind(i):0:2:z,ABehindE(i):0:2:z);
      else
        [ #######       ###      ############### #########/########## #############]
        (APage(i):7,ACount(i):3,ABefore(i):0:2:z,APerc(i):0:2:z,ACalc(i):0:2:z,ABehind(i):0:2:z);
      end;
    elif ((sb_dtyp.Kind == SA_STATIC) AND (sb_dtyp_s.CalcPcOnEoy))
      if (CrossRateEURO > 0.0)
        [ #######            ###          ############### ########### ############# #############]
        (APage(i):7,ACount(i):3,ABefore(i):0:2:z,APerc(i):0:2:z,ABehind(i):0:2:z,ABehindE(i):0:2:z);
      else
        [ #######            ###          ############### ########### #############]
        (APage(i):7,ACount(i):3,ABefore(i):0:2:z,APerc(i):0:2:z,ABehind(i):0:2:z);
      end;
    elif ((sb_dtyp.CalcPcOnEoy) OR (APerc(i) != 0))
      if (CrossRateEURO > 0.0)
        [ #######            ###          ############### ########### ############# #############]
        (APage(i):7,ACount(i):3,ABefore(i):0:2:z,APerc(i):0:2:z,ABehind(i):0:2:z,ABehindE(i):0:2:z);
      else
        [ #######            ###          ############### ########### #############]
        (APage(i):7,ACount(i):3,ABefore(i):0:2:z,APerc(i):0:2:z,ABehind(i):0:2:z);
      end;
    else
      if (CrossRateEURO > 0.0)
        [ #######            ###          ###############                           #############]
        (APage(i):7,ACount(i):3,ABefore(i):0:2:z,ABehindE(i):0:2:z);
      else
        [ #######            ###          ###############]
        (APage(i):7,ACount(i):3,ABefore(i):0:2:z);
      end;
    end;
    CurLine = CurLine + 1;
    CheckFormFeed(FALSE,Citizenship);
    i = i + 1;
  end;

  PrintSummary = FALSE;

end;


/**************************************************************************\
|          Обработка неподвижных счетов с заданной резидентностью          |
\**************************************************************************/
macro SA_TreatSvodAccount (SvodAccount, Citizenship)

  var stat;
  var pos     = GetPos(depositr); 
  var PrevKey = KeyNum(depositr,5);

  ClearRecord(depositr);
  depositr.FNCash      = ddep.rec.Code;
  depositr.SvodAccount = SvodAccount;
  stat = GetGE(depositr);
  while ( stat                                     AND 
         (depositr.FNCash      == ddep.rec.Code) AND 
         (depositr.SvodAccount == SvodAccount    )    )
    if (depositr.Open_Close == "")
      TreatAccount(Citizenship);
    end;
    stat = Next(depositr);
  end;                   

  KeyNum(depositr,PrevKey);

  GetDirect(depositr,pos); 

end;


/**************************************************************************\
|          Обработка одного вида вклада с заданной резидентностью          |
\**************************************************************************/
macro TreatDepType (Citizenship)

  var stat;
  var stat_s;
  var stat_d;
  var dpDepList = TdpDepList;
                            
  AtLeastOneAccountFound = FALSE;

  if (sb_dtyp.Kind != SA_STATIC)
    if ((sb_dtyp.Kind == SA_CHILD1) OR (sb_dtyp.Kind == SA_CHILD2))
      WasChild = TRUE;
    end;
    ClearRecord(depositr);
    depositr.IsCur         = IsCur;
    depositr.FNCash        = Branch;
    depositr.Type_Account  = sb_dtyp.Kind;
    depositr.Code_Currency = currency.Code_Currency;
    stat = GetGE(depositr);
    while ((stat                                            ) AND 
           (depositr.IsCur         == IsCur                 ) AND 
           (depositr.FNCash        == Branch                ) AND 
           (depositr.Open_Close    == ""                    ) AND 
           (depositr.Type_Account  == sb_dtyp.Kind          ) AND 
           (depositr.Code_Currency == currency.Code_Currency)    )
      if ( (Index(depositr.UserTypeAccount,"Н") == 0  ) )   /* Пропустим неподвижные */
/*       AND (depositr.SvodAccount                == "" )    )*/ 
        TreatAccount(Citizenship);
      end;
      stat = Next(depositr);
    end;
    if (AtLeastOneAccountFound)
      OutLinesForPagesTA(Citizenship);
      CurLine = LinesPerPage + 1;
      CheckFormFeed(TRUE,Citizenship);
    end;
  else
    /* Обработка неподвижных */
    if (BranchStatus == BS_OPERDEP)
      sb_dtyp_s.FlagCur = IsCur;
      sb_dtyp_s.Priorit = 0;
      dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
      ddep = dpDepList.RecHandler;
      stat_s = GetGE(sb_dtyp_s);
      while (stat_s)
        if (sb_dtyp_s.FlagCur == IsCur)
          AtLeastOneAccountFound = FALSE;
          dpDepList.rewind();
          while (dpDepList.next())
/*            if ((listfdep.StatusBranch == BS_BRANCH ) OR
                (listfdep.StatusBranch == BS_OPERDEP)   )*/
              BranchDisplayed = FALSE;
              ClearRecord(depositr);
              depositr.IsCur         = IsCur;
              depositr.FNCash        = ddep.rec.Code;
              depositr.Type_Account  = sb_dtyp.Kind;
              depositr.Code_Currency = currency.Code_Currency;
              stat = GetGE(depositr);
              while ((stat                                            ) AND 
                     (depositr.IsCur         == IsCur                 ) AND 
                     (depositr.FNCash        == ddep.rec.Code       ) AND 
                     (depositr.Open_Close    == ""                    ) AND 
                     (depositr.Type_Account  == sb_dtyp.Kind          ) AND 
                     (depositr.Code_Currency == currency.Code_Currency)    )
                if (depositr.PrevAccount == sb_dtyp_s.Kind)
                  SA_TreatSvodAccount(depositr.Account,Citizenship);
                end;
                stat = Next(depositr);
              end;
/*            end;*/
          end;
          if (AtLeastOneAccountFound)
            OutLinesForPagesTA(Citizenship);
            CurLine = LinesPerPage + 1;
            CheckFormFeed(TRUE,Citizenship);
          end;
          stat_s = Next(sb_dtyp_s);
        else
          stat_s = FALSE;
        end;
      end;
    end;
  end;

end;


/**************************************************************************\
|                   Обработка видов вкладов одной валюты                   |
\**************************************************************************/
macro TreatAllDepTypes

  var stat;
  var i = 0;

  CurrSubHeadDisplayed = FALSE;
  TANameDisplayed      = FALSE;

  CountCr  = 0;
  BeforeCr = $0L;
  PercCr   = $0L;
  CalcCr   = $0L;
  BehindCr = $0L;
  
  while (i < ASize(ABalAccounts))
    WasChild = FALSE;
    BANumbDisplayed = FALSE;
    CurBalAcc = Trim(ABalAccounts(i));
    KeyNum(sb_dtyp,1);
    sb_dtyp.FlagCur = IsCur;
    sb_dtyp.Priorit = 0;
    stat = GetGE(sb_dtyp);
    while (stat)
      if (sb_dtyp.FlagCur == IsCur)
        if ((sb_dtyp.Kind == SA_STATIC  ) OR
            (sb_dtyp.Kind == SA_UNITED  ) OR
            (sb_dtyp.Kind == "Шаблонный") OR
            (sb_dtyp.Kind == "Служебный")   )
          ;
        else
          if (Trim(sb_dtyp.BalAcc) == CurBalAcc)
            TreatDepType(StrFor(0));
          end;
          if (Trim(sb_dtyp.BalAccNotRez) == CurBalAcc)
            TreatDepType(StrFor(1));
          end;
        end;
        stat = Next(sb_dtyp);
      else
        stat = FALSE;
      end;
    end;
    /* Объединенный и неподвижный обрабатываем отдельно */
    if (BranchStatus == BS_OPERDEP)
      KeyNum(sb_dtyp,2);
      sb_dtyp.FlagCur = IsCur;
      sb_dtyp.Kind    = SA_UNITED;
      if (GetEQ(sb_dtyp) AND (Trim(sb_dtyp.BalAcc) == CurBalAcc))
        TreatDepType(StrFor(0));
      end;
      KeyNum(sb_dtyp,2);
      sb_dtyp.FlagCur = IsCur;
      sb_dtyp.Kind    = SA_STATIC;
      if (GetEQ(sb_dtyp) AND (Trim(sb_dtyp.BalAcc) == CurBalAcc))
        TreatDepType(StrFor(0));
      end;
    end;
    if (BANumbDisplayed)
      OutTotalsBA();
    end;
    i = i + 1;
  end;

  if (CountCr > 0)
    OutTotalsCr();
  end;

end;


/**************************************************************************\
|               Определение всех возможных балансовых счетов               |
\**************************************************************************/
macro DefBalAccounts

  var stat;
  var FindBA;
  var FindBA_NotRez;
  var TmpVal;
  var i = 0;
  var j = 0;

  Message(" Определение балансовых счетов ...");

  /* Получение массива всех возможных балансовых счетов */
  KeyNum(sb_dtyp,2);
  sb_dtyp.FlagCur = IsCur;
  sb_dtyp.Kind    = "";
  stat = GetGE(sb_dtyp);
  while (stat)
    if (sb_dtyp.FlagCur == IsCur)
      if (ASize(ABalAccounts) == 0)
        ABalAccounts(0) = Trim(sb_dtyp.BalAcc);
      end;
      FindBA        = FALSE;
      FindBA_NotRez = FALSE;
      i = 0;
      while (i < ASize(ABalAccounts))
        if (ABalAccounts(i) == Trim(sb_dtyp.BalAcc))
          FindBA = TRUE;
        end;
        if (ABalAccounts(i) == Trim(sb_dtyp.BalAccNotRez))
          FindBA_NotRez = TRUE;
        end;
        i = i + 1;
      end;
      if (FindBA == FALSE)
        ABalAccounts(ASize(ABalAccounts)) = Trim(sb_dtyp.BalAcc);
      end;
      if (FindBA_NotRez == FALSE)
        if (Trim(sb_dtyp.BalAcc) != Trim(sb_dtyp.BalAccNotRez))
          ABalAccounts(ASize(ABalAccounts)) = Trim(sb_dtyp.BalAccNotRez);
        end;
      end;
      stat = Next(sb_dtyp);
    else
      stat = FALSE;
    end;
  end;

  /* Отсортируем полученный массив балансовых счетов в порядке возрастания */
  if (ASize(ABalAccounts) > 0)
    i = 0;
    while (i < ASize(ABalAccounts))
      j = i;
      while (j < ASize(ABalAccounts))
        if (ABalAccounts(j) < ABalAccounts(i))
          TmpVal = ABalAccounts(i);
          ABalAccounts(i) = ABalAccounts(j);
          ABalAccounts(j) = TmpVal;
        end;
        j = j + 1;
      end;
      i = i + 1;
    end;
  end;

end;


/**************************************************************************\
|                     Г Л А В Н А Я   П Р О Г Р А М М А                    |
\**************************************************************************/

  var stat;

  DefBranchName();
  SetDates();
  DefBalAccounts();

  OpenDepFiles();

  if (IsCur == 0)
    currency.Code_Currency = 0;
    stat = GetEQ(currency);
  else
    currency.Code_Currency = 1;
    stat = GetGE(currency);
  end;

  while (stat)
    DefCrossRateEURO();
    TreatAllDepTypes();
    if (IsCur == 0)
      stat = FALSE;
    else
      stat = Next(currency);
    end;
  end;

  CloseDepFiles();

end;
