/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*                                                                          *
*  Класс, предназанченный для сохранения состояния файла                   *
* 									   *
*                                                                          *
*  Крестьянинов Е.А.                                                       *
*  20.07.2005                                                              *
\**************************************************************************/

// Чего сохранять
const SAVE_KEYNUM = 0;  // Сохранить номер ключа
const SAVE_POS = 1;     // Сохранить позицию + номер ключа


class TBFileSaver( fileSave, mode )

  private var
    _fileSave = fileSave,
    _mode = mode,
    _keyNum = 0,
    _pos;

/****************************************************************/

  macro getKeyNum()
    var numKey;

    if( valtype( _fileSave ) == V_GENOBJ )
      numKey = _fileSave.KeyNum;
    else
      if( valtype( _fileSave ) == V_FILE )
        numKey = KeyNum( _fileSave, 0 );
        KeyNum( _fileSave, numKey );
      end;
    end;
  
    return numKey;
  end;

/****************************************************************/

  macro setKeyNum( numKey  )
    if( valtype( _fileSave ) == V_GENOBJ )
      _fileSave.KeyNum = numKey;
    else
      if( valtype( _fileSave ) == V_FILE )
        KeyNum( _fileSave, numKey );
      end;
    end;
  end;

/****************************************************************/

  macro getPosition()
    var position;

    position = GetPos( _fileSave );
    GetDirect( _fileSave, position );

    return position;
  end;

/****************************************************************/

  macro setPos( pos  )
    GetDirect( _fileSave, pos );
  end;

/****************************************************************/
//Деструктор
  macro destructor()
    if ( ( _fileSave != null ) and ( _mode != null ) )
      setKeyNum( _keyNum );
      if( _mode == SAVE_POS )
        setPos( _pos );
      end;
    end;
  end;

  macro _init()
    _keyNum = getKeyNum();
    _pos = getPosition();
  end;

/****************************************************************/
// Конструктор
  if ( ( _fileSave != null ) and ( _mode != null ) )
    _init();
  end;


end;