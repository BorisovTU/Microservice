/*
  RS-Retail
  Обслуживание физических лиц
----------------------------------------------------------------------
  Макрос для Пробизнесбанка
  Операция # 80 (б\н покупка валюты)
  Шаг # 240 (вызывается после формирования документа, но- до транзакции)
----------------------------------------------------------------------
  В случае выбора третьего счета организуется своя схема
  выполнения операции 80, в противном случае- стандартная схема
----------------------------------------------------------------------
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!  Текущая реализация- только для рублей (покупка валюты за рубли) !!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
----------------------------------------------------------------------
      Рекомендации по специфическим настройкам для успешной работы
      по 80 операции с данным макросом
      ------------------------------------------------------------
1. "Сервис АРМ", меню "Справочники\Настройка параметров АБС" установить
   параметр "RS-RETAIL\СЕРВИСНЫЕ\ВКЛАДЫ\КУРСЫ_ВАЛЮТ\ПО_БАЗОВОЙ" равным
   1. Это означает, что курс операции будет расчитан исходя из курса ЦБ.
2. Для вида вклада, с которого будет идти покупка валюты, в 80 операции 
   отключить шаг "Подоходный налог"
3. Для рублевого вида вклада, с которого будет идти покупка валюты,
   в шаге "Ограничение на сумму списания" операции 80 описать данный
   макрос
4. Для вида вклада, на который будет зачислена сконвертированная сумма,
   а потом списана на третий счет, разрешить нулевой остаток: из панели
   параметров процентов по виду вклада- F6, включить параметр
   "Допустим нулевой остаток без закрытия счета"
----------------------------------------------------------------------
  Ромодин Александр Васильевич
  15.02.2001
*/
import deprintr,alg_vars;

record DocRec     ("sbdepdoc.1");
record SaveDocRec ("sbdepdoc.1"); /* Исходный документ */
record DocForOper ("sbdepdoc.1");
file   DEPOSITR   (depositr) key 8;
file   SBDEPDOC   (sbdepdoc);
file   TempFile   ("sbdepdoc.dbt");
/*
 Для настроек
*/
/*    Вид вклада для перечисления (третий счет операции, Срочный счет) */
const TypeAccountForTransfer = "";
/*    Лимит суммы (получателя) на одну операцию */
const LimitSumForOnOperation = $10000;


/*
  Глобальные переменные
*/
var RefForTransfer  = 0;         /* Референс третьего счета   */
var TypeAccTransfer = "";        /* Вид вклада третьего счета */
var AccountTransfer = "";        /* Номер срочного счета      */
var NumError        = 0;
var ErrStr          = "";

var FlagCur = NumFlagCur();      /* Рубли\Валюта */



/**********************************************************/

macro TrnProc
/*
  Выполнение операции в транзакции
  Реализовано только для рублей!!!
*/
  var stat          = True;
  var AllSumIn      = SaveDocRec.CurrencyBought; /* Купленная валюта */
  var SummaTaxRub   = SaveDocRec.SummaTaxRub;    /* Подоходный налог */
  var OperSumIn;                             /* Купленная валюта за 1 операцию */
  var NumCycle      = 0;
  var NoNeedFormDoc = False;                 /* - Можно ли не пересчитывать суммы операций
                                                при разбиении документов?
                                                - Можно, если будет только
                                                один док-т по 80 операции */
  var С_Панелью  = 0,
      UseMaxDate = 1,
      NoPrint    = 1;
/*
  Сумма операции (в долларах) разбивается по 10000 долларов каждая
  (реализовано только для продажи рублей)
*/
  while (stat AND (AllSumIn > $0))
    NumCycle = NumCycle + 1;
    if (AllSumIn >= LimitSumForOnOperation)
      OperSumIn = LimitSumForOnOperation;
    else
      OperSumIn = AllSumIn;
      if (NumCycle == 1)      /* Единственный документ */
        NoNeedFormDoc = True; /* Делать пересчет не надо */
      end;
    end;
    AllSumIn = AllSumIn - OperSumIn;
/*  Формирование документа с новыми (урезанными) суммами */
    Copy (DocForOper,SaveDocRec);
    DocForOper.NumDayDoc        = 0;
    DocForOper.ApplType         = 0;
    if (NOT NoNeedFormDoc)
      DocForOper.CurrencyBought = OperSumIn;  /* Купленная валюта */
                                              /* Сумма в рублях   */
      DocForOper.OutSum         = (OperSumIn/100) * (DocForOper.PercTrnRest/100);
      if (SummaTaxRub > $0)            /* Подоходный налог */
        DocForOper.SummaTaxRub = SummaTaxRub;
        SummaTaxRub = $0;
      else
        DocForOper.SummaTaxRub = $0;
      end;
    end;
/*  1. Проводка 80 операции по урезанной сумме */
    if (stat)
      ClearRecord(ALG_OPERPRM);
      ALG_OPERPRM.Account           = ALG_DEPOSITOR.Account;
      ALG_OPERPRM.Type_Account      = ALG_DEPOSITOR.Type_Account;
      ALG_OPERPRM.Code_Currency     = ALG_DEPOSITOR.Code_Currency;
      ALG_OPERPRM.Type_Oper         = 80;
      ALG_OPERPRM.Show_Panel        = С_Панелью;
      ALG_OPERPRM.UseMaxDate        = UseMaxDate;
      ALG_OPERPRM.NoPrint           = NoPrint;
      ALG_OPERPRM.NonCashCommission = ALG_PARAM.prmI;
      ALG_OPERPRM.TypeComplexOper   = ALG_PARAM.NumOpert;
      NumError = Выполнение_Операции(ALG_OPERPRM,DocForOper);
      if (NumError != 0)
        stat = False;
        ErrStr = "На итерации "+String(NumCycle)+" для операции 80 возникла ошибка "+String(NumError);
      elif (SaveDocRec.Account_Receiver == "")
/*        GetDirect (SBDEPDOC,ALG_OPERPRM.MainDocRecPos);
 Докеумент теперь надо искать по полям уникального ключа,
 переданным в параметре (Referenc,Date_Document,NumDayDoc */
        SetRecordAddr(DocRec,SBDEPDOC,0,0,True);
        SaveDocRec.Account_Receiver = DocRec.Account_Receiver;
      end;
    end;
/*  2. Проводка 65 операции по урезанной сумме- перечисление
       сконвертированной суммы (купленной валюты) на третий счет */
    if (stat)
      Copy (DocForOper,SaveDocRec);
      DocForOper.NumDayDoc        = 0;
      DocForOper.ApplType         = 0;
      DocForOper.OutSum           = OperSumIn;  /* Купленная валюта */
      DocForOper.Account_Receiver = AccountTransfer; /* Номер счета-получателя      */
      DocForOper.CorAcc_Receiver  = TypeAccTransfer; /* Вид вклада счета-получателя */
      DocForOper.Referenc         = 0;
      DocForOper.Account          = SaveDocRec.Account_Receiver; /* Плательщик */
      DocForOper.Type_Account     = SaveDocRec.CorAcc_Receiver;  /* Вид вклада плательщика */
      DocForOper.Code_Currency    = SaveDocRec.CodeCur_Receiver; /* Валюта счета-получателя */
      DocForOper.IsCur            = 1;
      DocForOper.TypeOper         = 65;
      ClearRecord(ALG_OPERPRM);
      ALG_OPERPRM.Account           = DocForOper.Account;
      ALG_OPERPRM.Type_Account      = DocForOper.Type_Account;
      ALG_OPERPRM.Code_Currency     = DocForOper.Code_Currency;
      ALG_OPERPRM.Type_Oper         = 65;
      ALG_OPERPRM.Show_Panel        = С_Панелью;
      ALG_OPERPRM.UseMaxDate        = UseMaxDate;
      ALG_OPERPRM.NoPrint           = NoPrint;
      ALG_OPERPRM.NonCashCommission = ALG_PARAM.prmI;
      ALG_OPERPRM.TypeComplexOper   = ALG_PARAM.NumOpert;
      ALG_OPERPRM.CorrAcc           = DocForOper.Account_Receiver;
      ALG_OPERPRM.CorrType          = DocForOper.CorAcc_Receiver;
      SetFlagCur(1,1);
      NumError = Выполнение_Операции(ALG_OPERPRM,DocForOper);
      SetFlagCur(FlagCur,1);
      if (NumError != 0)
        stat = False;
        ErrStr = "На итерации "+String(NumCycle)+" для операции 65 возникла ошибка "+String(NumError);
      elif (AccountTransfer == "")
/*        GetDirect (SBDEPDOC,ALG_OPERPRM.MainDocRecPos);
 Докеумент теперь надо искать по полям уникального ключа,
 переданным в параметре (Referenc,Date_Document,NumDayDoc */
        SetRecordAddr(DocRec,SBDEPDOC,0,0,True);
        AccountTransfer = DocRec.Account_Receiver;
      end;
    end;
  end;
  if (NOT stat)
    AbortTrn();
  end;
end;
/***********************************************************
############################################################
***********************************************************/

macro SelectThirdAccount
/*
  Выбор третьего (срочного) счета
*/
  var   NumSelect;
  var   Cycle = True;
  array WhatDo;
  WhatDo[0] = "Открыть";         /* Открыть новый счет */
  WhatDo[1] = "Выбрать";         /* Выбрать счет- вернуться в панель выбора */
  WhatDo[2] = "Прервать";        /* Прервать операцию */
  WhatDo[3] = "Cтандартно";      /* Выполнить как запрограммировано в exe */
  WhatDo[5] = "Выполнить макросом";

  MsgBox ("Задайте срочный счет");
  RefForTransfer = PanelAccountForSelect(
      TypeAccountForTransfer,         /* Вид вклада */
      SaveDocRec.CodeCur_Receiver,        /* Валюта- получатель */
      1, 1,
      ALG_COMDEPCLNT.rec.CodClient,
      0);
  if (RefForTransfer == 0)
    NumSelect = ConfDlg ("Вы не выбрали срочный счет. Что делать?",NULL,
      WhatDo[0],
      WhatDo[1],
      WhatDo[2],
      WhatDo[3]);
  else
    depositr.Referenc = RefForTransfer;
    if (NOT GetEQ(depositr))
      NumSelect = ConfDlg ("Выбранный Вами срочный счет не найден. Что делать?",NULL,
        WhatDo[0],
        WhatDo[1],
        WhatDo[2],
        WhatDo[3]);
      if (NumSelect == 0)
        RefForTransfer = 0; /* Открытие нового счета */
      end;
    else
      NumSelect = 5; /* Выполнить макросом */
      TypeAccTransfer = depositr.Type_Account;
      AccountTransfer = depositr.Account;
    end;
  end;
  if (NumSelect == 0) /* Для открытия нового счета надо выбрать вид вклада */
    if (TypeAccountForTransfer != "")
      TypeAccTransfer = TypeAccountForTransfer;
    else
      if (FlagCur == 0)
        SetFlagCur(1,1);
      else
        SetFlagCur(0,1);
      end;
      while (Cycle)
        MsgBox ("Выберите вид вклада для срочного счета");
        if (NOT (SelectDepType(TypeAccTransfer)))
          NumSelect = ConfDlg ("Вы не выбрали срочный вид вклада. Что делать?",NULL,
            WhatDo[1],   /* Выбрать */
            WhatDo[2]);  /* Прервать операцию */
          if (NumSelect == 1)
            NumSelect = NumSelect + 1;
            Cycle = False;
          end;
        else
          Cycle = False;
        end;
      end;
      SetFlagCur(FlagCur,1);
    end;
  end;
  return NumSelect;
end;
/*********************************************************************
 Точка входа в макрос
*/
  var NumSelect;
  var Cycle = True;

  if (FlagCur == 1)
    ALG_RESULT = OK_RES;   /* Выполнить операцию 80 стандартным образом */
    return;
  end;

  Copy (TempFile,ALG_SBDEPDOC);
  SetRecordAddr(DocRec,TempFile,0,0,True);
  Copy (SaveDocRec,DocRec);
  if (SaveDocRec.OutSum == 0)
    MsgBox ("Не задана сумма операции");
    ALG_RESULT = ERR_RES;
  elif (SaveDocRec.CorAcc_Receiver == "")
    MsgBox ("Не задан вид вклада- получателя");
    ALG_RESULT = ERR_RES;
  else
    while (Cycle)
      NumSelect = SelectThirdAccount ();
      Cycle = False;
      if (NumSelect == 0)
        ALG_RESULT = END_RES;  /* Открыть новый счет, выполнение- макросом */
        NumSelect = 5;
      elif (NumSelect == 1)
        Cycle = True;          /* Повторить выбор счета */
      elif (NumSelect == 2)
        ALG_RESULT = END_RES;  /* Прервать операцию */
      elif (NumSelect == 3)
        ALG_RESULT = OK_RES;   /* Выполнить операцию 80 стандартным образом */
      elif (NumSelect == 5)
        ALG_RESULT = END_RES;  /* Выполнить операцию макросом */
      end;
    end;
    if (NumSelect == 5)        /* Выполнение операции макросом */
      if (NOT ProcessTrn(1, "TrnProc"))
        ALG_RESULT = ERR_RES;  /* Ошибка в транзакции */
      end;
    end;
  end;
  if (NumError != 0)
    if (ErrStr == "")
      MsgBox ("При выполнении шага 240 допущена ошибка "+String(NumError));
    else
      MsgBox (ErrStr);
    end;
  end;
end;    /* --- Конец макроса --- */
