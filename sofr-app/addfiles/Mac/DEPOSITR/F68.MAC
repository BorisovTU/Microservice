/**********************************************************
*  ---------------------                                  *
*  R-Style SoftWare Lab.                                  *
*  ---------------------                                  *
*  RS-Retail                                              *
*                                                         *
*  Статистика по вкладам                                  *
*  Формирование оборотно-сальдовой ведомости (Форма 68)   *
**********************************************************/
import deprintr,"cur_rate.mac", sqlconv, rsd;

file F68Tmp     ("f68.tmp"          ) key 0 write;
file DepType    (sb_dtyp            ) key 2;
file Curr       (currency           ) key 0;
var DepStat = TBFile ("sb_depst","r",0);
var ddep = TRecHandler( "i_dp_dep.rec" );

var PrevStat = TRecHandler ("sb_depst");
var CurStat = TRecHandler ("sb_depst");
record Tot      ("f68.tmp");
record TotTot   ("f68.tmp");

var {curdate};
var {oper};
var IsCur       = NumFlagCur();
var Branch      = NumFNCash();
var Mode        = GetCurrentMode();
var TmpFileName = "f68" + String({oper}) + ".dbt";

var CurRate;
var PrevRate;
var Citizenship;
var DepTypeDisplayed;
var SmthDisplayed;

/* Какие формы печатать */
const BothForms        = 0; /* Печатать обе формы */
const OnlyForNativeCur = 1; /* Печатать формы с пересчетом на рубли   */
const OnlyForBaseCur   = 2; /* Печатать формы с пересчетом на доллары */
var   WhatForms        = -1;
var   CurrentForm      = -1;


/*******************************************************************
*******************************************************************/
macro ChooseWhatForms

  array AFormReport;
    AFormReport(0) = " Печатать обе ведомости ";
    AFormReport(1) = " Печатать ведомость с пересчетом на рубли ";
    AFormReport(2) = " Печатать ведомость с пересчетом на базовую валюту ";

    WhatForms = Menu(AFormReport," ~ENTER~ Печать  ~ESC~ Выход","Выберите тип ОСВ");

    if (WhatForms < 0)
      Exit();
    end;

end;


/*******************************************************************
*******************************************************************/

macro ClearTmpFile
  var cmd;

  cmd = RsdCommand( NULL, string("truncate table df68_tmp") );
  cmd.execute();
end;


/*******************************************************************
*******************************************************************/
macro GetCurrRate1 (Dd)

  var RateChangeID = 0;
  var Loop         = TRUE;
  var Rate         = 0.0;

  copy( RtlCurr, Curr );
  Rate = GetCurrRate(Dd);
  if (Rate != 0)
    if (CurrentForm == OnlyForBaseCur)
  Rate = CurrRate.CrossRate;
    end;
  end;

  return Rate;

end;


/*******************************************************************
*******************************************************************/
macro GetDepStat

  DepStat.Clear;
  CurStat.Clear;
  PrevStat.Clear;
  DepStat.rec.FNCash = Branch;
  DepStat.rec.IsCur = 1;
  DepStat.rec.Kind = DepType.Kind;
  DepStat.rec.CodCur = Curr.Code_Currency;
  DepStat.rec.FlagRez = Citizenship;
  DepStat.rec.Date = {curdate};
  DepStat.rec.Mode = Mode;
  DepStat.addFilter ( "t.t_fncash = " + string(Branch) +
                      " and t.t_iscur = " + string(IsCur) +
                      " and t.t_kind = " + GetSQLString(DepType.Kind) +
                      " and t.t_codcur = " + string(Curr.Code_Currency) +
                      " and t.t_flagrez = " + sqlChar(Citizenship));
  if(DepStat.GetLE
    and (DepStat.rec.FNCash == Branch)
    and (DepStat.rec.IsCur == 1)
    and (DepStat.rec.Kind == DepType.Kind)
    and (DepStat.rec.CodCur == Curr.Code_Currency)
    and (DepStat.rec.FlagRez == Citizenship))
    if( ( DepStat.rec.Date == {curdate} ) and ( DepStat.rec.Mode == Mode ) )
      Copy(CurStat, DepStat);
      if(DepStat.Prev
          and (DepStat.rec.FNCash == Branch)
          and (DepStat.rec.IsCur == 1)
          and (DepStat.rec.Kind == DepType.Kind)
          and (DepStat.rec.CodCur == Curr.Code_Currency)
          and (DepStat.rec.FlagRez == Citizenship))
        Copy(PrevStat, DepStat);
      end;
    else
      Copy(PrevStat, DepStat);
      Copy(CurStat, DepStat);
      CurStat.rec.DebSum = $0;
      CurStat.rec.KredSum = $0;
      CurStat.rec.MemDebSum = $0;
      CurStat.rec.MemKredSum = $0;
      CurStat.rec.CurOpenAc = 0;
      CurStat.rec.CurClosAc = 0;
    end;
  end;
  DepStat.dropFilter;
end;


/*******************************************************************
*******************************************************************/
/*         RA 07-03-01 нужно пользоваться справочником Retail
macro FindExchCurr

  ExchCurr.ID = Int(Curr.ExternalCode);
  return GetEQ(ExchCurr);
end;
*/

/*******************************************************************
*******************************************************************/
macro TreatDepType

  var
    Err,
    Delta;

  GetDepStat;
  Delta = Money(CurStat.rec.Rest * CurRate - PrevStat.rec.Rest * PrevRate +
    (CurStat.rec.DebSum - CurStat.rec.KredSum) * CurRate);
  Err = Money(CurStat.rec.Rest * CurRate - PrevStat.rec.Rest * CurRate +
    CurStat.rec.DebSum  * CurRate - CurStat.rec.KredSum * CurRate);
  ClearRecord(F68Tmp);
  F68Tmp.AccTypeName = DepType.Name;
  F68Tmp.CurrCode = Curr.Code_Currency;
  F68Tmp.CurrISO = Curr.Short_Name;
  F68Tmp.Citizenship = Citizenship;
  F68Tmp.CurrName = Curr.Name_Currency;
  F68Tmp.CurrPrevRate = PrevRate;
  F68Tmp.CurrCurRate = CurRate;
  F68Tmp.InNumAccs = PrevStat.rec.ColOpenedAcc;
  F68Tmp.InRest = PrevStat.rec.Rest;
  F68Tmp.InRestR = Money(PrevStat.rec.Rest * CurRate);
  F68Tmp.CredNumAccs = CurStat.rec.CurOpenAc;
  F68Tmp.Cred = CurStat.rec.KredSum;
  F68Tmp.CredR = Money(CurStat.rec.KredSum*CurRate);
  F68Tmp.DebNumAccs = CurStat.rec.CurClosAc;
  F68Tmp.Deb = CurStat.rec.DebSum;
  F68Tmp.DebR = Money(CurStat.rec.DebSum*CurRate);
  F68Tmp.OutNumAccs = CurStat.rec.ColOpenedAcc;
  F68Tmp.OutRest = CurStat.rec.Rest;
  F68Tmp.OutRestR = Money(CurStat.rec.Rest * CurRate);
  if(Delta > 0)
    F68Tmp.CredDelta = Delta;
  else
    F68Tmp.DebDelta = -Delta;
  end;
  if ( Err != 0 )
    if ( CurStat.rec.Rest > PrevStat.rec.Rest )
      F68Tmp.CredR = F68Tmp.CredR + Err;
    else
      F68Tmp.DebR = F68Tmp.DebR - Err;
    end;
  end;
  if((F68Tmp.InRest != 0)
      or (F68Tmp.Cred != 0)
      or (F68Tmp.Deb != 0)
      or (F68Tmp.OutRest != 0))
    Insert(F68Tmp);
  end;
  return;
end;


/*******************************************************************
*******************************************************************/
macro TreatCurrency

  var
    RecFound;

  /*FindExchCurr();*/
  PrevRate = GetCurrRate1({curdate} - 1);
  CurRate = GetCurrRate1({curdate});
  ClearRecord(DepType);
  DepType.FlagCur = 1;
  RecFound = GetGE(DepType);
  while(RecFound and (DepType.FlagCur == 1))
    if((DepType.Kind != "Служебный")
      and (DepType.Kind != "Неподвижный")
      and (DepType.Kind != "Объединенный"))
      TreatDepType;
    end;
    RecFound = Next(DepType);
  end;
end;


/*******************************************************************
*******************************************************************/
macro TreatAllCurrs

  var
    RecFound;

  Curr.Code_Currency = 1;
  RecFound = GetGE(Curr);
  while(RecFound)
    if( Curr.CrDifrRate ) /* валюта используется */
      TreatCurrency;
    end;
    RecFound = Next(Curr);
  end;
end;


/*******************************************************************
*******************************************************************/
macro FillTmpFile

  Citizenship = StrFor(0);
  TreatAllCurrs;
  Citizenship = StrFor(1);
  TreatAllCurrs;

end;


/*******************************************************************
*******************************************************************/
macro FindBranch

  return findDepartment(Branch, null, ddep);
end;


/*******************************************************************
*******************************************************************/
macro OutHead

  var
    Title, SubTitle, RecFound;

  FindBranch();
  Title = "Оборотно-сальдовая ведомость по вкладам за " + String({curdate});
  if (CurrentForm == OnlyForBaseCur)
    SubTitle = "(с пересчетом по кросс-курсу на базовую валюту)";
  else
    SubTitle = "(с пересчетом на национальную валюту)";
  end;
  [ ];
  [  Филиал #                                                                                                                                            Ф. 68]
  (ddep.rec.Name);
  [ ];
  [###########################################################################################################################################################]
  (Title:c);
  [###########################################################################################################################################################]
  (SubTitle:c);
  [ ];
  if (CurrentForm == OnlyForBaseCur)
    [-----------------------------------------];
    [|Кросс-курсы валюты|   |                |];
    [|------------------|Вал| Наимен. валюты |];
    [| Текущий | Предыд.|   |                |];
    [|         |        |   |                |];
    [-----------------------------------------];
  else
    [---------------------------------------];
    [|  Курсы валюты  |   |                |];
    [|----------------|Вал| Наимен. валюты |];
    [|Текущий |Предыд.|   |                |];
    [|        |       |   |                |];
    [---------------------------------------];
  end;
  ReWind(Curr);
  Curr.Code_Currency = 1;
  RecFound = GetGE(Curr);
  while(RecFound)
    if( Curr.CrDifrRate ) /* валюта используется */
      /*FindExchCurr();*/
      PrevRate = GetCurrRate1({curdate} - 1);
      CurRate  = GetCurrRate1({curdate});
      if (CurrentForm == OnlyForBaseCur)
        [|#########|########|###|################|]
        (Double(CurRate):9:4, Double(PrevRate):9:4,
         Curr.Short_Name, Curr.Name_Currency:c);
      else
        [|########|#######|###|################|]
        (Double(CurRate):8:4, Double(PrevRate):8:4,
         Curr.Short_Name, Curr.Name_Currency:c);
      end;
    end;
    RecFound = Next(Curr);
  end;
  [ ];
  [-------------------------------------------------------------------------------------------------------------------------];
  [|   |    Остатки на начало дня   |        П р и х о д         |        Р а с х о д         |   Остаток на конец дня     |];
  [|Вал|----------------------------|----------------------------|----------------------------|----------------------------|];
  if (CurrentForm == OnlyForBaseCur)
    [|   |Кол|  Номинал  | Баз.валюта |Кол|  Номинал  | Баз.валюта |Кол|  Номинал  | Баз.валюта |Кол|  Номинал  | Баз.валюта |];
  else
    [|   |Кол|  Номинал  |   Рубли    |Кол|  Номинал  |   Рубли    |Кол|  Номинал  |   Рубли    |Кол|  Номинал  |   Рубли    |];
  end;
  [|   |сч.|           |            |сч.|           |            |сч.|           |            |сч.|           |            |];
  [-------------------------------------------------------------------------------------------------------------------------];
end;


/*******************************************************************
*******************************************************************/
macro OutDepType(Cshp)

  var
    CshpStr;

  if(Cshp == StrFor(0))
    CshpStr = "(рез.)";
  else
    CshpStr = "(нер.)";
  end;
  [|   |   |           |            |   |           |            |   |           |            |   |           |            |];
  [| ####################################           |            |   |           |            |   |           |            |]
  (String(F68Tmp.AccTypeName:29) + " " + CshpStr);
  [|   |   |           |            |   |           |            |   |           |            |   |           |            |];
end;


/*******************************************************************
*******************************************************************/
macro OutLine

  if((F68Tmp.InRest != 0)
    or (F68Tmp.Cred != 0)
    or (F68Tmp.Deb != 0)
    or (F68Tmp.OutRest != 0))
    if(not DepTypeDisplayed)
      OutDepType(F68Tmp.Citizenship);
      DepTypeDisplayed = true;
    end;
  [|###|###|###########|############|###|###########|############|###|###########|############|###|###########|############|]
    (
      F68Tmp.CurrISO,
      F68Tmp.InNumAccs:z,
      F68Tmp.InRest:z,
      F68Tmp.InRestR:z,
      F68Tmp.CredNumAccs:z,
      F68Tmp.Cred:z,
      F68Tmp.CredR:z,
      F68Tmp.DebNumAccs:z,
      F68Tmp.Deb:z,
      F68Tmp.DebR:z,
      F68Tmp.OutNumAccs:z,
      F68Tmp.OutRest:z,
      F68Tmp.OutRestR:z
    );
    if((F68Tmp.CredDelta != 0) or (F68Tmp.DebDelta != 0))
  [|   |   |           |            |   |           |############|   |           |############|   |           |            |]
      (
  F68Tmp.CredDelta:z,
  F68Tmp.DebDelta:z
      );
    end;
    Tot.InNumAccs = Tot.InNumAccs + F68Tmp.InNumAccs;
    Tot.CredNumAccs = Tot.CredNumAccs + F68Tmp.CredNumAccs;
    Tot.DebNumAccs = Tot.DebNumAccs + F68Tmp.DebNumAccs;
    Tot.OutNumAccs = Tot.OutNumAccs + F68Tmp.OutNumAccs;
    Tot.InRestR = Tot.InRestR + F68Tmp.InRestR;
    Tot.CredR = Tot.CredR + F68Tmp.CredR;
    Tot.DebR = Tot.DebR + F68Tmp.DebR;
    Tot.CredDelta = Tot.CredDelta + F68Tmp.CredDelta;
    Tot.DebDelta = Tot.DebDelta + F68Tmp.DebDelta;
    Tot.OutRestR = Tot.OutRestR + F68Tmp.OutRestR;
    SmthDisplayed = true;
  end;
end;


/*******************************************************************
*******************************************************************/
macro OutTotals

  [|   |   |           |            |   |           |            |   |           |            |   |           |            |];
  [| И т о г о :       |            |   |           |            |   |           |            |   |           |            |];
  [|   |###|           |############|###|           |############|###|           |############|###|           |############|]
  (
    Tot.InNumAccs,
    Tot.InRestR,
    Tot.CredNumAccs,
    Tot.CredR,
    Tot.DebNumAccs,
    Tot.DebR,
    Tot.OutNumAccs,
    Tot.OutRestR
  );
  [|   |   |           |            |   |           |############|   |           |############|   |           |            |]
  (
    Tot.CredDelta,
    Tot.DebDelta
  );

  TotTot.InNumAccs   = TotTot.InNumAccs   + Tot.InNumAccs;
  TotTot.CredNumAccs = TotTot.CredNumAccs + Tot.CredNumAccs;
  TotTot.DebNumAccs  = TotTot.DebNumAccs  + Tot.DebNumAccs;
  TotTot.OutNumAccs  = TotTot.OutNumAccs  + Tot.OutNumAccs;
  TotTot.InRestR     = TotTot.InRestR     + Tot.InRestR;
  TotTot.CredR       = TotTot.CredR       + Tot.CredR;
  TotTot.DebR        = TotTot.DebR        + Tot.DebR;
  TotTot.CredDelta   = TotTot.CredDelta   + Tot.CredDelta;
  TotTot.DebDelta    = TotTot.DebDelta    + Tot.DebDelta;
  TotTot.OutRestR    = TotTot.OutRestR    + Tot.OutRestR;

end;


/*******************************************************************
*******************************************************************/
macro OutTotTotals

  Copy(Tot, TotTot);
  OutTotals;
end;


/*******************************************************************
*******************************************************************/
macro OutLines

  var
    PrevAccTypeName = "",
    PrevCitizenship = "?",
    F68TmpContain = false;

  Rewind(F68Tmp);
  SmthDisplayed = false;
  while(Next(F68Tmp))
    F68TmpContain = true;
    if((F68Tmp.AccTypeName != PrevAccTypeName)
      or (F68Tmp.Citizenship != PrevCitizenship))
      if(SmthDisplayed)
        OutTotals;
        SmthDisplayed = false;
      end;
      ClearRecord(Tot);
      PrevAccTypeName = F68Tmp.AccTypeName;
      PrevCitizenship = F68Tmp.Citizenship;
      DepTypeDisplayed = false;
    end;
    OutLine;
  end;
  if (F68TmpContain)
    OutTotals;
  end;
end;


/*******************************************************************
*******************************************************************/
  if(not IsCur)
    MsgBox("Ф.68 выпускается только в валютном режиме");
    Exit;
  end;

  if ( not UpdateStatistics( true ) )
    Exit;
  end;

  ChooseWhatForms();

  /* Печать отчета с пересчетом на национальную валюту */
  if ((WhatForms == BothForms) OR (WhatForms == OnlyForNativeCur))
    CurrentForm = OnlyForNativeCur;
    Message("Заполнение промежуточного файла ...");
    ClearTmpFile();
    FillTmpFile();
    Message("");
    ClearRecord(TotTot);
    OutHead;
    OutLines;
    OutTotTotals;
  end;
  /* Печать отчета с пересчетом на базовую валюту по кросс-курсу */
  if ((WhatForms == BothForms) OR (WhatForms == OnlyForBaseCur))
    Message("Заполнение промежуточного файла ...");
    CurrentForm = OnlyForBaseCur;
    ClearTmpFile();
    FillTmpFile();
    Message("");
    ClearRecord(TotTot);
    if (WhatForms == BothForms)
      [#](StrFor(12));
    end;
    OutHead;
    OutLines;
    OutTotTotals;
  end;

end;
