import deprintr, IsSrvDoc, cur_rate;

file Curr    ( currency ) key 0;
var  client = TClientList;
/*
file dt      ( sb_dtyp ) key 2;
file depositr( depositr ) key 4;
file sbdepdoc( sbdepdoc ) key 6;
*/
var  dt       = TBFile( "sb_dtyp.dbt", "r", 2 );
var  depositr = TBFile( "depositr.dbt", "r", 4 );
var  sbdepdoc = TBFile( "sbdepdoc.dbt", "r", 6 );

const FNCash = NumFNCash(); /* Номер филиала */

const                       /* Группы вкладчиков */
 VZAIM = 0,
 OFFSH = 1;

private var {curdate};      /* Текущая дата  */
private var {Name_Bank};
var ReportFNCash   = -1;    /* По какому филиалу выпустить отчет */
var ClntType = -1;          /* Группа вкладчиков */
var CBRate, Scale;

/* Массив филиалов, учавствующих в отчете */
array FNCashArray;

/* Панель редактирования */
record Panel( "REP_STR_" ) dialog;
const ne_BegDate    = 0, /* Начальная дата */
      ne_EndDate    = 1, /* Конечная дата  */
      ne_NameBranch = 2; /* Номер филиала  */

  Panel.NameBranch = "По всем";


macro ListDep

  array AFNCash;
  array ANBranch;
  array MenuName;
  var i = 0;
  var dpDepList = TdpDepList;

  AFNCash(0) = -1;
  ANBranch(0) = "По всем";
  MenuName(0) = " " + SubStr(ANBranch(0) + MkStr(" ",12),1,12) + " ";

  dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
  while (dpDepList.next())
    i = i + 1;
    AFNCash(i) = dpDepList.rec.Code;
    ANBranch(i) = dpDepList.rec.Name;
    MenuName(i) = " " + SubStr(dpDepList.rec.Name + MkStr(" ",12),1,12) + "   " + /*listfdep.Name +*/ " ";
  end;

  i = Menu(MenuName,NULL,"Выбор филиала");

  if (i >= 0)
    ReportFNCash = AFNCash(i);
    Panel.NameBranch = ANBranch(i);
  end;

end;

macro DlgHndlr( IP, cmd, id, key )

  var stat = CM_DEFAULT;
  var i;

  /* ==================== Обработчик клавиатуры ===================== */
  if (cmd == DLG_KEY)
    /* Enter --------------------------------------------------- */
    if (key == 13)
      if (id == ne_NameBranch)
        SetFocus(IP,ne_BegDate);
        stat = CM_IGNORE;
      end;
    /* Esc ----------------------------------------------------- */
    elif (key == 27)
      ;
    /* F3 ------------------------------------------------------ */
    elif (key == 317)
      if (id == ne_NameBranch)
        ListDep();
      end;
    /* F9 ------------------------------------------------------ */
    elif (key == 323)
      stat = CM_IGNORE;
    /* F7 ------------------------------------------------------ */
    elif (key == 321)
      stat = CM_SAVE;
    end;
  /* ========================== Сохранение ========================== */
  elif (cmd == DLG_SAVE)
    if( Panel.BegDate > Panel.EndDate )
       msgbox( "Дата начала периода не может быть больше даты конца!" );
       return CM_CANCEL;
    end;
  end;

  return stat;

end;

macro LastDayInMonth( Mon )

  var Day = 31;

  if( Mon == 2 )
    Day = 28;
  elif( ( Mon == 4 ) OR ( Mon == 6 ) OR ( Mon == 9 ) OR ( Mon == 11 ) )
    Day = 30;
  end;

  return Day;

end;

macro InitFields

  var d,m,y;

  DateSplit( {curdate}, d, m, y );
  if( d == LastDayInMonth( m ) )
    Panel.EndDate = {curdate};
  else
    m = m - 1;
    if (m < 1)
      m = 12;
      y = y - 1;
    end;
    d = LastDayInMonth( m );
    Panel.EndDate = Date( d, m, y );
  end;
  Panel.EndDate = Panel.EndDate + 1;
  Panel.BegDate = Date( 1, m, y );

end;

macro FindCurr( Code )

  Curr.Code_Currency = Code;
  if ( not GetEQ( Curr ) )
    MsgBox( "Не найдена валюта" );
    Exit( 1 );
  end;
end;

macro GetCBRate( WorkDate )

  Scale = 1;
  CBRate = 0.0;

  if (Curr.ScaleOfc != 0)
    Scale = Curr.ScaleOfc;
  end;

/*
  ExchCurr.ID = Curr.ExternalCode;
  if ( not GetEQ( ExchCurr ) )
    CBRate = 0;
  end;
*/
  Copy( RtlCurr, Curr );
  CBRate = GetCurrRate( WorkDate );
end;

macro Cur2Rub( Sum, WorkDate )

  if( Sum == 0.0 )
    return Sum;
  end;
  if( not depositr.rec.IsCur )
    return Sum;
  end;
  FindCurr( depositr.rec.Code_Currency );
  GetCBRate( WorkDate );
  return Money( CBRate * Sum );
end;

macro GetRestOnDate( d )

  var
    RecFound,
    Rest = $0.0;

  sbdepdoc.addFilter("t.t_Referenc = " + string(depositr.rec.Referenc) );
  sbdepdoc.rec.Referenc = depositr.rec.Referenc;
  sbdepdoc.rec.Date_Document = d;
  sbdepdoc.rec.NumDayDoc = 32000;
  RecFound = sbdepdoc.GetLE();
  while( RecFound and ( sbdepdoc.rec.Referenc == depositr.rec.Referenc ) )
    if ( sbdepdoc.rec.Date_Document < d )
      sbdepdoc.dropFilter();
      return sbdepdoc.rec.Rest;
    end;
    RecFound = sbdepdoc.Prev();
  end;
  sbdepdoc.dropFilter();
  return Rest;
end;

macro GetTermInDays

  var
    TmpDate = {curdate};

   GetDateOff( TmpDate, dt.rec.KindTerm, dt.rec.Term, TmpDate );
   return TmpDate - {curdate};
end;

macro GetChronAverageRest

  var tmp = $0.0, date = Panel.BegDate + 1;

  tmp = tmp + Cur2Rub( GetRestOnDate( Panel.BegDate ), Panel.BegDate ) / 2;
  while( date < Panel.EndDate )
    tmp = tmp + Cur2Rub( GetRestOnDate( date ), date );
    date = date + 1;
  end;
  tmp = tmp + Cur2Rub( GetRestOnDate( Panel.EndDate ), Panel.EndDate ) / 2;
  tmp = tmp / ( Panel.EndDate - Panel.BegDate );
  return tmp;
end;

macro FIO

  return client.CurRec.ConvertFIO();
end;

macro FindDType

  dt.rec.FlagCur = depositr.rec.IsCur;
  dt.rec.Kind = depositr.rec.Type_Account;
  return GetEQ(dt);
end;

macro ProcessDeposits

  var found, rest = $0.0;

  clearrecord( depositr );
  depositr.addFilter("t.t_CodClient = " + string(client.CurRec.rec.CodClient) );
  depositr.rec.CodClient = client.CurRec.rec.CodClient;
  found = depositr.getGE();
  while( found and ( depositr.rec.CodClient == client.CurRec.rec.CodClient ) )
    if( FindDType )
      if( dt.rec.FormContr and ( (dt.rec.NewGroupNumb != "1") and (dt.rec.NewGroupNumb != "7") ) )
        if( ClntType == VZAIM )
          if( client.CurRec.rec.OffshoreResident != "" )
            if( GetTermInDays >= 91 )
              rest = rest + Cur2Rub( GetRestOnDate( Panel.EndDate ), Panel.EndDate );
            end;
          else
            rest = rest + Cur2Rub( GetRestOnDate( Panel.EndDate ), Panel.EndDate );
          end;
        else
          if( GetTermInDays < 91 )
            rest = rest + Cur2Rub( GetRestOnDate( Panel.EndDate ), Panel.EndDate );
          end;
        end;
      else
        rest = rest + GetChronAverageRest;
      end;
    end;
    found = depositr.next();
  end;
  depositr.dropFilter();
  [|#################################|###############################|]
  ( FIO, rest );
  [|---------------------------------|-------------------------------|];
end;

macro ProcessClients
  var st;

  client.ClearFilterKey ();
  if (ReportFNCash >= 0)
    client.SetFilter_FNcash (ReportFNCash,ReportFNCash);
  end;
  client.SetFilter ();
  st = client.First ();
  while( st )
    if( ( ReportFNCash == -1 ) or ( client.CurRec.rec.FNcash == ReportFNCash ) )
      if( ( ( ClntType == VZAIM ) and ( client.CurRec.rec.Vzaimosvyazanniy != "" ) )
        or
        ( ( ClntType == OFFSH ) and ( client.CurRec.rec.OffshoreResident != "" ) ) )
        ProcessDeposits;
      end;
    end;
    st = client.Next ();
  end;
end;

macro PrintHeader

  var NameBranch = "", NameGroup = "";
  var dep_N;
  var dep_name;

  if (ReportFNCash == -1)
    dep_N = FNCash;
    NameBranch="Банк: "+ {Name_Bank};
  else
    dep_N = ReportFNCash;
    if (findDepartment(dep_N, dep_name))
      NameBranch ="Оперчасть: "+ dep_name;
    end;
  end;

  if( ClntType == VZAIM )
    NameGroup = "взаимосвязанный вкладчик";
  else
    NameGroup = "резидент оффшорной зоны";
  end;

  [ ];
  [
    #
  ]
  ( NameBranch );
  [ ];
  [          Остатки по группе вкладчиков #                                 ]
  ( NameGroup );
  [                   За период с ########## до ##########                  ]
  ( Panel.BegDate, Panel.EndDate );
  [ ];
  [-------------------------------------------------------------------];
  [|           ФИО клиента           |  Сумма остатков по договорам  |];
  [|---------------------------------|-------------------------------|];
end;

macro Report

  PrintHeader;
  ProcessClients;
end;

/* Голова */

  var i;
  array MenuName;

  /* Определение дат отчета при инициализации */
  InitFields;
  if( RunDialog( Panel,"DlgHndlr" ) )
    MenuName(0) = "взаимосвязанный вкладчик";
    MenuName(1) = "резидент оффшорной зоны";
    i = Menu( MenuName, NULL, "Выберите категорию" );
    if( i < 0 )
      [ ];
      [ Отказались от выпуска отчета по группам вкладчиков];
      [ ];
      Exit( -1 );
    else
      ClntType = i;
    end;
    Report;
  else
    [ ];
    [ Отказались от выпуска отчета по группам вкладчиков];
    [ ];
  end;
end;
