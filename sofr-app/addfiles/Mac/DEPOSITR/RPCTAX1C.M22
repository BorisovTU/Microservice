/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Формирование налоговых карточек и справок о доходах
  физического лица для налоговых органов.

  Общие константы, переменные и процедуры.

*****************************************************************/

Import CommonInter, DeprIntr, Cur_Rate;

/****************************************************************
             Заполните реквизиты Вашего банка.
*****************************************************************/

var Name_Bank      = "";           /* Название банка  */
var Name_Depart    = "";           /* Название подразделения */
var Bank_INN       = "_________";  /* ИНН банка */
var Bank_KPP       = "_________";  /* КПП для банка */
var Bank_Phone     = "";           /* Контактный телефон Банка */
var CodeOKATO      = "__________"; /* Код ОКАТО */
var CodeTaxAgency  = "6300";       /* Код налогового органа, где налоговый агент состоит на учете */

var Округлять_до_рублей = TRUE; // TRUE - расчитанные и оплаченные налоги, а также долги, округлять до рублей.

const cKBK_13 = "18210102021011000110";
const cKBK_30 = "18210102030011000110";
const cKBK_35 = "18210102040011000110";

/****************************************************************
       Блок заполнения реквизитов Вашего банка закончен.
*****************************************************************/

file fAddr    ( "adress.dbt"   ) key 1;
file fAdmTerr ( "admterr.dbt"  ) key 0;
file PAPRKIND ( "paprkind.dbt" ) key 0;

file TAX_CARD ( "pc_tax.dbt"   ) key 4 write;

var mPcTax  = TMdbRecordset( "pc_tax.tmp" );
var mPcRate = TMdbRecordset( "pc_tax_r.tmp" );

var COMCLNT = TClientList;  /* Единый клиент */

record TAX_CARD_2 ( "pc_tax2" );
record TAX_CARD_6 ( "pc_tax6" );

const ERR_OK     = 0, /* Нормальное завершение */
      ERR_SKIP   = 1, /* Пропустить обработку клиента */
      ERR_STOP   = 2, /* Прекратить выпуск налоговой карточки */
      ERR_REPEAT = 3; /* Повторить ввод данных */

/* Коды регионов: Москва и Санкт-Петербург */
const rMoscow = 77;  /* Москва */
const rSPb    = 78;  /* Санкт-Петербург */

const FNcash = NumFNcash();

// array ФункцииПоКодамДохода;

var CommonBase =
 GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\КЛИЕНТЫ_ФИЗЛИЦА\\ОБЩАЯ_БАЗА_КЛИЕНТОВ", FALSE );

var CodeModule = 0;

var InputPanel_IdentClient = 0; /* По всем вкладчикам */
var InputPanel_Year_InAll;      /* А вот год сейчас попытаемся угадать */

var TheOnlySole = True;  /* По одному вкладчику */

var vKBK = "__________";  // Код бюджетной классификации.

var BeginDate, EndDate;  /* Период выборки данных из файла налоговой карточки */

var {curdate};

var day;
var year;
var mon;

DateSplit({curdate},day,mon,year);
if (mon < 7)
  InputPanel_Year_InAll = year - 1;
else
  InputPanel_Year_InAll = year;
end;

var ErrCode = 0, ErrText = "";

/****************************************************************
               Общие суммы доходов и налогов.
****************************************************************/



/***********************************************************
                Округление сумм до рубля
***********************************************************/
macro RoundToRuble( sum:Money )
  var r;
  if ( Округлять_до_рублей )
    r = Money( ( sum / 100 ) * 100 );
  else
    r = sum;
  end;
  return r;
end;


/***********************************************************
                         Тип улицы.
***********************************************************/
macro Тип_улицы()
  var sStreetType;
  ClearRecord( fAddr );
  fAddr.Type    = 1;  // Адреса клиентов банка.
  fAddr.PartyID = COMCLNT.CurRec.rec.CodClient;
  if ( GetEQ( fAddr ) )
    ClearRecord( fAdmTerr );
    fAdmTerr.NumberPlace = fAddr.CodeStreet;
    if ( GetEQ( fAdmTerr ) )
      sStreetType = fAdmTerr.Place;
    else
      sStreetType = "";
    end;
  else
    sStreetType = "";
  end;
  return sStreetType;
end;

/***********************************************************
 Строка справочника по документу
*/
macro GetPaprKind (PaperKind)
 PAPRKIND.PaperKind = PaperKind;
 return GetEQ(PAPRKIND);
end;

/********************************************************************
*********************************************************************/
macro Set_TaxRate( Rate, Income, TaxCalc, TaxPay )
  var stat = TRUE;
  mPcRate.Clear();
  mPcRate.rec.TaxRate = Rate;
  if ( mPcRate.GetEQ() )
    if ( mPcRate.rec.Zero  AND
         ( ( Income  != 0.0 )  OR
           ( TaxCalc != 0.0 )  OR
           ( TaxPay  != 0.0 ) ) )
      mPcRate.rec.Zero = 0;
    end;
    // Общая сумма дохода.
    mPcRate.rec.CommonIncome  = mPcRate.rec.CommonIncome + Income;
    // Общая сумма рассчитанного налога.
    mPcRate.rec.CommonTaxCalc = mPcRate.rec.CommonTaxCalc + TaxCalc;
    // Общая сумма оплаченного налога.
    mPcRate.rec.CommonTaxPay  = mPcRate.rec.CommonTaxPay + TaxPay;
    stat = mPcRate.Update();
  else
    mPcRate.Clear();
    mPcRate.rec.TaxRate = Rate;
    if ( ( Income  != 0.0 )  OR
         ( TaxCalc != 0.0 )  OR
         ( TaxPay  != 0.0 ) )
      mPcRate.rec.Zero = 0;
    else
      mPcRate.rec.Zero = 1;
    end;
    // Общая сумма дохода.
    mPcRate.rec.CommonIncome  = Income;
    // Общая сумма рассчитанного налога.
    mPcRate.rec.CommonTaxCalc = TaxCalc;
    // Общая сумма оплаченного налога.
    mPcRate.rec.CommonTaxPay  = TaxPay;
    stat = mPcRate.Insert();
  end;
  return stat;
end;

/********************************************************************
*********************************************************************/
macro set_Code_Income( pCodeModule, pTaxRate )
  var vCode = 0;

  vKBK = "__________";

  if   ( pCodeModule == 2 )  // Вклады.
    if   ( pTaxRate == 13.00 )
      vCode = 4800;
      vKBK  = cKBK_13;
    elif ( pTaxRate == 30.00 )
      vCode = 3020;
      vKBK  = cKBK_30;
    elif ( pTaxRate == 35.00 )
      vCode = 3020;
      vKBK  = cKBK_35;
    end;
  elif ( pCodeModule == 5 )  // Валюто-обменные операции.
    if   ( pTaxRate == 13.00 )
      vCode = 2900;
      vKBK  = cKBK_13;
    elif ( pTaxRate == 30.00 )
      vCode = 2900;
      vKBK  = cKBK_30;
    elif ( pTaxRate == 35.00 )
      vCode = 2900;
      vKBK  = cKBK_35;
    end;
  elif ( pCodeModule == 6 )  // Ценные бумаги.
    if   ( pTaxRate == 13.00 )
      vCode = 2640;
      vKBK  = cKBK_13;
    elif ( pTaxRate == 30.00 )
      vCode = 2640;
      vKBK  = cKBK_30;
    elif ( pTaxRate == 35.00 )
      vCode = 2640;
      vKBK  = cKBK_35;
    end;
  end;

  return vCode;
end;

/********************************************************************
*********************************************************************/
macro Make_TMP()
  var stat = TRUE;
  var d, m, y;
  var vCodeModule = 2;
  var vTaxRate    = 0.0;
  var vIncomeRUR  = $0.0;

  while ( vCodeModule < 7 )

    ClearRecord( TAX_CARD );
    TAX_CARD.IdentClient = COMCLNT.CurRec.rec.CodClient;
    TAX_CARD.CodeModule  = vCodeModule;
    TAX_CARD.DateTaxPay  = BeginDate;
    stat = GetGE( TAX_CARD );
    if ( ( NOT stat )  OR
         ( TAX_CARD.IdentClient != COMCLNT.CurRec.rec.CodClient )  OR
         ( TAX_CARD.CodeModule  != vCodeModule )  OR
         ( TAX_CARD.DateTaxPay  < BeginDate ) )
      return FALSE;
    end;

    mPcTax.KeyNum = 0;
    mPcRate.KeyNum = 0;

    while ( stat  AND
            ( TAX_CARD.IdentClient == COMCLNT.CurRec.rec.CodClient )  AND
            ( TAX_CARD.CodeModule  == vCodeModule )  AND
            ( TAX_CARD.DateTaxPay  >= BeginDate )  AND
            ( TAX_CARD.DateTaxPay  <= EndDate ) )
      if ( ( TAX_CARD.CodeModule == 2 )  OR  ( TAX_CARD.CodeModule == 5 ) )
        SetRecordAddr( TAX_CARD_2, TAX_CARD, 0, 0, True );
        vTaxRate    = TAX_CARD_2.TaxRate;
        vIncomeRUR  = TAX_CARD_2.IncomeRUR;
      else
        SetRecordAddr( TAX_CARD_6, TAX_CARD );
        vTaxRate    = TAX_CARD_6.TaxRate;
        vIncomeRUR  = TAX_CARD_6.IncomeRUR;
      end;
      DateSplit( TAX_CARD.DateTaxPay, d, m, y );
      mPcTax.Clear();
      mPcTax.rec.TaxRate   = vTaxRate;
      mPcTax.rec.Mon       = m;
      mPcTax.rec.CodIncome = set_Code_Income( TAX_CARD.CodeModule, vTaxRate );
      Set_TaxRate( vTaxRate,
                   vIncomeRUR,
                   TAX_CARD.SummaTaxCalcRUR,
                   TAX_CARD.SummaTaxPayRUR );
      if ( mPcTax.GetEQ() )
        mPcTax.rec.CodeModule      = TAX_CARD.CodeModule;
        mPcTax.rec.IncomeRUR       = mPcTax.rec.IncomeRUR + vIncomeRUR;
        mPcTax.rec.SummaTaxCalcRUR = mPcTax.rec.SummaTaxCalcRUR + TAX_CARD.SummaTaxCalcRUR;
        mPcTax.rec.SummaTaxPayRUR  = mPcTax.rec.SummaTaxPayRUR + TAX_CARD.SummaTaxPayRUR;
        if ( NOT mPcTax.Update() )
          println( " *** UPDATE: ", vTaxRate, ", ", m );
        end;
      else
        mPcTax.Clear();
        mPcTax.rec.CodeModule      = TAX_CARD.CodeModule;
        mPcTax.rec.TaxRate         = vTaxRate;
        mPcTax.rec.Mon             = m;
        mPcTax.rec.CodIncome       = set_Code_Income( TAX_CARD.CodeModule,
                                                      vTaxRate );
        mPcTax.rec.IncomeRUR       = vIncomeRUR;
        mPcTax.rec.SummaTaxCalcRUR = TAX_CARD.SummaTaxCalcRUR;
        mPcTax.rec.SummaTaxPayRUR  = TAX_CARD.SummaTaxPayRUR;
        if ( Not ( mPcTax.Insert() ) )
          println( " *** INSERT: ", vTaxRate, ", ", m );
        end;
      end;

      // Подсчет общих сумм.

      stat = Next( TAX_CARD );
    end;

    if   ( vCodeModule == 2 )
      vCodeModule = 5;  // Валюто-обменные операции.
    elif ( vCodeModule == 5 )
      vCodeModule = 6;  // Ценные бумаги.
    else
      vCodeModule = 777;
    end;

  end;

  return TRUE;

end;

macro ПодготовитьОбщиеПеременные()

  // println( "ПодготовитьОбщиеПеременные(): ", COMCLNT.CurRec.rec.CodClient );

  mPcTax.ReWind();
  while ( mPcTax.Next() )
    mPcTax.Delete();
  end;

  mPcRate.ReWind();
  while ( mPcRate.Next() )
    mPcRate.Delete();
  end;

  // Очистка переменных общих сумм.


  // mPcTax.ReWind();
  // println( "ПодготовитьОбщиеПеременные(): ", mPcTax.Next() );

end;

macro Есть_Данные_По_Налогу_Для_Вкладчика()
  // println( "Есть_Данные_По_Налогу_Для_Вкладчика() ", COMCLNT.CurRec.rec.CodClient );
  Make_TMP();
  return TRUE;
end;

macro Удаление_Итоговых_Записей()
  // println( "Удаление_Итоговых_Записей()", COMCLNT.CurRec.rec.CodClient );
  return TRUE;
end;

macro ПодготовитьПеременныеПоКодуДохода( ИндексКодаДохода )
  // println( "ПодготовитьПеременныеПоКодуДохода()", COMCLNT.CurRec.rec.CodClient );
  // Make_TMP();
end;

macro Формирование_Итоговых_Данных_Для_Клиента()
  // println( "Формирование_Итоговых_Данных_Для_Клиента()", COMCLNT.CurRec.rec.CodClient );
end;

macro Формирование_Итоговой_Записи()
  // println( "Формирование_Итоговой_Записи()", COMCLNT.CurRec.rec.CodClient );
  return TRUE;
end;

macro Есть_Данные_По_Налогу_Для_Вкладчика_Все_Доходы()
  // println( "Есть_Данные_По_Налогу_Для_Вкладчика_Все_Доходы()", COMCLNT.CurRec.rec.CodClient );
  Make_TMP();
  return TRUE;
end;

macro Удаление_Итоговых_Записей_Все_Доходы()
  // println( "Удаление_Итоговых_Записей_Все_Доходы()", COMCLNT.CurRec.rec.CodClient );
  return TRUE;
end;

macro Выбрать_Данные_По_Очередному_клиенту( NumCycle )
/*
  Функция организует цикл по вкладчикам и заполнение данных
  по каждому вкладчику
*/
  var stat = ERR_OK; /* Возврат- одна из описанных перед функцией констант */
  var mon;
  var st;
  var i, ИндексКодаДохода;
  var OnlyCurrentBranch;

  if ( NumCycle == 1 )
    COMCLNT.ClearFilterKey();
    COMCLNT.SetFilter_CodClient( InputPanel_IdentClient,
                                 InputPanel_IdentClient );
    if ( ( InputPanel_IdentClient == 0 ) // Клиент не задан
     AND ( FNcash != 0 ) )               // Не опер.часть
      OnlyCurrentBranch = true;   // Выбираем только клиентов текущего филиала
    elif ( ( CommonBase == 0 )           // Не общая база клиентов
       AND ( FNcash != 0 ) )                // Не опер.часть
      OnlyCurrentBranch = true;   // Выбираем только клиентов текущего филиала
    else
      OnlyCurrentBranch = false;  // Выбираем клиентов всех филиалов
    end;
    if ( OnlyCurrentBranch )
      COMCLNT.SetFilter_FNcash( FNcash, FNcash );
    end;
    COMCLNT.SetFilter( OnlyCurrentBranch );
    st = COMCLNT.First();
  else
    st = COMCLNT.Next(); /* Продолжаем работу- следующий клиент */
  end;
  if ( ( NOT st )  OR
       ( OnlyCurrentBranch  AND  ( COMCLNT.CurRec.rec.FNcash != FNcash ) ) )
    stat = ERR_STOP;
    if ( NumCycle == 1 )
      MsgBox ( "Список вкладчиков по филиалу ",FNcash," пустой. Работа прекращена" );
    end;
  else
    if ( TheOnlySole  AND
         ( COMCLNT.CurRec.rec.CodClient != InputPanel_IdentClient ) )
      if ( NumCycle == 1 )
        MsgBox ( "Не найден вкладчик с заданным кодом ", InputPanel_IdentClient );
        stat = ERR_REPEAT;
      else
        stat = ERR_STOP;
      end;
    else
      /* Обработка очередного вкладчика */
      if ( true )
        stat == ERR_OK;
        Message ( " Обрабатывается клиент ", COMCLNT.CurRec.rec.CodClient );
        if ( st )
          ИндексКодаДохода = 0;
          ПодготовитьОбщиеПеременные();
          if ( CodeModule != 0 ) /* по определенному виду доходов */
            if ( not Есть_Данные_По_Налогу_Для_Вкладчика() )
              if( TheOnlySole )
                msgbox( "Для вкладчика с кодом ", COMCLNT.CurRec.rec.CodClient, " нет записей в нал.карточке за ", InputPanel_Year_InAll, " год" );
                stat = ERR_REPEAT;
              else
                stat = ERR_SKIP; /* Пропустить вкладчика */
              end;
            else
              if(st)
                st = Удаление_Итоговых_Записей ();
              end;
              if(st)
                ПодготовитьПеременныеПоКодуДохода( ИндексКодаДохода );
                /*
                st = ExecMacro2( ФункцииПоКодамДохода(CodeModule), ИндексКодаДохода );
                if( st )
                  Формирование_Итоговых_Данных_Для_Клиента ();
                  st = Формирование_Итоговой_Записи();
                end;
                */
              end;
            end;
          else /* по всем видам доходов */
            i = 0;
            if( not Есть_Данные_По_Налогу_Для_Вкладчика_Все_Доходы() )
              if( TheOnlySole )
                msgbox( "Для вкладчика с кодом ", COMCLNT.CurRec.rec.CodClient, " нет записей в нал.карточке за ", InputPanel_Year_InAll, " год" );
                stat = ERR_REPEAT;
              else
                stat = ERR_SKIP; /* Пропустить вкладчика */
              end;
            else
              if(st)
                st = Удаление_Итоговых_Записей_Все_Доходы ();
              end;
              /*
              while( ( st == true ) and ( i < ASize(ФункцииПоКодамДохода) ) )
                if( ФункцииПоКодамДохода(i) != null )
                  CodeModule = i;
                  if(st)
                    ПодготовитьПеременныеПоКодуДохода( ИндексКодаДохода );
                    st = ExecMacro2( ФункцииПоКодамДохода(i), ИндексКодаДохода );
                    ИндексКодаДохода = ИндексКодаДохода + 1;
                    if( st )
                      st = Формирование_Итоговой_Записи();
                    end;
                  end;
                  CodeModule = 0;
                end;
                i = i + 1;
              end;
              */
              Формирование_Итоговых_Данных_Для_Клиента ();
            end;
          end;
        end;
        if (NOT st)
          [ ];
          [ Возникла ошибка при обработке вкладчика с кодом ###############]
           (COMCLNT.CurRec.rec.CodClient);
          [ Вкладчик не обработан];
          stat = ERR_SKIP;
        end;
      end;
    end;
  end;

  // Make_TMP();

  return stat;
end;

/***********************************************************
 Ввод исходных данных через панель
 (вкладчика и год, за который идет выпуск
*/
macro Ввод_исходных_данных (PHead)
  var stat = True;
  var st = True;

  if( CodeModule == -1 )
    // st   = ВыборПодсистемы();
    stat = st;
  end;
  while (st)
    stat = Параметры_Выпуска_Налоговой_Карточки (PHead,True,True,
                                                 InputPanel_IdentClient,
                                                 InputPanel_Year_InAll);
    if (stat)
      if ( InputPanel_IdentClient == 0 )
        TheOnlySole = False;
        stat = GetTrue( True,
                        "Выпускать налоговые карточки по всем вкладчикам?" );
        if (stat)
          st = False; /* Согласились- выйдем из цикла, нет- повторим */
        end;
      else
        TheOnlySole = True;
        if (COMCLNT.GetRecord(InputPanel_IdentClient))
          st = False; /* Выбрали вкадчика */
        else
          MsgBox ("Не найден вкладчик с заданным кодом"); /* Повторим */
        end;
      end;
      if (NOT st) /* Собрались выходить, а год? */
        if (InputPanel_Year_InAll < 1900)
          st = True; /* Рано выходим- не введен год */
          MsgBox ("Введите год, за который будет выпущена карточка (например, 1999) ");
        else
          BeginDate = Date ( 1, 1,InputPanel_Year_InAll);
          EndDate   = Date (31,12,InputPanel_Year_InAll);
        end;
      end;
    else
      st = False; /* Выйти из цикла ввода данных */
    end;
  end;
  return stat;
end;
