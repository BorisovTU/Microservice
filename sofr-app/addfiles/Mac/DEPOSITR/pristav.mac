/*
  RS-Retail
  Сбербанк

  Петров ВВ, Вологодское ОСБ № 8638
  Поиск счетов клиентов по всему отделению

  Мелкие доработки- для оформления: Ромодин А.В.
-----------------------------------------------------
  Модуль № 5066 (Обслуживание физ.лиц)
  Меню: Справочники/Клиенты/Список счетов
-----------------------------------------------------
  Структура исходного файла:

Фамилия:Имя:Отчество:Год рождения
Фамилия:Имя:Отчество:Год рождения
...

Например:

Петров:Валерий:Владимирович:1973

*/
import deprintr;
import ptlib;

var cRet = StrFor(13)+StrFor(10);

/* ====================================================== 
   Введите свои настройки
*/ 
var PatternFile = "..//pristav//*.txt";             /* Маска исходного файла */
var Title_Bank1 = "ВОЛОГОДСКОЕ  ОТДЕЛЕНИЕ №8638";
var Title_Bank2 = "      (СЕВЕРНЫЙ БАНК)";
var Title_addr1 = "    160000, г.Вологда";
var Title_addr2 = "  ул.Предтеченская д.33";
var Title_Date  = "№_____ от \"___\" ___________ 2002 г.";

var Total_NoAcc   = "счетов в филиалах отделения №8638 не имеется.";
var Total_Office1 = "Заместитель управляющего-";
var Total_Office2 = "Директор ОПЕРУ";
var Total_Office3 = "Вологодского отделения №8638                    /О.А. Морева/";

var cBotTit = "───────────────────────────────────────────────────────────────────────────────────────────"+cRet+
              "   160035, г.Вологда, ул.Предтеченская, 33 Телефон: (8172) 72-11-61 Факс: (8172) 72-11-61  ";

/* ====================================================== */
var {curdate};
var nAcc=0, nSum=$0.0L, nRest=$0.0L;
var {CNum};
var TxtDir=GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true);

array aBad;
var Client = TArray;

var TmpFile     = String(TxtDir + "\\tmp.txt");     /* Временный файл */
file InputFile  () txt;              /* Исходный файл с клиентами */
file depositr ( depositr ) key 4;    /* Файл счетов вкладчиков    */
var  FCL = TClientList;          /* Файл вкладчиков           */

private macro CheckArray( shNameCurr )
var i = 0;
  while( i < Client.size )
    if(Client[i][0] == shNameCurr)
      return i;
    end;
    i = i + 1;
  end;
  return -1;
end;

Macro SeekAcnt()
 var Stat,Pos;
 array aAdr;

 depositr.CodClient=FCL.CurRec.rec.CodClient;
 depositr.IsCur=0;
 depositr.Number=0;
 rewind(depositr);
 Stat=GetGE(depositr);
 while ((Stat)and(depositr.CodClient==FCL.CurRec.rec.CodClient))
  nRest = depositr.Sum_Rest;
  if (nRest!=0)
   nAcc = nAcc + 1;

   Pos = CheckArray(GetNumCurrency(depositr.Code_Currency,"Краткое название валюты"));
   if( Pos == -1 )
     Client[Client.size] = TArray;
     Client[Client.size - 1][0] = GetNumCurrency(depositr.Code_Currency,"Краткое название валюты");
     Client[Client.size - 1][1] = nRest;
   else
     Client[Pos][1] = Client[Pos][1] + nRest;
   end;

   nSum = nSum + nRest;
   StrSplit(FCL.CurRec.rec.Address,aAdr,25,25,2);
   [│#########################│#########################│#######################│#############│](trim(FCL.CurRec.rec.Name1) + " " + Trim(FCL.CurRec.rec.Name2), aAdr(0), Slash(Acc_Form(depositr.account)), nRest);
   [│############## ##########│#########################│##########     ########│          ###│](Trim(FCL.CurRec.rec.Name3),  String(FCL.CurRec.rec.Born), aAdr(1), String(depositr.Open_Date),GetNumBranch(depositr.FNCash,"Отделение/Филиал"), GetNumCurrency(depositr.Code_Currency,"Краткое название валюты") );
  end;
  Stat=((Next(depositr))and(depositr.CodClient==FCL.CurRec.rec.CodClient));
 end;
end;

Macro SeekClnt(nMode)
 var nGod,Stat,cFam;
 cFam=Trim(InputFile(0));
 if (cFam!="")
  if (nMode==1)
   cFam=StrUpr(cFam);
  end;
  /* Цикл по клиентам */
  FCL.ClearFilterKey();
  FCL.SetFilter_Name1 (cFam, cFam);
  FCL.SetFilter_FNcash (0, 9999); /* По всем филиалам */
  FCL.SetFilter();
  Stat = FCL.First();
  while ( (Stat) and
          (StrUpr(Trim(FCL.CurRec.rec.Name1))==StrUpr(cFam)) )

   if (   (StrUpr(Trim(FCL.CurRec.rec.Name1))==StrUpr(Trim(InputFile(0)))) and
          (StrUpr(Trim(FCL.CurRec.rec.Name2))==StrUpr(Trim(InputFile(1)))) and
          (StrUpr(Trim(FCL.CurRec.rec.Name3))==StrUpr(Trim(InputFile(2))))
      )
    DateSplit(FCL.CurRec.rec.Born,Null,Null,nGod);
    if ((nGod==Int(Trim(InputFile(3))))or(Trim(InputFile(3))==""))
     SeekAcnt();
    end;
   end;
   Stat = FCL.Next();
  end;
 end;
end;

Macro PrintHeader()
[

 филиал акционерного коммерческого                ______________________________
     СБЕРЕГАТЕЛЬНОГО  БАНКА                       ______________________________
      РОССИЙСКОЙ ФЕДЕРАЦИИ                        Подразделение судебных
 (открытого акционерного общества)                приставов №___________________

   ############################################## Судебному приставу-исполнителю
   ############################################## ______________________________
   ##############################################
   ##############################################
    #############################################
На #############################################


    Направляем сведения по денежным вкладам согласно Ваших запросов №№
    ____________________________________________________________________
    ____________________________________________________________________

┌─────────────────────────┬─────────────────────────┬───────────────────────┬─────────────┐
│Фамилия Имя              │Адрес                    │Номер счета            │    Остаток  │
│Отчество    Дата рождения│                         │Дата открытия    Филиал│    Валюта   │
│─────────────────────────┼─────────────────────────┼───────────────────────┼─────────────┤
│           1             │             2           │            3          │      4      │
├─────────────────────────┼─────────────────────────┼───────────────────────┼─────────────┤
](Title_Bank1,Title_Bank2,Title_addr1,Title_addr2,Title_Date,Title_Date);
end;

Macro PrintFooting(aBad)
 var ii = 0;
 [└─────────────────────────┴─────────────────────────┴───────────────────────┴─────────────┘];
 if (aSize(aBad) > 0)

[

На имя граждан

];
  while (ii < aSize(aBad))
   [#](aBad(ii));
   ii = ii + 1;
  end;
[
#](Total_NoAcc);
 end;
[



#
#
#
](Total_Office1,Total_Office2,Total_Office3);
end;

/*Точка входа*/

 var NameInputFile="";
 var nProgress=0,nColStr=0;
 var cTopTit;
 record dlg(client) dialog;
 file InputFileTmp () txt write;
 
  array forMenu;
  var variant,stat,i = 0;
  forMenu(0) = "Загрузка записей из файла";
  forMenu(1) = "Ввод клиента вручную";
  variant = Menu(forMenu,null,"Выберите режим");
  if(variant == -2)
    stat = false;
  end;
  if(variant == 0)
    stat = SelectFile( NameInputFile, PatternFile, "Выберите исходный файл" ); 
    if( stat )
      stat = Open( InputFile,  NameInputFile );
    end;
  end;
  if( variant == 1 )
    NameInputFile = TmpFile;
    stat = Open( InputFileTmp,  NameInputFile );
    if( stat )
      stat = RunDialog( dlg );
      if ( stat )
        InputFileTmp.str = string(dlg.LastName + ":" + dlg.FirstName + ":" + dlg.SecondName + ":" + dlg.Year);
        stat = Insert( InputFileTmp );
        if( stat )
	  Close( InputFileTmp );
          stat = Open( InputFile,  NameInputFile );
        end;
      end;
    end;
  end;

  SetDelim(":"); /*Разделитель полей*/
 /* if ( SelectFile( NameInputFile, PatternFile, "Выберите исходный файл" ) )
    if ( Open( InputFile,  NameInputFile ) )*/

    if( stat )
      while (next(InputFile))   /* Определяем количество строк */
       nColStr = nColStr + 1;
      end;
      rewind(InputFile);
      initProgress(nColStr, "Обработка исходного файла ");
      PrintHeader();
      while (next(InputFile))
       nAcc = 0;
       nSum = $0.0L;
       SeekClnt(0);    /*SeekClnt(1);*/
       if (nAcc > 1)
        [│ИТОГО ПО ######################################### #### СЧЕТОВ НА СУММУ:                 │]
	(trim(InputFile(0)) + " " + Trim(InputFile(1)) + " " + Trim(InputFile(2)), nAcc);
        while( i < Client.size )
	  [│ 								 	        #############│]
	  (( Client[i][0] ):r);
	  [│ 								 	        #############│]
	  ( Client[i][1] );
          i = i + 1;
	end;
	Client.size = 0;
        i = 0;
       elif (nAcc == 0)
        aBad(aSize(aBad)) = trim(InputFile(0))+" "+Trim(InputFile(1))+" "+Trim(InputFile(2)) ;
       end;
       UseProgress(nProgress);
       nProgress=nProgress+1;
      end;
      remProgress();
      PrintFooting(aBad);
      cTopTit = "┌─────────────────────────┬─────────────────────────┬───────────────────────┬─────────────┐"+cRet+
                "│           1             │             2           │            3          │      4      │"+cRet+
                "├─────────────────────────┼─────────────────────────┼───────────────────────┼─────────────┤";

      InPages(SetOutput(TxtDir + "\\prist." + String( {CNum} )),
              72,
              2,
              cTopTit,
              cBotTit,
              54,True);
    end;
/*    end;
  end;*/
  if( variant == 1)
    Close( InputFileTmp );
    stat = DelFile( NameInputFile );
  end;

end;
