
/* Завершение года. Проверочный отчет для обработки договоров */

import deprintr, Календарь;

file Curr( "currency.dbt" ) key 0;
file DepType( "sb_dtyp.dbt" ) key 2;
file Dep( "depositr.dbt" ) key 1;

const
  OK_RES   =  0,  /* продолжать проводку */
  SKIP_RES =  1,  /* пропустить выполнение предопределенного модуля */
  END_RES  =  2,  /* завершить проводку */
  ERR_RES  = -1;  /* аваpийно завершить проводку */

var
  {curdate},
  IsCur = NumFlagCur,
  Branch = NumFNCash,
  SubHeadDisplayed,
  TADisplayed,
  TotRest = $0,
  TotRestTA = $0;

macro LastWorkdayBefore( D )

  while ( IsHoliday( D ) )
    D = D - 1;
  end;
  return D;
end;

macro OutHead

  [ ];
  [           Следующие договоры будут пролонгированы ];
  [ ];
end;

macro OutSubHead

  [-------------------------------------------------------------];
  [|                         |Дата исте-|                      |];
  [|        С ч е т          |чения сро-|       Остаток        |];
  [|                         |ка дог-ра |                      |];
  [-------------------------------------------------------------];
end;

macro OutLine

  [ ######################### ########## ###################### ]
  (
    Acc_Form(Dep.Account),
    Dep.End_DateDep,
    Dep.Sum_Rest
  );
  TotRest = TotRest + Dep.Sum_Rest;
  TotRestTA = TotRestTA + Dep.Sum_Rest;
end;

macro OutTotalsTA

  [-------------------------------------------------------------];
  [ Итого по виду вклада:                ###################### ]
  (
    TotRestTA
  );
  TotRestTA = $0;
end;

macro OutTotals

  [-------------------------------------------------------------];
  [ Итого:                               ###################### ]
  (
    TotRest
  );
  TotRest = $0;
end;

macro TreatDepType

  var
    RecFound;

  TADisplayed = false;

  ClearRecord( Dep );
  Dep.IsCur = IsCur;
  Dep.FNCash = Branch;
  Dep.Type_Account = DepType.Kind;
  Dep.Code_Currency = Curr.Code_Currency;
  RecFound = GetGE( Dep );
  while ( RecFound
    and ( Dep.IsCur == IsCur )
    and ( Dep.FNCash == Branch )
    and ( Dep.Type_Account == DepType.Kind )
    and ( Dep.Code_Currency == Curr.Code_Currency ) )
    if ( ( Dep.End_DateDep != Date( 0, 0, 0 ) )
      and ( LastWorkdayBefore( Dep.End_DateDep ) <= {curdate} - 1 )
      and ( not Index( Dep.UserTypeAccount, "Т" ) )
      and ( not Index( Dep.UserTypeAccount, "Я" ) ) )
      if ( not SubHeadDisplayed )
        if ( IsCur )
          PrintLn;
          PrintLn( Curr.Name_Currency );
          PrintLn( MkStr( "=", StrLen( Curr.Name_Currency ) ) );
          PrintLn;
        end;
        OutSubHead;
        SubHeadDisplayed = true;
      end;
      if ( not TADisplayed )
        PrintLn;
        PrintLn( DepType.Kind );
        PrintLn( MkStr( "-", StrLen( DepType.Kind ) ) );
        TADisplayed = true;
      end;
      OutLine;
    end;
    RecFound = Next( Dep );
  end;
  if ( TADisplayed )
    OutTotalsTA;
  end;
end;

macro TreatAllDepTypes

  var
    RecFound;

  SubHeadDisplayed = false;

  ClearRecord( DepType );
  DepType.FlagCur = IsCur;
  RecFound = GetGE( DepType );
  while ( RecFound
    and ( DepType.FlagCur == IsCur ) )
    if ( DepType.ProlOnEoy != StrFor( 0 ) )
      TreatDepType;
    end;
    RecFound = Next( DepType );
  end;
  if ( SubHeadDisplayed )
    OutTotals;
  end;
end;

  var
    RecFound;

  OutHead;
  if ( not IsCur )
    Curr.Code_Currency = 0;
    if ( GetEQ( Curr ) )
      TreatAllDepTypes;
    end;
  else
    Curr.Code_Currency = 1;
    RecFound = GetGE( Curr );
    while( RecFound )
      TreatAllDepTypes;
      RecFound = Next( Curr );
    end;
  end;
  ALG_RESULT = OK_RES;
end;

