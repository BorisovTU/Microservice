/*
$Name:           NUM_REFM.mac
$Module:         RETAIL
$Description:    Переоформление договора для "Номерных"
*/

/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Переоформление договора для "Номерных"                                  *
*                                                                          *
\**************************************************************************/

import deprintr, alg_vars;

file depositr     ( depositr ) key 8 write;
file depstat      ( sb_depst ) key 0 write;
file CDTpcestim   ( pcestim )  key 0;

const UPDATE_ALL  = 1;
const UPDATE_DEP  = 2;
const UPDATE_DOC  = 4;

const
  BIT_FLAG_HIDDEN = 17,
  BIT_FLAG_NOSTATTURNS = 18,
  BIT_FLAG_OPENCLOSE = 19,

  D_IN = 1,         /* Приход */
  D_OUT = 2,        /* Расход */
  D_GET = 3,        /* Списание */
  D_PUT = 5;        /* Зачисление */

const OP_GETCON =  62;  /* Списание простое */
const OP_REFNUM = 508;  /* Операция переоформления номерных вкладов */

const Binsert = 2;

const
  curRUB = 0,  /* Рублевый режим */
  curISO = 1;  /* Валютный режим */

const n_DepKind = 4;  /* Количество переоформляемых видов вкладов */

array DepKindOld;  /* Старые виды вкладов - "Особые номерные" */
array DepKindNew;  /* Новые виды вкладов - "Особые" */

var Mode = GetCurrentMode();

var oldType_Account;  /* Старый вид вклада */
var newType_Account;  /* Новый вид вклада */

/* var Флаг_замены_с_книжки; */

var FlagFindKind = FALSE;

var i;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro NewDepDoc ( Doc )

  ClearRecord( Doc );
  Doc.Referenc         = ALG_DEPOSITOR.Referenc;
  Doc.IsCur            = ALG_DEPOSITOR.IsCur;
  Doc.FNCash           = ALG_DEPOSITOR.FNCash;
  Doc.Account          = ALG_DEPOSITOR.Account;
  Doc.Oper             = {oper};
  Doc.Type_Account     = ALG_DEPOSITOR.Type_Account;
  Doc.Code_Currency    = ALG_DEPOSITOR.Code_Currency;
  Doc.CodClient        = ALG_DEPOSITOR.CodClient;
  Doc.YesSbook         = "X";
  Doc.Date_Document    = {curdate};
  Doc.DepDate_Document = {curdate};

end;

macro NewFakedDepDoc( sbdepdoc )

  NewDepDoc( sbdepdoc );
  sbdepdoc.TypeComplexOper  = OP_REFNUM;
  sbdepdoc.ApplType         = 0;
  sbdepdoc.DepDate_Document = {curdate};
end;

/**************************************************************************\
|           Поиск последнего документа по накопленным процентам            |
\**************************************************************************/
macro GetLastPcEstim

  var stat;
  var find_last = FALSE;

  CDTpcestim.Referenc   = ALG_DEPOSITOR.Referenc;
  CDTpcestim.DateOperat = Date(31,12,9999);
  CDTpcestim.NumDayDoc  = 32000;
  stat = GetLE(CDTpcestim);
  while (stat)
    if (CDTpcestim.Referenc != ALG_DEPOSITOR.Referenc)
      stat = FALSE;
    else
      if (CDTpcestim.Action < 2)
        find_last = TRUE;
        stat = FALSE;
      else
        stat = Prev(CDTpcestim);
      end;
    end;
  end;

  return find_last;

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro T_UpdateRecsForReformDepType

  record sbdepdoc      ( sbdepdoc );


  var stat = true;
  var appl_key;
  var str_tmp;
  var Rest  = ALG_DEPOSITOR.Sum_Rest;
  var LIMIT = ALG_DEPOSITOR.Limit;

  /* Обновление счета вкладчика */
  if ( stat )
    GetPos( depositr );
    stat = GetDirect( depositr );
  end;

  if ( stat )
    /* Смена вида вклада и сброс SpecialAccess и GiveBook */
    ALG_DEPOSITOR.NumSession       = 0;
    ALG_DEPOSITOR.Action           = 1;
    /* oldType_Account                        = ALG_DEPOSITOR.Type_Account; */
    ALG_DEPOSITOR.Type_Account     = newType_Account;
    ALG_DEPOSITOR.SpecialAccess    = StrFor(0);
    ALG_DEPOSITOR.Limit            = $0;

    /*
    if ( Флаг_замены_с_книжки )
/*      ALG_DEPOSITOR.NCurLine         = 0;  */
      ALG_DEPOSITOR.GiveBook = StrFor(0);
    end; */
    stat = Update( depositr );
  end;


  if ( stat )
    stat = UpdateAlgRecords( UPDATE_DEP, true, false );
  end;

  /* Формирование фиктивного документа по вкладу */

  if ( stat )
    NewFakedDepDoc( sbdepdoc );
    sbdepdoc.KindOp           = D_GET;
    sbdepdoc.OutSum           = Rest;
    sbdepdoc.Type_Account     = oldType_Account;
    sbdepdoc.TypeOper         = 62;

    SetBitFlag( sbdepdoc.Flags, BIT_FLAG_HIDDEN, 1 );
    SetBitFlag( sbdepdoc.Flags, BIT_FLAG_NOSTATTURNS, 1 );
    SetBitFlag( sbdepdoc.Flags, BIT_FLAG_OPENCLOSE, 1 );

    stat = InsertDocToGDDList( sbdepdoc );
  end;

  if ( stat )
    NewFakedDepDoc( sbdepdoc );
    sbdepdoc.KindOp           = D_PUT;
    sbdepdoc.InSum            = Rest;
    sbdepdoc.TypeOper         = 71;

    SetBitFlag( sbdepdoc.Flags, BIT_FLAG_HIDDEN, 1 );
    SetBitFlag( sbdepdoc.Flags, BIT_FLAG_NOSTATTURNS, 1 );
    SetBitFlag( sbdepdoc.Flags, BIT_FLAG_OPENCLOSE, 1 );

    stat = InsertDocToGDDList( sbdepdoc );
  end;

  if ( stat )
    NewFakedDepDoc( sbdepdoc );

    sbdepdoc.KindOp           = D_GET;
    sbdepdoc.IsControl        = StrFor( 1 ); /* Признак главного документа */
    sbdepdoc.OutSum           = $0;
    sbdepdoc.TypeOper         = 62;
    if ( StrFor( GetProgramID() ) != "П" )
      sbdepdoc.Ground         = "Переоформление договора";
    else
      if ( ALG_SBDEPDOC.Ground != "" )
        sbdepdoc.Ground       = ALG_SBDEPDOC.Ground;
      else
        sbdepdoc.Ground       = "Переоформление договора";
      end;
    end;

    stat = InsertDocToGDDList( sbdepdoc );
  end;

  if ( stat )
    if ( CarryGDDList( ) != 0 )
      stat = false;
    end;
  end;

  if (stat)
    ALG_DEPOSITOR.Limit = LIMIT;
    stat = UpdateAlgRecords (UPDATE_DEP, true, true);
  end;

  if ( not stat )
    AbortTrn( );
  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

macro Num_Reform()

  ALG_RESULT = OK_RES;

  /* Поиск счета */
  if ( ALG_RESULT == OK_RES )
    ClearRecord( depositr );
    depositr.Referenc = ALG_DEPOSITOR.Referenc;
    if ( not GetEQ( depositr ) )
      if ( StrFor( GetProgramID() ) != "П" )
        MsgBox( "Не найден счет с референсом " + String( ALG_DEPOSITOR.Referenc ) );
      end;
      ALG_RESULT = ERR_RES;
    end;
  end;

  /* Заполнение массивов соответствия видов вкладов */
  /* Старые виды вкладов - "Особые номерные" */
  DepKindOld(0) = "Особый номер";
  DepKindOld(1) = "Особ.н.1г_1м";
  DepKindOld(2) = "Особ.ном.2 г";
  DepKindOld(3) = "Особ.ном. 2г";  /* KIN -добавлено для вал на 2 года*/
  /* Новые виды вкладов - "Особые" */
  DepKindNew(0) = "Особый на 3м";
  DepKindNew(1) = "Особый 1г_1м";
  DepKindNew(2) = "Особый на 2г";
  DepKindNew(3) = "Особый на 2г";  /* KIN -добавлено для вал на 2 года*/

  /* Если новые виды вклада, то возвращаться и ничего не делать */
  i = 0;
  while ( i < n_DepKind )
    if ( depositr.Type_Account == DepKindNew(i) )
      MsgBox( "Вклад уже переоформлен. Операция отменяется" );
      Exit(0);
    end;
    i = i + 1;
  end;

  /* 1. Определить соответствие вида нового вклада старому */
  i = 0;
  while ( i < n_DepKind )
    if ( depositr.Type_Account == DepKindOld(i) )
/*      if ( ( ( i == 0  )  AND  ( depositr.IsCur == curISO ) )  OR
           ( ( i == 1  )  AND  ( depositr.IsCur == curRUB ) ) )
        MsgBox( "Вклад должен быть закрыт!  Операция отменяется" );
        Exit(0);
      else */
        oldType_Account = depositr.Type_Account;
        newType_Account = DepKindNew(i);
        FlagFindKind = TRUE;
/*      end; */
    end;
    i = i + 1;
  end;
  if ( NOT FlagFindKind )
    MsgBox( "Вклад не \"Номерной\". Операция отменяется" );
    ALG_RESULT = ERR_RES;
    Exit(0);
  end;

  /* 2. Запросить "Заменить с\кн" - ТОЛЬКО не для Последконтроля!!! */
  /*
  if ( StrFor( GetProgramID() ) != "П" )
    GetTrue( Флаг_замены_с_книжки, "Заменить сб/книжку ?" );
  else
    Флаг_замены_с_книжки = FALSE;
  end; */

  /* Транзакция переоформления вида вклада */
  if ( ALG_RESULT == OK_RES )
    if ( ProcessTrn( 1, "T_UpdateRecsForReformDepType",
         depositr, depstat, CDTpcestim ) )
      ALG_RESULT = SKIP_RES;
    else
      if ( StrFor( GetProgramID() ) != "П" )
        MsgBox( "Ошибка при переоформлении договора" );
      end;
      ALG_RESULT = ERR_RES;
      InterDesk_EndDocBunch( );
    end;
  end;

  if ( ALG_RESULT == SKIP_RES )
    return TRUE;
  else
    return FALSE;
  end;

end;

Num_Reform();
end;