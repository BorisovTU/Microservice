/*
               **************************
               *  R-Style SoftWare Lab. *  
               **************************  

    RS-Retail 2.0
    Сбербанк
 Вклад "Срочный пенсионный на 1 год и 1 месяц"

 Определение процентной ставки по альтернативным условиям
 (при нарушении) для Срочного пенсионного на 1 год и 1 месяц
------------------------------------------------------------
 Цель:
  обеспечить взятие различных ставок при закрытии договора
  в зависимости от срока, который пролежал вклад
  от начала договора либо от даты конца предыдущего
  договора:
- до 200 дней включительно- по До востребования
- больше 200 дней- берется 1/2 (0.5) ставки, определенной
  по основным условиям для вида вклада

  В случае, если операция отлична от закрытия счета,
  берется ставка по условиям "До востребования"
------------------------------------------------------------

  Ромодин Александр Васильевич
  07.04.2000
*/
Import DeprIntr,srpnrt_0;
/*
  Структуры
*/
private record PC_ALG   (pc_alg);     /* Параметры расчета процентов */
private record DEPOSITR_SRPNRT_1 (depositr);   /* Текущий счет вкладчика      */
private record SBDEPDOC (sbdepdoc);   /* Текущий документ вкладчика  */

private file PC_RATE (pc_rate) key 0; /* Справочник ставок           */
/*
  Количество дней, которые должен отлежать вклад, чтобы получить право
  закрываться по повышенной ставке
*/
private const NumLimitDay = 200;
/*
  Операции закрытия (добавьте свою следующим элементом массива)
*/
private Array OperClose;
OperClose (0) = 10; /* Закрытие наличными      */
OperClose (1) = 81; /* Закрытие безналичными   */ 
OperClose (2) = 96; /* Закрытие переводом      */
private const NumArr = Asize(OperClose);

/* var MacroError = 0; */

/**********************************************************/

macro BOOL_Операция_Закрытия (NumOper)
/*
  Возвращает True, если имеем дело с операцией закрытия
*/
  var stat = False;
  var i = 0;

  while ((NOT stat) AND (i < NumArr))
    if (NumOper == OperClose(i))
      stat = True;
    end;
    i = i + 1;
  end;
  return stat;
end;
/**********************************************************/

macro BOOL_Счет_закрыт
/*
  Возвращает True, если счет закрыт
*/
  var stat;
  stat = (DEPOSITR_SRPNRT_1.Open_Close == "З");
  return stat;
end;
/**********************************************************/

macro NeedSpecific (BeginDate,NumOper)
/*
  Возвращает True в случае, если ставка определяется
  по специфическим условиям
*/
  var stat;
  var ClosedAcc = False;
  var StartDate;
  var LimitDate;
  var TestDate = Date(31,12,2999);
  ClosedAcc = BOOL_Счет_закрыт ();
  if (NOT ClosedAcc)
    stat = BOOL_Операция_Закрытия (NumOper);
    if ((NOT stat)
     AND (DEPOSITR_SRPNRT_1.UseAlternate != 0)            /* Возможно, если была выдача наследства */
     AND (DEPOSITR_SRPNRT_1.End_DateDep  != Date(0,0,0)))
      stat = True;
    end;
  else
    stat = True;
  end;
  if (stat)
    if (DEPOSITR_SRPNRT_1.End_DateDep == Date(0,0,0))
      stat = False; /* Договор был завершен- расчет по "До востребования" */
    end;
  end;
  if (stat)
    if (DEPOSITR_SRPNRT_1.UseAlternate != 0)
      TestDate = GetFirstWillDate (Date(31,12,2999),DEPOSITR_SRPNRT_1,SBDEPDOC); // Первая выдача наследства и есть как бы закрытие счета 
      if (TestDate < BeginDate)  // Следующие после выдачи наследства периоды
        stat = False;  // Считаем по "До востребования"
      end;
    end;
  end;
  if (stat)
    if (TestDate == Date(31,12,2999))
      if (ClosedAcc)
        TestDate = DEPOSITR_SRPNRT_1.Close_Date;
      else
        TestDate = SBDEPDOC.DepDate_Document;
      end;
    end;
  end;
  if (stat)
    if (DEPOSITR_SRPNRT_1.Prol_DateDep != Date(0,0,0))
      StartDate = DEPOSITR_SRPNRT_1.Prol_DateDep - 1;
    else
      StartDate = DEPOSITR_SRPNRT_1.Start_DateDep;
    end;
    LimitDate = StartDate + NumLimitDay;
    if (TestDate <= LimitDate)
      stat = False; /* Ставка- по "До востребования" */
    end;
  end;
  return stat;
end;
/**********************************************************/

macro GetAltRate (alg,dr,BeginDate,EndDate,doc)
  var ResRate:doublel = 0.0l;
  var Rate;
  var RateDate; /* Ставка фиксированная, действует с начала договора */

  setbuff (PC_ALG,alg); 
  setbuff (DEPOSITR_SRPNRT_1,dr); 
  setbuff (SBDEPDOC,doc); 

  if (NeedSpecific(BeginDate,SBDEPDOC.TypeComplexOper))
/*
    Вычисление ставки как части от ставки по основным условиям
    (ради чего писался макрос)
*/
    if (DEPOSITR_SRPNRT_1.Prol_DateDep != Date(0,0,0))
      RateDate = DEPOSITR_SRPNRT_1.Prol_DateDep;
    else
      RateDate = DEPOSITR_SRPNRT_1.Start_DateDep;
    end;
    ClearRecord (PC_RATE);
    Rate = PercRateAL (DEPOSITR_SRPNRT_1.Referenc,2001,RateDate);
    if (Rate > 0)
      ResRate = doubleL(Rate/2);
      MacroError = 0;    /* Ставка определена */
    else
      MacroError = 3649; /* Ошибка при определении процентной ставки */
    end;
  else /* Ставка- по условиям "До востребования" (стандартным образом) */
    MacroError = -999;   /* Ставка будет определена из условий в настройке параметров процентов */
  end;
  return ResRate;
end;
