/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады (Филиал и Последконтроль)                                        *
*                                                                          *
*  Отчет "Прогноз причисленных процентов" (форма № 7.64)                   *
*                                                                          *
*  Лебедев С.В.                                                            *
*  14.05.2001                                                              *
\**************************************************************************/

import DeprIntr,cur_rate,IsSrvDoc,SqlConv;

var 
  currency = TBFile( "currency.dbt", "r", 0 ),
  sb_dtyp  = TBFile( "sb_dtyp.dbt",  "r", 3 ),
  pc_apltp = TBFile( "pc_apltp.dbt", "r", 0 ),
  pc_alg   = TBFile( "pc_alg.dbt",   "r", 0 ),
  depositr = TBFile( "depositr.dbt", "r", 6 ),
  sbdepdoc = TBFile( "sbdepdoc.dbt", "r", 1 );

var DateReport = {curdate};
var Branch     = NumFNCash( );
var ProgramID  = GetProgramID( );
var ErrCodeCur = 0;
var ErrFunc    = 0;

array ACodeCur;
array ACBRate;

array ACurSum;
array ASum;
array ACurSumFuture;
array ASumFuture;


/**************************************************************************\
|                      Определение курсов ЦБ за дату                       |
\**************************************************************************/
macro DefCBRatesForDate (cbratedate)

  var stat     = TRUE;
  var i        = 0;
  var CBRate   = 0.0;
  var BuyRate  = 0.0;
  var SellRate = 0.0;

  Message(" Определение курсов валют...");

  ASize(ACodeCur,0);
  ASize(ACBRate, 0);

  currency.rec.Code_Currency = 1;
  stat = currency.GetGE;
  while (stat)
    if (GetAllCurrRate(cbratedate,currency.rec.Code_Currency,CBRate,BuyRate,SellRate))
      i = ASize(ACodeCur);
      ACodeCur(i) = currency.rec.Code_Currency;
      ACBRate (i) = CBRate;
    end;
    stat = currency.Next;
  end;

end;


/**************************************************************************\
|              Обработка суммы операции для валютного вклада               |
\**************************************************************************/
macro TransSumToArray (in_sum, cod_cur, flag_future)

  var i = 0;
  var j = -1;

  if (in_sum != 0)
    if (flag_future)
      while (i < ASize(ACurSumFuture))
        if (cod_cur == ACurSumFuture(i))
          j = i;
          i = ASize(ACurSumFuture);
        end;
        i = i + 1;
      end;
      if (j == -1)
        j = ASize(ACurSumFuture);
        ACurSumFuture(j) = cod_cur;
        ASumFuture   (j) = in_sum;
      else
        ASumFuture(j) = ASumFuture(j) + in_sum;
      end;
    else
      while (i < ASize(ACurSum))
        if (cod_cur == ACurSum(i))
          j = i;
          i = ASize(ACurSum);
        end;
        i = i + 1;
      end;
      if (j == -1)
        j = ASize(ACurSum);
        ACurSum(j) = cod_cur;
        ASum   (j) = in_sum;
      else
        ASum(j) = ASum(j) + in_sum;
      end;
    end;
  end;

end;


/**************************************************************************\
|                 Обработка сумм операций валютных вкладов                 |
\**************************************************************************/
macro PostArrayForeignSums (perc_foreign, perc_foreign_future)

  var i = 0;
  var j = 0;
  var k = 0;
  var TmpVal;

  perc_foreign = $0L;
  perc_foreign_future = $0L;

  /* Сортировка прогнозируемой суммы */ 
  i = 0;
  j = 0;
  while (i < ASize(ACurSumFuture))
    j = i;
    while (j < ASize(ACurSumFuture))
      if (ACurSumFuture(j) < ACurSumFuture(i))
        TmpVal = ACurSumFuture(i);
        ACurSumFuture(i) = ACurSumFuture(j);
        ACurSumFuture(j) = TmpVal;
        TmpVal = ASumFuture(i);
        ASumFuture(i) = ASumFuture(j);
        ASumFuture(j) = TmpVal;
      end;
      j = j + 1;
    end;
    i = i + 1;
  end;

  /* Сортировка суммы текущего месяца */ 
  i = 0;
  j = 0;
  while (i < ASize(ACurSum))
    j = i;
    while (j < ASize(ACurSum))
      if (ACurSum(j) < ACurSum(i))
        TmpVal = ACurSum(i);
        ACurSum(i) = ACurSum(j);
        ACurSum(j) = TmpVal;
        TmpVal = ASum(i);
        ASum(i) = ASum(j);
        ASum(j) = TmpVal;
      end;
      j = j + 1;
    end;
    i = i + 1;
  end;

  i = 0;
  while (i < ASize(ACurSumFuture))
    j = 0;
    k = -1;
    while (j < ASize(ACodeCur))
      if (ACodeCur(j) == ACurSumFuture(i))
        k = j;
        j = ASize(ACodeCur);
      end;
      j = j + 1;
    end;
    if (k == -1)
      ErrCodeCur = ACurSumFuture(i);
    else
      perc_foreign_future = perc_foreign_future + ASumFuture(i) * ACBRate(k) / 100;
    end;
    i = i + 1;
  end;

  i = 0;
  while (i < ASize(ACurSum))
    j = 0;
    k = -1;
    while (j < ASize(ACodeCur))
      if (ACodeCur(j) == ACurSum(i))
        k = j;
        j = ASize(ACodeCur);
      end;
      j = j + 1;
    end;
    if (k == -1)
      ErrCodeCur = ACurSum(i);
    else
      perc_foreign = perc_foreign + ASum(i) * ACBRate(k) / 100;
    end;
    i = i + 1;
  end;

  if (ErrCodeCur > 0)
    perc_foreign = $0L;
    perc_foreign_future = $0L;
  end;

  while (ASize(ACurSumFuture) < 2)
    i = ASize(ACurSumFuture);
    ACurSumFuture(i) = -1;
    ASumFuture(i) = $0L;
  end;

  while (ASize(ACurSum) < 2)
    i = ASize(ACurSum);
    ACurSum(i) = -1;
    ASum(i) = $0L;
  end;

  SetParm(0,perc_foreign);
  SetParm(1,perc_foreign_future);
  
end;


/**************************************************************************\
|                 Определение номера кредитного учреждения                 |
\**************************************************************************/
macro DefNameBranch

  var out_name = "";
  var filialID;
  var dpDepList = TdpDepList;

  if (StrFor(ProgramID) == "П")
    filialID = dpDepList.getFilialID(NumFNCash());
  else
    filialID = Branch;
  end;

  if (not findDepartment(filialID, out_name))
    out_name = "";
  end;
  return out_name;

end;


/**************************************************************************\
|                Определение выплаченных процентов за даты                 |
\**************************************************************************/
macro DefPercSums ( FlagCur, PercFirstPeriod, PercSecondPeriod )

  var stat;
  var stat_doc;
  var d,m,y;
  var date_begin;
  var date_between;
  var date_end;
  var filterStr;
  var counterAll = 0;
  var counterSub = 0;

  PercFirstPeriod  = $0.0L;
  PercSecondPeriod = $0.0L;

  DateSplit(DateReport,d,m,y);
  m = m - 1;
  if (m < 1)
    m = 12;
    y = y -1;
  end;
  date_begin = Date(1,m,y);
  DateSplit(DateReport,d,m,y);
  date_between = Date(1,m,y);
  date_between = date_between - 1;
  DateSplit(DateReport,d,m,y);
  m = m + 1;
  if (m > 12)
    m = 1;
    y = y + 1;
  end;
  date_end = Date(1,m,y);
  date_end = date_end - 1;

  if ( FlagCur == 0 )
    Message( " Определение выплаченных процентов (рубли)...  Счет ", counterAll );
  else
    Message( " Определение выплаченных процентов (валюта)...  Счет ", counterAll );
  end;
  
  /* Цикл по счетам данной валютности */
  depositr.rec.IsCur        = FlagCur;
  filterStr = "t_IsCur = " + string( FlagCur );
  if (StrFor(ProgramID) == "П")
    depositr.rec.FNCash     = 0;
  else
    depositr.rec.FNCash     = Branch;
    filterStr = filterStr + " and t_FNCash = " + string( Branch );
  end;
  depositr.rec.Type_Account = "";
  depositr.rec.End_DateDep  = Date(0,0,0);

  depositr.addFilter( filterStr );

  stat = depositr.GetGE;
  while (stat)
    if (( depositr.rec.IsCur == FlagCur      ) AND
        ((StrFor(ProgramID) == "П"   ) OR
         (depositr.rec.FNCash   == Branch)   )    ) 

      counterAll = counterAll + 1;
      counterSub = counterSub + 1;
      if ( counterSub >= 50 )
        counterSub = 0;
        if ( FlagCur == 0 )
          Message( " Определение выплаченных процентов (рубли)...  Счет ", counterAll );
        else
          Message( " Определение выплаченных процентов (валюта)...  Счет ", counterAll );
        end;
      end;

      /* Цикл по зачисленям процентов данного счета */
      sbdepdoc.Clear;
      sbdepdoc.rec.Referenc         = depositr.rec.Referenc;
      sbdepdoc.rec.TypeOper         = 72;
      sbdepdoc.rec.DepDate_Document = date_begin;

      sbdepdoc.addFilter( "t_Referenc = " + string( depositr.rec.Referenc ) +
                          " and t_TypeOper = 72" +
                          " and t_DepDate_Document between " + sqlDateToStr( date_begin ) + " and " + sqlDateToStr( date_end ) );

      stat_doc = sbdepdoc.GetGE;
      while (stat_doc)
        if ((sbdepdoc.rec.Referenc         == depositr.rec.Referenc) AND
            (sbdepdoc.rec.TypeOper         == 72               ) AND 
            (sbdepdoc.rec.DepDate_Document <= date_end         )    )
          if (IsServDoc(sbdepdoc.rec) AND (sbdepdoc.rec.FlagStorn == ""))
            if (sbdepdoc.rec.DepDate_Document <= date_between)
              if (sbdepdoc.rec.Code_Currency == 0)
                PercFirstPeriod  = PercFirstPeriod + sbdepdoc.rec.InSum - sbdepdoc.rec.OutSum;
              else
                TransSumToArray( sbdepdoc.rec.InSum - sbdepdoc.rec.OutSum, sbdepdoc.rec.Code_Currency, FALSE );
              end;
            else
              if (sbdepdoc.rec.Code_Currency == 0)
                PercSecondPeriod = PercSecondPeriod + sbdepdoc.rec.InSum - sbdepdoc.rec.OutSum;
              else
                TransSumToArray(sbdepdoc.rec.InSum - sbdepdoc.rec.OutSum,sbdepdoc.rec.Code_Currency,TRUE);
              end;
            end;
          end;
          stat_doc = sbdepdoc.Next;
        else
          stat_doc = FALSE;
        end;
      end;
      sbdepdoc.dropFilter;
      stat = depositr.Next;
    else
      stat = FALSE;
    end;
  end;

  depositr.dropFilter;

  SetParm(1,PercFirstPeriod);
  SetParm(2,PercSecondPeriod);
  
end;


/**************************************************************************\
|                  Определение даты причисления процентов                  |
\**************************************************************************/
macro DefDatePayPerc (appl_type)

  var out_date = Date(0,0,0);
  var date_alg = Date(0,0,0);
  var d,m,y;

  DateSplit(DateReport,d,m,y);
  m = m + 1;
  if (m > 12)
    m = 1;
    y = y + 1;
  end;
  date_alg = Date(1,m,y);
  date_alg = date_alg - 1;
  DateSplit(DateReport,d,m,y);
                    
  pc_apltp.rec.IsCur    = sb_dtyp.rec.FlagCur;
  pc_apltp.rec.TypeRec  = 1003;
  pc_apltp.rec.Type     = sb_dtyp.rec.Kind;
  pc_apltp.rec.ApplType = appl_type;
  if (pc_apltp.GetEQ)
    pc_alg.rec.FlagCur    = pc_apltp.rec.IsCur;
    pc_alg.rec.Referenc   = pc_apltp.rec.ApType;
    pc_alg.rec.ObjectType = 1003;
    pc_alg.rec.BegDate    = date_alg;

    pc_alg.addFilter( "t_flagCur = " + string( pc_apltp.rec.IsCur ) +
                      " and t_referenc = " + GetSQLString( pc_apltp.rec.ApType ) +
                      " and t_objectType = 1003" );

    if ((pc_alg.GetLE                        ) AND 
        (pc_alg.rec.FlagCur    == pc_apltp.rec.IsCur ) AND
        (pc_alg.rec.Referenc   == pc_apltp.rec.ApType) AND 
        (pc_alg.rec.ObjectType == 1003           )    )
      if ((pc_alg.rec.GrafPay == 1) OR (pc_alg.rec.GrafPay == 2))
        m = m + 1;
        if (m > 12)
          m = 1;
          y = y + 1;
        end;
        out_date = Date(1,m,y) - 1;
      elif (pc_alg.rec.GrafPay == 3)
        if (m > 9)
          m = 1;
          y = y + 1;
        elif (m > 6)
          m = 10;
        elif (m > 3)
          m = 7;
        else
          m = 4;
        end;
        out_date = Date(1,m,y) - 1;
      elif (pc_alg.rec.GrafPay == 4)
        out_date = Date(31,12,y);
      elif (pc_alg.rec.GrafPay == 5)
        out_date = Date(pc_alg.rec.DayPay,m,y);
      end;
    end;
    pc_alg.dropFilter;
  end;

  return out_date;

end;


/**************************************************************************\
|                   Определение прогнозируемых процентов                   |
\**************************************************************************/
macro DefPercSumsFuture (FlagCur)

  var perc_future = $0L;
  var date_main   = Date(0,0,0);
  var date_alt    = Date(0,0,0);
  var date_period = Date(0,0,0);
  var date_enddep = Date(0,0,0);
  var date_beg    = Date(0,0,0);
  var date_end    = Date(0,0,0);
  var d,m,y;
  var SummaTmp    = $0L;
  var i;
  var stat;
  var stat_dep;
  var stat_func;
  var dpDepList = TdpDepList;

  array ABranches;

  if (FlagCur == 0)
    Message(" Определение прогнозируемых процентов (рубли)...");
  else
    Message(" Определение прогнозируемых процентов (валюта)...");
  end;

  ASize(ABranches,0);
  if (StrFor(ProgramID) == "П")
    dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
    while (dpDepList.next())
      i = ASize(ABranches);
      ABranches(i) = dpDepList.rec.Code;
    end;
  else
    ABranches(0) = Branch;
  end;

  DateSplit(DateReport,d,m,y);
  date_beg = Date(1,m,y);
  m = m + 1;
  if (m > 12)
    m = 1;
    y = y + 1;
  end;
  date_end = Date(1,m,y);
  date_end = date_end - 1;
            
  sb_dtyp.rec.FlagCur   = FlagCur;
  sb_dtyp.rec.FormContr = 0;

  sb_dtyp.addFilter( "t_flagCur = " + string( FlagCur ) );

  stat = sb_dtyp.GetGE;
  while (stat)
    date_main   = DefDatePayPerc(2001);
    date_alt    = DefDatePayPerc(2002);

    if (sb_dtyp.rec.FlagCur == FlagCur)
      i = 0;
      while (i < ASize(ABranches))
        depositr.Clear;
        depositr.rec.IsCur        = FlagCur;
        depositr.rec.FNCash       = ABranches(i);
        depositr.rec.Type_Account = sb_dtyp.rec.Kind;
        depositr.rec.End_DateDep  = Date(0,0,0);

        depositr.addFilter( "t_IsCur = " + string( FlagCur ) +
                            " and t_FNCash = " + string( ABranches(i) ) +
                            " and t_Type_Account = " + GetSQLString( sb_dtyp.rec.Kind ) +
                            " and t_Open_Date <= " + sqlDateToStr( date_end ) +
                            " and ( t_Close_Date == " + sqlDateToStr( date( 0, 0, 0 ) ) + " or t_Close_Date >= " + sqlDateToStr( date_beg ) + " )" );

        stat_dep = depositr.GetGE;
        while ( stat_dep )
          if ( ( depositr.rec.IsCur        == FlagCur          ) and
               ( depositr.rec.FNCash       == ABranches( i )   ) and
               ( depositr.rec.Type_Account == sb_dtyp.rec.Kind )    )
            if ( (   depositr.rec.Open_Date  <= date_end            ) and
                 ( ( depositr.rec.Close_Date == Date( 0, 0, 0 ) ) or 
                   ( depositr.rec.Close_Date >= date_beg        )   )    )
              if (((sb_dtyp.rec.FormContr == 20) OR 
                   (sb_dtyp.rec.FormContr == 30)   ) AND
                  ( depositr.rec.UseAlternate == 0 )    )
                date_enddep = depositr.rec.End_DateDep;
              else
                date_enddep = Date(0,0,0);
              end;
              if (depositr.rec.UseAlternate == 0)
                date_period = date_main;
              else
                date_period = date_alt;
              end;
              if (((date_enddep >= date_beg   ) AND
                   (date_enddep <= date_end   ) AND
                   (date_enddep != Date(0,0,0))    ) OR
                  ((date_period >= date_beg   ) AND
                   (date_period <= date_end   ) AND
                   (date_period != Date(0,0,0))     )   )
                stat_func = Проценты_Налог_К_Оплате_Учет_Конв( depositr.rec.Referenc, 0, date_end, SummaTmp );
                if (stat_func == 0)
                  if (depositr.rec.Code_Currency == 0)
                    perc_future = perc_future + SummaTmp;
                  else
                    TransSumToArray( SummaTmp, depositr.rec.Code_Currency, true );
                  end;
                else
                  ErrFunc = stat_func;
                end;
              end;
            end;    
            stat_dep = depositr.Next;
          else
            stat_dep = false;
          end;
        end;

        depositr.dropFilter;

        i = i + 1;
      end;
      stat = sb_dtyp.Next;
    else
      stat = false;
    end;
  end;

  sb_dtyp.dropFilter;

  return perc_future;

end;


/**************************************************************************\
|                              Выпуск отчета                               |
\**************************************************************************/
macro DoReport

  var report_namebranch          = "";
  var report_date                = "";
  var report_cbratedate          = Date(0,0,0);
  var report_perc_nat            = $0L;
  var report_perc_foreign        = $0L;
  var report_perc_nat_future     = $0L;
  var report_perc_foreign_future = $0L;
  var cur_foreign                = "";
  var sum_foreign                = "";
  var d,m,y;
  var i;

  /* Дата курса ЦБ для пересчета валютных вкладов */
  DateSplit( DateReport, d, m, y );
  report_cbratedate = Date( 1, m, y );
  report_cbratedate = report_cbratedate - 1;
  DefCBRatesForDate( report_cbratedate );

  /* Определение названия дат в отчете */
  report_date = StrLwr( MonName( m ) ) + " " + String( y ) + " г.";
  m = m - 1;
  if ( m < 1 )
    m = 12;
    y = y - 1;
  end;
  report_date = "на " + StrLwr( MonName( m ) ) + " " + String( y ) + " г. и " + report_date;

  /* Определение названия учреждения */
  report_namebranch = DefNameBranch( );

  /* Определение процентов для рублевых вкладов */
  DefPercSums( 0, report_perc_nat, report_perc_nat_future );
  report_perc_nat_future = report_perc_nat_future + DefPercSumsFuture( 0 );

  /* Определение процентов для валютных вкладов */
  DefPercSums( 1, NULL, NULL );
  DefPercSumsFuture( 1 );
  PostArrayForeignSums( report_perc_foreign, report_perc_foreign_future );

  [ ];
  [ Учр. № #]( report_namebranch );
  [ ];
  [ ###########################################################################]
  ("ПРОГНОЗ УПЛАЧЕННЫХ ПРОЦЕНТОВ ПО ВКЛАДАМ ФИЗИЧЕСКИХ ЛИЦ":c);
  [ ];
  [ ###########################################################################]
  ( report_date:c );
  [ ];
  [ ┌──────────────────────┬──────────────────┬───────────────────────────────┐];
  [ │  Способ выплаты %%   │Прогноз на текущий│     Прогноз на следующий      │];
  [ │                      │      месяц       │            месяц              │];
  [ ├──────────────────────┼──────────────────┼───────────────────────────────┤];
  [ │Проценты, уплаченные в│ ################ │          #################### │]
  ( report_perc_nat:16:2:a, report_perc_nat_future:20:2:a );
  [ │рублях                │                  │                               │];
  [ ├──────────────────────┼──────────────────┼────────────────┬──────────────┤];
  i = 0;
  while ( i < ASize( ACurSumFuture ) )
    cur_foreign = "";
    currency.Clear;
    currency.rec.Code_Currency = ACurSumFuture( i );
    if ( ACurSumFuture( i ) < 0 )
      cur_foreign = "";
    else
      currency.rec.Code_Currency = ACurSumFuture( i );
      if ( currency.GetEQ )
        cur_foreign = currency.rec.Short_Name;
      else
        cur_foreign = String( ACurSumFuture( i ) );
      end;
    end;
    if ( ASumFuture( i ) != 0 )
      sum_foreign = String( ASumFuture( i ):10:2:a );
    else
      sum_foreign = "";
    end;
    if ( i == 0 )
      [ │Проценты, уплаченные в│ ################ │ ### ########## │ ############ │]
      ( report_perc_foreign:16:2:a, cur_foreign, sum_foreign, report_perc_foreign_future:12:2:a );
    elif ( i == 1 )
      [ │инвалюте (руб.экв.*)  │                  │ ### ########## │              │]
      ( cur_foreign, sum_foreign );
    else
      [ │                      │                  │ ### ########## │              │]
      ( cur_foreign, sum_foreign );
    end;
    i = i + 1;
  end;
  [ └──────────────────────┴──────────────────┴────────────────┴──────────────┘];
  [ ];
  [ (*)По курсу ЦБ на #]( report_cbratedate );
  if ( ErrCodeCur > 0 )
    [ ];
    [ ОШИБКА!!! Не найден курс ЦБ для валюты код #]( String( ErrCodeCur ) );
  end;
  if ( ErrFunc > 0 )
    [ ];
    [ ОШИБКА!!! Функция Проценты_Налог_К_Оплате_Учет_Конв код #]( String( ErrFunc ) );
  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я    П Р О Г Р А М М А                    |
\**************************************************************************/

  if ( GetDate( DateReport, "Введите дату для формирования отчета" ) )
    OpenDepFiles( );
    DoReport( );
    CloseDepFiles( );
  else
    [ ];
    [ Отказались от выпуска отчета по прогнозу причисленных процентов];
    [ (форма № 7.64)];
  end;

end;
