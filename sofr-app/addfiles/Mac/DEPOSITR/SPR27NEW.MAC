 /*
  RS-Bank 4.3
  Вкладчики Сбербанка

  Справки на приход/расход средств
 Настpойка на впечатывание в спpавку
12/09/98 TJ - Добавил распечатку ФИО операциониста.
              доработана печать года и месяца


*/
/*import BankInter,deprintr,DeskInter,InputRN, ChkPrn, QueryRes, Alg_Vars, chkCdDup;*/
import BankInter, "formBSO.mac";
import ChkPrn, QueryRes, chkCdDup;
import "cur_rate.mac";

var ОТЧЕТ_В_EXCEL = false;

 file CashVal  ( sb_casvl            ) key 1;
 file namealg  ( namealg, "bank.def" ) key 0;
 file GroupMem ( groupmem            ) key 0;
 file prs      ( person,  "bank.def" );       /* Файл операционистов  */
 file dd       ( sbdepdoc            ) write; /* Документы вкладчика  */
 file cr       ( currency            ) Key 0; /* Файл валют           */
 file dtype    ( sb_dtyp             ) Key 2; /* виды вкладов         */
 private var sb_casln = TBFile( "sb_casln.dbt", "R", 1, null, "sbbank.def" );
 private var pay_doc  = TBFile( "pay_doc.dbt" , "R", 2, null, "sbbank.def" );
 private var sb_typop = TBFile( "sb_typop.dbt", "r", 0 );


/*---------- Инициализируемые структуры ---------------------------------*/
 record Parm(CashParm);

 private const SB_INCASH          =  100;   /* Документ порожден Кассой */
 private const CASHOP_DEBIT        = 5;
 private const CASHOP_CHVALUE      = 9;
 private const BIT_FLAG_F0406007   = 13;
 private const DateNull            = Date( 0, 0, 0 );

 const FlagCur = NumFlagCur( );

 private const Delim = "-";  /* Разделитель рублей и копееек */

 private const TYPERES_CURRENCY    =   1, /* Деньги (из справочника валют)           */
               TYPERES_VALFORM     =   2, /* Ценные бланки                           */
               TYPERES_VALUABLES   =   3, /* Др. ценности (из справочника ценностей) */
               TYPERES_PAYDOC      =   4, /* Платежные документы ОП                  */
               TYPERES_CARD        =  10, /* Пластиковые карточки                    */
               TYPERES_MINCAPISSUE = 500; /* Минимально возможный код ЦБ             */

 /* Код операции по 113i*/
 private const CURToCURAcc     = 60,
               CURToRURAcc     = 61,
               RURToCURAcc     = 62,
               CURFromCURAcc   = 63,
               CURFromRURAcc   = 64,
               RURFromCURAcc   = 65;


 private const
    SIGNORDER           = 2,    /* значение поля sb_edref.sign   */
    SIGNPRINTORDER      = 3,
    ACCOUTN_OWNER       = StrFor(  0 ),    /* Владелец счета      */
    ACCOUTN_PROXY       = StrFor(  1 ),    /* Доверенное лицо     */
    ACCOUTN_HEIR        = StrFor(  2 ),    /* Наследник - выдачи  */
    ANONIMUS_CLIENT     = StrFor(  3 ),    /* Анонимный клиент    */
    ACCOUNT_IMPORTER    = StrFor(  4 ),    /* Вноситель           */
    ACCOUNT_TUTOR       = StrFor(  5 ),    /* Опекун              */
    ACCOUNT_OBLIGATOR   = StrFor(  6 ),    /* Под обязательство   */
    ACCOUTN_HEIR_PAY    = StrFor(  7 ),    /* Наследник - выплаты */
    ACCOUNT_UNDER_TRIAL = StrFor(  8 ),    /* По суду             */
    ACCOUNT_PARENT      = StrFor(  9 ),    /* Родитель            */
    ACCOUNT_RETRAST     = StrFor( 10 );    /* Передоверенность    */

 Var
    {IsCashierReport},
    OperBrigade = GetOperBrigade,
    DocDay, DocMon, DocYear, TmpMon, Branch = NumRealFNCash,
    ClientList = TClientList;

 Var bDay, bMon, bYear;  /* День, месяц, год рождения клиента */
 Var cDay, cMon, cYear;  /* День, месяц, год текущей даты */
 Var ClientYears;        /* Возраст клиента */
 private var onlyInsert = false;

 ALG_RESULT = ERR_RES;/*заранее инициализируем код возврата*/

var
        Docum,
        Num_doc,
        Ser_doc,
        F_name,
        S_name,
        T_name,
        Rez,
        NoRez,
        Bad,
        Name,
        DebSum,
        KredSum,
        DebCur=-1,
        KredCur=-1,
        Name_op = "",
        DateDoc,
        Deb_str  = "",
        Kred_str = "",
        len_str,
        Short_deb,
        Short_kred,stmp,
        rub_str,
        kop_str,
        NullChr="";

array Summa_str;


macro checkTypeAccount ( typeAccount, addSuffix )

  var stat = false;
  var i = Index( StrUpr( typeAccount ), StrUpr( addSuffix ) );

  if ( ( i > 0 ) and ( i == ( StrLen( typeAccount ) - StrLen( addSuffix ) + 1 ) ) )
    stat = true;
  end;

  return stat;
end;


macro CodeToStr(Code)
var CodeStr="000";
var TmpStr;
var Code_tmp;

        if( Code == 0 )
                Code_tmp = 2;
        else
                Code_tmp = Code;
        end;

        TmpStr = string(Code_tmp);
        strset(CodeStr, 4-strlen(TmpStr), TmpStr);

return CodeStr;
end;

macro SumDelim( Sum )
   var SumStr = String( Sum );

   SumStr = StrSubst( SumStr, ".", Delim );

   return SumStr;
end;


private MACRO FindComissionDoc()

  var mainKind = 0;
  var mainKey  = "";
  var stat;
  var wasFind = false;

  sb_casln.KeyNum = 1;
  sb_casln.Clear;
  sb_casln.rec.ApplicationKind2 = ALG_SBDEPDOC.iApplicationKind;
  sb_casln.rec.ApplicationKey2  = ALG_SBDEPDOC.ApplicationKey;

  if ( sb_casln.GetEQ )
    mainKind = sb_casln.rec.ApplicationKind1;
    mainKey  = sb_casln.rec.ApplicationKey1;
  end;

  if ( mainKey != "" )
    sb_casln.KeyNum = 0;
    sb_casln.Clear;
    sb_casln.rec.ApplicationKind1 = mainKind;
    sb_casln.rec.ApplicationKey1  = mainKey;

    stat = sb_casln.GetGE;

    while ( stat )

      if ( ( sb_casln.rec.ApplicationKind1 == mainKind ) and
           ( sb_casln.rec.ApplicationKey1  == mainKey  )    )

        if ( ( sb_casln.rec.ApplicationKind2 == 200 ) and ( sb_casln.rec.RefValue2 == 0 ) )

          pay_doc.clear();
          pay_doc.keynum( 2 );

          pay_doc.rec.iApplicationKind = sb_casln.rec.ApplicationKind2;
          pay_doc.rec.ApplicationKey   = sb_casln.rec.ApplicationKey2;

          if( pay_doc.getEQ() )
            if ( pay_doc.rec.GroupOpert == 21 )
              wasFind = true;
              stat = false;
            end;
          end;
        end;
      else
        stat = false;
      end;

      if ( stat )
        stat = sb_casln.Next;
      end;
    end;
  end;

  return wasFind;

end;


/*========================================================================*/

MACRO PutNEWF0406007()

  var Отчет = COrderBSO();

  Отчет.Rate_Ref        = 0;
  /* По умолчанию текущая системная дата и время
  Отчет.formDate            = Date( );
  Отчет.formTime            = Time( int( substr( ALG_SBDEPDOC.ApplicationKey, 11, 2 ) ),
                                    int( substr( ALG_SBDEPDOC.ApplicationKey, 13, 2 ) ),
                                    int( substr( ALG_SBDEPDOC.ApplicationKey, 15, 2 ) ) );
  */

  Отчет.Sname           = ClientList.CurRec.Rec.Name1;
  Отчет.Name            = ClientList.CurRec.Rec.Name2;
  Отчет.Pname           = ClientList.CurRec.Rec.Name3;
  Отчет.ClientDocType   = ClientList.CurRec.Rec.PaperKind;
  Отчет.ClientDocSeria  = ClientList.CurRec.Rec.PaperSeries;
  Отчет.ClientDocNumber = ClientList.CurRec.Rec.PaperNumber;
  Отчет.ClientDocInfo   = ClientList.CurRec.Rec.PaperIssuer;
  Отчет.ClientDocDate   = ClientList.CurRec.Rec.PaperIssuedDate;

  FindCurrRate( ALG_SBDEPDOC.Code_Currency, 1 /* Осн курс*/, Отчет.formDate, Отчет.Rate, NULL, NULL, Отчет.formTime );

  sb_typop.Clear( );
  sb_typop.KeyNum = 0;
  sb_typop.rec.IsCur    = ALG_SBDEPDOC.IsCur;
  sb_typop.rec.Kind     = "Шаблонный";
  sb_typop.rec.NumOpert = ALG_SBDEPDOC.TypeOper;
  if ( sb_typop.GetEQ( ) )
    Отчет.KindOper = sb_typop.rec.Code113i; // Код операции по 113И
  end;

  if( ALG_SBDEPDOC.InSum )
      Отчет.IncomeRefVal    = -1;
      Отчет.IncomeTypeValue = TYPERES_CURRENCY;
      Отчет.IncomeCodCur    = ALG_SBDEPDOC.Code_Currency;
      Отчет.IncomeAmount    = ALG_SBDEPDOC.InSum;
      if ( ALG_SBDEPDOC.Code_Currency == 0 )
        Отчет.KindOper      = CURToRURAcc; // Код операции по 113И
      else
        Отчет.KindOper      = CURToCURAcc; // Код операции по 113И
      end;
      //onlyInsert = true;
   else
      Отчет.OutRefVal    = -1;
      Отчет.OutTypeValue = TYPERES_CURRENCY;
      Отчет.OutCodCur    = ALG_SBDEPDOC.Code_Currency;
      Отчет.OutAmount    = ALG_SBDEPDOC.OutSum;
      if ( ALG_SBDEPDOC.Code_Currency == 0 )
        Отчет.KindOper   = CURFromRURAcc; // Код операции по 113И
      else
        Отчет.KindOper   = CURFromCURAcc; // Код операции по 113И
      end;
      //onlyInsert = true;
  end;

  if ( FindComissionDoc( ) )
    Отчет.CommCodCur  = pay_doc.Rec.CodCur;
    Отчет.CommAmount  = pay_doc.Rec.InSum;
  end;

  if ( Int( Отчет.KindOper ) == 0 )
    ALG_RESULT = OK_RES;
    Exit( 1 );
  end;

  Отчет.ApplicationKind = ALG_SBDEPDOC.iApplicationKind;
  Отчет.ApplicationKey  = ALG_SBDEPDOC.ApplicationKey;
  Отчет.OnlyInsert = onlyInsert; // Не печатать справку

  Отчет.ОТЧЕТ_В_EXCEL = ОТЧЕТ_В_EXCEL;
  Отчет.Print();

END;


/***********************************************************
***********************************************************/

/*  Основной текст макроса*/

  var
      CodClient,
      Printed = FALSE,
      stat = FALSE,
      FIO  = ""   ,
      IOtmp,
      Otmp;


  if ( ( ALG_SBDEPDOC.OutSum == 0 ) and ( ALG_SBDEPDOC.InSum == 0 ) )
    ALG_RESULT = OK_RES;
    Exit( 1 );
  elif ( checkTypeAccount( ALG_SBDEPDOC.Type_Account, "изРФ" ) )
    if ( ( ( ALG_SBDEPDOC.TypeOper == 1 ) or ( ALG_SBDEPDOC.TypeOper == 3 ) ) /*and ( ALG_SBDEPDOC.ApplType == 40 )*/ )
      ;
    else
      ALG_RESULT = OK_RES;
      Exit( 1 );
    end;
  elif ( checkTypeAccount( ALG_SBDEPDOC.Type_Account, "вРФ" ) )
    if ( ( ALG_SBDEPDOC.TypeOper == 4 ) /*and ( ALG_SBDEPDOC.ApplType == 40 )*/ )
       ;
    else
      ALG_RESULT = OK_RES;
      Exit( 1 );
    end;
  elif ( FlagCur == 0 )
    ALG_RESULT = OK_RES;
    Exit( 1 );
  end;

  Rez   = "X";
  Norez = " ";

  /* Проверка дееспособности владельца счета */
  stat = ClientList.GetRecord( ALG_DEPOSITOR.CodClient );

  if ( NOT stat )
    MsgBox( "Невозможно прочитать данные владельца счета" );
    ALG_RESULT = ERR_RES;
    Exit( 1 );
  end;

  DateSplit( ClientList.CurRec.rec.Born, bDay, bMon, bYear );
  DateSplit( {curdate},                  cDay, cMon, cYear );

  ClientYears = cYear - bYear;
  if ( ClientYears > 0 )
    if ( bMon > cMon )
      ClientYears = ClientYears - 1;
    else
      if ( ( bMon == cMon )  AND  ( bDay > cDay ) )
        ClientYears = ClientYears - 1;
      end;
    end;
  else
    MsgBox( "Дата рождения клиента задана неправильно" );
    ALG_RESULT = ERR_RES;
    Exit( 1 );
  end;

  if ( ( ClientYears < 14 )  OR  /* Владелец несовершеннолетний */
       ( ClientList.CurRec.rec.DateDeath != DateNull ) )  /* Владелец умер */
    CodClient = ALG_SBDEPDOC.CodClient;
  else
    CodClient = ALG_DEPOSITOR.CodClient;
  end;

  stat = ClientList.GetRecord( CodClient );
  if( stat )
     PutNEWF0406007( );
     SetBitFlag( ALG_SBDEPDOC.Flags, BIT_FLAG_F0406007, 1 );
     ALG_UPDATE = 1;
     ALG_RESULT = OK_RES;
  else
     ClientList.CurRec.Clear;
     ALG_RESULT = ERR_RES;
  end;

  if ( ( MacroOutput == "PRN" ) or onlyInsert )
    Exit( 1 );
  end;

end;
