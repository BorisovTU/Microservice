/* Отчеты по лотереям */

import ci_defs, acc_form;
            
class ( TCIRecGenerator ) TLotteryRecGenerator( _oper, _makeReport, _observer )

  var
    lotteryDocInfo = TRecHandler( "ci_doc.2" ),
    lotteryRecInfo = TRecHandler( "ci_rec.2");
  
  var
    totNumItems=0,
    totSoldItems=0,
    totBoughtItems=0,
    totSoldDepo=0,
    totBoughtDepo=0,
    totSum=$0,
    totPrice=$0,
    totSumSold=$0,
    totSumBought=$0;

  macro f5_outHead;
  
    [
                                                                                 Филиал №
                                                                                 отделения
                                                                                 Сбербанка РФ
  
  
                                 Отчет по операциям по лотереям за ##########
  
    ]
    (
      {curdate}
    );
  end;
  
  macro f5_outCommonHead;
  
    [+----------+----------------------------------------------+--------+-------------------+];
    [|    №     |        Н а з в а н и е   л о т е р е и       | Колич. |     С у м м а     |];
    [+----------+----------------------------------------------+--------+-------------------+];
  end; 
  
  macro f5_outSellHead;
  
    [ ];
    [Продажа лотерей];
    [ ];
    f5_outCommonHead;
  end;
  
  macro getNumItems
  
    var
      recFound,
      numItems=0;
  
    capIssueDoc.rec.RecApplicKind=capIssueRec.rec.ApplicationKind;
    capIssueDoc.rec.RecApplicKey=capIssueRec.rec.ApplicationKey;
  
    recFound=capIssueDoc.getEQ;
    while(recFound
      and (capIssueDoc.rec.RecApplicKind==capIssueRec.rec.ApplicationKind)
      and (capIssueDoc.rec.RecApplicKey==capIssueRec.rec.ApplicationKey))
      if ( checkCIDoc )
        lotteryDocInfo.setRecordAddr(capIssueDoc,0,
          capIssueDoc.fldOffset("Info"),true);
        numItems=numItems+lotteryDocInfo.rec.NumItems;
      end;
      recFound=capIssueDoc.next;
    end;
    return numItems;
  end;
  
  macro f5_outPayWinHead;
  
    [ ];
    [Выплата выигрышей];
    [ ];
    f5_outCommonHead;
  end;
  
  macro f5_outLine;
  
    lotteryRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    [|  #####   | ############################################ | ###### | ################# |]
    (
      capIssueRec.rec.Issue,
      lotteryRecInfo.rec.Name,
      getNumItems,
      capIssueRec.rec.Sum
    );
  end;
  
  macro f5_outTotals;
  
    lotteryRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    [+---------------------------------------------------------+--------+-------------------+];
    [|  И т о г о :                                            | ###### | ################# |]
    (
      totNumItems,
      totSum
    );
  end;
  
  macro f5_sellReport
  
    var
      recFound;
  
    totNumItems=0;
    totSum=$0;
  
    capIssueRec.clear;
    capIssueRec.rec.Branch=Branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_LOTTERY;
  
    f5_outSellHead;
    recFound=capIssueRec.getGE;
    while(recFound
      and (capIssueRec.rec.Branch==branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_LOTTERY))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Brigade==operBrigade)
        and (capIssueRec.rec.Operation==O_LOTTERY_SELL))
        f5_outLine; 
        lotteryRecInfo.setRecordAddr(capIssueRec,0,
          capIssueRec.fldOffset("Info"),true);
        totNumItems=totNumItems+getNumItems;
        totSum=totSum+capIssueRec.rec.Sum;
      end;
      recFound=capIssueRec.next;
    end;
    f5_outTotals;
  end;
      
  macro f5_payWinReport
  
    var
      recFound;
  
    totNumItems=0;
    totSum=$0;
  
    capIssueRec.clear;
    capIssueRec.rec.Branch=Branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_LOTTERY;
  
    f5_outPayWinHead;
    recFound=capIssueRec.getGE;
    while(recFound
      and (capIssueRec.rec.Branch==branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_LOTTERY))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Brigade==OperBrigade)
        and (capIssueRec.rec.Operation==O_LOTTERY_PAYWIN))
        f5_outLine; 
        lotteryRecInfo.setRecordAddr(capIssueRec,0,
          capIssueRec.fldOffset("Info"),true);
        totNumItems=totNumItems+getNumItems;
        totSum=totSum+capIssueRec.rec.Sum;
      end;
      recFound=capIssueRec.next;
    end;
    f5_outTotals;
  end;
      
  macro f5_report;
  
    if ( not makeReport )
      return;
    end;

    f5_outHead;         
    f5_sellReport;
    f5_payWinReport;
  end;
  
  macro find;
  
    return findCIMisc( CI_LOTTERY, capIssueRec.rec.Issue );
  end;
  
  macro outSellRecHead;
  
    var
      branchStrNum = "",
      departNum = "",
      departName = "",
      comment = "";

    if ( not makeReport )
      return;
    end;
  
    getBranchParams( branch, branchStrNum, departNum, departName );
    lotteryRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);
  
    comment = "(Лотерея \""+lotteryRecInfo.rec.Name+"\")";
  
    [                                                                                                                                          Ф. № 56
       ##################################                                                                          
                                                                                                                                   Филиал № ##########
       Сберегательного банка № ##########         
  
  
                                                                
                                                                 КОНТРОЛЬНАЯ  ВЕДОМОСТЬ
                          
                                                      на продажу и покупку облигаций Российского
                                                            внутреннего выигрышного займа
                                                                       1992 года
                                                  ##################################################
                                                                     за ##########
            
  
       Смена № #####
  
    ]
    (
      departName,
      branchStrNum,
      departNum,
      comment:c,
      {curdate},
      operBrigade
    );
    [+--------+--------------------------------------------------------+---------------------------------------------------------+-------------------+];
    [|Номерной|                     П р о д а н о                      |                       К у п л е н о                     |   П о д п и с и   |];
    [| талон, |---------+-----------+-----------------+----------------|---------+-----------+-----------------+-----------------|---------+---------|];
    [|выданный| Номинал |Количество |Номинальная стои-|Получено за     | Номинал |Количество |Номинальная стои-| Получено за     |         |         |];
    [|клиенту |облигации|-----+-----|мость проданных  |проданные       |облигации|-----+-----|мость купленных  | купленные       |контролер| кассир  |];
    [|        |         | шт  |д/шт |облигаций        |облигации       |         | шт  |д/шт |облигаций        | облигации       |         |         |];
    [+--------+---------+-----+-----+-----------------+----------------+---------+-----+-----+-----------------+-----------------+---------+---------+];
  end;                                                                                                                                                
                                                                                                                                                      
  macro getDepo( numItems, name )                                                                                                                   
                                                                                                                                                      
    return 0;
  end;
  
  macro outSellRecLine;
  
    var
      priceRepr=$0,
      boughtPriceRepr=$0,
      soldItems=0,
      boughtItems=0,
      soldDepo=0,
      boughtDepo=0,
      soldPar=$0,
      boughtPar=$0,
      sumSoldRepr=$0,
      sumBoughtRepr=$0;
   
    findCIMisc( capIssueDoc.rec.Type, capIssueDoc.rec.Issue );
  
    lotteryDocInfo.setRecordAddr(capIssueDoc,0,
      capIssueDoc.fldOffset("Info"),true);

    if(capIssueDoc.rec.Operation==O_LOTTERY_SELL) 
      soldItems = lotteryDocInfo.rec.NumItems;
      totSoldItems = totSoldItems + SoldItems;
      soldDepo = getDepo( SoldItems, lotteryDocInfo.rec.Name );
      totSoldDepo = totSoldDepo + SoldDepo;
      totSumSold=totSumSold+capIssueDoc.rec.Sum;
      priceRepr=lotteryDocInfo.rec.Price;
      sumSoldRepr=capIssueDoc.rec.Sum;
    elif(capIssueDoc.rec.Operation==O_LOTTERY_BUY) 
      boughtItems = lotteryDocInfo.rec.NumItems;
      totBoughtItems = totBoughtItems + boughtItems;
      boughtDepo = getDepo( boughtItems, lotteryDocInfo.rec.Name  );
      totBoughtDepo = totBoughtDepo + boughtDepo;
      totSumBought=totSumBought+capIssueDoc.rec.Sum;
      boughtPriceRepr=lotteryDocInfo.rec.Price;
      sumBoughtRepr=capIssueDoc.rec.Sum;
    end;
  
    updateValCounter( capIssueDoc.rec.ApplicationKind, capIssueDoc.rec.ApplicationKey );

    if ( not makeReport )
      return;
    end;


    [|########|#########|#####|#####|#################|################|#########|#####|#####|#################|#################|         |         |]
    (
      capIssueDoc.rec.DocNumber,
      soldPar:z,
      soldItems:z,
      soldDepo:z,
      priceRepr:z,
      sumSoldRepr:z,
      boughtPar:z,
      boughtItems:z,
      boughtDepo:z,
      boughtPriceRepr:z,
      sumBoughtRepr:z
    );
  end;
  
  macro lotterySellRecTotals;
   
    var
      sellCredAcc = "",
      sellDebAcc = "",
      buyCredAcc = "",
      buyDebAcc = "";

    lotteryRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    notifyObserver( CI_LOTTERY, capIssueRec.rec.Issue, O_LOTTERY_SELL, "Продажа: " + lotteryRecInfo.rec.Name, totSumSold );
  
    if ( not makeReport )
      return;
    end;

    lotteryDocInfo.setRecordAddr(capIssueDoc,0,
      capIssueDoc.fldOffset("Info"),true);

    [+--------+---------+-----+-----+-----------------+----------------+---------+-----+-----+-----------------+-----------------+---------+---------+];
    [| Всего  |         |#####|#####|                 |################|         |#####|#####|                 |#################|         |         |]
    (
      totSoldItems,
      totSoldDepo,
      totSumSold,
      totBoughtItems,
      totBoughtDepo,
      totSumBought
    );
    [ ];
    [                                                    Д-т сч. ];
    [                                                    ########################## ]
    (Acc_Form(buyDebAcc));
    [ ];
    [                                 Д-т сч. ];
    [                                 ########################## ]
    (Acc_Form(sellDebAcc));
    [ ];
    [             Расход по сч.                          К-т сч. ];
    [             ___________________                    ########################## ]
    (Acc_Form(buyCredAcc));
    [ ];
    [                                 К-т сч. ];
    [                                 ########################## ]
    (Acc_Form(sellCredAcc));
    [ ];
    [                                                    Приход по сч.];
    [                                                    _________________ ];

  end;
  
  macro outPayRecHead;
  
    var
      branchStrNum = "",
      departNum = "",
      departName = "",
      debAcc = "",
      credAcc = "",
      comment = "";

    if ( not makeReport )
      return;
    end;
  
    getBranchParams( branch, branchStrNum, departNum, departName );

    lotteryRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);
  
    comment = "(Лотерея \""+lotteryRecInfo.rec.Name+"\")";
  
    [                                                                                                            Ф. № 92
       ##################################                                                                          
                                                                                                     Филиал № ##########
       Сберегательного банка № ##########         
  
  
    
                                                   КОНТРОЛЬНАЯ  ВЕДОМОСТЬ                    Отметки бухгалтерии 
  
                                             на оплату выигравших ценных бумаг         Дебет сч.  ######################
                                          #######################################      Кредит сч. ######################
                                                       за ##########                   Приход сч. ______________________
                                                                                               
                                                                                       
       Смена № #####
  
    ]
    (
      departName,
      branchStrNum,
      departNum,
      Acc_Form(debAcc),
      comment:c,
      Acc_Form(credAcc),
      {curdate},
      operBrigade
    );
    [+-------------------------------------+-------+-------+-----------+--------+--------+---------+-------------------+
     |                                     | Номер |       |           |        |Нарицат.|         |                   |
     |       Название ценной бумаги        |  тир. | Серия |   Номер   | Разряд |стоим-ть| Кол-во  |     Выплачено     |
     |                                     |       |       |           |        |        |         |                   |
     +-------------------------------------+-------+-------+-----------+--------+--------+---------+-------------------+];
  end;
  
  macro outPayRecLine;
  
    lotteryDocInfo.setRecordAddr(capIssueDoc,0,
      capIssueDoc.fldOffset("Info"),true);

    totPrice=TotPrice+lotteryDocInfo.rec.Price;
    totSum=TotSum+capIssueDoc.rec.Sum;
    totNumItems=TotNumItems+lotteryDocInfo.rec.NumItems;

    updateValCounter( capIssueDoc.rec.ApplicationKind, capIssueDoc.rec.ApplicationKey );

    if ( not makeReport )
      return;
    end;

    [| ################################### | ##### | ##### | ######### |        | ###### | ####### | ################# |]
    (
      ciMisc.rec.Name,
      lotteryDocInfo.rec.Drawing,
      lotteryDocInfo.rec.Series,
      lotteryDocInfo.rec.Number,
      lotteryDocInfo.rec.Price,
      lotteryDocInfo.rec.NumItems,
      capIssueDoc.rec.Sum
    );
  end;
  
  macro lotteryPayRecTotals;


    lotteryRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    notifyObserver( CI_LOTTERY, capIssueRec.rec.Issue, O_LOTTERY_PAYWIN, "Выплата выигрыша: " + lotteryRecInfo.rec.Name, totSum );
   
    if ( not makeReport )
      return;
    end;

    [+-------------------------------------+-------+-------+-----------+--------+--------+---------+-------------------+];
    [| В с е г о                           |       |       |           |        |        | ####### | ################# |]
    (
      totNumItems,
      totSum
    );
    [ ];
    [  # ]( rubToStr( totSum ) );
    [  ---------------------------------------------------------------------- ];
    [                          (сумма прописью) ];
    [ ];
  end;
  
  macro outRecTail;
  
    if ( not makeReport )
      return;
    end;

    [
  
  
  
          Контролер (оператор) ___________                                Кассир ____________________
    ];
    printLn( StrFor( 12 ) );
  end;
  
  macro lotterySellRecBody;
  
    var
      recFound;
  
    capIssueDoc.rec.RecApplicKind=capIssueRec.rec.ApplicationKind;
    capIssueDoc.rec.RecApplicKey=capIssueRec.rec.ApplicationKey;
  
    recFound = capIssueDoc.getEQ;
    while(recFound
      and (capIssueDoc.rec.RecApplicKind==capIssueRec.rec.ApplicationKind)
      and (capIssueDoc.rec.RecApplicKey==capIssueRec.rec.ApplicationKey))
      if ( checkCIDoc )
        outSellRecLine;
      end;
      recFound=capIssueDoc.next;
    end;
  end;
  
  macro lotterySellRec;
  
    var
      recFound;
   
    totNumItems=0;
    totSoldItems=0;
    totBoughtItems=0;
    totSoldDepo=0;
    totBoughtDepo=0;
    totSum=$0;
    totPrice=$0;
    totSumSold=$0;
    totSumBought=$0;
    resetValCounter;
  
    outSellRecHead; 
    lotterySellRecBody;
    lotterySellRecTotals;
    outRecTail;
  end;
  
  macro lotteryPayRecBody;
  
    var
      recFound;
  
    find;
    capIssueDoc.rec.RecApplicKind=capIssueRec.rec.ApplicationKind;
    capIssueDoc.rec.RecApplicKey=capIssueRec.rec.ApplicationKey;
    recFound=capIssueDoc.getEQ;
    while(recFound
      and (capIssueDoc.rec.RecApplicKind==capIssueRec.rec.ApplicationKind)
      and (capIssueDoc.rec.RecApplicKey==capIssueRec.rec.ApplicationKey))
      if ( checkCIDoc )
        outPayRecLine;
      end;
      recFound=capIssueDoc.next;
    end;
  end;
  
  macro lotteryPayRec;
   
    totNumItems = 0;
    totSum = $0;
    totPrice=$0;
    resetValCounter;
    outPayRecHead; 
    lotteryPayRecBody;
    lotteryPayRecTotals;
    outRecTail;
  end;
  
  macro printRec( applicationKind, applicationKey )
        
    if ( not makeReport )
      return;
    end;

    capIssueRec.keyNum = 2;                                        
    capIssueRec.rec.ApplicationKind=applicationKind;
    capIssueRec.rec.ApplicationKey=applicationKey;
    if( not capIssueRec.getEQ )
      msgBox("Ошибка при печати ведомости");
      return;
    end;
    if(capIssueRec.rec.Operation==O_LOTTERY_SELL) 
      lotterySellRec;
    elif(capIssueRec.rec.Operation==O_LOTTERY_PAYWIN) 
      lotteryPayRec;
    end;
  end;
  
  macro sellBuyReport
  
    var
      recFound;

    capIssueRec.clear;
    capIssueRec.rec.Branch=Branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_LOTTERY;
  
    recFound=capIssueRec.getGE;
    while(recFound
      and (capIssueRec.rec.Branch==Branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_LOTTERY))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Oper==oper)
        and (capIssueRec.rec.Brigade==operBrigade))
        if(capIssueRec.rec.Operation==O_LOTTERY_SELL)
          lotterySellRec;
        end;
      end;
      recFound=capIssueRec.next;
    end;
  end;

  macro payWinReport

    var
      recFound;
   
    ClearRecord(CapIssueRec);
    capIssueRec.rec.Branch=Branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_LOTTERY;
  
    recFound=capIssueRec.getGE;
    while(recFound
      and (capIssueRec.rec.Branch==Branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_LOTTERY))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Oper==oper)
        and (capIssueRec.rec.Brigade==operBrigade))
        if(capIssueRec.rec.Operation==O_LOTTERY_PAYWIN)
          lotteryPayRec;
        end;
      end;
      recFound=capIssueRec.next;
    end;
  end;
  
  macro proceed

    sellBuyReport;
    payWinReport;     
  end;    

  macro isOpIncluded( issue, op )
    
    if ( ( op == O_LOTTERY_SELL ) or ( op == O_LOTTERY_PAYWIN ) )
      return true;
    end;   

    return false;
  end; 

  InitTCIRecGenerator( _oper, _makeReport, _observer );
end;

macro lotteryRecReport

  var
    gen = TLotteryRecGenerator( {oper}, true );

  gen.proceed;
end;

macro lotteryRec( applicationKind, applicationKey )

  var
    gen = TLotteryRecGenerator( {oper}, true );

  gen.printRec( applicationKind, applicationKey );
end;

macro lotteryReport

  var
    gen = TLotteryRecGenerator( {oper}, true );

  gen.f5_report;
end;