import rsd;

class TWsProcess
  var PersistentId : String;
  var TimeStampDate : Date;
  var TimeStampTime : Time;
  var Name : String;
  var IsFinished : bool;
  var IsAborted : bool;
  var CurrentActivityId : String;
end;

private macro toInt(b : bool)
  if (b)
    return 1;
  else
    return 0;
  end;
end; 

macro getProcessList(isFinished : bool, isAborted : bool)

   var retVal = TArray;
   var sql = "select p.t_id as persistentId, p.t_timeStamp as timeStamp, d.t_name as name, p.t_isFinished as isFinished, " + 
             "p.t_isAborted as isAborted, nvl(p.t_currentActivityId, chr(1)) as currentActivityId " +
             "from dwf_process_dbt p join dwf_procdef_dbt d on (p.t_definitionId = d.t_id) ";
             
   if ((isFinished != null) or (isAborted != null))
     sql = sql + "where ";
     if (isFinished != null)
       sql = sql + "p.t_isFinished = ? ";
     end;
     if (isAborted != null)
       if (isFinished != null)
         sql = sql + " and ";
       end;
       sql = sql + "p.t_isAborted = ? ";
     end;
   end;

   sql = sql + "order by p.t_id desc";
   var cmd = RsdCommand(sql);
   if (isFinished != null)
     cmd.addParam("p_isFinished", RsdBp_In, toInt(isFinished));
   end;
   if (isAborted != null)
     cmd.addParam("p_isAborted", RsdBp_In, toInt(isAborted));
   end;
   cmd.nullConversion = true;
   cmd.execute;
   
   var rs = RsdRecordset(cmd);
   while (rs.moveNext)
     var item = TWsProcess;
     item.PersistentId = rs.value("persistentId");
     var d, t;
     dtTmSplit(rs.value("timeStamp"), d, t);
     item.TimeStampDate = d;
     item.TimeStampTime = t;
     item.Name = rs.value("name");
     item.IsFinished = rs.value("isFinished");
     item.IsAborted = rs.value("isAborted");
     item.CurrentActivityId = rs.value("currentActivityId");
//     printprops(item);
     retVal[retVal.size] = item;
   end;

   return retVal;
end;

// getProcessList(true, null);