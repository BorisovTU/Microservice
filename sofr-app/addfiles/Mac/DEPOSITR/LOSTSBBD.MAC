/*
$Name:             lostsbbd.mac
$Module:           RS-Retail
$Description:      Операция по выдаче дубликата сберкнижки
*/

/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Операция по выдаче дубликата сберкнижки                                 *
*                                                                          *
*  Лебедев С.В.                                                            *
*  25.12.2002                                                              *
\**************************************************************************/

import deprintr, alg_vars, cur_rate;

file depositr   ( depositr ) key 8 write;
file currency   ( currency ) key 0;
file tarif      ( sb_tarif ) key 0;
file tarif_plus ( sb_tarpl ) key 0;
file lostbook   ( lostbook ) key 5 write;
file suboper    ( suboper  ) key 0;

record ALG_TRAST ( sbtrast  );

var depclnt = TClientList;

record pay_doc  ( pay_doc  );
record sbdepdoc ( sbdepdoc );

private const UPDATE_ALL  = 1;
private const UPDATE_DEP  = 2;
private const UPDATE_DOC  = 4;
private const UPDATE_COMCLNT = 16; // Обновление для общего клиента

const OP_DUBL_SBOOK = 206;

/* Состояние заявления об утрате сбер.книжки. */
const LBDS_ANY         = 0,  /* Любое      */
      LBDS_INITIALIZED = 1,  /* Введено    */
      LBDS_PROCESSED   = 2,  /* Обработано */
      LBDS_CANCELLED   = 3;  /* Отменено   */

/* Вид обработки заявления об утрате сбер.книжки. */
const LBDP_UNDEF     = 0,  /* Не определен                */
      LBDP_CASHCLOSE = 1,  /* Закрыть счет наличными      */
      LBDP_TRANSFER  = 2,  /* Перечислить остаток на счет */
      LBDP_REISSUE   = 3;  /* Выдать дубликат сб/кн       */      

const TarifSbook  = 17;  /* Код тарифа за утерю сберкнижки */
const TarifSbookP = 19;  /* Код тарифа за утерю сберкнижки для пенсионеров */

const MinRestNat  = $100L;
const MinRestBase = $10L;

var NumTarif   = 0;
var ComisSum   = $0L;
var MinRest    = $0L;
var KindPay    = 0;      /* 0 - наличными / 1 - безналичными / 2 - отказаться */
var CBRateCur  = 0;
var CBRateBase = 0;

var flag_LB = FALSE;  /* Флаг: найдена запись в табл.рег.утрач.сб\кн для счета. */

/**************************************************************************\
|       Поиск записи в таблице регистр.утрач. сб\кн для данного счета.     |
\**************************************************************************/
macro LB_find_Ref()
  KeyNum( lostbook, 5 );
  ClearRecord( lostbook );
  lostbook.Reference = ALG_DEPOSITOR.Referenc;
  lostbook.State     = LBDS_INITIALIZED;  /* Введено */
  return getGE( lostbook );
end;

/**************************************************************************\
|      Изменение записи в таблице регистрации утраченных сбер.книжек.      |
\**************************************************************************/
macro LB_Update()
  /* Состояние заявления */
  lostbook.State = LBDS_PROCESSED;  /* Обработано */
  /* Дата обработки заявления */
  lostbook.DateProcessed = {curdate};
  /* Номер операциониста, который обработал заявление */
  lostbook.ProcessedByOper = {oper};
  /* Номер дубликата сбер.книжки */
  lostbook.BookCopyNo = ALG_DEPOSITOR.NumberLostSBBook;
  return Update( lostbook );
end;

/**************************************************************************\
|                               Поиск тарифа                               |
\**************************************************************************/
macro FindTarif( isCur, Num, _Date, _Sum )

  var stat;

  tarif.IsCur = isCur;
  tarif.NumTarif = Num;

  stat = GetEQ( tarif );

  if ( stat and ( tarif.TarifPlus ) )
    tarif_plus.IsCur    = isCur;
    tarif_plus.NumTarif = Num;
    tarif_plus.Date     = _Date;
    tarif_plus.SumOper  = _Sum;
    if ( ( GetGE( tarif_plus )          ) and
         ( tarif_plus.IsCur    == 0     ) and
         ( tarif_plus.NumTarif == Num   ) and
         ( tarif_plus.Date     == _Date )    )
      tarif.PerSent = tarif_plus.Percent;
      tarif.MinSum  = tarif_plus.MinSum;
      tarif.MaxSum  = tarif_plus.MaxSum;
      tarif.Summa   = tarif_plus.Summa;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                  Определение тарифа для подвида операции                 |
\**************************************************************************/
macro DefineTarif( isCur, SubOpNum )

  var stat;

  suboper.IsCur = isCur;
  suboper.OperType = OP_DUBL_SBOOK;
  suboper.Type = SubOpNum;
  suboper.Kind = "Шаблонный";

  stat = GetEQ( suboper );

  if ( stat )
    NumTarif = suboper.NumTarif;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro NewDepDoc ( Doc )

  ClearRecord( Doc );
  Doc.Referenc         = ALG_DEPOSITOR.Referenc;
  Doc.IsCur            = ALG_DEPOSITOR.IsCur;
  Doc.FNCash           = ALG_DEPOSITOR.FNCash;
  Doc.RealFNCash       = NumRealFNCash( );
  Doc.Account          = ALG_DEPOSITOR.Account;
  Doc.Oper             = {oper};
  Doc.Type_Account     = ALG_DEPOSITOR.Type_Account;
  Doc.Code_Currency    = ALG_DEPOSITOR.Code_Currency;
  if ( ALG_TRAST.CodClient != 0 )
    Doc.CodClient = ALG_TRAST.CodClient;
    ALG_TRAST.CodClient = 0;
  else
    Doc.CodClient = ALG_DEPOSITOR.CodClient;
  end;
  Doc.YesSbook         = "X";
  Doc.Date_Document    = {curdate};
  Doc.DepDate_Document = {curdate};

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro InitRecsForDuplicateSBBook

  /* Формирование документа на наличную плату */
  if ( ( ComisSum > 0 ) and ( KindPay == 0 ) and ( StrFor( GetProgramID( ) ) != "П" ) )
    ClearRecord( pay_doc );
    pay_doc.FNCash           = NumRealFNCash( );
    pay_doc.IsCur            = ALG_DEPOSITOR.IsCur;
    pay_doc.NumOpert         = 5;
    pay_doc.GroupOpert       = 21; /* Платы и налоги */
    pay_doc.Date_Document    = {curdate};
    pay_doc.CodCur           = 0;
    pay_doc.Oper             = {oper};
    pay_doc.InSum            = ComisSum;
    pay_doc.ClientLastName   = depclnt.CurRec.rec.Name1;
    pay_doc.ClientFirstName  = depclnt.CurRec.rec.Name2;
    pay_doc.ClientSecondName = depclnt.CurRec.rec.Name3;
    pay_doc.FlagRez          = depclnt.CurRec.rec.NotResident;
    pay_doc.CodClient        = depclnt.CurRec.rec.CodClient;
    pay_doc.iApplicationKind = 200;
    pay_doc.ApplicationKey   = FormApplicationKey( 200 );

    PaySetGround( pay_doc );
    if ( pay_doc.Ground == "" )
      pay_doc.Ground = "Плата за выдачу дубликата сберкнижки по счету " + 
                       Acc_Form(ALG_DEPOSITOR.Account);
    end;
  end;

  /* Формирование документа по вкладу */
  NewDepDoc( sbdepdoc );
  sbdepdoc.TypeComplexOper  = ALG_RET_OP.Operation;
  sbdepdoc.TypeOper         = 62;
  sbdepdoc.ApplType         = 30;
  sbdepdoc.DepDate_Document = {curdate};
  sbdepdoc.KindOp           = 3;
  sbdepdoc.IsControl        = StrFor( 1 ); /* Признак главного документа */
  if ( KindPay == 1 )
    sbdepdoc.OutSum         = ComisSum;
  end;

  if ( StrFor( GetProgramID( ) ) != "П" )
    DepSetGround( sbdepdoc );
    if ( sbdepdoc.Ground == "" )
      sbdepdoc.Ground = "Выдача дубликата сберкнижки";
    end;
  else
    if ( ALG_SBDEPDOC.Ground != "" )
      sbdepdoc.Ground = ALG_SBDEPDOC.Ground;
    else
      DepSetGround( sbdepdoc );
      if ( sbdepdoc.Ground == "" )
        sbdepdoc.Ground = "Выдача дубликата сберкнижки";
      end;
    end;
  end;

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro T_UpdateRecsForDuplicateSBBook

  record save_depositr ( depositr );
  record save_lostbook ( lostbook );

  record WriteLog_rec  ( "WriteLog.rec" );
  record op_parm_rec   ( "op_parm.rec"  );
  record op_link_rec   ( "op_link.rec"  );
  record ret_op_dep    ( "ret_op.dep"   );

  var stat = true;
  var appl_key;
  var str_tmp;
  var indY;

  /* Обновление счета вкладчика */
  if ( stat )
    GetPos( depositr );
    stat = GetDirect( depositr );
  end;

  if ( stat )
    /* Удаление признака утери с/к из поля UserTypeAccount. */
    indY = Index( ALG_DEPOSITOR.UserTypeAccount, "Y" );
    if ( indY > 0 )
      if ( indY > 1 )
        ALG_DEPOSITOR.UserTypeAccount =
          SubStr( ALG_DEPOSITOR.UserTypeAccount, 1, indY - 1 ) +
          SubStr( ALG_DEPOSITOR.UserTypeAccount, indY + 1 );
      else
        ALG_DEPOSITOR.UserTypeAccount =
          SubStr( ALG_DEPOSITOR.UserTypeAccount, indY + 1 );
      end;
    end;

    // нужно будет установливать эти признаки для всех счетов по контракту
    
    ALG_DEPOSITOR.NumberLostSBBook = ALG_DEPOSITOR.NumberLostSBBook + 1;
    ALG_DEPOSITOR.Givebook         = "X";
    ALG_DEPOSITOR.NCurLine         = 0;
    ALG_DEPOSITOR.NumSession       = 0;
    ALG_DEPOSITOR.Action           = 1;

    ALG_CONTRACT.BookGiven         = "X";
    ALG_CONTRACT.Clear_AccountsFlagCh("Y");

    Copy( save_depositr, depositr );
    Copy( depositr, ALG_DEPOSITOR );

    stat = Update( depositr );
  end;

  /* Журнализация изменений в файл нефинансовых операций */
  if ( stat )
    ClearRecord( WriteLog_rec );
    WriteLog_rec.NumOperation = 3;
    WriteLog_rec.ObjectType   = ALG_RET_OP.Operation;
    WriteLog_rec.ObjectID     = String( 501 ) + "/" + String( depositr.Referenc );

    if ( WriteChangesToLog( WriteLog_rec, depositr, save_depositr ) != 0 )
      stat = false;
    end;
  end;

  /* Формирование операции */
  if ( stat )
    appl_key = "rtnfopr#";
    str_tmp = String( WriteLog_rec.NumDprt );
    while ( StrLen( str_tmp ) < 5 )
      str_tmp = "0" + str_tmp;
    end;
    appl_key = appl_key + str_tmp;
    str_tmp = String( WriteLog_rec.RecID );
    while ( StrLen( str_tmp ) < 10 )
      str_tmp = "0" + str_tmp;
    end;
    appl_key = appl_key + str_tmp;
    appl_key = appl_key + "00";

    if ( not InterDesk_IsDocBunchActive )
      ClearRecord( op_parm_rec );
      op_parm_rec.Date      = {curdate};
      op_parm_rec.Operation = ALG_RET_OP.Operation;
      op_parm_rec.SubOp     = ALG_RET_OP.SubOp;
      op_parm_rec.ObjectRef = depositr.Type_Account;
      op_parm_rec.CodCur    = depositr.Code_Currency;
      op_parm_rec.Flags     = 1 /*ROF_NONFINANCIAL*/;

      ClearRecord( ret_op_dep );
      ret_op_dep.accRef = depositr.Referenc;

      stat = InterDesk_InitDocBunch( 3001, appl_key, op_parm_rec, ret_op_dep );
    end;
  end;

  if ( stat )
    stat = CashLink_AddRec( 3001, appl_key );
  end;

  if ( stat )
    stat = UpdateAlgRecords( UPDATE_DEP, true, false );
  end;

  /* Формирование документа на наличную плату */
  if ( stat and ( ComisSum > 0 ) and ( KindPay == 0 ) and ( StrFor( GetProgramID( ) ) != "П" ) )
    stat = InsertPayDocToGDDList( pay_doc );
  end;

  /* Формирование документа по вкладу */
  if ( stat )
    stat = InsertDocToGDDList( sbdepdoc );
  end;

  if ( stat )
    if ( CarryGDDList( ) != 0 )
      stat = false;
    end;
  end;

  /* Формирование связи операции и объекта */
  if ( stat )
    ClearRecord( op_link_rec );
    op_link_rec.Date         = {curdate};
    op_link_rec.ObjectType   = 1;
    op_link_rec.ObjectRef    = String( depositr.Referenc );

    stat = LinkOpToObject( op_link_rec );
  end;

  if ( flag_LB )  /* Найдена запись в табл.рег.утрач.сб\кн для счета. */
    /* Установка позиции в таблице регистрации утраченных сбер.книжек. */
    if ( stat )
      GetPos( lostbook );
      stat = GetDirect( lostbook );
    end;

    /* Отметка отмены в таблице регистрации утраченных сбер.книжек. */
    if ( stat )
      Copy( save_lostbook, lostbook );
      stat = LB_Update();
    end;

    /* Журнализация обновления в lostbook.dbt в файл нефинансовых операций */
    if ( stat )
      ClearRecord( WriteLog_rec );
      WriteLog_rec.NumOperation = 3;  /* Update */
      WriteLog_rec.ObjectType   = ALG_RET_OP.Operation;
      WriteLog_rec.ObjectID     = String( 517 ) + "/" + String( lostbook.ID );

      if ( WriteChangesToLog( WriteLog_rec, lostbook, save_lostbook ) != 0 )
        stat = false;
      end;
    end;

    /* Формирование операции */
    if ( stat )
      appl_key = "rtnfopr#";
      str_tmp = String( WriteLog_rec.NumDprt );
      while ( StrLen( str_tmp ) < 5 )
        str_tmp = "0" + str_tmp;
      end;
      appl_key = appl_key + str_tmp;
      str_tmp = String( WriteLog_rec.RecID );
      while ( StrLen( str_tmp ) < 10 )
        str_tmp = "0" + str_tmp;
      end;
      appl_key = appl_key + str_tmp;
      appl_key = appl_key + "00";

      if ( not InterDesk_IsDocBunchActive )
        ClearRecord( op_parm_rec );
        op_parm_rec.Date      = {curdate};
        op_parm_rec.Operation = ALG_RET_OP.Operation;
        op_parm_rec.SubOp     = ALG_RET_OP.SubOp;
        op_parm_rec.ObjectRef = lostbook.Number;
        op_parm_rec.CodCur    = depositr.Code_Currency;
        op_parm_rec.Flags     = 1 /*ROF_NONFINANCIAL*/;

        ClearRecord( ret_op_dep );
        ret_op_dep.accRef = depositr.Referenc;

        stat = InterDesk_InitDocBunch( 3001, appl_key, op_parm_rec, ret_op_dep );
      end;
    end;

    if ( stat )
      stat = CashLink_AddRec( 3001, appl_key );
    end;
  end;

  if ( not stat )
    AbortTrn( );
  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  ALG_RESULT = OK_RES;

  if ( ALG_RESULT == OK_RES )
    if ( Index( ALG_DEPOSITOR.UserTypeAccount, "Y" ) )
      if ( LB_find_Ref() )
        if ( lostbook.Processing == LBDP_REISSUE )  /* Выдать дубликат с/к. */
          flag_LB = TRUE;  /* Флаг: найдена запись в табл.рег.утрач.сб\кн для счета. */
        else
          if ( StrFor( GetProgramID( ) ) != "П" )
            MsgBox( "В заявлении не указан вид обработки \"Выдать дубликат с/к\"." );
          end;
          ALG_RESULT = SKIP_RES;
        end;
      else
        if ( NOT GetTrue( FALSE, "По счету заявление об утере с/к не найдено.|" +
                                 "Продолжить выполнение операции?" ) )
          if ( StrFor( GetProgramID( ) ) != "П" )
            MsgBox( "Для оформления заявления об утере с/к " +
                    "выполните операцию \"Утеря сберкнижки\"" );
          end;
          ALG_RESULT = SKIP_RES;
        end;
      end;
    end;
  end;

  if ( ALG_RESULT == OK_RES )
    if ( ( NOT flag_LB )  AND  ( ALG_CONTRACT.BookGiven == "" ) )
      if ( StrFor( GetProgramID( ) ) != "П" )
        MsgBox( "По счету сберкнижка не выдавалась" );
      end;
      ALG_RESULT = SKIP_RES;
    end;
  end;

  if ( ( ALG_RESULT == OK_RES ) AND ( StrFor( GetProgramID( ) ) != "П" ) )
    if ( not GetTrue( true, "Провести операцию по выдаче дубликата сберкнижки ?" ) )
      ALG_RESULT = SKIP_RES;
    end;
  end;

  /* Поиск счета */
  if ( ALG_RESULT == OK_RES )
    ClearRecord( depositr );
    depositr.Referenc = ALG_DEPOSITOR.Referenc;
    if ( not GetEQ( depositr ) )
      if ( StrFor( GetProgramID( ) ) != "П" )
        MsgBox( "Не найден счет с референсом " + String( ALG_DEPOSITOR.Referenc ) );
      end;
      ALG_RESULT = ERR_RES;
    end;
  end;

  /* Поиск клиента */
  if ( ALG_RESULT == OK_RES )
    if ( not depclnt.GetRecord( ALG_DEPOSITOR.CodClient ) )
      if ( StrFor( GetProgramID( ) ) != "П" )
        MsgBox( "Не найден клиент с кодом " + String( ALG_DEPOSITOR.CodClient ) );
      end;
      ALG_RESULT = ERR_RES;
    end;
  end;

  /* Определение минимальной суммы, с которой не берется плата */
  if ( ALG_RESULT == OK_RES )
    if ( ALG_DEPOSITOR.Code_Currency == 0 )
      MinRest = MinRestNat;
    elif ( ALG_DEPOSITOR.Code_Currency == 1 )
      MinRest = MinRestBase;
    else
      if ( not GetAllCurrRate( {curdate}, ALG_DEPOSITOR.Code_Currency, CBRateCur ) )
        ALG_RESULT = ERR_RES;
        if ( StrFor( GetProgramID( ) ) != "П" )
          MsgBox( "Ошибка при определении курса валюты счета" );
        end;
      else
        if ( not GetAllCurrRate( {curdate}, 1, CBRateBase ) )
          ALG_RESULT = ERR_RES;
          if ( StrFor( GetProgramID( ) ) != "П" )
            MsgBox( "Ошибка при определении курса базовой валюты" );
          end;
        end;
      end;
      if ( ALG_RESULT == OK_RES )
        MinRest = Money( MinRestBase * CBRateBase / CBRateCur );
      end;
    end;
  end;

  /* Определение тарифа */
  if ( ALG_RESULT == OK_RES )
    if ( ALG_DEPOSITOR.Sum_Rest <= MinRest )
      NumTarif = 0;
    elif ( not DefineTarif(ALG_DEPOSITOR.IsCur, ALG_RET_OP.SubOp) )
      NumTarif = 0;
    end;

    if ( NumTarif > 0 )
      if ( not FindTarif( ALG_DEPOSITOR.IsCur, NumTarif, Date( 0, 0, 0 ), $0L ) )
        if ( StrFor( GetProgramID( ) ) != "П" )
          MsgBox( "Не найден тариф за утерю сберкнижки" );
        end;
        ALG_RESULT = ERR_RES;
      end;
    else
      ClearRecord( tarif );
    end;
  end;

  if ( ALG_RESULT == OK_RES )
    ComisSum = tarif.Summa;
    if ( ( StrFor( GetProgramID( ) ) == "П" ) and ( ALG_SBDEPDOC.OutSum != 0 ) )
      ComisSum = ALG_SBDEPDOC.OutSum;
    end;
  end;

  /* Запрос - как брать плату */
  if ( ( ALG_RESULT == OK_RES ) AND ( ComisSum > 0 ) )
    if ( StrFor( GetProgramID( ) ) == "П" )
      if ( ALG_SBDEPDOC.OutSum != 0 )
        KindPay = 1;
        ComisSum = ALG_SBDEPDOC.OutSum;
      else
        KindPay = 0;
      end;
    else
      currency.Code_Currency = 0;
      if ( not GetEQ( currency ) )
        ClearRecord( currency );
      end;
      if ( ALG_DEPOSITOR.IsCur )
        KindPay = ConfDlg( "Плата за выдачу дубликата сберкнижки в размере " + String( ComisSum:0:2:a ) + " " + currency.Short_Name + " берется ?", NULL, "Наличными", "Не берется" );
        if ( KindPay == 1 )
          KindPay = 2;
        end;
      else
        KindPay = ConfDlg( "Плата за выдачу дубликата сберкнижки в размере " + String( ComisSum:0:2:a ) + " " + currency.Short_Name + " берется ?", NULL, "Наличными", "Безналичными", "Не берется" );
      end;
    end;

    if ( KindPay == 2 )
      if ( StrFor( GetProgramID( ) ) != "П" )
        MsgBox( "Нельзя провести операцию без взятия платы" );
      end;
      ALG_RESULT = SKIP_RES;
    end;
  end;

  if ( ALG_RESULT == OK_RES )
    InitRecsForDuplicateSBBook( );
    if ( ProcessTrn( 0, "T_UpdateRecsForDuplicateSBBook", depositr ) )
      ALG_RESULT = OK_RES;
    else
      if ( StrFor( GetProgramID( ) ) != "П" )
        MsgBox( "Ошибка при проводке по выдаче дубликата сберкнижки" );
      end;
      ALG_RESULT = ERR_RES;
      InterDesk_EndDocBunch( );
    end;
  end;

end;
