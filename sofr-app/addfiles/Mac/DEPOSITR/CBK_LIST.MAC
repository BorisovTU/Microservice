/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Справка о состоянии вклада(ов).

  Ведомость счетов, по которым предоставлялась справка
  о состоянии вкладов, закрытых другими дополнительными офисами.

*****************************************************************/

import DeprIntr, OperCode, SqlConv, RSD;

var aux = TBFile( "auxinfo.dbt", "r", 0 );

var aux_21 = TRecHandler( "auxinfo.21" );  /* Доп.информация по Справке о состоянии вклада  */

var depclnt = TClientList; /* Справочник вкладчиков */

const FNCash = NumFNCash();

const AI_CBK_PERIODICAL = 21;  /* Справка о сост.вкладов: периодичность */

var sFIO_Declarer = "";
var sDeclare_Date = "";

/****************************************************************
                    Чтение записи о Филиале
****************************************************************/
macro find_Branch( _FNCash )
  var sFilial;

  if ( not findDepartment(_FNCash, sFilial) )
    sFilial = "???";
  end;

  return sFilial;

end;

/****************************************************************
                  Поиск записи в auxinfo.dbt
****************************************************************/
macro find_Aux_Info( _Ref, _Date )

  var stat;

  aux.Clear;
  aux.rec.Reference = _Ref;
  aux.rec.InfoType  = AI_CBK_PERIODICAL;

  aux.addFilter( "t.t_Reference = " + string( _Ref ) +
                 " and t.t_InfoType = " + string( AI_CBK_PERIODICAL ) + 
                 " and t.t_Date <= " + sqlDateToStr( _Date ) );
  stat = aux.getGE;

  aux.dropFilter;

  if ( stat  AND
       ( aux.rec.Reference == _Ref )   AND
       ( aux.rec.InfoType  == AI_CBK_PERIODICAL )  AND
       ( aux.rec.Date      <= _Date )  AND
       ( aux.rec.FNCash    == FNCash ) )
    aux_21.SetRecordAddr( aux, 0, 0, TRUE );
    if ( aux_21.rec.Status == 0 )
      if ( depclnt.GetRecord( aux_21.rec.CodDeclarer ) )
        sFIO_Declarer = depclnt.CurRec.rec.Name1 + " " +
                        depclnt.CurRec.rec.Name2 + " " +
                        depclnt.CurRec.rec.Name3;
      else
        sFIO_Declarer = "???";
      end;
      sDeclare_Date = String( aux.rec.Date );
      return TRUE;
    end;
  end;

  return FALSE;

end;

/****************************************************************
                        Головная процедура
*****************************************************************/
macro CBK_Main( Ref, Date1, Date2, _sClient, _sFIO, CodClnt, Standart,
                Archive, Period, _sControlInfo )

  /*
     Алгоритм формирования "Ведомости счетов":
     - перебор документов данного филиала за указанную дату,
       у которых doc.TypeComplexOper == "Закрытие счета",
       с признаком "Закрыт в другом структурном подразделении";
     - для каждого такого документа ищем запись в auxinfo.dbt
       "Заявление на выдачу справки" - если такая запись есть -
       формируем строку "Ведомости".
  */

  var cmd, rs;
  var stat;
  var nn = 1;

  var sDateClose = "";
  var sOurBranch = find_Branch( FNCash );
  var sOtherBranch = "";
  var dep_name;




  [
    Дополнительный офис N #########



                         ВЕДОМОСТЬ СЧЕТОВ, ПО КОТОРЫМ ПРЕДОСТАВЛЯЛАСЬ СПРАВКА О СОСТОЯНИИ ВКЛАДА(ОВ) Ф.N 297,
                                                 ЗАКРЫТЫХ ДРУГИМИ ДОПОЛНИТЕЛЬНЫМИ ОФИСАМИ

                                                           ЗА  ##########

    +-----------------------------------------------------------------------------------------------------------------------------+
    | N N |      Фамилия, Имя, Отчество     |           Номер счета        |    Дата    |   Номер   |   Номер и дата оформления   |
    | п/п |            вкладчика            |                              |  закрытия  | дополни-  |     заявления ф. N 298      |
    |     |                                 |                              |  счета по  | тельного  |                             |
    |     |                                 |                              |   вкладу   | офиса, в  |                             |
    |     |                                 |                              |            |  котором  |                             |
    |     |                                 |                              |            | совершена |                             |
    |     |                                 |                              |            | операция  |                             |
    |     |                                 |                              |            | по закры- |                             |
    |     |                                 |                              |            | тию счета |                             |
    |     |                                 |                              |            | по вкладу |                             |
    +-----+---------------------------------+------------------------------+------------+-----------+-----------------------------+
    |  1  |                2                |                 3            |      4     |     5     |               6             |
    +-----+---------------------------------+------------------------------+------------+-----------+-----------------------------+]
  ( sOurBranch, Date1 );


  cmd = RsdCommand( "select doc.t_account as account, " +
                           "doc.t_referenc as reference, " +
                           "lfd.t_fnCash as otherBranch " +
                    "from dsbdepdoc_dbt doc, dlistfdep_dbt lfd " +
                    "where doc.t_isCur = ? " +
                      "and doc.t_fnCash = ? " +
                      "and doc.t_Date_Document = ? " +
                      "and doc.t_realFnCash != doc.t_fnCash " +
                      "and doc.t_typeComplexOper in ( " + string( Закрытие_Наличными    ) + ", " +
                                                          string( Закрытие_Безналичными ) + ", " +
                                                          string( Закрытие_Переводом    ) + " ) " +
                      "and lfd.t_fnCash = doc.t_realFNcash " +
                      "and exists ( select 0 from dauxinfo_dbt aux " +
                                   "where aux.t_reference = doc.t_referenc " +
                                     "and aux.t_infoType = " + string( AI_CBK_PERIODICAL ) + 
                                     "and aux.t_date <= doc.t_date_document ) " +
                    "group by doc.t_referenc, doc.t_account, lfd.t_fnCash" );

  cmd.addParam( "isCur", RSDBP_IN );
  cmd.addParam( "branch", RSDBP_IN );
  cmd.addParam( "date", RSDBP_IN );

  cmd.value( "isCur" ) = 0;
  cmd.value( "branch" ) = FNCash;
  cmd.value( "date" ) = Date1;

  cmd.execute;

  rs = RsdRecordset( cmd );

  while ( rs.moveNext )
    if ( find_Aux_Info( rs.value( "reference" ), Date1 ) )
      dep_name = find_Branch( rs.value( "otherBranch" ) );
      [ | ### | ############################### | ############################ | ########## | ######### | ########################### | ]
      ( nn:3,
        sFIO_Declarer:30:w,
        Acc_Form(String(rs.value( "account" ))):f:28,
        Date1:10,
        dep_name:9,
        sDeclare_Date:25:w );
      nn = nn + 1;
    end;
  end;

  cmd.value( "isCur" ) = 1;
  cmd.value( "branch" ) = FNCash;
  cmd.value( "date" ) = Date1;

  cmd.execute;

  rs = RsdRecordset( cmd );

  while ( rs.moveNext )
    if ( find_Aux_Info( rs.value( "reference" ), Date1 ) )
      [ | ### | ############################### | ############################ | ########## | ######### | ########################### | ]
      ( nn:3,
        sFIO_Declarer:30:w,
        Acc_Form(String(rs.value( "account" ))):f:28,
        Date1:10,
        rs.value( "otherBranch" ):9,
        sDeclare_Date:25:w );
      nn = nn + 1;
    end;
  end;

  [ +-----+---------------------------------+------------------------------+------------+-----------+-----------------------------+]

end;
