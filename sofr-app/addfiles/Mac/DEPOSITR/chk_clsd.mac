/*************************************************************************
  RS-Bank Сбебанка
  Макоpос пpовеpки базы пеpед запуском пpоцедуpы закpытия дня

  Список не обаботанных отложенных документов по вкладам и
  Список неподтвежденных кассовых документов на сегодня

  06.12.97  Косточко П.М.
**************************************************************************/

import deprintr, notcrdsk, chk_clup, rsd;

file PsDoc  ("psdepdoc.dbt") key 1;
file TypeOp ("sb_typop.dbt") key 0;
file SubOper("suboper.dbt" ) key 1;
file Group  ("rtgroup.dbt" ) key 1;
file CashVal("sb_casvl.dbt") key 0;

const
  GT_ORDINARY = 0,                      /* Обычная группа */
  GT_BRIGADE  = 1,                      /* Бригада        */

  D_DELETE    = 2,
  D_REJECT    = 50,

  BS_BRANCH   = 0,                      /* Филиал         */
  BS_DEP      = 1,                      /* Отделение      */
  BS_EXCH     = 2,                      /* Обменный пункт */
  BS_OPERDEP  = 3;                      /* Оперчасть      */


var
  FlagCur = NumFlagCur,
  FNCash  = NumFNCash,
  Op,
  SubOp,
  Sum;

macro OutHead;

  Print("                 Отчет по отложенным документам за "
    +String({curdate}));
  if(FlagCur)
    PrintLn(" (валюта)");
  else
    PrintLn(" (рубли)");
  end;
  PrintLn;
  [+----+-------------------------+------------------+------------+-------------------+-----+];
  [| №  |        С ч е т          |    С у м м а     |  Операция  |  Подвид операции  | № п |];
  [+----+-------------------------+------------------+------------+-------------------+-----+];
end;

macro OutLine;

  [|####|#########################|##################|############|###################|#####|]
  (
    PsDoc.NDoc,
    Acc_Form(PsDoc.Account),
    Sum,
    Op,
    SubOp,
    PsDoc.NPack
  );
end;

macro GetReportData;

  if(PsDoc.InSum>$0)
    Sum=PsDoc.InSum;
  else
    Sum=PsDoc.OutSum;
  end;

  TypeOp.IsCur=PsDoc.IsCur;
  TypeOp.Kind=PsDoc.Type_Account;
  TypeOp.NumOpert=PsDoc.TypeOper;
  if(GetEQ(TypeOp))
    Op=TypeOp.czNameAlg;
  else
    Op="Не найдена";
  end;

  if(PsDoc.TypeOper!=0)
    SubOper.IsCur    = PsDoc.IsCur;
    SubOper.Kind     = PsDoc.Type_Account;
    SubOper.OperType = PsDoc.TypeOper;
    SubOper.Type     = PsDoc.ApplType;
    if(GetEQ(SubOper))
      SubOp=SubOper.Name;
    else
      SubOp="Не найдена";
    end;
  else
    SubOp="";
  end;
end;

macro Список_незакрытых_бригад

  var
    RecFound,
    N = 0;

  [ ];
  [ ];
  [   Незакрытые бригады пользователей - ##########]({curdate}:c);
  [ ];
  [---------------------------------------------------];
  [|  №  |              О п и с а н и е              |];
  [---------------------------------------------------];
  ClearRecord(Group);
  Group.Branch = Branch;
  Group.Type = GT_BRIGADE;
  Group.Date = {curdate};
  Group.IsActive = "X";
  RecFound = GetGE(Group);
  while(RecFound
    and (Group.Branch == Branch)
    and (Group.Type == GT_BRIGADE)
    and (Group.Date == {curdate})
    and (Group.IsActive == "X"))
    N = N + 1;
    [ ##### ########################################### ]
    (Group.ID, Group.Desc);
    RecFound = Next(Group);
  end;
  [---------------------------------------------------];
  if(N > 0)
    [ Всего незакрытых бригад: ### ](N);
  end;
  [ ];
  return N;
end;

macro GetNativeSafe

  file Group("rtgroup.dbt") key 2;

  var
     OperBrigade = GetOperBrigade();

  Group.Branch = FNCash;
  Group.Date = {curdate};
  Group.ID = OperBrigade;
  if(GetEQ(Group))
    return Group.SafeID;
  else
    MsgBox("Не найден сейф бригады № ", OperBrigade);
    Exit;
  end;
end;

macro ReturnTypeCurrency
  if(FlagCur)
    return " (валюта)";
  else
    return " (рубли)";
  end;
end;

macro Отчет_по_непроведенным_документам

  printLn();
  printLn();
  [            Отчет по непроведенным документам за ##########   # ] 
  ( {curdate},
    ReturnTypeCurrency  );

  // Ежели будет здесь сильно тормозить, то придется создавать в депдоке индекс по изсуспендеду
  var cmd, rs;
  var n = 0;
/*  cmd = RsdCommand(" select t_Account as Acc, t_InSum - t_OutSum as Sum, t_TypeOper as Oper, t_TypeComplexOper as CompOper, t_Date_Document as DateDoc "  
                   " from dsbdepdoc_dbt "
                   " where t_FNcash = ? "
                     " and t_IsCur = ? "
                     " and t_issuspended = 'X' "); */
  cmd = RsdCommand(" select s.t_Account as Acc, s.t_InSum - s.t_OutSum as Sum, s.t_TypeOper as Oper,  s.t_TypeComplexOper as CompOper, s.t_Date_Document as DateDoc "
                   " from ddepositr_dbt d, dsbdepdoc_dbt s "
                   " where d.t_FNcash = ? "
                     " and d.t_IsCur = ?  "
                     " and d.t_numsuspendeddocs > 0 "
                     " and d.t_referenc = s.t_referenc "
                     " and s.t_issuspended = 'X' ");
  cmd.addParam("p_FNcash", RsdBp_in );
  cmd.value("p_FNcash") = FNCash;
  cmd.addParam("p_IsCur", RsdBp_in );
  cmd.value("p_IsCur") = FlagCur;
  
  cmd.execute();
  rs = RsdRecordset( cmd );
  while( rs.MoveNExt )
    if ( n == 0 )
      [+-------------------------+------------------+------------+-------------------+---------------+];
      [|        С ч е т          |    С у м м а     |  Операция  |  Подвид операции  | Дата операции |];
      [+-------------------------+------------------+------------+-------------------+---------------+];
    end;

	[|######################## |################# |##########  |  ###############  | ############# |]
	( Acc_Form(String(rs.value("Acc"))):25: r,
      rs.value("Sum"):12:2,
      rs.value("Oper"),
      rs.value("CompOper"),
      date(rs.value("DateDoc")) );
    n = n + 1;  
  end;
  if ( n > 0 )
    [+-------------------------+------------------+------------+-------------------+---------------+];
    printLn();
    printLn();
  else        
    printLn();
    printLn("                     Непроведенных документов нет");
    printLn();
  end;

  return n > 0;
end; 

  var
    RecFound,
    NDoc = 0,
    NCDoc = 0,
    NCBrig = 0;
  var isControlPsDoc = false;
  var reg_error = 0;

  Начало_Конец_отчета ();

  OutHead;

  GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\ОТКРЫТИЕ_ЗАВЕРШЕНИЕ_ДНЯ\\КОНТРОЛЬ_ОТЛОЖЕННЫХ_ПРИ_ЗАКР",
                    V_BOOL, isControlPsDoc, reg_error );
  if ( reg_error )
    isControlPsDoc = false;
  end;

  ALG_RESULT = 0;
  if ( isControlPsDoc!=false )
    ClearRecord(PsDoc);
    PsDoc.IsCur=FlagCur;
    PsDoc.FNCash=FNCash;
    RecFound=Next(PsDoc);
    while(RecFound and (PsDoc.IsCur==FlagCur) and (PsDoc.FNCash==FNCash))
      if((PsDoc.Action!=D_DELETE) and (PsDoc.ApplType != D_REJECT))
        NDoc = NDoc+1;
        GetReportData;
        OutLine;
      end;
      RecFound=Next(PsDoc);
    end;
    if(NDoc == 0)
      PrintLn(" Необpаботанных отложенных документов нет");
    else
      ALG_RESULT = 3;
    end;
  else
     PrintLn(" Проверка на наличие отложенных документов не выполнялась");
  end;

/* неподтвеpжденые кассовые документы */

  [ ];
  Разделитель_1 ();
  [ ];

  NCDoc = Список_непотв_кас_док();

  if(NCDoc == 0)
    PrintLn(" Неподтвержденных кассовых документов нет");
  else
    ALG_RESULT = 3;
  end;

  if ( Отчет_по_непроведенным_документам() )
    ALG_RESULT = 3;
  end;

  NCBrig = Список_незакрытых_бригад();

  if(NCBrig == 0)
    PrintLn(" Незакрытых бригад пользователей нет\n");
  end;

  /* Напоминание о необходимости очистки базы */
  ReportOnCleanUp({curdate});

  Начало_Конец_отчета ();
// Для вывода прогноза просрочки кредитов раскомменитровать следующую строку
// ExecMacroFile("chk_exp","ПрогнозПросрочки");

end;

/****************************************************************************/


