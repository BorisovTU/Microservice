/***************************************************************************
* SCHOOL.MAC Пример формирования списка на зачисления выигрышей            *
* Автор Зубов В.Ю.  Начало: 1-07-97                                        *
***************************************************************************/
/* Реализован следующий механизм расчета сумм выигрышей
 - Подсчитывается количество счетов данного вида вклада ВСЕГО_СЧЕТОВ в одном
   филиале и общий остаток на этих счетах ОБЩИЙ_ОСТАТОК,
 - Вычисляется сумма общая сумма выигрышей
   ОБЩАЯ_СУММА = ОБЩИЙ_ОСТАТОК * ПРОЦЕНТ / 100,
 - Вычисляется сумма зачисления на один счет
   СУММА_НА_СЧЕТ = ОБЩАЯ_СУММА / ВСЕГО_СЧЕТОВ;
 - Формируется файл для загрузки в отложенные документы SCHOOL.PSD
*/
import deprintr;
Var
    ВИД_ВКЛАДА = "ШКОЛЬНЫЙ",
    ПРОЦЕНТ = 10,
    ОБЩИЙ_ОСТАТОК = 0,
    ВСЕГО_СЧЕТОВ = 0,
    ОБЩАЯ_СУММА = 0,
    СУММА_НА_СЧЕТ = 0,
    found,
    tmpStr,
    i,
    Номер_Пачки = 0,
    Вид_Зачисления = 3,
    Строка;

file depositr ("depositr.dbt"); /* Файл счетов */
file out("school.psd") txt write; /* выходной файл */

 /* Перебор счетов */
 KeyNum(depositr,1);
 depositr.IsCur =   0;          /* Рублевые счета */
 depositr.FNCash =  NumFNcash ; /* Номер филиала */
 depositr.Open_Close = "";       /* Только открытые счета */
 depositr.Type_Account = ВИД_ВКЛАДА;
 depositr.Code_Currency = 0;
 if(GetGE(depositr))
   found=True;
   while( (depositr.IsCur == 0) AND
          (depositr.FNCash == NumFNcash) AND
          (depositr.Open_Close == "") AND
          (depositr.Type_Account == ВИД_ВКЛАДА) AND
          (depositr.Code_Currency == 0) AND
          (found == True))
     ВСЕГО_СЧЕТОВ = ВСЕГО_СЧЕТОВ +1;
     ОБЩИЙ_ОСТАТОК = ОБЩИЙ_ОСТАТОК + depositr.Sum_Rest;
     if(not Next(depositr))
       found = False;
     end; /* IF */
   end; /* of WHILE */
 end;/* IF */

 if(ВСЕГО_СЧЕТОВ == 0)
   println("Не найдено ни одного счета");
   return;
 end; /* IF */

 ОБЩАЯ_СУММА = ОБЩИЙ_ОСТАТОК * ПРОЦЕНТ / 100;
 СУММА_НА_СЧЕТ = ОБЩАЯ_СУММА / ВСЕГО_СЧЕТОВ;

 /* Снова перебор счетов */
 KeyNum(depositr,1);
 depositr.IsCur =   0;          /* Рублевые счета */
 depositr.FNCash =  NumFNcash ; /* Номер филиала */
 depositr.Open_Close = "";       /* Только открытые счета */
 depositr.Type_Account = ВИД_ВКЛАДА;
 depositr.Code_Currency = 0;
 if(GetGE(depositr))
   found=True;
   i = 1;
   while( (depositr.IsCur == 0) AND
          (depositr.FNCash == NumFNcash) AND
          (depositr.Open_Close == "") AND
          (depositr.Type_Account == ВИД_ВКЛАДА) AND
          (depositr.Code_Currency == 0) AND
          (found == True))
     /* вывод в файл */
     /* 0 - N3 Номер документа*/
     tmpStr = MkStr(" ", 3);
     StrSet(tmpStr,1,String(i));
     Строка = Trim(tmpStr);

     /* 1 - N5 Номер пачки */
     tmpStr = MkStr(" ", 5);
     StrSet(tmpStr,1,String(Номер_Пачки));
     Строка = Строка + "," + Trim(tmpStr);

     /* 2 - C17 Номер счета */
     tmpStr = MkStr(" ",17);
     StrSet(tmpStr,1,Acc_Form(depositr.Account));
     Строка = Строка + "," + Trim(tmpStr);

     /* 3 - N19.2  Сумма (в копейках и центах)*/
     tmpStr = MkStr(" ",21);
     StrSet(tmpStr,1,String(СУММА_НА_СЧЕТ));
     Строка = Строка + "," + Trim(tmpStr);

     /* 4 - N1 Вид зачисления */
     tmpStr = MkStr(" ", 1);
     StrSet(tmpStr,1,String(ВИД_ЗАЧИСЛЕНИЯ));
     Строка = Строка + "," + Trim(tmpStr);

     /* 5 - C60 Основание */
     tmpStr = MkStr(" ", 60);
     StrSet(tmpStr,1,"Зачисление выигрышей на школьные вклады");
     Строка = Строка + "," + Trim(tmpStr);

     out.str = Строка;
     insert(out);

     if(not Next(depositr))
       found = False;
     end; /* IF */
   i = i+1;
   end; /* of WHILE */
 end;/* IF */
 Println("Файл SCHOOL.PSD сформирован");