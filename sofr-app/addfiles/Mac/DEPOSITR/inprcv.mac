/*
$Name:             inprcv.mac
$Module:           RS-Retail
$Description:      Ввод информации о получателе/отправителе платежа.
*/

/*
  Операции: по вкладам:
            63
            64
            67
            71   
            73
            прочие:
            20
            42
*/

import CommonInter, CTInter;
import alg_vars, opercode, inprccmn, rtpm_lib, cur_rate, bankdefs, rsd;
import RsbDataSet;
import PmPropUtil;
import rtutils, ErrHandler;
/***************************************************************** константы */
const
   K_ENTER = 13,
   K_TAB = 9,
   F1  = 315,
   F2  = 316,
   F3  = 317,
   F4  = 318,
   F5  = 319,
   F6  = 320,
   F7  = 321,
   F8  = 322,
   F9  = 323,
   F10 = 324,
   CtrlF3 = 352;
const
   UPDATE_DOC = 4;
const
   UPDATE_SBDOC = 4;
const
   CODEKIND_BIC = 3;
const
   SB_CASHOPERT = 200;
const
   ATI_SK_LOST = 30,  /* С/к: утеря */
   ATI_SK_GET  = 31;  /* С/к: выдача */
const
   DEF_PRIORITY     = 5,
   DEF_PRIORITY_TAX = 3;
const OBJTYPE_TAXPAYTYPE = 1802,
      CLASSIF_TAXPAYMENTTYPE = 4;
/********************************************************************* файлы */
file depdoc  ("sbdepdoc.dbt");
file rtcp_recipient("rtcp_recipient.dbt") key 0;
file rtcp_type("rtcp_type.dbt") key 0;
file dplanpay("dplanpay.dbt") key 3;
file currency("currency.dbt") key 0;
file pay_doc ("pay_doc.dbt" ) key 2;
file sb_casln("sb_casln.dbt") key 0;
file trn_payf("trn_payf.dbt") key 0;
file trn_cntr("trn_cntr.dbt") key 0;
file auxinfo ("auxinfo.dbt" ) key 0;

file rt_paymf("rt_paym.dbt" ) write;

private var partcode = Tbfile( "partcode.dbt", "r", 1 );
private var party    = Tbfile( "party.dbt", "r", 0 );
private var fininstr = Tbfile( "fininstr.dbt", "r", 12 );

private var AttrBuff = TRecHandler("objattr.dbt");

Private var ClientList = TClientList;

private record auxinfo11 ( "auxinfo.11" );

/***************************************************************** структуры */
record ALG_SBDEPDOC_1("sbdepdoc.1");
record varPartForTrasnf( "rt_paym.fpm" ); // Переводы.
private record grdrec( "ground.rec" );
/******************************************************************** панели */
record otherP_INxC63xx( "INxC63xx", "sbbank.lbr" ) dialog;
record otherP_INOT63xx( "INOT63xx", "sbbank.lbr" ) dialog;
record P_INxC6306( "INxC6306", "sbbank.lbr" ) dialog;
record P_INxC6307( "INxC6307", "sbbank.lbr" ) dialog;
record P_INOT6307( "INOT6307", "sbbank.lbr" ) dialog;
/**************************************************************** переменные */
private var retValue = true, usedDlg  = NULL, globalActions;
private var isFillOnlyRtPaym = false /* rt_paym первичен? */;

private var vPartyID = -1, vFiID = -1;
private var notActive = false; // Признак неактивности заблокированной панели
private var glTaxAuthorStateCorrect = true;

/*********************************************/
macro getDefaultCodeKind
  if( NumFlagCur() )
    return 6; // SWIFT
  else
    return 3; // BIC
  end;
  
end;
/******************************************** замена (кривая) copy (кривого) */
macro xackCopy( to, from /* одинаковые по стр-ре  */ )
  var i = 0;
  while( i < to.fldNumber )
    to.item( i ) = from.item( i );
    i = i + 1;
  end;
end;
//---------------------------------------------------------------------------
private macro zeroStringIfEmpty(val)
  if ((ValType(val) == V_UNDEF) or (val == ""))
    return "0";
  end;
  return string(val);
end;
/******************************************************** получить имя банка */
private macro getOtherBankName( BankCode, CodeKind )
  var name = "";
  var p4, p5, p6;
  
  if( CodeKind == null ) CodeKind = CODEKIND_BIC; end;
  iFindBank( CodeKind, BankCode, name, p4, p5, p6, vPartyID );
  return name;
end;
/********************************************* найти получателя ком. платежа */
private macro findCPRecipient( id )
  rewind( rtcp_recipient );
  clearRecord( rtcp_recipient );
  rtcp_recipient.id = id;
  return getEQ( rtcp_recipient );
end;
/**************************************************** найти вид ком. платежа */
private macro findCPType( id )
  rewind( rtcp_type );
  clearRecord( rtcp_type );
  rtcp_type.id = id;
  return getEQ( rtcp_type );
end;
/*********************************************** найти плановое перечисление */
private macro findPlanPay( ref )
  rewind( dplanpay );
  clearRecord( dplanpay );
  dplanpay.Ref = ref;
  return getEQ( dplanpay );
end;
/****************************************** найти расшифровку п/п по филиалу */
private macro findPayF( ref )
  var retVal = false;
  
  rewind( trn_payf );
  clearRecord( trn_payf );
  trn_payf.AutoKey = ref;
  if( ( retVal = getEQ( trn_payf ) ) == true )
    rewind( trn_cntr );
    clearRecord( trn_cntr );
    trn_cntr.NumberContract = trn_payf.NumberContract;
    retVal = getEQ( trn_cntr );
  end;
  return retVal;
end;
/************************************************ найти запись в MDB rt_paym */
private macro mdb_rtpaymFindByAttrID( iApplicationKind, ApplicationKey, attrID )
  var userAttrs, varUserAttrs,
      oldRtPaym, oldRtPaym1,
      stat;

  /* pusha */
  oldRtPaym  = TRecHandler( "rt_paym.dbt" );
  oldRtPaym1 = TRecHandler( "rt_paym.1"   );
  userAttrs    = getDocUserAttrPool();
  varUserAttrs = userAttrs.overlayRecord( "rt_paym.1", 0, false );
  xackCopy( oldRtPaym,  userAttrs    );
  xackCopy( oldRtPaym1, varUserAttrs );
  /* поиск */
  userAttrs.rewind();
  userAttrs.clear();
  userAttrs.rec.iApplicationKind = iApplicationKind;
  userAttrs.rec.ApplicationKey   = ApplicationKey;
  userAttrs.rec.AttrID           = attrID;
  stat = userAttrs.getEQ();
  /* если нашел - там и остаемся, иначе - восстанавливаем запись */
  if( not stat )
    /* popa */
    xackCopy( userAttrs,    oldRtPaym  );
    xackCopy( varUserAttrs, oldRtPaym1 );
  end;
  return stat;
end;
/***************** преобразование типа налогового платежа из сокращения типа
 ************************************************ "ТП", "ЗД" в полную строку */
private macro getNalPayAssignmentText( strShortening )
  var stat;

  file na( "namealg.dbt" ) key 0;

  rewind( na ); clearRecord( na );
  na.iTypeAlg = 330;
  stat = getGE( na );
  while( ( stat                         == true          ) and
         ( na.iTypeAlg                  == 330           ) and
         ( substr( na.szNameAlg, 1, 2 ) != strShortening ) )
    stat = next( na );
  end;
  if( substr( na.szNameAlg, 1, 2 ) != strShortening )
    return "";
  else
    return na.szNameAlg;
  end;
end;
/*************************** цикл по полям панели и присвоение оным значения */
private macro findAndSetFieldValue( dlg, fldValue/* , ... */ )
  var idx, jdx, nameToFind;
  
  idx = 0;
  while( idx < fldNumber( dlg ) )
    jdx = 0 + 1 + 1;
    while( getparm( jdx, nameToFind ) )
      if( strUpr( fldName( dlg, idx ) ) == strUpr( nameToFind ) )
        dlg( idx ) = fldValue;
      end;
      jdx = jdx + 1;
    end;
    idx = idx + 1;
  end;
end;
/************************* цикл по полям панели и получение значения первого */
private macro findAndGetFieldValue( dlg/* , ... */ )
  var idx, jdx, nameToFind, retValue = "";
  
  idx = 0;
  while( idx < fldNumber( dlg ) )
    jdx = 0 + 1;
    while( getparm( jdx, nameToFind ) )
      if( strUpr( fldName( dlg, idx ) ) == strUpr( nameToFind ) )
        retValue = dlg( idx );
        jdx = 65536;
        idx = fldNumber( dlg ) + 1;
      end;
      jdx = jdx + 1;
    end;
    idx = idx + 1;
  end;
  return retValue;
end;
/************************************** обновление документа прочей кассовой */
macro UpdateCashRecords( mask, save )

  var userAttrs, varUserAttrs, grd;
  
  if( mask == UPDATE_DOC )
    OC_PAY_DOC_PMNT_PROP.BankCode  = PMNT_PROP_REC_BUF.BankCode;
    OC_PAY_DOC_PMNT_PROP.BankCodeKind = PMNT_PROP_REC_BUF.BankCodeKind;
    OC_PAY_DOC_PMNT_PROP.CorrAccount  = PMNT_PROP_REC_BUF.CorrAccount;
    OC_PAY_DOC_PMNT_PROP.Account = PMNT_PROP_REC_BUF.Account;

    if ( GetBitFlag( ALG_SBDEPDOC_1.Flags, 27 ) == 1 )
      SetBitFlag( OC_PAY_DOC.Flags, 4, 1 );
    else
      SetBitFlag( OC_PAY_DOC.Flags, 4, 0 );
    end;

    if( mdb_rtpaymFindByAttrID( ALG_SBDEPDOC_1.iApplicationKind, ALG_SBDEPDOC_1.ApplicationKey, "РЕКВ_ПОЛ" ) )
      userAttrs    = getDocUserAttrPool();
      varUserAttrs = userAttrs.overlayRecord( "rt_paym.1", 0, false );
      OC_PAY_DOC_PMNT_PROP.Name     = userAttrs.rec.RecivFIO;
      OC_PAY_DOC_PMNT_PROP.BankName = PMNT_PROP_REC_BUF.BankName;
      OC_PAY_DOC_PMNT_PROP.BankCode = PMNT_PROP_REC_BUF.BankCode;
      OC_PAY_DOC_PMNT_PROP.CorrAccount = PMNT_PROP_REC_BUF.CorrAccount;
      OC_PAY_DOC_PMNT_PROP.Account  = PMNT_PROP_REC_BUF.Account;
      if( OC_PAY_DOC.Ground == "" )
        grd = userAttrs.overlayRecord( "ground.rec", userAttrs.FldOffset( "Payment" ), true );
        OC_PAY_DOC.Ground = grd.rec.ground;
      end;
    end;
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindBank ( code, codeKind )

  var stat;
  
 //  Ищем по БИК 
  partcode.Clear;
  partcode.rec.CodeKind = codeKind;
  partcode.rec.Code     = code;
  stat = partcode.GetEQ;

  return stat;

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro FindParty ( id )
  
  party.Clear;
  party.rec.PartyID = id;

  return party.GetEQ;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindFininstr ( codCurr )

  var stat;
  
  vFiID = -1;
  ClearRecord( currency );
  currency.Code_Currency = codCurr;

  stat = GetEQ( currency );

  if ( stat )
    if ( Trim( currency.ExternalCode ) == "" )
      stat = false;
    else
      fininstr.Clear;
      fininstr.rec.ISO_Number = Int( Trim( currency.ExternalCode ) );
      stat = fininstr.GetEQ;

      if ( stat )
        fininstr.Clear;
        fininstr.rec.CodeInAccount = Int( Trim( currency.ExternalCode ) );
        stat = fininstr.GetEQ;
      end;

    end;
  end;

  if( stat )
    vFiID = fininstr.rec.fiid;
  end;

  return stat;

end;


private macro AllowInstPayment( CodeCurr )
  var flag = false;
  var groupID = 35;
  var objectID = string(party.rec.PartyID);

  while( strlen(objectID) < 10 )
    objectID = "0" + objectID;
  end;

  if( CodeCurr != 0 )
    groupID = 36;
  end;

  if( not ObjAttr_FindFirst( AttrBuff, 3, objectID, groupID ) )
    if( AttrBuff.rec.Name == "Да" )
      flag = true;
    end;
  end;
  ObjAttr_FindClose();

  return flag;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro checkCorAccountForUrgent( inBIK, inCodeKind,  codCurr )
  
  var stat = true;
  var flag;

  if ( stat )
    if ( StrLen( Trim( inBIK ) ) == 0 )
      MsgBox( "Не задан БИК/SWIFT|Перевод по счетам ЛОРО невозможен" );
      stat = false;
    end;
  end;

  if ( stat )
    if ( inCodeKind == 0 )
      MsgBox( "Не задан вид кода|Перевод по счетам ЛОРО невозможен" );
      stat = false;
    end;
  end;

  if ( stat )
    stat = FindBank( inBIK, inCodeKind );

    if ( stat )
      stat = FindParty( partcode.rec.PartyID );
    end;

    if ( not stat )
      MsgBox( "Не найден банк по его БИК/SWIFT|Перевод по счетам ЛОРО невозможен" );
    end;
  end;

  if ( stat )
    stat = FindFininstr( codCurr );

    if ( not stat )
      MsgBox( "Не определена валюта в справочнике|Перевод по счетам ЛОРО невозможен" );
    end;
  end;

  if ( stat )
    stat = AllowInstPayment(codCurr);

    if ( not stat )
      MsgBox( "Для банка не задан признак разрешения срочных платежей|Перевод по счетам ЛОРО невозможен" );
    end;
  end;

  return stat;

end;

/************************************** Запись в rt_paym.dbt для переводов. */
private macro WriteRtPaymTransf( pReceiver, pDLG, partyID )
  
  setRecordAddr( varPartForTrasnf, rt_paymf );
  clearRecord( rt_paymf );
  rt_paymf.iApplicationKind = ALG_SBDEPDOC_1.iApplicationKind;
  rt_paymf.ApplicationKey   = ALG_SBDEPDOC_1.ApplicationKey;
  rt_paymf.AttrID           = "ПЕРЕВОД";
  varPartForTrasnf.PartyID  = PartyID;
  varPartForTrasnf.RecipName = SubStr( pReceiver, 1, 138 );


  if (fldIndex(pDLG, "INN") != -1)
     varPartForTrasnf.RecipINN  = pDLG.INN;
  end;

  if (fldIndex(pDLG, "ADDR") != -1)
     varPartForTrasnf.RecipAddr  = pDLG.ADDR;
  end;

/*
  if ( StrUpr( Trim( FldName( pDLG,  9 ) ) ) == "INN" )
    varPartForTrasnf.RecipINN  = pDLG(9);
  end;
  if ( StrUpr( Trim( FldName( pDLG, 10 ) ) ) == "ADDR" )
    varPartForTrasnf.RecipAddr = pDLG(10);
  end;
  */

  Insert( rt_paymf, getRecordSize(varPartForTrasnf) );
end;

// this function search bank code up in the territory department tree
// by its Party code and necessary kind of bank code :)
private macro FindBankCodeUpByPartyID(PartyID, CodeKind)
    var cmd, rs;
    var ResultCode = "";
    var DepID;
    
    cmd = RsdCommand("  SELECT t_code FROM ddp_dep_dbt WHERE t_partyid = ? and t_status != 3");
    cmd.addParam ("Party", RsdBp_in);
    cmd.value    ("Party") = PartyID;
    cmd.NullConversion = true;
    cmd.execute;
    rs = RsdRecordset(cmd); 
    if (rs.moveNext())
        DepID = rs.value(0);
        ResultCode = findDprtPartyCode( DepID, CodeKind);
    end;
    return ResultCode;
end;

private macro FindBankCodeByPartyID( PartyID )
    var cmd, rs;
    var ResultCode = -1;
    cmd = RsdCommand("SELECT t_code FROM ddp_dep_dbt WHERE t_partyid = ?");
    cmd.addParam ("Party", RsdBp_in);
    cmd.value    ("Party") = PartyID;
    cmd.NullConversion = true;
    cmd.execute;
    rs = RsdRecordset( cmd );
    if ( rs.moveNext() )
        ResultCode = rs.value(0);
    end;
    return ResultCode;
end;

// Очистка номера счета от символов шаблона.
private macro delShablon( str )
  var l = StrLen( str );
  var i = 1;
  var sRet = "";
  while ( i <= l )
    if ( ( SubStr( str, i, 1 ) != "." )  AND
         ( SubStr( str, i, 1 ) != "'" )  AND
         ( SubStr( str, i, 1 ) != "," )  AND
         ( SubStr( str, i, 1 ) != "`" )  AND
         ( SubStr( str, i, 1 ) != "~" )  AND
         ( SubStr( str, i, 1 ) != "#" )  AND
         ( SubStr( str, i, 1 ) != "$" )  AND
         ( SubStr( str, i, 1 ) != "^" )  AND
         ( SubStr( str, i, 1 ) != "_" )  AND
         ( SubStr( str, i, 1 ) != ":" )  AND
         ( SubStr( str, i, 1 ) != "-" )  AND
         ( SubStr( str, i, 1 ) != "/" )  AND
         ( SubStr( str, i, 1 ) != "\"" ) AND
         ( SubStr( str, i, 1 ) != "\\" ) )
      sRet = sRet + SubStr( str, i, 1 );
    end;
    i = i + 1;
  end;
  return sRet;
end;

// **************************************************
//     Определение резидентности субъекта 
// **************************************************
private macro isResident( partyId : Integer )
  var cmd = RsdCommand( "select t_notResident from dparty_dbt where t_partyId = ?" );
  cmd.addParam( "p_partyId", RsdBp_In, partyId );
  cmd.execute;
  var rs = RsdRecordset( cmd );
  if ( rs.moveNext )
    return rs.value(0) == StrFor(0);  
  end;
  return true;
end;

// **************************************************
//         Определение: закрыт ли субъект
// **************************************************
private macro isLocked( partyId : Integer )
  var cmd = RsdCommand( "select t_Locked from dparty_dbt where t_partyId = ?" );
  cmd.addParam( "p_partyId", RsdBp_In, partyId );
  cmd.execute;
  var rs = RsdRecordset( cmd );
  if ( rs.moveNext )
    return rs.value(0) != StrFor(0);  
  end;
  return true;
end;

// **************************************************
//               Проверка: строка цифр
// **************************************************
private macro isDigitalStr( pStr )
  var stat = TRUE, i = 1, l = StrLen( pStr ), ch, DigitString = "0123456789";
  while ( stat  AND  ( i <= l ) )
    ch = SubStr( pStr, i, 1 );
    if ( NOT Index( DigitString, ch ) )
      stat = FALSE;
    end;
    i = i + 1;
  end;
  return stat;
END;

// **************************************************
//       Проверка: строка цифр и латинских букв
// **************************************************
private macro isDigitalLatStr( pStr )
  var stat = TRUE, i = 1, l = StrLen( pStr ), ch, checkString = "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ-";
  while ( stat  AND  ( i <= l ) )
    ch = SubStr( pStr, i, 1 );
    if ( NOT Index( checkString, ch ) )
      stat = FALSE;
    end;
    i = i + 1;
  end;
  return stat;
END;

//
//  Параметр "CB\\PAYMENTS\\MAXPRIORITY"
//
private macro getMaxPriority() : integer
  var MaxPriority : integer = 0, err : integer = 0, TypeVal : integer = V_UNDEF;
  TypeVal = GetRegistryValue( "CB\\PAYMENTS\\MAXPRIORITY", V_INTEGER, MaxPriority, err );
  if( ( TypeVal == V_UNDEF ) OR ( err != 0 ) )
    MaxPriority = 5;
  end;
  return MaxPriority;
end;

// **************************************************
//      Проверки для операции 63 в стандарте ББ
// **************************************************
private macro check63_CB( dlg, _partyID )
  var res = false;
  var loc = false;
  var ret = true;
  var maxPriority = getMaxPriority();
  if ( _partyID != 0 )
    loc = isLocked( _partyID );
    if ( loc )
      msgbox( "Банк-получатель не является действующим банком" );
      ret = false;
    else
      res = isResident( _partyID );
    end;
    if ( ( ret )  AND  ( StrLen( dlg.Receiver ) == 0 ) )
      msgbox( "Не задано наименование получателя" );
      ret = false;
      res = false;
    end;
    if ( ( ret )  AND  ( StrLen( dlg.Accnt ) == 0 ) )
      msgbox( "Не задан счет получателя" );
      ret = false;
      res = false;
    end;
    if ( ( ret )  AND  ( StrLen( dlg.PaymAssignment ) > 210 ) )
      msgbox( "Длина назначения платежа больше 210 символов" );
      ret = false;
      res = false;
    end;
    if ( res )  // Доп.проверки для счетов
      if ( ( Index( dlg.Accnt, " " ) != 0 )  OR  ( StrLen( dlg.Accnt ) != 20 )  OR  ( NOT isDigitalStr( SubStr( dlg.Accnt, 1, 5 ) ) )  OR  ( NOT isDigitalStr( SubStr( dlg.Accnt, 9, 12 ) ) ) )
        msgbox( "Счет получателя не соответствует|действующим правилам ЦБ РФ обозначения лицевых счетов" );
        ret = false;
      elif ( ( NOT isDigitalLatStr( StrUpr( SubStr( dlg.Accnt, 6, 3 ) ) ) ) )
        msgbox( "Счет получателя не соответствует|действующим правилам ЦБ РФ обозначения лицевых счетов" );
        ret = false;
      elif ( ( Index( dlg.Accnt, "*?" ) != 0 ) )
        msgbox( "Вместо конкретного номера счета получателя|задана маска, что недопустимо" );
        ret = false;
      end;
    end;
    if ( ret == true )
      if ( ( dlg.Priority < 0 )  OR  ( dlg.Priority > maxPriority ) )
        msgbox( "Неверно задана очередность платежа" );
        ret = false;
      end;
    end;
  else
    MsgBox( "Банк-получатель не содержится|в справочнике субъектов Ядра" );
  end;
  return ret;
end;

/************************************************************* базовый класс */
private class CBaseActions
  var savedINN = "", savedKPP = "";
  var m_BankCodeKind, m_PartyID;
  var p4, p5, p6;

  /* инициализация общих полей диалога */
  macro onInit( dlg, constPart, varPart )
  var CorAcc, BankName, BankCode;  
  if (fldIndex(dlg, "FlagUrgent") != -1)  
  disableFields( dlg, fldIndex( dlg, "FlagUrgent")); // for 63-32
  end;
  if (fldIndex(dlg, "CorrBranch") != -1)
  disableFields( dlg, fldIndex( dlg, "CorrBranch"));  // for 66,67
  end;

  /* Поле ReceiverText не во всех панелях */
  if (fldIndex(dlg, "ReceiverText") != -1)
     dlg.ReceiverText = "Получатель";
  end;

  var partyId = 0;
  m_partyID = 0;
/*
  Обрабатываем эти поля
  CodeKind
  BankCode
  Bank
  CorAcc
  INN
  KPP
  Accnt
  Receiver
*/
/* nikto ne znaet zachem */
    savedINN = PMNT_PROP_REC_BUF.INN;
    savedKPP = PMNT_PROP_REC_BUF.KPP;

    /* Проверяем пустой ли RecBuf таблицы RTPaymentProp */
    if( (PMNT_PROP_REC_BUF.BankCode != "") and (PMNT_PROP_REC_BUF.BankCodeKind != 0))

      dlg.CodeKind  = FindBankCodeKindName(PMNT_PROP_REC_BUF.BankCodeKind);
      dlg.BankCode  = PMNT_PROP_REC_BUF.BankCode;
      dlg.Bank      = PMNT_PROP_REC_BUF.BankName;
      dlg.CorAcc    = PMNT_PROP_REC_BUF.CorrAccount;
      dlg.Accnt     = PMNT_PROP_REC_BUF.Account;
      dlg.INN       = PMNT_PROP_REC_BUF.INN;
      dlg.KPP       = PMNT_PROP_REC_BUF.KPP;
      dlg.Receiver  = PMNT_PROP_REC_BUF.Name;
      m_BankCodeKind= PMNT_PROP_REC_BUF.BankCodeKind;
      m_PartyID     = PMNT_PROP_REC_BUF.PartyID;

      if (fldIndex(dlg, "Priority") != -1)
        dlg.Priority = PMNT_PROP_REC_BUF.Priority; 
      end;

      if ( fldIndex( dlg, "ChargeId" ) >= 0 )
        dlg.ChargeId = PMNT_PROP_REC_BUF.ChargeId;
      end;

      var p3 = "", p4 = "", p5 = "", p6 = "";

      if ( dlg.BankCode != "" ) 
        iFindBank( m_BankCodeKind, dlg.BankCode, p3, 
                   p4, p5, p6, partyId );
        m_PartyID = partyId;
      end;
    /* Иначе заполняем по-умолчанию */
    else
      PmntPropBufInit(); // RTPaymentProp RecBuff initialize
      /*if ( NumFlagCur() != 0 )
        m_BankCodeKind = 6;
      else
        m_BankCodeKind = 3;
      end;*/
      m_BankCodeKind = PMNT_PROP_REC_BUF.BankCodeKind;
      dlg.CodeKind = FindBankCodeKindName( m_BankCodeKind );
      dlg.BankCode = PMNT_PROP_REC_BUF.BankCode;
      dlg.Bank = PMNT_PROP_REC_BUF.BankName;
      dlg.CorAcc = PMNT_PROP_REC_BUF.CorrAccount;
      if( dlg.BankCode != "" )
        iFindBank( m_BankCodeKind, dlg.BankCode, BankName, 
                   CorAcc, dlg.INN, dlg.KPP, partyId );
        m_PartyID = partyId;
      end;
      if (fldIndex(dlg, "Priority") != -1)
       dlg.Priority = DEF_PRIORITY; 
      end;
    end;

  if (fldIndex(dlg, "PaymAssignment") != -1)
     dlg.PaymAssignment = PMNT_PROP_REC_BUF.Ground; 
  end;

    UpdateFields( dlg );
  end;
  /* при переходе от поля к полю */
  macro onRemFocus( dlg, activeField )
    var idx,
        codeKind = m_BankCodeKind,
        getName,
        getCorAccnt,
        getINN,
        getKPP,partyId;
    

    if(  activeField == "BankCode" )
      /* --- ручной ввод  --- */
      if( iFindBank( m_BankCodeKind, dlg.BankCode, getName, 
                  getCorAccnt, getINN, getKPP, partyId ) == true )
                  
        savedINN = getINN; savedKPP = getKPP;
        dlg.Bank    = getName;
        dlg.CorAcc  = getCorAccnt;
        dlg.INN     = getINN;
        dlg.KPP     = getKPP;
        m_PartyID   = partyId;
      else
        savedINN = ""; savedKPP = "";
        dlg.Bank    = "";
        dlg.CorAcc  = "";
        dlg.INN     = "";
        dlg.KPP     = "";
        m_PartyID   = 0;
      end;
      UpdateFields( dlg );
    end;
  end;
  /* при сохранении данных (F9) */
  macro onSave( dlg )
    
    if( ( dlg.CodeKind    == "" ) or
        ( dlg.Bank     == "" ) or
        ( dlg.BankCode      == "" ) or
        ( dlg.CorAcc == "" ) or
        ( dlg.Receiver == "" ) or
        ( dlg.Accnt    == "" ) )
      return false;
    else
      return true;
    end;
  end;
  /* При отказе (Esc) */
  macro onEsc (dlg)
    return true;
  end;
  /* Проверка */
  macro addCheck( dlg )
    var ok = true;
    var controlChargeId = true;
    var regError = 0;

    if ( ( getBitFlag( ALG_SBDEPDOC_1.Flags, 29 ) == 1 ) ) /* длительное поручение */
      if ( (m_BankCodeKind == 0) or (dlg.BankCode == "") )
        ok = false;
      elif ( NOT iFindBank( m_BankCodeKind, dlg.BankCode ) )
        MsgBox("Перевод за пределы России запрещен");
        ok = false;
      end;
    end;

    // Проверка УИП
    if ( ok )
      if ( fldIndex( dlg, "ChargeId" ) >= 0 )
        if ( StrLen( dlg.Accnt ) > 5 )
          if ( SubStr( dlg.Accnt, 1, 5 ) == "40822" )
            if ( GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\КОНТРОЛЬ_УИП", V_BOOL, controlChargeId, regError ) == V_BOOL )
              ;
            else
              controlChargeId = true;
            end;

            if ( controlChargeId )
              if ( ( StrLen( dlg.ChargeId ) < 25 ) or ( SubStr( dlg.ChargeId, 1, 25 ) == "0000000000000000000000000" ) )
                MsgBox( "Значение поля УИП указано неверно" );
                ok = false;
              elif ( CheckKeyChargeId( dlg.Accnt, dlg.ChargeId ) == false )
                MsgBox( "Значение УИП введено неверно" );
                ok = false;
              end;
            end;
          end;
        end;
      end;
    end;

    return ok;
  end;

  /* выход из поля */
  macro onEXITFIELD( dlg, activeField )
    if ( StrUpr( activeField ) == "ACCNT" )
      dlg.Accnt = delShablon( dlg.Accnt );
      UpdateFields( dlg );
    end;
  end;

  /* при выборе из справочника (F3) */
  macro onF3( dlg, activeField )
  
    var i;
    var getBank, getBankCode, getCorAcc, getINN;
    var getKPP, p7;
    var CodeKind, CodeKindName, partyId;
    
    if( ( ( StrUpr( activeField ) == "BANK"        ) or
          ( StrUpr( activeField ) == "BANKCODE"    ) or
          ( StrUpr( activeField ) == "CORACC"  )   ) and
        ( SelectBank( getBankCode, getCorAcc, NULL, getBank, m_BankCodeKind, 
                      getINN, getKPP, p7, partyId ) == true ) )
      
      savedINN = getINN; savedKPP = getKPP;
      
          dlg.Bank = getBank;
          dlg.CorAcc = getCorAcc;
          dlg.INN = getINN;
          dlg.KPP = getKPP;
          m_PartyID = partyId;
          if( getBankCode == "" ) // 115369
             getBankCode = FindBankCodeUpByPartyID( partyId, m_BankCodeKind);
          end;
          
          dlg.BankCode = getBankCode;
      UpdateFields( dlg );
    elif( ( StrUpr( activeField ) == "CODEKIND" ) and
        ( ( SelectCodeKind( CodeKind, CodeKindName ) == true ) ) )
      m_BankCodeKind = CodeKind;
      dlg.CodeKind = CodeKindName;
      
      dlg.Bank         = "";
      dlg.BankCode     = "";
      dlg.CorAcc       = "";
      dlg.INN          = "";
      dlg.KPP          = "";
      UpdateFields( dlg );
	elif( ( StrUpr( activeField ) == "TAXAUTHORSTATE" ) )
		var bufval = dlg.TaxAuthorState;
		dlg.TaxAuthorState = list_alg( 337 );
		if( dlg.TaxAuthorState == 0 )
			dlg.TaxAuthorState = bufval;
		elif( strlen(dlg.TaxAuthorState) == 1 ) 
			dlg.TaxAuthorState = "0" + dlg.TaxAuthorState;
		end;
    end;
  end;

  macro onAltF3( dlg, activeField )
  
    var depID, name, rec, partyRec, CorAcc;
    var partyId;

    if( ( ( StrUpr( activeField ) == "BANK"        ) or
          ( StrUpr( activeField ) == "BANKCODE"    ) or
          ( StrUpr( activeField ) == "CORACC"  )   ) and
          ( selectDepartment(depID, name, 0, 0, rec, partyRec) == true))
          
      //dlg.BankCode = findDprtPartyCode( depID, m_BankCodeKind);
      dlg.BankCode = FindBankCodeUpByPartyID(partyRec.rec.PartyID, m_BankCodeKind); // 115369
      dlg.Bank = partyRec.rec.Name;

      if( dlg.BankCode != "" )
        iFindBank( m_BankCodeKind, dlg.BankCode, name, 
                   dlg.CorAcc, dlg.INN, dlg.KPP, partyId );
        m_PartyID = partyId;
      end;

      UpdateFields( dlg );
    end;
  end; // onAltF3
  
  /* при выборе из справочника (^F3) */
  macro onCtrlF3( dlg, activeField )
  
    var pay_corr = TRecHandler( "pay_corr.dbt" );
    var numOpert = 0;
    var i, idx, getName, codeKind;
    
    if ( ALG_SBDEPDOC_1.TypeOper == 73 )
      numOpert = 41;
    elif ( ( ALG_SBDEPDOC_1.TypeOper == 63 ) or
           ( ALG_SBDEPDOC_1.TypeOper == 66 ) or
           ( ALG_SBDEPDOC_1.TypeOper == 67 )   )

      if ( ( ALG_SBDEPDOC_1.TypeComplexOper != 6 ) or ( getBitFlag( ALG_SBDEPDOC_1.Flags, 10 ) == 0 ) )
        numOpert = 11;
      end;
    end;
    
    if ( numOpert > 0 )
      if ( ( StrUpr( activeField ) == "BANK"        ) or
           ( StrUpr( activeField ) == "BANKCODE"      ) or
           ( StrUpr( activeField ) == "CORACC"      )  )
        ;
      else
        numOpert = 0;
      end;
    end;

    if ( numOpert > 0 )
      pay_corr.Clear;
      pay_corr.rec.NumGroup = numOpert;
      pay_corr.rec.CodCur   = ALG_SBDEPDOC_1.Code_Currency;
      if ( SelectPayCorr( pay_corr ) )
        i = 0;
        while ( i < FldNumber( dlg ) )

          if ( ( StrUpr( FldName( dlg, i ) ) == "BANK"     ) or
               ( StrUpr( FldName( dlg, i ) ) == "BANKNAME" )   )
            
            m_BankCodeKind = pay_corr.rec.BankCodeKind;

            idx = 0;
            while( idx < fldNumber( dlg ) )
              if( strUpr( fldName( dlg, idx ) ) == "CODEKIND" )
                dlg(idx) = FindBankCodeKindName(m_BankCodeKind);
                idx = fldNumber( dlg );                
              end;
              idx = idx + 1;
            end;
            
            dlg(i) = pay_corr.rec.BankName;
          end;

          if ( StrUpr( FldName( dlg, i ) ) == "RECEIVER")
            dlg(i) = pay_corr.rec.CorrName;
          end;

          if ( StrUpr( FldName( dlg, i ) ) == "INN" )
            dlg( i ) = pay_corr.rec.CorrINN;
          end;

          if (  StrUpr( FldName( dlg, i ) ) == "BANKCODE" )
            dlg( i ) = pay_corr.rec.BIK;
          end;

          if (  StrUpr( FldName( dlg, i ) ) == "CORACC" )
            dlg( i ) = pay_corr.rec.CorrAccount;
          end;

          if ( StrUpr( FldName( dlg, i ) ) == "ACCNT" )
            dlg( i ) = pay_corr.rec.Account;
          end;

          i = i + 1;
        end;

        UpdateFields( dlg );
      end;
    end;
  end;
  
  macro onSpace( dlg, activeField ) /*110652*/
    if ( activeField == "FlagUrgent" )
      if ( dlg.FlagUrgent == "" )
        dlg.FlagUrgent = "X";
      else
        dlg.FlagUrgent = "";
      end;
    end;
  end;

  /* финальное заполнение структур */
  macro fillVarPart( dlg, constPart, varPart )
    
    PMNT_PROP_REC_BUF.Name           = dlg.Receiver;
    PMNT_PROP_REC_BUF.BankName       = dlg.Bank;
    PMNT_PROP_REC_BUF.BankCode       = dlg.BankCode;
    PMNT_PROP_REC_BUF.BankCodeKind   = m_BankCodeKind;
    PMNT_PROP_REC_BUF.CorrAccount    = dlg.CorAcc;
    PMNT_PROP_REC_BUF.Account        = dlg.Accnt;
    // PMNT_PROP_REC_BUF.PartyID        = m_PartyID;
    PMNT_PROP_REC_BUF.INN            = dlg.INN;
    PMNT_PROP_REC_BUF.KPP            = dlg.KPP;
    if (fldIndex(dlg, "Priority") != -1)
      PMNT_PROP_REC_BUF.Priority     = dlg.Priority; 
    end;
    if (fldIndex(dlg, "PaymAssignment") != -1)
      PMNT_PROP_REC_BUF.Ground = dlg.PaymAssignment;
    end;

    if ( fldIndex( dlg, "ChargeId" ) >= 0 )
      PMNT_PROP_REC_BUF.ChargeId = dlg.ChargeId;
    end;

    //if( PMNT_PROP_REC_BUF.INN == "" ) PMNT_PROP_REC_BUF.INN = savedINN; end;
    //if( PMNT_PROP_REC_BUF.KPP == "" ) PMNT_PROP_REC_BUF.KPP = savedKPP; end;
    if ( ( ALG_SBDEPDOC_1.TypeOper == 63 )  AND  ( m_PartyID > 0 ) )
      WriteRtPaymTransf( PMNT_PROP_REC_BUF.Name, dlg, m_PartyID);
    end;
  end;
end;
/********************************************************************** 63.0 */
private class (CBaseActions) CActionsOn63_0
  /* инициализация общих полей диалога */
  macro onInit( dlg, constPart, varPart )
    
    file localDepDoc( "sbdepdoc.dbt" ) key 3;
    /* для этой операции записей в rt_paym не делается */
    /* т.о. режим (просмотр/редатирование) определяем вот так */
    rewind( localDepDoc );
    clearRecord( localDepDoc );
    localDepDoc.iApplicationKind = ALG_SBDEPDOC_1.iApplicationKind;
    localDepDoc.ApplicationKey   = ALG_SBDEPDOC_1.ApplicationKey;
    if( getEQ( localDepDoc ) )
      disableFields( dlg );
       notActive = true;
    end;
  end;
  /* при сохранении данных (F9) */
  macro onSave( dlg )
    var chk = check63_CB( dlg, m_PartyID );
    return chk;
  end;
end;
/********************************************************************** 63.1 */
private class (CBaseActions) CActionsOn63_1
  /* инициализация */
  macro onInit( dlg, constPart, varPart )
    
    onInit( dlg, constPart, varPart );
  end;
  /* сохранение */
  macro onSave( dlg )
    var chk = check63_CB( dlg, m_PartyID );
    if ( chk == false )
      return false;
    end;
    if( ( dlg.PaymAssignment == "" ) or
        ( dlg.INN            == "" ) or
        ( dlg.KPP == ""  ) )
      return false;
    else
      return onSave( dlg );
    end;
  end;
  /* финальное заполнение структур */
  macro fillVarPart( dlg, constPart, varPart )
    

    fillVarPart( dlg, constPart, varPart );

    PMNT_PROP_REC_BUF.Ground   = dlg.PaymAssignment;
    PMNT_PROP_REC_BUF.Priority = dlg.Priority;
  end;
end;
/********************************************************************* 63.32 */
private class (CBaseActions) CActionsOn63_32
  /* инициализация */
  macro onInit( dlg, constPart, varPart )
    
    
    onInit( dlg, constPart, varPart ); /*base*/
    enableFields( dlg, fldIndex( dlg, "FlagUrgent"));

    if ( GetBitFlag( ALG_SBDEPDOC_1.Flags, 27 ) != 0 )
      dlg.FlagUrgent = "X";
    end;
    UpdateFields( dlg );
  end;

  /* выход из поля */
  macro onEXITFIELD( dlg, activeField )
    onEXITFIELD( dlg, activeField );
  end;

  /* выбор из справочника */
  macro onF3( dlg, activeField )
    
    onF3( dlg, activeField );
  end;
  /* выбор из справочника */
  macro onSpace( dlg, activeField )
    
    if ( activeField == "FlagUrgent" )
      if ( dlg.FlagUrgent == "" )
        dlg.FlagUrgent = "X";
      else
        dlg.FlagUrgent = "";
      end;
    end;
  end;
  /* сохранение */
  macro onSave( dlg )
    var chk = check63_CB( dlg, m_PartyID );
    if ( chk == false )
      return false;
    end;
    if( ( dlg.PaymAssignment == "" ) )
      return false;
    else
      return onSave( dlg );
    end;
  end;
  /* При отказе (Esc) - сохранить поля панели в глобальном месте */
  macro onEsc (dlg)
    
    return true;
  end;
  /* Проверка срочного платежа */
  macro addCheck( dlg )
    
    var hour;
    var mrot;
    var errCode;
    var limOper;
    var cbRate1;
    var cbRate2;

    if ( dlg.FlagUrgent == "" )
      return true;
    end;

    TimeSplit( Time( ), hour );

    /* Проверка допустимой даты и времени перевода */
    if ( IsHoliday( {curdate} ) or ( IsHoliday( {curdate} + 1 ) and ( hour > 12 ) ) )
      if ( not GetTrue( false, "Время перевода срочного перевода истекло. Продолжить?" ) )
        return false;
      end;
    end;

    /* Проверка корсчета */
    if ( not checkCorAccountForUrgent( dlg.BankCode, m_BankCodeKind, ALG_SBDEPDOC_1.Code_Currency ) )
      return false;
    end;

    if  ( ALG_SBDEPDOC_1.Code_Currency > 0 )
      if ( ClientList.GetRecord( ALG_DEPOSITOR.CodClient ) )
        ;
      else
        MsgBox( "Не найден владелец счета" );
        return false;
      end;

      if ( ClientList.CurRec.Rec.NotResident )

        if ( ALG_SBDEPDOC_1.Source > 0 )
          ClearRecord( auxinfo );
          auxinfo.Reference = ALG_SBDEPDOC_1.Referenc;
          auxinfo.InfoType  = ALG_SBDEPDOC_1.Source;
          auxinfo.Date      = Date( 0, 0, 0 );
          if ( GetEQ( auxinfo ) )
            SetRecordAddr( auxinfo11, auxinfo, 0, 0, true );
            if ( ALG_SBDEPDOC_1.OutSum > auxinfo11.SumIn - auxinfo11.SumOut )
              MsgBox( "Сумма перевода превышает допустимое значение" );
              return false;
            end;
          else
            MsgBox( "По выбранному источнику нет суммы" );
            return false;
          end;
        end;
      else
        if ( GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\ЛИМИТЫ\\СРОЧНЫЕ_МЕЖДУНАРОДНЫЕ_ПЕРЕВОДЫ\\МАКС_СУММА_В_USD", V_DOUBLE, mrot, errCode ) == V_DOUBLE )
          ;
        else
          return false;
        end;

        limOper = MoneyL( mrot );

        if ( ALG_SBDEPDOC_1.Code_Currency > 1 )
          if ( not GetAllCurrRate( ALG_SBDEPDOC_1.Date_Document, 1, cbRate1 ) )
            MsgBox( "Ошибка при определении кросс-курса валюты" );
            return false;
          end;

          if ( not GetAllCurrRate( ALG_SBDEPDOC_1.Date_Document, ALG_SBDEPDOC_1.Code_Currency, cbRate2 ) )
            MsgBox( "Ошибка при определении кросс-курса валюты" );
            return false;
          end;

          limOper = Money( limOper * cbRate1 / cbRate2 );
        end;

        if ( ALG_SBDEPDOC_1.OutSum > limOper )
          MsgBox( "Сумма перевода превышает допустимое значение" );
          return false;
        end;

      end;
    end;

    return addCheck();
  end;
  /* финальное заполнение структур */
  macro fillVarPart( dlg, constPart, varPart )
    

    fillVarPart( dlg, constPart, varPart );

    PMNT_PROP_REC_BUF.Ground   = dlg.PaymAssignment;
    PMNT_PROP_REC_BUF.Priority = dlg.Priority;
    if ( dlg.Priority == 0)
         dlg.Priority = DEF_PRIORITY;
    end;

    if ( dlg.FlagUrgent != "" )
      SetBitFlag( ALG_SBDEPDOC_1.Flags, 27, 1 );
    else
      SetBitFlag( ALG_SBDEPDOC_1.Flags, 27, 0 );
    end;

    WriteRtPaymTransf( dlg.Receiver, dlg, m_PartyID );
  end;
end;
/********************************************************************* 63.5x */
private class (CBaseActions) CActionsOn63_5x
  /* подкачать название филиала */
  macro pumpBranch( dlg )
   
    var dep_name;
    if(findDepartment(ALG_SBDEPDOC_1.FNCash, dep_name))
      dlg.Filial = dep_name;
    end;
  end;
  /* подкачать название банка */
  macro pumpBank( dlg )
    dlg.BankName = getOtherBankName( dlg.BankCode );
  end;
  /* иницииализация */
  macro onInit( dlg, constPart, varPart )
    
    onInit( dlg, constPart, varPart );
    var rateCB = 0.0;
    m_BankCodeKind = getDefaultCodeKind();
    m_PartyID = 0;
    dlg.CodeKind = FindBankCodeKindName(m_BankCodeKind);
    dlg.Date = ALG_SBDEPDOC_1.Date_Document;
    dlg.PayerAccount = ALG_SBDEPDOC_1.Account; 
    if ( ClientList.GetRecord( ALG_SBDEPDOC_1.CodClient ) )
      dlg.FIO = ClientList.CurRec.ConvertFioFull;
    end;
    dlg.Sum = ALG_SBDEPDOC_1.OutSum;
    dlg.Total = ALG_SBDEPDOC_1.OutSum;

    dlg.Comment        = PMNT_PROP_REC_BUF.Ground;

    if(PMNT_PROP_REC_BUF.Priority != 0)
    dlg.Priority       = PMNT_PROP_REC_BUF.Priority;
    else
      dlg.Priority       = DEF_PRIORITY;  
    end;
      
    if( ( getBitFlag( ALG_SBDEPDOC_1.Flags, 31 ) == 1 ) and
        ( findPayF( ALG_SBDEPDOC_1.ListTransfer ) ) )
      dlg.Receiver = trn_payf.NameBusines;
    end;
    
    pumpBranch( dlg );
    /* плановое перечисление */
    if ( ( getBitFlag( ALG_SBDEPDOC_1.Flags, 29 ) == 1 ) and
         ( findPlanPay( ALG_SBDEPDOC_1.ListTransfer ) ) )
      dlg.Dogovor     = dplanpay.NumberContract;
      dlg.DateDogovor = dplanpay.Start_DatePay;
    /* безналичный платеж */
    elif( ( getBitFlag( ALG_SBDEPDOC_1.Flags, 31 ) == 1 ) and
          ( findPayF( ALG_SBDEPDOC_1.ListTransfer ) ) )
      dlg.Dogovor         = trn_payf.NumberContract;
      dlg.DateDogovor     = trn_cntr.DateContract;
    end;
    clearRecord( currency );
    currency.Code_Currency = ALG_SBDEPDOC_1.Code_Currency;
    if( GetEQ( currency ) )
      dlg.RateName = currency.Short_Name;
      if ( not GetAllCurrRate( ALG_SBDEPDOC_1.Date_Document, ALG_SBDEPDOC_1.Code_Currency, rateCB ) )
        dlg.USD_CBRate = 0;
      else
        dlg.USD_CBRate = rateCB;
      end;
    end;
    UpdateFields( dlg );
  end;
  /* переход от поля к полю */
  macro onRemFocus( dlg, activeField )
    
    if( activeField == "Penny" )
      if( dlg.Sum != $0.0 ) dlg.PennyPercent = dlg.Penny / dlg.Sum * 100; end;
      dlg.Total = dlg.Sum   + dlg.Penny;
      UpdateFields( dlg );
    elif( activeField == "Sum" )
      dlg.Penny = dlg.PennyPercent * dlg.Sum / 100;
      dlg.Total = dlg.Sum          + dlg.Penny;
    else
      onRemFocus( dlg, activeField );
    end;
  end;

  /* выход из поля */
  macro onEXITFIELD( dlg, activeField )
    onEXITFIELD( dlg, activeField );
  end;

  /* выбор */
  macro onF3( dlg, activeField )
    
    onF3( dlg, activeField );
  end;
  /* сохранение */
  macro onSave( dlg )
    var chk = check63_CB( dlg, m_PartyID );
    if ( chk == false )
      return false;
    end;    
    if( ( dlg.Receiver  == "" ) or
        ( dlg.Comment          == "" ) )
      return false;
    else
      return true;
    end;
  end;
  /* финальное заполнение структур */
  macro fillVarPart( dlg, constPart, varPart )
    fillVarPart( dlg, constPart, varPart );
    PMNT_PROP_REC_BUF.Ground   = dlg.Comment;
    PMNT_PROP_REC_BUF.Priority = dlg.Priority;
  end;
end;
/********************************************************************* 63.53 */
private class (CActionsOn63_5x) CActionsOn63_53
  /* выход из поля */
  macro onEXITFIELD( dlg, activeField )
    onEXITFIELD( dlg, activeField );
  end;

  /* выбор из справочника */
  macro onF3( dlg, activeField )
    var id = -1;
    if( ( activeField == "Receiver" ) or
        ( activeField == "INN"    ) or
        ( activeField == "KPP"    ) or
        ( activeField == "Accnt"  ) )
      id = Список_Получателей_КП;
    end;
    
    if( ( id >= 0 ) and ( findCPRecipient( id ) == true ) )
      dlg.Bank        = rtcp_recipient.BankName;
      m_BankCodeKind  = 3;
      dlg.CodeKind    = FindBankCodeKindName(m_BankCodeKind);
      dlg.BankCode    = rtcp_recipient.BankCode;
      dlg.CorAcc      = rtcp_recipient.CorrAccount;
      dlg.Receiver    = rtcp_recipient.Name;
      dlg.INN         = rtcp_recipient.INN;
      dlg.KPP         = rtcp_recipient.KPP;
      dlg.Accnt       = rtcp_recipient.Account;
      m_PartyID       = FindPartyIDByCode(rtcp_recipient.BankCode);
      UpdateFields( dlg );
    end;
    onF3( dlg, activeField );
  end;
end;
/********************************************************************* 63.54 */
private class (CActionsOn63_5x) CActionsOn63_54
end;
/************************************ пользовательские подвиды 63ей операции */
private class (CBaseActions) CActionsOn63others
  /* подкачать код банка */
  
  /* инициализация общих полей диалога */
  macro onInit( dlg, constPart, varPart )
    
    onInit( dlg, constPart, varPart );

    UpdateFields( dlg );
  end;
  /* при сохранении данных (F9) */
  macro onSave( dlg )
    var chk = check63_CB( dlg, m_PartyID );
    return chk; /* пользовательская => не знаем что нужно */
  end;

  /* выход из поля */
  macro onEXITFIELD( dlg, activeField )
    onEXITFIELD( dlg, activeField );
  end;

  /* при выборе из справочника (F3) */
  macro onF3( dlg, activeField )
    

    onF3( dlg, activeField );
    UpdateFields( dlg );
  end;
  /* финальное заполнение структур */
  macro fillVarPart( dlg, constPart, varPart )

    fillVarPart( dlg, constPart, varPart );
    
    PMNT_PROP_REC_BUF.Ground   = dlg.PaymAssignment;
    PMNT_PROP_REC_BUF.Priority = dlg.Priority;
  end;
end; //CActionsOn63others
/********************************************************************* 63.05 */
private class (CBaseActions) CActionsOn63_05
  /* инициализация */
  macro onInit( dlg, constPart, varPart )
    onInit( dlg, constPart, varPart );

    if ( ALG_SBDEPDOC_1.iApplicationKind != SB_CASHOPERT )
      
      if( fldIndex( dlg, "BankCode") != -1)
      disableFields( dlg, fldIndex( dlg, "BankCode"      ) );
      end;
      if( fldIndex( dlg, "CorAcc") != -1)
      disableFields( dlg, fldIndex( dlg, "CorAcc" ) );
      end;
      if( fldIndex( dlg, "Bank") != -1)
      disableFields( dlg, fldIndex( dlg, "Bank"     ) );
      end;
      UpdateFields( dlg );
      /* фокус на поле "Счет №" */
      setFocus( dlg, fldIndex( dlg, "Accnt" ) );
    end;
    return CM_IGNORE;
  end;
  /* инициализация */
  macro onRemFocus( dlg, activeField )
    
    if( activeField == "Accnt" )
    else
      onRemFocus( dlg, activeField );
    end;
  end;

  /* выход из поля */
  macro onEXITFIELD( dlg, activeField )
    onEXITFIELD( dlg, activeField );
  end;

  /* выбор */
  macro onF3( dlg, activeField )
    var partyId;
    var CodeKind, CodeKindName;
    record selAccnt( "depositr.dbt" );
    
    if( activeField == "Accnt" )
      if( iListBankAccount( ALG_SBDEPDOC_1.Code_Currency, dlg.Accnt ) )
        if( iFindBankAccount( dlg.Accnt, ALG_SBDEPDOC_1.Code_Currency, selAccnt ) )
          iFindBankClnt( selAccnt.CodClient, dlg.Receiver, dlg.INN, dlg.KPP );
        end;
      end;
    elif (activeField == "CodeKind")
            if (SelectCodeKind( CodeKind, CodeKindName ) == true)
                m_BankCodeKind = CodeKind;
                dlg.CodeKind = CodeKindName;
                dlg.BankCode = findDprtPartyCode(NumFnCash(), m_BankCodeKind);
                // other bank data not change!
            /*  if( dlg.BankCode != "" )
                    iFindBank( m_BankCodeKind, dlg.BankCode, dlg.Bank, 
                            dlg.CorAcc, dlg.INN, dlg.KPP, partyId );
                    dlg.Bank = {OperDprtFullName};
                    m_PartyID = partyId;*/
    end;
  end;
    UpdateFields( dlg );
  end;
  /* сохранение */
  macro onSave( dlg )
    var chk = check63_CB( dlg, m_PartyID );
    if ( chk == false )
      return false;
    end;    
    if( dlg.KPP == "" )
      return false;
    elif( onSave( dlg ) == true )
      if( iFindBankAccount( dlg.Accnt, ALG_SBDEPDOC_1.Code_Currency ) != true )
        if( MsgBoxEx( "Счет получателя не найден. Продолжить проводку?", MB_YES + MB_NO, IND_NO ) == IND_NO )
          return false;
        end;
      end;
      return true;
    else
      return false;
    end;
  end;
  /* финальное заполнение структур */
  macro fillVarPart( dlg, constPart, varPart )

    Private var clientList = TClientList;
    
    fillVarPart( dlg, constPart, varPart );
    if( ClientList.GetRecord( ALG_SBDEPDOC_1.CodClient ) )
      varPart.rec.PayerINN = ClientList.CurRec.Rec.INN;
      PMNT_PROP_REC_BUF.INN = ClientList.CurRec.Rec.INN;
    end;
    if( varPart.rec.PayerINN == "" )
      varPart.rec.PayerINN  = "0000000000";
      PMNT_PROP_REC_BUF.INN = "0000000000";
    end;
  end;
end;
/********************************************************************* 63.06 */
private class (CBaseActions) CActionsOn63_06
  /* инициализация */
  macro onInit( dlg, constPart, varPart )
    
    onInit( dlg, constPart, varPart );
    /* из rt_paym */

      dlg.Receiver             = PMNT_PROP_REC_BUF.Name;
      dlg.Priority             = PMNT_PROP_REC_BUF.Priority;
      dlg.PaymAssignment       = PMNT_PROP_REC_BUF.Ground;
      
    if( constPart != null )
      
      dlg.TaxAuthorState       = varPart.TaxAuthorState;
      dlg.KBK                  = varPart.KBK;
      dlg.OKATO                = varPart.OKATO;
      dlg.NalPayAssignment     = varPart.NalPayAssignment;
      dlg.NalPayAssignmentText = getNalPayAssignmentText( dlg.NalPayAssignment );
      dlg.NalPeriod            = varPart.NalPeriod;
      dlg.NalDocNumber         = varPart.NalDocNumber;
      dlg.NalDocDate           = varPart.NalDocDate;
      dlg.TotalSum             = varPart.SumNS  + varPart.SumAV +
                                 varPart.SumPE  + varPart.SumPC +
                                 varPart.SumSA  + varPart.SumASh +
                                 varPart.SumISh + varPart.SumPL +
                                 varPart.SumGP  + varPart.SumVZ;
      dlg.TaxPaymentType       = varPart.TaxPaymentType;
      
      if( dlg.TaxAuthorState == "" )dlg.TaxAuthorState = "13";                  end;
      if( dlg.Priority       == 0  )dlg.Priority       = DEF_PRIORITY_TAX;      end;
      if( dlg.TotalSum       == $0 )dlg.TotalSum       = ALG_SBDEPDOC_1.OutSum; end;
    /* из sbdepdoc */
    else
      dlg.NalDocDate           = ALG_SBDEPDOC_1.Date_Document;
      dlg.NalDocNumber         = "0";
      dlg.INN                  = ALG_SBDEPDOC_1.CP_INN;
      dlg.TaxAuthorState       = "13";
      dlg.Priority             = DEF_PRIORITY_TAX;
      dlg.TotalSum             = ALG_SBDEPDOC_1.OutSum;
    end;
    UpdateFields( dlg );
  end;

  /* выход из поля */
  macro onEXITFIELD( dlg, activeField )
    onEXITFIELD( dlg, activeField );
  end;

  /* выбор */
  macro onF3( dlg, activeField )
    
    var idAssignment;

    if( activeField == "OKATO" )
      dlg.OKATO = ListRegion();
      UpdateFields( dlg );
    elif( activeField == "KBK" )
      iListKBK( dlg.KBK );
    elif( ( activeField == "NalPayAssignment"     ) or
          ( activeField == "NalPayAssignmentText" )   )
      idAssignment = list_alg( 330 );
      dlg.NalPayAssignmentText = getNameAlg( 330, idAssignment );
      if ( substr( dlg.NalPayAssignmentText, 1, 2 ) == "0 " )
        dlg.NalPayAssignment = "0";
      else
        dlg.NalPayAssignment = substr( dlg.NalPayAssignmentText, 1, 2 );
      end;
      UpdateFields( dlg );
    elif( activeField == "TaxPaymentType" )
      if( BankVer() == 500 )
        iListLLVALUES( CLASSIF_TAXPAYMENTTYPE, null, dlg.TaxPaymentType );
      else
        iListLLVALUES( OBJTYPE_TAXPAYTYPE, null, dlg.TaxPaymentType );
      end;
    else
      onF3( dlg, activeField );
    end;

  end;

   macro checkTaxAuthorState (dlg) : bool
   var num = int (dlg.TaxAuthorState);
   if((num < 1) or (num > 26))
     return true;
   elif ((num >= 1) and (num <= 9) and (substr(dlg.TaxAuthorState, 1, 1) != 0) )
     return true;
   else
     return false;
   end;
   end;
  
  /* сохранение */
  macro onSave( dlg )
    var chk = check63_CB( dlg, m_PartyID );
    if ( chk == false )
      return false;
    end;
    glTaxAuthorStateCorrect = true;
    if(checkTaxAuthorState(dlg))
         //msgboxex("Неверно задано значение поля статус составителя", MB_ERROR, NULL, "", "");
         glTaxAuthorStateCorrect = false;
         return false;
    end;
    if( ( dlg.INN              == ""              ) or
        ( dlg.KPP              == ""              ) or
        ( dlg.TaxAuthorState   == ""              ) or
        ( dlg.KBK              == ""              ) or
        ( dlg.OKATO            == ""              ) or
        ( dlg.NalPayAssignment == ""              ) or
        ( dlg.NalPeriod        == ""              ) or
        ( dlg.NalDocNumber     == ""              ) or
        ( dlg.NalDocDate       == Date( 0, 0, 0 ) ) or
        ( ( dlg.TotalSum == $0 ) and ( not isFillOnlyRtPaym ) ) or
        ( dlg.TaxPaymentType   == ""              ) or
        ( dlg.NalPayAssignment == ""              ) )
      return false;
    else
      return onSave( dlg );
    end;
  end;
  /* финальное заполнение структур */
  macro fillVarPart( dlg, constPart, varPart )
    
    Private var clientList = TClientList;
    
    fillVarPart( dlg, constPart, varPart );
    
    PMNT_PROP_REC_BUF.INN              = dlg.INN;
    PMNT_PROP_REC_BUF.KPP              = dlg.KPP;
    PMNT_PROP_REC_BUF.Priority         = dlg.Priority;
    PMNT_PROP_REC_BUF.Ground           = dlg.PaymAssignment;

    varPart.rec.TaxAuthorState   = dlg.TaxAuthorState;
    varPart.rec.KBK              = dlg.KBK;
    varPart.rec.OKATO            = dlg.OKATO;
    varPart.rec.NalPayAssignment = dlg.NalPayAssignment;
    varPart.rec.NalPeriod        = dlg.NalPeriod;
    varPart.rec.NalDocNumber     = dlg.NalDocNumber;
    varPart.rec.NalDocDate       = dlg.NalDocDate;
    varPart.rec.PayDocStatus     = 2;
    varPart.rec.TaxPaymentType   = dlg.TaxPaymentType;

    if  ( varPart.rec.TaxPaymentType == "НС" ) varPart.rec.SumNS  = dlg.TotalSum;
    elif( varPart.rec.TaxPaymentType == "АВ" ) varPart.rec.SumAV  = dlg.TotalSum;
    elif( varPart.rec.TaxPaymentType == "ПЕ" ) varPart.rec.SumPE  = dlg.TotalSum;
    elif( varPart.rec.TaxPaymentType == "ПЦ" ) varPart.rec.SumPC  = dlg.TotalSum;
    elif( varPart.rec.TaxPaymentType == "СА" ) varPart.rec.SumSA  = dlg.TotalSum;
    elif( varPart.rec.TaxPaymentType == "АШ" ) varPart.rec.SumASh = dlg.TotalSum;
    elif( varPart.rec.TaxPaymentType == "ИШ" ) varPart.rec.SumISh = dlg.TotalSum;
    elif( varPart.rec.TaxPaymentType == "ПЛ" ) varPart.rec.SumPL  = dlg.TotalSum;
    elif( varPart.rec.TaxPaymentType == "ГП" ) varPart.rec.SumGP  = dlg.TotalSum;
    elif( varPart.rec.TaxPaymentType == "ВЗ" ) varPart.rec.SumVZ  = dlg.TotalSum;
    end;

    /*ryazhskih*/
    /*Закомментировала код, где вместо ИНН подставляется ИНН клиента*/
    /*if( ClientList.GetRecord( ALG_SBDEPDOC_1.CodClient ) )
      varPart.rec.PayerINN  = ClientList.CurRec.Rec.INN;
      PMNT_PROP_REC_BUF.INN = ClientList.CurRec.Rec.INN;
    end;*/
    /*ryazhskih end*/
    if( varPart.rec.PayerINN == "" )
      /*ryazhskih*/
      if(PMNT_PROP_REC_BUF.INN!="")
         varPart.rec.PayerINN = PMNT_PROP_REC_BUF.INN;
      end;
      /*ryazhskih end*/
    end;
  end;
end;
/********************************************************************* 63.07 */
private class (CActionsOn63_06) CActionsOn63_07
  /* инициализация */
  macro onInit( dlg, constPart, varPart )
    
    onInit( dlg, constPart, varPart );
    
    if( fldIndex( dlg, "BankCode") != -1)
    disableFields( dlg, fldIndex( dlg, "BankCode"      ) );
    end;
    if( fldIndex( dlg, "CorAcc") != -1)
    disableFields( dlg, fldIndex( dlg, "CorAcc" ) );
    end;
    if( fldIndex( dlg, "Bank") != -1)
    disableFields( dlg, fldIndex( dlg, "Bank"     ) );
    end;
    if( fldIndex( dlg, "Receiver") != -1)
    disableFields( dlg, fldIndex( dlg, "Receiver" ) );
    end;
    /* фокус на поле "Счет №" */
    setFocus( dlg, fldIndex( dlg, "Accnt" ) );
    UpdateFields( dlg );
    return CM_IGNORE;
  end;

  /* выход из поля */
  macro onEXITFIELD( dlg, activeField )
    onEXITFIELD( dlg, activeField );
  end;

  /* выбор */
  macro onF3( dlg, activeField )
    var partyId;
    var CodeKind, CodeKindName;
    var idAssignment;
    record selAccnt( "depositr.dbt" );

    if( activeField == "Accnt" )
      if( iListBankAccount( ALG_SBDEPDOC_1.Code_Currency, dlg.Accnt ) )
        if( iFindBankAccount( dlg.Accnt, ALG_SBDEPDOC_1.Code_Currency, selAccnt ) )
          iFindBankClnt( selAccnt.CodClient, dlg.Receiver, dlg.INN, dlg.KPP );
        end;
      end;
    elif( ( activeField == "NalPayAssignment"     ) or
          ( activeField == "NalPayAssignmentText" )   )
      idAssignment = list_alg( 330 );
      dlg.NalPayAssignmentText = getNameAlg( 330, idAssignment );
      dlg.NalPayAssignment = substr( dlg.NalPayAssignmentText, 1, 2 );
      UpdateFields( dlg );
    elif( activeField == "TaxPaymentType" )
      if( BankVer() == 500 )
        iListLLVALUES( CLASSIF_TAXPAYMENTTYPE, null, dlg.TaxPaymentType );
    else
        iListLLVALUES( OBJTYPE_TAXPAYTYPE, null, dlg.TaxPaymentType );
      end;
    elif (activeField == "CODEKIND")
            if (SelectCodeKind( CodeKind, CodeKindName ) == true)
                m_BankCodeKind = CodeKind;
                dlg.CodeKind = CodeKindName;
                dlg.BankCode = findDprtPartyCode(NumFnCash(), m_BankCodeKind);
  end;
    else
      onF3( dlg, activeField ); 
    end;
    UpdateFields( dlg );
  end;
  /* сохранение */
  macro onSave( dlg )
    var chk = check63_CB( dlg, m_PartyID );
    if ( chk == false )
      return false;
    end;    
    if( onSave( dlg ) == true )
      if( iFindBankAccount( dlg.Accnt, ALG_SBDEPDOC_1.Code_Currency ) != true )
        if( MsgBoxEx( "Счет получателя не найден. Продолжить проводку?", MB_YES + MB_NO, IND_NO ) == IND_NO )
          return false;
        end;
      end;
      return true;
    else
      return false;
    end;
  end;
end;
/************************************************************************ 67 */
private class (CBaseActions) CActionsOn67
  /* инициализация */
  macro onInit( dlg, constPart, varPart )
    
    
    onInit( dlg, constPart, varPart );
    enableFields( dlg, fldIndex( dlg, "CorrBranch"));
    dlg.CorrBranch     = PMNT_PROP_REC_BUF.CorrBranch;
    dlg.PaymAssignment = PMNT_PROP_REC_BUF.Ground;
    UpdateFields( dlg );
  end;
  /* сохранение */
  macro onSave( dlg )
    var filPay = getFilialNum();
    var brnRec = -1;
    var filRec = -1;
    PMNT_PROP_REC_BUF.PartyID = m_PartyID;
    if ( m_PartyID > 0 )
      brnRec = FindBankCodeByPartyID( m_PartyID );
    end;
    if ( brnRec > 0 )
      filRec = getFilialNum( brnRec );
    end;
    if ( filPay == filRec )
      if (not GetTrue(false,GetMessage(6917))) // user has cancelled operation
        return CM_CANCEL;
      end;
    end;
    if( ( dlg.CorrBranch  == "" ) or
        ( dlg.PaymAssignment == "" ) )
      return false;
    else
      return onSave( dlg );
    end;
  end;
  /* финальное заполнение структур */
  macro fillVarPart( dlg, constPart, varPart )
    
    fillVarPart( dlg, constPart, varPart );
    
    PMNT_PROP_REC_BUF.Ground     = dlg.PaymAssignment;
    PMNT_PROP_REC_BUF.CorrBranch = dlg.CorrBranch;
  end;
end;
/********************************************************************* 71.25 */
private class (CActionsOn63_1) CActionsOn71_25
  /* финальное заполнение структур */
  macro onInit( dlg, constPart, varPart )
  
   var result= onInit( dlg, constPart, varPart );
   dlg.ReceiverText = "Плательшик";
   SetDlgCaption(dlg, "Реквизиты плательщика");    
   if( constPart == null )
     //dlg.Priority = 0;
     UpdateFields( dlg );
  end;
 
    return result;
  end;

  macro fillVarPart( dlg, constPart, varPart )
    
    fillVarPart( dlg, constPart, varPart );
  end;
end;
/********************************************************************* 71.12 */
private class (CActionsOn63_05) CActionsOn71_12
  /* инициализация */
  macro onInit( dlg, constPart, varPart )
    
    var result = onInit( dlg, constPart, varPart );
    dlg.ReceiverText = "Плательщик";
    SetDlgCaption(dlg, "Реквизиты плательщика");
    if( constPart == null )
      //dlg.Priority = 0;
      UpdateFields( dlg );
    end;

    return result;
  end;
  /* финальное заполнение структур */
  macro fillVarPart( dlg, constPart, varPart )
    
    fillVarPart( dlg, constPart, varPart );
  end;
end;
/********************************************************************* 73.25 */
private class (CActionsOn71_25) CActionsOn73_25
end;
/********************************************************************* 73.12 */
private class (CActionsOn71_12) CActionsOn73_12
end;
/************************************************************************ 20 */
private class (CBaseActions) CActionsOn20
  /* инициализация */
  macro onInit( dlg, constPart, varPart )
    
    if (GetBitFlag (OC_PAY_DOC.Flags, 4) != 0)
      dlg.FlagUrgent = "X";
    end;
    
    if (OC_PAY_DOC.Ground != "")
      dlg.PaymAssignment = OC_PAY_DOC.Ground;
    end;
    UpdateFields( dlg );
  end;
  /* сохранение */
  macro onSave( dlg )
    
    if( ( dlg.PaymAssignment == "" ) )
      return false;
    else
      return onSave( dlg );
    end;
  end;
  /* Проверка срочного платежа */
  macro addCheck( dlg )
    
    var hour;
    var mrot;
    var errCode;
    var limOper;
    var cbRate1;
    var cbRate2;

    if ( dlg.FlagUrgent == "" )
      return true;
    end;

    TimeSplit( Time( ), hour );

    /* Проверка допустимой даты и времени перевода */
    if ( IsHoliday( {curdate} ) or ( IsHoliday( {curdate} + 1 ) and ( hour > 12 ) ) )
      if ( not GetTrue( false, "Время перевода срочного перевода истекло. Продолжить?" ) )
        return false;
      end;
    end;

    /* Проверка корсчета */
    if ( not checkCorAccountForUrgent( dlg.BankCode, PMNT_PROP_REC_BUF.BankCodeKind, OC_PAY_DOC.CodCur ) )
      return false;
    end;

    /* Проверка задания ФИО */
    if ( StrLen( Trim( OC_PAY_DOC.ClientLastName ) ) == 0 )
      MsgBox( "Не задана фамилия клиента" );
      return false;
    end;

    /* Проверка суммы перевода для валютных операций */
    if ( ( OC_PAY_DOC.CodCur > 0 ) and ( OC_PAY_DOC.FlagRez == "" ) )
      if ( GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\ЛИМИТЫ\\СРОЧНЫЕ_МЕЖДУНАРОДНЫЕ_ПЕРЕВОДЫ\\МАКС_СУММА_В_USD", V_DOUBLE, mrot, errCode ) == V_DOUBLE )
        ;
      else
        return false;
      end;

      limOper = MoneyL( mrot );

      if ( OC_PAY_DOC.CodCur > 1 )
        if ( not GetAllCurrRate( OC_PAY_DOC.Date_Document, 1, cbRate1 ) )
          MsgBox( "Ошибка при определении кросс-курса валюты" );
          return false;
        end;

        if ( not GetAllCurrRate( OC_PAY_DOC.Date_Document, OC_PAY_DOC.CodCur, cbRate2 ) )
          MsgBox( "Ошибка при определении кросс-курса валюты" );
          return false;
        end;

        limOper = Money( limOper * cbRate1 / cbRate2 );
      end;

      if ( OC_PAY_DOC.InSum > limOper )
        MsgBox( "Сумма перевода превышает допустимое значение" );
        return false;
      end;
    end;

    return addCheck();
  end;

end;
/********************************************************************* 42.01 */
private class (CActionsOn63_05) CActionsOn42_01
  /* финальное заполнение структур */
  macro fillVarPart( dlg, constPart, varPart )
    
    fillVarPart( dlg, constPart, varPart );
    varPart.rec.PaymentDirection = DefPaymentDirection( null, null, dlg.BankCode, true );
  end;
end;
/********************************************************************* 42.02 */
private class (CActionsOn63_06) CActionsOn42_02
end;
/********************************************************* обработка событий */
private var saveValue;

macro keyHandler( dlg, cmd, id, key )
  var retValue = CM_DEFAULT;
  var userAttrs, varUserAttrs;
  var userAttrs1, varUserAttrs1;
  var flagAllFields = true;
  var grd;
  var result;
  record localRtPaym ( "rt_paym.dbt" );
  record localRtPaym1( "rt_paym.1"   );
  var oldRtPaym, oldRtPaym1;

if (not notActive)
  if( cmd == DLG_KEY )
    if( key == F3 )
      globalActions.onF3( dlg, FldName( dlg, id ) );
      saveValue = dlg(id);
    end;

    if( key == 362 ) // AltF3
      globalActions.onAltF3( dlg, FldName( dlg, id ) );
      saveValue = dlg(id);
    end;

    if ( key == CtrlF3 )
      globalActions.onCtrlF3( dlg, FldName( dlg, id ) );
      saveValue = dlg(id);
    end;
    if ( key == 32 )
      globalActions.onSpace( dlg, FldName( dlg, id ) );
    end;
    if (key == 27)
      globalActions.onEsc (dlg);
    end;
    if ( ( key == 9 )  OR  ( key == 328 )  OR  ( key == 336 ) )
      globalActions.onEXITFIELD( dlg, FldName( dlg, id ) );
    end;
  elif( cmd == DLG_INIT )
    
    if( rtpaymFindByAttrID( ALG_SBDEPDOC_1.iApplicationKind, ALG_SBDEPDOC_1.ApplicationKey, "РЕКВ_ПОЛ" ) )
      setRecordAddr( rt_paym1, rt_paym );
      retValue = globalActions.onInit( dlg, rt_paym, rt_paym1 );
      if( ( not isFillOnlyRtPaym ) and
          ( ( ALG_SBDEPDOC_1.iApplicationKind != 5 ) or
            ( ( ALG_SBDEPDOC_1.TypeOper != Зачисление_По_Данным_ПК ) and
              ( ALG_SBDEPDOC_1.TypeOper != Зачисление_В_Пак_Режиме )   ) ) )
        disableFields( dlg );
         notActive = true;
      end;
    elif( mdb_rtpaymFindByAttrID( ALG_SBDEPDOC_1.iApplicationKind, ALG_SBDEPDOC_1.ApplicationKey, "РЕКВ_ПОЛ" ) == true )
      userAttrs = getDocUserAttrPool();
      userAttrs1 = TRecHandler( "rt_paym.dbt" );
      varUserAttrs = userAttrs.overlayRecord( "rt_paym.1", 0, false );
      varUserAttrs1 = TRecHandler( "rt_paym.1" );
      xackCopy( userAttrs1, userAttrs );
      xackCopy( varUserAttrs1, varUserAttrs );
      copy( localRtPaym, userAttrs1 );
      copy( localRtPaym1, varUserAttrs1 );
      retValue = globalActions.onInit( dlg, localRtPaym, localRtPaym1 );
    else
      retValue = globalActions.onInit( dlg, null, null );
    end;
    if( retValue == null )
      retValue = CM_DEFAULT;
    end;
  elif( cmd == DLG_SAVE )

    if ( not globalActions.addCheck( dlg ) )
      return CM_CANCEL;
    end;

    if ( StrUpr( FldName( dlg, id ) ) == "ACCNT" )
      dlg.Accnt = delShablon( dlg.Accnt );
    end;

    flagAllFields=globalActions.onSave(dlg);
    if ( ( ValType( flagAllFields ) == V_INTEGER ) and ( flagAllFields == CM_CANCEL ) ) // user has cancelled operation
      return CM_CANCEL;
    end;

    if ( not flagAllFields )
      if( ( rtpaymFindByAttrID( ALG_SBDEPDOC_1.iApplicationKind, ALG_SBDEPDOC_1.ApplicationKey, "РЕКВ_ПОЛ" ) == false ) or
          ( isFillOnlyRtPaym == true ) )
        if (glTaxAuthorStateCorrect == false)
          msgbox("Значение поля СТАТУС СОСТАВИТЕЛЯ некорректно");
          setfocus(dlg, 8); // Устанавливаем фокус ввода в поле СТАТУС СОСТАВИТЕЛЯ
          flagAllFields = false;
        else
        if ( not GetTrue( false, "Заполнены не все поля. Продолжить проводку ?" ) )
          flagAllFields = false;
        else
          flagAllFields = true;
        end;
      end;
        
      end;
    end;

    if ( (flagAllFields) and (ALG_SBDEPDOC_1.Account != "") )
      if ( SubStr( ALG_SBDEPDOC_1.Account, 6, 3 ) != SubStr( dlg.Accnt, 6, 3 ) )
        MsgBox( "Валюта счета плательщика не совпадает|с валютой счета получателя");
        SetFocus( dlg, 8 ); // Устанавливаем фокус ввода в поле СЧЕТ
        flagAllFields = false;
      end;
    end;
	
	if ( ((ALG_SBDEPDOC_1.ApplType == 6) or (ALG_SBDEPDOC_1.ApplType == 2) or (ALG_SBDEPDOC_1.ApplType == 7) or (( OC_PAY_DOC.NumOpert == 42 ) and ( OC_PAY_DOC.ApplType == PAYWOACC_NAL_TO_ODB ))) and (dlg.Priority > DEF_PRIORITY_TAX) )
		MsgBox("Неверно задана очередность платежа");
		SetFocus( dlg, 9 );
		flagAllFields = false;
	elif ( dlg.Priority > DEF_PRIORITY )
		MsgBox("Неверно задана очередность платежа");
		SetFocus( dlg, 12 );
		flagAllFields = false;
	end;

    if( not flagAllFields )
      return CM_CANCEL;
    else
      if( ( rtpaymFindByAttrID( ALG_SBDEPDOC_1.iApplicationKind, ALG_SBDEPDOC_1.ApplicationKey, "РЕКВ_ПОЛ" ) == false ) or
          ( ( ALG_SBDEPDOC_1.iApplicationKind   ==                       5 ) and
            ( ( ALG_SBDEPDOC_1.TypeOper         == Зачисление_По_Данным_ПК ) or
              ( ALG_SBDEPDOC_1.TypeOper         == Зачисление_В_Пак_Режиме )   ) ) or
          ( isFillOnlyRtPaym == true ) )
        userAttrs = getDocUserAttrPool();
        userAttrs.clear();
        userAttrs.rec.Branch           = NumFNCash( );
        userAttrs.rec.iApplicationKind = ALG_SBDEPDOC_1.iApplicationKind;
        userAttrs.rec.ApplicationKey   = ALG_SBDEPDOC_1.ApplicationKey;
        userAttrs.rec.AttrID           = "РЕКВ_ПОЛ";
        varUserAttrs = userAttrs.overlayRecord( "rt_paym.1", 0, false );
        varUserAttrs.clear();

        if ( ( getBitFlag( ALG_SBDEPDOC_1.Flags, 29 ) == 1 ) ) /* длительное поручение */
          userAttrs.rec.Branch = ALG_SBDEPDOC_1.FNcash;
        end;
  
        globalActions.fillVarPart( dlg, userAttrs, varUserAttrs );

        if( rtpaymFindByAttrID( ALG_SBDEPDOC_1.iApplicationKind, ALG_SBDEPDOC_1.ApplicationKey, "РЕКВ_ПОЛ" ) == true )
          userAttrs1    = TRecHandler( "rt_paym.dbt" );
          varUserAttrs1 = TRecHandler( "rt_paym.1"   );
          xackCopy( userAttrs1, userAttrs ); xackCopy( varUserAttrs1, varUserAttrs );
          copy( rt_paym, userAttrs1 ); copy( rt_paym1, varUserAttrs1 );
          update( rt_paym );
        else
          oldRtPaym  = TRecHandler( "rt_paym.dbt" );
          oldRtPaym1 = TRecHandler( "rt_paym.1"   );

          xackCopy( oldRtPaym,  userAttrs    );
          xackCopy( oldRtPaym1, varUserAttrs );
         
          if ( mdb_rtpaymFindByAttrID( ALG_SBDEPDOC_1.iApplicationKind, ALG_SBDEPDOC_1.ApplicationKey, "РЕКВ_ПОЛ" ) == true )
             userAttrs.delete( );
             xackCopy( userAttrs,    oldRtPaym  );
             xackCopy( varUserAttrs, oldRtPaym1 );
          end;
          userAttrs.insert( varUserAttrs.recSize );
        end;

        if ( ( OC_PAY_DOC.NumOpert == 42 ) and ( OC_PAY_DOC.ApplType == PAYWOACC_NAL_TO_ODB ) )
          if ( varUserAttrs.rec.KBK != "" )
            StoreValue( "doc:ВБюджет", true );
          else
            StoreValue( "doc:ВБюджет", false );
          end;

          StoreValue( "doc:_101_СтатусСоставителя", varUserAttrs.rec.TaxAuthorState);
          StoreValue( "doc:_104_КБК", varUserAttrs.rec.KBK );
          StoreValue( "doc:_105_ОКТМО", varUserAttrs.rec.OKATO );
          StoreValue( "doc:_106_ПоказОснованияПлатежа", varUserAttrs.rec.NalPayAssignment );
          StoreValue( "doc:_107_ПоказНалПериода", varUserAttrs.rec.NalPeriod );
          StoreValue( "doc:_108_ПоказНомДок", varUserAttrs.rec.NalDocNumber );
          StoreValue( "doc:_109_ПоказДатыДок", varUserAttrs.rec.NalDocDate );
          StoreValue( "doc:_110_ТипПлатежа", varUserAttrs.rec.TaxPaymentType );
          StoreValue( "doc:УИ_Начисления", userAttrs.rec.chargeId);
          StoreValue( "doc:ИдентифПлательщика", userAttrs.rec.personId);
          StoreValue( "doc:ИндексДокумента", userAttrs.rec.docIndex);
        end;
      end;

    end;
  elif( cmd == DLG_SETFOCUS )
    saveValue = dlg(id);
  elif( cmd == DLG_REMFOCUS )
    if ( saveValue != dlg(id) )
      globalActions.onRemFocus( dlg, FldName( dlg, id ) );
    end;
  end;
end;
  return retValue;
end;
/*****************************************************************************\
| Все что дальше - точки входа в макрос:                                      |
|  mainFun - для вкладных и прочих операций                                   |
|  mainFun_fillRtPaym - для плановых перечислений и групп счетов              |
\*****************************************************************************/
/********************************* главнейший макрос (для вкладных и прочих) */
macro mainFun( isPayOperat )
  
  var userAttrs, varUserAttrs, grd;
  var currCode, currCode_Recv;

  vPartyID = -1;
  
  if( /* не вкладная операция? */
      (
        ( isPayOperat         == true ) and
        ( OC_PAY_DOC.NumOpert != 20   ) and
        ( OC_PAY_DOC.NumOpert != 42   )
      ) or
      /* вкладная операция */
      (
        ( not isPayOperat ) and
        ( ALG_SBDEPDOC_1.TypeOper != Списание_По_Длит_Поруч  ) and
        ( ALG_SBDEPDOC_1.TypeOper != Списание_Карточное  ) and

        ( ALG_SBDEPDOC_1.TypeOper != Списание_В_Чужой_Филиал ) and
        ( ALG_SBDEPDOC_1.TypeOper != Списание_По_Раз_Поруч   ) and
        ( ( ALG_SBDEPDOC_1.TypeOper != Зачисление_По_Данным_ПК ) or ( ( ALG_SBDEPDOC_1.ApplType != 25 ) and ( ALG_SBDEPDOC_1.ApplType != 12 ) ) ) and
        ( ( ALG_SBDEPDOC_1.TypeOper != Зачисление_В_Пак_Режиме ) or ( ( ALG_SBDEPDOC_1.ApplType != 25 ) and ( ALG_SBDEPDOC_1.ApplType != 12 ) ) ) and
        ( ( Index( ALG_DEPOSITOR.UserTypeAccount, "Щ" ) <= 0 ) or
          ( ALG_SBDEPDOC_1.TypeOper != Списание_Простое ) or
          ( ALG_SBDEPDOC_1.ApplType == Безналичная_Комиссия ) or
          ( ALG_SBDEPDOC_1.ApplType == ATI_SK_LOST ) or
          ( ALG_SBDEPDOC_1.ApplType == ATI_SK_GET ) )
      )
    )
    if ( ( ALG_SBDEPDOC_1.TypeOper == Списание_Простое ) and
         ( Index( ALG_DEPOSITOR.UserTypeAccount, "Щ" ) > 0 ) )
      return OK_RES;
    else
      return SKIP_RES;
    end;
  end;
  if( not isPayOperat )
    if( (ALG_SBDEPDOC_1.TypeOper == Списание_По_Раз_Поруч ) or (ALG_SBDEPDOC_1.TypeOper == Списание_Карточное) )
      /*
      if( ALG_SBDEPDOC_1.ApplType == 0 )
        globalActions = CActionsOn63_0;
        usedDlg = otherP_INxC63xx;
      elif
      */

      if( ALG_SBDEPDOC_1.ApplType == 1 )
        globalActions = CActionsOn63_1;
        usedDlg = otherP_INxC63xx;
       // end;
      elif( ( ALG_SBDEPDOC_1.ApplType == 53 ) or ( ALG_SBDEPDOC_1.ApplType == 54 ) )
        return OK_RES;
      elif( ALG_SBDEPDOC_1.ApplType == 32 )
        globalActions = CActionsOn63_32;
        usedDlg = otherP_INxC63xx;
      elif( ALG_SBDEPDOC_1.ApplType == 5 )
        globalActions = CActionsOn63_05;
        usedDlg = otherP_INxC63xx;
      elif( ALG_SBDEPDOC_1.ApplType == 6 )
        globalActions = CActionsOn63_06;
        usedDlg = P_INxC6306;
      elif( ALG_SBDEPDOC_1.ApplType == 7 )
        globalActions = CActionsOn63_07;
        usedDlg = P_INxC6307;
      else /* пользовательская */
        globalActions = CActionsOn63others;
        usedDlg = otherP_INxC63xx;
      end;
    elif( ALG_SBDEPDOC_1.TypeOper == Списание_По_Длит_Поруч )
      return SKIP_RES;
    elif ( ( ALG_SBDEPDOC_1.TypeOper == Списание_Простое )  AND
           ( ALG_SBDEPDOC_1.ApplType != ATI_SK_LOST )  AND
           ( ALG_SBDEPDOC_1.ApplType != ATI_SK_GET ) )
      globalActions = CActionsOn63_1;
      usedDlg = otherP_INxC63xx;
    elif( ALG_SBDEPDOC_1.TypeOper == Списание_В_Чужой_Филиал )
      usedDlg = otherP_INxC63xx;
      globalActions = CActionsOn67;
    elif( ALG_SBDEPDOC_1.TypeOper == Зачисление_По_Данным_ПК )
      if( ALG_SBDEPDOC_1.ApplType == 25 )
        usedDlg = otherP_INxC63xx;
        globalActions = CActionsOn71_25;
      else
        usedDlg = otherP_INxC63xx;
        globalActions = CActionsOn71_12;
      end;
    elif( ALG_SBDEPDOC_1.TypeOper == Зачисление_В_Пак_Режиме )
      if( ALG_SBDEPDOC_1.ApplType == 25 )
        usedDlg = otherP_INxC63xx;
        globalActions = CActionsOn73_25;
      else
        usedDlg = otherP_INxC63xx;
        globalActions = CActionsOn73_12;
      end;
    end;
  else
    ReplaceMacro( "UpdateAlgRecords", "UpdateCashRecords" );
    /* инициализация структур */
    ALG_SBDEPDOC_1.iApplicationKind       = OC_PAY_DOC.iApplicationKind;
    ALG_SBDEPDOC_1.ApplicationKey         = OC_PAY_DOC.ApplicationKey;
    PMNT_PROP_REC_BUF.DocApplicationKind  = OC_PAY_DOC_PMNT_PROP.DocApplicationKind;
    PMNT_PROP_REC_BUF.DocApplicationKey   = OC_PAY_DOC_PMNT_PROP.DocApplicationKey;
    ALG_SBDEPDOC_1.CodClient              = OC_PAY_DOC.CodClient;
    ALG_SBDEPDOC_1.Code_Currency          = OC_PAY_DOC.CodCur;
    PMNT_PROP_REC_BUF.BankCode            = OC_PAY_DOC_PMNT_PROP.BankCode;
    PMNT_PROP_REC_BUF.CorrAccount         = OC_PAY_DOC_PMNT_PROP.CorrAccount;
    PMNT_PROP_REC_BUF.Account             = OC_PAY_DOC_PMNT_PROP.Account;
    ALG_SBDEPDOC_1.OutSum                 = OC_PAY_DOC.InSum;
    
    if ( GetBitFlag( OC_PAY_DOC.Flags, 4 ) == 1 )
      SetBitFlag( ALG_SBDEPDOC_1.Flags, 27, 1 );
    end;
    
    userAttrs = getDocUserAttrPool();
    userAttrs.clear();
    userAttrs.rec.Branch           = NumFNCash( );
    PMNT_PROP_REC_BUF.CorrBranch   = NumFNCash( );
    userAttrs.rec.iApplicationKind = ALG_SBDEPDOC_1.iApplicationKind;
    userAttrs.rec.ApplicationKey   = ALG_SBDEPDOC_1.ApplicationKey;
    userAttrs.rec.AttrID           = "РЕКВ_ПОЛ";
    
    //userAttrs.rec.RecivFIO         = OC_PAY_DOC_PMNT_PROP.Name;
    PMNT_PROP_REC_BUF.Name         = OC_PAY_DOC_PMNT_PROP.Name;
    
//    grd = userAttrs.overlayRecord( "ground.rec", userAttrs.FldOffset( "Payment" ), true );
    //grd.rec.ground                = OC_PAY_DOC.Ground;
    ALG_SBDEPDOC_1.Ground          = OC_PAY_DOC.Ground;
    
    varUserAttrs = userAttrs.overlayRecord( "rt_paym.1", 0, false );
    varUserAttrs.clear();
   // varUserAttrs.rec.CodeKind = RetrieveValue("CodeKindBIK");
    
    if (PMNT_PROP_REC_BUF.BankCodeKind == 0)
      PMNT_PROP_REC_BUF.BankCodeKind   = getDefaultCodeKind();
    end;
    
    PMNT_PROP_REC_BUF.BankName     = OC_PAY_DOC_PMNT_PROP.BankName;
    PMNT_PROP_REC_BUF.BankCode     = OC_PAY_DOC_PMNT_PROP.BankCode;
    PMNT_PROP_REC_BUF.CorrAccount  = OC_PAY_DOC_PMNT_PROP.CorrAccount;
    PMNT_PROP_REC_BUF.Account      = OC_PAY_DOC_PMNT_PROP.Account;
    PMNT_PROP_REC_BUF.INN          = OC_PAY_DOC_PMNT_PROP.INN;

    if ( ( OC_PAY_DOC_PMNT_PROP.INN == "" ) and ( OC_PAY_DOC.CorrINN != "" ) and ( OC_PAY_DOC.GroupOpert == 11 ) )
      PMNT_PROP_REC_BUF.INN = OC_PAY_DOC.CorrINN;
    end;
    
    varUserAttrs.rec.NalDocNumber = "0";
    varUserAttrs.rec.NalDocDate   = OC_PAY_DOC.Date_Document;
    varUserAttrs.rec.PayDocStatus = 2;
    
    if( ( OC_PAY_DOC.NumOpert == 42 ) and ( OC_PAY_DOC.ApplType == PAYWOACC_NAL_TO_ODB ) )
      PMNT_PROP_REC_BUF.Priority = DEF_PRIORITY_TAX;
    else
      PMNT_PROP_REC_BUF.Priority = DEF_PRIORITY;
    end;
    userAttrs.insert( varUserAttrs.recSize );
    if( OC_PAY_DOC.NumOpert == 20 )
      globalActions = CActionsOn20;
      usedDlg = otherP_INOT63xx;
    elif( OC_PAY_DOC.NumOpert == 42 )
      if( OC_PAY_DOC.ApplType == PAYWOACC_NAL_TO_ODB )
        globalActions = CActionsOn42_02;
        usedDlg = P_INOT6307;
      else
        globalActions = CActionsOn42_01;
        usedDlg = otherP_INOT63xx;
      end;
    end;
  end;

  if ( ( getBitFlag( ALG_SBDEPDOC_1.Flags, 29 ) == 1 ) ) /* длительное поручение */
    if ( PMNT_PROP_REC_BUF.Account != "" )
      currCode_Recv = substr(PMNT_PROP_REC_BUF.Account, 6, 3);
      currCode = substr(ALG_SBDEPDOC_1.Account, 6, 3);
      if( currCode_Recv != currCode )
        retValue = false;
        msgbox("Валюта счета плательщика не совпадает|с валютой счета получателя (ссудного счета)");
      end;
    else
      retValue = false;
      msgbox("Задайте счет получателя (ссудный счет)");
    end;
  end;

  if( retValue == true )
    if( ( RunDialog( usedDlg, "keyHandler" ) == true ) and ( retValue == true ) ) /* странно только на 1ый взгляд */
      retValue = true;
    else
      retValue = false;
    end;
  end;

  if( retValue )
    return OK_RES;
  else
    return ERR_RES;
  end;
end;

/************** главнейший макрос (для плановых перечислений и групп счетов) */
macro mainFun_fillRtPaym( DDAddress        /* адрес sbdepdoc'а  */,
                               PmntPropAdress )
  
  isFillOnlyRtPaym = true;
  setBuff( ALG_SBDEPDOC_1  , DDAddress        );
  setBuff( PMNT_PROP_REC_BUF, PmntPropAdress);
  if( mainFun( false ) == ERR_RES )
    return false;
  else
    return true;
  end;
end;

/************** Вызываемая процедура для вкладной операции 64 - коммунальный платеж */
macro mainFun_CP( DDAddress /* адрес sbdepdoc'а  */ )  
  // isFillOnlyRtPaym = true;
  setBuff( ALG_SBDEPDOC_1, DDAddress );
  ALG_RESULT = mainFun( false );
end;
