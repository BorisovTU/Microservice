/**********************************************************
*  ---------------------                                  *
*  R-Style Software Lab.                                  *
*  ---------------------                                  *
*  RS-Retail                                              *
*                                                         *
*  Отчет по счетам, которые не были пролонгированы        *
*                                                         *
*  Лебедев С.В.                                           *
*  30.08.99                                               *
**********************************************************/

import deprintr, RSD, SQLConv;

file currency (currency);
file sb_dtyp  (sb_dtyp );

file depositr (depositr);

var FlagCur    = NumFlagCur();
var FNCash     = NumFNCash();
var AccCounter = 0;

private var cmdProl, rsProl;


/**********************************************************
		       Шапка отчета
**********************************************************/
macro HeadReportForAllMustBeProlAccounts (EndProlDate)

  [ ];
  [ Счета, для которых срок пролонгации истекает до #](EndProlDate);

end;


/**********************************************************
		  Шапка отчета для валюты
**********************************************************/
macro HeadReportForMustBeProlAccounts

  [ ];
  [ Валюта #](currency.ExternalCode + " (" + currency.Name_Currency + ")");
  [+-------------------------+------------+----------+-----------+];
  [|       Номер счета       | Вид вклада |Дата прол.|Дата оконч.|];
  [+-------------------------+------------+----------+-----------+];

end;


/**********************************************************
		       Шапка отчета
**********************************************************/
macro BottomReportForMustBeProlAccounts

  if (AccCounter == 0)
    [|Необработанных счетов нет                                    |];
  end;

  [+-------------------------+------------+----------+-----------+];

end;


/**********************************************************
		 Написать о счете в отчет
**********************************************************/
macro WriteMustBeProlAccountToReport

  var DateForReport;

  if ( rslDate( rsProl.value( "dateP" ) ) != Date(0,0,0) )
    DateForReport = String( rslDate( rsProl.value( "dateP" ) ) );
  else
    DateForReport = String( rslDate( rsProl.value( "dateS" ) ) );
  end;

  [|#########################|############|##########|########## |]
  ( Acc_Form( rsProl.value( "acc" ) ), sb_dtyp.Kind, DateForReport, 
    String( rslDate( rsProl.value( "dateE" ) ) ) );

  AccCounter = AccCounter + 1;

end;


/**********************************************************
		Цикл по счетам вида вклада
**********************************************************/
macro ReportForAccountsOfDepTypep (EndProlDate)

  var stat;

  cmdProl = RsdCommand( "select t.t_account as acc, " +
                               "t.t_start_datedep as dateS, " +
                               "t.t_end_datedep as dateE, " +
                               "t.t_prol_datedep as dateP " +
                        "from ddepositr_dbt t " +
                        "where t.t_iscur = ? and " +
                              "t.t_fncash = ? and " +
                              "t.t_end_datedep <= ? and " +
                              "t.t_type_account = ? and " +
                              "t.t_code_currency = ? and " +
                              "t.t_Open_Close = chr(0) " +
                        "order by t.t_iscur DESC, " +
                                 "t.t_fncash DESC, " +
                                 "t.t_end_datedep DESC, " +
                                 "t.t_type_account DESC, " +
                                 "t.t_referenc DESC" );

  cmdProl.addParam( "isCur",  RSDBP_IN );
  cmdProl.addParam( "branch", RSDBP_IN );
  cmdProl.addParam( "date",   RSDBP_IN );
  cmdProl.addParam( "type",   RSDBP_IN );
  cmdProl.addParam( "curr",   RSDBP_IN );

  cmdProl.value( "isCur" )  = FlagCur;
  cmdProl.value( "branch" ) = FNCash;
  cmdProl.value( "date" )   = EndProlDate;
  cmdProl.value( "type" )   = sb_dtyp.Kind;
  cmdProl.value( "curr" )   = currency.Code_Currency;

  cmdProl.execute;

  rsProl = RsdRecordset( cmdProl );

  while ( rsProl.moveNext )
    if ( rslDate( rsProl.value( "dateE" ) ) != Date(0,0,0) )
      WriteMustBeProlAccountToReport();
    end;
  end;

end;


/**********************************************************
      Отчет по счетам, которые не были пролонгированы
**********************************************************/
macro ReportForMustBeProlAccounts (EndProlDate)

  var stat;
  var flag;
  var WasMustBeProlAccount = FALSE;

  HeadReportForAllMustBeProlAccounts(EndProlDate);
  /* Цикл по валютам */
  KeyNum(currency,0);
  currency.Code_Currency = 0;
  if (FlagCur)
    stat = GetGT(currency);
  else
    stat = GetEQ(currency);
  end;
  while (stat)
    AccCounter = 0;
    HeadReportForMustBeProlAccounts();
    /* Цикл по видам вкладов */
    KeyNum(sb_dtyp,1);
    sb_dtyp.FlagCur = FlagCur;
    sb_dtyp.Priorit = 0;
    flag = GetGE(sb_dtyp);
    while (flag)
      if (sb_dtyp.FlagCur == FlagCur)
	if ((sb_dtyp.FormContr > 0) AND (sb_dtyp.FlagNotProlong == 0))
	  Message(" Обрабатывается вклад ",sb_dtyp.Name," ",currency.Short_Name," ...");
	  ReportForAccountsOfDepTypep(EndProlDate);
	end;
	flag = Next(sb_dtyp);
      else
	flag = FALSE;
      end;
    end;
    if (AccCounter > 0)
      WasMustBeProlAccount = TRUE;
    end;
    BottomReportForMustBeProlAccounts();
    if (FlagCur)
      stat = Next(currency);
    else
      stat = FALSE;
    end;
  end;

  return WasMustBeProlAccount;

end;
