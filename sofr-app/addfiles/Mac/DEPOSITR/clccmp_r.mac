/*
  ---------------------
  R-Style Software Lab.
  ---------------------
  RS-Bank v6
  ---------------------
  RS-Retail
  ---------------------
  Макрос для компенсаций СБРФ:
  - в размере остатка
  - дополнительная в размере остатка
  ---------------------
  Расчет суммы компенсации

*/
import RSD, deprintr, issrvdoc;

private const NullDate = Date(0,0,0);
var CompensType      = 0;
var SumCompensBefore = $0,     // Сумма ранее выплаченных компенсаций 
                               // (за исключением оплаты ритуальных услуг)
    SumCompensRitual = $0,     // Ранее выплаченные суммы на оплату ритуальных услуг
    CompensCoef      = 1.0,    // Коэффициент пересчета компенсации
    RestOn20061991   = $0;     // Осаток на 20.06.1991
var FullSum          = $0, 
    Sum              = $0,
    AccCloseDate     = NullDate;

const
   CT8881 = 8881, // Компенсация в размере остатка
   CT8882 = 8882; // Дополнительная компенсация в размере остатка
const
   AT04 =  4,  // Инвалиды 1 группы 
   AT25 = 25,  // Родители и опекуны детей-инвалидов 
   AT58 = 58,  // Наследник участника
   AT63 = 63,  // Владельцам детских целевых 
   AT76 = 76,  // Родители и опекуны инвалидов с детства
   AT80 = 80,  // Дети - сироты
   AT88 = 88,  // Наследник инвалида 1 группы
   AT93 = 93,  // Вкладчики по 1942 г.р.
   AT94 = 94,  // Наследник вкладчика по 1942 г.р.
   AT95 = 95,  // Участник до 1935 г.р.
   AT96 = 96,  // Вкладчик по 1935 г.р.
   AT97 = 97;  // Наследник вкладчика по 1935 г.р.

// *********************************************************

macro isSumOfDrobIsOne(array_M, array_N, count_Legacy)
   var   i = 0;
   var   ОбщийЗнаменатель = 1;
   array Числитель;
   if ( count_Legacy < 2 )
      return 0;
   end;
   while (i < (count_Legacy-1) )
     array_M(i+1) = array_M(i)*array_N(i+1) + array_M(i+1)*array_N(i);
     array_N(i+1) = array_N(i)*array_N(i+1);
     i = i+1;
   end;
   if ( array_M(i) == array_N(i))
      return 1;  // Все доли распределены
   elif ( array_M(i) >= array_N(i))
      return 2;  // Числитель больше згнаменателя
   else
      return 0;  
   end;
end;
// ---------------------------------------------------------

macro MakeLegacyPart (compens)
//
// Обработка долей наследства
//
  var   cmd, rs;
  var   ClientType   = compens.rec.Appltype;
  var   count_Legacy = 0;
  ARRAY array_M, array_N;
  var   IsSum;
  var   SumCompens = 0;
//
// Сбор ранее выплаченных долей
//
  SumCompens = SumCompensBefore;
  cmd = RsdCommand(" select t_legacyPart_m, t_legacyPart_n from dcompens_dbt where t_account = ? and t_fncash = ? and t_appltype not in (17,26,56,81,82,83,98,99) and t_date <> '01.01.0001'" );
  cmd.addParam ("T_ACCOUNT", RsdBp_in);
  cmd.value    ("T_ACCOUNT") = compens.rec.Account;
  cmd.addParam ("T_FNCASH", RsdBp_in);
  cmd.value    ("T_FNCASH") = compens.rec.FNCash;
  cmd.execute;
  rs = RsdRecordset(cmd); 
  while ( rs.moveNext() )
    if ( (rs.value(0) == 0) or (rs.value(1) == 0) )
      array_M(count_Legacy) = 1;
      array_N(count_Legacy) = 1;
    elif( (rs.value(0) != 0) and (rs.value(1) != 0) ) 
      array_M(count_Legacy) = rs.value(0);
      array_N(count_Legacy) = rs.value(1);
    end;
    count_Legacy = count_Legacy + 1;
  end;
//
// Суммирование ранее выплаченных долей плюс доля текущей компенсации
//
  if (count_Legacy > 0)
    array_M(count_Legacy) = compens.rec.LegacyPart_m;
    array_N(count_Legacy) = compens.rec.LegacyPart_n;
    count_Legacy = count_Legacy + 1;
    isSum = isSumOfDrobIsOne(array_M, array_N, count_Legacy);
    if ( isSum == 1 )
      Sum = FullSum - SumCompens;
    elif (isSum == 2 )
      MsgBox("Сумма долей наследства больше 1");
      exit (1);
    else
      Sum = FullSum * compens.rec.LegacyPart_m / compens.rec.LegacyPart_n;
    end;            
  else
    Sum = FullSum * compens.rec.LegacyPart_m / compens.rec.LegacyPart_n;
  end;
end;
// ---------------------------------------------------------

macro IsHeir (ClientType)
//
// true - компенсацию получает наследник
//
  var RetVal = false;

  if ((ClientType == AT58) OR
      (ClientType == AT88) OR
      (ClientType == AT94) OR
      (ClientType == AT97))
    RetVal = true;
  end;
  return RetVal;
end;
// ---------------------------------------------------------

macro Check_SumCompensBefore (compens)
//
// Выплаченные ранее компенсации, кроме ритуальных услуг
//
  var cmd, rs;
  var ClientType = compens.rec.Appltype;
  if (CompensType == 8881)
    SumCompensBefore = $0.0;
    cmd = RsdCommand(" select nvl(Sum(t_sum),0) from dcompens_dbt where t_account = ? and t_fncash = ? and t_appltype not in (17,26,56,81,82,83,98,99) and t_date <> '01.01.0001'" );
    cmd.addParam ("T_ACCOUNT", RsdBp_in);
    cmd.value    ("T_ACCOUNT") = compens.rec.Account;
    cmd.addParam ("T_FNCASH", RsdBp_in);
    cmd.value    ("T_FNCASH") = compens.rec.FNCash;
    cmd.execute;
    rs = RsdRecordset(cmd); 
    if ( rs.moveNext() ) 
      SumCompensBefore = rs.value(0);
    else
      SumCompensBefore = $0.0;
    end;
  else
    SumCompensBefore = $0.0;
  end;
end;
// ---------------------------------------------------------

macro Check_SumCompensRitual (compens)
//
// Выплаченные ранее суммы на ритуальные услуги
// (только для компенсаций в размере 1000 рублей)
//
  var cmd, rs;
  var ClientType = compens.rec.Appltype,
  SumCompensRitual = $0.0;
  if (CompensType == 8881)
    cmd = RsdCommand(" select nvl(Sum(t_sum),0) from dcompens_dbt where t_account = ? and t_fncash = ? and t_appltype in (17,26,56,82) and t_date <> '01.01.0001'" );
    cmd.addParam ("T_ACCOUNT", RsdBp_in);
    cmd.value    ("T_ACCOUNT") = compens.rec.Account;
    cmd.addParam ("T_FNCASH", RsdBp_in);
    cmd.value    ("T_FNCASH") = compens.rec.FNCash;
    cmd.execute;
    rs = RsdRecordset(cmd); 
    if ( rs.moveNext() ) 
      SumCompensRitual = rs.value(0);
    else
      SumCompensRitual = $0.0;
    end;
  else
    SumCompensRitual = $0.0;
  end;
end;
// ---------------------------------------------------------

macro Check_CompensCoef (compens)
//
// Уточняющий коэффициент CompensCoef
//
  var cmd, rs;
  var cDay, cMon, cYear;
  AccCloseDate = NullDate;
  if (compens.rec.rate == 0)
//   Дата закрытия счета
    if (compens.rec.ACCCLSDATE == Date(0,0,0))
      cmd = RsdCommand("select T_CLOSE_DATE from ddepositr_dbt where T_ACCOUNT = ? and T_FNCASH = ? ");
      cmd.addParam("T_ACCOUNT", RsdBp_in);
      cmd.value("T_ACCOUNT") = compens.rec.Account;
      cmd.addParam("T_FNCASH", RsdBp_in);
      cmd.value("T_FNCASH") = compens.rec.FNCash;
      cmd.execute;
      cmd.NullConversion = true;
      rs = RsdRecordset(cmd); 
      if ( rs.moveNext() )
        AccCloseDate = rs.value(0);
      end;
    else
      AccCloseDate = compens.rec.ACCCLSDATE;
    end;
    if (CompensType == 8881)
      DateSplit (AccCloseDate, cDay, cMon, cYear);
      if (AccCloseDate == NullDate)
        CompensCoef = 1.0;
      elif ( cYear >= 1996 )
        CompensCoef = 1.0;
      elif ( cYear == 1995 )
        CompensCoef = 0.9;
      elif ( cYear == 1994 )
        CompensCoef = 0.8;
      elif ( cYear == 1993 )
        CompensCoef = 0.7;
      elif ( cYear == 1992 )
        CompensCoef = 0.6;
      elif ( cYear == 1991 )
        CompensCoef = 0.0;
      else
        CompensCoef = 0.0;
      end;  
    else
      CompensCoef = 1.0;
    end;
  else
    CompensCoef = compens.rec.rate;
  end;
end;
// ---------------------------------------------------------

macro Check_RestOn20061991 (compens)
//
// Остаток по счету на 20.06.1991
//
  var cmd, rs;
  var cmd_1, rs_1;
  RestOn20061991 = 0;
  if (compens.rec.restin91 == 0)
    cmd = RsdCommand(" select t_referenc from ddepositr_dbt where t_account = ? and T_FNCASH = ?");
    cmd.addParam ("t_account", RsdBp_in);
    cmd.value    ("t_account") = compens.rec.Account;
    cmd.addParam ("T_FNCASH", RsdBp_in);
    cmd.value    ("T_FNCASH") = compens.rec.FNCash;
    cmd.execute;
    rs = RsdRecordset(cmd); 
    if ( rs.moveNext() ) // Для счета ищется остаток
      cmd_1 = RsdCommand("select t_rest from ( "
                   " select * from dsbdepdoc_dbt "
                   " where t_referenc = ? "
                   "   and T_DATE_DOCUMENT <= ? "
                   "   and " + IsServ_const +
                   "  order by T_DATE_DOCUMENT DESC, t_numdaydoc DESC) "
                   " where rownum =1" );
      cmd_1.addParam("t_referenc", RsdBp_in);
      cmd_1.value   ("t_referenc") = rs.value(0);
      cmd_1.addParam("t_date_document", RsdBp_in);
      cmd_1.value   ("t_date_document") = date(20,06,1991);
      cmd_1.execute;
      rs_1 = RsdRecordset(cmd_1); 
      if (rs_1.moveNext())
        RestOn20061991 = RestOn20061991 + rs_1.value(0);           
      end;
    end;
    if (RestOn20061991 == 0)
      GetMoney (RestOn20061991, "Уточните остаток на 20.06.1991");
    end;
  else 
    RestOn20061991 = compens.rec.restin91;
  end;
  if (RestOn20061991 == 0)
    msgbox("Невозможно определить остаток на 20.06.1991");
    exit(1);
  end;
end;
// ---------------------------------------------------------

macro Check_compens_pay(compens)
  if ( compens.rec.Date != Date(0,0,0) )
     msgbox("Повторный расчет зачисленной компенсации запрещен");
     exit(1);
  end;
end;
//----------------MAIN ---- функция непосредственного расчета

MACRO CheckCompensationType (compens)

  var DestCodeClient = compens.rec.DestClientCode,
      CodeClient     = compens.rec.CodClient,
      ClientType     = compens.rec.Appltype,
      panel_compenscoef = 0;
      
  var dDay, dMon, dYear; // День, месяц, год смерти клиента 
  var bDay, bMon, bYear; // День, месяц, год рождения клиента 
  var cDay, cMon, cYear; // День, месяц, год закрыие счета 
  var cmd, rs;
// 
// Инициализация предварительных данных
// 
  CompensType    = compens.rec.Kind;
  Check_compens_pay      (compens);
  Check_RestOn20061991   (compens);
  Check_SumCompensBefore (compens);
  Check_SumCompensRitual (compens);
  Check_CompensCoef      (compens);
//
// Расчет компенсации
//
  if (NOT IsHeir (ClientType))   // Вкладчик
    FullSum = RestOn20061991 * CompensCoef - SumCompensBefore - SumCompensRitual;
    Sum = FullSum;
  else  // Наследник
//  Полная сумма компенсации
    FullSum = RestOn20061991 * CompensCoef - SumCompensRitual;
//  Доли наследования
    if ((compens.rec.LegacyPart_m == 0)
     OR (compens.rec.LegacyPart_n == 0))
      FullSum = FullSum - SumCompensBefore;
      Sum = FullSum;
    else
      MakeLegacyPart (compens);
    end;
  end;

  if ( FullSum < 0 )
    FullSum = 0;
    MsgBox("Сумма компенсации (без учета долей) получила отрицательное значение");
  end;
  if ( Sum < 0 )
    Sum = 0;
    MsgBox("Сумма компенсации получила отрицательное значение");
  end;

  cmd = RsdCommand("update dcompens_dbt set "
                                  " T_FUllSUM =  "+String(FullSum)        +", "
                                  " t_restin91 = "+String(RestOn20061991) +", "
                                  " t_rate =     "+String(CompensCoef)    +", "
                                  " t_sum =      "+String(Sum)            +", "
                                  " t_AccClsDate = ? "
                   " where T_FNCASH = ? and T_AUTOKEY = ? ");
  cmd.NullConversion = true;
  cmd.addParam ("t_date",     RsdBp_in);  
  cmd.value    ("t_date")     = AccCloseDate;
  cmd.addParam ("FNCash",     RsdBp_in);
  cmd.value    ("FNCash")     = compens.rec.FNCash;
  cmd.addParam ("AutoKey",    RsdBp_in);
  cmd.value    ("AutoKey")    = compens.rec.AutoKey;
  cmd.execute;
      
  return 0;

  OnError ( e )
    // Ошибки
    println ("Произошла ошибка");
    var i = 0;
    while ( i < RslDefEnv.ErrorCount )
      println ( " Системная ошибка - " + RslDefEnv.Error(i).descr );
      i = i + 1;
    end;
end;
