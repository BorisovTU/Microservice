/*
  RS-Retail 5.1
  Регистры налогового учета
  (Анализ начисленных процентов по вкладам)

  Подготовка реестров

  Ромодин Александр Васильевич
  24.04.2002
*/
Import CommonInter, opercode, cur_rate, acc_form, Календарь, RSD, SqlConv;

file   PERSON         ("person.dbt","bank.def") key 0;
file   CURRENCY       ("currency.dbt")key 0;
file   DEPOSITR       ("depositr.dbt")key 8;
var    FCL = TClientList;
/* file   CBRFRATE       ("cbrfrate.dbt","bank.def")key 0; */
file   SBDEPDOC       ("sbdepdoc.dbt")key 1;
record PCI_TAXR_PARAM ("PCI_TAXR.", "sbbank.def"); /* Входные параметры  */

const  BankVersion = BankVer();
var    CBRFRATE;

var    pc_alg = TBFile( "pc_alg.dbt", "r", 0 );

/***********************************************************
  Ситуации вызова макроса                                  
*/
const  MACRO_TAXR_Title            = 0; /* Заголовок отчета                            */
const  MACRO_TAXR_TitleAccount     = 1; /* Заголовок по счету                          */
const  MACRO_TAXR_PeriodAccount    = 2; /* Данные за период расчета процентов по счету */
const  MACRO_TAXR_TotalAccount     = 3; /* Итого по счету                              */
const  MACRO_TAXR_TotalTypeAccount = 4; /* Итого по виду вклада                        */
const  MACRO_TAXR_TotalCurrency    = 5; /* Итого по валюте                             */
const  MACRO_TAXR_Final            = 6; /* Вызов при завершении работы                 */
/***********************************************************
  Для просмотра протокола
*/
file LogFile() txt 250;         
var {CNum};
var WrkDir  = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true );
var NameLogFile  = WrkDir + "rpctaxr0." +String({CNum});
/***********************************************************
  Глобальные переменные- итоги
*/
var                /* По счету */                 
    NumDayForAcc                  = 0,
    SumPercentForAccCUR:moneyl    = $0,
    SumPercentForAccRUR:moneyl    = $0,
    SumDivergenceForAccCUR:moneyl = $0,
    SumDivergenceForAccRUR:moneyl = $0,
    SumTaxBasisForAccRUR:moneyl   = $0,
    LastDateEndPeriod             = Date(0,0,0),
                   /* По виду вклада */
    NumAccForType                  = 0,    
    SumPercentForTypeCUR:moneyl    = $0,
    SumPercentForTypeRUR:moneyl    = $0,
    SumDivergenceForTypeCUR:moneyl = $0,
    SumDivergenceForTypeRUR:moneyl = $0,
    SumTaxBasisForTypeRUR:moneyl   = $0,
                   /* По валюте */
    NumAccForCur                  = 0,    
    SumPercentForCurCUR:moneyl    = $0,
    SumPercentForCurRUR:moneyl    = $0,
    SumDivergenceForCurCUR:moneyl = $0,
    SumDivergenceForCurRUR:moneyl = $0,
    SumTaxBasisForCurRUR:moneyl   = $0;
/***********************************************************
  Глобальные переменные- рабочие
*/
var NameCur = "";
private var {Oper};
private var {Name_Bank};
/**********************************************************/

Macro TAXR_Title
/*
  Общий заголовок (вызывается один раз)
*/
  CBRFRATE = TCbRfRateList;
/****
  if (NOT Open (CBRFRATE, NameOpenFile))
    MsgBox ("ОШИБКА! Не открыт файл ставок рефинансирования ",NameOpenFile);
    return 18200;
  end;
***/
/* Отчет */
  [ Форма отчета];
  [ #]( {Name_Bank} );
  [ ];
  [ ];
  [                 Анализ начисленных процентов по вкладам];
  [ ];
  [                Отчетный период с ########## по ##########](PCI_TAXR_PARAM.BeginDate,PCI_TAXR_PARAM.EndDate);
  [ ];
  return 0;
end;
/**********************************************************/

Macro TAXR_Final
/*
  Итоговая запись по вывод отчета на экран (последний вызов макроса)
*/
  [ ];
  [ ];
  PERSON.Oper = {Oper};
  if (GetEQ(PERSON))
    [ Ответственный исполнитель ################################### /             /](PERSON.Name);
  else
    [ Ответственный исполнитель ___________________________________ /             /];
  end;
/* Вывод отчета на экран */
  SetOutput ();
  if (ExistFile(NameLogFile))
    if (Open(LogFile, NameLogFile))
      Close (LogFile);
      ViewFile (LogFile);
    end;       
  end;
  return 0;
end;
/**********************************************************/

Macro TAXR_TotalCurrency
/*
  Итого по валюте
*/
  if (NumAccForCur > 0)
    [ ];
    [ Итого по "###"  договоров: ########            V ###########|       |          |##########|                |](NameCur,NumAccForCur,
                                                                                                                    SumPercentForCurCUR,SumDivergenceForCurCUR);
    [                                                R ###########|       |          |##########| ############   |](SumPercentForCurRUR,SumDivergenceForCurRUR,SumTaxBasisForCurRUR);
    [ ];                                                                                             
  end;
  NumAccForCur           = 0;
  SumPercentForCurCUR    = $0;
  SumPercentForCurRUR    = $0;
  SumDivergenceForCurCUR = $0;
  SumDivergenceForCurRUR = $0;
  SumTaxBasisForCurRUR   = $0;
  return 0;
end;
/**********************************************************/

Macro TAXR_TotalTypeAccount
/*
  Итого по виду вклада
*/
  if (NumAccForType > 0)
    [ ];
    [ Итого по "############"  договоров: ########   V ###########|       |          |##########|                |](PCI_TAXR_PARAM.Type_Account,NumAccForType,
                                                                                                                    SumPercentForTypeCUR,SumDivergenceForTypeCUR);
    [                                                R ###########|       |          |##########| ############   |](SumPercentForTypeRUR,SumDivergenceForTypeRUR,SumTaxBasisForTypeRUR);
    [ ];                                                                                             
  end;
  NumAccForCur           = NumAccForCur           + NumAccForType;
  SumPercentForCurCUR    = SumPercentForCurCUR    + SumPercentForTypeCUR;
  SumPercentForCurRUR    = SumPercentForCurRUR    + SumPercentForTypeRUR;  
  SumDivergenceForCurCUR = SumDivergenceForCurCUR + SumDivergenceForTypeCUR;
  SumDivergenceForCurRUR = SumDivergenceForCurRUR + SumDivergenceForTypeRUR;
  SumTaxBasisForCurRUR   = SumTaxBasisForCurRUR   + SumTaxBasisForTypeRUR;
  NumAccForType           = 0;
  SumPercentForTypeCUR    = $0;
  SumPercentForTypeRUR    = $0;
  SumDivergenceForTypeCUR = $0;
  SumDivergenceForTypeRUR = $0;
  SumTaxBasisForTypeRUR   = $0;
  return 0;
end;
/**********************************************************/

Macro GetProlongForDate( NeedDate )
/*
  Поиск даты начала договора, в который входит
  заданная дата (по документам пролонгации)
   Если документ не найден- возвращает дату открытия
  текущего счета
*/
  var Fdate = DEPOSITR.Open_Date;
  var cmd, rs;

  cmd = RsdCommand( "select MAX(T.T_DEPDATE_DOCUMENT) as rProlDate " +
                    "from dsbdepdoc_dbt t " +
                    "where T.T_REFERENC= ? and " +
                          "T.T_TYPEOPER=72 and " +
                          "T.T_TYPECOMPLEXOPER=82 and " +
                          "T.T_DEPDATE_DOCUMENT<= ?" );

  cmd.addParam( "ref",  RSDBP_IN );
  cmd.addParam( "date", RSDBP_IN );

  cmd.value( "ref" )  = DEPOSITR.Referenc;
  cmd.value( "date" ) = NeedDate;

  cmd.execute;

  rs = RsdRecordset( cmd );

  if ( ( rs.moveNext )  AND  ( ValType(rs.value( "rProlDate" )) == V_DATE ) )
    Fdate = rs.value( "rProlDate" ) + 1;
  end;

  return Fdate;
end;
/* ------------------------------------------------------ */

Macro TAXR_PeriodAccount
/*
  Информация по одному периоду расчета процентов (для счета)
*/
  var MeanRate      = 0.,
      LimitRate     = 0.,
      SumDivergence:MoneyL = $0;
  var StartDate = DEPOSITR.Open_Date;
  var RateReFin = 0.;
  var vFlagCur = 0;
  var vFixedRateFlag = 1;
  var fProl = FALSE;
  var d, m, y;

  if ( PCI_TAXR_PARAM.Code_Currency == 0 )
    vFlagCur = 0;
  else
    vFlagCur = 1;
  end;

  if ( StrLen( PCI_TAXR_PARAM.Type_Account ) > 0 )
    pc_alg.Clear();
    pc_alg.rec.FlagCur    = vFlagCur;
    pc_alg.rec.Referenc   = PCI_TAXR_PARAM.Type_Account;
    pc_alg.rec.ObjectType = 1003;
    pc_alg.rec.BegDate    = PCI_TAXR_PARAM.BeginPeriod;
    if ( ( pc_alg.GetLE ) AND 
         ( pc_alg.rec.FlagCur    == vFlagCur ) AND
         ( pc_alg.rec.Referenc   == PCI_TAXR_PARAM.Type_Account ) AND 
         ( pc_alg.rec.ObjectType == 1003 ) )
      vFixedRateFlag = pc_alg.rec.FixedRateFlag;
    end;
  end;

/* Дополнительный расчет */
  if ( vFixedRateFlag != 0 )  // Фиксированная ставка.
    if (DEPOSITR.Prol_DateDep != Date(0,0,0))
      StartDate = DEPOSITR.Prol_DateDep;
      if (StartDate > PCI_TAXR_PARAM.BeginPeriod)
        StartDate = GetProlongForDate (PCI_TAXR_PARAM.BeginPeriod);
      end;
    end;
    if (StartDate < PCI_TAXR_PARAM.BeginPeriod)
      StartDate = PCI_TAXR_PARAM.BeginPeriod;
    end;
  else                        // Ставка не фиксированная.
    if ( PCI_TAXR_PARAM.EndPeriod == DEPOSITR.Close_Date )
      StartDate = PCI_TAXR_PARAM.EndPeriod;
    elif ( ( DEPOSITR.Prol_DateDep != Date(0,0,0) )  AND  ( ( PCI_TAXR_PARAM.EndPeriod == DEPOSITR.Prol_DateDep - 1 )  OR  ( PCI_TAXR_PARAM.EndPeriod == GetProlongForDate( PCI_TAXR_PARAM.EndPeriod ) ) ) )
      StartDate = PCI_TAXR_PARAM.EndPeriod;
    else
      DateSplit( PCI_TAXR_PARAM.EndPeriod, d, m, y );
      if ( m == 2 )
        StartDate = Date( 28, m, y );
      elif ( ( m == 1 )  OR  ( m == 3 )  OR  ( m == 5 )  OR  ( m == 7 )  OR  ( m == 8 )  OR  ( m == 10 )  OR  ( m == 12 ) )
        StartDate = Date( 31, m, y );
      else
        StartDate = Date( 30, m, y );
      end;
    end;
  end;

  CBRFRATE.SetFilter( PCI_TAXR_PARAM.Code_Currency );
  if ( CBRFRATE.GetRecForDate( StartDate ) )
    RateReFin = CBRFRATE.rec.Rate;
  end;
  
  MeanRate      = RateReFin;
  if (PCI_TAXR_PARAM.Code_Currency == 0)
    LimitRate = MeanRate * 1.8;
  else
    LimitRate = MeanRate * 0.8;
  end;
  if (PCI_TAXR_PARAM.RatePeriod > LimitRate)
    SumDivergence = PCI_TAXR_PARAM.RestPeriod * (PCI_TAXR_PARAM.RatePeriod - LimitRate) * PCI_TAXR_PARAM.NumDayInPeriod / 365. / 100.;
  end;
/* Вывод информации по периоду расчета */
  [|##########|##########|#####|###########|########|###########|#######|##########|##########|                |]
  (PCI_TAXR_PARAM.BeginPeriod,PCI_TAXR_PARAM.EndPeriod, PCI_TAXR_PARAM.NumDayInPeriod,
   PCI_TAXR_PARAM.RestPeriod, PCI_TAXR_PARAM.RatePeriod,PCI_TAXR_PARAM.SumPercentPeriod,
   MeanRate, LimitRate, SumDivergence);
/* Для "итого" */
  LastDateEndPeriod      = PCI_TAXR_PARAM.EndPeriod;
  NumDayForAcc           = NumDayForAcc           + PCI_TAXR_PARAM.NumDayInPeriod;
  SumPercentForAccCUR    = SumPercentForAccCUR    + PCI_TAXR_PARAM.SumPercentPeriod;
  SumDivergenceForAccCUR = SumDivergenceForAccCUR + SumDivergence;
  return 0;
end;
/**********************************************************/

Macro TAXR_TotalAccount
/*
  Итого по договору (счету вкладчика)
*/
  var DateRate = Date(0,0,0),
      CBRate:doublel = 0.;
  var BuyRate,SellRate;
  var st = True;
/* Расчет сумм в рублях по курсу на DateRate */
  if (LastDateEndPeriod != PCI_TAXR_PARAM.EndDate)
    DateRate = PCI_TAXR_PARAM.EndDate;
  else
    DateRate = PCI_TAXR_PARAM.EndDate;
    while (IsHoliday(DateRate))
      DateRate = DateRate - 1;
    end;
  end;
  if (PCI_TAXR_PARAM.Code_Currency > 0)
    st = GetAllCurrRate (DateRate,PCI_TAXR_PARAM.Code_Currency,CBRate,BuyRate,SellRate);
  else
    CBRate = 1; /* Рубли */
  end;
  if (st)
    SumPercentForAccRUR    = SumPercentForAccCUR    * CBRate;
    SumDivergenceForAccRUR = SumDivergenceForAccCUR * CBRate;
    SumTaxBasisForAccRUR   = SumPercentForAccRUR - SumDivergenceForAccRUR;
  end;
/* Вывод итога */
  [+----------+----------+-----+-----------+--------+-----------+-------+----------+----------+----------------+];
  [ Итого по договору     #####                    V ###########|       |          |##########|                |](NumDayForAcc,
                                                                                                                  SumPercentForAccCUR,SumDivergenceForAccCUR);
  if (st)
  [        Курс на ##########: ##########          R ###########|       |          |##########| ############   |](DateRate,CBRate,
                                                                                                                  SumPercentForAccRUR,SumDivergenceForAccRUR,SumTaxBasisForAccRUR);
  else
    [ ОШИБКА при определении ставки валюты на дату ##########](DateRate);
  end;
  [ ];
/* Обработка глобальных параметров итога */
  NumAccForType           = NumAccForType           + 1;
  SumPercentForTypeCUR    = SumPercentForTypeCUR    + SumPercentForAccCUR;
  SumPercentForTypeRUR    = SumPercentForTypeRUR    + SumPercentForAccRUR;
  SumDivergenceForTypeCUR = SumDivergenceForTypeCUR + SumDivergenceForAccCUR;
  SumDivergenceForTypeRUR = SumDivergenceForTypeRUR + SumDivergenceForAccRUR;
  SumTaxBasisForTypeRUR   = SumTaxBasisForTypeRUR   + SumTaxBasisForAccRUR;
  NumDayForAcc           = 0;
  SumPercentForAccCUR    = $0;
  SumPercentForAccRUR    = $0;
  SumDivergenceForAccCUR = $0;
  SumDivergenceForAccRUR = $0;
  SumTaxBasisForAccRUR   = $0;
  LastDateEndPeriod      = Date(0,0,0);
  return 0;
end;
/**********************************************************/

Macro TAXR_TitleAccount
/*
  Заголовок по счету
*/
  var FIO = "";
  if (NumAccForCur == 0)
    CURRENCY.Code_Currency = PCI_TAXR_PARAM.Code_Currency;
    if (GetEQ(CURRENCY))
      NameCur = CURRENCY.Short_Name;
    else
      NameCur = String(PCI_TAXR_PARAM.Code_Currency);
    end;
  end;
  [ ];
  [ Валюта       ###](NameCur);
  [ Вид вклада   #############](PCI_TAXR_PARAM.Type_Account);
  DEPOSITR.Referenc = PCI_TAXR_PARAM.Referenc;
  if (GetEQ(DEPOSITR))
    [ Лицевой счет #########################](DEPOSITR.Account);
    if (FCL.GetRecord (DEPOSITR.CodClient))
      FIO = FCL.CurRec.ConvertFIOFull ();
      [ Клиент       #](FIO);
    else
      [ Клиент с кодом ########## не найден](DEPOSITR.CodClient);
    end;
  else
    [ Лицевой счет с референсом ########## не найден](PCI_TAXR_PARAM.Referenc);
  end;
  [ ];
  [+---------------------+-----+-----------+--------+-----------+-------+----------+----------+----------------+];
  [|    Период расчета   |Число| Остаток   | Ставка |   Сумма   |Средняя|Предельная|  Сумма   |    Вычет из    |];
  [+----------+----------+дней |           |  по    |начисленных|ставка |  ставка  |отклонения|налогооблагаемой|];
  [| Начало   | Конец    |     |           |договору| процентов |       |          |          |      базы      |];
  [+----------+----------+-----+-----------+--------+-----------+-------+----------+----------+----------------+];
  return 0;
end;
/***********************************************************
##### Точка входа ##########################################
***********************************************************/

Macro Maker (NumCall)

  var stat = 0;

  if (NumCall == MACRO_TAXR_Title)
    SetOutput (NameLogFile, False); 
  else
    SetOutput (NameLogFile, True); 
  end;

  if (NumCall == MACRO_TAXR_Title)
    stat = TAXR_Title ();
  elif (NumCall == MACRO_TAXR_TitleAccount)   
    stat = TAXR_TitleAccount ();
  elif (NumCall == MACRO_TAXR_PeriodAccount)
    stat = TAXR_PeriodAccount ();
  elif (NumCall == MACRO_TAXR_TotalAccount)
    stat = TAXR_TotalAccount ();
  elif (NUmCall == MACRO_TAXR_TotalTypeAccount)
    stat = TAXR_TotalTypeAccount ();
  elif (NumCall == MACRO_TAXR_TotalCurrency)
    stat = TAXR_TotalCurrency ();
  elif (NumCall == MACRO_TAXR_Final)
    stat = TAXR_Final (); 
  end;

  return stat;
end;
/* ----- Конец файла ----- */
