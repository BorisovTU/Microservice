/********************************************************
*  ---------------------                                *
*  R-Style SoftWare Lab.                                *
*  ---------------------                                *
*  СБЕРБАНК 1.0                                         *
*                                                       *
*                                                       *
*  Печать Формы 68 по всем филиалам всех видов вкладов  *
*                                                       *
*  Лебедев С.В.                                         *
*  05.03.98                                             *
********************************************************/
import deprintr;

file CURRENCY(currency);            /* Справочник валют                 */
file sb_dtyp (sb_dtyp );            /* Типы вкладов                     */
file sb_depst(sb_depst);            /* Статистика по вкладам            */
var ddep = TRecHandler( "i_dp_dep.rec" );

file PodrF68() txt 150 write;       /* ID файла подробного отчета       */
file SvodF68() txt 150 write;       /* ID файла сводныого отчета        */
var  NamePodrF68 = "f68p";          /* Имя файла подробного отчета      */
var  NameSvodF68 = "f68s";          /* Имя файла сводного отчета        */

var FlagCur = NumFlagCur();         /* Валютность                       */
var FNCash  = NumFNCash();          /* Номер филиала                    */
var Mode    = GetCurrentMode();     /* Текущий режим работы             */ 
var {curdate};                      /* Текущая дата                     */


/*____________142833 - SAN - Removal of the relative paths__________*/
//const PathToTxt = "..\\TXTFILE";    
const PathToTxt = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", false ); /* Путь к выходным файлам           */
/*___________/142833 - 14/07/09_____________________________________*/
const DefaultCodCurRub = 0;         /* Код валюты по умолчанию !FlagCur */
const DefaultCodCurCur = 1;         /* Код валюты по умолчанию  FlagCur */
const ConstLenBalAcc   = 5;         /* Длина Балансового счета          */
const LengthReportP    = 97;        /* Длина подробного отчета          */
const LengthReportS    = 94;        /* Длина сводного отчета            */

array ABalAcc;                      /* Массив балансовых счетов         */

record InputPanel (FORMA68) dialog; /* Панель ввода исходных данных     */

const ne_Month          = 0,
      ne_Year           = 1,
      ne_BegDate        = 2,
      ne_EndDate        = 3,
      ne_ShCur          = 4,
      ne_PodrReport     = 5,
      ne_SvodReport     = 6;

var NumMonth = 0; /* Номер месяца из панели */
var CodCur   = 0; /* Код валюты из панели   */

var CountRecForSvodBA = 0;     /* Количество записей в сводном отчете         */
			       /* по Балансовому счету                        */
var WasPrintForPodr   = FALSE; /* Была ли напечатана шапка для подробного     */
			       /* отчета по вкладу                            */
var PrintPodrAlways   = TRUE;  /* TRUE печатать всегда подробный отчет,       */
			       /* если есть хоть какая-то сумма, FALSE -      */
			       /* печатать только если были обороты за период */

/* --------------- Подсчет сумм по вкладу ---------------- */
var SumDTColIn       = 0;
var SumDTColOut      = 0;
var SumDTColFinDay   = 0;
var SumDTSummaIn     = $0.0;
var SumDTSummaOut    = $0.0;
var SumDTSummaFinDay = $0.0;

/* ---------- Подсчет сумм по балансовому счету ---------- */
var SumBAColIn       = 0;
var SumBAColOut      = 0;
var SumBAColFinDay   = 0;
var SumBASummaIn     = $0.0;
var SumBASummaOut    = $0.0;
var SumBASummaFinDay = $0.0;

/* ----------------- Подсчет общей суммы ----------------- */
var SumTotColIn       = 0;
var SumTotColOut      = 0;
var SumTotColFinDay   = 0;
var SumTotSummaIn     = $0.0;
var SumTotSummaOut    = $0.0;
var SumTotSummaFinDay = $0.0;


/*******************************************************************
		     Начало-Окончание отчета
*******************************************************************/
macro BeginEndReport
  [ ------- #################### ------------------ ########## ------ ######## ---------
  ] (GetRetailVersion(),Date(),Time());
end;


/*******************************************************************
		      Поиск валюты по ее коду
*******************************************************************/
macro SetCurrency
(
  CodeCurrency
)

  var stat;

  KeyNum(CURRENCY,0);
  ReWind(CURRENCY);
  CURRENCY.Code_Currency = CodeCurrency;
  stat = GetEQ(CURRENCY);

  return stat;

end;


/*******************************************************************
		 Определение номера филиала
*******************************************************************/
macro DefFilial

  var NumFilial;

  if (not findDepartment(FNCash, NumFilial))
    NumFilial = "";
  end;

  return NumFilial;

end;


/*******************************************************************
	 Определение остатка на начало отчетного периода
*******************************************************************/
macro DefPrevRest
(
  PrevCount,
  PrevSum,
  FlagRez
)

  file prevrest(sb_depst);
  var  oldkey, oldpos;

  PrevCount = 0;
  PrevSum   = $0.0;
  var dpDepList = TdpDepList;

  /* Цикл для всех филиалов */
  dpDepList.saveState();
  dpDepList.rewind();
  while (dpDepList.next())
    KeyNum(prevrest,0);
    ReWind(prevrest);
    prevrest.FNCash  = dpDepList.rec.Code;
    prevrest.IsCur   = FlagCur;
    prevrest.Kind    = sb_dtyp.Kind;
    prevrest.CodCur  = CodCur;
    prevrest.FlagRez = FlagRez;
    prevrest.Date    = InputPanel.BegDate;
    prevrest.Mode    = Mode;
    if (GetLT(prevrest))
      if ((prevrest.FNCash  == dpDepList.rec.Code   ) AND
  	(prevrest.IsCur   == FlagCur           ) AND
  	(prevrest.Kind    == sb_dtyp.Kind      ) AND
  	(prevrest.CodCur  == CodCur            ) AND
  	(prevrest.FlagRez == FlagRez           ) AND
  	(prevrest.Date    <  InputPanel.BegDate)     )
         PrevCount = PrevCount + prevrest.ColOpenedAcc;
         PrevSum   = PrevSum + prevrest.Rest;
       end;
    end;
  end;
  dpDepList.restoreState();

  SetParm(0,PrevCount);
  SetParm(1,PrevSum);

end;


/*******************************************************************
	       Печать шапки для сводного отчета
*******************************************************************/
macro PrintTopForSvod

  var str;

  if (InputPanel.SvodReport == "X")
    Insert(SvodF68," ");
    Insert(SvodF68,"                 Сводная учетная карточка по учреждению СберБанка N " + DefFilial());
    Insert(SvodF68,"                          за интервал с " + String(InputPanel.BegDate) + " по " + String(InputPanel.EndDate));
    Insert(SvodF68," ");
    /* Разделитель */
    str = "";
    while (StrLen(str) < LengthReportS)
      str = str + "-";
    end;
    Insert(SvodF68,str);
    Insert(SvodF68,"   N б/сч   | Вид вклада |        Приход        |        Расход        | Остатки на конец дня ");
    Insert(SvodF68,"            |            |----------------------|----------------------|----------------------");
    Insert(SvodF68,"            |            | л/c |     Сумма      | л/с |     Сумма      | л/с |     Сумма      ");
    /* Разделитель */
    str = "";
    while (StrLen(str) < LengthReportS)
      str = str + "-";
    end;
    Insert(SvodF68,str);
  end;

end;


/*******************************************************************
	      Печать шапки для подробного отчета
*******************************************************************/
macro PrintTopForPodr
(
 NeedProv,
 FlagRez
)

  var str;
  var PrevMonthCount = 0;
  var PrevMonthSum   = $0.0;
  var NameRez;

  if (NOT WasPrintForPodr)
    if (InputPanel.PodrReport == "X")
      DefPrevRest(PrevMonthCount,PrevMonthSum,FlagRez);
      if ((NOT NeedProv) OR
	  (NeedProv AND PrintPodrAlways AND (PrevMonthCount != 0) AND (PrevMonthSum != $0.0)))
	WasPrintForPodr = TRUE;
	Insert(PodrF68," ");
	Insert(PodrF68," ");
	Insert(PodrF68," ");
	Insert(PodrF68," Детальная учетная карточка по учреждению СберБанка N " + DefFilial());
	Insert(PodrF68," за интервал с " + String(InputPanel.BegDate) + " по " + String(InputPanel.EndDate));
	if (FlagRez)
	  NameRez = "(нерезид.)";
	else
	  NameRez = "";
	end;
	Insert(PodrF68," " + sb_dtyp.Name + " " + NameRez);
	Insert(PodrF68," ");
	/* Разделитель */
	str = "";
	while (StrLen(str) < LengthReportP)
	  str = str + "-";
	end;
	Insert(PodrF68,str);
	str = " Остаток на " + String(InputPanel.BegDate) + "  л/с " + String(PrevMonthCount:5) + "  " + String(PrevMonthSum:20:2:a);
	while (StrLen(str) < 73)
	  str = str + " ";
	end;
	str = str + "|        Остатки";
	Insert(PodrF68,str);
	/* Разделитель */
	str = "";
	while (StrLen(str) < LengthReportP)
	  str = str + "-";
	end;
	Insert(PodrF68,str);
	/* Название полей */
	Insert(PodrF68,"      Номер филиала      |Откр.л/c|    Приход    |Закр.л/с|    Расход    |  л/c   |   Вкладов    ");
	/* Разделитель */
	str = "";
	while (StrLen(str) < LengthReportP)
	  str = str + "-";
	end;
	Insert(PodrF68,str);
      end;
    end;
  end;

end;


/*******************************************************************
	  Печать строки в подробный отчет и подсчет сумм
*******************************************************************/
macro WriteToReport

  var ColIn       = sb_depst.ColAccounts;
  var ColOut      = sb_depst.ColClosedAcc;
  var ColFinDay   = sb_depst.ColOpenedAcc;
  var SummaIn     = $0.0;
  var SummaOut    = $0.0;
  var SummaFinDay = sb_depst.Rest;
  var FlagRez     = sb_depst.FlagRez;
  var stat;
  var str;

  /* Уточнение открытых/закрытых лицевых счетов вкладчиков за месяц */
  KeyNum(sb_depst,0);
  ReWind(sb_depst);
  sb_depst.FNCash = ddep.rec.Code;
  sb_depst.IsCur  = FlagCur;
  sb_depst.Kind   = sb_dtyp.Kind;
  sb_depst.CodCur = CodCur;
  sb_depst.FlagRez = FlagRez;
  sb_depst.Date   = InputPanel.BegDate;
  sb_depst.Mode    = Mode;
  if (GetLT(sb_depst))
    if ((sb_depst.FNCash  == ddep.rec.Code   ) AND
	(sb_depst.IsCur   == FlagCur           ) AND
	(sb_depst.Kind    == sb_dtyp.Kind      ) AND
	(sb_depst.CodCur  == CodCur            ) AND
	(sb_depst.FlagRez == FlagRez            ) AND
	(sb_depst.Date    <=  InputPanel.BegDate)    )
      ColIn  = ColIn  - sb_depst.ColAccounts;
      ColOut = ColOut - sb_depst.ColClosedAcc;
    end;
  end;
  /* Расчет Прихода/Расхода за месяц */
  KeyNum(sb_depst,0);
  ReWind(sb_depst);
  sb_depst.FNCash  = ddep.rec.Code;
  sb_depst.IsCur   = FlagCur;
  sb_depst.Kind    = sb_dtyp.Kind;
  sb_depst.CodCur  = CodCur;
  sb_depst.FlagRez = FlagRez;
  sb_depst.Date    = InputPanel.BegDate;
  sb_depst.Mode    = Mode;
  stat = GetGE(sb_depst);
  while (stat)
    if ((sb_depst.FNCash  == ddep.rec.Code   ) AND
	(sb_depst.IsCur   == FlagCur           ) AND
	(sb_depst.Kind    == sb_dtyp.Kind      ) AND
	(sb_depst.CodCur  == CodCur            ) AND
	(sb_depst.FlagRez == FlagRez           ) AND
	(sb_depst.Date    >= InputPanel.BegDate) AND
	(sb_depst.Date    <= InputPanel.EndDate)    )
      SummaIn  = SummaIn  + sb_depst.KredSum;
      SummaOut = SummaOut + sb_depst.DebSum;
      stat = Next(sb_depst);
    else
      stat = FALSE;
    end;
  end;

  if (InputPanel.PodrReport == "X")
    if (((ColIn     != 0) OR (SummaIn     != $0.0) OR
	 (ColOut    != 0) OR (SummaOut    != $0.0)   ) OR
	(PrintPodrAlways AND ((ColFinDay != 0) OR (SummaFinDay != $0.0))))
      PrintTopForPodr(FALSE,FlagRez);
      str = "  " + ddep.rec.Name;
      while (StrLen(str) < 26)
	str = str + " ";
      end;
      str = str + String(ColIn    :8) + " " + String(SummaIn    :14:2:a) + " " +
		  String(ColOut   :8) + " " + String(SummaOut   :14:2:a) + " " +
		  String(ColFinDay:8) + " " + String(SummaFinDay:14:2:a);
      Insert(PodrF68,str);
    end;
  end;

  /* Подсчет сумм по вкладу */
  SumDTColIn        = SumDTColIn        + ColIn;
  SumDTColOut       = SumDTColOut       + ColOut;
  SumDTColFinDay    = SumDTColFinDay    + ColFinDay;
  SumDTSummaIn      = SumDTSummaIn      + SummaIn;
  SumDTSummaOut     = SumDTSummaOut     + SummaOut;
  SumDTSummaFinDay  = SumDTSummaFinDay  + SummaFinDay;
  /* Подсчет сумм по балансовому счету */
  SumBAColIn        = SumBAColIn        + ColIn;
  SumBAColOut       = SumBAColOut       + ColOut;
  SumBAColFinDay    = SumBAColFinDay    + ColFinDay;
  SumBASummaIn      = SumBASummaIn      + SummaIn;
  SumBASummaOut     = SumBASummaOut     + SummaOut;
  SumBASummaFinDay  = SumBASummaFinDay  + SummaFinDay;
  /* Подсчет общей суммы */
  SumTotColIn       = SumTotColIn       + ColIn;
  SumTotColOut      = SumTotColOut      + ColOut;
  SumTotColFinDay   = SumTotColFinDay   + ColFinDay;
  SumTotSummaIn     = SumTotSummaIn     + SummaIn;
  SumTotSummaOut    = SumTotSummaOut    + SummaOut;
  SumTotSummaFinDay = SumTotSummaFinDay + SummaFinDay;

end;


/*******************************************************************
	      Работа с вкладом для всех филиалов
*******************************************************************/
macro WriteAndCalcForAllFilials
(
 FlagRez
)

  var stat      = FALSE;
  var str;
  var FirstFind = TRUE;
  var dpDepList = TdpDepList;

  /* Обнуление сумм по вкладу */
  SumDTColIn       = 0;
  SumDTColOut      = 0;
  SumDTColFinDay   = 0;
  SumDTSummaIn     = $0.0;
  SumDTSummaOut    = $0.0;
  SumDTSummaFinDay = $0.0;

  /* Цикл по определенному вкладу для всех филиалов */
  dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
  ddep = dpDepList.RecHandler;
  while (dpDepList.next())
    KeyNum(sb_depst,0);
    ReWind(sb_depst);
    sb_depst.FNCash  = ddep.rec.Code;
    sb_depst.IsCur   = FlagCur;
    sb_depst.Kind    = sb_dtyp.Kind;
    sb_depst.CodCur  = CodCur;
    sb_depst.FlagRez = FlagRez;
    sb_depst.Date    = InputPanel.EndDate;
    sb_depst.Mode    = Mode;
    if (GetLE(sb_depst))
      if ((sb_depst.FNCash  == ddep.rec.Code   ) AND
	  (sb_depst.IsCur   == FlagCur           ) AND
	  (sb_depst.Kind    == sb_dtyp.Kind      ) AND
	  (sb_depst.CodCur  == CodCur            ) AND
	  (sb_depst.FlagRez == FlagRez           ) AND
	  (sb_depst.Date    <= InputPanel.EndDate)     )
	if (FirstFind)
	  FirstFind = FALSE;
	  PrintTopForPodr(TRUE,FlagRez);
	end;
	stat = TRUE;
	WriteToReport();
      end;
    end;
  end;

  /* Печать итого по вкладу в подробный отчет */
  if (stat AND (InputPanel.PodrReport == "X"))
    if (WasPrintForPodr)
      /* Разделитель */
      str = "";
      while (StrLen(str) < LengthReportP)
	str = str + "-";
      end;
      Insert(PodrF68,str);
      /* Итого */
      if ((SumDTColIn  != 0) OR (SumDTSummaIn  != $0.0) OR
	  (SumDTColOut != 0) OR (SumDTSummaOut != $0.0)   )
	str = " Обороты за интервал      ";
	str = str + String(SumDTColIn :8) + " " + String(SumDTSummaIn :14:2:a) + " " +
		    String(SumDTColOut:8) + " " + String(SumDTSummaOut:14:2:a);
	Insert(PodrF68,str);
      end;
      str = " Остаток на " + String(InputPanel.EndDate) + "    ";
      str = str + "        " + " " + "              " + " " +
		  "        " + " " + "              " + " " +
		  String(SumDTColFinDay:8) + " " + String(SumDTSummaFinDay:14:2:a);
      Insert(PodrF68,str);
      /* Разделитель */
      str = "";
      while (StrLen(str) < LengthReportP)
	str = str + "-";
      end;
      Insert(PodrF68,str);
    end;
  end;

  /* Печать строки по вкладу в сводный отчет */
  if (stat AND (InputPanel.SvodReport == "X"))
    if ((SumDTColIn     != 0) OR (SumDTSummaIn     != 0) OR
	(SumDTColOut    != 0) OR (SumDTSummaOut    != 0) OR
	(SumDTColFinDay != 0) OR (SumDTSummaFinDay != 0)   )
      str = sb_dtyp.BalAcc;
      while (StrLen(str) < 13)
	str = str + " ";
      end;
      str = str + sb_dtyp.Kind;
      while (StrLen(str) < 26)
	str = str + " ";
      end;
      str = str + String(SumDTColIn    :5) + " " + String(SumDTSummaIn    :16:2:a) + " " +
		  String(SumDTColOut   :5) + " " + String(SumDTSummaOut   :16:2:a) + " " +
		  String(SumDTColFinDay:5) + " " + String(SumDTSummaFinDay:16:2:a);
      Insert(SvodF68,str);
      if (FlagRez)
	str = "             (нерезид.) ";
	Insert(SvodF68,str);
      end;
      CountRecForSvodBA = CountRecForSvodBA + 1;
    end;
  end;

  return stat;

end;


/*******************************************************************
	  Печать сумм по балансовому счету в сводный отчет
*******************************************************************/
macro PrintSumBalAcc
(
 NumBalAcc
)

  var str;

  if (InputPanel.SvodReport == "X")
    if (CountRecForSvodBA > 0) /* Если в отчет попал хоть один вклад */
      /* Разделитель */
      str = "";
      while (StrLen(str) < LengthReportS)
	str = str + "-";
      end;
      Insert(SvodF68,str);
      if (CountRecForSvodBA > 1) /* Итого надо если вкладов больше одного */
	/* Итого */
	str = " Итого по счету " + NumBalAcc;
	while (StrLen(str) < 26)
	  str = str + " ";
	end;
	str = str + String(SumBAColIn    :5) + " " + String(SumBASummaIn    :16:2:a) + " " +
		    String(SumBAColOut   :5) + " " + String(SumBASummaOut   :16:2:a) + " " +
		    String(SumBAColFinDay:5) + " " + String(SumBASummaFinDay:16:2:a);
	Insert(SvodF68,str);
	/* Разделитель */
	str = "";
	while (StrLen(str) < LengthReportS)
	  str = str + "-";
	end;
	Insert(SvodF68,str);
      end;
    end;
  end;

end;


/*******************************************************************
	       Печать сумм по сводному отчету
*******************************************************************/
macro PrintSumSvod

  var str;

  if (InputPanel.SvodReport == "X")
    /* Итого */
    str = " Всего";
    while (StrLen(str) < 26)
      str = str + " ";
    end;
    str = str + String(SumTotColIn    :5) + " " + String(SumTotSummaIn    :16:2:a) + " " +
		String(SumTotColOut   :5) + " " + String(SumTotSummaOut   :16:2:a) + " " +
		String(SumTotColFinDay:5) + " " + String(SumTotSummaFinDay:16:2:a);
    Insert(SvodF68,str);
    /* Разделитель */
    str = "";
    while (StrLen(str) < LengthReportS)
      str = str + "-";
    end;
    Insert(SvodF68,str);
  end;

end;


/*******************************************************************
	  Вызов макрофункций по созданию выходных форм
*******************************************************************/
macro WriteToOutFiles

  var i           = 0;
  var CurBalAcc   = "";
  var WasDepTypep = FALSE;
  var stat;

  /* Печать шапки для сводного отчета */
  PrintTopForSvod();

  while (i < ASize(ABalAcc))
    /* -- Взведение параметров при переходе к новому счету -- */
    CountRecForSvodBA = 0;
    WasDepTypep = FALSE;
    /* Обнуляем суммы по балансовому счету */
    SumBAColIn       = 0;
    SumBAColOut      = 0;
    SumBAColFinDay   = 0;
    SumBASummaIn     = $0.0;
    SumBASummaOut    = $0.0;
    SumBASummaFinDay = $0.0;
    /* ------------------------------------------------------ */
    KeyNum(sb_dtyp,4);
    ReWind(sb_dtyp);
    sb_dtyp.FlagCur = FlagCur;
    sb_dtyp.BalAcc  = "";
    stat = GetGE(sb_dtyp);
    while (stat)
      if (sb_dtyp.FlagCur == FlagCur)
	/* Проверяем счет для резидента */
	CurBalAcc = Trim(sb_dtyp.BalAcc);
	if (StrLen(CurBalAcc) > ConstLenBalAcc)
	  CurBalAcc = SubStr(CurBalAcc,1,ConstLenBalAcc);
	end;
	if (ABalAcc(i) == CurBalAcc)
	  Message(" Обрабатывается вклад ",sb_dtyp.Kind);
	  /* Подсчет сумм по данному типу вклада для всех филиалов */
	  WasPrintForPodr = FALSE;
	  if (WriteAndCalcForAllFilials("\x00"))
	    WasDepTypep = TRUE;
	  end;
	end;
	/* Проверяем счет для нерезидента */
	CurBalAcc = Trim(sb_dtyp.BalAccNotRez);
	if (StrLen(CurBalAcc) > ConstLenBalAcc)
	  CurBalAcc = SubStr(CurBalAcc,1,ConstLenBalAcc);
	end;
	if (ABalAcc(i) == CurBalAcc)
	  Message(" Обрабатывается вклад ",sb_dtyp.Kind," (нерезид.)");
	  /* Подсчет сумм по данному типу вклада для всех филиалов */
	  WasPrintForPodr = FALSE;
	  if (WriteAndCalcForAllFilials("\x01"))
	    WasDepTypep = TRUE;
	  end;
	end;

	stat = Next(sb_dtyp);
      else
	stat = FALSE;
      end;
    end;

    if (WasDepTypep)
      /* Печать суммы по балансовому счету */
      PrintSumBalAcc(ABalAcc(i));
    end;

    i = i + 1;
  end;

  /* Печать сумм для сводного отчета */
  PrintSumSvod();

end;


/*******************************************************************
  Определение всех возможных балансовых счетов - резидентов и нет
*******************************************************************/
macro DefAllBalAccount

  var stat;
  var i,j,k;
  var BA_TMP;
  array BalAcc;

  Message(" Идет определение балансовых счетов");

  KeyNum(sb_dtyp,4);
  ReWind(sb_dtyp);
  sb_dtyp.FlagCur = FlagCur;
  sb_dtyp.BalAcc  = "";
  stat = GetGE(sb_dtyp);
  while (stat)
    if (sb_dtyp.FlagCur == FlagCur)
      BalAcc(0) = Trim(sb_dtyp.BalAcc);
      if (StrLen(BalAcc(0)) > ConstLenBalAcc)
	BalAcc(0) = SubStr(BalAcc(0),1,ConstLenBalAcc);
      end;
      BalAcc(1) = Trim(sb_dtyp.BalAccNotRez);
      if (StrLen(BalAcc(1)) > ConstLenBalAcc)
	BalAcc(1) = SubStr(BalAcc(1),1,ConstLenBalAcc);
      end;
      i = 0;
      while (i < ASize(BalAcc))
	if (ASize(ABalAcc) == 0)
	  ABalAcc(0) = BalAcc(i);
	else
	  j = 0;
	  k = -1;
	  while (j < ASize(ABalAcc))
	    if (BalAcc(i) == ABalAcc(j))
	      k = j;
	      j = ASize(ABalAcc); /* Чтобы быстрее выйти из цикла */
	    end;
	    j = j + 1;
	  end;
	  if (k == -1)
	    k = ASize(ABalAcc);
	    ABalAcc(k) = BalAcc(i);
	  end;
	end;
	i = i + 1;
      end;
      stat = Next(sb_dtyp);
    else
      stat = FALSE;
    end;
  end;

  /* Сортируем в порядке возрастания */
  i = 0;
  while (i < ASize(ABalAcc))
    j = i;
    k = i;
    while (j < ASize(ABalAcc))
      if (ABalAcc(k) > ABalAcc(j))
	k = j;
      end;
      j = j + 1;
    end;
    BA_TMP = ABalAcc(i);
    ABalAcc(i) = ABalAcc(k);
    ABalAcc(k) = BA_TMP;
    i = i + 1;
  end;

end;


/*******************************************************************
		   Резюме по работе программы
*******************************************************************/
macro Summary

  if (InputPanel.PodrReport == "X")
    Close(PodrF68);
    ViewFile(PodrF68);
  end;
  if (InputPanel.SvodReport == "X")
    Close(SvodF68);
    ViewFile(SvodF68);
  end;

  [ ];
  if ((InputPanel.PodrReport == "X") AND (InputPanel.SvodReport == "X"))
    [ Отчеты по формам выпущены];
    [ Результаты работы находятся в следующих файлах:];
  else
    [ Отчет по форме выпущен];
    [ Результат работы находится в следующем файле:];
  end;
  if (InputPanel.PodrReport == "X")
    [            подробный: #](NamePodrF68);
  end;
  if (InputPanel.SvodReport == "X")
    [            сводный  : #](NameSvodF68);
  end;
  [ ];

end;


/*******************************************************************
		    Создание выходных файлов
*******************************************************************/
macro OpenFiles

  var stat     = TRUE;
  var PathName = "";
  var Ras      = "";
  var Len      = 0;
  var y;

  /* Каталог для выходного файла */
  PathName = PathToTxt;
  Len = StrLen (PathName);
  if (Len > 0)
    if (SubStr(PathName,Len,1) != "\\")
      PathName = PathName + "\\";
    end;
  end;
  /* Расширение для выходного файла */
  DateSplit({curdate},null,null,y);
  Ras = Trim(String({curdate} - Date(1,1,y) + 1));
  while (StrLen(Ras) < 3)
    Ras = "0" + Ras;
  end;
  /* Открытие файла подробного отчета */
  if (stat AND (InputPanel.PodrReport == "X"))
    NamePodrF68 = PathName + NamePodrF68;
    Len = StrLen(NamePodrF68);
    if (Len > 0)
      if (SubStr(NamePodrF68,Len,1) != ".")
	NamePodrF68 = NamePodrF68 + ".";
      end;
      NamePodrF68 = NamePodrF68 + Ras;
    else
      stat = FALSE;
      [ ];
      [ Не могу сформировать имя подробного отчета];
      [ ];
    end;
    if (stat)
      stat = Open(PodrF68,NamePodrF68);
      if (NOT stat)
	[ ];
	[ Не могу открыть файл #](NamePodrF68);
	[ ];
      end;
    end;
  end;
  /* Открытие файла сводного отчета */
  if (stat AND (InputPanel.SvodReport == "X"))
    NameSvodF68 = PathName + NameSvodF68;
    Len = StrLen(NameSvodF68);
    if (Len > 0)
      if (SubStr(NameSvodF68,Len,1) != ".")
	NameSvodF68 = NameSvodF68 + ".";
      end;
      NameSvodF68 = NameSvodF68 + Ras;
    else
      stat = FALSE;
      [ ];
      [ Не могу сформировать имя сводного отчета];
      [ ];
    end;
    if (stat)
      stat = Open(SvodF68,NameSvodF68);
      if (NOT stat)
	[ ];
	[ Не могу открыть файл #](NameSvodF68);
	[ ];
      end;
    end;
  end;

  return stat;

end;


/*******************************************************************
		     Справочный скроллинг валют
*******************************************************************/
macro List_Currency
(
 InputCodCur
)

  array Code,
	Name;
  var   i            = 0;
  var   OutputCodCur = InputCodCur;

  KeyNum (CURRENCY,0);
  ReWind (CURRENCY);
  while (Next(CURRENCY))
    if (CURRENCY.Code_Currency != DefaultCodCurRub)
      Code(i)   = CURRENCY.Code_Currency;
      Name(i)   = " " + SubStr(CURRENCY.Short_Name + "    ",1,3) + " - " + CURRENCY.Name_Currency + " ";
      i = i + 1;
    end;
  end;
  if (Asize(Code))
    i = Menu (Name, NULL, "Валюта");
    if (i >= 0)
      OutputCodCur = Code(i);
    end;
  end;

  return OutputCodCur;

end;


/*******************************************************************
		  Справочный скроллинг месяцев
*******************************************************************/
macro List_Month
(
 InputMonth
)

  array Name;
  var   i            = 0;
  var   OutputMonth  = InputMonth;

  while (i < 12)
    Name(i) = " " + String((i + 1):2) + "   " + MonName(i + 1) + "  ";
    i = i + 1;
  end;
  if (Asize(Name))
    i = Menu (Name, NULL, "Месяц");
    if (i >= 0)
      OutputMonth = i + 1;
    end;
  end;

  return OutputMonth;

end;


/*******************************************************************
		   Последний день месяца
*******************************************************************/
macro LastDayInMonth
(
 Month,
 Year
)

  array LastDay;

  LastDay( 1) = 31; LastDay( 2) = 28; Lastday( 3) = 31; LastDay( 4) = 30;
  LastDay( 5) = 31; LastDay( 6) = 30; Lastday( 7) = 31; LastDay( 8) = 31;
  LastDay( 9) = 30; LastDay(10) = 31; Lastday(11) = 30; LastDay(12) = 31;

  if (Year/4 - Int(Year/4) != 0)
    LastDay( 2) = 29;
  end;

  return LastDay(Month);

end;


/*******************************************************************
	    Обработка нажатия клавишей в панели диалога
*******************************************************************/
var PrevYear;

macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;
  var PrevNumMonth;
  var y, m, d;
  var y1,m1,d1;

  /*******************************************************************/
  if (cmd == DLG_KEY)
    /* Enter ----------------------------------------------- */
    if (key == 13)
      if (id == ne_SvodReport)
	SetFocus (IP, ne_Month);
	stat = CM_IGNORE;
      end;
    /* Esc - Отказ от выпуска отчета ----------------------- */
    elif (key == 27)
      stat = CM_CANCEL;
    /* F7 - Выпуск отчета ---------------------------------- */
    elif (key == 321)
      stat = CM_SAVE;
    /* F9 -------------------------------------------------- */
    elif (key == 323)
      stat = CM_IGNORE;
    /* F3 - Список значений поля --------------------------- */
    elif (key == 317)
      if (id == ne_ShCur)         /* Валюта ------ */
	if (FlagCur)
	  CodCur = List_Currency(CodCur);
	  if (SetCurrency(CodCur))
	    InputPanel.ShCur = CURRENCY.Short_Name;
	  else
	    InputPanel.ShCur = "";
	  end;
	end;
      elif (id == ne_Month)       /* Mесяц ------- */
	PrevNumMonth     = NumMonth;
	NumMonth         = List_Month(NumMonth);
	if (PrevNumMonth != NumMonth)
	  InputPanel.Month = MonName(NumMonth);
	  DateSplit(InputPanel.BegDate,d,m,y);
	  InputPanel.BegDate = Date(1,NumMonth,y);
	  DateSplit(InputPanel.EndDate,d,m,y);
	  InputPanel.EndDate = Date(LastDayInMonth(NumMonth,y),NumMonth,y);
	end;
      end;
      UpdateFields (IP);
    /* Пробел ---------------------------------------------- */
    elif (key == 32)
      if (id == ne_PodrReport)    /* Подробный --- */
	if (InputPanel.PodrReport == "")
	  InputPanel.PodrReport = "X";
	else
	  InputPanel.PodrReport = "";
	end;
      elif (id == ne_SvodReport)  /* Сводный ----- */
	if (InputPanel.SvodReport == "")
	  InputPanel.SvodReport = "X";
	else
	  InputPanel.SvodReport = "";
	end;
      end;
    /* CtrlF3 ---------------------------------------------- */
    elif (key == 352)
      if (id == ne_BegDate)
	InputPanel.BegDate = {curdate};
      elif (id == ne_EndDate)
	InputPanel.EndDate = {curdate};
      end;
    end;
  /*******************************************************************/
  elif (cmd == DLG_SETFOCUS)
    if (id == ne_Year)
      PrevYear = InputPanel.Year;
    end;
  /*******************************************************************/
  elif (cmd == DLG_REMFOCUS)
    if (id == ne_Year)      /* Выходим из поля "Год"               */
      if (PrevYear != InputPanel.Year);
	DateSplit(InputPanel.BegDate,d,m,y);
	InputPanel.BegDate = Date(d,m,InputPanel.Year);
	DateSplit(InputPanel.EndDate,d,m,y);
	InputPanel.EndDate = Date(d,m,InputPanel.Year);
      end;
    elif (id == ne_BegDate) /* Выходим из поля "Начало периода"    */
      DateSplit(InputPanel.BegDate,d,m,y);
      DateSplit(InputPanel.EndDate,d1,m1,y1);
      if (m == m1)
	NumMonth = m;
	InputPanel.Month = MonName(NumMonth);
      else
	NumMonth = 0;
	InputPanel.Month = MonName(NumMonth);
      end;
      if (y == y1)
	InputPanel.Year = y;
      else
	InputPanel.Year = 0;
      end;
    elif (id == ne_EndDate) /* Выходим из поля "Окончание периода" */
      DateSplit(InputPanel.BegDate,d,m,y);
      DateSplit(InputPanel.EndDate,d1,m1,y1);
      if (m == m1)
	NumMonth = m;
	InputPanel.Month = MonName(NumMonth);
      else
	NumMonth = 0;
	InputPanel.Month = MonName(NumMonth);
      end;
      if (y == y1)
	InputPanel.Year = y;
      else
	InputPanel.Year = 0;
      end;
    end;
    UpdateFields (IP);
  end;

  return stat;

end;


/*******************************************************************
		      Ввод исходных данных
*******************************************************************/
macro InputDate

  var stat = TRUE;

  if (NOT RunDialog(InputPanel, "WorkDialog"))
    [ ];
    [ Отказались от выпуска отчета ];
    [ ];
    stat = FALSE;
  else
    if ((InputPanel.PodrReport == "") AND (InputPanel.SvodReport == ""))
      [ ];
      [ У подробного и сводного отчета не стоят флаги выпуска форм];
      [ ];
      stat = FALSE;
    end;
    if (stat)
      if (InputPanel.BegDate > InputPanel.EndDate)
	[ ];
	[ Дата начала отчета не может быть больше даты окончания];
	[ ];
	stat = FALSE;
      end;
    end;
  end;

  return stat;

end;


/*******************************************************************
		  Инициализация полей диалога
*******************************************************************/
macro InitDialogFields

  var d,m,y;
  var stat;

  InputPanel.PodrReport      = "X";
  InputPanel.SvodReport      = "X";

  /* Определение дня и месяца отчета по умолчанию */
  DateSplit({curdate},d,m,y);
  if (d != LastDayInMonth(m,y))
    m = m - 1;
    if (m <= 0)
      m = 1;
      y = y - 1;
    end;
  end;
  NumMonth          = m;
  InputPanel.Month  = MonName(NumMonth);
  InputPanel.Year   = y;
  PrevYear          = InputPanel.Year;

  InputPanel.BegDate = Date(1,m,y);
  InputPanel.EndDate = Date(LastDayInMonth(m,y),m,y);

  /* Определение кода валюты по умолчанию */
  if (FlagCur == 0)
    CodCur = DefaultCodCurRub;
  else
    CodCur = DefaultCodCurCur;
  end;
  stat = SetCurrency(CodCur);
  if (stat)
    InputPanel.ShCur = CURRENCY.Short_Name;
  else
    InputPanel.ShCur = "";
  end;

  if (NOT stat)
    [ ];
    [ Не могу проинициализировать поля для панели];
    [ ];
  end;

  return stat;

end;


/*******************************************************************
		Г Л А В Н А Я   П Р О Г Р А М М А
*******************************************************************/

  BeginEndReport();

  if (InitDialogFields())   /* Инициализация полей диалога        */
    if (InputDate())        /* Ввод исходных данных               */
      if (OpenFiles())      /* Открытие выходных файлов           */
	DefAllBalAccount(); /* Определение всех балансовых счетов */
	WriteToOutFiles();  /* Выпуск отчетов                     */
	Summary();          /* Резюме                             */
      end;
    end;
  end;

  BeginEndReport();

end;
