import docBunch, commonInter;

private const
  CASHOP_REINFORCE = 1,  /* Подкрепление кассира */
  CASHOP_RETURN    = 2,  /* Возврат средств в кладовую */
  CASHOP_COLLECT   = 3,  /* Инкассация */
  CASHOP_ADVANCE   = 4,  /* Аванс */
  CASHOP_DEBIT     = 5,  /* Расход */
  CASHOP_CREDIT    = 6,  /* Приход */
  CASHOP_REMOVE    = 7,  /* Внутреннее подкрепление (Расход) */
  CASHOP_REMOVE_1  = 8,  /* Внутреннее подкрепление (Приход) */
  CASHOP_CHVALUE   = 9,  /* Перевод на другой ресурс той же группы (Расход) */
  CASHOP_CHVALUE_1 = 10, /* Перевод на другой ресурс той же группы (Приход) */
  CASHOP_REMOVE_SAFE   = 11,  /* Внутреннее подкрепление сейфа (Расход) */
  CASHOP_REMOVE_SAFE_1 = 12,  /* Внутреннее подкрепление сейфа (Приход) */
  CASHOP_DEBET_SAFE = 13,  /* Расход из сейфа */
  CASHOP_CREDIT_SAFE = 14, /* Приход в сейф */
  CASHOP_REVAL_POS = 18, /* Переоценка (+) */
  CASHOP_REVAL_NEG = 19, /* Переоценка (-) */
  CASHOP_PANT_RI   = 20, /* Подкрепление филиала */
  CASHOP_PANT_RET  = 21, /* Возврат из филиала */
  CASHOP_PANT_RIO  = 22, /* Подкрепление оперчасти */
  CASHOP_PANT_RETO = 23, /* Возврат из оперчасти */
  CASHOP_PANT_COL  = 24, /* Инкассация */
  CASHOP_PANT_ADV  = 25, /* Аванс */
  CASHOP_PANT_RDEB = 30, /* Расход по кассе пересчета */
  CASHOP_PANT_RCRD = 31, /* Приход по кассе пересчета */
  CASHOP_PANT_RDRT = 32, /* Сдача ресурса в кладовую из Кассы пересчета */
  CASHOP_PANT_ECRD = 41, /* Приход по вечерней кассе */
  CASHOP_PANT_EDRT = 42; /* Сдача ресурса из вечерней кассы */

private const
  CK_NEW = 0, 
  CK_OLD = 1;

private const
  CO_Comission   =  21; /* Кассовая операция - Комиссия                       */

private const
  SB_DEPOSIT     =    1,   /* Документ вкладной операции                   */
  SB_CASHOPERT   =  200,   /* Документ для сервисно-кассовых операций      */
  SB_CAPISSUE    =  300;   /* Ценные бумаги                                */

private const
  TYPERES_MINCAPISSUE = 500,
  CI_COIN = 4;

private var 
  {curdate};

private var
  ciMisc = TBFile( "ci_misc.dbt", "r", 0 ), 
  cashVal = TBFile( "sb_casvl.dbt", "r", 0 ); 

private macro findCiMisc( type, issue )

  ciMisc.rec.Type = type;
  ciMisc.rec.IssueID = issue;
  if ( not ciMisc.getEQ )
    runError( "Монета не найдена в справчнике" );
  end;
end;

private macro findCashVal( ref )

  cashVal.rec.RefValue = ref;
  if ( not cashVal.getEQ )
    runError( "Не найден ресурс кассы" );
  end;
end; 

class ( TRecHandler ) TCoinCashDoc( _appKind, _appKey, _valueRef )

  var
    cashDoc = TBFile( "sb_casdc.dbt", "r", 7 ), 
    dbcDoc = TBFile( "dbc_doc.dbt", "r", 0 ), 
    coinType = TRecHandler( "ci_misc.dbt" ), 
    appKind = _appKind, 
    appKey = _appKey, 
    valueRef = _valueRef, 
    numItems = 0, 
    totalPar = $0, 
    balCost = $0, 
    incomingNds = $0;

  private macro calcBalCostAndNds

    var
      recFound;

    dbcDoc.clear;
    dbcDoc.rec.LinkApplicKind = cashDoc.rec.ApplicationKind;
    dbcDoc.rec.LinkApplicKey  = cashDoc.rec.ApplicationKey;
    recFound = dbcDoc.getGE;
    while ( recFound
      and ( dbcDoc.rec.LinkApplicKind == cashDoc.rec.ApplicationKind )
      and ( dbcDoc.rec.LinkApplicKey  == cashDoc.rec.ApplicationKey ) )
      balCost = balCost + dbcDoc.rec.BalCost * dbcDoc.rec.NumItems;
      incomingNds = incomingNds + dbcDoc.rec.Nds + dbcDoc.rec.NumItems;
      recFound = dbcDoc.next;
    end;                             
  end;

  private macro init

    cashDoc.rec.ApplicationKind = appKind;
    cashDoc.rec.ApplicationKey = appKey;
    cashDoc.rec.RefValue = valueRef;
    if ( not cashDoc.getEQ )
      clear;
      coinType.clear;
      return;
    end;

    copy( this, cashDoc );

    findCashVal( rec.RefValue );           
    findCiMisc( cashVal.rec.TypeValue - TYPERES_MINCAPISSUE, cashVal.rec.CodIntValue );
    copy( coinType, ciMisc );
    numItems = cashDoc.rec.ValueCol;
    totalPar = coinType.rec.Par * numItems;
    calcBalCostAndNds;
  end;

  macro getNumItems

    return numItems;
  end;

  macro getTotalPar

    return totalPar;
  end;

  macro getBalCost

    return balCost;
  end;

  macro getIncomingNds

    return incomingNds;
  end;

  macro getCoinType

    return coinType;
  end;

  InitTRecHandler( "sb_casdc.dbt" );
  init;
end;

class ( TDocBunch ) TCoinDocBunch( appKind, appKey )

  var
    ciDoc   = TBFile( "ci_doc.dbt", "r", 3 ), 
    payDoc  = TBFile( "pay_doc.dbt", "r", 2 ),
    cashDoc = TBFile( "sb_casdc.dbt", "r", 7 ), 
    payKind = TBFile( "pay_kind.dbt", "r", 0 ),
    clientList = TClientList, 
    coinType = null, 
    depClnt, 
    isSingCn = true, 
    clientDesc = "", 
    paperSeriesAndNumber = "", 
    paperInfo = "", 
    numItems = 0, 
    opSum = $0, 
    totalPar = $0, 
    balCost = $0, 
    incomingNds = $0, 
    outgoingNds = $0, 
    salesTax = $0,  
    stIsIncluded, 
    ndsIsIncluded;

  var
    dRefValue = 0, 
    dApplicationKind = 0, 
    dApplicationKey = "";

  private macro findPayKind( isCur, group, num )
    
    payKind.rec.IsCur = isCur;
    payKind.rec.GroupOpert = group;
    payKind.rec.NumOperat = num;
    if ( not payKind.getEQ )
      runError( "Не найдена прочая операция" );
    end;
  end;

  private macro processCashDoc

    var
      doc,
      curCoinType;

    cashDoc.rec.ApplicationKind = docAppKind;
    cashDoc.rec.ApplicationKey = docAppKey;
    cashDoc.rec.RefValue = docValueRef;
    if ( not cashDoc.getEQ )
      runError( "Не найден кассовый документ" ); 
    end;

    dRefValue = docValueRef;
    dApplicationKind = docAppKind;
    dApplicationKey = docAppKey;

    findCashVal( cashDoc.rec.RefValue );
    if ( cashVal.rec.TypeValue == TYPERES_MINCAPISSUE + CI_COIN )
      doc = TCoinCashDoc( docAppKind, docAppKey, docValueRef );
      curCoinType = doc.getCoinType;
      if ( isSingCn )
        if ( not coinType )
          coinType = curCoinType;
        elif ( coinType.rec.IssueID != curCoinType.rec.IssueID )
          coinType = null;
          isSingCn = false;
        end;
      end;
      numItems = numItems + doc.getNumItems;
      totalPar = totalPar + doc.getTotalPar;
      balCost  = balCost  + doc.getBalCost;
      incomingNds = incomingNds + doc.getIncomingNds;
    end;
  end;

  private macro processCiDoc

    var
      info;

    ciDoc.rec.ApplicationKind = docAppKind;
    ciDoc.rec.ApplicationKey = docAppKey;
    if ( not ciDoc.getEQ )
      runError( "Не найден документ по ЦБ/Ц" ); 
    end;

    opSum = opSum + ciDoc.rec.Sum;
  
    if ( clientDesc == "" )
      if ( ciDoc.rec.CodClient )
        if ( clientList.getRecord( ciDoc.rec.CodClient ) )
          depClnt = clientList.curRec;
          clientDesc = depClnt.convertFio;   
          paperSeriesAndNumber = GetNameOfPAPRKIND( depClnt.rec.PaperKind ) +
                                         depClnt.rec.PaperSeries +
                                         depClnt.rec.PaperNumber;
          paperInfo = depClnt.rec.PaperIssuer + " " + string( depClnt.rec.PaperIssuedDate );
        end;
      else
        info = TRecHandler( "ci_doc.2" );
        info.setRecordAddr( ciDoc, 0, ciDoc.fldOffset( "Info" ), true );
        clientDesc = info.rec.LastName;
        if ( info.rec.FirstName != "" )
          clientDesc = clientDesc + " " + subStr( info.rec.FirstName, 1, 1 ) + ".";
          if ( info.rec.PatrName != "" )
            clientDesc = clientDesc + " " + subStr( info.rec.PatrName, 1, 1 ) + ".";
          end;
        end;
        paperInfo = info.rec.PersID;
      end;
    end;

    findCiMisc( ciDoc.rec.Type, ciDoc.rec.Issue );
    ndsIsIncluded = ciMisc.rec.NDS_IsIncludedInCost;
    stIsIncluded  = ciMisc.rec.ST_IsIncludedInCost;
  end;

  private macro processPayDoc

    payDoc.rec.iApplicationKind = docAppKind;
    payDoc.rec.ApplicationKey = docAppKey;
    if ( not payDoc.getEQ )
      runError( "Не найден документ по прочей операции" ); 
    end;

    findPayKind( payDoc.rec.isCur, payDoc.rec.GroupOpert, payDoc.rec.NumOpert );

    if ( payKind.rec.GroupOpert == CO_Comission ) 
      if ( payKind.rec.Type_Comis == 1 )
        outgoingNds = outgoingNds + payDoc.rec.InSum;
      elif ( payKind.rec.Type_Comis == 2 )
        salesTax = salesTax + payDoc.rec.InSum;
      end  
    end;
  end;

  private macro processDoc

    if ( docValueRef )
      processCashDoc;
    else
      if ( docAppKind == SB_CAPISSUE )
        processCiDoc;
      elif ( docAppKind == SB_CASHOPERT )
        processPayDoc;
      end;
    end;
  end;

  private macro init

    var
      recFound;

    recFound = firstDoc;
    while ( recFound )
      processDoc;
      recFound = nextDoc;
    end; 
  end;

  macro getNumItems

    return numItems;
  end;

  macro getCiOpSum

    return opSum;
  end;

  macro getTotalPar

    return totalPar;
  end;

  macro getBalCost

    return balCost;
  end;

  macro getIncomingNds

    return incomingNds;
  end;

  macro getOutgoingNds

    return outgoingNds;
  end;

  macro getSalesTax

    return salesTax;
  end;

  macro getClientDesc

    return clientDesc;
  end;

  macro getCoinType

    return coinType;
  end;

  macro isSingleCoinTypeOp

    return isSingCn;
  end;

  macro getPaperSeriesAndNumber
    
    return paperSeriesAndNumber;
  end; 

  macro getPaperInfo
    
    return paperInfo;
  end; 

  initTDocBunch( appKind, appKey );
  init;
end;