
/* Отчет по срокам востребования */

import DeprIntr, Cur_Rate;

var ddep = TRecHandler( "i_dp_dep.rec" );
var dpDepList = TdpDepList;
file Curr( "currency.dbt" ) key 0;
file Dep( "depositr.dbt" );
file DepType( "sb_dtyp.dbt" ) key 4;
file DepStat( "sb_depst.dbt" ) key 0; 

const
  NullDate = Date( 0, 0, 0 );

var
  Branch = -1,
  CurrCode = -1,
  {curdate},
  RepDate = {curdate},
  IsCur,
  TermNo,
  Mode = GetCurrentMode,
  ClientList = TClientList, 
  CBRate,                          
  Scale,
  N = 0,
  Total,
  BranchTotal,
  CurrTotalC,
  CurrTotal,
  TermTotalC,
  TermTotal,
  DTypeTotalC,
  DTypeTotal,
  DTypeTotalCNR,
  DTypeTotalNR;

array
  Terms,
  Names;

  Terms( 0 ) = 0; Names( 0 ) = "Д о   в о с т р е б о в а н и я";
  Terms( 1 ) = 1; Names( 1 ) = "1   д е н ь";
  Terms( 2 ) = 7; Names( 2 ) = "2 - 7   д н е й";
  Terms( 3 ) = 30;  Names( 3 ) = "8 - 3 0   д н е й";
  Terms( 4 ) = 60;  Names( 4 ) = "3 1 - 6 0   д н е й";
  Terms( 5 ) = 90;  Names( 5 ) = "6 1 - 9 0   д н е й";
  Terms( 6 ) = 180; Names( 6 ) = "9 1 - 1 8 0   д н е й";
  Terms( 7 ) = -12; Names( 7 ) = "1 8 1   д е н ь   -   1  г о д";
  Terms( 8 ) = -13; Names( 8 ) = "1  г о д   -   1  г о д    1  м е с я ц";
  Terms( 9 ) = -14; Names( 9 ) = "1  г о д   1  м е с .  -   1  г о д    2   м е с .";
  Terms( 10 ) = -15;  Names( 10 ) = "1  г о д   2  м е с .  -   1  г о д    3   м е с .";
  Terms( 11 ) = -16;  Names( 11 ) = "1  г о д   3  м е с .  -   1  г о д    4   м е с .";
  Terms( 12 ) = -17;  Names( 12 ) = "1  г о д   4  м е с .  -   1  г о д    5   м е с .";
  Terms( 13 ) = -18;  Names( 13 ) = "1  г о д   5  м е с .  -   1  г о д    6   м е с .";
  Terms( 14 ) = -19;  Names( 14 ) = "1  г о д   6  м е с .  -   1  г о д    7   м е с .";
  Terms( 15 ) = -20;  Names( 15 ) = "1  г о д   7  м е с .  -   1  г о д    8   м е с .";
  Terms( 16 ) = -21;  Names( 16 ) = "1  г о д   8  м е с .  -   1  г о д    9   м е с .";
  Terms( 17 ) = -22;  Names( 17 ) = "1  г о д   9  м е с .  -   1  г о д    10   м е с .";
  Terms( 18 ) = -23;  Names( 18 ) = "1  г о д   10  м е с .  -   1  г о д    11   м е с .";
  Terms( 19 ) = -24;  Names( 19 ) = "1  г о д   12  м е с .  -   2  г о д а";
  Terms( 20 ) = -36;  Names( 20 ) = "2  г о д а  -  3  г о д а";
  Terms( 21 ) = 0;  Names( 21 ) = "С в ы ш е   3   л е т";

macro FindDepType( DType )
              
  DepType.FlagCur = IsCur;
  DepType.Kind = DType;
  return GetEQ( DepType );  
end;

macro FindBranch( BrNo )

  if ( not findDepartment(BrNo, null, ddep) )
    MsgBox( "Не найден филиал" );
    Exit( 1 );
  end;
end;

macro FindCurr( Code )

  Curr.Code_Currency = Code;
  if ( not GetEQ( Curr ) )
    MsgBox( "Не найдена валюта" );
    Exit( 1 );
  end;
end;

macro FindDepClient( Code )

  return ClientList.GetRecord( Code );
end;

macro GetCBRate

  Scale = 1;
  CBRate = 0.0;

  if (Curr.ScaleOfc != 0)
    Scale = Curr.ScaleOfc;
  end;
/*
  ExchCurr.ID = Int(Curr.ExternalCode);
  if (GetEQ(ExchCurr))
*/
    copy( RtlCurr, Curr );
    CBRate = GetCurrRate( RepDate );
/*  end;*/
end;

macro Cur2Rub(Sum)

  if ( IsCur )
    return Money( CBRate * Sum );
  else
    return Sum;
  end;
end;

macro MyMin( a, b )

  if ( a < b )
    return a;
  else
    return b;
  end;
end;

macro LastDayInMonth( Month, Year )

  var
    Day;

  if ( Month == 12 )
    Month = 1;
    Year = Year + 1;
  else
    Month = Month + 1;
  end;

  DateSplit( Date( 1, Month, Year ) - 1, Day );
  return Day; 
end;

macro AddMonths( Dd, Months2Add )

  var
    Day, Month, Year, Years2Add;

  DateSplit( Dd, Day, Month, Year );
  Month = Month + Months2Add;
  Years2Add = Int( ( Month - 1 ) / 12 );
  Month = Month - Years2Add * 12;
  Year = Year + Years2Add;
  Day = MyMin( Day, LastDayInMonth( Month, Year ) );
  return Date( Day, Month, Year );
end;

macro EndOfTerm( TermNo )

  var
    Term = Terms( TermNo ),
    TermEndDate;

  if ( Term > 0 )
    TermEndDate = RepDate + Term;
  elif ( Term < 0 )
    TermEndDate = AddMonths( RepDate, -Term );
  else                                           /* Term == 0 */
    TermEndDate = Date( 31, 12, 2099 );
  end;
  return TermEndDate;
end;                                 

macro PrintHeader
                     
  [ ];
  [ ];
  [ Справка № 1                                                                                   ];
  [                                                                                               ]; 
  [            Информация о структуре вкладов населения по срокам востребования       ];                                                                                                                                                        
  [            ################################################################                   ]
  ( ( "За " + String( RepDate:m ) ):c );
  [                                                                                               ];
  [                                                    Филиал № #                                 ]
  ( ddep.rec.Name );
  [                                                    Валюта   ### #                             ]
  ( Curr.Short_Name, Curr.Name_Currency );     
  [                                                                                               ];
  [-----------------------------------------------------------------------------------------------];
  [ Балансовый | Наименование вклада                      |   Сумма в валюте   |  Сумма в рублях  ];
  [ счет       |                                          |                    |  (руб.покрытие)  ];                                           
  [------------|------------------------------------------|--------------------|------------------];
end;

macro PrintDTypeTotals

  [ ########### ########################################## #################### ##################]
  (
    DepType.BalAcc,
    DepType.Name + " (рез.)":w,
    DTypeTotalC:z,
    DTypeTotal
  );
end;

macro PrintDTypeTotalsNR

  [ ########### ########################################## #################### ##################]
  (
    DepType.BalAcc,
    DepType.Name + " (нерез.)":w,
    DTypeTotalCNR:z,
    DTypeTotalNR
  );
end;

macro PrintTermTotals

  [ ###################################################### #################### ##################]
  (
    Names( TermNo ),
    TermTotalC:z,
    TermTotal
  );                   
  [ ];
end;

macro PrintCurrTotals

  [-----------------------------------------------------------------------------------------------];
  [ ИТОГО ПО ВАЛЮТЕ ###:                                   #################### ##################]
  (
    Curr.Short_Name,
    CurrTotalC:z,
    CurrTotal
  );
end;

macro PrintBranchTotals

  [ ИТОГО ПО ФИЛИАЛУ № #                                                        ##################]
  (
    ddep.rec.Name,
    BranchTotal
  );
end;

macro PrintTotals

  [ ];
  [===============================================================================================];
  [ ИТОГО ПО БАНКУ                                                              ##################]
  (
    Total
  );
end;

macro TreatAccount      
                       
/*
if(Dep.SvodAccount )
return;
end;
println(dep.account, " ", dep.sum_rest);
*/

  if ( not FindDepClient( Dep.CodClient ) )
    MsgBox( "Не найден клиент. Код ", Dep.CodClient );
    ClientList.CurRec.Clear;
  end;

  if ( IsCur )
    if ( ClientList.CurRec.Rec.NotResident == "" )
      DTypeTotal = DTypeTotal + Cur2Rub( Dep.Sum_Rest );
      DTypeTotalC = DTypeTotalC + Dep.Sum_Rest;
    else
      DTypeTotalNR = DTypeTotalNR + Cur2Rub( Dep.Sum_Rest );
      DTypeTotalCNR = DTypeTotalCNR + Dep.Sum_Rest;
    end;
  else
    if ( ClientList.CurRec.Rec.NotResident == "" )
      DTypeTotal = DTypeTotal + Dep.Sum_Rest;
    else
      DTypeTotalNR = DTypeTotalNR + Dep.Sum_Rest;
    end;
  end;                      
 
  N = N + 1;
  Message( "Обработано ", N, " счетов" );
end;              

macro GetDTypeRestFromStats( Cit, Rest )

  DepStat.FNCash = ddep.rec.Code;
  DepStat.IsCur = IsCur;
  DepStat.Kind = DepType.Kind;
  DepStat.CodCur = Curr.Code_Currency;
  DepStat.FlagRez = Cit;
  DepStat.Date = RepDate;
  DepStat.Mode = Mode;
  if ( GetLE ( DepStat )
    and ( DepStat.FNCash == ddep.rec.Code )
    and ( DepStat.IsCur == IsCur )
    and ( DepStat.Kind == DepType.Kind )
    and ( DepStat.CodCur == Curr.Code_Currency )
    and ( DepStat.FlagRez == Cit ) )
    Rest = DepStat.Rest;
  else
    Rest = $0;
  end;
  SetParm( 1, Rest );  
end;

macro TreatDepType

  var   
    Tmp,
    BOT, EOT,
    RecFound;

  DTypeTotal = DTypeTotalC = DTypeTotalNR = DTypeTotalCNR = $0;

  if ( DepType.FormContr /*= 1*/ and ( DepType.NewGroupNumb != "1" ) )
    if ( TermNo > 0 )
      EOT = EndOfTerm( TermNo );
    else
      EOT = NullDate;
    end;                        

    if ( TermNo > 1 )
      BOT = EndOfTerm( TermNo - 1 ) + 1;
    elif ( TermNo == 1 )
      BOT = RepDate;
    else
      BOT = NullDate;
    end;

    KeyNum( Dep, 6 );
    
    ClearRecord( Dep );
    Dep.IsCur = IsCur;
    Dep.FNCash = ddep.rec.Code;
    Dep.Type_Account = DepType.Kind;
    Dep.End_DateDep = BOT;
    RecFound = GetGE( Dep );
    while ( RecFound
      and ( Dep.IsCur == IsCur )
      and ( Dep.FNCash == ddep.rec.Code )
      and ( Dep.Type_Account == DepType.Kind )
      and ( Dep.End_DateDep <= EOT ) )
      if ( ( Dep.Open_Close == "" ) 
        and ( Dep.Open_Date <= RepDate )
        and ( Dep.Code_Currency == Curr.Code_Currency ) )
        TreatAccount;
      end;
      RecFound = Next( Dep );
    end;
  elif ( TermNo == 0 )
    GetDTypeRestFromStats( StrFor( 0 ), Tmp );
    if ( IsCur );
      DTypeTotal = Cur2Rub( Tmp );
      DTypeTotalC = Tmp;
    else
      DTypeTotal = Tmp;
    end;
    GetDTypeRestFromStats( StrFor( 1 ), Tmp );
    if ( IsCur );
      DTypeTotalNR = Cur2Rub( Tmp );
      DTypeTotalCNR = Tmp;
    else
      DTypeTotalNR = Tmp;
    end;
  end;

  if ( DTypeTotal or DTypeTotalC )
    PrintDTypeTotals;
    TermTotal = TermTotal + DTypeTotal; 
    TermTotalC = TermTotalC + DTypeTotalC; 
  end; 
  if ( DTypeTotalNR or DTypeTotalCNR )
    PrintDTypeTotalsNR;
    TermTotal = TermTotal + DTypeTotalNR; 
    TermTotalC = TermTotalC + DTypeTotalCNR; 
  end; 
end;          

macro TreatTerm

  var
    RecFound;

  TermTotal = TermTotalC = $0;
           
  ClearRecord( DepType );            
  DepType.FlagCur = IsCur;
  RecFound = GetGE( DepType );
  while ( RecFound and ( DepType.FlagCur == IsCur ) )
    if ( ( DepType.Kind != "Шаблонный" )  )
      TreatDepType;
    end; 
    RecFound = Next( DepType );
  end;

  PrintTermTotals;
  CurrTotal = CurrTotal + TermTotal;
  CurrTotalC = CurrTotalC + TermTotalC;
end;

macro AccountsPresent

  var
    RecFound;

  KeyNum( Dep, 1 );
  ClearRecord( Dep );
  Dep.IsCur = IsCur;
  Dep.FNCash = ddep.rec.Code;
  Dep.Open_Close = "";
  RecFound = GetGE( Dep );
  while ( RecFound
    and ( Dep.IsCur == IsCur )
    and ( Dep.FNCash == ddep.rec.Code )
    and ( Dep.Open_Close == "" ) )
    if ( ( Dep.Code_Currency == Curr.Code_Currency ) 
      and ( Dep.Open_Date <= RepDate ) )
      return true;
    end;
    RecFound = Next( Dep );
  end;
  return false;
end;

macro TreatCurr

  if ( Curr.Code_Currency )
    IsCur = 1;
    GetCBRate;
  else
    IsCur = 0;
  end;

  if ( not AccountsPresent )
    return;
  end;

  PrintHeader;
  CurrTotal = CurrTotalC = $0;
  TermNo = 0;
  while ( TermNo < ASize( Terms ) )
    TreatTerm;
    TermNo = TermNo + 1;
  end;
  PrintCurrTotals;
  BranchTotal = BranchTotal + CurrTotal;
end;

macro TreatBranch

  BranchTotal = $0;
  if ( CurrCode >= 0 )
    FindCurr( CurrCode );
    TreatCurr;
  else
    Rewind( Curr );
    while ( Next( Curr ) )
      if ( (Curr.Code_Currency >= 0) and Curr.CrDifrRate )
        TreatCurr;
      end; 
    end;
  end;
  if ( BranchTotal )
    PrintBranchTotals;
  end;
  Total = Total + BranchTotal;
end;

/* Голова */

  GetDate( RepDate, "Введите дату отчета" );
  Total = $0;
  if ( Branch >= 0 )
    FindBranch( Branch );
    if ( RepDate == {curdate} )
      if ( not UpdateStatistics( false, null, -1, Branch ) )
        Exit;
      end;
    end;
    TreatBranch;
  else
    dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
    ddep = dpDepList.RecHandler;
    while ( dpDepList.next() )
      if ( RepDate == {curdate} )
        if ( not UpdateStatistics( false, null, -1, ddep.rec.Code ) )
          Exit;
        end;
      end;
      TreatBranch;
    end;
  end;
  PrintTotals;
end;
