/*
$Name:           chdtype2.mac
$Module:         RETAIL
$Description:    Перевод в другой вид вклада
*/

/* Перевод в другой вид вклада */

import deprintr,DeskInter,OperCode,save_acc;

file CDTDep       (depositr) key 8 write;
file CDTDepDoc    (sbdepdoc) key 6 write;
file CDTDepRest   (pc_drest) key 0 write;
file CDTEdRef     ("rsbedref.tmp") write;
file CDTPcCalc    (pc__calc) key 0 write;
file dtyp         (sb_dtyp ) key 2;
file CDTpcestim   (pcestim ) key 0;
file accreg       (accreg  ) key 1 write;

record sbdepdocChDTyp ("sbdepdoc.3");
record Template       ("pcacc.dbt");

const
  CDT_SB_DEPOSIT = 1,

const
  ACCOUTN_OWNER = StrFor( 0 );

const
  NewType  = "ДО ВОСТРЕБ.";

const
  MINDATE = Date(01, 01, 1900),
  MAXDATE = Date(31, 12, 2999);

const
  UPDATE_ALL  = 1,
  UPDATE_DEP  = 2,
  UPDATE_DOC  = 4;

const
  Binsert = 2;

const
  D_IN = 1,         /* Приход */
  D_OUT = 2,        /* Расход */
  D_GET = 3,        /* Списание */
  D_PUT = 5;        /* Зачисление */

var
  curdat = {curdate},
  operman = {oper},
  {GroupProcRunning},
  OperBrigade = GetOperBrigade,
  Branch = NumFNCash,
  OldType,
  TrnStatus = true,
  DocApplicationKind,
  CDTClientList = TClientList, 
  TxtDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true);

var OldPosDepDoc;

macro AddEdRef

  ClearRecord( CDTEdRef );
  CDTEdRef.IsCur = CDTDepDoc.IsCur;
/*  CDTEdRef.PosRec = GetPos( CDTDepDoc );  */
  CDTEdRef.SignRec = 2;                         /* SIGNORDER */
  CDTEdRef.IsCur = CDTDepDoc.IsCur;
  CDTEdRef.AccType = CDTDepDoc.Type_Account;
  CDTEdRef.AccNumber = ALG_DEPOSITOR.Number;
  CDTEdRef.Operation = CDTDepDoc.TypeOper;
  CDTEdRef.Code_Currency = CDTDepDoc.Code_Currency;
  CDTEdRef.ApplicationKind = CDTDepDoc.iApplicationKind;
  CDTEdRef.ApplicationKey = CDTDepDoc.ApplicationKey;
  return Insert( CDTEdRef );
end;

macro IsResident

  TrnStatus = CDTClientList.GetRecord( ALG_DEPOSITOR.CodClient );
  if(TrnStatus)
    return CDTClientList.CurRec.Rec.NotResident;
  else
    return -1;
  end;
end;

macro GetDepTypeIdentChars

  rewind(dtyp);
  dtyp.FlagCur = ALG_DEPOSITOR.IsCur;
  dtyp.Kind = NewType;
  if(getEQ(dtyp))
    return dtyp.NewIdentChar;
  end;

  return "XX";

end;

macro ChangeAccReg

  accreg.IsCur         = ALG_DEPOSITOR.IsCur;
  accreg.FNCash        = ALG_DEPOSITOR.FNCash;
  accreg.GroupNumb     = dtyp.NewGroupNumb;
  accreg.Code_Currency = ALG_DEPOSITOR.Code_Currency;
  accreg.Number        = ALG_DEPOSITOR.Number;
  if(getEQ(accreg))
    accreg.Account = ALG_DEPOSITOR.Account;
    update(accreg);
  end;

end;

macro MakeDepOID( Ref )

  var
    OID = String( Ref );

  while ( StrLen( OID ) < 8 )
    OID = "0" + OID;
  end;

  return oID;
end;

macro CreateDepDocs

  record OpParm( "op_parm.rec" );

  var
    CDTDepDoc_Date_Document,
    CDTDepDoc_NumDayDoc,
    MainAppKey = FormApplicationKey( DocApplicationKind ),
    Rest, PercRest, SavePos;

  if ( not InterDesk_IsDocBunchActive )
    ClearRecord( OpParm );
    OpParm.Date = curdat;
    OpParm.Operation = Перевод_В_Другой_Вид_Вклада;
    OpParm.ObjectRef = MakeDepOID( ALG_DEPOSITOR.Referenc );

    TrnStatus = InterDesk_InitDocBunch( DocApplicationKind, MainAppKey, OpParm );
  end;
  if ( TrnStatus )
    TrnStatus = CashLink_AddRec( DocApplicationKind, MainAppKey );
  end;
  if ( TrnStatus )
    ClearRecord( CDTDepDoc );
    CDTDepDoc.Referenc = ALG_DEPOSITOR.Referenc;
    CDTDepDoc.Date_Document = ALG_SBDEPDOC.Date_Document; /*MAXDATE;*/
    CDTDepDoc.NumDayDoc = 32767;
    TrnStatus = GetLE(CDTDepDoc);
  end;
  if(TrnStatus and(CDTDepDoc.Referenc == ALG_DEPOSITOR.Referenc))
/*
    if(StrFor(GetProgramID) == "П")
      SavePos = GetPos(CDTDepDoc);
      if(Next(CDTDepDoc))
        if(Delete(CDTDepDoc))
          CDTDepDoc.Type_Account = NewType;
          Insert(CDTDepDoc);
        end;
      end;
      GetDirect(CDTDepDoc, SavePos);
    end;
*/
    Rest = CDTDepDoc.Rest;
    PercRest = CDTDepDoc.PercRest;
    CDTDepDoc_Date_Document = CDTDepDoc.Date_Document;
    CDTDepDoc_NumDayDoc = CDTDepDoc.NumDayDoc;
    ClearRecord( CDTDepDoc );
    CDTDepDoc.FNCash = ALG_DEPOSITOR.FNCash;
    CDTDepDoc.Referenc = ALG_DEPOSITOR.Referenc;
    CDTDepDoc.IsCur = ALG_DEPOSITOR.IsCur;
    CDTDepDoc.Code_Currency = ALG_DEPOSITOR.Code_Currency;
    CDTDepDoc.CodClient = ALG_DEPOSITOR.CodClient;
    CDTDepDoc.Author = ACCOUTN_OWNER;
    CDTDepDoc.Account = ALG_DEPOSITOR.Account;
    CDTDepDoc.Type_Account = OldType;
    CDTDepDoc.TypeOper = Перевод_В_Другой_Вид_Вклада;
    CDTDepDoc.TypeComplexOper = Перевод_В_Другой_Вид_Вклада;
    CDTDepDoc.ApplType = 1;
    CDTDepDoc.VidDoc = 0;
    CDTDepDoc.KindOp = D_GET;
    CDTDepDoc.iApplicationKind=DocApplicationKind;
    CDTDepDoc.ApplicationKey=FormApplicationKey(DocApplicationKind);
    CDTDepDoc.InSum = $0;
    CDTDepDoc.OutSum = Rest;
    CDTDepDoc.PercOprSum = $0;
    CDTDepDoc.Rest = $0;
    CDTDepDoc.PercRest = PercRest;
    CDTDepDoc.oper = operman;
    CDTDepDoc.Brigade = OperBrigade;
    CDTDepDoc.ObjectPerc = 2002;
    CDTDepDoc.Ground = "Смена вида вклада с \"" + OldType + "\" на \"" +
      NewType + "\"";
    CDTDepDoc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
    if(CDTDepDoc_Date_Document == curdat)
      CDTDepDoc.Date_Document = curdat;
      CDTDepDoc.NumDayDoc = CDTDepDoc_NumDayDoc + 100;
    else
      CDTDepDoc.Date_Document = curdat;
      CDTDepDoc.NumDayDoc = 100;
    end;
    TrnStatus=Insert(CDTDepDoc);
    if(TrnStatus and (StrFor(GetProgramID) != "П"))
      TrnStatus=AddEdRef;
    end;
    if(TrnStatus)
      OldPosDepDoc = GetPos (CDTDepDoc);
      TrnStatus = CashLink_AddRec( DocApplicationKind, CDTDepDoc.ApplicationKey );
    end;
    if(TrnStatus)
      if(GetDepTypeIdentChars != "XX")
        StrSet(ALG_DEPOSITOR.Account, 21, GetDepTypeIdentChars);
        UpdateAlgRecords( UPDATE_DEP, True );
      end;
      CDTDepDoc.Account = ALG_DEPOSITOR.Account;
      CDTDepDoc.ApplType = 2;
      CDTDepDoc.KindOp = D_PUT;
      CDTDepDoc.InSum = Rest;
      CDTDepDoc.OutSum = $0;
      CDTDepDoc.Rest = Rest;
      CDTDepDoc.ObjectPerc = 2001;
      CDTDepDoc.Type_Account = NewType;
      CDTDepDoc.NumDayDoc = CDTDepDoc.NumDayDoc + 100;
      CDTDepDoc.ApplicationKey=FormApplicationKey(DocApplicationKind);
      TrnStatus=Insert(CDTDepDoc);
    end;
    if(TrnStatus and (StrFor(GetProgramID) != "П"))
      TrnStatus=AddEdRef;
    end;
    if(TrnStatus)
      TrnStatus = CashLink_AddRec( DocApplicationKind, CDTDepDoc.ApplicationKey );
    end;
    if(TrnStatus)
      CDTDepDoc.ApplType = 0;
      CDTDepDoc.InSum = $0;
      CDTDepDoc.OutSum = $0;
      CDTDepDoc.Rest = Rest;
      CDTDepDoc.ApplicationKey=MainAppKey;
      CDTDepDoc.NumDayDoc = CDTDepDoc.NumDayDoc + 100;
      SetRecordAddr(sbdepdocChDTyp,CDTDepDoc,0,0,TRUE);
      sbdepdocChDTyp.TypeAccBefor = OldType;
      sbdepdocChDTyp.TypeAccAfter = NewType;
      TrnStatus=Insert(CDTDepDoc);
    end;
  end;
/*
  if(StrFor(GetProgramID) != "П")
    InterDesk_EndDocBunch;
  end;
*/
end;

macro ChangeObjectType2002To2001

  var
    RecFound,
    OldKey;
  if(TrnStatus)
    ClearRecord(CDTDepRest);
    CDTDepRest.Referenc = ALG_DEPOSITOR.Referenc;
    CDTDepRest.Type_Object = 2001;
    RecFound = GetGE(CDTDepRest);
    while(TrnStatus and RecFound
      and (CDTDepRest.Referenc == ALG_DEPOSITOR.Referenc)
      and (CDTDepRest.Type_Object == 2001))
      TrnStatus = Delete(CDTDepRest);
      if(TrnStatus)
/*        RecFound = Next(CDTDepRest); РАВ 20.10.98 Изменяемое поле Type_Object входит в ключ!!! */
        CDTDepRest.Referenc = ALG_DEPOSITOR.Referenc;
        CDTDepRest.Type_Object = 2001;
        CDTDepRest.Date_Rest = Date(0,0,0);
        RecFound = GetGE(CDTDepRest);
      end;
    end;
    ClearRecord(CDTDepRest);
    CDTDepRest.Referenc = ALG_DEPOSITOR.Referenc;
    CDTDepRest.Type_Object = 2002;
    RecFound = GetGE(CDTDepRest);
    while(TrnStatus and RecFound
      and (CDTDepRest.Referenc == ALG_DEPOSITOR.Referenc)
      and (CDTDepRest.Type_Object == 2002))
      CDTDepRest.Type_Object = 2001;
      CDTDepRest.NumSession = 0;
      TrnStatus = Update(CDTDepRest);
      if(TrnStatus)
/*        RecFound = Next(CDTDepRest); РАВ 20.10.98 Изменяемое поле Type_Object входит в ключ!!! */
        CDTDepRest.Referenc = ALG_DEPOSITOR.Referenc;
        CDTDepRest.Type_Object = 2002;
        CDTDepRest.Date_Rest = Date(0,0,0);
        RecFound = GetGE(CDTDepRest);
      end;
    end;
  end;
/* РАВ 20.10.98 Проценты на операцию */
  if(TrnStatus)
    OldKey = KeyNum(CDTDepDoc,2);
    ClearRecord(CDTDepDoc);
    CDTDepDoc.Referenc = ALG_DEPOSITOR.Referenc;
    RecFound = GetGE(CDTDepDoc);
    while(TrnStatus and RecFound
      and (CDTDepDoc.Referenc == ALG_DEPOSITOR.Referenc))
      if ((CDTDepDoc.DepDate_Document != curdat) OR
       (CDTDepDoc.TypeOper != Перевод_В_Другой_Вид_Вклада))
        CDTDepDoc.ObjectPerc = 2001; /* Для новых уже поставлено */
        TrnStatus = Update(CDTDepDoc);
      end;
      if(TrnStatus)
        RecFound = Next(CDTDepDoc);
      end;
    end;
    KeyNum(CDTDepDoc,OldKey);
  end;

/* Перенос pc__calc */

  if ( TrnStatus )
    ClearRecord( CDTPcCalc );
    CDTPcCalc.Referenc = ALG_DEPOSITOR.Referenc;
    CDTPcCalc.ObjectType = 2001;
    RecFound = GetGE( CDTPcCalc );
    while ( RecFound and TrnStatus
      and ( CDTPcCalc.Referenc == ALG_DEPOSITOR.Referenc )
      and ( CDTPcCalc.ObjectType == 2001 ) )
      TrnStatus = Delete( CDTPcCalc );
      RecFound = Next( CDTPcCalc );
    end;
  end;
  if ( TrnStatus )
    ClearRecord( CDTPcCalc );
    CDTPcCalc.Referenc = ALG_DEPOSITOR.Referenc;
    CDTPcCalc.ObjectType = 2002;
    RecFound = GetGE( CDTPcCalc );
    while ( RecFound and TrnStatus
      and ( CDTPcCalc.Referenc == ALG_DEPOSITOR.Referenc )
      and ( CDTPcCalc.ObjectType == 2002 ) )
      CDTPcCalc.ObjectType = 2001;
      TrnStatus = Update( CDTPcCalc );
      ClearRecord( CDTPcCalc );
      CDTPcCalc.Referenc = ALG_DEPOSITOR.Referenc;
      CDTPcCalc.ObjectType = 2002;
      RecFound = GetGE( CDTPcCalc );
    end;
  end;
end;

macro UpdateDepRecord

  ALG_DEPOSITOR.Type_Account = NewType;
  ALG_DEPOSITOR.UseAlternate = 0;
  UpdateAlgRecords( UPDATE_DEP, true );
end;


/**************************************************************************\
|           Поиск последнего документа по накопленным процентам            |
\**************************************************************************/
macro GetLastPcEstim

  var stat;
  var find_last = FALSE;

  CDTpcestim.Referenc   = ALG_DEPOSITOR.Referenc;
  CDTpcestim.DateOperat = Date(31,12,9999);
  CDTpcestim.NumDayDoc  = 32000;
  stat = GetLE(CDTpcestim);
  while (stat)
    if (CDTpcestim.Referenc != ALG_DEPOSITOR.Referenc)
      stat = FALSE;
    else
      if (CDTpcestim.Action < 2)
        find_last = TRUE;
        stat = FALSE;
      else
        stat = Prev(CDTpcestim);
      end;
    end;
  end;

  return find_last;
  
end;


/**************************************************************************\
|                     Обновление статистики по вкладам                     |
\**************************************************************************/
macro UpdateStatistics

  record StatParm ("StatParm.rec");

  var SummaPay = $0L;

  if (GetLastPcEstim())
    SummaPay = CDTpcestim.SummaPay_Rest;
  end;

  if (SummaPay != 0)
    ClearRecord(StatParm);
    StatParm.Op           = Binsert;
    StatParm.KindOp       = D_OUT;
    StatParm.Sum          = ALG_DEPOSITOR.Sum_Rest;
    StatParm.TransfFlag   = StrFor(1);
    StatParm.Type_Account = OldType;
    StatParm.CodCur       = ALG_DEPOSITOR.Code_Currency;
    StatParm.Date         = curdat;
    StatParm.IsCur        = ALG_DEPOSITOR.IsCur;
    StatParm.FlagRez      = IsResident;
    StatParm.EstimOverOut = SummaPay;
    if (Index(ALG_DEPOSITOR.UserTypeAccount,"Н") > 0)
      StatParm.Branch     = NumRealFNCash();
    else
      StatParm.Branch     = ALG_DEPOSITOR.FNCash;
    end;
  
    if (TrnStatus)
      TrnStatus = UpdateDepStats(StatParm);
    end;

    if (TrnStatus)
      StatParm.KindOp       = D_IN;
      StatParm.Type_Account = NewType;
      StatParm.EstimOverOut = $0L;
      StatParm.EstimIn      = SummaPay;
      TrnStatus = UpdateDepStats(StatParm);
    end;
  end;

end;


macro MoveToMainState

  TrnStatus = not moveAccountIntoAnotherState(
    Перевод_В_Доп_Сост,
    ALG_DEPOSITOR.Referenc,
    curdat + 1,
    Date(1, 1, 9999),
    /* VZ 05.03.99 Под новый прототип */
    curdat, curdat );
end;

macro MoveToAltState

  TrnStatus = not moveAccountIntoAnotherState(
    Перевод_В_Альт_Сост,
    ALG_DEPOSITOR.Referenc,
    ALG_DEPOSITOR.Open_Date,
    Date(1, 1, 9999)
    );
end;

/*
  Головная процендура
*/
macro TrnProcedure;

  if(TrnStatus)
   MoveToAltState;
  end;
  OldType = ALG_DEPOSITOR.Type_Account;
  if(TrnStatus)
    UpdateDepRecord;
  end;
  if(TrnStatus)
    ChangeObjectType2002To2001;
  end;
  if(TrnStatus)
    if((StrFor(GetProgramID) != "П")
       or ((StrFor(GetProgramID) == "П") and {GroupProcRunning}))
      UpdateStatistics;
    end;
  end;
  if(TrnStatus)
/*
    MoveToMainState;
*/
    if((GetDepTypeIdentChars != "XX") /*and (StrFor(GetProgramID) != "П")*/)
      StrSet(ALG_DEPOSITOR.Account, 21, GetDepTypeIdentChars);
    end;
    UpdateAlgRecords( UPDATE_DEP, true );
    ChangeAccReg();
  end;
/*
  Восстановление значения в строке после вызова стандартной функции
  смены ObjectType
*/
/*
  if (TrnStatus)
    GetDirect (CDTDepDoc,OldPosDepDoc);
    CDTDepDoc.ObjectPerc = 2002;
    TrnStatus = Update (CDTDepDoc);
  end;
*/

end;
/***********************************************************
  Точка входа
***********************************************************/

    DocApplicationKind = CDT_SB_DEPOSIT;

  if( Open (CDTEdRef, "rsbedref.tmp") )
    ProcessTrn( 1, "TrnProcedure",
      CDTDep, CDTDepDoc);

    ALG_UPDATE = UPDATE_DOC;
  else
    TrnStatus = false;
  end;

  if(TrnStatus)
    ALG_RESULT = OK_RES;
  else
    ALG_RESULT = ERR_RES;
  end;

end;
