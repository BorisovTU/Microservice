/*
$Name:             ingot.mac
$Module:           RS-Retail
$Description:      Общие функции для работы со слитками драгметаллов
*/


import commonInter, cur_rate, rsd;

const
  IWT_LIGATURE = 0,                /* Лигатурная */
  IWT_PURE     = 1;                /* Химически чистая */   

private const
  TYPERES_MINCAPISSUE = 500,
  CI_INGOT = 5;

private const 
  R_CLOSEST    = 0,
  R_FLOOR      = 1,
  R_CEIL       = 2;
  
private var 
  {curdate};

private var
  rate = TBFile( "ing_rate.dbt", "r" ), 
  ciMisc = TBFile( "ci_misc.dbt", "r", 0 ), 
  ciCode = TBFile( "ci_code.dbt", "r", 0 ), 
  cashVal = TBFile( "sb_casvl.dbt", "r", 0 ); 

private macro findCiMisc( type, issue )

  ciMisc.rec.Type = type;
  ciMisc.rec.IssueID = issue;
  return ciMisc.getEQ;
end;

private macro findCashVal( ref )

  cashVal.rec.RefValue = ref;
  if ( not cashVal.getEQ )
    runError( "Не найден ресурс кассы" );
  end;
end; 

private macro findCiCode( ref )

  ciCode.rec.AutoKey = ref;
  if ( not ciCode.getEQ )
    runError( "Не найдена запись в файле ci_code.dbt" );
  end;
end;

private macro getHallmark( ref )

  findCiCode( ref );
  return ciCode.rec.Double;
end;

macro ing_getRubRate( issue, dd )

  var currCBrate;
  var cmd, rs;

  if ( not findCiMisc( CI_INGOT, issue ) )
    runError( "Не найден вид слитков" );
  end;

  cmd = RsdCommand("select t_code_currency as cc from dcurrency_dbt where T_MaterialRef = :MaterialRef");

  cmd.addParam( "MaterialRef", RSDBP_IN );

  cmd.value( "MaterialRef" ) = ciMisc.rec.MaterialRef;
  cmd.execute;
  
  rs = RsdRecordSet(cmd);
  if ( rs.MoveNext() )
      getAllCurrRate( dd, rs.value(0), currCBRate );
  else
    runError( "= Не найдена валюта для вида слитков " + ciMisc.rec.MaterialRef );
  end; 

  return currCBRate;
end;

private macro doRound( val, roundingType, roundingBase )

  if ( roundingType == R_FLOOR )
    return floor( val / roundingBase ) * roundingBase;
  elif ( roundingType == R_CLOSEST )
    return floor( val / roundingBase + 0.5 ) * roundingBase;
  end;
  return val;
end;

macro ing_roundWeight( val, roundingType, roundingBase )

  if ( roundingBase == 0 )
    roundingBase = 0.1;
  end; 

  val = doRound( val, R_CLOSEST, 0.00001 );
    return doRound( val, roundingType, roundingBase );
end;

private macro getWeightStr( w, roundingBase )

  if ( roundingBase == 0 )
    roundingBase = 0.1;
  end;

  if ( roundingBase == 1 )
    return String( w:0:0 );
  elif ( roundingBase == 0.1 ) 
    return String( w:0:1 );
  end;
  return String( w );
end;

class ( TRecHandler ) TIngotCashDoc( _appKind, _appKey, _valueRef )

  var
    cashDoc = TBFile( "sb_casdc.dbt", "r", 7 ), 
    bunch = TBFile( "bunch.dbt", "r", 0 ), 
    deskAux = TBFile( "desk_aux.dbt", "r", 0 ), 
    ingType = TRecHandler( "ci_misc.dbt" ), 
    appKind = _appKind, 
    appKey = _appKey, 
    valueRef = _valueRef,  
    totalWeight = 0, 
    totalPureWeight = 0;

  private macro round( sum )
      
    return ing_roundWeight( sum, ingType.rec.PWRoundType, ingType.rec.PWRoundBase );
  end;

  private macro findDeskAuxInfo

    deskAux.rec.Branch = rec.Branch;
    deskAux.rec.ValueRef = rec.RefValue;
    deskAux.rec.Date = rec.CashDateDoc;
    deskAux.rec.IsGone = 0;
    deskAux.rec.Series = bunch.rec.Series;
    deskAux.rec.Number = bunch.rec.Min;
    if ( not deskAux.GetEQ )
      deskAux.rec.IsGone =  1;
      if ( not deskAux.GetEQ )
        //runError( "Не найдена доп. инфрмация по слитку" );
        println( "*** Ingot.mac = Не найдена доп. инфрмация по слитку" );
      end;
    end;
  end;

  private macro calcTotalWeight

    var
      ii = TRecHandler( "desk_aux.1" ), 
      w, 
      hm = getHallmark( ingType.rec.HallmarkRef ) / 1000, 
      recFound;

    ii.setRecordAddr( deskAux, 0, deskAux.fldOffset( "Info" ), true );

    bunch.clear;
    bunch.rec.BagAppKind = rec.ApplicationKind;
    bunch.rec.BagAppKey = rec.ApplicationKey;
    recFound = bunch.getGE;
    while ( recFound 
      and ( bunch.rec.BagAppKind == rec.ApplicationKind )
      and ( bunch.rec.BagAppKey == rec.ApplicationKey ) )
      if ( bunch.rec.Min != bunch.rec.Max )
        runError( "Пачка из более чем одного слитка" );
      end;
      findDeskAuxInfo;
      w = ii.rec.RealWeight / 10000;
      totalWeight = totalWeight + w;
      totalPureWeight = totalPureWeight + round( w * hm );
      recFound = bunch.next; 
    end;
  end;

  private macro init

    cashDoc.rec.ApplicationKind = appKind;
    cashDoc.rec.ApplicationKey = appKey;
    cashDoc.rec.RefValue = valueRef;
    if ( not cashDoc.getEQ )
      // runError( "Не найден кассовый документ" ); 
      println( "*** Ingot.mac = Не найден кассовый документ:" );
      println( "***   ApplicationKind = ", appKind, 
               " | ApplicationKey = ", appKey, " | RefValue = ", valueRef );
    end;

    copy( this, cashDoc );

    findCashVal( rec.RefValue );           
    findCiMisc( cashVal.rec.TypeValue - TYPERES_MINCAPISSUE, cashVal.rec.CodIntValue );
    copy( ingType, ciMisc );
    calcTotalWeight;
  end;

  macro getRubRate( dd )

    if ( dd == null )
      dd = {curdate};
    end;

    return ing_getRubRate( ciMisc.rec.IssueID, dd );
  end;

  macro getRubValue( dd )

    var
      w,
      rubRate = getRubRate( dd );

    if ( ingType.rec.UseWeightType == IWT_PURE )
      w = totalPureWeight;
    else
      w = totalWeight;
    end;

    return money( rubRate *  w );
  end;

  macro getTotalWeight( accordingToUsedWeightType )

    if ( accordingToUsedWeightType == null )
      accordingToUsedWeightType = false;
    end; 

    if ( accordingToUsedWeightType 
      and ( ingType.rec.UseWeightType == IWT_PURE ) )
      return totalPureWeight;
    end;

    return totalWeight;
  end;

  macro getTotalPureWeight

    return totalPureWeight;
  end;

  macro weightStr( w )
      
    return getWeightStr( w, ingType.rec.PWRoundBase );
  end;

  macro getIngotIssue
      
    return ciMisc.rec.IssueID;
  end;

  InitTRecHandler( "sb_casdc.dbt" );
  init;
end;

class ( TRecHandler ) TIngotCashAcc( _valueRef, _subj, _date, _branch )

  var
    cashAcc = TBFile( "sb_casac.dbt", "r", 0 ), 
    regNum = TBFile( "reg_num.dbt", "r", 0 ), 
    deskAux = TBFile( "desk_aux.dbt", "r", 0 ), 
    ingType = TRecHandler( "ci_misc.dbt" ), 
    valueRef = _valueRef,  
    subj = _subj, 
    accDate = _date, 
    branch = _branch,
    totalWeight = 0,
    totalPureWeight = 0;

  private macro round( sum )
      
    return ing_roundWeight( sum, ingType.rec.PWRoundType, ingType.rec.PWRoundBase );
  end;

  private macro processDeskAuxInfo

    var
      ii = TRecHandler( "desk_aux.1" ), 
      w, 
      hm = getHallmark( ingType.rec.HallmarkRef ) / 1000, 
      recFound; 

    ii.setRecordAddr( deskAux, 0, deskAux.fldOffset( "Info" ), true );

    deskAux.clear;
    deskAux.rec.Branch = regNum.rec.Branch;
    deskAux.rec.ValueRef = regNum.rec.ValueRef;
    deskAux.rec.Date = regNum.rec.Date;
    deskAux.rec.IsGone = 0;
    deskAux.rec.Series = regNum.rec.Series;
    deskAux.rec.Number = regNum.rec.Min;
    recFound = deskAux.GetGE;
    while ( recFound 
      and ( deskAux.rec.Branch == regNum.rec.Branch )
      and ( deskAux.rec.ValueRef == regNum.rec.ValueRef )
      and ( deskAux.rec.Date == regNum.rec.Date )
      and ( deskAux.rec.IsGone == 0 )
      and ( deskAux.rec.Series == regNum.rec.Series )
      and ( deskAux.rec.Number <= regNum.rec.Max ) )
      w = ii.rec.RealWeight / 10000;
      totalWeight = totalWeight + w;
      totalPureWeight = totalPureWeight + round( w * hm );
      recFound = deskAux.next;
    end;
  end;

  private macro calcTotalWeight

    var
      recFound;

    regNum.clear;
    regNum.rec.Branch = rec.Branch;
    regNum.rec.CashOper = rec.CashOper;
    regNum.rec.ValueRef = rec.RefValue;
    regNum.rec.Date = rec.Date;
    recFound = regNum.getGE;
    while ( recFound 
      and ( regNum.rec.Branch == rec.Branch )
      and ( regNum.rec.CashOper == rec.CashOper )
      and ( regNum.rec.SubAccount == "" )
      and ( regNum.rec.ValueRef == rec.RefValue )
      and ( regNum.rec.Date == rec.Date ) )
      processDeskAuxInfo;
      recFound = regNum.next; 
    end;
  end;

  private macro init

    cashAcc.rec.Branch = branch;
    cashAcc.rec.Date = accDate;
    cashAcc.rec.CashOper = subj;
    cashAcc.rec.SubAccount = "";
    cashAcc.rec.RefValue = valueRef;
    if ( not cashAcc.getEQ )
      cashAcc.clear;
      cashAcc.rec.Branch = branch;
      cashAcc.rec.Date = accDate;
      cashAcc.rec.CashOper = subj;
      cashAcc.rec.RefValue = valueRef;
    end;

    copy( this, cashAcc );

    findCashVal( rec.RefValue );           
    findCiMisc( cashVal.rec.TypeValue - TYPERES_MINCAPISSUE, cashVal.rec.CodIntValue );
    copy( ingType, ciMisc );
    calcTotalWeight;
  end;

  macro getRubRate( dd )

    if ( dd == null )
      dd = accDate;
    end;

    return ing_getRubRate( ciMisc.rec.IssueID, dd );
  end;

  macro getRubValue( dd )

    var
      w,
      rubRate = getRubRate( dd );

    if ( ingType.rec.UseWeightType == IWT_PURE )
      w = totalPureWeight;
    else
      w = totalWeight;
    end;

    return money( rubRate * w );
  end;

  macro getTotalWeight( accordingToUsedWeightType )

    if ( accordingToUsedWeightType == null )
      accordingToUsedWeightType = false;
    end; 

    if ( accordingToUsedWeightType 
      and ( ingType.rec.UseWeightType == IWT_PURE ) )
      return totalPureWeight;
    end;

    return totalWeight;
  end;

  macro getTotalPureWeight

    return totalPureWeight;
  end;

  macro weightStr( w )
      
    return getWeightStr( w, ingType.rec.PWRoundBase );
  end;

  macro getIngotIssue
      
    return ciMisc.rec.IssueID;
  end;

  if ( accDate == null )
    accDate = {curdate};
  end;

  if ( branch == null )
    branch = numFNCash;
  end;

  InitTRecHandler( "sb_casac.dbt" );
  init;
end;

