/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Обслуживание физических лиц                                             *
*                                                                          *
*  Проверка международных переводов по счетам ЛОРО                         *
*                                                                          *
*  Лебедев С.В.                                                            *
*  01.06.2005                                                              *
\**************************************************************************/

import alg_vars, alg_pay, cur_rate, BankInter, CTInter, rsd;

private file currency( currency ) key 0;

private var partcode = Tbfile( "partcode.dbt", "r", 1 );
private var party    = Tbfile( "party.dbt", "r", 0 );
private var fininstr = Tbfile( "fininstr.dbt", "r", 12 );

private var AttrBuff = TRecHandler("objattr.dbt");

private var hour;
private var mrot;
private var countMrot;
private var errCode;
private var limOper;
private var cbRate1;
private var cbRate2;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindBank ( code )

  var stat;

  /* Ищем по БИК */
  partcode.Clear;
  partcode.rec.CodeKind = 3;
  partcode.rec.Code     = code;
  stat = partcode.GetEQ;

  /* Не нашли по БИК, ищем по SWIFT */
  if ( not stat )
    partcode.Clear;
    partcode.rec.CodeKind = 6;
    partcode.rec.Code     = code;
    stat = partcode.GetEQ;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro FindParty ( id )

  party.Clear;
  party.rec.PartyID = id;

  return party.GetEQ;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindFininstr ( codCurr )

  var stat;

  ClearRecord( currency );
  currency.Code_Currency = codCurr;

  stat = GetEQ( currency );

  if ( stat )
    if ( Trim( currency.ExternalCode ) == "" )
      stat = false;
    else
      fininstr.Clear;
      fininstr.rec.ISO_Number = Int( Trim( currency.ExternalCode ) );

      stat = fininstr.GetEQ;
    end;
  end;

  return stat;

end;


private macro AllowInstPayment( CodeCurr )
  var flag = false;
  var groupID = 35;
  var objectID = string(party.rec.PartyID);

  while( strlen(objectID) < 10 )
    objectID = "0" + objectID;
  end;

  if( CodeCurr != 0 )
    groupID = 36;
  end;

  if( not ObjAttr_FindFirst( AttrBuff, 3, objectID, groupID ) )
    if( AttrBuff.rec.Name == "Да" )
      flag = true;
    end;
  end;
  ObjAttr_FindClose();

  return flag;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro checkCorAccountForUrgent( inBIK, codCurr )

  var stat = true;
  var flag;

  if ( stat )
    if ( StrLen( Trim( inBIK ) ) == 0 )
      MsgBox( "Не задан БИК/SWIFT|Перевод по счетам ЛОРО невозможен" );
      stat = false;
    end;
  end;

  if ( stat )
    stat = FindBank( inBIK );

    if ( stat )
      stat = FindParty( partcode.rec.PartyID );
    end;

    if ( not stat )
      MsgBox( "Не найден банк по его БИК/SWIFT|Перевод по счетам ЛОРО невозможен" );
    end;
  end;

  if ( stat )
    stat = FindFininstr( codCurr );

    if ( not stat )
      MsgBox( "Не определена валюта в справочнике|Перевод по счетам ЛОРО невозможен" );
    end;
  end;

  if ( stat )
    stat = AllowInstPayment(codCurr);

    if ( not stat )
      MsgBox( "Для банка не задан признак разрешения срочных платежей|Перевод по счетам ЛОРО невозможен" );
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  TimeSplit( Time( ), hour );

  OC_RESULT = OK_RES;

  /* Проверка допустимой даты и времени перевода */
  if ( IsHoliday( {curdate} ) or ( IsHoliday( {curdate} + 1 ) and ( hour > 12 ) ) )
    if ( not GetTrue( false, "Время перевода срочного перевода истекло. Продолжить?" ) )
      OC_RESULT = ERR_RES;
      return;
    end;
  end;

  /* Проверка корсчета */
  if ( not checkCorAccountForUrgent( OC_PAY_DOC_PMNT_PROP.BankCode, OC_PAY_DOC.CodCur ) )
    OC_RESULT = ERR_RES;
    return;
  end;

  /* Проверка задания ФИО */
  if ( StrLen( Trim( OC_PAY_DOC.ClientLastName ) ) == 0 )
    MsgBox( "Не задана фамилия клиента" );
    OC_RESULT = ERR_RES;
    return;
  end;
  
  /* Проверка суммы перевода для рублевых операций */
  if ( OC_PAY_DOC.CodCur == 0 ) 
    if ( GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\ЛИМИТЫ\\МРОТ_УЧЕТНЫЙ", V_DOUBLE, mrot, errCode ) == V_DOUBLE )
      ;
    else
      OC_RESULT = ERR_RES;
      return;
    end;

    if ( GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\ЛИМИТЫ\\СРОЧНЫЕ_МЕЖДУНАРОДНЫЕ_ПЕРЕВОДЫ\\ЛИМИТ_В_МРОТ", V_INTEGER, countMrot, errCode ) == V_INTEGER )
      ;
    else
      OC_RESULT = ERR_RES;
      return;
    end;

    limOper = MoneyL( mrot ) * countMrot;

    if ( OC_PAY_DOC.InSum > limOper )
      MsgBox( "Сумма перевода превышает допустимое значение" );
      OC_RESULT = ERR_RES;
      return;
    end;
  end;

  /* Проверка суммы перевода для валютных операций */
  if ( ( OC_PAY_DOC.CodCur > 0 ) and ( OC_PAY_DOC.FlagRez == "" ) )
    if ( GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\ЛИМИТЫ\\СРОЧНЫЕ_МЕЖДУНАРОДНЫЕ_ПЕРЕВОДЫ\\МАКС_СУММА_В_USD", V_DOUBLE, mrot, errCode ) == V_DOUBLE )
      ;
    else
      OC_RESULT = ERR_RES;
      return;
    end;

    limOper = MoneyL( mrot );

    if ( OC_PAY_DOC.CodCur > 1 )
      if ( not GetAllCurrRate( OC_PAY_DOC.Date_Document, 1, cbRate1 ) )
        MsgBox( "Ошибка при определении кросс-курса валюты" );
        OC_RESULT = ERR_RES;
        return;
      end;

      if ( not GetAllCurrRate( OC_PAY_DOC.Date_Document, OC_PAY_DOC.CodCur, cbRate2 ) )
        MsgBox( "Ошибка при определении кросс-курса валюты" );
        OC_RESULT = ERR_RES;
        return;
      end;

      limOper = Money( limOper * cbRate1 / cbRate2 );
    end;

    if ( OC_PAY_DOC.InSum > limOper )
      MsgBox( "Сумма перевода превышает допустимое значение" );
      OC_RESULT = ERR_RES;
      return;
    end;
  end;

end;
