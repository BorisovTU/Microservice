/*
$Name:               rpctx6_s.mac
$Module:             DEPOSITR
$Description:        Формирование 6-НДФЛ
*/

Import DeprIntr, rPcTax1c, or_tpl_h;

private var Font_Size = 14;

private var {Oper};
private var {CurDate};

private var opr = TBFile ( "person.dbt", "r", 0 );

private var T_PcTax = TBFile( "pc_tax.dbt", "r", 5 );

private var PcTax2 = TRecHandler( "pc_tax2" );

private FILE fout () txt;
private var rep;

private var sOper = "";

private var gTaxClients = 0;
private var gCommonTaxPayGet   = $0.0;
private var gCommonTaxPayNoGet = $0.0;
private var gCommonTaxPayPut   = $0.0;

// Массивы итоговых сумм по всем клиентам
private var aRate    = TArray;
private var aIncome  = TArray;
private var aTaxCalc = TArray;

//
//  Формат даты вида "03.05.2015"
//
private macro Date0( _Date )
  var d, m, y;
  DateSplit( _Date, d, m, y );
  return SubStr( String(d+100), 2 ) + "." + SubStr( String(m+100), 2 ) + "." + String(y);
end;

//
// Последний день месяца
//
private macro LastDayMon( _Date )
  var d, m, y;
  var retDate;
  DateSplit( _Date, d, m, y );
  if ( m > 11 )
    retDate = Date( 31, 12, y );
  else
    retDate = Date( 1, m+1, y ) - 1;
  end;
  return retDate;
end;

//
//  Добавить ставку
//
private macro arrRateAdd( _Rate, _Income, _TaxCalc )
  var i = 0;
  var Find = FALSE;
  var n = aRate.Size();

  while ( i < n )
    if ( aRate[i] == _Rate )
      aIncome[i]  = aIncome[i]  + _Income;
      aTaxCalc[i] = aTaxCalc[i] + _TaxCalc;
      Find = TRUE;
      i = n;
    else
      i = i + 1;
    end;
  end;

  if ( NOT Find )
    aRate[i] = _Rate;
    aIncome[i]  = _Income;
    aTaxCalc[i] = _TaxCalc;
  end;
end;

//
//  Раздел по отдельной ставке.
//
private macro Block_Rate( _Rate, _Income, _TaxCalc )

[Ставка налога, %                                                                         ];
[010 #######                                                                              ]
( Int( _Rate ):l );
[                                                                                         ];
[    Сумма начисленного дохода                 В том числе сумма начисленного             ];
[                                              дохода в виде дивидендов                   ];
[020 ##############################        025 0.00                                       ]
( _Income:18:2:a:l );
[                                                                                         ];
[    Сумма налоговых вычетов                   Сумма исчисленного налога                  ];
[030 0.00                                  040 #########################################  ]
( ( RoundToRuble( _TaxCalc ) ):15:0:a:l );
[                                                                                         ];
[    В том числе сумма исчисленного налога     Сумма фиксированного авансового            ];
[    на доходы в виде дивидендов               платежа                                    ];
[045 0.00                                  050 0.00                                       ];
[                                                                                         ];
[                                                                                         ];

end;

//
//  Итого по всем ставкам.
//
private macro Block_Total()

[Итого по всем ставкам                                                                    ];
[                                                                                         ];
[    Количество физических лиц, получивших     Сумма удержанного налога                   ];
[    доход                                                                                ];
[060 ###################################   070 #########################################  ]
( gTaxClients:l, ( RoundToRuble( gCommonTaxPayGet ) ):15:0:a:l );
[                                                                                         ];
[    Сумма налога, не удержанная налоговым     Сумма налога, возвращенная                 ];
[    агентом                                   налоговым агентом                          ];
[080 ###################################   090 #########################################  ]
( ( RoundToRuble( gCommonTaxPayNoGet ) ):15:0:a:l, ( RoundToRuble( gCommonTaxPayPut ) ):15:0:a:l );
[                                                                                         ];

end;

//
//  Дерево подчиненных подразделений данного филиала
//
private macro DepTree()
  var cmd, rs;
  var vNext = FALSE;
  var retTree = "(";

  cmd = RsdCommand( "select t_code as fnc from ddp_dep_dbt start with t_code=? connect by t_parentcode = prior t_code" );


  cmd.addParam( "brn", RSDBP_IN );
  cmd.value( "brn" ) = InputPanel_Branch;

  cmd.execute;

  rs = RsdRecordset( cmd );

  while ( rs.moveNext )
    if ( vNext )
      retTree = retTree + ",";
    end;
    retTree = retTree + String( rs.value( "fnc" ) );
    vNext = TRUE;
  end;

  retTree = retTree + ")";

  return retTree;
end;

//
//  Даты и суммы фактически полученных
//  доходов и удержанного налога на доходы физических лиц
//
private macro DatesSumsTax()
  var cmd, rs;
  var sTree = "";
  var DateCur = Date( 01, 01, 1900 );
  var SumCur = 0.0;
  var SumInc = 0.0;
  var SumT   = 0.0;

  if ( InputPanel_Branch < 0 )
    cmd = RsdCommand( "select T.T_DATETAXPAY as datep, T.T_SUMMATAXPAYRUR as sump, T.T_APPLICATIONKIND as appi, T.T_APPLICATIONKEY as appk " +
                        "from dpc_tax_dbt t " +
                          "where T.T_DATETAXPAY > ? and " +
                                "T.T_DATETAXPAY < ? " +
                                  "order by T.T_DATETAXPAY" );
  else
    sTree = DepTree();
    cmd = RsdCommand( "select T.T_DATETAXPAY as datep, T.T_SUMMATAXPAYRUR as sump, T.T_APPLICATIONKIND as appi, T.T_APPLICATIONKEY as appk " +
                        "from dpc_tax_dbt t " +
                          "where T.T_DATETAXPAY > ? and " +
                                "T.T_DATETAXPAY < ? and " +
                                "T.T_FNCASH in " + sTree + " " +
                                  "order by T.T_DATETAXPAY" );
  end;

  cmd.addParam( "date1", RSDBP_IN );
  cmd.addParam( "date2", RSDBP_IN );
  cmd.value( "date1" ) = BeginDate2;
  cmd.value( "date2" ) = EndDate;

  cmd.execute;

  rs = RsdRecordset( cmd );

  while ( rs.moveNext )
    T_PcTax.Clear();
    T_PcTax.rec.APPLICATIONKIND = rs.value( "appi" );
    T_PcTax.rec.APPLICATIONKEY  = rs.value( "appk" );
    if ( T_PcTax.GetEQ() )
      SetRecordAddr( PcTax2, T_PcTax, 0, 0, True );
      SumT = PcTax2.rec.IncomeRUR;
    end;

    if ( DateCur == rs.value( "datep" ) )
      SumCur = SumCur + rs.value( "sump" );
      SumInc = SumInc + SumT;
    else

if ( DateCur != Date( 01, 01, 1900 ) )
[                                                                                         ];
[100 #########################       130 ###############################################  ]
( Date0( Date( DateCur ) ), SumInc:18:2:a:l );
[                                                                                         ];
[110 #########################       140 ###############################################  ]
( Date0( Date( DateCur ) ), SumCur:15:0:a:l );
[                                                                                         ];
[120 #########################                                                            ]
( Date0( LastDayMon( Date( DateCur ) ) ) );
end;

      DateCur = rs.value( "datep" );
      SumCur  = rs.value( "sump" );
      SumInc = SumT;
    end;

  end;

[                                                                                         ];
[100 #########################       130 ###############################################  ]
( Date0( Date( DateCur ) ), SumInc:18:2:a:l );
[                                                                                         ];
[110 #########################       140 ###############################################  ]
( Date0( Date( DateCur ) ), SumCur:15:0:a:l );
[                                                                                         ];
[120 #########################                                                            ]
( Date0( LastDayMon( Date( DateCur ) ) ) );

end;

//
//
//
private macro Report6_Form()

  var vBankINN = "", vBankKPP = "", vIndKPP = 0;

  vIndKPP = Index( Bank_INN, "/" );

  if ( vIndKPP == 0 )
    vBankINN = Bank_INN;
  else
    vBankINN = SubStr( Bank_INN, 1, vIndKPP-1 );
    vBankKPP = SubStr( Bank_INN, vIndKPP+1 );
  end;

[                  ИНН ############                                                       ]
( vBankINN );
[                                                                                         ];
[                  КПП #########                                                          ]
( vBankKPP );
[                                                                                         ];
[ Форма по КНД 1151099                                                                    ];
[                                                                                         ];
[Расчет сумм налога на доходы физических лиц, исчисленных и удержанных налоговым агентом  ];
[                                   (форма 6-НДФЛ)                                        ];
[                                                                                         ];
[Номер корректировки ###       Период представления         ##        Налоговый    ####   ]
( SubStr( String( NF6_Correct + 1000 ), 2, 3 ), NF6_Period, NF6_Year );
[                              (код)                                  период (год)        ];
[                                                                                         ];
[Представляется в налоговый орган (код) ####       По месту нахождения (учета) (код) ###  ]
( CodeTaxAgency, NF6_Place ); 
[                                                                                         ];
[######################################################################################## ]
( Name_Bank:w );
[                                   (налоговый агент)                                     ];
[                                                                                         ];
[                    ---                           --------------------- -------------------];
[Форма реорганизации | |  ИНН/КПП реорганизованной | | | | | | | | | | |/| | | | | | | | | |];
[(ликвидации) (код)  ---               организации --------------------- -------------------];
[                                                                                         ];
[                                                                                         ];
[Код по ОКТМО               ###############                                               ]
( CodeOKATO );
[                                                                                         ];
[Номер контактного телефона ############################################################# ]
( Bank_Phone );
[                                                                                         ];
[                                                                                         ];
[На  003  страницах с приложением подтверждающих документов или их копий на   000  листах ];
[                                                                                         ];
[-----------------------------------------------------------------------------------------];
[     Достоверность и полноту сведений,     |   Заполняется работником налогового органа  ];
[указанных в настоящем расчете, подтверждаю:|       Сведения о представлении расчета      ];
[                                           |                                             ];
[    1                                      |                                             ];
[      1 - налоговый агент, правопреемник   |                                  -----      ];
[          налогового агента                | Данный расчет представлен (код)  | | |      ];
[      2 - представитель налогового агента  |                                  -----      ];
[                                           |                                             ];
[                                           |                                             ];
[                                           |    -------                                  ];
[                                           | на | | | | страницах                        ];
[                                           |    -------                                  ];
[########################################## |                                             ]
( sOper:w );
[    (фамилия, имя, отчество полностью)     |с приложением подтверждающих                 ];
[                                           |                           -------           ];
[                                           |документов или их копий на | | | | листах    ];
[                                           |                           -------           ];
[                                           |                                             ];
[                                           |Дата представления ----- ----- ---------     ];
[(наименование организации - представителя  | расчета           | | |.| | |.| | | | |     ];
[     налогового агента)                    |                   ----- ----- ---------     ];
[                                           |                                             ];
[Подпись _____________ Дата ############### |                ---------------------------  ]
( {curdate} );
[                                           |Зарегистрирован | | | | | | | | | | | | | |  ];
[    Наименование и реквизиты документа,    |за N            ---------------------------  ];
[подтверждающего полномочия представителя   |                                             ];
[налогового агента                          |                                             ];
[                                           |                                             ];
[                                           |                                             ];
[                                           |_____________________   _______________      ];
[                                           |  Фамилия, И.О.            Подпись           ];
[                                                                                         ];
[                                                                                         ];
[             Достоверность и полноту сведений, указанных на данной                       ];
[                               странице, подтверждаю:                                    ];
[                                                                                         ];
[               ______________ (подпись)   __________________ (дата)                      ];
[                                                                                         ];
[                                                                                         ];

  printLn( strFor( 12 ) );  // Переход на новую страницу

//  Страница 2.

[                                                                                         ];
[                  ИНН ############                                                       ]
( vBankINN );
[                                                                                         ];
[                  КПП ###########  Стр. 002                                              ]
( vBankKPP );
[                                                                                         ];
[                         Раздел 1. Обобщенные показатели                                 ];
[                                                                                         ];

var ii = 0;
var nn = aRate.Size();

  if ( nn > 0 )
    if ( nn == 1 )
      Block_Rate( aRate[0], aIncome[0], aTaxCalc[0] );
      Block_Total();
    else
      ii = 0;
      while ( ii < nn )
        Block_Rate( aRate[ii], aIncome[ii], aTaxCalc[ii] );
        ii = ii + 1;
      end;
      Block_Total();
    end;
  end;

[                                                                                         ];
[                                                                                         ];
[                 Достоверность и полноту сведений, указанных на данной                   ];
[                             странице, подтверждаю:                                      ];
[                                                                                         ];
[              ______________ (подпись)   __________________ (дата)                       ];
[                                                                                         ];

  printLn( strFor( 12 ) );  // Переход на новую страницу

//  Страница 3.

[                                                                                         ];
[                  ИНН ############                                                       ]
( vBankINN );
[                                                                                         ];
[                  КПП ###########  Стр. 003                                              ]
( vBankKPP );
[                                                                                         ];
[                                                                                         ];
[Раздел 2. Даты и суммы фактически полученных                                             ];
[          доходов и удержанного налога на доходы физических лиц                          ];
[                                                                                         ];
[Дата фактического получения дохода/     Сумма фактически полученного дохода/             ];
[Дата удержания налога/                  Сумма удержанного налога                         ];
[Срок перечисления налога                                                                 ];
[                                                                                         ];

  DatesSumsTax();

[                                                                                         ];
[                                                                                         ];
[            Достоверность и полноту сведений, указанных на данной                        ];
[                              странице, подтверждаю:                                     ];
[                                                                                         ];
[              ______________ (подпись)   __________________ (дата)                       ];
[                                                                                         ];
                                                                                          
end;

//
//  Добавить ставки по клиенту в массив ставок
//
private macro addArrayRate()

  var stat;

  mPcRate.KeyNum = 0;
  mPcRate.Clear();

  stat = mPcRate.GetGE();

  if ( stat )  // У данного клиента был доход
    gTaxClients = gTaxClients + 1;
  end;

  while ( stat )

    if ( mPcRate.rec.CommonTaxPay > $0.0 )
      gCommonTaxPayGet   = gCommonTaxPayGet   + mPcRate.rec.CommonTaxPay;
      gCommonTaxPayNoGet = gCommonTaxPayNoGet + mPcRate.rec.CommonTaxCalc - mPcRate.rec.CommonTaxPay;
    else
      gCommonTaxPayPut = gCommonTaxPayPut - mPcRate.rec.CommonTaxPay;
    end;

    if ( mPcRate.rec.TaxRate > 0 )
      arrRateAdd( mPcRate.rec.TaxRate, mPcRate.rec.CommonIncome, mPcRate.rec.CommonTaxCalc );
    end;

    stat = mPcRate.Next();
  end;

end;

//
//
//
private macro Выпуск_справки_о_доходах6()

  var stat = ERR_OK;
  var NumCycle = 0;

  while ( ( stat == ERR_OK )  OR  ( stat == ERR_SKIP ) )
    NumCycle = NumCycle + 1;
    stat = Выбрать_Данные_По_Очередному_клиенту_6( NumCycle );
    if ( stat == ERR_OK )
      addArrayRate();
    end;
  end;

  Report6_Form( 1 );

  return TRUE;
end;

// 
//  Головной макрос
//
private macro rpc_MakeIncomeInform6
  var stat = False;
  var flagRep = True;
  var Num_Lines = 27;
  var numLine = 0;
  var numEmpty = 0;
  var str = "";

  var nr = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", false ) + "\\rpctx6." + UserNumber();

  /* Чтение записи Операциониста */
  opr.Clear();
  opr.rec.Oper = {Oper};
  if ( opr.GetEQ() )
    sOper = opr.rec.Name;
  else
    sOper = "???";
  end;

  TheOnlySole = FALSE;  // По всем клиентам.

  SetOutput( nr, false );

  while ( NOT stat )
    if ( Ввод_исходных_данных6( "в печатном виде" ) )
      Bank_Attr_NDFL();
      stat = Выпуск_справки_о_доходах6();
    else
      MsgBox( "Отказались от выпуска справки о доходах" );
      stat = True;
      flagRep = False;
    end;
  end;

  if ( flagRep )
    rep = CMakeReport( "", SEP_DEFAULT, 99999 );

    rep.SetFileNameWin( "rpctx6" );

    rep.SetWinRepOutPut( WINREP_OUTPUT_WORD, WINREP_FORMAT_DOC );
    rep.SetExecuteWord( "WordDocument.PageSetup.LeftMargin = 20;" );
    rep.SetExecuteWord( "WordDocument.PageSetup.TopMargin  = 20;" );
    rep.SetZoomType( ZOOM_TYPE_A4L );  // альбомная ориентация

    if ( Font_Size < 8 )
      Font_Size = 8;
    end;
    if ( ( Font_Size > 14 )  OR  ( Font_Size == 13 ) )
      Font_Size = 14;
    end;

    rep.SetFont( "Courier New", Font_Size );

    SetOutput( null, false );

    if ( NOT Open( fout, nr ) )
      println( "Ошибка открытия файла " + nr );
      Exit();
    end;

    // Количество строк на страницу для различных размеров фонтов
    if   ( Font_Size ==  8 )
      Num_Lines = 44;
    elif ( Font_Size ==  9 )
      Num_Lines = 40;
    elif ( Font_Size == 10 )
      Num_Lines = 35;
    elif ( Font_Size == 11 )
      Num_Lines = 34;
    elif ( Font_Size == 12 )
      Num_Lines = 31;
    elif ( Font_Size == 14 )
      Num_Lines = 27;
    end;

    while ( Next( fout ) )

      str = fout.str();
      if ( SubStr( str, 1, 1 ) == strFor( 12 ) )

        numEmpty = Num_Lines - numLine;

        if ( ( numEmpty + 2 ) >= Num_Lines )
          numEmpty = 1;
        end;

        if ( numEmpty > 0 )
          rep.AddEmptyStr( numEmpty + 2 );
          numLine = 0;
        end;

      else

        numLine = numLine + 1;
        if ( numLine >= Num_Lines )
          numLine = 1;
        end;

        rep.AddPrintCell( str, 1, 0, "l:", REP_ELEM_STR );
        rep.AddStr();
      end;
    end;

    rep.PrintWinRep();
    rep.ShowWinRep();

    Close( fout );
    DelFile( nr );
  end;

end;

/*************************************************************************
                             Головная процедура.
**************************************************************************/

rpc_MakeIncomeInform6();

/* ----- Конец файла ----- */
