import vector;
import CommonInter;
import execmacro3;

//---------------------------------------------------------------------------
/*!
 * Для функций типа V_PROC и V_STRING возвращает имя
 */
macro getFuncName( fn )
  if( valtype(fn) == V_PROC )
    return strupr(GetProcName(fn));
  elif( valtype(fn) == V_STRING ) 
    return strupr(fn);
  else
    RunError("Неверный тип");
  end;
end;
//---------------------------------------------------------------------------
/*!
 * Проверка что параметр массив
 */
macro isArray( v )
  return ((valtype(v) == V_GENOBJ) and (isEqClass(v, "vector") or isEqClass(v, "tarray")));
end;
//---------------------------------------------------------------------------
/*!
 * Проверяет, что тип функции V_PROC или V_STRING
 */
private macro isProcOrString( fn )
  return (valtype(fn) == V_PROC) or (valtype(fn)  == V_STRING);
end;
//---------------------------------------------------------------------------
macro CompByFuncName( a, b )
  if( isProcOrString( a.fn ) and isProcOrString( b.fn ) )
    return IIF((getFuncName(a.fn) == getFuncName(b.fn)), 0, 1);
    
  elif( (valtype(a.fn) == valtype(b.fn)) == V_R2M )
    return IIF((a.fn == b.fn), 0, 1);
    
  else
    RunError("Неверный тип функтора");
  end;
end;
//---------------------------------------------------------------------------
/*!
 * Класс позволяет сделать отложенный вызов функции вместе с параметрами
 * var f = Functor("myFunc", makeParams(2,3,4,6,"") );
 * var f = Functor(@myFunc, makeParams(2,3,4,6,"") );
 * var f = Functor(R2M(obj,"myFunc"), makeParams(2,3,4,6,"") );
 * также можно задать параметры функции при вызове
 * var f = Functor("myFunc");
 * f.exec( makeParams(2,3,4,6,"") );
 * или f.exec(2,3,4,6,"");
 */
class Functor( _fn, _parms )
  private var m_fn = _fn;
  private var m_parms = _parms;
  //-----------------------------
  // Возвращает хранимую функцию
  macro fn()
    return m_fn;
  end;
  //-----------------------------
  // Возвращает массив параметров
  macro parms()
    return m_parms;
  end;
  //-----------------------------
  // Исполняет функтор
  macro exec( parms )
    var pv, i, v;
    
    if( parms != null )
      if( isArray(parms) )
        m_parms = parms;
      else
        i = 1;
        pv = vector();
        while( getparm(i, v) )
          pv.push_back(v);
          inc(i);       
        end;
      end;
    end;
    return ExecMacro3(m_fn, m_parms);
  end;
end;
//---------------------------------------------------------------------------
/*!
 * подходит ли для вызова ExecMacro?
 */
macro isCallableType( fn )
  return ((valtype(fn) == V_PROC) or 
          (valtype(fn) == V_R2M) or 
          ( valtype(fn) == V_STRING ));
end;
//---------------------------------------------------------------------------
/*!
 * Из списка параметров создаёт массив
 * ex: var parms = makeParams(2, obj, 4.7);
 */
macro makeParams()
  var parms = vector();
  var i = 0, v = null;
  while( getparm(i, v) )
    parms.push_back(v);
    inc(i);       
  end;
  return parms;
end;
//---------------------------------------------------------------------------

