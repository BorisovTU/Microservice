/*
 $Name: ic_deposit.mac
 $Module: UFO
 $Description: сервисы AccountOper, DepositOper
*/

import rsd, DeprIntr,cb_sql;

private const V_SPECVAL = 26;
private const ShablonAccType = "Шаблонный";



/**************************************************************************\
|                                                                          |
\**************************************************************************/
class OpInfo
  var NumOper : integer;
  var NameOper : string;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class AccOpInfo
  var AccountID : integer;
  var BranchID : integer;
  var AccNumber : string;
  var Operations : Tarray;
 end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro convertNullStr ( val )

  if ( ( ValType( val ) == V_SPECVAL ) or ( ValType( val ) == V_UNDEF ) ) 
    return "";
  else
    if ( val == StrFor( 1 ) )
      return "";
    else
      return val;
    end;
  end;
end;  


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro convertNullDate ( val )

  if ( ( ValType( val ) == V_DATE ) or ( ValType( val ) == V_DTTM ) )
    return val;
  else
    return Date( 0, 0, 0 );
  end;
end;  


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro convertNullInt ( val )

  if ( ( ValType( val ) == V_INTEGER ) or ( ValType( val ) == V_DOUBLE ) or ( ValType( val ) == V_DOUBLEL ) or ( ValType( val ) == V_MONEY ) or ( ValType( val ) == V_MONEYL ) )
    return val;
  else
    return 0;
  end;
end;  


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro ErrMessage ( errCode )

  var errMsg = "";  
  var cmd = RsdCommand( "select t_Contents from dsbbank_msg where t_Number = ? " );

  cmd.addParam( "p_number", RSDBP_IN, errCode );

  cmd.execute( );

  var rs = RsdRecordset( cmd );

  if ( rs.moveNext( ) )
    errMsg = convertNullStr( rs.value( 0 ) );
  end;

  if ( errMsg == "" )
    errMsg = String( errCode );
  end;

  return errMsg;
end;

/**************************************************************************\
|                    УДАЛЕНИЕ ВСЕХ ПРОБЕЛОВ                                |
\**************************************************************************/
macro DelAllSpaces( inString : String ) : String
  var outString = "";
  var nextChar = "";
  const SPACE_CHAR = " ";

  var i = 1;
  var inLen = strlen(inString);
  while( i <= inLen )
    nextChar = SubStr( inString, i, 1 );

    if( nextChar != SPACE_CHAR ) 
        outString = outString + nextChar;
    end;
    i = i + 1;
  end;

  return outString;
end;

/**************************************************************************\
|                    УДАЛЕНИЕ ВСЕХ ПОВТОРОВ                                |
\**************************************************************************/
macro DelAllRepeat( inString : String ) : String
  var outString = "";

  var i = 1;
  var inLen = strlen(inString);
  for (i, 1, inLen)
    if ( StrBrk(SubStr(inString,i,1), outString) == 0 )
      outString = outString + SubStr(inString,i,1);
    end;
  end;

  return outString;
end;


/********************************************************************************\
|                  ОПРЕДЕЛЕНИЕ ОПЕРАЦИЙ ПО ВИДУ ВКЛАДА                           |
\********************************************************************************/
macro GetTypeOper( Kind , CurCode , OperType , InteractType , OperCodes  )
  
  if ( ((ValType(Kind) != V_STRING) or (Kind == "")) or 
       ((ValType(CurCode) != V_INTEGER) or (CurCode < 0)) ) 
    RunError( ErrMessage( 16504 ), 16504 );
  end;
  if ( ( (ValType(OperType) != V_INTEGER) and (ValType(OperType) != V_UNDEF) ) or 
       ( (ValType(OperType) == V_INTEGER) and ((OperType < 0) or (OperType > 2)) ) )
    RunError( ErrMessage( 16517 ), 16517 );
  end;
  if ( ( (ValType(InteractType) != V_INTEGER) and (ValType(InteractType) != V_UNDEF) ) or 
       ( (ValType(InteractType) == V_INTEGER) and ((InteractType < 0) or (InteractType > 2)) ) )
    RunError( ErrMessage( 16518 ), 16518 );
  end;
  if ( (ValType(OperCodes) != V_STRING) and (ValType(OperCodes) != V_UNDEF) )
    RunError( ErrMessage( 16519 ), 16519 );
  end;

  var retVal = TArray;
  var RealKind = Kind;
  
//определяем валютность
  var currency;

  if (CurCode > 0)
    currency = 1;
  elif (CurCode == 0)
    currency = 0;
  end;   

//поиск вида вклада
  var findTypeAcc = rsdcommand("select * from dsb_dtyp_dbt where t_kind = :Kind and t_flagcur = :currency");
  findTypeAcc.addParam("Kind", RSDBP_IN, RealKind);
  findTypeAcc.addParam("currency", RSDBP_IN, currency);
  findTypeAcc.execute();

  var rsfindTypeAcc = RsdRecordset(findTypeAcc);

  if ( not rsfindTypeAcc.moveNext() )
    RunError( ErrMessage( 16513 ), 16513 );
  end;

  if ( ((ValType(OperType) == V_UNDEF) or (OperType != 2)) and (rsfindTypeAcc.value("t_OpFromOtherDepType") != 0))
    var findOtherTypeAcc = rsdcommand("select * from dpc_apltp_dbt where t_type = :Kind and t_iscur = :currency and "
                                 " t_typerec = 1003 and t_appltype = 3010");
    findOtherTypeAcc.addParam("Kind", RSDBP_IN, RealKind);
    findOtherTypeAcc.addParam("currency", RSDBP_IN, currency);
    findOtherTypeAcc.execute();

    rsfindTypeAcc = RsdRecordset(findOtherTypeAcc);
    if ( not rsfindTypeAcc.moveNext() )
      RunError( ErrMessage( 16513 ), 16513 );
    end;
    RealKind = rsfindTypeAcc.value("t_aptype");

    findTypeAcc = rsdcommand("select * from dsb_dtyp_dbt where t_kind = :Kind and t_flagcur = :currency");
    findTypeAcc.addParam("Kind", RSDBP_IN, RealKind);
    findTypeAcc.addParam("currency", RSDBP_IN, currency);
    findTypeAcc.execute();
    rsfindTypeAcc = RsdRecordset(findTypeAcc);
    if ( not rsfindTypeAcc.moveNext() )
      RunError( ErrMessage( 16513 ), 16513 );
    end;
    RealKind = rsfindTypeAcc.value("t_kind");
  end;
  
  if ( ValType(OperType) == V_UNDEF )
    OperType = 0;
  end;

  if ( ValType(InteractType) == V_UNDEF )
    OperType = 1;
  end;

  if ( ValType(OperCodes) == V_UNDEF )
    OperCodes = "";
  end;

//обработка OperCodes
  var WorkOperCodes;

  if (OperCodes != "")
    WorkOperCodes = Trim(OperCodes);
    WorkOperCodes = DelAllRepeat(WorkOperCodes);
    WorkOperCodes = DelAllSpaces(WorkOperCodes);
    WorkOperCodes = StrUpr(WorkOperCodes);
  end;
  var lenOperCodes = StrLen(WorkOperCodes);
  var i, retvalfindAccOp, sqlfindAccOpFin, sqlfindAccOpNoFin;

  if (OperType == 1)

    sqlfindAccOpFin = "select * from dsb_typop_dbt where t_kind = :Kind and t_iscur = :currency and (t_numopert > 0 and t_numopert <= 200) ";
 
    if ( InteractType == 1 )
      sqlfindAccOpFin = sqlfindAccOpFin + " and t_sysnumb = 0 ";
    elif ( InteractType == 2 )
      sqlfindAccOpFin = sqlfindAccOpFin + " and t_sysnumb <> 0 ";
    end;

    if ( lenOperCodes > 0 )
      sqlfindAccOpFin = sqlfindAccOpFin + " and ( t_regimes like ? ";
      if ( lenOperCodes > 1 )
        for (i, 2, lenOperCodes)
          sqlfindAccOpFin = sqlfindAccOpFin + " and t_regimes like ? ";
        end;
      end;
      sqlfindAccOpFin = sqlfindAccOpFin + " ) ";
    end;

    var findAccOpFin = rsdcommand(sqlfindAccOpFin);

    findAccOpFin.addParam("Kind", RSDBP_IN, RealKind);
    findAccOpFin.addParam("currency", RSDBP_IN, currency);
    if ( lenOperCodes > 0 )
     for (i, 1, lenOperCodes)
       var AddParamFin = "%" + SubStr(WorkOperCodes,i,1)+ "%";
       findAccOpFin.addParam("", RSDBP_IN, AddParamFin);
     end;
    end;

    findAccOpFin.execute();
  
    retvalfindAccOp = RsdRecordset(findAccOpFin);

  elif ( OperType == 2 )

    sqlfindAccOpNoFin = "select * from dsb_typop_dbt where t_kind = :Kind and t_iscur = :currency and (t_numopert > 200 and t_numopert <= 700) ";
 
    if ( InteractType == 1 )
      sqlfindAccOpNoFin = sqlfindAccOpNoFin + " and t_sysnumb = 0";
    elif ( InteractType == 2 )
      sqlfindAccOpNoFin = sqlfindAccOpNoFin + " and t_sysnumb <> 0";
    end;

    if ( lenOperCodes > 0 )
     sqlfindAccOpNoFin = sqlfindAccOpNoFin + " and ( t_regimes like ? ";
     if ( lenOperCodes > 1 )
        for (i, 2, lenOperCodes)
          sqlfindAccOpNoFin = sqlfindAccOpNoFin + " and t_regimes like ? ";
        end;
      end;
     sqlfindAccOpNoFin = sqlfindAccOpNoFin + " ) ";
    end;

    var findAccOpNoFin = rsdcommand(sqlfindAccOpNoFin);

    findAccOpNoFin.addParam("Kind", RSDBP_IN, ShablonAccType);
    findAccOpNoFin.addParam("currency", RSDBP_IN, currency);
    if ( lenOperCodes > 0 )
     for (i, 1, lenOperCodes)
       var AddParamNoFin = "%" + SubStr(WorkOperCodes,i,1)+ "%";
       findAccOpNoFin.addParam("", RSDBP_IN, AddParamNoFin);
     end;
    end;

    findAccOpNoFin.execute();
  
    retvalfindAccOp = RsdRecordset(findAccOpNoFin);
  
  elif( OperType == 0 )

    sqlfindAccOpFin = " t_kind = :Kind and t_iscur = :currency and (t_numopert > 0 and t_numopert <= 200) ";
 
    if ( InteractType == 1 )
      sqlfindAccOpFin = sqlfindAccOpFin + " and t_sysnumb = 0 ";
    elif ( InteractType == 2 )
      sqlfindAccOpFin = sqlfindAccOpFin + " and t_sysnumb <> 0 ";
    end;

    if ( lenOperCodes > 0 )
      sqlfindAccOpFin = sqlfindAccOpFin + " and ( t_regimes like ? ";
      if ( lenOperCodes > 1 )
        for (i, 2, lenOperCodes)
          sqlfindAccOpFin = sqlfindAccOpFin + " and t_regimes like ? ";
        end;
      end;
      sqlfindAccOpFin = sqlfindAccOpFin + " ) ";
    end;

    sqlfindAccOpNoFin = " t_kind = :ShablonKind and t_iscur = :ShablonCurrency and (t_numopert > 200 and t_numopert <= 700) ";
 
    if ( InteractType == 1 )
      sqlfindAccOpNoFin = sqlfindAccOpNoFin + " and t_sysnumb = 0";
    elif ( InteractType == 2 )
      sqlfindAccOpNoFin = sqlfindAccOpNoFin + " and t_sysnumb <> 0";
    end;

    if ( lenOperCodes > 0 )
      sqlfindAccOpNoFin = sqlfindAccOpNoFin + " and ( t_regimes like ? ";
      if ( lenOperCodes > 1 )
        for (i, 2, lenOperCodes)
          sqlfindAccOpNoFin = sqlfindAccOpNoFin + " and t_regimes like ? ";
        end;
      end;
      sqlfindAccOpNoFin = sqlfindAccOpNoFin + " ) ";
    end;
    
    var sqlfindAccOp = "select * from dsb_typop_dbt where ( " + sqlfindAccOpFin + " ) or ( " + sqlfindAccOpNoFin + " )";

    var findAccOp = rsdcommand(sqlfindAccOp);

    findAccOp.addParam("Kind", RSDBP_IN, RealKind);
    findAccOp.addParam("currency", RSDBP_IN, currency);
    if ( lenOperCodes > 0 )
     for (i, 1, lenOperCodes)
       var AddParamCodes = "%" + SubStr(WorkOperCodes,i,1)+ "%";
       findAccOp.addParam("", RSDBP_IN, AddParamCodes);
     end;
    end;
    findAccOp.addParam("ShablonKind", RSDBP_IN, ShablonAccType);
    findAccOp.addParam("ShablonCurrency", RSDBP_IN, currency);
    if ( lenOperCodes > 0 )
     for (i, 1, lenOperCodes)
       AddParamCodes = "%" + SubStr(WorkOperCodes,i,1)+ "%";
       findAccOp.addParam("", RSDBP_IN, AddParamCodes);
     end;
    end;

    findAccOp.execute();
  
    retvalfindAccOp = RsdRecordset(findAccOp);

  end;

  while ( retvalfindAccOp.moveNext )
    var item = OpInfo;

    item.NumOper = convertNullInt( retvalfindAccOp.value( "t_numopert" ) );
    item.NameOper = convertNullStr( retvalfindAccOp.value( "t_cznamealg" ) );

    retVal[retVal.size] = item;
  end;
  return retVal;
end;

/********************************************************************************\
|                                                                                |
\********************************************************************************/
macro AccountOper( AccountID , BranchID , AccNumber , OperType , InteractType , OperCodes )
  
  var retVal:AccOpInfo;  
  var searchByAccountID = false;
  var searchByAccNumber = false;

  if ( ValType(AccountID) == V_INTEGER )
    if (AccountID > 0)
      searchByAccountID = true;
    end;
  end;
  
  if ( not searchByAccountID )
    if ( (ValType(BranchID) == V_STRING) and (ValType(AccNumber) == V_STRING) )
      if( (BranchID != "") and (AccNumber != "") )
        searchByAccNumber = true;
      end;
    end;
  end;
  
  if ( searchByAccountID or searchByAccNumber )
   ;
  else
    RunError( ErrMessage( 16504 ), 16504 );
  end;

  if ( ValType(OperCodes) == V_UNDEF )
    OperCodes = "";
  end;

//поиск счета
  var findAccount;
  var whereFNcashCond = "";

  if (searchByAccountID)
    findAccount = rsdcommand("select * from ddepositr_dbt where T_REFERENC = :AccountID");
    findAccount.AddParam("AccountID", RSDBP_IN, AccountID);
  end;
  if (searchByAccNumber)
    whereFNcashCond = " and t_fncash in (select t_code from ddp_dep_dbt where t_name = :BranchID)";
    findAccount = rsdcommand("select * from ddepositr_dbt where t_account = :AccNumber" + whereFNcashCond);
    findAccount.AddParam("AccNumber", RSDBP_IN, AccNumber);
    findAccount.AddParam("BranchID", RSDBP_IN, BranchID);
  end;
  findAccount.execute();

  var rsfindAccount = RsdRecordset(findAccount);
  var stat = rsfindAccount.moveNext();

  // Дополнительный поиск без учёта последних 2 символов в номере счета
  if ((stat == false) and searchByAccNumber and (StrLen(AccNumber) >= 20) )
    findAccount = rsdcommand("select * from ddepositr_dbt where t_account like :AccNumber" + whereFNcashCond);
    findAccount.AddParam("AccNumber", RSDBP_IN, SubStr(AccNumber,1,20) + "%");
    findAccount.AddParam("BranchID", RSDBP_IN, BranchID);
    findAccount.execute();
    rsfindAccount = RsdRecordset(findAccount);
    stat = rsfindAccount.moveNext();
  end;
  
  if (stat == true)
    var depstate = rsfindAccount.value("t_open_close");
    if ( depstate != strfor(0) )
      findAccount = rsdcommand("select * from ddepositr_dbt where t_code_currency = :CodeCurrency and "
                               " t_svodaccount = :SvodAccount and t_open_close = chr(0) and (t_action <> 2 and t_action <> 11)");
      findAccount.AddParam("CodeCurrency", RSDBP_IN, rsfindAccount.value("t_code_currency"));
      findAccount.AddParam("SvodAccount", RSDBP_IN, rsfindAccount.value("t_svodaccount"));
      findAccount.execute();
      rsfindAccount = RsdRecordset(findAccount);
      stat = rsfindAccount.moveNext();
      if ( stat == false )
        RunError( ErrMessage( 16512 ), 16512 );
      end;
    end;
  else
    RunError( ErrMessage( 16511 ), 16511 );
  end;

//если владелец счета умер  
  var findClient = rsdcommand("select t_death from dpersn_dbt where t_personid = :PersonID");

  findClient.AddParam("PersonID", RSDBP_IN, rsfindAccount.value("t_codclient"));
  findClient.execute();

  var rsfindClient = RsdRecordset(findClient);

  if(not rsfindClient.moveNext())
    RunError( ErrMessage( 16501 ), 16501 );
  end;

  var DateDeath = SQL_ConvTypeDate(rsfindClient.value("t_death"));

  if ( (DateDeath > Date(0,0,0)) and (StrBrk("D", OperCodes) == 0) )
    OperCodes = OperCodes + "D";
  end;

//если на счет наложен полный арест
  var ArestTypeAccount = StrBrk("Т", rsfindAccount.value("t_UserTypeAccount"));

  if( (ArestTypeAccount > 0) and ((StrBrk("Т", OperCodes) == 0)) )
    OperCodes = OperCodes + "A";
  end;

//отказ от обслуживания
  var RefusalOfAccountServicing = StrBrk("И", rsfindAccount.value("t_UserTypeAccount"));

  if( (RefusalOfAccountServicing > 0) and ((StrBrk("Р", OperCodes) == 0)) )
    OperCodes = OperCodes + "Р";
  end;

//отбор операций для вида вклада
  var operation =  GetTypeOper(rsfindAccount.value("t_type_account"), rsfindAccount.value("t_code_currency"), OperType, InteractType, OperCodes);

  retVal.AccountID = convertNullInt(rsfindAccount.value("t_referenc"));
  retVal.BranchID = convertNullInt(rsfindAccount.value("t_fncash"));
  retVal.AccNumber = SubStr(convertNullStr(rsfindAccount.value("t_account")),1,20);
  retVal.Operations = operation;
  
  return retVal;
end;

/********************************************************************************\
|                                                                                |
\********************************************************************************/
macro DepositOper( BranchID , DepNumber , OperType , InteractType , OperCodes )

  var retVal = TArray;

  if ( (ValType(BranchID) == V_STRING) and (ValType(DepNumber) == V_STRING) )
    if( (BranchID == "") or (DepNumber == "") )
      RunError( ErrMessage( 16504 ), 16504 );
    end;
  else
    RunError( ErrMessage( 16504 ), 16504 );
  end;

  if ( ValType(OperCodes) == V_UNDEF )
    OperCodes = "";
  end;

  var findDeposit = rsdcommand("select * from drt_contract_dbt where t_number = :DepNumber and t_type = 3 and "
                               "t_branch = (select t_code from ddp_dep_dbt where t_name = :BranchID)");
  findDeposit.AddParam("DepNumber", RSDBP_IN, DepNumber);
  findDeposit.AddParam("BranchID", RSDBP_IN, BranchID);
  findDeposit.execute();

  var rsfindDeposit = RsdRecordset(findDeposit);
  
  if ( not rsfindDeposit.moveNext() )
    RunError( ErrMessage( 16514 ), 16514 );
  end;

  if ( rsfindDeposit.value("t_state") != 10 )
    RunError( ErrMessage( 16515 ), 16515 );
  end;

  var findNumberAccount = rsdcommand("select COUNT(*) from ddepositr_dbt where t_svodaccount = :SvodAccount "
                               " and t_open_close = chr(0) and (t_action <> 2 and t_action <> 11) and "
                               " t_fncash = (select t_code from ddp_dep_dbt where t_name = :BranchID)");
  findNumberAccount.AddParam("SvodAccount", RSDBP_IN, rsfindDeposit.value("t_number"));
  findNumberAccount.AddParam("BranchID", RSDBP_IN, BranchID);
  findNumberAccount.execute();
  var rsfindNumberAccount = RsdRecordset(findNumberAccount);
  rsfindNumberAccount.moveNext();

  var number = rsfindNumberAccount.value("COUNT(*)");
  var AccOper, findAccount, rsAccount;

  if (number == 0)
    RunError( ErrMessage( 16516 ), 16516 );
  elif (number == 1)
    findAccount = rsdcommand("select * from ddepositr_dbt where t_svodaccount = :SvodAccount "
                                 " and t_open_close = chr(0) and (t_action <> 2 and t_action <> 11) and "
                                 " t_fncash = (select t_code from ddp_dep_dbt where t_name = :BranchID)");
    findAccount.AddParam("SvodAccount", RSDBP_IN, rsfindDeposit.value("t_number"));
    findAccount.AddParam("BranchID", RSDBP_IN, BranchID);
    findAccount.execute();
    rsAccount = RsdRecordset(findAccount);
    rsAccount.moveNext();
    AccOper = AccountOper( rsAccount.value("t_referenc"),null,null, OperType, InteractType, OperCodes );
    retVal = AccOper.Operations;
  elif (number > 1)
    findAccount = rsdcommand("SELECT depos.t_referenc, dtcurr.t_isbasecurrency, dtcurr.t_currency "
                                   " FROM ddepositr_dbt depos, dsb_dtcurr_dbt dtcurr "
                                 " WHERE depos.t_svodaccount = :DepNumber "
                                 " AND depos.t_open_close = chr(0) AND (depos.t_action <> 2 AND depos.t_action <> 11) "
                                 " AND depos.t_fncash = (SELECT t_code FROM ddp_dep_dbt WHERE t_name = :BranchID)"
                                     " AND depos.t_code_currency = dtcurr.t_currency "
                                     " AND depos.t_type_account = dtcurr.t_kind "
                                 " ORDER BY dtcurr.t_isbasecurrency DESC, dtcurr.t_currency ASC");
    findAccount.addParam("DepNumber",RSDBP_IN,DepNumber);
    findAccount.AddParam("BranchID", RSDBP_IN, BranchID);
    findAccount.execute();
    rsAccount = RsdRecordset(findAccount);
    rsAccount.moveNext();
    AccOper = AccountOper( rsAccount.value("t_referenc"),null,null, OperType, InteractType, OperCodes );
    retVal = AccOper.Operations;

  end;  

  return retVal;
end;

 

/*
16501 Клиент не найден
16502 Недопустимое значение параметра "Вид карт"
16503 Недопустимое значение параметра "Активность карт"
16504 Заданы не все обязательные параметры
16505 Объект с переданным ID не найден
16506 Основная карта не найдена
16507 Карта не основная
16508 Дублирование номера карты
16509 Недопустимое значение параметра "Объем предоставляемых сведений"
16510 Карта закрыта. Перечисление невозможно.
16511 Счет не найден
16512 Счет закрыт
16513 Вид вклада не найден
16514 Вид вклада не найден
16516 Для договора не найден действующий счет
16517 Недопустимое значение параметра "Тип операции"
16518 Недопустимое значение параметра "Выполнение в диалоге"
16519 Недопустимое значение параметра "Дополнительные коды"
*/