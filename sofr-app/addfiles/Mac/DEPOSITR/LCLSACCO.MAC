/****************************************************************************
*                                                                           *
*  ---------------                                                          *
*  R-Style Softlab                                                          *
*  ---------------                                                          *
*  RS-Retail                                                                *
*                                                                           *
*  Фронт-оффис                                                              *
*                                                                           *
*  Отчет за период по счетам, закрытым в других подразделениях.             *
*                                                                           *
*****************************************************************************/

import DeprIntr, RSD;

var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

const
  page_Lines = 50;  /* Число строк отчета на странице */

const  FlagCur = NumFlagCur(),
       FNCash  = NumFNCash();

var
  currency = TBFile( "currency.dbt", "r", 0 );      /* Справочник валют */

var
  deptmp;  /* Временная таблица с нужным ключом. */

var
  {UseNewNumbering},
  {curdate},
  {CNum},
  StartDate = {curdate},
  EndDate = {curdate},
  ClientList = TClientList,
  Stat = true;

var
  flagFloor = FALSE;

var
  vCurr = -1,
  vTypeAcc = "",
  sCurr = -1,
  sTypeAcc = "";

var
  first_Page = TRUE,  /* Флаг первой страницы */
  num_Line_of_Page,   /* Номер строки на странице */
  num_Page = 0;       /* Номер страницы */

var
  Total_Page,    /* Итого: по странице */
  Total_ISO_TA,  /* Итого: по валюте и виду вклада */
  Total_Total;   /* Итого: всего по Ведомости */

var
  ErrCode, ErrText;

/*************************************************************************
          Обработчик нажатия клавиш процедуры ввода дат периода.
*************************************************************************/
macro Handler_ClsAccOther (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Esc - Отказ от выпуска отчета ----------------------- */
    if (key == 27)
      stat = CM_CANCEL;
    /* F7 - Выпуск отчета ---------------------------------- */
    elif (key == 321)
      stat = CM_SAVE;
    end;
  end;

  return stat;

end;

/*************************************************************************
                           Панель ввода дат периода.
*************************************************************************/
macro DatePeriod_ClsAccOther()

  record
    Period("SC_PRD", "rtusrmac") dialog;

  Period.BeginDate = StartDate;
  Period.EndDate = EndDate;
  Stat = RunDialog(Period, "Handler_ClsAccOther");
  if(Stat)
    StartDate = Period.BeginDate;
    EndDate = Period.EndDate;
  end;
end;

/*************************************************************************
               Получение наименования подразделения.
*************************************************************************/
macro get_Branch_Name( pCode )
  var vNameBranch;
  var i_dp_dep = TRecHandler( "i_dp_dep.rec" );
  if ( FindDepartment( pCode, NULL, i_dp_dep ) )
    vNameBranch = i_dp_dep.rec.Name;
  else
    vNameBranch = String( pCode );
  end;
  return vNameBranch;
end;

/*************************************************************************
                         Строка названия валюты.
*************************************************************************/
macro stringCurrency( pCode )
  var sc;
  currency.rec.Code_Currency = pCode;
  if ( currency.GetEQ )
    sc = String( currency.rec.ExternalCode ) +
         " (" + currency.rec.Name_Currency + ")";
  else
    sc = String( deptmp.rec.Code_Currency );
  end;
  return sc;
end;

/*************************************************************************
                          Заголовок по валюте.
*************************************************************************/
macro PrintHeadCurrency
  [ Валюта: #] ( stringCurrency( deptmp.rec.Code_Currency ) );
  [ ];
end;

/*************************************************************************
                                 Колонтитул.
*************************************************************************/
macro Colontitle
  num_Page = num_Page + 1;
  [ ---- #################### --------------- ########## ------ ######## -----------------------------  страница ####
  ] ( GetRetailVersion(), Date(), Time(), num_Page:l );
end;

/*************************************************************************
          Формирование временной таблицы с нужным ключом.
*************************************************************************/
macro Form_Temp_Table()
  var ClientName = "";

  var cmd = RsdCommand( "select distinct a.t_Account as Account, "   +
                        " a.t_Type_Account as accType, "   +
                        " a.t_Code_Currency as currCode, " +
                        " a.t_CodClient as clientCode, "   +
                        " a.t_Close_Date as closeDate, "   +
                        " a.t_Open_Date as openDate, "     +
                        " a.t_SpecialAccess as SpecialAccess, "  +
                        " d.t_RealFNCash as closeFNCash "  +
                        "from ddepositr_dbt a, dsbdepdoc_dbt d " +
                        "where d.t_IsCur = ? and"  +
                             " d.t_FNCash = ? and" +
                             " a.t_Close_Date between ? and ? and" +
                             " ( d.t_TypeComplexOper = 10  or"   + /* Закр.нал.   */
                               " d.t_TypeComplexOper = 81  or"   + /* Закр.б/н    */
                               " d.t_TypeComplexOper = 96 ) and" + /* Закр.перев. */
                             " d.t_IsControl = 'X' and"     +
                             " d.t_FNCash <> d.t_RealFNCash and " + /* В др.филиале */
                             " a.t_IsCur = d.t_IsCur and"   +
                             " a.t_FNCash = d.t_FNCash and" +
                             " a.t_Referenc = d.t_Referenc and" +
                             " a.t_Code_Currency = d.t_Code_Currency" );

  var rs = RsdRecordset( cmd );

  message( "Заполнение временного файла" );

  cmd.addParam( "isCur", RSDBP_IN );
  cmd.addParam( "branch", RSDBP_IN );
  cmd.addParam( "minDate", RSDBP_IN );
  cmd.addParam( "maxDate", RSDBP_IN );

  cmd.value( "isCur" ) = FlagCur;
  cmd.value( "branch" ) = FNCash;
  cmd.value( "minDate" ) = StartDate;
  cmd.value( "maxDate" ) = EndDate;

  cmd.execute;

  while ( rs.moveNext )

    if ( ClientList.GetRecord( rs.value( "clientCode" ) ) )
       if(  (not SpecialAccess) and OperMayListSpecialAccounts and rs.value( "SpecialAccess" ) )
          ClientName = "XXXXXX X.X.";
       else
     	  ClientName = ClientList.CurRec.ConvertFioFull();
       end;
    else
      ClientName = "";
    end;

    ClearRecord( deptmp.rec );

    deptmp.rec.Code_Currency = rs.value( "currCode" );
    DtTmSplit( rs.value( "openDate" ), deptmp.rec.Open_Date );
    DtTmSplit( rs.value( "closeDate" ), deptmp.rec.Close_Date );
    deptmp.rec.Type_Account = rs.value( "accType" );
    deptmp.rec.Account = rs.value( "account" );
    deptmp.rec.FIO_Client = ClientName;
    deptmp.rec.FNCashClose = rs.value( "closeFNCash" );

    if ( NOT deptmp.Insert )
      PrintLn( " *** Ошибка при записи во временный файл" );
      ErrCode = Status( ErrText );
      PrintLn( " *** ", ErrCode, ": ", ErrText );
      Exit( 0 );
    end;
  end;

  message( "" );
end;

/*************************************************************************
           Заголовок части отчета по валюте - виду вклада.
*************************************************************************/
macro PrintTitle()

  [ +-------------------------------------------------------------------------------------------------------------+];
  [ | № № |  Номер счета по вкладу   |        Ф.И.О.  вкладчика        |Дата закрытия|Подразделение,|  Примечание |];
  [ | п/п |                          |                                 |    счета    |закрывшее счет| (дата откр.)|];
  [ |-----|--------------------------|---------------------------------|-------------|--------------|-------------|];
  flagFloor = TRUE;
end;

/*************************************************************************
                Подвал части отчета по валюте - виду вклада.
*************************************************************************/
macro PrintFloor()
  if ( flagFloor )
    [ +-------------------------------------------------------------------------------------------------------------+];
  end;
end;

/********************************************************************
            Процедура перехода со страницы на страницу.
********************************************************************/
macro next_Page( pStep )
  PrintFloor();
  [ ];
  if ( NOT first_Page )
    [ #] ( "Итого счетов по странице " +
           String( num_Page ) + ": " +
           String( Total_Page ) );
    if ( pStep == 0 )  /* Конец части по валюте и виду вклада */
      [ #] ( "Итого счетов по виду вклада \"" +
             ( sTypeAcc ) + "\" в валюте " +
             stringCurrency( sCurr ) + ": " +
             String( Total_ISO_TA ) );
      Total_ISO_TA = 0;
    end;
    printLn( strFor( 12 ) );  /* Управляющий символ конца страницы */
    Colontitle();
    [ ];
    [ ];
  end;
  PrintHeadCurrency();
  [ Вид вклада: #] ( vTypeAcc );
  [ ];
  PrintTitle();
  first_Page = FALSE;
  num_Line_of_Page = 0;
  Total_Page = 0;
end;

/*************************************************************************
                    Печать отчета по временной таблице
*************************************************************************/
macro Print_Report()

  var stat;
  var NN = 1;
  var nr = 0;
  var str, ClientName = "";
  var NameBranch = "";
  var codeCurr;



  if ( FlagCur == 0 )
    codeCurr = 0;
  else
    codeCurr = 1;
  end;

  NameBranch = get_Branch_Name( FNCash );

  InitProgress( deptmp.NRecords, " Ж Д И Т Е ...",
                "Формирование отчета" );
  deptmp.KeyNum = 0;
  deptmp.ReWind();
  deptmp.Clear();

  [ ];
  [ Филиал № #](NameBranch);
  [ ];
  str = "                               Ведомость счетов, закрытых другими подразделениями";
  [#](str);
  str = "                                       за период с " + String(StartDate) + " по " + String(EndDate);
  [#](str);
  [ ];

  num_Line_of_Page = 0;

  Total_Page   = 0;  /* Итого: по странице */
  Total_ISO_TA = 0;  /* Итого: по валюте и виду вклада */
  Total_Total  = 0;  /* Итого: всего по Ведомости */

  stat = deptmp.getGE;
  while ( stat )

    num_Line_of_Page = num_Line_of_Page + 1;

    if ( ( vCurr    != deptmp.rec.Code_Currency )  OR
         ( vTypeAcc != deptmp.rec.Type_Account ) )
      sCurr    = vCurr;
      sTypeAcc = vTypeAcc;
      vCurr    = deptmp.rec.Code_Currency;
      vTypeAcc = deptmp.rec.Type_Account;
      next_Page( 0 );
    else
      if ( num_Line_of_Page >= page_Lines )
        next_Page( 1 );
      end;
    end;

    /* Формирование строки отчета */
    str = " |"  + String( NN:4 )                     + " | ";
    str = str   + String( Acc_Form(deptmp.rec.Account):24 ) + " | ";
    str = str   + String( deptmp.rec.FIO_Client:31 ) + " | ";
    str = str   + String( deptmp.rec.Close_Date:11 ) + " | ";
    str = str   + String( get_Branch_Name( deptmp.rec.FNCashClose ):12:c ) + " | ";
    str = str   + String( deptmp.rec.Open_Date:11 )  + " | ";
    [#](str);

    NN = NN + 1;

    Total_Page   = Total_Page + 1;    /* Итого: по странице */
    Total_ISO_TA = Total_ISO_TA + 1;  /* Итого: по валюте и виду вклада */
    Total_Total  = Total_Total + 1;   /* Итого: всего по Ведомости */

    UseProgress( nr );
    nr = nr + 1;
    stat = deptmp.Next;
  end;

  if ( Total_Total != 0 )
    PrintFloor();
    [ ];
    [ #] ( "Итого счетов по странице " +
           String( num_Page ) + ": " +
           String( Total_Page ) );
    [ #] ( "Итого счетов по виду вклада \"" +
           ( vTypeAcc ) + "\" в валюте " +
           stringCurrency( vCurr ) + ": " +
           String( Total_ISO_TA ) );
    [ ];
    [ #] ( "Всего счетов по Ведомости: " + String( Total_Total ) );
  end;

  RemProgress();

end;

/*************************************************************************
                          Основная процедура.
*************************************************************************/
macro Main(void)

  var TxtDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true );
  var FNdt = TxtDir + "depositr." + String( {CNum} );

  deptmp = TBFile( "depositr.t01", "wc+", 0, FNdt );

  if ( {UseNewNumbering} )
    DatePeriod_ClsAccOther();
    if ( Stat )
      Colontitle();
      [ ];
      Form_Temp_Table();
      Print_Report();
      [ ];
    end;
  end;
end;

main();
End.
