/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Формирование оборотов по балансовому счету                              *
*                                                                          *
*  Лебедев С.В.                                                            *
*  08.01.98                                                                *
\**************************************************************************/

import deprintr, BalAcc, RSD;

file sb_depst (sb_depst); /* Статистика по вкладам */
file sb_dtyp  (sb_dtyp ); /* Справочник вкладов    */

record sb_depst_rec (sb_depst);

var {curdate};
var FlagCur         = NumFlagCur();
var BalAccount      = "";
var FlagBeginReport = TRUE;
var Mode            = GetCurrentMode();

/* Значения кода режима работы */
const MODE_NORMAL = 0;       /* Нормальный режим */
const MODE_EOY = 1;          /* Конец периода */
const MODE_EOY_PRECALC = 2;  /* Конец периода - документы предварительного расчета */
const MODE_GZ = 4;           /* Группа зачисления */
 

/**************************************************************************\
|                               Шапка отчета                               |
\**************************************************************************/
macro HeadReport (Rest)

  var str;
  var d,m,y;
  var Month;
  var Day;

  DateSplit({curdate},d,m,y);
  if (m == 1)
    Month = "января  ";
  elif (m == 2)
    Month = "февраля ";
  elif (m == 3)
    Month = "марта   ";
  elif (m == 4)
    Month = "апреля  ";
  elif (m == 5)
    Month = "мая     ";
  elif (m == 6)
    Month = "июня    ";
  elif (m == 7)
    Month = "июля    ";
  elif (m == 8)
    Month = "августа ";
  elif (m == 9)
    Month = "сентября";
  elif (m == 10)
    Month = "октября ";
  elif (m == 11)
    Month = "ноября  ";
  elif (m == 12)
    Month = "декабря ";
  else
    Month = "???     ";
  end;

  if (d < 10)
    Day = "0" + String(d);
  else
    Day = String(d);
  end;

  [ ];
  [             Учреждение банка No______________];
  [ ];
  [                   УЧЕТНАЯ  КАРТОЧКА];
  [      ОБОРОТОВ  ПО  БАЛАНСОВОМУ  СЧЕТУ  No #](BalAccount);
  [ ];
  str = "                 за  " + Day + " " + Month + " " + String(y) + "г.";
  [ #](str);
  if (sb_depst_rec.Mode != 0)
    [                  (за завершение периода)];
  end;
  [ ];
  [ ─────────────┬──┬───────────────────┬──┬────────────────────];
  [    Вход.     │Кт│ ################# │Дт│]
  (Rest:17:2:a);
  [    остат.    │  │                   │  │];
  [ ─────────────┼──┴───────────────────┼──┴────────────────────];
  [     Дата     │      Кредит          │      Дебет];
  [              │      (приход)        │      (расход)];
  [ ─────────────┼──────────────────────┼───────────────────────];

end;


/**************************************************************************\
|                             Окончание отчета                             |
\**************************************************************************/
macro EndReport (Rest)

  [ ─────────────┼──────────────────────┼───────────────────────];
  [              │                      │];
  [  Исход.остат.│ #################### │]
  (Rest:20:2:a);
  [ ─────────────┴──────────────────────┴───────────────────────];

end;


/**************************************************************************\
|                   Определение остатка на заданную дату                   |
\**************************************************************************/
macro RestForDate (DateRest, ModeRest)

  var Rest     = $0L;
  var stat;

  KeyNum(sb_dtyp,2);
  ClearRecord( sb_dtyp ); 
  sb_dtyp.FlagCur = sb_depst_rec.IsCur;
  stat = GetGE(sb_dtyp);
  while (stat)
    if (sb_dtyp.FlagCur == sb_depst_rec.IsCur)
      if ( ( FindBalAccByAccType( sb_dtyp.Kind, sb_dtyp.FlagCur, false ) == BalAccount ) or ( FindBalAccByAccType( sb_dtyp.Kind, sb_dtyp.FlagCur, true ) == BalAccount ) )
        KeyNum(sb_depst,0);
        sb_depst.FNCash  = sb_depst_rec.FNCash;
        sb_depst.IsCur   = sb_depst_rec.IsCur;
        sb_depst.Kind    = sb_dtyp.Kind;
        sb_depst.CodCur  = sb_depst_rec.CodCur;
        sb_depst.FlagRez = "";
        sb_depst.Date    = DateRest;
        sb_depst.Mode    = ModeRest;
        if (GetLE(sb_depst))
          if ((sb_depst.FNCash  == sb_depst_rec.FNCash) AND
              (sb_depst.IsCur   == sb_depst_rec.IsCur ) AND
              (sb_depst.Kind    == sb_dtyp.Kind       ) AND
              (sb_depst.CodCur  == sb_depst_rec.CodCur) AND
              (sb_depst.FlagRez == ""                 ) AND
              (sb_depst.Date    <= DateRest           )    )
            Rest = Rest + sb_depst.Rest;
          end;
        end;
      end;
      stat = Next(sb_dtyp);
    else
      stat = FALSE;
    end;
  end;

  return Rest;

end;


/***********************************************************************\
|                     Запись выходной строки в отчет                    |
\***********************************************************************/
macro WriteToReport
(
 SumDebet,
 SumCredit,
 DateCalc
)

  if (FlagBeginReport)
    FlagBeginReport = FALSE;
  else
    [ ─────────────┼──────────────────────┼───────────────────────];
  end;
  [  ##########  │ #################### │ ####################  ]
  (DateCalc,SumCredit:20:2:a,SumDebet:20:2:a);

end;


/**************************************************************************\
|                    Определение Кредита/Дебета за дни                     |
\**************************************************************************/
macro DefDebetCreditForDays

  var stat;
  var SumDebet  = $0L;
  var SumCredit = $0L;

  KeyNum(sb_dtyp,2);
  ClearRecord( sb_dtyp ); 
  sb_dtyp.FlagCur = sb_depst_rec.IsCur;
  stat = GetGE(sb_dtyp);
  while (stat)
    if (sb_dtyp.FlagCur == sb_depst_rec.IsCur)
      if ( FindBalAccByAccType( sb_dtyp.Kind, sb_dtyp.FlagCur, false ) == BalAccount )
        KeyNum(sb_depst,0);
        sb_depst.FNCash  = sb_depst_rec.FNCash;
        sb_depst.IsCur   = sb_depst_rec.IsCur;
        sb_depst.Kind    = sb_dtyp.Kind;
        sb_depst.CodCur  = sb_depst_rec.CodCur;
        sb_depst.FlagRez = "";
        sb_depst.Date    = sb_depst_rec.Date;
        sb_depst.Mode    = sb_depst_rec.Mode;
        if (GetEQ(sb_depst))
          SumDebet  = SumDebet  + sb_depst.DebSum;
          SumCredit = SumCredit + sb_depst.KredSum;
        end;
      end;
      stat = Next(sb_dtyp);
    else
      stat = FALSE;
    end;
  end;

  ClearRecord( sb_dtyp ); 
  sb_dtyp.FlagCur      = sb_depst_rec.IsCur;
  stat = GetGE(sb_dtyp);
  while (stat)
    if (sb_dtyp.FlagCur      == sb_depst_rec.IsCur)
      if ( FindBalAccByAccType( sb_dtyp.Kind, sb_dtyp.FlagCur, true ) == BalAccount )
        KeyNum(sb_depst,0);
        sb_depst.FNCash  = sb_depst_rec.FNCash;
        sb_depst.IsCur   = sb_depst_rec.IsCur;
        sb_depst.Kind    = sb_dtyp.Kind;
        sb_depst.CodCur  = sb_depst_rec.CodCur;
        sb_depst.FlagRez = StrFor(1);
        sb_depst.Date    = sb_depst_rec.Date;
        sb_depst.Mode    = sb_depst_rec.Mode;
        if (GetEQ(sb_depst))
          SumDebet  = SumDebet  + sb_depst.DebSum;
          SumCredit = SumCredit + sb_depst.KredSum;
        end;
      end;
      stat = Next(sb_dtyp);
    else
      stat = FALSE;
    end;
  end;

  WriteToReport(SumDebet,SumCredit,sb_depst_rec.Date);

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/
macro Form68  /* !!! Имя этой макро-процедуры и параметры менять нельзя */
(
  RecAddr
)

  var Rest;
  var DateRep;
  var d,m,y;
  var cmdA, rsA;
  var cmdB, rsB;

  setbuff(sb_depst_rec,RecAddr);

  cmdA = RsdCommand( "select typgrp.t_groupid as rGrID "
                       "from dsbtypgrp_dbt typgrp "
                       "where typgrp.t_flagcur = :flagCur and "
                            "typgrp.t_kind = :kind" );

  rsA = RsdRecordset( cmdA );

  cmdA.addParam( "flagCur", RSDBP_IN, sb_depst_rec.IsCur );
  cmdA.addParam( "kind", RSDBP_IN, sb_depst_rec.Kind );

  cmdA.execute;

  while ( rsA.moveNext() )

    cmdB = RsdCommand( "select ng.t_BalAcc2 as rBalAcc, ng.t_BalAcc2NotResid as rBalAccNR "
                         "from dsbnumgrp_dbt ng "
                         "where ng.t_groupid = :gr_id" );

    rsB = RsdRecordset( cmdB );

    cmdB.addParam( "gr_id", RSDBP_IN, rsA.value( "rGrID" ) );

    cmdB.execute;

    if ( rsB.moveNext() )

      FlagBeginReport = TRUE;

      if (sb_depst_rec.FlagRez == "")
        BalAccount = rsB.value( "rBalAcc" );
      else
        BalAccount = rsB.value( "rBalAccNR" );
      end;

      if (StrLen(BalAccount) != 0)
        /* Определяем входящий остаток - остаток за предыдущую дату */
        if (sb_depst_rec.Mode == 0)
          Rest = RestForDate(sb_depst_rec.Date - 1,1);
        else
          Rest = RestForDate(sb_depst_rec.Date,0);
        end;
        /* Шапка отчета */
        HeadReport(Rest);
        /* Определяем Кредит/Дебет за период до текущей даты */
        DefDebetCreditForDays();
        /* Определяем входящий остаток - остаток за текущую дату */
        Rest = RestForDate(sb_depst_rec.Date,sb_depst_rec.Mode);
        /* Окончание отчета */
        EndReport(Rest);
      else
        [ ];
        [ Балансовый счет не задан];
      end;
    end;
  end;

end;
