/*
$Name:           PCE_DAY.mac
$Module:         RETAIL
$Description:    Ведомость начисленных процентов, отраженных по балансу за дату
*/

/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  * 
*                                                                          *
*  Ведомость начисленных процентов, отраженных по балансу за дату          *
*                                                                          *
\**************************************************************************/

import DeprIntr, IsSrvDoc, SqlConv, BalAcc;

/* Переменные для работы макрофайла ===================================== */
var   FlagCur    = NumFlagCur(); /* Валютность                      */
var   FNCash     = NumFNCash();  /* Номер филиала                   */
var   NumPP      = 1;            /* Номер по порядку                */
var   DateReport = {curdate};    /* Дата отчета                     */

var   KindInputRest  = $0L;
var   KindOutputRest = $0L;
var   KindInSum      = $0L;
var   KindOutSum     = $0L;
var   KindOutSum_97  = $0L;
var   KindOutOverSum = $0L;
/* ---------------------- */
var   BAInputRest    = $0L;
var   BAOutputRest   = $0L;
var   BAInSum        = $0L;
var   BAOutSum       = $0L;
var   BAOutSum_97    = $0L;
var   BAOutOverSum   = $0L;
/* ---------------------- */
var   SumInputRest   = $0L;
var   SumOutputRest  = $0L;
var   SumInSum       = $0L;
var   SumOutSum      = $0L;
var   SumOutSum_97   = $0L;
var   SumOutOverSum  = $0L;
/* ---------------------- */
var   TBAInputRest   = $0L;
var   TBAOutputRest  = $0L;
var   TBAInSum       = $0L;
var   TBAOutSum      = $0L;
var   TBAOutOverSum  = $0L;
/* ---------------------- */
var   TSumInputRest  = $0L;
var   TSumOutputRest = $0L;
var   TSumInSum      = $0L;
var   TSumOutSum     = $0L;
var   TSumOutOverSum = $0L;

var   NAccs = 0;
//var   {BatchMode};

var   offset = 0;

/* Значения кода режима работы */
const MODE_NORMAL = 0;       /* Нормальный режим */
const MODE_EOY = 1;          /* Конец периода */
const MODE_EOY_PRECALC = 2;  /* Конец периода - документы предварительного расчета */
const MODE_GZ = 4;           /* Группа зачисления */
 

/**************************************************************************\
|                          Напечатать шапку отчета                         |
\**************************************************************************/
macro WriteHeadReport

  var dep_name;
  
  if (not findDepartment(FNCash, dep_name))
    dep_name = "";
  end;

  [ ];
  [ Подразделение #](dep_name);
  [ ];
  [##########################################################################################################################]
  ("Ведомость начисленных процентов, отраженных по балансу за " + String(DateReport:f):c);

end;


/**************************************************************************\
|                     Напечатать шапку отчета по валюте                    |
\**************************************************************************/
macro WriteHeadForCurrency( ExternalCode, Name_Currency )

  [ ];
  if ( FlagCur )
    [ Валюта  #     #]( ExternalCode, Name_Currency );
    [ ];
  end;

  [--------------------------------------------------------------------------------------------------------------------------];
  [|№ п/п|    Номер счета вкладчика    |                     Сумма начисленных %%, отраженных по балансу                    |];
  [|     |                             |------------------------------------------------------------------------------------|];
  [|     |                             |    Остаток     |                |             Списано             |     Остаток    |];
  [|     |                             |   на начало    |   Начислено    |   Причислено   |   Излишне      |    на конец    |];
  [|     |                             |      дня       |                |   к вкладу     |   начислено    |      дня       |];
  [|------------------------------------------------------------------------------------------------------------------------|];

end;


/**************************************************************************\
|                  Напечатать шапку отчета по виду вкладу                  |
\**************************************************************************/
macro WriteHeadForType ( BalAcc, Name )

  [|###### #                                                                                                                |]
  ( BalAcc, Name );
  [|------------------------------------------------------------------------------------------------------------------------|];

end;


/**************************************************************************\
|                   Напечатать итог по валюте                              |
\**************************************************************************/
macro WriteSummaryForCurrency

  if ((SumInSum != 0) OR (SumOutSum != 0) OR (SumOutOverSum != 0))
    [|ИТОГО по документам                |################|################|################|################|################|]
    (SumInputRest:16:2, SumInSum:16:2, SumOutSum:16:2, SumOutOverSum:16:2, SumOutputRest:16:2);
    if (SumOutSum_97 != 0)
		[|                в т.ч. до 01.01.97 |                |                |################|                |                |]
		(SumOutSum_97:16:2);
    end;
  end;
  [|ИТОГО                              |################|################|################|################|################|]
  (TSumInputRest:16:2, TSumInSum:16:2, TSumOutSum:16:2, TSumOutOverSum:16:2, TSumOutputRest:16:2);
  [==========================================================================================================================];

  SumInputRest   = $0L;
  SumOutputRest  = $0L;
  SumInSum       = $0L;
  SumOutSum      = $0L;
  SumOutSum_97   = $0L;
  SumOutOverSum  = $0L;
  /*******************/
  TSumInputRest  = $0L;
  TSumOutputRest = $0L;
  TSumInSum      = $0L;
  TSumOutSum     = $0L;
  TSumOutOverSum = $0L;

end;


/**************************************************************************\
|                   Напечатать итог по балансовому счету                   |
\**************************************************************************/
macro WriteSummaryForBA ( NumBA ) 

  if ((BAInSum != 0) OR (BAOutSum != 0) OR (BAOutOverSum != 0))
    [|Итого по документам                |################|################|################|################|################|]
    ( BAInputRest:16:2, BAInSum:16:2, BAOutSum:16:2, BAOutOverSum:16:2, BAOutputRest:16:2);
    if (BAOutSum_97 != 0)
    [|                в т.ч. до 01.01.97 |                |                |################|                |                |]
      (BAOutSum_97:16:2);
    end;
  end;
  [|Итого по балансовому счету #       |################|################|################|################|################|]
  (NumBA, TBAInputRest:16:2, TBAInSum:16:2, TBAOutSum:16:2, TBAOutOverSum:16:2, TBAOutputRest:16:2);
  [|========================================================================================================================|];

  BAInputRest   = $0L;
  BAOutputRest  = $0L;
  BAInSum       = $0L;
  BAOutSum      = $0L;
  BAOutSum_97   = $0L;
  BAOutOverSum  = $0L;
  /******************/
  TBAInputRest  = $0L;
  TBAOutputRest = $0L;
  TBAInSum      = $0L;
  TBAOutSum     = $0L;
  TBAOutOverSum = $0L;

end;


/**************************************************************************\
|                      Напечатать итог по виду вклада                      |
\**************************************************************************/
//macro WriteSummaryForKind (Citizenship, BalAcc)
macro WriteSummaryForKind ( BalAcc, TypeAccount, Citizenship, CodeCurrency )

  var stat;
  var DateLastStat    = Date(0,0,0);
  var TKindInputRest  = $0L;
  var TKindOutputRest = $0L;
  var TKindInSum      = $0L;
  var TKindOutSum     = $0L;
  var TKindOutOverSum = $0L;

  var cmd_ta, rs_ta;
  cmd_ta = RsdCommand(
     "select nvl( sum (depst.t_estimin), 0 ) estimin, " + 
            "nvl( sum (depst.t_estimout), 0 ) estimout, " +
            "nvl( sum (depst.t_estimoverout), 0 ) estimoverout, " +
            "nvl (sum (depst.t_estimrest), 0) estimrest " 
       "from dsb_depst_dbt depst " +
      "where depst.t_fncash = ? " +
        "and depst.t_iscur = ? " +
        "and depst.t_kind = ? " +
        "and depst.t_codcur = ? " +
        "and depst.t_flagrez = nvl( ?, chr(0) ) " +
        "and depst.t_BalAcc = ? " +
        "and depst.t_mode != 4 " +
        "and depst.t_date = " +
               "(select t_date " +
                  "from (select   t_date " +
                            "from dsb_depst_dbt depst " +
                           "where depst.t_fncash = ? " +
                             "and depst.t_iscur = ? " +
                             "and depst.t_kind = ? " +
                             "and depst.t_codcur = ? " +
                             "and depst.t_flagrez = nvl( ?, chr(0) ) " +
                             "and depst.t_BalAcc = ? " +
                             "and depst.t_mode != 4 " +
                             "and t_date <= ? " +
                        "order by t_date desc) " +
                 "where rownum = 1) ");
  cmd_ta.AddParam( "FNCash0",       RSDBP_IN );  cmd_ta.value( "FNCash0"       ) = FNCash;
  cmd_ta.AddParam( "isCur0",        RSDBP_IN );  cmd_ta.value( "isCur0"        ) = FlagCur;
  cmd_ta.AddParam( "TypeAccount0",  RSDBP_IN );  cmd_ta.value( "TypeAccount0"  ) = TypeAccount;
  cmd_ta.AddParam( "CodeCurrency0", RSDBP_IN );  cmd_ta.value( "CodeCurrency0" ) = CodeCurrency;
  cmd_ta.AddParam( "Citizenship0",  RSDBP_IN );  cmd_ta.value( "Citizenship0"  ) = strFor( Citizenship );
  cmd_ta.AddParam( "BalAcc0",       RSDBP_IN );  cmd_ta.value( "BalAcc0"       ) = BalAcc;

  cmd_ta.AddParam( "FNCash1",       RSDBP_IN );  cmd_ta.value( "FNCash1"       ) = FNCash;
  cmd_ta.AddParam( "isCur1",        RSDBP_IN );  cmd_ta.value( "isCur1"        ) = FlagCur;
  cmd_ta.AddParam( "TypeAccount1",  RSDBP_IN );  cmd_ta.value( "TypeAccount1"  ) = TypeAccount;
  cmd_ta.AddParam( "CodeCurrency1", RSDBP_IN );  cmd_ta.value( "CodeCurrency1" ) = CodeCurrency;
  cmd_ta.AddParam( "Citizenship1",  RSDBP_IN );  cmd_ta.value( "Citizenship1"  ) = strFor( Citizenship );
  cmd_ta.AddParam( "BalAcc1",       RSDBP_IN );  cmd_ta.value( "BalAcc1"       ) = BalAcc;
  cmd_ta.AddParam( "date",          RSDBP_IN );  cmd_ta.value( "date"          ) = DateReport;

  cmd_ta.execute;

  rs_ta = RsdRecordSet( cmd_ta );
  if ( rs_ta.moveNext )
    TKindInSum      = rs_ta.value( "EstimIn" );
    TKindOutSum     = rs_ta.value( "EstimOut" );
    TKindOutOverSum = rs_ta.value( "EstimOverOut" );
    TKindOutputRest = rs_ta.value( "EstimRest" );
  end;


  TKindInputRest = TKindOutputRest - TKindInSum + TKindOutSum + TKindOutOverSum;

  [|------------------------------------------------------------------------------------------------------------------------|];
  [|Итого по документам                |################|################|################|################|################|]
  ( KindInputRest:16:2, KindInSum:16:2, KindOutSum:16:2, KindOutOverSum:16:2, KindOutputRest:16:2 );
  if (KindOutSum_97 != 0)
  [|                в т.ч. до 01.01.97 |                |                |################|                |                |]
    ( KindOutSum_97:16:2 );
  end;

  [|Итого по виду вклада               |################|################|################|################|################|]
  ( TKindInputRest:16:2, TKindInSum:16:2, TKindOutSum:16:2, TKindOutOverSum:16:2, TKindOutputRest:16:2 );
  [|------------------------------------------------------------------------------------------------------------------------|];


  KindInputRest  = $0L;
  KindOutputRest = $0L;
  KindInSum      = $0L;
  KindOutSum     = $0L;
  KindOutSum_97  = $0L;
  KindOutOverSum = $0L;

  TBAInputRest   = TBAInputRest   + TKindInputRest;
  TBAOutputRest  = TBAOutputRest  + TKindOutputRest;
  TBAInSum       = TBAInSum       + TKindInSum;
  TBAOutSum      = TBAOutSum      + TKindOutSum;
  TBAOutOverSum  = TBAOutOverSum  + TKindOutOverSum;
  /************************************************/
  TSumInputRest  = TSumInputRest  + TKindInputRest;
  TSumOutputRest = TSumOutputRest + TKindOutputRest;
  TSumInSum      = TSumInSum      + TKindInSum;
  TSumOutSum     = TSumOutSum     + TKindOutSum;
  TSumOutOverSum = TSumOutOverSum + TKindOutOverSum;

end;



/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/
//println(time);

  var cmd, rs;
  var cmd_struct;
  var cmd_offset, rs_offset;

  var cur_BalAcc,  cur_TypeAccount,  cur_Currency,  cur_referenc; 
  var save_BalAcc, save_TypeAccount, save_Currency, save_referenc, save_Citizenship, save_account; 
  var TypeOper, ApplType;
  var circle, lastIter, stat;



  var InputRest  = $0L;
  var OutputRest = $0L;
  var InSum      = $0L;
  var OutSum     = $0L;
  var OutSum_97  = $0L;
  var OutOverSum = $0L;

  cmd_struct = RsdCommand( "begin rsb_struct.readstruct( ? ); end;" );
  cmd_struct.addParam( "name", RSDBP_IN );  cmd_struct.value( "name" ) = "dsbdepdoc_2";
  cmd_struct.execute;
  
  cmd_offset = RsdCommand(
     "select t_offset offset " +
       "from fmt_fields " +
      "where t_fmtid = (select t_id " +
                         "from fmt_names " +
                        "where lower (t_name) = 'dsbdepdoc_2') " +
        "and lower (t_name) = 't_globperc' " );
  cmd_offset.execute;
  rs_offset = RsdRecordSet( cmd_offset );
  if ( rs_offset.moveNext )
    offset = rs_offset.value( "offset" );
  end;

  cmd = RsdCommand(
      "select   intermediatequery.*, " +
               "case numgrp.t_groupid " +
                  "when intermediatequery.groupnumb " +
                     "then numgrp.t_balacc2 " +
                  "else numgrp.t_balacc2notresid " +
               "end balacc, " +
               "case numgrp.t_groupid " +
                  "when intermediatequery.groupnumb " +
                     "then 0 " +
                  "else 1 " +
               "end Citizenship, " +
               "dtyp.t_name name, cur.t_code_currency code_currency, " +
               "cur.t_externalcode externalcode, cur.t_name_currency name_currency, " +
               "(select nvl " +
                          "(sum (rsb_struct.getmoney ('t_SummaOverEstimPrevYear', " +
                                                     "t_unionpart, " +
                                                     "-? " +
                                                    ") " +
                               "), " +
                           "0 " +
                          ") " +
                  "from dsbdepdoc_dbt " +
                 "where t_referenc = referenc " +
                   "and t_date_document = ?  " +
                   "and t_typeoper = 72 " +
                   "and t_mode != 4) summaoverestimprevyear " +
          "from (select   estim.t_referenc referenc, estim.t_typeoper typeoper, " +
                         "estim.t_appltype appltype, sum (estim.t_summapay_out) summapay_out, " +
                         "sum (estim.t_summapay_in) summapay_in, " +
                         "sum (estim.t_summapay_rest) summapay_rest, " +
                         "to_number (min (depos.t_groupnumb)) groupnumb, " +
                         "min (depos.t_iscur) iscur, " +
                         "min (depos.t_type_account) type_account, " +
                         "min (depos.t_code_currency) currency, " +
                         "min (depos.t_account) account " +
                    "from ddepositr_dbt depos, dpcestim_dbt estim " +
                   "where depos.t_iscur = ? " +
                     "and depos.t_fncash = ? " +
                     "and ascii (depos.t_groupnumb) > 40 " +
                     "and estim.t_referenc = depos.t_referenc " +
                     "and estim.t_dateoperat = ? " +
                     "and estim.t_action < 2 " +
                     "and estim.t_mode != 4 " +
                     "and estim.t_typeoper in (38, 39) " +
                "group by estim.t_referenc, estim.t_typeoper, estim.t_appltype " +
                  "having sum (estim.t_summapay_out) != 0 " +
                      "or sum (estim.t_summapay_in) != 0) intermediatequery, " +
               "dsbnumgrp_dbt numgrp, " +
               "dsb_dtyp_dbt dtyp, " +
               "dcurrency_dbt cur " +
         "where (   numgrp.t_groupid =  intermediatequery.groupnumb " +
                "or numgrp.t_groupid = -intermediatequery.groupnumb " +
               ") " +
           "and intermediatequery.iscur = dtyp.t_flagcur " +
           "and intermediatequery.type_account = dtyp.t_kind " +
           "and intermediatequery.currency = cur.t_code_currency " +
      "order by cur.t_code_currency, balacc, dtyp.t_kind, cur.t_code_currency, intermediatequery.referenc " );
  cmd.AddParam( "offset",      RSDBP_IN );   cmd.value( "offset"      ) = offset;
  cmd.AddParam( "DateReport0", RSDBP_IN );   cmd.value( "DateReport0" ) = DateReport;
  cmd.AddParam( "FlagCur",     RSDBP_IN );   cmd.value( "FlagCur"    ) = FlagCur;
  cmd.AddParam( "FNCash",      RSDBP_IN );   cmd.value( "FNCash"     ) = FNCash;
  cmd.AddParam( "DateReport",  RSDBP_IN );   cmd.value( "DateReport" ) = DateReport;

  cmd.execute;

  save_BalAcc = 0;
  save_TypeAccount = "";
  save_Currency = -1; 
  save_referenc = 0;
  save_Citizenship = 1;
  save_account = "";

  WriteHeadReport( );

  rs = RsdRecordSet( cmd );

  circle = true;
  lastIter = false;

  stat = rs.moveNext;
  if ( stat)
  while ( circle )
    if ( lastIter )
      circle = false;
    else
      cur_BalAcc      = rs.value( "balacc"       );
      cur_TypeAccount = rs.value( "type_account" );
      cur_Currency    = rs.value( "currency"     ); 
      cur_referenc    = rs.value( "referenc"     );

      TypeOper = rs.value( "typeoper" );
      ApplType = rs.value( "appltype" );
    end;

    if ( ( ( save_referenc != cur_referenc ) and ( save_referenc != 0 ) ) or lastIter )

      [|#####|#                            |################|################|################|################|################|]
      ( NumPP:5, Acc_Form( save_account ):f, InputRest:16:2, InSum:16:2, OutSum:16:2, OutOverSum:16:2, OutputRest:16:2 );
      if ( OutSum_97 != 0 )
		[|     |          в т.ч. до 01.01.97 |                |                |################|                |                |]
        (OutSum_97:16:2);
      end;

      NumPP = NumPP + 1;

      InputRest  = $0L;
      OutputRest = $0L;
      InSum      = $0L;
      OutSum     = $0L;
      OutSum_97  = $0L;
      OutOverSum = $0L;

    end;

    if ( ( ( ( save_TypeAccount != cur_TypeAccount ) or 
             ( save_BalAcc != cur_BalAcc )              ) and  // На случай, если для одного ВВ есть разные балансовые
           ( save_TypeAccount != "" )                           ) or lastIter )
        WriteSummaryForKind( save_BalAcc, save_TypeAccount, save_Citizenship, save_Currency );
    end;

    if  ( ( ( save_BalAcc != cur_BalAcc ) and ( save_BalAcc != 0 ) ) or lastIter )
        WriteSummaryForBA ( save_BalAcc );
    end;

    if ( ( ( save_Currency != cur_Currency ) and ( save_Currency != -1 ) ) or lastIter )
        WriteSummaryForCurrency;
    end;

    save_referenc = cur_referenc;
    if ( not lastIter )
      save_account = rs.value( "account" );

      if ( ( TypeOper == 39 ) AND ( ApplType == 1 ) )
        OutOverSum = OutOverSum + rs.value( "SummaPay_Out" );
      elif ( TypeOper == 39 )
        OutSum     = OutSum     + rs.value( "SummaPay_Out" );
      elif ( TypeOper == 38 )
        InSum      = InSum      + rs.value( "SummaPay_In" );
      end;
      OutputRest = rs.value( "SummaPay_Rest" );

      if ((InSum != 0) OR (OutSum != 0) OR (OutOverSum != 0))
        OutSum_97  = OutSum_97  + rs.value( "SummaOverEstimPrevYear" );
      end; 

    end;

    InputRest  = OutputRest - InSum + OutSum + OutOverSum;

    KindInputRest  = KindInputRest  + InputRest;
    KindOutputRest = KindOutputRest + OutputRest;
    KindInSum      = KindInSum      + InSum;
    KindOutSum     = KindOutSum     + OutSum;
    KindOutSum_97  = KindOutSum_97  + OutSum_97;
    KindOutOverSum = KindOutOverSum + OutOverSum;
    /*******************************************/
    BAInputRest    = BAInputRest    + InputRest;
    BAOutputRest   = BAOutputRest   + OutputRest;
    BAInSum        = BAInSum        + InSum;
    BAOutSum       = BAOutSum       + OutSum;
    BAOutSum_97    = BAOutSum_97    + OutSum_97;
    BAOutOverSum   = BAOutOverSum   + OutOverSum;
    /*******************************************/
    SumInputRest   = SumInputRest   + InputRest;
    SumOutputRest  = SumOutputRest  + OutputRest;
    SumInSum       = SumInSum       + InSum;
    SumOutSum      = SumOutSum      + OutSum;
    SumOutSum_97   = SumOutSum_97   + OutSum_97;
    SumOutOverSum  = SumOutOverSum  + OutOverSum;

    if ( ( save_Currency != cur_Currency ) and not lastIter )
      WriteHeadForCurrency( rs.value( "ExternalCode" ), rs.value( "Name_Currency" ) );
      save_Currency = cur_Currency;
    end;

    if ( ( 
          ( save_TypeAccount != cur_TypeAccount ) or
          ( save_BalAcc != cur_BalAcc ) ) and not lastIter ) 
      WriteHeadForType ( rs.value( "balacc" ), rs.value( "name" ) );
      save_TypeAccount = cur_TypeAccount;
    end;

    if  ( save_BalAcc != cur_BalAcc ) 
      save_BalAcc = cur_BalAcc;
    end;

    if ( not lastIter )
      save_Citizenship = rs.value( "Citizenship" );
    end;

    NAccs = NAccs + 1;
    Message( "Обработано счетов: " + NAccs );

   stat = rs.moveNext;
   if ( not stat )
     lastIter = true;
   end;

  end;
  end;

//println(time);
end; 
