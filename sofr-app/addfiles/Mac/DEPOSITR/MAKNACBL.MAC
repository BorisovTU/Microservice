/*********************************************************
*  Retail 1.0                                            *
*                                                        *
*  Макрос при формировании номера  счета вкладчика       *
*  для валютных счетов Белгородского СберБанка           *
*                                                        *
*                                                        *
*  13.02.98                                              *
*********************************************************/

 record dr      (depositr);  /* Счет вкладчика           */
 file   SB_DTYP (sb_dtyp );  /* Справочник типов вкладов */
 file   curr    (currency);  /* Справочник валют         */

 const BankMFO       = "041403633"; /* БИК банка                */
 const CodTerBank    = "07";        /* Код тербанка             */
 const LengthBA      = 5;           /* Длина балансового счета  */
 const LengthAccount = 20;          /* Длина счета              */



/*******************************************************************
	    Найти тип вклада в справочнике вкладов
*******************************************************************/
macro GetDepTypep

  KeyNum(SB_DTYP,2);
  SB_DTYP.FlagCur = dr.IsCur;
  SB_DTYP.Kind    = dr.Type_Account;

  return GetEQ(SB_DTYP);

end;


/*******************************************************************
		Найти валюту в справочнике валют
*******************************************************************/
macro GetCurrency

  KeyNum(curr,0);
  curr.Code_Currency = dr.Code_Currency;

  return GetEQ(curr);

end;


/*******************************************************************
	       Получить ключевой разряд для счета
*******************************************************************/
macro DefKeyRazr
(
 Account
)

  var   BegString;
  var   i,j,k,l,m;
  array Weigth;

  BegString = SubStr(BankMFO,StrLen(BankMFO) - 2,3) + Account;

  i = 1;
  j = 7;
  while (i <= StrLen(BegString))
    k = Int(SubStr(BegString,i,1)) * j;
    l = String(k);
    m = StrLen(l);
    Weigth(i - 1) = Int(SubStr(l,m,1));
    if (j == 7)
      j = 1;
    elif (j == 1)
      j = 3;
    else
      j = 7;
    end;
    i = i + 1;
  end;

  i = 0;
  k = 0;
  while (i < ASize(Weigth))
    k = k + Weigth(i);
    i = i + 1;
  end;
  l = String(k);
  m = StrLen(l);
  k = Int(SubStr(l,m,1));
  k = k * 3;
  l = String(k);
  m = StrLen(l);
  l = SubStr(l,m,1);

  return l;

end;


/****************************************************************
****************************************************************/
macro MakNAcBl    /* !!! Имя макропроцедуры должно соответствовать !!! */
(                 /* !!! имени файла                               !!! */
 depos
)

  var stat = TRUE;
  var Account;
  var BalAcc;
  var CodCurISO;
  var NumAcc;
  var IdChar;
  var KeyRazr;

  setbuff(dr,depos);

  if (dr.IsCur == 0)
    stat = FALSE;
  end;

  /* Найдем вид вклада */
  if (stat)
    stat = GetDepTypep();
    if (NOT stat)
      MsgBox(" Не найден вид вклада " + Trim(dr.Type_Account) + " ");
    end;
  end;

  /* Найдем код валюты */
  if (stat)
    stat = GetCurrency();
    if (stat)
      if (Trim(curr.ExternalCode) != "")
	CodCurISO = Trim(curr.ExternalCode);
	while (StrLen(CodCurISO) < 3)
	  CodCurISO = "0" + CodCurISO;
	end;
      else
	stat = FALSE;
	MsgBox(" У валюты с кодом " + String(dr.Code_Currency) + " не задан код ISO ");
      end;
    else
      MsgBox(" Не найдена валюта с кодом " + String(dr.Code_Currency) + " ");
    end;
  end;

  if (stat)
    if (GetTrue(TRUE," Владелец счета резидент ? "))
      BalAcc = Trim(SB_DTYP.BalAcc);
    else
      BalAcc = Trim(SB_DTYP.BalAccNotRez);
    end;
    if (StrLen(BalAcc) != LengthBA)
      MsgBox(" Длина балансового счета должна быть равна " + String(LengthBA) + " ");
      stat = FALSE;
    end;
  end;

  if (stat)
    if (BalAcc == "")
      stat = FALSE;
      MsgBox(" У вида вклада \"" + Trim(dr.Type_Account) + "\" не задан балансовый счет ");
    end;
  end;

  /* Определение идентификационного символа счета */
  if (stat)
    if (Trim(SB_DTYP.IdentChar) == "")
      stat = FALSE;
      MsgBox(" У вида вклада \"" + Trim(dr.Type_Account) + "\" не задан идент. символ ");
    else
      IdChar = Trim(SB_DTYP.IdentChar);
      while (StrLen(IdChar) < 2)
	IdChar = "0" + IdChar;
      end;
    end;
  end;

  /* Формирование номера счета */
  if (stat)

    /* BBBBB     VVV     K     TT       QQ    NNNNNNN */
    /* Бал.счет  Валюта  Ключ  ТерБанк  Вклад Номер   */

    NumAcc = String(dr.Number);
    /* Вначале на место ключа и идентификатора вклада ставим нули */
    /*        Бал.счет Валюта      Ключ  ТерБанк      Вклад       */
    Account = BalAcc + CodCurISO + "0" + CodTerBank + "00";
    /* Добавляем порядковый номер счета */
    while ((LengthAccount - StrLen(Account) - StrLen(NumAcc)) > 0)
      Account = Account + "0";
    end;
    Account = Account + NumAcc;
    /* Определим ключевой разряд */
    KeyRazr = DefKeyRazr(Account);
    /* Вставим в номер счета ключевой разряд и идентификатор вклада */
    StrSet(Account, 9,KeyRazr);
    StrSet(Account,12,IdChar);
  end;

  if (stat)
    dr.Account = Account;
  end;

end;
