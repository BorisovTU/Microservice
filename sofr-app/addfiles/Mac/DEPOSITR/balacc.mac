import RSD;

private var cachedKind, cachedFlagCur, cachedBalAcc, cachedBalAccNotResid;

macro FindBalAccByAccType( kind, flagCur, citizenship )

  var cmd, 
      rs, 
      balAcc, balAccNotResid; 

  if ( ( kind == cachedKind ) and ( flagCur == cachedFlagCur ) )
    if ( not citizenship )
      return cachedBalAcc;
    else
      return cachedBalAccNotResid;
    end;
  end;  

  cmd = RsdCommand( 
         "select numgrp.t_balacc2, t_balacc2notresid, to_char(numgrp.t_groupid) groupid "
          "from dsbnumgrp_dbt numgrp, dsbtypgrp_dbt typgrp "
         "where numgrp.t_groupid = typgrp.t_groupid "
           "and typgrp.t_flagcur = ? "
           "and typgrp.t_kind = ? " );

  cmd.addParam( "p_flagcur", RSDBP_IN );
  cmd.value( "p_flagcur" ) = flagcur;
  cmd.addParam( "p_kind", RSDBP_IN );
  cmd.value( "p_kind" ) = kind;
  cmd.execute();

  rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    balAcc = rs.value( "t_balacc2" );
    balAccNotResid = rs.value( "t_balacc2notresid" );
  else
    return "";
  end;

  if ( balAcc == null )
    balAcc = "";
  end; 

  if ( balAccNotResid == null )
    balAccNotResid = "";
  end; 

  if ( rs.moveNext )
    return "";
  else
    cachedKind = kind;
    cachedFlagCur = flagCur;
    cachedBalAcc = balAcc;
    cachedBalAccNotResid = balAccNotResid;
    if ( not citizenship )
      return balAcc;
    else
      return balAccNotResid;
    end;
  end;
end; 
