/*
               **************************
               *  R-Style SoftWare Lab. *
               **************************

    RS-Retail 2.0
    Сбербанк
 Вклад "Компенсационный-2"

 Откат наличного открытия счета
 (удаление и сторнирование операци)
------------------------------------------------------------
 Цель:
  - снять отметку у счета, по которому прошла компенсация,
    что некоторая сумма положена на Компенсационный
    (во избежание дублирования)
------------------------------------------------------------

  Ромодин Александр Васильевич
  06.06.2000
*/

import deprintr;
/*---------- Инициализируемые структуры ------------------*/
record ALG_DEPOSITOR ("depositr.dbt");
record ALG_SBDEPDOC  ("sbdepdoc.dbt");
record ALG_PARAM     ("sb_algop.dbt");

file   AUXINFO       ("auxinfo.dbt") write;
file   COMPENS       ("compens.dbt") write;
file   DEPOSITOR     ("depositr.dbt");

record AUXINFO_2("auxinfo.2");    /* Доп.информация по компенсациям */

/*---------- Коды возврата макроса -----------------------*/
const
  OK_RES   =  0,  /* продолжать проводку */
  SKIP_RES =  1,  /* пропустить выполнение предопределенного модуля */
  END_RES  =  2,  /* завершить проводку */
  ERR_RES  = -1;  /* аваpийно завершить проводку */
/*--------------------------------------------------------*/
var ErrMsg = "";
/*--------------------------------------------------------*/
  ALG_RESULT = ERR_RES;  /* Мы- пессимисты */


/**********************************************************/

Macro FindAuxInfo (Ref)
  KeyNum (AUXINFO,0);
  AUXINFO.Reference = Ref;
  AUXINFO.InfoType = 3;
  AUXINFO.Date     = Date(0,0,0);
  return GetEQ(AUXINFO);
end;
/**********************************************************/

macro FindAccByNum (FNCash,Account)
  KeyNum (DEPOSITOR,7);
  DEPOSITOR.FNCash  = FNCash;
  DEPOSITOR.Account = Account;
  return GetEQ (DEPOSITOR);
end;
/**********************************************************/

macro FindCompensRecord
/*
  Поиск записи по компенсации
*/
  var stat;

  ClearRecord (COMPENS);
  KeyNum (COMPENS,4);
  COMPENS.FNCash      = ALG_DEPOSITOR.FNCash;
  COMPENS.Kind        = 999;
  COMPENS.Date        = ALG_SBDEPDOC.Date_Document;
  COMPENS.DestAccount = ALG_DEPOSITOR.Account;
  stat = GetEQ(COMPENS);
  return stat;
end;
/**********************************************************/

macro MakeAuxInfo
/*
  Правка записи по компенсации для счета,
  по которому компенсация была начислена
*/
  var stat;
  SetRecordAddr(AUXINFO_2,AUXINFO,0,0,TRUE);
  AUXINFO_2.Include     = AUXINFO_2.Include     - ALG_SBDEPDOC.InSum;
  AUXINFO_2.CashInclude = AUXINFO_2.CashInclude - ALG_SBDEPDOC.InSum;
  stat = Update(AUXINFO);
  return stat;
end;
/**********************************************************/

macro TrnProc
/*
  Транзакция отката открытия счета
*/
  var stat = True;
  var Storn;
/*
  1.Удаление документа
*/
  if (stat)
    if (ALG_PARAM.PrmC)
      Storn = True;
    else
      Storn = False;
    end;
      Stat = DeleteDocument (ALG_SBDEPDOC.iApplicationKind,
                             ALG_SBDEPDOC.ApplicationKey, Storn);
  end;
/*
  2.Правка записи по компенсации в счете, по которому компенсация
  была зачислена
*/
  if (stat)
    stat = FindCompensRecord ();
    if (NOT stat)
      ErrMsg = "Ошибка при поиске записи в реестре компенсаций";
    end;
  end;
  if (stat)
    stat = FindAccByNum (COMPENS.FNCash,COMPENS.Account);
    if (NOT stat)
      ErrMsg = "Ошибка при поиске счета "+COMPENS.Account+",|на который начислена компенсация";
    end;
  end;
  if (stat)
    stat = FindAuxInfo (DEPOSITOR.Referenc);
    if (NOT stat)
      ErrMsg = "Ошибка при поиске записи по компенсации|для счета "+COMPENS.Account;
    end;
  end;
  if (stat)
    stat = MakeAuxInfo ();
    if (NOT stat)
      ErrMsg = "Ошибка корректировки записи по компенсации";
    end;
  end;
/*
  3. Удаление записи из реестра
*/
  if (stat)
    stat = Delete(COMPENS);
    if (NOT stat)
      ErrMsg = "Ошибка при удалении записи из реестра компенсаций";
    end;
  end;
/*
  Не дай-то Бог!
*/
  if (NOT stat)
    AbortTrn ();
  end;
end;
/***********************************************************
  Точка входа
*/
  var stat = true;
  var OpenAux = False;

  LoopInTrn(TRUE);
  if (stat)
    stat = ProcessTrn(1,"TrnProc",AUXINFO,COMPENS);
  end;
  if (NOT stat)
    if (ErrMsg != "")
      MsgBox (ErrMsg);
    else
      MsgBox ("Ошибка при откате наличного открытия счета");
    end;
  else
    ALG_RESULT = END_RES;
  end;
end;
/* ===== Конец макроса ===== */
