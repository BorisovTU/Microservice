/*-----------------------------------------------------------------------*-

 File Name   : rslscr.mac

 Copyright (c) 1991 - 1996 by R-Style Software Lab.
 All Rights Reserved.

-*- History -------------------------------------------------------------*-
 November 27,1995  Sergei Kubrin (K78) - Create file
 April 25,1996   Serg Kubrin (K78)
    Add UserFill macro function
-*-----------------------------------------------------------------------*/

const UP = 328,DOWN = 336,PGUP = 329,PGDOWN = 337, HOME = 327,ENDKEY = 335,
      ENTER = 13,F9 = 323;

var   lastUsed, Nrec,Nfields,Scrff;
array Apos;
array DlgFields;
array FileFields;

/* Опpеделение паpаметpов диалоговой панели и НОМЕРОВ полей файла для вывода */
/* ------------------------------------------------------------------------- */
macro SetScroll (fl)
  Scrff = fl;
  var tmp,i,j = 0;

  i = 1;
  while (GetParm (i,tmp))
    i = i + 1
  end;
  Nfields = (i - 1) / 2;

  Asize (DlgFields,Nfields);
  Asize (FileFields,Nfields);

  i = 1;
  while (GetParm (i,tmp))
    DlgFields (j) = tmp;
    GetParm (i+1,tmp);
    if (ValType (tmp) != V_UNDEF)
      FileFields (j) = FldIndex (Scrff,tmp)
    else
      FileFields (j) = FldIndex (Scrff,DlgFields (j))
    end;
    j = j + 1;
    i = i + 2
  end;
end;

macro UserFill (dlg)
/*  Эту макропроцедуру можно заменять при помощи
    ReplaceMacro для выполнения
    специфичной перерисовки полей скролинга
    См. scr.mac
*/
end;

macro SetDlgFields (dl,row)
  var i = 0;
  while (i < Nfields)
    dl (FldIndex (dl,DlgFields (i) + row)) = Scrff (FileFields (i));
    i = i + 1
  end
end;

macro FillDown (dl,pos)

  macro ClearDlgFields (row)
    var i = 0;
    while (i < Nfields)
      dl (FldIndex (dl,DlgFields (i) + row)) = "";
      i = i + 1
    end
  end;

  var stat,i = 0;

  if (not pos)
     return
  end;

  stat = GetDirect (Scrff,pos);

  while (stat and (i < Nrec))
    lastUsed = i;
    Apos (i) = GetPos (Scrff);
    SetDlgFields (dl,i);
    stat = next (Scrff);
    i = i + 1
  end;

  while (i < Nrec)
    ClearDlgFields (i);
    i = i + 1
  end;

  UserFill (dl);
  UpdateFields (dl)
end;

macro FillUp (dl,pos)
  var stat,i = Nrec-1;

  if (not pos)
     return
  end;

  stat = GetDirect (Scrff,pos);

  while ((i >= 0) and stat)
    Apos (i) = GetPos (Scrff);
    SetDlgFields (dl,i);
    stat = prev (Scrff);
    i = i - 1
  end;

  if (i >= 0)
    FillDown (dl,Apos (i + 1))
  else
    UserFill (dl);
    UpdateFields (dl);
    lastUsed = Nrec - 1
  end
end;

macro FindRow (dl,id)
   macro TestCol (name)
     var len = StrLen (name), str;
     if (strUpr(name) == strUpr( SubStr (FldName (dl,id),1,len) ))
       str = SubStr (FldName (dl,id),len+1);
       if (str)
         len = Int (str);
         if ((len > 0) or ((len == 0) and  (str == "0")))
           return len
         end
       end
     end;
     return -1
   end;
   var j = 0,row = -1;
   while ((row == -1) and (j < Nfields))
     row = TestCol (DlgFields (j));
     j = j + 1
   end;
   return row
end;

macro FindCol (dl,id)
  var j = 0;
  while (j < Nfields)
    if (DlgFields (j) == SubStr (FldName (dl,id),1,StrLen (DlgFields (j))))
      return j
    end;
    j = j + 1
  end;
  return -1
end;



macro ScrollMes (dl,cmd,id,key)

  var curRow;

  macro CalcNrec
    Nrec = 0;
    while (FldIndex (dl,DlgFields (0) + Nrec) != -1)
      Nrec = Nrec + 1
    end;
    Asize (Apos,Nrec)
  end;



  macro CopyDown
    var i = 0,j;
    while (i < Nrec - 1)
      j = 0;
      while (j < Nfields)
         dl (FldIndex (dl,DlgFields (j) + i)) = dl (FldIndex (dl,DlgFields (j) + (i+1)));
         j = j + 1
      end;
      Apos (i) = Apos (i + 1);
      i = i + 1
    end
  end;

  macro CopyUp
    var i = Nrec - 1,j;
    while (i > 0)
      j = 0;
      while (j < Nfields)
         dl (FldIndex (dl,DlgFields (j) + i)) = dl (FldIndex (dl,DlgFields (j) + (i-1)));
         j = j + 1
      end;
      Apos (i) = Apos (i - 1);
      i = i - 1
    end
  end;



   macro OneDown
     var stat;
     if (lastUsed != Nrec - 1)
        return
     end;

     stat = GetDirect (Scrff,Apos (lastUsed));
     if (stat and next (Scrff))
       CopyDown;
       SetDlgFields (dl,Nrec - 1);
       Apos (Nrec - 1) = GetPos (Scrff);
       UserFill (dl);
       UpdateFields (dl)
     end
   end;

   macro OneUp
      var stat;
      stat = GetDirect (Scrff,Apos (0));
      if (stat and prev (Scrff))
        CopyUp;
        SetDlgFields (dl,0);
        Apos (0) = GetPos (Scrff);
        if (lastUsed < Nrec - 1)
          lastUsed = lastUsed + 1
        end;
        UserFill (dl);
        UpdateFields (dl)
      end;
   end;

/*
   macro InSet (ar)
     var j = 0;
     while (j < Nfields)
       if (ar (j) == id)
         return True
       end;
       j = j + 1
     end;
     return False
   end;
*/

   if (cmd == DLG_INIT)
      CalcNrec;
      rewind (Scrff);
      if (next (Scrff))
        FillDown (dl,GetPos (Scrff))
      end
   elif (cmd == DLG_KEY)
      if ((key == PGDOWN) and (FindCol (dl,id) != -1))
         FillDown (dl,Apos (lastUsed))
      elif ((key == PGUP) and (FindCol (dl,id) != -1))
         FillUp (dl,Apos (0))
      elif ((key == DOWN)  and (FindRow (dl,id) == Nrec-1))
         OneDown;
         return CM_IGNORE
      elif ((key == UP) and (FindRow (dl,id) == 0))
         OneUp;
         return CM_IGNORE
      elif (key == HOME)
         curRow = FindCol (dl,id);
         if (curRow != -1)
           rewind (Scrff);
           if (next (Scrff))
             FillDown (dl,GetPos (Scrff));
             SetFocus (dl,FldIndex (dl,DlgFields (curRow) + 0))
           end
         end
      elif (key == ENDKEY)
         curRow = FindCol (dl,id);
         if (curRow != -1)
           rewind (Scrff);
           if (prev (Scrff))
             FillUp (dl,GetPos (Scrff));
             SetFocus (dl,FldIndex (dl,DlgFields (curRow) + lastUsed));
           end
         end
      elif (key == ENTER)
         curRow = FindRow (dl,id);
         if (curRow >= 0)
           if (curRow <= lastUsed)
             GetDirect (Scrff,Apos (curRow));
             return CM_SAVE
           elif (curRow < Nrec)
             return CM_IGNORE;
           end
         end
      elif (key == F9)
         return CM_IGNORE;
      end
   elif (cmd == DLG_DESTROY)
     Asize (DlgFields,0);
     Asize (FileFields,0);
     Asize (Apos,0);
   end;
   return CM_DEFAULT;
end;
