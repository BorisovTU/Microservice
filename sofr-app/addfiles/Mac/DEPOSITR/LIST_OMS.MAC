/*

  ---------------
  R-Style Softlab
  ---------------
  RS-Retail

  Список мерных слитков, принятых на обезличенные металлические счета

*/

import CommonInter, BankInter, globals, RSD;

file DeskAux( "desk_aux.dbt" ) key 0 write;
file Person ( "person.dbt"  );

record IngotAux( "desk_aux.1" );
record ALG_SBDEPDOC("sbdepdoc.dbt");

const ALG_SHORT_EXTEND_QUALITY_TYPE = 1204,
      ALG_SHORT_QUALITY_TYPE = 1203,
      CIC_MATERIAL_ME = 12,
      
      TYPE_RES_PAY = 0,
      CI_INGOT = 5,

      SET_CHAR = "X";

var ClientList = TClientList;

//------------------------------------------------------------------------------
//
//------------------------------------------------------------------------------
/* Печатает шапку
 * Metallname - название металла( золото, серебро и т.п )
 * isStandart - признак стандартных или мерных слитков
 */
 macro ListMet_head( Metallname, isStandart )
    var metName = "", ingotType = "";

    // Металл
    if( Metallname == "Золото" )
        metName = "золотых";
    elif( Metallname == "Серебро" )
        metName = "серебрянных";
    elif( Metallname == "Палладий" )
        metName = "палладиевых";
    elif( Metallname == "Платина" )
        metName = "платиновых";
    end;  

    // Тим слитка
    if( isStandart )
        ingotType = "стандартных";
    else
        ingotType = "мерных";
    end;

    [
    Место для штампа
    учреждения СБ РФ
        
        
                       Список ########### ########### слитков,
                принятых на обезличенные металлические счета физических лиц
                                  ###############################

         +-----------------------------------------------------------------------------------------------------------+
         | N п/п |  ФИО клиента            | Масса     | Номер                | Качество,       | Завод-изготовитель |
         |       |                         | слитка,   | слитка               | проба           |                    |
         |       |                         | грамм     |                      |                 |                    |
         +-------+-------------------------+-----------+----------------------+-----------------+--------------------+]
             ( metName, ingotType, {curdate}:m );

 end;
 
//------------------------------------------------------------------------------
//
//------------------------------------------------------------------------------

//------------------------------------------------------------------------------
// Печатает конец Отчета
//------------------------------------------------------------------------------
macro ListMet_end()
    Person.oper = {oper};
    if( GetEQ(person) )
     [* при принятии от одного клиента нескольких слитков, они отражаются по
     отдельности, после чего по ним подводится итог:
        в графе "N п/п" указывается: "Итого", в графе "Масса слитка, грамм"
     указывается суммарный вес принятых слитков.


     Итого _______________________________ граммов

     Контролер ______________________(#########################)
                   (подпись)             (фамилия, инициалы)

     Кассир _________________________(_________________________)
                   (подпись)             (фамилия, инициалы)


      М.П.


     С данными Операционного дневника ф. 24-дм сверено:

     Бухгалтер ______________________(_________________________)
                   (подпись)             (фамилия, инициалы)    




    ]( Person.name:c );
    end;
end;

//-------------------------------------------------------------------------------
//
//------------------------------------------------------------------------------

//------------------------------------------------------------------------------
// Основной макрос
//------------------------------------------------------------------------------
 macro ListMet( isStandart )
    var cmd, rs, cmd_1, rs_1, 
        FIO="", Qual_doubl=" ", serial_num=" ", manuf=" ", ingotType = " ", 
        count=1, sumOfMass=0, mass=0, qualFlag,
        temp_date;

    // Достаем ФИО клиента
    if ( ClientList.GetRecord(ALG_SBDEPDOC.CodClient) )
       FIO = ClientList.CurRec.rec.name1 + " " + SubStr( ClientList.CurRec.rec.name2, 1, 1 ) + ". " + SubStr( ClientList.CurRec.rec.name3, 1, 1 ) + ".";
    end;


    // Вытаскиваем список внесенных слитков
    cmd = RsdCommand(   " SELECT    ddesk_aux_dbt.t_branch, ddesk_aux_dbt.t_valueref, ddesk_aux_dbt.t_date, "
                        "           ddesk_aux_dbt.t_isgone, ddesk_aux_dbt.t_series, ddesk_aux_dbt.t_number, "
                        "           dci_misc_dbt.t_qualityref, mat.t_string, dcurrency_dbt.t_code_currency, "
                        "           probe.t_double, mat.t_integer, dci_misc_dbt.t_materialref, dci_misc_dbt.T_USEWEIGHTTYPE"
                        " FROM dsb_casln_dbt casln2, dsb_casln_dbt, dbunch_dbt, ddesk_aux_dbt, dci_misc_dbt, "
                        "      dci_code_dbt probe, dci_code_dbt mat, dcurrency_dbt, dret_op_dbt "
                        " WHERE dci_misc_dbt.t_type = ? AND dbunch_dbt.t_type = ? "
                        "   AND casln2.t_applicationkind2 = ? AND casln2.t_applicationkey2 = ? "
                        "   AND dsb_casln_dbt.t_applicationkind1 = casln2.t_applicationkind1 "
                        "   AND dsb_casln_dbt.t_applicationkey1 = casln2.t_applicationkey1 "
                        "   AND dret_op_dbt.t_applicationkind = dsb_casln_dbt.t_applicationkind1 "
                        "   AND dret_op_dbt.t_applicationkey = dsb_casln_dbt.t_applicationkey1 "
                        "   AND dbunch_dbt.t_bagappkind = dsb_casln_dbt.t_applicationkind2 "
                        "   AND dbunch_dbt.t_bagappkey = dsb_casln_dbt.t_applicationkey2 "
                        "   AND dci_misc_dbt.t_materialref = dcurrency_dbt.t_materialref "
                        "   AND ddesk_aux_dbt.t_valueref = dsb_casln_dbt.t_refvalue2 "
                        "   AND dci_misc_dbt.t_issueid = ddesk_aux_dbt.t_valuecode "
                        "   AND dbunch_dbt.t_series = ddesk_aux_dbt.t_series "
                        "   AND dci_misc_dbt.t_hallmarkref = probe.t_autokey "
                        "   AND dci_misc_dbt.t_materialref = mat.t_autokey "
                        "   AND ddesk_aux_dbt.t_date = dret_op_dbt.t_date "
                        "   AND dbunch_dbt.t_min = ddesk_aux_dbt.t_number "
                        "   AND dsb_casln_dbt.t_refvalue2 <> 0 " );
                        
    cmd.addParam( "dci_misc_dbt.t_type", RsdBp_in );
    cmd.value       ( "dci_misc_dbt.t_type" ) = CI_INGOT;
    cmd.addParam( "dbunch_dbt.T_TYPE_1", RsdBp_in );
    cmd.value       ( "dbunch_dbt.T_TYPE_1" ) = TYPE_RES_PAY;
    cmd.addParam( "casln2.t_applicationkind2", RsdBp_in );
    cmd.value       ( "casln2.t_applicationkind2" ) = ALG_SBDEPDOC.iApplicationKind;
    cmd.addParam( "casln2.t_applicationkey2",  RsdBp_in );
    cmd.value       ( "casln2.t_applicationkey2" )  = ALG_SBDEPDOC.ApplicationKey;
    
    cmd.execute();
    rs = RsdRecordset( cmd );


    // Для каждого слитка
    while( rs.moveNext() )

        // Вытаскиваем структуру Ingot из DeskAux
        DeskAux.Branch   = rs.value( 0 ); 
        DeskAux.ValueRef = rs.value( 1 );
        DtTmSplit( rs.value( 2 ), temp_date );
        DeskAux.Date     = temp_date;
        DeskAux.IsGone   = rs.value( 3 );
        DeskAux.Series   = rs.value( 4 );
        DeskAux.Number   = rs.value( 5 );
        GetEQ( DeskAux );
        SetRecordAddr( IngotAux, DeskAux, 0, FldOffset( DeskAux, "Info" ), true );
        
        serial_num  = string( RegNumToStr( rs.value( 4 ), rs.value( 5 ) ) );

     // Извращения из-за отсутствия битовых операций. По идее надо было вычленить 3-4 биты
     qualFlag = rs.value( 10 ) / 4;
     qualFlag = qualFlag - ( qualFlag / 4 ) * 4 ;
        if( IngotAux.Produceref != 0 )
            cmd_1 = RsdCommand(" select producer.T_STRING, dnamealg_dbt.t_sznamealg, ingotType.t_string from dci_code_dbt producer, dci_code_dbt ingotType, dnamealg_dbt where "
                               " producer.T_AUTOKEY = ? and dnamealg_dbt.t_itypealg = ? and dnamealg_dbt.t_inumberalg = ? "
                               " and ingotType.t_uplink = ? and ingotType.t_group = ? and ingotType.t_double = ? ");
            cmd_1.addParam("producer.T_AUTOKEY", RsdBp_in);
            cmd_1.value("producer.T_AUTOKEY") = IngotAux.Produceref;
        else
            cmd_1 = RsdCommand(" select dnamealg_dbt.t_sznamealg, ingotType.t_string from dci_code_dbt ingotType, dnamealg_dbt where "
                               " dnamealg_dbt.t_itypealg = ? and dnamealg_dbt.t_inumberalg = ? "
                               " and ingotType.t_uplink = ? and ingotType.t_group = ? and ingotType.t_double = ? ");
        end;
        
        cmd_1.addParam("dnamealg_dbt.t_itypealg", RsdBp_in);
        if( qualFlag  == 0 )
            cmd_1.value("dnamealg_dbt.t_itypealg") = ALG_SHORT_QUALITY_TYPE;
        else 
            cmd_1.value("dnamealg_dbt.t_itypealg") = ALG_SHORT_EXTEND_QUALITY_TYPE;
        end;
        
        cmd_1.addParam("dnamealg_dbt.t_inumberalg", RsdBp_in);
        cmd_1.value("dnamealg_dbt.t_inumberalg") = rs.value( 6 );
        cmd_1.addParam("ingotType.t_uplink", RsdBp_in);
        cmd_1.value("ingotType.t_uplink") = rs.value( 11 ); 
        cmd_1.addParam("ingotType.t_group", RsdBp_in);
        cmd_1.value("ingotType.t_group") = CI_INGOT; 
        cmd_1.addParam("ingotType.t_double", RsdBp_in);
        cmd_1.value("ingotType.t_double") = IngotAux.NominalWeight / 10000; 

        cmd_1.execute(); 
        rs_1 = RsdRecordset(cmd_1);
        
        if( rs_1.moveNext() )
        
            if( IngotAux.Produceref == 0 )
                Qual_doubl  = rs_1.value( 0 )+ ", " + string( rs.value( 9 ) ); 
                manuf       = " ";
                ingotType = rs_1.value( 1 );
            else
                Qual_doubl  = rs_1.value( 1 )+ ", " + string( rs.value( 9 ) ); 
                manuf       = rs_1.value( 0 );
                ingotType = rs_1.value( 2 );
            end;
            
            // Для стандартных слитков пропускаем мерные
            if( ( ( isStandart ) AND ( ingotType == "X" ) ) OR ( ( NOT isStandart ) AND ( ingotType != "X" ) ) ) 
            
                // Для золота выводим химически чистую массу дял остальных - лигатурная
                if( rs.value( 12 ) == 1 )
                    mass        = ( IngotAux.ChemicalWeight / 10000 );
                    sumOfMass   = sumOfMass + ( IngotAux.ChemicalWeight / 10000 );
                else 
                    mass        = ( IngotAux.NominalWeight / 10000 );
                    sumOfMass   = sumOfMass + ( IngotAux.NominalWeight / 10000 );
                end;
                
                // Лепим шапку
                if( count == 1 )
                     ListMet_head( rs.value( 7 ), isStandart )
                end;

                // Выводим ФИО клиента только один раз
                if ( count != 1 ) FIO = ""; end;
            
                [    |#######|#########################|###########|######################|#################|####################|]
                ( count,
                  FIO, 
                  mass,
                  serial_num,
                  Qual_doubl,
                  manuf
                 );
                count = count + 1;
                Qual_doubl = " ";
                manuf = " ";
                mass = 0;
            end;
        end;

    end;

    if( sumOfMass != 0 )
        [    +-----------------------------------------------------------------------------------------------------------+
             |#######|#########################|###########|######################|#################|####################|
             +-----------------------------------------------------------------------------------------------------------+]
        ( "Итого",
           "", 
           sumOfMass,
           "",
           "",
           ""
         );
     
    
        [
    
        Итого ######################### грамм(ов)]
        (sumOfMass);

    ListMet_end();
    end;
 end;   

//------------------------------------------------------------------------------
// 
//------------------------------------------------------------------------------


ListMet( true );          
ListMet( false );          

 ALG_RESULT = 0;

end;

