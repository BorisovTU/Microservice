/*
                           Макрос формирования реестра
                         вкладов подлежащих компенсации
                       Жертвам реформы Павлова посвящается

   Description:     Считаем вклад подлежащим компенсации, если он принадлежит
                    вкладчику родившемуся НЕ позднее 12 декабря 1916 г. и был
                    открыт НЕ позднее 20 июня 1991 г. Сумма подлежащая комп.
                    зависит от величины остатка на 20 июня, а коэфф. комп. -
                    от даты закрытия счета
*/

import deprintr;

Private var DEPOSITR = Tbfile ("depositr.dbt","r",4),
            DEPDOC   = Tbfile ("sbdepdoc.dbt","r",2);

const MAGIC = 1000;
const MAGIC_SUM = $1000L;
const OP_CLOSE = 34;

var SumToCompensate = $0L,
    CompensRate = $0L,
    CompensSum = $0L,
    ClientList = TClientList;

macro out_header()
  [ ];
  [                                        РЕЕСТР СЧЕТОВ ПО ВКЛАДАМ                                                                  ];
  [                           подлежащим предварительной компенсации в соответствии с                                                ];
  [                           Указом Президента Российской Федерации от 16 мая 1996 г.                                               ];
  [┌─────────────────────────┬─────────────┬──────────┬──────────┬───────────────┬─────────┬──────────────┬─────────────────────────┐];
  [│                         │             │    Год   │   Дата   │ Сумма вклада, │  Коэф.  │    Сумма     │ Номер счета, на который │];
  [│  Н о м е р   с ч е т а  │ Фамилия И.О.│ рождения │ закрытия │  подлежащая   │ компенс.│ компенсации  │ зачисляется компенсация │];
  [│                         │  вкладчика  │ вкладчика│  счета   │  компенсации  │         │              │                         │];
  [├─────────────────────────┼─────────────┼──────────┼──────────┼───────────────┼─────────┼──────────────┼─────────────────────────┤];
end;

macro out_line()

  [│#########################│#############│##########│##########│###############│#########│##############│                         │]
  (
    Acc_Form(DEPOSITR.rec.Account),
    ClientList.CurRec.ConvertFIOFull,
    ClientList.CurRec.Rec.Born,
    DEPOSITR.rec.Close_Date,
    SumToCompensate,
    CompensRate,
    CompensSum
  );
end;

macro out_bottom()

  [└─────────────────────────┴─────────────┴──────────┴──────────┴───────────────┴─────────┴──────────────┴─────────────────────────┘];
end;

macro get_rest_on_date_X(reference)

  var retval = 0;
  var strFilter;

  DEPDOC.Rewind ();
  DEPDOC.rec.Referenc         = reference;
  DEPDOC.rec.DepDate_Document = date(20,6,1991);
  strFilter = "t.T_REFERENC = " + String(reference);
  DEPDOC.addFilter (strFilter);
  if( DEPDOC.getLE() and (DEPDOC.rec.Referenc == reference) )
    retval = DEPDOC.rec.Rest;
  end;
  DEPDOC.dropFilter();
  return retval;
end;


macro print_victims_list()
  var clntFound, accFound;
  var rest, header_printed = FALSE;
  var strFilter;
  var NumClient = 0;

  ClientList.ClearFilterKey;
  ClientList.SetFilter_Born( date( 1, 1, 1000 ),  date( 31, 12, 1916 ) );
  ClientList.SetFilter(false);
  clntFound = ClientList.First;
// Цикл по клиентам
  while( clntFound  )
    NumClient = NumClient + 1;
    DEPOSITR.Rewind ();
    DEPOSITR.rec.CodClient = ClientList.CurRec.Rec.CodClient;
    DEPOSITR.rec.IsCur = 0;
    DEPOSITR.rec.Number = 0;
    strFilter = "t.T_ISCUR = 0" +
           " AND t.T_CODCLIENT = " + String(ClientList.CurRec.Rec.CodClient);
    DEPOSITR.addFilter (strFilter);
    accFound = DEPOSITR.getGE();
    if (accFound)
      Message (NumClient,"  Обрабатывается клиент с кодом ",String(ClientList.CurRec.Rec.CodClient));
    end;
//  Цикл по счетам
    while ( accFound
       and (DEPOSITR.rec.CodClient == ClientList.CurRec.Rec.CodClient)
       and (DEPOSITR.rec.IsCur == 0) )

      if (DEPOSITR.rec.Open_Date < date(20,6,1991))

        SumToCompensate = MoneyL(get_rest_on_date_X(DEPOSITR.rec.Referenc));
        if(SumToCompensate > MAGIC_SUM)
          SumToCompensate = MAGIC_SUM;
        end;
        if(DEPOSITR.rec.Open_Close == "")
          CompensRate = MAGIC;
        elif( (DEPOSITR.rec.Open_Close != "") and (DEPOSITR.rec.Close_Date < date(31,12,1991)) )
          CompensRate = 0;
        elif( (DEPOSITR.rec.Open_Close != "") and (DEPOSITR.rec.Close_Date < date(1,1,1993)) )
          CompensRate = MAGIC - 400;
        elif( (DEPOSITR.rec.Open_Close != "") and (DEPOSITR.rec.Close_Date < date(1,1,1994)) )
          CompensRate = MAGIC - 300;
        elif( (DEPOSITR.rec.Open_Close != "") and (DEPOSITR.rec.Close_Date < date(1,1,1995)) )
          CompensRate = MAGIC - 200;
        elif( (DEPOSITR.rec.Open_Close != "") and (DEPOSITR.rec.Close_Date < date(1,1,1996)) )
          CompensRate = MAGIC - 100;
        elif( (DEPOSITR.rec.Open_Close != "") and (DEPOSITR.rec.Close_Date < date(1,1,1997)) )
          CompensRate = MAGIC;
        end;

        CompensSum = MoneyL(SumToCompensate * CompensRate);
        if(CompensSum > 0)
          if(not header_printed)
            out_header();
            header_printed = TRUE;
          end;
          out_line();
        end;

      end;

      accFound = DEPOSITR.Next ();
    end;
    DEPOSITR.dropFilter();
    clntFound = ClientList.Next;
  end;

  if (header_printed)
    out_bottom();
  else
    [ Вкладов подлежащих компенсации не обнаружено! ];
  end;

end;
//
// Точка входа
//
/*
var a1,a2,dif;
a1 = Time();
[###################](a1);
*/
  print_victims_list();
/*
a2 = Time();
dif = a2-a1;
[################### ####################](a2,dif);
*/
end;
