/*  Макрос обработки объединения филиалов                     */

/*  Поучаствовали:                                            */

/*  RET  03.03.1999                                           */
/*  RA   07.04.2000                                           */
/*  LA   12.12.2000                                           */
/*  VIGO 24.07.2001                                           */

import deprintr, opercode;

/* =====================   Настройки ==========================*/

const SourceDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\TRAKFILE", true ); /* Источник данных. dbt-файлы
                                      необходимо переименовать в sen-файлы
                                     */

const NeedRenumb = True;             /* Флаг необходимости перенумерации,
                                        если True, то объединение диапазонов
                                        номеров филиалов не происходит
                                     */

const CopyDH     = False;            /* Флаг необходимости загрузки старой 
                                        истории перенумераций.
                                        Если True, то история загружается.
                                     */

/* ============================================================*/
file Dep        (depositr) key 7 write;
file T_Dep      (depositr) key 7 write;
file DepDoc     (sbdepdoc) key 6 write;
file T_DepDoc   (sbdepdoc) key 6 write;
file DepRest    (pc_drest) key 0 write;
file T_DepRest  (pc_drest) key 0 write;
file TrWill     (sbtrast ) key 0 write;
file T_TrWill   (sbtrast ) key 0 write;
file DepClient  (depclnt ) key 0 write;
file T_DepClient(depclnt ) key 0 write;
file AuxInfo    (auxinfo ) key 0 write;
file T_AuxInfo  (auxinfo ) key 0 write;
file ConGet     (sb_congt) key 0 write;
file T_ConGet   (sb_congt) key 0 write;
file PcCalc     (pc__calc) key 0 write;
file T_PcCalc   (pc__calc) key 0 write;
file PcEstim    (pcestim ) key 0 write;
file T_PcEstim  (pcestim ) key 0 write;
file CashLink   (sb_casln) key 1 write;
file T_CashLink (sb_casln) key 1 write;
file AccReg     (accreg  ) key 1 write;
file T_AccReg   (accreg  ) key 1 write;
file TextInfo   (textinfo) key 0 write;
file T_TextInfo (textinfo) key 0 write;
file PcRDate    (pc_rdate) key 2 write;
file T_PcRDate  (pc_rdate) key 2 write;
file ScAcc      ("scAcc.dbt",  "spcard.def") key 0 write;
file T_ScAcc    ("scAcc.dbt",  "spcard.def") key 0 write;
file ScLink     ("scLink.dbt", "spcard.def") key 3 write;
file T_ScLink   ("scLink.dbt", "spcard.def") key 3 write;
file ScCard     ("scCard.dbt", "spcard.def") key 0 write;
file T_ScCard   ("scCard.dbt", "spcard.def") key 0 write;
file ScTran     ("scTran.dbt", "spcard.def") key 3 write;
file T_ScTran   ("scTran.dbt", "spcard.def") key 3 write;
file PcTax      (pc_tax  ) key 0 write;
file T_PcTax    (pc_tax  ) key 0 write;
file RetOp      (ret_op  ) key 0 write;
file T_RetOp    (ret_op  ) key 0 write;
file OpObject   (opobject) key 0 write;
file T_OpObject (opobject) key 0 write;
file DepHistory (dephistr) key 0  write;
file T_DepHistr (dephistr) key 0 write;
file Compens    (compens) key 0 write;
file T_Compens  (compens) key 0 write;
file Numbers    (numbers) key 0 write;
file T_Numbers  (numbers) key 0 write;
file BranchList (listfdep);

file Report("report.dbt") write;

const
  I_Dep       =  0,
  I_DepDoc    =  2,
  I_DepRest   =  4,
  I_TrWill    =  6,
  I_DepClient =  7,
  I_AuxInfo   =  8,
  I_ConGet    = 10,
  I_PcCalc    = 11,
  I_PcEstim   = 13,
  I_CashLink  = 14,
  I_AccReg    = 15,
  I_TextInfo  = 16,
  I_PcRDate   = 17,
  I_DepHistr  = 19,
  I_ScAcc     = 20,
  I_ScLink    = 22,
  I_ScCard    = 23,
  I_ScTran    = 24,
  I_PcTax     = 25, 
  I_RetOp     = 27, 
  I_OpObject  = 28,
  I_Compens   = 30,
  I_Numbers   = 31;

const
  OST_INITIALIZED = 0,          /* Начата */
  OST_CONFIRMED = 1,            /* Подтверждена Кассой */
  OST_STORNED = 2,              /* Сторнирована */
  OST_DELETED = 3;              /* Удалена */

const
  RPCB_TAX_SBDEPR_RETAIL = 2;

const
  Binsert = 2;

const
  NullDate = Date( 0, 0, 0 );

const
  D_IN = 1,         /* Приход */
  D_OUT = 2,        /* Расход */
  D_GET = 3,        /* Списание */
  D_PUT = 5;        /* Зачисление */

var
  {curdate},
  BranchName_From,
  BranchName_To,
  FNCash_From,
  FNCash_To = NumFNCash(),
  Direction,
  Account,
  TrnStat,
  YesHistory = False ; /*Признак обработки по истории*/

var MinNumber = 0;

array  From, To;

Macro Constructor

 var OpenStat = True;

  if( not Create( Report ) )
     OpenStat = False;
  end;
  Return OpenStat;
end;


Macro MakeReport

  var StatInsert = False ;

  If(YesHistory)
      Report.Old_Account = From( I_DepHistr ).PrevAccount;
  else
      Report.Old_Account = "Нет истории перенумерации";
  end;

  Report.New_Account_Before = From( I_Dep ).Account;
  Report.New_Account_After = To( I_Dep ).Account;
  Report.Branch_Before = From(I_Dep).FNCash;
  Report.Branch_After = To(I_Dep).FNCash;
  Report.Type_Account = To(I_Dep).Type_Account;
  Report.Rest = From( I_Dep ).Sum_Rest;

  StatInsert = Insert ( Report );
  /* Для последующего добавления фамилии вкладчика */
  if( StatInsert)
     GetPos( Report );
  end;

  Return StatInsert;
end;

macro DefRezForAccount(CodClient)

  var NonRezident = 0;    /* Not FALSE because must be V_INTEGER!!! */

  ReWind(From(I_DepClient));
  From(I_DepClient).CodClient = CodClient;
  if(GetEQ(From(I_DepClient)))
    if(From(I_DepClient).Citizenship != "")
      NonRezident = 1;
    end;
  end;
  return NonRezident;
end;
/*
macro BranchNumForName( BranchName )

  KeyNum(BranchList,1);

  BranchList.NameBranch=BranchName;
  if(GetEQ(BranchList))
    return BranchList.FNCash;
  else
    return 0;
  end;
end;
*/
macro BranchNameForNum( BranchNum )

  var dep_name;
  
  KeyNum(BranchList,0);

  BranchList.FNCash=BranchNum;
  if( GetEQ( BranchList ) and findDepartment(BranchNum, dep_name) )
    MinNumber = BranchList.MinNumber;
    return dep_name;
  else
    return 0;
  end;
end;

macro OpenTransFiles;

 var Stat = False;

  if    ( not(     Open( T_Dep,        SourceDir + "depositr.sen" ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  depositr.sen ");
          Return Stat;
  elif  ( not(  Open( T_DepHistr,  SourceDir + "dephistr.sen" ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  dephistr.sen ");
          Return Stat;
  elif  ( not(  Open( T_DepDoc,    SourceDir + "sbdepdoc.sen" ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  sbdepdoc.sen ");
          Return Stat;
  elif  ( not(  Open( T_CashLink,  SourceDir + "sb_casln.sen" ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  sb_casln.sen ");
          Return Stat;
  elif  ( not(  Open( T_DepRest,   SourceDir + "pc_drest.sen" ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  pc_drest.sen ");
          Return Stat;
  elif  ( not(  Open( T_TrWill,    SourceDir + "sbtrast.sen"  ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  sbtrast.sen ");
          Return Stat;
  elif  ( not(  Open( T_DepClient, SourceDir + "depclnt.sen"  ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  depclnt.sen ");
          Return Stat;
  elif  ( not(  Open( T_AuxInfo,   SourceDir + "auxinfo.sen"  ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  auxinfo.sen ");
          Return Stat;
  elif  ( not(  Open( T_ConGet,    SourceDir + "sb_congt.sen" ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  sb_congt.sen ");
          Return Stat;
  elif  ( not(  Open( T_PcEstim,   SourceDir + "pcestim.sen"    ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  pcestim.sen ");
          Return Stat;
  elif  ( not(  Open( T_PcCalc,    SourceDir + "pc__calc.sen"   ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  pc__calc.sen ");
          Return Stat;
  elif  ( not(  Open( T_PcRDate,   SourceDir + "pc_rdate.sen"   ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  pc_rdate.sen ");
          Return Stat;
  elif  ( not(  Open( T_Numbers,   SourceDir + "numbers.sen"   ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  numbers.sen ");
          Return Stat;
  elif  ( not(  Open( T_AccReg,   SourceDir + "accreg.sen"   ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  accreg.sen ");
          Return Stat;
  elif  ( not(  Open( T_TextInfo, SourceDir + "textinfo.sen"   ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  textinfo.sen ");
          Return Stat;
  elif  ( not(  Open( T_ScAcc,    SourceDir + "scAcc.sen"   ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  scAcc.sen ");
          Return Stat;
  elif  ( not(  Open( T_ScLink,   SourceDir + "scLink.sen"   ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  scLink.sen ");
          Return Stat;
  elif  ( not(  Open( T_ScCard,   SourceDir + "scCard.sen"   ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  scCard.sen ");
          Return Stat;
  elif  ( not(  Open( T_ScTran,   SourceDir + "scTran.sen"   ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  scTran.sen ");
          Return Stat;
  elif  ( not(  Open( T_PcTax,    SourceDir + "pc_tax.sen"   ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  pc_tax.sen ");
          Return Stat;
  elif  ( not(  Open( T_RetOp,    SourceDir + "ret_op.sen"   ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  ret_op.sen ");
          Return Stat;
  elif  ( not(  Open( T_OpObject, SourceDir + "opobject.sen"   ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  opobject.sen ");
          Return Stat;
  elif  ( not(  Open( T_Compens,  SourceDir + "compens.sen"   ) ) )
          MsgBox("Ошибка при открытии транспортного  файла  compens.sen ");
          Return Stat;
  else
          Stat = True;
          Message ("  Транспортные   файлы   успешно  открыты ");
          Return Stat;
  end;
end;

/****************************************************************************/
macro MakeTurnOver
  var
    Stat = true,
    AppKey = 0;
    Rewind (To(I_DepDoc));
    ClearRecord (To(I_DepDoc));
    To(I_DepDoc).Referenc         = To(I_Dep).Referenc;
    To(I_DepDoc).Account          = To(I_Dep).Account;
    To(I_DepDoc).IsCur            = To(I_Dep).IsCur;
    To(I_DepDoc).FNCash           = To(I_Dep).FNCash;
    To(I_DepDoc).Code_Currency    = To(I_Dep).Code_Currency;
    To(I_DepDoc).Date_Document    = {curdate};
    To(I_DepDoc).DepDate_Document = {curdate};
    To(I_DepDoc).NumDayDoc        = 10001;
    To(I_DepDoc).Type_Account     = To(I_Dep).Type_Account;
    To(I_DepDoc).CodClient        = To(I_Dep).CodClient;
    To(I_DepDoc).Oper             = 9999;
    To(I_DepDoc).TypeOper         = 62;
    To(I_DepDoc).ApplType         = 51;
    To(I_DepDoc).TypeComplexOper  = 62;
    To(I_DepDoc).VidDoc           = 0;
    To(I_DepDoc).InSum            = $0;
    To(I_DepDoc).OutSum           = To(I_Dep).Sum_Rest;
    To(I_DepDoc).Rest             = $0;      
    To(I_DepDoc).KindOp           = 1;      
    To(I_DepDoc).IsControl        = StrFor (1);
    To(I_DepDoc).Ground           = "Реорганизация филиала. Списано в филиал " + FNCash_To;

    AppKey = FormApplicationkey(1);

    To(I_DepDoc).iApplicationKind = 1;
    To(I_DepDoc).ApplicationKey   = AppKey;

    stat = Insert (To(I_DepDoc));

    if (stat)
       Rewind      (To(I_CashLink));
       ClearRecord (To(I_CashLink));
       To(I_CashLink).ApplicationKind1 = To(I_DepDoc).iApplicationKind;
       To(I_CashLink).ApplicationKey1  = To(I_DepDoc).ApplicationKey;
       To(I_CashLink).NumLinkDoc       = 0;
       To(I_CashLink).ApplicationKind2 = To(I_DepDoc).iApplicationKind;
       To(I_CashLink).ApplicationKey2  = To(I_DepDoc).ApplicationKey;
       To(I_CashLink).RefValue2        = 0;
       To(I_CashLink).FNCash           = FNCash_To;
       To(I_CashLink).NumSession       = 0;
    end;

    stat = Insert (To(I_CashLink));

    if (not stat)
       println ("По счету ", To(I_Dep).Account, " не вставлена первая корректирующая запись");
    end;

    if (stat)
       Rewind (To(I_DepDoc));
       ClearRecord (To(I_DepDoc));

       To(I_DepDoc).Referenc         = To(I_Dep).Referenc;
       To(I_DepDoc).Account          = To(I_Dep).Account;
       To(I_DepDoc).IsCur            = To(I_Dep).IsCur;
       To(I_DepDoc).FNCash           = To(I_Dep).FNCash;
       To(I_DepDoc).Code_Currency    = To(I_Dep).Code_Currency;
       To(I_DepDoc).Date_Document    = {curdate};
       To(I_DepDoc).DepDate_Document = {curdate};
       To(I_DepDoc).NumDayDoc        = 10002;
       To(I_DepDoc).Type_Account     = To(I_Dep).Type_Account;
       To(I_DepDoc).CodClient        = To(I_Dep).CodClient;
       To(I_DepDoc).Oper             = 9999;
       To(I_DepDoc).TypeOper         = 71;
       To(I_DepDoc).ApplType         = 51;
       To(I_DepDoc).TypeComplexOper  = 71;
       To(I_DepDoc).VidDoc           = 0;
       To(I_DepDoc).InSum            = To(I_Dep).Sum_Rest;
       To(I_DepDoc).OutSum           = $0;
       To(I_DepDoc).Rest             = To(I_Dep).Sum_Rest;      
       To(I_DepDoc).KindOp           = 1;
       To(I_DepDoc).IsControl        = StrFor (1);
       To(I_DepDoc).Ground           = "Присоединение филиала. Зачислено с филиала " + FNCash_From;
   
       AppKey = FormApplicationkey(1);
   
       To(I_DepDoc).iApplicationKind = 1;
       To(I_DepDoc).ApplicationKey   = AppKey;

       stat = Insert (To(I_DepDoc));
   
       if (stat)
          Rewind      (To(I_CashLink));
          ClearRecord (To(I_CashLink));
          To(I_CashLink).ApplicationKind1 = To(I_DepDoc).iApplicationKind;
          To(I_CashLink).ApplicationKey1  = To(I_DepDoc).ApplicationKey;
          To(I_CashLink).NumLinkDoc       = 0;
          To(I_CashLink).ApplicationKind2 = To(I_DepDoc).iApplicationKind;
          To(I_CashLink).ApplicationKey2  = To(I_DepDoc).ApplicationKey;
          To(I_CashLink).RefValue2        = 0;
          To(I_CashLink).FNCash           = FNCash_To;
          To(I_CashLink).NumSession       = 0;
       end;
   
       stat = Insert (To(I_CashLink));

       if (not stat)
          println ("По счету ", To(I_Dep).Account, " не вставлена вторая корректирующая запись");
       end;
    end;
    return stat;
end;

/****************************************************************************/
macro CheckTurnOver
  var
    Stat = true,
    AppKey = 0;
end;

macro GetNewAutoKeyForCompens
  var
    NewAutoKey = 0;

  ClearRecord( To(I_Compens) );
  rewind( To(I_Compens) );
  To(I_Compens).FNCash = FNCash_To;
  To(I_Compens).AutoKey = 2000000000;
  if( GetLE( To(I_Compens) ) )
    NewAutoKey = To(I_Compens).AutoKey + 1;
  end;
  return NewAutoKey;
end;

/****************************************************************************/
macro CopyOpObject;

  var
    RecFound, 
    Stat = true;

  ClearRecord( From( I_OpObject ) );
  From( I_OpObject ).OpAppKind = To( I_RetOp ).ApplicationKind;
  From( I_OpObject ).OpAppKey = To( I_RetOp ).ApplicationKey;
  RecFound = GetGE( From( I_OpObject ) );
  while ( Stat and RecFound
    and ( From( I_OpObject ).OpAppKind == To( I_RetOp ).ApplicationKind )
    and ( From( I_OpObject ).OpAppKey == To( I_RetOp ).ApplicationKey ) )
    Copy( To( I_OpObject ), From( I_OpObject ) );
    To( I_OpObject ).Branch = FNCash_To;
    Stat = Insert( To( I_OpObject ) );
    if ( Stat )
      RecFound = Next( From( I_OpObject ) );
    end;
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyRetOp;

  var
    Stat = true;

  From( I_RetOp ).ApplicationKind = From( I_CashLink ).ApplicationKind1;
  From( I_RetOp ).ApplicationKey = From( I_CashLink ).ApplicationKey1;
  if ( GetGE( From( I_RetOp ) ) )
    Copy( To( I_RetOp ), From( I_RetOp ) );
    To( I_RetOp ).Branch = FNCash_To;
    To( I_RetOp ).NumUnconfCashDocs = 0;
    if ( To( I_RetOp ).State == OST_INITIALIZED ) 
       To( I_RetOp ).State = OST_CONFIRMED;
    end;
    Stat = Insert( To( I_RetOp ) );
    if ( Stat )  
      Stat = CopyOpObject; 
    elif ( Status == 5 )        /* Дублирование ключа здесь допустимо */
      Stat = true;
    end;
  end;
  return stat;
end;

/****************************************************************************/
macro CopyPcTax( fileNum );

  var
    RecFound,
    Stat = true;

  ClearRecord( From( fileNum ) );
  From( fileNum ).CodeModule = RPCB_TAX_SBDEPR_RETAIL;
  From( fileNum ).ObjectTax = String( From( I_Dep ).Referenc );
  RecFound = GetGE( From( fileNum ) );
  while ( Stat and RecFound 
    and ( From( fileNum ).CodeModule == RPCB_TAX_SBDEPR_RETAIL )
    and ( From( fileNum ).ObjectTax == String( From( I_Dep ).Referenc ) ) )
    Copy( To( fileNum ), From( fileNum ) );
    To( fileNum ).FNCash = FNCash_To;
    Stat = Insert( To( fileNum ) );
    if ( Stat )
      RecFound = Next( From( fileNum ) );
    end; 
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyScCard;

  var
    Stat;

  From( I_ScCard ).FNCash = From( I_ScLink ).FNCash;
  From( I_ScCard ).cardRef = From( I_ScLink ).cardRef;
  Stat = GetEQ( From( I_ScCard ) );
  if ( Stat )
    Copy( To( I_ScCard ), From( I_ScCard ) );
    To( I_ScCard ).FNCash = FNCash_To;
    Stat = Insert( To( I_ScCard ) );
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyScTran;

  var
    RecFound,
    Stat = true;

  ClearRecord( From( I_ScTran ) );
  From( I_ScTran ).FNCash = From( I_ScLink ).FNCash;
  From( I_ScTran ).accCardLinkRef = From( I_ScLink ).accCardLinkRef;
  RecFound = GetGE( From( I_ScTran ) );
  while ( RecFound and Stat
    and ( From( I_ScTran ).FNCash == From( I_ScLink ).FNCash )
    and ( From( I_ScTran ).accCardLinkRef == From( I_ScLink ).accCardLinkRef ) )
    Copy( To( I_ScTran ), From( I_ScTran ) );
    To( I_ScTran ).FNCash = FNCash_To;
    Stat = Insert( To( I_ScTran ) );
    if ( Stat ) 
      RecFound = Next( From( I_ScTran ) );
    end;
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyScLink;

  var
    RecFound,
    Stat = true;

  ClearRecord( From( I_ScLink ) );
  From( I_ScLink ).cardAccRef = From( I_ScAcc ).Referenc;
  RecFound = GetEQ( From( I_ScLink ) );
  while ( Stat and RecFound
    and ( From( I_ScLink ).cardAccRef == From( I_ScAcc ).Referenc ) )
    Copy( To( I_ScLink ), From( I_ScLink ) );
    To( I_ScLink ).FNCash = FNCash_To;
    To( I_ScLink ).cardAccRef = To(I_Dep).Referenc;
    Stat = Insert( To( I_ScLink ) );
    if ( Stat )
      Stat = CopyScCard;
    end;
    if ( Stat )
      Stat = CopyScTran;
    end;
    if ( Stat )
      RecFound = Next( From( I_ScLink ) );
    end;
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyScAcc;

  var
    Stat = true;

  From( I_ScAcc ).Referenc = From( I_Dep ).Referenc;
  if ( GetEQ( From( I_ScAcc ) ) )
    Copy( To( I_ScAcc ), From( I_ScAcc ) );
    To( I_ScAcc ).FNCash = FNCash_To;
    To( I_ScAcc ).Referenc = To(I_Dep).Referenc;
    Stat = Insert( To( I_ScAcc ) );
    if ( Stat )
      Stat = CopyScLink;
    end;
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyCashLink( AppKind, AppKey, _CopyRetOp );

  var
    Stat = true;

  ClearRecord( From( I_CashLink ) );
  From( I_CashLink ).ApplicationKind2 = AppKind;
  From( I_CashLink ).ApplicationKey2 = AppKey;
  if ( GetEQ( From( I_CashLink ) ) )
    if ( Stat )
      Copy( To( I_CashLink ), From( I_CashLink ) );
      To (I_CashLink).FNCash = FNCash_To;
      Stat = Insert( To( I_CashLink ) );
      if ( Stat )
        if ( _CopyRetOp )  
          Stat = CopyRetOp;
        end;
      elif ( Status == 5 )
        PrintLn( "Предупреждение: Значение ключа в файле связок дублируется" );
        Stat = true; 
      end; 
    end;
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyDepDoc;

  record StatParm("StatParm.rec");

  var
    RecFound,
    NuCompensAK,
    Stat=true;

  ClearRecord(From(I_DepDoc));
  From(I_DepDoc).Referenc=From(I_Dep).Referenc;
  RecFound=GetGE(From(I_DepDoc));
  while(Stat and RecFound and (From(I_DepDoc).Referenc==From(I_Dep).Referenc))
    if(Stat)
      Stat = CopyCashLink(From(I_DepDoc).iApplicationKind,From(I_DepDoc).ApplicationKey,True);
    end;
    if(Stat)
      Copy(To(I_DepDoc),From(I_DepDoc));
      To(I_DepDoc).FNCash=FNCash_To;
      To(I_DepDoc).Referenc=To(I_Dep).Referenc;
      To(I_DepDoc).Account=To(I_Dep).Account;
      if(To(I_DepDoc).TypeOper == Зачисление_Компенсации)
        From(I_Compens).FNCash = FNCash_From;
        From(I_Compens).AutoKey = To(I_DepDoc).ListTransfer;
        if(GetEQ(From(I_Compens)))
          NuCompensAK = GetNewAutoKeyForCompens;
          if(NuCompensAK)
            Copy(To(I_Compens),From(I_Compens));
            To(I_Compens).FNCash = FNCash_To;
            To(I_Compens).AutoKey = NuCompensAK;
            Stat = Insert( To(I_Compens) );
            if(Stat)
              To(I_DepDoc).ListTransfer = NuCompensAK;
            end;
          end;
        end;
      end;
      if(Stat)
        Stat=Insert(To(I_DepDoc));
      end;
    end;
    if(Stat)
      RecFound=Next(From(I_DepDoc));
    end;
  end;
  /* Статистика */
  if(Stat)
    ClearRecord(StatParm);
    StatParm.Op = Binsert;
    StatParm.KindOp = D_IN;
    StatParm.Sum = To(I_Dep).Sum_Rest;
    StatParm.TransfFlag = StrFor(1);
    StatParm.Type_Account = To(I_Dep).Type_Account;
    StatParm.CodCur = To(I_Dep).Code_Currency;
    StatParm.Date = {curdate};
    StatParm.IsCur = To(I_Dep).IsCur;
    StatParm.FlagRez = To(I_DepDoc).FlagRezid;
    StatParm.Branch = To(I_DepDoc).FNCash;

    Stat = UpdateDepStats(StatParm);
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyDepRest( fileNum )

  var
    RecFound,
    Stat=true;

  ClearRecord(From(fileNum));

  From(fileNum).Referenc=From(I_Dep).Referenc;
  RecFound=GetGE(From(fileNum));
  while(Stat and RecFound and (From(fileNum).Referenc==From(I_Dep).Referenc))
    Copy(To(fileNum),From(fileNum));
    To(fileNum).FNCash=FNCash_To;
    To(fileNum).Referenc=To(I_Dep).Referenc;
    Stat=Insert(To(fileNum));
    if(Stat)
      RecFound=Next(From(fileNum));
    end;
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyDepClient( CodClient );

  var
    Stat=True;

  From(I_DepClient).CodClient = CodClient;
  if(GetEQ(From(I_DepClient)))
    /* Берем только что вставленную запись в файл отчета */
    /* Если она есть! */
    if( GetDirect( Report ) )
      Report.FIO = String( Trim( From( I_DepClient ).Sname ) + " " +
                           Trim( From( I_DepClient ).Name  ) + " " +
                           Trim( From( I_DepClient ).Pname )        );
      if( not Update( Report ) )
        PrintLn (" Запись в файле отчёта не обновлена ");
      end;
    end;

    Copy(To(I_DepClient) , From(I_DepClient));
    To(I_DepClient).CodClient = FormClientCode;
    if(To(I_DepClient).CodClient == 0)
      Stat=false;
    end;

    if(Stat and not GetEQ(To(I_DepClient)))
      To(I_DepClient).FNCash = FNCash_To;
      Stat=Insert(To(I_DepClient));
    end;
  else
    Stat=false;
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyTrWill;

  var
    RecFound,
    Stat=true;

  ClearRecord(From(I_TrWill));
  From(I_TrWill).Referenc=From(I_Dep).Referenc;
  RecFound=GetGE(From(I_TrWill));
  while(Stat and RecFound and (From(I_TrWill).Referenc==From(I_Dep).Referenc))
    Copy(To(I_TrWill),From(I_TrWill));
    To(I_TrWill).FNCash=FNCash_To;
    To(I_TrWill).Referenc=To(I_Dep).Referenc;
    Stat=CopyDepClient(From(I_TrWill).CodClient);
    if(Stat)
      To(I_TrWill).CodClient=To(I_DepClient).CodClient;
      Stat=Insert(To(I_TrWill));
    end;
    if(Stat)
      RecFound=Next(From(I_TrWill));
    end;
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyAuxInfo;

  var
    RecFound,
    Stat=true;

  ClearRecord(From(I_AuxInfo));
  From(I_AuxInfo).Reference=From(I_Dep).Referenc;
  RecFound=GetGE(From(I_AuxInfo));
  while(Stat and RecFound
    and (From(I_AuxInfo).Reference==From(I_Dep).Referenc))
    Copy(To(I_AuxInfo),From(I_AuxInfo));
    To(I_AuxInfo).FNCash=FNCash_To;
    To(I_AuxInfo).Reference=To(I_Dep).Referenc;
    Stat=Insert(To(I_AuxInfo));
    if(Stat)
      RecFound=Next(From(I_AuxInfo));
    end;
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyTextInfo;

  var
    RecFound,
    Stat=true;

  ClearRecord(From(I_TextInfo));
  From(I_TextInfo).Reference=From(I_Dep).Referenc;
  RecFound=GetGE(From(I_TextInfo));
  while(Stat and RecFound
    and (From(I_TextInfo).Reference==From(I_Dep).Referenc))
    Copy(To(I_TextInfo),From(I_TextInfo));
    To(I_TextInfo).FNCash=FNCash_To;
    To(I_TextInfo).Reference=To(I_Dep).Referenc;
    Stat=Insert(To(I_TextInfo));
    if(Stat)
      RecFound=Next(From(I_TextInfo));
    end;
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyConGet;

  var
    RecFound,
    Stat=true;

  ClearRecord(From(I_ConGet));
  From(I_ConGet).ReferencAcc=From(I_Dep).Referenc;
  RecFound=GetGE(From(I_ConGet));
  while(Stat and RecFound
    and (From(I_ConGet).ReferencAcc==From(I_Dep).Referenc))
    Copy(To(I_ConGet),From(I_ConGet));
    To(I_ConGet).FNCash=FNCash_To;
    To(I_ConGet).ReferencAcc=To(I_Dep).Referenc;
    Stat=Insert(To(I_ConGet));
    if(Stat)
      RecFound=Next(From(I_ConGet));
    end;
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyPcCalc( fileNum );

  var
    RecFound,
    Stat=true;

  ClearRecord(From(fileNum));
  From(fileNum).Referenc=From(I_Dep).Referenc;
  RecFound=GetGE(From(fileNum));
  while(Stat and RecFound and (From(fileNum).Referenc==From(I_Dep).Referenc))
    Copy(To(fileNum),From(fileNum));
    To(fileNum).FNCash   = FNCash_To;
    To(fileNum).Referenc = To(I_Dep).Referenc;
    Stat=Insert(To(fileNum));
    if(Stat)
      RecFound=Next(From(fileNum));
    end;
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyPcEstim( fileNum );

  var
    RecFound,
    Stat=true;

  ClearRecord(From(fileNum));
  From(fileNum).Referenc=From(I_Dep).Referenc;
  RecFound=GetGE(From(fileNum));
  while(Stat and RecFound and (From(fileNum).Referenc==From(I_Dep).Referenc))
    Copy(To(fileNum),From(fileNum));
    To(fileNum).FNCash   = FNCash_To;
    To(fileNum).Referenc = To(I_Dep).Referenc;
    Stat=Insert(To(fileNum));
    if(Stat)
      RecFound=Next(From(fileNum));
    end;
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyAccReg;

  var
    Stat = true;

  From( I_AccReg ).IsCur = From( I_Dep ).IsCur;
  From( I_AccReg ).FNCash = From( I_Dep ).FNCash;
  From( I_AccReg ).GroupNumb = From( I_Dep ).GroupNumb;
  From( I_AccReg ).Code_Currency = From( I_Dep ).Code_Currency;
  From( I_AccReg ).Number = From( I_Dep ).Number;
  if ( GetEQ( From( I_AccReg ) ) )
    Copy( To( I_AccReg ), From( I_AccReg ) );
    To( I_AccReg ).FNCash = FNCash_To;
    To( I_AccReg ).Referenc = To(I_Dep).Referenc;
    To( I_AccReg ).Account = To(I_Dep).Account;
    To( I_AccReg ).AutoKey = 0;
    To( I_AccReg ).CodClient = To(I_Dep).CodClient;
    Stat = Insert( To( I_AccReg ) );
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyPcRDate( fileNum );

  var
    RecFound,
    Stat=true;

  ClearRecord(From(fileNum));
  From(fileNum).Referenc=From(I_Dep).Referenc;
  RecFound=GetGE(From(fileNum));
  while(Stat and RecFound and (From(fileNum).Referenc==From(I_Dep).Referenc))
    Copy(To(fileNum),From(fileNum));
    To(fileNum).Referenc=To(I_Dep).Referenc;
    Stat=Insert(To(fileNum));
    if(Stat)
      RecFound=Next(From(fileNum));
    end;
  end;
  return Stat;
end;

/****************************************************************************/
macro CopyDepHistory

  var
    RecFound,
    Stat=true,
    ShortNum=0;

  ClearRecord( From( I_DepHistr ) );
  From( I_DepHistr ).BranchName = BranchNameForNum( From( I_Dep ).FNCash );
  From( I_DepHistr ).Account = From( I_Dep ).Account;
  RecFound = GetEQ( From( I_DepHistr ) );
  while ( RecFound and Stat )
    Copy( To( I_DepHistr ), From( I_DepHistr ) );
    To(I_DepHistr).PrevNumber = int (trim (substr (ShortNum, 14, 7) ) );
    Stat = Insert( To( I_DepHistr ) );
    if ( Stat )
      ClearRecord( From( I_DepHistr ) );
      From( I_DepHistr ).BranchName = From( I_DepHistr ).PrevBranch;
      From( I_DepHistr ).Account = From( I_DepHistr ).PrevAccount;
      RecFound = GetEQ( From( I_DepHistr ) );
    end;
  end;
  return Stat;
end;

/****************************************************************************/
macro WriteDepHistory()

  DepHistory.PrevBranchName = BranchName_From;
  DepHistory.PrevAccount    = From(I_Dep).Account;
  DepHistory.PrevBranch     = FNCash_To; /* Чтобы затем искать по старому номеру */
  DepHistory.PrevNumGroup   = To(I_Dep).GroupNumb;
  DepHistory.PrevNumber     = From(I_Dep).Number;

  DepHistory.BranchName     = BranchName_To;
  DepHistory.RenumDate      = {curdate};
  DepHistory.Account        = To(I_Dep).Account;
  DepHistory.Number         = To(I_Dep).Number;
  DepHistory.NumGroup       = To(I_Dep).GroupNumb;
  DepHistory.Type_Account   = To(I_Dep).Type_Account;
  DepHistory.BalAcc         = SubStr( To(I_Dep).Account, 1, 5 );
  DepHistory.CurCode        = To(I_Dep).Code_Currency;

  return Insert(DepHistory);
end;

/**********************************************************/
Macro GetNextNumberNew

(
  DEPOSITR,     /* Вход - файл счетов, для которого ищется следующий номер */
  FNCash,
  GroupNumb,
  Code_Currency,
  NeedSave      /* Вход - Флаг нужно ли сохранять информацию, внесенную в
                          DEPOSITR ранее или нет */
)
  var OldKey = 0;
  var Number = 0L;

  record SAVEDEP ( "depositr.dbt" );

  if( NeedSave ) copy( SAVEDEP, DEPOSITR ); end;

  OldKey = KeyNum( DEPOSITR, 0 );

  DEPOSITR.FNCash        = FNCash;
  DEPOSITR.GroupNumb     = GroupNumb;
  DEPOSITR.Code_Currency = Code_Currency;
  DEPOSITR.Number        = 2000000000;
  if( GetLE( DEPOSITR ) and ( DEPOSITR.FNCash        == FNCash        )
                        and ( DEPOSITR.GroupNumb     == GroupNumb     )
                        and ( DEPOSITR.Code_Currency == Code_Currency ) )

    Number = DEPOSITR.Number + 1;
  else
    Number = MinNumber;
  end;

  KeyNum( DEPOSITR, OldKey );
  if( NeedSave )
    Copy( DEPOSITR, SAVEDEP ); /* Вернем сохраненные данные на место */
  end;
  return Number;
end;

Macro OutHead( BranchName_From , BranchName_To )

  PrintLn(" Слияние филиала ",BranchName_From," с филиалом ",BranchName_To);
  PrintLn;
  PrintLn(" Изменившиеся номера счетов");
  PrintLn;
  [+-----------------------------+---------------------------+----------------+---------------------------+----------------+------------------------------------------------------+];
  [|         Старый номер        |    После перенумерации    |   Вид Вклада   |     Номер после слияния   |     Остаток    |                    ФИО вкладчика                     |];
  [+-----------------------------+---------------------------+----------------+---------------------------+----------------+------------------------------------------------------+];

end;

Macro OutLine( );
  [| ########################### | ######################### | ############## | ######################### | ############### | #]
  ( Report.Old_Account, Report.New_Account_Before : c,
    Report.Old_Account , Report.New_Account_After : c,
    Report.Old_Account , Report.FIO : l );
end;

Macro OutBottom( Counter , Итог  , Вклад )
  [+-----------------------------+---------------------------+----------------+---------------------------+----------------+------------------------------------------------------+];
  [| Итого по вкладу  ###################  :  ############################ ]
   ( Вклад : r , Итог : l );
  [| Всего количество счетов по вкладу     :  ############################ ]
   ( Counter : l);
  [+-----------------------------+---------------------------+----------------+---------------------------+----------------+------------------------------------------------------+];
  [
  ];
end;

macro CopyDep;

  var
    NewAccount,
    NewNumber,
    Stat=true;

  SetFlagCur( From(I_Dep).IsCur, 1 );

  ClearRecord( From( I_Dep ) );
  From(I_Dep).FNCash  = FNCash_From;
  From(I_Dep).Account = Account;


  if( GetDirect( From( I_Dep) ) )
    if( NeedRenumb )
       NewNumber  = 0;
       NewAccount = FormAccountNumber( From(I_Dep).Code_Currency,
                                       From(I_Dep).Type_Account,
                                       NewNumber, DefRezForAccount(From(I_Dep).CodClient) );
       if( NewAccount == "" )
          [ Не определен новый счет для старого #]( From(I_Dep).Account:l );
          Stat=false;
       end;
    end;

    if(Stat)
      Copy(To(I_Dep),From(I_Dep));
      To(I_Dep).FNCash     = FNCash_To;
      if( NeedRenumb )
        To(I_Dep).Account    = NewAccount;
        To(I_Dep).Number     = NewNumber;
      end;
      To(I_Dep).Referenc   = FormReference();
      if(To(I_Dep).Referenc=="")
        Stat=false;
      end;
    end;

    if(Stat)
      /* Выведем строку в  файл отчета о перенумерации */
      If(YesHistory)
        ClearRecord( From( I_DepHistr ) );
        From( I_DepHistr ).BranchName = BranchName_From;
        From( I_DepHistr ).Account    = From(I_Dep).Account;
 
        Stat =  GetEQ( From( I_DepHistr ) );
        If ( Stat )
          Stat =  MakeReport;
          If ( Not ( Stat ) )
             PrintLn (" Запись в файл отчёта не вставлена ");
          end;
        end;
      else
        Stat =  MakeReport;
        If ( Not ( Stat ) )
           PrintLn (" Запись в файл отчёта не вставлена ");
        end;
      end;

      Stat = CopyDepDoc;
    end;
    if(Stat)
      Stat=CopyDepRest(I_DepRest);
    end;
    if(Stat)
      Stat=CopyTrWill;
    end;
    if(Stat)
      Stat=CopyDepClient(From(I_Dep).CodClient);
    end;
    To(I_Dep).CodClient=To(I_DepClient).CodClient;
    if(Stat)
      Stat=CopyAuxInfo;
    end;
    if(Stat)
      Stat=CopyTextInfo;
    end;
    if(Stat)
      Stat=CopyConGet;
    end;
    if(Stat)
      Stat=CopyPcCalc(I_PcCalc);
    end;
    if(Stat)
      Stat=CopyPcEstim(I_PcEstim);
    end;
    if(Stat)
      Stat=CopyAccReg;
    end;
    if(Stat)
      Stat=CopyPcRDate(I_PcRDate);
    end;
    if(Stat and CopyDH)
      Stat=CopyDepHistory;
    end;
    if(Stat)
      Stat=CopyScAcc;
    end;
    if(Stat)
      Stat=CopyPcTax(I_PcTax);
    end;
    if(Stat)
      Stat=Insert(To(I_Dep));
    end;
    if (stat)
/*    CheckTurnOver;    */
    end;
    if( Stat and ( ( Account!=NewAccount ) or ( FNCash_To != FNCash_From ) ) )
      Stat = WriteDepHistory;
    end;
  else
    Stat=False;
  end;
  return Stat;
end;

macro TrnProc;

  TrnStat = false;
  if( CopyDep )
    TrnStat=true;
  else
    AbortTrn;
  end;
end;

macro DoCopyTransaction;
    return ProcessTrn(1,"TrnProc",
      From(I_Dep),To(I_Dep),
      From(I_DepDoc),To(I_DepDoc),
      From(I_DepRest),To(I_DepRest),
      From(I_TrWill),To(I_TrWill),
      From(I_DepClient),To(I_DepClient),
      From(I_AuxInfo),To(I_AuxInfo),
      From(I_ConGet),To(I_ConGet),
      From(I_PcCalc),To(I_PcCalc),
      From(I_PcEstim),To(I_PcEstim),
      From(I_CashLink),To(I_CashLink),
      From(I_AccReg),To(I_AccReg),
      From(I_TextInfo),To(I_TextInfo),
      From(I_PcRDate),To(I_PcRDate),
      From(I_DepHistr),To(I_DepHistr),
      From(I_ScAcc),To(I_ScAcc),
      From(I_ScLink),To(I_ScLink),
      From(I_ScCard),To(I_ScCard),
      From(I_ScTran),To(I_ScTran),
      From(I_PcTax),To(I_PcTax), 
      From(I_RetOp),To(I_RetOp), 
      From(I_OpObject),To(I_OpObject),
      From(I_Compens),To(I_Compens));
  end;

macro SetNewPoints

  From(I_Dep)=T_Dep;
  To(I_Dep)=Dep;
  From(I_DepDoc)=T_DepDoc;
  To(I_DepDoc)=DepDoc;
  From(I_DepRest)=T_DepRest;
  To(I_DepRest)=DepRest;
  From(I_TrWill)=T_TrWill;
  To(I_TrWill)=TrWill;
  From(I_DepClient)=T_DepClient;
  To(I_DepClient)=DepClient;
  From(I_AuxInfo)=T_AuxInfo;
  To(I_AuxInfo)=AuxInfo;
  From(I_TextInfo)=T_TextInfo;
  To(I_TextInfo)=TextInfo;
  From(I_ConGet)=T_ConGet;
  To(I_ConGet)=ConGet;
  From(I_PcCalc)=T_PcCalc;
  To(I_PcCalc)=PcCalc;
  From(I_PcEstim)=T_PcEstim;
  To(I_PcEstim)=PcEstim;
  From(I_Dephistr)=T_DepHistr;
  To(I_Dephistr)=DepHistory;
  From(I_ScAcc)=T_ScAcc;
  To(I_ScAcc)=ScAcc;
  From(I_ScLink)=T_ScLink;
  To(I_ScLink)=ScLink;
  From(I_ScCard)=T_ScCard;
  To(I_ScCard)=ScCard;
  From(I_ScTran)=T_ScTran;
  To(I_ScTran)=ScTran;
  From(I_PcTax)=T_PcTax;
  To(I_PcTax)=PcTax;
  From(I_PcRDate)=T_PcRDate;
  To(I_PcRDate)=PcRDate;
  From(I_Numbers)=T_Numbers;
  To(I_Numbers)=Numbers;
  From(I_CashLink)=T_CashLink;
  To(I_CashLink)=CashLink;
  From(I_AccReg)=T_AccReg;
  To(I_AccReg)=AccReg;
  From(I_Compens)=T_Compens;
  To(I_Compens)=Compens;
  From(I_RetOp)=T_RetOp;
  To(I_RetOp)=RetOp;
  From(I_OpObject)=T_OpObject;
  To(I_OpObject)=OpObject;
end;

macro UploadAcc( Acc );

  Account = Acc;

  if(Account!="")
    DoCopyTransaction;
    if(not TrnStat)
      PrintLn(" Ошибка при загрузке счета ",Account);
    end;
  end;
end;


Macro DoReport( BranchName_From , BranchName_To )

  var TmpType = "";
  var Counter = 0l;
  var Itog    = $0;
  var i       = 0 ;

  OutHead   ( BranchName_From , BranchName_To);

  ClearRecord(Report);
  KeyNum(Report , 0);
  Rewind(Report);

  TmpType = Report.ВидВклада;

  Initprogress ( NRecords ( Report ) , NULL , " Формирование отчета ");
  While ( next ( report ) )

      If( TmpType == Report.ВидВклада )
          Counter = Counter + 1 ;
          Itog    = Itog    + Report.Rest ;
      else
          if( TmpType!= "" )
              OutBottom( Counter , Itog  , TmpType );
          end;
          TmpType = Report.ВидВклада;
          Counter = 1;
          Itog    = Report.Rest ;
      end;

   Outline();
   UseProgress ( i = i + 1);
   end;
   OutBottom( Counter , Itog  , TmpType );
   RemProgress ( i );
end;


macro UploadNumbers( void )

  var
    RecFound,
    Stat=true;

  ClearRecord(From(I_Numbers));
  From(I_Numbers).FNCash = FNCash_From;
  RecFound = GetGE(From(I_Numbers));
  while(Stat And RecFound And (From(I_Numbers).FNCash == FNCash_From))
    Copy(To(I_Numbers),From(I_Numbers));
    To(I_Numbers).FNCash = FNCash_To;
    Stat = Insert(To(I_Numbers));
    if(Stat)
      RecFound = Next(From(I_Numbers));
    end;
  end;
  return Stat;
end;


 var
   NumAcc,
   AutoKey = 0,
   RecFound,
   Stat = False;


 Stat = Constructor;
 If ( Stat )
   GetTrue(YesHistory," Обрабатывать историю перенумерации по счёту ");
   if( not GetTrue( True, String( "Номер филиала приемника \"", FNCash_To, "\"" ) ) )
     MsgBox( "Войдите в прoграмму под правильным номером филиала приемника" );
     Exit( -1 );
   end;
   BranchName_To=BranchNameForNum( FNCash_To );
   GetInt( FNCash_From,"Введите номер вливающегося филиала", 2 );

   if( ValType( FNCash_From ) == V_UNDEF )
     MsgBox( "Не введен номер вливающегося филиала" );
     Exit( 0 );
   end;

   BranchName_From = BranchNameForNum( FNCash_From );

   if( BranchName_From == 0 )
      MsgBox( "Не найден вливающийся филиал", FNCash_From );
      Exit( -1 );
   end;

   BranchNameForNum( FNCash_To );
   If(Stat)
     Stat = OpenTransFiles;
   end;
   If(Stat)
     OpenDepFiles;
   end;

   If(Stat)
     NumAcc=0;

     /* Переопределение указателей на файлы данных */
     SetNewPoints();

     /* Заливаем файл диапазонов */
     if( not NeedRenumb )
        if( not UploadNumbers() )
          MsgBox( "Ошибка при слиянии диапазонов номеров счетов" );
          Exit( -1 );
        end;
     end;

     ClearRecord( From(I_Dep) );
     keynum( From(I_Dep), 0 );
     rewind( From(I_Dep) );
     From(I_Dep).FNCash = FNCash_From;
     RecFound = GetGE( From(I_Dep) );
     while( RecFound and ( From(I_Dep).FNCash == FNCash_From ) )
       message(" Обрабатываю счет ",From(I_Dep).Account," ", From(I_Dep).Type_Account );
       GetPos( From(I_Dep) );
       UploadAcc( From(I_Dep).Account );
       NumAcc = NumAcc + 1;
       GetDirect( From(I_Dep) );
       RecFound=Next( From(I_Dep) );
     end;
/*
     ClearRecord( From(I_Compens) );
     rewind( From(I_Compens) );
     From(I_Compens).FNCash = FNCash_From;
     RecFound = GetGE( From(I_Compens) );
     while( RecFound and ( From(I_Compens).FNCash == FNCash_From ) )
       if( From(I_Compens).Date == NullDate )
         AutoKey = GetNewAutoKeyForCompens;
         if(AutoKey)
           Copy(To(I_Compens),From(I_Compens));
           To(I_Compens).FNCash = FNCash_To;
           To(I_Compens).AutoKey = AutoKey;
           Stat = Insert( To(I_Compens) );
         else
           Stat = false;
         end;
         if( not Stat )
           PrintLn( "Ошибка при переносе записи по компенсации. Счёт № ", From(I_Compens).Account );
         end;
       end;
       RecFound = Next( From(I_Compens) );
     end;
*/
     DoReport( BranchName_From , BranchName_To );
     CloseDepFiles;
   else
     Msgbox(" Работа прервана ");
     Exit(1) ;
   end;
 else
   Msgbox(" Файл отчета не открыт . Работа прервана ");
   Exit(1);
 end;
