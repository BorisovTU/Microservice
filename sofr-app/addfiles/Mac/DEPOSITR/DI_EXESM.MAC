/*
$Name:             DI_EXESM.mac
$Module:           RS-Retail
$Description:      Страхование частных вкладов.  Расчет сумм обязательств в разрезе каждого счета.
*/

 import DeprIntr, DI_Def, PtInter, RSD;

 var dip_ct = TBFile( "depiprev.dbt", "r", 1 );
 var dip_cc = TBFile( "depiprev.dbt", "w", 1 );  /* По клиентам. */

 private var smDepClnt = TClientList;

 private const MAX_INT32 = 2147483647;

 var clntLS = TClientList;

 /* Переменные параметров для предварительного формирования реестра */
 var panSumDateMake    = {curdate};  /* Дата формирования */
 var panSumClient      = 0;          /* Код клиента */
 var panSumEscape      = FALSE;      /* Флаг отказа от работы (нажата клавиша ESC) */

 var calc_DateMake;   /* Дата расчета. */
 var calc_CodClient;  /* Код клиента, для которого выполняется расчет. */

 /* Общие суммы остатков по клиенту. */
 var TotalRestRURDebet;   /* По депозитным счетам. */
 var TotalRestRURCredit;  /* По кредитовым счетам. */
 var TotalSummaRUR;       /* Общая сумма обязательств банка перед клиентом. */

 /* Массивы долей каждого счета данного клиента. */
 var   TotalInd;   /* Текущий индекс в массиве. */
 array TotalRef;   /* Массив Referenc-ов счетов. */
 array TotalPart;  /* Массив долей каждого счета. */

 var f_Find = FALSE;  /* Флаг: записи найдены \ не найдены. */

 var ErrCodeC, ErrTextC;

 var ErrorsSum_Num = 0;

 var {DI_LimitSum};

 var LimitSum = {DI_LimitSum};

 /***********************************************************************/
 /*                        Расчет сумм по клиенту.                      */
 /***********************************************************************/
 macro DI_Calc_Client()

   var ClientName;
   var TotalSize;
   var TotalSummaFullRUR;
   var TotalSummaFinalRUR;
   var Stat_c;

   /* Определение Ф.И.О. клиента. */
   if ( clntLS.GetRecord( calc_CodClient ) )
     ClientName = clntLS.CurRec.rec.Name1 + " " +
                  clntLS.CurRec.rec.Name2 + " " +
                  clntLS.CurRec.rec.Name3;
   else
     ClientName = String( calc_CodClient );
   end;

   /*** Шаг 1. Цикл 1: подсчет TotalRest..., заполнение массива TotalPart. ***/
   TotalRestRURDebet  = $0.0;
   TotalRestRURCredit = $0.0;

   dip_cc.ReWind();
   dip_cc.Clear();
   dip_cc.Rec.DateMake  = calc_DateMake;
   dip_cc.Rec.CodClient = calc_CodClient;
   Stat_c = dip_cc.GetGE();

   TotalInd = 0;

   while ( Stat_c  AND
           ( dip_cc.Rec.DateMake  == calc_DateMake )  AND
           ( dip_cc.Rec.CodClient == calc_CodClient ) )

     f_Find = TRUE;

     if     ( dip_cc.Rec.Type == TYPE_DEPOS )  /* По депозитным счетам. */

       TotalRestRURDebet = TotalRestRURDebet + dip_cc.Rec.SummaRestRUR;

       TotalRef [ TotalInd ] = dip_cc.Rec.Referenc;
       TotalPart[ TotalInd ] = DoubleL( dip_cc.Rec.SummaRestRUR );

       TotalInd = TotalInd + 1;

     elif ( ( dip_cc.Rec.Type == TYPE_CR_LOAN )  OR    /* Ссудный счет */
            ( dip_cc.Rec.Type == TYPE_CR_PERC )  OR    /* Счет процентов */
            ( dip_cc.Rec.Type == TYPE_CR_OVER )  OR    /* Просроченная задолженность */
            ( dip_cc.Rec.Type == TYPE_CR_OVERPERC ) )  /* Просроченные проценты */

       /* По кредитовым счетам. */
       TotalRestRURCredit = TotalRestRURCredit + dip_cc.Rec.SummaRestRUR;

     end;

     Stat_c = dip_cc.Next();

   end;

   /* Общая сумма обязательств банка перед клиентом. */
   TotalSummaRUR = TotalRestRURDebet - TotalRestRURCredit;

   /* Шаг 2. Проверка TotalSummaRUR. */

   if ( TotalSummaRUR <= $0.0 )

     /* Удаление всех записей клиента. */
     dip_cc.ReWind();
     dip_cc.Clear();
     dip_cc.Rec.DateMake  = calc_DateMake;
     dip_cc.Rec.CodClient = calc_CodClient;
     Stat_c = dip_cc.GetGE();

     while ( Stat_c  AND
             ( dip_cc.Rec.DateMake  == calc_DateMake )  AND
             ( dip_cc.Rec.CodClient == calc_CodClient ) )

       if ( NOT dip_cc.Delete() )
         println( "***  Ошибка при удалении записи клиента: ", ClientName );
         println( " По счету = ", Acc_Form( dip_cc.Rec.Account ) );

         ErrCodeC = Status( ErrTextC );
         println( "  Ошибка " + String( ErrCodeC ) + ": " + ErrTextC );
         println( "" );

         ErrorsSum_Num = ErrorsSum_Num + 1;
       end;

       Stat_c = dip_cc.Next();

     end;

     println( " Обязательств банка перед клиентом: ", ClientName, " нет." );
     println( " Записи по клиенту удалены." );

     return;
   end;

   /* Шаг 3. Пересчет массива TotalPart. */
   TotalSize = TotalInd;  /* Размер массива для текущего клиента. */
   TotalInd  = 0;

   while ( TotalInd < TotalSize )
     TotalPart[ TotalInd ] = DoubleL( TotalPart[ TotalInd ] / TotalRestRURDebet );
     TotalInd = TotalInd + 1;
   end;

   /* Шаг 4. Цикл 2: Заполнение SummaFullRUR. */
   TotalSummaFullRUR  = $0.0;
   TotalSummaFinalRUR = $0.0;

   dip_cc.ReWind();
   dip_cc.Clear();
   dip_cc.Rec.DateMake  = calc_DateMake;
   dip_cc.Rec.CodClient = calc_CodClient;
   Stat_c = dip_cc.GetGE();

   if ( TotalSize > 0 )
     TotalSize = TotalSize - 1;
   end;
   TotalInd = 0;

   while ( Stat_c  AND
           ( dip_cc.Rec.DateMake  == calc_DateMake )   AND
           ( dip_cc.Rec.CodClient == calc_CodClient )  AND
           ( dip_cc.Rec.Type == TYPE_DEPOS ) )

     if ( TotalRef [ TotalInd ] != dip_cc.Rec.Referenc )
       /* Нарушено соответствие счетов файла и массивов. */
       println( " Для клиента: ", ClientName,
                " произошло несоответствие счетов файла и массивов." );
       return;
     end;

     /* Обработка ошибок округления. */
     if ( TotalInd < TotalSize )
       /* Не последний счет. */

       dip_cc.Rec.SummaFullRUR = TotalPart[ TotalInd ] * TotalSummaRUR;
       TotalSummaFullRUR   = TotalSummaFullRUR + dip_cc.Rec.SummaFullRUR;

       if ( ( LimitSum == 0 )  OR  ( TotalSummaRUR <= LimitSum ) )
         dip_cc.Rec.SummaFinalRUR = Round( dip_cc.Rec.SummaFullRUR, 2 );
       else
         dip_cc.Rec.SummaFinalRUR = Round( TotalPart[ TotalInd ] * LimitSum, 2 );
         TotalSummaFinalRUR = TotalSummaFinalRUR + dip_cc.Rec.SummaFinalRUR;
       end;

     else

       /* Обработка ошибок округления для последнего счета. */
       dip_cc.Rec.SummaFullRUR = TotalSummaRUR - TotalSummaFullRUR;

       if ( ( LimitSum == 0 )  OR  ( TotalSummaRUR <= LimitSum ) )
         dip_cc.Rec.SummaFinalRUR = Round( dip_cc.Rec.SummaFullRUR, 2 );
       else
         dip_cc.Rec.SummaFinalRUR = Round( LimitSum - TotalSummaFinalRUR, 2 );
       end;

     end;

     TotalInd = TotalInd + 1;

     if ( NOT dip_cc.Update() )

       ErrorsSum_Num = ErrorsSum_Num + 1;

       println( "***  Ошибка при попытке обновления записи клиента: ", ClientName );
       println( " по счету = ", Acc_Form( dip_cc.Rec.Account ) );

       ErrCodeC = Status( ErrTextC );
       println( "  Ошибка " + String( ErrCodeC ) + ": " + ErrTextC );
       println( "" );

     end;

     Stat_c = dip_cc.Next();

   end;

 end;

 /***************************************************************************/
 /*  Головная процедура расчета суммы обязательств в разрезе каждого счета. */
 /***************************************************************************/
 macro DI_Calc_Acc( pDateMake,
                    pCodClient,
                    pNoInputPanel,
                    pNameLogFile )

   var vCodClient = pCodClient;  /* Код клиента */

   var NumRec;
   var Stat;
   var RetSum = 0;

   if ( pDateMake == Date( 0, 0, 0 ) )
     calc_DateMake  = {CurDate}
   else
     calc_DateMake  = pDateMake;
   end;

   /* Панель параметров расчета сумм. */
   if ( NOT pNoInputPanel )

     panSumDateMake    = calc_DateMake;   /* Дата формирования */
     panSumClient      = vCodClient;  /* Код клиента */
     panSumEscape      = FALSE;       /* Флаг отказа от работы (нажата клавиша ESC) */

     ExecMacroFile( "di_pnsm", "DI_Panel_Summ" );

     if ( panSumEscape )
       Exit( 1 );  /* В панели нажата клавиша Esc - отмена работы. */
     end;

     calc_DateMake = panSumDateMake;  /* Дата формирования */
     vCodClient = panSumClient;    /* Код клиента */

   end;

   ErrorsSum_Num = 0;

   if ( vCodClient == 0 )
     /* Цикл по типу - перебор всех клиентов. */

     dip_ct.ReWind();
     dip_ct.Clear();
     dip_ct.Rec.DateMake = calc_DateMake;
     Stat = dip_ct.GetGE();

     if ( Stat  AND  ( dip_ct.Rec.DateMake == calc_DateMake ) )

       f_Find = TRUE;

       calc_CodClient = dip_ct.Rec.CodClient;

       NumRec = 0;

       InitProgress( dip_ct.NRecords(), " Ж Д И Т Е ...",
                     "Перебор записей по клиентам." );

       while ( Stat  AND  ( dip_ct.Rec.DateMake == calc_DateMake ) )

         NumRec = NumRec + 1;

         UseProgress( NumRec );

         if ( calc_CodClient != dip_ct.Rec.CodClient )
           DI_Calc_Client();
           calc_CodClient = dip_ct.Rec.CodClient;
         end;

         Stat = dip_ct.Next();
       end;

       DI_Calc_Client();

       RemProgress();

     else
       f_Find = FALSE;
     end;
   else
     /* Расчет для одного конкретного клиента. */
     calc_CodClient = vCodClient;
     DI_Calc_Client();
   end;

   if ( f_Find )
     println( "Расчет сумм обязательств в разрезе каждого счета за ",
              calc_DateMake, " завершен." );
     println( "Обнаружено ошибок: ", ErrorsSum_Num );
   else
     println( "Расчет сумм обязательств: не найдено записей за ",
              calc_DateMake );
     RetSum = 1;
   end;

   println( "" );

   return RetSum;

 end;

//----------------------------------------------------------------------------
//     Определение клиента с минимальным PartyID среди клиентов-дублеров
//----------------------------------------------------------------------------
private macro unique_Client( _id )
  var cmd, rs;
  var ret = _id;
  cmd = RsdCommand( "select t_partyid as pi "
                    "from dparty_dbt "
                    "where t_mainpartyid = 0 start with t_partyid = ? connect by prior t_mainpartyid = t_partyid" );
  rs = RsdRecordset( cmd );
  cmd.addParam( "cc", RSDBP_IN );
  cmd.value( "cc" ) = _id;
  cmd.execute;
  if ( rs.moveNext() )
    ret = Int( rs.value( "pi" ) );
  end;
  return ret;
end;

//----------------------------------------------------------------------------
//                Установка кодов дублеров для клиентов
//----------------------------------------------------------------------------
private macro set_Unique_Client_Code( _date, _code, _unique )
  var cmd;
  cmd = RsdCommand( "update ddepiprev_dbt set T_UNIQUECLIENT=? "
                    "where T_DATEMAKE=? and T_CODCLIENT=?" );
  cmd.addParam ( "un", RSDBP_IN );
  cmd.value    ( "un" ) = _unique;
  cmd.addParam ( "dt", RSDBP_IN );
  cmd.value    ( "dt" ) = _date;
  cmd.addParam ( "cc", RSDBP_IN );
  cmd.value    ( "cc" ) = _code;
  cmd.execute;
end;

private macro set_Client_Codes( _date )
  var cmd;
  cmd = RsdCommand( "update ddepiprev_dbt set T_CODCLIENT=T_UNIQUECLIENT "
                    "where T_DATEMAKE=? and T_CODCLIENT<>T_UNIQUECLIENT and T_UNIQUECLIENT <> 0" );
  cmd.addParam ( "dt", RSDBP_IN );
  cmd.value    ( "dt" ) = _date;
  cmd.execute;
end;

macro set_Unique_Clients( _date )
  var cmd, rs, cc, cu, uc;
  cmd = RsdCommand( "select distinct(T_CODCLIENT) as clnt, T_UNIQUECLIENT as un "
                    "from ddepiprev_dbt "
                    "where T_DATEMAKE=? "
                    "order by T_CODCLIENT" );
  rs = RsdRecordset( cmd );
  cmd.addParam( "cc", RSDBP_IN );
  cmd.value( "cc" ) = _date;
  cmd.execute;
  while ( rs.moveNext() )
    cc = Int( rs.value( "clnt" ) );
    cu = Int( rs.value( "un" ) );
    uc = unique_Client( cc );
    if ( cu != uc )
      set_Unique_Client_Code( _date, cc, uc );
    end;
  end;
  set_Client_Codes( _date );
end;
