/*
$Name:           CLOSEDE2.mac
$Module:         RETAIL
$Description:    макрос закрытия счета для вида вклада "СБЕРЕГАТЕЛЬНЫЙ"
*/

/***************************************************************************\
| CLOSEDEP.MAC - макрос закрытия счета для вида вклада "СБЕРЕГАТЕЛЬНЫЙ"     |
|                                                     Зубов В.Ю. 09-09-97   |
\***************************************************************************/
import denomin, deprintr, Календарь, IsSrvDoc, alg_vars, sqlconv, PmPropUtil;

record FINISH_SBDEPDOC ("sbdepdoc.1");
file   fff             ("sbdepdoc.dbt");
record save_SBDEPDOC   (sbdepdoc      );
record save_DEPOSITOR  (depositr      );
private var doc = TDepFile( "sbdepdoc.dbt", "w" );
record tmpDoc          ("sbdepdoc.1"); /* файл документов */
file   dr              ("depositr.dbt"); /* файл счетов */
file   pc_apltp        (pc_apltp) key 0;
private var pcalg = TBFile( "pc_alg.dbt", "r", 0 );
private var pcestim = TBFile( "pcestim.dbt", "w", 0 );

var sbdepdoc12 = TRecHandler( "sbdepdoc.12" );

const ОТКАТ_С_РЕЗУЛЬТАТОМ = -777; /* Откат с получением процентов */

const ACCOUTN_HEIR     = 2; /* Наследник - выдачи */
const ACCOUTN_HEIR_PAY = 7; /* Наследник - выплаты */

const UPDATE_DEPDOC = 4;

const OnlyInputSum_Commission  = 1;
const OnlyInsertDoc_Commission = 2;

var IsFirst   = TRUE;
var Результат = 0;
var ErrMsg    = "";

var SummaComm = $0L;
var TypeComm  = "";
var NumOpert  = 0;

ALG_RESULT = OK_RES; /* заранее инициализируем код возврата */
ALG_UPDATE = 0;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DebitDayIncluded (bdate, IsInclude)
/* В качестве признака будем ориентироваться на правую границу соответсвующего
   алгоритма по виду вклада по альтернативным условиям */

  /* Ищем вид вклада по альтернативным условиям */
  pc_apltp.IsCur    = ALG_DEPOSITOR.IsCur;
  pc_apltp.TypeRec  = 1003;
  pc_apltp.Type     = ALG_DEPOSITOR.Type_Account;
  pc_apltp.ApplType = 2002; /* по альтернативным условиям */
  pc_apltp.ApType   = "";
  if (NOT GetEQ(pc_apltp))
    return FALSE;
  end;

  /* Ищем алгоритм расчета */
  pcalg.Clear;
  pcalg.addFilter( "T_FlagCur = " + String( ALG_DEPOSITOR.IsCur ) + " and " +
                   "T_Referenc = " + GetSQLString( pc_apltp.ApType ) + " and " +
                   "T_ObjectType = 1003"                                        );
  pcalg.rec.FlagCur    = ALG_DEPOSITOR.IsCur;
  pcalg.rec.Referenc   = pc_apltp.ApType;
  pcalg.rec.ObjectType = 1003;
  pcalg.rec.BegDate    = bdate;
  if ((NOT pcalg.GetLE                           ) OR
      (pcalg.rec.FlagCur    != ALG_DEPOSITOR.IsCur) OR
      (pcalg.rec.Referenc   != pc_apltp.ApType    ) OR
      (pcalg.rec.ObjectType != 1003               )   )
    pcalg.dropFilter;
    return FALSE;
  end;
  pcalg.dropFilter;

  if ((pcalg.rec.RangeAlg == 2) OR  /* левую исключать, правую включать */
      (pcalg.rec.RangeAlg == 3) OR  /* левую исключать, правую включать + первый день */
      (pcalg.rec.RangeAlg == 4)   ) /* левую включать, правую исключать + последний день */
    SetParm(1,TRUE);
  else
    SetParm(1,FALSE);
  end;

  return TRUE;

end;


/**************************************************************************\
|           Поиск последнего документа по накопленным процентам            |
\**************************************************************************/
macro GetLastPcEstim

  var stat;
  var findLast = false;

  pcestim.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) );
  pcestim.Clear;
  pcestim.rec.Referenc   = ALG_DEPOSITOR.Referenc;
  pcestim.rec.DateOperat = Date( 31, 12, 9999 );
  pcestim.rec.NumDayDoc  = 32000;
  stat = pcestim.GetLE;
  while ( stat )
    if ( pcestim.rec.Referenc != ALG_DEPOSITOR.Referenc )
      stat = false;
    else
      if ( pcestim.rec.Action < 2 )
        findLast = true;
        stat = false;
      else
        stat = pcestim.Prev;
      end;
    end;
  end;
  pcestim.dropFilter;

  return findLast;
  
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro CloseDep

  var
    NullDate  = Date(0,0,0),
    LimitDate = Date(0,0,0),
/*    EndCalcDate = {curdate},*/
    EndCalcDate = ALG_SBDEPDOC.DepDate_Document,
    VeryBigDate = Date(31,12,2999),
    found, Present,
    Summa, PercSum1,PercSum2, PercSum3,
    С_Панелью, UseMaxDate, NoPrint,
    Operation,
    SumPercOut = $0L,
    CorSumOut = $0L,
    st,
    stat = True,
    SumInCurrent,
    DateLastCapital,
    saveDate, saveSum,
    dd, mm, yy,
    Sum,
    SumInPerc, SumInPerc1,
    NeedInclude;

  Результат = 0;

  /* Для деноминированной базы*/
  CheckDenom(ALG_DEPOSITOR.Referenc);

  /* Вычисляем дату, с которой пересчитывать проценты */
  if (ALG_DEPOSITOR.Prol_DateDep != NullDate)
    LimitDate = ALG_DEPOSITOR.Prol_DateDep - 1;
  else
    LimitDate = ALG_DEPOSITOR.Start_DateDep;
  end;

  /* Добавлен учет новых условий по вкладам - включение дня
     расходной операции (письмо №02-536 от "01" февраля 1999г) */
  if (NOT DebitDayIncluded(LimitDate,NeedInclude))
    Результат = -1;
    ErrMsg = "Ошибка при определении вида учета дня совершения расх. операций";
    return;
  end;

  if (NeedInclude)
    EndCalcDate = EndCalcDate + 1;
  end;

  /* Механизм, полностью повторяющий инструкцию */
  /* 1.0 Сумма выданных процентов с учетом начисленных за незавершенный
         договор РАВ 21.01.99 */
  SumPercOut = 0;
  doc.KeyNum = 2;
  doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) );
  doc.Clear;
  doc.rec.Referenc         = ALG_DEPOSITOR.Referenc;
  doc.rec.DepDate_Document = LimitDate + 1;
  st = doc.GetGE;
  stat = TRUE;
  SumInPerc  = $0L;
  SumInPerc1 = $0L;
  while (st AND stat AND (doc.rec.Referenc == ALG_DEPOSITOR.Referenc))
    if ((doc.rec.TypeOper == Зачисление_Процентов) OR
        (doc.rec.TypeOper == Выдача_Процентов    )   )
      if (IsServDoc(doc.rec))
        if (doc.rec.TypeOper == Зачисление_Процентов)
          SumInPerc1 = SumInPerc1 + Den(doc.rec.InSum,doc.rec.DepDate_Document);
        elif (doc.rec.TypeOper == Выдача_Процентов)
          SumInPerc = SumInPerc + SumInPerc1;
          SumInPerc1 = 0;
          SumPercOut = SumPercOut + Den(doc.rec.OutSum,doc.rec.DepDate_Document) -
                                    Den(doc.rec.InSum,doc.rec.DepDate_Document);
        end;
      end;
    end;
    st = doc.Next;
  end;
  SumPercOut = SumPercOut - SumInPerc;
  if (SumPercOut < 0)
    SumPercOut = $0L;
  end;
  doc.dropFilter;

  /* 1. На остаток вклада на LimitDate  и на все дополнительные взносы */
  /*    начисляем  %% по до востребования(PercSum1) */
  Summa = $0L;
  PercSum1 = $0L;

  doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) );
  doc.KeyNum = 2;
  doc.Clear;
  doc.rec.Referenc = ALG_DEPOSITOR.Referenc;
  doc.rec.DepDate_Document = LimitDate;
  /* VZ 26.01.99 В последконтроле при закрытии счета в день открытия возник-
     ла ошибка "1.Не найдена необходимая операция за..." из за наличия уже
     вставленных записей последконтроля. Так же по видимому, данная ситуация
     могла возникать при наличии других операций за LimDate кроме ожидаемых -
     например допвзносов */
  found = doc.GetLE;
  Present = FALSE;
  while( found                                           AND
        (doc.rec.Referenc         == ALG_DEPOSITOR.Referenc) AND
        (doc.rec.DepDate_Document == LimitDate             )    )
    /* Проверяем, подходит ли нам операция */
    if (((doc.rec.TypeComplexOper == Открытие_Наличными   ) OR
         (doc.rec.TypeComplexOper == Открытие_Безналичными) OR
         (doc.rec.TypeOper        == Открытие_Безналичными) OR
         (doc.rec.TypeComplexOper == Открытие_Переводом   ) OR
         (doc.rec.TypeComplexOper == Пролонгация_Депозитов) OR
         (doc.rec.TypeOper == Зачисление_Процентов        )   ) AND
        /* Ромодин 25.11.97 в Уфе
           Сконвертировал 1-месячеый 4.11.97 (счет А20С16), а при
           завершении дня после конвертации он был пролонгирован
           Retail-ом, и записалась операция 35 - расчет процентов
           (doc.TypeOper == Расчет_Процентов))
        */
          IsServDoc(doc.rec)                                       )
      saveDate = doc.rec.DepDate_Document;

      Результат = Проценты_На_Операцию(ALG_DEPOSITOR.Account,ALG_DEPOSITOR.Type_Account,
                                       ALG_DEPOSITOR.Code_Currency,
                                       Den(doc.rec.Rest,doc.rec.DepDate_Document) - SumPercOut,
                                       Summa,doc.rec.DepDate_Document + 1,endCalcDate,2002,0 );
      if (Результат != 0)
        ErrMsg = "1.Ошибка при расчете %% = " + String(Результат);
        doc.dropFilter;
        return;
      end;
      PercSum1 =PercSum1 + Summa;
      found = FALSE;
      Present = TRUE;
    else
      found = doc.Prev;
    end;
  end;
  doc.dropFilter;

  if (NOT Present)
    Результат = -1;
    ErrMsg = "1.Не найдена необходимая операция за " + String(LimitDate);
    return;
  end;

  /* Продолжаем поиск */
  doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) );
  while ( doc.Next                                AND
         (doc.rec.Referenc == ALG_DEPOSITOR.Referenc) AND
         (doc.rec.DepDate_Document <= {curdate}     )    )
    if ( (( (   (doc.rec.TypeOper == Дополнительный_Взнос)
             OR ((doc.rec.TypeOper == Списание_Переводом) AND (doc.rec.InSum > $0l))
             OR (doc.rec.TypeOper == Зачисление_По_Данным_ПК)
             OR (doc.rec.TypeOper == Зачисление_В_Пак_Режиме)
             OR (doc.rec.TypeOper == Зачисление_Компенсации)
            ) AND
            ( doc.rec.DepDate_Document < {curdate} )
          ) OR
          ((doc.rec.Author == StrFor(ACCOUTN_HEIR    ) ) OR
           (doc.rec.Author == StrFor(ACCOUTN_HEIR_PAY) )
          )
         ) AND IsServDoc(doc.rec) )

      if (      (doc.rec.TypeOper == Дополнительный_Взнос)
             OR (doc.rec.TypeOper == Списание_Переводом)
             OR (doc.rec.TypeOper == Зачисление_По_Данным_ПК)
             OR (doc.rec.TypeOper == Зачисление_В_Пак_Режиме)
             OR (doc.rec.TypeOper == Зачисление_Компенсации) )
        Результат = Проценты_На_Операцию(ALG_DEPOSITOR.Account,ALG_DEPOSITOR.Type_Account,
                                         ALG_DEPOSITOR.Code_Currency,Den(doc.rec.InSum,doc.rec.DepDate_Document),
                                         Summa,doc.rec.DepDate_Document + 1,endCalcDate,2002,0 );
        CorSumOut = $0L;
      else
        saveSum = $0L;
        Результат = Проценты_На_Операцию(ALG_DEPOSITOR.Account,ALG_DEPOSITOR.Type_Account,
                                         ALG_DEPOSITOR.Code_Currency,Den(doc.rec.OutSum,saveDate) - CorSumOut,
                                         saveSum,saveDate + 1,endCalcDate,2002,0 );
        Summa = -saveSum;
        CorSumOut = 0;
      end;

      if (Результат != 0)
        ErrMsg = "1.Ошибка при расчете %% = "+String(Результат);
        doc.dropFilter;
        return;
      end;
      PercSum1 =PercSum1 + Summa;
    elif ((doc.rec.TypeOper == Зачисление_Процентов) AND
          (IsServDoc(doc.rec)                      ) AND
          (doc.rec.TypeComplexOper != Зачисление_Процентов )  OR
          (doc.rec.TypeComplexOper != Пролонгация_Депозитов)    )
      CorSumOut = doc.rec.InSum;
    end;
  end;
  doc.dropFilter;

  Summa = $0L;
  PercSum2 = $0L;
  Array NeedOperCode;
  var   NumOper = 0;

  NeedOperCode(0) = Зачисление_Процентов;
  NeedOperCode(1) = Переплата_Процентов;
  NeedOperCode(2) = Недоплата_Процентов;

  while (NumOper < Asize(NeedOperCode))
    doc.KeyNum = 1;
    doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) + " and " +
                   "T_TypeOper = " + String( NeedOperCode( NumOper ) )           );
    doc.Clear;
    doc.rec.Referenc         = ALG_DEPOSITOR.Referenc;
    doc.rec.TypeOper         = NeedOperCode(NumOper);
    doc.rec.DepDate_Document = LimitDate + 1; /* Пролонгация не учитывается */
    if ( doc.GetGE )
      found = TRUE;
      while ( found                                   AND
             (doc.rec.Referenc == ALG_DEPOSITOR.Referenc) AND
             (doc.rec.TypeOper == NeedOperCode(NumOper) ) AND
             (doc.rec.DepDate_Document <= {curdate}     )    )
        if (IsServDoc(doc.rec))
          if ((doc.rec.TypeComplexOper == Зачисление_Процентов ) OR
              (doc.rec.TypeComplexOper == Пролонгация_Депозитов) OR
              (doc.rec.TypeComplexOper == NeedOperCode(NumOper))   )
             PercSum2 = PercSum2 + Den(doc.rec.InSum,doc.rec.DepDate_Document)
                                 - Den(doc.rec.OutSum,doc.rec.DepDate_Document);
          end;
        end;

        if (NOT doc.Next)
          found = FALSE;
        end;
      end;
    end;
    doc.dropFilter;
    NumOper = NumOper + 1;
  end;

/* РАВ 22.01.99 Необходимо за период от последней капитализации
   (15 числа или начала договора) для каждой выдачи процентов,
   попавшей в этот период, расчитать процент на выданный процент,
   который был зачислен с начала договора по 15 числам
   Полная сумма зачисленных в это время процентов сейчас
   сидит в PercSum2, а выдачи надо просмотреть
   Если SumPerc2 = 0, то после начала договора
   капитализаций не было, и этот пункт мы пропустим
*/
  if (PercSum2 != 0)
    SumInCurrent = PercSum2;
/*    Дата последней капитализации */
    DateSplit({curdate},dd,mm,yy);
    if (dd <= 15)
      mm = mm - 1;
      if (mm == 0)
        mm = 12;
        yy = yy - 1;
      end;
    end;
    DateLastCapital = Date(16,mm,yy);
    if ((ALG_DEPOSITOR.Prol_DateDep != NullDate      ) AND
        (DateLastCapital < ALG_DEPOSITOR.Prol_DateDep)    )
      DateLastCapital = ALG_DEPOSITOR.Prol_DateDep;
    elif (DateLastCapital < ALG_DEPOSITOR.Start_DateDep)
      DateLastCapital = ALG_DEPOSITOR.Start_DateDep + 1;
    else /* Если найденное 15 число отстоит от начала договора меньше,
              чем на месяц, то дата капитализация = дате начала договора */
      if (DateAfterCalenMonths(LimitDate,1) > DateLastCapital)
        DateLastCapital = LimitDate + 1;
      end;
    end;
/*    Во всех выдачах процентов от последней капитализации
      выделяем выданные проценты, расчитанные с начала договора,
      и отчисляем проценты, расчитанные на эту долю выданных
      процентов от даты посл.капитализации */
    doc.KeyNum = 1;
    doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) + " and " +
                   "T_TypeOper = " + String( Выдача_Процентов )                  );
    doc.Clear;
    doc.rec.Referenc         = ALG_DEPOSITOR.Referenc;
    doc.rec.TypeOper         = Выдача_Процентов;
    doc.rec.DepDate_Document = DateLastCapital;
    found = doc.GetGE;
    while(found AND (SumInCurrent > 0) AND
         (doc.rec.Referenc == ALG_DEPOSITOR.Referenc) AND
         (doc.rec.TypeOper == Выдача_Процентов) AND
         (doc.rec.DepDate_Document <= {curdate})   )
      if (IsServDoc(doc.rec))
        sbdepdoc12.SetRecordAddr( doc, 0, 0, true );
        Sum = Den(doc.rec.OutSum-sbdepdoc12.rec.PcPercSum,doc.rec.DepDate_Document);
        if (Sum > SumInCurrent)
          Sum = SumInCurrent;
          SumInCurrent = $0L;
        else
          SumInCurrent = SumInCurrent - Sum;
        end;
        Результат = Проценты_На_Операцию(ALG_DEPOSITOR.Account,
                                         ALG_DEPOSITOR.Type_Account,
                                         ALG_DEPOSITOR.Code_Currency,Sum,
                Summa,DateLastCapital,doc.rec.DepDate_Document + 1,2002,0);

        if (Результат != 0)
          ErrMsg = "2.1 Ошибка при расчете %% = "+String(Результат);
          doc.dropFilter;
          return;
        end;
        PercSum2 = PercSum2 + Summa;
      end;
      found = doc.Next;
    end;
    doc.dropFilter;
  end;

  /* От полученной суммы причисленных %% отнимаем сумму отчисленного налога */
  PercSum3 = $0L;
  doc.KeyNum = 1;
  doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) + " and " +
                 "T_TypeOper = " + String( Списание_Простое )                  );
  doc.Clear;
  doc.rec.Referenc = ALG_DEPOSITOR.Referenc;
  doc.rec.TypeOper = Списание_Простое;
  doc.rec.DepDate_Document = LimitDate + 1; /* Пролонгация не учитывается */
  if ( doc.GetGE )
    found = TRUE;
    while( found                                   AND
          (doc.rec.Referenc == ALG_DEPOSITOR.Referenc) AND
          (doc.rec.TypeOper == Списание_Простое      ) AND
          (doc.rec.DepDate_Document <= {curdate}     )    )
      if ( IsServDoc( doc.rec ) and ( doc.rec.ApplType == Налог_Нерезидентов ) )
        PercSum3 = PercSum3 + Den(doc.rec.OutSum,doc.rec.DepDate_Document);
      end;
      if ( not doc.Next )
        found = FALSE;
      end;
    end;
  end;
  doc.dropFilter;

  if ((PercSum3 != 0) AND (PercSum2 != 0))
    PercSum2 = PercSum2 - PercSum3;
  end;

  /* 3. Отчисляем PercSum2 */
  if (PercSum2 != 0)
    ClearRecord(tmpDoc);
    tmpDoc.Referenc         = ALG_SBDEPDOC.Referenc;
    tmpDoc.IsCur            = ALG_SBDEPDOC.IsCur;
    tmpDoc.FNCash           = ALG_SBDEPDOC.FNCash;
    tmpDoc.RealFNCash       = ALG_SBDEPDOC.RealFNCash;
    tmpDoc.Account          = ALG_DEPOSITOR.Account;
    tmpDoc.Oper             = {oper};
    tmpDoc.Type_Account     = ALG_SBDEPDOC.Type_Account;
    tmpDoc.Code_Currency    = ALG_SBDEPDOC.Code_Currency;
    tmpDoc.CodClient        = ALG_SBDEPDOC.CodClient;
    tmpDoc.YesSbook         = "X";
    tmpDoc.Date_Document    = ALG_SBDEPDOC.Date_Document;
    tmpDoc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
    tmpDoc.Author           = ALG_SBDEPDOC.Author;
    tmpDoc.InSum            = -PercSum2;
    tmpDoc.TypeComplexOper  = ALG_PARAM.NumOpert;

    С_Панелью  = 0;
    UseMaxDate = 1;
    NoPrint    = 1;

    Результат = Выполнить_Операцию(ALG_DEPOSITOR.Account, ALG_DEPOSITOR.Type_Account,
                                   ALG_DEPOSITOR.Code_Currency,Зачисление_Процентов,
                                   tmpDoc,С_Панелью,UseMaxDate,NoPrint,ALG_PARAM.NumOpert);
    if (Результат != 0)
      ErrMsg = "3.Ошибка при отчислении PercSum2 = "+String(Результат);
      return;
    end;
  end;

  /* 5. Переводим на альтернативные условия */
  Результат = moveAccountIntoAnotherState(Перевод_В_Альт_Сост,ALG_DEPOSITOR.Referenc,
                                          LimitDate + 1,VeryBigDate );
  if (Результат != 0)
    ErrMsg = "5. Ошибка при переводе в альт.состояние " + String(Результат);
    return;
  end;

  /* 4-а Заполняем pcestim, иначе программа путается */
  if ( GetLastPcEstim( ) )
    pcestim.GetPos;
    pcestim.GetDirect;
    pcestim.rec.SummaProgn = PercSum1;
    pcestim.rec.SummaCalc  = PercSum1;
    if ( not pcestim.Update )
      ErrMsg = "10. Ошибка при заполнении накопленных процентов";
      return;
    end;
  end;

  /* 4. Причисляем PercSum1 */
  if (PercSum1 != 0)
    ClearRecord(tmpDoc);
    tmpDoc.Referenc         = ALG_SBDEPDOC.Referenc;
    tmpDoc.IsCur            = ALG_SBDEPDOC.IsCur;
    tmpDoc.FNCash           = ALG_SBDEPDOC.FNCash;
    tmpDoc.RealFNCash       = ALG_SBDEPDOC.RealFNCash;
    tmpDoc.Account          = ALG_DEPOSITOR.Account;
    tmpDoc.Oper             = {oper};
    tmpDoc.Type_Account     = ALG_SBDEPDOC.Type_Account;
    tmpDoc.Code_Currency    = ALG_SBDEPDOC.Code_Currency;
    tmpDoc.CodClient        = ALG_SBDEPDOC.CodClient;
    tmpDoc.YesSbook         = "X";
    tmpDoc.Date_Document    = ALG_SBDEPDOC.Date_Document;
    tmpDoc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
    tmpDoc.Author           = ALG_SBDEPDOC.Author;
    tmpDoc.InSum            = PercSum1;
    tmpDoc.TypeComplexOper  = ALG_PARAM.NumOpert;

    С_Панелью = 0;
    UseMaxDate = 1;
    NoPrint = 1;

    Результат = Выполнить_Операцию(ALG_DEPOSITOR.Account, ALG_DEPOSITOR.Type_Account,
                                   ALG_DEPOSITOR.Code_Currency,Зачисление_Процентов,
                                   tmpDoc,С_Панелью,UseMaxDate,NoPrint,ALG_PARAM.NumOpert);

    if (Результат != 0)
      ErrMsg = "4.Ошибка при зачислении PercSum1 = " + String(Результат);
      return;
    end;
  end;

  /* 6. Списываем весь остаток */
  KeyNum(dr,8);
  dr.Referenc = ALG_DEPOSITOR.Referenc;
  if (NOT GetEQ(dr))
    ErrMsg = "Не найдена запись по счету";
    Результат = -1;
    return;
  end;

  Copy(ALG_DEPOSITOR,dr);
  ALG_DEPOSITOR.End_DateDep = NullDate;

  if ((ALG_DEPOSITOR.Sum_Rest != 0) or (StrFor(GetProgramID) == "П"))
    ClearRecord(tmpDoc);
    tmpDoc.Referenc         = ALG_SBDEPDOC.Referenc;
    tmpDoc.IsCur            = ALG_SBDEPDOC.IsCur;
    tmpDoc.FNCash           = ALG_SBDEPDOC.FNCash;
    tmpDoc.RealFNCash       = ALG_SBDEPDOC.RealFNCash;
    tmpDoc.Account          = ALG_DEPOSITOR.Account;
    tmpDoc.Oper             = {oper};
    tmpDoc.Type_Account     = ALG_SBDEPDOC.Type_Account;
    tmpDoc.Code_Currency    = ALG_SBDEPDOC.Code_Currency;
    tmpDoc.CodClient        = ALG_SBDEPDOC.CodClient;
    tmpDoc.YesSbook         = "X";
    tmpDoc.Date_Document    = ALG_SBDEPDOC.Date_Document;
    tmpDoc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
    tmpDoc.Author           = ALG_SBDEPDOC.Author;
    tmpDoc.ApplType         = FINISH_SBDEPDOC.ApplType;
    //tmpDoc.Account_Receiver = FINISH_SBDEPDOC.Account_Receiver;
    //tmpDoc.CodeCur_Receiver = FINISH_SBDEPDOC.CodeCur_Receiver;
    tmpDoc.PercTrnRest      = FINISH_SBDEPDOC.PercTrnRest;
    tmpDoc.PercTrnAlt       = FINISH_SBDEPDOC.PercTrnAlt;
    tmpDoc.CurrencyBought   = FINISH_SBDEPDOC.CurrencyBought;
    tmpDoc.SummaTaxRub      = FINISH_SBDEPDOC.SummaTaxRub;
    //tmpDoc.MFO_Receiver     = FINISH_SBDEPDOC.MFO_Receiver;
    //tmpDoc.CorAcc_Receiver  = FINISH_SBDEPDOC.CorAcc_Receiver;
    tmpDoc.OutSum           = GetLastFCDocRest(ALG_DEPOSITOR.Referenc,{curdate});
    tmpDoc.TypeComplexOper  = ALG_PARAM.NumOpert;
    tmpDoc.IsControl        = StrFor(1); /* Признак главного документа */

    if ((FINISH_SBDEPDOC.TypeOper != 0                 ) AND
        (FINISH_SBDEPDOC.TypeOper != ALG_PARAM.NumOpert)    )
      Operation = FINISH_SBDEPDOC.TypeOper;
    elif (ALG_PARAM.NumOpert == Закрытие_Наличными)
      Operation = Расход;
    else
      Operation = Списание_По_Раз_Поруч;
    end;

    tmpDoc.TypeOper = Operation;

    if (Operation == Безналичная_Конверсия)
      /* Пересчет суммы с учетом подоходного налога */
      if (tmpDoc.SummaTaxRub != 0)
        tmpDoc.OutSum = tmpDoc.OutSum - tmpDoc.SummaTaxRub;
      end;
      /* Пересчет конвертируемой суммы с учетом процентов */
      if (tmpDoc.PercTrnRest != 0)
        tmpDoc.CurrencyBought = tmpDoc.OutSum / tmpDoc.PercTrnRest;
      end;
    end;

    if (IsFirst)
      Результат = ОТКАТ_С_РЕЗУЛЬТАТОМ;
      InterDesk_EndDocBunch();
      return;
    elif (SummaComm > 0)
      Результат = GetCommissionOnOper(tmpDoc,tmpDoc.OutSum,OnlyInsertDoc_Commission,SummaComm,TypeComm,NumOpert,ALG_PARAM.NumOpert);
      if (Результат != 0)
        ErrMsg = "Ошибка при вставке документа на комиссию " + String(Результат);
        return;
      end;
    end;

    С_Панелью  = 0;
    UseMaxDate = 1;
    NoPrint    = 1;

    ClearRecord(ALG_OPERPRM);
    ALG_OPERPRM.Account           = ALG_DEPOSITOR.Account;
    ALG_OPERPRM.Type_Account      = ALG_DEPOSITOR.Type_Account;
    ALG_OPERPRM.Code_Currency     = ALG_DEPOSITOR.Code_Currency;
    ALG_OPERPRM.Type_Oper         = Operation;
    ALG_OPERPRM.Show_Panel        = С_Панелью;
    ALG_OPERPRM.UseMaxDate        = UseMaxDate;
    ALG_OPERPRM.NoPrint           = NoPrint;
    ALG_OPERPRM.NonCashCommission = ALG_PARAM.prmI;
    ALG_OPERPRM.TypeComplexOper   = ALG_PARAM.NumOpert;
    ALG_OPERPRM.DocumentAuthor    = CodeFor(tmpDoc.Author);
    if (Operation == Списание_Переводом)
      ALG_OPERPRM.CorrAcc         = PMNT_PROP_REC_BUF.Account;
      ALG_OPERPRM.CorrType        = PMNT_PROP_REC_BUF.CorrAccount;
    end;

    Результат = Выполнение_Операции(ALG_OPERPRM,tmpDoc);

    if (Результат != 0)
     ErrMsg = "6. Ошибка при расходе "+String(Результат);
     return;
    end;

  else
    if (IsFirst)
      ClearRecord(tmpDoc);
      Результат = ОТКАТ_С_РЕЗУЛЬТАТОМ;
      InterDesk_EndDocBunch();
      return;
    end;
  end;

  /* Наводим марафет- чистим грязь в полях процентов */
  doc.KeyNum = 6;
  doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) );
  doc.Clear;
  doc.rec.Referenc      = ALG_DEPOSITOR.Referenc;
  doc.rec.Date_Document = {curdate};
  doc.rec.NumDayDoc     = 0;
  st = doc.GetGE;
  stat = TRUE;
  while (st AND stat AND (doc.rec.Referenc == ALG_DEPOSITOR.Referenc))
    doc.rec.PercOprSum = $0L;
    doc.rec.PercRest   = $0L;
    stat = doc.Update;
    st = doc.Next;
  end;
  doc.dropFilter;

  ALG_RESULT = END_RES; /* Все Ok */

  if ((ALG_PARAM.prmC == "X") and (Результат == 0))
    ALG_PARAM.prmD = PercSum1;
    Результат = ОТКАТ_С_РЕЗУЛЬТАТОМ;
  else
    UpdateAlgRecords(UPDATE_DEPDOC,true);
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro TrnProc

  CloseDep();

  if (Результат != 0)
    if (Результат == ОТКАТ_С_РЕЗУЛЬТАТОМ)
      ;
    else
      ALG_RESULT = ERR_RES;
    end;
    AbortTrn();
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  if ( ALG_DEPOSITOR.UseAlternate == 0 )

    ClearRecord(tmpDoc);

    Copy(save_DEPOSITOR,ALG_DEPOSITOR);
    Copy(save_SBDEPDOC,ALG_SBDEPDOC);
    Copy(fff,ALG_SBDEPDOC);
    SetRecordAddr(FINISH_SBDEPDOC,fff,0,0,true);

    LoopInTrn(TRUE);

    /* Нужно взять комиссию - для этого предварительно определим сумму расхода */
    IsFirst = TRUE;
    if ((ALG_PARAM.prmI2 >   0 ) AND
        (ALG_PARAM.prmC  != "X")    )
      if ((StrFor(GetProgramID) == "П") AND (ALG_PARAM.prmI > 0))
        ;
      else
        if (NOT ProcessTrn(1,"TrnProc",dr,pc_apltp))
          if (Результат != ОТКАТ_С_РЕЗУЛЬТАТОМ)
            ALG_RESULT = ERR_RES;
            Результат = 4714;
          end;
        end;
        Copy(ALG_DEPOSITOR,save_DEPOSITOR);
        Copy(ALG_SBDEPDOC,save_SBDEPDOC);
        Copy(fff,ALG_SBDEPDOC);
        SetRecordAddr(FINISH_SBDEPDOC,fff,0,0,true);
      end;
    end;

    /* Проведем собственно операцию закрытия счета */
    IsFirst = FALSE;
    if (ALG_RESULT != ERR_RES)
      Результат = 0;
      if ((ALG_PARAM.prmI2 >   0 ) AND
          (ALG_PARAM.prmC  != "X") AND
          (tmpDoc.OutSum   >   0 )    )
        if ((StrFor(GetProgramID) == "П") AND (ALG_PARAM.prmI > 0))
          ;
        else
          Результат = GetCommissionOnOper(tmpDoc,tmpDoc.OutSum,OnlyInputSum_Commission,SummaComm,TypeComm,NumOpert);
          if (Результат == 2)
            MsgBox("Отказались от проведения операции");
            ALG_RESULT = ERR_RES;
          elif (Результат != 0)
            ErrMsg = "Ошибка при взятии комиссии = " + String(Результат);
            ALG_RESULT = ERR_RES;
          end;
        end;
      end;
      if (Результат == 0)
        if (NOT ProcessTrn(1,"TrnProc",dr,pc_apltp))
          if (Результат != ОТКАТ_С_РЕЗУЛЬТАТОМ)
            ALG_RESULT = ERR_RES;
            Результат = 4714;
          end;
        end;
      end;
    end;

    if ((ALG_PARAM.prmC == "X") AND (Результат == ОТКАТ_С_РЕЗУЛЬТАТОМ))
      ALG_RESULT = END_RES;
      ALG_UPDATE = 0;
    end;

    if ((ErrMsg != "") AND (NOT InFCGroupOpRun()))
      MsgBox(ErrMsg);
    end;

  else
    ALG_RESULT = OK_RES; /* Выполнить закрытие стандартным образом */
  end;

end;
