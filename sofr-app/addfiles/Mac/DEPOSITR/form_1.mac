import deprintr, BankInter, brparm, ChkPrn, alg_vars, f_sign, OperCode, DocBunch, FirstLog;

file DepType(sb_dtyp) key 2;
file DepDoc(sbdepdoc) key 3;
file PAPRKIND ( paprkind,"bank.def" ) key 0;

/****************************************************************/
/*                  Флаги для пользователей                     */
/****************************************************************/
const FlagCard = FALSE;  /* Флаг печати в режиме Сбербанка для  */
                         /* карточных счетов ф.0401026.         */

const
   ACCOUTN_OWNER       = StrFor( 0 ),    /* Владелец счета      */
   ACCOUTN_PROXY       = StrFor( 1 ),    /* Доверенное лицо     */
   ACCOUTN_HEIR        = StrFor( 2 ),    /* Наследник           */
   ANONIMUS_CLIENT     = StrFor( 3 ),    /* Анонимный клиент    */
   ACCOUNT_IMPORTER    = StrFor( 4 ),    /* Вноситель           */
   ACCOUNT_TUTOR       = StrFor( 5 ),    /* Опекун              */
   ACCOUNT_OBLIGATOR   = StrFor( 6 ),    /* Под обязательство   */
   ACCOUTN_HEIR_PAY    = StrFor( 7 ),    /* Наследник - выплаты */
   ACCOUNT_UNDER_TRIAL = StrFor( 8 ),    /* По суду             */
   ACCOUNT_PARENT      = StrFor( 9 );    /* Родитель            */

const SB_DEPOSIT       = 1;

const BirthPlace = 1; // место рождения
const PhoneTypeProp = 4;     /* Вид номера телефона  */
                         /*  4 - домашний телефон,              */
                         /*  5 - рабочий телефон.               */

var Bank_Standart = GetBankStandart();

var query;

macro findDepDoc( appKind, appKey )

  DepDoc.iApplicationKind = appKind;
  DepDoc.ApplicationKey = appKey;
  return getEQ( DepDoc );
end;

macro getSum( dd )

  if ( dd.InSum )
    return dd.InSum;
  else
    return dd.OutSum;
  end;
end;

macro isOpenOp( dd )

 return ( dd.TypeOper == Открытие_Наличными )
        or ( dd.TypeOper == Открытие_Безналичными )
        or ( dd.TypeOper == Открытие_Переводом );
end;

macro isCloseOp( dd )

 return ( dd.TypeComplexOper == Закрытие_Наличными )
        or ( dd.TypeComplexOper == Закрытие_Безналичными )
        or ( dd.TypeComplexOper == Закрытие_Переводом );
end;

macro CheckPrn()

  var
    RegPath = "RS-RETAIL\\ПЕЧАТИ\\ВКЛАДЫ\\ФОРМА_1";

  CheckPrinter(RegPath, StrFor(0));
end;

macro GetDepTypeName()

  DepType.FlagCur = ALG_DEPOSITOR.IsCur;
  DepType.Kind = ALG_DEPOSITOR.Type_Account;
  if(GetEQ(DepType))
    return DepType.Name + " Сбербанка России";
  else
    return " Сбербанка России";
  end;
end;

macro GetCurrencyType()

  if(ALG_DEPOSITOR.IsCur)
    return "инвалюте";
  else
    return "рублях";
  end;
end;

macro GetOpeningType()

  if(ALG_SBDEPDOC.TypeOper == 1)
    return "наличными деньгами";
  else
    return "безналичным путем";
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetClientCountry()

  file  Country ( country ) key 2;

  var ret_name  = "";
  var name_file = "country.dbt";
/*  var name_file = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\DISTDBFILE", true );

  if ( StrLen( name_file ) > 0 )
    if ( SubStr( name_file, StrLen( name_file ), 1 ) != "\\" )
      name_file = name_file + "\\";
    end;
  end;
  name_file = name_file + "country.dbt";
*/
  if ( ALG_COMDEPCLNT.rec.NRCountry == "" )
    ret_name = "Россия";
  elif ( open( Country, name_file ) )
    Country.CodeLat3 = ALG_COMDEPCLNT.rec.NRCountry;
    if ( GetEQ( Country ) )
      ret_name = Country.Name;
    end;
  end;

  return ret_name;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetRezidence()

  if(ALG_COMDEPCLNT.rec.NotResident == "")
    return "Резидент";
  else
    return "Нерезидент";
  end;
end;

macro GetClientType

  var DepClnt = TClientList;

  if((ALG_SBDEPDOC.Author == ACCOUNT_IMPORTER)
     and (ALG_SBDEPDOC.CodClient != ALG_DEPOSITOR.CodClient))
    if(ALG_SBDEPDOC.CodClient > 0)
      if (DepClnt.GetRecord (ALG_SBDEPDOC.CodClient))
        return "вносителем " + DepClnt.CurRec.ConvertFIOFull();
      end;
    else
      return "вносителем";
    end;
  else
    return "вкладчиком";
  end;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetOperName()

  file person( person ) key 0;

  var s, s0, s1, s2, s3;
  var b;

  if(ALG_SBDEPDOC.Oper != 0)
    person.Oper = ALG_SBDEPDOC.Oper;
    if(getEQ(person))
      s0 = Trim( person.Name );
      b  = StrBrk( s0, " " );
      s1 = SubStr( s0, 1, b-1 );
      s2 = Trim(SubStr( s0, b ));
      b  = StrBrk( s2, " " );
      s3 = Trim(SubStr( s2, b ));
      s = s1 + " " + SubStr( s2, 1, 1 ) + "." + SubStr( s3, 1, 1 ) + ".";
      return s;
    else
      return "";
    end;
  end;
  return "";
end;

macro GetAccount( Doc )
  return "";
end;

macro GetPhoneNumber()
  var DepClnt = TClientList;
  var AddrTypes = TArray;
  var i = 0;
  var PhoneNumber = "", PhoneNumber2 = "";

  if (DepClnt.GetRecord (ALG_COMDEPCLNT.rec.CodClient))
    AddrTypes(0) = COMMCLNT_PTADDR_LEGAL;
    AddrTypes(1) = COMMCLNT_PTADDR_REAL;
    AddrTypes(2) = COMMCLNT_PTADDR_POST;

    while ((i < AddrTypes.Size) and (PhoneNumber == ""))
      PhoneNumber = commclnt_GetAddressField(DepClnt, AddrTypes(i), "PhoneNumber");
      PhoneNumber2 = commclnt_GetAddressField(DepClnt, AddrTypes(i), "PhoneNumber2");
      if ((PhoneNumber != "") and (PhoneNumber2 != ""))
        PhoneNumber = PhoneNumber + ", ";
      end;
      PhoneNumber = PhoneNumber + PhoneNumber2;
      i = i + 1;
    end;
  end;
  return PhoneNumber;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro PrintTwoColumns(text1, text2, count)
  var column1 = TArray;
  var column2 = TArray;
  var str1, str2;
  var i = 0;

  column1 = StrSplit2( text1, 30);
  column2 = StrSplit2( text2, 30);
  while (i < count)
    if (i < column1.size)
      str1 = column1[i];
    else
      str1 = "_";
    end;
    if (i < column2.size)
      str2= column2[i];
    else
      str2= "_";
    end;
[    #_______________________________ | #________________________________] (str1, str2);
    i = i + 1;
  end;
end;

macro PrintAddresses()
  var DepClnt = TClientList;
  var LegalAddr = "";
  var RealAddr = "";

  if (DepClnt.GetRecord (ALG_COMDEPCLNT.rec.CodClient))
    if(ALG_COMDEPCLNT.rec.NotResident == "")
      LegalAddr = commclnt_Получить_Поле_Address_Заданного_Типа(DepClnt, COMMCLNT_PTADDR_LEGAL);
    else
      RealAddr = commclnt_Получить_Поле_Address_Заданного_Типа(DepClnt, COMMCLNT_PTADDR_REAL);
    end;
  end;
[
     --------------------------------------------------------------------
       Адрес места жительства         |   Адрес места пребывания
       (регистрации) вкладчика        |   вкладчика
];
  PrintTwoColumns(LegalAddr, RealAddr, 6);
[    --------------------------------------------------------------------
];
end;


/****************************************************************/
/*             Печать формы 1 для Сбербанка РФ                  */
/****************************************************************/
macro printFormSBFront()

var DepClnt = TClientList;
var
  BranchStrNum = "",
  DepartNum  = "",
  DepartName = "",
  ClientName = "",
  s_BirthPlace = "",
  s_PaperName = "",
  s_PaperNum = "",
  s_PaperIssue = "",
  s_PaperIssuerCode = "",
  s_PaperNotResident = "";

  /* if ( ALG_DEPOSITOR.SpecialAccess == StrFor(0) ) */
    ClientName = ALG_COMDEPCLNT.ConvertFIOFull();
  /*
  else
    DepType.FlagCur = ALG_DEPOSITOR.IsCur;
    DepType.Kind    = ALG_DEPOSITOR.Type_Account;
    if ( getEQ( DepType ) )
      ClientName = DepType.Name;
    else
      ClientName = ALG_DEPOSITOR.Type_Account;
    end;
  end; */

GetBranchParams(ALG_SBDEPDOC.FNCash, BranchStrNum, DepartNum, DepartName);

//s_BirthPlace = GetBirthPlace();
if (DepClnt.GetRecord (ALG_COMDEPCLNT.rec.CodClient))
    s_BirthPlace = DepClnt.CurRec.rec.BirsPlace;
    //  Название документа
    PAPRKIND.PaperKind = DepClnt.CurRec.PaperKind;
    if (GetEQ(PAPRKIND))
      s_PaperName = Trim(PAPRKIND.Name);
    end;
    s_PaperNum = DepClnt.CurRec.rec.PaperSeries + " " + DepClnt.CurRec.rec.PaperNumber;
    s_PaperIssue = DepClnt.CurRec.rec.PaperIssuer + " " + DepClnt.CurRec.rec.PaperIssuedDate;
    s_PaperIssuerCode = DepClnt.CurRec.rec.PaperIssuerCode;
    // документ нерезидента
    if (ALG_COMDEPCLNT.rec.NotResident != "")
        DepClnt.CurRec.PaperKind = 11;  // Иностранный паспорт
        if (DepClnt.CurRec.PaperKind == 11)
            PAPRKIND.PaperKind = DepClnt.CurRec.PaperKind;
            if (GetEQ(PAPRKIND))
              s_PaperNotResident = Trim(PAPRKIND.Name);
            end;
            s_PaperNotResident = s_PaperNotResident + " " + DepClnt.CurRec.rec.PaperSeries + " " + DepClnt.CurRec.rec.PaperNumber;
            s_PaperNotResident = s_PaperNotResident + " " + DepClnt.CurRec.rec.PaperIssuer + " " + DepClnt.CurRec.rec.PaperIssuedDate;
        end;
    end;
end;

[
     СБЕРБАНК РОССИИ ОАО
                ВСП № #              СЧЕТ № #


     Вид вклада "##################################################"
     в #         Открыт #

     ##################################################################
                           ------------------------
                           |                      |
                           |                      |
                           |                      |
                           |                      |
                           ------------------------
                         (Образец подписи вкладчика)
     Дата рождения #

     ####################################################################

     Гражданство   #################################### #

     Счет открыт   #
]
(
  BranchStrNum,
  Acc_Form(ALG_DEPOSITOR.Account):f,
  GetDepTypeName:r,
  GetCurrencyType,
  GetOpeningType,
   ClientName:w,
  ALG_COMDEPCLNT.rec.Born:m,
  ("Место рождения " + s_BirthPlace):w,
  GetClientCountry,
  GetRezidence,
  GetClientType
);
PrintAddresses();
[    Дата открытия #                         ИНН #_______________________
     Номера контактных телефонов и факсов #______________________________
     Бухгалтерский работник _____________ ###############################
                            (подпись, фамилия, и., о.)
]
(
  ALG_DEPOSITOR.Open_Date:m,
  ALG_COMDEPCLNT.rec.INN,
  GetPhoneNumber,
  GetOperName
);
print("\f");
[
     Дата начала отношений с вкладчиком #

     Вид документа, удостоверяющего личность вкладчика __________________
     _#__________________________________________________________________

     серия (номер) #_____________________________________________________

     ####################################################################
                                     Код подразделения #_________________
     --------------------------------------------------------------------
]
(
  ALG_DEPOSITOR.Open_Date:m,
  s_PaperName,
  s_PaperNum,
  ("Кем, где, когда выдан " + s_PaperIssue):w,
  s_PaperIssuerCode
);
PrintTwoColumns(s_PaperNotResident, "", 3);
[
     --------------------------------------------------------------------
     Дата заполнения #

                                ДЛЯ  ОТМЕТОК
     ____________________________________________________________________
     ____________________________________________________________________
     ____________________________________________________________________
     ____________________________________________________________________
     ____________________________________________________________________

     Дата закрытия ______________________________________________________

         Бухгалтерский работник _________________________________________
]
(
  ALG_DEPOSITOR.Open_Date:m
);
print(StrFor(13));
end;

macro printFormSBBack()

  var
    pcSum = $0, pcAccount = "",
    commSum = $0, commAccount = "",
    outSum = $0, outAccount = "",
    isCashStr,
    pcDebCredStr,
    bunch = TDocBunch( ALG_SBDEPDOC.iApplicationKind, ALG_SBDEPDOC.ApplicationKey ),
    docFound;

  if ( ConfDlg( query, NULL, "Да", "Нет" ) != 0 )
    ALG_RESULT = SKIP_RES
  end;

  docFound = bunch.firstDoc;
  while ( docFound )
    if ( ( bunch.docAppKind == SB_DEPOSIT ) and ( bunch.docValueRef == 0 ) )
      if ( findDepDoc( bunch.docAppKind, bunch.docAppKey ) )
        if ( isCloseOp( depDoc ) )
          if ( depDoc.TypeOper == Зачисление_Процентов )
            pcSum = getSum( depDoc );
            pcAccount = getAccount( depDoc );
          elif ( ( depDoc.TypeOper == Списание_Простое )
             and ( depDoc.ApplType == Безналичная_Комиссия ) )
            commSum = getSum( depDoc );
            commAccount = getAccount( depDoc );
          elif ( ( depDoc.TypeOper == Расход )
              or ( depDoc.TypeOper == Списание_Простое )
              or ( depDoc.TypeOper == Списание_По_Раз_Поруч )
              or ( depDoc.TypeOper == Списание_По_Длит_Поруч )
              or ( depDoc.TypeOper == Списание_Переводом )
              or ( depDoc.TypeOper == Списание_В_Свой_Филиал )
              or ( depDoc.TypeOper == Списание_В_Чужой_Филиал ) )
            outSum = getSum( depDoc );
            outAccount = getAccount( depDoc );
          end;
        end;
      end;
    end;
    docFound = bunch.nextDoc;
  end;

  if ( ALG_SBDEPDOC.TypeComplexOper == Закрытие_Наличными )
    isCashStr = "наличными";
  else
    isCashStr = "           безналично";
  end;

  if ( pcSum < 0 )
    pcDebCredStr = "Удержаны %%";
    pcSum = -pcSum;
  else
    pcDebCredStr = "Причислены %%";
  end;

  [  Дата закрытия #                         Закрыт #

     #              #############     Корр.счет #
     Списана плата  #############     Корр.счет #
     Сумма          #############     Корр.счет #

                                Контролер ###############################
                                               (подпись, фамилия, и., о.)

                               ДОВЕРЕННОСТЬ
                                               "___" _________________ г.
     ____________________________________________________________________
     ____________________________________________________________________
     ____________________________________________________________________
     Вкладчик _________________ Контролер _______________________________
                  (подпись)                    (подпись, фамилия, и., о.)


                               ДЛЯ ОТМЕТОК
     ____________________________________________________________________
     ____________________________________________________________________
     ____________________________________________________________________
     Составлена доп. карточка №_________ Контролер ______________________
                                               (подпись, фамилия, и., о.)
  ]
  (
    ALG_DEPOSITOR.Close_Date, isCashStr,
    pcDebCredStr, pcSum:z, Acc_Form(pcAccount),
    commSum:z, Acc_Form(commAccount),
    outSum:z, Acc_Form(outAccount),
    getOperName
  );

  print(StrFor(13));
end;

macro printForm()
  if ( Bank_Standart == 0 )
    /* Сберегательный банк */
    if ( ( Index( ALG_DEPOSITOR.UserTypeAccount, "К" ) > 0 )  AND  FlagCard )
      if ( isOpenOp( ALG_SBDEPDOC ) )
        if ( ConfDlg(query, NULL, "Да", "Нет") == 0 )
          printFormKB();  /* Для карточных счетов печатается ф.0401026 */
        else
          ALG_RESULT = SKIP_RES
        end;
      end;
    elif ( Index( ALG_DEPOSITOR.UserTypeAccount, "Щ" ) > 0 )
      /* Для текущих и специальных счетов печатается ф.0401026 */
      if ( ConfDlg(query, NULL, "Да", "Нет") == 0 )
        printFormKB();
      else
        ALG_RESULT = SKIP_RES
      end;
    else
      if ( isOpenOp( ALG_SBDEPDOC ) )
        printFormSBFront();
      elif ( isCloseOp( ALG_SBDEPDOC ) )
        if ( ( ALG_SBDEPDOC.Author == ACCOUTN_HEIR )  OR
             ( ALG_SBDEPDOC.Author == ACCOUTN_HEIR_PAY ) )
          Exit( 1 );  // Если наследник - форма не выпускается. SCR #94047.
        else
          printFormSBBack();
        end;
      else
        Exit( 1 );
      end;
    end;
  else
    /* Коммерческий банк */
    if ( isOpenOp( ALG_SBDEPDOC ) )
      if ( ConfDlg(query, NULL, "Да", "Нет") == 0 )
        printFormKB();
      else
        ALG_RESULT = SKIP_RES
      end;
    else
      ALG_RESULT=OK_RES;
      EXIT(1);
    end;
  end;
end;

if ( ( ALG_SBDEPDOC.TypeOper == 0 )  AND  // Нефинансовая операция.
     ( NOT IsFirstLog() ) )  // Если повторный вызов при выполнении операции.
  ALG_RESULT = OK_RES;
  Exit( 1 ); // Убрать вывод на экран.
end;

/* Убираем вывод на экран*/
if(StrFor(GetProgramID) == "П")
  ALG_RESULT = OK_RES;
  Exit(1); /* Убрать вывод на экран */
  return;
end;

if ( isCloseOp( ALG_SBDEPDOC )  AND
     ( ( ALG_SBDEPDOC.Author == ACCOUTN_HEIR )  OR
       ( ALG_SBDEPDOC.Author == ACCOUTN_HEIR_PAY ) ) )
  Exit( 1 );  // Если наследник - форма не выпускается. SCR #94047.
end;

CheckPrn();

if ( isOpenOp( ALG_SBDEPDOC ) )
  query = "Печатать карточку счета?";
else
  query = "Карточка счета вставлена в принтер?";
end;

if ( GetBitFlag( ALG_SBDEPDOC.Flags, 12 ) == 0 )
  // if(ConfDlg(query, NULL, "Да", "Нет") == 0)
    printForm();
  //   ALG_RESULT = OK_RES;
  // else
  //   ALG_RESULT = SKIP_RES
  // end;
else
  if(ConfDlg("Карточка счета напечатана. Печатать еще раз?", NULL, "Да", "Нет") == 0)
    printForm();
    ALG_RESULT = OK_RES;
  else
    ALG_RESULT = SKIP_RES
  end;
end;
ExitMacro;
end;
