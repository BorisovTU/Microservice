
/*
**  Ведомость на выкуп облигаций целевого займа
**
**
*/

import DeprIntr, CvLnMain, commclnt;

file brCashLink( "sb_casln.dbt" );
file brCashDoc( "sb_casdc.dbt" ) key 7;
file brBunch( "bunch.dbt" ) key 0;
file brCashVal( "sb_casvl.dbt" ) key 1;
var ddep = TRecHandler( "i_dp_dep.rec" );
file brCIMisc( "ci_misc.dbt" ) key 0;

const
  brO_MISC_BUY = 2,
  brCV_MinCI = 500;

var
  brClientList = TClientList, 
  TotPayedSum = $0L,
  brHeadPrinted = false,
  brLineNo = 0,
  brDoc, 
  brInfo = TRecHandler( "ci_doc.2" );

private var {Name_Bank};

macro brFindBranch

  return findDepartment(brDoc.Rec.Branch, null, ddep);
end;

macro brFindCIMisc

  brCIMisc.Type = brCashVal.TypeValue - brCV_MinCI;
  brCIMisc.IssueID = brCashVal.CodIntValue;
  return GetEQ( brCIMisc );
end;

macro brFindCashVal

  var
    Type, ValType, ValCode;

  GetMainValueCode( brDoc.Rec.Type, brDoc.Rec.Issue, brDoc.Rec.Operation, brDoc.Rec.SubOp, Type, ValType, ValCode );

  brCashVal.TypeValue = ValType;
  brCashVal.CodIntValue = ValCode;
  brCashVal.Type = Type;
  return GetEQ( brCashVal );
end;

macro brFindDepClnt

  return brClientList.GetRecord( brDoc.Rec.ClientCode );
end;

macro brFindCashDoc

  var
    Stat;


  KeyNum( brCashLink, 1 );
  ClearRecord( brCashLink );
  brCashLink.ApplicationKind2 = brDoc.Rec.ApplicationKind;
  brCashLink.ApplicationKey2 = brDoc.Rec.ApplicationKey;
  Stat = GetEQ( brCashLink );
  if ( Stat )
    KeyNum( brCashLink, 0 );
    brCashLink.NumLinkDoc = 0;
    Stat = GetGE( brCashLink );
    while (Stat)
      if (brCashLink.RefValue2 == brCashVal.RefValue)
        brCashDoc.ApplicationKind = brCashLink.ApplicationKind2;
        brCashDoc.ApplicationKey = brCashLink.ApplicationKey2;
        brCashDoc.RefValue = brCashLink.RefValue2;
        return Stat = GetEQ( brCashDoc ); 
      else
        Stat = Next( brCashLink );
      end;
    end;
  end;
  return Stat;
end;

macro brPrintHead

  [ #                                                                                                                                ]
  ( {Name_Bank} );
  [ Cберегательного банка № #                                                                                                        ]
  ( ddep.rec.Name );
  [                                                                                                                                  ];                  
  [                                                                                                                                  ];                  
  [                                                         В е д о м о с т ь                                                        ];                  
  [                                                по выкупу облигаций государственных                                               ];
  [                                              целевых беспроцентных займов 1990 года                                              ];
  [                                               за #                                                                               ]
  ( brDoc.Rec.Date:m );
  [                                                                                                                                  ];                  
  [                                                                                                                                  ];                  
  [------------------------------------------------------------------------------------------------------------------------------------------------];
  [|№   | Ф.И.О.                 | №         |Нарицательная| Выплаченная | Данные документов,        | Сведения              |      Подписи       |];
  [|п/п | владельца облигации    | облигации | стоимость   |  сумма      | удостоверяющих личность   | о местожительстве     |--------------------|];
  [|    |                        |           |             |             | и подтв. гражданство      |                       |влад.  |контр.|кас- |];
  [|    |                        |           |             |             | РФ или статус             |                       |облигац|      |сир  |];
  [|    |                        |           |             |             | вынужденного переселенца  |                       |ии     |      |     |];
  [|    |                        |           |             |             | или беженца, дата         |                       |       |      |     |];
  [|    |                        |           |             |             | выдачи                    |                       |       |      |     |];                                                          
  [------------------------------------------------------------------------------------------------------------------------------------------------];
end;

macro brMakeNumStr( Bun )

  var
    Series,
    Min,
    Delim = "";

  if ( Bun )
    Series = brBunch.Series;
    Min = brBunch.Min;
  else
    Series = brCashDoc.Series;
    Min = brCashDoc.MinRegistrNumber;
  end;

  if ( Series != "" )
    Delim = "-";
  end;
  return Series + Delim + Min;  
end;

macro brGetCit
  
  if ( brClientList.curRec.rec.NotResident == StrFor( 0 ) )
    return " Гражд.: РФ";
  elif ( brClientList.curRec.rec.Country != "" )
    return " Гражд.: " + brClientList.curRec.rec.Country;
  else
    return "";
  end; 
end;

macro brPrintLine( Bun )

  var
    client = brClientList.curRec;

  brLineNo = brLineNo + 1;
  [ #### ######################## ########### ############# ############# ########################### #######################                      ]
  (
    brLineNo,
    ( client.rec.Name1 + " " + client.rec.Name2 + " " + client.rec.Name3 ):w,
    brMakeNumStr( Bun ):w,
    brCIMisc.Par,
    brDoc.Rec.Sum / brInfo.Rec.NumItems,
    ( client.makeDocStr + brGetCit ):w,
      commclnt_Получить_Поле_Address_Фактического_Адреса(client):w
  );
  TotPayedSum = TotPayedSum + brDoc.Rec.Sum / brInfo.Rec.NumItems;
end;

macro brPrintBottom

  if ( brHeadPrinted )
    [----------------------------------------------------------------------------------------------------------------------------------];
    [ ];
    [ ИТОГО            :                                      #############                                                            ]
    (TotPayedSum);
    [ ];
    [ Главный (старший) бухгалтер _________________________________________ ];
  end;
end;

macro brPrintRec( doc )

  var
    RecFound;

  if ( doc.Rec.Operation != brO_MISC_BUY )
    return;
  end;

  brDoc = doc;
  brFindBranch;
  brFindCashVal;
  brFindCIMisc;
  brFindCashDoc;
  brFindDepClnt;
  brInfo.SetRecordAddr( brDoc, 0, FldOffset( brDoc, "Info" ), true );
                                     
  if ( ( brCashDoc.Series != "" ) or ( brCashDoc.MinRegistrNumber != 0 ) )
    if ( not brHeadPrinted );
      brPrintHead;
      brHeadPrinted = true;
    end;
    brPrintLine( false );
    return;
  end;

  ClearRecord( brBunch );
  brBunch.BagAppKind = brCashDoc.ApplicationKind;
  brBunch.BagAppKey = brCashDoc.ApplicationKey;
  RecFound = GetGE( brBunch );
  while ( RecFound
    and ( brBunch.BagAppKind == brCashDoc.ApplicationKind )
    and ( brBunch.BagAppKey == brCashDoc.ApplicationKey ) )
    if ( not brHeadPrinted );
      brPrintHead;
      brHeadPrinted = true;
    end;
    brPrintLine;
    RecFound = Next( brBunch );
  end;  
end;                   	
