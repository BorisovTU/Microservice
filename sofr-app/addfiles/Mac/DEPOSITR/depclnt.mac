
/*
**
**  Класс TDepClient - работа с базой клиентов
**
**
*/

import CommonInter;


class TDepClient( id )

  private var
    f = TBFile( "depclnt.dbt", "w", 0 );

  private macro find( id )

    f.rec.CodClient = id; 
    return f.getEQ;
  end;

  macro write		/* Записать в файл */

    var
      stat; 

    if ( f.rec.CodClient )
      if ( not f.update )
        status( stat );
        runError( stat );
      end;
    else
      f.rec.CodClient = formClientCode;
      if ( not f.insert )
        status( stat );
        runError( stat );
      end;
    end;
  end;

  macro getId           /* Получить ID */

    return f.rec.CodClient;
  end;

  macro setId( id )     /* Установить ID */

    if ( f.rec.CodClient != 0 )
      runError( "Код клиента уже установлен" );
    end;

    f.rec.CodClient = id;
  end;

  macro setName( lastName, firstName, patrName ) /* Установить имя */

    if ( lastName != null )
      f.rec.Sname = lastName; 
    end;

    if ( firstName != null )
      f.rec.Name = firstName; 
    end;

    if ( patrName != null )
      f.rec.Pname = patrName; 
    end;
  end;

  macro getLastName     /* Получить фамилию */

    return f.rec.Sname;
  end;

  macro getFirstName    /* Получить имя */

    return f.rec.Name;
  end;

  macro getPatrName     /* Получить отчество */

    return f.rec.Pname;
  end;

  macro getRec		/* Получить ссылку на буфер записи */

    return f.rec;
  end;

  macro getFullName        /* Получить полное имя клиента */

    var
      n = f.rec.Sname;

    if ( f.rec.name )
      if ( strLen( n ) )
        n = n + " "; 
      end;
      n = n + f.rec.Name;
    end;

    if ( f.rec.Pname )
      if ( strLen( n ) )
        n = n + " "; 
      end;
      n = n + f.rec.Pname;
    end;

    return n; 
  end;

  macro getNameWithInitials        /* Получить ФИО клиента */

    var
      n = f.rec.Sname;

    if ( f.rec.name )
      if ( strLen( n ) )
        n = n + " "; 
      end;
      n = n + subStr( f.rec.Name, 1, 1 ) + ".";
    end;

    if ( f.rec.Pname )
      if ( strLen( n ) )
        n = n + " "; 
      end;
      n = n + subStr( f.rec.Pname, 1, 1 ) + ".";
    end;

    return n; 
  end;

  if ( id )
    if ( not find( id ) )
      runError( "Не найден клиент с кодом " + id );
    end;
  else
    f.clear;
  end; 
end;
