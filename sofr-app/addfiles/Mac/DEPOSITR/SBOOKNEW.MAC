/*
$Name:               SBOOKNEW.MAC
$Module:             DEPOSITR
$Description:        Печать записей в сберкнижку
*/


/* -@H------------------------------------------------------------------
*   System           : Вкладчики Сбербанка
*
*   File Name        : sbooknew.mac
*   Sourse Name      :
*
*
*   Description      : Печать записей в сберкнижку
*
*   Comment          : Основан на макросе SBOOK.MAC и функции
*                    : pr_sbook
*                    : Вызывается со следующими параметрами
* ALG_PARAM.prmC  : "\x00" - вывод сразу на принтер(иначе - на экран)
* ALG_PARAM.prmI  : 0 - c последней ненапечатанной
*                     (иначе - с текущей ALG_SBDEDOC)
* ALG_PARAM.prmI2 : 0 - принудительно(иначе с проверкой )
* ALG_PARAM.prmL  : 0 - все строки(иначе одну)
*
*   Public routines  :
*   Base files       :
*   Programmer       : Зубов В.Ю.
*
*                 History
*   3.12.1996        : Восстановлен после потери
*
* ----------------------------------------------------------------@H- */

import CommonInter,BankInter,deprintr,DeskInter,OperCode,InputRN,ChkPrn,IsSrvDoc,sbk_titl,getdocs,QueryRes,Alg_Vars, sqlconv;

/*---------- Инициализируемые структуры ---------------------------------*/
 record Parm(CashParm);


/*************************************************************************/
/*---------- Константы, переменные, структуры... ------------------------*/
/*----------        настраиваются Вами!          ------------------------*/

 const Выбор_сберкнижки = TRUE;     /*  FALSE;   */

 const Высота_заголовка      = 3;
 const Высота_заголовка_2005 = 4;
 const Строк_На_1_Странице   = 12;
 const Строк_На_Листе        = 26;
 const Строк_На_1_Странице_C = 8;
 const Строк_На_Листе_C      = 8;
 const CASHOP_DEBIT          = 5;
 const CASHOP_CHVALUE        = 9;
 const TYPERES_VALFORM       = 2;
 const VALFORM_BOOK          = 1;
 const VALFORM_BOOK_C        = 3;
 const VALFORM_CUR_BOOK      = 1;
 const VALFORM_2O            = 14;
 const VALFORM_BOOK_2005     = 16;
 const VALFORM_BOOK_C_2005   = 17;
 const VALFORM_2O_2005       = 18;
 const VALFORM_31            = 31;
 const UserQuery             = TRUE;
 const PrintForm31           = TRUE; /* Печать ф.31: если FALSE - только расход */
 const SB_INCASH             = 100;

 const MINDATE = Date(01,01,1900);
 const MAXDATE = Date(31,12,2999);

 const
    ACCOUTN_OWNER       = StrFor( 0 ),    /* Владелец счета      */
    ACCOUTN_PROXY       = StrFor( 1 ),    /* Доверенное лицо     */
    ACCOUTN_HEIR        = StrFor( 2 ),    /* Наследник - выдачи  */
    ANONIMUS_CLIENT     = StrFor( 3 ),    /* Анонимный клиент    */
    ACCOUNT_IMPORTER    = StrFor( 4 ),    /* Вноситель           */
    ACCOUNT_TUTOR       = StrFor( 5 ),    /* Опекун              */
    ACCOUNT_OBLIGATOR   = StrFor( 6 ),    /* Под обязательство   */
    ACCOUTN_HEIR_PAY    = StrFor( 7 ),    /* Наследник - выплаты */
    ACCOUNT_UNDER_TRIAL = StrFor( 8 ),    /* По суду             */
    ACCOUNT_PARENT      = StrFor( 9 );    /* Родитель            */

 const
   UPDATE_ALL  = 1,
   UPDATE_DEP  = 2,
   UPDATE_DOC  = 4;

 var
   {BatchMode};

 var dd = TBfile( "sbdepdoc.dbt", "w", 6 );

 var doc1 = TRecHandler( "sbdepdoc.1" );

 file GroupMem( "groupmem.dbt" ) key 0;

 const SB_2_o = 2;
 const SB_2_2005 = 3;
 const SB_2_c_2005 = 4;
 const SB_2_o_2005 = 5;
 var Type_of_SB;

 private var sb_casln        = TBFile( "sb_casln.dbt", "r", 0 );
 private var sbdepdocForLink = TBFile( "sbdepdoc.dbt", "r", 3 );
 private var acc             = TBFile( "depositr.dbt", "r", 8 );

 Var TypeI   = V_INTEGER,
     ValueI  = 0,
     CurLine = 0;

 Var CodIntValueBook = VALFORM_BOOK;
 Var LinesPage = Строк_На_1_Странице;
 Var LinesList = Строк_На_Листе;
 var HeadHeight = Высота_заголовка;
 Private var ClientList = TClientList;

 Var WasPrintHead = FALSE;

 Var stat = FALSE,
     found = FALSE,
     i,
     bookReady = True,
     Branch = NumFNCash,
     flag2,
     OperBrigade = GetOperBrigade,
     NullDate = date(0, 0, 0),
     LastPrintRest = $0L,
     str;
 var rSeries = "", rNumber = 0;  /* Серия\номер номерной сбер.книжки. */
 var saveReferenc, saveDate_Document, saveNumDayDoc;
 var need_out_res = false;
 var StrFltr;
 Var
     RegPath = "RS-RETAIL\\ПЕЧАТИ\\ВКЛАДЫ\\СБЕРКНИЖКА",
     RegPathCurStr = "RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ТЕКУЩАЯ_СТРОКА_СБЕРКНИЖКИ",
     ErrCode;

private var {ReissuingReports};


 /*************************************************************
               Установки для стандартной сберкнижки
 *************************************************************/
 macro BookStandart
   CodIntValueBook = VALFORM_BOOK;
   LinesPage = Строк_На_1_Странице;
   LinesList = Строк_На_Листе;
 end;

 /*************************************************************
                     Выбор вида сберкнижки
 *************************************************************/
 macro BookSelect

   array sbm;
   var m;

   if ( Выбор_сберкнижки )
     if ( ( ALG_DEPOSITOR.BookType == StrFor(0) )  OR  /* Вид с\к не установлен. */
          ( NOT ALG_CONTRACT.BookGiven ) )  /* Сберкнижка не выдавалась. */
       sbm(0) = "    Сберкнижка ф.2      ";
       sbm(1) = "    Сберкнижка ф.2-c    ";
       sbm(2) = "    Сберкнижка ф.2-о    ";
       sbm(3) = "    Сберкнижка ф.2 номерная";
       sbm(4) = "    Сберкнижка ф.2-c (2005 г.)";
       sbm(5) = "    Сберкнижка ф.2-о (2005 г.)";
       if ( {BatchMode} )
         m = 0;
       else
         m = Menu( sbm,
           " Вид сберкнижки:  ~ENTER~ - Выбор  ~ESC~ - Отказ",
           "Наименование ресурса" );
       end;
       if(ClientList.CurRec.Rec.IsLiterate == "X")
          m = SB_2_o;
       end;
       Type_of_SB = m;
       if ( m >= 0 )
         ALG_DEPOSITOR.BookType = StrFor( m + 1 );
         ALG_UPDATE = UPDATE_DEP;  /* Сохранить изменившийся ALG_DEPOSITOR */
       end;
     else
       m = CodeFor(ALG_DEPOSITOR.BookType) - 1;
       Type_of_SB = m;
     end;
     if ( m < 0 )
       MsgBox( "Вы отказались от заведения сберкнижки" );
       ALG_RESULT = ERR_RES;
       Exit(1);
     else
       if ( m == 0 )
         BookStandart();
       elif (m == 2)
         CodIntValueBook = VALFORM_2O;
         LinesPage = Строк_На_1_Странице_C;
         LinesList = Строк_На_Листе_C;
       elif (m == 3)
         CodIntValueBook = VALFORM_BOOK_2005;
         LinesPage = Строк_На_1_Странице;
         LinesList = Строк_На_Листе;
         HeadHeight = Высота_заголовка_2005;
       elif (m == 4)
         CodIntValueBook = VALFORM_BOOK_C_2005;
         LinesPage = Строк_На_1_Странице_C;
         LinesList = Строк_На_Листе_C;
       elif (m == 5)
         CodIntValueBook = VALFORM_2O_2005;
         LinesPage = Строк_На_1_Странице_C;
         LinesList = Строк_На_Листе_C;
       else
         CodIntValueBook = VALFORM_BOOK_C;
         LinesPage = Строк_На_1_Странице_C;
         LinesList = Строк_На_Листе_C;
       end;
     end;
   else
     BookStandart();
   end;

 end;

 macro GetPatern(Oper)

/*
   if ( ALG_DEPOSITOR.SpecialAccess != StrFor( 0 ) )
     return Oper;
   end;
*/

   GroupMem.Branch = NumRealFNCash;
   GroupMem.Date = {curdate};
   GroupMem.Group = OperBrigade;
   GroupMem.Oper = Oper;
   if ( GetEQ( GroupMem ) )
     return GroupMem.Linked;
   else
     return 0;
   end;
 end;

 macro А_Точно_Печать_Сберкнижки_Нужна

   var Надо_Печатать = true;

   file   ВкладнойДокумент ( "sbdepdoc.dbt" );
   file   ФайлСчетов       ( "depositr.dbt" ) key 7;
   record БНВыдачаПроцентов( "sbdepdoc.11"  );

   copy( ВкладнойДокумент, ALG_SBDEPDOC );
   /* При безналичной выдаче %% на другой счет, если получатель другой,
      строки печатаем только по счету с которого списываем */
   if( ( ALG_SBDEPDOC.TypeComplexOper == Выдача_Процентов   ) and
       ( ALG_SBDEPDOC.TypeOper        == Списание_Переводом ) and
       ( ALG_SBDEPDOC.ApplType        == 11                 )    )
     SetRecordAddr( БНВыдачаПроцентов, ВкладнойДокумент, 0, 0, true );
     if( БНВыдачаПроцентов.Account_Receiver != "" )
       rewind( ФайлСчетов ); clearRecord( ФайлСчетов );
       ФайлСчетов.FNCash  = БНВыдачаПроцентов.FNcash_Receiver;
       ФайлСчетов.Account = БНВыдачаПроцентов.Account_Receiver;
       if( ( getGE( ФайлСчетов )  == true                               ) and
           ( ФайлСчетов.FNCash    == БНВыдачаПроцентов.FNcash_Receiver  ) and
           ( ФайлСчетов.Account   == БНВыдачаПроцентов.Account_Receiver ) and
           /* ФайлСчетов    - счет, с которого списывали %%
              ALG_DEPOSITOR - счет, на который переводили %% */
           ( ФайлСчетов.CodClient != ALG_DEPOSITOR.CodClient            )    )
         Надо_Печатать = false;
       end;
     end;
   end;

   return Надо_Печатать;

 end;

 /* При закрытии счета нужно печатать информацию в сберкнижку
 if((ALG_SBDEPDOC.TypeComplexOper == Закрытие_Наличными) or
   (ALG_SBDEPDOC.TypeComplexOper == Закрытие_Безналичными))
   Exit;
 end;
 */

 if ( {ReissuingReports} )
   exit( 1 );
 end;

 if( А_Точно_Печать_Сберкнижки_Нужна() == false )
   exit( 1 );
 end;

 if ( Index( ALG_DEPOSITOR.UserTypeAccount, "Ф" ) != 0 )  /* Счета типа "Ф" */
   /*stat = CarryForm31( PrintForm31 );*/    //вызовы CarryForm31 теперь не имеют эффекта: # 209781
   ALG_RESULT = OK_RES;
   Exit();
 end;

 if ( ALG_CONTRACT.BookGiven )
   if ( (ALG_DEPOSITOR.NCurLine != 0) OR ( (CodeFor(ALG_DEPOSITOR.BookType) - 1) == SB_2_o) )
     if ( UserQuery )
       if(ALG_SBDEPDOC.TypeOper==Дополнительный_взнос)
         str="Клиент предъявил сберкнижку ?";
         GetTrue(bookReady,str)
       else
         bookReady=true;
       end;
     else
       bookReady = true;
     end;
   end;
 else
   if ( ( ALG_SBDEPDOC.TypeComplexOper == Закрытие_Наличными )  OR
        ( ALG_SBDEPDOC.TypeComplexOper == Закрытие_Безналичными ) )
     if ( ALG_SBDEPDOC.TypeOper == Расчет_Процентов )
       MsgBox("Сберкнижка по счету не выдавалась");
     end;
     Exit(1);  /* Блокируем вывод на экран */
   else
     /* Проверка факта отсутствия или утери с\к при выдаче наследства */
     if ( ( ( NOT ALG_CONTRACT.BookGiven )  OR
          ( Index( ALG_DEPOSITOR.UserTypeAccount, "Y" ) != 0 ) )  AND
          ( ( ALG_SBDEPDOC.Author == ACCOUTN_HEIR )  OR      /* Наследник - выдачи  */
            ( ALG_SBDEPDOC.Author == ACCOUTN_HEIR_PAY ) ) )  /* Наследник - выплаты */
       ALG_RESULT = OK_RES;
       Exit(1);  /* Блокируем вывод на экран */
     end;
     if ( ( ALG_CONTRACT.BookGiven == StrFor( 0 ) )  AND
          ( Index( ALG_DEPOSITOR.UserTypeAccount, "Y" ) == 0 ) )
       if ( UserQuery )
         str="Завести сберкнижку ?";
         GetTrue(bookReady,str);
       else
         bookReady = true;
       end;
     else
       bookReady = false;
     end;
   end;
 end;
 if(NOT bookReady)
   if ( ALG_SBDEPDOC.OutSum != $0 )
     ALG_RESULT = ERR_RES;  /* Сторнировать расходные без сберкнижки */
     MsgBox( "Расходные операции|без оформления сберкнижки|запрещены." );
     Exit(1);
   end;
   if ( ALG_SBDEPDOC.TypeOper == Открытие_Наличными )   /* Открытие счета наличными */
     ALG_RESULT = ERR_RES;  /* Сторнировать открытие счета */
     MsgBox( "При отмене выдачи сберкнижки|открытие счета сторнируется." );
     Exit(1);
   end;
   /*stat = CarryForm31( PrintForm31 );*/    //вызовы CarryForm31 теперь не имеют эффекта(по запросу # 209781)
   ALG_RESULT = OK_RES;
   Exit(1);
 end;

 ALG_RESULT = ERR_RES;/*заранее инициализируем код возврата*/
 CheckPrinter( RegPath, alg_param.prmC );
 ALG_RESULT = OK_RES;/*заранее инициализируем код возврата*/

/* --------------------------------------------------------------------*
*  Печать заголовка                                                    *
*  ------------------------------------------------------------------ */
/*
 macro Заголовок

   if (WasPrintHead == FALSE)
     if (Type_of_SB == SB_2_2005)
       [Счет № #                                                  ]
       ( Acc_Form(ALG_DEPOSITOR.Account):f);
     end;
     [----------------------------------------------------------];
     [  Дата    :   Приход      :    Расход     :   Остаток     ];
     [----------:---------------:---------------:---------------];
     WasPrintHead = TRUE;
   end;
 end;
*/


/* --------------------------------------------------------------------*
*  Печать записи                                                       *
*  ------------------------------------------------------------------ */
 macro Сбер_Книжка

   WasPrintHead = FALSE;
   if( (dd.rec.TypeOper == Открытие_Безналичными) and
       (dd.rec.ApplType == 11) and
       ( ( Trim(ALG_DEPOSITOR.Type_Account) == "Универсальн." ) or
         ( Trim(ALG_DEPOSITOR.Type_Account) == "Зарплатный" ) ) )
     doc1.SetRecordAddr( dd, 0, 0, TRUE );
     acc.Clear();
     if ( doc1.rec.CorrAcc_Referenc > 0 )
       acc.rec.Referenc = doc1.rec.CorrAcc_Referenc;
       acc.GetEQ();
     end;
     if ( StrUpr(Trim( acc.rec.Type_Account )) == "ДО ВОСТРЕБ." )
       [##########  Переоформ.со вклада До востр.  ###############]
       ( dd.rec.Date_Document, dd.rec.Rest:15:2 );
     else
       [##########                                 ###############]
       ( dd.rec.Date_Document, dd.rec.Rest:15:2 );
     end;
     ALG_DEPOSITOR.NCurLine = ALG_DEPOSITOR.NCurLine +1;
   elif( dd.rec.InSum )
     [########## ###############                 ###############]
     ( dd.rec.Date_Document, dd.rec.InSum:15:2, dd.rec.Rest:15:2 );
     ALG_DEPOSITOR.NCurLine = ALG_DEPOSITOR.NCurLine +1;
   elif(dd.rec.OutSum)
     [##########                 ############### ###############]
     ( dd.rec.Date_Document, dd.rec.OutSum:15:2, dd.rec.Rest:15:2 );
     ALG_DEPOSITOR.NCurLine = ALG_DEPOSITOR.NCurLine +1;
   elif((dd.rec.TypeOper == Открытие_Безналичными) and (dd.rec.ApplType == 15))
     [##########                                 ###############]
     ( dd.rec.Date_Document, dd.rec.Rest:15:2 );
     ALG_DEPOSITOR.NCurLine = ALG_DEPOSITOR.NCurLine +1;
   end;/*if*/

 end;/*macro*/


/* --------------------------------------------------------------------*
*  Перевод строки                                                      *
*  ------------------------------------------------------------------ */
 macro Перевод_Строки
   println( " " );
 end;/*macro*/

/* --------------------------------------------------------------------*
*  Завершить вывод - перевод страницы                                  *
*  ------------------------------------------------------------------ */
 macro Перевод_Страницы
  print( "\f" );
 end;/*macro*/

/* --------------------------------------------------------------------*
*  Пропуск сгиба                                                       *
*  ------------------------------------------------------------------ */
 macro Пропуск_Сгиба
   Перевод_Строки;
   Перевод_Строки;
   ALG_DEPOSITOR.NCurLine = ALG_DEPOSITOR.NCurLine +2;
 end;/*macro*/

/* --------------------------------------------------------------------*
*  Напечатать начиная с текущей все строки                             *
*  ------------------------------------------------------------------ */
 macro  Напечатать_ВСЕ_С_Текущей

   var move_to_duplicate = false;

   if ( ( ALG_DEPOSITOR.NCurLine == 0 ) and ( ALG_DEPOSITOR.NumberLostSBBook > 0 ) )
     move_to_duplicate = true;
   end;

   if((ALG_DEPOSITOR.NCurLine == 0) OR
      (ALG_DEPOSITOR.NCurLine > LinesList))
     ALG_DEPOSITOR.NCurLine = 1;
   end;/*IF*/

   if(ALG_DEPOSITOR.NCurLine != 1)
     /*пропускаем нужное количество строк*/
     i=1;
     while(i < (ALG_DEPOSITOR.NCurLine + HeadHeight))
       Перевод_Строки;
       i=i+1;
     end;/*WHILE*/
   end;/*IF*/
   stat=TRUE;

   /*
   StrFltr = "t.t_fncash = "       + ALG_DEPOSITOR.FNCash + " and " +
             "t.t_iscur = "        + ALG_DEPOSITOR.IsCur  + " and " +
             "t.t_type_account = \'" + ALG_DEPOSITOR.Type_Account + "\'";

   dd.addFilter( StrFltr ); */
 if ((Type_of_SB != SB_2_o) and (Type_of_SB != SB_2_o_2005))   // сберкнижка формы 2-о
   while(stat == TRUE)
     if(ALG_DEPOSITOR.NCurLine == 1)
       /* новая страница*/
       // Заголовок;
       ALG_DEPOSITOR.NCurLine = ALG_DEPOSITOR.NCurLine + 1;
       if ( move_to_duplicate )
         [Перенесено с исписанной сберкнижки               #]( String( LastPrintRest:0:2 ) );
       end;
     end;
     if ( ALG_DEPOSITOR.NCurLine == ( LinesPage +1 ) )
       Пропуск_Сгиба;
     end;/*IF*/
     if((ALG_PARAM.prmI2 == 0) OR  /* принудительно*/
        (dd.rec.YesSBook == "X"))
       if (IsServDoc(dd.rec))
         Сбер_Книжка;
         /* Увеличение строк перенесено в функцию, так как документ
            может не печататься, так как InSum и OutSum нулевые */
       end;
     end;/*IF*/
     if(ALG_DEPOSITOR.NCurLine > LinesList)
       /* конец страницы*/
       Перевод_Страницы;
       ALG_DEPOSITOR.NCurLine=1;
     end;/*IF*/
/*
     dd.rec.YesSBook = "\x00";
     Update(dd);
*/
     if(ALG_PARAM.prmL == 0) /* все строки*/
      stat = Next(dd);
     else
      stat=FALSE;
     end;/*IF*/
     if(stat == TRUE)
       if (dd.rec.Referenc != ALG_SBDEPDOC.Referenc)
         stat = FALSE;
       end;/*IF*/
     end;/*IF*/
     while ( stat and ( dd.rec.TypeOper == Перевод_В_Другой_Вид_Вклада ) )
       stat = Next( dd );
       if(stat == TRUE)
         if (dd.rec.Referenc != ALG_SBDEPDOC.Referenc)
           stat=FALSE;
         end;/*IF*/
       end;/*IF*/
     end;
   end;/*WHILE*/
 end;
   /* dd.DropFilter(); */

   ALG_UPDATE = UPDATE_DEP;  /* Сохранить изменившийся ALG_DEPOSITOR */
 end;/*macro*/

 macro  Пометить_Строки
   stat=TRUE;
   while(stat == TRUE)
     dd.rec.YesSBook = "\x00";
     Update(dd);
     if(ALG_PARAM.prmL == 0) /* все строки*/
      stat = Next(dd);
     else
      stat=FALSE;
     end;/*IF*/
     if(stat == TRUE)
       if (dd.rec.Referenc != ALG_SBDEPDOC.Referenc)
         stat = FALSE;
       end;/*IF*/
     end;/*IF*/
     while ( stat and ( dd.rec.TypeOper == Перевод_В_Другой_Вид_Вклада ) )
       stat = Next( dd );
       if(stat == TRUE)
         if (dd.rec.Referenc != ALG_SBDEPDOC.Referenc)
           stat=FALSE;
         end;/*IF*/
       end;/*IF*/
     end;
   end;/*WHILE*/
   ALG_UPDATE = UPDATE_DEP;  /* Сохранить изменившийся ALG_DEPOSITOR */
 end;/*macro*/


/************************************************************************\
|                                                                        |
\************************************************************************/
macro DefAlgSbDepDoc

   var applKind = 0;
   var applKey  = "";
   var wasFind  = false;
   var stat;

   sb_casln.KeyNum = 1;
   sb_casln.Clear;
   sb_casln.rec.ApplicationKind2 = ALG_RET_OP.ApplicationKind;
   sb_casln.rec.ApplicationKey2  = ALG_RET_OP.ApplicationKey;

   if ( sb_casln.GetEQ )
      applKind = sb_casln.rec.ApplicationKind1;
      applKey = sb_casln.rec.ApplicationKey1;

      sb_casln.KeyNum = 0;
      sb_casln.Clear;
      sb_casln.rec.ApplicationKind1 = applKind;
      sb_casln.rec.ApplicationKey1  = applKey;
      sb_casln.addFilter( "t.t_ApplicationKind1 = " + String( applKind ) + " and " +
                          "t.t_ApplicationKey1 = " + GetSQLString( applKey )        );

      stat = sb_casln.GetGE;

      while ( stat )
        if ( ( sb_casln.rec.ApplicationKind1 = applKind ) and
             ( sb_casln.rec.ApplicationKey1  = applKey  )    )
          if ( ( sb_casln.rec.ApplicationKind2 == 1   ) or
               ( sb_casln.rec.ApplicationKind2 == 110 ) or
               ( sb_casln.rec.ApplicationKind2 == 111 )   )
            sbdepdocForLink.Clear;
            sbdepdocForLink.rec.iApplicationKind = sb_casln.rec.ApplicationKind2;
            sbdepdocForLink.rec.ApplicationKey   = sb_casln.rec.ApplicationKey2;

            if ( sbdepdocForLink.GetEQ and ( sbdepdocForLink.rec.TypeComplexOper == ALG_RET_OP.Operation ) )
              Copy( ALG_SBDEPDOC, sbdepdocForLink );
              wasFind = true;
            end;
          end;

          if ( wasFind )
            stat = false;
          else
            stat = sb_casln.Next;
          end;
        else
          stat = false;
        end;
      end;
      sb_casln.dropFilter;

   end;

  if ( not wasFind )
    ClearRecord( ALG_SBDEPDOC );
    ALG_SBDEPDOC.Referenc         = ALG_DEPOSITOR.Referenc;
    ALG_SBDEPDOC.IsCur            = ALG_DEPOSITOR.IsCur;
    ALG_SBDEPDOC.FNCash           = ALG_DEPOSITOR.FNCash;
    ALG_SBDEPDOC.RealFNCash       = NumRealFNCash( );
    ALG_SBDEPDOC.Account          = ALG_DEPOSITOR.Account;
    ALG_SBDEPDOC.Oper             = {oper};
    ALG_SBDEPDOC.Type_Account     = ALG_DEPOSITOR.Type_Account;
    ALG_SBDEPDOC.Code_Currency    = ALG_DEPOSITOR.Code_Currency;
    ALG_SBDEPDOC.CodClient        = ALG_DEPOSITOR.CodClient;
    ALG_SBDEPDOC.Date_Document    = {curdate};
    ALG_SBDEPDOC.DepDate_Document = {curdate};
  end;

end;


/*----------- Печать в книжку ------------------------------------*/

 if ( ALG_RET_OP.ApplicationKind == 3001 ) /* Нефинансовая операция */
   DefAlgSbDepDoc( );
 end;

 /* Проверка при закрытии счета - если сберкнижка не выдавалась,
                                  то при закрытии не выдавать (SCR # 3740) */
 if(((ALG_SBDEPDOC.TypeComplexOper == Закрытие_Наличными)      OR
     (ALG_SBDEPDOC.TypeComplexOper == Закрытие_Безналичными))  AND
    (NOT ALG_CONTRACT.BookGiven))
   Exit(1);  /* Блокируем вывод на экран */
 end;

 if (Index(ALG_DEPOSITOR.UserTypeAccount,"Y") != 0)
   MsgBox("Сберкнижка утеряна");
   ALG_RESULT = OK_RES;
   Exit(1);  /* Блокируем вывод на экран */
 end;

 /* Для переоформленных вкладов "До востреб." -> "Универсальн.", "Зарплатн." */
 if ( ( Trim(ALG_DEPOSITOR.Type_Account) == "Универсальн." ) or
      ( Trim(ALG_DEPOSITOR.Type_Account) == "Зарплатный" ) )
   if ( ALG_CONTRACT.BookGiven  AND /* Сбер.книжка выдавалась */
        ( ALG_DEPOSITOR.NCurLine < 0 ) )  /* Отметка первого выпуска с\к после переоформления */
     if ( GetTrue( FALSE, "Менять сбер.книжку на новую ?" ) )
       /* Замена сбер.книжки на новую */
       ALG_CONTRACT.BookGiven = StrFor(0);
       ALG_DEPOSITOR.Givebook = StrFor(0);
       ALG_DEPOSITOR.NCurLine = 0;
     else
       /* Оставили старую сбер.книжку  */
       if ( ALG_DEPOSITOR.NCurLine == -777 )
         ALG_DEPOSITOR.NCurLine = 0;
       else
         ALG_DEPOSITOR.NCurLine = -ALG_DEPOSITOR.NCurLine;
       end;
       if ( GetTrue( TRUE, "Напечатать новый номер счета на титульном листе ?" ) )
         MsgBox( "Установите в принтере титульный лист сбер.книжки|" +
                 "а затем - текущий лист сбер.книжки" );
         Перевод_Строки();
         [                                         #]
         ( "Переоформлен на \"" + Trim(ALG_DEPOSITOR.Type_Account) + "\"" );
         [                                         #]
         ( "Счет: \"" + String(Acc_Form(ALG_DEPOSITOR.Account):f) + "\"" );
         Перевод_Страницы();
       end;
     end;
   end;
 end;

 /* Выбор вида сберкнижки */
 BookSelect();

 /* Проверка наличия ресурса кассы - сберкнижки */
 if ( ( NOT ALG_CONTRACT.BookGiven )  or ( ALG_DEPOSITOR.NCurLine == 0 ) )
   if ( NOT CheckCashRes( CodIntValueBook ) )
     ALG_RESULT = ERR_RES;
     exit( 1 );
   end;
 end;

 if(not ALG_CONTRACT.BookGiven or ( ALG_DEPOSITOR.NCurLine == 0 ) )
   if ((Type_of_SB != SB_2_o) and (Type_of_SB != SB_2_o_2005))
      need_out_res = true;
      BookTitle(ALG_DEPOSITOR.Referenc,TRUE, null, Type_of_SB);   /* Печать титульного листа */
   end;
 end;

 if ( ( GetRegistryValue(RegPathCurStr, TypeI, ValueI, ErrCode) == V_INTEGER )  AND
      ( ValueI != 0 ) )
   CurLine = ALG_DEPOSITOR.NCurLine;
   if ( GetInt( CurLine, "Укажите номер текущей строки сберкнижки", 3 ) )
     if ( CurLine < 0 )
       while ( CurLine < 0 )
         MsgBox( "Номер текущей строки сберкнижки|не должен быть отрицательным" );
         CurLine = ALG_DEPOSITOR.NCurLine;
         GetInt( CurLine, "Укажите номер текущей строки сберкнижки", 3 );
       end;
     end;
     ALG_DEPOSITOR.NCurLine = CurLine;
   end;
 end;

 /* keyNum(dd,6); */
 Rewind(dd);
 StrFltr = "t.t_referenc = " + ALG_SBDEPDOC.Referenc;
 dd.addFilter( StrFltr );
 stat=TRUE;

 if(ALG_PARAM.prmI == 0) /*c последней ненапечатанной*/
   /*встаем на последнюю напечатанную строку*/
   dd.rec.Referenc = ALG_SBDEPDOC.Referenc;
   dd.rec.Date_Document = MAXDATE;
   dd.rec.NumDayDoc = 30000;
   found = FALSE;/*признак - последняя напечатанная найдена*/
   stat = GetLE(dd);
   while(stat == TRUE)
     if(dd.rec.Referenc == ALG_SBDEPDOC.Referenc)
       if(dd.rec.YesSBook == "X")
         stat=prev(dd);
       else
         if (IsServDoc(dd.rec))
           LastPrintRest = dd.rec.Rest;
           stat  = FALSE;
           found = TRUE;
         else
           stat=prev(dd);
         end;
       end;/*if*/

     else
       stat=FALSE;
     end;/*if*/
   end;/*while*/

   if ( found )
     stat = TRUE;
   end;
if ((Type_of_SB != SB_2_o) and (Type_of_SB != SB_2_o_2005)) //закоментирвано, так как для ф.2-о никакие строчки в книжки не печатаются (заполнение книжки происходит вручную)

   if(found == FALSE)
     /* все строки не напечатаны - встаем на первую*/
/*
     if((ALG_DEPOSITOR.Limit_Date != NullDate) and ALG_DEPOSITOR.ArhFlag)
       GetDocsAndReopenTemp(ALG_DEPOSITOR.Referenc, ALG_DEPOSITOR.Open_Date, dd);
     end;
*/
     dd.rec.Referenc = ALG_SBDEPDOC.Referenc;
     dd.rec.Date_Document = MINDATE;
     dd.rec.NumDayDoc = 0;
     stat = FALSE;
     flag2 = GetGE(dd);
     while (flag2)
       if(dd.rec.Referenc == ALG_SBDEPDOC.Referenc)
         if (IsServDoc(dd.rec))
           flag2 = FALSE;
           stat = TRUE;
         else
           flag2 = Next(dd);
         end;
       else
         flag2 = FALSE;
       end;
     end;
   else
     /* стоим на последней напечатанной строке*/
     stat = FALSE;
     flag2 = Next(dd);
     while (flag2)
       if(dd.rec.Referenc == ALG_SBDEPDOC.Referenc)
         if (IsServDoc(dd.rec))
           flag2 = FALSE;
           stat = TRUE;
         else
           flag2 = Next(dd);
         end;
       else
         flag2 = FALSE;
       end;
     end;
   end;/*if*/
else
  stat = TRUE;
end;

 else
   /* с текущей записи*/
   dd.rec.Referenc = ALG_SBDEPDOC.Referenc;
   dd.rec.Date_Document = ALG_SBDEPDOC.Date_Document;
   dd.rec.NumDaydoc = ALG_SBDEPDOC.NumDayDoc;
   stat= GetEQ(dd);
 end;/*IF*/
 /* dd.dropFilter(); */

 /* Нет документа */
 if ( not stat )
   ALG_RESULT = SKIP_RES;
   Exit( 1 );
 end;
 saveReferenc      = dd.rec.Referenc;
 saveDate_Document = dd.rec.Date_Document;
 saveNumDayDoc     = dd.rec.NumDayDoc;

 if(stat == TRUE)
  if ((Type_of_SB != SB_2_o) and (Type_of_SB != SB_2_o_2005))
     Напечатать_ВСЕ_С_Текущей;
     stat = TRUE;
     QueryResult( "Строки в сберкнижку были напечатаны?" );
     if ( ALG_RESULT == ERR_RES )
       stat = false;
     end;
   end;

   if( (not ALG_CONTRACT.BookGiven or need_out_res ) and Stat )
     ClearRecord(Parm);
     Parm.TypeValue=TYPERES_VALFORM;
     if(ALG_COMDEPCLNT.rec.IsLiterate == StrFor(0))
       if(ALG_SBDEPDOC.IsCur)
         Parm.CodIntValue=CodIntValueBook;
       else
         Parm.CodIntValue=CodIntValueBook;
       end;
     else
       Parm.CodIntValue=VALFORM_2O;
     end;
     Parm.Oper={oper};
     Parm.CashDateDoc={curdate};
     Parm.ValueCol=1;
     Parm.NumbDocumCash=ALG_SBDEPDOC.NDoc;
     Parm.OperPatern={oper};
     Parm.Ground = Acc_Form(ALG_DEPOSITOR.Account);
     stat = InputRegNum(
       {oper},
       Parm.Oper,
       TYPERES_VALFORM,
       Parm.CodIntValue,
       "Введите номер сберкнижки:",
       Parm.Series, Parm.MinRegistrNumber);
     rSeries = SubStr( Parm.Series, 1, 2 );
     rNumber = Parm.MinRegistrNumber;
     if ( not stat )
       ALG_RESULT = ERR_RES;
     end;
     if ( ALG_RESULT != ERR_RES )
       Parm.Type=0;           /* Платежеспосбные ресурсы */
       Parm.NumOper=CASHOP_DEBIT;
       Parm.ApplicationKind=ALG_SBDEPDOC.iApplicationKind;
       Parm.ApplicationKey=ALG_SBDEPDOC.ApplicationKey;
       if ( ALG_RET_OP.ApplicationKind == 3001 ) /* ALG_SBDEPDOC может быть от другой операции */
         Parm.ApplicationKind = ALG_RET_OP.ApplicationKind;
         Parm.ApplicationKey  = ALG_RET_OP.ApplicationKey;
       end;
     else
       InterDesk_EndDocBunch;
       Parm.Type=0;           /* Платежеспосбные ресурсы */
       Parm.NumOper=CASHOP_CHVALUE;
       Parm.ApplicationKind=SB_INCASH;
       Parm.ApplicationKey=FormApplicationKey(SB_INCASH);
       Parm.TypePatern = 1;   /* Неплатежеспосбные ресурсы */
     end;
     if(stat)
       stat=InitCashDoc(Parm);
     end;

     /* Проверка наличия ресурса кассы по сберкнижкам */
     if ( ( NOT stat )  AND  ( NOT CheckCashRes( CodIntValueBook ) ) )
       ALG_RESULT = OK_RES;  /* Чтобы система больше по этому поводу не ругалась */
       exit( 1 );
     end;

     /* Запись в auxinfo.dbt номера и серии номерной сбер.книжки */
       if ( stat  AND  ( ALG_RESULT != ERR_RES ) )
       if ( CodIntValueBook == VALFORM_BOOK_C )

         if ( AI_SBook_Num( ALG_SBDEPDOC.Referenc, rSeries, rNumber ) != 0 )
           stat = FALSE;
         end;
       else
         if ( AI_SBook_Num( ALG_SBDEPDOC.Referenc, "", 0 ) != 0 )
           stat = FALSE;
         end;
       end;
     end;

     if(stat)
      ALG_DEPOSITOR.Givebook="X";
      ALG_CONTRACT.BookGiven="X";
      ALG_CONTRACT.Update();
      /* Установка флага выдачи сберкнижки в документе */
      ALG_SBDEPDOC.YesSBook = "";
      SetBitFlag(ALG_SBDEPDOC.Flags,2,1);
      UpdateAlgRecords( UPDATE_DOC, True, False );
      /* ALG_UPDATE=1; */
     end;
   end;
   if ( stat )
     /*
     StrFltr = "t.t_referenc = " + ALG_SBDEPDOC.Referenc;
     dd.addFilter( StrFltr ); */
     dd.rec.Referenc      = saveReferenc;
     dd.rec.Date_Document = saveDate_Document;
     dd.rec.NumDayDoc     = saveNumDayDoc;
     if ( getEQ( dd ) )
       Пометить_Строки;
       ALG_RESULT = OK_RES;
     else
       ALG_RESULT = ERR_RES;
     end;
     /* dd.dropFilter(); */
   else
     ALG_RESULT = ERR_RES;
   end;
   if ( MacroOutput == "PRN" )
     Exit( 1 );
   end;

   dd.DropFilter();
   if ((Type_of_SB == SB_2_o) or (Type_of_SB == SB_2_o_2005))
     msgbox("Заполните ордер сберкнижки ф.2-о");
     exit(1);
   end;


 end;
