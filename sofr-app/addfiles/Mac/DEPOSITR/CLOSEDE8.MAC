/***************************************************************************\
| CLOSEDE8.MAC - макрос закрытия счета для вида вклада "ПЕНСИОННЫЙ"         |
|                                                     Зубов В.Ю. 15-01-98   |
\***************************************************************************/
/* Макрос проверяет на наличие 2-х зачислений пенсий в текущем году. Если  */
/* условие не выполнено, то с начала года переводит счета на альтернативные*/
/* условия */
import deprintr, IsSrvDoc, alg_vars, opercode, PmPropUtil;

file dd (sbdepdoc);

/*---------- Это нам передают ---------------------------------*/
 record FINISH_SBDEPDOC ("sbdepdoc.1");
 record tmpDoc          ("sbdepdoc.1"); /* файл документов */
 record save_SBDEPDOC   (sbdepdoc    );
 record save_DEPOSITOR  (depositr    );
 
/*---------- Коды возврата макроса --------------------------------------*/
const
  ОТКАТ_С_РЕЗУЛЬТАТОМ = -777, /* Откат с получением процентов */

  OP_2_ALT = 85,
  OP_2_DEP = 86,

  OnlyInputSum_Commission                      =  1,
  OnlyInsertDoc_Commission                     =  2,

  НЕ_ОБНОВЛЯТЬ                                 =  0,
  ОБНОВИТЬ                                     =  1,
  ТИП_ОПЕРАЦИИ_ЗАЧИСЛЕНИЕ                      = 73,
  ТИП_ОПЕРАЦИИ_ОТКРЫТИЕ_БЕЗНАЛИЧНЫМИ           = 51,
  ПЕНСИЯ                                       =  2,
  ПЕНСИЯ_МО                                    =  5,
  ПЕНСИЯ_МВД                                   =  6,
  ПЕНСИЯ_ФСБ                                   =  7,
  ПЕНСИЯ_ФСНП                                  =  8,
  ПЕНСИЯ_ПРОКУРАТУРЫ                           =  9,
  СЧЕТ_ОТКРЫТ                                  =  "",
  СЧЕТ_В_ОСНОВНОМ_СОСТОЯНИИ                    =  0,
  СЧЕТ_В_АЛЬТЕРНАТИВНОМ_СОСТОЯНИИ              =  1,
  ОПЕРАЦИЯ_ПЕРЕВОДА_В_АЛЬТЕРНАТИВНОЕ_СОСТОЯНИЕ = OP_2_ALT,
  ОПЕРАЦИЯ_ПЕРЕВОДА_В_ОСНОВНОЕ_СОСТОЯНИЕ       = OP_2_DEP,
  FOREVER                                      = Date(31,12,2099),
  TODAY                                        = {curdate};

 ALG_RESULT = OK_RES;  /*заранее инициализируем код возврата*/
 ALG_UPDATE = НЕ_ОБНОВЛЯТЬ;
/*--------------------------------------------------------------*/
const      Необходимо_Пенсий = 2; /* необходимо зачислений */

private var doc = TDepFile( "sbdepdoc.dbt", "r" );
file dr       (depositr); /* файл счетов     */

var IsFirst   = TRUE;
var Результат = 0;
var ErrMsg    = "";

var SummaComm = $0L;
var TypeComm  = "";
var NumOpert  = 0;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro CloseDep

 var
   errorCode, found,
   MinDate, день, месяц, год, Число_Пенсий = 0,
   MinDate1,
   NullDate = Date(0,0,0),
   PercSum1 = $0L,
   С_Панелью,
   UseMaxDate,
   NoPrint,
   Operation,
   st, stat;

  /* 1. Действия, специфичные для пенсионного вида вклада ------------- */
  if ((ALG_DEPOSITOR.Open_Close   == СЧЕТ_ОТКРЫТ              ) AND
      (ALG_DEPOSITOR.UseAlternate == СЧЕТ_В_ОСНОВНОМ_СОСТОЯНИИ)    )
    DateSplit(TODAY,день,месяц,год);
    MinDate = Date(1,1,год);
    doc.KeyNum = 2;
    doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) );
    doc.Clear;
    doc.rec.Referenc         = ALG_DEPOSITOR.Referenc;
    doc.rec.DepDate_Document = MinDate;
    doc.rec.NumDayDoc        = 0;

    if ( doc.GetGE )
      found = TRUE;
      while ((doc.rec.Referenc         == ALG_DEPOSITOR.Referenc) AND
             (doc.rec.DepDate_Document <= TODAY                 ) AND
             (found                == TRUE                  )    )
        if ( IsServDoc(doc.rec)                                               AND
           ((doc.rec.TypeOper      == ТИП_ОПЕРАЦИИ_ЗАЧИСЛЕНИЕ           ) OR
            (doc.rec.TypeOper      == ТИП_ОПЕРАЦИИ_ОТКРЫТИЕ_БЕЗНАЛИЧНЫМИ)   ) AND
           ((doc.rec.ApplType      == ПЕНСИЯ                            ) OR
            (doc.rec.ApplType      == ПЕНСИЯ_МО                         ) OR
            (doc.rec.ApplType      == ПЕНСИЯ_МВД                        ) OR
            (doc.rec.ApplType      == ПЕНСИЯ_ФСБ                        ) OR
            (doc.rec.ApplType      == ПЕНСИЯ_ФСНП                       ) OR
            (doc.rec.ApplType      == ПЕНСИЯ_ПРОКУРАТУРЫ                )   )    )
          Число_Пенсий = Число_Пенсий + 1;
          if (Число_Пенсий >= Необходимо_Пенсий)
            found = FALSE; /* Убираем лишние поиски */
          end;
        end;
        if ( not doc.Next )
          found = FALSE;
        end;
      end;
    end;
    doc.dropFilter;

    if (Число_Пенсий < Необходимо_Пенсий)
      /* Для ежеквартального расчета */
      if (месяц < 4)
         MinDate1 = Date(1,1,год);
      elif (месяц < 7)
         MinDate1 = Date(1,4,год);
      elif (месяц < 10)
         MinDate1 = Date(1,7,год);
      else
         MinDate1 = Date(1,10,год);
      end;
      ErrorCode = moveAccountIntoAnotherState
                   (ОПЕРАЦИЯ_ПЕРЕВОДА_В_АЛЬТЕРНАТИВНОЕ_СОСТОЯНИЕ,
                    ALG_DEPOSITOR.Referenc, MinDate1, FOREVER,
                    MinDate1                                     );


      if (errorCode == 0)
        ALG_DEPOSITOR.UseAlternate = СЧЕТ_В_АЛЬТЕРНАТИВНОМ_СОСТОЯНИИ;
        ALG_DEPOSITOR.NumSession   = 0;
        ALG_RESULT                 = OK_RES;
      else
        ErrMsg = "1. Ошибка перевода в альтернативное состояние = " + String(ErrorCode);
        Результат = ErrorCode;
        ALG_RESULT = ERR_RES;
        return;
      end;
    end;
  end;

  /* 2. Расчет_Процентов ---------------------------------------------- */
  ClearRecord(tmpDoc);
  tmpDoc.Referenc         = ALG_SBDEPDOC.Referenc;
  tmpDoc.IsCur            = ALG_SBDEPDOC.IsCur;
  tmpDoc.FNCash           = ALG_SBDEPDOC.FNCash;
  tmpDoc.RealFNCash       = ALG_SBDEPDOC.RealFNCash;
  tmpDoc.Account          = ALG_DEPOSITOR.Account;
  tmpDoc.Oper             = {oper};
  tmpDoc.Type_Account     = ALG_SBDEPDOC.Type_Account;
  tmpDoc.Code_Currency    = ALG_SBDEPDOC.Code_Currency;
  tmpDoc.CodClient        = ALG_SBDEPDOC.CodClient;
  tmpDoc.YesSbook         = "X";
  tmpDoc.Date_Document    = ALG_SBDEPDOC.Date_Document;
  tmpDoc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
  tmpDoc.Author           = ALG_SBDEPDOC.Author;
  tmpDoc.InSum            = $0L;
  tmpDoc.TypeComplexOper  = ALG_PARAM.NumOpert;

  С_Панелью  = 0;
  UseMaxDate = 1;
  NoPrint    = 1;

  ALG_OPERPRM.Account         = ALG_DEPOSITOR.Account;
  ALG_OPERPRM.Type_Account    = ALG_DEPOSITOR.Type_Account;
  ALG_OPERPRM.Code_Currency   = ALG_DEPOSITOR.Code_Currency;
  ALG_OPERPRM.Type_Oper       = Расчет_Процентов;
  ALG_OPERPRM.Show_Panel      = С_Панелью;
  ALG_OPERPRM.UseMaxDate      = UseMaxDate;
  ALG_OPERPRM.NoPrint         = NoPrint;
  ALG_OPERPRM.TypeComplexOper = ALG_PARAM.NumOpert;
  ALG_OPERPRM.DocumentAuthor  = 0;
  ALG_OPERPRM.AuthorCode      = 0;
  ALG_OPERPRM.CorrAcc         = "";
  ALG_OPERPRM.CorrType        = "";
  ALG_OPERPRM.DepDate         = NullDate;
  ALG_OPERPRM.PercType        = 0;
  ALG_OPERPRM.CalcDate        = NullDate;

  Результат = Выполнение_Операции(ALG_OPERPRM,tmpDoc);

  if (Результат != 0)
    ErrMsg = "2.Ошибка при расчете процентов = " + String(Результат);
    return;
  end;

  /* 3. --------------------------------------------------------------- */

  Результат = Проценты_Налог_К_Оплате(ALG_DEPOSITOR.Referenc,0,NullDate,PercSum1);

  if (Результат != 0)
    ErrMsg = "3.Ошибка при определении PercSum1 = " + String(Результат);
    return;
  end;

  /* Выходим, если не нужно править данные */
  if ((ALG_PARAM.prmC == "X") AND (Результат == 0))
    ALG_PARAM.prmD = PercSum1;
    Результат      = ОТКАТ_С_РЕЗУЛЬТАТОМ;
    return;
  end;

  /* 4. Причисляем PercSum1 ------------------------------------------- */
  ClearRecord(tmpDoc);
  tmpDoc.Referenc         = ALG_SBDEPDOC.Referenc;
  tmpDoc.IsCur            = ALG_SBDEPDOC.IsCur;
  tmpDoc.FNCash           = ALG_SBDEPDOC.FNCash;
  tmpDoc.RealFNCash       = ALG_SBDEPDOC.RealFNCash;
  tmpDoc.Account          = ALG_DEPOSITOR.Account;
  tmpDoc.Oper             = {oper};
  tmpDoc.Type_Account     = ALG_SBDEPDOC.Type_Account;
  tmpDoc.Code_Currency    = ALG_SBDEPDOC.Code_Currency;
  tmpDoc.CodClient        = ALG_SBDEPDOC.CodClient;
  tmpDoc.YesSbook         = "X";
  tmpDoc.Date_Document    = ALG_SBDEPDOC.Date_Document;
  tmpDoc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
  tmpDoc.Author           = ALG_SBDEPDOC.Author;
  tmpDoc.InSum            = PercSum1;
  tmpDoc.TypeComplexOper  = ALG_PARAM.NumOpert;

  С_Панелью  = 0;
  UseMaxDate = 1;
  NoPrint    = 1;

  ALG_OPERPRM.Account         = ALG_DEPOSITOR.Account;
  ALG_OPERPRM.Type_Account    = ALG_DEPOSITOR.Type_Account;
  ALG_OPERPRM.Code_Currency   = ALG_DEPOSITOR.Code_Currency;
  ALG_OPERPRM.Type_Oper       = Зачисление_Процентов;
  ALG_OPERPRM.Show_Panel      = С_Панелью;
  ALG_OPERPRM.UseMaxDate      = UseMaxDate;
  ALG_OPERPRM.NoPrint         = NoPrint;
  ALG_OPERPRM.TypeComplexOper = ALG_PARAM.NumOpert;
  ALG_OPERPRM.DocumentAuthor  = 0;
  ALG_OPERPRM.AuthorCode      = 0;
  ALG_OPERPRM.CorrAcc         = "";
  ALG_OPERPRM.CorrType        = "";
  ALG_OPERPRM.DepDate         = NullDate;
  ALG_OPERPRM.PercType        = 0;
  ALG_OPERPRM.CalcDate        = NullDate;

  Результат = Выполнение_Операции(ALG_OPERPRM,tmpDoc);

  if (Результат != 0)
    ErrMsg = "4.Ошибка при зачислении PercSum1 = " + String(Результат);
    return;
  end;

  /* 5. Списываем весь остаток ---------------------------------------- */
  KeyNum(dr,8);
  dr.Referenc = ALG_DEPOSITOR.Referenc;
  if (NOT GetEQ(dr))
    ErrMsg = "Не найдена запись по счету";
    Результат = -1;
    return;
  end;

  Copy(ALG_DEPOSITOR,dr);
  ALG_DEPOSITOR.End_DateDep = NullDate;

  if ((ALG_DEPOSITOR.Sum_Rest != 0) OR (StrFor(GetProgramID) == "П"))
    ClearRecord(tmpDoc);
    tmpDoc.Referenc         = ALG_SBDEPDOC.Referenc;
    tmpDoc.IsCur            = ALG_SBDEPDOC.IsCur;
    tmpDoc.FNCash           = ALG_SBDEPDOC.FNCash;
    tmpDoc.RealFNCash       = ALG_SBDEPDOC.RealFNCash;
    tmpDoc.Account          = ALG_DEPOSITOR.Account;
    tmpDoc.Oper             = {oper};
    tmpDoc.Type_Account     = ALG_SBDEPDOC.Type_Account;
    tmpDoc.Code_Currency    = ALG_SBDEPDOC.Code_Currency;
    tmpDoc.CodClient        = ALG_SBDEPDOC.CodClient;
    tmpDoc.YesSbook         = "X";
    tmpDoc.Date_Document    = ALG_SBDEPDOC.Date_Document;
    tmpDoc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
    tmpDoc.Author           = ALG_SBDEPDOC.Author;
    tmpDoc.OutSum           = GetLastFCDocRest(ALG_DEPOSITOR.Referenc, {curdate});
    tmpDoc.TypeComplexOper  = ALG_PARAM.NumOpert;
    tmpDoc.ApplType         = FINISH_SBDEPDOC.ApplType;
    //tmpDoc.Account_Receiver = FINISH_SBDEPDOC.Account_Receiver;
    //tmpDoc.CodeCur_Receiver = FINISH_SBDEPDOC.CodeCur_Receiver;
    //tmpDoc.MFO_Receiver     = FINISH_SBDEPDOC.MFO_Receiver;
    //tmpDoc.CorAcc_Receiver  = FINISH_SBDEPDOC.CorAcc_Receiver;
    tmpDoc.IsControl        = StrFor(1); /* Признак главного документа */
    tmpDoc.PercTrnRest      = FINISH_SBDEPDOC.PercTrnRest;
    tmpDoc.PercTrnAlt       = FINISH_SBDEPDOC.PercTrnAlt;
    tmpDoc.CurrencyBought   = FINISH_SBDEPDOC.CurrencyBought;
    tmpDoc.SummaTaxRub      = FINISH_SBDEPDOC.SummaTaxRub;

    if ((FINISH_SBDEPDOC.TypeOper != 0                 ) AND
        (FINISH_SBDEPDOC.TypeOper != ALG_PARAM.NumOpert)    )
      Operation = FINISH_SBDEPDOC.TypeOper;
    elif (ALG_PARAM.NumOpert == Закрытие_Наличными)
      Operation = Расход;
    else
      Operation = Списание_По_Раз_Поруч;
    end;

    tmpDoc.TypeOper = Operation;

    if (Operation == Безналичная_Конверсия)
      /* Пересчет суммы с учетом подоходного налога */
      if (tmpDoc.SummaTaxRub != 0)
        tmpDoc.OutSum = tmpDoc.OutSum - tmpDoc.SummaTaxRub;
      end;
      /* Пересчет конвертируемой суммы с учетом процентов */
      if (tmpDoc.PercTrnRest != 0)
        tmpDoc.CurrencyBought = tmpDoc.OutSum / tmpDoc.PercTrnRest;
      end;
    end;

    if (IsFirst)
      Результат = ОТКАТ_С_РЕЗУЛЬТАТОМ;
      InterDesk_EndDocBunch();
      return;
    elif (SummaComm > 0)
      Результат = GetCommissionOnOper(tmpDoc,tmpDoc.OutSum,OnlyInsertDoc_Commission,SummaComm,TypeComm,NumOpert,ALG_PARAM.NumOpert);
      if (Результат != 0)
        ErrMsg = "Ошибка при вставке документа на комиссию " + String(Результат);
        return;
      end;
    end;

    С_Панелью  = 0;
    UseMaxDate = 1;
    NoPrint    = 1;

    ClearRecord(ALG_OPERPRM);
    ALG_OPERPRM.Account           = ALG_DEPOSITOR.Account;
    ALG_OPERPRM.Type_Account      = ALG_DEPOSITOR.Type_Account;
    ALG_OPERPRM.Code_Currency     = ALG_DEPOSITOR.Code_Currency;
    ALG_OPERPRM.Type_Oper         = Operation;
    ALG_OPERPRM.Show_Panel        = С_Панелью;
    ALG_OPERPRM.UseMaxDate        = UseMaxDate;
    ALG_OPERPRM.NoPrint           = NoPrint;
    ALG_OPERPRM.NonCashCommission = ALG_PARAM.prmI;
    ALG_OPERPRM.TypeComplexOper   = ALG_PARAM.NumOpert;
    ALG_OPERPRM.DocumentAuthor    = CodeFor(tmpDoc.Author);
    if (Operation == Списание_Переводом)
      ALG_OPERPRM.CorrAcc         = PMNT_PROP_REC_BUF.Account;
      ALG_OPERPRM.CorrType        = PMNT_PROP_REC_BUF.CorrAccount;
    end;

    Результат = Выполнение_Операции(ALG_OPERPRM,tmpDoc);

    if (Результат != 0)
      ErrMsg = "5. Ошибка при расходе "+String(Результат);
      return;
    end;

  else
    if (IsFirst)
      ClearRecord(tmpDoc);
      Результат = ОТКАТ_С_РЕЗУЛЬТАТОМ;
      InterDesk_EndDocBunch();
      return;
    end;
  end;

  ALG_RESULT = END_RES; /* Все Ok */

  return;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro TrnProc

  CloseDep();

  if (Результат != 0)
    if (Результат == ОТКАТ_С_РЕЗУЛЬТАТОМ)
      ;
    else
      ALG_RESULT = ERR_RES;
    end;
    AbortTrn();
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  LoopInTrn(TRUE);

  ClearRecord(tmpDoc);

  Copy(save_DEPOSITOR,ALG_DEPOSITOR);
  Copy(save_SBDEPDOC,ALG_SBDEPDOC);
  Copy(dd,ALG_SBDEPDOC);
  SetRecordAddr(FINISH_SBDEPDOC,dd,0,0,true);

  if ( FINISH_SBDEPDOC.TypeOper == Списание_Переводом )
    if ( PMNT_PROP_REC_BUF.CorrAccount == "Пенсионный +" )
      ALG_RESULT == OK_RES;
      return;
    end;
  end;

  /* Нужно взять комиссию - для этого предварительно определим сумму расхода */
  IsFirst = TRUE;
  if ((ALG_PARAM.prmI2 >   0 ) AND 
      (ALG_PARAM.prmC  != "X")    )
    if ((StrFor(GetProgramID) == "П") AND (ALG_PARAM.prmI > 0))
      ;
    else
      if ( not ProcessTrn( 1, "TrnProc" ) )
        if (Результат != ОТКАТ_С_РЕЗУЛЬТАТОМ)
          ALG_RESULT = ERR_RES;
          Результат = 4714;
        end;
      end;
      Copy(ALG_DEPOSITOR,save_DEPOSITOR);
      Copy(ALG_SBDEPDOC,save_SBDEPDOC);
      Copy(dd,ALG_SBDEPDOC);
      SetRecordAddr(FINISH_SBDEPDOC,dd,0,0,true);
    end;
  end;

  /* Проведем собственно операцию закрытия счета */
  IsFirst = FALSE;
  if (ALG_RESULT != ERR_RES)
    Результат = 0;
    if ((ALG_PARAM.prmI2 >   0 ) AND 
        (ALG_PARAM.prmC  != "X") AND
        (tmpDoc.OutSum   >   0 )    )
      if ((StrFor(GetProgramID) == "П") AND (ALG_PARAM.prmI > 0))
        ;
      else
        Результат = GetCommissionOnOper(tmpDoc,tmpDoc.OutSum,OnlyInputSum_Commission,SummaComm,TypeComm,NumOpert);
        if (Результат == 2)
          MsgBox("Отказались от проведения операции");
          ALG_RESULT = ERR_RES;
        elif (Результат != 0)
          ErrMsg = "Ошибка при взятии комиссии = " + String(Результат);
          ALG_RESULT = ERR_RES;
        end;
      end;
    end;
    if (Результат == 0)
      if ( not ProcessTrn( 1, "TrnProc" ) )
        if (Результат != ОТКАТ_С_РЕЗУЛЬТАТОМ)
          ALG_RESULT = ERR_RES;
          Результат = 4714;
        end;
      end;
    end;
  end;
  if ((ALG_PARAM.prmC == "X") AND (Результат == ОТКАТ_С_РЕЗУЛЬТАТОМ))
    ALG_RESULT = END_RES;
    ALG_UPDATE = 0;
  end;

  if ((ErrMsg != "") AND (NOT InFCGroupOpRun()))
    MsgBox(ErrMsg);
  end;

  /* Убираем вывод на экран */
  Exit(1);
   
end;
