/*
  СБЕРБАНК 1.0
  R-Style SoftWare Lab.
*/

IMPORT deprintr, deskinter, /*ci_ord_b, ci_ord_s,*/ brparm, CvLnMain, InputRN, docbunch;

file CIDoc  ("ci_doc.dbt")    key 3;
file CIOper ("ci_oper.dbt")   key 0;
file CISubOp("ci_subop.dbt")  key 0;
file CashDoc("sb_casdc.dbt")  key 7;
file CashLink("sb_casln.dbt") key 0;
file CashVal("sb_casvl.dbt");
file Bunch  ("bunch.dbt")     key 0;
file DeskAux("desk_aux.dbt")  key 0;
file PayDoc( "pay_doc.dbt" )  key 2;
file CIMisc( "ci_misc.dbt" )  key 0;

record IngotAux("desk_aux.1");
record CapIssueDoc("ci_doc.dbt");
record DragMetInfo("ci_doc.4");
record MoneyInfo("ci_doc.2");

record Parm(CashParm);

const
  CI_OGSZ          = 1,
  CI_LOTTERY       = 2,
  CI_CERT          = 3,
  CI_COIN          = 4,
  CI_INGOT         = 5,
  CI_MISC          = 10,

  O_OGSZ_SELL      = 1,
  O_OGSZ_BUY       = 2,
  O_OGSZ_PAYWARR   = 3,
  O_OGSZ_KEEP      = 4,
  O_OGSZ_RETURN    = 5,

  O_LOTTERY_SELL   = 1,
  O_LOTTERY_PAYWIN = 2,
  O_LOTTERY_BUY    = 3,
  O_LOTTERY_KEEP   = 4,
  O_LOTTERY_RETURN = 5,

  O_CERT_SELL      = 1,
  O_CERT_PAY       = 2,
  O_CERT_PAYALN    = 3,
  O_CERT_KEEP      = 4,
  O_CERT_RETURN    = 5,

  O_MISC_SELL      = 1,
  O_MISC_BUY       = 2,
  O_MISC_KEEP      = 3,
  O_MISC_RETURN    = 4,

  O_INGOT_SELL     = 1,
  O_INGOT_BUY      = 2,
  O_INGOT_KEEP     = 3,
  O_INGOT_RETURN   = 4;

const CASHOP_DEBET    = 5;
const VALFORM_SPR31   = 31;

const
  FLAGDEBCRED_IN   = 1,  /* Кредит */
  FLAGDEBCRED_OUT  = 2;  /* Дебет */

const
  TYPERES_CURRENCY    = 1,   /* Деньги (из справочника валют)     */
  TYPERES_VALFORM     = 2,   /* Ценные бланки                     */
  TYPERES_VALUABLES   = 3,   /* Др. ценности (из справочника ценностей) */
  TYPERES_MINCAPISSUE = 500; /* Минимально возможный код ЦБ     */

const
  SB_CAPISSUE = 300;

var {curdate}, {oper},
    OperBrigade = GetOperBrigade;

/* Поля для выходной формы */
var KvitFrom = "", KvitFor = "", KvitForAppl = "", KvitType = "";
var Total = $0, NumItems = 0;
var Tax = $0, TotalPrice = $0, TotalWeight = 0;

var
  G_ApplicationKind, G_ApplicationKey;

macro FindDeskAux( ValueRef, Date, Series, Number )

  DeskAux.Branch = NumFNCash;
  DeskAux.ValueRef = ValueRef;
  DeskAux.Date = Date;
  DeskAux.IsGone = 1;
  DeskAux.Series = Series;
  DeskAux.Number = Number;
  return GetEQ( DeskAux );
end;

macro FindCashVal( ValueRef )

  KeyNum( CashVal, 0 );
  CashVal.RefValue = ValueRef;
  return GetEQ( CashVal );
end;

macro FindCashValForCode( Type, ValType, ValCode )

  KeyNum( CashVal, 1 );
  CashVal.TypeValue = ValType;
  CashVal.CodIntValue = ValCode;
  CashVal.Type = Type;
  return GetEQ( CashVal );
end;

macro FindCashDoc           

  var
    Type, ValType, ValCode;

  GetMainValueCode( CIDoc.Type, CIDoc.Issue, CIDoc.Operation, CIDoc.SubOp, Type, ValType, ValCode );
  FindCashValForCode( Type, ValType, ValCode );

  CashDoc.RefValue = CashVal.RefValue;
  CashDoc.ApplicationKind = CIDoc.ApplicationKind;
  CashDoc.ApplicationKey = CIDoc.ApplicationKey;
  return GetEQ( CashDoc );
end;

macro FindCIDoc

  CIDoc.ApplicationKind = CashLink.ApplicationKind2;
  CIDoc.ApplicationKey = CashLink.ApplicationKey2;
  return GetEQ( CIDoc );
end;

macro FindCIMisc

  CIMisc.Type =    CIDoc.Type;
  CIMisc.IssueID = CIDoc.Issue;
  return GetEQ( CIMisc );
end;

macro PrintList
  var
    i = 0, str = "",
    PriceNDS,
    BunchFound, RecFound;

  [

                                          ОПИСЬ
                           приобретаемых золотых мерных слитков
                                        ##########                          

                                                                                              ]
  ( {curdate} );
  [-------------------------------------------------------------------------------------------];
  [| № |  Номер(шифр)   |    Завод    |  Металл  | Качество, | Цена реализации |Масса лигат.,|];
  [|п/п|    слитка      |производитель|          |   проба   | с учетом НДС    |    (гр.)    |];
  [-------------------------------------------------------------------------------------------];

  ClearRecord( CashLink );
  CashLink.ApplicationKind1 = G_ApplicationKind;
  CashLink.ApplicationKey1 = G_ApplicationKey;
  RecFound = GetGE( CashLink );  
  while ( RecFound
    and ( CashLink.ApplicationKind1 == G_ApplicationKind )
    and ( CashLink.ApplicationKey1 == G_ApplicationKey ) )
    if ( ( CashLink.RefValue2 == 0 ) and ( CashLink.ApplicationKind2 == G_ApplicationKind ) )
      if ( FindCIDoc )
        SetRecordAddr( DragMetInfo, CIDoc, 0, FldOffset( CIDoc, "Info" ), true ); 

        if ( not FindCIMisc )
          MsgBox( "Не найден вид слитков" );
          Exit( 1 );
        end;

        Total = Total + CIDoc.Sum;
        NumItems = NumItems + DragMetInfo.NumItems;

        if ( KvitFrom == "" )
          /* Принято от */
          KvitFrom = DragMetInfo.LastName + " " + DragMetInfo.FirstName + " " + DragMetInfo.PatrName;
        end;

        if ( FindCashDoc )
          ClearRecord( Bunch );
          Bunch.BagAppKind = CashDoc.ApplicationKind;
          Bunch.BagAppKey = CashDoc.ApplicationKey;
          BunchFound = GetGE( Bunch );
          if ( BunchFound
            and ( Bunch.BagAppKind == CashDoc.ApplicationKind )
            and ( Bunch.BagAppKey == CashDoc.ApplicationKey ) )
            if( FindDeskAux( CashDoc.RefValue, CashDoc.CashDateDoc, Bunch.Series, Bunch.Min ) )
              SetRecordAddr( IngotAux, DeskAux, 0, FldOffset( DeskAux, "Info" ), true );
              i = i + 1;
              if ( CIMisc.NDS_IsIncludedInCost )
                PriceNDS = DragMetInfo.Price;
              else
                PriceNDS = DragMetInfo.Price + DragMetInfo.Tax;
              end;
              TotalPrice = TotalPrice + PriceNDS;
              TotalWeight = TotalWeight + IngotAux.RealWeight / 10000;
              [|###|################|#############|##########|###########|#################|#############|]
              ( i, RegNumToStr(Bunch.Series,Bunch.Min):w, DragMetInfo.Manufacturer:w, 
                DragMetInfo.Material, DragMetInfo.Hallmark, PriceNDS, IngotAux.RealWeight / 10000 );
              KvitForAppl = KvitForAppl + DragMetInfo.Material + " " + DragMetInfo.Hallmark + " пробы " +
                            " массой " + DragMetInfo.Weight + 
                            " слиток № " + RegNumToStr(Bunch.Series,Bunch.Min) +
                            " изг. " + DragMetInfo.Manufacturer + "\n";
            end;
          end;
        end;
      end;
    end;
    RecFound = Next( CashLink );    
  end;

  [-------------------------------------------------------------------------------------------
                                                                Итого:          ############# 
  ]( TotalWeight );
  [-------------------------------------------------------------------------------------------
   Итого рублей
   (сумма цифрами и прописью)  ############
   (#########################################################################################)
  ]( TotalPrice, RubToStr( TotalPrice ) );

  [-------------------------------------------------------------------------------------------];

  RubToStr( Money( NumItems ), str );
   
  [
    Количество слитков  ####  #####################
                        (цифрами и прописью)

    Количество сертификатов _________________________________
                             (цифрами и прописью)

    Клиент ________________(##################################################)
            (подпись)
    Проверено _______________________ (Ф.И.О.)
            (подпись контролёра)

            М.П.
  ]( NumItems, str, 
     KvitFrom );

  print( "" ); /* End of page */

end;

macro GetPatern( Oper )

  file GroupMem( "groupmem.dbt" ) key 0;

  GroupMem.Branch = NumRealFNCash;
  GroupMem.Date = {curdate};
  GroupMem.Group = OperBrigade;
  GroupMem.Oper = Oper;
  if ( GetEQ( GroupMem ) )
    return GroupMem.Linked;
  else
    return 0;
  end;
end;

macro CalcTax

  const
    SB_CASHOPERT = 200, 
    CO_Commission = 21;
    
  var
    bun  = TDocBunch( G_ApplicationKind, G_ApplicationKey ), 
    found;

  found = bun.firstDoc;
  while ( found )
    if ( ( bun.docAppKind == SB_CASHOPERT ) and ( bun.docValueRef == 0 ) )
      PayDoc.iApplicationKind = bun.docAppKind;
      PayDoc.ApplicationKey = bun.docAppKey;
      if ( GetEQ( PayDoc ) )
        if ( PayDoc.GroupOpert == CO_Commission )
          Tax = Tax + PayDoc.InSum;
        end;
      end;
    end;
    found = bun.nextDoc;
  end;
end;


/*******************************************************************
                        Печать ордеров для слитков
*******************************************************************/
macro Report( ApplicationKind, ApplicationKey )

  var
    BranchStrNum = "",
    DepartNum = "",
    DepartName = "";

  GetBranchParams( NumFNCash, BranchStrNum, DepartNum, DepartName );

  G_ApplicationKind = ApplicationKind;
  G_ApplicationKey = ApplicationKey;

  PrintList;

end;
