/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Печать бланка сертификата на принтере EPSON TM.

*****************************************************************/

IMPORT DeprIntr;

const cListOffset = 1;  // Смещение строк печати бланка по листу.

file Cert     ( "cert.dbt"    ) key 1;
file CertRate ( "certrate.dbt") key 0;
file CIDoc    ( "ci_doc.dbt"  );

record CapIssueDoc ( "ci_doc.dbt" );
record CertDocInfo ( "ci_doc.3" );

private const PTCK_BIC = 3;   // БИК (РФ).
private const CODEKIND_BIC = 3;

private var OurBank_BIC     = Trim( getRegNumOurBank( PTCK_BIC ) );
private var OurBank_Name    = "";
private var OurBank_CorrAcc = "";
private var OurBank_INN     = "";
private var OurBank_KPP     = "";

var ClientList = TClientList;

array aMonth, aDayMonth;

//
// Дата в формате для сертификата.
//
macro dateCert( _date )
 var day, mon, year;
 DateSplit( _date, day, mon, year );
 return aDayMonth( day ) + aMonth( mon ) + year + " г.";
end;

//
// Получение ставки процентов за пользование сертификатом.
//
macro getCertRate()

 var stat;
 var rate = $0.0;

 ClearRecord( Cert );
 Cert.Year     = CertDocInfo.Year;
 Cert.Par      = CertDocInfo.Par;
 Cert.Duration = CertDocInfo.Duration;
 if ( GetEQ( Cert ) )
   ClearRecord( CertRate );
   CertRate.Issue = Cert.Issue;
   CertRate.Date  = Date( 1, 1, 1990 );
   stat = getGE( CertRate );
   while ( stat  AND
           ( CertRate.Issue == Cert.Issue )  AND
           ( CertRate.Date  <= CertDocInfo.SellDate ) )
     rate = CertRate.YearRate * 0.01;
     stat = Next( CertRate );
   end;
 else
   println( "Не найден сертификат." );
 end;

 return rate;

end;

macro PrintOrd( DocAddr )

 var PercentRub, PercentKop;
 var DemandDateDay, DemandDateMon, DemandDateYear;

 var vClient;
 var vPaperSeries;
 var vPaperNumber;
 var vPaperIssuedDate;
 var vPaperIssuer;
 var vSum;

 aMonth( 1) = "января ";
 aMonth( 2) = "февраля ";
 aMonth( 3) = "марта ";
 aMonth( 4) = "апреля ";
 aMonth( 5) = "мая ";
 aMonth( 6) = "июня ";
 aMonth( 7) = "июля ";
 aMonth( 8) = "августа ";
 aMonth( 9) = "сентября ";
 aMonth(10) = "октября ";
 aMonth(11) = "ноября ";
 aMonth(12) = "декабря ";

 aDayMonth( 1) = "Первое ";
 aDayMonth( 2) = "Второе ";
 aDayMonth( 3) = "Третье ";
 aDayMonth( 4) = "Четвертое ";
 aDayMonth( 5) = "Пятое ";
 aDayMonth( 6) = "Шестое ";
 aDayMonth( 7) = "Седьмое ";
 aDayMonth( 8) = "Восьмое ";
 aDayMonth( 9) = "Девятое ";
 aDayMonth(10) = "Десятое ";
 aDayMonth(11) = "Одиннадцатое ";
 aDayMonth(12) = "Двенадцатое ";
 aDayMonth(13) = "Тринадцатое ";
 aDayMonth(14) = "Четырнадцатое ";
 aDayMonth(15) = "Пятнадцатое ";
 aDayMonth(16) = "Шестнадцатое ";
 aDayMonth(17) = "Семнадцатое ";
 aDayMonth(18) = "Восемнадцатое ";
 aDayMonth(19) = "Девятнадцатое ";
 aDayMonth(20) = "Двадцатое ";
 aDayMonth(21) = "Двадцать первое ";
 aDayMonth(22) = "Двадцать второе ";
 aDayMonth(23) = "Двадцать третье ";
 aDayMonth(24) = "Двадцать четвертое ";
 aDayMonth(25) = "Двадцать пятое ";
 aDayMonth(26) = "Двадцать шестое ";
 aDayMonth(27) = "Двадцать седьмое ";
 aDayMonth(28) = "Двадцать восьмое ";
 aDayMonth(29) = "Двадцать девятое ";
 aDayMonth(30) = "Тридцатое ";
 aDayMonth(31) = "Тридцать первое ";

 SetBuff( CapIssueDoc, DocAddr );

 Copy( CIDoc, CapIssueDoc );

 SetRecordAddr( CertDocInfo, CIDoc, 0, FldOffset( CIDoc, "Info" ), TRUE );

 // Получение атрибутов нашего банка.
 if ( NOT iFindBank( CODEKIND_BIC, OurBank_BIC,
                                   OurBank_Name,
                                   OurBank_CorrAcc,
                                   OurBank_INN,
                                   OurBank_KPP ) )
   msgbox( "Не удалось получить атрибуты нашего банка." );
 end;

 vSum = CertDocInfo.Yield / CertDocInfo.NumItems;

 // Разбиение суммы процентов на рубли и копейки.
 RubToStr( vSum, PercentRub, PercentKop );

 // Разбиение даты срока возврата на день-месяц-год.
 DateSplit( CertDocInfo.DemandDate,
            DemandDateDay, DemandDateMon, DemandDateYear );

 if ( ClientList.GetRecord( CapIssueDoc.ClientCode ) )
   vClient = ClientList.CurRec;
   vPaperSeries     = vClient.rec.PaperSeries;
   vPaperNumber     = vClient.rec.PaperNumber;
   vPaperIssuedDate = String( vClient.rec.PaperIssuedDate );
   vPaperIssuer     = vClient.rec.PaperIssuer;
 end;

 SetPrintPort( "LPT4" );  // Печать на принтере EPSON TM.
 print("@c0");         // Печать на подкладном документе.

 [ #####













  ################################################################################## #####
                    ################################################################ #####
                 ##########                                                          #####
           #################################################            ####         #####


  ##################################################################################

  ################################################################################## #####
















  ##########################################    ##       ####################  ####  #####
       ####                                  ############                            #####
  ################################################################################## #####
  ################      ##############################################################]
  (
    "\x1B\x4A" + StrFor( cListOffset ),
    ( dateCert( CertDocInfo.SellDate ) ):l,
    "\x0D\x1B\x4A\x09",
    ( dateCert( CertDocInfo.DemandDate ) ):l,
    "\x1B\x4A\x08",
    getCertRate():l:6:2,
    "\x1B\x4A\x0D",
    PercentRub,
    PercentKop,
    "\x1B\x4A\x0F",
    OurBank_Name,
    "к/счет " + OurBank_CorrAcc + ", БИК " +
                OurBank_BIC     + ", ИНН " +
                OurBank_INN,
    "\x1B\x4A\x03",
    CertDocInfo.SellDate:m,
    DemandDateDay,
    aMonth( DemandDateMon ),
    DemandDateYear,
    "\x1B\x4A\x0E",
    Int( DoubleL( vSum ) ):l:4,
    PercentKop,
    "\x1B\x4A\x0F",
    CertDocInfo.LastName    + " " +
      CertDocInfo.FirstName + " " +
      CertDocInfo.PatrName,
    "\x1B\x4A\x11",
    "серия " + vPaperSeries + " № " + vPaperNumber,
    vPaperIssuedDate + ", " + vPaperIssuer
  );

 print( StrFor( 12 ) );

end;
