
/*
**  Опись на подкрепление слитками
**
*/

import RD_Comm;

file Bunch  ("bunch.dbt")     key 0;
file DeskAux("desk_aux.dbt")  key 0;
file CICode( "ci_code.dbt" )  key 0;
file CIMisc( "ci_misc.dbt" )  key 0;                

private const
  CI_INGOT = 5;

record IngotAux("desk_aux.1");

macro FindCIMisc( Type, Issue )

  CIMisc.Type = Type;
  CIMisc.IssueID = Issue;
  if ( not GetEQ( CIMisc ) )
    MsgBox( "Не найден слиток" );
    Exit( 1 );
  end;
end;

macro FindCashVal( ValueRef )

  if ( not FindCashValue( ValueRef ) )
    MsgBox( "Не найден ресурс кассы" );
    Exit( 1 );
  end;
end;

macro FindCICode( ID )

  CICode.AutoKey = ID; 
  if ( not GetEQ( CICode ) )
    MsgBox( "Не найдено значение в файле ci_code.dbt" );
    Exit( 1 );
  end;
end;

macro GetPrice

  if ( IngotAux.SellPriceCurrCode == 0 )
    return IngotAux.SellPrice;
  else
    Curr.Code_Currency = IngotAux.SellPriceCurrCode;
    GetCBRate;
    return Cur2Rub( IngotAux.SellPrice );
  end; 
end;

macro GetManufacturer
  
  if ( CIMisc.ManufRef )
    FindCICode( CIMisc.ManufRef );
    return CICode.String;
  else
    return "";
  end;
end;

macro GetMaterial

  FindCICode( CIMisc.MaterialRef );
  return CICode.String;
end;

macro GetHallmark
                          
  FindCICode( CIMisc.HallmarkRef );
  return CICode.Double;
end;
         
macro FindDeskAux( ValueRef, Date, Series, Number )

  DeskAux.Branch = NumFNCash;
  DeskAux.ValueRef = ValueRef;
  DeskAux.Date = Date;
  DeskAux.IsGone = 0;
  DeskAux.Series = Series;
  DeskAux.Number = Number;
  if ( GetEQ( DeskAux ) )
    return true;
  else
    DeskAux.IsGone = 1;
    return GetEQ( DeskAux );
  end;
end;


macro PrintList
  var
    i = 0, str = "",
    Price, TotalPrice = 0, TotalWeight = 0,
    BunchFound;          
                                                                                 
  FindCashVal( CashDoc.RefValue );

  if ( CashValue.TypeValue != TYPERES_MINCAPISSUE + CI_INGOT )
    Exit( 1 );
  end;

  FindCIMisc( CashValue.TypeValue - TYPERES_MINCAPISSUE, CashValue.CodIntValue );


  [

                                          ОПИСЬ
                        на подкрепление золотыми мерными слитками
                               ############################                 

                                                                                              ]
  ( String( CashDoc.CashDateDoc:m ):c );
  [-------------------------------------------------------------------------------------------];
  [| № |  Номер(шифр)   |    Завод    |  Металл  | Качество, | Цена реализации |Масса лигат.,|];
  [|п/п|    слитка      |производитель|          |   проба   |                 |    (гр.)    |];
  [-------------------------------------------------------------------------------------------];
               
  ClearRecord( Bunch );
  Bunch.BagAppKind = CashDoc.ApplicationKind;
  Bunch.BagAppKey = CashDoc.ApplicationKey;
  BunchFound = GetGE( Bunch );
  while ( BunchFound
    and ( Bunch.BagAppKind == CashDoc.ApplicationKind )
    and ( Bunch.BagAppKey == CashDoc.ApplicationKey ) )
     if( FindDeskAux( CashDoc.RefValue, CashDoc.CashDateDoc, Bunch.Series, Bunch.Min ) )
       SetRecordAddr( IngotAux, DeskAux, 0, FldOffset( DeskAux, "Info" ), true );
       i = i + 1;
       Price = GetPrice;
       TotalPrice = TotalPrice + Price;
       TotalWeight = TotalWeight + IngotAux.RealWeight / 10000;
       [|###|################|#############|##########|###########|#################|#############|]
       ( i, RegNumToStr(Bunch.Series, Bunch.Min):w, GetManufacturer:w, 
         GetMaterial, GetHallmark, Price, IngotAux.RealWeight / 10000 );
     end;
     BunchFound = Next( Bunch );
   end;
  [-------------------------------------------------------------------------------------------
                                                                Итого:          ############# 
  ]( TotalWeight );
  [-------------------------------------------------------------------------------------------
   Итого рублей
   (сумма цифрами и прописью)  ############
   (#########################################################################################)
  ]( TotalPrice, TotalPrice:m );

  [-------------------------------------------------------------------------------------------];

  RubToStr( Money( CashDoc.ValueCol ), str );
   
  [
    Количество слитков  ####  #####################
                        (цифрами и прописью)

    Количество сертификатов _________________________________
                             (цифрами и прописью)

    Начальник Отдела
    кассовых операций __________________________________________ (фамилия, инициалы)


    Контролер         __________________________________________ (фамилия, инициалы)

            М.П.
  ]( CashDoc.ValueCol, str );

  print( "" ); /* End of page */

end; 

  private var Result = true;

  if ( CashDoc.RefValue == 0 )
    Exit( 1 ); 
  end;

  PrintList;
  PrintList;
  PrintList;
  PrintLn( StrFor( 12 ) );
end;