/*
$Name:           ACC_ESTM.mac
$Module:         RETAIL
$Description:    Накопленные проценты по счету
*/

/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Накопленные проценты по счету                                           *
*                                                                          *
*  Лебедев С.В.                                                            *
*  22.03.2000                                                              *
\**************************************************************************/

import DeprIntr, IsSrvDoc, "cur_rate.mac";

private var pcestim  = TBFile( "pcestim.dbt",  "r", 0 );
private var depositr = TBFile( "depositr.dbt", "r", 8 );
private var sbdepdoc = TBFile( "sbdepdoc.dbt", "r", 3 );

private var depclnt = TClientList;

private var {curdate};

private var WasFindPrevRec = false;

private var CBRateEstim   = 0.0;
private var BuyRateEstim  = 0.0; 
private var SellRateEstim = 0.0;

private var NeedRestOnCurDate = false;

private record sc_prd ( SC_PRD ) dialog;
  const ne_BegDate = 0;
  const ne_EndDate = 1;
  sc_prd.BeginDate = Date( 0, 0, 0 );
  sc_prd.EndDate   = {curdate};


/**************************************************************************\
|                         Печать заголовка отчета                          |
\**************************************************************************/
private macro PrintHeadReport ( Referenc )

  var Account    = "";
  var DepAccount = "";
  var BegDate    = sc_prd.BeginDate;
  var DatePrev   = "- нет";
  var stat;

  Account    = DefPcEstimAccountForDepositr( depositr, true );
  DepAccount = depositr.rec.Account;
  if ( BegDate < depositr.rec.Open_Date )
    BegDate = depositr.rec.Open_Date;
  end;

  pcestim.Clear;
  pcestim.rec.Referenc   = Referenc;
  pcestim.rec.DateOperat = sc_prd.BeginDate; 
  pcestim.rec.NumDayDoc  = 0;
  pcestim.addFilter( "T_Referenc = " + String( Referenc ) );
  stat = pcestim.GetLT;
  while ( stat )
    if ( pcestim.rec.Referenc == Referenc )
      if ( ( ( pcestim.rec.TypeOper == 38 ) or 
             ( pcestim.rec.TypeOper == 39 )   ) and 
           (   pcestim.rec.Action   <   2     )    )
        DatePrev = String( pcestim.rec.DateOperat:f );
        WasFindPrevRec = true;
        stat = false;
      else
        stat = pcestim.Prev;
      end;
    else
      stat = false;
    end;
  end;
  pcestim.dropFilter;

  [ ];
  [ Выписка из лицевого счета #]( Acc_Form(Account):f );
  [ Счет вкладчика            #]( Acc_Form(DepAccount):f );
  [ ];
  [                                                                            Период с ########## по ##########]( BegDate, sc_prd.EndDate );
  [ Дата предыдущей операции #]( DatePrev );
  [ ----------------------------------------------------------------------------------------------------------------------------];
  [ |   Дата   |Номер |    Наименование    | Номер корреспондирующего |  Дебет  | Кредит  | Остаток в  |  Рублевый  |Вид (шифр)|];
  [ | операции |докум.|      операции      |          счета           |         |         |валюте счета| эквивалент | операции |];
  [ ----------------------------------------------------------------------------------------------------------------------------];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro PrintBottomReport ( Referenc )

  var RestOnCurDate = $0L;
  var stat;

  [ ];
  if ( NeedRestOnCurDate )
    pcestim.Clear;
    pcestim.rec.Referenc   = Referenc;
    pcestim.rec.DateOperat = {curdate} + 1; 
    pcestim.rec.NumDayDoc  = 0;
    pcestim.addFilter( "T_Referenc = " + String( Referenc ) );
    stat = pcestim.GetLT;
    while ( stat )
      if ( pcestim.rec.Referenc == Referenc )
        if ( ( ( pcestim.rec.TypeOper == 38 ) or 
               ( pcestim.rec.TypeOper == 39 )   ) and 
             (   pcestim.rec.Action   <   2     )    )
          RestOnCurDate = pcestim.rec.SummaPay_Rest;
          stat = false;
        else
          stat = pcestim.Prev;
        end;
      else
        stat = false;
      end;
    end;
    pcestim.dropFilter;

    [ По состоянию на дату формирования выписки сумма начисленных процентов составляет #]( Trim( String( RestOnCurDate:12:2:a ) ) );
    [ ];
  end;
  [ Дата составления ##########]( {curdate}:f );

end;


/**************************************************************************\
|                         Печать заголовка отчета                          |
\**************************************************************************/
private macro PrintEsimDocs ( Referenc )

  var wasRecs      = false;
  var nameOper     = "";
  var sumDebet     = "    -";
  var sumCredit    = "    -";
  var corrAccount  = "";
  var shifrOper    = "";
  var outRest      = $0.L;
  var sumSumDebet  = $0.L;
  var sumSumCredit = $0.L;
  var sumEqv       = "";
  var stat;

  if ( not WasFindPrevRec )
    pcestim.Clear;
  end;
  if ( ( CBRateEstim != 0 ) and ( depositr.rec.Code_Currency != 0 ) )
    sumEqv = Money( pcestim.rec.SummaPay_Rest * CBRateEstim );
    sumEqv = String( sumEqv:12:2:a );
  else
    sumEqv = "      -";
  end;

  [ |Входящий остаток                                                 |         |         |############|############|          |]
  ( pcestim.rec.SummaPay_Rest:12:2:a, sumEqv );
  [ ----------------------------------------------------------------------------------------------------------------------------];
  outRest = pcestim.rec.SummaPay_Rest;
 
  pcestim.Clear;
  pcestim.rec.Referenc   = Referenc;
  pcestim.rec.DateOperat = sc_prd.BeginDate; 
  pcestim.rec.NumDayDoc  = 0;
  stat = pcestim.GetGE;
  while ( stat )
    if ( ( pcestim.rec.Referenc   == Referenc       ) and
         ( pcestim.rec.DateOperat <= sc_prd.EndDate )    )
      if ( ( ( pcestim.rec.TypeOper == 38 ) or 
             ( pcestim.rec.TypeOper == 39 )   ) and 
           (   pcestim.rec.Action   <   2     )    )
        wasRecs = true;
        outRest = pcestim.rec.SummaPay_Rest;
        if ( pcestim.rec.TypeOper == 38 )
          if ( pcestim.rec.TypeComplexOper == 38 )
            nameOper = "Начисление процентов";
          else
            nameOper = "Доначисление процентов";
          end;
        elif ( ( pcestim.rec.TypeOper == 39 ) and ( pcestim.rec.ApplType == 1 ) )
          nameOper = "Списание излишне начисленных процентов";
        elif ( pcestim.rec.TypeOper == 39 )
          nameOper = "Причисление процентов";
        else
          ;
        end;

        sumDebet  = "    -";
        sumCredit = "    -";
        if ( pcestim.rec.TypeOper == 38 )
          sumCredit = String( pcestim.rec.SummaPay_In:9:2:a );
          sumSumCredit = sumSumCredit + pcestim.rec.SummaPay_In;
        else
          sumDebet = String( pcestim.rec.SummaPay_Out:9:2:a );
          sumSumDebet = sumSumDebet + pcestim.rec.SummaPay_Out;
        end;

        [ |##########|      |####################|##########################|#########|#########|            |      -     |##########|]
        ( pcestim.rec.DateOperat:f, nameOper:w, Acc_Form(corrAccount):f, sumDebet, sumCredit, shifrOper );
        [ ----------------------------------------------------------------------------------------------------------------------------];
      end;
      stat = pcestim.Next;
    else
      stat = false;
    end;
  end;

  if ( not wasRecs )
    [ |    -     |      |                    |                          |         |         |            |            |          |];
    [ ----------------------------------------------------------------------------------------------------------------------------];
  end;
  [ |                                                   Итого обороты |#########|#########|            |            |          |]
  ( sumSumDebet:9:2:a, sumSumCredit:9:2:a );
  [ ----------------------------------------------------------------------------------------------------------------------------];

  if ( ( CBRateEstim != 0 ) and ( depositr.rec.Code_Currency != 0 ) )
    sumEqv = Money( outRest * CBRateEstim );
    sumEqv = String( sumEqv:12:2:a );
  else
    sumEqv = "      -";
  end;
  [ |Исходящий остаток                                                |         |         |############|############|          |]
  ( outRest:12:2:a, sumEqv );
  [ ----------------------------------------------------------------------------------------------------------------------------];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro WorkDialogAccEstim ( IP, cmd, id, key )

  var stat = CM_DEFAULT;

  if ( cmd == DLG_KEY )
    /* ENTER -------------------------------------------------------- */
    if ( key == 13 )
      ;
    /* ESC ---------------------------------------------------------- */
    elif ( key == 27 )
      stat = CM_CANCEL;
    /* CtrlF3 ------------------------------------------------------- */
    elif ( key == 352 )
      if ( id == ne_BegDate )
        sc_prd.BeginDate = {curdate};
      elif (id == ne_EndDate)
        sc_prd.EndDate = {curdate};
      end;
    /* F7 ----------------------------------------------------------- */
    elif ( key == 321 )
      stat = CM_SAVE;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/
macro ACC_ESTM ( Referenc )

  depositr.Clear;
  depositr.rec.Referenc = Referenc;
  if ( not depositr.GetEQ )
    MsgBox( "Не найден счет вкладчика с референсом " + String( Referenc ) );
    Exit( 1 );
  end;

  if ( depositr.rec.Code_Currency != 0 )
    if ( not GetAllCurrRate( {curdate}, depositr.rec.Code_Currency, CBRateEstim, BuyRateEstim, SellRateEstim ) )
      CBRateEstim = 0.0;
    end;
  end;

  if ( RunDialog( sc_prd, "WorkDialogAccEstim" ) )
    OpenDepFiles( );
    PrintHeadReport( Referenc );
    GetTrue(NeedRestOnCurDate,"Отображать информацию о сумме начисленных процентов на текущую дату ?");
    PrintEsimDocs( Referenc );
    PrintBottomReport( Referenc );
    CloseDepFiles( );
  else
    Exit( 1 );
  end;

end;
