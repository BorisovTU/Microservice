
/* Сторнирование пролонгации */

import deprintr, rsd;

record ALG_DEPOSITOR ("depositr.dbt");  //счета вкладчиков
record ALG_SBDEPDOC  ("sbdepdoc.dbt");  //Документы по вкладам СБ РФ
record ALG_PARAM     ("sb_algop.dbt");  //Описание алгоритма операции

Private file SB_DTYP           ("sb_dtyp.dbt") key 2;   //Виды вкладов для СБ РФ
Private file SBDEPDOC_stornprl ("sbdepdoc.dbt")key 1;   //Документы по вкладам СБ РФ

Private record W_SBDEPDOC    ("sbdepdoc.1");   //докуметы по вкладам
Private file   fdoc          ("sbdepdoc.dbt"); //Документы по вкладам СБ РФ
Private file   fdep          ("depositr.dbt")write key 7; //счета вкладчиков

const
  OK_RES   =  0,  /* продолжать проводку */
  SKIP_RES =  1,  /* пропустить выполнение предопределенного модуля */
  END_RES  =  2,  /* завершить проводку */
  ERR_RES  = -1;  /* аваpийно завершить проводку */

const
  UPDATE_ALL  = 1,
  UPDATE_DEP  = 2,
  UPDATE_DOC  = 4;

ALG_RESULT = ERR_RES;



/***********************************************************/
macro RollbackDates1 (DEP_REC) //вход- запись счетов вкладчиков

  var
    PrevDate=Date(0,0,0),
    TmpDate=Date(0,0,0);
  var  stat = True;
  var  i = 0;

  var cmdSbDepDoc = RsdCommand;
  var rsSbDepDoc = RsdRecordset( cmdSbDepDoc );

  var cmdSbDTyp = RsdCommand ;
  var rsSbDTyp = RsdRecordset( cmdSbDTyp );

  var env;

  if(DEP_REC.Prol_DateDep > Date( 0, 0, 0 ))        //если дата пролонгации депозитного договора >0
   DEP_REC.End_DateDep = DEP_REC.Prol_DateDep - 1;  //дата окончания депозитного договора = 
  end;

  if ( (ALG_SBDEPDOC.ApplType != 6)  /* Если это была обработка не после отсрочки- */
   AND (ALG_SBDEPDOC.ApplType != 7)) /* обрабатываем как раньше */
/*  Поиск даты начала договора по документам */
    if (ALG_DEPOSITOR.Type_Account == "Целев. дети")
      PrevDate = Date(0,0,0);
    else
      PrevDate = DEP_REC.Limit_Date;//  дата, до которой операции не пров.
    end;

    cmdSbDepDoc.CmdText=
        " SELECT "+
        " t_Referenc ,   "+
        " t_TypeOper , "+
        " t_TypeComplexOper ,  "+
        " t_DepDate_Document  "+
        " FROM dsbdepdoc_dbt WHERE t_Referenc = ? AND t_TypeOper = ?  AND t_DepDate_Document <= ? " 
      ;

    cmdSbDepDoc.addParam("",RSDBP_IN,(DEP_REC.Referenc) );    //референс
    cmdSbDepDoc.addParam("",RSDBP_IN,72);    //тип операции
    cmdSbDepDoc.addParam("",RSDBP_IN,(DEP_REC.Prol_DateDep - 2));      //дата документа в карточке вкладчика = дата окончания депозитного договора-2

    cmdSbDepDoc.execute;       
   
    env = cmdSbDepDoc.connection.environment;
    i = 0;
    while( i<env.ErrorCount )
        [error ? ##: #####################################](i,env.error(i).descr);
        i=i+1;
    end;
  
    //-----------------------------------------------------------
    while ( rsSbDepDoc.MoveNext) //rsMovePrev  //st         //если запись найдена  и       
      if (rsSbDepDoc.Fld("t_TypeComplexOper").Value == 82)     //если  код составной операции ==82
        DtTmSplit(rsSbDepDoc.Fld("t_DepDate_Document").Value,PrevDate);    //   дата = дата документа в карточке вкладчика
      elif (rsSbDepDoc.Fld("t_DepDate_Document").Value <= DEP_REC.Limit_Date) //если дата документа в карточке вкладчика <=  дата, до которой операции не пров.

        if (ALG_DEPOSITOR.Type_Account == "Целев. дети")  //счета вкладчиков.тип счёта = целевой дети
          PrevDate = Date(0,0,0);                         //дата = 0
        else
          PrevDate = DEP_REC.Limit_Date;                  //дата = дата, до которой операции не пров.
        end;
      end;
    end;//while 

    if ( PrevDate <= DEP_REC.Start_DateDep + 1 )  //если дата <= дата начала депозитного договора
      DEP_REC.Prol_DateDep = Date (0,0,0);  //дата окончания депозитного договора = 0
    else 
      DEP_REC.Prol_DateDep = PrevDate + 1;  //дата окончания депозитного договора = дата+1
    end;
    //-----------------------------------------------------------

  else /* Обработка после отсрочки- сроки договоров не меняются */

    cmdSbDTyp.CmdText=
      " SELECT "+
      " t_FlagCur, "+
      " t_Kind, "+
      " t_FormContr, "+
      " t_NumberProl, "+
      " t_TermType "+
      " FROM dsb_dtyp_dbt "+
      " WHERE "+
      " t_FlagCur = ? AND t_Kind = ? " //форма протокола
    ;

    cmdSbDTyp.addParam("",RSDBP_IN,DEP_REC.IsCur);// признак принадлежности валюты 
    cmdSbDTyp.addParam("",RSDBP_IN,DEP_REC.Type_Account);//тип вклада

    cmdSbDTyp.execute;  // должен вернуть 1 запись

    env = cmdSbDTyp.connection.environment;
    i = 0;
    while( i<env.ErrorCount )
           [error ? ##: #####################################](i,env.error(i).descr);
         i=i+1;
    end;

    if( rsSbDTyp.RecCount==1 AND rsSbDTyp.MoveNext )

      if (  (rsSbDTyp.Fld("t_FormContr").Value == 30)         //форма протокола
            AND 
            (DEP_REC.NumberProl > rsSbDTyp.Fld("t_NumberProl").Value )    //количество пролонгаций
         )
        DEP_REC.NumberProl = DEP_REC.NumberProl - rsSbDTyp.Fld("t_NumberProl").Value; //декремент кол-ва пролонгаций
      else
        if ( (rsSbDTyp.Fld("t_TermType").Value == StrFor(1))  /* Плавающий срок- для предыдущего договора срок может быть иной */
         AND (DEP_REC.Prol_DateDep > Date(0,0,0)) )
/*        Поиск даты начала договора по документам */
          TmpDate  = DEP_REC.Prol_DateDep;
          PrevDate = Date(0,0,0);

          cmdSbDepDoc.CmdText=
            " SELECT   "+
            " t_TypeComplexOper ,  "+
            " t_DepDate_Document ,    "+
            " FROM dsbdepdoc_dbt  "+
            " WHERE  "+
            " t_Referenc = ? AND  "+
            " t_TypeOper = ? AND  "+
            " t_DepDate_Document <= ? "
          ;
                                                                          
          cmdSbDepDoc.addParam("",RSDBP_IN,DEP_REC.Referenc); //референсы 
          cmdSbDepDoc.addParam("",RSDBP_IN,72); //тип операции 
          cmdSbDepDoc.addParam("",RSDBP_IN,(DEP_REC.Prol_DateDep - 2));

          cmdSbDepDoc.execute;
      
          env = cmdSbDepDoc.connection.environment;
          i = 0;
          while( i<env.ErrorCount )
              [error ? ##: #####################################](i,env.error(i).descr);
               i=i+1;
          end;

          while ( rsSbDepDoc.MoveNext )//MovePrev //st
            if (rsSbDepDoc.Fld("t_TypeComplexOper").Value == 82)     //если  код составной операции ==82
              PrevDate = rsSbDepDoc.Fld("t_DepDate_Document").Value; //дата = дата документа в карточке вкладчика
            elif (rsSbDepDoc.Fld("DepDate_Document").Value <= DEP_REC.Limit_Date)   //дата документа в карточке вкладчика  <= дата, до которой операции не пров.
              PrevDate = DEP_REC.Limit_Date; //дата = дата, до которой операции не пров.
            end;
          end;//while

          if (PrevDate == Date(0,0,0))
            PrevDate = DEP_REC.Start_DateDep; //дата начала депозитного договора
          end;
          DEP_REC.Term     = TmpDate - PrevDate - 1;
          DEP_REC.KindTerm = 5; /* Другого предложить не могу */
        end;
      end;
    else
      stat = False;
      MsgBox ("Не найден вид вклада "+DEP_REC.Type_Account);
    end;
  end;
  return stat;
end;
/* ****************************************************** */


macro RollbackDates
  var stat = True;  
  Copy (fdoc,ALG_SBDEPDOC);//копирование в запись fdoc записи ALG_SBDEPDOC

  SetRecordAddr(W_SBDEPDOC,fdoc,0,0,true);//накладывание записи на файл документов СБ РФ fdoc 

  if ( (ALG_SBDEPDOC.TypeOper == 51)         /* Открытие переводом */ //код операции == 51
   AND (ALG_SBDEPDOC.TypeComplexOper == 81)                           //код составной операции ==81
   AND (W_SBDEPDOC.CorAcc_Receiver != ALG_DEPOSITOR.Type_Account) )   // корсчёт получателя внешний != типу вклада

    fdep.FNCash  = ALG_DEPOSITOR.FNCash;          //значения для ключа 7
    fdep.Account = W_SBDEPDOC.Account_Receiver;

    if (GetEQ (fdep))//поиск эквивалентной записи по ключу 7
      stat = RollbackDates1 (fdep);//откат для счетов вкладчиков
      if (stat)
        stat = Update (fdep);//обновление найденной записи в файле счетов вкладчиков
        if (NOT stat)
          MsgBox ("Ошибка при обновлении записи по счету- корреспонденту");
        end;
      end;

    else
      MsgBox ("Не найден счет корреспондента "+ W_SBDEPDOC.Account_Receiver);
      stat = False;
    end;

  else
    stat = RollbackDates1 (ALG_DEPOSITOR);//откат для счётов вкладчиков (запись)
  end;

  return stat;
end;
/* ****************************************************** */


var
    Storn,
    Stat=0,
    FlagStorn=ALG_SBDEPDOC.FlagStorn;

  if ( ALG_PARAM.PrmC )
    Storn = true;
  else
    Storn = false;
  end;

    Stat = DeleteDocument( ALG_SBDEPDOC.iApplicationKind,          //удаление документа из таблицы документов СБ РФ
                           ALG_SBDEPDOC.ApplicationKey, Storn );

  if ( Stat )
    UpdateAlgRecords( UPDATE_DEP ); 
// #190060 - так как уже есть логирование записи депозитора, то никаких пересчетов дат не надо.
//    if ( not FlagStorn )
//       Stat = RollbackDates;
//    end;
  end; 

  if ( Stat )
    ALG_UPDATE = UPDATE_DEP;
    ALG_RESULT = END_RES;
  end;
end;
