/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Проверка необходимости взятия комиссии при выдаче наличных, ранее       *
*  переведенных и находившихся на счете менее 1 месяца                     *
*                                                                          *
*  Лебедев С.В.                                                            *
*  10.05.2000                                                              *
\**************************************************************************/

import DeprIntr, IsSrvDoc, cur_rate, alg_vars;

private file sb_tarif     ( sb_tarif ) key 0;
private file tarif_plus   ( sb_tarpl ) key 0;
private var  sbdepdoc = TBFile( "sbdepdoc.dbt", "r", 6 );
private file auxinfo      ( auxinfo  ) key 0;
private file depositr_tax ( depositr ) key 8;

var depclnt_tax = TClientList;

private record auxinfo11    ("auxinfo.11");

var SummaOper71ForLastMonth = $0L;
var RestBeforeOper          = $0L;
var SumExchangeForRUR       = $0L;
var SumFromExchangeForRUR   = $0L;
var TaxForExchangeRUR       = $0L;
var RestForAccount          = $0L;
var NalTaxForExchangeRUR    = TRUE;
var Ref_depositr_tax        = 0;
var CBRateDoc,BuyRateDoc,SellRateDoc;
var CBRateTar,BuyRateTar,SellRateTar;
var CBRateTarOpr,BuyRateTarOpr,SellRateTarOpr;

var FNCash      = NumFNCash();
var RealFNCash  = NumRealFNcash();


/**************************************************************************\
|    Определение суммы вклады, которая была приобретена за счет обмена     |
\**************************************************************************/
macro DefSumExchangeForRUR

  var ExchangeSum = $0L;

  auxinfo.Reference = ALG_SBDEPDOC.Referenc;
  auxinfo.InfoType  = 13;
  auxinfo.Date      = Date(0,0,0);

  if (GetEQ(auxinfo))
    SetRecordAddr(auxinfo11,auxinfo,0,0,TRUE);
    ExchangeSum = auxinfo11.SumIn - auxinfo11.SumOut;
  end;

  return ExchangeSum;

end;


/**************************************************************************\
|    Определение суммы вклады, которая была приобретена за счет обмена     |
\**************************************************************************/
macro DefRestForAccount

  var RestAccount = $0L;
  var stat;

  sbdepdoc.rec.Referenc      = ALG_SBDEPDOC.Referenc;
  sbdepdoc.rec.Date_Document = Date(31,12,9999);
  sbdepdoc.rec.NumDayDoc     = 0;
  stat = sbdepdoc.GetLT;
  while (stat AND (sbdepdoc.rec.Referenc == ALG_SBDEPDOC.Referenc))
    if ( IsServDoc( sbdepdoc.rec ) and ( sbdepdoc.rec.FlagStorn == "" ) )
      RestAccount = sbdepdoc.Rest;
      stat = false;
    else
      stat = sbdepdoc.Prev;
    end;
  end;

  return RestAccount;

end;


/**************************************************************************\
|         Вставка документа на налог по безналичной покупке валюты         |
\**************************************************************************/
macro InsertDocToDDListForExchangeFromRUR

  var stat = TRUE;

  record sbdepdoc_dop (sbdepdoc);
  record sbdepdoc_tax (sbdepdoc);
  record pay_doc      (pay_doc );

  if (stat AND (SumFromExchangeForRUR > 0))
    if (ALG_SBDEPDOC.OutSum - SumFromExchangeForRUR <= 0)
      ALG_SBDEPDOC.Source = 13;
      ALG_UPDATE = 4;
    else
      Copy(sbdepdoc_dop,ALG_SBDEPDOC);
      sbdepdoc_dop.OutSum = SumFromExchangeForRUR;
      sbdepdoc_dop.KindOp = 2;
      sbdepdoc_dop.Source = 13;
      if (sbdepdoc_dop.ApplicationKey != "")
        sbdepdoc_dop.iApplicationKind = 1;
        sbdepdoc_dop.ApplicationKey   = FormApplicationKey(1);
      end;
      if (InsertDocToGDDList(sbdepdoc_dop))
        ALG_SBDEPDOC.OutSum = ALG_SBDEPDOC.OutSum - SumFromExchangeForRUR;
        ALG_SBDEPDOC.Source = 0;
        ALG_UPDATE = 4;
      else
        stat = FALSE;
      end;
    end;
  end;

  if (stat AND (TaxForExchangeRUR > 0))
    if (NalTaxForExchangeRUR)
      ClearRecord(pay_doc);
      pay_doc.FNCash           = RealFNCash;
      pay_doc.IsCur            = ALG_SBDEPDOC.IsCur;
      pay_doc.NumOpert         = 12;
      pay_doc.GroupOpert       = 21; /* Комиссия */
      pay_doc.Date_Document    = ALG_SBDEPDOC.Date_Document;
      pay_doc.CodCur           = 0;
      pay_doc.Oper             = {oper};
      pay_doc.InSum            = TaxForExchangeRUR;
      pay_doc.iApplicationKind = 200;
      pay_doc.ApplicationKey   = FormApplicationKey(200);
      pay_doc.Ground           = "Налог на б/н продажу валюты при выдаче со счета " + Acc_Form(ALG_DEPOSITOR.Account);
      if ( depclnt_tax.GetRecord( ALG_SBDEPDOC.CodClient ) )
        pay_doc.ClientLastName   = depclnt_tax.CurRec.rec.Name1;
        pay_doc.ClientFirstName  = depclnt_tax.CurRec.rec.Name2;
        pay_doc.ClientSecondName = depclnt_tax.CurRec.rec.Name3;
        pay_doc.FlagRez          = depclnt_tax.CurRec.rec.NotResident;
        pay_doc.CodClient        = depclnt_tax.CurRec.rec.CodClient;
      end;
      stat = InsertPayDocToGDDList(pay_doc);
    else
      ClearRecord(sbdepdoc_tax);
      sbdepdoc_tax.Referenc         = depositr_tax.Referenc;
      sbdepdoc_tax.IsCur            = depositr_tax.IsCur;
      sbdepdoc_tax.FNCash           = depositr_tax.FNCash;
      sbdepdoc_tax.Type_Account     = depositr_tax.Type_Account;
      sbdepdoc_tax.Account          = depositr_tax.Account;
      sbdepdoc_tax.Code_Currency    = depositr_tax.Code_Currency;
      sbdepdoc_tax.CodClient        = depositr_tax.CodClient;
      sbdepdoc_tax.Oper             = {oper};
      sbdepdoc_tax.YesSbook         = "X";
      sbdepdoc_tax.Date_Document    = ALG_SBDEPDOC.Date_Document;
      sbdepdoc_tax.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
      sbdepdoc_tax.TypeOper         = 62;
      sbdepdoc_tax.TypeComplexOper  = ALG_SBDEPDOC.TypeComplexOper;
      sbdepdoc_tax.ApplType         = 18;
      sbdepdoc_tax.KindOp           = 2;
      sbdepdoc_tax.OutSum           = TaxForExchangeRUR;
      sbdepdoc_tax.Author           = 0;
      sbdepdoc_tax.iApplicationKind = 1;
      sbdepdoc_tax.ApplicationKey   = FormApplicationKey(1);
      sbdepdoc_tax.Ground           = "Налог на б/н прод.вал.при выдаче с " + Acc_Form(ALG_DEPOSITOR.Account);
      stat = InsertDocToGDDList(sbdepdoc_tax);
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                   Переведены ли деньги из чужого банка                   |
\**************************************************************************/
macro IsInSumFromAlienBank

  var flagAlienBank = FALSE;

  if   ( sbdepdoc.rec.TypeOper == 51 )
    if ( sbdepdoc.rec.ApplType == 25 )
      flagAlienBank = TRUE;
    end;
  elif ( sbdepdoc.rec.TypeOper == 71 )
    if ( sbdepdoc.rec.ApplType == 25 )
      flagAlienBank = TRUE;
    end;
  elif ( sbdepdoc.rec.TypeOper == 73 )
    if ( sbdepdoc.rec.ApplType == 25 )
      flagAlienBank = TRUE;
    end;
  end;

  return flagAlienBank;

end;


/**************************************************************************\
|                    Определение даты месячной давности                    |
\**************************************************************************/
macro DefOneMonthAgoDate ( InputDate )

  var   OutputDate  = Date( 0, 0, 0 );
  var   TmpDate     = InputDate;
  var   d,m,y;
  const OneMonthAgo = 1;

  DateSplit( TmpDate, d, m, y );

  if ( m - OneMonthAgo <= 0 )
    m = m + 12;
    y = y - 1;
  end;

  m = m - OneMonthAgo;

  if ( (m == 4 ) or ( m == 6 ) or ( m == 9 ) or ( m == 11 ) )
    if ( d > 30 )
      d = 30;
    end;
  elif ( m == 2 )
    if ( ( y/4.0 == Int( y/4.0 ) ) and ( y != 2100 ) )
      if ( d > 29 )
        d = 29;
      end;
    else
      if ( d > 28 )
        d = 28;
      end;
    end;
  end;

  OutputDate = Date( d, m, y );

  return OutputDate;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  var statTmp;
  var ParamForChange = 0;
  var tm;
  var summaPlus = $0L;
  var dtRate,
      tmRate;

  PrepareDateTimeForRates( true, ALG_SBDEPDOC.Date_Document, null, dtRate, tmRate );

  ALG_RESULT = OK_RES;
  ALG_PARAM.prmD = 0;

  /* Поиск тарифа */
  if (ALG_RESULT == OK_RES)
    if (ALG_PARAM.prmI <= 0)
      ALG_RESULT = SKIP_RES;
    end;
    /* Поиск тарифа перенесен ниже */
  end;

  /* Вычисление суммы переведенных средств и находившихся на счете менее месяца */
  if ( ALG_RESULT == OK_RES )
    sbdepdoc.addFilter( "T_Referenc = " + String( ALG_SBDEPDOC.Referenc ) );
    sbdepdoc.Clear;
    sbdepdoc.rec.Referenc      = ALG_SBDEPDOC.Referenc;
    sbdepdoc.rec.Date_Document = DefOneMonthAgoDate(ALG_SBDEPDOC.Date_Document);
    sbdepdoc.rec.NumDayDoc     = 0;

    statTmp = sbdepdoc.GetGE;

    while ( statTmp )
      if ( ( sbdepdoc.rec.Referenc      == ALG_SBDEPDOC.Referenc      ) and
           ( sbdepdoc.rec.Date_Document <= ALG_SBDEPDOC.Date_Document )    )
        if ( ( IsServDoc( sbdepdoc.rec ) ) and ( sbdepdoc.rec.FlagStorn == "" ) )
          if ( IsInSumFromAlienBank( ) )
            SummaOper71ForLastMonth = SummaOper71ForLastMonth + sbdepdoc.rec.InSum;
          elif ( sbdepdoc.rec.OutSum > 0 )
            if ( ( SummaOper71ForLastMonth > 0 ) and ( sbdepdoc.rec.Rest < SummaOper71ForLastMonth ) )
              if ( sbdepdoc.rec.Rest + sbdepdoc.rec.OutSum < SummaOper71ForLastMonth )
                SummaOper71ForLastMonth = SummaOper71ForLastMonth - sbdepdoc.rec.OutSum;
              else
                SummaOper71ForLastMonth = SummaOper71ForLastMonth - ( SummaOper71ForLastMonth - sbdepdoc.rec.Rest );
              end;
            end;
          end;
        end;
        statTmp = sbdepdoc.Next;
      else
        statTmp = false;
      end;
    end;
    sbdepdoc.dropFilter( );
  end;

  /* Определение текущего остатка */
  if ( ( ALG_RESULT == OK_RES) and ( SummaOper71ForLastMonth != 0 ) )
    sbdepdoc.addFilter( "T_Referenc = " + String( ALG_SBDEPDOC.Referenc ) );
    sbdepdoc.Clear;
    sbdepdoc.rec.Referenc      = ALG_SBDEPDOC.Referenc;
    sbdepdoc.rec.Date_Document = ALG_SBDEPDOC.Date_Document + 1;
    sbdepdoc.rec.NumDayDoc     = 0;

    statTmp = sbdepdoc.GetLT;

    while ( statTmp )
      if ( sbdepdoc.rec.Referenc == ALG_SBDEPDOC.Referenc )
        if ( IsServDoc( sbdepdoc.rec ) and ( sbdepdoc.rec.FlagStorn == "" ) )
          RestBeforeOper = sbdepdoc.rec.Rest;
          statTmp = false;
        else
          statTmp = sbdepdoc.Prev;
        end;
      else
        statTmp = false;
      end;
    end;
    sbdepdoc.dropFilter( );
  end;

  if ( ( ALG_RESULT == OK_RES ) and ( SummaOper71ForLastMonth == 0 ) )
    ALG_RESULT = SKIP_RES;
  end;

  /* Поиск тарифа */
  if ( ALG_RESULT == OK_RES )
    sb_tarif.IsCur    = ALG_SBDEPDOC.IsCur;
    sb_tarif.NumTarif = ALG_PARAM.prmI;
    if ( GetEQ( sb_tarif ) )

      if ( ALG_SBDEPDOC.Code_Currency != sb_tarif.CodCur )
        if ( ALG_SBDEPDOC.Code_Currency == 0 )
          CBRateDoc = 1.0L;
        else
          tm = null;
          if( ALG_SBDEPDOC.ApplicationKey != "" )
            tm = Time( int( substr( ALG_SBDEPDOC.ApplicationKey, 11, 2 ) ),
                       int( substr( ALG_SBDEPDOC.ApplicationKey, 13, 2 ) ),
                       int( substr( ALG_SBDEPDOC.ApplicationKey, 15, 2 ) ) );
          end;
          statTmp = GetAllCurrRate(dtRate,ALG_SBDEPDOC.Code_Currency,CBRateDoc,BuyRateDoc,SellRateDoc,tmRate);
          if ( not statTmp )
            MsgBox("Не найден курс валюты документа");
            ALG_RESULT = ERR_RES;
          end;
        end;

        if ( sb_tarif.CodCur == 0 )
          CBRateTar = 1.0L;
        else
          tm = null;
          if( ALG_SBDEPDOC.ApplicationKey != "" )
            tm = Time( int( substr( ALG_SBDEPDOC.ApplicationKey, 11, 2 ) ),
                       int( substr( ALG_SBDEPDOC.ApplicationKey, 13, 2 ) ),
                       int( substr( ALG_SBDEPDOC.ApplicationKey, 15, 2 ) ) );
          end;
          statTmp = GetAllCurrRate(dtRate,sb_tarif.CodCur,CBRateTar,BuyRateTar,SellRateTar,tmRate);
          if ( not statTmp )
            MsgBox("Не найден курс валюты тарифа");
            ALG_RESULT = ERR_RES;
          end;
        end;

        if ( sb_tarif.CodCurOpr == sb_tarif.CodCur )
          CBRateTarOpr = CBRateTar;
        else
          tm = null;
          if( ALG_SBDEPDOC.ApplicationKey != "" )
            tm = Time( int( substr( ALG_SBDEPDOC.ApplicationKey, 11, 2 ) ),
                       int( substr( ALG_SBDEPDOC.ApplicationKey, 13, 2 ) ),
                       int( substr( ALG_SBDEPDOC.ApplicationKey, 15, 2 ) ) );
          end;
          statTmp = GetAllCurrRate( dtRate, sb_tarif.CodCurOpr, CBRateTarOpr, BuyRateTarOpr,  SellRateTarOpr, tmRate );
          if ( not statTmp )
            MsgBox("Не найден курс валюты операции тарифа");
            ALG_RESULT = ERR_RES;
          end;
        end;
      end;

      if ( sb_tarif.TarifPlus and ( ALG_RESULT == OK_RES ) )

        if (ALG_PARAM.prmD2 > RestBeforeOper - SummaOper71ForLastMonth)      
          if (RestBeforeOper < SummaOper71ForLastMonth)
            summaPlus = ALG_PARAM.prmD2;
          else
            summaPlus = ALG_PARAM.prmD2 - (RestBeforeOper - SummaOper71ForLastMonth);
            if ( summaPlus < 0 )
              summaPlus = 0;
            end;
          end;
        end;

        if ( ( summaPlus != 0 ) and ( ALG_SBDEPDOC.Code_Currency != sb_tarif.CodCurOpr ) )
          summaPlus = MoneyL( summaPlus * CBRateDoc / CBRateTarOpr );
        end;

        tarif_plus.IsCur    = ALG_SBDEPDOC.IsCur;
        tarif_plus.NumTarif = ALG_PARAM.prmI;
        tarif_plus.Date     = ALG_SBDEPDOC.DepDate_Document;
        tarif_plus.SumOper  = summaPlus;
        statTmp = GetLE( tarif_plus );

        if ( statTmp  AND
             ( tarif_plus.IsCur    == ALG_SBDEPDOC.IsCur )  AND
             ( tarif_plus.NumTarif == ALG_PARAM.prmI ) )

          while ( statTmp  AND
                  ( tarif_plus.IsCur    == ALG_SBDEPDOC.IsCur )  AND
                  ( tarif_plus.NumTarif == ALG_PARAM.prmI )        AND
                  ( tarif_plus.Date     <= ALG_SBDEPDOC.DepDate_Document ) )
            if ( tarif_plus.SumOper <= summaPlus )
              statTmp = FALSE;
            else
              statTmp = Prev( tarif_plus );
            end;
          end;

          if ( ( tarif_plus.IsCur    == ALG_SBDEPDOC.IsCur )  AND
               ( tarif_plus.NumTarif == ALG_PARAM.prmI )        AND
               ( tarif_plus.Date     <= ALG_SBDEPDOC.DepDate_Document ) )

            sb_tarif.Persent = tarif_plus.Percent;
            sb_tarif.MinSum  = tarif_plus.MinSum;
            sb_tarif.MaxSum  = tarif_plus.MaxSum;
            sb_tarif.Summa   = tarif_plus.Summa;

          end;
        end;
      end;

      /* Конвертация сумм */
      if ( ( ALG_RESULT == OK_RES ) and ( ALG_SBDEPDOC.Code_Currency != sb_tarif.CodCur ) )
        if (sb_tarif.Summa != 0)
          sb_tarif.Summa = MoneyL(sb_tarif.Summa * CBRateTar / CBRateDoc);
        end;
        if (sb_tarif.MinSum != 0)
          sb_tarif.MinSum = MoneyL(sb_tarif.MinSum * CBRateTar / CBRateDoc);
        end;
        if (sb_tarif.MaxSum != 0)
          sb_tarif.MaxSum = MoneyL(sb_tarif.MaxSum * CBRateTar / CBRateDoc);
        end;
      end;

    else
      MsgBox("Не найден тариф номер " + String(ALG_PARAM.prmI));
      ALG_RESULT = ERR_RES;
    end;
  end;

  /* Вычисление суммы налога */
  if ((ALG_RESULT == OK_RES) AND (SummaOper71ForLastMonth != 0))
    if (ALG_PARAM.prmD2 > RestBeforeOper - SummaOper71ForLastMonth)      
      if (RestBeforeOper < SummaOper71ForLastMonth)
        ALG_PARAM.prmD = ALG_PARAM.prmD2;
      else
        ALG_PARAM.prmD = ALG_PARAM.prmD2 - (RestBeforeOper - SummaOper71ForLastMonth);
        if (ALG_PARAM.prmD < 0)
          ALG_PARAM.prmD = 0;
          ALG_RESULT = SKIP_RES;
        end;
      end;
      if (ALG_RESULT == OK_RES)
        ALG_PARAM.prmD = MoneyL(ALG_PARAM.prmD * sb_tarif.Persent / 1000000 + sb_tarif.Summa);
        if ((ALG_PARAM.prmD < sb_tarif.MinSum) AND (sb_tarif.MinSum != 0))
          ALG_PARAM.prmD = sb_tarif.MinSum;
        end;
        if ((ALG_PARAM.prmD > sb_tarif.MaxSum) AND (sb_tarif.MaxSum != 0))
          ALG_PARAM.prmD = sb_tarif.MaxSum;
        end;
        ALG_PARAM.prmD = round( ALG_PARAM.prmD, 2 );
      end;
    else
      ALG_RESULT = SKIP_RES;
    end;
  end;

  /* Для валютного документа может быть плата за покупку валюты */
  if ( ( ALG_RESULT                 != ERR_RES            ) and 
       ( ALG_SBDEPDOC.IsCur         == 1                  ) and
       ( ALG_SBDEPDOC.Date_Document <  Date( 1, 1, 2003 ) ) and
       ( ALG_PARAM.prmD2            >  0                  )    )
    SumExchangeForRUR = DefSumExchangeForRUR( );
    if ( SumExchangeForRUR > 0 )
      RestForAccount = DefRestForAccount( );
/*
      SumFromExchangeForRUR = ALG_PARAM.prmD2 - (RestForAccount - SumExchangeForRUR);
*/
      SumFromExchangeForRUR = SumExchangeForRUR;
      if (SumFromExchangeForRUR > 0)
        tm = null;
        if( ALG_SBDEPDOC.ApplicationKey != "" )
          tm = Time( int( substr( ALG_SBDEPDOC.ApplicationKey, 11, 2 ) ),
                     int( substr( ALG_SBDEPDOC.ApplicationKey, 13, 2 ) ),
                     int( substr( ALG_SBDEPDOC.ApplicationKey, 15, 2 ) ) );
        end;
        statTmp = GetAllCurrRate(dtRate,ALG_SBDEPDOC.Code_Currency,CBRateDoc,BuyRateDoc,SellRateDoc,tmRate,SumFromExchangeForRUR);
        if ( not statTmp )
          MsgBox("Не найден курс валюты документа");
          ALG_RESULT = ERR_RES;
        end;
        if (ALG_RESULT != ERR_RES)
          TaxForExchangeRUR = Money(SumFromExchangeForRUR * SellRateDoc);
          sb_tarif.IsCur    = ALG_SBDEPDOC.IsCur;
          sb_tarif.NumTarif = 16;
          if (NOT GetEQ(sb_tarif))
            MsgBox("Не найден тариф номер 16");
            ALG_RESULT = ERR_RES;
          end;
        end;
        if (ALG_RESULT != ERR_RES)
          TaxForExchangeRUR = MoneyL(TaxForExchangeRUR * sb_tarif.Persent / 10000);
          TaxForExchangeRUR = round( TaxForExchangeRUR, 2 );
        end;
        if ((ALG_RESULT != ERR_RES) AND (TaxForExchangeRUR > 0))
/*          if (GetTrue(TRUE,"Взять налог " + Trim(String(TaxForExchangeRUR:10:2:a)) + " рублей за безналичную конвертацию ?"))*/
          if (GetTrue(TRUE,"Взять налог " +
                            Trim(String(TaxForExchangeRUR:10:2:a)) +
                            " рублей | {исх сумма:" +
                            Trim(String(SumFromExchangeForRUR:10:2:a)) +
                            " по курсу:"+
                            Trim(String(SellRateDoc:10:2:a)) +
                            "}| за безналичную конвертацию ?"))
            if (GetTrue(TRUE,"Взять налог за безналичную конвертацию налично ?"))
              NalTaxForExchangeRUR = TRUE;
            else
              NalTaxForExchangeRUR = FALSE;
              if (FNcash != RealFNcash)
                /* Идет проводка по счету чужого филиала. Значит, для выбора
                   счета даем возможность сменить филиал, одновременно открыв
                   поле вкладчика для его изменения (поиска по другому филиалу-
                   там тот же вкладчик имеет другой CodClient */
                ParamForChange = 256   /* Поле филиала   */
                                + 64;  /* Поле вкладчика */
              else
                ParamForChange = 0;
              end;
              Ref_depositr_tax = PanelAccountForSelect( "", 0, 1, 1, ALG_COMDEPCLNT.CurRec.rec.CodClient, ParamForChange, 3, FNCash );
              if (Ref_depositr_tax == 0)
                MsgBox("Не выбран счет для взятия налога за безналичную конвертацию");
                ALG_RESULT = ERR_RES;
              else
                depositr_tax.Referenc = Ref_depositr_tax;
                if (NOT GetEQ(depositr_tax))
                  MsgBox("Не найден счет для взятия налога за безналичную конвертацию");
                  ALG_RESULT = ERR_RES;
                end;
              end;
            end;
          else
            MsgBox("Нельзя продолжать операцию без взятия налога за безналичную конвертацию");
            ALG_RESULT = ERR_RES;
          end;
        end;
        if (ALG_RESULT != ERR_RES)
          if (NOT InsertDocToDDListForExchangeFromRUR())
            ALG_RESULT = ERR_RES;
          end;
        end;
      end;
    end;
  end;

end;
