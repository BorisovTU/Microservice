
/* Отчет по перенумерации счетов */

import deprintr;

file Dep("depositr.dbt") key 7;
file DepHistory("dephistr.dbt") key 5;

var
  i = 0,
  ClientList = TClientList, 
  Branch = NumFNCash;

macro FindDepAcc(Account)

  Dep.FNCash = Branch;
  Dep.Account = Account;
  return GetEQ(Dep);
end;

macro FindDepClient(ClientCode)

  return ClientList.GetRecord( ClientCode );
end;

macro OutHead

  [ ];
  [##########################################################################################################################]
  ("Таблица соответствия номеров лицевых счетов":c);
  [##########################################################################################################################]
  ("в связи с переходом на новый план счетов":c);
  [ ];
  [--------------------------------------------------------------------------------------------------------------------------];
  [|       |   Дата   |                                   |         Номера         |   Номер лицевого счета  |              |];
  [| № п/п | открытия |        Ф. И. О.  вкладчика        |       действующих      |     по новому плану     |  Примечание  |];
  [|       |  счета   |                                   |         счетов         |         счетов          |              |];
  [--------------------------------------------------------------------------------------------------------------------------];
end;

macro OutBottom

  [--------------------------------------------------------------------------------------------------------------------------];
  [ ];
  [ ];
  [             Бухгалтер                                    М.П. ];
  [ ];
  [             Главный бухгалтер ];
  [ ];
  [             "___" ______________ ____ г. ];
end;

macro OutLine

  var
    ClientName = "",
    OpenDate = "";

  if(FindDepAcc(DepHistory.Account))
    if((Dep.Open_Close != StrFor(0))
      or (Dep.Type_Account == "Служебный")
      or (Dep.Type_Account == "Неподвижный")
      or (Dep.Type_Account == "Объединенный"))
      return;
    end;
    OpenDate = String(Dep.Open_Date);
    if(FindDepClient(Dep.CodClient))
      ClientName = ClientList.CurRec.ConvertFIOFull;
    end;
  end;
  i = i + 1;

  [ ####### ########## ################################### ######################## ######################### ]
  (
    i,
    OpenDate,
    ClientName,
    Acc_Form(DepHistory.PrevAccount),
    Acc_Form(DepHistory.Account)
  );
end;

  OutHead;
  Rewind(DepHistory);
  while(Next(DepHistory))
    OutLine;
  end;
  OutBottom;
end;