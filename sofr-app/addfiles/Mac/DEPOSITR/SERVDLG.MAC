/*
  Макрос обработки панели ввода  исходных данных
  RET 12.03.99
*/

import filter;
Import rslscrp;

private var branchID;

const STR_ONLY_OPENED="Только открытые",
      STR_ONLY_CLOSED="Только закрытые",
      STR_ALL="Открытые и закрытые";

Array STR_VALUES;
STR_VALUES[0]=STR_ONLY_OPENED;
STR_VALUES[1]=STR_ONLY_CLOSED;
STR_VALUES[2]=STR_ALL;

const KEY_ENTER=13;
const KEY_ESC=27;
const KEY_F3=317;
const KEY_F9=323;
const KEY_Alt_F11=395;

Record dlg      ( "initpan", "rtusrmac" ) dialog ;
Record dlg_v    ( "initpanv", "rtusrmac" ) dialog ;
Record scr      ( "c_scroll", "rtusrmac" ) dialog ;

const Set_Handler = 32; /* Клавиша обработки ( по умолчанию пробел ) */

macro GetBranchID()
     return branchID;
end;
private macro MyNext( ff )
  var RetVal = next( ff );
  while( RetVal and not Filter( ff ) )
    RetVal = next( ff );
  end;
  return RetVal;
end;

private macro MyPrev( ff )
  var RetVal = prev( ff );
  while( RetVal and not Filter( ff ) )
     RetVal = prev( ff );
  end;
  return RetVal;
end;

private macro SetFilter()
  ReplaceMacro( "next", "MyNext" );
  ReplaceMacro( "prev", "MyPrev" );
end;

private macro DeleteFilter()
  ReplaceMacro( "next" );
  ReplaceMacro( "prev" );
end;


Macro Constructor_Scr()

 var  StatOpen = True ;

 If ( Not ( Open ( Currency )))
     Msgbox( " Файл списка валют не открыт ");
     StatOpen = False;
 end;

 Return StatOpen ;
end;


Macro Do_Scroll ()

 var stat = false ;
 stat = Constructor_Scr();

 If ( stat )
    SetFilter(); /* Переопределение функций Next и Prev*/
    SetScroll ( Currency , "COD" , "ExternalCode" , "NAME" , "Name_Currency");
    RunDialog ( scr , "ScrollMes");
    DeleteFilter(); /* Восстановление исходных значений */
 end;
 Return stat;

end;

macro Do_DepartmentScroll()
      var depname;
      if( ListBranches (branchID, depname) )
             return depname;
      else 
             msgbox("Ошибка выбора филиала");
             return 1;
      end;
end;


/* Макрос инициализации  полей панели диалога */
Macro InitPanel( DATE_C  , Branch  , IsCur )
branchID = Branch;
  If ( IsCur)

      dlg_v.FIO          = "X"    ; /* Инициалы вкладчика            */
      dlg_v.REST_A       = ""     ; /* Остаток из карточки счета     */
      dlg_v.REST_D       = ""     ; /* Остаток за дату               */
      dlg_v.REST_O       = "X"    ; /* Остаток из операции за дату   */
      dlg_v.DATE_REPORT  = DATE_C ; /* Дата отчета                   */
      dlg_v.ALL_KIND     = ""     ; /* Обработка видов вкладов       */
      dlg_v.ALL_BRANCH   = ""     ; /* Обработка филиалов            */
      dlg_v.BRANCH_ID    = Branch ; /* Обработка конкретного филиала */
      dlg_v.INIT_MES     = ""     ; /* Сообщение с указанием на дату */
      dlg_v.ALL_CUR      = ""     ; /* Обработка всех видов валют    */
      dlg_v.CUR_ID       = 840    ; /* Признак конкретной валюты     */
      dlg_v.NAME_CURR    = "Доллары США"     ; /* Название валюты               */

  else

      dlg.FIO          = "X"    ; /* Инициалы вкладчика            */
      dlg.REST_A       = ""     ; /* Остаток из карточки счета     */
      dlg.REST_D       = ""     ; /* Остаток за дату               */
      dlg.REST_O       = "■"    ; /* Остаток из операции за дату   */
      dlg.DATE_REPORT  = DATE_C ; /* Дата отчета                   */
      dlg.ALL_KIND     = ""     ; /* Обработка видов вкладов       */
      dlg.ONLY_SVOD    = "X"    ; /* Обработка видов вкладов       */

      dlg.ALL_BRANCH   = ""     ; /* Обработка филиалов            */
      dlg.BRANCH_ID    = Branch ; /* Обработка конкретного филиала */
      dlg.INIT_MES     = "ВВЕДИТЕ ДАТУ ОТЧЕТА ..."; /* Сообщение с указанием на дату */

      dlg.OPEN_CLOSE=STR_ONLY_OPENED;  /* Открытые или закрытые     */
      dlg.ACC_FIRST_NUM=0;             /* Наименьший короткий номер */
      dlg.ACC_LAST_NUM=9999999;        /* Наибольший короткий номер */
      dlg.FULL_REPORT_FLAG="";         /* Выдавать ли второй отчет  */
 end;
end;


/* Главная продцедура обработки панели  для рублей */
MACRO EvenMacro(dlg,cmd,id,key)
var i;
var stat=CM_DEFAULT;

   if(cmd==DLG_INIT)
      Message("~Tab,Стрелки~-перемещение,~Пробел~-выбор,~F3~-список, ~F9~-принять, ~Esc~-отказаться.");
      SetFocus (dlg,FldIndex(dlg,"FIO"));
      stat=CM_IGNORE;
   elif(cmd==DLG_KEY)
      if((dlg.ACC_FIRST_NUM>dlg.ACC_LAST_NUM) and (key!=KEY_F9))
         if(fldname(dlg,id)=="ACC_LAST_NUM")
               dlg.ACC_FIRST_NUM=dlg.ACC_LAST_NUM;
            else
               dlg.ACC_LAST_NUM=dlg.ACC_FIRST_NUM;
         end;
      end;
      if((key==KEY_ENTER) and (fldname(dlg,id)=="BRANCH_ID"))
         SetFocus(dlg,FldIndex(dlg,"FIO"));   /* Обходим поля сначала */
         stat=CM_IGNORE;
      end;
      if((key==Set_Handler) and (fldname(dlg,id)=="FIO"))
          if(dlg.FIO=="X")
               dlg.FIO="";
            else
               dlg.FIO="X";
          end;
      elif((key==Set_Handler) and (fldname(dlg,id)=="REST_A"))
          dlg.REST_D="";
          dlg.REST_O="";
          dlg.INIT_MES="";
          dlg.REST_A="■";
          SetFocus(dlg,FldIndex(dlg,"ALL_KIND")) ;
      elif((key==Set_Handler) and (fldname(dlg,id)=="REST_D"))
          dlg.REST_A="";
          dlg.REST_O="";
          dlg.INIT_MES="ВВЕДИТЕ ДАТУ ОТЧЕТА ...";
          dlg.REST_D="■";
          SetFocus(dlg,FldIndex(dlg,"DATE_REPORT"));
      elif((key==Set_Handler) and (fldname(dlg,id)=="REST_O"))
          dlg.REST_A="";
          dlg.REST_D="";
          dlg.INIT_MES="ВВЕДИТЕ ДАТУ ОТЧЕТА ...";
          dlg.REST_O="■";
          SetFocus(dlg,FldIndex(dlg,"DATE_REPORT"));
      elif((key==Set_Handler) and (fldname(dlg,id)=="ALL_KIND"))
          if(dlg.ALL_KIND=="X")
               dlg.ALL_KIND="";
            else
               dlg.ALL_KIND="X";
          end;
     elif((key==Set_Handler) and (fldname(dlg,id)=="ONLY_SVOD"))
          if(dlg.ONLY_SVOD=="X")
               dlg.ONLY_SVOD="";
            else
               dlg.ONLY_SVOD="X";
          end;
      elif((key==Set_Handler) and (fldname(dlg,id)=="ALL_BRANCH"))
          if(dlg.ALL_BRANCH=="X")
               dlg.ALL_BRANCH="";
            else
               dlg.ALL_BRANCH="X";
               dlg.BRANCH_ID=0;
          end;
      elif((key==Set_Handler) and (fldname(dlg,id)=="FULL_REPORT_FLAG"))
          if(dlg.FULL_REPORT_FLAG=="X")
               dlg.FULL_REPORT_FLAG="";
            else
               dlg.FULL_REPORT_FLAG="X";
          end;
      elif((key==KEY_F3) and (fldname(dlg,id)=="OPEN_CLOSE"))
          i=Menu(STR_VALUES,"Вид счетов");
          if((i>=0) and (i<=2))
             dlg.OPEN_CLOSE=STR_VALUES[i];
          end;
      elif(key==KEY_ESC)
          stat=CM_CANCEL;
      elif(key==KEY_F9)
          stat=CM_SAVE;
      end;
      UpdateFields(dlg);
   elif(cmd==DLG_REMFOCUS)
      if(dlg.ACC_FIRST_NUM>dlg.ACC_LAST_NUM)
         if(fldname(dlg,id)=="ACC_LAST_NUM")
               dlg.ACC_FIRST_NUM=dlg.ACC_LAST_NUM;
            else
               dlg.ACC_LAST_NUM=dlg.ACC_FIRST_NUM;
         end;
      end;
      UpdateFields(dlg);
   elif(cmd==DLG_SAVE)
      if(dlg.ACC_FIRST_NUM>dlg.ACC_LAST_NUM)
          msgbox("Неправильный диапазон счетов.|"+
                 "Нажмите ESC для коррекции.");
          if(fldname(dlg,id)=="ACC_LAST_NUM")
               dlg.ACC_FIRST_NUM=dlg.ACC_LAST_NUM;
            else
               dlg.ACC_LAST_NUM=dlg.ACC_FIRST_NUM;
          end;
          SetFocus(dlg,FldIndex(dlg,"ACC_FIRST_NUM"));
          UpdateFields(dlg);
          stat=CM_CANCEL;
      end;
   end;
   return stat;
end;



/* Главная продцедура обработки панели  для валюты */
Macro EvenMacro_v ( dlg_v , cmd , id , key )
var bran;
 if ( cmd == DLG_INIT )
      SetFocus ( dlg_v , 1 );
      return CM_IGNORE;

 elif ( cmd == DLG_KEY)
      if (( key == Set_Handler ) and ( id == 0 )) /* FIO */
            if( dlg_v(0) == "X" )
                dlg_v(0) = "";
                UpdateFields ( dlg_v );
            else
                dlg_v(0) = "X";
                UpdateFields ( dlg_v );
            end;

      elif (( key == Set_Handler ) and ( id == 1 ))   /* REST_A */

             if (( dlg_v(2) == "X")  or  ( dlg_v(4) == "X"))
                   dlg_v(2) =  "";
                   dlg_v(4) =  "";
                   dlg_v(8) =  "";
                   dlg_v(11) =  "";
                   UpdateFields ( dlg_v );
             end;

             dlg_v(1) = "X";
             SetFocus( dlg_v , 5 ) ;
             UpdateFields ( dlg_v );

      elif  (( key == Set_Handler ) and ( id == 2 ))  /* REST_D */

             if (( dlg_v(1) == "X")  or  ( dlg_v(4) == "X"))
                   dlg_v(1) =  "";
                   dlg_v(4) =  "";
                   dlg_v(11) =  "ВВЕДИТЕ ДАТУ ОТЧЕТА ...";
                   UpdateFields ( dlg_v );
             end;

             dlg_v(2) = "X";
             SetFocus( dlg_v , 3 );
             UpdateFields ( dlg_v );

      elif   (( key == Set_Handler ) and ( id == 4 ))  /* REST_O */

             if (( dlg_v(2) == "X")  or  ( dlg_v(1) == "X"))
                   dlg_v(1) =  "";
                   dlg_v(2) =  "";
                   dlg_v(11) =  "ВВЕДИТЕ ДАТУ ОТЧЕТА ...";
                   UpdateFields ( dlg_v );
             end;

             dlg_v(4) = "X";
             SetFocus( dlg_v , 3 );
             UpdateFields ( dlg_v );

      elif  (( key == Set_Handler ) and ( id == 5 ))  /* ALL_KIND */
            if( dlg_v(5) == "X" )
                dlg_v(5) = "";
                UpdateFields ( dlg_v );
            else
                dlg_v(5) = "X";
                UpdateFields ( dlg_v );
            end;

     elif   (( key == Set_Handler )  and ( id == 6 ))  /* ALL_BRANCH */

            if( dlg_v(6) == "X" )
                dlg_v(6) = ""         ;
            else
                dlg_v(6)    = "X";
                dlg_v(7)    =  0 ;
            end;
            UpdateFields ( dlg_v );

     elif  (( key == Set_Handler ) and ( id == 8 ))  /* ALL_KIND */
            if( dlg_v(8) == "X" )
                dlg_v(8) = "";
                UpdateFields ( dlg_v );
            else
                dlg_v(8)   = "X";
                dlg_v(9)   =  0 ;
                dlg_v(10)   = "";
                UpdateFields ( dlg_v );
            end;

     elif  (( key == 317 ) and ( id == 9 ))  /* CUR_ID */

           If ( Do_Scroll () )

                dlg_v(8)  = "";
                dlg_v(9)  = Currency./*Code_Currency*/ExternalCode;
                dlg_v(10) = Currency.Name_Currency;
                UpdateFields ( dlg_v );
           else
                dlg_v(8)   = "X" ;
                dlg_v(9)   =  0  ;
                dlg_v(10)  =  "" ;
                UpdateFields ( dlg_v );
          end;

     elif  (( key == 317 ) and ( id == 7 ))  /* BRANCH_ID */
           bran = Do_DepartmentScroll () ;
           dlg_v(6)  = "";
           dlg_v(7)  = bran;
           UpdateFields ( dlg_v );
           
                
          end;

     elif   (( key == 27 )) /* Обработка Esc */
               dlg_v(12) = 1 ;
               UpdateFields ( dlg_v );
               return CM_SAVE;
     end;
 end;
end;

