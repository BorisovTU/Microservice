        
import deprintr, od_comm, "cur_rate.mac", BankInter, Revalue, rsd, chkboss;

file CasAcc  (sb_casac) key 2;
file CasVal  (sb_casvl) key 1;
private file Curr    (currency) key 0;
file F68Tmp  ("f68.tmp")      key 1 write;
record parm( "GetBkAcc.rec" );
private file Alg( "namealg.dbt", "bank.def" ) key 0;

record Tot   ("f68.tmp");

const
  SB_NOTBALANCE = 1000,
  Debug = false;

const
  M_ALL     = 0,    /* Печатать все */
  M_CURR    = 1,    /* Только валюты */
  M_TREAS   = 2;    /* Только ценности */

var
  {RprtDSubj},
  RepMode = 0,
  programID        = IdentProgram;


var
  CurRate,
  PrevRate,
  Recalc;

const
  TxtFileDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true ),  
  TmpFileName = TxtFileDir + "f68" + String({oper}) + ".dbt";


macro GetAlgName( type, number )

  Alg.iTypeAlg = type;
  Alg.iNumberAlg = number;
  if ( GetEQ( Alg ) )
    return Alg.szNameAlg;
  end;
  return "";
end;

macro GetNativeSafe

  file Group("rtgroup.dbt") key 2;

  Group.Branch = Branch;
  Group.Date = {curdate};
  Group.ID = OperBrigade;
  if(GetEQ(Group))
    DeskSubj.rec.Branch = Branch;
    DeskSubj.rec.Name = Group.SafeID;
    if ( not DeskSubj.getEQ ) 
      MsgBox("Не найден сейф бригады № ", OperBrigade);
      Exit( 1 );
    end;   
  else
    MsgBox("Не найден сейф бригады № ", OperBrigade);
    Exit( 1 );
  end;
end;


macro ClearTmpFile
  var cmd;
  cmd = RsdCommand( NULL, string("truncate table df68_tmp") );
  cmd.execute();
end;


/*         RA 07-03-01 нужно пользоваться справочником Retail
macro FindExchCurr

  ExchCurr.ID = Int(Curr.ExternalCode);
  return GetEQ(ExchCurr);
end;
*/

macro GetRefValueForCurr

  var
    RefVal = 0;

  KeyNum(CasVal, 1);
  ReWind(CasVal);
  CasVal.TypeValue = TYPERES_CURRENCY;
  CasVal.CodIntValue = Curr.Code_Currency;
  CasVal.Type = 0; 
  if(getEQ(CasVal))
    RefVal = CasVal.RefValue;
  end;

  return RefVal;

end;

macro FindCashVal( ValType, ValCode )

  var
    RefVal = 0;

  KeyNum(CasVal, 1);
  ReWind(CasVal);
  CasVal.TypeValue = ValType;
  CasVal.CodIntValue = ValCode;
  CasVal.Type = 0; 
  if(getEQ(CasVal))
    RefVal = CasVal.RefValue;
  end;

  return RefVal;

end;

macro GetValTypeStr

  if ( CasVal.Type == 0 )
    return "";
  end;

  return  "(" + GetAlgName( 811, CasVal.Type ) + ")";
end;

macro Ins2F68(RefValue)

  ClearRecord(F68Tmp);
  F68Tmp.CurrCode = Curr.Code_Currency;
  F68Tmp.CurrISO = Curr.Short_Name;
  F68Tmp.CurrName = CasVal.NameValue + " " + GetValTypeStr;
  F68Tmp.CurrPrevRate = PrevRate;
  F68Tmp.CurrCurRate = CurRate;
  F68Tmp.ValueType = CasVal.TypeValue;
  F68Tmp.ValueCode = CasVal.CodIntValue;
  F68Tmp.Type = CasVal.Type;

  ClearRecord( CasAcc );
  CasAcc.Branch = Branch;
  CasAcc.CashOper = DeskSubj.rec.Name;
  CasAcc.SubAccount = "";
  CasAcc.RefValue = RefValue;
  CasAcc.Date = {curdate};
  if ( GetLT( CasAcc )
    and ( CasAcc.Branch == Branch )
    and ( CasAcc.CashOper == DeskSubj.rec.Name )
    and ( CasAcc.SubAccount == "" )
    and ( CasAcc.RefValue == RefValue ) )
    F68Tmp.InRest = CasAcc.RestMoney;
    F68Tmp.InRestR = Money(CasAcc.RestMoney * PrevRate);
    F68Tmp.OutRest = CasAcc.RestMoney;
    F68Tmp.OutRestR = F68Tmp.InRestR + GetRevalueSum( CasAcc.RefValue, {curdate}, CasAcc.CashOper );
  end;
/*
  ClearRecord(CasAcc);
  CasAcc.Branch = Branch;
  CasAcc.Date = {curdate};
  CasAcc.CashOper = DeskSubj.Name;
  CasAcc.RefValue = RefValue;
  if(getEQ(CasAcc))

    F68Tmp.OutRest = CasAcc.RestMoney;
    F68Tmp.OutRestR = Money(CasAcc.RestMoney * CurRate);
  end;
*/
  if(F68Tmp.InRest or F68Tmp.OutRest)
     Recalc = true;
  end;

  return Insert(F68Tmp);

end;

macro IsValMoney

  if ( CasVal.TypeValue == TYPERES_CURRENCY )
    if (  ( CasVal.Type == TYPE_RES_PAY )
      or ( CasVal.Type == TYPE_RES_NOPAY ) )
      return true;
    end;
  end;

  if ( CasVal.TypeValue == TYPERES_PAYDOC )
    if (  ( CasVal.Type == TYPE_RES_NOPAY )
      and ( CasVal.Type == TYPE_RES_PDNAT ) 
      and ( CasVal.Type == TYPE_RES_PDCUR ) 
      and ( CasVal.Type == TYPE_RES_PD ) )
      return true;
    end;
  end;

  return false;
end;

macro TreatCurrency
 var
   RecFound, refGroupVal;

  /*FindExchCurr;*/
  copy( RtlCurr, Curr );
  PrevRate = GetCurrRate({curdate} - 1);
  CurRate = GetCurrRate({curdate});
  refGroupVal = GetRefValueForCurr;
  CasVal.GroupValue = refGroupVal;
  KeyNum(CasVal, 4);
  ReWind(CasVal);
  RecFound = getGE(CasVal);
  while(RecFound and (CasVal.GroupValue == refGroupVal))
     if( ( CasVal.UnitMeas == RESMESUNIT_MONEY )
        and ( ( IsValMoney and ( RepMode != M_TREAS ) )
        or ( ( ( not IsValMoney ) /*and ( CasVal.TypeValue != TYPERES_PAYDOC )*/ ) and ( RepMode != M_CURR ) ) ) )
        Ins2F68(CasVal.RefValue);
     end;
     RecFound = Next(CasVal);
  end;
end;


macro TreatAllCurrs

  var
    RecFound;

  Curr.Code_Currency = 1;
  RecFound = GetGE(Curr);
  while(RecFound)
    TreatCurrency;
    RecFound = Next(Curr);
  end;
end;


macro FillTmpFile

  TreatAllCurrs;

end;


macro OutHead

  [ ];
  [ ];
  [Остатки по валютам ];
  [-------------------------------------------------------------------------------------------------------------];
  [|Код  | Наимен. валюты | Остаток на конец  предыдущего дня |  Пересчет  |        Остаток на начало дня      |];
  [|ва-  |                |-----------------------------------|            |-----------------------------------|];
  [|люты |                |     в валюте    |     в рублях    |    руб.    |     в валюте    |     в рублях    |];
  [-------------------------------------------------------------------------------------------------------------];
end;


macro OutLine

  var
    CurrName;

  if(((F68Tmp.InRest != 0)
    or (F68Tmp.OutRest != 0)) or Debug)
  [ ###   ################ ################# ################# ############ ################# ################# ]
    (
      F68Tmp.CurrISO,
      F68Tmp.CurrName:w,
      F68Tmp.InRest:z,
      F68Tmp.InRestR:z,
      (F68Tmp.OutRestR-F68Tmp.InRestR):z,
      F68Tmp.OutRest:z,
      F68Tmp.OutRestR:z
    );
    Tot.InRestR = Tot.InRestR + F68Tmp.InRestR;
    Tot.OutRestR = Tot.OutRestR + F68Tmp.OutRestR;
  end;
end;


macro OutTotals

  [ ];
  [  Bceгo     :                             ################# ############                   ################# ]
  (
    Tot.InRestR,
    (Tot.OutRestR - Tot.InRestR),
    Tot.OutRestR
  );
end;


macro OutLines

  Rewind(F68Tmp);
  while(Next(F68Tmp))
    OutLine;
  end;

end;

macro TreatCurrentSafe

  Message("Заполнение промежуточного файла ...");
  ClearTmpFile;
  Recalc = false;
  FillTmpFile;
  Message;
  if(Recalc)
    PrintLn;
    PrintLn( DeskSubj.rec.Name );
    PrintLn( MkStr( "=", StrLen( DeskSubj.rec.Name ) ) );
    PrintLn;
    OutHead;
    OutLines;
    OutTotals;
  end;
  
  ClearRecord(Tot);
end;

macro Report

  var
    RecFound;
    
  [Филиал #                                                              ]
  (GetBranchName);
  [ ];
  [                          Пересчет остатков при изменении курса валют ];
  [ ];
  [############]
  (String({curdate}));
  [ ];
  if ( IdentProgram == "Касса" )
     GetNativeSafe;
     TreatCurrentSafe;
  elif ( IdentProgram == "Кладовая" )
    if ( {RprtDSubj} == "" )
      DeskSubj.keyNum = 1;
      DeskSubj.clear;
      DeskSubj.rec.Branch = Branch;
      DeskSubj.rec.Type = DST_PANTRY;
      DeskSubj.addFilter ("exists (select * from dsb_casac_dbt a " +
                    "where a.t_branch = t.t_branch " +
                    "  and a.t_cashoper = t.t_name " +
                    "  and a.t_subaccount = chr(1)" +
                    "  and a.t_date < " + sqlDateToStr( {curdate} ) + " )" );
      RecFound = DeskSubj.getGE;
      while ( RecFound
        and ( DeskSubj.rec.Branch == Branch )
        and ( DeskSubj.rec.Type == DST_PANTRY ) )
        TreatCurrentSafe;
        RecFound = DeskSubj.next;
      end;
      DeskSubj.dropFilter;
    else
      DeskSubj.rec.Branch = Branch;
      DeskSubj.rec.Name = {RprtDSubj};
      if ( DeskSubj.getEQ )
        TreatCurrentSafe;
      end;
    end;
  end;
end;

macro MustSeparate

  const
    RegPath = "RS-RETAIL\\ПЕЧАТИ\\КЛАДОВАЯ\\ВЕДОМОСТЬ_ПЕРЕСЧЕТА\\РАЗДЕЛЬНАЯ_ПЕЧАТЬ";

  var
    Value,
    ErrCode;

  if ( GetRegistryValue( RegPath, V_INTEGER, Value, ErrCode ) == V_INTEGER )
    return Value != 0;
  end;
  return false;
end;

  ByeBoss(0);

  if ( MustSeparate )
    RepMode = M_CURR;
    [ ];         
    [====== ];
    [Валюты ];
    [====== ];
    [ ];
    Report;
    PrintLn( StrFor( 12 ) );
    RepMode = M_TREAS;
    [ ];
    [======== ];
    [Ценности ];
    [======== ];
    [ ];
    Report;
  else            
    Report;
  end;
end;
