
/* Выдача объединенного вклада (Филиал) */

import deprintr, OperCode;

file DepType("sb_dtyp.dbt") key 2;
record OC_PAY_DOC("pay_doc.np"); 

var
  {curdate},
  {oper},
  Op,
  PercSum;

const
  Date1 = Date(1, 7, 1936),
  Date2 = Date(1, 1, 1955),
  Date3 = Date(1, 1, 1984);

macro FindDepType

  DepType.FlagCur = OC_PAY_DOC.IsCur;
  DepType.Kind = OC_PAY_DOC.PrimAccType;
  if(not GetEQ(DepType))
    MsgBox("Вид вклада не найден: ", OC_PAY_DOC.PrimAccType);
    Exit(1);
  end;
end;

macro DateMax(a, b)
 
  if(a > b)
    return a;
  else
    return b;
  end;
end;

macro SelectRate(dem_rate, term_rate) 

  if(DepType.FormContr == 0)
    return dem_rate;
  else
    return term_rate;
  end;
end; 

macro GetPercSum;

  record UnitInfo(UnitInfo) dialog;

  var
    TmpDate,
    Sum = 0;
    
  FindDepType;
  if(OC_PAY_DOC.StaticRest == 0)
    ClearRecord(UnitInfo);
    RunDialog(UnitInfo);
    if((UnitInfo.LatestOpDate != Date(0, 0, 0)) and (UnitInfo.Rest != 0))
      OC_PAY_DOC.StaticLastOpDate = UnitInfo.LatestOpDate;
      OC_PAY_DOC.StaticRest = Money(UnitInfo.Rest);
    else
      Exit(0);
    end;
  end;
  if(OC_PAY_DOC.StaticLastOpDate < Date1)
    Sum = Sum + Money((Date1 - OC_PAY_DOC.StaticLastOpDate) / 365.0 *
      SelectRate(8, 5) * OC_PAY_DOC.StaticRest / 100.0);
  end;
  if(OC_PAY_DOC.StaticLastOpDate < Date2)
    TmpDate = DateMax(OC_PAY_DOC.StaticLastOpDate, Date1);
    Sum = Sum + Money((Date2 - TmpDate) / 365.0 *
      SelectRate(3, 5) * OC_PAY_DOC.StaticRest / 100.0);
  end;
  if(OC_PAY_DOC.StaticLastOpDate < Date3)
    TmpDate = DateMax(OC_PAY_DOC.StaticLastOpDate, Date2);
    Sum = Sum + Money((Date3 - TmpDate) / 365.0 *
      SelectRate(2, 3) * OC_PAY_DOC.StaticRest / 100.0);
  end;
  return Sum;      
end;

  PercSum = GetPercSum;                     
  GetMoney(PercSum, "Сумма причисляемых процентов");
  if(ValType(PercSum) == V_UNDEF)
    Exit;
  end; 
  OC_PAY_DOC.InSum = OC_PAY_DOC.StaticRest + PercSum;
  OC_PAY_DOC.Ground = "Выплата объединенного счета. Остаток: " +
    String(OC_PAY_DOC.StaticRest) + ", причисленные %%: " + PercSum;
end;