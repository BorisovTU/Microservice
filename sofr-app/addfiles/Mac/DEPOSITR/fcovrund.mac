import deprintr,deskinter,alg_vars;
file dd(sbdepdoc) key 6 write;
var TrnStatus=true;
macro GetPatern(Oper)
file GroupMem("groupmem.dbt") key 0;
  GroupMem.Branch=NumRealFNCash;
  GroupMem.Date={curdate};
  GroupMem.Group=GetOperBrigade;
  GroupMem.Oper=Oper;
  if(GetEQ(GroupMem))
    return GroupMem.Linked;
  else
    return 0;
  end;
end;
private macro TrnProc
record OpParm("op_parm.rec");
record Parm(CashParm);
var
  NumDayDoc=100;
  dd.Referenc=ALG_SBDEPDOC.Referenc;
  dd.Date_Document=ALG_SBDEPDOC.Date_Document;
  dd.NumDayDoc=32000;
  TrnStatus=GetLE(dd);
  if(TrnStatus)
    if(dd.Date_Document==ALG_SBDEPDOC.Date_Document)
      NumDayDoc=dd.NumDayDoc+100;
    end;
    Copy(dd,ALG_SBDEPDOC);
    dd.ApplicationKey=FormApplicationKey(dd.iApplicationKind);
    dd.NumDayDoc=NumDayDoc;
    NumDayDoc=NumDayDoc+100;
    dd.ApplType=0;
    dd.InSum=dd.OutSum=0;
    if(ALG_SBDEPDOC.TypeOper==54)
      dd.Rest=-ALG_SBDEPDOC.OutSum;
    else
      dd.Rest=ALG_SBDEPDOC.InSum;
    end;
    TrnStatus=Insert(dd);
    if ( TrnStatus  AND
        ( EdRefWrite (dd.iApplicationKind,dd.ApplicationKey) != 0 ) )   /*Запись в sb_edref.dbt*/
      TrnStatus = FALSE;
    end;
    if(TrnStatus and (not InterDesk_IsDocBunchActive))
      ClearRecord(OpParm);
      OpParm.Date={curdate};
      OpParm.Operation=ALG_SBDEPDOC.TypeOper;
      OpParm.ObjectRef=ALG_DEPOSITOR.Referenc;
      TrnStatus=InterDesk_InitDocBunch(dd.iApplicationKind,dd.ApplicationKey,OpParm);
    end;
    if(TrnStatus)
      TrnStatus=CashLink_AddRec(dd.iApplicationKind,dd.ApplicationKey);
    end;
    if(TrnStatus)
      Copy(dd,ALG_SBDEPDOC);
      dd.ApplicationKey=FormApplicationKey(dd.iApplicationKind);
      dd.NumDayDoc=NumDayDoc;
      if(ALG_SBDEPDOC.TypeOper==54)
        dd.InSum=ALG_SBDEPDOC.OutSum;
        dd.OutSum=0;
      else
        dd.OutSum=ALG_SBDEPDOC.InSum;
        dd.InSum=0;
      end;
      dd.Rest=0;
      if(ALG_SBDEPDOC.ApplType==3)
        dd.VidDoc=1;
      end;
      if(ALG_SBDEPDOC.TypeOper==54)
        dd.Ground="Зачисление переплаты вклада (п/к)";
      else
        dd.Ground="Списание недоплаты вклада (п/к)";
      end;
      TrnStatus=Insert(dd);
      if ( TrnStatus  AND
           ( EdRefWrite (dd.iApplicationKind,dd.ApplicationKey) != 0 ) )   /*Запись в sb_edref.dbt*/
        TrnStatus = FALSE;
      end;
    end;
    if(TrnStatus)
      TrnStatus=CashLink_AddRec(dd.iApplicationKind,dd.ApplicationKey);
    end;
    if(TrnStatus and (ALG_SBDEPDOC.ApplType==3))
      Parm.Type=0; /* Платежеспосбные ресурсы */
      Parm.TypeValue=1; /* TYPERES_CURRENCY */
      Parm.CodIntValue=dd.Code_Currency;
      Parm.Oper=GetPatern(dd.Oper);
      Parm.CashDateDoc=dd.Date_Document;
      Parm.NumOper=6; /* CASHOP_CREDIT */
      Parm.ValueMoney=dd.InSum;
      Parm.NumbDocumCash=dd.NDoc;
      Parm.ApplicationKind=dd.iApplicationKind;
      Parm.ApplicationKey=dd.ApplicationKey;
      Parm.CashPatern=dd.Oper;
      TrnStatus=InitCashDoc(Parm,true); /* true - for immediate processing! */
    end;
  end;
end;
/* entry point */
  SetOutput("NUL");
  if(((ALG_SBDEPDOC.TypeOper==54) and (ALG_SBDEPDOC.ApplType==2) or (ALG_SBDEPDOC.ApplType==3)))
    ;
  elif((ALG_SBDEPDOC.TypeOper==56) and (ALG_SBDEPDOC.ApplType==2))
    ;
  else
    ALG_RESULT=OK_RES;
    return;
  end;
  if(TrnStatus)
    ProcessTrn(1,"TrnProc",dd);
  end;
  if(TrnStatus)
    ALG_RESULT=END_RES;
  else
    ALG_RESULT=ERR_RES;
  end;
end;