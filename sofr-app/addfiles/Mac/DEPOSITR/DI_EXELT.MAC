/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Страхование частных вкладов.

  Прием данных по кредитам из транспортного файла.

  Выполняемые процедуры.

*****************************************************************/

import DeprIntr, DI_Def;

file dit ( depiprev ) key 1 write;  /* Файл "Предварительный реестр" */

file InputFile() txt 1024;  /* Исходный текстовый файл */

var clntLT = TClientList;

const Separate = "^";

const PatternImp = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\IMPORTMESDIR", TRUE ) +
                     "\\*.txt"; /* Маска исходного текстового файла */

/* Контрольная строка. */
const cControl = "РЕЕСТР ОБЯЗАТЕЛЬСТВ БАНКА ПЕРЕД ВКЛАДЧИКАМИ - КРЕДИТЫ";

var NameInputFile = "";  /* Имя транспортного файла */

var StrFile = "";  /* Строка транспортного файла */

var ErrCodeLt, ErrTextLt;

/*********************************************************************/
/*                  Поля записи транспортного файла                  */
/*********************************************************************/
var vDateMake;         /* Дата, на которую нужны данные */
var vBranch;           /* ВСП, в котором ведется кредит */
var vType;             /* Вид записи кредитного кредитования (21,22,23,24) */
var vCurrency;         /* Код валюты во внутренней кодировке */
var vReferenc;         /* Ссылка на счет в базе RS-Loans */
var vAccount;          /* Номер кредитного счета RS-Loans */
var vCodClient = 0;    /* Код клиента-владельца счета */
var vINN;              /* ИНН клиента-владельца счета */
var vPaperKind;        /* Вид документа - удостоверения личности */
var vPaperSeries;      /* Серия документа - удостоверения личности */
var vPaperNumber;      /* Номер документа - удостоверения личности */
var vPaperIssuedDate;  /* Дата выдачи документа - удостоверения личности */
var vPaperIssuerCode;  /* Код подразделения документа - удостовер.личности */
var vName1;            /* Фамилия клиента-владельца счета */
var vName2;            /* Имя клиента-владельца счета */
var vName3;            /* Отчество клиента-владельца счета */
var vIsMale;           /* Пол клиента-владельца счета ("X"-муж.,""-жен.) */
var vBorn;             /* Дата рождения клиента-владельца счета */
var vNumDoc;           /* Номер кредитного договора */
var vDateDoc;          /* Дата начала действия кредитного договора */
var vSummaRest;        /* Остаток в валюте счета на DateMake */
var vSummaRestRUR;     /* Остаток в нац.валюте на DateMake */

/*********************************************************************/
/*      Отделяет от StrFile начало строки до первого разделителя     */
/*   и смещает начало StrFile на следующий после разделителя символ  */
/*********************************************************************/
macro GetSeparateStr
  var Str = "";
  var Sym = "";
  var Cont = TRUE;
  var i = 1,
      k = 1;
  var Len = StrLen( StrFile );

  while ( ( i <= Len )  AND  Cont )
    Sym = SubStr( StrFile, k, 1 );
    if ( Sym == Separate )
      Cont = FALSE;
    end;
    k = k + 1;
    i = i + 1;
  end;
  if ( k > 1 )
    if ( NOT Cont )
      Str = SubStr( StrFile, 1, k-2 );
    else
      Str = SubStr( StrFile, 1, k-1 );
    end;
    StrFile = SubStr( StrFile, k );
  end;

  return Str;
end;

/**********************************************************/
/*         Проверка поля на формат целого числа.          */
/**********************************************************/
macro Is_Int( Str )
  var stat = TRUE;
  var StrT = Trim( Str );
  var Len  = StrLen( StrT );
  var i = 1;
  var Sym;

  while ( ( i <= Len )  AND  stat )
    Sym = SubStr( StrT, i, 1 );
    if ( ( Sym < "0" )  OR  ( Sym > "9" ) )
      stat = FALSE;
    end;
    i = i + 1;
  end;
  return stat;
end;

/**********************************************************/
/*               Проверка поля на формат даты.            */
/**********************************************************/
macro Is_Date( Str )
  var stat = TRUE;
  var StrT = Trim( Str );

  if ( StrLen( StrT ) != 10 )
    return FALSE;
  end;

  var S01 = SubStr( StrT,  1, 1 );
  var S02 = SubStr( StrT,  2, 1 );
  var S03 = SubStr( StrT,  3, 1 );
  var S04 = SubStr( StrT,  4, 1 );
  var S05 = SubStr( StrT,  5, 1 );
  var S06 = SubStr( StrT,  6, 1 );
  var S07 = SubStr( StrT,  7, 1 );
  var S08 = SubStr( StrT,  8, 1 );
  var S09 = SubStr( StrT,  9, 1 );
  var S10 = SubStr( StrT, 10, 1 );

  if ( ( S01 < "0" )  OR  ( S01 > "3" )  OR
       ( S02 < "0" )  OR  ( S02 > "9" )  OR
       ( S03 != "." )                    OR
       ( S04 < "0" )  OR  ( S04 > "1" )  OR
       ( S05 < "0" )  OR  ( S05 > "9" )  OR
       ( S06 != "." )                    OR
       ( S07 < "1" )  OR  ( S07 > "2" )  OR
       ( S08 < "0" )  OR  ( S08 > "9" )  OR
       ( S09 < "0" )  OR  ( S09 > "9" )  OR
       ( S10 < "0" )  OR  ( S10 > "9" ) )
    stat = FALSE;
  end;
  return stat;
end;

/**********************************************************/
/*      Проверка поля на формат денежной переменной.      */
/**********************************************************/
macro Is_Money( Str )
  var stat = TRUE;
  var StrT = Trim( Str );
  var Len  = StrLen( StrT );
  var Sym;
  var i = 1, l = ( Len - 2 );

  while ( ( i <= Len )  AND  stat )
    Sym = SubStr( StrT, i, 1 );
    if ( i == l )
      if ( Sym != "." )
        stat = FALSE;
      end;
    else
      if ( ( Sym < "0" )  OR  ( Sym > "9" ) )
        stat = FALSE;
      end;
    end;
    i = i + 1;
  end;
  return stat;
end;

/**********************************************************/
/*         Преобразование формата даты из строки.         */
/**********************************************************/
macro DateS( sDate )
  return Date( Int( SubStr( sDate, 1, 2 ) ),
               Int( SubStr( sDate, 4, 2 ) ),
               Int( SubStr( sDate, 7, 4 ) ) );
end;

/**********************************************************/
/*         Преобразование формата денег из строки.        */
/**********************************************************/
macro MoneyS( sSum )
  return MoneyL( DoubleL ( sSum ) );
end;

/**********************************************************/
/*               Определение кода клиента.                */
/**********************************************************/
macro FindCodeClient()

  var CodClientINN;
  var CodClientPaper;
  var CodClientFIO;
  var CodClient;
  var CodClientFind = TRUE;

  /* По ИНН. */
  if ( StrLen( vINN ) > 0 )
    CodClientINN = clntLT.FindCodeByINN( vINN );
  else
    CodClientINN = 0;
  end;

  /* По атрибутам документа-удостоверения личности. */
  if ( ( StrLen( vPaperKind ) > 0 )    AND
       ( StrLen( vPaperSeries ) > 0 )  AND
       ( StrLen( vPaperNumber ) > 0 ) )
    CodClientPaper = clntLT.FindCodeByPaper( Int( vPaperKind ),
                                             vPaperSeries,
                                             vPaperNumber );
  else
    CodClientPaper = 0;
  end;

  /* По Ф.И.О. клиента. */
  CodClientFIO = 0;
  if ( StrLen( vName1 ) > 0 )
    if ( ( StrLen( vName2 ) == 0 )  AND  ( StrLen( vName3 ) == 0 ) )
      CodClientFIO = clntLT.FindCodeByFIO( vName1 );
    else
      if ( ( StrLen( vName2 ) > 0 )  AND  ( StrLen( vName3 ) == 0 ) )
        CodClientFIO = clntLT.FindCodeByFIO( vName1, vName2 );
      else
        if ( ( StrLen( vName2 ) > 0 )  AND  ( StrLen( vName3 ) > 0 ) )
          CodClientFIO = clntLT.FindCodeByFIO( vName1, vName2, vName3 );
        end;
      end;
    end;
  end;

  /* Проверки совпадений кода клиента. */
  if ( CodClientINN > 0 )
    CodClient = CodClientINN;
    if ( ( CodClientPaper > 0 )  AND  ( CodClientPaper != CodClientINN ) )
      CodClientFind = FALSE;
    end;
    if ( ( CodClientFIO > 0 )  AND  ( CodClientFIO != CodClientINN ) )
      CodClientFind = FALSE;
    end;
  end;

  if ( CodClientFind  AND  ( CodClientPaper > 0 ) )
    CodClient = CodClientPaper;
    if ( ( CodClientFIO > 0 )  AND  ( CodClientFIO != CodClientPaper ) )
      CodClientFind = FALSE;
    end;
  end;

  if ( CodClientFind  AND  ( CodClientFIO > 0 ) )
    CodClient = CodClientFIO;
  end;

  if ( CodClientFind )
    vCodClient = CodClient;
  else
    vCodClient = 0;
  end;

end;

/************************************************************/
/*                Обработка транспортного файла.            */
/************************************************************/
macro Обработка_исходного_файла()

  var ValidField = TRUE;
  var sCodClient = "";
  var NumLines   = 0;
  var NumErrors  = 0;
  var ErrInsLt   = 0;

  while ( Next( InputFile ) )
    StrFile = InputFile.str;

    ValidField = TRUE;

    NumLines = NumLines + 1;

    /* Дата, на которую нужны данные */
    if ( ValidField )
      vDateMake = GetSeparateStr();
      if ( Is_Date( vDateMake ) )
        ValidField = TRUE;
      else
        ValidField = FALSE;
        NumErrors = NumErrors + 1;
        println( " *** Строка № ", NumLines:5,
                 ". Ошибка в поле 1.DateMake: ", vDateMake );
      end;
    end;

    /* ВСП, в котором ведется кредит */
    if ( ValidField )
      vBranch = GetSeparateStr();
      if ( Is_Int( vBranch ) )
        ValidField = TRUE;
      else
        ValidField = FALSE;
        NumErrors = NumErrors + 1;
        println( " *** Строка № ", NumLines:5,
                 ". Ошибка в поле 2.Branch: ", vBranch );
      end;
    end;

    /* Вид записи кредитного кредитования (21,22,23,24) */
    if ( ValidField )
      vType = GetSeparateStr();
      if ( Is_Int( vType ) )
        ValidField = TRUE;
      else
        ValidField = FALSE;
        NumErrors = NumErrors + 1;
        println( " *** Строка № ", NumLines:5,
                 ". Ошибка в поле 3.Type: ", vType );
      end;
    end;

    /* Код валюты во внутренней кодировке */
    if ( ValidField )
      vCurrency = GetSeparateStr();
      if ( Is_Int( vCurrency ) )
        ValidField = TRUE;
      else
        ValidField = FALSE;
        NumErrors = NumErrors + 1;
        println( " *** Строка № ", NumLines:5,
                 ". Ошибка в поле 4.Currency: ", vCurrency );
      end;
    end;

    /* Ссылка на счет в базе RS-Loans */
    if ( ValidField )
      vReferenc = GetSeparateStr();
      if ( Is_Int( vReferenc ) )
        ValidField = TRUE;
      else
        ValidField = FALSE;
        NumErrors = NumErrors + 1;
        println( " *** Строка № ", NumLines:5,
                 ". Ошибка в поле 5.Referenc: ", vReferenc );
      end;
    end;

    /* Номер кредитного счета RS-Loans */
    if ( ValidField )
      vAccount = GetSeparateStr();
    end;

    /* Код клиента-владельца счета */
    if ( ValidField )
      vCodClient = 0;
      sCodClient = GetSeparateStr();
      if ( ( StrLen( sCodClient ) == 0 )  OR  Is_Int( sCodClient ) )
        if ( StrLen( sCodClient ) == 0 )
          vCodClient = 0;
        else
          vCodClient = Int( sCodClient );
        end;
        ValidField = TRUE;
      else
        ValidField = FALSE;
        NumErrors = NumErrors + 1;
        println( " *** Строка № ", NumLines:5,
                 ". Ошибка в поле 7.CodClient: ", sCodClient );
      end;
    end;

    /* ИНН клиента-владельца счета */
    if ( ValidField )
      vINN = GetSeparateStr();
    end;

    /* Вид документа - удостоверения личности */
    if ( ValidField )
      vPaperKind = GetSeparateStr();
      if ( ( StrLen( vPaperKind ) == 0 )  OR  Is_Int( vPaperKind ) )
        ValidField = TRUE;
      else
        ValidField = FALSE;
        NumErrors = NumErrors + 1;
        println( " *** Строка № ", NumLines:5,
                 ". Ошибка в поле 9.PaperKind: ", vPaperKind );
      end;
    end;

    /* Серия документа - удостоверения личности */
    if ( ValidField )
      vPaperSeries = GetSeparateStr();
    end;

    /* Номер документа - удостоверения личности */
    if ( ValidField )
      vPaperNumber = GetSeparateStr();
    end;

    /* Дата выдачи документа - удостоверения личности */
    if ( ValidField )
      vPaperIssuedDate = GetSeparateStr();
      if ( ( StrLen( vPaperIssuedDate ) == 0 )  OR  Is_Date( vPaperIssuedDate ) )
        ValidField = TRUE;
      else
        ValidField = FALSE;
        NumErrors = NumErrors + 1;
        println( " *** Строка № ", NumLines:5,
                 ". Ошибка в поле 12.PaperIssuedDate: ", vPaperIssuedDate );
      end;
    end;

    /* Код подразделения документа - удостовер.личности */
    if ( ValidField )
      vPaperIssuerCode = GetSeparateStr();
    end;

    /* Фамилия клиента-владельца счета */
    if ( ValidField )
      vName1 = GetSeparateStr();
    end;

    /* Имя клиента-владельца счета */
    if ( ValidField )
      vName2 = GetSeparateStr();
    end;

    /* Отчество клиента-владельца счета */
    if ( ValidField )
      vName3 = GetSeparateStr();
    end;

    /* Пол клиента-владельца счета ("X"-муж.,""-жен.) */
    if ( ValidField )
      vIsMale = GetSeparateStr();
    end;

    /* Дата рождения клиента-владельца счета */
    if ( ValidField )
      vBorn = GetSeparateStr();
      if ( ( StrLen( vBorn ) == 0 )  OR  Is_Date( vBorn ) )
        ValidField = TRUE;
      else
        ValidField = FALSE;
        NumErrors = NumErrors + 1;
        println( " *** Строка № ", NumLines:5,
                 ". Ошибка в поле 18.Born: ", vBorn );
      end;
    end;

    /* Номер кредитного договора */
    if ( ValidField )
      vNumDoc = GetSeparateStr();
    end;

    /* Дата начала действия кредитного договора */
    if ( ValidField )
      vDateDoc = GetSeparateStr();
      if ( Is_Date( vDateDoc ) )
        ValidField = TRUE;
      else
        ValidField = FALSE;
        NumErrors = NumErrors + 1;
        println( " *** Строка № ", NumLines:5,
                 ". Ошибка в поле 20.DateDoc: ", vDateDoc );
      end;
    end;

    /* Остаток в валюте счета на DateMake */
    if ( ValidField )
      vSummaRest = GetSeparateStr();
      if ( Is_Money( vSummaRest ) )
        ValidField = TRUE;
      else
        ValidField = FALSE;
        NumErrors = NumErrors + 1;
        println( " *** Строка № ", NumLines:5,
                 ". Ошибка в поле 21.SummaRest: ", vSummaRest );
      end;
    end;

    /* Остаток в нац.валюте на DateMake */
    if ( ValidField )
      vSummaRestRUR = GetSeparateStr();
      if ( Is_Money( vSummaRestRUR ) )
        ValidField = TRUE;
      else
        ValidField = FALSE;
        NumErrors = NumErrors + 1;
        println( " *** Строка № ", NumLines:5,
                 ". Ошибка в поле 22.SummaRestRUR: ", vSummaRestRUR );
      end;
    end;

    /* Определение кода клиента. */
    if ( ValidField )
      if ( vCodClient == 0 )
        FindCodeClient();
        if ( vCodClient != 0 )
          if ( clntLT.GetRecord( vCodClient ) )
            vCodClient = clntLT.CurRec.PrimaryClientCode();
          else
            ValidField = FALSE;
          end;
        end;
      else
        if ( clntLT.GetRecord( vCodClient ) )
          vCodClient = clntLT.CurRec.PrimaryClientCode();
        else
          FindCodeClient();
          if ( vCodClient != 0 )
            if ( clntLT.GetRecord( vCodClient ) )
              vCodClient = clntLT.CurRec.PrimaryClientCode();
            else
              ValidField = FALSE;
            end;
          end;
        end;
      end;
    end;

    /* Код клиента на этот момент должен быть определен. */
    if ( ValidField  AND  ( vCodClient == 0 ) )
      ValidField = FALSE;
      println( " *** Строка № ", NumLines:5,
               ". Не найден клиент: CodClient=", sCodClient,
               ", INN=", vINN,
               ", Документ=", vPaperKind, ",", vPaperSeries, ",", vPaperNumber,
               ", Ф.И.О.=", vName1, ",", vName2, ",", vName3 );
    end;

    /* Проверка наличия записи по вкладам для данного клиента. */
    if ( ValidField )
      ClearRecord( dit );
      dit.DateMake  = DateS( vDateMake );  /* Дата, на которую нужны данные */
      dit.CodClient = vCodClient;          /* Код клиента-владельца счета */
      dit.Type      = TYPE_DEPOS;          /* Запись по вкладам */
      if ( NOT GetGE( dit )                         OR
           ( dit.DateMake  != DateS( vDateMake ) )  OR
           ( dit.CodClient != vCodClient )          OR
           ( dit.Type      != TYPE_DEPOS ) )
        println( " *** Строка № ", NumLines:5,
                 ". Нет данных по вкладам. Данные по кредитам не имеют смысла." );
        ValidField = FALSE;
        /* NumErrors = NumErrors + 1; */
      end;
    end;

    /* Проверка наличия записи данного типа для данного клиента. */
    if ( ValidField )
      ClearRecord( dit );
      dit.DateMake  = DateS( vDateMake );  /* Дата, на которую нужны данные */
      dit.CodClient = vCodClient;          /* Код клиента-владельца счета */
      dit.Type      = Int( vType );        /* Вид записи кредитного кредитования (21,22,23,24) */
      /* Код валюты во внутренней кодировке */
      dit.Currency  = GKBO_FindFinInstrCurr( Int( vCurrency ) );
      dit.Account   = vAccount;            /* Номер кредитного счета RS-Loans */
      if ( GetGE( dit )                             AND
           ( dit.DateMake  == DateS( vDateMake ) )  AND
           ( dit.CodClient == vCodClient )          AND
           ( dit.Type      == Int( vType ) )        AND
           ( dit.Account   == vAccount ) )
        println( " *** Строка № ", NumLines:5,
                 ". Ошибка. Запись уже включена в Реестр." );
        ValidField = FALSE;
        NumErrors = NumErrors + 1;
      end;
    end;

    /* Запись в файл "Предварительный реестр". */
    if ( ValidField )
      ClearRecord( dit );
      dit.DateMake      = DateS( vDateMake );       /* Дата, на которую нужны данные */
      dit.Branch        = Int( vBranch );           /* ВСП, в котором ведется кредит */
      dit.Type          = Int( vType );             /* Вид записи кредитного кредитования (21,22,23,24) */
      /* Код валюты во внутренней кодировке */
      dit.Currency      = GKBO_FindFinInstrCurr( Int( vCurrency ) );
      dit.Referenc      = Int( vReferenc );         /* Ссылка на счет в базе RS-Loans */
      dit.Account       = vAccount;                 /* Номер кредитного счета RS-Loans */
      dit.CodClient     = vCodClient;               /* Код клиента-владельца счета */
      dit.NumDoc        = vNumDoc;                  /* Номер кредитного договора */
      dit.DateDoc       = DateS( vDateDoc );        /* Дата начала действия кредитного договора */
      dit.SummaRest     = MoneyS( vSummaRest );     /* Остаток в валюте счета на DateMake */
      dit.SummaRestRUR  = MoneyS( vSummaRestRUR );  /* Остаток в нац.валюте на DateMake */
      if ( NOT Insert( dit ) )
        println( " *** Ошибка Insert." );

        ErrCodeLt = Status( ErrTextLt );
        println( "  Ошибка " + String( ErrCodeLt ) + ": " + ErrTextLt );
        println( "" );
        ErrInsLt  = ErrInsLt + 1;
        NumErrors = NumErrors + 1;
      end;
    end;

  end;

  println( "Завершена загрузка транспортного файла: ", NameInputFile );
  println( "Прочитано записей: ", NumLines, "." );
  println( "Обнаружено ошибок: ", NumErrors, "." );
  println( "" );

  return ErrInsLt;

end;

/**************************************************************************/
/*            Прием данных по кредитам из транспортного файла.            */
/**************************************************************************/
macro DI_Credit_Load()
  var cl_Ret = TRUE;

  if ( Open( InputFile, NameInputFile ) )
    ReWind( InputFile );
    if ( Next( InputFile )  AND  ( InputFile.str == cControl ) )
      if ( Обработка_исходного_файла() != 0 )
        cl_Ret = FALSE;
      end;
      Close( InputFile );
    else
      Close( InputFile );
      println( " *** Выбран файл неверного формата: " + NameInputFile );
      /* cl_Ret = FALSE; */
    end;
  else
    println( " *** Ошибка открытия файла \"", NameInputFile, "\"." );
    cl_Ret = FALSE;
  end;

  return cl_Ret;
end;

/**************************************************************************/
/*                           Головная процедура.                          */
/**************************************************************************/
macro DI_Credit_Transport( pLoansTxt, pLogFile )

  var ct_ret = 0;

  if ( StrLen( pLoansTxt ) == 0 )
    if ( SelectFile( NameInputFile, PatternImp, "Выберите транспортный файл" ) )
      while ( NOT DI_Credit_Load() )
        MsgBox( "Выбран файл неверного формата: ", NameInputFile );
        if ( NOT SelectFile( NameInputFile, PatternImp,
                             "Выберите транспортный файл" ) )
          Exit();
        end;
      end;
    else
      Exit( 1 );
    end;
  else
    NameInputFile = pLoansTxt;
    if ( NOT DI_Credit_Load() )
      ct_ret = 1;  /* Ошибка в транспортном файле. */
    end;
  end;

  return ct_ret;

end;
