/*
  СБЕРБАНК 1.0
  R-Style SoftWare Lab.

  Печать Кассового ордера ф.53

  Лебедев С.В.
  16.12.97
*/

IMPORT deprintr, ci_ord_b, ci_ord_s, brparm, CvLnMain, ChkPrn;

file CIDoc  ("ci_doc.dbt") key 3 write;
file CIOper ("ci_oper.dbt")  key 0;
file CIMisc ("ci_misc.dbt")  key 0;
file CISubOp("ci_subop.dbt") key 0;
file CIOGSZ ("ogsz_war.dbt") key 0;
file CIRate ("ci_rate.dbt")  key 1;
file CashDoc("sb_casdc.dbt") key 7;
file CashLink("sb_casln.dbt") key 0;
file CashVal("sb_casvl.dbt") key 0;

record CapIssueDoc("ci_doc.dbt");
record OGSZDocInfo("ci_doc.1");
record MiscDocInfo("ci_doc.2");
record CertDocInfo("ci_doc.3");
record MoneyInfo("ci_doc.2");
record DragMetInfo("ci_doc.4");

const
  SB_NOTBALANCE = 1000;

const
  CI_OGSZ          = 1,
  CI_LOTTERY       = 2,
  CI_CERT          = 3,
  CI_COIN          = 4,
  CI_INGOT         = 5,
  CI_ORVVZ         = 6,
  CI_MISC          = 10,

  O_OGSZ_SELL      = 1,
  O_OGSZ_BUY       = 2,
  O_OGSZ_PAYWARR   = 3,
  O_OGSZ_KEEP      = 4,
  O_OGSZ_RETURN    = 5,
  O_OGSZ_SELL_ACC  = 6,

  O_LOTTERY_SELL   = 1,
  O_LOTTERY_PAYWIN = 2,
  O_LOTTERY_BUY    = 3,
  O_LOTTERY_KEEP   = 4,
  O_LOTTERY_RETURN = 5,
  O_LOTTERY_SELL_ACC  = 6,

  O_CERT_SELL      = 1,
  O_CERT_PAY       = 2,
  O_CERT_PAYALN    = 3,
  O_CERT_KEEP      = 4,
  O_CERT_RETURN    = 5,
  O_CERT_SELL_ACC  = 6,

  O_MISC_SELL      = 1,
  O_MISC_BUY       = 2,
  O_MISC_KEEP      = 3,
  O_MISC_RETURN    = 4,
  O_MISC_SELL_ACC  = 6,

  O_INGOT_SELL     = 1,
  O_INGOT_BUY      = 2,
  O_INGOT_KEEP     = 3,
  O_INGOT_RETURN   = 4,
  O_INGOT_SELL_ACC = 5;

const
  FLAGDEBCRED_IN   = 1,  /* Кредит */
  FLAGDEBCRED_OUT  = 2;  /* Дебет */

const
  TYPERES_CURRENCY    = 1,   /* Деньги (из справочника валют)      */
  TYPERES_VALFORM     = 2,   /* Ценные бланки                      */
  TYPERES_VALUABLES   = 3,   /* Др. ценности (из справочника ценностей) */
  TYPERES_MINCAPISSUE = 500; /* Минимально возможный код ЦБ        */

const
  RESMESUNIT_POINT = 1,  /* Поштучно   */
  RESMESUNIT_MONEY = 2;  /* Стоимость  */

const
  CV_ = 500;

var
  ClientList = TClientList,
  Branch = NumFNCash,
  {curdate},
  Ground;

macro FindCIMisc

  CIMisc.Type = CIDoc.Type;
  CIMisc.IssueID = CIDoc.Issue;
  return GetEQ( CIMisc );
end;

macro FindCashVal( ValueRef )

  CashVal.RefValue = ValueRef;
  return GetEQ( CashVal );
end;

macro FindMainVal

  var
    Type, ValType, ValCode;

  GetMainValueCode( CIDoc.Type, CIDoc.Issue, CIDoc.Operation, CIDoc.SubOp, Type, ValType, ValCode );

  KeyNum( CashVal, 1 );
  CashVal.TypeValue = ValType;
  CashVal.CodIntValue = ValCode;
  CashVal.Type = Type;
  if ( not GetEQ( CashVal ) )
    MsgBox( "Не найден ресурс Кассы" );
    Exit( -1 );
  end;
  KeyNum( CashVal, 0 );
end;

private macro GetClientInfo( docSerNum, docWhoAndWhere )

  var
    Cl;

  if ( ClientList.GetRecord( CIDoc.ClientCode ) )
    Cl = ClientList.CurRec;
     SetParm( 0, GetNameOfPAPRKIND( Cl.rec.PaperKind ) + " " +
                                    Cl.rec.PaperSeries + " " +
                                    Cl.rec.PaperNumber );
     SetParm( 1, Cl.rec.PaperIssuer + " " + string(Cl.rec.PaperIssuedDate) );
    return Cl.Rec.Name1 + " " + Cl.Rec.Name2 + " " + Cl.Rec.Name3 +
//   "\nДокумент: " +  Cl.MakeDocStr +
   "\nАдрес: " + Cl.Rec.Address;
  else
    SetParm( 0, "" );
    SetParm( 1, "" );
    return "";
  end;
end;

macro CalcValTurns( DebCred )

  var
    Sum = $0,
    RecFound;

  ClearRecord( CashLink );
  CashLink.ApplicationKind1 = CIDoc.ApplicationKind;
  CashLink.ApplicationKey1 = CIDoc.ApplicationKey;
  RecFound = GetGE( CashLink );
  while ( RecFound
    and ( CashLink.ApplicationKind1 == CIDoc.ApplicationKind )
    and ( CashLink.ApplicationKey1 == CIDoc.ApplicationKey ) )
    if ( CashLink.RefValue2 > 0 )
      CashDoc.RefValue = CashLink.RefValue2;
      CashDoc.ApplicationKind = CashLink.ApplicationKind2;
      CashDoc.ApplicationKey = CashLink.ApplicationKey2;
      if ( GetEQ( CashDoc ) )
        if ( CashDoc.FlagDebCredOper == DebCred )
          FindCashVal( CashDoc.RefValue );
          if ( CashVal.TypeValue != TYPERES_CURRENCY )
            if ( CashDoc.ValueMoney )
              Sum = Sum + CashDoc.ValueMoney;
            else
              Sum = Sum + Money( CashDoc.ValueCol)
            end;
          end;
        end;
      end;
    end;
    RecFound = Next( CashLink );
  end;
  return Sum;
end;

macro CIOperIsCreditM(Type, Number)

  if(Type == CI_OGSZ)
    if((Number == O_OGSZ_SELL) or (Number == O_OGSZ_RETURN))
      return true;
    else
      return false;
    end;
  elif((Type == CI_LOTTERY) or (Type == CI_ORVVZ))
    if((Number == O_LOTTERY_SELL) or (Number == O_LOTTERY_RETURN))
      return true;
    else
      return false;
    end;
  elif(Type == CI_CERT)
    if((Number == O_CERT_SELL) or (Number == O_CERT_RETURN))
      return true;
    else
      return false;
    end;
  elif((Type == CI_MISC) or (Type == CI_COIN))
    if((Number == O_MISC_SELL) or (Number == O_MISC_RETURN))
      return true;
    else
      return false;
    end;
  elif(Type == CI_INGOT)
    if((Number == O_INGOT_SELL) or (Number == O_INGOT_RETURN))
      return true;
    else
      return false;
    end;
  end;
  return false;
end;

/*******************************************************************
                 Получение номера купона ОГСЗ
*******************************************************************/
macro Get_OGSZ_War
  var Ret = TRUE;
  ClearRecord( CIOGSZ );
  CIOGSZ.Issue = CIDoc.Issue;
  if ( getGE( CIOGSZ )  AND  ( CIOGSZ.Issue == CIDoc.Issue ) )
    while ( Ret )
      if ( ( CIOGSZ.RetStartDate >= {curdate} )  AND
           ( CIOGSZ.Number != 0 ) )
        return CIOGSZ.Number;
      end;
      Ret = ( Next( CIOGSZ )  AND  ( CIOGSZ.Issue == CIDoc.Issue ) );
    end;
  end;
  return -1;
end;

macro CheckPrn

  var
    RegPath = "RS-RETAIL\\ПЕЧАТИ\\ЦЕННЫЕ_БУМАГИ_И_ЦЕННОСТИ\\ОРДЕРА";

  CheckPrinter(RegPath, StrFor( 0 ) );
end;

macro PrintOrd(DocAddr)

  var    str;
  var    IsCredit;
  var    WarNum;
  var    ValSum;
  var    Gnd;
  var    ClInfo;
  var    IsOrder = false;
  var    ClientName = "";
  var    DocSerNum;
  var    DocWhoAndWhere;
  var    DocAccs;
  var    vOrderNum = 0;

  CheckPrn();

  SetBuff(CapIssueDoc, DocAddr);
  Copy(CIDoc, CapIssueDoc);
  GetEQ(CIDoc);

  if(CIDoc.Type == CI_OGSZ)
    if ( CIDoc.Operation == O_OGSZ_SELL_ACC )
      return;
    end;
  elif(CIDoc.Type == CI_LOTTERY)
    if ( CIDoc.Operation == O_LOTTERY_SELL_ACC )
      return;
    end;
  elif(CIDoc.Type == CI_CERT)
    if ( CIDoc.Operation == O_CERT_SELL_ACC )
      return;
    end;
  elif((CIDoc.Type == CI_MISC) or (CIDoc.Type == CI_COIN))
    if ( CIDoc.Operation == O_MISC_SELL_ACC )
      return;
    end;
  elif(CIDoc.Type == CI_INGOT)
    if ( CIDoc.Operation == O_INGOT_SELL_ACC )
      return;
    end;
  end;

  if(CIDoc.Sum == $0)
    return;
  end;

  IsCredit = CIOperIsCreditM(CIDoc.Type, CIDoc.Operation);

  /* Печать договоров по ОГСЗ */
  if(CIDoc.Type == CI_OGSZ)
    SetRecordAddr(OGSZDocInfo,CIDoc,0,FldOffset(CIDoc,"Info"),true);
    WarNum = Get_OGSZ_War();
    if ( WarNum > 0 )
      /* Чтение курса ОГСЗ */
      ClearRecord( CIRate );
      CIRate.Type          = CIDoc.Type;
      CIRate.Issue         = CIDoc.Issue;
      CIRate.WarrantNumber = WarNum;
      CIRate.Date          = {curdate};
      if ( getLE( CIRate )                                        AND
           ( CIRate.Type          == CIDoc.Type )                 AND
           ( CIRate.Issue         == CIDoc.Issue )                AND
           ( CIRate.WarrantNumber == WarNum ) )
        if (IsCredit)
          /* Продажа */
          PrintContractSale
          (
            CIDoc,
            CIRate
          );
        else
          /* Покупка */
          PrintContractBuy
          (
            CIDoc,
            CIRate
          );
        end;
      else
        MsgBox( "Невозможно прочитать курсы ОГСЗ|Договор не выпускается" );
      end;
    else
      MsgBox( "Невозможно прочитать купоны ОГСЗ|Договор не выпускается" );
    end;
  else
    EXIT( 1 );
  end;

  if ( IsCredit )
    ValSum = CalcValTurns( FLAGDEBCRED_OUT );
  else
    ValSum = CalcValTurns( FLAGDEBCRED_IN );
  end;

  PrintOutput;
end;

end;
