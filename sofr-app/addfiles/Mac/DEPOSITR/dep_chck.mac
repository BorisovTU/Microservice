
// Печать списка ошибок по процедуре проверки вкладов

import rsd, commonInter, acc_form;

const
  ACCTEST_NoError    =   0,
  // ----------------------
  PCREST_CodeErrMin  =   1,
  PCREST_TotRestErr  =   2,
  PCREST_CodeErrMax  =  99,
  // ----------------------
  PCCALC_CodeErrMin  = 101,
  PCCALC_TotRestErr  = 102,
  PCCALC_CodeErrMax  = 199,
  // ----------------------
  ACCREST_CodeErrMin = 201,
  ACCREST_LastDocErr = 202,
  ACCREST_TwoDocErr  = 203,
  ACCREST_CodeErrMax = 299,
  // ----------------------
  PCESTIM_CodeErrMin = 301,
  PCESTIM_NoEstFile  = 302,
  PCESTIM_EstSumErr  = 303,
  PCESTIM_EstDateErr = 304,
  PCESTIM_CodeErrMax = 399;

var 
  {curdate}, 
  {CNum}, 
  isCur = numFlagCur;

var 
  cmd = RsdCommand( "select dep.t_account as acc, err.t_errorCode as errCode " +
                    "from dacerrtmp_tmp" + " err, ddepositr_dbt dep " +
                    "where err.t_errorCode <> 0 and err.t_referenc = dep.t_referenc" );
  
var 
  rs  = RsdRecordset( cmd ), 
  errorCode, 
  desc;

  cmd.execute;

  [       Ошибки целостности БД по счетам вкладчиков за # ] ( {curdate} );
  [ ];
  [ ---------------------------------------------------------------------- ];
  [   Счет                    | Описание ошибки                            ];                              
  [ --------------------------|------------------------------------------- ];
  while ( rs.moveNext )
    errorCode = rs.value( "errCode" );
    if ( errorCode == PCREST_TotRestErr)
      desc = "Несоответствие суммы в файле остатков";
    elif ( errorCode == PCCALC_TotRestErr )
      desc = "Несоответствие суммы в файле процентов";
    elif ( errorCode == ACCREST_LastDocErr )
      desc = "Несоответствие остатка в документе и счете";
    elif ( errorCode == ACCREST_TwoDocErr )
      desc = "Несоответствие операции остатку в документе";
    elif ( errorCode == PCESTIM_NoEstFile )
      desc = "Отсутствует запись в файле накопленных %% по счету";
    elif ( errorCode == PCESTIM_EstSumErr )
      desc = "Несоответствие данных по накопленным %%";
    elif ( errorCode == PCESTIM_EstDateErr )
      desc = "Ошибка в определении периода по накопленным %%";
    else
      desc = "Ошибка";
    end;
    [ ########################## ########################################### ]
    ( Acc_Form(String(rs.value( "acc" ))), desc:w );
  end;

onError( e )
  printLn( e.message );
end;
