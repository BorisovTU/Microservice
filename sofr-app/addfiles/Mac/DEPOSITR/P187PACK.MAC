/*

    R-Style Software Lab.

    RS-Retail 2.01

    Отчет по плановым перечислениям

*/

import DeprIntr, F187Comm;

file aa ( depositr ) key 8;
file dd ( sbdepdoc ) key 2;
file pp ( dplanpay ) key 2;

record Doc1 ( "sbdepdoc.1" );

const FlagCur = NumFlagCur();
const FNCash  = NumFNCash();

var ret;
var num       = 0;
var FlagFirst = TRUE;

/**************************************************************************\
|                 Поиск докмента по плановому перечислению                 |
\**************************************************************************/
macro Document( DatePay )
  var find = FALSE;
  var ret  = TRUE;
  ClearRecord( dd );
  dd.Referenc         = pp.Reference;
  dd.TypeOper         = pp.TypeOper;
  dd.DepDate_Document = DatePay;
  if ( getEQ( dd ) )
    while ( ret  AND
            ( dd.Referenc         == pp.Reference )  AND
            ( dd.TypeOper         == pp.TypeOper  )  AND
            ( dd.DepDate_Document == DatePay      ) )
      if (  ( dd.ListTransfer     == pp.Ref       )  AND
            ( GetBitFlag( dd.Flags, 29 ) == 1 )   )
        ClearRecord( aa );
        aa.Referenc = dd.Referenc;
        if ( NOT GetEQ( aa ) )
          aa.Account = "*** Не найден ! ***";
        end;
        SetRecordAddr( Doc1, dd, 0, 0, TRUE );
        Init( aa, Doc1 );
        DefOutputFields;
        PrintOrder();
        [#](MkStr(12,1));
        find = TRUE;
        ret  = FALSE;
      else
        ret = Next( dd );
      end;
    end;
  end;
  if ( NOT find )
    println( "Не найден документ по п\п :", Acc_Form(pp.Account):f, ", ",
                                            pp.NumberContract,      ", ",
                                            pp.Sum:18:2:a,          ", ",
                                            pp.NamePay,             ", " );
  end;
end;

/**************************************************************************\
|          Пакетный выпуск ордеров ф.187 при плановоых перечислениях       |
\**************************************************************************/
macro Orders (Acc, DatePay)

  ClearRecord(pp);
  pp.IsCur       = FlagCur;
  pp.FNCash      = FNCash;
  pp.DateLastPay = DatePay;
  ret = GetGE(pp);

  while (ret)
    if ((pp.IsCur       == FlagCur) OR
        (pp.FNCash      == FNCash ) OR
        (pp.DateLastPay == DatePay)   )
      if (pp.DocApplKind != 200)
        if ((pp.NamePay == "") AND (pp.KindPay == 1))
          pp.NamePay = "Невостреб.платеж";
        end;

        if (FlagFirst)
          [ ];
          InitProgress(num,"dplanpay.dbt","Идет формирование ордеров ...");
          FlagFirst = FALSE;
        end;

        if ( ( pp.ErrCode == 0 )  AND
             ( ( StrLen(Acc) == 0 )  OR  ( Acc == pp.Account ) ) )
          Document( DatePay );
        end;

        num = num + 1;
        UseProgress(num);
      end;
      ret = Next(pp);
    else
      ret = FALSE;
    end;
  end;

  if (NOT FlagFirst)
    RemProgress();
  else
    println(" Плановые перечисления за ", DatePay, " не проводились");
   end;

  ExitMacro;
end;
