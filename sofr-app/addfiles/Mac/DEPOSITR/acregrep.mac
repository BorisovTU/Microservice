
/* Отчёт о проключёванных счетах */

import deprintr;            

file namealg ("namealg.dbt", "bank.def"); /* Названия алгоритмов */
file accreg  ("accreg.dbt");              /* Реестр счетов       */


/*************************************************************************/
macro Colontitle
  [ ------- #################### ------------------ ########## ------ ######## ---------
  ] (GetRetailVersion(),Date(),Time());
end;


/*************************************************************************/
macro FindName
(
  TypeAlg,
  NumberAlg
)
/*
 Подкачка названий из файла названий алгоритмов
*/

  var str;
  var stat;

  KeyNum(namealg,0);
  ReWind(namealg);
  namealg.iTypeAlg = TypeAlg;
  namealg.iNumberAlg = NumberAlg;
  stat = GetEQ(namealg);
  if (stat)
    str = Trim(namealg.szNameAlg);
  else
/*    str = "В namealg.dbt - не найдено";*/
    str = "";
  end;

  return str;

end;


/*************************************************************************/
/*                                                                       */
/*************************************************************************/
macro ListAccNums
(
  IsCur,
  FNCash,
  GroupNumb,
  Code_Currency,
  First,
  Last
)
           
  var groupName;
  var recordsCount = 0;
  var stat;
  var text = " Генерация отчёта...";
  var str;
          
  initProgress(Last - First, text);
  KeyNum(accreg, 1);
  ReWind(accreg);
  accreg.IsCur  = IsCur;
  accreg.FNCash = FNCash;
  accreg.GroupNumb = GroupNumb;
  accreg.Code_Currency = Code_Currency;
  accreg.Number = First;
  if ( Int(GroupNumb) < 0 )
    groupName = FindName( 935, -Int(GroupNumb) );
  else
    groupName = FindName( 935,  Int(GroupNumb) );
  end;      

  str = "       Список номеров счетов группы нумерации " + "'" + groupName + "'" +
        " с " + String(First) + " по " + String(Last);
  [#](str);
  stat = getGE(accreg);
  if(stat)
    if( (accreg.IsCur == IsCur) and (accreg.FNCash == FNCash) and 
       (accreg.GroupNumb == GroupNumb) and (accreg.Code_Currency == Code_Currency)
       and (accreg.Number == First) )

      str = "";
      [#](str);
      str = " ╔════════════════════════════════════ Номера счетов ═══════════════════════════════╗";
      [#](str); 
      [ ║                                                                                  ║];
      [ ║        Номер         ║            Ф.И.О.             ║Дата открытия║Дата закрытия║];
    end;
  end;
  while (stat)
    if( (accreg.IsCur == IsCur) and (accreg.FNCash == FNCash) and 
       (accreg.GroupNumb == GroupNumb) and (accreg.Code_Currency == Code_Currency)
       and (accreg.Number <= Last) )

       UseProgress(recordsCount);
       str = " ║" +String(Acc_Form(accreg.Account):22:c) + "║";
       str = str + "                               ║";
       str = str + String(accreg.OpenDate:13:c) + "║";
       str = str + String(accreg.CloseDate:13:c) + "║";
       [#](str);
    end;
    stat = Next(accreg);
    recordsCount = recordsCount + 1;
  end;
  remProgress();

end;





/************** Г О Л О В Н О Й   М А К Р О С ***************************/
macro ShowRegNums
(
  IsCur,
  FNCash,
  GroupNumb,
  Code_Currency,
  First,
  Last
)
  Open(namealg); 

  Colontitle();
  [ ];

  ListAccNums(IsCur, FNCash, GroupNumb, Code_Currency, First, Last);

  [ ];
  Colontitle();
end;

/*************** О К О Н Ч А Н И Е ** М А К Р О С А ********************/

                                  
  
  
