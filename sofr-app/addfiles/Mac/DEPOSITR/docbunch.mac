/*
**  Класс "Связка документов"
**
*/ 
import sqlconv;

class TDocBunch( appKind, appKey, refValue )

  var
    cashLink = TBFile( "sb_casln.dbt", "r" ), 
    retOp    = TBFile( "ret_op.dbt", "r",  0 ), 
    headAppKind,
    headAppKey,
    IsAddFilter = false;

  macro firstDoc

    var stat;
    cashLink.keyNum = 0;
    cashLink.rec.ApplicationKind1 = headAppKind;
    cashLink.rec.ApplicationKey1  = headAppKey;
    cashLink.rec.NumLinkDoc = 0;
    stat = cashLink.getGE;
    return ( stat
       and ( cashLink.rec.ApplicationKind1 == headAppKind )
       and ( cashLink.rec.ApplicationKey1  == headAppKey ) )
  end;

  macro nextDoc

    var stat;
    stat = cashLink.next;
    return ( stat
       and ( cashLink.rec.ApplicationKind1 == headAppKind )
       and ( cashLink.rec.ApplicationKey1  == headAppKey ) )
  end;

  macro docValueRef

    return cashLink.rec.RefValue2;
  end;
  
  macro docAppKind

    return cashLink.rec.ApplicationKind2;
  end;

  macro docAppKey

    return cashLink.rec.ApplicationKey2;
  end;

  macro headOperation

    return retOp.rec.Operation;
  end;

  macro headSubOp

    return retOp.rec.SubOp;
  end;

  macro date

    return retOp.rec.Date;
  end;

  macro oper

    return retOp.rec.Oper;
  end;

  macro tokenID

    return retOp.rec.TokenID;
  end;

  macro destructor
    if( IsAddFilter )
      cashLink.dropFilter();
    end;
  end;

  macro _init( appKind, appKey, refValue )

    if ( ( ValType( refValue ) == V_UNDEF ) or ( refValue == null ) )
      refValue = 0;
    end;

    cashLink.keyNum =  1;

    cashLink.clear;
    cashLink.rec.ApplicationKind2 = appKind;
    cashLink.rec.ApplicationKey2 = appKey;
    cashLink.rec.RefValue2 = refValue;
    if ( cashLink.getEQ )     /* Нашли голову */
      headAppKind = cashLink.rec.ApplicationKind1;
      headAppKey  = cashLink.rec.ApplicationKey1;
    else
      headAppKind = appKind;
      headAppKey  = appKey;
    end; 

    if( IsAddFilter )
      IsAddFilter = false;
      cashLink.dropFilter();
    end;

    cashLink.addFilter( "t.t_ApplicationKind1 = " + string( headAppKind ) +
  	  	      " and t.t_ApplicationKey1 = " + GetSQLString( headAppKey ) );
    IsAddFilter = true;
    retOp.rec.ApplicationKind = headAppKind;
    retOp.rec.ApplicationKey  = headAppKey;
    retOp.getEQ;
  end;

  if ( ( appKind != null ) and ( appKey != null ) )
    _init( appKind, appKey, refValue );
  end;
end;
