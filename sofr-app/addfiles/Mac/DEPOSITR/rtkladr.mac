/*
  RS-Bank 6.00.020.26
  RS-Retail

  Обработка адресов по КЛАДР
  Проект № 777, документ "PRJ_000777_КЛАДР_Retail.doc", п.5.2.2

  Ромодин Александр Васильевич
  21.02.2007
*/
Import CommonInter,ptkladr;
// Глобальные переменные - на случай нескольких проверок в пакетном режиме
// (при одной компиляции)
private var gpIsEnable = false;
private var gpIsCall   = false;

// *********************************************************

private macro isTabExist (name)
  var rs;
  rs = RsdRecordset("select 1 from "+name+" where 1 = 0");
  rs.movenext;
  return true;
  OnError(err);
  return false;
end;
// *********************************************************

private macro isKladrEnable (dep:TDepClient):bool 
//
// Вкючен ли КЛАДР
//
  var ret = false;
//
// 1. Если в адресе - не Россия, то никакого КЛАДР быть не может
//
  if (dep)
    if (StrUpr(Trim(dep.rec.Country)) == "RUS")
      ret = true;  // Первая проверка прошла
    else
      ret = false; // Точно не КЛАДР
    end; 
  else
    ret = true;  // Может быть включен КЛАДР (а может и не быть - просто на
                 // принадлежность адреса России не проверяем
  end;
  if (ret)
//  Если в пакетном режиме уже проверяли, то больше не проверяем
    if (NOT gpIsCall)
//    2. КЛАДР - только если параметр "CB \ PARTY \ КЛАДР \ КЛАДР" включен
      ret = GetRegVal("CB\\PARTY\\КЛАДР\\КЛАДР");
      if (ret)
//      3. КЛАДР - только если в базе существует файл kladr.dbt
        ret = isTabExist ("kladr_dbf");
      end;
      gpIsCall   = true;
      gpIsEnable = ret;
    else
      ret = gpIsEnable; // Уже было определено
    end;
  end;
  return ret;
end;
// *********************************************************

macro getRegionNumber (dep:TDepClient):string
//
// Номер региона из переданной записи TDepClient
//
  var ret = "";
  var rs;
  var cmd, cmdstr;
  if (dep) 
//  Если поле dep.Kladr заполнено, 
//  то числовой код равен первым двум символам из этого поля

    if (Trim(dep.rec.Kladr) != "")
      ret = SubStr (dep.rec.Kladr,1,2);
    elif (isKladrEnable(dep))
      cmdstr = "SELECT SubStr (t_code,1,2) FROM kladr_dbf WHERE "+
            sql_normalization("t_socr") + " = '" + normalization(dep.rec.CodeRegion) + "' AND " +
            sql_normalization("t_name") + " = '" + normalization(dep.rec.Region)     + "'";
      cmd = RsdCommand(cmdstr);
      cmd.execute;
      rs = RsdRecordset(cmd); 
      if (rs.moveNext())
        ret = SubStr(rs.value(0),1,2);
      end;
    end;
  end;
  return ret;
end;
// ---------- Конец файла **********
