/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*  Коммунальные платежи                                                    *
*                                                                          *
*  Проверка необходимости взятия комиссии при проведении коммунального     *
*  платежа из вкладной операции                                            *
*                                                                          *
*  Лебедев С.В.                                                            *
*  06.05.2000                                                              *
\**************************************************************************/

import DeprIntr,alg_vars;

file   depdoc         (sbdepdoc    );

record ALG_SBDEPDOC_1 ("sbdepdoc.1");

var    bankdprt : TBFile;
var    partcode : TBFile;

private var {MFO_Bank};

var    BankVersion   = BankVer();
var    CodeBankRecip = "";


/**************************************************************************\
|                          Открытие файлов RS-Bank                         |
\**************************************************************************/
macro OpenBankDprt

  var stat         = FALSE;

  partcode = Tbfile("partcode.dbt","r",1);
  bankdprt = Tbfile("bankdprt.dbt","r",0);
  if ((partcode) AND (bankdprt))
    stat = TRUE;
  end;

  return stat;

end;


/**************************************************************************\
|     Определение кода типа банка - Сбербанк, АКБ, РКЦ или что-то еще      |
\**************************************************************************/
macro DefCodeBankRecip (BIK)

  var stat = TRUE;
  var flag;

  CodeBankRecip = "";

  if (stat)
    stat = OpenBankDprt();
  end;

  if (stat)
    if (BankVersion == 510)
      partcode.rec.CodeKind = 3;
      partcode.rec.Code     = BIK;
      flag = partcode.GetEQ;
      if (flag)
        bankdprt.rec.PartyID = partcode.rec.PartyID;
        flag = bankdprt.GetEQ;
        if (flag)
          /* CodeBankRecip = bankdprt.rec.KindDepart; */
          if( bankdprt.rec.UERType == 3 )
            CodeBankRecip = "ОСБ";
          end;
        end;
      end;
    else
      bankdprt.rec.MFO_Depart = BIK;
      bankdprt.rec.Corr_Acc   = "";
      flag = bankdprt.GetGE;
      if (flag AND (bankdprt.rec.MFO_Depart == BIK))
        CodeBankRecip = bankdprt.rec.Kind_Depart;
      end;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  Copy( depdoc, ALG_SBDEPDOC );
  SetRecordAddr( ALG_SBDEPDOC_1, depdoc, 0, 0, true );

  ALG_PARAM.prmD = 0;

  if (ALG_SBDEPDOC_1.CP_Recip == 0)
    if ((ALG_SBDEPDOC_1.MFO_Receiver == ""        ) OR
        (ALG_SBDEPDOC_1.MFO_Receiver == {MFO_Bank})   )
      ALG_RESULT = SKIP_RES; /* Перевод осуществляется в наш тербанк */
    else
      if (DefCodeBankRecip(ALG_SBDEPDOC_1.MFO_Receiver))
        if ((CodeBankRecip == "ОСБ") OR (CodeBankRecip == "УСБ"))
          ALG_RESULT = SKIP_RES; /* БИК СБ РФ - комиссию не берем */
        else
          ALG_RESULT = OK_RES;
        end;
      else
        ALG_RESULT = ERR_RES;
      end;
    end;
  else
    ALG_RESULT = SKIP_RES;
  end;

end;
