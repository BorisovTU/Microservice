           
/* Сведения о привлеченных средствах */

import DeprIntr, Cur_Rate, Contain, sqlconv;

/*file BranchList( "listfdep.dbt" ) key 0;*/
var ddep = TRecHandler( "i_dp_dep.rec" );
var dpDepList = TdpDepList;
file Curr( "currency.dbt" ) key 0;
var Dep = TBFile("depositr.dbt");
var DepType = TBFile("sb_dtyp.dbt", "w", 4);
var DepStat = TBFile("sb_depst.dbt", "w", 0); 

const
  NullDate = Date( 0, 0, 0 ),
  BS_BRANCH=0,
  BS_OPERDEP=3;

private var
  Branch = NumFNCash,
  {curdate},
  RepDate = {curdate},
  IsCur,
  Mode = GetCurrentMode,
  CBRate,                          
  Scale,
  BuckCBRate,                          
  BuckScale,
  N = 0,
  m_nAllBranch;

/* 
**  Массив сроков востребования
**  Положительные значения обозначают срок в днях, 
**  отрицательные - в месяцах
**
**  При изменении количества элементов не забудьте 
**  перерисовать таблицу!!!
*/

array
  Terms;

  Terms( 0 ) = 0;	
  Terms( 1 ) = 1;	
  Terms( 2 ) = 7;	
  Terms( 3 ) = 30;	
  Terms( 4 ) = 60;	
  Terms( 5 ) = 90;	
  Terms( 6 ) = 180;	
  Terms( 7 ) = -12;	
  Terms( 8 ) = 0;

var
  RubDetailedAccs = TSet, 
  CurrDetailedAccs = TSet; 

var
  Total_PcRate_x_Rest;

array
  TermRests;   	

  RubDetailedAccs.Insert( "ДО ВОСТРЕБ." );
  RubDetailedAccs.Insert( "Пенсионный" );
  RubDetailedAccs.Insert( "Ср.пенсион." );
  RubDetailedAccs.Insert( "Ср.пенс.6мес" );
  RubDetailedAccs.Insert( "Ср.пен.1г1м" );
  RubDetailedAccs.Insert( "ЮБИЛЕЙНЫЙ" );
  RubDetailedAccs.Insert( "СБЕРЕГ.1МЕС." );
  RubDetailedAccs.Insert( "СБЕРЕГ.2МЕС." );
  RubDetailedAccs.Insert( "СБЕРЕГ.3МЕС." );
  RubDetailedAccs.Insert( "СБЕРЕГ.6МЕС." );
  RubDetailedAccs.Insert( "Особый номер" );
  RubDetailedAccs.Insert( "Особ.н.1г_1м" );
  RubDetailedAccs.Insert( "Компнес. 2" );
  RubDetailedAccs.Insert( "Молодежный" );
  RubDetailedAccs.Insert( "СБ-501" );

  CurrDetailedAccs.Insert( "до востреб." );
  CurrDetailedAccs.Insert( "СРОЧНЫЙ С ЕЖ" );
  CurrDetailedAccs.Insert( "Пополн.на 3М" );
  CurrDetailedAccs.Insert( "Пополн.на 6М" );
  CurrDetailedAccs.Insert( "ОСОБЫЙ" );
  CurrDetailedAccs.Insert( "Особый номер" );
  CurrDetailedAccs.Insert( "Особ.н.1г_1м" );

macro FindDepType( DType )
              
  DepType.rec.FlagCur = IsCur;
  DepType.rec.Kind = DType;
  return DepType.GetEQ();  
end;

macro FindBranch( BrNo )

  if ( not findDepartment(BrNo, null, ddep) )
    MsgBox( "Не найден филиал" );
    Exit( 1 );
  end;
end;

macro FindCurr( Code )

  Curr.Code_Currency = Code;
  if ( not GetEQ( Curr ) )
    MsgBox( "Не найдена валюта" );
    Exit( 1 );
  end;
end;

macro GetCBRate

  Scale = 1;
  CBRate = 0.0;

  if (Curr.ScaleOfc != 0)
    Scale = Curr.ScaleOfc;
  end;

/*
  ExchCurr.ID = Curr.ExternalCode;
  if ( not GetEQ( ExchCurr ) )
    CBRate = 0;
  end;
*/
  Copy( RtlCurr, Curr );
  CBRate = GetCurrRate( RepDate );
end;

macro GetBuckCBRate

  Curr.Code_Currency = 1;  
  GetEQ( Curr );
  GetCBRate;
  BuckCBRate = CBRate;
  BuckScale = Scale;
  if ( BuckCBRate == 0 )
    MsgBox( "Не задан курс доллара США" );
    Exit( 1 );
  end;
end;

macro Cur2Rub(Sum)

  if ( IsCur )
    return Money( CBRate * Sum );
  else
    return Sum;
  end;
end;

macro Cur2Bucks(Sum)

  if ( IsCur )
    return Money( CBRate * Sum / BuckCBRate );
  else
    return Sum;
  end;
end;

macro MyMin( a, b )

  if ( a < b )
    return a;
  else
    return b;
  end;
end;

macro passLastDayInMonth( Month, Year )

  var
    Day;

  if ( Month == 12 )
    Month = 1;
    Year = Year + 1;
  else
    Month = Month + 1;
  end;

  DateSplit( Date( 1, Month, Year ) - 1, Day );
  return Day; 
end;

macro AddMonths( Dd, Months2Add )

  var
    Day, Month, Year, Years2Add;

  DateSplit( Dd, Day, Month, Year );
  Month = Month + Months2Add;
  Years2Add = Int( ( Month - 1 ) / 12 );
  Month = Month - Years2Add * 12;
  Year = Year + Years2Add;
  Day = MyMin( Day, passLastDayInMonth( Month, Year ) );
  return Date( Day, Month, Year );
end;

macro EndOfTerm( TermNo )

  var
    Term = Terms( TermNo ),
    TermEndDate;

  if ( Term > 0 )
    TermEndDate = RepDate + Term;
  elif ( Term < 0 )
    TermEndDate = AddMonths( RepDate, -Term );
  else                                           /* Term == 0 */
    TermEndDate = Date( 31, 12, 2099 );
  end;
  return TermEndDate;
end;                                 

macro CalcTotalRest

  var
    TotalRest = $0, 
    i = 0;

  while ( i < ASize( Terms ) )
    TotalRest = TotalRest + TermRests( i );
    i = i + 1;
  end;

  return TotalRest;
end;

macro PrintHeader
                     

  [ ];
  [ ];
  [                                                                                                                                                                             Форма № 7.12            ];
  [                                                                                                                                                                                                                   ];
  [                                                                                                                                                                              Утверждено             ];
  [                                                                                                                                                                        Постановлением Правления     ];
  [                                                                                                                                                                           Сбербанка России          ];
  [                                                                                                                                                                        от 21.12.2000 г. № 242/1     ];
  [                                                                                                                                                                               месячная              ];
  [                                                                                                                                                                  представляется до 15 числа месяца, ];
  [                                                                                                                                                                         следующего за отчетным      ];
  if(m_nAllBranch==1)
  [                                                                                                                                                                                                                   ];
  [ Филиал #                                                                                                                                                                                                          ]
  ( ddep.rec.Name );                                                                              
  [ ];
  else
  [ Отделение                                                                                                                                                                                                         ];
  end;
  [ Исполнитель _____________________________________________________                                                                                                                                                 ];
  [                              (Ф.И.О., телефон)                                                                                                                                                                    ];      
  [ ];                                                                                                    
  [                                                                                    Сведения о привлеченных средствах физических лиц                                                                               ];
  [ ################################################################################################################################################################################################### ]     
  ( ( "на " + String( RepDate:m ) ):c );                                                                  
  [ ];                                                                                                    
  [ ];                                                                                                    
  [-----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------];
  [|    |                                       |Средневзв.|                                                     С р о к   д о   п о г а ш е н и я                                       |             |];
  [| №  |              Вид вклада               |  ставка  |-----------------------------------------------------------------------------------------------------------------------------|    Всего    |];
  [|    |                                       |          | До востреб. |    1 день   |    2 - 7    |    8 - 30   |   31 - 60   |   61 - 90   |   91 - 180  | 181 - 1 год |свыше 1 года |             |];
  [|----|---------------------------------------|----------|-------------|-------------|-------------|-------------|-------------|-------------|-------------|-------------|-------------|-------------|];
end;                                                                                    
                                                                                        
macro PrintSubHeader( Text )                                                            
                                                                                        
  PrintLn( " " + Text );
  PrintLn( " " + MkStr( "=", StrLen( Text ) ) );
end;

macro PrintLine( LineNo, Desc )

  var
    TotalRest = CalcTotalRest,
    WMR = 0; 
                                        
  if ( TotalRest != $0 )
    WMR = Total_PcRate_x_Rest / TotalRest;
  end;

  [ #### ####################################### ########## ############# ############# ############# ############# ############# ############# ############# ############# ############# ############# ]
  (
    LineNo, 
    Desc, 
    WMR:0:2:z, 
    TermRests( 0 ):z, 
    TermRests( 1 ):z, 
    TermRests( 2 ):z, 
    TermRests( 3 ):z, 
    TermRests( 4 ):z, 
    TermRests( 5 ):z, 
    TermRests( 6 ):z, 
    TermRests( 7 ):z, 
    TermRests( 8 ):z, 
    TotalRest:z
  );
end;

macro PrintBottom

  [-----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------];
  [ ];
  [ ]; 
  [ Заместитель Председателя банка          _____________________________ ];
  [ ]; 
  [ Директор управления (Начальник отдела ) _____________________________ ];
  [ ]; 
  [ "  " __________________ 200_ г. ];
  Print( StrFor( 12 ) );
end;

macro TreatAccount( TermNo )      

  var
    ObjectType,
    ConvertedRest;
                       
  if ( IsCur )    
    ConvertedRest = Cur2Bucks( Dep.rec.Sum_Rest );
  else
    ConvertedRest = Dep.rec.Sum_Rest;
  end;         

  if ( Dep.rec.UseAlternate )
    ObjectType = 2002;
    TermNo = 0;			/* Альтернативные условия - всегда до востребования */
  else                 
    ObjectType = 2001;
  end;                             

  TermRests( TermNo ) = TermRests( TermNo ) + ConvertedRest;
  Total_PcRate_x_Rest = Total_PcRate_x_Rest + ConvertedRest * PercRateAL( Dep.rec.Referenc, ObjectType, RepDate ); 

  N = N + 1;
  Message( "Обработано ", N, " счетов" );
end;              

macro GetDTypeRestFromStats( Cit, Rest )
 var stat;

  stat=1;
  Rest=$0;
  if(m_nAllBranch == 2)
    dpDepList.rewind();
    dpDepList.next();
    ddep = dpDepList.RecHandler;
  end;

  while( stat )
/*   if((BranchList.StatusBranch==BS_BRANCH)or(BranchList.StatusBranch==BS_OPERDEP))*/
     DepStat.addFilter( "t.t_FNCash = " + string(ddep.rec.Code) + 
			" and t.t_IsCur = " + string(IsCur) +
			" and t.t_Kind = " + GetSQLString(DepType.rec.Kind) +
			" and t.t_CodCur = " + string(Curr.Code_Currency) +
			" and t.t_FlagRez = " + sqlChar(Cit) );
     DepStat.rec.FNCash = ddep.rec.Code;
     DepStat.rec.IsCur = IsCur;
     DepStat.rec.Kind = DepType.rec.Kind;
     DepStat.rec.CodCur = Curr.Code_Currency;
     DepStat.rec.FlagRez = Cit;
     DepStat.rec.Date = RepDate;
     DepStat.rec.Mode = Mode;
     if ( DepStat.GetLE ()
       and ( DepStat.rec.FNCash == ddep.rec.Code )
       and ( DepStat.rec.IsCur == IsCur )
       and ( DepStat.rec.Kind == DepType.rec.Kind )
       and ( DepStat.rec.CodCur == Curr.Code_Currency )
       and ( DepStat.rec.FlagRez == Cit ) )
       Rest = Rest+DepStat.rec.Rest;
     end;
     DepStat.dropFilter();     
/*   end;*/
   if(m_nAllBranch==2)
    if(not(dpDepList.next()))
     stat=0;
    end;
   else
    stat=0;
   end;
  end;
 SetParm( 1, Rest );  

end;

macro ProcessTerm( TermNo )

  var
    RecFound,
    BOT, EOT,
    stat;

  if ( TermNo > 0 )
    EOT = EndOfTerm( TermNo );
  else
    EOT = NullDate;
  end;                        

  if ( TermNo > 1 )
    BOT = EndOfTerm( TermNo - 1 ) + 1;
  elif ( TermNo == 1 )
    BOT = RepDate;
  else
    BOT = NullDate;
  end;

  Dep.KeyNum = 6;

  stat=1;
  if(m_nAllBranch == 2)
    dpDepList.rewind();
    dpDepList.next();
    ddep = dpDepList.RecHandler;
  end;

  while( stat )
/*   if((BranchList.StatusBranch==BS_BRANCH)or(BranchList.StatusBranch==BS_OPERDEP))*/
    Dep.addFilter( "t.t_IsCur = " + string(IsCur) +
		   " and t.t_FNCash = " + string(ddep.rec.Code) +
		   " and t.t_Type_Account = " + GetSQLString(DepType.rec.Kind) );
    Dep.clear();
    Dep.rec.IsCur = IsCur;
    Dep.rec.FNCash = ddep.rec.Code;
    Dep.rec.Type_Account = DepType.rec.Kind;
    Dep.rec.End_DateDep = BOT;
    RecFound = Dep.GetGE();
    while ( RecFound
      and ( Dep.rec.IsCur == IsCur )
      and ( Dep.rec.FNCash == ddep.rec.Code )
      and ( Dep.rec.Type_Account == DepType.rec.Kind )
      and ( Dep.rec.End_DateDep <= EOT ) )
      if ( ( Dep.rec.Open_Close == "" ) 
        and ( Dep.rec.Open_Date <= RepDate )
        and ( Dep.rec.Code_Currency == Curr.Code_Currency ) )
        TreatAccount( TermNo );
      end;
      RecFound = Dep.Next();
    end;
    Dep.dropFilter();
/*   end;*/
   if(m_nAllBranch==2)
    if(not(dpDepList.next()))
     stat=0;
    end;
   else
    stat=0;
   end;
  end;
end;

macro GetAccTypeRate
  var stat;

  Dep.KeyNum = 1;

  stat=1;
  if(m_nAllBranch == 2)
    dpDepList.rewind();
    dpDepList.next();
    ddep = dpDepList.RecHandler;
  end;

  while( stat )
/*   if((BranchList.StatusBranch==BS_BRANCH)or(BranchList.StatusBranch==BS_OPERDEP))*/
  
    Dep.addFilter( "t.t_IsCur = " + string(IsCur) +
		   " and t.t_FNCash = " + string(ddep.rec.Code) +
		   " and t.t_Open_Close = " + sqlChar("") +
		   " and t.t_Type_Account = " + GetSQLString(DepType.rec.Kind) +
		   " and t.t_Code_Currency = " + string(Curr.Code_Currency) );
    Dep.clear();
    Dep.rec.IsCur = IsCur;
    Dep.rec.FNCash = ddep.rec.Code;
    Dep.rec.Open_Close = "";
    Dep.rec.Type_Account = DepType.rec.Kind;
    Dep.rec.Code_Currency = Curr.Code_Currency;
    if ( Dep.GetGE()
      and ( Dep.rec.IsCur == IsCur )
      and ( Dep.rec.FNCash == ddep.rec.Code )
      and ( Dep.rec.Open_Close == "" )
      and ( Dep.rec.Type_Account == DepType.rec.Kind )
      and ( Dep.rec.Code_Currency == Curr.Code_Currency ) )
      Dep.dropFilter();
      return PercRateAL( Dep.rec.Referenc, 2001, RepDate );
    else
      Dep.dropFilter();
      return 0;
    end;
/*   end;*/
   if(m_nAllBranch==2)
    if(not(dpDepList.next()))
     stat=0;
    end;
   else
    stat=0;
   end;
  end;

end;

macro ProcessDepTypeForCurr

  var   
    i, 
    Tmp,
    Rest,
    RecFound;
                          
  GetCBRate;
 
  if ( DepType.rec.FormContr and ( DepType.rec.NewGroupNumb != "1" ) )
    i = 0;
    while ( i < ASize( Terms ) )
      ProcessTerm( i );
      i = i + 1;
    end;
  else
    GetDTypeRestFromStats( StrFor( 0 ), Tmp );
    if ( IsCur );
      Rest = Cur2Bucks( Tmp );
    else
      Rest = Tmp;
    end;
    GetDTypeRestFromStats( StrFor( 1 ), Tmp );
    if ( IsCur );
      Rest = Rest + Cur2Bucks( Tmp );
    else
      Rest = Rest + Tmp;
    end; 
    TermRests( 0 ) = TermRests( 0 ) + Rest;
    Total_PcRate_x_Rest = Total_PcRate_x_Rest + GetAccTypeRate * Rest;
  end;                                                              
end;                       

macro ProcessDepType

  if ( not IsCur )
    FindCurr( 0 );
    ProcessDepTypeForCurr;
  else          
    Rewind( Curr );
    while ( Next( Curr ) )
      if ( ( Curr.Code_Currency > 0 ) and Curr.CrDifrRate )
        ProcessDepTypeForCurr;
      end;
    end;
  end;
end;

macro ResetResults

  var
    i = 0;

  Total_PcRate_x_Rest = $0;
  while ( i < ASize( Terms ) )
    TermRests( i ) = $0;
    i = i + 1;
  end;
end;

macro Report

  var
    i, 
    Desc,  
    RecFound;

  PrintHeader;
       
  IsCur = 0;
  PrintSubHeader( "1. Остаток средств по действующим рублевым вкладам, руб." );

  i = 1;    
  DepType.clear();
  DepType.addFilter("t.t_FlagCur = " + string(IsCur) );
  DepType.rec.FlagCur = IsCur;
  RecFound = DepType.GetGE();
  while ( RecFound and ( DepType.rec.FlagCur == IsCur ) )
    if ( RubDetailedAccs.Contains( DepType.rec.Kind ) )
      ResetResults;
      ProcessDepType;
      if ( DepType.rec.Kind == "Cirrus Пенс" )
        Desc = "Счета банковских карт Сбербанк-Maestro \"Пенсионная\"";
      else
        Desc = DepType.rec.Name;
      end; 
      PrintLine( "1." + i, Desc ); 
      i = i + 1;
    end; 
    RecFound = DepType.Next();
  end;

  DepType.dropFilter();
  ResetResults;
  DepType.addFilter("t.t_FlagCur = " + string(IsCur) );
  DepType.clear();
  DepType.rec.FlagCur = IsCur;
  RecFound = DepType.GetGE();
  while ( RecFound and ( DepType.rec.FlagCur == IsCur ) )
    if ( ( Index( DepType.rec.UserTypeAccount, "К" ) != 0 ) and ( not RubDetailedAccs.Contains( DepType.rec.Kind ) ) )
      ProcessDepType;
    end; 
    RecFound = DepType.Next();
  end;
  PrintLine( "1.17", "Счета банковских карт (кроме Сбербанк-Maestro \"Пенсионная\")" ); 
  DepType.dropFilter();

  IsCur = 1;

  [-----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------];
  PrintSubHeader( "2. Остаток средств по действующим вкладам в иностранной валюте, долл. США" );

  i = 1;    
  DepType.addFilter("t.t_FlagCur = " + string(IsCur) );
  DepType.clear();
  DepType.rec.FlagCur = IsCur;
  RecFound = DepType.GetGE();
  while ( RecFound and ( DepType.rec.FlagCur == IsCur ) )
    if ( CurrDetailedAccs.Contains( DepType.rec.Kind ) )
      ResetResults;
      ProcessDepType;
      Desc = DepType.rec.Name;
      PrintLine( "2." + i, Desc ); 
      i = i + 1;
    end; 
    RecFound = DepType.Next();
  end;
  DepType.dropFilter();

  ResetResults;
  DepType.addFilter("t.t_FlagCur = " + string(IsCur) );
  DepType.clear();
  DepType.rec.FlagCur = IsCur;
  RecFound = DepType.GetGE();
  while ( RecFound and ( DepType.rec.FlagCur == IsCur ) )
    if ( ( Index( DepType.rec.UserTypeAccount, "К" ) == 0 ) 
      and ( not CurrDetailedAccs.Contains( DepType.rec.Kind ) )
      and ( DepType.rec.FormContr == 20 ) )
      ProcessDepType;
    end; 
    RecFound = DepType.Next();
  end;
  PrintLine( "2.8", "Срочные депозиты Сбербанка России" ); 
  DepType.dropFilter();

  ResetResults;
  DepType.addFilter("t.t_FlagCur = " + string(IsCur) );
  DepType.clear();
  DepType.rec.FlagCur = IsCur;
  RecFound = DepType.GetGE();
  while ( RecFound and ( DepType.rec.FlagCur == IsCur ) )
    if ( ( Index( DepType.rec.UserTypeAccount, "К" ) != 0 ) and ( not CurrDetailedAccs.Contains( DepType.rec.Kind ) ) )
      ProcessDepType;
    end; 
    RecFound = DepType.Next();
  end;
  PrintLine( "2.9", "Счета банковских карт" ); 
  DepType.dropFilter();

  IsCur = 0;

  ResetResults;
  DepType.addFilter("t.t_FlagCur = " + string(IsCur) );
  DepType.clear();
  DepType.rec.FlagCur = IsCur;
  RecFound = DepType.GetGE();
  while ( RecFound and ( DepType.rec.FlagCur == IsCur ) )
    if ( ( Index( DepType.rec.UserTypeAccount, "К" ) == 0 ) 
      and ( DepType.rec.Kind != "Шаблонный" )
      and ( not RubDetailedAccs.Contains( DepType.rec.Kind ) ) )
      ProcessDepType;
    end; 
    RecFound = DepType.Next();
  end;
  PrintLine( "3.", "Прочие рублевые вклады,  B26 + B7, руб." ); 
  DepType.dropFilter();

  IsCur = 1;

  ResetResults;
  DepType.addFilter("t.t_FlagCur = " + string(IsCur) );
  DepType.clear();
  DepType.rec.FlagCur = IsCur;
  RecFound = DepType.GetGE();
  while ( RecFound and ( DepType.rec.FlagCur == IsCur ) )
    if ( ( Index( DepType.rec.UserTypeAccount, "К" ) == 0 ) 
      and ( DepType.rec.Kind != "Шаблонный" )
      and ( not CurrDetailedAccs.Contains( DepType.rec.Kind ) ) 
      and ( DepType.rec.FormContr != 20 ) )
      ProcessDepType;
    end; 
    RecFound = DepType.Next();
  end;
  PrintLine( "4.", "Прочие вклады в иностранной валюте, долл. США" ); 
  DepType.dropFilter();

  IsCur = 0;

  ResetResults;
  PrintLine( "5.", "Сберегательные сертификаты, руб." ); 
  PrintLine( "6.", "Рублевые векселя, руб." ); 

  IsCur = 1;

  PrintLine( "7.", "Валютные векселя, долл. США" ); 

  PrintBottom;
end;

/* Голова */
  GetDate( RepDate, "Введите дату отчета" );
  m_nAllBranch=confDlg("Вывести отчет по:",NULL,"Текущему филиалу","Отделению")+1;
  dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
  if ( RepDate == {curdate} )
    if ( not m_nAllBranch )
      if ( not UpdateStatistics( false, null, -1 ) )
        Exit;
      end;
    else                        
      dpDepList.rewind();
      while ( dpDepList.next() )
/*        if ( ( BranchList.StatusBranch == BS_BRANCH ) or ( BranchList.StatusBranch == BS_OPERDEP ) )*/
          if ( not UpdateStatistics( false, null, -1, dpDepList.rec.Code ) )
            Exit;
          end;
/*        end; */
      end;
    end;
  end;
  OpenDepFiles;
  GetBuckCBRate;
  FindBranch( Branch );
  Report;     
  CloseDepFiles;
end;
