/*

      R-Style SoftWare Lab.

      RS-Retail

      Отчет об утерянных сбер.книжках

*/

 import CommonInter, DeprIntr, RSD;

 var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
 var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

 file fCurrency( "currency.dbt" ) key 0;

 var clntL = TClientList;

 const  FlagCur = NumFlagCur(),
        FNCash  = NumFNCash();

 var {curdate};

 var StartDate, EndDate;

 var sDate;

 var sNum;

 var
   cmd,
   rs;
var FIO;

 /*********************************************************
                Поиск короткого названия валюты
 *********************************************************/
 macro Find_Curr_Short_Name( pCode )
   var rSN;
   fCurrency.Code_Currency = pCode;
   if ( GetEQ( fCurrency ) )
     rSN = fCurrency.Short_Name;
   else
     rSN = String( pCode );
   end;
   return rSN;
 end;

 /*********************************************************
                  Поиск Фамилии И.О. клиента
 *********************************************************/
 macro Find_ClientFIO( pCode )
   var rFIO;
   if ( clntL.GetRecord( pCode ) )
     rFIO = clntL.CurRec.ConvertFIO();
   else
     rFIO = String( pCode );
   end;
   return rFIO;
 end;

 /*********************************************************
                      Основная процедура
 *********************************************************/

 StartDate = {curdate};

 GetDate( StartDate, "Укажите дату начала периода" );
 EndDate = StartDate;
 GetDate( EndDate,   "Укажите дату конца периода" );

 cmd = RsdCommand( "select dd.t_account as account, dd.t_code_currency as Code_Currency, dd.t_codclient as CodClient, dd.t_ardate as Ardate, dep.t_SpecialAccess as SpecialAccess " +
                   " from dsbdepdoc_dbt dd, ddepositr_dbt dep " +
                   " where  dd.t_iscur = ?  and  " +
                           " dd.t_fncash = ?  and  " +
                           " dd.t_date_document between  ?  and  ?  and  " +
                           " dd.t_typeoper = 62  and  " +
                           " dd.t_referenc = dep.t_Referenc and "  +
                           " dd.t_typecomplexoper = 205 " );


 cmd.addParam( "isCur",   RSDBP_IN );
 cmd.addParam( "Branch",  RSDBP_IN );
 cmd.addParam( "minDate", RSDBP_IN );
 cmd.addParam( "maxDate", RSDBP_IN );

 cmd.value( "isCur" )   = FlagCur;
 cmd.value( "Branch" )  = FNCash;
 cmd.value( "minDate" ) = StartDate;
 cmd.value( "maxDate" ) = EndDate;

 cmd.execute();
 rs = RsdRecordset( cmd );

 sNum = 0;

 [
                              Отчет об утерянных сберегательных книжках

                                       с ########## по ##########

   +-------------------------------------------------------------------------------------------+
   |  NN  |          Счет вкладчика        | Вал |            Фамилия И.О.        | Дата утери |
   |  п/п |                                | юта |                                | сбер.книжки|
   |------+--------------------------------+-----+--------------------------------+------------|]
   ( StartDate, EndDate );

 while ( rs.moveNext )
   if(  (not SpecialAccess) and OperMayListSpecialAccounts and rs.value("SpecialAccess"))
      FIO = "XXXXXX X.X.";
   else
      FIO = Find_ClientFIO( rs.value( "CodClient" ) );
   end;
   DtTmSplit( rs.value( "Ardate" ), sDate );
   sNum = sNum + 1;
   [ | #### | ############################## | ### | ############################## | ########## |]
   (
     sNum:4,
     Acc_Form(String(rs.value( "Account" ))):f:30,
     Find_Curr_Short_Name( rs.value( "Code_Currency" ) ):3,
     FIO:30,
     sDate
   );
 end;

 [ +-------------------------------------------------------------------------------------------+];

 end;
