/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады и Прочие операции                                                *
*                                                                          *
*  Откат выплаты вклада бывшего Чеченского СБ                              *
*                                                                          *
*  Лебедев С.В.                                                            *
*  13.04.2004                                                              *
\**************************************************************************/

import DeprIntr, alg_vars, alg_pay, sqlconv;

private var chcnrgst = TBFile( "chcnrgst.dbt", "w", 0 );
private var chcn_pay = TBFile( "chcn_pay.dbt", "w", 1 );
private var sb_casln = TBFile( "sb_casln.dbt", "r", 1 );
private var sbdepdoc = TBFile( "sbdepdoc.dbt", "w", 3 );

private var countChcnPay = 0;
private var refAccount   = 0;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindChcnPay

  var mainKind = 0;
  var mainKey  = "";
  var stat;
  var wasFind  = false;

  sb_casln.KeyNum = 1;
  sb_casln.Clear;
  sb_casln.rec.ApplicationKind2 = OC_PAY_DOC.iApplicationKind;
  sb_casln.rec.ApplicationKey2  = OC_PAY_DOC.ApplicationKey;

  if ( sb_casln.GetEQ )
    mainKind = sb_casln.rec.ApplicationKind1;
    mainKey  = sb_casln.rec.ApplicationKey1;

    sb_casln.KeyNum = 0;
    sb_casln.Clear;
    sb_casln.rec.ApplicationKind1 = mainKind;
    sb_casln.rec.ApplicationKey1  = mainKey;

    sb_casln.addFilter( "t.t_ApplicationKind1 = " + String( mainKind ) + " and " +
                        "t.t_ApplicationKey1 = " + GetSQLString( mainKey )        );

    stat = sb_casln.GetGE;

    while ( stat )
      if ( ( sb_casln.rec.ApplicationKind1 == mainKind ) and
           ( sb_casln.rec.ApplicationKey1  == mainKey  )    )

        chcn_pay.KeyNum = 1;
        chcn_pay.Clear;
        chcn_pay.rec.iApplicationKind = sb_casln.rec.ApplicationKind2;
        chcn_pay.rec.ApplicationKey   = sb_casln.rec.ApplicationKey2;

        if ( chcn_pay.GetEQ )
          wasFind = true;
          stat = false;
        else
          stat = sb_casln.Next;
        end;
      else
        stat = false;
      end;
    end;

    sb_casln.dropFilter;

  end;

  return wasFind;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindChcnRgst

  chcnrgst.Clear;
  chcnrgst.rec.RgtpID = chcn_pay.rec.RgtpID;
  chcnrgst.rec.RgstID = chcn_pay.rec.RgstID;

  return chcnrgst.GetEQ;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefCountChchnPayForChcnRgst

  var recPos = chcn_pay.GetPos;
  var rgtpID = chcn_pay.rec.RgtpID;
  var rgstID = chcn_pay.rec.RgstID;
  var retVal = true;
  var stat;

  chcn_pay.KeyNum = 0;
  chcn_pay.Clear;
  chcn_pay.rec.RgtpID = rgtpID;
  chcn_pay.rec.RgstID = rgstID;

  chcn_pay.addFilter( "t.t_RgtpID = " + String( rgtpID ) + " and " +
                      "t.t_RgstID = " + String( rgstID )             );

  stat = chcn_pay.GetGE;

  while ( stat )
    if ( ( chcn_pay.rec.RgtpID == rgtpID ) and
         ( chcn_pay.rec.RgstID == rgstID )    )
      countChcnPay = countChcnPay + 1;
      stat = chcn_pay.Next;
    else
      stat = false;
    end;
  end;

  chcn_pay.dropFilter;

  retVal = chcn_pay.GetDirect( recPos );

  return retVal;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro T_DelChcnPay

  var mainKind = 0;
  var mainKey  = "";
  var flag;
  var stat;

  // Снимим у удаляемых документов признак сконтированности
  stat = true;

  sb_casln.KeyNum = 1;
  sb_casln.Clear;
  sb_casln.rec.ApplicationKind2 = OC_PAY_DOC.iApplicationKind;
  sb_casln.rec.ApplicationKey2  = OC_PAY_DOC.ApplicationKey;

/*
  // Удалим документы, появившиеся в результате выплаты
  if ( not DeleteDocument( OC_PAY_DOC.iApplicationKind, OC_PAY_DOC.ApplicationKey, false ) )
    AbortTrn( );
  end;
*/   
  // Удалим открытый счет вкладчика для выплаты
  if ( ( countChcnPay <= 1 ) and ( chcnrgst.rec.Referenc != 0 ) )
    if ( DeleteDepAcc( chcnrgst.rec.Referenc, true ) != 0 )
      AbortTrn( );
    end;
  end;

  // Изменение в реестре
  if ( not chcnrgst.GetPos )
    AbortTrn( );
  end;

  if ( not chcnrgst.GetDirect )
    AbortTrn( );
  end;

  if ( countChcnPay <= 1 )
    chcnrgst.rec.BwasPay                = 0;
    chcnrgst.rec.DatePay                = Date( 0, 0, 0 );
    chcnrgst.rec.SumPay                 = 0;
    chcnrgst.rec.Percent                = 0;
    chcnrgst.rec.Tax                    = 0;
    chcnrgst.rec.Compens1               = 0;
    chcnrgst.rec.Compens2               = 0;
    chcnrgst.rec.Compens3x              = 0;
    chcnrgst.rec.Account                = "";
    chcnrgst.rec.DateLastOperation      = Date( 0, 0, 0 );
    chcnrgst.rec.RestAfterLastOperation = 0;
    chcnrgst.rec.Rest200691             = 0;
    chcnrgst.rec.Rest010192             = 0;
    chcnrgst.rec.Rest010793             = 0;
    chcnrgst.rec.Rest010195             = 0;
    chcnrgst.rec.Referenc               = 0;
    chcnrgst.rec.Branch                 = 0;
    chcnrgst.rec.NumSession             = 0;
    chcnrgst.rec.Compens40p             = 0;
    chcnrgst.rec.NumStepOperation       = 0;
  else
    chcnrgst.rec.NumStepOperation = 0;
  end;

  if ( not chcnrgst.Update )
    AbortTrn( );
  end;

  // Удаление собственно выплаты
  if ( not chcn_pay.GetPos )
    AbortTrn( );
  end;

  if ( not chcn_pay.GetDirect )
    AbortTrn( );
  end;

  if ( not chcn_pay.Delete )
    AbortTrn( );
  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  private var statChcnDel = true;

  // Определение выплаты
  if ( statChcnDel )
    statChcnDel = FindChcnPay( );
    if ( not statChcnDel )
      MsgBox( "Не найдена удаляемая выплата по вкладу бывшего ЧСБ" );
    end;
  end;

  // Есть ли еще выплаты помимо удаляемой
  if ( statChcnDel )
    statChcnDel = DefCountChchnPayForChcnRgst( );
    if ( not statChcnDel )
      MsgBox( "Ошибка при определении количества выплат по вкладу бывшего ЧСБ" );
    end;
  end;

  // Поиск реестра по выплате
  if ( statChcnDel )
    statChcnDel = FindChcnRgst( );
    if ( not statChcnDel )
      MsgBox( "Не найден реестр по удаляемой выплате по вкладу бывшего ЧСБ" );
    end;
  end;

  // Транзакция по удалению выплаты
  if ( statChcnDel )
    if ( not ProcessTrn( 1, "T_DelChcnPay" ) )
      MsgBox( "Ошибка при удалении выплаты по вкладу бывшего ЧСБ" );
      statChcnDel = false;
    end;
  end;

  OC_RESULT = END_RES; // Все сделали сами

end;
