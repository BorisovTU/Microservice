/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Электронно-цифровая подпись стандартного потока вывода                  *
*                                                                          *
*  Лебедев С.В.                                                            *
*  26.04.2005                                                              *
\**************************************************************************/

import DeprIntr, cryptdlm;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ElectronDigitalSignReport ( isRefs, needMes )

  var cryptoAPI = RsCryptoAPI( );
  var nameOutputFile = "";
  var writeESign = false;
  var stat = true;

  /* Уточним входные параметры, которые не обязательны */
  if ( isRefs == null )
    isRefs = false;
  end;

  if ( needMes == null )
    needMes = true;
  end;

  /* Определим роль ЭЦП */
  if ( isRefs )
    writeESign = true; //GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\ЭЦП\\ОТЧЕТЫ\\СПРАВОЧНИКИ", false );
  else
    writeESign = GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\ЭЦП\\ОТЧЕТЫ\\ОПЕРАТИВНАЯ_ИНФОРМАЦИЯ", false );
  end;

  if ( ValType( writeESign ) != V_BOOL )
    writeESign = false;
  end;

  /* Подпишем файл */
  if ( writeESign )
    nameOutputFile = setoutput( "nul" ); /* Переключимся со стандартного вывода, который подписываем */

    if ( cryptoAPI.SignFile( "ЯдроRSBank|ПодписьФайлов|НаложениеЭЦП", nameOutputFile ) )
      ;
    else
      if ( needMes )
        MsgBox( "Ошибка при простановке ЭЦП" );
      end;
      stat = false;
    end;

    setoutput( nameOutputFile, true ); /* Вернемся на стандартный вывод */
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro CheckESignFile ( nameFile, errmes, isTran, needMes )

  var cryptoAPI = RsCryptoAPI( );
  var checkESign = false;
  var stat = true;

  errmes = "";

  /* Уточним входные параметры, которые не обязательны */
  if ( isTran == null )
    isTran = false;
  end;

  if ( needMes == null )
    needMes = true;
  end;

  /* Определим роль ЭЦП */
  if ( isTran )
    checkESign = GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\ЭЦП\\ЗАГРУЖАЕМАЯ_ИНФОРМАЦИЯ\\ТРАНЗАКЦИИ_ИЗ_ПЦ", false );
  else
    checkESign = GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\ЭЦП\\ЗАГРУЖАЕМАЯ_ИНФОРМАЦИЯ\\СПИСКИ_ЗАЧИСЛЕНИЙ", false );
  end;

  if ( ValType( checkESign ) != V_BOOL )
    checkESign = false;
  end;

  /* Проверим файл */
  if ( checkESign )
    if ( cryptoAPI.CheckFileSign( "RS-Retail|Загрузка|ПроверкаЭЦП", nameFile, 0, errmes ) )
      ;
    else
      if ( needMes )
        if ( errmes!="" )
           MsgBox( "Процедура проверки ЭЦП вернула ошибку:|"+errmes );
        else
           MsgBox( "Неизвестная ошибка при проверке ЭЦП" );
        end;
      end;      
      stat = false;
    end;

  end;

  setParm( 1, errmes );
  return stat;

end;

