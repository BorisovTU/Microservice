/****************************************************************************

  R-Style SoftLab.

  RS-Retail.

  Сводная ведомость учета начисленных процентов по вкладам физических лиц.

*****************************************************************************/

import DeprIntr, Cur_Rate, SqlConv, RSD;

file opr ( person   ) key 0;  // Список пользователей.
file cr  ( currency ) key 0;  // Список валют.

record StatParm ( "StatParm.rec" );

const
  Binsert = 2;

const
  D_IN = 1,         /* Приход */
  D_OUT = 2,        /* Расход */
  D_GET = 3,        /* Списание */
  D_PUT = 5;        /* Зачисление */

var {oper};
var {curdate};
var {UseNewNumbering};

var NP_Params = TPcEstimTotalRecParams;

var sTime = String( Time() );
var tTime = SubStr( sTime, 1, 2 ) + "-" + SubStr( sTime, 4, 2 );

var isCur = 0;

var cDepType = "";
var mDepType = $0;

var ind = 0;

var mTotalCateg;  // Итого по категориям.
var mTotalRezid;  // Итого по резидентам/нерезидентам.
var mTotalSum;    // Общая сумма по Ведомости.

var sOper = "";

var NumRazd = 1;  // Номер раздела ведомости.

// Массивы общих сумм по валютам.
array aCurr, aMCurr, aMRub;

/*****************************************************************/
/*      Сообщения о нештатных ситуациях в серверной процедуре.   */
/*****************************************************************/
macro ServErrors( cmd, mes, str_err )
  var i = 0;
  var env_err = cmd.Connection().Environment();
  if ( mes != "" )
        msgbox( mes + "|" + str_err );
        println( mes + " " + str_err );
  else  msgbox( str_err );
        println( str_err );
  end;
  while( i < env_err.ErrorCount )
    println( env_err.error(i).descr );
    i = i + 1;
  end;
end;

/*****************************************************************/
/*                 Выполнение серверной процедуры.               */
/*****************************************************************/
macro ExecCommand( cmd, mes )

  // message( mes + " ~ Ждите...~" );
  cmd.execute;

  return TRUE;

  OnError( er );
  ServErrors( cmd, mes, er.message );
  return FALSE;

end;

/****************************************************************
       Получение суммы начисленных процентов по резидентам.
*****************************************************************/
macro getEstimRestExec( _DepType, _Curr, _Rez )

 var mNP_Sum = $0;

 var sINI1, sINI2;
 var env = RsdEnvironment( "RDDrvO", "RDDrvO.dll" );
 var con, cmd;
 var statExec = TRUE;

 var p_isCur       = 0;
 var p_FNCash      = NP_Params.branch;
 var p_depType     = _DepType;
 var p_calcPayDate = NP_Params.reportDate;
 var p_CodCurr     = _Curr;
 var p_Rezid;

 var p_calcSum = 0;
 var p_stat    = 0;

 if ( _Curr == 0 )
   p_isCur = 0;
 else
   p_isCur = 1;
 end;

 if ( _Rez )
   p_Rezid = 0;
 else
   p_Rezid = 1;
 end;

 sINI1 = GetIniString( "CONNSTRING", "rsreq.ini" );
 sINI2 = SubStr( sINI1, 1, Index( sINI1, "\\" ) - 1 );

 con = RsdConnection( env, sINI2 );
 cmd = RsdCommand( con, "" );

 cmd.CmdText = "begin RSU_RTLDEPOSIT.CalcEstimPcForDepType(?,?,?,?,?,?,?,?); end;";

 cmd.addParam( "", RSDBP_IN, p_isCur );
 cmd.addParam( "", RSDBP_IN, p_FNCash );
 cmd.addParam( "", RSDBP_IN, p_depType );
 cmd.addParam( "", RSDBP_IN, p_calcPayDate );
 cmd.addParam( "", RSDBP_IN, p_CodCurr );
 cmd.addParam( "", RSDBP_IN, p_Rezid );

 cmd.addParam( "a_calcSum", RSDBP_OUT, V_DOUBLE );
 cmd.addParam( "a_stat",    RSDBP_OUT, V_DOUBLE );

 statExec = ExecCommand( cmd, "" );

 p_calcSum            = cmd.value( "a_calcSum" );
 p_stat               = cmd.value( "a_stat" );

 cmd.close();

 if ( p_stat == 0 )
   mNP_Sum = p_CalcSum;
 else
   mNP_Sum = $0;
 end;

 return mNP_Sum;

end;

/****************************************************************
       Получение суммы начисленных процентов по резидентам.
*****************************************************************/
macro getEstimRest( _DepType, _Curr, _Rez )
 var cmd_e, rs_e;
 var retSum = $0;

 cmd_e = RsdCommand( "select 0 from ddepositr_dbt " +
                     "where t_type_account = \'" + _DepType   + "\' and " +
                           "t_Code_Currency = " + String( _Curr ) );

 cmd_e.execute;

 rs_e = RsdRecordset( cmd_e );

 if ( rs_e.moveNext() )
   retSum = getEstimRestExec( _DepType, _Curr, _Rez );
 else
   retSum = $0;
 end;

 cmd_e.close();

 return retSum;
end;

/****************************************************************
      Заголовок для вкладов до востребования резидентов.
*****************************************************************/
macro TitleDemandRez( _Num )
 [  |  #. По вкладам до востребования резидентов:                                                                          |
    |------------+------------------------------------------+--------+--------------------------+--------------------------|]
 ( _Num );
end;

/****************************************************************
      Заголовок для вкладов до востребования нерезидентов.
*****************************************************************/
macro TitleDemandNotRez( _Num )
 [  |  #. По вкладам до востребования нерезидентов:                                                                        |
    |------------+------------------------------------------+--------+--------------------------+--------------------------|]
 ( _Num );
end;

/****************************************************************
           Заголовок для срочных вкладов резидентов.
*****************************************************************/
macro TitleTermRez( _Num )
 [  |  #. По срочным вкладам резидентов:                                                                                   |
    |------------+------------------------------------------+--------+--------------------------+--------------------------|]
 ( _Num );
end;

/****************************************************************
          Заголовок для срочных вкладов нерезидентов.
*****************************************************************/
macro TitleTermNotRez( _Num )
 [  |  #. По срочным вкладам нерезидентов:                                                                                 |
    |------------+------------------------------------------+--------+--------------------------+--------------------------|]
 ( _Num );
end;

/****************************************************************
                   Очистка массивов по валютам.
*****************************************************************/
macro clearArrayCur()
 var i = 0, n = aSize( aCurr );
 while ( i < n )
   aMCurr( i ) = $0L;
   aMRub ( i ) = $0L;
   i = i + 1;
 end;
end;

/****************************************************************
                   Добавление в массив по валютам.
*****************************************************************/
macro addArrayCur( _iso, _sumCurr, _sumRub )
 var i = 0, n = aSize( aCurr );
 while ( i < n )
   if ( aCurr( i ) == _iso )
     aMCurr( i ) = aMCurr( i ) + _sumCurr;
     aMRub ( i ) = aMRub ( i ) + _sumRub;
     i = n;
   end;
   i = i + 1;
 end;
end;

/****************************************************************
                 Вывод итого в разрезе видов валют.
*****************************************************************/
macro ReportArrayCur()
 var i = 0, n = aSize( aCurr );
 var vMCurr;
 var fTitle = TRUE;
 var vTitle = "";
 var fRep = FALSE;
 while ( i < n )
   if ( aMRub( i ) > $0L )
     fRep = TRUE;  // Хотя бы одна строка выводится.
     if ( fTitle )
       fTitle = FALSE;
       vTitle = "ИТОГО в разрезе видов валют";
     else
       vTitle = "";
     end;
     if ( i == 0 )  // Национальная валюта.
       vMCurr = "";
     else
       vMCurr = String( aMCurr( i ):0:2:a );
     end;
     [  |            | ######################################## |   ###  | ######################## | ######################## |]
     (
       vTitle,
       aCurr( i ),
       vMCurr:r,
       aMRub( i ):0:2:a:r
     );
   end;
   i = i + 1;
 end;
 if ( fRep )
   [  |------------+------------------------------------------+--------+--------------------------+--------------------------|];
 end;
 clearArrayCur();
end;

/****************************************************************
                   Формирование строки отчета.
*****************************************************************/
macro ReportLine( _id, _name, _sum, _curr, _iso )
 var tID = Trim( _id );
 var sID = "";
 var sSum = "";
 var CBRate = $0L;
 var eSum   = $0L;

 if ( CodeFor( tID ) < CodeFor( " " ) )
   sID = "";
 else
   sID = tID;
 end;

 if ( NOT GetAllCurrRate( NP_Params.reportDate, _curr, CBRate ) )
   println( "*** Ошибка при определении курса валюты = ", _curr );
   return;
 end;

 if ( _curr == 0 )
   sSum = "";
 else
   sSum = String( _sum:0:2:a );
 end;

 eSum = _sum * CBRate;

 mDepType = mDepType + eSum;

 // Итого по категориям.
 mTotalCateg = mTotalCateg + eSum;
 // Итого по резидентам/нерезидентам.
 mTotalRezid = mTotalRezid + eSum;
 // Общая сумма по Ведомости.
 mTotalSum = mTotalSum + eSum;

 // Добавление в массив по валютам.
 addArrayCur( _iso, _sum, eSum );

 [  |    ######  | ######################################## |   ###  | ######################## | ######################## |]
 (
   sID,
   _name:w,
   _iso,
   sSum:r,
   eSum:0:2:a:r
 );
end;

/****************************************************************
          Формирование строки "Всего по виду вклада".
*****************************************************************/
macro ReportDepType()
 var rDepType = cDepType;

 if ( rDepType == "ZZZZ" )
   rDepType = "";
 end;

 [  |------------+------------------------------------------+--------+--------------------------+--------------------------|];
 [  |    ######  | Всего по виду вклада                     |        |                          | ######################## |]
 (
   rDepType,
   mDepType:0:2:a:r
 );
 [  |------------+------------------------------------------+--------+--------------------------+--------------------------|];
end;

/****************************************************************
          Формирование строки ИТОГО по категории.
*****************************************************************/
macro ReportTotalCateg( _title )
 [  |            | ######################################## |        |                          | ######################## |]
 (
   _title:w,
   mTotalCateg:0:2:a:r
 );
 [  |------------+------------------------------------------+--------+--------------------------+--------------------------|];
end;

/****************************************************************
      Формирование строки ИТОГО по резидентам/нерезидентам.
*****************************************************************/
macro ReportTotalRezid( _title )
 [  |            | ######################################## |        |                          | ######################## |]
 (
   _title:w:r,
   mTotalRezid:0:2:a:r
 );
 [  |------------+------------------------------------------+--------+--------------------------+--------------------------|];
end;

/****************************************************************
     Формирование строки ИТОГО по резидентам и нерезидентам.
*****************************************************************/
macro ReportTotalSum()
 [  |            | ИТОГО по резидентам и нерезидентам       |        |                          | ######################## |]
 (
   mTotalSum:0:2:a:r
 );
 [  |------------+------------------------------------------+--------+--------------------------+--------------------------|];
end;

/****************************************************************
   Формирование Ведомости по всем видам вкладов и всем валютам.
                     Вклады до востребования.
*****************************************************************/
macro from_DB_AllCurr( _Cat, _Rez )
 var cmd, rs;
 var uID = "";
 var mDepTypeProc = $0;
 var CurrMin;
 var cCateg;
 var vKind = "";

 // Переменные для условий where.
 var wIsCur = "";
 // var wDepCateg = "";
 // var wDepType = "";

 cDepType = "ZZZZ";
 mDepType = $0;

 clearArrayCur();  // Очистка массивов по валютам.

 if ( _Cat )  // Вклады до востребования.
   cCateg = "=";
 else         // Срочные вклады.
   cCateg = "!=";
 end;

 // Определение условий выборки по заданным параметрам выпуска Ведомости.

 if ( NP_Params.isCur == 3 )  // Валютность не задана.
   wIsCur = "";
 else                         // Валютность задана.
   if ( NP_Params.isCur == 1 )  // Рубли.
     wIsCur = "and t_FlagCur = 0 ";
   else                         // Валюта.
     wIsCur = "and t_FlagCur != 0 ";
   end;
 end;

 /*
 if ( NP_Params.depCateg == -1 )   // Все виды вкладов.
   wDepCateg = "";
 else
   if ( NP_Params.depCateg == 0 )  // Вклады до востребования.
     wDepCateg = "and t_FormContr = 0 ";
   else                            // Срочные виды вкладов.
     wDepCateg = "and t_FormContr != 0 ";
   end;
 end;

 if ( StrLen( NP_Params.depType ) == 0 )  // Вид вклада не задан.
   wDepType = "";
 else                                     // Вид вклада задан.
   wDepType = "and t_Kind = \'" + NP_Params.depType + "\' ";
 end;
 */

 if ( {UseNewNumbering} != 0 )  // Новая нумерация.
   cmd = RsdCommand( "select t_Kind as dKind, " +
                            "t_NewIdentChar as nID, " +
                            "t_Name as dName " +
                     "from dsb_dtyp_dbt " +
                     "where t_FormContr " + cCateg + " 0 " +
                            wIsCur +
                            // wDepCateg +
                            // wDepType +
                       " and exists ( select 0 from ddepositr_dbt d where d.t_iscur = t_flagcur and d.t_type_account = t_kind ) " +
                     "order by t_NewIdentChar, t_Kind, t_Name" );
 else                           // Старая нумерация.
   cmd = RsdCommand( "select t_Kind as dKind, " +
                            "t_IdentChar as nID, " +
                            "t_Name as dName " +
                     "from dsb_dtyp_dbt " +
                     "where t_FormContr " + cCateg + " 0 " +
                            wIsCur +
                            // wDepCateg +
                            // wDepType +
                       " and exists ( select 0 from ddepositr_dbt d where d.t_iscur = t_flagcur and d.t_type_account = t_kind ) " +
                     "order by t_IdentChar, t_Kind, t_Name" );
 end;

 cmd.execute;

 rs = RsdRecordset( cmd );

 while ( rs.moveNext() )

   if ( vKind != rs.value( "dKind" ) )

     message( "  Вклад \"" + rs.value( "dName" ) + "\"" );

     uID = rs.value( "nID" );

     ReWind( cr );

     while ( Next( cr ) )

       if ( ( cr.CrDifrRate )          AND  // Валюта используется.
            ( cr.Code_Currency > -1 )  AND  //
            ( cr.Code_Currency < 98 ) )     // Не драг.металл.

         if ( ( NP_Params.currCode == -1 )  OR  // Любая валюта.
              ( NP_Params.currCode == cr.Code_Currency ) )

           mDepTypeProc = getEstimRest( rs.value( "dKind" ),
                                        cr.Code_Currency,
                                        _Rez );

           if ( mDepTypeProc )

             if ( cDepType != uID )
               if ( cDepType != "ZZZZ" )
                 ReportDepType();
               end;
               cDepType = uID;
               mDepType = $0;
             end;

             ReportLine( uID,
                         rs.value( "dName" ),
                         mDepTypeProc,
                         cr.Code_Currency,
                         cr.ExternalCode );
           end;

         end;

       end;

     end;

   end;

   vKind = rs.value( "dKind" );

 end;

 ReportDepType();
 ReportArrayCur();

end;

/****************************************************************
        Формирование Ведомости по заданному виду вкладов.
*****************************************************************/
macro from_DepType( _Rez )
 var cmd, rs;
 var uID = "";
 var mDepTypeProc = $0;

 clearArrayCur();  // Очистка массивов по валютам.

 if ( NP_Params.isCur == 2 )
   isCur = 1;  // Валютный режим.
 else
   isCur = 0;  // Рублевый режим.
 end;

 cmd = RsdCommand( "select t_IdentChar as oID, " +
                          "t_NewIdentChar as nID, " +
                          "t_Kind as dKind, " +
                          "t_Name as dName " +
                   "from dsb_dtyp_dbt " +
                   "where t_FlagCur = " + String( isCur ) + " and " +
                         "t_Kind = \'" + NP_Params.depType + "\' " +
                   "order by t_Name" );

 cmd.execute;

 rs = RsdRecordset( cmd );

 while ( rs.moveNext() )
   if ( {UseNewNumbering} != 0 )
     uID = rs.value( "nID" );
   else
     uID = rs.value( "oID" );
   end;

   if ( isCur == 0 )  // Национальная валюта.

     mDepTypeProc = getEstimRest( rs.value( "dKind" ), 0, _Rez );

     ReportLine( uID,
                 rs.value( "dName" ),
                 mDepTypeProc,
                 0,
                 810 );
   else  // Виды вкладов в иностранной валюте.
     ReWind( cr );
     while ( Next( cr ) )
       if ( ( cr.CrDifrRate )         AND  // Валюта используется.
            ( cr.Code_Currency > 0 )  AND  // Иностранная валюта.
            ( cr.Code_Currency < 98 ) )    // Не драг.металл.

         if ( ( NP_Params.currCode == -1 )  OR  // Любая валюта.
            ( NP_Params.currCode == cr.Code_Currency ) )

           mDepTypeProc = getEstimRest( rs.value( "dKind" ),
                                        cr.Code_Currency,
                                        _Rez );

           if ( mDepTypeProc )
             ReportLine( uID,
                         rs.value( "dName" ),
                         mDepTypeProc,
                         cr.Code_Currency,
                         cr.ExternalCode );
           end;
         end;
       end;
     end;
   end;
 end;
end;

/****************************************************************
                        Головная процедура
*****************************************************************/

 if ( NOT NP_Params.Edit() )
   println( "\n Выпуск сводной Ведомости учета начисленных процентов отменен." );
   Exit();
 end;

 /*
 println( NP_Params.reportDate,  " | ",
          NP_Params.branch,      " | ",
          NP_Params.branchName,  " | ",
          NP_Params.isCur,       " | ",
          NP_Params.currCode,    " | ",
          NP_Params.notResident, " | ",
          NP_Params.depCateg,    " | ",
          NP_Params.depType ); */

 if ( NP_Params.isCur == 2 )
   isCur = 1;  // Валютный режим.
 else
   isCur = 0;  // Рублевый режим.
 end;

 /* Чтение записи Операциониста */
 opr.Oper = {oper};
 if ( GetEQ( opr ) )
   sOper = opr.Name;
 else
   sOper = "???";
 end;

 // Заполнение массива валют.
 ReWind( cr );
 ind = 0;

 while ( Next( cr ) )
   if ( ( cr.CrDifrRate )          AND  // Валюта используется.
        ( cr.Code_Currency > -1 )  AND  //
        ( cr.Code_Currency < 98 ) )     // Не драг.металл.
     aCurr( ind ) = cr.ExternalCode;
     ind = ind + 1;
   end;
 end;

 /*
 if ( GetTrue( FALSE, "Обновить статистику по вкладам за текущий день ?" ) )
   ClearRecord( StatParm );
   StatParm.Op           = Binsert;
   StatParm.KindOp       = D_PUT;
   StatParm.Branch       = NP_Params.branch;
   //StatParm.Type_Account = NP_Params.depType;
   StatParm.CodCur       = NP_Params.currCode;
   StatParm.Date         = {curdate};
   StatParm.IsCur        = isCur;
   //StatParm.FlagRez      = IsResident;
   //StatParm.EstimOverOut = SummaPay;

   println(StatParm.Branch);

   UpdateDepStats( StatParm );
 end;
 */

 [
                                                        СВОДНАЯ ВЕДОМОСТЬ
                                      учета начисленных процентов по вкладам физических лиц
                                              в разрезе видов вкладов и видов валют
                                                  по состоянию на #########################

        Подразделение (филиал/ОСБ): ###########################################     Дата печати: ############################
    +----------------------------------------------------------------------------------------------------------------------+
    |  Код вида  |           Краткое наименование           |  Код   |             Сумма начисленных процентов             |
    |   вклада   |               вида вклада                | валюты |--------------------------+--------------------------|
    |            |                                          |        |        в номинале        |         в рублях         |
    |            |                                          |        |          валюты          | (в рублевом эквиваленте) |
    |------------+------------------------------------------+--------+--------------------------+--------------------------|]
 (
   NP_Params.reportDate + " г.",
   NP_Params.branchName,
   tTime + "   " + String( Date() ) + " г."
 );

 mTotalCateg = $0L;  // Итого по категориям.
 mTotalRezid = $0L;  // Итого по резидентам/нерезидентам.
 mTotalSum   = $0L;  // Общая сумма по Ведомости.

 if ( StrLen( NP_Params.depType ) > 0 )  // По одному виду вкладов.
   // Резиденты.
   if ( ( NP_Params.notResident == 0 )  OR  // И резиденты и нерезиденты.
        ( NP_Params.notResident == 1 ) )    // Резиденты.
     cDepType = "";
     mDepType = $0;
     TitleDemandRez( NumRazd );
     NumRazd = NumRazd + 1;
     from_DepType( TRUE );
     ReportDepType();
   end;
   // Нерезиденты.
   if ( ( NP_Params.notResident == 0 )  OR  // И резиденты и нерезиденты.
          ( NP_Params.notResident == 2 ) )    // Нерезиденты.
     cDepType = "";
     mDepType = $0;
     TitleDemandNotRez( NumRazd );
     NumRazd = NumRazd + 1;
     from_DepType( FALSE );
     ReportDepType();
   end;
   ReportTotalSum();
 else                           // По всем видам вкладов.
   // *** Резиденты.
   if ( ( NP_Params.notResident == 0 )  OR  // И резиденты и нерезиденты.
        ( NP_Params.notResident == 1 ) )    // Резиденты.
     if ( ( NP_Params.depCateg == -1 )  OR  // Все вклады.
          ( NP_Params.depCateg ==  0 ) )    // До востребования.
       // Вклады до востребования.
       TitleDemandRez( NumRazd );
       NumRazd = NumRazd + 1;
       from_DB_AllCurr( TRUE, TRUE );
       ReportTotalCateg( "ИТОГО по вкладам до востребования резидентов" );
     end;
     if ( ( NP_Params.depCateg == -1 )  OR  // Все вклады.
          ( NP_Params.depCateg ==  1 ) )    // Срочные вклады.
       // Срочные вклады.
       mTotalCateg = $0L;  // Итого по категориям.
       TitleTermRez( NumRazd );
       NumRazd = NumRazd + 1;
       from_DB_AllCurr( FALSE, TRUE );
       ReportTotalCateg( "ИТОГО по срочным вкладам резидентов" );
     end;
     ReportTotalRezid( "ИТОГО по резидентам" );
   end;
   // *** Нерезиденты.
   if ( ( NP_Params.notResident == 0 )  OR  // И резиденты и нерезиденты.
        ( NP_Params.notResident == 2 ) )    // Нерезиденты.
     mTotalRezid = $0L;  // Итого по резидентам/нерезидентам.
     if ( ( NP_Params.depCateg == -1 )  OR  // Все вклады.
          ( NP_Params.depCateg ==  0 ) )    // До востребования.
       // Вклады до востребования.
       mTotalCateg = $0L;  // Итого по категориям.
       TitleDemandNotRez( NumRazd );
       NumRazd = NumRazd + 1;
       from_DB_AllCurr( TRUE, FALSE );
       ReportTotalCateg( "ИТОГО по вкладам до востребования нерезидентов" );
     end;
     if ( ( NP_Params.depCateg == -1 )  OR  // Все вклады.
          ( NP_Params.depCateg ==  1 ) )    // Срочные вклады.
       // Срочные вклады.
       mTotalCateg = $0L;  // Итого по категориям.
       TitleTermNotRez( NumRazd );
       NumRazd = NumRazd + 1;
       from_DB_AllCurr( FALSE, FALSE );
       ReportTotalCateg( "ИТОГО по срочным вкладам нерезидентов" );
     end;
     ReportTotalRezid( "ИТОГО по нерезидентам" );
   end;
   // Общая итоговая сумма.
   if ( NP_Params.notResident == 0 )  // И резиденты и нерезиденты.
     ReportTotalSum();
   end;
 end;

 [


           Подписи ответственных лиц: ________________________________ / #]
 ( sOper + " /" );
