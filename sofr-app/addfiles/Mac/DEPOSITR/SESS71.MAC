import CommonInter;

private var ComSpec = GetEnv( "COMSPEC" );
private var ImportDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\IMPORTMESDIR", true );
var ErrCode, ErrText;

macro ConvertPcEstim( ):bool

  file old ( "pcestim.old", "sess71.def" ) key 9999;
  file new ( "pcestim.dbt", "sess71.def" ) write;
  file pcestim ( pcestim ) key 0;

  var Name      = "";
  var dirFile   = ImportDir;
  var NumDayDoc = 100;
  var find_last = false;
  var len_f     = 0;

  if ( StrLen( dirFile ) > 0 )
    if ( SubStr( dirFile, StrLen( dirFile), 1 ) != "\\" )
      dirFile = dirFile + "\\";
    end;
  end;

  GetParm( 0, Name );

  len_f = GetFactRecLenForFile( dirFile + Name + ".dbt" );
  if ( ( len_f < 100 ) and ( len_f > 0 ) )
    ;
  else
    return true;
  end;

  message( "Конвертация файла pcestim" );
  println( "Конвертация файла pcestim" );

  DelFile( dirFile + Name + ".old" );
  if ( Run( ComSpec, "/C ren " + dirFile + Name + ".dbt " + Name + ".old" ) )
    println( "Не удалось переименовать файл " + Name );
    return FALSE;
  end;

  if ( NOT Create( new, dirFile + Name + ".dbt" ) )
    println( "Не удалось создать файл " + Name );
    return FALSE;
  end;

  if ( NOT Open( old, dirFile + Name + ".old" ) )
    println( "Не удалось открыть файл " + Name );
    return FALSE;
  end;

  if ( NOT Open( new, dirFile + Name + ".dbt" ) )
    println( "Не удалось открыть файл " + Name );
    return FALSE;
  end;

  var rec = 0;

  ReWind( old );
  while ( Next( old ) )

    ClearRecord( pcestim );
    pcestim.Referenc   = old.Referenc;
    pcestim.DateOperat = old.DateLastCalc;
    pcestim.NumDayDoc  = 32000;

    if (   GetLE ( pcestim )                        and 
         ( pcestim.Referenc   == old.Referenc     ) and 
         ( pcestim.DateOperat == old.DateLastCalc )    )
      NumDayDoc = pcestim.NumDayDoc + 100;
    else
      NumDayDoc = 100;
    end;

    ClearRecord( pcestim );
    pcestim.Referenc   = old.Referenc;
    pcestim.DateOperat = Date( 31, 12, 2099 );
    pcestim.NumDayDoc  = 0;

    if ( GetLE ( pcestim ) and ( pcestim.Referenc == old.Referenc ) )
      find_last = true;
    end;

    ClearRecord( new );
    new.FNCash             = old.FNCash;
    new.Referenc           = old.Referenc;
    new.DateOperat         = old.DateLastCalc;
    new.NumDayDoc          = NumDayDoc;
    new.DateEndPeriod      = old.DateEndPeriod;
    new.SummaProgn         = old.SummaProgn;
    new.DateLastCalc       = old.DateLastCalc;
    new.SummaCalc          = old.SummaCalc;
    new.DateLastPay        = old.DateLastPay;
    new.SummaPay_Rest      = old.SummaPay;
    new.SummaPay_97        = old.SummaPay_97;
    new.NumSession         = old.NumSession;

    if ( find_last )
      if ( new.DateOperat >= pcestim.DateOperat )
        if ( pcestim.SummaPay_Rest > new.SummaPay_Rest )
          new.SummaPay_Out = pcestim.SummaPay_Rest - new.SummaPay_Rest;
        else
          new.SummaPay_In = new.SummaPay_Rest - pcestim.SummaPay_Rest;
        end;
      end;
    end;

    if ( Insert( new ) )
      rec = rec + 1;
    else
      println( "\n Ошибка при копировании: ",
               old.CodClient, " - ",
               old.Sname, " - ",
               old.Name, " - ",
               old.Pname );
      ErrCode = Status( ErrText );
      println( ErrCode, ": ", ErrText );
    end;
  end;

  if ( rec == 0 )
    println( "\n Не скопировано ни одной записи в файле " );
  else
    println( "\n Скопировано записей: ", rec );
  end;
  println( "" );
  message( "" );

  return true;
end;
