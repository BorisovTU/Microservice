/*
$Name:             IFRSReport.mac
$Module:           RS-Retail
$Description:      отчёт по процедуре "Дополнительная обработка вкладных операций по правилам МСФО"
*/


import CommonInter, rsd;


// Базовое имя файла отчёта
private const REP_FILE_NAME = "IFRSReport";

// Имя каталога с файлом отчёта
private const REP_DIR = GetRegVal("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR");


//----------------------------------------------------------------------------
private var {cnum};

private var count = 0;
private var errCount = 0;

private var fullRepName = "";


//----------------------------------------------------------------------------
private macro getAccount(ref: integer)
  var cmd = RsdCommand(
      "SELECT t_Account, t_IsCur, t_Type_Account " +
      "FROM ddepositr_dbt " +
      "WHERE t_Referenc = ? ");

  cmd.addParam("ref", RSDBP_IN, ref);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  return rs;
end;


//----------------------------------------------------------------------------
private macro getOpName(operType: integer): string
  var name = "";

  var cmd = RsdCommand(
      "SELECT t_szNameAlg " +
      "FROM dnamealg_dbt " +
      "WHERE t_iTypeAlg = 8225 " +
      "  AND t_iNumberAlg = ? ");

  cmd.addParam("operType", RSDBP_IN, operType);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  if (rs.moveNext())
    name = rs.value("t_szNameAlg");
  else
    name = string(operType);
  end;

  return name;
end;


//----------------------------------------------------------------------------
private macro getErrDescr(stat: integer): string
  var descr = "";

  var cmd = RsdCommand(
      "SELECT t_Contents " +
      "FROM dsbbank_msg " +
      "WHERE t_Number = ? ");

  cmd.addParam("stat", RSDBP_IN, stat);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  if (rs.moveNext())
    descr = rs.value("t_Contents");
  end;

  return descr;
end;


//----------------------------------------------------------------------------
private macro makeFullRepName(): string
  var name = REP_DIR + "\\" + REP_FILE_NAME;

  var y, m, d;
  DateSplit(date(), d, m, y);
  var h, mi, s;
  TimeSplit(time(), h, mi, s);
  name = name + "_" +
    string(y:o:4) + string(m:o:2) + string(d:o:2) + "_" +
    string(h:o:2) + string(mi:o:2) + string(s:o:2) + ".";
  if ({cnum})
    name = name + {cnum};
  else
    name = name + "txt";
  end;

  return name;
end;


//----------------------------------------------------------------------------
private macro getFullRepName(): string
  if (fullRepName == "")
    fullRepName = makeFullRepName();
  end;

  return fullRepName;
end;


//----------------------------------------------------------------------------
macro printHeader()
  SetOutput(getFullRepName(), true);

  [                                 Дополнительная обработка вкладных операций по правилам МСФО                                ];
  [ ];
  [ ];
  [  Операции, обработка которых завершилась с ошибкой:];
  [ ];
  [+----------------------+------------+---------------+----------------------------------------------------------------------+];
  [|         Счет         |    Дата    |    Операция   |                              Ошибка                                  |];
  [+----------------------+------------+---------------+----------------------------------------------------------------------+];

  SetOutput(null, true);
end;


//----------------------------------------------------------------------------
macro printFooter()
  SetOutput(getFullRepName(), true);

  [ ];
  [ ];
  [  Обработано операций: #](count : l);
  [  Операций с ошибками: #](errCount : l);
  [ ];
  [ ];
  [ ];
  [ ];

  SetOutput(null, true);
end;


//----------------------------------------------------------------------------
macro printAccount(ref: integer, operType: integer, depDate: date, msg: string)
  SetOutput(getFullRepName(), true);

  count = count + 1;

  if (msg != "")
    errCount = errCount + 1;

    var account = "";
    var opName = "";

    var accRs = getAccount(ref);
    if (accRs.moveNext())
      account = accRs.value("t_Account");
    end;

    opName = getOpName(operType);

    [| #################### | ########## | ############# | #################################################################### |]
        (account, depDate, opName : w, msg : w);
    [+----------------------+------------+---------------+----------------------------------------------------------------------+];
  end;

  SetOutput(null, true);
end;


