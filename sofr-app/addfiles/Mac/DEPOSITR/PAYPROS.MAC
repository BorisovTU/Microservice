/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Пластиковые карточки                                                    *
*                                                                          *
*  Сторнирование/удаление кассовой операции выплаты по карточкам           *
*                                                                          *
*  Лебедев С.В.                                                            *
*  11.08.2000                                                              *
\**************************************************************************/

import DeprIntr;

/* Глобальные переменные ------------------------------------------------ */
record OC_PAY_KIND (pay_kind     ); /* Вид платежа */
record OC_PAY_DOC  ("pay_doc.n36"); /* Документ    */

/* Файлы для сторнирования ---------------------------------------------- */
file reestr  (reestr,"exchange.def") key 0 write;
file pay_doc (pay_doc              ) key 2 write;

record pay_doc_n36 ("pay_doc.n36","sbbank.def");

/* Коды возврата */
const OK_RES   =  0;
const SKIP_RES =  1;
const END_RES  =  2;
const ERR_RES  = -1;


/**************************************************************************\
|              Транзакция по удалению/сторнированию документа              |
\**************************************************************************/
macro TrnDelStornDoc

  var stat = TRUE;
  var pay_doc_varlen;

  if (stat)
    stat = GetPos(reestr);
  end;

  if (stat)
    stat = GetDirect(reestr);
  end;

  if (stat)
    if (OC_PAY_DOC.GroupOpert == 41)
      reestr.OutSum     = reestr.OutSum - OC_PAY_DOC.InSum;
      reestr.NumberDocs = reestr.NumberDocs - 1;
    else
      reestr.IncomeSum  = reestr.IncomeSum - OC_PAY_DOC.InSum;
      reestr.NumberDocs = reestr.NumberDocs - 1;
    end;
    if (reestr.IncomeSum < 0)
      reestr.IncomeSum = 0;
    end;
    if (reestr.OutSum < 0)
      reestr.OutSum = 0;
    end;
    if (reestr.NumberDocs < 0)
      reestr.NumberDocs = 0;
    end;
    stat = Update(reestr);
  end;

  /* При сторнировании документа сбросим ссылку на реестр */
  if (stat AND (OC_OPERATION == 1))
    pay_doc.iApplicationKind = OC_PAY_DOC.iApplicationKind;
    pay_doc.ApplicationKey   = OC_PAY_DOC.ApplicationKey;
    if (GetEQ(pay_doc))
      pay_doc_varlen = GetVarSize(pay_doc);
      SetRecordAddr(pay_doc_n36,pay_doc,0,0,TRUE);
      if (pay_doc_n36.ReestrID > 0)
        pay_doc_n36.ReestrID = 0;
        stat = Update(pay_doc,pay_doc_varlen);
      end;
    end;
  end;

  if ( stat )
    if ( OC_OPERATION == 1 )
      stat = DeleteDocument( OC_PAY_DOC.iApplicationKind, OC_PAY_DOC.ApplicationKey, true );
    else
      stat = DeleteDocument( OC_PAY_DOC.iApplicationKind, OC_PAY_DOC.ApplicationKey, false );
    end;
  end;

  if ( NOT stat )
    AbortTrn( );
  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  OC_RESULT = OK_RES;

  if (OC_PAY_DOC.ReestrID > 0)

    /* Найдем реестр */
    if (OC_RESULT == OK_RES)
      reestr.Branch = OC_PAY_DOC.FNCash;
      reestr.ID     = OC_PAY_DOC.ReestrID;
      if (NOT GetEQ(reestr))
        MsgBox("Не найден реестр, в который входит документ");
        OC_RESULT = END_RES;
      end;
    end;

    /* Открытие файлов */
    if (OC_RESULT == OK_RES)
      if (NOT OpenDepFiles())
         MsgBox("Ошибка при открытии файлов для сторнирования/удаления");
         OC_RESULT = END_RES;
      end;
    end;

    if (OC_RESULT == OK_RES)
      if (NOT ProcessTrn(NULL,"TrnDelStornDoc",reestr,pay_doc))
         MsgBox("Ошибка при сторнировании/удалении");
         OC_RESULT = END_RES;
      end;
    end;

    CloseDepFiles();

    if (OC_RESULT == OK_RES)
      OC_RESULT = END_RES; /* Все сделали сами */
    end;

  end;

end;
