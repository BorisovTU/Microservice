/* Перевод счетов в неподвижные/объединенные */

import deprintr, T2S_Comm;

macro PrintUnderLn(Str, UlChr)

  var
    Len = StrLen(Str),
    i = 0;

  PrintLn(" " + Str);
  Print(" ");
  while(i < Len)
    Print(UlChr);
    i = i + 1;
  end;
  PrintLn;
end;

macro OutRepHead

  [ ];
  [Протокол перевода вкладов в неподвижные/объединенные               ##################################]
  ({curdate}:m:r);
  [ ];
  [-----------------------------------------------------------------------------------------------------];
  [: Счет вкладчика          : Дата откр. : Остаток счета    : Неподвижный/Объединенный : Статус       :];
  [-----------------------------------------------------------------------------------------------------];
end;

macro TreatDepTypeForCurr

  var
    CurPos,
    NextPos,
    StatStr,
    RecFound;

  StaticAcc = "";
  IsFirstForCurr = true;
  ClearRecord(Dep);
  Dep.IsCur = IsCur;
  Dep.FNCash = Branch;
  Dep.Open_Close = StrFor(0);
  Dep.Type_Account = DepType.Kind;
  Dep.Code_Currency = Curr.Code_Currency;
  RecFound = GetGE(Dep)
    and (Dep.IsCur == IsCur)
    and (Dep.FNCash == Branch)
    and (Dep.Open_Close == StrFor(0))
    and (Dep.Type_Account == DepType.Kind)
    and (Dep.Code_Currency == Curr.Code_Currency);
  while(RecFound)
    CurPos = GetPos(Dep);
    if(Next(Dep)
      and (Dep.IsCur == IsCur)
      and (Dep.FNCash == Branch)
      and (Dep.Open_Close == StrFor(0))
      and (Dep.Type_Account == DepType.Kind)
      and (Dep.Code_Currency == Curr.Code_Currency))
      NextPos = GetPos(Dep);
    else
      NextPos = 0;
    end;
    GetDirect(Dep, CurPos);
    if((Dep.SvodAccount == "") and CheckAndSetStaticAccType)
      TreatAccount;
      if(TrnStat)
        StatStr = "Нормально";
      else
        StatStr = "Ошибка";
      end;
      if(IsFirstForType)
        PrintUnderLn(DepType.Name, "=");
        IsFirstForType = false;
      end;
      if(IsCur and IsFirstForCurr)
        PrintUnderLn(Curr.Name_Currency, "-");
        IsFirstForCurr = false;
      end;
      [ #########################   ########## ################## #########################  ############### ]
      (
        Acc_Form(Dep.Account),
        Dep.Open_Date,
        Dep.StaticRest,
        Acc_Form(StaticAcc),
        StatStr
      );
    end;
    if(NextPos != 0)
      GetDirect(Dep, NextPos);
    else;
      RecFound = false;
    end;
  end;
end;

macro TreatDepType

  var
    RecFound;

  IsFirstForType = true;
  if(not IsCur)
    Curr.Code_Currency = 0;
    if(GetEQ(Curr))
      TreatDepTypeForCurr;
    end;
  else
    Curr.Code_Currency = 1;
    RecFound = GetGE(Curr);
    while(RecFound)
      TreatDepTypeForCurr;
      RecFound = Next(Curr);
    end;
  end;
end;

macro TransferToStatics;

  var
    RecFound;

  KeyNum(Dep, 1);
  KeyNum(DepDoc, 6);
  if(BranchStatus == BS_BRANCH)
    OpenOrCreateTrFiles(Branch);
  end;
  OutRepHead;
  ClearRecord(DepType);
  DepType.FlagCur = IsCur;
  RecFound = GetGE(DepType);
  while(RecFound and (DepType.FlagCur == IsCur))
    if((DepType.FormContr == 0)
      and (DepType.Kind != "Шаблонный")
      and (DepType.Kind != "Служебный")
      and (DepType.Kind != SA_STATIC)
      and (DepType.Kind != SA_UNITED))
      TreatDepType;
    end;
    RecFound = Next(DepType);
  end;
end;

/*    		 О С Н О В Н А Я   Ф У Н К Ц И Я              */

  msgbox("На данный момент данная функциональность не поддерживается!");
/*  TransferToStatics;*/
end;

