/*
   Макрос вывода на печать отчета о документах, помещенных в сессию
*/

import acc_form;

macro GetOpName( doc )
   var Name = "";

   file OpTypes ("sb_typop.dbt");

   KeyNum( OpTypes, 0 );
   ReWind( OpTypes    );
   OpTypes.IsCur    = doc.IsCur;
   OpTypes.Kind     = doc.Type_Account;
   OpTypes.NumOpert = doc.TypeOper;
   if( GetEQ( OpTypes ) )
      Name  = Trim( OpTypes.czNameAlg );
   end;

   return Name;
end;

macro OutHead( Top:date, Bot:date )
[             Отчет о документах, помещенных в сессию
               период c ############ по ############
  +==========================+==============+========================+
  |       Номер счета        |     Дата     | Информация об операции |
  +==========================+==============+========================+]
  ( Top, Bot );
end;

macro OutLine( Account:string, Date_Doc:date, Ground:string )
[ | ######################## | ############ | ###################### | ]
   ( Acc_Form(Account):r, Date_Doc:r, Ground:l );
[ +--------------------------+--------------+------------------------+ ];
end;

macro SessionReport():bool
   var stat    = True;
   var Path    = "";
   var TextDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true );
   var IndPath = "inddoc." + String( UserNumber() );
   var RepFile = TextDir + "report." + String( UserNumber() );
   var Ground  = "";
   var TopDate:date;
   var BotDate:date;

   file sessdoc ( "sbdepdoc.1"   ) key 9999;
   file inddoc  ( "sbdepdoc.dbt" ) key 0;
   file rep () txt 250;
 
   GetParm( 0, Path );

   if( stat = Open( sessdoc, Path ) )
      if( stat = Create( inddoc, IndPath ) )
         if( stat = Open( inddoc, IndPath, 1 ) )
            while( stat and next( sessdoc ) )
               copy( inddoc, sessdoc );
               stat = Insert( inddoc );
               if( not stat )
                  MsgBox( "Ошибка при формировании индексного файла!!!" );
               end;
            end;
         else
            MsgBox( "Ошибка создания файла ", IndPath );
         end;
      else
         MsgBox( "Ошибка открытия файла ", IndPath );
      end;
   else
      MsgBox( "Ошибка открытия файла ", Path );
   end;

   if( stat )
      rewind( inddoc );
      next( inddoc );
      TopDate = inddoc.Date_Document;
      rewind( inddoc );
      prev( inddoc );
      BotDate = inddoc.Date_Document;

      DelFile( RepFile );

      SetOutput( RepFile, True );
      OutHead( TopDate, BotDate );
      SetOutput( Null, True );

      rewind( inddoc );
      while( next( inddoc ) )
         if( not IsServDoc( inddoc, "" ) )
            Ground = "Служебный документ";
         else
            Ground = GetOpName( inddoc );
         end;
         SetOutput( RepFile, True );
         OutLine( inddoc.Account, inddoc.Date_Document, Ground );
         SetOutput( Null, True );
      end;
   end;

   if( not stat )
     MsgBox( "Ошибка формирования отчета по сессии" );
   else
     Open( rep, RepFile );
     Close( rep );
     ViewFile( rep );
   end;

   return stat;
end;