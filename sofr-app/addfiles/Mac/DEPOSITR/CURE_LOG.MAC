/*
   Ведение протокола конвертирования.
   Основной макрос заимствован из Белгородского конвертера А.Ромодина.

  25.09.1998 Сияльский В. Д.
  --------------------------------------------------------------------
  21.09.1999 Рыжиков А. А. - изменил чтение параметров для вывода
*/

/* Получить полное имя пути из составляющих. Возвращает полное имя файла. */
macro fnmerge
(
  Path,         /* Сюда помещается полное имя, оно же возвращается. */
  Dir,          /* Директория вместе с диском                       */
  Fname,        /* Имя файла без расширения                         */
  Ext           /* Расширение                                       */
)
  Path = "";
  Dir = trim(Dir);  Fname = trim(Fname);  Ext = trim(Ext);
  if( Dir != "" )
    if( substr( Dir, strlen(Dir), 1 ) != "\\" )
      Dir = Dir + "\\";
    end;
    Path = Path + Dir;
  end;
  if( Fname != "" )
    Path = Path + Fname;
    if( Ext != "" )
      if( substr( Fname, strlen(Fname), 1 ) != "." )
        Path = Path + ".";
      end;
      if( substr( Ext, 1, 1 ) == "." ) Ext = substr( Ext, 2 ); end;
      Path = Path + Ext;
    end;
  end;
  setparm( 0, Path );
  return Path;
end;

class clProtocol ( ProtocolDir:string, Ext:string )
  const M_VERYERROR = 1;  /* Ошибка, ведущая к прекращению работы */
  const M_ERROR     = 2;  /* Ошибка               */
  const M_WARN      = 3;  /* Предупреждение       */
  const M_MESSAGE   = 4;  /* Сообщение            */
  const M_TITLE     = 5;  /* Заголовок            */
  const M_FOOTER    = 6;  /* Подвал               */

  const Width       = 80; /* Ширина отчета        */
  var   NameLogFile = "", /* Файл протокола                          */
        ErrorCode   = 0,  /* Код последней ошибки Btrieve            */
        ErrorText   = ""; /* Текст по коду последней ошибки Btrieve  */

  macro Out       /* Вывод сообщения в протокол - невиртуальный метод */
  (
   MsgCode,       /* Вид сообщения        */
   MsgText        /* Текст сообщения      */
  )
  /*
    Вывод сообщения в файл протокола
  */
    macro OutStr( /* [just] */ )
      var LenStr, pos;
      var Str1 = MsgText, Str2, just;
      var NumPos;
      var Stat = TRUE;

      /* По умолчанию выравнивание - влево. */
      if( not getparm( 0, just ) ) just = "l"; end;
      just = strupr( just );

      while( Stat )
        NumPos = index( Str1, "|" );
        if( NumPos > 0 )
          Str2 = substr( Str1, 1, NumPos-1 );
          Str1 = substr( Str1, NumPos+1 );
        else
          Stat = FALSE;
          Str2 = Str1;
        end;
        LenStr = strlen( Str2 );
        if( just == "C" )
          if( LenStr < (Width-2) )
            Str2 = mkstr( " ", (Width-LenStr)/2 ) + Str2;
          end;
        end;
        println( mkstr( " ", pos ) + Str2 );
      end;
    end;

    if (NameLogFile != "")
      SetOutput (NameLogFile, True);
      if (MsgCode == M_VERYERROR)
        print (" Ошибка: ");
        println (MsgText);
        print (" Работа прервана ");
        println ("");
      elif (MsgCode == M_TITLE)
        println ("");
        OutStr ("c"); /* Вывести центрированную строку */
        println ("");
      else
        if (MsgCode == M_ERROR)
          print (" Ошибка: ");
        elif (MsgCode == M_WARN)
          print (" Предупреждение: ");
  /*      elif (MsgCode == M_MESSAGE)  /* Пока это печатать не будем */
          print (" Сообщение: ");  */
        end;
        OutStr ();
      end;
      SetOutput (Null, True);
    end;
  end;

  macro BigError()
     var   Text = "";
     var   i    = 1;
     var   MsgParam;

     while( Getparm( i, MsgParam ) )
        Text = Text + String( " ", MsgParam );
        i    = i + 1;
     end;

     this.Out( M_VERYERROR, Text );
  end;

  macro Error()
     var   Text = "";
     var   i    = 1;
     var   MsgParam;

     while( Getparm( i, MsgParam ) )
        Text = Text + String( " ", MsgParam );
        i    = i + 1;
     end;
     this.Out( M_ERROR, Text );
  end;

  macro Warning()
     var   Text = "";
     var   i    = 1;
     var   MsgParam;

     while( Getparm( i, MsgParam ) )
        Text = Text + String( " ", MsgParam );
        i    = i + 1;
     end;

     this.Out( M_WARN, Text );
  end;

  macro Message()
     var   Text = "";
     var   i    = 1;
     var   MsgParam;

     while( Getparm( i, MsgParam ) )
        Text = Text + String( " ", MsgParam );
        i    = i + 1;
     end;
     this.Out( M_MESSAGE  , Text );
  end;

  macro BtrError()
     var   Text = "";
     var   i    = 1;
     var   MsgParam;

     while( Getparm( i, MsgParam ) )
        Text = Text + String( " ", MsgParam );
        i    = i + 1;
     end;

     ErrorCode = status( ErrorText );

     this.Error  ( Text );
     this.Message("      Код ошибки "+string(ErrorCode)+": "+ErrorText);
  end;

  macro Header( ConvVersion )
    this.Out( M_TITLE,
    "----- Конвертирование данных в RS-Retail ----- "+
    string(date()) + " ----- " + string(time()) + " -----" +
    "----------------------- Версия " + ConvVersion +
    "-----------------------------------------------" );
  end;
  macro Footer()
    this.Out( M_TITLE,
    "----- Конвертирование закончено -------------- "+
    string(date()) + " ----- " + string(time()) + " -----" );
  end;

  /* Показать файл протокола */
  macro View()
    file LogFile() txt 250;         /* Для просмотра протокола */

    if( ExistFile( NameLogFile ) )
      if( open( LogFile, NameLogFile ) )
        close( LogFile );
        ViewFile( LogFile );
      end;
    end;
  end;

  /* Инициализатор */
  fnmerge( NameLogFile, ProtocolDir, "sfolc_log", ext );

  if( ExistFile( NameLogFile ) )
   /*and getTrue( True, "Очистить файл протокола?" ) )*/
    SetOutput( NameLogFile, False );
    SetOutput();
  end;
end;
