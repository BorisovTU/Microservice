/* Функция расчета суммы просрочки по счетам Retail (prj980) */
import CommonInter, rsd, cb_sql;

record OpenBkAcc( "OpnBkAcc.rec" );

/* Получение номера счета по шаблону из dsb_acdep_dbt */
macro ParseTmplAccount(Branch, CodeCurrency, TmplAccount)
  var ParseAcc = "", Account = "";
  var AccountLen = 0;
  var pos = 1, symb = "";
  var len = 0;
  
  ClearRecord( OpenBkAcc );
  OpenBkAcc.Branch = Branch;
  OpenBkAcc.CodCur = CodeCurrency;
  OpenBkAcc.Account = TmplAccount;
  ParseAcc = OpenSymbolsInAccount(OpenBkAcc);
  len = strlen(ParseAcc);
  /* Без точек */
  while( pos <= len )
    symb = substr(ParseAcc,pos,1);
    if( symb != "." )
      Account = Account + symb;
    end;
    pos = pos + 1;
  end;

  /* Без 9-го символа */
  AccountLen = strlen(Account);
  if( AccountLen > 8 )
    Account = substr(Account,1,8) + substr(Account,10,AccountLen-9);
  end;

  return Account;
end;

/* Получение суммы задолженности по подразделению Branch */
macro GetIndebtRest(Branch, AccNumber, CodeCurrency, RegDate, ReservDate)
  var cmdstr = "";
  var cmd, rs;
  var isCur = 0;
  var BalAcc = substr(AccNumber,1,5) + "%";
  var AccNumberLen = strlen(AccNumber);
  var CmpAccount = AccNumber;
  var Account = "";
  var BranchSum = 0.0;

  if( CodeCurrency != 0 )
    isCur = 1;
  end;
  /* Без 9-го символа */
  if( AccNumberLen > 8 )
    CmpAccount = substr(AccNumber,1,8) + substr(AccNumber,10,AccNumberLen-9);
  end;
  /* Определение, есть ли среди счетов подразделения счета по учету требований (47423) */
  cmdstr = "SELECT T_ACCOUNT FROM dsb_acdep_dbt"
           " WHERE T_ISCUR=? AND T_FNCASH=? AND T_CODCUR=?"
           " AND T_ACCOUNT LIKE ? AND T_SPECVARIABLE='Счет требований'";
  cmd = RsdCommand(cmdstr);
  cmd.addParam("ISCUR", RsdBp_in);
  cmd.addParam("FNCASH", RsdBp_in);
  cmd.addParam("CODCUR", RsdBp_in);
  cmd.addParam("ACCOUNT", RsdBp_in);
  cmd.value("ISCUR") = isCur;
  cmd.value("FNCASH") = Branch;
  cmd.value("CODCUR") = CodeCurrency;
  cmd.value("ACCOUNT") = BalAcc;
  cmd.execute();
  rs = RsdRecordset(cmd);

  if( rs.moveNext() )
    Account = ParseTmplAccount(Branch, CodeCurrency, rs.value(0));
    if( Account == CmpAccount )
      /* Подсчет сумм всех задолженностей, кроме: 2-Погашенных, 3-Отмененных, 4-Сторнированных */
      cmdstr = "SELECT /*+index (indebt ddpindebt_dbt_idx3) */" 
               " SUM(RSU_RTLDEPOSIT.GetIndebtRestOnNeedDate(indebt.T_BRANCH, indebt.T_INDEBTID, ?)) as BranchSum"
               " FROM ddpindebt_dbt indebt"
               " WHERE indebt.T_BRANCH = ? and indebt.T_DATE < ?"
               " AND indebt.T_CURRENCYCODE = ?"
               " AND NOT (indebt.T_STATESETTINGDATE <= ? AND indebt.T_STATE IN (2,3,4) ) "
               " AND indebt.T_ACTION <> 2";
      cmd = RsdCommand(cmdstr);
      cmd.addParam("DPCReservDate", RsdBp_in);
      cmd.addParam("Branch", RsdBp_in);
      cmd.addParam("IndebtDate", RsdBp_in);
      cmd.addParam("CurrCode", RsdBp_in);
      cmd.addParam("ReservDate", RsdBp_in);
      cmd.value("DPCReservDate") = ReservDate;
      cmd.value("Branch") = Branch;
      cmd.value("IndebtDate") = RegDate;
      cmd.value("CurrCode") = CodeCurrency;
      cmd.value("ReservDate") = ReservDate;
      cmd.execute();
      rs = RsdRecordset(cmd);

      if( rs.moveNext() )
        BranchSum = SQL_ConvTypeSum(rs.value("BranchSum"));
      end;

    end;
  end;

  return BranchSum;
end;

/* Определение валюты по фин.инструменту */
macro GetCodeCurrency(FIID)
  var cmdstr = "";
  var cmd, rs;
  var CodeCurrency = -1;
  var CodeInAccount = "", ISO_Number = 0;
  /* Поиск фин.инструмента */
  cmdstr = "SELECT t_CodeInAccount, t_ISO_Number FROM dfininstr_dbt WHERE T_FIID=?";
  cmd = RsdCommand(cmdstr);
  cmd.addParam("FIID", RsdBp_in);
  cmd.value("FIID") = FIID;
  cmd.execute();
  rs = RsdRecordset(cmd);

  if( rs.moveNext() )
    CodeInAccount = rs.value(0);
    ISO_Number = rs.value(1);
    /* Поиск валюты по CodeInAccount */
    cmdstr = "SELECT t_Code_Currency FROM dcurrency_dbt WHERE t_ExternalCode=?";
    cmd = RsdCommand(cmdstr);
    cmd.addParam("ExternalCode", RsdBp_in);
    cmd.value("ExternalCode") = CodeInAccount;
    cmd.execute();
    rs = RsdRecordset(cmd);

    if( rs.moveNext() )
      CodeCurrency = rs.value(0);
    else
      /* Поиск валюты по ISO_Number (для рублей) */
      cmd.addParam("ExternalCode", RsdBp_in);
      cmd.value("ExternalCode") = string(ISO_Number);
      cmd.execute();
      rs = RsdRecordset(cmd);
      if( rs.moveNext() )
        CodeCurrency = rs.value(0);
      end;
    end;

  end;

  return CodeCurrency;
end;

/* Функция расчета суммы просрочки по счетам Retail */
macro GetOverDueSumOnIndebt(AccNumber:string, Chapter:integer, FIID:integer, ReservDate:date, OverDuePeriod:integer, TotalSum:moneyl)
  var stat = 0;
  var cmdstr = "";
  var cmd, rs;
  var RegDate = Date(ReservDate - OverDuePeriod);
  var CodeCurrency = -1;
  var Branch = 0;
  var Sum = 0.0;

  CodeCurrency = GetCodeCurrency(FIID);
  if( CodeCurrency < 0 )
    stat = 9;  /* Не определена валюта */
  end;

  if( stat == 0 )
    /* Подсчет суммы задолженности для всех подразделений */
    Branch = getFilialNum();
    cmdstr = "SELECT t_code FROM ddp_dep_dbt"
             " START WITH t_code = ? CONNECT BY PRIOR t_code=t_parentcode AND t_nodetype!=1"
             " ORDER BY t_parentcode, t_code";
    cmd = RsdCommand(cmdstr);
    cmd.addParam("Branch", RsdBp_in);
    cmd.value("Branch") = Branch;
    cmd.execute();
    rs = RsdRecordset(cmd);
    while( (stat == 0) and (rs.moveNext()) )
      Branch = rs.value(0);
      Sum = Sum + GetIndebtRest(Branch, AccNumber, CodeCurrency, RegDate, ReservDate);
    end;
  end;

  if( stat == 0 )
    SetParm(5, Sum);
  end;

  return stat;
end;


/*GetOverDueSumOnIndebt("47423810177002300000", 0, 0, date(22,11,2007), 0, Summa);*/

END;