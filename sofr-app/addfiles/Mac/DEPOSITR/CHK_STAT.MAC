/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Анализ соответствия правильности заполнения файла статистики и          *
*  проведенных операций по вкладам                                         *
*                                                                          *
*  Лебедев С.В.                                                            *
*  25.08.98                                                                *
\**************************************************************************/

import deprintr, IsSrvDoc;

file sb_dtyp   (sb_dtyp);    /* Справочник видов вкладов      */
file sbdepdoc  (sbdepdoc);   /* Проводки по счетам вкладчиков */
file sb_depst  (sb_depst);   /* Статистика по видам вкладов   */
file prevstat  (sb_depst);

var FlagCur = NumFlagCur();  /* Флаг валютности */
var FNCash  = NumFNCash();   /* Номер филиала   */
private var {curdate};               /* Дата опердня    */
var Mode = GetCurrentMode();

array CodCur,       CodCurNR,
      ColAccounts,  ColAccountsNR,
      ColOpenedAcc, ColOpenedAccNR,
      ColClosedAcc, ColClosedAccNR,
      Rest,         RestNR,
      DebSum,       DebSumNR,
      KredSum,      KredSumNR,
      WasLooked,    WasLookedNR;

var WriteNoDiffType = FALSE;

var WasDiffer = FALSE;

const OnlyRest = TRUE;


/**************************************************************************\
|                       Определение названия филиала                       |
\**************************************************************************/
macro DefNameBranch (Number)

  var NameBranch;

  if (not findDepartment(Number, NameBranch))
    NameBranch = String(Number);
  end;

  return NameBranch;

end;


/**************************************************************************\
|                       Определение названия валюты                        |
\**************************************************************************/
macro DefNameCurrency (Code)

  file currency (currency);
  var ShortName = String(Code);

  KeyNum(currency,0);
  currency.Code_Currency = Code;
  if (GetEQ(currency))
    ShortName = currency.Short_Name;
  end;

  return ShortName;

end;


/**************************************************************************\
|            Сравнение статистики с операциями и написание отчета          |
\**************************************************************************/
macro WriteReport (i)

  var str;

  if (i == -1)
    WasDiffer = TRUE;
    if (OnlyRest)
      if (sb_depst.FlagRez == "")
        str = "рез.";
      else
        str = "нерез.";
      end;
      str = sb_depst.Kind + " (" + DefNameCurrency(sb_depst.CodCur) + " " + str + ")";
      [ ##################│ ################## │ ################## │ ############# ]
      (str:w,sb_depst.Rest:18:2:a,prevstat.Rest:18:2:a,(sb_depst.Rest - prevstat.Rest):13:2:a);
    else
      str = "По вкладу " + sb_depst.Kind + " (валюта " + DefNameCurrency(sb_depst.CodCur) + ", ";
      if (sb_depst.FlagRez == "")
        str = str + "резидент";
      else
        str = str + "нерезидент";
      end;
      str = str + ") есть обороты, но нет операций";
      [ #](str);
      [ ];
    end;
  else
    if (sb_depst.FlagRez == "")
      if ( WriteNoDiffType                                                   OR
          (ColAccounts(i)  != sb_depst.ColAccounts  - prevstat.ColAccounts ) OR
          (ColOpenedAcc(i) != sb_depst.ColOpenedAcc - prevstat.ColOpenedAcc) OR
          (ColClosedAcc(i) != sb_depst.ColClosedAcc - prevstat.ColClosedAcc) OR
          (Rest(i)         != sb_depst.Rest         - prevstat.Rest        ) OR
          (DebSum(i)       != sb_depst.DebSum       - prevstat.DebSum      ) OR
          (KredSum(i)      != sb_depst.KredSum      - prevstat.KredSum     )   )
        if ((ColAccounts(i)  != sb_depst.ColAccounts  - prevstat.ColAccounts ) OR
            (ColOpenedAcc(i) != sb_depst.ColOpenedAcc - prevstat.ColOpenedAcc) OR
            (ColClosedAcc(i) != sb_depst.ColClosedAcc - prevstat.ColClosedAcc) OR
            (Rest(i)         != sb_depst.Rest         - prevstat.Rest        ) OR
            (DebSum(i)       != sb_depst.DebSum       - prevstat.DebSum      ) OR
            (KredSum(i)      != sb_depst.KredSum      - prevstat.KredSum     )   )
          WasDiffer = TRUE;
        end;
        if (OnlyRest)
          str = sb_depst.Kind + " (" + DefNameCurrency(sb_depst.CodCur) + " рез.)";
          [ ##################│ ################## │ ################## │ ############# ]
          (str:w,sb_depst.Rest:18:2:a,(prevstat.Rest + KredSum(i) - DebSum(i)):18:2:a,(sb_depst.Rest - KredSum(i) + DebSum(i) - prevstat.Rest):13:2:a);
        else
          str = " По вкладу " + sb_depst.Kind + " (валюта " + DefNameCurrency(sb_depst.CodCur) + ", резидент) следующие расхождения";
          [#](str);
          [ --------------------------------------------------------------------------];
          [ |       Вид       |Сумма/Количество|Текущее состояние |  На начало дня   |];
          [ --------------------------------------------------------------------------];
          if (WriteNoDiffType OR (NOT WriteNoDiffType AND (ColAccounts(i) != sb_depst.ColAccounts - prevstat.ColAccounts)))
            [  Количество счетов           #####              #####              #####]
            (ColAccounts(i):5,sb_depst.ColAccounts:5,prevstat.ColAccounts:5);
          end;
          if (ColOpenedAcc(i) != sb_depst.ColOpenedAcc - prevstat.ColOpenedAcc)
            [  Открытых счетов             #####              #####              #####]
            (ColOpenedAcc(i):5,sb_depst.ColOpenedAcc:5,prevstat.ColOpenedAcc:5);
          end;
          if (ColClosedAcc(i) != sb_depst.ColClosedAcc - prevstat.ColClosedAcc)
            [  Закрытых счетов             #####              #####              #####]
            (ColOpenedAcc(i):5,sb_depst.ColClosedAcc:5,prevstat.ColClosedAcc:5);
          end;
          if (WriteNoDiffType OR (NOT WriteNoDiffType AND (Rest(i) != sb_depst.Rest - prevstat.Rest)))
            [  Изменение остатка  ##############   ################   ################]
            (Rest(i):16:2:a,sb_depst.Rest:16:2:a,prevstat.Rest:16:2:a);
          end;
          if (WriteNoDiffType OR (NOT WriteNoDiffType AND (DebSum(i) != sb_depst.DebSum - prevstat.DebSum)))
            [  Расход             ##############   ################   ################]
            (DebSum(i):16:2:a,sb_depst.DebSum:16:2:a,prevstat.DebSum:16:2:a);
          end;
          if (WriteNoDiffType OR (NOT WriteNoDiffType AND (KredSum(i) != sb_depst.KredSum - prevstat.KredSum)))
            [  Приход             ##############   ################   ################]
            (KredSum(i):16:2:a,sb_depst.KredSum:16:2:a,prevstat.KredSum:16:2:a);
          end;
          [ ];
        end;
      end;
    else
      if ( WriteNoDiffType                                                     OR
          (ColAccountsNR(i)  != sb_depst.ColAccounts  - prevstat.ColAccounts ) OR
          (ColOpenedAccNR(i) != sb_depst.ColOpenedAcc - prevstat.ColOpenedAcc) OR
          (ColClosedAccNR(i) != sb_depst.ColClosedAcc - prevstat.ColClosedAcc) OR
          (RestNR(i)         != sb_depst.Rest         - prevstat.Rest        ) OR
          (DebSumNR(i)       != sb_depst.DebSum       - prevstat.DebSum      ) OR
          (KredSumNR(i)      != sb_depst.KredSum      - prevstat.KredSum     )   )
        if ((ColAccountsNR(i)  != sb_depst.ColAccounts  - prevstat.ColAccounts ) OR
            (ColOpenedAccNR(i) != sb_depst.ColOpenedAcc - prevstat.ColOpenedAcc) OR
            (ColClosedAccNR(i) != sb_depst.ColClosedAcc - prevstat.ColClosedAcc) OR
            (RestNR(i)         != sb_depst.Rest         - prevstat.Rest        ) OR
            (DebSumNR(i)       != sb_depst.DebSum       - prevstat.DebSum      ) OR
            (KredSumNR(i)      != sb_depst.KredSum      - prevstat.KredSum     )   )
          WasDiffer = TRUE;
        end;
        if (OnlyRest)
          str = sb_depst.Kind + " (" + DefNameCurrency(sb_depst.CodCur) + " нерез.)";
          [ ##################│ ################## │ ################## │ ############# ]
          (str:w,sb_depst.Rest:18:2:a,(prevstat.Rest + KredSumNR(i) - DebSumNR(i)):18:2:a,(sb_depst.Rest - KredSumNR(i) + DebSumNR(i) - prevstat.Rest):13:2:a);
        else
          str = " По вкладу " + sb_depst.Kind + " (валюта " + DefNameCurrency(sb_depst.CodCur) + ", нерезидент) следующие расхождения";
          [#](str);
          [ --------------------------------------------------------------------------];
          [ |       Вид       |Сумма/Количество|Текущая статистика|Предыд. статистика|];
          [ --------------------------------------------------------------------------];
          if (WriteNoDiffType OR (NOT WriteNoDiffType AND (ColAccountsNR(i) != sb_depst.ColAccounts - prevstat.ColAccounts)))
            [  Количество счетов           #####              #####              #####]
            (ColAccountsNR(i):5,sb_depst.ColAccounts:5,prevstat.ColAccounts:5);
          end;
          if (ColOpenedAccNR(i) != sb_depst.ColOpenedAcc - prevstat.ColOpenedAcc)
            [  Открытых счетов             #####              #####              #####]
            (ColOpenedAccNR(i):5,sb_depst.ColOpenedAcc:5,prevstat.ColOpenedAcc:5);
          end;
          if (ColClosedAccNR(i) != sb_depst.ColClosedAcc - prevstat.ColClosedAcc)
            [  Закрытых счетов           #####              #####              #####]
            (ColOpenedAccNR(i):5,sb_depst.ColClosedAcc:5,prevstat.ColClosedAcc:5);
          end;
          if (WriteNoDiffType OR (NOT WriteNoDiffType AND (RestNR(i) != sb_depst.Rest - prevstat.Rest)))
            [  Изменение остатка  ##############   ################   ################]
            (RestNR(i):16:2:a,sb_depst.Rest:16:2:a,prevstat.Rest:16:2:a);
          end;
          if (WriteNoDiffType OR (NOT WriteNoDiffType AND (DebSumNR(i) != sb_depst.DebSum - prevstat.DebSum)))
            [  Расход             ##############   ################   ################]
            (DebSumNR(i):16:2:a,sb_depst.DebSum:16:2:a,prevstat.DebSum:16:2:a);
          end;
          if (WriteNoDiffType OR (NOT WriteNoDiffType AND (KredSumNR(i) != sb_depst.KredSum - prevstat.KredSum)))
            [  Приход             ##############   ################   ################]
            (KredSumNR(i):16:2:a,sb_depst.KredSum:16:2:a,prevstat.KredSum:16:2:a);
          end;
          [ ];
        end;
      end;
    end;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro WriteReport2 (FlagRez, i)

  var str;
  var CCur;
  var Delta;
  var Rez = "";

  if (FlagRez != 0)
    Rez = "\x01";
  end;

  WasDiffer = TRUE;

  if (FlagRez == 0)
    CCur = CodCur(i);
  else
    CCur = CodCurNR(i);
  end;

  if (OnlyRest)
    KeyNum(prevstat,0);
    ReWind(prevstat);
    prevstat.FNCash  = FNCash;
    prevstat.IsCur   = FlagCur;
    prevstat.Kind    = sb_dtyp.Kind;
    prevstat.CodCur  = CCur;
    prevstat.FlagRez = Rez;
    prevstat.Date    = {curdate};
    prevstat.Mode    = Mode;
    if (GetLT(prevstat))
      if ((prevstat.FNCash  != FNCash      ) OR
          (prevstat.IsCur   != FlagCur     ) OR
          (prevstat.Kind    != sb_dtyp.Kind) OR
          (prevstat.CodCur  != CCur        ) OR
          (prevstat.FlagRez != Rez         ) OR
          (prevstat.Date    >= {curdate}   )   )
        ClearRecord(prevstat);
      end;
    else
      ClearRecord(prevstat);
    end;

    if (FlagRez == 0)
      str = "рез.";
      Delta = DebSum(i) - KredSum(i);
    else
      str = "нерез.";
      Delta = DebSumNR(i) - KredSumNR(i);
    end;
    str = sb_dtyp.Kind + " (" + DefNameCurrency(CCur) + " " + str + ")";
    [ ##################│ ################## │ ################## │ ############# ]
    (str:w,prevstat.Rest:18:2:a,(prevstat.Rest - Delta):18:2:a,Delta:13:2:a);
  else
    str = "По вкладу " + sb_dtyp.Kind + " (валюта " + DefNameCurrency(CCur) + ", ";
    if (FlagRez == 0)
      str = str + "резидент";
    else
      str = str + "нерезидент";
    end;
    str = str + ") есть операции, но нет оборотов";
    [ #](str);
    [ -------------------------------------------------------];
    [ |      Счетов       |              Сумма              |];
    [ | открыто | закрыто |     Приход     |     Расход     |];
    [ -------------------------------------------------------];
    if (FlagRez == 0)
      [   #######   #######  ###############  ###############  ]
      (ColOpenedAcc(i):7,ColClosedAcc(i):7,KredSum(i):15:2:a,DebSum(i):15:2:a);
    else
      [   #######   #######  ###############  ###############  ]
      (ColOpenedAccNR(i):7,ColClosedAccNR(i):7,KredSumNR(i):15:2:a,DebSumNR(i):15:2:a);
    end;
    [ ];
  end;

end;


/**************************************************************************\
|                    Определение различий в виде вклада                    |
\**************************************************************************/
macro DefDifferKind

  var i = -1;
  var j = 0;
  var stat;

  KeyNum(sb_depst,4);
  ReWind(sb_depst);
  sb_depst.FNCash = FNCash;
  sb_depst.Date   = {curdate};
  sb_depst.BalAcc = "";
  sb_depst.CodCur = -1;
  stat = GetGE(sb_depst);
  while (stat)
    i = -1;
    if ((sb_depst.FNCash == FNCash   ) AND
        (sb_depst.Date   == {curdate})    )
      if ((sb_depst.Kind  == sb_dtyp.Kind   ) AND
          (sb_depst.IsCur == sb_dtyp.FlagCur)    )
        KeyNum(prevstat,0);
        ReWind(prevstat);
        Copy(prevstat,sb_depst);
        if (GetLT(prevstat))
          if ((prevstat.FNCash  != sb_depst.FNCash ) OR
              (prevstat.IsCur   != sb_depst.IsCur  ) OR
              (prevstat.Kind    != sb_depst.Kind   ) OR
              (prevstat.CodCur  != sb_depst.CodCur ) OR
              (prevstat.FlagRez != sb_depst.FlagRez) OR
              (prevstat.Date    >= sb_depst.Date   )   )
            ClearRecord(prevstat);
          end;
        else
          ClearRecord(prevstat);
        end;
        j = 0;
        if (sb_depst.FlagRez == "")
          while (j < Asize(CodCur))
            if (CodCur(j) == sb_depst.CodCur)
              i = j;
              j = Asize(CodCur);
              WasLooked(i) = 1;
            end;
            j = j + 1;
          end;
        else
          while (j < Asize(CodCurNR))
            if (CodCurNR(j) == sb_depst.CodCur)
              i = j;
              j = Asize(CodCurNR);
              WasLookedNR(i) = 1;
            end;
            j = j + 1;
          end;
        end;
        WriteReport(i);
      end;
      stat = Next(sb_depst);
    else
      stat = FALSE;
    end;
  end;

  j = 0;
  while (j < Asize(CodCur))
    if (WasLooked(j) == 0)
      WriteReport2(0,j);
    end;
    j = j + 1;
  end;

  j = 0;
  while (j < Asize(CodCurNR))
    if (WasLookedNR(j) == 0)
      WriteReport2(1,j);
    end;
    j = j + 1;
  end;

end;


/**************************************************************************\
|                    Поля статистики за исследуемую дату                   |
\**************************************************************************/
macro FillStatFields

  var i = -1;
  var j = 0;

  /* Для резидентов */
  if (sbdepdoc.FlagRezid == "")
    if (Asize(CodCur) > 0)
      while (j < Asize(CodCur))
        if (CodCur(j) == sbdepdoc.Code_Currency)
          i = j;
          j = Asize(CodCur);
        end;
        j = j + 1;
      end;
    end;
  /* Для нерезидентов */
  else
    if (Asize(CodCurNR) > 0)
      while (j < Asize(CodCurNR))
        if (CodCurNR(j) == sbdepdoc.Code_Currency)
          i = j;
          j = Asize(CodCurNR);
        end;
        j = j + 1;
      end;
    end;
  end;

  /* Так и небыло найдено нужного элемента массива, заводим его */
  if (i == -1)
    if (sbdepdoc.FlagRezid == "")
      i = Asize(CodCur);
      CodCur      (i) = sbdepdoc.Code_Currency;
      ColAccounts (i) = 0;
      ColOpenedAcc(i) = 0;
      ColClosedAcc(i) = 0;
      Rest        (i) = $0.0L;
      DebSum      (i) = $0.0L;
      KredSum     (i) = $0.0L;
      WasLooked   (i) = 0;
    else
      i = Asize(CodCurNR);
      CodCurNR      (i) = sbdepdoc.Code_Currency;
      ColAccountsNR (i) = 0;
      ColOpenedAccNR(i) = 0;
      ColClosedAccNR(i) = 0;
      RestNR        (i) = $0.0L;
      DebSumNR      (i) = $0.0L;
      KredSumNR     (i) = $0.0L;
      WasLookedNR   (i) = 0;
    end;
  end;

  /* Определяем как должны были отличиться суммы при совершении операции */
  if (sbdepdoc.FlagRezid == "")
    /* Открытие счета */
    if   ((sbdepdoc.TypeOper ==  1/*OP_OPENCAS*/) OR
          (sbdepdoc.TypeOper == 51/*OP_OPENNCS*/) OR
          (sbdepdoc.TypeOper == 58/*OP_OPENTRN*/)   )
      ColAccounts (i) = ColAccounts (i) + 1;
      ColOpenedAcc(i) = ColOpenedAcc(i) + 1;
    /* Закрытие счета */
    elif (((sbdepdoc.TypeComplexOper == 10/*OP_CLCAH   */) OR
           (sbdepdoc.TypeComplexOper == 81/*OP_CLNCAS  */) OR
           (sbdepdoc.TypeComplexOper == 96/*OP_CLSTRN  */)   ) AND
          ((sbdepdoc.TypeOper        ==  4/*OP_OUT     */) OR
           (sbdepdoc.TypeOper        == 63/*OP_GETPOR  */) OR
           (sbdepdoc.TypeOper        == 64/*OP_GETLPR  */) OR
           (sbdepdoc.TypeOper        == 65/*OP_GETIN   */) OR
           (sbdepdoc.TypeOper        == 66/*OP_GETOWN  */) OR
           (sbdepdoc.TypeOper        == 67/*OP_GETALIEN*/)   )    )
      ColOpenedAcc(i) = ColOpenedAcc(i) - 1;
      ColClosedAcc(i) = ColClosedAcc(i) + 1;
    /* Перевод в другой вид вклада */
    elif (sbdepdoc.TypeOper == 78)
      if   (sbdepdoc.InSum  > 0)
        ColAccounts (i) = ColAccounts (i) + 1;
        ColOpenedAcc(i) = ColOpenedAcc(i) + 1;
      elif (sbdepdoc.OutSum > 0)
        ColAccounts (i) = ColAccounts (i) - 1;
        ColOpenedAcc(i) = ColOpenedAcc(i) - 1;
      end;
    end;
    /* Поля сумм */
    if   (sbdepdoc.InSum  > 0)
      Rest   (i) = Rest   (i) + sbdepdoc.InSum;
      KredSum(i) = KredSum(i) + sbdepdoc.InSum;
    elif (sbdepdoc.OutSum > 0)
      Rest  (i) = Rest  (i) - sbdepdoc.OutSum;
      DebSum(i) = DebSum(i) + sbdepdoc.OutSum;
    end;
  else
    /* Открытие счета */
    if   ((sbdepdoc.TypeOper ==  1/*OP_OPENCAS*/) OR
          (sbdepdoc.TypeOper == 51/*OP_OPENNCS*/) OR
          (sbdepdoc.TypeOper == 58/*OP_OPENTRN*/)   )
      ColAccountsNR (i) = ColAccountsNR (i) + 1;
      ColOpenedAccNR(i) = ColOpenedAccNR(i) + 1;
    /* Закрытие счета */
    elif (((sbdepdoc.TypeComplexOper == 10/*OP_CLCAH   */) OR
           (sbdepdoc.TypeComplexOper == 81/*OP_CLNCAS  */) OR
           (sbdepdoc.TypeComplexOper == 96/*OP_CLSTRN  */)   ) AND
          ((sbdepdoc.TypeOper        ==  4/*OP_OUT     */) OR
           (sbdepdoc.TypeOper        == 63/*OP_GETPOR  */) OR
           (sbdepdoc.TypeOper        == 64/*OP_GETLPR  */) OR
           (sbdepdoc.TypeOper        == 65/*OP_GETIN   */) OR
           (sbdepdoc.TypeOper        == 66/*OP_GETOWN  */) OR
           (sbdepdoc.TypeOper        == 67/*OP_GETALIEN*/)   )    )
      ColOpenedAccNR(i) = ColOpenedAccNR(i) - 1;
      ColClosedAccNR(i) = ColClosedAccNR(i) + 1;
    /* Перевод в другой вид вклада */
    elif (sbdepdoc.TypeOper == 78)
      if   (sbdepdoc.InSum  > 0)
        ColAccountsNR (i) = ColAccountsNR (i) + 1;
        ColOpenedAccNR(i) = ColOpenedAccNR(i) + 1;
      elif (sbdepdoc.OutSum > 0)
        ColAccountsNR (i) = ColAccountsNR (i) - 1;
        ColOpenedAccNR(i) = ColOpenedAccNR(i) - 1;
      end;
    end;
    /* Поля сумм */
    if   (sbdepdoc.InSum  > 0)
      RestNR   (i) = RestNR   (i) + sbdepdoc.InSum;
      KredSumNR(i) = KredSumNR(i) + sbdepdoc.InSum;
    elif (sbdepdoc.OutSum > 0)
      RestNR  (i) = RestNR  (i) - sbdepdoc.OutSum;
      DebSumNR(i) = DebSumNR(i) + sbdepdoc.OutSum;
    end;
  end;

end;


/**************************************************************************\
|                      Цикл по проводкам видов вкладов                     |
\**************************************************************************/
macro HistoryDepDoc

  var stat;

  /* Обнуление массивов */
  Asize(CodCur,      0); Asize(CodCurNR,      0);
  Asize(ColAccounts, 0); Asize(ColAccountsNR, 0);
  Asize(ColOpenedAcc,0); Asize(ColOpenedAccNR,0);
  Asize(ColClosedAcc,0); Asize(ColClosedAccNR,0);
  Asize(Rest,        0); Asize(RestNR,        0);
  Asize(DebSum,      0); Asize(DebSumNR,      0);
  Asize(KredSum,     0); Asize(KredSumNR,     0);
  Asize(WasLooked,   0); Asize(WasLookedNR,   0);

  KeyNum(sbdepdoc,5);
  ReWind(sbdepdoc);
  sbdepdoc.Date_Document = {curdate};
  stat = GetGE(sbdepdoc);
  while (stat)
    if (sbdepdoc.Date_Document == {curdate})
      /* Соответствие документа виду вклада */
      if ((sbdepdoc.Type_Account == sb_dtyp.Kind   ) AND
          (sbdepdoc.FNCash       == FNCash         ) AND
          (sbdepdoc.IsCur        == sb_dtyp.FlagCur)    )
        /* Отсечение архивной проводки */

        if (IsServDoc(sbdepdoc) AND (sbdepdoc.FlagStorn == StrFor(0)))
          FillStatFields();
        end;
      end;
      stat = Next(sbdepdoc);
    else
      stat = FALSE;
    end;
  end;

end;


/**************************************************************************\
|                          Цикл по видам вкладов                           |
\**************************************************************************/
macro WorkWithDepTypep

  var stat;

  KeyNum(sb_dtyp,2);
  sb_dtyp.FlagCur = FlagCur;
  sb_dtyp.Kind    = "";
  stat = GetGE(sb_dtyp);
  while (stat)
    if (sb_dtyp.FlagCur == FlagCur)
      Message(" Обрабатывается вклад ",sb_dtyp.Kind);
      HistoryDepDoc();
      DefDifferKind();
      stat = Next(sb_dtyp);
    else
      stat = FALSE;
    end;
  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  if ( UpdateStatistics( false, "", FlagCur, FNCash ) )
    ;
  else
    MsgBox( "Ошибка при заполнении статистики" );
    exit( 1 );
  end;

  [ ];
  if (OnlyRest)
    [           Анализ соответствия остатков по видам вкладов к];
    [          проведенным операциям за ########## по филиалу #]({curdate},DefNameBranch(FNCash));
    [ ──────────────────┬────────────────────┬────────────────────┬───────────────];
    [     Вид вклада    │ Остаток по филиалу │   Остаток по ПК    │  Расхождение  ];
    [ ──────────────────┼────────────────────┼────────────────────┼───────────────];
  else
    [ Анализ соответствия оборотов и остатков по видам вкладов к];
    [  проведенным операциям за ########## по филиалу #]({curdate},DefNameBranch(FNCash));
    [ ];
  end;

  WorkWithDepTypep();

  if ((NOT WasDiffer) AND (NOT OnlyRest))
    [ Расхождения не обнаружены];
  end;

  if (OnlyRest)
    [ ──────────────────┴────────────────────┴────────────────────┴───────────────];
  end;

end;
