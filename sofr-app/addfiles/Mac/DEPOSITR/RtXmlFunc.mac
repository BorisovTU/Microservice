/*
$Name:               RtXmlFunc.mac
$Module:             DEPOSITR
$Description:        сериализация/десериализация RSL-объектов в/из XML
*/

import ServiceAction, rcw;

// Имя переменной с названием поля, хранящего объект с атрибутами
// Если такой переменной нет, то атрибуты должны храниться в поле с именем "attr"
const ATTR_PROPERTY_NAME_VAR = "__RSL_ATTR_PROPERTY_NAME";

const DEFAULT_ATTR_PROPERTY_NAME = "attr";

// Имя переменной с названием поля, хранящего значение объекта
// Если такой переменной нет, то значение должно храниться в поле с именем "value"
const VALUE_PROPERTY_NAME_VAR = "__RSL_VALUE_PROPERTY_NAME";

const DEFAULT_VALUE_PROPERTY_NAME = "value";


//-----------------------------------------------------------------------------
class TDynamicItem
  var name;
  var value;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class XmlRSLStruct

  // =====================================================================
  var __Attributes = TArray;
  var __Values = TArray;

  // =====================================================================
  macro SetAttr( name, value ) 
    var numElem = -1;
    var i = 0;

    while ( i < __Attributes.Size )
      if ( StrUpr( __Attributes[i].name ) == StrUpr( name ) )
        numElem = i;
        break;
      end;

      i = i + 1;
    end;

    if ( numElem > -1 )
      __Attributes[numElem].value = value;
    else
      numElem = __Attributes.Size;

      __Attributes[numElem] = TDynamicItem;

      __Attributes[numElem].name = name;
      __Attributes[numElem].value = value;
    end;
  end;

  // =====================================================================
  macro SetValue( name, value ) 
    var numElem = -1;
    var i = 0;

    while ( i < __Values.Size )
      if ( StrUpr( __Values[i].name ) == StrUpr( name ) )
        numElem = i;
        break;
      end;

      i = i + 1;
    end;

    if ( numElem > -1 )
      __Values[numElem].value = value;
    else
      numElem = __Values.Size;

      __Values[numElem] = TDynamicItem;

      __Values[numElem].name = name;
      __Values[numElem].value = value;
    end;
  end;

  // =====================================================================
  macro GetAttr( name ) 
    var retValue = null;
    var i = 0;

    while ( i < __Attributes.Size )
      if ( StrUpr( __Attributes[i].name ) == StrUpr( name ) )
        retValue = __Attributes[i].value;
        break;
      end;

      i = i + 1;
    end;

    return retValue;
  end;

  // =====================================================================
  macro GetValue( name ) 
    var retValue = null;
    var i = 0;

    while ( i < __Values.Size )
      if ( StrUpr( __Values[i].name ) == StrUpr( name ) )
        retValue = __Values[i].value;
        break;
      end;

      i = i + 1;
    end;

    return retValue;
  end;

  // =====================================================================
  macro GetAttributes
    return __Attributes;
  end;

  // =====================================================================
  macro GetValues
    return __Values;
  end;

end;


//-----------------------------------------------------------------------------
private macro IsServiceVariable(name)
  if ((name == ATTR_PROPERTY_NAME_VAR) or (name == VALUE_PROPERTY_NAME_VAR))
    return true;
  end;
  return false;
end;


//-----------------------------------------------------------------------------
private macro GetVarName(obj, name_var, def_name)
  var attr_name = def_name;

  var index = GenPropID(obj, name_var);
  if (index >= 0)
    attr_name = obj(index);
  end;

  return StrUpr(attr_name);
end;


//-----------------------------------------------------------------------------
private macro GetAttributeObjName(obj)
  return GetVarName(obj, ATTR_PROPERTY_NAME_VAR, DEFAULT_ATTR_PROPERTY_NAME);
end;


//-----------------------------------------------------------------------------
private macro GetValueObjName(obj)
  return GetVarName(obj, VALUE_PROPERTY_NAME_VAR, DEFAULT_VALUE_PROPERTY_NAME);
end;


//-----------------------------------------------------------------------------
private macro GetAttributeObj(obj)
  var attr_name = GetAttributeObjName(obj);

  var index = GenPropID(obj, attr_name);
  if (index >= 0)
    return obj(index);
  end;

  return null;
end;


//-----------------------------------------------------------------------------
// YYYYMMDD
private macro ParseISODate(value)
  var y = Int(substr(value, 1, 4));
  var m = Int(substr(value, 5, 2));
  var d = Int(substr(value, 7, 2));
  return Date(d, m, y);
end;


private var ParseDateMacro = @ParseISODate;


//-----------------------------------------------------------------------------
private macro ParseDate(value)
  return ExecMacro2(ParseDateMacro, value);
end;

//-----------------------------------------------------------------------------
// YYYYMMDD
private macro MakeXmlDate(dt)
  var y, m, d;
  DateSplit(dt, d, m, y);
  return string(y:o:4, m:o:2, d:o:2);
end;


//-----------------------------------------------------------------------------
// hh:mm:nnzzz
private macro ParseTime(value)
  var v = value;
  if (strlen(value) > 8)
    v = substr(value, 1, 8) + "." + substr(value, 9, 2);
  end;
  return Time(v);
end;


//-----------------------------------------------------------------------------
// hh:mm:nnzzz
private macro MakeXmlTime(tm)
  var h, m, s, ms;
  TimeSplit(tm, h, m, s, ms);
  return string(h:o:2, ":", m:o:2, ":", s:o:2, ms:o:2, "0");
end;


//-----------------------------------------------------------------------------
// YYYYMMDDThh:mm:nnzzz
private macro ParseDtTm(value)
  var dt = ParseDate(value);
  var tm = ParseTime(substr(value, 10));
  return DtTm(dt, tm);
end;


//-----------------------------------------------------------------------------
// YYYYMMDDThh:mm:nnzzz
private macro MakeXmlDateTime(d)
  var dt, tm;
  DtTmSplit(d, dt, tm);
  return MakeXmlDate(dt) + "T" + MakeXmlTime(tm);
end;


//-----------------------------------------------------------------------------
private macro Cast(value, type)
  var v;
  if (type == V_INTEGER)
    v = Int(value);
  elif (type == V_DATE)
    v = ParseDate(value);
  elif (type == V_TIME)
    v = ParseTime(value);
  elif (type == V_DTTM)
    v = ParseDtTm(value);
  elif ((type == V_MONEY) or (type == V_DECIMAL))
    v = Money(value);
  elif (type == V_DOUBLE)
    v = Double(value);
  else
    v = value;
  end;
  
  return v;
end;


//-----------------------------------------------------------------------------
private macro ToXmlString(value)
  var type = ValType(value);
  var v;
  if (type == V_DATE)
    v = MakeXmlDate(value);
  elif (type == V_TIME)
    v = MakeXmlTime(value);
  elif (type == V_DTTM)
    v = MakeXmlDateTime(value);
  else
    v = string(value);
  end;

  return v;
end;


//-----------------------------------------------------------------------------
private macro SetSimpleProperty(obj, prop_name, value)
  var type = ValType(GenGetProp(obj, prop_name));
  GenSetProp(obj, prop_name, Cast(value, type));
end;


//-----------------------------------------------------------------------------
private macro GetArrayFromXml(arr, xml_node)
  var type = V_STRING;
  var class_name = "";
  var sample = null;
  var dynamic = false;

  if (arr.size != 0)  // тип элементов массива берём из 0-го элемента
    type = ValType(arr[0]);
    class_name = GenClassName(arr[0]);

    if ((class_name == "TArray") and (arr[0].size != 0))
      sample = arr[0][0];
    end;

    if (class_name == StrUpr("TDynamicItem"))
      dynamic = true;
      type = ValType(arr[0].value);
      class_name = GenClassName(arr[0].value);
    end;

    arr.size = 0;
  end;

  var item;
  var prop = xml_node.firstChild();
  var i = 0;
  while (prop)
    if (type == V_GENOBJ)
      item = GenObject(class_name);

      if ((class_name == "TArray") and (sample != null))
        item[0] = sample;
      end;

      ExecMacro("ParseXml", item, prop);
    else
      item = prop.text;
    end;

    item = Cast(item, type);
    
    if (dynamic)
      var elem = TDynamicItem;
      elem.name = prop.nodeName;
      elem.value = item;
      item = elem;
    end;
    
    arr[i] = item;
    i = i + 1;

    prop = prop.nextSibling();
  end;

  
end;

//-----------------------------------------------------------------------------
private macro SetProperty(obj, prop_name, xml_node)
  var v = GenGetProp(obj, prop_name);
  var type = ValType(v);
  if (type == V_GENOBJ)
    /*if (GenClassName(v) == "TArray")
      GetArrayFromXml(v, xml_node);
    else*/
      ExecMacro("ParseXml", v, xml_node);
    //end;
  else
    SetSimpleProperty(obj, prop_name, xml_node.text);
  end;
end;


//-----------------------------------------------------------------------------
private macro MakeXmlNode(xml, xml_node, name, v)
  var node = xml.CreateNode(1, name, "");

  var type = ValType(v);
  if (type == V_GENOBJ)
    ExecMacro("MakeXml", v, xml, node);
  else
    node.text = ToXmlString(v);
  end;

  xml_node.AppendChild(node);
end;


//-----------------------------------------------------------------------------
private macro GetAttributesFromXml(obj, xml_node)
  var attr_obj = GetAttributeObj(obj);
  var name;
  var i = 0;

  while (i < xml_node.attributes.length)
    name = xml_node.attributes(i).name;
    if (GenPropID(attr_obj, name) >= 0)
      SetSimpleProperty(attr_obj, name, xml_node.attributes(i).text);
    else
      if ( IsEqClass( "XmlRSLStruct", obj ) )
        obj.SetAttr( name, xml_node.attributes(i).text );
      end;
    end;

    i = i + 1;
  end;	

end;


//-----------------------------------------------------------------------------
private macro MakeXmlAttributes(obj, xml, xml_node)
  var attr_obj = GetAttributeObj(obj);
  var props = GetObjProps(attr_obj, true);
  var i = 0;
  var name;
  var attr;
  var addElem;
  var v;

  while (i < props.size( ) )
    name = props[i];

    if ( IsEqClass( "XmlRSLStruct", obj ) and ( ( StrUpr( name ) == "__ATTRIBUTES" ) or ( StrUpr( name ) == "__VALUES" ) ) )
      ;
    else
      v = GenGetProp(attr_obj, name);

      if (v != null)
        attr = xml.CreateAttribute(name);
        attr.value = ToXmlString(v);
        xml_node.SetAttributeNode(attr);
      end;
    end;

    i = i + 1;
  end;	

  if ( IsEqClass( "XmlRSLStruct", obj ) )
    var addAttrs = obj.GetAttributes( );

    if ( addAttrs != null )
      i = 0;
      while ( i < addAttrs.Size )

        attr = xml.CreateAttribute( addAttrs[i].name );
        attr.value = ToXmlString( addAttrs[i].value );
        xml_node.SetAttributeNode( attr );

        i = i + 1;
      end;
    end;	
  end;	

end;


//-----------------------------------------------------------------------------
private macro GetPropertiesFromXml(obj, xml_node)
  var attr_name = GetAttributeObjName(obj);
  var value_name = GetValueObjName(obj);

  if (GenPropID(obj, value_name) >= 0)
    SetProperty(obj, value_name, xml_node);
  end;
  
  var prop = xml_node.firstChild();
  while (prop)
    if (StrUpr(prop.nodeName) != attr_name)
      if (GenPropID(obj, prop.nodeName) >= 0)
        SetProperty(obj, prop.nodeName, prop);
      else
        if ( IsEqClass( "XmlRSLStruct", obj ) )
          obj.SetValue( prop.nodeName, prop.text );
        end;
      end;
    end;

    prop = prop.nextSibling();
  end;	

end;


//-----------------------------------------------------------------------------
private macro MakeXmlProperties(obj, xml, xml_node)
  var attr_name = GetAttributeObjName(obj);
  var value_name = GetValueObjName(obj);

  if (GenPropID(obj, value_name) >= 0)
    var value = GenGetProp(obj, value_name);
    if (ValType(value) == V_GENOBJ)
      ExecMacro("MakeXml", value, xml, xml_node);
    else
      xml_node.text = ToXmlString(value);
    end;
  end;
  
  var props = GetObjProps(obj, true);
  var i = 0;
  var name;
  while (i < props.size())
    name = props[i];

    if ( IsEqClass( "XmlRSLStruct", obj ) and ( ( StrUpr( name ) == "__ATTRIBUTES" ) or ( StrUpr( name ) == "__VALUES" ) ) )
      ;
    else
      if ((StrUpr(name) != attr_name) and (StrUpr(name) != value_name) and 
          not IsServiceVariable(name) and (GenPropID(obj, name) >= 0))
        var v = GenGetProp(obj, name);
        if (v != null)
          MakeXmlNode(xml, xml_node, name, v);
        end;
      end;
    end;

    i = i + 1;
  end;	

  if ( IsEqClass( "XmlRSLStruct", obj ) )
    var addValues = obj.GetValues( );

    if ( addValues != null )
      i = 0;
      while ( i < addValues.Size )
        MakeXmlNode( xml, xml_node, addValues[i].name, addValues[i].value );

        i = i + 1;
      end;
    end;	
  end;	

end;


//-----------------------------------------------------------------------------
private macro ParseXml(obj, xml_node)
  if (GenClassName(obj) == "TArray")
    GetArrayFromXml(obj, xml_node);
  else
    // заполнить атрибуты
    var attr_obj = GetAttributeObj(obj);
    if (attr_obj and (xml_node.attributes.length > 0))
      GetAttributesFromXml(obj, xml_node);
    end;

    // прочитать свойства
    GetPropertiesFromXml(obj, xml_node);
  end;
end;


//-----------------------------------------------------------------------------
private macro MakeXml(obj, xml, xml_node)

  if (GenClassName(obj) == "TArray")
    var item;
    for (item, obj)
      if (IsEqClass("TDynamicItem", item))
        MakeXmlNode(xml, xml_node, item.name, item.value);
      end;
    end;
  else
    // сформировать атрибуты
    var attr_obj = GetAttributeObj(obj);
    if (attr_obj)
      MakeXmlAttributes(obj, xml, xml_node);
    end;

    MakeXmlProperties(obj, xml, xml_node);
  end;
end;


//-----------------------------------------------------------------------------
macro ReplaceParseDateMacro(newMacro)
  if (newMacro != null)
    ParseDateMacro = newMacro;
  else
    ParseDateMacro = @ParseISODate;
  end;
end;


//-----------------------------------------------------------------------------
// Преобразование XML в RSL
//   Возвращаемое значение: 0 - успешная обработка, иначе ошибка
//-----------------------------------------------------------------------------
macro RtXml2Rsl(
  xml_str, // XML-строка
  out_obj  // объект, который будет заполнен данными из XML-строки
)
  var xml = CreateXMLParser();
  xml.loadXML(xml_str);
  
  if (xml.parseError.errorCode != 0)
    return xml.parseError.errorCode;
  end;
  
  var root = xml.documentElement;

  ParseXml(out_obj, root);
  
  return 0;
end;



//-----------------------------------------------------------------------------
// Преобразование RSL в XML
//-----------------------------------------------------------------------------
macro RtRsl2Xml(
  obj,  // объект RSL
  name  // имя тега для объекта
)
  var xml = CreateXMLParser();

  var xml_node = xml.CreateNode(1, name, "");
  MakeXml(obj, xml, xml_node);
  xml.AppendChild(xml_node);

  return xml.xml;
end;


//-------------------------------------------------------------------------------------------------
// Тесты
//-------------------------------------------------------------------------------------------------
/*
import "c:\\#\\!Contact NG\\printObj.mac";

class DateVal
  var value:date = date();
end;

class T
  class TA
    var Dt_File:date = date();
    var Transaction_Fee:money;
    var Rsb_Fee:money;
    var NoAttr:integer;
  end;
  
  var attr = TA;
  var Params = DateVal;
  var trnReference:integer;
end;


//println(GenClassName($0.1));

var str = "<RESPONSE TRANSACTION_FEE=\"0.15\" RSB_FEE=\"0.10\" DT_FILE=\"20100730\"><trnReference>12145</trnReference><PARAMS>20101231</PARAMS></RESPONSE>";

var obj = T;
RtXml2Rsl(str, obj);

PrintObj(obj);
PrintObj(obj.attr);
PrintObj(obj.Params);

println();
var res = RtRsl2Xml(obj, "RESPONSE");
println(res);
println();


class WithArray
  var FieldsToPrint = TArray;
end;

str = "<RESPONSE ID=\"77916\" trnReference=\"12194\" RE=\"0\">" +
"<FieldsToPrint>" +
"<FIELD>trnReference</FIELD>" +
"<FIELD>trnDate</FIELD>" +
"</FieldsToPrint>" +
"</RESPONSE>";

obj = WithArray;
RtXml2Rsl(str, obj);
PrintObj(obj);
PrintObj(obj.FieldsToPrint);

str = "<RESPONSE ID=\"77916\" trnReference=\"12194\" RE=\"0\">" +
"<FieldsToPrint>" +
"<FIELD>1</FIELD>" +
"<FIELD>2</FIELD>" +
"<FIELD>3</FIELD>" +
"</FieldsToPrint>" +
"</RESPONSE>";

obj = WithArray;
obj.FieldsToPrint[0] = 1;
RtXml2Rsl(str, obj);
PrintObj(obj);
PrintObj(obj.FieldsToPrint);

class IntVal
  var value:integer;
end;

obj = WithArray;
obj.FieldsToPrint[0] = IntVal;
RtXml2Rsl(str, obj);
PrintObj(obj);
PrintObj(obj.FieldsToPrint);
PrintObj(obj.FieldsToPrint[0]);

class DateTimeVal
  var value = DtTm();
end;

str = "<RESPONSE>20100225T12:31:09820</RESPONSE>";

obj = DateTimeVal;
RtXml2Rsl(str, obj);
PrintObj(obj);


println();
res = RtRsl2Xml(obj, "RESPONSE");
println(res);
println();


str = "<DATA>" +
"<RESPONSE>" +
"<VALUES1>" +
"<LIST_SRC ID=\"1\" CAPTION=\"446789\"/>" +
"<LIST_SRC ID=\"2\" CAPTION=\"541123\"/>" +
"</VALUES1>" +
"<VALUES2>" +
"<LIST_SRC ID=\"1\" CAPTION=\"12344456667789\"/>" +
"<LIST_SRC ID=\"2\" CAPTION=\"44112344478888\"/>" +
"</VALUES2>" +
"</RESPONSE>" +
"</DATA>";


class ListItem
  class A
    var ID:integer;
    var CAPTION:string;
  end;

  var attr = A;
end;

class Array2D
  var RESPONSE = TArray;

  RESPONSE[0] = TArray;
  RESPONSE[0][0] = ListItem;
end;

obj = Array2D;
RtXml2Rsl(str, obj);
PrintObj(obj);
PrintObj(obj.RESPONSE);

var i = 0;
var j = 0;
while (i < obj.RESPONSE.size)
  j = 0;
  while (j < obj.RESPONSE[i].size)
    println("[" + i + ", " + j + "]");
    PrintObj(obj.RESPONSE[i][j].attr);
    j = j + 1;
  end;
  i = i + 1;
end;


class PrivateVal
  var Export = "public data";
  private var NoExport = "private data";
end;

println();

obj = PrivateVal;
res = RtRsl2Xml(obj, "RESPONSE");
println(res);
*/
