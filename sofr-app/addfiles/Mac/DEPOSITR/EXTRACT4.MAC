/*
$Name:             EXTRACT4.MAC
$Module:           RS-Retail
$Description:      Выписка по счету. Форма 204-с.
*/

import DeprIntr, OperCode, IsSrvDoc, GetDocs, ExchangeInter, SqlConv, extr_met;

private var
  fil = TBFile ( "listfdep.dbt", "r", 0 ), 
  dtp = TBFile ( "sb_dtyp.dbt",  "r", 2 ), 
  opr = TBFile ( "person.dbt",   "r", 0, "person.dbt", "bank.def" ), 
  cur = TBFile ( "currency.dbt", "r", 0 ), 
  dep = TBFile ( "depositr.dbt", "r", 8 ), 
  doc = TBFile ( "sbdepdoc.dbt", "r", 6 ), 
  sop = TBFile ( "suboper.dbt",  "r", 1 );

var depclnt = TClientList; /* Справочник вкладчиков */

var scAcc,
    scTran,
    scLink,
    scCard;

var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

/* Константы названий для коммерческих банков */
const cTypeBank = "Коммерческий банк",
      cNumForm  = "",
      cKontr    = "Проверяющий",
      cZav      = "Упр.филиалом";

const cRub = 0;  /* Код Российского рубля */
const NatISO = "рублях";  /* Название национальной валюты */
const NatRub = " руб. ", NatKop = " коп.";

/* Значения doc->TypeOper для операций открытия счета */
const OP_OPENCAS =  1; /* наличными */
const OP_OPENNCS = 51; /* безналичными */
const OP_OPENTRN = 58; /* переводом */    

const SB_DEPOSIT = 1;

const NullDate = date( 0, 0, 0 );

private var {Name_Bank};

macro GetQuart

  var
    Q,
    Year,
    Mon;

  DateSplit( doc.rec.Date_Document, null, Mon, Year );

  if ( Mon == 3 )
    Q = 1;
  elif ( Mon == 6 )
    Q = 2;
  elif ( Mon == 9 )
    Q = 3;
  else
    Q = 4;
  end;
  return Q + "-й квартал " + Year + " года";
end;

/*****************************************************************
               Определение кода национальной валюты
*****************************************************************/
macro NatCur()

  var retCur;

  cur.KeyNum = 1;
  cur.rec.ExternalCode = {NationalCur};
  if ( cur.getEQ )
    retCur = cur.rec.Code_Currency;
  else
    retCur = -1;
  end;

  cur.KeyNum = 0;

  return retCur;

end;

/*****************************************************************
                     Строковый формат даты
******************************************************************/
macro DateToStr( pDate )

  var d, m, y, Day, Month, Year;

  DateSplit( pDate, d, m, y );
  Day = String( d );

  if   (m ==  1) Month = " января ";
  elif (m ==  2) Month = " февраля ";
  elif (m ==  3) Month = " марта ";
  elif (m ==  4) Month = " апреля ";
  elif (m ==  5) Month = " мая ";
  elif (m ==  6) Month = " июня ";
  elif (m ==  7) Month = " июля ";
  elif (m ==  8) Month = " августа ";
  elif (m ==  9) Month = " сентября ";
  elif (m == 10) Month = " октября ";
  elif (m == 11) Month = " ноября ";
  elif (m == 12) Month = " декабря ";
  else           Month = "  ";
  end;

  Year = String(y);

  return Day + Month + Year + " г.";

end;

/*****************************************************************
                   Преобразование к Ф.И.О.
******************************************************************/
macro StrFIO( fio )
  var n, s, str;

  s = Trim( fio );
  n = Index( s, " " );

  if ( n == 0 )
    str = s;
  else
    str = StrUpr( SubStr( s, 1, n ), 1 );
    s = SubStr( s, n+1 );
    str = str + StrUpr(SubStr( s, 1, 1 )) + ".";
    n = Index( s, " " );
    if ( n != 0 )
      str = str + StrUpr(SubStr( s, n+1, 1 )) + ".";
    end;
  end;

  return str;
end;

/*****************************************************************
            Найти карточный счет и основную карту
******************************************************************/
macro findCardAccount( referenc )

  var oldKeyNum,
      retValue = false;

  scAcc.rewind(); scAcc.clear();
  scAcc.rec.Referenc = referenc;
  if( scAcc.getEQ() )
    oldKeyNum = scLink.keyNum;
    scLink.keyNum = 6;
    scLink.rewind(); scLink.clear();
    scLink.rec.cardAccRef = referenc;
    scLink.rec.mainCard   = "X";
    if( ( scLink.getGE()        == true     ) and
        ( scLink.rec.cardAccRef == referenc ) and
        ( scLink.rec.mainCard   == "X"      )    )
      scCard.rewind(); scCard.clear();
      scCard.rec.FNCash  = scLink.rec.FNCash;
      scCard.rec.cardRef = scLink.rec.cardRef;
      if( scCard.getEQ() )
        retValue = true;
      end;
    end;
    scLink.keyNum = oldKeyNum;
  end;

  return retValue;

end;

/*****************************************************************
  Получить № пластиковой карты, по которой проводилась транзакция
******************************************************************/
macro getAuthCardNum( authFNCash, authRef )

  var retValue = "";

  scTran.rewind(); scTran.clear();
  scTran.rec.FNCash  = authFNCash;
  scTran.rec.authRef = authRef;
  if( scTran.getEQ() )
    scLink.rewind(); scLink.clear();
    scLink.rec.FNCash         = scTran.rec.FNCash;
    scLink.rec.accCardLinkRef = scTran.rec.accCardLinkRef;
    if( scLink.getEQ() )
      scCard.rewind(); scCard.clear();
      scCard.rec.FNCash  = scLink.rec.FNCash;
      scCard.rec.cardRef = scLink.rec.cardRef;
      if( scCard.getEQ() )
        retValue = scCard.rec.cardNumber;
      end;
    end;
  end;

  return retValue;

end;

/*****************************************************************
                        Фильтр по записям
******************************************************************/
macro Filter( dc )
  var mustBePrinted = TRUE;
  if ( ( dc.rec.FlagStorn != StrFor(0) )  OR
       ( ( dc.rec.TypeOper == Перевод_В_Другой_Вид_Вклада )  AND
         dc.rec.ApplType ) )
    mustBePrinted = FALSE;
  end;
  return mustBePrinted;
end;

/*****************************************************************
                  Основная процедура выписки
******************************************************************/
macro Extract( Ref, Date1, Date2, sClient, sFIO, Standart, Archive )

  var stat, rub, kop, sOpenSum, sFilial, sISO, sVklad, sPeriod,
      totalOutSum = $0.00, totalInSum = $0.00, outcomeRest = $0.00,
      availableLimit = $0.00, blockedSum = $0.00,
      sDate, sOper, sOpen, sBaseCard;

  var needCard = false,
      cardNum;

  var sTypeBank, sNumForm, sKontr, sZav;

  /* Определение кода национальной валюты */
  var cNatCur = NatCur();

  var ClientName;

  /* Подстановка названий, согласно стандарту работы банка */
  if ( Standart == 0 )
  /* Сбербанк РФ */
    sTypeBank = "Сбербанк России";
    sNumForm  = "Ф.№204-с";
    sKontr    = "Контролер";
    sZav      = "Зав.филиалом";
  else
  /* Коммерческий банк */
    sTypeBank = cTypeBank;
    sNumForm  = cNumForm;
    sKontr    = cKontr;
    sZav      = cZav;
  end;


  /* Чтение записи счета */           
  dep.rec.Referenc = Ref;
  if ( NOT dep.GetEQ )
    MsgBox( "Не найдена запись счета с Reference = ", Ref );
    Exit( 0 );
  end;

 /* Подключение архивов */
/*  Шилов: Отрубил работу с архивом до лучших времен 
  if ( Archive and ( dep.rec.Limit_Date != NullDate ) and dep.rec.ArhFlag )
    GetDocsAndReopenTemp( dep.rec.Referenc, dep.rec.Open_Date, doc );
  end;
*/

  /* Чтение записи вида вклада */
  dtp.rec.FlagCur = dep.rec.IsCur;
  dtp.rec.Kind    = dep.rec.Type_Account;
  if ( NOT dtp.GetEQ )
    dtp.rec.Name = "???";
  end;
  /* Чтение названия валюты */
  if ( ( dep.rec.IsCur )  OR  ( cNatCur != cRub ) )
    cur.rec.Code_Currency = dep.rec.Code_Currency;
    if ( cur.GetEQ )
      sISO = cur.rec.Short_Name;
    else
      sISO = "???";
    end;
  else
    sISO = NatISO;  /* Национальная валюта */
  end;
  /* Поиск документа открытия счета */
  doc.Clear;
  doc.rec.Referenc = Ref;
  doc.rec.Date_Document = dep.rec.Open_Date;

  doc.addFilter( "t.t_Referenc = " + string( Ref ) +
                 " and t.t_Date_Document >= " + sqlDateToStr( dep.rec.Open_Date ) );

  stat = doc.getGE;
  if ( stat                                    AND
       ( doc.rec.Referenc == Ref )                 AND
       ( doc.rec.Date_Document >= dep.rec.Open_Date )  AND
       ( ( doc.rec.TypeOper == OP_OPENCAS )  OR
         ( doc.rec.TypeOper == OP_OPENNCS )  OR
         ( doc.rec.TypeOper == OP_OPENTRN ) ) )
    if ( ( dep.rec.IsCur == 0 )  AND  ( cNatCur == cRub ) )
      RubToStr( doc.rec.InSum, rub, kop );
      sOpenSum = String((Int(DoubleL(doc.rec.InSum))):a) +
                 NatRub + kop + NatKop;
    else
      sOpenSum = String(doc.rec.InSum:a) + " " + sISO;
    end;
  else
    sOpenSum = "_________________________";
  end;

  doc.dropFilter;

  /* Чтение записи вкладчика */
  if ( NOT depclnt.GetRecord( dep.rec.CodClient ) )
    MsgBox( "Не найдена запись вкладчика с CodClient = ", dep.rec.CodClient );
    Exit( 0 );
  end;
  /* Чтение записи Операциониста */
  opr.rec.Oper = {oper};
  if ( opr.GetEQ )
    sOper = opr.rec.Name;
  else
    sOper = "???";
  end;
  /* Чтение записи о Филиале */
  fil.rec.FNCash = dep.rec.FNCash;
  fil.GetEQ;
  if (not findDepartment(dep.rec.FNCash, sFilial))
    sFilial = "???";
  end;

  /* Подготовка элементов заголовка */
//  sVklad  = "\"" + dtp.rec.Name + "\" в " + sISO;
  sVklad  = "\"" + dtp.rec.Name;
  if (GetBankStandart( ) == 0)
    sVklad = sVklad + " Сбербанка России";
  end;
  sVklad = sVklad + "\" в " + sISO;

  sPeriod = DateToStr( Date1 ) + " по " + DateToStr( Date2 );
  sOpen   = DateToStr( dep.rec.Open_Date ) + " на сумму: " + sOpenSum;
  sBaseCard = "";

  if( index( dep.rec.UserTypeAccount, "К" ) != 0 )
    scAcc  = TBFile( "scAcc.dbt",  "R", 0, "scAcc.dbt",  "spcard.def" );
    scTran = TBFile( "scTran.dbt", "R", 0, "scTran.dbt", "spcard.def" );
    scLink = TBFile( "scLink.dbt", "R", 0, "scLink.dbt", "spcard.def" );
    scCard = TBFile( "scCard.dbt", "R", 0, "scCard.dbt", "spcard.def" );
    if( findCardAccount( dep.rec.Referenc ) )
      needCard       = true;
      sBaseCard      = "основная карта № " + scCard.rec.cardNumber;
      availableLimit = dep.rec.Limit + dep.rec.Sum_Rest - scAcc.rec.BlockSum - scAcc.rec.OverSum;
      blockedSum     = scAcc.rec.BlockSum;
    end;
  end;

  if ( index(dep.rec.UserTypeAccount, "М") )
     
     exec_extract_metal(sTypeBank, {Name_Bank}, sFilial, dep, dtp, sPeriod, depclnt, doc, Date1 , Date2 );
     exit(0);
  end;

  if(  (not SpecialAccess) and OperMayListSpecialAccounts and dep.rec.SpecialAccess)
     ClientName = "XXXXXX X.X.";
  else
     ClientName = depclnt.CurRec.rec.Name1 + " " + depclnt.CurRec.rec.Name2 + " " + depclnt.CurRec.rec.Name3;
  end;

  /* Печать заголовка выписки */
  [
     #                                                                #
     #
     Филиал № #

                                   Выписка из лицевого счета


     Счет № #                          #
     по вкладу: #
     за период с #

     Вкладчик: #

     Счет открыт: #

  ]
  ( sTypeBank,
    sNumForm,
    {Name_Bank},
    sFilial,
    Acc_Form(dep.rec.Account):f,
    sBaseCard,
    sVklad,
    sPeriod,
    ClientName,
    sOpen );

  doc.Clear;
  doc.rec.Referenc = Ref;
  doc.rec.Date_Document = Date1;

  doc.addFilter( "t.t_Referenc = " + string( Ref ) +
                 " and t.t_Date_Document between " + sqlDateToStr( Date1 ) + " and " + sqlDateToStr( Date2 ) );

  stat = doc.getGE;
  if ( stat  AND
       ( doc.rec.Referenc == Ref )         AND
       ( doc.rec.Date_Document >= Date1 )  AND
       ( doc.rec.Date_Document <= Date2 ) )
      [ Входящий остаток                                                                                         ###############]( ( doc.rec.Rest - doc.rec.InSum + doc.rec.OutSum ):a:15:2 );
    if( needCard )
      [ +--------------------+-----------------------+-------------------------------------+-----------------+-----------------+-----------------+--------------------------+---------------------+
        |    № документа     |      Дата операции    |               Операция              |    Зачисление   |     Списание    |     Остаток     |  Корреспондирующий счет  |     Номер карты     |
        +--------------------+-----------------------+-------------------------------------+-----------------+-----------------+-----------------+--------------------------+---------------------+];
    else
      [ +--------------------+-----------------------+-------------------------------------+-----------------+-----------------+-----------------+--------------------------+
        |    № документа     |      Дата операции    |               Операция              |    Зачисление   |     Списание    |     Остаток     |  Корреспондирующий счет  |
        +--------------------+-----------------------+-------------------------------------+-----------------+-----------------+-----------------+--------------------------+];
    end;
    while ( stat  AND
            ( doc.rec.Referenc == Ref )         AND
            ( doc.rec.Date_Document >= Date1 )  AND
            ( doc.rec.Date_Document <= Date2 ) )
      if ( IsServDoc( doc.rec )  AND  Filter( doc ) )
        totalOutSum = totalOutSum + doc.rec.OutSum;
        totalInSum  = totalInSum  + doc.rec.InSum;
        outcomeRest = doc.rec.Rest;
        if ( doc.rec.Date_Document == doc.rec.DepDate_Document )
          sDate = String( doc.rec.Date_Document );
        else
          sDate = String( doc.rec.Date_Document ) + "/" +
                  String( doc.rec.DepDate_Document );
        end;
        if ( ( doc.rec.Mode == 1 ) and ( doc.rec.TypeOper == Зачисление_Процентов ) )
          if( needCard )
            [ | ################################################################ |                 |                 |                 |                          |                     |]
            ( "Проценты за " + GetQuart + ":" );
          else
            [ | ################################################################ |                 |                 |                 |                          |]
            ( "Проценты за " + GetQuart + ":" );
          end;
        end;
        // найдем номер документа(номер РДД с минимальным ID в связке)
        var docNumber = "0";
        var vCorrAcc = "";
        // Определение Номера документа
        var cmd = RsdCommand( "SELECT RT.T_DOCUMENTNUMBER as docnumber, RT.T_PAYERACCOUNT as payacc, RT.T_RECEIVERACCOUNT as recacc FROM drtdocument_dbt rt, dsb_casln_dbt cas " +
                                "WHERE RT.T_OPAPPLICATIONKEY = CAS.T_APPLICATIONKEY1 AND RT.T_OPAPPLICATIONKIND = CAS.T_APPLICATIONKIND1 " +
                                  "AND CAS.T_APPLICATIONKEY2 = ? AND CAS.T_APPLICATIONKIND2 = ? ORDER BY RT.T_DOCUMENTID ASC" );
        cmd.AddParam( "appKey", RSDBP_IN, doc.rec.APPLICATIONKEY );
        cmd.AddParam( "appKind", RSDBP_IN, doc.rec.IAPPLICATIONKIND );
        cmd.NullConversion = true;
        cmd.execute;
        var rs = RsdRecordSet( cmd );
        if ( rs.moveNext )
          docNumber = rs.value( "docnumber" );
        end;
        // Определение Корреспондирующего счета
        var cmdA;
        if ( doc.rec.InSum > 0 )
          cmdA = RsdCommand( "SELECT RT.T_PAYERACCOUNT as corracc FROM drtdocument_dbt rt, dsb_casln_dbt cas " +
                               "WHERE RT.T_OPAPPLICATIONKEY = CAS.T_APPLICATIONKEY1 AND RT.T_OPAPPLICATIONKIND = CAS.T_APPLICATIONKIND1 " +
                                 "AND CAS.T_APPLICATIONKEY2 = ? AND CAS.T_APPLICATIONKIND2 = ? AND RT.T_RECEIVERACCOUNT = ? ORDER BY RT.T_DOCUMENTID ASC" );
        else
          cmdA = RsdCommand( "SELECT RT.T_RECEIVERACCOUNT as corracc FROM drtdocument_dbt rt, dsb_casln_dbt cas " +
                               "WHERE RT.T_OPAPPLICATIONKEY = CAS.T_APPLICATIONKEY1 AND RT.T_OPAPPLICATIONKIND = CAS.T_APPLICATIONKIND1 " +
                                 "AND CAS.T_APPLICATIONKEY2 = ? AND CAS.T_APPLICATIONKIND2 = ? AND RT.T_PAYERACCOUNT = ? ORDER BY RT.T_DOCUMENTID ASC" );
        end;
        cmdA.AddParam( "appKey", RSDBP_IN, doc.rec.APPLICATIONKEY );
        cmdA.AddParam( "appKind", RSDBP_IN, doc.rec.IAPPLICATIONKIND );
        cmdA.AddParam( "acc", RSDBP_IN, doc.rec.Account );
        cmdA.NullConversion = true;
        cmdA.execute;
        var rsA = RsdRecordSet( cmdA );
        if ( rsA.moveNext )
          vCorrAcc = rsA.value( "corracc" );
        end;
        /////////////////////////////////////////////////////////////
        if( needCard )
          cardNum = "";
          if( getBitFlag( doc.rec.Flags, 11 ) )
            cardNum = getAuthCardNum( doc.rec.FNCash, doc.rec.ListTransfer );
          end;
          [ |####################| ##################### | ################################### | ############### | ############### | ############### | ######################## | ################### |]
          ( docNumber:r, sDate,       doc.rec.Ground:w,        doc.rec.InSum:a:15:2, doc.rec.OutSum:a:15:2, doc.rec.Rest:a:15:2, vCorrAcc,    cardNum );
        else
          [ |####################| ##################### | ################################### | ############### | ############### | ############### | ######################## |]
          ( docNumber:r, sDate,       doc.rec.Ground:w,        doc.rec.InSum:a:15:2, doc.rec.OutSum:a:15:2, doc.rec.Rest:a:15:2, vCorrAcc );
        end;
      end;
      stat = doc.Next;
    end;
    if( needCard )
      [ +--------------------+-----------------------+-------------------------------------+-----------------+-----------------+-----------------+--------------------------+---------------------+];
    else
      [ +--------------------+-----------------------+-------------------------------------+-----------------+-----------------+-----------------+--------------------------+];
    end;
      [ Итого за период                                                      ###############   ###############                  ]( totalInSum:a:15:2, totalOutSum:a:15:2 );
      [ Исходящий остаток                                                                                        ###############]( outcomeRest:a:15:2 );
    if( needCard )
      [
        Доступный лимит на настоящий момент:                                                                     ###############]( availableLimit:a:15:2 );
      [ Блокированная сумма на настоящий момент:                                                                 ###############]( blockedSum:a:15:2 );
    end;
    [

         Выписка составлена по запросу #

         Предоставляемая информация является банковской тайной.


                                                             #_________________________ #

                                                             #_________________________ #


        #                                                        М. П.
    ]
    ( sClient + ": " + StrFIO(sFIO),
      sKontr, StrFIO(sOper),
      sZav, StrFIO(fil.rec.BranchChief),
      DateToStr(Date()) );
  else
    println( "  \nДокументов за период с ", Date1,
                                    " по ", Date2, " не найдено." );
  end;

  doc.dropFilter;
end;
