/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Страхование частных вкладов.

  Формирование реестра обязательств.

  Панель параметров предварительного формирования реестра.

*****************************************************************/

import DI_Def;

record dlg_prev ( "DIP_FORM", "rtusrmac" ) dialog;

/* Переменные параметров для предварительного формирования реестра */
var panDateMake = {curdate};  /* Дата формирования */
var panLoansOn  = FALSE;      /* Флаг: Loans Online */
var panLoansOff = FALSE;      /* Флаг: Loans Offline */
var panNeedCalc = FALSE;      /* Флаг выполнения расчета */
var panEscape   = FALSE;      /* Флаг отказа от работы (нажата клавиша ESC) */

/****************************************************************/
/*                   Обработка событий панели.                  */
/****************************************************************/
macro EventProcPrev( dlg, cmd, id, key )

  if ( cmd == DLG_KEY )
    /* msgbox(key); */
    if   ( key == KEY_F2 )
      if ( dlg.DateMake == Date( 0, 0, 0 ) )
        MsgBox( "Дата не должна быть нулевой!" );
        SetFocus( dlg, 0 );
        /* return CM_IGNORE; */
      else
        if ( NOT ( ( dlg.NeedCalc == FALSE )  OR
                   ( dlg.LoansOn  == TRUE  )  OR
                   ( dlg.LoansOff == TRUE  ) ) )
          MsgBox( "Расчет не проводится без записей по кредитам!" );
          SetFocus( dlg, 1 );
        else
          return CM_SAVE;
        end;
      end;
    elif ( key == KEY_F9 )
      return CM_IGNORE;
    elif ( key == KEY_ESC )
      panEscape = TRUE;
      return CM_SAVE;
    elif ( key == KEY_SPACE )
      if   ( id == 1 )
        if ( dlg.LoansOn == "X" )
          dlg.LoansOn = "";
        else
          dlg.LoansOn  = "X";
          dlg.LoansOff = "";
        end;
      elif ( id == 2 )
        if ( dlg.LoansOff == "X" )
          dlg.LoansOff = "";
        else
          dlg.LoansOff = "X";
          dlg.LoansOn  = "";
        end;
      elif ( id == 3 )
        if ( dlg.NeedCalc == "X" )
          dlg.NeedCalc = "";
        else
          dlg.NeedCalc = "X";
        end;
      end;
      UpdateFields( dlg );
    end;
  end;

end;

/****************************************************************/
/*            Панель параметров формирования реестра.           */
/****************************************************************/
macro DI_Prev_Panel()

  /* Установка начальных значений полей панели */
  dlg_prev.DateMake = panDateMake;
  if ( panLoansOn )
    dlg_prev.LoansOn = "X";
  else
    dlg_prev.LoansOn = "";
  end;
  if ( panLoansOff )
    dlg_prev.LoansOff = "X";
  else
    dlg_prev.LoansOff = "";
  end;
  if ( panNeedCalc )
    dlg_prev.NeedCalc = "X";
  else
    dlg_prev.NeedCalc = "";
  end;
  panEscape = FALSE;

  /* Работа с панелью */
  RunDialog( dlg_prev, "EventProcPrev" );

  /* Установка параметров для предварительного формирования реестра */
  panDateMake = dlg_prev.DateMake;
  if ( dlg_prev.LoansOn == "X" )
    panLoansOn = TRUE;
  else
    panLoansOn = FALSE;
  end;
  if ( dlg_prev.LoansOff == "X" )
    panLoansOff = TRUE;
  else
    panLoansOff = FALSE;
  end;
  if ( dlg_prev.NeedCalc == "X" )
    panNeedCalc = TRUE;
  else
    panNeedCalc = FALSE;
  end;

end;

/* DI_Prev_Panel(); */
