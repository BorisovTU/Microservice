import sbbankmsg;
import rtutils;

//---------------------------------------------------------------------------
/*!
 * Класс исключения для макросов
 * содержит код ошибки code и сообщение об ошибке message
 */
class BtException(msg, errcode)
  private var m_msg = msg;
  private var m_code = errcode;

  macro code()
    return m_code;
  end;
  macro message()
    return m_msg;
  end;
end;
//---------------------------------------------------------------------------
/*!
 * Возвращает true, если тип исключения BtException
 */
macro isXBT( errObj )
  if( isEqClass("BtException", errObj)
    or (objHasProp(errObj,"err") and isEqClass("BtException", errObj.err)) )
    return true;
  end;
  return false;
end;
//---------------------------------------------------------------------------
/*!
 * Возвращает ссылку на объект исключения BtException по объекту errObj
 * если тип errObj не BtException, возвращает null
 */
macro getXBT( errObj )
  var xbtObj = null;
  
  if( isEqClass("BtException", errObj) )
    xbtObj = errObj;
  elif( objHasProp(errObj,"err") and isEqClass("BtException", errObj.err) )
    xbtObj = errObj.err;
  end;

  return xbtObj;
end;
//---------------------------------------------------------------------------
/*!
 * Возвращает код ошибки исключения BtException
 * если тип errObj не BtException, генерируется ошибка RunError
 */
macro getXBTCode( errObj )
  var xbtObj = getXBT(errObj);
  
  if( xbtObj == null )
    RunError("Неверный тип исключения");
  end;
  
  return xbtObj.code;
end;
//---------------------------------------------------------------------------
/*!
 * Возвращает сообщение исключения BtException
 * если тип errObj не BtException, генерируется ошибка RunError
 */
macro getXBTMessage( errObj )
  var xbtObj = getXBT(errObj);
  
  if( xbtObj == null )
    RunError("Неверный тип исключения");
  end;
  
  return xbtObj.message;
end;
//---------------------------------------------------------------------------
/*!
 * Отображает сообщение исключения BtException
 * если тип errObj не BtException, генерируется ошибка RunError
 */
macro showXBT( errObj )
  msgbox(getXBTMessage(errObj));
end;
//---------------------------------------------------------------------------
/*!
 * Функция проверяет условия и при необходимости 
 * генерирует исключение BtException
 * для генерации исключения могут использоваться следующие комбинации
 * XBT(str);         // BtException(str         , 1664)
 * XBT(bool);        // BtException(msg(1664)   , 1664)
 * XBT(int);         // BtException(msg(int)    , int)
 * XBT(bool,str);    // BtException(str         , 1664)
 * XBT(bool,int);    // BtException(msg(1664)   , int)
 * XBT(int,str);     // BtException(msg(int)+str, int)
 * XBT(int,int2);    // BtException(msg(int2)   , int2)
 */
macro XBT( result, error )
	var fail = false; 
	var msg    = "",  errcode = 0;
	var altmsg = "",  altcode = 0;

	if(valtype(result) == V_BOOL)
		fail = not result;
		errcode = 1664;
		msg = getSbBankMessage(errcode);
		if(valtype(error) == V_STRING)
			msg = error;
		elif(valtype(error) == V_INTEGER)
			altcode = error;
		end;
	elif(valtype(result) == V_INTEGER)
		fail = (result != 0);
		errcode = result;
  	msg = getSbBankMessage(errcode);
		if(valtype(error) == V_STRING)
			altmsg = error;
		elif(valtype(error) == V_INTEGER)
			altcode = error;
		end;
  elif(valtype(result) == V_STRING)
    fail = true;
    errcode = 1664;
    msg = result;
	end;

	if( fail )
	  if(altcode != 0)
	    errcode = altcode;
	    msg = getSbBankMessage(altcode);
	  end;
	
	  if( altmsg != "" )
	    msg = altmsg + ". " + msg;
	  end;
    	
	  RunError( msg, BtException(msg, errcode) );
	end;
end;
//---------------------------------------------------------------------------
/*!
 * Заново генерирует ошибку obj
 * стандартный вызов RunError без параметров отбрасывает значение второго параметра,
 * а здесь мы его указываем заново
 */
macro ReRunError( obj )
  RunError(obj.message, obj.err);
end;
//---------------------------------------------------------------------------

