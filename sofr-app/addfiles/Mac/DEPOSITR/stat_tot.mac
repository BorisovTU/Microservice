/*

	Подсчет общестатистических данных (вместо общестатической записи в
	файле статистики
*/

import deprIntr;

record totalStat( "sb_depst.dbt" );
file   stDepStat( "sb_depst.dbt" ) key 0;
file   stDepType( "sb_dtyp.dbt" ) key 1;

macro stSumUp

  if ( totalStat.Date == date( 0, 0, 0 ) )
    copy( totalStat, stDepStat );
    stDepStat.Kind = "Общ.стат.оп.";
    stDepStat.FlagRez = strFor( 0 );
  else
    totalStat.ColAccounts = totalStat.ColAccounts + stDepStat.ColAccounts;
    totalStat.ColOpenedAcc = totalStat.ColOpenedAcc + stDepStat.ColOpenedAcc;
    totalStat.ColClosedAcc = totalStat.ColClosedAcc + stDepStat.ColClosedAcc;
    totalStat.ColOper = totalStat.ColOper + stDepStat.ColOper;
    totalStat.Rest = totalStat.Rest + stDepStat.Rest;
    totalStat.DebSum = totalStat.DebSum + stDepStat.DebSum;
    totalStat.KredSum = totalStat.KredSum + stDepStat.KredSum;
    totalStat.RestActiv = totalStat.RestActiv + stDepStat.RestActiv;
    totalStat.MemDebSum = totalStat.MemDebSum + stDepStat.MemDebSum;
    totalStat.MemKredSum = totalStat.MemKredSum + stDepStat.MemKredSum;
    totalStat.OpenInOper = totalStat.OpenInOper + stDepStat.OpenInOper;
    totalStat.DopInOper = totalStat.DopInOper + stDepStat.DopInOper;
    totalStat.OutOper = totalStat.OutOper + stDepStat.OutOper;
    totalStat.MemInOper = totalStat.MemInOper + stDepStat.MemInOper;
    totalStat.CurOpenAc = totalStat.CurOpenAc + stDepStat.CurOpenAc;
    totalStat.CurClosAc = totalStat.CurClosAc + stDepStat.CurClosAc;
  end;
end;

macro stQuartStart( dd )

  var
    d, m, y;

  dateSplit( dd, d, m, y );
  if ( m <= 3 )
    return( date( 1, 1, y ) );
  elif ( m <= 6 )
    return( date( 1, 4, y ) );
  elif ( m <= 9 )
    return( date( 1, 7, y ) );
  else
    return( date( 1, 10, y ) );
  end;
end;

macro stTreatTypeForCit( branch, isCur, currCode, dd, mode, cit )

  clearRecord( stDepStat );
  stDepStat.FNCash = branch;
  stDepStat.IsCur = isCur;
  stDepStat.Kind = stDepType.Kind;
  stDepStat.CodCur = currCode;
  stDepStat.FlagRez = cit;
  stDepStat.Date = dd;
  stDepStat.Mode = mode;
  if ( getLE( stDepStat ) 
    and ( stDepStat.FNCash == branch )
    and ( stDepStat.IsCur == isCur )
    and ( stDepStat.Kind == stDepType.Kind )
    and ( stDepStat.CodCur == currCode )
    and ( stDepStat.FlagRez == cit ) )
    if ( ( stDepStat.Date != dd ) or ( stDepStat.Mode != mode ) )
      stDepStat.DebSum = $0;
      stDepStat.KredSum = $0;
      stDepStat.MemDebSum = $0;
      stDepStat.CurOpenAc = 0;
      stDepStat.CurClosAc = 0;
      if ( stDepStat.Date < stQuartStart( dd ) )
        stDepStat.OpenInOper = 0; 
        stDepStat.DopInOper = 0; 
        stDepStat.MemInOper = 0; 
        stDepStat.OutOper = 0; 
      end;
    end;
    stSumUp; 
  end;
end;

macro getTotalDepStats( dd, currCode )

  var
    recFound,
    branch = numFNCash,
    isCur,
    mode = getCurrentMode;
  
  if ( currCode != 0 )
    isCur = 1;
  else 
    isCur = 0;
  end;

  clearRecord( totalStat );
  clearRecord( stDepType );
  stDepType.FlagCur = isCur;
  recFound = getGE( stDepType );
  while ( recFound and ( stDepType.FlagCur == isCur ) )
    stTreatTypeForCit( branch, isCur, currCode, dd, mode, strFor( 0 ) );
    stTreatTypeForCit( branch, isCur, currCode, dd, mode, strFor( 1 ) );
    recFound = next( stDepType );
  end;
end;
