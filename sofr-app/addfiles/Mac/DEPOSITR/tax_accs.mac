/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Выписки по требованию налоговых органов.

  Справка о наличии счетов клиента в банке.

*****************************************************************/

import DeprIntr, RSD, SQLConv, SQLExec;

private const PTCK_BIC = 3;   // БИК (РФ).
private const PTCK_INN = 16;  // ИНН нашего банка.

private var OurBank_BIC     = Trim( getRegNumOurBank( PTCK_BIC ) );
private var OurBank_Name    = "";
private var OurBank_INN     = Trim( getRegNumOurBank( PTCK_INN ) );
private var OurBank_KPP     = "";

private var {Bank_INN};
private var {Name_Bank};
private var {curdate};
private var {oper};

private var clientList = TClientList;

private var prsn = TBFile( "person.dbt", "bank.def", "r", 0 );
private var tpac = TBFile( "typeac.dbt", "bank.def", "r", 0 );
private var tcur = TBFile( "currency.dbt", "bank.def", "r", 0 );

var ErrCodeWR, ErrTextWR;

// ----------------------------------------
//    Получение должности операциониста.
// ----------------------------------------
macro GetNameTypePerson()
 var sWork = "";

 tpac.ReWind();
 tpac.Clear();
 tpac.rec.iNumType = 14;  /* Уровни доступов. */
 tpac.rec.Type_Account = prsn.rec.cTypePerson;
 if ( tpac.GetEQ() )
   sWork = tpac.rec.Name_Type;
 else
   sWork = "?????";
   println( " *** Ошибка при чтении должности операциониста с кодом = ", prsn.rec.cTypePerson );
   ErrCodeWR = prsn.Status( ErrTextWR );
   println( "  Ошибка " + String( ErrCodeWR ) + ": " + ErrTextWR );
 end;
 return sWork;
end;

// ----------------------------------------
//          Цифровой код валюты.
// ----------------------------------------
private macro Currency_Code( pCurr )
  var rCurr = "643";
  if ( pCurr > 0 )
    tcur.Clear();
    tcur.rec.Code_Currency = pCurr;
    if ( tcur.GetEQ() )
      rCurr = tcur.rec.EXTERNALCODE;
    end;
  end;
  return rCurr;
end;

// ----------------------------------------
private macro MakeReport( CodeClient, ReportDate )

  var cmd, rs;
  var flagTitle = TRUE;
  var flagYes   = FALSE;
  var vNo = "";
  var sOper = "";
  var sWork = "";

  if ( ( ValType( codeClient ) != V_INTEGER )  OR  ( codeClient == 0 )  OR  ( NOT clientList.GetRecord( codeClient ) ) )
    println( " " );
    println( "  Клиент с кодом " + CodeClient + " не найден." );
  end;

  prsn.Clear();
  prsn.rec.Oper = {oper};

  if ( prsn.GetEQ() )
    sOper = prsn.rec.Name; 
  else
    println( " " );
    println( "  Пользователь с кодом " + {oper} + " не найден." );
  end;

  sWork = GetNameTypePerson();

  OurBank_Name = {Name_Bank};

  [
                                                                          Утверждена
                                                             Приказом Федеральной налоговой службы
                                                             от 30 марта 2007 г. N ММ-3-06/178@


                                                             Наименование налогового органа
                                                             
                                                             ____________________________________________


                                                             Адрес ______________________________________



                           Справка о наличии счетов в банке у физического лица


     ####################################################################################################
                  (полное или сокращенное наименование банка в соответствии с Книгой государственной
                                        регистрации кредитных организаций)

             +-------------------+                    +-------------------+
     ИНН     |#|#|#|#|#|#|#|#|#|#|            КПП     | | | | | | | | | | |
             +-------------------+                    +-------------------+

             +-----------------+
     БИК     |#|#|#|#|#|#|#|#|#|
             +-----------------+

           В соответствии с запросом налогового органа от _________________   № ____________________

           в отношении
      ####################################################################################################
                                          (ФИО физического лица)

             +-----------------------+            +-----------------+
     ИНН/КИО |#|#|#|#|#|#|#|#|#|#|#|#|        КПП | | | | | | | | | |
             +-----------------------+            +-----------------+]
  (
    ( "Банк " + OurBank_Name ):w,
    SubStr( OurBank_INN,  1, 1 ),
    SubStr( OurBank_INN,  2, 1 ),
    SubStr( OurBank_INN,  3, 1 ),
    SubStr( OurBank_INN,  4, 1 ),
    SubStr( OurBank_INN,  5, 1 ),
    SubStr( OurBank_INN,  6, 1 ),
    SubStr( OurBank_INN,  7, 1 ),
    SubStr( OurBank_INN,  8, 1 ),
    SubStr( OurBank_INN,  9, 1 ),
    SubStr( OurBank_INN, 10, 1 ),
    SubStr( OurBank_BIC,  1, 1 ),
    SubStr( OurBank_BIC,  2, 1 ),
    SubStr( OurBank_BIC,  3, 1 ),
    SubStr( OurBank_BIC,  4, 1 ),
    SubStr( OurBank_BIC,  5, 1 ),
    SubStr( OurBank_BIC,  6, 1 ),
    SubStr( OurBank_BIC,  7, 1 ),
    SubStr( OurBank_BIC,  8, 1 ),
    SubStr( OurBank_BIC,  9, 1 ),
    ( ClientList.CurRec.Rec.Name1 + " " + ClientList.CurRec.Rec.Name2 + " " + ClientList.CurRec.Rec.Name3 ):w:c,
    SubStr( ClientList.CurRec.Rec.INN,  1, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  2, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  3, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  4, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  5, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  6, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  7, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  8, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  9, 1 ),
    SubStr( ClientList.CurRec.Rec.INN, 10, 1 ),
    SubStr( ClientList.CurRec.Rec.INN, 11, 1 ),
    SubStr( ClientList.CurRec.Rec.INN, 12, 1 )
  );


  cmd = RsdCommand( "select T.T_ACCOUNT as acc, T.T_TYPE_ACCOUNT as kind, T_CODE_CURRENCY as curr, T.T_OPEN_DATE as open, T.T_CLOSE_DATE as close " +
                      "from ddepositr_dbt t " +
                        "where T.T_CODCLIENT=? " +
                          "order by T.T_OPEN_CLOSE, T.T_CODE_CURRENCY, T.T_TYPE_ACCOUNT, T.T_ACCOUNT" );

  cmd.addParam( "clnt", RSDBP_IN );
  cmd.value( "clnt" ) = CodeClient;

  cmd.execute;

  rs = RsdRecordset( cmd );

  while ( rs.moveNext )

    if ( flagTitle )
      
      [                                                                                  +---+
                1. Представляет информацию о счетах, открытых в банке указанному лицу    | X |
                                                                                         +---+

          +---------------------------+---------------+-----------------+-------------------+-------------------+
          |             1             |       2       |        3        |         4         |          5        |
          +---------------------------+---------------+-----------------+-------------------+-------------------+
          |        Номер счета        |   Вид счета   |Цифровой код вида|Дата открытия счета|Дата закрытия счета|
          |                           |               |  валюты счета   |    (дд.мм.гггг)   |    (дд.мм.гг)     |
          |                           |               |(в соответствии с|                   |                   |
          |                           |               | Общероссийским  |                   |                   |
          |                           |               | классификатором |                   |                   |
          |                           |               |     валют)      |                   |                   |
          +---------------------------+---------------+-----------------+-------------------+-------------------+];
      flagYes = TRUE;
      flagTitle = FALSE;
    end;

    var vCurr = Currency_Code( rs.value( "curr"  ) );

    [   | ######################### | ############# |       ###       |     ##########    |     ##########    |]
    (
      rs.value( "acc"   ):25,
      rs.value( "kind"  ):12,
      vCurr: 3,
      String( rslDate( rs.value( "open" ) ) ),
      String( rslDate( rs.value( "close" ) ) ) );
  end;

  if ( flagYes )
    vNo = "";
    [   +---------------------------+---------------+-----------------+-------------------+-------------------+];
  else
    vNo = "X";
    [                                                                                  +---+
              1. Представляет информацию о счетах, открытых в банке указанному лицу    |   |
                                                                                       +---+];
  end;

  [
                                                                          +---+
            2. Сообщает об отсутствии счетов в банке у указанного лица    | # |
                                                                          +---+


              Указанная информация предоставляется по состоянию на #


          Представитель
              банка      #################  ###################################   _______________    ##########
                            (должность)                 (Ф.И.О.)                     (подпись)         (дата)



                                                                        М.П.]
  (
    vNo,
    {curdate}:m,
    sWork:17:c,
    sOper:35:c,
    {curdate}
  );

end;

// ----------------------------------------
macro TaxRestReport( CodeClient, ReportDate )

  // msgbox( "TAX_REST=", CodeClient );

  MakeReport( CodeClient, ReportDate );

end;
