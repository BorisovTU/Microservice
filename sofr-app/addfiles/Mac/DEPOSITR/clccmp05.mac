/*
  ---------------------
  R-Style Software Lab.
  ---------------------
  RS-Bank v6
  ---------------------
  RS-Retail
  ---------------------
  Макрос для компенсации 2005 года
  ---------------------
  Расчет суммы компенсации

*/
import RSD, deprintr, issrvdoc;

private const NullDate = Date(0,0,0);
var IsAllAccount     = false,  // true - все счета вкладчика
    SumCompensBefore = $0,     // Сумма ранее выплаченных компенсаций 
                               // (за исключением оплаты ритуальных услуг)
    SumCompensRitual = $0,     // Ранее выплаченные суммы на оплату ритуальных услуг
    CompensCoef      = 1.0,    // Коэффициент пересчета компенсации
    RestOn20061991   = $0;     // Осаток на 20.06.1991
var FullSum          = $0, 
    Sum              = $0,
    AccCloseDate     = NullDate;

const
   AT04 =  4,  // Инвалиды 1 группы 
   AT25 = 25,  // Родители и опекуны детей-инвалидов 
   AT58 = 58,  // Наследник участника
   AT63 = 63,  // Владельцам детских целевых 
   AT76 = 76,  // Родители и опекуны инвалидов с детства
   AT80 = 80,  // Дети - сироты
   AT82 = 82,  // На оплату ритуальных услуг умершего в 1998 - 2000 гг
   AT84 = 84,  // Родители погибших в мирное время
   AT85 = 85,  // Вкладчики по 1953 г.р.
   AT86 = 86,  // Инвалиды 2 группы по 1960 г.р.
   AT87 = 87,  // Наследник вкладчика по 1953 г.р.
   AT88 = 88,  // Наследник инвалида 1 группы
   AT89 = 89,  // Наследник инвалида 2 группы
   AT90 = 90,  // Наследник родителя детей-инвалидов
   AT91 = 91,  // Наследник родителя инвалида с детства
   AT92 = 92,  // Наследник родителей погибших в мирное время
   AT93 = 93,  // Вкладчики по 1942 г.р.
   AT94 = 94,  // Наследник вкладчика по 1942 г.р.
   AT95 = 95,  // Участник до 1935 г.р.
   AT96 = 96,  // Вкладчик по 1935 г.р.
   AT97 = 97,  // Наследник вкладчика по 1935 г.р.
   AT98 = 98,  // На оплату ритуальных услуг умершего в 2000 - 2006 гг
   AT99 = 99;  // На оплату ритуальных услуг участникам ВОВ

// *********************************************************

macro isSumOfDrobIsOne(array_M, array_N, count_Legacy)
   var   i = 0;
   var   ОбщийЗнаменатель = 1;
   array Числитель;
   if ( count_Legacy < 2 )
      return 0;
   end;
   while (i < (count_Legacy-1) )
     array_M(i+1) = array_M(i)*array_N(i+1) + array_M(i+1)*array_N(i);
     array_N(i+1) = array_N(i)*array_N(i+1);
     i = i+1;
   end;
   if ( array_M(i) == array_N(i))
      return 1;  // Все доли распределены
   elif ( array_M(i) >= array_N(i))
      return 2;  // Числитель больше згнаменателя
   else
      return 0;  
   end;
end;
// ---------------------------------------------------------

macro MakeLegacyPart (compens)
//
// Обработка долей наследства
//
  var   cmd, rs;
  var   ClientType   = compens.rec.Appltype;
  var   count_Legacy = 0;
  ARRAY array_M, array_N;
  var   IsSum;
  var   SumCompens = 0;
//
// Сбор ранее выплаченных долей
//
  if ( (ClientType == AT82)  
   OR  (ClientType == AT98)
   OR  (ClientType == AT99) )  // На ритуальные услуги
    SumCompens = SumCompensRitual;
    if (IsAllAccount)  // По всем счетам
      cmd = RsdCommand(" select t_legacyPart_m, t_legacyPart_n from dcompens_dbt where t_appltype in (17,26,56,81,82,83,98,99) " 
                       " and t_codclient = ? and t_date <> '01.01.0001' ");
      cmd.addParam ("T_codclient", RsdBp_in);
      cmd.value    ("T_codclient") = compens.rec.CodClient;
    else               // По одному счету
      cmd = RsdCommand(" select t_legacyPart_m, t_legacyPart_n from dcompens_dbt where t_account = ? and t_fncash = ? and t_appltype in (17,26,56,81,82,83,98,99) and t_date <> '01.01.0001'" );
      cmd.addParam ("T_ACCOUNT", RsdBp_in);
      cmd.value    ("T_ACCOUNT") = compens.rec.Account;
      cmd.addParam ("T_FNCASH", RsdBp_in);
      cmd.value    ("T_FNCASH") = compens.rec.FNCash;
    end;
  else
    SumCompens = SumCompensBefore;
    cmd = RsdCommand(" select t_legacyPart_m, t_legacyPart_n from dcompens_dbt where t_account = ? and t_fncash = ? and t_appltype not in (17,26,56,81,82,83,98,99) and t_date <> '01.01.0001'" );
    cmd.addParam ("T_ACCOUNT", RsdBp_in);
    cmd.value    ("T_ACCOUNT") = compens.rec.Account;
    cmd.addParam ("T_FNCASH", RsdBp_in);
    cmd.value    ("T_FNCASH") = compens.rec.FNCash;
  end;
  cmd.execute;
  rs = RsdRecordset(cmd); 
  while ( rs.moveNext() )
    if ( (rs.value(0) == 0) or (rs.value(1) == 0) )
      array_M(count_Legacy) = 1;
      array_N(count_Legacy) = 1;
    elif( (rs.value(0) != 0) and (rs.value(1) != 0) ) 
      array_M(count_Legacy) = rs.value(0);
      array_N(count_Legacy) = rs.value(1);
    end;
    count_Legacy = count_Legacy + 1;
  end;
//
// Суммирование ранее выплаченных долей плюс доля текущей компенсации
//
  if (count_Legacy > 0)
    array_M(count_Legacy) = compens.rec.LegacyPart_m;
    array_N(count_Legacy) = compens.rec.LegacyPart_n;
    count_Legacy = count_Legacy + 1;
    isSum = isSumOfDrobIsOne(array_M, array_N, count_Legacy);
    if ( isSum == 1 )
      Sum = FullSum - SumCompens;
    elif (isSum == 2 )
      MsgBox("Сумма долей наследства больше 1");
      exit (1);
    else
      Sum = FullSum * compens.rec.LegacyPart_m / compens.rec.LegacyPart_n;
    end;            
  else
    Sum = FullSum * compens.rec.LegacyPart_m / compens.rec.LegacyPart_n;
  end;
end;
// ---------------------------------------------------------

macro MakeCompAcc (compens,Referenc,Rest,Type_Account,Close_Date)
//
// Создание списка счетов, участвующих в расчете компенсации
//
  var cmd_2, rs_2, cmd_4;
  
  cmd_2 = RsdCommand(" select * from dcomp_acc_dbt where t_fncash = ? and t_autokey = ? and t_referenc = ? " );
  cmd_2.addParam ("t_fncash",   RsdBp_in);
  cmd_2.value    ("t_fncash")   = compens.rec.FNCash;
  cmd_2.addParam ("t_autokey",  RsdBp_in);
  cmd_2.value    ("t_autokey")  = compens.rec.AutoKey;
  cmd_2.addParam ("t_referenc", RsdBp_in);
  cmd_2.value    ("t_referenc") = Referenc;
  cmd_2.execute;
  rs_2 = RsdRecordset(cmd_2); 
  if ( NOT(rs_2.moveNext()) )                
    cmd_4 = RsdCommand("insert into dcomp_acc_dbt values(?, ?, ?, ?, ?, ?)");
    cmd_4.addParam ("t_fncash",           RsdBp_in);
    cmd_4.value    ("t_fncash")         = compens.rec.FNCash;
    cmd_4.addParam ("t_autokey",          RsdBp_in);
    cmd_4.value    ("t_autokey")        = compens.rec.AutoKey;
    cmd_4.addParam ("t_referenc",         RsdBp_in);
    cmd_4.value    ("t_referenc")       = Referenc;
    cmd_4.addParam ("t_restln20061991",   RsdBp_in);
    cmd_4.value    ("t_restln20061991") = Rest;
    cmd_4.addParam ("t_type_account",     RsdBp_in);
    cmd_4.value    ("t_type_account")   = Type_Account;
    cmd_4.addParam ("t_close_date",     RsdBp_in);
    cmd_4.value    ("t_close_date")     = Close_Date;
    if (NOT cmd_4.execute)
      MsgBox ("Ошибка при вводе в базу счетов, участвующих в расчете компенсации");
      exit (1);
    end;
  end;
end;
// ---------------------------------------------------------

macro IsHeir (ClientType)
//
// true - компенсацию получает наследник
//
  var RetVal = false;

  if ((ClientType == AT58) OR
      (ClientType == AT82) OR
      (ClientType == AT87) OR  
      (ClientType == AT88) OR
      (ClientType == AT89) OR
      (ClientType == AT90) OR
      (ClientType == AT91) OR
      (ClientType == AT92) OR
      (ClientType == AT94) OR
      (ClientType == AT97) OR
      (ClientType == AT98) OR
      (ClientType == AT99))
    RetVal = true;
  end;
  return RetVal;
end;
// ---------------------------------------------------------

macro Check_SumCompensBefore (compens)
//
// Выплаченные ранее компенсации, кроме ритуальных услуг
//
  var cmd, rs;
  var ClientType = compens.rec.Appltype,
  SumCompensBefore = 0;
  if (IsAllAccount)  // По всем счетам
    cmd = RsdCommand(" select nvl(Sum(dcompens_dbt.t_sum),0) from dcompens_dbt where dcompens_dbt.t_appltype not in (17,26,56,81,82,83,98,99) " 
                     " and dcompens_dbt.t_codclient = ? and dcompens_dbt.t_date <> '01.01.0001' ");
    cmd.addParam ("T_codclient", RsdBp_in);
    cmd.value    ("T_codclient") = compens.rec.CodClient;
  else               // По одному счету
    cmd = RsdCommand(" select nvl(Sum(t_sum),0) from dcompens_dbt where t_account = ? and t_fncash = ? and t_appltype not in (17,26,56,81,82,83,98,99) and t_date <> '01.01.0001'" );
    cmd.addParam ("T_ACCOUNT", RsdBp_in);
    cmd.value    ("T_ACCOUNT") = compens.rec.Account;
    cmd.addParam ("T_FNCASH", RsdBp_in);
    cmd.value    ("T_FNCASH") = compens.rec.FNCash;
  end;
  cmd.execute;
  rs = RsdRecordset(cmd); 
  if ( rs.moveNext() ) 
    SumCompensBefore = rs.value(0);
  else
    SumCompensBefore = 0;
  end;
end;
// ---------------------------------------------------------

macro Check_SumCompensRitual (compens)
//
// Выплаченные ранее суммы на ритуальные услуги
//
  var cmd, rs;
  var ClientType = compens.rec.Appltype,
  SumCompensRitual = 0;
  if ( (ClientType == AT82)  
   OR  (ClientType == AT98)
   OR  (ClientType == AT99) )
    if (IsAllAccount)  // По всем счетам
      cmd = RsdCommand(" select nvl(Sum(dcompens_dbt.t_sum),0) from dcompens_dbt where dcompens_dbt.t_appltype in (17,26,56,81,82,83,98,99) " 
                       " and dcompens_dbt.t_codclient = ? and dcompens_dbt.t_date <> '01.01.0001' ");
      cmd.addParam ("T_codclient", RsdBp_in);
      cmd.value    ("T_codclient") = compens.rec.CodClient;
    else               // По одному счету
      cmd = RsdCommand(" select nvl(Sum(t_sum),0) from dcompens_dbt where t_account = ? and t_fncash = ? and t_appltype in (17,26,56,81,82,83,98,99) and t_date <> '01.01.0001'" );
      cmd.addParam ("T_ACCOUNT", RsdBp_in);
      cmd.value    ("T_ACCOUNT") = compens.rec.Account;
      cmd.addParam ("T_FNCASH", RsdBp_in);
      cmd.value    ("T_FNCASH") = compens.rec.FNCash;
    end;
    cmd.execute;
    rs = RsdRecordset(cmd); 
    if ( rs.moveNext() ) 
      SumCompensRitual = rs.value(0);
    else
      SumCompensRitual = 0;
    end;
  else
    SumCompensRitual = 0;
  end;
end;
// ---------------------------------------------------------

macro Check_CompensCoef (compens)
//
// Уточняющий коэффициент CompensCoef
//
  var cmd, rs;
  var cDay, cMon, cYear;
  AccCloseDate = NullDate;
  if (compens.rec.rate == 0)
//   Дата закрытия счета
    if (compens.rec.ACCCLSDATE == Date(0,0,0))
      cmd = RsdCommand("select T_CLOSE_DATE from ddepositr_dbt where T_ACCOUNT = ? and T_FNCASH = ? ");
      cmd.addParam("T_ACCOUNT", RsdBp_in);
      cmd.value("T_ACCOUNT") = compens.rec.Account;
      cmd.addParam("T_FNCASH", RsdBp_in);
      cmd.value("T_FNCASH") = compens.rec.FNCash;
      cmd.execute;
      cmd.NullConversion = true;
      rs = RsdRecordset(cmd); 
      if ( rs.moveNext() )
        AccCloseDate = rs.value(0);
      end;
    else
      AccCloseDate = compens.rec.ACCCLSDATE;
    end;
    DateSplit (AccCloseDate, cDay, cMon, cYear);
    if (AccCloseDate == NullDate)
      CompensCoef = 1.0;
    elif ( cYear >= 1996 )
      CompensCoef = 1.0;
    elif ( cYear == 1995 )
      CompensCoef = 0.9;
    elif ( cYear == 1994 )
      CompensCoef = 0.8;
    elif ( cYear == 1993 )
      CompensCoef = 0.7;
    elif ( cYear == 1992 )
      CompensCoef = 0.6;
    elif ( cYear == 1991 )
      CompensCoef = 0.0;
    else
      CompensCoef = 0.0;
    end;  
  else
    CompensCoef = compens.rec.rate;
  end;
end;
// ---------------------------------------------------------

macro Check_RestOn20061991 (compens)
//
// Остаток по счету на 20.06.1991
//
  var cmd, rs;
  var cmd_1, rs_1;
  RestOn20061991 = 0;
  if (compens.rec.restin91 == 0)
    if (IsAllAccount)  // По всем счетам
//    Нахожу все референсы для конкретного кода клиента
      cmd = RsdCommand(" select t_referenc,t_type_account,t_close_date from ddepositr_dbt where t_codclient = ? and t_open_date <= ? ");
      cmd.addParam    ("t_codclient", RsdBp_in);
      cmd.value       ("t_codclient") = compens.rec.CodClient;
      cmd.addParam    ("t_open_date", RsdBp_in);
      cmd.value       ("t_open_date") = date(20,06,1991);
    else  // По одному счету
      cmd = RsdCommand(" select t_referenc from ddepositr_dbt where t_account = ? and T_FNCASH = ?");
      cmd.addParam ("t_account", RsdBp_in);
      cmd.value    ("t_account") = compens.rec.Account;
      cmd.addParam ("T_FNCASH", RsdBp_in);
      cmd.value    ("T_FNCASH") = compens.rec.FNCash;
    end;
    cmd.execute;
    rs = RsdRecordset(cmd); 
    while ( rs.moveNext() ) // Для счета ищется остаток
      cmd_1 = RsdCommand("select t_rest from ( "
                   " select * from dsbdepdoc_dbt "
                   " where t_referenc = ? "
                   "   and T_DATE_DOCUMENT <= ? "
                   "   and " + IsServ_const +
                   "  order by T_DATE_DOCUMENT DESC, t_numdaydoc DESC) "
                   " where rownum =1" );
      cmd_1.addParam("t_referenc", RsdBp_in);
      cmd_1.value   ("t_referenc") = rs.value(0);
      cmd_1.addParam("t_date_document", RsdBp_in);
      cmd_1.value   ("t_date_document") = date(20,06,1991);
      cmd_1.execute;
      rs_1 = RsdRecordset(cmd_1); 
      if (rs_1.moveNext())
        RestOn20061991 = RestOn20061991 + rs_1.value(0);           
        if (IsAllAccount and (rs_1.value(0) != 0))
          MakeCompAcc (compens,
                               rs.value(0),   // Referenc
                               rs_1.value(0), // RestOn20061991 для счета
                               rs.value(1),   // t_type_account
                               rs.value(2) )  // t_close_date
        end
      end;
    end;
    if (RestOn20061991 == 0)
      GetMoney (RestOn20061991, "Уточните остаток на 20.06.1991");
    end;
  else 
    RestOn20061991 = compens.rec.restin91;
  end;
  if (RestOn20061991 == 0)
     msgbox("Невозможно определить остаток на 20.06.1991");
     exit(1);
  end;
end;
// ---------------------------------------------------------

macro Check_compens_pay(compens)
  if ( compens.rec.Date != Date(0,0,0) )
     msgbox("Повторный расчет зачисленной компенсации запрещен");
     exit(1);
  end;
end;

macro Check_IsAllAccount (ClientType)
  if ( (ClientType == AT82)  
   OR  (ClientType == AT98)
   OR  (ClientType == AT99) )
    IsAllAccount = true;
  else
    IsAllAccount = false;
  end;
end;
//----------------MAIN ---- функция непосредственного расчета

MACRO CheckCompensationType (compens)

  var DestCodeClient = compens.rec.DestClientCode,
      CodeClient     = compens.rec.CodClient,
      CompensType    = compens.rec.Kind,
      ClientType     = compens.rec.Appltype,
      panel_compenscoef = 0;
      
  var dDay, dMon, dYear; // День, месяц, год смерти клиента 
  var bDay, bMon, bYear; // День, месяц, год рождения клиента 
  var cDay, cMon, cYear; // День, месяц, год закрыие счета 
  var cmd, rs;
  var DifSum = 0;
// 
// Инициализация предварительных данных
// 
  Check_compens_pay      (compens);
  Check_IsAllAccount     (ClientType);
  Check_RestOn20061991   (compens);
  Check_SumCompensBefore (compens);
  Check_SumCompensRitual (compens);
  Check_CompensCoef      (compens);
//
// Расчет компенсации
//
  if (NOT IsHeir (ClientType))   // Вкладчик
    if (RestOn20061991 >= 1000)
      FullSum = 1000 * CompensCoef - SumCompensBefore;
    else
      FullSum = RestOn20061991 * CompensCoef - SumCompensBefore;
    end;
    Sum = FullSum;
  else  // Наследник
//  Полная сумма компенсации
    if ((ClientType == AT98)
     OR (ClientType == AT99) )
      if (RestOn20061991 >= 400)
        FullSum = 6000;
      else
        FullSum = RestOn20061991 * 15;
      end;
      DifSum = SumCompensRitual;
    elif (ClientType == AT82)
      if (RestOn20061991 >= 1000)
        FullSum = 1000 * CompensCoef;
      else
        FullSum = RestOn20061991 * CompensCoef;
      end; 
      DifSum = SumCompensRitual; 
    else
      if (RestOn20061991 >= 1000)
        FullSum = 1000 * CompensCoef;
      else
        FullSum = RestOn20061991 * CompensCoef;
      end; 
      DifSum = SumCompensBefore;
    end;
//  Доли наследования
    if ((compens.rec.LegacyPart_m == 0)
     OR (compens.rec.LegacyPart_n == 0))
      FullSum = FullSum - DifSum;
      Sum = FullSum;
    else
      MakeLegacyPart (compens);
    end;
  end;

  if ( FullSum < 0 )
    FullSum = 0;
    MsgBox("Сумма компенсации (без учета долей) получила отрицательное значение");
  end;
  if ( Sum < 0 )
    Sum = 0;
    MsgBox("Сумма компенсации получила отрицательное значение");
  end;

  cmd = RsdCommand("update dcompens_dbt set "
                                  " T_FUllSUM =  "+String(FullSum)        +", "
                                  " t_restin91 = "+String(RestOn20061991) +", "
                                  " t_rate =     "+String(CompensCoef)    +", "
                                  " t_sum =      "+String(Sum)            +", "
                                  " t_AccClsDate = ? "
                   " where T_FNCASH = ? and T_AUTOKEY = ? ");
  cmd.NullConversion = true;
  cmd.addParam ("t_date",     RsdBp_in);  
  cmd.value    ("t_date")     = AccCloseDate;
  cmd.addParam ("FNCash",     RsdBp_in);
  cmd.value    ("FNCash")     = compens.rec.FNCash;
  cmd.addParam ("AutoKey",    RsdBp_in);
  cmd.value    ("AutoKey")    = compens.rec.AutoKey;
/*
  cmd = RsdCommand("update dcompens_dbt set "
                                          " T_FUllSUM = ?, "
                                          " t_restin91 = ?, "
                                          " t_rate = ?, "
                                          " t_sum = ?, "
                                          " t_AccClsDate = ? "
                   " where T_FNCASH = ? and T_AUTOKEY = ? ");
//  cmd.NullConversion = true;
  cmd.addParam ("t_fullsum",  RsdBp_in);
  cmd.value    ("t_fullsum")  = FullSum;
  cmd.addParam ("t_restln91", RsdBp_in);
  cmd.value    ("t_restln91") = RestOn20061991;
  cmd.addParam ("t_rate",     RsdBp_in);
  cmd.value    ("t_rate")     = CompensCoef;
  cmd.addParam ("t_sum",      RsdBp_in);
  cmd.value    ("t_sum")      = Sum;
  cmd.addParam ("t_date",     RsdBp_in);  
  cmd.value    ("t_date")     = AccCloseDate;
  cmd.addParam ("FNCash",     RsdBp_in);
  cmd.value    ("FNCash")     = compens.rec.FNCash;
  cmd.addParam ("AutoKey",    RsdBp_in);
  cmd.value    ("AutoKey")    = compens.rec.AutoKey;
*/
  cmd.execute;
      
  return 0;

  OnError ( e )
    // Ошибки
    println ("Произошла ошибка");
    var i = 0;
    while ( i < RslDefEnv.ErrorCount )
      println ( " Системная ошибка - " + RslDefEnv.Error(i).descr );
      i = i + 1;
    end;
end;
