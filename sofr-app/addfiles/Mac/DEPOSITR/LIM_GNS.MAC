/*

      R-Style SoftWare Lab.

      RS-Retail 2.1

      Отчет для Налоговой инспекции (ГНС) по операциям
      с превышением лимита ( $ 10,000 )

      Циклы переборов документов.

      Итоговый общий выпуск отчета.

*/

import CommonInter, BankInter, DeprIntr, ExchangeInter, DeskInter,
       Cur_Rate, IsSrvDoc, Lim_GNSR, SQLconv;

file paykind  ( pay_kind ) key 0;     /* Виды платежей */
file pay_csop ( pay_csop ) key 0;     /* Подвиды платежей */

/* Документы по счетам вкладчиков */
var ddoc = TBfile( "sbdepdoc.dbt", "r", 0 );
/* Документы платежей */
var pdoc = TBfile( "pay_doc.dbt", "r", 7 );
/* Документы "Обменного пункта" */
var edoc = TBFile( "exoperat.dbt", "r", 4, null, "exchange.def" );

var FlagCur = NumFlagCur();   /* Валютность */
var Branch  = NumFNCash();    /* Номер филиала */

var stat;
var NDoc;

/*
   Выбор из файла sbdepdoc.dbt документов по счетам вкладчиков
*/
macro Select_SBdepDoc()
 var Str = "";
 InitProgress( 1000, " Ж Д И Т Е...", "Операции по счетам" );
 NDoc = 0;
 Str = "T_IsCur = "              + String( FlagCur ) +
       " AND T_FNcash = "        + String( Branch ) +
       " AND T_Date_Document = " + sqlDateToStr( {curdate} );
 ddoc.addFilter( Str );
 ClearRecord( ddoc );
 ddoc.rec.IsCur         = FlagCur;
 ddoc.rec.FNCash        = Branch;
 ddoc.rec.Date_Document = {curdate};
 stat = getGE( ddoc );
 while ( stat                                   AND
         ( ddoc.rec.IsCur         == FlagCur )  AND
         ( ddoc.rec.FNCash        == Branch  )  AND
         ( ddoc.rec.Date_Document == {curdate} ) )
   if ( ( ddoc.rec.TypeOper != OP_GETLPR )  AND  /* Кроме коммунальных платежей */
        IsServDoc( ddoc.rec )               AND
        ( ( $1. * EQU_USD( ddoc.rec.Code_Currency, ddoc.rec.InSum  ) >= LimInSum )  OR
          ( $1. * EQU_USD( ddoc.rec.Code_Currency, ddoc.rec.OutSum ) >= LimOutSum ) ) )
     if ( $1. * EQU_USD( ddoc.rec.Code_Currency, ddoc.rec.InSum  ) >= LimInSum )
       FiscalReport( 1,
             getCodClient( ddoc.rec.Referenc, ddoc.rec.Account, ddoc.rec.CodClient ),
             ddoc.rec.InSum, ddoc.rec.Code_Currency,
             OperationName( ddoc.rec.InSum ), $0, 0, $0, 0, ddoc.rec.Oper );
     end;
     if ( $1. * EQU_USD( ddoc.rec.Code_Currency, ddoc.rec.OutSum ) >= LimOutSum )
       FiscalReport( 1,
             getCodClient( ddoc.rec.Referenc, ddoc.rec.Account, ddoc.rec.CodClient ),
             ddoc.rec.OutSum, ddoc.rec.Code_Currency,
             OperationName( ddoc.rec.InSum ), $0, 0, $0, 0, ddoc.rec.Oper );
     end;
   end;
   NDoc = Ndoc + 1;
   if ( NDoc < 1001 )
     UseProgress( NDoc );
   end;
   stat = Next( ddoc );
 end;
 ddoc.dropFilter();
 RemProgress();
end;

/*
   Вид операции платежа
*/
macro payNumOpert()
 var str;
 ClearRecord( paykind );
 paykind.IsCur = NumFlagCur;
 paykind.GroupOpert = pdoc.rec.GroupOpert;
 paykind.NumOperat  = pdoc.rec.NumOpert;
 if(GetEQ(paykind))
   str = " (" + paykind.SzNameAlg + ")";
 else
   str = "";
 end;
 return str;
end;

/*
   Подвид платежа
*/
macro payApplType()
 var str;
 if ( pdoc.rec.ApplType > 0)
   ClearRecord( pay_csop );
   pay_csop.IsCur     = pdoc.rec.IsCur;
   pay_csop.GroupType = pdoc.rec.GroupOpert;
   pay_csop.NumOperat = pdoc.rec.NumOpert;
   pay_csop.ApplType  = pdoc.rec.ApplType;
   if ( GetEQ( pay_csop ) )
     str = " (" + pay_csop.NameCompType + ")";
   else
     str = "";
   end;
 end;
 return str;
end;

/*
   Выбор из файла pay_doc.dbt документов платежей
*/
macro Select_Pay_Doc()
 var Str;
 InitProgress( 1000, " Ж Д И Т Е...", "Операции платежей" );
 NDoc = 0;
 Str = " T_FNcash = "            + String( Branch ) +
       " AND T_Date_Document = " + sqlDateToStr( {curdate} );
 pdoc.addFilter( Str );
 ClearRecord( pdoc );
 pdoc.rec.FNCash        = Branch;
 pdoc.rec.Date_Document = {curdate};
 stat = getGE( pdoc );
 while ( stat                               AND
         ( pdoc.rec.FNCash        == Branch  )  AND
         ( pdoc.rec.Date_Document == {curdate} ) )
   if ( ( pdoc.rec.GroupOpert != CO_Comission )  AND  /* Комиссия */
        ( pdoc.rec.GroupOpert != CO_BankPayed )  AND  /* Банковские расчеты */
        ( pdoc.rec.GroupOpert != CO_Trn       )  AND  /* Безналичные платежи */
        ( pdoc.rec.GroupOpert != CO_Treasure  )  AND  /* Ценности */
        ( pdoc.rec.VidDoc == StrFor( 0 )      )  AND  /* Наличный платеж */
        ( $1. * EQU_USD( pdoc.rec.CodCur, pdoc.rec.InSum ) >= LimInSum ) )
     FiscalReport( 1, pdoc.rec.CodClient, pdoc.rec.InSum, pdoc.rec.CodCur,
                   PayName( pdoc.rec.DebetCredit ), $0, 0, $0, 0, pdoc.rec.Oper );
   end;
   NDoc = Ndoc + 1;
   if ( NDoc < 1001 )
     UseProgress( NDoc );
   end;
   stat = Next( pdoc );
 end;
 pdoc.dropFilter();
 RemProgress();
end;

/*
   Выбор из файла exoperat.dbt документов операций "Обменного пункта"
*/
macro Select_ExchangeDoc()
 var Str;
 InitProgress( 1000, " Ж Д И Т Е...", "операции ОП" );
 NDoc = 0;
 Str = " T_Branch = "      + String( Branch ) +
       " AND T_CurDate = " + sqlDateToStr( {curdate} );
 pdoc.addFilter( Str );
 ClearRecord( edoc );
 edoc.rec.Branch  = Branch;
 edoc.rec.CurDate = {curdate};
 stat = getGE( edoc );
 while ( stat                         AND
         ( edoc.rec.Branch  == Branch  )  AND
         ( edoc.rec.CurDate == {curdate} ) )
   if ( (edoc.rec.Kind_Oper == CDF_OperCurrBuy          )  OR  /* Покупка платежеспособной валюты за рубли */
        (edoc.rec.Kind_Oper == CDF_OperPayDocBuy        )  OR  /* Покупка платежного документа за рубли  */
        (edoc.rec.Kind_Oper == CDF_OperPayDocBuyCurr    )  OR  /* Покупка платежного документа за валюту */
        (edoc.rec.Kind_Oper == CDF_OperPayDocBuyAcc     )  OR  /* Покупка платежного документа за рубли через счет */
        (edoc.rec.Kind_Oper == CDF_OperPayDocBuyCurrAcc )  OR  /* Покупка платежного документа за валюту через счет */
        (edoc.rec.Kind_Oper == CDF_OperUnpayCurrBuy     )  OR  /* Покупка неплатежеспособной валюты за рубли */
        (edoc.rec.Kind_Oper == CDF_OperIncasso          )  OR  /* Прием валюты на инкассо */
        (edoc.rec.Kind_Oper == CDF_OperExpert           )  OR  /* Прием валюты на экспертизу */
        (edoc.rec.Kind_Oper == CDF_OperPayDocIncasso    )  OR  /* Прием ПД на инкассо */
        (edoc.rec.Kind_Oper == CDF_OperPayDocExpert     )      /* Прием ПД на экспертизу */
      )
     if   ( $1. * EQU_EXC( FindCodCurByRefVal(edoc.rec.IncomeRefVal), edoc.rec.IncomeAmount ) >= LimInSum )
       FiscalReport( 0, edoc.rec.ClientCode, $0, edoc.rec.IncomeRefVal,
                     ExchName( edoc.rec.Kind_Oper ),
                     edoc.rec.OutAmount, edoc.rec.OutRefVal,
                     edoc.rec.IncomeAmount, edoc.rec.IncomeRefVal,
                     edoc.rec.Oper );
     end;
   end;
   if ( (edoc.rec.Kind_Oper == CDF_OperCurrSale            )  OR  /* Продажа платежеспособной валюты за рубли */
        (edoc.rec.Kind_Oper == CDF_OperPayDocSale          )  OR  /* Продажа платежного документа за рубли  */
        (edoc.rec.Kind_Oper == CDF_OperPayDocSaleCurr      )  OR  /* Продажа платежного документа за валюту */
        (edoc.rec.Kind_Oper == CDF_OperPayDocSaleAcc       )  OR  /* Продажа платежного документа за рубли через счет */
        (edoc.rec.Kind_Oper == CDF_OperPayDocSaleCurrAcc   )  OR  /* Продажа платежного документа за валюту через счет*/
        (edoc.rec.Kind_Oper == CDF_OperReturnIncasso       )  OR  /* Возврат валюты с инкассо */
        (edoc.rec.Kind_Oper == CDF_OperReturnExpert        )  OR  /* Возврат валюты с экспертизы */
        (edoc.rec.Kind_Oper == CDF_OperPayDocReturnIncasso )  OR  /* Возврат ПД с инкассо */
        (edoc.rec.Kind_Oper == CDF_OperPayDocReturnExpert  )      /* Возврат ПД с экспертизы */
      )
     if ( $1. * EQU_EXC( FindCodCurByRefVal(edoc.rec.OutRefVal), edoc.rec.OutAmount ) >= LimOutSum )
       FiscalReport( 0, edoc.rec.ClientCode, $0, edoc.rec.OutRefVal,
                     ExchName( edoc.rec.Kind_Oper ),
                     edoc.rec.OutAmount, edoc.rec.OutRefVal,
                     edoc.rec.IncomeAmount, edoc.rec.IncomeRefVal,
                     edoc.rec.Oper );
     end;
   end;
   if( (edoc.rec.Kind_Oper == CDF_OperCurrConversion )  OR /* Конверсия */
       (edoc.rec.Kind_Oper == CDF_OperChange         )     /* Замена неплатежной валюты */
     )
     if( $1. * EQU_EXC( FindCodCurByRefVal(edoc.rec.OutRefVal), edoc.rec.OutAmount ) >= LimOutSum )
       FiscalReport( 0, edoc.rec.ClientCode, $0, edoc.rec.OutRefVal,
                     ExchName( edoc.rec.Kind_Oper ),
                     edoc.rec.OutAmount, edoc.rec.OutRefVal,
                     edoc.rec.IncomeAmount, edoc.rec.IncomeRefVal,
                     edoc.rec.Oper, edoc.rec.CurDate );
     elif( $1. * EQU_EXC( FindCodCurByRefVal(edoc.rec.IncomeRefVal), edoc.rec.IncomeAmount ) >= LimInSum )
       FiscalReport( 0, edoc.rec.ClientCode, $0, edoc.rec.IncomeRefVal,
                     ExchName( edoc.rec.Kind_Oper ),
                     edoc.rec.OutAmount, edoc.rec.OutRefVal,
                     edoc.rec.IncomeAmount, edoc.rec.IncomeRefVal,
                     edoc.rec.Oper, edoc.rec.CurDate );
     end;
   end;
   NDoc = Ndoc + 1;
   if ( NDoc < 1001 )
     UseProgress( NDoc );
   end;
   stat = Next( edoc );
 end;
 edoc.addFilter();
 RemProgress();
end;

/*
    Основная процедура
*/

var RegPathIn  = "RS-RETAIL\\СЕРВИСНЫЕ\\ЛИМИТЫ\\ЛИМИТЫ_ГНС\\ПРИХОД_НА_Ф-Л_(USD)";
var RegPathOut = "RS-RETAIL\\СЕРВИСНЫЕ\\ЛИМИТЫ\\ЛИМИТЫ_ГНС\\РАСХОД_Ф-Л_(USD)";
var Value;

/* Открытие справочника документов (RS-Bank) */
/*if ( RSDir == "" )
  RSDir = GetIniString ("DATABASEDIR");
end;*/

/* Определение суммы лимита для выбора документов */
if ( GetRegistryValue( RegPathIn, V_INTEGER, Value, ErrCode ) == V_INTEGER )
  LimInSum = $1. * Value;
else
  LimInSum = $10000.;
end;
if ( GetRegistryValue( RegPathOut, V_INTEGER, Value, ErrCode ) == V_INTEGER )
  LimOutSum = $1. * Value;
else
  LimOutSum = $10000.;
end;

/* Открытие справочника стран */
open( country, GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\DISTDBFILE", true ) + "country.dbt" );

MakeFile ();
/* Выбор из файла sbdepdoc.dbt документов по счетам вкладчиков */
Select_SBdepDoc();
/* Выбор из файла pay_doc.dbt документов платежей */
if ( FlagCur == 0 )  /* Только для рублей */
  Select_Pay_Doc();
end;
/* Выбор из файла exoperat.dbt документов операций "Обменного пункта" */
Select_ExchangeDoc();

end;
