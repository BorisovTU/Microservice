/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Проверка необходимости взятия комиссии при переводе за пределы тербанка *
*  при условии хранения менее двух месяцев                                 *
*                                                                          *
*  Лебедев С.В.                                                            *
*  10.05.2000                                                              *
\**************************************************************************/

import DeprIntr, IsSrvDoc, cur_rate, alg_vars;
import PmPropUtil;

private file sb_tarif   (sb_tarif) key 0;
private file tarif_plus (sb_tarpl) key 0;
private var  sbdepdoc = TBFile( "sbdepdoc.dbt", "r", 6 );
private file depdoc     (sbdepdoc);
private file depositr   (depositr) key 7;

record ALG_SBDEPDOC_1 ("sbdepdoc.1");
var sbdepdoc1_51 = TRecHandler( "sbdepdoc.1" );

var bankdprt : TBFile;
var partcode : TBFile;

private var {MFO_Bank};

var {GroupProcRunning};
var BankVersion      = BankVer();
var CodeBankRecip    = "";
var MinRestTwoMonth  = $0L;
var SumPercTwoMonth  = $0L;
var SumOutOrBefClose = $0L;
var WasDocTwoMonth   = FALSE;
var FirstDocTwoMonth = FALSE;
var PrevAccount      = 0;
var docd,docm,docy;
var curd,curm,cury;
var CBRateDoc,BuyRateDoc,SellRateDoc;
var CBRateTar,BuyRateTar,SellRateTar;
var CBRateTarOpr,BuyRateTarOpr,SellRateTarOpr;


/**************************************************************************\
|                          Открытие файлов RS-Bank                         |
\**************************************************************************/
macro OpenBankDprt

  var stat         = FALSE;

  partcode = Tbfile("partcode.dbt","r",1);
  bankdprt = Tbfile("bankdprt.dbt","r",0);
  if ((partcode) AND (bankdprt))
    stat = TRUE;
  end;

  return stat;

end;


/**************************************************************************\
|     Определение кода типа банка - Сбербанк, АКБ, РКЦ или что-то еще      |
\**************************************************************************/
macro DefCodeBankRecip (BIK)

  var stat = TRUE;
  var flag;

  CodeBankRecip = "";

  if (stat)
    stat = OpenBankDprt();
  end;

  if (stat)
    if (BankVersion == 510)
      partcode.rec.CodeKind = 3;
      partcode.rec.Code     = BIK;
      flag = partcode.GetEQ;
      if (flag)
        bankdprt.rec.PartyID = partcode.rec.PartyID;
        flag = bankdprt.GetEQ;
        if (flag)
          /*CodeBankRecip = bankdprt.rec.KindDepart;*/
          if( bankdprt.rec.UERType == 3 )
            CodeBankRecip = "ОСБ"; // Сберегательный банк
          else
            CodeBankRecip = "КОМ"; // Фикция, чтобы не было ругани об отсутствии БИК
          end;
        end;
      end;
    else
      bankdprt.rec.MFO_Depart = BIK;
      bankdprt.rec.Corr_Acc   = "";
      flag = bankdprt.GetGE;
      if (flag AND (bankdprt.rec.MFO_Depart == BIK))
        CodeBankRecip = bankdprt.rec.Kind_Depart;
      end;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|    Определение суммы, учитываемой при расчете платы с учетом закрытия    |
\**************************************************************************/
macro DefSumOutOrBeforeClose

  var outSum = ALG_SBDEPDOC_1.OutSum;
  var stat;

  if ( ( ALG_SBDEPDOC_1.TypeComplexOper == 10 ) or  /* Закрытие счета           */
       ( ALG_SBDEPDOC_1.TypeComplexOper == 81 ) or  /* Процентов в истории нет, */
       ( ALG_SBDEPDOC_1.TypeComplexOper == 96 )   ) /* а в OutSum есть SCR 8353 */
    sbdepdoc.addFilter( "T_Referenc = " + String( ALG_SBDEPDOC_1.Referenc ) );
    sbdepdoc.Clear;
    sbdepdoc.rec.Referenc      = ALG_SBDEPDOC_1.Referenc;
    sbdepdoc.rec.Date_Document = Date( 31, 12, 9999 );
    sbdepdoc.rec.NumDayDoc     = 0;
    stat = sbdepdoc.GetLE;
    while ( stat )
      if ( sbdepdoc.rec.Referenc == ALG_SBDEPDOC_1.Referenc )
        if ( IsServDoc( sbdepdoc.rec ) and ( sbdepdoc.rec.FlagStorn == "" ) )
          outSum = sbdepdoc.rec.Rest;
          stat = false;
        else
          stat = sbdepdoc.Prev;
        end;
      else
        stat = false;
      end;
    end;
    sbdepdoc.dropFilter( );
  end;

  return outSum;

end;


/**************************************************************************\
|                  Определение даты двухмесячной давности                  |
\**************************************************************************/
macro DefDateTwoMonthAgo (InputDate)

  var   OutputDate  = Date(0,0,0);
  var   TmpDate     = InputDate;
  var   d,m,y;
  const TwoMonthAgo = 2;

  DateSplit(TmpDate,d,m,y);

  if (m - TwoMonthAgo <= 0)
    m = m + 12;
    y = y - 1;
  end;

  m = m - TwoMonthAgo;

  if ((m == 4) OR (m == 6) OR (m == 9) OR (m == 11))
    if (d > 30)
      d = 30;
    end;
  elif (m == 2)
    if ((y/4.0 == Int(y/4.0)) AND (y != 2100))
      if (d > 29)
        d = 29;
      end;
    else
      if (d > 28)
        d = 28;
      end;
    end;
  end;

  OutputDate = Date(d,m,y);

  return OutputDate;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefRefPrevAccount

  var refPrevAccount = 0;
  var stat;

  sbdepdoc.addFilter( "T_Referenc = " + String( ALG_SBDEPDOC_1.Referenc ) );
  sbdepdoc.Clear;
  sbdepdoc.rec.Referenc      = ALG_SBDEPDOC_1.Referenc;
  sbdepdoc.rec.Date_Document = DefDateTwoMonthAgo( ALG_SBDEPDOC_1.Date_Document );
  sbdepdoc.rec.NumDayDoc     = 0;
  stat = sbdepdoc.GetGE;
  while ( stat )
    if ( ( sbdepdoc.rec.Referenc      == ALG_SBDEPDOC_1.Referenc      ) and
         ( sbdepdoc.rec.Date_Document <= ALG_SBDEPDOC_1.Date_Document )    )
      if ( ( IsServDoc( sbdepdoc.rec ) ) and ( sbdepdoc.rec.FlagStorn == "" ) )
        stat = false;
        if ( (   sbdepdoc.rec.TypeOper == 51            ) and
             ( ( sbdepdoc.rec.TypeComplexOper == 81 ) or
               ( sbdepdoc.rec.TypeComplexOper == 78 )   )    )
          sbdepdoc1_51.SetRecordAddr( sbdepdoc, 0, 0, true );
          depositr.FNCash  = sbdepdoc1_51.rec.FNCash;
          depositr.Account = PMNT_PROP_REC_BUF.Account;
          if ( GetEQ( depositr ) and ( depositr.Code_Currency == sbdepdoc.rec.Code_Currency ) )
            refPrevAccount = depositr.Referenc;
          end;
        end;
      else
        stat = sbdepdoc.Next;
      end;
    else
      stat = false;
    end;
  end;
  sbdepdoc.dropFilter( );

  return refPrevAccount;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro IsRegistratOperation

  var stat = true;

  /* Зачисление процентов */
  if (sbdepdoc.rec.TypeOper == 72)
    stat = false;
  /* Переоформление вклада - расходная операция */
  elif (( sbdepdoc.rec.Referenc != ALG_SBDEPDOC_1.Referenc    ) AND
        ((sbdepdoc.rec.TypeComplexOper == 78              ) OR
         (sbdepdoc.rec.TypeComplexOper == 81              )   )    )
    stat = false;
  /* Переоформление вклада - приходная операция */
  elif (( sbdepdoc.rec.Referenc == ALG_SBDEPDOC_1.Referenc    ) AND
        ( sbdepdoc.rec.TypeOper == 51                         ) AND
        ((sbdepdoc.rec.TypeComplexOper == 78              ) OR
         (sbdepdoc.rec.TypeComplexOper == 81              )   )    )
    stat = false;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro IsClosedPrevAccount

  var stat = false;

  if (sbdepdoc.rec.Referenc != ALG_SBDEPDOC_1.Referenc)
    if ((  sbdepdoc.rec.Rest            == $0L        ) and
        (( sbdepdoc.rec.TypeComplexOper == 78     ) or
         ( sbdepdoc.rec.TypeComplexOper == 81     )   ) and
        (( sbdepdoc.rec.TypeOper        == 63     ) or
         ( sbdepdoc.rec.TypeOper        == 64     ) or
         ((sbdepdoc.rec.TypeOper        == 65) and
          /*(sbdepdoc.rec.ApplType        != 11)    ) or*/
          (sbdepdoc.rec.KindOp          !=  5)    ) or
         ( sbdepdoc.rec.TypeOper        == 66     ) or
         ( sbdepdoc.rec.TypeOper        == 67     ) or
         ( sbdepdoc.rec.TypeOper        == 80     )   )    )
      stat = true;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefSumPercAndMinRest ( RefAcc )

  var stat;
  var Rest;

  sbdepdoc.addFilter( "T_Referenc = " + String( RefAcc ) );
  sbdepdoc.Clear;
  sbdepdoc.rec.Referenc      = RefAcc;
  sbdepdoc.rec.Date_Document = DefDateTwoMonthAgo( ALG_SBDEPDOC_1.Date_Document );
  sbdepdoc.rec.NumDayDoc     = 0;
  stat = sbdepdoc.GetGE;
  while (stat)
    if ((sbdepdoc.rec.Referenc      == RefAcc                      ) AND
        (sbdepdoc.rec.Date_Document <= ALG_SBDEPDOC_1.Date_Document)    )
      if ((IsServDoc(sbdepdoc.rec)                                ) AND
          (sbdepdoc.rec.FlagStorn == ""                           ) AND
          ((sbdepdoc.rec.InSum != 0) OR (sbdepdoc.rec.OutSum != 0))    )
        if (sbdepdoc.rec.TypeOper == 72)
          SumPercTwoMonth = SumPercTwoMonth + sbdepdoc.rec.InSum;
        end;
        if (IsRegistratOperation())
          WasDocTwoMonth = TRUE;
        end;
        Rest = sbdepdoc.rec.Rest;
        if (IsClosedPrevAccount())
          Rest = sbdepdoc.rec.OutSum;
        end;
        if (NOT FirstDocTwoMonth)
          FirstDocTwoMonth = TRUE;
          if (sbdepdoc.rec.InSum > 0)
            MinRestTwoMonth = Rest - sbdepdoc.rec.InSum;
          else
            MinRestTwoMonth = Rest;
          end;
        else
          if (Rest < MinRestTwoMonth)
            MinRestTwoMonth = Rest;
            SumPercTwoMonth = $0L; /* Все расчитанные ранее проценты вошли */
          end;                     /* в минимальную сумму                  */
        end;
      end;
      stat = sbdepdoc.Next;
    else
      stat = false;
    end;
  end;
  sbdepdoc.dropFilter( );

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  var stat, stat1,
      dtRate, tmRate;
  var summaPlus = $0L;

  if (GetBankStandart() == 0)
//  Макрос написан только для Сбербанка
    Copy(depdoc,ALG_SBDEPDOC);
    SetRecordAddr(ALG_SBDEPDOC_1,depdoc,0,0,true);

    PrepareDateTimeForRates( true, ALG_SBDEPDOC_1.Date_Document, null, dtRate, tmRate );

    ALG_RESULT = OK_RES;
    ALG_PARAM.prmD = 0;

    if ((ALG_RESULT == OK_RES) AND (ALG_SBDEPDOC_1.ApplType == 0)) /* Перевод на "свое" имя */
      if ((PMNT_PROP_REC_BUF.BankCode == ""        ) OR
          (PMNT_PROP_REC_BUF.BankCode == {MFO_Bank})   )
        ; /* Перевод осуществляется в "наш" тербанк, проверим остаток двухмесячной давности */
      else
        if (DefCodeBankRecip(PMNT_PROP_REC_BUF.BankCode))
          if ((CodeBankRecip == "ОСБ") OR (CodeBankRecip == "УСБ"))
            ; /* Перевод осуществляется в Сбербанк, проверим остаток двухмесячной давности */
          elif (CodeBankRecip == "") /* БИК не найден или не определен */
            if ( {GroupProcRunning} )
              ;
            else
              MsgBox("БИК \"" + PMNT_PROP_REC_BUF.BankCode + "\" не найден или не определен тип|Данная операция невозможна");
            end;
            ALG_RESULT = ERR_RES;
          else /* Коммерческий банк */
            if ( {GroupProcRunning} )
              ;
            else
              MsgBox("БИК \"",PMNT_PROP_REC_BUF.BankCode,"\" не Сбербанка|Данная операция невозможна");
            end;
            ALG_RESULT = ERR_RES;
          end;
        else
          if ( {GroupProcRunning} )
            ;
          else
            MsgBox("Ошибка при определении типа банка");
          end;
          ALG_RESULT = ERR_RES;
        end;
      end;
    end;

    /* Поиск тарифа - для расчета комиссии на сумму перевода в "наш" тербанк */
    if ((ALG_RESULT == OK_RES) AND (ALG_SBDEPDOC_1.ApplType == 0))
      if (ALG_PARAM.prmI <= 0)
        ALG_RESULT = SKIP_RES;
      end;
      /* Поиск будет осуществлен ниже */
    end;

    /* Вычисление минимального остатка за два месяца для расчета комиссии на сумму перевода в "наш" тербанк */
    if ((ALG_RESULT == OK_RES) AND (ALG_SBDEPDOC_1.ApplType == 0))
      PrevAccount = DefRefPrevAccount();
      if (PrevAccount != 0)
        DefSumPercAndMinRest(PrevAccount);
      end;
      DefSumPercAndMinRest(ALG_SBDEPDOC_1.Referenc);
      if (NOT WasDocTwoMonth) /* Ближайшие два месяца документов не было */
        ALG_RESULT = SKIP_RES;
      end;
    end;

    /* Вычисление суммы налога */
    if ((ALG_RESULT == OK_RES) AND (ALG_SBDEPDOC_1.ApplType == 0))
      SumOutOrBefClose = DefSumOutOrBeforeClose();
      if (SumOutOrBefClose > MinRestTwoMonth + SumPercTwoMonth)

        /* Определение тарифа */
        sb_tarif.IsCur    = ALG_SBDEPDOC_1.IsCur;
        sb_tarif.NumTarif = ALG_PARAM.prmI;
        if ( GetEQ( sb_tarif ) )

          if ( ALG_SBDEPDOC_1.Code_Currency != sb_tarif.CodCur )
            if (ALG_SBDEPDOC_1.Code_Currency == 0)
              CBRateDoc = 1.0L;
            else
              stat = GetAllCurrRate(dtRate,ALG_SBDEPDOC_1.Code_Currency,CBRateDoc,BuyRateDoc,SellRateDoc,tmRate);
              if (NOT stat)
                if ( {GroupProcRunning} )
                  ;
                else
                  MsgBox("Не найден курс валюты документа");
                end;
                ALG_RESULT = ERR_RES;
              end;
            end;

            if ( sb_tarif.CodCur == 0 )
              CBRateTar = 1.0L;
            else
              stat = GetAllCurrRate(dtRate,sb_tarif.CodCur,CBRateTar,BuyRateTar,SellRateTar,tmRate);
              if (NOT stat)
                if ( {GroupProcRunning} )
                  ;
                else
                  MsgBox("Не найден курс валюты тарифа");
                end;
                ALG_RESULT = ERR_RES;
              end;
            end;

            if ( sb_tarif.CodCurOpr == sb_tarif.CodCur )
              CBRateTarOpr = CBRateTar;
            else
              stat = GetAllCurrRate( dtRate, sb_tarif.CodCurOpr, CBRateTarOpr, BuyRateTarOpr, SellRateTarOpr, tmRate );
              if ( not stat )
                if ( {GroupProcRunning} )
                  ;
                else
                  MsgBox("Не найден курс валюты операции тарифа");
                end;
                ALG_RESULT = ERR_RES;
              end;
            end;
          end;

          if ( sb_tarif.TarifPlus and ( ALG_RESULT == OK_RES ) )

            summaPlus = SumOutOrBefClose - MinRestTwoMonth - SumPercTwoMonth;
            if ( summaPlus < 0 )
              summaPlus = 0;
            end;

            if ( ( summaPlus != 0 ) and ( ALG_SBDEPDOC_1.Code_Currency != sb_tarif.CodCurOpr ) )
              summaPlus = MoneyL( summaPlus * CBRateDoc / CBRateTarOpr );
            end;

            tarif_plus.IsCur    = ALG_SBDEPDOC_1.IsCur;
            tarif_plus.NumTarif = ALG_PARAM.prmI;
            tarif_plus.Date     = ALG_SBDEPDOC_1.DepDate_Document;
            tarif_plus.SumOper  = summaPlus;
            stat1 = GetLE( tarif_plus );

            if ( stat1  AND
                 ( tarif_plus.IsCur    == ALG_SBDEPDOC_1.IsCur )  AND
                 ( tarif_plus.NumTarif == ALG_PARAM.prmI ) )

              while ( stat1  AND
                      ( tarif_plus.IsCur    == ALG_SBDEPDOC_1.IsCur )  AND
                      ( tarif_plus.NumTarif == ALG_PARAM.prmI )        AND
                      ( tarif_plus.Date     <= ALG_SBDEPDOC_1.DepDate_Document ) )
                if ( tarif_plus.SumOper <= summaPlus )
                  stat1 = FALSE;
                else
                  stat1 = Prev( tarif_plus );
                end;
              end;

              if ( ( tarif_plus.IsCur    == ALG_SBDEPDOC_1.IsCur )  AND
                   ( tarif_plus.NumTarif == ALG_PARAM.prmI )        AND
                   ( tarif_plus.Date     <= ALG_SBDEPDOC_1.DepDate_Document ) )

                sb_tarif.Persent = tarif_plus.Percent;
                sb_tarif.MinSum  = tarif_plus.MinSum;
                sb_tarif.MaxSum  = tarif_plus.MaxSum;
                sb_tarif.Summa   = tarif_plus.Summa;
              end;
            end;
          end;

          /* Конвертация сумм */
          if ( ( ALG_RESULT == OK_RES ) and ( ALG_SBDEPDOC_1.Code_Currency != sb_tarif.CodCur ) )
            if (sb_tarif.Summa != 0)
              sb_tarif.Summa = MoneyL(sb_tarif.Summa * CBRateTar / CBRateDoc);
            end;
            if (sb_tarif.MinSum != 0)
              sb_tarif.MinSum = MoneyL(sb_tarif.MinSum * CBRateTar / CBRateDoc);
            end;
            if (sb_tarif.MaxSum != 0)
              sb_tarif.MaxSum = MoneyL(sb_tarif.MaxSum * CBRateTar / CBRateDoc);
            end;
          end;
        else
          if ( {GroupProcRunning} )
            ;
          else
            MsgBox("Не найден тариф номер " + String(ALG_PARAM.prmI));
          end;
          ALG_RESULT = ERR_RES;
        end;


        if (ALG_RESULT == OK_RES)
          ALG_PARAM.prmD = SumOutOrBefClose - MinRestTwoMonth - SumPercTwoMonth;
          ALG_PARAM.prmD = MoneyL(ALG_PARAM.prmD * sb_tarif.Persent / 1000000 + sb_tarif.Summa);
          if ((ALG_PARAM.prmD < sb_tarif.MinSum) AND (sb_tarif.MinSum != 0))
            ALG_PARAM.prmD = sb_tarif.MinSum;
          end;
          if ((ALG_PARAM.prmD > sb_tarif.MaxSum) AND (sb_tarif.MaxSum != 0))
            ALG_PARAM.prmD = sb_tarif.MaxSum;
          end;
          ALG_PARAM.prmD = round( ALG_PARAM.prmD, 2 );
          if (ALG_PARAM.prmD == 0)
            ALG_RESULT = SKIP_RES;
          end;
        end;
      else
        ALG_RESULT = SKIP_RES;
      end;
    end;
  else
    ALG_RESULT = SKIP_RES; // Для коммерческого банка макрос не выполняется
  end;
end;
