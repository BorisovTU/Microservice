private const 
  srmOverdraft                   = 0,
  srmNotEqualRests               = 1,
  srmRejectedInFilial            = 2,
  srmReturnedPacketPut           = 3,
  srmReturnedReplicatedPacketPut = 4,
  srmNonZeroRest                 = 5;

private var MsgCounter = 0;

private macro GetOpName(is_cur, kind, num_oper)
  file TypeOp ("sb_typop.dbt") key 0;
  var op_name;
  
  TypeOp.IsCur = is_cur;
  TypeOp.Kind = kind;
  TypeOp.NumOpert = num_oper;
  if (GetEQ(TypeOp))
    op_name = TypeOp.czNameAlg;
  else
    op_name="не найдена";
  end;

  return op_name;
end;

private macro PrintHead
[
                     Ошибки, выявленные при загрузке сессии
              
  +------+-------------------------------------------------------------------------+
  | № ПП |                           Описание ошибки                               |
  +------+-------------------------------------------------------------------------+
];
end;

macro PrintErrorMessage(type, rec1, rec2)
  var str;
  record src_doc( "sbdepdoc" );
  record second_doc( "sbdepdoc" );

  setBuff(src_doc, rec1);

  if (type == srmOverdraft)
    str = "По счету " + src_doc.Account + " в случае приема операции " + src_doc.TypeOper + " (" + 
      GetOpName(src_doc.IsCur, src_doc.Type_Account, src_doc.TypeOper) + ") на сумму " + (src_doc.InSum + src_doc.OutSum) + 
      " получится отрицательный остаток - операция не принята";
  elif (type == srmNotEqualRests)
    setBuff(second_doc, rec2);
    str = "По счету " + src_doc.Account + " в случае приема операции " + src_doc.TypeOper + " (" + 
      GetOpName(src_doc.IsCur, src_doc.Type_Account, src_doc.TypeOper) + ") на сумму " + (src_doc.InSum + src_doc.OutSum) + 
      " получится расхождение остатков - операция не принята";
  elif (type == srmRejectedInFilial)
    str = "По счету " + src_doc.Account + " операция " + src_doc.TypeOper + " (" + 
      GetOpName(src_doc.IsCur, src_doc.Type_Account, src_doc.TypeOper) + ") на сумму " + (src_doc.InSum + src_doc.OutSum) + 
      " не принята - регламентные операции в филиале не принимаются";
  elif (type == srmReturnedPacketPut)
    str = "Пакетное зачисление по счету " + src_doc.Account + " за дату " + src_doc.Date_Document + 
      " возвращено плательщику в связи с тем, что оно не было обработано в ВСП до закрытия счета";
  elif (type == srmReturnedReplicatedPacketPut)
    str = "Пакетное зачисление по счету " + src_doc.Account + " за дату " + src_doc.Date_Document + 
      " возвращено плательщику - предполагаем, что оно не обработано в ВСП";
  elif (type == srmNonZeroRest)
    str = "Счет " + src_doc.Account + " закрыт, но на нем остался не нулевой остаток. Счет подлежит ручной обработке";
  else
    return;
  end;

  if (MsgCounter == 0)
    PrintHead();
  end;
  MsgCounter = MsgCounter + 1;
  
[ | #### | ####################################################################### |
  +------+-------------------------------------------------------------------------+
](MsgCounter, str:w);

end;


