
/* Печать книги регистрации открытых счетов */

import deprintr, sign_rep, rsd;

file accreg   ("accreg.dbt"  ) key 1;             /* Реестр счетов       */
file depositr ("depositr.dbt") key 7;
file deptmp   ("depositrttt.tmp") key 0 write;
file DepType  ("sb_dtyp.dbt" ) key 2;

var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

var    {UseNewNumbering},
       {curdate},
       {CNum},
       StartDate = {curdate},
       EndDate = {curdate},
       ClientList = TClientList,
       Stat = true,
       flagFloor = FALSE,
       BStandart = 0,
       ErrCode, ErrText;
const  FlagCur = NumFlagCur(),
       FNCash  = NumFNCash();
const
  D_DELETE = 2;


macro Colontitle
  [ ------- #################### ------------------ ########## ------ ######## ---------
  ] (GetRetailVersion(),Date(),Time());
end;

macro FindDepClient(ClientCode)

  return ClientList.GetRecord( ClientCode );
end;

macro FindDepType()

  depositr.FNCash = accreg.FNCash;
  depositr.Account = accreg.Account;
  if( not getEQ(depositr))
    return false;
  end;
  DepType.FlagCur = accreg.IsCur;
  DepType.Kind = depositr.Type_Account;
  return getEQ(DepType);

end;

macro PrintTitle()
  if ( BStandart )
    [ +--------------------------------------------------------------------------------------------------------------------------------+];
    [ |№ №|Дата откр.|   Номер    |     Ф.И.О.  вкладчика     | Вид вклада | Номер лицевого счета |Дата сооб.|Дата закр.|  Примечание  |];
    [ |п/п|  счета   |  договора  |                           |            |                      |налог.орг.|  счета   |              |];
    [ |---|----------|------------|---------------------------|------------|----------------------|----------|----------|--------------|];
  else
    [ +------------------------------------------------------------------------------------------------------------------------------+];
    [ |№ № |Номер счета по вкладу |       Ф.И.О.  вкладчика       |Дата открытия|Дата сообщ.|Дата закрытия|         Примечание       |];
    [ |п/п |                      |                               |    счета    |налог.орг. |    счета    |                          |];
    [ |----|----------------------|-------------------------------|-------------|-----------|-------------|--------------------------|];
  end;
  flagFloor = TRUE;
end;

macro PrintFloor()
  if ( flagFloor )
    if ( BStandart )
      [ +--------------------------------------------------------------------------------------------------------------------------------+];
    else
      [ +------------------------------------------------------------------------------------------------------------------------------+];
    end;
  end;
end;

macro PrintRegisteredAccounts()

  var stat;
  var NN = 1;
  var nr = 0;
  var str, ClientName = "";
  var vBalAcc = "";

  InitProgress( NRecords( accreg ), " Ж Д И Т Е ...",
                "Формирование отчета" );
  KeyNum( deptmp, 0 );
  ReWind( deptmp );
  ClearRecord( deptmp );
  deptmp.IsCur  = FlagCur;
  deptmp.FNCash = FNCash;

  str = "";
  [#](str);
  str = "                                        КНИГА регистрации открытых счетов";
  [#](str);
  str = "                                            с " + String(StartDate) + " по " + String(EndDate);
  [#](str);
  str = "";
  [#](str);

  stat = getGE(deptmp);
  while ( stat )
    if( ( deptmp.IsCur == FlagCur ) and ( deptmp.FNCash == FNCash ) )

      if ( vBalAcc != deptmp.BalAcc )
        vBalAcc = deptmp.BalAcc;
        PrintFloor();
        [ ];
        [ ];
        [ Балансовый счет 2-го порядка: #] ( vBalAcc );
        [ ];
        PrintTitle();
      end;

      if ( BStandart )
        str = " |" +String(NN:3)                     + "|";
        str = str + String(deptmp.Open_Date:10)      + "|";
        str = str + String(SubStr(Trim(deptmp.GroupNumb),1,4):r:4) + "/"+
              String(deptmp.Number:7)                + "|";
        str = str + String(deptmp.FIO_Client:27)     + "|";
        str = str  +String(deptmp.Type_Account:12)   + "|";
        str = str  +String(Acc_Form(deptmp.Account):22) + "|";
        str = str + String(deptmp.FiscalDate:10)     + "|";
        if ( deptmp.Open_Close )
          str = str + String(deptmp.Close_Date:10)   + "|";
        else
          str = str + "          "                   + "|";
        end;
        str = str + String(deptmp.Remark:14:c)       + "|";
        [#](str);
      else
        str = " |" +String(NN:4)                     + "|";
        str = str  +String(Acc_Form(deptmp.Account):22) + "|";
        str = str + String(deptmp.FIO_Client:31)     + "|";
        str = str + String(deptmp.Open_Date:13:c)    + "|";
        str = str + String(deptmp.FiscalDate:11:c)   + "|";
        if(depositr.Open_Close)
          str = str + String(deptmp.Close_Date:13:c) + "|";
        else
          str = str + "             "                + "|";
        end;
        str = str + String(deptmp.Remark:26:c)       + "|";
        [#](str);
      end;
      NN = NN + 1
    end;
    UseProgress( nr );
    nr = nr + 1;
    stat = Next(deptmp);
  end;

  PrintFloor();
  RemProgress();

end;

macro MakeTempFile()
  var nr = 0;
  var ClientName = "";
  InitProgress( NRecords( accreg ), " Ж Д И Т Е ...",
                "Заполнение временного файла" );
  KeyNum( accreg, 1 );
  ReWind( accreg );
  accreg.IsCur  = FlagCur;
  accreg.FNCash = FNCash;
  accreg.GroupNumb = "";
  accreg.Code_Currency = 0;
  accreg.Number = 0;
  stat = getGE( accreg );
  while ( stat )
    if ( ( accreg.IsCur == FlagCur )       AND
         ( accreg.FNCash == FNCash )       AND
         ( accreg.OpenDate >= StartDate )  AND
         ( accreg.OpenDate <= EndDate )    AND
         ( accreg.Action != D_DELETE ) )

      if ( FindDepClient( accreg.CodClient ) )
        if ( FindDepType() and ( DepType.SpecialAccess ) )
          ClientName = DepType.Kind;
        else
          ClientName = ClientList.CurRec.ConvertFioFull;
        end;
      end;

      Copy( deptmp, depositr );

      if(  (not SpecialAccess) and OperMayListSpecialAccounts and deptmp.SpecialAccess)
        ClientName = "XXXXXX X.X.";
      end;

      deptmp.FIO_Client = ClientName;
      deptmp.FiscalDate = accreg.FiscalDate;
      deptmp.Remark     = accreg.Remark;
      deptmp.BalAcc     = SubStr( depositr.Account, 1, 5 );

      if ( NOT Insert( deptmp ) )   
        PrintLn( " *** Ошибка при записи во временный файл" );
        PrintLn( " *** Cчет: ", Acc_Form(deptmp.Account) ); 
        ErrCode = Status( ErrText );
        PrintLn( " *** ", ErrCode, ": ", ErrText );
        PrintLn;
//        Exit( 0 );
      end;
    end;
    UseProgress( nr );
    nr = nr + 1;
    stat = Next( accreg );
  end;
  RemProgress();
end;

macro main(void)

  var d, m, y;
  var cmd;

  DateSplit( {curdate}, d, m, y );

  StartDate = Date(  1,  1, y-1 );
  EndDate   = Date( 31, 12, y-1 );

  BStandart = GetBankStandart();

  cmd = RsdCommand( NULL, string("truncate table ddepositrttt_tmp") );
  cmd.execute();

  if ( {UseNewNumbering} )
    if(Stat)
      /* SetOutput("PRN"); */
      Colontitle();
      [ ];
      MakeTempFile();
      PrintRegisteredAccounts();
      [ ];
      Colontitle();
    end;
  end;

  ElectronDigitalSignReport( );
end;

main();

End;
