/*

  R-Style SoftLab.

  RS-Retail.

  Текущие счета вкладчиков.

  Выписка по Текущему счету. Общие процедуры выпуска выписки.

*/

import Alg_Vars, IsSrvDoc, SQLconv, CommonInter, Acc_Form;

 const SB_DEPOSIT = 1;

 var cur = TBFile ( "currency.dbt", "r", 0 );
 var dtp = TBFile ( "sb_dtyp.dbt",  "r", 2 );
 var sop = TBFile ( "suboper.dbt",  "r", 1 );
 var doc = TBFile ( "sbdepdoc.dbt", "w", 6 );

 var Client = TClientList; /* Справочник вкладчиков */

 const C_FlagCur = NumFlagCur();

 private var {Name_Bank};

 var iDep, sDepart, sBranch;

 var sCorrAcc = "";

 var sPrevDate = "_________________ 20____ год";

 var sRestBegin = "____________________________";
 var sRestEnd   = "____________________________";

 var sISO = "";

 var sDType = "";

 var strFilter = "";

 var sOper;

 var nNoMark = 0;  /* Число непомеченных документов */

 var stat;

/*****************************************************************
                    Получение подоперации
******************************************************************/
macro GetSubOper
  var SubOp;
  if ( ( doc.rec.TypeOper != 0 )  AND  ( doc.rec.ApplType != 0 ) )
    sop.rec.IsCur    = doc.rec.IsCur;
    sop.rec.Kind     = doc.rec.Type_Account;
    sop.rec.OperType = doc.rec.TypeOper;
    sop.rec.Type     = doc.rec.ApplType;
    if ( sop.GetEQ() )
      SubOp = " (" + sop.rec.Name + ")";
    else
      SubOp = "";
    end;
  else
    SubOp = "";
  end;
  return SubOp;
end;

/*****************************************************************
                    Получение подоперации
******************************************************************/
macro GetAccount()
  return "";
end;

/*****************************************************************

       Головная процедура выпуска Выписок по Текущим счетам.

  Параметры:

    dep - таблица счетов depositr.dbt

    opr - флаг вида Выписки: TRUE - по операции, FALSE - за опер.день

    flagRes - флаг включения в Выписку строки исходящего остатка

*****************************************************************/

macro Current_Extract( dep, opr, flagRest )

 var sTitle = "";

 var fFindDocs = FALSE;  /* Флаг: документы для выписки нашли. */

 /* Проверка - есть ли непомеченные документы для выписки */
 if ( opr )  /* Нужно только для Выписки по операции */
   doc.Clear();
   doc.rec.Referenc = dep.Referenc;
   stat = doc.getGE();

   while ( stat  AND  ( doc.rec.Referenc == dep.Referenc ) )

     if ( IsServDoc( doc.rec )  AND
          ( doc.rec.YesSBook == "X" ) )  /* Документ не помечен */
       nNoMark = nNoMark + 1;
     end;

     stat = doc.Next();

   end;

   if ( nNoMark == 0 )  /* Непомеченных документов не найдено */
     Exit( 1 );
   end;
 end;

 /* Чтение записи вкладчика. */
 if ( NOT Client.GetRecord( dep.CodClient ) )
   MsgBox( "Не найдена запись вкладчика с CodClient = ",
           dep.CodClient );
   Exit( 0 );
 end;

 /* Чтение записи о Филиале. */
 if ( not findDepartment(dep.FNCash, sBranch) )
   sBranch = "______/______";
 end;

 /* Чтение записи о валюте. */
 if ( dep.Code_Currency > 0 )
   cur.Clear();
   cur.rec.Code_Currency = dep.Code_Currency;
   if ( cur.GetEQ() )
     sISO = cur.rec.Short_Name;
   else
     sISO = "(" + String(dep.Code_Currency) + ")";
   end;
 else
   sISO = "руб.";
 end;

 iDep = Index( Trim( sBranch ), "/" );

 if ( iDep > 0 )
   sDepart = SubStr( Trim( sBranch ), 1, iDep - 1 );
 else
   sDepart = "______";
 end;

 /* Поиск предпоследней операции по счету */
 doc.Clear();
 doc.rec.Referenc = dep.Referenc;
 doc.rec.Date_Document = {CurDate};
 doc.rec.NumDayDoc = 32700;
 stat = doc.getLE();
 while ( stat  AND  ( doc.rec.Referenc == dep.Referenc ) )
   stat = doc.Prev();
   if ( stat  AND  ( doc.rec.Referenc == dep.Referenc )  AND
        IsServDoc( doc.rec ) )
     sPrevDate = String( doc.rec.Date_Document:m );
     stat = FALSE;
   end;
 end;

 /* Поиск входящего остатка на начало дня */
 sRestBegin = "____________________________";
 doc.Clear();
 doc.rec.Referenc = dep.Referenc;
 doc.rec.Date_Document = {CurDate} - 1;  /* Предыдущий день */
 doc.rec.NumDayDoc = 32700;
 stat = doc.getLE();
 while ( stat  AND  ( doc.rec.Referenc == dep.Referenc ) )
   if ( stat  AND
        ( doc.rec.Referenc == dep.Referenc )  AND
        IsServDoc( doc.rec ) )
     sRestBegin = String( doc.rec.Rest:a ) + " " + sISO;
     stat = FALSE;
   else
     stat = doc.Prev();
   end;
 end;

 if ( dep.Type_Account == "Текущий счет" )
   sTitle = "ВЫПИСКА ПО ТЕКУЩЕМУ СЧЕТУ №";
 else
   sTitle = "ВЫПИСКА ПО СЧЕТУ №";
 end;

 [                                                                                                                Ф.N 319

        Наименование банка #
        Номер отделения ################ Номер структурного подразделения #

                        ########################################################################
                                  ####################################################
                                                 (ФИО владельца счета)

        выдана за #

        дата предыдущей операции по счету #

        входящий остаток по счету на начало дня #

  +---------------------------------------------------------------------------------------------------------------------------+
  |   Дата   | Вид  |Номер|     Операция     |         Сумма операции            | Корреспондирующие счета | Остаток по счету |
  |совершения|опера-|доку-|                  |                                   |                         | после проведения |
  | операции | ции  |мента|                  |                                   |                         |    операции      |
  |----------+------+-----+------------------+-----------------+-----------------+-------------------------+------------------|
  |          |      |     |                  |      По Дт сч   |    По Кт сч     |                         |                  |
  |----------+------+-----+------------------+-----------------+-----------------+-------------------------+------------------|]
  (
    "\"" + {Name_Bank} + "\"",
    sDepart:16,
    sBranch,
    ( sTitle + String( Acc_Form(dep.Account):f ) ):c,
    ( Client.CurRec.rec.Name1 + " " +
      Client.CurRec.rec.Name2 + " " +
      Client.CurRec.rec.Name3 ):c,
    {CurDate}:m,
    sPrevDate,
    sRestBegin
  );

 sRestEnd = "____________________________";

 if ( opr )  /* Формирование Выписки по операции */

   fFindDocs = FALSE;  /* Флаг: документы для выписки нашли. */

   strFilter = "t.t_referenc = " + String( dep.Referenc );
   doc.addFilter( strFilter );

   doc.Clear();
   doc.rec.Referenc = dep.Referenc;
   stat = doc.getGE();

   while ( stat  AND  ( doc.rec.Referenc == dep.Referenc ) )

     if ( IsServDoc( doc.rec )  AND
          ( ( doc.rec.InSum  != $0 )      OR   /* SCR #61061. */
            ( doc.rec.OutSum != $0 ) )    AND
          ( ( doc.rec.TypeOper ==  3 )    OR   /* Дополн. взносы       */
            ( doc.rec.TypeOper ==  4 )    OR   /* Расход               */
            ( doc.rec.TypeOper ==  6 )    OR   /* Выдача %%            */
            ( doc.rec.TypeOper == 10 )    OR   /* Закр. наличными      */
            ( doc.rec.TypeOper == 48 )    OR   /* Возмещение недоплаты */
            ( doc.rec.TypeOper == 49 ) ) )     /* Возмещение переплаты */


       sRestEnd = String( doc.rec.Rest:a ) + " " + sISO;
       if ( ( doc.rec.YesSBook == "X" ) )  /* Документ не помечен */

         /* Получение номера корреспондирующего счета */
         sCorrAcc = GetAccount();

         fFindDocs = TRUE;  /* Флаг: документы для выписки нашли. */

         [|##########|######|#####|##################|#################|#################|#########################|##################|]
         (
           doc.rec.Date_Document,
           doc.rec.TypeOper,
           doc.rec.NumDayDoc,
           ( doc.rec.Ground + GetSubOper() ):w,
           doc.rec.OutSum:f,
           doc.rec.InSum:f,
           sCorrAcc:w,
           doc.rec.Rest:f
         );

         doc.rec.YesSBook = "\x00";
         doc.rec.Mode = GetCurrentMode();  /* Иначе сохраняется Mode последнего документа */
         doc.Update();
       end;
     end;

     stat = doc.Next();

   end;

   doc.dropFilter();

   /* Флаг: документы для выписки нашли. */
   if ( NOT fFindDocs )
     Exit( 1 );  /* Ни одного документа не найдено. Выписка не выпускается. */
   end;

 else   /* Формирование Выписки по операцонному дню */

   strFilter = "t.t_referenc = " + String(dep.Referenc) + " and " +
               "t.t_date_document = " + sqlDateToStr( {CurDate} );
   doc.addFilter( strFilter );

   doc.Clear();
   doc.rec.Referenc      = dep.Referenc;
   doc.rec.Date_Document = {CurDate};
   stat = doc.getGE();

   while ( stat  AND
           ( doc.rec.Referenc      == dep.Referenc )  AND
           ( doc.rec.Date_Document == {CurDate} ) )

     if ( IsServDoc( doc.rec )  AND
          ( ( doc.rec.InSum  != $0 )  OR  /* SCR #61061. */
            ( doc.rec.OutSum != $0 ) ) )

       /* Исходящий остаток на конец операционного дня. */
       sRestEnd = String( doc.rec.Rest:a ) + " " + sISO;

       /* Получение номера корреспондирующего счета. */
       sCorrAcc = GetAccount();

       [|##########|######|#####|##################|#################|#################|#########################|##################|]
       (
         doc.rec.Date_Document,
         doc.rec.TypeOper,
         doc.rec.NumDayDoc,
         ( doc.rec.Ground + GetSubOper() ):w,
         doc.rec.OutSum:f,
         doc.rec.InSum:f,
         sCorrAcc:w,
         doc.rec.Rest:f
       );

     end;

     stat = doc.Next();

   end;

   doc.dropFilter();

 end;

 [+---------------------------------------------------------------------------------------------------------------------------+

 ];

 if ( flagRest )
   [      исходящий остаток по счету на конец дня #

   ] ( sRestEnd );
 end;

 dtp.Clear();
 dtp.rec.FlagCur = C_FlagCur;
 dtp.rec.Kind    = dep.Type_Account;
 if ( dtp.GetEQ() )
   sDType = "\"" + dtp.rec.Name + "\".";
 else
   sDType = "\"" + dep.Type_Account + "\".";
 end;

 if ( dep.Type_Account == "Текущий счет" )
   [      Выписка выдана за #

          Подпись работника структурного подразделения_______________    Дата #]
    (
      String( {CurDate}:m ) + " в порядке, установленном договором \"О текущем счете в валюте Российской Федерации\".",
      {CurDate}:m
    );
 else
   [      Выписка выдана за #

          Подпись работника структурного подразделения_______________    Дата #]
    (
      String( {CurDate}:m ) +
      " в порядке, установленном договором по виду вкладов " + sDType,
      {CurDate}:m
    );
 end;

end;
