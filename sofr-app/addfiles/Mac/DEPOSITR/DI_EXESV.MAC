/*
$Name:             DI_EXESV.mac
$Module:           RS-Retail
$Description:      Страхование частных вкладов.  Формирование Реестра по филиалу.
*/

 import DeprIntr, DI_PnSv, DI_ExeVD;

 file dim ( depimake ) key 0 write;  /* Файл "Формирование реестра". */
 file die ( depiprev ) key 1;        /* Файл "Предварительный реестр". */
 file dis ( depisum  ) key 0 write;  /* Файл "Итоговые суммы". */

 const PatternSvod = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\IMPORTMESDIR", TRUE ) +
                     "\\*.txt"; /* Маска исходного текстового файла */

 var NameLoansTxtFile = "";

 array aCur, aSumDebet, aSumCredit;  /* Массивы подсчета сумм. */

 var Errors_Num;  /* Количество ошибок, обнаруженных при формировании Реестра */

 var ErrCodeS, ErrTextS;

 /********************************************************************/
 /*         Формирование записи файла "Формирование реестра".        */
 /********************************************************************/
 macro Form_Make( pDateMake )

   var f_numClient = 0;
   var f_numDebet  = 0;
   var f_numCredit = 0;
   var f_CodClient;
   var f_Stat;

   /* Подсчет записей по вкладчикам. */
   ReWind( die );
   ClearRecord( die );
   die.DateMake = pDateMake;
   f_Stat = GetGE( die );

   if ( f_Stat  AND  ( die.DateMake == pDateMake ) )
     f_CodClient = die.CodClient;
     while ( f_Stat  AND  ( die.DateMake == pDateMake ) )

       if ( f_CodClient != die.CodClient )
         f_numClient = f_numClient + 1;
         f_CodClient = die.CodClient;
       end;

       f_Stat = Next( die );

     end;
     f_numClient = f_numClient + 1;
   end;

   /* Подсчет записей по обязательствам и требованиям. */
   KeyNum( die, 2 );
   ReWind( die );
   ClearRecord( die );
   die.DateMake = pDateMake;
   f_Stat = GetGE( die );

   while ( f_Stat  AND  ( die.DateMake == pDateMake ) )

     if ( die.Type == TYPE_DEPOS )
       f_numDebet = f_numDebet + 1;
     else
       if ( ( die.Type == TYPE_CR_LOAN )  OR
            ( die.Type == TYPE_CR_PERC )  OR
            ( die.Type == TYPE_CR_OVER )  OR
            ( die.Type == TYPE_CR_OVERPERC ) )
         f_numCredit = f_numCredit + 1;
       end;
     end;

     f_Stat = Next( die );

   end;

   dim.DateMake  = pDateMake;
   dim.Branch    = 0;
   dim.Status    = DI_MAKE_STATUS_IN;  /* Введено */
   dim.NumBranch = 1;
   dim.NumClient = f_numClient;
   dim.NumDebet  = f_numDebet;
   dim.NumCredit = f_numCredit;

 end;

 /********************************************************************/
 /*                  Добавление сумм в массивы.                      */
 /********************************************************************/
 macro DI_Sum_Array( pCur, pType, pSum )
   var vSize = aSize( aCur );
   var vInd = 0, vI = 0, vNoFind = TRUE;

   if ( vSize == 0 )  /* Массивы пустые. */
     aCur( 0 ) = pCur;
   else               /* Массивы не пустые. */
     while ( vI < vSize )
       if ( aCur( vI ) == pCur )
         vInd = vI;
         vI   = vSize;
         vNoFind = FALSE;
       else
         vI = vI + 1;
       end;
     end;

     /* Валюту не нашли. */
     if ( vNoFind )
       aCur( vSize ) = pCur;
       aSumDebet(  vSize ) = $0.0;
       aSumCredit( vSize ) = $0.0;
       vInd = vSize;
     end;
   end;

   if ( pType == TYPE_DEPOS )  /* Дебетовая сумма. */
     aSumDebet( vInd ) = aSumDebet( vInd ) + pSum;
   else
     if ( ( pType == TYPE_CR_LOAN )  OR
          ( pType == TYPE_CR_PERC )  OR
          ( pType == TYPE_CR_OVER )  OR
          ( pType == TYPE_CR_OVERPERC ) )  /* Кредитовая сумма. */
       aSumCredit( vInd ) = aSumCredit( vInd ) + pSum;
     end;
   end;

 end;

 /********************************************************************/
 /*                        Основная процедура.                       */
 /********************************************************************/

 macro DI_Svod( pDateMake )

   var vDateMake;       /* Заданная дата формирования реестра */
   var vNameInputFile;  /* Имя файла с данными Loans. */
   var RetParameters  = TRUE;  /* Повторение установки параметров. */
   var FormParameters = TRUE;
   var flagDelMake    = FALSE;
   var flagDelSum     = FALSE;
   var flagDelDebet   = FALSE;
   var flagDelCredit  = FALSE;
   var vArrSize = 0;
   var vArrInd  = 0;
   var NumRecSV = 0;
   var RegV_Stat;
   var StatSv;
   var ErrRV;

   Errors_Num = 0;

   if ( pDateMake == Date( 0, 0, 0 ) )
     pDateMake = {CurDate};
     if ( NOT GetDate( pDateMake, "Укажите дату формирования Реестра" ) )
       MsgBox( "Не заданы параметры работы." );
       Exit( 1 );
     end;
   end;

   while ( RetParameters )

     /* Установка значений для панели параметров */
     panSvDateMake = pDateMake;  /* Дата формирования */
     panSvEscape   = FALSE;      /* Флаг отказа от работы (нажата клавиша ESC) */

     DI_Panel_Svod( FormParameters );

     if ( panSvEscape )
       Exit( 1 );  /* В панели нажата клавиша Esc - отмена работы. */
     end;

     vDateMake = panSvDateMake;  /* Дата формирования */

     /* Чтение настроек регистра АБС. */
     if ( ( GetRegistryValue( RegP_STAT, V_BOOL, RegV_Stat, ErrRV ) != V_BOOL )  OR
          ( ErrRV != 0 ) )
       RegV_Stat = 0;
     end;

     /* Проверка: имеется ли сводная запись по филиалу. */
     ClearRecord( dim );
     dim.DateMake = vDateMake;
     if ( GetEQ( dim ) )
       if ( dim.Status == DI_MAKE_STATUS_SEND )  /* Сформировано */
         MsgBox( "Есть свод.запись за " +
                 String( vDateMake ) +
                 ". Понизьте ее статус и запустите формирование." );
         Exit( 1 );
       end;

       if ( dim.Status == DI_MAKE_STATUS_CONF )  /* Подтверждено */

         if ( RegV_Stat != 0 )
           if ( NOT GetTrue( FALSE, "Реестр за " +
                             String( vDateMake ) +
                             " подтвержден. " +
                             "Отменить подтверждение и формировать?" ) )
             MsgBox( "Переформирование свода отменено." );
             Exit( 1 );
           end;
         else

           MsgBox( "Реестр за " +
                   String( vDateMake ) +
                   " подтвержден. Понизьте статус и запустите формирование." );
           Exit( 1 );

         end;

       end;

       if ( dim.Status == DI_MAKE_STATUS_IN )  /* Введено */
         if ( NOT GetTrue( TRUE, "Имеется сводная запись за " +
                           String( vDateMake ) +
                           ". Начать формирование заново?" ) )
           MsgBox( "Завершите формирование свода вручную." );
           Exit( 1 );
         end;
       end;

       if ( NOT GetTrue( FALSE, "Сводная запись будет удалена и " +
                                "сформирована заново. Вы согласны?" ) )
         MsgBox( "Без удаления старой записи по своду формирование новой невозможно." );
         Exit( 1 );
       end;

       /* Включаются флаги удаления сводной записи и записей по суммам. */
       flagDelMake = TRUE;
       flagDelSum  = TRUE;

     end;

     /* Проверка наличия записей по вкладам за заданную дату. */
     if ( panSvNeedPrev )  /* Включается процедура предварительного формирования. */
       ReWind( did );
       ClearRecord( did );
       did.DateMake = vDateMake;
       did.Type     = TYPE_DEPOS;
       StatSv = GetGE( did );

       if ( StatSv  AND
            ( did.DateMake == vDateMake )  AND
            ( did.Type     == TYPE_DEPOS ) )
         if ( GetTrue( TRUE, "Имеются записи по вкладам за " +
                             String( vDateMake ) +
                             ". Удалить и сформировать заново?" ) )
           /* Нужно удаление записей по вкладам. */
           flagDelDebet = TRUE;
         else
           /* Прекращение работы. */
           Exit( 1 );
         end;
       end;
     end;

     /* Проверка наличия записей по кредитам за заданную дату. */
     if ( panSvLoansOn  OR  panSvLoansOff )
       ReWind( did );
       ClearRecord( did );
       did.DateMake = vDateMake;
       did.Type     = TYPE_CR_LOAN;
       StatSv = GetGE( did );

       if ( StatSv  AND
            ( did.DateMake == vDateMake )  AND
            ( did.Type     >= TYPE_CR_LOAN ) )
         if ( GetTrue( TRUE, "Есть записи по кредитам за " +
                             String( vDateMake ) +
                             ". Удалить и сформировать заново?" ) )
           /* Нужно удаление записей по кредитам. */
           flagDelCredit = TRUE;
         else
           /* Прекращение работы. */
           Exit( 1 );
         end;
       end;
     end;

     /* Выбор файла с исходными данными по кредитам. */
     if ( panSvLoansOff )
       if ( SelectFile( vNameInputFile, PatternSvod,
                        "Выберите файл данных по кредитам" ) )
         NameLoansTxtFile = vNameInputFile;
         RetParameters = FALSE;
       else
         panSvLoansOff = FALSE;
         RetParameters  = TRUE;
         FormParameters = FALSE;
       end;
     else
       RetParameters = FALSE;
     end;

   end;

   /* Удаление старых записей по своду. */
   if ( flagDelMake )
     if ( NOT Delete( dim ) )
       println( "***  Ошибка при удалении сводной записи за ", vDateMake );

       ErrCodeS = Status( ErrTextS );
       println( "  Ошибка " + String( ErrCodeS ) + ": " + ErrTextS );
       println( "" );

       Errors_Num = Errors_Num + 1;
     end;
   end;

   /* Удаление старых записей итоговых сумм. */
   if ( flagDelSum )
     ClearRecord( dis );
     dis.DateMake = vDateMake;
     StatSv = GetGE( dis );

     while ( StatSv  AND  ( dis.DateMake == vDateMake ) )
       if ( NOT Delete( dis ) )
         println( "***  Ошибка при удалении записи итоговых сумм за ", vDateMake );

         ErrCodeS = Status( ErrTextS );
         println( "  Ошибка " + String( ErrCodeS ) + ": " + ErrTextS );
         println( "" );

         Errors_Num = Errors_Num + 1;
       end;

       StatSv = Next( dis );
     end;
   end;

   /* Удаление старых записей по дебету. */
   if ( flagDelDebet )
     ReWind( did );
     ClearRecord( did );
     did.DateMake = vDateMake;
     did.Type     = TYPE_DEPOS;
     StatSv = GetGE( did );
     while ( StatSv  AND
             ( did.DateMake == vDateMake )  AND
             ( did.Type     == TYPE_DEPOS ) )
       if ( NOT Delete( did ) )
         println( "*** Ошибка при удалении записей по вкладам: ",
                  Acc_Form( did.Account ) );

         ErrCodeS = Status( ErrTextS );
         println( "  Ошибка " + String( ErrCodeS ) + ": " + ErrTextS );
         println( "" );

         Errors_Num = Errors_Num + 1;
       end;
       StatSv = Next( did );
     end;
   end;

   /* Удаление старых записей по кредиту. */
   if ( flagDelCredit )
     ReWind( did );
     ClearRecord( did );
     did.DateMake = vDateMake;
     did.Type     = TYPE_CR_LOAN;
     StatSv = GetGE( did );
     while ( StatSv  AND
             ( did.DateMake == vDateMake )  AND
             ( did.Type     >= TYPE_CR_LOAN ) )
       if ( NOT Delete( did ) )
         println( "*** Ошибка при удалении записей по кредитам: ",
                  Acc_Form( did.Account ) );

         ErrCodeS = Status( ErrTextS );
         println( "  Ошибка " + String( ErrCodeS ) + ": " + ErrTextS );
         println( "" );

         Errors_Num = Errors_Num + 1;
       end;
       StatSv = Next( did );
     end;
   end;

   /* Процедура "Предварительное формирование Реестра". */
   if ( panSvNeedPrev )
     if ( Main_Insurance_Reestr( vDateMake,
                                 panSvLoansOn,   /* Флаг: Loans Online */
                                 panSvLoansOff,  /* Флаг: Loans Offline */
                                 panSvNeedCalc,  /* Флаг выполнения расчета */
                                 TRUE,
                                 "",
                                 NameLoansTxtFile ) )
       Exit();
     end;
   end;

   /* Loans: общая база. */
   if ( NOT panSvNeedPrev  AND  panSvLoansOn )
     DI_Mac_Credit( vDateMake );
   end;

   /* Loans: Прием данных по кредитам из транспортного файла. */
   if ( NOT panSvNeedPrev  AND  panSvLoansOff )
     if ( DI_Credit_Transport( NameLoansTxtFile, "" ) )
       Exit();
     end;
   end;

  //  Установка кодов дублеров для клиентов
  set_Unique_Clients( vDateMake );

   /* Расчет суммы обязательств. */
   if ( NOT panSvNeedPrev  AND  panSvNeedCalc )
     if ( DI_Calc_Acc( vDateMake, 0, TRUE, "" ) )
       Exit();
     end;
   end;

   /* Формирование записи свода. */
   Form_Make( vDateMake );

   if ( NOT Insert( dim ) )
     Errors_Num = Errors_Num + 1;
     println( " *** Ошибка Insert свода за ", vDateMake );

     ErrCodeS = Status( ErrTextS );
     println( "  Ошибка " + String( ErrCodeS ) + ": " + ErrTextS );
     println( "" );
     Exit();
   end;

   /* Формирование итоговых сумм. */
   aSumDebet ( 0 ) = $0.0;
   aSumCredit( 0 ) = $0.0;

   InitProgress( NRecords( did ), " Ж Д И Т Е ...",
                 "Формирование итоговых сумм." );
   NumRecSV = 0;

   ReWind( did );
   ClearRecord( did );
   did.DateMake = vDateMake;
   did.Type     = TYPE_DEPOS;
   StatSv = GetGE( did );

   while ( StatSv  AND  ( did.DateMake == vDateMake ) )
     NumRecSV = NumRecSV + 1;
     UseProgress( NumRecSV );
     DI_Sum_Array( did.Currency, did.Type, did.SummaRest );
     StatSv = Next( did );
   end;

   RemProgress();

   vArrSize = aSize( aCur );

   while ( vArrInd < vArrSize )

     ClearRecord( dis );
     dis.DateMake  = vDateMake;
     dis.Branch    = 0;  /* Сводная запись по филиалу. */
     dis.Currency  = aCur( vArrInd );
     dis.SumDebet  = aSumDebet( vArrInd );
     dis.SumCredit = aSumCredit( vArrInd );

     if ( NOT Insert( dis ) )
       Errors_Num = Errors_Num + 1;
       println( " *** Error Sum Insert." );

       ErrCodeS = Status( ErrTextS );
       println( "  Ошибка " + String( ErrCodeS ) + ": " + ErrTextS );
       println( "" );
       Exit();
     end;

     vArrInd = vArrInd + 1;

   end;

   /* Проверка наличия записи в таблице "Предварительный Реестр". */
   ReWind( die );
   ClearRecord( die );
   die.DateMake = vDateMake;
   StatSv = GetGE( die );

   if ( StatSv  AND  ( die.DateMake == vDateMake ) )

     ClearRecord( dim );
     dim.DateMake = vDateMake;
     if ( GetEQ( dim ) )

       dim.Status = DI_MAKE_STATUS_CONF;  /* Подтверждено */

       if ( NOT Update( dim ) )
         Errors_Num = Errors_Num + 1;
         println( " *** Ошибка Update свода за ", vDateMake );

         ErrCodeS = Status( ErrTextS );
         println( "  Ошибка " + String( ErrCodeS ) + ": " + ErrTextS );
         println( "" );
         Exit();
       end;

     else
       Errors_Num = Errors_Num + 1;
       println( " *** Не найдена запись свода за ", vDateMake );

       ErrCodeS = Status( ErrTextS );
       println( "  Ошибка " + String( ErrCodeS ) + ": " + ErrTextS );
       println( "" );
     end;

   end;

   println( "Формирование реестра завершено." );
   println( "Обнаружено ошибок: ", Errors_Num );
   println( "" );

 end;
