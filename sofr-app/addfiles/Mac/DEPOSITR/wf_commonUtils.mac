/*
$Name:               wf_commonUtils.mac
$Module:             DEPOSITR
$Description:        wf_commonUtils
*/

import rsd, BankInter, wf_params, wf_uiFormManager, rsexts;

private file messages("sbbank.msg") key 0;
private file curr("currency.dbt") key 0;

const USER_ABORT = 4786,
      INTERNAL_ERROR = 9999;

private const OK = "OK";

private const V_SPECVAL = 26;


const NEW_LINE = "\r\n";


class DefaultSubsystem

  macro commonInit()
  end;

  macro commonDone()
  end;

  macro getTopErrorMessage(): string
    return null;
  end;
end;


var Subsystem = DefaultSubsystem;


/*************************************************
 *  Result handling 
 ************************************************/

class TAppError(_code : Integer, _message : String)
  var code : Integer = _code;
  var message : String = _message;
end;

private macro getMessageForNumber(number : Integer) : String
  messages.number = number;
  if (getEq(messages))
    return messages.contents;
  else
    return "Сообщение №" + number;
  end;
end;

macro handleResult(errCode : Integer, msg : String) : TParameters
  var outParams = TParameters;

  if (msg == null)
    if (errCode == 0)
      msg = OK;
    else
      msg = Subsystem.getTopErrorMessage();
      if ((msg == null) or (msg == ""))
        msg = getMessageForNumber(errCode);
      end;
    end;
  end;

  outParams.add("КодОшибки", errCode);
  outParams.add("Сообщение", msg);
  return outParams;
end;

macro handleAppError(e, defErrCode : Integer) : TParameters
  var code = defErrCode;
  var msg = e.message;
  if (isEqClass("TAppError", e.err))
    code = e.err.code;
    msg = e.err.message;
  end;
  return handleResult(code, msg);  
end; 

macro singleResult(name : String, value)
  var outParams = handleResult(0);
  outParams.add(name, value); 
  return outParams;
end;

macro handleStoredProcResult : TParameters
  var cmd = RsdCommand("{? = call rsu_rtlcommon.getLastErrorCode()}");
  cmd.addParam("p_retVal", RsdBp_Out, V_INTEGER);
  cmd.execute;
  var errorCode = cmd.value("p_retVal");

  var msg = null;
  if (errorCode != 0)
    cmd = RsdCommand("{? = call rsu_rtlcommon.getLastErrorMessage()}");
    cmd.addParam("p_retVal", RsdBp_Out, V_STRING, 10000);
    cmd.execute;
    msg = cmd.value("p_retVal");
    if (valType(msg) == V_SPECVAL)
      msg = "";
    end;
  end; 

  return handleResult(errorCode, msg);
end;

/*************************************************
 *  Common initialization
 *  Should be done once per worker process
 ************************************************/

macro commonInit
  Subsystem.commonInit();
end;

macro commonDone
  Subsystem.commonDone();
end;

/*************************************************
 *  UI sessions
 ************************************************/

macro uiGetSessionParams(sessionId : String)
  uiFormManager.recallInfo;
  if ((uiFormManager.uiSessionId == sessionId) and 
      (uiFormManager.currentForm != null))
    return uiFormManager.getFieldsAsParameters;
  else
    return null;
  end;
end;

macro uiGetSessionId(paramStr : String) : String
  var inParams = TParameters(paramStr);
  return inParams.get("uiSessionId");
end;

macro uiSimpleCheckInputReady(paramStr : String, valNameToBePresent : String)
  var retVal = false; 
  var sessId = uiGetSessionId(paramStr);
  var sessParams = uiGetSessionParams(sessId);
  if ( sessParams != null)
    if (sessParams.get(valNameToBePresent) != null) 
      retVal = true;
    end; 
  end;
  return singleResult("retVal", retVal).toString;
end;

macro uiTrue
  return singleResult("retVal", true).toString;
end;


/*************************************************
 *  Utility functions
 ************************************************/

macro getCurrencyCcy(currCode : Integer)
  curr.code_currency = currCode;
  if (not getEq(curr))
    runError(logError("Не найдена валюта с кодом " + currCode));
  end; 
  return curr.short_name;
end;

