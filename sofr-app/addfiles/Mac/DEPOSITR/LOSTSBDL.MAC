/*
$Name:             lostsbdl.mac
$Module:           RS-Retail
$Description:      Заявление об утрате сберегательной книжки
*/

/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  ЗАЯВЛЕНИЕ ОБ УТРАТЕ СБЕРЕГАТЕЛЬНОЙ КНИЖКИ.  Ф.№285.

*****************************************************************/

import Alg_Vars, Acc_Form;

/* Состояние заявления об утрате сбер.книжки. */
const LBDS_ANY         = 0,  /* Любое      */
      LBDS_INITIALIZED = 1,  /* Введено    */
      LBDS_PROCESSED   = 2,  /* Обработано */
      LBDS_CANCELLED   = 3;  /* Отменено   */

/* Вид обработки заявления об утрате сбер.книжки. */
const LBDP_UNDEF     = 0,  /* Не определен                */
      LBDP_CASHCLOSE = 1,  /* Закрыть счет наличными      */
      LBDP_TRANSFER  = 2,  /* Перечислить остаток на счет */
      LBDP_REISSUE   = 3;  /* Выдать дубликат сб/кн       */

var csln = TBFile( "sb_casln.dbt", "r", 0 );
var dep  = TBFile( "depositr.dbt", "r", 8 );
var lstb = TBFile( "lostbook.dbt", "r", 5 );

var DepClnt = TClientList; /* Справочник вкладчиков */

var vFlagClose     = "";
var vFlagNal       = "";
var vFlagAcc       = "";
var vFlagDublicate = "";
var vTransAcc      = "";

/**************************************************************************/
/*               Атрибуты удостоверения личности клиента.                 */
/**************************************************************************/
macro getPaperClient()
 return GetNameOfPAPRKIND( DepClnt.CurRec.rec.PaperKind ) + " " +
        DepClnt.CurRec.rec.PaperSeries + " № " +
        DepClnt.CurRec.rec.PaperNumber + ", выдан " +
        DepClnt.CurRec.rec.PaperIssuer + ", " +
        DepClnt.CurRec.rec.PaperIssuedDate;
end;

/**************************************************************************\
|      Проверка: первый вызов макроса из связки документов по операции.    |
\**************************************************************************/
macro IsFirstLog()

  var ret_flag       = false;
  var stat;
  var kind_main      = 0;
  var key_main       = "";
  var key_current    = "";
  var numDprt        = String( ALG_NFOPR.numDprt );
  var recID          = String( ALG_NFOPR.recID );

  while ( StrLen( numDprt ) < 5 )
    numDprt = "0" + numDprt;
  end;
  while ( StrLen( recID ) < 10 )
    recID = "0" + recID;
  end;
  key_current = "rtnfopr#" + numDprt + recID + "00";

  csln.KeyNum = 1;
  csln.rec.ApplicationKind2 = 3001;
  csln.rec.ApplicationKey2  = key_current;
  csln.rec.RefValue2        = 0;

  stat = csln.GetEQ();

  if ( stat )
    ;
  else
    numDprt = String( ALG_NFOPR.numDprt );

    while ( StrLen( numDprt ) < 3 )
      numDprt = "0" + numDprt;
    end;

    key_current = "rtnfopr#" + numDprt + recID + "00";

    csln.KeyNum = 1;
    csln.rec.ApplicationKind2 = 3001;
    csln.rec.ApplicationKey2  = key_current;
    csln.rec.RefValue2        = 0;

    stat = csln.GetEQ();
  end;

  if ( stat )
    kind_main = csln.rec.ApplicationKind1;
    key_main  = csln.rec.ApplicationKey1;

    csln.KeyNum = 0;
    csln.rec.ApplicationKind1 = kind_main;
    csln.rec.ApplicationKey1  = key_main;
    csln.rec.NumLinkDoc       = 0;

    stat = csln.GetGE();
    while ( stat )
      if ( ( csln.rec.ApplicationKind1 == kind_main ) and
           ( csln.rec.ApplicationKey1  == key_main  )    )
        if ( csln.rec.ApplicationKind2 == 3001 )
          if ( csln.rec.ApplicationKey2 == key_current )
            ret_flag = true;
          end;
          stat = false;
        else
          stat = csln.Next();
        end;
      else
        stat = false;
      end;
    end;
  end;

  return ret_flag;

end;

/**************************************************************************/
/*                   Получение номера подразделения.                      */
/**************************************************************************/
macro BranchNumber( pBranch )
 var sFilial = "???";
 var i_dp_dep = TRecHandler( "i_dp_dep.rec" );
 if ( FindDepartment( pBranch, NULL, i_dp_dep ) )
   sFilial = i_dp_dep.rec.Name;
 else
   sFilial = String( pBranch );
 end;
 return sFilial;
end;

/**************************************************************************/
/*                   Получение Ф.И.О. клиента по коду.                    */
/**************************************************************************/
macro getFIO( pClient )
 var sFIO_Declarer = "???";
 if ( DepClnt.GetRecord( pClient ) )
   sFIO_Declarer = DepClnt.CurRec.rec.Name1 + " " +
                   DepClnt.CurRec.rec.Name2 + " " +
                   DepClnt.CurRec.rec.Name3;
 end;
 return sFIO_Declarer;
end;

/**************************************************************************/
/*                   Чтение записи счета по Reference.                    */
/**************************************************************************/
macro AccByRef( pReference )
 var sAcc = "???";
 dep.Clear();
 dep.rec.Referenc = pReference;
 if ( dep.getEQ() )
   sAcc = String( dep.rec.Account:f );
 end;
 return sAcc;
end;

/**************************************************************************/
/*      Основная процедура выпуска заявления об утрате сбер.книжки.       */
/**************************************************************************/
macro Print_LB()

 if ( GetBankStandart() != 0 )  /* Коммерческий банк. */
   Exit( 1 );  /* Выйти без выпуска заявления. */
 end;

 lstb.Clear();
 lstb.rec.Reference = ALG_DEPOSITOR.Referenc;
 lstb.rec.State     = LBDS_INITIALIZED;

 if ( lstb.getEQ() )
   if ( ( lstb.rec.Processing == LBDP_CASHCLOSE )  OR  /* Закрыть счет наличными */
        ( lstb.rec.Processing == LBDP_TRANSFER ) )     /* Перечислить остаток на счет */
     vFlagClose = "X";
   else
     vFlagClose = "";
   end;

   if ( lstb.rec.Processing == LBDP_REISSUE )  /* Выдать дубликат сб/кн */
     vFlagDublicate = "X";
   else
     vFlagDublicate = "";
   end;

   if ( lstb.rec.Processing == LBDP_CASHCLOSE )  /* Закрыть счет наличными */
     vFlagNal = "X";
   else
     vFlagNal = "";
   end;

   if ( lstb.rec.Processing == LBDP_TRANSFER )  /* Перечислить остаток на счет */
     vFlagAcc = "X";
   else
     vFlagAcc = "";
   end;

   if ( StrLen( lstb.rec.DestAccType ) > 0 )
     vTransAcc = "новый \"" + lstb.rec.DestAccType + "\"";
   end;

   if ( lstb.rec.DestReference > 0 )
     vTransAcc = "№ " + Acc_Form( AccByRef( lstb.rec.DestReference ) );
   end;

   [
                                                                                                     Ф.№285

      СБЕРБАНК РОССИИ                          В _ ############ ______________________________________
                                               _______________________________________________________
                                                 (номер/наименование подразделения Сбербанка России)
                                               #######################################################
                                               _______________________________________________________
                                                              (Фамилия, имя и отчество)
                                               #######################################################
                                               _______________________________________________________
                                               _______________________________________________________
                                               #######################################################
                                               _______________________________________________________
                                               _______________________________________________________
                                               _______________________________________________________
                                               _______________________________________________________

                                   ЗАЯВЛЕНИЕ ОБ УТРАТЕ СБЕРЕГАТЕЛЬНОЙ КНИЖКИ


   В связи с утратой сберегательной книжки по вкладу № #######################################
   ###########################################################################################
                                (указывается Ф.И.О вкладчика)
   +-----+--------------------------------------------------------------------------------------------+
   |     | расторгнуть договор с закрытием счета по вкладу.                                           |
   |     |                             +-+                                                            |
   |     | Причитающуюся сумму вклада: |#| выдать наличными деньгами;                                 |
   | +-+ |                             +-+                                                            |
   | |#| |                             +-+                                                            |
   | +-+ |                             |#| перечислить на счет ###################################### |
   |     |                             +-+                                                            |
   |     | С размером платы, взимаемой за приостановление операций по счету при утрате сберегательной |
   |     | книжки (в случае, если она предусмотрена), согласен.                                       |
   +-----+--------------------------------------------------------------------------------------------+

   +-----+--------------------------------------------------------------------------------------------+
   |     | выдать ее дубликат.                                                                        |
   | +-+ | С размером платы, взимаемой за выдачу дубликата сберегательной книжки при ее утрате,       |
   | |#| | согласен.                                                                                  |
   | +-+ | Обязуюсь при обнаружении сберегательной книжки, ранее заявленной утраченной, вернуть ее в  |
   |     | структурное подразделение Сбербанка России.                                                |
   +-----+--------------------------------------------------------------------------------------------+


                                                                                   _________________________
                                                                                           (Подпись)

                                                   #########################################################


   РАСПИСКА ЗАЯВИТЕЛЯ:

   Денежные средства по счету №________________ в сумме__________руб.________коп. получил. (указывается если
   сумма вклада выдается наличными деньгами)

                                                                       ________________ /___________________/
                                                                      (Подпись)       (Фамилия, имя и отчество)

                                                                                    "___"_____________20____г.

   Дубликат №_____ сберегательной книжки по счету №________________ на сумму __________руб._______коп.  взамен
   утраченной книжки получил. (указывается если выдается дубликат сберкнижки)


                                                                       ________________ /___________________/
                                                                     (Подпись)       (Фамилия, имя и отчество)

                                                                                    "___"_____________20____г.
   

                                                 ОТМЕТКИ БАНКА

   КОНТРОЛЕР

   В личности заявителя убедился. На "___"________20____г. (указывается дата совершения последней
   операции) остаток вклада в сумме _________ остаток процентов в сумме___________ по счету
   №_________________ на имя _________________________________________
                                    (указывается Ф.И.О вкладчика)
   и подпись заявителя, подтверждаю.


                                                                        ________________ /___________________/
                                                                      (Подпись)       (Фамилия, имя и отчество)

                                                                                     "___"_____________20____г.


   СОТРУДНИК, УПОЛНОМОЧЕННЫЙ НА ВЫДАЧУ РАЗРЕШЕНИЙ
     +-----+--------------------------------------------------------------------------------------------+
     |     |                                                                                            |
     | +-+ |                                                                                            |
     | | | | В личности заявителя убедился, подпись заявителя подтверждаю.                              |
     | +-+ |                                                                                            |
     |     |                                                                                            |
     +-----+--------------------------------------------------------------------------------------------+

     +-----+--------------------------------------------------------------------------------------------+
     |     | Остаток вклада в сумме _________ остаток процентов в сумме__________ по счету              |
     | +-+ |                                                                                            |
     | | | | №_____________________ на "___"________20____г. (указывается дата совершения               |
     | +-+ | последней операции), а также подпись заявителя подтверждаю.                                |
     |     |                                                                                            |
     +-----+--------------------------------------------------------------------------------------------+


     +--------------------------------------------------------------------------------------------------+
     | РАЗРЕШАЮ:                                                                                        |
     | +-+                                                                                              |
     | | | закрыть счет по вкладу №____________________ на имя _______________________________.         |
     | +-+                                                       (указывается Ф.И.О вкладчика)          |
     |                                                                                                  |
     | +-+                                                                                              |
     | | | выдать дубликат №________ сберкнижки по вкладу №_________________________________            |
     | +-+                                                                                              |
     | на имя _____________________________________________________________________________.            |
     |                              (указывается Ф.И.О вкладчика)                                       |
     +--------------------------------------------------------------------------------------------------+


      _______________________________________________________________ ______________ /___________________/
       (Должность сотрудника, уполномоченного на выдачу разрешений)  (Подпись)   (Фамилия, имя и отчество)


                                                                                 "___"_____________20____г.]
   (
     BranchNumber( lstb.rec.Branch ):c,
     ( "от " + getFIO( lstb.rec.ClientCode ) ):w,
     ( "проживающего по адресу " + DepClnt.CurRec.MakeFullStrAddress() ):w,
     ( "паспортные данные " + getPaperClient() ):w,
     Acc_Form(ALG_DEPOSITOR.Account):f,
     ( "на имя " + getFIO( ALG_DEPOSITOR.CodClient ) + ", прошу:" ):w,
     vFlagNal,
     vFlagClose,
     vFlagAcc,
     vTransAcc,
     vFlagDublicate,
     lstb.rec.DateRegistered:m:r
   );

 else

   Exit( 1 );  /* Выйти без выпуска заявления. */

 end;

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/

 if ( IsFirstLog() )
   Print_LB();
 else
   Exit( 1 );
 end;

end;
