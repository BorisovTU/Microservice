/***************************************************************************\

                                 RS-Retail
                      Обслуживание безналичных платежей

                          Макрос копирования списка
                       зачислений на счета вкладчиков

\***************************************************************************/

  import deprintr, rslscrp;

  record scrl ( "TRNSPAYF" ) dialog;
  record pdlt ( "TRNPPAYF" ) dialog;

  record r_pf (trn_payf);   /* Текущее п\п по филиалу */

  file payf (trn_payf) key 2;        /* П\П по безнал.платежам по филиалу */
  file doc  (trn_doc)  key 0 write;  /* Зачисления по безнал.платежам     */

  var {curdate}, {oper};

  var AutoKey    = 0;    /* Идент.записи по п\п */
  var NumCntr    = 0;    /* Номер договора      */

  var DeltaSum   = $0L;  /* Приращение суммы    */
  var DeltaPerc  = $30L; /* Процент приращения  */

  var CurSum     = $0L;  /* Сумма зачислений текущего п\п  */
  var CopySum    = $0L;  /* Сумма скопированных зачислений */

  var FirstRecI  = TRUE;
  var MarkTitle  = TRUE;
  var FlagFind   = FALSE;
  var FlagDoc    = FALSE;

  var rec = true;
  var pos = 0;

  /***********************************************************/
  /*               Процедуры фильтра скролинга               */
  /***********************************************************/
  macro TestRecI( ff )
    if ( ( ff.NumberContract == NumCntr )  AND
         ( ff.AutoKey != AutoKey ) )
      return True;
    end;
    return False;
  end;

  macro NextIncass( ff )
    if ( FirstRecI )
      FirstRecI = False;
      ClearRecord( ff );
      ff.NumberContract = NumCntr;
      if ( GetGE( ff ) )
        return TestRecI( ff );
      end;
    else
      if ( Next( ff ) )
        return TestRecI( ff );
      end;
    end;
    return False;
  end;

  macro PrevIncass( ff )
    if ( FirstRecI )
      FirstRecI = False;
      ClearRecord( ff );
      ff.NumberContract = NumCntr;
      if ( GetLE( ff ) )
        return TestRecI( ff );
      end;
    else
      if ( Prev( ff ) )
        return TestRecI( ff );
      end;
    end;
    return False;
  end;

  macro RewindIncass( ff )
    Rewind( ff );
    FirstRecI = True;
  end;

  /*****************************************************************
            Процедура подсчета суммы зачислений текущего п\п
  ******************************************************************/
  macro CalcStartSum

    ClearRecord( doc );
    doc.FNCash       = r_pf.FNcash;
    doc.ListTransfer = r_pf.AutoKey;
    rec = getGE( doc );

    CurSum = $0L;

    while ( rec  and
            ( doc.FNCash       == r_pf.FNcash )  and
            ( doc.ListTransfer == r_pf.AutoKey ) )
      CurSum = CurSum + doc.Sum;
      rec = next( doc );
    end;

  end;

  /*****************************************************************
                  Процедура копирования зачислений
  ******************************************************************/
  macro CopyExec

    CopySum = $0L;

    ClearRecord( doc );
    doc.FNCash       = payf.FNcash;
    doc.ListTransfer = payf.AutoKey;

    rec = getGE( doc );

    /* Заголовок отчета */
    if ( rec                                   AND
         ( doc.FNCash       == payf.FNcash )   AND
         ( doc.ListTransfer == payf.AutoKey )  AND
         ( CurSum + (doc.Sum * ($100L + DeltaPerc) / $100L) + DeltaSum
                <= r_pf.GlobalSumFilial ) )
      FlagDoc = TRUE;
      [
                               Документы, скопированные по договору "#########################" п/п ####

        ┌───────────────────────────┬──────────────────────────────────────────┬───────────────────────────┬──────┬──────┬───────┐
        │           Счет            │             Ф.И.О. вкладчика             │           Сумма           │ Запр │ Лист │ Пачка │
        ├───────────────────────────┼──────────────────────────────────────────┼───────────────────────────┼──────┼──────┼───────┤]
        ( r_pf.NumberContract:25, r_pf.Num_PayMessage:4:l );
     end;

    while ( rec                                   AND
            ( doc.FNCash       == payf.FNcash )   AND
            ( doc.ListTransfer == payf.AutoKey ) )

      pos = GetPos( doc );  /* Позиция старой записи */

      CurSum = CurSum + (doc.Sum * ($100L + DeltaPerc) / $100L) + DeltaSum;

      if ( CurSum <= r_pf.GlobalSumFilial )

        doc.ListTransfer     = AutoKey;
        doc.Date_Document    = {curdate};
        doc.Oper             = {oper};
        doc.Sum              = (doc.Sum * ($100L + DeltaPerc) / $100L) + DeltaSum;
        doc.iApplicationKind = 0;
        doc.ApplicationKey   = "";
        doc.Status           = StrFor( 0 );
        doc.NumSession       = -1;  /* Введено в филиале ! */
        doc.Action           = 0;
        doc.SignCheckList    = StrFor( 0 );
        doc.ReasonNoCarry    = 0;
        doc.OldlistTransfer  = 0;
        doc.AutoKey          = 0;
        doc.OldAutoKey       = 0;
        doc.ErrorCode        = 0;
        doc.Fict             = StrFor( 0 );

        if ( Insert( doc ) )
          CopySum = CopySum + doc.Sum;
          [ │ ######################### │ ######################################## │ ######################### │ #### │ #### │ ##### │ ]
          ( Acc_Form(doc.Account):25, (doc.Sname+" "+doc.Nname+" "+doc.Pname):40,
            doc.Sum:25:2:a, doc.ReasonNoCarry:4, doc.NList:4, doc.NPack:5 );
        else
          println( "Ошибка при копировании зачисления по счету ", Acc_Form(doc.Account),
                   " вкладчика ", (doc.Sname+" "+doc.Nname+" "+doc.Pname),
                   " на сумму ", doc.Sum:0:2:a );
        end;

      else

        if ( MarkTitle )
          [ └───────────────────────────┴──────────────────────────────────────────┴───────────────────────────┴──────┴──────┴───────┘

              Общая сумма скопированных зачислений по п/п:    ###########################
              Общая сумма зачислений по платежному поручению: ###########################


                       Документы, не скопированные по договору "#########################" п/п #,
                                 вследствие превышения суммы платежного поручения


            ┌───────────────────────────┬──────────────────────────────────────────┬───────────────────────────┐
            │           Счет            │             Ф.И.О. вкладчика             │           Сумма           │
            ├───────────────────────────┼──────────────────────────────────────────┼───────────────────────────┤]
            ((CopySum/$1L):0:2:a:l,
             ((CurSum-(doc.Sum * ($100L + DeltaPerc) / $100L)-DeltaSum)/$1L):0:2:a:l,
             r_pf.NumberContract:25, r_pf.Num_PayMessage:l );
          MarkTitle = False;
        end;

        [ │ ######################### │ ######################################## │ ######################### │]
        ( Acc_Form(doc.Account):25, (doc.Sname+" "+doc.Nname+" "+doc.Pname):40,
          doc.Sum:25:2:a );

      end;

      GetDirect( doc, pos ); /* Возвращение на старую запись */

      rec = next( doc );

    end;

    if ( FlagDoc )
      if ( MarkTitle )
        [ └───────────────────────────┴──────────────────────────────────────────┴───────────────────────────┴──────┴──────┴───────┘

            Общая сумма скопированных зачислений по п/п:    ###########################
            Общая сумма зачислений по платежному поручению: ###########################]
        ((CopySum/$1L):0:2:a:l,(CurSum/$1L):0:2:a:l);
      else
        [ └───────────────────────────┴──────────────────────────────────────────┴───────────────────────────┘];
      end;
    else
      println( "\n Не найдено документов для копирования" );
    end;

  end;

  /*****************************************************************
                Процедура входа в копирование зачислений
  ******************************************************************/

  macro TRN_Copy( Addr )

    SetBuff( r_pf, Addr );

    /* Сохранение параметров п\п */
    AutoKey = r_pf.AutoKey;
    NumCntr = r_pf.NumberContract;

    /* Поиск записи по договору */
    ClearRecord( payf );
    payf.NumberContract = NumCntr;
    if ( ( NOT getGE( payf ) )              OR
         ( payf.NumberContract != NumCntr ) OR
         ( payf.AutoKey == AutoKey ) )
      println( "\n Платежных поручений по договору \"", NumCntr,
               "\",\n кроме текущего п/п не найдено" );
      return;
    end;

    ReplaceMacro( "Next",   "NextIncass"   );
    ReplaceMacro( "Prev",   "PrevIncass"   );
    ReplaceMacro( "Rewind", "RewindIncass" );

    scrl.Cntr = NumCntr;

    SetScroll( payf, "Num", "Num_PayMessage",
                     "Grn", "Ground",
                     "Sum", "GlobalSumFilial",
                     "C",   "SignCheckFilial",
                     "K",   "SignKvitFilial" );

    FlagFind = RunDialog( scrl, "ScrollMes" );

    ReplaceMacro( "Next" );
    ReplaceMacro( "Prev" );
    ReplaceMacro( "Rewind" );

    if ( FlagFind )
      pdlt.Perc = 0;
      pdlt.Sum  = $0L;
      FlagFind = RunDialog( pdlt );
    end;

    if ( FlagFind )
      CalcStartSum();
      if ( CurSum < r_pf.GlobalSumFilial )
        DeltaPerc = pdlt.Perc * 100;
        DeltaSum  = pdlt.Sum;
        CopyExec();
      else
        MsgBox( "Сумма зачислений по п/п ", CurSum,
                "|не меньше суммы п/п ",   r_pf.GlobalSumFilial,
                ".|Копирование отменяется" );
        println( "\n Копирование отменено" );
      end;
    else
      println( "\n Копирование отменено" );
    end;

  end;
