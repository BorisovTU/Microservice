/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Выпуск доп.соглашений по видам вкладов.                                 *
*                                                                          *
\**************************************************************************/

import DeprIntr, Alg_Vars, IsSrvDoc, RSD;
import pr___dda, pr___ddc;

const cNoMake = 0,
      cD1 = 1,
      cD2 = 2;

private const DateNull = Date(0,0,0);

var vVar = -1;

var ii = 0, vRet = TRUE;

// ******************************************************************
//    По виду вклада предусмотрено ограниченное число пролонгаций.
// ******************************************************************
private macro NumProlongation()
 var vRet = FALSE;
 var cmd, rs, q;

 q = "SELECT t_NumberProl FROM ddepositr_dbt " +
       "WHERE t_IsCur = " + String(ALG_DEPOSITOR.IsCur) + " AND " +
             "t_Referenc = " + String(ALG_DEPOSITOR.Referenc);
 cmd = RsdCommand( q );
 rs = RsdRecordSet( cmd );
 if ( rs.MoveNext() )
   if ( rs.Fld("t_NumberProl").Value != 0 )
     vRet = TRUE;
   end;
 end;

 return vRet;
end;

// ******************************************************************
//          До 15.02.2006 (включительно) прошла пролонгация.
// ******************************************************************
private macro ProlongationOperation()
 var vRet = FALSE;
 var cmd, rs, q;

 q = "SELECT count(*) nn FROM ddepositr_dbt dr,dsbdepdoc_dbt doc " +
       "WHERE dr.t_Referenc = " + String(ALG_DEPOSITOR.Referenc) + " AND " +
             "dr.t_NumberProl != 0 AND " +
             "doc.t_Referenc = dr.t_Referenc AND " +
             "doc.t_DepDate_Document <= to_date('15.02.2006','dd.mm.yyyy') AND " +
             "(doc.t_TypeOper = 82 OR doc.t_TypeComplexOper = 82) AND " +
             "doc.t_KindOp <> 8 AND doc.t_KindOp <> 9 AND " +
             "doc.t_KindOp <> 14 AND doc.t_KindOp <> 15 AND " +
             "doc.t_KindOp <> 16 AND doc.t_Action <> 2 AND " +
             "doc.t_TypeOper <> 38 AND doc.t_TypeOper <> 39 AND " +
             "doc.t_Mode <> 2 and bitand(doc.t_flags,131072 ) = 0";

 cmd = RsdCommand( q );
 rs = RsdRecordSet( cmd );
 if ( rs.MoveNext() )
   if ( rs.value("nn") > 0 )
     vRet = TRUE;
   end;
 end;

 return vRet;
end;

// ******************************************************************
//       Последняя операция пролонгации прошла до 20.03.2007.
// ******************************************************************
private macro LastProlongation()
 var vRet = FALSE;
 var cmd, rs, q;
 var depRef = ALG_DEPOSITOR.Referenc;
 var lastDaySeparator = 
    GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ОПЕРАЦИИ\\ОБРАБОТКА_ДОГОВОРОВ\\ПОСЛЕДНИЙ_ДЕНЬ_СРОКА\\ГРАНИЦА", false );

 q = "SELECT count(*) nn FROM dsbdepdoc_dbt doc " +
       "WHERE doc.t_Referenc = :DepRefParam AND " +
             "doc.t_DepDate_Document >= to_date(:LastDaySeparatorParam,'dd.mm.yyyy') AND " +
             "(doc.t_TypeOper = 82 OR doc.t_TypeComplexOper = 82) AND " +
             "doc.t_KindOp <> 8 AND doc.t_KindOp <> 9 AND " +
             "doc.t_KindOp <> 14 AND doc.t_KindOp <> 15 AND " +
             "doc.t_KindOp <> 16 AND doc.t_Action <> 2 AND " +
             "doc.t_TypeOper <> 38 AND doc.t_TypeOper <> 39 AND " +
             "doc.t_Mode <> 2 and bitand(doc.t_flags,131072 ) = 0";

 cmd = RsdCommand( q );
 cmd.AddParam( "DepRefParam", RSDBP_IN, depRef );
 cmd.AddParam( "LastDaySeparatorParam", RSDBP_IN, lastDaySeparator );
 rs = RsdRecordSet( cmd );
 if ( rs.MoveNext() )
   if ( rs.value("nn") == 0 )
     vRet = TRUE;
   end;
 end;

 return vRet;
end;

// ******************************************************************
//                       Основная процедура.
// ******************************************************************

 if ( ( ALG_DEPOSITOR.UseAlternate != 0 )  OR  // Счет в альтернативном состоянии.
      ( ALG_DEPOSITOR.End_DateDep == DateNull ) )
   vVar = cNoMake;
 elif ( ALG_DEPOSITOR.Open_Date >= Date(16,2,2006) )
   vVar = cNoMake;
 elif ( NumProlongation()  AND  ProlongationOperation() )
   vVar = cNoMake;
 elif ( LastProlongation() )
   vVar = cD1;
 else
   vVar = cD2;
 end;

 ALG_RESULT = END_RES;  // Кроме этого шага выпуска доп.соглашения по НФО 293 ничего не делать.

 if   ( vVar == cNoMake )
   MsgBox( "По счету \"", ALG_DEPOSITOR.Account:f, "\" доп.соглашение не выпускается."  );
   Exit(1);
 elif ( vVar == cD1 )
   aSize( Бланки, 200 );
   ЗаполнениеМассиваБланков_Старые( Бланки/*, "..\\mac\\template\\" */);
   ii = 0;
   vRet = FALSE;
   while ( ( NOT vRet )  AND  ( Бланки(ii) != "END_OF_LIST" ) )
     if ( Index( StrUpr(Бланки(ii)), StrUpr(Trim(ALG_DEPOSITOR.Type_Account)) ) != 0 )
       vRet = TRUE;
     end;
     ii = ii + 1;
   end;
   if ( NOT vRet )
     PrintLn( "  \n\n Бланка доп.соглашения 1 по виду вкладов \"",
              ALG_DEPOSITOR.Type_Account, "\" нет." );
   end;
 else
   aSize( Бланки, 200 );
   ЗаполнениеМассиваБланков_Новые( Бланки/*, "..\\mac\\template\\" */);
   ii = 0;
   vRet = FALSE;
   while ( ( NOT vRet )  AND  ( Бланки(ii) != "END_OF_LIST" ) )
     if ( Index( StrUpr(Бланки(ii)), StrUpr(Trim(ALG_DEPOSITOR.Type_Account)) ) != 0 )
       vRet = TRUE;
     end;
     ii = ii + 1;
   end;
   if ( NOT vRet )
     PrintLn( "  \n\n Бланка доп.соглашения 2 по виду вкладов \"",
              ALG_DEPOSITOR.Type_Account, "\" нет." );
   end;
 end;

 if ( vRet )
   flagAddContract = TRUE;
   pr___dda( ALG_DEPOSITOR, ALG_SBDEPDOC, ALG_COMDEPCLNT, ALG_COMPAYER );
 end;

end;
