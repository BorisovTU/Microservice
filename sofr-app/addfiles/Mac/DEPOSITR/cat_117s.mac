/*

  R-Style SoftLab.

  RS-Retail.

  Специальные счета вкладчиков.

  Помещение кода валютной операции в основание документа.

*/

import SpecMain, rsd, deprintr, BankInter;

const  /* Коды операций корректировки по данным ПК */
OP_FCIN      = 25,  /* приход (п/к) */
OP_FCOUT     = 29,  /* расход (п/к) */
OP_OVERPAY   = 54,  /* переплата вклада */
OP_OVERPERC  = 55,  /* переплата % */
OP_UNDERPAY  = 56,  /* недоплата вклада */
OP_UNDERPERC = 57,  /* недоплата % */
OP_EDITDOC   = 60,  /* корректировка документа */
OP_GETCON    = 62,  /* простое списание */
OP_GETPOR    = 63,  /* списание по поручению (разовому) */
OP_FCGET     = 68,  /* списание (п/к) */
OP_UNDRPRFIX = 69,  /* возмещение недоплаты % */
OP_PUTCON    = 71,  /* зачисление по данным последконтроля */
OP_PUTPAC    = 73,  /* зачисление в пакетном режиме */
OP_FCPUT     = 77,  /* зачисление (п/к) */
OP_OVERPRFIX = 79,  /* возмещение переплаты % */
IN_FROM_RUS  = 32;

var NotResident: bool;
private var {BatchMode};  /* Признак выполнения в пакетном режиме */

macro IsForeign( CurrCode )
  
  if ( CurrCode == 0 )
    return false;
  end;

  var Cmd = RsdCommand( "select t_MaterialRef from dcurrency_dbt where t_code_currency = ?" );
  var Rs = RsdRecordset( Cmd );

  Cmd.AddParam( "p_currCode", RsdBp_In );
  Cmd.Value( "p_currCode" ) = CurrCode;

  Cmd.Execute;  

  if ( Rs.MoveNext )
    return Rs.Value( 0 ) == 0; 
  end;   

  return false;
end;

macro TypeAccountIsF( IsCur, Type_Account )
  
  var Cmd = RsdCommand( "select * from dsb_dtyp_dbt where t_flagCur = ? and t_kind = ? and ( t_userTypeAccount like '%Ф%' or t_userTypeAccount like '%Щ%' )" );
  var Rs = RsdRecordset( Cmd );

  Cmd.AddParam( "p_isCur", RsdBp_In );
  Cmd.Value( "p_isCur" ) = IsCur;
  Cmd.AddParam( "p_kind", RsdBp_In );
  Cmd.Value( "p_kind" ) = Type_Account;

  Cmd.Execute;  

  if ( Rs.MoveNext )
    return true; 
  end;   

  return false;
end;

macro GetAccountOwner( DepRef )
  
  var Cmd = RsdCommand( "select t_codClient from ddepositr_dbt where t_referenc = ?" );
  var Rs = RsdRecordset( Cmd );

  Cmd.AddParam( "p_reference", RsdBp_In );
  Cmd.Value( "p_reference" ) = DepRef;

  Cmd.Execute;  

  if ( Rs.MoveNext )
    return Rs.Value( 0 ); 
  end;   

  return 0;
end;

macro SetNotResident( ClientCode )

  var ClientList = TClientList;
  
  if ( ClientList.GetRecord( ClientCode ) )
    NotResident = ClientList.CurRec.Rec.NotResident;
  end;
end;

/* Для вкладного документа */
macro SetSpecCode( DepDocAddr );
  record DepDoc( "sbdepdoc.dbt" );

  var AccOwner;
  var stat = 0;

  var needCod117 = false;
  GetRegistryValue( "COMMON\\ВЕДЕНИЕ БД ВО В ПРИКЛАДНЫХ БО", V_BOOL, needCod117);

  SetBuff( DepDoc, DepDocAddr );

  if ( SubStr( Trim( DepDoc.Ground ), 1, 3 ) == "{VO" )
    return stat;
  end;
  AccOwner = GetAccountOwner( DepDoc.Referenc ); 
  NotResident = DepDoc.FlagRezid;
  SetNotResident( AccOwner );

  if ( needCod117 and
       ( StrFor(GetProgramID) != "П"     ) and
       ( DepDoc.TypeOper != OP_OVERPAY   ) and   
       ( DepDoc.TypeOper != OP_OVERPERC  ) and
       ( DepDoc.TypeOper != OP_UNDERPAY  ) and
       ( DepDoc.TypeOper != OP_UNDERPERC ) and
       ( DepDoc.TypeOper != OP_EDITDOC   ) and
       ( DepDoc.TypeOper != OP_FCOUT     ) and
       ( DepDoc.TypeOper != OP_FCGET     ) and
       ( DepDoc.TypeOper != OP_FCIN      ) and
       ( DepDoc.TypeOper != OP_FCPUT     ) and
       ( DepDoc.TypeOper != OP_UNDRPRFIX ) and
       ( DepDoc.TypeOper != OP_OVERPRFIX ) )

    if ( (not NotResident) and (IsForeign(DepDoc.IsCur)) and
         (DepDoc.TypeOper == OP_PUTCON) and (DepDoc.ApplType == IN_FROM_RUS) )
      DepDoc.Ground = "{VO61140}" + DepDoc.Ground;

    elif ( (not NotResident) and (IsForeign(DepDoc.IsCur)) and
           (DepDoc.TypeOper == OP_GETCON) and (DepDoc.ApplType == IN_FROM_RUS) )
      DepDoc.Ground = "{VO61150}" + DepDoc.Ground;

    elif ( ((DepDoc.VidDoc != 0) and (NotResident) and (not IsForeign(DepDoc.IsCur))) or
           ((DepDoc.VidDoc == 0) and (NotResident)) or
           ((DepDoc.VidDoc == 0) and (not NotResident) and (IsForeign(DepDoc.IsCur)) and (TypeAccountIsF(DepDoc.IsCur, DepDoc.Type_Account))) or
           ((NotResident) and (not IsForeign(DepDoc.IsCur)) and ( (DepDoc.Type_Account == sSpecA) or 
                                                                  (DepDoc.Type_Account == sSpecO) or 
                                                                  (DepDoc.Type_Account == sSpecC) or 
                                                                  (DepDoc.Type_Account == sSpecB1) or 
                                                                  (DepDoc.Type_Account == sSpecB2) )) )
      if ({BatchMode} and not getInterbank )
        stat = 414;
      else
        DepDoc.Ground = DoSetSpecCode( DepDoc.IsCur, DepDoc.Type_Account, DepDoc.Ground );
      end;
    end;
  end;
  
  return stat; 
end;

/* Для документа по безналичным платежам */
macro SetSpecCode_TRNDoc( TRNDoc );

  var AccOwner;
  var stat = 0;

  var needCod117 = false;
  GetRegistryValue( "COMMON\\ВЕДЕНИЕ БД ВО В ПРИКЛАДНЫХ БО", V_BOOL, needCod117);

  if ( SubStr( Trim( TRNDoc.Ground ), 1, 3 ) == "{VO" )
    return stat;
  end;
  AccOwner = GetAccountOwner( TRNDoc.Referenc ); 
  NotResident = false;
  SetNotResident( AccOwner );

  if ( needCod117  and
       ( StrFor(GetProgramID) != "П"     ) and
       ( TRNDoc.TypeOper != OP_OVERPAY   ) and   
       ( TRNDoc.TypeOper != OP_OVERPERC  ) and
       ( TRNDoc.TypeOper != OP_UNDERPAY  ) and
       ( TRNDoc.TypeOper != OP_UNDERPERC ) and
       ( TRNDoc.TypeOper != OP_EDITDOC   ) and
       ( TRNDoc.TypeOper != OP_FCOUT     ) and
       ( TRNDoc.TypeOper != OP_FCGET     ) and
       ( TRNDoc.TypeOper != OP_FCIN      ) and
       ( TRNDoc.TypeOper != OP_FCPUT     ) and
       ( TRNDoc.TypeOper != OP_UNDRPRFIX ) and
       ( TRNDoc.TypeOper != OP_OVERPRFIX ) )

    if ( (NotResident) and (TRNDoc.TypeOper == OP_PUTPAC) )
      TRNDoc.Ground = "{VO61140}" + TRNDoc.Ground;

    elif ( (NotResident) and (TRNDoc.TypeOper == OP_GETPOR) )
      TRNDoc.Ground = "{VO61140}" + TRNDoc.Ground;

    elif ( ((not NotResident) and (IsForeign(TRNDoc.IsCur)) and (TypeAccountIsF(TRNDoc.IsCur, TRNDoc.Type_Account))) or
           ((NotResident) and ((IsForeign(TRNDoc.IsCur)) or
                                  ((not IsForeign(TRNDoc.IsCur)) and ( (TRNDoc.Type_Account == sSpecA) or 
                                                                       (TRNDoc.Type_Account == sSpecO) or 
                                                                       (TRNDoc.Type_Account == sSpecC) or 
                                                                       (TRNDoc.Type_Account == sSpecB1) or 
                                                                       (TRNDoc.Type_Account == sSpecB2) )))) )
      if ({BatchMode} and not getInterbank )
        stat = 414;
      else
        TRNDoc.Ground = DoSetSpecCode( TRNDoc.IsCur, TRNDoc.Type_Account, TRNDoc.Ground );
      end;
    end;
  end;

  return stat; 
end;
