/* Отчет по оборотам по счетам контировки */

import deprintr;

file Turn       (turn    ) key 0;
var ddep = TRecHandler( "i_dp_dep.rec" );
file Curr       (currency) key 0;

var
  {curdate},
  {oper},
  IsCur = NumFlagCur,
  Branch = NumFNCash,
  SubHeadDisplayed = false,
  TotalDebetSum=$0,
  TotalCreditSum=$0,
  TotalNCDebetSum=$0,
  TotalNCCreditSum=$0,
  TotalDebetSumNB=$0,
  TotalCreditSumNB=$0,
  TotalNCDebetSumNB=$0,
  TotalNCCreditSumNB=$0;

macro FindBranch

  if (not findDepartment(Branch, null, ddep))
    ClearRecord(ddep);
    ddep.rec.Code = Branch;
    if (Branch == -1)
      ddep.rec.Name = "Все";
    else
      MsgBox("Подразделение не найдено в справочнике (код ",Branch,")");
      Exit(1);
    end;
  end;
end;

macro FindCurr(CurCode)

  Curr.Code_Currency = CurCode;
  if(not GetEQ(Curr))
    MsgBox("Валюта не найдена в справочнике (код ",CurCode,")");
    Exit(1);
  end;
end;

macro ZeroTotals

  TotalDebetSum=$0;
  TotalCreditSum=$0;
  TotalNCDebetSum=$0;
  TotalNCCreditSum=$0;
  TotalDebetSumNB=$0;
  TotalCreditSumNB=$0;
  TotalNCDebetSumNB=$0;
  TotalNCCreditSumNB=$0;
end;

macro OutHead;

  FindBranch;
  [ Филиал #                                                                                             Ф 11-м (д)]
  (ddep.rec.Name);
  [ ];
  [ ];                   
  [#################################################################################################################]                       
  (String("Приложение к операционному дневнику за " + String({curdate}:m)):c);
  [ ];
end;

macro OutSubHead;

  [+---------------------------+-----------------------------------------+-----------------------------------------+];
  [|                           |      Н а л и ч н ы е   д е н ь г и      | Б е з н а л и ч н ы е   о п е р а ц и и |];
  [|     С ч е т   О Д Б       |--------------------+--------------------|--------------------+--------------------|];
  [|                           |     Д е б е т      |     К р е д и т    |      Д е б е т     |    К р е д и т     |];
  [+---------------------------+--------------------+--------------------+--------------------+--------------------+];
end;

macro OutTotals;

  [+---------------------------+--------------------+--------------------+--------------------+--------------------+];
  [| И т о г о          Баланс | ################## | ################## | ################## | ################## |]
  (
    TotalDebetSum,
    TotalCreditSum,
    TotalNCDebetSum,
    TotalNCCreditSum
  );
  [|                 Внебаланс | ################## | ################## | ################## | ################## |]
  (
    TotalDebetSumNB,
    TotalCreditSumNB,
    TotalNCDebetSumNB,
    TotalNCCreditSumNB
  );
  [+---------------------------+-----------------------------------------+-----------------------------------------+];
  [|                           |               Б а л а н с               |            В н е б а л а н с            |];
  [|       В с е г о           |--------------------+--------------------|--------------------+--------------------|];
  [|                           |     Д е б е т      |     К р е д и т    |      Д е б е т     |    К р е д и т     |];
  [+---------------------------+--------------------+--------------------+--------------------+--------------------|];
  [|                           | ################## | ################## | ################## | ################## |]
  (
    TotalDebetSum + TotalNCDebetSum,
    TotalCreditSum + TotalNCCreditSum,
    TotalDebetSumNB + TotalNCDebetSumNB,
    TotalCreditSumNB + TotalNCCreditSumNB
  );
  [+---------------------------+--------------------+--------------------+--------------------+--------------------+];
end;

macro OutLine;

  [| ######################### | ################## | ################## | ################## | ################## |]
  (
    Acc_Form(Turn.Account),
    Turn.DebetSum,
    Turn.CreditSum,
    Turn.NCDebetSum,
    Turn.NCCreditSum
  );
  if(Turn.IsNonBal == StrFor(0))
    TotalDebetSum=TotalDebetSum+Turn.DebetSum;
    TotalCreditSum=TotalCreditSum+Turn.CreditSum;
    TotalNCDebetSum=TotalNCDebetSum+Turn.NCDebetSum;
    TotalNCCreditSum=TotalNCCreditSum+Turn.NCCreditSum;
  else
    TotalDebetSumNB=TotalDebetSumNB+Turn.DebetSum;
    TotalCreditSumNB=TotalCreditSumNB+Turn.CreditSum;
    TotalNCDebetSumNB=TotalNCDebetSumNB+Turn.NCDebetSum;
    TotalNCCreditSumNB=TotalNCCreditSumNB+Turn.NCCreditSum;
  end;
end;

macro GenerateReport;

  var
    PrevCurCode = 0,
    RecFound;

  OutHead;
  SubHeadDisplayed = false;
  ClearRecord(Turn);
  Turn.Oper={oper};
  RecFound=GetGE(Turn);
  while(RecFound and(Turn.Oper=={oper}))
    if(IsCur and (Turn.CurCode != PrevCurCode))
      if(SubHeadDisplayed)
        OutTotals;
      end;   
      PrevCurCode = Turn.CurCode;
      SubHeadDisplayed = false;
    end;
    if(not SubHeadDisplayed)
      if(IsCur)
        FindCurr(Turn.CurCode);
        [ ];
        [#](Curr.Name_Currency);
        [#](MkStr("=", StrLen(Curr.Name_Currency)));
        [ ];
      end;
      ZeroTotals;
      OutSubHead;  
      SubHeadDisplayed = true;
    end;
    OutLine;
    RecFound=Next(Turn);
  end;
  if(SubHeadDisplayed)
    OutTotals;
  end;
end;
