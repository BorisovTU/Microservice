/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады и Прочие операции                                                *
*                                                                          *
*  Список счетов по вкладам бывшего Чеченского СБ, подлежащих              *
*  восстановлению                                                          *
*                                                                          *
*  Лебедев С.В.                                                            *
*  24.02.2004                                                              *
\**************************************************************************/

import DeprIntr, RSD, sqlconv;

/*private var listfdep = TBFile( "listfdep.dbt", "r", 0 );*/

private var {curdate};
private var Branch    = NumFNCash( );
private var MinBranch = Branch;
private var MaxBranch = Branch;
private var NumRec    = 0;

/* Панель и поля панели дополнительной информации по внешним налоговым платежам */
record CHCN_LAC ( "CHCN_LAC" ) dialog;
/* Номер полей диалога */
const ne_NameBranch = 0,
      ne_DateReport = 1;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro HeadReport

 [                                                                    СПИСОК];
 [             счетов по вкладам, внесенным в бывший Чеченский банк Сбербанка России, которые подлежат восстановлению и обеспечению];
 [                 сохранности их ценности в связи с Федеральным законом от 10 мая 1995 года № 73-ФЗ "О восстановлении и защите];
 [                                                   сбережений граждан Российской Федерации"];
 [ ];    
 [ ---------------------------------------------------------------------------------------------------------------------------------------------];
 [ | №п/п |Номер подр|Номер счета по|Фамилия, Имя и        |Год       |Остаток вклада|        Примечание (при выплате сумм компенсации)        |];
 [ |      |в ЧСБ     |вкладу        |Отчество вкладчика    |рождения  |на 20.06.91   |----------------------------------------------------------];
 [ |      |          |              |                      |          |              |Предварительная|Дата выплаты|Дополнительная |Дата выплаты|];
 [ |      |          |              |                      |          |              |компенсация    |по графе 7  |предварительная|по графе 9  |];
 [ ---------------------------------------------------------------------------------------------------------------------------------------------];
 [ |  1   |    2     |      3       |          4           |    5     |      6       |       7       |     8      |       9       |     10     |];
 [ ---------------------------------------------------------------------------------------------------------------------------------------------];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro BottomReport

 [ ---------------------------------------------------------------------------------------------------------------------------------------------];
 [ ];
 [     ______________________               __________________________];
 [      (подпись контролера)                     (ФИО контролера)];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro WorkWithOneChcnPay( Rs:RsdRecordSet )

  array aRgstName;

  var ChcnDepart   = rslString( Rs.Value( "ChcnDepart" ) );
  var ChcnBranch   = rslString( Rs.Value( "ChcnBranch" ) );
  var RgstAccount  = rslString( Rs.Value( "RgstAccount" ) );
  var RgstName1    = rslString( Rs.Value( "RgstName1" ) );
  var RgstName2    = rslString( Rs.Value( "RgstName2" ) );
  var RgstName3    = rslString( Rs.Value( "RgstName3" ) );
  var RgstBorn     = rslDate( Rs.Value( "RgstBorn" ) );
  var Rest200691   = Rs.Value( "Rest200691" );
  var DatePay      = rslDate( Rs.Value( "DatePay" ) );
  var PartCompens1 = Rs.Value( "PartCompens1" );
  var PartCompens2 = Rs.Value( "PartCompens2" );
  var RgstName     = "";

  if ( RgstName1 != "" )
    RgstName = RgstName1;
    if ( RgstName2 != "" )
      RgstName = RgstName + " " + RgstName1;
      if ( RgstName3 != "" )
        RgstName = RgstName + " " + RgstName3;
      end;
    end;
  end;

  StrSplit( RgstName, aRgstName, 22, 22, 1 );

  [ |##### | #########|##############|######################|##########|##############|###############| ########## |###############| ########## |]
  ( NumRec:5, ChcnDepart + "/" + ChcnBranch, Acc_Form(RgstAccount),
    aRgstName( 0 ), RgstBorn:f, 
    Rest200691:14:2:a, 
    PartCompens1:15:2:a, DatePay:f, 
    PartCompens2:15:2:a, DatePay:f                       );

  if ( ASize( aRgstName ) > 1 )
    [ |      |          |              |######################|          |              |               |            |               |            |]
    ( aRgstName( 1 ) );
  end;

  if ( ASize( aRgstName ) > 2 )
    [ |      |          |              |######################|          |              |               |            |               |            |]
    ( aRgstName( 2 ) );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro WorkWithChcnPay

  var Rs;
  var Cmd = RsdCommand( "select chcnrgst.t_ChcnDepart   ChcnDepart,"
                               "chcnrgst.t_ChcnBranch   ChcnBranch,"
                               "chcnrgst.t_RgstAccount  RgstAccount,"
                               "chcnrgst.t_RgstName1    RgstName1,"
                               "chcnrgst.t_RgstName2    RgstName2,"
                               "chcnrgst.t_RgstName3    RgstName3,"
                               "chcnrgst.t_RgstBorn     RgstBorn,"
                               "chcnrgst.t_Rest200691   Rest200691,"

                               "chcn_pay.t_DatePay      DatePay,"
                               "chcn_pay.t_PartCompens1 PartCompens1,"
                               "chcn_pay.t_PartCompens2 PartCompens2"

                        " from dchcnrgst_dbt chcnrgst,"
                              "dchcn_pay_dbt chcn_pay"

                        " where chcnrgst.t_RgstID = chcn_pay.t_RgstID"
                          " and chcnrgst.t_RgtpID = chcn_pay.t_RgtpID"
                        
                        " and chcn_pay.t_DatePay <= ?"
                        " and (chcn_pay.t_Branch BETWEEN ? and ?)"
                        
                        " order by chcnrgst.t_chcnDepart, chcnrgst.t_chcnBranch" );

  Cmd.AddParam( "MaxDate",   RSDBP_IN, CHCN_LAC.DateReport );
  Cmd.AddParam( "MinBranch", RSDBP_IN, MinBranch );
  Cmd.AddParam( "MaxBranch", RSDBP_IN, MaxBranch );

  Rs = RsdRecordSet( Cmd );

  while ( Rs.MoveNext )
    NumRec = NumRec + 1;
    Message( " Обрабатывается оплата номер " + String( NumRec ) );

    WorkWithOneChcnPay( Rs );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DoReport

  HeadReport( );

  WorkWithChcnPay( );

  BottomReport( );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetInfoFieldsCHCN_LAC( numField )

  var name;
  // ---------------------------------------------------------------------
  if ( ( numField == ne_NameBranch ) or ( numField == -1 ) )
    if ( Branch <= 0 )
      CHCN_LAC.NameBranch = "Все";
    else
      if (not findDepartment(Branch, name))
        name = String(Branch);
      end;
      CHCN_LAC.NameBranch = name;
    end;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ListFieldsCHCN_LAC( numField )

  array aChoice;
  array aNameMenu;

  var stat;
  var i;
  var dpDepList = TdpDepList;
  var filialID;

  // ---------------------------------------------------------------------
  if ( numField == ne_NameBranch )
    filialID = dpDepList.getFilialID(NumFNCash());
    dpDepList.SetFilter(filialID);
    while ( dpDepList.next() )
      i = ASize( aNameMenu );
      aChoice( i ) = dpDepList.rec.Code;
/*      aNameMenu( i ) = " " + SubStr( listfdep.rec.NameBranch + MkStr( " ", 12 ), 1, 12 ) + " " + SubStr( listfdep.rec.Name + MkStr( " ", 36 ), 1, 36 );*/
      aNameMenu( i ) = " " + SubStr( dpDepList.rec.Name + MkStr( " ", 12 ), 1, 12 ) + " " + SubStr( MkStr( " ", 36 ), 1, 36 );
    end;

    if ( ASize( aNameMenu ) > 0 )
      i = Menu( aNameMenu, NULL, "Подразделения банка", 14, 8 );
      if ( i >= 0 )
        Branch = aChoice( i );
      end;
    end;
   
    GetInfoFieldsCHCN_LAC( numField );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro WorkDialogCHCN_LAC( IP, cmd, id, key )

  var stat = CM_DEFAULT;
  var flag = true;
  var i;

  // =====================================================================
  if ( cmd == DLG_KEY )
    // ENTER -----------------------------------------------------------
    if ( key == 13 )
      ;
    // F3 --------------------------------------------------------------
    elif ( key == 317 )
      ListFieldsCHCN_LAC( id );
      UpdateFields( IP );
    // ^F3 -------------------------------------------------------------
    elif ( key == 352 )
      if ( id == ne_DateReport )
        CHCN_LAC.DateReport = {curdate};
        UpdateFields( IP );
      end;
    // F7 --------------------------------------------------------------
    elif ( key == 321 )            
      stat = CM_SAVE;
    // Пробел ----------------------------------------------------------
    elif ( key == 32 )
      if ( id == ne_NameBranch )
        Branch = -1;
        GetInfoFieldsCHCN_LAC( id );
        UpdateFields( IP );
      end;
    // ESC -------------------------------------------------------------
    elif ( key == 27 )
      stat = CM_CANCEL;
    end;
  // =====================================================================
  elif ( cmd == DLG_SAVE )
    if ( Branch < 0 )
      MinBranch = 0;
      MaxBranch = 100;
    else
      MinBranch = MaxBranch = Branch;
    end;
  // =====================================================================
  elif ( cmd == DLG_REMFOCUS )
    ;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  ClearRecord( CHCN_LAC );
  CHCN_LAC.DateReport = {curdate};
  GetInfoFieldsCHCN_LAC( -1 );

  if ( RunDialog( CHCN_LAC, "WorkDialogCHCN_LAC" ) )
    DoReport( );
  else
    Exit( 1 );
  end;

end;
