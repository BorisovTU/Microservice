import deprintr;
  const
       Mode         = GetCurrentMode,
       usedCurrency = NumFlagCur(),
       today ={curdate};

/***********************************************************************/
/*              Параметры, настраиваемые ВАМИ:                         */
/***********************************************************************/
 const
        ПЕЧАТЬ_СТРАНИЦАМИ        = True,  /* True -  вывод разбивается */
                                          /*         на страницы.      */
        СТРОК_В_СТРАНИЦЕ         =  70,   /* А это - размер страницы.  */

        ПЕЧАТЬ_НОМЕРА_СТРАНИЦЫ   = True,  /* Варианты - True/False     */
        ПЕЧАТЬ_НАЗВАНИЯ_СТРАНИЦЫ = True;  /* Варианты - True/False     */

/***********************************************************************/
/*-----------------------------------------------------------------------

                                             24 янв. 1997, Клепиков С.А.
 Описание:  - Распечатка отчета по статистике по типу вклада на заданную
              дату и признаку валютности.

-----------------------------------------------------------------------*/
/***************Внешние данные   ***************************************/

/***************Глобальные данные***************************************/
                                                 /* Используемые файлы */
 file СтатистикаПоТипуВклада ("sb_depst.dbt") key 0;
 file СправочникТиповВкладов ("sb_dtyp.dbt")  key 2;
 file СправочникВалют        ("currency.dbt") key 0;

 var                                            /* Переменные */
    linesPerPage,
    linesPerPageCount,
    pageNamePosition,
    pageNumberPosition,
    pageNumber;

 const                                           /* термины */
                                                 /* Описание бланка */
    PROCESS_STATISTICS  = "Обрабатываем статистику ...",

    PAGE_HEADER_TEXT    = "отчет по статистике по типу вклада",
    PAGE_HEADER_POSITION=  2,
    PAGE_NUMBER_POSITION= 70,
    PAGE_NUMBER_PREFIX  = "стр.";
                                                /* макет вывода */
const
    headerLine01     = "\n",
    headerLine02     = "       Статистика по типу вклада на SD",
    headerLine03     = "\n",
    headerLine04     = "┌──────┬────────────────────────┬──────────┬───────┬───────┬───────────────┐",
    headerLine05     = "│h2    │ Тип вклада             │ всего    │кол-во │кол-во │Сумма на конец │",
    headerLine06     = "│      │                        │ операций │откры- │закры- │дня            │",
    headerLine07     = "│      │                        │ с начала │тых    │тых    │               │",
    headerLine08     = "│      │                        │ квартала │счетов │счетов │               │",
    headerLine09     = "├──────┼────────────────────────┼──────────┼───────┼───────┼───────────────┤",
    lineTemplate     = "│ i1   │ i2                     │ i3       │ i4    │ i5    │ i6            │",
    footerLine01     = "│      │                        │          │       │       │               │",
    footerLine02     = "└──────┴────────────────────────┴──────────┴───────┴───────┴───────────────┘";

/**********************************************************************/
MACRO InsertHeader()

 var
    tmp;

 tmp = MkStr(" ",StrLen(lineTemplate));
 println(tmp);
 if(
       ПЕЧАТЬ_НОМЕРА_СТРАНИЦЫ
   AND ((PAGE_HEADER_POSITION + StrLen(PAGE_HEADER_TEXT)) < StrLen(tmp))
   )
   StrSet(tmp,PAGE_HEADER_POSITION,PAGE_HEADER_TEXT);
 end;
 if(
       ПЕЧАТЬ_НАЗВАНИЯ_СТРАНИЦЫ
   AND ((PAGE_NUMBER_POSITION+StrLen(PAGE_NUMBER_PREFIX+5)) < StrLen(tmp))
   )
   StrSet(tmp,PAGE_NUMBER_POSITION,PAGE_NUMBER_PREFIX);
   StrSet(tmp,PAGE_NUMBER_POSITION+StrLen(PAGE_NUMBER_PREFIX),
                                                       String(pageNumber));
 end;
 println(tmp);
 tmp = MkStr(" ",StrLen(lineTemplate));
 println(tmp);
 linesPerPageCount = linesPerPageCount + 3;

END;
/**********************************************************************/
MACRO PrintLine(text)

 if(                       /* подпечатка заголовка и номера страницы */
        ПЕЧАТЬ_СТРАНИЦАМИ
   AND (ПЕЧАТЬ_НОМЕРА_СТРАНИЦЫ OR ПЕЧАТЬ_НАЗВАНИЯ_СТРАНИЦЫ)
   AND (pageNumber == 1)
   AND (linesPerPageCount == 0)
   )
   InsertHeader();
 end;
 if(                                       /* перевод страницы */
         ПЕЧАТЬ_СТРАНИЦАМИ
    AND (linesPerPageCount >= linesPerPage)
   )
   linesPerPageCount=0;
   pageNumber = pageNumber + 1;
   print("\x0C");                          /* переведем страницу */
   if(                       /* подпечатка заголовка и номера страницы */
          ПЕЧАТЬ_СТРАНИЦАМИ
     AND (ПЕЧАТЬ_НОМЕРА_СТРАНИЦЫ OR ПЕЧАТЬ_НАЗВАНИЯ_СТРАНИЦЫ)
     )
     InsertHeader();
   end;
 end;
 linesPerPageCount = linesPerPageCount + 1;
 println(text);

END;
/**********************************************************************/
MACRO PrintHeader(selectedDate,currencyClass)

 var
    tmp,
    tmp1,
    tmp2;

 if(currencyClass == 0) tmp1 = " - рубли";  tmp2 = "      ";
 else                   tmp1 = " - валюта"; tmp2 = "Валюта";
 end;

 printLine(headerLine01);
 tmp = headerLine02;
 StrSet(tmp,Index(tmp,"SD"),String(selectedDate:m) + tmp1);
 printLine(tmp);
 printLine(headerLine03);
 printLine(headerLine04);
 tmp =  headerLine05;
 StrSet(tmp,Index(tmp,"h2"), tmp2);
 printLine(tmp);
 printLine(headerLine06);
 printLine(headerLine07);
 printLine(headerLine08);
 printLine(headerLine09);

END;
/**********************************************************************/
MACRO PrintFooter()

 printLine(footerLine01);
 printLine(footerLine02);

END;
/**********************************************************************/
MACRO PrintSingleLine(sta,aty,cur)

 var
    currencyName,
    operationName,
    tmp1,
    tmp;

 tmp = lineTemplate;
 if(sta.IsCur > 0)
   cur.Code_Currency = sta.CodCur;
   if(getEQ(cur)) currencyName = cur.Short_Name;
   else           currencyName = "???";
   end;
 else             currencyName = "  ";
 end;
 StrSet(tmp,Index(tmp,"i1"),currencyName);

 aty.FlagCur  = sta.IsCur;
 aty.Kind     = sta.Kind;
 if(getEQ(aty)) tmp1 = aty.Name;
 else           tmp1 = "не найден в справочнике";
 end;
 if(StrLen(tmp1) > 22) operationName = SubStr(tmp1,1,22);
 else                  operationName = tmp1;
 end;
 StrSet(tmp,Index(tmp,"i2"),operationName);

 StrSet(tmp,Index(tmp,"i3"),String(sta.ColOper:8:8));
 StrSet(tmp,Index(tmp,"i4"),String(sta.ColOpenedAcc:5:5));
 StrSet(tmp,Index(tmp,"i5"),String(sta.ColClosedAcc:5:5));
 StrSet(tmp,Index(tmp,"i6"),String(sta.Rest:14:14:a));
 printLine(tmp);

END;
/**************************************************************************/
MACRO              getFirstRecord(requestedDate,currencyClass,sta)

                   Rewind(sta);
                   while( next(sta))
                     if(   (sta.Date  == requestedDate)
                        AND (sta.Mode == Mode)
                        AND (sta.IsCur == currencyClass)
                       )
                       return True;
                     end;
                   end;

                   MsgBox(
                           "Чтобы получить статистику,|"
                         + "необходимо создать сводные документы.|"
                         + "В данном случае этого не сделано.|"
                         );
                   return False;
END;
/**************************************************************************/
MACRO getNextRecord(requestedDate,currencyClass,sta)

 while( next(sta))
    if(   (sta.Date  == requestedDate)
      AND (sta.Mode == Mode)
      AND (sta.IsCur == currencyClass)
      )
      return True;
    end;
 end;
 return False;

END;
/**********************************************************************/
MACRO initAll()

 linesPerPage            = СТРОК_В_СТРАНИЦЕ;
 linesPerPageCount       = 0;
 pageNamePosition        = PAGE_HEADER_POSITION;
 pageNumberPosition      = PAGE_NUMBER_POSITION;
 pageNumber = 1;

END;
/**************************************************************************/
MACRO printStatistics(requestedDate,currencyClass,sta,aty,cur)

 var
    numberOfRecords,
    ordersCount = 0,
    staNumber   = 1,
    thereIsRecord;

 initAll();
 printHeader(requestedDate,currencyClass);
 thereIsRecord = getFirstRecord(requestedDate,currencyClass,sta);
 initProgress(Nrecords(sta),PROCESS_STATISTICS,PROCESS_STATISTICS);
 while( thereIsRecord )
    UseProgress(staNumber);
    printSingleLine(sta,aty,cur);
    thereIsRecord = getNextRecord(requestedDate,currencyClass,sta);
 end;
 remProgress();
 printFooter();

END;
/**************************************************************************/
MACRO              p___stat (requestedDate,currencyClass)
                   printStatistics(
                                  requestedDate,
                                  currencyClass,
                                  СтатистикаПоТипуВклада,
                                  СправочникТиповВкладов,
                                  СправочникВалют
                                  );
END;
/**************************************************************************/
MACRO Main()

 LogProcStartEnd(0,"Инициализирована процедура формирования статистики");
 p___stat (today,usedCurrency);
 LogProcStartEnd(1,"Завершена процедура формирования статистики");

END;
/**************************************************************************/
Main();
/**************************************************************************/
