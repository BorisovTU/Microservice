/*
               **************************
               *  R-Style SoftWare Lab. *
               **************************

    RS-Retail 2.0
    Сбербанк
 Вклад "Компенсационный-2"

 Открытие счета
------------------------------------------------------------
 Цель:
  - проверить правомерность открытия счета
  - в случае наличного открытия отметить на счете, по которому
    прошла компенсация, что некоторая сумма положена на
    Компенсационный (во избежание дублирования)
------------------------------------------------------------

  Ромодин Александр Васильевич
  05.06.2000
*/
import deprintr;
/*---------- Инициализируемые структуры ------------------*/
record ALG_DEPOSITOR ("depositr.dbt");
record ALG_SBDEPDOC  ("sbdepdoc.dbt");
record ALG_PARAM     ("sb_algop.dbt");

file   DEPOSITOR     ("depositr.dbt");
file   AUXINFO       ("auxinfo.dbt") write;
file   COMPENS       ("compens.dbt") write;
record AUXINFO_2     ("auxinfo.2");    /* Доп.информация по компенсациям */
record doc           ("sbdepdoc.dbt");
record ALG_OPERPRM   ("oper_prm.1");
/*---------- Коды возврата макроса -----------------------*/
const  OK_RES   =  0,  /* продолжать проводку */
       SKIP_RES =  1,  /* пропустить выполнение предопределенного модуля */
       END_RES  =  2,  /* завершить проводку */
       ERR_RES  = -1;  /* аваpийно завершить проводку */
/*---------- Проверяемые операции ------------------------*/
const  OP_OPENCAS  =  1, /* Открытие наличными */
       OP_OPENNCAS =  2, /* Открытие безналичными */
       At_Compens  = 10; /* Зачислением компенсации */
/*---------- Глобальные переменные -----------------------*/
var FNCash = NumFNCash(); /* Номер филиала */
var ReferencAccountCompensation = "", /* Счет, с которого компенсацию сняли налично */
    AccountCompensation = "";
/*--------------------------------------------------------*/
  ALG_RESULT = OK_RES;   /* Мы- оптимисты*/

/***********************************************************
  Поиск счета по референсу
***********************************************************/

macro FindAcc (Ref)
  KeyNum (DEPOSITOR,8);
  DEPOSITOR.Referenc = Ref;
  return GetEQ(DEPOSITOR);
end;

macro FindAccByNum (Account)
  KeyNum (DEPOSITOR,7);
  DEPOSITOR.FNCash  = FNCash;
  DEPOSITOR.Account = Account;
  return GetEQ (DEPOSITOR);
end;
/***********************************************************
  Поиск записи в AuxInfo
***********************************************************/

Macro FindAuxInfo (Ref)
  KeyNum (AUXINFO,0);
  AUXINFO.Reference = Ref;
  AUXINFO.InfoType = 3;
  AUXINFO.Date     = Date(0,0,0);
  return GetEQ(AUXINFO);
end;
/***********************************************************
***********************************************************/

macro MakeAuxInfo
/*
  Правка записи по компенсации для счета,
  по которому компенсация была начислена
*/
  var stat = True;
  stat = FindAuxInfo (ReferencAccountCompensation);
  if (stat)
    SetRecordAddr(AUXINFO_2,AUXINFO,0,0,TRUE);
    AUXINFO_2.Include     = AUXINFO_2.Include     + ALG_SBDEPDOC.InSum;
    AUXINFO_2.CashInclude = AUXINFO_2.CashInclude + ALG_SBDEPDOC.InSum;
    stat = Update(AUXINFO);
  end;
  return stat;
end;
/**********************************************************/

macro MakeCompensFile
/*
  Занесение записи в файл реестра компенсаций для
  организации отката открытия счета
*/
  var stat = True;
  var LastAutoKey = 0;

  KeyNum (COMPENS,0);
  ClearRecord (COMPENS);
  COMPENS.FNCash  = FNCash;
  COMPENS.AutoKey = 2000000000;
  if (GetLE(COMPENS) AND (COMPENS.FNCash == FNCash))
    LastAutoKey = COMPENS.AutoKey;
  end;
  ClearRecord (COMPENS);
  COMPENS.AutoKey      = LastAutoKey + 1;
  COMPENS.Account      = AccountCompensation; /* Счет, с которого компенсацию сняли налично */
  COMPENS.DestAccount  = ALG_DEPOSITOR.Account; /* Открываемый счет */
  COMPENS.Date         = ALG_SBDEPDOC.Date_Document;
  COMPENS.FNCash       = FNCash;
  COMPENS.Kind         = 999;    /* Тип записи */
  COMPENS.Sum          = ALG_SBDEPDOC.InSum;
  COMPENS.Type_Account = ALG_DEPOSITOR.Type_Account;
  COMPENS.CodClient    = ALG_DEPOSITOR.CodClient;
  stat = Insert (COMPENS);
  if (NOT stat)
    MsgBox ("Ошибка при записи в реестр компенсаций");
  end;
  return stat;
end;
/**********************************************************/

macro TrnProc
/*
  Транзакция открытия счета
*/
  var stat = True;
  var st;
/*
  Вызов операции открытия счета
*/
  if (stat)
    ClearRecord (doc);
    doc.IsCur            = ALG_DEPOSITOR.IsCur;
    doc.FNCash           = FNCash;
    doc.Account          = ALG_DEPOSITOR.Account;
    doc.Oper             = ALG_SBDEPDOC.Oper;
    doc.Type_Account     = ALG_DEPOSITOR.Type_Account;
    doc.Code_Currency    = ALG_DEPOSITOR.Code_Currency;
    doc.CodClient        = ALG_DEPOSITOR.CodClient;
    doc.YesSbook         = "X";
    doc.Date_Document    = ALG_SBDEPDOC.Date_Document;
    doc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
    doc.Author           = 0;
    doc.iApplicationKind = 1;
    doc.InSum            = ALG_SBDEPDOC.InSum;
    doc.TypeComplexOper  = ALG_SBDEPDOC.TypeComplexOper;
    doc.ApplType         = ALG_SBDEPDOC.ApplType;

    ClearRecord(ALG_OPERPRM);
    ALG_OPERPRM.Account         = ALG_DEPOSITOR.Account;
    ALG_OPERPRM.Type_Account    = ALG_DEPOSITOR.Type_Account;
    ALG_OPERPRM.Code_Currency   = ALG_DEPOSITOR.Code_Currency;
    ALG_OPERPRM.Type_Oper       = OP_OPENCAS;
    ALG_OPERPRM.Show_Panel      = 0;
    ALG_OPERPRM.UseMaxDate      = 0;
    ALG_OPERPRM.NoPrint         = 1;
    ALG_OPERPRM.TypeComplexOper = OP_OPENCAS;
    st = Выполнение_Операции(ALG_OPERPRM,doc);
    if (st != 0)
      MsgBox ("Ошибка ",st," при выполнении операции открытия счета");
      stat = False;
    end;
  end;
/*
  Правка AuxInfo
*/
  if (stat)
    stat = MakeAuxInfo ();
  end;
/*
  Добавление записи в реестр компенсаций
  (введено для отката операции наличного открытия счета)
*/
  if (stat)
    stat = MakeCompensFile ();
  end;
/*
  Не дай-то Бог!
*/
  if (NOT stat)
    AbortTrn ();
  end;
end;

/*2222222222222*********************************************
**2222222222222********************************************/

macro TestAccountCompensation
/*
  Проверка счета, с которого компенсация была снята наличными
*/
  var stat = True;
  var LimitSum;
  var TestSum = ALG_SBDEPDOC.InSum;
  stat = FindAuxInfo (ReferencAccountCompensation);
  SetRecordAddr(AUXINFO_2,AUXINFO,0,0,TRUE);
  if (NOT stat)
    MsgBox ("По выбранному счету ",Acc_Form(DEPOSITOR.Account)," не было компенсаций");
  else
    if (AUXINFO_2.CashDebet == 0)
      stat = False;
      MsgBox ("По выбранному счету ",Acc_Form(DEPOSITOR.Account),"|не было наличной выдачи компенсации");
    end;
  end;
  if (stat)
    LimitSum = AUXINFO_2.CashDebet - AUXINFO_2.CashInclude;
    if (TestSum > LimitSum)
      stat = False;
      MsgBox ("Сумма операции ",TestSum," превосходит сумму компенсации|",LimitSum,", которая не внесена на другие счета");
    end;
  end;
  return stat;
end;
/**********************************************************/

macro SelectAccountCompensation
/*
  Выбор счета, с которого компенсация была снята наличными
*/
  var stat = True;
  var DestAccType = "";
  var DestAcc;
  var DestAccRef  = "";
/*
  Ввод счета
*/
  MsgBox ("Выберите вид вклада и счет,|с которого компенсация снята наличными");
  if (NOT(SelectDepType(DestAccType)))
    stat = False;
  end;
  if (stat)
    GetString (DestAcc,"Введите номер счета, с которого снята компенсация",25);
    if (ValType(DestAcc) == V_UNDEF)
      if (NOT SelectAccount(DestAccType,0,ALG_SBDEPDOC.CodClient,DestAccRef,2))
        stat = False;
      end;
    elif (DestAcc == "")
      stat = False;
    end;
  end;
  if (NOT stat)
    MsgBox ("Вы отказались от ввода счета, с которого снята компенсация");
  end;
/*
  Поиск счета
*/
  if (stat)
    if (DestAccRef != "")
      stat = FindAcc (DestAccRef);
      if (NOT stat)
        MsgBox ("Выбранный счет не найден");
      end;
    else
      stat = FindAccByNum (DestAcc);
      if (NOT stat)
        MsgBox("Введенный счет не найден");
      end;
    end;
  end;
  if (stat AND (DEPOSITOR.CodClient != ALG_DEPOSITOR.CodClient))
    stat = False;
    MsgBox ("Вы ввели счет другого вкладчика");
  end;
  if (stat)
    ReferencAccountCompensation = DEPOSITOR.Referenc;
    AccountCompensation         = DEPOSITOR.Account;
  end;
  return stat;
end;
/*1111111111111*********************************************
**1111111111111********************************************/

macro MakeOpenCash
/*
  Обработка наличного открытия
*/
  var stat = True;
/*
  1.1 Запрос счета, с которого была снята компенсация
*/
  if (stat)
    stat = SelectAccountCompensation ();
  end;
/*
  1.2 Проверка счета, с которого снята компенсация
*/
  if (stat)
    stat = TestAccountCompensation ();
  end;
/*
  1.3 Транзакция открытия счета
*/
  if (stat)
    stat = ProcessTrn(1,"TrnProc",AUXINFO);
  end;
  return stat;
end;
/***********************************************************
  Точка входа
*/
  if (ALG_SBDEPDOC.ApplType != At_Compens)
    ALG_RESULT = ERR_RES;
    MsgBox ("Разрешено открывать только зачислением компенсации");
  elif (ALG_SBDEPDOC.TypeOper == OP_OPENCAS)
    if (MakeOpenCash())
      ALG_RESULT = END_RES;
    else
      ALG_RESULT = ERR_RES;
    end;
  end;
end;
/* ===== Конец макроса ===== */
