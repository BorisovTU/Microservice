/*
$Name:             DI_RELEX_1420.mac
$Module:           RS-Retail
$Description:      Страхование частных вкладов.  Выписка из Реестра обязательств.
*/

  import DeprIntr, DI_Def, DI_PapTp, commclnt, RSD;

  private file dpe ( depiprev ) key 1;

  private var eDepClnt = TClientList; /* Справочник вкладчиков */

  private var rAddr = TRecHandler( "i_addr.rec" );
  private var tCountry  = TBFile( "country.dbt" , "R", 2, null, "bank.def" );

  private var obj_code  = TBFile ( "objcode.dbt",  "r", 0 );

  /* Глобализмы. */
  /*var {Name_Bank};*/
  /*var {Post_Addr};*/

  /* Атрибуты клиента. */
  private var veFIO;
  private var veAddress;
  private var vePhone;
  private var vePapers;
  private var vePaperCode;
  private var veSNILS;

/********************************************************************/
/*          Представление строчного формата даты ДД.ММ.ГГГГ         */
/********************************************************************/
private macro DateStrDD( pDate )
  var sDate = String( pDate );
  if ( SubStr( sDate, 1, 1 ) == " " )
    sDate = "0" + Trim( sDate );
  end;
  return sDate;
end;

/**************************************************************************\
|                        Адрес субъекта по 4120-У.                        |
\**************************************************************************/

private macro strSection( _code, _name )
  if ( ( ValType( _name ) == V_STRING )  AND  ( StrLen( _name ) > 0 ) )
    return _code + " " + _name + ", ";
  else
    return "";
  end;
end;

private macro strHouse( _code, _name )
  if ( ( ValType( _name ) == V_STRING )  AND  ( StrLen( _name ) > 0 ) )
    return ", " + _code + " " + _name;
  else
    return "";
  end;
end;

private macro strFlat( _name )
  if ( ( ValType( _name ) == V_STRING )  AND  ( StrLen( _name ) > 0 ) )
    return ", кв. " + _name;
  else
    return "";
  end;
end;

private macro addrOurBankRUS_Fields()
  var vPlace = FALSE;
  if   ( ( ValType( rAddr.rec.Region ) == V_STRING )  AND  ( StrLen( rAddr.rec.Region ) > 0 ) )
    vPlace = TRUE;
  elif ( ( ValType( rAddr.rec.Place ) == V_STRING )  AND  ( StrLen( rAddr.rec.Place ) > 0 ) )
    vPlace = TRUE;
  elif ( ( ValType( rAddr.rec.District ) == V_STRING )  AND  ( StrLen( rAddr.rec.District ) > 0 ) )
    vPlace = TRUE;
  elif ( ( ValType( rAddr.rec.Province ) == V_STRING )  AND  ( StrLen( rAddr.rec.Province ) > 0 ) )
    vPlace = TRUE;
  end;
  if ( vPlace )
    return rAddr.rec.Country + ", " + 
           rAddr.rec.PostIndex + ", " + 
           strSection( rAddr.rec.Region, rAddr.rec.CodeRegion ) +
           strSection( rAddr.rec.Province, rAddr.rec.lCodeProvince ) +
           strSection( rAddr.rec.lCodeDistrict, rAddr.rec.District ) +
           strSection( rAddr.rec.lCodePlace, rAddr.rec.Place ) +
           rAddr.rec.lCodeStreet + " " + 
           rAddr.rec.StreetName +
           strHouse( "д.", rAddr.rec.House ) +
           strHouse( "корп.", rAddr.rec.NumCorp ) +
           strFlat( rAddr.rec.Flat );
  else
    return rAddr.rec.Country + ", " + 
           rAddr.rec.Address;
  end;
end;

private macro addrRUS( _id )
  var vFind = FALSE;
  var strAddr = "";
  var retAddr = "";
  if ( Addr_Str( _id, I_PTADDR_POST, strAddr )  AND  ( StrLen( strAddr ) > 0 ) )
    vFind = TRUE;
  else
    if ( Addr_Str( _id, I_PTADDR_REAL, strAddr )  AND  ( StrLen( strAddr ) > 0 ) )
      vFind = TRUE;
    else
      if ( Addr_Str( _id, I_PTADDR_LEGAL, strAddr )  AND  ( StrLen( strAddr ) > 0 ) )
        vFind = TRUE;
      end;
    end;
  end;
  if ( vFind )
    retAddr = rAddr.rec.Country + ", " + strAddr;
  else
    retAddr = addrOurBankRUS_Fields();
  end;
  return retAddr;
end;

private macro addrRUS_Client( _id )
  var vFind = FALSE;
  var strAddr = "";
  var retAddr = "";
  if ( Addr_Str( _id, I_PTADDR_LEGAL, strAddr )  AND  ( StrLen( strAddr ) > 0 ) )
    vFind = TRUE;
  end;
  if ( vFind )
    retAddr = rAddr.rec.Country + ", " + strAddr;
  else
    retAddr = addrOurBankRUS_Fields();
  end;
  return retAddr;
end;

private macro countryNumCode( _CodeLat3 )
 tCountry.clear();
 tCountry.rec.CodeLat3 = _CodeLat3;
 if ( tCountry.getGE() )
   if ( tCountry.rec.CodeLat3 == _CodeLat3 )
     return tCountry.rec.Name + ", ";
     return "";
   end;
 end;
 return "";
end;

private macro addrForeign()
  var vPlace = "";
  if   ( ( ValType( rAddr.rec.Region ) == V_STRING )  AND  ( StrLen( rAddr.rec.Region ) > 0 ) )
    vPlace = strSection( rAddr.rec.CodeRegion, rAddr.rec.Region );
  elif ( ( ValType( rAddr.rec.Place ) == V_STRING )  AND  ( StrLen( rAddr.rec.Place ) > 0 ) )
    vPlace = strSection( rAddr.rec.lCodePlace, rAddr.rec.Place );
  elif ( ( ValType( rAddr.rec.District ) == V_STRING )  AND  ( StrLen( rAddr.rec.District ) > 0 ) )
    vPlace = strSection( rAddr.rec.lCodeDistrict, rAddr.rec.District );
  elif ( ( ValType( rAddr.rec.Province ) == V_STRING )  AND  ( StrLen( rAddr.rec.Province ) > 0 ) )
    vPlace = strSection( rAddr.rec.lCodeProvince, rAddr.rec.Province );
  end;
  if ( StrLen( vPlace ) > 0 ) 
    return rAddr.rec.Country + ", " + 
           countryNumCode( rAddr.rec.Country ) +
           vPlace +
           rAddr.rec.lCodeStreet + " " + 
           rAddr.rec.StreetName +
           strHouse( "д.", rAddr.rec.House ) +
           strHouse( "корп.", rAddr.rec.NumCorp ) +
           strFlat( rAddr.rec.Flat );
  else
    return rAddr.rec.Country + ", " + 
           countryNumCode( rAddr.rec.Country ) +
           rAddr.rec.Address;
  end;
end;

private macro addrOurBank()
  var sAddr = {Post_Addr};
  var vFind = FALSE;
  if ( FindAddress( {OurBank}, I_PTADDR_POST,  rAddr ) )
    vFind = TRUE;
  else
    if ( FindAddress( {OurBank}, I_PTADDR_REAL,  rAddr ) )
      vFind = TRUE;
    else
      if ( FindAddress( {OurBank}, I_PTADDR_LEGAL, rAddr ) )
        vFind = TRUE;
      end;
    end;
  end;
  if ( vFind )
    if ( ( ValType( rAddr.rec.Country ) == V_STRING )  AND  ( rAddr.rec.Country == "RUS" ) )
      sAddr = addrRUS( {OurBank} );
    else
      sAddr = addrForeign();
    end;
  end;
  return sAddr;
end;

private macro address_Client( _id )
  var sAddr = "";
  if ( FindAddress( _id, I_PTADDR_LEGAL, rAddr ) )
    if ( ( ValType( rAddr.rec.Country ) == V_STRING )  AND  ( rAddr.rec.Country == "RUS" ) )
      sAddr = addrRUS_Client( _id );
    else
      sAddr = addrForeign();
    end;
  end;
  return sAddr;
end;

/********************************************************************/
/*                         Получение СНИЛС                          */
/********************************************************************/
private macro getSNILS( pCodClient )
  var sSNILS = "";
  obj_code.Clear();
  obj_code.rec.ObjectType = 3;  
  obj_code.rec.ObjectID = pCodClient;
  obj_code.rec.CodeKind = 63;
  if ( obj_code.GetGE() )
    if ( ( obj_code.rec.ObjectType == 3 )  AND 
         ( obj_code.rec.ObjectID == pCodClient )  AND 
         ( obj_code.rec.CodeKind == 63 ) )
      sSNILS = obj_code.rec.Code;
    end;
    while ( obj_code.Next()  AND 
            ( obj_code.rec.ObjectType == 3 )  AND 
            ( obj_code.rec.ObjectID == pCodClient )  AND 
            ( obj_code.rec.CodeKind == 63 ) )
      sSNILS = obj_code.rec.Code;
    end;
  end;
  return sSNILS;
end;

/*****************************************************************/
/*         Процедура "Выписка из Реестра обязательств".          */
/*****************************************************************/
macro DI_Client_Excerpt_4120( eDateMake, eCodClient, eNameFileExcerpt, eNumClient )

  var eLine;
  var eCommonSumR   = $0.0;
  var eCommonSumL   = $0.0;
  var eCommonSum    = $0.0;
  var eCommonSumLim = $0.0;
  var eBankAddr = addrOurBank();
  var eStat;

  ReWind( dpe );
  ClearRecord( dpe );
  dpe.DateMake  = eDateMake;
  dpe.CodClient = eCodClient;
  eStat = GetGE( dpe );

  if ( eStat  AND
       ( dpe.DateMake  == eDateMake )  AND
       ( dpe.CodClient == eCodClient ) )

    if ( ( dpe.CodClient != 0 )  AND  ( eDepClnt.GetRecord( dpe.CodClient ) ) )
      veFIO = eDepClnt.CurRec.rec.Name1 + " " +
             eDepClnt.CurRec.rec.Name2 + " " +
             eDepClnt.CurRec.rec.Name3;
      vePhone = di_get_Phone( eDepClnt.CurRec.rec.CodClient );
      if ( StrLen( vePhone ) != 0 )
        vePhone = ", тел. " + vePhone;
      end;

      veAddress = address_Client( eDepClnt.CurRec.rec.CodClient, COMMCLNT_PTADDR_LEGAL ) + vePhone;

      if ( StrLen( eDepClnt.CurRec.rec.PaperIssuerCode ) > 0 )
        vePaperCode = ", код: " + eDepClnt.CurRec.rec.PaperIssuerCode;
      else
        vePaperCode = "";
      end;

      if ( ( StrLen( eDepClnt.CurRec.rec.PaperSeries ) != 0 )  AND
           ( StrLen( eDepClnt.CurRec.rec.PaperNumber ) != 0 )  AND
           ( StrLen( eDepClnt.CurRec.rec.PaperIssuer ) != 0 ) )
        vePapers = DI_PaperTypes4120( eDepClnt.CurRec.rec.PaperKind ) + ": " +
                   eDepClnt.CurRec.rec.PaperSeries + " № " +
                   eDepClnt.CurRec.rec.PaperNumber + ", выдан \"" +
                   DateStrDD( eDepClnt.CurRec.rec.PaperIssuedDate ) + "\", " +
                   eDepClnt.CurRec.rec.PaperIssuer + vePaperCode;
      else
        vePapers = "00 № 000000 выдан \"00.00.0000\" подразделение отсутствует";
      end;
      veSNILS = getSNILS( eDepClnt.CurRec.rec.CodClient );
    else
      veFIO     = "";
      veAddress = "";
      vePhone   = "";
      vePapers  = "";
    end;

    [


                                               ВЫПИСКА
                           ИЗ РЕЕСТРА ОБЯЗАТЕЛЬСТВ БАНКА ПЕРЕД ВКЛАДЧИКАМИ

     Сведения о банке
     Полное фирменное наименование банка: #
     Почтовый адрес банка: #
     Регистрационный номер банка: #

                      Сведения о вкладчике перед которым банк имеет обязательства
     +------------------------------------------------------------------------------------------+-----------------+
     |   Номер   |                                |         Адрес        |      Код вида и      | Страховой номер |
     |   вклад-  |             Ф.И.О.             |     регистрации,     |      реквизиты       | индивидуального |
     |    чика   |                                |    (для почтовых     |      документа,      | лицевого  счета |
     |   по ре-  |           вкладчика            |     уведомлений),    |      удостове-       |                 |
     |   естру   |                                |    номер телефона,   |       ряющего        |                 |
     |   обяза-  |                                |   электронная почта  |       личность       |                 |
     |  тельств  |                                |                      |                      |                 |
     |           |                                |                      |                      |                 |
     +-----------+--------------------------------+----------------------+----------------------+-----------------+
     | ######### | ############################## | #################### | #################### | ############### |
     +-----------+--------------------------------+----------------------+----------------------+-----------------+]
     (
       {Name_Bank},
       eBankAddr,
       reg_Num_Bank(),
       eNumClient:9,
       veFIO:w,
       veAddress:w,
       vePapers:w,
       veSNILS:w
     );

    /* Обязательства. */

    [
                     Сведения о подлежащих страхованию обязательствах банка перед вкладчиком
     +---------------------------------------------------------------------------------------------------------------------------+
     |  N  |      Номер      | Дата доку- |   Порядковый   |          Номер            |       Сумма в      |       Сумма в      |
     | п/п |    документа,   | мента, на  |  номер филиала |         лицевого          |       валюте       |      рублях по     |
     |     |   на основании  | основании  |  (головного    |         счета для         |       обяза-       |       курсу        |
     |     |     которого    |  которого  |     офиса)     |           учета           |      тельств       |       Банка        |
     |     |      принят     |   принят   |    банка, в    |        обязательств       |                    |       России       |
     |     |      вклад      |   вклад    |    котором     |                           |                    |                    |
     |     |                 |            |   вкладчик     |                           |                    |                    |
     |     |                 |            |   заключил     |                           |                    |                    |
     |     |                 |            |    договор     |                           |                    |                    |
     +-----+-----------------+------------+----------------+---------------------------+--------------------+--------------------+];

    eLine = 1;
    eCommonSumR = $0.0;

    while ( eStat  AND
            ( dpe.DateMake  == eDateMake )   AND
            ( dpe.CodClient == eCodClient )  AND
            ( dpe.Type == TYPE_DEPOS ) )

      [| ### | ############### | ########## | ############## | ######################### | ################## | ################## |]
      (
        eLine:3,
        dpe.NumDoc:8,
        DateStrDD( dpe.DateDoc ):10,
        find_OrdNumBank( dpe.Branch ):14,
        Acc_Form(dpe.Account):25,
        dpe.SummaRest:a:15:2,
        dpe.SummaRestRUR:a:15:2
      );

      eLine = eLine + 1;
      eCommonSumR = eCommonSumR + dpe.SummaRestRUR;

      /* Результирующие суммы. */
      eCommonSum    = eCommonSum    + dpe.SummaFullRUR;
      eCommonSumLim = eCommonSumLim + dpe.SummaFinalRUR;

      eStat = Next( dpe );

    end;

    [|     |                 |            |                |                           |                    |                    |
     +-----+-----------------+------------+----------------+---------------------------+--------------------+--------------------+
     |  ИТОГО обязательств по счетам (вкладам), не связанным с предпринимательской деятельностью (руб.):                         |
     |                             ############################################################################################# |
     +-----+-----------------+------------+----------------+---------------------------+--------------------+--------------------+
     |     |                 |            |                |                           |                    |                    |
     +-----+-----------------+------------+----------------+---------------------------+--------------------+--------------------+
     |  ИТОГО обязательств по счетам (вкладам) индивидуальных предпринимателей, открытым для                                     |
     |  осуществления предпринимательской деятельности (руб.):                                                                   |
     +---------------------------------------------------------------------------------------------------------------------------+
     |  ИТОГО обязательств (руб.)  ######################################################################## |                    |
     +---------------------------------------------------------------------------------------------------------------------------+]
    ( eCommonSumR:l:a:15:2, eCommonSumR:l:a:15:2 );

    /* Требования. */

    [
                             Сведения о встречных требованиях банка к вкладчику
     +---------------------------------------------------------------------------------------------------------------------------+
     |  N  |      Номер      | Дата доку- |   Порядковый   |       Номер лицевого      |       Сумма в      |       Сумма в      |
     | п/п |    документа,   | мента, на  | номер филиала  |         счета для         |       валюте       |      рублях по     |
     |     |   на основании  | основании  |   (головного   |           учета           |       требо-       |        курсу       |
     |     |     которого    |  которого  |  офиса) банка, |         встречных         |       вания        |        Банка       |
     |     |     возникло    |  возникло  |    в котором   |         требований        |                    |       России       |
     |     |    требование   | требование |    вкладчик    |                           |                    |                    |
     |     |                 |            |    заключил    |                           |                    |                    |
     |     |                 |            |    договор     |                           |                    |                    |
     +-----+-----------------+------------+----------------+---------------------------+--------------------+--------------------+];

    eLine = 1;
    eCommonSumL = $0.0;

    ReWind( dpe );
    ClearRecord( dpe );
    dpe.DateMake  = eDateMake;
    dpe.CodClient = eCodClient;
    dpe.Type      = TYPE_CR_LOAN;
    eStat = GetGE( dpe );

    while ( eStat  AND
            ( dpe.DateMake  == eDateMake )   AND
            ( dpe.CodClient == eCodClient )  AND
            ( dpe.Type >= TYPE_CR_LOAN ) )

      [| ### | ############### | ########## | ############## | ######################### | ################## | ################## |]
      (
        eLine:3,
        dpe.NumDoc:8,
        DateStrDD( dpe.DateDoc ):10,
        find_OrdNumBank( dpe.Branch ):14,
        Acc_Form(dpe.Account):25,
        dpe.SummaRest:a:15:2,
        dpe.SummaRestRUR:a:15:2
      );

      eLine = eLine + 1;
      eCommonSumL = eCommonSumL + dpe.SummaRestRUR;

      /* Результирующие суммы. */
      eCommonSum    = eCommonSum    + dpe.SummaFullRUR;
      eCommonSumLim = eCommonSumLim + dpe.SummaFinalRUR;

      eStat = Next( dpe );

    end;

    [|     |                 |            |                |                           |                    |                    |
     +-----+-----------------+------------+----------------+---------------------------+--------------------+--------------------+
     |  ИТОГО встречных требований (руб.)  ################################################################ |                    |
     +---------------------------------------------------------------------------------------------------------------------------+]
    ( eCommonSumL:l:a:15:2 );

    [   Сумма обязательств по вкладам за вычетом суммы встречных требований к вкладчику (руб.)  ##############################################
        Сумма, подлежащая страховому возмещению Агентством (руб.)  ##############################################
      в том числе по счетам (вкладам): 
      не связанным с предпринимательской деятельностью вкладчика (руб.)  ##############################################
      открытым для осуществления предпринимательской деятельности (руб.)  ______________________________.


     Руководитель банка                                 Агентство

     ______________/____________/                      _______________________________________

     Главный бухгалтер банка

     ______________/____________/


     М.П.     Дата составления выписки ______________ ]
     (
       eCommonSum:l:a:15:2,
       eCommonSumLim:l:a:15:2,
       eCommonSumLim:l:a:15:2
     );

  end;

end;

/* DI_Client_Excerpt_4120( Date(1,2,2003) , 2000003, "" ); */
