/*
$Name:             TDCReport.mac
$Module:           RS-Retail
$Description:      отчёт по процедуре "Предварительный расчет ПСВ"
*/


import CommonInter, rsd;


// Базовое имя файла отчёта
private const REP_FILE_NAME = "TDCReport";
private const FORECAST_FILE_NAME = "ForecastReport";

// Имя каталога с файлом отчёта
private const REP_DIR = GetRegVal("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR");


//----------------------------------------------------------------------------
private var {cnum};

private var count = 0;
private var errCount = 0;

private var fullRepName = "";

private var percForecast = false;


//----------------------------------------------------------------------------
private macro getAccount(ref: integer)
  var cmd = RsdCommand(
      "SELECT t_Account, t_IsCur, t_Type_Account " +
      "FROM ddepositr_dbt " +
      "WHERE t_Referenc = ? ");

  cmd.addParam("ref", RSDBP_IN, ref);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  return rs;
end;


//----------------------------------------------------------------------------
private macro getOpName(isCur: integer, kind: string, operType: integer): string
  var name = "";

  var cmd = RsdCommand(
      "SELECT t_czNameAlg " +
      "FROM dsb_typop_dbt " +
      "WHERE t_IsCur = ? " +
      "  AND t_Kind = ? " +
      "  AND t_NumOpert = ? ");

  cmd.addParam("isCur", RSDBP_IN, isCur);
  cmd.addParam("kind", RSDBP_IN, kind);
  cmd.addParam("operType", RSDBP_IN, operType);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  if (rs.moveNext())
    name = rs.value("t_czNameAlg");
  end;

  return name;
end;


//----------------------------------------------------------------------------
private macro getErrDescr(stat: integer): string
  var descr = "";

  var cmd = RsdCommand(
      "SELECT t_Contents " +
      "FROM dsbbank_msg " +
      "WHERE t_Number = ? ");

  cmd.addParam("stat", RSDBP_IN, stat);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  if (rs.moveNext())
    descr = rs.value("t_Contents");
  end;

  return descr;
end;


//----------------------------------------------------------------------------
private macro makeFullRepName(): string
  var name;
  if (percForecast)
    name = REP_DIR + "\\" + FORECAST_FILE_NAME;
  else
    name = REP_DIR + "\\" + REP_FILE_NAME;
  end;

  var y, m, d;
  DateSplit(date(), d, m, y);
  name = name + "_" + string(y:o:4) + string(m:o:2) + string(d:o:2) + ".";
  if ({cnum})
    name = name + {cnum};
  else
    name = name + "txt";
  end;

  return name;
end;


//----------------------------------------------------------------------------
private macro getFullRepName(): string
  if (fullRepName == "")
    fullRepName = makeFullRepName();
  end;

  return fullRepName;
end;


//----------------------------------------------------------------------------
macro printHeader(forecast: bool)
  percForecast = forecast;
  SetOutput(getFullRepName(), true);

  var header;
  if (not percForecast)
    header = "Предварительный расчёт ПСВ";
  else
    header = "Прогноз процентов";
  end;

  [                                                           #](header : c);
  [ ];
  [ ];
  [  Счета, обработанные с ошибками:];
  [ ];
  [+----------------------+------------+------------------------------------------+------------------------------------------+];
  [|      N счета         |    Дата    |            Название операции             |             Описание ошибки              |];
  [+----------------------+------------+------------------------------------------+------------------------------------------+];

  SetOutput(null, true);
end;


//----------------------------------------------------------------------------
macro printFooter()
  SetOutput(getFullRepName(), true);

  [+----------------------+------------+------------------------------------------+------------------------------------------+];
  [ ];
  [ ];
  [  Обработано счетов: #](count : l);
  [  Счетов с ошибками: #](errCount : l);
  [ ];
  [ ];
  [ ];
  [ ];

  SetOutput(null, true);
end;


//----------------------------------------------------------------------------
macro printAccount(ref: integer, operType: integer, depDate: date, stat: integer)
  SetOutput(getFullRepName(), true);

  count = count + 1;

  if (stat != 0)
    errCount = errCount + 1;

    var account = "";
    var opName = "";

    var accRs = getAccount(ref);
    if (accRs.moveNext())
      account = accRs.value("t_Account");
      opName = "(" + string(operType) + ") " + getOpName(accRs.value("t_IsCur"), accRs.value("t_Type_Account"), operType);
    end;

    var descr = "(" + string(stat) + ") " + getErrDescr(stat);

    [| #################### | ########## | ######################################## | ######################################## |]
        (account, depDate, opName : w, descr : w);
  end;

  SetOutput(null, true);
end;


/*
printHeader(true);
printAccount(170006729, 1, date(24, 7, 2007), 605);
printAccount(170006730, 82, date(24, 7, 2007), 116);
printAccount(170006730, 82, date(24, 7, 2007), 0);
printFooter();
*/

//println(makeFullRepName());
