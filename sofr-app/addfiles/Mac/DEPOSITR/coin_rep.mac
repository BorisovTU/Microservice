/* Отчеты по монетам */
            
import ci_defs;

const
  TFR_TOTAL_PAR = 1, 
  TFR_SALE_SUM  = 2, 
  TFR_OUT_NDS   = 3, 
  TFR_SALE_SUM_MINUS_TOTAL_PAR = 4, 
  TFR_IN_NDS    = 5, 
  TFR_INCOME    = 6;

class ( TCIRecGenerator ) TCoinRecGenerator( _oper, _makeReport, _observer )

  var
    tmpFile =  TBFile( "coin_ord.tmp", "w", 0 ),
    dbcDoc = TBFile( "dbc_doc.dbt", "r", 0 ), 
    payDoc = TBFile( "pay_doc.dbt", "r", 2 ), 
    coinDocInfo = TRecHandler( "ci_doc.2" ),
    coinRecInfo = TRecHandler( "ci_rec.2");
  
  var
    headPrinted = false,
    NDS = $0, 
    curPurchPrice, 
    totNumItems=0,
    totNumItems1=0,
    totNumItems2=0,
    totSum=$0,
    totCoinSum=$0, 
    totSumSold=$0,
    totSumBought=$0,
    totPurchPrice1=$0,
    totPrice1=$0, 
    totPar2=$0,
    totPrice2=$0;

  macro writeToTmpFile( koc, recType, issue, keySum, isDebCred, ordType, ordSum )

    tmpFile.rec.KindOffCoin = koc;
    tmpFile.rec.RecType = recType;
    tmpFile.rec.Issue   = issue;
    tmpFile.rec.KeySum  = keySum;
    if ( tmpFile.getEQ )
      tmpFile.rec.OrderSum = tmpFile.rec.OrderSum + ordSum;
      tmpFile.update; 
    else
      tmpFile.rec.IsDebCred = isDebCred;
      tmpFile.rec.OrderType = ordType;
      tmpFile.rec.OrderSum  = ordSum;
      tmpFile.insert; 
    end;
  end;
  
  macro findPayDoc( appKind, appKey )
  
    payDoc.rec.iApplicationKind = AppKind;
    payDoc.rec.ApplicationKey = AppKey;
    return payDoc.getEQ;
  end;
  
  macro getNDS

    var
      recFound;
  
    coinDocInfo.setRecordAddr(capIssueDoc,0,
      capIssueDoc.fldOffset("Info"),true);

    NDS = $0;
  
    desk.cashLink.keyNum = 0;
    desk.cashLink.clear;
    desk.cashLink.rec.ApplicationKind1 = capIssueDoc.rec.ApplicationKind;
    desk.cashLink.rec.ApplicationKey1 = capIssueDoc.rec.ApplicationKey;
    recFound = desk.cashLink.getEQ;
    while ( recFound
      and ( desk.cashLink.rec.ApplicationKind1 == capIssueDoc.rec.ApplicationKind )
      and ( desk.cashLink.rec.ApplicationKey1 == capIssueDoc.rec.ApplicationKey ) )
      if ( ( desk.cashLink.rec.ApplicationKind2 == 200 ) and ( desk.cashLink.rec.RefValue2 == 0 ) )
        if ( findPayDoc( desk.cashLink.rec.ApplicationKind2, desk.cashLink.rec.ApplicationKey2 ) )
          if ( payDoc.rec.NumOpert == ciMisc.rec.NDS_Operation )
            NDS = payDoc.rec.InSum;
          end;
        end; 
      end;
      recFound = desk.cashLink.next;
    end;
  end;
  
  macro f5_outHead;
  
    [
                                                                                 Филиал №
                                                                                 отделения
                                                                                 Сбербанка РФ
  
  
                             Отчет по операциям по памятным монетам за ##########
  
    ]
    (
      {curdate}
    );
  end;
  
  macro f5_outCommonHead;
  
    [+-------+----------------------------------------------+--------+-------------------+];
    [|   №   |               Н а з в а н и е                | Колич. |     С у м м а     |];
    [+-------+----------------------------------------------+--------+-------------------+];
  end; 
  
  macro f5_outSellHead;
  
    [ ];
    [Продажа памятных монет];
    [ ];
    f5_outCommonHead;
  end;
  
  macro f5_outLine;
  
    coinRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    [| ##### | ############################################ | ###### | ################# |]
    (
      capIssueRec.rec.Issue,
      coinRecInfo.rec.Name,
      capIssueRec.rec.NumItems,
      capIssueRec.rec.Sum
    );
  end;
  
  macro f5_outTotals;
  
    coinRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);
    [+------------------------------------------------------+--------+-------------------+];
    [|  И т о г о :                                         | ###### | ################# |]
    (
      totNumItems,
      totSum
    );
  end;
  
  macro f5_sellReport
  
    var
      recFound;
  
    totNumItems=0;
    totSum=$0;
  
    coinRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    capIssueRec.clear;
    capIssueRec.rec.Branch=branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_COIN;
  
    f5_outSellHead;
    recFound=capIssueRec.getGE;
    while(recFound
      and (capIssueRec.rec.Branch==branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_COIN))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Brigade==OperBrigade)
        and ((capIssueRec.rec.Operation==O_MISC_SELL) or 
        (capIssueRec.rec.Operation==O_MISC_SELL_ACC)))
        f5_outLine; 
        totNumItems=totNumItems+capIssueRec.rec.NumItems;
        totSum=totSum+capIssueRec.rec.Sum;
      end;
      recFound=capIssueRec.next;
    end;
    f5_outTotals;
  end;
      
  macro f5_report;
  
    if ( not makeReport )
      return;
    end;

    f5_outHead;         
    f5_sellReport;
  end;
  
  macro find
  
    findCIMisc( CI_COIN, capIssueRec.rec.Issue );
    desk.findCashValueForCode( TYPE_RES_PAY, 
                               TYPERES_MINCAPISSUE + CI_COIN, 
                               capIssueRec.rec.Issue );
  end;
  
  macro outSellRecHead;
  
    var
      branchStrNum = "",
      departNum = "",
      departName = "";

    if ( not makeReport )
      return;
    end;
  
    coinRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);
    getBranchParams( branch, branchStrNum, departNum, departName );

    [                                                                                                                         Приложение  № 4
       ##################################                                                                          
                                                                                                                          Филиал № ##########
       Сберегательного банка № ##########         
  
  
                                        
                                                             КОНТРОЛЬНАЯ ВЕДОМОСТЬ
                                                   по продаже монет из драгоценных металлов            
                                                                за ##########
  
  
       Смена № #####
  
    ]
    (
      departName,
      branchStrNum,
      departNum,
      {curdate},
      operBrigade    
    );
    [------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------];
    [|                       |                        Монеты старых образцов                            |               Монеты, учитываемые по номиналу                   |   П о д п и с и   |];  
    [| Фамилия имя отчество  |--------------------------------------------------------------------------|-----------------------------------------------------------------|-------------------|];
    [|                       |   Наименование -   | Номи-|Кол-| Цена при- | Цена реа- |Цена реал. минус |   Наименование -   |Кол-| Номинал | Цена реа- |Цена реал. минус |контролер| кассир  |];
    [|                       |    год выпуска     | нал  | во | обретения | лизации*  |цена приобретения|    год выпуска     | во |         | лизации*  |номинал          |         |         |];
    [|-----------------------|--------------------|------|----|-----------|-----------|-----------------|--------------------|----|---------|-----------|-----------------|---------|---------|];
  end;                                                                                       
                                                                                             
  macro getFio                                                                               
                                                                                             
    coinDocInfo.setRecordAddr(capIssueDoc,0,
      capIssueDoc.fldOffset("Info"),true);

    return makeFio( coinDocInfo.rec.LastName, 
                    coinDocInfo.rec.FirstName, 
                    coinDocInfo.rec.PatrName );
  end;
  
  macro outSellRecLine( purchasePrice, numItems );

    var
      price, 
      parSum; 
   
    coinDocInfo.setRecordAddr(capIssueDoc,0,
      capIssueDoc.fldOffset("Info"),true);

    parSum = ciMisc.rec.Par * numItems;

    price = capIssueDoc.rec.Sum * numItems / coinDocInfo.rec.NumItems;
 
   if ( ciMisc.rec.KindOfCoin == CK_OLD )
      totNumItems1 = totNumItems1 + numItems;
      totPrice1=totPrice1+price;
      totPurchPrice1= totPurchPrice1 + purchasePrice;
    else
      totNumItems2 = totNumItems2 + numItems;
      totPrice2=totPrice2+price;
      totPar2= totPar2 + parSum;
    end;

    if ( not makeReport )
      return;
    end;

    if ( ciMisc.rec.KindOfCoin == CK_OLD )
      [ ####################### #################### ###### #### ########### ########### ################# ]
      (
        getFio:w,
        ( coinDocInfo.rec.Name + " " + ciMisc.rec.IssueYear + " г." ):w,
        ciMisc.rec.Par, 
        numItems, 
        purchasePrice,
        price,
        price - purchasePrice
      );
      writeToTmpFile( ciMisc.rec.KindOfCoin, TFR_SALE_SUM, ciMisc.rec.IssueID, $0, FLAGDEBCRED_IN, 1 /*doDOT_CASH*/, capIssueDoc.rec.Sum );
    else
      [ #######################                                                                            #################### #### ######### ########### #################                     ]
      (
        getFio:w,
        ( coinDocInfo.rec.Name + " " + numItems + " шт." ):w,
        numItems, 
        parSum,
        price,
        price - parSum
      );
      writeToTmpFile( ciMisc.rec.KindOfCoin, TFR_TOTAL_PAR, 0, $0, FLAGDEBCRED_IN, 1 /*doDOT_CASH*/, ciMisc.rec.Par * coinDocInfo.rec.NumItems );
    end;
    writeToTmpFile( ciMisc.rec.KindOfCoin, TFR_OUT_NDS, 0, $0, FLAGDEBCRED_IN, 1 /*doDOT_CASH*/, NDS );
  end;
  
  macro miscSellRecTotals;
   
    coinRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    notifyObserver( CI_COIN, capIssueRec.rec.Issue, O_MISC_SELL, "Продажа: " + coinRecInfo.rec.Name, totSum, totCoinSum );
  
    if ( not makeReport )
      return;
    end;

    [------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------];
    [ И т о г о :                                         #### ########### ########### #################                      #### ######### ########### ################# ]
    (
      totNumItems1, 
      totPurchPrice1,
      totPrice1,
      totPrice1 - totPurchPrice1, 
      totNumItems2, 
      totPar2,
      totPrice2,
      totPrice2 - totPar2
    );
    [                                                                                                                                                       Подписи:    ______________________ ];
    [               * Без учета налога с продажи                                                                                                                              (контролер)      ];
    [                                                                                                                                                                   ______________________ ];
    [                                                                                                                                                                          (кассир)        ];
  end;
  
  macro reportOnBalCostDocs
  
    var
      pp, 
      recFound;

    curPurchPrice = $0;
  
    dbcDoc.clear;
    dbcDoc.rec.LinkApplicKind = capIssueDoc.rec.ApplicationKind;
    dbcDoc.rec.LinkApplicKey = capIssueDoc.rec.ApplicationKey;
    recFound = dbcDoc.getGE;
    while ( recFound 
      and ( dbcDoc.rec.LinkApplicKind == capIssueDoc.rec.ApplicationKind )
      and ( dbcDoc.rec.LinkApplicKey == capIssueDoc.rec.ApplicationKey ) )
      pp = dbcDoc.rec.BalCost * dbcDoc.rec.NumItems;
      curPurchPrice = curPurchPrice + pp;
      if ( ciMisc.rec.KindOfCoin == CK_OLD )
        outSellRecLine( pp, dbcDoc.rec.NumItems );
      else
        writeToTmpFile( ciMisc.rec.KindOfCoin, 
                        TFR_SALE_SUM_MINUS_TOTAL_PAR, 
                        ciMisc.rec.IssueID, 
                        dbcDoc.rec.BalCost, 
                        FLAGDEBCRED_IN, 
                        1, /*doDOT_CASH*/
                        capIssueDoc.rec.Sum * dbcDoc.rec.NumItems / coinDocInfo.rec.NumItems - ciMisc.rec.Par * dbcDoc.rec.NumItems );
      end;
      writeToTmpFile( ciMisc.rec.KindOfCoin, 
                      TFR_IN_NDS, 
                      ciMisc.rec.IssueID, 
                      dbcDoc.rec.Nds, 
                      FLAGDEBCRED_OUT, 
                      2, /*doDOT_MEM*/
                      dbcDoc.rec.Nds * dbcDoc.rec.NumItems );
      writeToTmpFile( ciMisc.rec.KindOfCoin, 
                      TFR_INCOME, 
                      ciMisc.rec.IssueID, 
                      dbcDoc.rec.BalCost, 
                      FLAGDEBCRED_IN, 
                      2, /*doDOT_MEM*/
                      capIssueDoc.rec.Sum * dbcDoc.rec.NumItems / coinDocInfo.rec.NumItems - dbcDoc.rec.BalCost * dbcDoc.rec.NumItems );
      recFound = dbcDoc.next;
    end; 
  end;

  macro resetTmpFile
    var cmd;

    cmd = RsdCommand( NULL, string("truncate table dcoin_ord_tmp") );
    cmd.execute();
  end;
  
  macro miscSellRecBody;
  
    var
      recFound;
  
    find;
    capIssueDoc.rec.RecApplicKind=capIssueRec.rec.ApplicationKind;
    capIssueDoc.rec.RecApplicKey=capIssueRec.rec.ApplicationKey;
  
    recFound=capIssueDoc.getEQ;
    while(recFound
      and (capIssueDoc.rec.RecApplicKind==capIssueRec.rec.ApplicationKind)
      and (capIssueDoc.rec.RecApplicKey==capIssueRec.rec.ApplicationKey))
      if ( checkCIDoc )
        getNDS;
        reportOnBalCostDocs;
        if ( ciMisc.rec.KindOfCoin == CK_NEW )
          outSellRecLine( curPurchPrice, coinDocInfo.rec.NumItems );
        end;
        totSum = totSum + capIssueDoc.rec.Sum;
        if ( ciMisc.rec.KindOfCoin == CK_NEW )
          totCoinSum = totCoinSum + coinDocInfo.rec.Par * coinDocInfo.rec.NumItems;
        end;
        updateValCounter( capIssueDoc.rec.ApplicationKind, capIssueDoc.rec.ApplicationKey );
      end;
      recFound=capIssueDoc.next;
    end;
  end;
  
  macro miscSellRec( commonHeader )
   
    if ( ( not commonHeader ) or ( not headPrinted ) )
      totNumItems1=0;
      totNumItems2=0;
      totSum=$0;
      totCoinSum=$0;
      totSumSold=$0;
      totSumBought=$0;
      totPurchPrice1=$0;
      totPrice1=$0; 
      totPar2=$0;
      totPrice2=$0;
      resetValCounter;
      outSellRecHead;
      headPrinted = true;
    end;
    miscSellRecBody;
    if ( not commonHeader )
      miscSellRecTotals;
    end;
  end;
  
  macro printRec( applicationKind, applicationKey )
        
    if ( not makeReport )
      return;
    end;

    capIssueRec.keyNum = 2;                                        
    capIssueRec.rec.ApplicationKind=applicationKind;
    capIssueRec.rec.ApplicationKey=applicationKey;
    if( not capIssueRec.getEQ )
      msgBox("Ошибка при печати ведомости");
      return;
    end;
    miscSellRec( false );
  end;
  
  macro proceed( commonHeader )
    
    var
      recFound;

    if ( commonHeader == null )
      commonHeader = false;
    end; 

    if ( commonHeader )
      headPrinted = false;
    end;

    capIssueRec.clear;
    capIssueRec.rec.Branch=Branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_COIN;
  
    recFound=capIssueRec.getGE;
    while(recFound
      and (capIssueRec.rec.Branch==Branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_COIN))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Oper==oper)
        and (capIssueRec.rec.Brigade==operBrigade))
        if(capIssueRec.rec.Operation==O_MISC_SELL)
          miscSellRec( commonHeader );
        end;
      end;
      recFound=capIssueRec.next;
    end;
    if ( commonHeader and headPrinted )
      miscSellRecTotals;
    end;
  end;               

  macro isOpIncluded( issue, op )
    
    if ( op == O_MISC_SELL )
      return true;
    end;   

    return false;
  end; 

  InitTCIRecGenerator( _oper, _makeReport, _observer );
  resetTmpFile;
end;

macro coinRecReport

  var
    gen = TCoinRecGenerator( {oper}, true );

  gen.proceed( true );
end;

macro coinRec( applicationKind, applicationKey )

  var
    gen = TCoinRecGenerator( {oper}, true );

  gen.printRec( applicationKind, applicationKey );
end;

macro coinReport

  var
    gen = TCoinRecGenerator( {oper}, true );

  gen.f5_report;
end;