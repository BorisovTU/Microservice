/*
$Name:               xr_wf_baseExecute.mac
$Module:             DEPOSITR
$Description:        API депозита
*/
import rsd, wf_common, wf_uiFormManager, wf_log, wf_commonUtils;

class TProcessData
  var PersistentId : String;
  var IsFinished = false;
  var IsAborted = false;
  var UiSessionId : String;
  var RetVal;
  var CurrentForm;
end;

private macro attachUiSession(proc, pd)
  if (pd.UiSessionId != null)
    var params = TParameters;
    params.setArray(pd.currentForm.Fields); 
    wf.attachUiSession(proc, pd.UiSessionId, pd.CurrentForm.Id, params.toString);
  end; 
end;

private macro detachUiSession(proc, pd)
  pd.UiSessionId = wf.getProcessUiSessionId(proc);
  if (pd.UiSessionId != null)
    var sess = wf.detachUiSession(proc, pd.UiSessionId);
    var form = TUiFormData;
    form.Id = sess.formId;
    form.Fields = TParameters(sess.formParamsString).asArray;
    pd.CurrentForm = form; 
  end;
end;

private macro doStartProcess(className, wfContext, p1, p2, p3, p4, p5, p6, p7, p8, p9, p10, p11, p12, p13, p14, p15)
  var proc = createProcess(className);
  if (proc != null)
    if (wfContext == null)
      wfContext = wfDefaultContext;
    end;
    var retVal = proc.proceed(wfContext, p1, p2, p3, p4, p5, p6, p7, p8, p9, p10, p11, p12, p13, p14, p15);
    var pd = TProcessData;
    if (valType(retVal) == V_STRING)
      pd.RetVal = retVal;
    end;
    pd.PersistentId = proc.persistentId;
    pd.IsFinished = proc.isFinished;
    detachUiSession(proc, pd);
    return pd;
  end; 
  return null;
end;  

private macro logResult(pd)
  if (pd == null)
    logError("Failed to start or resume process");
  else
    if (pd.IsFinished)
      logMessage("Process finished. Persistent ID is " + pd.PersistentId);
    else
      logMessage("Process suspended. Persistent ID is " + pd.PersistentId);
      if (pd.UiSessionId != null) 
        logMessage("UI session ID is " + pd.UiSessionId);
        if (pd.CurrentForm != null)
          logMessage("waiting for UI form to be filled in. Form ID is " + pd.CurrentForm.Id);
        end;
      end;
    end;
  end;
end;

private macro handleError(e)
  logError(string(e.code) + ": " + e.message + " in " + e.module + " (" + string(e.line) + ") " + e.err);
  if (isEqClass("TRsComErr", e.err))
    var ce = e.err;
    var i;
    var extractedMsg = "";
    for (i, 0, ce.count)
      var msg = ce.message(i);
      if (index(msg, "RSCOM") == 0)
        extractedMsg = extractedMsg + "\n" + msg;
      end;
    end;
    if (extractedMsg == "")
      extractedMsg = "Внетренняя ошибка. Обратитесь к разработчику";
    end;    
    runError(extractedMsg);
  else
    runError(e.message);
  end;
end;

macro startProcess(processDefinitionId : String, wfContext, p1, p2, p3, p4, p5, p6, p7, p8, p9, p10, p11, p12, p13, p14, p15)
  logMessage("Starting process: " + processDefinitionId);
  commonInit();
  var cmd = RsdCommand("select t_className from dwf_procdef_dbt where t_id = ?");
  var pd = null;
  cmd.addParam("p_id", RsdBp_In, processDefinitionId);
  cmd.execute;
  var rs = RsdRecordset(cmd);
  if (rs.moveNext)
    pd = doStartProcess(rs.value(0), wfContext, p1, p2, p3, p4, p5, p6, p7, p8, p9, p10, p11, p12, p13, p14, p15); 
  else
    RunError("Неверный идентификатор определения БП: " + processDefinitionId);
  end;
  logResult(pd);
  return pd;
onError(e)
  handleError(e);
end;

macro resumeProcess(pd, wfContext)
  logMessage("Resuming process with persistent ID: " + pd.PersistentId);
  commonInit();
  var proc = wf.loadProcess(pd.PersistentId);
  if (proc != null)
    attachUiSession(proc, pd);
    if ((pd.UiSessionId == null) and proc.uiSessionActive) 
      proc.discardUiSession;
    end;
    if (wfContext == null)
      wfContext = wfDefaultContext;
    end;
    var retVal = proc.resumeProcess(wfContext);
    if (valType(retVal) == V_STRING)
      pd.RetVal = retVal;
    end;
    pd.IsFinished = proc.isFinished;
    detachUiSession(proc, pd);
    logResult(pd);
    return pd;
  end; 
  logResult(null);
onError(e)
  handleError(e);
end;

macro abortProcess(pd)
  logMessage("Aborting process with persistent ID: " + pd.PersistentId);
  var proc = wf.loadProcess(pd.PersistentId);
  if (proc != null)
    proc.abort;
    pd.IsFinished = proc.isFinished;
    pd.IsAborted = proc.isAborted;
    logResult(pd);
    return pd;
  end;
  logResult(null);
onError(e)
  handleError(e);
end;