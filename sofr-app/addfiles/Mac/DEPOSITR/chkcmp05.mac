/*
  ---------------------
  R-Style Software Lab.
  ---------------------
  RS-Bank v6
  ---------------------
  RS-Retail
  ---------------------
  Макрос проверок для компенсаци 2005 года
  ---------------------
  Проверки при зачислениях компенсаций

*/

import deprintr, RSD;

 const
   AT04 =  4,  // Инвалиды 1 группы 
   AT25 = 25,  // Родители и опекуны детей-инвалидов 
   AT58 = 58,  // Наследник участника
   AT63 = 63,  // Владельцам детских целевых 
   AT76 = 76,  // Родители и опекуны инвалидов с детства
   AT80 = 80,  // Дети - сироты
   AT82 = 82,  // На оплату ритуальных услуг умершего в 1998 - 2000 гг
   AT84 = 84,  // Родители погибших в мирное время
   AT85 = 85,  // Вкладчики по 1953 г.р.
   AT86 = 86,  // Инвалиды 2 группы по 1960 г.р.
   AT87 = 87,  // Наследник вкладчика по 1953 г.р.
   AT88 = 88,  // Наследник инвалида 1 группы
   AT89 = 89,  // Наследник инвалида 2 группы
   AT90 = 90,  // Наследник родителя детей-инвалидов
   AT91 = 91,  // Наследник родителя инвалида с детства
   AT92 = 92,  // Наследник родителей погибших в мирное время
   AT93 = 93,  // Вкладчики по 1942 г.р.
   AT94 = 94,  // Наследник вкладчика по 1942 г.р.
   AT95 = 95,  // Участник до 1935 г.р.
   AT96 = 96,  // Вкладчик по 1935 г.р.
   AT97 = 97,  // Наследник вкладчика по 1935 г.р.
   AT98 = 98,  // На оплату ритуальных услуг умершего в 2000 - 2006 гг
   AT99 = 99;  // На оплату ритуальных услуг участникам ВОВ

var ClientList = TClientList;
var Client;
var cmd, rs;
// *********************************************************

macro IsHeir (ClientType)
//
// true - компенсацию получает наследник
//
  var RetVal = false;

  if ((ClientType == AT58) OR
      (ClientType == AT82) OR
      (ClientType == AT87) OR  
      (ClientType == AT88) OR
      (ClientType == AT89) OR
      (ClientType == AT90) OR
      (ClientType == AT91) OR
      (ClientType == AT92) OR
      (ClientType == AT94) OR
      (ClientType == AT97) OR
      (ClientType == AT98) OR
      (ClientType == AT99))
    RetVal = true;
  end;
  return RetVal;
end;
// ---------------------------------------------------------

macro CheckCompensTypeCommon ( compens )

  var DestCodeClient = compens.rec.DestClientCode,
      CodeClient     = compens.rec.CodClient,
      CompensType    = compens.rec.Kind,
      ClientType     = compens.rec.Appltype;

  var ClientName = "";
  var dDay = 0, dMon = 0, dYear = 0;  // День, месяц, год смерти вкладчика
  var bDay = 0, bMon = 0, bYear = 0;  // День, месяц, год рождения вкладчика
  var retVal = 0;
  var str;
  var DateDeath = Date(0,0,0);
  var IsClientRecord = false;

  if (CodeClient == 0)
     msgbox("Не задан владелец компенсируемого счета");
     exit(1);
  end;
//
// Существование счета в базе
//
  cmd = RsdCommand ("select count(*) from ddepositr_dbt where t_account = ? ");
  cmd.addParam ("T_ACCOUNT", RsdBp_in);
  cmd.value    ("T_ACCOUNT") = compens.rec.Account;
  cmd.execute;
  rs = RsdRecordset(cmd); 
  if ( rs.moveNext() )
    if (rs.value(0) == 0)
      DateSplit (compens.rec.AccClsDate, dDay, dMon, dYear);
      if ( (dDay == 1) and (dMon == 1) and (dYear == 1) )
        msgbox("Не задана дата закрытия несуществующего в базе счета");
        exit(1); 
      else
        if (NOT(GetTrue(true, "Компенсируемого счета нет в базе данных. Выполнить зачисление компенсации?")) )
          exit(1);
        end; 
      end;
    end;
  end;
//
// Выплата компенсации данной категории проводится один раз
//
  cmd = RsdCommand ("select count(*) from dcompens_dbt where T_ACCOUNT = ? and T_KIND = ? and T_APPLTYPE = ? and t_date <> '01.01.0001' ");
  cmd.addParam ("T_ACCOUNT", RsdBp_in);
  cmd.value    ("T_ACCOUNT")         = compens.rec.Account;
  cmd.addParam ("T_KIND", RsdBp_in);
  cmd.value    ("T_KIND")            = compens.rec.Kind;
  cmd.addParam ("T_APPLTYPE", RsdBp_in);
  cmd.value    ("T_APPLTYPE")        = compens.rec.ApplType;
  cmd.execute;
         
  rs = RsdRecordset(cmd); 
  rs.moveNext();
  if (rs.value(0) != 0)  // Компенсация уже выдавалась
                         // Такое может быть только для наследников, и то
                         // если задана доля
    if (IsHeir (ClientType))
      if ( (compens.rec.LegacyPart_m != 0) 
       AND (compens.rec.LegacyPart_n != 0) )
        if ( (compens.rec.Sum == compens.rec.FullSum)
         AND (compens.rec.Sum != 0) )
          retVal = 1;
          msgbox("Выплачиваемая доля не может равняться всей сумме");                 
          return 1;
        end;  
      else
        retVal = 1;
        msgbox("Компенсация по этому счету была выполнена");
        return 1;
      end;
    else
      retVal = 1;
      msgbox("Компенсация по этому счету была выполнена");
      return 1;
    end;  
  end;
//
// Поиск вкладчика в базе данных
//
  if (ClientList.GetRecord(CodeClient))
    IsClientRecord = true;
    Client = ClientList.CurRec;
    ClientName = Client.ConvertFIOFull;
    DateSplit (Client.rec.Born,      bDay, bMon, bYear );
    DateSplit (Client.rec.DateDeath, dDay, dMon, dYear );
    DateDeath = Client.rec.DateDeath;
  end;
//
// С заданной датой смерти могут быть только наследники
//
  if (NOT IsHeir (ClientType)
   AND (DateDeath != Date(0,0,0)) )
    retVal = 1;
    MsgBox ("У вкладчика задана дата смерти. Категория выбрана неверно");
    return 1;
  end;
//
// Проверка ограничения по дате рождения
//
  if ( ((ClientType == AT85) AND ((bYear > 1953) OR (bYear == 0)))
   OR  ((ClientType == AT86) AND ((bYear > 1960) OR (bYear == 0)))
   OR  ((ClientType == AT93) AND ((bYear > 1942) OR (bYear == 0)))
   OR  ((ClientType == AT95) AND ((bYear > 1935) OR (bYear == 0)))
   OR  ((ClientType == AT96) AND ((bYear > 1935) OR (bYear == 0)))
   OR  ((ClientType == AT87) AND ((bYear > 1953) OR (bYear == 0)))
   OR  ((ClientType == AT94) AND ((bYear > 1942) OR (bYear == 0)))
   OR  ((ClientType == AT97) AND ((bYear > 1935) OR (bYear == 0))) )
    retVal = 1;
    MsgBox ("Год рождения вкладчика|",ClientName,"|не сответствует виду компенсации");
    return 1;
  end;
//
// Доп.проверки
// 
  str = " |"+ClientName+"|";
  if (ClientType == AT95)
    str = "Вкладчик"+str + "является инвалидом или участником ВОВ ?";
  elif (ClientType == AT86)
    str = "Вкладчик"+str + "является инвалидом 2 группы ?";
  elif (ClientType == AT04)
    str = "Вкладчик"+str + "является инвалидом 1 группы ?";
  elif (ClientType == AT80)
    str = "Вкладчик"+str + "является владельцем счета на детей-сирот ?";
  elif (ClientType == AT25)
    str = "Вкладчик"+str + "является родителем или опекуном детей-инвалидов ?";
  elif (ClientType == AT76)
    str = "Вкладчик"+str + "является родителем или опекуном инвалида с детства ?";
  elif (ClientType == AT84)
    str = "Вкладчик"+str + "является родителем погибшего в мирное время ?";
  elif (ClientType == AT88)
    str = "Умерший вкладчик"+str + "был инвалидом 1 группы ?";
  elif (ClientType == AT89)
    str = "Умерший вкладчик"+str + "был инвалидом 2 группы ?";
  elif (ClientType == AT90)
    str = "Умерший вкладчик"+str + "был родителем детей-инвалидов ?";
  elif (ClientType == AT91)
    str = "Умерший вкладчик"+str + "был родителем инвалидов с детства ?";
  elif (ClientType == AT92)
    str = "Умерший вкладчик"+str + "был родителем погибшего в мирное время ?";
  elif (ClientType == AT99)
    str = "Умерший вкладчик"+str + "был участником ВОВ ?";
  elif (ClientType == AT58)
    str = "Умерший вкладчик"+str + "был участником или инвалидом ВОВ ?";
  else
    str = "";
  end;
  if (str != "")
    if (NOT GetTrue(true, str))
      retVal = 1;
      MsgBox ("Вкладчик не относится к выбранной категории");
      return 1;
    end;
  end;
//
// Детские
//
  if ( ((ClientType == AT63) OR (ClientType == AT80)) 
   AND (ClientList.CurRec.Rec.NotResident != StrFor(0)) ) // Нерезидент
    retVal = 1;
    MsgBox("Компенсация этой категории выдается только резидентам");
    return 1;
  end;
//
// Для наследника
//
  if (IsHeir (ClientType))
//  Счет для зачисления компенсации
    if ( (compens.rec.AccClsDate == Date(0,0,0))
     AND (compens.rec.Account != compens.rec.DestAccount) )
      retVal = 1;
      MsgBox("Компенсация должна зачисляться на счет умершего вкладчика");                 
      return 1;
    end;
    if ( (compens.rec.AccClsDate != Date(0,0,0))
     AND (compens.rec.Account == compens.rec.DestAccount) )
      retVal = 1;
      MsgBox("Компенсация должна зачисляться на счет наследника");
      return 1;                
    end;
//  Дата смерти
    if (DateDeath == Date(0,0,0))
      GetDate (DateDeath, "Укажите дату смерти вкладчика");
      if (IsClientRecord)
        ClientList.CurRec.rec.DateDeath = DateDeath;
        ClientList.MayUpdate = true;
        if (NOT ClientList.Update)
          retVal = 3;
          MsgBox ("Ошибка при сохранении даты смерти вкладчика");
          return 3;
        end;  
        ClientList.MayUpdate = false;
      end;
      DateSplit (DateDeath, dDay, dMon, dYear );
    end;
    if (DateDeath != Date(0,0,0))
      if ( (ClientType == AT82)
       AND ((dYear < 1998) OR (dYear > 2000)) )
        retVal = 1;
        MsgBox ("Дата смерти не соответствует выбранной категории");
        return 1;
      end;
      if ( (ClientType == AT98)
       AND ((dYear < 2000) OR (dYear > 2006)) )
        retVal = 1;
        MsgBox ("Дата смерти не соответствует выбранной категории");
        return 1;
      end;
    elif ( (ClientType == AT82)
     OR    (ClientType == AT98) )
      retVal = 2;
      MsgBox ("Дата смерти вкладчика не установлена");
      return 2;
    end;
  end;

  return retVal;
end;


MACRO CheckCompensationType (compens)

  var retVal=CheckCompensTypeCommon (compens);

  return retVal;

end;
