/*
  СБЕРБАНК 1.0
  R-Style SoftWare Lab.
*/

IMPORT deprintr, /*ci_ord_b, ci_ord_s,*/ brparm;

file CIDoc  ("ci_doc.dbt")    key 0;
file CIOper ("ci_oper.dbt")   key 0;
file CISubOp("ci_subop.dbt")  key 0;
file CashDoc("sb_casdc.dbt")  key 7;
file CashLink("sb_casln.dbt") key 0;
file CashVal("sb_casvl.dbt")  key 0;
file Bunch  ("bunch.dbt")     key 0;
file DeskAux("desk_aux.dbt")  key 0;
file IngRate("ing_rate.dbt")  key 1;

record IngotAux("desk_aux.1");
record DragMetInfo("ci_doc.4");

const
  IWT_LIGATURE = 0,                /* Лигатурная */
  IWT_PURE = 1;                    /* Химически чистая */   

const
  CI_OGSZ          = 1,
  CI_LOTTERY       = 2,
  CI_CERT          = 3,
  CI_COIN          = 4,
  CI_INGOT         = 5,
  CI_MISC          = 10,

  O_INGOT_SELL     = 1,
  O_INGOT_BUY      = 2,
  O_INGOT_KEEP     = 3,
  O_INGOT_RETURN   = 4;

const
  TYPERES_CURRENCY    = 1,   /* Деньги (из справочника валют) 	   */
  TYPERES_VALFORM     = 2,   /* Ценные бланки                 	   */
  TYPERES_VALUABLES   = 3,   /* Др. ценности (из справочника ценностей) */
  TYPERES_MINCAPISSUE = 500; /* Минимально возможный код ЦБ 	   */

const
  TYPE_RES_PAY = 0;

var 
    BranchStrNum = "",
    DepartNum = "",
    DepartName = "",
    {curdate};

macro FindDeskAux( ValueRef, Date, Series, Number )

  DeskAux.Branch = NumFNCash;
  DeskAux.ValueRef = ValueRef;
  DeskAux.Date = Date;
  DeskAux.IsGone = 1;
  DeskAux.Series = Series;
  DeskAux.Number = Number;
  return GetEQ( DeskAux );
end;

macro FindIngRate( Type, Issue )

  ClearRecord( IngRate );
  IngRate.Type = Type;
  IngRate.Issue = Issue;
  IngRate.Date = {curdate};
  IngRate.Time = Time( 23, 59, 59 );
  return GetLE( IngRate ) and ( IngRate.Type == Type ) and ( IngRate.Issue == Issue );
end;

macro FindCashVal( ValueRef )
    
  KeyNum( CashVal, 0 );
  CashVal.RefValue = ValueRef;
  return GetEQ( CashVal );
end;                 

macro FindCashValForTypeCode( Type, ValType, ValCode )

  KeyNum( CashVal, 1 );
  CashVal.Type = Type;
  CashVal.TypeValue = ValType;
  CashVal.CodIntValue = ValCode;
  return GetEQ( CashVal );
end;

macro FindCashDoc( void )

  if ( not FindCashValForTypeCode( TYPE_RES_PAY, TYPERES_MINCAPISSUE + CIDoc.Type, CIDoc.Issue ) )
    return false; 
  end;

  CashDoc.ApplicationKind = CIDoc.ApplicationKind;
  CashDoc.ApplicationKey = CIDoc.ApplicationKey;
  CashDoc.RefValue = CashVal.RefValue;
  return GetEQ( CashDoc );

/*
  var
    RecFound;

  ClearRecord( CashLink );
  CashLink.ApplicationKind1 = CIDoc.ApplicationKind;
  CashLink.ApplicationKey1 = CIDoc.ApplicationKey;
  RecFound = GetGE( CashLink );  
  while ( RecFound
    and ( CashLink.ApplicationKind1 == CIDoc.ApplicationKind )
    and ( CashLink.ApplicationKey1 == CIDoc.ApplicationKey ) )
    if ( CashLink.RefValue2 )
      CashDoc.RefValue = CashLink.RefValue2;
      CashDoc.ApplicationKind = CashLink.ApplicationKind2;
      CashDoc.ApplicationKey = CashLink.ApplicationKey2;
      if ( GetEQ( CashDoc ) )  
        FindCashVal( CashDoc.RefValue );
        if ( ( CashVal.TypeValue == CIDoc.Type + TYPERES_MINCAPISSUE)
             and ( CashVal.CodIntValue == CIDoc.Issue ) )
          return true;
        end;
      end;
    end;
    RecFound = Next( CashLink );    
  end;
  return false;
*/
end;

macro PrintList( void )
  var
    i = 0,
    BalCost, PriceNDS, Delta,
    TotalWeight = 0, TotalBalCost = 0, TotalPrice = 0, TotalDelta = 0,
    DocFound, BunchFound, HeadPrinted = false;

  ClearRecord( CIDoc );
  CIDoc.Branch = NumFNCash;
  CIDoc.IsCur = NumFlagCur;
  CIDoc.Type = CI_INGOT;
  DocFound = GetGE( CIDoc );
  while( DocFound )
    if ( ( CIDoc.Date == {curdate} )
        and ( CIDoc.Operation == O_INGOT_SELL ) )
      if( not HeadPrinted )
        [
                                                                                     ###############
      
      
                                        		   Контрольная ведомость
                                              по продаже мерных слитков физическим лицам
      
                                                         ######################
      
      
        ]( BranchStrNum, CIDoc.Date:m );
        [-------------------------------------------------------------------------------------------+--------------+------------------------------];
        [|   |                     |           |         |        |                  |              |              |             | разница между |];
        [|   |                     |           |         |        |                  | наименование |   текущая    |  розничная  | балансовой и  |];
        [|п/п|  Ф.И.О.  полностью  |  металл   |лигатурн.| проба  |     № слитка     |    завода    |  балансовая  |    цена     |   розничной   |];
        [|   |                     |           |масса(г.)|        |                  | изготовителя |  стоимость   | (в т.ч.НДС) |     ценой     |];
        [|   |                     |           |         |        |                  |              |              |             |   (без НДС)   |];
        [+---+---------------------+-----------+---------+--------+------------------+--------------+--------------+-------------+---------------+];
        HeadPrinted = true;
      end;
      SetRecordAddr( DragMetInfo, CIDoc, 0, FldOffset( CIDoc, "Info" ), true );
      if( FindCashDoc() )
       ClearRecord( Bunch );
       Bunch.BagAppKind = CashDoc.ApplicationKind;
       Bunch.BagAppKey = CashDoc.ApplicationKey;
       BunchFound = GetGE( Bunch );
       if ( BunchFound
         and ( Bunch.BagAppKind == CashDoc.ApplicationKind )
         and ( Bunch.BagAppKey == CashDoc.ApplicationKey ) )
          if( FindDeskAux( CashDoc.RefValue, CashDoc.CashDateDoc, Bunch.Series, Bunch.Min ) )
            SetRecordAddr( IngotAux, DeskAux, 0, FldOffset( DeskAux, "Info" ), true );
            i = i + 1;

            BalCost = CIDoc.BalCost;
            Delta =  DragMetInfo.Price - CIDoc.BalCost;

            PriceNDS = DragMetInfo.Price + DragMetInfo.Tax;
            TotalWeight = TotalWeight + DragMetInfo.Weight;
            TotalBalCost = TotalBalCost + BalCost;
            TotalPrice = TotalPrice + PriceNDS;
            TotalDelta = TotalDelta + Delta;
            [|###|#####################|###########|#########|########|##################|##############|##############|#############|###############|]
            ( i, ( DragMetInfo.LastName + " " + DragMetInfo.FirstName + " " + DragMetInfo.PatrName ):w,
              DragMetInfo.Material:w, DragMetInfo.Weight, DragMetInfo.Hallmark,
              RegNumToStr(Bunch.Series, Bunch.Min):w, DragMetInfo.Manufacturer:w,
              BalCost, PriceNDS, Delta );
          end;
        end;
      end;
    end;
    DocFound = Next( CIDoc );
  end;

  if( HeadPrinted )
    [+---+---------------------+-----------+---------+--------+------------------+--------------+--------------+-------------+---------------+
                                   Итого: ###########                                           ############## ############# ###############
    ]( TotalWeight, TotalBalCost, TotalPrice, TotalDelta );
  
    [
      Кассир-Контролёр _______________________   ____________________
                             (подпись)                  (Ф.И.О.)
  
  
              М.П.                                                   ];
  end;

end;

macro PrintDetailedList;

  GetBranchParams( NumFNCash, BranchStrNum, DepartNum, DepartName );
  PrintList();
end;