/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Прочие приходно-расходные операции                                      *
*                                                                          *
*  Печать ордера ф.31-а                                                    *
*                                                                          *
*  Лебедев С.В.                                                            *
*  30.12.1997                                                              *
\**************************************************************************/

import DeprIntr, DeskInter, InputRN, ChkPrn;

private record Recipient  (cp_recip);
private record OC_PAY_DOC ("pay_doc.np");

private record Parm       (CashParm);

private file   cr         (currency) key 0;  /* Файл валют               */
private file   pk         (pay_kind) key 0;  /* Файл получателей платежа */
private file   pay_csop   (pay_csop) key 0;

private var depclnt = TClientList; /* Справочник вкладчиков */

private const CASHOP_DEBET    = 5;
private const TYPERES_VALFORM = 2;
private const VALFORM_SPR31   = 31;

private const SUM_COMMIS = 0; /* Справка на сумму комиссии */
private const SUM_MAIN   = 1; /* Справка на основную сумму */
private const SUM_BOTH   = 2; /* Справка на обе суммы      */

private const RegPath = "RS-RETAIL\\ПЕЧАТИ\\ПРОЧИЕ_ОПЕРАЦИИ\\ФОРМА_31";

private var {curdate};
private var Branch = NumFNCash();
private var OperBrigade = GetOperBrigade;

private var SumF31    = $0.0L;
private var CodCurF31 = -1;

 /* Поля для выходной формы */
private var Day,Month,Year;
private var KvitFrom = "",KvitFor = "",KvitForAppl = "",KvitType = "";
private var SumRub,SumCop,SumWr,NdsStr = "";


/*******************************************************************
                  Получить связанного кассира
*******************************************************************/
 macro GetPatern(Oper)

   file GroupMem( "groupmem.dbt" ) key 0;

   GroupMem.Branch = NumRealFNCash;
   GroupMem.Date = {curdate};
   GroupMem.Group = OperBrigade;
   GroupMem.Oper = Oper;
   if ( GetEQ( GroupMem ) )
     return GroupMem.Linked;
   else
     return 0;
   end;
 end;


/*******************************************************************
           Формирование полей для выходного документа
*******************************************************************/
macro FormFieldsForDocument

  var d,m,y;
  var rub = "",kop = "",sRub = "",sKop = "";
  var str_tmp;

  DateSplit(/*OC_PAY_DOC.Date_Document*/Date(),d,m,y);
  /* День */
  Day = String(d);
  /* Год */
  if (y < 2000)
    y = y - 1900;
  else
    y = y - 2000;
  end;
  Year = String(y);
  if (StrLen(Year) < 2)
    Year = "0" + Year;
  end;
  /* Месяц */
  if (m == 1)
    Month = "января";
  elif (m == 2)
    Month = "февраля";
  elif (m == 3)
    Month = "марта";
  elif (m == 4)
    Month = "апреля";
  elif (m == 5)
    Month = "мая";
  elif (m == 6)
    Month = "июня";
  elif (m == 7)
    Month = "июля";
  elif (m == 8)
    Month = "августа";
  elif (m == 9)
    Month = "сентября";
  elif (m == 10)
    Month = "октября";
  elif (m == 11)
    Month = "ноября";
  elif (m == 12)
    Month = "декабря";
  else
    Month = "???";
  end;
  /* Принято от */
  if ( OC_PAY_DOC.CodClient )
    if ( depclnt.GetRecord( OC_PAY_DOC.CodClient ) )
      KvitFrom = depclnt.CurRec.rec.Name1 + " " +
                 depclnt.CurRec.rec.Name2 + " " +
                 depclnt.CurRec.rec.Name3;
    end;
  else
    KvitFrom = OC_PAY_DOC.ClientLastName + " " + OC_PAY_DOC.ClientFirstName + " " +
               OC_PAY_DOC.ClientSecondName;
  end;
  
  /* Принято для */
  KvitFor = pk.SzNameAlg;
  /* Подвид операции */
  KvitForAppl = "";
  if (OC_PAY_DOC.ApplType > 0)
    pay_csop.IsCur     = OC_PAY_DOC.IsCur;
    pay_csop.GroupType = OC_PAY_DOC.GroupOpert;
    pay_csop.NumOperat = OC_PAY_DOC.NumOpert;
    pay_csop.ApplType  = OC_PAY_DOC.ApplType;
    if (GetEQ(pay_csop))
      KvitForAppl = pay_csop.NameCompType;
    end;
  end;
  /* Наличные деньги, название ценностей или документов */
  KvitType = "Наличные деньги";
  if ( OC_PAY_DOC.NDSSum != 0 )
    NdsStr = "В том числе НДС: " + String( OC_PAY_DOC.NDSSum );
    SumF31 = SumF31 + OC_PAY_DOC.NDSSum;
  end;
  /* Рубли цифрами */
  SumRub = String(SumF31);
  if (Index(SumRub,".") > 0)
    SumRub = SubStr(SumRub,1,Index(SumRub,".") - 1);
  end;
  /* Копейки цифрами */
  str_tmp = CurToStrAlt(SumF31,rub,kop,Int(cr.ExternalCode));
  if ((StrLen(kop) > 0) AND (Index(str_tmp,kop) > 0))
    sKop = Trim(SubStr(str_tmp,Index(str_tmp,kop) + StrLen(kop)));
    if (Index(sKop,"?") > 0)
      sKop = "";
    end;
  end;

  if ( SumF31 < $1.0 )
    rub = "Ноль";
  end;
  SumCop = kop;
  sRub = cr.Short_Name;
  if   ( StrUpr(cr.Short_Name) == "RUR" )
    sRub = "руб.";
    sKop = "коп.";
  end;
  /* Сумма прописью */
  SumWr = rub + " " + sRub + " " + kop + " " + sKop;
end;


/*******************************************************************
                          Печать справки
*******************************************************************/
macro Spr_tp

  print("\x1b0"); /* Режим печати - 8 строк на дюйм */
  [

                    #   #              #

      #
    #
    #

    #


                #                     #
    #
    #
  ]
  (Day:l,Month:l,Year:l,KvitFrom:l,KvitFor:l,KvitForAppl:l,KvitType:l,SumRub:l,SumCop:l,NdsStr:l,SumWr:l);
  print(""); /* End of page */

end;


/*******************************************************************
               Г Л А В Н А Я   П Р О Г Р А М М А
*******************************************************************/

  var stat     = TRUE;
  var ExitFlag = FALSE;
  var Retry    = FALSE;

  OC_RESULT = 1;

  CheckPrinter(RegPath,StrFor(0));

  KeyNum(pk,0);
  pk.IsCur      = OC_PAY_DOC.IsCur;
  pk.GroupOpert = OC_PAY_DOC.GroupOpert;
  pk.NumOperat  = OC_PAY_DOC.NumOpert;
  if (GetEQ(pk))
    SumF31    = OC_PAY_DOC.InSum;
    CodCurF31 = OC_PAY_DOC.CodCur;
    if (SumF31 != 0)
      KeyNum(cr,0);
      cr.Code_Currency = CodCurF31;
      if (GetEQ(cr))
        FormFieldsForDocument();
        Parm.Type             = 0;       /* Платежеспосбные ресурсы */
        Parm.TypeValue        = TYPERES_VALFORM;
        Parm.CodIntValue      = VALFORM_SPR31;
        Parm.Oper             = OC_PAY_DOC.Oper;
        Parm.CashDateDoc      = OC_PAY_DOC.Date_Document;
        Parm.NumOper          = CASHOP_DEBET;
        Parm.ValueCol         = 1;
        Parm.NumbDocumCash    = OC_PAY_DOC.NDoc;
        Parm.ApplicationKind  = OC_PAY_DOC.iApplicationKind;
        Parm.ApplicationKey   = OC_PAY_DOC.ApplicationKey;
        Parm.Series           = "";
        Parm.MinRegistrNumber = 0;
        Parm.OperPatern       = OC_PAY_DOC.Oper;
        while (not ExitFlag)
          /* Для перечислений номер ф.31 попытаемся взять из записи */
          if ((OC_PAY_DOC.GroupOpert == 11) OR (OC_PAY_DOC.GroupOpert == 41))
            if ((StrLen(Trim(OC_PAY_DOC.KvitSer )) > 0) OR
                (StrLen(Trim(OC_PAY_DOC.KvitNumb)) > 0)   )
              Parm.Series           = OC_PAY_DOC.KvitSer;
              Parm.MinRegistrNumber = OC_PAY_DOC.KvitNumb;
            end;
          end;
          if (Parm.MinRegistrNumber == 0)
            stat = InputRegNum(OC_PAY_DOC.Oper,
                               Parm.Oper,
                               TYPERES_VALFORM,
                               VALFORM_SPR31,
                               "Введите номер справки ф.31:",
                               Parm.Series,
                               Parm.MinRegistrNumber);
          end;
          if (stat)
            stat = InitCashDoc(Parm);
          end;
          if (stat)
            ExitFlag = TRUE;
          else
            Retry = FALSE;
            GetTrue(Retry, "Ошибка при расходе. Повторить ?");
            if ( Retry )
              Parm.MinRegistrNumber = 0;
            else
              ExitFlag = true;
            end;
          end;
        end;
        if (stat)
          Spr_tp();
        else
          MsgBox("Ошибка при расходе справки ф.31");
          Exit(1);
        end;
      else
        MsgBox("Расход справки ф.31|Не найдена валюта в справочнике валют");
        Exit(1);
      end;
    else
      OC_RESULT = 0;
      Exit(1);
    end;
  else
    MsgBox("Расход справки ф.31|Не найден вид платежа в справочнике видов платежей");
    Exit(1);
  end;

  if (stat)
    OC_RESULT = 0;
  end;

  ExitMacro();

end;
