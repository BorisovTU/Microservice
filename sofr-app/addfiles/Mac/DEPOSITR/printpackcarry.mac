//**********************************************************************
//*  Печать отчета о допроводке непроведеных документов                *
//*  PRJ000338                                                         *
//*  Чижова Г.А.                                                       *
//*  25.10.2004                                                        *
//**********************************************************************

import CommonInter, Acc_Form, rsd;

private var {curdate},
            {CNum},
            {Oper};

var FNCash      = NumFNCash,
    FlagCur     = NumFlagCur,
    OperBrigade = GetOperBrigade,
    TrnNumber   = GetTransactionNumber;

var flagPrintHeder = false;

var NameMacro      = "";


macro GetBranchName
  var name;
  
  if  (not findDepartment(NumFNcash, name) )
    name = string(NumFNcash);
  end;
  return name;
end;

// Печать заголовка отчета
Macro PrintReportTitle

  [ 
    Филиал N #

            Отчет об проводке непроведенных документов за ###########  ]
//         Количество непроведенных документов ##### ]
  ( GetBranchName(), 
    date : f/*,
    CountAllSuspDocs */);
  println();
  
[ +-----------------------+----------------------------+-----------+-----------+-------+-----------------------+-----------------------------------------------------+
  |      № Счета          |       ФИО вкладчика        |   Сумма   |   Сумма   |Кол-во |         Даты          |                     Статус                          |
  |                       |                            |  прихода  |  расхода  |док-тов|       документов      |                                                     |
  +-----------------------+----------------------------+-----------+-----------+-------+-----------------------+-----------------------------------------------------+ ]

end;

// Печать подвала отчета
Macro PrintReportFooter

  var CountCariedAcc = 0;
  var CountCariedDoc = 0;
  var CountNonCarredAcc = 0; 
  var CountError = 0;
  var ErrCode;

  var cmd, rs;
  cmd = RsdCommand( "select acc err, count(acc) errcount, sum(doc) carrieddoc " + 
                    "from ( select case c.t_errcode when 0 then 0 " +  
                                                   "when -777 then c.t_errcode " + 
                                                   "else 1 end acc, " + 
                                  "case c.t_errcode when 0 then c.t_count " +  
                                                   "else 1 end doc " + 
                           "from dcarry_sus_docs_task_dbt c " + 
                           "where c.t_cnum = ? ) " +
                    "group by acc " );

  cmd.addParam( "CNum", RSDBP_IN );
  cmd.value( "CNum" ) = {CNum};

  cmd.execute();

  rs = RsdRecordset( cmd );
  while ( rs.moveNext )
    ErrCode = rs.value("err");
    if   (ErrCode == 0)
      CountCariedAcc = rs.value("errcount"); 
      CountCariedDoc = rs.value("carrieddoc"); 
    elif (ErrCode == 1)
      CountNonCarredAcc = rs.value("errcount"); 
    elif (ErrCode == -777)	
      CountError = rs.value("errcount");
    end;
  end;
  

  [ +-----------------------+----------------------------+-----------+-----------+-------+-----------------------+-----------------------------------------------------+];
  [

        Обработано счетов:               ######
        Проведено документов:            ######
                               
        Счетов с ошибками:               ###### 
        Счетов, не попавших в обработку: ###### ]
   ( CountCariedAcc:12:0,
     CountCariedDoc:12:0,
     CountNonCarredAcc:12:0, 
     CountError:12:0
  );

  
end;

Macro ShowFile
  file a() txt;
  open (a, SetOutput("pr"));
  ViewFile(a);
end; 


Macro PrintDescription
 
  [
    Филиал N #
  
            Отчет об проводке непроведенных документов за ########### 
          

                    Непроведенных документов не обнаружено.
  ]
  ( GetBranchName(),
    date : f );

end;

macro CheckDate( d )
  if ( d == null )
    d = date(0,0,0);
  end;
  return d;
end;

// Печать строки отчета                             
macro PrintString( rs )
  var mind, maxd;
  
  if ( not flagPrintHeder )
    PrintReportTitle;
    flagPrintHeder = true;
  end;
     

  Array TempState;
  StrSplit( rs.value("errtext"), TempState, 52, 52, 1 );

  DtTmSplit(rs.value("mind"), mind);
  DtTmSplit(rs.value("maxd"), maxd);


  [ |#######################|############################|###########|###########| ##### | ##########-########## |#####################################################|] 
  ( //Acc_Form(rs.value("acc")),
    rs.value("acc"),
    rs.value("fio"), 
    rs.value("insum") :12:2, 
    rs.value("outsum") :12:2, 
    rs.value("ct"),
    CheckDate(mind),
    CheckDate(maxd),
    TempState( 0 ) 
  );
  if ( StrLen( TempState( 1 ) ) != 0 )
    [ |                       |                            |           |           |       |                       |#####################################################|] 
    ( TempState( 1 ) );
  end;
end;


// Распечатать строки массива записей
macro PrintReport

  var cmd;
  var rs;

  cmd = RsdCommand( "select c.t_account acc, c.t_count ct, c.t_insum insum, c.t_outsum outsum, c.t_mindocdate mind, c.t_maxdocdate maxd, " +
                           "replace ( case p.t_name1 when chr(1) then '' else p.t_name1 end || ' ' || " +
                                     "case p.t_name2 when chr(1) then '' else p.t_name2 end || ' ' || " +
                                     "case p.t_name3 when chr(1) then '' else p.t_name3 end, '  ', 'данные не найдены' ) fio, " +
                           "replace(nvl(m.t_contents, c.t_errcode ),'0',' ') errtext " +
                    "from dcarry_sus_docs_task_dbt c " +
                    "left outer join dpersn_dbt p " +
                      "on c.t_codclient = p.t_personid " +
                    "left outer join dsbbank_msg m " +
                      "on c.t_errcode = m.t_number " +
                      "and m.t_number != 0 " +  
                    "where c.t_cnum = ? "  );

  cmd.addParam( "CNum", RSDBP_IN );
  cmd.value( "CNum" ) = {CNum};

  cmd.execute();

  rs = RsdRecordset( cmd );
  while ( rs.moveNext )
    PrintString(rs);
  end;

  if ( flagPrintHeder )
    PrintReportFooter;
  else
    PrintDescription;
  end;

end;


macro SelectDepartmentForCarry(fromCurrentBranch)
  var dpDepList = TdpDepList;
  array AFNCash;
  array MenuName;
  var i = 0;
  var selected;
  var topBranch = NumFNCash();

  AFNCash(0) = -1;
  MenuName(0) = " По всем              ";
  if (not fromCurrentBranch)
     topBranch = dpDepList.getFilialID(topBranch);
  end;
  dpDepList.SetFilter(topBranch);
  while (dpDepList.next())
    i = i + 1;
    AFNCash(i) = dpDepList.rec.Code;
    MenuName(i) = dpDepList.rec.Name;
  end;
  i = Menu(MenuName,NULL,"Выберите подразделение");
  if (i >= 0)
    selected = AFNCash(i);
  else
    exit(1);  // ничего не выбрано
  end;
  return selected;
end;

//Для отладки 
//PrintReport;