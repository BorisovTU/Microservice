//
// RS-Bank 6.00.004
// RS-Retail
// Клиент - физическое лицо
// Общие переменные, константы и функции
//
// Создаг
//
Import CommonInter;
//
// Типы адресов
//
const COMMCLNT_PTADDR_LEGAL = 1;             // Юридический (постоянная регистрация)
const COMMCLNT_PTADDR_REAL  = 2;             // Фактический (жительства)
const COMMCLNT_PTADDR_POST  = 3;             // Почтовый (временной регистрации)
const COMMCLNT_NO_DEFINED_ADRESS_TYPE = -1;  // Тип адреса не определен

const COMMCLNT_DEFAULT_ADRESS_TYPE = COMMCLNT_PTADDR_LEGAL; // Берется по умолчанию

// #########################################################

macro commclnt_GetAddressField
(
 clnt,    // Запись клиента типа TClientList
 tAdr,    // Тип адреса
 field    // Имя поля
)
  var strret = "";
  var OldType;
  var OldMayMakeNewTypeAdress;
  if (tAdr > 0)
//  1. Сохранение текущего типа адреса и установка заданного
    OldMayMakeNewTypeAdress = clnt.MayMakeNewTypeAdress;
    OldType                 = clnt.AdressType;
    clnt.MayMakeNewTypeAdress = false;
    clnt.AdressType           = tAdr;
//  2. Получение строки адреса
//     Ради этого все и затевалось
//    if (clnt.AdressType != COMMCLNT_NO_DEFINED_ADRESS_TYPE)
    if (clnt.AdressType == tAdr)
      if (StrUpr(GenClassName(clnt)) == "TCLIENTLIST")
        strret = GenGetProp(clnt.CurRec.rec, field);
      elif (StrUpr(GenClassName(clnt)) == "TDEPCLIENT")
        strret = GenGetProp(clnt.rec, field);
      end;
    end;
//  3. Восстановление типа адреса
    clnt.MayMakeNewTypeAdress = true;
    clnt.AdressType           = OldType;
    clnt.MayMakeNewTypeAdress = OldMayMakeNewTypeAdress;
  end;
  return strret;
end;

macro commclnt_Получить_Поле_Address_Заданного_Типа
(
 clnt,    // Запись клиента типа TClientList
 tAdr     // Тип адреса
)
//
// Получение строки Address заданного типа адреса
//
// Возвращает строку
// Если адреса заданного типа нет - возвращается пустая строка
//
  return commclnt_GetAddressField(clnt, tAdr, "Address");
end;
// *********************************************************

macro commclnt_Получить_Поле_Address_Фактического_Адреса
(
 clnt     // Запись клиента типа TClientList
)
//
// Получение строки фактического адреса (бывшего поля AddressF)
//
// Возвращает строку.
// Если адреса нет - возвращается пустая строка
//
  var strret;
// 1. Фактический адрес
  strret = commclnt_Получить_Поле_Address_Заданного_Типа (clnt,COMMCLNT_PTADDR_REAL);
// 2. Если фактического адреса нет - берем почтовый
  if (strret == "")
    strret = commclnt_Получить_Поле_Address_Заданного_Типа (clnt,COMMCLNT_PTADDR_POST);
  end;
  return strret;
end;
