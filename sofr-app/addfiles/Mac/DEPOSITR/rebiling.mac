/*******************************************************************************
  RS-Retail 5.10 Rebiling.mac
     Build  071

     Создан для получения отчета для переоформленных счетов с ОСОБЫХ НОМЕРНЫХ
     на ОСОБЫЕ СБ РФ, согласно приказам от 15-02-2003 года

 LAA 18.02.2003
*******************************************************************************/

import DeprIntr,issrvdoc;

var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

file DEPOSITR (depositr) key 8;
file SBDEPDOC (sbdepdoc) key 0;
file CURRENCY (currency) key 0;

var clnt = TClientList;

private var
    FlagCur = NumFlagCur(),
    Branch  = NumFNCash(),
    {CNum},
    {Name_Bank},
    {NumberBranch},
    Beg_Date = date(0,0,0),
    End_Date = date(0,0,0);

var MaxValueCurrency     = 0,     /*по умолчанию рубли*/
    CodeCur              = 0, 
    FlagPresence         = FALSE, /*флаг наличия счетов*/
    FlagPrintSmallHeader = FALSE; /*флаг печати шапки*/


var DateReport = {curdate},
    FIO        = "",
    aRec       = 0; /*количество счетов всего переведенных*/

/******************************************************************************/
MACRO findClient()

   if (clnt.GetRecord(DEPOSITR.CodClient))
       if(  (not SpecialAccess) and OperMayListSpecialAccounts and DEPOSITR.SpecialAccess )
           FIO = "XXXXXX X.X.";
       else
    	   FIO = clnt.CurRec.ConvertFIOFull ();
       end;
   else
     FIO = ""
   end;

END;
/******************************************************************************/
MACRO findAccount()

   var
     stat = FALSE;

     ClearRecord(DEPOSITR);
     Rewind     (DEPOSITR);

     DEPOSITR.Referenc = SBDEPDOC.Referenc;
     stat = GetEQ (DEPOSITR);

     if (not stat)
       msgBox("Счет с REFERENC ", DEPOSITR.Referenc, " не найден");
     else
       /*найти клиента (владельца) счета*/
       findClient();
     end;


END;

/******************************************************************************/
MACRO PrintSmallHeader(NewTypeAccount, OldTypeAccount, PrintDate)

  var 
    stat    = FALSE,
    nameCur = "";
  
  REWIND (CURRENCY);
  ClearRecord(CURRENCY);

  CURRENCY.Code_Currency = CodeCur;
  stat = GetEQ (CURRENCY);

  if (stat)
    NameCur = "(" + CURRENCY.Name_Currency + ")";
  end;

  if (not FlagPresence)
    [

      Дата ########## Переведенныe счета с ############# на ############# по валюте ### #############
     ---+--------------------------+-----------------------------------------------------------------
      № | Номер счета              | Ф.И.О. владельца счета
     ---+--------------------------+-----------------------------------------------------------------
    ](PrintDate, OldTypeAccount, NewTypeAccount, CURRENCY.ExternalCode, NameCur);
  end;
END;

/******************************************************************************/
MACRO PrintOperatioMove(NewTypeAccount,OldTypeAccount)

  var
    stat = FALSE,
    iRec = 0;

  var cur_date = Beg_Date;

    while ( cur_date <= End_Date )

      iRec = 0;
      /*убираем флаг наличия*/
      FlagPresence = FALSE;
  
      ClearRecord(SBDEPDOC);
      Rewind (SBDEPDOC);
      /*
      if (CodeCur > 0)
          FlagCur = 1;
      else
          FlagCur = 0;
      end;
      */
  
      SBDEPDOC.IsCur         = FlagCur;
      SBDEPDOC.FNCash        = Branch;
      SBDEPDOC.Date_Document = cur_date;
      SBDEPDOC.Type_Account  = NewTypeAccount;
      SBDEPDOC.Code_Currency = CodeCur;
      SBDEPDOC.TypeOper      = 62;
      stat = GetGE(SBDEPDOC);
     
      while (stat and
              (SBDEPDOC.IsCur == FlagCur) and
              (SBDEPDOC.Date_Document == cur_date) and
              (SBDEPDOC.Type_Account  == NewTypeAccount) and
              (SBDEPDOC.Code_Currency == CodeCur) and 
              (SBDEPDOC.TypeOper == 62)
            )
          if ((SBDEPDOC.TypeOper == 62)
           and ((SBDEPDOC.TypeComplexOper == 103) or (SBDEPDOC.TypeComplexOper == 508))
           and isServDoc(SBDEPDOC) and ( not SBDEPDOC.FlagStorn ) )
              iRec = iRec + 1;
              aRec = aRec + 1;
              /**/
              if (not FlagPrintSmallHeader)
                FlagPrintSmallHeader = TRUE;
                PrintSmallHeader(NewTypeAccount, OldTypeAccount, cur_date );
              end;
              /*найти счет*/
              findAccount();
              [### ########################## ################################################################](iRec, Acc_Form(DEPOSITR.Account):f, FIO);
              /*единожды взводим флаг наличия записей*/
              if (not FlagPresence)
                FlagPresence = True;
              end;       
          end;
          stat = next(SBDEPDOC);
      end;
      if (FlagPresence);
        [---+--------------------------+-----------------------------------------------------------------
             Всего за дату по заданной валюте переоформлено ### счета (счетов)
        ]( iRec:l);
        FlagPrintSmallHeader = FALSE;
      end;
      /*обнуляем счетчик*/
      iRec = 0;

      FlagPresence = FALSE;

      cur_date = cur_date + 1;

    end;

END;

/******************************************************************************/
MACRO PrintHader()

  record i_dp_dep ( "i_dp_dep.rec" );

  var 
    stat = TRUE;
  var nameBranch = "";

  if ( FindDepartment( Branch, NULL, i_dp_dep ) )
    nameBranch = i_dp_dep.Name;
  end;

    [ Отделение   ################
      Филиал      #######################
      ##############################

      
                                         ВЕДОМОСТЬ СЧЕТОВ 
                      переоформленных с "Особо Номерных СБ РФ" на "Особые СБ РФ"
    
    ]({NumberBranch}, nameBranch, {Name_Bank});

END;

macro Hand1er (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* ENTER */
    if ( key == 13 )
      if ( id == 1 )
        SetFocus( IP, 0 );
        stat = CM_IGNORE;
      end;
    /* ESC */
    elif (key == 27)
      stat = CM_CANCEL;
    /* F7 - Выпуск отчета ---------------------------------- */
    elif (key == 321)
      stat = CM_SAVE;
    end;
  end;

  return stat;

end;


/******************************************************************************/
MACRO SetupPeriod() /*задание периода подсчета*/

  var 
    stat      = FALSE,
    day = 0, 
    mon = 0, 
    year = 0;
  
  record Period("SC_PRD", "rtusrmac") dialog;

  DateSplit (DateReport,day,mon,year);
  /*FirstEndMonthDate(DateReport,Beg_Date,End_Date);*/

  Beg_Date = DateReport;
  End_Date = DateReport;
  
  Period.BeginDate = Beg_Date;
  Period.EndDate   = End_Date; 
  stat = RunDialog(Period, "Hand1er");
  if (stat)
    Beg_Date = Period.BeginDate;
    End_Date = Period.EndDate;
  end;
  /*при условии неверного ввода данных*/
  if (Beg_Date > End_Date)
    MsgBox ("Диапазон дат введён неверно| | |Начало -",beg_Date,"|Конец  -",End_Date);
    stat = FALSE;
  end;
  return stat;

END;

/******************************************************************************/
MACRO get_dates()

  var stat = FALSE;

  while ( not stat )  /*цикл задания дат*/
    stat = SetupPeriod();
    if (not stat)
      Exit( 1 );
    else
      stat = TRUE;
      return stat;
    end;
  end;

END;

/******************************************************************************/
MACRO MAIN()

  var stat = FALSE,
      recFound, 
      NewTypeAccount = "",
      OldTypeAccount = "";

    /*ввод даты*/
    get_dates();

    /*ввод шапки*/
    PrintHader();

    if ( FlagCur );
      CURRENCY.Code_Currency = 1;
      recFound = GetGE( CURRENCY );
      while ( recFound )                 
          CodeCur = CURRENCY.Code_Currency;

          OldTypeAccount = "Особ.ном. 2г";
          NewTypeAccount = "Особый на 2г";
          PrintOperatioMove(NewTypeAccount, OldTypeAccount);

          OldTypeAccount = "Особ.н.1г_1м";
          NewTypeAccount = "Особый 1г_1м";
          PrintOperatioMove(NewTypeAccount, OldTypeAccount);
          
          OldTypeAccount = "Особый номер";
          NewTypeAccount = "Особый на 3м";
          PrintOperatioMove(NewTypeAccount, OldTypeAccount);
          
          recFound = next( CURRENCY ); 
      end;
    else
      CURRENCY.Code_Currency = CodeCur = 0;
      if ( getEQ( CURRENCY ) )
          OldTypeAccount = "Особ.ном.2 г";
          NewTypeAccount = "Особый на 2г"; 
          PrintOperatioMove(NewTypeAccount, OldTypeAccount);

          OldTypeAccount = "Особ.н.1г_1м";
          NewTypeAccount = "Особый 1г_1м";
          PrintOperatioMove(NewTypeAccount, OldTypeAccount);
          
          OldTypeAccount = "Особый номер";
          NewTypeAccount = "Особый на 3м";
          PrintOperatioMove(NewTypeAccount, OldTypeAccount);
      end;
    end;

    if (aRec != 0)/*записей вообще не обнаружено*/
      [
       ВСЕГО ПЕРЕОФОРМЛЕННЫХ СЧЕТОВ  ######
      
      
        ______________________ 
        подпись старшего смены
      ](aRec);
    end;

END;

/*ГОЛОВА*/
main();
