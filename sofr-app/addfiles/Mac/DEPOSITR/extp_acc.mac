/* Макрос поиска счета в полях "Получатель" и "назначение платежа" */

import rsd;

macro GetNextAccount(TextField, start)
  var result="", cur=start, len=strlen(TextField), symb;
  while( cur<=len )
     symb = substr( TextField, cur, 1 );
     if ( (symb>="0") and (symb<="9") )
        result = result + symb;
     else
        if ( (strlen(result)==20) OR (strlen(result)==22) )
          setparm(1, cur);
          return result;
        else
          result = "";
        end;
     end;
     cur = cur + 1;
  end;

  setparm(1, cur);
  if ( (strlen(result)==20) OR (strlen(result)==22) )    
    return result;
  end;
  
  return "";
end;

macro GetAccount( paymentID, paymentAccount )
   var rs;
   var Account = "";
   var TextField, tmpAcc, pos = 1;
   var codeCurrency;

   rs = RsdRecordset( string("select casvl.t_codintvalue from dsb_casvl_dbt casvl, dpmpaym_dbt pm where casvl.t_typeValue=1 and casvl.t_type=0 and casvl.t_FIID=pm.t_PayFIID and pm.t_paymentID=",
                              paymentID," order by casvl.t_refvalue") );
   if ( rs.moveNext() )
     codeCurrency = rs.Value(0);
     rs = RsdRecordset( string("select t_ground, t_ReceiverName from dpmrmprop_dbt where t_PaymentID=",paymentID) );
     if ( rs.moveNext() )
        TextField = string( rs.Value(0), " ", rs.Value(1) );
        while( (tmpAcc=GetNextAccount(TextField, pos))!="" )
          if ( strlen(tmpAcc)==22 )
             rs = RsdRecordset( string("select main.t_Account from ddepositr_dbt main where (main.t_Account='", 
                                tmpAcc, "' OR main.t_svodaccount = (select serv.t_svodaccount from ddepositr_dbt serv ",
                                "where serv.t_account='",tmpAcc,"' ",
                                "and serv.t_code_currency=",codeCurrency," ",
                                "and serv.t_open_close='З' "
                                "and serv.t_FnCash=main.t_FnCash))",
                                "and main.t_code_currency=",codeCurrency," ",
                                "and main.t_open_close!='З'") );
          else
             rs = RsdRecordset( string("select main.t_Account from ddepositr_dbt main where (main.t_Account like '", 
                                tmpAcc, "%' OR main.t_svodaccount = (select serv.t_svodaccount from ddepositr_dbt serv ",
                                "where serv.t_account like '",tmpAcc,"%' ",
                                "and serv.t_code_currency=",codeCurrency," ",
                                "and serv.t_open_close='З' "
                                "and serv.t_FnCash=main.t_FnCash))",
                                "and main.t_code_currency=",codeCurrency," ",
                                "and main.t_open_close!='З'") );
          end;
          if ( rs.moveNext() )
             if ( (Account=="") OR (Account==rs.Value(0)) )
                Account = rs.Value(0);
             else
                /* Нaшли более одного счета */
                return "";
             end;
          end;
        end;
     end;
   end;

   if( Account == "" )
     Account = paymentAccount;
   end;

   return Account;
end;