/****************************************************************************
   RS-Retail 2.01                 (СБЕРБАНК)
   Subsystem:   ПОСЛЕДКОНТРОЛЬ
                ~~~~~~~~~~~~~~
   Description: Загрузка данных в Последконтроль RS-Retail из филиалов
                автоматизированных RS-Retail
   ------------------------------------------------------------------------
   1999(c)R-Style ShareWare Lab.

   01.08.1999 - учтена смена вида вклада
   11.08.1999 - счет и клиент выгружались, если это операция открытия.
                сделано так, что они выгружаются, если операция по счету -
                первая (возможны случаи, что счет заводиьтся с нулевым
                остатком, а затем идут допвзносы, зачисл. и пр.)
   27.08.1999 - Для первого открытия в базе не выгружалось
****************************************************************************/

/* Внешние символы */
import deprintr, opercode;

/* Текстовые файлы */
file extrn_dep() txt write;
file extrn_doc() txt write;
file extrn_cln() txt write;

/* Файлы базы последконтроля  !Номера ключей только для билда со 110 и старше! */
file depositr("depositr.dbt") key 8;
file sbdepdoc("sbdepdoc.dbt") key 5;
file sb(sbdepdoc) key 2;
file sb_casln("sb_casln.dbt") key 0;
file curr(currency) key 0;
record twodate (SC_PRD) dialog;

/* Разделитель полей */
const DELIM = "|",
      D_AR            =  8, /* Проводка в архиве      */
      D_ARC           =  9, /* Корректировка в архиве */
      D_AUDIT_STORN   = 14, /* Сторнирование операции */
      D_AUDIT_DELETE  = 15, /* Удаление операции      */
      D_AUDIT_CORRECT = 16, /* Корректировка операции */
      D_DELETE        =  2;


/* Глобальные переменные */
var
  {curdate},
  branch = NumFNCash(), 
  ClientList = TClientList,
  numsession = 1,
  sesDate = {curdate}, DateEnd, numdoc=0;



/***********************************************************************\
| Макрофункция                                                          |
\***********************************************************************/
macro CureStr(st);
   while (index(st,strfor(10))>0)
     StrSet(st,index(st,strfor(10))," ");
   end;
   return st;
end;

macro iszero(st);
  if (st=="")
     return st;
  else
     return "1";
  end;
end;

macro GetIntNum(cod);
  ReWind(curr);
  curr.Code_Currency=cod;
  if (GetEQ(curr))
     return curr.ExternalCode;
  else
     return 0;
  end;
end;

macro complex_oper_main_doc( void )
  if(sbdepdoc.TypeOper == Зачисление_Процентов)
    if ((sbdepdoc.TypeComplexOper == Закрытие_Наличными) or
        (sbdepdoc.TypeComplexOper == Закрытие_Безналичными) or
        (sbdepdoc.TypeComplexOper == Выдача_Процентов))
       return "1";
    end;
  end;
  if((sbdepdoc.TypeOper == Перевод_В_Другой_Вид_Вклада) and (sbdepdoc.ApplType == 1))
       return "1";
  end;
  return "";
end;

/***********************************************************************\
| Макрофункция                                                          |
\***********************************************************************/
macro unloadDeposit( void )

  var
    str;

  rewind(depositr);
  clearrecord(depositr);
  depositr.Referenc = sbdepdoc.Referenc;
  if(getEQ(depositr))
     str = sbdepdoc.Type_Account +          DELIM +
           Acc_Form(depositr.Account) +     DELIM +
           string(GetIntNum(depositr.Code_Currency)) + DELIM +
           string(depositr.Number) +        DELIM +
           string(depositr.GroupNumb) +     DELIM +
           string(depositr.CodClient) +     DELIM +
           string(depositr.Oper) +          DELIM +
           string(depositr.Sum_Rest) +      DELIM +
           string(depositr.Open_Date) +     DELIM +
           string(depositr.Start_DateDep) + DELIM +
           string(depositr.End_DateDep) +   DELIM +
           string(depositr.Action);
        
     return insert( extrn_dep, str );
  else
    return false;
  end;

end;

/***********************************************************************\
| Макрофункция                                                          |
\***********************************************************************/
macro unloadClient( void )

  var
    depclnt,
    str;

  if ( ClientList.GetRecord( sbdepdoc.CodClient ) )
     depclnt = ClientList.CurRec;
     depclnt.GetPersnCard();
     str = string(depclnt.rec.CodClient) +  DELIM +
           Acc_Form(depositr.Account) + DELIM +
           depclnt.rec.Name1 +          DELIM +
           depclnt.rec.Name2 +          DELIM +
           depclnt.rec.Name3 +          DELIM +
           string(depclnt.rec.Born) +   DELIM +
           curestr(depclnt.rec.Address) + DELIM +
           string(depclnt.rec.PaperKind) + DELIM +
           depclnt.rec.PaperSeries +    DELIM +
           depclnt.rec.PaperNumber +    DELIM +
           depclnt.rec.PaperIssuer +    DELIM +
           iszero(depclnt.rec.NotResident) + DELIM +
           0 +                          DELIM +  
           string( codeFor( depclnt.rec.Kategor ) ) + DELIM +
           string(depclnt.rec.Groupauthor)+  DELIM +
           string(depclnt.rec.Groupwill)+   DELIM +
           depclnt.rec.SocialNumber +       DELIM +
           string(depclnt.rec.SocialDate);
        
     return insert( extrn_cln, str );
  else
    return false;
  end;

end;

/***********************************************************************\
| Макрофункция                                                          |
\***********************************************************************/
macro unloadDoc( void )

  var
    str, LinkFlag = complex_oper_main_doc();
  if ((sbdepdoc.InSum==0) and (sbdepdoc.OutSum==0))
     return true;
  end;
  if ((SBDEPDOC.KindOp == D_AR) or
      (SBDEPDOC.KindOp == D_ARC) or
      (SBDEPDOC.KindOp == D_AUDIT_STORN) or
      (SBDEPDOC.KindOp == D_AUDIT_DELETE) or
      (SBDEPDOC.KindOp == D_AUDIT_CORRECT) or
      (SBDEPDOC.Action == D_DELETE))
      if (SBDEPDOC.TypeOper!=78)
         return true;
      end;
  end;
  str = Acc_Form(sbdepdoc.Account) +        DELIM +
        string(sbdepdoc.CodClient)   +      DELIM +
        string(sbdepdoc.Oper) +             DELIM +
        string(sbdepdoc.DepDate_Document) + DELIM +
        string(sbdepdoc.Date_Document) +    DELIM +
        string(sbdepdoc.TypeOper) +         DELIM +
        string(sbdepdoc.ApplType) +         DELIM +
        string(sbdepdoc.TypeComplexOper) +  DELIM +
        string(sbdepdoc.VidDoc) +           DELIM +
        string(sbdepdoc.InSum) +            DELIM +
        string(sbdepdoc.OutSum) +           DELIM +
        string(sbdepdoc.PercOprSum) +       DELIM +
        string(sbdepdoc.Rest) +             DELIM +
        string(sbdepdoc.PercRest) +         DELIM +
        string(sbdepdoc.NumDayDoc) +        DELIM +
        LinkFlag +                          DELIM +
        string(sbdepdoc.Action)+            DELIM +
        string(sbdepdoc.ObjectPerc) +       DELIM +
        curestr(sbdepdoc.Ground);
        numdoc=numdoc+1;
  return insert( extrn_doc, str );

end;

/***********************************************************************\
| Макрофункция                                                          |
\***********************************************************************/
macro createSession( void )

var 
  stat,i=0;

  rewind(sbdepdoc);
  clearrecord(sbdepdoc);
  sbdepdoc.Date_Document = sesDate;
  stat = getGE(sbdepdoc);
  while(stat)
    ReWind(sb);
    Copy(sb,sbdepdoc);
    if (not GetEQ(sb))
       [Не найдена операция];
    else
       if((sbdepdoc.FNCash == branch) and (sbdepdoc.Date_Document <= DateEnd))
        /* Счета и вкладчиков выгружаем только по факту док-та об открытии счёта! */
           if (not Prev(sb))
              unloadDeposit();
              unloadClient();
           else;
              if(sbdepdoc.Referenc != sb.Referenc)
                  unloadDeposit();
                  unloadClient();
              end;
           end;
           unloadDoc();
           i = i + 1;
           message("Выгружено "+string(i)+" документов");
       end;
    end;
    stat = ((next(sbdepdoc)) and (sbdepdoc.Date_Document<=DateEnd));
  end;
end;

/***********************************************************************\
|  1999(c) Copyright R-Style ShareWare Lab.                             |
|          Unauthorised duplication in any form prohibited              |
\***********************************************************************/

/*
                        M A I N    R O U T I N E
*/

 var
   extrn_depName, extrn_docName, extrn_clnName,
   stat, i, tmp, impDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\EXPORTMESDIR", true );

twodate.BeginDate = {curdate};
twodate.EndDate = {curdate};
Stat = RunDialog(twodate);
if(Stat)
  sesDate = twodate.BeginDate;
  DateEnd = twodate.EndDate;
end;

 /* Выбор даты и номера сессии */
 if( stat )
    if( GetInt( numsession, "Номер сессии:", 4 ) )
       if(   ( numsession < 1 ) or ( numsession > 9999 )   )                                                                                            
          msgbox( "Недопустимый номер сессии!" );                                                                                                       
          exit( 1 );                                                                                                                                    
       end;                                                                                                                                             
       tmp = string( branch:2, numsession:4 );                                                                                                          
       while ( ( i = index( tmp, " " ) ) )                                                                                                              
         strset( tmp, i, "0" );                                                                                                                         
       end;                                                                                                                                             
       extrn_depName = impDir + tmp + "00.txt";                                                                                                         
       extrn_docName = impDir + tmp + "01.txt";                                                                                                         
       extrn_clnName = impDir + tmp + "03.txt";                                                                                                         
                                                                                                                                                        
       stat = open( extrn_dep, extrn_depName );                                                                                                       
       if( stat )                                                                                                                                       
          stat = open( extrn_doc, extrn_docName );                                                                                                    
          if( stat )                                                                                                                                    
             stat = open( extrn_cln, extrn_clnName );                                                                                                 
             if( not stat )                                                                                                                             
                tmp = extrn_clnName;                                                                                                                    
             end;                                                                                                                                       
          else                                                                                                                                          
             tmp = extrn_docName;                                                                                                                       
          end;                                                                                                                                          
       else           
          tmp = extrn_depName;                                                                                                                          
       end;                                                                                                                                             
                                                                                                                                                        
       if( stat )                                                                                                                                       
          createSession();                                                                                                                                              
       else                                                                                                                                             
          msgbox( "Ошибка создания файла " + tmp);                                                                                                      
          exit( 1 );                                                                                                                                    
       end;                                                                                                                                             
    end;
 end;
[Сессия #### выгружена](numsession);
[Выгружено ######## документов](numdoc);
end.
