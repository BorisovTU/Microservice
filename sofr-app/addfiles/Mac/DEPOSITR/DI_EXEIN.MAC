/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Страхование частных вкладов.

  Выпуск Реестра обязательств в промежуточной форме.

*****************************************************************/

 import DeprIntr, Define, DI_Def, DI_PapTp, SQLConv, SQLExec;

 file dnm ( depimake ) key 0;
 file dns ( depisum  ) key 0;
 file dpl ( depiprev ) key 1;
 file dpw ( depiprev ) key 1;

 var elDepClnt = TClientList; /* Справочник вкладчиков */

 FILE f_vkl ( "VKL" ) txt 1024 write;
 FILE f_crd ( "CRD" ) txt 1024 write;
 FILE f_ctr ( "CTR" ) txt 1024 write;

 private const ENCODING = "rsansi";

 var ExportDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\EXPORTMESDIR", TRUE );

 var sNumBranch = "";
 var nNumBranch = String( NumFNCash() );
 var lNumBranch = 0;

 var FN_VKL = ExportDir + "vkl_0000.txt";
 var FN_CRD = ExportDir + "crd_0000.txt";
 var FN_CTR = ExportDir + "ctr_0000.txt";

 private var {Name_Bank};
 private var {Post_Addr};
 var {NumberBranch};

 var save_CodClient;

 var stat_i;

 /********************************************************************/
 /*          Представление строчного формата даты ДД.ММ.ГГГГ         */
 /********************************************************************/
 macro DateStrDD( pDate )
   var sDate = String( pDate );
   if ( SubStr( sDate, 1, 1 ) == " " )
     sDate = "0" + Trim( sDate );
   end;
   return sDate;
 end;

 /********************************************************************/
 /*                Формирование записи о клиенте.                    */
 /********************************************************************/
 macro ClientLine( rDateMake )

   var stat_w;

   /* Атрибуты клиента. */
   var elName3;
   var elFIO;
   var elAddress;
   var elPapers;

   if ( ( save_CodClient != 0 )  AND  ( elDepClnt.GetRecord( save_CodClient ) ) )
     if ( StrLen( elDepClnt.CurRec.rec.Name3 ) > 0 )
       elName3 = elDepClnt.CurRec.rec.Name3;
     else
       elName3 = "Отсутствует";
     end;
     elFIO = elDepClnt.CurRec.rec.Name1 + "^" +
             elDepClnt.CurRec.rec.Name2 + "^" +
             elName3;
     elAddress = elDepClnt.CurRec.rec.Address;

     if ( StrLen( elAddress ) == 0 )
       elAddress = "000000 Отсутствует";
     end;

     if ( ( StrLen( elDepClnt.CurRec.rec.PaperSeries ) != 0 )  AND
          ( StrLen( elDepClnt.CurRec.rec.PaperNumber ) != 0 )  AND
          ( StrLen( elDepClnt.CurRec.rec.PaperIssuer ) != 0 ) )
       elPapers = DI_PaperTypes1417( elDepClnt.CurRec.rec.PaperKind,
                                     elDepClnt.CurRec.rec.NotResident ) + "^" +
                  elDepClnt.CurRec.rec.PaperSeries + " № " +
                  elDepClnt.CurRec.rec.PaperNumber + ", " +
                  elDepClnt.CurRec.rec.PaperIssuer + ", " +
                  elDepClnt.CurRec.rec.PaperIssuedDate;
     else
       elPapers = "14^00 № 000000 выдан \"00.00.0000\" подразделение отсутствует";
     end;
   else
     elFIO     = "^^";
     elAddress = "000000 Отсутствует";
     elPapers  = "14^00 № 000000 выдан \"00.00.0000\" подразделение отсутствует";
   end;

   /* Цикл по записям клиента. */
   ReWind( dpw );
   ClearRecord( dpw );
   dpw.DateMake  = rDateMake;
   dpw.CodClient = save_CodClient;
   stat_w = GetGE( dpw );

   while ( stat_w  AND
           ( dpw.DateMake  == rDateMake )       AND
           ( dpw.CodClient == save_CodClient ) )

     /* Вкладная запись. */
     if ( dpw.Type == TYPE_DEPOS )
       Insert( f_vkl, elFIO                    + "^" +
                      elAddress                + "^" +
                      "000000 Отсутствует"     + "^" +
                      "Отсутствует"            + "^" +
                      elPapers                 + "^" +
                      dpw.NumDoc               + "^" +
                      DateStrDD( dpw.DateDoc ) + "^" +
                      Acc_Form( dpw.Account )  + "^" +
                      String( dpw.SummaRest )  + "^" +
                      String( dpw.SummaRestRUR ) );
     end;

     /* Кредитовая запись. */
     if ( ( dpw.Type == TYPE_CR_LOAN )  OR
          ( dpw.Type == TYPE_CR_PERC )  OR
          ( dpw.Type == TYPE_CR_OVER )  OR
          ( dpw.Type == TYPE_CR_OVERPERC ) )
       Insert( f_crd, elFIO                    + "^" +
                      elAddress                + "^" +
                      "000000 Отсутствует"     + "^" +
                      "Отсутствует"            + "^" +
                      elPapers                 + "^" +
                      dpw.NumDoc               + "^" +
                      DateStrDD( dpw.DateDoc ) + "^" +
                      Acc_Form( dpw.Account )  + "^" +
                      String( dpw.SummaRest )  + "^" +
                      String( dpw.SummaRestRUR ) );
     end;

     stat_w = Next( dpw );

   end;

 end;

 /********************************************************************/
 /*                        Основная процедура.                       */
 /********************************************************************/

 macro DI_Mediate( pDateMake )

   var vCountRec;
   var f_Find;  /* Флаг: записи найдены \ не найдены. */
   var vRec = 0;

   if ( pDateMake == Date( 0, 0, 0 ) )
     pDateMake = {CurDate};
     if ( NOT GetDate( pDateMake, "Укажите дату выпуска Реестра" ) )
       Exit( 1 );
     end;
   end;

   if ( pDateMake == Date( 0, 0, 0 ) )
     println( "Не задана дата." );
     Exit();
   end;

   /*** Формирование имен файлов промежуточного формата. ***/

   lNumBranch = StrLen( nNumBranch );
   if ( lNumBranch < 4 )
     sNumBranch = SubStr( "0000", 1, 4 - lNumBranch ) + nNumBranch;
   else
     sNumBranch = SubStr( nNumBranch, 1, 4 );
   end;

   FN_VKL = ExportDir + "vkl_" + sNumBranch + ".txt";
   FN_CRD = ExportDir + "crd_" + sNumBranch + ".txt";
   FN_CTR = ExportDir + "ctr_" + sNumBranch + ".txt";

   /*** Файл обязательств. ***/

   if ( ExistFile( FN_VKL, 0 ) )
     Close( f_vkl );
     DelFile( FN_VKL );
   end;

   if ( NOT Open( f_vkl, FN_VKL, ENCODING ) )
     println( "Ошибка открытия файла: ", FN_VKL );
     Exit();
   end;

   Insert( f_vkl, reg_Num_Bank() );

   /*** Файл требований. ***/

   if ( ExistFile( FN_CRD, 0 ) )
     Close( f_crd );
     DelFile( FN_CRD );
   end;

   if ( NOT Open( f_crd, FN_CRD, ENCODING ) )
     println( "Ошибка открытия файла: ", FN_CRD );
     Exit();
   end;

   Insert( f_crd, reg_Num_Bank() );

   /*** Цикл по клиентам. ***/

   vCountRec = NRecordsSQL( "depiprev.dbt", "t_DateMake = " + sqlDateToStr( pDateMake ) );

   ReWind( dpl );
   ClearRecord( dpl );
   dpl.DateMake = pDateMake;
   stat_i = GetGE( dpl );

   if ( stat_i  AND  ( dpl.DateMake == pDateMake )  AND  ( vCountRec > 0 ) )

     InitProgress( vCountRec, " Ж Д И Т Е ...",
                              "Формирование промежуточного Реестра ..." );

     vRec = 0;

     f_Find = TRUE;

     save_CodClient = dpl.CodClient;

     while ( stat_i  AND  ( dpl.DateMake == pDateMake ) )

       UseProgress( vRec );
       vRec = vRec + 1;

       if ( save_CodClient != dpl.CodClient )
         ClientLine( pDateMake );
         save_CodClient = dpl.CodClient;
       end;

       stat_i = Next( dpl );

     end;

     ClientLine( pDateMake );

     RemProgress();

   else

     f_Find = FALSE;

   end;

   /*** Файл контрольной информации. ***/

   if ( ExistFile( FN_CTR, 0 ) )
     Close( f_ctr );
     DelFile( FN_CTR );
   end;

   if ( NOT Open( f_ctr, FN_CTR, ENCODING ) )
     println( "Ошибка открытия файла: ", FN_CTR );
     Exit();
   end;

   Insert( f_ctr, reg_Num_Bank() + "^" +
                  {Name_Bank}   + "^" +
                  {Post_Addr} );
   Insert( f_ctr, DateStrDD( pDateMake ) );

   ReWind( dnm );
   ClearRecord( dnm );
   dnm.DateMake = pDateMake;
   if ( GetEQ( dnm ) )
     Insert( f_ctr, String( dnm.NumDebet  ) );
     Insert( f_ctr, String( dnm.NumCredit ) );
   else
     println( " *** Не найдена сводная запись за ", pDateMake );
     Exit();
   end;

   ReWind( dns );
   ClearRecord( dns );
   dns.DateMake = pDateMake;
   stat_i = GetGE( dns );

   while ( stat_i  AND  ( dns.DateMake == pDateMake ) )
     Insert( f_ctr, GetCurrNumericalCode( dns.Currency ) + "^" +
                    String( dns.SumDebet:19:2  ) + "^" +
                    String( dns.SumCredit:19:2 ) );
     stat_i = Next( dns );
   end;

   Close( f_ctr );
   Close( f_crd );
   Close( f_vkl );

   if ( f_Find )
     PrintLn( "" );
     PrintLn( "   Выпуск Реестра обязательств в промежуточном формате за ",
              pDateMake );
     PrintLn( "" );
     PrintLn( "   Файл обязательств банка:     ", FN_VKL );
     PrintLn( "   Файл требований   банка:     ", FN_CRD );
     PrintLn( "   Файл контрольной информации: ", FN_CTR );
   else
     PrintLn( "" );
     PrintLn( "   Не найдено записей за ", pDateMake );

     DelFile( FN_VKL );
     DelFile( FN_CRD );
   end;

 end;
