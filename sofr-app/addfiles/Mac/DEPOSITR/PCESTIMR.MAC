/*
$Name:           PCESTIMR.mac
$Module:         RETAIL
$Description:    Отчет о накопленных процентах, отраженных по балансу, за период
*/

/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Отчет о накопленных процентах, отраженных по балансу, за период         *
*                                                                          *
*  Лебедев С.В.                                                            *
*  01.10.1999                                                              *
\**************************************************************************/

import DeprIntr, alg_vars;

/* Панель ввода исходных данных --------------------------- */
record InputPanel ( pcestim ) dialog;
  /*--------------------------------*/
  InputPanel.BegDate      = {curdate};
  InputPanel.EndDate      = {curdate};
  InputPanel.TypeAccount  = "";
  InputPanel.CurrencyName = "";
  InputPanel.TypeReport   = "";
  /*--------------------------------*/
  const ne_BegDate     = 0;
  const ne_EndDate     = 1;
  const ne_TypeAccount = 2;
  const ne_CurencyName = 3;
  const ne_TypeReport  = 4;
  /*--------------------------------*/
  var CodCurrency = -1;

/* Файлы для работы программы ----------------------------- */
file currency ( currency ) key 0; /* Справочник валют */
file sb_dtyp  ( sb_dtyp  ) key 1; /* Виды вкладов     */
private var depositr = TBFile( "depositr.dbt", "r", 6 );
private var pcestim  = TBFile( "pcestim.dbt", "r", 0 );
private var sbdepdoc = TBFile( "sbdepdoc.dbt", "r", 6 );

var depclnt = TClientList;

/* Переменные для работы программы ------------------------ */
var FlagCur = NumFlagCur( );
var FNCash  = NumFNCash( );
var {IDBank};
var {IDDep};

var NeedEstAcc = false; /* В подробный отчет печатать счет процентов */

var SumEstmForTypeRez  = $0.0L;
var SumEstmForTypeNRez = $0.0L;
var SumEstmForCurrency = $0.0L;
var SumCalcForTypeRez  = $0.0L;
var SumCalcForTypeNRez = $0.0L;
var SumCalcForCurrency = $0.0L;
var SumRestForTypeRez  = $0.0L;
var SumRestForTypeNRez = $0.0L;
var SumRestForCurrency = $0.0L;

var WasAccountForType  = false;


/**************************************************************************\
|                               Шапка отчета                               |
\**************************************************************************/
macro HeadReport

  var nameBranch = "";
  var i_dp_dep = TRecHandler( "i_dp_dep.rec" );
  var str;

  if ( FindDepartment( FNCash, NULL, i_dp_dep ) )
    nameBranch = i_dp_dep.rec.Name;
  end;

  [ ];
  str = "Отчет об отраженных в балансе накопленных процентах для подразделения " + NameBranch;
  [ #########################################################################################################](str:c);

  if (InputPanel.TypeAccount != "")
    str = "по вкладу " + InputPanel.TypeAccount;
    [ #########################################################################################################](str:c);
  end;

  if ( ( InputPanel.BegDate == Date( 0, 0, 0 ) ) and 
       ( InputPanel.BegDate == Date( 0, 0, 0 ) )    )
    str = "(текущее состояние)";
  else
    if (InputPanel.BegDate == InputPanel.EndDate)
      str = "за дату " + String(InputPanel.EndDate:f);
    else
      str = "с " + String(InputPanel.BegDate:f) + " по " + String(InputPanel.EndDate:f);
    end;
  end;
  [ #########################################################################################################](str:c);

end;


/**************************************************************************\
|                          Шапка отчета по валюте                          |
\**************************************************************************/
macro HeadReportForCur

  [ ];
  [ Валюта: #](currency.ExternalCode + " " + currency.Name_Currency);
  [ ];
  if (InputPanel.TypeReport == "")
    [ =========================================================================================================];
    [ : Вид вклада                   : Остаток на вкладе      : Отраженные проценты   : Рассчитанные проценты :];
    [ =========================================================================================================];
  else
    [ =========================================================================================================];
    [ : Номер счета                  : Остаток на счете : Отраженные  : Дата посл. : Рассчитанные: Дата посл. :];
    [ :                              :                  : проценты    : отражения  : проценты    : расчета    :];
    [ =========================================================================================================];
  end;

end;


/**************************************************************************\
|                        Напечатать итог по валюте                         |
\**************************************************************************/
macro WriteSummaryForCur

  if (InputPanel.TypeReport == "")
    [ =========================================================================================================];
    [ : Всего по валюте              : ###################### : ##################### : ##################### :]
    (SumRestForCurrency:22:2:a,SumEstmForCurrency:21:2:a,SumCalcForCurrency:21:2:a);
  else
    [ : Всего по валюте              :################# :############ :            :############ :            :]
    (SumRestForCurrency:17:2:a,SumEstmForCurrency:12:2:a,SumCalcForCurrency:12:2:a);
  end;
  [ =========================================================================================================];

  [ ];

end;


/**************************************************************************\
|                          Шапка отчета по валюте                          |
\**************************************************************************/
macro WriteNameType

  [ : ##################################################################################################### :]
  (sb_dtyp.Name);
  [ ---------------------------------------------------------------------------------------------------------];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefAccountNumber

  var retAccount = "";

  if ( NeedEstAcc )
    retAccount = DefPcEstimAccountForDepositr( depositr, true );
  else
    retAccount = depositr.rec.Account;
  end;

  return retAccount;

end;


/**************************************************************************\
|                    Написать информацию о счете в отчет                   |
\**************************************************************************/
macro WriteAccountToReport ( RestOnAccount, PayEstim, DateLastPay, CalcEstim, DateLastCalc, FlagRezid )

  var NumAccount = DefAccountNumber( );

  [ : #############################:################# :############ : ########## :############ : ########## :]
  ( Acc_Form(NumAccount):f, RestOnAccount:17:2:a, PayEstim:12:2:a, DateLastPay:f, CalcEstim:12:2:a, DateLastCalc:f );

end;


/**************************************************************************\
|                       Написать итог по виду вклада                       |
\**************************************************************************/
macro WriteSummaryForType

  if (InputPanel.TypeReport == "")
    [ : ############################ : ###################### : ##################### : ##################### :]
    (sb_dtyp.Kind,SumRestForTypeRez:22:2:a,SumEstmForTypeRez:21:2:a,SumCalcForTypeRez:21:2:a);

    if ((SumEstmForTypeNRez != 0) OR (SumRestForTypeNRez != 0))
      [ : ############################ : ###################### : ##################### : ##################### :]
      (sb_dtyp.Kind + " (н/р)",SumRestForTypeNRez:22:2:a,SumEstmForTypeNRez:21:2:a,SumCalcForTypeNRez:21:2:a);
    end;
  else
    [ ---------------------------------------------------------------------------------------------------------];
    [ : Итого по вкладу              :################# :############ :            :############ :            :]
    (SumRestForTypeRez:17:2:a,SumEstmForTypeRez:12:2:a,SumCalcForTypeRez:12:2:a);

    if ((SumEstmForTypeNRez != 0) OR (SumRestForTypeNRez != 0))
      [ :                       (н/р)  :################# :############ :            :############ :            :]
      (SumRestForTypeNRez:17:2:a,SumEstmForTypeNRez:12:2:a,SumCalcForTypeNRez:12:2:a);
    end;
    [ =========================================================================================================];
  end;

end;


/**************************************************************************\
|                       Определение остатка на счете                       |
\**************************************************************************/
macro DefRest

  var restOnAccount = $0.0L;
  var stat;

  const D_AR            =  8;  /* Проводка в архиве      */
  const D_ARC           =  9;  /* Корректировка в архиве */
  const D_AUDIT_STORN   = 14;  /* Сторнирование операции */
  const D_AUDIT_DELETE  = 15;  /* Удаление операции      */
  const D_AUDIT_CORRECT = 16;  /* Корректировка операции */
  const D_DELETE        =  2;  /* Запись удалена         */

  if ( ( InputPanel.BegDate == Date( 0, 0, 0 ) ) and 
       ( InputPanel.EndDate == Date( 0, 0, 0 ) )    )
    RestOnAccount = depositr.rec.Sum_Rest;
  else
    sbdepdoc.addFilter( "t.t_Referenc = " + String( depositr.rec.Referenc ) );
    sbdepdoc.Clear;
    sbdepdoc.rec.Referenc      = depositr.rec.Referenc;
    sbdepdoc.rec.Date_Document = InputPanel.EndDate + 1;
    sbdepdoc.rec.NumDayDoc     = 0;
    stat = sbdepdoc.GetLT;
    while ( stat )
      if ( sbdepdoc.rec.Referenc == depositr.rec.Referenc )
        if ( ( sbdepdoc.rec.KindOp   == D_AUDIT_STORN   ) or
             ( sbdepdoc.rec.KindOp   == D_AUDIT_DELETE  ) or
             ( sbdepdoc.rec.KindOp   == D_AUDIT_CORRECT ) or
             ( sbdepdoc.rec.KindOp   == D_AR            ) or
             ( sbdepdoc.rec.KindOp   == D_ARC           ) or
             ( sbdepdoc.rec.Action   == D_DELETE        ) or
             ( ( sbdepdoc.rec.IsSuspended != "" ) and ( sbdepdoc.rec.IsSuspended != "X" ) ) or
             ( sbdepdoc.rec.TypeOper == 38              ) or
             ( sbdepdoc.rec.TypeOper == 39              )   )
          stat = sbdepdoc.Prev;
        else
          RestOnAccount = sbdepdoc.rec.Rest;
          stat          = false;
        end;
      else
        stat = false;
      end;
    end;
    sbdepdoc.dropFilter;
  end;

  return restOnAccount;

end;


/**************************************************************************\
|            Определение суммы отраженных накопленных процентов            |
\**************************************************************************/
macro DefPayEstim (PayEstim, DateLastPay, CalcEstim, DateLastCalc)
  
  var stat;
  var find_last = false;

  PayEstim     = $0L;
  DateLastPay  = Date( 0, 0, 0 );
  CalcEstim    = $0L;
  DateLastCalc = Date( 0, 0, 0 );

  pcestim.addFilter( "t.t_Referenc = " + String( depositr.rec.Referenc ) );

  /* Обе даты нулевые - последнее состояние --------------------------- */
  if ( ( InputPanel.BegDate == Date( 0, 0, 0 ) ) and 
       ( InputPanel.EndDate == Date( 0, 0, 0 ) )    )
    pcestim.Clear;
    pcestim.rec.Referenc   = depositr.rec.Referenc;
    pcestim.rec.DateOperat = Date( 31, 12, 9999 );
    pcestim.rec.NumDayDoc  = 32000;
    stat = pcestim.GetLE;
    while ( stat )
      if ( pcestim.rec.Referenc != depositr.rec.Referenc )
        stat = false;
      else
        if ( pcestim.rec.Action < 2 )
          find_last = true;
          stat = false;
        else
          stat = pcestim.Prev;
        end;
      end;
    end;

    if ( not find_last )
      pcestim.Clear;
    end;

    DateLastPay  = pcestim.rec.DateLastPay;
    PayEstim     = pcestim.rec.SummaPay_Rest;
    DateLastCalc = pcestim.rec.DateLastCalc;
    CalcEstim    = pcestim.rec.SummaCalc;

  /* Состояние на дату или интервал ----------------------------------- */
  else
    pcestim.Clear;
    pcestim.rec.Referenc   = depositr.rec.Referenc;
    pcestim.rec.DateOperat = InputPanel.EndDate + 1;
    pcestim.rec.NumDayDoc  = 0;
    stat = pcestim.GetLT;
    while ( stat )
      if ( pcestim.rec.Referenc != depositr.rec.Referenc )
        stat = false;
      else
        if ( pcestim.rec.Action < 2 )
          find_last = true;
          stat = false;
        else
          stat = pcestim.Prev;
        end;
      end;
    end;

    if ( not find_last )
      pcestim.Clear;
    end;

    DateLastCalc = pcestim.rec.DateLastCalc;
    CalcEstim    = pcestim.rec.SummaCalc;

    if ( ( pcestim.rec.DateLastCalc != Date( 0, 0 ,0 ) ) and ( pcestim.rec.DateEndPeriod != Date( 0, 0, 0 ) ) )
      if ( pcestim.rec.DateLastCalc == InputPanel.EndDate )
        ;
      elif ( pcestim.rec.DateEndPeriod == InputPanel.EndDate )
        CalcEstim = pcestim.rec.SummaProgn;
      elif ( ( pcestim.rec.DateEndPeriod > InputPanel.EndDate ) and ( pcestim.rec.DateEndPeriod > pcestim.rec.DateLastCalc ) )
        CalcEstim = pcestim.rec.SummaCalc + MoneyL( ( pcestim.rec.SummaProgn - pcestim.rec.SummaCalc ) * ( InputPanel.EndDate - pcestim.rec.DateLastCalc ) / ( pcestim.rec.DateEndPeriod - pcestim.rec.DateLastCalc ) );
      end;
    end;

    if ( InputPanel.BegDate == InputPanel.EndDate )
      DateLastPay  = pcestim.rec.DateLastPay;
      PayEstim     = pcestim.rec.SummaPay_Rest;
    else
      pcestim.Clear;
      pcestim.rec.Referenc   = depositr.rec.Referenc;
      pcestim.rec.DateOperat = InputPanel.BegDate;
      pcestim.rec.NumDayDoc  = 0;
      stat = pcestim.GetGE;
      while (stat)
        if ( ( pcestim.rec.Referenc == depositr.rec.Referenc ) and ( pcestim.rec.DateOperat <= InputPanel.EndDate ) )
          if ( ( ( pcestim.rec.TypeOper == 38 ) or 
                 ( pcestim.rec.TypeOper == 39 )   ) and 
               (   pcestim.rec.Action   <   2     )    )
            PayEstim = PayEstim + pcestim.rec.SummaPay_In - pcestim.rec.SummaPay_Out;
            DateLastPay  = pcestim.rec.DateLastPay;
          end;
          stat = pcestim.Next;
        else
          stat = false;
        end;
      end;
    end;

  end;

  pcestim.dropFilter;

  SetParm( 0, PayEstim );
  SetParm( 1, DateLastPay );
  SetParm( 2, CalcEstim );
  SetParm( 3, DateLastCalc );

end;


/**************************************************************************\
|                     Определение резидентности счета                      |
\**************************************************************************/
macro DefRezid

  var FlagRezid = true;

  if ( depclnt.GetRecord( depositr.rec.CodClient ) )
    if ( depclnt.CurRec.rec.NotResident != "" )
      FlagRezid = false;
    end;
  end;

  return FlagRezid;

end;


/**************************************************************************\
|            Обработка счетов одного вида вклада и одной валюты            |
\**************************************************************************/
macro WorkWithAccountsOneTypeAndCurr

  var stat;
  var Rest;
  var PayEstim;
  var DateLastPay;
  var CalcEstim;
  var DateLastCalc;
  var Rezid;
  var flag;
  var Counter = 0;

  WasAccountForType  = false;
  SumEstmForTypeRez  = $0.0L;
  SumEstmForTypeNRez = $0.0L;
  SumCalcForTypeRez  = $0.0L;
  SumCalcForTypeNRez = $0.0L;
  SumRestForTypeRez  = $0.0L;
  SumRestForTypeNRez = $0.0L;

  depositr.Clear;
  depositr.rec.IsCur        = FlagCur;
  depositr.rec.FNCash       = FNCash;
  depositr.rec.Type_Account = sb_dtyp.Kind;
  depositr.rec.End_DateDep  = Date(0,0,0);

  stat = depositr.GetGE;
  while ( stat )
    if ( ( depositr.rec.IsCur        == FlagCur      ) and
         ( depositr.rec.FNCash       == FNCash       ) and
         ( depositr.rec.Type_Account == sb_dtyp.Kind )    )

      if ( ( InputPanel.BegDate == Date( 0, 0, 0 ) ) and 
           ( InputPanel.EndDate == Date( 0, 0, 0 ) )    )
        if ( ( depositr.rec.Code_Currency == currency.Code_Currency ) and
             ( depositr.rec.Open_Close    == ""                     )    )
          flag = true;
        else
          flag = false;
        end;
      else
        if ( (   depositr.rec.Code_Currency == currency.Code_Currency    ) and
             (   depositr.rec.Open_Date     <= InputPanel.EndDate        ) and
             ( ( depositr.rec.Close_Date    >= InputPanel.BegDate    ) or 
               ( depositr.rec.Close_Date    == Date( 0, 0, 0 )       )   )    )
          flag = true;
        else
          flag = false;
        end;
      end;

      if ( flag )
        Counter = Counter + 1;
        Message(" Вклад ",sb_dtyp.Kind," (",currency.Short_Name,") . Обрабатывается ",Counter:5," счет ...");

        if ( ( WasAccountForType      == false ) AND
             ( InputPanel.TypeReport  != ""    ) AND
             ( InputPanel.TypeAccount == ""    )    )
          WriteNameType( );
        end;

        WasAccountForType = true;

        Rest     = DefRest( );
        DefPayEstim( PayEstim, DateLastPay, CalcEstim, DateLastCalc );
        Rezid    = DefRezid( );

        if ( InputPanel.TypeReport != "" )
          WriteAccountToReport( Rest, PayEstim, DateLastPay, CalcEstim, DateLastCalc, Rezid );
        end;

        if ( Rezid == true )
          SumEstmForTypeRez  = SumEstmForTypeRez  + PayEstim;
          SumCalcForTypeRez  = SumCalcForTypeRez  + CalcEstim;
          SumRestForTypeRez  = SumRestForTypeRez  + Rest;
        else
          SumEstmForTypeNRez = SumEstmForTypeNRez + PayEstim;
          SumCalcForTypeNRez = SumCalcForTypeNRez + CalcEstim;
          SumRestForTypeNRez = SumRestForTypeNRez + Rest;
        end;
        SumEstmForCurrency = SumEstmForCurrency + PayEstim;
        SumCalcForCurrency = SumCalcForCurrency + CalcEstim;
        SumRestForCurrency = SumRestForCurrency + Rest;
      end;
      stat = depositr.Next;
    else
      stat = false;
    end;
  end;

  if ( WasAccountForType )
    WriteSummaryForType( );
  end;

end;


/**************************************************************************\
|            Выпуск отчета по отраженным накопленным процентам             |
\**************************************************************************/
macro DoReport

  var stat;
  var flag;

  if (FlagCur == 0)
    CodCurrency = 0;
  end;

  if (CodCurrency == -1)
    currency.Code_Currency = 1;
    stat = GetGE(currency);
  else
    currency.Code_Currency = CodCurrency;
    stat = GetEQ(currency);
  end;

  HeadReport();

  while (stat)

    SumEstmForCurrency = $0.0L;
    SumCalcForCurrency = $0.0L;
    SumRestForCurrency = $0.0L;

    HeadReportForCur();

    sb_dtyp.FlagCur = FlagCur;
    sb_dtyp.Priorit = 0;
    flag = GetGE(sb_dtyp);
    while (flag)
      if (sb_dtyp.FlagCur == FlagCur)
        if (sb_dtyp.CalcAddPc != "")
          if ((InputPanel.TypeAccount == ""          ) OR
              (InputPanel.TypeAccount == sb_dtyp.Kind)   )
            WorkWithAccountsOneTypeAndCurr( );
          end;
        end;
        flag = Next(sb_dtyp);
      else
        flag = false;
      end;
    end;

    WriteSummaryForCur();

    if (CodCurrency == -1)
      stat = Next(currency);
    else
      stat = false;
    end;
  end;

end;


/**************************************************************************\
|                        Справочник по видам вклада                        |
\**************************************************************************/
macro ListTypeAccount

  array AMenu;
  array AType;
  var stat;
  var i;

  /* Формируем список */
  sb_dtyp.FlagCur = FlagCur;
  sb_dtyp.Priorit = 0;
  stat = GetGE(sb_dtyp);
  while ( ( sb_dtyp.FlagCur == FlagCur ) and stat )
    if ( sb_dtyp.CalcAddPc != "" )
      i = ASize(AType);
      AType(i) = sb_dtyp.Kind;
      AMenu(i) = " " + SubStr(sb_dtyp.Kind + MkStr(" ",13),1,13) + " " + sb_dtyp.Name + " ";
    end;
    stat = Next( sb_dtyp );
  end;

  /* Выбор */
  if (ASize(AMenu))
    i = Menu(AMenu," ~ESC~ Отказ  ~ENTER~ Выбор","Виды вкладов");
    if (i >= 0)
      InputPanel.TypeAccount = AType(i);
    end;
  end;

end;


/**************************************************************************\
|                          Справочник по валютам                           |
\**************************************************************************/
macro ListCurrency

  array AMenu;
  array AName;
  array ACode;
  var stat;
  var i;

  /* Формируем список */
  currency.Code_Currency = 1;
  stat = GetGE(currency);
  while (stat)
    i = ASize(ACode);
    ACode(i) = currency.Code_Currency;
    AName(i) = currency.Short_Name;
    AMenu(i) = " " + SubStr(currency.ExternalCode + MkStr(" ",3),1,3) + " " +
                     SubStr(currency.Short_Name   + MkStr(" ",3),1,3) + " " +
                     currency.Name_Currency                           + " ";
    stat = Next(currency);
  end;

  /* Выбор */
  if (ASize(AMenu))
    i = Menu(AMenu," ~ESC~ Отказ  ~ENTER~ Выбор","Справочник валют");
    if (i >= 0)
      InputPanel.CurrencyName = AName(i);
      CodCurrency             = ACode(i);
    end;
  end;

end;


/**************************************************************************\
|                            Обработчик панели                             |
\**************************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter ---------------------------------------------------------- */
    if (key == 13)
      if (id == ne_TypeReport)
        SetFocus(IP,ne_BegDate);
        stat = CM_IGNORE;
      end;
    /* Esc - Отказ от выпуска отчета ---------------------------------- */
    elif (key == 27)
      stat = CM_CANCEL;
    /* Пробел --------------------------------------------------------- */
    elif (key == 32)
      if   (id == ne_TypeAccount)
        InputPanel.TypeAccount = "";
      elif (id == ne_CurencyName)
        if (FlagCur)
          InputPanel.CurrencyName = "";
          CodCurrency             = -1;
        end;
      elif (id == ne_TypeReport)
        if (InputPanel.TypeReport == "")
           InputPanel.TypeReport = "X";
         else
           InputPanel.TypeReport = "";
         end;
      end;
    /* F3 - Списки ---------------------------------------------------- */
    elif (key == 317)
      if   (id == ne_TypeAccount)
        ListTypeAccount();
      elif (id == ne_CurencyName)
        if (FlagCur)
          ListCurrency();
        end;
      end;
    /* F7 - Выпуск отчета --------------------------------------------- */
    elif (key == 321)
      stat = CM_SAVE;
    /* CtrlF3 - Установка по умолчанию -------------------------------- */
    elif (key == 352)
      if (id == ne_BegDate)
        InputPanel.BegDate = {curdate};
      elif (id == ne_EndDate)
        InputPanel.EndDate = {curdate};
      end;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                 Вызов панели для ввода данных по отчету                  |
\**************************************************************************/
macro ReportForPcEstim

  if ( FlagCur == 0 )
    CodCurrency = 0;
    currency.Code_Currency = CodCurrency;
    if ( GetEQ( currency ) )
      InputPanel.CurrencyName = currency.Short_Name;
    else
      CodCurrency = -1;
    end;
  end;

  if ( RunDialog( InputPanel, "WorkDialog" ) )
    DoReport( );
  else
    Exit( 1 );
  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я    П Р О Г Р А М М А                    |
\**************************************************************************/

  ReportForPcEstim( );

end;
