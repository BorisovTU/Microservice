/*

    R-Style SoftLab

    RS-Retail

    Отчет по выпуску карточки ф.1-у при открытии счетов

*/

import DeprIntr;

file prm ( "depparm.dbt"  ) key 0;
file dep ( "depositr.dbt" ) key 2;
file doc ( "sbdepdoc.dbt" ) key 6;

const
  IsCur  = NumFlagCur(),
  Branch = NumFNCash();

var {curdate};

var Prev_Date;
var stat;

 ClearRecord( prm );
 prm.FNCash  = Branch;
 prm.FlagCur = IsCur;
 stat = GetEQ( prm );

 if ( stat )
   Prev_Date = prm.PrevDate;
 else
   MsgBox( "Не найдена запись в depparm.dbt по филиалу ", Branch );
   Exit( 0 );
 end;

 ClearRecord( dep );
 dep.IsCur     = IsCur;
 dep.FNCash    = Branch;
 dep.Open_Date = Prev_Date;
 stat = GetGE( dep );

 if ( ( stat )                        AND
      ( dep.IsCur     == IsCur )      AND
      ( dep.FNCash    == Branch )     AND
      ( dep.Open_Date <  {curdate} ) )
   [
      Счета, открытые с #
         для которых не выпущена карточка ф. 1-у

     |------------------------------|--------------|
     |           Номер счета        |  Вид вклада  |
     |------------------------------|--------------|
   ] ( String(Prev_Date:m) + " по " + String({curdate}:m) + "," );
 end;

 while ( stat                            AND
         ( dep.IsCur     == IsCur )      AND
         ( dep.FNCash    == Branch )     AND
         ( dep.Open_Date >= Prev_Date )  AND
         ( dep.Open_Date <  {curdate} ) )

   ClearRecord( doc );
   doc.Referenc      = dep.Referenc;
   doc.Date_Document = dep.Open_Date;
   doc.NumDayDoc     = 100;
   stat = GetEQ( doc );

   if ( stat )

     if ( GetBitFlag( doc.Flags, 12 ) == 0 )
       println( " | ", Acc_Form(dep.Account):28:f, " | ", dep.Type_Account:13, "|" );
     end;

     stat = Next( dep );
   else
     MsgBox( "Не найден документ открытия счета ", Acc_Form( dep.Account ) );
   end;
 end;

 [ |------------------------------|--------------|];
 