/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады и Прочие операции                                                *
*                                                                          *
*  Выплата вклада бывшего Чеченского СБ                                    *
*                                                                          *
*  Лебедев С.В.                                                            *
*  20.01.2004                                                              *
\**************************************************************************/

import DeprIntr, issrvdoc, sqlconv;

private var chcnrgst = TBFile( "chcnrgst.dbt", "w", 0 );
private var chcn_pay = TBFile( "chcn_pay.dbt", "w", 1 );
private var sb_dtyp  = TBFile( "sb_dtyp.dbt",  "r", 2 );
private var depositr = TBFile( "depositr.dbt", "w", 8 );
private var sbdepdoc = TBFile( "sbdepdoc.dbt", "w", 6 );
private var sb_casln = TBFile( "sb_casln.dbt", "w", 0 );
private var pc_apltp = TBFile( "pc_apltp.dbt", "r", 0 );
private var pc__calc = TBFile( "pc__calc.dbt", "w", 0 );
private var pc_drest = TBFile( "pc_drest.dbt", "w", 0 );
private var sbtrast  = TBFile( "sbtrast.dbt",  "r", 0 );
private var pay_kind = TBFile( "pay_kind.dbt", "r", 0 );
private var pay_doc  = TBFile( "pay_doc.dbt",  "w", 0 );
private var currency = TBFile( "currency.dbt", "r", 0 );
private var namealg  = TBFile( "namealg.dbt",  "r", 0, NULL, "bank.def" );
private var depclnt  = TClientList;

record chcnTmpDoc ( "sbdepdoc.1" );
record chcnOperPrm ("oper_prm.1");
 
private var FlagCur = NumFlagCur( );
private var Branch  = NumFNCash( );
private var {curdate};
private var {oper};
private var saveCurDate = {curdate};
private var numOpert    = 43;

private var TypeAccount      = "";
private var CodeClient       = 0;
private var CodeClient_Trust = 0;
private var FlagPayHeir      = false;

private var mainApplKind = 0;
private var mainApplKey  = "";

// Панель ввода данных для оплаты
record PCHCNPAY ( PCHCNPAY ) dialog;
  const ne_DatePay              = 0;
  const ne_DateOperDay          = 1;
  const ne_NameTypeAccount      = 2;
  const ne_RgstAccount          = 3;
  const ne_NameCurrency         = 4;
  const ne_Account              = 5;
  const ne_DateOpen             = 6;
  const ne_AccountInBase        = 7;
  const ne_RgstNameOwnerAccount = 8;
  const ne_NameDocument         = 9;
  const ne_FlagTrust            = 10;
  const ne_FlagWill             = 11;
  const ne_Permit               = 12;

// Панель расчета компенсаций
record PCHCNCMP ( PCHCNCMP ) dialog;
  const ne_CompensCoeff  = 0;
  const ne_FlagCompens1  = 1;
  const ne_Compens1      = 2;
  const ne_FlagCompens2  = 3;
  const ne_Compens2      = 4;
  const ne_FlagCompens3x = 5;
  const ne_Compens3x     = 6;
  const ne_FlagCompens40 = 7;
  const ne_Compens40     = 8;
  const ne_SumCompens    = 9;
private var saveCoeff = 0;
private var tmpSaveCoeff;
const DefaultCoeff = 0.6;
const MinCoeff     = 0.6;
const MaxCoeff     = 1.0;
const canWork3x    = false;

// Панель подтверждения последнего уточненного остатка по вкладу
record PCHLAST ( PCHLAST ) dialog;
  const ne_RgstDateLastOperation      = 0;
  const ne_DateLastOperation          = 1;
  const ne_RgstRestAfterLastOper      = 2;
  const ne_RestAfterLastOperation     = 3;

// Панель задания атрибутов получателя для безналичного закрытия
record PCHATTR ( PCHATTR ) dialog;
  const ne_BIK_Receiver         = 0;
  const ne_CorrAccount_Receiver = 1;
  const ne_Account_Receiver     = 2;

// Что делать с реестром
const whatDo_Error        = -1; // Ошибка при определении что делать
const whatDo_Nothing      =  0; // Нечего делать
const whatDo_PayClient    =  1; // Выплата вкладчику или первому наследнику
const whatDo_PayNextHeir  =  2; // Выплата следующему наследнику
const whatDo_PayCompens3x =  3; // Дополнительная выплата трехкратной компенсации
                  
array aSymbTypeAcc;
array aNameTypeAcc;
  aSymbTypeAcc(  0 ) = "1";    aNameTypeAcc(  0 ) = "ЧСБ-до востр";
  aSymbTypeAcc(  1 ) = "2";    aNameTypeAcc(  1 ) = aNameTypeAcc(  0 );
  aSymbTypeAcc(  2 ) = "3";    aNameTypeAcc(  2 ) = aNameTypeAcc(  0 );
  aSymbTypeAcc(  3 ) = "4";    aNameTypeAcc(  3 ) = aNameTypeAcc(  0 );
  aSymbTypeAcc(  4 ) = "5";    aNameTypeAcc(  4 ) = aNameTypeAcc(  0 );
  aSymbTypeAcc(  5 ) = "6";    aNameTypeAcc(  5 ) = aNameTypeAcc(  0 );
  aSymbTypeAcc(  6 ) = "7";    aNameTypeAcc(  6 ) = aNameTypeAcc(  0 );
  aSymbTypeAcc(  7 ) = "8";    aNameTypeAcc(  7 ) = aNameTypeAcc(  0 );
  aSymbTypeAcc(  8 ) = "9";    aNameTypeAcc(  8 ) = aNameTypeAcc(  0 );
  aSymbTypeAcc(  9 ) = "А-";   aNameTypeAcc(  9 ) = "ЧСБ-выигрышн";
  aSymbTypeAcc( 10 ) = "В-";   aNameTypeAcc( 10 ) = "ЧСБ-ден-вещ.";
  aSymbTypeAcc( 11 ) = "ОД-";  aNameTypeAcc( 11 ) = "ЧСБ-ср.ежем.";
  aSymbTypeAcc( 12 ) = "Н-";   aNameTypeAcc( 12 ) = "ЧСБ-цел.дети";
  aSymbTypeAcc( 13 ) = "Д-";   aNameTypeAcc( 13 ) = "ЧСБ-сроч. ДВ";
  aSymbTypeAcc( 14 ) = "070";  aNameTypeAcc( 14 ) = "ЧСБ-договорн";
  aSymbTypeAcc( 15 ) = "057";  aNameTypeAcc( 15 ) = "ЧСБ-компенс.";
  aSymbTypeAcc( 16 ) = "0";    aNameTypeAcc( 16 ) = "ЧСБ-ср.до 3л";
  aSymbTypeAcc( 17 ) = "0";    aNameTypeAcc( 17 ) = "ЧСБ-ср.до 5л";
  aSymbTypeAcc( 18 ) = "0";    aNameTypeAcc( 18 ) = "ЧСБ-ср.св.5л";
  aSymbTypeAcc( 19 ) = "0";    aNameTypeAcc( 19 ) = "ЧСБ-срочный";
  aSymbTypeAcc( 20 ) = "Б";    aNameTypeAcc( 20 ) = "ЧСБ-д.3м (з)";
  aSymbTypeAcc( 21 ) = "Б";    aNameTypeAcc( 21 ) = "ЧСБ-д.6м (з)";
  aSymbTypeAcc( 22 ) = "Б";    aNameTypeAcc( 22 ) = "ЧСБ-д12м (з)";
  aSymbTypeAcc( 23 ) = "Б";    aNameTypeAcc( 23 ) = "ЧСБ-д12м-псл";
  aSymbTypeAcc( 24 ) = "Б";    aNameTypeAcc( 24 ) = "ЧСБ-ср.деп 4";
  aSymbTypeAcc( 25 ) = "Б";    aNameTypeAcc( 25 ) = "ЧСБ-ср.деп 6";
  aSymbTypeAcc( 26 ) = "Б";    aNameTypeAcc( 26 ) = "ЧСБ-ср.деп12";


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefLastDayMonth ( m, y )

  var d;

  m = m + 1;

  if ( m > 12 )
    m = 1;
    y = y + 1;
  end;

  DateSplit( ( Date( 1, m, y ) - 1 ), d, m, y );

  return d;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro AddMonth ( m, y, dm )

  m = m + dm;
  while ( m > 12 )
    m = m - 12;
    y = y + 1;
  end;

  SetParm( 0, m );
  SetParm( 1, y );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro SumDate ( BegDate, Term, KindTerm )

  var retDate = Date( 0, 0, 0 );
  var d, m, y;
  var dd;
  var nd, nm, ny;

  const R_DAY     = 1; // Дней
  const R_MON     = 2; // Месяцев
  const R_QW      = 3; // Кварталов
  const R_YEAR    = 4; // Лет
  const R_REALDAY = 5; // Календарных дней

  if ( KindTerm == R_QW )
    Term = Term * 3;
    KindTerm = R_MON;
  end;

  DateSplit( BegDate, d, m, y );

  // ---------------------------------------------------------------------
  if ( KindTerm == R_YEAR )
    y = y + Term;
    if ( ( m == 2 ) and ( d > 28 ) )
      d = DefLastDayMonth( m, y );
    end;
    retDate = Date( d, m, y );
  // ---------------------------------------------------------------------
  elif ( KindTerm == R_DAY )
    nm = Int( Term / 30 );
    nd = Term - nm * 30;
    AddMonth( m, y, nm );
    retDate = Date( d, m, y ) + nd;
  // ---------------------------------------------------------------------
  elif ( KindTerm == R_REALDAY )
    retDate = BegDate + Term;
  // ---------------------------------------------------------------------
  elif ( KindTerm == R_MON )
    AddMonth( m, y, Term );
    dd = DefLastDayMonth( m, y );
    if ( d > dd )
      d = dd;
    end;
    retDate = Date( d, m, y );
  end;

  return retDate;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ChooseNumOtherOpert

  array aNamePayKind;
  array aNumPayKind;

  var stat;
  var i;
  var retVal = 0;

  pay_kind.Clear;
  pay_kind.rec.IsCur      = FlagCur;
  pay_kind.rec.GroupOpert = 41;
  pay_kind.rec.NumOperat  = 43;

  stat = pay_kind.GetGE;

  while ( stat )
    if ( ( pay_kind.rec.IsCur      == FlagCur ) and
         ( pay_kind.rec.GroupOpert == 41      ) and
         ( pay_kind.rec.NumOperat  <= 44      )    )
      i = ASize( aNamePayKind );
      aNumPayKind( i ) = pay_kind.rec.NumOperat;
      aNamePayKind( i ) = " " + String( pay_kind.rec.NumOperat ) + " " + pay_kind.rec.SzNameAlg + " ";
      stat = pay_kind.Next;
    else
      stat = false;
    end;
  end;

  if ( ASize( aNamePayKind ) == 0 )
    MsgBox( "Отсутствуют прочие расходные операции для выплат ЧСБ" );
  else
    i = Menu( aNamePayKind, NULL, "Виды выплат ЧСБ" );
    if ( i >= 0 )
      retVal = aNumPayKind( i );
    else
      MsgBox( "Отказались от проведения операции выплаты ЧСБ" );
    end;
  end;

  return retVal;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ChooseChcnRgst( rgtpID, rgstID )

  var retVal = false;

  //if ( SelectCHCNRgTp( rgtpID ) )
  //  retVal = SelectChcnRgst( rgtpID, rgstID );
  //end;

  retVal = SelectChcnRgst( rgtpID, rgstID );

  if ( retVal )
    SetParm( 0, rgtpID );
    SetParm( 1, rgstID );
  end;

  return retVal;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindChcnRgst( rgtpID, rgstID )

  var stat;

  chcnrgst.Clear;
  chcnrgst.rec.RgtpID = rgtpID;
  chcnrgst.rec.RgstID = rgstID;

  stat = chcnrgst.GetEQ;

  if ( not stat )
    MsgBox( "Не найдена запись в реестре вкладчиков" );
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindDepositr( referenc )

  var stat;

  depositr.Clear;
  depositr.rec.Referenc = referenc;

  stat = depositr.GetEQ;

  if ( not stat )
    MsgBox( "Не найден счет вкладчика" );
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindTypeAccount( kind )

  sb_dtyp.KeyNum = 2;
  sb_dtyp.Clear;
  sb_dtyp.rec.FlagCur = FlagCur;
  sb_dtyp.rec.Kind    = kind;

  return sb_dtyp.GetEQ;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro InitNewDepDoc( depDocRec )

  depDocRec.FNCash        = depositr.rec.FNCash;
  depDocRec.IsCur         = depositr.rec.IsCur;
  depDocRec.Referenc      = depositr.rec.Referenc;
  depDocRec.Account       = depositr.rec.Account;
  depDocRec.Code_Currency = depositr.rec.Code_Currency;
  depDocRec.Type_Account  = depositr.rec.Type_Account;
  depDocRec.CodClient     = depositr.rec.CodClient;
  depDocRec.Oper          = {oper};

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefTypeAccountFromList

  var retVal = "";
  var i      = 0;
  var tmpAccount;
  var tmpStr;

  if ( chcnrgst.rec.Account != "" )
    tmpAccount = chcnrgst.rec.Account;
  else
    tmpAccount = chcnrgst.rec.RgstAccount;
  end;

  while ( i < ASize( aSymbTypeAcc ) )
    if ( ( tmpAccount           != ""                          ) and 
         ( aSymbTypeAcc( i )    != ""                          ) and 
         ( StrLen( tmpAccount ) >  StrLen( aSymbTypeAcc( i ) ) )    )
      if ( StrUpr ( SubStr( tmpAccount, 1, StrLen( aSymbTypeAcc( i ) ) ) ) == StrUpr( aSymbTypeAcc( i ) ) )
        retVal = aNameTypeAcc( i );
        i = ASize( aSymbTypeAcc );
      end;
    end;
    i = i + 1;
  end;

  return retVal;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefTypeAccount

  var stat = true;

  TypeAccount = "";

  if ( chcnrgst.rec.Referenc != 0 )
    if ( FindDepositr( chcnrgst.rec.Referenc ) )
      TypeAccount = depositr.rec.Type_Account;
    end;
  else
    TypeAccount = DefTypeAccountFromList( );
  end;

  if ( TypeAccount == "" )
    MsgBox( "Ошибка при определении вида вклада" );
    stat = false;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetInfoFieldsPCHCNCMP

  PCHCNCMP.SumCompens = PCHCNCMP.Compens1 +
                        PCHCNCMP.Compens2 +
                        PCHCNCMP.Compens3x;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefRestFromDocOnEndDate( dateRest )

  var retVal = $0L;
  var stat;

  dateRest = dateRest + 1;

  sbdepdoc.Clear;
  sbdepdoc.rec.Referenc = depositr.rec.Referenc;
  sbdepdoc.rec.Date_Document = dateRest;
  sbdepdoc.addFilter( "t.t_Referenc = " + String( depositr.rec.Referenc ) );

  stat = sbdepdoc.GetLT;

  while ( stat )
    if ( sbdepdoc.rec.Referenc == depositr.rec.Referenc )
      if ( IsServDoc( sbdepdoc.rec ) and ( sbdepdoc.rec.FlagStorn == "" ) )
        retVal = sbdepdoc.rec.Rest;
        stat = false;
      else
        stat = sbdepdoc.Prev;
      end;
    else
      stat = false;
    end;
  end;
  sbdepdoc.dropFilter;

  return retVal;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefRestFromDocOnEndDepDate( dateRest )

  var retVal  = $0L;
  var saveKey = sbdepdoc.KeyNum;
  var stat;

  dateRest = dateRest + 1;

  sbdepdoc.KeyNum = 2;
  sbdepdoc.Clear;
  sbdepdoc.rec.Referenc = depositr.rec.Referenc;
  sbdepdoc.rec.DepDate_Document = dateRest;
  sbdepdoc.addFilter( "t.t_Referenc = " + String( depositr.rec.Referenc ) );

  stat = sbdepdoc.GetLT;

  while ( stat )
    if ( sbdepdoc.rec.Referenc == depositr.rec.Referenc )
      if ( IsServDoc( sbdepdoc.rec ) and ( sbdepdoc.rec.FlagStorn == "" ) )
        retVal = sbdepdoc.rec.Rest;
        stat = false;
      else
        stat = sbdepdoc.Prev;
      end;
    else
      stat = false;
    end;
  end;
  sbdepdoc.dropFilter;

  sbdepdoc.KeyNum = saveKey;

  return retVal;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro CalcCompens ( numCompens )

  var stat;
  var RestOnDate;

  // Расчет 40-процентной компенсации ------------------------------------
  if ( ( numCompens < 0 ) or ( numCompens == 4 ) )
    if ( PCHCNCMP.FlagCompens40 == "" )
      PCHCNCMP.Compens40 = 0;
    else
      RestOnDate = DefRestFromDocOnEndDepDate( Date( 28, 2, 1991 ) );

      if ( RestOnDate <= 0 )
        PCHCNCMP.Compens40 = 0;
      else
        PCHCNCMP.Compens40 = MoneyL( RestOnDate * 0.4 );
        if ( PCHCNCMP.Compens40 > $220L )
          PCHCNCMP.Compens40 = $200L;
        end;
      end;
    end;
  end;

  // Расчет предварительной компенсации ----------------------------------
  if ( ( numCompens < 0 ) or ( numCompens == 1 ) )
    if ( PCHCNCMP.FlagCompens1 == "" )
      PCHCNCMP.Compens1 = 0;
    else
      RestOnDate = DefRestFromDocOnEndDepDate( Date( 20, 6, 1991 ) ) + PCHCNCMP.Compens40;

      if ( RestOnDate <= 0 )
        PCHCNCMP.Compens1 = 0;
      elif ( RestOnDate < $1000L )
        PCHCNCMP.Compens1 = MoneyL( RestOnDate * PCHCNCMP.CompensCoeff );
      else
        PCHCNCMP.Compens1 = MoneyL( $1000L * PCHCNCMP.CompensCoeff );
      end;
    end;
  end;

  // Расчет дополнительной предварительной компенсации -------------------
  if ( ( numCompens < 0 ) or ( numCompens == 2 ) )
    if ( PCHCNCMP.FlagCompens2 == "" )
      PCHCNCMP.Compens2 = 0;
    else
      RestOnDate = DefRestFromDocOnEndDepDate( Date( 20, 6, 1991 ) ) + PCHCNCMP.Compens40;

      if ( RestOnDate <= $1000L )
        PCHCNCMP.Compens2 = 0;
      elif ( RestOnDate < $2000L )
        PCHCNCMP.Compens2 = MoneyL( ( RestOnDate - $1000L ) * PCHCNCMP.CompensCoeff );
      else
        PCHCNCMP.Compens2 = MoneyL( $1000L * PCHCNCMP.CompensCoeff );
      end;
    end;
  end;

  // Расчет трехкратной компенсации --------------------------------------
  if ( ( numCompens < 0 ) or ( numCompens == 3 ) )
    if ( PCHCNCMP.FlagCompens3x == "" )
      PCHCNCMP.Compens3x = 0;
    else
      RestOnDate = DefRestFromDocOnEndDepDate( Date( 31, 12, 1991 ) ) + PCHCNCMP.Compens40;

      if ( RestOnDate <= 0 )
        PCHCNCMP.Compens3x = 0;
      else
        PCHCNCMP.Compens3x = MoneyL( RestOnDate * 0.003 );
      end;
    end;
  end;

  GetInfoFieldsPCHCNCMP( );

end;

    
/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro InitFieldsPCHCNCMP

  ClearRecord( PCHCNCMP );

  // ---------------------------------------------------------------------
  if ( chcnrgst.rec.Compens1 != 0 )
    PCHCNCMP.FlagCompens1 = "X";
    PCHCNCMP.Compens1     = chcnrgst.rec.Compens1;
  end;
  // ---------------------------------------------------------------------
  if ( chcnrgst.rec.Compens2 != 0 )
    PCHCNCMP.FlagCompens2 = "X";
    PCHCNCMP.Compens2     = chcnrgst.rec.Compens2;
  end;
  // ---------------------------------------------------------------------
  if ( chcnrgst.rec.Compens3x != 0 )
    PCHCNCMP.FlagCompens3x = "X";
    PCHCNCMP.Compens3x     = chcnrgst.rec.Compens3x;
  end;
  // ---------------------------------------------------------------------
  if ( chcnrgst.rec.Compens40p != 0 )
    PCHCNCMP.FlagCompens40 = "X";
    PCHCNCMP.Compens40     = chcnrgst.rec.Compens40p;
  end;

  if ( saveCoeff != 0 )
    PCHCNCMP.CompensCoeff = saveCoeff;
  else
    PCHCNCMP.CompensCoeff = DefaultCoeff;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro WorkDialogCMP ( IP, cmd, id, key )

  var stat = CM_DEFAULT;
  var i;

  // =====================================================================
  if ( cmd == DLG_KEY )
    // ENTER -----------------------------------------------------------
    if ( key == 13 )
      ;
    // ^F8 -------------------------------------------------------------
    elif ( key == 357 )
      MsgBox( "^F8" );
    // F9 --------------------------------------------------------------
    elif ( key == 323 )
      saveCoeff = PCHCNCMP.CompensCoeff;
      stat = CM_SAVE;
    // Пробел ----------------------------------------------------------
    elif ( key == 32 )
      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
      if ( id == ne_FlagCompens1 )
        if ( PCHCNCMP.FlagCompens1 == "" )
          PCHCNCMP.FlagCompens1 = "X";
        else
          PCHCNCMP.FlagCompens1 = "";
        end;
        CalcCompens( 1 );
        UpdateFields( IP );
      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
      elif ( id == ne_FlagCompens2 )
        if ( PCHCNCMP.FlagCompens2 == "" )
          PCHCNCMP.FlagCompens2 = "X";
        else
          PCHCNCMP.FlagCompens2 = "";
        end;
        CalcCompens( 2 );
        UpdateFields( IP );
      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
      elif ( id == ne_FlagCompens3x )
        if ( canWork3x )
          if ( PCHCNCMP.FlagCompens3x == "" )
            PCHCNCMP.FlagCompens3x = "X";
          else
            PCHCNCMP.FlagCompens3x = "";
          end;      
          CalcCompens( 3 );
          UpdateFields( IP );
        end;
      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
      elif ( id == ne_FlagCompens40 )
        if ( PCHCNCMP.FlagCompens40 == "" )
          PCHCNCMP.FlagCompens40 = "X";
        else
          PCHCNCMP.FlagCompens40 = "";
        end;
        CalcCompens( -1 );
        UpdateFields( IP );
      end;
    // ESC -------------------------------------------------------------
    elif ( key == 27 )
      if ( ( PCHCNCMP.Compens1  != 0 ) or ( PCHCNCMP.Compens2  != 0 ) or
           ( PCHCNCMP.Compens3x != 0 ) or ( PCHCNCMP.Compens40 != 0 )   )
        if ( GetTrue( true, "Вы хотите выйти с обнулением всех сумм ?" ) )
          PCHCNCMP.Compens1 = PCHCNCMP.Compens2 = PCHCNCMP.Compens3x = PCHCNCMP.Compens40 = 0;
          stat = CM_SAVE;
        else
          stat = CM_IGNORE;
        end;
      end;
    end;
  // =====================================================================
  elif ( cmd == DLG_SETFOCUS )
    if ( id == ne_CompensCoeff )
      tmpSaveCoeff = PCHCNCMP.CompensCoeff;
    end;
  // =====================================================================
  elif ( cmd == DLG_REMFOCUS )
    if ( id == ne_CompensCoeff )
      if ( tmpSaveCoeff != PCHCNCMP.CompensCoeff )
        if ( PCHCNCMP.CompensCoeff < MinCoeff )
          MsgBox( "Коэффициент должен быть в пределах между ", 
                  MinCoeff, " и ", MaxCoeff, "|",
                  "Коэффициенту установлено значение ", MinCoeff );
          PCHCNCMP.CompensCoeff = MinCoeff;
        end;
        if ( PCHCNCMP.CompensCoeff > MaxCoeff )
          MsgBox( "Коэффициент должен быть в пределах между ", 
                  MinCoeff, " и ", MaxCoeff, "|", 
                  "Коэффициенту установлено значение ", MaxCoeff );
          PCHCNCMP.CompensCoeff = MaxCoeff;
        end;
        CalcCompens( -1 );
        UpdateFields( IP );
      end;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro EditCHCNCompens
      
  var stat = true;

  if ( chcnrgst.rec.NumStepOperation != 10 )
    chcnrgst.rec.NumStepOperation = 10;
    stat = chcnrgst.Update;
  end;

  if ( stat )
    InitFieldsPCHCNCMP( );
    GetInfoFieldsPCHCNCMP( );
    RunDialog( PCHCNCMP, "WorkDialogCMP" );
  end;

  if ( stat )
    chcnrgst.rec.Compens1   = PCHCNCMP.Compens1;
    chcnrgst.rec.Compens2   = PCHCNCMP.Compens2;
    chcnrgst.rec.Compens3x  = PCHCNCMP.Compens3x;
    chcnrgst.rec.Compens40p = PCHCNCMP.Compens40;
    chcnrgst.rec.NumStepOperation = 11;
    stat = chcnrgst.Update;
  end;

  if ( not stat )
    MsgBox( "Ошибка при расчете компенсаций" );
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro InputDepClient ( fromPanel )

  var depclntL    = TClientList;
  var depclntI    = TClientList;
  var depclntInit = depclntI.CurRec;
  var stat        = true;
      
  if ( chcnrgst.rec.Referenc != 0 )
    stat = FindDepositr( chcnrgst.rec.Referenc );
    if ( stat )
      CodeClient = chcnrgst.rec.Referenc;
    end;
  end;
             
  if ( stat and ( fromPanel or ( CodeClient == 0 ) ) )
    depclntInit.Clear( );
    depclntInit.rec.CodClient       = CodeClient;
    depclntInit.rec.Name1           = chcnrgst.rec.RgstName1;
    depclntInit.rec.Name2           = chcnrgst.rec.RgstName2;
    depclntInit.rec.Name3           = chcnrgst.rec.RgstName3;
    depclntInit.rec.Born            = chcnrgst.rec.RgstBorn;
    depclntInit.rec.PaperKind       = chcnrgst.rec.RgstPaperKind;
    depclntInit.rec.PaperSeries     = chcnrgst.rec.RgstPaperSeries;
    depclntInit.rec.PaperNumber     = chcnrgst.rec.RgstPaperNumber;
    depclntInit.rec.PaperIssuer     = chcnrgst.rec.RgstPaperIssuer;
    depclntInit.rec.PaperIssuedDate = chcnrgst.rec.RgstPaperIssuedDate;

    if ( depclntL.Select_Search( NULL, TypeAccount, depclntInit ) )
      CodeClient = depclntL.CurRec.rec.CodClient;
      stat = true;
    else
      if ( not fromPanel )
        MsgBox( "Отказались от ввода вкладчика|Дальнейшее проведение операции невозможно" );
      end;
      stat = false;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro InputTrustClient ( fromPanel )

  var depclntL    = TClientList;
  var depclntI    = TClientList;
  var depclntInit = depclntI.CurRec;
  var stat        = true;

  if ( chcnrgst.rec.Referenc != 0 )
    sbtrast.KeyNum = 0;
    sbtrast.Clear;
    sbtrast.rec.Referenc = chcnrgst.rec.Referenc;
    sbtrast.rec.VidDoc   = "";
    sbtrast.addFilter( "t.t_Referenc = " + String( chcnrgst.rec.Referenc ) );

    if (   sbtrast.GetGE                                   and
         ( sbtrast.rec.Referenc == depositr.rec.Referenc ) and
         ( sbtrast.rec.VidDoc   == ""                    )    )
      CodeClient_Trust = sbtrast.rec.CodClient;
    end;
    sbtrast.dropFilter;

  else

    depclntInit.Clear( );
    if ( CodeClient_Trust != 0 )
      if ( depclnt.GetRecord( CodeClient_Trust ) )
        depclntInit.rec.CodClient = CodeClient_Trust;
        depclntInit.rec.Name1     = depclnt.CurRec.rec.Name1;
        depclntInit.rec.Name2     = depclnt.CurRec.rec.Name2;
        depclntInit.rec.Name3     = depclnt.CurRec.rec.Name3;
      else
        CodeClient_Trust = 0;
      end;
    end;      

    if ( fromPanel or ( CodeClient_Trust == 0 ) )
      if ( depclntL.Select_Search( NULL, TypeAccount, depclntInit ) )
        CodeClient_Trust = depclntL.CurRec.rec.CodClient;
      end;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro T_OpenNewCHCNAccount

  var stat = НовыйСчетБезДокументов( depositr, 1 );
  var d, m, y;
  var closeDate;
  var refID = 0;

  if ( stat != 0 )
    AbortTrn( );
  end;

  if ( not FindDepositr( depositr.rec.Referenc ) )
    AbortTrn( );
  end;

  if ( chcnrgst.rec.RgstAccount != "" )
    depositr.rec.Account = chcnrgst.rec.RgstAccount;
    if ( not depositr.Update( ) )
      AbortTrn( );
    end;
  end;

  // Заведение доверенности, если задана
  if ( ( PCHCNPAY.FlagTrust != "" ) and ( CodeClient_Trust != 0 ) )
    DateSplit( PCHCNPAY.DatePay, d, m, y );
    y = y + 1;
    if ( ( m == 2 ) and ( d > 28 ) )
      d = DefLastDayMonth( m, y );
    end;
    closeDate = Date( d, m, y );

    sbtrast.KeyNum = 4;
    sbtrast.Clear;
    sbtrast.rec.RefID = ( depositr.rec.FNCash  + 1 ) * 10000000 - 1;
    sbtrast.addFilter( "t.t_RefID <= " + String( ( depositr.rec.FNCash  + 1 ) * 10000000 ) + " and " +
                       "t.t_RefID >= " + String( depositr.rec.FNCash * 10000000 )                     );

    if ( sbtrast.GetLT and ( sbtrast.rec.RefID >= depositr.rec.FNCash * 10000000 ) )
      refID = sbtrast.rec.RefID + 1;
    else
      refID = depositr.rec.FNCash * 10000000 + 1;
    end;
    sbtrast.dropFilter;

    sbtrast.Clear;
    sbtrast.rec.FNCash    = depositr.rec.FNCash;
    sbtrast.rec.Referenc  = depositr.rec.Referenc;
    sbtrast.rec.CodClient = CodeClient_Trust;
    sbtrast.rec.DateOpen  = PCHCNPAY.DatePay;
    sbtrast.rec.CloseDate = closeDate;
    sbtrast.rec.RefID     = refID;
    sbtrast.rec.Type      = StrFor( 1 );

    if ( not sbtrast.Insert )
      AbortTrn( );
    end;

/*
    if(not ALG_CONTRACT.Update)
      AbortTrn( );
    end;
*/

    depositr.rec.FlagTrast = StrFor( 1 );
    if ( not depositr.Update )
      AbortTrn( );
    end;
  end;

  // Обновление записи реестра -------------------------------------------
  if ( not chcnrgst.GetPos )
    AbortTrn( );
  end;

  if ( not chcnrgst.GetDirect )
    AbortTrn( );
  end;

  chcnrgst.rec.Referenc = depositr.rec.Referenc;
  chcnrgst.rec.NumStepOperation = 6;
  if ( not chcnrgst.Update )
    AbortTrn( );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro OpenNewCHCNAccount ( fromPanel )

  record paramEditAcc( "AccParm.rec" );

  var stat = true;

  if ( chcnrgst.rec.NumStepOperation <= 5 )
    chcnrgst.rec.NumStepOperation = 5;
    stat = chcnrgst.Update;
  end;

  // Проверка задания обязательных полей
  if ( stat and ( chcnrgst.rec.Referenc == 0 ) )
    if ( CodeClient == 0 )
      MsgBox( "Владелец счета не задан|Открытие счета невозможно" );
      stat = false;
    elif ( TypeAccount == "" )
      MsgBox( "Вид вклада не задан|Открытие счета невозможно" );
      stat = false;
    elif ( PCHCNPAY.DateOpen == Date( 0, 0, 0 ) )
      MsgBox( "Дата открытия счета не задана|Открытие счета невозможно" );
      stat = false;
    end;
  end;

  if ( stat )
    if ( chcnrgst.rec.Referenc == 0 )
      depositr.Clear;
      depositr.rec.Type_Account  = TypeAccount;
      depositr.rec.Code_Currency = chcnrgst.rec.RgstCodeCurrency;
      depositr.rec.CodClient     = CodeClient;
      depositr.rec.Open_Date     = PCHCNPAY.DateOpen;
      if ( chcnrgst.rec.RgstPaperKindPay == 0 )
        depositr.rec.Givebook      = "X";
        /*ALG_CONTRACT.BookGiven     = "X";*/
      end;
      if (sb_dtyp.rec.FormContr != 0)
        depositr.rec.Start_DateDep = depositr.rec.Open_Date;
      end;

      stat = ProcessTrn( 1, "T_OpenNewCHCNAccount" );
    else
      chcnrgst.rec.NumStepOperation = 6;
      stat = chcnrgst.Update;
    end;
  end;

  if ( not stat )
    MsgBox( "Ошибка при открытии счета" );
    stat = false;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefCriticalDate

  var calcDateLastOperation = Date( 0, 0, 0 );
  var limitDate             = depositr.rec.Limit_Date;
  var useAlternante         = depositr.rec.UseAlternate;
  var endDateDep            = depositr.rec.End_DateDep;
  var prolDateDep           = depositr.rec.Prol_DateDep;
  var yearLastOper          = 0;
  var stat                  = true;
  var flag                  = true;

  if ( chcnrgst.rec.NumStepOperation != 6 )
    chcnrgst.rec.NumStepOperation = 6;
    stat = chcnrgst.Update;
  end;

  // ---------------------------------------------------------------------
  if ( ( chcnrgst.rec.RgstDateLastOperation > Date( 31, 12, 1994 ) ) and
       ( chcnrgst.rec.RgstRestAfterLastOpert != 0                  )    )
    calcDateLastOperation = chcnrgst.rec.RgstDateLastOperation;
  // ---------------------------------------------------------------------
  elif ( chcnrgst.rec.RgstRest010195 != 0 )
    calcDateLastOperation = Date( 31, 12, 1994 );
  // ---------------------------------------------------------------------
  elif ( ( chcnrgst.rec.RgstDateLastOperation > Date( 30, 06, 1993 ) ) and
         ( chcnrgst.rec.RgstRestAfterLastOpert != 0                  )    )
    calcDateLastOperation = chcnrgst.rec.RgstDateLastOperation;
  // ---------------------------------------------------------------------
  elif ( chcnrgst.rec.RgstRest010793 != 0 )
    calcDateLastOperation = Date( 30, 06, 1993 );
  // ---------------------------------------------------------------------
  elif ( ( chcnrgst.rec.RgstDateLastOperation > Date( 31, 12, 1991 ) ) and
         ( chcnrgst.rec.RgstRestAfterLastOpert != 0                  )    )
    calcDateLastOperation = chcnrgst.rec.RgstDateLastOperation;
  // ---------------------------------------------------------------------
  elif ( chcnrgst.rec.RgstRest010192 != 0 )
    calcDateLastOperation = Date( 31, 12, 1991 );
  // ---------------------------------------------------------------------
  elif ( ( chcnrgst.rec.RgstDateLastOperation > Date( 19, 06, 1991 ) ) and
         ( chcnrgst.rec.RgstRestAfterLastOpert != 0                  )    )
    calcDateLastOperation = chcnrgst.rec.RgstDateLastOperation;
  // ---------------------------------------------------------------------
  elif ( chcnrgst.rec.RgstRest200691 != 0 )
    calcDateLastOperation = Date( 19, 06, 1991 );
  // ---------------------------------------------------------------------
  elif ( chcnrgst.rec.RgstRestAfterLastOpert != 0 )
    calcDateLastOperation = chcnrgst.rec.RgstDateLastOperation;
  end;

  // Целевой детский -----------------------------------------------------
  if ( depositr.rec.Type_Account == "ЧСБ-цел.дети" )
    if ( endDateDep != Date( 0, 0, 0 ) )
      useAlternante = 0;
      limitDate = Date( 0, 0, 0 );
    else
      useAlternante = 1;
      if ( calcDateLastOperation != Date( 0, 0, 0 ) )
        DateSplit( calcDateLastOperation, NULL, NULL, yearLastOper );
        limitDate = Date( 31, 12, yearLastOper - 1 );
      end;
      if ( limitDate < depositr.rec.Open_Date )
        limitDate = Date( 0, 0, 0 );
      end;
    end;
  // Не договор ----------------------------------------------------------
  elif ( sb_dtyp.rec.FormContr == 0 )
    if ( calcDateLastOperation != Date( 0, 0, 0 ) )
      DateSplit( calcDateLastOperation, NULL, NULL, yearLastOper );
      limitDate = Date( 31, 12, yearLastOper - 1 );
    end;
    if ( limitDate < depositr.rec.Open_Date )
      limitDate = Date( 0, 0, 0 );
    end;
    useAlternante = 0;
  // Срочный счет --------------------------------------------------------
  elif ( sb_dtyp.rec.FormContr == 1 )
    if ( endDateDep == Date( 0, 0, 0 ) )
      endDateDep = SumDate( depositr.rec.Open_Date, depositr.rec.Term, depositr.rec.KindTerm );
    end;
    if ( endDateDep > calcDateLastOperation )
      limitDate = Date( 0, 0, 0 );
    elif ( endDateDep == calcDateLastOperation )
      limitDate = endDateDep;
    elif ( endDateDep < calcDateLastOperation )
      DateSplit( calcDateLastOperation, NULL, NULL, yearLastOper );
      limitDate = Date( 31, 12, yearLastOper - 1 );
      if ( limitDate < endDateDep )
        limitDate = endDateDep;
      end;
    end;
    useAlternante = 0;
  // Непролонгируемый депозит --------------------------------------------
  elif ( sb_dtyp.rec.FormContr == 20 )
    if ( endDateDep == Date( 0, 0, 0 ) )
      endDateDep = SumDate( depositr.rec.Open_Date, depositr.rec.Term, depositr.rec.KindTerm );
    end;
    if ( endDateDep > calcDateLastOperation )
      limitDate = Date( 0, 0, 0 );
      useAlternante = 0;
    elif ( endDateDep == calcDateLastOperation )
      limitDate = endDateDep;
      useAlternante = 1;
    elif ( endDateDep < calcDateLastOperation )
      DateSplit( calcDateLastOperation, NULL, NULL, yearLastOper );
      limitDate = Date( 31, 12, yearLastOper - 1 );
      if ( limitDate < endDateDep )
        limitDate = endDateDep;
      end;
      useAlternante = 1;
    end;
  // Пролонгируемый депозит ----------------------------------------------
  elif ( sb_dtyp.rec.FormContr == 30 )
    useAlternante = 0;
    prolDateDep = Date( 0, 0, 0 );
    endDateDep  = SumDate( depositr.rec.Open_Date, depositr.rec.Term, depositr.rec.KindTerm );
    while ( flag )
      if ( ( calcDateLastOperation <= endDateDep  ) and
           ( calcDateLastOperation >= prolDateDep )    )
        flag = false;
      else
        prolDateDep = endDateDep + 1;
        endDateDep  = SumDate( endDateDep, depositr.rec.Term_Prol, depositr.rec.KindTerm_Prol );
      end;
    end;
    if ( calcDateLastOperation == endDateDep )
      limitDate = endDateDep;
    elif ( prolDateDep != Date( 0, 0, 0 ) )
      limitDate = prolDateDep - 1;
    end;
  end;

  if ( stat )
    depositr.rec.Limit_Date   = limitDate;
    depositr.rec.UseAlternate = useAlternante;
    depositr.rec.End_DateDep  = endDateDep;
    depositr.rec.Prol_DateDep = prolDateDep;
    depositr.rec.NumSession   = 0;
    stat = depositr.Update;
  end;

  if ( stat )
    chcnrgst.rec.NumStepOperation = 7;
    stat = chcnrgst.Update;
  end;

  if ( not stat )
    MsgBox( "Ошибка при определении критической даты и состояния счета" );
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro T_FillPcCalc

  var stat;

  if ( depositr.rec.Limit_Date != Date( 0, 0, 0 ) )
    pc_apltp.Clear;
    pc_apltp.rec.IsCur    = depositr.rec.IsCur;
    pc_apltp.rec.TypeRec  = 1003;
    pc_apltp.rec.Type     = depositr.rec.Type_Account;
    pc_apltp.rec.ApplType = 2001;
    pc_apltp.addFilter( "t.t_IsCur = " + String( depositr.rec.IsCur ) + " and " +
                        "t.t_TypeRec = 1003 and " +
                        "t.t_Type = " + GetSQLString( depositr.rec.Type_Account ) );

    stat = pc_apltp.GetGE;

    while ( stat )
      if ( ( pc_apltp.rec.IsCur    == depositr.rec.IsCur        ) and
           ( pc_apltp.rec.TypeRec  == 1003                      ) and
           ( pc_apltp.rec.Type     == depositr.rec.Type_Account ) and
           ( pc_apltp.rec.ApplType <= 2004                      )    )
        pc__calc.Clear;
        pc__calc.rec.Referenc   = chcnrgst.rec.Referenc;
        pc__calc.rec.ObjectType = pc_apltp.rec.ApplType;
        pc__calc.rec.TypeRecord = 1;
        pc__calc.rec.EndDate    = depositr.rec.Limit_Date;
        if ( not pc__calc.GetEQ )
          pc__calc.Clear;
          pc__calc.rec.Referenc   = chcnrgst.rec.Referenc;
          pc__calc.rec.ObjectType = pc_apltp.rec.ApplType;
          pc__calc.rec.TypeRecord = 1;
          pc__calc.rec.BeginDate  = depositr.rec.Limit_Date;
          pc__calc.rec.EndDate    = depositr.rec.Limit_Date;
          pc__calc.rec.FNcash     = depositr.rec.FNCash;
          if ( not pc__calc.Insert )
            pc_apltp.dropFilter;
            AbortTrn( );
          end;
        end;
        stat = pc_apltp.Next;
      else
        stat = false;
      end;
    end;
    pc_apltp.dropFilter;
  end;

  // Обновление записи реестра -------------------------------------------
  if ( not chcnrgst.GetPos )
    AbortTrn( );
  end;

  if ( not chcnrgst.GetDirect )
    AbortTrn( );
  end;

  chcnrgst.rec.NumStepOperation = 8;
  if ( not chcnrgst.Update )
    AbortTrn( );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FillPcCalc

  var stat = true;

  if ( chcnrgst.rec.NumStepOperation != 7 )
    chcnrgst.rec.NumStepOperation = 7;
    stat = chcnrgst.Update;
  end;

  if ( stat )
    if ( not ProcessTrn( 0, "T_FillPcCalc" ) )
      stat = false;
    end;
  end;

  if ( not stat )
    MsgBox( "Ошибка при заполнении графика расчета процентов" );
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro OpenInterDesk( referenc, wasOpenLink )

  var stat = true;

  wasOpenLink = false;

  sbdepdoc.Clear;
  sbdepdoc.rec.Referenc = referenc;
  sbdepdoc.addFilter( "t.t_Referenc = " + String( referenc ) );

  if ( sbdepdoc.GetGE and ( sbdepdoc.rec.Referenc == referenc ) )
    if ( not InterDesk_MakeDocBunchCurrent( sbdepdoc.rec.iApplicationKind, sbdepdoc.rec.ApplicationKey, true ) )
      stat = false;
    else
      wasOpenLink = true;
    end;
  end;
  sbdepdoc.dropFilter;

  SetParm( 1, wasOpenLink );

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro OpenInterDesk2( referenc, dateDoc, wasOpenLink )

  var stat = true;
  var flag;

  wasOpenLink = false;

  sbdepdoc.Clear;
  sbdepdoc.rec.Referenc = referenc;
  sbdepdoc.rec.Date_Document = dateDoc + 1;
  sbdepdoc.addFilter( "t.t_Referenc = " + String( referenc ) );

  flag = sbdepdoc.GetLT;

  while ( flag )
    if ( sbdepdoc.rec.Referenc == referenc )
      if ( IsServDoc( sbdepdoc.rec ) and ( sbdepdoc.rec.FlagStorn == "" ) )
        if ( not InterDesk_MakeDocBunchCurrent( sbdepdoc.rec.iApplicationKind, sbdepdoc.rec.ApplicationKey, true ) )
          stat = false;
        else
          wasOpenLink = true;
        end;
        flag = false;
      else
        flag = sbdepdoc.Prev;
      end;
    else
      flag = false;
    end;
  end;

  sbdepdoc.dropFilter;

  SetParm( 2, wasOpenLink );

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro InitFieldsLAST

  var stat;

  ClearRecord( PCHLAST );

  PCHLAST.RgstDateLastOperation      = chcnrgst.rec.RgstDateLastOperation;
  PCHLAST.RgstRestAfterLastOper      = chcnrgst.rec.RgstRestAfterLastOpert;

  sbdepdoc.Clear;
  sbdepdoc.rec.Referenc = depositr.rec.Referenc;
  sbdepdoc.rec.Date_Document = Date( 31, 12, 9999 );
  sbdepdoc.addFilter( "t.t_Referenc = " + String( depositr.rec.Referenc ) );

  stat = sbdepdoc.GetLE;

  while ( stat )
    if ( sbdepdoc.rec.Referenc == depositr.rec.Referenc )
      if ( IsServDoc( sbdepdoc.rec ) and ( sbdepdoc.rec.FlagStorn == "" ) )
        PCHLAST.DateLastOperation      = sbdepdoc.rec.Date_Document;
        PCHLAST.RestAfterLastOperation = sbdepdoc.rec.Rest;
        stat = false;
      else
        stat = sbdepdoc.Prev;
      end;
    else
      stat = false;
    end;
  end;
  sbdepdoc.dropFilter;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro WorkDialogLAST ( IP, cmd, id, key )

  var stat = CM_DEFAULT;
  var flag = true;
  var i;

  // =====================================================================
  if (cmd == DLG_KEY)
    // ENTER -----------------------------------------------------------
    if ( key == 13 )
      ;
    // F2 --------------------------------------------------------------
    elif ( key == 316 )
      stat = CM_SAVE;
    // ESC -------------------------------------------------------------
    elif (key == 27)
      stat = CM_CANCEL;
    end;
  // =====================================================================
  elif ( cmd == DLG_REMFOCUS )
    ;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro InputDepHistory ( fromPanel )

  var stat = true;
  var cycle = true;
  var wasOpenLink = false;

  if ( chcnrgst.rec.NumStepOperation < 8 )
    chcnrgst.rec.NumStepOperation = 8;
    stat = chcnrgst.Update;
  end;

  if ( stat )
    stat = OpenInterDesk( chcnrgst.rec.Referenc, wasOpenLink );
  end;

  if ( stat )
    while ( cycle )

      cycle = false;

      if ( РучнойВводИсторииОпераций( chcnrgst.rec.Referenc, 2 ) != 0 )
        stat = false;
      end;

      if ( stat )
        stat = FindDepositr( chcnrgst.rec.Referenc );
      end;

      // Подтверждение последнего остатка по счету
      if ( stat )
        InitFieldsLAST( );
        if ( ( PCHLAST.RgstDateLastOperation      != PCHLAST.DateLastOperation      ) or
             ( PCHLAST.RgstRestAfterLastOper      != PCHLAST.RestAfterLastOperation )   )
          if ( RunDialog( PCHLAST, "WorkDialogLAST" ) )
            ;
          else
            cycle = true;
          end;
        end;
      end;

    end;
  end;

  if ( wasOpenLink )
    InterDesk_EndDocBunch( );
  end;

  if ( stat )
    chcnrgst.rec.DateLastOperation      = PCHLAST.DateLastOperation;
    chcnrgst.rec.RestAfterLastOperation = PCHLAST.RestAfterLastOperation;
    chcnrgst.rec.NumStepOperation       = 9;
    stat = chcnrgst.Update;
  end;

  if ( stat )
    sbdepdoc.Clear;
    sbdepdoc.rec.Referenc = chcnrgst.rec.Referenc;
    sbdepdoc.addFilter( "t.t_Referenc = " + String( chcnrgst.rec.Referenc ) );

    cycle = sbdepdoc.GetGE;

    while ( cycle )
      if ( sbdepdoc.rec.Referenc == chcnrgst.rec.Referenc )
        if ( IsServDoc( sbdepdoc.rec ) and ( sbdepdoc.rec.FlagStorn == "" ) )
          sbdepdoc.rec.YesSbook = "";
          sbdepdoc.Update;
        end;
        cycle = sbdepdoc.Next;
      else
        cycle = false;
      end;
    end;
    sbdepdoc.dropFilter;
  end;

  if ( not stat )
    MsgBox( "Ошибка при ручном вводе истории операций по счету" );
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ExecutionRegularOperationCompens

  var stat = true;
  var wasOpenLink = false;

  if ( chcnrgst.rec.NumStepOperation != 9 )
    chcnrgst.rec.NumStepOperation = 9;
    stat = chcnrgst.Update;
  end;

  if ( stat )
    stat = OpenInterDesk( chcnrgst.rec.Referenc, wasOpenLink );
  end;

  if ( stat )
    if ( depositr.rec.Limit_Date < Date( 31, 12, 1991 ) )
      if ( {curdate} > Date( 31, 12, 1991 ) )
        SetCurDate( Date( 31, 12, 1991 ) );
      end;
      if ( ВыполнитьРегламентныеФункции( depositr.rec.Referenc, 
                                         Date( 31, 12, 1991 ) + 1, 0 ) != 0 )
        stat = false;
      end;
      SetCurDate( saveCurDate );
    end;
  end;

  if ( stat )
    stat = FindDepositr( chcnrgst.rec.Referenc );
  end;

  if ( wasOpenLink )
    InterDesk_EndDocBunch( );
  end;

  if ( stat )
    chcnrgst.rec.NumStepOperation = 10;
    stat = chcnrgst.Update;
  end;

  if ( not stat )
    MsgBox( "Ошибка при выполнении регламентных действий до даты компенсации" );
  end;
  
  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ExecutionRegularOperationDenom

  var stat = true;
  var wasOpenLink = false;

  if ( chcnrgst.rec.NumStepOperation != 12 )
    chcnrgst.rec.NumStepOperation = 12;
    stat = chcnrgst.Update;
  end;

  if ( stat )
    stat = OpenInterDesk( chcnrgst.rec.Referenc, wasOpenLink );
  end;

  if ( stat )
    if ( {curdate} > Date( 31, 12, 1997 ) )
      SetCurDate( Date( 31, 12, 1997 ) );
    end;
    if ( ВыполнитьРегламентныеФункции( depositr.rec.Referenc, 
                                       Date( 31, 12, 1997 ) + 1, 0 ) != 0 )
      stat = false;
    end;
    SetCurDate( saveCurDate );
  end;

  if ( stat )
    stat = FindDepositr( chcnrgst.rec.Referenc );
  end;

  if ( wasOpenLink )
    InterDesk_EndDocBunch( );
  end;

  if ( stat )
    if ( depositr.rec.End_DateDep != Date( 0, 0, 0 ) )           
      if ( ( depositr.rec.Prol_DateDep != Date( 0, 0, 0 )          ) and
           ( depositr.rec.Prol_DateDep <= Date( 31, 12, 1997 ) + 1 )    )
        depositr.rec.Limit_Date = depositr.rec.Prol_DateDep - 1;
      end;
    else
      depositr.rec.Limit_Date = Date( 31, 12, 1997 );
    end;

    stat = depositr.Update;
  end;

  if ( stat )
    chcnrgst.rec.NumStepOperation = 13;
    stat = chcnrgst.Update;
  end;

  if ( not stat )
    MsgBox( "Ошибка при выполнении регламентных действий до деноминации" );
  end;
   
  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro T_SetSignAccount
  // Обновление записи реестра -------------------------------------------
  if ( not chcnrgst.GetPos )
    AbortTrn( );
  end;

  if ( not chcnrgst.GetDirect )
    AbortTrn( );
  end;

  chcnrgst.rec.Referenc = depositr.rec.Referenc;
  chcnrgst.rec.NumStepOperation = 15;
  if ( not chcnrgst.Update )
    AbortTrn( );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ExecutionRegularOperationPay

  var stat = true;
  var wasOpenLink = false;

  if ( chcnrgst.rec.NumStepOperation != 14 )
    chcnrgst.rec.NumStepOperation = 14;
    stat = chcnrgst.Update;
  end;

  if ( stat )
    stat = OpenInterDesk( chcnrgst.rec.Referenc, wasOpenLink );
  end;

  if ( stat )
    if ( ВыполнитьРегламентныеФункции( depositr.rec.Referenc, PCHCNPAY.DatePay, 0 ) != 0 )
      stat = false;
    end;
  end;

  if ( stat )
    stat = FindDepositr( chcnrgst.rec.Referenc );
  end;

  if ( wasOpenLink )
    InterDesk_EndDocBunch( );
  end;

  if ( stat )
    if ( not ProcessTrn( 0, "T_SetSignAccount" ) )
      stat = false;
    end;
  end;

  if ( not stat )
    MsgBox( "Ошибка при выполнении регламентных действий до дата выплаты" );
  end;
   
  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DenomSum ( Sum )

  if ( Sum < 0 )
    return -MoneyL( Floor( DoubleL( -Sum * 100 ) / 1000 + 0.5 ) ) / 100;
  else
    return MoneyL( Floor( DoubleL( Sum * 100 ) / 1000 + 0.5 ) ) / 100;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro T_DenominationSums

  var oldRest     = depositr.rec.Sum_Rest;
  var limitDate   = depositr.rec.Limit_Date;
  var numLastDoc  = 0;
  var applKind    = 0;
  var applKey     = "";
  var numLastLink = 0;
  var branchLink  = 0;
  var stat;

  if ( limitDate == Date( 0, 0, 0 ) )
    limitDate = depositr.rec.Open_Date;
  end;

  // Определим номер последнего документа для формирования ключа ---------
  sbdepdoc.Clear;
  sbdepdoc.rec.Referenc      = depositr.rec.Referenc;
  sbdepdoc.rec.Date_Document = Date( 31, 12, 1997 ) + 1;
  sbdepdoc.addFilter( "t.t_Referenc = " + String( depositr.rec.Referenc ) + " and " +
                      "t.t_Date_Document between " + sqlDateToStr( Date( 31, 12, 1997 ) ) + " and " + 
                                                     sqlDateToStr( Date( 31, 12, 1997 ) + 1 )        );
  if (   sbdepdoc.GetLT                                        and
       ( sbdepdoc.rec.Referenc      == depositr.rec.Referenc ) and
       ( sbdepdoc.rec.Date_Document == Date( 31, 12, 1997 )  )    )
    numLastDoc = sbdepdoc.rec.NumDayDoc;
    applKind = sbdepdoc.rec.iApplicationKind;
    applKey  = sbdepdoc.rec.ApplicationKey;
  end;
  sbdepdoc.dropFilter;

  // Определим связку для вкладных документов по деноминации
  if ( applKey == "" )
    sbdepdoc.Clear;
    sbdepdoc.rec.Referenc = depositr.rec.Referenc;
    sbdepdoc.rec.Date_Document = Date( 31, 12, 1997 ) + 1;

    sbdepdoc.addFilter( "t.t_Referenc = " + String( depositr.rec.Referenc ) );

    stat = sbdepdoc.GetLT;

    while ( stat )
      if ( sbdepdoc.rec.Referenc == depositr.rec.Referenc )
        if ( IsServDoc( sbdepdoc.rec ) and ( sbdepdoc.rec.FlagStorn == "" ) )
          applKind = sbdepdoc.rec.iApplicationKind;
          applKey  = sbdepdoc.rec.ApplicationKey;
          stat = false;
        else
          stat = sbdepdoc.Prev;
        end;
      else
        stat = false;
      end;
    end;
    sbdepdoc.dropFilter;
  end;

  if ( applKey != "" )
    sb_casln.KeyNum = 1;
    sb_casln.Clear;
    sb_casln.rec.ApplicationKind2 = applKind;
    sb_casln.rec.ApplicationKey2  = applKey;
    sb_casln.addFilter( "t.t_ApplicationKind2 = " + String( applKind ) + " and " +
                        "t.t_ApplicationKey2 = " + GetSQLString( applKey )        );

    if (   sb_casln.GetGE                              and
         ( sb_casln.rec.ApplicationKind2 == applKind ) and
         ( sb_casln.rec.ApplicationKey2  == applKey  )    )
      applKind = sb_casln.rec.ApplicationKind1;
      applKey = sb_casln.rec.ApplicationKey1;
      branchLink = sb_casln.rec.FNcash;
    else
      applKind = 0;
      applKey  = "";
    end;
    sb_casln.dropFilter;
  end;

  if ( applKey != "" )
    sb_casln.KeyNum = 0;
    sb_casln.Clear;
    sb_casln.rec.ApplicationKind1 = applKind;
    sb_casln.rec.ApplicationKey1  = applKey;
    sb_casln.rec.NumLinkDoc       = 32000;
    sb_casln.addFilter( "t.t_ApplicationKind1 = " + String( applKind ) + " and " +
                        "t.t_ApplicationKey1 = " + GetSQLString( applKey )        );

    if (   sb_casln.GetLE                             and
         ( sb_casln.rec.ApplicationKind1 == applKind ) and
         ( sb_casln.rec.ApplicationKey1  == applKey  )    )
      numLastLink = sb_casln.rec.NumLinkDoc;
    end;
    sb_casln.dropFilter;
  end;

  // Обновление записи по счету ------------------------------------------
  if ( not depositr.GetPos )
    AbortTrn( );
  end;

  if ( not depositr.GetDirect )
    AbortTrn( );
  end;

  depositr.rec.Sum_In      = 0;
  depositr.rec.Sum_Out     = 0;
  depositr.rec.Sum_Rest    = DenomSum( depositr.rec.Sum_Rest );
  depositr.rec.Limit       = DenomSum( depositr.rec.Limit );
  depositr.rec.SumPercOut  = DenomSum( depositr.rec.SumPercOut );
  depositr.rec.StaticRest  = DenomSum( depositr.rec.StaticRest );
  //depositr.rec.ArrestedSum = DenomSum( depositr.rec.ArrestedSum );
  if ( not depositr.Update )
    AbortTrn( );
  end;

  // Вставка вкладного документа на деноминацию --------------------------
  sbdepdoc.Clear;
  InitNewDepDoc( sbdepdoc.rec );
  sbdepdoc.rec.Date_Document    = Date( 31, 12, 1997 );
  sbdepdoc.rec.DepDate_Document = Date( 31, 12, 1997 );
  sbdepdoc.rec.NumDayDoc        = numLastDoc + 10;
  sbdepdoc.rec.KindOp           = 2;
  sbdepdoc.rec.OutSum           = oldRest;
  sbdepdoc.rec.iApplicationKind = 1;
  sbdepdoc.rec.ApplicationKey   = FormApplicationKey( 1 );
  sbdepdoc.rec.YesSbook         = "X";
  sbdepdoc.rec.Ground           = "Деноминация 01.01.98";
  if ( not sbdepdoc.Insert )
    AbortTrn( );
  end;

  if ( applKey != "" )
    sb_casln.Clear;
    sb_casln.rec.ApplicationKind1 = applKind;
    sb_casln.rec.ApplicationKey1  = applKey;
    sb_casln.rec.NumLinkDoc       = numLastLink + 10;
    sb_casln.rec.ApplicationKind2 = sbdepdoc.rec.iApplicationKind;
    sb_casln.rec.ApplicationKey2  = sbdepdoc.rec.ApplicationKey;
    sb_casln.rec.FNcash           = branchLink;

    if ( not sb_casln.Insert )
      AbortTrn( );
    end;
  end;

  sbdepdoc.Clear;
  InitNewDepDoc( sbdepdoc.rec );
  sbdepdoc.rec.Date_Document    = Date( 31, 12, 1997 );
  sbdepdoc.rec.DepDate_Document = Date( 31, 12, 1997 );
  sbdepdoc.rec.NumDayDoc        = numLastDoc + 20;
  sbdepdoc.rec.KindOp           = 1;
  sbdepdoc.rec.InSum            = depositr.rec.Sum_Rest;
  sbdepdoc.rec.Rest             = depositr.rec.Sum_Rest;
  sbdepdoc.rec.iApplicationKind = 1;
  sbdepdoc.rec.ApplicationKey   = FormApplicationKey( 1 );
  sbdepdoc.rec.YesSbook         = "X";
  sbdepdoc.rec.Ground           = "Деноминация 01.01.98";
  if ( not sbdepdoc.Insert )
    AbortTrn( );
  end;

  if ( applKey != "" )
    sb_casln.Clear;
    sb_casln.rec.ApplicationKind1 = applKind;
    sb_casln.rec.ApplicationKey1  = applKey;
    sb_casln.rec.NumLinkDoc       = numLastLink + 20;
    sb_casln.rec.ApplicationKind2 = sbdepdoc.rec.iApplicationKind;
    sb_casln.rec.ApplicationKey2  = sbdepdoc.rec.ApplicationKey;
    sb_casln.rec.FNcash           = branchLink;

    if ( not sb_casln.Insert )
      AbortTrn( );
    end;
  end;

  // Обновление в файле для расчета процентов ----------------------------
  pc__calc.Clear;
  pc__calc.rec.Referenc = depositr.rec.Referenc;
  pc__calc.addFilter( "t.t_Referenc = " + String( depositr.rec.Referenc ) );

  stat = pc__calc.GetGE;

  while ( stat )
    if ( pc__calc.rec.Referenc == depositr.rec.Referenc )
      if ( pc__calc.rec.EndDate > limitDate )
        pc__calc.rec.SumCalc      = DenomSum( pc__calc.rec.SumCalc );
        pc__calc.rec.SumPay       = DenomSum( pc__calc.rec.SumPay );
        pc__calc.rec.SumForPay    = DenomSum( pc__calc.rec.SumForPay );
        pc__calc.rec.SumPayAll    = DenomSum( pc__calc.rec.SumPayAll );
        pc__calc.rec.TaxSumCalc   = DenomSum( pc__calc.rec.TaxSumCalc );
        pc__calc.rec.TaxSumPay    = DenomSum( pc__calc.rec.TaxSumPay );
        pc__calc.rec.TaxSumForPay = DenomSum( pc__calc.rec.TaxSumForPay );
        pc__calc.rec.SumCalcDelta = 0;
        if ( not pc__calc.Update )
          pc__calc.dropFilter;
          AbortTrn( );
        end;
      end;
      stat = pc__calc.Next;
    else
      stat = false;
    end;
  end;
  pc__calc.dropFilter;

  // Обновление в файле остатков по счету --------------------------------
  pc_drest.Clear;
  pc_drest.rec.Referenc = depositr.rec.Referenc;
  pc_drest.addFilter( "t.t_Referenc = " + String( depositr.rec.Referenc ) );

  stat = pc_drest.GetGE;

  while ( stat )
    if ( pc_drest.rec.Referenc == depositr.rec.Referenc )
      if ( pc_drest.rec.Date_Rest >= limitDate )
        pc_drest.rec.Rest = DenomSum( pc_drest.rec.Rest );
        if ( not pc_drest.Update )
          pc_drest.dropFilter;
          AbortTrn( );
        end;
      end;
      stat = pc_drest.Next;
    else
      stat = false;
    end;
  end;
  pc_drest.dropFilter;

  // Обновление записи реестра -------------------------------------------
  if ( not chcnrgst.GetPos )
    AbortTrn( );
  end;

  if ( not chcnrgst.GetDirect )
    AbortTrn( );
  end;

  chcnrgst.rec.NumStepOperation = 14;
  if ( not chcnrgst.Update )
    AbortTrn( );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DenominationSums

  var stat = true;

  if ( chcnrgst.rec.NumStepOperation != 13 )
    chcnrgst.rec.NumStepOperation = 13;
    stat = chcnrgst.Update;
  end;
 
  if ( stat )
    if ( not ProcessTrn( 0, "T_DenominationSums" ) )
      stat = false;
    end;
  end;

  if ( not stat )
    MsgBox( "Ошибка при деноминации" );
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro T_CarryCompens40p

  record sbdepdoc71_10 ( sbdepdoc );

  var wasOpenLink = false;
  var stat = true;

  if ( chcnrgst.rec.Compens40p != 0 )
    stat = OpenInterDesk(  depositr.rec.Referenc, wasOpenLink );

    if ( stat )
      ClearRecord( sbdepdoc71_10 );
      InitNewDepDoc( sbdepdoc71_10 );
      sbdepdoc71_10.TypeComplexOper  = 71;
      sbdepdoc71_10.TypeOper         = 71;
      sbdepdoc71_10.ApplType         = 10;
      sbdepdoc71_10.Date_Document    = Date( 28, 2, 1991 );
      sbdepdoc71_10.DepDate_Document = Date( 28, 2, 1991 );
      sbdepdoc71_10.InSum            = chcnrgst.rec.Compens40p;
      sbdepdoc71_10.YesSbook         = "X";
      sbdepdoc71_10.YesForm          = "X";
      sbdepdoc71_10.Ground           = "Выплата 40-процентной компенсации бывшего ЧСБ";

      if ( РучнойВводВкладногоДокумента( sbdepdoc71_10, 2 ) != 0 )
        stat = false;
      end;
    end;

    if ( wasOpenLink )
      InterDesk_EndDocBunch( );
    end;

    if ( not stat )
      AbortTrn( );
    end;
  end;

  // Обновление записи реестра -------------------------------------------
  if ( not chcnrgst.GetPos )
    AbortTrn( );
  end;

  if ( not chcnrgst.GetDirect )
    AbortTrn( );
  end;

  chcnrgst.rec.NumStepOperation = 12;
  if ( not chcnrgst.Update )
    AbortTrn( );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro CarryCompens40p

  var stat = true;
  var wasOpenLink = false;

  if ( chcnrgst.rec.NumStepOperation != 11 )
    chcnrgst.rec.NumStepOperation = 11;
    stat = chcnrgst.Update;
  end;

  if ( stat )
    if ( chcnrgst.rec.Compens40p != 0 )
      stat = ProcessTrn( 1, "T_CarryCompens40p" );
    else
      chcnrgst.rec.NumStepOperation = 12;
      stat = chcnrgst.Update;
    end;
  end;

  if ( not stat )
    MsgBox( "Ошибка при включении в список компенсаций,|учитываемых фиксированной датой" );
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro T_CarryCompens1_2_3x

  record sbdepdoc71_10 ( sbdepdoc );

  var stat;
  var percSum = $0L;
  var taxSum  = $0L;

  if ( chcnrgst.rec.Compens1 != 0 )
    ClearRecord( sbdepdoc71_10 );
    InitNewDepDoc( sbdepdoc71_10 );
    sbdepdoc71_10.TypeComplexOper  = 71;
    sbdepdoc71_10.TypeOper         = 71;
    sbdepdoc71_10.ApplType         = 10;
    sbdepdoc71_10.Date_Document    = PCHCNPAY.DatePay;
    sbdepdoc71_10.DepDate_Document = PCHCNPAY.DatePay;
    sbdepdoc71_10.InSum            = chcnrgst.rec.Compens1;
    sbdepdoc71_10.YesSbook         = "X";
    sbdepdoc71_10.YesForm          = "X";
    sbdepdoc71_10.Ground           = "Выплата предварительной компенсации бывшего ЧСБ";

    if ( РучнойВводВкладногоДокумента( sbdepdoc71_10, 2 ) != 0 )
      AbortTrn( );
    end;
  end;

  if ( chcnrgst.rec.Compens2 != 0 )
    ClearRecord( sbdepdoc71_10 );
    InitNewDepDoc( sbdepdoc71_10 );
    sbdepdoc71_10.TypeComplexOper  = 71;
    sbdepdoc71_10.TypeOper         = 71;
    sbdepdoc71_10.ApplType         = 10;
    sbdepdoc71_10.Date_Document    = PCHCNPAY.DatePay;
    sbdepdoc71_10.DepDate_Document = PCHCNPAY.DatePay;
    sbdepdoc71_10.InSum            = chcnrgst.rec.Compens2;
    sbdepdoc71_10.YesSbook         = "X";
    sbdepdoc71_10.YesForm          = "X";
    sbdepdoc71_10.Ground           = "Выплата дополнительной компенсации бывшего ЧСБ";

    if ( РучнойВводВкладногоДокумента( sbdepdoc71_10, 2 ) != 0 )
      AbortTrn( );
    end;
  end;

  if ( chcnrgst.rec.Compens3x != 0 )
    ClearRecord( sbdepdoc71_10 );
    InitNewDepDoc( sbdepdoc71_10 );
    sbdepdoc71_10.TypeComplexOper  = 71;
    sbdepdoc71_10.TypeOper         = 71;
    sbdepdoc71_10.ApplType         = 10;
    sbdepdoc71_10.Date_Document    = PCHCNPAY.DatePay;
    sbdepdoc71_10.DepDate_Document = PCHCNPAY.DatePay;
    sbdepdoc71_10.InSum            = chcnrgst.rec.Compens3x;
    sbdepdoc71_10.YesSbook         = "X";
    sbdepdoc71_10.YesForm          = "X";
    sbdepdoc71_10.Ground           = "Выплата трехкратной компенсации бывшего ЧСБ";

    if ( РучнойВводВкладногоДокумента( sbdepdoc71_10, 2 ) != 0 )
      AbortTrn( );
    end;
  end;

  // Обновление записи реестра -------------------------------------------
  if ( not chcnrgst.GetPos )
    AbortTrn( );
  end;

  if ( not chcnrgst.GetDirect )
    AbortTrn( );
  end;

  pc__calc.Clear;
  pc__calc.rec.Referenc = depositr.rec.Referenc;
  pc__calc.addFilter( "t.t_Referenc = " + String( depositr.rec.Referenc ) );

  stat = pc__calc.GetGE;

  while ( stat )
    if ( pc__calc.rec.Referenc == depositr.rec.Referenc )
      if ( ( pc__calc.rec.EndDate >  chcnrgst.rec.DateLastOperation ) and
           ( pc__calc.rec.EndDate <= PCHCNPAY.DatePay               )    )
       if ( pc__calc.rec.EndDate <= Date( 31, 12, 1997 ) )
          percSum = percSum + DenomSum( pc__calc.rec.SumPay );
          taxSum  = taxSum + DenomSum( pc__calc.rec.TaxSumPay );
        else
          percSum = percSum + pc__calc.rec.SumPay;
          taxSum  = taxSum + pc__calc.rec.TaxSumPay;
        end;
      end;
      stat = pc__calc.Next;
    else
      stat = false;
    end;
  end;
  pc__calc.dropFilter;

  chcnrgst.rec.BwasPay    = 1;
  chcnrgst.rec.DatePay    = PCHCNPAY.DatePay;
  chcnrgst.rec.Percent    = percSum;
  chcnrgst.rec.Tax        = taxSum;
  chcnrgst.rec.Account    = PCHCNPAY.Account;
  chcnrgst.rec.Rest200691 = DefRestFromDocOnEndDepDate( Date( 20, 6, 1991 ) );
  chcnrgst.rec.Rest010192 = DefRestFromDocOnEndDepDate( Date( 31, 12, 1991 ) );
  chcnrgst.rec.Rest010793 = DefRestFromDocOnEndDepDate( Date( 30, 6, 1993 ) );
  chcnrgst.rec.Rest010195 = DefRestFromDocOnEndDepDate( Date( 31, 12, 1994 ) );
  chcnrgst.rec.Branch     = Branch;

  chcnrgst.rec.NumStepOperation = 16;
  if ( not chcnrgst.Update )
    AbortTrn( );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro CarryCompens1_2_3x

  var wasOpenLink = false;
  var stat = true;
  
  if ( chcnrgst.rec.NumStepOperation != 15 )
    chcnrgst.rec.NumStepOperation = 15;
    stat = chcnrgst.Update;
  end;

  if ( stat )
    stat = OpenInterDesk2( chcnrgst.rec.Referenc, PCHCNPAY.DatePay, wasOpenLink );
  end;

  if ( stat and ( not wasOpenLink ) )
    stat = OpenInterDesk( chcnrgst.rec.Referenc, wasOpenLink );
  end;

  if ( stat )
    stat = ProcessTrn( 1, "T_CarryCompens1_2_3x" );
  end;

  if ( wasOpenLink )
    InterDesk_EndDocBunch( );
  end;

  if ( not stat )
    MsgBox( "Ошибка при включении в список компенсаций,|учитываемых датой выплаты" );
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro IsOperPaymentChcnDep ( recDoc )

  var retValue = false;

  if ( ( recDoc.TypeComplexOper ==  4 ) or  // Расход
       ( recDoc.TypeComplexOper == 63 ) or  // Списание по разовому поручению
       ( recDoc.TypeComplexOper == 64 ) or  // Списание по длительному поручению
       ( recDoc.TypeComplexOper == 65 ) or  // Списание переводом вклада
       ( recDoc.TypeComplexOper == 66 ) or  // Списание в свой филиал
       ( recDoc.TypeComplexOper == 67 ) or  // Списание в чужой филиал
       ( recDoc.TypeComplexOper == 80 ) or  // Конвертация валюты
       ( recDoc.TypeComplexOper == 10 ) or  // Закрытие наличными
       ( recDoc.TypeComplexOper == 81 ) or  // Закрытие безналичными
       ( recDoc.TypeComplexOper == 96 )   ) // Закрытие переводом
    if ( (   recDoc.TypeOper  ==  4      ) or // Расход
         (   recDoc.TypeOper  == 63      ) or // Списание по разовому поручению
         (   recDoc.TypeOper  == 64      ) or // Списание по длительному поручению
         ( ( recDoc.TypeOper  == 65 ) and     // Списание переводом вклада
           ( recDoc.KindOp    !=  5 )    ) or
         (   recDoc.TypeOper  == 66      ) or // Списание в свой филиал
         (   recDoc.TypeOper  == 67      ) or // Списание в чужой филиал
         ( ( recDoc.TypeOper  == 80 ) and     // Конвертация валюты
           ( recDoc.KindOp    !=  5 )    )   )
      retValue = true;
    end;
  end;

  return retValue;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro CarryCHCNPay

  var stat = true;
  var cycle;
  var retMakeOper = 0;

  InterDesk_EndDocBunch( );

  if ( chcnrgst.rec.NumStepOperation != 16 )
    chcnrgst.rec.NumStepOperation = 16;
    stat = chcnrgst.Update;
  end;

  if ( stat )
    if ( PCHCNPAY.FlagWill == "" )
      ClearRecord( chcnTmpDoc );
      InitNewDepDoc( chcnTmpDoc );
      chcnTmpDoc.Date_Document    = {curdate};
      chcnTmpDoc.DepDate_Document = PCHCNPAY.DatePay;
      chcnTmpDoc.YesSbook         = "X";
      chcnTmpDoc.IsControl        = StrFor(1); /* Признак главного документа */
      chcnTmpDoc.OutSum           = DefRestFromDocOnEndDate( Date( 30, 12, 9999 ) );

      if ( numOpert == 44 )
        chcnTmpDoc.TypeOper        = 63; // Списание по разовому поручению
        chcnTmpDoc.TypeComplexOper = 81; // Закрытие безналичными

        chcnTmpDoc.MFO_Receiver     = PCHATTR.BIK;
        chcnTmpDoc.CorAcc_Receiver  = PCHATTR.CorrAccount;
        chcnTmpDoc.Account_Receiver = PCHATTR.Account;
        chcnTmpDoc.CodeCur_Receiver = chcnTmpDoc.Code_Currency;
      else
        chcnTmpDoc.TypeOper        =  4; // Расход
        chcnTmpDoc.TypeComplexOper = 10; // Закрытие наличными
      end;

      ClearRecord( chcnOperPrm );
      chcnOperPrm.Account         = depositr.rec.Account;
      chcnOperPrm.Type_Account    = depositr.rec.Type_Account;
      chcnOperPrm.Code_Currency   = depositr.rec.Code_Currency;
      chcnOperPrm.TypeComplexOper = chcnTmpDoc.TypeComplexOper;
      chcnOperPrm.Type_Oper       = chcnTmpDoc.TypeComplexOper;
      chcnOperPrm.Show_Panel      = 0;
      chcnOperPrm.UseMaxDate      = 1;
      chcnOperPrm.NoPrint         = 1;

      retMakeOper = Выполнение_Операции( chcnOperPrm, chcnTmpDoc );

      if ( retMakeOper != 0 )
        stat = false;
      end;
    else
      stat = false;
      СписокНаследниковВклада( depositr.rec.Referenc );

      sbdepdoc.Clear;
      sbdepdoc.rec.Referenc = depositr.rec.Referenc;
      sbdepdoc.rec.Date_Document = Date( 1, 1, 1995 );
      sbdepdoc.addFilter( "t.t_Referenc = " + String( depositr.rec.Referenc ) );

      cycle = sbdepdoc.GetGE;

      while ( cycle )
        if ( sbdepdoc.rec.Referenc == depositr.rec.Referenc )
          if ( IsServDoc( sbdepdoc.rec ) and ( sbdepdoc.rec.FlagStorn == "" ) )
            if ( IsOperPaymentChcnDep( sbdepdoc.rec ) )
              stat = true;
            end;
          end;
          if ( stat )
            cycle = false;
          else
            cycle = sbdepdoc.Next;
          end;
        else
          cycle = false;
        end;
      end;
      sbdepdoc.dropFilter;

      if ( not stat )
        MsgBox( "Не было проведено документов" );
        InterDesk_EndDocBunch( );
        return stat;
      end;
    end;
  end;

  InterDesk_EndDocBunch( );

  if ( stat )
    stat = FindDepositr( chcnrgst.rec.Referenc );
  end;

  if ( stat )
    chcnrgst.rec.NumStepOperation = 17;
    stat = chcnrgst.Update;
  end;

  if ( not stat )
    if ( retMakeOper != 0 )
      MsgBox( "Ошибка при проводке выплаты ", retMakeOper );
    else
      MsgBox( "Ошибка при проводке выплаты" );
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro InitPayDoc

  pay_doc.Clear;
  pay_doc.rec.FNCash           = depositr.rec.FNCash;
  pay_doc.rec.IsCur            = depositr.rec.IsCur;
  pay_doc.rec.NumOpert         = numOpert;
  pay_doc.rec.GroupOpert       = 41;
  pay_doc.rec.Date_Document    = PCHCNPAY.DatePay;
  pay_doc.rec.CodCur           = depositr.rec.Code_Currency;
  pay_doc.rec.Oper             = {oper};
  pay_doc.rec.CodClient        = depositr.rec.CodClient;
  pay_doc.rec.iApplicationKind = 200;
  pay_doc.rec.ApplicationKey   = FormApplicationKey( 200 );
  if ( depclnt.GetRecord( depositr.rec.CodClient ) )
    pay_doc.rec.FlagRez        = depclnt.CurRec.rec.NotResident;
  end;
  if ( numOpert == 44 )
    pay_doc.rec.VidDoc         = StrFor( 1 );
  end;

  // Информация о клиенте, проведшем операцию
  if ( depclnt.GetRecord( sbdepdoc.rec.CodClient ) )
    pay_doc.rec.ClientLastName   = depclnt.CurRec.rec.Name1;
    pay_doc.rec.ClientFirstName  = depclnt.CurRec.rec.Name2;
    pay_doc.rec.ClientSecondName = depclnt.CurRec.rec.Name3;
    pay_doc.rec.PersIDType       = depclnt.CurRec.rec.PaperKind ;
    pay_doc.rec.PassportSer      = depclnt.CurRec.rec.PaperSeries;
    pay_doc.rec.PassportNumb     = depclnt.CurRec.rec.PaperNumber;
    pay_doc.rec.PassportIssuer   = depclnt.CurRec.rec.PaperIssuer;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefSums ( sumRest, sumCompens1, sumCompens2, sumCompens3x, sumPercent, sumTax )

  var prevRest      = $0L;
  var prevCompens1  = $0L;
  var prevCompens2  = $0L;
  var prevCompens3x = $0L;
  var prevPercent   = $0L;
  var prevTax       = $0L;
  var deltaSum      = $0L;
  var qPart0_m;
  var qPart0_n;
  var stat;

  // Определим, что было выплачено ранее
  chcn_pay.KeyNum = 0;
  chcn_pay.Clear;
  chcn_pay.rec.RgtpID = chcnrgst.rec.RgtpID;
  chcn_pay.rec.RgstID = chcnrgst.rec.RgstID;
  chcn_pay.addFilter( "t.t_RgtpID = " + String( chcnrgst.rec.RgtpID ) + " and " +
                      "t.t_RgstID = " + String( chcnrgst.rec.RgstID )            );

  stat = chcn_pay.GetGE( );

  while ( stat )
    if ( ( chcn_pay.rec.RgtpID == chcnrgst.rec.RgtpID ) and
         ( chcn_pay.rec.RgstID == chcnrgst.rec.RgstID )    )
      prevRest      = prevRest + chcn_pay.rec.PartRest;
      prevCompens1  = prevCompens1 + chcn_pay.rec.PartCompens1;
      prevCompens2  = prevCompens2 + chcn_pay.rec.PartCompens2;
      prevCompens3x = prevCompens3x + chcn_pay.rec.PartCompens3x;
      prevPercent   = prevPercent + chcn_pay.rec.PartPercent;
      prevTax       = prevTax + chcn_pay.rec.PartTax;
      stat = chcn_pay.Next;
    else
      stat = false;
    end;
  end;
  chcn_pay.dropFilter;

  sumRest = chcnrgst.rec.RestAfterLastOperation + chcnrgst.rec.Compens40p - prevRest;
  sumCompens1 = chcnrgst.rec.Compens1 - prevCompens1;
  sumCompens2 = chcnrgst.rec.Compens2 - prevCompens2;
  sumCompens3x = chcnrgst.rec.Compens3x - prevCompens3x;
  sumPercent = chcnrgst.rec.Percent - prevPercent;
  sumTax = chcnrgst.rec.Tax - prevTax;

  if ( ( sbdepdoc.rec.TypeComplexOper == 10 ) or  // Закрытие наличными
       ( sbdepdoc.rec.TypeComplexOper == 81 ) or  // Закрытие безналичными
       ( sbdepdoc.rec.TypeComplexOper == 96 )   ) // Закрытие переводом
    deltaSum = sbdepdoc.rec.OutSum - ( DenomSum( sumRest ) + sumCompens1 + sumCompens2 + sumCompens3x + sumPercent - sumTax );
  else
    if ( sbdepdoc.rec.ListTransfer > 0 )
      sbtrast.KeyNum = 4;
      sbtrast.Clear;
      sbtrast.rec.RefID = sbdepdoc.rec.ListTransfer;
      if ( sbtrast.GetEQ )
        qPart0_m = sbtrast.rec.QPart0_m;
        qPart0_n = sbtrast.rec.QPart0_n;

        if ( ( qPart0_m == 0 ) or ( qPart0_n == 0 ) )
          qPart0_m = sbtrast.rec.QPart_m;
          qPart0_n = sbtrast.rec.QPart_n;
        end;

        sumRest = Money( ( chcnrgst.rec.RestAfterLastOperation + chcnrgst.rec.Compens40p ) * qPart0_m / qPart0_n );
        sumCompens1 = Money( chcnrgst.rec.Compens1 * qPart0_m / qPart0_n );
        sumCompens2 = Money( chcnrgst.rec.Compens2 * qPart0_m / qPart0_n );
        sumCompens3x = Money( chcnrgst.rec.Compens3x * qPart0_m / qPart0_n );
        sumPercent = Money( ( chcnrgst.rec.Percent - prevPercent ) * sbtrast.rec.QPart_m / sbtrast.rec.QPart_n );
        sumTax = Money( ( chcnrgst.rec.Tax - prevTax ) * sbtrast.rec.QPart_m / sbtrast.rec.QPart_n );

        deltaSum = sbdepdoc.rec.OutSum - ( DenomSum( sumRest ) + sumCompens1 + sumCompens2 + sumCompens3x + sumPercent - sumTax );
      end;
    end;
  end;

  // Могут быть расхождения из-за округлений и деноминаций - подгоним
  if ( ( deltaSum <= $0.03L ) and ( deltaSum >= -$0.03L ) )
    sumPercent = sumPercent + deltaSum;
    chcnrgst.rec.Percent = chcnrgst.rec.Percent + deltaSum;
    if ( chcnrgst.rec.Percent < 0 )
      chcnrgst.rec.Percent = 0;
    end;
    if ( not chcnrgst.Update )
      ;
    end;
  end;
  
  SetParm( 0, sumRest );
  SetParm( 1, sumCompens1 );
  SetParm( 2, sumCompens2 );
  SetParm( 3, sumCompens3x );
  SetParm( 4, sumPercent );
  SetParm( 5, sumTax );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefAddGround

  var retVal = "Подразделение " + Trim( chcnrgst.rec.ChcnDepart ) + "/" + Trim( chcnrgst.rec.ChcnBranch );

  retVal = retVal + " Счет ";
  if ( chcnrgst.rec.Account != "" )
    retVal = retVal + chcnrgst.rec.Account;
  else
    retVal = retVal + chcnrgst.rec.RgstAccount;
  end;

  if ( sbdepdoc.rec.CodClient != depositr.rec.CodClient )
    if ( depclnt.GetRecord( depositr.rec.CodClient ) )
      if ( StrLen( Trim( depclnt.CurRec.rec.Name1 ) ) != 0 )
        retVal = retVal + " Владелец " + Trim( depclnt.CurRec.rec.Name1 );
        if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) != 0 )
          retVal = retVal + " " + SubStr( Trim( depclnt.CurRec.rec.Name1 ), 1, 1 ) + ".";
          if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) != 0 )
            retVal = retVal + SubStr( Trim( depclnt.CurRec.rec.Name1 ), 1, 1 ) + ".";
          end;
        end;
      end;
    end;
  end;

  return retVal;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro T_AppearanceAndFragmentationCHCNPay

  var applKind       = 0;
  var applKey        = "";
  var numLastLink    = 0;
  var branchLink     = 0;
  var wasPayedBefore = false;
  var percSum        = $0L;
  var taxSum         = $0L;
  var addGround      = "";
  var stat;
  // Разбивка сумм
  var sumRest        = $0L;
  var sumCompens1    = $0L;
  var sumCompens2    = $0L;
  var sumCompens3x   = $0L; 
  var sumPercent     = $0L;
  var sumTax         = $0L;

  // Уточним суммы процентов и налогов после проведенной операции --------
  pc__calc.Clear;
  pc__calc.rec.Referenc = depositr.rec.Referenc;
  pc__calc.addFilter( "t.t_Referenc = " + String( depositr.rec.Referenc ) );

  stat = pc__calc.GetGE;

  while ( stat )
    if ( pc__calc.rec.Referenc == depositr.rec.Referenc )
      if ( pc__calc.rec.EndDate > chcnrgst.rec.DateLastOperation )
       if ( pc__calc.rec.EndDate <= Date( 31, 12, 1997 ) )
          percSum = percSum + DenomSum( pc__calc.rec.SumPay );
          taxSum  = taxSum + DenomSum( pc__calc.rec.TaxSumPay );
        else
          percSum = percSum + pc__calc.rec.SumPay;
          taxSum  = taxSum + pc__calc.rec.TaxSumPay;
        end;
      end;
      stat = pc__calc.Next;
    else
      stat = false;
    end;
  end;
  pc__calc.dropFilter;

  if ( not chcnrgst.GetPos )
    AbortTrn( );
  end;

  if ( not chcnrgst.GetDirect )
    AbortTrn( );
  end;

  if ( ( chcnrgst.rec.Percent != percSum ) or ( chcnrgst.rec.Tax != taxSum ) )
    chcnrgst.rec.Percent = percSum;
    chcnrgst.rec.Tax     = taxSum;

    if ( not chcnrgst.Update )
      AbortTrn( );
    end;
  end;

  // Найдем последний не обработанный вкладной документ на выплату с вклада
  sbdepdoc.Clear;
  sbdepdoc.rec.Referenc = depositr.rec.Referenc;
  sbdepdoc.rec.Date_Document = Date( 31, 12, 9999 );
  sbdepdoc.rec.NumDayDoc = 32000;
  sbdepdoc.addFilter( "t.t_Referenc = " + String( depositr.rec.Referenc ) );

  stat = sbdepdoc.GetLE;
  while ( stat )
    if ( sbdepdoc.rec.Referenc == depositr.rec.Referenc )
      if ( IsServDoc( sbdepdoc.rec ) and ( sbdepdoc.rec.FlagStorn == "" ) and IsOperPaymentChcnDep( sbdepdoc.rec ) )
        chcn_pay.KeyNum = 1;
        chcn_pay.Clear;
        chcn_pay.rec.iApplicationKind = sbdepdoc.rec.iApplicationKind;
        chcn_pay.rec.ApplicationKey   = sbdepdoc.rec.ApplicationKey;
        if ( not chcn_pay.GetEQ )
          DefSums( sumRest, sumCompens1, sumCompens2, sumCompens3x, sumPercent, sumTax );

          chcn_pay.Clear;
          chcn_pay.rec.RgtpID           = chcnrgst.rec.RgtpID;
          chcn_pay.rec.RgstID           = chcnrgst.rec.RgstID;
          chcn_pay.rec.iApplicationKind = sbdepdoc.rec.iApplicationKind;
          chcn_pay.rec.ApplicationKey   = sbdepdoc.rec.ApplicationKey;
          chcn_pay.rec.PaperKindPay     = chcnrgst.rec.RgstPaperKindPay;
          chcn_pay.rec.DatePay          = sbdepdoc.rec.Date_Document;
          chcn_pay.rec.Branch           = Branch;
          chcn_pay.rec.PartRest         = sumRest;
          chcn_pay.rec.PartCompens1     = sumCompens1;
          chcn_pay.rec.PartCompens2     = sumCompens2;
          chcn_pay.rec.PartCompens3x    = sumCompens3x;
          chcn_pay.rec.PartPercent      = sumPercent;
          chcn_pay.rec.PartTax          = sumTax;
          if ( not chcn_pay.Insert )
            sbdepdoc.dropFilter;
            AbortTrn( );
          end;
        else
          wasPayedBefore = true;
        end;
        stat = false;
      else
        stat = sbdepdoc.Prev;
      end;
    else
      stat = false;
    end;
  end;
  sbdepdoc.dropFilter;

  if ( wasPayedBefore )
    return;
  end;

  // Определение параметров для формирования связок ----------------------
  sb_casln.KeyNum = 1;
  sb_casln.Clear;
  sb_casln.rec.ApplicationKind2 = sbdepdoc.rec.iApplicationKind;
  sb_casln.rec.ApplicationKey2  = sbdepdoc.rec.ApplicationKey;
  sb_casln.addFilter( "t.t_ApplicationKind2 = " + String( sbdepdoc.rec.iApplicationKind ) + " and " +
                      "t.t_ApplicationKey2 = " + GetSQLString( sbdepdoc.rec.ApplicationKey )         );

  if (   sb_casln.GetGE                                                   and
       ( sb_casln.rec.ApplicationKind2 == sbdepdoc.rec.iApplicationKind ) and
       ( sb_casln.rec.ApplicationKey2  == sbdepdoc.rec.ApplicationKey   )    )
    applKind = sb_casln.rec.ApplicationKind1;
    applKey = sb_casln.rec.ApplicationKey1;
    branchLink = sb_casln.rec.FNcash;
  end;
  sb_casln.dropFilter;

  // Определим номер последнего документа в связке -----------------------
  if ( applKey != "" )
    sb_casln.KeyNum = 0;
    sb_casln.Clear;
    sb_casln.rec.ApplicationKind1 = applKind;
    sb_casln.rec.ApplicationKey1  = applKey;
    sb_casln.rec.NumLinkDoc       = 32000;
    sb_casln.addFilter( "t.t_ApplicationKind1 = " + String( applKind ) + " and " +
                        "t.t_ApplicationKey1 = " + GetSQLString( applKey )        );

    if (   sb_casln.GetLE                             and
         ( sb_casln.rec.ApplicationKind1 == applKind ) and
         ( sb_casln.rec.ApplicationKey1  == applKey  )    )
      numLastLink = sb_casln.rec.NumLinkDoc;
    end;
    sb_casln.dropFilter;
  end;

  // Разбиение суммы выплаты по видам документов -------------------------
  addGround = DefAddGround( );

  if ( DenomSum( sumRest ) > 0 )
    InitPayDoc( );
    pay_doc.rec.ApplType = 6;
    pay_doc.rec.InSum    = DenomSum( sumRest );
    pay_doc.rec.Ground   = "Вклады бывшего ЧСБ. Последний остаток по счету. " + addGround;

    if ( not pay_doc.Insert )
      AbortTrn( );
    end;

    if ( applKey != "" )
      sb_casln.Clear;
      sb_casln.rec.ApplicationKind1 = applKind;
      sb_casln.rec.ApplicationKey1  = applKey;
      sb_casln.rec.NumLinkDoc       = numLastLink + 10;
      sb_casln.rec.ApplicationKind2 = pay_doc.rec.iApplicationKind;
      sb_casln.rec.ApplicationKey2  = pay_doc.rec.ApplicationKey;
      sb_casln.rec.FNcash           = branchLink;

      if ( not sb_casln.Insert )
        AbortTrn( );
      end;
    end;
  end;

  if ( sumCompens1 > 0 )
    InitPayDoc( );
    pay_doc.rec.ApplType = 1;
    pay_doc.rec.InSum    = sumCompens1;
    pay_doc.rec.Ground   = "Вклады бывшего ЧСБ. Предварительная компенсация. " + addGround;

    if ( not pay_doc.Insert )
      AbortTrn( );
    end;

    if ( applKey != "" )
      sb_casln.Clear;
      sb_casln.rec.ApplicationKind1 = applKind;
      sb_casln.rec.ApplicationKey1  = applKey;
      sb_casln.rec.NumLinkDoc       = numLastLink + 20;
      sb_casln.rec.ApplicationKind2 = pay_doc.rec.iApplicationKind;
      sb_casln.rec.ApplicationKey2  = pay_doc.rec.ApplicationKey;
      sb_casln.rec.FNcash           = branchLink;

      if ( not sb_casln.Insert )
        AbortTrn( );
      end;
    end;
  end;

  if ( sumCompens2 > 0 )
    InitPayDoc( );
    pay_doc.rec.ApplType = 2;
    pay_doc.rec.InSum    = sumCompens2;
    pay_doc.rec.Ground   = "Вклады бывшего ЧСБ. Дополнительная предварительная компенсация. " + addGround;

    if ( not pay_doc.Insert )
      AbortTrn( );
    end;

    if ( applKey != "" )
      sb_casln.Clear;
      sb_casln.rec.ApplicationKind1 = applKind;
      sb_casln.rec.ApplicationKey1  = applKey;
      sb_casln.rec.NumLinkDoc       = numLastLink + 30;
      sb_casln.rec.ApplicationKind2 = pay_doc.rec.iApplicationKind;
      sb_casln.rec.ApplicationKey2  = pay_doc.rec.ApplicationKey;
      sb_casln.rec.FNcash           = branchLink;

      if ( not sb_casln.Insert )
        AbortTrn( );
      end;
    end;
  end;

  if ( sumCompens3x > 0 )
    InitPayDoc( );
    pay_doc.rec.ApplType = 3;
    pay_doc.rec.InSum    = sumCompens3x;
    pay_doc.rec.Ground   = "Вклады бывшего ЧСБ. Трехкратная компенсация. " + addGround;

    if ( not pay_doc.Insert )
      AbortTrn( );
    end;

    if ( applKey != "" )
      sb_casln.Clear;
      sb_casln.rec.ApplicationKind1 = applKind;
      sb_casln.rec.ApplicationKey1  = applKey;
      sb_casln.rec.NumLinkDoc       = numLastLink + 40;
      sb_casln.rec.ApplicationKind2 = pay_doc.rec.iApplicationKind;
      sb_casln.rec.ApplicationKey2  = pay_doc.rec.ApplicationKey;
      sb_casln.rec.FNcash           = branchLink;

      if ( not sb_casln.Insert )
        AbortTrn( );
      end;
    end;
  end;

  if ( sumPercent - sumTax > 0 )
    InitPayDoc( );
    pay_doc.rec.ApplType = 4;
    pay_doc.rec.InSum    = sumPercent - sumTax;
    pay_doc.rec.Ground   = "Вклады бывшего ЧСБ. Начисленные проценты без налога. " + addGround;

    if ( not pay_doc.Insert )
      AbortTrn( );
    end;

    if ( applKey != "" )
      sb_casln.Clear;
      sb_casln.rec.ApplicationKind1 = applKind;
      sb_casln.rec.ApplicationKey1  = applKey;
      sb_casln.rec.NumLinkDoc       = numLastLink + 50;
      sb_casln.rec.ApplicationKind2 = pay_doc.rec.iApplicationKind;
      sb_casln.rec.ApplicationKey2  = pay_doc.rec.ApplicationKey;
      sb_casln.rec.FNcash           = branchLink;

      if ( not sb_casln.Insert )
        AbortTrn( );
      end;
    end;
  end;

  if ( sumTax > 0 )
    InitPayDoc( );
    pay_doc.rec.ApplType = 5;
    pay_doc.rec.InSum    = sumTax;
    pay_doc.rec.Ground   = "Вклады бывшего ЧСБ. Подоходный налог. " + addGround;

    if ( not pay_doc.Insert )
      AbortTrn( );
    end;

    if ( applKey != "" )
      sb_casln.Clear;
      sb_casln.rec.ApplicationKind1 = applKind;
      sb_casln.rec.ApplicationKey1  = applKey;
      sb_casln.rec.NumLinkDoc       = numLastLink + 60;
      sb_casln.rec.ApplicationKind2 = pay_doc.rec.iApplicationKind;
      sb_casln.rec.ApplicationKey2  = pay_doc.rec.ApplicationKey;
      sb_casln.rec.FNcash           = branchLink;

      if ( not sb_casln.Insert )
        AbortTrn( );
      end;
    end;
  end;

  // Обновление записи реестра -------------------------------------------
  if ( FlagPayHeir )
    chcnrgst.rec.NumStepOperation = 4;
  else
    chcnrgst.rec.NumStepOperation = 19;
  end;

  if ( not chcnrgst.Update )
    AbortTrn( );
  end;

  mainApplKind = applKind;
  mainApplKey  = applKey;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro AppearanceAndFragmentationCHCNPay

  var stat = true;

  if ( chcnrgst.rec.NumStepOperation != 17 )
    chcnrgst.rec.NumStepOperation = 17;
    stat = chcnrgst.Update;
  end;

  if ( stat )
    FlagPayHeir = false;
    stat = ProcessTrn( 0, "T_AppearanceAndFragmentationCHCNPay" );
  end;

  if ( not stat )
    MsgBox( "Ошибка при оформлении выплаты и разбиении суммы" );
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro IssueOrders

  var stat = true;

  InterDesk_EndDocBunch( );

  if ( chcnrgst.rec.NumStepOperation != 19 )
    chcnrgst.rec.NumStepOperation = 19;
    stat = chcnrgst.Update;
  end;

  // ?????

  if ( stat )
    chcnrgst.rec.NumStepOperation = 0;
    stat = chcnrgst.Update;
  end;

  if ( not stat )
    MsgBox( "Ошибка при выпуске ордеров" );
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro WorkDialogATTR ( IP, cmd, id, key )

  var stat = CM_DEFAULT;
  var i;

  // =====================================================================
  if ( cmd == DLG_KEY )
    // ENTER -----------------------------------------------------------
    if ( key == 13 )
      ;
    // ESC -------------------------------------------------------------
    elif ( key == 27 )
      stat = CM_SAVE;
    end;
  // =====================================================================
  elif ( cmd == DLG_SETFOCUS )
    ;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro InputAttrForPay

  RunDialog( PCHATTR, "WorkDialogATTR" );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetInfoFieldsPCHCNPAY( numField )

  // ---------------------------------------------------------------------
  if ( ( numField == ne_NameTypeAccount ) or ( numField == -1 ) )
    if ( TypeAccount == "" )
      PCHCNPAY.NameTypeAccount = "";
    else
      if ( FindTypeAccount( TypeAccount ) )
        PCHCNPAY.NameTypeAccount = sb_dtyp.rec.Name;
      else
        PCHCNPAY.NameTypeAccount = TypeAccount;
      end;
    end;
  end;

  // ---------------------------------------------------------------------
  if ( ( numField == ne_NameCurrency ) or ( numField == -1 ) )
    if ( chcnrgst.rec.RgstCodeCurrency < 0 )
      PCHCNPAY.NameCurrency = "";
    else
      currency.Clear;
      currency.rec.Code_Currency = chcnrgst.rec.RgstCodeCurrency;
      if ( currency.GetEQ )
        PCHCNPAY.NameCurrency = currency.rec.Short_Name;
      else
        PCHCNPAY.NameCurrency = String( chcnrgst.rec.RgstCodeCurrency );
      end;
    end;
  end;

  // ---------------------------------------------------------------------
  if ( ( numField == ne_AccountInBase ) or ( numField == -1 ) )
    PCHCNPAY.AccountInBase = "";
    if ( chcnrgst.rec.Referenc != 0 )
      if ( FindDepositr( chcnrgst.rec.Referenc ) )
        PCHCNPAY.AccountInBase = depositr.rec.Account;
      end;
    end;
  end;

  // ---------------------------------------------------------------------
  if ( ( numField == ne_RgstNameOwnerAccount ) or ( numField == -1 ) )
    PCHCNPAY.RgstNameOwnerAccount = chcnrgst.rec.RgstName1;
    if ( StrLen( chcnrgst.rec.RgstName2 ) > 0 )
      PCHCNPAY.RgstNameOwnerAccount = PCHCNPAY.RgstNameOwnerAccount + " " + 
                                      SubStr( chcnrgst.rec.RgstName2, 1, 1 ) + ".";
      if ( StrLen( chcnrgst.rec.RgstName3 ) > 0 )
        PCHCNPAY.RgstNameOwnerAccount = PCHCNPAY.RgstNameOwnerAccount + 
                                        SubStr( chcnrgst.rec.RgstName3, 1, 1 ) + ".";
      end;
    end;
  end;

  // ---------------------------------------------------------------------
  if ( ( numField == ne_NameDocument ) or ( numField == -1 ) )
    namealg.Clear;
    namealg.rec.iTypeAlg   = 871;
    namealg.rec.iNumberAlg = chcnrgst.rec.RgstPaperKindPay;
    if ( namealg.GetEQ )
      PCHCNPAY.NameDocument = namealg.rec.szNameAlg;
    else
      PCHCNPAY.NameDocument = "";
    end;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ListFieldsPCHCNPAY( numField, numKey )

  array aChoice;
  array aNameMenu;

  var stat;
  var i;
  var j = 0;
  var tmpAccount;
  var tmpStr;

  // ---------------------------------------------------------------------
  if ( numField == ne_NameTypeAccount )
    if ( numKey == 317 )
      if ( chcnrgst.rec.Account != "" )
        tmpAccount = chcnrgst.rec.Account;
      else
        tmpAccount = chcnrgst.rec.RgstAccount;
      end;

      while ( j < ASize( aSymbTypeAcc ) )
        if ( ( tmpAccount           != ""                          ) and 
             ( aSymbTypeAcc( j )    != ""                          ) and 
             ( StrLen( tmpAccount ) >  StrLen( aSymbTypeAcc( j ) ) )    )
          if ( StrUpr ( SubStr( tmpAccount, 1, StrLen( aSymbTypeAcc( j ) ) ) ) == StrUpr( aSymbTypeAcc( j ) ) )
            if ( FindTypeAccount( aNameTypeAcc( j ) ) )
              i = ASize( aNameMenu );
              aChoice( i ) = sb_dtyp.rec.Kind;
              aNameMenu( i ) = " " + sb_dtyp.rec.Name + MkStr( " ", 50 );
              aNameMenu( i ) = SubStr( aNameMenu( i ), 1, 43 );
            end;
          end;
        end;
        j = j + 1;
      end;
    elif ( numKey == 352 )
      sb_dtyp.KeyNum = 1;
      sb_dtyp.Clear;
      sb_dtyp.rec.FlagCur = FlagCur;
      stat = sb_dtyp.GetGE;
      while ( stat )
        if ( sb_dtyp.rec.FlagCur == FlagCur )
          if ( ( StrLen( sb_dtyp.rec.Kind ) > 3 ) and ( StrUpr ( SubStr( sb_dtyp.rec.Kind, 1, 3 ) ) == "ЧСБ" ) )
            // !!!!! Надо перейти на Index( sb_dtyp.rec.UserTypeAccount, "Ч" ) > 0
            i = ASize( aNameMenu );
            aChoice( i ) = sb_dtyp.rec.Kind;
            aNameMenu( i ) = " " + sb_dtyp.rec.Name + MkStr( " ", 50 );
            aNameMenu( i ) = SubStr( aNameMenu( i ), 1, 43 );
          end;
          stat = sb_dtyp.Next;
        else
          stat = false;
        end;
      end;
    end;

    if ( ASize( aNameMenu ) > 0 )
      i = Menu( aNameMenu, NULL, "Виды вкладов", 14, 8 );
      if ( i >= 0 )
        TypeAccount = aChoice( i );
      end;
    end;

    GetInfoFieldsPCHCNPAY( numField );
  end;

  // ---------------------------------------------------------------------
  if ( ( numField == ne_NameDocument ) and ( numKey == 317 ) )
    namealg.Clear;
    namealg.rec.iTypeAlg = 871;
    stat = namealg.GetGE;
    while ( stat )
      if ( namealg.rec.iTypeAlg == 871 )
        i = ASize( aNameMenu );
        aChoice( i ) = namealg.rec.iNumberAlg;
        aNameMenu( i ) = " " + namealg.rec.szNameAlg + " ";
        stat = namealg.Next;
      else
        stat = false;
      end;
    end;

    if ( ASize( aNameMenu ) > 0 )
      i = Menu( aNameMenu, NULL, "Документ", 26, 16 );
      if ( i >= 0 )
        chcnrgst.rec.RgstPaperKindPay = aChoice( i );
      end;
    end;

    GetInfoFieldsPCHCNPAY( numField );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro InitFieldsPCHCNPAY

  PCHCNPAY.DatePay     = {curdate};
  PCHCNPAY.DateOperDay = {curdate};
  PCHCNPAY.RgstAccount = chcnrgst.rec.RgstAccount;
  PCHCNPAY.Account     = chcnrgst.rec.Account;
  PCHCNPAY.Permit      = chcnrgst.rec.Permit;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro SaveFieldsPCHCNPAY

  var stat = true;

  chcnrgst.rec.DatePay = PCHCNPAY.DatePay;
  chcnrgst.rec.Account = PCHCNPAY.Account;
  chcnrgst.rec.Permit  = PCHCNPAY.Permit;
                         
  stat = chcnrgst.Update;

  if ( not stat )
    MsgBox( "Ошибка при обновлении реестра вкладчиков" );
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro WorkDialogPAY ( IP, cmd, id, key )

  var stat = CM_DEFAULT;
  var flag = true;
  var i;

  // =====================================================================
  if (cmd == DLG_KEY)
    // ENTER -----------------------------------------------------------
    if ( key == 13 )
      ;
    // F2 --------------------------------------------------------------
    elif ( key == 316 )
      stat = CM_SAVE;
    // F3 --------------------------------------------------------------
    elif ( key == 317 )
      ListFieldsPCHCNPAY( id, key );
      UpdateFields( IP );
    // ^F3 -------------------------------------------------------------
    elif ( key == 352 )
      ListFieldsPCHCNPAY( id, key );
      UpdateFields( IP );
    // F5 --------------------------------------------------------------
    elif ( key == 319 )
      if ( numOpert == 44 )
        InputAttrForPay( );
      end;
    // F6 --------------------------------------------------------------
    elif ( key == 320 )
      if ( chcnrgst.rec.NumStepOperation <  9 )
        MsgBox( "Расчет компенсаций возможен только после ручного ввода операций" );
        flag = false;
      elif ( chcnrgst.rec.NumStepOperation > 11 )
        MsgBox( "Расчет компенсаций не возможен после включения|в список операций компенсации" );
        flag = false;
      end;

      if ( flag )
        flag = SaveFieldsPCHCNPAY( );
      end;

      if ( flag )
        if ( chcnrgst.rec.NumStepOperation <= 9 )
          ExecutionRegularOperationCompens( );
        end;

        if ( ( chcnrgst.rec.NumStepOperation == 10 ) or
             ( chcnrgst.rec.NumStepOperation == 11 )   )
          EditCHCNCompens( );
        end;
      end;
    // ^F6 -------------------------------------------------------------
    elif ( key == 355 )
      if ( chcnrgst.rec.Referenc == 0 )
        MsgBox( "Счет еще не открыт" );
        flag = false;
      elif ( chcnrgst.rec.NumStepOperation > 9 )
        MsgBox( "Проведены регламентные действия по счету|Ручной ввод операций невозможен" );
        flag = false;
      end;

      if ( flag )
        flag = SaveFieldsPCHCNPAY( );
      end;

      if ( flag )
        if ( chcnrgst.rec.NumStepOperation <= 6 )
          flag = DefCriticalDate( );
        end;

        if ( flag and ( chcnrgst.rec.NumStepOperation <= 7 ) )
          flag = FillPcCalc( );
        end;

        if ( flag and ( chcnrgst.rec.NumStepOperation <= 9 ) )
          InputDepHistory( true );
        end;
      end;
    // AltF6 -----------------------------------------------------------
    elif ( key == 365 )
      if ( chcnrgst.rec.Referenc == 0 )
        flag = OpenNewCHCNAccount( true );
      else
        if ( chcnrgst.rec.NumStepOperation > 9 )
          MsgBox( "Проведены регламентные действия по счету|Ручной ввод операций невозможен" );
          flag = false;
        end;
      end;

      if ( flag )
        flag = SaveFieldsPCHCNPAY( );
      end;

      if ( flag )
        if ( chcnrgst.rec.NumStepOperation <= 6 )
          flag = DefCriticalDate( );
        end;

        if ( flag and ( chcnrgst.rec.NumStepOperation <= 7 ) )
          flag = FillPcCalc( );
        end;

        if ( flag and ( chcnrgst.rec.NumStepOperation <= 9 ) )
          InputDepHistory( true );
        end;
      end;

      GetInfoFieldsPCHCNPAY( -1 );
      UpdateFields( IP );
    // F7 --------------------------------------------------------------
    elif ( key == 321 )            

      if ( ( chcnrgst.rec.NumStepOperation == 0 ) and
           ( chcnrgst.rec.Referenc         != 0 ) and
           ( chcnrgst.rec.BwasPay          != 0 )    )
        MsgBox( "Уже произведена выплата с печатью ордеров" );
        flag = false;
      end;

      if ( flag )
        if ( chcnrgst.rec.NumStepOperation < 11 )
          MsgBox( "Печать ордеров возможна только после расчета компенсаций" );
          flag = false;
        elif ( chcnrgst.rec.NumStepOperation < 19 )
          if ( GetTrue( true, "Не все этапы операции завершены. Завершить ?" ) )
            ;
          else
            flag = false;
          end;
        end;
      end;

      if ( flag )
        flag = SaveFieldsPCHCNPAY( );
      end;

      if ( flag )
        if ( chcnrgst.rec.NumStepOperation <= 9 )
          flag = ExecutionRegularOperationCompens( );
        end;

        if ( flag and ( chcnrgst.rec.NumStepOperation <= 10 ) )
          flag = EditCHCNCompens( );
        end;

        if ( flag and ( chcnrgst.rec.NumStepOperation <= 11 ) )
          flag =  CarryCompens40p( );
        end;

        if ( flag and ( chcnrgst.rec.NumStepOperation <= 12 ) )
          flag = ExecutionRegularOperationDenom( );
        end;

        if ( flag and ( chcnrgst.rec.NumStepOperation <= 13 ) )
          flag =  DenominationSums( );
        end;

        if ( flag and ( chcnrgst.rec.NumStepOperation <= 14 ) )
          flag = ExecutionRegularOperationPay( );
        end;
  
        if ( flag and ( chcnrgst.rec.NumStepOperation <= 15 ) )
          flag = CarryCompens1_2_3x( );
        end;

        if ( flag and ( chcnrgst.rec.NumStepOperation <= 16 ) )
          flag = CarryCHCNPay( );
        end;

        if ( flag and ( chcnrgst.rec.NumStepOperation <= 17 ) )
          flag = AppearanceAndFragmentationCHCNPay( );
        end;                 

        if ( flag and ( chcnrgst.rec.NumStepOperation <= 19 ) )
          IssueOrders( );
        end;
      end;
    // ^F7 -------------------------------------------------------------
    elif ( key == 356 )
      if ( PCHCNPAY.FlagTrust != "" )
        if ( chcnrgst.rec.Referenc == 0 )
          InputTrustClient( true );
        else
          MsgBox( "Счет уже открыт. Доверенность заводится при открытии счета" );
        end;
      end;
    // AltF7 -----------------------------------------------------------
    elif ( key == 366 )
      if ( chcnrgst.rec.Referenc == 0 )
        InputDepClient( true );
      else
        MsgBox( "Счет уже открыт. Владельца счета менять нельзя" );
      end;
    // Пробел ----------------------------------------------------------
    elif ( key == 32 )
      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
      if ( id == ne_FlagTrust )
        if ( PCHCNPAY.FlagTrust == "" )
          PCHCNPAY.FlagTrust = "X";
        else
          PCHCNPAY.FlagTrust = "";
        end;
      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
      elif ( id == ne_FlagWill )
        if ( PCHCNPAY.FlagWill == "" )
          PCHCNPAY.FlagWill = "X";
        else
          PCHCNPAY.FlagWill = "";
        end;
      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
      elif ( id == ne_Permit )
        if ( PCHCNPAY.Permit == "" )
          PCHCNPAY.Permit = "X";
        else
          PCHCNPAY.Permit = "";
        end;      
      end;
    // ESC -------------------------------------------------------------
    elif (key == 27)
      if ( ( chcnrgst.rec.NumStepOperation == 0 ) and
           ( chcnrgst.rec.Referenc         != 0 ) and
           ( chcnrgst.rec.BwasPay          != 0 )    )
        stat = CM_CANCEL;
      else
        if ( GetTrue( true, "Вы хотите отложить проведение операции ?" ) )
          stat = CM_CANCEL;
        else
          stat = CM_IGNORE;
        end;
      end;
    end;
  // =====================================================================
  elif ( cmd == DLG_REMFOCUS )
    ;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro PanelCarryOperation

  var stat = true;

  ClearRecord( PCHCNPAY );
  ClearRecord( PCHATTR );

  // Счет уже есть, панель не вызывается ---------------------------------
  if ( chcnrgst.rec.Referenc != 0 )
    stat = FindDepositr( chcnrgst.rec.Referenc );
    if ( stat )
      CodeClient = chcnrgst.rec.Referenc;
      stat = depclnt.GetRecord( depositr.rec.CodClient );
      if ( not stat )
        MsgBox( "Не найден клиент - владелец счета" );
      end;
    end;
    if ( stat )
      if ( depclnt.CurRec.rec.DateDeath != Date( 0, 0, 0 ) )
        PCHCNPAY.FlagWill = "X";
      else
        sbtrast.KeyNum = 0;
        sbtrast.Clear;
        sbtrast.rec.Referenc = depositr.rec.Referenc;
        sbtrast.rec.VidDoc   = "";

        sbtrast.addFilter( "t.t_Referenc = " + String( depositr.rec.Referenc ) );

        if (   sbtrast.GetGE                                   and
             ( sbtrast.rec.Referenc == depositr.rec.Referenc ) and
             ( sbtrast.rec.VidDoc   == ""                    )    )
          PCHCNPAY.FlagTrust = "X";
        end;
        sbtrast.dropFilter;
      end;
    end;
    if ( chcnrgst.rec.DatePay != Date( 0, 0, 0 ) )
      PCHCNPAY.DatePay = chcnrgst.rec.DatePay;
    else
      PCHCNPAY.DatePay = {curdate};
    end;
    PCHCNPAY.Account = chcnrgst.rec.Account;
    PCHCNPAY.Permit  = chcnrgst.rec.Permit;
  // Счета нет, вызовем панель -------------------------------------------
  else
    InitFieldsPCHCNPAY( );
    GetInfoFieldsPCHCNPAY( -1 );
    if ( RunDialog( PCHCNPAY, "WorkDialogPAY" ) )
      stat = SaveFieldsPCHCNPAY( );
    else
      stat = false;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ChcnPayClient

  if ( ( chcnrgst.rec.BwasPay          == 0 ) and
       ( chcnrgst.rec.Referenc         == 0 ) and
       ( chcnrgst.rec.NumStepOperation != 0 )    )
    chcnrgst.rec.NumStepOperation = 0;
    if ( not chcnrgst.Update )
      MsgBox( "Ошибка при обновлении реестра" );
      return -1;
    end;
  end;

  // Определение вида вклада ---------------------------------------------
  if ( not DefTypeAccount( ) )
    return -1;
  end;

  // Панель операции -----------------------------------------------------
  if ( not PanelCarryOperation( ) )
    if ( ( chcnrgst.rec.NumStepOperation == 0 ) and
         ( chcnrgst.rec.Referenc         != 0 ) and
         ( chcnrgst.rec.BwasPay          != 0 )    )
      return 0;
    else
      return -1;
    end;
  end;

  // Ввод вкладчика в справочник клиентов --------------------------------
  if ( not InputDepClient( false ) )
    return -1;
  end;

  // Ввод доверенного лица в справочник клиентов -------------------------
  if ( PCHCNPAY.FlagTrust != "" )
    if ( not InputTrustClient( false ) )
      return -1;
    end;
  end;

  // Открытие нового счета -----------------------------------------------
  if ( chcnrgst.rec.NumStepOperation <= 5 )
    if ( not OpenNewCHCNAccount( false ) )
      return -1;
    end;
  end;

  // Определение критической даты и состояния счета ----------------------
  if ( chcnrgst.rec.NumStepOperation <= 6 )
    if ( not DefCriticalDate( ) )
      return -1;
    end;
  end;

  // Заполнение графика расчета процентов --------------------------------
  if ( chcnrgst.rec.NumStepOperation <= 7 )
    if ( not FillPcCalc( ) )
      return -1;
    end;
  end;

  // Ручной ввод истории операций ----------------------------------------
  if ( chcnrgst.rec.NumStepOperation <= 8 )
    if ( not InputDepHistory( true ) )
      return -1;
    end;
  end;

  // Выполнение регламентных действий от критической даты до даты компенсации
  if ( chcnrgst.rec.NumStepOperation <= 9 )
    if ( not ExecutionRegularOperationCompens( ) )
      return -1;
    end;
  end;

  // Расчет компенсаций --------------------------------------------------
  if ( chcnrgst.rec.NumStepOperation <= 10 )
    if ( not EditCHCNCompens( ) )
      return -1;
    end;
  end;

  // Включение в список операций компенсаций -----------------------------
  if ( chcnrgst.rec.NumStepOperation <= 11 )
    if ( not CarryCompens40p( ) )
      return -1;
    end;
  end;

  // Выполнение регламентных операций до деноминации ---------------------
  if ( chcnrgst.rec.NumStepOperation <= 12 )
    if ( not ExecutionRegularOperationDenom( ) )
      return -1;
    end;
  end;

  // Деноминация ---------------------------------------------------------
  if ( chcnrgst.rec.NumStepOperation <= 13 )
    if ( not DenominationSums( ) )
      return -1;
    end;
  end;

  // Выполнение регламентных операций до даты выплаты --------------------
  if ( chcnrgst.rec.NumStepOperation <= 14 )
    if ( not ExecutionRegularOperationPay( ) )
      return -1;
    end;
  end;
      
  // Включение в список операций компенсаций, которые учитываются датой выплаты
  if ( chcnrgst.rec.NumStepOperation <= 15 )
    if ( not CarryCompens1_2_3x( ) )
      return -1;
    end;
  end;

  // Проводка выплаты ----------------------------------------------------
  if ( chcnrgst.rec.NumStepOperation <= 16 )
    if ( not CarryCHCNPay( ) )
      return -1;
    end;
  end;

  // Оформление выплаты --------------------------------------------------
  // Разбиение суммы выплаты по видам документов -------------------------
  if ( chcnrgst.rec.NumStepOperation <= 17 )
    if ( not AppearanceAndFragmentationCHCNPay( ) )
      return -1;
    end;
  end;                 

  // Выпуск ордеров ------------------------------------------------------
  if ( chcnrgst.rec.NumStepOperation <= 19 )
    if ( not IssueOrders( ) )
      return -1;
    end;
  end;

  if ( mainApplKey != "" )
    InterDesk_MakeDocBunchCurrent( mainApplKind, mainApplKey, false );
  else
    InterDesk_EndDocBunch( );
  end;

  return 0;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro AppearanceCHCNPayHeir

  var stat = true;

  if ( stat )
    FlagPayHeir = true;
    stat = ProcessTrn( 0, "T_AppearanceAndFragmentationCHCNPay" );
  end;

  if ( not stat )
    MsgBox( "Ошибка при оформлении выплаты и разбиении суммы" );
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro IssueOrdersHeir

  var stat = true;

  InterDesk_EndDocBunch( );

  // ?????

  if ( stat )
    chcnrgst.rec.NumStepOperation = 0;
    stat = chcnrgst.Update;
  end;

  if ( not stat )
    MsgBox( "Ошибка при выпуске ордеров" );
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ChcnPayNextHeir

  record AccParm( "AccParm.rec" );

  if ( not FindDepositr( chcnrgst.rec.Referenc ) )
    return -1;
  end;

  // Вызов панели счета
  ClearRecord( AccParm );
  AccParm.CodClient     = depositr.rec.CodClient;
  AccParm.Code_Currency = depositr.rec.Code_Currency;
  AccParm.TypeAccount   = depositr.rec.Type_Account;
  AccParm.Account       = depositr.rec.Account;

  // EditDepositor( AccParm );
  СписокНаследниковВклада( depositr.rec.Referenc );

  // Ввод строки в файл выплат
  if ( not AppearanceCHCNPayHeir( ) )
    return - 1;
  end;
    
  // Выпуск ордеров
  if ( chcnrgst.rec.NumStepOperation <= 4 )
    if ( not IssueOrdersHeir( ) )
      return -1;
    end;
  end;

  if ( mainApplKey != "" )
    InterDesk_MakeDocBunchCurrent( mainApplKind, mainApplKey, false );
  else
    InterDesk_EndDocBunch( );
  end;

  return 0;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefWhatDo

  var retValue = whatDo_Nothing;

  // Проверим режим валютности
  if ( ( chcnrgst.rec.RgstCodeCurrency > 0 ) and ( FlagCur == 0 ) )
    MsgBox( "Данный реестр надо проводить в валютном режиме" );
    retValue = whatDo_Error;
    return retValue;
  elif ( ( chcnrgst.rec.RgstCodeCurrency == 0 ) and ( FlagCur > 0 ) )
    MsgBox( "Данный реестр надо проводить в режиме национальной валюты" );
    retValue = whatDo_Error;
    return retValue;
  end;
     
  // Есть ссылка на счет - найдем счет и владельца счета
  if ( chcnrgst.rec.Referenc != 0 )
    if ( FindDepositr( chcnrgst.rec.Referenc ) )
      if ( depositr.rec.Open_Close != "" )
        MsgBox( "Счет закрыт" );
        retValue = whatDo_Nothing;
        return retValue;
      else
        if ( depclnt.GetRecord( depositr.rec.CodClient ) )
          ;
        else
          MsgBox( "Не найден клиент - владелец счета" );
          retValue = whatDo_Error;
        end;
      end;
    else
      retValue = whatDo_Error;
    end;
  end;

  if ( retValue == whatDo_Error )
    return retValue;
  end;

  // Определим что надо делать
  if ( chcnrgst.rec.BwasPay == 0 )
    if ( chcnrgst.rec.Referenc == 0 )
       retValue = whatDo_PayClient;
    else
      if ( depclnt.CurRec.rec.DateDeath == Date( 0, 0, 0 ) )
        retValue = whatDo_PayClient;
      else
        if ( chcnrgst.rec.NumStepOperation >= 17 )
          retValue = whatDo_PayClient;
        end;
      end;
    end;
  else
    if ( chcnrgst.rec.Referenc != 0 )
      if ( depclnt.CurRec.rec.DateDeath != Date( 0, 0, 0 ) )
        if ( chcnrgst.rec.NumStepOperation < 17 )
          retValue = whatDo_PayNextHeir;
        end;
      else
        if ( chcnrgst.rec.Compens3x == 0 )
          retValue = whatDo_PayCompens3x;
        end;
      end;
    else
      MsgBox( "При проставленном флаге выплаты отсутствует ссылка на счет" );
      retValue = whatDo_Error;
    end;
  end;

  return retValue;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ChcnPay ( rgtpID, rgstID, numOtherOpert )

  var whatDo = whatDo_Nothing;

  InterDesk_EndDocBunch( );

  // Определение реестра вкладчиков и прочей расходной операции
  if ( numOtherOpert == 0 )
    if ( not FindChcnRgst( rgtpID, rgstID ) )
      return -1;
    end;
    whatDo = DefWhatDo( );
    if ( ( whatDo == whatDo_PayClient ) or ( whatDo == whatDo_PayNextHeir ) )
      numOtherOpert = ChooseNumOtherOpert( );
      if ( numOtherOpert == 0 )
        return -1;
      end;
    end;
  elif ( rgstID == 0 )
    if ( not ChooseChcnRgst( rgtpID, rgstID ) )
      return -1;
    end;
    if ( not FindChcnRgst( rgtpID, rgstID ) )
      return -1;
    end;
    whatDo = DefWhatDo( );
  end;

  numOpert = numOtherOpert;

  // ---------------------------------------------------------------------
  if ( whatDo == whatDo_Error )
    return -1;
  // ---------------------------------------------------------------------
  elif ( whatDo == whatDo_Nothing )
    MsgBox( "Данный реестр возможно уже был полностью обработан" );
    return -1;
  // ---------------------------------------------------------------------
  elif ( whatDo == whatDo_PayClient )
    return ChcnPayClient( );
  // ---------------------------------------------------------------------
  elif ( whatDo == whatDo_PayNextHeir )
    return ChcnPayNextHeir( );
  // ---------------------------------------------------------------------
  elif ( whatDo == whatDo_PayCompens3x )
    return -1;
  // ---------------------------------------------------------------------
  else
    return -1;
  end;

  return 0;

end;
