/*

      Макрос заполнения номера и даты пенсионного удостоверения
        для видов вклада "Пенсионный" и "Срочный пенсионный".

*/

  import deprintr, issrvdoc, alg_vars, opercode;

  record dial ( "PENSCLNT" ) dialog;  /* Диалог задания атрибутов пенс.удостоверения */

  var Cont;
  var DifDate;
  var OldRest;
  var StartDate;
  var TestDate,
      d,m,y;

  var name;  /* Ф.И.О. вкладчика */

  private var {GroupProcRunning}; 

  /* Процедура обработки событий диалога */
  macro EventMacro( dlg, cmd, id, key )
    Message("~Esc~ отказ ~F9~ cохранение");
    if ( ( cmd == DLG_KEY ) and
         ( ( key == 323 )  or                     /* Клавиша F9 */
           ( ( key == 13 ) and ( id == 2 ) ) ) )  /* Клавиша Enter в последнем поле */
      if ( dlg.SocialNumber == "" )
        if(StrFor(GetProgramID) != "П")
          MsgBox( "Номер пенсионного удостоверения|не может быть пустым" );
        end;
        SetFocus( dlg, 1 );
        return CM_IGNORE;
      else
        return CM_SAVE;
      end;
    end;
  end;

/*  SetOutput("NUL");  /* Убираем вывод на экран */ */

  ALG_COMDEPCLNT.GetPersnCard();
  if ( (StrLen( ALG_COMDEPCLNT.rec.SocialNumber ) == 0) and 
       (not InFCGroupOpRun()) and
       (StrFor(GetProgramID) != "П") and
       (not {GroupProcRunning}) )  
 
    /* Начальная установка полей диалога */
    name = ALG_COMDEPCLNT.ConvertFIO ();
    dial.ClientName   = SubStr( name, 1, 38 );
    dial.SocialNumber = "";
    dial.RegistrDate  = {curdate};

    if ( RunDialog( dial, "EventMacro" ) )  /* Команда установить значения диалога */
      ALG_COMDEPCLNT.rec.SocialNumber = dial.SocialNumber; /* Номер пенс.удостоверения */
      ALG_COMDEPCLNT.rec.SocialDate  = dial.RegistrDate;  /* Дата пенс.удостоверения */
      ALG_UPDATE = 1;  /* Обновить запись вкладчика */
    else
      if(StrFor(GetProgramID) != "П")
        MsgBox( "Номер пенсионного удостоверения|не может быть пустым|Документ не может быть проведен" );
        ALG_RESULT=ERR_RES; /* Аварийно завершаем проводку */
        return;
      end;
    end;

  end;

// if( ALG_SBDEPDOC.TypeOper == Дополнительный_Взнос )
  ALG_RESULT = SKIP_RES;

  if ( (StrFor(GetProgramID) == "П")            /* Последующий контроль */
   AND (CodeFor(ALG_SBDEPDOC.FlagStorn) != 0) ) /* Сторнирование */
    ALG_RESULT = OK_RES;
  else
    if (ALG_SBDEPDOC.InSum < ALG_PARAM.prmD)
        if (not {GroupProcRunning})
          MsgBox ("Минимальная сумма прихода - ",ALG_PARAM.prmD);
        end;
        ALG_RESULT = ERR_RES;
        return;
    end;
/*
    2. Год после начала договора
*/
    if ( (ALG_RESULT != ERR_RES)
     AND ((ALG_DEPOSITOR.Type_Account == "Нов.европ.2г")
       OR (ALG_DEPOSITOR.Type_Account == "Ср.пен.на 2г")
       OR (ALG_DEPOSITOR.Type_Account == "Накоп.на 2 г")) )
      if (ALG_DEPOSITOR.Prol_DateDep != Date(0,0,0))
        StartDate = ALG_DEPOSITOR.Prol_DateDep - 1;
      else
        StartDate = ALG_DEPOSITOR.Start_DateDep;
      end;
      DateSplit (StartDate,d,m,y);
      y = y + 1;
      TestDate = Date(d,m,y);
      if (ALG_SBDEPDOC.DepDate_Document > TestDate)
          if (not {GroupProcRunning})
            MsgBox ("С момента начала договора прошло больше года - доп.взносы запрещены");
          end;
          ALG_RESULT = ERR_RES;
          return;
      end;
    end;
  end;
// end;

  ALG_RESULT = OK_RES;    /* Продолжать проводку */

  return;
