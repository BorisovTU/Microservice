import CommonInter, alg_vars, RSD, cur_rate, issrvdoc, deposInter, BankInter, Acc_Form;

record ing_req("ing_req.dbt");

const CI_INGOT = 5,
      TYPE_RES_PAY = 0;

var ClientList = TClientList;


macro ceil( d )
  var f = floor( d );
  if ( f != d )
    f = f + 1;
  end;
  return f;
end;

macro GetOperName
  file person( person ) key 0;
  var operName="", s0, s1, s2, s3, p;

  person.Oper = {oper};
  if (getEQ(person))
    s0 = Trim( person.Name );
    p  = StrBrk( s0, " " );
    s1 = SubStr( s0, 1, p-1 );
    s2 = Trim(SubStr( s0, p ));
    p  = StrBrk( s2, " " );
    s3 = Trim(SubStr( s2, p ));
    operName = s1;
    if (strlen(s2) > 0)
      operName = operName + " " + SubStr( s2, 1, 1 ) + ".";
    end;
    if (strlen(s3) > 0)
      operName = operName + " " + SubStr( s3, 1, 1 ) + ".";
    end;
  end;

  return operName;
end;


/* подсчет массы металла */
macro GetWeight( reqID, currCode, WeightLeg, WeightCh, WeightLA )
var cmd, rs;
var curWeightCh; 
var RoundBase, RoundType;

 WeightLeg = 0.0;
 WeightCh = 0.0;
 WeightLA = 0.0;
 cmd = RsdCommand(" select d.T_WEIGHT as Weight, a.T_AMOUNT as Amount, z.T_DOUBLE as Double, "
                   " d.t_PWRoundBase as PWRoundBase, d.t_PWRoundType as PWRoundType, d.T_USEWEIGHTTYPE as USEWEIGHTTYPE"
                  " from dci_misc_dbt d, ding_rqsp_dbt a, dsb_casvl_dbt c, dci_code_dbt z where "
                  " d.T_TYPE = ? and d.T_ISSUEID = c.T_CODINTVALUE and "
                  " c.T_REFVALUE = a.t_valueref and a.t_reqid = ? and d.T_HALLMARKREF = z.T_AUTOKEY ");
 cmd.addParam("T_TYPE", RsdBp_in);
 cmd.value("T_TYPE") = CI_INGOT;
 cmd.addParam( "t_ID",  RsdBp_in);
 cmd.value("t_ID") = reqID;
 cmd.execute();
 rs = RsdRecordset(cmd);

 while ( rs.moveNext() )
   WeightLeg = WeightLeg + (rs.value("Weight")*rs.value("Amount"))/10000.0;  

   if (rs.value("USEWEIGHTTYPE") == 1) /* химчистота */
     curWeightCh = (rs.value("Weight")*rs.value("Double"))/10000000.0;
     RoundBase = rs.value("PWRoundBase");
     RoundType = rs.value("PWRoundType");
     if ( RoundBase > 0 )
       if ( RoundType == 0 ) /* Округление */
         curWeightCh = floor( curWeightCh / RoundBase + 0.5 ) * RoundBase;
       elif ( RoundType == 1 ) /* Отбрасывание */
         curWeightCh = floor( curWeightCh / RoundBase ) * RoundBase;
       elif ( RoundType == 2 ) /* Перекрывание */
         curWeightCh = ceil( curWeightCh / RoundBase ) * RoundBase;
       end;
     end;
     WeightCh = WeightCh + curWeightCh*rs.value("Amount");
   else
     WeightLA = WeightLA + (rs.value("Weight")*rs.value("Amount"))/10000.0;  
   end;
 end;

 SetParm(2, WeightLeg);
 SetParm(3, WeightCh);
 SetParm(4, WeightLA);
end;


macro request_body(ing_req, sumNDS)
var cmd, rs;
var WeightLeg, WeightCh,WeightLA;
var sellCBcurs=0, CBcurs=0;
var Cost, TotalCost;
var FormOfMetal, Nomenklat="", WeightStr = "", tmp="";
var count=0, i, standart=false, refValue=0;
array NomenklatStr, TotalCostStr;

 /* строка номенклатуры */
 cmd = RsdCommand("SELECT casvl.t_refvalue AS refvalue, ing.t_amount AS amount, "
                        " misc.t_weight AS weight, code.t_string AS t_string "
                  " FROM dci_misc_dbt misc, ding_rqsp_dbt ing, dsb_casvl_dbt casvl, dci_code_dbt code WHERE "
                   " misc.t_type = ? AND misc.t_issueid = casvl.t_codintvalue AND "
                   " casvl.t_refvalue = ing.t_valueref AND ing.t_reqid = ? AND "
                   " code.t_group(+) = 5 AND code.t_uplink(+) = misc.t_materialref AND code.t_double(+) = misc.t_weight/10000 "
                  " ORDER BY NVL(code.t_string,chr(1)), misc.t_weight ");
 cmd.addParam("t_type", RsdBp_in);
 cmd.value("t_type") = CI_INGOT;
 cmd.addParam("t_id", RsdBp_in);
 cmd.value("t_id") = ing_req.ID;
 cmd.execute();
 rs = RsdRecordset(cmd);
 
 while ( rs.moveNext() )
   refValue = rs.value("refvalue"); /* для определения вида металла */
   tmp = ", ";
   if ((rs.value("t_string") == "X") and (not standart)) /* первый стандартный слиток */
     standart = true;
     if (count > 0)
       tmp = "; стандартные - ";
     end;
   end;
   if (count == 0)
     if (standart)
       tmp = "стандартные - ";
     else
       tmp = "мерные - ";
     end;
   end;

   Nomenklat = Nomenklat + tmp + String((rs.value("weight")/10000.0):0:2) + " гр. - " + rs.value("amount") + " шт.";
   count = count + 1;
 end;
 
 StrSplit( Nomenklat, NomenklatStr, 57 );
 i = 0;
 while ( i < 3 )
   if ( ValType( NomenklatStr( i ) ) != V_STRING )
     NomenklatStr( i ) = ""; 
   end;
   i = i + 1;
 end;

 /* вид металла */
 cmd = RsdCommand("select code.T_STRING as T_STRING "
                  " from dci_misc_dbt misc, dci_code_dbt code, dsb_casvl_dbt casvl where "
                  " misc.T_TYPE = ? and misc.T_ISSUEID = casvl.T_CODINTVALUE and"
                  " casvl.T_REFVALUE = ? and misc.T_MATERIALREF = code.T_AUTOKEY ");
 cmd.addParam("T_TYPE", RsdBp_in);
 cmd.value("T_TYPE") = CI_INGOT;
 cmd.addParam("T_REFVALUE", RsdBp_in);
 cmd.value("T_REFVALUE") = refValue;
 cmd.execute();
 rs = RsdRecordset(cmd);
 if (rs.moveNext())
   if ( rs.value("T_STRING") != strfor(1) )
     FormOfMetal = String(rs.value("T_STRING"));
   end;
 end;

 /* подсчет массы металла */
 GetWeight( ing_req.ID, ing_req.currCode, WeightLeg, WeightCh, WeightLA );  
 WeightStr = NumToStr( WeightLeg, "грамм", "грамма", "граммов", true, 4 );
 WeightStr = strupr( substr( WeightStr, 1, 1 ) ) + substr( WeightStr, 2 );
 
 /* расчет стоимости */
 GetAllCurrRate(ing_req.PlannedCloseDate, ing_req.currCode, CBcurs, null, sellCBcurs, null);
 Cost = (WeightCh+WeightLA)*CBcurs;
 /* расчет общей стоимости */
 sumNDS = CalcNDSforMetal(ing_req.Reference, ing_req.ID); /* расчет суммы НДС */
 TotalCost = Cost + sumNDS;

 StrSplit( rubtostr(TotalCost), TotalCostStr, 57 );
 i = 0;
 while ( i < 2 )
   if ( ValType( TotalCostStr( i ) ) != V_STRING )
     TotalCostStr( i ) = ""; 
   end;
   i = i + 1;
 end;

[ 
  Вид металла                                                        #############    
       
  Масса металла в лигатуре                                                     # г
                         #
                         
  Масса металла в химической чистоте                                           # г

  Номенклатура слитков   #
                         #
                         #
  Дата выдачи                                                          ###########

  Цена выдаваемого металла по учетной цене ЦБ РФ    ####################### (руб.)
     
  Общая стоимость выдаваемого
  металла (руб.)                                     ######################        
                         #
                         #
    
]
 ( FormOfMetal,
   WeightLeg:11:1:r,
   WeightStr,
   WeightCh:11:4:r,
   NomenklatStr(0),
   NomenklatStr(1),
   NomenklatStr(2),
   ing_req.PlannedCloseDate,
   Cost:0:2:a:f,
   TotalCost:0:2:a:f,
   TotalCostStr(0),
   TotalCostStr(1)
 );

end;


macro request_rep(ing_req)
var FIO="", account="";
var cmd, rs, cmd_r, rs_r;
var RegPath = "RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ПАРАМЕТРЫ_ДОГОВОРОВ\\НАЗВ_ГОРОДА";
var Value, Town, ErrCode, Request, i;
var sumNDS;
array RequestStr;

  cmd = RsdCommand(" select t_codclient, t_account from ddepositr_dbt where t_referenc = ? ");
  cmd.addParam("t_referenc", RsdBp_in);
  cmd.value("t_referenc") = ing_req.Reference;
  cmd.execute();
  rs = RsdRecordset(cmd);
  if ( rs.moveNext() )  
     if ( ClientList.GetRecord(rs.value(0)) )
       FIO = ClientList.CurRec.rec.name1 + " " + ClientList.CurRec.rec.name2 + " " + ClientList.CurRec.rec.name3;
     end;
     account = rs.value(1);
  end;

  if(GetRegistryValue(RegPath, V_STRING, Value, ErrCode) == V_STRING)
    Town = Value;
  end;

  sumNDS = CalcNDSforMetal(ing_req.Reference, ing_req.ID); /* расчет суммы НДС */

  Request = "     Я, " + FIO + " прошу ";           
  if ( sumNDS > 0 )
    Request = Request + "принять сумму в размере 18% учетной стоимости металла на день составления акта приема-передачи и ";
  end;
  Request = Request + "организовать выдачу физического металла с металлического счета на следующих условиях:";


  StrSplit( Request, RequestStr, 80 );
  i = 0;
  while ( i < 4 )
    if ( ValType( RequestStr( i ) ) != V_STRING )
      RequestStr( i ) = ""; 
    end;
    i = i + 1;
  end;

[
                                   Заявка № ######
                          на получение физического металла с
                обезличенного металлического счета № ##########################



          г. ####################                                    ###########




  #
  #
  #
  #
]
 (ing_req.Number:l, 
  Acc_Form(account):f,
  Town:l,
  ing_req.OpenDate,
  RequestStr(0),
  RequestStr(1),
  RequestStr(2),
  RequestStr(3)
 );

  request_body( ing_req, sumNDS );

[



   ############################################################# _______________
                                                                   (подпись)
                            
  ════════════════════════════════════════════════════════════════════════════════

  "ПРОВЕРИЛ":

  Контролер __________________ #######################
                 (подпись)
]
 (
   FIO,
   GetOperName
 );

  OnError( e )
      MsgBox( e.Message );
      
end;


macro letOutRequest(IngRequest)

 SetBuff( ing_req, IngRequest );

 request_rep(ing_req);


end;
