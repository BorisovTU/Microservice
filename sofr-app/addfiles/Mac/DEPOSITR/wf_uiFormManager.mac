import bankInter, wf_params;

class TUiFormData
  var Id : String;
  var Fields : TArray;  
end;

class TUiFormManager
  private const GLOB_PARAM_NAME_PREFIX = "uiFormManager_";
  private const GLOB_PARAM_NAME_SESSID = GLOB_PARAM_NAME_PREFIX + "sessionId";
  private const GLOB_PARAM_NAME_FORMID = GLOB_PARAM_NAME_PREFIX + "formId";
  private const GLOB_PARAM_NAME_PARAMS = GLOB_PARAM_NAME_PREFIX + "params";

  var uiSessionId : String;
  var currentForm;

  macro saveInfo
    setGlobalParameter(GLOB_PARAM_NAME_SESSID, uiSessionId);
    if (currentForm != null)
      setGlobalParameter(GLOB_PARAM_NAME_FORMID, currentForm.Id);
      var params = TParameters;
      params.setArray(currentForm.Fields); 
      setGlobalParameter(GLOB_PARAM_NAME_PARAMS, params.toString);
    else
      setGlobalParameter(GLOB_PARAM_NAME_FORMID, null);
      setGlobalParameter(GLOB_PARAM_NAME_PARAMS, null);
    end;
  end;

  macro recallInfo
    uiSessionId = getGlobalParameter(GLOB_PARAM_NAME_SESSID, false);
    var curFormId = getGlobalParameter(GLOB_PARAM_NAME_FORMID, false); 
    if (curFormId != null)
      currentForm = TUiFormData;
      currentForm.Id = curFormId;
      currentForm.Fields = TParameters(getGlobalParameter(GLOB_PARAM_NAME_PARAMS, false)).asArray;
    end;
  end;

  macro reset
    uiSessionId = null;
    currentForm = null;
    saveInfo;
  end;

  macro setInfo(sessId : String, formId : String, params : TParameters)
    uiSessionId = sessId;
    currentForm = TUiFormData;
    currentForm.Id = formId;
    currentForm.Fields = params.asArray; 
    saveInfo; 
  end;

  macro getFieldsAsParameters
    if (currentForm != null)
      var rv = TParameters;
      rv.setArray(currentForm.Fields);
      return rv;
    end;
    return null;
  end;
end;

var uiFormManager = TUiFormManager;