/*
  ---------------------
  R-Style SoftWare Lab.
  ---------------------
  RS-Retail

  Список на открытие счетов по вкладам (ф. N 246-с)

*/

import CommonInter, BankInter, DeprIntr;

var clntL = TClientList;  /* Клиенты банка */

file ppr ( "paprkind.dbt", "bank.def" ) key 0; /* Виды удостоверения личности */
file cur ( "currency.dbt" ) key 0;        /* Справочник валют */
file sop ( "suboper.dbt"  ) key 1;        /* Справочник подопераций */
file dog ( "trn_cntr.dbt" ) key 0;        /* Договора по безнал.платежам */
file pay ( "trn_paym.dbt" ) key 11;        /* Платежное поручение по Договору */
file doc ( "trn_doc.dbt"  ) key 0;        /* Документы по безнал.платежам */
file sbd ( "sbdepdoc.dbt" ) key 3;        /* Документы по безнал.платежам */

record PayFil( "trn_payf.dbt" );  /* Расшифровка по филиалу */

const
  OP_OPENCAS =  1,  /* Открытие наличными */
  OP_OPENNCS = 51,  /* Открытие безналичными */
  OP_OPENTRN = 58;  /* Открытие переводом */

private var {curdate};
private var {Name_Bank};

var flag_Cur = NumFlagCur();
var FNCash   = NumFNCash();

var saveList = -777;
var nnn, num, list;
var AllLists = 0;
var sFilial;
var saveTypeAcc = "???";
var DateB = {curdate};
var TableFlag = FALSE;

var sPaper, nPaper;

var SumI, SumICom;
var SumL, SumLCom;

/**************************************************************************/
/*                    Получение названия подоперации                      */
/**************************************************************************/
macro GetSubOpName( IsCurDoc, Kind, OpNum, SubOper )

 Rewind( sop );
 ClearRecord( sop );
 sop.IsCur    = IsCurDoc;
 sop.Kind     = Kind;
 sop.OperType = OpNum;
 sop.Type     = SubOper;
 if ( GetEQ( sop ) )
   return sop.Name;
 else
   return "";
 end;
end;

/**************************************************************************/
/*                       Получение вида зачисления                        */
/**************************************************************************/
macro getApplType()
 var ttt;
 if ( doc.ApplType >= 0 )
   ttt = GetSubOpName( doc.IsCur,
                       doc.Type_Account,
                       doc.TypeOper,
                       doc.ApplType );
   if ( StrLen( ttt ) != 0 )
     ttt = "\"" + ttt + "\"";
   end;
 else
   ttt = "???";
 end;
 return ttt;
end;

/**************************************************************************/
/*                        Получение названия валюты                       */
/**************************************************************************/
macro getISO()
 var sISO = "???";
 if ( flag_Cur == 0 )
   return "";
 end;
 ReWind( cur );
 ClearRecord( cur );
 cur.Code_Currency = doc.Code_Currency;
 if ( GetEQ( cur ) )
   sISO = cur.Short_Name;
 end;
 return "(" + sISO + ")";
end;

/************************************************************************/
/*               Получение вида удостоверения личности                  */
/************************************************************************/
macro getPaperKind()
 ReWind( ppr );
 ClearRecord( ppr );
 ppr.PaperKind = clntL.CurRec.rec.PaperKind;
 if ( getEQ( ppr ) )
   sPaper = ppr.Name;
 else
   sPaper = "";
 end;
end;

/************************************************************************/
/*                        Получение данных клиента                      */
/************************************************************************/
macro getClient()
 if ( clntL.GetRecord( doc.CodClient ) )
   getPaperKind();
   nPaper = clntL.CurRec.rec.PaperSeries + " " +
            clntL.CurRec.rec.PaperNumber + " " +
            clntL.CurRec.rec.PaperIssuer;
 else
   sPaper = "";
   nPaper = "";
 end;
end;

/****************************************************************************/
/* Найти документ по счету вкладчика, связанный с документом безнал.платежа */
/****************************************************************************/
macro FindSBDepDoc()
 var ret;
 ReWind( sbd );
 ClearRecord( sbd );
 sbd.iApplicationKind = doc.iApplicationKind;
 sbd.ApplicationKey   = doc.ApplicationKey;
 ret = getEQ( sbd );
 if ( NOT ret )
   sbd.TypeComplexOper = -777;
   /*
   println( " Не найден документ в sbdepdoc.dbt (", doc.iApplicationKind, ", ",
                                                    doc.ApplicationKey, ")" ); */
 end;
 return ret;
end;

/**************************************************************************/
/*                        Заголовок списка-ордера                         */
/**************************************************************************/
macro Order_title()
 [
                                                                   ф.N 246-c


                    Сберегательный банк Российской Федерации
                                       #

            Список на открытие счетов по вкладам N ________________

    Филиал #                                                  #

    Зачисление #

    за период ###########################################   Лист #
                                                            Листов #
    Наименование (шифр) организации #

    Вклад #
    _____________________________________________________________________________________________________________________
    | N N |           Фамилия,            |             |  Вид документа,  | Серия, номер, |       Номер открытого      |
    | п/п |             имя,              |    Сумма    |  удостоверяющего | кем и когда   |        лицевого счета      |
    |     |           отчество            |             |     личность     |     выдан     |                            |
    +-----+-------------------------------+-------------+------------------+---------------+----------------------------+]
    ( {Name_Bank}:c,
      sFilial,
      {curdate}:m,
      getApplType(),
      "с " + String( DateB ) + " по " + String( {curdate} ),
      saveList,
      AllLists,
      "\"" + dog.NameBusines + "\"",
      "\"" + PayFil.Type_Account + "\"" + getISO()
      );
end;

/**************************************************************************/
/*                             Строка ордера                              */
/**************************************************************************/
macro OrderString()
  [  | ### | ############################# | ########### | ################ | ############# | ########################## |]
  ( num:3,
    doc.Sname + " " +
      SubStr( doc.Nname, 1, 1 ) + "." +
      SubStr( doc.Pname, 1, 1 ) + ".",
    doc.Sum:a:2:10,
    sPaper:w,
    nPaper:w,
    Acc_Form(doc.Account):26:f );
  SumI = SumI + doc.Sum;
  TableFlag = TRUE;
end;

/**************************************************************************/
/*                         Закрытие листа ордера                          */
/**************************************************************************/
macro Order_Table()
  [  +-----+-------------------------------+-------------+------------------+---------------+----------------------------+
                          Итого по листу:  | ########### |
                                           +-------------+
                         Итого по списку:  | ########### |
                                           +-------------+
                Итого зачислено по листу:  | ########### |
                                           +-------------+
               Итого зачислено по списку:  | ########### |
                                           +-------------+]
  ( SumL:a:2:10, SumLCom:a:2:10, SumI:a:2:10, SumICom:a:2:10 );

end;

/**************************************************************************/
/*                              Подвал ордера                             */
/**************************************************************************/
macro Order_end()
  [


     Проверено [                 ] ______________________________
                                              (подпись)

     ________________________________________________________________________
                               Отметки филиала

                               Проведено по операционному дневнику

                               "___"_________________ 200__г.

                               Контролер ________________________
                                                (подпись)

   _____________________________________________________________________________________


  ];
end;

/**************************************************************************/
/*                            Головная процедура                          */
/**************************************************************************/
macro Order( Addr, PaymentID )

 var stat;

 if ( GetBankStandart() == 0 )  /* для Сбербанка открытие счетов из списка невозможно */
   Exit(1);
 end;

 /* Чтение записи о Подразделении */
 if ( not findDepartment(FNCash, sFilial) )
   sFilial = "???";
 end;

 SetBuff( PayFil, Addr );

 /* Чтение записи о договоре */
 Rewind( dog );
 ClearRecord( dog );
 dog.NumberContract = PayFil.NumberContract;
 if ( GetEQ( dog ) )
   DateB = dog.DateContract;       /* Дата ввода Договора */
 end;

 /* Чтение записи о платежном поручении по договору */
 Rewind( pay );
 ClearRecord( pay );
 pay.iApplicationKind = PayFil.PaymAppKind;
 pay.ApplicationKey = PayFil.PaymAppKey;
 if ( GetEQ( pay ) )
   DateB = pay.DateInputDocument;  /* Дата ввода платежного поручения */
 end;

 /*** Первый проход. Подсчет общих сумм и числа листов ***/
 num = 0;
 list = 0;
 SumI = $0;
 SumICom = $0;
 SumL = $0;
 SumLCom = $0;
 Rewind( doc );
 ClearRecord( doc );
 doc.FNCash       = FNCash;
 doc.ListTransfer = PayFil.AutoKey;
 stat = GetGE( doc );

 InitProgress( Nrecords( doc ),
               " Идет перебор документов по безналичным перечислениям. Ждите..."
               "Формирование ордеров на открытие счетов" );

 while ( stat                                    AND
         ( doc.FNCash       == FNCash         )  AND
         ( doc.ListTransfer == PayFil.AutoKey ) )
   FindSBDepDoc();
   if ( ( sbd.TypeComplexOper == OP_OPENNCS )  OR
        ( ( sbd.TypeComplexOper == -777 )  AND
          ( doc.Referenc == 0 ) ) )
     if ( saveList != doc.NList )
       saveList = doc.NList;
       AllLists = AllLists + 1;
     end;
     SumLCom  = SumLCom + doc.Sum;
     if ( doc.Status == "X" )
       SumICom  = SumICom + doc.Sum;
     end;
     num = num + 1;
     UseProgress( num );
   end;
   stat = Next( doc );
 end;
 RemProgress();

 if ( SumLCom == $0 )
   PrintLn( "\n\n  Документов открытия новых счетов не найдено" );
   Exit(0);
 end;

 /*** Основной цикл. Формирование ордеров на открытие счета ***/
 num = 0;
 list = 0;
 saveList = -777;
 SumI = $0;
 stat = FALSE;
 Rewind( doc );
 ClearRecord( doc );
 doc.FNCash       = FNCash;
 doc.ListTransfer = PayFil.AutoKey;
 stat = GetGE( doc );

 InitProgress( Nrecords( doc ),
               " Идет перебор документов по безналичным перечислениям. Ждите..."
               "Формирование ордеров на открытие счетов" );

 while ( stat                                    AND
         ( doc.FNCash       == FNCash         )  AND
         ( doc.ListTransfer == PayFil.AutoKey ) )
   FindSBDepDoc();
   if ( ( sbd.TypeComplexOper == OP_OPENNCS )  OR
        ( ( sbd.TypeComplexOper == -777 )  AND
          ( doc.Referenc == 0 ) ) )
     if ( saveList != doc.NList )
       if ( saveList != -777 )
         Order_Table();
         Order_end();
       end;
       saveList = doc.NList;
       list = list + 1;
       SumI = $0;
       SumL = $0;
       Order_title();
     end;
     SumL = SumL + doc.Sum;
     if ( doc.Status == "X" )
       getClient();
       OrderString();
     end;
     num = num + 1;
     UseProgress( num );
   end;
   stat = Next( doc );
 end;
 RemProgress();

 if ( TableFlag )
   Order_Table();
   Order_end();
 end;

end;
