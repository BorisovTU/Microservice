/*
$Name:             TRNS_NP.MAC
$Module:           RS-Retail
$Description:      Проверка при приеме наличных денег для перевода
*/

import CommonInter, deprintr, bankdefs, rcw, rsexts, ChkPrn;
import PmPropUtil;

private var {oper};

private record OC_PAY_DOC  ("pay_doc.n40" ); /* Глобальная переменная */
private record OC_PAY_DOC_PMNT_PROP("rtpaymentprop.dbt"); /* Глобальная переменная */
private record OC_PAY_KIND ("pay_kind.dbt"); /* Глобальная переменная */

private record pay_doc ("pay_doc.dbt");

file   sb_dtyp     ("sb_dtyp.dbt" );
file   sb_typop    ("sb_typop.dbt") key 0;
file   sb_tarif    ("sb_tarif.dbt") key 0;
file   tarif_plus  ("sb_tarpl.dbt") key 0;

var userAttrs;
var varUserAttrs;

var FlagCur = NumFlagCur( );

const ServTarifNum = 45;
const ServTarifPy  = 41;
const ServTarifRc  = 42;
const ServTarifMsPy = 48;
const ServTarifMsRc = 49;
const ServTarifMsTx = 50;
const ServTarifMs  = 12; // Кассовая операция - За услуги банка
const CO_Comission = 21; // Кассовая операция - Комиссия
const CO_Payed      = 41;  /* Прочая расходная операция */
const OP_CardPay    = 26;  /* Перевод/перечисление по карте */

const I_DPRT_LISTALL    = 0;    // все подразделения
const I_DPRT_LISTOPEN   = 1;    // открытые подразделения
const I_DPRT_LISTACTIVE = 2;    // активные подразделения
const I_DPRT_LISTCLOSE  = 4;    // закрытые подразделения
const I_DPRT_LISTFILIAL = 16;    // подразделения типа "Филиал"
const I_DPRT_LISTVSP    = 32;     // подразделения типа "ВСП"

const BIT_F_HAVE_DOCUMENT = 0;
const POSRepFileName = "trns_posrp" + string({oper}) + ".txt";
const POSRepPahtTerm = ".\\";
const POSRepPathServ = "..\\txtfile\\";
var OP_WithAuth = 0;       /* > 0: оп. 26 с авторизацией через POS */
                           /* = 1: подвид "В организацию" */
                           /* = 2: подвид "Физич. лицу" */
var ServTarifSum = $0L;
var AuthSum = $0L;

var g_CodeKind = 3; // вид кода (число) т.к. в панели подкачивается строка

record InputPanel ("TRNS_NP") dialog;

const ne_FNameRecp       =  0;
const ne_NameRecp        =  1;
const ne_SNameRecp       =  2;
const ne_AddrRecp        =  3;
const ne_PhoneRecp       =  4;
const ne_CodeKind         =  5;
const ne_BankCode        =  6;
const ne_CorAccRecp      =  7;
const ne_AccountRecp     =  8;
const ne_TypeAccRecp     =  9;
const ne_NotifRecp       =  10;
const ne_NotifPayer      = 11;
const ne_RecpMessage     = 12;
const ne_SummaPay        = 13;
const ne_ComisPay        = 14;
const ne_ComisNotifRecp  = 15;
const ne_ComisNotifPayer = 16;
const ne_ComisRecpMesage = 17;
const ne_SummaTotal      = 18;
const ne_FlagRez         = 19;

private macro fillRddAttrs()
  StoreValue("doc:Название@Получ", OC_PAY_DOC.FNameRecp + " " + OC_PAY_DOC.NameRecp + " " + OC_PAY_DOC.SNameRecp);
end;

/* разделение ФИО на Ф, И, О */
private macro GetFIO(fio, name1, name2, name3)
  var i;

  name1 = Trim(fio);
  name2 = "";
  name3 = "";

  i = Index(name1, " ");
  if (i > 0)
    name2 = Trim(SubStr(name1, i + 1));
    name1 = SubStr(name1, 1, i - 1);
  end;

  i = Index(name2, " ");
  if (i > 0)
    name3 = Trim(SubStr(name2, i + 1));
    name2 = SubStr(name2, 1, i - 1);
  end;

  setparm(1, name1);
  setparm(2, name2);
  setparm(3, name3);
end;

/* ФИО операциониста */
private macro GetOperName(oper)
  file opr (person, "bank.def") key 0;
  var fio = "";
  var name1, name2, name3;
  
  opr.Oper = oper;
  if (getEQ(opr))
    GetFIO(opr.Name, name1, name2, name3);
    fio = name1;
    if (name2 != "")
      fio = fio + " " + SubStr(name2, 1, 1) + ".";
    end;
    if (name3 != "")
      fio = fio + " " + SubStr(name3, 1, 1) + ".";
    end;
  end;
  if (fio == "")
    fio = string(oper);
  end;
  return fio;
end;

private macro PrintReport( fname )
  file report() txt;

  CopyFile("$" + POSRepPahtTerm + fname, POSRepPathServ + fname);
  if ((MacroOutput == "PRN"))
    PrintFile(POSRepPathServ + fname);
  else
    open(report, POSRepPathServ + fname);
    ViewFile(report);
  end;
end;

/* ISO код валюты */
private macro GetCurrISO(cod_curr)
  file currency("currency.dbt") key 0;
  var iso_curr = 0;

  ClearRecord(currency);
  currency.Code_Currency = cod_curr;
  if (GetEQ(currency))
    iso_curr = Int(currency.ExternalCode);
  end;

  if (iso_curr == 810)
    iso_curr = 643;
  end;
  return iso_curr;
end;

/* код валюты */
private macro GetCurrCode(iso_curr)
  file currency("currency.dbt") key 1;
  var code = 0;

  ClearRecord(currency);
  currency.ExternalCode = string(iso_curr:3:o);
  if (GetEQ(currency))
    code = currency.Code_Currency;
  end;

  return code;
end;

/**************************************************************************\
|                       Запуск авторизации через POS                       |
\**************************************************************************/
macro Authorization( Sum )
  var ok = true;
  var operation, terminal;
  var userAttrs, varUserAttrs;
  var have_document = false;
  
  terminal = CreateObject("rtposcom", "TPosTerminal", null, false);
  ok = terminal.setUserName(GetOperName(OC_PAY_DOC.Oper));

  /* Авторизация */
  if( ok )
    operation = terminal.createOperation(4004); /* Безналичный перевод при наличии карты клиента */
    operation.inParams.amount   = Int(double(Sum * 100));
    operation.inParams.currency = GetCurrISO(OC_PAY_DOC.CodCur);
    ok = operation.execute;
    if (ok and GetBitFlag(operation.postprocessingFlags, BIT_F_HAVE_DOCUMENT))
      ok = terminal.getReports(POSRepPahtTerm + POSRepFileName);
      have_document = ok;
    end;
    
    if( ok ) /* Авторизация прошла успешно */
      userAttrs = getDocUserAttrPool();
      varUserAttrs = userAttrs.overlayRecord("rt_paym.rrn", userAttrs.FldOffset("Payment"), true);
      userAttrs.clear;
      userAttrs.rec.Branch           = OC_PAY_DOC.FNcash;
      userAttrs.rec.iApplicationKind = OC_PAY_DOC.iApplicationKind;
      userAttrs.rec.ApplicationKey   = OC_PAY_DOC.ApplicationKey;
      userAttrs.rec.AttrID           = "RRN";
      varUserAttrs.clear;
      varUserAttrs.rec.RRN        = operation.outParams.rrn;
      varUserAttrs.rec.CardNumber = operation.outParams.clientCard;
      varUserAttrs.rec.AuthCurr   = GetCurrCode(operation.outParams.currency);
      varUserAttrs.rec.AuthSum    = Money(operation.outParams.amount) / 100;
      ok = userAttrs.insert(varUserAttrs.recSize);
      if (not ok)
        msgbox("Ошибка при вставке документа в список");
      end;
    else
      msgbox("Не выполнена авторизация через POS-терминал");
    end;
  end;

  if ((not ok) and (terminal.lastError != 0))
    msgbox("Ошибка " + string(terminal.lastError) + " при работе с POS-терминалом");
  end;

  if (ok and have_document)
    PrintReport(POSRepFileName);
  end;

  return ok;

  OnError(err)
    msgbox("Ошибка при работе с POS-терминалом");
    return false;
end;

/**************************************************************************\
|                            Поиск вида вклада                             |
\**************************************************************************/
macro FindTypeAcc (TypeName)

  var OutName = "";

  KeyNum(sb_dtyp,2);
  sb_dtyp.FlagCur = FlagCur;
  sb_dtyp.Kind    = TypeName;

  if ((TypeName != "") AND (GetEQ(sb_dtyp)))
    OutName = sb_dtyp.Name;
  else
    OutName = TypeName;
  end;

  return OutName;

end;


/**************************************************************************\
|                      Получить вид операции                               |
\**************************************************************************/
macro GetVidDoc( _isCur, _GroupOpert, _NumOperat )
  file pay_kind("pay_kind.dbt") key 0;
  var VidDoc = StrFor(0);

  pay_kind.isCur = _isCur;
  pay_kind.GroupOpert = _GroupOpert;
  pay_kind.NumOperat = _NumOperat;
  if ((GetEQ(pay_kind)) and (pay_kind.FlagCash) )
    VidDoc = StrFor(1);
  end;

  return VidDoc;
end;


/**************************************************************************\
|                      Справочный скролинг                         |
\**************************************************************************/
macro list_CodeKinds
  var CodeKind;
  var codeKindName;
  
   if( SelectCodeKind( CodeKind, CodeKindName ) == true )
    g_CodeKind = CodeKind;
     InputPanel.CodeKind = CodeKindName;
     UpdateFields( InputPanel );
   end;
end;
/**************************************************************************\
|                      Справочный скролинг                         |
\**************************************************************************/
macro list_BankCodes
    var CodeKind, CodeKindName, /*oldBIC*/
        getBIC, getCorAccnt, getBank, p5, p6, p7,vPartyID;
        
      if ( SelectBank( getBIC, getCorAccnt, NULL, getBank, 
          g_CodeKind, p5, p6, p7, vPartyID ) == true  )
            
          OC_PAY_DOC_PMNT_PROP.PartyID = vPartyID;
          InputPanel.BankCode      = getBIC;
          InputPanel.CorAccRecp = getCorAccnt;
          
          UpdateFields( InputPanel );
    end;
end;
macro list_localBankCodes
  var BankCode;
  var depRecBuf;
  var dprt;
  var partyBuf;
  var depId = 0;
  var locBankName, locCorrAcc, locINN, locKPP, locPartyID;


        if(selectDepartment(depId, NULL, I_DPRT_LISTACTIVE, NULL, depRecBuf, partyBuf) == true)
            OC_PAY_DOC_PMNT_PROP.BankName = partyBuf.rec.ShortName;
              BankCode = findDprtPartyCode(depId, g_CodeKind);
              OC_PAY_DOC_PMNT_PROP.BankCode = BankCode;
              InputPanel.BankCode = BankCode;
               if( iFindBank(g_CodeKind, InputPanel.BankCode, locBankName, 
                                    locCorrAcc, locINN, locKPP, locPartyID) == true)
                                    
                  InputPanel.CorAccRecp             = locCorrAcc;
                  OC_PAY_DOC_PMNT_PROP.BankName     = locBankName;
                  OC_PAY_DOC_PMNT_PROP.INN          = locINN;
                  OC_PAY_DOC_PMNT_PROP.KPP          = locKPP;
                  OC_PAY_DOC_PMNT_PROP.PartyId = locPartyID;
               end;
            end;
       end;
/**************************************************************************\
|                      Справочный скролинг филиалов                        |
\**************************************************************************/
macro list_branches

  array AName;

  var i;
  var dpDepList = TdpDepList;

  dpDepList.SetFilter( dpDepList.getFilialID( NumFNCash( ) ) );

  while ( dpDepList.next( ) )
    i = ASize( AName );
    AName( i ) = " " + dpDepList.rec.Name + "         ";
  end;

  if ( ASize( AName) > 0 )
    i = Menu( AName, NULL, "Подразделения", 42, 10 );
    if ( i >= 0 )
      InputPanel.CorAccRecp = Trim( AName( i ) );
    end;
  end;

end;


/**************************************************************************\
|                    Справочный скролинг видов вкладов                     |
\**************************************************************************/
macro list_deptypes

  array AName;
  array AKind;

  var stat;
  var i;

  KeyNum(sb_dtyp,1);
  sb_dtyp.FlagCur = FlagCur;
  sb_dtyp.Priorit = 0;
  stat = GetGE(sb_dtyp);
  while (stat)
    if (sb_dtyp.FlagCur == FlagCur)
      if (sb_dtyp.Kind != "Шаблонный")
        sb_typop.IsCur    = sb_dtyp.FlagCur;
        sb_typop.Kind     = sb_dtyp.Kind;
        sb_typop.NumOpert = 0;
        if ((GetGE(sb_typop)                  ) AND
            (sb_typop.IsCur == sb_dtyp.FlagCur) AND
            (sb_typop.Kind  == sb_dtyp.Kind   )    )
          i = ASize(AKind);
          AKind(i) = sb_dtyp.Kind;
          AName(i) = " " + sb_dtyp.Name + " ";
        end;
      end;
      stat = Next(sb_dtyp);
    else
      stat = FALSE;
    end;
  end;

  if (ASize(AKind) > 0)
    i = Menu(AName,NULL,"Виды вкладов");
    if (i >= 0)
      InputPanel.TypeAccRecp = Trim(AName(i));
      OC_PAY_DOC.TypeAccRecp = AKind(i);
    end;
  end;

end;

macro checkBankExists
  var locBankName, locCorrAcc, locINN, locKPP, locPartyID;

   if( iFindBank(g_CodeKind, 
                        InputPanel.BankCode, locBankName, 
                        locCorrAcc, locINN, locKPP, locPartyID) == true)
                        
      OC_PAY_DOC_PMNT_PROP.BankName     = locBankName;
      OC_PAY_DOC_PMNT_PROP.CorrAccount  = locCorrAcc;
      OC_PAY_DOC_PMNT_PROP.INN          = locINN;
      OC_PAY_DOC_PMNT_PROP.KPP          = locKPP;
      OC_PAY_DOC_PMNT_PROP.PartyId      = locPartyID;

      InputPanel.CorAccRecp =  locCorrAcc;
      return true;
   else
      msgbox("Указаный банк не найден");
   end;
end;

/**************************************************************************\
|             Сохранение полей записи налично-кассовой операции            |
\**************************************************************************/
macro SaveFields

  OC_PAY_DOC.FNameRecp         = InputPanel.FNameRecp;
  OC_PAY_DOC.NameRecp          = InputPanel.NameRecp;
  OC_PAY_DOC.SNameRecp         = InputPanel.SNameRecp;
  OC_PAY_DOC.AddrRecp          = InputPanel.AddrRecp;
  OC_PAY_DOC.PhoneRecp         = InputPanel.PhoneRecp;
  OC_PAY_DOC_PMNT_PROP.BankCodeKind   = g_CodeKind;
  OC_PAY_DOC_PMNT_PROP.BankCode       = InputPanel.BankCode;
  OC_PAY_DOC_PMNT_PROP.CorrAccount = InputPanel.CorAccRecp;
  OC_PAY_DOC_PMNT_PROP.Account = InputPanel.AccountRecp;

  if ( ( OC_PAY_DOC_PMNT_PROP.INN == "" ) and ( OC_PAY_DOC.CorrINN != "" ) and ( OC_PAY_DOC.GroupOpert == 11 ) )
    OC_PAY_DOC_PMNT_PROP.INN = OC_PAY_DOC.CorrINN;
  end;

  OC_PAY_DOC.NotifPayer        = InputPanel.NotifPayer;
  OC_PAY_DOC.NotifRecp         = InputPanel.NotifRecp;
  OC_PAY_DOC.ComisNotifPayer   = InputPanel.ComisNotifPayer;
  OC_PAY_DOC.ComisNotifRecp    = InputPanel.ComisNotifRecp;
  OC_PAY_DOC.RecpMessage       = InputPanel.RecpMessage;
  OC_PAY_DOC.ComisRecpMessage  = InputPanel.ComisRecpMessage;

  if ( InputPanel.FlagRez == "" )
    SetBitFlag( OC_PAY_DOC.Flags, 8, 0 );
  else
    SetBitFlag( OC_PAY_DOC.Flags, 8, 1 );
  end;

  fillRddAttrs();
end;


/**************************************************************************\
|    Проверка заполнения полей  панели ввода дополнительной информации     |
\**************************************************************************/
macro CheckFields

  var stat = true;
  var i;
  var strTmp;
  var countSpace = 0;

  if ( stat and ( InputPanel.FNameRecp == "" ) )
    stat = false;
    MsgBox( "Не задана фамилия получателя|Фамилия получателя является обязательным параметром" );
  end;

  if ( stat and ( InputPanel.NotifRecp != "" ) )
    if ( (InputPanel.AddrRecp == "" ) and ( InputPanel.PhoneRecp == "" ) )
      stat = false;
      MsgBox( "Не задан адрес или телефон получателя|Для уведомления получателя должен быть задан его адрес или телефон" );
    end;
  end;

  if ( stat and ( OC_PAY_DOC.ApplType == 1 ) )
    if ( ( InputPanel.AccountRecp == "" ) and ( OC_PAY_DOC.TypeAccRecp == "" ) )
      stat = false;
      MsgBox( "Не задан ни счет зачисления ни вклад зачисления|При зачислении во вклад должен быть задан счет или вклад зачисления" );
    end;
  end;

  if ( stat and ( ( strTmp = Trim( InputPanel.RecpMessage ) ) != "" ) )
    for ( i, 1, StrLen( strTmp ) )
      if ( SubStr( strTmp, i, 1 ) == " " )
        countSpace = countSpace + 1;
      end;
    end;

    if ( countSpace > 9 )
      stat = false;
      MsgBox( "Текстовое сообщение из ", countSpace + 1 ," слов|Текстовое сообщение не должно превышать 10 слов" );
    end;
  end;

  return stat;

end;


/**************************************************************************\
|      Обработчик для панели задания информации об оплачиваемом чеке       |
\**************************************************************************/
macro WorkWithDependedFields

  if ( Trim( InputPanel.RecpMessage ) != "" )
    InputPanel.ComisRecpMessage = ServTarifSum;
  else
    InputPanel.ComisRecpMessage = $0L;
  end;

  if ( ( ( OC_PAY_DOC.CodCur != 0 ) and ( OC_PAY_DOC.InSum != 0 ) ) or
       ( ( OC_PAY_DOC.CommCodCur != 0 ) and ( OC_PAY_DOC.CommSum != 0 ) ) )
    ;
  else
    InputPanel.SummaTotal = InputPanel.SummaPay        +
                            InputPanel.ComisPay        +
                            InputPanel.ComisNotifPayer +
                            InputPanel.ComisNotifRecp  +
                            InputPanel.ComisRecpMessage;
  end;

end;


/**************************************************************************\
|      Обработчик для панели задания информации об оплачиваемом чеке       |
\**************************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;
  var i;

  /* ================================================================== */
  if (cmd == DLG_KEY)
    /* ENTER -------------------------------------------------------- */
    if ( key == 13 )
      if ( id == ne_RecpMessage )
        SetFocus( IP, ne_FNameRecp );
        stat = CM_IGNORE;
      end;
    /* -------------------------------------------------------------- */
    elif (key == 32)
      /* ---------------------------------------------------------- */
      if (id == ne_TypeAccRecp)
        InputPanel.TypeAccRecp = "";
        OC_PAY_DOC.TypeAccRecp = "";
      /* ---------------------------------------------------------- */
      elif (id == ne_NotifPayer)
        if (InputPanel.NotifPayer == "")
          InputPanel.NotifPayer      = "X";
          InputPanel.ComisNotifPayer = ServTarifSum;
        else
          InputPanel.NotifPayer      = "";
          InputPanel.ComisNotifPayer = $0L;
        end;

        WorkWithDependedFields( );
        UpdateFields(IP);
      /* ---------------------------------------------------------- */
      elif (id == ne_NotifRecp)
        if (InputPanel.NotifRecp == "")
          InputPanel.NotifRecp      = "X";
          InputPanel.ComisNotifRecp = ServTarifSum;
        else
          InputPanel.NotifRecp      = "";
          InputPanel.ComisNotifRecp = $0L;
        end;

        WorkWithDependedFields( );
        UpdateFields( IP );
      /* ---------------------------------------------------------- */
      elif (id == ne_FlagRez)
        if (InputPanel.FlagRez == "")
          InputPanel.FlagRez = "X";
        else
          InputPanel.FlagRez = "";
        end;

        WorkWithDependedFields( );
        UpdateFields( IP );
      end;
    /* F2 ----------------------------------------------------------- */
    elif (key == 316)
      WorkWithDependedFields( );
      UpdateFields( IP );
      if ( CheckFields( ) )
        SaveFields( );
        stat = CM_SAVE;
      else
        stat = CM_IGNORE;
      end;
    /* F3 ----------------------------------------------------------- */
    elif ( key == 317 )
      /* ---------------------------------------------------------- */    
      if ( id == ne_CodeKind )
        list_CodeKinds( );
      /* ---------------------------------------------------------- */        
      elif ( id == ne_BankCode )
        list_BankCodes();
      /* ---------------------------------------------------------- */
      elif ( id == ne_CorAccRecp )
        list_branches( );
      /* ---------------------------------------------------------- */
      elif ( id == ne_TypeAccRecp )
        list_deptypes( );
      end;
    /* ALTF3--------------------------------------------------------- */
    elif ( key == 362)
      if ( id == ne_BankCode )
        list_localBankCodes();
        UpdateFields( IP );
      end;
      // EXITField
    elif ( key == 530 )
      if ( id == ne_BankCode )
        checkBankExists();
      end;
    /* ESC ---------------------------------------------------------- */
    elif ( key == 27 )
      if ( GetTrue( true, "Вы хотите отменить проведение операции ?" ) )
        WorkWithDependedFields( );
        UpdateFields( IP );
        SaveFields( );
        stat = CM_CANCEL;
      else
        stat = CM_IGNORE;
      end;
    end;
  /* ================================================================== */
  elif (cmd == DLG_REMFOCUS)
    WorkWithDependedFields( );
    UpdateFields( IP );
  elif (cmd == DLG_INIT)
    IP.CodeKind = FindBankCodeKindName(g_CodeKind);
  end;

  return stat;

end;


/**************************************************************************\
|                    Г Л А В Н А Я    П Р О Г Р А М М А                    |
\**************************************************************************/

macro Main_Trns_NP()

  OC_RESULT = 0;

  if ((OC_PAY_DOC.GroupOpert == CO_Payed) AND (OC_PAY_DOC.NumOpert == OP_CardPay))
    OP_WithAuth = OC_PAY_DOC.ApplType;
  end;
      
  if ((OC_RESULT == 0) AND (OC_PAY_DOC.ApplType == 0))
    if ( OC_PAY_DOC.NumOpert != 42 )
      MsgBox("Не задан подвид операции|Подвид операции является обязательным параметром");
      OC_RESULT = 1;
    end;
  end;

  if ((OC_RESULT == 0) AND (OC_PAY_DOC.ClientLastName == ""))
    MsgBox("Не задана фамилия клиента|Фамилия клиента является обязательным параметром");
    OC_RESULT = 1;
  end;

  if (OC_RESULT == 0)
    sb_tarif.IsCur    = FlagCur;
    sb_tarif.NumTarif = ServTarifNum;
    if (GetEQ(sb_tarif))
      if ( sb_tarif.TarifPlus )
        tarif_plus.IsCur    = FlagCur;
        tarif_plus.NumTarif = ServTarifNum;
        tarif_plus.Date     = OC_PAY_DOC.Date_Document;
        tarif_plus.SumOper  = OC_PAY_DOC.InSum;
        if ( GetGE( tarif_plus )  AND
             ( tarif_plus.IsCur    == FlagCur )  AND
             ( tarif_plus.NumTarif == ServTarifNum )  AND
             ( tarif_plus.Date     == OC_PAY_DOC.Date_Document ) )
          sb_tarif.PerSent = tarif_plus.Percent;
          sb_tarif.MinSum  = tarif_plus.MinSum;
          sb_tarif.MaxSum  = tarif_plus.MaxSum;
          sb_tarif.Summa   = tarif_plus.Summa;
        end;
      end;
      ServTarifSum = sb_tarif.Summa;
    else
      MsgBox("Не найден тариф за уведомление (номер " + String(ServTarifNum) + ")");
      OC_RESULT = 1;
    end;
  end;

  /* Для оп. 26 */
  if ((OC_RESULT == 0) AND (OP_WithAuth > 0) )
    if ( NOT iFindBank( OC_PAY_DOC_PMNT_PROP.BankCodeKind, OC_PAY_DOC_PMNT_PROP.BankCode ) )
      MsgBox("Перевод за пределы России запрещен");
      OC_RESULT = 1;

    elif ( (OP_WithAuth == 1) AND (OC_PAY_DOC.CodCur != OC_PAY_DOC.CommCodCur) AND (OC_PAY_DOC.CommSum > 0) )
      MsgBox("Валюта комиссии не совпадает с валютой операции.|Авторизация через POS невозможна");
      OC_RESULT = 1;
    end;

    AuthSum = OC_PAY_DOC.InSum + OC_PAY_DOC.CommSum;
  end;

  if ( ( OC_RESULT == 0 ) AND ( OP_WithAuth != 1 ) )
    ClearRecord( InputPanel );
    InputPanel.FNameRecp        = OC_PAY_DOC.FNameRecp;
    InputPanel.NameRecp         = OC_PAY_DOC.NameRecp;
    InputPanel.SNameRecp        = OC_PAY_DOC.SNameRecp;
    InputPanel.AddrRecp         = OC_PAY_DOC.AddrRecp;
    InputPanel.PhoneRecp        = OC_PAY_DOC.PhoneRecp;
    InputPanel.CodeKind          = OC_PAY_DOC_PMNT_PROP.BankCodeKind;
    InputPanel.BankCode         = OC_PAY_DOC_PMNT_PROP.BankCode;
    InputPanel.CorAccRecp       = OC_PAY_DOC_PMNT_PROP.CorrAccount;
    InputPanel.AccountRecp      = OC_PAY_DOC_PMNT_PROP.Account;
    InputPanel.TypeAccRecp      = FindTypeAcc(OC_PAY_DOC.TypeAccRecp);
    InputPanel.NotifPayer       = OC_PAY_DOC.NotifPayer;
    InputPanel.NotifRecp        = OC_PAY_DOC.NotifRecp;
    InputPanel.RecpMessage      = OC_PAY_DOC.RecpMessage;
    InputPanel.SummaPay         = OC_PAY_DOC.InSum;
    InputPanel.ComisPay         = OC_PAY_DOC.CommSum;
    InputPanel.ComisNotifPayer  = OC_PAY_DOC.ComisNotifPayer;
    InputPanel.ComisNotifRecp   = OC_PAY_DOC.ComisNotifRecp;
    InputPanel.ComisRecpMessage = OC_PAY_DOC.ComisRecpMessage;
    if ( ( ( OC_PAY_DOC.CodCur != 0 ) and ( OC_PAY_DOC.InSum != 0 ) ) or
         ( ( OC_PAY_DOC.CommCodCur != 0 ) and ( OC_PAY_DOC.CommSum != 0 ) ) )
      ;
    else
      InputPanel.SummaTotal = InputPanel.SummaPay        +
                              InputPanel.ComisPay        +
                              InputPanel.ComisNotifPayer +
                              InputPanel.ComisNotifRecp  +
                              InputPanel.ComisRecpMessage;
      AuthSum = InputPanel.SummaTotal;
    end;

    if (NOT RunDialog(InputPanel,"WorkDialog"))
      OC_RESULT = 1;
    end;

    if ( ( OC_RESULT == 0 ) AND ( OC_PAY_DOC.ComisNotifPayer > 0 ) )
      ClearRecord( pay_doc );
      pay_doc.FNCash           = OC_PAY_DOC.FNCash;
      pay_doc.IsCur            = OC_PAY_DOC.IsCur;
      pay_doc.GroupOpert       = CO_Comission;
      if (OP_WithAuth == 2)
        pay_doc.NumOpert       = ServTarifMsPy;
      else
        pay_doc.NumOpert       = ServTarifPy;
      end;
      pay_doc.VidDoc           = GetVidDoc(pay_doc.IsCur, pay_doc.GroupOpert, pay_doc.NumOpert);
      pay_doc.Oper             = OC_PAY_DOC.Oper;
      pay_doc.InSum            = OC_PAY_DOC.ComisNotifPayer;
      pay_doc.Date_Document    = OC_PAY_DOC.Date_Document;
      pay_doc.ClientLastName   = OC_PAY_DOC.ClientLastName;
      pay_doc.ClientFirstName  = OC_PAY_DOC.ClientFirstName;
      pay_doc.ClientSecondName = OC_PAY_DOC.ClientSecondName;
      pay_doc.SocialNumber     = OC_PAY_DOC.SocialNumber;
      pay_doc.PersIDType       = OC_PAY_DOC.PersIDType;
      pay_doc.PassportSer      = OC_PAY_DOC.PassportSer;
      pay_doc.PassportNumb     = OC_PAY_DOC.PasportNumb;
      pay_doc.Ground           = "Плата за уведомление отправителю по переводу наличных денег";
      if (InsertPayDocToGDDList(pay_doc))
        OC_RESULT = 0;
      else
        OC_RESULT = 1;
      end;
    end;

    if ( ( OC_RESULT == 0 ) AND ( OC_PAY_DOC.ComisNotifRecp > 0 ) )
      ClearRecord( pay_doc );
      pay_doc.FNCash           = OC_PAY_DOC.FNCash;
      pay_doc.IsCur            = OC_PAY_DOC.IsCur;
      pay_doc.GroupOpert       = CO_Comission;
      if (OP_WithAuth == 2)
        pay_doc.NumOpert       = ServTarifMsRc;
      else
        pay_doc.NumOpert       = ServTarifRc;
      end;
      pay_doc.VidDoc           = GetVidDoc(pay_doc.IsCur, pay_doc.GroupOpert, pay_doc.NumOpert);
      pay_doc.Oper             = OC_PAY_DOC.Oper;
      pay_doc.InSum            = OC_PAY_DOC.ComisNotifRecp;
      pay_doc.Date_Document    = OC_PAY_DOC.Date_Document;
      pay_doc.ClientLastName   = OC_PAY_DOC.ClientLastName;
      pay_doc.ClientFirstName  = OC_PAY_DOC.ClientFirstName;
      pay_doc.ClientSecondName = OC_PAY_DOC.ClientSecondName;
      pay_doc.SocialNumber     = OC_PAY_DOC.SocialNumber;
      pay_doc.PersIDType       = OC_PAY_DOC.PersIDType;
      pay_doc.PassportSer      = OC_PAY_DOC.PassportSer;
      pay_doc.PassportNumb     = OC_PAY_DOC.PasportNumb;
      pay_doc.Ground           = "Плата за уведомление получателя по переводу наличных денег";
      if (InsertPayDocToGDDList(pay_doc))
        OC_RESULT = 0;
      else
        OC_RESULT = 1;
      end;
    end;

    if ( ( OC_RESULT == 0 ) and ( OC_PAY_DOC.ComisRecpMessage > 0 ) )
      ClearRecord( pay_doc );
      pay_doc.FNCash           = OC_PAY_DOC.FNCash;
      pay_doc.IsCur            = OC_PAY_DOC.IsCur;
      pay_doc.GroupOpert       = CO_Comission;
      if (OP_WithAuth == 2)
        pay_doc.NumOpert       = ServTarifMsTx;
      else
        pay_doc.NumOpert       = ServTarifMs;
      end;
      pay_doc.VidDoc           = GetVidDoc(pay_doc.IsCur, pay_doc.GroupOpert, pay_doc.NumOpert);
      pay_doc.Oper             = OC_PAY_DOC.Oper;
      pay_doc.InSum            = OC_PAY_DOC.ComisRecpMessage;
      pay_doc.Date_Document    = OC_PAY_DOC.Date_Document;
      pay_doc.ClientLastName   = OC_PAY_DOC.ClientLastName;
      pay_doc.ClientFirstName  = OC_PAY_DOC.ClientFirstName;
      pay_doc.ClientSecondName = OC_PAY_DOC.ClientSecondName;
      pay_doc.SocialNumber     = OC_PAY_DOC.SocialNumber;
      pay_doc.PersIDType       = OC_PAY_DOC.PersIDType;
      pay_doc.PassportSer      = OC_PAY_DOC.PassportSer;
      pay_doc.PassportNumb     = OC_PAY_DOC.PasportNumb;
      pay_doc.Ground           = "Плата за текстовое сообщение получателю";
      if (InsertPayDocToGDDList(pay_doc))
        OC_RESULT = 0;
      else
        OC_RESULT = 1;
      end;
    end;

  end;

  if ( OC_RESULT == 0 )
    OC_PAY_DOC.Ground = "От " + OC_PAY_DOC.ClientLastName;
    if (StrLen(OC_PAY_DOC.ClientFirstName) > 0)
      OC_PAY_DOC.Ground = OC_PAY_DOC.Ground + " " + SubStr(OC_PAY_DOC.ClientFirstName,1,1) + ".";
      if (StrLen(OC_PAY_DOC.ClientSecondName) > 0)
        OC_PAY_DOC.Ground = OC_PAY_DOC.Ground + SubStr(OC_PAY_DOC.ClientSecondName,1,1) + ".";
      end;
    end;

    OC_PAY_DOC.Ground = OC_PAY_DOC.Ground + " для перевода в филиал";

    if (StrLen(OC_PAY_DOC_PMNT_PROP.CorrAccount) > 0)
      OC_PAY_DOC.Ground = OC_PAY_DOC.Ground + " № " + OC_PAY_DOC_PMNT_PROP.CorrAccount;
    end;

    if ((OC_PAY_DOC.ApplType == 1) AND (OP_WithAuth == 0))
      OC_PAY_DOC.Ground = OC_PAY_DOC.Ground + " для зачисления во вклад";
    else
      OC_PAY_DOC.Ground = OC_PAY_DOC.Ground + " для выплаты наличных денег";
    end;

    if (OC_PAY_DOC.FNameRecp != "")
      OC_PAY_DOC.Ground = OC_PAY_DOC.Ground + " " + OC_PAY_DOC.FNameRecp;
      if (StrLen(OC_PAY_DOC.NameRecp) > 0)
        OC_PAY_DOC.Ground = OC_PAY_DOC.Ground + " " + SubStr(OC_PAY_DOC.NameRecp,1,1) + ".";
        if (StrLen(OC_PAY_DOC.SNameRecp) > 0)
          OC_PAY_DOC.Ground = OC_PAY_DOC.Ground + SubStr(OC_PAY_DOC.SNameRecp,1,1) + ".";
        end;
      end;
    end;
  end;

  if ( ( OC_RESULT == 0 ) AND ( OP_WithAuth > 0 ) )
    if ( NOT Authorization(AuthSum) )
      OC_RESULT = 1;
    end;
  end;

  if ( OC_RESULT == 0 )
    if ( NeedFillRtPaym1( null, null, OC_PAY_DOC_PMNT_PROP.BankCode, OC_PAY_DOC_PMNT_PROP.CorrAccount, OC_PAY_DOC_PMNT_PROP.Account, OC_PAY_DOC.CodClient, OC_PAY_DOC.FlagRez ) )
      userAttrs = getDocUserAttrPool( );
      varUserAttrs = userAttrs.overlayRecord( "rt_paym.1", 0, false );

      // сдесь сделано предположение о том что userAttrs указывает на блоб той же записи что и OC_PAY_DOC.
      // к сожалению я не могу проверить так ли это на самом деле в рамках 772 проекта. По крайней мере на
      // этом его этапе. Если предположение не верно, то тестеры укажут на отвал, тада будет думать как
      // же вытащить нужную структуру RTPaymentProp :(
      userAttrs.clear( );
      OC_PAY_DOC_PMNT_PROP.Branch    = NumFNCash( );
      userAttrs.rec.iApplicationKind = OC_PAY_DOC.iApplicationKind;
      OC_PAY_DOC_PMNT_PROP.DocApplicationKind = OC_PAY_DOC.iApplicationKind;
      userAttrs.rec.ApplicationKey   = OC_PAY_DOC.ApplicationKey;
      OC_PAY_DOC_PMNT_PROP.DocApplicationKey = OC_PAY_DOC.ApplicationKey;
      userAttrs.rec.AttrID           = "РЕКВ_ПОЛ";
      varUserAttrs.clear( );
      varUserAttrs.rec.PaymentDirection = DefPaymentDirection( null, null, OC_PAY_DOC_PMNT_PROP.BankCode, true );

      if ( not userAttrs.insert( varUserAttrs.recSize ) )
        OC_RESULT = 1;
      end;
    end;
  end;

  OC_VARLEN = GetRecordSize( OC_PAY_DOC ) - GetRecordSize( pay_doc );
end;

Main_Trns_NP();
end;
