/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Завещательное распоряжение
  (выпускается при операции 232 "Ввод завещания".)

*****************************************************************/

 import DeprIntr, Alg_Vars, Will_Com, rsd;

 private macro get_Heir_ByID( CodClient, Name1, Name2, Name3, QPart_m, QPart_n )
   var sHeir;
   if ( CodClient == 0 )
     sHeir = Name1 + " " + Name2 + " " + Name3;
     /* Формирование доли */
     if ( ( QPart_m != 0 ) and ( QPart_n != 0 ) )
       sHeir = sHeir + ", доля " + String( QPart_m ) + "/" +
                                   String( QPart_n ) + " ( " +
                                   Str_Fraction( QPart_m,
                                                 QPart_n ) + " )";
     end;
   else
     if ( DepClntH.GetRecord( CodClient ) )
       /* Формирование ФИО */
       sHeir = DepClntH.CurRec.ConvertFIOFull();
       /* Формирование доли */
       if ( ( QPart_m != 0 ) and ( QPart_n != 0 ) )
         sHeir = sHeir + ", доля " + String( QPart_m ) + "/" +
                                     String( QPart_n ) + " ( " +
                                     Str_Fraction( QPart_m,
                                                   QPart_n ) + " )";
       end;
     else
       sHeir = "????????";
     end;
   end;
   return sHeir;
 end;

 /**************************************************************************\
 |           Основная процедура выпуска Завещательного распоряжения.        |
 \**************************************************************************/
 macro Will_Input()

   var vNumber;
   var cmd, rs;
   var CodClient = 0;
   var ConditionClient = 0;
   var sFIO_Client = "";
   var Name1 = "", Name2 = "", Name3 = "";

   /* Поиск записи по завещанию. */
   /*
   ReWind( will );
   ClearRecord( will );
   will.RefID = ALG_WREG.WillRef;
   if ( NOT GetEQ( will ) )
     println( "*** Не найдена запись по завещанию с RefID = ", ALG_WREG.WillRef );
     ErrCodeWR = Status( ErrTextWR );
     println( "  Ошибка " + String( ErrCodeWR ) + ": " + ErrTextWR );
     Exit;
   end;
   */

   if ( will_r.IsNotarial == StrFor(0) )
     vNumber = ALG_WREG.Number;  /* Сбер.банковское завещание. */
   else
     vNumber = 0;                /* Нотариально оформленное завещание. */
   end;

   // Получение атрибутов структурного подразделения.
   get_Branch_Attributs();

   sFIO_Client = get_FIO_Client();

   [
                                                                                 Ф.№ 308
                                    ЗАВЕЩАТЕЛЬНОЕ РАСПОРЯЖЕНИЕ


      ###################################################################################
       (Номер/наименование и местонахождение структурного подразделения Сбербанка России)

      Я,__ ##############################################################################
                                 (Фамилия, имя и отчество завещателя)
      ###################################################################################
        (Наименование документа,                               (Где, кем, когда выдан)
       удостоверяющего личность)
      ###################################################################################
                             (По регистрации места жительства)
      завещаю  свои  права  на   денежные  средства,  внесенные   во   вклад(ы) / счет(а)
                                                                     (ненужное зачеркнуть)
      № #################################################################################
      ___________________________________________________________________________________
      ___________________________________________________________________________________
      ###################################################################################
                         (Номер/наименование структурного подразделения Сбербанка России)
      ___________________________________________________________________________________ ]
     ( ( " __ " + sBranchCode + " __ / __ " + sBranchName +
       " ____ " + sBranchAddr ):w,
     Str_Line( sFIO_Client + ", ", 83 ),
     ( "__ " + GetNameOfPAPRKIND( DepClnt.CurRec.PaperKind ) + " __ номер " +
       DepClnt.CurRec.MakeDocumentString() + ", выдан " + DepClnt.CurRec.rec.PaperIssuedDate +  " __ " ):w,
     ( "проживающий(ая) по адресу " + DepClnt.CurRec.MakeFullStrAddress() + "," ):w,
     Str_Line( String( Acc_Form(ALG_DEPOSITOR.Account):f ) + " ", 83 ),
     ( "находящиеся в структурном подразделении __ " + sBranchCode + " / " +
       sBranchName + "," ):w );

     cmd = RsdCommand( "select t_CodClient as rClnt, " +
                              "t_Name1 as rName1, " +
                              "t_Name2 as rName2, " +
                              "t_Name3 as rName3, " +
                              "t_QPart_m as rM, " +
                              "t_QPart_n as rN " +
                       "from dsbtrast_dbt " +
                       "where t_fncash=? and " +
                       "t_willregnum=?" );

     cmd.addParam( "branch", RSDBP_IN );
     cmd.addParam( "regnum", RSDBP_IN );

     cmd.value( "branch" ) = FNCash;
     cmd.value( "regnum" ) = ALG_WREG.Number;

     cmd.execute;

     rs = RsdRecordset( cmd );

     while( rs.moveNext() )
        [      ###################################################################################]
        ( get_Heir_ByID( rs.value( "rClnt" ), 
                         rs.value( "rName1" ), 
                         rs.value( "rName2" ), 
                         rs.value( "rName3" ), 
                         rs.value( "rM" ), 
                         rs.value( "rN" ) ) );
     end;

     cmd = RsdCommand( "select t_CodClient as rClnt, " +
                              "t_Name1 as rName1, " +
                              "t_Name2 as rName2, " +
                              "t_Name3 as rName3, " +
                              "t_ConditionClient as rCond " +
                        "from dsbtrast_dbt " +
                        "where t_fncash=? and " +
                              "t_willregnum=? and " +
                              "t_ConditionClient>0" );

     cmd.addParam( "branch", RSDBP_IN );
     cmd.addParam( "regnum", RSDBP_IN );

     cmd.value( "branch" ) = FNCash;
     cmd.value( "regnum" ) = ALG_WREG.Number;

     cmd.execute;

     rs = RsdRecordset( cmd );

     if( rs.moveNext() )
         CodClient = rs.value( "rClnt" );
         ConditionClient = rs.value( "rCond" );
         Name1 = rs.value( "rName1" );
         Name2 = rs.value( "rName2" );
         Name3 = rs.value( "rName3" );
         if ( rs.moveNext() )
            /* Если таких записей больше одной, даем возможность пользователю самому заполнить эту информацию */
            CodClient = 0;
            ConditionClient = 0;
         end;
     end;
     if ( ( CodClient > 0 )  and  ( ConditionClient > 0 ) )
   [  ___________________________________________________________________________________
      (Фамилия, имя, отчество лица (лиц), год(ы) рождения (если  завещатель располагает
      данными), или/и полное наименование, местонахождение юридического лица (лиц),
      которому(ым) права на денежные средства завещаются; если наследников несколько -
      при желании указать кому, в каких долях завещается (доли указываются в виде
      правильной простой дроби цифрами и прописью)

      ###################################################################################
      ___________________________________________________________________________________
      умрет до открытия наследства, либо одновременно с завещателем, либо после открытия
      наследства, не успев его принять, либо не примет наследство по другим причинам или
      откажется от него, либо не будет иметь право наследовать или будет отстранен от
      наследования как недостойный, завещаю названный(ые) вклад(ы)/счет(а)_______________
                                                       (ненужное зачеркнуть)
      ###################################################################################](
      ( "В случае, если " + get_Heir1_ReWillByID(CodClient, ConditionClient) ):w,
                            get_Heir2_ReWillByID(CodClient, ConditionClient):w );
      else
   [  ###################################################################################
      ___________________________________________________________________________________
      (Фамилия, имя, отчество лица (лиц), год(ы) рождения (если  завещатель располагает
      данными), или/и полное наименование, местонахождение юридического лица (лиц),
      которому(ым) права на денежные средства завещаются; если наследников несколько -
      при желании указать кому, в каких долях завещается (доли указываются в виде
      правильной простой дроби цифрами и прописью)

                                                                                         
      ___________________________________________________________________________________
      умрет до открытия наследства, либо одновременно с завещателем, либо после открытия
      наследства, не успев его принять, либо не примет наследство по другим причинам или
      откажется от него, либо не будет иметь право наследовать или будет отстранен от
      наследования как недостойный, завещаю названный(ые) вклад(ы)/счет(а)_______________
                                                       (ненужное зачеркнуть)
                                                                                         ]
      ( ( Name1 + " " + Name2 + " " + Name3 ):w );
      end;

   [  ___________________________________________________________________________________
      О содержании статей 1128, 1130, 1149, 1150 и 1162 Гражданского кодекса Российской
      Федерации проинформирован(а).

      Настоящее завещательное распоряжение составлено и подписано в двух экземплярах,
      один из которых получен мною лично.
      Завещатель ____________________/__________________________________________________ /
                      (Подпись)  (Фамилия, имя и отчество - указываются завещателем полностью)
      ______________________________________________________________________________ года
                           (Дата - число, месяц, год - указываются прописью)

      ###################################################################################
                                                              (Фамилия, инициалы, должность)
      ###################################################################################
                                                         (Фамилия, имя, отчество завещателя)
      в моем присутствии. Личность его установлена. О содержании статей 1128, 1130, 1149, 1150 и
      1162  Гражданского кодекса Российской Федерации завещатель мною проинформирован.

      В книге регистрации завещательных распоряжений зарегистрировано за № ##############
      __ ################## __  __________________ #######################################
      (Должность работника           (Подпись)         (Фамилия, имя и отчество)
       структурного подразделения
       Сбербанка России)


      М.П.                                ###################################################]
   (     
     ( "Настоящее завещательное распоряжение удостоверено мной, " +
        get_FIO_Oper() + ", " + GetNameTypePerson() + "." ):w,
     ( "Завещательное распоряжение собственноручно подписано: " +
        get_FIO_Client() ):w,
     vNumber:l,
     GetNameTypePerson(),
     "/ " + get_FIO_Oper() + " /",
     will_r.DateOpen:r:m
   );

 end;

 /**************************************************************************\
 |                           Головная процедура.                            |
 \**************************************************************************/

 ALG_RESULT = OK_RES;

 if ( IsFirstLog() )

   /* Поиск записи по завещанию. */
   Get_Will_Record();

   Will_Input();

   /*
   if ( will_r.IsNotarial == StrFor(0) )
     Will_Input();
   else
     Exit( 1 );
   end;
   */
 else
   Exit( 1 );
 end;
