/*
  RS-Retail
  Операция "Выдача процентов"
  Шаг "Расчет суммы к выдаче"

  Расчет и выдача процентов по нестандартным схемам:
  - за полные месяцы хранения: за полные месяцы от начала договора
    вычисляются проценты и предлагаются к выдаче
  - расчитанные, но не оплаченные: если проценты считаются по графику
    чаще, чем оплачиваются, то можно выдать расчитанные по графику проценты,
    которые еще не оплачены (в соответствии с графиком)
----------------------------------------------------------------------
  ВНИМАНИЕ!
    Выдача процентов за полный месяц и расчитанных процентов одновременно
  для одного вида вклада не совместимы- разрешать либо то, либо другое
----------------------------------------------------------------------
  Зубов Виктор Юрьевич
  29.09.1998
  Ромодин Александр Васильевич
  25.05.2001
*/
import deprintr, opercode, srpenlib, IsSrvDoc;

private file tmp_sbdepdoc ( sbdepdoc );
record sbdepdoc_12 ( "sbdepdoc.12" );

record  tmpDoc8("sbdepdoc.8"); /* файл документов оплаты налога */
/*
  Определение суммы процентов за полные месяцы
*/
macro TrnProc
  Результат = 0;
  ErrMsg = "";
  GetPercentsFromFullMonths;
  AbortTrn;
  /* if(Результат != 0)
    AbortTrn;
  end; */
end;

/* Позволяет интерактивно выбрать одну из подопераций данной операции */
/***********
Macro GetApplType(TypeOper)
file suboper (suboper) key 1;
Array ArrName, ArrApplType;
Var i, found;
suboper.IsCur    = ALG_DEPOSITOR.IsCur;
suboper.Kind     = ALG_DEPOSITOR.Type_Account;
suboper.OperType = TypeOper;
suboper.Type     = 0;
found = GetGE(suboper);
i=0;
while ( found                                           AND
       (suboper.IsCur    == ALG_DEPOSITOR.IsCur)        AND
       (suboper.Kind     == ALG_DEPOSITOR.Type_Account) AND
       (suboper.OperType == TypeOper           )    )
  if (suboper.Type != За_Полные_Месяцы_Уст)
    ArrName(i) = suboper.Name;
    ArrApplType(i) = suboper.Type;
    i = i + 1;
  end; /* if */
  found = Next(suboper);
end; /* while */
if (i > 0)
  i = Menu(ArrName,"Выберите подвид операции:");
  Return ArrApplType(i);
end; /* if */
Return 0;
end; /* MACRO*/
*************/

/*---------------Точка входа в макрос ------------------------------------*/

  var st, TaxSum = $0l;
  var SumForPay = $0l,
      Sum1 = $0l, Sum2 = $0l;
/* Заранее формируем код возврата */
  ALG_RESULT = ERR_RES;
/* Запрашиваем подвид*/
  ApplType = ALG_SBDEPDOC.ApplType; /*GetApplType(Выдача_Процентов);*/
/* Если это не подвид "За полные месяцы, то продолжаем проводку */
  if (ApplType == 0)
    ALG_RESULT = OK_RES;
    return;
  end; /* IF */
/* Начало договора */
  if(ALG_DEPOSITOR.Prol_DateDep != NullDate)
    chkDate = ALG_DEPOSITOR.Prol_DateDep;
  else
    chkDate = ALG_DEPOSITOR.Start_DateDep;
  end; /* if */
/* Запрашиваем дату операции */
  currentDate = {curdate};

  if ( (ApplType == За_Полные_Месяцы)
   OR  (ApplType == За_Полные_Месяцы_Уст) )
/*
    Выдача процентов за полные месяцы
*/
    if ( (ALG_DEPOSITOR.Type_Account != "Ср.пен.1г1м")
     AND (ALG_DEPOSITOR.Type_Account != "Ср.пенсион.")
     AND (ALG_DEPOSITOR.Type_Account != "Ср.пенс.6мес") )
      MsgBox ("Выдача процентов за полные месяцы не производится");
      ALG_RESULT = ERR_RES;
      return;
    end;
/*  Не завершен ли договор? */
    if (ALG_DEPOSITOR.UseAlternate)
      MsgBox ("Договор завершен- выплата за полные месяцы не производится");
      ALG_RESULT = ERR_RES;
      return;
    end;
/*  Проверяем, вступили ли в силу условия о выплате процентов за новые месяцы */
    LimitDate = Date(16,9,1998);
    if ((chkDate < LimitDate) OR (ALG_DEPOSITOR.Type_Account == "Ср.пен.1г1м"))
      if((StrFor(GetProgramID) != "П") or (not InFCGroupOpRun()))
        MsgBox("По данному счету нельзя выдавать проценты за полные месяцы хранения");
      end;
      ALG_RESULT = ERR_RES;
      return;
    end; /* if */
    LoopInTrn(TRUE);
    ProcessTrn(1, "TrnProc", doc, dr);
  elif (ApplType == Выдача_Расчитанных_Проц)
/*
    Выдача расчитанных, но не оплаченных процентов
*/
    if ( (ALG_DEPOSITOR.Type_Account == "Ср.пен.1г1м")
     OR  (ALG_DEPOSITOR.Type_Account == "Ср.пенсион.")
     OR  (ALG_DEPOSITOR.Type_Account == "Ср.пенс.6мес") )
      MsgBox ("Выдача расчитанных процентов не производится");
      ALG_RESULT = ERR_RES;
      return;
    end;
    eDate = Date(0,0,0);
    st = Проценты_Налог_К_Оплате_Учет_Конв (ALG_DEPOSITOR.Referenc, 0,
      currentDate, SumForPay, Sum1, Sum2, eDate);
    if (st != 0)
      MsgBox ("Ошибка "+String(st)+" при определении расчитанной суммы процентов");
      ALG_RESULT = ERR_RES;
      return;
    end;
    ALG_SBDEPDOC.DepDate_Document = eDate;
    UpdateAlgRecords (4, true);
/*    bDate = chkDate; */
    bDate = eDate; /* Пока так отсечем расчет налога на всю сумму к выдаче */
    ALG_PARAM.prmD = SumForPay;
    Результат = 0;
    ErrMsg = "";
  else
    MsgBox ("Введенный номер подоперации не обрабатывается");
    ALG_RESULT = ERR_RES;
    return;
  end; /* if */
/*
  Расчет налога на выдаваемую сумму процентов (за полный месяц)
*/
  if (bDate < eDate)
    st = Расчет_Налога_По_Счету_Вкладчика (ALG_DEPOSITOR.Referenc, 2001,
     bDate,eDate, True,False, TaxSum, False,True, False, False);
/*
    Проводка налога (документ оплаты- в глобальный список)
*/
    if (TaxSum > 1) /* Округление */
      TaxSum = Floor(Money(TaxSum)+0.5);
    end;
    if (TaxSum > 1)
      Copy( tmp_sbdepdoc, ALG_SBDEPDOC );
      SetRecordAddr( sbdepdoc_12, tmp_sbdepdoc, 0, 0, true );
      sbdepdoc_12.PcPercTax = TaxSum;
      Copy( ALG_SBDEPDOC, tmp_sbdepdoc );
      UpdateAlgRecords (4, true);
/*    ALG_PARAM.prmD       = ALG_PARAM.prmD - TaxSum;*/  /* Меньше на сумму налога */
      ClearRecord(tmpDoc8);
      tmpDoc8.Referenc         = ALG_DEPOSITOR.Referenc;
      tmpDoc8.IsCur            = ALG_DEPOSITOR.IsCur;
      tmpDoc8.FNCash           = ALG_DEPOSITOR.FNCash;
      tmpDoc8.Account          = ALG_DEPOSITOR.Account;
      tmpDoc8.Oper             = {oper};
      tmpDoc8.Type_Account     = ALG_DEPOSITOR.Type_Account;
      tmpDoc8.Code_Currency    = ALG_DEPOSITOR.Code_Currency;
      tmpDoc8.CodClient        = ALG_DEPOSITOR.CodClient;
      tmpDoc8.YesSbook         = "X";
      tmpDoc8.Date_Document    = {curdate};
      tmpDoc8.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
      tmpDoc8.Author           = ALG_SBDEPDOC.Author;
      tmpDoc8.ApplType         = Налог_Ставки_Рефинансирования;
      tmpDoc8.KindOp           = 3; /* D_GET */
      tmpDoc8.TypeOper         = Списание_Простое;
      tmpDoc8.TypeComplexOper  = ALG_SBDEPDOC.TypeComplexOper;
      tmpDoc8.ObjectPerc       = 2001;
      tmpDoc8.OutSum           = TaxSum;
      tmpDoc8.Ground           = "Налог на проценты сверх ставки рефинансирования";
      tmpDoc8.GlobTax          = TaxSum;
      tmpDoc8.DateEndPeriodCalc2001 = eDate;
      InsertDocToGDDList (tmpDoc8);
    end;
  end;
  if(Результат == 0)
    ALG_RESULT = SKIP_RES; /* Все Ok */
  else
    ALG_RESULT = ERR_RES; /* Ошибка */
  end;
  if((ErrMsg != "") and (not InFCGroupOpRun()))
    MsgBox(ErrMsg);
  end;
end;  /* Конец макроса (точки входа) */
