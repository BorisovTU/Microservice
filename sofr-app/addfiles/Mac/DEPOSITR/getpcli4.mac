/***************************************************************************\
| GETPCLI4.MAC - Библиотека макрофункций  для вида вклада "Срочный с ежеме- |
| сячной выдачей %% валютный"                         Зубов В.Ю. 20-10-97   |
\***************************************************************************/
import deprintr/*,opercode*/, issrvdoc, Календарь, alg_vars, sqlconv;

var SBDEPDOC = TDepFile( "sbdepdoc.dbt", "w" );
var SB_DREST = TDepFile( "pc_drest.dbt", "w" );
var sb_apltp = TBFile( "pc_apltp.dbt", "r", 0 );
var pcalg    = TBFile( "pc_alg.dbt", "r", 0 );
private var doc1 = TDepFile( "sbdepdoc.dbt", "r" );


/***************************************************************************\
| DebitDayIncluded - Возвращает TRUE, если в расчет %% необходимо включать  | |
| день расхода                                                              |
\***************************************************************************/
macro DebitDayIncluded( bdate, IsInclude, IsCur, Type_Account)
  /* В качестве признака будем ориентироваться на правую границу соответсвующего
     алгоритма по виду вклада по ОСНОВНЫМ условиям */

  /*msgbox(0,STATUS);*/
  /* Ищем вид вклада по основным условиям */
  sb_apltp.Clear;
  sb_apltp.rec.IsCur    = IsCur;
  sb_apltp.rec.TypeRec  = 1003;
  sb_apltp.rec.Type     = Type_Account;
  sb_apltp.rec.ApplType = 2001; /* по альтернативным условиям */
  sb_apltp.rec.ApType   = "";
  if ( not sb_apltp.GetEQ )
    /*msgbox(1,STATUS);*/
    return False;
  end; /*fi*/

  /* ищем алгоритм расчета */
  pcalg.Clear;
  pcalg.addFilter( "T_FlagCur = " + String( IsCur ) + " and " +
                   "T_Referenc = " + GetSQLString( sb_apltp.rec.ApType ) + " and " +
                   "T_ObjectType = 1003"                                            );
  pcalg.rec.FlagCur  = IsCur;
  pcalg.rec.Referenc = sb_apltp.rec.ApType;
  pcalg.rec.ObjectType = 1003;
  pcalg.rec.BegDate  = bdate;
  if ( not pcalg.GetLE OR (pcalg.rec.FlagCur  != IsCur) OR
       (pcalg.rec.Referenc != sb_apltp.rec.ApType) OR (pcalg.rec.ObjectType != 1003) )
    /*msgbox(2);*/
    pcalg.dropFilter;
    return False;
  end; /*fi*/
  if (   (pcalg.rec.RangeAlg == 2) /* левую исключать, правую включать */
      OR (pcalg.rec.RangeAlg == 3) /* левую исключать, правую включать + первый день */
      OR (pcalg.rec.RangeAlg == 4) /* левую включать, правую исключать + последний день */
     )
    SetParm(1, True);
  else
    SetParm(1, False);
  end;
  pcalg.dropFilter;
  return True;
end; /*orcam*/

/***************************************************************************\
| Число_Дней_В_Месяце                                                       |
\***************************************************************************/
MACRO Число_Дней_В_Месяце(Дата)
Var tmpDate,begDate,endDate,день,месяц,год;
 DateSplit(Дата,день,месяц,год);
 begDate = Date(1,месяц,год);
 месяц = месяц + 1;
 if( месяц > 12)
   месяц = 1;
   год = год +1;
 end;/* IF */
 tmpDate = Date(1,месяц,год);
 endDate = tmpDate - 1;
 день = endDate - begDate + 1;
/*
 [ Начало #
   Конец  #
   Дней   #](begDate:l,endDate:l,день:l);
*/
 return день;
end; /* of MACRO */

/***************************************************************************\
| Последний_День_Предыдущего_Месяца                                         |
\***************************************************************************/
MACRO Последний_День_Предыдущего_Месяца(Дата)
Var tmpDate,day,mon,year;
 DateSplit(Дата,day,mon,year);
 tmpDate = Date(1,mon,year);
 tmpDate = tmpDate - 1;
 return tmpDate;
end; /* of MACRO */

/***************************************************************************\
| Расчет процентов,уже начисл. на доп.взносы,не пролежавшие к сегодня месяца|
\***************************************************************************/
MACRO Расчет_Излишков_Процентов(Referenc, Отчислить, СуммаДопВзносов )
Var Дата_последнего_расчета,
    MaxDate,MinDate,
    Проценты=$0L, /*ПроцентыДВС,*/ Сумма=$0L,СуммаПроцентов=$0L,Результат=$0L,
    day,mon,year,found,
    Дата_Начала;

Отчислить = 0;
/*Причислить = 0;*/
Дата_последнего_расчета = Последний_День_Предыдущего_Месяца({curdate}); /* Если они закрывают счет предыдущим месяцем */
/*MSGBOX("Дата_последнего_расчета="+String(Дата_последнего_расчета));*/
doc1.KeyNum = 2;
doc1.addFilter( "T_Referenc = " + String( Referenc ) );
doc1.Clear;
doc1.rec.Referenc = Referenc;
MaxDate = Дата_Последнего_расчета;
MinDate = DateAfterCalenMonths(ALG_SBDEPDOC.DepDate_Document, -1);
doc1.rec.DepDate_Document = MinDate;


Проценты = $0;
/*ПроцентыДВС = $0;*/
Сумма = $0;
if( doc1.GetGE )
  found=True;
  while((doc1.rec.Referenc == Referenc) AND
        (doc1.rec.DepDate_Document <= MaxDate) AND
        (found == True))
    if( /*(doc.TypeOper == Зачисление_процентов) OR */
        /*(doc.TypeOper == Выдача_процентов) OR     */
        IsServDoc(doc1.rec)
        /* VZ 08.04.99 Проверка на сторнирующие и сторнированные операции*/
         AND (doc1.rec.FlagStorn == StrFor(0)) AND
        ((doc1.rec.TypeOper == Открытие_Наличными) OR
        (doc1.rec.TypeOper == Открытие_Безналичными) OR
        (doc1.rec.TypeOper == Дополнительный_Взнос) OR
        /*(doc.TypeOper == Списание_переводом) OR   */
        (doc1.rec.TypeOper == Зачисление_В_Пак_Режиме)))
      /*[#](doc.InSum:l);*/
      /* Документ может быть не деноминирован */
      Сумма = Сумма + Den(doc1.rec.InSum, doc1.rec.DepDate_Document);
      /*MsgBox("Сумма "+String(Сумма));*/
      /* VZ 6.01.98 Для операций открытия счета и других операций надо */
      /* задавать разное количество дней, т.к. интервал расчета процентов*/
      /* для операции открытия начинается со следующего дня*/
      if((doc1.rec.TypeOper == Открытие_Наличными) OR
        (doc1.rec.TypeOper == Открытие_Безналичными))
        Дата_Начала = doc1.rec.DepDate_Document;
      else
        Дата_Начала = doc1.rec.DepDate_Document+1;
      end; /* IF */

      Результат = Проценты_На_Операцию(doc1.rec.Account,doc1.rec.Type_Account, doc1.rec.Code_Currency,
                        Den(doc1.rec.InSum, doc1.rec.DepDate_Document), СуммаПроцентов,
                        Дата_Начала/*doc.DepDate_Document*/,
                        Дата_Последнего_расчета+1, 2001/*основной*/, 0/*Save*/);
      if(Результат != 0)
        doc1.dropFilter;
        return 1;
      end;/* IF */
      /*MsgBox("СуммаПроцентов"+String(СуммаПроцентов));*/
      Проценты=Проценты + СуммаПроцентов;
/*
      Результат = Проценты_На_Операцию(doc.Account,doc.Type_Account, doc.Code_Currency,
                        doc.InSum, СуммаПроцентов, doc.DepDate_Document,
                        Дата_Последнего_расчета, 2002/*альтернативный*/, 0/*Save*/);

      ПроцентыДВС=ПроцентыДВС + СуммаПроцентов;
      if(Результат != 0)
        return 1;
      end;/* IF */
*/
    end; /* IF */
    if ( not doc1.Next )
      found=False;
    end;/* IF */
  end; /* WHILE */
end; /* IF */

doc1.dropFilter;

SetParm(1,Проценты);
SetParm(2,Сумма);

/*Причислить = ПроцентыДВС*/;
return 0;
end; /* of MACRO */

/***************************************************************************\
| Расчет процентов на доп.взносы, не пролежавшие к сегодня месяца(по ДВС)   |
\***************************************************************************/
MACRO Расчет_Процентов_ДВС(Referenc, Причислить)

Var Дата_последнего_расчета,
    MaxDate,MinDate,
    Проценты=$0L, ПроцентыДВС=$0L, Сумма=$0L,СуммаПроцентов=$0L,Результат=$0L,
    /*{curdate},*/
    day,mon,year,found,
    Дата_Начала, Дата_Конца_Расчета, Включать_день_расхода;
Причислить = 0;

doc1.KeyNum = 2;
doc1.addFilter( "T_Referenc = " + String( Referenc ) );
doc1.Clear;
doc1.rec.Referenc = Referenc;
MaxDate = ALG_SBDEPDOC.DepDate_Document;
MinDate = DateAfterCalenMonths(ALG_SBDEPDOC.DepDate_Document, -1);
doc1.rec.DepDate_Document = MinDate;

ПроцентыДВС = $0;
Сумма = $0;
if( doc1.GetGE )
  found=True;
  while((doc1.rec.Referenc == Referenc) AND
        (doc1.rec.DepDate_Document <= MaxDate) AND
        (found == True))
    if( /*(doc1.TypeOper == Зачисление_процентов) OR */
        /*(doc1.TypeOper == Выдача_процентов) OR     */
        IsServDoc( doc1.rec )
        /* VZ 08.04.99 Проверка на сторнирующие и сторнированные операции*/
         AND (doc1.rec.FlagStorn == StrFor(0)) AND

        ((doc1.rec.TypeOper == Открытие_Наличными) OR
        (doc1.rec.TypeOper == Открытие_Безналичными) OR
        (doc1.rec.TypeOper == Дополнительный_Взнос) OR
        /*(doc1.TypeOper == Списание_переводом) OR   */
        (doc1.rec.TypeOper == Зачисление_В_Пак_Режиме)))
      /*[#](doc1.InSum:l);*/
      Сумма = Сумма + Den(doc1.rec.InSum,doc1.rec.DepDate_Document);
      /*MsgBox("Сумма"+String(Сумма));*/
      /* VZ 6.01.98 Аналогично Расчет_Излишков_Процентов */
      if((doc1.rec.TypeOper == Открытие_Наличными) OR
        (doc1.rec.TypeOper == Открытие_Безналичными))
        Дата_Начала = doc1.rec.DepDate_Document;
      else
        Дата_Начала = doc1.rec.DepDate_Document+1;
      end; /* IF */

      Дата_Конца_Расчета = ALG_SBDEPDOC.DepDate_Document;
      if (not DebitDayIncluded( ALG_SBDEPDOC.DepDate_Document, Включать_день_расхода, doc1.rec.IsCur,
                                doc1.rec.Type_Account))
        Результат = -1;
        doc1.dropFilter;
        return 1;
      end; /*fi*/
      if (Включать_день_расхода)
        Дата_Конца_Расчета = Дата_Конца_Расчета + 1;
      end; /*fi*/
      Результат = Проценты_На_Операцию(doc1.rec.Account,doc1.rec.Type_Account, doc1.rec.Code_Currency,
                        Den(doc1.rec.InSum,doc1.rec.DepDate_Document), СуммаПроцентов,
                        Дата_Начала/*doc1.DepDate_Document*/,
                        Дата_Конца_Расчета/*{curdate}*/,
                        2002/*альтернативный*/, 0/*Save*/);
      /*MsgBox("ПроцентыДВС"+String(СуммаПроцентов));*/
      ПроцентыДВС=ПроцентыДВС + СуммаПроцентов;
      if(Результат != 0)
        doc1.dropFilter;
        return 1;
      end;/* IF */

    end; /* IF */
    if ( not doc1.Next )
      found=False;
    end;/* IF */
  end; /* WHILE */
end; /* IF */

doc1.dropFilter;

SetParm(1,ПроцентыДВС);

return 0;

end; /* of MACRO */

/***********************************************************
   Корректировка файлов для получения верного отчета по
  расчету процентов (Ctrl-F10 из карточки вкладчика)
   Ромодин Александр Васильевич
***********************************************************/


macro Запись_суммы_на_альтернативный (Referenc, DepDate_Document, InSum, Object)
  var stat = True;
  var st;
  var Rest = 0;
  var NeedInsert = False;

  SB_DREST.KeyNum = 0;
  SB_DREST.addFilter( "T_Referenc = " + String( Referenc ) + " and " +
                      "T_Type_Object = " + String( Object )           );
  SB_DREST.Clear;
  SB_DREST.rec.Referenc   = Referenc;
  SB_DREST.rec.Type_Object = Object;
  SB_DREST.rec.Date_Rest   = DepDate_Document;
  if (SB_DREST.GetLE AND
      (SB_DREST.rec.Referenc == Referenc) AND
      (SB_DREST.rec.Type_Object == Object) )
    if (SB_DREST.rec.Date_Rest < DepDate_Document)
      Rest = SB_DREST.rec.Rest;
      NeedInsert = True;
    else
      SB_DREST.rec.Rest = SB_DREST.rec.Rest + InSum;
      stat = SB_DREST.Update;
    end;
  else
    NeedInsert = True;
  end;
  if (NeedInsert)
    SB_DREST.rec.Referenc   = Referenc;
    SB_DREST.rec.Type_Object = Object;
    SB_DREST.rec.Date_Rest   = DepDate_Document;
    SB_DREST.rec.Rest = Rest + InSum;
    SB_DREST.rec.FNCash = NumFNcash();
    stat = SB_DREST.Insert;
  end;
  if (stat)
    SB_DREST.rec.Referenc   = Referenc;
    SB_DREST.rec.Type_Object = Object;
    SB_DREST.rec.Date_Rest   = DepDate_Document + 1;
    st = SB_DREST.GetGE;
    while (stat AND st AND
    (SB_DREST.rec.Referenc == Referenc) AND
    (SB_DREST.rec.Type_Object == Object ))
      SB_DREST.rec.Rest = SB_DREST.rec.Rest + InSum;
      stat = SB_DREST.Update;
      if (stat)
        st = SB_DREST.Next;
      end;
    end;
  end;
  SB_DREST.dropFilter;
  
  return stat;
end;
/**********************************************************/

macro NullSumRest (Referenc,Object)
/*
  Обнуление остатка после закрытия
*/
  var stat = True, st = True;
  var DateEndPeriod = ALG_SBDEPDOC.DepDate_Document;
  var ErrCode, ErrText;
      SB_DREST.KeyNum = 0;
      SB_DREST.addFilter( "T_Referenc = " + String( Referenc ) + " and " +
                          "T_Type_Object = " + String( Object )           );
      SB_DREST.rec.Referenc   = Referenc;
      SB_DREST.rec.Type_Object = Object;
      SB_DREST.rec.Date_Rest   = DateEndPeriod;
      if (SB_DREST.GetEQ)
        SB_DREST.rec.Rest = 0;
        stat = SB_DREST.Update;
      else
        SB_DREST.rec.Referenc   = Referenc;
        SB_DREST.rec.Type_Object = Object;
        SB_DREST.rec.Date_Rest   = DateEndPeriod;
        SB_DREST.rec.Rest = 0;
        SB_DREST.rec.FNCash = NumFNcash();
        stat = SB_DREST.Insert;
      end;
      if (stat)
        SB_DREST.rec.Referenc   = Referenc;
        SB_DREST.rec.Type_Object = Object;
        SB_DREST.rec.Date_Rest   = DateEndPeriod + 1;
        st = SB_DREST.GetGE;
        while (stat AND st AND
        (SB_DREST.rec.Referenc == Referenc) AND
        (SB_DREST.rec.Type_Object == Object) )
          SB_DREST.rec.Rest = 0;
          stat = SB_DREST.Update;
          if (stat)
            st = SB_DREST.Next;
          end;
        end;
        if (NOT stat)
          ErrCode = Status(ErrText);
          if ((ErrCode == 4) OR (ErrCode == 9))
            stat = True;
          end;
        end;
      end;
      SB_DREST.dropFilter;
  return stat;
end;
/**********************************************************/

macro CorrectSbDrest (Referenc,IsCur,Sum)
  var stat = True;
  var st;
  var DateLastMon;
  var Rest;
  var day,mon,year, MinDate;
  var ErrCode,ErrText;

/*
  Правка sb_drest
*/
  DateLastMon = Последний_День_Предыдущего_Месяца(ALG_SBDEPDOC.DepDate_Document);
  SB_DREST.KeyNum = 0;
  SB_DREST.addFilter( "T_Referenc = " + String( Referenc ) + " and " +
                      "T_Type_Object = 2001"                          );
  SB_DREST.rec.Referenc   = Referenc;
  SB_DREST.rec.Type_Object = 2001;
  SB_DREST.rec.Date_Rest   = DateLastMon;
  if (SB_DREST.GetLE AND
      (SB_DREST.rec.Referenc == Referenc) AND
      (SB_DREST.rec.Type_Object == 2001) )
    if (SB_DREST.rec.Date_Rest < DateLastMon)
      SB_DREST.rec.Date_Rest = DateLastMon;
      SB_DREST.rec.FNCash = NumFNcash();
      stat = SB_DREST.Insert;
    end;
    if (stat AND (Sum != 0))
      SB_DREST.rec.Referenc   = Referenc;
      SB_DREST.rec.Type_Object = 2001;
      SB_DREST.rec.Date_Rest   = DateLastMon;
      st = SB_DREST.GetGE;
      while (stat AND st AND
      (SB_DREST.rec.Referenc == Referenc) AND
      (SB_DREST.rec.Type_Object == 2001) )
        if (SB_DREST.rec.Date_Rest < (ALG_SBDEPDOC.DepDate_Document-1))
          SB_DREST.rec.Rest = SB_DREST.rec.Rest + Sum;
          stat = SB_DREST.Update;
        end;
        if (stat)
          st = SB_DREST.Next;
        end;
      end;
    end;
  end;
  SB_DREST.dropFilter;
/*
  Правка в sb_depdoc
*/
  if (stat)
    SBDEPDOC.KeyNum = 2;
    SBDEPDOC.addFilter( "T_Referenc = " + String( Referenc ) );
    SBDEPDOC.Clear;
    SBDEPDOC.rec.Referenc = Referenc;
    MinDate = DateAfterCalenMonths(ALG_SBDEPDOC.DepDate_Document, -1);
    SBDEPDOC.rec.DepDate_Document = MinDate;
    st = SBDEPDOC.GetGE;
    while (stat AND st AND (SBDEPDOC.rec.Referenc == Referenc))
      if ( IsServDoc(SBDEPDOC.rec)
        /* VZ 08.04.99 Проверка на сторнирующие и сторнированные операции*/
         AND (SBDEPDOC.rec.FlagStorn == StrFor(0)) AND

           ((SBDEPDOC.rec.TypeOper == Открытие_Наличными) OR
            (SBDEPDOC.rec.TypeOper == Открытие_Безналичными) OR
            (SBDEPDOC.rec.TypeOper == Дополнительный_Взнос) OR
            (SBDEPDOC.rec.TypeOper == Зачисление_В_Пак_Режиме)))
        stat = Запись_суммы_на_альтернативный (Referenc,
         SBDEPDOC.rec.DepDate_Document, - SBDEPDOC.rec.InSum, 2001);
        if (stat)
          if (IsCur == 0)
            SBDEPDOC.rec.ObjectPerc = 0;
          else
            SBDEPDOC.rec.ObjectPerc = 2002;
            stat = Запись_суммы_на_альтернативный (Referenc,
             SBDEPDOC.rec.DepDate_Document, SBDEPDOC.rec.InSum, 2002);
          end;
        end;
        if (stat)
          stat = SBDEPDOC.Update;
        end;
      end;
      if (stat)
        st = SBDEPDOC.Next;
      end;
    end;
    SBDEPDOC.dropFilter;
/*
    Для валюты обнуляем остатки на альтернативном с даты закрытия
*/
    if (stat)
      stat = NullSumRest (Referenc,2001);
    end;
    if (stat AND (IsCur != 0))
      stat = NullSumRest (Referenc,2002);
    end;
  end;
  return stat;
end; /* of MACRO */