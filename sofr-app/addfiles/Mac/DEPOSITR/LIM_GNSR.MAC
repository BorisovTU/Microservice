/*

      R-Style SoftWare Lab.

      RS-Retail 2.1

      Отчет для Налоговой инспекции (ГНС) по операциям
      с превышением лимита ( $ 10,000 )

      Процедура формирования отчета.

      Часть для вызова при операциях.

*/

import CommonInter, BankInter, DeskInter, ExchangeInter, IsSrvDoc, Cur_Rate, commclnt, acc_form, rtkladr;

var clntL = TClientList;

file person   ( person,  "bank.def"   )  key 0;  /* Пользователи Системы */
file accnt    ( depositr              )  key 8;  /* Счета вкладчиков Банка */
file country  ( country, "rsrfdb.def" )  key 2;  /* Страны */
file currency ( currency              )  key 0;  /* Валюты */
file paprkind ( paprkind,"bank.def"   )  key 0;  /* Виды удостоверений личности */

file eAddition () txt 2048 write;

private const ENCODING = "rsansi";

const Client_Pol = 62;  /* Вид пользовательского поля: пол клиента */

const cUSD = 1;  /* Код базовой валюты (USD) */

/* Виды документов */
const cTypeDep = 1;  /* Документ по счету вкладчика */
const cTypePay = 2;  /* Платежный документ */
const cTypeExc = 3;  /* Документ Обменного пункта */

const OP_GETLPR = 64;  /* Списание по коммунальному платежу */

/* Виды платежей */
const
 CO_CommPay     =   1,  /* Коммунальные платежи */
 CO_NalPer      =  11,  /* Наличный перевод */
 CO_Comission   =  21,  /* Комиссия */
 CO_Conversion  =  31,  /* Конверсия */
 CO_Payed       =  41,  /* Выплата */
 CO_BankPayed   =  51,  /* Банковские расчеты */
 CO_OverCred    =  61,  /* Документ овердрафтного кредитования */
 CO_Trn         = 100,  /* Безналичные платежи */
 CO_Treasure    = 101;  /* Ценности */

/* Вид операции в pay_doc.dbt */
const
 CI_CREDIT = 1,  /* Приходная операция */
 C__DEBET  = 2;  /* Расходная операция */

/* Виды валютно-обменных операций */
const
 CDF_OperCurrBuy             =  6,   /* Покупка платежеспособной валюты за рубли */
 CDF_OperCurrSale            =  7,   /* Продажа платежеспособной валюты за рубли */
 CDF_OperPayDocBuy           =  3,   /* Покупка платежного документа за рубли  */
 CDF_OperPayDocSale          =  4,   /* Продажа платежного документа за рубли  */
 CDF_OperPayDocBuyCurr       = 20,   /* Покупка платежного документа за валюту */
 CDF_OperPayDocSaleCurr      = 21,   /* Продажа платежного документа за валюту */
 CDF_OperCurrConversion      = 18,   /* Конверсия */
 CDF_OperChange              = 23,   /* Замена неплатежной валюты */
 CDF_OperUnpayCurrBuy        =  1,   /* Покупка неплатежеспособной валюты */
 CDF_OperIncasso             = 16,   /* Прием валюты на инкассо */
 CDF_OperExpert              = 17,   /* Прием валюты на экспертизу */
 CDF_OperReturnIncasso       = 24,   /* Возврат валюты с инкассо */
 CDF_OperReturnExpert        = 25,   /* Возврат валюты с экспертизы */
 CDF_OperPayDocIncasso       = 27,   /* Прием ПД на инкассо */
 CDF_OperPayDocExpert        = 28,   /* Прием ПД на экспертизу */
 CDF_OperPayDocReturnIncasso = 29,   /* Возврат ПД с инкассо */
 CDF_OperPayDocReturnExpert  = 30,   /* Возврат ПД с экспертизы */
 CDF_OperPayDocBuyAcc        = 38,   /* Покупка ПД за рубли через счет   */
 CDF_OperPayDocSaleAcc       = 39,   /* Продажа ПД за рубли через счет   */
 CDF_OperPayDocBuyCurrAcc    = 40,   /* Покупка ПД за валюту через счет  */
 CDF_OperPayDocSaleCurrAcc   = 41;   /* Продажа ПД за валюту через счет  */


const
   FILESTR_BANK_NAME       =  0,
   FILESTR_BANK_ADDR       =  1,
   FILESTR_CLN_SNAME       =  2,
   FILESTR_CLN_NAME        =  3,
   FILESTR_CLN_PNAME       =  4,
   FILESTR_CLN_PERSIDTYPE  =  5,
   FILESTR_CLN_PERSIDSER   =  6,
   FILESTR_CLN_PERSIDNUM   =  7,
   FILESTR_CLN_PERSIDEXTRA =  8,
   FILESTR_CLN_PERSIDDATE  =  9,
   FILESTR_CLN_COUNTRY     = 10,
   FILESTR_CLN_ADRESS      = 11,
   FILESTR_CLN_AREGION     = 12,
   FILESTR_CLN_ADISTRICT   = 13,
   FILESTR_CLN_ACITY       = 14,
   FILESTR_CLN_APOINTNAME  = 15,
   FILESTR_CLN_ASTREETNAME = 16,
   FILESTR_CLN_AHB         = 17,
   FILESTR_CLN_AAPARTMENT  = 18,
   FILESTR_SUM             = 19,
   FILESTR_CUR             = 20,
   FILESTR_OPERATION       = 21,
   FILESTR_DATEOP          = 22,
   FILESTR_CURSELL         = 23,
   FILESTR_SUMSELL         = 24,
   FILESTR_CURBUY          = 25,
   FILESTR_SUMBUY          = 26,
   FILESTR_OPER            = 27,
   FILESTR_BOSS            = 28;

private var {curdate};                /* Текущая дата опердня */

private var {Name_Bank};
private var {Post_Addr};

var LimInSum  = $0.;          /* Сумма лимита для выбора документов прихода */
var LimOutSum = $0.;          /* Сумма лимита для выбора документов расхода */

var v_shrt, v_name, v_cop;
var Curr_Name = "";
var markFirst = FALSE;

var BankName_C = {Name_Bank},
    BankAddr_C = {Post_Addr};

var ErrCode;

array FileStr;

/*
   Получение названия страны
*/
macro getCountry ( code )

  var ret_name = "";

  ClearRecord( country );
  country.CodeLat3 = code;

  if ( GetEQ( country ) )
    ret_name = country.Name;
  else
    if ( code == "" )
      ret_name = "Россия";
    end;
  end;

  return ret_name;

end;

/*
   Вспомогательная процедура для getCurr()
*/
macro byWords( sum )
 var tmp, words;
 RubToStr( sum, words, tmp );
 return words
end;

/*
   Получение названия валюты
*/
macro getCurr( sum, code )
var
    wholePart    = byWords( sum ),
    wholeEmpty   = ( Strlen(wholePart) == 0 ),
    handredsEmpty,
    handredsPart,
    tmp;

 Curr_Name = v_name;

 if ( v_shrt == "RUR" )
   v_shrt = "руб.";
 end;

 RubToStr(sum,tmp,handredsPart);
 handredsEmpty = (StrLen(handredsPart)==0);
 if( wholeEmpty)    wholePart    = "Ноль"; end;
 if( handredsEmpty) handredsPart = "00";   end;

 return wholePart + " " + v_shrt + " " + handredsPart + " " + v_cop;
end;

/*
   Получение ФИО операциониста
*/
macro get_Oper( oper )
 var str;
 person.Oper = oper;
 if ( getEQ( person ) )
   str = person.Name;
 else
   str = "";
 end;
 return str;
end;

/*
  Получение вида удостоверения личности
*/
macro GetPaperKind (PapCode)
   var stat, Res;
   ClearRecord (paprkind);
   Rewind (paprkind);
   paprkind.PaperKind = PapCode;
   stat = GetEQ (paprkind);
   if (stat)
      Res = paprkind.Name;
   else
      Res = "";
   end;
   return Res;
end;

/*
  Находим ISO-код валюты
*/
macro FindIsoCodCurr( CodCurr )
  Rewind( currency );
  ClearRecord( currency );
  currency.Code_Currency = CodCurr;
  if ( GetEQ( currency ) )
    return currency.ExternalCode;
  else
    return "";
  end;
end;

/*
  Формирование строк текстового файла
*/
macro MakeTxtStr( Arr )
   const Delim = ";";

   var i   = 0;
   var Str = "";
   var AddStr = "";
   while( i < Asize( Arr ) )
      AddStr = String( Arr( i ) );
      if ( AddStr == "Undefined")
         AddStr = "";
      end;
      Str = Str + AddStr + Delim;
      i = i + 1;
   end;
   return Str;
end;

/*
  Образовать текстовый файл для форматного вывода
*/
macro MakeFile ()
   var eAdditionName = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true) + "eAdd9.txt";
   return (Open (eAddition, eAdditionName, ENCODING));
end;

/*
   Нахождение эквивалента суммы в долларах
*/
macro EQU_USD( curr, sum )
 var CBRateUSD, BuyRateUSD, SellRateUSD;
 var CBRateVal, BuyRateVal, SellRateVal;
 var equ;
 if ( curr == cUSD )  /* USD */
   equ = sum / $1.0;
 else
   if ( NOT GetAllCurrRate( {curdate}, 1,
                             CBRateUSD, BuyRateUSD, SellRateUSD ) )
       MsgBox("Ошибка при определении курса USD");
       Exit(1);
   end;

   if ( curr == 0 )
     equ = sum / ( CBRateUSD * 100 );
   else
     if ( NOT GetAllCurrRate( {curdate}, curr,
                               CBRateVal, BuyRateVal, SellRateVal ) )
         MsgBox("Ошибка при определении курса валюты=", curr);
         Exit(1);
     end;

     equ = sum * CBRateVal / ( CBRateUSD * 100 );
   end;
 end;
 return equ;
end;

/*
   Название операции для документов из sbdepdoc.dbt
*/
macro OperationName( p_Sum )
 var str = "8. Вид операции: ";
 if ( p_Sum > $0 )
   str = str + "зачисление средств на счет по вкладу физического лица " +
               "либо любая другая операция по поступлению денежных средств " +
               "в пользу физического лица";
 else
   str = str + "снятие средств со счета по вкладу лица";
 end;
 return str;
end;

/*
   Поиск кода клиента для документа вкладчика
*/
macro getCodClient( p_Referenc, p_Account, p_CodClient )
  var code = 0;
  ClearRecord( accnt );
  accnt.Referenc = p_Referenc;
  if ( getEQ( accnt ) )
    code = accnt.CodClient;
  else
    MsgBox( " Счет ", Acc_Form(p_Account), " не найден." );
    code = p_CodClient;
  end;
  return code;
end;

/*
   Наименование платежа для документов из pay_doc.dbt
*/
macro PayName( dc )
 var str = "8. Вид операции: ";
 if ( dc == CI_CREDIT )
   str = str + "зачисление средств на счет по вкладу физического лица " +
               "либо любая другая операция по поступлению денежных средств " +
               "в пользу физического лица";
 else
   str = str + "снятие средств со счета по вкладу лица";
 end;
 return str;
end;

/*
   Наименование валютно-обменных операций
*/
macro ExchName( Kind_Oper )
 var name;
 name = "8. Вид операции: Валютно-обменная операция: ";
 if   ( Kind_Oper == CDF_OperCurrBuy        )
   name = name + "Покупка платежеспособной валюты за рубли";
 elif ( Kind_Oper == CDF_OperCurrSale       )
   name = name + "Продажа платежеспособной валюты за рубли";
 elif ( Kind_Oper == CDF_OperPayDocBuy      )
   name = name + "Покупка платежного документа за рубли";
 elif ( Kind_Oper == CDF_OperPayDocSale     )
   name = name + "Продажа платежного документа за рубли";
 elif ( Kind_Oper == CDF_OperPayDocBuyCurr  )
   name = name + "Покупка платежного документа за валюту";
 elif ( Kind_Oper == CDF_OperPayDocSaleCurr )
   name = name + "Продажа платежного документа за валюту";
 elif ( Kind_Oper == CDF_OperCurrConversion )
   name = name + "Конверсия валюты";
 elif ( Kind_Oper == CDF_OperChange )
   name = name + "Замена неплатежеспособной валюты";
 elif ( Kind_Oper == CDF_OperUnpayCurrBuy )
   name = name + "Покупка неплатежеспособной валюты за рубли";
 elif ( Kind_Oper == CDF_OperIncasso )
   name = name + "Прием иностранной валюты на инкассо";
 elif ( Kind_Oper == CDF_OperExpert )
   name = name + "Прием иностранной валюты на экспертизу";
 elif ( Kind_Oper == CDF_OperReturnIncasso )
   name = name + "Возврат иностранной валюты с инкассо";
 elif ( Kind_Oper == CDF_OperReturnExpert )
   name = name + "Возврат иностранной валюты с экспертизы";
 elif ( Kind_Oper == CDF_OperPayDocIncasso )
   name = name + "Прием платежного документа на инкассо";
 elif ( Kind_Oper == CDF_OperPayDocExpert )
   name = name + "Прием платежного документа на экспертизу";
 elif ( Kind_Oper == CDF_OperPayDocReturnIncasso )
   name = name + "Возврат платежного документа с инкассо";
 elif ( Kind_Oper == CDF_OperPayDocReturnExpert )
   name = name + "Возврат платежного документа с экспертизы";
 elif ( Kind_Oper == CDF_OperPayDocBuyAcc )
   name = name + "Покупка платежного документа за рубли через счет";
 elif ( Kind_Oper == CDF_OperPayDocSaleAcc )
   name = name + "Продажа платежного документа за рубли через счет";
 elif ( Kind_Oper == CDF_OperPayDocBuyCurrAcc )
   name = name + "Покупка платежного документа за валюту через счет";
 elif ( Kind_Oper == CDF_OperPayDocSaleCurrAcc )
   name = name + "Продажа платежного документа за валюту через счет";
 end;
 return name;
end;

/*
   Нахождение эквивалента суммы в долларах для валютно-обменных операций
*/
macro EQU_EXC( curr, sum )
 var CBRateUSD, BuyRateUSD, SellRateUSD;
 var CBRateVal, BuyRateVal, SellRateVal;
 var equ;
 if ( curr == 1 )  /* USD */
   equ = sum / $1.0;
 else
   if ( NOT GetAllCurrRate( {curdate}, 1,
                             CBRateUSD, BuyRateUSD, SellRateUSD ) )
       MsgBox("Ошибка при определении курса USD");
       Exit(1);
   end;

   if ( curr == 0 )
     equ = sum / ( BuyRateUSD * 100 );
   else
     if ( NOT GetAllCurrRate( {curdate}, curr,
                               CBRateVal, BuyRateVal, SellRateVal ) )
         MsgBox("Ошибка при определении курса валюты=", curr);
         Exit(1);
     end;

     equ = sum * SellRateVal / ( BuyRateUSD * 100 );
   end;
 end;
 return equ;
end;

/********************************************************************
   Формирование отчета для Налоговой инспекции
*********************************************************************/
macro FiscalReport(
                   type,
                   client,
                   sum,
                   curr,
                   operation,
                   sumSell,
                   currSell,
                   sumBuy,
                   currBuy,
                   oper
                  )

 var clntFIO, clntPasp, clntCount, clntAddr;
 var sSumSell, sCurrSell, sSumBuy, sCurrBuy;
 var iSumOper, sSumOper;
 var StatIns;
 var Pol_of_Client;

 if ( clntL.GetRecord( client ) )
   clntFIO = clntL.CurRec.rec.Name1 + " " +
             clntL.CurRec.rec.Name2 + " " +
             clntL.CurRec.rec.Name3;

   /* Чтение пользовательского поля "Пол клиента" */
   Pol_of_Client = clntL.CurRec.rec.IsMale;  /* Пол клиента */
   if (Pol_of_Client != StrFor(88) )
     clntFIO = clntFIO + " (пол: женский)";
   else
     clntFIO = clntFIO + " (пол: мужской)";
   end;
   clntPasp  = clntL.CurRec.MakeDocStr();
   if ( clntL.CurRec.rec.NotResident == "" )
     clntCount = "";
     clntAddr  = clntL.CurRec.MakeFullStrAddress();
   else
     clntAddr = "";
     clntCount = getCountry( clntL.CurRec.rec.Country );
     clntL.AdressType = COMMCLNT_PTADDR_REAL;
     if (clntL.AdressType != COMMCLNT_PTADDR_REAL)
       clntL.AdressType = COMMCLNT_PTADDR_POST;
     end;
     if (clntAddr != COMMCLNT_NO_DEFINED_ADRESS_TYPE)
//       clntAddr  = clntL.CurRec.MakeFullStrAddress(); // Можно только для Единого клиента
       if (Trim(clntAddr) == "")
         clntAddr = clntL.CurRec.rec.Address;
       end;
     end;
   end;
 else
   clntFIO   = "";
   clntPasp  = "";
   clntCount = "";
   clntAddr  = "";
 end;

 /* Атрибуты продажи и покупки валюты */
 if ( sumSell == $0 )
   sSumSell  = "";
   sCurrSell = "";
 else
   sSumSell  = String(sumSell:a:0:2) + " " + byWords(sumSell);
   sCurrSell = GetNameCurr( FindCodCurByRefVal( currSell ) );
 end;
 if ( sumBuy == $0 )
   sSumBuy  = "";
   sCurrBuy = "";
 else
   sSumBuy  = String(sumBuy:a:0:2) + " " + byWords(sumBuy);
   sCurrBuy = GetNameCurr( FindCodCurByRefVal( currBuy ) );
 end;

 if ( type == 0 )
   Get_Currency( FindCodCurByRefVal( curr ), v_shrt, v_name, v_cop );
   v_shrt = GetNameCurr( FindCodCurByRefVal( curr ) );
   Curr_Name = v_shrt;
 else
   Get_Currency( curr, v_shrt, v_name, v_cop );
 end;

 if ( sum == $0 )
   sSumOper = "";
   iSumOper = "";
 else
   sSumOper = getCurr( sum, curr );
   iSumOper = String(sum:a:0:2) + " " + v_shrt;
 end;

 if ( markFirst )
   [ ];
   [ ];
   [------------------------------------------------------------------------];
   [ ];
 else
   markFirst = TRUE;
 end;

 [
                                                           Приложение N 9
                                           к Порядку применения положений
                                    Указа Президента Российской Федерации
                                                 от 23 мая 1994 г. N 1006

                                   СВЕДЕНИЯ
                  ОБ ОПЕРАЦИИ, СВЯЗАННОЙ С ДВИЖЕНИЕМ ДЕНЕЖНЫХ
               СРЕДСТВ ФИЗИЧЕСКОГО ЛИЦА, НА СУММУ ЭКВИВАЛЕНТНУЮ
                          10 ТЫС. ДОЛЛАРОВ США И ВЫШЕ

       1. Наименование банка (кредитного учреждения)
       ####################################################################]
 ( BankName_C:w );
 [     2. Почтовый индекс, адрес, телефон банка (кредитного учреждения)
       ####################################################################
       ____________________________________________________________________]
  ( BankAddr_C:w );
 [     3. Физическое лицо #________________________________________________
                                 (фамилия, имя, отчество, пол)            ]
 ( clntFIO );
 [     4. Документ, удостоверяющий личность физического лица ______________
       ####################################################################
       ____________________________________________________________________
                  (вид, серия и номер документа, кем и когда выдан)       ]
 ( clntPasp:w );
 [     5. Страна постоянного проживания нерезидента #______________________ ]
 ( clntCount );
 [     6. Адрес прописки резидента или адрес проживания нерезидента _______
       ####################################################################]
 ( clntAddr:w );

 [     7. Сумма операции в валюте счета #__________________________________
       ####################################################################
                                         (сумма цифрами и прописью,]
 ( iSumOper, sSumOper:w );
 if ( type == 0 )
   Curr_Name = " ";
 end;
 [     #___________________________________________________________________
                              наименование валюты)

            Операция, связанная с движением средств физического лица

       ####################################################################
       ____________________________________________________________________
       9. Дата операции #
 ]( Curr_Name, operation:w, {curdate}:m );
 [
                  Валютно - обменная операция физического лица

       10. Наименование  иностранной  или национальной валюты,  проданной
       физическому лицу #__________________________________________________ ]
 ( sCurrSell );
 [     11. Сумма этой валюты #_____________________________________________
                                     (сумма цифрами и прописью)             ]
 ( sSumSell );
 [     12. Наименование иностранной или национальной валюты,  купленной у
       физического лица #__________________________________________________ ]
 ( sCurrBuy );
 [     13. Сумма этой валюты #_____________________________________________
                                     (сумма цифрами и прописью)             ]
 ( sSumBuy );
 [
       М.П.    ____________________________________   (#
               (должность работника банка) (подпись)  (фамилия, инициалы)

 ]( get_Oper( oper )+")" );

   /*
   MsgBox ("currSell ", currSell);
   MsgBox ("sumSell  ", sumSell );
   MsgBox ("currBuy  ", currBuy );
   MsgBox ("sumBuy   ", sumBuy  ); */

   if (SubStr (operation, 1, 2) == "8.")
      operation = SubStr (operation, 4);
   end;
   FileStr (FILESTR_BANK_NAME)       = BankName_C;
   FileStr (FILESTR_BANK_ADDR)       = BankAddr_C;
   FileStr (FILESTR_CLN_SNAME)       = clntL.CurRec.rec.Name1;
   FileStr (FILESTR_CLN_NAME)        = clntL.CurRec.rec.Name2;
   FileStr (FILESTR_CLN_PNAME)       = clntL.CurRec.rec.Name3;
   FileStr (FILESTR_CLN_PERSIDTYPE)  = GetPaperKind( clntL.CurRec.rec.PaperKind );
   FileStr (FILESTR_CLN_PERSIDSER)   = clntL.CurRec.rec.PaperSeries;
   FileStr (FILESTR_CLN_PERSIDNUM)   = clntL.CurRec.rec.PaperNumber;
   FileStr (FILESTR_CLN_PERSIDEXTRA) = clntL.CurRec.rec.PaperIssuer;
   FileStr (FILESTR_CLN_PERSIDDATE)  = "";
   FileStr (FILESTR_CLN_COUNTRY)     = clntCount;
   FileStr (FILESTR_CLN_ADRESS)      = clntAddr;
   FileStr (FILESTR_CLN_AREGION)     = getRegionNumber( clntL.CurRec );
   FileStr (FILESTR_CLN_ADISTRICT)   = clntL.CurRec.rec.District;
/*   FileStr (FILESTR_CLN_ACITY)       = clntL.CurRec.rec.City; */
   FileStr (FILESTR_CLN_APOINTNAME)  = clntL.CurRec.rec.Place;
   FileStr (FILESTR_CLN_ASTREETNAME) = clntL.CurRec.rec.StreetName;
   FileStr (FILESTR_CLN_AHB)         = clntL.CurRec.rec.House + "-" +
                                       clntL.CurRec.rec.NumCorp;
   FileStr (FILESTR_CLN_AAPARTMENT)  = clntL.CurRec.rec.Flat;
   FileStr (FILESTR_SUM)             = sum;
   FileStr (FILESTR_CUR)             = FindIsoCodCurr (curr);
   FileStr (FILESTR_OPERATION)       = operation;
   FileStr (FILESTR_DATEOP)          = {curdate};
   FileStr (FILESTR_CURSELL)         = FindIsoCodCurr (currSell - 1);
   FileStr (FILESTR_SUMSELL)         = sumSell;
   FileStr (FILESTR_CURBUY)          = FindIsoCodCurr (currBuy - 1);
   FileStr (FILESTR_SUMBUY)          = sumBuy;
   FileStr (FILESTR_OPER)            = get_Oper( oper );
   FileStr (FILESTR_BOSS)            = "";

   eAddition.Str = MakeTxtStr (FileStr);
   StatIns = insert (eAddition);
end;

/*
  Процедура для вызова из операций
*/
macro LimGNIOpert( type, addr )

record deprec ( "sbdepdoc.dbt" );   /* Запись документа по счету вкладчика */
record payrec ( "pay_doc.dbt"  );   /* Запись платежного документа */
record excrec ( exoperat, "exchange.def" );  /* Валюто-обменный документ */

var RegPathIn  = "RS-RETAIL\\СЕРВИСНЫЕ\\ЛИМИТЫ\\ЛИМИТЫ_ГНС\\ПРИХОД_НА_Ф-Л_(USD)";
var RegPathOut = "RS-RETAIL\\СЕРВИСНЫЕ\\ЛИМИТЫ\\ЛИМИТЫ_ГНС\\РАСХОД_Ф-Л_(USD)";
var Value;
var PrintFlag = FALSE;

/* Открытие справочника документов (RS-Bank) */
/*if ( RSDir == "" )
  RSDir = GetIniString ("DATABASEDIR");
end;*/

/* Определение суммы лимита для выбора документов */
if ( GetRegistryValue( RegPathIn, V_INTEGER, Value, ErrCode ) == V_INTEGER )
  LimInSum = $1. * Value;
else
  LimInSum = $10000.;
end;
if ( GetRegistryValue( RegPathOut, V_INTEGER, Value, ErrCode ) == V_INTEGER )
  LimOutSum = $1. * Value;
else
  LimOutSum = $10000.;
end;

/* Открытие справочника стран */
open( country, GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\DISTDBFILE", true) + "country.dbt" );

MakeFile();
if ( type == cTypeDep )
  /* Документ по счету вкладчика */
  SetBuff( deprec, addr );

  if ( ( deprec.TypeOper != OP_GETLPR )  AND  /* Кроме коммунальных платежей */
       IsServDoc( deprec )               AND
       ( ( $1. * EQU_USD( deprec.Code_Currency, deprec.InSum  ) >= LimInSum )  OR
         ( $1. * EQU_USD( deprec.Code_Currency, deprec.OutSum ) >= LimOutSum ) ) )
    if ( $1. * EQU_USD( deprec.Code_Currency, deprec.InSum  ) >= LimInSum )
      FiscalReport( 1,
            getCodClient( deprec.Referenc, deprec.Account, deprec.CodClient ),
            deprec.InSum, deprec.Code_Currency,
            OperationName( deprec.InSum ), $0, 0, $0, 0, deprec.Oper );
      PrintFlag = TRUE;
    end;
    if ( $1. * EQU_USD( deprec.Code_Currency, deprec.OutSum ) >= LimOutSum )
      FiscalReport( 1,
            getCodClient( deprec.Referenc, deprec.Account, deprec.CodClient ),
            deprec.OutSum, deprec.Code_Currency,
            OperationName( deprec.InSum ), $0, 0, $0, 0, deprec.Oper );
      PrintFlag = TRUE;
    end;
  end;
elif ( type == cTypePay )
  /* Платежный документ */
  SetBuff( payrec, addr );

  if ( ( payrec.GroupOpert != CO_Comission )  AND  /* Комиссия */
       ( payrec.GroupOpert != CO_BankPayed )  AND  /* Банковские расчеты */
       ( payrec.GroupOpert != CO_Trn       )  AND  /* Безналичные платежи */
       ( payrec.GroupOpert != CO_Treasure  )  AND  /* Ценности */
       ( payrec.VidDoc == StrFor( 0 )      )  AND  /* Наличный платеж */
       ( $1. * EQU_USD( payrec.CodCur, payrec.InSum ) >= LimInSum ) )
    FiscalReport( 1, payrec.CodClient, payrec.InSum, payrec.CodCur,
                  PayName( payrec.DebetCredit ), $0, 0, $0, 0, payrec.Oper );
    PrintFlag = TRUE;
  end;
else
  /* Документ Обменного пункта */
  SetBuff( excrec, addr );
  if ( (excrec.Kind_Oper == CDF_OperCurrBuy          )  OR  /* Покупка платежеспособной валюты за рубли */
       (excrec.Kind_Oper == CDF_OperPayDocBuy        )  OR  /* Покупка платежного документа за рубли  */
       (excrec.Kind_Oper == CDF_OperPayDocBuyCurr    )  OR  /* Покупка платежного документа за валюту */
       (excrec.Kind_Oper == CDF_OperPayDocBuyAcc     )  OR  /* Покупка платежного документа за рубли через счет  */
       (excrec.Kind_Oper == CDF_OperPayDocBuyCurrAcc )  OR  /* Покупка платежного документа за валюту через счет */
       (excrec.Kind_Oper == CDF_OperUnpayCurrBuy     )  OR  /* Покупка неплатежеспособной валюты за рубли */
       (excrec.Kind_Oper == CDF_OperIncasso          )  OR  /* Прием валюты на инкассо */
       (excrec.Kind_Oper == CDF_OperExpert           )  OR  /* Прием валюты на экспертизу */
       (excrec.Kind_Oper == CDF_OperPayDocIncasso    )  OR  /* Прием ПД на инкассо */
       (excrec.Kind_Oper == CDF_OperPayDocExpert     )      /* Прием ПД на экспертизу */
     )
    if   ( $1. * EQU_EXC( FindCodCurByRefVal(excrec.IncomeRefVal),
                          excrec.IncomeAmount ) >= LimInSum )
      FiscalReport( 0, excrec.ClientCode, $0, excrec.IncomeRefVal,
                    ExchName( excrec.Kind_Oper ),
                    excrec.OutAmount, excrec.OutRefVal,
                    excrec.IncomeAmount, excrec.IncomeRefVal,
                    excrec.Oper );
      PrintFlag = TRUE;
    end;
  end;
  if ( (excrec.Kind_Oper == CDF_OperCurrSale            )  OR  /* Продажа платежеспособной валюты за рубли */
       (excrec.Kind_Oper == CDF_OperPayDocSale          )  OR  /* Продажа платежного документа за рубли  */
       (excrec.Kind_Oper == CDF_OperPayDocSaleCurr      )  OR  /* Продажа платежного документа за валюту */
       (excrec.Kind_Oper == CDF_OperPayDocSaleAcc       )  OR  /* Продажа платежного документа за рубли через счет */
       (excrec.Kind_Oper == CDF_OperPayDocSaleCurrAcc   )  OR  /* Продажа платежного документа за валюту через счет*/
       (excrec.Kind_Oper == CDF_OperReturnIncasso       )  OR  /* Возврат валюты с инкассо */
       (excrec.Kind_Oper == CDF_OperReturnExpert        )  OR  /* Возврат валюты с экспертизы */
       (excrec.Kind_Oper == CDF_OperPayDocReturnIncasso )  OR  /* Возврат ПД с инкассо */
       (excrec.Kind_Oper == CDF_OperPayDocReturnExpert  )      /* Возврат ПД с экспертизы */
     )
    if ( $1. * EQU_EXC( FindCodCurByRefVal(excrec.OutRefVal), excrec.OutAmount ) >= LimOutSum )
      FiscalReport( 0, excrec.ClientCode, $0, excrec.OutRefVal,
                    ExchName( excrec.Kind_Oper ),
                    excrec.OutAmount, excrec.OutRefVal,
                    excrec.IncomeAmount, excrec.IncomeRefVal,
                    excrec.Oper );
      PrintFlag = TRUE;
    end;
  end;
  if( ( excrec.Kind_Oper == CDF_OperCurrConversion )  OR /* Конверсия */
      ( excrec.Kind_Oper == CDF_OperChange         )     /* Замена неплатежной валюты */
    )
    if( $1. * EQU_EXC( FindCodCurByRefVal(excrec.OutRefVal), excrec.OutAmount ) >= LimOutSum )
      FiscalReport( 0, excrec.ClientCode, $0, excrec.OutRefVal,
                    ExchName( excrec.Kind_Oper ),
                    excrec.OutAmount, excrec.OutRefVal,
                    excrec.IncomeAmount, excrec.IncomeRefVal,
                    excrec.Oper );
      PrintFlag = TRUE;
    elif( $1. * EQU_EXC( FindCodCurByRefVal(excrec.IncomeRefVal), excrec.IncomeAmount ) >= LimInSum  )
      FiscalReport( 0, excrec.ClientCode, $0, excrec.IncomeRefVal,
                    ExchName( excrec.Kind_Oper ),
                    excrec.OutAmount, excrec.OutRefVal,
                    excrec.IncomeAmount, excrec.IncomeRefVal,
                    excrec.Oper );
      PrintFlag = TRUE;
    end;
  end;
end;

if ( NOT PrintFlag )
  Exit( 1 );  /* Не показывать пустой экран */
end;

end;
