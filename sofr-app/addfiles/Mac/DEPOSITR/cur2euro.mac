/*
$Name:           cur2euro.mac
$Module:         RETAIL
$Description:    cur2euro
*/

/* (c) demaio 2001 */
import deprintr,alg_vars,opercode;
private file dr(depositr) key 0 write;
private file cr(currency) key 0;
private file dd(sbdepdoc) key 6 write;
private file dt(sb_dtyp) key 2 write;
private file ai(auxinfo) key 0 write;
private file pc(pc__calc) key 0 write;
private file pd(pc_drest) key 0 write;
private file pe(pcestim) key 0 write;
private file dh(dephistr) key 0 write;
private file ar(accreg) key 1 write;
private var cl = TClientList;
var ddep = TRecHandler( "i_dp_dep.rec" );
record ai_prc("auxinfo.1");
record tmpDoc("sbdepdoc.1");
record StatParm("StatParm.rec");
const
  UPDATE_ALL=1,
  UPDATE_DEP=2,
  UPDATE_DOC=4;
const
  D_IN=1,
  D_OUT=2,
  D_GET=3,
  D_PUT=5;
private const NullDate=Date(0,0,0);
var TrnStatus=true;
var DateX=NullDate;
var Rate=0.0L;
var SumPcEstim=$0.0L;
var OperBrigade=GetOperBrigade;
var AppKind;
var EURO_Code_Currency=0;
private var {MFO_Bank};
array Rates;
Rates(0)="ATS13.7603";
Rates(1)="BEF40.3399";
Rates(2)="GRD340.750";
Rates(3)="IEP0.787564";
Rates(4)="ESP166.386";
Rates(5)="ITL1936.27";
Rates(6)="LUF40.3399";
Rates(7)="DEM1.95583";
Rates(8)="NLG2.20371";
Rates(9)="PTE200.482";
Rates(10)="FIM5.94573";
Rates(11)="FRF6.55957";
macro DefRezForAccount
var
  NonRezident=0;
  if(cl.CurRec.rec.NotResident!="")
    NonRezident=1;
  end;
  return NonRezident;
end;
macro FindDepType
  dt.FlagCur=ALG_DEPOSITOR.IsCur;
  dt.Kind=ALG_DEPOSITOR.Type_Account;
  return GetEQ(dt);
end;
macro GetPcCalcForDate(operdate)
var
  retdate=NullDate,ObjectType=2001;
  if(ALG_DEPOSITOR.UseAlternate)
    ObjectType=2002;
  end;
  pc.Referenc=ALG_DEPOSITOR.Referenc;
  pc.ObjectType=ObjectType;
  pc.TypeRecord=1;
  pc.EndDate=operdate;
  if(GetGE(pc)
     and (pc.Referenc==ALG_DEPOSITOR.Referenc)
     and (pc.ObjectType==ObjectType) and (pc.TypeRecord==1))
    retdate=pc.BeginDate;
  end;
  return retdate;
end;
macro GetRate
var
  i=0;
  cr.Code_Currency=ALG_DEPOSITOR.Code_Currency;
  if(GetEQ(cr))
    while(i<ASize(Rates))
      if(SubStr(Rates(i),1,3)==cr.Short_Name)
        Rate=Double(SubStr(Rates(i),4));
        return true;
      end;
      i=i+1;
    end;
  end;
  return false;
end;
macro GetEUROcodE
var
  stat=false;
  KeyNum(cr,1);
  cr.ExternalCode=978;
  stat=GetEQ(cr);
  if(stat)
    EURO_Code_Currency=cr.Code_Currency;
  end;
  return stat;
end;
macro GetDateX
var
  retdate=NullDate,tmpdate;
  if(dt.FormContr and not ALG_DEPOSITOR.UseAlternate)
    GetProlongDateForCurDate(ALG_DEPOSITOR.Referenc, {curdate}, retdate);
  else
    retdate=GetPcCalcForDate({curdate});
  end;
  tmpdate=ALG_DEPOSITOR.Open_Date+1;
  if(retdate==tmpdate)
    retdate=ALG_DEPOSITOR.Open_Date;
  end;
  if(retdate==NullDate)
    if(ALG_DEPOSITOR.Limit_Date==NullDate)
      retdate=ALG_DEPOSITOR.Limit_Date;
    else
      retdate=ALG_DEPOSITOR.DateBeginPerc;
    end;
  end;
  if((ALG_DEPOSITOR.Limit_Date!=NullDate)
     and (retdate<=ALG_DEPOSITOR.Limit_Date))
    retdate=ALG_DEPOSITOR.Limit_Date+1;
  end;
  return retdate;
end;
macro UpdatePCDrest
var
  stat,res=true;
  pd.Referenc=ALG_DEPOSITOR.Referenc;
  pd.Type_Object=2001;
  pd.Date_Rest=DateX;
  stat=GetLE(pd);
  while(stat and (pd.Referenc==ALG_DEPOSITOR.Referenc))
    pd.Rest=pd.Rest/Rate;
    pd.NumSession=0;
    res=Update(pd);
    if(res)
      stat=Next(pd);
    else
      stat=res;
    end;
  end;
  return res;
end;
macro UpdatePCCalc
var
  stat,res=true;
  pc.Referenc=ALG_DEPOSITOR.Referenc;
  pc.ObjectType=2001;
  pc.TypeRecord=1;/*0 - ?*/
  pc.EndDate=DateX;
  stat=GetGE(pc);
  while(stat and (pc.Referenc==ALG_DEPOSITOR.Referenc))
    pc.SumCalc=pc.SumCalc/Rate;
    pc.SumPay=pc.SumPay/Rate;
    pc.SumForPay=pc.SumForPay/Rate;
    pc.SumPayAll=pc.SumPayAll/Rate;
    pc.TaxSumCalc=pc.TaxSumCalc/Rate;
    pc.TaxSumPay=pc.TaxSumPay/Rate;
    pc.TaxSumForPay=pc.TaxSumForPay/Rate;
    pc.NumSession=0;
    res=Update(pc);
    if(res)
      stat=Next(pc);
    else
      stat=res;
    end;
  end;
  return res;
end;
/**************************************************************************\
|           Поиск последнего документа по накопленным процентам            |
\**************************************************************************/
macro GetLastPcEstim

  var stat;
  var find_last = FALSE;

  pe.Referenc   = ALG_DEPOSITOR.Referenc;
  pe.DateOperat = Date(31,12,9999);
  pe.NumDayDoc  = 32000;
  stat = GetLE(pe);
  while (stat)
    if (pe.Referenc != ALG_DEPOSITOR.Referenc)
      stat = FALSE;
    else
      if (pe.Action < 2)
        find_last = TRUE;
        stat = FALSE;
      else
        stat = Prev(pe);
      end;
    end;
  end;

  return find_last;
  
end;

macro UpdatePCEstim
var
  res=true;

  SumPcEstim=$0L;

  if (GetLastPcEstim())
    SumPcEstim = pe.SummaPay_Rest;
    pe.SummaProgn    = pe.SummaProgn/Rate;
    pe.SummaCalc     = pe.SummaCalc/Rate;
    pe.SummaPay_Rest = pe.SummaPay_Rest/Rate;
    pe.SummaPay_97   = pe.SummaPay_97/Rate;
    pe.SummaPay_In   = pe.SummaPay_In/Rate;
    pe.SummaPay_Out  = pe.SummaPay_Out/Rate;
    pe.NumSession    = 0;
    res=Update(pe);
  end;

  return res;
end;
macro UpdateAccReg(PrevNumber)
var
  res=true;
  ar.IsCur=ALG_DEPOSITOR.IsCur;
  ar.FNCash=ALG_DEPOSITOR.FNCash;
  ar.GroupNumb=ALG_DEPOSITOR.GroupNumb;
  ar.Code_Currency=ALG_DEPOSITOR.Code_Currency;
  ar.Number=PrevNumber;
  if(GetEQ(ar))
    ar.Remark="Перевод в ЕВРО. Новый номер: "+Acc_Form(ALG_DEPOSITOR.Account);
    res=Update(ar);
  end;
  return res;
end;
macro UpdateStatistics(oldCurr)
var
  stat=true;
  ClearRecord(StatParm);
  StatParm.Op=2;
  StatParm.Sum=0;
  StatParm.KindOp=2;
  StatParm.EstimOverOut=SumPcEstim;
  StatParm.VidDoc=0;
  StatParm.CloseFlag=StrFor(1);
  StatParm.Type_Account=ALG_DEPOSITOR.Type_Account;
  StatParm.CodCur=oldCurr;
  StatParm.Date={curdate};
  StatParm.IsCur=1;
  StatParm.FlagRez=cl.CurRec.rec.NotResident;
  StatParm.Mode=0;
  StatParm.ModeFlag=StrFor(1);
  if(UpdateDepStats(StatParm)==0)
    stat = FALSE;
  else
    stat = TRUE;
  end;
  if(stat)
    StatParm.KindOp=1;
    StatParm.Sum=0;
    StatParm.EstimOverOut=0;
    StatParm.EstimIn=SumPcEstim/Rate;
    StatParm.VidDoc=0;
    StatParm.OpenFlag=StrFor(1);
    StatParm.KindOp=1;
    StatParm.Type_Account=ALG_DEPOSITOR.Type_Account;
    StatParm.CodCur=EURO_Code_Currency;
    StatParm.Date={curdate};
    StatParm.IsCur=1;
    StatParm.FlagRez=cl.CurRec.rec.NotResident;
    StatParm.Mode=0;
    StatParm.ModeFlag=StrFor(1);
    StatParm.PutOper=StrFor(1);
    if(UpdateDepStats(StatParm)==0)
      stat = FALSE;
    else
      stat = TRUE;
    end;
  end;
  return stat;
end;
macro CreateDepHistory(PrevNumber)
  ClearRecord(dh);
  dh.PrevNumber=PrevNumber;
  dh.Number=ALG_DEPOSITOR.Number;
  dh.PrevNumGroup=ALG_DEPOSITOR.GroupNumb;
  dh.NumGroup=ALG_DEPOSITOR.GroupNumb;
  dh.PrevAccount=ALG_DEPOSITOR.PrevAccount;
  dh.Account=ALG_DEPOSITOR.Account;
  dh.PrevBranchName=ddep.rec.Name;
  dh.BranchName=ddep.rec.Name;
  dh.PrevBranch=ALG_DEPOSITOR.FNCash;
  dh.CurCode=ALG_DEPOSITOR.Code_Currency;
  dh.Type_Account=ALG_DEPOSITOR.Type_Account;
  dh.RenumDate={curdate};
  dh.BalAcc=SubStr(ALG_DEPOSITOR.Account,1,5);
  return Insert(dh);
end;
macro CreatePcEstimDocs
var
  stat=false;
  ClearRecord(dd);
  dd.FNCash=ALG_DEPOSITOR.FNCash;
  dd.Referenc=-ALG_DEPOSITOR.Referenc;
  dd.IsCur=ALG_DEPOSITOR.IsCur;
  dd.Code_Currency=ALG_DEPOSITOR.Code_Currency;
  dd.CodClient=ALG_DEPOSITOR.CodClient;
  dd.Account=ALG_DEPOSITOR.Account;
  dd.Type_Account=ALG_DEPOSITOR.Type_Account;
  dd.FlagRezid=cl.CurRec.rec.NotResident;
  dd.TypeOper=39;
  dd.TypeComplexOper=38;
  dd.ApplType=49;
  dd.VidDoc=0;
  dd.KindOp=0;
  dd.iApplicationKind=AppKind;
  dd.ApplicationKey=FormApplicationKey(AppKind);
  dd.InSum=$0;
  dd.OutSum=SumPcEstim;
  dd.PercOprSum=$0;
  dd.Rest=$0;
  dd.PercRest=$0;
  dd.oper={oper};
  dd.Brigade=OperBrigade;
  dd.ObjectPerc=2001;
  dd.Ground="Списание накопл.% при переводе в ЕВРО";
  dd.DepDate_Document={curdate};
  dd.Date_Document={curdate};
  dd.NumDayDoc=32000;
  stat=Insert(dd);
  if(stat)
    stat=CashLink_AddRec(AppKind,dd.ApplicationKey);
  end;
  if(stat)
    dd.TypeOper=38;
    dd.ApplType=49;
    dd.KindOp=0;
    dd.Code_Currency=EURO_Code_Currency;
    dd.InSum=SumPcEstim/Rate;
    dd.OutSum=$0;
    dd.Rest=$0;
    dd.NumDayDoc=32100;
    dd.ApplicationKey=FormApplicationKey(AppKind);
    dd.Ground="Зачисление накопл.% при переводе в ЕВРО";
    stat=Insert(dd);
    if(stat)
      stat=CashLink_AddRec(AppKind,dd.ApplicationKey);
    end;
  end;
  return stat;
end;
macro CreateOutcomeDoc
var
  stat;
  ClearRecord(tmpDoc);
  tmpDoc.Referenc=ALG_DEPOSITOR.Referenc;
  tmpDoc.IsCur=ALG_DEPOSITOR.IsCur;
  tmpDoc.FNCash=ALG_DEPOSITOR.FNCash;
  tmpDoc.Account=ALG_DEPOSITOR.Account;
  tmpDoc.Oper={oper};
  tmpDoc.Type_Account=ALG_DEPOSITOR.Type_Account;
  tmpDoc.Code_Currency=ALG_DEPOSITOR.Code_Currency;
  tmpDoc.CodClient=ALG_DEPOSITOR.CodClient;
  tmpDoc.YesSbook="X"; /*?*/
  tmpDoc.Date_Document={curdate};
  tmpDoc.DepDate_Document={curdate};
  tmpDoc.OutSum=ALG_DEPOSITOR.Sum_Rest;
  tmpDoc.TypeComplexOper=ALG_PARAM.NumOpert;
  tmpDoc.ApplType=49;
  ClearRecord(ALG_OPERPRM);
  ALG_OPERPRM.Account=ALG_DEPOSITOR.Account;
  ALG_OPERPRM.Type_Account=ALG_DEPOSITOR.Type_Account;
  ALG_OPERPRM.Code_Currency=ALG_DEPOSITOR.Code_Currency;
  ALG_OPERPRM.Type_Oper=Списание_Простое;
  ALG_OPERPRM.TypeComplexOper=ALG_PARAM.NumOpert;
  ALG_OPERPRM.Show_Panel=0;
  ALG_OPERPRM.UseMaxDate=1;
  ALG_OPERPRM.NoPrint=1;
  stat=Выполнение_Операции(ALG_OPERPRM,tmpDoc);
  return not stat;
end;
macro CreateIncomeDoc
var
  stat,NumDayDoc;
  ClearRecord(tmpDoc);
  tmpDoc.Referenc=ALG_DEPOSITOR.Referenc;
  tmpDoc.IsCur=ALG_DEPOSITOR.IsCur;
  tmpDoc.FNCash=ALG_DEPOSITOR.FNCash;
  tmpDoc.Account=ALG_DEPOSITOR.Account;
  tmpDoc.Oper={oper};
  tmpDoc.Type_Account=ALG_DEPOSITOR.Type_Account;
  tmpDoc.Code_Currency=EURO_Code_Currency;
  tmpDoc.CodClient=ALG_DEPOSITOR.CodClient;
  tmpDoc.YesSbook="X"; /*?*/
  tmpDoc.Date_Document={curdate};
  tmpDoc.DepDate_Document={curdate};
  tmpDoc.InSum=ALG_DEPOSITOR.Sum_Rest/Rate;
  tmpDoc.TypeComplexOper=ALG_PARAM.NumOpert;
  tmpDoc.ApplType=49;
  tmpDoc.IsControl=StrFor(1);
  if(tmpDoc.InSum<1)
    dd.Referenc=ALG_DEPOSITOR.Referenc;
    dd.Date_Document={curdate};
    dd.NumDayDoc=31999;
    if(GetLE(dd))
      NumDayDoc=dd.NumDayDoc+100;
      Copy(dd,tmpDoc);
      dd.FlagRezid=cl.CurRec.rec.NotResident;
      dd.NumDayDoc=NumDayDoc;
      dd.ApplicationKey=FormApplicationKey(AppKind);
      dd.Ground="Зачисление при переводе в ЕВРО";
      stat=Insert(dd);
      if(stat)
        stat=CashLink_AddRec(AppKind,dd.ApplicationKey);
      end;
      return stat;
    end;
  else
    ClearRecord(ALG_OPERPRM);
    ALG_OPERPRM.Account=ALG_DEPOSITOR.Account;
    ALG_OPERPRM.Type_Account=ALG_DEPOSITOR.Type_Account;
    ALG_OPERPRM.Code_Currency=ALG_DEPOSITOR.Code_Currency;
    ALG_OPERPRM.Type_Oper=Зачисление_По_Данным_ПК;
    ALG_OPERPRM.TypeComplexOper=ALG_PARAM.NumOpert;
    ALG_OPERPRM.Show_Panel=0;
    ALG_OPERPRM.UseMaxDate=1;
    ALG_OPERPRM.NoPrint=1;
    stat=Выполнение_Операции(ALG_OPERPRM,tmpDoc);
    return not stat;
  end;
  return false;
end;
macro UpdateAuxInfo
var
  stat,res=true;
  ai.Reference=ALG_DEPOSITOR.Referenc;
  ai.InfoType=1;
  ai.Date=Date(31,12,2099);
  stat=GetLE(ai);
  while(stat and res and (ai.Reference==ALG_DEPOSITOR.Referenc) and (ai.InfoType==1))
    SetRecordAddr(ai_prc,ai,0,0,true);
    ai_prc.GlobPerc=ai_prc.GlobPerc/Rate;
    ai_prc.AltPerc=ai_prc.AltPerc/Rate;
    ai_prc.GlobPerc_Copy=ai_prc.GlobPerc_Copy/Rate;
    ai_prc.AltPerc_Copy=ai_prc.AltPerc_Copy/Rate;
    res=Update(ai);
    stat=Prev(ai);
  end;
  return res;
end;
macro TrnProc
var
  OldFlag0Rest,PrevNumber;
var
  OldLimit=ALG_DEPOSITOR.Limit;
record
  OpParm("op_parm.rec");
  if(StrFor(GetProgramID)!="П")
    AppKind=1;
  else
    AppKind=10;
  end;
  TrnStatus=FindDepType;
  if(TrnStatus)
    ALG_DEPOSITOR.Limit=0;
    TrnStatus=UpdateAlgRecords(UPDATE_DEP,true);
  end;
  if(TrnStatus)
    TrnStatus=GetRate;
  end;
  if(TrnStatus)
    TrnStatus=GetEUROcodE;
  end;
  if(TrnStatus)
    DateX=GetDateX;
    if(DateX==NullDate)
      TrnStatus=false;
    end;
  end;
  if(TrnStatus)
    OldFlag0Rest=dt.MayBeZeroRest;
    dt.MayBeZeroRest="X";
    TrnStatus=Update(dt);
  end;
  if(TrnStatus)
    TrnStatus=CreateOutcomeDoc;
  end;
  if(TrnStatus)
    TrnStatus=UpdatePCDrest;
  end;
  if(TrnStatus)
    TrnStatus=UpdatePCCalc;
  end;
  if(TrnStatus)
    TrnStatus=UpdatePCEstim;
  end;
  if(TrnStatus and SumPcEstim)
    TrnStatus=CreatePcEstimDocs;
  end;
  if(TrnStatus)
    TrnStatus=UpdateStatistics(ALG_DEPOSITOR.Code_Currency);
  end;
  if(TrnStatus)
    if(StrFor(GetProgramID)!="П")
      ALG_DEPOSITOR.PrevAccount=ALG_DEPOSITOR.Account;
      PrevNumber=ALG_DEPOSITOR.Number;
      ALG_DEPOSITOR.Number=0;
      ALG_DEPOSITOR.Account=FormAccountNumber(EURO_Code_Currency,ALG_DEPOSITOR.Type_Account,ALG_DEPOSITOR.Number,DefRezForAccount);
      TrnStatus=UpdateAccReg(PrevNumber);
      if(TrnStatus)
        ALG_DEPOSITOR.Code_Currency=EURO_Code_Currency;
        TrnStatus=CreateDepHistory(PrevNumber);
      end;
    else
      ALG_DEPOSITOR.PrevAccount=ALG_DEPOSITOR.Account;
      ALG_DEPOSITOR.Account=ALG_SBDEPDOC.Account;
      ALG_DEPOSITOR.Code_Currency=EURO_Code_Currency;
    end;
  end;
  if(TrnStatus)
    TrnStatus=CreateIncomeDoc;
  end;
  if(TrnStatus)
    TrnStatus=UpdateAuxInfo;
  end;
  if(TrnStatus)
    InterDesk_EndDocBunch;
    ALG_DEPOSITOR.Sum_Rest=ALG_DEPOSITOR.Sum_Rest/Rate;
    ALG_DEPOSITOR.Limit=OldLimit;
    ALG_DEPOSITOR.NumSession=0;
    TrnStatus=UpdateAlgRecords(UPDATE_DEP,true);
  end;
  if(TrnStatus)
    OldFlag0Rest=dt.MayBeZeroRest;
    dt.MayBeZeroRest="X";
    TrnStatus=Update(dt);
  end;
  if(not TrnStatus)
    AbortTrn;
  end;
end;
/* entry point */
  SetOutput("NUL");
  if(findDepartment(ALG_DEPOSITOR.FNCash, null, ddep))
    TrnStatus=true;
  end;
  if (cl.GetRecord (ALG_DEPOSITOR.CodClient))
    TrnStatus=true;
  end;
  if(TrnStatus)
    ProcessTrn(1,"TrnProc",dr,dd,dt,ai,pc,pd,pe,dh,ar);
  end;
  if(TrnStatus)
    ALG_RESULT=OK_RES;
  else
    ALG_RESULT=ERR_RES;
  end;
end;
