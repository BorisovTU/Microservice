/*

  ---------------------
  R-Style Software Lab.
  ---------------------
  RS-Retail

  Макрос выпуска карточки счета ф.0401026.

*/
import commclnt, all_vars,  acc_form;
/****************************************************************/
/*                 Константы для пользователей                  */
/****************************************************************/
const PhoneType = 4;     /* Вид номера телефона для ф.0401026:  */
                         /*  4 - домашний телефон,              */
                         /*  5 - рабочий телефон.               */
const FlagDate = TRUE;   /* Флаг простановки даты операционного */
                         /* дня в поле "Дата заполнения".       */
/**************************************************************************\
|                               Получение телефона                         |
\**************************************************************************/

macro GetPhoneNumberFSign(ClntCode)
  var DepClnt = TClientList;
  var AddrTypes = TArray;
  var i = 0;
  var PhoneNumber = "";
  var PhoneNumber2 = "";
  
  if (DepClnt.GetRecord (clntCode))
    AddrTypes(0) = COMMCLNT_PTADDR_LEGAL;
    AddrTypes(1) = COMMCLNT_PTADDR_REAL;
    AddrTypes(2) = COMMCLNT_PTADDR_POST;

    while ((i < AddrTypes.Size) and (PhoneNumber == ""))
      PhoneNumber = commclnt_GetAddressField(DepClnt, AddrTypes(i), "PhoneNumber");
      PhoneNumber2 = commclnt_GetAddressField(DepClnt, AddrTypes(i), "PhoneNumber2");
      if ((PhoneNumber != "") and (PhoneNumber2 != ""))
        PhoneNumber = PhoneNumber + ", ";
      end;
      PhoneNumber = PhoneNumber + PhoneNumber2;
      i = i + 1;
    end;
  end;
  return PhoneNumber;
end;


private macro getClientName(clientCode)
  var v_ClientList = TClientList;
  var name;

  if ( NOT v_ClientList.GetRecord(clientCode) )
    println( "Не найдена запись клиента с кодом ",
             clientCode);
    Exit();
  end;
  
  name = Trim( v_ClientList.CurRec.rec.Name1 ) + " " +
         Trim( v_ClientList.CurRec.rec.Name2 ) + " " +
         Trim( v_ClientList.CurRec.rec.Name3 );

  return name;
end;

macro printFormKB

  var v_ClientList = TClientList;
  record ALG_TRAST (sbtrast);

  if ( NOT v_ClientList.GetRecord( ALG_COMDEPCLNT.rec.CodClient ) )
    println( "Не найдена запись клиента с кодом ",
             ALG_COMDEPCLNT.rec.CodClient );
    Exit();
  end;

  var
    v_FIO   = ALG_COMDEPCLNT.ConvertFIOFull(),
    v_Owner = v_FIO + ", дата рождения: " +
      String( ALG_COMDEPCLNT.rec.Born:m ) + ", " +
      ALG_COMDEPCLNT.MakeDocStr(),
    v_Address  = commclnt_Получить_Поле_Address_Заданного_Типа( v_ClientList,
                                                                 COMMCLNT_PTADDR_LEGAL ),
    v_Phone    = GetPhoneNumberFSign(ALG_COMDEPCLNT.rec.CodClient),
    v_BankName = {Name_Bank},
    v_Account  = ALG_DEPOSITOR.Account,
    v_DateSet  = "",
    v_firstPerson = v_FIO,
    v_secondPerson = "";

    if ( StrLen( v_Address ) <= 0 )
      v_Address = commclnt_Получить_Поле_Address_Фактического_Адреса( v_ClientList );
    end;
    
    if ((ALG_SBDEPDOC.TypeOper == 0) and (ALG_NFOPR.ObjectType == 222))  // ввод новой доверенности
      v_firstPerson = "";
      v_secondPerson = getClientName(ALG_TRAST.CodClient);
    end;

    if ( FlagDate )  /* Проставить значение в поле "Дата заполнения". */
      v_DateSet  = String( {curdate}:m );
    end;

  [
                                                            ------------------
                                                            |    Код формы   |
                                                            |    документа   |
                                                            |     по ОКУД    |
                                                            ------------------
                                                            |     0401026    |
                                                            ------------------

                 Карточка
    с образцами подписей и оттиска печати

                                                |----------------------------|
                                                |        Отметка банка       |
    ########################################### |                            |
                                                | -------------------------- |
                                                |          (подпись)         |
                                                | "___" ____________ 20___г. |
    ------------------------------------------- |                            |
         Место нахождения (место жительства)    |----------------------------|
     ########################################## |                            |
     ------------------------------------------ |----------------------------|
                                                |                            |
     ------------------------------------------ |----------------------------|
                                                |                            |
     ------------------------------------------ |----------------------------|
                                                |                            |
     _тел. N (_______) ######################## |                            |
      ######################################### |                            |
     ------------------------------------------ |----------------------------|
                                                |  Прочие отметки            |
     ------------------------------------------ |                            |
                                                |                            |
     ------------------------------------------ |----------------------------|
                                                |                            |
     ------------------------------------------ |----------------------------|
                                                |                            |
     ------------------------------------------ |----------------------------|
                                                |                            |
     ------------------------------------------ |----------------------------|
                                                |                            |
     -------------------------------------------------------------------------]
     (
       ("Владелец счета " + v_Owner):w,
       v_Address:w,
       v_Phone,
       ("Банк " + "\"" +  v_BankName + "\""):w
     );
  [#](MkStr(12,1));  /* Переход к новой странице */
  [
     #########################################################################
     -------------------------------------------------------------------------
       (сокращенное наименование владельца счета)
     N банковского счета #####################################################
     -------------------------------------------------------------------------
                    |       Фамилия, имя,      | Образец |  Срок полномочий 
                    |          отчество        | подписи |      
     -------------------------------------------------------------------------
     #######|       |##########################|         |
            |       |--------------------------|---------|
            |       |                          |         |
            |       |--------------------------|---------|
            |       |                          |         |
            |       |--------------------------|---------|
            |       |                          |         |
     -------------------------------------------------------------------------
     #######|       |##########################|         |
            |       |--------------------------|---------|
            |       |                          |         |
            |       |--------------------------|---------|
            |       |                          |         |
            |       |--------------------------|---------|
            |       |                          |         |
     -------------------------------------------------------------------------
     Дата           |####################################| Образец оттиска
     заполнения     |                                    | печати
     ----------------------------------------------------|
     Подпись        |                                    |
     клиента        |                                    |
     -------------------------------------------------------------------------
     Место для удостоверительной   |                                         |
     надписи о свидетельствовании  |            Выданы денежные чеки         |
     подлинностей подписей         |                                         |
                                   |-----------------------------------------|
                                   | Дата | с N  | по N | Дата | с N  | по N |
                                   |------|------|------|------|------|------|
                                   |      |      |      |      |      |      |
                                   |------|------|------|------|------|------|
                                   |      |      |      |      |      |      |
                                   |------|------|------|------|------|------|
                                   |      |      |      |      |      |      |
                                   |------|------|------|------|------|------|
                                   |      |      |      |      |      |      |
                                   |------|------|------|------|------|------|
                                   |      |      |      |      |      |      |
     ------------------            |------|------|------|------|------|------|]
     (
       v_FIO,
       Acc_Form(v_Account):f,
       "первая подпись":w,
       v_firstPerson:w,
       "вторая подпись":w,
       v_secondPerson:w,
       v_DateSet
     );
end;
