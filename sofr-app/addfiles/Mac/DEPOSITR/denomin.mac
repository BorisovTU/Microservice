/***************************************************************************\
| DENOMIN.MAC - Макрос деноминации сумм.                                    |
|                                                     Зубов В.Ю. 10-01-98   |
\***************************************************************************/
/* Данный макрос служит для работе по деноминированной базе с Лароса. */
/* Особенностью такой базы является наличие документа об операции  */
/* "Деноминация", до даты которой идут документы с неденоминированными */
/* сумма. Для макросов, работающих по полям сумм из документов, необходимо:*/
/*  1. Вызвать макрофункцию CheckDenom(Referenc), которая устанавливает дату */
/*     деноминации ДатаДеном по дате операции "Деноминация" для счета с */
/*     Referenc */
/*  2. При работе с суммами, которые возможно надо деноминировать, надо */
/*     ВСЕГДА вызывать макрофункцию Den(Сумма, Дата), которая в случае  */
/*     если Дата > ДатаДеном возвращает ту же сумму, а если меньше - */
/*     деноминирует ее.*/

Import OperCode;

Var ДатаДеном = Date(0,0,0); /* Дата деноминации */

MACRO CheckDenom(Referenc)
 file doc("sbdepdoc.dbt") key 1; /* файл документов */
 doc.Referenc = Referenc;
 doc.TypeOper = Деноминация;
 doc.DepDate_Document = Date(31,12,2999);
 if(GetLE(doc) AND
    (doc.Referenc == Referenc) AND
    (doc.TypeOper == Деноминация) AND
    (doc.KindOp != 9) /* не архивный документ */)
  ДатаДеном = doc.DepDate_Document;
 else
  ДатаДеном = Date(0,0,0);
 end; /* IF */
END; /* MACRO */


MACRO Den(Сумма, Дата)
 if(Дата > ДатаДеном)
   return Сумма;
 else
   if( Сумма < 0 )
     return -MoneyL( Floor( DoubleL( -Сумма * 100 ) / 1000 + 0.5 ) ) / 100;
   else
     return MoneyL( Floor( DoubleL( Сумма * 100 ) / 1000 + 0.5 ) ) / 100;
   end;
 end;/* IF */
end; /* MACRO */
