/**************************************************************************

  R-Style SoftLab.

  RS-Retail.

  Выпуск "Заявления о переводе ф.143".

***************************************************************************/

import DeprIntr, CommClnt, Alg_Vars, OperCode, RtPm_Lib, RSD;
import PmPropUtil;

 const
   ACCOUTN_OWNER       = StrFor(  0 ),  /* Владелец счета      */
   ACCOUTN_PROXY       = StrFor(  1 ),  /* Доверенное лицо     */
   ACCOUTN_HEIR        = StrFor(  2 ),  /* Наследник - выдачи  */
   ANONIMUS_CLIENT     = StrFor(  3 ),  /* Анонимный клиент    */
   ACCOUNT_IMPORTER    = StrFor(  4 ),  /* Вноситель           */
   ACCOUNT_TUTOR       = StrFor(  5 ),  /* Опекун              */
   ACCOUNT_OBLIGATOR   = StrFor(  6 ),  /* Под обязательство   */
   ACCOUTN_HEIR_PAY    = StrFor(  7 ),  /* Наследник - выплаты */
   ACCOUNT_UNDER_TRIAL = StrFor(  8 ),  /* По суду             */
   ACCOUNT_PARENT      = StrFor(  9 ),  /* Родитель            */
   ACCOUNT_RETRAST     = StrFor( 10 ),  /* Передоверенность    */
   ACCOUNT_BURIAL      = StrFor( 14 );  /* На дост.похороны    */

 const CODEKIND_BIC = 3;

 const CO_CommPay = 1;

 const Date0 = Date( 0, 0, 0 );

 /* Значения iApplicationKind */
 const SB_DEPOSIT   =   1;  /* Документ вкладной операции */
 const SB_CASHOPERT = 200;  /* Документ для сервисно-кассовых операций */

 /* Значения TypeValue */
 const TYPERES_CURRENCY = 1;  /* Деньги */

 /* Коды клавиш. */
 const KEY_F2    = 316;  /* Код нажатия клавиши F2. */
 const KEY_F3    = 317;  /* Код нажатия клавиши F3. */
 const KEY_F9    = 323;  /* Код нажатия клавиши F9. */
 const KEY_SPACE =  32;  /* Код нажатия клавиши "Пробел". */
 const KEY_ESC   =  27;  /* Код нажатия клавиши Esc. */

 var clntL = TClientList;

 var clntT = TClientList;  /* Доверенное лицо */

 file prsn ( person   ) key 0;  /* список пользователей */
 file cr   ( currency ) Key 0;  /* список валют */
 file cntr ( country  ) Key 2;  /* список стран */
 file dt   ( sb_dtyp  ) Key 2;  /* виды вкладов */
 file ln   ( sb_casln ) Key 0;  /* связки документов */
 file lnd  ( sbdepdoc ) Key 3;  /* вкладные документы из связки */
 file lnp  ( pay_doc  ) Key 2;  /* кассовые документы из связки */
 file casv ( sb_casvl ) Key 0;  /* виды ресурсов кассы */
 file trs  ( sbtrast  ) Key 2;  /* доверенности и т.п. */

 file paprkind ( "paprkind.dbt", "bank.def" ) key 0;

 var ddep  = TRecHandler( "i_dp_dep.rec" );
 var party = TRecHandler( "i_party.rec" );

 private var dp_dep   = TBFile( "dp_dep.dbt", "r", 2 );
 private var dp_party = TBFile( "party.dbt",  "r", 0 );
 private var address  = TBFile( "adress.dbt", "r", 1 );

 var bankdprt : TBFile;
 var partcode : TBFile;

 private file dd ( sbdepdoc );

 file ddPmntProp ("RTPaymentProp.dbt") key 0;

 private record rt_paym_fpm ("rt_paym.fpm");

 record dlg_inf ( "P_ADD143", "sbbank.lbr" ) dialog;

 var Bank_Standart = GetBankStandart();

 var flag_Out_Form_143 = TRUE;

 var flag_Out_Form_364 = FALSE;

 var sKNF;

 var nCommiss = "__________________";
 var mCommiss = "";

 private var mark_RtPaym = FALSE;

 private var flag_Order = 0;

 array aPaper;  /* Виды документов, удостоверяющих личность */


macro MoneyToStr( CurrCode, Sum )
  file Curr( "currency.dbt" ) key 0;
  Curr.Code_Currency = CurrCode;
  if( GetEQ( Curr ) );
    return CurToStrAlt( Sum, null, null, Int( Curr.ExternalCode ) );
  end;
  return "";
end;

/*************************************************************

                Получение телефонного номера

**************************************************************/
macro GetPhoneNumber(ClntCode)
  var DepClnt = TClientList;
  var AddrTypes = TArray;
  var i = 0;
  var PhoneNumber  = "";
  var PhoneNumber2 = "";

  if (DepClnt.GetRecord (clntCode))
    AddrTypes(0) = COMMCLNT_PTADDR_LEGAL;
    AddrTypes(1) = COMMCLNT_PTADDR_REAL;
    AddrTypes(2) = COMMCLNT_PTADDR_POST;

    while ((i < AddrTypes.Size) and (PhoneNumber == ""))
      PhoneNumber = commclnt_GetAddressField(DepClnt, AddrTypes(i), "PhoneNumber");
      PhoneNumber2 = commclnt_GetAddressField(DepClnt, AddrTypes(i), "PhoneNumber2");
      if ((PhoneNumber != "") and (PhoneNumber2 != ""))
        PhoneNumber = PhoneNumber + ", ";
      end;
      PhoneNumber = PhoneNumber + PhoneNumber2;
      i = i + 1;
    end;
  end;
  return PhoneNumber;
end;

 /**********************************************************

    Получение названия подразделения.

 ***********************************************************/

 macro getNameBranch

  var NameBranch = "";

  if (not findDepartment(dd.FNCash, NameBranch))
    NameBranch = "";
  end;

  return NameBranch;

 end;

 /**************************************************************************\
 |                        Разделение строки наклонными                      |
 \**************************************************************************/
 macro Str_Select( _Str )
  var i = 0, l = StrLen( _Str );
  var str = "/";

  while ( i < l )
    i = i + 1;
    str = str + SubStr( _Str, i, 1 ) + "/";
  end;

  return str;
 end;

 /*****************************************************************
                      Строковый формат даты
 ******************************************************************/
 macro DateToStr( pDate )

   var d, m, y, Day, Month, Year;

   DateSplit( pDate, d, m, y );
   Day = String( d );

   if   (m ==  1) Month = " января ";
   elif (m ==  2) Month = " февраля ";
   elif (m ==  3) Month = " марта ";
   elif (m ==  4) Month = " апреля ";
   elif (m ==  5) Month = " мая ";
   elif (m ==  6) Month = " июня ";
   elif (m ==  7) Month = " июля ";
   elif (m ==  8) Month = " августа ";
   elif (m ==  9) Month = " сентября ";
   elif (m == 10) Month = " октября ";
   elif (m == 11) Month = " ноября ";
   elif (m == 12) Month = " декабря ";
   else           Month = "  ";
   end;

   Year = String(y);

   return Day + Month + Year + " г.";

 end;

 /******************************************************************/
 /*              Название вида представителя вкладчика             */
 /******************************************************************/
 macro Trust_GetKind( pAuthor )

   var sAuthor = "";

   if ( ( pAuthor == ACCOUTN_PROXY )  OR  ( pAuthor == ACCOUNT_RETRAST ) )
     sAuthor="доверенность";
   elif ( pAuthor == ACCOUTN_HEIR )
     sAuthor = "завещание";
   elif ( ( pAuthor == ANONIMUS_CLIENT )  OR  ( pAuthor == ACCOUNT_IMPORTER ) )
     sAuthor="на вносителя";
   elif ( pAuthor == ACCOUNT_TUTOR )
     sAuthor = "на опекуна";
   elif ( pAuthor == ACCOUNT_OBLIGATOR )
     sAuthor = "обязательство";
   elif ( pAuthor == ACCOUTN_HEIR_PAY )
     sAuthor = "завещание";
   elif ( pAuthor == ACCOUNT_UNDER_TRIAL )
     sAuthor = "решение суда";
   elif ( pAuthor == ACCOUNT_PARENT )
     sAuthor = "на родителя";
   elif ( pAuthor == ACCOUNT_BURIAL )
     sAuthor = "на дост.похороны";
   end;

   return sAuthor;

 end;

 /******************************************************************/
 /*                 Реквизиты представителя вкладчика              */
 /******************************************************************/
 macro Trust_Properties( pCodClient, pReferenc, pAuthor, pLine )

   var sTrust = "";
   var stat;

   pReferenc = ALG_CONTRACT.ID;

   ReWind( trs );
   ClearRecord( trs );
   trs.CodClient = pCodClient;
   stat = GetEQ( trs );

   while ( stat  AND  ( trs.CodClient == pCodClient ) )
     if ( ( trs.Referenc == pReferenc )  AND  ( trs.Type == pAuthor ) )
       stat = FALSE;
       sTrust = Trust_GetKind( pAuthor ) + " от " + trs.DateOpen;
     else
       stat = Next( trs );
     end;
   end;

   if ( ( StrLen( sTrust ) == 0 )  AND  pLine )
     sTrust = "\n\n_______________________________________________________";
   end;

   return sTrust;

 end;

 /************************************/
 /*    Получение названия страны     */
 /************************************/
 macro getCountry( code )

   var ret_name = "";

   ClearRecord( cntr );
   cntr.CodeLat3 = code;

   if ( GetEQ( cntr ) )
     ret_name = cntr.Name;
   else
     if ( code == "" )
       ret_name = "Россия";
     end;
   end;

   return ret_name;

 end;

 /**************************************************************************\
 |                                                                          |
 \**************************************************************************/
 macro GetPaperKind( paperCode )
   var retValue = "Документ";
   ClearRecord( paprkind );
   ReWind( paprkind );
   paprkind.PaperKind = paperCode;
   if ( GetEQ( paprkind ) )
     retValue = paprkind.Name;
   end;
   return retValue;
 end;

/****************************************************************/
/*             Проверка сообщения: не более 10 слов.            */
/****************************************************************/
macro Check_Words_10( SI )
  var Ret = CM_SAVE;
  var i = 1, k = 0, l = StrLen( SI );
  var sSymbol;
  var mBlank = TRUE, mCopy = TRUE;
  while ( ( i <= l )  AND  mCopy )
    sSymbol = SubStr( SI, i, 1 );
    if ( sSymbol == " " )
      mBlank = TRUE;
    else
      if ( mBlank )
        k = k + 1;
        if ( k == 11 )
          mCopy = FALSE;
          Ret = CM_IGNORE;
          MsgBox( "Текстовое сообщение не должно содержать более 10 слов." );
        end;
      end;
      mBlank = FALSE;
    end;
    i = i + 1;
  end;
  return Ret;
end;

/****************************************************************/
/*                   Обработка событий панели.                  */
/****************************************************************/
macro EventProcPr( dlg, cmd, id, key )

  if ( cmd == DLG_KEY )
    /* msgbox(key); */
    if   ( key == KEY_F2 )
      return Check_Words_10( dlg.Text );
      // CM_SAVE;
    //elif ( key == KEY_F9 )
    //  return CM_IGNORE;
    elif ( key == KEY_ESC )
      // panEscape = TRUE;
      return CM_CANCEL;
    end;
  end;
end;

/****************************************************************/
/*              Сокращение сообщения до 10 слов.                */
/****************************************************************/
macro Words_10( SI )
  var SO = "";
  var i = 1, k = 0, l = StrLen( SI );
  var sSymbol;
  var mBlank = TRUE, mCopy = TRUE;
  while ( ( i <= l )  AND  mCopy )
    sSymbol = SubStr( SI, i, 1 );
    if ( sSymbol == " " )
      mBlank = TRUE;
    else
      if ( mBlank )
        k = k + 1;
        if ( k == 11 )
          mCopy = FALSE;
        end;
      end;
      mBlank = FALSE;
    end;
    if ( mCopy )
      SO = SO + sSymbol;
    end;
    i = i + 1;
  end;
  return SO;
end;

/**************************************************************************\
|                        Атрибуты банка получателя.                        |
\**************************************************************************/
macro getRecipientBank( pPartyID )
  var stat = TRUE;
  // if ( BankVersion == 510 )
    dp_dep.rec.PartyID = pPartyID;
    dp_dep.rec.Code    = 0;
    stat = dp_dep.GetGE();
    if ( stat  AND  ( dp_dep.rec.PartyID != pPartyID ) )
      stat = FALSE;
    end;
  // end;
  return stat;
end;

/**************************************************************************\
|                        Атрибуты банка получателя.                        |
\**************************************************************************/
macro getRecipientBankAddr( pPartyID )
  var stat = TRUE;
  // if ( BankVersion == 510 )
    address.rec.Type    = 1;  // Юридический адрес.
    address.rec.PartyID = pPartyID;
    stat = address.GetEQ();
    if ( NOT stat )
      address.rec.Type    = 2;  // Фактический адрес.
      address.rec.PartyID = pPartyID;
      stat = address.GetEQ();
    end;
    if ( NOT stat )
      address.rec.Type    = 3;  // Почтовый адрес.
      address.rec.PartyID = pPartyID;
      stat = address.GetEQ();
    end;
  // end;
  return stat;
end;

/**************************************************************************\
|          Получение регистрационного номера заявления о переводе.         |
\**************************************************************************/
macro getRegNumTransf()
var stat = 0;
  return stat;
end;

 /**********************************************************

    Форма 143 для стандарта работы "Сберегательный банк".

 ***********************************************************/

 macro Form_143_for_Sber_Bank()

 /* Атрибуты владельца счета */
 var sDeclarer = "";
 var sDeclPhone = "";
 var sPaper = "";
 var sPaperSeries = "";
 var sPaperNumber = "";
 var sPaperWho = "";
 var sPaperWhere = "";
 var sAddressF = "";

 var mResident = " ";
 var mNotResident = " ";

 /* Атрибуты доверенного лица */
 var sDeclarerT = "";
 var sPaperT = "";
 var sPaperWhoT = "";
 var sPaperWhereT = "";

 var sBranchName = "";
 var sBranchAddress = "";

 var sINN = "";

 var sTrust = "";
 var sTrustRecv = "";
 var sTrustRecv2 = "";

 var sRecipBankNum  = "";
 var sRecipBankAddr = "";

 var sOper = "";

 var mSummaNal, sSummaNal;

 /* Отметка способа получения перевода */
 var mAllSum = " ", mPartAcc = " ";
 var mPayAcc = " ", mPayOpen = " ", mPayNal = " ";
 /* Ф.И.О. получателя для разных способов получения перевода */
 var rPayAcc = "", rPayOpen = "", rPayNalF = "", rPayNalIO = "";

 var sRecipAcc = "", sRecipTypeAcc = "";

 var NameRecpF = "", NameRecpIO = "";

 var mNotifPayerPhone = " ", mNotifReciverPhone = " ";

 var yesUrgent = "";
 var noUrgent = "";

 // Атрибуты уведомления получателя\отправителя.
 var mMessage = "";
 var sText = "";
 // Получатель.
 var mR_PH = " ",
     mR_P  = " ",
     mR_S  = " ",
     mR_E  = " ";
 var sR_PHCOD = "", sR_PHNUM = "";
 var sR_PCOD  = "", sR_PNUM  = "", sR_PAB = "";
 var sR_SCOD  = "", sR_SNUM  = "";
 var sR_E     = "";
 // Отправитель.
 var mP_PH = " ",
     mP_P  = " ",
     mP_S  = " ",
     mP_E  = " ";
 var sP_PHCOD = "", sP_PHNUM = "";
 var sP_PCOD  = "", sP_PNUM  = "", sP_PAB = "";
 var sP_SCOD  = "", sP_SNUM  = "";
 var sP_E     = "";

 mSummaNal = String( ALG_SBDEPDOC.OutSum:a );
 sSummaNal = MoneyToStr( ALG_SBDEPDOC.Code_Currency, mSummaNal );

 if ( NOT clntL.GetRecord( ALG_DEPOSITOR.CodClient ) )
   MsgBox( "Не найдена запись клиента с CodClient = ",
           ALG_DEPOSITOR.CodClient );
   Exit( 0 );
 end;

/*****  Лицевая сторона  *****/

 // Атрибуты Заявителя (Плательщика).
 sDeclarer = StrUpr( Trim( clntL.CurRec.rec.Name1 ) ) + " " +
                     Trim( clntL.CurRec.rec.Name2 ) + " " +
                     Trim( clntL.CurRec.rec.Name3 );

 // Резидентность.
 if ( clntL.CurRec.rec.NotResident == "" )  // Резидент.
   mResident = "V";
   mNotResident = " ";
 else
   mResident = " ";
   mNotResident = "V";
 end;

 sINN = Str_Select( clntL.CurRec.rec.INN );

 // sPaper = aPaper( clntL.CurRec.rec.PaperKind );
 sPaper = GetPaperKind( clntL.CurRec.rec.PaperKind );

 sPaperSeries = Trim( clntL.CurRec.rec.PaperSeries );
 sPaperNumber = Trim( clntL.CurRec.rec.PaperNumber );

 sDeclPhone  = GetPhoneNumber(clntL.CurRec.rec.CodClient);
 sPaperWho   = clntL.CurRec.rec.PaperIssuer;  /* Кем выдан паспорт */
 sPaperWhere = String( clntL.CurRec.rec.PaperIssuedDate );  /* Когда выдан паспорт */

 if ( ( StrLen( sPaperWho ) == 0 )  OR
      ( clntL.CurRec.rec.PaperIssuedDate == Date0 ) )
   sPaperWho   = clntL.CurRec.rec.PaperIssuer;
   sPaperWhere = clntL.CurRec.rec.PaperIssuedDate;
 end;

 /* Получение фактического адреса */
 clntL.CurRec.AdressType = 2;
 sAddressF = clntL.CurRec.rec.Address;

 /* Проверка: является ли вклад избирательным */
 ReWind( dt );
 ClearRecord( dt );
 dt.FlagCur = ALG_DEPOSITOR.IsCur;
 dt.Kind    = ALG_DEPOSITOR.Type_Account;
 if ( GetEQ( dt )  AND
      ( ( dt.NewGroupNumb ==  "9" )  OR
        ( dt.NewGroupNumb == "10" )  OR
        ( dt.NewGroupNumb == "11" ) ) )
   /* Избирательные виды вкладов */
   sDeclarer = sDeclarer + ", год рождения: " + clntL.CurRec.rec.Born +
               ", гражданство: " + getCountry( clntL.CurRec.rec.Country );
 end;

 /* Заполнение данных по доверенности */
 if ( ALG_SBDEPDOC.Author )

   sTrustRecv = Trust_Properties( ALG_SBDEPDOC.CodClient,
                                  ALG_SBDEPDOC.Referenc,
                                  ALG_SBDEPDOC.Author, TRUE );

   if ( NOT clntT.GetRecord( ALG_SBDEPDOC.CodClient ) )
     MsgBox( "Не найдена запись клиента с CodClient = ",
             ALG_SBDEPDOC.CodClient );
     Exit( 0 );
   end;


   /* Аттрибуты доверенного лица */
   sDeclarerT = StrUpr( Trim( clntT.CurRec.rec.Name1 ) ) + " " +
                        Trim( clntT.CurRec.rec.Name2 ) + " " +
                        Trim( clntT.CurRec.rec.Name3 );

   sPaperWhoT   = clntT.CurRec.rec.PaperIssuer;  /* Кем выдан паспорт */
   sPaperWhereT = String( clntT.CurRec.rec.PaperIssuedDate );  /* Когда выдан паспорт */

   if ( ( StrLen( sPaperWhoT ) == 0 )  OR
        ( clntT.CurRec.rec.PaperIssuedDate == Date0 ) )
     sPaperWhoT   = clntT.CurRec.rec.PaperIssuer;
     sPaperWhereT = clntT.CurRec.rec.PaperIssuedDate;
   end;

   // sPaperT = aPaper( clntT.CurRec.rec.PaperKind );
   sPaperT = GetPaperKind( clntT.CurRec.rec.PaperKind );

   sTrust = "От имени вкладчика/отправителя/наследника по документу: " +
            sTrustRecv + "\n" +
            "\t\t(реквизиты доверенности и т.п.)\n" +
            sDeclarerT + "\n" +
            "\t\t(Ф.И.О. представителя полностью)\n" +
            "документ, удостоверяющий личность представителя (вид):\n" +
            String(sPaperT:w) + "\n" +
            " серия " + Trim( clntT.CurRec.rec.PaperSeries ) +
            " № " + Trim( clntT.CurRec.rec.PaperNumber ) +
            "  выдан " + sPaperWhoT + ", " + sPaperWhereT + "\n" +
            "\t\t\t\t(кем, когда)";
 else
   sTrust = "документ, удостоверяющий личность отправителя (вид):\n" +
            String(sPaper:w) + "\n" +
            "серия " + sPaperSeries + " № " + sPaperNumber +
            "  выдан " + sPaperWho + ", " + sPaperWhere + "\n" +
            "\t\t\t\t(кем, когда)";
 end;

 /* Поиск записи о подразделении */
 if (findDepartment(ALG_DEPOSITOR.FNCash, null, ddep, party))
   sBranchName = Trim( ddep.rec.Name );
   sBranchAddress = Trim( party.rec.address );
 else
   sBranchName = "";
   sBranchAddress = "";
 end;

 // Банк получателя.
 if ( getRecipientBank( rt_paym_fpm.PartyID ) )
   sRecipBankNum = dp_dep.rec.Name;
 else
   sRecipBankNum = "";
 end;

 // Адрес банка получателя.
 if ( getRecipientBankAddr( rt_paym_fpm.PartyID ) )
   sRecipBankAddr = address.rec.Adress;
 else
   sRecipBankAddr = "";
 end;

 /* Аттрибуты Получателя */

 /* Заполнение полей для разных способов получения перевода */
 if ( ALG_SBDEPDOC.TypeComplexOper == 81 )
   mAllSum   = "V";
   mPartAcc  = " ";
   mSummaNal = "";
   sSummaNal = "";
 else
   mAllSum   = " ";
   mPartAcc  = "V";
 end;

 mPayAcc   = " ";
 mPayOpen  = " ";
 mPayNal   = " ";
 rPayAcc   = "";
 rPayOpen  = "";
 rPayNalF  = NameRecpF;
 rPayNalIO = NameRecpIO;
 sRecipAcc = "";
 sRecipTypeAcc = "";

 if ( ALG_SBDEPDOC.TypeOper == Списание_В_Чужой_Филиал )
   /* Получатель - физическое лицо */
   if ( rtpaymFindByAttrID( ALG_SBDEPDOC.iApplicationKind,
                            ALG_SBDEPDOC.ApplicationKey, "РЕКВ_ПОЛ" ) == TRUE )
     setRecordAddr( rt_paym1, rt_paym );
     if ( StrLen( ddPmntProp.Account ) > 0 )
       mPayAcc   = "V";
       sRecipAcc = Acc_Form( ddPmntProp.Account );
       rPayAcc   = ddPmntProp.Name;
     end;
   end;
 end;

 if ( GetBitFlag( ALG_SBDEPDOC.Flags, 27 ) )
   yesUrgent = "V";
 else
   noUrgent = "V";
 end;

[
                                                                                              ф.№ 143

##############################     ####################################################################


                                   ЗАЯВЛЕНИЕ О ПЕРЕВОДЕ

                               ┌─┐          ┌─┐
Категория перевода (выбрать): │#│ Простой  │#│ Срочный
                               └─┘          └─┘
    Прошу перевести в структурное подразделение Сбербанка России № ###############################################
############################################################################################################
                                    (город, посёлок и т.п.)

+-----+-----------------------------------------------------------------------------------------------------+
|     | Вклад (сумму со вклада, компенсацию по вкладу:                                                      |
|     | Спишите со счета N ###############################################################################  |
|     | ┌─┐                                                                                                 |
|     | │#│ всю сумму вклада с причитающимися процентами                                                    |
|     | └─┘                                                                                                 |
|     | ┌─┐                                                                                                 |
|     | │#│ сумму ########################################################################################  |
|     | └─┘                           (цифрами и прописью, вид валюты)                                      |
|     |     ##############################################################################################  |
|     | ┌─┐                                                                                                 |
|     | │#│ и зачислите на счет N ########################################################################  |
|     | └─┘                                                                                                 |
|  I  |     ##############################################################################################  |
|     |                      (Ф.И.О. вкладчика полностью)                                                   |
|     | ┌─┐                                                                                                 |
|     | │#│ и зачислите на счет новый: ###################################################################  |
|     | └─┘                                               (вид вклада)                    ┌─┐               |
|     |     ############################################################################  │ │ резидент      |
|     |                      (Ф.И.О. вкладчика полностью)                                 ├─┤               |
|     |                                                                                   │ │ нерезидент    |
|     | ┌─┐                                                                               └─┘               |
|     | │#│ и выдайте наличными рублями ##################################################################  |
|     | └─┘                  (Ф.И.О. получателя полностью)                                                  |
|     |     ##############################################################################################  |
+-----+-----------------------------------------------------------------------------------------------------+

Назначение платежа (заполняется при переводе между резидентом и нерезидентом) ________________________________
                                                                                 (дарение; пожертвование;
______________________________________________________________________________________________________________
     алименты или выплаты на содержание детей; выплаты или компенсации, связанные с возмещением ущерба)
]
(
 ( "В структурное подразделение\nСбербанка России\n\n" +
   "№ " + sBranchName + ",\n" +
   "находящееся в: " + sBranchAddress + "\n" +
   "(место нахождения структурного подразделения - город, посёлок и т.д., " +
   "в котором находится вклад, или в котором вносятся наличные деньги " +
   "для перевода)\n\n\n" +
   "Регистрационный номер заявления " + String( getRegNumTransf() ) + "." ):w,

 ( "От: " + sDeclarer + "\n" +
   "\t(Ф.И.О. вкладчика/отправителя/наследника полностью)\n" +
   "адрес регистрации: " + clntL.CurRec.MakeFullStrAddress() + "\n" +
   "адрес фактического проживания: " + sAddressF + "\n" +
   "телефон: " + sDeclPhone + "\n" +

   "ИНН (при наличии) " + sINN + "\n" +
   "┌─┐                    ┌─┐\n" +
   "│" + mResident + "│ резидент           │" + mNotResident + "│ нерезидент\n" +
   "└─┘                    └─┘\n" +
   sTrust ):w,


   /*
   "От имени вкладчика/отправителя/наследника по __________\n" +
   "_______________________________________________________\n" +
   "\t\t(реквизиты доверенности и т.п.)\n" +
   "_______________________________________________________\n" +
   "\t\t(Ф.И.О. представителя полностью)\n" +
   "документ, удостоверяющий личность отправителя / представителя /\n" +
   "наследника (вид): " + sPaper + "\n" +
   "серия " + sPaperSeries + " № " + sPaperNumber +
   "  выдан " + sPaperWho + ", " + sPaperWhere + "\n" +
   "\t\t\t\t(кем, когда)" ):w,
   */

  noUrgent, yesUrgent,

  Trim( sRecipBankNum ) + ", находящееся",
  ( "в " + sRecipBankAddr ):w,

 /****************************************************************/

 Str_Select( Acc_Form( Trim( ALG_SBDEPDOC.Account ) ) ),
 mAllSum,
 mPartAcc,
 String( mSummaNal:a ),
 sSummaNal:w,
 mPayAcc,
 sRecipAcc,
 rPayAcc:w,
 mPayOpen,
 sRecipTypeAcc:w,
 rPayOpen:w,
 mPayNal,
 rPayNalF,
 rPayNalIO
);

print("\f");  /* Разделитель страниц - сторон Заявления */

/*****  Оборотная сторона  *****/

 /* Отметки о уведомлении по телефону */
 /* Решили, что указание номера телефона НЕ является признаком уведомления
 if ( StrLen( sDeclPhone ) > 0 )
   mNotifReciverPhone = "V";
 end; */

 /* Чтение записи Операциониста */
 prsn.Oper = ALG_SBDEPDOC.Oper;
 if ( GetEQ( prsn ) )
   sOper = Trim( prsn.Name );
 else
   sOper = "???";
 end;

 if ( StrLen( sTrustRecv ) == 0 )
   sTrustRecv2 = "____________________________________________________________________________________________";
 else
   sTrustRecv2 = "документ: " + sTrustRecv + " на имя: " + sDeclarerT;
 end;

 // Атрибуты уведомления получателя\отправителя.

 if ( ( flag_Order == 0 )  OR  // Первый выпуск формы.
      ( NOT mark_RtPaym ) )    // Запись rt_paym.fpm не прочитана.
   dlg_inf.Text = "";
   dlg_inf.R_PHNUM = "";
   dlg_inf.P_PHNUM = sDeclPhone;

   if ( RunDialog( dlg_inf, "EventProcPr" ) )
     sText = Words_10( dlg_inf.Text );
     // Получатель.
     sR_PHCOD = dlg_inf.R_PHCOD;
     sR_PHNUM = dlg_inf.R_PHNUM;
     sR_PCOD  = dlg_inf.R_PCOD;
     sR_PNUM  = dlg_inf.R_PNUM;
     sR_PAB   = dlg_inf.R_PAB;
     sR_SCOD  = dlg_inf.R_SCOD;
     sR_SNUM  = dlg_inf.R_SNUM;
     sR_E     = dlg_inf.R_E;
     // Отправитель.
     sP_PHCOD = dlg_inf.P_PHCOD;
     sP_PHNUM = dlg_inf.P_PHNUM;
     sP_PCOD  = dlg_inf.P_PCOD;
     sP_PNUM  = dlg_inf.P_PNUM;
     sP_PAB   = dlg_inf.P_PAB;
     sP_SCOD  = dlg_inf.P_SCOD;
     sP_SNUM  = dlg_inf.P_SNUM;
     sP_E     = dlg_inf.P_E;
   end;

   if ( StrLen( sText ) > 0 )
     mMessage = "V";
   else
     mMessage = " ";
   end;

   // Уведомление получателя.
   if ( StrLen( sR_PHNUM ) > 0 )
     mR_PH = "V";
   end;
   if ( StrLen( sR_PNUM ) > 0 )
     mR_P  = "V";
   end;
   if ( StrLen( sR_SNUM ) > 0 )
     mR_S  = "V";
   end;
   if ( StrLen( sR_E ) > 0 )
     mR_E  = "V";
   end;

   // Уведомление отправителя.
   if ( StrLen( sP_PHNUM ) > 0 )
     mP_PH = "V";
   end;
   if ( StrLen( sP_PNUM ) > 0 )
     mP_P  = "V";
   end;
   if ( StrLen( sP_SNUM ) > 0 )
     mP_S  = "V";
   end;
   if ( StrLen( sP_E ) > 0 )
     mP_E  = "V";
   end;

   if ( mark_RtPaym )
     rt_paym_fpm.Message = sText;
     // Получатель.
     rt_paym_fpm.R_PH_C  = sR_PHCOD;
     rt_paym_fpm.R_PH_N  = sR_PHNUM;
     rt_paym_fpm.R_P_C   = sR_PCOD;
     rt_paym_fpm.R_P_N   = sR_PNUM;
     rt_paym_fpm.R_AB    = sR_PAB;
     rt_paym_fpm.R_S_C   = sR_SCOD;
     rt_paym_fpm.R_S_N   = sR_SNUM;
     rt_paym_fpm.R_E     = sR_E;
     // Отправитель.
     rt_paym_fpm.P_PH_C  = sP_PHCOD;
     rt_paym_fpm.P_PH_N  = sP_PHNUM;
     rt_paym_fpm.P_P_C   = sP_PCOD;
     rt_paym_fpm.P_P_N   = sP_PNUM;
     rt_paym_fpm.P_AB    = sP_PAB;
     rt_paym_fpm.P_S_C   = sP_SCOD;
     rt_paym_fpm.P_S_N   = sP_SNUM;
     rt_paym_fpm.P_E     = sP_E;
     update( rt_paym );
   end;

 else  // Повторный выпуск формы.
   sText = rt_paym_fpm.Message;
   if ( StrLen( sText ) > 0 )
     mMessage = "V";
   else
     mMessage = " ";
   end;

   // Уведомление получателя.
   sR_PHCOD = rt_paym_fpm.R_PH_C;
   sR_PHNUM = rt_paym_fpm.R_PH_N;
   sR_PCOD  = rt_paym_fpm.R_P_C;
   sR_PNUM  = rt_paym_fpm.R_P_N;
   sR_PAB   = rt_paym_fpm.R_AB;
   sR_SCOD  = rt_paym_fpm.R_S_C;
   sR_SNUM  = rt_paym_fpm.R_S_N;
   sR_E     = rt_paym_fpm.R_E;
   if ( StrLen( sR_PHNUM ) > 0 )
     mR_PH = "V";
   end;
   if ( StrLen( sR_PNUM ) > 0 )
     mR_P  = "V";
   end;
   if ( StrLen( sR_SNUM ) > 0 )
     mR_S  = "V";
   end;
   if ( StrLen( sR_E ) > 0 )
     mR_E  = "V";
   end;

   // Уведомление отправителя.
   sP_PHCOD = rt_paym_fpm.P_PH_C;
   sP_PHNUM = rt_paym_fpm.P_PH_N;
   sP_PCOD  = rt_paym_fpm.P_P_C;
   sP_PNUM  = rt_paym_fpm.P_P_N;
   sP_PAB   = rt_paym_fpm.P_AB;
   sP_SCOD  = rt_paym_fpm.P_S_C;
   sP_SNUM  = rt_paym_fpm.P_S_N;
   sP_E     = rt_paym_fpm.P_E;
   if ( StrLen( sP_PHNUM ) > 0 )
     mP_PH = "V";
   end;
   if ( StrLen( sP_PNUM ) > 0 )
     mP_P  = "V";
   end;
   if ( StrLen( sP_SNUM ) > 0 )
     mP_S  = "V";
   end;
   if ( StrLen( sP_E ) > 0 )
     mP_E  = "V";
   end;
 end;

[

┌─┐
│#│ Передать следующее текстовое сообщение получателю/вкладчику (не более 10 слов):
└─┘
########################################################################################

Уведомить о поступлении суммы перевода:

Получателя (вкладчика)                                   Отправителя (вкладчика)
┌─┐                                                      ┌─┐
│#│ по телефону:                                         │#│ по телефону:
└─┘                                                      └─┘
код ########## № #########################               код ########## № #########################
┌─┐                                                      ┌─┐
│#│ на пейджер: по телефону                              │#│ на пейджер: по телефону
└─┘                                                      └─┘
(код) ######## № #########################               (код) ######## № #########################
для абонента ##########################                  для абонента #######################
┌─┐                                                      ┌─┐
│#│ SMS-сообщением по телефону:                          │#│ SMS-сообщением по телефону:
└─┘                                                      └─┘
(код) ######## № #########################               (код) ######## № #########################
┌─┐                                                      ┌─┐
│#│ на E-mail: #############################             │#│ на E-mail: #############################
└─┘                                                      └─┘

С установленными в Сбербанке России условиями совершения переводов, тарифами и порядком взимания платы
ознакомлен и согласен.

Плату за перевод, за передачу текстового сообщения и/или за уведомления о поступлении суммы перевода:

┌─┐
│ │ вношу наличными деньгами;
└─┘
┌─┐                    ┌─┐
│ │ списать со счёта:  │ │ с которого осуществляется перевод;
└─┘                    └─┘
                   ┌─┐
                   │ │ № /__/__/__/__/__/__/__/__/__/__/__/__/__/__/__/__/__/__/__/__/.
                   └─┘

#################################################################################################################
                     (доверенность, сберегательную книжку, документы, подтверждающие право на наследство и т.д.)

Сберегательную книжку, изъятую банком при переводе части вклада (новую, оформленную взамен
исписанной), хочу получить:
┌─┐
│ │ в структурном подразделении Сбербанка России, в котором принято настоящее заявление;
└─┘
┌─┐
│ │ в структурном подразделении Сбербанка России, где ведётся счёт по вкладу.
└─┘


                                                   ___________________________________________________________
                                                     (подпись вкладчика/отправителя/наследника/представителя)

                                                   ###########################################################

______________________________________________________________________________________________________________

Заявление проверено и принято к исполнению.


Контролер _____________________________ / #
                      подпись                   Ф.И.О.

                   "____" час. "____" мин. "____"_________________20___г.

М.П. (штамп)

]
(
 mMessage,
 sText:w,
 mR_PH,
 mP_PH,
 sR_PHCOD,
 sR_PHNUM,
 sP_PHCOD,
 sP_PHNUM,
 mR_P,
 mP_P,
 sR_PCOD,
 sR_PNUM,
 sP_PCOD,
 sP_PNUM,
 sR_PAB,
 sP_PAB,
 mR_S,
 mP_S,
 sR_SCOD,
 sR_SNUM,
 sP_SCOD,
 sP_SNUM,
 mR_E,
 sR_E,
 mP_E,
 sP_E,

 ( "К заявлению прилагаю " + sTrustRecv2 ):w,

 DateToStr( {curdate} ):c,
 sOper + " /"
);

 end;

 /**********************************************************/
 /*            Поиск документа платы за перевод            */
 /**********************************************************/
 macro Commiss_Doc()

 var stCD;

   ReWind( ln );
   ClearRecord( ln );
   ln.ApplicationKind1 = ALG_SBDEPDOC.iApplicationKind;
   ln.ApplicationKey1  = ALG_SBDEPDOC.ApplicationKey;
   stCD = GetGE( ln );

   while ( stCD  AND
           ( ln.ApplicationKind1 == ALG_SBDEPDOC.iApplicationKind )  AND
           ( ln.ApplicationKey1  == ALG_SBDEPDOC.ApplicationKey ) )

     if ( ( ln.ApplicationKind2 == SB_DEPOSIT )  AND
          ( ln.ApplicationKey2  != ALG_SBDEPDOC.ApplicationKey ) )
       /* Документ вкладной операции */
       ReWind( lnd );
       ClearRecord( lnd );
       lnd.iApplicationKind = ln.ApplicationKind2;
       lnd.ApplicationKey   = ln.ApplicationKey2;
       if ( GetEQ( lnd )  AND
            ( lnd.TypeOper == 62 )  AND  ( lnd.ApplType == 43 ) )
         nCommiss = "__________________";
         mCommiss = String( lnd.OutSum );
         stCD = FALSE;
       end;
     end;

     if ( ln.ApplicationKind2 == SB_CASHOPERT )
       /* Кассовый документ */
       ReWind( casv );
       ClearRecord( casv );
       casv.RefValue = ln.RefValue2;
       if ( GetEQ( casv )  AND  ( casv.TypeValue == TYPERES_CURRENCY ) )
         ReWind( lnp );
         ClearRecord( lnp );
         lnp.iApplicationKind = ln.ApplicationKind2;
         lnp.ApplicationKey   = ln.ApplicationKey2;
         if ( GetEQ( lnp )  AND
              ( lnp.GroupOpert == 21 )  AND
              ( lnp.NumOpert   ==  2 )  AND
              ( lnp.ApplType   ==  0 ) )
           nCommiss = String( lnp.InSum ) + " " + cr.Short_Name;
           mCommiss = String( lnp.InSum );
           stCD = FALSE;
         end;
       end;
     end;

     if ( stCD )
       stCD = Next( ln );
     end;
   end;

 end;

 /**********************************************************/
 /*    Подсчет причисленных процентов при закрытии счета   */
 /**********************************************************/
 macro Add_Close_Percents()
   var mPercents = $0.0;
   var sPercents = "";
   var cmd, rs;

   if ( ALG_SBDEPDOC.TypeComplexOper == 81 )

     cmd = RsdCommand( "select doc.t_InSum as Sum " +
                       "from dsbdepdoc_dbt doc " +
                       "where doc.t_Referenc = ? " +
                         "and doc.t_Date_Document = ? " +
                         "and doc.t_TypeOper = 72 " +
                         "and doc.t_TypeComplexOper = 81 " +
                         "and doc.t_ApplType in ( 0, 3 )" );

     cmd.addParam( "Ref",  RSDBP_IN );
     cmd.addParam( "Date", RSDBP_IN );

     cmd.value( "Ref" )  = ALG_SBDEPDOC.Referenc;
     cmd.value( "Date" ) = {CurDate};

     cmd.execute;

     rs = RsdRecordset( cmd );

     while ( rs.moveNext() )
       mPercents = mPercents + rs.value( "Sum" );
     end;

     sPercents = String( mPercents:a );

   end;

   return sPercents;
 end;

 /**********************************************************

    Форма 143 для стандарта работы "Коммерческий банк".

 ***********************************************************/

 macro Form_143_for_Commerce_Banks()

 var sBranchName = "";
 var sBranchAddress = "";
 var ddep = TRecHandler( "i_dp_dep.rec" );
 var party = TRecHandler( "i_party.rec" );
 var sAddressF  = "";
 var sRecipName = "";
 var sRecipBIC  = ddPmntProp.BankCode;
 var sRecipBank = "";
 var sRecipCorr = Acc_Form( ddPmntProp.CorrAccount );
 var sRecipINN  = ddPmntProp.INN;
 var sRecipKPP  = "________";
 var SummaComm  = "_____________";
 var sSum       = "_____________";
 var sClosePerc = "";
 var sOper      = "";

 /* Получение фактического адреса */
 clntL.CurRec.AdressType = 2;
 sAddressF = clntL.CurRec.rec.Address;

 if ( rtpaymFindByAttrID( ALG_SBDEPDOC.iApplicationKind,
                          ALG_SBDEPDOC.ApplicationKey, "РЕКВ_ПОЛ" ) == TRUE )
   setRecordAddr( rt_paym1, rt_paym );
   sRecipKPP   = ddPmntProp.KPP;
 end;

 sRecipName = ddPmntProp.Name;

 /* Поиск записи о подразделении */
 if (findDepartment(ALG_SBDEPDOC.FNCash, null, ddep, party))
   sBranchName = "В " + Trim( party.rec.Name ) + ", № " +
                        Trim( ddep.rec.Name );
   sBranchAddress = Trim( party.rec.address );
 else
   sBranchName = "";
   sBranchAddress = "";
 end;

 /* Чтение записи Операциониста */
 prsn.Oper = ALG_SBDEPDOC.Oper;
 if ( GetEQ( prsn ) )
   sOper = Trim( prsn.Name );
 else
   sOper = "???";
 end;

 /* Реквизиты банка получателя */
 if ( iFindBank( ddPmntProp.BankCodeKind, ddPmntProp.BankCode, sRecipBank ) != TRUE )
   sRecipBank = "";
 end;

 /* Строчное представление суммы */
 sSum = MoneyToStr( ddPmntProp.Account_CurrCode, dd.OutSum );

 /* Поиск документов платы за перевод */
 Commiss_Doc();
 SummaComm = nCommiss;

 /* Причисленные проценты при закрытии счета */
 sClosePerc = Add_Close_Percents();

[
 #######################################  ##########################################
                                                    (фамилия, имя, отчество)
                                          ##########################################
                                                             (адрес)


                З А Я В Л Е Н И Е   О   П Е Р Е В О Д Е   В К Л А Д А

 ###################################################################################
                                     (цифрами и прописью)
 ###################################################################################
 - внесенную мною наличными деньгами

      Переведенную сумму
 - зачислите на счет №                          на имя
 ###################################################################################

 Реквизиты получателя:
 ##########################################  #######################################
 ##########################################  #######################################
 ##########################################
 ###################################################################################

 Доп. информация по переводу                 │  Если плата за перевод подлежит
                                             │   удержанию, то спишите ее с моего
                                             │   счета.
 __________________________________________  │  Плату за перевод вношу наличными
                                             │   ###################################
 __________________________________________  │

 Порядок взимания платы мне известен
 К заявлению прилагаю

 ___________________________________________________________________________________

 ___________________________________________________________________________________

 Подпись вкладчика или лица, внесшего наличные деньги за перевод ___________________
  ##############################################


 ------------------------------------------- ---------------------------------------
 Счет N ########################### │  Принято ____________/####################################
                             РАСЧЕТ │           контролер(оператор)
 Остаток вклада по счету:      по   │  ############
 ########################## ПЕРЕВОДУ│
 ###################################│
 ###################################│###############################################
 Сумма перевода: ###################                      ############
 ОСТАТКИ (после списания):
       вклада     ###################################################################
       процентов  ###################################################################
]
(
 sBranchName:w,
 ( "От гр. " + clntL.CurRec.rec.Name1 + " " +
               clntL.CurRec.rec.Name2 + " " +
               clntL.CurRec.rec.Name3 ):w,
 ( "проживающего: " + sAddressF ):w,
 ( "  Прошу перевести сумму  " + dd.OutSum +
   " ( " + sSum + " )" ):w,
 ( "- списав со счета № " + String( Acc_Form( ALG_SBDEPDOC.Account ):f ) + "  " +
   clntL.CurRec.rec.Name1 + " " +
   SubStr( clntL.CurRec.rec.Name2, 1, 1 ) + "." +
   SubStr( clntL.CurRec.rec.Name3, 1, 1 ) + ". " ):w,
 ( "- выдайте наличными деньгами гр. " ):w,

 ( "НАИМЕНОВАНИЕ: " + sRecipName ):w,
 ( "ИНН\\КПП " + sRecipINN + " \\ " + sRecipKPP ),
 ( "БАНК ПОЛУЧАТЕЛЯ: " + sRecipBank ):w,
 ( "БИК: " + sRecipBIC ),
 ( "КОР.СЧЕТ БАНКА: " + String(sRecipCorr:f) ):w,
 ( "НАЗНАЧЕНИЕ ПЛАТЕЖА: " + ALG_SBDEPDOC.Ground ):w,
 ( "деньгами в сумме " + SummaComm ):w,
 DateToStr( {curdate} ),
 Acc_Form( ALG_SBDEPDOC.Account ),
 sOper + "/",
 String( {curdate} ),
 ( dd.Rest + dd.OutSum ):a:l,
 ( "Присоединено %%: " + sClosePerc ):w,
 ( "Удержана\\внесена наличными плата: " + mCommiss ):w,
 ( "Выписано платежное поручение № " ),
 String( ALG_SBDEPDOC.OutSum:a ),
 String( ALG_SBDEPDOC.Date_Document ),
 dd.Rest:a:l,
 dd.PercRest:a:l);


 end;

 /**********************************************************

    Форма 364 для стандарта работы "Сберегательный банк".

            Заявление о международном переводе.

 ***********************************************************/

 macro Form_364_for_Sber_Bank()

 var sDeclarerRUS = "", sDeclarerLAT = "";

 var L1 = "1                                                                                                                                                                                                                                  ";
 var L2 = "2                                                                                                                                                                                                                                  ";
 var L3 = "3                                                                                                                                                                                                                                  ";

 var sBranchNum = "", sBranchName = "", sBranchAddress = "";

 var sSum, lSum, sSumma;

 var sISO = "";

 var sAddressF    = "";
 var sAddressR    = "";
 var sDeclPhone   = "";
 var sPaperWho    = "";
 var sPaperWhere  = "";
 var sPaper       = "";
 var sPaperSeries = "";
 var sPaperNumber = "";
 var sINN         = "";
 var sCountry     = "";

 var mResident = " ", mNotResident = " ";

 // Аттрибуты представителя.
 var sTrustRecv = "";

 var sDeclarerT   = "";
 var sDeclPhoneT  = "";

 var sAddressTF   = "";

 // Аттрибуты получателя.
 var sRecipName = "";
 var sRecipINN  = "";
 var sRecipKPP  = "";
 var sRecipAcc  = "";
 var sRecipBank = "";
 var sRecipBIC  = "";
 var sRecipCorr = "";

 // Аттрибуты банка получателя.
 var sRecipBankNum = "";
 var sRecipBankAddr = "";

 var sOper = "";

 /* Поиск записи о подразделении */
 if ( findDepartment( ALG_DEPOSITOR.FNCash, null, ddep, party ) )
   sBranchNum = Trim( ddep.rec.Name );
   sBranchName = Trim( party.rec.Name );
   sBranchAddress = Trim( party.rec.Address );
 else
   sBranchNum = "";
   sBranchName = "";
   sBranchAddress = "";
 end;

 // Сумма.
 sSum = String( ALG_SBDEPDOC.OutSum:a );
 lSum = StrLen( sSum );
 sSumma = SubStr( sSum, 1, lSum-3 ) + "=" + SubStr( sSum, lSum-1, 2 );

 // Код валюты.
 cr.Code_Currency = ALG_SBDEPDOC.Code_Currency;
 if ( GetEQ( cr ) )
   sISO = cr.Short_Name + " (" + cr.Name_Currency + ")";
 else
   sISO = String( ALG_SBDEPDOC.Code_Currency );
 end;

 // Запись владельца счета.
 if ( ALG_DEPOSITOR.CodClient != 0 )
   if ( clntL.GetRecord( ALG_DEPOSITOR.CodClient ) )
     // Получение фактического адреса.
     clntL.CurRec.AdressType = 2;
     sAddressF = clntL.CurRec.rec.Address;
     sAddressR = clntL.CurRec.MakeFullStrAddress( );
     sDeclPhone = GetPhoneNumber(clntL.CurRec.rec.CodClient);

     sPaperWho = clntL.CurRec.rec.PaperIssuer;  /* Кем выдан паспорт */
     sPaperWhere = String( clntL.CurRec.rec.PaperIssuedDate:m );  /* Когда выдан паспорт */
     sPaper = GetPaperKind( clntL.CurRec.rec.PaperKind );
     sPaperSeries = Trim( clntL.CurRec.rec.PaperSeries );
     sPaperNumber = Trim( clntL.CurRec.rec.PaperNumber );

     if ( ( StrLen( sPaperWho ) == 0 )  OR
          ( clntL.CurRec.rec.PaperIssuedDate == Date0 ) )
       sPaperWho   = clntL.CurRec.rec.PaperIssuer;
       sPaperWhere = clntL.CurRec.rec.PaperIssuedDate;
     end;

     // Резидентность.
     if ( clntL.CurRec.rec.NotResident == "" )  // Резидент.
       mResident = "V";
       mNotResident = " ";
     else
       mResident = " ";
       mNotResident = "V";
     end;

     sINN = Str_Select( clntL.CurRec.rec.INN );

     sCountry = getCountry( clntL.CurRec.rec.Country );
   else
     MsgBox( "Не найдена запись клиента с CodClient = ",
             ALG_DEPOSITOR.CodClient );
     Exit( 0 );
   end;
 end;

 // Атрибуты Заявителя (Плательщика).
 sDeclarerRUS = StrUpr( Trim( clntL.CurRec.rec.Name1 ) ) + " " +
                        Trim( clntL.CurRec.rec.Name2 ) + " " +
                        Trim( clntL.CurRec.rec.Name3 );

 if ( TransLiteration( Trim( clntL.CurRec.rec.Name1 ),
                       Trim( clntL.CurRec.rec.Name2 ),
                       Trim( clntL.CurRec.rec.Name3 ),
                       L1, L2, L3 ) == 0 )
   sDeclarerLAT = L1 + " " + L2 + " " + L3;
 else
   sDeclarerLAT = "";
 end;

 /* Заполнение данных по доверенности */
 if ( ALG_SBDEPDOC.Author )

   sTrustRecv = Trust_Properties( ALG_SBDEPDOC.CodClient,
                                  ALG_SBDEPDOC.Referenc,
                                  ALG_SBDEPDOC.Author, FALSE );

   if ( NOT clntT.GetRecord( ALG_SBDEPDOC.CodClient ) )
     MsgBox( "Не найдена запись клиента с CodClient = ",
             ALG_SBDEPDOC.CodClient );
     Exit( 0 );
   end;

   /* Аттрибуты доверенного лица */
   sDeclarerT = StrUpr( Trim( clntT.CurRec.rec.Name1 ) ) + " " +
                        Trim( clntT.CurRec.rec.Name2 ) + " " +
                        Trim( clntT.CurRec.rec.Name3 );

   sDeclPhoneT  = GetPhoneNumber(clntT.CurRec.rec.CodClient);

   sPaperWho   = clntT.CurRec.rec.PaperIssuer;  /* Кем выдан паспорт */
   sPaperWhere = String( clntT.CurRec.rec.PaperIssuedDate:m );  /* Когда выдан паспорт */

   sPaper = GetPaperKind( clntT.CurRec.rec.PaperKind );

   sPaperSeries = Trim( clntT.CurRec.rec.PaperSeries );
   sPaperNumber = Trim( clntT.CurRec.rec.PaperNumber );

   /* Получение фактического адреса */
   clntT.CurRec.AdressType = 2;
   sAddressTF = clntT.CurRec.rec.Address;
 end;

 /*
 // Получение реквизитов получателя.
 ClearRecord( pay_corr );
 pay_corr.IsCur    = OC_PAY_DOC.IsCur;
 pay_corr.NumGroup = OC_PAY_DOC.GroupOpert;
 if ( GetGE( pay_corr )  AND
      ( pay_corr.IsCur    == OC_PAY_DOC.IsCur )  AND
      ( pay_corr.NumGroup == OC_PAY_DOC.GroupOpert ) )
   sRecipName = pay_corr.CorrName;
   sRecipINN  = pay_corr.CorrINN;
   sRecipAcc  = pay_corr.Account;
   sRecipBank = pay_corr.BankName;
   sRecipBIC  = pay_corr.BIK;
   sRecipCorr = pay_corr.CorrAccount;
 end;
 */

 // Чтение записи операциониста.
 prsn.Oper = ALG_SBDEPDOC.Oper;
 if ( GetEQ( prsn ) )
   sOper = Trim( prsn.Name );
 else
   sOper = "???";
 end;

 // Банк получателя.
 dp_party.rec.PartyID = rt_paym_fpm.PartyID;
 if ( dp_party.GetEQ() )
   sRecipBankNum = dp_party.rec.Name;
 else
   sRecipBankNum = "";
 end;

 // Адрес банка получателя.
 if ( getRecipientBankAddr( rt_paym_fpm.PartyID ) )
   sRecipBankAddr = address.rec.Adress;
 else
   sRecipBankAddr = "";
 end;

 [
                                                                                       ф. №364

  В структурное подразделение Сбербанка России
  № #################################             Регистрационный номер заявления #################
  #######################################
  (наименование филиала Сбербанка России)


                                 Заявление о международном переводе

  __________________________________________________________________________________________
  Перечислите сумму

  1. Сумма цифрами  ####################   2. Вид валюты #######################################
                                                                    (наименование валюты)
  ┌─┐
  │V│ - списав с моего счета № ###################################################################################
  └─┘
  ┌─┐
  │ │ - перевод без открытия счета
  └─┘

  __________________________________________________________________________________________
  Отправитель
  ┌─┐                                                                       ┌─┐
  │#│ - Резидент  3. ИНН (при наличии) #################################### │#│ - Нерезидент
  └─┘                                                                       └─┘

  ##########################################################################################

  ##########################################################################################

  ##########################################################################################

  ##########################################################################################

  ##########################################################################################

  ##########################################################################################
  __________________________________________________________________________________________

  ##########################################################################################

  ##########################################################################################

  ##########################################################################################

  15. Телефон

                  код страны              код региона             номер телефона
  __________________________________________________________________________________________
  Получатель

  16. Фамилия, имя и отчество (при его наличии) или наименование (для юр. лица) получателя
  ########################################################################################

  17. ИНН (при наличии) ##################################################################

  ┌─┐
  │ │ - на счет  № /__/__/__/__/__/__/__/__/__/__/__/__/__/__/__/__/__/__/__/__/
  └─┘
  __________________________________________________________________________________________

  ┌─┐
  │V│ - выдать наличными деньгами     18. Документ, удостоверяющий личность  _______________
  └─┘

  19.Серия                20.Номер                21.Выдан

  ########################################################################################
                                    ( с обязательным указанием города, страны)
  __________________________________________________________________________________________
  Банк получателя

  23. SWIFT-код ############                24. Клиринговый код банка ______________________

  25. Номер корреспонденского счета ########################################################

  ##########################################################################################

  ##########################################################################################

  28. Город, страна
 ]
 (
   sBranchNum,
   String( getRegNumTransf() ),
   sBranchName:w,
   sSumma,
   sISO:w,
   Str_Select( Acc_Form( Trim( ALG_SBDEPDOC.Account ) ) ),
   mResident,
   sINN,
   mNotResident,
   ( "4. Фамилия, имя и отчество (при его наличии) отправителя русскими буквами  " +
     sDeclarerRUS ):w,
   ( "5. Фамилия, имя и отчество (при его наличии) отправителя латинскими буквами  " +
     sDeclarerLAT ):w,
   ( "6. Фамилия, имя и отчество (при его наличии) представителя русскими буквами  " +
     sDeclarerT ):w,
   ( "7. Реквизиты доверенности представителя  " +
     sTrustRecv ):w,
   ( "8. Документ, удостоверяющий личность (отправителя/представителя)  " +
     sPaper ):w,
   ( "9.Серия " + sPaperSeries +
     "  10.Номер " + sPaperNumber +
     "  11.Выдан " + sPaperWho + ", " + sPaperWhere ):w,
   ( "12. Адрес отправителя  " + sAddressR ):w,
   ( "13. Город, страна  " + sCountry ):w,
   ( "14. Адрес представителя  " + sAddressTF ):w,
   rt_paym_fpm.RecipName:w,
   rt_paym_fpm.RecipINN,
   ( "22. Адрес  " + rt_paym_fpm.RecipAddr ):w,
   Trim( ddPmntProp.BankCode ),
   Trim( ddPmntProp.CorrAccount ),
   ( "26. Наименование банка " + Trim( sRecipBankNum ) ):w,
   ( "27. Адрес " + Trim( sRecipBankAddr ) ):w
 );

 // print("\f");  /* Разделитель страниц - сторон Заявления */

 [
  __________________________________________________________________________________________
  Банк посредник (при наличии)

  29. SWIFT-код ________                    30. Клиринговый код банка ______________________

  31. Наименование банка _______________

  32. Адрес _______________

  33. Город, страна

  __________________________________________________________________________________________
  Цель перевода


  Подтверждаю, что данный перевод не связан с осуществлением мною предпринимательской деятельности,
  не связан с расчетами в иностранной валюте между резидентами (за исключением переводов без
  открытия счета, со счета в сумме не превышающей эквивалента 5.000 долларов США, а также дарения
  валютных ценностей супругу и близким родственникам, Российской Федерации, субъекту Российской
  Федерации и/или мунициальному образованию) и не требует проведения операции по специальному
  банковскому счету "Ф".


  Код валютной операции (заполняется при переводе нерезидентом)  {                     }
  __________________________________________________________________________________________
  Дополнительные услуги

  ┌─┐
  │ │ - осуществить перевод суммы платежа в полном объеме (услуга FULLPAY)
  └─┘
  ┌─┐
  │ │ - осуществить международный  срочный перевод
  └─┘
  ┌─┐
  │ │ - проконвертировать иностранную валюту перевода в валюту счета получателя (при срочном
  └─┘   переводе не заполняется)
  ┌─┐
  │ │ - уведомить получателя по телефону  _________________
  └─┘
  ┌─┐
  │ │ - уведомить получателя с помощью средств элетронной связи   ______________________________
  └─┘
  ┌─┐
  │ │ -  _________________
  └─┘
  __________________________________________________________________________________________
  Расходы по переводу

                 ПОРЯДОК ВЗИМАНИЯ ПЛАТЫ ЗА ПЕРЕВОД МНЕ ИЗВЕСТЕН. ПЛАТУ ЗА ПЕРЕВОД:

  ┌─┐
  │ │ - спишите с моего счета №
  └─┘
  ┌─┐
  │ │ - вношу наличными деньгами
  └─┘
  ┌─┐
  │ │ - удержать из суммы перевода
  └─┘

  ДОПОЛНИТЕЛЬНУЮ ПЛАТУ, ВЗИМАЕМУЮ ДРУГИМИ БАНКАМИ ПРИ ПРОХОЖДЕНИИ ЧЕРЕЗ НИХ ПЕРЕВОДА, ПРОШУ:

  ┌─┐
  │ │ - списать с моего счета №
  └─┘
  ┌─┐
  │ │ - получить с получателя перевода (обязательно указывается при переводе без открытия счета)
  └─┘

  ПРИ НЕВОЗМОЖНОСТИ ВОЗМЕЩЕНИЯ ДОПОЛНИТЕЛЬНОЙ ПЛАТЫ С ПОЛУЧАТЕЛЯ ПЕРЕВОДА ИЛИ С МОЕГО
  СЧЕТА, ОБЯЗУЮСЬ УПЛАТИТЬ ЕЁ НАЛИЧНЫМИ ДЕНЬГАМИ.

  __________________________________________________________________________________________

  Согласен с тем, что банк не отвечает за действия и размер удержанного комиссионного вознаграждения
  банков-корреспондентов и банков-посредников, за последствия ошибочных указаний в тексте данного
  заявления, а также за другие обстоятельства, не зависящие от банка и могущие повлечь за собой
  непоступление перевода по назначению.


  С условиями совершения Сбербанком России данного перевода ознакомлен и согласен.

  К заявлению прилагаю следующие документы: ________________________________________________

  __________________________________________________________________________________________

  "___________" __________________ _______________ г. ______________________________________
                                                       (подпись отправителя/представителя)

  __________________________________________________________________________________________
  Отметки банка

  Заявление проверено и принято к исполнению.  Контролер

                                               ______________  / ##############################################
                                                 (подпись)                (Ф.И.О.)

                  М. П.                         "_____" _______________________ __________ г.

  Время приема заявления (только для международных срочных переводов)

  Контроль перевода осуществлен. Уполномоченный работник

                                               ______________  /  __________________________
                                                 (подпись)                (Ф.И.О.)

 ]
 (
   sOper + " /"
 );

 end;

/**************************************************************************\
|                    Открытие файлов атрибутов банков.                     |
\**************************************************************************/
macro OpenBankDprt()
  var stat = FALSE;
  partcode = Tbfile( "partcode.dbt", "r", 1 );
  bankdprt = Tbfile( "bankdprt.dbt", "r", 0 );
  if ( ( partcode )  AND  ( bankdprt ) )
    stat = TRUE;
  end;
  return stat;
end;

/**************************************************************************\
|     Определение кода типа банка - Сбербанк, АКБ, РКЦ или что-то еще      |
\**************************************************************************/
macro is_SB( BIK )
  var stat = TRUE;
  var flag;

  stat = OpenBankDprt();

  if ( stat )
    // if ( BankVersion == 510 )
      partcode.rec.CodeKind = 3;
      partcode.rec.Code     = BIK;
      flag = partcode.GetEQ();
      if ( flag )
        bankdprt.rec.PartyID = partcode.rec.PartyID;
        flag = bankdprt.GetEQ();
        if ( flag )
          if ( bankdprt.rec.UERType != 3 )  // Не Сбербанк РФ.
            stat = FALSE;
          end;
        end;
      end;
    // end;
  end;

  return stat;

end;

/***************************************************************************

                             Головная процедура.

****************************************************************************/

 aPaper( 0) = "Паспорт гражданина РФ";
 aPaper( 1) = "Удостоверение личности офицера";
 aPaper( 2) = "Военный билет солдата (матроса, сержанта, старшины)";
 aPaper( 3) = "Паспорт Минморфлота";
 aPaper( 4) = "Свидетельство о рождении";
 aPaper( 5) = "Вид на жительство";
 aPaper( 6) = "Загранпаспорт гражданина РФ";
 aPaper( 7) = "Паспорт гражданина СССР";
 aPaper( 8) = "Загранпаспорт гражданина СССР";
 aPaper( 9) = "Справка об освобождении из места лишения свободы";
 aPaper(10) = "Дипломатический паспорт гражд. РФ";
 aPaper(11) = "Иностранный паспорт";
 aPaper(12) = "Свидетельство о регистрации ходатайства иммигранта о признании его беженцем";
 aPaper(13) = "Удостоверение беженца";
 aPaper(14) = "Врем. удостов. личности гражд. РФ";
 aPaper(15) = "Паспорт моряка";
 aPaper(16) = "Военный билет офицера запаса";
 aPaper(17) = "Иные докум., выдаваемые органами МВ";

 Copy( dd, ALG_SBDEPDOC );
 FindPmntProp(ddPmntProp, dd.iApplicationKind, dd.ApplicationKey);

 if ( ALG_COMDEPCLNT.rec.CodClient != 0 )
   if ( NOT clntL.GetRecord( ALG_COMDEPCLNT.rec.CodClient ) )
     clntL.CurRec.rec.Name1 = "";
     clntL.CurRec.rec.Name2  = "";
     clntL.CurRec.rec.Name3 = "";
   end;
 else
   clntL.CurRec.rec.Name1 = "";
   clntL.CurRec.rec.Name2  = "";
   clntL.CurRec.rec.Name3 = "";
 end;

 if ( rtpaymFindByAttrID( ALG_SBDEPDOC.iApplicationKind,
                          ALG_SBDEPDOC.ApplicationKey, "ПЕРЕВОД" ) == TRUE )
   setRecordAddr( rt_paym_fpm, rt_paym );
   mark_RtPaym = TRUE;
   // vCodeKind   = rt_paym_fpm.BankCodeKind;
   flag_Order  = rt_paym_fpm.FlagOrder;
   rt_paym_fpm.FlagOrder = rt_paym_fpm.FlagOrder + 1;
 end;

 if ( Index( ALG_DEPOSITOR.UserTypeAccount, "Ф" ) != 0 )  /* Счета типа "Ф" */
   sKNF = "KNF" + String( dd.KNFCode );
 else
   sKNF = "";
 end;

 if( ALG_DEPOSITOR.Code_Currency > 0 )
   /* Найти Валюту в файле */
   cr.Code_Currency = ALG_DEPOSITOR.Code_Currency;
   if ( NOT GetEQ( cr ) )
     cr.Short_Name = "(" + String( ALG_DEPOSITOR.Code_Currency ) + ")";
   end;
 else
   cr.Short_Name = "руб.";
 end;

 if ( ALG_SBDEPDOC.TypeOper == Списание_По_Раз_Поруч )
   if ( ( ALG_SBDEPDOC.ApplType !=  1 )  AND
        ( ALG_SBDEPDOC.ApplType != 53 )  AND
        ( ALG_SBDEPDOC.ApplType != 54 ) )
     flag_Out_Form_143 = FALSE;
   end;
 end;

 /* Закрыт выпуск для списаний на счет юр.лица - SCR #62123. */
 if ( ALG_SBDEPDOC.TypeOper == Списание_Простое )
   flag_Out_Form_143 = FALSE;
 end;

 /* Закрыт выпуск для списаний на счет юр.лица - SCR #62123. */
 if ( ALG_SBDEPDOC.TypeOper == Списание_По_Раз_Поруч )
   if ( ( ALG_SBDEPDOC.ApplType == 40 )  OR  /* По договору с ю/л.    */
        ( ALG_SBDEPDOC.ApplType == 41 )  OR  /* Без договора с ю/л.   */
        ( ALG_SBDEPDOC.ApplType == 53 )  OR  /* По договору с ю/л.    */
        ( ALG_SBDEPDOC.ApplType == 54 ) )    /* За пределы банка ю/л. */
     flag_Out_Form_143 = FALSE;
   end;
 end;

 if ( ( ALG_SBDEPDOC.TypeOper == Списание_По_Раз_Поруч )  AND
      ( ALG_SBDEPDOC.ApplType == 32 ) )  // Из России.
   flag_Out_Form_364 = TRUE;
 end;

 if ( flag_Out_Form_143 )
   if ( Bank_Standart == 0 )  /* Сберегательный банк */
     if ( is_SB( ddPmntProp.BankCode ) )  // Первод в СБ РФ.
       Form_143_for_Sber_Bank();
     else
       Form_143_for_Commerce_Banks();
     end;
   else                       /* Коммерческий банк   */
     Form_143_for_Commerce_Banks();
   end;
 else
   if ( flag_Out_Form_364 )
     Form_364_for_Sber_Bank();
   else
     Exit( 1 );
   end;
 end;
