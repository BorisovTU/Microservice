/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*  Эмитент                                                                 *
*                                                                          *
*  Макрос по инициализации "металлического" счета                          *
*                                                                          *
*  Гробов А. А.                                                            *
*  10.10.2004                                                              *
\**************************************************************************/

import DeprIntr, alg_vars, RSD;

private var {MFO_Bank}; /* БИК банка */


/**************************************************************************\
|                    Получить ключевой разряд для счета                    |
\**************************************************************************/
macro DefKeyRazr (Account, BikAcc)

  var   BegString;
  var   i,j,k,l,m;
  array Weigth;

  BegString = SubStr(BikAcc,7,3) + SubStr(Account,1,20);

  i = 1;
  j = 7;
  while (i <= StrLen(BegString))
    k = Int(SubStr(BegString,i,1)) * j;
    l = String(k);
    m = StrLen(l);
    Weigth(i - 1) = Int(SubStr(l,m,1));
    if (j == 7)
      j = 1;
    elif (j == 1)
      j = 3;
    else
      j = 7;
    end;
    i = i + 1;
  end;

  i = 0;
  k = 0;
  while (i < ASize(Weigth))
    k = k + Weigth(i);
    i = i + 1;
  end;
  l = String(k);
  m = StrLen(l);
  k = Int(SubStr(l,m,1));
  k = k * 3;
  l = String(k);
  m = StrLen(l);
  l = SubStr(l,m,1);

  return l;

end;


macro get_SHORT_NAME()

var commandstring = "select curr.T_SHORT_NAME from dcurrency_dbt curr " +   
                    "where curr.T_CODE_CURRENCY = " + string(ALG_DEPOSITOR.CODE_CURRENCY);

  var cmd = RSDCommand(commandstring);
  var rs = RSDrecordset (cmd);
  cmd.EXECUTE;

  if(rs.movenext)
    return rs.value ("T_SHORT_NAME");
  else
    msgbox("Валюта не найдена");
    return "000";
  end;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/

  var key_digit;

  ALG_RESULT = OK_RES;

  /* Счет Металлический, новая нумерация */
  if ((Index(ALG_DEPOSITOR.UserTypeAccount,"М") > 0           ) AND
      (StrLen(ALG_DEPOSITOR.Account) >= 20                    ) AND
      ((ALG_PARAM.NumOpert == 1) OR (ALG_PARAM.NumOpert == 51))    )

      ALG_DEPOSITOR.Account = SubStr(ALG_DEPOSITOR.Account, 1,5) + get_SHORT_NAME() +
                                                                   "0"   +
                              SubStr(ALG_DEPOSITOR.Account,10,6) + "1"   +
                              SubStr(ALG_DEPOSITOR.Account,17);

      key_digit = DefKeyRazr(ALG_DEPOSITOR.Account,{MFO_Bank});

      ALG_DEPOSITOR.Account = SubStr(ALG_DEPOSITOR.Account, 1,8) + key_digit +
                              SubStr(ALG_DEPOSITOR.Account,10);

      ALG_UPDATE = 1;
  end;

end;
