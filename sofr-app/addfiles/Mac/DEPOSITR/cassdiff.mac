/****************************************************************************
   RS-Retail 1.0                 (СБЕРБАНК)
   Subsystem:   ФИЛИАЛ ОТДЕЛЕНИЯ
                ~~~~~~~~~~~~~~
   Description: Макропроцедура сравнения первичных док-тов
                и док-тов по кассе
   ------------------------------------------------------------------------
   1998(c)R-Style SoftWare Lab.
****************************************************************************/

import deprintr, IsSrvDoc;

file depdoc  ("sbdepdoc.dbt") key 0;
file paydoc  ("pay_doc.dbt")  key 5;
file paykind ("pay_kind.dbt") key 0;
file cidoc   ("ci_doc.dbt")   key 6;
file cert    ("cert.dbt")     key 0;
file ogsz    ("ogsz_iss.dbt") key 0;
file misc    ("ci_misc.dbt")  key 0;
file casdoc  ("sb_casdc.dbt") key 1;
file casval  ("sb_casvl.dbt") key 0;
file casacc  ("sb_casac.dbt") key 0;
file currency("currency.dbt") key 0;

const FNcash  = NumFNcash();

const
  CO_CommPay     =   1, /* Коммунальные платежи */
  CO_NalPer      =  11, /* Наличный перевод */
  CO_Comission   =  21, /* Комиссия */
  CO_Conversion  =  31, /* Конверсия */
  CO_Payed       =  41, /* Выплата */
  CO_BankPayed   =  51, /* Банковские расчеты */
  CO_OverCred    =  61, /* Документ овердрафтного кредитования */
  CO_OtherBranch =  71, /* Операция по чужому филиалу */
  CO_Trn         = 100, /* Безналичные платежи */
  CO_Treasure    = 101; /* Ценности */

const
 CASHOP_REINFORCE = 1,  /* Подкрепление кассира */
 CASHOP_RETURN    = 2,  /* Возврат средств в кладовую */
 CASHOP_COLLECT   = 3,  /* Инкассация */
 CASHOP_ADVANCE   = 4,  /* Аванс */
 CASHOP_DEBIT     = 5,  /* Расход */
 CASHOP_CREDIT    = 6,  /* Приход */
 CASHOP_REMOVE    = 7,  /* Внутреннее подкрепление */
 CASHOP_CHVALUE   = 8;  /* Перевод на другой ресурс той же группы */

const
  VT_CURRENCY    =    1,
  VT_BLANKS      =    2,
  VT_CI          =  500;

const
  CI_OGSZ        =    1,
  CI_LOTTERY     =    2,
  CI_CERT        =    3,
  CI_MISC        =   10,

  TYPEDOC_CARRYOFF = 1,  /* Отложен (не подтвержден) */
  TYPEDOC_CARRYON  = 2,  /* Проведен (подтвержден) */

  FLAGDEBCRED_IN   = 1,  /* Кредит */
  FLAGDEBCRED_OUT  = 2;  /* Дебет */

const
  D_INSERT  = 0,     /* Вставка      */
  D_UPDATE  = 1,     /* Обновить     */
  D_DELETE  = 2,     /* Удалить      */
  D_STORN   = 11;    /* Сторнировать */

var anything_printed = FALSE;    /* Вообще не напечаиано ни строки - плохо! */

var ValueMoney = $0L,

    DebCred;                     /* Флаг дебета/кредита */

var TreatValueAsMoney = TRUE;         /* Трактовать ценность как имеющую ден.выр. */

var CashDebetSum  = $0L,
    CashCreditSum = $0L,
    DocsDebetSum  = $0L,
    DocsCreditSum = $0L;
var comment;

var Branch = NumFNCash;

var ClientList = TClientList;

macro CheckCIDoc

  var _retValue = true;

  if ( ( cidoc.Action == D_STORN ) or ( cidoc.Action == D_DELETE ) )
    _retValue = false;
  end;

  /* Непроведенный документ */
  if ( ( cidoc.IsSuspended != "" ) and ( cidoc.IsSuspended != "X" ) )
    _retValue = false;
  end;

  return _retValue;

end;

macro CheckType

  var _retValue = false;

  if ( ( cidoc.type == CI_OGSZ )	 or 
	( cidoc.type == CI_LOTTERY ) or
	( cidoc.type == CI_CERT ) 	 or
       ( cidoc.type == CI_MISC ) 
     )
    _retValue = true;
  end;

  return _retValue;

end;


/***********************************************************************\
|  1998(c) Copyright R-Style SoftWare Lab.                              |
|          Unauthorised duplication in any form prohibited              |
\***********************************************************************/
macro FindNameValue(RefValue)

  var str = "";
  var stat;

  ReWind(casval);
  casval.RefValue = RefValue;
  stat = getEQ(casval);
  if(stat)
     str = casval.NameValue;
  end;

  return str;
end;


/***********************************************************************\
|  1998(c) Copyright R-Style SoftWare Lab.                              |
|          Unauthorised duplication in any form prohibited              |
\***********************************************************************/
macro FindDepClient(ClientCode)

  var
    ClientName = "Б/и";

  if ( ClientList.GetRecord( ClientCode ) )
    ClientName = ClientList.CurRec.ConvertFio;
  end;

  return ClientName;

end;


/***********************************************************************\
|  1998(c) Copyright R-Style SoftWare Lab.                              |
|          Unauthorised duplication in any form prohibited              |
\***********************************************************************/
macro CurrName(Code)

  var ShortName = String(Code);

  currency.Code_Currency = Code;
  if (GetEQ(currency))
    ShortName = currency.Short_Name;
  end;

  return ShortName;

end;


/***********************************************************************\
|  1998(c) Copyright R-Style SoftWare Lab.                              |
|          Unauthorised duplication in any form prohibited              |
\***********************************************************************/
macro CheckCashDoc(ApplKind, ApplKey, CodCur)

  var
    DocFound = FALSE;

    comment = "";
    ReWind(casdoc);
    casdoc.ApplicationKind = ApplKind;
    casdoc.ApplicationKey = ApplKey;
    if(getEQ(casdoc))
      if(casdoc.TypeDoc == TYPEDOC_CARRYOFF)
        comment = "Док.не подтв.";
      end;
      ReWind(casval);
      casval.RefValue = casdoc.RefValue;
      if(getEQ(casval) and (casval.TypeValue == VT_CURRENCY)
         and (casval.CodIntValue == CodCur) )
        ValueMoney = MoneyL(casdoc.ValueMoney);
        DebCred = casdoc.FlagDebCredOper;
        DocFound = TRUE;
      end;
    else
      comment = "Док.отсутств.";
    end;
    return DocFound;

end;


/***********************************************************************\
|  1998(c) Copyright R-Style SoftWare Lab.                              |
|          Unauthorised duplication in any form prohibited              |
\***********************************************************************/
macro GetSum

  var
    sum = $0L;

  if(DebCred == FLAGDEBCRED_IN)
    sum = MoneyL(depdoc.InSum);
  elif(DebCred == FLAGDEBCRED_OUT)
    sum = MoneyL(depdoc.OutSum);
  end;

  return sum;

end;


/***********************************************************************\
|  1998(c) Copyright R-Style SoftWare Lab.                              |
|          Unauthorised duplication in any form prohibited              |
\***********************************************************************/
macro ProcessDepDocs

  var
    str = " ", DocSum = $0L, DeskSum = $0L,
    RecFound, header_printed = FALSE, sum = $0L, stat;

    ReWind(depdoc);
    ClearRecord(depdoc);
    depdoc.IsCur = NumFlagCur;
    depdoc.FNCash = FNCash;
    depdoc.Date_Document = {curdate};
    RecFound = getGE(depdoc);
    while( RecFound and (depdoc.IsCur == NumFlagCur)
          and (depdoc.FNCash == FNCash)
          and (depdoc.Date_Document == {curdate}) )
     if( (depdoc.VidDoc == 1)
        and ( depdoc.InSum or depdoc.OutSum )
        and IsServDoc(depdoc)
        and (depdoc.FlagStorn == StrFor(0))
       )

      ValueMoney = $0L;
      stat = CheckCashDoc(depdoc.iApplicationKind,
                          depdoc.ApplicationKey, depdoc.Code_Currency);
      if(stat)
        sum = GetSum;
      else
        if(depdoc.InSum)
          sum = depdoc.InSum;
        elif(depdoc.OutSum)
          sum = depdoc.OutSum;
        else
          sum = $0L;
        end;
      end;

      if(not stat  or (sum!=ValueMoney) or (comment!="") )

       if( (not stat and sum) or comment!="" )
        if(not header_printed)
 [                                        Расхождения по вкладам
 ┌────────────────────────┬────────────────────────────────┬───────┬─────────────┬────────────┬─────┬─────────────┐
 │        № счёта         │         Ф.И.О. вкладчика       │ Опер. │  Сумма док. │ Сумма кас. │Вал. │  Примечание │
 ├────────────────────────┼────────────────────────────────┼───────┼─────────────┼────────────┼─────┼─────────────┤
 ];
           header_printed = TRUE;
        end;

        str = "│" + String(Acc_Form(depdoc.Account):24:c) + "│" + String(FindDepClient(depdoc.CodClient):32:l) +
              "│" + String(depdoc.Oper:7:c) + "│" +  String(sum:13:r) +
              "│" + String(ValueMoney:12:r) + "│" + String(CurrName(depdoc.Code_Currency):5:c) +
              "│" + comment + "│";

        [#](str);
        DocSum = DocSum + sum;
        DeskSum = DeskSum + ValueMoney;
       end;
      end;

     end;

      RecFound = Next(depdoc);
    end;

    if(header_printed)

[└────────────────────────┴────────────────────────────────┴───────┴─────────────┴────────────┴─────┴─────────────┘
   Итого:                                                           ############# ############
   Расхождение:          ################
 ](DocSum, DeskSum, (DocSum - DeskSum));
      anything_printed = TRUE;
    end;

end;


/***********************************************************************\
|  1998(c) Copyright R-Style SoftWare Lab.                              |
|          Unauthorised duplication in any form prohibited              |
\***********************************************************************/
macro ProcessPayDocs

  var
    str = " ", DocSum = $0L, DeskSum = $0L,
    RecFound, header_printed = FALSE,
    PayerName = "Б/и", group;

    ReWind(paydoc);
    ClearRecord(paydoc);
    paydoc.FNCash = FNCash;
    paydoc.IsCur = NumFlagCur;
    paydoc.Date_Document = {curdate};
    RecFound = getGE(paydoc);
    while( RecFound and (paydoc.FNCash == FNCash)
          and (paydoc.IsCur == NumFlagCur)
          and (paydoc.Date_Document == {curdate}) )

       ValueMoney = $0L;

      if( (paydoc.VidDoc == StrFor(0))  AND  /* Наличный платеж */
         ( not CheckCashDoc(paydoc.iApplicationKind, paydoc.ApplicationKey, paydoc.CodCur)
           or (MoneyL(paydoc.InSum) != ValueMoney) or (comment!="") ) )

        if(not header_printed)
 [                                        Расхождения по кассовым операциям
 ┌────────────────────────┬────────────┬───────────────────────────┬───────┬─────────────┬────────────┬─────┬─────────────┐
 │    Группа операции     │ № операции │     Ф.И.О. вносителя      │ Опер. │  Сумма док. │ Сумма кас. │Вал. │  Примечание │
 ├────────────────────────┼────────────┼───────────────────────────┼───────┼─────────────┼────────────┼─────┼─────────────┤
 ];
           header_printed = TRUE;
        end;

        PayerName = paydoc.ClientLastName;
        if(paydoc.ClientFirstName)
          PayerName = PayerName + " " + SubStr(paydoc.ClientFirstName, 1, 1) + ".";
          if(paydoc.ClientSecondName)
            PayerName = PayerName + " " + SubStr(paydoc.ClientSecondName, 1, 1) + ".";
          end;
        end;


        if ( paydoc.GroupOpert == CO_CommPay )
          group = "Коммунальные платежи";
        elif ( paydoc.GroupOpert == CO_NalPer )
          group = "Прочие приходные";
        elif ( paydoc.GroupOpert == CO_Comission )
          group = "Платы и налоги";
        elif ( paydoc.GroupOpert == CO_Conversion )
          group = "Конверсии";
        elif ( paydoc.GroupOpert == CO_Payed )
          group = "Прочие расходные";
        elif ( paydoc.GroupOpert == CO_BankPayed )
          group = "Банковские расчеты";
        elif ( paydoc.GroupOpert == CO_OverCred )
          group = "Овердрафтное кредитование";
        elif ( paydoc.GroupOpert == CO_OtherBranch ) 
          group = "Опер.по чужому филиалу";
        elif ( paydoc.GroupOpert == CO_Trn )
          group = "Безналичные платежи";
        elif ( paydoc.GroupOpert == CO_Treasure )
          group = "Операции с ценностями";
        end;

        str = "│" + String(group:24:c) + "│" + String(paydoc.NumOpert:12:c) +
              "│" + String(PayerName:27:c) +
              "│" + String(paydoc.Oper:7:c) + "│" +  String(paydoc.InSum:13:r) +
              "│" + String(ValueMoney:12:r) + "│" + String(CurrName(paydoc.CodCur):5:c) +
              "│" + comment + "│";

        [#](str);
        DocSum = DocSum + MoneyL(paydoc.InSum);
        DeskSum = DeskSum + ValueMoney;
      end;


      RecFound = Next(paydoc);
    end;

    if(header_printed)

[└────────────────────────┴────────────┴───────────────────────────┴───────┴─────────────┴────────────┴─────┴─────────────┘
   Итого:                                                                   ############# ############
   Расхождение:          ################
 ](DocSum, DeskSum, (DocSum - DeskSum));
      anything_printed = TRUE;
    end;

end;


/***********************************************************************\
|  1998(c) Copyright R-Style SoftWare Lab.                              |
|          Unauthorised duplication in any form prohibited              |
\***********************************************************************/
macro ProcessCapIsDocs

  var
    str = " ", DocSum = $0L, DeskSum = $0L,
    RecFound, header_printed = FALSE,
    group;

    ReWind(cidoc);
    ClearRecord(cidoc);
    cidoc.Branch = FNCash;
    cidoc.IsCur = NumFlagCur;
    cidoc.Date = {curdate};
    RecFound = getGE(cidoc);
    while( RecFound and (cidoc.Branch == FNCash)
          and (cidoc.IsCur == NumFlagCur)
          and (cidoc.Date == {curdate}) )

       ValueMoney = $0L;

      if( CheckType and CheckCIDoc and ( not CheckCashDoc(cidoc.ApplicationKind, cidoc.ApplicationKey, cidoc.CurCode)
          or (MoneyL(cidoc.Sum) != ValueMoney) or (comment!="") ) )

        if(not header_printed)
 [                                        Расхождения по ценным бумагам
 ┌─────────────────────────────────────┬────────────┬───────┬─────────────┬────────────┬─────┬─────────────┐
 │         Тип  и выпуск  ЦБ           │ № операции │ Опер. │  Сумма док. │ Сумма кас. │Вал. │  Примечание │
 ├─────────────────────────────────────┼────────────┼───────┼─────────────┼────────────┼─────┼─────────────┤
 ];
           header_printed = TRUE;
        end;

        group = "";
        if(cidoc.Type == CI_OGSZ)
          ogsz.Type = cidoc.Type;
          ogsz.IssueID = cidoc.Issue;
          if (GetEQ(ogsz))
            group = "ОГСЗ Выпуск " + String(ogsz.IssueID)
                       + " Серия " + ogsz.Series;
          else
            group = " ";
          end;
        elif (cidoc.Type == CI_CERT)
          cert.Type = cidoc.Type;
          cert.Issue = cidoc.Issue;
          if (GetEQ(cert))
            group = cert.Desc;
          else
            group = " ";
          end;
        elif ( (cidoc.Type == CI_LOTTERY) or (cidoc.Type == CI_MISC) )
          misc.Type = cidoc.Type;
          misc.IssueID = cidoc.Issue;
          if (GetEQ(misc))
            group = misc.Name;
          else
            group = " ";
          end;
        end;


        str = "│" + String(group:37:c) + "│" + String(cidoc.Operation:12:c) +
              "│" + String(cidoc.Oper:7:c) + "│" +  String(cidoc.Sum:13:r) +
              "│" + String(ValueMoney:12:r) + "│" + String(CurrName(cidoc.CurCode):5:c) +
              "│" + comment + "│";
        [#](str);
        DocSum = DocSum + MoneyL(cidoc.Sum);
        DeskSum = DeskSum + ValueMoney;
      end;


      RecFound = Next(cidoc);
    end;

    if(header_printed)

[└─────────────────────────────────────┴────────────┴───────┴─────────────┴────────────┴─────┴─────────────┘
   Итого:                                                    ############# ############
   Расхождение:          ################
 ](DocSum, DeskSum, (DocSum - DeskSum));
      anything_printed = TRUE;
    end;

end;


/***********************************************************************\
|  1998(c) Copyright R-Style SoftWare Lab.                              |
|          Unauthorised duplication in any form prohibited              |
\***********************************************************************/
macro CollectDocsForOper

  var RecFound;

  DocsDebetSum  = $0L;
  DocsCreditSum = $0L;
  KeyNum(casdoc,0);
  ReWind(casdoc);
  ClearRecord(casdoc);
  casdoc.Branch = Branch;
  casdoc.CashDateDoc = {curdate};
  casdoc.TypeDoc = TYPEDOC_CARRYON;
  casdoc.CashOper = casacc.CashOper;
  casdoc.RefValue = casacc.RefValue;
  RecFound = getEQ(casdoc);
  while( RecFound
         and (casdoc.Branch == Branch)
         and (casdoc.CashDateDoc == {curdate})
         and (casdoc.TypeDoc == TYPEDOC_CARRYON)
         and (casdoc.CashOper == casacc.CashOper)
         and (casdoc.RefValue == casacc.RefValue) )
    if(     (casdoc.NumOper != CASHOP_REINFORCE)
        and (casdoc.NumOper != CASHOP_REMOVE)
        and (casdoc.NumOper != CASHOP_RETURN)
      )
     if(TreatValueAsMoney)
       if(casdoc.FlagDebCredOper == FLAGDEBCRED_IN)
          DocsCreditSum = DocsCreditSum + MoneyL(casdoc.ValueMoney);
       elif(casdoc.FlagDebCredOper == FLAGDEBCRED_OUT)
          DocsDebetSum = DocsDebetSum + MoneyL(casdoc.ValueMoney);
       end;
     else
       if(casdoc.FlagDebCredOper == FLAGDEBCRED_IN)
          DocsCreditSum = DocsCreditSum + MoneyL(casdoc.ValueCol);
       elif(casdoc.FlagDebCredOper == FLAGDEBCRED_OUT)
          DocsDebetSum = DocsDebetSum + MoneyL(casdoc.ValueCol);
       end;
     end;
    end;

     RecFound = Next(casdoc);
  end;

end;


/***********************************************************************\
|  1998(c) Copyright R-Style SoftWare Lab.                              |
|          Unauthorised duplication in any form prohibited              |
\***********************************************************************/
macro CollectDocsForSafe

  var RecFound;

  DocsDebetSum  = $0L;
  DocsCreditSum = $0L;
  KeyNum(casdoc,5);
  ReWind(casdoc);
  ClearRecord(casdoc);
  casdoc.Branch = Branch;
  casdoc.CashDateDoc = {curdate};
  casdoc.TypeDoc = TYPEDOC_CARRYON;
  casdoc.CashPatern = casacc.CashOper;
  casdoc.RefValue = casacc.RefValue;
  RecFound = getGE(casdoc);
  while( RecFound
         and (casdoc.CashDateDoc == {curdate})
         and (casdoc.TypeDoc == TYPEDOC_CARRYON)
         and (casdoc.CashPatern == casacc.CashOper)
         and (casdoc.RefValue == casacc.RefValue) )

    if(     (casdoc.NumOper != CASHOP_DEBIT)
        and (casdoc.NumOper != CASHOP_CREDIT)
        and (casdoc.NumOper != CASHOP_CHVALUE)
      )


     if(TreatValueAsMoney)
       if(casdoc.FlagDebCredOper == FLAGDEBCRED_OUT)
          DocsCreditSum = DocsCreditSum + MoneyL(casdoc.ValueMoney);
       elif(casdoc.FlagDebCredOper == FLAGDEBCRED_IN)
          DocsDebetSum = DocsDebetSum + MoneyL(casdoc.ValueMoney);
       end;
     else
       if(casdoc.FlagDebCredOper == FLAGDEBCRED_OUT)
          DocsCreditSum = DocsCreditSum + MoneyL(casdoc.ValueCol);
       elif(casdoc.FlagDebCredOper == FLAGDEBCRED_IN)
          DocsDebetSum = DocsDebetSum + MoneyL(casdoc.ValueCol);
       end;
     end;

    end;

     RecFound = Next(casdoc);
  end;


end;


/***********************************************************************\
|  1998(c) Copyright R-Style SoftWare Lab.                              |
|          Unauthorised duplication in any form prohibited              |
\***********************************************************************/
macro ProcessCashAccs

  var
    RecFound, TotCashDebetSum = $0L, TotCashCreditSum = $0L,
              TotDocsDebetSum = $0L, TotDocsCreditSum = $0L,
    header_printed = FALSE, str = "";

    ReWind(casacc);
    ClearRecord(casacc);
    casacc.Branch = Branch;
    casacc.Date = {curdate};
    RecFound = GetGE(casacc);
    while( RecFound and (casacc.Branch == Branch) and (casacc.Date == {curdate}) )
     if( ( (casacc.KredMoney) or (casacc.DebMoney)
                  or (casacc.KredCol) or (casacc.DebCol)    ) and ( casacc.SubAccount == "" ) )

      CashCreditSum = CashDebetSum = $0L;

      if( (casacc.KredMoney) or (casacc.DebMoney) )
         TreatValueAsMoney = TRUE;
         CashCreditSum = MoneyL(casacc.KredMoney);
         CashDebetSum = MoneyL(casacc.DebMoney);
      elif( (casacc.KredCol) or (casacc.DebCol) )
         TreatValueAsMoney = FALSE;
         CashCreditSum = MoneyL(casacc.KredCol);
         CashDebetSum = MoneyL(casacc.DebCol);
      end;

      if( (not Index(casacc.CashOper,"Кладовая"))
         and (not Index(casacc.CashOper,"Сейф")) )
         CollectDocsForOper;
      else
         CollectDocsForSafe;
      end;
/*
  MsgBox(String(casacc.CashOper) + " " + String(casacc.RefValue) + "|"+
         String(CashCreditSum) + " " +  String(DocsCreditSum) + "|"+
         String(CashDebetSum) + " " +  String(DocsDebetSum));
*/
      if( (CashCreditSum != DocsCreditSum) or (CashDebetSum != DocsDebetSum) )

        if(not header_printed)
 [                                  Расхождения по кассовым счетам
 ┌──────────────────────────────────────────────────┬───────────────┬──────────────────────────┬──────────────────────────┐
 │                                                  │               │    Обороты по дебету.    │     Обороты по кредиту   │
 │            Наименование ценности                 │ Операционист  ├─────────────┬────────────┼─────────────┬────────────┤
 │                                                  │               │  по счёту   │ по док-там │  по счёту   │ по док-там │
 ├──────────────────────────────────────────────────┼───────────────┼─────────────┼────────────┼─────────────┼────────────┤
 ];
           header_printed = TRUE;
        end;

        if( (CashCreditSum == DocsCreditSum) )
           CashCreditSum = DocsCreditSum = $0L;
        end;
        if( (CashDebetSum == DocsDebetSum) )
           CashDebetSum = DocsDebetSum = $0L;
        end;

        str = "│" + String(FindNameValue(casacc.RefValue):50:l) +
              "│" + String(casacc.CashOper:15:c) + "│" +  String(CashDebetSum:13:z) +
              "│" + String(DocsDebetSum:12:z) + "│" + String(CashCreditSum:13:z) +
              "│" + String(DocsCreditSum:12:z) + "│";

        [#](str);
        TotCashDebetSum = TotCashDebetSum + CashDebetSum;
        TotDocsDebetSum = TotDocsDebetSum + DocsDebetSum;
        TotCashCreditSum = TotCashCreditSum + CashCreditSum;
        TotDocsCreditSum = TotDocsCreditSum + DocsCreditSum;


      end;

     end;

      RecFound = Next(casacc);
    end;

    if(header_printed)

[└──────────────────────────────────────────────────┴───────────────┴─────────────┴────────────┴─────────────┴────────────┘
   Итого:                                                            ############# ############ ############# ############

   Расхождение                                         по дебету:      ################    по кредиту:   ###############
 ](TotCashDebetSum:z, TotDocsDebetSum:z,
   TotCashCreditSum:z, TotDocsCreditSum:z,
   (TotCashDebetSum - TotDocsDebetSum):z,
   (TotCashCreditSum - TotDocsCreditSum):z);
      anything_printed = TRUE;
    end;


end;


/***********************************************************************\
|  1998(c) Copyright R-Style SoftWare Lab.                              |
|          Unauthorised duplication in any form prohibited              |
\***********************************************************************/
macro Main

  Message("Обрабатываем документы по вкладам...");
  ProcessDepDocs;
  Message("Обрабатываем документы по кассовым операциям...");
  ProcessPayDocs;
  Message("Обрабатываем документы по ценным бумагам...");
  ProcessCapIsDocs;
  if(not anything_printed)
    MsgBox("Расхождений с первичными документами не найдено");
  end;

  Message("Обрабатываем счета кассы...");
  ProcessCashAccs;
  if(not anything_printed)
    [ Расхождений по наличности не найдено];
  end;


end;





  Main;
  End.