/* Отчеты по слиткам */

import ci_defs;
            
class ( TCIRecGenerator ) TIngotRecGenerator( _oper, _makeReport, _observer )

  var
    ingotDocInfo = TRecHandler( "ci_doc.4" ),
    ingotRecInfo = TRecHandler( "ci_rec.2");
  
  var
    totNumItems=0,
    totWeight=$0,
    totBalCost=$0,
    totTax=$0,
    totSum=$0,
    totMemSum=$0;
  
  var
    clNameArr = TArray,
    manufArr = TArray;
  
  macro find;
  
    return findCIMisc( CI_INGOT, capIssueRec.rec.Issue );
  end;
  
  macro OutSellRecHead;
  
    var
      branchStrNum = "",
      departNum = "",
      departName = "";

    if ( not makeReport )
      return;
    end;
  
    getBranchParams( branch, branchStrNum, departNum, departName );
    ingotRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    [
       Отделение № _______________       
       Филиал    № ###############                                                                                  
  
  
                                    		   КАССОВЫЙ ЖУРНАЛ
   
                                                по продаже мерных слитков    
  
                                             ###############################
  
                                                за ######################
  
  
    ]
    (
      branchStrNum,
      "("+ingotRecInfo.rec.Name+")":c,
      {curdate}:m
    );
    [+-----+---------------------+--------------------+--------+-------------------+------+-------+----------+--------------+-----------+---------+---------------+-----------------------------+-------------+];
    [|     |                     |                    |        |                   |      |       |          |              |           |         |               |    Мемориальные обороты     |   Подписи   |];
    [|     |	  	         |      Завод -       |        |                   |Кол-во| Масса |  Учетная |  Балансовая  |   Цена    |  Сумма  |    Внесено    |--------------+--------------|------+------|];
    [| п/п |  Ф.И.О.  полностью  |                    | Проба  |      № слитка     |слит- |       |   цена   |              |  продажи  |         |               |              |              |      |      |];
    [|     |                     |    изготовитель    |        |                   |ков   | в гр. | за грамм |  стоимость   | за  грамм |   НДС   |    в кассу    |  зачисление  |   списание   |контр.|кассир|];
    [|     |                     |                    |        |                   |      |       |          |              |           |         |               |              |              |      |      |];
    [+-----+---------------------+--------------------+--------+-------------------+------+-------+----------+--------------+-----------+---------+---------------+--------------+--------------+------+------+];
  end;
  
  macro outLineTail
  
    var
      i = 1, 
      endReached = false,
      clNameFrag, manufFrag;
  
      while ( not endReached )
        endReached = true;
        clNameFrag = manufFrag = "";
        if ( i < clNameArr.size )
           clNameFrag = clNameArr[i];
           endReached = false;
        end;
        if ( i < manufArr.size )
           manufFrag = manufArr[i];
           endReached = false;
        end;
        if ( not endReached )
          [       ##################### #################### ]
          ( 
            clNameFrag,
            manufFrag
          );
        end;
        i = i + 1;
      end;
  end;

  macro outSellRecLine;

    array
      tmp1, tmp2;
  
    var
      sum = $0, 
      memSum = $0,
      clName;
  
    ingotDocInfo.setRecordAddr(capIssueDoc,0,
      capIssueDoc.fldOffset("Info"),true);

    clName = ingotDocInfo.rec.LastName + " " +
      ingotDocInfo.rec.FirstName + " " +
      ingotDocInfo.rec.PatrName;
    if ( capIssueDoc.rec.Operation == O_INGOT_SELL_ACC )
      memSum = capIssueDoc.rec.Sum;
    else
      sum = capIssueDoc.rec.Sum;
    end; 

    strSplit( clName, tmp1, 21, 21, 1 );
    strSplit( ingotDocInfo.rec.Manufacturer, tmp2, 20, 20, 1 );

    copyArrayToTArray( tmp1, clNameArr );
    copyArrayToTArray( tmp2, manufArr );

    totNumItems = totNumItems + ingotDocInfo.rec.NumItems;
    totWeight = totWeight + ingotDocInfo.rec.Weight;
    totBalCost = totBalCost + capIssueDoc.rec.BalCost;
    totTax = totTax + ingotDocInfo.rec.Tax;
    totSum = totSum + sum;
    totMemSum = totMemSum + memSum;

    updateValCounter( capIssueDoc.rec.ApplicationKind, capIssueDoc.rec.ApplicationKey );

    if ( not makeReport )
      return;
    end;

    [       ##################### #################### ######## ################### ###### ####### ########## ############## ########### ######### ############### ##############                              ]
    (
      clNameArr[0],
      ManufArr[0],
      ingotDocInfo.rec.Hallmark:4:2:z,
      ingotDocInfo.rec.Series + " " + String( ingotDocInfo.rec.Number ),
      ingotDocInfo.rec.NumItems:z,
      ingotDocInfo.rec.Weight:4:2:z,
      ingotDocInfo.rec.CBRate:z,
      capIssueDoc.rec.BalCost:z,
      ingotDocInfo.rec.Rate:z,
      ingotDocInfo.rec.Tax:z,
      sum:z,
      memSum:z
    );
    outLineTail();
  end;
  
  macro SellRecTotals;

    var
      sum, 
      opName;
   
    ingotRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    if ( capIssueRec.rec.Operation == O_INGOT_SELL )
      opName = "Продажа";
      sum = totSum;
    else
      opName = "Продажа безнал.";
      sum = totMemSum;
    end;

    notifyObserver( CI_INGOT, capIssueRec.rec.Issue, capIssueRec.rec.Operation, opName + ": " + ingotRecInfo.rec.Name, sum );
  
    if ( not makeReport )
      return;
    end;

    [----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------];
    [И т о г о :                                                                    ###### #######            ##############             ######### ############### ##############                              ]
    (
      totNumItems,
      totWeight:4:2,
      totBalCost,
      totTax,
      totSum,
      totMemSum
    );
    [----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------];
    [ ];
    [Контролер _______________                 Принято к учету: Дата _____________              Дебет  счета № _________________                  Дебет  счета № _________________                             ];
    [ ];
    [Кассир __________________                 Бухгалтер__________________________              Кредит счета № _________________                  Кредит счета № _________________                             ];
    [ ];
    [                                                                                           Дебет  счета № _________________                                                                               ];                     
    [ ];
    [                                                                                           Кредит счета № _________________                                                                               ];                     
    [ ];
  end;
  
  macro sellRecBody;
  
    var
      recFound;
  
    capIssueDoc.rec.RecApplicKind=capIssueRec.rec.ApplicationKind;
    capIssueDoc.rec.RecApplicKey=capIssueRec.rec.ApplicationKey;
  
    recFound=capIssueDoc.getEQ;
    while(recFound
      and (capIssueDoc.rec.RecApplicKind==capIssueRec.rec.ApplicationKind)
      and (capIssueDoc.rec.RecApplicKey==capIssueRec.rec.ApplicationKey))
      if ( checkCIDoc )
        outSellRecLine;
      end;
      recFound=capIssueDoc.next;
    end;
  end;
  
  macro sellRec;
   
    totNumItems=0;
    totWeight=$0;
    totBalCost=$0;
    totTax=$0;
    totSum=$0;
    totMemSum=$0;
    resetValCounter;
    
    outSellRecHead; 
    sellRecBody;
    sellRecTotals;
  end;
  
  macro printRec( applicationKind, applicationKey )
        
    if ( not makeReport )
      return;
    end;

    capIssueRec.keyNum = 2;                                        
    capIssueRec.rec.ApplicationKind=applicationKind;
    capIssueRec.rec.ApplicationKey=applicationKey;
    if(not capIssueRec.getEQ )
      msgBox("Ошибка при печати ведомости");
      return;
    end;
    if((capIssueRec.rec.Operation==O_INGOT_SELL)
      or (capIssueRec.rec.Operation==O_INGOT_SELL_ACC)) 
      sellRec;
    end;
  end;
  
  macro proceed
    
    var
      recFound;
   
    ClearRecord(CapIssueRec);
    capIssueRec.rec.Branch=Branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_INGOT;
  
    recFound=capIssueRec.getGE;
    while(recFound
      and (capIssueRec.rec.Branch==Branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_INGOT))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Oper==oper)
        and (capIssueRec.rec.Brigade==operBrigade))
        if((capIssueRec.rec.Operation==O_INGOT_SELL) 
          or (capIssueRec.rec.Operation==O_INGOT_SELL_ACC)) 
          sellRec;
        end;
      end;
      recFound=capIssueRec.next;
    end;
  end;               

  macro isOpIncluded( issue, op )
    
    if ( ( op == O_INGOT_SELL ) or ( op == O_INGOT_SELL_ACC ) )
      return true;
    end;   

    return false;
  end; 

  InitTCIRecGenerator( _oper, _makeReport, _observer );
end;

macro ingotRecReport

  var
    gen = TIngotRecGenerator( {oper}, true );

  gen.proceed;
end;

macro ingotRec( applicationKind, applicationKey )

  var
    gen = TIngotRecGenerator( {oper}, true );

  gen.printRec( applicationKind, applicationKey );
end;

macro ingotReport

end;