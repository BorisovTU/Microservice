/***************************************************************************\
| WRAPPERS.MAC - Формирование и подсчет бандеролей                          |
| Программист Зубов В.Ю. 21-07-97                                           |
\***************************************************************************/
/* Принцип формирования бандеролей: бандероль, переданная из филиала,
   распадается на 2 бандероли - приход и расход. Каждая из бандеролей ведется
   в разрезе видов вкладов */

Import deprintr;

/* Cписок маркеров:
   00 - Вид вклада
   01 - Количество приходных документов по виду вклада
   02 - Сумма приходных документов по виду вклада
   03 - Количество расходных документов по виду вклада
   04 - Сумма расходных документов по виду вклада
   05 - Общая сумма приходных документов
   06 - Общая сумма расходных документов
   07 - Номер филиала
   08 - Дата
*/

file doc("sbdepdoc.dbt");  /* файл документов вкладчиков*/

/* определение переменных*/
Var
    Вид_Вклада,
    Приход_Докум_по_виду_вклада=0,
    Приход_Сумма_по_виду_вклада=$0,
    Расход_Докум_по_виду_вклада=0,
    Расход_Сумма_по_виду_вклада=$0,
    Общая_Сумма_Прихода=$0,
    Общая_Сумма_Расхода=$0,
    Номер_филиала,
    {curdate};  /* текущая дата */

array FillArr; /* Массив подстановок*/

array F_head, F_body, F_tail; /*Строки формы*/

F_head( 0) ="                         Сводная ведомость по бандеролям,                     ";
F_head( 1) ="                        передаваемых из филиала <07        >                  ";
F_head( 2) ="                                  за <08      >                               ";
F_head( 3) ="                                                                              ";
F_head( 4) ="╔════════════╦══════════════════════════════╦════════════════════════════════╗";
F_head( 5) ="║            ║          Приход              ║            Расход              ║";
F_head( 6) ="║Вид вклада  ╟──────┬───────────────────────╫──────┬─────────────────────────╢";
F_head( 7) ="║            ║Док-ты│      Сумма            ║Док-ты│      Сумма              ║";
F_head( 8) ="╠════════════╬══════╪═══════════════════════╬══════╪═════════════════════════╣";
F_body( 0) ="║<00        >║<01  >│<02                   >║<03  >│<04                     >║";
F_tail( 0) ="╟────────────╫──────┼───────────────────────╫──────┼─────────────────────────╢";
F_tail( 1) ="║            ║Итого:│<05                   >║Итого:│<06                     >║";
F_tail( 2) ="╚════════════╩══════╧═══════════════════════╩══════╧═════════════════════════╝";

array Массив_Вид_Вклада;
array Массив_Приход_Докум_по_виду_вклада;
array Массив_Приход_Сумма_по_виду_вклада;
array Массив_Расход_Докум_по_виду_вклада;
array Массив_Расход_Сумма_по_виду_вклада;

/***************************************************************************\
|  SetFillArr Заполнение массива подстановок                                |
\***************************************************************************/
MACRO SetFillArr
  /* заполняем массив подстановок */
  FillArr(0)  = String(Вид_Вклада);/* Вид вклада    */
  /* Количество приходных документов по виду вклада */
  FillArr(1)  = String(Приход_Докум_по_виду_вклада);
  /* Сумма приходных документов по виду вклада */
  FillArr(2)  = String(Приход_Сумма_по_виду_вклада);
  /* Количество расходных документов по виду вклада */
  FillArr(3)  = String(Расход_Докум_по_виду_вклада);
  /* Сумма расходных документов по виду вклада */
  FillArr(4)  = String(Расход_Сумма_по_виду_вклада);
  /* Общая сумма приходных документов */
  FillArr(5)  = String(Общая_Сумма_Прихода);
  /* Общая сумма расходных документов */
  FillArr(6)  = String(Общая_Сумма_Расхода);
  FillArr(7)  = String(NumFNCash); /* Номер филиала */
  FillArr(8)  = String({curdate});/* Дата */
/*Для отладки*/
/*
var i;
i=0;
while(i < asize(FillArr))
  println(FillArr(i));
  i=i+1;
end; /* while*/
*/
END;/*of MACRO SetFillArr*/

/***************************************************************************\
|  ReplaceMarks Замена маркеров на нужные значения                          |
\***************************************************************************/
MACRO ReplaceMarks(str)
 Var LineSize,         /* длина строки */
     LeftPos,RightPos, /* позиции '<' и '>'*/
     Len,              /* длина маркера */
     markNo,           /* номер маркера */
     tmpStr,           /* временная строка */
     newStr;           /* новая строка */
 NewStr = Str;
 LineSize = strlen(newStr);
 LeftPos  = Index(newStr,"<");
 RightPos = Index(NewStr,">");
 while((LeftPos != 0) AND (RightPos != 0) AND (LeftPos+2 < RightPos))
    Len = RightPos - LeftPos + 1;
    MarkNo = Int(SubStr(newStr,LeftPos+1,Len-2));
    if( Strlen(FillArr(MarkNo)) <= Len)
      tmpStr=MkStr(" ",Len);
      StrSet(tmpStr,1,FillArr(MarkNo));
    else
      /*Строка не влезает в отведенное место*/
      tmpStr = MkStr("*",Len);
    end; /* of IF */
    StrSet(newStr,LeftPos,tmpStr);
    LeftPos  = Index(newStr,"<");
    RightPos = Index(NewStr,">");
 end;/*of while*/
 /*SetParm(0,newStr);*/
 return NewStr;
END;/*of MACRO ReplaceMarks*/

/***************************************************************************\
|  PrintHead Печать заголовка формы                                         |
\***************************************************************************/
MACRO PrintHead
var i =0;
while(i < asize(F_head))
  PrintLn(ReplaceMarks(F_head(i)));
  i = i + 1;
end;/*of while*/
END;/*of MACRO PrintHead */

/***************************************************************************\
|  PrintBodyStr  Печать одной строки содержимого формы                      |
\***************************************************************************/
MACRO PrintBodyStr(i)
  Var Tmp =i;
  if( tmp >= asize(F_Body))
    tmp = asize(F_Body)-1; /* Повторяем последнюю строку */
  end; /* of IF */
  PrintLn(ReplaceMarks(F_body(tmp)));
END;/*of MACRO PrintBodyStr */

/***************************************************************************\
|  PrintBody  Печать содержимого формы                                      |
\***************************************************************************/
MACRO PrintBody
  var i=0;

  while(i < asize(Массив_Вид_Вклада))
      Вид_Вклада                  = Массив_Вид_Вклада(i);
      Приход_Докум_по_виду_вклада = Массив_Приход_Докум_по_виду_вклада(i);
      Приход_Сумма_по_виду_вклада = Массив_Приход_Сумма_по_виду_вклада(i);
      Расход_Докум_по_виду_вклада = Массив_Расход_Докум_по_виду_вклада(i);
      Расход_Сумма_по_виду_вклада = Массив_Расход_Сумма_по_виду_вклада(i);
      SetFillArr;
      PrintBodyStr(i);
     i = i + 1;
  end;/* IF */
END;/*of MACRO PrintBody */

/***************************************************************************\
|  PrintTail   Печать завершения формы                                      |
\***************************************************************************/
MACRO PrintTail
var i =0;
while(i < asize(F_tail))
  PrintLn(ReplaceMarks(F_tail(i)));
  i = i + 1;
end;/*of while*/
END;/*of MACRO PrintTail */

/***************************************************************************\
|  PrintForm Печать формы                                                   |
\***************************************************************************/
MACRO PrintForm
  Var i,index,found;
  /* Перебор документов за сегодня */
  KeyNum(doc,0);
  doc.isCur = NumFlagCur;
  doc.FNCash = NumFNCash;
  doc.Date_Document = {curdate};
  doc.Oper = 0;
  if(GetGE(doc))
    found=True;
    while((doc.isCur == NumFlagCur) AND
          (doc.FNCash == NumFNCash) AND
          (doc.Date_Document == {curdate}) AND
          (found == True))
      /* Ищем информацию о данном виде вклада */
      i=0;
      index = -1;
      while(i < asize(Массив_Вид_Вклада))
         if(Массив_Вид_Вклада(i) == doc.Type_Account)
           index = i;
         end;/* IF */
         i=i+1;
      end;/* WHILE */
      if(index == -1)
        index = asize(Массив_Вид_Вклада);
        Массив_Вид_Вклада(index) = doc.Type_Account;
        Массив_Приход_Докум_по_виду_вклада(index)=0;
        Массив_Приход_Сумма_по_виду_вклада(index)=$0;
        Массив_Расход_Докум_по_виду_вклада(index)=0;
        Массив_Расход_Сумма_по_виду_вклада(index)=$0;

      end; /* IF */
      if(doc.InSum != 0)
        Массив_Приход_Докум_по_виду_вклада(index)=
            Массив_Приход_Докум_по_виду_вклада(index)+1;
        Массив_Приход_Сумма_по_виду_вклада(index)=
            Массив_Приход_Сумма_по_виду_вклада(index)+doc.InSum;
        Общая_Сумма_Прихода = doc.InSum;
      else
        Массив_Расход_Докум_по_виду_вклада(index)=
            Массив_Расход_Докум_по_виду_вклада(index)+1;
        Массив_Расход_Сумма_по_виду_вклада(index)=
            Массив_Расход_Сумма_по_виду_вклада(index)+doc.OutSum;
        Общая_Сумма_Расхода = doc.OutSum;
      end;/* IF */
      if (not Next(doc))
        found=False;
      end;/* IF */
    end; /* WHILE */
  end; /* IF */
/*
  i=0;
 [#](Массив_Вид_Вклада(i));
 [#](Массив_Приход_Докум_по_виду_вклада(i));
 [#](Массив_Приход_Сумма_по_виду_вклада(i));
 [#](Массив_Расход_Докум_по_виду_вклада(i));
 [#](Массив_Расход_Сумма_по_виду_вклада(i));
*/
 SetFillArr;
 PrintHead;
 PrintBody;
 PrintTail;
END;/*of MACRO PrintForm */

/* Вызов печати формы */
PrintForm;