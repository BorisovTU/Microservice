/*
  RS-Bank (Сбербанк)
  Частные вклады
  Перенос информации о зачислениях на счета вкладчиков
  из текстового файла в файл отложенных документов

  Структура файла:
  ---------------------------------------------------------
  Номер поля | Тип, длина | Комментарий
  ---------------------------------------------------------
        1       N3         Номер документа
        2       N5         Номер пачки
        3       C25        Номер счета. Если счет необходимо открыть,
                           вместо номера счета в это поле помещаются
                           необходимые сведения (фамилия, имя, отчество,
                           год рождения и пр.)
        4       N19.2      Сумма (в копейках и центах)
        5       N1         Вид зачисления:
                           1 - Заработная плата
                           2 - Пенсия
                           3 - Выигрыш
                           4 - Прочее
                              ... + Все подвиды операции зачисления в пак.реж.
        6       C150       Основание / Инфо по клиенту

  Формат информации по клиенту
  ----------------------------

        6.1     С*         Фамилия
        6.2     С*         Имя
        6.3     С*         Отчество
        6.4     Date       Дата рождения
        6.5     С*         Документ, удостоверяющий личность
        6.6     С*         Адрес прописки

  ---------------------------------------------------------
  ВНИМАНИЕ! Разделители полей задаются в массиве Separate


  Создал: 01.08.96
  Ромодин Александр Васильевич

  Модификация: 05.08.97 Ромодин
*/
import BankInter,deprintr,"rc_file.mac","sb_rprt.mac";

private var {curdate};
private var {oper};

file InputFile() txt 250;       /* Исходный текстовый файл      */
file PostDoc(psdepdoc) write;   /* Файл отложенных документов   */
file Account(depositr);         /* Файл счетов вкладчиков       */
file Currency(currency);        /* Справочник валют             */
file psd_rep() txt 250 write;   /* Отчет о занесенных записях   */
file err_NoAcc() txt 250 write; /* Отчет по не существующим счетам      */
file err_NoAcc1() txt 250;     /* Отчет по не существующим счетам (на чтение)*/
file err_CloseAcc() txt 250 write;/* Отчет по закрытым счетам   */
file NewAcc() txt 250 write;     /* Отчет по вновь открытым счетам */
var  DepClient = TClientList;

DepClient.MayInsert = true;
/*
  Разделители
  ВНИМАНИЕ! Пробел в качестве разделителя не желателен, но если
  он используется в качестве разделителя, то в основании пробелы
  между словами не ставить
  Впереди каждого поля можно вставлять пробелы для выравнивания
*/
Array Separate;
Separate(0) = "!";
Separate(1) = ":";
Separate(2) = ";";
Separate(3) = ",";
Separate(4) = "|";
const NumberSeparate = ASize(Separate);
var SpaceSeparate = FALSE;      /* Есть ли пробел в разделителях */
var ii = 0;
while ((NOT SpaceSeparate) AND (ii < NumberSeparate))
  if (Separate(ii) == " ")
    SpaceSeparate = TRUE;
  end;
  ii = ii + 1;
end;
/*
  Константы
*/
const TypeAcc="ДО ВОСТРЕБ.";
const CurTypeAcc="до востреб.";
const LenAcc = 25;      /* Максимальная длина счета */
const PatternFile = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true ) + "*.psd"; /* Маска исходного текстового файла   */
const Name_rep =                "psd_load.txt"; /* Отчет по занесенным записям */
const Name_err_NoAcc =          "psdl_no.txt";  /* Отчет по новым счетам */
const Name_err_CloseAcc =       "psdl_cl.txt";  /* Отчет по закрытым счетам */
const Name_NewAcc=              "psd_new.txt";
const TypeOper = 73;        /* Тип операции- пакетное зачисление        */
const OpenNCash = 51;
const ApplType = 1;         /* Вид зачисления- зар.плата (по умолчанию) */
const AT_Rejected = 50;
const HeadHeight = 15;

const FNcash  = NumFNcash();    /* Номер филиала        */
const FlagCur = NumFlagCur();   /* Код системы          */
/*
  Глобальные переменные
*/
var NameInputFile      = "";    /* Имя Операционного файла      */
var StrFile            = "";    /* Строка Операционного файла   */
var KolStrErr = 0;              /* Количяество ошибок при записи*/
var KolStrFile;                 /* Номер строки и количество
                                   строк в Операционном файле   */
var KolStrInp = 0;              /* Количество введенных записей */
Array Code_Currency;            /* Код валюты записей           */
Array Sum_Credit;               /* Сумма по кредиту             */
/*
  Параметр TEXTDIR файла bank.ini (для вывода отчетов)
*/
var ReportFile = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true );
/*var ReportFile = GetIniString("TEXTDIR","bank.ini");
var len = StrLen(ReportFile);
if (len > 0)
  if (SubStr(ReportFile,len,1) != "\\")
    ReportFile = ReportFile + "\\";
  end;
end;*/
var Kol_err_NoAcc = 0;          /* Количество записей с ошибками */
var Kol_err_CloseAcc = 0;
var Kol_NewAcc = 0;
Array Code_Currency_err_CloseAcc;
var   Sum_Credit_err_NoAcc = 0;     /* Сумма по кредиту записей с ошибками */
var   Sum_Credit_NewAcc=0;
Array Sum_Credit_err_CloseAcc;
var Str_Err = "";
var ForNewAcc;

/***********************************************************
***********************************************************/

macro Описание_ошибки (ErrCode)
/*
  Строка описания ошибки

  27.01.97
*/
  var StrErr = "";

  if (ErrCode == 0)
    StrErr = "Нет ошибки";
  elif (ErrCode == 1)
    StrErr = "Не найден счет";
  elif (ErrCode == 2)
    StrErr = "Счет закрыт";
  elif (ErrCode == 3)
    StrErr = "Неверно задана сумма";
  elif (ErrCode == 4)
    StrErr = "Неверно задан вид операции";
  elif (ErrCode == 5)
    StrErr = "Неверно задан номер документа";
  elif (ErrCode == 6)
    StrErr = "Неверно задан номер пачки";
  end;
  return StrErr;
end;
/***********************************************************
        Макросы обслуживания
***********************************************************/

macro GetNameCurrency (Code_Currency)
/*
  Возвращает короткое имя валюты

  27.01.97
*/
  var ShCur = "";

  Currency.Code_Currency = Code_Currency;
  if (getEQ(Currency))
    ShCur = Currency.Short_Name;
  end;
  return ShCur;
end;
/**********************************************************/

macro GetAccount (Acc)
/*
  Становится на строку счета
*/
  var stat = FALSE;

  KeyNum (Account, 7);
  Account.FNCash = FNcash;
  Account.Account = Acc;
  stat = getEQ (Account);
/*  if (stat AND Account.IsCur != FlagCur)
    stat = FALSE;
  end;*/
  return stat;
end;
/**********************************************************/

macro GetSeparateStr
/*
  Отделяет от StrFile начало строки до первого разделителя
  и смещает начало StrFile на следующий после разделителя символ
*/
  var Str = "";
  var Sym = "";
  var Cont = TRUE;
  var i = 1,
      j = 0,
      k = 1;
  var Len = StrLen(StrFile);
  var SymSpace =TRUE;

  while ((i <= Len) AND Cont)
    Sym = SubStr (StrFile,k,1);
    if (SymSpace AND (Sym == " "))
      StrFile = SubStr (StrFile,2);     /* Чистка от пробелов */
    else
      SymSpace = FALSE;
      j = 0;
      while (j < NumberSeparate)
        if (Sym == Separate(j))
          Cont = FALSE;
        end;
        j = j + 1;
      end;
      k = k + 1;
    end;
    i = i + 1;
  end;
  if (k > 1)
    if (NOT Cont)
      Str = SubStr (StrFile,1,k-2);
    else
      Str = SubStr (StrFile,1,k-1);
    end;
    StrFile = SubStr (StrFile,k);
  end;
  return Str;
end;
/**********************************************************/

macro IsInt (Str)
/*
  Записано ли в строке целое число (без знака)?

  27.01.97
*/
  var stat = TRUE;
  var Len = StrLen(Str);
  var i = 1;
  var Sym;
  var EndSpace = FALSE;

  while ((i <= Len) AND stat)
    Sym = SubStr(Str,i,1);
    if (Sym == " ")
      if (EndSpace)
        stat = FALSE;
      end;
    elif ((Sym < "0") OR (Sym > "9"))
      stat = FALSE;
    else
      EndSpace = TRUE;
    end;
    i = i + 1;
  end;
  return stat;
end;
/***********************************************************
***********************************************************/

macro Запись_в_отчет ()
/*
  Заносит информацию по обработанной строке в отчет
*/
  var str;
  str = "  " +
   String(PostDoc.NDoc:3) + " " + String(PostDoc.NPack:5) + " " +
   String(Acc_Form(PostDoc.Account):25) + " " + String(PostDoc.InSum:a:25) + " " +
   GetNameCurrency(PostDoc.Code_Currency);
  Insert (psd_rep, str);
end;
/***********************************************************/

macro Запись_в_отчет_отсутствующих ()
/*
*/
  var str;
  str = "  " +
   String(PostDoc.NDoc:3) + "|" + String(PostDoc.NPack:5) + "|" +
   String(PostDoc.InSum:25) +
     "|" + String(PostDoc.ApplType:2) + "|" + PostDoc.Ground;
  Insert (err_NoAcc, str);
end;
/***********************************************************/

macro Запись_в_отчет_закрытых ()
/*
*/
  var str;
  str = "  " +
   String(PostDoc.NDoc:3) + " " + String(PostDoc.NPack:5) + " " +
   String(Acc_Form(PostDoc.Account):25) + " " + String(PostDoc.InSum:a:25) + " " +
   GetNameCurrency(PostDoc.Code_Currency);
  Insert (err_CloseAcc, str);
end;

macro Запись_в_отчет_новых (ФИО,ДатаРожд,НомерСч)
/*
*/
  var str;
  str = "  " +
   String(ФИО:20) + " " + String(ДатаРожд:10) + " " +
   String(НомерСч:25);
  Insert (NewAcc, str);
end;
/**********************************************************/
macro Разделитель_1_1
  var str;
  str =" ----------------------------------------------------------";
  return str;
end;
/**********************************************************/

macro Главный_Заголовок_отчета
/*
  Шапка заголовка для всех отчетов
*/
  var str;
  str = " \n" +
  "  Копирование информации по зачислениям на счета вкладчиков \n" +
  "    из текстового файла в Отложенные документы вкладчиков \n" +
  " \n" +
  " Обработан файл: " + NameInputFile + "\n" +
  " Дата обработки: " + String({curdate}) + "\n" +
/*  " Операционист:   " + String({oper}) + "\n" */
  " \n";
  return str;
end;
/**********************************************************/

macro Заголовок_отчета
/*
  Шапка отчета по занесенным счетам
*/
  var str;
  str = Главный_Заголовок_отчета ();
  Insert (psd_rep,str);
  str = Разделитель_1_1();
  Insert (psd_rep,str);
  str = "  N  |Пачка|   Счет          |    Сумма зачисления         \n"+
        " д-та|     |                 |                             \n" +
        " ------------------------------------------------------------";
  Insert (psd_rep,str);
end;
/**********************************************************/

macro Заголовок_отсутствующих_счетов
/*
  Шапка отчета по отсутствующим счетам
*/
  var str;
  str = Главный_Заголовок_отчета ();
  Insert (err_NoAcc, str);
  str = "           Список новых счетов \n ";
  Insert (err_NoAcc, str);
  str = Разделитель_1_1();
  Insert (err_NoAcc,str);
  str = "  N  |Пачка|    Сумма зачисления     |ВЗ| Параметры нового счета \n"+
        " д-та|     |                         |  | \n" +
  Разделитель_1_1();
  Insert (err_NoAcc,str);
end;

macro Заголовок_новых_счетов
/*
  Шапка отчета по отсутствующим счетам
*/
  var str;
  str = Главный_Заголовок_отчета ();
  Insert (NewAcc, str);
  str = "           Список вновь открытых счетов \n ";
  Insert (NewAcc, str);
  str = Разделитель_1_1();
  Insert (NewAcc,str);
  str="   Фамилия И.О.       |  Дата р. | Номер счета \n"+
      "                      |          |\n"+
      " ------------------------------------------------------------";
  Insert (NewAcc,str);
end;
/**********************************************************/

macro Заголовок_закрытых_счетов
  var str;
  str = Главный_Заголовок_отчета ();
  Insert (err_CloseAcc,str);
  str = "              Закрытые счета \n \n" +
  Разделитель_1_1();
  Insert (err_CloseAcc,str);
  str = "  N  |Пачка|   Счет          |    Сумма зачисления         \n"+
        " д-та|     |                 |                             \n" +
  Разделитель_1_1();
  Insert (err_CloseAcc,str);
end;
/***********************************************************
***********************************************************/

macro Конец_отчета
/*
  Вывод итоговых строк в отчет
*/
  var i = 0;
  var kol_cur = Asize (Code_Currency);
  var str;

  str = Разделитель_1_1 ();
  Insert (psd_rep, str);
  while (i < kol_cur)
    str = "      Итого:                  " +
     String(Money(Double(Sum_Credit(i))):a:25) + " " +
     String(GetNameCurrency (Code_Currency(i)):3);
    Insert (psd_rep, str);
    i = i + 1;
  end;
  str = " \n" +
  " Общее число обработанных записей: " + String(KolStrFile) + " \n" +
  " \n" +
  " Введено в базу данных " + String(KolStrInp) + " отложенных документов \n" +
  " \n";
  Insert (psd_rep, str);
  if (KolStrErr > 0)
    str = " Не занесено в базу данных из-за системной ошибки: "+String(KolStrErr);
    Insert (psd_rep, str);
  end;
  str = " ";
  Insert (psd_rep, str);
end;
/***********************************************************/

macro Конец_отчета_отсутствующих
/*
*/
  var i = 0;
  var str;

  str = Разделитель_1_1 ();
  Insert (err_NoAcc, str);
    str = "     Итого: " +
     String(Money(Double(Sum_Credit_err_NoAcc)):a:25);
    Insert (err_NoAcc, str);
    i = i + 1;
  str = " \n" +
  " Общее число обработанных записей: " + String(KolStrFile) + " \n" +
  " \n" +
  " Необходимо открыть новых счетов: " + String(Kol_err_NoAcc) + " \n";
  Insert (err_NoAcc, str);
end;
/***********************************************************/
macro Конец_отчета_новых
/*
*/
  var i = 0;
  var str;

  str = Разделитель_1_1 ();
  Insert (NewAcc, str);
  str = " \n" +
  " Общее число обработанных записей: " + String(KolStrFile) + " \n" +
  " \n" +
  " Открыто новых счетов: " + String(Kol_NewAcc) + " \n";
  Insert (NewAcc, str);
end;
/***********************************************************/

macro Конец_отчета_закрытых
/*
*/
  var i = 0;
  var kol_cur = Asize (Code_Currency_err_CloseAcc);
  var str;

  str = Разделитель_1_1 ();
  Insert (err_CloseAcc, str);
  while (i < kol_cur)
    str = "      Итого:                  " +
     String(Money(Double(Sum_Credit_err_CloseAcc(i))):a:25) + " " +
     String(GetNameCurrency (Code_Currency_err_CloseAcc(i)):3);
    Insert (err_CloseAcc, str);
    i = i + 1;
  end;
  str = " \n" +
  " Общее число обработанных записей: " + String(KolStrFile) + " \n";
  Insert (err_CloseAcc, str);
    str = "\n Не занесено в базу данных по закрытым счетам: "+String(Kol_err_CloseAcc);
    Insert (err_CloseAcc, str);
  str = " ";
  Insert (err_CloseAcc, str);
end;
/***********************************************************
***********************************************************/

macro Инициализация_отложенного
/*
  Разбирает строку исходного файла и инициализирует
  отложенный документ
*/
  var i = 0;
  var InputString;
  var stat = 0;
  Str_Err = "";
  ForNewAcc = "";
/*
  Номер документа
*/
  InputString = GetSeparateStr ();
  if (NOT ((InputString != "") AND IsInt(InputString)))
    Str_Err = Str_Err + Описание_ошибки (5);
    PostDoc.NDoc = 0;
  else
    PostDoc.NDoc = Int(InputString);
  end;
/*
  Номер пачки
*/
  InputString = GetSeparateStr ();
  if (NOT (InputString != "" AND IsInt(InputString)))
    Str_Err = Str_Err + Описание_ошибки (6);
    PostDoc.NPack = 0;
  else
    PostDoc.NPack = Int(InputString);
  end;
/*
  Номер счета
*/
  InputString = GetSeparateStr ();
  if (StrLen(InputString) > LenAcc)
    stat = -1;
  else
    if (NOT GetAccount (InputString))
      stat = 1;
    else
      if (Account.Open_Close == "З")
        stat = 2;
      end;
    end;
  end;
  if (stat >= 0)
    PostDoc.Account = InputString;
  else
    ForNewAcc = InputString;
  end;
/*
  Сумма
*/
  InputString = GetSeparateStr ();
  if (NOT (InputString != "" AND IsInt(InputString)))
    Str_Err = Str_Err + Описание_ошибки (3);
    PostDoc.InSum = 0;
  else
    PostDoc.InSum = Money(Double(InputString));
  end;
/*
  Вид зачисления
*/
  InputString = GetSeparateStr ();
  if (NOT (InputString != "" AND IsInt(InputString)))
    Str_Err = Str_Err + Описание_ошибки (4);
  else
    PostDoc.ApplType = Int(InputString);
    if ((PostDoc.ApplType < 1) OR (PostDoc.ApplType > 99))
      Str_Err = Str_Err + Описание_ошибки (4);
    end;
  end;
/*
  Основание
*/
  PostDoc.Ground = StrFile;
/*
  Инициализация остальных полей
*/
  if (stat >= 0)
    PostDoc.Referenc = Account.Referenc;
    PostDoc.NumDayDoc = 0;
    PostDoc.Type_Account = Account.Type_Account;
    PostDoc.FNCash = FNcash;
    PostDoc.IsCur = Account.IsCur;
    PostDoc.Date_Document = {curdate};
    PostDoc.Code_Currency = Account.Code_Currency;
    PostDoc.Oper = {oper};
    PostDoc.VidDoc = 0;
    PostDoc.KindOp = 5;
    /* PostDoc.Result_Carry = 0; */
    PostDoc.OutSum = 0;
    PostDoc.Rest = 0;
    PostDoc.PercOprSum = 0;
    PostDoc.PercRest = 0;
    /* PostDoc.Account_Receiver = ""; */
    /* PostDoc.CodeCur_Receiver = -1; */
    /* PostDoc.MFO_Receiver = ""; */
    /* PostDoc.CorAcc_Receiver = ""; */
    /* PostDoc.CAccount = ""; */
    PostDoc.ArDate = Date (0,0,0);
    PostDoc.YesSbook = "X";
    PostDoc.YesForm = "X";
    /* PostDoc.KindOpSave = 0; */
    PostDoc.iApplicationKind = 0;
    PostDoc.ApplicationKey = "";
    PostDoc.CodClient = Account.CodClient;
    PostDoc.TypeOper = TypeOper;
    PostDoc.NotConfirm = "";
    PostDoc.TypeComplexOper = 0;
    /* PostDoc.ArchievedDocBooked = 0; */
  end;
  return stat;
end;
/**********************************************************/

macro Валюта_основного
  var i = 0,
      kol = 0;
  var Cont = TRUE;

  kol = ASize(Code_Currency);
  if (kol > 0)
    while ((i < kol) AND Cont)
      if (Code_Currency(i) == PostDoc.Code_Currency)
        Sum_Credit(i) = Sum_Credit(i) + PostDoc.InSum;
        Cont = FALSE;
      end;
      i = i + 1;
    end;
  end;
  if (Cont)
    Code_Currency(kol) = PostDoc.Code_Currency;
    Sum_Credit(kol) = PostDoc.InSum;
  end;
end;
/**********************************************************/

macro Валюта_закрытых
  var i = 0,
      kol = 0;
  var Cont = TRUE;

  kol = ASize(Code_Currency_err_CloseAcc);
  if (kol > 0)
    while ((i < kol) AND Cont)
      if (Code_Currency_err_CloseAcc(i) == PostDoc.Code_Currency)
        Sum_Credit_err_CloseAcc(i) = Sum_Credit_err_CloseAcc(i) + PostDoc.InSum;
        Cont = FALSE;
      end;
      i = i + 1;
    end;
  end;
  if (Cont)
    Code_Currency_err_CloseAcc(kol) = PostDoc.Code_Currency;
    Sum_Credit_err_CloseAcc(kol) = PostDoc.InSum;
  end;
end;
/**********************************************************/

macro Обработка_строк_исходного_файла
/*
  Организует цикл по строкам файла
  Вызывает макрос инициализации отложенного
  Записывает очередной отложенный документ
*/
  var stat = 0;

  ReWind (InputFile);
  KolStrFile = 0;
  KolStrErr = 0;
  KolStrInp = 0;
  Kol_err_NoAcc = 0;
  Kol_err_CloseAcc = 0;

  while (Next(InputFile))
    StrFile = InputFile.str;
    KolStrFile = KolStrFile + 1;
    stat = Инициализация_отложенного();
    if (stat == 0)
      Запись_в_отчет ();
    elif (stat == 1)
      Запись_в_отчет_отсутствующих ();
      PostDoc.ApplType = AT_Rejected;
      Kol_err_NoAcc = Kol_err_NoAcc + 1;
      Sum_Credit_err_NoAcc = Sum_Credit_err_NoAcc + PostDoc.InSum;
    elif (stat == 2)
      Запись_в_отчет_закрытых ();
      PostDoc.ApplType = AT_Rejected;
      Kol_err_CloseAcc = Kol_err_CloseAcc + 1;
      Валюта_закрытых ();
    end;
    if ((stat >= 0) and (stat != 1))
      if (NOT Insert (PostDoc))
        Insert (psd_rep," Ошибка при занесении документа в файл отложенных ");
        KolStrErr = KolStrErr + 1;
      else
        KolStrInp = KolStrInp + 1;
        Валюта_основного ();
      end;
      if (Str_Err)
        Insert (psd_rep, Str_Err);
        Str_Err = "";
      end;
    end;
  end;
end;
/**********************************************************/

macro Обработка_исходного_файла
/*
  - Отделяет имя исходного файла от пути
  - Вызывает макрос копирования информации из исходного файла
    в файл Отложенных документов вкладчиков
*/
  var str;
  str = Строка_Начало_Конец_отчета ();
  Insert (psd_rep,str);
  Insert (err_NoAcc,str);
  Insert (err_CloseAcc,str);
  Заголовок_отчета ();
  Заголовок_отсутствующих_счетов ();
  Заголовок_закрытых_счетов ();

  Обработка_строк_исходного_файла ();

  Конец_отчета ();
  Конец_отчета_отсутствующих ();
  Конец_отчета_закрытых ();
  str = Строка_Начало_Конец_отчета ();
  Insert (psd_rep,str);
  Insert (err_NoAcc,str);
  Insert (err_CloseAcc,str);
end;

macro ОбработкаСтрокОтчетаПоОтсутствующим();

  record Parm("AccParm.rec");
  var
    rec_found,i,stat,
    NDoc,
    NPack,
    InSum,
    ApplType,
    LastName,
    FirstName,
    PatrName,
    BirthDate,
    PersID,
    Address,
    Account,
    FIO,
    dtmp;

  Rewind(err_NoAcc1);
  i=0;
  while(i<=HeadHeight)
    if(not Next(err_NoAcc1))
      MsgBox("Ошибка чтения файла");
      return;
    end;
    i=i+1;
  end;

  i=0;
  while(i<Kol_err_NoAcc)
    stat=false;
    StrFile=err_NoAcc1.Str;
    NDoc=Int(GetSeparateStr());
    NPack=Int(GetSeparateStr());
    InSum=Money(Double(GetSeparateStr()));
    ApplType=Int(GetSeparateStr());
    LastName=GetSeparateStr();
    FirstName=GetSeparateStr();
    PatrName=GetSeparateStr();
    dtmp=Trim(GetSeparateStr());
    BirthDate=Date(
      Int(SubStr(dtmp,1,2)),
      Int(SubStr(dtmp,4,2)),
      Int(SubStr(dtmp,7,4)));
    PersID=GetSeparateStr();
    Address=GetSeparateStr();
    FIO=LastName+" "+SubStr(FirstName,1,1)+"."+SubStr(PatrName,1,1)+".";

    DepClient.CurRec.Clear();
    DepClient.CurRec.rec.CodClient = DepClient.MakeClientCode();
    DepClient.CurRec.rec.Name1  = LastName;
    DepClient.CurRec.rec.Name2  = FirstName;
    DepClient.CurRec.rec.Name3  = PatrName;
    DepClient.CurRec.rec.Born   = BirthDate;
    DepClient.CurRec.rec.FNcash = NumFNCash();
    DepClient.CurRec.SplitDocStr (PersID);
    DepClient.CurRec.rec.Address= Address;

    if(DepClient.CurRec.rec.CodClient)
      if(not DepClient.Insert())
        MsgBox("Ошибка при создании нового вкладчика");
      end;
      ClearRecord(Parm);
      Parm.CodClient=DepClient.CurRec.rec.CodClient;
      if(FlagCur)
        Parm.Code_Currency=1;
      else
        Parm.Code_Currency=0;
      end;
      if(FlagCur)
        Parm.TypeAccount="";
      else
        Parm.TypeAccount="";
      end;
      Parm.FlagNew=1;
      Parm.Operate=OpenNCash;
      Parm.ApplType=ApplType;
      Parm.InSum=InSum;
      if(EditDepositor(Parm))
        stat=true;
        Account=Parm.Account;
        Kol_NewAcc=Kol_NewAcc+1;
        Sum_Credit_NewAcc=Sum_Credit_NewAcc+InSum;
      else
        Account="Не открыт";
      end;
    else
      Account="Не открыт";
    end;
    if(not stat)
      PostDoc.NDoc=NDoc;
      PostDoc.NPack=NPack;
      PostDoc.ApplType=Parm.ApplType;
      PostDoc.Account="";
      PostDoc.Referenc = "";
      PostDoc.NumDayDoc = 0;
      PostDoc.Type_Account = Parm.TypeAccount;
      PostDoc.FNCash = FNcash;
      PostDoc.IsCur = FlagCur;
      PostDoc.Date_Document = {curdate};
      PostDoc.Code_Currency = Parm.Code_Currency;
      PostDoc.Oper = {oper};
      PostDoc.VidDoc = 0;
      PostDoc.KindOp = 5;
      /* PostDoc.Result_Carry = 0; */
      PostDoc.InSum = Parm.InSum;
      PostDoc.OutSum = 0;
      PostDoc.Rest = 0;
      PostDoc.PercOprSum = 0;
      PostDoc.PercRest = 0;
      /* PostDoc.Account_Receiver = ""; */
      /* PostDoc.CodeCur_Receiver = -1; */
      /* PostDoc.MFO_Receiver = ""; */
      /* PostDoc.CorAcc_Receiver = ""; */
      /* PostDoc.CAccount = ""; */
      PostDoc.ArDate = Date (0,0,0);
      PostDoc.YesSbook = "X";
      PostDoc.YesForm = "X";
      /* PostDoc.KindOpSave = 0; */
      PostDoc.iApplicationKind = 0;
      PostDoc.ApplicationKey = "";
      PostDoc.CodClient = Parm.CodClient;
      PostDoc.TypeOper = Parm.Operate;
      PostDoc.NotConfirm = "";
      PostDoc.TypeComplexOper = 0;
      /* PostDoc.ArchievedDocBooked = 0; */
      PostDoc.Ground="";
      if(not Insert(PostDoc))
        MsgBox("Ошибка при создании отложенного документа");
      end;
    end;
    Запись_в_отчет_новых(FIO,BirthDate,Account);
    Next(err_NoAcc1);
    i=i+1;
  end;
end;

macro CreateNewAccounts(n)

  var str;
  if((not Open(NewAcc,ReportFile + Name_NewAcc)) or
    (not Open(err_NoAcc1,ReportFile + Name_err_NoAcc)))
    MsgBox("Файлы не открыты");
    return;
  end;
  str = Строка_Начало_Конец_отчета ();
  Insert (NewAcc,str);
  Заголовок_новых_счетов ();

  ОбработкаСтрокОтчетаПоОтсутствующим();

  Конец_отчета_новых();
  str = Строка_Начало_Конец_отчета ();
  Insert (NewAcc,str);
end;


/***********************************************************
############################################################
***********************************************************/

/*
  Головной макрос

  - Организует выбор исходного файла из списка
  - Вызывает макрос обработки исходного файла
*/
  var PathName = "";
  var len;

  if (SelectFile(NameInputFile,PatternFile,"Выберите исходный файл"))
    if (Open(InputFile,NameInputFile)                   AND
     Open (psd_rep, ReportFile + Name_rep)              AND
     Open (err_NoAcc, ReportFile + Name_err_NoAcc)      AND
     Open(err_CloseAcc,ReportFile + Name_err_CloseAcc))
      Обработка_исходного_файла ();
      Close (psd_rep);
      if (KolStrInp > 0)
        ViewFile (psd_rep);
      end;
      Close (err_NoAcc);
      if (Kol_err_NoAcc > 0)
        ViewFile (err_NoAcc);
        CreateNewAccounts;
        Close(NewAcc);
        if(KolStrFile)
          ViewFile(NewAcc);
        end;
      end;
      Close (err_CloseAcc);
      if (Kol_err_CloseAcc > 0)
        ViewFile (err_CloseAcc);
      end;
    else
      [ Ошибка при открытии файлов ];
    end;
  else
    [ Отказались от выбора исходного текстового файла ];
  end;
end;