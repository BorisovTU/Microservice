/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Операция по установке ареста счета вкладчика                            *
*                                                                          *
\**************************************************************************/

import deprintr, alg_vars;

const UPDATE_ALL  = 1;
const UPDATE_DEP  = 2;

file depo (depositr);

record depositr_pre (depositr);

const LogSC_Update      = 3;
const LogSC_sb_depositr = 501;


/**************************************************************************\
| Закрытие плановых перечислений                                           |
\**************************************************************************/
private macro closePPays

  var ret   = true,
      stat  = true,
      dppay = TBFile( "dplanpay.dbt", "W" );

  dppay.rewind(); dppay.clear();

  dppay.rec.IsCur      = ALG_DEPOSITOR.IsCur;
  dppay.rec.FNCash     = ALG_DEPOSITOR.FNCash;
  dppay.rec.Open_Close = strFor(0);
  ret = dppay.getGE();
  while( ( ret                  == true                  ) and
         ( stat                 == true                  ) and
         ( dppay.rec.IsCur      == ALG_DEPOSITOR.IsCur   ) and
         ( dppay.rec.FNCash     == ALG_DEPOSITOR.FNCash  ) and
         ( dppay.rec.Open_Close == strFor(0)             )    )
    /* открытое ПП */
    if( ( dppay.rec.Account       == ALG_DEPOSITOR.Account       ) and
        ( dppay.rec.Code_Currency == ALG_DEPOSITOR.Code_Currency )    )
      dppay.rec.Open_Close = "A";
      stat = dppay.update();
      if( stat )
        dppay.rec.IsCur      = ALG_DEPOSITOR.IsCur;
        dppay.rec.FNCash     = ALG_DEPOSITOR.FNCash;
        dppay.rec.Open_Close = strFor(0);
        ret = dppay.getGE();
      end;
    else
      ret = dppay.next();
    end;
  end;

  return stat;

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro TrnProc

  var stat  = TRUE;

  if ( stat )
    Copy( depositr_pre, ALG_DEPOSITOR );
  end;

  if ( stat )
    ALG_DEPOSITOR.UserTypeAccount = ALG_DEPOSITOR.UserTypeAccount + "Т";
    ALG_DEPOSITOR.NumSession      = 0;
    if ( ALG_DEPOSITOR.Action == 0 )
      ALG_DEPOSITOR.Action        = 1;
    end;
    stat = UpdateAlgRecords( UPDATE_DEP, TRUE );
  end;

  if (stat)
    WriteChronologicalLog(LogSC_Update,LogSC_sb_depositr,ALG_DEPOSITOR,0,depositr_pre,0);
  end;

  if (stat)
    stat = closePPays();
  end;

  if (NOT stat)
    AbortTrn();
  end;

end;

/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  ALG_RESULT = OK_RES;

  if (ALG_RESULT == OK_RES)
    if (Index(ALG_DEPOSITOR.UserTypeAccount,"Т"))
      MsgBox("Счет уже арестован");
      ALG_RESULT = END_RES;
    end;
  end;

  if (ALG_RESULT == OK_RES)
    if (ProcessTrn(1,"TrnProc",depo))
      ALG_RESULT = SKIP_RES;
      [
        Операция завершилась успешно.
        Арест установлен на счет.
      ];
    else
      MsgBox("Ошибка во время проведения операции");
      ALG_RESULT = ERR_RES;
    end;
  end;

end;
