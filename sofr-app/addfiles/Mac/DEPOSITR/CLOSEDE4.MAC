/***************************************************************************\
| CLOSEDE4.MAC - макрос закрытия счета для вида вклада "Срочный с ежеме-    |
| сячной выдачей %% "                                 Зубов В.Ю. 20-10-97   |
\***************************************************************************/
import deprintr, denomin, getpcli4, Календарь, IsSrvDoc, alg_vars, PmPropUtil;

private var doc = TDepFile( "sbdepdoc.dbt", "r" );
record tmpDoc          ("sbdepdoc.1"  );
record FINISH_SBDEPDOC ("sbdepdoc.1"  );
file   fff             ("sbdepdoc.dbt");
record save_SBDEPDOC   (sbdepdoc      );
record save_DEPOSITOR  (depositr      );
record pay_doc_comm    (pay_doc       );

const ОБЛОМИТЬ_ТРАНЗАКЦИЮ = -123; /* Откатить транзакцию */
const ОТКАТ_С_РЕЗУЛЬТАТОМ = -777; /* Откат с получением процентов */

const CDT_SB_DEPOSIT =   1;
const CVT_NONCASH    =   2; /* Безналичная конверсия */
const SB_CASHOPERT   = 200; /* Документ для сервисно-кассовых операций */

const UPDATE_ALL  = 1;
const UPDATE_DEP  = 2;
const UPDATE_DOC  = 4;

const OnlyInputSum_Commission  = 1;
const OnlyInsertDoc_Commission = 2;

var Результат = 0;
var ErrMsg    = "";
var IsFirst   = TRUE;

var ConvertSum     = $0L;
var ConvertSumComm = $0L;

var SummaComm = $0L;
var TypeComm  = "";
var NumOpert  = 0;

var NeedPreClose = FALSE;

var Operation = 0;

ALG_RESULT = OK_RES; /* заранее инициализируем код возврата */
ALG_UPDATE = 0;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro CloseDep

  var
    Отчислено = $0L, Начислено = $0L,
    Зачислено,Зачислено_Всего = $0L, ВыданоПроцентов_Всего = $0L,
    ВыданоПроцентов_БезПроцентов = $0L, СуммаДопВзносов = $0L,
    Дата_последнего_расчета, MaxDate,
    Сумма, СуммаПроцентов, ОстатокНаКонецМесяца = $0L,
    С_Панелью,Проценты = $0L, found,
    Использовать_Макс_Дату, Не_Печатать, UseMaxDate, NoPrint,
    Сумма_прокатки_остатка = $0L,
    Начисление_Отчисление  = $0L,
    Отчислен_Налог = $0L, Налог_На_Отчислено = $0L;
  var Больше_месяца_со_дня_открытия = True;
  var Открыт_не_в_текущем_месяце    = True;
  var Sum_pp = $0L;
  var PromDate;
  var day, mon, year;
  var day1, mon1, year1;
  var Дата_Конца_Расчета, Включать_день_расхода;
  var tmpdate1;

  CheckDenom(ALG_DEPOSITOR.Referenc);

  /* Определяем, сколько лежит вклад- больше месяца или нет */
  PromDate = DateAfterCalenMonths(ALG_SBDEPDOC.DepDate_Document,-1);

  if (PromDate <= ALG_DEPOSITOR.Open_Date)
    Больше_месяца_со_дня_открытия = FALSE;
    ALG_DEPOSITOR.UseAlternate = 1;
    UpdateAlgRecords(UPDATE_DEP,TRUE);
    DateSplit(ALG_SBDEPDOC.DepDate_Document,day,mon,year);
    DateSplit(ALG_DEPOSITOR.Open_Date,day1,mon1,year1);
    if (mon == mon1)
      Открыт_не_в_текущем_месяце = FALSE;
    end;
  end;

/*
  1. Проценты за прошлый месяц на доп.взносы, не пролежавшие месяц.
     Расчитываются для того, чтобы списать, т.к. в прошлом месяце
     на эти доп.взносы были начислены проценты, а при закрытии
     проценты выплачиваются только на то, что пролежало месяц
*/
  Результат = Расчет_Излишков_Процентов(ALG_DEPOSITOR.Referenc,Отчислено,СуммаДопВзносов);
  if (Результат != 0)
    ErrMsg = "1. Ошибка при расчете излишков процентов " + String(Результат);
    return;
  end;

  /*- VZ ------------02.06.98 15:48-------------------
   - Вычислить сумму налога, отчисленного при причислении процентов в конце
      месяца "Отчислен_Налог"
   - Уменьшить сумму "Отчислено" на "Налог_На_Отчислено"
                             "Отчислено" * "Отчислен_Налог"
      Налог_На_Отчислено  =  ------------------------------
                                     "Зачислено_Всего"

   --------------------------------------------------*/

  Отчислен_Налог = $0L;
  doc.KeyNum = 1;
  doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) + " and " +
                 "T_TypeOper = " + String( Списание_Простое )                  );
  doc.Clear;
  doc.rec.Referenc = ALG_DEPOSITOR.Referenc;
  doc.rec.TypeOper = Списание_Простое;
  if (ALG_SBDEPDOC.DepDate_Document > {curdate})
    Дата_последнего_расчета = Последний_День_Предыдущего_Месяца(ALG_SBDEPDOC.DepDate_Document);
  else
    Дата_последнего_расчета = Последний_День_Предыдущего_Месяца({curdate});
  end;
  doc.rec.DepDate_Document = Дата_последнего_расчета;
  if ( doc.GetGE )
    found = True;
    while(
           found AND
           (doc.rec.Referenc == ALG_DEPOSITOR.Referenc) AND
           (doc.rec.TypeOper == Списание_Простое) AND
           (doc.rec.DepDate_Document == Дата_последнего_расчета)
         )
      if ( IsServDoc( doc.rec ) and ( doc.rec.ApplType == Налог_Нерезидентов ) )
        Отчислен_Налог = Отчислен_Налог + Den( doc.rec.OutSum, doc.rec.DepDate_Document );
      end; /* IF */
      if ( not doc.Next )
        found = False;
      end;
    end;
  end;
  doc.dropFilter;

/* VZ & PAB 25.02.1999 В случае взятия налога при расчете "Налог_На_Отчислено"
   возникало деление на 0 из-за неопределенности суммы "Зачислено_Всего"*/
  /* VZ 08.01.98 Для конвертера с Лароса 98 г. - ищем запись либо о зачислении*/
  /* процентов, либо о деноминации */
  doc.KeyNum = 2;
  doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) );
  doc.Clear;
  doc.rec.Referenc = ALG_DEPOSITOR.Referenc;
  if (ALG_SBDEPDOC.DepDate_Document > {curdate})
    Дата_последнего_расчета = Последний_День_Предыдущего_Месяца(ALG_SBDEPDOC.DepDate_Document);
  else
    Дата_последнего_расчета = Последний_День_Предыдущего_Месяца({curdate});
  end;
  doc.rec.DepDate_Document = Дата_последнего_расчета;
  doc.rec.NumDayDoc = 32767; /* максимально возможное число */
  Зачислено_Всего = $0L;
  ОстатокНаКонецМесяца = $0L;

  found = doc.GetLE;
  while( found AND
         (doc.rec.Referenc == ALG_DEPOSITOR.Referenc) AND
         (doc.rec.DepDate_Document == Дата_последнего_расчета )
       )
      if(IsServDoc(doc.rec) AND
         ((doc.rec.TypeOper == Зачисление_Процентов) OR
          (doc.rec.TypeOper == Деноминация))
       )
          /* VZ 15.01.98 Не деноминируем суммы: считаем что с даты последнего */
          /* расчета все записи деноминированы */
          Зачислено_Всего = doc.rec.InSum;
          ОстатокНаКонецМесяца = doc.rec.Rest;
          found = FALSE; /* выход из цикла */
     end;
     if(found)
       found = doc.Prev;
     end;
  end;
  doc.dropFilter;

  if( Отчислен_Налог != 0)
    Налог_На_Отчислено = Money(Отчислено * Отчислен_Налог / Зачислено_Всего);
    Отчислено = Отчислено - Налог_На_Отчислено;
  end;

/*
  2. Списание процентов на доп.взносы, не пролежавшие месяц
     (на сумму "Отчислено")
*/
  Результат = 0;
  if (Отчислено != 0)
    ClearRecord(tmpDoc);
    tmpDoc.Referenc = ALG_SBDEPDOC.Referenc;
    tmpDoc.IsCur=ALG_SBDEPDOC.IsCur;
    tmpDoc.FNCash = ALG_SBDEPDOC.FNCash;
    tmpDoc.RealFNCash       = ALG_SBDEPDOC.RealFNCash;
    tmpDoc.Account = ALG_DEPOSITOR.Account;
    tmpDoc.Oper = {oper};/*ALG_SBDEPDOC.Oper;*/
    tmpDoc.Type_Account = ALG_SBDEPDOC.Type_Account;
    tmpDoc.Code_Currency = ALG_SBDEPDOC.Code_Currency;
    tmpDoc.CodClient = ALG_SBDEPDOC.CodClient;
    tmpDoc.YesSbook = "X";
    tmpDoc.Date_Document = ALG_SBDEPDOC.Date_Document;
    tmpDoc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
    tmpDoc.Author = ALG_SBDEPDOC.Author;
    tmpDoc.InSum = -Отчислено;
    tmpDoc.TypeComplexOper = ALG_PARAM.NumOpert;

    С_Панелью = 0;
    UseMaxDate = 1;
    NoPrint = 1;

    Результат = Выполнить_Операцию(ALG_DEPOSITOR.Account,ALG_DEPOSITOR.Type_Account,
                ALG_DEPOSITOR.Code_Currency, Зачисление_Процентов/*Списание_По_Раз_Поруч*/,
                tmpDoc, С_Панелью, UseMaxDate, NoPrint, ALG_PARAM.NumOpert);
    if(Результат != 0)
      ErrMsg = "2. Ошибка при списании "+String(Результат);
      return;
    end;
  end;
  Начисление_Отчисление = Начисление_Отчисление - Отчислено;
  Сумма_прокатки_остатка = Отчислено;

/*==========================================*/
  /*  П.№3 Расчитать проценты на разницу между причисленными в конце месяца */
  /*  процентами и и суммой П.№1 */
/*
  3. Сумма выданных процентов за текущий месяц
     (с последнего причисления до закрытия)
*/
  ВыданоПроцентов_Всего = $0L;
  doc.KeyNum = 1;
  doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) + " and " +
                 "T_TypeOper = " + String( Выдача_Процентов )                  );
  doc.Clear;
  doc.rec.Referenc = ALG_DEPOSITOR.Referenc;
  doc.rec.TypeOper = Выдача_Процентов;
  if (ALG_SBDEPDOC.DepDate_Document > {curdate})
    Дата_последнего_расчета = Последний_День_Предыдущего_Месяца(ALG_SBDEPDOC.DepDate_Document);
    tmpdate1 = ALG_SBDEPDOC.DepDate_Document;
  else
    Дата_последнего_расчета = Последний_День_Предыдущего_Месяца({curdate});
    tmpdate1 = {curdate};
  end;
  doc.rec.DepDate_Document = Дата_последнего_расчета+1;
  if ( doc.GetGE )
    found = True;
    while((doc.rec.Referenc == ALG_DEPOSITOR.Referenc)        AND
          (doc.rec.TypeOper == Выдача_Процентов)              AND
          (doc.rec.DepDate_Document <= tmpdate1 )            AND
          (found == True))
      if ( IsServDoc(doc.rec ) )
       ВыданоПроцентов_Всего = ВыданоПроцентов_Всего + doc.rec.OutSum;
      end;
      if ( not doc.Next )
        found = False;
      end;
    end;
  end;
  doc.dropFilter;

/*
  Для валюты часть выдачи идет как простое списание при конвертации
  центов в рубли
*/
  if (ALG_DEPOSITOR.Code_Currency != 0)
    doc.KeyNum = 1;
    doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) + " and " +
                   "T_TypeOper = " + String( Списание_Простое )                  );
    doc.Clear;
    doc.rec.Referenc = ALG_DEPOSITOR.Referenc;
    doc.rec.TypeOper = Списание_Простое;
    if (ALG_SBDEPDOC.DepDate_Document > {curdate})
      Дата_последнего_расчета = Последний_День_Предыдущего_Месяца(ALG_SBDEPDOC.DepDate_Document);
      tmpdate1 = ALG_SBDEPDOC.DepDate_Document;
    else
      Дата_последнего_расчета = Последний_День_Предыдущего_Месяца({curdate});
      tmpdate1 = {curdate};
    end;
    doc.rec.DepDate_Document = Дата_последнего_расчета+1;
    if ( doc.GetGE )
      found=True;
      while((doc.rec.Referenc == ALG_DEPOSITOR.Referenc)        AND
            (doc.rec.TypeOper == Списание_Простое)              AND
            (doc.rec.DepDate_Document <= tmpdate1 )            AND
            (found == True))
        if ( IsServDoc( doc.rec ) and (doc.rec.TypeComplexOper == Выдача_Процентов ) )
          ВыданоПроцентов_Всего = ВыданоПроцентов_Всего + doc.rec.OutSum;
        end;
        if ( not doc.Next )
          found=False;
        end;/* IF */
      end; /* WHILE */
    end; /* IF */
    doc.dropFilter;
  end;

  ВыданоПроцентов_БезПроцентов = ВыданоПроцентов_Всего;

  doc.KeyNum = 1;
  doc.Clear;
  doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) + " and " +
                 "T_TypeOper = " + String( Зачисление_Процентов )              );
  doc.rec.Referenc = ALG_DEPOSITOR.Referenc;
  doc.rec.TypeOper = Зачисление_Процентов;
  if (ALG_SBDEPDOC.DepDate_Document > {curdate})
    Дата_последнего_расчета = Последний_День_Предыдущего_Месяца(ALG_SBDEPDOC.DepDate_Document);
    tmpdate1 = ALG_SBDEPDOC.DepDate_Document;
  else
    Дата_последнего_расчета = Последний_День_Предыдущего_Месяца({curdate});
    tmpdate1 = {curdate};
  end;
  doc.rec.DepDate_Document = Дата_последнего_расчета+1;
  if ( doc.GetGE )
    found=True;
    while((doc.rec.Referenc == ALG_DEPOSITOR.Referenc) AND
          (doc.rec.TypeOper == Зачисление_Процентов) AND
          (doc.rec.DepDate_Document <= tmpdate1 ) AND
          (found == True))
      if ( IsServDoc( doc.rec ) )
        ВыданоПроцентов_БезПроцентов = ВыданоПроцентов_БезПроцентов - doc.rec.InSum;
        Начисление_Отчисление = Начисление_Отчисление + doc.rec.InSum; /* При выдаче % на % вписывается с "-" в остатки % */
      end; /*IF */
      if ( not doc.Next )
        found=False;
      end;
    end;
  end;
  doc.dropFilter;

/*
  4. Расчет %% по До Востребования нужен только для валютного вклада
*/
  if ( ALG_DEPOSITOR.Code_Currency != 0 )
    Результат = Расчет_Процентов_ДВС(ALG_DEPOSITOR.Referenc, Начислено);
    if(Результат != 0)
      ErrMsg = "1. Ошибка при расчете процентов по ДВС"+String(Результат);
      return;
    end;
    if (Начислено != 0)
      Результат = Начисление_Отчисление_Процентов(ALG_DEPOSITOR.Referenc,
                  ALG_SBDEPDOC.DepDate_Document, Начислено, 2002);

      if(Результат != 0)
        ErrMsg = "1. Ошибка при коррекции процентов по ДВС "+String(Результат);
        return;
      end;
      ClearRecord(tmpDoc);
      tmpDoc.Referenc         = ALG_SBDEPDOC.Referenc;
      tmpDoc.IsCur            = ALG_SBDEPDOC.IsCur;
      tmpDoc.FNCash           = ALG_SBDEPDOC.FNCash;
      tmpDoc.RealFNCash       = ALG_SBDEPDOC.RealFNCash;
      tmpDoc.Account          = ALG_DEPOSITOR.Account;
      tmpDoc.Oper             = {oper};
      tmpDoc.Type_Account     = ALG_SBDEPDOC.Type_Account;
      tmpDoc.Code_Currency    = ALG_SBDEPDOC.Code_Currency;
      tmpDoc.CodClient        = ALG_SBDEPDOC.CodClient;
      tmpDoc.YesSbook         = "X";
      tmpDoc.Date_Document    = ALG_SBDEPDOC.Date_Document;
      tmpDoc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
      tmpDoc.Author           = ALG_SBDEPDOC.Author;
      tmpDoc.InSum            = Начислено;
      tmpDoc.TypeComplexOper  = ALG_PARAM.NumOpert;

      С_Панелью  = 0;
      UseMaxDate = 1;
      NoPrint    = 1;
      Результат = Выполнить_Операцию(ALG_DEPOSITOR.Account,ALG_DEPOSITOR.Type_Account,
                  ALG_DEPOSITOR.Code_Currency,Зачисление_Процентов,
                  tmpDoc,С_Панелью,UseMaxDate,NoPrint,ALG_PARAM.NumOpert );
    end;
    if (Результат != 0)
      ErrMsg = "2. Ошибка при зачислении "+String(Результат);
      return;
    end;
  end;

/*
  5. Сумма, на которую надо считать процент в текущем месяце
  по основным условиям
*/
  Зачислено = ОстатокНаКонецМесяца - Отчислено - СуммаДопВзносов
              - ВыданоПроцентов_БезПроцентов - Отчислен_Налог;

  if (Зачислено < $0L)
    /* Ошибка при расчете сумм */
    ErrMsg = "Ошибка при расчете сумм";
    Результат = -1;
    return;
  end;

  if ((Зачислено != 0))
  /* VZ 19.05.99 Исправлено для включения дня закрытия, когда закрытие
     приходится на 1 число */
    if(Дата_последнего_расчета < ALG_SBDEPDOC.DepDate_Document)
      Дата_Конца_Расчета = ALG_SBDEPDOC.DepDate_Document;
      if (not DebitDayIncluded( ALG_SBDEPDOC.DepDate_Document, Включать_день_расхода,
                                ALG_DEPOSITOR.IsCur,
                                ALG_DEPOSITOR.Type_Account))
        ErrMsg = "Ошибка при определении включения даты закрытия в расчет %%";
        Результат = -1;
        return ;
      end; /*fi*/
      if (Включать_день_расхода)
        Дата_Конца_Расчета = Дата_Конца_Расчета + 1;
      end; /*fi*/

      Результат = Проценты_На_Операцию(ALG_DEPOSITOR.Account,
                  ALG_DEPOSITOR.Type_Account,
                  ALG_DEPOSITOR.Code_Currency, Зачислено, Проценты,
                  Дата_последнего_расчета+1,
                  Дата_Конца_Расчета/*{curdate}*/,2001/*основной*/,0);

    else
      Проценты = $0L;
      Результат = 0;
    end;

    if(Результат != 0)
     ErrMsg = "3. Ошибка при расчете %% "+String(Результат);
     return;
    end;
  else
    Проценты = $0L;
    Результат = 0;
  end;

  if (Начисление_Отчисление != 0)
    Результат = Начисление_Отчисление_Процентов(ALG_DEPOSITOR.Referenc,
                ALG_SBDEPDOC.DepDate_Document, Начисление_Отчисление , 2001);
    if (Результат != 0)
      ErrMsg = "4. Ошибка при начислении-отчислении "+String(Результат);
      return;
    end;
    Начисление_Отчисление = $0L;
  end;

/*
  6. Причисление процентов за текущий месяц (верная сумма)
*/
  Результат = 0;
  if (Проценты != 0)
    ClearRecord(tmpDoc);
    tmpDoc.Referenc         = ALG_SBDEPDOC.Referenc;
    tmpDoc.IsCur            = ALG_SBDEPDOC.IsCur;
    tmpDoc.FNCash           = ALG_SBDEPDOC.FNCash;
    tmpDoc.RealFNCash       = ALG_SBDEPDOC.RealFNCash;
    tmpDoc.Account          = ALG_DEPOSITOR.Account;
    tmpDoc.Oper             = {oper};
    tmpDoc.Type_Account     = ALG_SBDEPDOC.Type_Account;
    tmpDoc.Code_Currency    = ALG_SBDEPDOC.Code_Currency;
    tmpDoc.CodClient        = ALG_SBDEPDOC.CodClient;
    tmpDoc.YesSbook         = "X";
    tmpDoc.Date_Document    = ALG_SBDEPDOC.Date_Document;
    tmpDoc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
    tmpDoc.Author           = ALG_SBDEPDOC.Author;
    tmpDoc.InSum            = Проценты;
    tmpDoc.TypeComplexOper  = ALG_PARAM.NumOpert;

    С_Панелью  = 0;
    UseMaxDate = 1;
    NoPrint    = 1;

    Результат = Выполнить_Операцию(ALG_DEPOSITOR.Account, ALG_DEPOSITOR.Type_Account,
                ALG_DEPOSITOR.Code_Currency, Зачисление_Процентов/*Зачисление_По_Данным_ПК*/,
                tmpDoc, С_Панелью, UseMaxDate, NoPrint, ALG_PARAM.NumOpert);
  end;
  if (Результат != 0)
   ErrMsg = "4. Ошибка при причислении " + String(Результат);
   return;
  end;
  if (Проценты != 0)
    Начисление_Отчисление = Начисление_Отчисление + Проценты;
  end;
  if (Начисление_Отчисление != 0)
    Результат = Начисление_Отчисление_Процентов(ALG_DEPOSITOR.Referenc,
                ALG_SBDEPDOC.DepDate_Document, Начисление_Отчисление , 2001);
    if(Результат != 0)
      ErrMsg = "10. Ошибка при начислении-отчислении "+String(Результат);
      return;
    end; /* IF */
    Начисление_Отчисление = 0;
  end;

/*
  7. Выдача всей суммы при закрытии счета
     (= остатку по последнему документу)
*/

  doc.KeyNum = 2;
  doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) );
  doc.Clear;
  doc.rec.Referenc = ALG_DEPOSITOR.Referenc;
  MaxDate = ALG_SBDEPDOC.DepDate_Document+1;
  doc.rec.DepDate_Document = MaxDate;
  doc.rec.NumDayDoc = 0;
  Сумма = $0;
  if ( doc.GetLE )
    found = True;
    while((doc.rec.Referenc == ALG_DEPOSITOR.Referenc) AND
          (doc.rec.DepDate_Document <= MaxDate) AND
          (found == True))
      if ( IsServDoc( doc.rec ) )
          Сумма = doc.rec.Rest;
          found = False;
        end;
      if(found)
        if ( not doc.Prev )
          found=False;
        end;
      end;
    end;
  end;
  doc.dropFilter;

  /* Расчет процентов без записи транзакции */
  if ((ALG_PARAM.prmC == "X") and (Результат == 0))
    ALG_PARAM.prmD = Проценты;
    Результат      = ОТКАТ_С_РЕЗУЛЬТАТОМ;
    ALG_RESULT     = END_RES;
    ALG_UPDATE     = 0;
    return;
  end;
  
  if (Сумма != 0)
    ClearRecord(tmpDoc);
    tmpDoc.Referenc         = ALG_SBDEPDOC.Referenc;
    tmpDoc.IsCur            = ALG_SBDEPDOC.IsCur;
    tmpDoc.FNCash           = ALG_SBDEPDOC.FNCash;
    tmpDoc.RealFNCash       = ALG_SBDEPDOC.RealFNCash;
    tmpDoc.Account          = ALG_DEPOSITOR.Account;
    tmpDoc.Oper             = {oper};
    tmpDoc.Type_Account     = ALG_DEPOSITOR.Type_Account;
    tmpDoc.Code_Currency    = ALG_SBDEPDOC.Code_Currency;
    tmpDoc.CodClient        = ALG_SBDEPDOC.CodClient;
    tmpDoc.YesSbook         = "X";
    tmpDoc.Date_Document    = ALG_SBDEPDOC.Date_Document;
    tmpDoc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
    tmpDoc.Author           = ALG_SBDEPDOC.Author;
    tmpDoc.OutSum           = Сумма;
    tmpDoc.TypeComplexOper  = ALG_PARAM.NumOpert;
    tmpDoc.TypeOper         = Operation;
    tmpDoc.ApplType         = FINISH_SBDEPDOC.ApplType;

    if (IsFirst)
      Результат = ОТКАТ_С_РЕЗУЛЬТАТОМ;
      InterDesk_EndDocBunch();
      return;
    else
      /* Вставка документа на сумму комиссии */
      if (SummaComm > 0)
        Результат = GetCommissionOnOper(tmpDoc,tmpDoc.OutSum,OnlyInsertDoc_Commission,SummaComm,TypeComm,NumOpert,ALG_PARAM.NumOpert);
        if (Результат != 0)
          ErrMsg = "Ошибка при вставке документа на комиссию " + String(Результат);
          return;
        end;
      end;
      /* Вставка документа на сумму конверсии */
      if (ConvertSumComm > 0)
        Результат = ConvertPartSum(pay_doc_comm,2,2,ConvertSumComm);
        if (Результат != 0)
          ErrMsg = "Ошибка при вставке документа на конверсию " + String(Результат);
          return;
        end;
      end;
      /* Вставка документа на сумму конверсии */
      if ((ConvertSum > 0) AND (StrFor(GetProgramID) != "П"))
        Результат = ConvertPartSum(tmpDoc,1,2,ConvertSum);
        if (Результат != 0)
          ErrMsg = "Ошибка при вставке документа на конверсию " + String(Результат);
          return;
        end;
      end;
    end;

    С_Панелью  = 0;
    UseMaxDate = 1;
    NoPrint    = 1;

    //tmpDoc.Account_Receiver = FINISH_SBDEPDOC.Account_Receiver;
    //tmpDoc.CodeCur_Receiver = FINISH_SBDEPDOC.CodeCur_Receiver;
    //tmpDoc.MFO_Receiver     = FINISH_SBDEPDOC.MFO_Receiver;
    //tmpDoc.CorAcc_Receiver  = FINISH_SBDEPDOC.CorAcc_Receiver;
    tmpDoc.PercTrnRest      = FINISH_SBDEPDOC.PercTrnRest;
    tmpDoc.PercTrnAlt       = FINISH_SBDEPDOC.PercTrnAlt;
    tmpDoc.CurrencyBought   = FINISH_SBDEPDOC.CurrencyBought;
    tmpDoc.SummaTaxRub      = FINISH_SBDEPDOC.SummaTaxRub;

    if (Operation == Безналичная_Конверсия)
      /* Пересчет суммы с учетом подоходного налога */
      if (tmpDoc.SummaTaxRub != 0)
        tmpDoc.OutSum = tmpDoc.OutSum - tmpDoc.SummaTaxRub;
      end;
      /* Пересчет конвертируемой суммы с учетом процентов */
      if (tmpDoc.PercTrnRest != 0)
        tmpDoc.CurrencyBought = tmpDoc.OutSum / tmpDoc.PercTrnRest;
      end;
    end;

    if (StrFor(GetProgramID) == "П")
      tmpDoc.OutSum = tmpDoc.OutSum - ConvertSum;
    end;
    tmpDoc.IsControl        = StrFor(1); /* Признак главного документа */

    ClearRecord(ALG_OPERPRM);
    ALG_OPERPRM.Account           = ALG_DEPOSITOR.Account;
    ALG_OPERPRM.Type_Account      = ALG_DEPOSITOR.Type_Account;
    ALG_OPERPRM.Code_Currency     = ALG_DEPOSITOR.Code_Currency;
    ALG_OPERPRM.Type_Oper         = Operation;
    ALG_OPERPRM.Show_Panel        = С_Панелью;
    ALG_OPERPRM.UseMaxDate        = UseMaxDate;
    ALG_OPERPRM.NoPrint           = NoPrint;
    ALG_OPERPRM.NonCashCommission = ALG_PARAM.prmI;
    ALG_OPERPRM.TypeComplexOper   = ALG_PARAM.NumOpert;
    ALG_OPERPRM.DocumentAuthor    = CodeFor(tmpDoc.Author);
    if (Operation == Списание_Переводом)
      ALG_OPERPRM.CorrAcc         = PMNT_PROP_REC_BUF.Account;
      ALG_OPERPRM.CorrType        = PMNT_PROP_REC_BUF.CorrAccount;
    end;

    Результат = Выполнение_Операции(ALG_OPERPRM,tmpDoc);

    if (Результат != 0)
      ErrMsg = "6. Ошибка при расходе "+String(Результат);
      return;
    end;

  else
    if (IsFirst)
      ClearRecord(tmpDoc);
      Результат = ОТКАТ_С_РЕЗУЛЬТАТОМ;
      InterDesk_EndDocBunch();
      return;
    end;
  end;

  /* Корректировка файлов для получения верного отчета */
  if (NOT CorrectSbDrest(ALG_DEPOSITOR.Referenc,ALG_SBDEPDOC.IsCur,-Сумма_прокатки_остатка))
    Результат = 9999;
    ErrMsg = "7. Ошибка при корректировке файлов остатков";
    return;
  end;

  ALG_RESULT = END_RES; /* Все Ok */

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro TrnProc

  CloseDep( );

  if (Результат != 0)
    if (Результат == ОТКАТ_С_РЕЗУЛЬТАТОМ)
      ALG_UPDATE = 0;
    else
      ALG_RESULT = ERR_RES;
    end;
    AbortTrn();
  else
    ALG_RESULT = END_RES;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  if (ALG_DEPOSITOR.UseAlternate == 0)

    LoopInTrn(TRUE);

    ClearRecord(tmpDoc);

    Copy(save_DEPOSITOR,ALG_DEPOSITOR);
    Copy(save_SBDEPDOC,ALG_SBDEPDOC);
    Copy(fff,ALG_SBDEPDOC);
    SetRecordAddr(FINISH_SBDEPDOC,fff,0,0,true);

    if ((FINISH_SBDEPDOC.TypeOper != 0                 ) AND 
        (FINISH_SBDEPDOC.TypeOper != ALG_PARAM.NumOpert)    )
      Operation = FINISH_SBDEPDOC.TypeOper;
    elif (ALG_PARAM.NumOpert == Закрытие_Наличными)
      Operation = Расход;
    else
      Operation = Списание_По_Раз_Поруч;
    end;
    
    /* Нужно взять комиссию или конверсию - для этого предварительно определим сумму расхода */
    IsFirst = TRUE;
    if (ALG_PARAM.prmC != "X")
      /* Нужно взять комиссию, причем для последконтроля она безналичная */
      if (ALG_PARAM.prmI2 > 0)
        if ((StrFor(GetProgramID) == "П") AND (ALG_PARAM.prmI > 0))
          ;
        else
          NeedPreClose = TRUE;
        end;
      end;
      /* Валютный вклад и не последконтроль (ALG_PARAM.prmD) - нужна конвертация */
      if ((ALG_SBDEPDOC.IsCur != 0) AND (Operation == Расход))
        if (StrFor(GetProgramID) == "П")
          ;
        else
          NeedPreClose = TRUE;
        end;
      end;
    end;

    if (NeedPreClose)
      if ( not ProcessTrn( 1, "TrnProc" ) )
        if (Результат != ОТКАТ_С_РЕЗУЛЬТАТОМ)
          ALG_RESULT = ERR_RES;
          Результат = 4714;
        end;
      end;
      Copy(ALG_DEPOSITOR,save_DEPOSITOR);
      Copy(ALG_SBDEPDOC,save_SBDEPDOC);
      Copy(fff,ALG_SBDEPDOC);
      SetRecordAddr(FINISH_SBDEPDOC,fff,0,0,true);
    end;

    /* Проведем собственно операцию закрытия счета */
    IsFirst = FALSE;
    if (ALG_RESULT != ERR_RES)
      /* Определение суммы комиссии */
      Результат = 0;
      if ((ALG_PARAM.prmI2 >   0 ) AND 
          (ALG_PARAM.prmC  != "X") AND
          (tmpDoc.OutSum   >   0 )    )
        if ((StrFor(GetProgramID) == "П") AND (ALG_PARAM.prmI > 0))
          ;
        else
          Результат = GetCommissionOnOper(tmpDoc,tmpDoc.OutSum,OnlyInputSum_Commission,SummaComm,TypeComm,NumOpert);
          if (Результат == 2)
            MsgBox("Отказались от проведения операции");
            ALG_RESULT = ОБЛОМИТЬ_ТРАНЗАКЦИЮ;
          elif (Результат != 0)
            ErrMsg = "Ошибка при взятии комиссии = " + String(Результат);
            ALG_RESULT = ERR_RES;
          end;
        end;
      end;
      /* Определение суммы конвертации для суммы комиссии */
      if ((Результат == 0) AND (ALG_PARAM.prmC != "X"))
        if ((SummaComm            >  0  ) AND
            (TypeComm             != "" ) AND
            (ALG_SBDEPDOC.IsCur   != 0  ) AND
            (StrFor(GetProgramID) != "П")    )
          ClearRecord(pay_doc_comm);
          pay_doc_comm.FNCash   = tmpDoc.FNCash;
          pay_doc_comm.IsCur    = tmpDoc.IsCur;
          pay_doc_comm.NumGroup = 21;
          pay_doc_comm.NumOpert = NumOpert;
          pay_doc_comm.CodCur   = tmpDoc.Code_Currency;
          pay_doc_comm.Oper     = {oper};
          pay_doc_comm.InSum    = SummaComm;
          Результат = ConvertPartSum(pay_doc_comm,2,1,ConvertSumComm);
          if (Результат != 0)
            ErrMsg = "Ошибка при конвертации  = " + String(Результат);
            ALG_RESULT = ОБЛОМИТЬ_ТРАНЗАКЦИЮ;
            return;
          end;
        end;
      end;
      /* Определение суммы конвертации для расхода основной суммы */
      if ((Результат == 0) AND (ALG_PARAM.prmC != "X"))
        if (TypeComm == "")
          tmpDoc.OutSum = tmpDoc.OutSum - SummaComm;
        end;
        if (StrFor(GetProgramID) == "П")
          ConvertSum = ALG_PARAM.prmD;
        elif ((ALG_SBDEPDOC.IsCur != 0) AND (Operation == Расход))
          Результат = ConvertPartSum(tmpDoc,1,1,ConvertSum);
          if (Результат != 0)
            ErrMsg = "Ошибка при конвертации  = " + String(Результат);
            ALG_RESULT = ОБЛОМИТЬ_ТРАНЗАКЦИЮ;
            return;
          end;
        end;
      end;
      /* Собственно списание */
      if (Результат == 0)
        if ( not ProcessTrn( 1, "TrnProc" ) )
          if (Результат != ОТКАТ_С_РЕЗУЛЬТАТОМ)
            ALG_RESULT = ERR_RES;
            Результат = 4714;
          end;
        end;
      end;
    end;

    if ((ALG_PARAM.prmC == "X") AND (Результат == ОТКАТ_С_РЕЗУЛЬТАТОМ))
      ALG_RESULT = END_RES;
      ALG_UPDATE = 0;
    end;
    
    if ((ErrMsg != "") AND (NOT InFCGroupOpRun()))
      MsgBox(ErrMsg);
    end;

  else

    /* Счет в альтернативном состоянии- значит, была выдача наследства,
       и счет живет по "До востребования"
       Закроется стандартной процедурой                                 */
    ALG_RESULT = OK_RES;
  end;

end;
