/*

    R-Style SoftWare Lab.

    RS-Retail

    Макрос выпуска сводных реестр-ордеров форм
    210 (на причисление %%) и 245 (на списание налога)

*/

import CommonInter, BankInter, DeprIntr, ChkPrn, rsd, setrtglobvar;

private var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
private var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

var
  cmdGetDocs,
  docsRs;

private var depclnt = TClientList;

/* Количество строк в листе */
const ListLineNum = 18;

/* Значение поля SignRec файла sb_edref.dbt  */
const SIGNORDER      = 2,
      SIGNPRINTORDER = 3,
      SIGN_NOFOUND   = 4;

/* Значения полей операции ( TypeOper и TypeComplexOper ) */
const OP_CALCPR = 35,  /* Расчет процентов */
      OP_GETCON = 62,  /* Списание налога */
      OP_PUTPR  = 72,  /* Зачисление процентов */
      OP_PAYTR  = 84;  /* Оплпта транзакции */

/* Значения полей подвидов операций ( ApllType ) */
const OP_GETCNINCOMTAX      =  8,  /* Подоходный налог на нерезидентов */
      OP_GETTAX_CARD        = 14,  /* Налог (формирование налоговой карточки) */
      OP_GETTAX_CARD_CORR   = 16,  /* Налог (карточка) для другого счета */
      OP_GETCNINCOMTAX_CORR = 17;  /* Налог нерезидента для др.счет */

const OT_OP_FROM_OTHER      = 3010; //Вид вклада, операции которого выполняются для заданного
const SB_DEPOSIT = 1;

const RegPath = "RS-RETAIL\\ПЕЧАТИ\\ВКЛАДЫ\\ПРИХ_РАСХ_ОРДЕР";

var flag_Cur = NumFlagCur();
var FNCash   = NumFNCash();
var {CNum};
var {curdate};
var {Name_Bank};
var {EoyProcRunning} = GetCurrentMode;

var dt  = TBFile( "sb_dtyp.dbt",  "r", 2 );
var top = TBFile( "sb_typop.dbt", "r", 0 );
var sop = TBFile( "suboper.dbt",  "r", 1 );

var TxtDir;

var stat0, stat1, stat2;
var num, list;
var AllLists = 0;
var sFilial;
var saveTypeAcc  = "???";
var saveCodCurr  = -1;
var saveTypeOper = -1;
var saveApplType = -1;
var firstTypeAcc;
var firstCodCurr;
var firstCurrName = "???";

var sISO = "???";

var vSignRec;

var SumI, SumICom, StrCount;

private file dep ( "depositr.dbt" ) key 8;

private var ddep  = TRecHandler( "i_dp_dep.rec" );
private var party = TRecHandler( "i_party.rec" );

// =================================
//
// =================================
private macro getOurBankPartyID( pFNCash )
  var vID = 0;
  if ( findDepartment( pFNCash, NULL, ddep, party ) )
    vID = party.rec.PartyID;
  end;
  return vID;
end;

/* Получение полного наименования вида вклада */
macro getDepTypeName( _Curr, _Kind )
 var sName = _Kind;
 dt.Clear();
 dt.rec.FlagCur = _Curr;
 dt.rec.Kind    = _Kind;
 if ( dt.getEQ() )
   sName = dt.rec.Name;
 end;
 return "\"" + sName + "\"";
end;

/*****************************************************************
                    Получение подоперации
******************************************************************/
macro GetSubOper( _IsCur, _Type_Account, _TypeOper, _ApplType )
  var SubOp;
  if ( ( _TypeOper != 0 )  AND  ( _ApplType != 0 ) )
    sop.Clear();
    sop.rec.IsCur    = _IsCur;
    sop.rec.Kind     = _Type_Account;
    sop.rec.OperType = _TypeOper;
    sop.rec.Type     = _ApplType;
    if ( sop.GetEQ() )
      SubOp = "( " + sop.rec.Name + " )";
    else
      SubOp = "";
    end;
  else
    SubOp = "";
  end;
  return SubOp;
end;

/*****************************************************************
                    Название операции и подвида.
******************************************************************/
macro OperationName( _IsCur, _Type_Account, _TypeOper, _ApplType )
  var sOperation = "";
  top.Clear();
  top.rec.IsCur    = _IsCur;
  top.rec.Kind     = _Type_Account;
  top.rec.NumOpert = _TypeOper;
  if ( top.GetEQ() )
    sOperation = "\"" + top.rec.czNameAlg +
                 GetSubOper( _IsCur, _Type_Account, _TypeOper, _ApplType ) +
                 "\"";
  end;
  return sOperation;
end;

/* Получение Ф.И.О. клиента */
macro getFIO()
 var fio = "???";
 if ( depclnt.GetRecord( docsRs.value( "doc_codClient" ) ) )
   if(  (not SpecialAccess) and OperMayListSpecialAccounts and docsRs.value( "SpecialAccess" ))
      FIO = "XXXXXX X.X.";
   else
     fio = Trim( depclnt.CurRec.rec.Name1 );
     if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
     	fio = fio + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
     	if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
	    	fio = fio + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
     end;
   end;
 end;
 end;
 return fio;
end;

/* Получение названия валюты */
macro getISO()

 var
   cmd,
   rs;

 if ( flag_Cur == 0 )
   return "";
 end;

 cmd = RsdCommand( "select t_short_name from dcurrency_dbt where t_code_currency = ? " );
 cmd.addParam( "t_code_currency", RSDBP_IN );
 cmd.value( "t_code_currency" ) = docsRs.value( "doc_code_currency" );

 cmd.execute();
 rs = RsdRecordset( cmd );

 if ( rs.moveNext )
   return "(" + rs.value( 0 ) + ")";
 else
   return "(???)";
 end;
end;

/*  Получение названия операции  */
macro GetOperName( Num, ComplexNum )

  var s = "";

/*
  ClearRecord(sbt);
  KeyNum     (sbt,0);
  ReWind     (sbt);
  sbt.IsCur    = flag_Cur;
  sbt.Kind     = "Шаблонный";
  sbt.NumOpert = Num;
  if ( NOT GetEQ (sbt))
    s = "Не определено";
  else
    s = String( Num ) + ":" + sbt.czNameAlg;
  end;
*/
  s = string( Num );
  if ( Num != 0 )  
    s = s + ": " + docsRs.value( "sbt_to_czNameAlg" );
  end;

  /* Определение в составе */
  if ((ComplexNum != NULL) and (Num != ComplexNum))
/*
    ClearRecord(sbt);
    ReWind     (sbt);
    sbt.IsCur    = flag_Cur;
    sbt.Kind     = "Шаблонный";
    sbt.NumOpert = ComplexNum;
    if (not GetEQ (sbt))
      s = "не определено";
    else
      s = s + " (" + sbt.czNameAlg + ")";
    end;
*/
    s = s + " (" + docsRs.value( "sbt_tco_czNameAlg" ) + ")";
  end;
  /* Возвращаем основание опрерации */
  return s;
end;

/*  Получение текста сообщения об ошибке  */
macro GetErrMessage

  var s, num = docsRs.value( "ref_errCode" );

  if ( docsRs.value( "msg_contents" ) )
    s = String( Num ) + ": " + docsRs.value( "msg_contents" );
  else
    s = String( Num );
  end;
  return s;
end;

/*  Заголовок ордера 210 */
macro Order210_Title()
  var num_ord = String( docsRs.value( "doc_orderNum" ) );
  var sOperatName = "Зачисление процентов по вкладам ";

  if ( saveTypeOper == 84 )
    sOperatName = docsRs.value( "sbt_to_czNameAlg" );
  elif ( saveTypeAcc > 0 )
    sOperatName = sOperatName + GetSubOper( flag_Cur, saveTypeAcc, saveTypeOper, saveApplType );
  end;

  /*
  if ( doc.OrderCount != 0 )
    num_ord = num_ord + "( Повторно N " + String( doc.OrderCount ) + " )";
  end; */
  [
    Сбербанк России                                                ф.N 210-c
    #

          Список-ордер на ЗАЧИСЛЕНИЕ средств на счета по вкладу N #

    Филиал #                                                  #

    #

    за период                                               Лист #####
                                                            Листов #####
    Наименование (шифр) организации #

    Вклад #
   +-----+-------------------------+-------------------------------+----------+-------------+----------+----------+---------------+
   | N N |          Номер          |           Фамилия,            |   Сумма  |   Остаток   |Начислен-е|  Остаток |     Причина   |
   | п/п |   лицевого счета        |        имя, отчество          |          |    вклада   | проценты | процентов|  незачисления |
   +-----+-------------------------+-------------------------------+----------+-------------+----------+----------+---------------+]
     ( {Name_Bank},
      num_ord,
      sFilial,
      {curdate}:m,
      sOperatName,
      list:l,
      AllLists:l,
      getFilialName(),
      getDepTypeName( docsRs.value( "doc_code_currency" ), saveTypeAcc )
      );
end;

/*  Заголовок ордера 245 */
macro Order245_Title()
  var num_ord = String( docsRs.value( "doc_orderNum" ) );
  var sOperatName = "Списание ";

  if ( saveTypeOper == 84 )
    sOperatName = docsRs.value( "sbt_to_czNameAlg" );
  elif ( saveTypeAcc > 0 )
    sOperatName = sOperatName + GetSubOper( flag_Cur, saveTypeAcc, saveTypeOper, saveApplType );
  end;

  /*
  if ( doc.OrderCount != 0 )
    num_ord = num_ord + "( Повторно N " + String( doc.OrderCount ) + " )";
  end; */
  [
    Сбербанк России                                                ф.N 245-c
    #

          Список-ордер на СПИСАНИЕ средств со счетов по вкладу N #

    Филиал #                                                  #

    #

    за период на дату #                                     Лист #####
                                                            Листов #####
    Наименование (шифр) организации #

    Вклад #
   +-----+-------------------------+-------------------------------+----------+-------------+----------+----------+---------------+
   | N N |          Номер          |           Фамилия,            |   Сумма  |   Остаток   |Отчислен-е|  Остаток |Причина невыпол|
   | п/п |   лицевого счета        |        имя, отчество          |          |    вклада   | проценты | процентов|нения списания |
   +-----+-------------------------+-------------------------------+----------+-------------+----------+----------+---------------+]
    ( {Name_Bank},
      num_ord,
      sFilial,
      {curdate}:m,
      sOperatName,
      {curdate},
      list:l,
      AllLists:l,
      "\"Налоговая инспекция\"",
      getDepTypeName( docsRs.value( "doc_code_currency" ), saveTypeAcc )
      );
end;

/*  Заголовок протокола */
macro Protocol_Title()

  [
           Протокол сумм процентов по вкладам, не зачисленных на карты.

    #
    Дата зачисления #

    Зачисление процентов по вкладам
    за период                                               Лист #####
                                                            Листов #####
    Зачислено на карточный счет, но не зачислено на карту:
    Вид вклада #
   +-----+-------------------------+-------------------------------+-----------+---------------+
   | N N |    Номер счета          |           Фамилия,            |  Сумма    |     Причина   |
   | п/п |        СКС              |        имя, отчество          | процентов |  незачисления |
   +-----+-------------------------+-------------------------------+-----------+---------------+]
     ( {Name_Bank},
      /*sFilial,*/
      {curdate},
      list:l,
      AllLists:l,
      "\"" + saveTypeAcc + "\" " + " Валюта вклада " + String(docsRs.value( "ExternalCode" ))
      );
end;

/*  Строка ордера */
macro OrderString( sum )
  [|#####|#########################|###############################|##########|#############|##########|##########|               |]
  ( num:5,
    Acc_Form(String(docsRs.value( "doc_account" ))):25,
    getFIO():31,
    sum:10:2,
    docsRs.value( "doc_rest" ):13:2,
    docsRs.value( "doc_percOprSum" ):10:2,
    docsRs.value( "doc_percRest" ):10:2);
    SumI = SumI + sum;
    SumICom = SumICom + sum;
end;

/*  Закрытие листа ордера */
/*
macro Order_Table()
  [+-----+-------------------------+-------------------------------+----------+-------------+----------+----------+---------------+
                                                Итого по листу:   ############|
                                                                  ------------+
                                                Итого по списку:  ############|
                                                                  ------------+]
  ( SumI:12:2, SumICom:12:2 );
end;
*/

/*  Закрытие листа протокола */
macro Protocol_Table()
   var total = "Итого: не зачислены суммы на карты по вкладу \"" + saveTypeAcc +"\" общей суммой "+ 
                                          String(SumI)+" на субсчета "+String(StrCount) + " карт";
   if(StrCount == 1)
      total = total + "ы";
   end;

   [+-----+-------------------------+-------------------------------+-----------+---------------+
     
      #
      
      Требуется выполнить распределение нераспредленной части счета СКС в размере суммы процентов]
//      Итого: не зачислены суммы на карты по вкладу # общей суммой - ############ на субсчета карты]
//  ( saveTypeAcc, SumI:12:2 );
    (total);
end;

/*  Строка протокола */
macro ProtocolString( sum )
  [|#####|#########################|###############################|########## |               |]
  ( num:5,
    docsRs.value( "doc_account" ):25,
    getFIO():31,
    sum:10:2);
    SumI = SumI + sum;
    StrCount = StrCount + 1;
end;

/* Заголовок отчета по ордерам 210 */
macro ReportHead210
  [

                           Отчет по ордерам 210: #
                           За дату: #
  ]
  ( getDepTypeName( docsRs.value( "doc_code_currency" ), saveTypeAcc ),
    {curdate}:m );
end;

/* Заголовок отчета по ошибкам в ордерах 210 */
macro ReportHead210Err
  [

          Отчет по найденным ошибкам по зачислению на счета
  ];
end;

/* Заголовок отчета по ордерам 245 */
macro ReportHead245
  [

                           Отчет по ордерам 245: #
                           За дату: #
  ]
  ( getDepTypeName( docsRs.value( "doc_code_currency" ), saveTypeAcc ),
    {curdate}:m );
end;

/* Заголовок отчета по ошибкам в ордерах 245 */
macro ReportHead245Err
  [

           Отчет по найденным ошибкам по списанию со счетов
  ];
end;

/* Заголовок отчета по расчету процентов */
macro ReportCalcPercents
  var type_acc;

  if ( saveTypeAcc == "???" )
    type_acc = docsRs.value( "doc_type_account" );
  else
    type_acc = saveTypeAcc;
  end;

  [
                                     Ведомость расчета процентов за #

    Вид вклада: #
  ] ( {curdate}, "\"" + type_acc + "\" " + getISO() );
end;

/* Заголовок отчета ошибок по расчету процентов */
macro ReportErrorCalcPercents

  [

                                 Ведомость ошибок расчета процентов за #

  ] ( {curdate} );
end;

/* Заголовок отчета */
macro ReportTitle
  [+--------------------------+-----------------------------------------+-------------+-------------+-------------+----------------+
   |         Номер            |                                         |             | Начисленные |   Остаток   |     Остаток    |
   |    лицевого  счета       |                Операция                 |    Сумма    |  проценты   |  процентов  |      счета     |
   +--------------------------+-----------------------------------------+-------------+-------------+-------------+----------------+];
end;

/* Строка отчета */
macro ReportString( sum )
var account,
    op,
    ComplexOp,
    percOprSum = "",
    percRest = "",
    rest = "";

  account = String( docsRs.value( "doc_account" ):f );

  op = docsRs.value( "doc_typeOper" );
  ComplexOp = docsRs.value( "doc_typeComplexOper" );

  percOprSum = String( docsRs.value( "doc_percOprSum" ):a:13:2 );
  percRest = String( docsRs.value( "doc_percRest" ):a:13:2 );
  rest = String( docsRs.value( "doc_rest" ):a:16:2 );

  SumI = SumI + sum;
  [|##########################|#########################################|#############|#############|#############|################|]
  (
    Acc_Form(account),
    GetOperName( op, ComplexOp ):w,
    sum:a:13:2,
    percOprSum:r,
    percRest:r,
    rest:r
  );
end;

/* Подвал отчета */
macro ReportEnd
  [+--------------------------+-------------------------------------------------------+-------------+-------------+----------------+
                                                               Итого: ################|
                                                                      ----------------+
  ]
  ( SumI:a:16:2 );
end;

/* Заголовок отчета по ошибкам */
macro ReportTitleErr
  [+-------------------+--------------------------+-----------------------------------+--------------------------------------------------+
   |     Вид вклада    |           Номер          |             Операция              |                  Описание ошибки                 |
   |                   |      лицевого счета      |                                   |                                                  |
   +-------------------+--------------------------+-----------------------------------+--------------------------------------------------+];
end;

/* Строка отчета по ошибкам */
macro ReportStringErr
  var status,
      account,
      op,
      percOprSum = "",
      percRest = "",
      rest = "",
      errCode = docsRs.value( "ref_errCode" );

  account = docsRs.value( "acc_account" );
  op = docsRs.value( "ref_operation" );
  [|###################|##########################|###################################|##################################################|]
  (
    docsRs.value( "ref_accType" ),
    Acc_Form(account),
    GetOperName( op ):w,
    GetErrMessage:w
  );
end;

/* Подвал отчета */
macro ReportErrEnd
  [+-------------------+--------------------------+-----------------------------------+--------------------------------------------------+];
end;

/*** Основная процедура ***/

macro Report( rep, err, ord )

 var line_num;
 var dpDepList = TdpDepList;
 var InitialFNcash :integer = FNcash;
 var CounterPass = 0;

 var isDebet = TRUE;

  SetRtGlobalVariables;

 /* Переопределение выходного потока и включение прямой печати */
 CheckPrinter( RegPath, StrFor( 0 ), "ШИРОКИЙ" );
 SetPrintPort( "ШИРОКИЙ" );

  dpDepList.rewind;

while( 1 )

 if ( ( InitialFNcash == 0 ) and not dpDepList.next )
   break;
 end;
 if ( ( InitialFNcash != 0 ) and ( CounterPass > 0 ) )
   break;
 end;

 CounterPass = CounterPass + 1;

 /* Чтение записи о Подразделении */
 if ( InitialFNcash == 0 )
   sFilial = dpDepList.rec.Name;
   FNCash = dpDepList.rec.Code;
 elif ( not findDepartment(FNCash, sFilial) )
   sFilial = "???";
 end;


 /*************************************/
 /* Отчет по ордерам 210 - по ошибкам */
 /*************************************/

 if ( ( rep == 0 )  OR  ( err != 0 ) )
   list = 0;
   num = 0;
   SumI = $0;
   SumICom = $0;
   saveTypeAcc  = "";
   saveCodCurr  = -1;
   saveTypeOper = -1;
   saveApplType = -1;

   Message( " Отчет по найденным ошибкам по зачислению на счета" );

   cmdGetDocs = RsdCommand( "select ref.t_ErrCode as ref_errCode, " +
                                   "ref.t_AccType as ref_accType, " +
                                   "ref.t_Code_Currency as ref_code_currency, " +
                                   "ref.t_Operation as ref_operation, " +
                                   "acc.t_Account as acc_account, " +
                                   "sbt_to.t_czNameAlg as sbt_to_czNameAlg, " +
                                   "msg.t_Contents as msg_contents " +
                            "from dsb_edref_tmp ref, ddepositr_dbt acc, dsbbank_msg msg, dsb_typop_dbt sbt_to, dpc_apltp_dbt apl " +
                            "where ref.t_isCur = ? " +
                            "and acc.t_FNcash = ? " +
                            "and ref.t_ErrCode != 0 " +
                            "and ref.t_signRec in (" + string( SIGNORDER ) + ", " + string( SIGNPRINTORDER ) + ") " +
                            "and ref.t_Operation in (" + string( OP_PUTPR ) + ", " + string( OP_PAYTR ) + ", 0 ) " + 
                            "and apl.t_iscur(+) = acc.t_IsCur " +
                            "and apl.t_type(+) = acc.t_Type_Account " +
                            "and apl.t_appltype(+) = " + string( OT_OP_FROM_OTHER ) + " " +
                            "and acc.t_Referenc = ref.t_AccNumber " +
                            "and msg.t_Number(+) = ref.t_ErrCode " +
                            "and sbt_to.t_IsCur = nvl(apl.t_iscur, ref.t_IsCur) " +
                            "and sbt_to.t_Kind =  nvl(apl.t_aptype, ref.t_AccType) " +
                            "and sbt_to.t_NumOpert = DECODE (ref.t_Operation, 0, " + string( OP_PUTPR ) + ", ref.t_Operation) " +
                            "order by ref.t_Code_Currency, ref.t_AccType" );

   cmdGetDocs.addParam( "isCur", RSDBP_IN );
   cmdGetDocs.value( "isCur" ) = flag_Cur;
   cmdGetDocs.addParam( "FNcash", RSDBP_IN );
   cmdGetDocs.value( "FNcash" ) = FNcash;

   cmdGetDocs.execute;

   docsRs = RsdRecordset( cmdGetDocs );

   while ( docsRs.moveNext )
     if ( ( saveTypeAcc != docsRs.value( "ref_accType" ) )  OR
          ( saveCodCurr != docsRs.value( "ref_code_currency" ) ) )
       if ( ( saveCodCurr != -1 ) and ( saveTypeAcc != "" ) )
         ReportEnd();
       end;
       if ( saveTypeAcc != docsRs.value( "ref_accType" ) )
         saveTypeAcc = docsRs.value( "ref_accType" );
       end;
       if ( saveCodCurr != docsRs.value( "ref_code_currency" ) )
         saveCodCurr = docsRs.value( "ref_code_currency" );
       end;
       list = list + 1;
       num = 0;
       SumI = $0;
       ReportHead210Err();
       ReportTitleErr();
     end;
     ReportStringErr;
   end;

   if ( ( saveTypeAcc != "" )  AND  ( saveCodCurr != -1 ) )
     /* Были листы, закрыть таблицу */
     ReportErrEnd();
   end;

   Message( "" );
 end;

 if ( AllLists != 0 )
   printLn( strFor( 12 ) );
 end;

 /*************************************/
 /* Отчет по ордерам 245 - по ошибкам */
 /*************************************/

 if ( ( rep == 0 )  OR  ( err != 0 ) )
   list = 0;
   num = 0;
   SumI = $0;
   SumICom = $0;
   saveTypeAcc  = "";
   saveCodCurr  = -1;
   saveTypeOper = -1;
   saveApplType = -1;

   Message( " Отчет по найденным ошибкам по списанию со счетов" );

   cmdGetDocs = RsdCommand( "select ref.t_ErrCode as ref_errCode, " +
                                   "ref.t_AccType as ref_accType, " +
                                   "ref.t_Code_Currency as ref_code_currency, " +
                                   "ref.t_Operation as ref_operation, " +
                                   "acc.t_Account as acc_account, " +
                                   "sbt_to.t_czNameAlg as sbt_to_czNameAlg, " +
                                   "msg.t_Contents as msg_contents " +
                            "from dsb_edref_tmp ref, ddepositr_dbt acc, dsbbank_msg msg, dsb_typop_dbt sbt_to, dpc_apltp_dbt apl " +
                            "where ref.t_isCur = ? " +
                            "and acc.t_FNcash = ? " +
                            "and ref.t_ErrCode != 0 " +
                            "and ref.t_signRec in (" + string( SIGNORDER ) + ", " + string( SIGNPRINTORDER ) + ") " +
                            "and ref.t_Operation in (" + string( OP_GETCON ) + ", " + string( OP_PAYTR ) + ") " + 
                            "and acc.t_Referenc = ref.t_AccNumber " +
                            "and msg.t_Number(+) = ref.t_ErrCode " +
                            "and apl.t_iscur(+) = acc.t_IsCur " +
                            "and apl.t_type(+) = acc.t_Type_Account " +
                            "and apl.t_appltype(+) = " + string( OT_OP_FROM_OTHER ) + " " +
                            "and sbt_to.t_IsCur = nvl(apl.t_iscur, ref.t_IsCur) " +
                            "and sbt_to.t_Kind =  nvl(apl.t_aptype, ref.t_AccType) " +
                            "and sbt_to.t_NumOpert = ref.t_Operation " +
                            "order by ref.t_Code_Currency, ref.t_AccType" );

   cmdGetDocs.addParam( "isCur", RSDBP_IN );
   cmdGetDocs.value( "isCur" ) = flag_Cur;
   cmdGetDocs.addParam( "FNcash", RSDBP_IN );
   cmdGetDocs.value( "FNcash" ) = FNcash;

   cmdGetDocs.execute;

   docsRs = RsdRecordset( cmdGetDocs );

   while ( docsRs.moveNext )
     if ( ( saveTypeAcc != docsRs.value( "ref_accType" ) )  OR
          ( saveCodCurr != docsRs.value( "ref_code_currency" ) ) )
       if ( ( saveCodCurr != -1 ) and ( saveTypeAcc != "" ) )
         ReportEnd();
       end;
       if ( saveTypeAcc != docsRs.value( "ref_accType" ) )
         saveTypeAcc = docsRs.value( "ref_accType" );
       end;
       if ( saveCodCurr != docsRs.value( "ref_code_currency" ) )
         saveCodCurr = docsRs.value( "ref_code_currency" );
       end;
       list = list + 1;
       num = 0;
       SumI = $0;
       ReportHead245Err();
       ReportTitleErr();
     end;
     ReportStringErr;
   end;

   if ( ( saveTypeAcc != "" )  AND  ( saveCodCurr != -1 ) )
     /* Были листы, закрыть таблицу */
     ReportErrEnd();
   end;

   Message( "" );
 end;


 /*************************************/
 /* Отчет по расчету %% - по ошибкам  */
 /*************************************/

 if ( ( rep == 0 )  OR  ( err != 0 ) )
   list = 0;
   num = 0;
   SumI = $0;
   SumICom = $0;
   saveTypeAcc  = "";
   saveCodCurr  = -1;
   saveTypeOper = -1;
   saveApplType = -1;

   Message( " Отчет по ошибкам в процедуре расчета %%" );

   cmdGetDocs = RsdCommand( "select ref.t_ErrCode as ref_errCode, " +
                                   "ref.t_AccType as ref_accType, " +
                                   "ref.t_Code_Currency as ref_code_currency, " +
                                   "ref.t_Operation as ref_operation, " +
                                   "acc.t_Account as acc_account, " +
                                   "sbt_to.t_czNameAlg as sbt_to_czNameAlg, " +
                                   "msg.t_Contents as msg_contents " +
                            "from dsb_edref_tmp ref, ddepositr_dbt acc, dsbbank_msg msg, dsb_typop_dbt sbt_to, dpc_apltp_dbt apl " +
                            "where ref.t_isCur = ? " +
                            "and acc.t_FNcash = ? " +
                            "and ref.t_ErrCode != 0 " +
                            "and ref.t_signRec in (" + string( SIGNORDER ) + ", " + string( SIGNPRINTORDER ) + ") " +
                            "and ref.t_Operation = " + string( OP_CALCPR ) + " " +
                            "and acc.t_Referenc = ref.t_AccNumber " +
                            "and msg.t_Number(+) = ref.t_ErrCode " +
                            "and apl.t_iscur(+) = acc.t_IsCur " +
                            "and apl.t_type(+) = acc.t_Type_Account " +
                            "and apl.t_appltype(+) = " + string( OT_OP_FROM_OTHER ) + " " +
                            "and sbt_to.t_IsCur = nvl(apl.t_iscur, ref.t_IsCur) " +
                            "and sbt_to.t_Kind =  nvl(apl.t_aptype, ref.t_AccType) " +
                            "and sbt_to.t_NumOpert = ref.t_Operation " +
                            "order by ref.t_Code_Currency, ref.t_AccType" );

   cmdGetDocs.addParam( "isCur", RSDBP_IN );
   cmdGetDocs.value( "isCur" ) = flag_Cur;
   cmdGetDocs.addParam( "FNcash", RSDBP_IN );
   cmdGetDocs.value( "FNcash" ) = FNcash;

   cmdGetDocs.execute;

   docsRs = RsdRecordset( cmdGetDocs );

   while ( docsRs.moveNext )
     if ( ( saveTypeAcc != docsRs.value( "ref_accType" ) ) OR
          ( saveCodCurr != docsRs.value( "ref_code_currency" ) ) )
       if ( ( saveCodCurr != -1 ) and ( saveTypeAcc != "" ) )
         ReportEnd();
       end;
       if ( saveTypeAcc != docsRs.value( "ref_accType" ) )
         saveTypeAcc = docsRs.value( "ref_accType" );
       end;
       if ( saveCodCurr != docsRs.value( "ref_code_currency" ) )
         saveCodCurr = docsRs.value( "ref_code_currency" );
       end;
       list = list + 1;
       num = 0;
       SumI = $0;
       ReportErrorCalcPercents();
       ReportTitleErr();
     end;
     ReportStringErr;
   end;

   if ( ( saveTypeAcc != "" )  AND  ( saveCodCurr != -1 ) )
     /* Были листы, закрыть таблицу */
     ReportErrEnd();
   end;

   Message( "" );
 end;

 /**********************************************************************************/
 /* Подсчет листов по протоколу сумм процентов по вкладам, не зачисленных на карты */
 /**********************************************************************************/

// MsgBox(ord);
 if ( ord != 0 )
   line_num = 0;
   AllLists = 0;
   SumICom = $0;
   saveTypeAcc = "???";
   saveCodCurr = -1;

   Message( " Протокол сумм процентов по вкладам, подсчет листов" );

   cmdGetDocs = RsdCommand( "select doc.t_Type_Account as doc_type_account, doc.t_Code_Currency as doc_code_currency " +
                            "from dsb_edref_tmp ref, dsbdepdoc_dbt doc, DDEPOSITR_DBT dep " +
                            "where ref.t_isCur = ? " +
                            "and ref.t_ErrCode = 0 " +
                            "and ref.t_signRec in (" + string( SIGNORDER ) + ", " + string( SIGNPRINTORDER ) + ") " +
                            "and doc.t_iApplicationKind = ref.t_ApplicationKind " +
                            "and doc.t_ApplicationKey = ref.t_ApplicationKey " +
                            "and doc.t_typeOper = " + string( OP_PUTPR ) + " " +
                            "and doc.t_inSum <> 0 " +
                            "AND dep.t_referenc = doc.t_referenc " +
                            "AND BITAND( doc.t_flags2, 1 ) = 0 " +
                            "AND INSTR(dep.T_USERTYPEACCOUNT, 'К') <> 0 " +
                            "order by doc.t_Code_Currency, doc.t_Type_Account" );

   cmdGetDocs.addParam( "isCur", RSDBP_IN );
   cmdGetDocs.value( "isCur" ) = flag_Cur;
   cmdGetDocs.execute;

   docsRs = RsdRecordset( cmdGetDocs );

   while ( docsRs.moveNext )
     if ( ( saveTypeAcc != docsRs.value( "doc_type_account" ) )  OR
          ( saveCodCurr != docsRs.value( "doc_code_currency" ) ) )
       if ( saveTypeAcc != docsRs.value( "doc_type_account" ) )
         saveTypeAcc = docsRs.value( "doc_type_account" );
       end;
       if ( saveCodCurr != docsRs.value( "doc_code_currency" ) )
         saveCodCurr = docsRs.value( "doc_code_currency" );
       end;
       if ( ( saveTypeAcc != "???" )  AND
            ( saveCodCurr != -1 ) )
         AllLists = AllLists + 1;
       end;
       line_num = 0;
       num = 0;
     else
       if ( line_num >= ListLineNum )
         line_num = 0;
         AllLists = AllLists + 1;
       end;
     end;
     line_num = line_num + 1;
   end;
   Message( "" );

 /*****************************************************************************/
 /*       Выпуск протокола сумм процентов по вкладам, не зачисленных на карты */
 /*****************************************************************************/

   list = 0;
   line_num = 0;
   num = 0;
   SumI = $0;
   saveTypeAcc = "";
   saveCodCurr = -1;

   Message( " Формирование протокола сумм процентов по вкладам, подсчет листов" );

   cmdGetDocs = RsdCommand( "select doc.t_Type_Account as doc_type_account, doc.t_Code_Currency as doc_code_currency, " +
                            "doc.t_CodClient as doc_codClient, " +
                            "doc.t_InSum as doc_inSum, " +
                            "doc.t_Account as doc_account, " +
                            "cur.t_ExternalCode as ExternalCode, " +
                            "dep.t_SpecialAccess as SpecialAccess " +
                            "from dsb_edref_tmp ref, dsbdepdoc_dbt doc, DDEPOSITR_DBT dep, dcurrency_dbt cur " +
                            "where ref.t_isCur = ? " +
                            "and ref.t_ErrCode = 0 " +
                            "and ref.t_signRec in (" + string( SIGNORDER ) + ", " + string( SIGNPRINTORDER ) + ") " +
                            "and doc.t_iApplicationKind = ref.t_ApplicationKind " +
                            "and doc.t_ApplicationKey = ref.t_ApplicationKey " +
                            "and doc.t_typeOper = " + string( OP_PUTPR ) + " " +
                            "and doc.t_inSum <> 0 " +
                            "and cur.t_code_currency = doc.t_Code_Currency " +
                            "AND dep.t_referenc = doc.t_referenc " +
                            "AND BITAND( doc.t_flags2, 1 ) = 0 " +
                            "AND INSTR(dep.T_USERTYPEACCOUNT, 'К') <> 0 " +
                            "and not exists ( " +
                                   "select 1 " +
                                     "from dsclink_dbt lnk, dsccrddoc_dbt scr " +
                                    "where lnk.t_fncash = dep.t_fncash " +
                                      "and lnk.t_cardaccref = dep.t_referenc " +
                                      "and scr.t_acccardlinkref = lnk.t_acccardlinkref " +
                                      "and scr.t_fncash = lnk.t_fncash " +
                                      "and scr.t_date = ? " +
                                      "and scr.t_numoper = 72) " +
                            "order by doc.t_Code_Currency, doc.t_Type_Account" );

   cmdGetDocs.addParam( "isCur", RSDBP_IN );
   cmdGetDocs.value( "isCur" ) = flag_Cur;
   cmdGetDocs.addParam( "curdate", RSDBP_IN );
   cmdGetDocs.value( "curdate" ) = {curdate};
   cmdGetDocs.execute;

   docsRs = RsdRecordset( cmdGetDocs );

   while ( docsRs.moveNext )
     if ( ( saveTypeAcc != docsRs.value( "doc_type_account" ) ) OR
          ( saveCodCurr != docsRs.value( "doc_code_currency" ) ) )
       line_num = 0;
       list = list + 1;
       num = 0;
       if ( ( saveCodCurr != -1 ) and ( saveTypeAcc != "" ) )
         Protocol_Table();
       end;
       if ( saveTypeAcc != docsRs.value( "doc_type_account" ) )
         saveTypeAcc = docsRs.value( "doc_type_account" );
       end;
       if ( saveCodCurr != docsRs.value( "doc_code_currency" ) )
         saveCodCurr = docsRs.value( "doc_code_currency" );
       end;
       Protocol_Title();
       SumI = $0;
       StrCount = 0;
     end;
     if ( line_num >= ListLineNum )
       line_num = 0;
       list = list + 1;
       num = 0;
       Protocol_Table();
       Protocol_Title();
       SumI = $0;
       StrCount = 0;
     end;
     line_num = line_num + 1;
     num = num + 1;
     ProtocolString( docsRs.value( "doc_inSum" ) );
   end;

   if ( list != 0 )
     /* Были листы, закрыть таблицу */
     Protocol_Table();
   end;

   Message( "" );

 end;

end; // while( 1 )

/*
  OnError ( e )
    // Ошибки
    println ("Произошла ошибка");
    var i = 0;
    while ( i < RslDefEnv.ErrorCount )
      println ( " Системная ошибка - " + RslDefEnv.Error(i).descr );
      i = i + 1;
    end;
*/
end;

if ( {EoyProcRunning} )
  /* Для отключения макроса в процедуре завершения периода */
  /* return; */
  TxtDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\ENDPERIODTXTDIR", true );
  TxtDir = TxtDir + String( FNcash ) + "\\";
else
  TxtDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true );
end;

