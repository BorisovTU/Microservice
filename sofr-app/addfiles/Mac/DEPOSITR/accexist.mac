/*
$Name:             accexist.mac
$Module:           RS-Retail
$Description:      Проверка наличия счета у клиента в данном филиале
*/
//=================================================================
//  R-Style SoftWare Lab. 
//  RS-Retail 
// 
//  Проверка наличия счета у клиента в данном филиале
// 
//  Лепихов А.А. 
//  22.05.2006 
//=================================================================

import CommonInter, alg_vars, rsd;

private var ddep = TRecHandler( "i_dp_dep.rec" );
private var dpDepList = TdpDepList;
private var on_line;
private var ok = true;
private var cmd, rs;
private var acc_exists = false;

if (findDepartment(NumFNCash(), null, ddep))
  if (ddep.rec.AccessMode == I_DEPARTMENT_ACCESS_OFFLINE)
    on_line = false;
  else
    on_line = true;
  end;

  if (on_line)
    dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
    ddep = dpDepList.RecHandler;
    ok = dpDepList.next();
  end;

  // счета клиента определяются с учетом возможного дублирования клиента
  cmd = RsdCommand( "select count(*) nn from ddepositr_dbt " +
                    "where t_FNCash = ? " +
                    "  and t_Open_Close = chr(0) " +
                    "  and t_Referenc != " + ALG_DEPOSITOR.Referenc +	 // в момент вызова макроса уже сущетвует хотябы 1 счет - тот, который открываем
                    "  and t_CodClient in (select t_PartyId from dparty_dbt where t_PartyId = ? " +
                    "    union all " +
                    "    select d.t_PartyId from dparty_dbt d, dparty_dbt p " +
                    "      where d.t_MainPartyId = p.t_PartyId " +
                    "        and p.t_HasDoubler != chr(0) " +
                    "        and p.t_PartyId = ? " +
                    "    union all " +
                    "    select t_MainPartyId from dparty_dbt " +
                    "      where t_IsDoubler != chr(0) " +
                    "        and t_PartyId = ?) ");

  cmd.addParam( "FNCash", RSDBP_IN );
  cmd.addParam( "CodClient1", RSDBP_IN );
  cmd.addParam( "CodClient2", RSDBP_IN );
  cmd.addParam( "CodClient3", RSDBP_IN );

  while (ok)
    cmd.value( "FNCash" ) = ddep.rec.Code;
    cmd.value( "CodClient1" ) = ALG_COMDEPCLNT.rec.CodClient;
    cmd.value( "CodClient2" ) = ALG_COMDEPCLNT.rec.CodClient;
    cmd.value( "CodClient3" ) = ALG_COMDEPCLNT.rec.CodClient;
    cmd.execute;

    rs = RsdRecordset( cmd );
    if (rs.moveNext and (rs.value("nn") > 0))
      acc_exists = true;
      ok = false;
    elif (on_line)
      ok = dpDepList.next();
    else
      ok = false;
    end;
    rs.close();
  end;
end;

if (acc_exists)
  ALG_RESULT = OK_RES;
else
  MsgBox("У клиента нет открытых счетов");
  ALG_RESULT = ERR_RES;
end;

exit(1);
