/*
   VZ 07.10.98
   Библиотека макрофункций по вкладу "Срочный пенсионный"
*/
Import IsSrvDoc;

Const
  NullDate = Date(0,0,0);

/*---------- Инициализируемые структуры ---------------------------------*/
Var currentDate, ProcessingDate, 
    bDate = Date(0,0,0), 
    eDate = Date(0,0,0), 
    FromDate,
    found, Результат, ErrMsg,
    С_Панелью, UseMaxDate, NoPrint, ApplType,
    chkDate, LimitDate;

var doc = TDepFile ("sbdepdoc.dbt", "w");
file dr(depositr);

record  tmpDoc("sbdepdoc.dbt"); /* файл документов */

/* Определение количества полных месяцев, за которые может быть выплачен доход*/
MACRO GetFullMonths(BegDate, EndDate)
Var tmpDate, lastDate,
    day,mon,year, found;
Var Num,FirstDate;
/* 1.Определяем дату начала начисления дохода (ДННД)*/
/* 1.1. Определяем, были ли выплаты дохода за предыдущие месяцы */
  BegDate = ProcessingDate+1;
/* 1.3. Ищем последнюю операцию по выдаче дохода за полные месяцы в интервале
        от ProcessingDate до currentdate*/
  doc.KeyNum = 1;
  doc.Clear;
  doc.rec.Referenc = ALG_DEPOSITOR.Referenc;
  doc.rec.TypeOper = Выдача_Процентов;
  doc.rec.DepDate_Document = currentdate;
  doc.addFilter ("T_Referenc = " + String(ALG_DEPOSITOR.Referenc) + " and " +
                 "T_TypeOper = " + String(Выдача_Процентов) );
  found = doc.GetLE;
  while(found AND
       (doc.rec.Referenc == ALG_DEPOSITOR.Referenc) AND
       (doc.rec.TypeOper == Выдача_Процентов) AND
       (doc.rec.DepDate_Document <= currentdate) AND
       (doc.rec.DepDate_Document > ProcessingDate) )/* AND */
       /* VZ 22.12.98 Учитываем только выдачу процентов за полные месяцы */
/*       ((doc.ApplType == За_Полные_Месяцы_Уст) OR
        (doc.ApplType == За_Полные_Месяцы))) */

    /* ищем дату завершения полных месяцев */
    if( IsServDoc(doc.rec)
        /* VZ Не обращаем внимания на сторнирующие операции */
        AND (doc.rec.FlagStorn == StrFor(0))
        AND ((doc.rec.ApplType == За_Полные_Месяцы_Уст) OR
             (doc.rec.ApplType == За_Полные_Месяцы)
            ) )
        tmpDate  = ProcessingDate;
        lastDate = ProcessingDate;
        DateSplit(tmpDate,day,mon,year);
        /* VZ 18.11.98 Исправил ошибку по следующему счету
           16.09.98 - Открытие
           17.10.98 - Выдача %% за 1-ый полный месяц
           18.11.98 - Выдача %% за 2-ой полный месяц(ошибка была здесь!)
        */
        FirstDate = ProcessingDate /*-1*/; /* Дата отсчета месяцев */
        Num = 0;                    /* Номер просмотренного месяца */
        while (tmpDate < doc.rec.DepDate_Document)
          Num = Num + 1;
          lastDate = tmpDate;
          GetDateOff (FirstDate,2,Num,tmpDate); /* Дата отсчета + Num месяцев */
        end; /* while */
        BegDate = lastDate+1;
        found = False;
    else
      found = doc.Prev;
    end; /* if */
  end; /* while */
  doc.dropFilter;
  /* Определяем дату конца начисления дохода ДКНД */
  FirstDate = ProcessingDate /*-1*/; /* Дата отсчета месяцев */
  Num = 0;                    /* Номер просмотренного месяца */
  tmpDate  = ProcessingDate;
  lastDate = ProcessingDate;
  DateSplit(tmpDate,day,mon,year);
  while (tmpDate < currentDate)
    Num = Num + 1;
    lastDate = tmpDate;
    GetDateOff (FirstDate,2,Num,tmpDate); /* Дата отсчета + Num месяцев */
  end; /* while */
  EndDate = lastDate;
  SetParm(0,BegDate);
  SetParm(1,EndDate);
end; /* of MACRO*/

MACRO DefineAccParams
  /* Определяем дату обработки (ДОб) - ProcessingDate - которая равна дате
     пролонгации ДП или дате открытия счета + 1 день */
  if(ALG_DEPOSITOR.Prol_DateDep != NullDate)
    ProcessingDate = ALG_DEPOSITOR.Prol_DateDep-1;
  else
    ProcessingDate = ALG_DEPOSITOR.Start_DateDep;
  end; /* if */
  if (ProcessingDate < ALG_DEPOSITOR.Limit_Date)
    ProcessingDate = ALG_DEPOSITOR.Limit_Date; /* Ограничение критической датой */
  end;
end; /* MACRO*/

macro GetPercentsFromFullMonths
Var
    Rest, Summa, PercSum, AllPercSum;
  /* Предварительно для расчета процентов по операциям, надо рассчитать %% */
/*  VZ 12.04.99 Сбивает позицию в файле после отката транзакции
  Результат = Выполнить_Операцию(ALG_DEPOSITOR.Account, ALG_DEPOSITOR.Type_Account,
              ALG_DEPOSITOR.Code_Currency, Расчет_Процентов);
  if(Результат != 0)
    ErrMsg = "1.Ошибка при расчете %% = "+String(Результат);
    return;
  end;/* IF */
*/
  /* Определяем параметры счета */
  DefineAccParams;

  /* Ищем последний документ за дату, предыдущую ДОб */
  doc.KeyNum = 2;
  doc.Clear;
  doc.rec.Referenc = ALG_DEPOSITOR.Referenc;
  doc.rec.DepDate_Document = ProcessingDate;
  doc.addFilter("T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) );
  Rest = $0L;
  found = doc.GetLE;
  while(found AND
        (doc.rec.Referenc == ALG_DEPOSITOR.Referenc) AND
        (doc.rec.DepDate_Document == (ProcessingDate)))
    if(IsServDoc(doc.rec)
       AND (doc.rec.FlagStorn == StrFor(0)))
      Rest = doc.rec.Rest;
      found = False;
    else
      found = doc.Prev;
    end; /* if */
  end;/* while */
  doc.dropFilter;
  if(Rest == $0L)
    ErrMsg = "Не найдена необходимая операция за " + String(ProcessingDate);
    return;
  end;
  /* Ищем сумму выплаченных процентов - с ДОб до Дтек ищем операции по
     причислению и выдаче %%*/
  doc.KeyNum = 2;
  doc.Clear;
  doc.rec.Referenc = ALG_DEPOSITOR.Referenc;
  doc.rec.DepDate_Document = ProcessingDate+1;
  doc.rec.NumDayDoc = 0;
  doc.addFilter("T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) );
  Summa = $0L;
  found = doc.GetGE;
  while(found AND
        (doc.rec.Referenc == ALG_DEPOSITOR.Referenc) AND
        (doc.rec.DepDate_Document > ProcessingDate) AND
        (doc.rec.DepDate_Document <= currentDate))
    if(IsServDoc(doc.rec)
       AND (doc.rec.FlagStorn == StrFor(0)))
      /* Только для операций выдачи процентов за пред. сроки хранения */
      if((doc.rec.TypeOper == Зачисление_Процентов) AND
         (doc.rec.ApplType == Без_Подоперации)) /* не задан */
        Summa = Summa + doc.rec.InSum;
      end; /* if */

      if((doc.rec.TypeOper == Выдача_Процентов) AND
         (doc.rec.ApplType == Без_Подоперации))
        Summa = Summa - doc.rec.OutSum;
      end; /* if */

      if((doc.rec.TypeOper        == Зачисление_Процентов) AND
         (doc.rec.TypeComplexOper == Выдача_Процентов)     AND
         (doc.rec.ApplType        == Излишне_Причисленные))
        Summa = Summa - doc.rec.OutSum;
      end; /* if */

    end; /* if */
    found = doc.Next;
  end;/* while */
  doc.dropFilter;
  /* Начисляем проценты на Rest + Summa с ДННД ПО ДКНД*/
  if((Rest + Summa) < $0L)
    ErrMsg = "Сумма выплаченных процентов больше остатка на дату пролонгации";
    return;
  end; /* if */
  PercSum  = $0L;
  if((Rest + Summa) > $0L)
    GetFullMonths(bDate,eDate);
    Результат=Проценты_На_Операцию(ALG_DEPOSITOR.Account,
              ALG_DEPOSITOR.Type_Account,
              ALG_DEPOSITOR.Code_Currency, Rest+Summa,
              PercSum, bDate, eDate+1, 2001, 0); /* Расчет процентов идет до EndDate-1 */
    if(Результат != 0)
      ErrMsg = "2.Ошибка при расчете %% = "+String(Результат);
      return;
    end;/* IF */
  end; /* IF */
  AllPercSum = PercSum;
  /* С ProcessingDate по ДКНД  ищем операции дополнительных взносов */
  doc.KeyNum = 2;
  doc.rec.Referenc = ALG_DEPOSITOR.Referenc;
/*  doc.TypeOper = Дополнительный_Взнос; */
  doc.rec.DepDate_Document = ProcessingDate+1;
  doc.rec.NumDayDoc = 0;
  doc.addFilter ("T_Referenc = " + String(ALG_DEPOSITOR.Referenc) );
  found = doc.GetGE;
  while(found AND
       (doc.rec.Referenc == ALG_DEPOSITOR.Referenc) AND
/*       (doc.TypeOper == Дополнительный_Взнос) AND */
       (doc.rec.DepDate_Document > ProcessingDate) AND
       (doc.rec.DepDate_Document <  eDate))
     if(IsServDoc(doc.rec)
        AND (doc.rec.FlagStorn == StrFor(0))
        AND (doc.rec.TypeOper != Зачисление_Процентов)
        AND (doc.rec.InSum != 0))
       /* Начисляем %% на InSum c Min(даты операции,bDate) по ДКНД*/
        if(doc.rec.DepDate_Document < bDate)
          FromDate = bDate;
        else
          FromDate = doc.rec.DepDate_Document+1;
        end; /* if */
        Результат=Проценты_На_Операцию(ALG_DEPOSITOR.Account,
              ALG_DEPOSITOR.Type_Account,
              ALG_DEPOSITOR.Code_Currency, doc.rec.InSum,
              PercSum, FromDate, eDate+1, 2001, 0);
        if(Результат != 0)
          ErrMsg = "3.Ошибка при расчете %% = "+String(Результат);
          return;
        end;/* IF */
        AllPercSum = AllPercSum + PercSum;
     end; /* if */
     found = doc.Next;
  end; /* while */
  doc.dropFilter;
/* Указываем сумму в качестве максимально допустимой */
  ALG_PARAM.prmD = AllPercSum;
  /*ALG_UPDATE = 1;*/
end; /* macro*/
