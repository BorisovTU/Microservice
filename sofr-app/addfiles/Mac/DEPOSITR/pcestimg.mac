/****************************************************************
*  ---------------------                                        *
*  R-Style Software Lab.                                        *
*  ---------------------                                        *
*  RS-Retail                                                    *
*                                                               *
*  Отчет для группового начисления наращенных процентов         *
*  Имя файла и главной макрофункции менять нельзя               *
*                                                               *
*  Лебедев С.В.                                                 *
*  08.09.99                                                     *
28.11.2002г -KIN поменяны местами столбцы отчета по резерву

****************************************************************/
import rsd, commonInter;
import GrPrPrintErrors;

file currency (currency) key 0;

var
 TotalSummaCalcAll = 0,
 TotalSummaPay = 0;

var WasPrintHeader = FALSE;
var saveFlagCur = -1;

/****************************************************************
                         Печать заголовка
****************************************************************/
macro PrintHeader

  [ ];
  [            Групповой расчет и начисление накопленных процентов];
  [ ];
  [ +--------------------+------+-------------------------------+-------------+];
  [ |                    |      |          Сумма процентов      |             |];
  [ |     Вид вклада     |Валюта+-------------------------------+     Дата    |];
  [ |                    |      |   Начисленная |  Начисленная  |             |];
  [ |                    |      |    на дату    |     всего     |             |];
  [ +--------------------+------+---------------+---------------+-------------+];

  WasPrintHeader = TRUE;

end;


/****************************************************************
                Г Л А В Н А Я   П Р О Г Р А М М А
****************************************************************/
macro PcEstimG 
(
 TypeAccount,
 CodCur,
 CalcPayDate,
 SummaCalc,
 SummaCalcAll,
 SummaPay,
 SummaCalcNRez,
 SummaCalcAllNRez,
 SummaPayNRez
)

  if ( saveFlagCur != NumFlagCur() ) 
    PrintHeader();
    saveFlagCur = NumFlagCur();
    TotalSummaCalcAll = 0;
    TotalSummaPay = 0;
  end;


 TotalSummaCalcAll = TotalSummaCalcAll + SummaCalcAll + SummaCalcAllNRez;
 TotalSummaPay = TotalSummaPay + SummaPay + SummaPayNRez;

  var NameCur = "";


  if ((SummaCalc != 0) OR (SummaCalcAll != 0) OR (SummaPay != 0))
    currency.Code_Currency = CodCur;
    if (GetEQ(currency))
      NameCur = currency.Short_Name;
    end;
    [  ####################  ###   ############### ###############    ##########]
    (TypeAccount,NameCur,SummaPay:11:2:a,SummaCalcAll:11:2:a,CalcPayDate);

/* KIN поменяны местами столбцы отчета по резерву    
    (TypeAccount,NameCur,SummaCalcAll:11:2:a,SummaPay:11:2:a,CalcPayDate); */
  end;

  if ((SummaCalcNRez != 0) OR (SummaCalcAllNRez != 0) OR (SummaPayNRez != 0))
    currency.Code_Currency = CodCur;
    if (GetEQ(currency))
      NameCur = currency.Short_Name;
    end;
    [  ####################  ###   ############### ###############    ##########]
    (TypeAccount + " (н/р)",NameCur,SummaPayNRez:11:2:a,SummaCalcAllNRez:11:2:a,CalcPayDate);
    
/* KIN поменяны местами столбцы отчета по резерву      
     (TypeAccount + " (н/р)",NameCur,SummaCalcAllNRez:11:2:a,SummaPayNRez:11:2:a,CalcPayDate); */
  end;

end;

macro PrintFooter

  if ( saveFlagCur == NumFlagCur() ) 
  [ +---------------------------+---------------+---------------+];
  [  Итого                      |###############|###############|              ]
  (TotalSummaPay:11:2:a,TotalSummaCalcAll:11:2:a);  
  [ +---------------------------+---------------+---------------+];  

  end;
end;
macro PrintReportFromTempTable
// Когда обрабатываем через ХП, то для отчета все данные кидаем в таблицу InsertRecordIntoReport

  var Type_Account : String;
  var IsResident   : integer;
  var CurCurrency  : integer;
  var SummaPay, SummaCalcAll;
  var CalcPayDate  : Date;


  var cmd = RsdCommand( " select t_Type_Account, t_IsResident, t_Currency, t_SummaPay, t_SummaCalcAll, t_CalcPayDate " 
                        " from dcestrep_tmp " 
//                        " order by Type_Account, IsResident, Currency "
                      );

  cmd.execute();

  var rs = RsdRecordSet( cmd );

  while ( rs.moveNext )
    Type_Account = rs.value( "t_Type_Account" );
    IsResident   = rs.value( 1 );
    CurCurrency  = rs.value( 2 );
    SummaPay     = rs.value( 3 );
    SummaCalcAll = rs.value( 4 );
    CalcPayDate  = rs.value( 5 );

//    TotalSummaCalcAll = TotalSummaCalcAll + SummaCalcAll;
//    TotalSummaPay = TotalSummaPay + SummaPay;
  
    if ( IsResident == 0 )
      PcEstimG( Type_Account, CurCurrency, CalcPayDate, 0, SummaCalcAll, SummaPay, 0, 0,            0 ); 
    else
      PcEstimG( Type_Account, CurCurrency, CalcPayDate, 0, 0,            0,        0, SummaCalcAll, SummaPay ); 
    end;
  end;

  PrintFooter;
	
  cmd = RsdCommand(" truncate table dcestrep_tmp ");
  cmd.execute();

  OnError ( e )

    var i = 0;
    while ( i < RslDefEnv.ErrorCount )
      println ( " Системная ошибка - " + RslDefEnv.Error(i).descr );
      i = i + 1;
    end;

end;

macro printErrors
  PrintErrorsFromTable;
end;
