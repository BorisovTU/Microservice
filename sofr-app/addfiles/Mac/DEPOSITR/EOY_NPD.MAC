/*
   VZ 17.12.98
   Макрос корректирует историю по счетам некоторых видов вкладов
   (непролонгируемых депозитов) после завершения года.
   По счетам, которые в данный момент находятся в альт. состоянии и по
   которым в дальнейшем необходимо считать проценты на первоначальный взнос,
   а в процессе завершения года на них были начислены проценты, в файл
   дополнительной информации auxinfo.dbt заносятся корректирующие записи
*/
import deprintr, opercode;
file Curr(currency) key 0;
file dt  (sb_dtyp)  key 2;
file dr  (depositr) key 1;
file doc (sbdepdoc) key 1;
file aux (auxinfo)  key 0 write;
record  aux1 ("auxinfo.1");
array Обрабатываемые_Виды_Вкладов_Рубли;
array Обрабатываемые_Виды_Вкладов_Валюта;
Var IsCur, i, FNCash = NumFNCash, {curdate};

/**************************************************************************\
| Найти_Документ_по_Завершению_Года                                        |
\**************************************************************************/
MACRO Найти_Документ_по_Завершению_Года()
  doc.Referenc = dr.Referenc;
  doc.TypeOper = Зачисление_Процентов;
  doc.DepDate_Document = {curdate} - 1;
  if( GetEQ(doc))
    return True;
  else
    [Не найдена запись о завершении года по счету #########################]
     (Acc_Form(dr.Account));
    return False;
  end; /*fi*/
END; /*MACRO*/

/**************************************************************************\
| Создать_Корректирующую_Запись                                            |
\**************************************************************************/
MACRO Создать_Корректирующую_Запись()
Var day,mon,year, tmpDate, OldGlobPerc = $0L, OldAltPerc = $0L;
  /* ищем запись за прошлый год*/
  DateSplit(doc.DepDate_Document, day, mon, year);
  TmpDate = Date(day, mon, year-1);
  aux.Reference =  doc.Referenc;
  aux.InfoType  =  1;
  aux.Date      = TmpDate;
  if ( GetGE(aux) AND
       (aux.Reference ==  doc.Referenc) AND
       (aux.InfoType  ==  1) AND
       ((aux.Date  == TmpDate) OR
        (aux.Date  == (TmpDate+1)))) /* М.б. за 01.01.XXXX */
    SetRecordAddr(aux1,aux,0,0,True);
    OldGlobPerc = aux1.GlobPerc;
    OldAltPerc  = aux1.AltPerc;
  end; /* fi */
  ClearRecord(aux);
  SetRecordAddr(aux1,aux,0,0,True);
  aux1.Reference  = doc.Referenc;
  aux1.InfoType   = 1;
  aux1.Date       = doc.DepDate_Document + 1; /* Д.б. за 1.1.ХХХХ*/
  aux1.FNcash     = doc.FNCash;
  aux1.NumSession = 0;
  aux1.GlobPerc   = - doc.InSum + OldGlobPerc;
  aux1.GlobFlag   = "X";
  aux1.AltPerc    = - doc.InSum + OldAltPerc;
  aux1.AltFlag    = "X";
  if(Insert(aux))
   return True;
  else
    [Не удалось добавить корректирующую запись по счету #########################]
     (Acc_Form(dr.Account));
    return False;
  end; /*if*/
END; /*MACRO*/

/**************************************************************************\
| Вывести_Отчет                                                            |
\**************************************************************************/
MACRO Вывести_Отчет()
  [########################## - ########################]
  (Acc_Form(dr.Account), doc.InSum);
END; /*MACRO*/

/**************************************************************************\
| Обработать_Счета                                                         |
\**************************************************************************/
MACRO Обработать_Счета()
Var RecFound;
  if ( IsCur )
    dr.IsCur = 1;
  else
    dr.IsCur = 0;
  end; /* if */
  dr.FNCash = FNCash;
  dr.Open_Close = "";
  dr.Type_Account = dt.Kind;
  dr.Code_Currency = Curr.Code_Currency;
  dr.Number = 0;
  /* dr.Account = ""; */
  RecFound = GetGE(dr);
  while(  RecFound  AND
         ( dr.FNCash == FNCash ) AND
         ( dr.Open_Close == "" ) AND
         ( dr.Type_Account == dt.Kind ) AND
         ( dr.Code_Currency == Curr.Code_Currency))
    if ( dr.UseAlternate )
     if ( not Найти_Документ_по_Завершению_Года())
       return False;
     end; /*if*/
     if ( not Создать_Корректирующую_Запись())
       return False;
     end; /*if*/
     Вывести_Отчет;
    end; /*if*/
    RecFound = Next( dr );
  end; /* while */
  return True;
END; /*MACRO*/

/**************************************************************************\
| Обработать_Валюты                                                        |
\**************************************************************************/
MACRO Обработать_Валюты()
Var RecFound;
  if( not IsCur)
    Curr.Code_Currency = 0;
    if ( not GetEQ( Curr ) )
      [Не найдены рубли в справочнике валют];
      return False;
    end; /*if*/
    if ( not Обработать_Счета())
      return False;
    end; /*if*/
  else
    Curr.Code_Currency = 1;
    RecFound = GetGE( Curr );
    if ( not RecFound )
      [Не найдено ни одной валюты в справочнике валют];
      return False;
    end; /*if*/
    while( RecFound )
      [Валюта #########################](Curr.Name_Currency);
      if ( not Обработать_Счета())
        return False;
      end; /*if*/
      RecFound = Next( Curr );
    end;
  end; /*if*/
  return True;
END; /*MACRO*/

/**************************************************************************\
| Обработать_Вид_Вклада                                                    |
\**************************************************************************/
MACRO Обработать_Вид_Вклада(Вид_Вклада)
  if(IsCur)
   dt.FlagCur = 1;
  else
   dt.FlagCur = 0;
  end;
  dt.Kind    = Вид_Вклада;
  if (not GetEQ(dt))
    [Не найден вид вклада "############"](Вид_Вклада);
    return False;
  end;/*if*/
  [Обрабатывается вид вклада "############"]((Вид_Вклада));
  if (not Обработать_Валюты())
    return False;
  end; /*fi*/
  return True;
END; /*MACRO*/

/*=== Точка входа в макрос ===============================================*/
/*Заполняем массив обрабатываемых видов вкладов */
Обрабатываемые_Виды_Вкладов_Рубли(0)  = "СР.ДЕП. 4 М.";
Обрабатываемые_Виды_Вкладов_Рубли(1)  = "СР.ДЕП. 6 М.";
Обрабатываемые_Виды_Вкладов_Рубли(2)  = "СР.ДЕП. 12М.";
Обрабатываемые_Виды_Вкладов_Рубли(3)  = "Деп-6";
Обрабатываемые_Виды_Вкладов_Рубли(4)  = "Деп-12";
Обрабатываемые_Виды_Вкладов_Рубли(5)  = "Деп-12-после";
Обрабатываемые_Виды_Вкладов_Рубли(6)  = "Деп-3";
Обрабатываемые_Виды_Вкладов_Рубли(7)  = "Деп-1";

Обрабатываемые_Виды_Вкладов_Валюта(0) = "СР.ДЕП. 3 М.";
Обрабатываемые_Виды_Вкладов_Валюта(1) = "СР. ДЕП.12М.";
Обрабатываемые_Виды_Вкладов_Валюта(2) = "СР.ДЕП. 6 М.";
Обрабатываемые_Виды_Вкладов_Валюта(3) = "СР.ДЕП. 9 М.";

if (NumFlagCur == 0)
  /*Рубли*/
  IsCur = False;
  i=0;
  while ( i < asize(Обрабатываемые_Виды_Вкладов_Валюта))
    if (not Обработать_Вид_Вклада (Обрабатываемые_Виды_Вкладов_Рубли(i)))
      return;
    end; /* fi */
    i = i + 1;
  end; /*while*/
else
  /*Валюта*/
  IsCur = True;
  i=0;
  while ( i < asize(Обрабатываемые_Виды_Вкладов_Валюта))
    if (not Обработать_Вид_Вклада (Обрабатываемые_Виды_Вкладов_Валюта(i)))
      return;
    end; /*fi*/
    i = i + 1;
  end; /*while*/
end; /* fi */
/*--- EOF EOY_NPD.MAC ----------------------------------------------------*/