/*
               **************************
               *  R-Style SoftWare Lab. *
               **************************

    RS-Retail 2.0
    Сбербанк
 Вклад "СБ-501"

 Определение процентной ставки по альтернативным условиям
 (при нарушении) для СБ-501
------------------------------------------------------------
 Цель:
  обеспечить взятие различных ставок при закрытии договора
  в зависимости от срока, который пролежал вклад
  от начала договора либо от даты конца предыдущего
  договора:
- до 250 дней включительно - берется 1/2 (0.5) основной ставки
- после 250 дней . - 2/3 основной ставки

  В случае, если операция отлична от закрытия счета,
  берется ставка по условиям "До востребования"
------------------------------------------------------------

  Ромодин Александр Васильевич & Макашов М.И.
  12.04.2000
*/
Import DeprIntr,srpnrt_0;
/*
  Структуры
*/
record PC_ALG            (pc_alg);     /* Параметры расчета процентов */
record DEPOSITR_SRPNRT_2 (depositr);   /* Текущий счет вкладчика      */
private record SBDEPDOC          (sbdepdoc);   /* Текущий документ вкладчика  */

file PC_RATE (pc_rate) key 0; /* Справочник ставок           */
/*
  Количество дней, которые должен отлежать вклад, чтобы получить право
  закрываться по повышенной ставке
*/
const NumLimitDay  = 200,
      NumLimitDay2 = 250;
/*
  Операции закрытия (добавьте свою следующим элементом массива)
*/
Array OperClose;
OperClose (0) = 10; /* Закрытие наличными      */
OperClose (1) = 81; /* Закрытие безналичными   */
OperClose (2) = 96; /* Закрытие переводом      */
const NumArr = Asize(OperClose);

/* var MacroError = 0; */

/**********************************************************/

macro BOOL_Операция_Закрытия (NumOper)
/*
  Возвращает True, если имеем дело с операцией закрытия
*/
  var stat = False;
  var i = 0;

  while ((NOT stat) AND (i < NumArr))
    if (NumOper == OperClose(i))
      stat = True;
    end;
    i = i + 1;
  end;
  return stat;
end;
/**********************************************************/

macro BOOL_Счет_закрыт
/*
  Возвращает True, если счет закрыт
*/
  var stat;
  stat = (DEPOSITR_SRPNRT_2.Open_Close == "З");
  return stat;
end;
/**********************************************************/

macro NeedSpecific (BeginDate,NumOper)
/*
  Возвращает True в случае, если ставка определяется
  по специфическим условиям
*/
  var stat;
  var statDate = 0;
  var ClosedAcc = False;
  var StartDate;
  var LimitDate, LimitDate2;
  var TestDate = Date(31,12,2999);
  ClosedAcc = BOOL_Счет_закрыт ();
  if (NOT ClosedAcc)
    stat =  BOOL_Операция_Закрытия (NumOper);
    if ((NOT stat)
     AND (DEPOSITR_SRPNRT_2.UseAlternate != 0)            /* Возможно, если была выдача наследства */
     AND (DEPOSITR_SRPNRT_2.End_DateDep  != Date(0,0,0)))
      stat = True;
    end;
  else
    stat = True;
  end;
  if (stat)
    if (DEPOSITR_SRPNRT_2.End_DateDep == Date(0,0,0))
      stat = False; /* Договор был завершен- расчет по "До востребования" */
    end;
  end;
  if (stat)
    if (DEPOSITR_SRPNRT_2.UseAlternate != 0)
      TestDate = GetFirstWillDate (Date(31,12,2999),DEPOSITR_SRPNRT_2,SBDEPDOC); // Первая выдача наследства и есть как бы закрытие счета 
      if (TestDate < BeginDate)  // Следующие после выдачи наследства периоды
        stat = False;  // Считаем по "До востребования"
      end;
    end;
  end;
  if (stat)
    if (TestDate == Date(31,12,2999))
      if (ClosedAcc)
        TestDate = DEPOSITR_SRPNRT_2.Close_Date;
      else
        TestDate = SBDEPDOC.DepDate_Document;
      end;
    end;
  end;
  if (stat)
    if (DEPOSITR_SRPNRT_2.Prol_DateDep != Date(0,0,0))
      StartDate = DEPOSITR_SRPNRT_2.Prol_DateDep - 1;
    else
      StartDate = DEPOSITR_SRPNRT_2.Start_DateDep;
    end;
    LimitDate = StartDate + NumLimitDay;
    LimitDate2 = StartDate + NumLimitDay2;
/*  if (TestDate <= LimitDate) */
/*      statDate = 0;  Ставка- по "До востребования" */
/*    else */
      if (TestDate <= LimitDate2)
        statDate = 1; /* Ставка- по 1/2 основной */
      else
        statDate = 2; /* Ставка- по 2/3 основной */
      end;
/*  end;  */
  end;
  return statDate;
end;
/**********************************************************/

macro GetAltRate (alg,dr,BeginDate,EndDate,doc)
  var ResRate:doublel = 0.0l;
  var Rate;
  var RateDate; /* Ставка фиксированная, действует с начала договора */
  var NeedSp;

  setbuff (PC_ALG,alg);
  setbuff (DEPOSITR_SRPNRT_2,dr);
  setbuff (SBDEPDOC,doc);

  NeedSp = NeedSpecific(BeginDate,SBDEPDOC.TypeComplexOper);
  if (NeedSp == 1)
/*
    Вычисление ставки как части от ставки по основным условиям
    (ради чего писался макрос)
*/
    if (DEPOSITR_SRPNRT_2.Prol_DateDep != Date(0,0,0))
      RateDate = DEPOSITR_SRPNRT_2.Prol_DateDep;
    else
      RateDate = DEPOSITR_SRPNRT_2.Start_DateDep;
    end;
    ClearRecord (PC_RATE);
    Rate = PercRateAL (DEPOSITR_SRPNRT_2.Referenc,2001,RateDate);
    if (Rate > 0)
      ResRate = doubleL(Rate/2);
      MacroError = 0;    /* Ставка определена */
    else
      MacroError = 3649; /* Ошибка при определении процентной ставки */
    end;
  elif (NeedSp == 2)
/*
    Вычисление ставки 2/3 от ставки по основным условиям

*/
    if (DEPOSITR_SRPNRT_2.Prol_DateDep != Date(0,0,0))
      RateDate = DEPOSITR_SRPNRT_2.Prol_DateDep;
    else
      RateDate = DEPOSITR_SRPNRT_2.Start_DateDep;
    end;
    ClearRecord (PC_RATE);
    Rate = PercRateAL (DEPOSITR_SRPNRT_2.Referenc,2001,RateDate);
    if (Rate > 0)
      ResRate = doubleL(Rate*2/3);
      MacroError = 0;    /* Ставка определена */
    else
      MacroError = 3649; /* Ошибка при определении процентной ставки */
    end;
  else
    MacroError = -999;   /* Ставка будет опред. из усл. в настройке процентов */
  end;
  return ResRate;
end;
