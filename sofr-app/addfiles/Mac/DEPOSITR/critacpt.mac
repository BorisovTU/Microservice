/*
$Name:             critacpt.mac
$Module:           RS-Retail
$Description:      Макрос запроса на подтверждение рискованных действий
*/

import CommonInter, OperCode;

const
  CM_DEP       = 1,  // Вкладные операции, кроме закрытия счета и наследства.
  CM_CLOSE     = 2,  // Операции закрытия счета.
  CM_WILL      = 3,  // Выплаты наследникам ( операция 99 ).
  CM_NOTFIN    = 4,  // Нефинансовые операции.
  CM_STORN_OP  = 5,  // Сторнирование операции.
  CM_DELETE_OP = 6;  // Удаление операции.

const FNCash  = NumFNCash();

var fil = TBFile ( "listfdep.dbt", "r", 0 );

record ro ( ret_op );
record depDoc ( "sbdepdoc.dbt" );
record trast  ( "sbtrast.dbt" );

var fAdd = FALSE;  // Флаг дополнительного контроля.

var sDesc = "";   // Комментарий по рискованному действию.

/*******************************************************************/
/*   Проверки для вкладных операций, кроме закрытия и наследства.  */
/*******************************************************************/
macro check_Dep( _IsCur, _Operation, _rez, _trast, _income, _sum )
 var ret = FALSE;

 /*
 if ( ( _IsCur != 0 )  AND  // Операция в ин.валюте.
      ( ( _Operation == Открытие_Наличными )  OR
        ( _Operation == Дополнительный_Взнос ) ) )
   if ( _rez != 0 )  // Нерезидент.
     ret = TRUE;
   end;
 end;
 */

 if ( _Operation == Расход )
   if ( _trast != 0 )  // Доверенное лицо.
     sDesc = sDesc + ". Операция выполнена доверенным лицом.";
     ret = TRUE;
   end;
 end;

 if ( _income < 0 )  // Расходные операции.
   if ( _sum > 15000.0 )  // Сумма более 15000 рублей в эквиваленте.
     sDesc = sDesc + ". Расходная операция на сумму более 15000 рублей.";
     ret = TRUE;
   end;
 end;

 if ( _rez != 0 )  // Нерезидент.
   sDesc = sDesc + ". Операция по счету нерезидента.";
   ret = TRUE;
 end;

 return ret;
end;


/*******************************************************************/
/*                 Проверки для закрытия счета.                    */
/*******************************************************************/
macro check_Close( _trast, _rez, _sum )

 var ret = false;

 if ( GetBankStandart( ) == 0 ) /* 0 - Сбербанк, 1 - коммерческий банк */
   if ( _sum > 15000.0 )  // Сумма более 15000 рублей в эквиваленте.
     sDesc = sDesc + ". Счет закрыт на сумму более 15000 рублей.";
   end;

   ret = true;
 else
   if ( _sum > 15000.0 )  // Сумма более 15000 рублей в эквиваленте.
     sDesc = sDesc + ". Счет закрыт на сумму более 15000 рублей.";
     ret = true;
   end;
   if ( _trast != 0 )  // Доверенное лицо.
     ret = true;
   end;
   if ( _rez != 0 )  // Нерезидент.
     ret = true;
   end;
 end;

 return ret;

end;


/*******************************************************************/
/*                 Проверки для выдачи наследства.                 */
/*******************************************************************/
macro check_Will()
 return fAdd;
end;

/*******************************************************************/
/*              Проверки для нефинансовых оперций.                 */
/*******************************************************************/
macro check_NotFin( _operation )
 var ret = FALSE;
 if ( ( _operation == 206 )  OR  // Новая сбер.книжка взамен исписанной.
      ( _operation == 222 )  OR  // Ввод новой доверенности.
      ( _operation == 225 )  OR  // Отмена доверенности.
      ( _operation == 232 )  OR  // Ввод завещания.
      ( _operation == 233 )  OR  // Изменение завещания.
      ( _operation == 235 )  OR  // Отмена завещания.
      ( _operation == 252 )  OR  // Наложение ареста на весь счет.
      ( _operation == 253 )  OR  // Наложение ареста на часть счета.
      ( _operation == 255 )  OR  // Снятие ареста со счета.
      ( _operation == 502 )  OR  // Наложение ареста на счет.
      ( _operation == 503 )  OR  // Снятие ареста со счета.
      ( _operation == 504 ) )    // Выдача сбер.книжки.
   ret = fAdd;
 end;
 return ret;
end;

/*******************************************************************/
/*              Проверки для сторнирования оперций.                */
/*******************************************************************/
private macro check_Storn( _Desc, _Spec, _ApplKey )
  var vApplKind = Int( SubStr( _Spec, 1, 4 ) );
  var VDesc = "", vSum = "";
  var VMsg = "";
  if ( GetDocDesc( vApplKind, _ApplKey, VDesc, vSum ) )
    VMsg = "Прошу разрешить сторнирование документа: " + VDesc + " на сумму " + vSum;
  else
    VMsg = "Прошу разрешить сторнирование документа";
  end;
  return CritWaitAcceptEx( 0, vMsg, _Spec );
end;

/*******************************************************************/
/*                  Проверки для удаления оперций.                 */
/*******************************************************************/
private macro check_Del( _Desc, _Spec, _ApplKey )
  var vApplKind = Int( SubStr( _Spec, 1, 4 ) );
  var VDesc = "", vSum = "";
  var VMsg = "";
  if ( GetDocDesc( vApplKind, _ApplKey, VDesc, vSum ) )
    VMsg = "Прошу разрешить удаление документа: " + VDesc + " на сумму " + vSum;
  else
    VMsg = "Прошу разрешить удаление документа";
  end;
  return CritWaitAcceptEx( 0, VMsg, _Spec );
end;

/*******************************************************************/
/*                         Головная процедура.                     */
/*******************************************************************/
macro Crit_Main( _Type, _Prm, _rez, _trast, _income, _age, _sum, _Desc, _Spec, _AddrDoc, _AddrTrast )
 var accept = TRUE;
 var fMake  = FALSE;

 Message( " Ожидание разрешения на выполнение рискованного действия. ~Alt-Break~ - прервать" );

 if (StrFor(GetProgramID) == "П")
   return TRUE;
 end;

 /* Чтение записи о Филиале */
 fil.rec.FNCash = FNCash;
 if ( fil.GetEQ()  AND  fil.rec.FlagCritAdd )
   fAdd = TRUE;
 end;

 SetBuff( ro, _Prm );

 sDesc = _Desc;

 // Проверки для видов вызова макроса.
 if   ( _Type == CM_DEP )     // Вкладные операции, кроме закрытия и наследства.
   ClearRecord( depDoc );
   if ( _AddrDoc != NULL )
     SetBuff( depDoc, _AddrDoc );
   end;
   fMake = check_Dep( ro.IsCur, ro.Operation, _rez, _trast, _income, _sum );
 elif ( _Type == CM_CLOSE )   // Операции закрытия счета.
   fMake = check_Close( _trast, _rez, _sum );
 elif ( _Type == CM_WILL )    // Выплаты наследникам ( операция 99 ).
   fMake = check_Will();
 elif ( _Type == CM_NOTFIN )  // Нефинансовые операции.
   if ( _AddrTrast != NULL )
     SetBuff( trast, _AddrTrast );
   end;
   fMake = check_NotFin( ro.Operation );
 elif ( _Type == CM_STORN_OP )  // Сторнирование операции.
   if ( check_Storn( _Desc, _Spec, ro.ApplicationKey ) )
     return TRUE;
   else
     msgBox( "Действие отклонено." );
     return FALSE;
   end;
 elif ( _Type == CM_DELETE_OP )  // Удаление операции.
   if ( check_Del( _Desc, _Spec, ro.ApplicationKey ) )
     return TRUE;
   else
     msgBox( "Действие отклонено." );
     return FALSE;
   end;
 end;

 // Несовершеннолетний клиент.
 if ( _age < 18 )
   fMake = TRUE;
 end;

 // Вызов процедуры контроля критических (рискованных) действий.
 if ( fMake )
   accept = CritWaitAcceptEx( 0, sDesc );
 end;

 if ( NOT accept )
   msgBox( "Действие отклонено." );
 end;

 return accept;

end;
