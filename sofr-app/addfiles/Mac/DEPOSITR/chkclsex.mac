/*************************************************************************

  Макpос пpовеpки базы пеpед запуском пpоцедуpы закpытия дня ОП

**************************************************************************/

import DeprIntr, Alg_Vars;

file CashAcc("sb_casac.dbt") key 0;
file CashVal("sb_casvl.dbt") key 0;

var
  FNCash  = NumFNCash;

macro GetNativeSafe

  file Group("rtgroup.dbt") key 2;

  var
     OperBrigade = GetOperBrigade();

  Group.Branch = FNCash;
  Group.Date = {curdate};
  Group.ID = OperBrigade;
  if(GetEQ(Group))
    return Group.SafeID;
  else
    MsgBox("Не найден сейф бригады № ", OperBrigade);
    Exit;
  end;
end;

/* Проверка на нулевые остатки в кассе ОП */
macro CheckExchangeSafeRests()
  var
      Loop,
      Safe    = GetNativeSafe(),
      NameValue,
      NoRests = true;

  Println( "\n                 Остатки ценностей в кассе ОП \"", Safe, "\"\n");
  [-------------------------------------------------------------------------------];
  [|Код|            Наименование ресурса               |       Сумма      |Кол-во|];
  [-------------------------------------------------------------------------------];

  ClearRecord( CashAcc );
  CashAcc.Branch   = FNCash;
  CashAcc.Date     = {curdate};
  CashAcc.CashOper = Safe;
  Loop = getGE( CashAcc );

  while(       Loop
      and ( CashAcc.Branch   == FNCash   )
      and ( CashAcc.Date     == {curdate})
      and ( CashAcc.CashOper == Safe     )
      and ( CashAcc.SubAccount == ""     )
       )
    if( ( CashAcc.RestMoney != 0 ) or ( CashAcc.RestCol != 0 ) )
      CashVal.RefValue = CashAcc.RefValue;
      if( GetEQ( CashVal ) )
        NameValue = CashVal.NameValue;
      else
        NameValue = "";
      end;
      [|###|###############################################|##################|######|]
      (CashAcc.RefValue, NameValue, CashAcc.RestMoney, CashAcc.RestCol );
      NoRests = false;
    end;
    Loop = Next( CashAcc );
  end;
  if( not NoRests )
    [-------------------------------------------------------------------------------];
  end;

  return NoRests;
end;



/* В ОП или ОКВКУ нужно сдать остатки кассы */

  ALG_RESULT = OK_RES;
  if( CheckExchangeSafeRests() )
    PrintLn(" Остатков в кассе нет\n");
  else
    ALG_RESULT = ERR_RES;
  end;
end;
