/* Отчеты по ЦЗ */

import ci_defs;
            
class ( TCIRecGenerator ) TMiscRecGenerator( _oper, _makeReport, _observer )

  var
    miscDocInfo = TRecHandler( "ci_doc.2" ),
    miscRecInfo = TRecHandler( "ci_rec.2" );

  var
    totNumItems=0,
    totSum=$0,
    totSumSold=$0,
    totItemsSold=0,
    totSumBought=$0,
    totItemsBought=0;
  
  macro f5_outHead;
  
    [
                                                                                 Филиал №
                                                                                 отделения
                                                                                 Сбербанка РФ
  
  
                   Отчет по операциям по облигациям целевого займа за ##########
  
    ]
    (
      {curdate}
    );
  end;
  
  macro f5_outCommonHead;
  
    [+-------+----------------------------------------------+--------+-------------------+];
    [|   №   |               Н а з в а н и е                | Колич. |     С у м м а     |];
    [+-------+----------------------------------------------+--------+-------------------+];
  end; 
  
  macro f5_outSellHead;
  
    [ ];
    [Продажа облигаций целевого займа];
    [ ];
    f5_outCommonHead;
  end;
  
  macro getNumItems
  
    var
      recFound,
      numItems=0;
  
    miscDocInfo.setRecordAddr(capIssueDoc,0,
      capIssueDoc.fldOffset("Info"),true);

    capIssueDoc.rec.RecApplicKind=capIssueRec.rec.ApplicationKind;
    capIssueDoc.rec.RecApplicKey=capIssueRec.rec.ApplicationKey;
  
    recFound=capIssueDoc.getEQ;
    while(recFound
      and (capIssueDoc.rec.RecApplicKind==capIssueRec.rec.ApplicationKind)
      and (capIssueDoc.rec.RecApplicKey==capIssueRec.rec.ApplicationKey))
      if ( checkCIDoc )
        numItems=numItems+miscDocInfo.rec.NumItems;
      end;
      recFound=capIssueDoc.next;
    end;
    return numItems;
  end;
  
  macro f5_outBuyHead;
  
    [ ];
    [Покупка облигаций целевого займа];
    [ ];
    f5_outCommonHead;
  end;
  
  macro f5_outLine;
  
    miscRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    [| ##### | ############################################ | ###### | ################# |]
    (
      capIssueRec.rec.Issue,
      miscRecInfo.rec.Name,
      getNumItems,
      capIssueRec.rec.Sum
    );
  end;
  
  macro f5_outTotals;
  
    [+------------------------------------------------------+--------+-------------------+];
    [|  И т о г о :                                         | ###### | ################# |]
    (
      totNumItems,
      totSum
    );
  end;
  
  macro f5_sellReport
  
    var
      recFound;
  
    miscRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    totNumItems=0;
    totSum=$0;
  
    capIssueRec.clear;
    capIssueRec.rec.Branch=branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_MISC;
  
    f5_outSellHead;
    recFound=capIssueRec.getGE;
    while(RecFound
      and (capIssueRec.rec.Branch==branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_MISC))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Brigade==operBrigade)
        and (capIssueRec.rec.Operation==O_MISC_SELL))
        f5_outLine; 
        totNumItems=totNumItems+getNumItems;
        totSum=totSum+capIssueRec.rec.Sum;
      end;
      recFound=capIssueRec.next;
    end;
    f5_outTotals;
  end;
      
  macro f5_buyReport
  
    var
      recFound;
  
    miscRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    totNumItems=0;
    totSum=$0;
  
    capIssueRec.clear;
    capIssueRec.rec.Branch=branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_MISC;
  
    f5_outBuyHead;
    recFound=capIssueRec.getGE;
    while(recFound
      and (capIssueRec.rec.Branch==branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_MISC))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Brigade==operBrigade)
        and (capIssueRec.rec.Operation==O_MISC_BUY))
        f5_outLine; 
        totNumItems=totNumItems+getNumItems;
        totSum=totSum+capIssueRec.rec.Sum;
      end;
      recFound=capIssueRec.next;
    end;
    f5_outTotals;
  end;
      
  macro f5_report;
  
    if ( not makeReport )
      return;
    end;

    f5_outHead;         
    f5_sellReport;
    f5_buyReport;
  end;
  
  InitTCIRecGenerator( _oper, _makeReport, _observer );
end;

macro miscReport

  var
    gen = TMiscRecGenerator( {oper}, true );

  gen.f5_report;
end;