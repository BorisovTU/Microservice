/*
$Name:           euro2cur.mac
$Module:         RETAIL
$Description:    euro2cur
*/

/* (c) demaio 2001 */
import deprintr,alg_vars,opercode;
private file dr(depositr) key 0 write;
private file cr(currency) key 0;
private file dd(sbdepdoc) key 3;
private file dt(sb_dtyp) key 2 write;
private file ai(auxinfo) key 0 write;
private file pc(pc__calc) key 0 write;
private file pd(pc_drest) key 0 write;
private file pe(pcestim) key 0 write;
private file dh(dephistr) key 0 write;
private file ar(accreg) key 1 write;
var ddep = TRecHandler( "i_dp_dep.rec" );
record ai_prc("auxinfo.1");
record tmpDoc("sbdepdoc.1");
const
  UPDATE_ALL=1,
  UPDATE_DEP=2,
  UPDATE_DOC=4;
private const NullDate=Date(0,0,0);
var TrnStatus=true;
var DateX=NullDate;
var Rate=0.0L;
var Old_Code_Currency=0;
array Rates;
Rates(0)="ATS13.7603";
Rates(1)="BEF40.3399";
Rates(2)="GRD340.750";
Rates(3)="IEP0.787564";
Rates(4)="ESP166.386";
Rates(5)="ITL1936.27";
Rates(6)="LUF40.3399";
Rates(7)="DEM1.95583";
Rates(8)="NLG2.20371";
Rates(9)="PTE200.482";
Rates(10)="FIM5.94573";
Rates(11)="FRF6.55957";
macro FindDepType
  dt.FlagCur=ALG_DEPOSITOR.IsCur;
  dt.Kind=ALG_DEPOSITOR.Type_Account;
  return GetEQ(dt);
end;
macro GetPcCalcForDate(operdate)
var
  retdate=NullDate,ObjectType=2001;
  if(ALG_DEPOSITOR.UseAlternate)
    ObjectType=2002;
  end;
  pc.Referenc=ALG_DEPOSITOR.Referenc;
  pc.ObjectType=ObjectType;
  pc.TypeRecord=1;
  pc.EndDate=operdate;
  if(GetGE(pc)
     and (pc.Referenc==ALG_DEPOSITOR.Referenc)
     and (pc.ObjectType==ObjectType) and (pc.TypeRecord==1))
    retdate=pc.BeginDate;
  end;
  return retdate;
end;
macro GetRate
var
  i=0;
  cr.Code_Currency=Old_Code_Currency;
  if(GetEQ(cr))
    while(i<ASize(Rates))
      if(SubStr(Rates(i),1,3)==cr.Short_Name)
        Rate=Double(SubStr(Rates(i),4));
        return true;
      end;
      i=i+1;
    end;
  end;
  return false;
end;
macro FindDoc(AppKind,AppKey)
  dd.iApplicationKind=AppKind;
  dd.ApplicationKey=AppKey;
  return GetEQ(dd);
end;
macro FindOldCodeCurrency
file ln(sb_casln) key 1;
var
  stat,AppKind,AppKey;
  ln.ApplicationKind2=ALG_SBDEPDOC.iApplicationKind;
  ln.ApplicationKey2=ALG_SBDEPDOC.ApplicationKey;
  ln.RefValue2=0;
  if(GetEQ(ln))
    AppKind=ln.ApplicationKind1;
    AppKey=ln.ApplicationKey1;
    KeyNum(ln,0);
    ln.ApplicationKind1=AppKind;
    ln.ApplicationKey1=AppKey;
    ln.NumLinkDoc=0;
    stat=GetGE(ln);
    while(stat and (ln.ApplicationKind1==AppKind) and (ln.ApplicationKey1==AppKey))
      if(FindDoc(ln.ApplicationKind2,ln.ApplicationKey2) and (dd.TypeOper==Списание_Простое))
        Old_Code_Currency=dd.Code_Currency;
        return true;
      end;
      stat=Next(ln);
    end;
  end;
  return false;
end;
macro GetDateX
var
  retdate=NullDate,tmpdate;
  if(dt.FormContr and not ALG_DEPOSITOR.UseAlternate)
    GetProlongDateForCurDate(ALG_DEPOSITOR.Referenc, {curdate}, retdate);
  else
    retdate=GetPcCalcForDate({curdate});
  end;
  tmpdate=ALG_DEPOSITOR.Open_Date+1;
  if(retdate==tmpdate)
    retdate=ALG_DEPOSITOR.Open_Date;
  end;
  if(retdate==NullDate)
    if(ALG_DEPOSITOR.Limit_Date==NullDate)
      retdate=ALG_DEPOSITOR.Limit_Date;
    else
      retdate=ALG_DEPOSITOR.DateBeginPerc;
    end;
  end;
  if((ALG_DEPOSITOR.Limit_Date!=NullDate)
     and (retdate<=ALG_DEPOSITOR.Limit_Date))
    retdate=ALG_DEPOSITOR.Limit_Date+1;
  end;
  return retdate;
end;
macro UpdatePCDrest
var
  stat,res=true,ObjectType=2001;
  if(ALG_DEPOSITOR.UseAlternate)
    ObjectType=2002;
  end;
  pd.Referenc=ALG_DEPOSITOR.Referenc;
  pd.Type_Object=2001;
  pd.Date_Rest=DateX;
  stat=GetLE(pd);
  while(stat and (pd.Referenc==ALG_DEPOSITOR.Referenc))
    if((pd.Date_Rest==ALG_SBDEPDOC.DepDate_Document) and (pd.Type_Object==ObjectType))
      res=true;
    else
      pd.Rest=pd.Rest*Rate;
      pd.NumSession=0;
      res=Update(pd);
    end;
    if(res)
      stat=Next(pd);
    else
      stat=res;
    end;
  end;
  return res;
end;
macro UpdatePCCalc
var
  stat,res=true;
  pc.Referenc=ALG_DEPOSITOR.Referenc;
  pc.ObjectType=2001;
  pc.TypeRecord=1;/*0 - ?*/
  pc.EndDate=DateX;
  stat=GetGE(pc);
  while(stat and (pc.Referenc==ALG_DEPOSITOR.Referenc))
    pc.SumCalc=pc.SumCalc*Rate;
    pc.SumPay=pc.SumPay*Rate;
    pc.SumForPay=pc.SumForPay*Rate;
    pc.SumPayAll=pc.SumPayAll*Rate;
    pc.TaxSumCalc=pc.TaxSumCalc*Rate;
    pc.TaxSumPay=pc.TaxSumPay*Rate;
    pc.TaxSumForPay=pc.TaxSumForPay*Rate;
    pc.NumSession=0;
    res=Update(pc);
    if(res)
      stat=Next(pc);
    else
      stat=res;
    end;
  end;
  return res;
end;
/**************************************************************************\
|           Поиск последнего документа по накопленным процентам            |
\**************************************************************************/
macro GetLastPcEstim

  var stat;
  var find_last = FALSE;

  pe.Referenc   = ALG_DEPOSITOR.Referenc;
  pe.DateOperat = Date(31,12,9999);
  pe.NumDayDoc  = 32000;
  stat = GetLE(pe);
  while (stat)
    if (pe.Referenc != ALG_DEPOSITOR.Referenc)
      stat = FALSE;
    else
      if (pe.Action < 2)
        find_last = TRUE;
        stat = FALSE;
      else
        stat = Prev(pe);
      end;
    end;
  end;

  return find_last;
  
end;

macro UpdatePCEstim
var
  res=true;

  if (GetLastPcEstim())
    pe.SummaProgn    = pe.SummaProgn*Rate;
    pe.SummaCalc     = pe.SummaCalc*Rate;
    pe.SummaPay_Rest = pe.SummaPay_Rest*Rate;
    pe.SummaPay_97   = pe.SummaPay_97*Rate;
    pe.SummaPay_In   = pe.SummaPay_In*Rate;
    pe.SummaPay_Out  = pe.SummaPay_Out*Rate;
    pe.NumSession    = 0;
    res=Update(pe);
  end;
  return res;
end;
macro UpdateAuxInfo
var
  stat,res=true;
  ai.Reference=ALG_DEPOSITOR.Referenc;
  ai.InfoType=1;
  ai.Date=Date(31,12,2099);
  stat=GetLE(ai);
  while(stat and res and (ai.Reference==ALG_DEPOSITOR.Referenc) and (ai.InfoType==1))
    SetRecordAddr(ai_prc,ai,0,0,true);
    ai_prc.GlobPerc=ai_prc.GlobPerc*Rate;
    ai_prc.AltPerc=ai_prc.AltPerc*Rate;
    ai_prc.GlobPerc_Copy=ai_prc.GlobPerc_Copy*Rate;
    ai_prc.AltPerc_Copy=ai_prc.AltPerc_Copy*Rate;
    res=Update(ai);
    stat=Prev(ai);
  end;
  return res;
end;
macro DeleteDocuments
var
  Storn,stat;
  if(ALG_PARAM.PrmC)
    Storn=true;
  else
    Storn=false;
  end;

    stat=DeleteDocument(ALG_SBDEPDOC.iApplicationKind,ALG_SBDEPDOC.ApplicationKey,Storn);

  return stat;
end;
macro DeleteDepHistory
  dh.BranchName=ddep.rec.Name;
  dh.Account=ALG_DEPOSITOR.Account;
  if(GetEQ(dh))
    ALG_DEPOSITOR.Account=dh.PrevAccount;
    ALG_DEPOSITOR.Number=dh.PrevNumber;
    ALG_DEPOSITOR.Code_Currency=Old_Code_Currency;
    ALG_DEPOSITOR.PrevAccount="";
      return Delete(dh);
  end;
  return false;
end;
macro UpdateAccReg
var
  res=true;
  ar.IsCur=ALG_DEPOSITOR.IsCur;
  ar.FNCash=ALG_DEPOSITOR.FNCash;
  ar.GroupNumb=ALG_DEPOSITOR.GroupNumb;
  ar.Code_Currency=ALG_DEPOSITOR.Code_Currency;
  ar.Number=ALG_DEPOSITOR.Number;
  if(GetEQ(ar))
    ar.Remark="";
    res=Update(ar);
  end;
  return res;
end;
macro RunPcCalc
var
  stat;
  ClearRecord(tmpDoc);
  tmpDoc.Referenc=ALG_DEPOSITOR.Referenc;
  tmpDoc.IsCur=ALG_DEPOSITOR.IsCur;
  tmpDoc.FNCash=ALG_DEPOSITOR.FNCash;
  tmpDoc.Account=ALG_DEPOSITOR.Account;
  tmpDoc.Oper={oper};
  tmpDoc.Type_Account=ALG_DEPOSITOR.Type_Account;
  tmpDoc.Code_Currency=ALG_DEPOSITOR.Code_Currency;
  tmpDoc.CodClient=ALG_DEPOSITOR.CodClient;
  tmpDoc.YesSbook="X"; /*?*/
  tmpDoc.Date_Document={curdate};
  tmpDoc.DepDate_Document=DateX;
  tmpDoc.TypeComplexOper=Расчет_Процентов;
  ClearRecord(ALG_OPERPRM);
  ALG_OPERPRM.Account=ALG_DEPOSITOR.Account;
  ALG_OPERPRM.Type_Account=ALG_DEPOSITOR.Type_Account;
  ALG_OPERPRM.Code_Currency=ALG_DEPOSITOR.Code_Currency;
  ALG_OPERPRM.Type_Oper=Расчет_Процентов;
  ALG_OPERPRM.TypeComplexOper=Расчет_Процентов;
  ALG_OPERPRM.Show_Panel=0;
  ALG_OPERPRM.UseMaxDate=1;
  ALG_OPERPRM.NoPrint=1;
  stat=Выполнение_Операции(ALG_OPERPRM,tmpDoc);
  return not stat;
end;
macro TrnProc
var
  OldFlag0Rest;
var
  OldLimit=ALG_DEPOSITOR.Limit;
  TrnStatus=FindDepType;
  if(TrnStatus)
    ALG_DEPOSITOR.Limit=0;
    TrnStatus=UpdateAlgRecords(UPDATE_DEP,true);
  end;
  if(TrnStatus)
    FindOldCodeCurrency;
    if(not Old_Code_Currency)
      TrnStatus=false;
    end;
  end;
  if(TrnStatus)
    TrnStatus=GetRate;
  end;
  if(TrnStatus)
    DateX=GetDateX;
    if(DateX==NullDate)
      TrnStatus=false;
    end;
  end;
  if(TrnStatus)
    OldFlag0Rest=dt.MayBeZeroRest;
    dt.MayBeZeroRest="X";
    TrnStatus=Update(dt);
  end;
  if(TrnStatus)
    TrnStatus=UpdatePCDrest;
  end;
  if(TrnStatus)
    TrnStatus=UpdatePCCalc;
  end;
  if(TrnStatus)
    TrnStatus=UpdatePCEstim;
  end;
  if(TrnStatus)
    TrnStatus=UpdateAuxInfo;
  end;
  if(TrnStatus)
    TrnStatus=DeleteDocuments;
  end;
  if(TrnStatus)
    TrnStatus=DeleteDepHistory;
  end;
  if(TrnStatus)
      TrnStatus=UpdateAccReg;
    end;
  if(TrnStatus)
    ALG_DEPOSITOR.Sum_Rest=ALG_DEPOSITOR.Sum_Rest*Rate;
    ALG_DEPOSITOR.Limit=OldLimit;
    ALG_DEPOSITOR.NumSession=0;
    TrnStatus=UpdateAlgRecords(UPDATE_DEP,true);
  end;
  if(TrnStatus)
    OldFlag0Rest=dt.MayBeZeroRest;
    dt.MayBeZeroRest="X";
    TrnStatus=Update(dt);
  end;
  if(TrnStatus)
    TrnStatus=RunPcCalc;
  end;
  if(not TrnStatus)
    AbortTrn;
  end;
end;
/* entry point */
  SetOutput("NUL");
  if(findDepartment(ALG_DEPOSITOR.FNCash, null, ddep))
    TrnStatus=true;
  end;
  ProcessTrn(1,"TrnProc",dr,dd,dt,ai,pc,pd,pe,dh,ar);
  if(TrnStatus)
    ALG_RESULT=OK_RES;
  else
    ALG_RESULT=ERR_RES;
  end;
end;
