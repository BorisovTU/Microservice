/*
  RS-=Retail
  Макрос Вологодского Сбербанка

  Вызывается из pristav.mac для отображения
  счетов для заданных клиентов по всем филиалам
  (на единой базе)

  Петров В.В. Вологодское ГОСБ 8638
*/
import CommonInter, Acc_Form;
/******************************************************************************/
/* if, как функция                                                            */
/******************************************************************************/
macro iif(lIf,IfTrue,IfFalse)
  if (lIf)
    return(IfTrue);
  else
    return(IfFalse);
  end;
end;
/******************************************************************************/
/* Преобразует строку в дату                                                  */
/******************************************************************************/
macro CToD( InDate )
  var day, month, year;
  day = month = year = 0;

  day   = int(substr( InDate, 1, 2 ));
  month = int(substr( InDate, 4, 2 ));
  year  = int(substr( InDate, 7 ));

/*if( year and month and day )
    if( (year >= 0) and (year <= 99) )
       year = 1900 + year;
    else
       year = 2000 + year;
    end;
  elif( year )
     year = 1900 + year;
  end;*/
  if (day>31) day=0; end;
  if (month>12) month=0; end;

  return (date( Iif(day  ==0,Null,day)  ,
                Iif(month==0,Null,month),
                Iif(year ==0,Null,year) ));
end;

/******************************************************************************/
/* Ищет и заменяет символы в строке                                           */
/* Устаревшая функция. Рекомендуется использовать стандартную StrSubst        */
/******************************************************************************/
macro StrTran(cString,cSearch,cReplace)
 var nIndex;
 While ((nIndex=Index(cString,cSearch))>0)
  cString=SubStr(cString,1,nIndex-1)+cReplace+SubStr(cString,nIndex+StrLen(cSearch));
 end;
 Return(cString);
end; /*macro*/

/*************************************************************/
/*Разбивает символьную строку на поля и добавляет их в массив*/
/*************************************************************/
macro StrToArray(cStr,aFld,cDelim);
 var n;
 if (cDelim == Null) cDelim =" "; end;
 cStr=trim(cStr);
 n = Index(cStr,cDelim);
 while (n  > 0)
  aFld(aSize(aFld)) = SubStr(cStr,1,n-1);
  cStr = SubStr(cStr,n+1);
  n = Index(cStr,cDelim);
 end; /*while*/
 aFld(aSize(aFld)) = cStr;
end; /*macro*/

/*************************************************************/
/*Ищет позицию последнего вхождения подстроки в строку       */
/*************************************************************/
macro RIndex(cStr,cFind);
 var nPos,nLenFind;
 nPos = StrLen(cStr);
 nLenFind= StrLen(cFind);
 while (nPos > 0)
  If (SubStr(cStr,nPos,nLenFind)==cFind)
   Return nPos;
  end;
  nPos=nPos-1;
 end; /*while*/
 Return 0;
end; /*macro*/

/*************************************************************/
/*Изменяет имя файла                                         */
/*************************************************************/
macro FRename(cOld,cNew);
 cNew=SubStr(cNew,RIndex(cNew,"\\")+1); /*Отбрасываем полный путь*/
 Return Run( "CMD.EXE"," /c ren " + cOld+" "+cNew )
end; /*macro*/

/**************************/
MACRO PADL(x,nDln,cSim) /**/
/**************************/

 var cNew;
 var nLen;
 var nDop; /* сколько символов надо добавить */

 if ( cSim == Null )
  cSim =" ";
 end;

 if (VALTYPE(x) == V_INTEGER)
  x = string(x);
 elif (VALTYPE(x) != V_STRING)
  msgbox("Некорректный тип параметра PADL()")
 end;

 nLen = StrLen(x);
 cNew = x;

 if ( nDln > nLen )
  nDop = nDln - nLen;
  cNew = MkStr(cSim,nDop) + x;
 end;

 return(cNew);

END; /* MACRO */

/**************************/
MACRO PADR(x,nDln,cSim) /**/
/**************************/

 var cNew;
 var nLen;
 var nDop; /* сколько символов надо добавить */

 if ( cSim == Null )
  cSim =" ";
 end;

 if (VALTYPE(x)==V_INTEGER)
  x = string(x);
 elif (VALTYPE(x) != V_STRING)
  msgbox("Некорректный тип параметра PADR()")
 end;

 nLen = StrLen(x);
 cNew = x;

 if ( nDln > nLen )
  nDop = nDln - nLen;
  cNew = x + MkStr(cSim,nDop);
 end;

 return(cNew);

END; /* MACRO */

/****************************************************************************/
MACRO Kluch(cSch,cBik) /* cBik - необязательный параметр define = 041909644 */
/****************************************************************************/

 var cMas = "71371371371371371371";
 var cUsl;
 var cTxt;
 var cSim;
 var nSim;
 var cNew = cSch;
 var i;
 var cKlu;
 var nKlu;
 var nDov;

 if (cBik == Null )
  cBik = "041909644";
 end;

 if (StrLen(cBik) != 9)
  cTxt = "Длина БИК - ";
  cTxt = string(cTxt,StrLen(cBik));
  cTxt = cTxt + " знаков";
  msgbox(cTxt);
  return False;
 end;

 if (StrLen(cSch) != 20)
  cTxt = "Длина счета - ";
  cTxt = string(cTxt,StrLen(cSch));
  cTxt = cTxt + " знаков";
  msgbox(cTxt);
  return False;
 end;

 /* Вставляем 0 в 9-ю позицию */
 cNew = substr(cSch,1,8);
 cNew = cNew + "0";
 cNew = cNew + substr(cSch,10);

 nKlu = 0;
 i = 1;
 while (i<21)
  cSim = substr(cNew,i,1);
  if ( Index("0123456789",cSim) > 0 )
   /* если в i-й позиции цифра */
   nSim = int(cSim);
  else
   if (i==6)
    /* в -й позиции допускается не цифра */
    if (Index("AaАа",cSim)>0)
     nSim = 0;
    elif (Index("BbВв",cSim)>0)
     nSim = 1;
    elif (Index("CcСс",cSim)>0)
     nSim = 2;
    elif (Index("EeЕе",cSim)>0)
     nSim = 3;
    elif (Index("HhНн",cSim)>0)
     nSim = 4;
    elif (Index("KkКк",cSim)>0)
     nSim = 5;
    elif (Index("MmМм",cSim)>0)
     nSim = 6;
    elif (Index("PpРр",cSim)>0)
     nSim = 7;
    elif (Index("TtТт",cSim)>0)
     nSim = 8;
    elif (Index("XxХх",cSim)>0)
     nSim = 9;
    else
     msgbox("Недопустимый символ в счете " + cSch);
     return False;
    end;
   else
    msgbox("Недопустимый символ в счете " + cSch);
    return False;
   end;
  end;
  nKlu = nKlu + nSim * int(substr(cMas,i,1));
  i = i + 1;
 end; /* while */

 cUsl = substr(cBik,7,3);
 if (int(cUsl)<3)
  cUsl = "0"+substr(cBik,5,2);
 end;

 nDov = 0;
 i = 1;
 while (i<4) /* for i=1 to 3 */
  nSim = int(substr(cUsl,i,1));
  nDov = nDov + nSim * int(substr(cMas,i,1));
  i = i + 1;
 end; /* while */

 /* Делим nDov по модулю 10 */
 cTxt = string(nDov);
 nDov = int(substr(cTxt,StrLen(cTxt),1));
 nKlu = nKlu + nDov;
 nKlu = nKlu * 3;

 /* Делим nKlu по модулю 10 */
 cTxt = string(nKlu);
 cKlu = substr(cTxt,StrLen(cTxt),1);

 /* вставляем в -ю позицию */
 cNew = substr(cSch,1,8);
 cNew = cNew + cKlu;
 cNew = cNew + substr(cSch,10);

 return cNew;

END; /* MACRO */

/******************************************************************************/
/* Определяет любой параметр филиала по внутреннему номеру                    */
/******************************************************************************/
macro GetNumBranch(nCodeDep,cMode)
 /*
 cMode может принимать следующие значения:
 cMode == "Короткий номер" (по умолчанию)  001
 cMode == "Отделение/Филиал"               8638/001
 cMode == "Наименование"                   Филиал №8638/001
 cMode == "Адрес"                          ул.Ленина д.1
 cMode == "Заведующая"                     Кожанова Т.Б.
 cMode == "Принтеры"                       TM950
 */

 file listfdep (listfdep) key 0; /*Справочник филиалов*/
 var ddep = TRecHandler( "i_dp_dep.rec" );
 var party = TRecHandler( "i_party.rec" );
 var nIndex,cBranch="";

 rewind(listfdep);
 listfdep.FNCash = nCodeDep;
 if ( GetEQ(listfdep) and findDepartment(nCodeDep, null, ddep, party))
  if   (cMode == "Отделение/Филиал")       cBranch=ddep.rec.Name;
  elif (cMode == "Наименование")           cBranch=party.rec.Name;
  elif (cMode == "Адрес")                  cBranch=party.rec.address;
  elif (cMode == "Заведующая")             cBranch=listfdep.BranchChief;
  elif (cMode == "Принтеры")               cBranch=listfdep.UserField1;
  else /*По умолчанию возвращает как для (cMode=="Короткий номер")*/
   cBranch=ddep.rec.Name;
   nIndex=Index(cBranch,"/");
   if (nIndex>0)
    cBranch=PadL(trim(SubStr(cBranch,nIndex+1)),3,"0");
   end;
  end;
 else
  msgbox("Филиал " + string(nCodeDep) + " не найден в справочнике филиалов!");
 end;
 return(cBranch);
end; /*macro*/

/******************************************************************************/
/* Заменяет FFF или FF на номер филиала                                              */
/******************************************************************************/
macro ReplaceFFF(cAccount,nFNCash)
 var cFFF="000";
 cFFF = GetNumBranch(nFNCash,"Отделение/Филиал");
 cFFF = SubStr(cFFF,RIndex(cFFF,"/")+1,3);
 cAccount=StrTran(cAccount,"FFF",cFFF);
 if (StrLen(cAccount)==20)
  cAccount=Kluch(cAccount);
 end;
 Return(cAccount);
end;
/******************************************************************************/
/* Определяет любой параметр валюты по внутреннему номеру                     */
/******************************************************************************/
macro GetNumCurrency(nCodeCur,cMode)
 /*
 cMode может принимать следующие значения:
 cMode == "Внешний код валюты" (по умолчанию)  ExternalCode  810
 cMode == "Название валюты"                    Name_Currency Российские рубли
 cMode == "Краткое название валюты"            Short_Name    RUR
 */

 file currency (currency) key 0; /*Справочник филиалов*/
 var cCur="";
 rewind(currency);
 currency.Code_Currency = nCodeCur;
 if ( GetEQ(currency) )
  if   (cMode == "Внешний код валюты")       cCur=currency.ExternalCode;
  elif (cMode == "Название валюты")          cCur=currency.Name_Currency;
  elif (cMode == "Краткое название валюты")  cCur=currency.Short_Name;
  else /*По умолчанию возвращает как для (cMode=="Внешний код валюты")*/
   cCur=currency.ExternalCode;
  end;
 else
  msgbox("Валюта " + string(nCodeCur) + " не найдена в справочнике валют!");
 end;
 return(cCur);
end; /*macro*/
/******************************************************************************/
/* Определяет любой параметр вида вклада по краткому наименованию             */
/******************************************************************************/
macro GetNumDTyp(cDTyp,cMode)
 /*
 cMode может принимать следующие значения:
 cMode == "Уникальный номер" (по умолчанию)  autoKey      5
 cMode == "Название вида вклада"             Name         ВКЛАД ДО ВОСТРЕБОВАНИЯ
 cMode == "Приоритет"                        Priorit      1
 cMode == "Балансовый"                       BalAcc       42301
 cMode == "Балансовый нерезидентов"          BalAccNotRez 42601
 cMode == "Группа нумерации"                 NewGroupNumb 1
 cMode == "Код вклада СБРФ"                  NewIdentChar 01
 */

 file sb_dtyp (sb_dtyp) key 2; /*Справочник видов вкладов*/
 var cRetDTyp="";
 rewind(sb_dtyp);
 sb_dtyp.FlagCur=NumFlagCur();
 sb_dtyp.Kind = trim(cDTyp);
 if ( GetEQ(sb_dtyp) )
  if   (cMode == "Уникальный номер"       )  cRetDTyp=sb_dtyp.autoKey     ;
  elif (cMode == "Название вида вклада"   )  cRetDTyp=sb_dtyp.Name        ;
  elif (cMode == "Приоритет"              )  cRetDTyp=sb_dtyp.Priorit     ;
  elif (cMode == "Балансовый"             )  cRetDTyp=sb_dtyp.BalAcc      ;
  elif (cMode == "Балансовый нерезидентов")  cRetDTyp=sb_dtyp.BalAccNotRez;
  elif (cMode == "Группа нумерации"       )  cRetDTyp=sb_dtyp.NewGroupNumb;
  elif (cMode == "Код вклада СБРФ"        )  cRetDTyp=sb_dtyp.NewIdentChar;
  else /*По умолчанию возвращает как для (cMode=="Уникальный номер")*/
   cRetDTyp=sb_dtyp.autoKey;
  end;
 else
  msgbox("Вид вклада " + string(cDTyp) + " не найден в справочнике!");
 end;
 return(cRetDTyp);
end; /*macro*/

/*****************************************/
/*Определяет курс ЦБ, покупки, продажи   */
/*Работает под многими версиями RS-Retail*/
/*****************************************/
Macro GetRate(dRate,nCode,cMode,ChangNom)
 /*
 cMode может принимать следующие значения:
 cMode == "Курс ЦБ"
 cMode == "Курс продажи"
 cMode == "Курс покупки"
 */
 file ratechng ("ratechng.dbt") key 0; /*Смены курсов*/
 file cur_rate ("cur_rate.dbt") key 0; /*Курсы валют по сменам*/
 file currency ("currency.dbt") key 1; /*Справочник валют*/
 var nRet=0;
 var nVersion;
 var cVersion;
ChangNom=0;

 cVersion = GetRetailVersion();
 nVersion = RIndex(cVersion,".") + 1;
 nVersion = Int(SubStr(cVersion,nVersion));
 if (nVersion != 179)
  currency.ExternalCode = nCode;
  if (GetEQ(currency))
   nCode = currency.Code_Currency;
  else
   [Не найден внешний код валюты: ####](nCode);
   Return 0;
  end;
 end;
 Rewind(ratechng);
 While (Prev(ratechng))
  if ((ratechng.StartDate<=dRate) and ((ratechng.StopDate>=dRate)or(ratechng.StopDate==Date(0,0,0))))
   Rewind(cur_rate);
   cur_rate.curr_ref=nCode;
   cur_rate.Change_Ref=ratechng.ID;
   cur_rate.RateFlag  ="X";
   if (GetEQ(cur_rate))
    if   (cMode == "Курс ЦБ")      nRet=cur_rate.CBRate;
    elif (cMode == "Курс продажи") nRet=cur_rate.BuyRate;
    elif (cMode == "Курс покупки") nRet=cur_rate.SellRate;
    end;
    ChangNom=Cur_rate.ChangeNom;
    setparm(3, ChangNom);
    Return(nRet);
   end;
  end;
 end;
 Return(nRet);
end; /*macro*/

/**************************************/
/*Выбор филиала                       */
/**************************************/
macro GetBranch;

 file listfdep (listfdep) key 0; /*Справочник филиалов*/
 var dpDepList = TdpDepList;
 var party = TRecHandler( "i_party.rec" );
 var address;
 array AFNCash;
 array MenuName;
 var i = 0;

 AFNCash(0) = 0;
 MenuName(0) = " По всем";

 dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
 while (dpDepList.next())
   i = i + 1;
   AFNCash(i) = dpDepList.rec.Code;
   if (findDepartment(dpDepList.rec.Code, null, null, party))
     address = party.rec.address;
   else
     address = "";
   end;
   MenuName(i) = " " + PadR(trim(dpDepList.rec.Name),12) + " | " + address + " ";
 end;
 i = Menu(MenuName,NULL,"Выберите филиал");
 if (i >= 0)
  return AFNCash(i);
 else
  return -1;
 end;
end;

/**************************************************/
/*Падежная пропись даты                           */
/**************************************************/
macro DateToStr(dDat)
var dd,mm,yy;
array
  aMonth;

  aMonth(0) ="Января";
  aMonth(1) ="Февраля";
  aMonth(2) ="Марта";
  aMonth(3) ="Апреля";
  aMonth(4) ="Мая";
  aMonth(5) ="Июня";
  aMonth(6) ="Июля";
  aMonth(7) ="Августа";
  aMonth(8) ="Сентября";
  aMonth(9) ="Октября";
  aMonth(10)="Ноября";
  aMonth(11)="Декабря";

  datesplit(dDat,dd,mm,yy);
  return(PadL(string(dd),2,"0")+" "+aMonth(mm-1)+" "+string(yy)+" года ");
end;

/**************************************************/
/*Заполняет массив заданным значением             */
/*AFILL(<принимающий массив>, <выражение>,        */
/*   [<начальный элемент>], [<кол-во элементов>]) */
/*   --> принимающий массив                       */
/**************************************************/
Macro AFill(aArr,uVal,nBegin,nCount)
 var ii=0;
 if (nBegin==Null) nBegin=0; end;
 if (nCount==Null) nCount=aSize(aArr); end;
 while (ii<nCount)
  aArr(nBegin+ii)=uVal;
  ii=ii+1
 end;
 Return aArr;
end;

/**************************************/
/*Выбор операции по вкладу            */
/**************************************/
macro SelectTypeOper(cDepType);

 file sb_typop("sb_typop.dbt") key 0;

 array aNum;
 array aNam;
 var i = 0, stat;

 sb_typop.IsCur    = NumFlagCur();
 sb_typop.Kind     = cDepType;

 stat = GetGE(sb_typop);
 while (stat)
   aNum(i) = sb_typop.NumOpert;
   aNam(i) = PadL(trim(string(sb_typop.NumOpert)),4)+"|"+sb_typop.czNameAlg;
   stat=(
        (next(sb_typop)) and
        (sb_typop.IsCur==NumFlagCur()) and
        (sb_typop.Kind ==cDepType    )
        );
   i = i + 1;
 end;
 i = Menu(aNam,NULL,"Выберите вид операции");
 if (i >= 0)
  return aNum(i);
 else
  return -1;
 end;
end;

/**************************************/
/*Возвращает последний день месяца    */
/**************************************/
macro EOMonth(dD);
 var d,m,y;
 DateSplit(dD,d,m,y);
 m=m+1;
 if (m==13)
  m=1;
  y=y+1;
 end;
 d=1;
 dD=Date(d,m,y)-1;
 Return dD;
end;

/*************************************************************
                 Определение остатка на счете

Определяет исходящий остаток по счету с идентификатором ReferencAccount
на дату dDate.
*************************************************************/
macro RestAccount(ReferencAccount,dDate)

 var RestOnAccount = $0.0L;
 var stat;

 const D_AR            =  8;  /* Проводка в архиве      */
 const D_ARC           =  9;  /* Корректировка в архиве */
 const D_AUDIT_STORN   = 14;  /* Сторнирование операции */
 const D_AUDIT_DELETE  = 15;  /* Удаление операции      */
 const D_AUDIT_CORRECT = 16;  /* Корректировка операции */
 const D_DELETE        =  2;  /* Запись удалена         */

 file depositr ( depositr ) key 8;       /* Файл счетов вкладчиков       */
 file sbdepdoc ( sbdepdoc ) key 6;       /* Документы по вкладам СБ РФ   */

 depositr.Referenc=ReferencAccount;
 if (GetEQ(depositr))
   if ((dDate == Date(0,0,0)) /*or (depositr.final_date <= dDate)*/)
     RestOnAccount = depositr.Sum_Rest;
   else
     sbdepdoc.Referenc      = depositr.Referenc;
     sbdepdoc.Date_Document = dDate + 1;
     sbdepdoc.NumDayDoc     = 0;

     stat = GetLT(sbdepdoc);
     while (stat)
       if (sbdepdoc.Referenc == depositr.Referenc)
         if ((sbdepdoc.KindOp == D_AUDIT_STORN  ) OR
             (sbdepdoc.KindOp == D_AUDIT_DELETE ) OR
             (sbdepdoc.KindOp == D_AUDIT_CORRECT) OR
             (sbdepdoc.KindOp == D_AR           ) OR
             (sbdepdoc.KindOp == D_ARC          ) OR
             (sbdepdoc.Action == D_DELETE       ) or
             ( ( sbdepdoc.IsSuspended != "" ) and ( sbdepdoc.IsSuspended != "X" ) ) )
           stat = Prev(sbdepdoc);
         else
           RestOnAccount = sbdepdoc.Rest;
           stat          = FALSE;
         end;
       else
         stat = FALSE;
       end;
     end;
   end;
 end;
 return RestOnAccount;

end;

/*************************************************************
Определение Referenc для Account
************************************************************/
Macro GetReferencAcc(cAcc);
 file depositr (depositr) key 7;
 var nRef=0;
 depositr.FNCash =NumFNCash();
 depositr.Account=Trim(cAcc);
 if (GetEQ(depositr))
  nRef=depositr.Referenc;
 end;
 Return nRef;
end;

/*************************************************************
Определение группы нумерации GroupNumber для счета Account
************************************************************/
Macro GetGroupNumb(cAcc);
 var nGN;
 nGN=Int(SubStr(Trim(cAcc),5,1));
 nGN=iif(nGN==2,8,iif(nGN>2,nGN-1,nGN));
 Return Trim(String(nGN));
end;

/***************************************************
 Определение ФИО контролера
***************************************************/
Macro Name_Oper(Name);
 var nName;
 var FName; 
 file prs (person, "bank.def");
 prs.Oper = Name;
 if (GetEQ(prs))
   nName = SubStr(prs.Name, 1, Index(prs.Name, " ") - 1);
   fName = Trim (Substr (prs.Name, Index(prs.Name, " ")));
   nName = nName + " " + strupr(Substr (fName, 1, 1)) + ".";
   fName = Trim (Substr (prs.Name, Index(prs.Name, " ")));
   fName = Trim (Substr (fName, Index(fName," ")));
   nName = nName + strupr(Substr(fName, 1, 1)) + ".";
  else
   nName = "";
 end;
 Return Trim(nName);
end;

/***************************************************
 Вставляет "/" в номер счета
***************************************************/
Macro Slash(account)
 account=trim(account);
 Return SubStr(account,1,20)+"/"+SubStr(account,21,2);
end;

/******************************************************************
 Поиск главного счета по карточке
******************************************************************/
macro MainAccForCard(nCardRef,cMode)
 /*
 cMode может принимать следующие значения:
 1. "Referenc"
 2. "Account"
 */

 file sclink   ("scLink.dbt"  , "spcard.def") key 5; 
 file depositr ("depositr.dbt") key 8;

 var  stat,uRet;

 uRet = iif(cMode=="Referenc",0,"");

 scLink.FNCash  = NumFNCash();
 scLink.cardRef = nCardRef;
 scLink.mainAcc = "X";
 stat = GetEQ(scLink);
 if (NOT stat)
  ReWind(scLink);
  scLink.FNCash  = NumFNCash();
  scLink.cardRef = nCardRef;   
  scLink.mainAcc = "";
  stat = GetEQ(scLink);
 end;

 if (stat)
  if (cMode=="Referenc")
   uRet = scLink.cardAccRef;
  else
   depositr.Referenc = scLink.cardAccRef;
   if (GetEQ(depositr))
    uRet = depositr.Account;
   end;
  end;
 end;

 Return uRet;

end;

/******************************************************************
 Поиск главной карты по счету
******************************************************************/
macro MainCardForAcc(nReferenc)
 file sclink  ("scLink.dbt" , "spcard.def") key 6;
 file sccard  ("scCard.dbt" , "SPCARD.DEF") key 0;

 var  stat,cRet;

 cRet = "";

 scLink.cardAccRef = nReferenc;
 scLink.mainCard   = "X";
 stat = GetEQ(scLink);
 if (NOT stat)
  ReWind(scLink);
  scLink.cardRef  = nReferenc;   
  scLink.mainCard = "";
  stat = GetEQ(scLink);
 end;

 if (stat)
  scCard.FNCash  = NumFNCash();
  scCard.cardRef = scLink.cardRef;
  if (GetEQ(scCard))
   cRet = scCard.cardNumber;
  end;
 end;

 Return cRet;

end;

/******************************************************************
 Раскрывает значения бухгалтерского счета филиала по спецпеременной
******************************************************************/
Macro ДайБухгалтерскийСчетФилиала(nCodCur,cSpecVariable)
 file sb_acdep (sb_acdep) key 0;
 var cRet = cSpecVariable;
 sb_acdep.IsCur         = NumFlagCur();
 sb_acdep.FNCash        = NumFNCash();
 sb_acdep.CodCur        = nCodCur;
 sb_acdep.SpecVariable  = cSpecVariable;
 if (GetEQ(sb_acdep))
  cRet = sb_acdep.account;
 end;
 Return cRet;
end;

/******************************************************************
 Возвращает record pay_cacn с реальными счетами для контировки 
 кассовых операций
******************************************************************/
Macro ДайЗаписьОКонтировкеКассовых(REC_PAY_CACN)
 var lRet=False;
 Return lRet;
end;

/******************************************************************
 Возвращает record nbalac с реальными счетами для контировки 
 внебалансовых операций
******************************************************************/

Macro ДайЗаписьОКонтировкеВнебалансовых(REC_NBALAC)
 var lRet=False;
 Return lRet;
end;

/******************************************************************
 Возвращает код ценности по коду ресурса кассы
******************************************************************/
Macro ДайКодЦенногоБланка(nRefValue)
 file sb_casvl (sb_casvl) key 0;
 var nRet = 0;
 sb_casvl.RefValue = nRefValue;
 if (GetEQ(sb_casvl))
  nRet = sb_casvl.CodIntValue;
 end;
 Return nRet;
end;

/******************************************************************
 Возвращает True, если тип принтеров в филиале - TM950
******************************************************************/
Macro IsTM()
 Return (GetNumBranch(NumFNCash,"Принтеры")=="TM950")
end;

/******************************************************************************/
/* Вывод строк массива на печать (используется в следующей функции)           */
/******************************************************************************/
macro OutArr(aLines)
 var ii=0;
 while (ii<aSize(aLines))
  if (aLines(ii)!="")
   [#](aLines(ii));
   aLines(ii) = "";
  end;
  ii=ii+1;
 end;
end;
/******************************************************************************/
/* Подготовка файла для печати в несколько рядов                              */
/******************************************************************************/
macro InRows(cIn,              /* Входящий файл */
             nOrdersInString,  /* Ордеров в одной строке */
             cDelim            /* Символ-разделитель */
            )
 file fin () txt;
 var nLines = 0;
 var nOrders = 0;
 array aLines;

 if (open(fin,cIn))
  while (next(fin))
   if (Index(fin.str,cDelim) > 0) /* Найден символ-разделитель */
    nOrders = nOrders + 1;
    nLines = 0;
    if (nOrders>=nOrdersInString) /*Готовы строчки*/
     OutArr(aLines);
     nOrders=0;
    end;
   else
    if (aLines(nLines) == Null)
     aLines(nLines) = "";
    end;
    aLines(nLines) = PadR(aLines(nLines),nOrders * StrLen(fin.str)," ") + fin.str;
    nLines = nLines + 1;
   end;
  end;
  if (nOrders > 0) /* Остались ненапечатанные */
   OutArr(aLines);
  end;
 else
  [Ошибка открытия #](cIn);
 end;
end;

/******************************************************************************/
/* Подготовка файла для постраничной печати с колонтитулами и номерами страниц*/
/******************************************************************************/
macro InPages(cIn,              /* Входящий файл */
              nLinesInString,   /* Строк в одной странице */
              nLeft,            /* Смещение слева */
              cTopTitle,        /* Верхний колонтитул */
              cBotTitle,        /* Нижний  колонтитул */
              nLeftForNumber,   /* Смещение слева для номера страницы */
              lNumBot           /* Номер страницы указывается вверху */
             )
 file fin () txt;
 var nLines = 0;
 var nPage = 1;
 var cRet = StrFor(13)+StrFor(10);
 array aLines;

 if (open(fin,cIn))
  GetInt(nLinesInString,"Количество строк на странице : ");
  GetInt(nLeft         ,"Смещение от левого края: ");
  cTopTitle = StrSubst(cTopTitle,cRet,cRet+PadR("",nLeft)); /*Обеспечиваем многострочные */
  cBotTitle = StrSubst(cBotTitle,cRet,cRet+PadR("",nLeft)); /*колонтитулы смещением слева*/
  if (ValType(nLeftForNumber)!=V_INTEGER)
   nLeftForNumber = 40; /*По умолчанию примерно по центру*/
  end;
  if (ValType(lNumBot)!=V_BOOL)
   lNumBot = True;      /*По умолчанию нумерация страниц сверху*/
  end;
  while (next(fin))
   if (nLines == nLinesInString)
    nLines = 0;
    nPage = nPage + 1;
    if (ValType(cBotTitle) == V_STRING)
     [ ];
     [#](PadR("",nLeft)+cBotTitle);
    end;
    if (lNumBot)
     [];
    end;
    [#](PadR("",nLeftForNumber)+"- "+Trim(String(nPage))+" -");
    if (not lNumBot)
     [];
    end;
    if (ValType(cTopTitle) == V_STRING)
     [#](PadR("",nLeft)+cTopTitle);
    end;
   end;
   [#](PadR("",nLeft)+fin.str);
   nLines = nLines + 1;
  end;
  /*Печать нижнего колонтитула на последней странице*/
  if ((ValType(cBotTitle) == V_STRING)and(nLines!=nLinesInString))
   while (nLines<nLinesInString)
    [ ];
    nLines = nLines + 1;
   end;
   [ ];
   [#](PadR("",nLeft)+cBotTitle);
   [];
  end;
 else
  [Ошибка открытия #](cIn);
 end;
end;


/******************************************************************************/
/* Печать мемориального ордера формы № 203                                    */
/* R_DOC - запись структуры document.dbt из словаря bank.def RS-Bank          */
/* cGroundDop - используется для более длинного наименования платежа          */
/******************************************************************************/
Macro F203(R_DOC,cGroundDop)
 var cSumProp;
 var nPrivSum;
 var nVVV;
 var cUSD;
 if (ValType(cGroundDop) == V_UNDEF)
  cGroundDop = "";
 end;
 nVVV = Int(GetNumCurrency(R_DOC.Code_Currency,"Внешний код валюты"));
 if (R_DOC.Code_Currency > 0 )
  cUSD = GetNumCurrency(R_DOC.Code_Currency,"Краткое название валюты");
  nPrivSum = Money(R_DOC.Sum * GetRate(R_DOC.Date_Document,nVVV,"Курс ЦБ"));
  cSumProp = CurToStrAlt(nPrivSum,Null,Null,810);
 else
  cUSD = "";
  nPrivSum = 0;
  cSumProp = CurToStrAlt(R_DOC.Sum,Null,Null,nVVV);
 end;
/*
[
 
 -------------------------------------------------------------------------------------------
                                                                                       Ф.203
 МЕМОРИАЛЬНЫЙ ОРДЕР N     ##############
          #######################
 Сумма  прописью
          ##################################################################################
 ----------------------------------------------|----------|---------------------------------
 ИНН ####################                      |Сумма     |             ####################
 ##############################################|          |
 ##############################################|          |
                                               |----------|---------------------------------
                                               |Сч.N      |#########################
 Плательщик                                    |          |
 ----------------------------------------------|----------|
 ##############################################|БИК       |##########
 ##############################################|          |
                                               |          |
                                               |----------|
 Банк плательщика                              |Сч.N      |#########################
 ----------------------------------------------|----------|---------------------------------
 ##############################################|БИК       |##########
 ##############################################|          |
                                               |          |
                                               |----------|
 Банк получателя                               |Сч.N      |#########################
 ----------------------------------------------|----------|
 ИНН ####################                      |Сч.N      |#########################
 ##############################################|          |
 ##############################################|          |
                                               |          |
                                               |          |
 Получатель                                    |          |
                                               |----------|--------|----------|-------------
                                               | Вид.оп   | 1      |Срок плат.|
                                               |----------|--------|----------|-------------
                                               | Наз.пл.  |        |Очер.плат.|
                                               |----------|--------|----------|-------------
                                               | Код      |        |Рез.поле  |
 ----------------------------------------------|----------|--------|----------|-------------
 Назначение платежа(содержание операции) 
 ###########################################################################################
 ###########################################################################################
 --------------------------------------------------------------------------
              Подписи                        Отметки банка
 
          --------------------
 
 
          --------------------
 
]
*/

[
 
 -------------------------------------------------------------------------------------------
 МЕМОРИАЛЬНЫЙ ОРДЕР N     ##############      #######################                  Ф.203
 Сумма  прописью
          ##################################################################################
 ----------------------------------------------|----------|---------------------------------
 ИНН ####################                      |Сумма     |###          ####################
 ##############################################|          |             ####################
 ##############################################|----------|---------------------------------
 Плательщик                                    |Сч.N      |#########################
 ----------------------------------------------|----------|
 ##############################################|БИК       |##########
 ##############################################|----------|
 Банк плательщика                              |Сч.N      |#########################
 ----------------------------------------------|----------|---------------------------------
 ##############################################|БИК       |##########
 ##############################################|----------|
 Банк получателя                               |Сч.N      |#########################
 ----------------------------------------------|----------|
 ИНН ####################                      |Сч.N      |#########################
 ##############################################|----------|--------|----------|-------------
 ##############################################| Вид.оп   | 1      |Срок плат.|             
 Получатель                                    |----------|--------|----------|-------------
                                               | Наз.пл.  |        |Очер.плат.|             
 ----------------------------------------------|----------|--------|----------|-------------
                                               | Код      |        |Рез.поле  |             
 Назначение платежа(содержание операции)       |----------|--------|----------|-------------
 ###########################################################################################
 ###########################################################################################
 -------------------------------------------------------------------------------------------
              Подписи                        Отметки банка

 
]
(
 R_DOC.Numb_Document,
 DateToStr(R_DOC.Date_Document),
 (cSumProp):w:82,
 R_DOC.OKPO_Payer,
 cUSD,
 R_DOC.Sum,
 SubStr(R_DOC.Payer,1,46),
 iif(nPrivSum==0,"",nPrivSum),
 SubStr(R_DOC.Payer,47),
 Acc_Form(R_DOC.Account_Payer),
 SubStr(R_DOC.Bank_Payer,1,46),
 R_DOC.MFO_Payer,
 SubStr(R_DOC.Bank_Payer,47),
 Acc_Form(R_DOC.CorAcc_Payer),
 SubStr(R_DOC.Bank_Receiver,1,46),
 R_DOC.MFO_Receiver,
 SubStr(R_DOC.Bank_Receiver,47),
 Acc_Form(R_DOC.CorAcc_Receiver),
 R_DOC.OKPO_Receiver,
 Acc_Form(R_DOC.Account_Receiver),
 SubStr(R_DOC.Receiver,1,46),
 SubStr(R_DOC.Receiver,47),
 R_DOC.Ground,
 cGroundDop:w:91
);
end;

/****************************************************/
/* Возвращает значение тэга "cTeg" поля "cReserv"   */
/* Формат тэгов: ИмяТэга1:ЗначениеТэга1|...         */
/****************************************************/
Macro GetTegReservField(cTeg,cReserv)
 var nI;
 nI = Index(cReserv,cTeg);
 if (nI > 0)
  cReserv = SubStr(cReserv, nI + StrLen(cTeg));
  nI = Index(cReserv,"|");
  if (nI > 0)
   cReserv = SubStr(cReserv,1,nI-1)
  end;
 end;
 Return cReserv;
end;
