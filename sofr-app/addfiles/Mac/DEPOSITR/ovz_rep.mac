/* Отчеты по ОРВВЗ */

import ci_defs, acc_form;
            
class ( TCIRecGenerator ) TORVVZRecGenerator( _oper, _makeReport, _observer )

  var
    orvvzDocInfo = TRecHandler( "ci_doc.2" ),
    orvvzRecInfo = TRecHandler( "ci_rec.2");

  var
    totNumItems=0,
    totSoldItems=0,
    totBoughtItems=0,
    totSoldDepo=0,
    totBoughtDepo=0,
    totSum=$0,
    totPrice=$0,
    totSumSold=$0,
    totSumBought=$0;
  
  macro f5_outHead;
  
    [
                                                                                 Филиал №
                                                                                 отделения
                                                                                 Сбербанка РФ
  
  
                                 Отчет по операциям по ОРВВЗ за ##########
  
    ]
    (
      {curdate}
    );
  end;
  
  macro f5_outCommonHead;
  
    [+----------+----------------------------------------------+--------+-------------------+];
    [|    №     |        Н а з в а н и е                       | Колич. |     С у м м а     |];
    [+----------+----------------------------------------------+--------+-------------------+];
  end;
  
  macro f5_outSellHead;
  
    [ ];
    [Продажа ОРВВЗ];
    [ ];
    f5_outCommonHead;
  end;
  
  macro getNumItems
  
    var
      recFound,
      numItems=0;
  
    orvvzDocInfo.setRecordAddr(capIssueDoc,0,
      capIssueDoc.fldOffset("Info"),true);

    capIssueDoc.rec.RecApplicKind=capIssueRec.rec.ApplicationKind;
    capIssueDoc.rec.RecApplicKey=capIssueRec.rec.ApplicationKey;
  
    recFound=capIssueDoc.getEQ;
    while(recFound
      and (capIssueDoc.rec.RecApplicKind==capIssueRec.rec.ApplicationKind)
      and (capIssueDoc.rec.RecApplicKey==capIssueRec.rec.ApplicationKey))
      if ( checkCIDoc )
        numItems=numItems+orvvzDocInfo.rec.NumItems;
      end;
      recFound=capIssueDoc.next;
    end;
    return numItems;
  end;
  
  macro f5_outPayWinHead;
  
    [ ];
    [Выплата выигрышей];
    [ ];
    f5_outCommonHead;
  end;
  
  macro f5_outLine;
  
    orvvzRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    [|  #####   | ############################################ | ###### | ################# |]
    (
      capIssueRec.rec.Issue,
      orvvzRecInfo.rec.Name,
      getNumItems,
      capIssueRec.rec.Sum
    );
  end;
  
  macro f5_outTotals;
  
    orvvzRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    [+---------------------------------------------------------+--------+-------------------+];
    [|  И т о г о :                                            | ###### | ################# |]
    (
      totNumItems,
      totSum
    );
  end;
  
  macro f5_sellReport
  
    var
      recFound;
  
    orvvzRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    totNumItems=0;
    totSum=$0;
  
    capIssueRec.clear;
    capIssueRec.rec.Branch=Branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_ORVVZ;
  
    f5_outSellHead;
    recFound=capIssueRec.getEQ;
    while(recFound
      and (capIssueRec.rec.Branch==Branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_ORVVZ))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Brigade==OperBrigade)
        and (capIssueRec.rec.Operation==O_LOTTERY_SELL))
        f5_outLine;
        totNumItems=totNumItems+getNumItems;
        totSum=totSum+capIssueRec.rec.Sum;
      end;
      recFound=capIssueRec.next;
    end;
    f5_outTotals;
  end;
  
  macro f5_payWinReport
  
    var
      recFound;
  
    orvvzRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    totNumItems=0;
    totSum=$0;
  
    capIssueRec.clear;
    capIssueRec.rec.Branch=branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_ORVVZ;
  
    f5_outPayWinHead;
    recFound=capIssueRec.getGE;
    while(recFound
      and (capIssueRec.rec.Branch==branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_ORVVZ))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Brigade==operBrigade)
        and (capIssueRec.rec.Operation==O_LOTTERY_PAYWIN))
        f5_outLine;
        totNumItems=totNumItems+getNumItems;
        totSum=totSum+capIssueRec.rec.Sum;
      end;
      recFound=capIssueRec.next;
    end;
    f5_outTotals;
  end;
  
  macro f5_report;
  
    if ( not makeReport )
      return;
    end;

    f5_outHead;         
    f5_sellReport;
    f5_payWinReport;
  end;
  
  macro outSellRecHead;
  
    var
      branchStrNum = "",
      departNum = "",
      departName = "",
      comment = "";
  
    if ( not makeReport )
      return;
    end;
  
    getBranchParams( branch, branchStrNum, departNum, departName );
    orvvzRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);
  
    findCIMisc( capIssueRec.rec.type, capIssueRec.rec.Issue );
    comment = "номинальной стоимостью " + ciMisc.rec.Price + " рублей";
  
    [  Сбербанк России                                                                                   Ф. № 56
  
       Филиал № ##########
       --------------------
       Наименование филиала
       Сбербанка России
  
          
  
                                               КОНТРОЛЬНАЯ  ВЕДОМОСТЬ
  
                                    на продажу и покупку облигаций Российского
                                          внутреннего выигрышного займа 1992 года
                                ##################################################
                                                   за ##########
          
  
       Смена № #####
  
    ]
    (
      branchStrNum,
      comment:c,
      {curdate},
      operBrigade
    );
    [+--------+----------------------------------------------+-----------------------------+-------------------+];
    [|Номерной|                 П р о д а н о                |        К у п л е н о        |   П о д п и с и   |];
    [| талон, |-----------+-----------------+----------------|-----------+-----------------|---------+---------|];
    [|выданный|Количество |Номинальная стои-|Получено за     |Количество | Выплачено за    |         |         |];
    [|клиенту |-----+-----|мость облигаций  |облигации (руб.)|-----+-----| облигации       |контролер| кассир  |];
    [|        | шт  |д/шт |(руб.)           |                | шт  |д/шт | (руб.)          |         |         |];
    [+--------+-----+-----+-----------------+----------------+-----+-----+-----------------+---------+---------+];
  end;                                                                                                                                                
                                                                                                                                                      
  macro getDepo( numItems, name )                                                                                                                   
                                                                                                                                                      
    return int( double( numItems * ciMisc.rec.Price * 2 ) );                                                                                      
  end;
  
  macro outSellRecLine;
  
    var
      priceRepr=$0,
      boughtPriceRepr=$0,
      soldItems=0,
      boughtItems=0,
      soldDepo=0,
      boughtDepo=0,
      soldPar=$0,
      boughtPar=$0,
      sumSoldRepr=$0,
      sumBoughtRepr=$0;
  
    findCIMisc( capIssueDoc.rec.type, capIssueDoc.rec.Issue );
  
    orvvzDocInfo.setRecordAddr(capIssueDoc,0,
      capIssueDoc.fldOffset("Info"),true);

    if(capIssueDoc.rec.Operation==O_LOTTERY_SELL)
      soldItems = orvvzDocInfo.rec.NumItems;
      totSoldItems = totSoldItems + soldItems;
      soldDepo = getDepo( soldItems, orvvzDocInfo.rec.Name );
      totSoldDepo = totSoldDepo + soldDepo;
      totSumSold=totSumSold+capIssueDoc.rec.Sum;
      priceRepr=money( ciMisc.rec.Price * orvvzDocInfo.rec.NumItems );
      soldPar = ciMisc.rec.Price;
      sumSoldRepr=capIssueDoc.rec.Sum;
    elif(capIssueDoc.rec.Operation==O_LOTTERY_BUY)
      boughtItems = orvvzDocInfo.rec.NumItems;
      totBoughtItems = totBoughtItems + boughtItems;
      boughtDepo = getDepo( boughtItems, orvvzDocInfo.rec.Name  );
      totBoughtDepo = totBoughtDepo + boughtDepo;
      totSumBought=totSumBought+capIssueDoc.rec.Sum;
      boughtPriceRepr=money( ciMisc.rec.Price * orvvzDocInfo.rec.NumItems );
      boughtPar = ciMisc.rec.Price;
      sumBoughtRepr=capIssueDoc.rec.Sum;
    end;
  
    updateValCounter( capIssueDoc.rec.ApplicationKind, capIssueDoc.rec.ApplicationKey );

    if ( not makeReport )
      return;
    end;

    [|########|#####|#####|#################|################|#####|#####|#################|         |         |]
    (
      capIssueDoc.rec.DocNumber,
      SoldItems:z,
      SoldDepo:z,
      PriceRepr:z,
      SumSoldRepr:z,
      BoughtItems:z,
      BoughtDepo:z,
      SumBoughtRepr:z
    );
  end;
  
  macro orvvzSellRecTotals;
  
    var
      sellCredAcc = "",
      sellDebAcc = "",
      buyCredAcc = "",
      buyDebAcc = "";
  
    orvvzRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    if ( capIssueRec.rec.Operation == O_LOTTERY_SELL ) 
      notifyObserver( CI_ORVVZ, capIssueRec.rec.Issue, O_LOTTERY_SELL, "Продажа: " + orvvzRecInfo.rec.Name, totSumSold );
    else
      notifyObserver( CI_ORVVZ, capIssueRec.rec.Issue, O_LOTTERY_BUY, "Покупка: " + orvvzRecInfo.rec.Name, totSumBought );
    end;
  
    if ( not makeReport )
      return;
    end;

    [+--------+-----+-----+-----------------+----------------+-----+-----+-----------------+---------+---------+];
    [| Всего  |#####|#####|                 |################|#####|#####|#################|         |         |]
    (
      totSoldItems,
      totSoldDepo,
      totSumSold,
      totBoughtItems,
      totBoughtDepo,
      totSumBought
    );
    [         |-----------|-----------------|----------------|-----------|-----------------| ];
    [         | Дебет     | Дебет           | Дебет          | Дебет     | Дебет           | ];
    [         | счета     | счета           | счета          | счета     | счета           | ];
    [         | #         |                 |                | #         |                 | ]
    ( Acc_Form(sellDebAcc), Acc_Form(buyDebAcc) );                                          
    [         |-----------|-----------------|----------------|-----------|-----------------| ];
    [         | Кредит    | Кредит          | Кредит         | Кредит    | Кредит          | ];
    [         | счета     | счета           | счета          | счета     | счета           | ];
    [         | #         |                 |                | #         |                 | ]
    ( Acc_Form(sellCredAcc), Acc_Form(buyCredAcc) );                                          
    [ ];
    [                        Гл. (ст.) бухгалтер _______________________ ];
    [ ];
    [ ];
  end;
  
  macro outPayRecHead;
  
    var
      branchStrNum = "",
      departNum = "",
      departName = "",
      debAcc = "",
      credAcc = "",
      comment = "";
  
    if ( not makeReport )
      return;
    end;

    getBranchParams( branch, branchStrNum, departNum, departName );

    orvvzRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    comment = orvvzRecInfo.rec.Name;
  
    [                                                                                                            Ф. № 92
       ##################################
                                                                                                     Филиал № ##########
       Сберегательного банка № ##########
  
  
  
                                                   КОНТРОЛЬНАЯ  ВЕДОМОСТЬ                    Отметки бухгалтерии
  
                                             на оплату выигравших ценных бумаг         Дебет сч.  ######################
                                          #######################################      Кредит сч. ######################
                                                       за ##########                   Приход сч. ______________________
  
  
       Смена № #####
  
    ]
    (
      departName,
      branchStrNum,
      departNum,
      Acc_Form(debAcc),
      comment:c,
      Acc_Form(credAcc),
      {curdate},
      operBrigade
    );
    [+-------------------------------------+-------+-------+-----------+--------+--------+---------+-------------------+
     |                                     | Номер |       |           |        |Нарицат.|         |                   |
     |       Название ценной бумаги        |  тир. | Серия |   Номер   | Разряд |стоим-ть| Кол-во  |     Выплачено     |
     |                                     |       |       |           |        |        |         |                   |
     +-------------------------------------+-------+-------+-----------+--------+--------+---------+-------------------+];
  end;
  
  macro outPayRecLine;
  
    orvvzDocInfo.setRecordAddr(capIssueDoc,0,
      capIssueDoc.fldOffset("Info"),true);

    totPrice=totPrice+orvvzDocInfo.rec.Price;
    totSum=totSum+capIssueDoc.rec.Sum;
    totNumItems=totNumItems+orvvzDocInfo.rec.NumItems;

    updateValCounter( capIssueDoc.rec.ApplicationKind, capIssueDoc.rec.ApplicationKey );

    if ( not makeReport )
      return;
    end;

    [| ################################### | ##### | ##### |###########| ###### | ###### | ####### | ################# |]
    (
      orvvzDocInfo.rec.Name,
      orvvzDocInfo.rec.Drawing,
      orvvzDocInfo.rec.Series,
      orvvzDocInfo.rec.Number,
      orvvzDocInfo.rec.Category:z,
      orvvzDocInfo.rec.Price,
      orvvzDocInfo.rec.NumItems,
      capIssueDoc.rec.Sum
    );
  end;
  
  macro orvvzPayRecTotals;
  
    orvvzRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    notifyObserver( CI_ORVVZ, capIssueRec.rec.Issue, O_LOTTERY_PAYWIN, "Выплата выигрыша: " + orvvzRecInfo.rec.Name, totSum );
   
    if ( not makeReport )
      return;
    end;

    [+-------------------------------------+-------+-------+-----------+--------+--------+---------+-------------------+];
    [| В с е г о                           |       |       |           |        |        | ####### | ################# |]
    (
      totNumItems,
      totSum
    );
    [ ];
    [  # ]( rubToStr( totSum ) );
    [  ---------------------------------------------------------------------- ];
    [                          (сумма прописью) ];
    [ ];
  end;
  
  macro outRecTail;
  
    if ( not makeReport )
      return;
    end;

    [
  
  
  
          Контролер (оператор) ___________                                Кассир ____________________
    ];
    printLn( StrFor( 12 ) );
  end;
  
  macro orvvzSellRecBody;
  
    var
      recFound;
  
    capIssueDoc.rec.RecApplicKind=capIssueRec.rec.ApplicationKind;
    capIssueDoc.rec.RecApplicKey=capIssueRec.rec.ApplicationKey;
  
    recFound=capIssueDoc.getEQ;
    while(recFound
      and (capIssueDoc.rec.RecApplicKind==capIssueRec.rec.ApplicationKind)
      and (capIssueDoc.rec.RecApplicKey==capIssueRec.rec.ApplicationKey))
      if ( checkCIDoc )
        outSellRecLine;
      end; 
      recFound=capIssueDoc.next;
    end;
  end;
  
  macro orvvzSellRec;
  
    var
      RecFound;
  
    totNumItems=0;
    totSoldItems=0;
    totBoughtItems=0;
    totSoldDepo=0;
    totBoughtDepo=0;
    totSum=$0;
    totPrice=$0;
    totSumSold=$0;
    totSumBought=$0;
    resetValCounter;
  
    outSellRecHead;
    orvvzSellRecBody;
    orvvzSellRecTotals;
    outRecTail;
  end;
  
  macro orvvzPayRecBody;
  
    var
      recFound;
  
    capIssueDoc.rec.RecApplicKind=capIssueRec.rec.ApplicationKind;
    capIssueDoc.rec.RecApplicKey=capIssueRec.rec.ApplicationKey;
    recFound=capIssueDoc.getGE;
    while(recFound
      and (capIssueDoc.rec.RecApplicKind==capIssueRec.rec.ApplicationKind)
      and (capIssueDoc.rec.RecApplicKey==capIssueRec.rec.ApplicationKey))
      if ( checkCIDoc )
        outPayRecLine;
      end;
      recFound=capIssueDoc.next;
    end;
  end;
  
  macro orvvzPayRec;
  
    totNumItems=0;
    totSum=$0;
    resetValCounter;
    outPayRecHead;
    orvvzPayRecBody;
    orvvzPayRecTotals;
    outRecTail;
  end;
  
  macro printRec( applicationKind, applicationKey);
  
    if ( not makeReport )
      return;
    end;

    capIssueRec.keyNum = 2;
    capIssueRec.rec.ApplicationKind=applicationKind;
    capIssueRec.rec.ApplicationKey=applicationKey;
    if(not capIssueRec.getEQ)
      msgBox("Ошибка при печати ведомости");
      return;
    end;
    if((capIssueRec.rec.Operation==O_LOTTERY_SELL) or (capIssueRec.rec.Operation==O_LOTTERY_BUY))
      orvvzSellRec;
    elif(capIssueRec.rec.Operation==O_LOTTERY_PAYWIN)
      orvvzPayRec;
    end;
  end;
  
  macro sellBuyReport

    var
      recFound;
  
    capIssueRec.clear;
    capIssueRec.rec.Branch=Branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_ORVVZ;
  
    recFound=capIssueRec.getGE;
    while(recFound
      and (capIssueRec.rec.Branch==Branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_ORVVZ))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Oper==oper)
        and (capIssueRec.rec.Brigade==operBrigade))
        if((capIssueRec.rec.Operation==O_LOTTERY_SELL) or (capIssueRec.rec.Operation==O_LOTTERY_BUY))
          orvvzSellRec;
        end;
      end;
      recFound=capIssueRec.next;
    end;
  end;
  
  macro payWinReport
  
    var
      recFound;

    capIssueRec.clear;
    capIssueRec.rec.Branch=Branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_ORVVZ;
  
    recFound=capIssueRec.getGE;
    while(recFound
      and (capIssueRec.rec.Branch==Branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_ORVVZ))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Oper==oper)
        and (capIssueRec.rec.Brigade==operBrigade))
        if(capIssueRec.rec.Operation==O_LOTTERY_PAYWIN)
          orvvzPayRec;
        end;
      end;
      recFound=capIssueRec.next;
    end;
  end;
  
  macro proceed;
  
    sellBuyReport;
    payWinReport;
  end;

  macro isOpIncluded( issue, op )
    
    if ( ( op == O_LOTTERY_SELL ) 
      or ( op == O_LOTTERY_BUY ) 
      or ( op == O_LOTTERY_PAYWIN ) )
      return true;
    end;   

    return false;
  end; 

  InitTCIRecGenerator( _oper, _makeReport, _observer );
end;

macro orvvzRecReport

  var
    gen = TORVVZRecGenerator( {oper}, true );

  gen.proceed;
end;

macro orvvzRec( applicationKind, applicationKey )

  var
    gen = TORVVZRecGenerator( {oper}, true );

  gen.printRec( applicationKind, applicationKey );
end;

macro orvvzReport

  var
    gen = TORVVZRecGenerator( {oper}, true );

  gen.f5_report;
end;
