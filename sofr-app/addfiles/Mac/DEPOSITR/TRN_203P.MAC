//
//  R-Style SoftLab.
//
//  RS-Retail.
//
//  Форма 203 по внешнему платежу.
//

import DeprIntr, RSD, extp_recvfio;

private file curr ( "fininstr.dbt" ) key 0;
private file paym ( "pmpaym.dbt"   ) key 0;
private file lind ( "sb_lindc.dbt" ) key 1;
private file svod ( "sboksvod.dbt" ) key 2;
private file pmdata("rtpmdata.dbt") key 0;
private file person("person.dbt") key 0;
private var ddep = TRecHandler( "i_dp_dep.rec" );

// Виды операций
const
  IN_OPER  =  0,  // Приходная
  OUT_OPER =  1,  // Расходная
  XXX_OPER = -1;  // Неопознанная

/* *** переменные *** */
private var {Name_Bank};

/* *** собственно - массив расшифровок *** */
private array МассивРасшифровок;
private var   ОбщаяСуммаВозврата = $0;
private var   ФактическаяПроведеннаяСумма = $0;
private var   ФактическаяНеПроведеннаяСумма = $0;
private var   ФактическаяСуммаВозврата = $0;
private var   ReasonNoCarry = 0;

private var baDebetAcc = "", baCreditAcc = "";

/*********************************************************************/
private macro findCurr( codeCurr )
  rewind( curr );
  clearRecord( curr );
  curr.FIID = codeCurr;
  getEQ( curr );
end;

/* *** элементы массива расшифровок *** */
private class ИнформацияПоВозврату( ИмяКлиента_,
                                    НомерСчета_,
                                    СуммаВозврата_,
                                    НеПроводить_,
                                    КодОшибки_
                                  )
  var ИмяКлиента    = ИмяКлиента_,
      НомерСчета    = НомерСчета_,
      СуммаВозврата = СуммаВозврата_,
      НеПроводить   = НеПроводить_,
      КодОшибки     = КодОшибки_;
end;

/*********************************************************************/
private macro ИмеютсяДокументыTrnDoc( PaymID )
  var cmd, rs;
  var stat = FALSE;

  cmd = RsdCommand( "select t_Sum as sum " +
                      "from dtrn_doc_dbt "
                        "where t_PaymentID = " + String( PaymID ) );

  cmd.execute;

  rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    stat = TRUE;
  end;

  return stat;

end;

/*********************************************************************/
private macro ЗаполнитьМассивРасшифровок( PaymID )
  var cmd, rs;
  var stat;

  cmd = RsdCommand( "select t_Sum as sum, " +
                           "t_ReasonNoCarry as ReasNoCarry, " +
                           "t_Sname as sName, " +
                           "t_Nname as nName, " +
                           "t_Pname as pName, " +
                           "t_Account as Acc, " +
                           "t_ErrorCode as ErrCode "
                      "from dtrn_doc_dbt "
                        "where t_PaymentID = " + String( PaymID ) + " and " +
                              "t_Status <> \'X\'" );

  cmd.execute;

  rs = RsdRecordset( cmd );

  while ( ( rs.moveNext )  AND  ( ReasonNoCarry != 10 ) )

    if ( rs.value( "ReasNoCarry" ) == 10 ) // Проводок не было - исходная сумма меньше той, которая должна быть проведена по списку
      ReasonNoCarry = 10;
    end;
    ФактическаяНеПроведеннаяСумма = ФактическаяНеПроведеннаяСумма + rs.value( "sum" );
    МассивРасшифровок[asize(МассивРасшифровок)] = ИнформацияПоВозврату( rs.value( "sName" ) + " " + substr( rs.value( "nName" ), 1, 1 ) + "." + substr( rs.value( "pName" ), 1, 1 ) + ".",
                                                                        rs.value( "Acc" ),
                                                                        rs.value( "Sum" ),
                                                                        rs.value( "ReasNoCarry" ),
                                                                        rs.value( "ErrCode" ) );
  end;

  cmd = RsdCommand( "select nvl(Sum(t_Sum),0) as sum "
                      "from dtrn_doc_dbt "
                        "where t_PaymentID = " + String( PaymID ) + " and " +
                              "t_Status = \'X\'" );
  cmd.execute;
  rs = RsdRecordset( cmd );
  if( rs.moveNext )
    ФактическаяПроведеннаяСумма = rs.value( "sum" );
  else
    ФактическаяПроведеннаяСумма = 0;
  end;

end;

/* *** Сообщение об ошибке *** */
private macro СообщениеОбОшибке( _КодОшибки )
  private file sbm ( "sbbank.msg" ) key 0;
  var sErr = "";
  ClearRecord( sbm );
  sbm.Number = _КодОшибки;
  if ( GetEQ( sbm ) )
    sErr = sbm.Contents;
  else
    sErr = "Ошибка с кодом " + String( _КодОшибки ) + ".";
  end;
  return sErr;
end;

/* *** причина незачисления *** */
private macro ПричинаНезачисления( _НеПроводить, _КодОшибки )
  var sMess = "";
  if   ( _НеПроводить ==  0 )
    if ( _КодОшибки == 0 )
      sMess = "Проводка не выполнялась.";
    else
      sMess = СообщениеОбОшибке( _КодОшибки );
    end;
  elif ( _НеПроводить ==  3 )
    sMess = "Несоответствие ФИО.";
  elif ( _НеПроводить ==  4 )
    sMess = "Неверно задан номер счета (счет не найден).";
  elif ( _НеПроводить ==  5 )
    sMess = "Счет закрыт.";
  elif ( _НеПроводить ==  6 )
    sMess = "Не указан счет в загружаемом документе.";
  elif ( _НеПроводить ==  7 )
    sMess = "Установлена дата смерти вкладчика.";
  elif ( _НеПроводить ==  8 )
    sMess = "Установлен арест по счету.";
  elif ( _НеПроводить ==  9 )
    sMess = "Утеряна сберкнижка.";
  elif ( _НеПроводить == 11 )
    sMess = "Клиент найден в блокирующем стоп-листе.";
  elif ( _НеПроводить == 12 )
    sMess = "Клиент найден в предупреждающем стоп-листе.";
  elif ( _НеПроводить == 21 )
    sMess = "Проверка возможности операции не прошла.";  
  elif ( _НеПроводить == 50 )
    sMess = "Неверная платежная система.";
  elif ( _НеПроводить == 51 )
    sMess = "Неверный тип пластиковой карточки.";
  elif ( _НеПроводить == 52 )
    sMess = "Не задано эмбоссирование.";
  elif ( _НеПроводить == 53 )
    sMess = "Счет не карточный (не найдена карточная часть счета).";
  elif ( _НеПроводить == 54 )
    sMess = "Платежные системы карточки и счета не совпадают.";
  elif ( _НеПроводить == 55 )
    sMess = "Ошибка при заведении карточки.";
  elif ( _НеПроводить == 70 )
    sMess = "Ошибка при заведении клиента.";
  else
    sMess = "Установлен принудительный запрет на проводку с кодом " +
            String( _НеПроводить ) + ".";
  end;
  return sMess;
end;

/* *** напечатаь расшифровки *** */
private macro НапечататьРасшифровки
  var i = 0;
  if( asize(МассивРасшифровок) > 0 )

    [ +---------------------------------+--------------------------------+----------------------+---------------------------------+
      |           Номер счета           |        Ф.И.О. вкладчика        | Незачисленная сумма  |       Причина незачисления      |
      +---------------------------------+--------------------------------+----------------------+---------------------------------+];
    while( i < asize( МассивРасшифровок ) )
      [ | ############################### | ############################## | #################### | ############################### |]
      ( Acc_Form(МассивРасшифровок[i].НомерСчета):f:26,
        МассивРасшифровок[i].ИмяКлиента:29,
        МассивРасшифровок[i].СуммаВозврата:a:20:2,
        ПричинаНезачисления( МассивРасшифровок[i].НеПроводить,
                             МассивРасшифровок[i].КодОшибки ):w
      );
       [ +---------------------------------+--------------------------------+----------------------+---------------------------------+];
      i = i + 1;
    end;
    [ ];
  end;

end;

/**************************************************************************/
/*             Поиск счетов для платежей, введенных в RS-Bank.            */
/**************************************************************************/
macro findRSBAccs( _PaymentID )

  var AccFromPP = "???";  /* Счет, определяемый из платежного поручения */
  var cmdpm = RsdCommand;
  var rspm  = RsdRecordset( cmdpm );
  var cmdcor = RsdCommand;
  var rscor  = RsdRecordset( cmdcor );
  var vOperType = IN_OPER;

  /* Номера в T_DOCKIND для платежа из АРМ Позиционера */
  const ARM_POS1 = 320;
  const ARM_POS2 = 322;

  cmdpm.CmdText = " SELECT T_DOCKIND, T_PAYERACCOUNT FROM dpmpaym_dbt WHERE t_PaymentID = " +
                     String( _PaymentID );
  cmdpm.execute;
  if ( rspm.moveNext )
    if ( ( rspm.Fld("T_DOCKIND").Value != ARM_POS1 ) and
         ( rspm.Fld("T_DOCKIND").Value != ARM_POS2 ) )
      /* Платеж из ББ или РКО */
      AccFromPP = rspm.Fld("T_PAYERACCOUNT").Value;
    else
      /* Платеж из АРМ Позиционера */
      cmdcor.CmdText = " select cors.t_Account from dcorschem_dbt cors, dpmprop_dbt pmprop "
                       " where pmprop.t_PaymentID = " + String( _PaymentID ) +
                       " and pmprop.t_IsSender = 'X' "
                       " and cors.t_Number = pmprop.t_Corschem "
                       " and cors.t_FIID = pmprop.t_PayFIID and cors.t_FI_Kind = 1";
      cmdcor.execute;

      if ( rscor.moveNext )
          AccFromPP = rscor.Value(0);
      end;

    end;
  end;

  if ( AccFromPP != "???" )
    if ( vOperType == IN_OPER )
      baDebetAcc = AccFromPP;
    else
      baCreditAcc = AccFromPP;
    end;
  end;

end;

/***********************************************************************/
// Поиск счета получателя из pay_doc.dbt и sboksvod.dbt.
private macro findBookAccsPD_SV( pApplicationKind, pApplicationKey )
  var fbaErr = 0;
  var rs, IsCreditPaym;
  var cmdpm = RsdCommand;
  var rspm  = RsdRecordset( cmdpm );

      if ( paym.PaymentID>0 )
         rs = RsdRecordset( string("select pmprop.t_IsSender, pmprop.t_DebetCredit from dpmprop_dbt pmprop where ",
                            "pmprop.t_PaymentID = ", paym.PaymentID ) );

         IsCreditPaym = false;
         if ( rs.MoveNext() )
            if ( rs.value(0)!="X" )
               if ( rs.value(1)==1 )
                  IsCreditPaym = true;
               end;
            else
               if ( rs.value(1)==0 )
                  IsCreditPaym = true;
               end;
            end;
         else
            IsCreditPaym = true;
         end;

         cmdpm.CmdText = " SELECT t_futurePayerAccount, t_futureReceiverAccount FROM dpmpaym_dbt WHERE t_PaymentID = " +
                         String( paym.PaymentID );
         cmdpm.execute;
         if ( rspm.moveNext )
           if ( IsCreditPaym )
              baDebetAcc = rspm.Value(0);
           else
              baCreditAcc = rspm.Value(1);
           end;
         end;
      else
         fbaErr = 1;
      end;

  if ( fbaErr != 0 )
    PrintLn( " *** Ошибка: счет получателя не найден. Код = ", fbaErr );
  end;
end;

/***********************************************************************/
// Поиск счета получателя.
private macro findBookAccs( _PaymentID )

  var cmdpd = RsdCommand;
  var rspd  = RsdRecordset( cmdpd );

  cmdpd.CmdText = "select t_iApplicationKind, t_ApplicationKey " +
                    "from dpay_doc_dbt " +
                      "where t_PaymentID = " + String( _PaymentID ) + " and " +
                            "t_NumOpert = 4";

  cmdpd.execute;
  if ( rspd.moveNext )
    findBookAccsPD_SV( rspd.Fld("t_iApplicationKind").Value,
                       rspd.Fld("t_ApplicationKey").Value );
  end;
end;

/***********************************************************************/
// Определение Фамилии И.О. контроллера
private macro GetControllerFIO( _PaymentID )

  var FIO = "", Family, Name, Patron;

  // Поиск ритейловских свойств внеш. платежа
  pmdata.PaymentID = _PaymentID;
  if( getEQ( pmdata ) )
    // Поиск контроллера
    person.Oper = pmdata.Controller;
    if( getEQ( person ) )

      ParseFIO( person.Name, Family, Name, Patron );
      FIO = Family;
      if( strlen(Name) > 0 )
        FIO = FIO + " " + substr(Name, 1, 1) + ".";
      end;
      if( strlen(Patron) > 0 )
        FIO = FIO + " " + substr(Patron, 1, 1) + ".";
      end;

    end;
  end;

  return FIO;
end;

/*********************************************************************/
macro НапечататьОрдер203( PaymID, PaymNumb, IsConsolidated )

 var existTrnDoc = FALSE;
 var Контроллер = "";

 // Находим запись внешнего платежа.
 ClearRecord( paym );
 paym.PaymentID = PaymID;
 if ( getEQ( paym ) )
   ОбщаяСуммаВозврата = paym.PayAmount;
 else
   println( " ***  Не найдена запись внешнего платежа с PaymentID = ", PaymID );
   return FALSE;
 end;

 // Находим запись по ВСП.
 findDepartment(NumFNcash, null, ddep);

 // Находим код валюты.
 findCurr( paym.FIID );

 if ( IsConsolidated )
   existTrnDoc = ИмеютсяДокументыTrnDoc( PaymID );

   if ( existTrnDoc )
     ЗаполнитьМассивРасшифровок( PaymID );
     ФактическаяСуммаВозврата = ОбщаяСуммаВозврата - ФактическаяПроведеннаяСумма;
   else
     ФактическаяСуммаВозврата = paym.PayAmount;
   end;
 else
   ФактическаяСуммаВозврата = paym.PayAmount;
 end;

 findRSBAccs( PaymID );
 findBookAccs( PaymID );
 Контроллер = GetControllerFIO( PaymID );


    [ #                                                                ф.№ 203 ]( {Name_Bank} );
    [ Филиал № #                                                               ]( ddep.rec.Name );
    [ --------------------                                                     ];
    [ наименование филиала банка                                               ];
    [                                                                          ];
    [                МЕМОРИАЛЬНЫЙ ОРДЕР  №                                     ];
    [                                                                          ];
    [ #                                                                        ]( paym.ValueDate:m );
    [                                                                          ];
    [ Сумма прописью                                                           ];
    [ ######################################################################## ]( CurToStrAlt( ФактическаяСуммаВозврата, NULL, NULL, int( int( curr.ISO_Number ) ) ):w );
    [ ------------------------------------------------------------------------ ];
    [ ИНН                                   | Сумма     | #################### ]( ФактическаяСуммаВозврата );
    [                                       |           |                      ];
    [                                       |-----------|--------------------- ];
    [ Плательщик                            | Сч. №     | ########################## ]( Acc_Form(baDebetAcc) );
    [                                       |           |                      ];
    [ --------------------------------------|-----------|                      ];
    [                                       | БИК       |                      ];
    [                                       |-----------|                      ];
    [ Банк плательщика                      | Сч. №     |                      ];
    [                                       |           |                      ];
    [ --------------------------------------|-----------|--------------------- ];
    [                                       | БИК       |                      ];
    [                                       |-----------|                      ];
    [ Банк получателя                       | Сч. №     |                      ];
    [                                       |           |                      ];
    [ --------------------------------------|-----------|                      ];
    [ ИНН                                   | Сч. №     | ########################## ]( Acc_Form(baCreditAcc) );
    [                                       |           |                      ];
    [ Получатель                            |           |                      ];
    [                                       |           |                      ];
    [                                       |-----------|--------------------- ];
    [                                       | Вид оп.   |     | Срок пл.|      ];
    [                                       |-----------|-----|---------|----- ];
    [                                       | Наз. пл.  |     | Очер.п. |      ];
    [                                       |-----------|-----|---------|----- ];
    [                                       | Код       |     | Рез.поле|      ];
    [ ------------------------------------------------------------------------ ];
    [ Назначение платежа (содержание операции)                                 ];
    [ ######################################################################## ]
    ( ( "Сумма не зачислена (на счета физических лиц): п/п № " +
      PaymNumb + " от " + paym.ValueDate ):w );
    [ ________________________________________________________________________ ];
    [                      Подписи                     Отметки банка           ];
    [                                                                          ];
    [                      _______________________                             ];
    [                                                                          ];
    [ #################### _______________________                             ]( Контроллер );
    [                                                                          ];
    if ( IsConsolidated )
      if ( existTrnDoc )
        [ -------------------------------------------------------------------------];
        [ Сумма не зачислена (на счета физических лиц): п/п № #]( PaymNumb + " от " + paym.ValueDate );
        [                                                                          ];
        if (ReasonNocarry != 10)
          НапечататьРасшифровки();
          if (ОбщаяСуммаВозврата != ФактическаяПроведеннаяСумма + ФактическаяНеПроведеннаяСумма)
            [ Нераспределенная сумма: #################### ](ОбщаяСуммаВозврата - ФактическаяПроведеннаяСумма - ФактическаяНеПроведеннаяСумма);
          end;
        else
          [ Проводка по счетам вкладчиков не прошла: загруженная сумма больше суммы платежного поручения];
        end;
      else
        [ Сводный платеж не имеет документов безналичных перечислений на счета вкладчиков.];
      end;
    end;
    return true;

end;
