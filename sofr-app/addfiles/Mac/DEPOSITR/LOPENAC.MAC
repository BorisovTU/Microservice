/****************************************************************************
*  ---------------------                                                    *
*  R-Style SoftWare Lab.                                                    *
*  ---------------------                                                    *
*  RS-Retail                                                                *
*                                                                           *
*  Филиал                                                                   *
*                                                                           *
*  Отчет по закрытым счетам за период                                       *
*                                                                           *
*  Лебедев С.В.                                                             *
*  11.01.2000                                                               *
****************************************************************************/

import DeprIntr, SqlConv, CommonInter, rsd;

var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

var
  sb_dtyp = TBFile( "sb_dtyp.dbt" ),             /* Виды вкладов                      */
  sb_typop = TBFile( "sb_typop.dbt" ),           /* Операции по видам вкладов         */
  depositr = TBFile( "depositr.dbt" ),           /* Счета вкладчиков                  */
  depositrtmp = TBFile( "depositrttt.tmp", "w"),      /* Счета вкладчиков - для отчета     */
  currency = TBFile( "currency.dbt" );           /* Справочник валют                  */

var depclnt = TClientList;               /* Справочник вкладчиков             */

var FlagCur = NumFlagCur();              /* Валютность                        */
var Branch  = NumFNCash();               /* Номер филиала                     */
var {curdate};                           /* Текущая дата опердня              */

var NumPage              = 1;            /* Номер текущей страница отчета     */
var NumPP                = 0;            /* Номер по порядку                  */
var CountPage            = 0;            /* Количество записей на странице    */
var CountDepType         = 0;            /* Количество счетов вклада          */
var CountCurrency        = 0;            /* Количество счетов одной валюты    */
var CountAll             = 0;            /* Общее количество счетов           */
var CurLine              = 0;            /* Номер текущей линии на странице   */
var WasPrintHeadDepType  = FALSE;        /* Был напечатан заголовок по вкладу */
var WasPrintHeadCurrency = FALSE;        /* Был напечатан заголовок по валюте */

const MaxLine = 60;                      /* Количество строк на странице      */

var CreateTmp   = FALSE;
var {BatchMode};

/* Панель ввода данных для отчета */
record ReportData ("LOPENAC", "rtusrmac") dialog;
/* Номер полей панели */
const ne_BegDate     = 0; /* Начальная дата  */
const ne_EndDate     = 1; /* Конечная дата   */
const ne_TypeAccount = 2; /* Вид вклада      */
const ne_TypeSort    = 3; /* Тип сортировки  */

var NumTypeSort = 0;
array ATypeSort;
  ATypeSort(0)  = " по номеру         ";
  ATypeSort(1)  = " по ФИО";

ReportData.BegDate     = {curdate};
ReportData.EndDate     = {curdate};
ReportData.TypeAccount = "";
ReportData.TypeSort    = Trim(ATypeSort(NumTypeSort));

/* Значения кода режима работы */
const MODE_NORMAL = 0;       /* Нормальный режим */
const MODE_EOY = 1;          /* Конец периода */
const MODE_EOY_PRECALC = 2;  /* Конец периода - документы предварительного расчета */
const MODE_GZ = 4;           /* Группа зачисления */


/****************************************************************************
                         Поиск клиента по его коду
****************************************************************************/
macro FindClient ( CodClient )

  return depclnt.GetRecord( CodClient );

end;


/****************************************************************************
                         Создание временного файла
****************************************************************************/
macro CreateTmpDepositr

  var stat = TRUE;
  var flag;
  var filterStr, cmd;
  var ErrCode, ErrText;

  Message(" Создается временный файл ...");

//  depositrtmp = TBFile( "depositrttt.tmp", "c+", 0 );

  cmd = RsdCommand( NULL, string("truncate table ddepositrttt_tmp") );
  cmd.execute();

  depositr.KeyNum = 2;
  depositr.rec.IsCur     = FlagCur;
  depositr.rec.FNCash    = Branch;
  depositr.rec.Open_Date = ReportData.BegDate;

  filterStr = "t.t_IsCur = " + string( FlagCur ) +
              " and t.t_FNCash = " + string( Branch ) +
              " and t.t_Open_Date between " + sqlDateToStr( ReportData.BegDate ) + " and " + sqlDateToStr( ReportData.EndDate ) +
              " and t.t_Action not in ( 2, 11 )" +
              " and not exists (select 1 from dsbdepdoc_dbt d " +
              "   where t.t_Referenc = d.t_Referenc " +
              "     and d.t_TypeOper in (1, 31, 51, 58) " +
              "     and d.t_Mode = " + String(MODE_GZ) + ") ";

  if ( ReportData.TypeAccount != "" )
    filterStr = filterStr + " and t_Type_Account = " + GetSQLString( ReportData.TypeAccount );
  end;
  
  depositr.addFilter( filterStr );

  flag = depositr.GetGE;
  while (flag)
    if ((depositr.rec.IsCur      == FlagCur ) AND
        (depositr.rec.FNCash     == Branch  ) AND
        (depositr.rec.Open_Date >= ReportData.BegDate ) )
      if ( ( depositr.rec.Open_Date <= ReportData.EndDate ) )
        if ((ReportData.TypeAccount == ""                    ) OR
            (depositr.rec.Type_Account  == ReportData.TypeAccount)   )
          if ((depositr.rec.Action == 2) OR (depositr.rec.Action == 11))
            ;
          else
            depositrtmp.Clear;

            Copy( depositrtmp, depositr );

            if ( FindClient( depositr.rec.CodClient ) )
              depositrtmp.rec.FIO_Client =
                Trim( depclnt.CurRec.rec.Name1 + " " +
                      depclnt.CurRec.rec.Name2 + " " +
                      depclnt.CurRec.rec.Name3         );
            end;
            if ( not depositrtmp.Insert )
              ErrCode = Status( ErrText );
              if ( ErrCode == 5 )
                ;
              else
                flag = FALSE;
                stat = FALSE;

                [ ];
                PrintLn( " *** ", ErrCode, ": ", ErrText );
                [ Ошибка: Запись во временный файл];
              end;
            end;
          end;
        end;
        if (flag)
          flag = depositr.Next;
        end;
      else
        flag = FALSE;
      end;
    else
      flag = FALSE;
    end;
  end;

  depositr.dropFilter;

  return stat;

end;


/****************************************************************************
                            Всего по виду вклада
****************************************************************************/
macro WriteItogPage

  [ ------------------------------------------------------------------------------------------------];
  [ ];
  [  Всего по странице          ##](CountPage);
  CountPage = 0;
  CurLine = CurLine + 1;

end;


/****************************************************************************
                           Начать новую страницу
****************************************************************************/
macro BeginNewPage

  NumPage = NumPage + 1;
  [#](StrFor(12));
  [                                                                                                 Стр. #####](NumPage);
  CurLine = 1;

end;


/****************************************************************************
                          Печать общего заголовка
****************************************************************************/
macro WriteAllHead

  var NameBranch;

  if (not findDepartment(Branch, NameBranch))
    NameBranch = "";
  end;

  [ ];
  [ Филиал № #](NameBranch);
  [ ];
  [ ];
  [       Ведомость открытых счетов за период с ########## по ##########]
  (ReportData.BegDate,ReportData.EndDate);
  [ ];
  [ ];

  CurLine = CurLine + 6;

end;


/****************************************************************************
                         Написать заголовок страницы
****************************************************************************/
macro WriteHeadPage

 [ ------------------------------------------------------------------------------------------------];
 [ | № п/п |     Номер счета      |                 Ф.И.О.  вкладчика                  |Дата откр.|];
 [ ------------------------------------------------------------------------------------------------];

 CurLine = CurLine + 3;

end;

/****************************************************************************
                        Написать заголовок по валюте
****************************************************************************/
macro PrintHeadCurrency

  [ Валюта: #](currency.rec.ExternalCode + " (" + currency.rec.Name_Currency + ")");
  [ ];
  CurLine = CurLine + 2;
  WasPrintHeadCurrency = TRUE;

end;


/****************************************************************************
                      Написать заголовок по виду вклада
****************************************************************************/
macro PrintHeadDepType (FlagRez)

  var NameType = sb_dtyp.rec.Name;

  if (FlagRez)
    NameType = NameType + " (нерезидентный)";
  else
    NameType = NameType + " (резидентный)";
  end;

  [ Вид вклада: #](NameType);
  [ ];
  CurLine = CurLine + 2;
  WriteHeadPage();
  WasPrintHeadDepType = TRUE;

end;


/****************************************************************************
                     Вывести информацию о счете в отчет
****************************************************************************/
macro WriteAccountToReport (FlagRez)

  var FIO;

   if(  (not SpecialAccess) and OperMayListSpecialAccounts and depositrtmp.rec.SpecialAccess )
    FIO = "XXXXXX X.X.";
   else
    FIO = depositrtmp.rec.FIO_Client;
   end;

  NumPP         = NumPP         + 1;
  CountCurrency = CountCurrency + 1;
  CountDepType  = CountDepType  + 1;
  CountAll      = CountAll      + 1;

  /*
  if (depositrtmp.SpecialAccess != "")
    FIO = depositrtmp.Type_Account;
  end; */

  if (WasPrintHeadCurrency == FALSE)
    if (NumPage > 1)
      BeginNewPage();
    end;
    PrintHeadCurrency();
    PrintHeadDepType(FlagRez);
  elif (WasPrintHeadDepType == FALSE)
    BeginNewPage();
    PrintHeadDepType(FlagRez);
  end;

  [ |#######|######################|####################################################|##########|]
  ( NumPP:7, Acc_Form(depositrtmp.rec.Account), FIO, depositrtmp.rec.Open_Date );

  CountPage = CountPage + 1;
  CurLine   = CurLine   + 1;

  if (CurLine >= MaxLine - 1)
    WriteItogPage();
    BeginNewPage();
    WriteHeadPage();
  end;

end;


/****************************************************************************
                            Всего по виду вклада
****************************************************************************/
macro WriteItogDepType

  [  Всего по виду вклада ########](CountDepType);
  CountDepType = 0;
  CurLine = CurLine + 1;

end;


/****************************************************************************
                               Всего по валюте
****************************************************************************/
macro WriteItogCurrency

  [  Всего по валюте      ########](CountCurrency);
  CountCurrency = 0;
  CurLine = CurLine + 1;

end;


/****************************************************************************
                                   Итого
****************************************************************************/
macro WriteItogAll

  [  Итого                ########](CountAll);
  CountAll = 0;
  CurLine = CurLine + 1;

end;


/****************************************************************************
                Обработка одного вида вклада заданной валюты
****************************************************************************/
macro WorkWithOneType (FlagRez)

  var flag;
  var RezAcc = StrFor(0);

  Message(" Выводится ведомость по вкладу ",sb_dtyp.rec.Kind," (",currency.rec.ExternalCode,") ...");

  WasPrintHeadDepType = FALSE;

  if (NumTypeSort == 0)
    depositrtmp.KeyNum = 1;
    depositrtmp.rec.IsCur         = FlagCur;
    depositrtmp.rec.FNCash        = Branch;
    depositrtmp.rec.Type_Account  = sb_dtyp.rec.Kind;
    depositrtmp.rec.Code_Currency = currency.rec.Code_Currency;
    depositrtmp.rec.Number        = 0;
  else
    depositrtmp.KeyNum = 2;
    depositrtmp.rec.IsCur         = FlagCur;
    depositrtmp.rec.FNCash        = Branch;
    depositrtmp.rec.Type_Account  = sb_dtyp.rec.Kind;
    depositrtmp.rec.Code_Currency = currency.rec.Code_Currency;
    depositrtmp.rec.FIO_Client    = "";
  end;

  depositrtmp.addFilter( "t.t_IsCur = " + string( FlagCur ) +
                         " and t.t_FNCash = " + string( Branch ) +
                         " and t.t_Type_Account = " + GetSQLString( sb_dtyp.rec.Kind ) +
                         " and t.t_Code_Currency = " + string( currency.rec.Code_Currency ) );

  flag = depositrtmp.GetGE;
  while (flag)
    if ((depositrtmp.rec.IsCur         == FlagCur                   ) AND
        (depositrtmp.rec.FNCash        == Branch                    ) AND
        (depositrtmp.rec.Type_Account  == sb_dtyp.rec.Kind          ) AND
        (depositrtmp.rec.Code_Currency == currency.rec.Code_Currency)    )
      RezAcc = StrFor( 0 );
      if ( FindClient( depositrtmp.rec.CodClient ) )
        RezAcc = depclnt.CurRec.rec.NotResident;
      end;
      if (FlagRez == RezAcc)
        WriteAccountToReport(FlagRez);
      end;
      flag = depositrtmp.Next;
    else
      flag = FALSE;
    end;
  end;

  depositrtmp.dropFilter;

  if (CountDepType > 0)
    WriteItogPage();
    if (CurLine >= MaxLine)
      BeginNewPage();
      WriteHeadPage();
      WriteItogPage();
    end;
    WriteItogDepType();
  end;

end;


/****************************************************************************
                                Выпуск отчета
****************************************************************************/
macro DoReport

   var flag;
   var stat;

   WriteAllHead();

   if (CreateTmpDepositr())
     CreateTmp = TRUE;
     currency.KeyNum = 0;
     currency.rec.Code_Currency = 0;
     if (FlagCur)
       flag = currency.GetGT;
     else
       flag = currency.GetEQ;
     end;
     while (flag)
       WasPrintHeadCurrency = FALSE;

       if ( ReportData.TypeAccount == "" )
         sb_dtyp.KeyNum = 1;
         sb_dtyp.rec.FlagCur = FlagCur;
         sb_dtyp.rec.Priorit = 0;

         sb_dtyp.addFilter( "t.t_FlagCur = " + string( FlagCur ) +
                            " and exists ( select 0 from ddepositrttt_tmp d where d.t_IsCur = t.t_FlagCur and d.t_Type_Account = t.t_Kind )" );

         stat = sb_dtyp.GetGE;
         while (stat)
           if (sb_dtyp.rec.FlagCur == FlagCur)
             WorkWithOneType(StrFor(0));
             WorkWithOneType(StrFor(1));
             stat = sb_dtyp.Next;
           else
             stat = FALSE;
           end;
         end;
         sb_dtyp.dropFilter;
       else
         sb_dtyp.KeyNum = 2;
         sb_dtyp.rec.FlagCur = FlagCur;
         sb_dtyp.rec.Kind = ReportData.TypeAccount; 
         if ( sb_dtyp.GetEQ )
           WorkWithOneType(StrFor(0));
           WorkWithOneType(StrFor(1));
         end; 
       end;

       if ((FlagCur) AND (CountCurrency > 0))
         if (CurLine >= MaxLine)
           BeginNewPage();
           WriteHeadPage();
           WriteItogPage();
         end;
         WriteItogCurrency();
       end;

       if (FlagCur)
         flag = currency.Next;
       else
         flag = FALSE;
       end;
     end;
   end;

   if (CountAll > 0)
     if (CurLine >= MaxLine)
       BeginNewPage();
       WriteHeadPage();
       WriteItogPage();
     end;
     WriteItogAll();
   end;

end;


/****************************************************************************
               Выбор из списка типов вкладов в панели диалога
****************************************************************************/
macro ListTypeAccount

  array ANameKind;
  array ANameMenu;

  var stat;
  var i;

  sb_dtyp.KeyNum = 1;
  sb_dtyp.rec.FlagCur = FlagCur;
  sb_dtyp.rec.Priorit = 0;

  sb_dtyp.addFilter( "t.t_FlagCur = " + string( FlagCur ) +
                     " and exists ( select 0 from dsb_typop_dbt o where o.t_IsCur = t.t_FlagCur and o.t_Kind = t.t_Kind )" );

  stat = sb_dtyp.GetGE;
  while (stat)
    if (sb_dtyp.rec.FlagCur == FlagCur)
      i = ASize(ANameKind);
      ANameKind(i) = sb_dtyp.rec.Kind;
      ANameMenu(i) = " " + SubStr(sb_dtyp.rec.Kind + MkStr(" ",13),1,13) + " " + sb_dtyp.rec.Name + " ";
      stat = sb_dtyp.Next;
    else
      stat = FALSE;
    end;
  end;

  sb_dtyp.dropFilter;

  if (ASize(ANameKind) > 0)
    i = Menu(ANameMenu,Null,"Виды вкладов");
    if (i >= 0)
      ReportData.TypeAccount = ANameKind(i);
    end;
  end;

end;


/****************************************************************************
              Выбор из списка типов сортировок в панели диалога
****************************************************************************/
macro ListTypeSort

  var i = Menu(ATypeSort,Null,"Типы сортировок");

  if (i >= 0)
    NumTypeSort = i;
    ReportData.TypeSort = Trim(ATypeSort(NumTypeSort));
  end;

end;


/****************************************************************************
                 Обработка нажатия клавишей в панели диалога
****************************************************************************/
macro WorkDialogForReport (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  /* =================================================================== */
  if (cmd == DLG_KEY)
    /* Enter --------------------------------------------------------- */
    if (key == 13)
      if (id == ne_TypeSort)
        SetFocus(IP,ne_BegDate);
        stat = CM_IGNORE;
      end;
    /* Пробел -------------------------------------------------------- */
    elif (key == 32)
      if (id == ne_TypeAccount)
        ReportData.TypeAccount = "";
      end;
    /* Esc ----------------------------------------------------------- */
    elif (key == 523)
      stat = CM_CANCEL;
    /* F7 ------------------------------------------------------------ */
    elif (key == 321)
      stat = CM_SAVE;
    /* F3 ------------------------------------------------------------ */
    elif (key == 317)
      if (id == ne_TypeAccount)
        ListTypeAccount();
      elif (id == ne_TypeSort)
        ListTypeSort();
      end;
    end;
  /* =================================================================== */
  elif (cmd == DLG_SAVE)
    ;
  end;

  return stat;

end;


/****************************************************************************
                      Г Л А В Н А Я   П Р О Г Р А М М А
****************************************************************************/

  if ({BatchMode} or RunDialog(ReportData,"WorkDialogForReport"))
    DoReport();
  else
    [ ];
    [ Отказались от выпуска ведомости по открытым счетам за период];
  end;

end;
