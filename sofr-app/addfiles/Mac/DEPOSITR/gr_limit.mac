/*
               **************************
               *  R-Style SoftWare Lab. *
               **************************

    RS-Retail 5.1
    Сбербанк
 Для вкладов, ставка по которым берется в зависимости от 
 первоначального взноса либо от неснижаемого остатка.

------------------------------------------------------------
 Цель:
  обеспечить взятие различных ставок в зависимости от:
- первоначального взноса (либо остатка на начало договора)
- установленного для счета неснижаемого остатка
------------------------------------------------------------
 Справочник ставок задается стандартным образом, 
 для вида ставки "дата + остаток"
------------------------------------------------------------

  Ромодин Александр Васильевич
  09.08.2002
*/                                                    
Import srpnrt_0,opercode,IsSrvDoc,sqlconv,rsd;
/*
  Структуры
*/
record DEPOSITR_GrLim    (depositr);   /* Текущий счет вкладчика      */
record SBDEPDOC_GrLim    (sbdepdoc);   /* Текущий документ вкладчика  */

/* file PC_DREST_GrLim  (pc_drest) key 0; */ /* Остатки по счету          */
var PC_RATE_GrLim = TBFile("pc_rate.dbt", "w", 0); /* Справочник ставок         */
var PC_CALC_GrLim = TDepFile("pc__calc.dbt", "w", 0); /* История оплат процентов   */
var SBDEPDOC_FGrLim = TDepFile("sbdepdoc.dbt", "w", 2); /* История операций по счету */

/* Список видов вкладов, для которых ставка считается по первоначальному
   взносу (по полному остатку на начало договора) */
Array TypeAccForFirst; 
TypeAccForFirst (0) = "Дол.депоз 3м";
TypeAccForFirst (1) = "Дол.депоз 6м";
TypeAccForFirst (2) = "ЕВРО-деп. 3м";
TypeAccForFirst (3) = "ЕВРО-деп. 6м";
TypeAccForFirst (4) = "ЕВРО-деп. 1м";
//TypeAccForFirst (7) = "ЕВРО-депФв1м";
const NumForFirst = Asize (TypeAccForFirst);
/* Список видов вкладов, для которых ставка считается по первоначальному
   взносу, а если была пролонгация- то по остатку на начало договора
   без учета причисленных процентов по виду вклада */
Array TypeAccForFirstWithOutPerc;
TypeAccForFirstWithOutPerc (0) = "П.депоз.1г1м";
TypeAccForFirstWithOutPerc (1) = "Нов.европ.1г";
TypeAccForFirstWithOutPerc (2) = "Нов.европ.2г";
TypeAccForFirstWithOutPerc (3) = "Юб.рен.1г_1м";
TypeAccForFirstWithOutPerc (4) = "Юбил.рента";
const NumForFirstWithOutPerc = Asize (TypeAccForFirstWithOutPerc);
/* Список видов вкладов, для которых ставка считается по величине
   неснижаемого остатка (лимита по счету) */
Array TypeAccForLimit;
TypeAccForLimit (0) = "Особ.ном.2 г";
TypeAccForLimit (1) = "Особ.ном. 2г";
TypeAccForLimit (2) = "Особ.н.1г_1м"; /* KIN 17.01.03 добавлен этот вид вклада  */
TypeAccForLimit (3) = "Особый 1г_1м"; /* KIN 21.02.03 добавлен этот вид вклада  */
TypeAccForLimit (4) = "Особый на 2г"; /* KIN 21.02.03 добавлен этот вид вклада  */

const NumForLimit = Asize (TypeAccForLimit);

/* var MacroError = 0; */

/**********************************************************/

macro NeedSpecificGrLim (Type_Account)
/*
  Возвращает:
 - 0, если вид вклада в списке не найден
 - 1, если вид вклада- в списке TypeAccForFirst (остаток на начало договора)
 - 2, если вид вклада- в списке TypeAccForFirstWithOutPerc (остаток на начало договора минус проценты) 
 - 3, если вид вклада- в списке TypeAccForLimit (неснижаемый остаток)
*/
  var stat = 0;
  var i;

  i = 0;
  while ((stat == 0) AND (i < NumForFirst))
    if (Type_Account == TypeAccForFirst(i))
      stat = 1;
    end;
    i = i + 1;
  end;
  i = 0;
  while ((stat == 0) AND (i < NumForFirstWithOutPerc))
    if (Type_Account == TypeAccForFirstWithOutPerc(i))
      stat = 2;
    end;
    i = i + 1;
  end;
  i = 0;
  while ((stat == 0) AND (i < NumForLimit))
    if (Type_Account == TypeAccForLimit(i))
      stat = 3;
    end;
    i = i + 1;
  end;
  return stat;
end;
/**********************************************************/

macro GetRateRecord (FlagCur, ObjectType, Referenc, Code_Currency, BeginDate)
/*
  Получить запись по ставке
*/
  var stat;

  PC_RATE_GrLim.addFilter("t.t_FlagCur = " + string(FlagCur) +
			  " and t.t_ObjectType = " + string(ObjectType) +
			  " and t.t_Referenc = " + GetSQLString(Referenc) +
			  " and t.t_Code_Currency = " + string(Code_Currency) );
  PC_RATE_GrLim.clear();
  PC_RATE_GrLim.rec.FlagCur        = FlagCur;    
  PC_RATE_GrLim.rec.ObjectType     = ObjectType;
  PC_RATE_GrLim.rec.Referenc       = Referenc;
  PC_RATE_GrLim.rec.Code_Currency  = Code_Currency;
  PC_RATE_GrLim.rec.BeginDate      = BeginDate;
  PC_RATE_GrLim.rec.DatePeriodType = 999;
  PC_RATE_GrLim.rec.DatePeriod     = 999;
  stat = PC_RATE_GrLim.GetLE ();
  if (stat)
    if ( (PC_RATE_GrLim.rec.FlagCur        != FlagCur)
     OR  (PC_RATE_GrLim.rec.ObjectType     != ObjectType)
     OR  (PC_RATE_GrLim.rec.Referenc       != Referenc)
     OR  (PC_RATE_GrLim.rec.Code_Currency  != Code_Currency) )
      stat = false;
    end;
  end;
  PC_RATE_GrLim.dropFilter();
  return stat;
end;
/**********************************************************/

macro IsOpenOper (Op)
/* 
  True - если передана операция открытия
*/
  return ((Op == Открытие_Cчета)
       OR (Op == Открытие_Наличными)
       OR (Op == Открытие_Безналичными)
       OR (Op == Открытие_Переводом) )
end;
/**********************************************************/

macro GetRest (dr,Need,RateDate)
/*
  Получить остаток для ставки
*/
  var RestForRate:money = 0;
  var cont = True;
  if (Need == 3)
    RestForRate = dr.Limit;
    if ( (RestForRate == 0)
     AND (dr.UseAlternate != 0) ) // Возможно, если идет выдача наследства
      RestForRate = dr.StaticRest;
    end;
  elif ((Need == 2) OR (Need == 1))
    if (RateDate == dr.Start_DateDep)
      if (IsOpenOper(SBDEPDOC_GrLim.TypeOper)) /* При выполнении открытия док-та в файле еще нет */
        RestForRate = SBDEPDOC_GrLim.InSum;    /* Берем из текущего документа */
      else
/*      Ищем операцию открытия счета: по PcDRest нельзя-
        в день открытия может пройти несколько операций, а
        нас интересует только первоначальный взнос */
	SBDEPDOC_FGrLim.addFilter("t.t_Referenc = " + string(dr.Referenc) );
        SBDEPDOC_FGrLim.clear();
        SBDEPDOC_FGrLim.rec.Referenc         = dr.Referenc;
        SBDEPDOC_FGrLim.rec.DepDate_Document = dr.Start_DateDep;
        cont = SBDEPDOC_FGrLim.GetGE();
        while (cont
         AND (SBDEPDOC_FGrLim.rec.Referenc == dr.Referenc))
          if ((NOT IsServDoc(SBDEPDOC_FGrLim.rec))
           OR (NOT IsOpenOper(SBDEPDOC_FGrLim.rec.TypeOper)) )
            cont = SBDEPDOC_FGrLim.Next();
          else
            cont = False;
            RestForRate = SBDEPDOC_FGrLim.rec.Rest;
          end;
        end;
	SBDEPDOC_FGrLim.dropFilter();
      end;
    else
/*
      ClearRecord (PC_DREST_GrLim);
      PC_DREST_GrLim.Referenc    = dr.Referenc;
      PC_DREST_GrLim.Type_Object = 2001;
      PC_DREST_GrLim.Date_Rest   = RateDate;
      if (GetLT(PC_DREST_GrLim)
       AND (PC_DREST_GrLim.Referenc    == dr.Referenc)
       AND (PC_DREST_GrLim.Type_Object == 2001) )
        RestForRate = PC_DREST_GrLim.Rest;
*/
      SBDEPDOC_FGrLim.addFilter("t.t_Referenc = " + string(dr.Referenc) );
      SBDEPDOC_FGrLim.clear();
      SBDEPDOC_FGrLim.rec.Referenc         = dr.Referenc;
      SBDEPDOC_FGrLim.rec.DepDate_Document = RateDate;
      cont = SBDEPDOC_FGrLim.GetLT();
      while (cont 
       AND (SBDEPDOC_FGrLim.rec.Referenc == dr.Referenc) )
        if (NOT IsServDoc(SBDEPDOC_FGrLim.rec) )
          cont = SBDEPDOC_FGrLim.Prev();
        else
          cont = False;
          RestForRate = SBDEPDOC_FGrLim.rec.Rest;
        end;
      end;
      SBDEPDOC_FGrLim.dropFilter();
      if (RestForRate > 0)
        if (Need == 2)  /* Уточним проценты */  
	  PC_CALC_GrLim.addFilter("t.t_Referenc = " + string(dr.Referenc) +
				  " and t.t_ObjectType = 2001" + 
				  " and t.t_TypeRecord = 1" );
          PC_CALC_GrLim.clear();
          PC_CALC_GrLim.rec.Referenc     = dr.Referenc;
          PC_CALC_GrLim.rec.ObjectType   = 2001;
          PC_CALC_GrLim.rec.TypeRecord   = 1;
          PC_CALC_GrLim.rec.EndDate      = RateDate;
          if (PC_CALC_GrLim.GetLT()
           AND (PC_CALC_GrLim.rec.Referenc   == dr.Referenc)
           AND (PC_CALC_GrLim.rec.ObjectType == 2001)
           AND (PC_CALC_GrLim.rec.TypeRecord == 1) )
            RestForRate = RestForRate - (PC_CALC_GrLim.rec.SumPayAll - dr.SumPercOut);
          end;
	  PC_CALC_GrLim.dropFilter();
	  PC_CALC_GrLim.addFilter("t.t_Referenc = " + string(dr.Referenc) +
				  " and t.t_ObjectType = 2002" + 
				  " and t.t_TypeRecord = 1" );
          PC_CALC_GrLim.clear();
          PC_CALC_GrLim.rec.Referenc     = dr.Referenc;
          PC_CALC_GrLim.rec.ObjectType   = 2002;
          PC_CALC_GrLim.rec.TypeRecord   = 1;
          PC_CALC_GrLim.rec.EndDate      = RateDate;
          if (PC_CALC_GrLim.GetLT()
           AND (PC_CALC_GrLim.rec.Referenc   == dr.Referenc)
           AND (PC_CALC_GrLim.rec.ObjectType == 2002)
           AND (PC_CALC_GrLim.rec.TypeRecord == 1) )
            RestForRate = RestForRate - PC_CALC_GrLim.rec.SumPayAll;
          end;
	  PC_CALC_GrLim.dropFilter();
        end;
      end;
    end;
  end;
  return RestForRate;
end;
/**********************************************************/

macro GetRateForRest (depRest)
/*
  По записи PC_RATE_GrLim выбрать ставку от остатка
*/
  var   Rate:doublel = 0.0l;
  var   Num  = PC_RATE_GrLim.rec.RestCount,
        Cont = true; 
  array Rest,
        Percent;
  if (PC_RATE_GrLim.rec.RestCount > 0)
    Rest(1)    = PC_RATE_GrLim.rec.Rest1;
    Percent(1) = PC_RATE_GrLim.rec.Percent1;
    Rest(2)    = PC_RATE_GrLim.rec.Rest2;
    Percent(2) = PC_RATE_GrLim.rec.Percent2;
    Rest(3)    = PC_RATE_GrLim.rec.Rest3;
    Percent(3) = PC_RATE_GrLim.rec.Percent3;
    Rest(4)    = PC_RATE_GrLim.rec.Rest4;
    Percent(4) = PC_RATE_GrLim.rec.Percent4;
    Rest(5)    = PC_RATE_GrLim.rec.Rest5;
    Percent(5) = PC_RATE_GrLim.rec.Percent5;
    Rest(6)    = PC_RATE_GrLim.rec.Rest6;
    Percent(6) = PC_RATE_GrLim.rec.Percent6;
    Rest(7)    = PC_RATE_GrLim.rec.Rest7;
    Percent(7) = PC_RATE_GrLim.rec.Percent7;
    Rest(8)    = PC_RATE_GrLim.rec.Rest8;
    Percent(8) = PC_RATE_GrLim.rec.Percent8;
    Rest(9)    = PC_RATE_GrLim.rec.Rest9;
    Percent(9) = PC_RATE_GrLim.rec.Percent9;
    Rest(10)    = PC_RATE_GrLim.rec.Rest10;
    Percent(10) = PC_RATE_GrLim.rec.Percent10;
    Rest(11)    = PC_RATE_GrLim.rec.Rest11;
    Percent(11) = PC_RATE_GrLim.rec.Percent11;
    Rest(12)    = PC_RATE_GrLim.rec.Rest12;
    Percent(12) = PC_RATE_GrLim.rec.Percent12;
    Rest(13)    = PC_RATE_GrLim.rec.Rest13;
    Percent(13) = PC_RATE_GrLim.rec.Percent13;
    Rest(14)    = PC_RATE_GrLim.rec.Rest14;
    Percent(14) = PC_RATE_GrLim.rec.Percent14;
    Rest(15)    = PC_RATE_GrLim.rec.Rest15;
    Percent(15) = PC_RATE_GrLim.rec.Percent15;
    Rest(16)    = PC_RATE_GrLim.rec.Rest16;
    Percent(16) = PC_RATE_GrLim.rec.Percent16;
    Rest(17)    = PC_RATE_GrLim.rec.Rest17;
    Percent(17) = PC_RATE_GrLim.rec.Percent17;
    Rest(18)    = PC_RATE_GrLim.rec.Rest18;
    Percent(18) = PC_RATE_GrLim.rec.Percent18;
    Rest(19)    = PC_RATE_GrLim.rec.Rest19;
    Percent(19) = PC_RATE_GrLim.rec.Percent19;
    Rest(20)    = PC_RATE_GrLim.rec.Rest20;
    Percent(20) = PC_RATE_GrLim.rec.Percent20;
    while (Cont AND (Num > 0))
      if (Rest(Num) <= depRest)
        Rate = Percent(Num);
        Cont = false;
      end;
      Num = Num - 1;
    end;
  end;
  return Rate;
end;
/**********************************************************/

macro GetProlDateDep (TestDate)
/*
  Поиск даты начала договора, в который
 попала заданная дата (по документам)
*/
  var FindDate = Date(0,0,0);
  var Cont;

  SBDEPDOC_FGrLim.addFilter("t.t_Referenc = " + string(DEPOSITR_GrLim.Referenc) );
  SBDEPDOC_FGrLim.clear();
  SBDEPDOC_FGrLim.rec.Referenc         = DEPOSITR_GrLim.Referenc;
  SBDEPDOC_FGrLim.rec.DepDate_Document = TestDate;
  Cont = SBDEPDOC_FGrLim.GetLE();
  while (Cont
   AND  (SBDEPDOC_FGrLim.rec.Referenc == DEPOSITR_GrLim.Referenc))
    if ((SBDEPDOC_FGrLim.rec.TypeOper        == Пролонгация_Депозитов)
     OR (SBDEPDOC_FGrLim.rec.TypeComplexOper == Пролонгация_Депозитов) )
      FindDate = SBDEPDOC_FGrLim.rec.DepDate_Document + 1;
      Cont = false;
    elif ( IsOpenOper(SBDEPDOC_FGrLim.rec.TypeOper)
     OR IsOpenOper(SBDEPDOC_FGrLim.rec.TypeComplexOper) )
      FindDate = SBDEPDOC_FGrLim.rec.DepDate_Document;
      Cont = false;
    else
      Cont = SBDEPDOC_FGrLim.Prev();
    end;
  end;
  SBDEPDOC_FGrLim.dropFilter();
  return FindDate;
end;
/**********************************************************/

macro GetRateForLimit (alg,dr,BeginDate,EndDate,doc)
  var ResRate:doublel = 0.0l;
  var Rate;
  var RateDate; /* Ставка фиксированная, действует с начала договора */
  var Need;
  var ObjectType,
      Referenc;
  var RestForRate:money;
  var TestDate:date;
  var cmd = RsdCommand ("select t_rategroup from dpc_alg_dbt where t_referenc in " +
                        "(select t_aptype from dpc_apltp_dbt " +
                        " where t_typerec = 1003 and t_appltype = 2001 " +
                        "and t_type = ? and t_iscur = ?)");
  var rs = RsdRecordset( cmd );

  setbuff (DEPOSITR_GrLim,dr);
  setbuff (SBDEPDOC_GrLim,doc);

  Need = NeedSpecificGrLim(DEPOSITR_GrLim.Type_Account);
  if (Need == 0)
    MacroError = -999;   /* Ставка будет опред. из усл. в настройке процентов */
  else
/*  Ставка фиксированная- на дату начала договора */
    if (DEPOSITR_GrLim.Prol_DateDep != Date(0,0,0))
      RateDate = DEPOSITR_GrLim.Prol_DateDep;
    else
      RateDate = DEPOSITR_GrLim.Start_DateDep;
    end;
/*
    Уточнение даты начала договора, если период расчета- не в текущем сроке
*/
    if ( (ValType(BeginDate) == V_DATE)
     AND (BeginDate != Date(0,0,0))
     AND (BeginDate < RateDate))
      if (BeginDate == DEPOSITR_GrLim.Open_Date)
        RateDate = DEPOSITR_GrLim.Open_Date;
      elif (BeginDate == DEPOSITR_GrLim.Start_DateDep)
        RateDate = DEPOSITR_GrLim.Start_DateDep;
      else
        TestDate = GetProlDateDep (BeginDate);
        if (TestDate != Date(0,0,0))
          RateDate = TestDate;
        else
          MacroError = 3649; /* Не найдена дата начала договора для заданного периода */
        end;
      end;
    end;
/*
    Ставка на дату начала договора
*/
    if (MacroError == 0)
/*
      if (DEPOSITR_GrLim.Perc_Type == 0)
*/
        cmd.addParam ("t_type", RsdBp_in);
        cmd.value ("t_type") = DEPOSITR_GrLim.Type_Account;
        cmd.addParam ("t_iscur", RsdBp_in);
        cmd.value ("t_iscur") = DEPOSITR_GrLim.IsCur;
        cmd.execute;
        if (rs.moveNext() )
          Referenc = rs.value(0);
          ObjectType = 1003;
        else
          MacroError = 3649; /* Ошибка при определении процентной ставки */
        end;
/*
      else
        ObjectType = 2001;
        Referenc   = String(DEPOSITR_GrLim.Referenc);
      end;
*/
    end; 
    if (MacroError == 0)
      if (GetRateRecord (DEPOSITR_GrLim.IsCur,ObjectType,Referenc,
          DEPOSITR_GrLim.Code_Currency,RateDate) )
        RestForRate = GetRest (DEPOSITR_GrLim,Need,RateDate);
        ResRate     = GetRateForRest (RestForRate);
      else
        MacroError = 3649; /* Ошибка при определении процентной ставки */
      end;
    end;
  end;
  return ResRate;
end;
/* ----- Конец файла ----- */
