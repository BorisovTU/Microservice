/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Печать штампа на документе коммунальных платежей.

*****************************************************************/

import DeprIntr, BankInter, RtPm_Lib, Acc_Form;

 record recip ( "rtpaymentprop.dbt" );
 record doccp ( "pay_doc_cp.dbt" );
 record doc   ( "pay_doc.dbt"   );

 private var pers  = TBFile( "person.dbt", "r", 0 );

 const cNumberPTK = 123;  // Номер ПТК.

/****************************************************************

****************************************************************/
private macro getDocTime()
  var vTime = Time( Int( SubStr( doc.ApplicationKey, 11, 2 ) ),
                    Int( SubStr( doc.ApplicationKey, 13, 2 ) ),
                    Int( SubStr( doc.ApplicationKey, 15, 2 ) ) );
  return vTime;
end;

/****************************************************************

****************************************************************/
private macro sDateZero( _Date )
 var dDay, dMon, dYear, StrDate;
 DateSplit( _Date, dDay, dMon, dYear );
 StrDate = string( _Date );
 if( dDay < 10 )
    StrSet( StrDate, 1, "0" );
 end;
 return StrDate;
end;

/****************************************************************

****************************************************************/
private macro sTimeZero( _Time )
 var dHour, dMin;
 TimeSplit( _Time, dHour, dMin );
 return Substr(String(dHour+100),2,2) + ":" + Substr(String(dMin+100),2,2);
end;

/****************************************************************
                    Чтение записи о Филиале
****************************************************************/
macro find_Branch( _FNCash )
  private var ddep  = TRecHandler( "i_dp_dep.rec" );
  private var party = TRecHandler( "i_party.rec" );
  var sFilial;
  if ( findDepartment( _FNCash, null, ddep, party ) )
    sFilial = ddep.rec.Name;
  else
    sFilial = "???";
  end;
  return sFilial;
end;

/****************************************************************
                    Чтение записи о
****************************************************************/
/*
macro GetOperName( pOper )
  pers.rec.Oper = pOper;
  if ( pers.getEQ()  )
    return pers.rec.Name;
  else
    return "";
  end;
end;
*/

/**********************************************************************/
 macro PrnStamp( pDocument ,     /* документ платежа           */
                 pPayDocument,   /* параметры комплатежа       */
                 pRecipient,     /* данные получателя платежа  */
                 DirectToPrn     /* != 0 - отпpавлять на пpинтеp */
               )

 var sBranch = "";
 var sSum = "";

 setbuff( recip, pRecipient);
 setbuff( doc,   pDocument);
 setbuff( doccp, pPayDocument);

 if ( DirectToPrn )
   SetOutput( "PRN" );  // назначаем вывод на принтер
 end;

 sBranch = find_Branch( doc.FNCash );

 if ( StrLen( doccp.KBK ) == 0 )

   sSum = String( doccp.PaySum:a );
   if ( doccp.PennySum != 0 )
     sSum = sSum + " " + String( doccp.PennySum:a );
   end;
   if ( doc.ComissionSum != 0 )
     sSum = sSum + "\n Плата " + String( doc.ComissionSum:a );
   end;

   [
     СБ ##########  #
     #
     ##########    #
     #
     ########## #
     ################ #
     Списание со счета
     #
     #
     Итого: #
     Кассир __________
   ]
   (
     sBranch,
     cNumberPTK,
     doc.ApplicationKey,
     sDateZero( doc.Date_Document ),
     sTimeZero( getDocTime() ):l,
     recip.BankName,
     recip.BankCode,
     Acc_Form(recip.CorrAccount),
     recip.INN,
     Acc_Form(recip.Account),
     doc.Ground,
     // doc.ApplType,
     sSum,
     doc.InSum:a:l
     // GetOperName( doc.Oper )
   );
 else

   sSum = String( doccp.PaySum:a );
   if ( doccp.PennySum != 0 )
     sSum = sSum + " " + String( doccp.PennySum:a );
   end;

   [
     СБ ##########  #
     #
     ##########    #
     #
     ######### ############### #
     #
     Списание со счета
     #
     #
     #
     Итого к уплате: #
     Кассир __________
   ]
   (
     sBranch,
     cNumberPTK,
     doc.ApplicationKey,
     sDateZero( doc.Date_Document ),
     sTimeZero( getDocTime() ):l,
     recip.BankName,
     recip.BankCode,
     recip.INN,
     Acc_Form(recip.Account),
     doccp.OKATO,
     doccp.KBK,
     doc.Ground,
     // doc.ApplType,
     sSum,
     doc.InSum:a:l
     // GetOperName( doc.Oper )
   );
 end;
end;
