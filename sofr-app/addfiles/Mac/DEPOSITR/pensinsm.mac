/***********************************************************************/
import deprintr, opercode;
/*----------------------------------------------------------------------

                                             27 мар. 1997, Клепиков С.А.

 Описание: Ревизия пенсионных вкладов

           Все открытые счета типа ПЕНСИОННЫЕ находящиеся в основном
           состоянии, для которых за задаваемый период времени
           ( например, последние 3 месяца) не было операции ПЕРЕЧИСЛЕНИЕ
           ПЕНСИИ, перевести в альтернативное состояние.

 19-08-97 Зубов В.Ю. Алгоритм проверки изменен на 2 зачисления пенсии в
          текущем календарном году
-----------------------------------------------------------------------*/

file ФайлСчетов     ( depositr          ) key 1;
file ФайлДокументов ( sbdepdoc          ) key 2;
file Person         ( person, "bank.def") key 0;

var
  accelOpen = false,
  {curdate},
  {oper};

const
  OP_2_ALT = 85,
  OP_2_DEP = 86,

  /*ТИП_ОПЕРАЦИИ_ЗАЧИСЛЕНИЕ                      = 73,*/
  ПЕНСИЯ                                       =  2,
  ПЕНСИЯ_МО                                    =  5,
  ПЕНСИЯ_МВД                                   =  6,
  ПЕНСИЯ_ФСБ                                   =  7,
  ПЕНСИЯ_ФСНП                                  =  8,
  ПЕНСИЯ_ПРОКУРАТУРЫ                           =  9,
  СЧЕТ_ОТКРЫТ                                  =  "",
  СЧЕТ_В_ОСНОВНОМ_СОСТОЯНИИ                    =  0,
  ОПЕРАЦИЯ_ПЕРЕВОДА_В_АЛЬТЕРНАТИВНОЕ_СОСТОЯНИЕ = OP_2_ALT,
  ОПЕРАЦИЯ_ПЕРЕВОДА_В_ОСНОВНОЕ_СОСТОЯНИЕ       = OP_2_DEP,
  FOREVER                                      = Date(1,1,9999),
  НЕОБХОДИМО_ЗАЧИСЛЕНИЙ                        = 2,
  today = {curdate};

const
  headerLine01 = "                    Отчет",
  headerLine02 = " ",
  headerLine03 = "     Dх-хх-хххх следующие счета переведены",
  headerLine04 = "     на альтернативные условия хранения:",
  headerLine13 = "  Следующие счета следует перевести на альтернативные",
  headerLine14 = "                 условия хранения:",
  headerLine05 = "┌─────────────────────────────┬────────────────────────┐",
  headerLine06 = "│ Номер счета                 │ начало действия        │",
  headerLine07 = "│                             │ альтернативных условий │",
  headerLine08 = "├─────────────────────────────┼────────────────────────┤",
  lineTemplate = "│ N                           │      S                 │",
  footerLine01 = "└─────────────────────────────┴────────────────────────┘";

/***********************************************************************/
MACRO printHeader(preview)

 var
   tmp;

   if(NOT preview) println(headerLine01); end;
   println(headerLine02);
   tmp = headerLine03;
   StrSet(tmp,Index(tmp,"D"),String(today));
   if(NOT preview) println(tmp         ); println(headerLine04);
   else            println(headerLine13); println(headerLine14);
   end;
   println(headerLine05);
   println(headerLine06);
   println(headerLine07);
   println(headerLine08);

END;
/***********************************************************************/
MACRO printLine(account,date)

 var
   tmp;

 tmp = lineTemplate;
 StrSet(tmp,Index(tmp,"S"),String(date));
 StrSet(tmp,Index(tmp,"N"),account);
 println(tmp);

END;
/***********************************************************************/
MACRO printFooter()

   println(footerLine01);

END;
/***********************************************************************/
MACRO MaxDate(date1,date2)

 if(date1 > date2)  return date1;
 else               return date2;
 end;

END;
/***********************************************************************/
MACRO нетОперацийНачисления (accountReferenc,startDate,endDate,account)

 var
   thereIsDocument,
   ЧислоЗачислений;
   /*result = True;*/

   if ( accelOpen )
     if ( not Open( ФайлДокументов, null, 1 ) )
       PrintLn( "Ошибка открытия файла sbdepdoc.dbt" );
       Exit( 1 );
     end;
   end;
   Message("Просматриваем историю счета "+account);
   rewind(ФайлДокументов);
   ФайлДокументов.Referenc      = accountReferenc;
   ФайлДокументов.DepDate_Document = Date(0,0,0);
   ФайлДокументов.NumDayDoc     = 0;
   ЧислоЗачислений              = 0;

   thereIsDocument = getGE(ФайлДокументов);
   while(    thereIsDocument
        AND  (ФайлДокументов.Referenc == accountReferenc)
        )
        if(   (ФайлДокументов.DepDate_Document >= startDate)
          AND (ФайлДокументов.DepDate_Document <= endDate)
          AND ((ФайлДокументов.TypeOper      == Зачисление_В_Пак_Режиме) OR
               (ФайлДокументов.TypeOper      == Открытие_Безналичными))
          AND ((ФайлДокументов.ApplType      == ПЕНСИЯ)     OR
               (ФайлДокументов.ApplType      == ПЕНСИЯ_МО)  OR
               (ФайлДокументов.ApplType      == ПЕНСИЯ_МВД) OR
               (ФайлДокументов.ApplType      == ПЕНСИЯ_ФСБ) OR
               (ФайлДокументов.ApplType      == ПЕНСИЯ_ФСНП)  OR
               (ФайлДокументов.ApplType      == ПЕНСИЯ_ПРОКУРАТУРЫ)))
          Message("Для счета "+account
                    +" найден документ о операции зачисления пенсии ");
          ЧислоЗачислений = ЧислоЗачислений + 1;
          if(ЧислоЗачислений >= НЕОБХОДИМО_ЗАЧИСЛЕНИЙ)
            return False;
          end; /* IF */
        end;
        thereIsDocument = next(ФайлДокументов);
   end;
   Message(  "Для счета "+account
              +" нет документов о начислении пенсии"
              +" с "+String(startDate)+" по "+String(endDate)
          );
   return True;
END;
/***********************************************************************/

macro ревизияПенсионных( adjPeriod, accel, prvw )


 var
    preview           = True,
    result            = 0,
    foundedAccounts   = 0,
    processedAccounts = 0,
    recordsCount      = 0,
    accountType       = "Пенсионный",
    monthsBack        = 3,
    errorCode,
    errorOccured = false,
    accountMoved,
    day,month,year,
    startDate,
    endDate,
    startOfThisYear,
    startOfNewState,
    resultNotes,
    thereIsAccount,
    FNCash = NumFNCash;

 record
    period(pens_ins)dialog;

 DateSplit( today, day, month, year );
 if( month < 10 )
   MsgBox( "Ревизию пенсионных вкладов можно| проводить только в 4-ом квартале!" );
   Exit( 1 );
 end;

 LogProcStartEnd(0,"Инициализирована процедура ревизии пенсионных вкладов");

 accelOpen = accel;
 if ( accelOpen )
   if ( not Open( ФайлСчетов, null, 1 ) )
     PrintLn( "Ошибка открытия файла depositr.dbt" );
     Exit( 1 );
   end;
 end;
 if ( ( day == 1 ) and ( month == 1 ) )
   year = year - 1;
 end;
 period.startDate = Date(1,1,year);
 period.endDate = Date(31,12,year);
 if ( adjPeriod )
   RunDialog(period);
 end;

 Person.Oper = {oper};
 if (NOT GetEQ(Person))
   Person.cTypePerson = "У";   /* Ревизор */
 end;

 if( period.startDate <= period.endDate )
    if (Person.cTypePerson == "У")
      preview = True; /* Ревизору- только отчет */
    elif ( ValType( prvw ) == V_UNDEF )
      preview = GetTrue(preview,"Выдать только отчет без обработки счетов?");
    else
      preview = prvw;
    end;
    endDate   = period.endDate;
    startDate = period.startDate;

    initProgress(NRecords(ФайлСчетов),
                 "Просматриваем счета за период с "
                  +String(startDate)
                  +" по "
                  +String(endDate)
                );
    rewind(ФайлСчетов);
    ФайлСчетов.IsCur          =  0;
    ФайлСчетов.FNCash         =  FNCash;
    ФайлСчетов.Open_Close     =  СЧЕТ_ОТКРЫТ;
    /*ФайлСчетов.Oper           =  0;*/
    ФайлСчетов.Type_Account   =  accountType;
    ФайлСчетов.Code_Currency  =  0;
    ФайлСчетов.Number         =  0;
    /*ФайлСчетов.Account        = "";*/

    printHeader(preview);
    thereIsAccount = getGE(ФайлСчетов);

/*MSGBOX("Account="+String(ФайлСчетов.Account+
       "|FNCash="+String(FNCash)));*/

    while(thereIsAccount)
         UseProgress(recordsCount);
         if(   (ФайлСчетов.IsCur        == 0)
           AND (ФайлСчетов.FNCash       == FNCash)
           AND (ФайлСчетов.Open_Close   == СЧЕТ_ОТКРЫТ)
           AND (ФайлСчетов.Type_Account == accountType))

             IF(( ФайлСчетов.UseAlternate == СЧЕТ_В_ОСНОВНОМ_СОСТОЯНИИ)
             AND ( ФайлСчетов.Open_Date < endDate )
             AND (нетОперацийНачисления
                     (ФайлСчетов.Referenc,startDate,endDate,ФайлСчетов.Account))
             AND ( not Index( ФайлСчетов.UserTypeAccount, "Т" ) )
             AND ( not Index( ФайлСчетов.UserTypeAccount, "Я" ) )
             )
             foundedAccounts = foundedAccounts+1;
             DateSplit(endDate,day,month,year);
             startOfThisYear = Date(1,10,year);
             startOfNewState = MaxDate(ФайлСчетов.Open_Date,startOfThisYear);
             if(NOT preview)
               Message( "Переводим счет "+ФайлСчетов.Account
                       +" в альтернативное состояние");
               errorCode = moveAccountIntoAnotherState
                                 (OP_2_ALT,ФайлСчетов.Referenc,
                                          startOfNewState,FOREVER,
                 /* VZ 04-03-1999 Что бы не было пересчета за прошлый период*/
                                          startOfNewState,endDate);

               InterDesk_EndDocBunch;
               if(errorCode==0)  accountMoved = True;
               else              accountMoved = False;
               end;
               Message(" ");
               if(accountMoved)
                 processedAccounts = processedAccounts+1;
               else
                 PrintLn(  "Возникли проблемы с переводом счета "
                                             +ФайлСчетов.Account
                         +". Код завершения ="+ String(errorCode)
                       );
                 errorOccured = true;
               end;
             end;
             printLine(ФайлСчетов.Account,startOfNewState);
           end;
           thereIsAccount = next(ФайлСчетов);
           recordsCount = recordsCount + 1;
         else
           thereIsAccount = False;
         end;

    end; /*while*/
    remProgress();
    printFooter();
    if(preview)
      resultNotes =  "  Всего за период с "+ String(startDate) +" по "
                                                       + String(endDate) +
                     "\nобнаружено " + String(foundedAccounts)
                                         + " счетов типа " + accountType +","
                     "\nтребующих перевода в альтернативное состояние.";
    else
      resultNotes =  "  Перевод счетов в альтернативное состояние завершен.\n"
                    +" \nВсего обнаружено " + String(foundedAccounts)
                                            + " счетов типа " + accountType
                    +" \nтребующих перевода в альтернативное состояние."
                    +" \nПереведено в альтернативное состояние "+
                                       String(processedAccounts) +" счетов."
                    +" \n ";
    end;
    PrintLn(resultNotes);
 end;

 LogProcStartEnd(1,"Завершена процедура ревизии пенсионных вкладов");

 return not errorOccured;
END;
/***********************************************************************/
