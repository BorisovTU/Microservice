/**********************************************************
*  ---------------------                                  *
*  R-Style SoftWare Lab.                                  *
*  ---------------------                                  *
*                                                         *
*  СБЕРБАНК 1.0                                           *
*                                                         *
*  Отчет по структуре вкладов по привлеченным средствам   *
*                                                         *
*  Лебедев С.В.                                           *
*  13.03.98                                               *
**********************************************************/

IMPORT deprintr, IsSrvDoc;

/*file listfdep(listfdep);*/
file sb_dtyp (sb_dtyp);
file depositr(depositr);
file sbdepdoc(sbdepdoc);
file currency(currency);

const FNCash = NumFNCash(); /* Номер филиала */
const FlagCur = NumFlagCur();

const NationalCodCur = 0;   /* Код валюты страны в файле currency.dbt */

private var {curdate};              /* Текущая дата  */

var ReportFNCash   = -1;    /* По какому филиалу выпустить отчет */

var NeedPrintZeroString = FALSE;

/* Граница даты и сумма за период дат */
array RightDate;
array SRestIn,SRestOut,SSumIn,SSumProl,SSumProc1,SSumProc2;
array NameDate;

/* Если граница даты до 1000 включительно - то это количество месяцев, */
/* если больше, то (RightDate - 1000) - количество лет                 */
  RightDate(0) =    1; NameDate (0) = "До 1 месяца";
  RightDate(1) =    3; NameDate (1) = "Свыше 1 до 3 мес";
  RightDate(2) =    6; NameDate (2) = "Свыше 3 до 6 мес";
  RightDate(3) = 1001; NameDate (3) = "Свыше 6 м до 1 г";
                       NameDate (4) = "Свыше 1 года";
  SRestIn  (0) = SRestIn  (1) = SRestIn  (2) = SRestIn  (3) = SRestIn  (4) = $0.0L;
  SRestOut (0) = SRestOut (1) = SRestOut (2) = SRestOut (3) = SRestOut (4) = $0.0L;
  SSumIn   (0) = SSumIn   (1) = SSumIn   (2) = SSumIn   (3) = SSumIn   (4) = $0.0L;
  SSumProl (0) = SSumProl (1) = SSumProl (2) = SSumProl (3) = SSumProl (4) = $0.0L;
  SSumProc1(0) = SSumProc1(1) = SSumProc1(2) = SSumProc1(3) = SSumProc1(4) = $0.0L;
  SSumProc2(0) = SSumProc2(1) = SSumProc2(2) = SSumProc2(3) = SSumProc2(4) = $0.0L;

/* Массив филиалов, учавствующих в отчете */
array FNCashArray;
array OpenCloseArray;
  OpenCloseArray(0) = "";
  OpenCloseArray(1) = "З";

/* Панель редактирования */
record PanelForRepStr2 ("REP_STR2") dialog;
const ne_BegDate    = 0, /* Начальная дата */
      ne_EndDate    = 1, /* Конечная дата  */
      ne_NameBranch = 2; /* Номер филиала  */

  PanelForRepStr2.NameBranch = "По всем";

const  R_DAY =   1, /* Дней              */
       R_MONTH = 2, /* Месяцев           */
       R_QW    = 3, /* Кварталов         */
       R_YEAR  = 4, /* Лет               */
       R_CDAY  = 5; /* Калкендарных дней */

const  OP_OPEN    = 31, /* ( 0) Открытие счета                       */
       OP_OPENCAS =  1, /* (31) -------------- наличными             */
       OP_OPENNCS = 51, /* (31) -------------- безналичными          */

       OP_FIND    =  2, /* ( 0) Поиск счета                          */
       OP_INDOP   =  3, /* ( 0) Дополнительный взнос                 */
       OP_OUT     =  4, /* ( 0) Расход                               */

       OP_PUT     = 70, /* ( 0) Зачисление                           */
       OP_PUTCON  = 71, /* (70) ---------- по данным последконтроля  */
       OP_PUTPR   = 72, /* (70) ---------- процентов                 */
       OP_PUTPAC  = 73, /* (70) ---------- в пакетном режиме         */

       OP_OUTPR   =  6, /* ( 0) Выдача процентов                     */
       OP_CALCPR  = 35, /* ( 0) Расчет процентов                     */

       OP_CLOSE   = 34, /* ( 0) Закрытие счета                       */
       OP_CLCAH   = 10, /* (34) -------------- наличными             */
       OP_CLNCAS  = 81, /* (34) -------------- безналичными          */

       OP_PRLDEP  = 82, /* ( 0) Пролонгация депозитов                */
       OP_PUTACC  = 83; /* ( 0) Пополнение счета                     */


/********************************************************************
                           Начало отчета
********************************************************************/
macro TopReport

  var day,month,year;
  var d,m,y;
  var NameBranch;
  var Code;

  DateSplit(PanelForRepStr2.EndDate,d,m,y);
  day = String(d);
  if (StrLen(day) == 1)
    day = "0" + day;
  end;
  if   (m ==  1) month = "января";
  elif (m ==  2) month = "февраля";
  elif (m ==  3) month = "марта";
  elif (m ==  4) month = "апреля";
  elif (m ==  5) month = "мая";
  elif (m ==  6) month = "июня";
  elif (m ==  7) month = "июля";
  elif (m ==  8) month = "августа";
  elif (m ==  9) month = "сентября";
  elif (m == 10) month = "октября";
  elif (m == 11) month = "ноября";
  elif (m == 12) month = "декабря";
  else month = "";
  end;
  year = String(y);

  if (ReportFNCash == -1)
    Code = FNCash;
  else
    Code = ReportFNCash;
  end;
  if (not findDepartment(Code, NameBranch))
    NameBranch = "";
  end;

  [ ];
  [ Справка 2];
  [ ];
  if (currency.Code_Currency == NationalCodCur)
    [    Информация о структуре привлеченных средств в рублевые вклады и депозиты физических лиц по срокам и причисленным процентам];
  else
    [  Информация о структуре привлеченных средств во вклады и депозиты физических лиц по срокам и причисленным процентам в валюте #](currency.Short_Name);
  end;
  [                                        Оперчасть ############  на #  #        #    года](NameBranch,day,month,year);
  [───┬────────────────┬─────────────╥──────────────┬──────────────╥──────────────┬─────────────┬──────────────┬──────────────╥───────────────];
  [Раз│Срок по договору│ Вид вкладов ║  Остаток на  │  Остаток на  ║    Сумма     │    Сумма    │    Сумма     │    Сумма     ║  Данные для];
  [дел│                │             ║  ##########  │  ##########  ║ привлеченных │ пролонгации │ причисленных │начисленных и ║  составления](PanelForRepStr2.BegDate,PanelForRepStr2.EndDate);
  [   │                │             ║              │              ║    средств   │             │      %%      │выплаченных %%║  Приложения 2];
  [───┼────────────────┼─────────────╫──────────────┼──────────────╫──────────────┼─────────────┼──────────────┼──────────────╫───────────────];

end;


/********************************************************************
                          Окончание отчета
********************************************************************/
macro BottomReport

  var i = 0;
  var SSRestIn   = $0.0L;
  var SSRestOut  = $0.0L;
  var SSSumIn    = $0.0L;
  var SSSumProl  = $0.0L;
  var SSSumProc1 = $0.0L;
  var SSSumProc2 = $0.0L;

  var SPRestIn   = $0.0L;
  var SPRestOut  = $0.0L;
  var SPSumIn    = $0.0L;
  var SPSumProl  = $0.0L;
  var SPSumProc1 = $0.0L;
  var SPSumProc2 = $0.0L;

  const NumPodSum = 3;

  while (i <= ASize(RightDate))
    SSRestIn   = SSRestIn   + SRestIn  (i);
    SSRestOut  = SSRestOut  + SRestOut (i);
    SSSumIn    = SSSumIn    + SSumIn   (i);
    SSSumProl  = SSSumProl  + SSumProl (i);
    SSSumProc1 = SSSumProc1 + SSumProc1(i);
    SSSumProc2 = SSSumProc2 + SSumProc2(i);
    i = i + 1;
  end;

  i = NumPodSum - 1;
  while (i <= ASize(RightDate))
    SPRestIn   = SPRestIn   + SRestIn  (i);
    SPRestOut  = SPRestOut  + SRestOut (i);
    SPSumIn    = SPSumIn    + SSumIn   (i);
    SPSumProl  = SPSumProl  + SSumProl (i);
    SPSumProc1 = SPSumProc1 + SSumProc1(i);
    SPSumProc2 = SPSumProc2 + SSumProc2(i);
    i = i + 1;
  end;

  [###│ИТОГО по разделам ## - ##     ║##############│##############║##############│#############│##############│##############║###############]
    ((ASize(NameDate) + 1):r,NumPodSum,ASize(RightDate),SPRestIn:a:r,SPRestOut:a:r,SPSumIn:a:r,
     SPSumProl:a:r,SPSumProc1:a:r,SPSumProc2:a:r,
     (SPSumIn + SPSumProl + SPSumProc1 + SPSumProc2):a:r);
  [═══╪══════════════════════════════╬══════════════╪══════════════╬══════════════╪═════════════╪══════════════╪══════════════╬═══════════════];
  [###│ИТОГО по всем разделам        ║##############│##############║##############│#############│##############│##############║###############]
    ((ASize(NameDate) + 2):r,SSRestIn:a:r,SSRestOut:a:r,SSSumIn:a:r,SSSumProl:a:r,
     SSSumProc1:a:r,SSSumProc2:a:r,
     (SSSumIn + SSSumProl + SSSumProc1 + SSSumProc2):a:r);
  [═══╧══════════════════════════════╩══════════════╧══════════════╩══════════════╧═════════════╧══════════════╧══════════════╩═══════════════];
  [ ];
  [ Исполнитель:];
  [ ##########]({curdate});
  [ ];
  [ ];

end;


/********************************************************************
                    Печать заголовка по периоду
********************************************************************/
macro PrintHeadPeriod (NumPeriod)

  [###│################│             ║              │              ║              │             │              │              ║               ]
  ((NumPeriod+1):r,NameDate(NumPeriod));

end;


/********************************************************************
                Определение суммы по типу вклада
********************************************************************/
macro DefSumForDepTypeAndPrint (NumPeriod)

  var i = 0;
  var j = 0;
  var stat;
  var flag;
  var RestIn   = $0.0L;
  var RestOut  = $0.0L;
  var SumIn    = $0.0L;
  var SumProl  = $0.0L;
  var SumProc1 = $0.0L;
  var SumProc2 = $0.0L;

  while (i < ASize(FNCashArray))
    j = 0;
    while (j < ASize(OpenCloseArray))
      KeyNum(depositr,1);
      ReWind(depositr);
      depositr.IsCur          = FlagCur;
      depositr.FNCash         = FNCashArray(i);
      depositr.Open_Close     = OpenCloseArray(j);
      depositr.Type_Account   = sb_dtyp.Kind;
      depositr.Code_Currency  = currency.Code_Currency;
      depositr.Number         = 0;
      /*depositr.Account        = "";*/
      stat = GetGE(depositr);
      while (stat)
        if ((depositr.IsCur          == FlagCur               ) AND
            (depositr.FNCash         == FNCashArray(i)        ) AND
            (depositr.Open_Close     == OpenCloseArray(j)     ) AND
            (depositr.Type_Account   == sb_dtyp.Kind          ) AND
            (depositr.Code_Currency  == currency.Code_Currency)    )
          /* Определение сумм */
          KeyNum(sbdepdoc,6);
          ReWind(sbdepdoc);
          sbdepdoc.Referenc      = depositr.Referenc;
          sbdepdoc.Date_Document = PanelForRepStr2.BegDate;
          sbdepdoc.NumDayDoc     = 0;
          flag = GetGE(sbdepdoc);
          while (flag)
            if ((sbdepdoc.Referenc         == depositr.Referenc      ) AND
                (sbdepdoc.Date_Document >= PanelForRepStr2.BegDate) AND
                (sbdepdoc.Date_Document <= PanelForRepStr2.EndDate)    )
              if (IsServDoc(sbdepdoc))
                /* ### Сумма привлеченных средств ################### */
                if ((sbdepdoc.TypeOper == OP_INDOP  ) OR
                    (sbdepdoc.TypeOper == OP_PUTCON ) OR
                    (sbdepdoc.TypeOper == OP_PUTPAC ) OR
                    (sbdepdoc.TypeOper == OP_OPENCAS) OR
                    (sbdepdoc.TypeOper == OP_OPENNCS)   )
                  SumIn = SumIn + sbdepdoc.InSum;
                /* ### Сумма пролонгации ############################ */
                elif ((sbdepdoc.TypeOper        == OP_PUTPR ) AND
                      (sbdepdoc.TypeComplexOper == OP_PRLDEP)    )
                  SumProl = SumProl + sbdepdoc.InSum;
                /* ### Сумма причисленных процентов ################# */
                elif ((sbdepdoc.TypeOper        == OP_PUTPR) AND
                      (sbdepdoc.TypeComplexOper == OP_PUTPR)    )
                  SumProc1 = SumProc1 + sbdepdoc.InSum;
                /* ### Сумма начисленых и выплаченных процентов ##### */
                elif (( sbdepdoc.TypeOper        == OP_PUTPR )    AND
                      ((sbdepdoc.TypeComplexOper == OP_CLCAH ) OR
                       (sbdepdoc.TypeComplexOper == OP_CLNCAS)   )    )
                  SumProc2 = SumProc2 + sbdepdoc.InSum - sbdepdoc.OutSum;
                elif ((sbdepdoc.TypeOper        == OP_OUTPR) AND
                      (sbdepdoc.TypeComplexOper == OP_OUTPR)    )
                  SumProc2 = SumProc2 + sbdepdoc.OutSum;
                end;
              end;
              flag = Next(sbdepdoc);
            else
              flag = FALSE;
            end;
          end;
          /* Определение исходящего остатка */
          if (depositr.Open_Date <= PanelForRepStr2.EndDate)
            KeyNum(sbdepdoc,6);
            ReWind(sbdepdoc);
            sbdepdoc.Referenc      = depositr.Referenc;
            sbdepdoc.Date_Document = PanelForRepStr2.EndDate;
            sbdepdoc.NumDayDoc     = 0;
            flag = GetLE(sbdepdoc);
            while (flag)
              if ((sbdepdoc.Referenc      == depositr.Referenc      ) AND
                  (sbdepdoc.Date_Document <= PanelForRepStr2.EndDate)    )
                if (IsServDoc(sbdepdoc))
                  RestOut = RestOut + sbdepdoc.Rest;
                  flag = FALSE;
                else
                  flag = Prev(sbdepdoc);
                end;
              else
                flag = FALSE;
              end;
            end;
          end;
          /* Определение входящего остатка */
          if ((depositr.Close_Date >= PanelForRepStr2.BegDate) OR
              (depositr.Close_Date == Date(0,0,0)            )   )
            KeyNum(sbdepdoc,6);
            ReWind(sbdepdoc);
            sbdepdoc.Referenc      = depositr.Referenc;
            sbdepdoc.Date_Document = PanelForRepStr2.BegDate;
            sbdepdoc.NumDayDoc     = 0;
            flag = GetLT(sbdepdoc);
            while (flag)
              if ((sbdepdoc.Referenc      == depositr.Referenc      ) AND
                  (sbdepdoc.Date_Document <  PanelForRepStr2.BegDate)    )
                if (IsServDoc(sbdepdoc))
                  RestIn = RestIn + sbdepdoc.Rest;
                  flag = FALSE;
                else
                  flag = Prev(sbdepdoc);
                end;
              else
                flag = FALSE;
              end;
            end;
          end;
          stat = Next(depositr);
        else
          stat = FALSE;
        end;
      end;
      j = j + 1;
    end;
    i = i + 1;
  end;

  if ((NOT NeedPrintZeroString) AND (RestIn   == 0) AND (RestOut  == 0) AND
                                    (SumIn    == 0) AND (SumProl  == 0) AND
                                    (SumProc1 == 0) AND (SumProc2 == 0)    )
    ;
  else
    [   │                │#############║##############│##############║##############│#############│##############│##############║###############]
    (sb_dtyp.Kind,RestIn:a:r,RestOut:a:r,SumIn:a:r,SumProl:a:r,SumProc1:a:r,
     SumProc2:a:r,(SumIn + SumProl + SumProc1 + SumProc2):a:r);
  end;

  SRestIn  (NumPeriod) = SRestIn  (NumPeriod) + RestIn;
  SRestOut (NumPeriod) = SRestOut (NumPeriod) + RestOut;
  SSumIn   (NumPeriod) = SSumIn   (NumPeriod) + SumIn;
  SSumProl (NumPeriod) = SSumProl (NumPeriod) + SumProl;
  SSumProc1(NumPeriod) = SSumProc1(NumPeriod) + SumProc1;
  SSumProc2(NumPeriod) = SSumProc2(NumPeriod) + SumProc2;

end;


/********************************************************************
      Входит ли срок действия типа вклада в текущий период
********************************************************************/
macro IsDepTypeInCurrentPeriod (NumPeriod)

  var DepTypeDate;
  var stat = FALSE;

  if (sb_dtyp.Term == 0)
    DepTypeDate = 0;
  /* День -------------------------------------------------------- */
  elif ((sb_dtyp.KindTerm == R_DAY) OR (sb_dtyp.KindTerm == R_CDAY))
    if (sb_dtyp.Term < 365)
      if (sb_dtyp.Term <= 30)
        DepTypeDate = 1;
      elif (sb_dtyp.Term <= 60)
        DepTypeDate = 2;
      elif (sb_dtyp.Term <= 90)
        DepTypeDate = 3;
      elif (sb_dtyp.Term <= 180)
        DepTypeDate = 6;
      else
        DepTypeDate = sb_dtyp.Term/30.0;
      end;
    elif (sb_dtyp.Term == 365)
      DepTypeDate = 1001;
    else
      DepTypeDate = 1000 + sb_dtyp.Term/365.0;
    end;
  elif (sb_dtyp.KindTerm == R_MONTH)
    if (sb_dtyp.Term < 12)
      DepTypeDate = sb_dtyp.Term;
    elif (sb_dtyp.Term == 12)
      DepTypeDate = 1001;
    else
      DepTypeDate = 1000 + sb_dtyp.Term/12.0;
    end;
  elif (sb_dtyp.KindTerm == R_QW)
    if (sb_dtyp.Term < 4)
      DepTypeDate = Int(sb_dtyp.Term * 3);
    elif (sb_dtyp.Term == 4)
      DepTypeDate = 1001;
    else
      DepTypeDate = 1000 + sb_dtyp.Term/4.0;
    end;
  elif (sb_dtyp.KindTerm == R_YEAR)
    DepTypeDate = 1000 + sb_dtyp.Term;
  end;

  if (NumPeriod == 0)
    if (DepTypeDate <= RightDate(0))
      stat = TRUE;
    end;
  elif (NumPeriod == ASize(RightDate))
    if (DepTypeDate > RightDate(ASize(RightDate) - 1))
      stat = TRUE;
    end;
  else
    if ((DepTypeDate >  RightDate(NumPeriod - 1)) AND
        (DepTypeDate <= RightDate(NumPeriod    ))    )
      stat = TRUE;
    end;
  end;

  return stat;

end;


/********************************************************************
                          Выпуск отчета
********************************************************************/
macro PrintReport

  var i = 0;
  var stat;
  var str;
  var dpDepList = TdpDepList;

  if (ReportFNCash == -1)
    dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
    while (dpDepList.next())
      FNCashArray(i) = dpDepList.rec.Code;
      i = i + 1;
    end;
  else
    FNCashArray(0) = ReportFNCash;
  end;

  i = 0;
  while (i <= ASize(RightDate))
    PrintHeadPeriod(i);
    KeyNum(sb_dtyp,2);
    sb_dtyp.FlagCur = FlagCur;
    sb_dtyp.Kind    = "";
    stat = GetGE(sb_dtyp);
    while (stat)
      if (sb_dtyp.FlagCur == FlagCur)
        if ((sb_dtyp.Kind == "Шаблонный") OR (sb_dtyp.Kind == "Служебный"))
          ;
        elif (IsDepTypeInCurrentPeriod(i))
          str = " Раздел " + String((i + 1):2) + ". Вклад " + sb_dtyp.Kind + " " + currency.Short_Name;
          Message(str);
          DefSumForDepTypeAndPrint(i);
        end;
        stat = Next(sb_dtyp);
      else
        stat = FALSE;
      end;
    end;
    [   │                ╞═════════════╬══════════════╪══════════════╬══════════════╪═════════════╪══════════════╪══════════════╬═══════════════];
    [   │                │Всего по р.##║##############│##############║##############│#############│##############│##############║###############]
    (i+1,SRestIn(i):a:r,SRestOut(i):a:r,SSumIn(i):a:r,SSumProl(i):a:r,
     SSumProc1(i):a:r,SSumProc2(i):a:r,
     (SSumIn(i) + SSumProl(i) + SSumProc1(i) + SSumProc2(i)):a:r);
    if (i < ASize(RightDate))
      [═══╪════════════════╪═════════════╬══════════════╪══════════════╬══════════════╪═════════════╪══════════════╪══════════════╬═══════════════];
    else
      [═══╪════════════════╧═════════════╬══════════════╪══════════════╬══════════════╪═════════════╪══════════════╪══════════════╬═══════════════];
    end;
    i = i + 1;
  end;

end;



/******************************************************************
                    Справочный скролинг филиалов
******************************************************************/
macro ListDep

  array AFNCash;
  array ANBranch;
  array MenuName;
  var i = 0;
  var dpDepList = TdpDepList;

  AFNCash(0) = -1;
  ANBranch(0) = "По всем";
  MenuName(0) = " " + SubStr(ANBranch(0) + MkStr(" ",12),1,12) + " ";

  dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
  while (dpDepList.next())
    i = i + 1;
    AFNCash(i) = dpDepList.rec.Code;
    ANBranch(i) = dpDepList.rec.Name;
    MenuName(i) = " " + SubStr(dpDepList.rec.Name + MkStr(" ",12),1,12) + "   " + /*listfdep.Name +*/ " ";
  end;

  i = Menu(MenuName,NULL,"Выберите филиал");

  if (i >= 0)
    ReportFNCash = AFNCash(i);
    PanelForRepStr2.NameBranch = ANBranch(i);
  end;

end;


/******************************************************************
                         Обработчик панели
******************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;
  var i;

  /* ==================== Обработчик клавиатуры ===================== */
  if (cmd == DLG_KEY)
    /* Enter --------------------------------------------------- */
    if (key == 13)
      if (id == ne_NameBranch)
        SetFocus(IP,ne_BegDate);
        stat = CM_IGNORE;
      end;
    /* Esc ----------------------------------------------------- */
    elif (key == 27)
      ;
    /* F3 ------------------------------------------------------ */
    elif (key == 317)
      if (id == ne_NameBranch)
        ListDep();
      end;
    /* F9 ------------------------------------------------------ */
    elif (key == 323)
      stat = CM_IGNORE;
    /* F7 ------------------------------------------------------ */
    elif (key == 321)
      stat = CM_SAVE;
    end;
  /* ========================== Сохранение ========================== */
  elif (cmd == DLG_SAVE)
    ;
  end;

  return stat;

end;


/********************************************************************
               Определение последнего дня месяца
********************************************************************/
macro LastDayInMonth (Mon)

  var Day = 31;

  if (Mon == 2)
    Day = 28;
  elif ((Mon == 4) OR (Mon == 6) OR (Mon == 9) OR (Mon == 11))
    Day = 30;
  end;

  return Day;

end;


/********************************************************************
                       Инициализация полей
********************************************************************/
macro InitFields

  var d,m,y;

  DateSplit({curdate},d,m,y);
  if (d == LastDayInMonth(m))
    PanelForRepStr2.EndDate = {curdate};
  else
    m = m - 1;
    if (m < 1)
      m = 12;
      y = y - 1;
    end;
    d = LastDayInMonth(m);
    PanelForRepStr2.EndDate = Date(d,m,y);
  end;
  PanelForRepStr2.BegDate = Date(1,m,y);

end;


/********************************************************************
                 Г Л А В Н А Я   П Р О Г Р А М М А
********************************************************************/

  var i;

  /* Определение дат отчета при инициализации */
  InitFields();
  if (RunDialog(PanelForRepStr2,"WorkDialog"))
    KeyNum(currency,0);
    ReWind(currency);
    while (Next(currency))
      if (((FlagCur == 0) AND (currency.Code_Currency == NationalCodCur)) OR
          ((FlagCur == 1) AND (currency.Code_Currency != NationalCodCur))   )
        i = 0;
        while (i < ASize(SRestIn))
          SRestIn(i) = SRestOut(i) = SSumIn(i) = SSumProl(i) = SSumProc1(i) = SSumProc2(i) = $0.0L;
          i = i + 1;
        end;
        TopReport();
        PrintReport();
        BottomReport();
      end;
    end;
  else
    [ ];
    [ Отказались от выпуска отчет о стуктуре привлеченных средств по срокам];
    [ ];
  end;

end;
