/*
$Name:               rpctax_x.mac
$Module:             DEPOSITR
$Description:        Формирование налоговых карточек и справок о доходах физического лица для налоговых органов
*/

/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Формирование налоговых карточек и справок о доходах
  физического лица для налоговых органов.

  Форма "Справка о доходах" в формате xml.

*****************************************************************/

Import rPcTax1c, RCW, rtkladr, XmlRpcInter;

private var {oper};
private var {CurDate};

private file TAXFILE () txt 1024 write;  // Файл формата XML.

private var opr = TBFile ( "person.dbt", "r", 0 );
private var obj_code  = TBFile ( "objcode.dbt",  "r", 0 );
private var obj_atcor = TBFile ( "objatcor.dbt", "r", 0 );

private const ENCODING = "rsansi";

private var sGUID = "";
private var OpenExportFile = FALSE;
private var aNameTaxFile = "";  // Имя файла формата XML в кодировке Windows-1251.
private var dNameTaxFile = "";  // Имя файла формата XML в кодировке DOS.
private var vNameFile = "";
private var vNumDoc = 1;
private var sDateDoc;

private var vINN = "", vKPP = "", iKPP = 0;

private var sOper = "";

private var gFlagName3 = FALSE;

private var sXML = "";

/***************************************************************************
***************************************************************************/
macro addZero( pVar, pLen )
  var str = Trim( String( pVar ) );
  var sRet = "";
  var l = StrLen( str );
  var i = 0, k = 0;
  if ( pLen > l )
    k = pLen - l;
    while ( i < k )
      sRet = sRet + "0";
      i = i + 1;
    end;
  end;
  sRet = sRet + str;
  return sRet;
end;

/***************************************************************************
***************************************************************************/
macro XML_Заголовок()
  var stat = TRUE;
  sXML = "<?xml version=\"1.0\" encoding=\"windows-1251\"?>";
  stat = Insert( TAXFILE, ToANSI( sXML ) );

  sXML = "<Файл";
  sXML = sXML + " ИдФайл=\"" + vNameFile + "\"";
  sXML = sXML + " ВерсПрог=\"RS-Retail v6\"";
  sXML = sXML + " ВерсФорм=\"5.05\">";
  return stat;
end;

macro Normalize( str )
   var lenght = strlen(str);

   if ( (lenght==8) or (lenght==11) )
      return str;
   end;

   if ( strlen(str)<8 )
      lenght = 8;
   elif ( strlen(str)<11 )
      lenght = 11;
   end;

   while( strlen(str)<lenght )
      str = str + "-";
   end;
   return str;
end;

// ===============  Ф.И.О. выпускающего отчет =======================
private macro FIO_Name1()
  var vInd = Index( sOper, " " );
  var vRet = "";
  if ( vInd <= 0 )
    vRet = sOper;
  else
    vRet = SubStr( sOper, 1, vInd-1 );
  end;
  return vRet;
end;

private macro FIO_Name2()
  var vInd1 = Index( sOper, " " );
  var vInd2 = -1;
  var vRet = "";
  if ( vInd1 > 0 )
    vInd2 = Index( SubStr( sOper, vInd1+1 ), " " );
    if ( vInd2 > 0 )
      vRet = SubStr( sOper, vInd1+1, vInd2-1 );
    else
      vRet = SubStr( sOper, vInd1+1 );
    end;
  end;
  return vRet;
end;


private macro FIO_Name3()
  var vInd1 = Index( sOper, " " );
  var vStr1 = "";
  var vInd2 = -1;
  var vRet = "";
  gFlagName3 = FALSE;
  if ( vInd1 > 0 )
    vStr1 = SubStr( sOper, vInd1+1 );
    vInd2 = Index( vStr1, " " );
    if ( vInd2 > 0 )
      gFlagName3 = TRUE;
      vRet = SubStr( vStr1, vInd2+1 );
    end;
  end;
  return vRet;
end;

/***************************************************************************
***************************************************************************/
macro Служебная_Часть()
  var stat = TRUE;
  sXML = sXML + "<СвРекв";
  sXML = sXML + " ОКТМО=\"" + Trim( Normalize(CodeOKATO) ) + "\"";
  sXML = sXML + " ОтчетГод=\"" + InputPanel_Year_InAll + "\"";
  sXML = sXML + " ПризнакФ=\"1\">";

  sXML = sXML + "<СвЮЛ";
  sXML = sXML + " ИННЮЛ=\"" + vINN + "\"";
  sXML = sXML + " КПП=\"" + vKPP + "\"></СвЮЛ>";

  sXML = sXML + "<Подписант";
  sXML = sXML + " ПрПодп=\"1\">";

  sXML = sXML + "<ФИО";
  sXML = sXML + " Фамилия=\"" + FIO_Name1() + "\"";
  sXML = sXML + " Имя=\""     + FIO_Name2() + "\"";
  FIO_Name3();
  if ( gFlagName3 )
    sXML = sXML + " Отчество=\"" + FIO_Name3() + "\"";
  end;
  sXML = sXML + "></ФИО";

  sXML = sXML + "></Подписант>";

  sXML = sXML + "</СвРекв>";
  return stat;
end;

/***************************************************************************
***************************************************************************/
macro Атрибуты_Физического_Лица( pStatus )
  var stat = TRUE;
  var sIsMale;
  var sIDCode;
  var sCountry;
  var sCountry1;
  var sCountry2;
  var vAddrType = 1;
  var sPostIndex  = "";
  var sRegion     = "";
  var sRegionName = "";
  var sProvince   = "";
  var sDistrict   = "";
  var sPlaceType  = "";
  var sPlace      = "";
  var sStreet     = "";
  var sHouse      = "";
  var sFlat       = "";
  var sCorpus     = "", iCorpus = 0;
  var sAddressNR  = "";
  var vObjStr = "";
  var sINNcitizen = "";
  var vStatNalog  = -1;

  COMCLNT.GetRecord( COMCLNT.CurRec.rec.CodClient );

  vObjStr = SubStr( String( 10000000000 + COMCLNT.CurRec.rec.CodClient ), 2 );

  // Резидентность клиента.
  if ( pStatus == 1 )
    if ( StrLen( COMCLNT.CurRec.rec.NRCountry ) != 0 )
      sCountry1 = COMCLNT.CurRec.rec.NRCountry;
    else
      sCountry1 = "RUS";
    end;
  else
    sCountry1 = COMCLNT.CurRec.rec.NRCountry;
  end;

  // Пол клиента.
  if ( COMCLNT.CurRec.rec.IsMale != StrFor(0) )
    sIsMale = "1";
  else
    sIsMale = "2";
  end;

  // Код удостоверения личности.
  if ( GetPaprKind( COMCLNT.CurRec.rec.PaperKind ) )
    // IDType = PAPRKIND.Name;
    sIDCode = PAPRKIND.CodeDocum;
  else
    sIDCode = COMCLNT.CurRec.rec.PaperKind;
  end;

  // Построение формата гражданства.
  ClearRecord( cntr );
  cntr.CodeLat3 = sCountry1;
  if ( getGE( cntr )  AND
       ( cntr.CodeLat3 == sCountry1 ) )
    sCountry = cntr.CodeNum3;
    // sCountry = cntr.Name + " (" + cntr.CodeNum3 + ")";
  else
    sCountry = "643";  // sCountry1;
  end;
 
  // Заполнение полей адреса.
  if ( pStatus == 1 )  // Резидент.
    vAddrType = 1;
  else                 // Нерезидент.
    vAddrType = 2;
  end;

  while ( ( vAddrType < 4 )  AND
          ( StrLen( sPostIndex ) == 0 )  AND
          ( StrLen( sRegion    ) == 0 )  AND
          ( StrLen( sPlaceType ) == 0 )  AND
          ( StrLen( sPlace     ) == 0 )  AND
          ( StrLen( sProvince  ) == 0 )  AND
          ( StrLen( sDistrict  ) == 0 )  AND
          ( StrLen( sStreet    ) == 0 ) )

    COMCLNT.AdressType = vAddrType;

    if ( COMCLNT.AdressType == vAddrType )
      sPostIndex  = COMCLNT.CurRec.rec.PostIndex;
      sRegion     = getRegionNumber( COMCLNT.CurRec );
      sProvince   = COMCLNT.CurRec.rec.Province;
      sDistrict   = COMCLNT.CurRec.rec.District;
      sPlaceType = Тип_населенного_пункта();
      sPlace = TRIM( COMCLNT.CurRec.rec.Place );
      sStreet = Trim( COMCLNT.CurRec.rec.StreetName ) + " " + Тип_улицы();
      sHouse  = COMCLNT.CurRec.rec.House;
      sFlat   = COMCLNT.CurRec.rec.Flat;

      /* Обработка строки "Корпус" */
      iCorpus = Index( StrUpr( COMCLNT.CurRec.rec.NumCorp ), "КОРПУС" );
      if ( iCorpus > 0 )
        if ( StrLen(COMCLNT.CurRec.rec.NumCorp) > ( iCorpus + 5 ) )
          sCorpus = SubStr( COMCLNT.CurRec.rec.NumCorp, iCorpus + 6 );
        else
          sCorpus = "";
        end;
      else
        sCorpus = COMCLNT.CurRec.rec.NumCorp;
      end;
    end;

    vAddrType = vAddrType + 1;
  end;

  /* Чтение записи ИНН в стране гражданства */
  sINNcitizen = "";
  obj_code.Clear();
  obj_code.rec.ObjectType = 3;  
  obj_code.rec.ObjectID = COMCLNT.CurRec.rec.CodClient;
  obj_code.rec.CodeKind = 33;
  if ( obj_code.GetGE() )
    if ( ( obj_code.rec.ObjectType == 3 )  AND 
         ( obj_code.rec.ObjectID == COMCLNT.CurRec.rec.CodClient )  AND 
         ( obj_code.rec.CodeKind == 33 ) )
      sINNcitizen = obj_code.rec.Code;
    end;
    while ( obj_code.Next()  AND 
            ( obj_code.rec.ObjectType == 3 )  AND 
            ( obj_code.rec.ObjectID == COMCLNT.CurRec.rec.CodClient )  AND 
            ( obj_code.rec.CodeKind == 33 ) )
      sINNcitizen = obj_code.rec.Code;
    end;
  end;

  /* Чтение записи статуса налогоплательщика */
  vStatNalog = -1;
  obj_atcor.Clear();
  obj_atcor.rec.ObjectType = 3;  
  obj_atcor.rec.Object = vObjStr;
  obj_atcor.rec.GroupID = 42;
  if ( obj_atcor.GetGE() )
    if ( ( obj_atcor.rec.ObjectType == 3 )  AND
         ( obj_atcor.rec.Object == vObjStr ) AND
         ( obj_atcor.rec.GroupID == 42 ) )
      vStatNalog = obj_atcor.rec.AttrID;
    end;
    while ( obj_atcor.Next()  AND 
            ( obj_atcor.rec.ObjectType == 3 )  AND
            ( obj_atcor.rec.Object == vObjStr ) AND
            ( obj_atcor.rec.GroupID == 42 ) )
      vStatNalog = obj_atcor.rec.AttrID;
    end;
  end;

  if ( vStatNalog < 0 )
    if ( COMCLNT.CurRec.Rec.NotResident == StrFor( 0 ) )
      vStatNalog = 1;  // Резидент
    else                                         
      vStatNalog = 2;  // Нерезидент
    end;
  end;

  sXML = sXML + "<ПолучДох";

  if ( StrLen( COMCLNT.CurRec.rec.INN ) > 0 )
    sXML = sXML + " ИННФЛ=\"" + SubStr( COMCLNT.CurRec.rec.INN, 1, 12 ) + "\"";
  end;
  if ( ( addZero( sCountry, 3 ) != "643" )  AND  ( Strlen( sINNcitizen ) > 0 ) )
    sXML = sXML + " ИННИно=\"" + sINNcitizen + "\"";
  end;
  sXML = sXML + " Статус=\"" + String( vStatNalog ) + "\"";
  sXML = sXML + " ДатаРожд=\"" + addZero( COMCLNT.CurRec.rec.Born, 10 ) + "\"";
  sXML = sXML + " Гражд=\"" + addZero( sCountry, 3 ) +  "\">";

//-------------------------------------------

  sXML = sXML + "<ФИО";
  sXML = sXML + " Фамилия=\""  + COMCLNT.CurRec.rec.Name1 + "\"";
  sXML = sXML + " Имя=\""      + COMCLNT.CurRec.rec.Name2 + "\"";
  if ( StrLen( COMCLNT.CurRec.rec.Name3 ) > 0 )
    sXML = sXML + " Отчество=\"" + COMCLNT.CurRec.rec.Name3 + "\"";
  end;
  sXML = sXML + "></ФИО>";

//-------------------------------------------

  sXML = sXML + "<УдЛичнФЛ";
  sXML = sXML + " КодУдЛичн=\"" + addZero( sIDCode, 2 ) + "\"";
  sXML = sXML + " СерНомДок=\"" + 
                SubStr( make_Paper_Series_Number( COMCLNT.CurRec.rec.PaperKind, COMCLNT.CurRec.rec.PaperSeries, COMCLNT.CurRec.rec.PaperNumber ), 1, 25 ) + 
                "\"></УдЛичнФЛ>";

//-------------------------------------------

  sXML = sXML + "</ПолучДох>";

  return stat;
end;

/***************************************************************************
***************************************************************************/
macro Общие_Суммы()
  var stat = TRUE;
  var vCommonTaxPayGet = $0.0;
  var vCommonTaxPayPut = $0.0;
  var vDuty            = $0.0;
  var vDutyPayer       = $0.0;
  var vDutyInspection  = $0.0;
  var vZero            = $0.0;

  if ( mPcRate.rec.CommonTaxPay > $0.0 )
    vCommonTaxPayGet = mPcRate.rec.CommonTaxPay;
    vCommonTaxPayPut = $0.0;
  else
    vCommonTaxPayGet = $0.0;
    vCommonTaxPayPut = - mPcRate.rec.CommonTaxPay;
  end;

  // Расчет задолженностей.
  vDuty = mPcRate.rec.CommonTaxCalc - mPcRate.rec.CommonTaxPay;
  if ( vDuty > $0.0 )
    vDutyPayer      = vDuty;
    vDutyInspection = $0.0;
  else
    vDutyPayer      = $0.0;
    vDutyInspection = -vDuty;
  end;

  sXML = sXML + "<СумИтНалПер";

  sXML = sXML + " СумДохОбщ=\""   + Trim( String( mPcRate.rec.CommonIncome:17:2 ) ) +
         "\"";
  sXML = sXML + " НалБаза=\""     + Trim( String( mPcRate.rec.CommonIncome:17:2 ) ) +
         "\"";
  sXML = sXML + " НалИсчисл=\""   + Trim( String( (RoundToRuble(mPcRate.rec.CommonTaxCalc)):15:0 ) ) +
         "\"";
  sXML = sXML + " АвансПлатФикс=\""   + Trim( String(vZero:15:0) ) +
         "\"";
  sXML = sXML + " НалУдерж=\""    + Trim( String( (RoundToRuble(vCommonTaxPayGet)):15:0 ) ) +
         "\"";
  sXML = sXML + " НалПеречисл=\""    + Trim( String( (RoundToRuble(vCommonTaxPayGet)):15:0 ) ) +
         "\"";
  sXML = sXML + " НалУдержЛиш=\"" + Trim( String( (RoundToRuble(vDutyInspection)):15:0 ) ) +
         "\"";
  sXML = sXML + " НалНеУдерж=\""  + Trim( String( (RoundToRuble(vDutyPayer)):15:0 ) ) +
         "\"/>";

  return stat;
end;

/***************************************************************************
***************************************************************************/
macro Цикл_по_Доходам( pStatus )
  var stat = TRUE;
  sXML = sXML + "<СведДох";
  sXML = sXML + " Ставка=\"" + String( Round( Money( mPcRate.rec.TaxRate ) ):0:0 ) + "\">";

  sXML = sXML + "<ДохВыч>";

  mPcTax.Clear();
  mPcTax.rec.TaxPayerStatus = pStatus;
  mPcTax.rec.TaxRate = mPcRate.rec.TaxRate;
  stat = mPcTax.GetGE();

  while ( stat  AND  
          ( mPcTax.rec.TaxPayerStatus == pStatus )  AND
          ( mPcTax.rec.TaxRate == mPcRate.rec.TaxRate ) )

    if ( mPcTax.rec.IncomeRUR != 0.0 )

      sXML = sXML + "<СвСумДох";

      sXML = sXML + " Месяц=\""    + addZero( mPcTax.rec.Mon, 2 ) + "\"";
      sXML = sXML + " КодДоход=\"" + addZero( mPcTax.rec.CodIncome, 4 ) + "\"";
      sXML = sXML + " СумДоход=\"" + Trim( String( mPcTax.rec.IncomeRUR:20:2 ) ) +
             "\"></СвСумДох>";

    end;

    stat = mPcTax.Next();
  end;

  sXML = sXML + "</ДохВыч>";

  Общие_Суммы();

  sXML = sXML + "</СведДох>";
  return stat;
end;

/***************************************************************************
***************************************************************************/
macro Информационная_Часть( pStatus )
  var vSeqVal = 1, numSeq = 1;
  var stat = TRUE;
  var vLenCorr = StrLen( InputPanel_Correct );
  var sPanCorr = "";

  if ( vLenCorr == 2 )
    sPanCorr = InputPanel_Correct;
  else
    if ( vLenCorr < 2 )
      sPanCorr = "0" + InputPanel_Correct;
    else
      sPanCorr = SubStr( InputPanel_Correct, vLenCorr-1 );
    end;
  end;

  sXML = sXML + "<Документ";

  if ( getCountUseSeq( 184, vSeqVal ) )  // Номер из последовательности.
    numSeq = vSeqVal;
  end;

  sXML = sXML + " КНД=\"1151078\"";
  sXML = sXML + " ДатаДок=\"" + sDateDoc + "\"";
  sXML = sXML + " НомСпр=\"" + Trim( String( numSeq ) ) + "\"";
  sXML = sXML + " ОтчетГод=\"" + InputPanel_Year_InAll + "\"";
  sXML = sXML + " Признак=\"1\"";
  sXML = sXML + " НомКорр=\"" + sPanCorr + "\"";
  sXML = sXML + " КодНО=\"" + Trim( CodeTaxAgency ) + "\">";

//------------------------------------------------

  sXML = sXML + "<СвНА";
  sXML = sXML + " ОКТМО=\"" + Trim( Normalize(CodeOKATO) ) + "\"";
  if ( StrLen( Trim( Bank_Phone ) ) > 0 )
    sXML = sXML + " Тлф=\"" + Trim( Bank_Phone ) + "\">";
  else
    sXML = sXML + ">";
  end;
  sXML = sXML + "<СвНАЮЛ";
  sXML = sXML + " НаимОрг=\"" + Name_Bank + " " + Name_Depart + "\"";
  sXML = sXML + " ИННЮЛ=\"" + vINN + "\"";
  sXML = sXML + " КПП=\"" + vKPP + "\"></СвНАЮЛ>";

  sXML = sXML + "</СвНА>";

  if ( stat )
    stat = Атрибуты_Физического_Лица( pStatus );
  end;

  return stat;
end;

/***************************************************************************
***************************************************************************/
macro Содержание_XML_файла( pStatus )
  var stat = TRUE;
  var st   = TRUE;
  stat = XML_Заголовок();
  if ( stat )
    stat = Служебная_Часть();
  end;
  if ( stat )
    stat = Информационная_Часть( pStatus );
  end;

  mPcRate.Clear();
  mPcRate.rec.Zero = 0;
  mPcRate.rec.TaxPayerStatus = pStatus;

  st = mPcRate.GetGE();

  while ( st  AND  
          ( mPcRate.rec.Zero == 0 )  AND
          ( mPcRate.rec.TaxPayerStatus == pStatus ) )

    if ( mPcRate.rec.TaxRate != 0.0 )
      Цикл_по_Доходам( pStatus );
    end;

    st = mPcRate.Next();
  end;

  // if ( stat )
    sXML = sXML + "</Документ>";
    sXML = sXML + "</Файл>";
    stat = Insert( TAXFILE, ToANSI( sXML ) );
  // end;
  return stat;
end;

/***************************************************************************
***************************************************************************/
macro Открытие_файла()
  var stat = TRUE;
  var cGUID = CreateGUID();
  var TaxFileDir;
  var db, mb, yb;

  DateSplit( {CurDate}, db, mb, yb );
  sGUID = SubStr( cGUID, 2, StrLen(cGUID)-2 );
  vNameFile = "NO_NDFL2_" + 
              Trim( CodeTaxAgency ) + "_" +
              Trim( CodeTaxAgency ) + "_" +
              vINN + vKPP + "_" +
              addZero( yb, 4 ) + addZero( mb, 2 ) + addZero( db, 2 ) + "_" +
              sGUID;
  if ( stat  AND  ( NOT OpenExportFile ) )
    TaxFileDir =
      GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\TAXATION_EXPORT", TRUE );
    dNameTaxFile = TaxFileDir + vNameFile + ".xml";
    aNameTaxFile = dNameTaxFile;
    stat = Open( TAXFILE, aNameTaxFile, ENCODING );
    if (NOT stat)
      [ Ошибка при открытии файла # ]( dNameTaxFile );
    else
      OpenExportFile = TRUE;
    end;
  end;

  return stat;
end;

/***************************************************************************
***************************************************************************/
macro Закрытие_Файла()
  if ( OpenExportFile )
    Close( TAXFILE );
    println( " XML-файл \"" + dNameTaxFile + "\" сформирован.\n" );
    OpenExportFile = FALSE;
  end;
  return TRUE;
end;

/***************************************************************************
***************************************************************************/
macro Формирование_XML_файла( pStatus )
  var stat = Открытие_файла();
  if ( stat )
    stat = Содержание_XML_файла( pStatus );
  end;
  if ( stat )
    stat = Закрытие_Файла();
  end;
  return stat;
end;

/***********************************************************
***********************************************************/
macro XML_файлы_по_клиенту()
  var vFindTax = FALSE;
  var vFindSum = FALSE;
  var vFormXML = FALSE;
  var vStatus = 1;
  var stat = True;
  var st;

  mPcRate.KeyNum = 1;

  while ( vStatus < 4 )

    vFindTax = FALSE;
    vFindSum = FALSE;

    mPcRate.Clear();
    mPcRate.rec.Zero = 0;
    mPcRate.rec.TaxPayerStatus = vStatus;

    st = mPcRate.GetGE();

    while ( st  AND  
            ( mPcRate.rec.Zero == 0 )  AND
            ( mPcRate.rec.TaxPayerStatus == vStatus ) )

      vFindTax = FALSE;
      vFindSum = FALSE;

      mPcTax.Clear();
      mPcTax.rec.TaxPayerStatus = vStatus;
      mPcTax.rec.TaxRate = mPcRate.rec.TaxRate;

      stat = mPcTax.GetGE();

      while ( stat  AND
              ( mPcTax.rec.TaxPayerStatus == vStatus )  AND
              ( mPcTax.rec.TaxRate == mPcRate.rec.TaxRate ) )
        vFindTax = TRUE;
        if ( mPcTax.rec.IncomeRUR != 0.0 )
          vFindSum = TRUE;
        end;
        stat = mPcTax.Next();
      end;

      st = mPcRate.Next();

    end;

    if ( vFindSum )
      Формирование_XML_файла( vStatus );
      vFormXML = TRUE;
    else
      if ( vFindTax )
        PrintLn( " " );
        PrintLn( "*** Ошибка в исходных данных по налогам." );
        COMCLNT.GetRecord( COMCLNT.CurRec.rec.CodClient );
        PrintLn( "*** Клиент " + COMCLNT.CurRec.rec.Name1 + " " +
                                 COMCLNT.CurRec.rec.Name2 + " " +
                                 COMCLNT.CurRec.rec.Name3 );
        PrintLn( "*** Ставка = " + String( Round( Money( mPcRate.rec.TaxRate ) ):0:0 ) + " %" );
        if ( ( mPcRate.rec.CommonTaxCalc != 0.0 )  OR
             ( mPcRate.rec.CommonTaxPay != 0.0 ) )
          PrintLn( "*** Несоответствие данных: суммы доходов нулевые, а суммы налога не нулевые" );
        else
          PrintLn( "*** Справка не выпущена - суммы нулевые" );
        end;
        PrintLn( " " );
      end;
    end;

    vStatus = vStatus + 1;

  end;

  if ( NOT vFormXML )
    PrintLn( "Клиент: " + COMCLNT.CurRec.rec.Name1 + " " +
                          COMCLNT.CurRec.rec.Name2 + " " +
                          COMCLNT.CurRec.rec.Name3 + "\n" +
             "Данных по 2-НДФЛ за указанный период нет.\n" );
  end;

  stat = TRUE;

  if ( NOT stat )
    ErrCode = Status( ErrText );
    if ( ( ErrCode == 0 )  OR  ( ErrCode == 4 )  OR  ( ErrCode == 9 ) )
     stat = TRUE;
    end;
  end;

  return stat;
end;

/***************************************************************************
***************************************************************************/
macro Выпуск_налоговой_карточки()
  var stat = ERR_OK;
  var st;
  var NumCycle = 0;
  var vChanel;

  sDateDoc = addZero( {CurDate}, 10 );

  if ( InputPanel_IdentClient != 0 )
    // По одному клиенту.
    while ( ( stat == ERR_OK )  OR  ( stat == ERR_SKIP ) )
      NumCycle = NumCycle + 1;
      stat = Выбрать_Данные_По_Очередному_клиенту( NumCycle );
      if ( stat == ERR_OK )
        XML_файлы_по_клиенту();
      end;
    end;
  else
    // По всем клиентам.
    while ( ( stat == ERR_OK )  OR  ( stat == ERR_SKIP ) )
      NumCycle = NumCycle + 1;
      stat = Выбрать_Данные_По_Очередному_клиенту( NumCycle );
      if ( stat == ERR_OK )
        XML_файлы_по_клиенту();
      end;
    end;
    //stat = ERR_STOP;
  end;

  if ( stat == ERR_REPEAT )
    st = False; /* Требуется повторить ввод данных */
  else
    st = True;
  end;
  return st;
end;

/***************************************************************************
                            Головная процедура.
***************************************************************************/
macro rpc_MakeXML_FileTaxCard
  var stat     = False;
  var NumCycle = 1;

  /* Чтение записи Операциониста */
  opr.Clear();
  opr.rec.Oper = {Oper};
  if ( opr.GetEQ() )
    sOper = opr.rec.Name;
  else
    sOper = "???";
  end;

  while (NOT stat)
    if ( Ввод_исходных_данных( "Форм-ие XML-файлов справки о доходах" ) )
      Bank_Attr_NDFL();

      iKPP = Index( Bank_INN, "/" );

      if ( iKPP == 0 )
        iKPP = Index( Bank_INN, "\\" );
      end;

      if ( iKPP == 0 )
        vINN = Bank_INN;
        vKPP = "";
      else
        vINN = SubStr( SubStr( Bank_INN, 1, iKPP-1 ), 1, 10 );
        vKPP = SubStr( SubStr( Bank_INN, iKPP+1 ), 1, 9 );
      end;

      stat = Выпуск_налоговой_карточки();
    else
      [ Отказались от Формирования файла для налоговых органов ];
      stat = True;
    end;
    NumCycle = NumCycle + 1;
  end;
end;

/***************************************************************************
                         Вызов головной процедуры.
***************************************************************************/
rpc_MakeXML_FileTaxCard();
