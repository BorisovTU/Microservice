/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Плановые перечисления                                                   *
*                                                                          *
*  Выполнение процедуры для переменной суммы "Зачисления"                  *
*                                                                          *
\**************************************************************************/

import DeprIntr, OperCode, rsd;

record pp( "dplanpay.dbt" ) key 2;  /* Договор планового платежа */

const FLAG_PLAN_PAY = 29;  /* Смещение битового флага для планового платежа */

CONST
  ПЕНСИЯ             =  2,
  ПЕНСИЯ_МО          =  5,
  ПЕНСИЯ_МВД         =  6,
  ПЕНСИЯ_ФСБ         =  7,
  ПЕНСИЯ_ФСНП        =  8,
  ПЕНСИЯ_ПРОКУРАТУРЫ =  9;

private var {curdate};

macro Var_Proc( addr )

  var cmd, rs;
  var Sum = $0, InSum = $0;
  var VidDoc, Flags, ListTransfer, TypeOper, ApplType;
  var stat, ret = TRUE;

  SetBuff( pp, addr );

  cmd = RsdCommand("SELECT * FROM dsbdepdoc_dbt " +
                   " WHERE t_Referenc = ? AND t_Date_Document >= ? AND t_Date_Document <= ? " +
                   " AND t_Action <> 2 AND t_Action <> 11 AND t_FlagStorn = chr(0) " +
                   " AND t_KindOp <> 8 AND t_KindOp <> 9 " +
                   " AND t_KindOp <> 14 AND t_KindOp <> 15 AND t_KindOp <> 16 " +
                   " ORDER BY t_Date_Document DESC, t_NumDayDoc DESC");

  cmd.addParam("Referenc", RsdBp_in, pp.Reference);
  cmd.addParam("Date_From", RsdBp_in, pp.Start_DatePay);
  cmd.addParam("Date_To", RsdBp_in, {curdate});
  cmd.execute();
  rs = RsdRecordset(cmd);

  while ( ret AND rs.moveNext() )
    VidDoc = rs.value("t_VidDoc"); 
    InSum = rs.value("t_InSum");
    Flags = rs.value("t_Flags");
    ListTransfer = rs.value("t_ListTransfer");
    TypeOper = rs.value("t_TypeOper");
    ApplType = rs.value("t_ApplType");

    /* Документ безналичного зачисления пенсии */
    if ( ( VidDoc != StrFor(0) )  AND  /* Безналичная операция */
         ( InSum  != 0 )          AND  /* Приходная операция */
         ( ( TypeOper == Открытие_Безналичными   )    OR
           ( TypeOper == Открытие_Переводом      )    OR
           ( TypeOper == Списание_Переводом      )    OR
           ( TypeOper == Зачисление_По_Данным_ПК )    OR
           ( TypeOper == Зачисление_В_Пак_Режиме )    OR
           ( TypeOper == Зачисление_Компенсации  ) )  AND
         ( ( ApplType != ПЕНСИЯ      )  AND
           ( ApplType != ПЕНСИЯ_МО   )  AND
           ( ApplType != ПЕНСИЯ_МВД  )  AND
           ( ApplType != ПЕНСИЯ_ФСБ  )  AND
           ( ApplType != ПЕНСИЯ_ФСНП )  AND
           ( ApplType != ПЕНСИЯ_ПРОКУРАТУРЫ ) ) )
      Sum = Sum + InSum;
    end;

    /* Документ предыдущей проводки по данному плановому платежу */
    if ( ( GetBitFlag( Flags, FLAG_PLAN_PAY ) == 1 )   AND
         ( ListTransfer == pp.Ref ) )
      ret = FALSE;  /* Поиск документов закончить */
    end;
  end;

  return Sum;

end;
