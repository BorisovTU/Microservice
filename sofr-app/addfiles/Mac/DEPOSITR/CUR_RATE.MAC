/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Определение курса валюты за число                                       *
*                                                                          *
*  Лебедев С.В.                                                            *
*  24.03.1999                                                              *
\**************************************************************************/

import office, DeprIntr, BankInter, rsd;

file CurrRate   ( cur_rate, "exchange.def" ) key 0;
file RtlCurr    ( currency )                 key 1;

private const MainRateKind = 1;
private const V_SPECVAL = 26;

private var rtexchComplexRate = false;


/**************************************************************************\
|          Определение курса валюты из смены данного вида за дату          |
\**************************************************************************/
private macro Get_CurrRate( CodeCurrency, RateChangeID, RateKind, MinSum )

  var stat = false;
  var CBRate = 0;

  if ( MinSum == null )
    MinSum = $0L;
  end;

  if ( not rtexchComplexRate )
    MinSum = $0L;
  end;

  if ( RateKind!=MainRateKind )
     if ( Get_CurrRate(CodeCurrency, RateChangeID, MainRateKind, MinSum) )
        CBRate = CurrRate.CBRate;
     end;     
  end;

  ClearRecord( CurrRate );
  Rewind( CurrRate );
  CurrRate.Change_Ref = RateChangeID;
  CurrRate.Curr_Ref   = CodeCurrency;
  CurrRate.Kind_Ref   = RateKind;
  CurrRate.MinSum     = MinSum;


  if ( MinSum == 0 )
    stat = GetEQ( CurrRate );
  else
    if ( GetLE( CurrRate ) and ( CurrRate.Change_Ref == RateChangeID ) and
                               ( CurrRate.Curr_Ref   == CodeCurrency ) and
                               ( CurrRate.Kind_Ref   == RateKind ) )
      stat = true;
    end;
  end;

  if ( stat and (RateKind!=MainRateKind) )
     CurrRate.CBRate = CBRate;
  end;

  return stat;

end;

private macro GetRateChangeID( RateDate, RateTime )
  var cmd = RsdCommand( "select t_id from dratechng_dbt where rownum=1 and t_branch = ? and (t_startDate < ? or (t_startDate = ? and t_startTime <= ?)) order by t_startDate desc, t_startTime desc" );
  cmd.addParam("branch", RsdBp_In, Office.BranchForRate);
  cmd.addParam("startDate1", RsdBp_In, RateDate);
  cmd.addParam("startDate2", RsdBp_In, RateDate);
  cmd.addParam("startTime", RsdBp_In, RateTime);
  cmd.execute;

  var rs = RsdRecordset( cmd );
  if ( rs.moveNext )    
    var v = rs.value( 0 );
    return int(v);
  end;
 
  return null;
end;


/**************************************************************************\
|                      Получить курс валюты за число                       |
\**************************************************************************/
macro GetCurrRate ( Dd, RateTime )

  var Rate         = 0.0;
  var ScaleRate    = 1;
  var tm           = RateTime;

  if ( (RateTime == null) or (RateTime == Time(0,0,0)) ) 
    tm = Time( 23, 59, 59 ); 
  end;

  var RateChangeID = GetRateChangeID( Dd, tm );

  if ( RateChangeID != null )
    if ( Get_CurrRate( RtlCurr.Code_Currency, RateChangeID, MainRateKind ) )
      if ( CurrRate.ChangeNom != 0 )
        Rate = CurrRate.CBRate / CurrRate.ChangeNom;
      end;
      if ( CurrRate.OutRate and Rate )
        Rate = 1 / Rate;
      end;
    end;
  end;

  if ( ( Rate == 0 ) and RtlCurr.CrDifrRate )
    MsgBox( "Не задан курс валюты '", RtlCurr.Name_Currency, "' за дату ", dd );
    Exit( 1 );
  end;

  return Rate;

end;


/**************************************************************************\
|                      Получить курс валюты за число                       |
\**************************************************************************/
macro GetAllCurrRateNoScaling ( DateRate, CodeCurr, Scale, CBRate, BuyRate, SellRate, RateTime, MinSum )

  var stat      = true;
  var flag;
  var CBRate_ = 0.0, BuyRate_ = 0.0, SellRate_ = 0.0, Scale_ = 1;
  var tm = RateTime;
  var RateChangeID;

  if ( (RateTime == null) or (RateTime == Time(0,0,0)) ) 
    tm = Time( 23, 59, 59 ); 
  end;

  if ( MinSum == null )
    MinSum = $0L;
  end;

  if ( not rtexchComplexRate )
    MinSum = $0L;
  end;

  if ( stat )
    KeyNum( RtlCurr, 0 );
    RtlCurr.Code_Currency = CodeCurr;
    stat = GetEQ( RtlCurr );
  end;

  if ( stat )
    RateChangeID = GetRateChangeID( DateRate, tm );
    if ( RateChangeID == null ) 
      stat = false;
    end;
  end;

  if ( stat )
    stat = Get_CurrRate( CodeCurr, RateChangeID, MainRateKind, MinSum );
    if ( stat and ( CurrRate.MinSum != 0 ) )
      BuyRate_ = CurrRate.BuyRate;
      SellRate_ = CurrRate.SellRate;

      stat = Get_CurrRate( CodeCurr, RateChangeID, MainRateKind );

      if ( stat )
        CurrRate.BuyRate = BuyRate_;
        CurrRate.SellRate = SellRate_;
      end;

      BuyRate_ = 0.0;
      SellRate_ = 0.0;
    end;
  end;

  CBRate_   = CurrRate.CBRate;
  BuyRate_  = CurrRate.BuyRate;
  SellRate_ = CurrRate.SellRate;

  if ( stat and ( CurrRate.ChangeNom != 0 ) )
    Scale_ = CurrRate.ChangeNom;
  end; 

  if ( stat and CurrRate.OutRate )
    if ( CBRate_   ) CBRate_   = 1 / CBRate_;   end;
    if ( BuyRate_  ) BuyRate_  = 1 / BuyRate_;  end;
    if ( SellRate_ ) SellRate_ = 1 / SellRate_; end;
    Scale_ = 1 / Scale_;
  end;
 
/*  if (stat)*/
  SetParm( 2, Scale_ );
  SetParm( 3, CBRate_   );
  SetParm( 4, BuyRate_  );
  SetParm( 5, SellRate_ );
/*  end;*/

  return stat;
      
end;

macro GetAllCurrRate( DateRate, CodeCurr, CBRate, BuyRate, SellRate, RateTime, MinSum )

  var Scale, stat;

  stat = GetAllCurrRateNoScaling ( DateRate, CodeCurr, Scale, CBRate, BuyRate, SellRate, RateTime, MinSum );

  if ( stat and ( Scale != 1 ) and ( Scale != 0 ) and ( Scale != null ) )
    CBRate = CBRate / Scale;
    BuyRate = BuyRate / Scale;
    SellRate = SellRate / Scale;
  end;

  SetParm( 2, CBRate );
  SetParm( 3, BuyRate  );
  SetParm( 4, SellRate );

  return stat;
end;


/**************************************************************************\
|          Определение курса валюты из смены данного вида за дату          |
\**************************************************************************/
macro FindCurrRate ( CodeCurrency, RateKind, RateDate, CBRate, BuyRate, SellRate, RateTime, MinSum, RateChangeId )

  var stat         = true;
  var CBRate_ = 0.0, BuyRate_ = 0.0, SellRate_ = 0.0;
  var tm = RateTime;

  if ( (RateTime == null) or (RateTime == Time(0,0,0)) ) 
    tm = Time( 23, 59, 59 ); 
  end;

  if ( MinSum == null )
    MinSum = $0L;
  end;

  if ( not rtexchComplexRate )
    MinSum = $0L;
  end;

  RateChangeID = GetRateChangeID( RateDate, tm );
  if ( RateChangeID == null )
    stat = false;
  end;

  if ( stat )
    stat = Get_CurrRate( CodeCurrency, RateChangeID, RateKind, MinSum );
    if ( stat and ( CurrRate.MinSum != 0 ) )
      BuyRate_ = CurrRate.BuyRate;
      SellRate_ = CurrRate.SellRate;

      stat = Get_CurrRate( CodeCurrency, RateChangeID, RateKind );

      if ( stat )
        CurrRate.BuyRate = BuyRate_;
        CurrRate.SellRate = SellRate_;
      end;

      BuyRate_ = 0.0;
      SellRate_ = 0.0;
    end;
  end;

  if ( stat )
    if ( ( ( CurrRate.CBRate   == 0 ) or 
           ( CurrRate.BuyRate  == 0 ) or 
           ( CurrRate.SellRate == 0 )   ) and RtlCurr.CrDifrRate )
      MsgBox( "Не задан курс валюты '", RtlCurr.Name_Currency, "' за дату ", RateDate );
      Exit( 1 );
    end;

    if ( CurrRate.ChangeNom != 0 )
      CBRate_   = CurrRate.CBRate   / CurrRate.ChangeNom;
      BuyRate_  = CurrRate.BuyRate  / CurrRate.ChangeNom;
      SellRate_ = CurrRate.SellRate / CurrRate.ChangeNom;
    end;

    if ( CurrRate.OutRate )
      if ( CBRate_   )   CBRate_   = 1 / CBRate_;    end;
      if ( BuyRate_  )   BuyRate_  = 1 / BuyRate_;   end;
      if ( SellRate_ )   SellRate_ = 1 / SellRate_;  end;
    end;
    
    if ( CurrRate.ChangeNom != 0 )
      SetParm( 3, CBRate_   );
      SetParm( 4, BuyRate_  );
      SetParm( 5, SellRate_ );
      SetParm( 8, RateChangeId );
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro DefFlagComplexRate

  var regValue = false;
  var regError = 0;

  GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\КУРСЫ\\ИСПОЛЬЗ_СЛОЖНЫЕ_КУРСЫ",
                    V_BOOL, regValue, regError                           );

  if ( ( regError == 0 ) and regValue )
    rtexchComplexRate = true;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
DefFlagComplexRate( );
