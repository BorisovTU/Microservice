import functor;
import xbt;

//---------------------------------------------------------------------------
private macro execFunc( functor )
  return functor.exec();
end;
//---------------------------------------------------------------------------
macro execFuncXBT( functor )
  XBT(functor.exec());
end;

//---------------------------------------------------------------------------
/*!
 * Класс регистрирует функции и вызывает из разом
 * при исполнении exec()
 * atexit = AtExitCaller();
 * atexit.add("fn", 3,8,"");
 * atexit.add("fn2", obj,8,"");
 * atexit.exec(); // выполняет по очереди добавленные функции
 * atexit.del("fn2");
 * atexit.exec();
 */
class AtExitCaller( _execFunc )
  private var m_funcs = vector();
  private var m_execFunc = IIF(valType(_execFunc)==V_UNDEF, @execFunc, _execFunc);
  //---------------------------
  private macro getFnObj( fn )
    if( not isCallableType(fn) )
      RunError("Неверный тип функции");
    end;
    
    return fn;
  end;
  //--------------------------
  /*!
   * Добавляет функцию с параметрами
   * ex: atexit.add("fn", obj, 8, "");
   */
  macro add( fn )
    var i = 2, v;
    var parms = vector();
    while( getparm(i, v) )
      parms.push_back(v);
      inc(i);       
    end;
    m_funcs.push_back( Functor(fn, parms) );
  end;
  //--------------------------
  /*!
   * Удаляет ранее добавленную функцию
   */
  macro del( fn )
    var objFn = getFnObj(fn);
    var i = m_funcs.lsearch(Functor(objFn,null), @CompByFuncName);
    if( i < 0 )
      RunError("Функция не найдена");
    end;
    m_funcs.remove(i);
  end;
  macro clear()
    m_funcs.clear();
  end;
  //---------------------------
  /*!
   * Последовательно исполняет ранее добавленные функции
   */
  macro exec()
    m_funcs.for_each(m_execFunc);
  OnError(obj)
    clear();
    ReRunError(obj);
  end;
end;
//---------------------------------------------------------------------------

