/* Обороты по вкладным операциям */

import deprintr, OperCode, sqlconv;

const IsNeedDifferForFlagRez = TRUE; /* Различать ли отчет по резидентности */
array FlagRez;
  FlagRez(0) = "\x00"; /* Резидент */
  FlagRez(1) = "\x01"; /* Не резидент */

file DepType("sb_dtyp.dbt") key 2;
var DepStat = TBFile ("sb_depst.dbt","r",0);
var Curr = TBFile ("currency.dbt","r",0);

const
  SB_DEPOSIT = 1;

var
  {CNum},
  {curdate},
  Mode = GetCurrentMode,
  IsCur = NumFlagCur,
  Branch = NumFNCash;

var
  TotPrevRest,
  TotRest,
  TotStatRest,
  TotCredit,
  TotDebet,
  TotMemCredit,
  TotMemDebet;

var
  SubHeadDisplayed,
  StatRecFound;


/**************************************************************/
macro OutHead;

  [                            Обороты по вкладным операциям за ####################### ]
  ({curdate}:m);
  [ ];
end;


/**************************************************************/
macro OutSubHead;

  if(IsCur)
    [ ];
    [#](Curr.rec.Name_Currency);
    [#](MkStr("=", StrLen(Curr.rec.Name_Currency)));
    [ ];
  end;

  [--------------------------------------------------------------------------------------------------------------------------------];
  [|    Вид     |   Входящий    |        Н а л и ч н ы е        |     Б е з н а л и ч н ы е     | Исход. остаток | Исход. остаток |];
  [|  вклада    |   остаток     |    Приход     |     Расход    |    Приход     |     Расход    | по сводным док.|  по статистике |];
  [--------------------------------------------------------------------------------------------------------------------------------];
end;


/**************************************************************/
macro OutLine(AccType, PrevRest, Rest, StatRest, Debet, Credit, MemDebet, MemCredit, Rezid)

  if((PrevRest == $0)
    and (Rest == $0)
    and (StatRest == $0)
    and (Debet == $0)
    and (Credit == $0)
    and (MemDebet == $0)
    and (MemCredit == $0))
    return;
  end;

  if (IsNeedDifferForFlagRez AND (Rezid != ""))
    AccType = AccType + " (нерезидент)";
  end;

  if(not SubHeadDisplayed)
    OutSubHead;
    SubHeadDisplayed = true;
  end;

  [ ############ ############### ############### ############### ############### ############### ################ ################]
  (AccType:w, PrevRest, Credit, Debet, MemCredit, MemDebet, Rest, StatRest);
  TotRest = TotRest + Rest;
  TotStatRest = TotStatRest + StatRest;
  TotPrevRest = TotPrevRest + PrevRest;
  TotDebet = TotDebet + Debet;
  TotCredit = TotCredit + Credit;
  TotMemDebet = TotMemDebet + MemDebet;
  TotMemCredit = TotMemCredit + MemCredit;
end;

macro OutTotals

  [--------------------------------------------------------------------------------------------------------------------------------];
  [ ############ ############### ############### ############### ############### ############### ################ ################]
  ("И т о г о :", TotPrevRest, TotCredit, TotDebet, TotMemCredit, TotMemDebet, TotRest, TotStatRest);
end;


/**************************************************************/
macro GetRests(Rest, PrevRest, Rezid)

  var
    Rst = $0,
    PrRst = $0;
  var i = 0;

  StatRecFound = false;
  while (i < Asize(FlagRez))
    if ((NOT IsNeedDifferForFlagRez) OR (FlagRez(i) == Rezid))
      DepStat.Rewind;
      DepStat.Clear;
      DepStat.rec.FNCash = Branch;
      DepStat.rec.IsCur = IsCur;
      DepStat.rec.Kind = DepType.Kind;
      DepStat.rec.CodCur = Curr.rec.Code_Currency;
      DepStat.rec.FlagRez = FlagRez(i);
      DepStat.rec.Date = {curdate};
      DepStat.rec.Mode = Mode;
      if(DepStat.GetLE
        and (DepStat.rec.FNCash == Branch)
        and (DepStat.rec.IsCur == IsCur)
        and (DepStat.rec.Kind == DepType.Kind)
        and (DepStat.rec.FlagRez == FlagRez(i))
        and (DepStat.rec.CodCur == Curr.rec.Code_Currency))
        if( ( DepStat.rec.Date == {curdate} ) and ( DepStat.rec.Mode == Mode ) )
          StatRecFound = true;
          Rst = Rst + DepStat.rec.Rest;
          if(DepStat.Prev
          and (DepStat.rec.FNCash == Branch)
          and (DepStat.rec.IsCur == IsCur)
          and (DepStat.rec.Kind == DepType.Kind)
          and (DepStat.rec.FlagRez == FlagRez(i))
          and (DepStat.rec.CodCur == Curr.rec.Code_Currency))
            PrRst = PrRst + DepStat.rec.Rest;
          end;
        else
          Rst = Rst + DepStat.rec.Rest;
          PrRst = PrRst + DepStat.rec.Rest;
        end;
      end;
    end;
    i = i + 1;
  end;
  SetParm(0,Rst);
  SetParm(1,PrRst);
end;


/**************************************************************/
macro GetTurns(Debet, Credit, MemDebet, MemCredit, Rezid)

  var
    Deb = $0,
    Cred = $0,
    MemDeb = $0,
    MemCred = $0;
 
  SetParm(0,Deb);
  SetParm(1,Cred);
  SetParm(2,MemDeb);
  SetParm(3,MemCred);
end;


/**************************************************************/
macro ReportOnDepType

  var
    Rest      = $0,
    PrevRest  = $0,
    Debet     = $0,
    Credit    = $0,
    MemDebet  = $0,
    MemCredit = $0,
    statRest  = $0,
    ARez      =  0,
    i         =  0;

  if (IsNeedDifferForFlagRez)
    ARez = 2;
  else
    ARez = 1;
  end;

  while (i < ARez)
    if (IsNeedDifferForFlagRez)
      Rest = PrevRest = Debet = Credit = MemDebet = MemCredit = statRest = $0;
    end;
    GetRests(Rest, PrevRest, FlagRez(i));
    if(StatRecFound)
      GetTurns(Debet, Credit, MemDebet, MemCredit, FlagRez(i));
    end;
    /*VZ 19.08.98 Берем исходящие обороты не из статистики, а
         остаток_пред_дня + обороты_текущего_дня*/
    statRest = Rest;
    Rest = PrevRest-Debet-MemDebet+Credit+MemCredit;
    OutLine(DepType.Kind, PrevRest, Rest, statRest, Debet, Credit, MemDebet, MemCredit, FlagRez(i));
    /*
    if(Rest != statRest)
      [Внимание!!!! возможно расхождение с ф.25!!!!]
    end;
    */
    i = i + 1;
  end; /* while */
end;


/**************************************************************/
macro Report

  var
    RecFound;

  TotPrevRest = $0;
  TotRest = $0;
  TotStatRest = $0;
  TotCredit = $0;
  TotDebet = $0;
  TotMemCredit = $0;
  TotMemDebet = $0;

  var sStatus;
  var Records = 0;
  ReWind (DepType );
  ClearRecord(DepType);
  DepType.FlagCur = IsCur;
  RecFound = GetGE(DepType);
  while(RecFound and (DepType.FlagCur == IsCur))
    if (DepType.Kind != "Служебный")
        Records = Records + 1;
    end;
    RecFound = Next (DepType);
  end;             
  ReWind (DepType );

  sStatus = "Просмотр видов вкладов";
  if (IsCur==0)
    sStatus = sStatus + " в национальной валюте";
  else
    sStatus = sStatus + " в валюте: " + Curr.rec.Name_Currency;
  end;
  InitProgress( Records, sStatus, "Идет формирование отчета по оборотам..." );
  Records = 0;

  SubHeadDisplayed = false;
  ClearRecord(DepType);
  DepType.FlagCur = IsCur;
  RecFound = GetGE(DepType);
  while(RecFound and (DepType.FlagCur == IsCur))
    if(DepType.Kind != "Служебный")
      ReportOnDepType;
      Records = Records + 1;
      UseProgress( Records );
    end;
    RecFound = Next(DepType);
  end;
  RemProgress();
  if(SubHeadDisplayed)
    OutTotals;
  end;
end;


/**************************************************************/
  var
    RecFound;

  if ( not UpdateStatistics( true ) )
    Exit;
  end;

  Message(" Формирование отчета по оборотам ...");

  OutHead;
  if(not IsCur)
    Curr.rec.Code_Currency = 0;
    if(Curr.GetEQ)
      Report;
    end;
  else
    Curr.Rewind;
    Curr.Clear;
    Curr.rec.Code_Currency = 1;
    RecFound = Curr.GetGE;
    while(RecFound)
        Report;
      RecFound = Curr.Next;
    end;
  end;
end;
