/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Нефинансовые операции по счету                                          *
*                                                                          *
*  Лебедев С.В.                                                            *
*  17.09.2002                                                              *
\**************************************************************************/

import alg_vars, acc_form;

file ret_op   ( ret_op   ) key 3;
file sb_casln ( sb_casln ) key 0;
file pay_doc  ( pay_doc  ) key 2;
file pay_kind ( pay_kind ) key 0;
file sbdepdoc ( sbdepdoc ) key 3;
file sb_typop ( sb_typop ) key 0;
file suboper  ( suboper  ) key 1;
file currency ( currency ) key 0;
file rtnfopr  ( rtnfopr  ) key 1;
file dic      ( "dict.def", "def.def" ) key 1;

record ret_op_dep  ( "ret_op.dep" );
record dictfile    ( "dictfile.rec", "def.def" );
record sbtrast_rec ( sbtrast );
record depclnt_rec ( depclnt ); /* Остается и после перевода на Ед.клиента-
                                   запись хранится в журнале */

var  clnt = TClientList;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefNameCurr ( code_curr )

  var out_name = "";

  ClearRecord( currency );
  currency.Code_Currency = code_curr;
  if ( GetEQ( currency ) )
    out_name = currency.Short_Name;
  end;

  return out_name;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefNameOpert ( flag_cur, type_acc, num_opert )

  var out_name = String( num_opert );

  ClearRecord( sb_typop );
  sb_typop.IsCur    = flag_cur;
  sb_typop.Kind     = type_acc;
  sb_typop.NumOpert = num_opert;

  if ( GetEQ( sb_typop ) )
    out_name = sb_typop.czNameAlg;
  end;

  return out_name;

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefNameSubOpert ( flag_cur, type_acc, num_opert, num_appl )

  var out_name = String( num_appl );

  ClearRecord( suboper );
  suboper.IsCur    = flag_cur;
  suboper.Kind     = type_acc;
  suboper.OperType = num_opert;
  suboper.Type     = num_appl;

  if ( GetEQ( suboper ) )
    out_name = suboper.Name;
  end;

  return out_name;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefNameFile 

  var name_file = "";
  var stat;

  if ( Open( dic, "sbbank.def" ) )
    ClearRecord( dic );
    SetRecordAddr( dictfile, dic, 0, 0, TRUE );
    dic.Group = "f";
    stat = GetGE( dic );
    while ( stat AND ( dic.Group == "f" ) )
      if ( dictfile.Name == rtnfopr.tableName )
        name_file = dictfile.Contents;
        stat = FALSE;
      else
        stat = Next( dic );
      end;
    end;
  end;

  if ( name_file == "" )
    if ( Open( dic, "spcard.def" ) )
      ClearRecord( dic );
      SetRecordAddr( dictfile, dic, 0, 0, TRUE );
      dic.Group = "f";
      stat = GetGE( dic );
      while ( stat AND ( dic.Group == "f" ) )
        if ( dictfile.Name == rtnfopr.tableName )
          name_file = dictfile.Contents;
          stat = FALSE;
        else
          stat = Next( dic );
        end;
      end;
    end;
  end;

  return name_file;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro MakeFIO ( CodCl )

  var out_fio = "";

  if (clnt.GetRecord(CodCl))
    out_fio = clnt.CurRec.ConvertFIO ();
  end;

  return out_fio;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefDescrLog

  var out_descr = "";

  /*--------------------------------------------------------------------*/
  if ( ( rtnfopr.objectType == 262 ) )
    if ( rtnfopr.tableName == "depclnt.dbt" )
      SetRecordAddr( depclnt_rec, rtnfopr, 0, 2, false );
      out_descr = MakeFIO( depclnt_rec.CodClient );
    end;
  /*--------------------------------------------------------------------*/
  elif ( ( rtnfopr.objectType == 232 ) or
         ( rtnfopr.objectType == 235 ) or
         ( rtnfopr.objectType == 238 )   )
    if ( rtnfopr.tableName == "sbtrast.dbt" )
      SetRecordAddr( sbtrast_rec, rtnfopr, 0, 2, false );
        out_descr = MakeFIO( sbtrast_rec.CodClient );
        if ( ( sbtrast_rec.QPart_m != 0 ) and ( sbtrast_rec.QPart_n != 0 ) )
          out_descr = out_descr + " доля " + String( sbtrast_rec.QPart_m ) + "/" +
                                             String( sbtrast_rec.QPart_n );
        end;
    end;
  /*--------------------------------------------------------------------*/
  elif ( ( rtnfopr.objectType == 222 ) or
         ( rtnfopr.objectType == 224 ) or
         ( rtnfopr.objectType == 226 ) or
         ( rtnfopr.objectType == 228 ) or
         ( rtnfopr.objectType == 242 )   )
    if ( rtnfopr.tableName == "sbtrast.dbt" )
      SetRecordAddr( sbtrast_rec, rtnfopr, 0, 2, false );
        out_descr = MakeFIO( sbtrast_rec.CodClient );
        if ( sbtrast_rec.CloseDate != Date( 0, 0, 0 ) )
          out_descr = out_descr + " срок " + String( sbtrast_rec.CloseDate:m );
        end;
    end;
  end;

  return out_descr;

end; 


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetNameDoc ( name_doc, dscr_doc )

  var name_file;

  name_doc = "";
  dscr_doc = "";

  /*--------------------------------------------------------------------*/
  if ( sb_casln.ApplicationKind2 == 1 )
    ClearRecord( sbdepdoc );
    sbdepdoc.iApplicationKind = sb_casln.ApplicationKind2;
    sbdepdoc.ApplicationKey   = sb_casln.ApplicationKey2;
    if ( GetEQ ( sbdepdoc ) )
      name_doc = String( Acc_Form(sbdepdoc.Account):f ) + " " + DefNameOpert( sbdepdoc.IsCur, sbdepdoc.Type_Account, sbdepdoc.TypeOper );
      if ( sbdepdoc.ApplType > 0 )
        name_doc = name_doc + " / " + DefNameSubOpert( sbdepdoc.IsCur, sbdepdoc.Type_Account, sbdepdoc.TypeOper, sbdepdoc.ApplType );
      end;
      if ( sbdepdoc.InSum > 0 )
        dscr_doc = Trim( String( sbdepdoc.InSum:20:2:a ) + " " + DefNameCurr( sbdepdoc.Code_Currency ) );
      elif ( sbdepdoc.OutSum > 0 )
        dscr_doc = Trim( String( sbdepdoc.OutSum:20:2:a ) + " " + DefNameCurr( sbdepdoc.Code_Currency ) );
      end;
    end;
  /*--------------------------------------------------------------------*/
  elif ( sb_casln.ApplicationKind2 == 200 )
    ClearRecord( pay_doc );
    pay_doc.iApplicationKind = sb_casln.ApplicationKind2;
    pay_doc.ApplicationKey   = sb_casln.ApplicationKey2;
    if ( GetEQ ( pay_doc ) )
      ClearRecord( pay_kind );
      pay_kind.IsCur      = pay_doc.IsCur;
      pay_kind.GroupOpert = pay_doc.GroupOpert;
      pay_kind.NumOperat  = pay_doc.NumOpert;
      if ( GetEQ( pay_kind ) )
        name_doc = pay_kind.SzNameAlg;
      end;
      if ( pay_doc.InSum > 0 )
        dscr_doc = Trim( String( pay_doc.InSum:20:2:a ) + " " + DefNameCurr( pay_doc.CodCur ) );
      end;
    end;
  /*--------------------------------------------------------------------*/
  elif ( sb_casln.ApplicationKind2 == 3001 )
    name_doc = "Нефинансовая операция";
    name_file = "";
    if ( StrLen( sb_casln.ApplicationKey2 ) >= 20 )
      ClearRecord( rtnfopr );
      rtnfopr.numDprt = Int( SubStr( sb_casln.ApplicationKey2, 9, 3 ) );
      rtnfopr.recID   = Int( SubStr( sb_casln.ApplicationKey2, 12 ) );
      if ( GetEQ( rtnfopr ) )
        if ( ( rtnfopr.opCode == 2 ) or ( rtnfopr.opCode == 1004 ) )
          name_doc = "Вставка записи";
        elif ( ( rtnfopr.opCode == 3 ) or ( rtnfopr.opCode == 1005 ) )
          name_doc = "Обновление записи";
        elif ( ( rtnfopr.opCode == 4 ) or ( rtnfopr.opCode == 1006 ) )
          name_doc = "Удаление записи";
        else
          name_doc = "Операция " + String( rtnfopr.opCode );
        end;
        name_file = DefNameFile( );
        if ( StrLen( name_file ) > 0 )
          name_doc = name_doc + " \"" + name_file + "\" (" + rtnfopr.tableName + ")";
        else
          name_doc = name_doc + " " + rtnfopr.tableName;
        end;
        dscr_doc = DefDescrLog( );
      end;
    end;
  end;

  SetParm( 0, name_doc );
  SetParm( 1, dscr_doc );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  var name_opert;
  var name_doc;
  var dscr_doc;
  var date_opert;
  var oper_opert;
  var count_doc;
  var stat;
  var stat_ln;

  [ ];
  [ Нефинансовые операции по счету #]( Acc_Form(ALG_DEPOSITOR.Account):f );
  [ ];
  [ ======================================================================================];
  [  Название операции  |         Документы         |    Расшифровка    |   Дата   | Опер ];
  [ ======================================================================================];

  ClearRecord( ret_op );
  ret_op.Branch          = ALG_DEPOSITOR.FNCash;
  ret_op.IsCur           = ALG_DEPOSITOR.IsCur;
  ret_op.ApplicationKind = 3001;
  ret_op.Date            = ALG_DEPOSITOR.Open_Date;
  ret_op.State           = 0;

  stat = GetGE( ret_op );
  while ( stat )
    if ( ( ret_op.Branch          == ALG_DEPOSITOR.FNCash ) AND
         ( ret_op.IsCur           == ALG_DEPOSITOR.IsCur  ) AND
         ( ret_op.ApplicationKind == 3001                 )    )
      if ( ( ret_op.State == 51 ) OR ( ret_op.State == 52 ) )
        ;
      else
        SetRecordAddr( ret_op_dep, ret_op, 0, FldOffset( ret_op, "Extra" ), true );
        if ( ret_op_dep.accRef == ALG_DEPOSITOR.Referenc )
          count_doc  = 0;
          name_opert = DefNameOpert( ret_op.IsCur, "Шаблонный", ret_op.Operation );
          name_doc   = "";
          dscr_doc   = "";
          date_opert = String( ret_op.Date:f );
          oper_opert = String( ret_op.Oper );
          
          ClearRecord( sb_casln );
          sb_casln.ApplicationKind1 = ret_op.ApplicationKind;
          sb_casln.ApplicationKey1  = ret_op.ApplicationKey;
          sb_casln.NumLinkDoc       = 0;
          stat_ln = GetGE( sb_casln );
          while ( stat_ln )
            if ( ( sb_casln.ApplicationKind1 == ret_op.ApplicationKind ) AND
                 ( sb_casln.ApplicationKey1  == ret_op.ApplicationKey  )    )
              if ( sb_casln.RefValue2 == 0 )
                count_doc = count_doc + 1;
                if ( count_doc > 1 )
                  name_opert = "";
                  date_opert = "";
                  oper_opert = "";
                  [                     |-----------------------------------------------|          |     ];
                end;
                GetNameDoc( name_doc, dscr_doc );
                [ ####################|###########################|###################|##########| ####]
                ( name_opert, name_doc:w, dscr_doc:w, date_opert, oper_opert );
              end;
              stat_ln = Next( sb_casln );
            else
              stat_ln = FALSE;
            end;
          end;

          if ( count_doc == 0 )
            [ ####################|###########################|###################|##########| ####]
            ( name_opert, name_doc:w, dscr_doc:w, date_opert, oper_opert );
          end;

          [ --------------------------------------------------------------------------------------];
        end;
      end;
      stat = Next( ret_op );
    else
      stat = FALSE;
    end;
  end;

end;
