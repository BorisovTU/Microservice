/***************************************************************************

  RS-Bank ( Сбербанк )
  Обслуживание безналичных платежей
  Макрос загрузки информации о безналичных платежах
  из текстового файла в файлы расшифровок по филиалалам и
  документов вкладчиков по безналичным платежам
  в модуле "Филиал".


  ВНИМАНИЕ !  Предполагается, что номера счетов вкладчиков задаются
              ТОЛЬКО в "новой" 20-ти значной нумерации, введенной
              по плану счетов с сентября 1998 года.


  1. Структура исходного текстового файла
  ---------------------------------------

  1.0. Каждая запись в исходном файле занимает отдельную строку.
       Начальные пробелы в поле игнорируются, что позволяет струтурировать
       записи в файле либо по филиалам, либо по типам записей, а внутри
       последовательности записей одного типа - струкутрировать по полям.

  1.1. Первой записью текстового файла должна быть запись о платежном
       поручении. Такая запись должна быть одна на файл. Таким образом, в
       одном файле вводятся записи по ОДНОМУ платежному поручению.

  1.2. Затем должны следовать записи сумм по филиалам и по документам
       вкладчиков. Эти записи могут располагаться в произвольной
       последовательности. Главное условие: по каждому филиалу запись
       суммы должна ОБЯЗАТЕЛЬНО предшествовать записям по документам по
       соответствующему филиалу. Кроме того, по каждому филиалу запись
       суммы должна быть ОДНА в исходном файле.

  1.3. Рекомендуемый порядок расположения записей в исходном файле:

       Вариант 1.

       1). Запись по платежному поручению
       2). По каждому филиалу: сначала запись суммы, затем последовательность
           всех записей по документам по этому филиалу

       Вариант 2.

       1). Запись по платежному поручению
       2). Сначала располагается последовательность записей сумм по филиалам
       3). Далее следуют записи по документам, при условии (п.1.2), что по
           каждому филиалу во второй порции записей наличествует запись суммы

  1.4. При загрузке в филиале из записи по платежному поручению берутся
       номер договора, номер поручения, валюта, номер операции и вид
       операции. Сама запись по платежному поручению в филиале не создается.
       В филиале формируются записи по расшифровке ТОЛЬКО по данному филиалу,
       в котором производится загрузка, и - по документам вкладчика этого же
       филиала.


  2. Структура записей исходного файла
  ------------------------------------

  2.0. Поля каждой записи должны следовать СТРОГО в указанном ниже порядке.
       Если какое-то поле опускается (например, поле счета вкладчика), то
       разделитель в конце пустого поля ОБЯЗАТЕЛЬНО должен быть проставлен.


  2.1. Структура записи по платежному поручению следующая:

       ____________________________________________________________
       Номер поля|Тип поля|            Содержание поля
       ____________________________________________________________
            1       Число   Код записи, всегда: 1
            2       Строка  Номер договора, до 25 символов
            3       Число   Номер платежного поручения
            4       Сумма   Заявленная сумма платежного поручения
            5       Число   Код валюты (должен совпадать с указанным в договоре)
            6       Число   Номер операции ( 63 - Списание, 73 - Зачисление )
            7       Число   Вид операции ( 1 - З.плата, 2 - Пенсия,
                                           3 - Выигрыш, 4 - Прочее,
                                           10 - По компенсации )
            8       Строка  Вид вклада - необязательный параметр (в случае
                            если отличается от Пенсионного или До востребования
                            в зависмости от вида операции, например VISA Classic)


  2.1. Структура записи суммы по филиалу следующая:

       ____________________________________________________________
       Номер поля|Тип поля|            Содержание поля
       ____________________________________________________________
            1       Число   Код записи, всегда: 2
            2       Число   Номер филиала
            3       Сумма   Сумма по филиалу


  2.2. Структура записи по документу вкладчика следующая:

       ____________________________________________________________
       Номер поля|Тип поля|            Содержание поля
       ____________________________________________________________
            1       Число   Код записи, всегда: 3
            2       Число   Номер филиала
            3       Число   Номер листа
            4       Строка  Номер счета вкладчика (может быть пустым)
            5       Число   Код клиента (можно не задавать, если указан
                            Номер счета. Либо Счет, либо Код клиента
                            обязательно должны быть указаны)
            6       Строка  Сумма документа
            Поля, необходимые для занесение информации по получателю
                (необязательные в случае, если номер операции не 80)
            7       Строка  Вид вклада получателя
            8       Число   Код валюты получателя
            9       Строка  Номер счета получателя
            Поля, необходимые для открытия карточки по счету (необязательные)
           10       Строка  Дата рождения (например в формате дд.мм.гггг)
           11       Строка  Код страны (в соответствии со справочником RS-Retail,
                            для России - RUS)
           12       Строка  Гражданство (Р - резидент, Н - нерезидент)
           13       Число   Вид документа, удостоверяющего личность (0 - паспорт
                            нового образца)
           14       Строка  Серия документа, удостоверяющего личность
           15       Строка  Номер документа, удостоверяющего личность
           16       Число   Адрес: индекс
           17       Строка  Адрес: регион
           18       Строка  Адрес: район
           19       Строка  Адрес: город
           20       Строка  Адрес: Тип населенного пункта
           21       Строка  Адрес: Название населенного пункта
           22       Строка  Адрес: тип улицы
           23       Строка  Адрес: название улицы
           24       Строка  Адрес: номер дома
           25       Строка  Адрес: корпус\строение
           26       Число   Номер квартиры
           27       Строка  Код платежной системы (короткое название платежной системы)
           28       Строка  Тип карточки (пользовательский тип карточки, который
                            выводится на экран)
           29       Строка  Поле для эмбоссирования 1
           30       Строка  Поле для эмбоссирования 2


  2.3. Структура записи строки окончания содержания файла

       ____________________________________________________________
       Номер поля|Тип поля|            Содержание поля
       ____________________________________________________________
            1       Число   Код записи, всегда: 4
            2       Число   Разделитель для ЭЦП. Всегда: 0


  3. Суммы по платежному поручению, по филиалам и по документам вкладчика
     при загрузке сверяются. При равенстве соответствующих сумм ( по
     платежному поручению = сумме Сумм по филиалам, Сумма по филиалу =
     сумме Сумм по документам вкладчика ) проставляется признак Сверки сумм.
     При неравенстве сумм выдаются соответствующие сообщения.


****************************************************************************/

import BankInter, deprintr, "sb_rep.mac", spCardInter, sign_rep;

var {oper};
private var {curdate};

file InputFile() txt 250;        /* Исходный текстовый файл      */
file RepFile()   txt 250 write;  /* Отчет о занесенных записях   */

var clntL = TClientList;

file Acnt (depositr) key 7;      /* Файл счетов вкладчиков       */

file Cntr (trn_cntr) key 0;      /* Файл договоров               */
file PayF (trn_payf) write;      /* Файл расшифровок по филиалам */
file Docs (trn_doc ) write;      /* Файл документов по БП        */

file scPos   (scPos,  "spcard.def") key 1;
file scCodif (scCodif,"spcard.def") key 2;
file scAcc   (scAcc,  "spcard.def") key 0;
file scCard  (scCard, "spcard.def") write;
file scLink  (scLink, "spcard.def") write;

/*****************  Константы  *************************/
const Name_rep =    "trn_ld_b.txt";       /* Отчет по занесенным записям */
/*____________142833 - SAN - Removal of the relative paths__________*/
const PatternFile = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", false ) + "\\*.trn";   /* Маска исходного текстового файла   "..\\TXTFILE\\*.trn"*/
const ReportFile = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", false ) + "\\";            // "..\\TXTFILE\\";
/*___________/142833 - 14/07/09_____________________________________*/

const cFNcash  = NumFNcash();             /* Номер филиала */

/*** Значения флага защиты пакетных загрузок безнал.платежей ***/
const LP_SHIELD_NOT   = 0;  /* Защиты загрузок нет             */
const LP_SHIELD_NO_ES = 1;  /* Защита без электронных подписей */
const LP_SHIELD_ES    = 2;  /* Защита с электронными подписями */

/******************  Настраиваемые константы  ********************/
/* Вид операции */
const atPension = 2;        /* Пенсия */
/* Виды вклада */
const taWorkPay = "ДО ВОСТРЕБ.";
const taPension = "Пенсионный";

/*************  Глобальные переменные  *****************/
var NameInputFile      = "";    /* Имя Операционного файла          */
var KolStrFile;                 /* Номер строки и количество
                                   строк в Операционном файле       */
var KolStrPayF = 0;             /* Количество введенных расшифровок */
var KolStrDocs = 0;             /* Количество введенных документов  */
var KolFilDocs = 0;             /* Количество документов по филиалу */
var KolStrErr  = 0;             /* Количяество ошибок при записи    */
var StrFile    = "";            /* Строка Операционного файла       */
var Sum_Credit = $0;
var sFilial = "???";
var LogStr  = "";
var Str_Err = "";
var ErrCode, ErrText;

var FlagShield = LP_Shield();  /* Флаг защиты пакетных загрузок:  */
                               /*   0 - защиты нет                */
                               /*   1 - защита без ЭЦП            */
                               /*   2 - защита с ЭЦП              */

/* Флаг записи по филиалу */
var FlagFilial = FALSE;

/*  Параметры платежного поручения  */
var NumberContract = "";
var Num_PayMessage = "";
var GlobalSumFilial = $0;
var SumDocuments = $0;
var vFlagCur = 0;
var GlobalCurrency = 0;
var Ground = "";
var TypeOper = 0;
var ApplType = 0;
var TypeAccount = "";
var errmes="";

/*
  Разделители
  ВНИМАНИЕ! Пробел в качестве разделителя не желателен, но если
  он используется в качестве разделителя, то в основании пробелы
  между словами не ставить
  Впереди каждого поля можно вставлять пробелы для выравнивания
*/
Array Separate;
Separate(0) = "!";
Separate(1) = ":";
Separate(2) = ";";
Separate(3) = ",";
Separate(4) = "|";
const NumberSeparate = ASize(Separate);

clntL.MayInsert = true;

/**********************************************************/
macro GetSeparateStr
/*
  Отделяет от StrFile начало строки до первого разделителя
  и смещает начало StrFile на следующий после разделителя символ
*/
  var Str = "";
  var Sym = "";
  var Cont = TRUE;
  var i = 1,
      j = 0,
      k = 1;
  var Len = StrLen(StrFile);
  var SymSpace =TRUE;

  while ((i <= Len) AND Cont)
    Sym = SubStr (StrFile,k,1);
    if (SymSpace AND (Sym == " "))
      StrFile = SubStr (StrFile,2);     /* Чистка от пробелов */
    else
      SymSpace = FALSE;
      j = 0;
      while (j < NumberSeparate)
        if (Sym == Separate(j))
          Cont = FALSE;
        end;
        j = j + 1;
      end;
      k = k + 1;
    end;
    i = i + 1;
  end;
  if (k > 1)
    if (NOT Cont)
      Str = SubStr (StrFile,1,k-2);
    else
      Str = SubStr (StrFile,1,k-1);
    end;
    StrFile = SubStr (StrFile,k);
  end;
  return Str;
end;

/**********************************************************/
macro IsInt (Str)
/*
  Записано ли в строке целое число (без знака)?

  27.01.97
*/
  var stat = TRUE;
  var Len = StrLen(Str);
  var i = 1;
  var Sym;
  var EndSpace = FALSE;

  while ((i <= Len) AND stat)
    Sym = SubStr(Str,i,1);
    if (Sym == " ")
      if (EndSpace)
        stat = FALSE;
      end;
    elif ((Sym < "0") OR (Sym > "9"))
      stat = FALSE;
    else
      EndSpace = TRUE;
    end;
    i = i + 1;
  end;
  return stat;
end;

/**********************************************************/
macro Разделитель_1_1
  var str;
  str = " ---------------------------------------------------------------------------------------------------------------------------------";
  return str;
end;

/************************************************************/
macro Главный_Заголовок_отчета
/*
  Шапка заголовка для всех отчетов
*/
  var str;
  str = " \n" +
  "    Загрузка информации из текстового файла \n" +
  "    в Платежные поручения на перечисления \n" +
  " \n" +
  " Обработан файл: " + NameInputFile + "\n" +
  " Дата обработки: " + String({curdate}) + "\n" +
/*  " Операционист:   " + String({oper}) + "\n" */
  " \n";
  return str;
end;

/**********************************************************/
macro Заголовок_отчета
  var str;
  /* str = Главный_Заголовок_отчета ();
  Insert (RepFile,str); */
  str = Разделитель_1_1();
  Insert (RepFile,str);
  str = "             Действие               |Лист|          Счет           |        Фамилия Имя Отчество       |          Сумма          |\n" +
        "                                    |    |                         |             вкладчика             |                         |";
  Insert (RepFile,str);
  str = Разделитель_1_1();
  Insert (RepFile,str);
end;

/**********************************************************/
macro Запись_в_отчет_расшифровки ( Pref )
  /*
  var str;
  str = "  " + String(Pref:35) +
    String(PayF.NumberContract:25) + " " +
    String(PayF.Num_PayMessage:6)  + " " +
    String(PayF.SumCheck:a:25);
  Insert (RepFile, str); */

  Insert( RepFile, "    " + Pref + ":" );
  Insert( RepFile, "        Согласно договору " + PayF.NumberContract );
  Insert( RepFile, "  по платежному поручению " + PayF.Num_PayMessage );
  Insert( RepFile, "                 на сумму " + PayF.SumCheck + "\n\n" );

end;

/**********************************************************/
macro Запись_в_отчет_документа ( Pref )
  var str;
  str = "  " + String(Pref:35) + " " +
    String(Docs.NList:3) + " " +
    String(Acc_Form(Docs.Account):25) + " " +
    String(Docs.Sname+" "+Docs.Nname+" "+Docs.Pname:35)  + " " +
    String(Docs.SumCheck:a:25);
  Insert (RepFile, str);
end;

/***********************************************************/
macro Конец_отчета
/*
  Вывод итоговых строк в отчет
*/
  /* var i = 0; */
  /* var kol_cur = Asize (Code_Currency); */
  var str;

  str = Разделитель_1_1 ();
  Insert (RepFile, str);
    str = "      Итого:                  " +
     String(Money(Double(Sum_Credit)):a:25);
    Insert (RepFile, str);
  str = " \n" +
  " Общее число обработанных записей: " + String(KolStrFile) + " \n" +
  " \n" +
  " Введено в базу данных " + String(KolStrPayF) + " расшифровок по филиалам \n";
  Insert (RepFile, str);
  str = " Введено в базу данных " + String(KolStrDocs) + " документов \n" +
  " \n";
  Insert (RepFile, str);
  if (KolStrErr > 0)
    str = " Не занесено в базу данных из-за системной ошибки: "+String(KolStrErr);
    Insert (RepFile, str);
  end;
  str = " ";
  Insert (RepFile, str);
end;

/***********************************************************/
macro Есть_Договор( NumberContract )
  var stat = 0;

  Cntr.NumberContract = NumberContract;
  if ( getEQ( Cntr ) )
    vFlagCur = Cntr.FlagCur;
    GlobalCurrency = Cntr.Currency_Busines;
    Ground = Cntr.GroundContract;
    TypeOper = Cntr.TypeOper;
    ApplType = Cntr.ApplType;
  else
    Ground = "";
    stat = 1;
  end;

  return stat;
end;

/***********************************************************/
macro Сверка_сумм_филиала ()
  var stat = 0;

  if ( NOT FlagFilial )
    return 0;    /* Записи по филиалу не было */
  else
    Insert (RepFile, "" );
    if ( GlobalSumFilial == SumDocuments )
      PayF.SignCheckFilial = "X";
      PayF.InputMethod = FlagShield;  /* Способ ввода записи */
      if ( Update( PayF ) )
        Запись_в_отчет_расшифровки( "Проставлена сверка сумм" );
      else
        Запись_в_отчет_расшифровки( "Не занесен признак сверки сумм" );
        ErrCode = Status( ErrText );
        Insert (RepFile, "  Ошибка " + String( ErrCode ) +
                         " при изменении записи по филиалу: " + ErrText );
        stat = 3;
      end;
    else
      if ( GlobalSumFilial > SumDocuments )
        Insert( RepFile, "  Заданные суммы " + String( SumDocuments ) +
                         " по документам не составляют суммы филиала " +
                         String( GlobalSumFilial ) );
      else
        Insert( RepFile, "  Заданные суммы " + String( SumDocuments ) +
                         " по документам превышают сумму филиала " +
                         String( GlobalSumFilial ) );
      end;
    end;
  end;

  Insert (RepFile, Разделитель_1_1() );

  return stat;
end;

/***********************************************************/
macro Платежное_поручение ()
  var stat = 0;
  var InputString;  /* Входная строка */
  var Type;         /* Тип записи     */
  var pCntr, pCurrency, pTypeOper, pApplType;

  /* FlagFilial = FALSE; */

  NumberContract = "";
  Num_PayMessage = "";
  GlobalSumFilial = $0;
  SumDocuments = $0;

  /* Номер договора */
  InputString = GetSeparateStr ();
  if ( InputString != "" )
    pCntr = Есть_Договор( InputString );
    NumberContract = InputString;
  else
    Str_Err = Str_Err + "Не задан номер договора для п/п";
    return 1;
  end;

  /* Номер платежного поручения */
  InputString = GetSeparateStr ();
  if ( InputString != "" )
    Num_PayMessage = InputString;
  else
    Str_Err = Str_Err + "Не задан номер п/п для п/п";
    return 2;
  end;

  /* Сумма */
  InputString = GetSeparateStr ();
  if ( ( InputString == "" ) OR NOT IsInt( InputString ) )
    Str_Err = Str_Err + "Не задана сумма для п/п";
    return 2;
  end;

  /* Код валюты */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
    pCurrency = Int( InputString );
    if ( pCntr != 0 )
      GlobalCurrency = pCurrency;
      if ( pCurrency == 0 )
        vFlagCur = 0;
      else
        vFlagCur = 1;
      end;
    end;
    if ( ( pCntr == 0 ) AND ( Cntr.Currency_Busines != pCurrency ) )
      Str_Err = Str_Err + "Код валюты в договоре " +
                String( Cntr.Currency_Busines ) +
                " не совпадает с заданным " + String( pCurrency );
    end;
  else
    Str_Err = Str_Err + "Не задан код валюты";
    return 3;
  end;

  /* Номер операции */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
    pTypeOper = Int( InputString );
    if ( ( pCntr == 0 ) AND ( TypeOper != pTypeOper ) )
      Str_Err = Str_Err + "Номер операции из договора " +
                String( TypeOper ) +
                " заменен на " + String( pTypeOper );
    end;
    TypeOper = pTypeOper;
  else
    Str_Err = Str_Err + "Не задан номер операции";
    return 4;
  end;

  /* Вид операции */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
    pApplType = Int( InputString );
    if ( ( pCntr == 0 ) AND ( ApplType != pApplType ) )
      Str_Err = Str_Err + "Вид операции из договора " + String( ApplType ) +
                " заменен на " + String( pApplType );
    end;
    ApplType = pApplType;
  else
    Str_Err = Str_Err + "Не задан вид операции";
    return 5;
  end;

  /* Вид вклада */
  TypeAccount = GetSeparateStr();
  TypeAccount = Trim(TypeAccount);

  return stat;
end;

/***********************************************************/
macro Расшифровка ()
  var stat = 0;
  var InputString;  /* Входная строка */
  var FNcash = 0;

  /* FlagFilial = FALSE; */

  if ( NumberContract == "" )
    Str_Err = Str_Err + "Попытка задать сумму по филиалу без п/п";
    return 1;
  end;

  /* Номер филиала */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
      FNcash = Int( InputString );
      if ( FNcash == cFNcash )
        if ( FlagFilial )
          Str_Err = Str_Err + "Дублирование записи по филиалу";
          return 2;
        end;
        PayF.FNcash = FNcash;
        PayF.RealFNcash = FNcash;
      else
        /* Str_Err = Str_Err + "Филал " + String( FNcash ) + " задан ошибочно"; */
        Str_Err = "";
        return 0;
      end;
  else
    Str_Err = Str_Err + "Не задан номер филиала для записи по филиалу";
    return 2;
  end;

  /* Сумма */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
    PayF.SumCheck = Money( Double( InputString ) / 100 );
    if ( FlagShield == LP_SHIELD_NO_ES )
      PayF.GlobalSumFilial = $0;  /* Защита без ЭЦП */
    else
      PayF.GlobalSumFilial = PayF.SumCheck;
    end;
  else
    Str_Err = Str_Err + "Не задана сумма для записи по филиалу";
    return 3;
  end;

  if ( stat == 0 )
    PayF.AutoKey = 0;
    PayF.Num_PayMessage = Num_PayMessage;
    PayF.NumberContract = NumberContract;
    /* PayF.FNcash */
    PayF.FlagCur = vFlagCur;
    PayF.NPack = 0;
    PayF.Code_Currency = GlobalCurrency;
    PayF.TypeOper = TypeOper;
    PayF.ApplType = ApplType;
    PayF.DateDocumentFilial = {curdate};
    PayF.GlobalSumFactFilial = $0;
    PayF.SignCheckFilial = StrFor( 0 );
    PayF.DateKvitFilial = Date( 0, 0, 0 );
    PayF.SignKvitFilial = StrFor( 0 );
    PayF.iApplicationKindFi = 0;
    PayF.ApplicationKeyFi = "";
    PayF.NumSession = -1;  /* Введено в филиале ! */
    PayF.Action = 0;
    PayF.Ground = Ground;

    /* Выбор вида вклада */
    if (TypeAccount != "")
      PayF.Type_Account = TypeAccount;
    elif ( ApplType == atPension )
      PayF.Type_Account = taPension;
    else
      PayF.Type_Account = taWorkPay;
    end;

    PayF.InputMethod = FlagShield;  /* Способ ввода записи */

    PayF.Oper = {oper};

    PayF.iApplicationKind = 1;
    PayF.ApplicationKey   = FormApplicationKey(1);

    PayF.TRN_System = 1;  /* Безналичные перечисления */
    PayF.PaymAppKind = 0;
    PayF.PaymAppKey = "";

    if ( Insert( PayF ) )
      FlagFilial = TRUE;
      KolStrPayF = KolStrPayF + 1;
      Запись_в_отчет_расшифровки( "Занесена расшифровка" );
      Заголовок_отчета ();
      GlobalSumFilial = PayF.SumCheck;
      Sum_Credit = Sum_Credit + GlobalSumFilial;
    else
      KolStrErr = KolStrErr + 1;
      Запись_в_отчет_расшифровки( "Не занесена расшифровка" );
      ErrCode = Status( ErrText );
      Insert (RepFile, "  Ошибка " + String( ErrCode ) +
                       " при занесении расшифровки: " + ErrText );
      stat = 3;
    end;
  end;

  return stat;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro T_InputCardAndClient

  var stat = TRUE;

  if (stat AND (clntL.CurRec.rec.CodClient == 0))
    clntL.CurRec.rec.CodClient = FormClientCode();
    stat = clntL.Insert();
  end;

  if (stat)
    scCard.cardRef   = scDefReferCard();
    scCard.CodClient = clntL.CurRec.rec.CodClient;
    stat = Insert(scCard);
  end;

  if (stat AND (Docs.Referenc > 0)) /* Счет уже существует */
    ClearRecord(scLink);
    scLink.accCardLinkRef        = scDefReferLink();
    scLink.accCardLinkType       = "EXT__";
    scLink.accCardLinkState      = "OK___";
    scLink.oper                  = {oper};
    scLink.cardAccRef            = Docs.Referenc;
    scLink.cardRef               = scCard.cardRef;
    scLink.FNCash                = scCard.FNCash;
    scLink.FlagCur               = Acnt.IsCur;
    scLink.CodCur                = Acnt.Code_Currency;
    scLink.psRef                 = scCard.psRef;
    scLink.mainCard              = "";
    scLink.mainAcc               = "";
    scLink.accCardLinkCreateDate = {curdate};
    scLink.accCardLinkOpenDate   = {curdate};
    scLink.accCardLinkNotes      = "Загрузка файла безналичных платежей " + NameInputFile + " строка " + String(KolStrFile);
    stat = Insert(scLink);
  end;

  if (NOT stat)
    AbortTrn();
  else
    if (Docs.Account == "")
      Docs.CodClient = clntL.CurRec.rec.CodClient;
    end;
    Docs.CardRef = scCard.cardRef;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro T_InputClient

  var stat = true;

  if ( stat )
    clntL.CurRec.rec.CodClient = FormClientCode( );
    stat = clntL.Insert();
  end;

  if ( not stat )
    AbortTrn( );
  else
    Docs.CodClient = clntL.CurRec.rec.CodClient;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FormNameString ( InNameString )

  var OutNameString = "";
  var i             = 1;

  while ( i <= StrLen( InNameString ) )
    if ( i == 1 )
      OutNameString = OutNameString + StrUpr( SubStr( InNameString, i, 1 ) );
    else
      OutNameString = OutNameString + StrLwr( SubStr( InNameString, i, 1 ) );
    end;
    i = i + 1;
  end;

  return OutNameString;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro InputCardAndClientInfo ( SName, Name, PName )

  var stat = 0;
  var dd   = "";
  var mm   = "";
  var yy   = "";
  var i;
  var j;
  /* Поля из исходного файла */
  var BirthDay         = GetSeparateStr( );
  var Country          = GetSeparateStr( );
  var NotResident      = GetSeparateStr( );
  var DocType          = Int( GetSeparateStr( ) );
  var DocSer           = GetSeparateStr( );
  var DocNumb          = GetSeparateStr( );
  var AddrIndex        = Int(GetSeparateStr( ) );
  var AddrRegion       = GetSeparateStr( );
  var AddrDistrict     = GetSeparateStr( );
  var AddrCity         = GetSeparateStr( );
  var AddrTypePoint    = GetSeparateStr( );
  var AddrNamePoint    = GetSeparateStr( );
  var AddrTypeStreet   = GetSeparateStr( );
  var AddrNameStreet   = GetSeparateStr( );
  var AddrNumbHouse    = GetSeparateStr( );
  var AddrNumbBuilding = GetSeparateStr( );
  var AddrNumberFlat   = Int( GetSeparateStr( ) );
  var PSCode           = GetSeparateStr( );
  var CardUserType     = GetSeparateStr( );
  var Emboss1          = GetSeparateStr( );
  var Emboss2          = GetSeparateStr( );

  /* Приведем дату к виду RS-Retail */
  i = 1;
  j = 0;
  while (i <= StrLen( BirthDay ) )
    if ( ( SubStr( BirthDay, i, 1 ) < "0" ) or ( SubStr( BirthDay, i, 1 ) > "9" ) )
      j = j + 1;
    else
      if ( j == 0 )
        dd = dd + SubStr( BirthDay, i, 1 );
      elif (j == 1)
        mm = mm + SubStr( BirthDay, i, 1 );
      else
        yy = yy + SubStr( BirthDay, i, 1 );
      end;
    end;
    i = i + 1;
  end;
  if ( dd == "" ) dd = "0"; end;
  if ( mm == "" ) mm = "0"; end;
  if ( yy == "" ) yy = "0"; end;
  BirthDay = Date( Int( Trim( dd ) ), Int( Trim( mm ) ), Int( Trim ( yy ) ) );

  /* Приведем гражданство к виду RS-Retail */
  if ( StrUpr( NotResident ) == "Н" )
    NotResident = StrFor( 1 );
  else
    NotResident = "";
  end;

  if ( ( PSCode != "" ) or ( CardUserType != "" ) or ( Emboss1 != "" ) )

    /* Найдем платежную систему */
    scPos.psCode = PSCode;
    if ( not GetEQ( scPos ) )
      Docs.ReasonNoCarry = 50;
      Docs.Ground = "Проводка запрещена при загрузке: Платежная система " + PSCode + " не найдена";
      Str_Err = Str_Err + "Платежная система " + PSCode + " не найдена";
    end;

    /* Найдем тип карточки */
    if ( Docs.ReasonNoCarry == 0 )
      scCodif.codType     = 7;
      scCodif.codUserCode = CardUserType;
      scCodif.psRef       = scPos.psRef;
      if ( not GetEQ( scCodif ) )
        scCodif.codType     = 7;
        scCodif.codUserCode = CardUserType;
        scCodif.psRef       = scPos.psRef;
        if ( not GetEQ( scCodif ) )
          Docs.ReasonNoCarry = 51;
          Docs.Ground = "Проводка запрещена при загрузке: Тип карточки " + CardUserType + " не найден";
          Str_Err = Str_Err + "Тип карточки " + CardUserType + " не найден";
        end;
      end;
    end;

    /* Должно быть задано эмбоссирование */
    if ( Docs.ReasonNoCarry == 0 )
      if ( Emboss1 == "" )
        Docs.ReasonNoCarry = 52;
        Docs.Ground = "Проводка запрещена при загрузке: Не задано эмбоссирование";
        Str_Err = Str_Err + "Не задано эмбоссирование";
      end;
    end;

    /* Если задан счет - он должен быть той же платежной системы */
    if ( Docs.ReasonNoCarry == 0 )
      if ( ( Docs.Account != "" ) AND ( Docs.Referenc > 0 ) )
        scAcc.Referenc = Docs.Referenc;
        if ( not GetEQ( scAcc ) )
          Docs.ReasonNoCarry = 53;
          Docs.Ground = "Проводка запрещена при загрузке: Не найдена карточная часть счета";
          Str_Err = Str_Err + "Не найдена карточная часть счета";
        else
          if ( scAcc.psRef != scPos.psRef )
            Docs.ReasonNoCarry = 54;
            Docs.Ground = "Проводка запрещена при загрузке: Платежные системы карточки и счета не совпадают";
            Str_Err = Str_Err + "Платежные системы карточки и счета не совпадают";
          end;
        end;
      end;
    end;

    if ( Docs.ReasonNoCarry == 0 )
      if ( SName != "" )
        clntL.CurRec.Clear();
        clntL.CurRec.rec.Name1         = FormNameString( SName );
        clntL.CurRec.rec.Name2         = FormNameString( Name );
        clntL.CurRec.rec.Name3         = FormNameString( PName );
        clntL.CurRec.rec.Born          = BirthDay;
        clntL.CurRec.rec.FNcash        = cFNcash;
        clntL.CurRec.rec.NotResident   = NotResident;
        clntL.CurRec.rec.PaperKind     = DocType;
        clntL.CurRec.rec.PaperSeries   = DocSer;
        clntL.CurRec.rec.PaperNumber   = DocNumb;
        clntL.CurRec.rec.PaperIssuer   = "";
        clntL.CurRec.rec.Country       = Country;
        clntL.CurRec.rec.PostIndex     = AddrIndex;
        clntL.CurRec.rec.Region        = AddrRegion;
        clntL.CurRec.rec.District      = AddrDistrict;
        /* clntL.CurRec.rec.City        = AddrCity; */
        clntL.CurRec.rec.lCodePlace    = AddrTypePoint;
        clntL.CurRec.rec.CodePlace     = AddrTypePoint;
        clntL.CurRec.rec.Place         = AddrNamePoint;
        clntL.CurRec.rec.lCodeStreet   = AddrTypeStreet;
        clntL.CurRec.rec.StreetType    = AddrTypeStreet;
        clntL.CurRec.rec.StreetName    = AddrNameStreet;
        clntL.CurRec.rec.House         = AddrNumbHouse;
        clntL.CurRec.rec.NumCorp       = AddrNumbBuilding;
        clntL.CurRec.rec.Flat          = AddrNumberFlat;
        clntL.CurRec.rec.Address       = "";
      else
        clntL.CurRec.rec.CodClient = Docs.CodClient;
      end;
      ClearRecord(scCard);
      scCard.cardNumber       = "0";
      scCard.cardStateCode    = "OFF__";
      scCard.cardTypeCode     = scCodif.codCode;
      scCard.oper             = {oper};
      scCard.cardOpenDate     = {curdate};
      DateSplit( scCard.cardOpenDate, dd, mm, yy );
      dd = 1;
      yy = yy + 1;
      mm = mm + 1;
      if (mm > 12)
        mm = 1;
        yy = yy + 1;
      end;
      scCard.cardMaturityDate = Date( dd, mm, yy ) - 1;
      scCard.embossingName1   = Emboss1;
      scCard.embossingName2   = Emboss2;
      scCard.cardNotes        = "Загрузка файла безналичных платежей " + NameInputFile + " строка " + String(KolStrFile);
      scCard.cardStatusDate   = {curdate};
      scCard.cardStatusOper   = {oper};
      scCard.FNCash           = cFNcash;
      scCard.psRef            = scPos.psRef;
      if ( not ProcessTrn( 1, "T_InputCardAndClient", scCard ) )
        Docs.ReasonNoCarry = 55;
        Docs.Ground = "Проводка запрещена при загрузке: Ошибка при вставке карточки в базу данных";
        Str_Err = Str_Err + "Ошибка при вставке карточки в базу данных";
      end;
    end;

  elif ( ( Docs.Account == "" ) and ( Docs.CodClient == 0 ) and ( SName != "" ) )
    if ( ( BirthDay         != Date( 0, 0, 0 ) ) or
         ( Country          != ""              ) or
         ( NotResident      != ""              ) or
         ( DocType          != 0               ) or
         ( DocSer           != ""              ) or
         ( DocNumb          != ""              ) or
         ( AddrIndex        != 0               ) or
         ( AddrRegion       != ""              ) or
         ( AddrDistrict     != ""              ) or
         ( AddrCity         != ""              ) or
         ( AddrTypePoint    != ""              ) or
         ( AddrNamePoint    != ""              ) or
         ( AddrTypeStreet   != ""              ) or
         ( AddrNameStreet   != ""              ) or
         ( AddrNumbHouse    != ""              ) or
         ( AddrNumbBuilding != ""              ) or
         ( AddrNumberFlat   != 0               )   )
      clntL.CurRec.Clear();
      clntL.CurRec.rec.Name1         = FormNameString( SName );
      clntL.CurRec.rec.Name2         = FormNameString( Name );
      clntL.CurRec.rec.Name3         = FormNameString( PName );
      clntL.CurRec.rec.Born          = BirthDay;
      clntL.CurRec.rec.FNcash        = cFNcash;
      clntL.CurRec.rec.NotResident   = NotResident;
      clntL.CurRec.rec.PaperKind     = DocType;
      clntL.CurRec.rec.PaperSeries   = DocSer;
      clntL.CurRec.rec.PaperNumber   = DocNumb;
      clntL.CurRec.rec.PaperIssuer   = "";
      clntL.CurRec.rec.Country       = Country;
      clntL.CurRec.rec.PostIndex     = AddrIndex;
      clntL.CurRec.rec.Region        = AddrRegion;
      clntL.CurRec.rec.District      = AddrDistrict;
      /* clntL.CurRec.rec.City        = AddrCity; */
      clntL.CurRec.rec.lCodePlace     = AddrTypePoint;
      clntL.CurRec.rec.CodePlace     = AddrTypePoint;
      clntL.CurRec.rec.Place         = AddrNamePoint;
      clntL.CurRec.rec.lCodeStreet   = AddrTypeStreet;
      clntL.CurRec.rec.StreetType    = AddrTypeStreet;
      clntL.CurRec.rec.StreetName    = AddrNameStreet;
      clntL.CurRec.rec.House         = AddrNumbHouse;
      clntL.CurRec.rec.NumCorp       = AddrNumbBuilding;
      clntL.CurRec.rec.Flat          = AddrNumberFlat;
      clntL.CurRec.rec.Address       = "";

      if ( not ProcessTrn( 1, "T_InputClient" ) )
        Docs.ReasonNoCarry = 70;
        Docs.Ground = "Проводка запрещена при загрузке: Ошибка при вставке клиента в базу данных";
        Str_Err = Str_Err + "Ошибка при вставке клиента в базу данных";
      end;

    end;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro Документ ()
  var stat = 0;
  var InputString;  /* Входная строка */
  var FNcash = 0;
  var FlagAccount = TRUE;
  var FlagAccFIO  = TRUE;
  var SName = "";
  var Name = "";
  var PName = "";

  ClearRecord( Docs );

  if ( NumberContract == "" )
    Str_Err = Str_Err + "Попытка задать сумму по документу без п/п";
    return 1;
  end;

  /* Номер филиала */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
      FNcash = Int( InputString );
      if ( FNcash == cFNcash )
        Docs.FNcash = FNcash;
      else
        Str_Err = "";
        return 0;
      end;
  else
    Str_Err = Str_Err + "Не задан номер филиала для записи по документу";
    return 2;
  end;

  if ( NOT FlagFilial )
    Str_Err = Str_Err + "Не найдена запись по филиалу перед записью по документу";
    return 3;
  end;

  /* Номер листа */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
    Docs.NList = Int( InputString );
    if ( Docs.NList <= 0 )
      Docs.NList = 1;
    end;
  else
    Str_Err = Str_Err + "Не задан номер листа для записи по документу";
    return 4;
  end;

  /* Номер счета */
  Docs.Account = GetSeparateStr ();

  if ( Docs.Account == "" )
    Str_Err = Str_Err + "Не указан счет для записи по документу, проводка запрещена";
    return 5;
  end;


  /* Код клиента */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
    Docs.CodClient = Int( InputString );
    if ( Docs.CodClient < 0 )
      Docs.CodClient = 0;
    end;
  end;

  if ( clntL.GetRecord( Docs.CodClient ) )
    /* Нашли клиента */
    Docs.Sname = SubStr( Trim( clntL.CurRec.rec.Name1 ), 1, 24 );
    Docs.Nname = SubStr( Trim( clntL.CurRec.rec.Name2 ), 1, 24 );
    Docs.PName = SubStr( Trim( clntL.CurRec.rec.Name3 ), 1, 24 );
  else
    Str_Err = Str_Err + "Клиент с кодом " + String( Docs.CodClient ) +
                        " не найден.";
    return 6;
  end;

  /* // Chizhova SCR #57445 Запрет на ввод документа без номера счета
  if ( ( Docs.Account == "" ) AND ( Docs.CodClient == 0 ) )
    Str_Err = Str_Err + "Не указаны счет и клиент для записи по документу";
    return 5;
  end;
  */

  /* Сумма */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
    Docs.SumCheck = Money( Double( InputString ) / 100 );
    if ( FlagShield == LP_SHIELD_NO_ES )
      Docs.Sum = $0;  /* Защита без ЭЦП */
    else
      Docs.Sum = Docs.SumCheck;
    end;
  else

    /*
    Str_Err = Str_Err + "Не задана сумма для записи по документу";
    return 6*/;
  end;

  /* Вид вклада получателя */
  InputString = GetSeparateStr();
  Docs.Rcp_Type_Account = InputString;

  /* Код валюты получателя */
  InputString = GetSeparateStr();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
        Docs.Rcp_Code_Currency = int(InputString);
  else
        Docs.Rcp_Code_Currency = 0;
  end;

  /* Счет получателя */
  InputString = GetSeparateStr();
  Docs.Rcp_Account = InputString;

  Docs.Type_Account = PayF.Type_Account;

  if ( ( stat == 0 )  and  ( strlen(Docs.Account) != 0 ) )
  /* Поиск заданного счета вкладчика и простановка Referenc */
    Acnt.FNCash  = cFNcash;
    Acnt.Account = Docs.Account;
    if ( getEQ( Acnt ) )
      /* Счет найден */
      Docs.CodClient = Acnt.CodClient;
      Docs.Referenc  = Acnt.Referenc;
      Docs.Type_Account = Acnt.Type_Account;
      if (Index(Acnt.UserTypeAccount,"К") == 0)
        /* Поиск записи клиента */
        if ( clntL.GetRecord( Acnt.CodClient ) )
          /* Нашли клиента */
          if ( ( ( StrLen( Docs.Sname ) != 0 )  AND
                 ( StrUpr(Trim(Docs.Sname)) != StrUpr(Trim(clntL.CurRec.rec.Name1)) ) )  OR
               ( ( StrLen( Docs.Nname ) != 0 )  AND
                 ( StrUpr(Trim(Docs.Nname)) != StrUpr(Trim(clntL.CurRec.rec.Name2)) ) )  OR
               ( ( StrLen( Docs.Pname ) != 0 )  AND
                 ( StrUpr(Trim(Docs.Pname)) != StrUpr(Trim(clntL.CurRec.rec.Name3)) ) ) )
            Str_Err = Str_Err + "Заданное ФИО не соответствует счету, проводка запрещена";
            Docs.ReasonNoCarry = 3;
            Docs.Ground = "Проводка запрещена при загрузке: " +
                          "несоответствие счету Ф.И.О. вкладчика";
            FlagAccFIO = FALSE;
          else
            Docs.Sname = clntL.CurRec.rec.Name1;
            Docs.Nname = clntL.CurRec.rec.Name2;
            Docs.Pname = clntL.CurRec.rec.Name3;
          end;
        end;
      end;
    else
      /* Счет не найден */
      Str_Err = Str_Err + "Счет " + Acc_Form(Docs.Account) + " не найден, проводка запрещена";
      FlagAccount = FALSE;
      Docs.CodClient = 0;
      Docs.Referenc = 0;
      Docs.Type_Account = "";
      Docs.Sname = clntL.CurRec.rec.Name1;
      Docs.Nname = clntL.CurRec.rec.Name2;
      Docs.Pname = clntL.CurRec.rec.Name3;
      Docs.ReasonNoCarry = 4;
      Docs.Ground = "Пр-ка запр. при загр.: " +
                    "Сч. " + Docs.Account + " не найден";
    end;
  end;

  /* Заполнение полей, связанных с пластиковой карточкой */
  if ( ( stat == 0 ) and ( Docs.ReasonNoCarry == 0 ) )
    stat = InputCardAndClientInfo( SName, Name, PName );
  end;

  if ( stat == 0 )

    Docs.IsCur = vFlagCur;
    Docs.FNCash = cFNcash;
    Docs.ListTransfer = PayF.AutoKey;
    Docs.Code_Currency = PayF.Code_Currency;
    Docs.Date_Document = {curdate};
    Docs.Oper = {oper};
    Docs.TypeOper = PayF.TypeOper;
    Docs.ApplType = PayF.ApplType;
    Docs.NPack = 0;
    Docs.NDoc = 0;
    /* Docs.NList */
    Docs.iApplicationKind = 0;
    Docs.ApplicationKey = "";
    Docs.Status = StrFor( 0 );
    Docs.NumSession = -1;  /* Введено в филиале ! */
    Docs.Action = 0;
    Docs.SignCheckList = StrFor( 0 );
    if ( FlagAccFIO )
      ;
      /*
      Docs.ReasonNoCarry = 0;
      Docs.Ground = PayF.Ground;
      */
    end;
    Docs.OldlistTransfer = 0;
    Docs.AutoKey = 0;
    Docs.OldAutoKey = 0;
    Docs.ErrorCode = 0;
    if ( Acnt.Open_Close != StrFor(0) )
      Docs.ReasonNoCarry = 5;
      Docs.Ground = "Проводка запрещена при загрузке: " +
                    "Счет закрыт";
    end;
    Docs.Fict = StrFor( 0 );
    Docs.InputMethod = FlagShield;  /* Способ ввода записи */

    if ( Insert( Docs ) )
      KolStrDocs = KolStrDocs + 1;
      KolFilDocs = KolFilDocs + 1;
      Запись_в_отчет_документа( "Занесен документ" );
      if ( Docs.ReasonNoCarry == 0 )  /* Учитываются только док-ты без ошибок */
        SumDocuments = SumDocuments + Docs.SumCheck;
      end;
    else
      KolStrErr = KolStrErr + 1;
      Запись_в_отчет_документа( "Не занесен документ" );
      ErrCode = Status( ErrText );
      Insert (RepFile, "  Ошибка " + String( ErrCode ) +
                       " при занесении документа: " + ErrText );
      stat = 3;
    end;

    if ( ( NOT FlagAccount )  OR  ( NOT FlagAccFIO ) )
      stat = 7;
    end;
  end;

  return stat;
end;

/***********************************************************/
macro Обработка_записи ()
/*
  Разбирает строку исходного файла
*/
  var stat = 0;
  var InputString;  /* Входная строка */
  var Type;         /* Тип записи     */

  Str_Err = "";

  /* Тип записи */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
    Type = Int( InputString );
    if ( Type == 1 )
      stat = Платежное_поручение();
    else
      if ( Type == 2 )
        stat = Расшифровка();
      else
        if ( Type == 3 )
          stat = Документ();
        else
          return 777;  /* Окончание записей файла */
        end;
      end;
    end;
  else
    Str_Err = Str_Err + "Ошибка в типе записи" + InputString;
    stat = 1;
  end;

  return stat;
end;

/**********************************************************/
macro Обработка_строк_исходного_файла ()
  var stat        = 0;
  var NextStr     = TRUE;
  var sTypeShield = "";

  ReWind (InputFile);
  KolStrFile = 0;
  KolStrPayF = 0;
  KolStrDocs = 0;
  KolFilDocs = 0;

  while ( NextStr  AND  Next(InputFile) )
    StrFile = InputFile.str;
    KolStrFile = KolStrFile + 1;
    stat = Обработка_записи();
    if ( stat == 777 )
      NextStr = FALSE;
      stat = 0;
    else
      if  ( ( stat != 0 )  AND  ( Str_Err != "" ) )
        Insert( RepFile, "  Строка " + String( KolStrFile ) + ": " + Str_Err );
        Str_Err = "";
      end;
    end;
  end;

  if ( stat == 0 )
    stat = Сверка_сумм_филиала();
  end;

  /* Запись в журнал аудита о вводе платежного поручения по филиалу */
  if ( KolFilDocs != 0 )

    /* Описание способа защиты пакетной загрузки. */
    if   ( FlagShield == LP_SHIELD_NOT )
      sTypeShield = "Загрузка проведена без защиты.";
    elif ( FlagShield == LP_SHIELD_NO_ES )
      sTypeShield = "Защита загрузки без ЭЦП.";
    elif ( FlagShield == LP_SHIELD_ES )
      sTypeShield = "Защита загрузки с ЭЦП. " +
                    "Файл подписан ЭЦП \"Бикрипт-КСБ\". " +
                    "ЭЦП соответствует пакету загрузки.";
    else
      sTypeShield = "Способ защиты загрузки не опознан";
    end;

    LogStr = "Загружен список п/п по безнал.платежам:\n\n" +
             "          Договор: \"" + PayF.NumberContract + "\"\n" +
             "              п/п: "   + String( PayF.Num_PayMessage ) + "\n" +
             "           филиал: "   + sFilial + "\n" +
             "        сумма п/п: "   + String( PayF.SumCheck:0:2:a ) + " руб.\n" +
             "кол-во документов: "   + String( KolFilDocs ) + "\n\n" +
             sTypeShield;

    TrnPayLog( LogStr );

    KolFilDocs = 0;  /* Количество документов по филиалу */
  end;

  return stat;
end;

/************************************************************/
macro Обработка_исходного_файла
  var str;
  str = Строка_Начало_Конец_отчета ();
  Insert (RepFile,str);
  /* Заголовок_отчета (); */
  str = Главный_Заголовок_отчета ();
  Insert (RepFile,str);

  Обработка_строк_исходного_файла ();

  Конец_отчета ();
  str = Строка_Начало_Конец_отчета ();
  Insert (RepFile,str);
end;

/************************************************************/
macro Чтение_записи_о_Подразделении
 if ( not findDepartment(cFNcash, sFilial) )
   sFilial = "???";
 end;
end;

/************************************************************
                  Головная функция макроса
************************************************************/

  if ( SelectFile( NameInputFile, PatternFile, "Выберите исходный файл" ) )
    if ( CheckESignFile( NameInputFile, errmes ) ) /* Проверка ЭЦП */
      if ( Open( InputFile, NameInputFile )   AND
           Open( RepFile, ReportFile + Name_rep ) )
        Чтение_записи_о_Подразделении();
        Обработка_исходного_файла();
        Close( RepFile );
        Close( InputFile );

        if ( BC_Archive( NameInputFile ) == 0 )
          [
            Архивация файла загрузки прошла успешно
          ];
        else
          [
            Ошибка при архивировании загрузочного файла

          ];
        end;

        ViewFile( RepFile );
        [ Работа закончена ]
      else
        [ Ошибка при открытии файлов ];
      end;
    else
      if ( errmes!="" )
         LogStr = "Список п/п по безнал.платежам не загружен.\n\n" +
                  "Файл: \"" + NameInputFile + "\"\n" +
                  "Защита загрузки с ЭЦП. " +
                  "Файл подписан ЭЦП \"Бикрипт-КСБ\". " +
                  "Процедура проверки ЭЦП вернула ошибку: "+errmes;
      else
         LogStr = "Список п/п по безнал.платежам не загружен.\n\n" +
                  "Файл: \"" + NameInputFile + "\"\n" +
                  "Защита загрузки с ЭЦП. " +
                  "Файл подписан ЭЦП \"Бикрипт-КСБ\". " +
                  "ЭЦП не соответствует пакету загрузки.";
      end;

      TrnPayLog( LogStr );
    
      if ( errmes!="" )
        [
           Процедура проверки ЭЦП вернула ошибку:
           #
           Загрузка записей отменяется.](errmes);
      else
        [
           ЭЦП не соответствует содержанию файла.
           Загрузка записей отменяется.];
      end;
    end;
  else
    [ Отказались от выбора исходного текстового файла ];
  end;

end;
