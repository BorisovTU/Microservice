/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Электронная выписка из Реестра ВОО по 113-И.

*****************************************************************/

import ExchangeInter, CommonInter, DeskInter;
import Define, isSrvDoc, Cur_Rate, RstrFunc, Acc_Form;
import SqlConv, RSD;

private var sbdepdoc = TBFile( "sbdepdoc.dbt", "R", 3, null, "sbbank.def" );
private var pay_doc  = TBFile( "pay_doc.dbt" , "R", 2, null, "sbbank.def" );
private var exoperat = TBFile( "exoperat.dbt", "R", 1, null, "exchange.def" );
private var exoperat2 = TBFile( "exoperat.dbt", "R", 1, null, "exchange.def" );

private var country  = TBFile( "country.dbt" , "R", 2, null, "bank.def" );
private var sb_casln = TBFile( "sb_casln.dbt", "R", 1, null, "sbbank.def" );

private var paydocop   = TRecHandler( "paydocop.str", "exchange.def" );
private var paydocop2  = TRecHandler( "paydocop.str", "exchange.def" );
private var paydoc_cnv = TRecHandler( "pay_doc.cnv" , "sbbank.def" );
private var paydoc_n36 = TRecHandler( "pay_doc.n36" , "sbbank.def" );

file tmp1 () txt 512 write;  // Временный файл этапа 1 - запись.
file tmr1 () txt 512;        // Временный файл этапа 1 - чтение.
file tmp2 () txt 512 write;  // Временный файл этапа 2 - запись.
file tmr2 () txt 512;        // Временный файл этапа 2 - чтение.
file tmp3 () txt 512 write;  // Временный файл этапа 3 - запись.
file tmr3 () txt 512;        // Временный файл этапа 3 - чтение.
file tmp4 () txt 512 write;  // Временный файл этапа 4 - запись.
file tmr4 () txt 512;        // Временный файл этапа 4 - чтение.
file tmp5 () txt 512 write;  // Временный файл этапа 5 - запись.
file tmr5 () txt 512;        // Временный файл этапа 5 - чтение.

file f_el () txt 512 write;  // Файл Электрон.выписки из Реестра ВОО по 113-И.

private var txtDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", TRUE );
private var TMPF_Name1 = txtDir + "re117_1." + String({CNum});
private var TMPF_Name2 = txtDir + "re117_2." + String({CNum});
private var TMPF_Name3 = txtDir + "re117_3." + String({CNum});
private var TMPF_Name4 = txtDir + "re117_4." + String({CNum});
private var TMPF_Name5 = txtDir + "re117_5." + String({CNum});

private var ExportDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\EXPORTMESDIR", TRUE );

const FNCash = NumFNCash();

private const ControlSum = $600000;

private const
  SB_DEPOSIT  =    1,  // Документ вкладной операции
  SB_PAYDOC   =  200,  // Документ прочие операции
  SB_EXCHANGE =  250,  // Реестр по обменному пункту
  SB_EXCHNDOC =  260;  // Документ валютнообменной операции

private const
  PTCK_BIC        = 3,   // БИК (РФ).
  PTCK_BANKREGNUM = 13,  // Вид субъекта: регистр. номер нашего банка.
  PTCK_OGRN       = 27;  // Код ОГРН.

private const
  CODEKIND_BIC = 3;

/* Типы ресурсов */
private const
  TYPERES_CURRENCY    =   1,  // Деньги (из справочника валют).
  TYPERES_VALFORM     =   2,  // Ценные бланки.
  TYPERES_VALUABLES   =   3,  // Др. ценности (из справочника ценностей).
  TYPERES_PAYDOC      =   4,  // Платежные документы ОП.
  TYPERES_CARD        =  10,  // Пластиковые карточки.
  TYPERES_MINCAPISSUE = 500;  // Минимально возможный код ЦБ.

private const
   ACCOUTN_OWNER       = StrFor(  0 ),    /* Владелец счета      */
   ACCOUTN_PROXY       = StrFor(  1 ),    /* Доверенное лицо     */
   ACCOUTN_HEIR        = StrFor(  2 ),    /* Наследник - выдачи  */
   ANONIMUS_CLIENT     = StrFor(  3 ),    /* Анонимный клиент    */
   ACCOUNT_IMPORTER    = StrFor(  4 ),    /* Вноситель           */
   ACCOUNT_TUTOR       = StrFor(  5 ),    /* Опекун              */
   ACCOUNT_OBLIGATOR   = StrFor(  6 ),    /* Под обязательство   */
   ACCOUTN_HEIR_PAY    = StrFor(  7 ),    /* Наследник - выплаты */
   ACCOUNT_UNDER_TRIAL = StrFor(  8 ),    /* По суду             */
   ACCOUNT_PARENT      = StrFor(  9 ),    /* Родитель            */
   ACCOUNT_RETRAST     = StrFor( 10 );    /* Передоверенность    */

private var OurBank_RegNum = Trim( getRegNumOurBank( PTCK_BANKREGNUM ) );
private var OurBank_OKATO  = Trim( getOurBankOKATO() );
private var OurBank_OKPO   = Trim( getOurBankOKPO() );
private var OurBank_BIC    = Trim( getRegNumOurBank( PTCK_BIC ) );
private var OurBank_OGRN   = Trim( getRegNumOurBank( PTCK_OGRN ) );
private var OurBank_Name   = "";

private var FN_EL;

/**************************************************************************\
|                         Получение адреса ВСП.                            |
\**************************************************************************/
private macro addrBranch()
 var rAddr = "";
 var party = TRecHandler( "i_party.rec" );
 if ( findDepartment(FNCash, null, null, party) )
   rAddr = party.rec.address;
 else
   rAddr = "???";
 end;
 return rAddr;
end;

/**************************************************************************\
|        Получение суммы операции в рублевом эквиваленте по курсу ЦБ.      |
\**************************************************************************/
private macro Operation_Sum( sumCur, codCur, dateSum )

 var outSum = $0L;
 var cbRate = 0.0;

 if ( ( ValType( sumCur ) != V_STRING ) and ( sumCur > 0 ) )
   if ( codCur == 0 )
     outSum = sumCur;
   elif ( codCur > 0 )
     if ( GetAllCurrRate( dateSum, codCur, cbRate ) )
       outSum = Money( sumCur * cbRate );
     else
       outSum = sumCur;
     end;
   end;
 end;

 return ( outSum >= ControlSum );

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro sDateZero( _Date )
 var dDay, dMon, dYear, StrDate;
 DateSplit( _Date, dDay, dMon, dYear );
 StrDate = string( _Date );
 if( dDay < 10 )
    StrSet( StrDate, 1, "0" );
 end;
 return StrDate;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro sDate( _Date )
 return sDateZero( rslDate( _Date ) );
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro sTime( _Time )
 var rTime = SubStr( String( _Time ), 1, 5 );
 if ( SubStr( rTime, 1, 1 ) == " " )
   StrSet( rTime, 1, "0" );
 end;
 return rTime;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro sTimeSec( _Time )
 var rTime = SubStr( String( _Time ), 1, 8 );
 if ( SubStr( rTime, 1, 1 ) == " " )
   StrSet( rTime, 1, "0" );
 end;
 return rTime;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro sNumZero( _Num )
 var rNum = String( _Num:2 );
 if ( SubStr( rNum, 1, 1 ) == " " )
   StrSet( rNum, 1, "0" );
 end;
 return rNum;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro sCode113i( _Code )
 var sRet = _Code;
 if ( CodeFor( sRet ) < 32 )
   if ( StrLen( sRet ) > 1 )
     StrSet( sRet, 1, " " );
   else
     sRet = "";
   end;
 end;
 return sRet;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro countryNumCode( CodeLat3 )

 country.clear();
 country.rec.CodeLat3 = CodeLat3;
 if( country.getEQ() )
   return country.rec.CodeNum3;
 end;

 return "";

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro InsMes( _f, _n )

 if ( NOT Insert( _f ) )
   println( "Ошибка при записи в файл \"", _n, "\"" );
   Exit();
 end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro IsExOperatWithPD
                                
  if ( ( exoperat.rec.Kind_Oper ==  3 ) or
       ( exoperat.rec.Kind_Oper ==  4 ) or
       ( exoperat.rec.Kind_Oper == 20 ) or
       ( exoperat.rec.Kind_Oper == 21 ) or
       ( exoperat.rec.Kind_Oper == 38 ) or
       ( exoperat.rec.Kind_Oper == 39 ) or
       ( exoperat.rec.Kind_Oper == 40 ) or
       ( exoperat.rec.Kind_Oper == 41 )   )
    return true;
  end;

  return false;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro IsFirstExOperat

  var cmd = RsdCommand( "select 0 from dsb_casln_dbt c1, dsb_casln_dbt c2 "
                        "where c1.t_applicationKind1 = c2.t_applicationKind1 "
                        "  and c1.t_applicationKey1 = c2.t_applicationKey1 "
                        "  and c1.t_numLinkDoc < c2.t_numLinkDoc "
                        "  and c1.t_applicationKind2 = c2.t_applicationKind2 "
                        "  and c1.t_refValue2 = 0 "
                        "  and c2.t_refValue2 = 0 "
                        "  and c2.t_applicationKind2 = ? "
                        "  and c2.t_applicationKey2 = ? " );

  cmd.addParam( "p_appKind", RsdBp_In );
  cmd.value( "p_appKind" ) = exoperat.rec.ApplicationKind;
  cmd.addParam( "p_appKey", RsdBp_In );
  cmd.value( "p_appKey" ) = exoperat.rec.ApplicationKey;

  cmd.execute;

  var rs = RsdRecordset( cmd );

  return not rs.moveNext;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro CalcSumForPD

  var mainKind = 0;
  var mainKey  = "";
  var stat;

  exoperat.rec.IncomeAmount = $0L;
  exoperat.rec.OutAmount = $0L;
  paydocop.rec.Amount = 0;

  paydocop2.SetRecordAddr( exoperat2, 0, FldOffset( exoperat2, "ExtInfo" ), true );

  sb_casln.KeyNum = 1;
  sb_casln.Clear;
  sb_casln.rec.ApplicationKind2 = exoperat.rec.ApplicationKind;
  sb_casln.rec.ApplicationKey2  = exoperat.rec.ApplicationKey;

  if ( sb_casln.GetEQ )
    mainKind = sb_casln.rec.ApplicationKind1;
    mainKey  = sb_casln.rec.ApplicationKey1;
  end;

  if ( mainKey != "" )
    sb_casln.KeyNum = 0;
    sb_casln.Clear;
    sb_casln.rec.ApplicationKind1 = mainKind;
    sb_casln.rec.ApplicationKey1  = mainKey;

    stat = sb_casln.GetGE;

    while ( stat )

      if ( ( sb_casln.rec.ApplicationKind1 == mainKind ) and
           ( sb_casln.rec.ApplicationKey1  == mainKey  )    )

        if ( ( sb_casln.rec.ApplicationKind2 == 260 ) and ( sb_casln.rec.RefValue2 == 0 ) )

          exoperat2.Clear( );
          exoperat2.rec.ApplicationKind  = sb_casln.rec.ApplicationKind2;
          exoperat2.rec.ApplicationKey   = sb_casln.rec.ApplicationKey2;

          if ( exoperat2.GetEQ( ) and ( exoperat2.rec.Kind_Oper == exoperat.rec.Kind_Oper ) and 
                                      ( exoperat2.rec.IncomeRefVal == exoperat.rec.IncomeRefVal ) and
                                      ( exoperat2.rec.OutRefVal == exoperat.rec.OutRefVal )          )
            exoperat.rec.IncomeAmount = exoperat.rec.IncomeAmount + exoperat2.rec.IncomeAmount;
            exoperat.rec.OutAmount = exoperat.rec.OutAmount + exoperat2.rec.OutAmount;

            if ( ( exoperat2.rec.IncomeTypeValue == TYPERES_PAYDOC ) or ( exoperat2.rec.OutTypeValue == TYPERES_PAYDOC ) )
              paydocop.rec.Amount = paydocop.rec.Amount + paydocop2.rec.Amount;
            end;

          end;

        end;

        stat = sb_casln.Next;
      else
        stat = false;
      end;
    end;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ExtractEl113( mon, year )

 var vDate1 = Date( 1, mon, year );
 var vDate2 = Date( 1, mon, year );

 var flagDoc = FALSE;  /* Флаг включения документа в Выписку. */

 var NumReestr = 0;

 var codCurI, codCurO;
 var _time = "";
 var _rate = "";
 var _incomeISO = "", _incomeSum = $0, _sIncomeSum = "";
 var _outISO    = "", _outSum    = $0, _sOutSum    = "";
 var _isCard    = "";
 var _pdNum = "", _pdISO = "", _pdSum = $0, _sPdSum = "";
 var typePDSum;
 var _account = "";
 var _warrant = "";
 var _country = "";
 var _clientF = "", _clientI = "", _clientO = "";
 var _documentM = "", _documentS = "", _documentN = "";
 var _address = "";

 var rMon, rYear;

 var iRec = 0;

 var lenUNH = 0;
 var nCES02 = 0;

 var lenZGL = 0;

 var cmd, rs;

 if ( iFindBank( CODEKIND_BIC, OurBank_BIC, OurBank_Name ) != TRUE )
   OurBank_Name = "";
 end;

 /*** Этап 1. Формирование временного файла уровня 1. ***/

 if ( NOT Open( tmp1, TMPF_Name1 ) )
   println( "Невозможно открыть временный файл этапа 1 - запись" );
   Exit();
 end;

 SetDelim( "" );  // Задание символа-разделителя.

 if ( mon == 12 )
   vDate2 = Date( 1, 1, year+1 );
 else
   vDate2 = Date( 1, mon+1, year );
 end;

 cmd = RsdCommand( "select dreestr_2_dbt.t_ApplicationKind as ApplKind, " +
                          "dreestr_2_dbt.t_ApplicationKey  as ApplKey, " +
                          "dreestr_2_dbt.t_Date            as DateR, " +
                          "dreestr_2_dbt.t_Number          as NumR, " +
                          "dreestr_2_dbt.t_Code113i        as CodeR " +
                   "from  dreestr_2_dbt " +
                   "where dreestr_2_dbt.t_branch = " + String( FNCash ) + " and " +
                         "dreestr_2_dbt.t_CurDate >= " + sqlDateToStr( vDate1 ) + " and " +
                         "dreestr_2_dbt.t_CurDate < "  + sqlDateToStr( vDate2 ) );

 cmd.execute;

 rs = RsdRecordset( cmd );

 while ( rs.moveNext )

   flagDoc = FALSE;  /* Флаг включения документа в Выписку. */

   _time = "";
   _rate = "";

   _incomeISO = "";
   _incomeSum = $0;
   _sIncomeSum = "";

   _outISO    = "";
   _outSum    = $0;
   _sOutSum    = "";

   _isCard    = "";
   _pdNum = "";
   _pdISO = "";
   _pdSum = $0;
   _sPdSum = "";

   _account = "";

   _warrant = "";

   _country = "";
   _clientF = "";
   _clientI = "";
   _clientO = "";
   _documentM = "";
   _documentS = "";
   _documentN = "";
   _address = "";

   // Вкладные документы.
   if ( rs.value( "ApplKind" ) == SB_DEPOSIT )

     sbdepdoc.clear();
     sbdepdoc.keynum( 3 );

     sbdepdoc.rec.iApplicationKind = rs.value( "ApplKind" );
     sbdepdoc.rec.ApplicationKey   = rs.value( "ApplKey" );

     if ( sbdepdoc.getEQ()  AND
          ( sbdepdoc.rec.Action < 2 )  AND
          IsServDoc( sbdepdoc.rec )    AND
          ( sbdepdoc.rec.FlagStorn == "" ) )

       // flagDoc = TRUE;  /* Флаг включения документа в Выписку. */

       // NumReestr = NumReestr + 1;

       _time = Time( int( substr( sbdepdoc.rec.ApplicationKey, 11, 2 ) ),
                     int( substr( sbdepdoc.rec.ApplicationKey, 13, 2 ) ),
                     int( substr( sbdepdoc.rec.ApplicationKey, 15, 2 ) ) );

       _account = sbdepdoc.rec.Account;

       if( sbdepdoc.rec.InSum )
         _incomeISO = GetCurrNumericalCode( sbdepdoc.rec.Code_Currency );
         _incomeSum = sbdepdoc.rec.InSum;
         _sIncomeSum = String( _incomeSum:0:2 );
         //checkSum = DefCheckSumForControl( sbdepdoc.rec.InSum, sbdepdoc.rec.Code_Currency, sbdepdoc.rec.Date_Document );
       else
         _outISO  = GetCurrNumericalCode( sbdepdoc.rec.Code_Currency );
         _outSum  = sbdepdoc.rec.OutSum;
         _sOutSum   = String( _outSum:0:2 );
         //checkSum = DefCheckSumForControl( sbdepdoc.rec.OutSum, sbdepdoc.rec.Code_Currency, sbdepdoc.rec.Date_Document );
       end;

       if ( ( sbdepdoc.rec.Author == ACCOUTN_PROXY )  OR
            ( sbdepdoc.rec.Author == ACCOUNT_RETRAST ) )
         _warrant = "X";  // операция по доверенности
       end;

       if ( ClientList.GetRecord( sbdepdoc.rec.CodClient ) )
         _country = countryNumCode( ClientList.curRec.rec.Country );
         // if ( ( checkSum >= ControlSum ) or ( _warrant == "X" ) )
         if ( TRUE )
           _clientF = ClientList.CurRec.Rec.Name1;
           _clientI = ClientList.CurRec.Rec.Name2;
           _clientO = ClientList.CurRec.Rec.Name3;
           _documentM = GetNameOfPAPRKIND( ClientList.CurRec.Rec.PaperKind );
           _documentS = ClientList.CurRec.Rec.PaperSeries;
           _documentN = ClientList.CurRec.Rec.PaperNumber;
           _address  = ClientList.curRec.MakeFullStrAddress();
         end;
       end;

       if ( Operation_Sum( sbdepdoc.rec.InSum,
                           sbdepdoc.rec.Code_Currency,
                           sbdepdoc.rec.Date_Document ) )
         flagDoc = TRUE;  /* Флаг включения документа в Выписку. */
       end;

     end;
   end;

   // Валютнообменные операции.
   if ( rs.value( "ApplKind" ) == SB_EXCHNDOC )

     exoperat.clear();
     exoperat.rec.ApplicationKind = rs.value( "ApplKind" );
     exoperat.rec.ApplicationKey  = rs.value( "ApplKey" );
     paydocop.SetRecordAddr( exoperat, 0, FldOffset( exoperat, "ExtInfo" ), true );

     if ( exoperat.getEQ( )  AND ( exoperat.rec.Action < 2 )  AND
          ( not IsExOperatWithPD( ) OR IsFirstExOperat( ) ) and
          ( ( exoperat.rec.IsSuspended == "" )  OR
            ( exoperat.rec.IsSuspended == "X" ) ) )

        if ( IsExOperatWithPD( ) )
          CalcSumForPD( );
        end;

       _time = sTime( exoperat.rec.Time );
       _rate = String( exoperat.rec.Kurs:0:2 );

       if ( ( exoperat.rec.Kind_Oper == CDF_OperCardTake )  OR
            ( exoperat.rec.Kind_Oper == CDF_OperCardGive ) )
         _isCard = "1";
       else
         _isCard = "";
       end;

       if ( ( exoperat.rec.IncomeTypeValue == TYPERES_PAYDOC )  AND
            exoperat.rec.IncomeAmount )
         if ( ( exoperat.rec.Kind_Oper == CDF_OperPayDocIncasso       )  OR
              ( exoperat.rec.Kind_Oper == CDF_OperPayDocExpert        )  OR
              ( exoperat.rec.Kind_Oper == CDF_OperPayDocReturnIncasso )  OR
              ( exoperat.rec.Kind_Oper == CDF_OperPayDocReturnExpert  ) )
           _pdNum = "1"; /* Структура incasso.str, в которой нет количества */
         else
           _pdNum = String( paydocop.rec.Amount ); /* Структура paydocop.str */
         end;
         codCurI = FindCodCurByRefVal( exoperat.rec.IncomeRefVal );
         _pdISO = String( GetCurrNumericalCode( codCurI ) );
         _pdSum = exoperat.rec.IncomeAmount;
         _sPdSum = String( _pdSum:0:2 );
         typePDSum = 1;
         if ( Operation_Sum( _pdSum, codCurI, exoperat.rec.CurDate ) )
           flagDoc = TRUE;  /* Флаг включения документа в Выписку. */
         end;
       elif ( ( exoperat.rec.OutTypeValue == TYPERES_PAYDOC )  AND
              exoperat.rec.OutAmount )
         if ( ( exoperat.rec.Kind_Oper == CDF_OperPayDocIncasso       ) or
              ( exoperat.rec.Kind_Oper == CDF_OperPayDocExpert        ) or
              ( exoperat.rec.Kind_Oper == CDF_OperPayDocReturnIncasso ) or
              ( exoperat.rec.Kind_Oper == CDF_OperPayDocReturnExpert  )   )
           _pdNum = "1"; /* Структура incasso.str, в которой нет количества */
         else
           _pdNum = String( paydocop.rec.Amount ); /* Структура paydocop.str */
         end;
         codCurO = FindCodCurByRefVal( exoperat.rec.OutRefVal );
         _pdISO = String( GetCurrNumericalCode( codCurO ) );
         _pdSum = exoperat.rec.OutAmount;
         _sPdSum = String( _pdSum:0:2 );
         typePDSum = 2;
         if ( Operation_Sum( _pdSum, codCurO, exoperat.rec.CurDate ) )
           flagDoc = TRUE;  /* Флаг включения документа в Выписку. */
         end;
       end;

       if ( ( exoperat.rec.IncomeTypeValue == TYPERES_CURRENCY )  AND
              exoperat.rec.IncomeAmount )

         // flagDoc = TRUE;  /* Флаг включения документа в Выписку. */

         // NumReestr = NumReestr + 1;

         codCurI = FindCodCurByRefVal( exoperat.rec.IncomeRefVal );
         _incomeISO = String( GetCurrNumericalCode( codCurI ) );
         _incomeSum = exoperat.rec.IncomeAmount;
         _sIncomeSum = String( _incomeSum:0:2 );
         codCurO = FindCodCurByRefVal( exoperat.rec.OutRefVal );
         _outISO    = String( GetCurrNumericalCode( codCurO ) );
         _outSum    = exoperat.rec.OutAmount;
         _sOutSum   = String( _outSum:0:2 );
         //_pdNum     = "";
         //_pdISO     = "";

         if ( Operation_Sum( _incomeSum, codCurI, exoperat.rec.CurDate ) )
           flagDoc = TRUE;  /* Флаг включения документа в Выписку. */
         end;

       elif ( ( exoperat.rec.OutTypeValue == TYPERES_CURRENCY )  AND
              exoperat.rec.OutAmount )

         // flagDoc = TRUE;  /* Флаг включения документа в Выписку. */

         // NumReestr = NumReestr + 1;

         codCurO = FindCodCurByRefVal( exoperat.rec.OutRefVal );
         _outISO = String( GetCurrNumericalCode( codCurO ) );
         _outSum = exoperat.rec.OutAmount;
         _sOutSum = String( _outSum:0:2 );

         if ( Operation_Sum( _outSum, codCurO, exoperat.rec.CurDate ) )
           flagDoc = TRUE;  /* Флаг включения документа в Выписку. */
         end;

       end;

       _account = ПолучитьВкладнойСчетДляВОО( exoperat.rec.ApplicationKind,
                                              exoperat.rec.ApplicationKey );

       if ( ClientList.GetRecord( exoperat.rec.ClientCode ) )
         _country = countryNumCode( ClientList.curRec.rec.Country );
         // if ( checkSum >= ControlSum )
         if ( TRUE )
           _clientF = ClientList.CurRec.Rec.Name1;
           _clientI = ClientList.CurRec.Rec.Name2;
           _clientO = ClientList.CurRec.Rec.Name3;
           _documentM = GetNameOfPAPRKIND( ClientList.CurRec.Rec.PaperKind );
           _documentS = ClientList.CurRec.Rec.PaperSeries;
           _documentN = ClientList.CurRec.Rec.PaperNumber;
           _address  = ClientList.curRec.MakeFullStrAddress( );
         end;
       end;

       if ( ( ( _incomeISO == _pdISO )  OR  ( _outISO == _pdISO ) )  AND
            ( _pdISO != "" ) )
         _rate = "";
       end;

       if ( ( _incomeISO == _outISO )  AND  ( _outISO != "" ) )
         _rate = "";
       end;

       if ( ( ( _incomeISO == "" )  OR  ( _outISO == "" ) )  AND
            ( _pdISO == "" ) )
         _rate = "";
       end;

       if ( ( _incomeISO == "" )  AND  ( _outISO == "" ) )
         _rate = "";
       end;

     end;

   end;

   // Прочие операции.
   if ( rs.value( "ApplKind" ) == SB_PAYDOC )

     pay_doc.clear();
     pay_doc.keynum( 2 );

     pay_doc.rec.iApplicationKind = rs.value( "ApplKind" );
     pay_doc.rec.ApplicationKey   = rs.value( "ApplKey" );

     if ( pay_doc.getEQ()  AND
          ( pay_doc.rec.Action < 2 )  AND
            ( ( pay_doc.rec.IsSuspended == "" )  OR
              ( pay_doc.rec.IsSuspended == "X" ) ) )

       // flagDoc = TRUE;  /* Флаг включения документа в Выписку. */

       // NumReestr = NumReestr + 1;

       _time = Time( int( substr( pay_doc.rec.ApplicationKey, 11, 2 ) ),
                     int( substr( pay_doc.rec.ApplicationKey, 13, 2 ) ),
                     int( substr( pay_doc.rec.ApplicationKey, 15, 2 ) ) );

       if ( pay_doc.rec.GroupOpert == 41 )
         _outISO = String( GetCurrNumericalCode( pay_doc.rec.CodCur ) );
         _outSum = pay_doc.rec.InSum;
         _sOutSum = String( _outSum:0:2 );

         if ( Operation_Sum( pay_doc.rec.InSum,
                             pay_doc.rec.CodCur,
                             pay_doc.rec.Date_Document ) )
           flagDoc = TRUE;  /* Флаг включения документа в Выписку. */
         end;

       elif ( pay_doc.rec.GroupOpert == 31)

         paydoc_cnv.SetRecordAddr( pay_doc, 0, 0, true );

         _rate = paydoc_cnv.rec.Rate;
         _outISO = String( GetCurrNumericalCode( pay_doc.Rec.Codcur ) );
         _outSum = pay_doc.Rec.Insum;
         _sOutSum = String( _outSum:0:2 );

         if ( ( _incomeISO == "" )  OR  ( _outISO == "" ) )
           _rate = "";
         end;

         if ( Operation_Sum( pay_doc.rec.InSum,
                             pay_doc.rec.CodCur,
                             pay_doc.rec.Date_Document ) )
           flagDoc = TRUE;  /* Флаг включения документа в Выписку. */
         end;

       else

         _incomeISO  = String( GetCurrNumericalCode( pay_doc.rec.CodCur ) );
         _incomeSum  = pay_doc.rec.InSum;
         _sIncomeSum = String( _incomeSum:0:2 );

         if ( Operation_Sum( pay_doc.rec.InSum,
                             pay_doc.rec.CodCur,
                             pay_doc.rec.Date_Document ) )
           flagDoc = TRUE;  /* Флаг включения документа в Выписку. */
         end;

       end;

       if ( ( pay_doc.rec.GroupOpert == 41 )  OR
            ( pay_doc.rec.GroupOpert == 11 ) )
         if ( GetVarSize( pay_doc ) == GetRecordSize( paydoc_n36 ) -
                                       GetRecordSize( pay_doc ) )
           paydoc_n36.SetRecordAddr( pay_doc, 0, 0, true );
           _account = paydoc_n36.rec.cardNumber;
           _isCard  = "X";
         end;
       end;

       if ( pay_doc.rec.GroupOpert == 31 ) /* Конверсия по вкладам */
         paydoc_cnv.SetRecordAddr( pay_doc, 0, 0, true );
         if ( paydoc_cnv.rec.Kind != "" )
           if ( ( Index( StrUpr( pay_doc.rec.Ground ), StrUpr( "Счет"        ) ) >  0 )  AND  /* conv_tax.mac */
                ( Index( StrUpr( pay_doc.rec.Ground ), StrUpr( "Счет вклада" ) ) == 0 )    )
             _account = Trim( SubStr( pay_doc.rec.Ground, 5 ) );
             if ( Index( _account, " " ) > 1 )
               _account = Trim( SubStr( _account, 1, Index( _account, " " ) - 1 ) );
             end;
           end;
         end;
       end;

       // if ( checkSum >= ControlSum )
       if ( TRUE )
          if( ClientList.GetRecord( pay_doc.rec.CodClient ) )
             _country = countryNumCode( ClientList.curRec.rec.Country );
             _clientF = ClientList.CurRec.Rec.Name1;
             _clientI = ClientList.CurRec.Rec.Name2;
             _clientO = ClientList.CurRec.Rec.Name3;
             _documentM = GetNameOfPAPRKIND( ClientList.CurRec.Rec.PaperKind );
             _documentS = ClientList.CurRec.Rec.PaperSeries;
             _documentN = ClientList.CurRec.Rec.PaperNumber;
             _address     = ClientList.curRec.MakeFullStrAddress();
          else
             _country = "";
             _clientF = pay_doc.rec.ClientLastName;
             _clientI = pay_doc.rec.ClientFirstName;
             _clientO = pay_doc.rec.ClientSecondName;
             /*
             _document    = PaperTypeName( pay_doc.rec.PersIDType ) + pay_doc.rec.PassportSer + "№" +
                            pay_doc.rec.PassportNumb + pay_doc.rec.PassportDate + pay_doc.rec.PassportIssuer;
             _documentM = PaperTypeName( pay_doc.rec.PersIDType );
             */
             _documentM = GetNameOfPAPRKIND( pay_doc.rec.PersIDType );
             _documentS = pay_doc.rec.PassportSer;
             _documentN = pay_doc.rec.PassportNumb;
             _address     = pay_doc.rec.ClientAddress;
          end;
       elif ( ClientList.GetRecord( pay_doc.rec.CodClient ) )
         _country = countryNumCode( ClientList.curRec.rec.Country );
       end;

     end;

   end;

   if ( flagDoc )

     NumReestr = NumReestr + 1;

     tmp1.str = String( NumReestr )              + "" +  //  1
                sDate ( rs.value( "DateR" ) )    + "" +  //  2
                String( rs.value( "NumR" ) )     + "" +  //  3
                sTime( _time )                   + "" +  //  4
                sCode113i( rs.value( "CodeR" ) ) + "" +  //  5
                _rate                            + "" +  //  6
                _incomeISO                       + "" +  //  7
                _sIncomeSum                      + "" +  //  8
                _outISO                          + "" +  //  9
                _sOutSum                         + "" +  // 10
                _isCard                          + "" +  // 11
                _pdNum                           + "" +  // 12
                _pdISO                           + "" +  // 13
                _sPdSum                          + "" +  // 14
                Acc_Form(_account)               + "" +  // 15
                _warrant                         + "" +  // 16
                _country                         + "" +  // 17
                _clientF                         + "" +  // 18
                _clientI                         + "" +  // 19
                _clientO                         + "" +  // 20
                _documentM                       + "" +  // 21
                _documentS                       + "" +  // 22
                _documentN                       + "" +  // 23
                _address;                                 // 24

     InsMes( tmp1, TMPF_Name1 );
   end;

 end;

 Close( tmp1 );

 /*** Этап 2. Формирование временного файла уровня 2. ***/

 if ( NOT Open( tmr1, TMPF_Name1 ) )
   println( "Невозможно открыть временный файл этапа 1 - чтение." );
   Exit();
 end;

 if ( NOT Open( tmp2, TMPF_Name2 ) )
   println( "Невозможно открыть временный файл этапа 2 - запись." );
   Exit();
 end;

 while ( Next( tmr1 ) )

   nCES02 = nCES02 + 1;

   tmp2.str = "CES02";

   iRec = 0;

   while ( iRec < 25 )

     if ( StrLen( tmr1( iRec ) ) > 0 )

       tmp2.str = tmp2.str + "+" + sNumZero( iRec + 1 ) + tmr1( iRec );

     end;

     iRec = iRec + 1;

   end;

   tmp2.str = tmp2.str + "'";

   InsMes( tmp2, TMPF_Name2 );

 end;

 Close( tmr1 );
 Close( tmp2 );

 /*** Этап 3. Формирование временного файла уровня 3. ***/

 if ( NOT Open( tmr2, TMPF_Name2 ) )
   println( "Невозможно открыть временный файл этапа 2 - чтение." );
   Exit();
 end;

 if ( NOT Open( tmp3, TMPF_Name3 ) )
   println( "Невозможно открыть временный файл этапа 3 - запись." );
   Exit();
 end;

 tmp3.str = "DSI+001+ОПЕРАЦИИ С НАЛИЧНОЙ ВАЛЮТОЙ И ЧЕКАМИ'";
 lenUNH = lenUNH + StrLen( tmp3.str ) + 2;
 InsMes( tmp3, TMPF_Name3 );

 tmp3.str = "CES01+01" + addrBranch() + "+02" + String( nCES02 ) + "'";
 lenUNH = lenUNH + StrLen( tmp3.str ) + 2;
 InsMes( tmp3, TMPF_Name3 );

 while ( Next( tmr2 ) )

   tmp3.str = tmr2.str;
   lenUNH = lenUNH + StrLen( tmp3.str ) + 2;
   InsMes( tmp3, TMPF_Name3 );

 end;

 tmp3.str = "UNT+" + String( nCES02 + 2 ) + "'";
 lenUNH = lenUNH + StrLen( tmp3.str ) + 2;
 InsMes( tmp3, TMPF_Name3 );

 Close( tmp3 );

 /*** Этап 4. Формирование временного файла уровня 4. ***/

 if ( NOT Open( tmr3, TMPF_Name3 ) )
   println( "Невозможно открыть временный файл этапа 3 - чтение." );
   Exit();
 end;

 if ( NOT Open( tmp4, TMPF_Name4 ) )
   println( "Невозможно открыть временный файл этапа 4 - запись." );
   Exit();
 end;

 tmp4.str = "UNH+" + String( 7 + StrLen( String( lenUNH ) ) + lenUNH ) + "'";
 InsMes( tmp4, TMPF_Name4 );

 while ( Next( tmr3 ) )
   tmp4.str = tmr3.str;
   InsMes( tmp4, TMPF_Name4 );
 end;

 Close( tmp4 );

 /*** Этап 5. Формирование временного файла уровня 5. ***/

 if ( NOT Open( tmp5, TMPF_Name5 ) )
   println( "Невозможно открыть временный файл этапа 5 - запись." );
   Exit();
 end;

 if ( mon < 12 )
   rMon  = mon + 1;
   rYear = year;
 else
   rMon  = 1;
   rYear = year + 1;
 end;

 tmp5.str = "DTM+01" + sDateZero( Date() ) +
               "+02" + sTimeSec( Time() ) +
               "+05" + sNumZero( rMon ) + sNumZero( rMon ) +
                       SubStr( String( rYear ), 3, 2 ) +
               "+06" + String( nCES02 ) + "'";
 lenZGL = lenZGL + StrLen( tmp5.str ) + 2;
 InsMes( tmp5, TMPF_Name5 );

 tmp5.str = "NAD";

 if ( StrLen( OurBank_RegNum ) > 0 )
   tmp5.str = tmp5.str + "+01" + OurBank_RegNum;
 end;

 if ( StrLen( OurBank_OKATO ) > 0 )
   tmp5.str = tmp5.str + "+02" + OurBank_OKATO;
 end;

 if ( StrLen( OurBank_OKPO ) > 0 )
   tmp5.str = tmp5.str + "+03" + OurBank_OKPO;
 end;

 if ( StrLen( OurBank_BIC ) > 0 )
   tmp5.str = tmp5.str + "+04" + OurBank_BIC;
 end;

 if ( StrLen( OurBank_OGRN ) > 0 )
   tmp5.str = tmp5.str + "+05" + OurBank_OGRN;
 end;

 if ( StrLen( OurBank_Name ) > 0 )
   tmp5.str = tmp5.str + "+06" + OurBank_Name;
 end;

 tmp5.str = tmp5.str + "'";

 lenZGL = lenZGL + StrLen( tmp5.str ) + 2;
 InsMes( tmp5, TMPF_Name5 );

 tmp5.str = "STA+01" + {FIO_oper} + "'";
 lenZGL = lenZGL + StrLen( tmp5.str ) + 2;
 InsMes( tmp5, TMPF_Name5 );

 Close( tmp5 );

 /*** Этап 5. Формирование Электронной выписки из Реестра ВОО по 113-И. ***/

 FN_EL = ExportDir + SubStr( OurBank_RegNum, 1, 4 ) +
                     SubStr( OurBank_RegNum, 6, 4 ) + ".v" + sNumZero( mon );

 if ( ExistFile( FN_EL, 0 ) )
   DelFile( FN_EL );
 end;

 if ( NOT Open( f_el, FN_EL ) )
   println( "Ошибка открытия файла: ", FN_EL );
   Exit();
 end;

 if ( NOT Open( tmr4, TMPF_Name4 ) )
   println( "Невозможно открыть временный файл этапа 4 - чтение." );
   Exit();
 end;

 if ( NOT Open( tmr5, TMPF_Name5 ) )
   println( "Невозможно открыть временный файл этапа 5 - чтение." );
   Exit();
 end;

 f_el.str = "UNB+РЕЕСТРЫ'";
 InsMes( f_el, FN_EL );

 f_el.str = "ZGL+" + String( 68 + StrLen( String( lenZGL ) ) + lenZGL ) +
            "+01ИНФОРМАЦИЯ ИЗ РЕЕСТРА ОПЕРАЦИЙ С НАЛИЧНОЙ ВАЛЮТОЙ И ЧЕКАМИ'";
 InsMes( f_el, FN_EL );

 // Копирование записей заголовка.
 while ( Next( tmr5 ) )
   f_el.str = tmr5.str;
   InsMes( f_el, FN_EL );
 end;

 // Копирование записей по операциям.
 while ( Next( tmr4 ) )
   f_el.str = tmr4.str;
   InsMes( f_el, FN_EL );
 end;

 f_el.str = "UNZ+1'";
 InsMes( f_el, FN_EL );

 Close( f_el );

 println( "Файл выписки с именем \"" + FN_EL + "\" сформирован." );

end;
