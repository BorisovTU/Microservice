/****************************************************************************
   RS-Retail X.0                 (СБЕРБАНК)
   Subsystem:   Сервис АРМов
                ~~~~~~~~~~~~~~
   Description: Отчёт о выгрузке/загрузке сессии

   ------------------------------------------------------------------------
   Y2K (c) R-Style SoftWare Lab.
****************************************************************************/

import FT184_CloseAccountReport, CommonInter;

/* Файлы */
file sb_sesn ( sb_sesn  ) key 0;
file fc_sesn ( fc_sesn  ) key 0;
file sb_infss( sb_infss ) key 0;
file fc_infss( fc_infss ) key 0;

/* Константы */
const
   SESSION_INITIALIZED = 0,
   SESSION_CREATED     = 1,
   SESSION_CONFIRMED   = 3;


/************************************************************************\
|                   Позиция последнего включения символа                 |
\************************************************************************/
macro StrRChr( str, sym )
 var i = index( str, sym ),
     k = 0;
  while( i )
    str = substr( str, i+1 );
    k = k + i;
    i = index( str, sym );
  end;
  return k;
end;


/************************************************************************\
|              Разбить входное имя файла Path на составляющие            |
\************************************************************************/
macro fnsplit
(
  Path,        /* Входное полное имя файла     */
  Dir,         /* Директория вместе с диском   */
  Fname,       /* Имя файла без расширения     */
  Ext          /* Расширение                   */
)
  var last = strrchr( Path = trim(Path), "\\" );
  Dir = Fname = Ext = "";
  if( last )
    Dir  = substr( Path, 1, last-1 );
    Fname= substr( Path, last+1 );
  else
    Fname = Path;
  end;
  if( last = strrchr( Fname, "." ) )
    Ext   = substr( Fname, last+1 );
    Fname = substr( Fname, 1, last-1 );
  end;
  setparm( 1, Dir   );
  setparm( 2, Fname );
  setparm( 3, Ext   );
end;

/************************************************************************\
|                      Протокол синхронизации                            |
\************************************************************************/
private macro SyncReport()
  file err() txt;
  var name;

  name = "..\\txtfile\\sess_sync_rep." + string(GetCNum());
  if (open(err, name))
    //ViewFile(err);
    while (next(err))
      println(err.str);
    end;
    close(err);
    DelFile(name);
  end;
end;

/************************************************************************\
|                        Собсно отчёт                                    |
\************************************************************************/
macro Report( выгрузка, sesn, infss, FNCash, NumSession, NameFile, NumRecord )

  var
    found, status;

  sesn.FNCash = FNCash;
  sesn.NumSession = NumSession;
  if( not getEQ( sesn ) )
    msgbox( "Не найдена информация о сессии!" );
    return;
  end;

  if( выгрузка )
     [
                       Информация о сессии выгрузки № ######
                  (даты документов от ########### до ###########)
 
      ─────┬────────────┬─────────────────────────┬─────────┬─────────────────────
      Номер│ Имя трансп.│          Вид            │Выгружено│    Дата и время
      файла│   файла    │       информации        │ записей │
      ─────┼────────────┼─────────────────────────┼─────────┼─────────────────────
     ]( sesn.NumSession, sesn.MinSBDocDate, sesn.MaxSBDocDate );
  else
     [
                       Информация о сессии загрузки № ######
                  (даты документов от ########## до ##########)
 
      ─────┬────────────┬─────────────────────────┬──────┬──────┬───────────────────
      Номер│ Имя трансп.│          Вид            │Всего │Загр. │    Дата и время
      файла│   файла    │       информации        │ зап. │ зап. │
      ─────┼────────────┼─────────────────────────┼──────┼──────┼───────────────────
     ]( sesn.NumSession, sesn.MinSBDocDate, sesn.MaxSBDocDate );
  end;

  infss.FNCash = sesn.FNCash;
  infss.NumSession = sesn.NumSession;
  found = getGE( infss );
  while( found 
         and ( infss.FNCash == sesn.FNCash ) 
         and ( infss.NumSession == sesn.NumSession ) )
    if( выгрузка )
      [#####│############│#########################│#########│########## ########]
      ( infss.NumFile:c, infss.TR_NameFile, infss.FileDescription,
        infss.ColRecord, infss.DateConf, infss.TimeConf );
    else
      [#####│############│#########################│######│######│########## ########]
      ( infss.NumFile:c, infss.TR_NameFile, infss.FileDescription, 
        infss.ColRecord, infss.RealColRecord, infss.DateConf, infss.TimeConf );
    end;
    found = next( infss );
  end;

  // Протокол синхронизации
  SyncReport();
  
  if(   ( codefor( sesn.StateSess ) == SESSION_CREATED )
     or ( codefor( sesn.StateSess ) == SESSION_CONFIRMED )   )
    if( выгрузка )
      status = "Сессия выгружена успешно";
    else
      status = "Сессия загружена успешно";
    end;
    [
     #]( status );

	// Напечатаем отчет о закрытых счетах	
    CheckAndPrint( sesn.NumSession );

  elif( codefor( sesn.StateSess ) == SESSION_INITIALIZED )
    if( выгрузка )
      status = "ОШИБКА ПРИ ВЫГРУЗКЕ СЕССИИ: " + 
               " файл " + NameFile + " запись " + NumRecord;
    else
      status = "ОШИБКА ПРИ ЗАГРУЗКЕ СЕССИИ: " + 
               " файл " + NameFile + " запись " + NumRecord;
    end;
    [
     #]( status );
    println( "Обратитесь к системному администратору!" );
  end;
end;

/************************************************************************\
|                        Запускалка отчёта                               |
\************************************************************************/
macro RunReport( выгрузка, FNCash, NumSession, NameFile, NumRecord )

  var
    Dir, Fname, Ext;

  fnsplit( NameFile, Dir, Fname, Ext );

  if( выгрузка )
     Report( выгрузка, sb_sesn, sb_infss, FNCash, NumSession, 
             Fname + "." + Ext, NumRecord );
  else
     Report( выгрузка, fc_sesn, fc_infss, FNCash, NumSession, 
             Fname + "." + Ext, NumRecord );
  end;

end;


/*----------------------------------------------------------------------*\
                          Проверка макроса
\*----------------------------------------------------------------------*/
/*
  RunReport( 0, 2, 3, "", 0 );
  End.
*/
