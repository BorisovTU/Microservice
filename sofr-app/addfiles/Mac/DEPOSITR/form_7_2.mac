/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  * 
*                                                                          *
*  Проценты, выплаченные наличными				           *
*                                                                          *
*  Идея (с) Лебедев С.В.                                                   *
*  14.05.2001                                                              *
\**************************************************************************/

import DeprIntr, IsSrvDoc, SqlConv;

/* Файлы для работы макрофайла ========================================== */
file currency ( currency ) key 0;                // Справочник валют
/*file listfdep ( listfdep ) key 0;*/                // Справочник филиалов
var sb_dtyp  = TBFile( "sb_dtyp.dbt",  "r"    ); // Справочник видов вкладов
var depositr = TBFile( "depositr.dbt", "r", 6 ); // Счета вкладчиков
var sbdepdoc = TBFile( "sbdepdoc.dbt", "r", 6 ); // Документы по счетам

var depclnt = TClientList;        /* Справочник вкладчиков     */

/* Переменные для работы макрофайла ===================================== */
var FlagCur    = NumFlagCur(); /* Валютность                 */
var Branch     = NumFNCash();  /* Номер филиала              */
private var {Name_Bank};       /* Название банка             */  
var {NumberBranch};
var WasAccKind = FALSE;        /* По данному вкладу был счет */
var WasAccBA   = FALSE;        /* По данному БС был счет     */
var WasAccCur  = FALSE;        /* По данной валюте был счет  */
private var counterAll = 0;
private var counterSub = 0;

array ABalAccs;
array ABranches;

array MonthName;
MonthName( 0) = "январь";    MonthName( 1) = "февраль";
MonthName( 2) = "март";      MonthName( 3) = "апрель";
MonthName( 4) = "май";       MonthName( 5) = "июнь";
MonthName( 6) = "июль";      MonthName( 7) = "август";
MonthName( 8) = "сентябрь";  MonthName( 9) = "октябрь";
MonthName(10) = "ноябрь";    MonthName(11) = "декабрь";

var   KindNumAcc     = 0;
var   KindPercPut    = $0L;
var   KindPercOut    = $0L;
var   BANumAcc       = 0;
var   BAPercPut      = $0L;
var   BAPercOut      = $0L;

var   MonthNumber;
var   StartDate, EndDate;
var   NumBA = "";

/**************************************************************************\
|                      Определение отчетного месяца                        |
\**************************************************************************/
macro Hand1er (IP, cmd, id, key)

  var stat = CM_DEFAULT, i;

  /* =================================================================== */
  if (cmd == DLG_KEY)
    /* Enter --------------------------------------------------------- */
    if (key == 13)
      if (id == 0)
        SetFocus(IP,1);
        stat = CM_IGNORE;
      end;
    /* Esc ----------------------------------------------------------- */
    elif (key == 523)
      stat = CM_CANCEL;
    /* F3 ------------------------------------------------------------ */
    elif (key == 317)
      if (id == 0)
        i = Menu(MonthName," ~ESC~ Отказ  ~ENTER~ Выбор","Месяц");
        if (i >= 0)
          IP.Month = MonthName(i);
          MonthNumber = i + 1;
        end;
      end;
    end;
  /* =================================================================== */
  elif (cmd == DLG_SAVE)
    ;
  end;

  return stat;

end;

/**************************************************************************\
|                      Определение отчетного месяца                        |
\**************************************************************************/
macro DefDates
                                  
  var stat;
  var d, m, y;
  record Period (MNTHYEAR) dialog;

  DateSplit( {curdate}, d, m, y );
  m = m - 1;
  if ( m <= 0 )
    m = 12;
    y = y - 1;
  end;

  if ( y <= 0 );
    y = 2000;
    m = 1;
  end;

  Period.Month = MonthName( m - 1 );
  Period.Year = y;
  MonthNumber = m;
  stat = RunDialog(Period, "Hand1er");
  if (stat)
    StartDate = Date(1, MonthNumber, Period.Year);
    if (MonthNumber == 12)
      EndDate = Date(31, MonthNumber, Period.Year);
    else
      EndDate = Date(1, MonthNumber + 1, Period.Year) - 1;
    end;
  else
    Exit( 1 );
  end;

end;

/**************************************************************************\
|                      Определение балансовых счетов                       |
\**************************************************************************/
macro DefBalAccs

  var stat;
  var FindBA;
  var FindBA_NotRez;
  var TmpVal;
  var i = 0;
  var j = 0;

  Message(" Определение балансовых счетов...");

  /* Получение массива всех возможных балансовых счетов */
  sb_dtyp.addFilter( "t_FlagCur = " + String( FlagCur ) );

  sb_dtyp.KeyNum = 2;
  sb_dtyp.Clear;
  sb_dtyp.rec.FlagCur = FlagCur;
  sb_dtyp.rec.Kind    = "";
  stat = sb_dtyp.GetGE;
  while ( stat )
    if ( sb_dtyp.rec.FlagCur == FlagCur )
      if ( ASize( ABalAccs ) == 0 )
        ABalAccs( 0 ) = Trim( sb_dtyp.rec.BalAcc );
      end;
      FindBA        = FALSE;
      FindBA_NotRez = FALSE;
      i = 0;
      while ( i < ASize( ABalAccs ) )
        if ( ABalAccs( i ) == Trim( sb_dtyp.rec.BalAcc ) )
          FindBA = TRUE;
        end;
        if ( ABalAccs( i ) == Trim( sb_dtyp.rec.BalAccNotRez ) )
          FindBA_NotRez = TRUE;
        end;
        i = i + 1;
      end;
      if ( FindBA == FALSE )
        ABalAccs( ASize( ABalAccs ) ) = Trim( sb_dtyp.rec.BalAcc );
      end;
      if ( FindBA_NotRez == FALSE )
        if ( Trim( sb_dtyp.rec.BalAcc ) != Trim( sb_dtyp.rec.BalAccNotRez ) )
          ABalAccs( ASize( ABalAccs ) ) = Trim( sb_dtyp.rec.BalAccNotRez );
        end;
      end;
      stat = sb_dtyp.Next;
    else
      stat = FALSE;
    end;
  end;

  sb_dtyp.dropFilter;

  /* Отсортируем полученный массив балансовых счетов в порядке возрастания */
  if ( ASize( ABalAccs ) > 0 )
    i = 0;
    while ( i < ASize( ABalAccs ) )
      j = i;
      while ( j < ASize( ABalAccs ) )
        if ( ABalAccs( j ) < ABalAccs( i ) )
          TmpVal = ABalAccs( i );
          ABalAccs( i ) = ABalAccs( j );
          ABalAccs( j ) = TmpVal;
        end;
        j = j + 1;
      end;
      i = i + 1;
    end;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefBranches

  var i = 0;
  var dpDepList = TdpDepList;

  if ( StrFor( GetProgramID ) == "П" )
    dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
    while ( dpDepList.next() )
      ABranches( i ) = dpDepList.rec.Code;
      i = i + 1;
    end;
  else
    ABranches( 0 ) = Branch;
  end;

end;


/**************************************************************************\
|                             Найти вкладчика                              |
\**************************************************************************/
macro FindClient ( CodClient )

  if ( depclnt.GetRecord( depositr.rec.CodClient ) )
    ;
  else
    depclnt.CurRec.Clear( );
    depclnt.CurRec.rec.CodClient = depositr.rec.CodClient;
  end;

end;


/**************************************************************************\
|                          Напечатать шапку отчета                         |
\**************************************************************************/
macro WriteHeadReport

  var namestr = "", d, m, y, Month = "";
  var name;

  if ( StrFor( GetProgramID ) == "П" )
    namestr = {Name_Bank} + " " + {NumberBranch};
  else
    if ( findDepartment(Branch, name) )
      namestr = "Филиал " + name;
/*    else
      ClearRecord( listfdep );*/
    end;
  end;

  [ ];
  [ #](namestr);
  [ ];
  [########################################################################################]
  ("Проценты, выплаченные наличными":c);

  DateSplit (StartDate,d,m,y);
  Month = MonthName(m - 1);

  [########################################################################################]
  ("за " + Month:c);

end;


/**************************************************************************\
|                     Напечатать шапку отчета по валюте                    |
\**************************************************************************/
macro WriteHeadForCurrency

  [ ];
/*  if (FlagCur)  */
    [ Валюта  #     #](currency.ExternalCode,currency.Name_Currency);
    [ ];
/*  end;          */

  [--------------------------------------------------------------------------------------------];
  [|           Вид вклада           |  Кол-во  |               |              | Разница между |];
  [|                                |  счетов  |Причисленные %%|Выплаченные %%|причисленными и|];
  [|                                |          |               |              |выплаченными %%|];
  [|------------------------------------------------------------------------------------------|];

end;


/**************************************************************************\
|                  Напечатать шапку отчета по виду вкладу                  |
\**************************************************************************/
macro WriteHeadForType ( Citizenship )

  if ( NumBA == "" )
    if ( Citizenship == StrFor( 0 ) )
      NumBA = sb_dtyp.rec.BalAcc;
    else
      NumBA = sb_dtyp.rec.BalAccNotRez;
    end;
    [|######                          |          |               |              |               |]
    ( NumBA );
  end;

end;


/**************************************************************************\
|                      Напечатать итог по виду вклада                      |
\**************************************************************************/
macro WriteSummaryForKind (Citizenship)

  if (KindNumAcc)
    [|     ###########################|##########|###############|##############|###############|]
    (sb_dtyp.rec.Name:w,KindNumAcc,KindPercPut,KindPercOut,KindPercPut - KindPercOut);
    [|------------------------------------------------------------------------------------------|];
  end;

  KindNumAcc   = 0;
  KindPercPut  = $0L;
  KindPercOut  = $0L;

end;

/**************************************************************************\
|                   Напечатать итог по балансовому счету                   |
\**************************************************************************/
macro WriteSummaryForBA (BANumber)

  if (BANumAcc)
    [|Итого по б/с #                  |##########|###############|##############|###############|]
    (BANumber,BANumAcc,BAPercPut,BAPercOut,BAPercPut - BAPercOut);
    [|==========================================================================================|];
  end;

  BANumAcc   = 0;
  BAPercPut  = $0L;
  BAPercOut  = $0L;
  NumBA      = "";

end;


/**************************************************************************\
|                          Обработка одного счета                          |
\**************************************************************************/
macro WorkWithOneAccount ( Citizenship )

  var stat;
  var Put = $0L;
  var Out = $0L;

  sbdepdoc.addFilter( "t_Referenc = " + String( depositr.rec.Referenc ) + " and " +
                      "t_Date_Document between " + sqlDateToStr( StartDate ) + " and " + sqlDateToStr( EndDate ) );

  sbdepdoc.Clear;
  sbdepdoc.rec.Referenc      = depositr.rec.Referenc;
  sbdepdoc.rec.Date_Document = StartDate;
  sbdepdoc.rec.NumDayDoc     = 0;
  stat = sbdepdoc.GetGE;
  while ( stat )
    if ( ( sbdepdoc.rec.Referenc      == depositr.rec.Referenc ) and
         ( sbdepdoc.rec.Date_Document >= StartDate             ) and
         ( sbdepdoc.rec.Date_Document <= EndDate               )    )
      if ( IsServDoc( sbdepdoc.rec ) and ( sbdepdoc.rec.FlagStorn == "" ) )
        if ( (   sbdepdoc.rec.TypeOper == 72                                             ) or 
             ( ( sbdepdoc.rec.TypeOper == 62 ) and ( ( sbdepdoc.rec.ApplType == 14 ) or 
                                                     ( sbdepdoc.rec.ApplType == 16 )   ) )   )
          if ( sbdepdoc.rec.InSum )
            Put = Put + sbdepdoc.rec.InSum;
          else
            Put = Put - sbdepdoc.rec.OutSum;
          end;
        end;

        if ( ( sbdepdoc.rec.TypeOper == 4 ) or ( sbdepdoc.rec.TypeOper == 6 ) )
          Out = Out + sbdepdoc.rec.OutSum;
        end;
      end;
      stat = sbdepdoc.Next;
    else
      stat = false;
    end;
  end;

  sbdepdoc.dropFilter;

  if ( Out > Put )
    Out = Put;
  end;

  if ( (Put != 0 ) or ( Out != 0 ) )

    WasAccBA = true;

    if ( not WasAccCur )
      WasAccCur = true;
      WriteHeadForCurrency( );
    end;

    if ( not WasAccKind )
      WasAccKind = true;
      WriteHeadForType( Citizenship );
    end;

    KindNumAcc   = KindNumAcc  + 1;
    KindPercPut  = KindPercPut + Put;
    KindPercOut  = KindPercOut + Out;
    /*******************************************/
    BANumAcc   = BANumAcc  + 1;
    BAPercPut  = BAPercPut + Put;
    BAPercOut  = BAPercOut + Out;

  end;

end;


/**************************************************************************\
|      Обработка счетов одной валютности, вида вклада и резидентности      |
\**************************************************************************/
macro WorkWithAccountsOneDepType ( Citizenship )

  var stat;
  var i = 0;

  WasAccKind = false;

  while ( i < ASize( ABranches ) )

    depositr.addFilter( "t_IsCur = " + String( FlagCur ) + " and " +
                        "t_FNCash = " + String( ABranches( i ) ) + " and " +
                        "t_Type_Account = " + GetSQLString( sb_dtyp.rec.Kind ) );

    depositr.Clear;
    depositr.rec.IsCur        = FlagCur;
    depositr.rec.FNCash       = ABranches( i );
    depositr.rec.Type_Account = sb_dtyp.rec.Kind;
    depositr.rec.End_DateDep  = Date( 0, 0, 0 );
    stat = depositr.GetGE;
    while ( stat )
      if ( ( depositr.rec.IsCur         == FlagCur                ) and
           ( depositr.rec.FNCash        == ABranches( i )         ) and
           ( depositr.rec.Type_Account  == sb_dtyp.rec.Kind       )    )
        if ( depositr.rec.Code_Currency == currency.Code_Currency )
          FindClient( depositr.rec.CodClient );
          if ( depclnt.CurRec.rec.NotResident == Citizenship )

            counterAll = counterAll + 1;
            counterSub = counterSub + 1;
            if ( counterSub >= 50 )
              counterSub = 0;
  
              if ( Citizenship == StrFor( 0 ) )
                Message(" Обработка балансового счета ",sb_dtyp.rec.BalAcc," (",currency.ExternalCode,")...  Счет ",counterAll);
              else
                Message(" Обработка балансового счета ",sb_dtyp.rec.BalAccNotRez," (",currency.ExternalCode,")...  Счет ",counterAll);
              end;
            end;

            WorkWithOneAccount( Citizenship );
          end;
        end;
        stat = depositr.Next;
      else
        stat = false;
      end;
    end;
  
    depositr.dropFilter;

    i = i + 1;

  end;

  WriteSummaryForKind( Citizenship );

end;


/**************************************************************************\
|                    Обработка счетов одной валютности                     |
\**************************************************************************/
macro WorkWithAccountsOneCurrency

  var i = 0;
  var stat;

  WasAccCur = FALSE;

  while ( i < ASize( ABalAccs ) )

    Message(" Обработка балансового счета ",ABalAccs(i)," (",currency.ExternalCode,")...");
    counterAll = 0;
    counterSub = 0;

    WasAccBA = FALSE;

    /* Обработка резидентных счетов видов вклада */
    sb_dtyp.addFilter( "t_FlagCur = " + String( FlagCur ) + " and " +
                       "t_BalAcc = " + GetSQLString( ABalAccs( i ) )    );

    sb_dtyp.KeyNum = 4;
    sb_dtyp.Clear;
    sb_dtyp.rec.FlagCur = FlagCur;
    sb_dtyp.rec.BalAcc  = ABalAccs( i );
    stat = sb_dtyp.GetEQ;
    while ( stat )
      if ( ( sb_dtyp.rec.FlagCur == FlagCur       ) and
           ( sb_dtyp.rec.BalAcc  == ABalAccs( i ) )    )
        WorkWithAccountsOneDepType( StrFor( 0 ) );
        stat = sb_dtyp.Next;
      else
        stat = false;
      end;
    end;

    sb_dtyp.dropFilter;

    /* Обработка нерезидентных счетов видов вклада */
    sb_dtyp.addFilter( "t_FlagCur = " + String( FlagCur ) + " and " +
                       "t_BalAccNotRez = " + GetSQLString( ABalAccs( i ) ) );

    sb_dtyp.KeyNum = 5;
    sb_dtyp.rec.FlagCur      = FlagCur;
    sb_dtyp.rec.BalAccNotRez = ABalAccs( i );
    stat = sb_dtyp.GetEQ;
    while ( stat )
      if ( ( sb_dtyp.rec.FlagCur      == FlagCur       ) and
           ( sb_dtyp.rec.BalAccNotRez == ABalAccs( i ) )    )
        WorkWithAccountsOneDepType( StrFor( 1 ) );
        stat = sb_dtyp.Next;
      else
        stat = false;
      end;
    end;

    sb_dtyp.dropFilter;

    if ( WasAccBA )
      WriteSummaryForBA( ABalAccs( i ) );
    end;

    i = i + 1;

  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  var stat;

  DefDates( );
  
  DefBalAccs( ); // Определение балансовых счетов
  DefBranches( );

  WriteHeadReport( );

  if ( FlagCur )
    currency.Code_Currency = 1;
    stat = GetGE( currency );
  else
    currency.Code_Currency = 0;
    stat = GetEQ( currency );
  end;

  while ( stat )
    WorkWithAccountsOneCurrency( );
    if ( FlagCur )
      stat = Next( currency );
    else
      stat = false;
    end;
  end;

end;
