/*
$Name:             lostsbbk.mac
$Module:           RS-Retail
$Description:      Операция по утере сберкнижки
*/

/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Операция по утере сберкнижки                                            *
*                                                                          *
*  Лебедев С.В.                                                            *
*  12.07.2000                                                              *
\**************************************************************************/

import DeprIntr, alg_vars, cur_rate, lostsbbp, acc_form;

file depositr   ( depositr ) key 8 write;
file currency   ( currency ) key 0;
file lostbook   ( lostbook ) key 0 write;
file tarif      ( sb_tarif ) key 0;
file tarif_plus ( sb_tarpl ) key 0;
file suboper    ( suboper )  key 0;

var depclnt = TClientList;

record pay_doc  ( pay_doc  );
record sbdepdoc ( sbdepdoc );

const UPDATE_ALL  = 1;
const UPDATE_DEP  = 2;
const UPDATE_DOC  = 4;

const  /* Коды ошибок при операциях с базой данных. */
 BEinoper   = 1,
 BEioerr    = 2,
 BEnoopen   = 3,
 BEnorec    = 4,
 BEdupkey   = 5,
 BEinvkey   = 6,
 BEdifkey   = 7,
 BEinpos    = 8,
 BEeof      = 9;

const OP_LOST_SBOOK = 205;

/* Состояние заявления об утрате сбер.книжки. */
const LBDS_ANY         = 0,  /* Любое      */
      LBDS_INITIALIZED = 1,  /* Введено    */
      LBDS_PROCESSED   = 2,  /* Обработано */
      LBDS_CANCELLED   = 3;  /* Отменено   */

/* Вид обработки заявления об утрате сбер.книжки. */
const LBDP_UNDEF     = 0,  /* Не определен                */
      LBDP_CASHCLOSE = 1,  /* Закрыть счет наличными      */
      LBDP_TRANSFER  = 2,  /* Перечислить остаток на счет */
      LBDP_REISSUE   = 3;  /* Выдать дубликат сб/кн       */

const TarifSbook  = 17;  /* Код тарифа за утерю сберкнижки */
const TarifSbookP = 19;  /* Код тарифа за утерю сберкнижки для пенсионеров */

const MinRestNat  = $100L;
const MinRestBase = $10L;

var NumTarif   = 0;
var ComisSum   = $0L;
var MinRest    = $0L;
var KindPay    = 0;      /* 0 - наличными / 1 - безналичными / 2 - при закрытии */
var CBRateCur  = 0;
var CBRateBase = 0;

var vDate_Lost = {curdate};  /* Дата утери сбер.книжки */

var LB_MaxID = 0;  /* Максим.идентификатор в таблице регистр.утрач. сб\кн. */

/**************************************************************************\
|                               Поиск тарифа                               |
\**************************************************************************/
macro FindTarif( isCur, Num, _Date, _Sum )

  var stat;

  tarif.IsCur = isCur;
  tarif.NumTarif = Num;

  stat = GetEQ( tarif );

  if ( stat and ( tarif.TarifPlus ) )
    tarif_plus.IsCur    = isCur;
    tarif_plus.NumTarif = Num;
    tarif_plus.Date     = _Date;
    tarif_plus.SumOper  = _Sum;
    if ( ( GetGE( tarif_plus )          ) and
         ( tarif_plus.IsCur    == 0     ) and
         ( tarif_plus.NumTarif == Num   ) and
         ( tarif_plus.Date     == _Date )    )
      tarif.PerSent = tarif_plus.Percent;
      tarif.MinSum  = tarif_plus.MinSum;
      tarif.MaxSum  = tarif_plus.MaxSum;
      tarif.Summa   = tarif_plus.Summa;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                  Определение тарифа для подвида операции                 |
\**************************************************************************/
macro DefineTarif( isCur, SubOpNum )

  var stat;

  suboper.IsCur = isCur;
  suboper.OperType = OP_LOST_SBOOK;
  suboper.Type = SubOpNum;
  suboper.Kind = "Шаблонный";

  stat = GetEQ( suboper );

  if ( stat )
    NumTarif = suboper.NumTarif;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro NewDepDoc ( Doc )

  ClearRecord( Doc );
  Doc.Referenc         = ALG_DEPOSITOR.Referenc;
  Doc.IsCur            = ALG_DEPOSITOR.IsCur;
  Doc.FNCash           = ALG_DEPOSITOR.FNCash;
  Doc.RealFNCash       = NumRealFNCash( );
  Doc.Account          = ALG_DEPOSITOR.Account;
  Doc.Oper             = {oper};
  Doc.Type_Account     = ALG_DEPOSITOR.Type_Account;
  Doc.Code_Currency    = ALG_DEPOSITOR.Code_Currency;
  Doc.CodClient        = ALG_DEPOSITOR.CodClient;
  Doc.YesSbook         = "X";
  Doc.Date_Document    = {curdate};
  Doc.DepDate_Document = {curdate};

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro InitRecsForLostSBBook

  /* Формирование документа на наличную плату */
  if ( ( ComisSum > 0 ) and ( KindPay == 0 ) and ( StrFor( GetProgramID( ) ) != "П" ) )
    ClearRecord( pay_doc );
    pay_doc.FNCash           = NumRealFNCash( );
    pay_doc.IsCur            = ALG_DEPOSITOR.IsCur;
    pay_doc.NumOpert         = 5;
    pay_doc.GroupOpert       = 21; /* Платы и налоги */
    pay_doc.Date_Document    = {curdate};
    pay_doc.CodCur           = 0;
    pay_doc.Oper             = {oper};
    pay_doc.InSum            = ComisSum;
    pay_doc.ClientLastName   = depclnt.CurRec.rec.Name1;
    pay_doc.ClientFirstName  = depclnt.CurRec.rec.Name2;
    pay_doc.ClientSecondName = depclnt.CurRec.rec.Name3;
    pay_doc.FlagRez          = depclnt.CurRec.rec.NotResident;
    pay_doc.CodClient        = depclnt.CurRec.rec.CodClient;
    pay_doc.iApplicationKind = 200;
    pay_doc.ApplicationKey   = FormApplicationKey( 200 );

    PaySetGround( pay_doc );
    if ( pay_doc.Ground == "" )
      pay_doc.Ground = "Плата за утерю сберкнижки по счету " + 
                       Acc_Form(ALG_DEPOSITOR.Account);
    end;
  end;

  /* Формирование документа по вкладу */
  NewDepDoc( sbdepdoc );
  sbdepdoc.TypeComplexOper  = ALG_RET_OP.Operation;
  sbdepdoc.TypeOper         = 62;
  sbdepdoc.ApplType         = 30;
  sbdepdoc.DepDate_Document = {curdate};
  sbdepdoc.KindOp           = 3;
  sbdepdoc.IsControl        = StrFor( 1 ); /* Признак главного документа */
  sbdepdoc.ArDate           = vDate_Lost;
  if ( KindPay == 1 )
    sbdepdoc.OutSum         = ComisSum;
  end;

  if ( StrFor( GetProgramID( ) ) != "П" )
    DepSetGround( sbdepdoc );
    if ( sbdepdoc.Ground == "" )
      sbdepdoc.Ground = "Установка признака потери сберкнижки";
    end;
  else
    if ( ALG_SBDEPDOC.Ground != "" )
      sbdepdoc.Ground = ALG_SBDEPDOC.Ground;
    else
      DepSetGround( sbdepdoc );
      if ( sbdepdoc.Ground == "" )
        sbdepdoc.Ground = "Установка признака потери сберкнижки";
      end;
    end;
  end;

end;

/**************************************************************************\
|    Поиск максимального идентификатора в таблице регистр.утрач. сб\кн.    |
\**************************************************************************/
macro LB_find_MaxID()
  var statLBM = TRUE;
  KeyNum( lostbook, 0 );
  ClearRecord( lostbook );
  lostbook.ID = 2147483646;
  if ( getLE( lostbook ) )
    LB_MaxID = lostbook.ID;
  else
    ErrCodeLB = Status( ErrTextLB );
    if ( ( ErrCodeLB == BEnorec )  OR  ( ErrCodeLB == BEeof ) )
      LB_MaxID = 0;
    else
      LB_MaxID = -1;
      statLBM = FALSE;  /* Ошибка при поиске в таблице регистр.утрач. сб\кн. */
    end;
  end;
  return statLBM;
end;

/**************************************************************************\
|           Запись в таблицу регистрации утраченных сбер.книжек.           |
\**************************************************************************/
macro LB_Insert()
  ClearRecord( lostbook );
  /* Идентификатор записи */
  lostbook.ID = LB_MaxID + 1;
  /* Номер заявления об утрате с\к */
  lostbook.Number = String( lostbook.ID:20:r );
  /* Дата регистрац.заявлен.об утрате с\к */
  lostbook.DateRegistered = {curdate}; //T_DATEREGISTERED, scr # 86752
  /* Номер подразд.->зарегистр.заявление */
  lostbook.Branch = NumRealFNCash();
  /* Код клиента, подавшего заявление */
  lostbook.ClientCode = ALG_DEPOSITOR.CodClient;
  /* Референс счета -> утрачена с\к */
  lostbook.Reference = ALG_DEPOSITOR.Referenc;
  /* Код клиента-владельца счета */
  lostbook.ClientCodeAcc = ALG_DEPOSITOR.CodClient;
  /* Состояние заявления */
  lostbook.State = LBDS_INITIALIZED;
  /* Номер операциониста->заявл.об утрате */
  lostbook.RegisteredByOper = {oper};//T_REGISTEREDBYOPER, scr # 87944 
  /* Вид обработки заявления */
  if   ( panCloseNal )
    lostbook.Processing = LBDP_CASHCLOSE;
  elif ( panDubl )
    lostbook.Processing = LBDP_REISSUE;
  elif ( panNewAcc )
    lostbook.Processing  = LBDP_TRANSFER;
    lostbook.DestAccType = panKindAcc;  /* Вид вклада счета-получателя */
  elif ( StrLen( panAcc ) > 0 )
    lostbook.Processing    = LBDP_TRANSFER;
    lostbook.DestReference = panRef;  /* Референс счета-получателя */
  end;

  /* Дата обработки заявления */
  /* lostbook.DateProcessed */
  /* Номер операциониста->обработал заяв. */
  /* lostbook.ProcessedByOper */
  /* Остаток счета на момент закрытия */
  /* lostbook.Rest */
  /* Порядковый номер дубликата с\к */
  /* lostbook.BookCopyNo */
  /* Дата отмены заявления */
  /* lostbook.DateCancelled */
  /* Номер операциониста->отмена утери */
  /* lostbook.CancelledByOper */

  return Insert( lostbook );
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro T_UpdateRecsForLostSBBook

  record save_depositr ( depositr );

  record WriteLog_rec  ( "WriteLog.rec" );
  record op_parm_rec   ( "op_parm.rec"  );
  record op_link_rec   ( "op_link.rec"  );
  record ret_op_dep    ( "ret_op.dep"   );

  var stat = true;
  var appl_key;
  var str_tmp;

  /* Изменение записи по счету */
  if ( stat )
    GetPos( depositr );
    stat = GetDirect( depositr );
  end;

  if ( stat )
    ALG_DEPOSITOR.Givebook   = StrFor( 0 );
    ALG_DEPOSITOR.NumSession = 0;
    ALG_DEPOSITOR.Action     = 1;

    Copy( save_depositr, depositr );
    Copy( depositr, ALG_DEPOSITOR );

    stat = Update( depositr );
  end;

  if ( stat )
    ALG_CONTRACT.BookGiven = StrFor( 0 );
    stat = ALG_CONTRACT.Set_AccountsFlagCh( "Y" );     // устанавливает признак утери сберкнижки на все счета договора и на сам договор. делает Update.
    ALG_DEPOSITOR.UserTypeAccount = ALG_DEPOSITOR.UserTypeAccount + "Y"; // иначе при апдейте, который идет ниже установленные флаги перетираются
  end;

  /* Журнализация изменений в файл нефинансовых операций */
  if ( stat )
    ClearRecord( WriteLog_rec );
    WriteLog_rec.NumOperation = 3;
    WriteLog_rec.ObjectType   = ALG_RET_OP.Operation;
    WriteLog_rec.ObjectID     = String( 501 ) + "/" + String( depositr.Referenc );

    Copy( depositr, ALG_DEPOSITOR );

    if ( WriteChangesToLog( WriteLog_rec, depositr, save_depositr ) != 0 )
      stat = false;
    end;
  end;

  /* Формирование операции */
  if ( stat )
    appl_key = "rtnfopr#";
    str_tmp = String( WriteLog_rec.NumDprt );
    while ( StrLen( str_tmp ) < 5 )
      str_tmp = "0" + str_tmp;
    end;
    appl_key = appl_key + str_tmp;
    str_tmp = String( WriteLog_rec.RecID );
    while ( StrLen( str_tmp ) < 10 )
      str_tmp = "0" + str_tmp;
    end;
    appl_key = appl_key + str_tmp;
    appl_key = appl_key + "00";

    if ( not InterDesk_IsDocBunchActive )
      ClearRecord( op_parm_rec );
      op_parm_rec.Date      = {curdate};
      op_parm_rec.Operation = ALG_RET_OP.Operation;
      op_parm_rec.SubOp     = ALG_RET_OP.SubOp;
      op_parm_rec.ObjectRef = depositr.Type_Account;
      op_parm_rec.CodCur    = depositr.Code_Currency;
      op_parm_rec.Flags     = 1 /*ROF_NONFINANCIAL*/;

      ClearRecord( ret_op_dep );
      ret_op_dep.accRef = depositr.Referenc;

      stat = InterDesk_InitDocBunch( 3001, appl_key, op_parm_rec, ret_op_dep );
    end;
  end;

  if ( stat )
    stat = CashLink_AddRec( 3001, appl_key );
  end;

  if ( stat )
    stat = UpdateAlgRecords( UPDATE_DEP, true, false );
  end;

  /* Формирование документа на наличную плату */
  if ( stat AND ( ComisSum > 0 ) AND ( KindPay == 0 ) AND ( StrFor( GetProgramID( ) ) != "П" ) )
    stat = InsertPayDocToGDDList( pay_doc );
  end;

  /* Формирование документа по вкладу */
  if ( stat )
    stat = InsertDocToGDDList( sbdepdoc );
  end;

  if ( stat )
    if ( CarryGDDList( ) != 0 )
      stat = false;
    end;
  end;

  /* Формирование связи операции и объекта */
  if ( stat )
    ClearRecord( op_link_rec );
    op_link_rec.Date         = {curdate};
    op_link_rec.ObjectType   = 1;
    op_link_rec.ObjectRef    = String( depositr.Referenc );

    stat = LinkOpToObject( op_link_rec );
  end;

  /* Поиск максимального идентификатора в таблице регистр.утрач. сб\кн. */
  if ( stat )
    stat = LB_find_MaxID();
  end;

  /* Запись в таблицу регистрации утраченных сбер.книжек. */
  if ( stat )
    stat = LB_Insert();
  end;

  /* Журнализация записи в lostbook.dbt в файл нефинансовых операций */
  if ( stat )
    ClearRecord( WriteLog_rec );
    WriteLog_rec.NumOperation = 2;  /* Insert */
    WriteLog_rec.ObjectType   = ALG_RET_OP.Operation;
    WriteLog_rec.ObjectID     = String( 517 ) + "/" + String( lostbook.ID );

    if ( WriteChangesToLog( WriteLog_rec, lostbook, lostbook ) != 0 )
      stat = false;
    end;
  end;

  /* Формирование операции */
  if ( stat )
    appl_key = "rtnfopr#";
    str_tmp = String( WriteLog_rec.NumDprt );
    while ( StrLen( str_tmp ) < 5 )
      str_tmp = "0" + str_tmp;
    end;
    appl_key = appl_key + str_tmp;
    str_tmp = String( WriteLog_rec.RecID );
    while ( StrLen( str_tmp ) < 10 )
      str_tmp = "0" + str_tmp;
    end;
    appl_key = appl_key + str_tmp;
    appl_key = appl_key + "00";

    if ( not InterDesk_IsDocBunchActive )
      ClearRecord( op_parm_rec );
      op_parm_rec.Date      = {curdate};
      op_parm_rec.Operation = ALG_RET_OP.Operation;
      op_parm_rec.SubOp     = ALG_RET_OP.SubOp;
      op_parm_rec.ObjectRef = lostbook.Number;
      op_parm_rec.CodCur    = depositr.Code_Currency;
      op_parm_rec.Flags     = 1 /*ROF_NONFINANCIAL*/;

      ClearRecord( ret_op_dep );
      ret_op_dep.accRef = depositr.Referenc;

      stat = InterDesk_InitDocBunch( 3001, appl_key, op_parm_rec, ret_op_dep );
    end;
  end;

  if ( stat )
    stat = CashLink_AddRec( 3001, appl_key );
  end;

  if ( not stat )
    AbortTrn( );
  end;

end;

/**************************************************************************\
|   Проверка наличия записи по утере с\к для счета в состоянии "Введено".  |
\**************************************************************************/
macro LB_check_Init()
  KeyNum( lostbook, 5 );
  ClearRecord( lostbook );
  lostbook.Reference = ALG_DEPOSITOR.Referenc;
  lostbook.State     = LBDS_INITIALIZED;
  return getEQ( lostbook );
end;

/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  ALG_RESULT = OK_RES;

  if ( ALG_RESULT == OK_RES )
    if ( ALG_CONTRACT.BookGiven == "" )
//    if ( ALG_DEPOSITOR.Givebook == "" )
      if ( StrFor( GetProgramID( ) ) != "П" )
        MsgBox( "По счету сберкнижка не выдавалась" );
      end;
      ALG_RESULT = SKIP_RES;
    end;
  end;

  if ( ALG_RESULT == OK_RES )
    if ( LB_check_Init() )
      MsgBox( "Для данного счета существует|" +
              "необработанное заявление об утере сберкнижки." );
      ALG_RESULT = SKIP_RES;
    end;
  end;

  if ( ALG_RESULT == OK_RES )
    if ( Index( ALG_DEPOSITOR.UserTypeAccount, "Y" ) )
      if ( StrFor( GetProgramID( ) ) != "П" )
        MsgBox( "У счета сберкнижка уже утеряна" );
      end;
      ALG_RESULT = SKIP_RES;
    end;
  end;

  if ( ( ALG_RESULT == OK_RES ) AND ( StrFor( GetProgramID( ) ) != "П" ) )
    if ( not GetTrue( true, "Провести операцию по утере сберкнижки ?" ) )
      ALG_RESULT = SKIP_RES;
    end;
  end;

  /* Поиск счета */
  if ( ALG_RESULT == OK_RES )
    ClearRecord( depositr );
    depositr.Referenc = ALG_DEPOSITOR.Referenc;
    if ( not GetEQ( depositr ) )
      if ( StrFor( GetProgramID( ) ) != "П" )
        MsgBox( "Не найден счет с референсом " + String( ALG_DEPOSITOR.Referenc ) );
      end;
      ALG_RESULT = ERR_RES;
    end;
  end;

  /* Поиск клиента */
  if ( ALG_RESULT == OK_RES )
    if ( not depclnt.GetRecord( ALG_DEPOSITOR.CodClient ) )
      if ( StrFor( GetProgramID( ) ) != "П" )
        MsgBox( "Не найден клиент с кодом " + String( ALG_DEPOSITOR.CodClient ) );
      end;
      ALG_RESULT = ERR_RES;
    end;
  end;

  /* Определение минимальной суммы, с которой не берется плата */
  if ( ALG_RESULT == OK_RES )
    if ( ALG_DEPOSITOR.Code_Currency == 0 )
      MinRest = MinRestNat;
    elif ( ALG_DEPOSITOR.Code_Currency == 1 )
      MinRest = MinRestBase;
    else
      if ( not GetAllCurrRate( {curdate}, ALG_DEPOSITOR.Code_Currency, CBRateCur ) )
        ALG_RESULT = ERR_RES;
        if ( StrFor( GetProgramID( ) ) != "П" )
          MsgBox( "Ошибка при определении курса валюты счета" );
        end;
      else
        if ( not GetAllCurrRate( {curdate}, 1, CBRateBase ) )
          ALG_RESULT = ERR_RES;
          if ( StrFor( GetProgramID( ) ) != "П" )
            MsgBox( "Ошибка при определении курса базовой валюты" );
          end;
        end;
      end;
      if ( ALG_RESULT == OK_RES )
        MinRest = Money( MinRestBase * CBRateBase / CBRateCur );
      end;
    end;
  end;

  /* Определение тарифа */
  if ( ALG_RESULT == OK_RES )
    if ( depclnt.CurRec.rec.DateDeath != Date( 0, 0, 0 ) )
      NumTarif = 0;
    elif ( ALG_DEPOSITOR.Sum_Rest <= MinRest )
      NumTarif = 0;
    elif ( not DefineTarif(ALG_DEPOSITOR.IsCur, ALG_RET_OP.SubOp) )
      NumTarif = 0;
    end;

    if ( NumTarif > 0 )
      if ( not FindTarif( ALG_DEPOSITOR.IsCur, NumTarif, Date( 0, 0, 0 ), $0L ) )
        if ( StrFor( GetProgramID( ) ) != "П" )
          MsgBox( "Не найден тариф за утерю сберкнижки" );
        end;
        ALG_RESULT = ERR_RES;
      end;
    else
      ClearRecord( tarif );
    end;
  end;

  if ( ALG_RESULT == OK_RES )
    ComisSum = tarif.Summa;
    if ( ( StrFor( GetProgramID( ) ) == "П" ) and ( ALG_SBDEPDOC.OutSum != 0 ) )
      ComisSum = ALG_SBDEPDOC.OutSum;
    end;
  end;

  if ( ALG_RESULT == OK_RES )
    PanelLostBook( ALG_SBDEPDOC.Code_Currency, ALG_SBDEPDOC.CodClient );
    if ( panEscape )
      MsgBox( "Операция отменена пользователем." );
      ALG_RESULT = SKIP_RES;
    end;
    vDate_Lost = panDateReg;
  end;

  /* Запрос - как брать плату */
  if ( ( ALG_RESULT == OK_RES ) AND ( ComisSum > 0 ) )
/*    if ( StrFor( GetProgramID( ) ) == "П" )
      if ( ALG_SBDEPDOC.OutSum != 0 )
        KindPay = 1;
        ComisSum = ALG_SBDEPDOC.OutSum;
      else
        KindPay = 0;
      end;
    else
      currency.Code_Currency = 0;
      if ( not GetEQ( currency ) )
        ClearRecord( currency );
      end;
      if ( ALG_DEPOSITOR.IsCur )
        KindPay = ConfDlg( "Плата за утерю сберкнижки в размере " + String( ComisSum:0:2:a ) + " " + currency.Short_Name + " берется ?", NULL, "Наличными", "Не берется" );
        if ( KindPay == 1 )
          KindPay = 2;
        end;
      else
        KindPay = ConfDlg( "Плата за утерю сберкнижки в размере " + String( ComisSum:0:2:a ) + " " + currency.Short_Name + " берется ?", NULL, "Наличными", "Безналичными", "Не берется" );
      end;
    end; */
    KindPay = 2; // При проведении операции утери сберкнижки плата не берется. SCR# 77917
  end;

  if ( ALG_RESULT == OK_RES )
    InitRecsForLostSBBook( );
    if ( ProcessTrn( 0, "T_UpdateRecsForLostSBBook", depositr ) )
      if ( AI_SBook_Num( ALG_DEPOSITOR.Referenc, "", 0 ) != 0 )
        MsgBox( "Ошибка при сбросе серии\номера сбер.книжки" );
      end;
      ALG_RESULT = SKIP_RES;
    else
      if ( StrFor( GetProgramID( ) ) != "П" )
        MsgBox( "Ошибка при проводке по утере сберкнижки" );
      end;
      ALG_RESULT = ERR_RES;
      InterDesk_EndDocBunch( );
    end;
  end;

end;
