/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*  Эмитент                                                                 *
*                                                                          *
*  Макрос по инициализации карточного счета                                *
*                                                                          *
*  Лебедев С.В.                                                            *
*  01.10.2001                                                              *
\**************************************************************************/

import DeprIntr, alg_vars;

private var {MFO_Bank}; /* БИК банка */


/**************************************************************************\
|                    Получить ключевой разряд для счета                    |
\**************************************************************************/
macro DefKeyRazr (Account, BikAcc)

  var   BegString;
  var   i,j,k,l,m;
  array Weigth;

  BegString = SubStr(BikAcc,7,3) + SubStr(Account,1,20);

  i = 1;
  j = 7;
  while (i <= StrLen(BegString))
    k = Int(SubStr(BegString,i,1)) * j;
    l = String(k);
    m = StrLen(l);
    Weigth(i - 1) = Int(SubStr(l,m,1));
    if (j == 7)
      j = 1;
    elif (j == 1)
      j = 3;
    else
      j = 7;
    end;
    i = i + 1;
  end;

  i = 0;
  k = 0;
  while (i < ASize(Weigth))
    k = k + Weigth(i);
    i = i + 1;
  end;
  l = String(k);
  m = StrLen(l);
  k = Int(SubStr(l,m,1));
  k = k * 3;
  l = String(k);
  m = StrLen(l);
  l = SubStr(l,m,1);

  return l;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  var key_digit;

  ALG_RESULT = OK_RES;

  /* Счет спецкарточный, новая нумерация */
  if ((Index(ALG_DEPOSITOR.UserTypeAccount,"К") > 0           ) AND
      (StrLen(ALG_DEPOSITOR.Account) >= 20                    ) AND
      ((ALG_PARAM.NumOpert == 1) OR (ALG_PARAM.NumOpert == 51))    )

    if (ALG_DEPOSITOR.Number > 999999)
      MsgBox("Согласно инструкции 14 цифра номера карточного счета должна быть \"9\|" +
             "Короткий номер счета внутри группы не позволяет это сделать");
      ALG_RESULT = ERR_RES;
    else
      ALG_DEPOSITOR.Account = SubStr(ALG_DEPOSITOR.Account, 1,8) + "0" + 
                              SubStr(ALG_DEPOSITOR.Account,10,4) + "9" +
                              SubStr(ALG_DEPOSITOR.Account,15);

      key_digit = DefKeyRazr( ALG_DEPOSITOR.Account, {MFO_Bank} );

      ALG_DEPOSITOR.Account = SubStr(ALG_DEPOSITOR.Account, 1,8) + key_digit +
                              SubStr(ALG_DEPOSITOR.Account,10);

      ALG_UPDATE = 1;
    end;
  end;

end;
