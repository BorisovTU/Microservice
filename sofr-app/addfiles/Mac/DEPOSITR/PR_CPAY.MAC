/*
$Name:             PR_CPAY.MAC
$Module:           RS-Retail
$Description:      Справки на приход/расход средств
*/

import BankInter, "formBSO.mac";
import ChkPrn, QueryRes, chkCdDup;
import "cur_rate.mac", "alg_pay.mac", sqlconv;

private var ОТЧЕТ_В_EXCEL = false;

private file cr (currency) Key 0;             /* Файл валют           */
private file pd ("pay_doc.dbt") key 2 write;  /* Список вкладчиков    */
private file pay_doc  ("pay_doc.dbt" );
private file pay_kind ("pay_kind.dbt" );
private var sb_casln = TBFile( "sb_casln.dbt", "R", 1, null, "sbbank.def" );
private var pay_doc_comm  = TBFile( "pay_doc.dbt" , "R", 2, null, "sbbank.def" );
private var rt_paym = TBFile ( "rt_paym.dbt",  "r", 0 );

private var payDocT = TBFile ( "pay_doc.dbt",   "r", 2 );
private var planpay = TBFile ( "dplanpay.dbt",  "r", 3 );

private var rt_paym_1 = TRecHandler( "rt_paym.1"  );

private var pay_doc_n = TRecHandler( "pay_doc.np" );

/*---------- Инициализируемые структуры ---------------------------------*/

private record Parm(CashParm);
private record pay_doc_cnv  ("pay_doc.cnv" );

private Const SB_INCASH          =  100;   /* Документ порожден Кассой */
private Const CASHOP_DEBIT        = 5;
private Const CASHOP_CHVALUE      = 9;
private Const VALFORM_SPR27       = 6;
private Const VALFORM_BAD_SPR27   = 7;

private const TYPERES_CURRENCY    =   1, /* Деньги (из справочника валют)           */
              TYPERES_VALFORM     =   2, /* Ценные бланки                           */
              TYPERES_VALUABLES   =   3, /* Др. ценности (из справочника ценностей) */
              TYPERES_PAYDOC      =   4, /* Платежные документы ОП                  */
              TYPERES_CARD        =  10, /* Пластиковые карточки                    */
              TYPERES_MINCAPISSUE = 500; /* Минимально возможный код ЦБ             */

 /* Код операции по 113i*/
 private const CURInForTransfer   = 55,
               RURInForTransfer   = 56,
               CUROutForTransfer  = 57,
               RUROutForTransfer  = 58;

private const CO_Comission  =  21; /* Кассовая операция - Комиссия  */
private const CO_Conversion =  31; /* Кассовая операция - Конверсия */
              
private Var OperBrigade = GetOperBrigade, {oper}, {IsCashierReport},
 DocDay, DocMon, DocYear, TmpMon, Branch = NumFNCash,
 ClientList = TClientList;

private array AGroupOper, ANumOper, AGround;

AGroupOper(0) = 41; ANumOper(0) = 20; AGround(0) = "Получено по переводу из-за границы";
AGroupOper(1) = 41; ANumOper(1) = 91; AGround(1) = "Получено по переводу из-за границы";
AGroupOper(2) = 11; ANumOper(2) = 95; AGround(2) = "Принято для отправления перевода за границу без открытия счета";

var
        Docum,
        Num_doc,
        Ser_doc,
        F_name,
        S_name,
        T_name,
        Rez,
        NoRez,
        Bad,
        Name,
        DebSum,
        KredSum,
        DebCur=-1,
        KredCur=-1,
        Name_op = "",
        DateDoc,
        Deb_str  = "",
        Kred_str = "",
        len_str,
        Short_deb,
        Short_kred,stmp,
        rub_str,
        kop_str,
        NullChr = "",
        Ground = "";

array Summa_str, Summa_str2;

private var ClientList1 = TClientList;

/*═════════════════════════════════════════════════════════════════════════*/
private MACRO GetKindOper()
   var stat = True;
   var FlagRez1 = 0;
   var FlagRez2 = 0;
   var kindOp = "";

   if ( ( OC_PAY_DOC.CodCur != 0 )  AND  ( OC_PAY_DOC.NumOpert == 1 )  AND  ( OC_PAY_DOC.GroupOpert == 41 ) )  // Выдача валютного перевода БОС
     if ( ClientList1.GetRecord( OC_PAY_DOC.CodClient ) )
       FlagRez1 = CodeFor( ClientList1.CurRec.Rec.NotResident );
     end;
     payDocT.KeyNum = 2;
     payDocT.ReWind();
     payDocT.rec.iApplicationKind = OC_PAY_DOC.iApplicationKind;
     payDocT.rec.ApplicationKey   = OC_PAY_DOC.ApplicationKey;
     if ( payDocT.GetEQ() )
       pay_doc_n.SetRecordAddr( payDocT, 0, 0, TRUE );
       planpay.Clear();
       planpay.rec.Ref = pay_doc_n.rec.RefUncalimed;
       if ( planpay.GetEQ() )
         FlagRez2 = planpay.rec.AddInfo;
       end;
     end;
     if ( FlagRez1 == 1 )    // Получатель  - нерезидент
       if ( FlagRez2 == "X" )  // Отправитель - нерезидент
         kindOp = "66";
       else
         kindOp = "70";
       end;
     else                    // Получатель  - резидент
       if ( FlagRez2 == "X" )  // Отправитель - нерезидент
         kindOp = "72";
       else
         kindOp = "68";
       end;
     end;
     return kindOp;
   end;

   if ( ( OC_PAY_DOC.CodCur != 0 )  AND  ( OC_PAY_DOC.NumOpert == 40 )  AND  ( OC_PAY_DOC.GroupOpert == 11 ) )  // Прием наличных для валютного перевода БОС
     if ( ClientList1.GetRecord( OC_PAY_DOC.CodClient ) )
       FlagRez1 = CodeFor( ClientList1.CurRec.Rec.NotResident );
     else
       FlagRez1 = CodeFor( OC_PAY_DOC.FlagRez );
     end;
     FlagRez2 = GetBitFlag( OC_PAY_DOC.Flags, 8 );
     if ( FlagRez1 == 1 )    // Отправитель - нерезидент
       if ( FlagRez2 == 1 )  // Получатель  - нерезидент
         kindOp = "65";
       else
         kindOp = "71";
       end;
     else                    // Отправитель - резидент
       if ( FlagRez2 == 1 )  // Получатель  - нерезидент
         kindOp = "69";
       else
         kindOp = "67";
       end;
     end;
     return kindOp;
   end;

   keynum     ( pay_kind, 0 );
   rewind     ( pay_kind );
   ClearRecord( pay_kind );

   pay_kind.IsCur      = OC_PAY_DOC.IsCur;
   pay_kind.GroupOpert = OC_PAY_DOC.GroupOpert;
   pay_kind.NumOperat  = OC_PAY_DOC.NumOpert;

   stat = GetEQ( pay_kind );

   if ( stat )
      return pay_kind.Code113i;
   else
      MsgBox( "Не определен код операции по 113i" );
      exit( 1 );

   end;

END;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO FindComissionDoc( )

  var pay_doc_comm_rec = TRecHandler( "pay_doc.com" , "sbbank.def" );

  var mainKind = 0;
  var mainKey  = "";
  var stat;
  var wasFind = false;

  sb_casln.KeyNum = 1;
  sb_casln.Clear;
  sb_casln.rec.ApplicationKind2 = OC_PAY_DOC.iApplicationKind;
  sb_casln.rec.ApplicationKey2  = OC_PAY_DOC.ApplicationKey;

  if ( sb_casln.GetEQ )
    mainKind = sb_casln.rec.ApplicationKind1;
    mainKey  = sb_casln.rec.ApplicationKey1;
  end;

  if ( mainKey != "" )
    sb_casln.KeyNum = 0;
    sb_casln.Clear;
    sb_casln.rec.ApplicationKind1 = mainKind;
    sb_casln.rec.ApplicationKey1  = mainKey;

    sb_casln.addFilter( "t.t_applicationKind1 = " + string( mainKind ) +
                        " and t.t_applicationKey1 = " + GetSQLString( mainKey ) );
    stat = sb_casln.GetGE;

    while ( stat )

      if ( ( sb_casln.rec.ApplicationKind1 == mainKind ) and
           ( sb_casln.rec.ApplicationKey1  == mainKey  )    )

        if ( ( sb_casln.rec.ApplicationKind2 == 200 ) and ( sb_casln.rec.RefValue2 == 0 ) )

          pay_doc_comm.clear();
          pay_doc_comm.keynum( 2 );

          pay_doc_comm.rec.iApplicationKind = sb_casln.rec.ApplicationKind2;
          pay_doc_comm.rec.ApplicationKey   = sb_casln.rec.ApplicationKey2;

          if ( pay_doc_comm.getEQ( ) )
            if ( pay_doc_comm.rec.GroupOpert == CO_Comission )
              pay_doc_comm_rec.SetRecordAddr( pay_doc_comm, 0, 0, true );

              if ( OC_PAY_DOC.GroupOpert == CO_Conversion )
                if ( ( pay_doc_comm_rec.rec.DepNumOper == CO_Conversion ) and ( pay_doc_comm_rec.rec.Kind == "" ) )
                  wasFind = true;
                  stat = false;
                end;
              else
                wasFind = true;
                stat = false;
              end;
            end;
          end;
        end;
      else
        stat = false;
      end;

      if ( stat )
        stat = sb_casln.Next;
      end;
    end;
    sb_casln.dropFilter;
  end;

  return wasFind;

end;


/*========================================================================*/

MACRO PutNEWF0406007()

  var Отчет = COrderBSO( );

  Отчет.Rate_Ref = 0;
  Отчет.KindOper = GetKindOper( );
  Отчет.Sname           = OC_PAY_DOC.ClientLastName;
  Отчет.Name            = OC_PAY_DOC.ClientFirstName;
  Отчет.Pname           = OC_PAY_DOC.ClientSecondName;
  Отчет.ClientDocType   = OC_PAY_DOC.PersIDType;
  Отчет.ClientDocSeria  = OC_PAY_DOC.PassportSer;
  Отчет.ClientDocNumber = OC_PAY_DOC.PassportNumb;
  Отчет.ClientDocInfo   = OC_PAY_DOC.PassportIssuer;
  Отчет.ClientDocDate   = OC_PAY_DOC.PassportDate;
  Отчет.ClntIdentType   = OC_PAY_DOC.ClntIdentType;

  if ( StrLen( Trim( OC_PAY_DOC.ClientLastName ) ) > 0 )
    ;
  elif ( OC_PAY_DOC.CodClient != 0 )
    if ( ClientList.GetRecord( OC_PAY_DOC.CodClient ) )
      Отчет.Sname           = ClientList.CurRec.Rec.Name1;
      Отчет.Name            = ClientList.CurRec.Rec.Name2;
      Отчет.Pname           = ClientList.CurRec.Rec.Name3;
      Отчет.ClientDocType   = ClientList.CurRec.Rec.PaperKind;
      Отчет.ClientDocSeria  = ClientList.CurRec.Rec.PaperSeries;
      Отчет.ClientDocNumber = ClientList.CurRec.Rec.PaperNumber;
      Отчет.ClientDocInfo   = ClientList.CurRec.Rec.PaperIssuer;
      Отчет.ClientDocDate   = ClientList.CurRec.Rec.PaperIssuedDate;
    end;
  end;

  FindCurrRate( OC_PAY_DOC.CodCur, 1 /* Осн курс*/, Отчет.formDate, Отчет.Rate, NULL, NULL, Отчет.formTime );

   if (OC_PAY_DOC.GroupOpert == 41)
      Отчет.OutRefVal       = -1;
      Отчет.OutTypeValue    = TYPERES_CURRENCY;
      Отчет.OutCodCur       = OC_PAY_DOC.CodCur;
      Отчет.OutAmount       = Money(Double(OC_PAY_DOC.InSum));
   elif ( OC_PAY_DOC.GroupOpert == CO_Conversion )
      copy( pay_doc, OC_PAY_DOC );
      SetRecordAddr(pay_doc_cnv,pay_doc,0,0,TRUE);

      /*
      Отчет.IncomeRefVal       = -1;
      Отчет.IncomeTypeValue    = TYPERES_CURRENCY;
      Отчет.IncomeCodCur       = pay_doc_cnv.KonvertCodCur;
      Отчет.IncomeAmount       = Money(Double(pay_doc_cnv.KonvertSum));
      */

      if ( pay_doc_cnv.DebetCredit == 1 ) /* CI_CREDIT */
        Отчет.IncomeRefVal    = -1;
        Отчет.IncomeTypeValue = TYPERES_CURRENCY;
        Отчет.IncomeCodCur    = OC_PAY_DOC.CodCur;
        Отчет.IncomeAmount    = Money(Double(OC_PAY_DOC.InSum));
      else
        Отчет.OutRefVal    = -1;
        Отчет.OutTypeValue = TYPERES_CURRENCY;
        Отчет.OutCodCur    = OC_PAY_DOC.CodCur;
        Отчет.OutAmount    = Money(Double(OC_PAY_DOC.InSum));
      end;

      Отчет.Rate = pay_doc_cnv.Rate;

   else
      Отчет.IncomeRefVal    = -1;
      Отчет.IncomeTypeValue = TYPERES_CURRENCY;
      Отчет.IncomeCodCur    = OC_PAY_DOC.CodCur;
      Отчет.IncomeAmount    = Money(Double(OC_PAY_DOC.InSum));

   end;

  if ( ( OC_PAY_DOC.GroupOpert != CO_Comission ) and FindComissionDoc( ) )
    Отчет.CommCodCur  = pay_doc_comm.Rec.CodCur;
    Отчет.CommAmount  = pay_doc_comm.Rec.InSum;
  end;

  Отчет.ApplicationKind = OC_PAY_DOC.iApplicationKind;
  Отчет.ApplicationKey  = OC_PAY_DOC.ApplicationKey;

  Отчет.ОТЧЕТ_В_EXCEL = ОТЧЕТ_В_EXCEL;
  Отчет.Print();

END;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro NeedPrintDocForm

  var stat = true;

  if ( ( OC_PAY_DOC.GroupOpert == CO_Conversion ) or (OC_PAY_DOC.GroupOpert == 11) or (OC_PAY_DOC.GroupOpert == 41) )
    ;
  else
    stat = false;

    rt_paym.Clear( );
    rt_paym_1.Clear( );
    rt_paym.rec.iApplicationKind = OC_PAY_DOC.iApplicationKind;
    rt_paym.rec.ApplicationKey   = OC_PAY_DOC.ApplicationKey;
    rt_paym.rec.AttrID           = "РЕКВ_ПОЛ";
    if ( rt_paym.GetEQ( ) )
      rt_paym_1.SetRecordAddr( rt_paym, 0, 0, false );
      if ( ( rt_paym_1.rec.PaymentDirection == 1 ) or  /* Из РФ в дальнее зарубежье  */
           ( rt_paym_1.rec.PaymentDirection == 2 ) or  /* Из РФ в СНГ                */
           ( rt_paym_1.rec.PaymentDirection == 3 ) or  /* Из дальнего зарубежья в РФ */
           ( rt_paym_1.rec.PaymentDirection == 4 )   ) /* Из СНГ в РФ                */
        stat = true;
      end;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
  var stat = false;

  OC_RESULT = 1;

if (GetKindOper( ) == "")
   /*Msgbox("Для данной операции не задан код 113-И.","|",
   "СПРАВКА О ПРОВЕДЕННОЙ ОПЕРАЦИИ С НАЛИЧНОЙ ВАЛЮТОЙ И ЧЕКАМИ не выводится.","|",
   "Операция не будет занесена в реестр 113-И");*/
   OC_RESULT = 0;
   Exit( 1 );
end;

  if ( not NeedPrintDocForm( ) )
    OC_RESULT = 0;
    Exit( 1 );
  end;

  PutNEWF0406007( );

  KeyNum( pd, 2 );
  ReWind( pd );
  pd.iApplicationKind = OC_PAY_DOC.iApplicationKind;
  pd.ApplicationKey   = OC_PAY_DOC.ApplicationKey;
  stat = GetEQ( pd );
  if ( stat )
    pd.F0406007Issued = "X";
    stat = Update( pd ); 
  end;

  if ( stat )
    OC_RESULT = 0;
  end;

end;
