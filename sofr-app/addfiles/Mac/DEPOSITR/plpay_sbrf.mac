/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Длительное поручение для Сбербанка РФ

  Выпускается из панели плановых перечислений по Ctrl-F6

*****************************************************************/

import DeprIntr, OperCode, rsd, CommonInter, BankInter;

const  /* Периодичность планового перечисления */
  TERM_DAY   = 1,  /* в днях */
  TERM_MONTH = 2,  /* в месяцах */
  TERM_MON3  = 3,  /* в кварталах */
  TERM_YEAR  = 4;  /* в годах */

var sPlace = "Должность";

var {curdate};

var clntL = TClientList;

file f_oper   ( person   ) key 0;
file f_curr   ( currency ) key 0;
file f_paym   ( rt_paym  ) key 0;

var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

/*****************************************************
                  Основная процедура
На вход получает первый параметр - Ref ДП, второй параметр - строку с БИК-ом банка
*****************************************************/
macro Plan_Transfer_SBRF( pln , OwnBankBIK)

 //record dp ( "dplanpay.dbt" ); // старое

 var sBranchName = ""; /* Название филиала  */
 var sReceiverNameBranch = "";  // филиал получателя
 var sFullBranchName = ""; /* полное название филиала */
 var party = TRecHandler( "i_party.rec" ); // наш субъект экономики
 var sFIO = "";      // ФИО плательщика
 var sAddress = "";   // адрес плательщика
 var sPeriod = "";       // Переодичность платежа
 var sISOCur = "???"; // Название валюты
 var rSum=$0;    // Сумма платежа
 var sOper = "";     // Операционист
 var dDay, dMon, dYear, sDayMon = "";// временные  показатели платежа
 var OwnBIK:string;   // БИК нашего банка
 OwnBIK = OwnBankBIK;

 var IfAccClose = 0; // надо ли в условиях договора писать "- Счёт по вкладу закрыт"
 var IfSaleStop = 0; // надо ли в условиях догоовра писать "- безналичная продажа иностранной валюты физическим лицам приостановлена Сбербанком России"
 var IfBuyStop  = 0; // надо ли в условиях догоовра писать "- безналичная покупка иностранной валюты у физических лиц приостановлена Сбербанком России"

 var temp_date = date;

 var sChangeSum = "";
 var sOutPeriod = "";

 var HavePerm = 0;    // есть разрешение платить за прошедшие периоды
 var NotHavePerm = 1; // нет разрешения платить за прошедшие периоды

 var HavePermStr = "Есть разрешение о платежах за прошедший период для данного ДП";
 var NotHavePermStr = "Нет разрешения о платежах за прошедший период для данного ДП";

 var PermissonMenu = TArray;
 PermissonMenu(0)=HavePermStr;
 PermissonMenu(1)=NotHavePermStr;

 /* Аттрибуты получателя */
 var sRecipName = "";
 var sRecipINN  = "";
 var sRecipKPP  = "";
 var sRecipAcc  = "";
 var sRecipBank = "";
 var sRecipBIC  = "";
 var sRecipCorr = "";

 var rs_branch:RsdRecordSet;
 var rs_curr:RsdRecordSet;

 var cmd, rs;
 var IsSpecialAccount = "";

 // setbuff( dp, pln ); // старое
 var rs_own_pp:RsdRecordSet;
 rs_own_pp = RsdRecordSet("SELECT * FROM ddplanpay_dbt WHERE t_ref = "+String(pln));
 if(NOT rs_own_pp.MoveNext)
     println("Ошибка. В макрос передан неверный референс на ДП."); 
     RETURN;  
 end;
 /* Поиск записи о подразделении */
 if ( findDepartment(rs_own_pp.value("t_FNCash"), sBranchName, null, party) )
   sFullBranchName = Trim( party.rec.Name ) + " " +
                        Trim( sBranchName );
 else
   sFullBranchName = "???";
   sBranchName = "???";
 end;

 cmd = RSDCommand("select dep.t_SpecialAccess as SpecialAccess from ddepositr_dbt dep where dep.t_referenc = "+
                                                                                             String(rs_own_pp.value("t_Reference")));
 cmd.Execute();
 rs = RsdRecordset(cmd);
 
 if(rs.MoveNext)
   IsSpecialAccount = rs.value("SpecialAccess");
 end;

 /* Поиск записи о клиенте */
 if ( rs_own_pp.value("t_CodClientMake") != 0 )
   if ( NOT clntL.GetRecord( rs_own_pp.value("t_CodClientMake") ) )
     clntL.CurRec.rec.Name1 = "";
     clntL.CurRec.rec.Name2 = "";
     clntL.CurRec.rec.Name3 = "";
   end;
 else
   clntL.CurRec.rec.Name1 = "";
   clntL.CurRec.rec.Name2 = "";
   clntL.CurRec.rec.Name3 = "";
 end;


 if(  (not SpecialAccess) and OperMayListSpecialAccounts and IsSpecialAccount)
    sFIO = sFIO + "XXXXXX X.X.";
    sAddress = sAddress + "XXXXXX";
 else
    sFIO = sFIO+ clntL.CurRec.rec.Name1 + " " +
                    clntL.CurRec.rec.Name2 + " " +
                    clntL.CurRec.rec.Name3;
    sAddress = sAddress + clntL.CurRec.MakeFullStrAddress();
 end;

 

 
 f_curr.Code_Currency = rs_own_pp.value("t_Code_Currency");
 if ( GetEQ( f_curr ) )
   sISOCur = Trim( f_curr.Short_Name ) + " ( " +
             Trim( f_curr.Name_Currency ) + " )";
 end;

 /* Чтение записи Операциониста */
 f_oper.Oper = rs_own_pp.value("t_Oper");
 if ( GetEQ( f_oper ) )
   sOper = f_oper.Name;
 else
   sOper = "???";
 end;
 var sQuery = "SELECT COUNT(1) FROM ddplanpay_dbt pp WHERE (pp.t_account = '"+rs_own_pp.value("t_account")+"' AND pp.t_open_close=chr(0))";
 var count_q = RsdRecordSet(sQuery);
 count_q.MoveNext;
 var list_count = count_q.value(0)+1; /* количество ДП */
 var PlPayCounter = 1;
 var rs2 = RsdRecordSet("SELECT * FROM ddplanpay_dbt pp WHERE (pp.t_account = '"+rs_own_pp.value("t_account")+"' AND pp.t_open_close=chr(0)) ORDER BY pp.t_priority");

 // А теперь внимание! Сейчас найчнётся самое-самое священно-действие!
 // Приготовились? Тогда начинаем выводить длительные поручения...
 while (PlPayCounter < list_count) // main loop
     // начинаем лист
     rs2.MoveNext;
     [                                                                                   
             Сбербанк России                                                            ф.№190-у                                               ф.№190-у
    
                      Длительное поручение владельца счета на перечисление денежных средств
                   Счет № #########################,  вкладчик: ##############################
                                            Лист ##, Листов ##   
    ]
    (Acc_Form(String(rs_own_pp.value("t_Account"))):f,
    sFIO:w,
    String(PlPayCounter):l,
    String(list_count):l);

    [
            Я, ##############################
            прошу перечислять с моего
            счёта № #########################
            следующие платежи:
    ]
    ( sFIO:w,
      Acc_Form(String(rs_own_pp.value("t_Account"))):f);

    [
            п/п № ##
    ](String(PlPayCounter):l);

    /* Получение суммы платежа */
    if ( PP_GetPaymentSum( rs2.value("t_Ref"), rSum ) )
        rSum = $0;
    end;

    /* Периодичность планового перечисления    */
    if   ( rs2.value("t_KindTerm") == TERM_DAY )    /* в днях */
        sPeriod = String( rs2.value("t_SizeTerm") ) + " дней";
    elif ( rs2.value("t_KindTerm") == TERM_MONTH )  /* в месяцах */
        sPeriod = String( rs2.value("t_SizeTerm") ) + " месяц(ев)";
    elif ( rs2.value("t_KindTerm") == TERM_MON3 )   /* в кварталах */
        sPeriod = String( rs2.value("t_SizeTerm") ) + " квартал(ов)";
    elif ( rs2.value("t_KindTerm") == TERM_YEAR )   /* в годах */
        sPeriod = String( rs2.value("t_SizeTerm") ) + " год(а)";
    else
        sPeriod = String( rs2.value("t_SizeTerm") ) + "???";
    end;    

    /* Периодичность: один раз в месяц */
    if ( (rs2.value("t_KindTerm") == TERM_MONTH) AND (rs2.value("t_SizeTerm") == 1) )
        DtTmSplit(rs2.value("t_Start_DatePay"), temp_date, 0);
        DateSplit( temp_date, dDay, dMon, dYear );
        sDayMon = String( dDay ) + " числа";
    else
        sDayMon = "";
    end;

    IF ((rs2.value("t_MFO_receiver")=="") OR (rs2.value("t_MFO_receiver")==OwnBIK))  // если платёж внутри банка
        rs_branch = RsdRecordSet("SELECT dp.T_NAME FROM ddepositr_dbt dep, ddp_dep_dbt dp WHERE dep.T_ACCOUNT = '"+ rs2.value("t_account_receiver") +"' AND dep.T_ACTION <> 2 AND dep.T_OPEN_CLOSE = chr(0) AND dp.T_CODE = dep.T_FNCASH");
        if (NOT rs_branch.MoveNext)
            sReceiverNameBranch = rs_branch.value(0);
        else
            sReceiverNameBranch = "???"
        end;

        [
                Подразделение № ########
        ](sReceiverNameBranch);
        
        if (rs2.value("t_account")!="")
            [       Счёт № #########################](Acc_Form(String(rs2.value("t_account_receiver"))));
        end;
        
        
        [       Сумма:########################](rSum:l:f);
        
        if (rs2.value("t_typeoper")==80)  // если у нас б/н конверсия валюты
            if (rs2.value("t_iscur")==0)  // и счёт плательщика рублёвый
                rs_curr = RsdRecordSet("SELECT cur.T_NAME_CURRENCY FROM ddplanpay_dbt pp, dcurrency_dbt cur WHERE pp.T_CODE_CURRENCYRECEIVER = cur.T_CODE_CURRENCY AND pp.T_REF = '"+rs2.value("t_ref")+"'"); 
                if(NOT rs_curr.MoveNext)
                    [       путем безналичной покупки #############################](rs_curr.value(0));
                else
                    [       путем безналичной покупки ???];
                end;
                IfBuyStop = 1;
            else  // счёт плательщика валютный
                [       путем безналичной продажи за рубли];
                IfSaleStop = 1;
            end;
        end;

        [       Начало исполнения: ##########](String(rs2.value("t_Start_DatePay")));
        if (sDayMon!="")
            [       Сроки исполнения:  ########## ##########](sDayMon, sPeriod);
        else
            [       Сроки исполнения:  ##########](sPeriod);
        end;
        [       Срок действия: до  ##########](String(rs2.value("t_end_datepay")));      

    ELSE  // если платёж в другой банк
        sRecipName = "Получатель без договора";
        sRecipINN  = rs2.value("t_cp_inn");
        sRecipAcc  = rs2.value("t_account_receiver");
        sRecipBIC  = rs2.value("T_MFO_RECEIVER");
        if (iFindBank(3,rs2.value("t_MFO_RECEIVER"), sRecipBank, sRecipCorr )!=TRUE)
            sRecipBank = "???";
        end;
    
        [       ##################################
                ИНН: #############################
                На счёт №#########################
                в ################################
                БИК: #############################
                Корреспонденский счёт банка, в 
                котором открыт счёт организации
                получателя платежей:#########################
                Сумма платежа: ###################
        ](sRecipName,
          sRecipINN:l,
          Acc_Form(sRecipAcc):l,
          sRecipBank:l,
          sRecipBIC:l,
          Acc_Form(sRecipCorr):l,
          rSum:f:l
        );
        if (rs2.value("t_namepay")!="")
            [       за #############################################################](rs2.value("t_namepay"));
        end;
        [       Начало исполнения: ##########](String(rs2.value("t_Start_DatePay")));
        if (sDayMon!="")
            sPeriod = sDayMon+" "+sPeriod+",";
            //[       Сроки исполнения:  ########## ##########](sDayMon, sPeriod);
        else
            sPeriod = sPeriod + ",";
        end;
        [       Сроки исполнения:  ############################](sPeriod:l);
        [       Срок действия: до  ##########](String(rs2.value("t_end_datepay")));

    END; // end if (rs2.value("MFO_recei...

    /* Определяем надо ли в условии договора писать "- Счёт по вкладу закрыт" */
    if ((IfAccClose==0) AND ((rs2.value("t_typeoper")==65) OR (rs2.value("t_typeoper")==80)))
        IfAccClose = 1;
    end;

    /* что будет писаться в условиях дговра про изменение суммы платежа */
    if (rs2.value("T_EDITSUMFLAG")!=0)
        if (sChangeSum=="")
            sChangeSum = "По платежам №№ "+String(PlPayCounter);
        else
            sChangeSum = sChangeSum+", "+String(PlPayCounter);
        end;    
    end;    

    if (Menu (PermissonMenu, "Есть ли разрешение проводить платежи за прошедшие периоды?")==HavePerm)
        if (sOutPeriod == "")
            sOutPeriod = sOutPeriod + "По платежам №№ "+String(PlPayCounter);
        else
            sOutPeriod = sOutPeriod + ", " + String(PlPayCounter);
        end;    
    end;

    PlPayCounter = PlPayCounter + 1;
    print("");
 end; // end of while (main loop)

    [                                                                                   
             Сбербанк России                                                            ф.№190-у                                               ф.№190-у
    
                      Длительное поручение владельца счета на перечисление денежных средств
                   Счет № #########################,  вкладчик: ##############################
                                            Лист ##, Листов ##   
    ]
    (Acc_Form(String(rs_own_pp.value("t_Account"))):f,
    sFIO:w,
    String(PlPayCounter):l,
    String(list_count):l);

if (sChangeSum != "")
    sChangeSum = sChangeSum + " предоставляю Банку право самостоятельно \n\tкорректировать суммы перечисления при изменении тарифов на услуги организации-получателя платежей";
end;
if (sOutPeriod != "")
    sOutPeriod = sOutPeriod + " предоставляю Банку право перечислять платежи \n\tкак за текущий, так и за истекший периоды платежей";
end; 
[
        Я проинформирован, что:
        1. Банк будет осуществлять платежи (перечисления, переводы) в порядке их указания в 
           поручении. 
        2. Если указанная мной в поручении дата исполнения поручения приходится на нерабочий
           день структурного подразделения № #########, операция 
           будет совершена в первый следующий за ним рабочий день.
        3. Операция не будет совершаться, если:
            -   оговоренная поручением сумма списания превышает или равна остатку вклада;
            -   истек срок данного поручения;    
](sBranchName);
if (IfAccClose==1)
    [           -   счет по вкладу закрыт;];
end;
if (IfSaleStop==1)
    [           -   безналичная продажа иностранной валюты физическим лицам приостановлена Сбербанком России;];
end;
if (IfBuyStop ==1)
    [           -   безналичная покупка иностранной валюты у физических лиц приостановлена Сбербанком России;];
end;
[           -   по вкладу приостановлены операции в случаях, установленных законом;
        4. В случае невыполнения или приостановления банком операций в указанных выше случаях, 
           после их возобновления каждая операция будет выполняться в размерах и в сроки, 
           указанные в настоящем длительном поручении.
];

var CurrentPunct = 5;
if (IfBuyStop==1)
    [       #. Банк вправе провести конверсионную операцию в течение рабочего дня (после зачисления 
               денежных средств на рублевый счет) по курсу, действующему на момент совершения операции.
    ]
    (String(CurrentPunct));
    CurrentPunct=CurrentPunct+1;
end;

[       #. За совершение отдельных платежей (перечислений, переводов) Банком будет взиматься плата в 
           соответствии с Тарифами, действующими в Банке на день составления поручения.
](String(CurrentPunct));

[       Я ознакомлен и согласен с Тарифами Банка, взимаемыми за совершение платежей 
        (перечислений, переводов) на дату подписания настоящего поручения. Банк 
        вправе в одностороннем порядке изменять действующие Тарифы и/или устанавливать 
        новые Тарифы. При изменении действующих Тарифов и/или установлении новых Тарифов 
        Банк обязуется известить об этом меня путем размещения информации в структурных 
        подразделениях Банка, оказывающих данные услуги, за один месяц до изменения и/или 
        введения новых Тарифов.
            
            и с этими условиями согласен.

];

if (sChangeSum != "")
    println("       ", sChangeSum);
end;
if (sOutPeriod != "")
    println("       ", sOutPeriod);
end;
println();
println("       ",{curdate}:20:c:m," ____________________":22:r);
[                               подпись вкладчика


    #########################:____________________ ########################
                                     подпись

]
(sPlace:r,sOper:l);

END; /* end macros */